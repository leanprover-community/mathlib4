/-
Copyright (c) 2019 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Fabian Gl√∂ckle, Kyle Miller
-/
import Mathlib.Algebra.Module.LinearMap.DivisionRing
import Mathlib.LinearAlgebra.Basis.Basic
import Mathlib.LinearAlgebra.Dimension.ErdosKaplansky
import Mathlib.LinearAlgebra.Dual.Basis
import Mathlib.LinearAlgebra.FiniteDimensional.Lemmas
import Mathlib.LinearAlgebra.FreeModule.Finite.Basic
import Mathlib.LinearAlgebra.FreeModule.StrongRankCondition
import Mathlib.LinearAlgebra.Matrix.InvariantBasisNumber
import Mathlib.LinearAlgebra.Projection
import Mathlib.LinearAlgebra.SesquilinearForm.Basic
import Mathlib.RingTheory.Finiteness.Projective
import Mathlib.RingTheory.LocalRing.Basic
import Mathlib.RingTheory.TensorProduct.Maps

/-!
# Dual vector spaces

The dual space of an $R$-module $M$ is the $R$-module of $R$-linear maps $M \to R$.
This file contains basic results on dual vector spaces.

## Main definitions

* Submodules:
  * `Submodule.dualRestrict_comap W'` is the dual annihilator of `W' : Submodule R (Dual R M)`,
    pulled back along `Module.Dual.eval R M`.
  * `Submodule.dualCopairing W` is the canonical pairing between `W.dualAnnihilator` and `M ‚ß∏ W`.
    It is nondegenerate for vector spaces (`subspace.dualCopairing_nondegenerate`).
* Vector spaces:
  * `Subspace.dualLift W` is an arbitrary section (using choice) of `Submodule.dualRestrict W`.

## Main results

* Annihilators:
  * `LinearMap.ker_dual_map_eq_dualAnnihilator_range` says that
    `f.dual_map.ker = f.range.dualAnnihilator`
  * `LinearMap.range_dual_map_eq_dualAnnihilator_ker_of_subtype_range_surjective` says that
    `f.dual_map.range = f.ker.dualAnnihilator`; this is specialized to vector spaces in
    `LinearMap.range_dual_map_eq_dualAnnihilator_ker`.
  * `Submodule.dualQuotEquivDualAnnihilator` is the equivalence
    `Dual R (M ‚ß∏ W) ‚âÉ‚Çó[R] W.dualAnnihilator`
  * `Submodule.quotDualCoannihilatorToDual` is the nondegenerate pairing
    `M ‚ß∏ W.dualCoannihilator ‚Üí‚Çó[R] Dual R W`.
    It is an perfect pairing when `R` is a field and `W` is finite-dimensional.
* Vector spaces:
  * `Subspace.dualAnnihilator_dualCoannihilator_eq` says that the double dual annihilator,
    pulled back ground `Module.Dual.eval`, is the original submodule.
  * `Subspace.dualAnnihilator_gci` says that `module.dualAnnihilator_gc R M` is an
    antitone Galois coinsertion.
  * `Subspace.quotAnnihilatorEquiv` is the equivalence
    `Dual K V ‚ß∏ W.dualAnnihilator ‚âÉ‚Çó[K] Dual K W`.
  * `LinearMap.dualPairing_nondegenerate` says that `Module.dualPairing` is nondegenerate.
  * `Subspace.is_compl_dualAnnihilator` says that the dual annihilator carries complementary
    subspaces to complementary subspaces.
* Finite-dimensional vector spaces:
  * `Subspace.orderIsoFiniteCodimDim` is the antitone order isomorphism between
    finite-codimensional subspaces of `V` and finite-dimensional subspaces of `Dual K V`.
  * `Subspace.orderIsoFiniteDimensional` is the antitone order isomorphism between
    subspaces of a finite-dimensional vector space `V` and subspaces of its dual.
  * `Subspace.quotDualEquivAnnihilator W` is the equivalence
    `(Dual K V ‚ß∏ W.dualLift.range) ‚âÉ‚Çó[K] W.dualAnnihilator`, where `W.dualLift.range` is a copy
    of `Dual K W` inside `Dual K V`.
  * `Subspace.quotEquivAnnihilator W` is the equivalence `(V ‚ß∏ W) ‚âÉ‚Çó[K] W.dualAnnihilator`
  * `Subspace.dualQuotDistrib W` is an equivalence
    `Dual K (V‚ÇÅ ‚ß∏ W) ‚âÉ‚Çó[K] Dual K V‚ÇÅ ‚ß∏ W.dualLift.range` from an arbitrary choice of
    splitting of `V‚ÇÅ`.
-/

open Module Submodule

noncomputable section

namespace Module

variable (R A M : Type*)
variable [CommSemiring R] [AddCommMonoid M] [Module R M]

section Prod

variable (M' : Type*) [AddCommMonoid M'] [Module R M']

/-- Taking duals distributes over products. -/
@[simps!]
def dualProdDualEquivDual : (Module.Dual R M √ó Module.Dual R M') ‚âÉ‚Çó[R] Module.Dual R (M √ó M') :=
  LinearMap.coprodEquiv R

@[simp]
theorem dualProdDualEquivDual_apply (œÜ : Module.Dual R M) (œà : Module.Dual R M') :
    dualProdDualEquivDual R M M' (œÜ, œà) = œÜ.coprod œà :=
  rfl

end Prod

end Module

section

open Module Module.Dual Submodule LinearMap Cardinal Function

universe uR uM uK uV uŒπ
variable {R : Type uR} {M : Type uM} {K : Type uK} {V : Type uV} {Œπ : Type uŒπ}

section CommSemiring

variable [CommSemiring R] [AddCommMonoid M] [Module R M]

section Finite

variable [Finite Œπ]

-- Not sure whether this is true for free modules over a commutative ring
/-- A vector space over a field is isomorphic to its dual if and only if it is finite-dimensional:
  a consequence of the Erd≈ës-Kaplansky theorem. -/
theorem Basis.linearEquiv_dual_iff_finiteDimensional [Field K] [AddCommGroup V] [Module K V] :
    Nonempty (V ‚âÉ‚Çó[K] Dual K V) ‚Üî FiniteDimensional K V := by
  refine ‚ü®fun ‚ü®e‚ü© ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®(Module.Free.chooseBasis K V).toDualEquiv‚ü©‚ü©
  rw [FiniteDimensional, ‚Üê Module.rank_lt_aleph0_iff]
  by_contra!
  apply (lift_rank_lt_rank_dual this).ne
  have := e.lift_rank_eq
  rwa [lift_umax, lift_id'.{uV}] at this

theorem Module.Basis.dual_rank_eq (b : Basis Œπ R M) :
    Module.rank R (Dual R M) = Cardinal.lift.{uR,uM} (Module.rank R M) := by
  classical rw [‚Üê lift_umax.{uM,uR}, b.toDualEquiv.lift_rank_eq, lift_id'.{uM,uR}]

end Finite

namespace Module

variable [Module.Finite R M]

instance dual_free [Free R M] : Free R (Dual R M) :=
  Free.of_basis (Free.chooseBasis R M).dualBasis

instance dual_projective [Projective R M] : Projective R (Dual R M) :=
  have ‚ü®_, f, g, _, _, hfg‚ü© := Finite.exists_comp_eq_id_of_projective R M
  .of_split f.dualMap g.dualMap (congr_arg dualMap hfg)

instance dual_finite [Projective R M] : Module.Finite R (Dual R M) :=
  have ‚ü®n, f, g, _, _, hfg‚ü© := Finite.exists_comp_eq_id_of_projective R M
  have := Finite.of_basis (Free.chooseBasis R <| Fin n ‚Üí R).dualBasis
  .of_surjective _ (surjective_of_comp_eq_id f.dualMap g.dualMap <| congr_arg dualMap hfg)

end Module

@[deprecated (since := "2025-04-11")] alias Basis.dual_free := Module.dual_free
@[deprecated (since := "2025-04-11")] alias Basis.dual_projective := Module.dual_projective
@[deprecated (since := "2025-04-11")] alias Basis.dual_finite := Module.dual_finite

end CommSemiring

end

namespace Module

universe uK uV
variable {K : Type uK} {V : Type uV}
variable [CommSemiring K] [AddCommMonoid V] [Module K V] [Projective K V]

open Module Module.Dual Submodule LinearMap Cardinal Basis Module

section

variable (K)

theorem eval_apply_injective : Function.Injective (eval K V) :=
  have ‚ü®s, hs‚ü© := Module.projective_def'.mp ‚ÄπProjective K V‚Ä∫
  .of_comp (f := s.dualMap.dualMap)
    (Finsupp.basisSingleOne.eval_injective.comp <| injective_of_comp_eq_id s _ hs)

variable (V)

theorem eval_ker : LinearMap.ker (eval K V) = ‚ä• := ker_eq_bot_of_injective (eval_apply_injective K)

theorem map_eval_injective : (Submodule.map (eval K V)).Injective :=
  Submodule.map_injective_of_injective (eval_apply_injective K)

theorem comap_eval_surjective : (Submodule.comap (eval K V)).Surjective :=
  Submodule.comap_surjective_of_injective (eval_apply_injective K)

end

section

variable (K)

theorem eval_apply_eq_zero_iff (v : V) : (eval K V) v = 0 ‚Üî v = 0 :=
  SetLike.ext_iff.mp (eval_ker K V) v

theorem forall_dual_apply_eq_zero_iff (v : V) : (‚àÄ œÜ : Module.Dual K V, œÜ v = 0) ‚Üî v = 0 := by
  rw [‚Üê eval_apply_eq_zero_iff K v, LinearMap.ext_iff]
  simp only [eval_apply, zero_apply]

@[simp]
theorem subsingleton_dual_iff : Subsingleton (Dual K V) ‚Üî Subsingleton V :=
  ‚ü®fun _ ‚Ü¶ ‚ü®fun _ _ ‚Ü¶ eval_apply_injective K (Subsingleton.elim ..)‚ü©, fun _ ‚Ü¶ inferInstance‚ü©

@[simp]
theorem nontrivial_dual_iff : Nontrivial (Dual K V) ‚Üî Nontrivial V := by
  rw [‚Üê not_iff_not, not_nontrivial_iff_subsingleton, not_nontrivial_iff_subsingleton,
    subsingleton_dual_iff]

instance instNontrivialDual [Nontrivial V] : Nontrivial (Dual K V) :=
  (nontrivial_dual_iff K).mpr inferInstance

omit [Projective K V] in
/-- For an example of a non-free projective `K`-module `V` for which the forward implication
fails, see https://stacks.math.columbia.edu/tag/05WG#comment-9913. -/
theorem finite_dual_iff [Free K V] : Module.Finite K (Dual K V) ‚Üî Module.Finite K V := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun _ ‚Ü¶ inferInstance‚ü©
  have ‚ü®‚ü®Œπ, b‚ü©‚ü© := Free.exists_basis (R := K) (M := V)
  cases finite_or_infinite Œπ
  ¬∑ exact .of_basis b
  nontriviality K
  have ‚ü®n, hn‚ü© := Module.Finite.exists_nat_not_surjective K (Dual K V)
  let g := Finsupp.llift K K K Œπ ‚â™‚â´‚Çó b.repr.dualMap
  exact hn (LinearMap.funLeft K K (Fin.valEmbedding.trans (Infinite.natEmbedding Œπ)) ‚àò‚Çó _)
    ((Function.Embedding.injective _).surjective_comp_right.comp g.symm.surjective) |>.elim

end

omit [Projective K V]

theorem dual_rank_eq [Free K V] [Module.Finite K V] :
    Module.rank K (Dual K V) = Cardinal.lift.{uK,uV} (Module.rank K V) :=
  (Free.chooseBasis K V).dual_rank_eq

section IsReflexive

open Function

variable (R M N : Type*)
variable [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N] [Module R M] [Module R N]

/-- See also `Module.instFiniteDimensionalOfIsReflexive` for the converse over a field. -/
instance (priority := 900) IsReflexive.of_finite_of_free [Module.Finite R M] [Free R M] :
    IsReflexive R M where
  bijective_dual_eval'.left := (Free.chooseBasis R M).eval_injective
  bijective_dual_eval'.right := range_eq_top.mp (Free.chooseBasis R M).eval_range

variable [IsReflexive R M]

instance (priority := 900) [Module.Finite R N] [Projective R N] : IsReflexive R N :=
  have ‚ü®_, f, hf‚ü© := Finite.exists_fin' R N
  have ‚ü®g, H‚ü© := projective_lifting_property f .id hf
  .of_split g f H

instance _root_.Prod.instModuleIsReflexive [IsReflexive R N] :
    IsReflexive R (M √ó N) where
  bijective_dual_eval' := by
    let e : Dual R (Dual R (M √ó N)) ‚âÉ‚Çó[R] Dual R (Dual R M) √ó Dual R (Dual R N) :=
      (dualProdDualEquivDual R M N).dualMap.trans
        (dualProdDualEquivDual R (Dual R M) (Dual R N)).symm
    have : Dual.eval R (M √ó N) = e.symm.comp ((Dual.eval R M).prodMap (Dual.eval R N)) := by
      ext m f <;> simp [e]
    simp only [this,
      coe_comp, LinearEquiv.coe_coe, EquivLike.comp_bijective]
    exact (bijective_dual_eval R M).prodMap (bijective_dual_eval R N)

instance _root_.ULift.instModuleIsReflexive.{w} : IsReflexive R (ULift.{w} M) :=
  equiv ULift.moduleEquiv.symm

instance instFiniteDimensionalOfIsReflexive (K V : Type*)
    [Field K] [AddCommGroup V] [Module K V] [IsReflexive K V] :
    FiniteDimensional K V := by
  rw [FiniteDimensional, ‚Üê rank_lt_aleph0_iff]
  by_contra! contra
  suffices lift (Module.rank K V) < Module.rank K (Dual K (Dual K V)) by
    have heq := lift_rank_eq_of_equiv_equiv (R := K) (R' := K) (M := V) (M' := Dual K (Dual K V))
      (ZeroHom.id K) (evalEquiv K V) bijective_id (fun r v ‚Ü¶ (evalEquiv K V).map_smul _ _)
    rw [‚Üê lift_umax, heq, lift_id'] at this
    exact lt_irrefl _ this
  have h‚ÇÅ : lift (Module.rank K V) < Module.rank K (Dual K V) := lift_rank_lt_rank_dual contra
  have h‚ÇÇ : Module.rank K (Dual K V) < Module.rank K (Dual K (Dual K V)) := by
    convert lift_rank_lt_rank_dual <| le_trans (by simpa) h‚ÇÅ.le
    rw [lift_id']
  exact lt_trans h‚ÇÅ h‚ÇÇ

end IsReflexive

end Module

namespace Submodule

open Module

variable {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M] {p : Submodule R M}

@[simp]
theorem dualCoannihilator_top [Projective R M] :
    (‚ä§ : Submodule R (Module.Dual R M)).dualCoannihilator = ‚ä• := by
  rw [dualCoannihilator, dualAnnihilator_top, comap_bot, Module.eval_ker]

theorem exists_dual_map_eq_bot_of_notMem {x : M} (hx : x ‚àâ p) (hp' : Free R (M ‚ß∏ p)) :
    ‚àÉ f : Dual R M, f x ‚â† 0 ‚àß p.map f = ‚ä• := by
  suffices ‚àÉ f : Dual R (M ‚ß∏ p), f (p.mkQ x) ‚â† 0 by
    obtain ‚ü®f, hf‚ü© := this; exact ‚ü®f.comp p.mkQ, hf, by simp [Submodule.map_comp]‚ü©
  rwa [‚Üê Submodule.Quotient.mk_eq_zero, ‚Üê Submodule.mkQ_apply,
    ‚Üê forall_dual_apply_eq_zero_iff (K := R), not_forall] at hx

@[deprecated (since := "2025-05-24")]
alias exists_dual_map_eq_bot_of_nmem := exists_dual_map_eq_bot_of_notMem

theorem exists_dual_map_eq_bot_of_lt_top (hp : p < ‚ä§) (hp' : Free R (M ‚ß∏ p)) :
    ‚àÉ f : Dual R M, f ‚â† 0 ‚àß p.map f = ‚ä• := by
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : M, x ‚àâ p := by rw [lt_top_iff_ne_top] at hp; contrapose! hp; ext; simp [hp]
  obtain ‚ü®f, hf, hf'‚ü© := p.exists_dual_map_eq_bot_of_notMem hx hp'
  exact ‚ü®f, by aesop, hf'‚ü©

/-- Consider a reflexive module and a set `s` of linear forms. If for any `z ‚â† 0` there exists
`f ‚àà s` such that `f z ‚â† 0`, then `s` spans the whole dual space. -/
theorem span_eq_top_of_ne_zero [IsReflexive R M]
    {s : Set (M ‚Üí‚Çó[R] R)} [Free R ((M ‚Üí‚Çó[R] R) ‚ß∏ (span R s))]
    (h : ‚àÄ z ‚â† 0, ‚àÉ f ‚àà s, f z ‚â† 0) : span R s = ‚ä§ := by
  by_contra! hn
  obtain ‚ü®œÜ, œÜne, hœÜ‚ü© := exists_dual_map_eq_bot_of_lt_top hn.lt_top inferInstance
  let œÜs := (evalEquiv R M).symm œÜ
  have this f (hf : f ‚àà s) : f œÜs = 0 := by
    rw [‚Üê mem_bot R, ‚Üê hœÜ, mem_map]
    exact ‚ü®f, subset_span hf, (apply_evalEquiv_symm_apply R M f œÜ).symm‚ü©
  obtain ‚ü®x, xs, hx‚ü© := h œÜs (by simp [œÜne, œÜs])
  exact hx <| this x xs

variable {Œπ ùïú E : Type*} [Field ùïú] [AddCommGroup E] [Module ùïú E]

open LinearMap Set FiniteDimensional

theorem _root_.FiniteDimensional.mem_span_of_iInf_ker_le_ker [FiniteDimensional ùïú E]
    {L : Œπ ‚Üí E ‚Üí‚Çó[ùïú] ùïú} {K : E ‚Üí‚Çó[ùïú] ùïú}
    (h : ‚®Ö i, LinearMap.ker (L i) ‚â§ ker K) : K ‚àà span ùïú (range L) := by
  by_contra hK
  rcases exists_dual_map_eq_bot_of_notMem hK inferInstance with ‚ü®œÜ, œÜne, hœÜ‚ü©
  let œÜs := (Module.evalEquiv ùïú E).symm œÜ
  have : K œÜs = 0 := by
    refine h <| (Submodule.mem_iInf _).2 fun i ‚Ü¶ (mem_bot ùïú).1 ?_
    rw [‚Üê hœÜ, Submodule.mem_map]
    exact ‚ü®L i, Submodule.subset_span ‚ü®i, rfl‚ü©, (apply_evalEquiv_symm_apply ùïú E _ œÜ).symm‚ü©
  simp only [apply_evalEquiv_symm_apply, œÜs, œÜne] at this

/-- Given some linear forms $L_1, ..., L_n, K$ over a vector space $E$, if
$\bigcap_{i=1}^n \mathrm{ker}(L_i) \subseteq \mathrm{ker}(K)$, then $K$ is in the space generated
by $L_1, ..., L_n$. -/
theorem _root_.mem_span_of_iInf_ker_le_ker [Finite Œπ] {L : Œπ ‚Üí E ‚Üí‚Çó[ùïú] ùïú} {K : E ‚Üí‚Çó[ùïú] ùïú}
    (h : ‚®Ö i, ker (L i) ‚â§ ker K) : K ‚àà span ùïú (range L) := by
  have _ := Fintype.ofFinite Œπ
  let œÜ : E ‚Üí‚Çó[ùïú] Œπ ‚Üí ùïú := LinearMap.pi L
  let p := ‚®Ö i, ker (L i)
  have p_eq : p = ker œÜ := (ker_pi L).symm
  let œà : (E ‚ß∏ p) ‚Üí‚Çó[ùïú] Œπ ‚Üí ùïú := p.liftQ œÜ p_eq.le
  have _ : FiniteDimensional ùïú (E ‚ß∏ p) := of_injective œà (ker_eq_bot.1 (ker_liftQ_eq_bot' p œÜ p_eq))
  let L' i : (E ‚ß∏ p) ‚Üí‚Çó[ùïú] ùïú := p.liftQ (L i) (iInf_le _ i)
  let K' : (E ‚ß∏ p) ‚Üí‚Çó[ùïú] ùïú := p.liftQ K h
  have : ‚®Ö i, ker (L' i) ‚â§ ker K' := by
    simp_rw +zetaDelta [‚Üê ker_pi, pi_liftQ_eq_liftQ_pi, ker_liftQ_eq_bot' p œÜ p_eq]
    exact bot_le
  obtain ‚ü®c, hK'‚ü© :=
    (mem_span_range_iff_exists_fun ùïú).1 (FiniteDimensional.mem_span_of_iInf_ker_le_ker this)
  refine (mem_span_range_iff_exists_fun ùïú).2 ‚ü®c, ?_‚ü©
  conv_lhs => enter [2]; intro i; rw [‚Üê p.liftQ_mkQ (L i) (iInf_le _ i)]
  rw [‚Üê p.liftQ_mkQ K h]
  ext x
  convert LinearMap.congr_fun hK' (p.mkQ x)
  simp only [L',coeFn_sum, Finset.sum_apply, smul_apply, coe_comp, Function.comp_apply,
    smul_eq_mul]

end Submodule

namespace Subspace

open Submodule LinearMap

-- We work in vector spaces because `exists_isCompl` only hold for vector spaces
variable {K V : Type*} [Field K] [AddCommGroup V] [Module K V]

@[simp]
theorem dualAnnihilator_dualCoannihilator_eq {W : Subspace K V} :
    W.dualAnnihilator.dualCoannihilator = W := by
  refine le_antisymm (fun v ‚Ü¶ Function.mtr ?_) (le_dualAnnihilator_dualCoannihilator _)
  simp only [mem_dualAnnihilator, mem_dualCoannihilator]
  rw [‚Üê Quotient.mk_eq_zero W, ‚Üê Module.forall_dual_apply_eq_zero_iff K]
  push_neg
  refine fun ‚ü®œÜ, hœÜ‚ü© ‚Ü¶ ‚ü®œÜ.comp W.mkQ, fun w hw ‚Ü¶ ?_, hœÜ‚ü©
  rw [comp_apply, mkQ_apply, (Quotient.mk_eq_zero W).mpr hw, œÜ.map_zero]

-- exact elaborates slowly
theorem forall_mem_dualAnnihilator_apply_eq_zero_iff (W : Subspace K V) (v : V) :
    (‚àÄ œÜ : Module.Dual K V, œÜ ‚àà W.dualAnnihilator ‚Üí œÜ v = 0) ‚Üî v ‚àà W := by
  rw [‚Üê SetLike.ext_iff.mp dualAnnihilator_dualCoannihilator_eq v, mem_dualCoannihilator]

theorem comap_dualAnnihilator_dualAnnihilator (W : Subspace K V) :
    W.dualAnnihilator.dualAnnihilator.comap (Module.Dual.eval K V) = W := by
  ext; rw [Iff.comm, ‚Üê forall_mem_dualAnnihilator_apply_eq_zero_iff]; simp

theorem map_le_dualAnnihilator_dualAnnihilator (W : Subspace K V) :
    W.map (Module.Dual.eval K V) ‚â§ W.dualAnnihilator.dualAnnihilator :=
  map_le_iff_le_comap.mpr (comap_dualAnnihilator_dualAnnihilator W).ge

/-- `Submodule.dualAnnihilator` and `Submodule.dualCoannihilator` form a Galois coinsertion. -/
def dualAnnihilatorGci (K V : Type*) [Field K] [AddCommGroup V] [Module K V] :
    GaloisCoinsertion
      (OrderDual.toDual ‚àò (dualAnnihilator : Subspace K V ‚Üí Subspace K (Module.Dual K V)))
      (dualCoannihilator ‚àò OrderDual.ofDual) where
  choice W _ := dualCoannihilator W
  gc := dualAnnihilator_gc K V
  u_l_le _ := dualAnnihilator_dualCoannihilator_eq.le
  choice_eq _ _ := rfl

theorem dualAnnihilator_le_dualAnnihilator_iff {W W' : Subspace K V} :
    W.dualAnnihilator ‚â§ W'.dualAnnihilator ‚Üî W' ‚â§ W :=
  (dualAnnihilatorGci K V).l_le_l_iff

theorem dualAnnihilator_inj {W W' : Subspace K V} :
    W.dualAnnihilator = W'.dualAnnihilator ‚Üî W = W' :=
  ‚ü®fun h ‚Ü¶ (dualAnnihilatorGci K V).l_injective h, congr_arg _‚ü©

/-- Given a subspace `W` of `V` and an element of its dual `œÜ`, `dualLift W œÜ` is
an arbitrary extension of `œÜ` to an element of the dual of `V`.
That is, `dualLift W œÜ` sends `w ‚àà W` to `œÜ x` and `x` in a chosen complement of `W` to `0`. -/
noncomputable def dualLift (W : Subspace K V) : Module.Dual K W ‚Üí‚Çó[K] Module.Dual K V :=
  (Classical.choose <| W.subtype.exists_leftInverse_of_injective W.ker_subtype).dualMap

variable {W : Subspace K V}

@[simp]
theorem dualLift_of_subtype {œÜ : Module.Dual K W} (w : W) : W.dualLift œÜ (w : V) = œÜ w :=
  congr_arg œÜ <| DFunLike.congr_fun
    (Classical.choose_spec <| W.subtype.exists_leftInverse_of_injective W.ker_subtype) w

theorem dualLift_of_mem {œÜ : Module.Dual K W} {w : V} (hw : w ‚àà W) : W.dualLift œÜ w = œÜ ‚ü®w, hw‚ü© :=
  dualLift_of_subtype ‚ü®w, hw‚ü©

@[simp]
theorem dualRestrict_comp_dualLift (W : Subspace K V) : W.dualRestrict.comp W.dualLift = 1 := by
  ext œÜ x
  simp

theorem dualRestrict_leftInverse (W : Subspace K V) :
    Function.LeftInverse W.dualRestrict W.dualLift := fun x => by
  rw [‚Üê LinearMap.comp_apply, dualRestrict_comp_dualLift, End.one_apply]

theorem dualLift_rightInverse (W : Subspace K V) :
    Function.RightInverse W.dualLift W.dualRestrict :=
  W.dualRestrict_leftInverse

theorem dualRestrict_surjective : Function.Surjective W.dualRestrict :=
  W.dualLift_rightInverse.surjective

theorem dualLift_injective : Function.Injective W.dualLift :=
  W.dualRestrict_leftInverse.injective

/-- The quotient by the `dualAnnihilator` of a subspace is isomorphic to the
  dual of that subspace. -/
noncomputable def quotAnnihilatorEquiv (W : Subspace K V) :
    (Module.Dual K V ‚ß∏ W.dualAnnihilator) ‚âÉ‚Çó[K] Module.Dual K W :=
  (quotEquivOfEq _ _ W.dualRestrict_ker_eq_dualAnnihilator).symm.trans <|
    W.dualRestrict.quotKerEquivOfSurjective dualRestrict_surjective

@[simp]
theorem quotAnnihilatorEquiv_apply (W : Subspace K V) (œÜ : Module.Dual K V) :
    W.quotAnnihilatorEquiv (Submodule.Quotient.mk œÜ) = W.dualRestrict œÜ := by
  ext
  rfl

/-- The natural isomorphism from the dual of a subspace `W` to `W.dualLift.range`. -/
noncomputable def dualEquivDual (W : Subspace K V) :
    Module.Dual K W ‚âÉ‚Çó[K] LinearMap.range W.dualLift :=
  LinearEquiv.ofInjective _ dualLift_injective

theorem dualEquivDual_def (W : Subspace K V) :
    W.dualEquivDual.toLinearMap = W.dualLift.rangeRestrict :=
  rfl

@[simp]
theorem dualEquivDual_apply (œÜ : Module.Dual K W) :
    W.dualEquivDual œÜ = ‚ü®W.dualLift œÜ, mem_range.2 ‚ü®œÜ, rfl‚ü©‚ü© :=
  rfl

section

open FiniteDimensional Module

instance instModuleDualFiniteDimensional [FiniteDimensional K V] :
    FiniteDimensional K (Module.Dual K V) := by
  infer_instance

@[simp]
theorem dual_finrank_eq : finrank K (Module.Dual K V) = finrank K V := by
  by_cases h : FiniteDimensional K V
  ¬∑ classical exact LinearEquiv.finrank_eq (Basis.ofVectorSpace K V).toDualEquiv.symm
  rw [finrank_eq_zero_of_basis_imp_false, finrank_eq_zero_of_basis_imp_false]
  ¬∑ exact fun _ b ‚Ü¶ h (Module.Finite.of_basis b)
  ¬∑ exact fun _ b ‚Ü¶ h ((Module.finite_dual_iff K).mp <| Module.Finite.of_basis b)

variable [FiniteDimensional K V]

theorem dualAnnihilator_dualAnnihilator_eq (W : Subspace K V) :
    W.dualAnnihilator.dualAnnihilator = Module.mapEvalEquiv K V W := by
  have : _ = W := Subspace.dualAnnihilator_dualCoannihilator_eq
  rw [dualCoannihilator, ‚Üê Module.mapEvalEquiv_symm_apply] at this
  rwa [‚Üê OrderIso.symm_apply_eq]

/-- The quotient by the dual is isomorphic to its dual annihilator. -/
noncomputable def quotDualEquivAnnihilator (W : Subspace K V) :
    (Module.Dual K V ‚ß∏ LinearMap.range W.dualLift) ‚âÉ‚Çó[K] W.dualAnnihilator :=
  LinearEquiv.quotEquivOfQuotEquiv <| LinearEquiv.trans W.quotAnnihilatorEquiv W.dualEquivDual

open scoped Classical in
/-- The quotient by a subspace is isomorphic to its dual annihilator. -/
noncomputable def quotEquivAnnihilator (W : Subspace K V) : (V ‚ß∏ W) ‚âÉ‚Çó[K] W.dualAnnihilator :=
  let œÜ := (Basis.ofVectorSpace K W).toDualEquiv.trans W.dualEquivDual
  let œà := LinearEquiv.quotEquivOfEquiv œÜ (Basis.ofVectorSpace K V).toDualEquiv
  œà ‚â™‚â´‚Çó W.quotDualEquivAnnihilator

open Module

theorem finrank_add_finrank_dualAnnihilator_eq (W : Subspace K V) :
    finrank K W + finrank K W.dualAnnihilator = finrank K V := by
  rw [‚Üê W.quotEquivAnnihilator.finrank_eq, add_comm, Submodule.finrank_quotient_add_finrank]

@[simp]
theorem finrank_dualCoannihilator_eq {Œ¶ : Subspace K (Module.Dual K V)} :
    finrank K Œ¶.dualCoannihilator = finrank K Œ¶.dualAnnihilator := by
  rw [Submodule.dualCoannihilator, ‚Üê Module.evalEquiv_toLinearMap]
  exact LinearEquiv.finrank_eq (LinearEquiv.ofSubmodule' _ _)

theorem finrank_add_finrank_dualCoannihilator_eq (W : Subspace K (Module.Dual K V)) :
    finrank K W + finrank K W.dualCoannihilator = finrank K V := by
  rw [finrank_dualCoannihilator_eq, finrank_add_finrank_dualAnnihilator_eq, dual_finrank_eq]

end

end Subspace

open Module

section CommRing

variable {R M M' : Type*}
variable [CommRing R] [AddCommGroup M] [Module R M] [AddCommGroup M'] [Module R M']

namespace Submodule

/-- Given a submodule, corestrict to the pairing on `M ‚ß∏ W` by
simultaneously restricting to `W.dualAnnihilator`.

See `Subspace.dualCopairing_nondegenerate`. -/
def dualCopairing (W : Submodule R M) : W.dualAnnihilator ‚Üí‚Çó[R] M ‚ß∏ W ‚Üí‚Çó[R] R :=
  LinearMap.flip <|
    W.liftQ ((Module.dualPairing R M).domRestrict W.dualAnnihilator).flip
      (by
        intro w hw
        ext ‚ü®œÜ, hœÜ‚ü©
        exact (mem_dualAnnihilator œÜ).mp hœÜ w hw)

instance (W : Submodule R M) : FunLike (W.dualAnnihilator) M R where
  coe œÜ := œÜ.val
  coe_injective' œÜ œà h := by
    ext
    simp only [funext_iff] at h
    exact h _

@[simp]
theorem dualCopairing_apply {W : Submodule R M} (œÜ : W.dualAnnihilator) (x : M) :
    W.dualCopairing œÜ (Quotient.mk x) = œÜ x :=
  rfl

/-- Given a submodule, restrict to the pairing on `W` by
simultaneously corestricting to `Module.Dual R M ‚ß∏ W.dualAnnihilator`.
This is `Submodule.dualRestrict` factored through the quotient by its kernel (which
is `W.dualAnnihilator` by definition).

See `Subspace.dualPairing_nondegenerate`. -/
def dualPairing (W : Submodule R M) : Module.Dual R M ‚ß∏ W.dualAnnihilator ‚Üí‚Çó[R] W ‚Üí‚Çó[R] R :=
  W.dualAnnihilator.liftQ W.dualRestrict le_rfl

@[simp]
theorem dualPairing_apply {W : Submodule R M} (œÜ : Module.Dual R M) (x : W) :
    W.dualPairing (Quotient.mk œÜ) x = œÜ x :=
  rfl

/-- That $\operatorname{im}(q^* : (V/W)^* \to V^*) = \operatorname{ann}(W)$. -/
theorem range_dualMap_mkQ_eq (W : Submodule R M) :
    LinearMap.range W.mkQ.dualMap = W.dualAnnihilator := by
  ext œÜ
  rw [LinearMap.mem_range]
  constructor
  ¬∑ rintro ‚ü®œà, rfl‚ü©
    have := LinearMap.mem_range_self W.mkQ.dualMap œà
    simpa only [ker_mkQ] using W.mkQ.range_dualMap_le_dualAnnihilator_ker this
  ¬∑ intro hœÜ
    exists W.dualCopairing ‚ü®œÜ, hœÜ‚ü©

/-- Equivalence $(M/W)^* \cong \operatorname{ann}(W)$. That is, there is a one-to-one
correspondence between the dual of `M ‚ß∏ W` and those elements of the dual of `M` that
vanish on `W`.

The inverse of this is `Submodule.dualCopairing`. -/
def dualQuotEquivDualAnnihilator (W : Submodule R M) :
    Module.Dual R (M ‚ß∏ W) ‚âÉ‚Çó[R] W.dualAnnihilator :=
  LinearEquiv.ofLinear
    (W.mkQ.dualMap.codRestrict W.dualAnnihilator fun œÜ =>
      W.range_dualMap_mkQ_eq ‚ñ∏ LinearMap.mem_range_self W.mkQ.dualMap œÜ)
    W.dualCopairing (by ext; rfl) (by ext; rfl)

@[simp]
theorem dualQuotEquivDualAnnihilator_apply (W : Submodule R M) (œÜ : Module.Dual R (M ‚ß∏ W)) (x : M) :
    dualQuotEquivDualAnnihilator W œÜ x = œÜ (Quotient.mk x) :=
  rfl

theorem dualCopairing_eq (W : Submodule R M) :
    W.dualCopairing = (dualQuotEquivDualAnnihilator W).symm.toLinearMap :=
  rfl

@[simp]
theorem dualQuotEquivDualAnnihilator_symm_apply_mk (W : Submodule R M) (œÜ : W.dualAnnihilator)
    (x : M) : (dualQuotEquivDualAnnihilator W).symm œÜ (Quotient.mk x) = œÜ x :=
  rfl

theorem finite_dualAnnihilator_iff {W : Submodule R M} [Free R (M ‚ß∏ W)] :
    Module.Finite R W.dualAnnihilator ‚Üî Module.Finite R (M ‚ß∏ W) :=
  (Finite.equiv_iff W.dualQuotEquivDualAnnihilator.symm).trans (finite_dual_iff R)

lemma dualAnnihilator_eq_bot_iff' {W : Submodule R M} :
    W.dualAnnihilator = ‚ä• ‚Üî Subsingleton (Dual R (M ‚ß∏ W)) := by
  rw [W.dualQuotEquivDualAnnihilator.toEquiv.subsingleton_congr, subsingleton_iff_eq_bot]

@[simp] lemma dualAnnihilator_eq_bot_iff {W : Submodule R M} [Projective R (M ‚ß∏ W)] :
    W.dualAnnihilator = ‚ä• ‚Üî W = ‚ä§ := by
  rw [dualAnnihilator_eq_bot_iff', subsingleton_dual_iff, subsingleton_quotient_iff_eq_top]

@[simp] lemma dualAnnihilator_eq_top_iff {W : Submodule R M} [Projective R M] :
    W.dualAnnihilator = ‚ä§ ‚Üî W = ‚ä• := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ h ‚ñ∏ dualAnnihilator_bot‚ü©
  refine W.eq_bot_iff.mpr fun v hv ‚Ü¶ (forall_dual_apply_eq_zero_iff R v).mp fun f ‚Ü¶ ?_
  refine (mem_dualAnnihilator f).mp ?_ v hv
  simp [h]

open LinearMap in
/-- The pairing between a submodule `W` of a dual module `Dual R M` and the quotient of
`M` by the coannihilator of `W`, which is always nondegenerate. -/
def quotDualCoannihilatorToDual (W : Submodule R (Dual R M)) :
    M ‚ß∏ W.dualCoannihilator ‚Üí‚Çó[R] Dual R W :=
  liftQ _ (flip <| Submodule.subtype _) le_rfl

@[simp]
theorem quotDualCoannihilatorToDual_apply (W : Submodule R (Dual R M)) (m : M) (w : W) :
    W.quotDualCoannihilatorToDual (Quotient.mk m) w = w.1 m := rfl

theorem quotDualCoannihilatorToDual_injective (W : Submodule R (Dual R M)) :
    Function.Injective W.quotDualCoannihilatorToDual :=
  LinearMap.ker_eq_bot.mp (ker_liftQ_eq_bot _ _ _ le_rfl)

theorem flip_quotDualCoannihilatorToDual_injective (W : Submodule R (Dual R M)) :
    Function.Injective W.quotDualCoannihilatorToDual.flip :=
  fun _ _ he ‚Ü¶ Subtype.ext <| LinearMap.ext fun m ‚Ü¶ DFunLike.congr_fun he ‚ü¶m‚üß

open LinearMap in
theorem quotDualCoannihilatorToDual_nondegenerate (W : Submodule R (Dual R M)) :
    W.quotDualCoannihilatorToDual.Nondegenerate := by
  rw [Nondegenerate, separatingLeft_iff_ker_eq_bot, separatingRight_iff_flip_ker_eq_bot]
  letI : AddCommGroup W := inferInstance
  simp_rw [ker_eq_bot]
  exact ‚ü®W.quotDualCoannihilatorToDual_injective, W.flip_quotDualCoannihilatorToDual_injective‚ü©

end Submodule

namespace LinearMap

open Submodule

theorem range_dualMap_eq_dualAnnihilator_ker_of_surjective (f : M ‚Üí‚Çó[R] M')
    (hf : Function.Surjective f) : LinearMap.range f.dualMap = (LinearMap.ker f).dualAnnihilator :=
  ((f.quotKerEquivOfSurjective hf).dualMap.range_comp _).trans
    (LinearMap.ker f).range_dualMap_mkQ_eq

-- Note, this can be specialized to the case where `R` is an injective `R`-module, or when
-- `f.coker` is a projective `R`-module.
theorem range_dualMap_eq_dualAnnihilator_ker_of_subtype_range_surjective (f : M ‚Üí‚Çó[R] M')
    (hf : Function.Surjective (range f).subtype.dualMap) :
    LinearMap.range f.dualMap = (ker f).dualAnnihilator := by
  have rr_surj : Function.Surjective f.rangeRestrict := by
    rw [‚Üê range_eq_top, range_rangeRestrict]
  have := range_dualMap_eq_dualAnnihilator_ker_of_surjective f.rangeRestrict rr_surj
  convert this using 1
  ¬∑ calc
      _ = range ((range f).subtype.comp f.rangeRestrict).dualMap := by simp
      _ = _ := ?_
    rw [‚Üê dualMap_comp_dualMap, range_comp_of_range_eq_top]
    rwa [range_eq_top]
  ¬∑ apply congr_arg
    exact (ker_rangeRestrict f).symm

end LinearMap

end CommRing

section VectorSpace

variable {K V‚ÇÅ V‚ÇÇ : Type*} [Field K]
variable [AddCommGroup V‚ÇÅ] [Module K V‚ÇÅ] [AddCommGroup V‚ÇÇ] [Module K V‚ÇÇ]

namespace Module.Dual

variable {f : Module.Dual K V‚ÇÅ}

section
variable (hf : f ‚â† 0)

lemma range_eq_top_of_ne_zero {K V‚ÇÅ : Type*} [Semifield K] [AddCommMonoid V‚ÇÅ] [Module K V‚ÇÅ]
    {f : Module.Dual K V‚ÇÅ} (hf : f ‚â† 0) : LinearMap.range f = ‚ä§ :=
  LinearMap.range_eq_top.mpr (LinearMap.surjective hf)

variable [FiniteDimensional K V‚ÇÅ]
include hf

lemma finrank_ker_add_one_of_ne_zero :
    finrank K (LinearMap.ker f) + 1 = finrank K V‚ÇÅ := by
  suffices finrank K (LinearMap.range f) = 1 by
    rw [‚Üê (LinearMap.ker f).finrank_quotient_add_finrank, add_comm, add_left_inj,
    f.quotKerEquivRange.finrank_eq, this]
  rw [range_eq_top_of_ne_zero hf, finrank_top, finrank_self]

lemma isCompl_ker_of_disjoint_of_ne_bot {p : Submodule K V‚ÇÅ}
    (hpf : Disjoint (LinearMap.ker f) p) (hp : p ‚â† ‚ä•) :
    IsCompl (LinearMap.ker f) p := by
  refine ‚ü®hpf, codisjoint_iff.mpr <| eq_of_le_of_finrank_le le_top ?_‚ü©
  have : finrank K ‚Üë(LinearMap.ker f ‚äî p) = finrank K (LinearMap.ker f) + finrank K p := by
    simp [‚Üê Submodule.finrank_sup_add_finrank_inf_eq (LinearMap.ker f) p, hpf.eq_bot]
  rwa [finrank_top, this, ‚Üê finrank_ker_add_one_of_ne_zero hf, add_le_add_iff_left,
    Submodule.one_le_finrank_iff]

end

lemma eq_of_ker_eq_of_apply_eq [FiniteDimensional K V‚ÇÅ] {f g : Module.Dual K V‚ÇÅ} (x : V‚ÇÅ)
    (h : LinearMap.ker f = LinearMap.ker g) (h' : f x = g x) (hx : f x ‚â† 0) :
    f = g := by
  let p := K ‚àô x
  have hp : p ‚â† ‚ä• := by aesop
  have hpf : Disjoint (LinearMap.ker f) p := by
    rw [disjoint_iff, Submodule.eq_bot_iff]
    rintro y ‚ü®hfy : f y = 0, hpy : y ‚àà p‚ü©
    obtain ‚ü®t, rfl‚ü© := Submodule.mem_span_singleton.mp hpy
    have ht : t = 0 := by simpa [hx] using hfy
    simp [ht]
  have hf : f ‚â† 0 := by aesop
  ext v
  obtain ‚ü®y, hy, z, hz, rfl‚ü© : ‚àÉ·µâ (y ‚àà LinearMap.ker f) (z ‚àà p), y + z = v := by
    have : v ‚àà (‚ä§ : Submodule K V‚ÇÅ) := Submodule.mem_top
    rwa [‚Üê (isCompl_ker_of_disjoint_of_ne_bot hf hpf hp).sup_eq_top, Submodule.mem_sup] at this
  have hy' : g y = 0 := by rwa [‚Üê LinearMap.mem_ker, ‚Üê h]
  replace hy : f y = 0 := by rwa [LinearMap.mem_ker] at hy
  obtain ‚ü®t, rfl‚ü© := Submodule.mem_span_singleton.mp hz
  simp [h', hy, hy']

end Module.Dual

namespace LinearMap

theorem dualPairing_nondegenerate : (dualPairing K V‚ÇÅ).Nondegenerate :=
  ‚ü®separatingLeft_iff_ker_eq_bot.mpr ker_id, fun x => (forall_dual_apply_eq_zero_iff K x).mp‚ü©

theorem dualMap_surjective_of_injective {f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ} (hf : Function.Injective f) :
    Function.Surjective f.dualMap := fun œÜ ‚Ü¶
  have ‚ü®f', hf'‚ü© := f.exists_leftInverse_of_injective (ker_eq_bot.mpr hf)
  ‚ü®œÜ.comp f', ext fun x ‚Ü¶ congr(œÜ <| $hf' x)‚ü©

theorem range_dualMap_eq_dualAnnihilator_ker (f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ) :
    LinearMap.range f.dualMap = (LinearMap.ker f).dualAnnihilator :=
  range_dualMap_eq_dualAnnihilator_ker_of_subtype_range_surjective f <|
    dualMap_surjective_of_injective (range f).injective_subtype

/-- For vector spaces, `f.dualMap` is surjective if and only if `f` is injective -/
@[simp]
theorem dualMap_surjective_iff {f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ} :
    Function.Surjective f.dualMap ‚Üî Function.Injective f := by
  rw [‚Üê LinearMap.range_eq_top, range_dualMap_eq_dualAnnihilator_ker,
      ‚Üê Submodule.dualAnnihilator_bot, Subspace.dualAnnihilator_inj, LinearMap.ker_eq_bot]

end LinearMap

namespace Subspace

open Submodule

theorem dualPairing_eq (W : Subspace K V‚ÇÅ) :
    W.dualPairing = W.quotAnnihilatorEquiv.toLinearMap := by
  ext
  rfl

theorem dualPairing_nondegenerate (W : Subspace K V‚ÇÅ) : W.dualPairing.Nondegenerate := by
  constructor
  ¬∑ rw [LinearMap.separatingLeft_iff_ker_eq_bot, dualPairing_eq]
    apply LinearEquiv.ker
  ¬∑ intro x h
    rw [‚Üê forall_dual_apply_eq_zero_iff K x]
    intro œÜ
    simpa only [Submodule.dualPairing_apply, dualLift_of_subtype] using
      h (Submodule.Quotient.mk (W.dualLift œÜ))

theorem dualCopairing_nondegenerate (W : Subspace K V‚ÇÅ) : W.dualCopairing.Nondegenerate := by
  constructor
  ¬∑ rw [LinearMap.separatingLeft_iff_ker_eq_bot, dualCopairing_eq]
    apply LinearEquiv.ker
  ¬∑ rintro ‚ü®x‚ü©
    simp only [Quotient.quot_mk_eq_mk, dualCopairing_apply, Quotient.mk_eq_zero]
    rw [‚Üê forall_mem_dualAnnihilator_apply_eq_zero_iff, SetLike.forall]
    exact id

-- Argument from https://math.stackexchange.com/a/2423263/172988
theorem dualAnnihilator_inf_eq (W W' : Subspace K V‚ÇÅ) :
    (W ‚äì W').dualAnnihilator = W.dualAnnihilator ‚äî W'.dualAnnihilator := by
  refine le_antisymm ?_ (sup_dualAnnihilator_le_inf W W')
  let F : V‚ÇÅ ‚Üí‚Çó[K] (V‚ÇÅ ‚ß∏ W) √ó V‚ÇÅ ‚ß∏ W' := (Submodule.mkQ W).prod (Submodule.mkQ W')
  have : LinearMap.ker F = W ‚äì W' := by simp only [F, LinearMap.ker_prod, ker_mkQ]
  rw [‚Üê this, ‚Üê LinearMap.range_dualMap_eq_dualAnnihilator_ker]
  intro œÜ
  rw [LinearMap.mem_range]
  rintro ‚ü®x, rfl‚ü©
  rw [Submodule.mem_sup]
  obtain ‚ü®‚ü®a, b‚ü©, rfl‚ü© := (dualProdDualEquivDual K (V‚ÇÅ ‚ß∏ W) (V‚ÇÅ ‚ß∏ W')).surjective x
  obtain ‚ü®a', rfl‚ü© := (dualQuotEquivDualAnnihilator W).symm.surjective a
  obtain ‚ü®b', rfl‚ü© := (dualQuotEquivDualAnnihilator W').symm.surjective b
  use a', a'.property, b', b'.property
  rfl

-- This is also true if `V‚ÇÅ` is finite dimensional since one can restrict `Œπ` to some subtype
-- for which the infimum and supremum are the same.
-- The obstruction to the `dualAnnihilator_inf_eq` argument carrying through is that we need
-- for `Module.Dual R (Œ† (i : Œπ), V ‚ß∏ W i) ‚âÉ‚Çó[K] Œ† (i : Œπ), Module.Dual R (V ‚ß∏ W i)`, which is not
-- true for infinite `Œπ`. One would need to add additional hypothesis on `W` (for example, it might
-- be true when the family is inf-closed).
-- TODO: generalize to `Sort`
theorem dualAnnihilator_iInf_eq {Œπ : Type*} [Finite Œπ] (W : Œπ ‚Üí Subspace K V‚ÇÅ) :
    (‚®Ö i : Œπ, W i).dualAnnihilator = ‚®Ü i : Œπ, (W i).dualAnnihilator := by
  revert Œπ
  apply Finite.induction_empty_option
  ¬∑ intro Œ± Œ≤ h hyp W
    rw [‚Üê h.iInf_comp, hyp _, ‚Üê h.iSup_comp]
  ¬∑ intro W
    rw [iSup_of_empty', iInf_of_isEmpty, sInf_empty, sSup_empty, dualAnnihilator_top]
  ¬∑ intro Œ± _ h W
    rw [iInf_option, iSup_option, dualAnnihilator_inf_eq, h]

/-- For vector spaces, dual annihilators carry direct sum decompositions
to direct sum decompositions. -/
theorem isCompl_dualAnnihilator {W W' : Subspace K V‚ÇÅ} (h : IsCompl W W') :
    IsCompl W.dualAnnihilator W'.dualAnnihilator := by
  rw [isCompl_iff, disjoint_iff, codisjoint_iff] at h ‚ä¢
  rw [‚Üê dualAnnihilator_inf_eq, ‚Üê dualAnnihilator_sup_eq, h.1, h.2, dualAnnihilator_top,
    dualAnnihilator_bot]
  exact ‚ü®rfl, rfl‚ü©

/-- For finite-dimensional vector spaces, one can distribute duals over quotients by identifying
`W.dualLift.range` with `W`. Note that this depends on a choice of splitting of `V‚ÇÅ`. -/
def dualQuotDistrib [FiniteDimensional K V‚ÇÅ] (W : Subspace K V‚ÇÅ) :
    Module.Dual K (V‚ÇÅ ‚ß∏ W) ‚âÉ‚Çó[K] Module.Dual K V‚ÇÅ ‚ß∏ LinearMap.range W.dualLift :=
  W.dualQuotEquivDualAnnihilator.trans W.quotDualEquivAnnihilator.symm

end Subspace

section FiniteDimensional

open Module LinearMap

namespace LinearMap

@[simp]
theorem finrank_range_dualMap_eq_finrank_range (f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ) :
    finrank K (LinearMap.range f.dualMap) = finrank K (LinearMap.range f) := by
  rw [congr_arg dualMap (show f = (range f).subtype.comp f.rangeRestrict by rfl),
    ‚Üê dualMap_comp_dualMap, range_comp,
    range_eq_top.mpr (dualMap_surjective_of_injective (range f).injective_subtype),
    Submodule.map_top, finrank_range_of_inj, Subspace.dual_finrank_eq]
  exact dualMap_injective_of_surjective (range_eq_top.mp f.range_rangeRestrict)

/-- `f.dualMap` is injective if and only if `f` is surjective -/
@[simp]
theorem dualMap_injective_iff {f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ} :
    Function.Injective f.dualMap ‚Üî Function.Surjective f := by
  refine ‚ü®Function.mtr fun not_surj inj ‚Ü¶ ?_, dualMap_injective_of_surjective‚ü©
  rw [‚Üê range_eq_top, ‚Üê Ne, ‚Üê lt_top_iff_ne_top] at not_surj
  obtain ‚ü®œÜ, œÜ0, range_le_ker‚ü© := (range f).exists_le_ker_of_lt_top not_surj
  exact œÜ0 (inj <| ext fun x ‚Ü¶ range_le_ker ‚ü®x, rfl‚ü©)

/-- `f.dualMap` is bijective if and only if `f` is -/
@[simp]
theorem dualMap_bijective_iff {f : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ} :
    Function.Bijective f.dualMap ‚Üî Function.Bijective f := by
  simp_rw [Function.Bijective, dualMap_surjective_iff, dualMap_injective_iff, and_comm]

variable {B : V‚ÇÅ ‚Üí‚Çó[K] V‚ÇÇ ‚Üí‚Çó[K] K}

@[simp]
lemma dualAnnihilator_ker_eq_range_flip [IsReflexive K V‚ÇÇ] :
    (ker B).dualAnnihilator = range B.flip := by
  change _ = range (B.dualMap.comp (Module.evalEquiv K V‚ÇÇ).toLinearMap)
  rw [‚Üê range_dualMap_eq_dualAnnihilator_ker, range_comp_of_range_eq_top _ (LinearEquiv.range _)]

open Function

theorem flip_injective_iff‚ÇÅ [FiniteDimensional K V‚ÇÅ] : Injective B.flip ‚Üî Surjective B := by
  rw [‚Üê dualMap_surjective_iff, ‚Üê (evalEquiv K V‚ÇÅ).toEquiv.surjective_comp]; rfl

theorem flip_injective_iff‚ÇÇ [FiniteDimensional K V‚ÇÇ] : Injective B.flip ‚Üî Surjective B := by
  rw [‚Üê dualMap_injective_iff]; exact (evalEquiv K V‚ÇÇ).toEquiv.injective_comp B.dualMap

theorem flip_surjective_iff‚ÇÅ [FiniteDimensional K V‚ÇÅ] : Surjective B.flip ‚Üî Injective B :=
  flip_injective_iff‚ÇÇ.symm

theorem flip_surjective_iff‚ÇÇ [FiniteDimensional K V‚ÇÇ] : Surjective B.flip ‚Üî Injective B :=
  flip_injective_iff‚ÇÅ.symm

theorem flip_bijective_iff‚ÇÅ [FiniteDimensional K V‚ÇÅ] : Bijective B.flip ‚Üî Bijective B := by
  simp_rw [Bijective, flip_injective_iff‚ÇÅ, flip_surjective_iff‚ÇÅ, and_comm]

theorem flip_bijective_iff‚ÇÇ [FiniteDimensional K V‚ÇÇ] : Bijective B.flip ‚Üî Bijective B :=
  flip_bijective_iff‚ÇÅ.symm

end LinearMap

namespace Subspace

variable {K V : Type*} [Field K] [AddCommGroup V] [Module K V]

theorem quotDualCoannihilatorToDual_bijective (W : Subspace K (Dual K V)) [FiniteDimensional K W] :
    Function.Bijective W.quotDualCoannihilatorToDual :=
  ‚ü®W.quotDualCoannihilatorToDual_injective, letI : AddCommGroup W := inferInstance
    flip_injective_iff‚ÇÇ.mp W.flip_quotDualCoannihilatorToDual_injective‚ü©

theorem flip_quotDualCoannihilatorToDual_bijective (W : Subspace K (Dual K V))
    [FiniteDimensional K W] : Function.Bijective W.quotDualCoannihilatorToDual.flip :=
  letI : AddCommGroup W := inferInstance
  flip_bijective_iff‚ÇÇ.mpr W.quotDualCoannihilatorToDual_bijective

theorem dualCoannihilator_dualAnnihilator_eq {W : Subspace K (Dual K V)} [FiniteDimensional K W] :
    W.dualCoannihilator.dualAnnihilator = W :=
  let e := (LinearEquiv.ofBijective _ W.flip_quotDualCoannihilatorToDual_bijective).trans
    (Submodule.dualQuotEquivDualAnnihilator _)
  letI : AddCommGroup W := inferInstance
  haveI : FiniteDimensional K W.dualCoannihilator.dualAnnihilator := LinearEquiv.finiteDimensional e
  (eq_of_le_of_finrank_eq W.le_dualCoannihilator_dualAnnihilator e.finrank_eq).symm

theorem finiteDimensional_quot_dualCoannihilator_iff {W : Submodule K (Dual K V)} :
    FiniteDimensional K (V ‚ß∏ W.dualCoannihilator) ‚Üî FiniteDimensional K W :=
  ‚ü®fun _ ‚Ü¶ FiniteDimensional.of_injective _ W.flip_quotDualCoannihilatorToDual_injective,
    fun _ ‚Ü¶ FiniteDimensional.of_injective _ W.quotDualCoannihilatorToDual_injective‚ü©

open OrderDual in
/-- For any vector space, `dualAnnihilator` and `dualCoannihilator` gives an antitone order
  isomorphism between the finite-codimensional subspaces in the vector space and the
  finite-dimensional subspaces in its dual. -/
def orderIsoFiniteCodimDim :
    {W : Subspace K V // FiniteDimensional K (V ‚ß∏ W)} ‚âÉo
    {W : Subspace K (Dual K V) // FiniteDimensional K W}·µí·µà where
  toFun W := toDual ‚ü®W.1.dualAnnihilator, Submodule.finite_dualAnnihilator_iff.mpr W.2‚ü©
  invFun W := ‚ü®(ofDual W).1.dualCoannihilator,
    finiteDimensional_quot_dualCoannihilator_iff.mpr (ofDual W).2‚ü©
  left_inv _ := Subtype.ext dualAnnihilator_dualCoannihilator_eq
  right_inv W := have := (ofDual W).2; Subtype.ext dualCoannihilator_dualAnnihilator_eq
  map_rel_iff' := dualAnnihilator_le_dualAnnihilator_iff

open OrderDual in
/-- For any finite-dimensional vector space, `dualAnnihilator` and `dualCoannihilator` give
  an antitone order isomorphism between the subspaces in the vector space and the subspaces
  in its dual. -/
def orderIsoFiniteDimensional [FiniteDimensional K V] :
    Subspace K V ‚âÉo (Subspace K (Dual K V))·µí·µà where
  toFun W := toDual W.dualAnnihilator
  invFun W := (ofDual W).dualCoannihilator
  left_inv _ := dualAnnihilator_dualCoannihilator_eq
  right_inv _ := dualCoannihilator_dualAnnihilator_eq
  map_rel_iff' := dualAnnihilator_le_dualAnnihilator_iff

open Submodule in
theorem dualAnnihilator_dualAnnihilator_eq_map (W : Subspace K V) [FiniteDimensional K W] :
    W.dualAnnihilator.dualAnnihilator = W.map (Dual.eval K V) := by
  let e1 := (Free.chooseBasis K W).toDualEquiv ‚â™‚â´‚Çó W.quotAnnihilatorEquiv.symm
  haveI := e1.finiteDimensional
  let e2 := (Free.chooseBasis K _).toDualEquiv ‚â™‚â´‚Çó W.dualAnnihilator.dualQuotEquivDualAnnihilator
  haveI := LinearEquiv.finiteDimensional (V‚ÇÇ := W.dualAnnihilator.dualAnnihilator) e2
  rw [eq_of_le_of_finrank_eq (map_le_dualAnnihilator_dualAnnihilator W)]
  rw [‚Üê (equivMapOfInjective _ (eval_apply_injective K (V := V)) W).finrank_eq, e1.finrank_eq]
  exact e2.finrank_eq

theorem map_dualCoannihilator (W : Subspace K (Dual K V)) [FiniteDimensional K V] :
    W.dualCoannihilator.map (Dual.eval K V) = W.dualAnnihilator := by
  rw [‚Üê dualAnnihilator_dualAnnihilator_eq_map, dualCoannihilator_dualAnnihilator_eq]

end Subspace

end FiniteDimensional

end VectorSpace

theorem span_flip_eq_top_iff_linearIndependent {Œπ Œ± F} [Finite Œπ] [Field F] {f : Œπ ‚Üí Œ± ‚Üí F} :
    span F (Set.range <| flip f) = ‚ä§ ‚Üî LinearIndependent F f := by
  rw [linearIndependent_iff_ker, ‚Üê Submodule.map_eq_top_iff (e := Finsupp.llift F F F Œπ),
    ‚Üê Subspace.dualCoannihilator_dualAnnihilator_eq (W := map ..), dualAnnihilator_eq_top_iff]
  congr!
  rw [SetLike.ext'_iff, map_span, Submodule.coe_dualCoannihilator_span, ‚Üê Set.range_comp]
  ext
  simp [funext_iff, Finsupp.linearCombination, Finsupp.sum, Finset.sum_apply, flip]

namespace TensorProduct

variable (R A : Type*) (M : Type*) (N : Type*)
variable {Œπ Œ∫ : Type*}
variable [DecidableEq Œπ] [DecidableEq Œ∫]
variable [Fintype Œπ] [Fintype Œ∫]

open TensorProduct

attribute [local ext] TensorProduct.ext

open TensorProduct

open LinearMap

section

variable [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N]
variable [Module R M] [Module R N]

/-- The canonical linear map from `Dual M ‚äó Dual N` to `Dual (M ‚äó N)`,
sending `f ‚äó g` to the composition of `TensorProduct.map f g` with
the natural isomorphism `R ‚äó R ‚âÉ R`.
-/
def dualDistrib : Dual R M ‚äó[R] Dual R N ‚Üí‚Çó[R] Dual R (M ‚äó[R] N) :=
  compRight _ ‚Üë(TensorProduct.lid R R) ‚àò‚Çó homTensorHomMap (.id R) M N R R

variable {R M N}

@[simp]
theorem dualDistrib_apply (f : Dual R M) (g : Dual R N) (m : M) (n : N) :
    dualDistrib R M N (f ‚äó‚Çú g) (m ‚äó‚Çú n) = f m * g n :=
  rfl

end

namespace AlgebraTensorModule
variable [CommSemiring R] [CommSemiring A] [Algebra R A] [AddCommMonoid M] [AddCommMonoid N]
variable [Module R M] [Module A M] [Module R N] [IsScalarTower R A M]

/-- Heterobasic version of `TensorProduct.dualDistrib` -/
def dualDistrib : Dual A M ‚äó[R] Dual R N ‚Üí‚Çó[A] Dual A (M ‚äó[R] N) :=
  compRight _ (Algebra.TensorProduct.rid R A A).toLinearMap ‚àò‚Çó homTensorHomMap R A A M N A R

variable {R M N}

@[simp]
theorem dualDistrib_apply (f : Dual A M) (g : Dual R N) (m : M) (n : N) :
    dualDistrib R A M N (f ‚äó‚Çú g) (m ‚äó‚Çú n) = g n ‚Ä¢ f m :=
  rfl

end AlgebraTensorModule

variable {R M N}
variable [CommSemiring R] [AddCommMonoid M] [AddCommMonoid N]
variable [Module R M] [Module R N]

/-- An inverse to `TensorProduct.dualDistrib` given bases.
-/
noncomputable def dualDistribInvOfBasis (b : Basis Œπ R M) (c : Basis Œ∫ R N) :
    Dual R (M ‚äó[R] N) ‚Üí‚Çó[R] Dual R M ‚äó[R] Dual R N :=
  ‚àë i, ‚àë j,
    (ringLmapEquivSelf R ‚Ñï _).symm (b.dualBasis i ‚äó‚Çú c.dualBasis j) ‚àò‚Çó
      apply‚Çó (c j) ‚àò‚Çó apply‚Çó (b i) ‚àò‚Çó lcurry (.id R) M N R

@[simp]
theorem dualDistribInvOfBasis_apply (b : Basis Œπ R M) (c : Basis Œ∫ R N) (f : Dual R (M ‚äó[R] N)) :
    dualDistribInvOfBasis b c f = ‚àë i, ‚àë j, f (b i ‚äó‚Çú c j) ‚Ä¢ b.dualBasis i ‚äó‚Çú c.dualBasis j := by
  simp [dualDistribInvOfBasis]

theorem dualDistrib_dualDistribInvOfBasis_left_inverse (b : Basis Œπ R M) (c : Basis Œ∫ R N) :
    comp (dualDistrib R M N) (dualDistribInvOfBasis b c) = LinearMap.id := by
  apply (b.tensorProduct c).dualBasis.ext
  rintro ‚ü®i, j‚ü©
  apply (b.tensorProduct c).ext
  rintro ‚ü®i', j'‚ü©
  simp only [dualDistrib, Basis.coe_dualBasis, coe_comp, Function.comp_apply,
    dualDistribInvOfBasis_apply, Basis.coord_apply, Basis.tensorProduct_repr_tmul_apply,
    Basis.repr_self, _root_.map_sum, map_smul, homTensorHomMap_apply, compRight_apply,
    Basis.tensorProduct_apply, coeFn_sum, Finset.sum_apply, smul_apply, LinearEquiv.coe_coe,
    map_tmul, lid_tmul, smul_eq_mul, id_coe, id_eq]
  rw [Finset.sum_eq_single i, Finset.sum_eq_single j]
  ¬∑ simpa using mul_comm _ _
  all_goals { intros; simp [*] at * }

theorem dualDistrib_dualDistribInvOfBasis_right_inverse (b : Basis Œπ R M) (c : Basis Œ∫ R N) :
    comp (dualDistribInvOfBasis b c) (dualDistrib R M N) = LinearMap.id := by
  apply (b.dualBasis.tensorProduct c.dualBasis).ext
  rintro ‚ü®i, j‚ü©
  simp only [Basis.tensorProduct_apply, Basis.coe_dualBasis, coe_comp, Function.comp_apply,
    dualDistribInvOfBasis_apply, dualDistrib_apply, Basis.coord_apply, Basis.repr_self,
    id_coe, id_eq]
  rw [Finset.sum_eq_single i, Finset.sum_eq_single j]
  ¬∑ simp
  all_goals { intros; simp [*] at * }

/-- A linear equivalence between `Dual M ‚äó Dual N` and `Dual (M ‚äó N)` given bases for `M` and `N`.
It sends `f ‚äó g` to the composition of `TensorProduct.map f g` with the natural
isomorphism `R ‚äó R ‚âÉ R`.
-/
@[simps!]
noncomputable def dualDistribEquivOfBasis (b : Basis Œπ R M) (c : Basis Œ∫ R N) :
    Dual R M ‚äó[R] Dual R N ‚âÉ‚Çó[R] Dual R (M ‚äó[R] N) := by
  refine LinearEquiv.ofLinear (dualDistrib R M N) (dualDistribInvOfBasis b c) ?_ ?_
  ¬∑ exact dualDistrib_dualDistribInvOfBasis_left_inverse _ _
  ¬∑ exact dualDistrib_dualDistribInvOfBasis_right_inverse _ _

variable (R M N)
variable [Module.Finite R M] [Module.Finite R N] [Module.Free R M] [Module.Free R N]

/--
A linear equivalence between `Dual M ‚äó Dual N` and `Dual (M ‚äó N)` when `M` and `N` are finite free
modules. It sends `f ‚äó g` to the composition of `TensorProduct.map f g` with the natural
isomorphism `R ‚äó R ‚âÉ R`.
-/
@[simp]
noncomputable def dualDistribEquiv : Dual R M ‚äó[R] Dual R N ‚âÉ‚Çó[R] Dual R (M ‚äó[R] N) :=
  dualDistribEquivOfBasis (Module.Free.chooseBasis R M) (Module.Free.chooseBasis R N)

end TensorProduct
