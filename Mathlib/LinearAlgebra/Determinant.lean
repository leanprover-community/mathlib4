/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, Casper Putz, Anne Baanen
-/
import Mathlib.LinearAlgebra.FreeModule.StrongRankCondition
import Mathlib.LinearAlgebra.GeneralLinearGroup
import Mathlib.LinearAlgebra.Matrix.Reindex
import Mathlib.Tactic.FieldSimp
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Mathlib.LinearAlgebra.Matrix.Basis

/-!
# Determinant of families of vectors

This file defines the determinant of an endomorphism, and of a family of vectors
with respect to some basis. For the determinant of a matrix, see the file
`LinearAlgebra.Matrix.Determinant`.

## Main definitions

In the list below, and in all this file, `R` is a commutative ring (semiring
is sometimes enough), `M` and its variations are `R`-modules, `Î¹`, `Îº`, `n` and `m` are finite
types used for indexing.

* `Basis.det`: the determinant of a family of vectors with respect to a basis,
  as a multilinear map
* `LinearMap.det`: the determinant of an endomorphism `f : End R M` as a
  multiplicative homomorphism (if `M` does not have a finite `R`-basis, the
  result is `1` instead)
* `LinearEquiv.det`: the determinant of an isomorphism `f : M â‰ƒâ‚—[R] M` as a
  multiplicative homomorphism (if `M` does not have a finite `R`-basis, the
  result is `1` instead)

## Tags

basis, det, determinant
-/


noncomputable section

open Matrix Module LinearMap Submodule Set Function

universe u v w

variable {R : Type*} [CommRing R]
variable {M : Type*} [AddCommGroup M] [Module R M]
variable {M' : Type*} [AddCommGroup M'] [Module R M']
variable {Î¹ : Type*} [DecidableEq Î¹] [Fintype Î¹]
variable (e : Basis Î¹ R M)

section Conjugate

variable {A : Type*} [CommRing A]
variable {m n : Type*}

/-- If `R^m` and `R^n` are linearly equivalent, then `m` and `n` are also equivalent. -/
def equivOfPiLEquivPi {R : Type*} [Finite m] [Finite n] [CommRing R] [Nontrivial R]
    (e : (m â†’ R) â‰ƒâ‚—[R] n â†’ R) : m â‰ƒ n :=
  Basis.indexEquiv (Basis.ofEquivFun e.symm) (Pi.basisFun _ _)

namespace Matrix

variable [Fintype m] [Fintype n]

/-- If `M` and `M'` are each other's inverse matrices, they are square matrices up to
equivalence of types. -/
def indexEquivOfInv [Nontrivial A] [DecidableEq m] [DecidableEq n] {M : Matrix m n A}
    {M' : Matrix n m A} (hMM' : M * M' = 1) (hM'M : M' * M = 1) : m â‰ƒ n :=
  equivOfPiLEquivPi (toLin'OfInv hMM' hM'M)

theorem det_comm [DecidableEq n] (M N : Matrix n n A) : det (M * N) = det (N * M) := by
  rw [det_mul, det_mul, mul_comm]

/-- If there exists a two-sided inverse `M'` for `M` (indexed differently),
then `det (N * M) = det (M * N)`. -/
theorem det_comm' [DecidableEq m] [DecidableEq n] {M : Matrix n m A} {N : Matrix m n A}
    {M' : Matrix m n A} (hMM' : M * M' = 1) (hM'M : M' * M = 1) : det (M * N) = det (N * M) := by
  nontriviality A
  -- Although `m` and `n` are different a priori, we will show they have the same cardinality.
  -- This turns the problem into one for square matrices, which is easy.
  let e := indexEquivOfInv hMM' hM'M
  rw [â† det_submatrix_equiv_self e, â† submatrix_mul_equiv _ _ _ (Equiv.refl n) _, det_comm,
    submatrix_mul_equiv, Equiv.coe_refl, submatrix_id_id]

/-- If `M'` is a two-sided inverse for `M` (indexed differently), `det (M * N * M') = det N`.

See `Matrix.det_conj` and `Matrix.det_conj'` for the case when `M' = Mâ»Â¹` or vice versa. -/
theorem det_conj_of_mul_eq_one [DecidableEq m] [DecidableEq n] {M : Matrix m n A}
    {M' : Matrix n m A} {N : Matrix n n A} (hMM' : M * M' = 1) (hM'M : M' * M = 1) :
    det (M * N * M') = det N := by
  rw [â† det_comm' hM'M hMM', â† Matrix.mul_assoc, hM'M, Matrix.one_mul]

end Matrix

end Conjugate

namespace LinearMap

/-! ### Determinant of a linear map -/


variable {A : Type*} [CommRing A] [Module A M]
variable {Îº : Type*} [Fintype Îº]

/-- The determinant of `LinearMap.toMatrix` does not depend on the choice of basis. -/
theorem det_toMatrix_eq_det_toMatrix [DecidableEq Îº] (b : Basis Î¹ A M) (c : Basis Îº A M)
    (f : M â†’â‚—[A] M) : det (LinearMap.toMatrix b b f) = det (LinearMap.toMatrix c c f) := by
  rw [â† linearMap_toMatrix_mul_basis_toMatrix c b c, â† basis_toMatrix_mul_linearMap_toMatrix b c b,
      Matrix.det_conj_of_mul_eq_one] <;>
    rw [Basis.toMatrix_mul_toMatrix, Basis.toMatrix_self]


/-- The determinant of an endomorphism given a basis.

See `LinearMap.det` for a version that populates the basis non-computably.

Although the `Trunc (Basis Î¹ A M)` parameter makes it slightly more convenient to switch bases,
there is no good way to generalize over universe parameters, so we can't fully state in `detAux`'s
type that it does not depend on the choice of basis. Instead you can use the `detAux_def''` lemma,
or avoid mentioning a basis at all using `LinearMap.det`.
-/
irreducible_def detAux : Trunc (Basis Î¹ A M) â†’ (M â†’â‚—[A] M) â†’* A :=
  Trunc.lift
    (fun b : Basis Î¹ A M => detMonoidHom.comp (toMatrixAlgEquiv b : (M â†’â‚—[A] M) â†’* Matrix Î¹ Î¹ A))
    fun b c => MonoidHom.ext <| det_toMatrix_eq_det_toMatrix b c

/-- Unfold lemma for `detAux`.

See also `detAux_def''` which allows you to vary the basis.
-/
theorem detAux_def' (b : Basis Î¹ A M) (f : M â†’â‚—[A] M) :
    LinearMap.detAux (Trunc.mk b) f = Matrix.det (LinearMap.toMatrix b b f) := by
  rw [detAux]
  rfl

theorem detAux_def'' {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (tb : Trunc <| Basis Î¹ A M)
    (b' : Basis Î¹' A M) (f : M â†’â‚—[A] M) :
    LinearMap.detAux tb f = Matrix.det (LinearMap.toMatrix b' b' f) := by
  induction tb using Trunc.induction_on with
  | h b => rw [detAux_def', det_toMatrix_eq_det_toMatrix b b']

@[simp]
theorem detAux_id (b : Trunc <| Basis Î¹ A M) : LinearMap.detAux b LinearMap.id = 1 :=
  (LinearMap.detAux b).map_one

@[simp]
theorem detAux_comp (b : Trunc <| Basis Î¹ A M) (f g : M â†’â‚—[A] M) :
    LinearMap.detAux b (f.comp g) = LinearMap.detAux b f * LinearMap.detAux b g :=
  (LinearMap.detAux b).map_mul f g

section

open scoped Classical in
-- Discourage the elaborator from unfolding `det` and producing a huge term by marking it
-- as irreducible.
/-- The determinant of an endomorphism independent of basis.

If there is no finite basis on `M`, the result is `1` instead.
-/
protected irreducible_def det : (M â†’â‚—[A] M) â†’* A :=
  if H : âˆƒ s : Finset M, Nonempty (Basis s A M) then LinearMap.detAux (Trunc.mk H.choose_spec.some)
  else 1

open scoped Classical in
theorem coe_det [DecidableEq M] :
    â‡‘(LinearMap.det : (M â†’â‚—[A] M) â†’* A) =
      if H : âˆƒ s : Finset M, Nonempty (Basis s A M) then
        LinearMap.detAux (Trunc.mk H.choose_spec.some)
      else 1 := by
  ext
  rw [LinearMap.det_def]
  split_ifs
  Â· congr -- use the correct `DecidableEq` instance
  rfl

end

-- Auxiliary lemma, the `simp` normal form goes in the other direction
-- (using `LinearMap.det_toMatrix`)
theorem det_eq_det_toMatrix_of_finset [DecidableEq M] {s : Finset M} (b : Basis s A M)
    (f : M â†’â‚—[A] M) : LinearMap.det f = Matrix.det (LinearMap.toMatrix b b f) := by
  have : âˆƒ s : Finset M, Nonempty (Basis s A M) := âŸ¨s, âŸ¨bâŸ©âŸ©
  rw [LinearMap.coe_det, dif_pos this, detAux_def'' _ b]

@[simp]
theorem det_toMatrix (b : Basis Î¹ A M) (f : M â†’â‚—[A] M) :
    Matrix.det (toMatrix b b f) = LinearMap.det f := by
  haveI := Classical.decEq M
  rw [det_eq_det_toMatrix_of_finset b.reindexFinsetRange,
    det_toMatrix_eq_det_toMatrix b b.reindexFinsetRange]

@[simp]
theorem det_toMatrix' {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹] (f : (Î¹ â†’ A) â†’â‚—[A] Î¹ â†’ A) :
    Matrix.det (LinearMap.toMatrix' f) = LinearMap.det f := by simp [â† toMatrix_eq_toMatrix']

@[simp]
theorem det_toLin (b : Basis Î¹ R M) (f : Matrix Î¹ Î¹ R) :
    LinearMap.det (Matrix.toLin b b f) = f.det := by
  rw [â† LinearMap.det_toMatrix b, LinearMap.toMatrix_toLin]

@[simp]
theorem det_toLin' (f : Matrix Î¹ Î¹ R) : LinearMap.det (Matrix.toLin' f) = Matrix.det f := by
  simp only [â† toLin_eq_toLin', det_toLin]

/-- To show `P (LinearMap.det f)` it suffices to consider `P (Matrix.det (toMatrix _ _ f))` and
`P 1`. -/
@[elab_as_elim]
theorem det_cases [DecidableEq M] {P : A â†’ Prop} (f : M â†’â‚—[A] M)
    (hb : âˆ€ (s : Finset M) (b : Basis s A M), P (Matrix.det (toMatrix b b f))) (h1 : P 1) :
    P (LinearMap.det f) := by
  classical
  if H : âˆƒ s : Finset M, Nonempty (Basis s A M) then
    obtain âŸ¨s, âŸ¨bâŸ©âŸ© := H
    rw [â† det_toMatrix b]
    exact hb s b
  else
    rwa [LinearMap.det_def, dif_neg H]

@[simp]
theorem det_comp (f g : M â†’â‚—[A] M) :
    LinearMap.det (f.comp g) = LinearMap.det f * LinearMap.det g :=
  LinearMap.det.map_mul f g

@[simp]
theorem det_id : LinearMap.det (LinearMap.id : M â†’â‚—[A] M) = 1 :=
  LinearMap.det.map_one

/-- Multiplying a map by a scalar `c` multiplies its determinant by `c ^ dim M`. -/
@[simp]
theorem det_smul [Module.Free A M] (c : A) (f : M â†’â‚—[A] M) :
    LinearMap.det (c â€¢ f) = c ^ Module.finrank A M * LinearMap.det f := by
  nontriviality A
  by_cases H : âˆƒ s : Finset M, Nonempty (Basis s A M)
  Â· have : Module.Finite A M := by
      rcases H with âŸ¨s, âŸ¨hsâŸ©âŸ©
      exact Module.Finite.of_basis hs
    simp only [â† det_toMatrix (Module.finBasis A M), LinearEquiv.map_smul,
      Fintype.card_fin, Matrix.det_smul]
  Â· classical
      have : Module.finrank A M = 0 := finrank_eq_zero_of_not_exists_basis H
      simp [coe_det, H, this]

theorem det_zero' {Î¹ : Type*} [Finite Î¹] [Nonempty Î¹] (b : Basis Î¹ A M) :
    LinearMap.det (0 : M â†’â‚—[A] M) = 0 := by
  haveI := Classical.decEq Î¹
  cases nonempty_fintype Î¹
  rwa [â† det_toMatrix b, LinearEquiv.map_zero, det_zero]

/-- In a finite-dimensional vector space, the zero map has determinant `1` in dimension `0`,
and `0` otherwise. We give a formula that also works in infinite dimension, where we define
the determinant to be `1`. -/
@[simp]
theorem det_zero [Module.Free A M] :
    LinearMap.det (0 : M â†’â‚—[A] M) = (0 : A) ^ Module.finrank A M := by
  simp only [â† zero_smul A (1 : M â†’â‚—[A] M), det_smul, mul_one, MonoidHom.map_one]

theorem det_eq_one_of_not_module_finite (h : Â¬Module.Finite R M) (f : M â†’â‚—[R] M) : f.det = 1 := by
  rw [LinearMap.det, dif_neg, MonoidHom.one_apply]
  exact fun âŸ¨_, âŸ¨bâŸ©âŸ© â†¦ h (Module.Finite.of_basis b)

@[nontriviality]
theorem det_eq_one_of_subsingleton [Subsingleton M] (f : M â†’â‚—[R] M) :
    LinearMap.det (f : M â†’â‚—[R] M) = 1 := by
  have b : Basis (Fin 0) R M := Basis.empty M
  rw [â† f.det_toMatrix b]
  exact Matrix.det_isEmpty

theorem det_eq_one_of_finrank_eq_zero {ğ•œ : Type*} [Field ğ•œ] {M : Type*} [AddCommGroup M]
    [Module ğ•œ M] (h : Module.finrank ğ•œ M = 0) (f : M â†’â‚—[ğ•œ] M) :
    LinearMap.det (f : M â†’â‚—[ğ•œ] M) = 1 := by
  classical
    refine @LinearMap.det_cases M _ ğ•œ _ _ _ (fun t => t = 1) f ?_ rfl
    intro s b
    have : IsEmpty s := by
      rw [â† Fintype.card_eq_zero_iff]
      exact (Module.finrank_eq_card_basis b).symm.trans h
    exact Matrix.det_isEmpty

/-- Conjugating a linear map by a linear equiv does not change its determinant. -/
@[simp]
theorem det_conj {N : Type*} [AddCommGroup N] [Module A N] (f : M â†’â‚—[A] M) (e : M â‰ƒâ‚—[A] N) :
    LinearMap.det ((e : M â†’â‚—[A] N) âˆ˜â‚— f âˆ˜â‚— (e.symm : N â†’â‚—[A] M)) = LinearMap.det f := by
  classical
    by_cases H : âˆƒ s : Finset M, Nonempty (Basis s A M)
    Â· rcases H with âŸ¨s, âŸ¨bâŸ©âŸ©
      rw [â† det_toMatrix b f, â† det_toMatrix (b.map e), toMatrix_comp (b.map e) b (b.map e),
        toMatrix_comp (b.map e) b b, â† Matrix.mul_assoc, Matrix.det_conj_of_mul_eq_one]
      Â· rw [â† toMatrix_comp, LinearEquiv.comp_coe, e.symm_trans_self, LinearEquiv.refl_toLinearMap,
          toMatrix_id]
      Â· rw [â† toMatrix_comp, LinearEquiv.comp_coe, e.self_trans_symm, LinearEquiv.refl_toLinearMap,
          toMatrix_id]
    Â· have H' : Â¬âˆƒ t : Finset N, Nonempty (Basis t A N) := by
        contrapose! H
        rcases H with âŸ¨s, âŸ¨bâŸ©âŸ©
        exact âŸ¨_, âŸ¨(b.map e.symm).reindexFinsetRangeâŸ©âŸ©
      simp only [coe_det, H, H', MonoidHom.one_apply, dif_neg, not_false_eq_true]

/-- If a linear map is invertible, so is its determinant. -/
theorem isUnit_det {A : Type*} [CommRing A] [Module A M] (f : M â†’â‚—[A] M) (hf : IsUnit f) :
    IsUnit (LinearMap.det f) := IsUnit.map LinearMap.det hf

/-- If a linear map has determinant different from `1`, then the space is finite-dimensional. -/
theorem finiteDimensional_of_det_ne_one {ğ•œ : Type*} [Field ğ•œ] [Module ğ•œ M] (f : M â†’â‚—[ğ•œ] M)
    (hf : LinearMap.det f â‰  1) : FiniteDimensional ğ•œ M := by
  by_cases H : âˆƒ s : Finset M, Nonempty (Basis s ğ•œ M)
  Â· rcases H with âŸ¨s, âŸ¨hsâŸ©âŸ©
    exact FiniteDimensional.of_fintype_basis hs
  Â· classical simp [LinearMap.coe_det, H] at hf

/-- If the determinant of a map vanishes, then the map is not onto. -/
theorem range_lt_top_of_det_eq_zero {ğ•œ : Type*} [Field ğ•œ] [Module ğ•œ M] {f : M â†’â‚—[ğ•œ] M}
    (hf : LinearMap.det f = 0) : LinearMap.range f < âŠ¤ := by
  have : FiniteDimensional ğ•œ M := by simp [f.finiteDimensional_of_det_ne_one, hf]
  contrapose hf
  simp only [lt_top_iff_ne_top, Classical.not_not, â† isUnit_iff_range_eq_top] at hf
  exact isUnit_iff_ne_zero.1 (f.isUnit_det hf)

/-- If the determinant of a map vanishes, then the map is not injective. -/
theorem bot_lt_ker_of_det_eq_zero {ğ•œ : Type*} [Field ğ•œ] [Module ğ•œ M] {f : M â†’â‚—[ğ•œ] M}
    (hf : LinearMap.det f = 0) : âŠ¥ < LinearMap.ker f := by
  have : FiniteDimensional ğ•œ M := by simp [f.finiteDimensional_of_det_ne_one, hf]
  contrapose hf
  simp only [bot_lt_iff_ne_bot, Classical.not_not, â† isUnit_iff_ker_eq_bot] at hf
  exact isUnit_iff_ne_zero.1 (f.isUnit_det hf)

/-- When the function is over the base ring, the determinant is the evaluation at `1`. -/
@[simp] lemma det_ring (f : R â†’â‚—[R] R) : f.det = f 1 := by
  simp [â† det_toMatrix (Basis.singleton Unit R)]

lemma det_mulLeft (a : R) : (mulLeft R a).det = a := by simp
lemma det_mulRight (a : R) : (mulRight R a).det = a := by simp

theorem det_prodMap [Module.Free R M] [Module.Free R M'] [Module.Finite R M] [Module.Finite R M']
    (f : Module.End R M) (f' : Module.End R M') :
    (prodMap f f').det = f.det * f'.det := by
  let b := Module.Free.chooseBasis R M
  let b' := Module.Free.chooseBasis R M'
  rw [â† det_toMatrix (b.prod b'), â† det_toMatrix b, â† det_toMatrix b', toMatrix_prodMap,
    det_fromBlocks_zeroâ‚‚â‚, det_toMatrix]

omit [DecidableEq Î¹] in
theorem det_pi [Module.Free R M] [Module.Finite R M] (f : Î¹ â†’ M â†’â‚—[R] M) :
    (LinearMap.pi (fun i â†¦ (f i).comp (LinearMap.proj i))).det = âˆ i, (f i).det := by
  classical
  let b := Module.Free.chooseBasis R M
  let B := (Pi.basis (fun _ : Î¹ â†¦ b)).reindex <|
    (Equiv.sigmaEquivProd _ _).trans (Equiv.prodComm _ _)
  simp_rw [â† LinearMap.det_toMatrix B, â† LinearMap.det_toMatrix b]
  have : ((LinearMap.toMatrix B B) (LinearMap.pi fun i â†¦ f i âˆ˜â‚— LinearMap.proj i)) =
      Matrix.blockDiagonal (fun i â†¦ LinearMap.toMatrix b b (f i)) := by
    ext âŸ¨iâ‚, iâ‚‚âŸ© âŸ¨jâ‚, jâ‚‚âŸ©
    unfold B
    simp_rw [LinearMap.toMatrix_apply', Matrix.blockDiagonal_apply, Basis.coe_reindex,
      Function.comp_apply, Basis.repr_reindex_apply, Equiv.symm_trans_apply, Equiv.prodComm_symm,
      Equiv.prodComm_apply, Equiv.sigmaEquivProd_symm_apply, Prod.swap_prod_mk, Pi.basis_apply,
      Pi.basis_repr, LinearMap.pi_apply, LinearMap.coe_comp, Function.comp_apply,
      LinearMap.toMatrix_apply', LinearMap.coe_proj, Function.eval, Pi.single_apply]
    split_ifs with h
    Â· rw [h]
    Â· simp only [map_zero, Finsupp.coe_zero, Pi.zero_apply]
  rw [this, Matrix.det_blockDiagonal]

end LinearMap


namespace LinearEquiv

/-- On a `LinearEquiv`, the domain of `LinearMap.det` can be promoted to `RË£`. -/
protected def det : (M â‰ƒâ‚—[R] M) â†’* RË£ :=
  (Units.map (LinearMap.det : (M â†’â‚—[R] M) â†’* R)).comp
    (LinearMap.GeneralLinearGroup.generalLinearEquiv R M).symm.toMonoidHom

@[simp]
theorem coe_det (f : M â‰ƒâ‚—[R] M) : â†‘(LinearEquiv.det f) = LinearMap.det (f : M â†’â‚—[R] M) :=
  rfl

@[simp]
theorem coe_inv_det (f : M â‰ƒâ‚—[R] M) : â†‘(LinearEquiv.det f)â»Â¹ = LinearMap.det (f.symm : M â†’â‚—[R] M) :=
  rfl

@[simp]
theorem det_refl : LinearEquiv.det (LinearEquiv.refl R M) = 1 :=
  Units.ext <| LinearMap.det_id

@[simp]
theorem det_trans (f g : M â‰ƒâ‚—[R] M) :
    LinearEquiv.det (f.trans g) = LinearEquiv.det g * LinearEquiv.det f :=
  map_mul _ g f

@[simp]
theorem det_symm (f : M â‰ƒâ‚—[R] M) : LinearEquiv.det f.symm = LinearEquiv.det fâ»Â¹ :=
  map_inv _ f

/-- Conjugating a linear equiv by a linear equiv does not change its determinant. -/
@[simp]
theorem det_conj (f : M â‰ƒâ‚—[R] M) (e : M â‰ƒâ‚—[R] M') :
    LinearEquiv.det ((e.symm.trans f).trans e) = LinearEquiv.det f := by
  rw [â† Units.val_inj, coe_det, coe_det, â† comp_coe, â† comp_coe, LinearMap.det_conj]

attribute [irreducible] LinearEquiv.det

end LinearEquiv

/-- The determinants of a `LinearEquiv` and its inverse multiply to 1. -/
@[simp]
theorem LinearEquiv.det_mul_det_symm {A : Type*} [CommRing A] [Module A M] (f : M â‰ƒâ‚—[A] M) :
    LinearMap.det (f : M â†’â‚—[A] M) * LinearMap.det (f.symm : M â†’â‚—[A] M) = 1 := by
  simp [â† LinearMap.det_comp]

/-- The determinants of a `LinearEquiv` and its inverse multiply to 1. -/
@[simp]
theorem LinearEquiv.det_symm_mul_det {A : Type*} [CommRing A] [Module A M] (f : M â‰ƒâ‚—[A] M) :
    LinearMap.det (f.symm : M â†’â‚—[A] M) * LinearMap.det (f : M â†’â‚—[A] M) = 1 := by
  simp [â† LinearMap.det_comp]

-- Cannot be stated using `LinearMap.det` because `f` is not an endomorphism.
theorem LinearEquiv.isUnit_det (f : M â‰ƒâ‚—[R] M') (v : Basis Î¹ R M) (v' : Basis Î¹ R M') :
    IsUnit (LinearMap.toMatrix v v' f).det := by
  apply isUnit_det_of_left_inverse
  simpa using (LinearMap.toMatrix_comp v v' v f.symm f).symm

/-- Specialization of `LinearEquiv.isUnit_det` -/
theorem LinearEquiv.isUnit_det' {A : Type*} [CommRing A] [Module A M] (f : M â‰ƒâ‚—[A] M) :
    IsUnit (LinearMap.det (f : M â†’â‚—[A] M)) :=
  isUnit_of_mul_eq_one _ _ f.det_mul_det_symm

-- see https://github.com/leanprover-community/mathlib4/issues/29041
set_option linter.unusedSimpArgs false in
/-- The determinant of `f.symm` is the inverse of that of `f` when `f` is a linear equiv. -/
theorem LinearEquiv.det_coe_symm {ğ•œ : Type*} [Field ğ•œ] [Module ğ•œ M] (f : M â‰ƒâ‚—[ğ•œ] M) :
    LinearMap.det (f.symm : M â†’â‚—[ğ•œ] M) = (LinearMap.det (f : M â†’â‚—[ğ•œ] M))â»Â¹ := by
  simp [field, IsUnit.ne_zero f.isUnit_det']

/-- Builds a linear equivalence from a linear map whose determinant in some bases is a unit. -/
@[simps]
def LinearEquiv.ofIsUnitDet {f : M â†’â‚—[R] M'} {v : Basis Î¹ R M} {v' : Basis Î¹ R M'}
    (h : IsUnit (LinearMap.toMatrix v v' f).det) : M â‰ƒâ‚—[R] M' where
  toFun := f
  map_add' := f.map_add
  map_smul' := f.map_smul
  invFun := toLin v' v (toMatrix v v' f)â»Â¹
  left_inv x :=
    calc toLin v' v (toMatrix v v' f)â»Â¹ (f x)
      _ = toLin v v ((toMatrix v v' f)â»Â¹ * toMatrix v v' f) x := by
        rw [toLin_mul v v' v, toLin_toMatrix, LinearMap.comp_apply]
      _ = x := by simp [h]
  right_inv x :=
    calc f (toLin v' v (toMatrix v v' f)â»Â¹ x)
      _ = toLin v' v' (toMatrix v v' f * (toMatrix v v' f)â»Â¹) x := by
        rw [toLin_mul v' v v', LinearMap.comp_apply, toLin_toMatrix v v']
      _ = x := by simp [h]

@[simp]
theorem LinearEquiv.coe_ofIsUnitDet {f : M â†’â‚—[R] M'} {v : Basis Î¹ R M} {v' : Basis Î¹ R M'}
    (h : IsUnit (LinearMap.toMatrix v v' f).det) :
    (LinearEquiv.ofIsUnitDet h : M â†’â‚—[R] M') = f := by
  ext x
  rfl

/-- Builds a linear equivalence from a linear map on a finite-dimensional vector space whose
determinant is nonzero. -/
abbrev LinearMap.equivOfDetNeZero {ğ•œ : Type*} [Field ğ•œ] {M : Type*} [AddCommGroup M] [Module ğ•œ M]
    [FiniteDimensional ğ•œ M] (f : M â†’â‚—[ğ•œ] M) (hf : LinearMap.det f â‰  0) : M â‰ƒâ‚—[ğ•œ] M :=
  have : IsUnit (LinearMap.toMatrix (Module.finBasis ğ•œ M)
      (Module.finBasis ğ•œ M) f).det := by
    rw [LinearMap.det_toMatrix]
    exact isUnit_iff_ne_zero.2 hf
  LinearEquiv.ofIsUnitDet this

theorem LinearMap.associated_det_of_eq_comp (e : M â‰ƒâ‚—[R] M) (f f' : M â†’â‚—[R] M)
    (h : âˆ€ x, f x = f' (e x)) : Associated (LinearMap.det f) (LinearMap.det f') := by
  suffices Associated (LinearMap.det (f' âˆ˜â‚— â†‘e)) (LinearMap.det f') by
    convert this using 2
    ext x
    exact h x
  rw [â† mul_one (LinearMap.det f'), LinearMap.det_comp]
  exact Associated.mul_left _ (associated_one_iff_isUnit.mpr e.isUnit_det')

theorem LinearMap.associated_det_comp_equiv {N : Type*} [AddCommGroup N] [Module R N]
    (f : N â†’â‚—[R] M) (e e' : M â‰ƒâ‚—[R] N) :
    Associated (LinearMap.det (f âˆ˜â‚— â†‘e)) (LinearMap.det (f âˆ˜â‚— â†‘e')) := by
  refine LinearMap.associated_det_of_eq_comp (e.trans e'.symm) _ _ ?_
  intro x
  simp only [LinearMap.comp_apply, LinearEquiv.coe_coe, LinearEquiv.trans_apply,
    LinearEquiv.apply_symm_apply]

namespace Module.Basis

/-- The determinant of a family of vectors with respect to some basis, as an alternating
multilinear map. -/
nonrec def det : M [â‹€^Î¹]â†’â‚—[R] R where
  toMultilinearMap :=
    MultilinearMap.mk' (fun v â†¦ det (e.toMatrix v))
      (fun v i x y â†¦ by
        simp only [e.toMatrix_update, map_add, Finsupp.coe_add, det_updateCol_add])
      (fun u i c x â†¦ by
        simp only [e.toMatrix_update, Algebra.id.smul_eq_mul, LinearEquiv.map_smul]
        apply det_updateCol_smul)
  map_eq_zero_of_eq' := by
    intro v i j h hij
    dsimp
    rw [â† Function.update_eq_self i v, h, â† det_transpose, e.toMatrix_update, â† updateRow_transpose,
      â† e.toMatrix_transpose_apply]
    apply det_zero_of_row_eq hij
    rw [updateRow_ne hij.symm, updateRow_self]

theorem det_apply (v : Î¹ â†’ M) : e.det v = Matrix.det (e.toMatrix v) :=
  rfl

theorem det_self : e.det e = 1 := by simp [e.det_apply]

@[simp]
theorem det_isEmpty [IsEmpty Î¹] : e.det = AlternatingMap.constOfIsEmpty R M Î¹ 1 := by
  ext v
  exact Matrix.det_isEmpty

/-- `Basis.det` is not the zero map. -/
theorem det_ne_zero [Nontrivial R] : e.det â‰  0 := fun h => by simpa [h] using e.det_self

theorem smul_det {G} [Group G] [DistribMulAction G M] [SMulCommClass G R M]
    (g : G) (v : Î¹ â†’ M) :
    (g â€¢ e).det v = e.det (gâ»Â¹ â€¢ v) := by
  simp_rw [det_apply, toMatrix_smul_left]

theorem is_basis_iff_det {v : Î¹ â†’ M} :
    LinearIndependent R v âˆ§ span R (Set.range v) = âŠ¤ â†” IsUnit (e.det v) := by
  constructor
  Â· rintro âŸ¨hli, hspanâŸ©
    set v' := Basis.mk hli hspan.ge
    rw [e.det_apply]
    convert LinearEquiv.isUnit_det (LinearEquiv.refl R M) v' e using 2
    ext i j
    simp [v']
  Â· intro h
    rw [Basis.det_apply, Basis.toMatrix_eq_toMatrix_constr] at h
    set v' := Basis.map e (LinearEquiv.ofIsUnitDet h) with v'_def
    have : â‡‘v' = v := by
      ext i
      rw [v'_def, Basis.map_apply, LinearEquiv.ofIsUnitDet_apply, e.constr_basis]
    rw [â† this]
    exact âŸ¨v'.linearIndependent, v'.span_eqâŸ©

theorem isUnit_det (e' : Basis Î¹ R M) : IsUnit (e.det e') :=
  (is_basis_iff_det e).mp âŸ¨e'.linearIndependent, e'.span_eqâŸ©

end Module.Basis

/-- Any alternating map to `R` where `Î¹` has the cardinality of a basis equals the determinant
map with respect to that basis, multiplied by the value of that alternating map on that basis. -/
theorem AlternatingMap.eq_smul_basis_det (f : M [â‹€^Î¹]â†’â‚—[R] R) : f = f e â€¢ e.det := by
  refine Basis.ext_alternating e fun i h => ?_
  let Ïƒ : Equiv.Perm Î¹ := Equiv.ofBijective i (Finite.injective_iff_bijective.1 h)
  change f (e âˆ˜ Ïƒ) = (f e â€¢ e.det) (e âˆ˜ Ïƒ)
  simp [AlternatingMap.map_perm, Basis.det_self]

@[simp]
theorem AlternatingMap.map_basis_eq_zero_iff {Î¹ : Type*} [Finite Î¹] (e : Basis Î¹ R M)
    (f : M [â‹€^Î¹]â†’â‚—[R] R) : f e = 0 â†” f = 0 :=
  âŸ¨fun h => by
    cases nonempty_fintype Î¹
    letI := Classical.decEq Î¹
    simpa [h] using f.eq_smul_basis_det e,
   fun h => h.symm â–¸ AlternatingMap.zero_apply _âŸ©

theorem AlternatingMap.map_basis_ne_zero_iff {Î¹ : Type*} [Finite Î¹] (e : Basis Î¹ R M)
    (f : M [â‹€^Î¹]â†’â‚—[R] R) : f e â‰  0 â†” f â‰  0 :=
  not_congr <| f.map_basis_eq_zero_iff e

variable {A : Type*} [CommRing A] [Module A M]

namespace Module.Basis

@[simp]
theorem det_comp (e : Basis Î¹ A M) (f : M â†’â‚—[A] M) (v : Î¹ â†’ M) :
    e.det (f âˆ˜ v) = (LinearMap.det f) * e.det v := by
  rw [det_apply, det_apply, â† f.det_toMatrix e, â† Matrix.det_mul,
    e.toMatrix_eq_toMatrix_constr (f âˆ˜ v), e.toMatrix_eq_toMatrix_constr v, â† toMatrix_comp,
    e.constr_comp]

@[simp]
theorem det_comp_basis [Module A M'] (b : Basis Î¹ A M) (b' : Basis Î¹ A M') (f : M â†’â‚—[A] M') :
    b'.det (f âˆ˜ b) = LinearMap.det (f âˆ˜â‚— (b'.equiv b (Equiv.refl Î¹) : M' â†’â‚—[A] M)) := by
  rw [det_apply, â† LinearMap.det_toMatrix b', LinearMap.toMatrix_comp _ b, Matrix.det_mul,
    LinearMap.toMatrix_basis_equiv, Matrix.det_one, mul_one]
  congr 1; ext i j
  rw [toMatrix_apply, LinearMap.toMatrix_apply, Function.comp_apply]

@[simp]
theorem det_basis (b : Basis Î¹ A M) (b' : Basis Î¹ A M) :
    LinearMap.det (b'.equiv b (Equiv.refl Î¹)).toLinearMap = b'.det b :=
  (b.det_comp_basis b' (LinearMap.id)).symm

theorem det_mul_det (b b' b'' : Basis Î¹ A M) :
    b.det b' * b'.det b'' = b.det b'' := by
  have : b'' = (b'.equiv b'' (Equiv.refl Î¹)).toLinearMap âˆ˜ b'  := by
    ext; simp
  conv_rhs =>
    rw [this, Basis.det_comp, det_basis, mul_comm]

theorem det_inv (b : Basis Î¹ A M) (b' : Basis Î¹ A M) :
    (b.isUnit_det b').unitâ»Â¹ = b'.det b := by
  rw [â† Units.mul_eq_one_iff_inv_eq, IsUnit.unit_spec, â† det_basis, â† det_basis]
  exact LinearEquiv.det_mul_det_symm _

theorem det_reindex {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (b : Basis Î¹ R M) (v : Î¹' â†’ M)
    (e : Î¹ â‰ƒ Î¹') : (b.reindex e).det v = b.det (v âˆ˜ e) := by
  rw [det_apply, toMatrix_reindex', det_reindexAlgEquiv, det_apply]

theorem det_reindex' {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (b : Basis Î¹ R M)
    (e : Î¹ â‰ƒ Î¹') : (b.reindex e).det = b.det.domDomCongr e :=
  AlternatingMap.ext fun _ => det_reindex _ _ _

theorem det_reindex_symm {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (b : Basis Î¹ R M)
    (v : Î¹ â†’ M) (e : Î¹' â‰ƒ Î¹) : (b.reindex e.symm).det (v âˆ˜ e) = b.det v := by
  rw [det_reindex, Function.comp_assoc, e.self_comp_symm, Function.comp_id]

@[simp]
theorem det_map (b : Basis Î¹ R M) (f : M â‰ƒâ‚—[R] M') (v : Î¹ â†’ M') :
    (b.map f).det v = b.det (f.symm âˆ˜ v) := by
  rw [det_apply, toMatrix_map, det_apply]

theorem det_map' (b : Basis Î¹ R M) (f : M â‰ƒâ‚—[R] M') :
    (b.map f).det = b.det.compLinearMap f.symm :=
  AlternatingMap.ext <| b.det_map f

end Module.Basis

@[simp]
theorem Pi.basisFun_det : (Pi.basisFun R Î¹).det = Matrix.detRowAlternating := by
  ext M
  rw [Basis.det_apply, Basis.coePiBasisFun.toMatrix_eq_transpose, det_transpose]

theorem Pi.basisFun_det_apply (v : Î¹ â†’ Î¹ â†’ R) :
    (Pi.basisFun R Î¹).det v = (Matrix.of v).det := by
  rw [Pi.basisFun_det]
  rfl

namespace Module.Basis

/-- If we fix a background basis `e`, then for any other basis `v`, we can characterise the
coordinates provided by `v` in terms of determinants relative to `e`. -/
theorem det_smul_mk_coord_eq_det_update {v : Î¹ â†’ M} (hli : LinearIndependent R v)
    (hsp : âŠ¤ â‰¤ span R (range v)) (i : Î¹) :
    e.det v â€¢ (Basis.mk hli hsp).coord i = e.det.toMultilinearMap.toLinearMap v i := by
  apply (Basis.mk hli hsp).ext
  intro k
  rcases eq_or_ne k i with (rfl | hik) <;>
    simp only [Algebra.id.smul_eq_mul, coe_mk, LinearMap.smul_apply,
      MultilinearMap.toLinearMap_apply]
  Â· rw [mk_coord_apply_eq, mul_one, update_eq_self]
    congr
  Â· rw [mk_coord_apply_ne hik, mul_zero, eq_comm]
    exact e.det.map_eq_zero_of_eq _ (by simp [hik]) hik

/-- If a basis is multiplied columnwise by scalars `w : Î¹ â†’ RË£`, then the determinant with respect
to this basis is multiplied by the product of the inverse of these scalars. -/
theorem det_unitsSMul (e : Basis Î¹ R M) (w : Î¹ â†’ RË£) :
    (e.unitsSMul w).det = (â†‘(âˆ i, w i)â»Â¹ : R) â€¢ e.det := by
  ext f
  change
    (Matrix.det fun i j => (e.unitsSMul w).repr (f j) i) =
      (â†‘(âˆ i, w i)â»Â¹ : R) â€¢ Matrix.det fun i j => e.repr (f j) i
  simp only [e.repr_unitsSMul]
  convert Matrix.det_mul_column (fun i => (â†‘(w i)â»Â¹ : R)) fun i j => e.repr (f j) i
  simp [â† Finset.prod_inv_distrib]

/-- The determinant of a basis constructed by `unitsSMul` is the product of the given units. -/
@[simp]
theorem det_unitsSMul_self (w : Î¹ â†’ RË£) : e.det (e.unitsSMul w) = âˆ i, (w i : R) := by
  simp [det_apply]

/-- The determinant of a basis constructed by `isUnitSMul` is the product of the given units. -/
@[simp]
theorem det_isUnitSMul {w : Î¹ â†’ R} (hw : âˆ€ i, IsUnit (w i)) :
    e.det (e.isUnitSMul hw) = âˆ i, w i :=
  e.det_unitsSMul_self _

end Module.Basis
