/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.LinearAlgebra.BilinearForm
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.LinearAlgebra.TensorProduct.Tower

#align_import linear_algebra.bilinear_form.tensor_product from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# The bilinear form on a tensor product

## Main definitions

* `BilinForm.tensorDistrib (B‚ÇÅ ‚äó‚Çú B‚ÇÇ)`: the bilinear form on `M‚ÇÅ ‚äó M‚ÇÇ` constructed by applying
  `B‚ÇÅ` on `M‚ÇÅ` and `B‚ÇÇ` on `M‚ÇÇ`.
* `BilinForm.tensorDistribEquiv`: `BilinForm.tensorDistrib` as an equivalence on finite free
  modules.

-/


universe u v w uŒπ uR uA uM‚ÇÅ uM‚ÇÇ

variable {Œπ : Type uŒπ} {R : Type uR} {A : Type uA} {M‚ÇÅ : Type uM‚ÇÅ} {M‚ÇÇ : Type uM‚ÇÇ}

open TensorProduct

namespace BilinForm

section CommSemiring
variable [CommSemiring R] [CommSemiring A]
variable [AddCommMonoid M‚ÇÅ] [AddCommMonoid M‚ÇÇ]
variable [Algebra R A] [Module R M‚ÇÅ] [Module A M‚ÇÅ]
variable [SMulCommClass R A M‚ÇÅ] [SMulCommClass A R M‚ÇÅ] [IsScalarTower R A M‚ÇÅ]
variable [Module R M‚ÇÇ]

variable (R A) in
/-- The tensor product of two bilinear forms injects into bilinear forms on tensor products.

Note this is heterobasic; the bilinear form on the left can take values in an (commutative) algebra
over the ring in which the right bilinear form is valued. -/
def tensorDistrib : BilinForm A M‚ÇÅ ‚äó[R] BilinForm R M‚ÇÇ ‚Üí‚Çó[A] BilinForm A (M‚ÇÅ ‚äó[R] M‚ÇÇ) :=
  ((TensorProduct.AlgebraTensorModule.tensorTensorTensorComm R A M‚ÇÅ M‚ÇÇ M‚ÇÅ M‚ÇÇ).dualMap
    ‚â™‚â´‚Çó (TensorProduct.lift.equiv A (M‚ÇÅ ‚äó[R] M‚ÇÇ) (M‚ÇÅ ‚äó[R] M‚ÇÇ) A).symm
    ‚â™‚â´‚Çó LinearMap.toBilin).toLinearMap
  ‚àò‚Çó TensorProduct.AlgebraTensorModule.dualDistrib R _ _ _
  ‚àò‚Çó (TensorProduct.AlgebraTensorModule.congr
    (BilinForm.toLin ‚â™‚â´‚Çó TensorProduct.lift.equiv A _ _ _)
    (BilinForm.toLin ‚â™‚â´‚Çó TensorProduct.lift.equiv R _ _ _)).toLinearMap
#align bilin_form.tensor_distrib BilinForm.tensorDistrib

-- TODO: make the RHS `MulOpposite.op (B‚ÇÇ m‚ÇÇ m‚ÇÇ') ‚Ä¢ B‚ÇÅ m‚ÇÅ m‚ÇÅ'` so that this has a nicer defeq for
-- `R = A` of `B‚ÇÅ m‚ÇÅ m‚ÇÅ' * B‚ÇÇ m‚ÇÇ m‚ÇÇ'`, as it did before the generalization in #6306.
@[simp]
theorem tensorDistrib_tmul (B‚ÇÅ : BilinForm A M‚ÇÅ) (B‚ÇÇ : BilinForm R M‚ÇÇ) (m‚ÇÅ : M‚ÇÅ) (m‚ÇÇ : M‚ÇÇ)
    (m‚ÇÅ' : M‚ÇÅ) (m‚ÇÇ' : M‚ÇÇ) :
    tensorDistrib R A (B‚ÇÅ ‚äó‚Çú B‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) (m‚ÇÅ' ‚äó‚Çú m‚ÇÇ')
      = B‚ÇÇ m‚ÇÇ m‚ÇÇ' ‚Ä¢ B‚ÇÅ m‚ÇÅ m‚ÇÅ' :=
  rfl
#align bilin_form.tensor_distrib_tmul BilinForm.tensorDistrib_tmul‚Çì

/-- The tensor product of two bilinear forms, a shorthand for dot notation. -/
@[reducible]
protected def tmul (B‚ÇÅ : BilinForm A M‚ÇÅ) (B‚ÇÇ : BilinForm R M‚ÇÇ) : BilinForm A (M‚ÇÅ ‚äó[R] M‚ÇÇ) :=
  tensorDistrib R A (B‚ÇÅ ‚äó‚Çú[R] B‚ÇÇ)
#align bilin_form.tmul BilinForm.tmul

attribute [ext] TensorProduct.ext in
/-- A tensor product of symmetric bilinear forms is symmetric. -/
lemma IsSymm.tmul {B‚ÇÅ : BilinForm A M‚ÇÅ} {B‚ÇÇ : BilinForm R M‚ÇÇ}
    (hB‚ÇÅ : B‚ÇÅ.IsSymm) (hB‚ÇÇ : B‚ÇÇ.IsSymm) : (B‚ÇÅ.tmul B‚ÇÇ).IsSymm := by
  rw [isSymm_iff_flip R]
  -- ‚ä¢ ‚Üë(flipHom R) (BilinForm.tmul B‚ÇÅ B‚ÇÇ) = BilinForm.tmul B‚ÇÅ B‚ÇÇ
  apply toLin.injective
  -- ‚ä¢ ‚ÜëtoLin (‚Üë(flipHom R) (BilinForm.tmul B‚ÇÅ B‚ÇÇ)) = ‚ÜëtoLin (BilinForm.tmul B‚ÇÅ B‚ÇÇ)
  ext x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ
  -- ‚ä¢ ‚Üë(‚Üë(AlgebraTensorModule.curry (‚Üë(‚Üë(AlgebraTensorModule.curry (‚ÜëtoLin (‚Üë(flip ‚Ä¶
  exact (congr_arg‚ÇÇ (HSMul.hSMul) (hB‚ÇÇ x‚ÇÇ y‚ÇÇ) (hB‚ÇÅ x‚ÇÅ y‚ÇÅ)).symm
  -- üéâ no goals

variable (A) in
/-- The base change of a bilinear form. -/
protected def baseChange (B : BilinForm R M‚ÇÇ) : BilinForm A (A ‚äó[R] M‚ÇÇ) :=
  BilinForm.tmul (R := R) (A := A) (M‚ÇÅ := A) (M‚ÇÇ := M‚ÇÇ) (LinearMap.toBilin <| LinearMap.mul A A) B

@[simp]
theorem baseChange_tmul (B‚ÇÇ : BilinForm R M‚ÇÇ) (a : A) (m‚ÇÇ : M‚ÇÇ)
    (a' : A) (m‚ÇÇ' : M‚ÇÇ) :
    B‚ÇÇ.baseChange A (a ‚äó‚Çú m‚ÇÇ) (a' ‚äó‚Çú m‚ÇÇ') = (B‚ÇÇ m‚ÇÇ m‚ÇÇ') ‚Ä¢ (a * a') :=
  rfl

variable (A) in
/-- The base change of a symmetric bilinear form is symmetric. -/
lemma IsSymm.baseChange {B‚ÇÇ : BilinForm R M‚ÇÇ} (hB‚ÇÇ : B‚ÇÇ.IsSymm) : (B‚ÇÇ.baseChange A).IsSymm :=
  IsSymm.tmul mul_comm hB‚ÇÇ

end CommSemiring

section CommRing

variable [CommRing R]

variable [AddCommGroup M‚ÇÅ] [AddCommGroup M‚ÇÇ]

variable [Module R M‚ÇÅ] [Module R M‚ÇÇ]

variable [Module.Free R M‚ÇÅ] [Module.Finite R M‚ÇÅ]

variable [Module.Free R M‚ÇÇ] [Module.Finite R M‚ÇÇ]

variable [Nontrivial R]

variable (R) in
/-- `tensorDistrib` as an equivalence. -/
noncomputable def tensorDistribEquiv :
    BilinForm R M‚ÇÅ ‚äó[R] BilinForm R M‚ÇÇ ‚âÉ‚Çó[R] BilinForm R (M‚ÇÅ ‚äó[R] M‚ÇÇ) :=
  -- the same `LinearEquiv`s as from `tensorDistrib`,
  -- but with the inner linear map also as an equiv
  TensorProduct.congr (BilinForm.toLin ‚â™‚â´‚Çó TensorProduct.lift.equiv R _ _ _)
    (BilinForm.toLin ‚â™‚â´‚Çó TensorProduct.lift.equiv R _ _ _) ‚â™‚â´‚Çó
  TensorProduct.dualDistribEquiv R (M‚ÇÅ ‚äó M‚ÇÅ) (M‚ÇÇ ‚äó M‚ÇÇ) ‚â™‚â´‚Çó
  (TensorProduct.tensorTensorTensorComm R _ _ _ _).dualMap ‚â™‚â´‚Çó
  (TensorProduct.lift.equiv R _ _ _).symm ‚â™‚â´‚Çó LinearMap.toBilin
#align bilin_form.tensor_distrib_equiv BilinForm.tensorDistribEquiv

-- this is a dsimp lemma
@[simp, nolint simpNF]
theorem tensorDistribEquiv_tmul (B‚ÇÅ : BilinForm R M‚ÇÅ) (B‚ÇÇ : BilinForm R M‚ÇÇ) (m‚ÇÅ : M‚ÇÅ) (m‚ÇÇ : M‚ÇÇ)
    (m‚ÇÅ' : M‚ÇÅ) (m‚ÇÇ' : M‚ÇÇ) :
    tensorDistribEquiv R (M‚ÇÅ := M‚ÇÅ) (M‚ÇÇ := M‚ÇÇ) (B‚ÇÅ ‚äó‚Çú[R] B‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) (m‚ÇÅ' ‚äó‚Çú m‚ÇÇ')
      = B‚ÇÅ m‚ÇÅ m‚ÇÅ' * B‚ÇÇ m‚ÇÇ m‚ÇÇ' :=
  rfl

variable (R M‚ÇÅ M‚ÇÇ) in
-- TODO: make this `rfl`
@[simp]
theorem tensorDistribEquiv_toLinearMap :
    (tensorDistribEquiv R (M‚ÇÅ := M‚ÇÅ) (M‚ÇÇ := M‚ÇÇ)).toLinearMap = tensorDistrib R R := by
  ext B‚ÇÅ B‚ÇÇ : 3
  -- ‚ä¢ ‚Üë(‚Üë(AlgebraTensorModule.curry ‚Üë(tensorDistribEquiv R)) B‚ÇÅ) B‚ÇÇ = ‚Üë(‚Üë(AlgebraT ‚Ä¶
  apply toLin.injective
  -- ‚ä¢ ‚ÜëtoLin (‚Üë(‚Üë(AlgebraTensorModule.curry ‚Üë(tensorDistribEquiv R)) B‚ÇÅ) B‚ÇÇ) = ‚Üëto ‚Ä¶
  ext
  -- ‚ä¢ ‚Üë(‚Üë(AlgebraTensorModule.curry (‚Üë(‚Üë(AlgebraTensorModule.curry (‚ÜëtoLin (‚Üë(‚Üë(Al ‚Ä¶
  exact mul_comm _ _
  -- üéâ no goals

@[simp]
theorem tensorDistribEquiv_apply (B : BilinForm R M‚ÇÅ ‚äó BilinForm R M‚ÇÇ) :
    tensorDistribEquiv R (M‚ÇÅ := M‚ÇÅ) (M‚ÇÇ := M‚ÇÇ) B = tensorDistrib R R B :=
  FunLike.congr_fun (tensorDistribEquiv_toLinearMap R M‚ÇÅ M‚ÇÇ) B
#align bilin_form.tensor_distrib_equiv_apply BilinForm.tensorDistribEquiv_apply

end CommRing

end BilinForm
