/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.LinearAlgebra.Quotient
import Mathlib.LinearAlgebra.Prod

#align_import linear_algebra.projection from "leanprover-community/mathlib"@"6d584f1709bedbed9175bd9350df46599bdd7213"

/-!
# Projection to a subspace

In this file we define
* `Submodule.linearProjOfIsCompl (p q : Submodule R E) (h : IsCompl p q)`:
  the projection of a module `E` to a submodule `p` along its complement `q`;
  it is the unique linear map `f : E â†’ p` such that `f x = x` for `x âˆˆ p` and `f x = 0` for `x âˆˆ q`.
* `Submodule.isComplEquivProj p`: equivalence between submodules `q`
  such that `IsCompl p q` and projections `f : E â†’ p`, `âˆ€ x âˆˆ p, f x = x`.

We also provide some lemmas justifying correctness of our definitions.

## Tags

projection, complement subspace
-/

noncomputable section Ring

variable {R : Type*} [Ring R] {E : Type*} [AddCommGroup E] [Module R E]
variable {F : Type*} [AddCommGroup F] [Module R F] {G : Type*} [AddCommGroup G] [Module R G]
variable (p q : Submodule R E)
variable {S : Type*} [Semiring S] {M : Type*} [AddCommMonoid M] [Module S M] (m : Submodule S M)

namespace LinearMap

variable {p}

open Submodule

theorem ker_id_sub_eq_of_proj {f : E â†’â‚—[R] p} (hf : âˆ€ x : p, f x = x) :
    ker (id - p.subtype.comp f) = p := by
  ext x
  -- âŠ¢ x âˆˆ ker (id - comp (Submodule.subtype p) f) â†” x âˆˆ p
  simp only [comp_apply, mem_ker, subtype_apply, sub_apply, id_apply, sub_eq_zero]
  -- âŠ¢ x = â†‘(â†‘f x) â†” x âˆˆ p
  exact âŸ¨fun h => h.symm â–¸ Submodule.coe_mem _, fun hx => by erw [hf âŸ¨x, hxâŸ©, Subtype.coe_mk]âŸ©
  -- ğŸ‰ no goals
#align linear_map.ker_id_sub_eq_of_proj LinearMap.ker_id_sub_eq_of_proj

theorem range_eq_of_proj {f : E â†’â‚—[R] p} (hf : âˆ€ x : p, f x = x) : range f = âŠ¤ :=
  range_eq_top.2 fun x => âŸ¨x, hf xâŸ©
#align linear_map.range_eq_of_proj LinearMap.range_eq_of_proj

theorem isCompl_of_proj {f : E â†’â‚—[R] p} (hf : âˆ€ x : p, f x = x) : IsCompl p (ker f) := by
  constructor
  -- âŠ¢ Disjoint p (ker f)
  Â· rw [disjoint_iff_inf_le]
    -- âŠ¢ p âŠ“ ker f â‰¤ âŠ¥
    rintro x âŸ¨hpx, hfxâŸ©
    -- âŠ¢ x âˆˆ âŠ¥
    erw [SetLike.mem_coe, mem_ker, hf âŸ¨x, hpxâŸ©, mk_eq_zero] at hfx
    -- âŠ¢ x âˆˆ âŠ¥
    simp only [hfx, SetLike.mem_coe, zero_mem]
    -- ğŸ‰ no goals
  Â· rw [codisjoint_iff_le_sup]
    -- âŠ¢ âŠ¤ â‰¤ p âŠ” ker f
    intro x _
    -- âŠ¢ x âˆˆ p âŠ” ker f
    rw [mem_sup']
    -- âŠ¢ âˆƒ y z, â†‘y + â†‘z = x
    refine' âŸ¨f x, âŸ¨x - f x, _âŸ©, add_sub_cancel'_right _ _âŸ©
    -- âŠ¢ x - â†‘(â†‘f x) âˆˆ ker f
    rw [mem_ker, LinearMap.map_sub, hf, sub_self]
    -- ğŸ‰ no goals
#align linear_map.is_compl_of_proj LinearMap.isCompl_of_proj

end LinearMap

namespace Submodule

open LinearMap

/-- If `q` is a complement of `p`, then `M/p â‰ƒ q`. -/
def quotientEquivOfIsCompl (h : IsCompl p q) : (E â§¸ p) â‰ƒâ‚—[R] q :=
  LinearEquiv.symm <|
    LinearEquiv.ofBijective (p.mkQ.comp q.subtype)
      âŸ¨by rw [â† ker_eq_bot, ker_comp, ker_mkQ, disjoint_iff_comap_eq_bot.1 h.symm.disjoint], by
          -- ğŸ‰ no goals
        rw [â† range_eq_top, range_comp, range_subtype, map_mkQ_eq_top, h.sup_eq_top]âŸ©
        -- ğŸ‰ no goals
#align submodule.quotient_equiv_of_is_compl Submodule.quotientEquivOfIsCompl

@[simp]
theorem quotientEquivOfIsCompl_symm_apply (h : IsCompl p q) (x : q) :
    -- Porting note: type ascriptions needed on the RHS
    (quotientEquivOfIsCompl p q h).symm x = (Quotient.mk (x:E) : E â§¸ p) := rfl
#align submodule.quotient_equiv_of_is_compl_symm_apply Submodule.quotientEquivOfIsCompl_symm_apply

@[simp]
theorem quotientEquivOfIsCompl_apply_mk_coe (h : IsCompl p q) (x : q) :
    quotientEquivOfIsCompl p q h (Quotient.mk x) = x :=
  (quotientEquivOfIsCompl p q h).apply_symm_apply x
#align submodule.quotient_equiv_of_is_compl_apply_mk_coe Submodule.quotientEquivOfIsCompl_apply_mk_coe

@[simp]
theorem mk_quotientEquivOfIsCompl_apply (h : IsCompl p q) (x : E â§¸ p) :
    (Quotient.mk (quotientEquivOfIsCompl p q h x) : E â§¸ p) = x :=
  (quotientEquivOfIsCompl p q h).symm_apply_apply x
#align submodule.mk_quotient_equiv_of_is_compl_apply Submodule.mk_quotientEquivOfIsCompl_apply

/-- If `q` is a complement of `p`, then `p Ã— q` is isomorphic to `E`. It is the unique
linear map `f : E â†’ p` such that `f x = x` for `x âˆˆ p` and `f x = 0` for `x âˆˆ q`. -/
def prodEquivOfIsCompl (h : IsCompl p q) : (p Ã— q) â‰ƒâ‚—[R] E := by
  apply LinearEquiv.ofBijective (p.subtype.coprod q.subtype)
  -- âŠ¢ Function.Bijective â†‘(coprod (Submodule.subtype p) (Submodule.subtype q))
  constructor
  -- âŠ¢ Function.Injective â†‘(coprod (Submodule.subtype p) (Submodule.subtype q))
  Â· rw [â† ker_eq_bot, ker_coprod_of_disjoint_range, ker_subtype, ker_subtype, prod_bot]
    -- âŠ¢ Disjoint (range (Submodule.subtype p)) (range (Submodule.subtype q))
    rw [range_subtype, range_subtype]
    -- âŠ¢ Disjoint p q
    exact h.1
    -- ğŸ‰ no goals
  Â· rw [â† range_eq_top, â† sup_eq_range, h.sup_eq_top]
    -- ğŸ‰ no goals
#align submodule.prod_equiv_of_is_compl Submodule.prodEquivOfIsCompl

@[simp]
theorem coe_prodEquivOfIsCompl (h : IsCompl p q) :
    (prodEquivOfIsCompl p q h : p Ã— q â†’â‚—[R] E) = p.subtype.coprod q.subtype := rfl
#align submodule.coe_prod_equiv_of_is_compl Submodule.coe_prodEquivOfIsCompl

@[simp]
theorem coe_prodEquivOfIsCompl' (h : IsCompl p q) (x : p Ã— q) :
    prodEquivOfIsCompl p q h x = x.1 + x.2 := rfl
#align submodule.coe_prod_equiv_of_is_compl' Submodule.coe_prodEquivOfIsCompl'

@[simp]
theorem prodEquivOfIsCompl_symm_apply_left (h : IsCompl p q) (x : p) :
    (prodEquivOfIsCompl p q h).symm x = (x, 0) :=
  (prodEquivOfIsCompl p q h).symm_apply_eq.2 <| by simp
                                                   -- ğŸ‰ no goals
#align submodule.prod_equiv_of_is_compl_symm_apply_left Submodule.prodEquivOfIsCompl_symm_apply_left

@[simp]
theorem prodEquivOfIsCompl_symm_apply_right (h : IsCompl p q) (x : q) :
    (prodEquivOfIsCompl p q h).symm x = (0, x) :=
  (prodEquivOfIsCompl p q h).symm_apply_eq.2 <| by simp
                                                   -- ğŸ‰ no goals
#align submodule.prod_equiv_of_is_compl_symm_apply_right Submodule.prodEquivOfIsCompl_symm_apply_right

@[simp]
theorem prodEquivOfIsCompl_symm_apply_fst_eq_zero (h : IsCompl p q) {x : E} :
    ((prodEquivOfIsCompl p q h).symm x).1 = 0 â†” x âˆˆ q := by
  conv_rhs => rw [â† (prodEquivOfIsCompl p q h).apply_symm_apply x]
  -- âŠ¢ (â†‘(LinearEquiv.symm (prodEquivOfIsCompl p q h)) x).fst = 0 â†” â†‘(prodEquivOfIs â€¦
  rw [coe_prodEquivOfIsCompl', Submodule.add_mem_iff_left _ (Submodule.coe_mem _),
    mem_right_iff_eq_zero_of_disjoint h.disjoint]
#align submodule.prod_equiv_of_is_compl_symm_apply_fst_eq_zero Submodule.prodEquivOfIsCompl_symm_apply_fst_eq_zero

@[simp]
theorem prodEquivOfIsCompl_symm_apply_snd_eq_zero (h : IsCompl p q) {x : E} :
    ((prodEquivOfIsCompl p q h).symm x).2 = 0 â†” x âˆˆ p := by
  conv_rhs => rw [â† (prodEquivOfIsCompl p q h).apply_symm_apply x]
  -- âŠ¢ (â†‘(LinearEquiv.symm (prodEquivOfIsCompl p q h)) x).snd = 0 â†” â†‘(prodEquivOfIs â€¦
  rw [coe_prodEquivOfIsCompl', Submodule.add_mem_iff_right _ (Submodule.coe_mem _),
    mem_left_iff_eq_zero_of_disjoint h.disjoint]
#align submodule.prod_equiv_of_is_compl_symm_apply_snd_eq_zero Submodule.prodEquivOfIsCompl_symm_apply_snd_eq_zero

@[simp]
theorem prodComm_trans_prodEquivOfIsCompl (h : IsCompl p q) :
    LinearEquiv.prodComm R q p â‰ªâ‰«â‚— prodEquivOfIsCompl p q h = prodEquivOfIsCompl q p h.symm :=
  LinearEquiv.ext fun _ => add_comm _ _
#align submodule.prod_comm_trans_prod_equiv_of_is_compl Submodule.prodComm_trans_prodEquivOfIsCompl

/-- Projection to a submodule along its complement. -/
def linearProjOfIsCompl (h : IsCompl p q) : E â†’â‚—[R] p :=
  LinearMap.fst R p q âˆ˜â‚— â†‘(prodEquivOfIsCompl p q h).symm
#align submodule.linear_proj_of_is_compl Submodule.linearProjOfIsCompl

variable {p q}

@[simp]
theorem linearProjOfIsCompl_apply_left (h : IsCompl p q) (x : p) :
    linearProjOfIsCompl p q h x = x := by simp [linearProjOfIsCompl]
                                          -- ğŸ‰ no goals
#align submodule.linear_proj_of_is_compl_apply_left Submodule.linearProjOfIsCompl_apply_left

@[simp]
theorem linearProjOfIsCompl_range (h : IsCompl p q) : range (linearProjOfIsCompl p q h) = âŠ¤ :=
  range_eq_of_proj (linearProjOfIsCompl_apply_left h)
#align submodule.linear_proj_of_is_compl_range Submodule.linearProjOfIsCompl_range

@[simp]
theorem linearProjOfIsCompl_apply_eq_zero_iff (h : IsCompl p q) {x : E} :
    linearProjOfIsCompl p q h x = 0 â†” x âˆˆ q := by simp [linearProjOfIsCompl]
                                                  -- ğŸ‰ no goals
#align submodule.linear_proj_of_is_compl_apply_eq_zero_iff Submodule.linearProjOfIsCompl_apply_eq_zero_iff

theorem linearProjOfIsCompl_apply_right' (h : IsCompl p q) (x : E) (hx : x âˆˆ q) :
    linearProjOfIsCompl p q h x = 0 :=
  (linearProjOfIsCompl_apply_eq_zero_iff h).2 hx
#align submodule.linear_proj_of_is_compl_apply_right' Submodule.linearProjOfIsCompl_apply_right'

@[simp]
theorem linearProjOfIsCompl_apply_right (h : IsCompl p q) (x : q) :
    linearProjOfIsCompl p q h x = 0 :=
  linearProjOfIsCompl_apply_right' h x x.2
#align submodule.linear_proj_of_is_compl_apply_right Submodule.linearProjOfIsCompl_apply_right

@[simp]
theorem linearProjOfIsCompl_ker (h : IsCompl p q) : ker (linearProjOfIsCompl p q h) = q :=
  ext fun _ => mem_ker.trans (linearProjOfIsCompl_apply_eq_zero_iff h)
#align submodule.linear_proj_of_is_compl_ker Submodule.linearProjOfIsCompl_ker

theorem linearProjOfIsCompl_comp_subtype (h : IsCompl p q) :
    (linearProjOfIsCompl p q h).comp p.subtype = LinearMap.id :=
  LinearMap.ext <| linearProjOfIsCompl_apply_left h
#align submodule.linear_proj_of_is_compl_comp_subtype Submodule.linearProjOfIsCompl_comp_subtype

theorem linearProjOfIsCompl_idempotent (h : IsCompl p q) (x : E) :
    linearProjOfIsCompl p q h (linearProjOfIsCompl p q h x) = linearProjOfIsCompl p q h x :=
  linearProjOfIsCompl_apply_left h _
#align submodule.linear_proj_of_is_compl_idempotent Submodule.linearProjOfIsCompl_idempotent

theorem existsUnique_add_of_isCompl_prod (hc : IsCompl p q) (x : E) :
    âˆƒ! u : p Ã— q, (u.fst : E) + u.snd = x :=
  (prodEquivOfIsCompl _ _ hc).toEquiv.bijective.existsUnique _
#align submodule.exists_unique_add_of_is_compl_prod Submodule.existsUnique_add_of_isCompl_prod

theorem existsUnique_add_of_isCompl (hc : IsCompl p q) (x : E) :
    âˆƒ (u : p) (v : q), (u : E) + v = x âˆ§ âˆ€ (r : p) (s : q), (r : E) + s = x â†’ r = u âˆ§ s = v :=
  let âŸ¨u, huâ‚, huâ‚‚âŸ© := existsUnique_add_of_isCompl_prod hc x
  âŸ¨u.1, u.2, huâ‚, fun r s hrs => Prod.eq_iff_fst_eq_snd_eq.1 (huâ‚‚ âŸ¨r, sâŸ© hrs)âŸ©
#align submodule.exists_unique_add_of_is_compl Submodule.existsUnique_add_of_isCompl

theorem linear_proj_add_linearProjOfIsCompl_eq_self (hpq : IsCompl p q) (x : E) :
    (p.linearProjOfIsCompl q hpq x + q.linearProjOfIsCompl p hpq.symm x : E) = x := by
  dsimp only [linearProjOfIsCompl]
  -- âŠ¢ â†‘(â†‘(comp (LinearMap.fst R { x // x âˆˆ p } { x // x âˆˆ q }) â†‘(LinearEquiv.symm  â€¦
  rw [â† prodComm_trans_prodEquivOfIsCompl _ _ hpq]
  -- âŠ¢ â†‘(â†‘(comp (LinearMap.fst R { x // x âˆˆ p } { x // x âˆˆ q }) â†‘(LinearEquiv.symm  â€¦
  exact (prodEquivOfIsCompl _ _ hpq).apply_symm_apply x
  -- ğŸ‰ no goals
#align submodule.linear_proj_add_linear_proj_of_is_compl_eq_self Submodule.linear_proj_add_linearProjOfIsCompl_eq_self

end Submodule

namespace LinearMap

open Submodule

/-- Given linear maps `Ï†` and `Ïˆ` from complement submodules, `LinearMap.ofIsCompl` is
the induced linear map over the entire module. -/
def ofIsCompl {p q : Submodule R E} (h : IsCompl p q) (Ï† : p â†’â‚—[R] F) (Ïˆ : q â†’â‚—[R] F) : E â†’â‚—[R] F :=
  LinearMap.coprod Ï† Ïˆ âˆ˜â‚— â†‘(Submodule.prodEquivOfIsCompl _ _ h).symm
#align linear_map.of_is_compl LinearMap.ofIsCompl

variable {p q}

@[simp]
theorem ofIsCompl_left_apply (h : IsCompl p q) {Ï† : p â†’â‚—[R] F} {Ïˆ : q â†’â‚—[R] F} (u : p) :
    ofIsCompl h Ï† Ïˆ (u : E) = Ï† u := by simp [ofIsCompl]
                                        -- ğŸ‰ no goals
#align linear_map.of_is_compl_left_apply LinearMap.ofIsCompl_left_apply

@[simp]
theorem ofIsCompl_right_apply (h : IsCompl p q) {Ï† : p â†’â‚—[R] F} {Ïˆ : q â†’â‚—[R] F} (v : q) :
    ofIsCompl h Ï† Ïˆ (v : E) = Ïˆ v := by simp [ofIsCompl]
                                        -- ğŸ‰ no goals
#align linear_map.of_is_compl_right_apply LinearMap.ofIsCompl_right_apply

theorem ofIsCompl_eq (h : IsCompl p q) {Ï† : p â†’â‚—[R] F} {Ïˆ : q â†’â‚—[R] F} {Ï‡ : E â†’â‚—[R] F}
    (hÏ† : âˆ€ u, Ï† u = Ï‡ u) (hÏˆ : âˆ€ u, Ïˆ u = Ï‡ u) : ofIsCompl h Ï† Ïˆ = Ï‡ := by
  ext x
  -- âŠ¢ â†‘(ofIsCompl h Ï† Ïˆ) x = â†‘Ï‡ x
  obtain âŸ¨_, _, rfl, _âŸ© := existsUnique_add_of_isCompl h x
  -- âŠ¢ â†‘(ofIsCompl h Ï† Ïˆ) (â†‘wâœÂ¹ + â†‘wâœ) = â†‘Ï‡ (â†‘wâœÂ¹ + â†‘wâœ)
  simp [ofIsCompl, hÏ†, hÏˆ]
  -- ğŸ‰ no goals
#align linear_map.of_is_compl_eq LinearMap.ofIsCompl_eq

theorem ofIsCompl_eq' (h : IsCompl p q) {Ï† : p â†’â‚—[R] F} {Ïˆ : q â†’â‚—[R] F} {Ï‡ : E â†’â‚—[R] F}
    (hÏ† : Ï† = Ï‡.comp p.subtype) (hÏˆ : Ïˆ = Ï‡.comp q.subtype) : ofIsCompl h Ï† Ïˆ = Ï‡ :=
  ofIsCompl_eq h (fun _ => hÏ†.symm â–¸ rfl) fun _ => hÏˆ.symm â–¸ rfl
#align linear_map.of_is_compl_eq' LinearMap.ofIsCompl_eq'

@[simp]
theorem ofIsCompl_zero (h : IsCompl p q) : (ofIsCompl h 0 0 : E â†’â‚—[R] F) = 0 :=
  ofIsCompl_eq _ (fun _ => rfl) fun _ => rfl
#align linear_map.of_is_compl_zero LinearMap.ofIsCompl_zero

@[simp]
theorem ofIsCompl_add (h : IsCompl p q) {Ï†â‚ Ï†â‚‚ : p â†’â‚—[R] F} {Ïˆâ‚ Ïˆâ‚‚ : q â†’â‚—[R] F} :
    ofIsCompl h (Ï†â‚ + Ï†â‚‚) (Ïˆâ‚ + Ïˆâ‚‚) = ofIsCompl h Ï†â‚ Ïˆâ‚ + ofIsCompl h Ï†â‚‚ Ïˆâ‚‚ :=
  ofIsCompl_eq _ (by simp) (by simp)
                     -- ğŸ‰ no goals
                               -- ğŸ‰ no goals
#align linear_map.of_is_compl_add LinearMap.ofIsCompl_add

@[simp]
theorem ofIsCompl_smul {R : Type*} [CommRing R] {E : Type*} [AddCommGroup E] [Module R E]
    {F : Type*} [AddCommGroup F] [Module R F] {p q : Submodule R E} (h : IsCompl p q)
    {Ï† : p â†’â‚—[R] F} {Ïˆ : q â†’â‚—[R] F} (c : R) : ofIsCompl h (c â€¢ Ï†) (c â€¢ Ïˆ) = c â€¢ ofIsCompl h Ï† Ïˆ :=
  ofIsCompl_eq _ (by simp) (by simp)
                     -- ğŸ‰ no goals
                               -- ğŸ‰ no goals
#align linear_map.of_is_compl_smul LinearMap.ofIsCompl_smul

section

variable {Râ‚ : Type*} [CommRing Râ‚] [Module Râ‚ E] [Module Râ‚ F]

/-- The linear map from `(p â†’â‚—[Râ‚] F) Ã— (q â†’â‚—[Râ‚] F)` to `E â†’â‚—[Râ‚] F`. -/
def ofIsComplProd {p q : Submodule Râ‚ E} (h : IsCompl p q) :
    (p â†’â‚—[Râ‚] F) Ã— (q â†’â‚—[Râ‚] F) â†’â‚—[Râ‚] E â†’â‚—[Râ‚] F where
  toFun Ï† := ofIsCompl h Ï†.1 Ï†.2
  map_add' := by intro Ï† Ïˆ; dsimp only; rw [Prod.snd_add, Prod.fst_add, ofIsCompl_add]
                 -- âŠ¢ (fun Ï† => ofIsCompl h Ï†.fst Ï†.snd) (Ï† + Ïˆ) = (fun Ï† => ofIsCompl h Ï†.fst Ï†.s â€¦
                            -- âŠ¢ ofIsCompl h (Ï† + Ïˆ).fst (Ï† + Ïˆ).snd = ofIsCompl h Ï†.fst Ï†.snd + ofIsCompl h  â€¦
                                        -- ğŸ‰ no goals
  map_smul' := by intro c Ï†; simp [Prod.smul_snd, Prod.smul_fst, ofIsCompl_smul]
                  -- âŠ¢ AddHom.toFun { toFun := fun Ï† => ofIsCompl h Ï†.fst Ï†.snd, map_add' := (_ : âˆ€ â€¦
                             -- ğŸ‰ no goals
#align linear_map.of_is_compl_prod LinearMap.ofIsComplProd

@[simp]
theorem ofIsComplProd_apply {p q : Submodule Râ‚ E} (h : IsCompl p q)
    (Ï† : (p â†’â‚—[Râ‚] F) Ã— (q â†’â‚—[Râ‚] F)) : ofIsComplProd h Ï† = ofIsCompl h Ï†.1 Ï†.2 :=
  rfl
#align linear_map.of_is_compl_prod_apply LinearMap.ofIsComplProd_apply

/-- The natural linear equivalence between `(p â†’â‚—[Râ‚] F) Ã— (q â†’â‚—[Râ‚] F)` and `E â†’â‚—[Râ‚] F`. -/
def ofIsComplProdEquiv {p q : Submodule Râ‚ E} (h : IsCompl p q) :
    ((p â†’â‚—[Râ‚] F) Ã— (q â†’â‚—[Râ‚] F)) â‰ƒâ‚—[Râ‚] E â†’â‚—[Râ‚] F :=
  { ofIsComplProd h with
    invFun := fun Ï† => âŸ¨Ï†.domRestrict p, Ï†.domRestrict qâŸ©
    left_inv := fun Ï† â†¦ by
      ext x
      -- âŠ¢ â†‘((fun Ï† => (domRestrict Ï† p, domRestrict Ï† q)) (AddHom.toFun { toAddHom :=  â€¦
      Â· exact ofIsCompl_left_apply h x
        -- ğŸ‰ no goals
      Â· exact ofIsCompl_right_apply h x
        -- ğŸ‰ no goals
    right_inv := fun Ï† â†¦ by
      ext x
      -- âŠ¢ â†‘(AddHom.toFun { toAddHom := srcâœ.toAddHom, map_smul' := (_ : âˆ€ (r : Râ‚) (x  â€¦
      obtain âŸ¨a, b, hab, _âŸ© := existsUnique_add_of_isCompl h x
      -- âŠ¢ â†‘(AddHom.toFun { toAddHom := srcâœ.toAddHom, map_smul' := (_ : âˆ€ (r : Râ‚) (x  â€¦
      rw [â† hab]; simp }
      -- âŠ¢ â†‘(AddHom.toFun { toAddHom := srcâœ.toAddHom, map_smul' := (_ : âˆ€ (r : Râ‚) (x  â€¦
                  -- ğŸ‰ no goals
#align linear_map.of_is_compl_prod_equiv LinearMap.ofIsComplProdEquiv

end

@[simp, nolint simpNF] -- Porting note: linter claims that LHS doesn't simplify, but it does
theorem linearProjOfIsCompl_of_proj (f : E â†’â‚—[R] p) (hf : âˆ€ x : p, f x = x) :
    p.linearProjOfIsCompl (ker f) (isCompl_of_proj hf) = f := by
  ext x
  -- âŠ¢ â†‘(â†‘(linearProjOfIsCompl p (ker f) (_ : IsCompl p (ker f))) x) = â†‘(â†‘f x)
  have : x âˆˆ p âŠ” (ker f) := by simp only [(isCompl_of_proj hf).sup_eq_top, mem_top]
  -- âŠ¢ â†‘(â†‘(linearProjOfIsCompl p (ker f) (_ : IsCompl p (ker f))) x) = â†‘(â†‘f x)
  rcases mem_sup'.1 this with âŸ¨x, y, rflâŸ©
  -- âŠ¢ â†‘(â†‘(linearProjOfIsCompl p (ker f) (_ : IsCompl p (ker f))) (â†‘x + â†‘y)) = â†‘(â†‘f â€¦
  simp [hf]
  -- ğŸ‰ no goals
#align linear_map.linear_proj_of_is_compl_of_proj LinearMap.linearProjOfIsCompl_of_proj

/-- If `f : E â†’â‚—[R] F` and `g : E â†’â‚—[R] G` are two surjective linear maps and
their kernels are complement of each other, then `x â†¦ (f x, g x)` defines
a linear equivalence `E â‰ƒâ‚—[R] F Ã— G`. -/
def equivProdOfSurjectiveOfIsCompl (f : E â†’â‚—[R] F) (g : E â†’â‚—[R] G) (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) : E â‰ƒâ‚—[R] F Ã— G :=
  LinearEquiv.ofBijective (f.prod g)
    âŸ¨by simp [â† ker_eq_bot, hfg.inf_eq_bot], by
        -- ğŸ‰ no goals
      rw [â† range_eq_top]
      -- âŠ¢ range (prod f g) = âŠ¤
      simp [range_prod_eq hfg.sup_eq_top, *]âŸ©
      -- ğŸ‰ no goals
#align linear_map.equiv_prod_of_surjective_of_is_compl LinearMap.equivProdOfSurjectiveOfIsCompl

@[simp]
theorem coe_equivProdOfSurjectiveOfIsCompl {f : E â†’â‚—[R] F} {g : E â†’â‚—[R] G} (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) :
    (equivProdOfSurjectiveOfIsCompl f g hf hg hfg : E â†’â‚—[R] F Ã— G) = f.prod g := rfl
#align linear_map.coe_equiv_prod_of_surjective_of_is_compl LinearMap.coe_equivProdOfSurjectiveOfIsCompl

@[simp]
theorem equivProdOfSurjectiveOfIsCompl_apply {f : E â†’â‚—[R] F} {g : E â†’â‚—[R] G} (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) (x : E) :
    equivProdOfSurjectiveOfIsCompl f g hf hg hfg x = (f x, g x) := rfl
#align linear_map.equiv_prod_of_surjective_of_is_compl_apply LinearMap.equivProdOfSurjectiveOfIsCompl_apply

end LinearMap

namespace Submodule

open LinearMap

/-- Equivalence between submodules `q` such that `IsCompl p q` and linear maps `f : E â†’â‚—[R] p`
such that `âˆ€ x : p, f x = x`. -/
def isComplEquivProj : { q // IsCompl p q } â‰ƒ { f : E â†’â‚—[R] p // âˆ€ x : p, f x = x } where
  toFun q := âŸ¨linearProjOfIsCompl p q q.2, linearProjOfIsCompl_apply_left q.2âŸ©
  invFun f := âŸ¨ker (f : E â†’â‚—[R] p), isCompl_of_proj f.2âŸ©
  left_inv := fun âŸ¨q, hqâŸ© => by simp only [linearProjOfIsCompl_ker, Subtype.coe_mk]
                                -- ğŸ‰ no goals
  right_inv := fun âŸ¨f, hfâŸ© => Subtype.eq <| f.linearProjOfIsCompl_of_proj hf
#align submodule.is_compl_equiv_proj Submodule.isComplEquivProj

@[simp]
theorem coe_isComplEquivProj_apply (q : { q // IsCompl p q }) :
    (p.isComplEquivProj q : E â†’â‚—[R] p) = linearProjOfIsCompl p q q.2 := rfl
#align submodule.coe_is_compl_equiv_proj_apply Submodule.coe_isComplEquivProj_apply

@[simp]
theorem coe_isComplEquivProj_symm_apply (f : { f : E â†’â‚—[R] p // âˆ€ x : p, f x = x }) :
    (p.isComplEquivProj.symm f : Submodule R E) = ker (f : E â†’â‚—[R] p) := rfl
#align submodule.coe_is_compl_equiv_proj_symm_apply Submodule.coe_isComplEquivProj_symm_apply

end Submodule

namespace LinearMap

open Submodule

/--
A linear endomorphism of a module `E` is a projection onto a submodule `p` if it sends every element
of `E` to `p` and fixes every element of `p`.
The definition allow more generally any `FunLike` type and not just linear maps, so that it can be
used for example with `ContinuousLinearMap` or `Matrix`.
-/
structure IsProj {F : Type*} [FunLike F M fun _ => M] (f : F) : Prop where
  map_mem : âˆ€ x, f x âˆˆ m
  map_id : âˆ€ x âˆˆ m, f x = x
#align linear_map.is_proj LinearMap.IsProj

theorem isProj_iff_idempotent (f : M â†’â‚—[S] M) : (âˆƒ p : Submodule S M, IsProj p f) â†” f âˆ˜â‚— f = f := by
  constructor
  -- âŠ¢ (âˆƒ p, IsProj p f) â†’ comp f f = f
  Â· intro h
    -- âŠ¢ comp f f = f
    obtain âŸ¨p, hpâŸ© := h
    -- âŠ¢ comp f f = f
    ext x
    -- âŠ¢ â†‘(comp f f) x = â†‘f x
    rw [comp_apply]
    -- âŠ¢ â†‘f (â†‘f x) = â†‘f x
    exact hp.map_id (f x) (hp.map_mem x)
    -- ğŸ‰ no goals
  Â· intro h
    -- âŠ¢ âˆƒ p, IsProj p f
    use range f
    -- âŠ¢ IsProj (range f) f
    constructor
    -- âŠ¢ âˆ€ (x : M), â†‘f x âˆˆ range f
    Â· intro x
      -- âŠ¢ â†‘f x âˆˆ range f
      exact mem_range_self f x
      -- ğŸ‰ no goals
    Â· intro x hx
      -- âŠ¢ â†‘f x = x
      obtain âŸ¨y, hyâŸ© := mem_range.1 hx
      -- âŠ¢ â†‘f x = x
      rw [â† hy, â† comp_apply, h]
      -- ğŸ‰ no goals
#align linear_map.is_proj_iff_idempotent LinearMap.isProj_iff_idempotent

namespace IsProj

variable {p m}

/-- Restriction of the codomain of a projection of onto a subspace `p` to `p` instead of the whole
space.
-/
def codRestrict {f : M â†’â‚—[S] M} (h : IsProj m f) : M â†’â‚—[S] m :=
  f.codRestrict m h.map_mem
#align linear_map.is_proj.cod_restrict LinearMap.IsProj.codRestrict

@[simp]
theorem codRestrict_apply {f : M â†’â‚—[S] M} (h : IsProj m f) (x : M) : â†‘(h.codRestrict x) = f x :=
  f.codRestrict_apply m x
#align linear_map.is_proj.cod_restrict_apply LinearMap.IsProj.codRestrict_apply

@[simp]
theorem codRestrict_apply_cod {f : M â†’â‚—[S] M} (h : IsProj m f) (x : m) : h.codRestrict x = x := by
  ext
  -- âŠ¢ â†‘(â†‘(codRestrict h) â†‘x) = â†‘x
  rw [codRestrict_apply]
  -- âŠ¢ â†‘f â†‘x = â†‘x
  exact h.map_id x x.2
  -- ğŸ‰ no goals
#align linear_map.is_proj.cod_restrict_apply_cod LinearMap.IsProj.codRestrict_apply_cod

theorem codRestrict_ker {f : M â†’â‚—[S] M} (h : IsProj m f) : ker h.codRestrict = ker f :=
  f.ker_codRestrict m _
#align linear_map.is_proj.cod_restrict_ker LinearMap.IsProj.codRestrict_ker

theorem isCompl {f : E â†’â‚—[R] E} (h : IsProj p f) : IsCompl p (ker f) := by
  rw [â† codRestrict_ker]
  exact isCompl_of_proj h.codRestrict_apply_cod
  -- ğŸ‰ no goals
#align linear_map.is_proj.is_compl LinearMap.IsProj.isCompl

theorem eq_conj_prod_map' {f : E â†’â‚—[R] E} (h : IsProj p f) :
    f = (p.prodEquivOfIsCompl (ker f) h.isCompl).toLinearMap âˆ˜â‚—
        prodMap id 0 âˆ˜â‚— (p.prodEquivOfIsCompl (ker f) h.isCompl).symm.toLinearMap := by
  rw [â† LinearMap.comp_assoc, LinearEquiv.eq_comp_toLinearMap_symm]
  -- âŠ¢ comp f â†‘(prodEquivOfIsCompl p (ker f) (_ : IsCompl p (ker f))) = comp (â†‘(pro â€¦
  ext x
  -- âŠ¢ â†‘(comp (comp f â†‘(prodEquivOfIsCompl p (ker f) (_ : IsCompl p (ker f)))) (inl â€¦
  Â· simp only [coe_prodEquivOfIsCompl, comp_apply, coe_inl, coprod_apply, coeSubtype,
      _root_.map_zero, add_zero, h.map_id x x.2, prodMap_apply, id_apply]
  Â· simp only [coe_prodEquivOfIsCompl, comp_apply, coe_inr, coprod_apply, _root_.map_zero,
      coeSubtype, zero_add, map_coe_ker, prodMap_apply, zero_apply, add_zero]
#align linear_map.is_proj.eq_conj_prod_map' LinearMap.IsProj.eq_conj_prod_map'

end IsProj

end LinearMap

end Ring

section CommRing

namespace LinearMap

variable {R : Type*} [CommRing R] {E : Type*} [AddCommGroup E] [Module R E] {p : Submodule R E}

theorem IsProj.eq_conj_prodMap {f : E â†’â‚—[R] E} (h : IsProj p f) :
    f = (p.prodEquivOfIsCompl (ker f) h.isCompl).conj (prodMap id 0) := by
  rw [LinearEquiv.conj_apply]
  -- âŠ¢ f = comp (comp (â†‘(Submodule.prodEquivOfIsCompl p (ker f) (_ : IsCompl p (ker â€¦
  exact h.eq_conj_prod_map'
  -- ğŸ‰ no goals
#align linear_map.is_proj.eq_conj_prod_map LinearMap.IsProj.eq_conj_prodMap

end LinearMap

end CommRing
