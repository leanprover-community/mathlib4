/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.Algebra.BigOperators.Fin
import Mathlib.Algebra.GeomSum
import Mathlib.LinearAlgebra.Matrix.Determinant
import Mathlib.LinearAlgebra.Matrix.Nondegenerate

#align_import linear_algebra.vandermonde from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Vandermonde matrix

This file defines the `vandermonde` matrix and gives its determinant.

## Main definitions

 - `vandermonde v`: a square matrix with the `i, j`th entry equal to `v i ^ j`.

## Main results

 - `det_vandermonde`: `det (vandermonde v)` is the product of `v i - v j`, where
   `(i, j)` ranges over the unordered pairs.
-/


variable {R : Type*} [CommRing R]

open Equiv Finset

open BigOperators Matrix

namespace Matrix

/-- `vandermonde v` is the square matrix with `i`th row equal to `1, v i, v i ^ 2, v i ^ 3, ...`.
-/
def vandermonde {n : ‚Ñï} (v : Fin n ‚Üí R) : Matrix (Fin n) (Fin n) R := fun i j => v i ^ (j : ‚Ñï)
#align matrix.vandermonde Matrix.vandermonde

@[simp]
theorem vandermonde_apply {n : ‚Ñï} (v : Fin n ‚Üí R) (i j) : vandermonde v i j = v i ^ (j : ‚Ñï) :=
  rfl
#align matrix.vandermonde_apply Matrix.vandermonde_apply

@[simp]
theorem vandermonde_cons {n : ‚Ñï} (v0 : R) (v : Fin n ‚Üí R) :
    vandermonde (Fin.cons v0 v : Fin n.succ ‚Üí R) =
      Fin.cons (fun (j : Fin n.succ) => v0 ^ (j : ‚Ñï)) fun i => Fin.cons 1
      fun j => v i * vandermonde v i j := by
  ext i j
  -- ‚ä¢ vandermonde (Fin.cons v0 v) i j = Fin.cons (fun j => v0 ^ ‚Üëj) (fun i => Fin. ‚Ä¶
  refine' Fin.cases (by simp) (fun i => _) i
  -- ‚ä¢ vandermonde (Fin.cons v0 v) (Fin.succ i) j = Fin.cons (fun j => v0 ^ ‚Üëj) (fu ‚Ä¶
  refine' Fin.cases (by simp) (fun j => _) j
  -- ‚ä¢ vandermonde (Fin.cons v0 v) (Fin.succ i) (Fin.succ j) = Fin.cons (fun j => v ‚Ä¶
  simp [pow_succ]
  -- üéâ no goals
#align matrix.vandermonde_cons Matrix.vandermonde_cons

theorem vandermonde_succ {n : ‚Ñï} (v : Fin n.succ ‚Üí R) :
    vandermonde v =
      Fin.cons (fun (j : Fin n.succ) => v 0 ^ (j : ‚Ñï)) fun i =>
        Fin.cons 1 fun j => v i.succ * vandermonde (Fin.tail v) i j := by
  conv_lhs => rw [‚Üê Fin.cons_self_tail v, vandermonde_cons]
  -- üéâ no goals
#align matrix.vandermonde_succ Matrix.vandermonde_succ

theorem vandermonde_mul_vandermonde_transpose {n : ‚Ñï} (v w : Fin n ‚Üí R) (i j) :
    (vandermonde v * (vandermonde w)·µÄ) i j = ‚àë k : Fin n, (v i * w j) ^ (k : ‚Ñï) := by
  simp only [vandermonde_apply, Matrix.mul_apply, Matrix.transpose_apply, mul_pow]
  -- üéâ no goals
#align matrix.vandermonde_mul_vandermonde_transpose Matrix.vandermonde_mul_vandermonde_transpose

theorem vandermonde_transpose_mul_vandermonde {n : ‚Ñï} (v : Fin n ‚Üí R) (i j) :
    ((vandermonde v)·µÄ * vandermonde v) i j = ‚àë k : Fin n, v k ^ (i + j : ‚Ñï) := by
  simp only [vandermonde_apply, Matrix.mul_apply, Matrix.transpose_apply, pow_add]
  -- üéâ no goals
#align matrix.vandermonde_transpose_mul_vandermonde Matrix.vandermonde_transpose_mul_vandermonde

theorem det_vandermonde {n : ‚Ñï} (v : Fin n ‚Üí R) :
    det (vandermonde v) = ‚àè i : Fin n, Finset.prod (Ioi i) (fun j => v j - v i) := by
  unfold vandermonde
  -- ‚ä¢ (det fun i j => v i ^ ‚Üëj) = ‚àè i : Fin n, ‚àè j in Ioi i, (v j - v i)
  induction' n with n ih
  -- ‚ä¢ (det fun i j => v i ^ ‚Üëj) = ‚àè i : Fin Nat.zero, ‚àè j in Ioi i, (v j - v i)
  ¬∑ exact det_eq_one_of_card_eq_zero (Fintype.card_fin 0)
    -- üéâ no goals
  calc
    det (of fun i j : Fin n.succ => v i ^ (j : ‚Ñï)) =
        det
          (of fun i j : Fin n.succ =>
            Matrix.vecCons (v 0 ^ (j : ‚Ñï)) (fun i => v (Fin.succ i) ^ (j : ‚Ñï) - v 0 ^ (j : ‚Ñï)) i) :=
      det_eq_of_forall_row_eq_smul_add_const (Matrix.vecCons 0 1) 0 (Fin.cons_zero _ _) ?_
    _ =
        det
          (of fun i j : Fin n =>
            Matrix.vecCons (v 0 ^ (j.succ : ‚Ñï))
              (fun i : Fin n => v (Fin.succ i) ^ (j.succ : ‚Ñï) - v 0 ^ (j.succ : ‚Ñï))
              (Fin.succAbove 0 i)) := by
      simp_rw [det_succ_column_zero, Fin.sum_univ_succ, of_apply, Matrix.cons_val_zero, submatrix,
        of_apply, Matrix.cons_val_succ, Fin.val_zero, pow_zero, one_mul, sub_self,
        mul_zero, zero_mul, Finset.sum_const_zero, add_zero]
    _ =
        det
          (of fun i j : Fin n =>
              (v (Fin.succ i) - v 0) *
                ‚àë k in Finset.range (j + 1 : ‚Ñï), v i.succ ^ k * v 0 ^ (j - k : ‚Ñï) :
            Matrix _ _ R) := by
      congr
      ext i j
      rw [Fin.succAbove_zero, Matrix.cons_val_succ, Fin.val_succ, mul_comm]
      exact (geom_sum‚ÇÇ_mul (v i.succ) (v 0) (j + 1 : ‚Ñï)).symm
    _ =
        (Finset.prod Finset.univ (fun i => v (Fin.succ i) - v 0)) *
          det fun i j : Fin n =>
            ‚àë k in Finset.range (j + 1 : ‚Ñï), v i.succ ^ k * v 0 ^ (j - k : ‚Ñï) :=
      (det_mul_column (fun i => v (Fin.succ i) - v 0) _)
    _ = (Finset.prod Finset.univ (fun i => v (Fin.succ i) - v 0)) *
    det fun i j : Fin n => v (Fin.succ i) ^ (j : ‚Ñï) := congr_arg _ ?_
    _ = ‚àè i : Fin n.succ, Finset.prod (Ioi i) (fun j => v j - v i) := by
      simp_rw [Fin.prod_univ_succ, Fin.prod_Ioi_zero, Fin.prod_Ioi_succ]
      have h := ih (v ‚àò Fin.succ)
      unfold Function.comp at h
      rw [h]

  ¬∑ intro i j
    -- ‚ä¢ ‚Üëof (fun i j => v i ^ ‚Üëj) i j = ‚Üëof (fun i j => vecCons (v 0 ^ ‚Üëj) (fun i => ‚Ä¶
    simp_rw [of_apply]
    -- ‚ä¢ v i ^ ‚Üëj = vecCons (v 0 ^ ‚Üëj) (fun i => v (Fin.succ i) ^ ‚Üëj - v 0 ^ ‚Üëj) i +  ‚Ä¶
    rw [Matrix.cons_val_zero]
    -- ‚ä¢ v i ^ ‚Üëj = vecCons (v 0 ^ ‚Üëj) (fun i => v (Fin.succ i) ^ ‚Üëj - v 0 ^ ‚Üëj) i +  ‚Ä¶
    refine' Fin.cases _ (fun i => _) i
    -- ‚ä¢ v 0 ^ ‚Üëj = vecCons (v 0 ^ ‚Üëj) (fun i => v (Fin.succ i) ^ ‚Üëj - v 0 ^ ‚Üëj) 0 +  ‚Ä¶
    ¬∑ simp
      -- üéâ no goals
    rw [Matrix.cons_val_succ, Matrix.cons_val_succ, Pi.one_apply]
    -- ‚ä¢ v (Fin.succ i) ^ ‚Üëj = v (Fin.succ i) ^ ‚Üëj - v 0 ^ ‚Üëj + 1 * v 0 ^ ‚Üëj
    ring
    -- üéâ no goals
  ¬∑ cases n
    -- ‚ä¢ (det fun i j => ‚àë k in range (‚Üëj + 1), v (Fin.succ i) ^ k * v 0 ^ (‚Üëj - k))  ‚Ä¶
    ¬∑ rw [det_eq_one_of_card_eq_zero (Fintype.card_fin 0),
      det_eq_one_of_card_eq_zero (Fintype.card_fin 0)]
    apply det_eq_of_forall_col_eq_smul_add_pred fun _ => v 0
    -- ‚ä¢ ‚àÄ (i : Fin (n‚úù + 1)), ‚àë k in range (‚Üë0 + 1), v (Fin.succ i) ^ k * v 0 ^ (‚Üë0  ‚Ä¶
    ¬∑ intro j
      -- ‚ä¢ ‚àë k in range (‚Üë0 + 1), v (Fin.succ j) ^ k * v 0 ^ (‚Üë0 - k) = v (Fin.succ j)  ‚Ä¶
      simp
      -- üéâ no goals
    ¬∑ intro i j
      -- ‚ä¢ ‚àë k in range (‚Üë(Fin.succ j) + 1), v (Fin.succ i) ^ k * v 0 ^ (‚Üë(Fin.succ j)  ‚Ä¶
      simp only [smul_eq_mul, Pi.add_apply, Fin.val_succ, Fin.coe_castSucc, Pi.smul_apply]
      -- ‚ä¢ ‚àë x in range (‚Üëj + 1 + 1), v (Fin.succ i) ^ x * v 0 ^ (‚Üëj + 1 - x) = v (Fin. ‚Ä¶
      rw [Finset.sum_range_succ, add_comm, tsub_self, pow_zero, mul_one, Finset.mul_sum]
      -- ‚ä¢ v (Fin.succ i) ^ (‚Üëj + 1) + ‚àë x in range (‚Üëj + 1), v (Fin.succ i) ^ x * v 0  ‚Ä¶
      congr 1
      -- ‚ä¢ ‚àë x in range (‚Üëj + 1), v (Fin.succ i) ^ x * v 0 ^ (‚Üëj + 1 - x) = ‚àë x in rang ‚Ä¶
      refine' Finset.sum_congr rfl fun i' hi' => _
      -- ‚ä¢ v (Fin.succ i) ^ i' * v 0 ^ (‚Üëj + 1 - i') = v 0 * (v (Fin.succ i) ^ i' * v 0 ‚Ä¶
      rw [mul_left_comm (v 0), Nat.succ_sub, pow_succ]
      -- ‚ä¢ i' ‚â§ ‚Üëj
      exact Nat.lt_succ_iff.mp (Finset.mem_range.mp hi')
      -- üéâ no goals
#align matrix.det_vandermonde Matrix.det_vandermonde

theorem det_vandermonde_eq_zero_iff [IsDomain R] {n : ‚Ñï} {v : Fin n ‚Üí R} :
    det (vandermonde v) = 0 ‚Üî ‚àÉ i j : Fin n, v i = v j ‚àß i ‚â† j := by
  constructor
  -- ‚ä¢ det (vandermonde v) = 0 ‚Üí ‚àÉ i j, v i = v j ‚àß i ‚â† j
  ¬∑ simp only [det_vandermonde v, Finset.prod_eq_zero_iff, sub_eq_zero, forall_exists_index]
    -- ‚ä¢ ‚àÄ (x : Fin n), (x ‚àà univ ‚àß ‚àÉ a, a ‚àà Ioi x ‚àß v a = v x) ‚Üí ‚àÉ i j, v i = v j ‚àß  ‚Ä¶
    rintro i ‚ü®_, j, h‚ÇÅ, h‚ÇÇ‚ü©
    -- ‚ä¢ ‚àÉ i j, v i = v j ‚àß i ‚â† j
    exact ‚ü®j, i, h‚ÇÇ, (mem_Ioi.mp h‚ÇÅ).ne'‚ü©
    -- üéâ no goals
  ¬∑ simp only [Ne.def, forall_exists_index, and_imp]
    -- ‚ä¢ ‚àÄ (x x_1 : Fin n), v x = v x_1 ‚Üí ¬¨x = x_1 ‚Üí det (vandermonde v) = 0
    refine' fun i j h‚ÇÅ h‚ÇÇ => Matrix.det_zero_of_row_eq h‚ÇÇ (funext fun k => _)
    -- ‚ä¢ vandermonde v i k = vandermonde v j k
    rw [vandermonde_apply, vandermonde_apply, h‚ÇÅ]
    -- üéâ no goals
#align matrix.det_vandermonde_eq_zero_iff Matrix.det_vandermonde_eq_zero_iff

theorem det_vandermonde_ne_zero_iff [IsDomain R] {n : ‚Ñï} {v : Fin n ‚Üí R} :
    det (vandermonde v) ‚â† 0 ‚Üî Function.Injective v := by
  unfold Function.Injective
  -- ‚ä¢ det (vandermonde v) ‚â† 0 ‚Üî ‚àÄ ‚¶Éa‚ÇÅ a‚ÇÇ : Fin n‚¶Ñ, v a‚ÇÅ = v a‚ÇÇ ‚Üí a‚ÇÅ = a‚ÇÇ
  simp only [det_vandermonde_eq_zero_iff, Ne.def, not_exists, not_and, Classical.not_not]
  -- üéâ no goals
#align matrix.det_vandermonde_ne_zero_iff Matrix.det_vandermonde_ne_zero_iff

theorem eq_zero_of_forall_index_sum_pow_mul_eq_zero {R : Type*} [CommRing R] [IsDomain R] {n : ‚Ñï}
    {f v : Fin n ‚Üí R} (hf : Function.Injective f)
    (hfv : ‚àÄ j, (‚àë i : Fin n, f j ^ (i : ‚Ñï) * v i) = 0) : v = 0 :=
  eq_zero_of_mulVec_eq_zero (det_vandermonde_ne_zero_iff.mpr hf) (funext hfv)
#align matrix.eq_zero_of_forall_index_sum_pow_mul_eq_zero Matrix.eq_zero_of_forall_index_sum_pow_mul_eq_zero

theorem eq_zero_of_forall_index_sum_mul_pow_eq_zero {R : Type*} [CommRing R] [IsDomain R] {n : ‚Ñï}
    {f v : Fin n ‚Üí R} (hf : Function.Injective f) (hfv : ‚àÄ j, (‚àë i, v i * f j ^ (i : ‚Ñï)) = 0) :
    v = 0 := by
  apply eq_zero_of_forall_index_sum_pow_mul_eq_zero hf
  -- ‚ä¢ ‚àÄ (j : Fin n), ‚àë i : Fin n, f j ^ ‚Üëi * v i = 0
  simp_rw [mul_comm]
  -- ‚ä¢ ‚àÄ (j : Fin n), ‚àë x : Fin n, v x * f j ^ ‚Üëx = 0
  exact hfv
  -- üéâ no goals
#align matrix.eq_zero_of_forall_index_sum_mul_pow_eq_zero Matrix.eq_zero_of_forall_index_sum_mul_pow_eq_zero

theorem eq_zero_of_forall_pow_sum_mul_pow_eq_zero {R : Type*} [CommRing R] [IsDomain R] {n : ‚Ñï}
    {f v : Fin n ‚Üí R} (hf : Function.Injective f)
    (hfv : ‚àÄ i : Fin n, (‚àë j : Fin n, v j * f j ^ (i : ‚Ñï)) = 0) : v = 0 :=
  eq_zero_of_vecMul_eq_zero (det_vandermonde_ne_zero_iff.mpr hf) (funext hfv)
#align matrix.eq_zero_of_forall_pow_sum_mul_pow_eq_zero Matrix.eq_zero_of_forall_pow_sum_mul_pow_eq_zero

end Matrix
