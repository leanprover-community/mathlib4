/-
Copyright (c) 2021 Riccardo Brasca. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Riccardo Brasca
-/
import Mathlib.Data.Finsupp.Fintype
import Mathlib.LinearAlgebra.Basis.Basic
import Mathlib.LinearAlgebra.TensorProduct.Basis

/-!
# Free modules

We introduce a class `Module.Free R M`, for `R` a `Semiring` and `M` an `R`-module and we provide
several basic instances for this class.

Use `Finsupp.linearCombination_id_surjective` to prove that any module is the quotient of a free
module.

## Main definition

* `Module.Free R M` : the class of free `R`-modules.
-/


universe u v w z

variable {Î¹ : Type*} (R : Type u) (M : Type v) (N : Type z)

open TensorProduct DirectSum

section Basic

variable [Semiring R] [AddCommMonoid M] [Module R M]

/-- `Module.Free R M` is the statement that the `R`-module `M` is free. -/
class Module.Free : Prop where
  exists_basis : Nonempty <| (I : Type v) Ã— Basis I R M

/-- If `M` fits in universe `w`, then freeness is equivalent to existence of a basis in that
universe.

Note that if `M` does not fit in `w`, the reverse direction of this implication is still true as
`Module.Free.of_basis`. -/
theorem Module.free_def [Small.{w,v} M] :
    Module.Free R M â†” âˆƒ I : Type w, Nonempty (Basis I R M) :=
  âŸ¨fun h =>
    âŸ¨Shrink (Set.range h.exists_basis.some.2),
      âŸ¨(Basis.reindexRange h.exists_basis.some.2).reindex (equivShrink _)âŸ©âŸ©,
    fun h => âŸ¨(nonempty_sigma.2 h).map fun âŸ¨_, bâŸ© => âŸ¨Set.range b, b.reindexRangeâŸ©âŸ©âŸ©

theorem Module.free_iff_set : Module.Free R M â†” âˆƒ S : Set M, Nonempty (Basis S R M) :=
  âŸ¨fun h => âŸ¨Set.range h.exists_basis.some.2, âŸ¨Basis.reindexRange h.exists_basis.some.2âŸ©âŸ©,
    fun âŸ¨S, hSâŸ© => âŸ¨nonempty_sigma.2 âŸ¨S, hSâŸ©âŸ©âŸ©

variable {R M}

theorem Module.Free.of_basis {Î¹ : Type w} (b : Basis Î¹ R M) : Module.Free R M :=
  (Module.free_def R M).2 âŸ¨Set.range b, âŸ¨b.reindexRangeâŸ©âŸ©

end Basic

namespace Module.Free

section Semiring

variable [Semiring R] [AddCommMonoid M] [Module R M] [Module.Free R M]
variable [AddCommMonoid N] [Module R N]

/-- If `Module.Free R M` then `ChooseBasisIndex R M` is the `Î¹` which indexes the basis
  `Î¹ â†’ M`. Note that this is defined such that this type is finite if `R` is trivial. -/
def ChooseBasisIndex : Type _ :=
  ((Module.free_iff_set R M).mp â€¹_â€º).choose

/-- There is no hope of computing this, but we add the instance anyway to avoid fumbling with
`open scoped Classical`. -/
noncomputable instance : DecidableEq (ChooseBasisIndex R M) := Classical.decEq _

/-- If `Module.Free R M` then `chooseBasis : Î¹ â†’ M` is the basis.
Here `Î¹ = ChooseBasisIndex R M`. -/
noncomputable def chooseBasis : Basis (ChooseBasisIndex R M) R M :=
  ((Module.free_iff_set R M).mp â€¹_â€º).choose_spec.some

/-- The isomorphism `M â‰ƒâ‚—[R] (ChooseBasisIndex R M â†’â‚€ R)`. -/
noncomputable def repr : M â‰ƒâ‚—[R] ChooseBasisIndex R M â†’â‚€ R :=
  (chooseBasis R M).repr

/-- The universal property of free modules: giving a function `(ChooseBasisIndex R M) â†’ N`, for `N`
an `R`-module, is the same as giving an `R`-linear map `M â†’â‚—[R] N`.

This definition is parameterized over an extra `Semiring S`,
such that `SMulCommClass R S M'` holds.
If `R` is commutative, you can set `S := R`; if `R` is not commutative,
you can recover an `AddEquiv` by setting `S := â„•`.
See library note [bundled maps over different rings]. -/
noncomputable def constr {S : Type z} [Semiring S] [Module S N] [SMulCommClass R S N] :
    (ChooseBasisIndex R M â†’ N) â‰ƒâ‚—[S] M â†’â‚—[R] N :=
  Basis.constr (chooseBasis R M) S

instance (priority := 100) noZeroSMulDivisors [NoZeroDivisors R] : NoZeroSMulDivisors R M :=
  let âŸ¨âŸ¨_, bâŸ©âŸ© := exists_basis (R := R) (M := M)
  b.noZeroSMulDivisors

instance [Nontrivial M] : Nonempty (Module.Free.ChooseBasisIndex R M) :=
  (Module.Free.chooseBasis R M).index_nonempty

theorem infinite [Infinite R] [Nontrivial M] : Infinite M :=
  (Equiv.infinite_iff (chooseBasis R M).repr.toEquiv).mpr Finsupp.infinite_of_right

variable {R M N}

theorem of_equiv (e : M â‰ƒâ‚—[R] N) : Module.Free R N :=
  of_basis <| (chooseBasis R M).map e

/-- A variation of `of_equiv`: the assumption `Module.Free R P` here is explicit rather than an
instance. -/
theorem of_equiv' {P : Type v} [AddCommMonoid P] [Module R P] (_ : Module.Free R P)
    (e : P â‰ƒâ‚—[R] N) : Module.Free R N :=
  of_equiv e

variable (R M N)

/-- The module structure provided by `Semiring.toModule` is free. -/
instance self : Module.Free R R :=
  of_basis (Basis.singleton Unit R)

instance prod [Module.Free R N] : Module.Free R (M Ã— N) :=
  of_basis <| (chooseBasis R M).prod (chooseBasis R N)

/-- The product of finitely many free modules is free. -/
instance pi (M : Î¹ â†’ Type*) [Finite Î¹] [âˆ€ i : Î¹, AddCommMonoid (M i)] [âˆ€ i : Î¹, Module R (M i)]
    [âˆ€ i : Î¹, Module.Free R (M i)] : Module.Free R (âˆ€ i, M i) :=
  let âŸ¨_âŸ© := nonempty_fintype Î¹
  of_basis <| Pi.basis fun i => chooseBasis R (M i)

/-- The module of finite matrices is free. -/
instance matrix {m n : Type*} [Finite m] [Finite n] : Module.Free R (Matrix m n M) :=
  Module.Free.pi R _

instance ulift [Free R M] : Free R (ULift M) := of_equiv ULift.moduleEquiv.symm

variable (Î¹)

/-- The product of finitely many free modules is free (non-dependent version to help with typeclass
search). -/
instance function [Finite Î¹] : Module.Free R (Î¹ â†’ M) :=
  Free.pi _ _

instance finsupp : Module.Free R (Î¹ â†’â‚€ M) :=
  of_basis (Finsupp.basis fun _ => chooseBasis R M)

variable {Î¹}

instance (priority := 100) of_subsingleton [Subsingleton N] : Module.Free R N :=
  of_basis.{u,z,z} (Basis.empty N : Basis PEmpty R N)

instance (priority := 100) of_subsingleton' [Subsingleton R] : Module.Free R N :=
  letI := Module.subsingleton R N
  Module.Free.of_subsingleton R N

instance dfinsupp {Î¹ : Type*} (M : Î¹ â†’ Type*) [âˆ€ i : Î¹, AddCommMonoid (M i)]
    [âˆ€ i : Î¹, Module R (M i)] [âˆ€ i : Î¹, Module.Free R (M i)] : Module.Free R (Î â‚€ i, M i) :=
  of_basis <| DFinsupp.basis fun i => chooseBasis R (M i)

instance directSum {Î¹ : Type*} (M : Î¹ â†’ Type*) [âˆ€ i : Î¹, AddCommMonoid (M i)]
    [âˆ€ i : Î¹, Module R (M i)] [âˆ€ i : Î¹, Module.Free R (M i)] : Module.Free R (â¨ i, M i) :=
  Module.Free.dfinsupp R M

end Semiring

section CommSemiring

variable {S} [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M]
  [Module S M] [IsScalarTower R S M] [Module.Free S M]
  [AddCommMonoid N] [Module R N] [Module.Free R N]

instance tensor : Module.Free S (M âŠ—[R] N) :=
  let âŸ¨bMâŸ© := exists_basis (R := S) (M := M)
  let âŸ¨bNâŸ© := exists_basis (R := R) (M := N)
  of_basis (bM.2.tensorProduct bN.2)

end CommSemiring

section

variable [CommSemiring R]
variable [AddCommMonoid M] [Module R M]
variable [AddCommMonoid N] [Module R N]

variable (x : M âŠ—[R] N)

lemma TensorProduct.sum_tmul_basis_right_eq_zero [Free R M]
    (ð’ž : Basis Î¹ R N) (b : Î¹ â†’â‚€ M) (h : (b.sum fun i m => m âŠ—â‚œ[R] ð’ž i) = 0) :
    b = 0 := by
  let â„¬ := chooseBasis R M
  classical
  let ð’¯ := Basis.tensorProduct â„¬ ð’ž
  let I := b.support.biUnion fun i => (â„¬.repr (b i)).support
  have eq1 := calc (0 : M âŠ—[R] N)
      _ = âˆ‘ i âˆˆ b.support, b i âŠ—â‚œ[R] ð’ž i := h.symm
      _ = âˆ‘ i âˆˆ b.support, (âˆ‘ k âˆˆ (â„¬.repr (b i)).support, (â„¬.repr (b i)) k â€¢ â„¬ k) âŠ—â‚œ[R] ð’ž i := by
          refine Finset.sum_congr rfl fun z _ => ?_
          congr
          exact â„¬.linearCombination_repr (b z) |>.symm
      _ = âˆ‘ i âˆˆ b.support, âˆ‘ k âˆˆ (â„¬.repr (b i)).support, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[R] ð’ž i) := by
          refine Finset.sum_congr rfl fun z _ => ?_
          rw [TensorProduct.sum_tmul]
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul']
      _ = âˆ‘ i âˆˆ b.support, âˆ‘ k âˆˆ I, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[R] ð’ž i) := by
          congr! with j h
          apply Finset.sum_subset
          Â· intro i hi
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_biUnion, I] at h hi âŠ¢
            exact âŸ¨_, h, hiâŸ©
          Â· intro i hi1 hi2
            simp only [Finsupp.mem_support_iff, ne_eq, Decidable.not_not, smul_eq_zero]
              at hi1 hi2 âŠ¢
            simp only [hi2, zero_smul]
      _ = âˆ‘ k âˆˆ I, âˆ‘ i âˆˆ b.support, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[R] ð’ž i) := Finset.sum_comm
      _ = âˆ‘ ij âˆˆ I Ã—Ë¢ b.support, (â„¬.repr (b ij.2)) ij.1 â€¢ (â„¬ ij.1 âŠ—â‚œ[R] ð’ž ij.2) := by
          rw [Finset.sum_product]
      _ = âˆ‘ ij âˆˆ I Ã—Ë¢ b.support, (â„¬.repr (b ij.2)) ij.1 â€¢ ð’¯ ij := by
          refine Finset.sum_congr rfl fun ij _ => ?_
          rw [Basis.tensorProduct_apply]
  have LI := ð’¯.linearIndependent
  rw [linearIndependent_iff'] at LI
  specialize LI (I Ã—Ë¢ b.support) _ eq1.symm
  ext i
  by_cases hi : i âˆˆ b.support
  swap
  Â· simp only [Finsupp.mem_support_iff, ne_eq, Decidable.not_not] at hi; exact hi
  rw [â† â„¬.linearCombination_repr (b i)]
  change âˆ‘ _ âˆˆ _, _ = 0
  simp only [LinearMap.coe_smulRight, LinearMap.id_coe, id_eq]
  refine Finset.sum_eq_zero fun j hj => ?_
  specialize LI âŸ¨j, iâŸ© (by
    simp only [Finset.mem_product, Finset.mem_biUnion, Finsupp.mem_support_iff, ne_eq, I] at hi hj âŠ¢
    refine âŸ¨âŸ¨_, hi, hjâŸ©, hiâŸ©)
  simp [LI]

lemma TensorProduct.sum_tmul_basis_left_eq_zero [Free R N]
    (â„¬ : Basis Î¹ R M) (c : Î¹ â†’â‚€ N) (h : (c.sum fun i n => â„¬ i âŠ—â‚œ[R] n) = 0) :
    c = 0 := by
  refine TensorProduct.sum_tmul_basis_right_eq_zero R N M â„¬ c ?_
  apply_fun TensorProduct.comm R M N at h
  simp only [Finsupp.sum, map_sum, comm_tmul, map_zero] at h
  exact h

end

end Module.Free
