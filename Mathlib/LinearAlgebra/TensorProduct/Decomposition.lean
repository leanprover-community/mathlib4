/-
Copyright (c) 2025 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau
-/
import Mathlib.Algebra.DirectSum.Decomposition
import Mathlib.LinearAlgebra.DirectSum.TensorProduct

/-! # Decomposition of tensor product

In this file we show that if `ğ’œ` is a graded `R`-algebra, and `S` is any `R`-algebra, then
`S âŠ—[R] ğ’œ` is a graded `S`-algebra with the same grading.
-/

open TensorProduct LinearMap

namespace DirectSum

variable {Î¹ R M S : Type*} [DecidableEq Î¹]
  [CommSemiring R] [AddCommMonoid M] [Module R M]
  (â„³ : Î¹ â†’ Submodule R M) [Decomposition â„³]
  [CommSemiring S] [Algebra R S]

instance Decomposition.baseChange : Decomposition fun i â†¦ (â„³ i).baseChange S := by
  refine .ofLinearMap _ (lmap (â„³ Â·|>.toBaseChange S) âˆ˜â‚—
    (directSumRight R S S fun i â†¦ â„³ i).toLinearMap âˆ˜â‚—
    ((decomposeLinearEquiv â„³).baseChange R S)) ?_ ?_
  Â· simp_rw [â† comp_assoc]
    rw [â† LinearEquiv.eq_comp_toLinearMap_symm]
    ext
    simp
  Â· ext : 1
    rw [â† LinearMap.cancel_right ((â„³ _).toBaseChange_surjective S)]
    ext : 3
    simp

theorem toBaseChange_injective (i : Î¹) : Function.Injective ((â„³ i).toBaseChange S) := fun x y h â†¦ by
  have := (Function.Bijective.of_comp_iff (lmap (â„³ Â·|>.toBaseChange S))
    (by rw [â† LinearEquiv.coe_trans]; exact LinearEquiv.bijective _)).1
    (decompose (M := S âŠ—[R] M) fun i â†¦ (â„³ i).baseChange S).bijective
  refine of_injective (Î² := fun i â†¦ S âŠ—[R] â„³ i) i <| this.injective ?_
  simpa using congr(of (fun i â†¦ (â„³ i).baseChange S) i $h)

theorem toBaseChange_bijective (i : Î¹) : Function.Bijective ((â„³ i).toBaseChange S) :=
  âŸ¨toBaseChange_injective â„³ i, (â„³ i).toBaseChange_surjective SâŸ©

namespace IsInternal

omit [Decomposition â„³]

theorem baseChange (hm : IsInternal â„³) : IsInternal fun i â†¦ (â„³ i).baseChange S :=
  haveI := hm.chooseDecomposition
  Decomposition.isInternal _

nonrec theorem toBaseChange_bijective (hm : IsInternal â„³) (i : Î¹) :
    Function.Bijective ((â„³ i).toBaseChange S) :=
  haveI := hm.chooseDecomposition
  toBaseChange_bijective â„³ i

theorem toBaseChange_injective (hm : IsInternal â„³) (i : Î¹) :
    Function.Injective ((â„³ i).toBaseChange S) :=
  (toBaseChange_bijective â„³ hm i).injective

end IsInternal

end DirectSum
