/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
module

public import Mathlib.LinearAlgebra.Basis.Basic
public import Mathlib.LinearAlgebra.DirectSum.Finsupp
public import Mathlib.LinearAlgebra.Finsupp.VectorSpace
public import Mathlib.LinearAlgebra.FreeModule.Basic

/-!
# Bases and dimensionality of tensor products of modules

This file defines various bases on the tensor product of modules,
and shows that the tensor product of free modules is again free.
-/

@[expose] public section


noncomputable section

open LinearMap Module Set Submodule

open scoped TensorProduct

section CommSemiring

variable {R : Type*} {S : Type*} {M : Type*} {N : Type*} {Î¹ : Type*} {Îº : Type*}
  [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M] [Module S M]
  [IsScalarTower R S M] [AddCommMonoid N] [Module R N]

namespace Module.Basis

/-- If `b : Î¹ â†’ M` and `c : Îº â†’ N` are bases then so is `fun i â†¦ b i.1 âŠ—â‚œ c i.2 : Î¹ Ã— Îº â†’ M âŠ— N`. -/
def tensorProduct (b : Basis Î¹ S M) (c : Basis Îº R N) :
    Basis (Î¹ Ã— Îº) S (M âŠ—[R] N) :=
  Finsupp.basisSingleOne.map
    ((TensorProduct.AlgebraTensorModule.congr b.repr c.repr).trans <|
        (finsuppTensorFinsupp R S _ _ _ _).trans <|
          Finsupp.lcongr (Equiv.refl _) (TensorProduct.AlgebraTensorModule.rid R S S)).symm

@[simp]
theorem tensorProduct_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹) (j : Îº) :
    tensorProduct b c (i, j) = b i âŠ—â‚œ c j := by
  simp [tensorProduct]

theorem tensorProduct_apply' (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹ Ã— Îº) :
    tensorProduct b c i = b i.1 âŠ—â‚œ c i.2 := by
  simp [tensorProduct]

@[simp]
theorem tensorProduct_repr_tmul_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (m : M) (n : N)
    (i : Î¹) (j : Îº) :
    (tensorProduct b c).repr (m âŠ—â‚œ n) (i, j) = c.repr n j â€¢ b.repr m i := by
  simp [tensorProduct]

variable (S : Type*) [Semiring S] [Algebra R S]

/-- The lift of an `R`-basis of `M` to an `S`-basis of the base change `S âŠ—[R] M`. -/
noncomputable
def baseChange (b : Basis Î¹ R M) : Basis Î¹ S (S âŠ—[R] M) :=
  (tensorProduct (.singleton Unit S) b).reindex (Equiv.punitProd Î¹)

@[simp]
lemma baseChange_repr_tmul (b : Basis Î¹ R M) (x y i) :
    (b.baseChange S).repr (x âŠ—â‚œ y) i = b.repr y i â€¢ x := by
  simp [baseChange, tensorProduct]

@[simp]
lemma baseChange_apply (b : Basis Î¹ R M) (i) :
    b.baseChange S i = 1 âŠ—â‚œ b i := by
  simp [baseChange, tensorProduct]

end Module.Basis

section

variable [DecidableEq Î¹] [DecidableEq Îº]
variable (â„¬ : Basis Î¹ R M) (ğ’ : Basis Îº R N) (x : M âŠ—[R] N)

/--
If `{ğ’áµ¢}` is a basis for the module `N`, then every elements of `x âˆˆ M âŠ— N` can be uniquely written
as `âˆ‘áµ¢ máµ¢ âŠ— ğ’áµ¢` for some `máµ¢ âˆˆ M`.
-/
def TensorProduct.equivFinsuppOfBasisRight : M âŠ—[R] N â‰ƒâ‚—[R] Îº â†’â‚€ M :=
  LinearEquiv.lTensor M ğ’.repr â‰ªâ‰«â‚— TensorProduct.finsuppScalarRight R M Îº

@[simp]
lemma TensorProduct.equivFinsuppOfBasisRight_apply_tmul (m : M) (n : N) :
    (TensorProduct.equivFinsuppOfBasisRight ğ’) (m âŠ—â‚œ n) =
    (ğ’.repr n).mapRange (Â· â€¢ m) (zero_smul _ _) := by
  ext; simp [equivFinsuppOfBasisRight]

lemma TensorProduct.equivFinsuppOfBasisRight_apply_tmul_apply
    (m : M) (n : N) (i : Îº) :
    (TensorProduct.equivFinsuppOfBasisRight ğ’) (m âŠ—â‚œ n) i =
    ğ’.repr n i â€¢ m := by
  simp only [equivFinsuppOfBasisRight_apply_tmul, Finsupp.mapRange_apply]

lemma TensorProduct.equivFinsuppOfBasisRight_symm :
    (TensorProduct.equivFinsuppOfBasisRight ğ’).symm.toLinearMap =
    Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N).flip (ğ’ i) := by
  ext; simp [equivFinsuppOfBasisRight]

@[simp]
lemma TensorProduct.equivFinsuppOfBasisRight_symm_apply (b : Îº â†’â‚€ M) :
    (TensorProduct.equivFinsuppOfBasisRight ğ’).symm b = b.sum fun i m â†¦ m âŠ—â‚œ ğ’ i :=
  congr($(TensorProduct.equivFinsuppOfBasisRight_symm ğ’) b)

omit [DecidableEq Îº] in
lemma TensorProduct.sum_tmul_basis_right_injective :
    Function.Injective (Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N).flip (ğ’ i)) :=
  have := Classical.decEq Îº
  (equivFinsuppOfBasisRight_symm (M := M) ğ’).symm â–¸
    (TensorProduct.equivFinsuppOfBasisRight ğ’).symm.injective

omit [DecidableEq Îº] in
lemma TensorProduct.sum_tmul_basis_right_eq_zero
    (b : Îº â†’â‚€ M) (h : (b.sum fun i m â†¦ m âŠ—â‚œ[R] ğ’ i) = 0) : b = 0 :=
  have := Classical.decEq Îº
  (TensorProduct.equivFinsuppOfBasisRight ğ’).symm.injective (aâ‚‚ := 0) <| by simpa

/--
If `{â„¬áµ¢}` is a basis for the module `M`, then every elements of `x âˆˆ M âŠ— N` can be uniquely written
as `âˆ‘áµ¢ â„¬áµ¢ âŠ— náµ¢` for some `náµ¢ âˆˆ N`.
-/
def TensorProduct.equivFinsuppOfBasisLeft : M âŠ—[R] N â‰ƒâ‚—[R] Î¹ â†’â‚€ N :=
  TensorProduct.comm R M N â‰ªâ‰«â‚— TensorProduct.equivFinsuppOfBasisRight â„¬

@[simp]
lemma TensorProduct.equivFinsuppOfBasisLeft_apply_tmul (m : M) (n : N) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬) (m âŠ—â‚œ n) =
    (â„¬.repr m).mapRange (Â· â€¢ n) (zero_smul _ _) := by
  simp [equivFinsuppOfBasisLeft]

lemma TensorProduct.equivFinsuppOfBasisLeft_apply_tmul_apply
    (m : M) (n : N) (i : Î¹) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬) (m âŠ—â‚œ n) i =
    â„¬.repr m i â€¢ n := by
  simp only [equivFinsuppOfBasisLeft_apply_tmul, Finsupp.mapRange_apply]

lemma TensorProduct.equivFinsuppOfBasisLeft_symm :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.toLinearMap =
    Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N) (â„¬ i) := by
  ext; simp [equivFinsuppOfBasisLeft]

@[simp]
lemma TensorProduct.equivFinsuppOfBasisLeft_symm_apply (b : Î¹ â†’â‚€ N) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm b = b.sum fun i n â†¦ â„¬ i âŠ—â‚œ n :=
  congr($(TensorProduct.equivFinsuppOfBasisLeft_symm â„¬) b)

omit [DecidableEq Îº] in
/-- Elements in `M âŠ— N` can be represented by sum of elements in `M` tensor elements of basis of
`N`. -/
lemma TensorProduct.eq_repr_basis_right :
    âˆƒ b : Îº â†’â‚€ M, b.sum (fun i m â†¦ m âŠ—â‚œ ğ’ i) = x := by
  classical simpa using (TensorProduct.equivFinsuppOfBasisRight ğ’).symm.surjective x

omit [DecidableEq Î¹] in
/-- Elements in `M âŠ— N` can be represented by sum of elements of basis of `M` tensor elements of
  `N`. -/
lemma TensorProduct.eq_repr_basis_left :
    âˆƒ (c : Î¹ â†’â‚€ N), (c.sum fun i n â†¦ â„¬ i âŠ—â‚œ n) = x := by
  classical obtain âŸ¨c, rflâŸ© := (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.surjective x
  exact âŸ¨c, (TensorProduct.comm R M N).injective <| by simp [Finsupp.sum]âŸ©

omit [DecidableEq Î¹] in
/-- Given a finite basis `â„¬` for `M`, any tensor `x âˆˆ M âŠ— N` decomposes as `âˆ‘áµ¢ â„¬áµ¢ âŠ—â‚œ náµ¢`
where the `N`-component `náµ¢` is obtained by applying `â„¬.dualBasis i âŠ— id` to `x` and then
identifying `R âŠ— N â‰ƒ N` via `lid`. -/
lemma TensorProduct.eq_sum_basis_tmul_dualBasis_apply [Fintype Î¹] :
    x = âˆ‘ i, â„¬ i âŠ—â‚œ[R] (TensorProduct.lid R N)
      ((TensorProduct.map (â„¬.coord i) LinearMap.id) x) := by
  induction x using TensorProduct.induction_on with
  | zero => simp
  | tmul u v =>
    conv_lhs => rw [â† â„¬.sum_repr u, TensorProduct.sum_tmul]
    apply Finset.sum_congr rfl
    intro i _
    simp [TensorProduct.map_tmul, LinearMap.id_coe, id_eq, TensorProduct.lid_tmul,
      Basis.coord_apply, TensorProduct.tmul_smul, TensorProduct.smul_tmul]
  | add w1 w2 hw1 hw2 =>
    simp only [map_add, TensorProduct.tmul_add, Finset.sum_add_distrib]
    rw [â† hw1, â† hw2]

omit [DecidableEq Îº] in
/-- Given a finite basis `ğ’` for `N`, any tensor `x âˆˆ M âŠ— N` decomposes as `âˆ‘â±¼ mâ±¼ âŠ—â‚œ ğ’â±¼`
where the `M`-component `mâ±¼` is obtained by applying `id âŠ— ğ’.dualBasis j` to `x` and then
identifying `M âŠ— R â‰ƒ M` via `rid`. -/
lemma TensorProduct.eq_sum_dualBasis_apply_tmul_basis [Fintype Îº] :
    x = âˆ‘ j, (TensorProduct.rid R M)
          ((TensorProduct.map LinearMap.id (ğ’.coord j)) x) âŠ—â‚œ[R] ğ’ j := by
  apply (TensorProduct.comm R M N).injective
  rw [eq_sum_basis_tmul_dualBasis_apply ğ’ (TensorProduct.comm R M N x)]
  simp only [map_sum, comm_tmul, map_comm, â† comm_trans_lid, LinearEquiv.trans_apply]

omit [DecidableEq Î¹] in
lemma TensorProduct.sum_tmul_basis_left_injective :
    Function.Injective (Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N) (â„¬ i)) :=
  have := Classical.decEq Î¹
  (equivFinsuppOfBasisLeft_symm (N := N) â„¬).symm â–¸
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.injective

omit [DecidableEq Î¹] in
lemma TensorProduct.sum_tmul_basis_left_eq_zero
    (b : Î¹ â†’â‚€ N) (h : (b.sum fun i n â†¦ â„¬ i âŠ—â‚œ[R] n) = 0) : b = 0 :=
  have := Classical.decEq Î¹
  (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.injective (aâ‚‚ := 0) <| by simpa

end

variable [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M]
  [Module S M] [IsScalarTower R S M] [Module.Free S M]
  [AddCommMonoid N] [Module R N] [Module.Free R N]
instance Module.Free.tensor : Module.Free S (M âŠ—[R] N) :=
  let âŸ¨bMâŸ© := exists_basis (R := S) (M := M)
  let âŸ¨bNâŸ© := exists_basis (R := R) (M := N)
  of_basis (bM.2.tensorProduct bN.2)

end CommSemiring

end
