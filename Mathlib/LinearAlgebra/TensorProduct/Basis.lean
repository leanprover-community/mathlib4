/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.LinearAlgebra.DirectSum.Finsupp
import Mathlib.LinearAlgebra.FinsuppVectorSpace

/-!
# Bases and dimensionality of tensor products of modules

These can not go into `LinearAlgebra.TensorProduct` since they depend on
`LinearAlgebra.FinsuppVectorSpace` which in turn imports `LinearAlgebra.TensorProduct`.
-/


noncomputable section

open Set LinearMap Submodule

open scoped TensorProduct

section CommSemiring

variable {R : Type*} {S : Type*} {M : Type*} {N : Type*} {Î¹ : Type*} {Îº : Type*}
  [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M] [Module S M]
  [IsScalarTower R S M] [AddCommMonoid N] [Module R N]

/-- If `b : Î¹ â†’ M` and `c : Îº â†’ N` are bases then so is `fun i â†¦ b i.1 âŠ—â‚œ c i.2 : Î¹ Ã— Îº â†’ M âŠ— N`. -/
def Basis.tensorProduct (b : Basis Î¹ S M) (c : Basis Îº R N) :
    Basis (Î¹ Ã— Îº) S (M âŠ—[R] N) :=
  Finsupp.basisSingleOne.map
    ((TensorProduct.AlgebraTensorModule.congr b.repr c.repr).trans <|
        (finsuppTensorFinsupp R S _ _ _ _).trans <|
          Finsupp.lcongr (Equiv.refl _) (TensorProduct.AlgebraTensorModule.rid R S S)).symm

@[simp]
theorem Basis.tensorProduct_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹) (j : Îº) :
    Basis.tensorProduct b c (i, j) = b i âŠ—â‚œ c j := by
  simp [Basis.tensorProduct]

theorem Basis.tensorProduct_apply' (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹ Ã— Îº) :
    Basis.tensorProduct b c i = b i.1 âŠ—â‚œ c i.2 := by
  simp [Basis.tensorProduct]

@[simp]
theorem Basis.tensorProduct_repr_tmul_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (m : M) (n : N)
    (i : Î¹) (j : Îº) :
    (Basis.tensorProduct b c).repr (m âŠ—â‚œ n) (i, j) = c.repr n j â€¢ b.repr m i := by
  simp [Basis.tensorProduct, mul_comm]

variable (S : Type*) [Semiring S] [Algebra R S]

/-- The lift of an `R`-basis of `M` to an `S`-basis of the base change `S âŠ—[R] M`. -/
noncomputable
def Basis.baseChange (b : Basis Î¹ R M) : Basis Î¹ S (S âŠ—[R] M) :=
  ((Basis.singleton Unit S).tensorProduct b).reindex (Equiv.punitProd Î¹)

@[simp]
lemma Basis.baseChange_repr_tmul (b : Basis Î¹ R M) (x y i) :
    (b.baseChange S).repr (x âŠ—â‚œ y) i = b.repr y i â€¢ x := by
  simp [Basis.baseChange, Basis.tensorProduct]

@[simp]
lemma Basis.baseChange_apply (b : Basis Î¹ R M) (i) :
    b.baseChange S i = 1 âŠ—â‚œ b i := by
  simp [Basis.baseChange, Basis.tensorProduct]

section

variable (â„¬ : Basis Î¹ R M) (ð’ž : Basis Îº R N) (x : M âŠ—[R] N)

/-- Elements in `M âŠ— N` can be represented by sum of elements in `M` tensor elements of basis of
`N`. -/
lemma TensorProduct.eq_repr_basis_right :
    âˆƒ b : Îº â†’â‚€ M, b.sum (fun i m â†¦ m âŠ—â‚œ ð’ž i) = x := by
  classical
  induction x using TensorProduct.induction_on with
  | zero => exact âŸ¨0, by simpâŸ©
  | tmul m n =>
    use (ð’ž.repr n).mapRange (Â· â€¢ m) (by simp)
    simp only [Finsupp.mapRange, zero_tmul, implies_true, Finsupp.onFinset_sum, Function.comp_apply,
      smul_tmul]
    rw [â† tmul_sum]
    congr
    conv_rhs => rw [â† Basis.linearCombination_repr ð’ž n]
    rfl
  | add x y hx hy =>
    rcases hx with âŸ¨x, rflâŸ©
    rcases hy with âŸ¨y, rflâŸ©
    exact âŸ¨x + y, Finsupp.sum_add_index (by simp) fun _ _ => by simp [add_tmul]âŸ©

/-- Elements in `M âŠ— N` can be represented by sum of elements of basis of `M` tensor elements of
  `N`.-/
lemma TensorProduct.eq_repr_basis_left :
    âˆƒ (c : Î¹ â†’â‚€ N), (c.sum fun i n â†¦ â„¬ i âŠ—â‚œ n) = x := by
  obtain âŸ¨c, hcâŸ© := TensorProduct.eq_repr_basis_right â„¬ (TensorProduct.comm R M N x)
  exact âŸ¨c, (TensorProduct.comm R M N).injective <| by simp [Finsupp.sum, â† hc]âŸ©

lemma TensorProduct.sum_tmul_basis_left_injective :
    Function.Injective <| Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N) (â„¬ i) := by
  classical
  intro b b' h
  refine (TensorProduct.congr â„¬.repr (LinearEquiv.refl R N) â‰ªâ‰«â‚—
    (finsuppScalarLeft R N Î¹)).symm.injective ?_
  rw [â† b.sum_single, â† b'.sum_single, Finsupp.sum, map_sum, Finsupp.sum, map_sum]
  simpa

lemma TensorProduct.sum_tmul_basis_left_eq_zero
    (b : Î¹ â†’â‚€ N) (h : (b.sum fun i n â†¦ â„¬ i âŠ—â‚œ[R] n) = 0) : b = 0 :=
  TensorProduct.sum_tmul_basis_left_injective â„¬ (aâ‚ := b) (aâ‚‚ := 0) (h.trans (by simp))

lemma TensorProduct.sum_tmul_basis_right_injective :
    Function.Injective <| Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N).flip (ð’ž i):= by
  classical
  intro b b' h
  refine (TensorProduct.congr (LinearEquiv.refl R M) ð’ž.repr â‰ªâ‰«â‚—
    (finsuppScalarRight R M Îº)).symm.injective ?_
  rw [â† b.sum_single, â† b'.sum_single, Finsupp.sum, map_sum, Finsupp.sum, map_sum]
  simpa

lemma TensorProduct.sum_tmul_basis_right_eq_zero
    (b : Îº â†’â‚€ M) (h : (b.sum fun i m â†¦ m âŠ—â‚œ[R] ð’ž i) = 0) : b = 0 :=
  TensorProduct.sum_tmul_basis_right_injective ð’ž (aâ‚ := b) (aâ‚‚ := 0) (h.trans (by simp))


end

end CommSemiring

end
