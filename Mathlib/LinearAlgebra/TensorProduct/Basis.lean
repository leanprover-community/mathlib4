/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.LinearAlgebra.Basis.Basic
import Mathlib.LinearAlgebra.DirectSum.Finsupp
import Mathlib.LinearAlgebra.Finsupp.VectorSpace
import Mathlib.LinearAlgebra.FreeModule.Basic

/-!
# Bases and dimensionality of tensor products of modules

This file defines various bases on the tensor product of modules,
and shows that the tensor product of free modules is again free.
-/


noncomputable section

open Set LinearMap Submodule

open scoped TensorProduct

section CommSemiring

variable {R : Type*} {S : Type*} {M : Type*} {N : Type*} {Î¹ : Type*} {Îº : Type*}
  [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M] [Module S M]
  [IsScalarTower R S M] [AddCommMonoid N] [Module R N]

/-- If `b : Î¹ â†’ M` and `c : Îº â†’ N` are bases then so is `fun i â†¦ b i.1 âŠ—â‚œ c i.2 : Î¹ Ã— Îº â†’ M âŠ— N`. -/
def Basis.tensorProduct (b : Basis Î¹ S M) (c : Basis Îº R N) :
    Basis (Î¹ Ã— Îº) S (M âŠ—[R] N) :=
  Finsupp.basisSingleOne.map
    ((TensorProduct.AlgebraTensorModule.congr b.repr c.repr).trans <|
        (finsuppTensorFinsupp R S _ _ _ _).trans <|
          Finsupp.lcongr (Equiv.refl _) (TensorProduct.AlgebraTensorModule.rid R S S)).symm

@[simp]
theorem Basis.tensorProduct_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹) (j : Îº) :
    Basis.tensorProduct b c (i, j) = b i âŠ—â‚œ c j := by
  simp [Basis.tensorProduct]

theorem Basis.tensorProduct_apply' (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹ Ã— Îº) :
    Basis.tensorProduct b c i = b i.1 âŠ—â‚œ c i.2 := by
  simp [Basis.tensorProduct]

@[simp]
theorem Basis.tensorProduct_repr_tmul_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (m : M) (n : N)
    (i : Î¹) (j : Îº) :
    (Basis.tensorProduct b c).repr (m âŠ—â‚œ n) (i, j) = c.repr n j â€¢ b.repr m i := by
  simp [Basis.tensorProduct, mul_comm]

variable (S : Type*) [Semiring S] [Algebra R S]

/-- The lift of an `R`-basis of `M` to an `S`-basis of the base change `S âŠ—[R] M`. -/
noncomputable
def Basis.baseChange (b : Basis Î¹ R M) : Basis Î¹ S (S âŠ—[R] M) :=
  ((Basis.singleton Unit S).tensorProduct b).reindex (Equiv.punitProd Î¹)

@[simp]
lemma Basis.baseChange_repr_tmul (b : Basis Î¹ R M) (x y i) :
    (b.baseChange S).repr (x âŠ—â‚œ y) i = b.repr y i â€¢ x := by
  simp [Basis.baseChange, Basis.tensorProduct]

@[simp]
lemma Basis.baseChange_apply (b : Basis Î¹ R M) (i) :
    b.baseChange S i = 1 âŠ—â‚œ b i := by
  simp [Basis.baseChange, Basis.tensorProduct]

section

variable [DecidableEq Î¹] [DecidableEq Îº]
variable (â„¬ : Basis Î¹ R M) (ð’ž : Basis Îº R N) (x : M âŠ—[R] N)

/--
If `{ð’žáµ¢}` is a basis for the module `N`, then every elements of `x âˆˆ M âŠ— N` can be uniquely written
as `âˆ‘áµ¢ máµ¢ âŠ— ð’žáµ¢` for some `máµ¢ âˆˆ M`.
-/
def TensorProduct.equivFinsuppOfBasisRight : M âŠ—[R] N â‰ƒâ‚—[R] Îº â†’â‚€ M :=
  LinearEquiv.lTensor M ð’ž.repr â‰ªâ‰«â‚— TensorProduct.finsuppScalarRight R M Îº

@[simp]
lemma TensorProduct.equivFinsuppOfBasisRight_apply_tmul (m : M) (n : N) :
    (TensorProduct.equivFinsuppOfBasisRight ð’ž) (m âŠ—â‚œ n) =
    (ð’ž.repr n).mapRange (Â· â€¢ m) (zero_smul _ _) := by
  ext; simp [equivFinsuppOfBasisRight]

lemma TensorProduct.equivFinsuppOfBasisRight_apply_tmul_apply
    (m : M) (n : N) (i : Îº) :
    (TensorProduct.equivFinsuppOfBasisRight ð’ž) (m âŠ—â‚œ n) i =
    ð’ž.repr n i â€¢ m := by
  simp only [equivFinsuppOfBasisRight_apply_tmul, Finsupp.mapRange_apply]

lemma TensorProduct.equivFinsuppOfBasisRight_symm :
    (TensorProduct.equivFinsuppOfBasisRight ð’ž).symm.toLinearMap =
    Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N).flip (ð’ž i) := by
  ext; simp [equivFinsuppOfBasisRight]

@[simp]
lemma TensorProduct.equivFinsuppOfBasisRight_symm_apply (b : Îº â†’â‚€ M) :
    (TensorProduct.equivFinsuppOfBasisRight ð’ž).symm b = b.sum fun i m â†¦ m âŠ—â‚œ ð’ž i :=
  congr($(TensorProduct.equivFinsuppOfBasisRight_symm ð’ž) b)

omit [DecidableEq Îº] in
lemma TensorProduct.sum_tmul_basis_right_injective :
    Function.Injective (Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N).flip (ð’ž i)) :=
  have := Classical.decEq Îº
  (equivFinsuppOfBasisRight_symm (M := M) ð’ž).symm â–¸
    (TensorProduct.equivFinsuppOfBasisRight ð’ž).symm.injective

omit [DecidableEq Îº] in
lemma TensorProduct.sum_tmul_basis_right_eq_zero
    (b : Îº â†’â‚€ M) (h : (b.sum fun i m â†¦ m âŠ—â‚œ[R] ð’ž i) = 0) : b = 0 :=
  have := Classical.decEq Îº
  (TensorProduct.equivFinsuppOfBasisRight ð’ž).symm.injective (aâ‚‚ := 0) <| by simpa

/--
If `{â„¬áµ¢}` is a basis for the module `M`, then every elements of `x âˆˆ M âŠ— N` can be uniquely written
as `âˆ‘áµ¢ â„¬áµ¢ âŠ— náµ¢` for some `náµ¢ âˆˆ N`.
-/
def TensorProduct.equivFinsuppOfBasisLeft : M âŠ—[R] N â‰ƒâ‚—[R] Î¹ â†’â‚€ N :=
  TensorProduct.comm R M N â‰ªâ‰«â‚— TensorProduct.equivFinsuppOfBasisRight â„¬

@[simp]
lemma TensorProduct.equivFinsuppOfBasisLeft_apply_tmul (m : M) (n : N) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬) (m âŠ—â‚œ n) =
    (â„¬.repr m).mapRange (Â· â€¢ n) (zero_smul _ _) := by
  ext; simp [equivFinsuppOfBasisLeft]

lemma TensorProduct.equivFinsuppOfBasisLeft_apply_tmul_apply
    (m : M) (n : N) (i : Î¹) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬) (m âŠ—â‚œ n) i =
    â„¬.repr m i â€¢ n := by
  simp only [equivFinsuppOfBasisLeft_apply_tmul, Finsupp.mapRange_apply]

lemma TensorProduct.equivFinsuppOfBasisLeft_symm :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.toLinearMap =
    Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N) (â„¬ i) := by
  ext; simp [equivFinsuppOfBasisLeft]

@[simp]
lemma TensorProduct.equivFinsuppOfBasisLeft_symm_apply (b : Î¹ â†’â‚€ N) :
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm b = b.sum fun i n â†¦ â„¬ i âŠ—â‚œ n :=
  congr($(TensorProduct.equivFinsuppOfBasisLeft_symm â„¬) b)

omit [DecidableEq Îº] in
/-- Elements in `M âŠ— N` can be represented by sum of elements in `M` tensor elements of basis of
`N`. -/
lemma TensorProduct.eq_repr_basis_right :
    âˆƒ b : Îº â†’â‚€ M, b.sum (fun i m â†¦ m âŠ—â‚œ ð’ž i) = x := by
  classical simpa using (TensorProduct.equivFinsuppOfBasisRight ð’ž).symm.surjective x

omit [DecidableEq Î¹] in
/-- Elements in `M âŠ— N` can be represented by sum of elements of basis of `M` tensor elements of
  `N`. -/
lemma TensorProduct.eq_repr_basis_left :
    âˆƒ (c : Î¹ â†’â‚€ N), (c.sum fun i n â†¦ â„¬ i âŠ—â‚œ n) = x := by
  classical obtain âŸ¨c, rflâŸ© := (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.surjective x
  exact âŸ¨c, (TensorProduct.comm R M N).injective <| by simp [Finsupp.sum]âŸ©

omit [DecidableEq Î¹] in
lemma TensorProduct.sum_tmul_basis_left_injective :
    Function.Injective (Finsupp.lsum R fun i â†¦ (TensorProduct.mk R M N) (â„¬ i)) :=
  have := Classical.decEq Î¹
  (equivFinsuppOfBasisLeft_symm (N := N) â„¬).symm â–¸
    (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.injective

omit [DecidableEq Î¹] in
lemma TensorProduct.sum_tmul_basis_left_eq_zero
    (b : Î¹ â†’â‚€ N) (h : (b.sum fun i n â†¦ â„¬ i âŠ—â‚œ[R] n) = 0) : b = 0 :=
  have := Classical.decEq Î¹
  (TensorProduct.equivFinsuppOfBasisLeft â„¬).symm.injective (aâ‚‚ := 0) <| by simpa

end

variable {S} [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M]
  [Module S M] [IsScalarTower R S M] [Module.Free S M]
  [AddCommMonoid N] [Module R N] [Module.Free R N]
instance Module.Free.tensor : Module.Free S (M âŠ—[R] N) :=
  let âŸ¨bMâŸ© := exists_basis (R := S) (M := M)
  let âŸ¨bNâŸ© := exists_basis (R := R) (M := N)
  of_basis (bM.2.tensorProduct bN.2)

end CommSemiring

end
