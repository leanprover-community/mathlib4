/-
Copyright (c) 2021 Jakob von Raumer. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob von Raumer
-/
import Mathlib.LinearAlgebra.DirectSum.Finsupp
import Mathlib.LinearAlgebra.FinsuppVectorSpace
import Mathlib.LinearAlgebra.Basis.Basic

/-!
# Bases and dimensionality of tensor products of modules

These can not go into `LinearAlgebra.TensorProduct` since they depend on
`LinearAlgebra.FinsuppVectorSpace` which in turn imports `LinearAlgebra.TensorProduct`.
-/


noncomputable section

open Set LinearMap Submodule

open scoped TensorProduct

section CommSemiring

variable {R : Type*} {S : Type*} {M : Type*} {N : Type*} {Î¹ : Type*} {Îº : Type*}
  [CommSemiring R] [Semiring S] [Algebra R S] [AddCommMonoid M] [Module R M] [Module S M]
  [IsScalarTower R S M] [AddCommMonoid N] [Module R N]

/-- If `b : Î¹ â†’ M` and `c : Îº â†’ N` are bases then so is `fun i â†¦ b i.1 âŠ—â‚œ c i.2 : Î¹ Ã— Îº â†’ M âŠ— N`. -/
def Basis.tensorProduct (b : Basis Î¹ S M) (c : Basis Îº R N) :
    Basis (Î¹ Ã— Îº) S (M âŠ—[R] N) :=
  Finsupp.basisSingleOne.map
    ((TensorProduct.AlgebraTensorModule.congr b.repr c.repr).trans <|
        (finsuppTensorFinsupp R S _ _ _ _).trans <|
          Finsupp.lcongr (Equiv.refl _) (TensorProduct.AlgebraTensorModule.rid R S S)).symm

@[simp]
theorem Basis.tensorProduct_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹) (j : Îº) :
    Basis.tensorProduct b c (i, j) = b i âŠ—â‚œ c j := by
  simp [Basis.tensorProduct]

theorem Basis.tensorProduct_apply' (b : Basis Î¹ S M) (c : Basis Îº R N) (i : Î¹ Ã— Îº) :
    Basis.tensorProduct b c i = b i.1 âŠ—â‚œ c i.2 := by
  simp [Basis.tensorProduct]

@[simp]
theorem Basis.tensorProduct_repr_tmul_apply (b : Basis Î¹ S M) (c : Basis Îº R N) (m : M) (n : N)
    (i : Î¹) (j : Îº) :
    (Basis.tensorProduct b c).repr (m âŠ—â‚œ n) (i, j) = c.repr n j â€¢ b.repr m i := by
  simp [Basis.tensorProduct, mul_comm]

variable (S : Type*) [Semiring S] [Algebra R S]

/-- The lift of an `R`-basis of `M` to an `S`-basis of the base change `S âŠ—[R] M`. -/
noncomputable
def Basis.baseChange (b : Basis Î¹ R M) : Basis Î¹ S (S âŠ—[R] M) :=
  ((Basis.singleton Unit S).tensorProduct b).reindex (Equiv.punitProd Î¹)

@[simp]
lemma Basis.baseChange_repr_tmul (b : Basis Î¹ R M) (x y i) :
    (b.baseChange S).repr (x âŠ—â‚œ y) i = b.repr y i â€¢ x := by
  simp [Basis.baseChange, Basis.tensorProduct]

@[simp]
lemma Basis.baseChange_apply (b : Basis Î¹ R M) (i) :
    b.baseChange S i = 1 âŠ—â‚œ b i := by
  simp [Basis.baseChange, Basis.tensorProduct]

variable (K : Type*) [CommSemiring K] (B C: Type*) [AddCommMonoid B] [Module K B]
    [AddCommMonoid C] [Module K C]

/-- Elements in B âŠ— C can be represented by sum of elements in B tensor elements of basis of C.-/
lemma TensorProduct.eq_repr_basis_right
    {Î¹C Î¹B : Type*} (ğ’ : Basis Î¹C K C) (â„¬ : Basis Î¹B K B) (x : B âŠ—[K] C) :
    âˆƒ (s : Finset Î¹C) (b : Î¹C â†’ B), âˆ‘ i âˆˆ s, b i âŠ—â‚œ[K] ğ’ i = x := by
  classical
  let ğ’¯ := Basis.tensorProduct â„¬ ğ’
  have eq1 := calc x
      _ = âˆ‘ ij âˆˆ (ğ’¯.repr x).support, (ğ’¯.repr x) ij â€¢ ğ’¯ ij := ğ’¯.linearCombination_repr x |>.symm
      _ = âˆ‘ ij âˆˆ (ğ’¯.repr x).support, (ğ’¯.repr x) (ij.1, ij.2) â€¢ ğ’¯ (ij.1, ij.2) :=
          Finset.sum_congr rfl <| by simp
      _ = âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst, âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd,
            ğ’¯.repr x (i, j) â€¢ ğ’¯ (i, j) := by
          rw [â† Finset.sum_product']
          apply Finset.sum_subset
          Â· rintro âŸ¨i, jâŸ© hij
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_product, Finset.mem_image,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, ğ’¯] at hij âŠ¢
            exact âŸ¨âŸ¨j, hijâŸ©, âŸ¨i, hijâŸ©âŸ©
          Â· rintro âŸ¨i, jâŸ© hij1 hij2
            simp only [Finset.mem_product, Finset.mem_image, Finsupp.mem_support_iff, ne_eq,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, Decidable.not_not,
              Basis.tensorProduct_apply, smul_eq_zero, ğ’¯] at hij1 hij2 âŠ¢
            rw [hij2]
            simp only [zero_smul]
      _ = âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd, âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst,
            ğ’¯.repr x (i, j) â€¢ ğ’¯ (i, j) := Finset.sum_comm
      _ = âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd, âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst,
            ğ’¯.repr x (i, j) â€¢ (â„¬ i âŠ—â‚œ[K] ğ’ j) := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          simp only [ğ’¯, Basis.tensorProduct_apply]
      _ =  âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd, âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst,
            (ğ’¯.repr x (i, j) â€¢ â„¬ i) âŠ—â‚œ[K] ğ’ j := by
          refine Finset.sum_congr rfl fun _ _ => Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul']
      _ =  âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd, (âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst,
            (ğ’¯.repr x (i, j) â€¢ â„¬ i)) âŠ—â‚œ[K] ğ’ j := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.sum_tmul]
  exact âŸ¨_, _, eq1.symmâŸ©

/-- Elements in B âŠ— C can be represented by sum of elements of basis of B tensor elements of C.-/
lemma TensorProduct.eq_repr_basis_left
    {Î¹B Î¹C: Type*} (â„¬ : Basis Î¹B K B) (ğ’ : Basis Î¹C K C) (x : B âŠ—[K] C) :
    âˆƒ (s : Finset Î¹B) (c : Î¹B â†’ C), âˆ‘ i âˆˆ s, â„¬ i âŠ—â‚œ[K] c i = x := by
  classical
  let ğ’¯ := Basis.tensorProduct â„¬ ğ’
  have eq1 := calc x
      _ = âˆ‘ ij âˆˆ (ğ’¯.repr x).support, (ğ’¯.repr x) ij â€¢ ğ’¯ ij := ğ’¯.linearCombination_repr x |>.symm
      _ = âˆ‘ ij âˆˆ (ğ’¯.repr x).support, (ğ’¯.repr x) (ij.1, ij.2) â€¢ ğ’¯ (ij.1, ij.2) :=
          Finset.sum_congr rfl <| by simp
      _ = âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst, âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd,
            ğ’¯.repr x (i, j) â€¢ ğ’¯ (i, j) := by
          rw [â† Finset.sum_product']
          apply Finset.sum_subset
          Â· rintro âŸ¨i, jâŸ© hij
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_product, Finset.mem_image,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, ğ’¯] at hij âŠ¢
            exact âŸ¨âŸ¨j, hijâŸ©, âŸ¨i, hijâŸ©âŸ©
          Â· rintro âŸ¨i, jâŸ© hij1 hij2
            simp only [Finset.mem_product, Finset.mem_image, Finsupp.mem_support_iff, ne_eq,
              Prod.exists, exists_and_right, exists_eq_right, Subtype.exists, Decidable.not_not,
              Basis.tensorProduct_apply, smul_eq_zero, ğ’¯] at hij1 hij2 âŠ¢
            rw [hij2]
            simp only [zero_smul]
      _ = âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst, âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd,
            ğ’¯.repr x (i, j) â€¢ (â„¬ i âŠ—â‚œ[K] ğ’ j) := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          simp only [ğ’¯, Basis.tensorProduct_apply]
      _ =  âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst, âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd,
            â„¬ i âŠ—â‚œ[K] (ğ’¯.repr x (i, j) â€¢ ğ’ j : C) := by
          refine Finset.sum_congr rfl fun _ _ => Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul', TensorProduct.smul_tmul]
      _ =  âˆ‘ i âˆˆ (ğ’¯.repr x).support.image Prod.fst,
            â„¬ i âŠ—â‚œ[K] (âˆ‘ j âˆˆ (ğ’¯.repr x).support.image Prod.snd, (ğ’¯.repr x (i, j) â€¢ ğ’ j)) := by
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.tmul_sum]
  exact âŸ¨_, _, eq1.symmâŸ©

lemma TensorProduct.sum_tmul_basis_right_eq_zero
    {Î¹C Î¹B: Type*} (ğ’ : Basis Î¹C K C) (â„¬ : Basis Î¹B K B)
    (s : Finset Î¹C) (b : Î¹C â†’ B)
    (h : âˆ‘ i âˆˆ s, b i âŠ—â‚œ[K] ğ’ i = 0) :
    âˆ€ i âˆˆ s, b i = 0 := by
  classical
  let ğ’¯ := Basis.tensorProduct â„¬ ğ’
  let I := s.biUnion fun i => (â„¬.repr (b i)).support
  have eq1 := calc (0 : B âŠ—[K] C)
      _ = âˆ‘ i âˆˆ s, b i âŠ—â‚œ[K] ğ’ i := h.symm
      _ = âˆ‘ i âˆˆ s, (âˆ‘ k âˆˆ (â„¬.repr (b i)).support, (â„¬.repr (b i)) k â€¢ â„¬ k) âŠ—â‚œ[K] ğ’ i := by
          refine Finset.sum_congr rfl fun z _ => ?_
          congr
          exact â„¬.linearCombination_repr (b z) |>.symm
      _ = âˆ‘ i âˆˆ s, âˆ‘ k âˆˆ (â„¬.repr (b i)).support, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[K] ğ’ i) := by
          refine Finset.sum_congr rfl fun z _ => ?_
          rw [TensorProduct.sum_tmul]
          refine Finset.sum_congr rfl fun _ _ => ?_
          rw [TensorProduct.smul_tmul']
      _ = âˆ‘ i âˆˆ s, âˆ‘ k âˆˆ I, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[K] ğ’ i) := by
          refine Finset.sum_congr rfl fun j h => ?_
          apply Finset.sum_subset
          Â· intro i hi
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_biUnion, I] at hi âŠ¢
            exact âŸ¨_, h, hiâŸ©
          Â· intro i hi1 hi2
            simp only [Finsupp.mem_support_iff, ne_eq, Decidable.not_not, smul_eq_zero]
              at hi1 hi2 âŠ¢
            simp only [hi2, zero_smul]
      _ = âˆ‘ k âˆˆ I, âˆ‘ i âˆˆ s, (â„¬.repr (b i)) k â€¢ (â„¬ k âŠ—â‚œ[K] ğ’ i) := Finset.sum_comm
      _ = âˆ‘ ij âˆˆ I Ã—Ë¢ s, (â„¬.repr (b ij.2)) ij.1 â€¢ (â„¬ ij.1 âŠ—â‚œ[K] ğ’ ij.2) := by
          rw [Finset.sum_product]
      _ = âˆ‘ ij âˆˆ I Ã—Ë¢ s, (â„¬.repr (b ij.2)) ij.1 â€¢ ğ’¯ ij := by
          refine Finset.sum_congr rfl fun ij _ => ?_
          rw [Basis.tensorProduct_apply]
  have LI := ğ’¯.linearIndependent
  rw [linearIndependent_iff'] at LI
  specialize LI (I Ã—Ë¢ s) _ eq1.symm
  intro i hi
  rw [â† â„¬.linearCombination_repr (b i)]
  change âˆ‘ _ âˆˆ _, _ = 0
  simp only [LinearMap.coe_smulRight, LinearMap.id_coe, id_eq]
  refine Finset.sum_eq_zero fun j hj => ?_
  specialize LI âŸ¨j, iâŸ© (by
    simp only [Finset.mem_product, Finset.mem_biUnion, Finsupp.mem_support_iff, ne_eq, I] at hj âŠ¢
    refine âŸ¨âŸ¨_, hi, hjâŸ©, hiâŸ©)
  simp [LI]

lemma TensorProduct.sum_tmul_basis_left_eq_zero
    {Î¹B Î¹C: Type*} (â„¬ : Basis Î¹B K B) (ğ’ : Basis Î¹C K C)
    (s : Finset Î¹B) (c : Î¹B â†’ C)
    (h : âˆ‘ i âˆˆ s, â„¬ i âŠ—â‚œ[K] c i = 0) :
    âˆ€ i âˆˆ s, c i = 0 := by
  classical
  let ğ’¯ := Basis.tensorProduct â„¬ ğ’
  let I := s.biUnion fun i => (ğ’.repr (c i)).support
  have eq1 := calc (0 : B âŠ—[K] C)
      _ = âˆ‘ i âˆˆ s, â„¬ i âŠ—â‚œ[K] c i := h.symm
      _ = âˆ‘ i âˆˆ s, (â„¬ i âŠ—â‚œ[K] (âˆ‘ k âˆˆ (ğ’.repr (c i)).support, (ğ’.repr (c i)) k â€¢ ğ’ k)) := by
          refine Finset.sum_congr rfl fun z _ => ?_
          congr
          exact ğ’.linearCombination_repr (c z) |>.symm
      _ = âˆ‘ i âˆˆ s, âˆ‘ k âˆˆ (ğ’.repr (c i)).support, (ğ’.repr (c i)) k â€¢ (â„¬ i âŠ—â‚œ[K] ğ’ k) := by
          refine Finset.sum_congr rfl fun z _ => ?_
          rw [TensorProduct.tmul_sum]
          simp_rw [TensorProduct.smul_tmul', TensorProduct.smul_tmul]
      _ = âˆ‘ i âˆˆ s, âˆ‘ k âˆˆ I, (ğ’.repr (c i)) k â€¢ (â„¬ i âŠ—â‚œ[K] ğ’ k) := by
          refine Finset.sum_congr rfl fun j h => ?_
          apply Finset.sum_subset
          Â· intro i hi
            simp only [Finsupp.mem_support_iff, ne_eq, Finset.mem_biUnion, I] at hi âŠ¢
            exact âŸ¨_, h, hiâŸ©
          Â· intro i hi1 hi2
            simp only [Finsupp.mem_support_iff, ne_eq, Decidable.not_not, smul_eq_zero]
              at hi1 hi2 âŠ¢
            simp only [hi2, zero_smul]
      _ = âˆ‘ ij âˆˆ s Ã—Ë¢ I, (ğ’.repr (c ij.1)) ij.2 â€¢ (â„¬ ij.1 âŠ—â‚œ[K] ğ’ ij.2) := by
          rw [Finset.sum_product]
      _ = âˆ‘ ij âˆˆ s Ã—Ë¢ I, (ğ’.repr (c ij.1)) ij.2 â€¢ ğ’¯ ij := by
          refine Finset.sum_congr rfl fun ij _ => ?_
          rw [Basis.tensorProduct_apply]
  have LI := ğ’¯.linearIndependent
  rw [linearIndependent_iff'] at LI
  specialize LI (s Ã—Ë¢ I) _ eq1.symm
  intro i hi
  rw [â† ğ’.linearCombination_repr (c i)]
  change âˆ‘ _ âˆˆ _, _ = 0
  simp only [LinearMap.coe_smulRight, LinearMap.id_coe, id_eq]
  refine Finset.sum_eq_zero fun j hj => ?_
  specialize LI âŸ¨i, jâŸ© (by
    simp only [Finset.mem_product, Finset.mem_biUnion, Finsupp.mem_support_iff, ne_eq, I] at hj âŠ¢
    exact âŸ¨hi, âŸ¨_, hi, hjâŸ©âŸ©)
  simp [LI]

end CommSemiring

end
