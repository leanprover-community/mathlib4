/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Data.ZMod.IntUnitsPower
import Mathlib.RingTheory.TensorProduct
import Mathlib.RingTheory.GradedAlgebra.Basic
import Mathlib.LinearAlgebra.DirectSum.TensorProduct

/-!
# Graded tensor products over super- (`ZMod 2`-graded)

The graded tensor product $A \hat\otimes_R B$ is imbued with a multiplication defined on homogeneous
tensors by:

$$(a \otimes b) \cdot (a' \otimes b') = (-1)^{\deg a' \deg b} (a \cdot a') \otimes (b \cdot b')$$

where $A$ and $B$ are algebras graded by `ZMod 2`, also known as superalgebras.

## Main results

* `TensorProduct.gradedMul`: this multiplication on externally-graded rings, as a bilinear map
* `SuperTensorProduct R ğ’œ â„¬`: for families of submodules of `A` and `B` that form a graded algebra,
  this is a type alias for `A âŠ—'[R] B` with the appropriate multiplication.
* `SuperTensorProduct.instAlgebra`: the ring structure induced by this multiplication.
* `SuperTensorProduct.liftEquiv`: a universal property for graded tensor products


## Notation

`ğ’œ âŠ—'[R] â„¬` is notation for `SuperTensorProduct R ğ’œ â„¬`

## References

* https://math.stackexchange.com/q/202718/1896
* TODO: find appropriate part of Bourbaki

## TODO

Show that the tensor product of graded algebras is itself a graded algebra.
-/

suppress_compilation

local notation "â„¤â‚‚" => ZMod 2
open scoped TensorProduct

variable {R A B : Type*}

namespace TensorProduct

section external
variable (ğ’œ : ZMod 2 â†’ Type*) (â„¬ : ZMod 2 â†’ Type*)
variable [CommRing R]
variable [âˆ€ i, AddCommGroup (ğ’œ i)] [âˆ€ i, AddCommGroup (â„¬ i)]
variable [âˆ€ i, Module R (ğ’œ i)] [âˆ€ i, Module R (â„¬ i)]
variable [DirectSum.GRing ğ’œ] [DirectSum.GRing â„¬]
variable [DirectSum.GAlgebra R ğ’œ] [DirectSum.GAlgebra R â„¬]

local notation "ğ’œâ„¬" => (fun i : â„¤â‚‚ Ã— â„¤â‚‚ => ğ’œ (Prod.fst i) âŠ—[R] â„¬ (Prod.snd i))

-- this helps with performance
instance (i : â„¤â‚‚ Ã— â„¤â‚‚) : Module R (ğ’œ (Prod.fst i) âŠ—[R] â„¬ (Prod.snd i)) :=
  TensorProduct.leftModule

variable (R) in
/-- Auxliary construction used to build `TensorProduct.gradedMul`.

This operates on direct sums of tensors instead of tensors of direct sums. -/
noncomputable irreducible_def gradedMulAux :
    (DirectSum _ ğ’œâ„¬) â†’â‚—[R] (DirectSum _ ğ’œâ„¬) â†’â‚—[R] (DirectSum _ ğ’œâ„¬) := by
  refine TensorProduct.curry ?_
  refine ?_ âˆ˜â‚— (TensorProduct.directSum R ğ’œâ„¬ ğ’œâ„¬).toLinearMap
  refine DirectSum.toModule R _ _ fun i => ?_
  have o := DirectSum.lof R _ ğ’œâ„¬ (i.1.1 + i.2.1, i.1.2 + i.2.2)
  have s : â„¤Ë£ := ((-1 : â„¤Ë£)^(i.1.2 * i.2.1 : â„¤â‚‚) : â„¤Ë£)
  refine (s â€¢ o) âˆ˜â‚— ?_
  refine ?_ âˆ˜â‚— (TensorProduct.tensorTensorTensorComm R _ _ _ _).toLinearMap
  refine TensorProduct.map (TensorProduct.lift ?_) (TensorProduct.lift ?_)
  Â· exact DirectSum.gMulLHom R _
  Â· exact DirectSum.gMulLHom R _

open DirectSum (lof)
open GradedMonoid (GMul)

set_option maxHeartbeats 400000 in
@[simp]
theorem gradedMulAux_lof_tmul_lof_tmul (iâ‚ jâ‚ iâ‚‚ jâ‚‚ : â„¤â‚‚)
    (aâ‚ : ğ’œ iâ‚) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : â„¬ jâ‚‚) :
    gradedMulAux R ğ’œ â„¬ (lof R _ ğ’œâ„¬ (iâ‚, jâ‚) (aâ‚ âŠ—â‚œ bâ‚)) (lof R _ ğ’œâ„¬ (iâ‚‚, jâ‚‚) (aâ‚‚ âŠ—â‚œ bâ‚‚)) =
      (-1 : â„¤Ë£)^(jâ‚ * iâ‚‚)
        â€¢ lof R _ ğ’œâ„¬ (_, _) (GMul.mul aâ‚ aâ‚‚ âŠ—â‚œ GMul.mul bâ‚ bâ‚‚) := by
  rw [gradedMulAux]
  dsimp
  simp

set_option maxHeartbeats 4000000
variable (R) in
/-- The multiplication operation for tensor products of externally `ZMod 2`-graded algebras. -/
noncomputable irreducible_def gradedMul :
    letI AB := (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)
    letI : Module R AB := TensorProduct.leftModule
    AB â†’â‚—[R] AB â†’â‚—[R] AB := by
  refine TensorProduct.curry ?_
  let e := TensorProduct.directSum R ğ’œ â„¬
  let e' := e.symm.toLinearMap
  refine e' âˆ˜â‚— ?_
  refine ?_ âˆ˜â‚— TensorProduct.map e.toLinearMap e.toLinearMap
  refine TensorProduct.lift ?_
  exact gradedMulAux R ğ’œ â„¬

theorem gradedMul_of_tmul_of (iâ‚ jâ‚ iâ‚‚ jâ‚‚ : â„¤â‚‚)
    (aâ‚ : ğ’œ iâ‚) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : â„¬ jâ‚‚) :
    gradedMul R ğ’œ â„¬ (lof R _ ğ’œ iâ‚ aâ‚ âŠ—â‚œ lof R _ â„¬ jâ‚ bâ‚) (lof R _ ğ’œ iâ‚‚ aâ‚‚ âŠ—â‚œ lof R _ â„¬ jâ‚‚ bâ‚‚) =
      (-1 : â„¤Ë£)^(jâ‚ * iâ‚‚)
        â€¢ (lof R _ ğ’œ _ (GMul.mul aâ‚ aâ‚‚) âŠ—â‚œ lof R _ â„¬ _ (GMul.mul bâ‚ bâ‚‚)) := by
  rw [gradedMul]
  dsimp only [TensorProduct.curry_apply, LinearMap.coe_comp, LinearEquiv.coe_coe,
    Function.comp_apply, TensorProduct.map_tmul, TensorProduct.lift.tmul]
  rw [TensorProduct.directSum_lof_tmul_lof, TensorProduct.directSum_lof_tmul_lof,
    gradedMulAux_lof_tmul_lof_tmul, Units.smul_def, zsmul_eq_smul_cast R, map_smul,
    TensorProduct.directSum_symm_lof_tmul, â†zsmul_eq_smul_cast, â†Units.smul_def]

theorem algebraMap_gradedMul (r : R) (x : (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)) :
    gradedMul R ğ’œ â„¬ (algebraMap R _ r âŠ—â‚œ 1) x = r â€¢ x := by
  suffices gradedMul R ğ’œ â„¬ (algebraMap R _ r âŠ—â‚œ 1) = DistribMulAction.toLinearMap R _ r by
    exact FunLike.congr_fun this x
  ext ia a ib b
  dsimp
  erw [gradedMul_of_tmul_of]
  rw [zero_mul, zâ‚‚pow_zero, one_smul]
  simp_rw [DirectSum.lof_eq_of]
  rw [â†DirectSum.of_mul_of, â†DirectSum.of_mul_of, smul_tmul']
  erw [one_mul, _root_.Algebra.smul_def]
  rfl

theorem one_gradedMul (x : (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)) :
    gradedMul R ğ’œ â„¬ 1 x = x := by
  simpa only [_root_.map_one, one_smul] using algebraMap_gradedMul ğ’œ â„¬ 1 x

theorem gradedMul_algebraMap (x : (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)) (r : R) :
    gradedMul R ğ’œ â„¬ x (algebraMap R _ r âŠ—â‚œ 1) = r â€¢ x := by
  suffices (gradedMul R ğ’œ â„¬).flip (algebraMap R _ r âŠ—â‚œ 1) = DistribMulAction.toLinearMap R _ r by
    exact FunLike.congr_fun this x
  ext
  dsimp
  erw [gradedMul_of_tmul_of]
  rw [mul_zero, zâ‚‚pow_zero, one_smul]
  simp_rw [DirectSum.lof_eq_of]
  rw [â†DirectSum.of_mul_of, â†DirectSum.of_mul_of, smul_tmul']
  erw [mul_one, _root_.Algebra.smul_def, Algebra.commutes]
  rfl

theorem gradedMul_one (x : (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)) :
    gradedMul R ğ’œ â„¬ x 1 = x := by
  simpa only [_root_.map_one, one_smul] using gradedMul_algebraMap ğ’œ â„¬ x 1

theorem gradedMul_assoc (x y z : (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i)) :
    gradedMul R ğ’œ â„¬ (gradedMul R ğ’œ â„¬ x y) z = gradedMul R ğ’œ â„¬ x (gradedMul R ğ’œ â„¬ y z) := by
  let mA := gradedMul R ğ’œ â„¬
    -- restate as an equality of morphisms so that we can use `ext`
  suffices LinearMap.llcomp R _ _ _ mA âˆ˜â‚— mA =
      (LinearMap.llcomp R _ _ _ LinearMap.lflip <| LinearMap.llcomp R _ _ _ mA.flip âˆ˜â‚— mA).flip by
    exact FunLike.congr_fun (FunLike.congr_fun (FunLike.congr_fun this x) y) z
  ext ixa xa ixb xb iya ya iyb yb iza za izb zb
  dsimp
  save
  simp_rw [gradedMul_of_tmul_of, Units.smul_def, zsmul_eq_smul_cast R,
    LinearMap.map_smulâ‚‚, LinearMap.map_smul, gradedMul_of_tmul_of, DirectSum.lof_eq_of,
    â†DirectSum.of_mul_of, mul_assoc]
  save
  simp_rw [â†zsmul_eq_smul_cast R, â†Units.smul_def, smul_smul, â†zâ‚‚pow_add, add_mul, mul_add]
  congr 2
  abel

end external

end TensorProduct

section internal
variable [CommRing R] [Ring A] [Ring B] [Algebra R A] [Algebra R B]
variable (ğ’œ : ZMod 2 â†’ Submodule R A) (â„¬ : ZMod 2 â†’ Submodule R B)
variable [GradedAlgebra ğ’œ] [GradedAlgebra â„¬]

open DirectSum


variable (R) in
/-- A Type synonym for `A âŠ—[R] B`, but with multiplication as `TensorProduct.gradedMul`.

This has notation `ğ’œ âŠ—'[R] â„¬`. -/
@[nolint unusedArguments]
def SuperTensorProduct
    (ğ’œ : ZMod 2 â†’ Submodule R A) (â„¬ : ZMod 2 â†’ Submodule R B)
    [GradedAlgebra ğ’œ] [GradedAlgebra â„¬] :
    Type _ :=
  A âŠ—[R] B

namespace SuperTensorProduct

open TensorProduct

@[inherit_doc SuperTensorProduct]
scoped[TensorProduct] notation:100 ğ’œ " âŠ—'[" R "] " â„¬:100 => SuperTensorProduct R ğ’œ â„¬

instance instAddCommGroupWithOne : AddCommGroupWithOne (ğ’œ âŠ—'[R] â„¬) :=
  Algebra.TensorProduct.instAddCommGroupWithOne
instance : Module R (ğ’œ âŠ—'[R] â„¬) := TensorProduct.leftModule

variable (R) in
/-- The casting equivalence to move between regular and graded tensor products. -/
def of : A âŠ—[R] B â‰ƒâ‚—[R] ğ’œ âŠ—'[R] â„¬ := LinearEquiv.refl _ _

@[simp]
theorem of_one : of R ğ’œ â„¬ 1 = 1 := rfl

@[simp]
theorem of_symm_one : (of R ğ’œ â„¬).symm 1 = 1 := rfl

-- for dsimp
@[simp, nolint simpNF]
theorem of_symm_of (x : A âŠ—[R] B) : (of R ğ’œ â„¬).symm (of R ğ’œ â„¬ x) = x := rfl

-- for dsimp
@[simp, nolint simpNF]
theorem symm_of_of (x : ğ’œ âŠ—'[R] â„¬) : of R ğ’œ â„¬ ((of R ğ’œ â„¬).symm x) = x := rfl

/-- Two linear maps from the graded tensor product agree if they agree on the underlying tensor
product. -/
@[ext]
theorem hom_ext {M} [AddCommMonoid M] [Module R M] â¦ƒf g : ğ’œ âŠ—'[R] â„¬ â†’â‚—[R] Mâ¦„
    (h : f âˆ˜â‚— of R ğ’œ â„¬ = (g âˆ˜â‚— of R ğ’œ â„¬ : A âŠ—[R] B â†’â‚—[R] M)) :
    f = g :=
  h

variable (R) {ğ’œ â„¬} in
/-- The graded tensor product of two elements of graded rings. -/
abbrev tmul (a : A) (b : B) := of R ğ’œ â„¬ (a âŠ—â‚œ b)

@[inherit_doc]
notation:100 x " âŠ—â‚œ'" y:100 => tmul _ x y

@[inherit_doc]
notation:100 x " âŠ—â‚œ'[" R "] " y:100 => tmul R x y

variable (R) in
/-- An auxiliary construction to move between the graded tensor product of internally-graded objects
and the product of direct sums.-/
noncomputable def auxEquiv : (ğ’œ âŠ—'[R] â„¬) â‰ƒâ‚—[R] (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i) :=
  let fA := (decomposeAlgEquiv ğ’œ).toLinearEquiv
  let fB := (decomposeAlgEquiv â„¬).toLinearEquiv
  (of R ğ’œ â„¬).symm.trans (TensorProduct.congr fA fB)

@[simp] theorem auxEquiv_tmul (a : A) (b : B) :
    auxEquiv R ğ’œ â„¬ (a âŠ—â‚œ' b : ğ’œ âŠ—'[R] â„¬) = decompose ğ’œ a âŠ—â‚œ decompose â„¬ b := rfl

@[simp] theorem auxEquiv_one : auxEquiv R ğ’œ â„¬ 1 = 1 := by
  rw [â†of_one, Algebra.TensorProduct.one_def, auxEquiv_tmul ğ’œ â„¬, DirectSum.decompose_one,
    DirectSum.decompose_one, Algebra.TensorProduct.one_def]

/-- Auxiliary construction used to build the `Mul` instance and get distributivity of `+` and
`\smul`. -/
noncomputable def mulHom : (ğ’œ âŠ—'[R] â„¬) â†’â‚—[R] (ğ’œ âŠ—'[R] â„¬) â†’â‚—[R] (ğ’œ âŠ—'[R] â„¬) := by
  letI fAB1 := auxEquiv R ğ’œ â„¬
  have := ((gradedMul R (ğ’œ Â·) (â„¬ Â·)).complâ‚â‚‚ fAB1.toLinearMap fAB1.toLinearMap).comprâ‚‚
    fAB1.symm.toLinearMap
  exact this

theorem mulHom_apply (x y : ğ’œ âŠ—'[R] â„¬) :
    mulHom ğ’œ â„¬ x y
      = (auxEquiv R ğ’œ â„¬).symm (gradedMul R (ğ’œ Â·) (â„¬ Â·) (auxEquiv R ğ’œ â„¬ x) (auxEquiv R ğ’œ â„¬ y)) :=
  rfl

/-- The multipication on the super tensor product.

See `SuperTensorProduct.coe_mul_coe` for a characterization on pure tensors. -/
noncomputable instance : Mul (ğ’œ âŠ—'[R] â„¬) where mul x y := mulHom ğ’œ â„¬ x y

theorem mul_def (x y : ğ’œ âŠ—'[R] â„¬) : x * y = mulHom ğ’œ â„¬ x y := rfl

noncomputable instance instMonoid : Monoid (ğ’œ âŠ—'[R] â„¬) where
  mul_one x := by
    rw [mul_def, mulHom_apply, auxEquiv_one, gradedMul_one, LinearEquiv.symm_apply_apply]
  one_mul x := by
    rw [mul_def, mulHom_apply, auxEquiv_one, one_gradedMul, LinearEquiv.symm_apply_apply]
  mul_assoc x y z := by
    simp_rw [mul_def, mulHom_apply, LinearEquiv.apply_symm_apply]
    rw [gradedMul_assoc]

noncomputable instance instRing : Ring (ğ’œ âŠ—'[R] â„¬) where
  __ := instAddCommGroupWithOne ğ’œ â„¬
  __ := instMonoid ğ’œ â„¬
  right_distrib x y z := by simp_rw [mul_def, LinearMap.map_addâ‚‚]
  left_distrib x y z := by simp_rw [mul_def, map_add]
  mul_zero x := by simp_rw [mul_def, map_zero]
  zero_mul x := by simp_rw [mul_def, LinearMap.map_zeroâ‚‚]

set_option maxHeartbeats 800000 in
/-- Note that a more general `tmul_coe_mul_coe_tmul` is available. -/
theorem coe_tmul_coe_mul_coe_tmul_coe {iâ‚ jâ‚ iâ‚‚ jâ‚‚ : â„¤â‚‚}
    (aâ‚ : ğ’œ iâ‚) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : â„¬ jâ‚‚) :
    ((aâ‚ : A) âŠ—â‚œ'[R] (bâ‚ : B) * (aâ‚‚ : A) âŠ—â‚œ'[R] (bâ‚‚ : B) : ğ’œ âŠ—'[R] â„¬) =
      (-1 : â„¤Ë£)^(jâ‚ * iâ‚‚) â€¢ ((aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B)) := by
  dsimp only [mul_def, mulHom_apply, of_symm_of]
  dsimp [auxEquiv, tmul]
  erw [decompose_coe, decompose_coe, decompose_coe, decompose_coe]
  dsimp
  simp_rw [â†lof_eq_of R]
  rw [gradedMul_of_tmul_of]
  simp_rw [lof_eq_of R]
  rw [LinearEquiv.symm_symm]
  rw [@Units.smul_def _ _ (_) (_), zsmul_eq_smul_cast R, map_smul, map_smul,
    â†zsmul_eq_smul_cast R, â†@Units.smul_def _ _ (_) (_)]
  rw [congr_symm_tmul]
  dsimp
  rw [decompose_symm_of, decompose_symm_of, SetLike.coe_gMul, SetLike.coe_gMul]

/-- The characterization of this multiplication on partially homogenous elements. -/
theorem tmul_coe_mul_coe_tmul {jâ‚ iâ‚‚ : â„¤â‚‚} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (bâ‚ : B) * (aâ‚‚ : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) =
      (-1 : â„¤Ë£)^(jâ‚ * iâ‚‚) â€¢ ((aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B)) := by
  classical
  rw [â†DirectSum.sum_support_decompose ğ’œ aâ‚, â†DirectSum.sum_support_decompose â„¬ bâ‚‚]
  rw [Finset.sum_mul, Finset.mul_sum]
  simp_rw [tmul, sum_tmul, tmul_sum, map_sum, Finset.smul_sum]
  rw [Finset.sum_mul]
  simp_rw [Finset.mul_sum, coe_tmul_coe_mul_coe_tmul_coe]

/-- A special case for when `bâ‚` has grade 0. -/
theorem tmul_zero_coe_mul_coe_tmul {iâ‚‚ : â„¤â‚‚} (aâ‚ : A) (bâ‚ : â„¬ 0) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (bâ‚ : B) * (aâ‚‚ : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) =
      ((aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B)) := by
  rw [tmul_coe_mul_coe_tmul, zero_mul, zâ‚‚pow_zero, one_smul]

/-- A special case for when `aâ‚‚` has grade 0. -/
theorem tmul_coe_mul_zero_coe_tmul {jâ‚ : â„¤â‚‚} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ 0) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (bâ‚ : B) * (aâ‚‚ : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) =
      ((aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B)) := by
  rw [tmul_coe_mul_coe_tmul, mul_zero, zâ‚‚pow_zero, one_smul]

theorem tmul_one_mul_coe_tmul {iâ‚‚ : â„¤â‚‚} (aâ‚ : A) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (1 : B) * (aâ‚‚ : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) = (aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (bâ‚‚ : B) := by
  convert tmul_zero_coe_mul_coe_tmul ğ’œ â„¬ aâ‚ (@GradedMonoid.GOne.one _ (â„¬ Â·) _ _) aâ‚‚ bâ‚‚
  rw [SetLike.coe_gOne, one_mul]

theorem tmul_coe_mul_one_tmul {jâ‚ : â„¤â‚‚} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (bâ‚ : B) * (1 : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) = (aâ‚ : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B) := by
  convert tmul_coe_mul_zero_coe_tmul ğ’œ â„¬ aâ‚ bâ‚ (@GradedMonoid.GOne.one _ (ğ’œ Â·) _ _) bâ‚‚
  rw [SetLike.coe_gOne, mul_one]

theorem tmul_one_mul_one_tmul (aâ‚ : A) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (1 : B) * (1 : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬) = (aâ‚ : A) âŠ—â‚œ' (bâ‚‚ : B) := by
  convert tmul_coe_mul_zero_coe_tmul ğ’œ â„¬
    aâ‚ (@GradedMonoid.GOne.one _ (â„¬ Â·) _ _) (@GradedMonoid.GOne.one _ (ğ’œ Â·) _ _) bâ‚‚
  Â· rw [SetLike.coe_gOne, mul_one]
  Â· rw [SetLike.coe_gOne, one_mul]

/-- The ring morphism `A â†’+* A âŠ—[R] B` sending `a` to `a âŠ—â‚œ 1`. -/
@[simps]
def includeLeftRingHom : A â†’+* ğ’œ âŠ—'[R] â„¬ where
  toFun a := a âŠ—â‚œ' 1
  map_zero' := by simp
  map_add' := by simp [tmul, TensorProduct.add_tmul]
  map_one' := rfl
  map_mul' aâ‚ aâ‚‚ := by
    dsimp
    classical
    rw [â†DirectSum.sum_support_decompose ğ’œ aâ‚‚, Finset.mul_sum]
    simp_rw [tmul, sum_tmul, map_sum, Finset.mul_sum]
    congr
    ext i
    rw [â†SetLike.coe_gOne â„¬, tmul_coe_mul_coe_tmul, zero_mul, zâ‚‚pow_zero, one_smul,
      SetLike.coe_gOne, one_mul]

noncomputable instance instAlgebra : Algebra R (ğ’œ âŠ—'[R] â„¬) where
  toRingHom := (includeLeftRingHom ğ’œ â„¬).comp (algebraMap R A)
  commutes' r x := by
    dsimp [mul_def, mulHom_apply, auxEquiv_tmul]
    simp_rw [DirectSum.decompose_algebraMap, DirectSum.decompose_one, algebraMap_gradedMul,
      gradedMul_algebraMap]
  smul_def' r x := by
    dsimp [mul_def, mulHom_apply, auxEquiv_tmul]
    simp_rw [DirectSum.decompose_algebraMap, DirectSum.decompose_one, algebraMap_gradedMul,
      map_smul, LinearEquiv.symm_apply_apply]

lemma algebraMap_def (r : R) : algebraMap R (ğ’œ âŠ—'[R] â„¬) r = algebraMap R A r âŠ—â‚œ'[R] 1 := rfl

theorem tmul_algebraMap_mul_coe_tmul {iâ‚‚ : â„¤â‚‚} (aâ‚ : A) (r : R) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] algebraMap R B r * (aâ‚‚ : A) âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬)
      = (aâ‚ * aâ‚‚ : A) âŠ—â‚œ' (algebraMap R B r * bâ‚‚ : B) :=
  tmul_zero_coe_mul_coe_tmul ğ’œ â„¬ aâ‚ (GAlgebra.toFun (A := (â„¬ Â·)) r) aâ‚‚ bâ‚‚

theorem tmul_coe_mul_algebraMap_tmul {jâ‚ : â„¤â‚‚} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (r : R) (bâ‚‚ : B) :
    (aâ‚ âŠ—â‚œ'[R] (bâ‚ : B) * algebraMap R A r âŠ—â‚œ'[R] bâ‚‚ : ğ’œ âŠ—'[R] â„¬)
      = (aâ‚ * algebraMap R A r : A) âŠ—â‚œ' (bâ‚ * bâ‚‚ : B) :=
  tmul_coe_mul_zero_coe_tmul ğ’œ â„¬ aâ‚ bâ‚ (GAlgebra.toFun (A := (ğ’œ Â·)) r) bâ‚‚

/-- The algebra morphism `A â†’â‚[R] A âŠ—[R] B` sending `a` to `a âŠ—â‚œ 1`. -/
@[simps!]
def includeLeft : A â†’â‚[R] ğ’œ âŠ—'[R] â„¬ where
  toRingHom := includeLeftRingHom ğ’œ â„¬
  commutes' _ := rfl

/-- The algebra morphism `B â†’â‚[R] A âŠ—[R] B` sending `b` to `1 âŠ—â‚œ b`. -/
@[simps!]
def includeRight : B â†’â‚[R] (ğ’œ âŠ—'[R] â„¬) :=
  AlgHom.ofLinearMap (R := R) (A := B) (B := ğ’œ âŠ—'[R] â„¬)
    (f := {
       toFun := fun b => 1 âŠ—â‚œ' b
       map_add' := by simp [tmul, TensorProduct.tmul_add]
       map_smul' := by simp [tmul, TensorProduct.tmul_smul] })
    (map_one := rfl)
    (map_mul := by
      rw [LinearMap.map_mul_iff]
      refine DirectSum.decompose_lhom_ext â„¬ fun iâ‚ => ?_
      ext bâ‚ bâ‚‚ : 2
      dsimp
      rw [tmul_coe_mul_one_tmul])

lemma algebraMap_def' (r : R) : algebraMap R (ğ’œ âŠ—'[R] â„¬) r = 1 âŠ—â‚œ'[R] algebraMap R B r :=
  (includeRight ğ’œ â„¬).commutes r |>.symm

variable {C} [Ring C] [Algebra R C]

/-- The forwards direction of the universal property; an algebra morphism out of the graded tensor
product can be assembed from maps on each component that (anti)commute on pure elements of the
corresponding graded algebras. -/
def lift (f : A â†’â‚[R] C) (g : B â†’â‚[R] C)
    (h_anti_commutes : âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), f a * g b = (-1 : â„¤Ë£)^(j * i) â€¢ (g b * f a)) :
    (ğ’œ âŠ—'[R] â„¬) â†’â‚[R] C :=
  AlgHom.ofLinearMap
    (LinearMap.mul' R C
      âˆ˜â‚— (TensorProduct.map f.toLinearMap g.toLinearMap)
      âˆ˜â‚— ((of R ğ’œ â„¬).symm : ğ’œ âŠ—'[R] â„¬ â†’â‚—[R] A âŠ—[R] B))
    (by dsimp [Algebra.TensorProduct.one_def]; simp only [_root_.map_one, mul_one])
    (by
      rw [LinearMap.map_mul_iff]
      ext aâ‚ : 3
      refine DirectSum.decompose_lhom_ext â„¬ fun jâ‚ => ?_
      ext bâ‚ : 3
      refine DirectSum.decompose_lhom_ext ğ’œ fun iâ‚‚ => ?_
      ext aâ‚‚ bâ‚‚ : 2
      dsimp
      rw [tmul_coe_mul_coe_tmul]
      rw [@Units.smul_def _ _ (_) (_), zsmul_eq_smul_cast R, map_smul, map_smul, map_smul]
      rw [â†zsmul_eq_smul_cast R, â†@Units.smul_def _ _ (_) (_)]
      rw [of_symm_of, map_tmul, LinearMap.mul'_apply]
      simp_rw [AlgHom.toLinearMap_apply, _root_.map_mul]
      simp_rw [mul_assoc (f aâ‚), â†mul_assoc _ _ (g bâ‚‚), h_anti_commutes, mul_smul_comm,
        smul_mul_assoc, smul_smul, zâ‚‚pow_mul_self, one_smul])

@[simp]
theorem lift_tmul (f : A â†’â‚[R] C) (g : B â†’â‚[R] C)
    (h_anti_commutes : âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), f a * g b = (-1 : â„¤Ë£)^(j * i) â€¢ (g b * f a))
    (a : A) (b : B) :
    lift ğ’œ â„¬ f g h_anti_commutes (a âŠ—â‚œ' b) = f a * g b :=
  rfl

/-- The universal property of the graded tensor product; every algebra morphism uniquely factors
as a pair of algebra morphisms that anticommute with respect to the grading. -/
def liftEquiv :
    { fg : (A â†’â‚[R] C) Ã— (B â†’â‚[R] C) //
        âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), fg.1 a * fg.2 b = (-1 : â„¤Ë£)^(j * i) â€¢ (fg.2 b * fg.1 a)} â‰ƒ
      ((ğ’œ âŠ—'[R] â„¬) â†’â‚[R] C) where
  toFun fg := lift ğ’œ â„¬ _ _ fg.prop
  invFun F := âŸ¨(F.comp (includeLeft ğ’œ â„¬), F.comp (includeRight ğ’œ â„¬)), fun i j a b => by
    dsimp
    rw [â†F.map_mul, â†F.map_mul, tmul_coe_mul_coe_tmul, one_mul, mul_one, AlgHom.map_smul_of_tower,
      tmul_one_mul_one_tmul, smul_smul, zâ‚‚pow_mul_self, one_smul]âŸ©
  left_inv fg := by ext <;> (dsimp; simp only [_root_.map_one, mul_one, one_mul])
  right_inv F := by
    apply AlgHom.toLinearMap_injective
    ext
    dsimp
    rw [â†F.map_mul, tmul_one_mul_one_tmul]

/-- Two algebra morphism from the graded tensor product agree if their compositions with the left
and right inclusions agree. -/
@[ext]
lemma algHom_ext â¦ƒf g : (ğ’œ âŠ—'[R] â„¬) â†’â‚[R] Câ¦„
    (ha : f.comp (includeLeft ğ’œ â„¬) = g.comp (includeLeft ğ’œ â„¬))
    (hb : f.comp (includeRight ğ’œ â„¬) = g.comp (includeRight ğ’œ â„¬)) : f = g :=
  (liftEquiv ğ’œ â„¬).symm.injective <| Subtype.ext <| Prod.ext ha hb

end SuperTensorProduct

end internal
