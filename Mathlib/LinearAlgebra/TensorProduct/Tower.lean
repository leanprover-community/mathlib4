/-
Copyright (c) 2020 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison, Johan Commelin, Eric Wieser
-/
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.Algebra.Algebra.Tower

#align_import ring_theory.tensor_product from "leanprover-community/mathlib"@"88fcdc3da43943f5b01925deddaa5bf0c0e85e4e"

/-!
# The `A`-module structure on `M âŠ—[R] N`

When `M` is both an `R`-module and an `A`-module, and `Algebra R A`, then many of the morphisms
preserve the actions by `A`.

The `Module` instance itself is provided elsewhere as `TensorProduct.leftModule`. This file provides
more general versions of the definitions already in `LinearAlgebra/TensorProduct`.

In this file, we use the convention that `M`, `N`, `P`, `Q` are all `R`-modules, but only `M` and
`P` are simultaneously `A`-modules.

## Main definitions

 * `TensorProduct.AlgebraTensorModule.curry`
 * `TensorProduct.AlgebraTensorModule.uncurry`
 * `TensorProduct.AlgebraTensorModule.lcurry`
 * `TensorProduct.AlgebraTensorModule.lift`
 * `TensorProduct.AlgebraTensorModule.lift.equiv`
 * `TensorProduct.AlgebraTensorModule.mk`
 * `TensorProduct.AlgebraTensorModule.map`
 * `TensorProduct.AlgebraTensorModule.mapBilinear`
 * `TensorProduct.AlgebraTensorModule.congr`
 * `TensorProduct.AlgebraTensorModule.rid`
 * `TensorProduct.AlgebraTensorModule.homTensorHomMap`
 * `TensorProduct.AlgebraTensorModule.assoc`
 * `TensorProduct.AlgebraTensorModule.leftComm`
 * `TensorProduct.AlgebraTensorModule.rightComm`
 * `TensorProduct.AlgebraTensorModule.tensorTensorTensorComm`

## Implementation notes

We could thus consider replacing the less general definitions with these ones. If we do this, we
probably should still implement the less general ones as abbreviations to the more general ones with
fewer type arguments.
-/

namespace TensorProduct

namespace AlgebraTensorModule

universe uR uA uB uM uN uP uQ
variable {R : Type uR} {A : Type uA} {B : Type uB}
variable {M : Type uM} {N : Type uN} {P : Type uP} {Q : Type uQ}

open LinearMap
open Algebra (lsmul)

section Semiring

variable [CommSemiring R] [Semiring A] [Semiring B] [Algebra R A] [Algebra R B]

variable [AddCommMonoid M] [Module R M] [Module A M] [Module B M]
variable [IsScalarTower R A M] [IsScalarTower R B M] [SMulCommClass A B M]

variable [AddCommMonoid N] [Module R N]

variable [AddCommMonoid P] [Module R P] [Module A P] [Module B P]
variable [IsScalarTower R A P] [IsScalarTower R B P] [SMulCommClass A B P]

variable [AddCommMonoid Q] [Module R Q]

theorem smul_eq_lsmul_rTensor (a : A) (x : M âŠ—[R] N) : a â€¢ x = (lsmul R R M a).rTensor N x :=
  rfl
#align tensor_product.algebra_tensor_module.smul_eq_lsmul_rtensor TensorProduct.AlgebraTensorModule.smul_eq_lsmul_rTensor

/-- Heterobasic version of `TensorProduct.curry`:

Given a linear map `M âŠ—[R] N â†’[A] P`, compose it with the canonical
bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N` to form a bilinear map `M â†’[A] N â†’[R] P`. -/
@[simps]
nonrec def curry (f : M âŠ—[R] N â†’â‚—[A] P) : M â†’â‚—[A] N â†’â‚—[R] P :=
  { curry (f.restrictScalars R) with
    toFun := curry (f.restrictScalars R)
    map_smul' := fun c x => LinearMap.ext fun y => f.map_smul c (x âŠ—â‚œ y) }
#align tensor_product.algebra_tensor_module.curry TensorProduct.AlgebraTensorModule.curry
#align tensor_product.algebra_tensor_module.curry_apply TensorProduct.AlgebraTensorModule.curry_apply

theorem restrictScalars_curry (f : M âŠ—[R] N â†’â‚—[A] P) :
    restrictScalars R (curry f) = TensorProduct.curry (f.restrictScalars R) :=
  rfl
#align tensor_product.algebra_tensor_module.restrict_scalars_curry TensorProduct.AlgebraTensorModule.restrictScalars_curry

/-- Just as `TensorProduct.ext` is marked `ext` instead of `TensorProduct.ext'`, this is
a better `ext` lemma than `TensorProduct.AlgebraTensorModule.ext` below.

See note [partially-applied ext lemmas]. -/
@[ext high]
nonrec theorem curry_injective : Function.Injective (curry : (M âŠ— N â†’â‚—[A] P) â†’ M â†’â‚—[A] N â†’â‚—[R] P) :=
  fun _ _ h =>
  LinearMap.restrictScalars_injective R <|
    curry_injective <| (congr_arg (LinearMap.restrictScalars R) h : _)
#align tensor_product.algebra_tensor_module.curry_injective TensorProduct.AlgebraTensorModule.curry_injective

theorem ext {g h : M âŠ—[R] N â†’â‚—[A] P} (H : âˆ€ x y, g (x âŠ—â‚œ y) = h (x âŠ—â‚œ y)) : g = h :=
  curry_injective <| LinearMap.extâ‚‚ H
#align tensor_product.algebra_tensor_module.ext TensorProduct.AlgebraTensorModule.ext

/-- Heterobasic version of `TensorProduct.lift`:

Constructing a linear map `M âŠ—[R] N â†’[A] P` given a bilinear map `M â†’[A] N â†’[R] P` with the
property that its composition with the canonical bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N` is
the given bilinear map `M â†’[A] N â†’[R] P`. -/
nonrec def lift (f : M â†’â‚—[A] N â†’â‚—[R] P) : M âŠ—[R] N â†’â‚—[A] P :=
  { lift (f.restrictScalars R) with
    map_smul' := fun c =>
      show
        âˆ€ x : M âŠ—[R] N,
          (lift (f.restrictScalars R)).comp (lsmul R R _ c) x =
            (lsmul R R _ c).comp (lift (f.restrictScalars R)) x
        from
        ext_iff.1 <|
          TensorProduct.ext' fun x y => by
            simp only [comp_apply, Algebra.lsmul_coe, smul_tmul', lift.tmul,
              coe_restrictScalars, f.map_smul, smul_apply] }
#align tensor_product.algebra_tensor_module.lift TensorProduct.AlgebraTensorModule.lift

-- Porting note: autogenerated lemma unfolded to `liftAux`
@[simp]
theorem lift_apply (f : M â†’â‚—[A] N â†’â‚—[R] P) (a : M âŠ—[R] N) :
    AlgebraTensorModule.lift f a = TensorProduct.lift (LinearMap.restrictScalars R f) a :=
  rfl

@[simp]
theorem lift_tmul (f : M â†’â‚—[A] N â†’â‚—[R] P) (x : M) (y : N) : lift f (x âŠ—â‚œ y) = f x y :=
  rfl
#align tensor_product.algebra_tensor_module.lift_tmul TensorProduct.AlgebraTensorModule.lift_tmul

variable (R A B M N P Q)

/-- Heterobasic version of `TensorProduct.uncurry`:

Linearly constructing a linear map `M âŠ—[R] N â†’[A] P` given a bilinear map `M â†’[A] N â†’[R] P`
with the property that its composition with the canonical bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N` is
the given bilinear map `M â†’[A] N â†’[R] P`. -/
@[simps]
def uncurry : (M â†’â‚—[A] N â†’â‚—[R] P) â†’â‚—[B] M âŠ—[R] N â†’â‚—[A] P where
  toFun := lift
  map_add' _ _ := ext fun x y => by simp only [lift_tmul, add_apply]
                                    -- ğŸ‰ no goals
  map_smul' _ _ := ext fun x y => by simp only [lift_tmul, smul_apply, RingHom.id_apply]
                                     -- ğŸ‰ no goals
-- porting note: new `B` argument
#align tensor_product.algebra_tensor_module.uncurry TensorProduct.AlgebraTensorModule.uncurryâ‚“

/-- Heterobasic version of `TensorProduct.lcurry`:

Given a linear map `M âŠ—[R] N â†’[A] P`, compose it with the canonical
bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N` to form a bilinear map `M â†’[A] N â†’[R] P`. -/
@[simps]
def lcurry : (M âŠ—[R] N â†’â‚—[A] P) â†’â‚—[B] M â†’â‚—[A] N â†’â‚—[R] P where
  toFun := curry
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
-- porting note: new `B` argument
#align tensor_product.algebra_tensor_module.lcurry TensorProduct.AlgebraTensorModule.lcurryâ‚“

/-- Heterobasic version of `TensorProduct.lift.equiv`:

A linear equivalence constructing a linear map `M âŠ—[R] N â†’[A] P` given a
bilinear map `M â†’[A] N â†’[R] P` with the property that its composition with the
canonical bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N` is the given bilinear map `M â†’[A] N â†’[R] P`. -/
def lift.equiv : (M â†’â‚—[A] N â†’â‚—[R] P) â‰ƒâ‚—[B] M âŠ—[R] N â†’â‚—[A] P :=
  LinearEquiv.ofLinear (uncurry R A B M N P) (lcurry R A B M N P)
    (LinearMap.ext fun _ => ext fun x y => lift_tmul _ x y)
    (LinearMap.ext fun f => LinearMap.ext fun x => LinearMap.ext fun y => lift_tmul f x y)
-- porting note: new `B` argument
#align tensor_product.algebra_tensor_module.lift.equiv TensorProduct.AlgebraTensorModule.lift.equivâ‚“

/-- Heterobasic version of `TensorProduct.mk`:

The canonical bilinear map `M â†’[A] N â†’[R] M âŠ—[R] N`. -/
@[simps! apply]
nonrec def mk : M â†’â‚—[A] N â†’â‚—[R] M âŠ—[R] N :=
  { mk R M N with map_smul' := fun _ _ => rfl }
#align tensor_product.algebra_tensor_module.mk TensorProduct.AlgebraTensorModule.mk
#align tensor_product.algebra_tensor_module.mk_apply TensorProduct.AlgebraTensorModule.mk_apply

variable {R A B M N P Q}

/-- Heterobasic version of `TensorProduct.map` -/
def map (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) : M âŠ—[R] N â†’â‚—[A] P âŠ—[R] Q :=
  lift <|
    { toFun := fun h => h âˆ˜â‚— g,
      map_add' := fun hâ‚ hâ‚‚ => LinearMap.add_comp g hâ‚‚ hâ‚,
      map_smul' := fun c h => LinearMap.smul_comp c h g } âˆ˜â‚— mk R A P Q âˆ˜â‚— f

@[simp] theorem map_tmul (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) (m : M) (n : N) :
    map f g (m âŠ—â‚œ n) = f m âŠ—â‚œ g n :=
  rfl

theorem map_add_left (fâ‚ fâ‚‚ : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) :
    map (fâ‚ + fâ‚‚) g = map fâ‚ g + map fâ‚‚ g := by
  ext
  -- âŠ¢ â†‘(â†‘(curry (map (fâ‚ + fâ‚‚) g)) xâœÂ¹) xâœ = â†‘(â†‘(curry (map fâ‚ g + map fâ‚‚ g)) xâœÂ¹) â€¦
  simp_rw [curry_apply, TensorProduct.curry_apply, restrictScalars_apply, add_apply, map_tmul,
    add_apply, add_tmul]

theorem map_add_right (f : M â†’â‚—[A] P) (gâ‚ gâ‚‚ : N â†’â‚—[R] Q) :
    map f (gâ‚ + gâ‚‚) = map f gâ‚ + map f gâ‚‚ := by
  ext
  -- âŠ¢ â†‘(â†‘(curry (map f (gâ‚ + gâ‚‚))) xâœÂ¹) xâœ = â†‘(â†‘(curry (map f gâ‚ + map f gâ‚‚)) xâœÂ¹) â€¦
  simp_rw [curry_apply, TensorProduct.curry_apply, restrictScalars_apply, add_apply, map_tmul,
    add_apply, tmul_add]

theorem map_smul_right (r : R) (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) : map f (r â€¢ g) = r â€¢ map f g := by
  ext
  -- âŠ¢ â†‘(â†‘(curry (map f (r â€¢ g))) xâœÂ¹) xâœ = â†‘(â†‘(curry (r â€¢ map f g)) xâœÂ¹) xâœ
  simp_rw [curry_apply, TensorProduct.curry_apply, restrictScalars_apply, smul_apply, map_tmul,
    smul_apply, tmul_smul]

theorem map_smul_left (b : B) (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) : map (b â€¢ f) g = b â€¢ map f g := by
  ext
  -- âŠ¢ â†‘(â†‘(curry (map (b â€¢ f) g)) xâœÂ¹) xâœ = â†‘(â†‘(curry (b â€¢ map f g)) xâœÂ¹) xâœ
  simp_rw [curry_apply, TensorProduct.curry_apply, restrictScalars_apply, smul_apply, map_tmul,
    smul_apply, smul_tmul']

variable (R A B M N P Q)

/-- Heterobasic version of `TensorProduct.map_bilinear` -/
def mapBilinear : (M â†’â‚—[A] P) â†’â‚—[B] (N â†’â‚—[R] Q) â†’â‚—[R] (M âŠ—[R] N â†’â‚—[A] P âŠ—[R] Q) :=
  LinearMap.mkâ‚‚' _ _ map map_add_left map_smul_left map_add_right map_smul_right

variable {R A B M N P Q}

@[simp]
theorem mapBilinear_apply (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) :
    mapBilinear R A B M N P Q f g = map f g :=
  rfl

variable (R A B M N P Q)

/-- Heterobasic version of `TensorProduct.homTensorHomMap` -/
def homTensorHomMap : ((M â†’â‚—[A] P) âŠ—[R] (N â†’â‚—[R] Q)) â†’â‚—[B] (M âŠ—[R] N â†’â‚—[A] P âŠ—[R] Q) :=
  lift <| mapBilinear R A B M N P Q

variable {R A B M N P Q}

@[simp] theorem homTensorHomMap_apply (f : M â†’â‚—[A] P) (g : N â†’â‚—[R] Q) :
    homTensorHomMap R A B M N P Q (f âŠ—â‚œ g) = map f g :=
  rfl

/-- Heterobasic version of `TensorProduct.congr` -/
def congr (f : M â‰ƒâ‚—[A] P) (g : N â‰ƒâ‚—[R] Q) : (M âŠ—[R] N) â‰ƒâ‚—[A] (P âŠ—[R] Q) :=
  LinearEquiv.ofLinear (map f g) (map f.symm g.symm)
    (ext fun _m _n => congr_argâ‚‚ (Â· âŠ—â‚œ Â·) (f.apply_symm_apply _) (g.apply_symm_apply _))
    (ext fun _m _n => congr_argâ‚‚ (Â· âŠ—â‚œ Â·) (f.symm_apply_apply _) (g.symm_apply_apply _))

@[simp] theorem congr_tmul (f : M â‰ƒâ‚—[A] P) (g : N â‰ƒâ‚—[R] Q) (m : M) (n : N) :
    congr f g (m âŠ—â‚œ n) = f m âŠ—â‚œ g n :=
  rfl

@[simp] theorem congr_symm_tmul (f : M â‰ƒâ‚—[A] P) (g : N â‰ƒâ‚—[R] Q) (p : P) (q : Q) :
    (congr f g).symm (p âŠ—â‚œ q) = f.symm p âŠ—â‚œ g.symm q :=
  rfl

variable (R A M)

/-- Heterobasic version of `TensorProduct.rid`. -/
protected def rid : M âŠ—[R] R â‰ƒâ‚—[A] M :=
  LinearEquiv.ofLinear
    (lift <| Algebra.lsmul _ _ _ |>.toLinearMap |>.flip)
    (mk R A M R |>.flip 1)
    (LinearMap.ext <| one_smul _)
    (ext <| fun _ _ => smul_tmul _ _ _ |>.trans <| congr_arg _ <| mul_one _)

variable {R M}

@[simp]
theorem rid_tmul (r : R) (m : M) : AlgebraTensorModule.rid R A M (m âŠ—â‚œ r) = r â€¢ m := rfl

end Semiring

section CommSemiring

variable [CommSemiring R] [CommSemiring A] [Semiring B] [Algebra R A] [Algebra R B]

variable [AddCommMonoid M] [Module R M] [Module A M] [Module B M]
variable [IsScalarTower R A M] [IsScalarTower R B M] [SMulCommClass A B M]

variable [AddCommMonoid N] [Module R N]

variable [AddCommMonoid P] [Module R P] [Module A P] [Module B P]
variable [IsScalarTower R A P] [IsScalarTower R B P] [SMulCommClass A B P]

variable [AddCommMonoid Q] [Module R Q]

variable (R A B M N P Q)

attribute [local ext high] TensorProduct.ext

section assoc
variable [Algebra A B] [IsScalarTower A B M]

/-- Heterobasic version of `TensorProduct.assoc`:

Linear equivalence between `(M âŠ—[A] N) âŠ—[R] P` and `M âŠ—[A] (N âŠ—[R] P)`.

Note this is especially useful with `A = R` (where it is a "more linear" version of
`TensorProduct.assoc`), or with `B = A`. -/
def assoc : (M âŠ—[A] P) âŠ—[R] Q â‰ƒâ‚—[B] M âŠ—[A] (P âŠ—[R] Q) :=
  LinearEquiv.ofLinear
    (lift <| lift <| lcurry R A B P Q _ âˆ˜â‚— mk A B M (P âŠ—[R] Q))
    (lift <| uncurry R A B P Q _ âˆ˜â‚— curry (mk R B _ Q))
    (by ext; rfl)
        -- âŠ¢ â†‘(â†‘(curry (â†‘(curry (comp (lift (lift (comp (lcurry R A B P Q (M âŠ—[A] P âŠ—[R]  â€¦
             -- ğŸ‰ no goals
    (by ext; rfl)
        -- âŠ¢ â†‘(â†‘(â†‘(curry (curry (comp (lift (comp (uncurry R A B P Q ((M âŠ—[A] P) âŠ—[R] Q)) â€¦
             -- ğŸ‰ no goals
-- porting note: new `B` argument
#align tensor_product.algebra_tensor_module.assoc TensorProduct.AlgebraTensorModule.assocâ‚“

variable {M P N Q}

@[simp]
theorem assoc_tmul (m : M) (p : P) (q : Q) :
    assoc R A B M P Q ((m âŠ—â‚œ p) âŠ—â‚œ q) = m âŠ—â‚œ (p âŠ—â‚œ q) :=
  rfl

@[simp]
theorem assoc_symm_tmul (m : M) (p : P) (q : Q) :
    (assoc R A B M P Q).symm (m âŠ—â‚œ (p âŠ—â‚œ q)) = (m âŠ—â‚œ p) âŠ—â‚œ q :=
  rfl

end assoc

section leftComm

/-- Heterobasic version of `TensorProduct.leftComm` -/
def leftComm : M âŠ—[A] (P âŠ—[R] Q) â‰ƒâ‚—[A] P âŠ—[A] (M âŠ—[R] Q) :=
  let eâ‚ := (assoc R A A M P Q).symm
  let eâ‚‚ := congr (TensorProduct.comm A M P) (1 : Q â‰ƒâ‚—[R] Q)
  let eâ‚ƒ := assoc R A A P M Q
  eâ‚ â‰ªâ‰«â‚— eâ‚‚ â‰ªâ‰«â‚— eâ‚ƒ

variable {M N P Q}

@[simp]
theorem leftComm_tmul (m : M) (p : P) (q : Q) :
    leftComm R A M P Q (m âŠ—â‚œ (p âŠ—â‚œ q)) = p âŠ—â‚œ (m âŠ—â‚œ q) :=
  rfl

@[simp]
theorem leftComm_symm_tmul (m : M) (p : P) (q : Q):
    (leftComm R A M P Q).symm (p âŠ—â‚œ (m âŠ—â‚œ q)) = m âŠ—â‚œ (p âŠ—â‚œ q) :=
  rfl

end leftComm

section rightComm

/-- A tensor product analogue of `mul_right_comm`. -/
def rightComm : (M âŠ—[A] P) âŠ—[R] Q â‰ƒâ‚—[A] (M âŠ—[R] Q) âŠ—[A] P :=
  LinearEquiv.ofLinear
    (lift <| TensorProduct.lift <| LinearMap.flip <|
      lcurry R A A M Q ((M âŠ—[R] Q) âŠ—[A] P) âˆ˜â‚— (mk A A (M âŠ—[R] Q) P).flip)
    (TensorProduct.lift <| lift <| LinearMap.flip <|
      (TensorProduct.lcurry A M P ((M âŠ—[A] P) âŠ—[R] Q)).restrictScalars R
        âˆ˜â‚— (mk R A (M âŠ—[A] P) Q).flip)
    -- explicit `Eq.refl`s here help with performance, but also make it clear that the `ext` are
    -- letting us prove the result as an equality of pure tensors.
    (TensorProduct.ext <| ext <| fun m q => LinearMap.ext <| fun p => Eq.refl <|
      (m âŠ—â‚œ[R] q) âŠ—â‚œ[A] p)
    (curry_injective <| TensorProduct.ext' <| fun m p => LinearMap.ext <| fun q => Eq.refl <|
      (m âŠ—â‚œ[A] p) âŠ—â‚œ[R] q)

variable {M N P Q}

@[simp]
theorem rightComm_tmul (m : M) (p : P) (q : Q) :
    rightComm R A M P Q ((m âŠ—â‚œ p) âŠ—â‚œ q) = (m âŠ—â‚œ q) âŠ—â‚œ p :=
  rfl

@[simp]
theorem rightComm_symm_tmul (m : M) (p : P) (q : Q):
    (rightComm R A M P Q).symm ((m âŠ—â‚œ q) âŠ—â‚œ p) = (m âŠ—â‚œ p) âŠ—â‚œ q :=
  rfl

end rightComm

section tensorTensorTensorComm

/-- Heterobasic version of `tensorTensorTensorComm`. -/
def tensorTensorTensorComm :
  (M âŠ—[R] N) âŠ—[A] (P âŠ—[R] Q) â‰ƒâ‚—[A] (M âŠ—[A] P) âŠ—[R] (N âŠ—[R] Q) :=
(assoc R A A (M âŠ—[R] N) P Q).symm
  â‰ªâ‰«â‚— congr (rightComm R A M P N).symm (1 : Q â‰ƒâ‚—[R] Q)
  â‰ªâ‰«â‚— assoc R _ _ (M âŠ—[A] P) N Q

variable {M N P Q}

@[simp]
theorem tensorTensorTensorComm_tmul (m : M) (n : N) (p : P) (q : Q) :
    tensorTensorTensorComm R A M N P Q ((m âŠ—â‚œ n) âŠ—â‚œ (p âŠ—â‚œ q)) = (m âŠ—â‚œ p) âŠ—â‚œ (n âŠ—â‚œ q) :=
  rfl

@[simp]
theorem tensorTensorTensorComm_symm_tmul (m : M) (n : N) (p : P) (q : Q) :
    (tensorTensorTensorComm R A M N P Q).symm ((m âŠ—â‚œ p) âŠ—â‚œ (n âŠ—â‚œ q)) = (m âŠ—â‚œ n) âŠ—â‚œ (p âŠ—â‚œ q) :=
  rfl

end tensorTensorTensorComm

end CommSemiring

end AlgebraTensorModule

end TensorProduct
