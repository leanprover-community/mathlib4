/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.LinearAlgebra.TensorProduct.Graded.External
import Mathlib.RingTheory.GradedAlgebra.Basic

/-!
# Graded tensor products over graded algebras

The graded tensor product $A \hat\otimes_R B$ is imbued with a multiplication defined on homogeneous
tensors by:

$$(a \otimes b) \cdot (a' \otimes b') = (-1)^{\deg a' \deg b} (a \cdot a') \otimes (b \cdot b')$$

where $A$ and $B$ are algebras graded by `â„•`, `â„¤`, or `Î¹` (or more generally, any index
that satisfies `Module Î¹ (Additive â„¤Ë£)`).

## Main results

* `GradedTensorProduct R ğ’œ â„¬`: for families of submodules of `A` and `B` that form a graded algebra,
  this is a type alias for `A âŠ—[R] B` with the appropriate multiplication.
* `GradedTensorProduct.instAlgebra`: the ring structure induced by this multiplication.
* `GradedTensorProduct.liftEquiv`: a universal property for graded tensor products

## Notation

* `ğ’œ áµâŠ—[R] â„¬` is notation for `GradedTensorProduct R ğ’œ â„¬`.
* `a áµâŠ—â‚œ b` is notation for `GradedTensorProduct.tmul _ a b`.

## References

* https://math.stackexchange.com/q/202718/1896
* [*Algebra I*, Bourbaki : Chapter III, Â§4.7, example (2)][bourbaki1989]

## Implementation notes

We cannot put the multiplication on `A âŠ—[R] B` directly as it would conflict with the existing
multiplication defined without the $(-1)^{\deg a' \deg b}$ term. Furthermore, the ring `A` may not
have a unique graduation, and so we need the chosen graduation `ğ’œ` to appear explicitly in the
type.

## TODO

* Show that the tensor product of graded algebras is itself a graded algebra.
* Determine if replacing the synonym with a single-field structure improves performance.
-/

suppress_compilation

open scoped TensorProduct

variable {R Î¹ A B : Type*}
variable [CommSemiring Î¹] [DecidableEq Î¹]
variable [CommRing R] [Ring A] [Ring B] [Algebra R A] [Algebra R B]
variable (ğ’œ : Î¹ â†’ Submodule R A) (â„¬ : Î¹ â†’ Submodule R B)
variable [GradedAlgebra ğ’œ] [GradedAlgebra â„¬]

open DirectSum


variable (R) in
/-- A Type synonym for `A âŠ—[R] B`, but with multiplication as `TensorProduct.gradedMul`.

This has notation `ğ’œ áµâŠ—[R] â„¬`. -/
@[nolint unusedArguments]
def GradedTensorProduct
    (ğ’œ : Î¹ â†’ Submodule R A) (â„¬ : Î¹ â†’ Submodule R B)
    [GradedAlgebra ğ’œ] [GradedAlgebra â„¬] :
    Type _ :=
  A âŠ—[R] B

namespace GradedTensorProduct

open TensorProduct

@[inherit_doc GradedTensorProduct]
scoped[TensorProduct] notation:100 ğ’œ " áµâŠ—[" R "] " â„¬:100 => GradedTensorProduct R ğ’œ â„¬

instance instAddCommGroupWithOne : AddCommGroupWithOne (ğ’œ áµâŠ—[R] â„¬) :=
  Algebra.TensorProduct.instAddCommGroupWithOne
instance : Module R (ğ’œ áµâŠ—[R] â„¬) := TensorProduct.leftModule

variable (R) in
/-- The casting equivalence to move between regular and graded tensor products. -/
def of : A âŠ—[R] B â‰ƒâ‚—[R] ğ’œ áµâŠ—[R] â„¬ := LinearEquiv.refl _ _

@[simp]
theorem of_one : of R ğ’œ â„¬ 1 = 1 := rfl

@[simp]
theorem of_symm_one : (of R ğ’œ â„¬).symm 1 = 1 := rfl

@[simp]
theorem of_symm_of (x : A âŠ—[R] B) : (of R ğ’œ â„¬).symm (of R ğ’œ â„¬ x) = x := rfl

@[simp]
theorem symm_of_of (x : ğ’œ áµâŠ—[R] â„¬) : of R ğ’œ â„¬ ((of R ğ’œ â„¬).symm x) = x := rfl

/-- Two linear maps from the graded tensor product agree if they agree on the underlying tensor
product. -/
@[ext]
theorem hom_ext {M} [AddCommMonoid M] [Module R M] â¦ƒf g : ğ’œ áµâŠ—[R] â„¬ â†’â‚—[R] Mâ¦„
    (h : f âˆ˜â‚— of R ğ’œ â„¬ = (g âˆ˜â‚— of R ğ’œ â„¬ : A âŠ—[R] B â†’â‚—[R] M)) :
    f = g :=
  h

variable (R) {ğ’œ â„¬} in
/-- The graded tensor product of two elements of graded rings. -/
abbrev tmul (a : A) (b : B) : ğ’œ áµâŠ—[R] â„¬ := of R ğ’œ â„¬ (a âŠ—â‚œ b)

@[inherit_doc]
notation:100 x " áµâŠ—â‚œ " y:100 => tmul _ x y

@[inherit_doc]
notation:100 x " áµâŠ—â‚œ[" R "] " y:100 => tmul R x y

variable (R) in
/-- An auxiliary construction to move between the graded tensor product of internally-graded objects
and the tensor product of direct sums. -/
noncomputable def auxEquiv : (ğ’œ áµâŠ—[R] â„¬) â‰ƒâ‚—[R] (â¨ i, ğ’œ i) âŠ—[R] (â¨ i, â„¬ i) :=
  let fA := (decomposeAlgEquiv ğ’œ).toLinearEquiv
  let fB := (decomposeAlgEquiv â„¬).toLinearEquiv
  (of R ğ’œ â„¬).symm.trans (TensorProduct.congr fA fB)

theorem auxEquiv_tmul (a : A) (b : B) :
    auxEquiv R ğ’œ â„¬ (a áµâŠ—â‚œ b) = decompose ğ’œ a âŠ—â‚œ decompose â„¬ b := rfl

theorem auxEquiv_one : auxEquiv R ğ’œ â„¬ 1 = 1 := by
  rw [â† of_one, Algebra.TensorProduct.one_def, auxEquiv_tmul ğ’œ â„¬, DirectSum.decompose_one,
    DirectSum.decompose_one, Algebra.TensorProduct.one_def]

theorem auxEquiv_symm_one : (auxEquiv R ğ’œ â„¬).symm 1 = 1 :=
  (LinearEquiv.symm_apply_eq _).mpr (auxEquiv_one _ _).symm

variable [Module Î¹ (Additive â„¤Ë£)]

/-- Auxiliary construction used to build the `Mul` instance and get distributivity of `+` and
`\smul`. -/
noncomputable def mulHom : (ğ’œ áµâŠ—[R] â„¬) â†’â‚—[R] (ğ’œ áµâŠ—[R] â„¬) â†’â‚—[R] (ğ’œ áµâŠ—[R] â„¬) := by
  letI fAB1 := auxEquiv R ğ’œ â„¬
  have := ((gradedMul R (ğ’œ Â·) (â„¬ Â·)).complâ‚â‚‚ fAB1.toLinearMap fAB1.toLinearMap).comprâ‚‚
    fAB1.symm.toLinearMap
  exact this

theorem mulHom_apply (x y : ğ’œ áµâŠ—[R] â„¬) :
    mulHom ğ’œ â„¬ x y
      = (auxEquiv R ğ’œ â„¬).symm (gradedMul R (ğ’œ Â·) (â„¬ Â·) (auxEquiv R ğ’œ â„¬ x) (auxEquiv R ğ’œ â„¬ y)) :=
  rfl

/-- The multiplication on the graded tensor product.

See `GradedTensorProduct.coe_mul_coe` for a characterization on pure tensors. -/
instance : Mul (ğ’œ áµâŠ—[R] â„¬) where mul x y := mulHom ğ’œ â„¬ x y

theorem mul_def (x y : ğ’œ áµâŠ—[R] â„¬) : x * y = mulHom ğ’œ â„¬ x y := rfl

-- Before https://github.com/leanprover-community/mathlib4/pull/8386 this was `@[simp]` but it times out when we try to apply it.
theorem auxEquiv_mul (x y : ğ’œ áµâŠ—[R] â„¬) :
    auxEquiv R ğ’œ â„¬ (x * y) = gradedMul R (ğ’œ Â·) (â„¬ Â·) (auxEquiv R ğ’œ â„¬ x) (auxEquiv R ğ’œ â„¬ y) :=
  LinearEquiv.eq_symm_apply _ |>.mp rfl

instance instMonoid : Monoid (ğ’œ áµâŠ—[R] â„¬) where
  mul_one x := by
    rw [mul_def, mulHom_apply, auxEquiv_one, gradedMul_one, LinearEquiv.symm_apply_apply]
  one_mul x := by
    rw [mul_def, mulHom_apply, auxEquiv_one, one_gradedMul, LinearEquiv.symm_apply_apply]
  mul_assoc x y z := by
    simp_rw [mul_def, mulHom_apply, LinearEquiv.apply_symm_apply]
    rw [gradedMul_assoc]

instance instRing : Ring (ğ’œ áµâŠ—[R] â„¬) where
  __ := instAddCommGroupWithOne ğ’œ â„¬
  __ := instMonoid ğ’œ â„¬
  right_distrib x y z := by simp_rw [mul_def, LinearMap.map_addâ‚‚]
  left_distrib x y z := by simp_rw [mul_def, map_add]
  mul_zero x := by simp_rw [mul_def, map_zero]
  zero_mul x := by simp_rw [mul_def, LinearMap.map_zeroâ‚‚]

/-- The characterization of this multiplication on partially homogeneous elements. -/
theorem tmul_coe_mul_coe_tmul {jâ‚ iâ‚‚ : Î¹} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (bâ‚ : B) * (aâ‚‚ : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) =
      (-1 : â„¤Ë£) ^ (jâ‚ * iâ‚‚) â€¢ ((aâ‚ * aâ‚‚ : A) áµâŠ—â‚œ (bâ‚ * bâ‚‚ : B)) := by
  dsimp only [mul_def, mulHom_apply, of_symm_of]
  dsimp [auxEquiv, tmul]
  rw [decompose_coe, decompose_coe]
  simp_rw [â† lof_eq_of R]
  rw [tmul_of_gradedMul_of_tmul]
  simp_rw [lof_eq_of R]
  -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 had to specialize `map_smul` to `LinearEquiv.map_smul`
  rw [@Units.smul_def _ _ (_) (_), â† Int.cast_smul_eq_zsmul R, LinearEquiv.map_smul, map_smul,
    Int.cast_smul_eq_zsmul R, â† @Units.smul_def _ _ (_) (_)]
  rw [congr_symm_tmul]
  dsimp
  simp_rw [decompose_symm_mul, decompose_symm_of, Equiv.symm_apply_apply]

/-- A special case for when `bâ‚` has grade 0. -/
theorem tmul_zero_coe_mul_coe_tmul {iâ‚‚ : Î¹} (aâ‚ : A) (bâ‚ : â„¬ 0) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (bâ‚ : B) * (aâ‚‚ : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) =
      ((aâ‚ * aâ‚‚ : A) áµâŠ—â‚œ (bâ‚ * bâ‚‚ : B)) := by
  rw [tmul_coe_mul_coe_tmul, zero_mul, uzpow_zero, one_smul]

/-- A special case for when `aâ‚‚` has grade 0. -/
theorem tmul_coe_mul_zero_coe_tmul {jâ‚ : Î¹} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (aâ‚‚ : ğ’œ 0) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (bâ‚ : B) * (aâ‚‚ : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) =
      ((aâ‚ * aâ‚‚ : A) áµâŠ—â‚œ (bâ‚ * bâ‚‚ : B)) := by
  rw [tmul_coe_mul_coe_tmul, mul_zero, uzpow_zero, one_smul]

theorem tmul_one_mul_coe_tmul {iâ‚‚ : Î¹} (aâ‚ : A) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (1 : B) * (aâ‚‚ : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) = (aâ‚ * aâ‚‚ : A) áµâŠ—â‚œ (bâ‚‚ : B) := by
  convert tmul_zero_coe_mul_coe_tmul ğ’œ â„¬ aâ‚ (@GradedMonoid.GOne.one _ (â„¬ Â·) _ _) aâ‚‚ bâ‚‚
  rw [SetLike.coe_gOne, one_mul]

theorem tmul_coe_mul_one_tmul {jâ‚ : Î¹} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (bâ‚ : B) * (1 : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) = (aâ‚ : A) áµâŠ—â‚œ (bâ‚ * bâ‚‚ : B) := by
  convert tmul_coe_mul_zero_coe_tmul ğ’œ â„¬ aâ‚ bâ‚ (@GradedMonoid.GOne.one _ (ğ’œ Â·) _ _) bâ‚‚
  rw [SetLike.coe_gOne, mul_one]

theorem tmul_one_mul_one_tmul (aâ‚ : A) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (1 : B) * (1 : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬) = (aâ‚ : A) áµâŠ—â‚œ (bâ‚‚ : B) := by
  convert tmul_coe_mul_zero_coe_tmul ğ’œ â„¬
    aâ‚ (GradedMonoid.GOne.one (A := (â„¬ Â·))) (GradedMonoid.GOne.one (A := (ğ’œ Â·))) bâ‚‚
  Â· rw [SetLike.coe_gOne, mul_one]
  Â· rw [SetLike.coe_gOne, one_mul]

/-- The ring morphism `A â†’+* A âŠ—[R] B` sending `a` to `a âŠ—â‚œ 1`. -/
@[simps]
def includeLeftRingHom : A â†’+* ğ’œ áµâŠ—[R] â„¬ where
  toFun a := a áµâŠ—â‚œ 1
  map_zero' := by simp
  map_add' := by simp [tmul, TensorProduct.add_tmul]
  map_one' := rfl
  map_mul' aâ‚ aâ‚‚ := by
    classical
    rw [â† DirectSum.sum_support_decompose ğ’œ aâ‚‚, Finset.mul_sum]
    simp_rw [tmul, sum_tmul, map_sum, Finset.mul_sum]
    congr
    ext i
    rw [â† SetLike.coe_gOne â„¬, tmul_coe_mul_coe_tmul, zero_mul, uzpow_zero, one_smul,
      SetLike.coe_gOne, one_mul]

instance instAlgebra : Algebra R (ğ’œ áµâŠ—[R] â„¬) where
  algebraMap := (includeLeftRingHom ğ’œ â„¬).comp (algebraMap R A)
  commutes' r x := by
    dsimp [mul_def, mulHom_apply, auxEquiv_tmul]
    simp_rw [DirectSum.decompose_algebraMap, DirectSum.decompose_one, algebraMap_gradedMul,
      gradedMul_algebraMap]
  smul_def' r x := by
    dsimp [mul_def, mulHom_apply, auxEquiv_tmul]
    simp_rw [DirectSum.decompose_algebraMap, DirectSum.decompose_one, algebraMap_gradedMul]
    -- Qualified `map_smul` to avoid a TC timeout https://github.com/leanprover-community/mathlib4/pull/8386
    rw [LinearEquiv.map_smul]
    simp

lemma algebraMap_def (r : R) : algebraMap R (ğ’œ áµâŠ—[R] â„¬) r = algebraMap R A r áµâŠ—â‚œ[R] 1 := rfl

theorem tmul_algebraMap_mul_coe_tmul {iâ‚‚ : Î¹} (aâ‚ : A) (r : R) (aâ‚‚ : ğ’œ iâ‚‚) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] algebraMap R B r * (aâ‚‚ : A) áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬)
      = (aâ‚ * aâ‚‚ : A) áµâŠ—â‚œ (algebraMap R B r * bâ‚‚ : B) :=
  tmul_zero_coe_mul_coe_tmul ğ’œ â„¬ aâ‚ (GAlgebra.toFun (A := (â„¬ Â·)) r) aâ‚‚ bâ‚‚

theorem tmul_coe_mul_algebraMap_tmul {jâ‚ : Î¹} (aâ‚ : A) (bâ‚ : â„¬ jâ‚) (r : R) (bâ‚‚ : B) :
    (aâ‚ áµâŠ—â‚œ[R] (bâ‚ : B) * algebraMap R A r áµâŠ—â‚œ[R] bâ‚‚ : ğ’œ áµâŠ—[R] â„¬)
      = (aâ‚ * algebraMap R A r : A) áµâŠ—â‚œ (bâ‚ * bâ‚‚ : B) :=
  tmul_coe_mul_zero_coe_tmul ğ’œ â„¬ aâ‚ bâ‚ (GAlgebra.toFun (A := (ğ’œ Â·)) r) bâ‚‚

/-- The algebra morphism `A â†’â‚[R] A âŠ—[R] B` sending `a` to `a âŠ—â‚œ 1`. -/
@[simps!]
def includeLeft : A â†’â‚[R] ğ’œ áµâŠ—[R] â„¬ where
  toRingHom := includeLeftRingHom ğ’œ â„¬
  commutes' _ := rfl

/-- The algebra morphism `B â†’â‚[R] A âŠ—[R] B` sending `b` to `1 âŠ—â‚œ b`. -/
@[simps!]
def includeRight : B â†’â‚[R] (ğ’œ áµâŠ—[R] â„¬) :=
  AlgHom.ofLinearMap (R := R) (A := B) (B := ğ’œ áµâŠ—[R] â„¬)
    (f := {
       toFun := fun b => 1 áµâŠ—â‚œ b
       map_add' := by simp [tmul, TensorProduct.tmul_add]
       map_smul' := by simp [tmul, TensorProduct.tmul_smul] })
    (map_one := rfl)
    (map_mul := by
      rw [LinearMap.map_mul_iff]
      refine DirectSum.decompose_lhom_ext â„¬ fun iâ‚ => ?_
      ext bâ‚ bâ‚‚ : 2
      dsimp
      rw [tmul_coe_mul_one_tmul])

lemma algebraMap_def' (r : R) : algebraMap R (ğ’œ áµâŠ—[R] â„¬) r = 1 áµâŠ—â‚œ[R] algebraMap R B r :=
  (includeRight ğ’œ â„¬).commutes r |>.symm

variable {C} [Ring C] [Algebra R C]

/-- The forwards direction of the universal property; an algebra morphism out of the graded tensor
product can be assembled from maps on each component that (anti)commute on pure elements of the
corresponding graded algebras. -/
def lift (f : A â†’â‚[R] C) (g : B â†’â‚[R] C)
    (h_anti_commutes : âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), f a * g b = (-1 : â„¤Ë£) ^ (j * i) â€¢ (g b * f a)) :
    (ğ’œ áµâŠ—[R] â„¬) â†’â‚[R] C :=
  AlgHom.ofLinearMap
    (LinearMap.mul' R C
      âˆ˜â‚— (TensorProduct.map f.toLinearMap g.toLinearMap)
      âˆ˜â‚— ((of R ğ’œ â„¬).symm : ğ’œ áµâŠ—[R] â„¬ â†’â‚—[R] A âŠ—[R] B))
    (by
      dsimp [Algebra.TensorProduct.one_def]
      simp only [map_one, mul_one])
    (by
      rw [LinearMap.map_mul_iff]
      ext aâ‚ : 3
      refine DirectSum.decompose_lhom_ext â„¬ fun jâ‚ => ?_
      ext bâ‚ : 3
      refine DirectSum.decompose_lhom_ext ğ’œ fun iâ‚‚ => ?_
      ext aâ‚‚ bâ‚‚ : 2
      dsimp
      rw [tmul_coe_mul_coe_tmul]
      rw [@Units.smul_def _ _ (_) (_), â† Int.cast_smul_eq_zsmul R, map_smul, map_smul, map_smul]
      rw [Int.cast_smul_eq_zsmul R, â† @Units.smul_def _ _ (_) (_)]
      rw [of_symm_of, map_tmul, LinearMap.mul'_apply]
      simp_rw [AlgHom.toLinearMap_apply, map_mul]
      simp_rw [mul_assoc (f aâ‚), â† mul_assoc _ _ (g bâ‚‚), h_anti_commutes, mul_smul_comm,
        smul_mul_assoc, smul_smul, Int.units_mul_self, one_smul])

@[simp]
theorem lift_tmul (f : A â†’â‚[R] C) (g : B â†’â‚[R] C)
    (h_anti_commutes : âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), f a * g b = (-1 : â„¤Ë£) ^ (j * i) â€¢ (g b * f a))
    (a : A) (b : B) :
    lift ğ’œ â„¬ f g h_anti_commutes (a áµâŠ—â‚œ b) = f a * g b :=
  rfl

/-- The universal property of the graded tensor product; every algebra morphism uniquely factors
as a pair of algebra morphisms that anticommute with respect to the grading. -/
def liftEquiv :
    { fg : (A â†’â‚[R] C) Ã— (B â†’â‚[R] C) //
        âˆ€ â¦ƒi jâ¦„ (a : ğ’œ i) (b : â„¬ j), fg.1 a * fg.2 b = (-1 : â„¤Ë£)^(j * i) â€¢ (fg.2 b * fg.1 a)} â‰ƒ
      ((ğ’œ áµâŠ—[R] â„¬) â†’â‚[R] C) where
  toFun fg := lift ğ’œ â„¬ _ _ fg.prop
  invFun F := âŸ¨(F.comp (includeLeft ğ’œ â„¬), F.comp (includeRight ğ’œ â„¬)), fun i j a b => by
    dsimp
    rw [â† map_mul, â† map_mul F, tmul_coe_mul_coe_tmul, one_mul, mul_one, AlgHom.map_smul_of_tower,
      tmul_one_mul_one_tmul, smul_smul, Int.units_mul_self, one_smul]âŸ©
  left_inv fg := by ext <;> (dsimp; simp only [map_one, mul_one, one_mul])
  right_inv F := by
    apply AlgHom.toLinearMap_injective
    ext
    dsimp
    rw [â† map_mul, tmul_one_mul_one_tmul]

/-- Two algebra morphism from the graded tensor product agree if their compositions with the left
and right inclusions agree. -/
@[ext]
lemma algHom_ext â¦ƒf g : (ğ’œ áµâŠ—[R] â„¬) â†’â‚[R] Câ¦„
    (ha : f.comp (includeLeft ğ’œ â„¬) = g.comp (includeLeft ğ’œ â„¬))
    (hb : f.comp (includeRight ğ’œ â„¬) = g.comp (includeRight ğ’œ â„¬)) : f = g :=
  (liftEquiv ğ’œ â„¬).symm.injective <| Subtype.ext <| Prod.ext ha hb

/-- The non-trivial symmetric braiding, sending $a \otimes b$ to
$(-1)^{\deg a' \deg b} (b \otimes a)$. -/
def comm : (ğ’œ áµâŠ—[R] â„¬) â‰ƒâ‚[R] (â„¬ áµâŠ—[R] ğ’œ) :=
  AlgEquiv.ofLinearEquiv
    (auxEquiv R ğ’œ â„¬ â‰ªâ‰«â‚— gradedComm R _ _ â‰ªâ‰«â‚— (auxEquiv R â„¬ ğ’œ).symm)
    (fun x y => by
      dsimp
      simp_rw [auxEquiv_mul, gradedComm_gradedMul, LinearEquiv.symm_apply_eq,
        â† gradedComm_gradedMul, auxEquiv_mul, LinearEquiv.apply_symm_apply, gradedComm_gradedMul])

lemma auxEquiv_comm (x : ğ’œ áµâŠ—[R] â„¬) :
    auxEquiv R â„¬ ğ’œ (comm ğ’œ â„¬ x) = gradedComm R (ğ’œ Â·) (â„¬ Â·) (auxEquiv R ğ’œ â„¬ x) :=
  LinearEquiv.eq_symm_apply _ |>.mp rfl

@[simp] lemma comm_coe_tmul_coe {i j : Î¹} (a : ğ’œ i) (b : â„¬ j) :
    comm ğ’œ â„¬ (a áµâŠ—â‚œ b) = (-1 : â„¤Ë£) ^ (j * i) â€¢ (b áµâŠ—â‚œ a : â„¬ áµâŠ—[R] ğ’œ) :=
  (auxEquiv R â„¬ ğ’œ).injective <| by
    simp_rw [auxEquiv_comm, auxEquiv_tmul, decompose_coe, â† lof_eq_of R, gradedComm_of_tmul_of,
      @Units.smul_def _ _ (_) (_), â† Int.cast_smul_eq_zsmul R]
    -- Qualified `map_smul` to avoid a TC timeout https://github.com/leanprover-community/mathlib4/pull/8386
    rw [LinearEquiv.map_smul, auxEquiv_tmul]
    simp_rw [decompose_coe, lof_eq_of]

end GradedTensorProduct
