/-
Copyright (c) 2023 Mohanad Ahmed. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mohanad Ahmed
-/

import Mathlib.Data.Matrix.ColumnRowPartitioned
import Mathlib.Data.Matrix.Rank
import Mathlib.LinearAlgebra.Matrix.SVD.Reindex

/-! # Singular Value Decomposition

This file provides the SVD theorem which decomposes a real/complex matrix into left
eigenvectors, singular values block diagonal matrix and right eigenvectors.

Any matrix A (M Ã— N) with rank r = A.rank and with elements in the field â„ or â„‚ can be decomposed
into the product of three matrices `A = UÎ¾Vá´´` where:

  * U: an `M Ã— M` matrix containing the left eigenvectors of the matrix
  * Î¾: an `M Ã— N` matrix with an `r Ã— r` block in the upper left corner with nonzero singular values
  * V: an `N Ã— N` matrix containing the right eigenvectors of the matrix

Note that:
  * `Î¾` is a block matrix `S = [Î¾â‚â‚, Î¾â‚â‚‚; Î¾â‚‚â‚, Î¾â‚‚â‚‚]` with
    - `Î¾â‚â‚`: a diagonal `r Ã— r` matrix and
    - `Î¾â‚â‚‚`: r Ã— (N - r) zero matrix, `Î¾â‚‚â‚` : `(M - r) Ã— r` zero matrix and
    - `Î¾â‚‚â‚‚`: (M - r) Ã— (N - r) zero matrix
  * `U` is a block column matrix `U = [Uâ‚ Uâ‚‚]` with
    - `Uâ‚` : a `M Ã— r` containing left eigenvectors with nonzero singular values.
    - `Uâ‚‚` : a `M Ã— (M - r)` containing left eigenvectors with zero singular values.
  * `V` is a block column matrix `V = [Vâ‚ Vâ‚‚]` with
    - `Vâ‚` : a `N Ã— r` containing right eigenvectors with nonzero singular values.
    - `Vâ‚‚` : a `M` Ã— `(M - r)` containing right eigenvectors with zero singular values.

In many linear algebra materials the matrix of singular values `Î¾` is called `Î£`. In Lean4/Mathlib4
this is "reserved" operator. We opted for `Î¾` rather than `S` due to its visual similarity to `Î£`.

Since in mathlib the eigenvalues of hermitian matrices are defined in an "arbitrary" undetermined
order, we begin by partitioning the singular values into zero and non-zero values. We partition the
corresponding eigenvectors from Aá´´A and AAá´´ using similar rearrangements. These are included in
`Mathlib.LinearAlgebra.Matrix.SVD.Reindex`. The basic API for Column and Row partitioned matrices is
 from `ColumnRowPartitioned`.

Recall that `Aá´´A` and `AAá´´` are Hermitian matrices `docs#Matrix.isHermitian_transpose_mul_self` and
`docs#Matrix.isHermitian_mul_conjTranspose_self`. Hence, they are diagonalizable/decomposable as
`Aá´´A = V E Vá´´` with `E` a matrix of eigenvalues of `Aá´´A`. We can apply the reordering/partitioning
from the last paragraph `eigenColumnEquiv` to the columns of `V`, rows and columns of `E` and rows
of `Vá´´`with out changing the value of the product `VEVá´´`. With this in mind we have:
    $$Aá´´A = V Ã— E Ã— Vá´´ = [Vâ‚ Vâ‚‚] [Î¼ 0; 0 0] [Vâ‚á´´; Vâ‚‚á´´] = Vâ‚Î¼Vâ‚á´´$$
where the matrix `Î¼` is a diagonal matrix containing the non-zero eigenvalues of the matrix Aá´´A
ordered so as to correspond to the eigenvectors of Aá´´A. This is given the name
`Vâ‚_mul_Î¼_mul_Vâ‚_conjTranspose` **reduced spectral theorem, right eigenvectors** below.

We can repeat a similar procedure with `AAá´´ = UEUá´´` and obtain:
    $$AAá´´ = U Ã— E Ã— Uá´´ = [Uâ‚ Uâ‚‚] [Î¼' 0; 0 0] [Uâ‚á´´; Uâ‚‚á´´] = Uâ‚Î¼'Uâ‚á´´$$
where the matrix `Î¼'` is a diagonal matrix containing the non-zero eigenvalues of the matrix AAá´´
ordered so as to correspond to the eigenvectors of AAá´´. This is given the name
`Uâ‚'_mul_Î¼'_mul_Uâ‚'_conjTranspose` **reduced spectral theroem, left eigenvectors**

Note that `Î¼` and `Î¼'` contain the same values in different orders.

Note that we chose (arbitrarily) to favor the right-eigenvector order. Hence, the matrix `svdÏƒ` is
also the diagonal matrix whose entries are the square roots of the diagonals of `svdÎ¼`. That is
we define `svdÎ¾ = [svdÏƒ, 0; 0 0]` with appropriate dimensions for the zero matrices. Further the
following equation holds: `svdÎ¾áµ€ * svdÎ¾ = [Î¼, 0; 0 0]`

Lemmas `reduced_spectral_theorem` (`reduced_spectral_theorem'`) shows that Aá´´A and AAá´´ can be
reduced to products containing only the non-zero singular eigenvalue/eigenvectors. This is later
used in proving the main SVD theorem. A few lemmas are provided about the invertibility of the
non-zero singular values matrix: `svdÏƒ_inv`, `Ïƒ_inv_Î¼_Ïƒ_inv_eq_one`, `IsUnit_det_svdÏƒ`,
`IsUnit_det_svdÏƒ_mapK` and `svdÏƒ_inv_mapK`.

We also transfer some of the lemmas we need about eigenvector matrices (for example that
they are unitary: i.e. inverse is conjugate transpose.). Note that since invertibility in mathlib is
defined for square matrices while our matrices are partitioned i.e. N Ã— (Nâ‚ âŠ• Nâ‚‚) and N â‰ƒ (N âŠ• Nâ‚‚)
Lean cannot apply the Invertible definition. We work around this where necessary.

To make relating left eigenvectors to right eigenvectors easier we define Uâ‚ = AVâ‚Ïƒâ»Â¹ while Uâ‚‚ is
obtained from the eigenvectors of (AAá´´). This avoid a lengthy reindexing operation with many proofs.
The vectors in Uâ‚‚ have no such issue since they are multiplied by zero singular values anyway.

## Tags
Singular Value decomposition, SVD
-/

variable {ğ•‚ : Type*} [IsROrC ğ•‚] [DecidableEq ğ•‚]
variable {M N : â„•}

open Matrix BigOperators

namespace Matrix

/-- The right eigenvectors of a matrix A corresponding to its non-zero eigenvalues -/
noncomputable def svdVâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin N) (Fin (A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin N)) (eigenColumnEquiv A))
    (isHermitian_transpose_mul_self A).eigenvectorMatrix).toColumnsâ‚

/-- The right eigenvectors of a matrix A corresponding to the zero eigenvalues of the matrix Aá´´A -/
noncomputable def svdVâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin N) (Fin (N - A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin N)) (eigenColumnEquiv A))
    (isHermitian_transpose_mul_self A).eigenvectorMatrix).toColumnsâ‚‚

/-- The diagonal matrix containing the non-zero eigenvalues of matrix Aá´´ * A These are also the
squares of the non-zero singular values of the matrix A. Note that these are the same elements as in
`svdÎ¼'` but permuted in some arbitrary order -/
noncomputable def svdÎ¼ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (finRankEquivEigsConjTransposeMulSelf A).symm
    (finRankEquivEigsConjTransposeMulSelf A).symm)
  (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
      (isHermitian_transpose_mul_self A).eigenvalues i))

/-- The diagonal matrix containing the non-zero eigenvalues of matrix A * Aá´´ These are also the
squares of the non-zero singular values of the matrix A. Note that these are the same elements as in
`svdÎ¼` but permuted in some arbitrary order -/
noncomputable def svdÎ¼' (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (finRankEquivEigsMulConjTranspose A).symm
    (finRankEquivEigsMulConjTranspose A).symm)
  (diagonal (fun (i : {a // (isHermitian_mul_conjTranspose_self A).eigenvalues a â‰  0}) =>
      (isHermitian_mul_conjTranspose_self A).eigenvalues i))

/-- The diagonal matrix containing the non-zero singular values of matrix A. These are also the
square roots of the non-zero eigenvalues of the matrix Aá´´ * A. -/
noncomputable def svdÏƒ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (finRankEquivEigsConjTransposeMulSelf A).symm
    (finRankEquivEigsConjTransposeMulSelf A).symm)
  (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
      Real.sqrt ((isHermitian_transpose_mul_self A).eigenvalues i)))

/-- The left eigenvectors of a matrix A corresponding to its non-zero eigenvalues, obtained as the
image of the corresponding right eigenvectors. The transformation is given by the matrix itself and
scaling by the singular values. -/
noncomputable def svdUâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin A.rank) ğ•‚ :=
  A * A.svdVâ‚ * (A.svdÏƒ.map (algebraMap â„ ğ•‚))â»Â¹

/-- The left eigenvectors of a matrix A corresponding to its non-zero eigenvalues, obtained directly
from the eigendecomposition of the AAá´´ matrix. These do NOT share the same ordering as `svdUâ‚`. -/
noncomputable def svdUâ‚' (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin (A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin M)) (eigenRowEquiv A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix).toColumnsâ‚

/-- The left eigenvectors of a matrix A corresponding to its zero eigenvalues, obtained directly
from the eigendecomposition of the AAá´´ matrix. The order of these eigenvectors is not relevant as
they are multiplied by zero anyway. Hence we do not have `svdUâ‚‚` -/
noncomputable def svdUâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin (M - A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin M)) (eigenRowEquiv A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix).toColumnsâ‚‚

/-- Concatenation of the left eigenvectors Uâ‚ and Uâ‚‚ into one eigenvector matrix. This is a unitary
matrix. Note however we cannot use `Matrix.unitaryGroup` because the indices of the rows and columns
do not match. -/
noncomputable def svdU (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix (Fin M) (Fin (A.rank) âŠ• Fin (M - A.rank)) ğ•‚ := fromColumns A.svdUâ‚ A.svdUâ‚‚

/-- Concatenation of the right eigenvectors Vâ‚ and Vâ‚‚ into one eigenvector matrix. This is a unitary
matrix. Note however we cannot use `Matrix.unitaryGroup` because the indices of the rows and columns
do not match.-/
noncomputable def svdV (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix (Fin N) (Fin (A.rank) âŠ• Fin (N - A.rank)) ğ•‚ := fromColumns A.svdVâ‚ A.svdVâ‚‚

/-- Given a matrix A of size m Ã— n: `svdÎ¾` is a matrix of the same dimensions but partitioned into
four blocks such that Sâ‚â‚ contains the non-zero singular values, Î¾â‚â‚‚, Î¾â‚‚â‚ and Î¾â‚‚â‚‚ are zeros. The Î¾â‚â‚
block is the diagonal matrix `svdÏƒ` above. -/
noncomputable def svdÎ¾ (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix (Fin (A.rank) âŠ• Fin (M - A.rank)) (Fin (A.rank) âŠ• Fin (N - A.rank)) ğ•‚ :=
  (fromBlocks ((A.svdÏƒ).map (algebraMap â„ ğ•‚)) 0 0 0)

lemma reindex_eigenRowEquiv_eigenvectorMatrix (A : Matrix (Fin M) (Fin N) ğ•‚) :
  ((reindex (Equiv.refl (Fin M)) (eigenRowEquiv A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix) = fromColumns A.svdUâ‚' A.svdUâ‚‚ := by
  rw [svdUâ‚‚, svdUâ‚']
  simp only [reindex_apply, Equiv.refl_symm, Equiv.coe_refl, fromColumns_toColumns]

lemma Vâ‚_conjTranspose_mul_Vâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚á´´ * A.svdVâ‚ = 1 := by
  simp_rw [svdVâ‚, toColumnsâ‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl, submatrix_apply,
    id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply, â† conjTranspose_apply,
    IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _), one_apply,
    EmbeddingLike.apply_eq_iff_eq, Sum.inl.injEq]
  rfl

lemma Vâ‚‚_conjTranspose_mul_Vâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚‚á´´ * A.svdVâ‚‚ = 1 := by
  simp_rw [svdVâ‚‚, toColumnsâ‚‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _), one_apply,
    EmbeddingLike.apply_eq_iff_eq, Sum.inr.injEq]
  rfl

lemma Vâ‚‚_conjTranspose_mul_Vâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚‚á´´ * A.svdVâ‚ = 0 := by
  simp_rw [svdVâ‚‚, toColumnsâ‚‚, svdVâ‚, toColumnsâ‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _), one_apply,
    EmbeddingLike.apply_eq_iff_eq]
  rfl

lemma Vâ‚_conjTranspose_mul_Vâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚á´´ * A.svdVâ‚‚ = 0 := by
  simp_rw [svdVâ‚‚, toColumnsâ‚‚, svdVâ‚, toColumnsâ‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _), one_apply,
    EmbeddingLike.apply_eq_iff_eq]
  rfl

lemma V_conjTranspose_mul_V (A : Matrix (Fin M) (Fin N) ğ•‚) : (A.svdV)á´´ * (A.svdV) = 1 := by
  rw [svdV, conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromRows_mul_fromColumns,
    Vâ‚_conjTranspose_mul_Vâ‚‚, Vâ‚_conjTranspose_mul_Vâ‚, Vâ‚‚_conjTranspose_mul_Vâ‚‚,
    Vâ‚‚_conjTranspose_mul_Vâ‚, fromBlocks_one]

lemma V_mul_conjTranspose_V (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdV * A.svdVá´´ = 1 := by
  rw [svdV, conjTranspose_fromColumns_eq_fromRows_conjTranspose,
    fromColumns_mul_fromRows_eq_one_comm, â† conjTranspose_fromColumns_eq_fromRows_conjTranspose,
    â† svdV, V_conjTranspose_mul_V]
  exact eigenColumnEquiv A

@[simp]
lemma Î¾_toBlocksâ‚â‚ (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdÎ¾.toBlocksâ‚â‚ = A.svdÏƒ.map (algebraMap â„ ğ•‚) := rfl

@[simp]
lemma Î¾_toBlocksâ‚â‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÎ¾.toBlocksâ‚â‚‚ = 0 := rfl

@[simp]
lemma Î¾_toBlocksâ‚‚â‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÎ¾.toBlocksâ‚‚â‚ = 0 := rfl

@[simp]
lemma Î¾_toBlocksâ‚‚â‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÎ¾.toBlocksâ‚‚â‚‚ = 0 := rfl

lemma conjTranspose_svdÏƒ_eq_svdÏƒ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÏƒá´´ = A.svdÏƒ := by
  rw [conjTranspose_eq_transpose_of_trivial, svdÏƒ]
  simp

lemma Î¼_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex (eigenColumnEquiv A) (eigenColumnEquiv A))
      (diagonal ( (isHermitian_transpose_mul_self A).eigenvalues)) =
        fromBlocks A.svdÎ¼ 0 0 0 := by
  rw [reindex_apply, submatrix_diagonal_equiv, svdÎ¼]
  funext i j
  cases' j with j j <;> cases' i with i i <;>
  simp [diagonal_apply]
  rfl

lemma Î¼'_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex (eigenRowEquiv A) (eigenRowEquiv A))
        (diagonal ( (isHermitian_mul_conjTranspose_self A).eigenvalues)) =
          fromBlocks A.svdÎ¼' 0 0 0 := by
  rw [reindex_apply, submatrix_diagonal_equiv, svdÎ¼']
  funext i j
  cases' j with j j <;> cases' i with i i <;>
  simp [diagonal_apply]
  rfl

lemma reindex_eigenColumnEquiv_eigenvectorMatrix (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex (Equiv.refl (Fin N)) (eigenColumnEquiv A))
      (isHermitian_transpose_mul_self A).eigenvectorMatrix =
      fromColumns A.svdVâ‚ A.svdVâ‚‚ := by
  rw [reindex_apply, fromColumns, svdVâ‚, svdVâ‚‚, toColumnsâ‚, toColumnsâ‚‚]
  funext i j
  cases' j with j j <;>
  simp only [Equiv.refl_symm, Equiv.coe_refl, submatrix_apply, id_eq,
    reindex_apply, of_apply, Sum.elim_inl, Sum.elim_inr]

/-- **Reduced spectral theorem**, right eigenvector version. -/
lemma Vâ‚_mul_Î¼_mul_Vâ‚_conjTranspose (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdVâ‚ * (A.svdÎ¼.map (algebraMap â„ ğ•‚)) * A.svdVâ‚á´´ = Aá´´ * A := by
  let hAHA := isHermitian_transpose_mul_self A
  -- "Ugly" (submatrix_mul_equiv) explicit rewrites: each one on its own line for
  -- readability!!
  rw [eq_comm, â† submatrix_id_id (Aá´´ * A), IsHermitian.spectral_theorem' hAHA,
    â† IsHermitian.conjTranspose_eigenvectorMatrix, Matrix.mul_assoc,
    â† submatrix_mul_equiv
      hAHA.eigenvectorMatrix (diagonal (IsROrC.ofReal âˆ˜ hAHA.eigenvalues)  *
      (hAHA.eigenvectorMatrixá´´)) _ (eigenColumnEquiv A).symm _,
    â† submatrix_mul_equiv (diagonal (IsROrC.ofReal âˆ˜ hAHA.eigenvalues)) (hAHA.eigenvectorMatrixá´´)
    _ (eigenColumnEquiv A).symm _,
    â† @IsROrC.algebraMap_eq_ofReal ğ•‚]
  simp_rw [Function.comp]
  rw [â† diagonal_map, submatrix_map, â† reindex_apply, â† Equiv.coe_refl, â† Equiv.refl_symm,
    â† reindex_apply, â†conjTranspose_submatrix, â† reindex_apply, Î¼_block,
    reindex_eigenColumnEquiv_eigenvectorMatrix,
    conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromBlocks_map, fromBlocks_mul_fromRows,
    fromColumns_mul_fromRows]
  simp only [map_zero, Matrix.map_zero, Matrix.zero_mul, add_zero, Matrix.mul_zero]
  rw [Matrix.mul_assoc]
  apply map_zero

/-- **Reduced spectral theorem**, left eigenvector version. -/
lemma Uâ‚'_mul_Î¼'_mul_Uâ‚'_conjTranspose (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdUâ‚' * (A.svdÎ¼'.map (algebraMap â„ ğ•‚)) * A.svdUâ‚'á´´ = A * Aá´´ := by
  let hAAH := isHermitian_mul_conjTranspose_self A
  rw [eq_comm, â† submatrix_id_id (A * Aá´´), IsHermitian.spectral_theorem' hAAH,
    â† IsHermitian.conjTranspose_eigenvectorMatrix, Matrix.mul_assoc,
    â† submatrix_mul_equiv hAAH.eigenvectorMatrix
      (diagonal (IsROrC.ofReal âˆ˜ hAAH.eigenvalues) * (hAAH.eigenvectorMatrixá´´)) _
        (eigenRowEquiv A).symm _,
    â† submatrix_mul_equiv (diagonal (IsROrC.ofReal âˆ˜ hAAH.eigenvalues))
      (hAAH.eigenvectorMatrixá´´) _ (eigenRowEquiv A).symm _,
      â† @IsROrC.algebraMap_eq_ofReal ğ•‚]
  simp_rw [Function.comp]
  rw [â† diagonal_map, submatrix_map,
    â† reindex_apply, â† Equiv.coe_refl, â† Equiv.refl_symm, â† reindex_apply,
    â† conjTranspose_submatrix, â† reindex_apply, Î¼'_block, reindex_eigenRowEquiv_eigenvectorMatrix,
    conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromBlocks_map,
    fromBlocks_mul_fromRows, fromColumns_mul_fromRows]
  simp only [map_zero, Matrix.map_zero, Matrix.zero_mul, add_zero, Matrix.mul_zero]
  rw [Matrix.mul_assoc]
  rw [map_zero]

open scoped ComplexOrder

lemma eigenvalues_conjTranspose_mul_self_nonneg {m n : Type*}
    [Fintype m] [Fintype n] [DecidableEq n] {A : Matrix m n ğ•‚} :
    âˆ€ i , 0 â‰¤ (isHermitian_transpose_mul_self A).eigenvalues i :=
  Matrix.PosSemidef.eigenvalues_nonneg (Matrix.posSemidef_conjTranspose_mul_self _)

lemma eigenvalues_self_mul_conjTranspose_nonneg {m n : Type*}
    [Fintype m] [Fintype n] [DecidableEq m] {A : Matrix m n ğ•‚} :
    âˆ€ i , 0 â‰¤ (isHermitian_mul_conjTranspose_self A).eigenvalues i :=
  Matrix.PosSemidef.eigenvalues_nonneg (Matrix.posSemidef_self_mul_conjTranspose _)

lemma svdÏƒ_inv (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÏƒâ»Â¹ =
    (reindex
      (finRankEquivEigsConjTransposeMulSelf A).symm
      (finRankEquivEigsConjTransposeMulSelf A).symm)
      (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
        1 / Real.sqrt ((isHermitian_transpose_mul_self A).eigenvalues i))) := by
  apply inv_eq_right_inv
  rw [svdÏƒ]
  simp only [ne_eq, reindex_apply, submatrix_diagonal_equiv, diagonal_mul_diagonal,
    Function.comp_apply]
  rw [â† diagonal_one, diagonal_eq_diagonal_iff]
  intros i
  rw [mul_one_div_cancel]
  exact (Real.sqrt_ne_zero (eigenvalues_conjTranspose_mul_self_nonneg _)).2
    ((finRankEquivEigsConjTransposeMulSelf A).1 i).prop

lemma Ïƒ_inv_Î¼_Ïƒ_inv_eq_one (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (A.svdÏƒâ»Â¹) * A.svdÎ¼ * A.svdÏƒâ»Â¹ = 1 := by
  rw [svdÏƒ_inv, svdÎ¼]
  simp only [ne_eq, one_div, reindex_apply, submatrix_diagonal_equiv, diagonal_conjTranspose,
    star_trivial, diagonal_mul_diagonal, Function.comp_apply]
  rw [â† diagonal_one, diagonal_eq_diagonal_iff]
  intro i
  rw [mul_comm, â† mul_assoc, â† mul_inv, Real.mul_self_sqrt, inv_mul_cancel]
  exact ((finRankEquivEigsConjTransposeMulSelf A).1 i).prop
  exact (eigenvalues_conjTranspose_mul_self_nonneg _)

lemma IsUnit_det_svdÏƒ (A : Matrix (Fin M) (Fin N) ğ•‚) : IsUnit (A.svdÏƒ.det) := by
  unfold svdÏƒ
  rw [reindex_apply]
  simp only [ne_eq, submatrix_diagonal_equiv, det_diagonal, Function.comp_apply]
  apply Ne.isUnit _
  exact Finset.prod_ne_zero_iff.2 ( fun i _ =>
    (Real.sqrt_ne_zero (eigenvalues_conjTranspose_mul_self_nonneg _)).2
      ((finRankEquivEigsConjTransposeMulSelf A).1 i).prop)

lemma IsUnit_det_svdÏƒ_mapK (A : Matrix (Fin M) (Fin N) ğ•‚) :
    IsUnit (det (map A.svdÏƒ (algebraMap â„ ğ•‚))) := by
  unfold svdÏƒ
  simp only [ne_eq, reindex_apply, submatrix_diagonal_equiv, map_zero, diagonal_map,
    Function.comp_apply, det_diagonal]
  rw [isUnit_iff_ne_zero, Finset.prod_ne_zero_iff]
  intro i
  simp only [Finset.mem_univ, ne_eq, map_eq_zero, forall_true_left]
  apply (Real.sqrt_ne_zero (eigenvalues_conjTranspose_mul_self_nonneg _)).2
      ((finRankEquivEigsConjTransposeMulSelf A).1 i).prop

lemma svdÏƒ_inv_mapK (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (map (A.svdÏƒ) (algebraMap â„ ğ•‚))â»Â¹ = (map (A.svdÏƒ)â»Â¹ (algebraMap â„ ğ•‚)) := by
  apply inv_eq_left_inv
  rw [â† map_mul, nonsing_inv_mul]
  simp only [map_zero, _root_.map_one, map_one]
  apply IsUnit_det_svdÏƒ

lemma mul_Vâ‚‚_eq_zero (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A * A.svdVâ‚‚ = 0 := by
  suffices h : Aá´´ * A * A.svdVâ‚‚ = 0
  Â· exact (conjTranspose_mul_self_mul_eq_zero _ _).1 h
  rw [â†Vâ‚_mul_Î¼_mul_Vâ‚_conjTranspose, Matrix.mul_assoc, Vâ‚_conjTranspose_mul_Vâ‚‚, Matrix.mul_zero]

lemma Uâ‚_mul_Î¼_mul_Uâ‚_conjTranspose (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdUâ‚ * (A.svdÎ¼.map (algebraMap â„ ğ•‚)) * A.svdUâ‚á´´ = A * Aá´´ := by
  unfold svdUâ‚
  have h1 : A * A.svdVâ‚‚ * (A * A.svdVâ‚‚)á´´ = 0 := by rw [mul_Vâ‚‚_eq_zero, Matrix.zero_mul]
  have h2 : A.svdV * A.svdVá´´ = A.svdVâ‚ * A.svdVâ‚á´´ + A.svdVâ‚‚ * A.svdVâ‚‚á´´ := by
    rw [svdV, conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromColumns_mul_fromRows]

  rw [svdÏƒ_inv_mapK, Matrix.conjTranspose_mul, Matrix.conjTranspose_mul,
    Matrix.mul_assoc (A * A.svdVâ‚) , â† Matrix.map_mul, Matrix.mul_assoc (A * A.svdVâ‚),
    â† Matrix.conjTranspose_map, â† Matrix.mul_assoc _ _ (A.svdVâ‚á´´ * Aá´´), â† Matrix.map_mul,
    conjTranspose_nonsing_inv, conjTranspose_svdÏƒ_eq_svdÏƒ, Ïƒ_inv_Î¼_Ïƒ_inv_eq_one,
    Matrix.map_one _ (RingHom.map_zero _) (RingHom.map_one _), Matrix.one_mul, â† conjTranspose_mul,
    â† add_zero ((A*A.svdVâ‚) * ( A*A.svdVâ‚)á´´), â† h1, Matrix.mul_assoc, Matrix.mul_assoc,
    â† Matrix.mul_add, conjTranspose_mul, conjTranspose_mul, â† Matrix.mul_assoc, â† Matrix.mul_assoc,
    â† Matrix.add_mul, â† h2, V_mul_conjTranspose_V, Matrix.one_mul]
  intros x
  rw [IsROrC.star_def, IsROrC.algebraMap_eq_ofReal, starRingEnd_apply, star_trivial,
    IsROrC.star_def, IsROrC.conj_ofReal]

lemma Uâ‚_conjTranspose_mul_Uâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚á´´ * A.svdUâ‚ = 1 := by
  rw [svdUâ‚, conjTranspose_mul, conjTranspose_mul, Matrix.mul_assoc, Matrix.mul_assoc,
    Matrix.mul_assoc, â† Matrix.mul_assoc Aá´´, â†Vâ‚_mul_Î¼_mul_Vâ‚_conjTranspose, Matrix.mul_assoc,
    â† Matrix.mul_assoc _ A.svdVâ‚, Vâ‚_conjTranspose_mul_Vâ‚, Matrix.one_mul,
    Matrix.mul_assoc A.svdVâ‚, â† Matrix.mul_assoc _ A.svdVâ‚, Vâ‚_conjTranspose_mul_Vâ‚,
    Matrix.one_mul, svdÏƒ_inv_mapK, â† conjTranspose_map, â† Matrix.map_mul, â† Matrix.map_mul,
    â† Matrix.mul_assoc, conjTranspose_nonsing_inv, conjTranspose_svdÏƒ_eq_svdÏƒ, Ïƒ_inv_Î¼_Ïƒ_inv_eq_one]
  simp only [map_zero, _root_.map_one, map_one]
  intros x
  rw [IsROrC.star_def, IsROrC.algebraMap_eq_ofReal, starRingEnd_apply, star_trivial,
    IsROrC.star_def, IsROrC.conj_ofReal]

lemma Uâ‚‚_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚‚á´´ * A.svdUâ‚‚ = 1 := by
  rw [svdUâ‚‚, toColumnsâ‚‚]
  simp only [reindex_apply, Equiv.refl_symm, Equiv.coe_refl, submatrix_apply, id_eq]
  funext i j
  simp_rw [Matrix.mul_apply, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix,
    â† Matrix.mul_apply, Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _)]
  by_cases hij: i = j
  Â· simp_rw [hij, one_apply_eq]
  Â· rw [one_apply_ne hij, one_apply_ne]
    simpa only [ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inr.injEq]

lemma Uâ‚'_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚'á´´ * A.svdUâ‚‚ = 0 := by
  simp_rw [svdUâ‚', svdUâ‚‚, toColumnsâ‚, toColumnsâ‚‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _)]
  funext i j
  simp only [ne_eq, EmbeddingLike.apply_eq_iff_eq, not_false_eq_true, one_apply_ne, zero_apply]

lemma conjTranspose_mul_Uâ‚‚_eq_zero (A : Matrix (Fin M) (Fin N) ğ•‚) : Aá´´ * A.svdUâ‚‚ = 0 := by
  suffices h : A * Aá´´ * A.svdUâ‚‚ = 0
  Â· exact (self_mul_conjTranspose_mul_eq_zero _ _).1 h
  rw [â†Uâ‚'_mul_Î¼'_mul_Uâ‚'_conjTranspose, Matrix.mul_assoc, Uâ‚'_conjTranspose_mul_Uâ‚‚]
  simp only [Matrix.mul_zero]

lemma Uâ‚_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚á´´ * A.svdUâ‚‚ = 0 := by
  unfold svdUâ‚
  simp_rw [conjTranspose_mul, Matrix.mul_assoc, conjTranspose_mul_Uâ‚‚_eq_zero, Matrix.mul_zero]

lemma Uâ‚‚_conjTranspose_mul_Uâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚‚á´´ * A.svdUâ‚ = 0 := by
  rw [â† conjTranspose_conjTranspose (A.svdUâ‚), â† conjTranspose_mul, Uâ‚_conjTranspose_mul_Uâ‚‚,
    conjTranspose_zero]

lemma U_conjTranspose_mul_U (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUá´´ * A.svdU = 1 := by
  rw [svdU, conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromRows_mul_fromColumns,
    Uâ‚_conjTranspose_mul_Uâ‚‚, Uâ‚_conjTranspose_mul_Uâ‚, Uâ‚‚_conjTranspose_mul_Uâ‚‚,
    Uâ‚‚_conjTranspose_mul_Uâ‚, fromBlocks_one]

lemma U_mul_U_conjTranspose (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdU * A.svdUá´´ = 1 := by
  rw [svdU, conjTranspose_fromColumns_eq_fromRows_conjTranspose,
    fromColumns_mul_fromRows_eq_one_comm, â† conjTranspose_fromColumns_eq_fromRows_conjTranspose,
    â†svdU, U_conjTranspose_mul_U ]
  exact eigenRowEquiv A

/-- **Singular Value Decomposition (SVD) Theorem**
Any matrix A (M Ã— N) with rank r = A.rank and  with elements in â„ or â„‚ fields can be decompsed
into three matrices:
  * U: an M Ã— M matrix containing the left eigenvectors of the matrix
  * S: an M Ã— N matrix with an r Ã— r block in the upper left corner with nonzero singular values
  * V: an N Ã— N matrix containing the right eigenvectors of the matrix

Note that UUá´´ = Uá´´U = 1 and VVá´´=Vá´´V = 1 as proven in `V_conjTranspose_mul_V`,`V_mul_conjTranspose_V`
`U_conjTranspose_mul_U` and  `U_mul_U_conjTranspose`-/

theorem U_mul_S_mul_V_conjTranspose (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdU * A.svdÎ¾ * A.svdVá´´ = A := by
  apply_fun (fun x => x * A.svdV)
  simp_rw [svdU, Matrix.mul_assoc, V_conjTranspose_mul_V, Matrix.mul_one, svdÎ¾,
    fromColumns_mul_fromBlocks, svdV, mul_fromColumns, Matrix.mul_zero, add_zero,
    fromColumns_ext_iff, mul_Vâ‚‚_eq_zero, and_true, svdUâ‚,
    Matrix.nonsing_inv_mul_cancel_right _ _ (IsUnit_det_svdÏƒ_mapK _)]
  exact Matrix.mul_left_injective_of_inv _ _ (V_mul_conjTranspose_V _)

end Matrix
