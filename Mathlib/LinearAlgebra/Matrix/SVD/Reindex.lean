/-
Copyright (c) 2023 Mohanad Ahmed. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mohanad Ahmed
-/

import Mathlib.Data.Matrix.Rank
import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # Reindexing using Non-zero/Zero Partition Eigenvalues of AA·¥¥ and A·¥¥A

Given a Fin M √ó Fin N we wish to define equivalences that allow us to move between the following
representations smoothly. Let R be the rank. Then:
  Fin N ‚âÉ (Fin R ‚äï Fin (N - R)) which can be achieved through
  Fin N ‚âÉ {Non-zero Eigs} ‚äï (Zero Eigs) ‚âÉ Fin R ‚äï Fin (N - R)

  Fin M ‚âÉ (Fin R ‚äï Fin (M - R)) which can be achieved through
  Fin M ‚âÉ {Non-zero Eigs} ‚äï (Zero Eigs) ‚âÉ Fin R ‚äï Fin (M - R)

  Note that we know R ‚â§ MIN(M, N) -/
variable {ùïÇ: Type*} [IsROrC ùïÇ] [DecidableEq ùïÇ]
variable {M N : ‚Ñï}

open Matrix BigOperators

namespace Matrix

open scoped ComplexOrder

lemma rank_eq_card_pos_eigs_conjTranspose_mul_self (A : Matrix (Fin M) (Fin N) ùïÇ) :
    A.rank = Fintype.card {i // (isHermitian_transpose_mul_self A).eigenvalues i ‚â† 0} := by
  rw [‚Üê rank_conjTranspose_mul_self, IsHermitian.rank_eq_card_non_zero_eigs]

lemma rank_eq_card_pos_eigs_self_mul_conjTranspose (A : Matrix (Fin M) (Fin N) ùïÇ) :
    A.rank = Fintype.card {i // (isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0} := by
  rw [‚Üê rank_self_mul_conjTranspose, IsHermitian.rank_eq_card_non_zero_eigs]

/-- For matrix A of size m √ó n and rank A.rank : we have a bijection between elements of
Fin (A.rank) and the set non-zero eigenvalues of the matrix A·¥¥‚¨ùA -/
noncomputable def finRankEquivEigsConjTransposeMulSelf (A : Matrix (Fin M) (Fin N) ùïÇ) :
    Fin (A.rank) ‚âÉ {i // (isHermitian_transpose_mul_self A).eigenvalues i ‚â† 0} :=
  (Fintype.equivFinOfCardEq (rank_eq_card_pos_eigs_conjTranspose_mul_self A).symm).symm

/-- For matrix A of size m √ó n and rank A.rank : we have an bijection between elements of
Fin (A.rank) and the set non-zero eigenvalues of the matrix A‚¨ùA·¥¥ -/
noncomputable def finRankEquivEigsMulConjTranspose (A: Matrix (Fin M) (Fin N) ùïÇ) :
    Fin (A.rank) ‚âÉ {i // (isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0} :=
  (Fintype.equivFinOfCardEq (rank_eq_card_pos_eigs_self_mul_conjTranspose A).symm).symm

/-- For matrix of size m √ó n and rank A.rank : we have an bijection the elements
of Fin n and the eigenvalues of the matrix A·¥¥‚¨ùA, partitioned into
- non-zero eigenvalues: (exactly A.rank) of them, see `fin_rank_equiv_eigs_conjTranspose_mul_self`
- zero eigenvalues: (exactly n - A.rank) of them -/
noncomputable def eigenColumnEquiv (A : Matrix (Fin M) (Fin N) ùïÇ) :
    (Fin N) ‚âÉ (Fin A.rank) ‚äï (Fin (N - A.rank)) := by
  let en := Equiv.sumCompl (fun i =>  (isHermitian_transpose_mul_self A).eigenvalues i ‚â† 0)
  let e‚Çô·µ£ : {i // ¬¨(isHermitian_transpose_mul_self A).eigenvalues i ‚â† 0} ‚âÉ Fin (N - A.rank) :=
    Fintype.equivFinOfCardEq (by rw [Fintype.card_subtype_compl, Fintype.card_fin,
      rank_eq_card_pos_eigs_conjTranspose_mul_self])
  exact Equiv.trans en.symm  (Equiv.sumCongr (finRankEquivEigsConjTransposeMulSelf A).symm e‚Çô·µ£)

/-- For matrix of size m √ó n and rank A.rank : we have an bijeciton between the elements
of Fin (height) and the eigevalues of the matrix A‚¨ùA·¥¥ partitioned into
- non-zero eigenvalues: (exactly A.rank) of them see `fin_rank_equiv_eigs_mul_conjTranspose`
- zero eigenvalues: (exactly height - A.rank) of them -/
noncomputable def eigenRowEquiv (A : Matrix (Fin M) (Fin N) ùïÇ) :
    (Fin M) ‚âÉ (Fin A.rank) ‚äï (Fin (M - A.rank)) := by
  let em := Equiv.sumCompl (fun i =>  (isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0)
  let e·µ£' : {i // (isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0} ‚âÉ Fin A.rank :=
    Fintype.equivFinOfCardEq (by rw [rank_eq_card_pos_eigs_self_mul_conjTranspose])
  let e‚Çò·µ£ : {i // ¬¨(isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0} ‚âÉ Fin (M - A.rank) :=
    Fintype.equivFinOfCardEq (by rw [Fintype.card_subtype_compl, Fintype.card_fin,
      rank_eq_card_pos_eigs_self_mul_conjTranspose])
  exact Equiv.trans em.symm  (Equiv.sumCongr e·µ£' e‚Çò·µ£)

/-- When the eigenvalues of the matrix A·¥¥‚¨ùA are partitioned using
`equiv_fin_width_eigs_conjTranspose_mul_self` i.e. into non-zero and zero eigenvalues, any element
from the second partition is obviously zero! -/
@[simp]
lemma eigen_eigenColumnEquiv_inr (A : Matrix (Fin M) (Fin N) ùïÇ) (i: Fin (N - A.rank)) :
  (isHermitian_transpose_mul_self A).eigenvalues
    ((eigenColumnEquiv A).symm (Sum.inr i)) = 0 := by
  unfold eigenColumnEquiv Equiv.sumCongr
  simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk, Sum.elim_inr,
    Equiv.sumCompl_apply_inr]
  let e‚Çô·µ£ : {i // ¬¨(isHermitian_transpose_mul_self A).eigenvalues i ‚â† 0} ‚âÉ Fin (N - A.rank) := by
    apply Fintype.equivFinOfCardEq
    rw [Fintype.card_subtype_compl, Fintype.card_fin, rank_eq_card_pos_eigs_conjTranspose_mul_self]
  exact Iff.mp Function.nmem_support ((e‚Çô·µ£.symm i).prop)

/-- When the eigenvalues of the matrix A‚¨ùA·¥¥ are partitioned using
`equiv_fin_height_eigs_mul_conjTranspose` i.e. into non-zero and zero eigenvalues, any element from
the second partition is obviously zero! -/
@[simp]
lemma eigen_eigenRowEquiv_inr (A : Matrix (Fin M) (Fin N) ùïÇ) (i: Fin (M - A.rank)) :
    (isHermitian_mul_conjTranspose_self A).eigenvalues
      ((eigenRowEquiv A).symm (Sum.inr i)) = 0 := by
  unfold eigenRowEquiv Equiv.sumCongr
  simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk, Sum.elim_inr,
    Equiv.sumCompl_apply_inr]
  let e‚Çò·µ£ : {i // ¬¨(isHermitian_mul_conjTranspose_self A).eigenvalues i ‚â† 0} ‚âÉ
    Fin (M - A.rank) := by
    apply Fintype.equivFinOfCardEq
    rw [Fintype.card_subtype_compl, Fintype.card_fin, rank_eq_card_pos_eigs_self_mul_conjTranspose]
  exact Iff.mp Function.nmem_support ((e‚Çò·µ£.symm i).prop)

end Matrix
