/-
Copyright (c) 2023 Mohanad ahmed. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mohanad Ahmed
-/


import Mathlib.Data.Matrix.Rank
import Mathlib.Data.Matrix.ColumnRowPartitioned
import Mathlib.LinearAlgebra.Matrix.SVD.svdReindex



/-! # Singular Value Decomposition

This file provides proves the SVD theorem which decomposes a real/complex matrix into left
eigenvectors, singular values block diagonal matrix and right eigenvectors.

Any matrix A (M Ã— N) with rank r = A.rank and  with elements in â„ or â„‚ fields can be decompsed
into three matrices:
  U: an M Ã— M matrix containing the left eigenvectors of the matrix
  S: an M Ã— N matrix with an r Ã— r block in the upper left corner with nonzero singular values
  V: an N Ã— N matrix containing the right eigenvectors of the matrix
  Note that
  S is a block matrix S = [Sâ‚â‚, Sâ‚â‚‚; Sâ‚‚â‚, Sâ‚‚â‚‚] with
  - Sâ‚â‚: a diagonal r Ã— r matrix and
  - Sâ‚â‚‚: r Ã— (N - r) zero matrix, Sâ‚‚â‚ : (M - r) Ã— r zero matrix and
  - Sâ‚‚â‚‚: (M - r) Ã— (N - r) zero matrix
  U is a block column matrix U = [Uâ‚ Uâ‚‚] with
  - Uâ‚ : a M Ã— r containing left eigenvectors with nonzero singular values.
  - Uâ‚‚ : a M Ã— (M - r) containing left eigenvectors with zero singular values.
  V is a block column matrix V = [Vâ‚ Vâ‚‚] with
  - Vâ‚ : a N Ã— r containing right eigenvectors with nonzero singular values.
  - Vâ‚‚ : a M Ã— (M - r) containing right eigenvectors with zero singular values.

Since in mathlib the eigenvalues of hermitian matrices are defined in an "arbitrary" undetermined
order, we begin by partitioning the singular values into zero and non-zero values. We partition the
corresponding eigenvectors from Aá´´A and AAá´´ using similar rearrangements. These are included in
`SVD.svdReindex`. The basic API for Column and Row partitioned matrices is from
`ColumnRowPartitioned`.

We then proceed to transfer some of the lemmas we need about eigenvector matrices (for example that
they are unitary: i.e. inverse is conjugate transpose.). Note that since invertibility in mathlib is
defined for square matrices while our matrices are partitioned i.e. N Ã— (Nâ‚ âŠ• Nâ‚‚) and N â‰ƒ (N âŠ• Nâ‚‚)
Lean cannot apply the Invertible definition. We workaround this were necessary.

Lemma `reduced_spectral_theorem` (`reduced_spectral_theorem'`) shows that Aá´´A and AAá´´, can be
reduced to products containing only the non-zero singular eigenvectors. This is later used in
proving the main SVD theroem. A few lemmas are provided about the invertibility of the non-zero
singular values matrix: `svdÏƒ_inv`, `Ïƒ_inv_Î¼_Ïƒ_inv_eq_one`, `IsUnit_det_svdÏƒ`,
`IsUnit_det_svdÏƒ_mapK` and `svdÏƒ_inv_mapK`.

To make relating left eigenvectors to right eigenvectors easier we define Uâ‚ = AVâ‚Ïƒâ»Â¹ while Uâ‚‚ is
obtained from the eigenvectors of (AAá´´). This avoid a lengthy reindexing operation with many proofs.
The vectors in Uâ‚‚ have no such issue since they are multiplied by zero singular values anyway.

## Tags
Singular Value decomposition, SVD
-/


variable {ğ•‚: Type}[IsROrC ğ•‚][DecidableEq ğ•‚]
variable {M N: â„•}

open Matrix BigOperators

namespace Matrix

/-- The right eigenvectors of a matrix A corresponding to its non-zero eigenvaules -/
noncomputable def svdVâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin N) (Fin (A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin N)) (equiv_fin_width_eigs_conjTranspose_mul_self A))
    (isHermitian_transpose_mul_self A).eigenvectorMatrix).toColumnsâ‚

/-- The right eigenvectors of a matrix A corresponding to the zero eigenvaules of the matrix Aá´´A -/
noncomputable def svdVâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin N) (Fin (N - A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin N)) (equiv_fin_width_eigs_conjTranspose_mul_self A))
    (isHermitian_transpose_mul_self A).eigenvectorMatrix).toColumnsâ‚‚

/-- The diagonal matrix containing the non-zero eigenvalues of matrix Aá´´ * A These are also the
squares of the non-zero singular values of the matrix A. Note that these are the same elements as in
`svdÎ¼'` but permuted in some arbitrary order -/
noncomputable def svdÎ¼ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (fin_rank_equiv_eigs_conjTranspose_mul_self A)
    (fin_rank_equiv_eigs_conjTranspose_mul_self A))
  (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
      (isHermitian_transpose_mul_self A).eigenvalues i))

/-- The diagonal matrix containing the non-zero eigenvalues of matrix A * Aá´´ These are also the
squares of the non-zero singular values of the matrix A. Note that these are the same elements as in
`svdÎ¼` but permuted in some arbitrary order -/
noncomputable def svdÎ¼' (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (fin_rank_equiv_eigs_mul_conjTranspose A) (fin_rank_equiv_eigs_mul_conjTranspose A))
  (diagonal (fun (i : {a // (isHermitian_mul_conjTranspose_self A).eigenvalues a â‰  0}) =>
      (isHermitian_mul_conjTranspose_self A).eigenvalues i))

/-- The diagonal matrix containing the non-zero singular values of matrix A. These are also the
square roots of the non-zero eigenvalues of the matrix Aá´´ * A. -/
noncomputable def svdÏƒ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin A.rank) (Fin A.rank) â„ :=
  (reindex
    (fin_rank_equiv_eigs_conjTranspose_mul_self A)
    (fin_rank_equiv_eigs_conjTranspose_mul_self A))
  (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
      Real.sqrt ((isHermitian_transpose_mul_self A).eigenvalues i)))

/-- The left eigenvectors of a matrix A corresponding to its non-zero eigenvaules, obtained as the
image of the corresponding right eigenvectors. The transformation is given by the matrix itself and
scaling by the singular values. -/
noncomputable def svdUâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin A.rank) ğ•‚ :=
  A  *  A.svdVâ‚  *  (A.svdÏƒ.map (algebraMap â„ ğ•‚))â»Â¹

/-- The left eigenvectors of a matrix A corresponding to its non-zero eigenvaules, obtained directly
from the eigendecomposition of the AAá´´ matrix. These do NOT share the same ordering as `svdUâ‚`. -/
noncomputable def svdUâ‚' (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin (A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin M)) (equiv_fin_height_eigs_mul_conjTranspose A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix).toColumnsâ‚

/-- The left eigenvectors of a matrix A corresponding to its zero eigenvaules, obtained directly
from the eigendecomposition of the AAá´´ matrix. The order of these eigenvectors is not relevatn as
they are multiplied by zero anyway. Hence we do not have `svdUâ‚‚` -/
noncomputable def svdUâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : Matrix (Fin M) (Fin (M - A.rank)) ğ•‚ :=
  ((reindex (Equiv.refl (Fin M)) (equiv_fin_height_eigs_mul_conjTranspose A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix).toColumnsâ‚‚

/-- Concatenation of the left eigenvectors Uâ‚ and Uâ‚‚ into one eigenvector matrix. This is a unitary
matrix. -/
noncomputable def svdU (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix (Fin M) (Fin (A.rank) âŠ• Fin (M - A.rank)) ğ•‚ := fromColumns A.svdUâ‚ A.svdUâ‚‚

/-- Concatenation of the right eigenvectors Vâ‚ and Vâ‚‚ into one eigenvector matrix. This is a unitary
matrix. -/
noncomputable def svdV (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix (Fin N) (Fin (A.rank) âŠ• Fin (N - A.rank)) ğ•‚ := fromColumns A.svdVâ‚ A.svdVâ‚‚

/-- Given a matrix A of size m Ã— n: `svdS` is a matrix of the same dimensions but partitioned into
four blocks such that Sâ‚â‚ contains the non-zero singular values, Sâ‚â‚‚, Sâ‚‚â‚ and Sâ‚‚â‚‚ are zeros. The Sâ‚â‚
block is the diagonal matrix `svdÏƒ` above. -/
noncomputable def svdS (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix ((Fin A.rank) âŠ• (Fin (N - A.rank))) ((Fin A.rank) âŠ• (Fin (N - A.rank))) â„ :=
  (reindex
    (equiv_fin_width_eigs_conjTranspose_mul_self A) (equiv_fin_width_eigs_conjTranspose_mul_self A))
    (diagonal (isHermitian_transpose_mul_self A).eigenvalues)

/-- Given a matrix A of size m Ã— n: `svdS'` is a matrix of the same dimensions but partitioned into
four blocks such that Sâ‚â‚ contains the non-zero singular values, Sâ‚â‚‚, Sâ‚‚â‚ and Sâ‚‚â‚‚ are zeros. The Sâ‚â‚
block is the diagonal matrix `svdÏƒ` above but permuted by the same arbitrary order relating svdUâ‚
and svdUâ‚' above . -/
noncomputable def svdS' (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Matrix ((Fin A.rank) âŠ• (Fin (M - A.rank))) ((Fin A.rank) âŠ• (Fin (M - A.rank))) â„ :=
  (reindex (equiv_fin_height_eigs_mul_conjTranspose A) (equiv_fin_height_eigs_mul_conjTranspose A))
    (diagonal (isHermitian_mul_conjTranspose_self A).eigenvalues)

lemma U_columns' (A : Matrix (Fin M) (Fin N) ğ•‚) :
  ((reindex (Equiv.refl (Fin M)) (equiv_fin_height_eigs_mul_conjTranspose A))
    (isHermitian_mul_conjTranspose_self A).eigenvectorMatrix) = fromColumns A.svdUâ‚' A.svdUâ‚‚ := by
  rw [svdUâ‚‚, svdUâ‚']
  simp only [reindex_apply, Equiv.refl_symm, Equiv.coe_refl, fromColumns_toColumns]

lemma V_conjTranspose_mul_V (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (A.svdVâ‚á´´  *  A.svdVâ‚ = 1 âˆ§ A.svdVâ‚‚á´´  *  A.svdVâ‚‚ = 1) âˆ§
    (A.svdVâ‚‚á´´  *  A.svdVâ‚ = 0 âˆ§ A.svdVâ‚á´´  *  A.svdVâ‚‚ = 0) := by
  simp_rw [svdVâ‚, toColumnsâ‚, svdVâ‚‚, toColumnsâ‚‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _)]
  constructor
  swap
  simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, EmbeddingLike.apply_eq_iff_eq,
   not_false_eq_true, one_apply_ne, zero_apply]
  exact âŸ¨rfl, rflâŸ©
  constructor
  all_goals (
    funext i j
    by_cases i = j
    simp_rw [h, one_apply_eq, one_apply_ne h]
    rw [one_apply_ne]
    simp only [ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inr.injEq, Sum.inl.injEq, h]
  )

lemma Vâ‚_conjTranspose_mul_Vâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚á´´ * A.svdVâ‚ = 1 :=
  (V_conjTranspose_mul_V A).1.1

lemma Vâ‚‚_conjTranspose_mul_Vâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚‚á´´ * A.svdVâ‚‚ = 1 :=
  (V_conjTranspose_mul_V A).1.2

lemma Vâ‚‚_conjTranspose_mul_Vâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚‚á´´ * A.svdVâ‚ = 0 :=
  (V_conjTranspose_mul_V A).2.1

lemma Vâ‚_conjTranspose_mul_Vâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdVâ‚á´´ * A.svdVâ‚‚ = 0 :=
  (V_conjTranspose_mul_V A).2.2

lemma V_inv (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (fromColumns A.svdVâ‚ A.svdVâ‚‚)á´´  *  (fromColumns A.svdVâ‚ A.svdVâ‚‚) = 1 := by
  rw [conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromRows_mul_fromColumns,
    Vâ‚_conjTranspose_mul_Vâ‚‚, Vâ‚_conjTranspose_mul_Vâ‚, Vâ‚‚_conjTranspose_mul_Vâ‚‚,
    Vâ‚‚_conjTranspose_mul_Vâ‚, fromBlocks_one]

lemma S_zero_blocks (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdS.toBlocksâ‚â‚‚ = 0 âˆ§ A.svdS.toBlocksâ‚‚â‚ = 0 âˆ§ A.svdS.toBlocksâ‚‚â‚‚ = 0 := by
  unfold toBlocksâ‚â‚‚ toBlocksâ‚‚â‚ toBlocksâ‚‚â‚‚ svdS
  simp only [reindex_apply, submatrix_apply, ne_eq, EmbeddingLike.apply_eq_iff_eq,
    not_false_eq_true, diagonal_apply_ne]
  simp_rw [â† Matrix.ext_iff, of_apply, zero_apply, implies_true, true_and ]
  intro i j
  by_cases i = j
  unfold equiv_fin_width_eigs_conjTranspose_mul_self Equiv.sumCongr
  simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk, Sum.elim_inr,
    Equiv.sumCompl_apply_inr]
  simp_rw [h, diagonal_apply_eq]
  apply enz_nr_zero
  rw [diagonal_apply_ne]
  exact enz_inj _ _ _ h

lemma S'_zero_blocks (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdS'.toBlocksâ‚â‚‚ = 0 âˆ§ A.svdS'.toBlocksâ‚‚â‚ = 0 âˆ§ A.svdS'.toBlocksâ‚‚â‚‚ = 0 := by
  unfold toBlocksâ‚â‚‚ toBlocksâ‚‚â‚ toBlocksâ‚‚â‚‚ svdS'
  simp only [reindex_apply, submatrix_apply, ne_eq, EmbeddingLike.apply_eq_iff_eq,
    not_false_eq_true, diagonal_apply_ne]
  simp_rw [â† Matrix.ext_iff, of_apply, zero_apply, implies_true, true_and ]
  intro i j
  by_cases i = j
  Â· unfold equiv_fin_height_eigs_mul_conjTranspose Equiv.sumCongr
    simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk, Sum.elim_inr,
      Equiv.sumCompl_apply_inr]
    simp_rw [h, diagonal_apply_eq]
    apply emz_mr_zero
  Â· rw [diagonal_apply_ne]
    exact emz_inj _ _ _ h

lemma S_Ïƒpos_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdS.toBlocksâ‚â‚ = A.svdÎ¼ := by
  unfold toBlocksâ‚â‚ svdÎ¼ svdS
  simp only [reindex_apply, submatrix_apply, ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inl.injEq,
    submatrix_diagonal_equiv]
  funext i j
  by_cases h: i=j
  Â· simp_rw [h]
    unfold equiv_fin_width_eigs_conjTranspose_mul_self fin_rank_equiv_eigs_conjTranspose_mul_self
      Equiv.sumCongr Sum.map
    simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk,
      Sum.elim_inl, Equiv.sumCompl_apply_inl, of_apply,
      diagonal_apply_eq, Function.comp_apply]
  Â· rw [diagonal_apply_ne, of_apply, diagonal_apply_ne]
    rw [ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inl.injEq]
    assumption'

lemma S'_Ïƒpos_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdS'.toBlocksâ‚â‚ = A.svdÎ¼' := by
  unfold toBlocksâ‚â‚ svdÎ¼' svdS'
  simp only [reindex_apply, submatrix_apply, ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inl.injEq,
    submatrix_diagonal_equiv]
  funext i j
  by_cases h: i=j
  Â· simp_rw [h]
    unfold equiv_fin_height_eigs_mul_conjTranspose fin_rank_equiv_eigs_mul_conjTranspose
      Equiv.sumCongr Sum.map
    simp only [ne_eq, Equiv.symm_trans_apply, Equiv.symm_symm, Equiv.coe_fn_symm_mk,
      Sum.elim_inl, Equiv.sumCompl_apply_inl, of_apply,
      diagonal_apply_eq, Function.comp_apply]
  Â· rw [diagonal_apply_ne, of_apply, diagonal_apply_ne]
    rw [ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inl.injEq]
    assumption'

lemma S_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex
      (equiv_fin_width_eigs_conjTranspose_mul_self A)
      (equiv_fin_width_eigs_conjTranspose_mul_self A))
        ( diagonal ( (isHermitian_transpose_mul_self A).eigenvalues)) =
          fromBlocks A.svdÎ¼ 0 0 0 := by
  let hz := S_zero_blocks A
  rw [â† svdS, â† fromBlocks_toBlocks (A.svdS), â† S_Ïƒpos_block, hz.1, hz.2.1, hz.2.2]

lemma S'_block (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex
      (equiv_fin_height_eigs_mul_conjTranspose A)
      (equiv_fin_height_eigs_mul_conjTranspose A))
        (diagonal ( (isHermitian_mul_conjTranspose_self A).eigenvalues)) =
          fromBlocks A.svdÎ¼' 0 0 0 := by
  let hz := S'_zero_blocks A
  rw [â† svdS', â† fromBlocks_toBlocks (A.svdS'), â† S'_Ïƒpos_block, hz.1, hz.2.1, hz.2.2]

lemma V_columns (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (reindex (Equiv.refl (Fin N)) (equiv_fin_width_eigs_conjTranspose_mul_self A))
      (isHermitian_transpose_mul_self A).eigenvectorMatrix =
      fromColumns A.svdVâ‚ A.svdVâ‚‚ := by
  rw [reindex_apply, fromColumns, svdVâ‚, svdVâ‚‚, toColumnsâ‚, toColumnsâ‚‚]
  funext i j
  cases' j with j j <;>
  simp only [Equiv.refl_symm, Equiv.coe_refl, submatrix_apply, id_eq,
    reindex_apply, of_apply, Sum.elim_inl, Sum.elim_inr]

lemma reduced_spectral_theorem (A : Matrix (Fin M) (Fin N) ğ•‚) :
    Aá´´  *  A = A.svdVâ‚  *  (A.svdÎ¼.map (algebraMap â„ ğ•‚))  *  A.svdVâ‚á´´ := by
  let hAHA := isHermitian_transpose_mul_self A
  -- "Ugly" (submatrix_mul_equiv) explicit rewrites: each one on its own line for
  -- readability!!
  rw [â† submatrix_id_id (Aá´´ * A), IsHermitian.spectral_theorem' hAHA,
    â† IsHermitian.conjTranspose_eigenvectorMatrix, Matrix.mul_assoc,
    â† submatrix_mul_equiv
      hAHA.eigenvectorMatrix (diagonal (IsROrC.ofReal âˆ˜ hAHA.eigenvalues)  *
      (hAHA.eigenvectorMatrixá´´)) _ (equiv_fin_width_eigs_conjTranspose_mul_self A).symm _,
    â† submatrix_mul_equiv (diagonal (IsROrC.ofReal âˆ˜ hAHA.eigenvalues)) (hAHA.eigenvectorMatrixá´´)
    _ (equiv_fin_width_eigs_conjTranspose_mul_self A).symm _,
    â† @IsROrC.algebraMap_eq_ofReal ğ•‚]
  simp_rw [Function.comp]
  rw [â† diagonal_map, submatrix_map, â† reindex_apply, â† Equiv.coe_refl, â† Equiv.refl_symm,
    â† reindex_apply, â†conjTranspose_submatrix, â† reindex_apply, S_block, V_columns,
    conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromBlocks_map, fromBlocks_mul_fromRows,
    fromColumns_mul_fromRows]
  simp only [map_zero, Matrix.map_zero, Matrix.zero_mul, add_zero, Matrix.mul_zero]
  rw [Matrix.mul_assoc]
  apply map_zero

lemma reduced_spectral_theorem' (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A  *  Aá´´ = A.svdUâ‚'  *  (A.svdÎ¼'.map (algebraMap â„ ğ•‚))  *  A.svdUâ‚'á´´ := by
  let hAAH := isHermitian_mul_conjTranspose_self A
  rw [â† submatrix_id_id (A * Aá´´), IsHermitian.spectral_theorem' hAAH,
    â† IsHermitian.conjTranspose_eigenvectorMatrix, Matrix.mul_assoc,
    â† submatrix_mul_equiv hAAH.eigenvectorMatrix
      (diagonal (IsROrC.ofReal âˆ˜ hAAH.eigenvalues)  *  (hAAH.eigenvectorMatrixá´´)) _
        (equiv_fin_height_eigs_mul_conjTranspose A).symm _,
    â† submatrix_mul_equiv (diagonal (IsROrC.ofReal âˆ˜ hAAH.eigenvalues))
      (hAAH.eigenvectorMatrixá´´) _ (equiv_fin_height_eigs_mul_conjTranspose A).symm _,
      â† @IsROrC.algebraMap_eq_ofReal ğ•‚]
  simp_rw [Function.comp]
  rw [â† diagonal_map, submatrix_map,
    â† reindex_apply, â† Equiv.coe_refl, â† Equiv.refl_symm, â† reindex_apply,
    â† conjTranspose_submatrix, â† reindex_apply, S'_block, U_columns',
    conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromBlocks_map,
    fromBlocks_mul_fromRows, fromColumns_mul_fromRows]
  simp only [map_zero, Matrix.map_zero, Matrix.zero_mul, add_zero, Matrix.mul_zero]
  rw [Matrix.mul_assoc]
  rw [map_zero]

open scoped ComplexOrder
lemma eig_vals_ne_zero_pos {m n: Type} [Fintype m] [Fintype n] [DecidableEq n]
    (A : Matrix m n ğ•‚) (z: {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0 }) :
    0 < ((isHermitian_transpose_mul_self A).eigenvalues z) :=
  lt_of_le_of_ne
    (Matrix.PosSemidef.eigenvalues_nonneg (posSemidef_conjTranspose_mul_self _) _) -- 0 â‰¤ _
    (z.prop.symm) -- 0 â‰  _

lemma svdÏƒ_inv (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdÏƒâ»Â¹ =
    (reindex
      (fin_rank_equiv_eigs_conjTranspose_mul_self A)
      (fin_rank_equiv_eigs_conjTranspose_mul_self A))
      (diagonal (fun (i : {a // (isHermitian_transpose_mul_self A).eigenvalues a â‰  0}) =>
        1 / Real.sqrt ((isHermitian_transpose_mul_self A).eigenvalues i))) := by
  apply inv_eq_right_inv
  rw [svdÏƒ]
  simp only [ne_eq, reindex_apply, submatrix_diagonal_equiv, diagonal_mul_diagonal,
    Function.comp_apply]
  rw [â† diagonal_one, diagonal_eq_diagonal_iff]
  intros i
  rw [mul_one_div_cancel]
  apply ne_of_gt
  apply (Real.sqrt_pos.2 (eig_vals_ne_zero_pos _ _))

lemma Ïƒ_inv_Î¼_Ïƒ_inv_eq_one (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (A.svdÏƒâ»Â¹)á´´  *  A.svdÎ¼  *  A.svdÏƒâ»Â¹ = 1 := by
  rw [svdÏƒ_inv, svdÎ¼]
  simp only [ne_eq, one_div, reindex_apply, submatrix_diagonal_equiv, diagonal_conjTranspose,
    star_trivial, diagonal_mul_diagonal, Function.comp_apply]
  rw [â† diagonal_one, diagonal_eq_diagonal_iff]
  intro i
  rw [mul_comm, â† mul_assoc, â† mul_inv, Real.mul_self_sqrt, inv_mul_cancel]
  apply ne_of_gt (eig_vals_ne_zero_pos A _)
  apply le_of_lt (eig_vals_ne_zero_pos A _)

lemma IsUnit_det_svdÏƒ (A : Matrix (Fin M) (Fin N) ğ•‚) : IsUnit (A.svdÏƒ.det) := by
  unfold svdÏƒ
  rw [reindex_apply]
  simp only [ne_eq, submatrix_diagonal_equiv, det_diagonal, Function.comp_apply]
  apply Ne.isUnit (Finset.prod_ne_zero_iff.2
    (fun i _ => (ne_of_gt (Real.sqrt_pos.2 (eig_vals_ne_zero_pos _ _)))))

lemma IsUnit_det_svdÏƒ_mapK (A : Matrix (Fin M) (Fin N) ğ•‚) :
    IsUnit (det (map A.svdÏƒ (algebraMap â„ ğ•‚))) := by
  unfold svdÏƒ
  simp only [ne_eq, reindex_apply, submatrix_diagonal_equiv, map_zero, diagonal_map,
    Function.comp_apply, det_diagonal]
  rw [isUnit_iff_ne_zero, Finset.prod_ne_zero_iff]
  intro i
  simp only [Finset.mem_univ, ne_eq, map_eq_zero, forall_true_left]
  apply (ne_of_gt (Real.sqrt_pos.2 (eig_vals_ne_zero_pos _ _)))

lemma svdÏƒ_inv_mapK (A : Matrix (Fin M) (Fin N) ğ•‚) :
    (map (A.svdÏƒ) (algebraMap â„ ğ•‚))â»Â¹ = (map (A.svdÏƒ)â»Â¹ (algebraMap â„ ğ•‚)) := by
  apply inv_eq_left_inv
  rw [â† map_mul, nonsing_inv_mul]
  simp only [map_zero, _root_.map_one, map_one]
  apply IsUnit_det_svdÏƒ

lemma Uâ‚_conjTranspose_mul_Uâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚á´´  *  A.svdUâ‚ = 1 := by
  rw [svdUâ‚, conjTranspose_mul, conjTranspose_mul, Matrix.mul_assoc, Matrix.mul_assoc,
    Matrix.mul_assoc, â† Matrix.mul_assoc Aá´´, reduced_spectral_theorem, Matrix.mul_assoc,
    â† Matrix.mul_assoc _ A.svdVâ‚, Vâ‚_conjTranspose_mul_Vâ‚, Matrix.one_mul,
    Matrix.mul_assoc A.svdVâ‚, â† Matrix.mul_assoc _ A.svdVâ‚, Vâ‚_conjTranspose_mul_Vâ‚,
    Matrix.one_mul, svdÏƒ_inv_mapK, â† conjTranspose_map, â† Matrix.map_mul, â† Matrix.map_mul,
    â† Matrix.mul_assoc, Ïƒ_inv_Î¼_Ïƒ_inv_eq_one]
  simp only [map_zero, _root_.map_one, map_one]
  unfold Function.Semiconj
  intros x
  rw [IsROrC.star_def, IsROrC.algebraMap_eq_ofReal, starRingEnd_apply, star_trivial,
    IsROrC.star_def, IsROrC.conj_ofReal]

lemma Uâ‚‚_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚‚á´´  *  A.svdUâ‚‚ = 1 := by
  rw [svdUâ‚‚, toColumnsâ‚‚]
  simp only [reindex_apply, Equiv.refl_symm, Equiv.coe_refl, submatrix_apply, id_eq]
  funext i j
  simp_rw [Matrix.mul_apply, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix,
    â† Matrix.mul_apply, Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _)]
  by_cases hij: i = j
  Â· simp_rw [hij, one_apply_eq]
  Â· rw [one_apply_ne hij, one_apply_ne]
    simpa only [ne_eq, EmbeddingLike.apply_eq_iff_eq, Sum.inr.injEq]

lemma Uâ‚'_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A.svdUâ‚'á´´  *  A.svdUâ‚‚ = 0 := by
  simp_rw [svdUâ‚', svdUâ‚‚, toColumnsâ‚, toColumnsâ‚‚, reindex_apply, Equiv.refl_symm, Equiv.coe_refl,
    submatrix_apply, id_eq, HMul.hMul, dotProduct, conjTranspose_apply, of_apply,
    â† conjTranspose_apply, IsHermitian.conjTranspose_eigenvectorMatrix, â† mul_apply,
    Matrix.mul_eq_one_comm.1 (IsHermitian.eigenvectorMatrix_mul_inv _)]
  funext i j
  simp only [ne_eq, EmbeddingLike.apply_eq_iff_eq, not_false_eq_true, one_apply_ne, zero_apply]

lemma mul_Vâ‚‚_eq_zero (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A  *  A.svdVâ‚‚ = 0 := by
  suffices h : Aá´´ * A * A.svdVâ‚‚ = 0
  Â· exact (conjTranspose_mul_self_mul_eq_zero _ _).1 h
  rw [reduced_spectral_theorem, Matrix.mul_assoc, Vâ‚_conjTranspose_mul_Vâ‚‚, Matrix.mul_zero]

lemma conjTranspose_mul_Uâ‚‚_eq_zero (A : Matrix (Fin M) (Fin N) ğ•‚) : Aá´´  *  A.svdUâ‚‚ = 0 := by
  suffices h : A * Aá´´ * A.svdUâ‚‚ = 0
  Â· exact (self_mul_conjTranspose_mul_eq_zero _ _).1 h
  rw [reduced_spectral_theorem', Matrix.mul_assoc, Uâ‚'_conjTranspose_mul_Uâ‚‚]
  simp only [Matrix.mul_zero]

lemma Uâ‚_conjTranspose_mul_Uâ‚‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚á´´  *  A.svdUâ‚‚ = 0 := by
  unfold svdUâ‚
  simp_rw [conjTranspose_mul, Matrix.mul_assoc, conjTranspose_mul_Uâ‚‚_eq_zero, Matrix.mul_zero]

lemma Uâ‚‚_conjTranspose_mul_Uâ‚ (A : Matrix (Fin M) (Fin N) ğ•‚) : A.svdUâ‚‚á´´  *  A.svdUâ‚ = 0 := by
  rw [â† conjTranspose_conjTranspose (A.svdUâ‚), â† conjTranspose_mul, Uâ‚_conjTranspose_mul_Uâ‚‚,
    conjTranspose_zero]

lemma U_inv (A : Matrix (Fin M) (Fin N) ğ•‚) :
  (fromColumns A.svdUâ‚ A.svdUâ‚‚)á´´ * (fromColumns A.svdUâ‚ A.svdUâ‚‚) = 1 := by
  rw [conjTranspose_fromColumns_eq_fromRows_conjTranspose, fromRows_mul_fromColumns,
    Uâ‚_conjTranspose_mul_Uâ‚‚, Uâ‚_conjTranspose_mul_Uâ‚, Uâ‚‚_conjTranspose_mul_Uâ‚‚,
    Uâ‚‚_conjTranspose_mul_Uâ‚, fromBlocks_one]

lemma V_conjTranspose_mul_inj (A : Matrix (Fin M) (Fin N) ğ•‚) {m : Type} :
    Function.Injective (fun x : Matrix m (Fin N) ğ•‚ => x  *  (fromColumns A.svdVâ‚ A.svdVâ‚‚)) := by
  intro X Y h
  replace h := congr_arg (fun x => x * (fromColumns A.svdVâ‚ A.svdVâ‚‚)á´´) h
  dsimp at h
  have V_inv' := V_inv A
  rw [conjTranspose_fromColumns_eq_fromRows_conjTranspose, â† fromColumns_mul_fromRows_eq_one_comm,
    â† conjTranspose_fromColumns_eq_fromRows_conjTranspose] at V_inv'
  rwa [Matrix.mul_assoc, Matrix.mul_assoc, V_inv', Matrix.mul_one, Matrix.mul_one] at h
  apply equiv_fin_width_eigs_conjTranspose_mul_self

/-- # Main SVD Theorem
Any matrix A (M Ã— N) with rank r = A.rank and  with elements in â„ or â„‚ fields can be decompsed
into three matrices:
  U: an M Ã— M matrix containing the left eigenvectors of the matrix
  S: an M Ã— N matrix with an r Ã— r block in the upper left corner with nonzero singular values
  V: an N Ã— N matrix containing the right eigenvectors of the matrix

Note that UUá´´ = Uá´´U = 1 and VVá´´=Vá´´V = 1 as can be seen in lemmas `U_inv` and `V_inv` together with
`fromColumns_mul_fromRows_eq_one_comm` and `conjTranspose_fromColumns_eq_fromRows_conjTranspose` -/

theorem svd_theorem (A : Matrix (Fin M) (Fin N) ğ•‚) :
    A = A.svdU  *  (fromBlocks (map A.svdÏƒ (algebraMap â„ ğ•‚)) 0 0 0)  *  A.svdVá´´ := by
  apply_fun (fun x => x * (fromColumns A.svdVâ‚ A.svdVâ‚‚))
  simp_rw [svdU, svdV, Matrix.mul_assoc, V_inv, Matrix.mul_one, fromColumns_mul_fromBlocks,
    mul_fromColumns, mul_Vâ‚‚_eq_zero, Matrix.mul_zero, add_zero, fromColumns_ext_iff, and_true,
    svdUâ‚, Matrix.nonsing_inv_mul_cancel_right _ _ (IsUnit_det_svdÏƒ_mapK _)]
  exact (V_conjTranspose_mul_inj _)

end Matrix
