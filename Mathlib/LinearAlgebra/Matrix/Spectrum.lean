/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Data.Matrix.Rank
import Mathlib.LinearAlgebra.Matrix.Diagonal
import Mathlib.LinearAlgebra.Matrix.Hermitian

#align_import linear_algebra.matrix.spectrum from "leanprover-community/mathlib"@"46b633fd842bef9469441c0209906f6dddd2b4f5"

/-! # Spectral theory of hermitian matrices

This file proves the spectral theorem for matrices. The proof of the spectral theorem is based on
the spectral theorem for linear maps (`LinearMap.IsSymmetric.eigenvectorBasis_apply_self_apply`).

## Tags

spectral theorem, diagonalization theorem-/

namespace Matrix

variable {ùïú : Type*} [RCLike ùïú] {n : Type*} [Fintype n]
variable {A : Matrix n n ùïú}

namespace IsHermitian

section DecidableEq

variable [DecidableEq n]
variable (hA : A.IsHermitian)

/-- The eigenvalues of a hermitian matrix, indexed by `Fin (Fintype.card n)` where `n` is the index
type of the matrix. -/
noncomputable def eigenvalues‚ÇÄ : Fin (Fintype.card n) ‚Üí ‚Ñù :=
  (isHermitian_iff_isSymmetric.1 hA).eigenvalues finrank_euclideanSpace
#align matrix.is_hermitian.eigenvalues‚ÇÄ Matrix.IsHermitian.eigenvalues‚ÇÄ

/-- The eigenvalues of a hermitian matrix, reusing the index `n` of the matrix entries. -/
noncomputable def eigenvalues : n ‚Üí ‚Ñù := fun i =>
  hA.eigenvalues‚ÇÄ <| (Fintype.equivOfCardEq (Fintype.card_fin _)).symm i
#align matrix.is_hermitian.eigenvalues Matrix.IsHermitian.eigenvalues

/-- A choice of an orthonormal basis of eigenvectors of a hermitian matrix. -/
noncomputable def eigenvectorBasis : OrthonormalBasis n ùïú (EuclideanSpace ùïú n) :=
  ((isHermitian_iff_isSymmetric.1 hA).eigenvectorBasis finrank_euclideanSpace).reindex
    (Fintype.equivOfCardEq (Fintype.card_fin _))
#align matrix.is_hermitian.eigenvector_basis Matrix.IsHermitian.eigenvectorBasis

lemma mulVec_eigenvectorBasis (j : n) :
    A *·µ• ‚áë(hA.eigenvectorBasis j) = (hA.eigenvalues j) ‚Ä¢ ‚áë(hA.eigenvectorBasis j) := by
  simpa only [eigenvectorBasis, OrthonormalBasis.reindex_apply, toEuclideanLin_apply,
    RCLike.real_smul_eq_coe_smul (K := ùïú)] using
      congr(‚áë$((isHermitian_iff_isSymmetric.1 hA).apply_eigenvectorBasis
        finrank_euclideanSpace ((Fintype.equivOfCardEq (Fintype.card_fin _)).symm j)))

/-- Unitary matrix whose columns are `Matrix.IsHermitian.eigenvectorBasis`. -/
noncomputable def eigenvectorUnitary {ùïú : Type*} [RCLike ùïú] {n : Type*}
    [Fintype n]{A : Matrix n n ùïú} [DecidableEq n] (hA : Matrix.IsHermitian A) :
    Matrix.unitaryGroup n ùïú :=
  ‚ü®(EuclideanSpace.basisFun n ùïú).toBasis.toMatrix (hA.eigenvectorBasis).toBasis,
    (EuclideanSpace.basisFun n ùïú).toMatrix_orthonormalBasis_mem_unitary (eigenvectorBasis hA)‚ü©
#align matrix.is_hermitian.eigenvector_matrix Matrix.IsHermitian.eigenvectorUnitary

lemma eigenvectorUnitary_coe {ùïú : Type*} [RCLike ùïú] {n : Type*} [Fintype n]
    {A : Matrix n n ùïú} [DecidableEq n] (hA : Matrix.IsHermitian A) :
    eigenvectorUnitary hA =
      (EuclideanSpace.basisFun n ùïú).toBasis.toMatrix (hA.eigenvectorBasis).toBasis :=
  rfl

@[simp]
theorem eigenvectorUnitary_apply (i j : n) :
    eigenvectorUnitary hA i j = ‚áë(hA.eigenvectorBasis j) i :=
  rfl
#align matrix.is_hermitian.eigenvector_matrix_apply Matrix.IsHermitian.eigenvectorUnitary_apply

theorem eigenvectorUnitary_mulVec (j : n) :
    eigenvectorUnitary hA *·µ• Pi.single j 1 = ‚áë(hA.eigenvectorBasis j) := by
  simp only [mulVec_single, eigenvectorUnitary_apply, mul_one]

theorem star_eigenvectorUnitary_mulVec (j : n) :
    (star (eigenvectorUnitary hA : Matrix n n ùïú)) *·µ• ‚áë(hA.eigenvectorBasis j) = Pi.single j 1 := by
  rw [‚Üê eigenvectorUnitary_mulVec, mulVec_mulVec, unitary.coe_star_mul_self, one_mulVec]

/-- Unitary diagonalization of a Hermitian matrix. -/
theorem star_mul_self_mul_eq_diagonal :
    (star (eigenvectorUnitary hA : Matrix n n ùïú)) * A * (eigenvectorUnitary hA : Matrix n n ùïú)
      = diagonal (RCLike.ofReal ‚àò hA.eigenvalues) := by
  apply Matrix.toEuclideanLin.injective
  apply Basis.ext (EuclideanSpace.basisFun n ùïú).toBasis
  intro i
  simp only [toEuclideanLin_apply, OrthonormalBasis.coe_toBasis, EuclideanSpace.basisFun_apply,
    WithLp.equiv_single, ‚Üê mulVec_mulVec, eigenvectorUnitary_mulVec, ‚Üê mulVec_mulVec,
    mulVec_eigenvectorBasis, Matrix.diagonal_mulVec_single, mulVec_smul,
    star_eigenvectorUnitary_mulVec, RCLike.real_smul_eq_coe_smul (K := ùïú), WithLp.equiv_symm_smul,
    WithLp.equiv_symm_single, Function.comp_apply, mul_one, WithLp.equiv_symm_single]
  apply PiLp.ext
  intro j
  simp only [PiLp.smul_apply, EuclideanSpace.single_apply, smul_eq_mul, mul_ite, mul_one, mul_zero]


/-- **Diagonalization theorem**, **spectral theorem** for matrices; A hermitian matrix can be
diagonalized by a change of basis. For the spectral theorem on linear maps, see
`LinearMap.IsSymmetric.eigenvectorBasis_apply_self_apply`.-/
theorem spectral_theorem :
    A = (eigenvectorUnitary hA : Matrix n n ùïú) * diagonal (RCLike.ofReal ‚àò hA.eigenvalues)
      * (star (eigenvectorUnitary hA : Matrix n n ùïú)) := by
  rw [‚Üê star_mul_self_mul_eq_diagonal, mul_assoc, mul_assoc,
    (Matrix.mem_unitaryGroup_iff).mp (eigenvectorUnitary hA).2, mul_one,
    ‚Üê mul_assoc, (Matrix.mem_unitaryGroup_iff).mp (eigenvectorUnitary hA).2, one_mul]
#align matrix.is_hermitian.spectral_theorem' Matrix.IsHermitian.spectral_theorem

theorem eigenvalues_eq (i : n) :
    (hA.eigenvalues i) = RCLike.re (Matrix.dotProduct (star ‚áë(hA.eigenvectorBasis i))
    (A *·µ• ‚áë(hA.eigenvectorBasis i))):= by
  simp only [mulVec_eigenvectorBasis, dotProduct_smul,‚Üê EuclideanSpace.inner_eq_star_dotProduct,
    inner_self_eq_norm_sq_to_K, RCLike.smul_re, hA.eigenvectorBasis.orthonormal.1 i,
    mul_one, algebraMap.coe_one, one_pow, RCLike.one_re]
#align matrix.is_hermitian.eigenvalues_eq Matrix.IsHermitian.eigenvalues_eq

/-- The determinant of a hermitian matrix is the product of its eigenvalues. -/
theorem det_eq_prod_eigenvalues : det A = ‚àè i, (hA.eigenvalues i : ùïú) := by
  convert congr_arg det hA.spectral_theorem
  rw [det_mul_right_comm]
  simp
#align matrix.is_hermitian.det_eq_prod_eigenvalues Matrix.IsHermitian.det_eq_prod_eigenvalues

/-- rank of a hermitian matrix is the rank of after diagonalization by the eigenvector unitary -/
lemma rank_eq_rank_diagonal : A.rank = (Matrix.diagonal hA.eigenvalues).rank := by
  conv_lhs => rw [hA.spectral_theorem, ‚Üê unitary.coe_star]
  simp [-isUnit_iff_ne_zero, -unitary.coe_star, rank_diagonal]
#align matrix.is_hermitian.rank_eq_rank_diagonal Matrix.IsHermitian.rank_eq_rank_diagonal

/-- rank of a hermitian matrix is the number of nonzero eigenvalues of the hermitian matrix -/
lemma rank_eq_card_non_zero_eigs : A.rank = Fintype.card {i // hA.eigenvalues i ‚â† 0} := by
  rw [rank_eq_rank_diagonal hA, Matrix.rank_diagonal]
#align matrix.is_hermitian.rank_eq_card_non_zero_eigs Matrix.IsHermitian.rank_eq_card_non_zero_eigs

end DecidableEq

/-- A nonzero Hermitian matrix has an eigenvector with nonzero eigenvalue. -/
lemma exists_eigenvector_of_ne_zero (hA : IsHermitian A) (h_ne : A ‚â† 0) :
    ‚àÉ (v : n ‚Üí ùïú) (t : ‚Ñù), t ‚â† 0 ‚àß v ‚â† 0 ‚àß A *·µ• v = t ‚Ä¢ v := by
  classical
  have : hA.eigenvalues ‚â† 0 := by
    contrapose! h_ne
    have := hA.spectral_theorem
    rwa [h_ne, Pi.comp_zero, RCLike.ofReal_zero, (by rfl : Function.const n (0 : ùïú) = fun _ ‚Ü¶ 0),
      diagonal_zero, mul_zero, zero_mul] at this
  obtain ‚ü®i, hi‚ü© := Function.ne_iff.mp this
  exact ‚ü®_, _, hi, hA.eigenvectorBasis.orthonormal.ne_zero i, hA.mulVec_eigenvectorBasis i‚ü©
#align matrix.is_hermitian.exists_eigenvector_of_ne_zero Matrix.IsHermitian.exists_eigenvector_of_ne_zero

end IsHermitian

end Matrix

/-The following were removed as a result of the refactor, since they either were
unused in the library, followed as immediate consequences of, or were replaced by
above results (e.g. results about inverses don't need replacement because their unitary
analogues have replaced them).-/

#noalign Matrix.IsHermitian.eigenvector_matrix_inv
#noalign matrix.is_hermitian.eigenvector_matrix_mul_inv
#noalign matrix.is_hermitian.eigenvector_matrix_inv_apply
#noalign matrix.is_hermitian.conj_transpose_eigenvector_matrix_inv
#noalign matrix.is_hermitian.conj_transpose_eigenvector_matrix
#noalign matrix.is_hermitian.spectral_theorem
