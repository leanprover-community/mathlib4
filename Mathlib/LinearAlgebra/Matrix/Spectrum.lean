/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.LinearAlgebra.Eigenspace.Matrix
import Mathlib.LinearAlgebra.Matrix.Charpoly.Eigs
import Mathlib.LinearAlgebra.Matrix.Diagonal
import Mathlib.LinearAlgebra.Matrix.Hermitian
import Mathlib.LinearAlgebra.Matrix.Rank
import Mathlib.Topology.Algebra.Module.FiniteDimension

/-! # Spectral theory of Hermitian matrices

This file proves the spectral theorem for matrices. The proof of the spectral theorem is based on
the spectral theorem for linear maps (`LinearMap.IsSymmetric.eigenvectorBasis_apply_self_apply`).

## Tags

spectral theorem, diagonalization theorem -/

namespace Matrix

variable {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
variable {A B : Matrix n n ğ•œ}

lemma finite_real_spectrum [DecidableEq n] : (spectrum â„ A).Finite := by
  rw [â† spectrum.preimage_algebraMap ğ•œ]
  exact A.finite_spectrum.preimage (FaithfulSMul.algebraMap_injective â„ ğ•œ).injOn

instance [DecidableEq n] : Finite (spectrum â„ A) := A.finite_real_spectrum

/-- The spectrum of a matrix `A` coincides with the spectrum of `toEuclideanLin A`. -/
theorem spectrum_toEuclideanLin [DecidableEq n] : spectrum ğ•œ (toEuclideanLin A) = spectrum ğ•œ A :=
  AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv (PiLp.basisFun 2 ğ•œ n)) _

@[deprecated (since := "13-08-2025")] alias IsHermitian.spectrum_toEuclideanLin :=
  spectrum_toEuclideanLin

namespace IsHermitian

section DecidableEq

variable [DecidableEq n]
variable (hA : A.IsHermitian) (hB : B.IsHermitian)

/-- The eigenvalues of a Hermitian matrix, indexed by `Fin (Fintype.card n)` where `n` is the index
type of the matrix. -/
noncomputable def eigenvaluesâ‚€ : Fin (Fintype.card n) â†’ â„ :=
  (isHermitian_iff_isSymmetric.1 hA).eigenvalues finrank_euclideanSpace

lemma eigenvaluesâ‚€_antitone : Antitone hA.eigenvaluesâ‚€ :=
  LinearMap.IsSymmetric.eigenvalues_antitone ..

/-- The eigenvalues of a Hermitian matrix, reusing the index `n` of the matrix entries. -/
noncomputable def eigenvalues : n â†’ â„ := fun i =>
  hA.eigenvaluesâ‚€ <| (Fintype.equivOfCardEq (Fintype.card_fin _)).symm i

/-- A choice of an orthonormal basis of eigenvectors of a Hermitian matrix. -/
noncomputable def eigenvectorBasis : OrthonormalBasis n ğ•œ (EuclideanSpace ğ•œ n) :=
  ((isHermitian_iff_isSymmetric.1 hA).eigenvectorBasis finrank_euclideanSpace).reindex
    (Fintype.equivOfCardEq (Fintype.card_fin _))

lemma mulVec_eigenvectorBasis (j : n) :
    A *áµ¥ â‡‘(hA.eigenvectorBasis j) = (hA.eigenvalues j) â€¢ â‡‘(hA.eigenvectorBasis j) := by
  simpa only [eigenvectorBasis, OrthonormalBasis.reindex_apply, toEuclideanLin_apply,
    RCLike.real_smul_eq_coe_smul (K := ğ•œ)] using
      congr(â‡‘$((isHermitian_iff_isSymmetric.1 hA).apply_eigenvectorBasis
        finrank_euclideanSpace ((Fintype.equivOfCardEq (Fintype.card_fin _)).symm j)))

/-- Eigenvalues of a Hermitian matrix A are in the â„ spectrum of A. -/
theorem eigenvalues_mem_spectrum_real (i : n) : hA.eigenvalues i âˆˆ spectrum â„ A := by
  apply spectrum.of_algebraMap_mem ğ•œ
  rw [â† Matrix.spectrum_toEuclideanLin]
  exact LinearMap.IsSymmetric.hasEigenvalue_eigenvalues _ _ _ |>.mem_spectrum

/-- Unitary matrix whose columns are `Matrix.IsHermitian.eigenvectorBasis`. -/
noncomputable def eigenvectorUnitary {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*}
    [Fintype n] {A : Matrix n n ğ•œ} [DecidableEq n] (hA : Matrix.IsHermitian A) :
    Matrix.unitaryGroup n ğ•œ :=
  âŸ¨(EuclideanSpace.basisFun n ğ•œ).toBasis.toMatrix (hA.eigenvectorBasis).toBasis,
    (EuclideanSpace.basisFun n ğ•œ).toMatrix_orthonormalBasis_mem_unitary (eigenvectorBasis hA)âŸ©

lemma eigenvectorUnitary_coe {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    {A : Matrix n n ğ•œ} [DecidableEq n] (hA : Matrix.IsHermitian A) :
    eigenvectorUnitary hA =
      (EuclideanSpace.basisFun n ğ•œ).toBasis.toMatrix (hA.eigenvectorBasis).toBasis :=
  rfl

@[simp]
theorem eigenvectorUnitary_transpose_apply (j : n) :
    (eigenvectorUnitary hA)áµ€ j = â‡‘(hA.eigenvectorBasis j) :=
  rfl

@[simp]
theorem eigenvectorUnitary_col_eq (j : n) :
    Matrix.col (eigenvectorUnitary hA) j = â‡‘(hA.eigenvectorBasis j) :=
  rfl

@[simp]
theorem eigenvectorUnitary_apply (i j : n) :
    eigenvectorUnitary hA i j = â‡‘(hA.eigenvectorBasis j) i :=
  rfl

theorem eigenvectorUnitary_mulVec (j : n) :
    eigenvectorUnitary hA *áµ¥ Pi.single j 1 = â‡‘(hA.eigenvectorBasis j) := by
  simp_rw [mulVec_single_one, eigenvectorUnitary_col_eq]

theorem star_eigenvectorUnitary_mulVec (j : n) :
    (star (eigenvectorUnitary hA : Matrix n n ğ•œ)) *áµ¥ â‡‘(hA.eigenvectorBasis j) = Pi.single j 1 := by
  rw [â† eigenvectorUnitary_mulVec, mulVec_mulVec, Unitary.coe_star_mul_self, one_mulVec]

/-- Unitary diagonalization of a Hermitian matrix. -/
theorem star_mul_self_mul_eq_diagonal :
    (star (eigenvectorUnitary hA : Matrix n n ğ•œ)) * A * (eigenvectorUnitary hA : Matrix n n ğ•œ)
      = diagonal (RCLike.ofReal âˆ˜ hA.eigenvalues) := by
  apply Matrix.toEuclideanLin.injective
  apply (EuclideanSpace.basisFun n ğ•œ).toBasis.ext
  intro i
  simp only [toEuclideanLin_apply, OrthonormalBasis.coe_toBasis, EuclideanSpace.basisFun_apply,
    EuclideanSpace.ofLp_single, â† mulVec_mulVec, eigenvectorUnitary_mulVec, â† mulVec_mulVec,
    mulVec_eigenvectorBasis, Matrix.diagonal_mulVec_single, mulVec_smul,
    star_eigenvectorUnitary_mulVec, RCLike.real_smul_eq_coe_smul (K := ğ•œ), WithLp.toLp_smul,
    EuclideanSpace.toLp_single, Function.comp_apply, mul_one]
  apply PiLp.ext
  intro j
  simp only [PiLp.smul_apply, EuclideanSpace.single_apply, smul_eq_mul, mul_ite, mul_one, mul_zero]

/-- **Diagonalization theorem**, **spectral theorem** for matrices; A Hermitian matrix can be
diagonalized by a change of basis. For the spectral theorem on linear maps, see
`LinearMap.IsSymmetric.eigenvectorBasis_apply_self_apply`. -/
theorem spectral_theorem :
    A = (eigenvectorUnitary hA : Matrix n n ğ•œ) * diagonal (RCLike.ofReal âˆ˜ hA.eigenvalues)
      * (star (eigenvectorUnitary hA : Matrix n n ğ•œ)) := by
  rw [â† star_mul_self_mul_eq_diagonal, mul_assoc, mul_assoc,
    (Matrix.mem_unitaryGroup_iff).mp (eigenvectorUnitary hA).2, mul_one,
    â† mul_assoc, (Matrix.mem_unitaryGroup_iff).mp (eigenvectorUnitary hA).2, one_mul]

theorem eigenvalues_eq (i : n) :
    (hA.eigenvalues i) = RCLike.re (dotProduct (star â‡‘(hA.eigenvectorBasis i))
    (A *áµ¥ â‡‘(hA.eigenvectorBasis i))) := by
  rw [dotProduct_comm]
  simp only [mulVec_eigenvectorBasis, smul_dotProduct, â† EuclideanSpace.inner_eq_star_dotProduct,
    inner_self_eq_norm_sq_to_K, RCLike.smul_re, hA.eigenvectorBasis.orthonormal.1 i,
    mul_one, algebraMap.coe_one, one_pow, RCLike.one_re]

open Polynomial in
lemma charpoly_eq : A.charpoly = âˆ i, (X - C (hA.eigenvalues i : ğ•œ)) := by
  conv_lhs => rw [hA.spectral_theorem, charpoly_mul_comm, â† mul_assoc]
  simp [charpoly_diagonal]

lemma roots_charpoly_eq_eigenvalues :
    A.charpoly.roots = Multiset.map (RCLike.ofReal âˆ˜ hA.eigenvalues) Finset.univ.val := by
  rw [hA.charpoly_eq, Polynomial.roots_prod]
  Â· simp
  Â· simp [Finset.prod_ne_zero_iff, Polynomial.X_sub_C_ne_zero]

lemma roots_charpoly_eq_eigenvaluesâ‚€ :
    A.charpoly.roots = Multiset.map (RCLike.ofReal âˆ˜ hA.eigenvaluesâ‚€) Finset.univ.val := by
  rw [hA.roots_charpoly_eq_eigenvalues]
  simp only [â† Multiset.map_map, eigenvalues, â† Function.comp_apply (f := hA.eigenvaluesâ‚€)]
  simp

lemma sort_roots_charpoly_eq_eigenvaluesâ‚€ :
    (A.charpoly.roots.map RCLike.re).sort (Â· â‰¥ Â·) = List.ofFn hA.eigenvaluesâ‚€ := by
  simp_rw [hA.roots_charpoly_eq_eigenvaluesâ‚€, Fin.univ_val_map, Multiset.map_coe, List.map_ofFn,
    Function.comp_def, RCLike.ofReal_re, Multiset.coe_sort]
  rw [List.mergeSort_of_sorted]
  simpa [List.Sorted] using (eigenvaluesâ‚€_antitone hA).ofFn_sorted

lemma eigenvalues_eq_eigenvalues_iff :
    hA.eigenvalues = hB.eigenvalues â†” A.charpoly = B.charpoly := by
  constructor <;> intro h
  Â· rw [hA.charpoly_eq, hB.charpoly_eq, h]
  Â· suffices hA.eigenvaluesâ‚€ = hB.eigenvaluesâ‚€ by unfold eigenvalues; rw [this]
    simp_rw [â† List.ofFn_inj, â† sort_roots_charpoly_eq_eigenvaluesâ‚€, h]

theorem splits_charpoly (hA : A.IsHermitian) : A.charpoly.Splits (RingHom.id ğ•œ) :=
  Polynomial.splits_iff_card_roots.mpr (by simp [hA.roots_charpoly_eq_eigenvalues])

/-- The determinant of a Hermitian matrix is the product of its eigenvalues. -/
theorem det_eq_prod_eigenvalues : det A = âˆ i, (hA.eigenvalues i : ğ•œ) := by
  simp [det_eq_prod_roots_charpoly_of_splits hA.splits_charpoly, hA.roots_charpoly_eq_eigenvalues]

/-- rank of a Hermitian matrix is the rank of after diagonalization by the eigenvector unitary -/
lemma rank_eq_rank_diagonal : A.rank = (Matrix.diagonal hA.eigenvalues).rank := by
  conv_lhs => rw [hA.spectral_theorem, â† Unitary.coe_star]
  simp [-isUnit_iff_ne_zero, -Unitary.coe_star, rank_diagonal]

/-- rank of a Hermitian matrix is the number of nonzero eigenvalues of the Hermitian matrix -/
lemma rank_eq_card_non_zero_eigs : A.rank = Fintype.card {i // hA.eigenvalues i â‰  0} := by
  rw [rank_eq_rank_diagonal hA, Matrix.rank_diagonal]

/-- The spectrum of a Hermitian matrix is the range of its eigenvalues under `RCLike.ofReal`. -/
theorem spectrum_eq_image_range :
    spectrum ğ•œ A = RCLike.ofReal '' Set.range hA.eigenvalues := Set.ext fun x => by
  conv_lhs => rw [hA.spectral_theorem]
  simp

/-- The `â„`-spectrum of a Hermitian matrix over `RCLike` field is the range of the eigenvalue
function. -/
theorem spectrum_real_eq_range_eigenvalues :
    spectrum â„ A = Set.range hA.eigenvalues := Set.ext fun x => by
  conv_lhs => rw [hA.spectral_theorem, â† spectrum.algebraMap_mem_iff ğ•œ]
  simp

@[deprecated (since := "2025-08-14")]
alias eigenvalues_eq_spectrum_real := spectrum_real_eq_range_eigenvalues

/-- The eigenvalues of a Hermitian matrix `A` are all zero iff `A = 0`. -/
theorem eigenvalues_eq_zero_iff :
    hA.eigenvalues = 0 â†” A = 0 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ by ext; simp [h, eigenvalues_eq]âŸ©
  rw [hA.spectral_theorem, h, Pi.comp_zero, RCLike.ofReal_zero, Function.const_zero,
    Pi.zero_def, diagonal_zero, mul_zero, zero_mul]

end DecidableEq

/-- A nonzero Hermitian matrix has an eigenvector with nonzero eigenvalue. -/
lemma exists_eigenvector_of_ne_zero (hA : IsHermitian A) (h_ne : A â‰  0) :
    âˆƒ (v : n â†’ ğ•œ) (t : â„), t â‰  0 âˆ§ v â‰  0 âˆ§ A *áµ¥ v = t â€¢ v := by
  classical
  have : hA.eigenvalues â‰  0 := by
    contrapose! h_ne
    have := hA.spectral_theorem
    rwa [h_ne, Pi.comp_zero, RCLike.ofReal_zero, (by rfl : Function.const n (0 : ğ•œ) = fun _ â†¦ 0),
      diagonal_zero, mul_zero, zero_mul] at this
  obtain âŸ¨i, hiâŸ© := Function.ne_iff.mp this
  exact âŸ¨_, _, hi, hA.eigenvectorBasis.orthonormal.ne_zero i, hA.mulVec_eigenvectorBasis iâŸ©

theorem trace_eq_sum_eigenvalues [DecidableEq n] (hA : A.IsHermitian) :
    A.trace = âˆ‘ i, (hA.eigenvalues i : ğ•œ) := by
  simp [trace_eq_sum_roots_charpoly_of_splits hA.splits_charpoly, hA.roots_charpoly_eq_eigenvalues]

end IsHermitian

end Matrix
