/-
Copyright (c) 2021 Lu-Ming Zhang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lu-Ming Zhang
-/
import Mathlib.Data.Matrix.Block

#align_import linear_algebra.matrix.symmetric from "leanprover-community/mathlib"@"3e068ece210655b7b9a9477c3aff38a492400aa1"

/-!
# Symmetric matrices

This file contains the definition and basic results about symmetric matrices.

## Main definition

 * `Matrix.isSymm`: a matrix `A : Matrix n n Œ±` is "symmetric" if `A·µÄ = A`.

## Tags

symm, symmetric, matrix
-/


variable {Œ± Œ≤ n m R : Type*}

namespace Matrix

open Matrix

/-- A matrix `A : Matrix n n Œ±` is "symmetric" if `A·µÄ = A`. -/
def IsSymm (A : Matrix n n Œ±) : Prop :=
  A·µÄ = A
#align matrix.is_symm Matrix.IsSymm

theorem IsSymm.eq {A : Matrix n n Œ±} (h : A.IsSymm) : A·µÄ = A :=
  h
#align matrix.is_symm.eq Matrix.IsSymm.eq

/-- A version of `Matrix.ext_iff` that unfolds the `Matrix.transpose`. -/
theorem IsSymm.ext_iff {A : Matrix n n Œ±} : A.IsSymm ‚Üî ‚àÄ i j, A j i = A i j :=
  Matrix.ext_iff.symm
#align matrix.is_symm.ext_iff Matrix.IsSymm.ext_iff

/-- A version of `Matrix.ext` that unfolds the `Matrix.transpose`. -/
-- @[ext] -- Porting note: removed attribute
theorem IsSymm.ext {A : Matrix n n Œ±} : (‚àÄ i j, A j i = A i j) ‚Üí A.IsSymm :=
  Matrix.ext
#align matrix.is_symm.ext Matrix.IsSymm.ext

theorem IsSymm.apply {A : Matrix n n Œ±} (h : A.IsSymm) (i j : n) : A j i = A i j :=
  IsSymm.ext_iff.1 h i j
#align matrix.is_symm.apply Matrix.IsSymm.apply

theorem isSymm_mul_transpose_self [Fintype n] [CommSemiring Œ±] (A : Matrix n n Œ±) :
    (A * A·µÄ).IsSymm :=
  transpose_mul _ _
#align matrix.is_symm_mul_transpose_self Matrix.isSymm_mul_transpose_self

theorem isSymm_transpose_mul_self [Fintype n] [CommSemiring Œ±] (A : Matrix n n Œ±) :
    (A·µÄ * A).IsSymm :=
  transpose_mul _ _
#align matrix.is_symm_transpose_mul_self Matrix.isSymm_transpose_mul_self

theorem isSymm_add_transpose_self [AddCommSemigroup Œ±] (A : Matrix n n Œ±) : (A + A·µÄ).IsSymm :=
  add_comm _ _
#align matrix.is_symm_add_transpose_self Matrix.isSymm_add_transpose_self

theorem isSymm_transpose_add_self [AddCommSemigroup Œ±] (A : Matrix n n Œ±) : (A·µÄ + A).IsSymm :=
  add_comm _ _
#align matrix.is_symm_transpose_add_self Matrix.isSymm_transpose_add_self

@[simp]
theorem isSymm_zero [Zero Œ±] : (0 : Matrix n n Œ±).IsSymm :=
  transpose_zero
#align matrix.is_symm_zero Matrix.isSymm_zero

@[simp]
theorem isSymm_one [DecidableEq n] [Zero Œ±] [One Œ±] : (1 : Matrix n n Œ±).IsSymm :=
  transpose_one
#align matrix.is_symm_one Matrix.isSymm_one

@[simp]
theorem IsSymm.map {A : Matrix n n Œ±} (h : A.IsSymm) (f : Œ± ‚Üí Œ≤) : (A.map f).IsSymm :=
  transpose_map.symm.trans (h.symm ‚ñ∏ rfl)
#align matrix.is_symm.map Matrix.IsSymm.map

@[simp]
theorem IsSymm.transpose {A : Matrix n n Œ±} (h : A.IsSymm) : A·µÄ.IsSymm :=
  congr_arg _ h
#align matrix.is_symm.transpose Matrix.IsSymm.transpose

@[simp]
theorem IsSymm.conjTranspose [Star Œ±] {A : Matrix n n Œ±} (h : A.IsSymm) : A·¥¥.IsSymm :=
  h.transpose.map _
#align matrix.is_symm.conj_transpose Matrix.IsSymm.conjTranspose

@[simp]
theorem IsSymm.neg [Neg Œ±] {A : Matrix n n Œ±} (h : A.IsSymm) : (-A).IsSymm :=
  (transpose_neg _).trans (congr_arg _ h)
#align matrix.is_symm.neg Matrix.IsSymm.neg

@[simp]
theorem IsSymm.add {A B : Matrix n n Œ±} [Add Œ±] (hA : A.IsSymm) (hB : B.IsSymm) : (A + B).IsSymm :=
  (transpose_add _ _).trans (hA.symm ‚ñ∏ hB.symm ‚ñ∏ rfl)
#align matrix.is_symm.add Matrix.IsSymm.add

@[simp]
theorem IsSymm.sub {A B : Matrix n n Œ±} [Sub Œ±] (hA : A.IsSymm) (hB : B.IsSymm) : (A - B).IsSymm :=
  (transpose_sub _ _).trans (hA.symm ‚ñ∏ hB.symm ‚ñ∏ rfl)
#align matrix.is_symm.sub Matrix.IsSymm.sub

@[simp]
theorem IsSymm.smul [SMul R Œ±] {A : Matrix n n Œ±} (h : A.IsSymm) (k : R) : (k ‚Ä¢ A).IsSymm :=
  (transpose_smul _ _).trans (congr_arg _ h)
#align matrix.is_symm.smul Matrix.IsSymm.smul

@[simp]
theorem IsSymm.submatrix {A : Matrix n n Œ±} (h : A.IsSymm) (f : m ‚Üí n) : (A.submatrix f f).IsSymm :=
  (transpose_submatrix _ _ _).trans (h.symm ‚ñ∏ rfl)
#align matrix.is_symm.submatrix Matrix.IsSymm.submatrix

/-- The diagonal matrix `diagonal v` is symmetric. -/
@[simp]
theorem isSymm_diagonal [DecidableEq n] [Zero Œ±] (v : n ‚Üí Œ±) : (diagonal v).IsSymm :=
  diagonal_transpose _
#align matrix.is_symm_diagonal Matrix.isSymm_diagonal

/-- A block matrix `A.fromBlocks B C D` is symmetric,
    if `A` and `D` are symmetric and `B·µÄ = C`. -/
theorem IsSymm.fromBlocks {A : Matrix m m Œ±} {B : Matrix m n Œ±} {C : Matrix n m Œ±}
    {D : Matrix n n Œ±} (hA : A.IsSymm) (hBC : B·µÄ = C) (hD : D.IsSymm) :
    (A.fromBlocks B C D).IsSymm := by
  have hCB : C·µÄ = B := by
    rw [‚Üê hBC]
    simp
  unfold Matrix.IsSymm
  -- ‚ä¢ (Matrix.fromBlocks A B C D)·µÄ = Matrix.fromBlocks A B C D
  rw [fromBlocks_transpose]
  -- ‚ä¢ Matrix.fromBlocks A·µÄ C·µÄ B·µÄ D·µÄ = Matrix.fromBlocks A B C D
  congr; rw [hA, hCB, hBC, hD]
  -- ‚ä¢ Matrix.fromBlocks A·µÄ C·µÄ B·µÄ D·µÄ = Matrix.fromBlocks A B C D
         -- üéâ no goals
#align matrix.is_symm.from_blocks Matrix.IsSymm.fromBlocks

/-- This is the `iff` version of `Matrix.isSymm.fromBlocks`. -/
theorem isSymm_fromBlocks_iff {A : Matrix m m Œ±} {B : Matrix m n Œ±} {C : Matrix n m Œ±}
    {D : Matrix n n Œ±} : (A.fromBlocks B C D).IsSymm ‚Üî A.IsSymm ‚àß B·µÄ = C ‚àß C·µÄ = B ‚àß D.IsSymm :=
  ‚ü®fun h =>
    ‚ü®(congr_arg toBlocks‚ÇÅ‚ÇÅ h : _), (congr_arg toBlocks‚ÇÇ‚ÇÅ h : _), (congr_arg toBlocks‚ÇÅ‚ÇÇ h : _),
      (congr_arg toBlocks‚ÇÇ‚ÇÇ h : _)‚ü©,
    fun ‚ü®hA, hBC, _, hD‚ü© => IsSymm.fromBlocks hA hBC hD‚ü©
#align matrix.is_symm_from_blocks_iff Matrix.isSymm_fromBlocks_iff

end Matrix
