/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup.Basic
import Mathlib.LinearAlgebra.Matrix.PosDef

/-!
# â‹†-algebra equivalences on matrices

This file shows that â‹†-algebra equivalences on matrices are unitarily inner.
-/

variable {ğ•œ n : Type*} [RCLike ğ•œ] [Fintype n] [DecidableEq n]

open Matrix
open scoped ComplexOrder

/-- A â‹†-algebra equivalence on matrices is unitarily inner, i.e., there exists a unitary matrix `U`
such that `f x = U * x * star U`. -/
theorem Matrix.StarAlgEquiv.coe_eq_unitaryGroup_conjugate
    (f : Matrix n n ğ•œ â‰ƒâ‹†â‚[ğ•œ] Matrix n n ğ•œ) : âˆƒ U : unitaryGroup n ğ•œ,
    â‡‘f = fun x => U * x * (star U : Matrix n n ğ•œ) := by
  obtain hn | hn := isEmpty_or_nonempty n
  Â· exact âŸ¨1, funext fun a => Matrix.ext fun i => isEmpty_iff.mp hn i |>.elimâŸ©
  let f' := f.toAlgEquiv
  obtain âŸ¨y, hyâŸ© := f'.coe_eq_generalLinearGroup_conjugate
  let y' := (y : Matrix n n ğ•œ)
  have hf (x : Matrix n n ğ•œ) : f' x = y' * x * y'â»Â¹ := by simp [y', â† coe_units_inv, hy]
  replace hf (x : Matrix n n ğ•œ) : f x = y' * x * y'â»Â¹ := hf _ â–¸ rfl
  have (x : Matrix n n ğ•œ) : (f x)á´´ = f xá´´ := map_star _ _ |>.symm
  simp_rw [hf, conjTranspose_mul, conjTranspose_nonsing_inv] at this
  replace this (x : Matrix n n ğ•œ) : y'á´´ * y' * xá´´ * y'â»Â¹ = xá´´ * y'á´´ := by
    simp_rw [mul_assoc, â† mul_assoc y', â† this, â† mul_assoc,
      â† conjTranspose_nonsing_inv, â† conjTranspose_mul, inv_mul_of_invertible, mul_one]
  replace this (x : Matrix n n ğ•œ) : Commute x (y'á´´ * y') := by
    simp_rw [Commute, SemiconjBy, â† mul_assoc, â† (conjTranspose_conjTranspose x â–¸ this xá´´),
      mul_assoc, inv_mul_of_invertible, mul_one]
  simp_rw [Commute, SemiconjBy, â† Semigroup.mem_center_iff, center_eq_range] at this
  obtain âŸ¨Î±, hÎ±âŸ© := this
  have this : IsUnit (y'á´´ * y') := isUnit_iff_exists.mpr âŸ¨y'â»Â¹ * y'â»Â¹á´´, by
    simp [â† mul_assoc, mul_assoc _ _ y'á´´, â† conjTranspose_mul]âŸ©
  replace this := (PosSemidef.posDef_iff_isUnit (posSemidef_conjTranspose_mul_self _)).mpr this
  have thisÎ± : Î± = RCLike.re Î± := by
    have this10 := IsHermitian.coe_re_diag (IsSelfAdjoint.star_mul_self y')
    simp only [star, â† hÎ±, scalar_apply, diag_apply, diagonal_apply_eq, diag_diagonal] at this10
    have this11 : (RCLike.re Î± : ğ•œ) â€¢ (1 : n â†’ ğ•œ) = Î± â€¢ 1 := by ext i; simp [congr($this10 i)]
    rwa [(smul_left_injective _ (by simp : (1 : n â†’ ğ•œ) â‰  0)).eq_iff, eq_comm] at this11
  simp only [PosDef, â† hÎ±, scalar_apply, ne_eq, diagonal_const_mulVec, dotProduct_smul,
    smul_eq_mul] at this
  obtain âŸ¨thisl, thisâŸ© := this
  specialize this 1 (by simp)
  have this1 : star (1 : n â†’ ğ•œ) â¬áµ¥ (1 : n â†’ ğ•œ) = (Fintype.card n : â„) := by simp
  rw [thisÎ±, this1, â† RCLike.ofReal_mul, RCLike.ofReal_pos, mul_pos_iff] at this
  simp only [Nat.cast_pos, Fintype.card_pos] at this
  have this14 : Â¬(Fintype.card n : â„) < 0 := by simp only [not_lt, Nat.cast_nonneg]
  simp_rw [this14, and_false, and_true, or_false] at this
  have isU : (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ) â€¢ y' âˆˆ unitaryGroup n ğ•œ := by
    simp_rw [mem_unitaryGroup_iff', star_eq_conjTranspose, conjTranspose_smul,
      RCLike.star_def, Matrix.smul_mul, Matrix.mul_smul, RCLike.conj_ofReal,
      smul_smul, â† RCLike.ofReal_mul]
    rw [â† Real.rpow_add this, â† hÎ±, thisÎ±, scalar_apply, â† diagonal_smul]
    ext
    simp only [diagonal, RCLike.ofReal_re, one_div, Pi.smul_apply, smul_eq_mul,
      â† RCLike.ofReal_mul (K := ğ•œ), â† Real.rpow_add_one (NeZero.of_pos this).out, of_apply]
    norm_num; rfl
  let U : unitaryGroup n ğ•œ := âŸ¨_, isUâŸ©
  have hU : (U : Matrix n n ğ•œ) = (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ) â€¢ y' := rfl
  have hU2 : ((((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ) â€¢ y')â»Â¹ = (star U : Matrix n n ğ•œ) :=
    inv_eq_left_inv (by rw [â† hU, UnitaryGroup.star_mul_self])
  have hU3 : ((((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ) â€¢ y')â»Â¹ =
      (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ)â»Â¹ â€¢ y'â»Â¹ := by
    apply inv_eq_left_inv
    simp_rw [smul_mul, mul_smul, smul_smul]
    rw [inv_mul_cancelâ‚€, one_smul, inv_mul_of_invertible]
    Â· simp [Real.rpow_eq_zero_iff_of_nonneg (le_of_lt this), (NeZero.of_pos this).out]
  use U
  ext x
  simp_rw [â† hU2, hU3, hf, hU, smul_mul, mul_smul, smul_smul, â† RCLike.ofReal_inv,
    â† RCLike.ofReal_mul, â† Real.rpow_neg_one, â† Real.rpow_mul (le_of_lt this),
    â† Real.rpow_add this]
  norm_num
