/-
Copyright (c) 2019 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Patrick Massot, Casper Putz, Anne Baanen
-/
import Mathlib.Algebra.Star.Order
import Mathlib.Data.Matrix.Basic
import Mathlib.LinearAlgebra.StdBasis

#align_import linear_algebra.matrix.dot_product from "leanprover-community/mathlib"@"31c24aa72e7b3e5ed97a8412470e904f82b81004"

/-!
# Dot product of two vectors

This file contains some results on the map `Matrix.dotProduct`, which maps two
vectors `v w : n ‚Üí R` to the sum of the entrywise products `v i * w i`.

## Main results

* `Matrix.dotProduct_stdBasis_one`: the dot product of `v` with the `i`th
  standard basis vector is `v i`
* `Matrix.dotProduct_eq_zero_iff`: if `v`'s' dot product with all `w` is zero,
  then `v` is zero

## Tags

matrix, reindex

-/


variable {m n p R : Type*}

namespace Matrix

section Semiring

variable [Semiring R] [Fintype n]

@[simp]
theorem dotProduct_stdBasis_eq_mul [DecidableEq n] (v : n ‚Üí R) (c : R) (i : n) :
    dotProduct v (LinearMap.stdBasis R (fun _ => R) i c) = v i * c := by
  rw [dotProduct, Finset.sum_eq_single i, LinearMap.stdBasis_same]
  -- ‚ä¢ ‚àÄ (b : n), b ‚àà Finset.univ ‚Üí b ‚â† i ‚Üí v b * ‚Üë(LinearMap.stdBasis R (fun x =>  ‚Ä¶
  exact fun _ _ hb => by rw [LinearMap.stdBasis_ne _ _ _ _ hb, mul_zero]
  -- ‚ä¢ ¬¨i ‚àà Finset.univ ‚Üí v i * ‚Üë(LinearMap.stdBasis R (fun x => R) i) c i = 0
  exact fun hi => False.elim (hi <| Finset.mem_univ _)
  -- üéâ no goals
#align matrix.dot_product_std_basis_eq_mul Matrix.dotProduct_stdBasis_eq_mul

-- @[simp] -- Porting note: simp can prove this
theorem dotProduct_stdBasis_one [DecidableEq n] (v : n ‚Üí R) (i : n) :
    dotProduct v (LinearMap.stdBasis R (fun _ => R) i 1) = v i := by
  rw [dotProduct_stdBasis_eq_mul, mul_one]
  -- üéâ no goals
#align matrix.dot_product_std_basis_one Matrix.dotProduct_stdBasis_one

theorem dotProduct_eq (v w : n ‚Üí R) (h : ‚àÄ u, dotProduct v u = dotProduct w u) : v = w := by
  funext x
  -- ‚ä¢ v x = w x
  classical rw [‚Üê dotProduct_stdBasis_one v x, ‚Üê dotProduct_stdBasis_one w x, h]
  -- üéâ no goals
#align matrix.dot_product_eq Matrix.dotProduct_eq

theorem dotProduct_eq_iff {v w : n ‚Üí R} : (‚àÄ u, dotProduct v u = dotProduct w u) ‚Üî v = w :=
  ‚ü®fun h => dotProduct_eq v w h, fun h _ => h ‚ñ∏ rfl‚ü©
#align matrix.dot_product_eq_iff Matrix.dotProduct_eq_iff

theorem dotProduct_eq_zero (v : n ‚Üí R) (h : ‚àÄ w, dotProduct v w = 0) : v = 0 :=
  dotProduct_eq _ _ fun u => (h u).symm ‚ñ∏ (zero_dotProduct u).symm
#align matrix.dot_product_eq_zero Matrix.dotProduct_eq_zero

theorem dotProduct_eq_zero_iff {v : n ‚Üí R} : (‚àÄ w, dotProduct v w = 0) ‚Üî v = 0 :=
  ‚ü®fun h => dotProduct_eq_zero v h, fun h w => h.symm ‚ñ∏ zero_dotProduct w‚ü©
#align matrix.dot_product_eq_zero_iff Matrix.dotProduct_eq_zero_iff

end Semiring

section Self

variable [Fintype m] [Fintype n] [Fintype p]

@[simp]
theorem dotProduct_self_eq_zero [LinearOrderedRing R] {v : n ‚Üí R} : dotProduct v v = 0 ‚Üî v = 0 :=
  (Finset.sum_eq_zero_iff_of_nonneg fun i _ => mul_self_nonneg (v i)).trans <| by
    simp [Function.funext_iff]
    -- üéâ no goals
#align matrix.dot_product_self_eq_zero Matrix.dotProduct_self_eq_zero

section StarOrderedRing

variable [PartialOrder R] [NonUnitalRing R] [StarOrderedRing R] [NoZeroDivisors R]

/-- Note that this applies to `‚ÑÇ` via `Complex.strictOrderedCommRing`. -/
@[simp]
theorem dotProduct_star_self_eq_zero {v : n ‚Üí R} : dotProduct (star v) v = 0 ‚Üî v = 0 :=
  (Finset.sum_eq_zero_iff_of_nonneg fun i _ => (@star_mul_self_nonneg _ _ _ _ (v i) : _)).trans <|
    by simp [Function.funext_iff, mul_eq_zero]
       -- üéâ no goals
#align matrix.dot_product_star_self_eq_zero Matrix.dotProduct_star_self_eq_zero

/-- Note that this applies to `‚ÑÇ` via `Complex.strictOrderedCommRing`. -/
@[simp]
theorem dotProduct_self_star_eq_zero {v : n ‚Üí R} : dotProduct v (star v) = 0 ‚Üî v = 0 :=
  (Finset.sum_eq_zero_iff_of_nonneg fun i _ => (@star_mul_self_nonneg' _ _ _ _ (v i) : _)).trans <|
    by simp [Function.funext_iff, mul_eq_zero]
       -- üéâ no goals
#align matrix.dot_product_self_star_eq_zero Matrix.dotProduct_self_star_eq_zero

@[simp]
lemma conjTranspose_mul_self_eq_zero {A : Matrix m n R} : A·¥¥ * A = 0 ‚Üî A = 0 :=
  ‚ü®fun h => Matrix.ext fun i j =>
    (congr_fun <| dotProduct_star_self_eq_zero.1 <| Matrix.ext_iff.2 h j j) i,
  fun h => h ‚ñ∏ Matrix.mul_zero _‚ü©

@[simp]
lemma self_mul_conjTranspose_eq_zero {A : Matrix m n R} : A * A·¥¥ = 0 ‚Üî A = 0 :=
  ‚ü®fun h => Matrix.ext fun i j =>
    (congr_fun <| dotProduct_self_star_eq_zero.1 <| Matrix.ext_iff.2 h i i) j,
  fun h => h ‚ñ∏ Matrix.zero_mul _‚ü©

lemma conjTranspose_mul_self_mul_eq_zero (A : Matrix m n R) (B : Matrix n p R) :
    (A·¥¥ * A) * B = 0 ‚Üî A * B = 0 := by
  refine ‚ü®fun h => ?_, fun h => by simp only [Matrix.mul_assoc, h, Matrix.mul_zero]‚ü©
  -- ‚ä¢ A * B = 0
  apply_fun (B·¥¥ * ¬∑) at h
  -- ‚ä¢ A * B = 0
  rwa [Matrix.mul_zero, Matrix.mul_assoc, ‚Üê Matrix.mul_assoc, ‚Üê conjTranspose_mul,
    conjTranspose_mul_self_eq_zero] at h

lemma self_mul_conjTranspose_mul_eq_zero (A : Matrix m n R) (B : Matrix m p R) :
    (A * A·¥¥) * B = 0 ‚Üî A·¥¥ * B = 0 := by
  simpa only [conjTranspose_conjTranspose] using conjTranspose_mul_self_mul_eq_zero A·¥¥ _
  -- üéâ no goals

lemma mul_self_mul_conjTranspose_eq_zero (A : Matrix m n R) (B : Matrix p m R) :
    B * (A * A·¥¥) = 0 ‚Üî B * A = 0 := by
  rw [‚Üê conjTranspose_eq_zero, conjTranspose_mul, conjTranspose_mul, conjTranspose_conjTranspose,
    self_mul_conjTranspose_mul_eq_zero, ‚Üê conjTranspose_mul, conjTranspose_eq_zero]

lemma mul_conjTranspose_mul_self_eq_zero (A : Matrix m n R) (B : Matrix p n R) :
    B * (A·¥¥ * A) = 0 ‚Üî B * A·¥¥ = 0 := by
  simpa only [conjTranspose_conjTranspose] using mul_self_mul_conjTranspose_eq_zero A·¥¥ _
  -- üéâ no goals

lemma conjTranspose_mul_self_mulVec_eq_zero (A : Matrix m n R) (v : n ‚Üí R) :
    (A·¥¥ * A).mulVec v = 0 ‚Üî A.mulVec v = 0 := by
  simpa only [‚Üê Matrix.col_mulVec, col_eq_zero] using
    conjTranspose_mul_self_mul_eq_zero A (col v)

lemma self_mul_conjTranspose_mulVec_eq_zero (A : Matrix m n R) (v : m ‚Üí R) :
    (A * A·¥¥).mulVec v = 0 ‚Üî A·¥¥.mulVec v = 0 := by
  simpa only [conjTranspose_conjTranspose] using conjTranspose_mul_self_mulVec_eq_zero A·¥¥ _
  -- üéâ no goals

lemma vecMul_conjTranspose_mul_self_eq_zero (A : Matrix m n R) (v : n ‚Üí R) :
    vecMul v (A·¥¥ * A) = 0 ‚Üî vecMul v A·¥¥ = 0 := by
  simpa only [‚Üê Matrix.row_vecMul, row_eq_zero] using
    mul_conjTranspose_mul_self_eq_zero A (row v)

lemma vecMul_self_mul_conjTranspose_eq_zero (A : Matrix m n R) (v : m ‚Üí R) :
    vecMul v (A * A·¥¥) = 0 ‚Üî vecMul v A = 0 := by
  simpa only [conjTranspose_conjTranspose] using vecMul_conjTranspose_mul_self_eq_zero A·¥¥ _
  -- üéâ no goals

end StarOrderedRing

end Self

end Matrix
