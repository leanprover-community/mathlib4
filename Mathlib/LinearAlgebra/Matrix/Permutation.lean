/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
module

public import Mathlib.Analysis.CStarAlgebra.Matrix
public import Mathlib.Data.Matrix.PEquiv
public import Mathlib.Data.Set.Card
public import Mathlib.LinearAlgebra.Matrix.Determinant.Basic
public import Mathlib.LinearAlgebra.Matrix.Trace

/-!
# Permutation matrices

This file defines the matrix associated with a permutation

## Main definitions

- `Equiv.Perm.permMatrix`: the permutation matrix associated with an `Equiv.Perm`

## Main results

- `Matrix.det_permutation`: the determinant is the sign of the permutation
- `Matrix.trace_permutation`: the trace is the number of fixed points of the permutation

-/

@[expose] public section

open Equiv

variable {n R : Type*} [DecidableEq n] (Ïƒ Ï„ : Perm n)

variable (R) in
/-- the permutation matrix associated with an `Equiv.Perm` -/
abbrev Equiv.Perm.permMatrix [Zero R] [One R] : Matrix n n R :=
  Ïƒ.toPEquiv.toMatrix

namespace Matrix

@[simp] lemma permMatrix_refl [Zero R] [One R] : Equiv.Perm.permMatrix R (.refl n) = 1 := by
  simp [â† Matrix.ext_iff, Matrix.one_apply]

@[simp]
lemma permMatrix_one [Zero R] [One R] : (1 : Equiv.Perm n).permMatrix R = 1 :=
  permMatrix_refl

@[simp]
lemma transpose_permMatrix [Zero R] [One R] : (Ïƒ.permMatrix R).transpose = (Ïƒâ»Â¹).permMatrix R := by
  rw [â† PEquiv.toMatrix_symm, â† Equiv.toPEquiv_symm, â† Equiv.Perm.inv_def]

@[simp]
lemma conjTranspose_permMatrix [NonAssocSemiring R] [StarRing R] :
    (Ïƒ.permMatrix R).conjTranspose = (Ïƒâ»Â¹).permMatrix R := by
  simp only [conjTranspose, transpose_permMatrix, map]
  aesop

variable [Fintype n]

/-- The determinant of a permutation matrix equals its sign. -/
@[simp]
theorem det_permutation [CommRing R] : det (Ïƒ.permMatrix R) = Perm.sign Ïƒ := by
  rw [â† Matrix.mul_one (Ïƒ.permMatrix R), PEquiv.toMatrix_toPEquiv_mul,
    det_permute, det_one, mul_one]

/-- The trace of a permutation matrix equals the number of fixed points. -/
theorem trace_permutation [AddCommMonoidWithOne R] :
    trace (Ïƒ.permMatrix R) = (Function.fixedPoints Ïƒ).ncard := by
  delta trace
  simp [toPEquiv_apply, â† Set.ncard_coe_finset, Function.fixedPoints, Function.IsFixedPt]

lemma permMatrix_mulVec {v : n â†’ R} [CommRing R] :
    Ïƒ.permMatrix R *áµ¥ v = v âˆ˜ Ïƒ := by
  ext j
  simp [mulVec_eq_sum, Pi.single, Function.update, Equiv.eq_symm_apply]

lemma vecMul_permMatrix {v : n â†’ R} [CommRing R] :
    v áµ¥* Ïƒ.permMatrix R = v âˆ˜ Ïƒ.symm := by
  ext j
  simp [vecMul_eq_sum, Pi.single, Function.update, â† Equiv.symm_apply_eq]

@[simp]
lemma permMatrix_mul [NonAssocSemiring R] :
    (Ïƒ * Ï„).permMatrix R = Ï„.permMatrix R * Ïƒ.permMatrix R := by
  rw [Perm.permMatrix, Perm.mul_def, toPEquiv_trans, PEquiv.toMatrix_trans]

/-- `permMatrix` as a homomorphism. -/
@[simps]
def permMatrixHom [NonAssocSemiring R] : Perm n â†’* Matrix n n R where
  toFun Ïƒ := Ïƒâ»Â¹.permMatrix R
  map_one' := permMatrix_one
  map_mul' Ïƒ Ï„ := by rw [_root_.mul_inv_rev, permMatrix_mul]

open scoped Matrix.Norms.L2Operator

variable {ğ•œ : Type*} [RCLike ğ•œ]

/--
The l2-operator norm of a permutation matrix is bounded above by 1.
See `Matrix.permMatrix_l2_opNorm_eq` for the equality statement assuming the matrix is nonempty.
-/
theorem permMatrix_l2_opNorm_le : â€–Ïƒ.permMatrix ğ•œâ€– â‰¤ 1 :=
  ContinuousLinearMap.opNorm_le_bound _ (by simp) <| by
    simp [EuclideanSpace.norm_eq, toEuclideanLin_apply, permMatrix_mulVec,
      Ïƒ.sum_comp _ (fun i â†¦ â€–_â€– ^ 2)]

/--
The l2-operator norm of a nonempty permutation matrix is equal to 1.
Note that this is not true for the empty case, since the empty matrix has l2-operator norm 0.
See `Matrix.permMatrix_l2_opNorm_le` for the inequality version of the empty case.
-/
theorem permMatrix_l2_opNorm_eq [Nonempty n] : â€–Ïƒ.permMatrix ğ•œâ€– = 1 :=
  le_antisymm (permMatrix_l2_opNorm_le Ïƒ) <| by
    inhabit n
    simpa [EuclideanSpace.norm_eq, permMatrix_mulVec, â† Equiv.eq_symm_apply, apply_ite] using
      (Ïƒ.permMatrix ğ•œ).l2_opNorm_mulVec (WithLp.toLp _ (Pi.single default 1))

end Matrix
