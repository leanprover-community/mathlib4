/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.Analysis.NormedSpace.Star.ContinuousFunctionalCalculus
import Mathlib.Topology.ContinuousFunction.UniqueCFC
import Mathlib.Analysis.NormedSpace.Star.Matrix
import Mathlib.Algebra.Star.Unitary

/-!
# Continuous Functional Calculus for Hermitian Matrices

This file defines an instance of the continuous functional calculus for Hermitian matrices over an
RCLike field ğ•œ.

## Main Results

- definition of Ï† : the real StarAlgHom from C(spectrum â„ A, â„) to (Matrix n n ğ•œ) appearing in the
                    instance.
- instContinuousFunctionalCalculus : Instance of the Continuous functional Calculus for a hermitian
                                     matrix A over ğ•œ.

## Tags

spectral theorem, diagonalization theorem, continuous functional calculus
-/

section Prereq

@[simp, norm_cast]
lemma SemilinearMapClass.coe_coe {R S M Mâ‚ƒ F : Type*} [Semiring R] [Semiring S] [AddCommMonoid M]
    [AddCommMonoid Mâ‚ƒ] [Module R M] [Module S Mâ‚ƒ] {Ïƒ : R â†’+* S} (f : F) [FunLike F M Mâ‚ƒ]
    [SemilinearMapClass F Ïƒ M Mâ‚ƒ] :
    â‡‘(f : M â†’â‚›â‚—[Ïƒ] Mâ‚ƒ) = f :=
  rfl

instance Finite.instDiscreteTopology {Î± : Type*} [TopologicalSpace Î±] [T1Space Î±] [Finite Î±] :
    DiscreteTopology Î± := by
  rw [discreteTopology_iff_forall_isClosed]
  intro s
  let _ := Fintype.ofFinite s
  rw [show s = â‹ƒ x âˆˆ s.toFinset, {x} by simp]
  apply isClosed_biUnion_finset fun _ _ => isClosed_singleton

end Prereq

section SpectrumDiagonal

variable {R : Type*} [Field R] {n : Type*} [DecidableEq n][Fintype n]

open Module.End

/--  Standard basis vectors are eigenvectors of any associated diagonal linear operator. -/
lemma Matrix.hasEigenvector_toLin'_diagonal (d : n â†’ R) (i : n) :
    Module.End.HasEigenvector (Matrix.toLin' (diagonal d)) (d i) (Pi.basisFun R n i) := by
  constructor
  Â· rw [mem_eigenspace_iff]
    ext j
    simp only [diagonal, Pi.basisFun_apply, toLin'_apply, mulVec_stdBasis_apply, transpose_apply,
      of_apply, Pi.smul_apply, LinearMap.stdBasis_apply', smul_eq_mul, mul_ite, mul_one, mul_zero]
    split_ifs
    all_goals simp_all
  Â· rw [Function.ne_iff]; simp

/-- Eigenvalues of a diagonal linear operator are the diagonal entries. -/
lemma Matrix.hasEigenvalue_toLin'_diagonal_iff (d : n â†’ R) {Î¼ : R} :
    HasEigenvalue (toLin' (diagonal d)) Î¼ â†” âˆƒ i, d i = Î¼ := by
  have (i : n) : HasEigenvalue (toLin' (diagonal d)) (d i) := by
    exact hasEigenvalue_of_hasEigenvector <| hasEigenvector_toLin'_diagonal d i
  constructor
  Â· contrapose!
    intro hÎ¼ h_eig
    have h_iSup : â¨† Î¼ âˆˆ Set.range d, eigenspace (toLin' (diagonal d)) Î¼ = âŠ¤ := by
      rw [eq_top_iff, â† (Pi.basisFun R n).span_eq, Submodule.span_le]
      rintro - âŸ¨i, rflâŸ©
      simp only [SetLike.mem_coe]
      apply Submodule.mem_iSup_of_mem (d i)
      apply Submodule.mem_iSup_of_mem âŸ¨i, rflâŸ©
      rw [mem_eigenspace_iff]
      exact (hasEigenvector_toLin'_diagonal d i).apply_eq_smul
    have hÎ¼_not_mem : Î¼ âˆ‰ Set.range d := by simpa using fun i â†¦ (hÎ¼ i)
    have := eigenspaces_independent (toLin' (diagonal d)) |>.disjoint_biSup hÎ¼_not_mem
    rw [h_iSup, disjoint_top] at this
    exact h_eig this
  Â· rintro âŸ¨i, rflâŸ©
    exact this i

/-- The spectrum of the diagonal operator is the range of the diagonal viewed as a function. -/
lemma Matrix.spectrum_diagonal (d : n â†’ R) :
    spectrum R (diagonal d) = Set.range d := by
  ext Î¼
  rw [â† AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv <| Pi.basisFun R n),
    â† hasEigenvalue_iff_mem_spectrum, Set.mem_range]
  exact Matrix.hasEigenvalue_toLin'_diagonal_iff d

end SpectrumDiagonal
namespace Matrix

variable {n ğ•œ : Type*} [RCLike ğ•œ] [Fintype n] [DecidableEq n] {A : Matrix n n ğ•œ}

lemma finite_real_spectrum : (spectrum â„ A).Finite := by
  rw [â† spectrum.preimage_algebraMap ğ•œ]
  exact A.finite_spectrum.preimage (NoZeroSMulDivisors.algebraMap_injective â„ ğ•œ).injOn

instance : Finite (spectrum â„ A) := A.finite_real_spectrum

namespace IsHermitian

variable (hA : IsHermitian A)

/-- The â„-spectrum of a Hermitian Matrix over RCLike field is the range of the eigenvalue function-/
theorem eigenvalues_eq_spectrum {a : Matrix n n ğ•œ} (ha : IsHermitian a) :
    (spectrum â„ a) = Set.range (ha.eigenvalues) := by
  ext x
  conv_lhs => rw [ha.spectral_theorem, unitary.spectrum.unitary_conjugate,
  â† spectrum.algebraMap_mem_iff ğ•œ, spectrum_diagonal, RCLike.algebraMap_eq_ofReal]
  simp

/--Eigenvalues of a Hermitian Matrix, coerced, belong to the spectrum of the assoc.toEuclideanLin -/
theorem ofReal_eigenvalue_mem_spectrum_toEuclideanLin (i : n) :
    (RCLike.ofReal âˆ˜ hA.eigenvalues) i âˆˆ spectrum ğ•œ (toEuclideanLin A) :=
  LinearMap.IsSymmetric.hasEigenvalue_eigenvalues _ _ _ |>.mem_spectrum

/-- Algebra equivalence between the linear maps and continuous linear maps on a finite-dim module.
Compare with `LinearMap.toContinuousLinearMap`, the linear equivalence version of this result.-/
def Module.End.toContinuousLinearMap.{v} (E : Type v) [NormedAddCommGroup E]
    [NormedSpace ğ•œ E][FiniteDimensional ğ•œ E] : (E â†’â‚—[ğ•œ] E) â‰ƒâ‚[ğ•œ] (E â†’L[ğ•œ] E) :=
    {LinearMap.toContinuousLinearMap with
    map_mul' := fun _ _ â†¦ rfl
    commutes' := fun _ â†¦ rfl}

/--Spectrum of a Hermitian matrix equals the spectrum as a EuclideanLin. -/
theorem spec_toEuclideanLin_eq_spec : spectrum ğ•œ (toEuclideanLin A) = spectrum ğ•œ A :=
  AlgEquiv.spectrum_eq ((AlgEquiv.trans ((toEuclideanCLM : Matrix n n ğ•œ â‰ƒâ‹†â‚[ğ•œ]
  EuclideanSpace ğ•œ n â†’L[ğ•œ] EuclideanSpace ğ•œ n) : Matrix n n ğ•œ â‰ƒâ‚[ğ•œ]
  EuclideanSpace ğ•œ n â†’L[ğ•œ] EuclideanSpace ğ•œ n))
  (Module.End.toContinuousLinearMap (EuclideanSpace ğ•œ n)).symm) _

/--Definition of the StarAlgHom for the functional calculus of a Hermitian matrix. -/
@[simps]
noncomputable def cfcAux : C(spectrum â„ A, â„) â†’â‹†â‚[â„] (Matrix n n ğ•œ) where
  toFun := fun g => (eigenvectorUnitary hA : Matrix n n ğ•œ) *
    diagonal (RCLike.ofReal âˆ˜ g âˆ˜ (fun i â†¦ âŸ¨hA.eigenvalues i, hA.eigenvalues_mem_spectrum_real iâŸ©))
    * star (eigenvectorUnitary hA : Matrix n n ğ•œ)
  map_one' := by simp [Pi.one_def (f := fun _ : n â†¦ ğ•œ)]
  map_mul' f g := by
    have {a b c d e f : Matrix n n ğ•œ} : (a * b * c) * (d * e * f) = a * (b * (c * d) * e) * f := by
      simp only [mul_assoc]
    simp only [this, ContinuousMap.coe_mul, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one,
      diagonal_mul_diagonal, Function.comp_apply]
    congr! with i
    simp
  map_zero' := by simp [Pi.zero_def (f := fun _ : n â†¦ ğ•œ)]
  map_add' f g := by
    simp only [ContinuousMap.coe_add, â† add_mul, â† mul_add, diagonal_add, Function.comp_apply]
    congr! with i
    simp
  commutes' r := by
    simp only [Function.comp, algebraMap_apply, smul_eq_mul, mul_one]
    rw [â† mul_one (algebraMap _ _ _), â† unitary.coe_mul_star_self hA.eigenvectorUnitary,
      â† Algebra.left_comm, unitary.coe_star, mul_assoc]
    congr!
  map_star' f := by
    simp only [star_trivial, StarMul.star_mul, star_star, star_eq_conjTranspose (diagonal _),
      diagonal_conjTranspose, mul_assoc]
    congr!
    ext
    simp

lemma closedEmbedding_cfcAux : ClosedEmbedding hA.cfcAux := by
  have h0 : FiniteDimensional â„ C(spectrum â„ A, â„) :=
    FiniteDimensional.of_injective (ContinuousMap.coeFnLinearMap â„ (M := â„)) DFunLike.coe_injective
  refine LinearMap.closedEmbedding_of_injective (ğ•œ := â„) (E := C(spectrum â„ A, â„))
    (F := Matrix n n ğ•œ) (f := hA.cfcAux) <| LinearMap.ker_eq_bot'.mpr fun f hf â†¦ ?_
  have h2 :
      diagonal (RCLike.ofReal âˆ˜ f âˆ˜ fun i â†¦ âŸ¨hA.eigenvalues i, hA.eigenvalues_mem_spectrum_real iâŸ©)
        = (0 : Matrix n n ğ•œ) := by
    simp only [SemilinearMapClass.coe_coe, cfcAux_apply] at hf
    replace hf := congr($(hf) * (eigenvectorUnitary hA : Matrix n n ğ•œ))
    simp only [mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one, zero_mul] at hf
    simpa [â† mul_assoc] using congr((star hA.eigenvectorUnitary : Matrix n n ğ•œ) * $(hf))
  ext x
  simp only [ContinuousMap.zero_apply]
  obtain âŸ¨x, hxâŸ© := x
  obtain âŸ¨i, rflâŸ© := hA.eigenvalues_eq_spectrum â–¸ hx
  rw [â† diagonal_zero] at h2
  have := (diagonal_eq_diagonal_iff).mp h2
  refine RCLike.ofReal_eq_zero.mp (this i)

lemma cfcAux_id : hA.cfcAux (.restrict (spectrum â„ A) (.id â„)) = A := by
  conv_rhs => rw [hA.spectral_theorem]
  congr!

/-- Instance of the continuous functional calculus for a Hermitian matrix over `ğ•œ` with
`RCLike ğ•œ`. -/
instance instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„ (IsSelfAdjoint : Matrix n n ğ•œ â†’ Prop) where
  exists_cfc_of_predicate a ha := by
    replace ha : IsHermitian a := ha
    refine âŸ¨ha.cfcAux, ha.closedEmbedding_cfcAux, ha.cfcAux_id, fun f â†¦ ?map_spec,
      fun f â†¦ ?hermitianâŸ©
    case map_spec =>
      apply Set.eq_of_subset_of_subset
      Â· rw [â† ContinuousMap.spectrum_eq_range f]
        apply AlgHom.spectrum_apply_subset
      Â· rw [cfcAux_apply, unitary.spectrum.unitary_conjugate]
        rintro - âŸ¨x , rflâŸ©
        apply spectrum.of_algebraMap_mem ğ•œ
        simp only [Function.comp_apply, Set.mem_range, spectrum_diagonal]
        obtain âŸ¨x, hxâŸ© := x
        obtain âŸ¨i, rflâŸ© := ha.eigenvalues_eq_spectrum â–¸ hx
        exact âŸ¨i, rflâŸ©
    case hermitian =>
      simp only [isSelfAdjoint_iff, cfcAux_apply, mul_assoc, star_mul, star_star]
      rw [star_eq_conjTranspose, diagonal_conjTranspose]
      congr!
      simp [Pi.star_def, Function.comp]

instance instUniqueContinuousFunctionalCalculus :
    UniqueContinuousFunctionalCalculus â„ (Matrix n n ğ•œ) :=
  let _ : NormedRing (Matrix n n ğ•œ) := Matrix.linftyOpNormedRing
  let _ : NormedAlgebra â„ (Matrix n n ğ•œ) := Matrix.linftyOpNormedAlgebra
  inferInstance

/-- The continuous functional calculus of a Hermitian matrix as a triple product using the
spectral theorem. Note that this actually operates on bare functions since every function is
continuous on the spectrum of a matrix, since the spectrum is finite. This is shown to be equal to
the generic continuous functional calculus API in `Matrix.IsHermitian.cfc_eq`. In general, users
should prefer the generic API, especially because it will make rewriting easier. -/
protected noncomputable def cfc (f : â„ â†’ â„) : Matrix n n ğ•œ :=
  (eigenvectorUnitary hA : Matrix n n ğ•œ) * diagonal (RCLike.ofReal âˆ˜ f âˆ˜ hA.eigenvalues)
    * star (eigenvectorUnitary hA : Matrix n n ğ•œ)

lemma cfc_eq (f : â„ â†’ â„) : cfc f A = hA.cfc f := by
  have hA' : IsSelfAdjoint A := hA
  have := cfcHom_eq_of_continuous_of_map_id hA' hA.cfcAux hA.closedEmbedding_cfcAux.continuous
    hA.cfcAux_id
  rw [cfc_apply f A hA' (by rw [continuousOn_iff_continuous_restrict]; fun_prop), this]
  rfl

end IsHermitian
end Matrix
