/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.Analysis.NormedSpace.Star.ContinuousFunctionalCalculus
import Mathlib.Topology.ContinuousFunction.UniqueCFC
import Mathlib.Analysis.NormedSpace.Star.Matrix

/-!
# Continuous Functional Calculus for Hermitian Matrices

This file defines an instance of the continuous functional calculus for Hermitian matrices over an
RCLike field ğ•œ.

## Main Results

- definition of Ï† : the real StarAlgHom from C(spectrum â„ A, â„) to (Matrix n n ğ•œ) appearing in the
                    instance.
- instContinuousFunctionalCalculus : Instance of the Continuous functional Calculus for a hermitian
                                     matrix A over ğ•œ.

## Tags

spectral theorem, diagonalization theorem, continuous functional calculus
-/

section ConjugateUnits

variable {R A : Type*} [CommSemiring R] [Ring A] [Algebra R A]

/-- Conjugation by a unit preserves the spectrum, inverse on right. -/
@[simp]
lemma spectrum.conjugate_units {a : A} {u : AË£} :
    spectrum R (u * a * uâ»Â¹) = spectrum R a := by
  suffices âˆ€ (b : A) (v : AË£), spectrum R (v * b * vâ»Â¹) âŠ† spectrum R b by
    refine le_antisymm (this a u) ?_
    apply le_of_eq_of_le ?_ <| this (u * a * uâ»Â¹) uâ»Â¹
    simp [mul_assoc]
  intro a u Î¼ hÎ¼
  rw [spectrum.mem_iff] at hÎ¼ âŠ¢
  contrapose! hÎ¼
  simpa [mul_sub, sub_mul, Algebra.right_comm] using u.isUnit.mul hÎ¼ |>.mul uâ»Â¹.isUnit

/-- Conjugation by a unit preserves the spectrum, inverse on left. -/
@[simp]
lemma spectrum.conjugate_units' {a : A} {u : AË£} :
    spectrum R (uâ»Â¹ * a * u) = spectrum R a := by
  simpa using spectrum.conjugate_units (u := uâ»Â¹)

end ConjugateUnits

section UnitaryConjugate

universe u

variable {R A : Type*} [CommSemiring R] [Ring A] [Algebra R A] [StarMul A]

/-- Unitary conjugation preserves the spectrum, star on left. -/
@[simp]
lemma spectrum.unitary_conjugate {a : A} {u : unitary A} :
    spectrum R (u * a * (star u : A)) = spectrum R a :=
  spectrum.conjugate_units (u := unitary.toUnits u)

/-- Unitary conjugation preserves the spectrum, star on right. -/
@[simp]
lemma spectrum.unitary_conjugate' {a : A} {u : unitary A} :
    spectrum R ((star u : A) * a * u) = spectrum R a := by
  simpa using spectrum.unitary_conjugate (u := star u)

end UnitaryConjugate

section FiniteSpectrum

universe u v w

/-- An endomorphism of a finite-dimensional vector space has a finite spectrum. -/
theorem Module.End.finite_spectrum {K : Type v} {V : Type w} [Field K] [AddCommGroup V]
    [Module K V] [FiniteDimensional K V] (f : Module.End K V) :
    Set.Finite (spectrum K f) := by
  convert f.finite_hasEigenvalue
  ext f x
  exact Module.End.hasEigenvalue_iff_mem_spectrum.symm

variable {n R : Type*} [Field R] [Fintype n] [DecidableEq n]

/-- An n x n matrix over a ring has a finite spectrum. -/
theorem Matrix.finite_spectrum (A : Matrix n n R) : Set.Finite (spectrum R A) := by
  rw [â† AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv <| Pi.basisFun R n) A]
  exact Module.End.finite_spectrum _

instance Matrix.instFiniteSpectrum (A : Matrix n n R) : Finite (spectrum R A) :=
  Set.finite_coe_iff.mpr (Matrix.finite_spectrum A)

end FiniteSpectrum

section SpectrumDiagonal

variable {R : Type*} [Field R] {n : Type*} [DecidableEq n][Fintype n]

open Module.End

/--  Standard basis vectors are eigenvectors of any associated diagonal linear operator. -/
lemma Matrix.hasEigenvector_toLin'_diagonal (d : n â†’ R) (i : n) :
    Module.End.HasEigenvector (Matrix.toLin' (diagonal d)) (d i) (Pi.basisFun R n i) := by
  constructor
  Â· rw [mem_eigenspace_iff]
    ext j
    simp only [diagonal, Pi.basisFun_apply, toLin'_apply, mulVec_stdBasis_apply, transpose_apply,
      of_apply, Pi.smul_apply, LinearMap.stdBasis_apply', smul_eq_mul, mul_ite, mul_one, mul_zero]
    split_ifs
    all_goals simp_all
  Â· rw [Function.ne_iff]; simp

/-- Eigenvalues of a diagonal linear operator are the diagonal entries. -/
lemma Matrix.hasEigenvalue_toLin'_diagonal_iff (d : n â†’ R) {Î¼ : R} :
    HasEigenvalue (toLin' (diagonal d)) Î¼ â†” âˆƒ i, d i = Î¼ := by
  have (i : n) : HasEigenvalue (toLin' (diagonal d)) (d i) := by
    exact hasEigenvalue_of_hasEigenvector <| hasEigenvector_toLin'_diagonal d i
  constructor
  Â· contrapose!
    intro hÎ¼ h_eig
    have h_iSup : â¨† Î¼ âˆˆ Set.range d, eigenspace (toLin' (diagonal d)) Î¼ = âŠ¤ := by
      rw [eq_top_iff, â† (Pi.basisFun R n).span_eq, Submodule.span_le]
      rintro - âŸ¨i, rflâŸ©
      simp only [SetLike.mem_coe]
      apply Submodule.mem_iSup_of_mem (d i)
      apply Submodule.mem_iSup_of_mem âŸ¨i, rflâŸ©
      rw [mem_eigenspace_iff]
      exact (hasEigenvector_toLin'_diagonal d i).apply_eq_smul
    have hÎ¼_not_mem : Î¼ âˆ‰ Set.range d := by simpa using fun i â†¦ (hÎ¼ i)
    have := eigenspaces_independent (toLin' (diagonal d)) |>.disjoint_biSup hÎ¼_not_mem
    rw [h_iSup, disjoint_top] at this
    exact h_eig this
  Â· rintro âŸ¨i, rflâŸ©
    exact this i

/-- The spectrum of the diagonal operator is the range of the diagonal viewed as a function. -/
lemma Matrix.spectrum_diagonal (d : n â†’ R) :
    spectrum R (diagonal d) = Set.range d := by
  ext Î¼
  rw [â† AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv <| Pi.basisFun R n),
    â† hasEigenvalue_iff_mem_spectrum, Set.mem_range]
  exact Matrix.hasEigenvalue_toLin'_diagonal_iff d

end SpectrumDiagonal
namespace Matrix

variable {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]

namespace IsHermitian

variable [DecidableEq n]

variable {A : Matrix n n ğ•œ} (hA : IsHermitian A)

/--Eigenvalues of a Hermitian Matrix, coerced, belong to the spectrum of the assoc.toEuclideanLin -/
theorem eigenvalue_mem_toEuclideanLin_spectrum_RCLike (i : n) :
    (RCLike.ofReal âˆ˜ hA.eigenvalues) i âˆˆ spectrum ğ•œ (toEuclideanLin A) :=
  LinearMap.IsSymmetric.hasEigenvalue_eigenvalues _ _ _ |>.mem_spectrum

/-- Algebra equivalence between the linear maps and continuous linear maps on a finite-dim module.-/
def AlgEquivFiniteDimNormedLinearCLM.{v} (E : Type v) [NormedAddCommGroup E]
    [NormedSpace ğ•œ E][FiniteDimensional ğ•œ E] :
    AlgEquiv (R := ğ•œ) (A := E â†’â‚—[ğ•œ] E) (B := E â†’L[ğ•œ] E) :=
    {LinearMap.toContinuousLinearMap with
    map_mul' := fun _ _ â†¦ rfl
    commutes' := fun _ â†¦ rfl}

/--Spectrum of a Hermitian matrix equals the spectrum as a EuclideanLin. -/
theorem spec_toEuclideanLin_eq_spec : spectrum ğ•œ (toEuclideanLin A) = spectrum ğ•œ A :=
  AlgEquiv.spectrum_eq ((AlgEquiv.trans ((toEuclideanCLM : Matrix n n ğ•œ â‰ƒâ‹†â‚[ğ•œ]
  EuclideanSpace ğ•œ n â†’L[ğ•œ] EuclideanSpace ğ•œ n) : Matrix n n ğ•œ â‰ƒâ‚[ğ•œ]
  EuclideanSpace ğ•œ n â†’L[ğ•œ] EuclideanSpace ğ•œ n))
  (AlgEquivFiniteDimNormedLinearCLM (EuclideanSpace ğ•œ n)).symm) _

/--Eigenvalues of a hermitian matrix A are in the â„ spectrum of A. -/
theorem eigenvalue_mem_real : âˆ€ (i : n), (hA.eigenvalues) i âˆˆ spectrum â„ A := by
  intro i
  apply spectrum.of_algebraMap_mem (S := ğ•œ) (R := â„) (A := Matrix n n ğ•œ)
  rw [â† spec_toEuclideanLin_eq_spec]
  apply hA.eigenvalue_mem_toEuclideanLin_spectrum_RCLike i

/--Definition of the StarAlgHom for the continuous functional calculus of a Hermitian matrix. -/
@[simps]
noncomputable def Ï† : StarAlgHom â„ C(spectrum â„ A, â„) (Matrix n n ğ•œ) where
  toFun := fun g => (eigenvectorUnitary hA : Matrix n n ğ•œ) *
    diagonal (RCLike.ofReal âˆ˜ g âˆ˜ (fun i â†¦ âŸ¨hA.eigenvalues i, hA.eigenvalue_mem_real iâŸ©))
    * star (eigenvectorUnitary hA : Matrix n n ğ•œ)
  map_one' := by simp [Pi.one_def (f := fun _ : n â†¦ ğ•œ)]
  map_mul' f g := by
    have {a b c d e f : Matrix n n ğ•œ} : (a * b * c) * (d * e * f) = a * (b * (c * d) * e) * f := by
      simp only [mul_assoc]
    simp only [this, ContinuousMap.coe_mul, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one,
      diagonal_mul_diagonal, Function.comp_apply]
    congr! with i
    simp
  map_zero' := by simp [Pi.zero_def (f := fun _ : n â†¦ ğ•œ)]
  map_add' f g := by
    simp only [ContinuousMap.coe_add, â† add_mul, â† mul_add, diagonal_add, Function.comp_apply]
    congr! with i
    simp
  commutes' r := by
    simp only [Function.comp, algebraMap_apply, smul_eq_mul, mul_one]
    rw [IsScalarTower.algebraMap_apply â„ ğ•œ _ r, RCLike.algebraMap_eq_ofReal,
      â† mul_one (algebraMap _ _ _), â† unitary.coe_mul_star_self hA.eigenvectorUnitary,
      â† Algebra.left_comm, unitary.coe_star, mul_assoc]
    congr!
  map_star' f := by
    simp only [star_trivial, StarMul.star_mul, star_star, star_eq_conjTranspose (diagonal _),
      diagonal_conjTranspose, mul_assoc]
    congr!
    ext
    simp

/-- The â„-spectrum of a Hermitian Matrix over RCLike field is the range of the eigenvalue function-/
theorem eigenvalues_eq_spectrum {a : Matrix n n ğ•œ} (ha : IsHermitian a) :
    (spectrum â„ a) = Set.range (ha.eigenvalues) := by
  ext x
  conv_lhs => rw [ha.spectral_theorem, spectrum.unitary_conjugate,
  â† spectrum.algebraMap_mem_iff ğ•œ, spectrum_diagonal, RCLike.algebraMap_eq_ofReal]
  simp

/--The â„-spectrum of an n x n Hermitian matrix is finite. -/
theorem finite_spectrum {a : Matrix n n ğ•œ} (ha : IsHermitian a) : (spectrum â„ a).Finite := by
  have H := Set.finite_range (ha.eigenvalues)
  exact (ha.eigenvalues_eq_spectrum).symm â–¸ H

/-- The â„-spectrum of an n x n Hermitian matrix over an RCLike field is a compact space. -/
theorem compact_spectrum {a : Matrix n n ğ•œ} (ha : IsHermitian a) : CompactSpace (spectrum â„ a) := by
  convert Finite.compactSpace (X := spectrum â„ a)
  refine Set.finite_coe_iff.mpr ?_
  apply finite_spectrum
  assumption

/-- Instance of the Continuous Functional Calculus for a Hermitian Matrix over an RCLike field.-/
instance instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„ (IsHermitian : Matrix n n ğ•œ â†’ Prop) where
  exists_cfc_of_predicate a ha := by
    refine âŸ¨Ï† ha, ?closedEmbedding, ?mapId, ?map_spec, ?hermitianâŸ©
    case closedEmbedding =>
      have h0 : FiniteDimensional â„ C(spectrum â„ a, â„) := by
        have : Finite (spectrum â„ a) := by refine finite_spectrum ha
        apply FiniteDimensional.of_injective (ContinuousMap.coeFnLinearMap â„ (M := â„))
        exact DFunLike.coe_injective
      have hÏ† : LinearMap.ker ha.Ï† = âŠ¥ := by
        refine LinearMap.ker_eq_bot'.mpr ?_
        intro f hf
        have h2 : diagonal
             (RCLike.ofReal âˆ˜ â‡‘f âˆ˜ fun i â†¦ âŸ¨ha.eigenvalues i, ha.eigenvalue_mem_real iâŸ©)
             = (0 : Matrix n n ğ•œ) := by
           rw [Ï†_apply] at hf
           have hlr : (star ha.eigenvectorUnitary : Matrix n n ğ•œ) *
              ((eigenvectorUnitary ha : Matrix n n ğ•œ) * diagonal (RCLike.ofReal âˆ˜ f âˆ˜
                (fun i â†¦ âŸ¨ha.eigenvalues i, ha.eigenvalue_mem_real iâŸ©)) *
                star (eigenvectorUnitary ha : Matrix n n ğ•œ)) *
                (ha.eigenvectorUnitary : Matrix n n ğ•œ) =
                (star ha.eigenvectorUnitary : Matrix n n ğ•œ) *
                (0 : Matrix n n ğ•œ) * (ha.eigenvectorUnitary : Matrix n n ğ•œ) := by congr
           simp only [â† mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, one_mul,
                    mul_zero, zero_mul] at hlr
           simp only [mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one] at hlr
           exact hlr
        ext x
        simp only [ContinuousMap.zero_apply]
        obtain âŸ¨x, hxâŸ© := x
        obtain âŸ¨i, rflâŸ© := ha.eigenvalues_eq_spectrum â–¸ hx
        rw [â† diagonal_zero] at h2
        have := (diagonal_eq_diagonal_iff).mp h2
        exact RCLike.ofReal_eq_zero.mp (this i)
      have H := ha.compact_spectrum
      apply LinearMap.closedEmbedding_of_injective (ğ•œ := â„) (E := C(spectrum â„ a, â„))
        (F := Matrix n n ğ•œ) (f := ha.Ï†) hÏ†
    case mapId =>
      conv_rhs => rw [ha.spectral_theorem]
      congr!
    case map_spec =>
      intro f
      apply Set.eq_of_subset_of_subset
      Â· rw [â† ContinuousMap.spectrum_eq_range f]
        apply AlgHom.spectrum_apply_subset
      Â· rw [Ï†_apply ,spectrum.unitary_conjugate]
        rintro - âŸ¨x , rflâŸ©
        apply spectrum.of_algebraMap_mem (R := â„) (S := ğ•œ)
        simp only [spectrum_diagonal (R := ğ•œ)
            (RCLike.ofReal âˆ˜ f âˆ˜ (fun i â†¦ âŸ¨ha.eigenvalues i, ha.eigenvalue_mem_real iâŸ©))
            , Function.comp_apply, Set.mem_range]
        obtain âŸ¨x, hxâŸ© := x
        obtain âŸ¨i, rflâŸ© := ha.eigenvalues_eq_spectrum â–¸ hx
        exact âŸ¨i, rflâŸ©
    case hermitian =>
      intro f
      simp only [Ï†_apply, mul_assoc, IsHermitian, â† star_eq_conjTranspose, star_mul, star_star]
      congr!
      rw [star_eq_conjTranspose, diagonal_conjTranspose]
      congr!
      simp only [Pi.star_def, Function.comp_apply, RCLike.star_def, RCLike.conj_ofReal]
      rfl
end IsHermitian
end Matrix
