/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.Analysis.NormedSpace.Star.ContinuousFunctionalCalculus
import Mathlib.Topology.ContinuousFunction.UniqueCFC
import Mathlib.Analysis.NormedSpace.Star.Matrix

/-
This file defines an instance of the continuous functional calculus for Hermitian matrices over an
RCLike field ùïú.

## Tags

spectral theorem, diagonalization theorem, continuous functional calculus
-/

section ConjugateUnits

variable {R A : Type*} [CommSemiring R] [Ring A] [Algebra R A]

/-- Conjugation by a unit preserves the spectrum, inverse on right. -/
@[simp]
lemma spectrum.conjugate_units {a : A} {u : AÀ£} :
    spectrum R (u * a * u‚Åª¬π) = spectrum R a := by
  suffices ‚àÄ (b : A) (v : AÀ£), spectrum R (v * b * v‚Åª¬π) ‚äÜ spectrum R b by
    refine le_antisymm (this a u) ?_
    apply le_of_eq_of_le ?_ <| this (u * a * u‚Åª¬π) u‚Åª¬π
    simp [mul_assoc]
  intro a u Œº hŒº
  rw [spectrum.mem_iff] at hŒº ‚ä¢
  contrapose! hŒº
  simpa [mul_sub, sub_mul, Algebra.right_comm] using u.isUnit.mul hŒº |>.mul u‚Åª¬π.isUnit

/-- Conjugation by a unit preserves the spectrum, inverse on left. -/
@[simp]
lemma spectrum.conjugate_units' {a : A} {u : AÀ£} :
    spectrum R (u‚Åª¬π * a * u) = spectrum R a := by
  simpa using spectrum.conjugate_units (u := u‚Åª¬π)

end ConjugateUnits

section UnitaryConjugate

universe u

variable {R A : Type*} [CommSemiring R] [Ring A] [Algebra R A] [StarMul A]

/-- Unitary conjugation preserves the spectrum, star on left. -/
@[simp]
lemma spectrum.unitary_conjugate {a : A} {u : unitary A} :
    spectrum R (u * a * (star u : A)) = spectrum R a :=
  spectrum.conjugate_units (u := unitary.toUnits u)

/-- Unitary conjugation preserves the spectrum, star on right. -/
@[simp]
lemma spectrum.unitary_conjugate' {a : A} {u : unitary A} :
    spectrum R ((star u : A) * a * u) = spectrum R a := by
  simpa using spectrum.unitary_conjugate (u := star u)

end UnitaryConjugate

section FiniteSpectrum

universe u v w

/-- An endomorphism of a finite-dimensional vector space has a finite spectrum. -/
theorem Module.End.finite_spectrum {K : Type v} {V : Type w} [Field K] [AddCommGroup V]
    [Module K V] [FiniteDimensional K V] (f : Module.End K V) :
    Set.Finite (spectrum K f) := by
  convert f.finite_hasEigenvalue
  ext f x
  exact Module.End.hasEigenvalue_iff_mem_spectrum.symm

variable {n R : Type*} [Field R] [Fintype n] [DecidableEq n]

/-- An n x n matrix over a ring has a finite spectrum. -/
theorem Matrix.finite_spectrum (A : Matrix n n R) : Set.Finite (spectrum R A) := by
  rw [‚Üê AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv <| Pi.basisFun R n) A]
  exact Module.End.finite_spectrum _

instance Matrix.instFiniteSpectrum (A : Matrix n n R) : Finite (spectrum R A) :=
  Set.finite_coe_iff.mpr (Matrix.finite_spectrum A)

end FiniteSpectrum

section SpectrumDiagonal

variable {R : Type*} [Field R] {n : Type*} [DecidableEq n][Fintype n]

open Module.End

lemma Matrix.hasEigenvector_toLin'_diagonal (d : n ‚Üí R) (i : n) :
    Module.End.HasEigenvector (Matrix.toLin' (diagonal d)) (d i) (Pi.basisFun R n i) := by
  constructor
  ¬∑ rw [mem_eigenspace_iff]
    ext j
    simp only [diagonal, Pi.basisFun_apply, toLin'_apply, mulVec_stdBasis_apply, transpose_apply,
      of_apply, Pi.smul_apply, LinearMap.stdBasis_apply', smul_eq_mul, mul_ite, mul_one, mul_zero]
    split_ifs
    all_goals simp_all
  ¬∑ rw [Function.ne_iff]; simp

lemma Matrix.hasEigenvalue_toLin'_diagonal_iff (d : n ‚Üí R) {Œº : R} :
    HasEigenvalue (toLin' (diagonal d)) Œº ‚Üî ‚àÉ i, d i = Œº := by
  have (i : n) : HasEigenvalue (toLin' (diagonal d)) (d i) := by
    exact hasEigenvalue_of_hasEigenvector <| hasEigenvector_toLin'_diagonal d i
  constructor
  ¬∑ contrapose!
    intro hŒº h_eig
    have h_iSup : ‚®Ü Œº ‚àà Set.range d, eigenspace (toLin' (diagonal d)) Œº = ‚ä§ := by
      rw [eq_top_iff, ‚Üê (Pi.basisFun R n).span_eq, Submodule.span_le]
      rintro - ‚ü®i, rfl‚ü©
      simp only [SetLike.mem_coe]
      apply Submodule.mem_iSup_of_mem (d i)
      apply Submodule.mem_iSup_of_mem ‚ü®i, rfl‚ü©
      rw [mem_eigenspace_iff]
      exact (hasEigenvector_toLin'_diagonal d i).apply_eq_smul
    have hŒº_not_mem : Œº ‚àâ Set.range d := by simpa using fun i ‚Ü¶ (hŒº i)
    have := eigenspaces_independent (toLin' (diagonal d)) |>.disjoint_biSup hŒº_not_mem
    rw [h_iSup, disjoint_top] at this
    exact h_eig this
  ¬∑ rintro ‚ü®i, rfl‚ü©
    exact this i

lemma Matrix.spectrum_diagonal (d : n ‚Üí R) :
    spectrum R (diagonal d) = Set.range d := by
  ext Œº
  rw [‚Üê AlgEquiv.spectrum_eq (Matrix.toLinAlgEquiv <| Pi.basisFun R n),
    ‚Üê hasEigenvalue_iff_mem_spectrum, Set.mem_range]
  exact Matrix.hasEigenvalue_toLin'_diagonal_iff d

end SpectrumDiagonal
namespace Matrix

variable {ùïú : Type*} [RCLike ùïú] {n : Type*} [Fintype n]

namespace IsHermitian

variable [DecidableEq n]

variable {A : Matrix n n ùïú} (hA : IsHermitian A)

theorem eigenvalue_mem_toEuclideanLin_spectrum_RCLike (i : n) :
    (RCLike.ofReal ‚àò hA.eigenvalues) i ‚àà spectrum ùïú (toEuclideanLin A) :=
  LinearMap.IsSymmetric.hasEigenvalue_eigenvalues _ _ _ |>.mem_spectrum

/-The following needs a name change-/
theorem range_thm_RCLike : Set.range
    (fun (i : n) ‚Ü¶ (RCLike.ofReal ‚àò hA.eigenvalues) i) ‚äÜ (spectrum ùïú (toEuclideanLin A)) := by
    rw [Set.range_subset_iff]
    apply eigenvalue_mem_toEuclideanLin_spectrum_RCLike

def AlgEquivFiniteDimNormedLinearCLM.{v} (E : Type v) [NormedAddCommGroup E]
    [NormedSpace ùïú E][FiniteDimensional ùïú E] :
    AlgEquiv (R := ùïú) (A := E ‚Üí‚Çó[ùïú] E) (B := E ‚ÜíL[ùïú] E) :=
    {LinearMap.toContinuousLinearMap with
    map_mul' := fun _ _ ‚Ü¶ rfl
    commutes' := fun _ ‚Ü¶ rfl}

theorem spec_toEuclideanLin_eq_spec : spectrum ùïú (toEuclideanLin A) = spectrum ùïú A
    := AlgEquiv.spectrum_eq ((AlgEquiv.trans ((toEuclideanCLM : Matrix n n ùïú ‚âÉ‚ãÜ‚Çê[ùïú]
    EuclideanSpace ùïú n ‚ÜíL[ùïú] EuclideanSpace ùïú n) : Matrix n n ùïú ‚âÉ‚Çê[ùïú]
    EuclideanSpace ùïú n ‚ÜíL[ùïú] EuclideanSpace ùïú n))
    (AlgEquivFiniteDimNormedLinearCLM (EuclideanSpace ùïú n)).symm) _

theorem eigenvalue_mem_real : ‚àÄ (i : n), (hA.eigenvalues) i ‚àà spectrum ‚Ñù A := by
    intro i
    apply spectrum.of_algebraMap_mem (S := ùïú) (R := ‚Ñù) (A := Matrix n n ùïú)
    rw [‚Üêspec_toEuclideanLin_eq_spec]
    apply hA.eigenvalue_mem_toEuclideanLin_spectrum_RCLike i

@[simps]
noncomputable def œÜ : StarAlgHom ‚Ñù C(spectrum ‚Ñù A, ‚Ñù) (Matrix n n ùïú) where
  toFun := fun g => (eigenvectorUnitary hA : Matrix n n ùïú) *
    diagonal (RCLike.ofReal ‚àò g ‚àò (fun i ‚Ü¶ ‚ü®hA.eigenvalues i, hA.eigenvalue_mem_real i‚ü©))
    * star (eigenvectorUnitary hA : Matrix n n ùïú)
  map_one' := by simp [Pi.one_def (f := fun _ : n ‚Ü¶ ùïú)]
  map_mul' f g := by
    have {a b c d e f : Matrix n n ùïú} : (a * b * c) * (d * e * f) = a * (b * (c * d) * e) * f := by
      simp only [mul_assoc]
    simp only [this, ContinuousMap.coe_mul, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one,
      diagonal_mul_diagonal, Function.comp_apply]
    congr! with i
    simp
  map_zero' := by simp [Pi.zero_def (f := fun _ : n ‚Ü¶ ùïú)]
  map_add' f g := by
    simp only [ContinuousMap.coe_add, ‚Üê add_mul, ‚Üê mul_add, diagonal_add, Function.comp_apply]
    congr! with i
    simp
  commutes' r := by
    simp only [Function.comp, algebraMap_apply, smul_eq_mul, mul_one]
    rw [IsScalarTower.algebraMap_apply ‚Ñù ùïú _ r, RCLike.algebraMap_eq_ofReal,
      ‚Üê mul_one (algebraMap _ _ _), ‚Üê unitary.coe_mul_star_self hA.eigenvectorUnitary,
      ‚Üê Algebra.left_comm, unitary.coe_star, mul_assoc]
    congr!
  map_star' f := by
    simp only [star_trivial, StarMul.star_mul, star_star, star_eq_conjTranspose (diagonal _),
      diagonal_conjTranspose, mul_assoc]
    congr!
    ext
    simp

theorem eigenvalues_eq_spectrum {a : Matrix n n ùïú} (ha : IsHermitian a) :
    (spectrum ‚Ñù a) = Set.range (ha.eigenvalues) := by
   ext x
   conv_lhs => rw [ha.spectral_theorem, spectrum.unitary_conjugate,
   ‚Üê spectrum.algebraMap_mem_iff ùïú, spectrum_diagonal, RCLike.algebraMap_eq_ofReal]
   simp

theorem finite_spectrum {a : Matrix n n ùïú} (ha : IsHermitian a) : (spectrum ‚Ñù a).Finite := by
   have H := Set.finite_range (ha.eigenvalues)
   exact (ha.eigenvalues_eq_spectrum).symm ‚ñ∏ H

theorem compact_spectrum {a : Matrix n n ùïú} (ha : IsHermitian a) : CompactSpace (spectrum ‚Ñù a) := by
   convert Finite.compactSpace (X := spectrum ‚Ñù a)
   refine Set.finite_coe_iff.mpr ?_
   apply finite_spectrum
   assumption

instance instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus ‚Ñù (IsHermitian : Matrix n n ùïú ‚Üí Prop) where
exists_cfc_of_predicate a ha := by
    refine ‚ü®œÜ ha, ?closedEmbedding, ?mapId, ?map_spec, ?hermitian‚ü©
    case closedEmbedding =>
      have h0 : FiniteDimensional ‚Ñù C(spectrum ‚Ñù a, ‚Ñù) := by
        have : Finite (spectrum ‚Ñù a) := by refine finite_spectrum ha
        apply FiniteDimensional.of_injective (ContinuousMap.coeFnLinearMap ‚Ñù (M := ‚Ñù))
        exact DFunLike.coe_injective
      have hœÜ : LinearMap.ker ha.œÜ = ‚ä• := by
        refine LinearMap.ker_eq_bot'.mpr ?_
        intro f hf
        have h2 : diagonal
             (RCLike.ofReal ‚àò ‚áëf ‚àò fun i ‚Ü¶ ‚ü®ha.eigenvalues i, ha.eigenvalue_mem_real i‚ü©)
             = (0 : Matrix n n ùïú) := by
           rw [œÜ_apply] at hf
           have hlr : (star ha.eigenvectorUnitary : Matrix n n ùïú) *
              ((eigenvectorUnitary ha : Matrix n n ùïú) * diagonal (RCLike.ofReal ‚àò f ‚àò
                (fun i ‚Ü¶ ‚ü®ha.eigenvalues i, ha.eigenvalue_mem_real i‚ü©)) *
                star (eigenvectorUnitary ha : Matrix n n ùïú)) *
                (ha.eigenvectorUnitary : Matrix n n ùïú) =
                (star ha.eigenvectorUnitary : Matrix n n ùïú) *
                (0 : Matrix n n ùïú) * (ha.eigenvectorUnitary : Matrix n n ùïú)
                := by congr
           simp only [‚Üê mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, one_mul,
                    mul_zero, zero_mul] at hlr
           simp only [mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one] at hlr
           exact hlr
        ext x
        simp only [ContinuousMap.zero_apply]
        obtain ‚ü®x, hx‚ü© := x
        obtain ‚ü®i, rfl‚ü© := ha.eigenvalues_eq_spectrum ‚ñ∏ hx
        rw [‚Üê diagonal_zero] at h2
        have := (diagonal_eq_diagonal_iff).mp h2
        exact RCLike.ofReal_eq_zero.mp (this i)
      have H := ha.compact_spectrum
      apply LinearMap.closedEmbedding_of_injective (ùïú := ‚Ñù) (E := C(spectrum ‚Ñù a, ‚Ñù))
        (F := Matrix n n ùïú) (f := ha.œÜ) hœÜ
    case mapId =>
      conv_rhs => rw [ha.spectral_theorem]
      congr!
    case map_spec =>
      intro f
      apply Set.eq_of_subset_of_subset
      ¬∑ rw [‚Üê ContinuousMap.spectrum_eq_range f]
        apply AlgHom.spectrum_apply_subset
      ¬∑ rw [œÜ_apply ,spectrum.unitary_conjugate]
        rintro - ‚ü®x , rfl‚ü©
        apply spectrum.of_algebraMap_mem (R := ‚Ñù) (S := ùïú)
        simp only [spectrum_diagonal (R := ùïú)
            (RCLike.ofReal ‚àò f ‚àò (fun i ‚Ü¶ ‚ü®ha.eigenvalues i, ha.eigenvalue_mem_real i‚ü©))
            , Function.comp_apply, Set.mem_range]
        obtain ‚ü®x, hx‚ü© := x
        obtain ‚ü®i, rfl‚ü© := ha.eigenvalues_eq_spectrum ‚ñ∏ hx
        exact ‚ü®i, rfl‚ü©
    case hermitian =>
      intro f
      simp only [œÜ_apply, mul_assoc, IsHermitian, ‚Üê star_eq_conjTranspose, star_mul, star_star]
      congr!
      rw [star_eq_conjTranspose, diagonal_conjTranspose]
      congr!
      simp only [Pi.star_def, Function.comp_apply, RCLike.star_def, RCLike.conj_ofReal]
      rfl
end IsHermitian
end Matrix
