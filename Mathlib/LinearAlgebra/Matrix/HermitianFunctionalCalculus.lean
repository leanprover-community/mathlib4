/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.LinearAlgebra.Eigenspace.Matrix
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique
import Mathlib.Topology.ContinuousMap.Units

/-!
# Continuous Functional Calculus for Hermitian Matrices

This file defines an instance of the continuous functional calculus for Hermitian matrices over an
`RCLike` field `ğ•œ`.

## Main Results

- `Matrix.IsHermitian.cfc` : Realization of the functional calculus for a Hermitian matrix
  as the triple product `U * diagonal (RCLike.ofReal âˆ˜ f âˆ˜ hA.eigenvalues) * star U` with
  `U = eigenvectorUnitary hA`.

- `cfc_eq` : Proof that the above agrees with the continuous functional calculus.

- `Matrix.IsHermitian.instContinuousFunctionalCalculus` : Instance of the continuous functional
  calculus for a Hermitian matrix `A` over `ğ•œ`.

## Tags

spectral theorem, diagonalization theorem, continuous functional calculus
-/

open Topology

namespace Matrix

variable {n ğ•œ : Type*} [RCLike ğ•œ] [Fintype n] [DecidableEq n] {A : Matrix n n ğ•œ}

lemma finite_real_spectrum : (spectrum â„ A).Finite := by
  rw [â† spectrum.preimage_algebraMap ğ•œ]
  exact A.finite_spectrum.preimage (FaithfulSMul.algebraMap_injective â„ ğ•œ).injOn

instance : Finite (spectrum â„ A) := A.finite_real_spectrum

namespace IsHermitian

variable (hA : IsHermitian A)

/-- The `â„`-spectrum of a Hermitian matrix over `RCLike` field is the range of the eigenvalue
function -/
theorem eigenvalues_eq_spectrum_real {a : Matrix n n ğ•œ} (ha : IsHermitian a) :
    spectrum â„ a = Set.range (ha.eigenvalues) := by
  ext x
  conv_lhs => rw [ha.spectral_theorem, unitary.spectrum.unitary_conjugate,
  â† spectrum.algebraMap_mem_iff ğ•œ, spectrum_diagonal, RCLike.algebraMap_eq_ofReal]
  simp

/-- The star algebra homomorphism underlying the instance of the continuous functional
calculus of a Hermitian matrix. This is an auxiliary definition and is not intended
for use outside of this file. -/
@[simps]
noncomputable def cfcAux : C(spectrum â„ A, â„) â†’â‹†â‚[â„] (Matrix n n ğ•œ) where
  toFun := fun g => (eigenvectorUnitary hA : Matrix n n ğ•œ) *
    diagonal (RCLike.ofReal âˆ˜ g âˆ˜ (fun i â†¦ âŸ¨hA.eigenvalues i, hA.eigenvalues_mem_spectrum_real iâŸ©))
    * star (eigenvectorUnitary hA : Matrix n n ğ•œ)
  map_zero' := by simp [Pi.zero_def, Function.comp_def]
  map_one' := by simp [Pi.one_def, Function.comp_def]
  map_mul' f g := by
    have {a b c d e f : Matrix n n ğ•œ} : (a * b * c) * (d * e * f) = a * (b * (c * d) * e) * f := by
      simp only [mul_assoc]
    simp only [this, ContinuousMap.coe_mul, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one,
      diagonal_mul_diagonal, Function.comp_apply]
    congr! with i
    simp
  map_add' f g := by
    simp only [ContinuousMap.coe_add, â† add_mul, â† mul_add, diagonal_add, Function.comp_apply]
    congr! with i
    simp
  commutes' r := by
    simp only [Function.comp_def, algebraMap_apply, smul_eq_mul, mul_one]
    rw [â† mul_one (algebraMap _ _ _), â† unitary.coe_mul_star_self hA.eigenvectorUnitary,
      â† Algebra.left_comm, unitary.coe_star, mul_assoc]
    congr!
  map_star' f := by
    simp only [star_trivial, StarMul.star_mul, star_star, star_eq_conjTranspose (diagonal _),
      diagonal_conjTranspose, mul_assoc]
    congr!
    ext
    simp

lemma isClosedEmbedding_cfcAux : IsClosedEmbedding hA.cfcAux := by
  have h0 : FiniteDimensional â„ C(spectrum â„ A, â„) :=
    FiniteDimensional.of_injective (ContinuousMap.coeFnLinearMap â„ (M := â„)) DFunLike.coe_injective
  refine LinearMap.isClosedEmbedding_of_injective (ğ•œ := â„) (E := C(spectrum â„ A, â„))
    (F := Matrix n n ğ•œ) (f := hA.cfcAux) <| LinearMap.ker_eq_bot'.mpr fun f hf â†¦ ?_
  have h2 :
      diagonal (RCLike.ofReal âˆ˜ f âˆ˜ fun i â†¦ âŸ¨hA.eigenvalues i, hA.eigenvalues_mem_spectrum_real iâŸ©)
        = (0 : Matrix n n ğ•œ) := by
    simp only [LinearMap.coe_coe, cfcAux_apply] at hf
    replace hf := congr($(hf) * (eigenvectorUnitary hA : Matrix n n ğ•œ))
    simp only [mul_assoc, SetLike.coe_mem, unitary.star_mul_self_of_mem, mul_one, zero_mul] at hf
    simpa [â† mul_assoc] using congr((star hA.eigenvectorUnitary : Matrix n n ğ•œ) * $(hf))
  ext x
  simp only [ContinuousMap.zero_apply]
  obtain âŸ¨x, hxâŸ© := x
  obtain âŸ¨i, rflâŸ© := hA.eigenvalues_eq_spectrum_real â–¸ hx
  rw [â† diagonal_zero] at h2
  have := (diagonal_eq_diagonal_iff).mp h2
  refine RCLike.ofReal_eq_zero.mp (this i)

lemma cfcAux_id : hA.cfcAux (.restrict (spectrum â„ A) (.id â„)) = A := by
  conv_rhs => rw [hA.spectral_theorem]
  congr!

/-- Instance of the continuous functional calculus for a Hermitian matrix over `ğ•œ` with
`RCLike ğ•œ`. -/
instance instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„ (Matrix n n ğ•œ) IsSelfAdjoint where
  exists_cfc_of_predicate a ha := by
    replace ha : IsHermitian a := ha
    refine âŸ¨ha.cfcAux, ha.isClosedEmbedding_cfcAux, ha.cfcAux_id, fun f â†¦ ?map_spec,
      fun f â†¦ ?hermitianâŸ©
    case map_spec =>
      apply Set.eq_of_subset_of_subset
      Â· rw [â† ContinuousMap.spectrum_eq_range f]
        apply AlgHom.spectrum_apply_subset
      Â· rw [cfcAux_apply, unitary.spectrum.unitary_conjugate]
        rintro - âŸ¨x , rflâŸ©
        apply spectrum.of_algebraMap_mem ğ•œ
        simp only [Function.comp_apply, Set.mem_range, spectrum_diagonal]
        obtain âŸ¨x, hxâŸ© := x
        obtain âŸ¨i, rflâŸ© := ha.eigenvalues_eq_spectrum_real â–¸ hx
        exact âŸ¨i, rflâŸ©
    case hermitian =>
      simp only [isSelfAdjoint_iff, cfcAux_apply, mul_assoc, star_mul, star_star]
      rw [star_eq_conjTranspose, diagonal_conjTranspose]
      congr!
      simp [Pi.star_def, Function.comp_def]
  spectrum_nonempty a ha := by
    obtain (h | h) := isEmpty_or_nonempty n
    Â· obtain âŸ¨x, y, hxyâŸ© := exists_pair_ne (Matrix n n ğ•œ)
      exact False.elim <| Matrix.of.symm.injective.ne hxy <| Subsingleton.elim _ _
    Â· exact eigenvalues_eq_spectrum_real ha â–¸ Set.range_nonempty _
  predicate_zero := .zero _

/-- The continuous functional calculus of a Hermitian matrix as a triple product using the
spectral theorem. Note that this actually operates on bare functions since every function is
continuous on the spectrum of a matrix, since the spectrum is finite. This is shown to be equal to
the generic continuous functional calculus API in `Matrix.IsHermitian.cfc_eq`. In general, users
should prefer the generic API, especially because it will make rewriting easier. -/
protected noncomputable def cfc (f : â„ â†’ â„) : Matrix n n ğ•œ :=
  (eigenvectorUnitary hA : Matrix n n ğ•œ) * diagonal (RCLike.ofReal âˆ˜ f âˆ˜ hA.eigenvalues)
    * star (eigenvectorUnitary hA : Matrix n n ğ•œ)

lemma cfc_eq (f : â„ â†’ â„) : cfc f A = hA.cfc f := by
  have hA' : IsSelfAdjoint A := hA
  have := cfcHom_eq_of_continuous_of_map_id hA' hA.cfcAux hA.isClosedEmbedding_cfcAux.continuous
    hA.cfcAux_id
  rw [cfc_apply f A hA' (by rw [continuousOn_iff_continuous_restrict]; fun_prop), this]
  simp only [cfcAux_apply, ContinuousMap.coe_mk, Function.comp_def, Set.restrict_apply,
    IsHermitian.cfc]

end IsHermitian
end Matrix
