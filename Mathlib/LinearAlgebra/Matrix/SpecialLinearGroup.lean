/-
Copyright (c) 2020 Anne Baanen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anne Baanen
-/
import Mathlib.LinearAlgebra.GeneralLinearGroup
import Mathlib.LinearAlgebra.Matrix.Adjugate
import Mathlib.LinearAlgebra.Matrix.ToLin

#align_import linear_algebra.matrix.special_linear_group from "leanprover-community/mathlib"@"f06058e64b7e8397234455038f3f8aec83aaba5a"

/-!
# The Special Linear group $SL(n, R)$

This file defines the elements of the Special Linear group `SpecialLinearGroup n R`, consisting
of all square `R`-matrices with determinant `1` on the fintype `n` by `n`.  In addition, we define
the group structure on `SpecialLinearGroup n R` and the embedding into the general linear group
`GeneralLinearGroup R (n â†’ R)`.

## Main definitions

 * `Matrix.SpecialLinearGroup` is the type of matrices with determinant 1
 * `Matrix.SpecialLinearGroup.group` gives the group structure (under multiplication)
 * `Matrix.SpecialLinearGroup.toGL` is the embedding `SLâ‚™(R) â†’ GLâ‚™(R)`

## Notation

For `m : â„•`, we introduce the notation `SL(m,R)` for the special linear group on the fintype
`n = Fin m`, in the locale `MatrixGroups`.

## Implementation notes
The inverse operation in the `SpecialLinearGroup` is defined to be the adjugate
matrix, so that `SpecialLinearGroup n R` has a group structure for all `CommRing R`.

We define the elements of `SpecialLinearGroup` to be matrices, since we need to
compute their determinant. This is in contrast with `GeneralLinearGroup R M`,
which consists of invertible `R`-linear maps on `M`.

We provide `Matrix.SpecialLinearGroup.hasCoeToFun` for convenience, but do not state any
lemmas about it, and use `Matrix.SpecialLinearGroup.coeFn_eq_coe` to eliminate it `â‡‘` in favor
of a regular `â†‘` coercion.

## References

 * https://en.wikipedia.org/wiki/Special_linear_group

## Tags

matrix group, group, matrix inverse
-/


namespace Matrix

universe u v

open Matrix

open LinearMap

section

variable (n : Type u) [DecidableEq n] [Fintype n] (R : Type v) [CommRing R]

/-- `SpecialLinearGroup n R` is the group of `n` by `n` `R`-matrices with determinant equal to 1.
-/
def SpecialLinearGroup :=
  { A : Matrix n n R // A.det = 1 }
#align matrix.special_linear_group Matrix.SpecialLinearGroup

end

scoped[MatrixGroups] notation "SL(" n ", " R ")" => Matrix.SpecialLinearGroup (Fin n) R

namespace SpecialLinearGroup

variable {n : Type u} [DecidableEq n] [Fintype n] {R : Type v} [CommRing R]

instance hasCoeToMatrix : Coe (SpecialLinearGroup n R) (Matrix n n R) :=
  âŸ¨fun A => A.valâŸ©
#align matrix.special_linear_group.has_coe_to_matrix Matrix.SpecialLinearGroup.hasCoeToMatrix

/-- In this file, Lean often has a hard time working out the values of `n` and `R` for an expression
like `det â†‘A`. Rather than writing `(A : Matrix n n R)` everywhere in this file which is annoyingly
verbose, or `A.val` which is not the simp-normal form for subtypes, we create a local notation
`â†‘â‚˜A`. This notation references the local `n` and `R` variables, so is not valid as a global
notation. -/
local notation:1024 "â†‘â‚˜" A:1024 => ((A : SpecialLinearGroup n R) : Matrix n n R)

-- Porting note: moved this section upwards because it used to be not simp-normal.
-- Now it is, since coercion arrows are unfolded.
section CoeFnInstance

/-- This instance is here for convenience, but is literally the same as the coercion from
`hasCoeToMatrix`. -/
instance instCoeFun : CoeFun (SpecialLinearGroup n R) fun _ => n â†’ n â†’ R where coe A := â†‘â‚˜A

end CoeFnInstance

theorem ext_iff (A B : SpecialLinearGroup n R) : A = B â†” âˆ€ i j, â†‘â‚˜A i j = â†‘â‚˜B i j :=
  Subtype.ext_iff.trans Matrix.ext_iff.symm
#align matrix.special_linear_group.ext_iff Matrix.SpecialLinearGroup.ext_iff

@[ext]
theorem ext (A B : SpecialLinearGroup n R) : (âˆ€ i j, â†‘â‚˜A i j = â†‘â‚˜B i j) â†’ A = B :=
  (SpecialLinearGroup.ext_iff A B).mpr
#align matrix.special_linear_group.ext Matrix.SpecialLinearGroup.ext

instance hasInv : Inv (SpecialLinearGroup n R) :=
  âŸ¨fun A => âŸ¨adjugate A, by rw [det_adjugate, A.prop, one_pow]âŸ©âŸ©
                            -- ğŸ‰ no goals
#align matrix.special_linear_group.has_inv Matrix.SpecialLinearGroup.hasInv

instance hasMul : Mul (SpecialLinearGroup n R) :=
  âŸ¨fun A B => âŸ¨â†‘â‚˜A * â†‘â‚˜B, by rw [det_mul, A.prop, B.prop, one_mul]âŸ©âŸ©
                             -- ğŸ‰ no goals
#align matrix.special_linear_group.has_mul Matrix.SpecialLinearGroup.hasMul

instance hasOne : One (SpecialLinearGroup n R) :=
  âŸ¨âŸ¨1, det_oneâŸ©âŸ©
#align matrix.special_linear_group.has_one Matrix.SpecialLinearGroup.hasOne

instance : Pow (SpecialLinearGroup n R) â„• where
  pow x n := âŸ¨â†‘â‚˜x ^ n, (det_pow _ _).trans <| x.prop.symm â–¸ one_pow _âŸ©

instance : Inhabited (SpecialLinearGroup n R) :=
  âŸ¨1âŸ©

section CoeLemmas

variable (A B : SpecialLinearGroup n R)

-- Porting note: shouldn't be `@[simp]` because cast+mk gets reduced anyway
theorem coe_mk (A : Matrix n n R) (h : det A = 1) : â†‘(âŸ¨A, hâŸ© : SpecialLinearGroup n R) = A :=
  rfl
#align matrix.special_linear_group.coe_mk Matrix.SpecialLinearGroup.coe_mk

@[simp]
theorem coe_inv : â†‘â‚˜Aâ»Â¹ = adjugate A :=
  rfl
#align matrix.special_linear_group.coe_inv Matrix.SpecialLinearGroup.coe_inv

@[simp]
theorem coe_mul : â†‘â‚˜(A * B) = â†‘â‚˜A * â†‘â‚˜B :=
  rfl
#align matrix.special_linear_group.coe_mul Matrix.SpecialLinearGroup.coe_mul

@[simp]
theorem coe_one : â†‘â‚˜(1 : SpecialLinearGroup n R) = (1 : Matrix n n R) :=
  rfl
#align matrix.special_linear_group.coe_one Matrix.SpecialLinearGroup.coe_one

@[simp]
theorem det_coe : det â†‘â‚˜A = 1 :=
  A.2
#align matrix.special_linear_group.det_coe Matrix.SpecialLinearGroup.det_coe

@[simp]
theorem coe_pow (m : â„•) : â†‘â‚˜(A ^ m) = â†‘â‚˜A ^ m :=
  rfl
#align matrix.special_linear_group.coe_pow Matrix.SpecialLinearGroup.coe_pow

theorem det_ne_zero [Nontrivial R] (g : SpecialLinearGroup n R) : det â†‘â‚˜g â‰  0 := by
  rw [g.det_coe]
  -- âŠ¢ 1 â‰  0
  norm_num
  -- ğŸ‰ no goals
#align matrix.special_linear_group.det_ne_zero Matrix.SpecialLinearGroup.det_ne_zero

theorem row_ne_zero [Nontrivial R] (g : SpecialLinearGroup n R) (i : n) : â†‘â‚˜g i â‰  0 := fun h =>
  g.det_ne_zero <| det_eq_zero_of_row_eq_zero i <| by simp [h]
                                                      -- ğŸ‰ no goals
#align matrix.special_linear_group.row_ne_zero Matrix.SpecialLinearGroup.row_ne_zero

end CoeLemmas

instance monoid : Monoid (SpecialLinearGroup n R) :=
  Function.Injective.monoid (â†‘) Subtype.coe_injective coe_one coe_mul coe_pow

instance : Group (SpecialLinearGroup n R) :=
  { SpecialLinearGroup.monoid, SpecialLinearGroup.hasInv with
    mul_left_inv := fun A => by
      ext1
      -- âŠ¢ â†‘(Aâ»Â¹ * A) iâœ jâœ = â†‘1 iâœ jâœ
      simp [adjugate_mul] }
      -- ğŸ‰ no goals

/-- A version of `Matrix.toLin' A` that produces linear equivalences. -/
def toLin' : SpecialLinearGroup n R â†’* (n â†’ R) â‰ƒâ‚—[R] n â†’ R where
  toFun A :=
    LinearEquiv.ofLinear (Matrix.toLin' â†‘â‚˜A) (Matrix.toLin' â†‘â‚˜Aâ»Â¹)
      (by rw [â† toLin'_mul, â† coe_mul, mul_right_inv, coe_one, toLin'_one])
          -- ğŸ‰ no goals
      (by rw [â† toLin'_mul, â† coe_mul, mul_left_inv, coe_one, toLin'_one])
          -- ğŸ‰ no goals
  map_one' := LinearEquiv.toLinearMap_injective Matrix.toLin'_one
  map_mul' A B := LinearEquiv.toLinearMap_injective <| Matrix.toLin'_mul â†‘â‚˜A â†‘â‚˜B
#align matrix.special_linear_group.to_lin' Matrix.SpecialLinearGroup.toLin'

theorem toLin'_apply (A : SpecialLinearGroup n R) (v : n â†’ R) :
    SpecialLinearGroup.toLin' A v = Matrix.toLin' (â†‘â‚˜A) v :=
  rfl
#align matrix.special_linear_group.to_lin'_apply Matrix.SpecialLinearGroup.toLin'_apply

theorem toLin'_to_linearMap (A : SpecialLinearGroup n R) :
    â†‘(SpecialLinearGroup.toLin' A) = Matrix.toLin' â†‘â‚˜A :=
  rfl
#align matrix.special_linear_group.to_lin'_to_linear_map Matrix.SpecialLinearGroup.toLin'_to_linearMap

theorem toLin'_symm_apply (A : SpecialLinearGroup n R) (v : n â†’ R) :
    A.toLin'.symm v = Matrix.toLin' (â†‘â‚˜Aâ»Â¹) v :=
  rfl
#align matrix.special_linear_group.to_lin'_symm_apply Matrix.SpecialLinearGroup.toLin'_symm_apply

theorem toLin'_symm_to_linearMap (A : SpecialLinearGroup n R) :
    â†‘A.toLin'.symm = Matrix.toLin' â†‘â‚˜Aâ»Â¹ :=
  rfl
#align matrix.special_linear_group.to_lin'_symm_to_linear_map Matrix.SpecialLinearGroup.toLin'_symm_to_linearMap

theorem toLin'_injective :
    Function.Injective â†‘(toLin' : SpecialLinearGroup n R â†’* (n â†’ R) â‰ƒâ‚—[R] n â†’ R) := fun _ _ h =>
  Subtype.coe_injective <| Matrix.toLin'.injective <| LinearEquiv.toLinearMap_injective.eq_iff.mpr h
#align matrix.special_linear_group.to_lin'_injective Matrix.SpecialLinearGroup.toLin'_injective

/-- `toGL` is the map from the special linear group to the general linear group -/
def toGL : SpecialLinearGroup n R â†’* GeneralLinearGroup R (n â†’ R) :=
  (GeneralLinearGroup.generalLinearEquiv _ _).symm.toMonoidHom.comp toLin'
set_option linter.uppercaseLean3 false in
#align matrix.special_linear_group.to_GL Matrix.SpecialLinearGroup.toGL

-- Porting note: broken dot notation
theorem coe_toGL (A : SpecialLinearGroup n R) : SpecialLinearGroup.toGL A = A.toLin'.toLinearMap :=
  rfl
set_option linter.uppercaseLean3 false in
#align matrix.special_linear_group.coe_to_GL Matrix.SpecialLinearGroup.coe_toGL

variable {S : Type*} [CommRing S]

/-- A ring homomorphism from `R` to `S` induces a group homomorphism from
`SpecialLinearGroup n R` to `SpecialLinearGroup n S`. -/
@[simps]
def map (f : R â†’+* S) : SpecialLinearGroup n R â†’* SpecialLinearGroup n S where
  toFun g :=
    âŸ¨f.mapMatrix â†‘â‚˜g, by
      rw [â† f.map_det]
      -- âŠ¢ â†‘f (det â†‘g) = 1
      simp [g.prop]âŸ©
      -- ğŸ‰ no goals
  map_one' := Subtype.ext <| f.mapMatrix.map_one
  map_mul' x y := Subtype.ext <| f.mapMatrix.map_mul â†‘â‚˜x â†‘â‚˜y
#align matrix.special_linear_group.map Matrix.SpecialLinearGroup.map

section cast

/-- Coercion of SL `n` `â„¤` to SL `n` `R` for a commutative ring `R`. -/
instance : Coe (SpecialLinearGroup n â„¤) (SpecialLinearGroup n R) :=
  âŸ¨fun x => map (Int.castRingHom R) xâŸ©

@[simp]
theorem coe_matrix_coe (g : SpecialLinearGroup n â„¤) :
    â†‘(g : SpecialLinearGroup n R) = (â†‘g : Matrix n n â„¤).map (Int.castRingHom R) :=
  map_apply_coe (Int.castRingHom R) g
#align matrix.special_linear_group.coe_matrix_coe Matrix.SpecialLinearGroup.coe_matrix_coe

end cast

section Neg

variable [Fact (Even (Fintype.card n))]

/-- Formal operation of negation on special linear group on even cardinality `n` given by negating
each element. -/
instance : Neg (SpecialLinearGroup n R) :=
  âŸ¨fun g => âŸ¨-g, by
    simpa [(@Fact.out <| Even <| Fintype.card n).neg_one_pow, g.det_coe] using det_smul (â†‘â‚˜g) (-1)âŸ©âŸ©
    -- ğŸ‰ no goals

@[simp]
theorem coe_neg (g : SpecialLinearGroup n R) : â†‘(-g) = -(g : Matrix n n R) :=
  rfl
#align matrix.special_linear_group.coe_neg Matrix.SpecialLinearGroup.coe_neg

instance : HasDistribNeg (SpecialLinearGroup n R) :=
  Function.Injective.hasDistribNeg _ Subtype.coe_injective coe_neg coe_mul

@[simp]
theorem coe_int_neg (g : SpecialLinearGroup n â„¤) : â†‘(-g) = (-â†‘g : SpecialLinearGroup n R) :=
  Subtype.ext <| (@RingHom.mapMatrix n _ _ _ _ _ _ (Int.castRingHom R)).map_neg â†‘g
#align matrix.special_linear_group.coe_int_neg Matrix.SpecialLinearGroup.coe_int_neg

end Neg

section SpecialCases

open scoped MatrixGroups

theorem SL2_inv_expl_det (A : SL(2, R)) :
    det ![![A.1 1 1, -A.1 0 1], ![-A.1 1 0, A.1 0 0]] = 1 := by
  rw [Matrix.det_fin_two, mul_comm]
  -- âŠ¢ vecCons ![â†‘A 1 1, -â†‘A 0 1] ![![-â†‘A 1 0, â†‘A 0 0]] 1 1 * vecCons ![â†‘A 1 1, -â†‘A â€¦
  simp only [cons_val_zero, cons_val_one, head_cons, mul_neg, neg_mul, neg_neg]
  -- âŠ¢ â†‘A 0 0 * â†‘A 1 1 - â†‘A 0 1 * â†‘A 1 0 = 1
  have := A.2
  -- âŠ¢ â†‘A 0 0 * â†‘A 1 1 - â†‘A 0 1 * â†‘A 1 0 = 1
  rw [Matrix.det_fin_two] at this
  -- âŠ¢ â†‘A 0 0 * â†‘A 1 1 - â†‘A 0 1 * â†‘A 1 0 = 1
  convert this
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.special_linear_group.SL2_inv_expl_det Matrix.SpecialLinearGroup.SL2_inv_expl_det

theorem SL2_inv_expl (A : SL(2, R)) :
    Aâ»Â¹ = âŸ¨![![A.1 1 1, -A.1 0 1], ![-A.1 1 0, A.1 0 0]], SL2_inv_expl_det AâŸ© := by
  ext
  -- âŠ¢ â†‘Aâ»Â¹ iâœ jâœ = â†‘{ val := ![![â†‘A 1 1, -â†‘A 0 1], ![-â†‘A 1 0, â†‘A 0 0]], property : â€¦
  have := Matrix.adjugate_fin_two A.1
  -- âŠ¢ â†‘Aâ»Â¹ iâœ jâœ = â†‘{ val := ![![â†‘A 1 1, -â†‘A 0 1], ![-â†‘A 1 0, â†‘A 0 0]], property : â€¦
  rw [coe_inv, this]
  -- âŠ¢ â†‘of ![![â†‘A 1 1, -â†‘A 0 1], ![-â†‘A 1 0, â†‘A 0 0]] iâœ jâœ = â†‘{ val := ![![â†‘A 1 1,  â€¦
  rfl
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.special_linear_group.SL2_inv_expl Matrix.SpecialLinearGroup.SL2_inv_expl

theorem fin_two_induction (P : SL(2, R) â†’ Prop)
    (h : âˆ€ (a b c d : R) (hdet : a * d - b * c = 1), P âŸ¨!![a, b; c, d], by rwa [det_fin_two_of]âŸ©)
                                                                           -- ğŸ‰ no goals
    (g : SL(2, R)) : P g := by
  obtain âŸ¨m, hmâŸ© := g
  -- âŠ¢ P { val := m, property := hm }
  convert h (m 0 0) (m 0 1) (m 1 0) (m 1 1) (by rwa [det_fin_two] at hm)
  -- âŠ¢ m = â†‘of ![![m 0 0, m 0 1], ![m 1 0, m 1 1]]
  ext i j; fin_cases i <;> fin_cases j <;> rfl
  -- âŠ¢ m i j = â†‘of ![![m 0 0, m 0 1], ![m 1 0, m 1 1]] i j
           -- âŠ¢ m { val := 0, isLt := (_ : 0 < 2) } j = â†‘of ![![m 0 0, m 0 1], ![m 1 0, m 1  â€¦
                           -- âŠ¢ m { val := 0, isLt := (_ : 0 < 2) } { val := 0, isLt := (_ : 0 < 2) } = â†‘of  â€¦
                           -- âŠ¢ m { val := 1, isLt := (_ : (fun a => a < 2) 1) } { val := 0, isLt := (_ : 0  â€¦
                                           -- ğŸ‰ no goals
                                           -- ğŸ‰ no goals
                                           -- ğŸ‰ no goals
                                           -- ğŸ‰ no goals
#align matrix.special_linear_group.fin_two_induction Matrix.SpecialLinearGroup.fin_two_induction

theorem fin_two_exists_eq_mk_of_apply_zero_one_eq_zero {R : Type*} [Field R] (g : SL(2, R))
    (hg : (g : Matrix (Fin 2) (Fin 2) R) 1 0 = 0) :
    âˆƒ (a b : R) (h : a â‰  0), g = (âŸ¨!![a, b; 0, aâ»Â¹], by simp [h]âŸ© : SL(2, R)) := by
                                                        -- ğŸ‰ no goals
  induction' g using Matrix.SpecialLinearGroup.fin_two_induction with a b c d h_det
  -- âŠ¢ âˆƒ a_1 b_1 h, { val := â†‘of ![![a, b], ![c, d]], property := (_ : det (â†‘of ![! â€¦
  replace hg : c = 0 := by simpa using hg
  -- âŠ¢ âˆƒ a_1 b_1 h, { val := â†‘of ![![a, b], ![c, d]], property := (_ : det (â†‘of ![! â€¦
  have had : a * d = 1 := by rwa [hg, mul_zero, sub_zero] at h_det
  -- âŠ¢ âˆƒ a_1 b_1 h, { val := â†‘of ![![a, b], ![c, d]], property := (_ : det (â†‘of ![! â€¦
  refine' âŸ¨a, b, left_ne_zero_of_mul_eq_one had, _âŸ©
  -- âŠ¢ { val := â†‘of ![![a, b], ![c, d]], property := (_ : det (â†‘of ![![a, b], ![c,  â€¦
  simp_rw [eq_inv_of_mul_eq_one_right had, hg]
  -- ğŸ‰ no goals
#align matrix.special_linear_group.fin_two_exists_eq_mk_of_apply_zero_one_eq_zero Matrix.SpecialLinearGroup.fin_two_exists_eq_mk_of_apply_zero_one_eq_zero

end SpecialCases

end SpecialLinearGroup

end Matrix

namespace ModularGroup

open MatrixGroups

open Matrix Matrix.SpecialLinearGroup

local notation:1024 "â†‘â‚˜" A:1024 => ((A : SL(2, â„¤)) : Matrix (Fin 2) (Fin 2) â„¤)

set_option linter.uppercaseLean3 false

/-- The matrix `S = [[0, -1], [1, 0]]` as an element of `SL(2, â„¤)`.

This element acts naturally on the Euclidean plane as a rotation about the origin by `Ï€ / 2`.

This element also acts naturally on the hyperbolic plane as rotation about `i` by `Ï€`. It
represents the MobiÃ¼s transformation `z â†¦ -1/z` and is an involutive elliptic isometry. -/
def S : SL(2, â„¤) :=
  âŸ¨!![0, -1; 1, 0], by norm_num [Matrix.det_fin_two_of] âŸ©
                       -- ğŸ‰ no goals
#align modular_group.S ModularGroup.S

/-- The matrix `T = [[1, 1], [0, 1]]` as an element of `SL(2, â„¤)` -/
def T : SL(2, â„¤) :=
  âŸ¨!![1, 1; 0, 1], by norm_num [Matrix.det_fin_two_of] âŸ©
                      -- ğŸ‰ no goals
#align modular_group.T ModularGroup.T

theorem coe_S : â†‘â‚˜S = !![0, -1; 1, 0] :=
  rfl
#align modular_group.coe_S ModularGroup.coe_S

theorem coe_T : â†‘â‚˜T = !![1, 1; 0, 1] :=
  rfl
#align modular_group.coe_T ModularGroup.coe_T

theorem coe_T_inv : â†‘â‚˜Tâ»Â¹ = !![1, -1; 0, 1] := by simp [coe_inv, coe_T, adjugate_fin_two]
                                                  -- ğŸ‰ no goals
#align modular_group.coe_T_inv ModularGroup.coe_T_inv

theorem coe_T_zpow (n : â„¤) : â†‘â‚˜(T ^ n) = !![1, n; 0, 1] := by
  induction' n using Int.induction_on with n h n h
  Â· rw [zpow_zero, coe_one, Matrix.one_fin_two]
    -- ğŸ‰ no goals
  Â· simp_rw [zpow_add, zpow_one, coe_mul, h, coe_T, Matrix.mul_fin_two]
    -- âŠ¢ â†‘of ![![1 * 1 + â†‘n * 0, 1 * 1 + â†‘n * 1], ![0 * 1 + 1 * 0, 0 * 1 + 1 * 1]] =  â€¦
    congrm !![_, ?_; _, _]
    -- âŠ¢ 1 * 1 + â†‘n * 1 = â†‘n + 1
    rw [mul_one, mul_one, add_comm]
    -- ğŸ‰ no goals
  Â· simp_rw [zpow_sub, zpow_one, coe_mul, h, coe_T_inv, Matrix.mul_fin_two]
    -- âŠ¢ â†‘of ![![1 * 1 + -â†‘n * 0, 1 * -1 + -â†‘n * 1], ![0 * 1 + 1 * 0, 0 * -1 + 1 * 1] â€¦
    congrm !![?_, ?_; _, _] <;> ring
    -- âŠ¢ 1 * 1 + -â†‘n * 0 = 1
                                -- ğŸ‰ no goals
                                -- ğŸ‰ no goals
#align modular_group.coe_T_zpow ModularGroup.coe_T_zpow

@[simp]
theorem T_pow_mul_apply_one (n : â„¤) (g : SL(2, â„¤)) : â†‘â‚˜(T ^ n * g) 1 = â†‘â‚˜g 1 := by
  ext j
  -- âŠ¢ â†‘(T ^ n * g) 1 j = â†‘g 1 j
  simp [coe_T_zpow, Matrix.vecMul, Matrix.dotProduct, Fin.sum_univ_succ, vecTail]
  -- ğŸ‰ no goals
#align modular_group.T_pow_mul_apply_one ModularGroup.T_pow_mul_apply_one

@[simp]
theorem T_mul_apply_one (g : SL(2, â„¤)) : â†‘â‚˜(T * g) 1 = â†‘â‚˜g 1 := by
  simpa using T_pow_mul_apply_one 1 g
  -- ğŸ‰ no goals
#align modular_group.T_mul_apply_one ModularGroup.T_mul_apply_one

@[simp]
theorem T_inv_mul_apply_one (g : SL(2, â„¤)) : â†‘â‚˜(Tâ»Â¹ * g) 1 = â†‘â‚˜g 1 := by
  simpa using T_pow_mul_apply_one (-1) g
  -- ğŸ‰ no goals
#align modular_group.T_inv_mul_apply_one ModularGroup.T_inv_mul_apply_one

end ModularGroup
