/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Eric Wieser, Jeremy Avigad, Johan Commelin
-/
import Mathlib.Data.Matrix.Invertible
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse
import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # 2Ã—2 block matrices and the Schur complement

This file proves properties of 2Ã—2 block matrices `[A B; C D]` that relate to the Schur complement
`D - C*Aâ»Â¹*B`.

Some of the results here generalize to 2Ã—2 matrices in a category, rather than just a ring. A few
results in this direction can be found in `Mathlib/CategoryTheory/Preadditive/Biproducts.lean`,
especially the declarations `CategoryTheory.Biprod.gaussian` and `CategoryTheory.Biprod.isoElim`.
Compare with `Matrix.invertibleOfFromBlocksâ‚â‚Invertible`.

## Main results

* `Matrix.det_fromBlocksâ‚â‚`, `Matrix.det_fromBlocksâ‚‚â‚‚`: determinant of a block matrix in terms of
  the Schur complement.
* `Matrix.invOf_fromBlocks_zeroâ‚‚â‚_eq`, `Matrix.invOf_fromBlocks_zeroâ‚â‚‚_eq`: the inverse of a
  block triangular matrix.
* `Matrix.isUnit_fromBlocks_zeroâ‚‚â‚`, `Matrix.isUnit_fromBlocks_zeroâ‚â‚‚`: invertibility of a
  block triangular matrix.
* `Matrix.det_one_add_mul_comm`: the **Weinsteinâ€“Aronszajn identity**.
* `Matrix.PosSemidef.fromBlocksâ‚â‚` and `Matrix.PosSemidef.fromBlocksâ‚‚â‚‚`: If a matrix `A` is
  positive definite, then `[A B; Bá´´ D]` is positive semidefinite if and only if `D - Bá´´ Aâ»Â¹ B` is
  positive semidefinite.

-/


variable {l m n Î± : Type*}

namespace Matrix

open scoped Matrix

section CommRing

variable [Fintype l] [Fintype m] [Fintype n]
variable [DecidableEq l] [DecidableEq m] [DecidableEq n]
variable [CommRing Î±]

/-- LDU decomposition of a block matrix with an invertible top-left corner, using the
Schur complement. -/
theorem fromBlocks_eq_of_invertibleâ‚â‚ (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix l m Î±)
    (D : Matrix l n Î±) [Invertible A] :
    fromBlocks A B C D =
      fromBlocks 1 0 (C * â…ŸA) 1 * fromBlocks A 0 0 (D - C * â…ŸA * B) *
        fromBlocks 1 (â…ŸA * B) 0 1 := by
  simp only [fromBlocks_multiply, Matrix.mul_zero, Matrix.zero_mul, add_zero, zero_add,
    Matrix.one_mul, Matrix.mul_one, invOf_mul_self, Matrix.mul_invOf_cancel_left,
    Matrix.mul_assoc, add_sub_cancel]

/-- LDU decomposition of a block matrix with an invertible bottom-right corner, using the
Schur complement. -/
theorem fromBlocks_eq_of_invertibleâ‚‚â‚‚ (A : Matrix l m Î±) (B : Matrix l n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] :
    fromBlocks A B C D =
      fromBlocks 1 (B * â…ŸD) 0 1 * fromBlocks (A - B * â…ŸD * C) 0 0 D *
        fromBlocks 1 0 (â…ŸD * C) 1 :=
  (Matrix.reindex (Equiv.sumComm _ _) (Equiv.sumComm _ _)).injective <| by
    simpa [reindex_apply, Equiv.sumComm_symm, â† submatrix_mul_equiv _ _ _ (Equiv.sumComm n m), â†
      submatrix_mul_equiv _ _ _ (Equiv.sumComm n l), Equiv.sumComm_apply,
      fromBlocks_submatrix_sum_swap_sum_swap] using fromBlocks_eq_of_invertibleâ‚â‚ D C B A

section Triangular

/-! #### Block triangular matrices -/


/-- An upper-block-triangular matrix is invertible if its diagonal is. -/
def fromBlocksZeroâ‚‚â‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (D : Matrix n n Î±)
    [Invertible A] [Invertible D] : Invertible (fromBlocks A B 0 D) :=
  invertibleOfLeftInverse _ (fromBlocks (â…ŸA) (-(â…ŸA * B * â…ŸD)) 0 (â…ŸD)) <| by
    simp_rw [fromBlocks_multiply, Matrix.mul_zero, Matrix.zero_mul, zero_add, add_zero,
      Matrix.neg_mul, invOf_mul_self, Matrix.invOf_mul_cancel_right, add_neg_cancel,
      fromBlocks_one]

/-- A lower-block-triangular matrix is invertible if its diagonal is. -/
def fromBlocksZeroâ‚â‚‚Invertible (A : Matrix m m Î±) (C : Matrix n m Î±) (D : Matrix n n Î±)
    [Invertible A] [Invertible D] : Invertible (fromBlocks A 0 C D) :=
  invertibleOfLeftInverse _
      (fromBlocks (â…ŸA) 0 (-(â…ŸD * C * â…ŸA))
        (â…ŸD)) <| by -- a symmetry argument is more work than just copying the proof
    simp_rw [fromBlocks_multiply, Matrix.mul_zero, Matrix.zero_mul, zero_add, add_zero,
      Matrix.neg_mul, invOf_mul_self, Matrix.invOf_mul_cancel_right, neg_add_cancel,
      fromBlocks_one]

theorem invOf_fromBlocks_zeroâ‚‚â‚_eq (A : Matrix m m Î±) (B : Matrix m n Î±) (D : Matrix n n Î±)
    [Invertible A] [Invertible D] [Invertible (fromBlocks A B 0 D)] :
    â…Ÿ(fromBlocks A B 0 D) = fromBlocks (â…ŸA) (-(â…ŸA * B * â…ŸD)) 0 (â…ŸD) := by
  letI := fromBlocksZeroâ‚‚â‚Invertible A B D
  convert (rfl : â…Ÿ(fromBlocks A B 0 D) = _)

theorem invOf_fromBlocks_zeroâ‚â‚‚_eq (A : Matrix m m Î±) (C : Matrix n m Î±) (D : Matrix n n Î±)
    [Invertible A] [Invertible D] [Invertible (fromBlocks A 0 C D)] :
    â…Ÿ(fromBlocks A 0 C D) = fromBlocks (â…ŸA) 0 (-(â…ŸD * C * â…ŸA)) (â…ŸD) := by
  letI := fromBlocksZeroâ‚â‚‚Invertible A C D
  convert (rfl : â…Ÿ(fromBlocks A 0 C D) = _)

/-- Both diagonal entries of an invertible upper-block-triangular matrix are invertible (by reading
off the diagonal entries of the inverse). -/
def invertibleOfFromBlocksZeroâ‚‚â‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (D : Matrix n n Î±)
    [Invertible (fromBlocks A B 0 D)] : Invertible A Ã— Invertible D where
  fst :=
    invertibleOfLeftInverse _ (â…Ÿ(fromBlocks A B 0 D)).toBlocksâ‚â‚ <| by
      have := invOf_mul_self (fromBlocks A B 0 D)
      rw [â† fromBlocks_toBlocks (â…Ÿ(fromBlocks A B 0 D)), fromBlocks_multiply] at this
      replace := congr_arg Matrix.toBlocksâ‚â‚ this
      simpa only [Matrix.toBlocks_fromBlocksâ‚â‚, Matrix.mul_zero, add_zero, â† fromBlocks_one] using
        this
  snd :=
    invertibleOfRightInverse _ (â…Ÿ(fromBlocks A B 0 D)).toBlocksâ‚‚â‚‚ <| by
      have := mul_invOf_self (fromBlocks A B 0 D)
      rw [â† fromBlocks_toBlocks (â…Ÿ(fromBlocks A B 0 D)), fromBlocks_multiply] at this
      replace := congr_arg Matrix.toBlocksâ‚‚â‚‚ this
      simpa only [Matrix.toBlocks_fromBlocksâ‚‚â‚‚, Matrix.zero_mul, zero_add, â† fromBlocks_one] using
        this

/-- Both diagonal entries of an invertible lower-block-triangular matrix are invertible (by reading
off the diagonal entries of the inverse). -/
def invertibleOfFromBlocksZeroâ‚â‚‚Invertible (A : Matrix m m Î±) (C : Matrix n m Î±) (D : Matrix n n Î±)
    [Invertible (fromBlocks A 0 C D)] : Invertible A Ã— Invertible D where
  fst :=
    invertibleOfRightInverse _ (â…Ÿ(fromBlocks A 0 C D)).toBlocksâ‚â‚ <| by
      have := mul_invOf_self (fromBlocks A 0 C D)
      rw [â† fromBlocks_toBlocks (â…Ÿ(fromBlocks A 0 C D)), fromBlocks_multiply] at this
      replace := congr_arg Matrix.toBlocksâ‚â‚ this
      simpa only [Matrix.toBlocks_fromBlocksâ‚â‚, Matrix.zero_mul, add_zero, â† fromBlocks_one] using
        this
  snd :=
    invertibleOfLeftInverse _ (â…Ÿ(fromBlocks A 0 C D)).toBlocksâ‚‚â‚‚ <| by
      have := invOf_mul_self (fromBlocks A 0 C D)
      rw [â† fromBlocks_toBlocks (â…Ÿ(fromBlocks A 0 C D)), fromBlocks_multiply] at this
      replace := congr_arg Matrix.toBlocksâ‚‚â‚‚ this
      simpa only [Matrix.toBlocks_fromBlocksâ‚‚â‚‚, Matrix.mul_zero, zero_add, â† fromBlocks_one] using
        this

/-- `invertibleOfFromBlocksZeroâ‚‚â‚Invertible` and `Matrix.fromBlocksZeroâ‚‚â‚Invertible` form
an equivalence. -/
def fromBlocksZeroâ‚‚â‚InvertibleEquiv (A : Matrix m m Î±) (B : Matrix m n Î±) (D : Matrix n n Î±) :
    Invertible (fromBlocks A B 0 D) â‰ƒ Invertible A Ã— Invertible D where
  toFun _ := invertibleOfFromBlocksZeroâ‚‚â‚Invertible A B D
  invFun i := by
    letI := i.1
    letI := i.2
    exact fromBlocksZeroâ‚‚â‚Invertible A B D
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

/-- `invertibleOfFromBlocksZeroâ‚â‚‚Invertible` and `Matrix.fromBlocksZeroâ‚â‚‚Invertible` form
an equivalence. -/
def fromBlocksZeroâ‚â‚‚InvertibleEquiv (A : Matrix m m Î±) (C : Matrix n m Î±) (D : Matrix n n Î±) :
    Invertible (fromBlocks A 0 C D) â‰ƒ Invertible A Ã— Invertible D where
  toFun _ := invertibleOfFromBlocksZeroâ‚â‚‚Invertible A C D
  invFun i := by
    letI := i.1
    letI := i.2
    exact fromBlocksZeroâ‚â‚‚Invertible A C D
  left_inv _ := Subsingleton.elim _ _
  right_inv _ := Subsingleton.elim _ _

/-- An upper block-triangular matrix is invertible iff both elements of its diagonal are.

This is a propositional form of `Matrix.fromBlocksZeroâ‚‚â‚InvertibleEquiv`. -/
@[simp]
theorem isUnit_fromBlocks_zeroâ‚‚â‚ {A : Matrix m m Î±} {B : Matrix m n Î±} {D : Matrix n n Î±} :
    IsUnit (fromBlocks A B 0 D) â†” IsUnit A âˆ§ IsUnit D := by
  simp only [â† nonempty_invertible_iff_isUnit, â† nonempty_prod,
    (fromBlocksZeroâ‚‚â‚InvertibleEquiv _ _ _).nonempty_congr]

/-- A lower block-triangular matrix is invertible iff both elements of its diagonal are.

This is a propositional form of `Matrix.fromBlocksZeroâ‚â‚‚InvertibleEquiv` forms an `iff`. -/
@[simp]
theorem isUnit_fromBlocks_zeroâ‚â‚‚ {A : Matrix m m Î±} {C : Matrix n m Î±} {D : Matrix n n Î±} :
    IsUnit (fromBlocks A 0 C D) â†” IsUnit A âˆ§ IsUnit D := by
  simp only [â† nonempty_invertible_iff_isUnit, â† nonempty_prod,
    (fromBlocksZeroâ‚â‚‚InvertibleEquiv _ _ _).nonempty_congr]

/-- An expression for the inverse of an upper block-triangular matrix, when either both elements of
diagonal are invertible, or both are not. -/
theorem inv_fromBlocks_zeroâ‚‚â‚_of_isUnit_iff (A : Matrix m m Î±) (B : Matrix m n Î±) (D : Matrix n n Î±)
    (hAD : IsUnit A â†” IsUnit D) :
    (fromBlocks A B 0 D)â»Â¹ = fromBlocks Aâ»Â¹ (-(Aâ»Â¹ * B * Dâ»Â¹)) 0 Dâ»Â¹ := by
  by_cases hA : IsUnit A
  Â· have hD := hAD.mp hA
    cases hA.nonempty_invertible
    cases hD.nonempty_invertible
    letI := fromBlocksZeroâ‚‚â‚Invertible A B D
    simp_rw [â† invOf_eq_nonsing_inv, invOf_fromBlocks_zeroâ‚‚â‚_eq]
  Â· have hD := hAD.not.mp hA
    have : Â¬IsUnit (fromBlocks A B 0 D) :=
      isUnit_fromBlocks_zeroâ‚‚â‚.not.mpr (not_and'.mpr fun _ => hA)
    simp_rw [nonsing_inv_eq_ringInverse, Ring.inverse_non_unit _ hA, Ring.inverse_non_unit _ hD,
      Ring.inverse_non_unit _ this, Matrix.zero_mul, neg_zero, fromBlocks_zero]

/-- An expression for the inverse of a lower block-triangular matrix, when either both elements of
diagonal are invertible, or both are not. -/
theorem inv_fromBlocks_zeroâ‚â‚‚_of_isUnit_iff (A : Matrix m m Î±) (C : Matrix n m Î±) (D : Matrix n n Î±)
    (hAD : IsUnit A â†” IsUnit D) :
    (fromBlocks A 0 C D)â»Â¹ = fromBlocks Aâ»Â¹ 0 (-(Dâ»Â¹ * C * Aâ»Â¹)) Dâ»Â¹ := by
  by_cases hA : IsUnit A
  Â· have hD := hAD.mp hA
    cases hA.nonempty_invertible
    cases hD.nonempty_invertible
    letI := fromBlocksZeroâ‚â‚‚Invertible A C D
    simp_rw [â† invOf_eq_nonsing_inv, invOf_fromBlocks_zeroâ‚â‚‚_eq]
  Â· have hD := hAD.not.mp hA
    have : Â¬IsUnit (fromBlocks A 0 C D) :=
      isUnit_fromBlocks_zeroâ‚â‚‚.not.mpr (not_and'.mpr fun _ => hA)
    simp_rw [nonsing_inv_eq_ringInverse, Ring.inverse_non_unit _ hA, Ring.inverse_non_unit _ hD,
      Ring.inverse_non_unit _ this, Matrix.zero_mul, neg_zero, fromBlocks_zero]

end Triangular

/-! ### 2Ã—2 block matrices -/


section Block

/-! #### General 2Ã—2 block matrices -/


/-- A block matrix is invertible if the bottom right corner and the corresponding schur complement
is. -/
def fromBlocksâ‚‚â‚‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] [Invertible (A - B * â…ŸD * C)] :
    Invertible (fromBlocks A B C D) := by
  -- factor `fromBlocks` via `fromBlocks_eq_of_invertibleâ‚‚â‚‚`, and state the inverse we expect
  convert Invertible.copy' _ _ (fromBlocks (â…Ÿ(A - B * â…ŸD * C)) (-(â…Ÿ(A - B * â…ŸD * C) * B * â…ŸD))
    (-(â…ŸD * C * â…Ÿ(A - B * â…ŸD * C))) (â…ŸD + â…ŸD * C * â…Ÿ(A - B * â…ŸD * C) * B * â…ŸD))
      (fromBlocks_eq_of_invertibleâ‚‚â‚‚ _ _ _ _) _
  Â· -- the product is invertible because all the factors are
    letI : Invertible (1 : Matrix n n Î±) := invertibleOne
    letI : Invertible (1 : Matrix m m Î±) := invertibleOne
    refine Invertible.mul ?_ (fromBlocksZeroâ‚â‚‚Invertible _ _ _)
    exact
      Invertible.mul (fromBlocksZeroâ‚‚â‚Invertible _ _ _)
        (fromBlocksZeroâ‚‚â‚Invertible _ _ _)
  Â· -- unfold the `Invertible` instances to get the raw factors
    change
      _ =
        fromBlocks 1 0 (-(1 * (â…ŸD * C) * 1)) 1 *
          (fromBlocks (â…Ÿ(A - B * â…ŸD * C)) (-(â…Ÿ(A - B * â…ŸD * C) * 0 * â…ŸD)) 0 (â…ŸD) *
            fromBlocks 1 (-(1 * (B * â…ŸD) * 1)) 0 1)
    -- combine into a single block matrix
    simp only [fromBlocks_multiply, Matrix.one_mul, Matrix.mul_one, Matrix.zero_mul,
      Matrix.mul_zero, add_zero, zero_add, neg_zero, Matrix.mul_neg, Matrix.neg_mul, neg_neg, â†
      Matrix.mul_assoc, add_comm (â…ŸD)]

/-- A block matrix is invertible if the top left corner and the corresponding schur complement
is. -/
def fromBlocksâ‚â‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible A] [Invertible (D - C * â…ŸA * B)] :
    Invertible (fromBlocks A B C D) := by
  -- we argue by symmetry
  letI := fromBlocksâ‚‚â‚‚Invertible D C B A
  letI iDCBA :=
    submatrixEquivInvertible (fromBlocks D C B A) (Equiv.sumComm _ _) (Equiv.sumComm _ _)
  exact
    iDCBA.copy' _
      (fromBlocks (â…ŸA + â…ŸA * B * â…Ÿ(D - C * â…ŸA * B) * C * â…ŸA) (-(â…ŸA * B * â…Ÿ(D - C * â…ŸA * B)))
        (-(â…Ÿ(D - C * â…ŸA * B) * C * â…ŸA)) (â…Ÿ(D - C * â…ŸA * B)))
      (fromBlocks_submatrix_sum_swap_sum_swap _ _ _ _).symm
      (fromBlocks_submatrix_sum_swap_sum_swap _ _ _ _).symm

theorem invOf_fromBlocksâ‚‚â‚‚_eq (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] [Invertible (A - B * â…ŸD * C)]
    [Invertible (fromBlocks A B C D)] :
    â…Ÿ(fromBlocks A B C D) =
      fromBlocks (â…Ÿ(A - B * â…ŸD * C)) (-(â…Ÿ(A - B * â…ŸD * C) * B * â…ŸD))
        (-(â…ŸD * C * â…Ÿ(A - B * â…ŸD * C))) (â…ŸD + â…ŸD * C * â…Ÿ(A - B * â…ŸD * C) * B * â…ŸD) := by
  letI := fromBlocksâ‚‚â‚‚Invertible A B C D
  convert (rfl : â…Ÿ(fromBlocks A B C D) = _)

theorem invOf_fromBlocksâ‚â‚_eq (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible A] [Invertible (D - C * â…ŸA * B)]
    [Invertible (fromBlocks A B C D)] :
    â…Ÿ(fromBlocks A B C D) =
      fromBlocks (â…ŸA + â…ŸA * B * â…Ÿ(D - C * â…ŸA * B) * C * â…ŸA) (-(â…ŸA * B * â…Ÿ(D - C * â…ŸA * B)))
        (-(â…Ÿ(D - C * â…ŸA * B) * C * â…ŸA)) (â…Ÿ(D - C * â…ŸA * B)) := by
  letI := fromBlocksâ‚â‚Invertible A B C D
  convert (rfl : â…Ÿ(fromBlocks A B C D) = _)

/-- If a block matrix is invertible and so is its bottom left element, then so is the corresponding
Schur complement. -/
def invertibleOfFromBlocksâ‚‚â‚‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] [Invertible (fromBlocks A B C D)] :
    Invertible (A - B * â…ŸD * C) := by
  suffices Invertible (fromBlocks (A - B * â…ŸD * C) 0 0 D) by
    exact (invertibleOfFromBlocksZeroâ‚â‚‚Invertible (A - B * â…ŸD * C) 0 D).1
  letI : Invertible (1 : Matrix n n Î±) := invertibleOne
  letI : Invertible (1 : Matrix m m Î±) := invertibleOne
  letI iDC : Invertible (fromBlocks 1 0 (â…ŸD * C) 1 : Matrix (m âŠ• n) (m âŠ• n) Î±) :=
    fromBlocksZeroâ‚â‚‚Invertible _ _ _
  letI iBD : Invertible (fromBlocks 1 (B * â…ŸD) 0 1 : Matrix (m âŠ• n) (m âŠ• n) Î±) :=
    fromBlocksZeroâ‚‚â‚Invertible _ _ _
  letI iBDC := Invertible.copy â€¹_â€º _ (fromBlocks_eq_of_invertibleâ‚‚â‚‚ A B C D).symm
  refine (iBD.mulLeft _).symm ?_
  exact (iDC.mulRight _).symm iBDC

/-- If a block matrix is invertible and so is its bottom left element, then so is the corresponding
Schur complement. -/
def invertibleOfFromBlocksâ‚â‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible A] [Invertible (fromBlocks A B C D)] :
    Invertible (D - C * â…ŸA * B) := by
  -- another symmetry argument
  letI iABCD' :=
    submatrixEquivInvertible (fromBlocks A B C D) (Equiv.sumComm _ _) (Equiv.sumComm _ _)
  letI iDCBA := iABCD'.copy _ (fromBlocks_submatrix_sum_swap_sum_swap _ _ _ _).symm
  exact invertibleOfFromBlocksâ‚‚â‚‚Invertible D C B A

/-- `Matrix.invertibleOfFromBlocksâ‚‚â‚‚Invertible` and `Matrix.fromBlocksâ‚‚â‚‚Invertible` as an
equivalence. -/
def invertibleEquivFromBlocksâ‚‚â‚‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] :
    Invertible (fromBlocks A B C D) â‰ƒ Invertible (A - B * â…ŸD * C) where
  toFun _iABCD := invertibleOfFromBlocksâ‚‚â‚‚Invertible _ _ _ _
  invFun _i_schur := fromBlocksâ‚‚â‚‚Invertible _ _ _ _
  left_inv _iABCD := Subsingleton.elim _ _
  right_inv _i_schur := Subsingleton.elim _ _

/-- `Matrix.invertibleOfFromBlocksâ‚â‚Invertible` and `Matrix.fromBlocksâ‚â‚Invertible` as an
equivalence. -/
def invertibleEquivFromBlocksâ‚â‚Invertible (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible A] :
    Invertible (fromBlocks A B C D) â‰ƒ Invertible (D - C * â…ŸA * B) where
  toFun _iABCD := invertibleOfFromBlocksâ‚â‚Invertible _ _ _ _
  invFun _i_schur := fromBlocksâ‚â‚Invertible _ _ _ _
  left_inv _iABCD := Subsingleton.elim _ _
  right_inv _i_schur := Subsingleton.elim _ _

/-- If the bottom-left element of a block matrix is invertible, then the whole matrix is invertible
iff the corresponding schur complement is. -/
theorem isUnit_fromBlocks_iff_of_invertibleâ‚‚â‚‚ {A : Matrix m m Î±} {B : Matrix m n Î±}
    {C : Matrix n m Î±} {D : Matrix n n Î±} [Invertible D] :
    IsUnit (fromBlocks A B C D) â†” IsUnit (A - B * â…ŸD * C) := by
  simp only [â† nonempty_invertible_iff_isUnit,
    (invertibleEquivFromBlocksâ‚‚â‚‚Invertible A B C D).nonempty_congr]

/-- If the top-right element of a block matrix is invertible, then the whole matrix is invertible
iff the corresponding schur complement is. -/
theorem isUnit_fromBlocks_iff_of_invertibleâ‚â‚ {A : Matrix m m Î±} {B : Matrix m n Î±}
    {C : Matrix n m Î±} {D : Matrix n n Î±} [Invertible A] :
    IsUnit (fromBlocks A B C D) â†” IsUnit (D - C * â…ŸA * B) := by
  simp only [â† nonempty_invertible_iff_isUnit,
    (invertibleEquivFromBlocksâ‚â‚Invertible A B C D).nonempty_congr]

end Block

/-! ### Lemmas about `Matrix.det` -/


section Det

/-- Determinant of a 2Ã—2 block matrix, expanded around an invertible top left element in terms of
the Schur complement. -/
theorem det_fromBlocksâ‚â‚ (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible A] :
    (Matrix.fromBlocks A B C D).det = det A * det (D - C * â…ŸA * B) := by
  rw [fromBlocks_eq_of_invertibleâ‚â‚ (A := A), det_mul, det_mul, det_fromBlocks_zeroâ‚‚â‚,
    det_fromBlocks_zeroâ‚‚â‚, det_fromBlocks_zeroâ‚â‚‚, det_one, det_one, one_mul, one_mul, mul_one]

@[simp]
theorem det_fromBlocks_oneâ‚â‚ (B : Matrix m n Î±) (C : Matrix n m Î±) (D : Matrix n n Î±) :
    (Matrix.fromBlocks 1 B C D).det = det (D - C * B) := by
  haveI : Invertible (1 : Matrix m m Î±) := invertibleOne
  rw [det_fromBlocksâ‚â‚, invOf_one, Matrix.mul_one, det_one, one_mul]

/-- Determinant of a 2Ã—2 block matrix, expanded around an invertible bottom right element in terms
of the Schur complement. -/
theorem det_fromBlocksâ‚‚â‚‚ (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±)
    (D : Matrix n n Î±) [Invertible D] :
    (Matrix.fromBlocks A B C D).det = det D * det (A - B * â…ŸD * C) := by
  have : fromBlocks A B C D =
      (fromBlocks D C B A).submatrix (Equiv.sumComm _ _) (Equiv.sumComm _ _) := by
    ext (i j)
    cases i <;> cases j <;> rfl
  rw [this, det_submatrix_equiv_self, det_fromBlocksâ‚â‚]

@[simp]
theorem det_fromBlocks_oneâ‚‚â‚‚ (A : Matrix m m Î±) (B : Matrix m n Î±) (C : Matrix n m Î±) :
    (Matrix.fromBlocks A B C 1).det = det (A - B * C) := by
  haveI : Invertible (1 : Matrix n n Î±) := invertibleOne
  rw [det_fromBlocksâ‚‚â‚‚, invOf_one, Matrix.mul_one, det_one, one_mul]

/-- The **Weinsteinâ€“Aronszajn identity**. Note the `1` on the LHS is of shape mÃ—m, while the `1` on
the RHS is of shape nÃ—n. -/
theorem det_one_add_mul_comm (A : Matrix m n Î±) (B : Matrix n m Î±) :
    det (1 + A * B) = det (1 + B * A) :=
  calc
    det (1 + A * B) = det (fromBlocks 1 (-A) B 1) := by
      rw [det_fromBlocks_oneâ‚‚â‚‚, Matrix.neg_mul, sub_neg_eq_add]
    _ = det (1 + B * A) := by rw [det_fromBlocks_oneâ‚â‚, Matrix.mul_neg, sub_neg_eq_add]

/-- Alternate statement of the **Weinsteinâ€“Aronszajn identity** -/
theorem det_mul_add_one_comm (A : Matrix m n Î±) (B : Matrix n m Î±) :
    det (A * B + 1) = det (B * A + 1) := by rw [add_comm, det_one_add_mul_comm, add_comm]

theorem det_one_sub_mul_comm (A : Matrix m n Î±) (B : Matrix n m Î±) :
    det (1 - A * B) = det (1 - B * A) := by
  rw [sub_eq_add_neg, â† Matrix.neg_mul, det_one_add_mul_comm, Matrix.mul_neg, â† sub_eq_add_neg]

/-- A special case of the **Matrix determinant lemma** for when `A = I`. -/
theorem det_one_add_replicateCol_mul_replicateRow {Î¹ : Type*} [Unique Î¹] (u v : m â†’ Î±) :
    det (1 + replicateCol Î¹ u * replicateRow Î¹ v) = 1 + v â¬áµ¥ u := by
  rw [det_one_add_mul_comm, det_unique, Pi.add_apply, Pi.add_apply, Matrix.one_apply_eq,
    Matrix.replicateRow_mul_replicateCol_apply]

@[deprecated (since := "2025-03-20")] alias
  det_one_add_col_mul_row := det_one_add_replicateCol_mul_replicateRow


/-- The **Matrix determinant lemma**

TODO: show the more general version without `hA : IsUnit A.det` as
`(A + replicateCol u * replicateRow v).det = A.det + v â¬áµ¥ (adjugate A) *áµ¥ u`.
-/
theorem det_add_replicateCol_mul_replicateRow {Î¹ : Type*} [Unique Î¹]
    {A : Matrix m m Î±} (hA : IsUnit A.det) (u v : m â†’ Î±) :
    (A + replicateCol Î¹ u * replicateRow Î¹ v).det =
    A.det * (1 + replicateRow Î¹ v * Aâ»Â¹ * replicateCol Î¹ u).det := by
  nth_rewrite 1 [â† Matrix.mul_one A]
  rwa [â† Matrix.mul_nonsing_inv_cancel_left A (replicateCol Î¹ u * replicateRow Î¹ v),
    â† Matrix.mul_add, det_mul, â† Matrix.mul_assoc, det_one_add_mul_comm, â† Matrix.mul_assoc]

@[deprecated (since := "2025-03-20")] alias
  det_add_col_mul_row := det_add_replicateCol_mul_replicateRow

/-- A generalization of the **Matrix determinant lemma** -/
theorem det_add_mul {A : Matrix m m Î±} (U : Matrix m n Î±)
    (V : Matrix n m Î±) (hA : IsUnit A.det) :
    (A + U * V).det = A.det * (1 + V * Aâ»Â¹ * U).det := by
  nth_rewrite 1 [â† Matrix.mul_one A]
  rwa [â† Matrix.mul_nonsing_inv_cancel_left A (U * V), â† Matrix.mul_add,
    det_mul, â† Matrix.mul_assoc, det_one_add_mul_comm, â† Matrix.mul_assoc]

end Det

end CommRing

/-! ### Lemmas about `â„` and `â„‚` and other `StarOrderedRing`s -/


section StarOrderedRing

variable {ğ•œ : Type*} [CommRing ğ•œ] [StarRing ğ•œ]

/-- Notation for `Sum.elim`, scoped within the `Matrix` namespace. -/
scoped infixl:65 " âŠ•áµ¥ " => Sum.elim

theorem schur_complement_eqâ‚â‚ [Fintype m] [DecidableEq m] [Fintype n] {A : Matrix m m ğ•œ}
    (B : Matrix m n ğ•œ) (D : Matrix n n ğ•œ) (x : m â†’ ğ•œ) (y : n â†’ ğ•œ) [Invertible A]
    (hA : A.IsHermitian) :
    (star (x âŠ•áµ¥ y)) áµ¥* (fromBlocks A B Bá´´ D) â¬áµ¥ (x âŠ•áµ¥ y) =
      (star (x + (Aâ»Â¹ * B) *áµ¥ y)) áµ¥* A â¬áµ¥ (x + (Aâ»Â¹ * B) *áµ¥ y) +
        (star y) áµ¥* (D - Bá´´ * Aâ»Â¹ * B) â¬áµ¥ y := by
  simp [Function.star_sumElim, vecMul_fromBlocks, add_vecMul,
    dotProduct_mulVec, vecMul_sub, Matrix.mul_assoc, hA.eq,
    conjTranspose_nonsing_inv, star_mulVec]
  abel

theorem schur_complement_eqâ‚‚â‚‚ [Fintype m] [Fintype n] [DecidableEq n] (A : Matrix m m ğ•œ)
    (B : Matrix m n ğ•œ) {D : Matrix n n ğ•œ} (x : m â†’ ğ•œ) (y : n â†’ ğ•œ) [Invertible D]
    (hD : D.IsHermitian) :
    (star (x âŠ•áµ¥ y)) áµ¥* (fromBlocks A B Bá´´ D) â¬áµ¥ (x âŠ•áµ¥ y) =
      (star ((Dâ»Â¹ * Bá´´) *áµ¥ x + y)) áµ¥* D â¬áµ¥ ((Dâ»Â¹ * Bá´´) *áµ¥ x + y) +
        (star x) áµ¥* (A - B * Dâ»Â¹ * Bá´´) â¬áµ¥ x := by
  simp [Function.star_sumElim, vecMul_fromBlocks, add_vecMul,
    dotProduct_mulVec, vecMul_sub, Matrix.mul_assoc, hD.eq,
    conjTranspose_nonsing_inv, star_mulVec]
  abel

theorem IsHermitian.fromBlocksâ‚â‚ [Fintype m] [DecidableEq m] {A : Matrix m m ğ•œ} (B : Matrix m n ğ•œ)
    (D : Matrix n n ğ•œ) (hA : A.IsHermitian) :
    (Matrix.fromBlocks A B Bá´´ D).IsHermitian â†” (D - Bá´´ * Aâ»Â¹ * B).IsHermitian := by
  have hBAB : (Bá´´ * Aâ»Â¹ * B).IsHermitian := by
    apply isHermitian_conjTranspose_mul_mul
    apply hA.inv
  rw [isHermitian_fromBlocks_iff]
  constructor
  Â· intro h
    apply IsHermitian.sub h.2.2.2 hBAB
  Â· intro h
    refine âŸ¨hA, rfl, conjTranspose_conjTranspose B, ?_âŸ©
    rw [â† sub_add_cancel D]
    apply IsHermitian.add h hBAB

theorem IsHermitian.fromBlocksâ‚‚â‚‚ [Fintype n] [DecidableEq n] (A : Matrix m m ğ•œ) (B : Matrix m n ğ•œ)
    {D : Matrix n n ğ•œ} (hD : D.IsHermitian) :
    (Matrix.fromBlocks A B Bá´´ D).IsHermitian â†” (A - B * Dâ»Â¹ * Bá´´).IsHermitian := by
  rw [â† isHermitian_submatrix_equiv (Equiv.sumComm n m), Equiv.sumComm_apply,
    fromBlocks_submatrix_sum_swap_sum_swap]
  convert IsHermitian.fromBlocksâ‚â‚ _ _ hD <;> simp

variable [PartialOrder ğ•œ] [StarOrderedRing ğ•œ]

theorem PosSemidef.fromBlocksâ‚â‚ [Fintype m] [DecidableEq m] [Fintype n] {A : Matrix m m ğ•œ}
    (B : Matrix m n ğ•œ) (D : Matrix n n ğ•œ) (hA : A.PosDef) [Invertible A] :
    (fromBlocks A B Bá´´ D).PosSemidef â†” (D - Bá´´ * Aâ»Â¹ * B).PosSemidef := by
  rw [PosSemidef, IsHermitian.fromBlocksâ‚â‚ _ _ hA.1]
  constructor
  Â· refine fun h => âŸ¨h.1, fun x => ?_âŸ©
    have := h.2 (-((Aâ»Â¹ * B) *áµ¥ x) âŠ•áµ¥ x)
    rw [dotProduct_mulVec, schur_complement_eqâ‚â‚ B D _ _ hA.1, neg_add_cancel, dotProduct_zero,
      zero_add] at this
    rw [dotProduct_mulVec]; exact this
  Â· refine fun h => âŸ¨h.1, fun x => ?_âŸ©
    rw [dotProduct_mulVec, â† Sum.elim_comp_inl_inr x, schur_complement_eqâ‚â‚ B D _ _ hA.1]
    apply le_add_of_nonneg_of_le
    Â· rw [â† dotProduct_mulVec]
      apply hA.posSemidef.2
    Â· rw [â† dotProduct_mulVec (star (x âˆ˜ Sum.inr))]
      apply h.2

theorem PosSemidef.fromBlocksâ‚‚â‚‚ [Fintype m] [Fintype n] [DecidableEq n] (A : Matrix m m ğ•œ)
    (B : Matrix m n ğ•œ) {D : Matrix n n ğ•œ} (hD : D.PosDef) [Invertible D] :
    (fromBlocks A B Bá´´ D).PosSemidef â†” (A - B * Dâ»Â¹ * Bá´´).PosSemidef := by
  rw [â† posSemidef_submatrix_equiv (Equiv.sumComm n m), Equiv.sumComm_apply,
    fromBlocks_submatrix_sum_swap_sum_swap]
  convert PosSemidef.fromBlocksâ‚â‚ Bá´´ A hD <;>
    simp

end StarOrderedRing

end Matrix
