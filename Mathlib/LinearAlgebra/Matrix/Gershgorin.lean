/-
Copyright (c) 2023 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.LinearAlgebra.Determinant

/-!
# Gershgorin's circle theorem

This file gives the proof of Gershgorin's circle theorem `eigenvalue_mem_ball` on the eigenvalues
of matrices and some applications.

## Reference

* https://en.wikipedia.org/wiki/Gershgorin_circle_theorem
-/

open BigOperators

variable {K n : Type*} [NormedField K] [Fintype n] [DecidableEq n] {A : Matrix n n K}

/-- **Gershgorin's circle theorem**: for any eigenvalue `Î¼` of a square matrix `A`, there exists an
index `k` such that `Î¼` lies in the closed ball of center the diagonal term `A k k` and of
radius the sum of the norms `âˆ‘ j â‰  k, â€–A k jâ€–. -/
theorem eigenvalue_mem_ball {Î¼ : K} (hÎ¼ : Module.End.HasEigenvalue (Matrix.toLin' A) Î¼) :
      âˆƒ k, Î¼ âˆˆ Metric.closedBall (A k k) (âˆ‘ j in Finset.univ.erase k, â€–A k jâ€–) := by
  cases isEmpty_or_nonempty n
  -- âŠ¢ âˆƒ k, Î¼ âˆˆ Metric.closedBall (A k k) (âˆ‘ j in Finset.erase Finset.univ k, â€–A k  â€¦
  Â· exfalso
    -- âŠ¢ False
    exact hÎ¼ (Submodule.eq_bot_of_subsingleton _)
    -- ðŸŽ‰ no goals
  Â· obtain âŸ¨v, h_eg, h_nzâŸ© := hÎ¼.exists_hasEigenvector
    -- âŠ¢ âˆƒ k, Î¼ âˆˆ Metric.closedBall (A k k) (âˆ‘ j in Finset.erase Finset.univ k, â€–A k  â€¦
    obtain âŸ¨i, -, h_iâŸ© := Finset.exists_mem_eq_sup' Finset.univ_nonempty (fun i => â€–v iâ€–)
    -- âŠ¢ âˆƒ k, Î¼ âˆˆ Metric.closedBall (A k k) (âˆ‘ j in Finset.erase Finset.univ k, â€–A k  â€¦
    have h_nz : v i â‰  0 := by
      contrapose! h_nz
      ext j
      rw [Pi.zero_apply, â† norm_le_zero_iff]
      refine (h_i â–¸ Finset.le_sup' (fun i => â€–v iâ€–) (Finset.mem_univ j)).trans ?_
      exact norm_le_zero_iff.mpr h_nz
    have h_le : âˆ€ j, â€–v j * (v i)â»Â¹â€– â‰¤ 1 := fun j => by
      rw [norm_mul, norm_inv, mul_inv_le_iff' (norm_pos_iff.mpr h_nz), one_mul]
      exact h_i â–¸ Finset.le_sup' (fun i => â€–v iâ€–) (Finset.mem_univ j)
    simp_rw [mem_closedBall_iff_norm']
    -- âŠ¢ âˆƒ k, â€–A k k - Î¼â€– â‰¤ âˆ‘ j in Finset.erase Finset.univ k, â€–A k jâ€–
    refine âŸ¨i, ?_âŸ©
    -- âŠ¢ â€–A i i - Î¼â€– â‰¤ âˆ‘ j in Finset.erase Finset.univ i, â€–A i jâ€–
    calc
      _ = â€–(A i i * v i - Î¼ * v i) * (v i)â»Â¹â€– := by congr; field_simp [h_nz]; ring
      _ = â€–(A i i * v i - âˆ‘ j, A i j * v j) * (v i)â»Â¹â€– := by
                rw [show Î¼ * v i = âˆ‘ x : n, A i x * v x by
                  rw [â† Matrix.dotProduct, â† Matrix.mulVec]
                  exact (congrFun (Module.End.mem_eigenspace_iff.mp h_eg) i).symm]
      _ = â€–(âˆ‘ j in Finset.univ.erase i, A i j * v j) * (v i)â»Â¹â€– := by
                rw [Finset.sum_erase_eq_sub (Finset.mem_univ i), â† neg_sub, neg_mul, norm_neg]
      _ â‰¤ âˆ‘ j in Finset.univ.erase i, â€–A i jâ€– * â€–v j * (v i)â»Â¹â€– := by
                rw [Finset.sum_mul]
                exact (norm_sum_le _ _).trans (le_of_eq (by simp_rw [mul_assoc, norm_mul]))
      _ â‰¤ âˆ‘ j in Finset.univ.erase i, â€–A i jâ€– :=
                (Finset.sum_le_sum fun j _ => mul_le_of_le_one_right (norm_nonneg _) (h_le j))

/-- If `A` is a row strictly dominant diagonal matrix, then it's determinant is nonzero. -/
theorem det_ne_zero_of_sum_row_lt_diag (h : âˆ€ k, âˆ‘ j in Finset.univ.erase k, â€–A k jâ€– < â€–A k kâ€–) :
    A.det â‰  0 := by
  contrapose! h
  -- âŠ¢ âˆƒ k, â€–A k kâ€– â‰¤ âˆ‘ j in Finset.erase Finset.univ k, â€–A k jâ€–
  suffices âˆƒ k, 0 âˆˆ Metric.closedBall (A k k) (âˆ‘ j in Finset.univ.erase k, â€–A k jâ€–) by
    exact this.imp (fun a h â†¦ by rwa [mem_closedBall_iff_norm', sub_zero] at h)
  refine eigenvalue_mem_ball ?_
  -- âŠ¢ Module.End.HasEigenvalue (â†‘Matrix.toLin' fun k => A k) 0
  rw [Module.End.HasEigenvalue,  Module.End.eigenspace_zero, ne_comm]
  -- âŠ¢ âŠ¥ â‰  LinearMap.ker (â†‘Matrix.toLin' fun k => A k)
  exact ne_of_lt (LinearMap.bot_lt_ker_of_det_eq_zero (by rwa [LinearMap.det_toLin']))
  -- ðŸŽ‰ no goals

/-- If `A` is a column strictly dominant diagonal matrix, then it's determinant is nonzero. -/
theorem det_ne_zero_of_sum_col_lt_diag (h : âˆ€ k, âˆ‘ i in Finset.univ.erase k, â€–A i kâ€– < â€–A k kâ€–) :
    A.det â‰  0 := by
  rw [â† Matrix.det_transpose]
  -- âŠ¢ Matrix.det (Matrix.transpose A) â‰  0
  exact det_ne_zero_of_sum_row_lt_diag (by simp_rw [Matrix.transpose_apply]; exact h)
  -- ðŸŽ‰ no goals
