/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Mohanad Ahmed
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.LinearAlgebra.Matrix.Vec
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-! # Positive Definite Matrices

This file defines positive (semi)definite matrices and connects the notion to positive definiteness
of quadratic forms.
In `Mathlib/Analysis/Matrix/Order.lean`, positive semi-definiteness is used to define the partial
order on matrices on `â„` or `â„‚`.

## Main definitions

* `Matrix.PosSemidef` : a matrix `M : Matrix n n R` is positive semidefinite if it is Hermitian
  and `xá´´Mx` is nonnegative for all `x`.
* `Matrix.PosDef` : a matrix `M : Matrix n n R` is positive definite if it is Hermitian and `xá´´Mx`
  is greater than zero for all nonzero `x`.
* `Matrix.PosDef.innerProductSpace`: the inner product on `n â†’ ğ•œ` induced by a positive definite
  matrix `M`, and is given by `âŸªx, yâŸ« = xá´´My`.

## Main results

* `Matrix.PosSemidef.fromBlocksâ‚â‚` and `Matrix.PosSemidef.fromBlocksâ‚‚â‚‚`: If a matrix `A` is
  positive definite, then `[A B; Bá´´ D]` is positive semidefinite if and only if `D - Bá´´ Aâ»Â¹ B` is
  positive semidefinite.
* `Matrix.PosDef.isUnit`: A positive definite matrix in a field is invertible.
-/

open scoped ComplexOrder

namespace Matrix

variable {m n R R' ğ•œ : Type*}
variable [Fintype m] [Fintype n]
variable [Ring R] [PartialOrder R] [StarRing R]
variable [CommRing R'] [PartialOrder R'] [StarRing R']
variable [RCLike ğ•œ]
open scoped Matrix

/-!
## Positive semidefinite matrices
-/

/-- A matrix `M : Matrix n n R` is positive semidefinite if it is Hermitian and `xá´´ * M * x` is
nonnegative for all `x`. -/
def PosSemidef (M : Matrix n n R) :=
  M.IsHermitian âˆ§ âˆ€ x : n â†’ R, 0 â‰¤ star x â¬áµ¥ (M *áµ¥ x)

protected theorem PosSemidef.diagonal [StarOrderedRing R] [DecidableEq n] {d : n â†’ R} (h : 0 â‰¤ d) :
    PosSemidef (diagonal d) :=
  âŸ¨isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i),
    fun x => by
      refine Fintype.sum_nonneg fun i => ?_
      simpa only [mulVec_diagonal, â† mul_assoc] using conjugate_nonneg (h i) _âŸ©

/-- A diagonal matrix is positive semidefinite iff its diagonal entries are nonnegative. -/
lemma posSemidef_diagonal_iff [StarOrderedRing R] [DecidableEq n] {d : n â†’ R} :
    PosSemidef (diagonal d) â†” (âˆ€ i : n, 0 â‰¤ d i) :=
  âŸ¨fun âŸ¨_, hPâŸ© i â†¦ by simpa using hP (Pi.single i 1), .diagonalâŸ©

namespace PosSemidef

theorem isHermitian {M : Matrix n n R} (hM : M.PosSemidef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_nonneg {M : Matrix n n ğ•œ} (hM : M.PosSemidef) (x : n â†’ ğ•œ) :
    0 â‰¤ RCLike.re (star x â¬áµ¥ (M *áµ¥ x)) :=
  RCLike.nonneg_iff.mp (hM.2 _) |>.1

lemma conjTranspose_mul_mul_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix n m R) :
    PosSemidef (Bá´´ * A * B) := by
  constructor
  Â· exact isHermitian_conjTranspose_mul_mul B hA.1
  Â· intro x
    simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 (B *áµ¥ x)

lemma mul_mul_conjTranspose_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix m n R) :
    PosSemidef (B * A * Bá´´) := by
  simpa only [conjTranspose_conjTranspose] using hA.conjTranspose_mul_mul_same Bá´´

theorem submatrix {M : Matrix n n R} (hM : M.PosSemidef) (e : m â†’ n) :
    (M.submatrix e e).PosSemidef := by
  classical
  rw [(by simp : M = 1 * M * 1), submatrix_mul (heâ‚‚ := Function.bijective_id),
    submatrix_mul (heâ‚‚ := Function.bijective_id), submatrix_id_id]
  simpa only [conjTranspose_submatrix, conjTranspose_one] using
    conjTranspose_mul_mul_same hM (Matrix.submatrix 1 id e)

theorem transpose {M : Matrix n n R'} (hM : M.PosSemidef) : Máµ€.PosSemidef := by
  refine âŸ¨IsHermitian.transpose hM.1, fun x => ?_âŸ©
  convert hM.2 (star x) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem _root_.Matrix.posSemidef_transpose_iff {M : Matrix n n R'} : Máµ€.PosSemidef â†” M.PosSemidef :=
  âŸ¨(by simpa using Â·.transpose), .transposeâŸ©

theorem conjTranspose {M : Matrix n n R} (hM : M.PosSemidef) : Má´´.PosSemidef := hM.1.symm â–¸ hM

@[simp]
theorem _root_.Matrix.posSemidef_conjTranspose_iff {M : Matrix n n R} :
    Má´´.PosSemidef â†” M.PosSemidef :=
  âŸ¨(by simpa using Â·.conjTranspose), .conjTransposeâŸ©

protected lemma zero : PosSemidef (0 : Matrix n n R) :=
  âŸ¨isHermitian_zero, by simpâŸ©

protected lemma one [StarOrderedRing R] [DecidableEq n] : PosSemidef (1 : Matrix n n R) :=
  âŸ¨isHermitian_one, fun x => by
    rw [one_mulVec]; exact Fintype.sum_nonneg fun i => star_mul_self_nonneg _âŸ©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] (d : â„•) :
    PosSemidef (d : Matrix n n R) :=
  âŸ¨isHermitian_natCast _, fun x => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_nonneg (dotProduct_star_self_nonneg _) _âŸ©

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] (d : â„•) [d.AtLeastTwo] :
    PosSemidef (ofNat(d) : Matrix n n R) :=
  .natCast d

protected theorem intCast [StarOrderedRing R] [DecidableEq n] (d : â„¤) (hd : 0 â‰¤ d) :
    PosSemidef (d : Matrix n n R) :=
  âŸ¨isHermitian_intCast _, fun x => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_nonneg (dotProduct_star_self_nonneg _) hdâŸ©

@[simp]
protected theorem _root_.Matrix.posSemidef_intCast_iff
    [StarOrderedRing R] [DecidableEq n] [Nonempty n] [Nontrivial R] (d : â„¤) :
    PosSemidef (d : Matrix n n R) â†” 0 â‰¤ d :=
  posSemidef_diagonal_iff.trans <| by simp

protected lemma pow [StarOrderedRing R] [DecidableEq n]
    {M : Matrix n n R} (hM : M.PosSemidef) (k : â„•) :
    PosSemidef (M ^ k) :=
  match k with
  | 0 => .one
  | 1 => by simpa using hM
  | (k + 2) => by
    rw [pow_succ, pow_succ']
    simpa only [hM.isHermitian.eq] using (hM.pow k).mul_mul_conjTranspose_same M

protected lemma inv [DecidableEq n] {M : Matrix n n R'} (hM : M.PosSemidef) : Mâ»Â¹.PosSemidef := by
  by_cases h : IsUnit M.det
  Â· have := (conjTranspose_mul_mul_same hM Mâ»Â¹).conjTranspose
    rwa [mul_nonsing_inv_cancel_right _ _ h, conjTranspose_conjTranspose] at this
  Â· rw [nonsing_inv_apply_not_isUnit _ h]
    exact .zero

protected lemma zpow [StarOrderedRing R'] [DecidableEq n]
    {M : Matrix n n R'} (hM : M.PosSemidef) (z : â„¤) :
    (M ^ z).PosSemidef := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· simpa using hM.pow n
  Â· simpa using (hM.pow n).inv

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosSemidef) : (A + B).PosSemidef :=
  âŸ¨hA.isHermitian.add hB.isHermitian, fun x => by
    rw [add_mulVec, dotProduct_add]
    exact add_nonneg (hA.2 x) (hB.2 x)âŸ©

protected theorem smul {Î± : Type*} [CommSemiring Î±] [PartialOrder Î±] [StarRing Î±]
    [StarOrderedRing Î±] [Algebra Î± R] [StarModule Î± R] [PosSMulMono Î± R] {x : Matrix n n R}
    (hx : x.PosSemidef) {a : Î±} (ha : 0 â‰¤ a) : (a â€¢ x).PosSemidef := by
  refine âŸ¨IsSelfAdjoint.smul (IsSelfAdjoint.of_nonneg ha) hx.1, fun y => ?_âŸ©
  simp only [smul_mulVec, dotProduct_smul]
  exact smul_nonneg ha (hx.2 _)

/-- The eigenvalues of a positive semi-definite matrix are non-negative -/
lemma eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : Matrix.PosSemidef A) (i : n) : 0 â‰¤ hA.1.eigenvalues i :=
  (hA.re_dotProduct_nonneg _).trans_eq (hA.1.eigenvalues_eq _).symm

theorem trace_nonneg {A : Matrix n n ğ•œ} (hA : A.PosSemidef) : 0 â‰¤ A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, â† map_sum,
    Finset.sum_nonneg (fun _ _ => hA.eigenvalues_nonneg _)]

theorem det_nonneg [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosSemidef) :
    0 â‰¤ M.det := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  exact Finset.prod_nonneg fun i _ â†¦ by simpa using hM.eigenvalues_nonneg i

end PosSemidef

@[simp]
theorem posSemidef_submatrix_equiv {M : Matrix n n R} (e : m â‰ƒ n) :
    (M.submatrix e e).PosSemidef â†” M.PosSemidef :=
  âŸ¨fun h => by simpa using h.submatrix e.symm, fun h => h.submatrix _âŸ©

/-- The conjugate transpose of a matrix multiplied by the matrix is positive semidefinite -/
theorem posSemidef_conjTranspose_mul_self [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (Aá´´ * A) := by
  refine âŸ¨isHermitian_transpose_mul_self _, fun x => ?_âŸ©
  rw [â† mulVec_mulVec, dotProduct_mulVec, vecMul_conjTranspose, star_star]
  exact Finset.sum_nonneg fun i _ => star_mul_self_nonneg _

/-- A matrix multiplied by its conjugate transpose is positive semidefinite -/
theorem posSemidef_self_mul_conjTranspose [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (A * Aá´´) := by
  simpa only [conjTranspose_conjTranspose] using posSemidef_conjTranspose_mul_self Aá´´

theorem posSemidef_sum {Î¹ : Type*} [AddLeftMono R]
    {x : Î¹ â†’ Matrix n n R} (s : Finset Î¹) (h : âˆ€ i âˆˆ s, PosSemidef (x i)) :
    PosSemidef (âˆ‘ i âˆˆ s, x i) := by
  refine âŸ¨isSelfAdjoint_sum s fun _ hi => h _ hi |>.1, fun y => ?_âŸ©
  simp [sum_mulVec, dotProduct_sum, Finset.sum_nonneg fun _ hi => (h _ hi).2 _]

section trace
-- TODO: move these results to an earlier file

variable {R : Type*} [PartialOrder R] [NonUnitalRing R]
  [StarRing R] [StarOrderedRing R] [NoZeroDivisors R]

theorem trace_conjTranspose_mul_self_eq_zero_iff {A : Matrix m n R} :
    (Aá´´ * A).trace = 0 â†” A = 0 := by
  rw [â† star_vec_dotProduct_vec, dotProduct_star_self_eq_zero, vec_eq_zero_iff]

theorem trace_mul_conjTranspose_self_eq_zero_iff {A : Matrix m n R} :
    (A * Aá´´).trace = 0 â†” A = 0 := by
  simpa using trace_conjTranspose_mul_self_eq_zero_iff (A := Aá´´)

end trace

lemma eigenvalues_conjTranspose_mul_self_nonneg (A : Matrix m n ğ•œ) [DecidableEq n] (i : n) :
    0 â‰¤ (isHermitian_transpose_mul_self A).eigenvalues i :=
  (posSemidef_conjTranspose_mul_self _).eigenvalues_nonneg _

lemma eigenvalues_self_mul_conjTranspose_nonneg (A : Matrix m n ğ•œ) [DecidableEq m] (i : m) :
    0 â‰¤ (isHermitian_mul_conjTranspose_self A).eigenvalues i :=
  (posSemidef_self_mul_conjTranspose _).eigenvalues_nonneg _

/-- A Hermitian matrix is positive semi-definite if and only if its eigenvalues are non-negative. -/
lemma IsHermitian.posSemidef_iff_eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : IsHermitian A) : PosSemidef A â†” 0 â‰¤ hA.eigenvalues := by
  refine âŸ¨fun h => h.eigenvalues_nonneg, fun h => ?_âŸ©
  rw [hA.spectral_theorem]
  refine (posSemidef_diagonal_iff.mpr ?_).mul_mul_conjTranspose_same _
  simpa using h

@[deprecated (since := "2025-08-17")] alias âŸ¨_, IsHermitian.posSemidef_of_eigenvalues_nonnegâŸ© :=
  IsHermitian.posSemidef_iff_eigenvalues_nonneg

theorem PosSemidef.trace_eq_zero_iff {A : Matrix n n ğ•œ} (hA : A.PosSemidef) :
    A.trace = 0 â†” A = 0 := by
  refine âŸ¨fun h => ?_, fun h => h â–¸ trace_zero n ğ•œâŸ©
  classical
  simp_rw [hA.isHermitian.trace_eq_sum_eigenvalues, â† RCLike.ofReal_sum,
    RCLike.ofReal_eq_zero, Finset.sum_eq_zero_iff_of_nonneg (s := Finset.univ)
      (by simpa using hA.eigenvalues_nonneg), Finset.mem_univ, true_imp_iff] at h
  exact funext_iff.eq â–¸ hA.isHermitian.eigenvalues_eq_zero_iff.mp <| h

section conjugate
variable [DecidableEq n] {U x : Matrix n n R}

/-- For an invertible matrix `U`, `star U * x * U` is positive semi-definite iff `x` is.
This works on any â‹†-ring with a partial order.

See `IsUnit.conjugate_nonneg_iff'` for a similar statement for star-ordered rings. -/
theorem IsUnit.posSemidef_conjugate_iff' (hU : IsUnit U) :
    PosSemidef (star U * x * U) â†” x.PosSemidef := by
  simp_rw [PosSemidef, isHermitian_iff_isSelfAdjoint, hU.isSelfAdjoint_conjugate_iff',
    and_congr_right_iff, â† mulVec_mulVec, dotProduct_mulVec, star_eq_conjTranspose, â† star_mulVec,
    â† dotProduct_mulVec]
  obtain âŸ¨V, hVâŸ© := hU.exists_right_inv
  exact fun _ => âŸ¨fun H y => by simpa [hV] using H (V *áµ¥ y), fun H _ => H _âŸ©

open Matrix in
/-- For an invertible matrix `U`, `U * x * star U` is positive semi-definite iff `x` is.
This works on any â‹†-ring with a partial order.

See `IsUnit.conjugate_nonneg_iff` for a similar statement for star-ordered rings. -/
theorem IsUnit.posSemidef_conjugate_iff (hU : IsUnit U) :
    PosSemidef (U * x * star U) â†” x.PosSemidef := by simpa using hU.star.posSemidef_conjugate_iff'

end conjugate

/-- The matrix `vecMulVec a (star a)` is always positive semi-definite. -/
theorem posSemidef_vecMulVec_self_star [StarOrderedRing R] (a : n â†’ R) :
    (vecMulVec a (star a)).PosSemidef := by
  simp [vecMulVec_eq Unit, â† conjTranspose_replicateCol, posSemidef_self_mul_conjTranspose]

/-- The matrix `vecMulVec (star a) a` is always postive semi-definite. -/
theorem posSemidef_vecMulVec_star_self [StarOrderedRing R] (a : n â†’ R) :
    (vecMulVec (star a) a).PosSemidef := by
  simp [vecMulVec_eq Unit, â† conjTranspose_replicateRow, posSemidef_conjTranspose_mul_self]

/-!
## Positive definite matrices
-/

/-- A matrix `M : Matrix n n R` is positive definite if it is Hermitian
and `xá´´Mx` is greater than zero for all nonzero `x`. -/
def PosDef (M : Matrix n n R) :=
  M.IsHermitian âˆ§ âˆ€ x : n â†’ R, x â‰  0 â†’ 0 < star x â¬áµ¥ (M *áµ¥ x)

namespace PosDef

theorem isHermitian {M : Matrix n n R} (hM : M.PosDef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_pos {M : Matrix n n ğ•œ} (hM : M.PosDef) {x : n â†’ ğ•œ} (hx : x â‰  0) :
    0 < RCLike.re (star x â¬áµ¥ (M *áµ¥ x)) :=
  RCLike.pos_iff.mp (hM.2 _ hx) |>.1

theorem posSemidef {M : Matrix n n R} (hM : M.PosDef) : M.PosSemidef := by
  refine âŸ¨hM.1, ?_âŸ©
  intro x
  by_cases hx : x = 0
  Â· simp only [hx, zero_dotProduct, star_zero]
    exact le_rfl
  Â· exact le_of_lt (hM.2 x hx)

theorem transpose {M : Matrix n n R'} (hM : M.PosDef) : Máµ€.PosDef := by
  refine âŸ¨IsHermitian.transpose hM.1, fun x hx => ?_âŸ©
  convert hM.2 (star x) (star_ne_zero.2 hx) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem transpose_iff {M : Matrix n n R'} : Máµ€.PosDef â†” M.PosDef :=
  âŸ¨(by simpa using Â·.transpose), .transposeâŸ©

protected theorem diagonal [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    {d : n â†’ R} (h : âˆ€ i, 0 < d i) :
    PosDef (diagonal d) :=
  âŸ¨isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i).le,
    fun x hx => by
      refine Fintype.sum_pos ?_
      simp_rw [mulVec_diagonal, â† mul_assoc, Pi.lt_def]
      obtain âŸ¨i, hiâŸ© := Function.ne_iff.mp hx
      exact âŸ¨fun i => conjugate_nonneg (h i).le _,
        i, conjugate_pos (h _) (isRegular_of_ne_zero hi)âŸ©âŸ©

@[simp]
theorem _root_.Matrix.posDef_diagonal_iff
    [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] [Nontrivial R] {d : n â†’ R} :
    PosDef (diagonal d) â†” âˆ€ i, 0 < d i := by
  refine âŸ¨fun h i => ?_, .diagonalâŸ©
  have := h.2 (Pi.single i 1)
  simp_rw [mulVec_single_one, â† Pi.single_star, star_one, single_dotProduct, one_mul,
    col_apply, diagonal_apply_eq, Function.ne_iff] at this
  exact this âŸ¨i, by simpâŸ©

protected theorem one [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] :
    PosDef (1 : Matrix n n R) :=
  âŸ¨isHermitian_one, fun x hx => by simpa only [one_mulVec, dotProduct_star_self_pos_iff]âŸ©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„•) (hd : d â‰  0) :
    PosDef (d : Matrix n n R) :=
  âŸ¨isHermitian_natCast _, fun x hx => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hdâŸ©

@[simp]
theorem _root_.Matrix.posDef_natCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : â„•} :
    PosDef (d : Matrix n n R) â†” 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„•) [d.AtLeastTwo] :
    PosDef (ofNat(d) : Matrix n n R) :=
  .natCast d (NeZero.ne _)

protected theorem intCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„¤) (hd : 0 < d) :
    PosDef (d : Matrix n n R) :=
  âŸ¨isHermitian_intCast _, fun x hx => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hdâŸ©

@[simp]
theorem _root_.Matrix.posDef_intCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : â„¤} :
    PosDef (d : Matrix n n R) â†” 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected lemma add_posSemidef [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosSemidef) : (A + B).PosDef :=
  âŸ¨hA.isHermitian.add hB.isHermitian, fun x hx => by
    rw [add_mulVec, dotProduct_add]
    exact add_pos_of_pos_of_nonneg (hA.2 x hx) (hB.2 x)âŸ©

protected lemma posSemidef_add [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosDef) : (A + B).PosDef :=
  add_comm A B â–¸ hB.add_posSemidef hA

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosDef) : (A + B).PosDef :=
  hA.add_posSemidef hB.posSemidef

theorem _root_.Matrix.posDef_sum {Î¹ : Type*} [AddLeftMono R] {A : Î¹ â†’ Matrix m m R}
    {s : Finset Î¹} (hs : s.Nonempty) (hA : âˆ€ i âˆˆ s, (A i).PosDef) : (âˆ‘ i âˆˆ s, A i).PosDef := by
  classical
  induction s using Finset.induction_on with
  | empty => simp at hs
  | insert i hi hins H =>
      rw [Finset.sum_insert hins]
      by_cases h : Â¬ hi.Nonempty
      Â· simp_all
      Â· exact PosDef.add (hA _ <| Finset.mem_insert_self i hi) <|
          H (not_not.mp h) fun _ _hi => hA _ (Finset.mem_insert_of_mem _hi)

protected theorem smul {Î± : Type*} [CommSemiring Î±] [PartialOrder Î±] [StarRing Î±]
    [StarOrderedRing Î±] [Algebra Î± R] [StarModule Î± R] [PosSMulStrictMono Î± R]
    {x : Matrix n n R} (hx : x.PosDef) {a : Î±} (ha : 0 < a) : (a â€¢ x).PosDef := by
  refine âŸ¨IsSelfAdjoint.smul (IsSelfAdjoint.of_nonneg ha.le) hx.1, fun y hy => ?_âŸ©
  simp only [smul_mulVec, dotProduct_smul]
  exact smul_pos ha (hx.2 _ hy)

lemma conjTranspose_mul_mul_same {A : Matrix n n R} {B : Matrix n m R} (hA : A.PosDef)
    (hB : Function.Injective B.mulVec) :
    (Bá´´ * A * B).PosDef := by
  refine âŸ¨Matrix.isHermitian_conjTranspose_mul_mul _ hA.1, fun x hx => ?_âŸ©
  have : B *áµ¥ x â‰  0 := fun h => hx <| hB.eq_iff' (mulVec_zero _) |>.1 h
  simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 _ this

lemma mul_mul_conjTranspose_same {A : Matrix n n R} {B : Matrix m n R} (hA : A.PosDef)
    (hB : Function.Injective B.vecMul) :
    (B * A * Bá´´).PosDef := by
  replace hB := star_injective.comp <| hB.comp star_injective
  simp_rw [Function.comp_def, star_vecMul, star_star] at hB
  simpa using hA.conjTranspose_mul_mul_same (B := Bá´´) hB

theorem conjTranspose_mul_self [StarOrderedRing R] [NoZeroDivisors R] (A : Matrix m n R)
    (hA : Function.Injective A.mulVec) :
    PosDef (Aá´´ * A) := by
  classical
  simpa using conjTranspose_mul_mul_same .one hA

theorem mul_conjTranspose_self [StarOrderedRing R] [NoZeroDivisors R] (A : Matrix m n R)
    (hA : Function.Injective A.vecMul) :
    PosDef (A * Aá´´) := by
  classical
  simpa using mul_mul_conjTranspose_same .one hA

theorem conjTranspose {M : Matrix n n R} (hM : M.PosDef) : Má´´.PosDef := hM.1.symm â–¸ hM

@[simp]
theorem _root_.Matrix.posDef_conjTranspose_iff {M : Matrix n n R} : Má´´.PosDef â†” M.PosDef :=
  âŸ¨(by simpa using Â·.conjTranspose), .conjTransposeâŸ©

theorem of_toQuadraticForm' [DecidableEq n] {M : Matrix n n â„} (hM : M.IsSymm)
    (hMq : M.toQuadraticMap'.PosDef) : M.PosDef := by
  refine âŸ¨hM, fun x hx => ?_âŸ©
  simp only [toQuadraticMap', QuadraticMap.PosDef, LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMapâ‚‚'_apply'] at hMq
  apply hMq x hx

theorem toQuadraticForm' [DecidableEq n] {M : Matrix n n â„} (hM : M.PosDef) :
    M.toQuadraticMap'.PosDef := by
  intro x hx
  simp only [Matrix.toQuadraticMap', LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMapâ‚‚'_apply']
  apply hM.2 x hx

/-- The eigenvalues of a positive definite matrix are positive -/
lemma eigenvalues_pos [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : Matrix.PosDef A) (i : n) : 0 < hA.1.eigenvalues i := by
  simp only [hA.1.eigenvalues_eq]
  exact hA.re_dotProduct_pos <| hA.1.eigenvectorBasis.orthonormal.ne_zero i

/-- A Hermitian matrix is positive-definite if and only if its eigenvalues are positive. -/
lemma _root_.Matrix.IsHermitian.posDef_iff_eigenvalues_pos [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : A.IsHermitian) : A.PosDef â†” âˆ€ i, 0 < hA.eigenvalues i := by
  refine âŸ¨fun h => h.eigenvalues_pos, fun h => ?_âŸ©
  rw [hA.spectral_theorem]
  refine (posDef_diagonal_iff.mpr <| by simpa using h).mul_mul_conjTranspose_same ?_
  rw [vecMul_injective_iff_isUnit, â† unitary.val_toUnits_apply]
  exact Units.isUnit _

theorem trace_pos [Nonempty n] {A : Matrix n n ğ•œ} (hA : A.PosDef) : 0 < A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, â† map_sum,
    Finset.sum_pos (fun _ _ => hA.eigenvalues_pos _)]

theorem det_pos [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosDef) : 0 < det M := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  apply Finset.prod_pos
  intro i _
  simpa using hM.eigenvalues_pos i

section Field
variable {K : Type*} [Field K] [PartialOrder K] [StarRing K]

theorem isUnit [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : IsUnit M := by
  by_contra h
  obtain âŸ¨a, ha, ha2âŸ© : âˆƒ a â‰  0, M *áµ¥ a = 0 := by
    obtain âŸ¨a, b, haâŸ© := Function.not_injective_iff.mp <| mulVec_injective_iff_isUnit.not.mpr h
    exact âŸ¨a - b, by simp [sub_eq_zero, ha, mulVec_sub]âŸ©
  simpa [ha2] using hM.2 _ ha

protected theorem inv [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : Mâ»Â¹.PosDef := by
  have := hM.mul_mul_conjTranspose_same (B := Mâ»Â¹) ?_
  Â· let _ := hM.isUnit.invertible
    simpa using this.conjTranspose
  Â· simp only [Matrix.vecMul_injective_iff_isUnit, isUnit_nonsing_inv_iff, hM.isUnit]

@[simp]
theorem _root_.Matrix.posDef_inv_iff [DecidableEq n] {M : Matrix n n K} :
    Mâ»Â¹.PosDef â†” M.PosDef :=
  âŸ¨fun h =>
    letI := (Matrix.isUnit_nonsing_inv_iff.1 <| h.isUnit).invertible
    Matrix.inv_inv_of_invertible M â–¸ h.inv, (Â·.inv)âŸ©

end Field

section conjugate
variable [DecidableEq n] {x U : Matrix n n R}

/-- For an invertible matrix `U`, `star U * x * U` is positive definite iff `x` is.
This works on any â‹†-ring with a partial order.

See `IsUnit.isStrictlyPositive_conjugate_iff'` for a similar statement for star-ordered rings.
For matrices, positive definiteness is equivalent to strict positivity when the underlying field is
`â„` or `â„‚` (see `Matrix.isStrictlyPositive_iff_posDef`). -/
theorem _root_.Matrix.IsUnit.posDef_conjugate_iff' (hU : IsUnit U) :
    PosDef (star U * x * U) â†” x.PosDef := by
  simp_rw [PosDef, isHermitian_iff_isSelfAdjoint, hU.isSelfAdjoint_conjugate_iff',
    and_congr_right_iff, â† mulVec_mulVec, dotProduct_mulVec, star_eq_conjTranspose, â† star_mulVec,
    â† dotProduct_mulVec]
  obtain âŸ¨V, hV, hV2âŸ© := isUnit_iff_exists.mp hU
  have hV3 (y : n â†’ R) (hy : y â‰  0) : U *áµ¥ y â‰  0 := fun h => by simpa [hy, hV2] using congr(V *áµ¥ $h)
  have hV4 (y : n â†’ R) (hy : y â‰  0) : V *áµ¥ y â‰  0 := fun h => by simpa [hy, hV] using congr(U *áµ¥ $h)
  exact fun _ => âŸ¨fun h x hx => by simpa [hV] using h _ (hV4 _ hx), fun h x hx => h _ (hV3 _ hx)âŸ©

open Matrix in
/-- For an invertible matrix `U`, `U * x * star U` is positive definite iff `x` is.
This works on any â‹†-ring with a partial order.

See `IsUnit.isStrictlyPositive_conjugate_iff` for a similar statement for star-ordered rings.
For matrices, positive definiteness is equivalent to strict positivity when the underlying field is
`â„` or `â„‚` (see `Matrix.isStrictlyPositive_iff_posDef`). -/
theorem _root_.Matrix.IsUnit.posDef_conjugate_iff (hU : IsUnit U) :
    PosDef (U * x * star U) â†” x.PosDef := by
  simpa using hU.star.posDef_conjugate_iff'

end conjugate

section SchurComplement

variable [StarOrderedRing R']

theorem fromBlocksâ‚â‚ [DecidableEq m] {A : Matrix m m R'}
    (B : Matrix m n R') (D : Matrix n n R') (hA : A.PosDef) [Invertible A] :
    (fromBlocks A B Bá´´ D).PosSemidef â†” (D - Bá´´ * Aâ»Â¹ * B).PosSemidef := by
  rw [PosSemidef, IsHermitian.fromBlocksâ‚â‚ _ _ hA.1]
  constructor
  Â· refine fun h => âŸ¨h.1, fun x => ?_âŸ©
    have := h.2 (-((Aâ»Â¹ * B) *áµ¥ x) âŠ•áµ¥ x)
    rwa [dotProduct_mulVec, schur_complement_eqâ‚â‚ B D _ _ hA.1, neg_add_cancel, dotProduct_zero,
      zero_add, â† dotProduct_mulVec] at this
  Â· refine fun h => âŸ¨h.1, fun x => ?_âŸ©
    rw [dotProduct_mulVec, â† Sum.elim_comp_inl_inr x, schur_complement_eqâ‚â‚ B D _ _ hA.1]
    apply le_add_of_nonneg_of_le
    Â· rw [â† dotProduct_mulVec]
      apply hA.posSemidef.2
    Â· rw [â† dotProduct_mulVec (star (x âˆ˜ Sum.inr))]
      apply h.2

theorem fromBlocksâ‚‚â‚‚ [DecidableEq n] (A : Matrix m m R')
    (B : Matrix m n R') {D : Matrix n n R'} (hD : D.PosDef) [Invertible D] :
    (fromBlocks A B Bá´´ D).PosSemidef â†” (A - B * Dâ»Â¹ * Bá´´).PosSemidef := by
  rw [â† posSemidef_submatrix_equiv (Equiv.sumComm n m), Equiv.sumComm_apply,
    fromBlocks_submatrix_sum_swap_sum_swap]
  convert fromBlocksâ‚â‚ Bá´´ A hD <;> simp

end SchurComplement

end PosDef

end Matrix

namespace QuadraticForm

open QuadraticMap

variable {n : Type*} [Fintype n]

theorem posDef_of_toMatrix' [DecidableEq n] {Q : QuadraticForm â„ (n â†’ â„)}
    (hQ : Q.toMatrix'.PosDef) : Q.PosDef := by
  rw [â† toQuadraticMap_associated â„ Q,
    â† (LinearMap.toMatrixâ‚‚' â„).left_inv ((associatedHom (R := â„) â„) Q)]
  exact hQ.toQuadraticForm'

theorem posDef_toMatrix' [DecidableEq n] {Q : QuadraticForm â„ (n â†’ â„)} (hQ : Q.PosDef) :
    Q.toMatrix'.PosDef := by
  rw [â† toQuadraticMap_associated â„ Q, â†
    (LinearMap.toMatrixâ‚‚' â„).left_inv ((associatedHom (R := â„) â„) Q)] at hQ
  exact .of_toQuadraticForm' (isSymm_toMatrix' Q) hQ

end QuadraticForm

namespace Matrix
variable {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]

/-- The pre-inner product space structure implementation. Only an auxiliary for the below. -/
private def PosSemidef.preInnerProductSpace {M : Matrix n n ğ•œ} (hM : M.PosSemidef) :
    PreInnerProductSpace.Core ğ•œ (n â†’ ğ•œ) where
  inner x y := (M *áµ¥ y) â¬áµ¥ star x
  conj_inner_symm x y := by
    rw [dotProduct_comm, star_dotProduct, starRingEnd_apply, star_star,
      star_mulVec, dotProduct_comm (M *áµ¥ y), dotProduct_mulVec, hM.isHermitian.eq]
  re_inner_nonneg x := dotProduct_comm _ (star x) â–¸ hM.re_dotProduct_nonneg x
  add_left := by simp only [star_add, dotProduct_add, forall_const]
  smul_left _ _ _ := by rw [â† smul_eq_mul, â† dotProduct_smul, starRingEnd_apply, â† star_smul]

/-- A positive semi-definite matrix `M` induces a norm `â€–xâ€– = sqrt (re xá´´Mx)`. -/
noncomputable abbrev PosSemidef.seminormedAddCommGroup (M : Matrix n n ğ•œ) (hM : M.PosSemidef) :
    SeminormedAddCommGroup (n â†’ ğ•œ) :=
  @InnerProductSpace.Core.toSeminormedAddCommGroup _ _ _ _ _ hM.preInnerProductSpace

/-- A positive definite matrix `M` induces a norm `â€–xâ€– = sqrt (re xá´´Mx)`. -/
noncomputable abbrev PosDef.normedAddCommGroup (M : Matrix n n ğ•œ) (hM : M.PosDef) :
    NormedAddCommGroup (n â†’ ğ•œ) :=
  @InnerProductSpace.Core.toNormedAddCommGroup _ _ _ _ _
  { __ := hM.posSemidef.preInnerProductSpace
    definite x (hx : _ â¬áµ¥ _ = 0) := by
      by_contra! h
      simpa [hx, lt_irrefl, dotProduct_comm] using hM.re_dotProduct_pos h }

/-- A positive semi-definite matrix `M` induces an inner product `âŸªx, yâŸ« = xá´´My`. -/
def PosSemidef.innerProductSpace (M : Matrix n n ğ•œ) (hM : M.PosSemidef) :
    @InnerProductSpace ğ•œ (n â†’ ğ•œ) _ hM.seminormedAddCommGroup :=
  InnerProductSpace.ofCore _

/-- A positive definite matrix `M` induces an inner product `âŸªx, yâŸ« = xá´´My`. -/
def PosDef.innerProductSpace (M : Matrix n n ğ•œ) (hM : M.PosDef) :
    @InnerProductSpace ğ•œ (n â†’ ğ•œ) _ hM.normedAddCommGroup.toSeminormedAddCommGroup :=
  hM.posSemidef.innerProductSpace

example (M : Matrix n n ğ•œ) (hM : M.PosDef) :
    hM.normedAddCommGroup.toSeminormedAddCommGroup = hM.posSemidef.seminormedAddCommGroup := rfl
example (M : Matrix n n ğ•œ) (hM : M.PosDef) :
    hM.innerProductSpace = hM.posSemidef.innerProductSpace := rfl

@[deprecated (since := "2025-10-26")] alias NormedAddCommGroup.ofMatrix := PosDef.normedAddCommGroup
@[deprecated  (since := "2025-10-26")] alias InnerProductSpace.ofMatrix := PosDef.innerProductSpace

end Matrix
