/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Mohanad Ahmed
-/
import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.LinearAlgebra.Matrix.Vec
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-! # Positive Definite Matrices

This file defines positive (semi)definite matrices and connects the notion to positive definiteness
of quadratic forms. Most results require `ğ•œ = â„` or `â„‚`.

## Main definitions

* `Matrix.PosDef` : a matrix `M : Matrix n n ğ•œ` is positive definite if it is hermitian and `xá´´Mx`
  is greater than zero for all nonzero `x`.
* `Matrix.PosSemidef` : a matrix `M : Matrix n n ğ•œ` is positive semidefinite if it is hermitian
  and `xá´´Mx` is nonnegative for all `x`.

## Main results

* `Matrix.posSemidef_iff_eq_conjTranspose_mul_self` : a matrix `M : Matrix n n ğ•œ` is positive
  semidefinite iff it has the form `Bá´´ * B` for some `B`.
* `Matrix.posDef_iff_eq_conjTranspose_mul_self` : a matrix `M : Matrix n n ğ•œ` is positive
  definite iff it has the form `Bá´´ * B` for some _invertible_ `B`.
* `Matrix.PosSemidef.sqrt` : the unique positive semidefinite square root of a positive semidefinite
  matrix. (See `Matrix.PosSemidef.eq_sqrt_of_sq_eq` for the proof of uniqueness.)
-/

open scoped ComplexOrder

namespace Matrix

variable {m n R R' ğ•œ : Type*}
variable [Fintype m] [Fintype n]
variable [Ring R] [PartialOrder R] [StarRing R]
variable [CommRing R'] [PartialOrder R'] [StarRing R']
variable [RCLike ğ•œ]
open scoped Matrix

/-!
## Positive semidefinite matrices
-/

/-- A matrix `M : Matrix n n R` is positive semidefinite if it is Hermitian and `xá´´ * M * x` is
nonnegative for all `x`. -/
def PosSemidef (M : Matrix n n R) :=
  M.IsHermitian âˆ§ âˆ€ x : n â†’ R, 0 â‰¤ star x â¬áµ¥ (M *áµ¥ x)

protected theorem PosSemidef.diagonal [StarOrderedRing R] [DecidableEq n] {d : n â†’ R} (h : 0 â‰¤ d) :
    PosSemidef (diagonal d) :=
  âŸ¨isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i),
    fun x => by
      refine Fintype.sum_nonneg fun i => ?_
      simpa only [mulVec_diagonal, â† mul_assoc] using conjugate_nonneg (h i) _âŸ©

/-- A diagonal matrix is positive semidefinite iff its diagonal entries are nonnegative. -/
lemma posSemidef_diagonal_iff [StarOrderedRing R] [DecidableEq n] {d : n â†’ R} :
    PosSemidef (diagonal d) â†” (âˆ€ i : n, 0 â‰¤ d i) :=
  âŸ¨fun âŸ¨_, hPâŸ© i â†¦ by simpa using hP (Pi.single i 1), .diagonalâŸ©

namespace PosSemidef

theorem isHermitian {M : Matrix n n R} (hM : M.PosSemidef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_nonneg {M : Matrix n n ğ•œ} (hM : M.PosSemidef) (x : n â†’ ğ•œ) :
    0 â‰¤ RCLike.re (star x â¬áµ¥ (M *áµ¥ x)) :=
  RCLike.nonneg_iff.mp (hM.2 _) |>.1

lemma conjTranspose_mul_mul_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix n m R) :
    PosSemidef (Bá´´ * A * B) := by
  constructor
  Â· exact isHermitian_conjTranspose_mul_mul B hA.1
  Â· intro x
    simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 (B *áµ¥ x)

lemma mul_mul_conjTranspose_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix m n R) :
    PosSemidef (B * A * Bá´´) := by
  simpa only [conjTranspose_conjTranspose] using hA.conjTranspose_mul_mul_same Bá´´

theorem submatrix {M : Matrix n n R} (hM : M.PosSemidef) (e : m â†’ n) :
    (M.submatrix e e).PosSemidef := by
  classical
  rw [(by simp : M = 1 * M * 1), submatrix_mul (heâ‚‚ := Function.bijective_id),
    submatrix_mul (heâ‚‚ := Function.bijective_id), submatrix_id_id]
  simpa only [conjTranspose_submatrix, conjTranspose_one] using
    conjTranspose_mul_mul_same hM (Matrix.submatrix 1 id e)

theorem transpose {M : Matrix n n R'} (hM : M.PosSemidef) : Máµ€.PosSemidef := by
  refine âŸ¨IsHermitian.transpose hM.1, fun x => ?_âŸ©
  convert hM.2 (star x) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem _root_.Matrix.posSemidef_transpose_iff {M : Matrix n n R'} : Máµ€.PosSemidef â†” M.PosSemidef :=
  âŸ¨(by simpa using Â·.transpose), .transposeâŸ©

theorem conjTranspose {M : Matrix n n R} (hM : M.PosSemidef) : Má´´.PosSemidef := hM.1.symm â–¸ hM

@[simp]
theorem _root_.Matrix.posSemidef_conjTranspose_iff {M : Matrix n n R} :
    Má´´.PosSemidef â†” M.PosSemidef :=
  âŸ¨(by simpa using Â·.conjTranspose), .conjTransposeâŸ©

protected lemma zero : PosSemidef (0 : Matrix n n R) :=
  âŸ¨isHermitian_zero, by simpâŸ©

protected lemma one [StarOrderedRing R] [DecidableEq n] : PosSemidef (1 : Matrix n n R) :=
  âŸ¨isHermitian_one, fun x => by
    rw [one_mulVec]; exact Fintype.sum_nonneg fun i => star_mul_self_nonneg _âŸ©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] (d : â„•) :
    PosSemidef (d : Matrix n n R) :=
  âŸ¨isHermitian_natCast _, fun x => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_nonneg (dotProduct_star_self_nonneg _) _âŸ©

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] (d : â„•) [d.AtLeastTwo] :
    PosSemidef (ofNat(d) : Matrix n n R) :=
  .natCast d

protected theorem intCast [StarOrderedRing R] [DecidableEq n] (d : â„¤) (hd : 0 â‰¤ d) :
    PosSemidef (d : Matrix n n R) :=
  âŸ¨isHermitian_intCast _, fun x => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_nonneg (dotProduct_star_self_nonneg _) hdâŸ©

@[simp]
protected theorem _root_.Matrix.posSemidef_intCast_iff
    [StarOrderedRing R] [DecidableEq n] [Nonempty n] [Nontrivial R] (d : â„¤) :
    PosSemidef (d : Matrix n n R) â†” 0 â‰¤ d :=
  posSemidef_diagonal_iff.trans <| by simp

protected lemma pow [StarOrderedRing R] [DecidableEq n]
    {M : Matrix n n R} (hM : M.PosSemidef) (k : â„•) :
    PosSemidef (M ^ k) :=
  match k with
  | 0 => .one
  | 1 => by simpa using hM
  | (k + 2) => by
    rw [pow_succ, pow_succ']
    simpa only [hM.isHermitian.eq] using (hM.pow k).mul_mul_conjTranspose_same M

protected lemma inv [DecidableEq n] {M : Matrix n n R'} (hM : M.PosSemidef) : Mâ»Â¹.PosSemidef := by
  by_cases h : IsUnit M.det
  Â· have := (conjTranspose_mul_mul_same hM Mâ»Â¹).conjTranspose
    rwa [mul_nonsing_inv_cancel_right _ _ h, conjTranspose_conjTranspose] at this
  Â· rw [nonsing_inv_apply_not_isUnit _ h]
    exact .zero

protected lemma zpow [StarOrderedRing R'] [DecidableEq n]
    {M : Matrix n n R'} (hM : M.PosSemidef) (z : â„¤) :
    (M ^ z).PosSemidef := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· simpa using hM.pow n
  Â· simpa using (hM.pow n).inv

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosSemidef) : (A + B).PosSemidef :=
  âŸ¨hA.isHermitian.add hB.isHermitian, fun x => by
    rw [add_mulVec, dotProduct_add]
    exact add_nonneg (hA.2 x) (hB.2 x)âŸ©

/-- The eigenvalues of a positive semi-definite matrix are non-negative -/
lemma eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : Matrix.PosSemidef A) (i : n) : 0 â‰¤ hA.1.eigenvalues i :=
  (hA.re_dotProduct_nonneg _).trans_eq (hA.1.eigenvalues_eq _).symm

theorem trace_nonneg {A : Matrix n n ğ•œ} (hA : A.PosSemidef) : 0 â‰¤ A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, â† map_sum,
    Finset.sum_nonneg (fun _ _ => hA.eigenvalues_nonneg _)]

theorem det_nonneg [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosSemidef) :
    0 â‰¤ M.det := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  exact Finset.prod_nonneg fun i _ â†¦ by simpa using hM.eigenvalues_nonneg i

section sqrt

variable [DecidableEq n] {A : Matrix n n ğ•œ} (hA : PosSemidef A)

/-- The positive semidefinite square root of a positive semidefinite matrix -/
noncomputable def sqrt : Matrix n n ğ•œ :=
  hA.1.eigenvectorUnitary.1 * diagonal ((â†‘) âˆ˜ (âˆšÂ·) âˆ˜ hA.1.eigenvalues) *
  (star hA.1.eigenvectorUnitary : Matrix n n ğ•œ)

open Lean PrettyPrinter.Delaborator SubExpr in
/-- Custom elaborator to produce output like `(_ : PosSemidef A).sqrt` in the goal view. -/
@[app_delab Matrix.PosSemidef.sqrt]
def delabSqrt : Delab :=
  whenPPOption getPPNotation <|
  whenNotPPOption getPPAnalysisSkip <|
  withOverApp 7 <|
  withOptionAtCurrPos `pp.analysis.skip true do
    let e â† getExpr
    guard <| e.isAppOfArity ``Matrix.PosSemidef.sqrt 7
    let optionsPerPos â† withNaryArg 6 do
      return (â† read).optionsPerPos.setBool (â† getPos) `pp.proofs.withType true
    withTheReader Context ({Â· with optionsPerPos}) delab

lemma posSemidef_sqrt : PosSemidef hA.sqrt := by
  apply PosSemidef.mul_mul_conjTranspose_same
  refine posSemidef_diagonal_iff.mpr fun i â†¦ ?_
  rw [Function.comp_apply, RCLike.nonneg_iff]
  constructor
  Â· simp only [RCLike.ofReal_re]
    exact Real.sqrt_nonneg _
  Â· simp only [RCLike.ofReal_im]

@[simp]
lemma sq_sqrt : hA.sqrt ^ 2 = A := by
  let C : Matrix n n ğ•œ := hA.1.eigenvectorUnitary
  let E := diagonal ((â†‘) âˆ˜ (âˆšÂ·) âˆ˜ hA.1.eigenvalues : n â†’ ğ•œ)
  suffices C * (E * (star C * C) * E) * star C = A by
    rw [Matrix.PosSemidef.sqrt, pow_two]
    simpa only [â† mul_assoc] using this
  have : E * E = diagonal ((â†‘) âˆ˜ hA.1.eigenvalues) := by
    rw [diagonal_mul_diagonal]
    congr! with v
    simp [â† pow_two, â† RCLike.ofReal_pow, Real.sq_sqrt (hA.eigenvalues_nonneg v)]
  simpa [C, this] using hA.1.spectral_theorem.symm

@[simp]
lemma sqrt_mul_self : hA.sqrt * hA.sqrt = A := by rw [â† pow_two, sq_sqrt]

include hA in
lemma eq_of_sq_eq_sq {B : Matrix n n ğ•œ} (hB : PosSemidef B) (hAB : A ^ 2 = B ^ 2) : A = B := by
  /- This is deceptively hard, much more difficult than the positive *definite* case. We follow a
  clever proof due to Koeber and SchÃ¤fer. The idea is that if `A â‰  B`, then `A - B` has a nonzero
  real eigenvalue, with eigenvector `v`. Then a manipulation using the identity
  `A ^ 2 - B ^ 2 = A * (A - B) + (A - B) * B` leads to the conclusion that
  `âŸ¨v, A vâŸ© + âŸ¨v, B vâŸ© = 0`. Since `A, B` are positive semidefinite, both terms must be zero. Thus
  `âŸ¨v, (A - B) vâŸ© = 0`, but this is a nonzero scalar multiple of `âŸ¨v, vâŸ©`, contradiction. -/
  by_contra h_ne
  let âŸ¨v, t, ht, hv, hv'âŸ© := (hA.1.sub hB.1).exists_eigenvector_of_ne_zero (sub_ne_zero.mpr h_ne)
  have h_sum : 0 = t * (star v â¬áµ¥ A *áµ¥ v + star v â¬áµ¥ B *áµ¥ v) := calc
    0 = star v â¬áµ¥ (A ^ 2 - B ^ 2) *áµ¥ v := by rw [hAB, sub_self, zero_mulVec, dotProduct_zero]
    _ = star v â¬áµ¥ A *áµ¥ (A - B) *áµ¥ v + star v â¬áµ¥ (A - B) *áµ¥ B *áµ¥ v := by
      rw [mulVec_mulVec, mulVec_mulVec, â† dotProduct_add, â† add_mulVec, mul_sub, sub_mul,
        add_sub, sub_add_cancel, pow_two, pow_two]
    _ = t * (star v â¬áµ¥ A *áµ¥ v) + (star v) áµ¥* (A - B)á´´ â¬áµ¥ B *áµ¥ v := by
      rw [hv', mulVec_smul, dotProduct_smul, RCLike.real_smul_eq_coe_mul,
        dotProduct_mulVec _ (A - B), hA.1.sub hB.1]
    _ = t * (star v â¬áµ¥ A *áµ¥ v + star v â¬áµ¥ B *áµ¥ v) := by
      simp_rw [â† star_mulVec, hv', mul_add, â† RCLike.real_smul_eq_coe_mul, â† smul_dotProduct]
      congr 2 with i
      simp only [Pi.star_apply, Pi.smul_apply, RCLike.real_smul_eq_coe_mul, star_mul',
        RCLike.star_def, RCLike.conj_ofReal]
  replace h_sum : star v â¬áµ¥ A *áµ¥ v + star v â¬áµ¥ B *áµ¥ v = 0 := by
    rw [eq_comm, â† mul_zero (t : ğ•œ)] at h_sum
    exact mul_left_cancelâ‚€ (RCLike.ofReal_ne_zero.mpr ht) h_sum
  have h_van : star v â¬áµ¥ A *áµ¥ v = 0 âˆ§ star v â¬áµ¥ B *áµ¥ v = 0 := by
    refine âŸ¨le_antisymm ?_ (hA.2 v), le_antisymm ?_ (hB.2 v)âŸ©
    Â· rw [add_comm, add_eq_zero_iff_eq_neg] at h_sum
      simpa only [h_sum, neg_nonneg] using hB.2 v
    Â· simpa only [add_eq_zero_iff_eq_neg.mp h_sum, neg_nonneg] using hA.2 v
  have aux : star v â¬áµ¥ (A - B) *áµ¥ v = 0 := by
    rw [sub_mulVec, dotProduct_sub, h_van.1, h_van.2, sub_zero]
  rw [hv', dotProduct_smul, RCLike.real_smul_eq_coe_mul, â† mul_zero â†‘t] at aux
  exact hv <| dotProduct_star_self_eq_zero.mp <| mul_left_cancelâ‚€
    (RCLike.ofReal_ne_zero.mpr ht) aux

include hA in
lemma sq_eq_sq_iff {B : Matrix n n ğ•œ} (hB : PosSemidef B) : A ^ 2 = B ^ 2 â†” A = B :=
  âŸ¨eq_of_sq_eq_sq hA hB, fun h => h â–¸ rflâŸ©

lemma sqrt_sq : (hA.pow 2 : PosSemidef (A ^ 2)).sqrt = A :=
  (hA.pow 2).posSemidef_sqrt.eq_of_sq_eq_sq hA (hA.pow 2).sq_sqrt

include hA in
lemma eq_sqrt_iff_sq_eq {B : Matrix n n ğ•œ} (hB : PosSemidef B) : A = hB.sqrt â†” A ^ 2 = B :=
  âŸ¨fun h => h â–¸ hB.sq_sqrt, fun h => by subst h; rw [hA.sqrt_sq]âŸ©

include hA in
lemma sqrt_eq_iff_eq_sq {B : Matrix n n ğ•œ} (hB : PosSemidef B) : hA.sqrt = B â†” A = B ^ 2 := by
  simpa [eq_comm] using eq_sqrt_iff_sq_eq hB hA

@[deprecated (since := "2025-05-07")] alias âŸ¨_, eq_sqrt_of_sq_eqâŸ© := eq_sqrt_iff_sq_eq

@[simp]
lemma sqrt_eq_zero_iff : hA.sqrt = 0 â†” A = 0 := by
  rw [sqrt_eq_iff_eq_sq _ .zero, zero_pow two_ne_zero]

@[simp]
lemma sqrt_eq_one_iff : hA.sqrt = 1 â†” A = 1 := by
  rw [sqrt_eq_iff_eq_sq _ .one, one_pow]

@[simp]
lemma isUnit_sqrt_iff : IsUnit hA.sqrt â†” IsUnit A := by
  conv_rhs => rw [â† hA.sqrt_mul_self]
  rw [isUnit_mul_self_iff]

lemma inv_sqrt : hA.sqrtâ»Â¹ = hA.inv.sqrt := by
  rw [eq_sqrt_iff_sq_eq hA.posSemidef_sqrt.inv, sq, â† Matrix.mul_inv_rev, â† sq, sq_sqrt]

end sqrt

end PosSemidef

@[simp]
theorem posSemidef_submatrix_equiv {M : Matrix n n R} (e : m â‰ƒ n) :
    (M.submatrix e e).PosSemidef â†” M.PosSemidef :=
  âŸ¨fun h => by simpa using h.submatrix e.symm, fun h => h.submatrix _âŸ©

/-- The conjugate transpose of a matrix multiplied by the matrix is positive semidefinite -/
theorem posSemidef_conjTranspose_mul_self [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (Aá´´ * A) := by
  refine âŸ¨isHermitian_transpose_mul_self _, fun x => ?_âŸ©
  rw [â† mulVec_mulVec, dotProduct_mulVec, vecMul_conjTranspose, star_star]
  exact Finset.sum_nonneg fun i _ => star_mul_self_nonneg _

/-- A matrix multiplied by its conjugate transpose is positive semidefinite -/
theorem posSemidef_self_mul_conjTranspose [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (A * Aá´´) := by
  simpa only [conjTranspose_conjTranspose] using posSemidef_conjTranspose_mul_self Aá´´

section trace
-- TODO: move these results to an earlier file

variable {R : Type*} [PartialOrder R] [NonUnitalRing R]
  [StarRing R] [StarOrderedRing R] [NoZeroDivisors R]

theorem trace_conjTranspose_mul_self_eq_zero_iff {A : Matrix m n R} :
    (Aá´´ * A).trace = 0 â†” A = 0 := by
  rw [â† star_vec_dotProduct_vec, dotProduct_star_self_eq_zero, vec_eq_zero_iff]

theorem trace_mul_conjTranspose_self_eq_zero_iff {A : Matrix m n R} :
    (A * Aá´´).trace = 0 â†” A = 0 := by
  simpa using trace_conjTranspose_mul_self_eq_zero_iff (A := Aá´´)

end trace

lemma eigenvalues_conjTranspose_mul_self_nonneg (A : Matrix m n ğ•œ) [DecidableEq n] (i : n) :
    0 â‰¤ (isHermitian_transpose_mul_self A).eigenvalues i :=
  (posSemidef_conjTranspose_mul_self _).eigenvalues_nonneg _

lemma eigenvalues_self_mul_conjTranspose_nonneg (A : Matrix m n ğ•œ) [DecidableEq m] (i : m) :
    0 â‰¤ (isHermitian_mul_conjTranspose_self A).eigenvalues i :=
  (posSemidef_self_mul_conjTranspose _).eigenvalues_nonneg _

/-- A matrix is positive semidefinite if and only if it has the form `Bá´´ * B` for some `B`. -/
lemma posSemidef_iff_eq_conjTranspose_mul_self {A : Matrix n n ğ•œ} :
    PosSemidef A â†” âˆƒ (B : Matrix n n ğ•œ), A = Bá´´ * B := by
  classical
  refine âŸ¨fun hA â†¦ âŸ¨hA.sqrt, ?_âŸ©, fun âŸ¨B, hBâŸ© â†¦ (hB â–¸ posSemidef_conjTranspose_mul_self B)âŸ©
  simp_rw [â† PosSemidef.sq_sqrt hA, pow_two]
  rw [hA.posSemidef_sqrt.1]

@[deprecated (since := "2025-05-07")]
alias posSemidef_iff_eq_transpose_mul_self := posSemidef_iff_eq_conjTranspose_mul_self

/-- A Hermitian matrix is positive semi-definite if and only if its eigenvalues are non-negative. -/
lemma IsHermitian.posSemidef_iff_eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : IsHermitian A) : PosSemidef A â†” 0 â‰¤ hA.eigenvalues := by
  refine âŸ¨fun h => h.eigenvalues_nonneg, fun h => ?_âŸ©
  rw [hA.spectral_theorem]
  refine (posSemidef_diagonal_iff.mpr ?_).mul_mul_conjTranspose_same _
  simpa using h

@[deprecated (since := "2025-08-17")] alias âŸ¨_, IsHermitian.posSemidef_of_eigenvalues_nonnegâŸ© :=
  IsHermitian.posSemidef_iff_eigenvalues_nonneg

/-- For `A` positive semidefinite, we have `xâ‹† A x = 0` iff `A x = 0`. -/
theorem PosSemidef.dotProduct_mulVec_zero_iff
    {A : Matrix n n ğ•œ} (hA : PosSemidef A) (x : n â†’ ğ•œ) :
    star x â¬áµ¥ A *áµ¥ x = 0 â†” A *áµ¥ x = 0 := by
  constructor
  Â· obtain âŸ¨B, rflâŸ© := posSemidef_iff_eq_conjTranspose_mul_self.mp hA
    rw [â† Matrix.mulVec_mulVec, dotProduct_mulVec,
      vecMul_conjTranspose, star_star, dotProduct_star_self_eq_zero]
    intro h0
    rw [h0, mulVec_zero]
  Â· intro h0
    rw [h0, dotProduct_zero]

/-- For `A` positive semidefinite, we have `xâ‹† A x = 0` iff `A x = 0` (linear maps version). -/
theorem PosSemidef.toLinearMapâ‚‚'_zero_iff [DecidableEq n]
    {A : Matrix n n ğ•œ} (hA : PosSemidef A) (x : n â†’ ğ•œ) :
    Matrix.toLinearMapâ‚‚' ğ•œ A (star x) x = 0 â†” A *áµ¥ x = 0 := by
  simpa only [toLinearMapâ‚‚'_apply'] using hA.dotProduct_mulVec_zero_iff x

/-!
## Positive definite matrices
-/

/-- A matrix `M : Matrix n n R` is positive definite if it is hermitian
and `xá´´Mx` is greater than zero for all nonzero `x`. -/
def PosDef (M : Matrix n n R) :=
  M.IsHermitian âˆ§ âˆ€ x : n â†’ R, x â‰  0 â†’ 0 < star x â¬áµ¥ (M *áµ¥ x)

namespace PosDef

theorem isHermitian {M : Matrix n n R} (hM : M.PosDef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_pos {M : Matrix n n ğ•œ} (hM : M.PosDef) {x : n â†’ ğ•œ} (hx : x â‰  0) :
    0 < RCLike.re (star x â¬áµ¥ (M *áµ¥ x)) :=
  RCLike.pos_iff.mp (hM.2 _ hx) |>.1

theorem posSemidef {M : Matrix n n R} (hM : M.PosDef) : M.PosSemidef := by
  refine âŸ¨hM.1, ?_âŸ©
  intro x
  by_cases hx : x = 0
  Â· simp only [hx, zero_dotProduct, star_zero]
    exact le_rfl
  Â· exact le_of_lt (hM.2 x hx)

theorem transpose {M : Matrix n n R'} (hM : M.PosDef) : Máµ€.PosDef := by
  refine âŸ¨IsHermitian.transpose hM.1, fun x hx => ?_âŸ©
  convert hM.2 (star x) (star_ne_zero.2 hx) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem transpose_iff {M : Matrix n n R'} : Máµ€.PosDef â†” M.PosDef :=
  âŸ¨(by simpa using Â·.transpose), .transposeâŸ©

protected theorem diagonal [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    {d : n â†’ R} (h : âˆ€ i, 0 < d i) :
    PosDef (diagonal d) :=
  âŸ¨isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i).le,
    fun x hx => by
      refine Fintype.sum_pos ?_
      simp_rw [mulVec_diagonal, â† mul_assoc, Pi.lt_def]
      obtain âŸ¨i, hiâŸ© := Function.ne_iff.mp hx
      exact âŸ¨fun i => conjugate_nonneg (h i).le _,
        i, conjugate_pos (h _) (isRegular_of_ne_zero hi)âŸ©âŸ©

@[simp]
theorem _root_.Matrix.posDef_diagonal_iff
    [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] [Nontrivial R] {d : n â†’ R} :
    PosDef (diagonal d) â†” âˆ€ i, 0 < d i := by
  refine âŸ¨fun h i => ?_, .diagonalâŸ©
  have := h.2 (Pi.single i 1)
  simp_rw [mulVec_single_one, â† Pi.single_star, star_one, single_dotProduct, one_mul,
    col_apply, diagonal_apply_eq, Function.ne_iff] at this
  exact this âŸ¨i, by simpâŸ©

protected theorem one [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] :
    PosDef (1 : Matrix n n R) :=
  âŸ¨isHermitian_one, fun x hx => by simpa only [one_mulVec, dotProduct_star_self_pos_iff]âŸ©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„•) (hd : d â‰  0) :
    PosDef (d : Matrix n n R) :=
  âŸ¨isHermitian_natCast _, fun x hx => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hdâŸ©

@[simp]
theorem _root_.Matrix.posDef_natCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : â„•} :
    PosDef (d : Matrix n n R) â†” 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„•) [d.AtLeastTwo] :
    PosDef (ofNat(d) : Matrix n n R) :=
  .natCast d (NeZero.ne _)

protected theorem intCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : â„¤) (hd : 0 < d) :
    PosDef (d : Matrix n n R) :=
  âŸ¨isHermitian_intCast _, fun x hx => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hdâŸ©

@[simp]
theorem _root_.Matrix.posDef_intCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : â„¤} :
    PosDef (d : Matrix n n R) â†” 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected lemma add_posSemidef [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosSemidef) : (A + B).PosDef :=
  âŸ¨hA.isHermitian.add hB.isHermitian, fun x hx => by
    rw [add_mulVec, dotProduct_add]
    exact add_pos_of_pos_of_nonneg (hA.2 x hx) (hB.2 x)âŸ©

protected lemma posSemidef_add [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosDef) : (A + B).PosDef :=
  add_comm A B â–¸ hB.add_posSemidef hA

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosDef) : (A + B).PosDef :=
  hA.add_posSemidef hB.posSemidef

lemma conjTranspose_mul_mul_same {A : Matrix n n R} {B : Matrix n m R} (hA : A.PosDef)
    (hB : Function.Injective B.mulVec) :
    (Bá´´ * A * B).PosDef := by
  refine âŸ¨Matrix.isHermitian_conjTranspose_mul_mul _ hA.1, fun x hx => ?_âŸ©
  have : B *áµ¥ x â‰  0 := fun h => hx <| hB.eq_iff' (mulVec_zero _) |>.1 h
  simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 _ this

lemma mul_mul_conjTranspose_same {A : Matrix n n R} {B : Matrix m n R} (hA : A.PosDef)
    (hB : Function.Injective B.vecMul) :
    (B * A * Bá´´).PosDef := by
  replace hB := star_injective.comp <| hB.comp star_injective
  simp_rw [Function.comp_def, star_vecMul, star_star] at hB
  simpa using hA.conjTranspose_mul_mul_same (B := Bá´´) hB

theorem conjTranspose_mul_self [StarOrderedRing R] [NoZeroDivisors R] (A : Matrix m n R)
    (hA : Function.Injective A.mulVec) :
    PosDef (Aá´´ * A) := by
  classical
  simpa using conjTranspose_mul_mul_same .one hA

theorem conjTranspose {M : Matrix n n R} (hM : M.PosDef) : Má´´.PosDef := hM.1.symm â–¸ hM

@[simp]
theorem _root_.Matrix.posDef_conjTranspose_iff {M : Matrix n n R} : Má´´.PosDef â†” M.PosDef :=
  âŸ¨(by simpa using Â·.conjTranspose), .conjTransposeâŸ©

theorem of_toQuadraticForm' [DecidableEq n] {M : Matrix n n â„} (hM : M.IsSymm)
    (hMq : M.toQuadraticMap'.PosDef) : M.PosDef := by
  refine âŸ¨hM, fun x hx => ?_âŸ©
  simp only [toQuadraticMap', QuadraticMap.PosDef, LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMapâ‚‚'_apply'] at hMq
  apply hMq x hx

theorem toQuadraticForm' [DecidableEq n] {M : Matrix n n â„} (hM : M.PosDef) :
    M.toQuadraticMap'.PosDef := by
  intro x hx
  simp only [Matrix.toQuadraticMap', LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMapâ‚‚'_apply']
  apply hM.2 x hx

/-- The eigenvalues of a positive definite matrix are positive -/
lemma eigenvalues_pos [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : Matrix.PosDef A) (i : n) : 0 < hA.1.eigenvalues i := by
  simp only [hA.1.eigenvalues_eq]
  exact hA.re_dotProduct_pos <| hA.1.eigenvectorBasis.orthonormal.ne_zero i

/-- A Hermitian matrix is positive-definite if and only if its eigenvalues are positive. -/
lemma _root_.Matrix.IsHermitian.posDef_iff_eigenvalues_pos [DecidableEq n] {A : Matrix n n ğ•œ}
    (hA : A.IsHermitian) : A.PosDef â†” âˆ€ i, 0 < hA.eigenvalues i := by
  refine âŸ¨fun h => h.eigenvalues_pos, fun h => ?_âŸ©
  rw [hA.spectral_theorem]
  refine (posDef_diagonal_iff.mpr <| by simpa using h).mul_mul_conjTranspose_same ?_
  rw [vecMul_injective_iff_isUnit, â† unitary.val_toUnits_apply]
  exact Units.isUnit _

theorem trace_pos [Nonempty n] {A : Matrix n n ğ•œ} (hA : A.PosDef) : 0 < A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, â† map_sum,
    Finset.sum_pos (fun _ _ => hA.eigenvalues_pos _)]

theorem det_pos [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosDef) : 0 < det M := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  apply Finset.prod_pos
  intro i _
  simpa using hM.eigenvalues_pos i

section Field
variable {K : Type*} [Field K] [PartialOrder K] [StarRing K]

theorem isUnit [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : IsUnit M := by
  by_contra h
  obtain âŸ¨a, ha, ha2âŸ© : âˆƒ a â‰  0, M *áµ¥ a = 0 := by
    obtain âŸ¨a, b, haâŸ© := Function.not_injective_iff.mp <| mulVec_injective_iff_isUnit.not.mpr h
    exact âŸ¨a - b, by simp [sub_eq_zero, ha, mulVec_sub]âŸ©
  simpa [ha2] using hM.2 _ ha

protected theorem inv [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : Mâ»Â¹.PosDef := by
  have := hM.mul_mul_conjTranspose_same (B := Mâ»Â¹) ?_
  Â· let _ := hM.isUnit.invertible
    simpa using this.conjTranspose
  Â· simp only [Matrix.vecMul_injective_iff_isUnit, isUnit_nonsing_inv_iff, hM.isUnit]

@[simp]
theorem _root_.Matrix.posDef_inv_iff [DecidableEq n] {M : Matrix n n K} :
    Mâ»Â¹.PosDef â†” M.PosDef :=
  âŸ¨fun h =>
    letI := (Matrix.isUnit_nonsing_inv_iff.1 <| h.isUnit).invertible
    Matrix.inv_inv_of_invertible M â–¸ h.inv, (Â·.inv)âŸ©

end Field

lemma posDef_sqrt [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    PosDef hM.posSemidef.sqrt := by
  apply PosDef.mul_mul_conjTranspose_same
  Â· rw [posDef_diagonal_iff]
    simpa using hM.eigenvalues_pos
  Â· apply Matrix.vecMul_injective_of_isUnit
    convert (Group.isUnit _).map (unitaryGroup n ğ•œ).subtype

/--
A matrix is positive definite if and only if it has the form `Bá´´ * B` for some invertible `B`.
-/
lemma _root_.Matrix.posDef_iff_eq_conjTranspose_mul_self [DecidableEq n] {A : Matrix n n ğ•œ} :
    PosDef A â†” âˆƒ B : Matrix n n ğ•œ, IsUnit B âˆ§ A = Bá´´ * B := by
  refine âŸ¨fun hA â†¦ âŸ¨_, hA.posDef_sqrt.isUnit, ?_âŸ©, fun âŸ¨B, hB, hAâŸ© â†¦ (hA â–¸ ?_)âŸ©
  Â· simp [hA.posDef_sqrt.isHermitian.eq]
  Â· exact conjTranspose_mul_self _ (mulVec_injective_of_isUnit hB)

@[deprecated (since := "07-08-2025")] alias posDef_iff_eq_conjTranspose_mul_self :=
  Matrix.posDef_iff_eq_conjTranspose_mul_self

/-- A positive semi-definite matrix is positive definite if and only if it is invertible. -/
@[grind =]
theorem _root_.Matrix.PosSemidef.posDef_iff_isUnit [DecidableEq n] {x : Matrix n n ğ•œ}
    (hx : x.PosSemidef) : x.PosDef â†” IsUnit x := by
  refine âŸ¨fun h => h.isUnit, fun h => âŸ¨hx.1, fun v hv => ?_âŸ©âŸ©
  obtain âŸ¨y, rflâŸ© := posSemidef_iff_eq_conjTranspose_mul_self.mp hx
  simp_rw [dotProduct_mulVec, â† vecMul_vecMul, â† star_mulVec, â† dotProduct_mulVec,
    dotProduct_star_self_pos_iff]
  contrapose! hv
  rw [â† map_eq_zero_iff (f := (yá´´ * y).mulVecLin) (mulVec_injective_iff_isUnit.mpr h),
    mulVecLin_apply, â† mulVec_mulVec, hv, mulVec_zero]

end PosDef

end Matrix

namespace QuadraticForm

open QuadraticMap

variable {n : Type*} [Fintype n]

theorem posDef_of_toMatrix' [DecidableEq n] {Q : QuadraticForm â„ (n â†’ â„)}
    (hQ : Q.toMatrix'.PosDef) : Q.PosDef := by
  rw [â† toQuadraticMap_associated â„ Q,
    â† (LinearMap.toMatrixâ‚‚' â„).left_inv ((associatedHom (R := â„) â„) Q)]
  exact hQ.toQuadraticForm'

theorem posDef_toMatrix' [DecidableEq n] {Q : QuadraticForm â„ (n â†’ â„)} (hQ : Q.PosDef) :
    Q.toMatrix'.PosDef := by
  rw [â† toQuadraticMap_associated â„ Q, â†
    (LinearMap.toMatrixâ‚‚' â„).left_inv ((associatedHom (R := â„) â„) Q)] at hQ
  exact .of_toQuadraticForm' (isSymm_toMatrix' Q) hQ

end QuadraticForm

namespace Matrix

variable {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]

/-- A positive definite matrix `M` induces a norm `â€–xâ€– = sqrt (re xá´´Mx)`. -/
noncomputable abbrev NormedAddCommGroup.ofMatrix {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    NormedAddCommGroup (n â†’ ğ•œ) :=
  @InnerProductSpace.Core.toNormedAddCommGroup _ _ _ _ _
    { inner := fun x y => (M *áµ¥ y) â¬áµ¥ star x
      conj_inner_symm := fun x y => by
        rw [dotProduct_comm, star_dotProduct, starRingEnd_apply, star_star,
          star_mulVec, dotProduct_comm (M *áµ¥ y), dotProduct_mulVec, hM.isHermitian.eq]
      re_inner_nonneg := fun x => by
        by_cases h : x = 0
        Â· simp [h]
        Â· exact (dotProduct_comm _ (M *áµ¥ x) â–¸ hM.re_dotProduct_pos h).le
      definite := fun x (hx : _ â¬áµ¥ _ = 0) => by
        by_contra! h
        simpa [hx, lt_irrefl, dotProduct_comm] using hM.re_dotProduct_pos h
      add_left := by simp only [star_add, dotProduct_add, forall_const]
      smul_left := fun x y r => by
        rw [â† smul_eq_mul, â† dotProduct_smul, starRingEnd_apply, â† star_smul] }

/-- A positive definite matrix `M` induces an inner product `âŸªx, yâŸ« = xá´´My`. -/
def InnerProductSpace.ofMatrix {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    @InnerProductSpace ğ•œ (n â†’ ğ•œ) _ (NormedAddCommGroup.ofMatrix hM).toSeminormedAddCommGroup :=
  InnerProductSpace.ofCore _

end Matrix
