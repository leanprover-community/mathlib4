/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Mohanad Ahmed
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.LinearAlgebra.Matrix.Spectrum
import Mathlib.LinearAlgebra.Matrix.Vec
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-! # Positive Definite Matrices

This file defines positive (semi)definite matrices and connects the notion to positive definiteness
of quadratic forms. Most results require `ùïú = ‚Ñù` or `‚ÑÇ`.

## Main definitions

* `Matrix.PosDef` : a matrix `M : Matrix n n ùïú` is positive definite if it is Hermitian and `x·¥¥Mx`
  is greater than zero for all nonzero `x`.
* `Matrix.PosSemidef` : a matrix `M : Matrix n n ùïú` is positive semidefinite if it is Hermitian
  and `x·¥¥Mx` is nonnegative for all `x`.

## Main results

* `Matrix.PosSemidef.fromBlocks‚ÇÅ‚ÇÅ` and `Matrix.PosSemidef.fromBlocks‚ÇÇ‚ÇÇ`: If a matrix `A` is
  positive definite, then `[A B; B·¥¥ D]` is positive semidefinite if and only if `D - B·¥¥ A‚Åª¬π B` is
  positive semidefinite.
-/

open scoped ComplexOrder

namespace Matrix

variable {m n R R' ùïú : Type*}
variable [Fintype m] [Fintype n]
variable [Ring R] [PartialOrder R] [StarRing R]
variable [CommRing R'] [PartialOrder R'] [StarRing R']
variable [RCLike ùïú]
open scoped Matrix

/-!
## Positive semidefinite matrices
-/

/-- A matrix `M : Matrix n n R` is positive semidefinite if it is Hermitian and `x·¥¥ * M * x` is
nonnegative for all `x`. -/
def PosSemidef (M : Matrix n n R) :=
  M.IsHermitian ‚àß ‚àÄ x : n ‚Üí R, 0 ‚â§ star x ‚¨ù·µ• (M *·µ• x)

protected theorem PosSemidef.diagonal [StarOrderedRing R] [DecidableEq n] {d : n ‚Üí R} (h : 0 ‚â§ d) :
    PosSemidef (diagonal d) :=
  ‚ü®isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i),
    fun x => by
      refine Fintype.sum_nonneg fun i => ?_
      simpa only [mulVec_diagonal, ‚Üê mul_assoc] using conjugate_nonneg (h i) _‚ü©

/-- A diagonal matrix is positive semidefinite iff its diagonal entries are nonnegative. -/
lemma posSemidef_diagonal_iff [StarOrderedRing R] [DecidableEq n] {d : n ‚Üí R} :
    PosSemidef (diagonal d) ‚Üî (‚àÄ i : n, 0 ‚â§ d i) :=
  ‚ü®fun ‚ü®_, hP‚ü© i ‚Ü¶ by simpa using hP (Pi.single i 1), .diagonal‚ü©

namespace PosSemidef

theorem isHermitian {M : Matrix n n R} (hM : M.PosSemidef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_nonneg {M : Matrix n n ùïú} (hM : M.PosSemidef) (x : n ‚Üí ùïú) :
    0 ‚â§ RCLike.re (star x ‚¨ù·µ• (M *·µ• x)) :=
  RCLike.nonneg_iff.mp (hM.2 _) |>.1

lemma conjTranspose_mul_mul_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix n m R) :
    PosSemidef (B·¥¥ * A * B) := by
  constructor
  ¬∑ exact isHermitian_conjTranspose_mul_mul B hA.1
  ¬∑ intro x
    simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 (B *·µ• x)

lemma mul_mul_conjTranspose_same {A : Matrix n n R} (hA : PosSemidef A)
    {m : Type*} [Fintype m] (B : Matrix m n R) :
    PosSemidef (B * A * B·¥¥) := by
  simpa only [conjTranspose_conjTranspose] using hA.conjTranspose_mul_mul_same B·¥¥

theorem submatrix {M : Matrix n n R} (hM : M.PosSemidef) (e : m ‚Üí n) :
    (M.submatrix e e).PosSemidef := by
  classical
  rw [(by simp : M = 1 * M * 1), submatrix_mul (he‚ÇÇ := Function.bijective_id),
    submatrix_mul (he‚ÇÇ := Function.bijective_id), submatrix_id_id]
  simpa only [conjTranspose_submatrix, conjTranspose_one] using
    conjTranspose_mul_mul_same hM (Matrix.submatrix 1 id e)

theorem transpose {M : Matrix n n R'} (hM : M.PosSemidef) : M·µÄ.PosSemidef := by
  refine ‚ü®IsHermitian.transpose hM.1, fun x => ?_‚ü©
  convert hM.2 (star x) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem _root_.Matrix.posSemidef_transpose_iff {M : Matrix n n R'} : M·µÄ.PosSemidef ‚Üî M.PosSemidef :=
  ‚ü®(by simpa using ¬∑.transpose), .transpose‚ü©

theorem conjTranspose {M : Matrix n n R} (hM : M.PosSemidef) : M·¥¥.PosSemidef := hM.1.symm ‚ñ∏ hM

@[simp]
theorem _root_.Matrix.posSemidef_conjTranspose_iff {M : Matrix n n R} :
    M·¥¥.PosSemidef ‚Üî M.PosSemidef :=
  ‚ü®(by simpa using ¬∑.conjTranspose), .conjTranspose‚ü©

protected lemma zero : PosSemidef (0 : Matrix n n R) :=
  ‚ü®isHermitian_zero, by simp‚ü©

protected lemma one [StarOrderedRing R] [DecidableEq n] : PosSemidef (1 : Matrix n n R) :=
  ‚ü®isHermitian_one, fun x => by
    rw [one_mulVec]; exact Fintype.sum_nonneg fun i => star_mul_self_nonneg _‚ü©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] (d : ‚Ñï) :
    PosSemidef (d : Matrix n n R) :=
  ‚ü®isHermitian_natCast _, fun x => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_nonneg (dotProduct_star_self_nonneg _) _‚ü©

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] (d : ‚Ñï) [d.AtLeastTwo] :
    PosSemidef (ofNat(d) : Matrix n n R) :=
  .natCast d

protected theorem intCast [StarOrderedRing R] [DecidableEq n] (d : ‚Ñ§) (hd : 0 ‚â§ d) :
    PosSemidef (d : Matrix n n R) :=
  ‚ü®isHermitian_intCast _, fun x => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_nonneg (dotProduct_star_self_nonneg _) hd‚ü©

@[simp]
protected theorem _root_.Matrix.posSemidef_intCast_iff
    [StarOrderedRing R] [DecidableEq n] [Nonempty n] [Nontrivial R] (d : ‚Ñ§) :
    PosSemidef (d : Matrix n n R) ‚Üî 0 ‚â§ d :=
  posSemidef_diagonal_iff.trans <| by simp

protected lemma pow [StarOrderedRing R] [DecidableEq n]
    {M : Matrix n n R} (hM : M.PosSemidef) (k : ‚Ñï) :
    PosSemidef (M ^ k) :=
  match k with
  | 0 => .one
  | 1 => by simpa using hM
  | (k + 2) => by
    rw [pow_succ, pow_succ']
    simpa only [hM.isHermitian.eq] using (hM.pow k).mul_mul_conjTranspose_same M

protected lemma inv [DecidableEq n] {M : Matrix n n R'} (hM : M.PosSemidef) : M‚Åª¬π.PosSemidef := by
  by_cases h : IsUnit M.det
  ¬∑ have := (conjTranspose_mul_mul_same hM M‚Åª¬π).conjTranspose
    rwa [mul_nonsing_inv_cancel_right _ _ h, conjTranspose_conjTranspose] at this
  ¬∑ rw [nonsing_inv_apply_not_isUnit _ h]
    exact .zero

protected lemma zpow [StarOrderedRing R'] [DecidableEq n]
    {M : Matrix n n R'} (hM : M.PosSemidef) (z : ‚Ñ§) :
    (M ^ z).PosSemidef := by
  obtain ‚ü®n, rfl | rfl‚ü© := z.eq_nat_or_neg
  ¬∑ simpa using hM.pow n
  ¬∑ simpa using (hM.pow n).inv

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosSemidef) : (A + B).PosSemidef :=
  ‚ü®hA.isHermitian.add hB.isHermitian, fun x => by
    rw [add_mulVec, dotProduct_add]
    exact add_nonneg (hA.2 x) (hB.2 x)‚ü©

protected theorem smul {Œ± : Type*} [CommSemiring Œ±] [PartialOrder Œ±] [StarRing Œ±]
    [StarOrderedRing Œ±] [Algebra Œ± R] [StarModule Œ± R] [PosSMulMono Œ± R] {x : Matrix n n R}
    (hx : x.PosSemidef) {a : Œ±} (ha : 0 ‚â§ a) : (a ‚Ä¢ x).PosSemidef := by
  refine ‚ü®IsSelfAdjoint.smul (IsSelfAdjoint.of_nonneg ha) hx.1, fun y => ?_‚ü©
  simp only [smul_mulVec, dotProduct_smul]
  exact smul_nonneg ha (hx.2 _)

/-- The eigenvalues of a positive semi-definite matrix are non-negative -/
lemma eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ùïú}
    (hA : Matrix.PosSemidef A) (i : n) : 0 ‚â§ hA.1.eigenvalues i :=
  (hA.re_dotProduct_nonneg _).trans_eq (hA.1.eigenvalues_eq _).symm

theorem trace_nonneg {A : Matrix n n ùïú} (hA : A.PosSemidef) : 0 ‚â§ A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, ‚Üê map_sum,
    Finset.sum_nonneg (fun _ _ => hA.eigenvalues_nonneg _)]

theorem det_nonneg [DecidableEq n] {M : Matrix n n ùïú} (hM : M.PosSemidef) :
    0 ‚â§ M.det := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  exact Finset.prod_nonneg fun i _ ‚Ü¶ by simpa using hM.eigenvalues_nonneg i

end PosSemidef

@[simp]
theorem posSemidef_submatrix_equiv {M : Matrix n n R} (e : m ‚âÉ n) :
    (M.submatrix e e).PosSemidef ‚Üî M.PosSemidef :=
  ‚ü®fun h => by simpa using h.submatrix e.symm, fun h => h.submatrix _‚ü©

/-- The conjugate transpose of a matrix multiplied by the matrix is positive semidefinite -/
theorem posSemidef_conjTranspose_mul_self [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (A·¥¥ * A) := by
  refine ‚ü®isHermitian_transpose_mul_self _, fun x => ?_‚ü©
  rw [‚Üê mulVec_mulVec, dotProduct_mulVec, vecMul_conjTranspose, star_star]
  exact Finset.sum_nonneg fun i _ => star_mul_self_nonneg _

/-- A matrix multiplied by its conjugate transpose is positive semidefinite -/
theorem posSemidef_self_mul_conjTranspose [StarOrderedRing R] (A : Matrix m n R) :
    PosSemidef (A * A·¥¥) := by
  simpa only [conjTranspose_conjTranspose] using posSemidef_conjTranspose_mul_self A·¥¥

section trace
-- TODO: move these results to an earlier file

variable {R : Type*} [PartialOrder R] [NonUnitalRing R]
  [StarRing R] [StarOrderedRing R] [NoZeroDivisors R]

theorem trace_conjTranspose_mul_self_eq_zero_iff {A : Matrix m n R} :
    (A·¥¥ * A).trace = 0 ‚Üî A = 0 := by
  rw [‚Üê star_vec_dotProduct_vec, dotProduct_star_self_eq_zero, vec_eq_zero_iff]

theorem trace_mul_conjTranspose_self_eq_zero_iff {A : Matrix m n R} :
    (A * A·¥¥).trace = 0 ‚Üî A = 0 := by
  simpa using trace_conjTranspose_mul_self_eq_zero_iff (A := A·¥¥)

end trace

lemma eigenvalues_conjTranspose_mul_self_nonneg (A : Matrix m n ùïú) [DecidableEq n] (i : n) :
    0 ‚â§ (isHermitian_transpose_mul_self A).eigenvalues i :=
  (posSemidef_conjTranspose_mul_self _).eigenvalues_nonneg _

lemma eigenvalues_self_mul_conjTranspose_nonneg (A : Matrix m n ùïú) [DecidableEq m] (i : m) :
    0 ‚â§ (isHermitian_mul_conjTranspose_self A).eigenvalues i :=
  (posSemidef_self_mul_conjTranspose _).eigenvalues_nonneg _

/-- A Hermitian matrix is positive semi-definite if and only if its eigenvalues are non-negative. -/
lemma IsHermitian.posSemidef_iff_eigenvalues_nonneg [DecidableEq n] {A : Matrix n n ùïú}
    (hA : IsHermitian A) : PosSemidef A ‚Üî 0 ‚â§ hA.eigenvalues := by
  refine ‚ü®fun h => h.eigenvalues_nonneg, fun h => ?_‚ü©
  rw [hA.spectral_theorem]
  refine (posSemidef_diagonal_iff.mpr ?_).mul_mul_conjTranspose_same _
  simpa using h

@[deprecated (since := "2025-08-17")] alias ‚ü®_, IsHermitian.posSemidef_of_eigenvalues_nonneg‚ü© :=
  IsHermitian.posSemidef_iff_eigenvalues_nonneg

theorem PosSemidef.trace_eq_zero_iff {A : Matrix n n ùïú} (hA : A.PosSemidef) :
    A.trace = 0 ‚Üî A = 0 := by
  refine ‚ü®fun h => ?_, fun h => h ‚ñ∏ trace_zero n ùïú‚ü©
  classical
  simp_rw [hA.isHermitian.trace_eq_sum_eigenvalues, ‚Üê RCLike.ofReal_sum,
    RCLike.ofReal_eq_zero, Finset.sum_eq_zero_iff_of_nonneg (s := Finset.univ)
      (by simpa using hA.eigenvalues_nonneg), Finset.mem_univ, true_imp_iff] at h
  exact funext_iff.eq ‚ñ∏ hA.isHermitian.eigenvalues_eq_zero_iff.mp <| h

/-!
## Positive definite matrices
-/

/-- A matrix `M : Matrix n n R` is positive definite if it is Hermitian
and `x·¥¥Mx` is greater than zero for all nonzero `x`. -/
def PosDef (M : Matrix n n R) :=
  M.IsHermitian ‚àß ‚àÄ x : n ‚Üí R, x ‚â† 0 ‚Üí 0 < star x ‚¨ù·µ• (M *·µ• x)

namespace PosDef

theorem isHermitian {M : Matrix n n R} (hM : M.PosDef) : M.IsHermitian :=
  hM.1

theorem re_dotProduct_pos {M : Matrix n n ùïú} (hM : M.PosDef) {x : n ‚Üí ùïú} (hx : x ‚â† 0) :
    0 < RCLike.re (star x ‚¨ù·µ• (M *·µ• x)) :=
  RCLike.pos_iff.mp (hM.2 _ hx) |>.1

theorem posSemidef {M : Matrix n n R} (hM : M.PosDef) : M.PosSemidef := by
  refine ‚ü®hM.1, ?_‚ü©
  intro x
  by_cases hx : x = 0
  ¬∑ simp only [hx, zero_dotProduct, star_zero]
    exact le_rfl
  ¬∑ exact le_of_lt (hM.2 x hx)

theorem transpose {M : Matrix n n R'} (hM : M.PosDef) : M·µÄ.PosDef := by
  refine ‚ü®IsHermitian.transpose hM.1, fun x hx => ?_‚ü©
  convert hM.2 (star x) (star_ne_zero.2 hx) using 1
  rw [mulVec_transpose, dotProduct_mulVec, star_star, dotProduct_comm]

@[simp]
theorem transpose_iff {M : Matrix n n R'} : M·µÄ.PosDef ‚Üî M.PosDef :=
  ‚ü®(by simpa using ¬∑.transpose), .transpose‚ü©

protected theorem diagonal [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    {d : n ‚Üí R} (h : ‚àÄ i, 0 < d i) :
    PosDef (diagonal d) :=
  ‚ü®isHermitian_diagonal_of_self_adjoint _ <| funext fun i => IsSelfAdjoint.of_nonneg (h i).le,
    fun x hx => by
      refine Fintype.sum_pos ?_
      simp_rw [mulVec_diagonal, ‚Üê mul_assoc, Pi.lt_def]
      obtain ‚ü®i, hi‚ü© := Function.ne_iff.mp hx
      exact ‚ü®fun i => conjugate_nonneg (h i).le _,
        i, conjugate_pos (h _) (isRegular_of_ne_zero hi)‚ü©‚ü©

@[simp]
theorem _root_.Matrix.posDef_diagonal_iff
    [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] [Nontrivial R] {d : n ‚Üí R} :
    PosDef (diagonal d) ‚Üî ‚àÄ i, 0 < d i := by
  refine ‚ü®fun h i => ?_, .diagonal‚ü©
  have := h.2 (Pi.single i 1)
  simp_rw [mulVec_single_one, ‚Üê Pi.single_star, star_one, single_dotProduct, one_mul,
    col_apply, diagonal_apply_eq, Function.ne_iff] at this
  exact this ‚ü®i, by simp‚ü©

protected theorem one [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R] :
    PosDef (1 : Matrix n n R) :=
  ‚ü®isHermitian_one, fun x hx => by simpa only [one_mulVec, dotProduct_star_self_pos_iff]‚ü©

protected theorem natCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : ‚Ñï) (hd : d ‚â† 0) :
    PosDef (d : Matrix n n R) :=
  ‚ü®isHermitian_natCast _, fun x hx => by
    rw [natCast_mulVec, Nat.cast_smul_eq_nsmul, dotProduct_smul]
    exact nsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hd‚ü©

@[simp]
theorem _root_.Matrix.posDef_natCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : ‚Ñï} :
    PosDef (d : Matrix n n R) ‚Üî 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected theorem ofNat [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : ‚Ñï) [d.AtLeastTwo] :
    PosDef (ofNat(d) : Matrix n n R) :=
  .natCast d (NeZero.ne _)

protected theorem intCast [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    (d : ‚Ñ§) (hd : 0 < d) :
    PosDef (d : Matrix n n R) :=
  ‚ü®isHermitian_intCast _, fun x hx => by
    rw [intCast_mulVec, Int.cast_smul_eq_zsmul, dotProduct_smul]
    exact zsmul_pos (dotProduct_star_self_pos_iff.mpr hx) hd‚ü©

@[simp]
theorem _root_.Matrix.posDef_intCast_iff [StarOrderedRing R] [DecidableEq n] [NoZeroDivisors R]
    [Nonempty n] [Nontrivial R] {d : ‚Ñ§} :
    PosDef (d : Matrix n n R) ‚Üî 0 < d :=
  posDef_diagonal_iff.trans <| by simp

protected lemma add_posSemidef [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosSemidef) : (A + B).PosDef :=
  ‚ü®hA.isHermitian.add hB.isHermitian, fun x hx => by
    rw [add_mulVec, dotProduct_add]
    exact add_pos_of_pos_of_nonneg (hA.2 x hx) (hB.2 x)‚ü©

protected lemma posSemidef_add [AddLeftMono R]
    {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosSemidef) (hB : B.PosDef) : (A + B).PosDef :=
  add_comm A B ‚ñ∏ hB.add_posSemidef hA

protected lemma add [AddLeftMono R] {A : Matrix m m R} {B : Matrix m m R}
    (hA : A.PosDef) (hB : B.PosDef) : (A + B).PosDef :=
  hA.add_posSemidef hB.posSemidef

protected theorem smul {Œ± : Type*} [CommSemiring Œ±] [PartialOrder Œ±] [StarRing Œ±]
    [StarOrderedRing Œ±] [Algebra Œ± R] [StarModule Œ± R] [PosSMulStrictMono Œ± R]
    {x : Matrix n n R} (hx : x.PosDef) {a : Œ±} (ha : 0 < a) : (a ‚Ä¢ x).PosDef := by
  refine ‚ü®IsSelfAdjoint.smul (IsSelfAdjoint.of_nonneg ha.le) hx.1, fun y hy => ?_‚ü©
  simp only [smul_mulVec, dotProduct_smul]
  exact smul_pos ha (hx.2 _ hy)

lemma conjTranspose_mul_mul_same {A : Matrix n n R} {B : Matrix n m R} (hA : A.PosDef)
    (hB : Function.Injective B.mulVec) :
    (B·¥¥ * A * B).PosDef := by
  refine ‚ü®Matrix.isHermitian_conjTranspose_mul_mul _ hA.1, fun x hx => ?_‚ü©
  have : B *·µ• x ‚â† 0 := fun h => hx <| hB.eq_iff' (mulVec_zero _) |>.1 h
  simpa only [star_mulVec, dotProduct_mulVec, vecMul_vecMul] using hA.2 _ this

lemma mul_mul_conjTranspose_same {A : Matrix n n R} {B : Matrix m n R} (hA : A.PosDef)
    (hB : Function.Injective B.vecMul) :
    (B * A * B·¥¥).PosDef := by
  replace hB := star_injective.comp <| hB.comp star_injective
  simp_rw [Function.comp_def, star_vecMul, star_star] at hB
  simpa using hA.conjTranspose_mul_mul_same (B := B·¥¥) hB

theorem conjTranspose_mul_self [StarOrderedRing R] [NoZeroDivisors R] (A : Matrix m n R)
    (hA : Function.Injective A.mulVec) :
    PosDef (A·¥¥ * A) := by
  classical
  simpa using conjTranspose_mul_mul_same .one hA

theorem conjTranspose {M : Matrix n n R} (hM : M.PosDef) : M·¥¥.PosDef := hM.1.symm ‚ñ∏ hM

@[simp]
theorem _root_.Matrix.posDef_conjTranspose_iff {M : Matrix n n R} : M·¥¥.PosDef ‚Üî M.PosDef :=
  ‚ü®(by simpa using ¬∑.conjTranspose), .conjTranspose‚ü©

theorem of_toQuadraticForm' [DecidableEq n] {M : Matrix n n ‚Ñù} (hM : M.IsSymm)
    (hMq : M.toQuadraticMap'.PosDef) : M.PosDef := by
  refine ‚ü®hM, fun x hx => ?_‚ü©
  simp only [toQuadraticMap', QuadraticMap.PosDef, LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMap‚ÇÇ'_apply'] at hMq
  apply hMq x hx

theorem toQuadraticForm' [DecidableEq n] {M : Matrix n n ‚Ñù} (hM : M.PosDef) :
    M.toQuadraticMap'.PosDef := by
  intro x hx
  simp only [Matrix.toQuadraticMap', LinearMap.BilinMap.toQuadraticMap_apply,
    toLinearMap‚ÇÇ'_apply']
  apply hM.2 x hx

/-- The eigenvalues of a positive definite matrix are positive -/
lemma eigenvalues_pos [DecidableEq n] {A : Matrix n n ùïú}
    (hA : Matrix.PosDef A) (i : n) : 0 < hA.1.eigenvalues i := by
  simp only [hA.1.eigenvalues_eq]
  exact hA.re_dotProduct_pos <| hA.1.eigenvectorBasis.orthonormal.ne_zero i

/-- A Hermitian matrix is positive-definite if and only if its eigenvalues are positive. -/
lemma _root_.Matrix.IsHermitian.posDef_iff_eigenvalues_pos [DecidableEq n] {A : Matrix n n ùïú}
    (hA : A.IsHermitian) : A.PosDef ‚Üî ‚àÄ i, 0 < hA.eigenvalues i := by
  refine ‚ü®fun h => h.eigenvalues_pos, fun h => ?_‚ü©
  rw [hA.spectral_theorem]
  refine (posDef_diagonal_iff.mpr <| by simpa using h).mul_mul_conjTranspose_same ?_
  rw [vecMul_injective_iff_isUnit, ‚Üê unitary.val_toUnits_apply]
  exact Units.isUnit _

theorem trace_pos [Nonempty n] {A : Matrix n n ùïú} (hA : A.PosDef) : 0 < A.trace := by
  classical
  simp [hA.isHermitian.trace_eq_sum_eigenvalues, ‚Üê map_sum,
    Finset.sum_pos (fun _ _ => hA.eigenvalues_pos _)]

theorem det_pos [DecidableEq n] {M : Matrix n n ùïú} (hM : M.PosDef) : 0 < det M := by
  rw [hM.isHermitian.det_eq_prod_eigenvalues]
  apply Finset.prod_pos
  intro i _
  simpa using hM.eigenvalues_pos i

section Field
variable {K : Type*} [Field K] [PartialOrder K] [StarRing K]

theorem isUnit [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : IsUnit M := by
  by_contra h
  obtain ‚ü®a, ha, ha2‚ü© : ‚àÉ a ‚â† 0, M *·µ• a = 0 := by
    obtain ‚ü®a, b, ha‚ü© := Function.not_injective_iff.mp <| mulVec_injective_iff_isUnit.not.mpr h
    exact ‚ü®a - b, by simp [sub_eq_zero, ha, mulVec_sub]‚ü©
  simpa [ha2] using hM.2 _ ha

protected theorem inv [DecidableEq n] {M : Matrix n n K} (hM : M.PosDef) : M‚Åª¬π.PosDef := by
  have := hM.mul_mul_conjTranspose_same (B := M‚Åª¬π) ?_
  ¬∑ let _ := hM.isUnit.invertible
    simpa using this.conjTranspose
  ¬∑ simp only [Matrix.vecMul_injective_iff_isUnit, isUnit_nonsing_inv_iff, hM.isUnit]

@[simp]
theorem _root_.Matrix.posDef_inv_iff [DecidableEq n] {M : Matrix n n K} :
    M‚Åª¬π.PosDef ‚Üî M.PosDef :=
  ‚ü®fun h =>
    letI := (Matrix.isUnit_nonsing_inv_iff.1 <| h.isUnit).invertible
    Matrix.inv_inv_of_invertible M ‚ñ∏ h.inv, (¬∑.inv)‚ü©

end Field

section SchurComplement

variable [StarOrderedRing R']

theorem fromBlocks‚ÇÅ‚ÇÅ [DecidableEq m] {A : Matrix m m R'}
    (B : Matrix m n R') (D : Matrix n n R') (hA : A.PosDef) [Invertible A] :
    (fromBlocks A B B·¥¥ D).PosSemidef ‚Üî (D - B·¥¥ * A‚Åª¬π * B).PosSemidef := by
  rw [PosSemidef, IsHermitian.fromBlocks‚ÇÅ‚ÇÅ _ _ hA.1]
  constructor
  ¬∑ refine fun h => ‚ü®h.1, fun x => ?_‚ü©
    have := h.2 (-((A‚Åª¬π * B) *·µ• x) ‚äï·µ• x)
    rwa [dotProduct_mulVec, schur_complement_eq‚ÇÅ‚ÇÅ B D _ _ hA.1, neg_add_cancel, dotProduct_zero,
      zero_add, ‚Üê dotProduct_mulVec] at this
  ¬∑ refine fun h => ‚ü®h.1, fun x => ?_‚ü©
    rw [dotProduct_mulVec, ‚Üê Sum.elim_comp_inl_inr x, schur_complement_eq‚ÇÅ‚ÇÅ B D _ _ hA.1]
    apply le_add_of_nonneg_of_le
    ¬∑ rw [‚Üê dotProduct_mulVec]
      apply hA.posSemidef.2
    ¬∑ rw [‚Üê dotProduct_mulVec (star (x ‚àò Sum.inr))]
      apply h.2

theorem fromBlocks‚ÇÇ‚ÇÇ [DecidableEq n] (A : Matrix m m R')
    (B : Matrix m n R') {D : Matrix n n R'} (hD : D.PosDef) [Invertible D] :
    (fromBlocks A B B·¥¥ D).PosSemidef ‚Üî (A - B * D‚Åª¬π * B·¥¥).PosSemidef := by
  rw [‚Üê posSemidef_submatrix_equiv (Equiv.sumComm n m), Equiv.sumComm_apply,
    fromBlocks_submatrix_sum_swap_sum_swap]
  convert fromBlocks‚ÇÅ‚ÇÅ B·¥¥ A hD <;> simp

end SchurComplement

end PosDef

end Matrix

namespace QuadraticForm

open QuadraticMap

variable {n : Type*} [Fintype n]

theorem posDef_of_toMatrix' [DecidableEq n] {Q : QuadraticForm ‚Ñù (n ‚Üí ‚Ñù)}
    (hQ : Q.toMatrix'.PosDef) : Q.PosDef := by
  rw [‚Üê toQuadraticMap_associated ‚Ñù Q,
    ‚Üê (LinearMap.toMatrix‚ÇÇ' ‚Ñù).left_inv ((associatedHom (R := ‚Ñù) ‚Ñù) Q)]
  exact hQ.toQuadraticForm'

theorem posDef_toMatrix' [DecidableEq n] {Q : QuadraticForm ‚Ñù (n ‚Üí ‚Ñù)} (hQ : Q.PosDef) :
    Q.toMatrix'.PosDef := by
  rw [‚Üê toQuadraticMap_associated ‚Ñù Q, ‚Üê
    (LinearMap.toMatrix‚ÇÇ' ‚Ñù).left_inv ((associatedHom (R := ‚Ñù) ‚Ñù) Q)] at hQ
  exact .of_toQuadraticForm' (isSymm_toMatrix' Q) hQ

end QuadraticForm

namespace Matrix
variable {ùïú : Type*} [RCLike ùïú] {n : Type*} [Fintype n]

/-- A positive definite matrix `M` induces a norm `‚Äñx‚Äñ = sqrt (re x·¥¥Mx)`. -/
noncomputable abbrev NormedAddCommGroup.ofMatrix {M : Matrix n n ùïú} (hM : M.PosDef) :
    NormedAddCommGroup (n ‚Üí ùïú) :=
  @InnerProductSpace.Core.toNormedAddCommGroup _ _ _ _ _
    { inner x y := (M *·µ• y) ‚¨ù·µ• star x
      conj_inner_symm x y := by
        rw [dotProduct_comm, star_dotProduct, starRingEnd_apply, star_star,
          star_mulVec, dotProduct_comm (M *·µ• y), dotProduct_mulVec, hM.isHermitian.eq]
      re_inner_nonneg x := dotProduct_comm _ (star x) ‚ñ∏ hM.posSemidef.re_dotProduct_nonneg x
      definite x (hx : _ ‚¨ù·µ• _ = 0) := by
        by_contra! h
        simpa [hx, lt_irrefl, dotProduct_comm] using hM.re_dotProduct_pos h
      add_left := by simp only [star_add, dotProduct_add, forall_const]
      smul_left _ _ _ := by rw [‚Üê smul_eq_mul, ‚Üê dotProduct_smul, starRingEnd_apply, ‚Üê star_smul] }

/-- A positive definite matrix `M` induces an inner product `‚ü™x, y‚ü´ = x·¥¥My`. -/
def InnerProductSpace.ofMatrix {M : Matrix n n ùïú} (hM : M.PosDef) :
    @InnerProductSpace ùïú (n ‚Üí ùïú) _ (NormedAddCommGroup.ofMatrix hM).toSeminormedAddCommGroup :=
  InnerProductSpace.ofCore _

end Matrix
