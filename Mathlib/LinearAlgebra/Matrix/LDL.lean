/-
Copyright (c) 2022 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.Analysis.InnerProductSpace.GramSchmidtOrtho
import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # LDL decomposition

This file proves the LDL-decomposition of matrices: Any positive definite matrix `S` can be
decomposed as `S = LDLá´´` where `L` is a lower-triangular matrix and `D` is a diagonal matrix.

## Main definitions

* `LDL.lower` is the lower triangular matrix `L`.
* `LDL.lowerInv` is the inverse of the lower triangular matrix `L`.
* `LDL.diag` is the diagonal matrix `D`.

## Main result

* `LDL.lower_conj_diag` states that any positive definite matrix can be decomposed as `LDLá´´`.

## TODO

* Prove that `LDL.lower` is lower triangular from `LDL.lowerInv_triangular`.

-/

open Module

variable {ğ•œ : Type*} [RCLike ğ•œ]
variable {n : Type*} [LinearOrder n] [WellFoundedLT n] [LocallyFiniteOrderBot n]

section set_options

set_option quotPrecheck false
local notation "âŸª" x ", " y "âŸ«â‚‘" => inner ğ•œ (WithLp.toLp 2 x) (WithLp.toLp 2 y)

open Matrix InnerProductSpace

open scoped ComplexOrder

variable {S : Matrix n n ğ•œ} [Fintype n] (hS : S.PosDef)

/-- The inverse of the lower triangular matrix `L` of the LDL-decomposition. It is obtained by
applying Gram-Schmidt-Orthogonalization w.r.t. the inner product induced by `Sáµ€` on the standard
basis vectors `Pi.basisFun`. -/
noncomputable def LDL.lowerInv : Matrix n n ğ•œ :=
  @gramSchmidt ğ•œ (n â†’ ğ•œ) _ (_ :) (InnerProductSpace.ofMatrix hS.transpose) n _ _ _
    (Pi.basisFun ğ•œ n)

theorem LDL.lowerInv_eq_gramSchmidtBasis :
    LDL.lowerInv hS =
      ((Pi.basisFun ğ•œ n).toMatrix
          (@gramSchmidtBasis ğ•œ (n â†’ ğ•œ) _ (_ :) (InnerProductSpace.ofMatrix hS.transpose) n _ _ _
            (Pi.basisFun ğ•œ n)))áµ€ := by
  letI := NormedAddCommGroup.ofMatrix hS.transpose
  letI := InnerProductSpace.ofMatrix hS.transpose
  ext i j
  rw [LDL.lowerInv, Basis.coePiBasisFun.toMatrix_eq_transpose, coe_gramSchmidtBasis]
  rfl

noncomputable instance LDL.invertibleLowerInv : Invertible (LDL.lowerInv hS) := by
  rw [LDL.lowerInv_eq_gramSchmidtBasis]
  haveI :=
    Basis.invertibleToMatrix (Pi.basisFun ğ•œ n)
      (@gramSchmidtBasis ğ•œ (n â†’ ğ•œ) _ (_ :) (InnerProductSpace.ofMatrix hS.transpose) n _ _ _
        (Pi.basisFun ğ•œ n))
  infer_instance

theorem LDL.lowerInv_orthogonal {i j : n} (hâ‚€ : i â‰  j) :
    âŸªLDL.lowerInv hS i, Sáµ€ *áµ¥ LDL.lowerInv hS jâŸ«â‚‘ = 0 :=
  @gramSchmidt_orthogonal ğ•œ _ _ (_ :) (InnerProductSpace.ofMatrix hS.transpose) _ _ _ _ _ _ _ hâ‚€

/-- The entries of the diagonal matrix `D` of the LDL decomposition. -/
noncomputable def LDL.diagEntries : n â†’ ğ•œ := fun i â†¦
  âŸªstar (LDL.lowerInv hS i), S *áµ¥ star (LDL.lowerInv hS i)âŸ«â‚‘

/-- The diagonal matrix `D` of the LDL decomposition. -/
noncomputable def LDL.diag : Matrix n n ğ•œ :=
  Matrix.diagonal (LDL.diagEntries hS)

theorem LDL.lowerInv_triangular {i j : n} (hij : i < j) : LDL.lowerInv hS i j = 0 := by
  rw [â†
    @gramSchmidt_triangular ğ•œ (n â†’ ğ•œ) _ (_ :) (InnerProductSpace.ofMatrix hS.transpose) n _ _ _
      i j hij (Pi.basisFun ğ•œ n),
    Pi.basisFun_repr, LDL.lowerInv]

/-- Inverse statement of **LDL decomposition**: we can conjugate a positive definite matrix
by some lower triangular matrix and get a diagonal matrix. -/
theorem LDL.diag_eq_lowerInv_conj : LDL.diag hS = LDL.lowerInv hS * S * (LDL.lowerInv hS)á´´ := by
  ext i j
  by_cases hij : i = j
  Â· simp only [diag, diagEntries, EuclideanSpace.inner_toLp_toLp, star_star, hij,
      diagonal_apply_eq, Matrix.mul_assoc, dotProduct_comm]
    rfl
  Â· simp only [LDL.diag, hij, diagonal_apply_ne, Ne, not_false_iff, mul_mul_apply]
    rw [conjTranspose, transpose_map, transpose_transpose, dotProduct_mulVec,
      (LDL.lowerInv_orthogonal hS fun h : j = i â†¦ hij h.symm).symm, â† inner_conj_symm,
      mulVec_transpose, EuclideanSpace.inner_toLp_toLp, â† RCLike.star_def, â†
      star_dotProduct_star, star_star]
    rfl

/-- The lower triangular matrix `L` of the LDL decomposition. -/
noncomputable def LDL.lower :=
  (LDL.lowerInv hS)â»Â¹

/-- **LDL decomposition**: any positive definite matrix `S` can be
decomposed as `S = LDLá´´` where `L` is a lower-triangular matrix and `D` is a diagonal matrix. -/
theorem LDL.lower_conj_diag : LDL.lower hS * LDL.diag hS * (LDL.lower hS)á´´ = S := by
  rw [LDL.lower, conjTranspose_nonsing_inv, Matrix.mul_assoc,
    Matrix.inv_mul_eq_iff_eq_mul_of_invertible (LDL.lowerInv hS),
    Matrix.mul_inv_eq_iff_eq_mul_of_invertible]
  exact LDL.diag_eq_lowerInv_conj hS

end set_options
