/-
Copyright (c) 2020 Adam Topaz. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhangir Azerbayev, Adam Topaz, Eric Wieser
-/
import Mathlib.LinearAlgebra.CliffordAlgebra.Basic
import Mathlib.LinearAlgebra.Alternating

#align_import linear_algebra.exterior_algebra.basic from "leanprover-community/mathlib"@"b8d2eaa69d69ce8f03179a5cda774fc0cde984e4"

/-!
# Exterior Algebras

We construct the exterior algebra of a module `M` over a commutative semiring `R`.

## Notation

The exterior algebra of the `R`-module `M` is denoted as `ExteriorAlgebra R M`.
It is endowed with the structure of an `R`-algebra.

Given a linear morphism `f : M ‚Üí A` from a module `M` to another `R`-algebra `A`, such that
`cond : ‚àÄ m : M, f m * f m = 0`, there is a (unique) lift of `f` to an `R`-algebra morphism,
which is denoted `ExteriorAlgebra.lift R f cond`.

The canonical linear map `M ‚Üí ExteriorAlgebra R M` is denoted `ExteriorAlgebra.Œπ R`.

## Theorems

The main theorems proved ensure that `ExteriorAlgebra R M` satisfies the universal property
of the exterior algebra.
1. `Œπ_comp_lift` is the fact that the composition of `Œπ R` with `lift R f cond` agrees with `f`.
2. `lift_unique` ensures the uniqueness of `lift R f cond` with respect to 1.

## Definitions

* `ŒπMulti` is the `AlternatingMap` corresponding to the wedge product of `Œπ R m` terms.

## Implementation details

The exterior algebra of `M` is constructed as simply `CliffordAlgebra (0 : QuadraticForm R M)`,
as this avoids us having to duplicate API.
-/


universe u1 u2 u3

variable (R : Type u1) [CommRing R]

variable (M : Type u2) [AddCommGroup M] [Module R M]

/-- The exterior algebra of an `R`-module `M`.
-/
@[reducible]
def ExteriorAlgebra :=
  CliffordAlgebra (0 : QuadraticForm R M)
#align exterior_algebra ExteriorAlgebra

namespace ExteriorAlgebra

variable {M}

/-- The canonical linear map `M ‚Üí‚Çó[R] ExteriorAlgebra R M`.
-/
@[reducible]
def Œπ : M ‚Üí‚Çó[R] ExteriorAlgebra R M :=
  CliffordAlgebra.Œπ _
#align exterior_algebra.Œπ ExteriorAlgebra.Œπ

variable {R}

/-- As well as being linear, `Œπ m` squares to zero. -/
-- @[simp] -- Porting note: simp can prove this
theorem Œπ_sq_zero (m : M) : Œπ R m * Œπ R m = 0 :=
  (CliffordAlgebra.Œπ_sq_scalar _ m).trans <| map_zero _
#align exterior_algebra.Œπ_sq_zero ExteriorAlgebra.Œπ_sq_zero

variable {A : Type*} [Semiring A] [Algebra R A]

-- @[simp] -- Porting note: simp can prove this
theorem comp_Œπ_sq_zero (g : ExteriorAlgebra R M ‚Üí‚Çê[R] A) (m : M) : g (Œπ R m) * g (Œπ R m) = 0 := by
  rw [‚Üê AlgHom.map_mul, Œπ_sq_zero, AlgHom.map_zero]
  -- üéâ no goals
#align exterior_algebra.comp_Œπ_sq_zero ExteriorAlgebra.comp_Œπ_sq_zero

variable (R)

/-- Given a linear map `f : M ‚Üí‚Çó[R] A` into an `R`-algebra `A`, which satisfies the condition:
`cond : ‚àÄ m : M, f m * f m = 0`, this is the canonical lift of `f` to a morphism of `R`-algebras
from `ExteriorAlgebra R M` to `A`.
-/
@[simps! symm_apply]
def lift : { f : M ‚Üí‚Çó[R] A // ‚àÄ m, f m * f m = 0 } ‚âÉ (ExteriorAlgebra R M ‚Üí‚Çê[R] A) :=
  Equiv.trans (Equiv.subtypeEquiv (Equiv.refl _) <| by simp) <| CliffordAlgebra.lift _
                                                       -- üéâ no goals
#align exterior_algebra.lift ExteriorAlgebra.lift

@[simp]
theorem Œπ_comp_lift (f : M ‚Üí‚Çó[R] A) (cond : ‚àÄ m, f m * f m = 0) :
    (lift R ‚ü®f, cond‚ü©).toLinearMap.comp (Œπ R) = f :=
  CliffordAlgebra.Œπ_comp_lift f _
#align exterior_algebra.Œπ_comp_lift ExteriorAlgebra.Œπ_comp_lift

@[simp]
theorem lift_Œπ_apply (f : M ‚Üí‚Çó[R] A) (cond : ‚àÄ m, f m * f m = 0) (x) :
    lift R ‚ü®f, cond‚ü© (Œπ R x) = f x :=
  CliffordAlgebra.lift_Œπ_apply f _ x
#align exterior_algebra.lift_Œπ_apply ExteriorAlgebra.lift_Œπ_apply

@[simp]
theorem lift_unique (f : M ‚Üí‚Çó[R] A) (cond : ‚àÄ m, f m * f m = 0) (g : ExteriorAlgebra R M ‚Üí‚Çê[R] A) :
    g.toLinearMap.comp (Œπ R) = f ‚Üî g = lift R ‚ü®f, cond‚ü© :=
  CliffordAlgebra.lift_unique f _ _
#align exterior_algebra.lift_unique ExteriorAlgebra.lift_unique

variable {R}

@[simp]
theorem lift_comp_Œπ (g : ExteriorAlgebra R M ‚Üí‚Çê[R] A) :
    lift R ‚ü®g.toLinearMap.comp (Œπ R), comp_Œπ_sq_zero _‚ü© = g :=
  CliffordAlgebra.lift_comp_Œπ g
#align exterior_algebra.lift_comp_Œπ ExteriorAlgebra.lift_comp_Œπ

/-- See note [partially-applied ext lemmas]. -/
@[ext]
theorem hom_ext {f g : ExteriorAlgebra R M ‚Üí‚Çê[R] A}
    (h : f.toLinearMap.comp (Œπ R) = g.toLinearMap.comp (Œπ R)) : f = g :=
  CliffordAlgebra.hom_ext h
#align exterior_algebra.hom_ext ExteriorAlgebra.hom_ext

/-- If `C` holds for the `algebraMap` of `r : R` into `ExteriorAlgebra R M`, the `Œπ` of `x : M`,
and is preserved under addition and muliplication, then it holds for all of `ExteriorAlgebra R M`.
-/
@[elab_as_elim]
theorem induction {C : ExteriorAlgebra R M ‚Üí Prop}
    (h_grade0 : ‚àÄ r, C (algebraMap R (ExteriorAlgebra R M) r)) (h_grade1 : ‚àÄ x, C (Œπ R x))
    (h_mul : ‚àÄ a b, C a ‚Üí C b ‚Üí C (a * b)) (h_add : ‚àÄ a b, C a ‚Üí C b ‚Üí C (a + b))
    (a : ExteriorAlgebra R M) : C a :=
  CliffordAlgebra.induction h_grade0 h_grade1 h_mul h_add a
#align exterior_algebra.induction ExteriorAlgebra.induction

/-- The left-inverse of `algebraMap`. -/
def algebraMapInv : ExteriorAlgebra R M ‚Üí‚Çê[R] R :=
  ExteriorAlgebra.lift R ‚ü®(0 : M ‚Üí‚Çó[R] R), fun m => by simp‚ü©
                                                       -- üéâ no goals
#align exterior_algebra.algebra_map_inv ExteriorAlgebra.algebraMapInv

variable (M)

theorem algebraMap_leftInverse :
    Function.LeftInverse algebraMapInv (algebraMap R <| ExteriorAlgebra R M) := fun x => by
  simp [algebraMapInv]
  -- üéâ no goals
#align exterior_algebra.algebra_map_left_inverse ExteriorAlgebra.algebraMap_leftInverse

@[simp]
theorem algebraMap_inj (x y : R) :
    algebraMap R (ExteriorAlgebra R M) x = algebraMap R (ExteriorAlgebra R M) y ‚Üî x = y :=
  (algebraMap_leftInverse M).injective.eq_iff
#align exterior_algebra.algebra_map_inj ExteriorAlgebra.algebraMap_inj

@[simp]
theorem algebraMap_eq_zero_iff (x : R) : algebraMap R (ExteriorAlgebra R M) x = 0 ‚Üî x = 0 :=
  map_eq_zero_iff (algebraMap _ _) (algebraMap_leftInverse _).injective
#align exterior_algebra.algebra_map_eq_zero_iff ExteriorAlgebra.algebraMap_eq_zero_iff

@[simp]
theorem algebraMap_eq_one_iff (x : R) : algebraMap R (ExteriorAlgebra R M) x = 1 ‚Üî x = 1 :=
  map_eq_one_iff (algebraMap _ _) (algebraMap_leftInverse _).injective
#align exterior_algebra.algebra_map_eq_one_iff ExteriorAlgebra.algebraMap_eq_one_iff

theorem isUnit_algebraMap (r : R) : IsUnit (algebraMap R (ExteriorAlgebra R M) r) ‚Üî IsUnit r :=
  isUnit_map_of_leftInverse _ (algebraMap_leftInverse M)
#align exterior_algebra.is_unit_algebra_map ExteriorAlgebra.isUnit_algebraMap

/-- Invertibility in the exterior algebra is the same as invertibility of the base ring. -/
@[simps!]
def invertibleAlgebraMapEquiv (r : R) :
    Invertible (algebraMap R (ExteriorAlgebra R M) r) ‚âÉ Invertible r :=
  invertibleEquivOfLeftInverse _ _ _ (algebraMap_leftInverse M)
#align exterior_algebra.invertible_algebra_map_equiv ExteriorAlgebra.invertibleAlgebraMapEquiv

variable {M}

/-- The canonical map from `ExteriorAlgebra R M` into `TrivSqZeroExt R M` that sends
`ExteriorAlgebra.Œπ` to `TrivSqZeroExt.inr`. -/
def toTrivSqZeroExt [Module R·µê·µí·µñ M] [IsCentralScalar R M] :
    ExteriorAlgebra R M ‚Üí‚Çê[R] TrivSqZeroExt R M :=
  lift R ‚ü®TrivSqZeroExt.inrHom R M, fun m => TrivSqZeroExt.inr_mul_inr R m m‚ü©
#align exterior_algebra.to_triv_sq_zero_ext ExteriorAlgebra.toTrivSqZeroExt

@[simp]
theorem toTrivSqZeroExt_Œπ [Module R·µê·µí·µñ M] [IsCentralScalar R M] (x : M) :
    toTrivSqZeroExt (Œπ R x) = TrivSqZeroExt.inr x :=
  lift_Œπ_apply _ _ _ _
#align exterior_algebra.to_triv_sq_zero_ext_Œπ ExteriorAlgebra.toTrivSqZeroExt_Œπ

/-- The left-inverse of `Œπ`.

As an implementation detail, we implement this using `TrivSqZeroExt` which has a suitable
algebra structure. -/
def ŒπInv : ExteriorAlgebra R M ‚Üí‚Çó[R] M := by
  letI : Module R·µê·µí·µñ M := Module.compHom _ ((RingHom.id R).fromOpposite mul_comm)
  -- ‚ä¢ ExteriorAlgebra R M ‚Üí‚Çó[R] M
  haveI : IsCentralScalar R M := ‚ü®fun r m => rfl‚ü©
  -- ‚ä¢ ExteriorAlgebra R M ‚Üí‚Çó[R] M
  exact (TrivSqZeroExt.sndHom R M).comp toTrivSqZeroExt.toLinearMap
  -- üéâ no goals
#align exterior_algebra.Œπ_inv ExteriorAlgebra.ŒπInv

-- Porting note: In the type, changed `ŒπInv` to `ŒπInv.1`
theorem Œπ_leftInverse : Function.LeftInverse ŒπInv.1 (Œπ R : M ‚Üí ExteriorAlgebra R M) := fun x => by
  -- Porting note: Original proof didn't have `letI` and `haveI`
  letI : Module R·µê·µí·µñ M := Module.compHom _ ((RingHom.id R).fromOpposite mul_comm)
  -- ‚ä¢ ‚ÜëŒπInv.toAddHom (‚Üë(Œπ R) x) = x
  haveI : IsCentralScalar R M := ‚ü®fun r m => rfl‚ü©
  -- ‚ä¢ ‚ÜëŒπInv.toAddHom (‚Üë(Œπ R) x) = x
  simp [ŒπInv]
  -- üéâ no goals
#align exterior_algebra.Œπ_left_inverse ExteriorAlgebra.Œπ_leftInverse

variable (R)

@[simp]
theorem Œπ_inj (x y : M) : Œπ R x = Œπ R y ‚Üî x = y :=
  Œπ_leftInverse.injective.eq_iff
#align exterior_algebra.Œπ_inj ExteriorAlgebra.Œπ_inj

variable {R}

@[simp]
theorem Œπ_eq_zero_iff (x : M) : Œπ R x = 0 ‚Üî x = 0 := by rw [‚Üê Œπ_inj R x 0, LinearMap.map_zero]
                                                        -- üéâ no goals
#align exterior_algebra.Œπ_eq_zero_iff ExteriorAlgebra.Œπ_eq_zero_iff

@[simp]
theorem Œπ_eq_algebraMap_iff (x : M) (r : R) : Œπ R x = algebraMap R _ r ‚Üî x = 0 ‚àß r = 0 := by
  refine' ‚ü®fun h => _, _‚ü©
  -- ‚ä¢ x = 0 ‚àß r = 0
  ¬∑ letI : Module R·µê·µí·µñ M := Module.compHom _ ((RingHom.id R).fromOpposite mul_comm)
    -- ‚ä¢ x = 0 ‚àß r = 0
    haveI : IsCentralScalar R M := ‚ü®fun r m => rfl‚ü©
    -- ‚ä¢ x = 0 ‚àß r = 0
    have hf0 : toTrivSqZeroExt (Œπ R x) = (0, x) := toTrivSqZeroExt_Œπ _
    -- ‚ä¢ x = 0 ‚àß r = 0
    rw [h, AlgHom.commutes] at hf0
    -- ‚ä¢ x = 0 ‚àß r = 0
    have : r = 0 ‚àß 0 = x := Prod.ext_iff.1 hf0
    -- ‚ä¢ x = 0 ‚àß r = 0
    exact this.symm.imp_left Eq.symm
    -- üéâ no goals
  ¬∑ rintro ‚ü®rfl, rfl‚ü©
    -- ‚ä¢ ‚Üë(Œπ R) 0 = ‚Üë(algebraMap R (ExteriorAlgebra R M)) 0
    rw [LinearMap.map_zero, RingHom.map_zero]
    -- üéâ no goals
#align exterior_algebra.Œπ_eq_algebra_map_iff ExteriorAlgebra.Œπ_eq_algebraMap_iff

@[simp]
theorem Œπ_ne_one [Nontrivial R] (x : M) : Œπ R x ‚â† 1 := by
  rw [‚Üê (algebraMap R (ExteriorAlgebra R M)).map_one, Ne.def, Œπ_eq_algebraMap_iff]
  -- ‚ä¢ ¬¨(x = 0 ‚àß 1 = 0)
  exact one_ne_zero ‚àò And.right
  -- üéâ no goals
#align exterior_algebra.Œπ_ne_one ExteriorAlgebra.Œπ_ne_one

/-- The generators of the exterior algebra are disjoint from its scalars. -/
theorem Œπ_range_disjoint_one :
    Disjoint (LinearMap.range (Œπ R : M ‚Üí‚Çó[R] ExteriorAlgebra R M))
      (1 : Submodule R (ExteriorAlgebra R M)) := by
  rw [Submodule.disjoint_def]
  -- ‚ä¢ ‚àÄ (x : ExteriorAlgebra R M), x ‚àà LinearMap.range (Œπ R) ‚Üí x ‚àà 1 ‚Üí x = 0
  rintro _ ‚ü®x, hx‚ü© ‚ü®r, rfl : algebraMap R (ExteriorAlgebra R M) r = _‚ü©
  -- ‚ä¢ ‚Üë(algebraMap R (ExteriorAlgebra R M)) r = 0
  rw [Œπ_eq_algebraMap_iff x] at hx
  -- ‚ä¢ ‚Üë(algebraMap R (ExteriorAlgebra R M)) r = 0
  rw [hx.2, RingHom.map_zero]
  -- üéâ no goals
#align exterior_algebra.Œπ_range_disjoint_one ExteriorAlgebra.Œπ_range_disjoint_one

@[simp]
theorem Œπ_add_mul_swap (x y : M) : Œπ R x * Œπ R y + Œπ R y * Œπ R x = 0 :=
  calc
    _ = Œπ R (y + x) * Œπ R (y + x) := by simp [mul_add, add_mul]
                                        -- üéâ no goals
    _ = _ := Œπ_sq_zero _
#align exterior_algebra.Œπ_add_mul_swap ExteriorAlgebra.Œπ_add_mul_swap

theorem Œπ_mul_prod_list {n : ‚Ñï} (f : Fin n ‚Üí M) (i : Fin n) :
    (Œπ R <| f i) * (List.ofFn fun i => Œπ R <| f i).prod = 0 := by
  induction' n with n hn
  -- ‚ä¢ ‚Üë(Œπ R) (f i) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
  ¬∑ exact i.elim0
    -- üéâ no goals
  ¬∑ rw [List.ofFn_succ, List.prod_cons, ‚Üê mul_assoc]
    -- ‚ä¢ ‚Üë(Œπ R) (f i) * ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.s ‚Ä¶
    by_cases h : i = 0
    -- ‚ä¢ ‚Üë(Œπ R) (f i) * ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.s ‚Ä¶
    ¬∑ rw [h, Œπ_sq_zero, zero_mul]
      -- üéâ no goals
    ¬∑ replace hn :=
        congr_arg
          ((¬∑ * ¬∑) <| Œπ R <| f 0) (hn (fun i => f <| Fin.succ i) (i.pred h))
      simp only at hn
      -- ‚ä¢ ‚Üë(Œπ R) (f i) * ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.s ‚Ä¶
      rw [Fin.succ_pred, ‚Üê mul_assoc, mul_zero] at hn
      -- ‚ä¢ ‚Üë(Œπ R) (f i) * ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.s ‚Ä¶
      refine' (eq_zero_iff_eq_zero_of_add_eq_zero _).mp hn
      -- ‚ä¢ ‚Üë(Œπ R) (f 0) * ‚Üë(Œπ R) (f i) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.s ‚Ä¶
      rw [‚Üê add_mul, Œπ_add_mul_swap, zero_mul]
      -- üéâ no goals
#align exterior_algebra.Œπ_mul_prod_list ExteriorAlgebra.Œπ_mul_prod_list

variable (R)

/-- The product of `n` terms of the form `Œπ R m` is an alternating map.

This is a special case of `MultilinearMap.mkPiAlgebraFin`, and the exterior algebra version of
`TensorAlgebra.tprod`. -/
def ŒπMulti (n : ‚Ñï) : AlternatingMap R M (ExteriorAlgebra R M) (Fin n) :=
  let F := (MultilinearMap.mkPiAlgebraFin R n (ExteriorAlgebra R M)).compLinearMap fun _ => Œπ R
  { F with
    map_eq_zero_of_eq' := fun f x y hfxy hxy => by
      dsimp
      -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
      clear F
      -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
      wlog h : x < y
      -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
      ¬∑ exact this R (A := A) n f y x hfxy.symm hxy.symm (hxy.lt_or_lt.resolve_left h)
        -- üéâ no goals
      clear hxy
      -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
      induction' n with n hn
      -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f i)) = 0
      ¬∑ exact x.elim0
        -- üéâ no goals
      ¬∑ rw [List.ofFn_succ, List.prod_cons]
        -- ‚ä¢ ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.succ i))) = 0
        by_cases hx : x = 0
        -- ‚ä¢ ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.succ i))) = 0
        -- one of the repeated terms is on the left
        ¬∑ rw [hx] at hfxy h
          -- ‚ä¢ ‚Üë(Œπ R) (f 0) * List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.succ i))) = 0
          rw [hfxy, ‚Üê Fin.succ_pred y (ne_of_lt h).symm]
          -- ‚ä¢ ‚Üë(Œπ R) (f (Fin.succ (Fin.pred y (_ : y ‚â† 0)))) * List.prod (List.ofFn fun i  ‚Ä¶
          exact Œπ_mul_prod_list (f ‚àò Fin.succ) _
          -- üéâ no goals
        -- ignore the left-most term and induct on the remaining ones, decrementing indices
        ¬∑ convert mul_zero (Œπ R (f 0))
          -- ‚ä¢ List.prod (List.ofFn fun i => ‚Üë(Œπ R) (f (Fin.succ i))) = 0
          refine'
            hn
              (fun i => f <| Fin.succ i) (x.pred hx)
              (y.pred (ne_of_lt <| lt_of_le_of_lt x.zero_le h).symm) _
              (Fin.pred_lt_pred_iff.mpr h)
          simp only [Fin.succ_pred]
          -- ‚ä¢ f x = f y
          exact hfxy
          -- üéâ no goals
    toFun := F }
#align exterior_algebra.Œπ_multi ExteriorAlgebra.ŒπMulti

variable {R}

theorem ŒπMulti_apply {n : ‚Ñï} (v : Fin n ‚Üí M) : ŒπMulti R n v = (List.ofFn fun i => Œπ R (v i)).prod :=
  rfl
#align exterior_algebra.Œπ_multi_apply ExteriorAlgebra.ŒπMulti_apply

@[simp]
theorem ŒπMulti_zero_apply (v : Fin 0 ‚Üí M) : ŒπMulti R 0 v = 1 :=
  rfl
#align exterior_algebra.Œπ_multi_zero_apply ExteriorAlgebra.ŒπMulti_zero_apply

@[simp]
theorem ŒπMulti_succ_apply {n : ‚Ñï} (v : Fin n.succ ‚Üí M) :
    ŒπMulti R _ v = Œπ R (v 0) * ŒπMulti R _ (Matrix.vecTail v) :=
  (congr_arg List.prod (List.ofFn_succ _)).trans List.prod_cons
#align exterior_algebra.Œπ_multi_succ_apply ExteriorAlgebra.ŒπMulti_succ_apply

theorem ŒπMulti_succ_curryLeft {n : ‚Ñï} (m : M) :
    (ŒπMulti R n.succ).curryLeft m = (LinearMap.mulLeft R (Œπ R m)).compAlternatingMap (ŒπMulti R n) :=
  AlternatingMap.ext fun v =>
    (ŒπMulti_succ_apply _).trans <| by
      simp_rw [Matrix.tail_cons]
      -- ‚ä¢ ‚Üë(Œπ R) (Matrix.vecCons m v 0) * ‚Üë(ŒπMulti R n) v = ‚Üë(‚Üë(LinearMap.compAlternat ‚Ä¶
      rfl
      -- üéâ no goals
#align exterior_algebra.Œπ_multi_succ_curry_left ExteriorAlgebra.ŒπMulti_succ_curryLeft

end ExteriorAlgebra

namespace TensorAlgebra

variable {R M}

/-- The canonical image of the `TensorAlgebra` in the `ExteriorAlgebra`, which maps
`TensorAlgebra.Œπ R x` to `ExteriorAlgebra.Œπ R x`. -/
def toExterior : TensorAlgebra R M ‚Üí‚Çê[R] ExteriorAlgebra R M :=
  TensorAlgebra.lift R (ExteriorAlgebra.Œπ R : M ‚Üí‚Çó[R] ExteriorAlgebra R M)
#align tensor_algebra.to_exterior TensorAlgebra.toExterior

@[simp]
theorem toExterior_Œπ (m : M) :
    TensorAlgebra.toExterior (TensorAlgebra.Œπ R m) = ExteriorAlgebra.Œπ R m := by
  simp [toExterior]
  -- üéâ no goals
#align tensor_algebra.to_exterior_Œπ TensorAlgebra.toExterior_Œπ

end TensorAlgebra
