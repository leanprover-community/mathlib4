/-
Copyright (c) 2020 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser, Utensil Song
-/
import Mathlib.Algebra.RingQuot
import Mathlib.LinearAlgebra.TensorAlgebra.Basic
import Mathlib.LinearAlgebra.QuadraticForm.IsometryEquiv

#align_import linear_algebra.clifford_algebra.basic from "leanprover-community/mathlib"@"d46774d43797f5d1f507a63a6e904f7a533ae74a"

/-!
# Clifford Algebras

We construct the Clifford algebra of a module `M` over a commutative ring `R`, equipped with
a quadratic form `Q`.

## Notation

The Clifford algebra of the `R`-module `M` equipped with a quadratic form `Q` is
an `R`-algebra denoted `CliffordAlgebra Q`.

Given a linear morphism `f : M â†’ A` from a module `M` to another `R`-algebra `A`, such that
`cond : âˆ€ m, f m * f m = algebraMap _ _ (Q m)`, there is a (unique) lift of `f` to an `R`-algebra
morphism from `CliffordAlgebra Q` to `A`, which is denoted `CliffordAlgebra.lift Q f cond`.

The canonical linear map `M â†’ CliffordAlgebra Q` is denoted `CliffordAlgebra.Î¹ Q`.

## Theorems

The main theorems proved ensure that `CliffordAlgebra Q` satisfies the universal property
of the Clifford algebra.
1. `Î¹_comp_lift` is the fact that the composition of `Î¹ Q` with `lift Q f cond` agrees with `f`.
2. `lift_unique` ensures the uniqueness of `lift Q f cond` with respect to 1.

## Implementation details

The Clifford algebra of `M` is constructed as a quotient of the tensor algebra, as follows.
1. We define a relation `CliffordAlgebra.Rel Q` on `TensorAlgebra R M`.
   This is the smallest relation which identifies squares of elements of `M` with `Q m`.
2. The Clifford algebra is the quotient of the tensor algebra by this relation.

This file is almost identical to `Mathlib/LinearAlgebra/ExteriorAlgebra/Basic.lean`.
-/


variable {R : Type*} [CommRing R]

variable {M : Type*} [AddCommGroup M] [Module R M]

variable (Q : QuadraticForm R M)

variable {n : â„•}

namespace CliffordAlgebra

open TensorAlgebra

/-- `Rel` relates each `Î¹ m * Î¹ m`, for `m : M`, with `Q m`.

The Clifford algebra of `M` is defined as the quotient modulo this relation.
-/
inductive Rel : TensorAlgebra R M â†’ TensorAlgebra R M â†’ Prop
  | of (m : M) : Rel (Î¹ R m * Î¹ R m) (algebraMap R _ (Q m))
#align clifford_algebra.rel CliffordAlgebra.Rel

end CliffordAlgebra

/-- The Clifford algebra of an `R`-module `M` equipped with a quadratic_form `Q`.
-/
def CliffordAlgebra :=
  RingQuot (CliffordAlgebra.Rel Q)
#align clifford_algebra CliffordAlgebra

namespace CliffordAlgebra

-- Porting note: Expanded `deriving Inhabited, Semiring, Algebra`
instance instInhabited : Inhabited (CliffordAlgebra Q) := RingQuot.instInhabitedRingQuot _
#align clifford_algebra.inhabited CliffordAlgebra.instInhabited
instance instRing : Ring (CliffordAlgebra Q) := RingQuot.instRing _
#align clifford_algebra.ring CliffordAlgebra.instRing

instance (priority := 900) instAlgebra' {R A M} [CommSemiring R] [AddCommGroup M] [CommRing A]
    [Algebra R A] [Module R M] [Module A M] (Q : QuadraticForm A M)
    [IsScalarTower R A M] :
    Algebra R (CliffordAlgebra Q) :=
  RingQuot.instAlgebraRingQuot _

-- verify there are no diamonds
example : (algebraNat : Algebra â„• (CliffordAlgebra Q)) = instAlgebra' _ := rfl
example : (algebraInt _ : Algebra â„¤ (CliffordAlgebra Q)) = instAlgebra' _ := rfl

-- shortcut instance, as the other instance is slow
instance instAlgebra : Algebra R (CliffordAlgebra Q) := instAlgebra' _
#align clifford_algebra.algebra CliffordAlgebra.instAlgebra

instance {R S A M} [CommSemiring R] [CommSemiring S] [AddCommGroup M] [CommRing A]
    [Algebra R A] [Algebra S A] [Module R M] [Module S M] [Module A M] (Q : QuadraticForm A M)
    [IsScalarTower R A M] [IsScalarTower S A M] [SMulCommClass R S A] :
    SMulCommClass R S (CliffordAlgebra Q) :=
  RingQuot.instSMulCommClassRingQuot _

instance {R S A M} [CommSemiring R] [CommSemiring S] [AddCommGroup M] [CommRing A]
    [SMul R S] [Algebra R A] [Algebra S A] [Module R M] [Module S M] [Module A M]
    [IsScalarTower R A M] [IsScalarTower S A M] [IsScalarTower R S A] (Q : QuadraticForm A M) :
    IsScalarTower R S (CliffordAlgebra Q) :=
  RingQuot.instIsScalarTowerRingQuot _

/-- The canonical linear map `M â†’â‚—[R] CliffordAlgebra Q`.
-/
def Î¹ : M â†’â‚—[R] CliffordAlgebra Q :=
  (RingQuot.mkAlgHom R _).toLinearMap.comp (TensorAlgebra.Î¹ R)
#align clifford_algebra.Î¹ CliffordAlgebra.Î¹

/-- As well as being linear, `Î¹ Q` squares to the quadratic form -/
@[simp]
theorem Î¹_sq_scalar (m : M) : Î¹ Q m * Î¹ Q m = algebraMap R _ (Q m) := by
  erw [â† AlgHom.map_mul, RingQuot.mkAlgHom_rel R (Rel.of m), AlgHom.commutes]
  -- âŠ¢ â†‘(algebraMap R (RingQuot (Rel Q))) (â†‘Q m) = â†‘(algebraMap R (CliffordAlgebra  â€¦
  rfl
  -- ðŸŽ‰ no goals
#align clifford_algebra.Î¹_sq_scalar CliffordAlgebra.Î¹_sq_scalar

variable {Q} {A : Type*} [Semiring A] [Algebra R A]

@[simp]
theorem comp_Î¹_sq_scalar (g : CliffordAlgebra Q â†’â‚[R] A) (m : M) :
    g (Î¹ Q m) * g (Î¹ Q m) = algebraMap _ _ (Q m) := by
  rw [â† AlgHom.map_mul, Î¹_sq_scalar, AlgHom.commutes]
  -- ðŸŽ‰ no goals
#align clifford_algebra.comp_Î¹_sq_scalar CliffordAlgebra.comp_Î¹_sq_scalar

variable (Q)

/-- Given a linear map `f : M â†’â‚—[R] A` into an `R`-algebra `A`, which satisfies the condition:
`cond : âˆ€ m : M, f m * f m = Q(m)`, this is the canonical lift of `f` to a morphism of `R`-algebras
from `CliffordAlgebra Q` to `A`.
-/
@[simps symm_apply]
def lift : { f : M â†’â‚—[R] A // âˆ€ m, f m * f m = algebraMap _ _ (Q m) } â‰ƒ (CliffordAlgebra Q â†’â‚[R] A)
    where
  toFun f :=
    RingQuot.liftAlgHom R
      âŸ¨TensorAlgebra.lift R (f : M â†’â‚—[R] A), fun x y (h : Rel Q x y) => by
        induction h
        -- âŠ¢ â†‘(â†‘(TensorAlgebra.lift R) â†‘f) (â†‘(TensorAlgebra.Î¹ R) mâœ * â†‘(TensorAlgebra.Î¹ R â€¦
        rw [AlgHom.commutes, AlgHom.map_mul, TensorAlgebra.lift_Î¹_apply, f.prop]âŸ©
        -- ðŸŽ‰ no goals
  invFun F :=
    âŸ¨F.toLinearMap.comp (Î¹ Q), fun m => by
      rw [LinearMap.comp_apply, AlgHom.toLinearMap_apply, comp_Î¹_sq_scalar]âŸ©
      -- ðŸŽ‰ no goals
  left_inv f := by
    ext x
    -- âŠ¢ â†‘â†‘((fun F => { val := LinearMap.comp (AlgHom.toLinearMap F) (Î¹ Q), property  â€¦
    -- porting note: removed `simp only` proof which gets stuck simplifying `LinearMap.comp_apply`
    exact (RingQuot.liftAlgHom_mkAlgHom_apply _ _ _ _).trans (TensorAlgebra.lift_Î¹_apply _ x)
    -- ðŸŽ‰ no goals
  right_inv F :=
    -- porting note: replaced with proof derived from the one for `TensorAlgebra`
    RingQuot.ringQuot_ext' _ _ _ <|
      TensorAlgebra.hom_ext <|
        LinearMap.ext fun x => by
          exact
            (RingQuot.liftAlgHom_mkAlgHom_apply _ _ _ _).trans (TensorAlgebra.lift_Î¹_apply _ _)
#align clifford_algebra.lift CliffordAlgebra.lift

variable {Q}

@[simp]
theorem Î¹_comp_lift (f : M â†’â‚—[R] A) (cond : âˆ€ m, f m * f m = algebraMap _ _ (Q m)) :
    (lift Q âŸ¨f, condâŸ©).toLinearMap.comp (Î¹ Q) = f :=
  Subtype.mk_eq_mk.mp <| (lift Q).symm_apply_apply âŸ¨f, condâŸ©
#align clifford_algebra.Î¹_comp_lift CliffordAlgebra.Î¹_comp_lift

@[simp]
theorem lift_Î¹_apply (f : M â†’â‚—[R] A) (cond : âˆ€ m, f m * f m = algebraMap _ _ (Q m)) (x) :
    lift Q âŸ¨f, condâŸ© (Î¹ Q x) = f x :=
  (LinearMap.ext_iff.mp <| Î¹_comp_lift f cond) x
#align clifford_algebra.lift_Î¹_apply CliffordAlgebra.lift_Î¹_apply

@[simp]
theorem lift_unique (f : M â†’â‚—[R] A) (cond : âˆ€ m : M, f m * f m = algebraMap _ _ (Q m))
    (g : CliffordAlgebra Q â†’â‚[R] A) : g.toLinearMap.comp (Î¹ Q) = f â†” g = lift Q âŸ¨f, condâŸ© := by
  convert (lift Q : _ â‰ƒ (CliffordAlgebra Q â†’â‚[R] A)).symm_apply_eq
  -- âŠ¢ LinearMap.comp (AlgHom.toLinearMap g) (Î¹ Q) = f â†” â†‘(lift Q).symm g = { val : â€¦
  -- porting note: added `Subtype.mk_eq_mk`
  rw [lift_symm_apply, Subtype.mk_eq_mk]
  -- ðŸŽ‰ no goals
#align clifford_algebra.lift_unique CliffordAlgebra.lift_unique

@[simp]
theorem lift_comp_Î¹ (g : CliffordAlgebra Q â†’â‚[R] A) :
    lift Q âŸ¨g.toLinearMap.comp (Î¹ Q), comp_Î¹_sq_scalar _âŸ© = g := by
  -- porting note: removed `rw [lift_symm_apply]; rfl`, changed `convert` to `exact`
  exact (lift Q : _ â‰ƒ (CliffordAlgebra Q â†’â‚[R] A)).apply_symm_apply g
  -- ðŸŽ‰ no goals
#align clifford_algebra.lift_comp_Î¹ CliffordAlgebra.lift_comp_Î¹

/-- See note [partially-applied ext lemmas]. -/
@[ext high]
theorem hom_ext {A : Type*} [Semiring A] [Algebra R A] {f g : CliffordAlgebra Q â†’â‚[R] A} :
    f.toLinearMap.comp (Î¹ Q) = g.toLinearMap.comp (Î¹ Q) â†’ f = g := by
  intro h
  -- âŠ¢ f = g
  apply (lift Q).symm.injective
  -- âŠ¢ â†‘(lift Q).symm f = â†‘(lift Q).symm g
  rw [lift_symm_apply, lift_symm_apply]
  -- âŠ¢ { val := LinearMap.comp (AlgHom.toLinearMap f) (Î¹ Q), property := (_ : âˆ€ (m  â€¦
  simp only [h]
  -- ðŸŽ‰ no goals
#align clifford_algebra.hom_ext CliffordAlgebra.hom_ext

-- This proof closely follows `TensorAlgebra.induction`
/-- If `C` holds for the `algebraMap` of `r : R` into `CliffordAlgebra Q`, the `Î¹` of `x : M`,
and is preserved under addition and muliplication, then it holds for all of `CliffordAlgebra Q`.

See also the stronger `CliffordAlgebra.left_induction` and `CliffordAlgebra.right_induction`.
-/
@[elab_as_elim]
theorem induction {C : CliffordAlgebra Q â†’ Prop}
    (h_grade0 : âˆ€ r, C (algebraMap R (CliffordAlgebra Q) r)) (h_grade1 : âˆ€ x, C (Î¹ Q x))
    (h_mul : âˆ€ a b, C a â†’ C b â†’ C (a * b)) (h_add : âˆ€ a b, C a â†’ C b â†’ C (a + b))
    (a : CliffordAlgebra Q) : C a := by
  -- the arguments are enough to construct a subalgebra, and a mapping into it from M
  let s : Subalgebra R (CliffordAlgebra Q) :=
    { carrier := C
      mul_mem' := @h_mul
      add_mem' := @h_add
      algebraMap_mem' := h_grade0 }
  -- porting note: Added `h`. `h` is needed for `of`.
  letI h : AddCommMonoid s := inferInstanceAs (AddCommMonoid (Subalgebra.toSubmodule s))
  -- âŠ¢ C a
  let of : { f : M â†’â‚—[R] s // âˆ€ m, f m * f m = algebraMap _ _ (Q m) } :=
    âŸ¨(Î¹ Q).codRestrict (Subalgebra.toSubmodule s) h_grade1, fun m => Subtype.eq <| Î¹_sq_scalar Q mâŸ©
  -- the mapping through the subalgebra is the identity
  have of_id : AlgHom.id R (CliffordAlgebra Q) = s.val.comp (lift Q of) := by
    ext
    simp
    -- porting note: `simp` can't apply this
    erw [LinearMap.codRestrict_apply]
  -- finding a proof is finding an element of the subalgebra
  -- porting note: was `convert Subtype.prop (lift Q of a); exact AlgHom.congr_fun of_id a`
  rw [â† AlgHom.id_apply (R := R) a, of_id]
  -- âŠ¢ C (â†‘(AlgHom.comp (Subalgebra.val s) (â†‘(lift Q) of)) a)
  exact Subtype.prop (lift Q of a)
  -- ðŸŽ‰ no goals
#align clifford_algebra.induction CliffordAlgebra.induction

/-- The symmetric product of vectors is a scalar -/
theorem Î¹_mul_Î¹_add_swap (a b : M) :
    Î¹ Q a * Î¹ Q b + Î¹ Q b * Î¹ Q a = algebraMap R _ (QuadraticForm.polar Q a b) :=
  calc
    Î¹ Q a * Î¹ Q b + Î¹ Q b * Î¹ Q a = Î¹ Q (a + b) * Î¹ Q (a + b) - Î¹ Q a * Î¹ Q a - Î¹ Q b * Î¹ Q b := by
      rw [(Î¹ Q).map_add, mul_add, add_mul, add_mul]; abel
      -- âŠ¢ â†‘(Î¹ Q) a * â†‘(Î¹ Q) b + â†‘(Î¹ Q) b * â†‘(Î¹ Q) a = â†‘(Î¹ Q) a * â†‘(Î¹ Q) a + â†‘(Î¹ Q) b * â€¦
                                                     -- ðŸŽ‰ no goals
                                                     -- ðŸŽ‰ no goals
    _ = algebraMap R _ (Q (a + b)) - algebraMap R _ (Q a) - algebraMap R _ (Q b) := by
      rw [Î¹_sq_scalar, Î¹_sq_scalar, Î¹_sq_scalar]
      -- ðŸŽ‰ no goals
    _ = algebraMap R _ (Q (a + b) - Q a - Q b) := by rw [â† RingHom.map_sub, â† RingHom.map_sub]
                                                     -- ðŸŽ‰ no goals
    _ = algebraMap R _ (QuadraticForm.polar Q a b) := rfl
#align clifford_algebra.Î¹_mul_Î¹_add_swap CliffordAlgebra.Î¹_mul_Î¹_add_swap

theorem Î¹_mul_comm (a b : M) :
    Î¹ Q a * Î¹ Q b = algebraMap R _ (QuadraticForm.polar Q a b) - Î¹ Q b * Î¹ Q a :=
  eq_sub_of_add_eq (Î¹_mul_Î¹_add_swap a b)
#align clifford_algebra.Î¹_mul_comm CliffordAlgebra.Î¹_mul_comm

/-- $aba$ is a vector. -/
theorem Î¹_mul_Î¹_mul_Î¹ (a b : M) :
    Î¹ Q a * Î¹ Q b * Î¹ Q a = Î¹ Q (QuadraticForm.polar Q a b â€¢ a - Q a â€¢ b) := by
  rw [Î¹_mul_comm, sub_mul, mul_assoc, Î¹_sq_scalar, â† Algebra.smul_def, â† Algebra.commutes, â†
    Algebra.smul_def, â† map_smul, â† map_smul, â† map_sub]
#align clifford_algebra.Î¹_mul_Î¹_mul_Î¹ CliffordAlgebra.Î¹_mul_Î¹_mul_Î¹

@[simp]
theorem Î¹_range_map_lift (f : M â†’â‚—[R] A) (cond : âˆ€ m, f m * f m = algebraMap _ _ (Q m)) :
    (Î¹ Q).range.map (lift Q âŸ¨f, condâŸ©).toLinearMap = LinearMap.range f := by
  rw [â† LinearMap.range_comp, Î¹_comp_lift]
  -- ðŸŽ‰ no goals
#align clifford_algebra.Î¹_range_map_lift CliffordAlgebra.Î¹_range_map_lift

section Map

variable {Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*}

variable [AddCommGroup Mâ‚] [AddCommGroup Mâ‚‚] [AddCommGroup Mâ‚ƒ]

variable [Module R Mâ‚] [Module R Mâ‚‚] [Module R Mâ‚ƒ]

variable (Qâ‚ : QuadraticForm R Mâ‚) (Qâ‚‚ : QuadraticForm R Mâ‚‚) (Qâ‚ƒ : QuadraticForm R Mâ‚ƒ)

/-- Any linear map that preserves the quadratic form lifts to an `AlgHom` between algebras.

See `CliffordAlgebra.equivOfIsometry` for the case when `f` is a `QuadraticForm.Isometry`. -/
def map (f : Mâ‚ â†’â‚—[R] Mâ‚‚) (hf : âˆ€ m, Qâ‚‚ (f m) = Qâ‚ m) :
    CliffordAlgebra Qâ‚ â†’â‚[R] CliffordAlgebra Qâ‚‚ :=
  CliffordAlgebra.lift Qâ‚
    âŸ¨(CliffordAlgebra.Î¹ Qâ‚‚).comp f, fun m => (Î¹_sq_scalar _ _).trans <| RingHom.congr_arg _ <| hf mâŸ©
#align clifford_algebra.map CliffordAlgebra.map

@[simp]
theorem map_comp_Î¹ (f : Mâ‚ â†’â‚—[R] Mâ‚‚) (hf) :
    (map Qâ‚ Qâ‚‚ f hf).toLinearMap.comp (Î¹ Qâ‚) = (Î¹ Qâ‚‚).comp f :=
  Î¹_comp_lift _ _
#align clifford_algebra.map_comp_Î¹ CliffordAlgebra.map_comp_Î¹

@[simp]
theorem map_apply_Î¹ (f : Mâ‚ â†’â‚—[R] Mâ‚‚) (hf) (m : Mâ‚) : map Qâ‚ Qâ‚‚ f hf (Î¹ Qâ‚ m) = Î¹ Qâ‚‚ (f m) :=
  lift_Î¹_apply _ _ m
#align clifford_algebra.map_apply_Î¹ CliffordAlgebra.map_apply_Î¹

@[simp]
theorem map_id :
    (map Qâ‚ Qâ‚ (LinearMap.id : Mâ‚ â†’â‚—[R] Mâ‚) fun m => rfl) = AlgHom.id R (CliffordAlgebra Qâ‚) := by
  ext m; exact map_apply_Î¹ _ _ _ _ m
  -- âŠ¢ â†‘(LinearMap.comp (AlgHom.toLinearMap (map Qâ‚ Qâ‚ LinearMap.id (_ : âˆ€ (m : Mâ‚) â€¦
         -- ðŸŽ‰ no goals
#align clifford_algebra.map_id CliffordAlgebra.map_id

@[simp]
theorem map_comp_map (f : Mâ‚‚ â†’â‚—[R] Mâ‚ƒ) (hf) (g : Mâ‚ â†’â‚—[R] Mâ‚‚) (hg) :
    (map Qâ‚‚ Qâ‚ƒ f hf).comp (map Qâ‚ Qâ‚‚ g hg)
      = map Qâ‚ Qâ‚ƒ (f.comp g) fun m => (hf _).trans <| hg m := by
  ext m
  -- âŠ¢ â†‘(LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp (map Qâ‚‚ Qâ‚ƒ f hf) (map Qâ‚ Q â€¦
  dsimp only [LinearMap.comp_apply, AlgHom.comp_apply, AlgHom.toLinearMap_apply, AlgHom.id_apply]
  -- âŠ¢ â†‘(map Qâ‚‚ Qâ‚ƒ f hf) (â†‘(map Qâ‚ Qâ‚‚ g hg) (â†‘(Î¹ Qâ‚) m)) = â†‘(map Qâ‚ Qâ‚ƒ (LinearMap.c â€¦
  rw [map_apply_Î¹, map_apply_Î¹, map_apply_Î¹, LinearMap.comp_apply]
  -- ðŸŽ‰ no goals
#align clifford_algebra.map_comp_map CliffordAlgebra.map_comp_map

@[simp]
theorem Î¹_range_map_map (f : Mâ‚ â†’â‚—[R] Mâ‚‚) (hf : âˆ€ m, Qâ‚‚ (f m) = Qâ‚ m) :
    (Î¹ Qâ‚).range.map (map Qâ‚ Qâ‚‚ f hf).toLinearMap = f.range.map (Î¹ Qâ‚‚) :=
  (Î¹_range_map_lift _ _).trans (LinearMap.range_comp _ _)
#align clifford_algebra.Î¹_range_map_map CliffordAlgebra.Î¹_range_map_map

variable {Qâ‚ Qâ‚‚ Qâ‚ƒ}

/-- Two `CliffordAlgebra`s are equivalent as algebras if their quadratic forms are
equivalent. -/
@[simps! apply]
def equivOfIsometry (e : Qâ‚.IsometryEquiv Qâ‚‚) : CliffordAlgebra Qâ‚ â‰ƒâ‚[R] CliffordAlgebra Qâ‚‚ :=
  AlgEquiv.ofAlgHom (map Qâ‚ Qâ‚‚ e e.map_app) (map Qâ‚‚ Qâ‚ e.symm e.symm.map_app)
    ((map_comp_map _ _ _ _ _ _ _).trans <| by
      convert map_id Qâ‚‚ using 2  -- porting note: replaced `_` with `Qâ‚‚`
      -- âŠ¢ LinearMap.comp â†‘â†‘e â†‘â†‘(QuadraticForm.IsometryEquiv.symm e) = LinearMap.id
      ext m
      -- âŠ¢ â†‘(LinearMap.comp â†‘â†‘e â†‘â†‘(QuadraticForm.IsometryEquiv.symm e)) m = â†‘LinearMap. â€¦
      exact e.toLinearEquiv.apply_symm_apply m)
      -- ðŸŽ‰ no goals
    ((map_comp_map _ _ _ _ _ _ _).trans <| by
      convert map_id Qâ‚ using 2  -- porting note: replaced `_` with `Qâ‚`
      -- âŠ¢ LinearMap.comp â†‘â†‘(QuadraticForm.IsometryEquiv.symm e) â†‘â†‘e = LinearMap.id
      ext m
      -- âŠ¢ â†‘(LinearMap.comp â†‘â†‘(QuadraticForm.IsometryEquiv.symm e) â†‘â†‘e) m = â†‘LinearMap. â€¦
      exact e.toLinearEquiv.symm_apply_apply m)
      -- ðŸŽ‰ no goals
#align clifford_algebra.equiv_of_isometry CliffordAlgebra.equivOfIsometry

@[simp]
theorem equivOfIsometry_symm (e : Qâ‚.IsometryEquiv Qâ‚‚) :
    (equivOfIsometry e).symm = equivOfIsometry e.symm :=
  rfl
#align clifford_algebra.equiv_of_isometry_symm CliffordAlgebra.equivOfIsometry_symm

@[simp]
theorem equivOfIsometry_trans (eâ‚â‚‚ : Qâ‚.IsometryEquiv Qâ‚‚) (eâ‚‚â‚ƒ : Qâ‚‚.IsometryEquiv Qâ‚ƒ) :
    (equivOfIsometry eâ‚â‚‚).trans (equivOfIsometry eâ‚‚â‚ƒ) = equivOfIsometry (eâ‚â‚‚.trans eâ‚‚â‚ƒ) := by
  ext x
  -- âŠ¢ â†‘(AlgEquiv.trans (equivOfIsometry eâ‚â‚‚) (equivOfIsometry eâ‚‚â‚ƒ)) x = â†‘(equivOfI â€¦
  exact AlgHom.congr_fun (map_comp_map Qâ‚ Qâ‚‚ Qâ‚ƒ _ _ _ _) x
  -- ðŸŽ‰ no goals
#align clifford_algebra.equiv_of_isometry_trans CliffordAlgebra.equivOfIsometry_trans

@[simp]
theorem equivOfIsometry_refl :
    (equivOfIsometry <| QuadraticForm.IsometryEquiv.refl Qâ‚) = AlgEquiv.refl := by
  ext x
  -- âŠ¢ â†‘(equivOfIsometry (QuadraticForm.IsometryEquiv.refl Qâ‚)) x = â†‘AlgEquiv.refl x
  exact AlgHom.congr_fun (map_id Qâ‚) x
  -- ðŸŽ‰ no goals
#align clifford_algebra.equiv_of_isometry_refl CliffordAlgebra.equivOfIsometry_refl

end Map

variable (Q)

/-- If the quadratic form of a vector is invertible, then so is that vector. -/
def invertibleÎ¹OfInvertible (m : M) [Invertible (Q m)] : Invertible (Î¹ Q m) where
  invOf := Î¹ Q (â…Ÿ (Q m) â€¢ m)
  invOf_mul_self := by
    rw [map_smul, smul_mul_assoc, Î¹_sq_scalar, Algebra.smul_def, â† map_mul, invOf_mul_self, map_one]
    -- ðŸŽ‰ no goals
  mul_invOf_self := by
    rw [map_smul, mul_smul_comm, Î¹_sq_scalar, Algebra.smul_def, â† map_mul, invOf_mul_self, map_one]
    -- ðŸŽ‰ no goals
#align clifford_algebra.invertible_Î¹_of_invertible CliffordAlgebra.invertibleÎ¹OfInvertible

/-- For a vector with invertible quadratic form, $v^{-1} = \frac{v}{Q(v)}$ -/
theorem invOf_Î¹ (m : M) [Invertible (Q m)] [Invertible (Î¹ Q m)] :
    â…Ÿ (Î¹ Q m) = Î¹ Q (â…Ÿ (Q m) â€¢ m) := by
  letI := invertibleÎ¹OfInvertible Q m
  -- âŠ¢ â…Ÿ(â†‘(Î¹ Q) m) = â†‘(Î¹ Q) (â…Ÿ(â†‘Q m) â€¢ m)
  convert (rfl : â…Ÿ (Î¹ Q m) = _)
  -- ðŸŽ‰ no goals
#align clifford_algebra.inv_of_Î¹ CliffordAlgebra.invOf_Î¹

theorem isUnit_Î¹_of_isUnit {m : M} (h : IsUnit (Q m)) : IsUnit (Î¹ Q m) := by
  cases h.nonempty_invertible
  -- âŠ¢ IsUnit (â†‘(Î¹ Q) m)
  letI := invertibleÎ¹OfInvertible Q m
  -- âŠ¢ IsUnit (â†‘(Î¹ Q) m)
  exact isUnit_of_invertible (Î¹ Q m)
  -- ðŸŽ‰ no goals
#align clifford_algebra.is_unit_Î¹_of_is_unit CliffordAlgebra.isUnit_Î¹_of_isUnit

/-- $aba^{-1}$ is a vector. -/
theorem Î¹_mul_Î¹_mul_invOf_Î¹ (a b : M) [Invertible (Î¹ Q a)] [Invertible (Q a)] :
    Î¹ Q a * Î¹ Q b * â…Ÿ (Î¹ Q a) = Î¹ Q ((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b) := by
  rw [invOf_Î¹, map_smul, mul_smul_comm, Î¹_mul_Î¹_mul_Î¹, â† map_smul, smul_sub, smul_smul, smul_smul,
    invOf_mul_self, one_smul]
#align clifford_algebra.Î¹_mul_Î¹_mul_inv_of_Î¹ CliffordAlgebra.Î¹_mul_Î¹_mul_invOf_Î¹

/-- $a^{-1}ba$ is a vector. -/
theorem invOf_Î¹_mul_Î¹_mul_Î¹ (a b : M) [Invertible (Î¹ Q a)] [Invertible (Q a)] :
    â…Ÿ (Î¹ Q a) * Î¹ Q b * Î¹ Q a = Î¹ Q ((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b) := by
  rw [invOf_Î¹, map_smul, smul_mul_assoc, smul_mul_assoc, Î¹_mul_Î¹_mul_Î¹, â† map_smul, smul_sub,
    smul_smul, smul_smul, invOf_mul_self, one_smul]
#align clifford_algebra.inv_of_Î¹_mul_Î¹_mul_Î¹ CliffordAlgebra.invOf_Î¹_mul_Î¹_mul_Î¹

end CliffordAlgebra

namespace TensorAlgebra

variable {Q}

/-- The canonical image of the `TensorAlgebra` in the `CliffordAlgebra`, which maps
`TensorAlgebra.Î¹ R x` to `CliffordAlgebra.Î¹ Q x`. -/
def toClifford : TensorAlgebra R M â†’â‚[R] CliffordAlgebra Q :=
  TensorAlgebra.lift R (CliffordAlgebra.Î¹ Q)
#align tensor_algebra.to_clifford TensorAlgebra.toClifford

@[simp]
theorem toClifford_Î¹ (m : M) : toClifford (TensorAlgebra.Î¹ R m) = CliffordAlgebra.Î¹ Q m := by
  simp [toClifford]
  -- ðŸŽ‰ no goals
#align tensor_algebra.to_clifford_Î¹ TensorAlgebra.toClifford_Î¹

end TensorAlgebra
