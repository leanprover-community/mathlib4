/-
Copyright (c) 2021 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Algebra.DualNumber
import Mathlib.Algebra.QuaternionBasis
import Mathlib.Data.Complex.Module
import Mathlib.LinearAlgebra.CliffordAlgebra.Conjugation
import Mathlib.LinearAlgebra.CliffordAlgebra.Star
import Mathlib.LinearAlgebra.QuadraticForm.Prod

#align_import linear_algebra.clifford_algebra.equivs from "leanprover-community/mathlib"@"cf7a7252c1989efe5800e0b3cdfeb4228ac6b40e"

/-!
# Other constructions isomorphic to Clifford Algebras

This file contains isomorphisms showing that other types are equivalent to some `CliffordAlgebra`.

## Rings

* `CliffordAlgebraRing.equiv`: any ring is equivalent to a `CliffordAlgebra` over a
  zero-dimensional vector space.

## Complex numbers

* `CliffordAlgebraComplex.equiv`: the `Complex` numbers are equivalent as an `‚Ñù`-algebra to a
  `CliffordAlgebra` over a one-dimensional vector space with a quadratic form that satisfies
  `Q (Œπ Q 1) = -1`.
* `CliffordAlgebraComplex.toComplex`: the forward direction of this equiv
* `CliffordAlgebraComplex.ofComplex`: the reverse direction of this equiv

We show additionally that this equivalence sends `Complex.conj` to `CliffordAlgebra.involute` and
vice-versa:

* `CliffordAlgebraComplex.toComplex_involute`
* `CliffordAlgebraComplex.ofComplex_conj`

Note that in this algebra `CliffordAlgebra.reverse` is the identity and so the clifford conjugate
is the same as `CliffordAlgebra.involute`.

## Quaternion algebras

* `CliffordAlgebraQuaternion.equiv`: a `QuaternionAlgebra` over `R` is equivalent as an
  `R`-algebra to a clifford algebra over `R √ó R`, sending `i` to `(0, 1)` and `j` to `(1, 0)`.
* `CliffordAlgebraQuaternion.toQuaternion`: the forward direction of this equiv
* `CliffordAlgebraQuaternion.ofQuaternion`: the reverse direction of this equiv

We show additionally that this equivalence sends `QuaternionAlgebra.conj` to the clifford conjugate
and vice-versa:

* `CliffordAlgebraQuaternion.toQuaternion_star`
* `CliffordAlgebraQuaternion.ofQuaternion_star`

## Dual numbers

* `CliffordAlgebraDualNumber.equiv`: `R[Œµ]` is equivalent as an `R`-algebra to a clifford
  algebra over `R` where `Q = 0`.

-/


open CliffordAlgebra

/-! ### The clifford algebra isomorphic to a ring -/


namespace CliffordAlgebraRing

open scoped ComplexConjugate

variable {R : Type*} [CommRing R]

@[simp]
theorem Œπ_eq_zero : Œπ (0 : QuadraticForm R Unit) = 0 :=
  Subsingleton.elim _ _
#align clifford_algebra_ring.Œπ_eq_zero CliffordAlgebraRing.Œπ_eq_zero

/-- Since the vector space is empty the ring is commutative. -/
instance : CommRing (CliffordAlgebra (0 : QuadraticForm R Unit)) :=
  { CliffordAlgebra.instRing _ with
    mul_comm := fun x y => by
      induction x using CliffordAlgebra.induction
      case h_grade0 r => apply Algebra.commutes
      -- üéâ no goals
      case h_grade1 x => simp
      -- ‚ä¢ a‚úù¬≤ * b‚úù * y = y * (a‚úù¬≤ * b‚úù)
      -- üéâ no goals
      case h_add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [mul_add, add_mul, hx‚ÇÅ, hx‚ÇÇ]
      -- ‚ä¢ a‚úù¬≤ * b‚úù * y = y * (a‚úù¬≤ * b‚úù)
      -- üéâ no goals
      case h_mul x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [mul_assoc, hx‚ÇÇ, ‚Üê mul_assoc, hx‚ÇÅ, ‚Üê mul_assoc] }
      -- üéâ no goals
      -- üéâ no goals

-- Porting note: Changed `x.reverse` to `reverse (R := R) x`
theorem reverse_apply (x : CliffordAlgebra (0 : QuadraticForm R Unit)) :
    reverse (R := R) x = x := by
  induction x using CliffordAlgebra.induction
  case h_grade0 r => exact reverse.commutes _
  -- üéâ no goals
  case h_grade1 x => rw [Œπ_eq_zero, LinearMap.zero_apply, reverse.map_zero]
  -- ‚ä¢ ‚Üëreverse (a‚úù¬≤ * b‚úù) = a‚úù¬≤ * b‚úù
  -- üéâ no goals
  case h_mul x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [reverse.map_mul, mul_comm, hx‚ÇÅ, hx‚ÇÇ]
  -- ‚ä¢ ‚Üëreverse (a‚úù¬≤ + b‚úù) = a‚úù¬≤ + b‚úù
  -- üéâ no goals
  case h_add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [reverse.map_add, hx‚ÇÅ, hx‚ÇÇ]
  -- üéâ no goals
  -- üéâ no goals
#align clifford_algebra_ring.reverse_apply CliffordAlgebraRing.reverse_apply

@[simp]
theorem reverse_eq_id :
    (reverse : CliffordAlgebra (0 : QuadraticForm R Unit) ‚Üí‚Çó[R] _) = LinearMap.id :=
  LinearMap.ext reverse_apply
#align clifford_algebra_ring.reverse_eq_id CliffordAlgebraRing.reverse_eq_id

@[simp]
theorem involute_eq_id :
    (involute : CliffordAlgebra (0 : QuadraticForm R Unit) ‚Üí‚Çê[R] _) = AlgHom.id R _ := by ext; simp
                                                                                          -- ‚ä¢ ‚Üë(LinearMap.comp (AlgHom.toLinearMap involute) (Œπ 0)) x‚úù = ‚Üë(LinearMap.comp  ‚Ä¶
                                                                                               -- üéâ no goals
#align clifford_algebra_ring.involute_eq_id CliffordAlgebraRing.involute_eq_id

/-- The clifford algebra over a 0-dimensional vector space is isomorphic to its scalars. -/
protected def equiv : CliffordAlgebra (0 : QuadraticForm R Unit) ‚âÉ‚Çê[R] R :=
  AlgEquiv.ofAlgHom
    (CliffordAlgebra.lift (0 : QuadraticForm R Unit) <|
      ‚ü®0, fun m : Unit => (zero_mul (0 : R)).trans (algebraMap R _).map_zero.symm‚ü©)
    (Algebra.ofId R _) (by ext x; exact AlgHom.commutes _ x)
                           -- ‚ä¢ ‚Üë(AlgHom.comp (‚Üë(lift 0) { val := 0, property := (_ : ‚àÄ (m : Unit), 0 * 0 =  ‚Ä¶
                                  -- üéâ no goals
    (by ext : 1; rw [Œπ_eq_zero, LinearMap.comp_zero, LinearMap.comp_zero])
        -- ‚ä¢ LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp (Algebra.ofId R (CliffordAlg ‚Ä¶
                 -- üéâ no goals
#align clifford_algebra_ring.equiv CliffordAlgebraRing.equiv

end CliffordAlgebraRing

/-! ### The clifford algebra isomorphic to the complex numbers -/


namespace CliffordAlgebraComplex

open scoped ComplexConjugate

/-- The quadratic form sending elements to the negation of their square. -/
def Q : QuadraticForm ‚Ñù ‚Ñù :=
  -QuadraticForm.sq (R := ‚Ñù) -- Porting note: Added `(R := ‚Ñù)`
set_option linter.uppercaseLean3 false in
#align clifford_algebra_complex.Q CliffordAlgebraComplex.Q

@[simp]
theorem Q_apply (r : ‚Ñù) : Q r = -(r * r) :=
  rfl
set_option linter.uppercaseLean3 false in
#align clifford_algebra_complex.Q_apply CliffordAlgebraComplex.Q_apply

/-- Intermediate result for `CliffordAlgebraComplex.equiv`: clifford algebras over
`CliffordAlgebraComplex.Q` above can be converted to `‚ÑÇ`. -/
def toComplex : CliffordAlgebra Q ‚Üí‚Çê[‚Ñù] ‚ÑÇ :=
  CliffordAlgebra.lift Q
    ‚ü®LinearMap.toSpanSingleton _ _ Complex.I, fun r => by
      dsimp [LinearMap.toSpanSingleton, LinearMap.id]
      -- ‚ä¢ ‚Üër * Complex.I * (‚Üër * Complex.I) = ‚Üë(-(r * r))
      rw [mul_mul_mul_comm]
      -- ‚ä¢ ‚Üër * ‚Üër * (Complex.I * Complex.I) = ‚Üë(-(r * r))
      simp‚ü©
      -- üéâ no goals
#align clifford_algebra_complex.to_complex CliffordAlgebraComplex.toComplex

@[simp]
theorem toComplex_Œπ (r : ‚Ñù) : toComplex (Œπ Q r) = r ‚Ä¢ Complex.I :=
  CliffordAlgebra.lift_Œπ_apply _ _ r
#align clifford_algebra_complex.to_complex_Œπ CliffordAlgebraComplex.toComplex_Œπ

/-- `CliffordAlgebra.involute` is analogous to `Complex.conj`. -/
@[simp]
theorem toComplex_involute (c : CliffordAlgebra Q) :
    toComplex (involute c) = conj (toComplex c) := by
  have : toComplex (involute (Œπ Q 1)) = conj (toComplex (Œπ Q 1)) := by
    simp only [involute_Œπ, toComplex_Œπ, AlgHom.map_neg, one_smul, Complex.conj_I]
  suffices toComplex.comp involute = Complex.conjAe.toAlgHom.comp toComplex by
    exact AlgHom.congr_fun this c
  ext : 2
  -- ‚ä¢ ‚Üë(LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp toComplex involute)) (Œπ Q) ‚Ä¶
  exact this
  -- üéâ no goals
#align clifford_algebra_complex.to_complex_involute CliffordAlgebraComplex.toComplex_involute

/-- Intermediate result for `CliffordAlgebraComplex.equiv`: `‚ÑÇ` can be converted to
`CliffordAlgebraComplex.Q` above can be converted to. -/
def ofComplex : ‚ÑÇ ‚Üí‚Çê[‚Ñù] CliffordAlgebra Q :=
  Complex.lift
    ‚ü®CliffordAlgebra.Œπ Q 1, by
      rw [CliffordAlgebra.Œπ_sq_scalar, Q_apply, one_mul, RingHom.map_neg, RingHom.map_one]‚ü©
      -- üéâ no goals
#align clifford_algebra_complex.of_complex CliffordAlgebraComplex.ofComplex

@[simp]
theorem ofComplex_I : ofComplex Complex.I = Œπ Q 1 :=
  Complex.liftAux_apply_I _ (by simp)
                                -- üéâ no goals
set_option linter.uppercaseLean3 false in
#align clifford_algebra_complex.of_complex_I CliffordAlgebraComplex.ofComplex_I

@[simp]
theorem toComplex_comp_ofComplex : toComplex.comp ofComplex = AlgHom.id ‚Ñù ‚ÑÇ := by
  ext1
  -- ‚ä¢ ‚Üë(AlgHom.comp toComplex ofComplex) Complex.I = ‚Üë(AlgHom.id ‚Ñù ‚ÑÇ) Complex.I
  dsimp only [AlgHom.comp_apply, Subtype.coe_mk, AlgHom.id_apply]
  -- ‚ä¢ ‚ÜëtoComplex (‚ÜëofComplex Complex.I) = Complex.I
  rw [ofComplex_I, toComplex_Œπ, one_smul]
  -- üéâ no goals
#align clifford_algebra_complex.to_complex_comp_of_complex CliffordAlgebraComplex.toComplex_comp_ofComplex

@[simp]
theorem toComplex_ofComplex (c : ‚ÑÇ) : toComplex (ofComplex c) = c :=
  AlgHom.congr_fun toComplex_comp_ofComplex c
#align clifford_algebra_complex.to_complex_of_complex CliffordAlgebraComplex.toComplex_ofComplex

@[simp]
theorem ofComplex_comp_toComplex : ofComplex.comp toComplex = AlgHom.id ‚Ñù (CliffordAlgebra Q) := by
  ext
  -- ‚ä¢ ‚Üë(LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp ofComplex toComplex)) (Œπ Q ‚Ä¶
  dsimp only [LinearMap.comp_apply, Subtype.coe_mk, AlgHom.id_apply, AlgHom.toLinearMap_apply,
    AlgHom.comp_apply]
  rw [toComplex_Œπ, one_smul, ofComplex_I]
  -- üéâ no goals
#align clifford_algebra_complex.of_complex_comp_to_complex CliffordAlgebraComplex.ofComplex_comp_toComplex

@[simp]
theorem ofComplex_toComplex (c : CliffordAlgebra Q) : ofComplex (toComplex c) = c :=
  AlgHom.congr_fun ofComplex_comp_toComplex c
#align clifford_algebra_complex.of_complex_to_complex CliffordAlgebraComplex.ofComplex_toComplex

/-- The clifford algebras over `CliffordAlgebraComplex.Q` is isomorphic as an `‚Ñù`-algebra to `‚ÑÇ`. -/
@[simps!]
protected def equiv : CliffordAlgebra Q ‚âÉ‚Çê[‚Ñù] ‚ÑÇ :=
  AlgEquiv.ofAlgHom toComplex ofComplex toComplex_comp_ofComplex ofComplex_comp_toComplex
#align clifford_algebra_complex.equiv CliffordAlgebraComplex.equiv

/-- The clifford algebra is commutative since it is isomorphic to the complex numbers.

TODO: prove this is true for all `CliffordAlgebra`s over a 1-dimensional vector space. -/
instance : CommRing (CliffordAlgebra Q) :=
  { CliffordAlgebra.instRing _ with
    mul_comm := fun x y =>
      CliffordAlgebraComplex.equiv.injective <| by
        rw [AlgEquiv.map_mul, mul_comm, AlgEquiv.map_mul] }
        -- üéâ no goals

-- Porting note: Changed `x.reverse` to `reverse (R := ‚Ñù) x`
/-- `reverse` is a no-op over `CliffordAlgebraComplex.Q`. -/
theorem reverse_apply (x : CliffordAlgebra Q) : reverse (R := ‚Ñù) x = x := by
  induction x using CliffordAlgebra.induction
  case h_grade0 r => exact reverse.commutes _
  -- üéâ no goals
  case h_grade1 x => rw [reverse_Œπ]
  -- ‚ä¢ ‚Üëreverse (a‚úù¬≤ * b‚úù) = a‚úù¬≤ * b‚úù
  -- üéâ no goals
  case h_mul x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [reverse.map_mul, mul_comm, hx‚ÇÅ, hx‚ÇÇ]
  -- ‚ä¢ ‚Üëreverse (a‚úù¬≤ + b‚úù) = a‚úù¬≤ + b‚úù
  -- üéâ no goals
  case h_add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => rw [reverse.map_add, hx‚ÇÅ, hx‚ÇÇ]
  -- üéâ no goals
  -- üéâ no goals
#align clifford_algebra_complex.reverse_apply CliffordAlgebraComplex.reverse_apply

@[simp]
theorem reverse_eq_id : (reverse : CliffordAlgebra Q ‚Üí‚Çó[‚Ñù] _) = LinearMap.id :=
  LinearMap.ext reverse_apply
#align clifford_algebra_complex.reverse_eq_id CliffordAlgebraComplex.reverse_eq_id

/-- `Complex.conj` is analogous to `CliffordAlgebra.involute`. -/
@[simp]
theorem ofComplex_conj (c : ‚ÑÇ) : ofComplex (conj c) = involute (ofComplex c) :=
  CliffordAlgebraComplex.equiv.injective <| by
    rw [equiv_apply, equiv_apply, toComplex_involute, toComplex_ofComplex, toComplex_ofComplex]
    -- üéâ no goals
#align clifford_algebra_complex.of_complex_conj CliffordAlgebraComplex.ofComplex_conj

-- this name is too short for us to want it visible after `open CliffordAlgebraComplex`
--attribute [protected] Q -- Porting note: removed

end CliffordAlgebraComplex

/-! ### The clifford algebra isomorphic to the quaternions -/


namespace CliffordAlgebraQuaternion

open scoped Quaternion

open QuaternionAlgebra

variable {R : Type*} [CommRing R] (c‚ÇÅ c‚ÇÇ : R)

/-- `Q c‚ÇÅ c‚ÇÇ` is a quadratic form over `R √ó R` such that `CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ)` is isomorphic
as an `R`-algebra to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
def Q : QuadraticForm R (R √ó R) :=
  (c‚ÇÅ ‚Ä¢ QuadraticForm.sq (R := R)).prod (c‚ÇÇ ‚Ä¢ QuadraticForm.sq) -- Porting note: Added `(R := R)`
set_option linter.uppercaseLean3 false in
#align clifford_algebra_quaternion.Q CliffordAlgebraQuaternion.Q

@[simp]
theorem Q_apply (v : R √ó R) : Q c‚ÇÅ c‚ÇÇ v = c‚ÇÅ * (v.1 * v.1) + c‚ÇÇ * (v.2 * v.2) :=
  rfl
set_option linter.uppercaseLean3 false in
#align clifford_algebra_quaternion.Q_apply CliffordAlgebraQuaternion.Q_apply

/-- The quaternion basis vectors within the algebra. -/
@[simps i j k]
def quaternionBasis : QuaternionAlgebra.Basis (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ)) c‚ÇÅ c‚ÇÇ where
  i := Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0)
  j := Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1)
  k := Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0) * Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1)
  i_mul_i := by
    rw [Œπ_sq_scalar, Q_apply, ‚Üê Algebra.algebraMap_eq_smul_one]
    -- ‚ä¢ ‚Üë(algebraMap R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ))) (c‚ÇÅ * ((1, 0).fst * (1, 0).fst)  ‚Ä¶
    simp
    -- üéâ no goals
  j_mul_j := by
    rw [Œπ_sq_scalar, Q_apply, ‚Üê Algebra.algebraMap_eq_smul_one]
    -- ‚ä¢ ‚Üë(algebraMap R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ))) (c‚ÇÅ * ((0, 1).fst * (0, 1).fst)  ‚Ä¶
    simp
    -- üéâ no goals
  i_mul_j := rfl
  j_mul_i := by
    rw [eq_neg_iff_add_eq_zero, Œπ_mul_Œπ_add_swap, QuadraticForm.polar]
    -- ‚ä¢ ‚Üë(algebraMap R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ))) (‚Üë(Q c‚ÇÅ c‚ÇÇ) ((0, 1) + (1, 0)) -  ‚Ä¶
    simp
    -- üéâ no goals
#align clifford_algebra_quaternion.quaternion_basis CliffordAlgebraQuaternion.quaternionBasis

variable {c‚ÇÅ c‚ÇÇ}

/-- Intermediate result of `CliffordAlgebraQuaternion.equiv`: clifford algebras over
`CliffordAlgebraQuaternion.Q` can be converted to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
def toQuaternion : CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ) ‚Üí‚Çê[R] ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] :=
  CliffordAlgebra.lift (Q c‚ÇÅ c‚ÇÇ)
    ‚ü®{  toFun := fun v => (‚ü®0, v.1, v.2, 0‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ])
        map_add' := fun v‚ÇÅ v‚ÇÇ => by simp
                                    -- üéâ no goals
        map_smul' := fun r v => by dsimp; rw [mul_zero]; rfl }, fun v => by
                                   -- ‚ä¢ { re := 0, imI := (r ‚Ä¢ v).fst, imJ := (r ‚Ä¢ v).snd, imK := 0 } = { re := r *  ‚Ä¶
                                          -- ‚ä¢ { re := 0, imI := (r ‚Ä¢ v).fst, imJ := (r ‚Ä¢ v).snd, imK := 0 } = { re := 0, i ‚Ä¶
                                                         -- üéâ no goals
      dsimp
      -- ‚ä¢ { re := 0 * 0 + c‚ÇÅ * v.fst * v.fst + c‚ÇÇ * v.snd * v.snd - c‚ÇÅ * c‚ÇÇ * 0 * 0, i ‚Ä¶
      ext
      all_goals dsimp; ring‚ü©
      -- üéâ no goals
#align clifford_algebra_quaternion.to_quaternion CliffordAlgebraQuaternion.toQuaternion

@[simp]
theorem toQuaternion_Œπ (v : R √ó R) :
    toQuaternion (Œπ (Q c‚ÇÅ c‚ÇÇ) v) = (‚ü®0, v.1, v.2, 0‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) :=
  CliffordAlgebra.lift_Œπ_apply _ _ v
#align clifford_algebra_quaternion.to_quaternion_Œπ CliffordAlgebraQuaternion.toQuaternion_Œπ

/-- The "clifford conjugate" maps to the quaternion conjugate. -/
theorem toQuaternion_star (c : CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ)) :
    toQuaternion (star c) = star (toQuaternion c) := by
  simp only [CliffordAlgebra.star_def']
  -- ‚ä¢ ‚ÜëtoQuaternion (‚Üëinvolute (‚Üëreverse c)) = star (‚ÜëtoQuaternion c)
  induction c using CliffordAlgebra.induction
  case h_grade0 r =>
    simp only [reverse.commutes, AlgHom.commutes, QuaternionAlgebra.coe_algebraMap,
      QuaternionAlgebra.star_coe]
  case h_grade1 x =>
    rw [reverse_Œπ, involute_Œπ, toQuaternion_Œπ, AlgHom.map_neg, toQuaternion_Œπ,
      QuaternionAlgebra.neg_mk, star_mk, neg_zero]
  case h_mul x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [reverse.map_mul, AlgHom.map_mul, hx‚ÇÅ, hx‚ÇÇ, star_mul]
  -- ‚ä¢ ‚ÜëtoQuaternion (‚Üëinvolute (‚Üëreverse (a‚úù¬≤ + b‚úù))) = star (‚ÜëtoQuaternion (a‚úù¬≤ + ‚Ä¶
  -- üéâ no goals
  case h_add x‚ÇÅ x‚ÇÇ hx‚ÇÅ hx‚ÇÇ => simp only [reverse.map_add, AlgHom.map_add, hx‚ÇÅ, hx‚ÇÇ, star_add]
  -- üéâ no goals
  -- üéâ no goals
#align clifford_algebra_quaternion.to_quaternion_star CliffordAlgebraQuaternion.toQuaternion_star

/-- Map a quaternion into the clifford algebra. -/
def ofQuaternion : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] ‚Üí‚Çê[R] CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ) :=
  (quaternionBasis c‚ÇÅ c‚ÇÇ).liftHom
#align clifford_algebra_quaternion.of_quaternion CliffordAlgebraQuaternion.ofQuaternion

@[simp]
theorem ofQuaternion_mk (a‚ÇÅ a‚ÇÇ a‚ÇÉ a‚ÇÑ : R) :
    ofQuaternion (‚ü®a‚ÇÅ, a‚ÇÇ, a‚ÇÉ, a‚ÇÑ‚ü© : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) =
      algebraMap R _ a‚ÇÅ + a‚ÇÇ ‚Ä¢ Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0) + a‚ÇÉ ‚Ä¢ Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1) +
        a‚ÇÑ ‚Ä¢ (Œπ (Q c‚ÇÅ c‚ÇÇ) (1, 0) * Œπ (Q c‚ÇÅ c‚ÇÇ) (0, 1)) :=
  rfl
#align clifford_algebra_quaternion.of_quaternion_mk CliffordAlgebraQuaternion.ofQuaternion_mk

@[simp]
theorem ofQuaternion_comp_toQuaternion :
    ofQuaternion.comp toQuaternion = AlgHom.id R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ)) := by
  ext : 1
  -- ‚ä¢ LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp ofQuaternion toQuaternion))  ‚Ä¶
  dsimp -- before we end up with two goals and have to do this twice
  -- ‚ä¢ LinearMap.comp (LinearMap.comp (AlgHom.toLinearMap ofQuaternion) (AlgHom.toL ‚Ä¶
  ext
  -- ‚ä¢ ‚Üë(LinearMap.comp (LinearMap.comp (LinearMap.comp (AlgHom.toLinearMap ofQuate ‚Ä¶
  all_goals
    dsimp
    rw [toQuaternion_Œπ]
    dsimp
    simp only [toQuaternion_Œπ, zero_smul, one_smul, zero_add, add_zero, RingHom.map_zero]
#align clifford_algebra_quaternion.of_quaternion_comp_to_quaternion CliffordAlgebraQuaternion.ofQuaternion_comp_toQuaternion

@[simp]
theorem ofQuaternion_toQuaternion (c : CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ)) :
    ofQuaternion (toQuaternion c) = c :=
  AlgHom.congr_fun ofQuaternion_comp_toQuaternion c
#align clifford_algebra_quaternion.of_quaternion_to_quaternion CliffordAlgebraQuaternion.ofQuaternion_toQuaternion

@[simp]
theorem toQuaternion_comp_ofQuaternion :
    toQuaternion.comp ofQuaternion = AlgHom.id R ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] := by
  apply QuaternionAlgebra.lift.symm.injective
  -- ‚ä¢ ‚ÜëQuaternionAlgebra.lift.symm (AlgHom.comp toQuaternion ofQuaternion) = ‚ÜëQuat ‚Ä¶
  ext1 <;> dsimp [QuaternionAlgebra.Basis.lift] <;> simp
  -- ‚ä¢ (‚ÜëQuaternionAlgebra.lift.symm (AlgHom.comp toQuaternion ofQuaternion)).i = ( ‚Ä¶
           -- ‚ä¢ ‚ÜëtoQuaternion (‚Üë(algebraMap R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ))) 0 + 1 ‚Ä¢ ‚Üë(Œπ (Q c‚ÇÅ ‚Ä¶
           -- ‚ä¢ ‚ÜëtoQuaternion (‚Üë(algebraMap R (CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ))) 0 + 0 ‚Ä¢ ‚Üë(Œπ (Q c‚ÇÅ ‚Ä¶
                                                    -- üéâ no goals
                                                    -- üéâ no goals
#align clifford_algebra_quaternion.to_quaternion_comp_of_quaternion CliffordAlgebraQuaternion.toQuaternion_comp_ofQuaternion

@[simp]
theorem toQuaternion_ofQuaternion (q : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) : toQuaternion (ofQuaternion q) = q :=
  AlgHom.congr_fun toQuaternion_comp_ofQuaternion q
#align clifford_algebra_quaternion.to_quaternion_of_quaternion CliffordAlgebraQuaternion.toQuaternion_ofQuaternion

/-- The clifford algebra over `CliffordAlgebraQuaternion.Q c‚ÇÅ c‚ÇÇ` is isomorphic as an `R`-algebra
to `‚Ñç[R,c‚ÇÅ,c‚ÇÇ]`. -/
@[simps!]
protected def equiv : CliffordAlgebra (Q c‚ÇÅ c‚ÇÇ) ‚âÉ‚Çê[R] ‚Ñç[R,c‚ÇÅ,c‚ÇÇ] :=
  AlgEquiv.ofAlgHom toQuaternion ofQuaternion toQuaternion_comp_ofQuaternion
    ofQuaternion_comp_toQuaternion
#align clifford_algebra_quaternion.equiv CliffordAlgebraQuaternion.equiv

/-- The quaternion conjugate maps to the "clifford conjugate" (aka `star`). -/
@[simp]
theorem ofQuaternion_star (q : ‚Ñç[R,c‚ÇÅ,c‚ÇÇ]) : ofQuaternion (star q) = star (ofQuaternion q) :=
  CliffordAlgebraQuaternion.equiv.injective <| by
    rw [equiv_apply, equiv_apply, toQuaternion_star, toQuaternion_ofQuaternion,
      toQuaternion_ofQuaternion]
#align clifford_algebra_quaternion.of_quaternion_star CliffordAlgebraQuaternion.ofQuaternion_star

-- this name is too short for us to want it visible after `open CliffordAlgebraQuaternion`
--attribute [protected] Q -- Porting note: removed

end CliffordAlgebraQuaternion

/-! ### The clifford algebra isomorphic to the dual numbers -/


namespace CliffordAlgebraDualNumber

open scoped DualNumber

open DualNumber TrivSqZeroExt

variable {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M]

theorem Œπ_mul_Œπ (r‚ÇÅ r‚ÇÇ) : Œπ (0 : QuadraticForm R R) r‚ÇÅ * Œπ (0 : QuadraticForm R R) r‚ÇÇ = 0 := by
  rw [‚Üê mul_one r‚ÇÅ, ‚Üê mul_one r‚ÇÇ, ‚Üê smul_eq_mul R, ‚Üê smul_eq_mul R, LinearMap.map_smul,
    LinearMap.map_smul, smul_mul_smul, Œπ_sq_scalar, QuadraticForm.zero_apply, RingHom.map_zero,
    smul_zero]
#align clifford_algebra_dual_number.Œπ_mul_Œπ CliffordAlgebraDualNumber.Œπ_mul_Œπ

/-- The clifford algebra over a 1-dimensional vector space with 0 quadratic form is isomorphic to
the dual numbers. -/
protected def equiv : CliffordAlgebra (0 : QuadraticForm R R) ‚âÉ‚Çê[R] R[Œµ] :=
  AlgEquiv.ofAlgHom
    (CliffordAlgebra.lift (0 : QuadraticForm R R) ‚ü®inrHom R _, fun m => inr_mul_inr _ m m‚ü©)
    (DualNumber.lift ‚ü®Œπ (R := R) _ 1, Œπ_mul_Œπ (1 : R) 1‚ü©)
    (by ext : 1; simp)
        -- ‚ä¢ ‚Üë(AlgHom.comp (‚Üë(CliffordAlgebra.lift 0) { val := inrHom R R, property := (_ ‚Ä¶
                 -- üéâ no goals
    (by ext : 2; simp)
        -- ‚ä¢ ‚Üë(LinearMap.comp (AlgHom.toLinearMap (AlgHom.comp (‚ÜëDualNumber.lift { val := ‚Ä¶
                 -- üéâ no goals
#align clifford_algebra_dual_number.equiv CliffordAlgebraDualNumber.equiv

@[simp]
theorem equiv_Œπ (r : R) : CliffordAlgebraDualNumber.equiv (Œπ (R := R) _ r) = r ‚Ä¢ Œµ :=
  (lift_Œπ_apply _ _ r).trans (inr_eq_smul_eps _)
#align clifford_algebra_dual_number.equiv_Œπ CliffordAlgebraDualNumber.equiv_Œπ

@[simp]
theorem equiv_symm_eps :
    CliffordAlgebraDualNumber.equiv.symm (eps : R[Œµ]) = Œπ (0 : QuadraticForm R R) 1 :=
  -- Porting note: Original proof was `DualNumber.lift_apply_eps _`
  DualNumber.lift_apply_eps (R := R) (A := CliffordAlgebra (0 : QuadraticForm R R)) _
#align clifford_algebra_dual_number.equiv_symm_eps CliffordAlgebraDualNumber.equiv_symm_eps

end CliffordAlgebraDualNumber
