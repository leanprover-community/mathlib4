/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Utensil Song, Eric Wieser
-/
import Mathlib.GroupTheory.GroupAction.ConjAct
import Mathlib.Algebra.Star.Unitary
import Mathlib.LinearAlgebra.CliffordAlgebra.Star
import Mathlib.LinearAlgebra.CliffordAlgebra.Even
import Mathlib.LinearAlgebra.CliffordAlgebra.Inversion

/-!
# The Pin group and the Spin group

In this file we define `lipschitz`, `pinGroup` and `spinGroup` and show they form a group.

## Main definitions

* `lipschitz`: the Lipschitz group with a quadratic form.
* `pinGroup`: the Pin group defined as the infimum of `lipschitz` and `unitary`.
* `spinGroup`: the Spin group defined as the infimum of `pinGroup` and `CliffordAlgebra.even`.

## Implementation Notes

Here are some discussion about the latent ambiguity of definition :
https://mathoverflow.net/q/427881/172242 and https://mathoverflow.net/q/251288/172242

The definition of the Lipschitz group `{ğ‘¥ âˆˆ ğ¶ğ‘™(ğ‘‰,ğ‘) â”‚ ğ‘¥ ğ‘–ğ‘  ğ‘–ğ‘›ğ‘£ğ‘’ğ‘Ÿğ‘¡ğ‘–ğ‘ğ‘™ğ‘’ ğ‘ğ‘›ğ‘‘ ğ‘¥ğ‘£ğ‘¥â»Â¹âˆˆ ğ‘‰}` is given by:
â€¢ Fulton, W. and Harris, J., 2004. Representation theory. New York: Springer, p.chapter 20.
â€¢ https://en.wikipedia.org/wiki/Clifford_algebra#Lipschitz_group
But they presumably form a group only in finite dimensions. So we define `lipschitz` with closure of
all the invertible elements in the form of `Î¹ Q m`, and we show this definition is at least as large
as the other definition (See `mem_lipschitz_conj_act_le` and `mem_lipschitz_involute_le`).
The reverse statement presumably being true only in finite dimensions.

## TODO

Try to show the reverse statement is true in finite dimensions.
-/

variable {R : Type*} [CommRing R]

variable {M : Type*} [AddCommGroup M] [Module R M]

variable {Q : QuadraticForm R M}

section Pin

open CliffordAlgebra MulAction

open scoped Pointwise

/-- `lipschitz` is the subgroup closure of all the invertible elements in the form of `Î¹ Q m`
where `Î¹` is the canonical linear map `M â†’â‚—[R] CliffordAlgebra Q`. -/
def lipschitz (Q : QuadraticForm R M) :=
  Subgroup.closure ((â†‘) â»Â¹' Set.range (Î¹ Q) : Set (CliffordAlgebra Q)Ë£)
#align lipschitz lipschitz

/-- If x is in `lipschitz Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
theorem mem_lipschitz_conjAct_le {x : (CliffordAlgebra Q)Ë£} (hx : x âˆˆ lipschitz Q)
    [Invertible (2 : R)] :
    ConjAct.toConjAct x â€¢ LinearMap.range (Î¹ Q) â‰¤ LinearMap.range (Î¹ Q) := by
  unfold lipschitz at hx
  apply Subgroup.closure_induction'' hx
  Â· rintro x âŸ¨a, haâŸ© y âŸ¨z, âŸ¨âŸ¨b, hbâŸ©, hzâŸ©âŸ©
    letI := x.invertible
    letI : Invertible (Î¹ Q a) := by rwa [ha]
    letI : Invertible (Q a) := invertibleOfInvertibleÎ¹ Q a
    rw [LinearMap.mem_range]
    simp only [HSMul.hSMul, SMul.smul, DistribMulAction.toLinearMap_apply,
                ConjAct.ofConjAct_toConjAct, SetLike.mem_coe] at hz
    subst hb
    suffices âˆƒ y : M, Î¹ Q y = Î¹ Q a * Î¹ Q b * â…Ÿ (Î¹ Q a) by simp_all only [invOf_units]
    rw [Î¹_mul_Î¹_mul_invOf_Î¹ Q a b]
    use ((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b)
  Â· rintro x âŸ¨a, haâŸ© y âŸ¨z, âŸ¨âŸ¨b, hbâŸ©, hzâŸ©âŸ©
    letI := x.invertible
    letI : Invertible (Î¹ Q a) := by rwa [ha]
    letI : Invertible (Q a) := invertibleOfInvertibleÎ¹ Q a
    rw [LinearMap.mem_range]
    simp only [HSMul.hSMul, SMul.smul, DistribMulAction.toLinearMap_apply,
      ConjAct.ofConjAct_toConjAct, ConjAct.toConjAct_inv, map_inv, inv_inv] at hz
    subst hb
    suffices âˆƒ y : M, Î¹ Q y = â…Ÿ (Î¹ Q a) * Î¹ Q b * Î¹ Q a by
      simp_all only [invOf_units]
      done
    rw [invOf_Î¹_mul_Î¹_mul_Î¹ Q a b]
    use ((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b)
  Â· simp only [ConjAct.toConjAct_one, (one_smul _ (LinearMap.range (Î¹ Q))), le_refl]
  Â· intro x y hx1 hy1 z hz
    simp only [ConjAct.toConjAct_mul] at hz
    suffices
      (ConjAct.toConjAct x * ConjAct.toConjAct y) â€¢ LinearMap.range (Î¹ Q) â‰¤ LinearMap.range (Î¹ Q) by
        exact this hz
    rintro m âŸ¨a, âŸ¨b, hbâŸ©, haâŸ©
    simp only [HSMul.hSMul, SMul.smul, DistribMulAction.toLinearMap_apply, Units.val_mul,
      mul_inv_rev] at ha
    subst hb
    have hb : â†‘x * (â†‘y * (Î¹ Q b) * â†‘yâ»Â¹) * â†‘xâ»Â¹ = m := by
      simp only [mul_assoc, â† ha, map_mul, ConjAct.ofConjAct_toConjAct, Units.val_mul, mul_inv_rev]
    have hy2 : â†‘y * (Î¹ Q) b * â†‘yâ»Â¹ âˆˆ ConjAct.toConjAct y â€¢ LinearMap.range (Î¹ Q) := by
      simp only [HSMul.hSMul, SMul.smul, exists_exists_eq_and, exists_apply_eq_apply,
        Submodule.mem_map, LinearMap.mem_range, DistribMulAction.toLinearMap_apply,
        ConjAct.ofConjAct_toConjAct]
    specialize hy1 hy2
    have hx2 : â†‘x * (â†‘y * (Î¹ Q) b * â†‘yâ»Â¹) * â†‘xâ»Â¹ âˆˆ ConjAct.toConjAct x â€¢ LinearMap.range (Î¹ Q) := by
      simp only [HSMul.hSMul, SMul.smul, Units.mul_left_inj, Units.mul_right_inj,
        exists_exists_eq_and, Submodule.mem_map, LinearMap.mem_range,
        DistribMulAction.toLinearMap_apply, ConjAct.ofConjAct_toConjAct]
      exact hy1
    specialize hx1 hx2
    rwa [hb] at hx1
#align mem_lipschitz_conj_act_le mem_lipschitz_conjAct_le

/-- This is another version of `mem_lipschitz_conj_act_le` which uses `involute`.-/
theorem mem_lipschitz_involute_le [Invertible (2 : R)]
    {x : (CliffordAlgebra Q)Ë£} (hx : x âˆˆ lipschitz Q) (b : M) :
      involute (Q := Q) â†‘x * Î¹ Q b * â†‘xâ»Â¹ âˆˆ LinearMap.range (Î¹ Q) := by
  revert b
  unfold lipschitz at hx
  apply Subgroup.closure_induction'' hx
  Â· rintro x âŸ¨a, haâŸ© b
    letI := x.invertible
    letI : Invertible (Î¹ Q a) := by rwa [ha]
    letI : Invertible (Q a) := invertibleOfInvertibleÎ¹ Q a
    rw [LinearMap.mem_range, â† invOf_units x]
    simp_rw [â† ha, involute_Î¹]
    refine'
      âŸ¨-((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b), by
        simp only [map_neg, neg_mul, Î¹_mul_Î¹_mul_invOf_Î¹ Q a b]âŸ©
    done
  Â· rintro x âŸ¨a, haâŸ© b
    letI := x.invertible
    letI : Invertible (Î¹ Q a) := by rwa [ha]
    letI : Invertible (Q a) := invertibleOfInvertibleÎ¹ Q a
    letI := invertibleNeg (Î¹ Q a)
    letI := Invertible.map (involute : CliffordAlgebra Q â†’â‚[R] CliffordAlgebra Q) (Î¹ Q a)
    rw [LinearMap.mem_range, â† invOf_units x, inv_inv]
    simp_rw [â† ha, map_invOf, involute_Î¹, invOf_neg]
    refine'
      âŸ¨-((â…Ÿ (Q a) * QuadraticForm.polar Q a b) â€¢ a - b), by
        simp only [map_neg, neg_mul, invOf_Î¹_mul_Î¹_mul_Î¹ Q a b]âŸ©
    done
  Â· simp only [Units.val_one, map_one, one_mul, inv_one, mul_one, LinearMap.mem_range,
      exists_apply_eq_apply, forall_const]
  Â· intro y z hy hz b
    simp only [Units.val_mul, map_mul, mul_inv_rev, LinearMap.mem_range]
    let âŸ¨z', hz'âŸ© := hz b
    let âŸ¨y', hy'âŸ© := hy z'
    suffices
        âˆƒ c : M, (Î¹ Q) c = involute (Q := Q) â†‘y * (involute (Q := Q) â†‘z * (Î¹ Q) b * â†‘zâ»Â¹) * â†‘yâ»Â¹ by
      obtain âŸ¨p, hpâŸ© := this
      refine' âŸ¨p, by simp only [hp, mul_assoc]âŸ©
    rw [â† hz']
    use y'
    done
#align mem_lipschitz_involute_le mem_lipschitz_involute_le

theorem coe_mem_lipschitz_iff_mem {x : (CliffordAlgebra Q)Ë£} :
    â†‘x âˆˆ (lipschitz Q).toSubmonoid.map (Units.coeHom <| CliffordAlgebra Q) â†” x âˆˆ lipschitz Q := by
  simp only [Submonoid.mem_map, Subgroup.mem_toSubmonoid, Units.coeHom_apply, exists_prop]
  norm_cast
  exact exists_eq_right
#align coe_mem_lipschitz_iff_mem coe_mem_lipschitz_iff_mem

/-- `pinGroup Q` is defined as the infimum of `lipschitz Q` and `unitary (CliffordAlgebra Q)`.
See `mem_iff`. -/
def pinGroup (Q : QuadraticForm R M) : Submonoid (CliffordAlgebra Q) :=
  (lipschitz Q).toSubmonoid.map (Units.coeHom <| CliffordAlgebra Q) âŠ“ unitary _
#align pin_group pinGroup

namespace pinGroup

/-- An element is in `pinGroup Q` if and only if it is in `lipschitz Q` and `unitary`. -/
theorem mem_iff {x : CliffordAlgebra Q} :
    x âˆˆ pinGroup Q â†”
      x âˆˆ (lipschitz Q).toSubmonoid.map (Units.coeHom <| CliffordAlgebra Q) âˆ§
        x âˆˆ unitary (CliffordAlgebra Q) :=
  Iff.rfl
#align pin_group.mem_iff pinGroup.mem_iff

theorem mem_lipschitz {x : CliffordAlgebra Q} (hx : x âˆˆ pinGroup Q) :
    x âˆˆ (lipschitz Q).toSubmonoid.map (Units.coeHom <| CliffordAlgebra Q) :=
  hx.1
#align pin_group.mem_lipschitz pinGroup.mem_lipschitz

theorem mem_unitary {x : CliffordAlgebra Q} (hx : x âˆˆ pinGroup Q) :
    x âˆˆ unitary (CliffordAlgebra Q) :=
  hx.2
#align pin_group.mem_unitary pinGroup.mem_unitary

theorem units_mem_iff {x : (CliffordAlgebra Q)Ë£} :
    â†‘x âˆˆ pinGroup Q â†” x âˆˆ lipschitz Q âˆ§ â†‘x âˆˆ unitary (CliffordAlgebra Q) := by
  rw [mem_iff, coe_mem_lipschitz_iff_mem]
#align pin_group.units_mem_iff pinGroup.units_mem_iff

theorem units_mem_lipschitz {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ pinGroup Q) : x âˆˆ lipschitz Q :=
  (units_mem_iff.1 hx).1
#align pin_group.units_mem_lipschitz pinGroup.units_mem_lipschitz

/-- If x is in `pinGroup Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
theorem units_mem_conjAct_le {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ pinGroup Q)
    [Invertible (2 : R)] : ConjAct.toConjAct x â€¢ LinearMap.range (Î¹ Q) â‰¤ LinearMap.range (Î¹ Q) :=
  mem_lipschitz_conjAct_le (units_mem_lipschitz hx)
#align pin_group.units_mem_conj_act_le pinGroup.units_mem_conjAct_le

/-- This is another version of `units_mem_conjAct_le` which uses `involute`. -/
theorem units_mem_involute_act_le {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ pinGroup Q)
    [Invertible (2 : R)] (y : M) : involute (Q := Q) â†‘x * Î¹ Q y * â†‘xâ»Â¹ âˆˆ LinearMap.range (Î¹ Q) :=
  mem_lipschitz_involute_le (units_mem_lipschitz hx) y
#align pin_group.units_mem_involute_act_le pinGroup.units_mem_involute_act_le

@[simp]
theorem star_hMul_self_of_mem {x : CliffordAlgebra Q} (hx : x âˆˆ pinGroup Q) : star x * x = 1 :=
  hx.2.1
#align pin_group.star_mul_self_of_mem pinGroup.star_hMul_self_of_mem

@[simp]
theorem hMul_star_self_of_mem {x : CliffordAlgebra Q} (hx : x âˆˆ pinGroup Q) : x * star x = 1 :=
  hx.2.2
#align pin_group.mul_star_self_of_mem pinGroup.hMul_star_self_of_mem

/-- See `star_mem_iff` for both directions. -/
theorem star_mem {x : CliffordAlgebra Q} (hx : x âˆˆ pinGroup Q) : star x âˆˆ pinGroup Q := by
  rw [mem_iff] at hx âŠ¢
  refine' âŸ¨_, unitary.star_mem hx.2âŸ©
  rcases hx with âŸ¨âŸ¨y, hyâ‚, hyâ‚‚âŸ©, _hxâ‚‚, hxâ‚ƒâŸ©
  simp only [Subgroup.coe_toSubmonoid, SetLike.mem_coe] at hyâ‚
  simp only [Units.coeHom_apply] at hyâ‚‚
  simp only [Submonoid.mem_map, Subgroup.mem_toSubmonoid, Units.coeHom_apply, exists_prop]
  refine' âŸ¨star y, _, by simp only [hyâ‚‚, Units.coe_star]âŸ©
  rw [â† hyâ‚‚] at hxâ‚ƒ
  have hyâ‚ƒ : y * star y = 1 := by
    rw [â† Units.eq_iff]
    simp only [hxâ‚ƒ, Units.val_mul, Units.coe_star, Units.val_one]
  apply_fun fun x => yâ»Â¹ * x at hyâ‚ƒ
  simp only [inv_mul_cancel_left, mul_one] at hyâ‚ƒ
  simp only [hyâ‚ƒ, hyâ‚, inv_mem_iff]
#align pin_group.star_mem pinGroup.star_mem

/-- An element is in `pinGroup Q` if and only if `star x` is in `pinGroup Q`.
See `star_mem` for only one direction. -/
@[simp]
theorem star_mem_iff {x : CliffordAlgebra Q} : star x âˆˆ pinGroup Q â†” x âˆˆ pinGroup Q := by
  refine' âŸ¨_, star_memâŸ©
  intro hx
  convert star_mem hx
  exact (star_star x).symm
#align pin_group.star_mem_iff pinGroup.star_mem_iff

instance : Star (pinGroup Q) :=
  âŸ¨fun x => âŸ¨star x, star_mem x.propâŸ©âŸ©

@[simp, norm_cast]
theorem coe_star {x : pinGroup Q} : â†‘(star x) = (star x : CliffordAlgebra Q) :=
  rfl
#align pin_group.coe_star pinGroup.coe_star

theorem coe_star_hMul_self (x : pinGroup Q) : (star x : CliffordAlgebra Q) * x = 1 :=
  star_hMul_self_of_mem x.prop
#align pin_group.coe_star_mul_self pinGroup.coe_star_hMul_self

theorem coe_hMul_star_self (x : pinGroup Q) : (x : CliffordAlgebra Q) * star x = 1 :=
  hMul_star_self_of_mem x.prop
#align pin_group.coe_mul_star_self pinGroup.coe_hMul_star_self

@[simp]
theorem star_hMul_self (x : pinGroup Q) : star x * x = 1 :=
  Subtype.ext <| coe_star_hMul_self x
#align pin_group.star_mul_self pinGroup.star_hMul_self

@[simp]
theorem hMul_star_self (x : pinGroup Q) : x * star x = 1 :=
  Subtype.ext <| coe_hMul_star_self x
#align pin_group.mul_star_self pinGroup.hMul_star_self

/-- `pinGroup Q` forms a group where the inverse is `star`. -/
instance : Group (pinGroup Q) :=
  { Submonoid.toMonoid _ with
    inv := star
    mul_left_inv := star_hMul_self }

instance : InvolutiveStar (pinGroup Q) :=
  âŸ¨fun _ => by
    ext; simp only [coe_star, star_star]
  âŸ©

instance : StarMul (pinGroup Q) :=
  âŸ¨fun _ _ => by ext; simp only [coe_star, Submonoid.coe_mul, star_mul]âŸ©

instance : Inhabited (pinGroup Q) :=
  âŸ¨1âŸ©

theorem star_eq_inv (x : pinGroup Q) : star x = xâ»Â¹ :=
  rfl
#align pin_group.star_eq_inv pinGroup.star_eq_inv

theorem star_eq_inv' : (star : pinGroup Q â†’ pinGroup Q) = Inv.inv :=
  rfl
#align pin_group.star_eq_inv' pinGroup.star_eq_inv'

/-- The elements in `pinGroup Q` embed into (CliffordAlgebra Q)Ë£. -/
@[simps]
def toUnits : pinGroup Q â†’* (CliffordAlgebra Q)Ë£
    where
  toFun x := âŸ¨x, â†‘xâ»Â¹, coe_hMul_star_self x, coe_star_hMul_self xâŸ©
  map_one' := Units.ext rfl
  map_mul' _x _y := Units.ext rfl
#align pin_group.to_units pinGroup.toUnits

theorem toUnits_injective : Function.Injective (toUnits : pinGroup Q â†’ (CliffordAlgebra Q)Ë£) :=
  fun _x _y h => Subtype.ext <| Units.ext_iff.mp h
#align pin_group.to_units_injective pinGroup.toUnits_injective

end pinGroup

end Pin

section Spin

open CliffordAlgebra MulAction

open scoped Pointwise

/-- `spinGroup Q` is defined as the infimum of `pinGroup Q` and `CliffordAlgebra.even Q`.
See `mem_iff`. -/
def spinGroup (Q : QuadraticForm R M) :=
  pinGroup Q âŠ“ (CliffordAlgebra.even Q).toSubring.toSubmonoid
#align spin_group spinGroup

namespace spinGroup

/-- An element is in `spinGroup Q` if and only if it is in `pinGroup Q` and `even Q`. -/
theorem mem_iff {x : CliffordAlgebra Q} : x âˆˆ spinGroup Q â†” x âˆˆ pinGroup Q âˆ§ x âˆˆ even Q :=
  Iff.rfl
#align spin_group.mem_iff spinGroup.mem_iff

theorem mem_pin {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : x âˆˆ pinGroup Q :=
  hx.1
#align spin_group.mem_pin spinGroup.mem_pin

theorem mem_even {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : x âˆˆ even Q :=
  hx.2
#align spin_group.mem_even spinGroup.mem_even

theorem units_mem_lipschitz {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ spinGroup Q) : x âˆˆ lipschitz Q :=
  pinGroup.units_mem_lipschitz (mem_pin hx)
#align spin_group.units_mem_lipschitz spinGroup.units_mem_lipschitz

/-- If x is in `spinGroup Q`, then `involute x` is equal to x.-/
theorem mem_involute_eq {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : involute x = x :=
  involute_eq_of_mem_even (mem_even hx)
#align spin_group.mem_involute_eq spinGroup.mem_involute_eq

theorem units_involute_act_eq_conjAct {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ spinGroup Q) (y : M) :
    involute (Q := Q) â†‘x * Î¹ Q y * â†‘xâ»Â¹ = ConjAct.toConjAct x â€¢ (Î¹ Q y) := by
  rw [mem_involute_eq hx, @ConjAct.units_smul_def, @ConjAct.ofConjAct_toConjAct]
#align spin_group.units_involute_act_eq_conj_act spinGroup.units_involute_act_eq_conjAct

/- If x is in `spinGroup Q`, then `(Î¹ Q).range` is closed under twisted conjugation. The reverse
statement presumably being true only in finite dimensions.-/
theorem units_mem_conjAct_le {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ spinGroup Q)
    [Invertible (2 : R)] : ConjAct.toConjAct x â€¢ LinearMap.range (Î¹ Q) â‰¤ LinearMap.range (Î¹ Q) :=
  mem_lipschitz_conjAct_le (units_mem_lipschitz hx)
#align spin_group.units_mem_conj_act_le spinGroup.units_mem_conjAct_le

/- This is another version of `units_mem_conjAct_le` which uses `involute`.-/
theorem units_mem_involute_act_le {x : (CliffordAlgebra Q)Ë£} (hx : â†‘x âˆˆ spinGroup Q)
    [Invertible (2 : R)] (y : M) : involute (Q := Q) â†‘x * Î¹ Q y * â†‘xâ»Â¹ âˆˆ LinearMap.range (Î¹ Q) :=
  mem_lipschitz_involute_le (units_mem_lipschitz hx) y
#align spin_group.units_mem_involute_act_le spinGroup.units_mem_involute_act_le

@[simp]
theorem star_hMul_self_of_mem {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : star x * x = 1 :=
  hx.1.2.1
#align spin_group.star_mul_self_of_mem spinGroup.star_hMul_self_of_mem

@[simp]
theorem hMul_star_self_of_mem {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : x * star x = 1 :=
  hx.1.2.2
#align spin_group.mul_star_self_of_mem spinGroup.hMul_star_self_of_mem

/-- See `star_mem_iff` for both directions. -/
theorem star_mem {x : CliffordAlgebra Q} (hx : x âˆˆ spinGroup Q) : star x âˆˆ spinGroup Q := by
  rw [mem_iff] at hx âŠ¢
  cases' hx with hxâ‚ hxâ‚‚
  refine' âŸ¨pinGroup.star_mem hxâ‚, _âŸ©
  dsimp only [CliffordAlgebra.even] at hxâ‚‚ âŠ¢
  simp only [Submodule.mem_toSubalgebra] at hxâ‚‚ âŠ¢
  simp only [star_def, reverse_mem_evenOdd_iff, involute_mem_evenOdd_iff, hxâ‚‚]
#align spin_group.star_mem spinGroup.star_mem

/-- An element is in `spinGroup Q` if and only if `star x` is in `spinGroup Q`.
See `star_mem` for only one direction.
-/
@[simp]
theorem star_mem_iff {x : CliffordAlgebra Q} : star x âˆˆ spinGroup Q â†” x âˆˆ spinGroup Q := by
  refine' âŸ¨_, star_memâŸ©
  intro hx
  convert star_mem hx
  exact (star_star x).symm
#align spin_group.star_mem_iff spinGroup.star_mem_iff

instance : Star (spinGroup Q) :=
  âŸ¨fun x => âŸ¨star x, star_mem x.propâŸ©âŸ©

@[simp, norm_cast]
theorem coe_star {x : spinGroup Q} : â†‘(star x) = (star x : CliffordAlgebra Q) :=
  rfl
#align spin_group.coe_star spinGroup.coe_star

theorem coe_star_hMul_self (x : spinGroup Q) : (star x : CliffordAlgebra Q) * x = 1 :=
  star_hMul_self_of_mem x.prop
#align spin_group.coe_star_mul_self spinGroup.coe_star_hMul_self

theorem coe_hMul_star_self (x : spinGroup Q) : (x : CliffordAlgebra Q) * star x = 1 :=
  hMul_star_self_of_mem x.prop
#align spin_group.coe_mul_star_self spinGroup.coe_hMul_star_self

@[simp]
theorem star_hMul_self (x : spinGroup Q) : star x * x = 1 :=
  Subtype.ext <| coe_star_hMul_self x
#align spin_group.star_mul_self spinGroup.star_hMul_self

@[simp]
theorem hMul_star_self (x : spinGroup Q) : x * star x = 1 :=
  Subtype.ext <| coe_hMul_star_self x
#align spin_group.mul_star_self spinGroup.hMul_star_self

/-- `spinGroup Q` forms a group where the inverse is `star`. -/
instance : Group (spinGroup Q) :=
  { Submonoid.toMonoid _ with
    inv := star
    mul_left_inv := star_hMul_self }

instance : InvolutiveStar (spinGroup Q) :=
  âŸ¨fun _ => by ext; simp only [coe_star, star_star]âŸ©

instance : StarMul (spinGroup Q) :=
  âŸ¨fun _ _ => by ext; simp only [coe_star, Submonoid.coe_mul, star_mul]âŸ©

instance : Inhabited (spinGroup Q) :=
  âŸ¨1âŸ©

theorem star_eq_inv (x : spinGroup Q) : star x = xâ»Â¹ :=
  rfl
#align spin_group.star_eq_inv spinGroup.star_eq_inv

theorem star_eq_inv' : (star : spinGroup Q â†’ spinGroup Q) = Inv.inv :=
  rfl
#align spin_group.star_eq_inv' spinGroup.star_eq_inv'

/-- The elements in `spinGroup Q` embed into (CliffordAlgebra Q)Ë£. -/
@[simps]
def toUnits : spinGroup Q â†’* (CliffordAlgebra Q)Ë£
    where
  toFun x := âŸ¨x, â†‘xâ»Â¹, coe_hMul_star_self x, coe_star_hMul_self xâŸ©
  map_one' := Units.ext rfl
  map_mul' _x _y := Units.ext rfl
#align spin_group.to_units spinGroup.toUnits

theorem toUnits_injective : Function.Injective (toUnits : spinGroup Q â†’ (CliffordAlgebra Q)Ë£) :=
  fun _x _y h => Subtype.ext <| Units.ext_iff.mp h
#align spin_group.to_units_injective spinGroup.toUnits_injective

end spinGroup

end Spin
