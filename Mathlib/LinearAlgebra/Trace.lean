/-
Copyright (c) 2019 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes HÃ¶lzl, Patrick Massot, Casper Putz, Anne Baanen, Antoine Labelle
-/
import Mathlib.LinearAlgebra.Matrix.ToLin
import Mathlib.LinearAlgebra.Matrix.Trace
import Mathlib.LinearAlgebra.Contraction
import Mathlib.LinearAlgebra.TensorProductBasis
import Mathlib.LinearAlgebra.FreeModule.StrongRankCondition
import Mathlib.LinearAlgebra.FreeModule.Finite.Rank
import Mathlib.LinearAlgebra.Projection

#align_import linear_algebra.trace from "leanprover-community/mathlib"@"4cf7ca0e69e048b006674cf4499e5c7d296a89e0"

/-!
# Trace of a linear map

This file defines the trace of a linear map.

See also `LinearAlgebra/Matrix/Trace.lean` for the trace of a matrix.

## Tags

linear_map, trace, diagonal

-/


noncomputable section

universe u v w

namespace LinearMap

open BigOperators

open Matrix

open FiniteDimensional

open TensorProduct

section

variable (R : Type u) [CommSemiring R] {M : Type v} [AddCommMonoid M] [Module R M]

variable {Î¹ : Type w} [DecidableEq Î¹] [Fintype Î¹]

variable {Îº : Type*} [DecidableEq Îº] [Fintype Îº]

variable (b : Basis Î¹ R M) (c : Basis Îº R M)

/-- The trace of an endomorphism given a basis. -/
def traceAux : (M â†’â‚—[R] M) â†’â‚—[R] R :=
  Matrix.traceLinearMap Î¹ R R âˆ˜â‚— â†‘(LinearMap.toMatrix b b)
#align linear_map.trace_aux LinearMap.traceAux

-- Can't be `simp` because it would cause a loop.
theorem traceAux_def (b : Basis Î¹ R M) (f : M â†’â‚—[R] M) :
    traceAux R b f = Matrix.trace (LinearMap.toMatrix b b f) :=
  rfl
#align linear_map.trace_aux_def LinearMap.traceAux_def

theorem traceAux_eq : traceAux R b = traceAux R c :=
  LinearMap.ext fun f =>
    calc
      Matrix.trace (LinearMap.toMatrix b b f) =
          Matrix.trace (LinearMap.toMatrix b b ((LinearMap.id.comp f).comp LinearMap.id)) := by
        rw [LinearMap.id_comp, LinearMap.comp_id]
        -- ğŸ‰ no goals
      _ = Matrix.trace (LinearMap.toMatrix c b LinearMap.id * LinearMap.toMatrix c c f *
          LinearMap.toMatrix b c LinearMap.id) := by
        rw [LinearMap.toMatrix_comp _ c, LinearMap.toMatrix_comp _ c]
        -- ğŸ‰ no goals
      _ = Matrix.trace (LinearMap.toMatrix c c f * LinearMap.toMatrix b c LinearMap.id *
          LinearMap.toMatrix c b LinearMap.id) := by
        rw [Matrix.mul_assoc, Matrix.trace_mul_comm]
        -- ğŸ‰ no goals
      _ = Matrix.trace (LinearMap.toMatrix c c ((f.comp LinearMap.id).comp LinearMap.id)) := by
        rw [LinearMap.toMatrix_comp _ b, LinearMap.toMatrix_comp _ c]
        -- ğŸ‰ no goals
      _ = Matrix.trace (LinearMap.toMatrix c c f) := by rw [LinearMap.comp_id, LinearMap.comp_id]
                                                        -- ğŸ‰ no goals
#align linear_map.trace_aux_eq LinearMap.traceAux_eq

open Classical

variable (M)

/-- Trace of an endomorphism independent of basis. -/
def trace : (M â†’â‚—[R] M) â†’â‚—[R] R :=
  if H : âˆƒ s : Finset M, Nonempty (Basis s R M) then traceAux R H.choose_spec.some else 0
#align linear_map.trace LinearMap.trace

variable {M}

/-- Auxiliary lemma for `trace_eq_matrix_trace`. -/
theorem trace_eq_matrix_trace_of_finset {s : Finset M} (b : Basis s R M) (f : M â†’â‚—[R] M) :
    trace R M f = Matrix.trace (LinearMap.toMatrix b b f) := by
  have : âˆƒ s : Finset M, Nonempty (Basis s R M) := âŸ¨s, âŸ¨bâŸ©âŸ©
  -- âŠ¢ â†‘(trace R M) f = Matrix.trace (â†‘(toMatrix b b) f)
  rw [trace, dif_pos this, â† traceAux_def]
  -- âŠ¢ â†‘(traceAux R (Nonempty.some (_ : Nonempty (Basis { x // x âˆˆ Exists.choose th â€¦
  congr 1
  -- âŠ¢ traceAux R (Nonempty.some (_ : Nonempty (Basis { x // x âˆˆ Exists.choose this â€¦
  apply traceAux_eq
  -- ğŸ‰ no goals
#align linear_map.trace_eq_matrix_trace_of_finset LinearMap.trace_eq_matrix_trace_of_finset

theorem trace_eq_matrix_trace (f : M â†’â‚—[R] M) :
    trace R M f = Matrix.trace (LinearMap.toMatrix b b f) := by
  rw [trace_eq_matrix_trace_of_finset R b.reindexFinsetRange, â† traceAux_def, â† traceAux_def,
    traceAux_eq R b b.reindexFinsetRange]
#align linear_map.trace_eq_matrix_trace LinearMap.trace_eq_matrix_trace

theorem trace_mul_comm (f g : M â†’â‚—[R] M) : trace R M (f * g) = trace R M (g * f) :=
  if H : âˆƒ s : Finset M, Nonempty (Basis s R M) then by
    let âŸ¨s, âŸ¨bâŸ©âŸ© := H
    -- âŠ¢ â†‘(trace R M) (f * g) = â†‘(trace R M) (g * f)
    simp_rw [trace_eq_matrix_trace R b, LinearMap.toMatrix_mul]
    -- âŠ¢ Matrix.trace (â†‘(toMatrix b b) f * â†‘(toMatrix b b) g) = Matrix.trace (â†‘(toMat â€¦
    apply Matrix.trace_mul_comm
    -- ğŸ‰ no goals
  else by rw [trace, dif_neg H, LinearMap.zero_apply, LinearMap.zero_apply]
          -- ğŸ‰ no goals
#align linear_map.trace_mul_comm LinearMap.trace_mul_comm

lemma trace_mul_cycle (f g h : M â†’â‚—[R] M) :
    trace R M (f * g * h) = trace R M (h * f * g) := by
  rw [LinearMap.trace_mul_comm, â† mul_assoc]
  -- ğŸ‰ no goals

lemma trace_mul_cycle' (f g h : M â†’â‚—[R] M) :
    trace R M (f * (g * h)) = trace R M (h * (f * g)) := by
  rw [â† mul_assoc, LinearMap.trace_mul_comm]
  -- ğŸ‰ no goals

/-- The trace of an endomorphism is invariant under conjugation -/
@[simp]
theorem trace_conj (g : M â†’â‚—[R] M) (f : (M â†’â‚—[R] M)Ë£) :
    trace R M (â†‘f * g * â†‘fâ»Â¹) = trace R M g := by
  rw [trace_mul_comm]
  -- âŠ¢ â†‘(trace R M) (â†‘fâ»Â¹ * (â†‘f * g)) = â†‘(trace R M) g
  simp
  -- ğŸ‰ no goals
#align linear_map.trace_conj LinearMap.trace_conj

end

section

variable {R : Type*} [CommRing R] {M : Type*} [AddCommGroup M] [Module R M]

variable (N P : Type*) [AddCommGroup N] [Module R N] [AddCommGroup P] [Module R P]

variable {Î¹ : Type*}

/-- The trace of a linear map correspond to the contraction pairing under the isomorphism
 `End(M) â‰ƒ M* âŠ— M`-/
theorem trace_eq_contract_of_basis [Finite Î¹] (b : Basis Î¹ R M) :
    LinearMap.trace R M âˆ˜â‚— dualTensorHom R M M = contractLeft R M := by
  classical
    cases nonempty_fintype Î¹
    apply Basis.ext (Basis.tensorProduct (Basis.dualBasis b) b)
    rintro âŸ¨i, jâŸ©
    simp only [Function.comp_apply, Basis.tensorProduct_apply, Basis.coe_dualBasis, coe_comp]
    rw [trace_eq_matrix_trace R b, toMatrix_dualTensorHom]
    by_cases hij : i = j
    Â· rw [hij]
      simp
    rw [Matrix.StdBasisMatrix.trace_zero j i (1 : R) hij]
    simp [Finsupp.single_eq_pi_single, hij]
#align linear_map.trace_eq_contract_of_basis LinearMap.trace_eq_contract_of_basis

/-- The trace of a linear map correspond to the contraction pairing under the isomorphism
 `End(M) â‰ƒ M* âŠ— M`-/
theorem trace_eq_contract_of_basis' [Fintype Î¹] [DecidableEq Î¹] (b : Basis Î¹ R M) :
    LinearMap.trace R M = contractLeft R M âˆ˜â‚— (dualTensorHomEquivOfBasis b).symm.toLinearMap := by
  simp [LinearEquiv.eq_comp_toLinearMap_symm, trace_eq_contract_of_basis b]
  -- ğŸ‰ no goals
#align linear_map.trace_eq_contract_of_basis' LinearMap.trace_eq_contract_of_basis'

variable (R M)

variable [Module.Free R M] [Module.Finite R M] [Module.Free R N] [Module.Finite R N]
  [Module.Free R P] [Module.Finite R P]

/-- When `M` is finite free, the trace of a linear map correspond to the contraction pairing under
the isomorphism `End(M) â‰ƒ M* âŠ— M`-/
@[simp]
theorem trace_eq_contract : LinearMap.trace R M âˆ˜â‚— dualTensorHom R M M = contractLeft R M :=
  trace_eq_contract_of_basis (Module.Free.chooseBasis R M)
#align linear_map.trace_eq_contract LinearMap.trace_eq_contract

@[simp]
theorem trace_eq_contract_apply (x : Module.Dual R M âŠ—[R] M) :
    (LinearMap.trace R M) ((dualTensorHom R M M) x) = contractLeft R M x := by
  rw [â† comp_apply, trace_eq_contract]
  -- ğŸ‰ no goals
#align linear_map.trace_eq_contract_apply LinearMap.trace_eq_contract_apply

/-- When `M` is finite free, the trace of a linear map correspond to the contraction pairing under
the isomorphism `End(M) â‰ƒ M* âŠ— M`-/
theorem trace_eq_contract' :
    LinearMap.trace R M = contractLeft R M âˆ˜â‚— (dualTensorHomEquiv R M M).symm.toLinearMap :=
  trace_eq_contract_of_basis' (Module.Free.chooseBasis R M)
#align linear_map.trace_eq_contract' LinearMap.trace_eq_contract'

/-- The trace of the identity endomorphism is the dimension of the free module -/
@[simp]
theorem trace_one : trace R M 1 = (finrank R M : R) := by
  cases subsingleton_or_nontrivial R; simp
  -- âŠ¢ â†‘(trace R M) 1 = â†‘(finrank R M)
                                      -- âŠ¢ â†‘(trace R M) 1 = â†‘(finrank R M)
  have b := Module.Free.chooseBasis R M
  -- âŠ¢ â†‘(trace R M) 1 = â†‘(finrank R M)
  rw [trace_eq_matrix_trace R b, toMatrix_one, finrank_eq_card_chooseBasisIndex]
  -- âŠ¢ Matrix.trace 1 = â†‘(Fintype.card (Module.Free.ChooseBasisIndex R M))
  simp
  -- ğŸ‰ no goals
#align linear_map.trace_one LinearMap.trace_one

/-- The trace of the identity endomorphism is the dimension of the free module -/
@[simp]
theorem trace_id : trace R M id = (finrank R M : R) := by rw [â† one_eq_id, trace_one]
                                                          -- ğŸ‰ no goals
#align linear_map.trace_id LinearMap.trace_id

@[simp]
theorem trace_transpose : trace R (Module.Dual R M) âˆ˜â‚— Module.Dual.transpose = trace R M := by
  let e := dualTensorHomEquiv R M M
  -- âŠ¢ comp (trace R (Module.Dual R M)) Module.Dual.transpose = trace R M
  have h : Function.Surjective e.toLinearMap := e.surjective
  -- âŠ¢ comp (trace R (Module.Dual R M)) Module.Dual.transpose = trace R M
  refine' (cancel_right h).1 _
  -- âŠ¢ comp (comp (trace R (Module.Dual R M)) Module.Dual.transpose) â†‘e = comp (tra â€¦
  ext f m; simp
  -- âŠ¢ â†‘(â†‘(AlgebraTensorModule.curry (comp (comp (trace R (Module.Dual R M)) Module â€¦
           -- ğŸ‰ no goals
#align linear_map.trace_transpose LinearMap.trace_transpose

theorem trace_prodMap :
    trace R (M Ã— N) âˆ˜â‚— prodMapLinear R M N M N R =
      (coprod id id : R Ã— R â†’â‚—[R] R) âˆ˜â‚— prodMap (trace R M) (trace R N) := by
  let e := (dualTensorHomEquiv R M M).prod (dualTensorHomEquiv R N N)
  -- âŠ¢ comp (trace R (M Ã— N)) (prodMapLinear R M N M N R) = comp (coprod id id) (pr â€¦
  have h : Function.Surjective e.toLinearMap := e.surjective
  -- âŠ¢ comp (trace R (M Ã— N)) (prodMapLinear R M N M N R) = comp (coprod id id) (pr â€¦
  refine' (cancel_right h).1 _
  -- âŠ¢ comp (comp (trace R (M Ã— N)) (prodMapLinear R M N M N R)) â†‘e = comp (comp (c â€¦
  ext
  -- âŠ¢ â†‘(â†‘(AlgebraTensorModule.curry (comp (comp (comp (trace R (M Ã— N)) (prodMapLi â€¦
  Â· simp only [dualTensorHomEquiv, LinearEquiv.coe_prod, dualTensorHomEquivOfBasis_toLinearMap,
      AlgebraTensorModule.curry_apply, curry_apply, coe_restrictScalars, coe_comp, coe_inl,
      Function.comp_apply, prodMap_apply, map_zero, prodMapLinear_apply, dualTensorHom_prodMap_zero,
      trace_eq_contract_apply, contractLeft_apply, fst_apply, coprod_apply, id_coe, id_eq, add_zero]
  Â· simp only [dualTensorHomEquiv, LinearEquiv.coe_prod, dualTensorHomEquivOfBasis_toLinearMap,
      AlgebraTensorModule.curry_apply, curry_apply, coe_restrictScalars, coe_comp, coe_inr,
      Function.comp_apply, prodMap_apply, map_zero, prodMapLinear_apply, zero_prodMap_dualTensorHom,
      trace_eq_contract_apply, contractLeft_apply, snd_apply, coprod_apply, id_coe, id_eq, zero_add]
#align linear_map.trace_prod_map LinearMap.trace_prodMap

variable {R M N P}

theorem trace_prodMap' (f : M â†’â‚—[R] M) (g : N â†’â‚—[R] N) :
    trace R (M Ã— N) (prodMap f g) = trace R M f + trace R N g := by
  have h := ext_iff.1 (trace_prodMap R M N) (f, g)
  -- âŠ¢ â†‘(trace R (M Ã— N)) (prodMap f g) = â†‘(trace R M) f + â†‘(trace R N) g
  simp only [coe_comp, Function.comp_apply, prodMap_apply, coprod_apply, id_coe, id.def,
    prodMapLinear_apply] at h
  exact h
  -- ğŸ‰ no goals
#align linear_map.trace_prod_map' LinearMap.trace_prodMap'

variable (R M N P)

open TensorProduct Function

theorem trace_tensorProduct : comprâ‚‚ (mapBilinear R M N M N) (trace R (M âŠ— N)) =
    complâ‚â‚‚ (lsmul R R : R â†’â‚—[R] R â†’â‚—[R] R) (trace R M) (trace R N) := by
  apply
    (complâ‚â‚‚_inj (show Surjective (dualTensorHom R M M) from (dualTensorHomEquiv R M M).surjective)
        (show Surjective (dualTensorHom R N N) from (dualTensorHomEquiv R N N).surjective)).1
  ext f m g n
  -- âŠ¢ â†‘(â†‘(AlgebraTensorModule.curry (â†‘(â†‘(AlgebraTensorModule.curry (complâ‚â‚‚ (compr â€¦
  simp only [AlgebraTensorModule.curry_apply, toFun_eq_coe, TensorProduct.curry_apply,
    coe_restrictScalars, complâ‚â‚‚_apply, comprâ‚‚_apply, mapBilinear_apply,
    trace_eq_contract_apply, contractLeft_apply, lsmul_apply, Algebra.id.smul_eq_mul,
    map_dualTensorHom, dualDistrib_apply]
#align linear_map.trace_tensor_product LinearMap.trace_tensorProduct

theorem trace_comp_comm :
    comprâ‚‚ (llcomp R M N M) (trace R M) = comprâ‚‚ (llcomp R N M N).flip (trace R N) := by
  apply
    (complâ‚â‚‚_inj (show Surjective (dualTensorHom R N M) from (dualTensorHomEquiv R N M).surjective)
        (show Surjective (dualTensorHom R M N) from (dualTensorHomEquiv R M N).surjective)).1
  ext g m f n
  -- âŠ¢ â†‘(â†‘(AlgebraTensorModule.curry (â†‘(â†‘(AlgebraTensorModule.curry (complâ‚â‚‚ (compr â€¦
  simp only [AlgebraTensorModule.curry_apply, TensorProduct.curry_apply, coe_restrictScalars,
    complâ‚â‚‚_apply, comprâ‚‚_apply, llcomp_apply', comp_dualTensorHom, map_smulâ‚›â‚—, RingHom.id_apply,
    trace_eq_contract_apply, contractLeft_apply, smul_eq_mul, mul_comm, flip_apply]
#align linear_map.trace_comp_comm LinearMap.trace_comp_comm

variable {R M N P}

@[simp]
theorem trace_transpose' (f : M â†’â‚—[R] M) :
    trace R _ (Module.Dual.transpose (R := R) f) = trace R M f := by
  rw [â† comp_apply, trace_transpose]
  -- ğŸ‰ no goals
#align linear_map.trace_transpose' LinearMap.trace_transpose'

theorem trace_tensorProduct' (f : M â†’â‚—[R] M) (g : N â†’â‚—[R] N) :
    trace R (M âŠ— N) (map f g) = trace R M f * trace R N g := by
  have h := ext_iff.1 (ext_iff.1 (trace_tensorProduct R M N) f) g
  -- âŠ¢ â†‘(trace R (M âŠ—[R] N)) (TensorProduct.map f g) = â†‘(trace R M) f * â†‘(trace R N â€¦
  simp only [comprâ‚‚_apply, mapBilinear_apply, complâ‚â‚‚_apply, lsmul_apply,
    Algebra.id.smul_eq_mul] at h
  exact h
  -- ğŸ‰ no goals
#align linear_map.trace_tensor_product' LinearMap.trace_tensorProduct'

theorem trace_comp_comm' (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] M) :
    trace R M (g âˆ˜â‚— f) = trace R N (f âˆ˜â‚— g) := by
  have h := ext_iff.1 (ext_iff.1 (trace_comp_comm R M N) g) f
  -- âŠ¢ â†‘(trace R M) (comp g f) = â†‘(trace R N) (comp f g)
  simp only [llcomp_apply', comprâ‚‚_apply, flip_apply] at h
  -- âŠ¢ â†‘(trace R M) (comp g f) = â†‘(trace R N) (comp f g)
  exact h
  -- ğŸ‰ no goals
#align linear_map.trace_comp_comm' LinearMap.trace_comp_comm'

lemma trace_comp_cycle (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P) (h : P â†’â‚—[R] M) :
    trace R P (g âˆ˜â‚— f âˆ˜â‚— h) = trace R N (f âˆ˜â‚— h âˆ˜â‚— g) := by
  rw [trace_comp_comm', comp_assoc]
  -- ğŸ‰ no goals

lemma trace_comp_cycle' (f : M â†’â‚—[R] N) (g : N â†’â‚—[R] P) (h : P â†’â‚—[R] M) :
    trace R P ((g âˆ˜â‚— f) âˆ˜â‚— h) = trace R M ((h âˆ˜â‚— g) âˆ˜â‚— f) := by
  rw [trace_comp_comm', â† comp_assoc]
  -- ğŸ‰ no goals

@[simp]
theorem trace_conj' (f : M â†’â‚—[R] M) (e : M â‰ƒâ‚—[R] N) : trace R N (e.conj f) = trace R M f := by
  rw [e.conj_apply, trace_comp_comm', â† comp_assoc, LinearEquiv.comp_coe,
    LinearEquiv.self_trans_symm, LinearEquiv.refl_toLinearMap, id_comp]
#align linear_map.trace_conj' LinearMap.trace_conj'

theorem IsProj.trace {p : Submodule R M} {f : M â†’â‚—[R] M} (h : IsProj p f) [Module.Free R p]
    [Module.Finite R p] [Module.Free R (ker f)] [Module.Finite R (ker f)] :
    trace R M f = (finrank R p : R) := by
  rw [h.eq_conj_prodMap, trace_conj', trace_prodMap', trace_id, map_zero, add_zero]
  -- ğŸ‰ no goals
#align linear_map.is_proj.trace LinearMap.IsProj.trace

end

end LinearMap
