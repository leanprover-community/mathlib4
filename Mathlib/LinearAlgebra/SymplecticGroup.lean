/-
Copyright (c) 2022 Matej Penciak. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Matej Penciak, Moritz Doll, Fabien Clery
-/
import Mathlib.LinearAlgebra.Matrix.NonsingularInverse

#align_import linear_algebra.symplectic_group from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# The Symplectic Group

This file defines the symplectic group and proves elementary properties.

## Main Definitions

* `Matrix.J`: the canonical `2n Ã— 2n` skew-symmetric matrix
* `symplecticGroup`: the group of symplectic matrices

## TODO
* Every symplectic matrix has determinant 1.
* For `n = 1` the symplectic group coincides with the special linear group.
-/


open Matrix

variable {l R : Type*}

namespace Matrix

variable (l) [DecidableEq l] (R) [CommRing R]

section JMatrixLemmas

/-- The matrix defining the canonical skew-symmetric bilinear form. -/
def J : Matrix (Sum l l) (Sum l l) R :=
  Matrix.fromBlocks 0 (-1) 1 0
set_option linter.uppercaseLean3 false in
#align matrix.J Matrix.J

@[simp]
theorem J_transpose : (J l R)áµ€ = -J l R := by
  rw [J, fromBlocks_transpose, â† neg_one_smul R (fromBlocks _ _ _ _ : Matrix (l âŠ• l) (l âŠ• l) R),
    fromBlocks_smul, Matrix.transpose_zero, Matrix.transpose_one, transpose_neg]
  simp [fromBlocks]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.J_transpose Matrix.J_transpose

variable [Fintype l]

theorem J_squared : J l R * J l R = -1 := by
  rw [J, fromBlocks_multiply]
  -- âŠ¢ fromBlocks (0 * 0 + -1 * 1) (0 * -1 + -1 * 0) (1 * 0 + 0 * 1) (1 * -1 + 0 *  â€¦
  simp only [Matrix.zero_mul, Matrix.neg_mul, zero_add, neg_zero, Matrix.one_mul, add_zero]
  -- âŠ¢ fromBlocks (-1) 0 0 (-1) = -1
  rw [â† neg_zero, â† Matrix.fromBlocks_neg, â† fromBlocks_one]
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.J_squared Matrix.J_squared

theorem J_inv : (J l R)â»Â¹ = -J l R := by
  refine' Matrix.inv_eq_right_inv _
  -- âŠ¢ J l R * -J l R = 1
  rw [Matrix.mul_neg, J_squared]
  -- âŠ¢ - -1 = 1
  exact neg_neg 1
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.J_inv Matrix.J_inv

theorem J_det_mul_J_det : det (J l R) * det (J l R) = 1 := by
  rw [â† det_mul, J_squared, â† one_smul R (-1 : Matrix _ _ R), smul_neg, â† neg_smul, det_smul,
    Fintype.card_sum, det_one, mul_one]
  apply Even.neg_one_pow
  -- âŠ¢ Even (Fintype.card l + Fintype.card l)
  exact even_add_self _
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align matrix.J_det_mul_J_det Matrix.J_det_mul_J_det

theorem isUnit_det_J : IsUnit (det (J l R)) :=
  isUnit_iff_exists_inv.mpr âŸ¨det (J l R), J_det_mul_J_det _ _âŸ©
set_option linter.uppercaseLean3 false in
#align matrix.is_unit_det_J Matrix.isUnit_det_J

end JMatrixLemmas

variable [Fintype l]

/-- The group of symplectic matrices over a ring `R`. -/
def symplecticGroup : Submonoid (Matrix (Sum l l) (Sum l l) R) where
  carrier := { A | A * J l R * Aáµ€ = J l R }
  mul_mem' {a b} ha hb := by
    simp only [Set.mem_setOf_eq, transpose_mul] at *
    -- âŠ¢ a * b * J l R * (báµ€ * aáµ€) = J l R
    rw [â† Matrix.mul_assoc, a.mul_assoc, a.mul_assoc, hb]
    -- âŠ¢ a * J l R * aáµ€ = J l R
    exact ha
    -- ğŸ‰ no goals
  one_mem' := by simp
                 -- ğŸ‰ no goals
#align matrix.symplectic_group Matrix.symplecticGroup

end Matrix

namespace SymplecticGroup

variable [DecidableEq l] [Fintype l] [CommRing R]

open Matrix

theorem mem_iff {A : Matrix (Sum l l) (Sum l l) R} :
    A âˆˆ symplecticGroup l R â†” A * J l R * Aáµ€ = J l R := by simp [symplecticGroup]
                                                           -- ğŸ‰ no goals
#align symplectic_group.mem_iff SymplecticGroup.mem_iff

-- Porting note: Previous proof was `by infer_instance`
instance coeMatrix : Coe (symplecticGroup l R) (Matrix (Sum l l) (Sum l l) R) :=
  âŸ¨Subtype.valâŸ©
#align symplectic_group.coe_matrix SymplecticGroup.coeMatrix

section SymplecticJ

variable (l) (R)

theorem J_mem : J l R âˆˆ symplecticGroup l R := by
  rw [mem_iff, J, fromBlocks_multiply, fromBlocks_transpose, fromBlocks_multiply]
  -- âŠ¢ fromBlocks ((0 * 0 + -1 * 1) * 0áµ€ + (0 * -1 + -1 * 0) * (-1)áµ€) ((0 * 0 + -1  â€¦
  simp
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align symplectic_group.J_mem SymplecticGroup.J_mem

/-- The canonical skew-symmetric matrix as an element in the symplectic group. -/
def symJ : symplecticGroup l R :=
  âŸ¨J l R, J_mem l RâŸ©
set_option linter.uppercaseLean3 false in
#align symplectic_group.sym_J SymplecticGroup.symJ

variable {l} {R}

@[simp]
theorem coe_J : â†‘(symJ l R) = J l R := rfl
set_option linter.uppercaseLean3 false in
#align symplectic_group.coe_J SymplecticGroup.coe_J

end SymplecticJ

variable {A : Matrix (Sum l l) (Sum l l) R}

theorem neg_mem (h : A âˆˆ symplecticGroup l R) : -A âˆˆ symplecticGroup l R := by
  rw [mem_iff] at h âŠ¢
  -- âŠ¢ -A * J l R * (-A)áµ€ = J l R
  simp [h]
  -- ğŸ‰ no goals
#align symplectic_group.neg_mem SymplecticGroup.neg_mem

theorem symplectic_det (hA : A âˆˆ symplecticGroup l R) : IsUnit <| det A := by
  rw [isUnit_iff_exists_inv]
  -- âŠ¢ âˆƒ b, det A * b = 1
  use A.det
  -- âŠ¢ det A * det A = 1
  refine' (isUnit_det_J l R).mul_left_cancel _
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R) * 1
  rw [mul_one]
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R)
  rw [mem_iff] at hA
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R)
  apply_fun det at hA
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R)
  simp only [det_mul, det_transpose] at hA
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R)
  rw [mul_comm A.det, mul_assoc] at hA
  -- âŠ¢ det (J l R) * (det A * det A) = det (J l R)
  exact hA
  -- ğŸ‰ no goals
#align symplectic_group.symplectic_det SymplecticGroup.symplectic_det

theorem transpose_mem (hA : A âˆˆ symplecticGroup l R) : Aáµ€ âˆˆ symplecticGroup l R := by
  rw [mem_iff] at hA âŠ¢
  -- âŠ¢ Aáµ€ * J l R * Aáµ€áµ€ = J l R
  rw [transpose_transpose]
  -- âŠ¢ Aáµ€ * J l R * A = J l R
  have huA := symplectic_det hA
  -- âŠ¢ Aáµ€ * J l R * A = J l R
  have huAT : IsUnit Aáµ€.det := by
    rw [Matrix.det_transpose]
    exact huA
  calc
    Aáµ€ * J l R * A = (-Aáµ€) * (J l R)â»Â¹ * A := by
      rw [J_inv]
      simp
    _ = (-Aáµ€) * (A * J l R * Aáµ€)â»Â¹ * A := by rw [hA]
    _ = -(Aáµ€ * (Aáµ€â»Â¹ * (J l R)â»Â¹)) * Aâ»Â¹ * A := by
      simp only [Matrix.mul_inv_rev, Matrix.mul_assoc, Matrix.neg_mul]
    _ = -(J l R)â»Â¹ := by
      rw [mul_nonsing_inv_cancel_left _ _ huAT, nonsing_inv_mul_cancel_right _ _ huA]
    _ = J l R := by simp [J_inv]
#align symplectic_group.transpose_mem SymplecticGroup.transpose_mem

@[simp]
theorem transpose_mem_iff : Aáµ€ âˆˆ symplecticGroup l R â†” A âˆˆ symplecticGroup l R :=
  âŸ¨fun hA => by simpa using transpose_mem hA, transpose_memâŸ©
                -- ğŸ‰ no goals
#align symplectic_group.transpose_mem_iff SymplecticGroup.transpose_mem_iff

theorem mem_iff' : A âˆˆ symplecticGroup l R â†” Aáµ€ * J l R * A = J l R := by
  rw [â† transpose_mem_iff, mem_iff, transpose_transpose]
  -- ğŸ‰ no goals
#align symplectic_group.mem_iff' SymplecticGroup.mem_iff'

instance hasInv : Inv (symplecticGroup l R) where
  inv A := âŸ¨(-J l R) * (A : Matrix (Sum l l) (Sum l l) R)áµ€ * J l R,
      mul_mem (mul_mem (neg_mem <| J_mem _ _) <| transpose_mem A.2) <| J_mem _ _âŸ©

theorem coe_inv (A : symplecticGroup l R) : (â†‘Aâ»Â¹ : Matrix _ _ _) = (-J l R) * (â†‘A)áµ€ * J l R := rfl
#align symplectic_group.coe_inv SymplecticGroup.coe_inv

theorem inv_left_mul_aux (hA : A âˆˆ symplecticGroup l R) : -(J l R * Aáµ€ * J l R * A) = 1 :=
  calc
    -(J l R * Aáµ€ * J l R * A) = (-J l R) * (Aáµ€ * J l R * A) := by
      simp only [Matrix.mul_assoc, Matrix.neg_mul]
      -- ğŸ‰ no goals
    _ = (-J l R) * J l R := by
      rw [mem_iff'] at hA
      -- âŠ¢ -J l R * (Aáµ€ * J l R * A) = -J l R * J l R
      rw [hA]
      -- ğŸ‰ no goals
    _ = (-1 : R) â€¢ (J l R * J l R) := by simp only [Matrix.neg_mul, neg_smul, one_smul]
                                         -- ğŸ‰ no goals
    _ = (-1 : R) â€¢ (-1 : Matrix _ _ _) := by rw [J_squared]
                                             -- ğŸ‰ no goals
    _ = 1 := by simp only [neg_smul_neg, one_smul]
                -- ğŸ‰ no goals
#align symplectic_group.inv_left_mul_aux SymplecticGroup.inv_left_mul_aux

theorem coe_inv' (A : symplecticGroup l R) : (â†‘Aâ»Â¹ : Matrix (Sum l l) (Sum l l) R) = (â†‘A)â»Â¹ := by
  refine' (coe_inv A).trans (inv_eq_left_inv _).symm
  -- âŠ¢ -J l R * (â†‘A)áµ€ * J l R * â†‘A = 1
  simp [inv_left_mul_aux, coe_inv]
  -- ğŸ‰ no goals
#align symplectic_group.coe_inv' SymplecticGroup.coe_inv'

theorem inv_eq_symplectic_inv (A : Matrix (Sum l l) (Sum l l) R) (hA : A âˆˆ symplecticGroup l R) :
    Aâ»Â¹ = (-J l R) * Aáµ€ * J l R :=
  inv_eq_left_inv (by simp only [Matrix.neg_mul, inv_left_mul_aux hA])
                      -- ğŸ‰ no goals
#align symplectic_group.inv_eq_symplectic_inv SymplecticGroup.inv_eq_symplectic_inv

instance : Group (symplecticGroup l R) :=
  { SymplecticGroup.hasInv, Submonoid.toMonoid _ with
    mul_left_inv := fun A => by
      apply Subtype.ext
      -- âŠ¢ â†‘(Aâ»Â¹ * A) = â†‘1
      simp only [Submonoid.coe_one, Submonoid.coe_mul, Matrix.neg_mul, coe_inv]
      -- âŠ¢ -(J l R * (â†‘A)áµ€ * J l R * â†‘A) = 1
      exact inv_left_mul_aux A.2 }
      -- ğŸ‰ no goals

end SymplecticGroup
