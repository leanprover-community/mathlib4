/-
Copyright (c) 2023 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.Trace
import Mathlib.LinearAlgebra.FreeModule.PID

/-!
# Linear maps of modules with coefficients in a principal ideal domain

Since a submodule of a free module over a PID is free, certain constructions which are often
developed only for vector spaces may be generalised to any module with coefficients in a PID.

This file is a location for such results and exists to avoid making large parts of the linear
algebra import hierarchy have to depend on the theory of PIDs.

## Main results:
 * `LinearMap.trace_restrict_eq_of_forall_mem`

-/

open BigOperators

namespace LinearMap

variable {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M]
  [Module.Finite R M] [Module.Free R M]

/-- If a linear endomorphism of a (finite, free) module `M` takes values in a submodule `p âŠ† M`,
then the trace of its restriction to `p` is equal to its trace on `M`. -/
lemma trace_restrict_eq_of_forall_mem [IsDomain R] [IsPrincipalIdealRing R]
    (p : Submodule R M) (f : M â†’â‚—[R] M)
    (hf : âˆ€ x, f x âˆˆ p) (hf' : âˆ€ x âˆˆ p, f x âˆˆ p := fun x _ â†¦ hf x) :
    trace R p (f.restrict hf') = trace R M f := by
  let Î¹ := Module.Free.ChooseBasisIndex R M
  -- âŠ¢ â†‘(trace R { x // x âˆˆ p }) (restrict f hf') = â†‘(trace R M) f
  obtain âŸ¨n, snf : Basis.SmithNormalForm p Î¹ nâŸ© := p.smithNormalForm (Module.Free.chooseBasis R M)
  -- âŠ¢ â†‘(trace R { x // x âˆˆ p }) (restrict f hf') = â†‘(trace R M) f
  rw [trace_eq_matrix_trace R snf.bM, trace_eq_matrix_trace R snf.bN]
  -- âŠ¢ Matrix.trace (â†‘(toMatrix snf.bN snf.bN) (restrict f hf')) = Matrix.trace (â†‘( â€¦
  set A : Matrix (Fin n) (Fin n) R := toMatrix snf.bN snf.bN (f.restrict hf')
  -- âŠ¢ Matrix.trace A = Matrix.trace (â†‘(toMatrix snf.bM snf.bM) f)
  set B : Matrix Î¹ Î¹ R := toMatrix snf.bM snf.bM f
  -- âŠ¢ Matrix.trace A = Matrix.trace B
  have aux : âˆ€ i, B i i â‰  0 â†’ i âˆˆ Set.range snf.f := fun i hi â†¦ by
    contrapose! hi; exact snf.repr_eq_zero_of_nmem_range âŸ¨_, (hf _)âŸ© hi
  change âˆ‘ i, A i i = âˆ‘ i, B i i
  -- âŠ¢ âˆ‘ i : Fin n, A i i = âˆ‘ i : Î¹, B i i
  rw [â† Finset.sum_filter_of_ne (p := fun j â†¦ j âˆˆ Set.range snf.f) (by simpa using aux)]
  -- âŠ¢ âˆ‘ i : Fin n, A i i = âˆ‘ x in Finset.filter (fun j => j âˆˆ Set.range â†‘snf.f) Fi â€¦
  simp
  -- ğŸ‰ no goals

end LinearMap
