/-
Copyright (c) 2020 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.FieldTheory.Minpoly.Field

#align_import linear_algebra.eigenspace.minpoly from "leanprover-community/mathlib"@"c3216069e5f9369e6be586ccbfcde2592b3cec92"

/-!
# Eigenvalues are the roots of the minimal polynomial.

## Tags

eigenvalue, minimal polynomial
-/


universe u v w

namespace Module

namespace End

open Polynomial FiniteDimensional

open scoped Polynomial

variable {K : Type v} {V : Type w} [Field K] [AddCommGroup V] [Module K V]

theorem eigenspace_aeval_polynomial_degree_1 (f : End K V) (q : K[X]) (hq : degree q = 1) :
    eigenspace f (-q.coeff 0 / q.leadingCoeff) = LinearMap.ker (aeval f q) :=
  calc
    eigenspace f (-q.coeff 0 / q.leadingCoeff)
    _ = LinearMap.ker (q.leadingCoeff â€¢ f - algebraMap K (End K V) (-q.coeff 0)) := by
          rw [eigenspace_div]
          -- âŠ¢ leadingCoeff q â‰  0
          intro h
          -- âŠ¢ False
          rw [leadingCoeff_eq_zero_iff_deg_eq_bot.1 h] at hq
          -- âŠ¢ False
          cases hq
          -- ğŸ‰ no goals
    _ = LinearMap.ker (aeval f (C q.leadingCoeff * X + C (q.coeff 0))) := by
          rw [C_mul', aeval_def]; simp [algebraMap, Algebra.toRingHom]
          -- âŠ¢ LinearMap.ker (leadingCoeff q â€¢ f - â†‘(algebraMap K (End K V)) (-coeff q 0))  â€¦
                                  -- ğŸ‰ no goals
    _ = LinearMap.ker (aeval f q) := by rwa [â† eq_X_add_C_of_degree_eq_one]
                                        -- ğŸ‰ no goals
#align module.End.eigenspace_aeval_polynomial_degree_1 Module.End.eigenspace_aeval_polynomial_degree_1

theorem ker_aeval_ring_hom'_unit_polynomial (f : End K V) (c : K[X]Ë£) :
    LinearMap.ker (aeval f (c : K[X])) = âŠ¥ := by
  rw [Polynomial.eq_C_of_degree_eq_zero (degree_coe_units c)]
  -- âŠ¢ LinearMap.ker (â†‘(aeval f) (â†‘C (coeff (â†‘c) 0))) = âŠ¥
  simp only [aeval_def, evalâ‚‚_C]
  -- âŠ¢ LinearMap.ker (â†‘(algebraMap K (End K V)) (coeff (â†‘c) 0)) = âŠ¥
  apply ker_algebraMap_end
  -- âŠ¢ coeff (â†‘c) 0 â‰  0
  apply coeff_coe_units_zero_ne_zero c
  -- ğŸ‰ no goals
#align module.End.ker_aeval_ring_hom'_unit_polynomial Module.End.ker_aeval_ring_hom'_unit_polynomial

theorem aeval_apply_of_hasEigenvector {f : End K V} {p : K[X]} {Î¼ : K} {x : V}
    (h : f.HasEigenvector Î¼ x) : aeval f p x = p.eval Î¼ â€¢ x := by
  refine' p.induction_on _ _ _
  Â· intro a; simp [Module.algebraMap_end_apply]
    -- âŠ¢ â†‘(â†‘(aeval f) (â†‘C a)) x = eval Î¼ (â†‘C a) â€¢ x
             -- ğŸ‰ no goals
  Â· intro p q hp hq; simp [hp, hq, add_smul]
    -- âŠ¢ â†‘(â†‘(aeval f) (p + q)) x = eval Î¼ (p + q) â€¢ x
                     -- ğŸ‰ no goals
  Â· intro n a hna
    -- âŠ¢ â†‘(â†‘(aeval f) (â†‘C a * X ^ (n + 1))) x = eval Î¼ (â†‘C a * X ^ (n + 1)) â€¢ x
    rw [mul_comm, pow_succ, mul_assoc, AlgHom.map_mul, LinearMap.mul_apply, mul_comm, hna]
    -- âŠ¢ â†‘(â†‘(aeval f) X) (eval Î¼ (â†‘C a * X ^ n) â€¢ x) = eval Î¼ (X * (â†‘C a * X ^ n)) â€¢ x
    simp only [mem_eigenspace_iff.1 h.1, smul_smul, aeval_X, eval_mul, eval_C, eval_pow, eval_X,
      LinearMap.map_smulâ‚›â‚—, RingHom.id_apply, mul_comm]
#align module.End.aeval_apply_of_has_eigenvector Module.End.aeval_apply_of_hasEigenvector

theorem isRoot_of_hasEigenvalue {f : End K V} {Î¼ : K} (h : f.HasEigenvalue Î¼) :
    (minpoly K f).IsRoot Î¼ := by
  rcases(Submodule.ne_bot_iff _).1 h with âŸ¨w, âŸ¨H, ne0âŸ©âŸ©
  -- âŠ¢ IsRoot (minpoly K f) Î¼
  refine' Or.resolve_right (smul_eq_zero.1 _) ne0
  -- âŠ¢ eval Î¼ (minpoly K f) â€¢ w = 0
  simp [â† aeval_apply_of_hasEigenvector âŸ¨H, ne0âŸ©, minpoly.aeval K f]
  -- ğŸ‰ no goals
#align module.End.is_root_of_has_eigenvalue Module.End.isRoot_of_hasEigenvalue

variable [FiniteDimensional K V] (f : End K V)

variable {f} {Î¼ : K}

theorem hasEigenvalue_of_isRoot (h : (minpoly K f).IsRoot Î¼) : f.HasEigenvalue Î¼ := by
  cases' dvd_iff_isRoot.2 h with p hp
  -- âŠ¢ HasEigenvalue f Î¼
  rw [HasEigenvalue, eigenspace]
  -- âŠ¢ LinearMap.ker (f - â†‘(algebraMap K (End K V)) Î¼) â‰  âŠ¥
  intro con
  -- âŠ¢ False
  cases' (LinearMap.isUnit_iff_ker_eq_bot _).2 con with u hu
  -- âŠ¢ False
  have p_ne_0 : p â‰  0 := by
    intro con
    apply minpoly.ne_zero f.isIntegral
    rw [hp, con, mul_zero]
  have : (aeval f) p = 0 := by
    have h_aeval := minpoly.aeval K f
    revert h_aeval
    simp [hp, â† hu]
  have h_deg := minpoly.degree_le_of_ne_zero K f p_ne_0 this
  -- âŠ¢ False
  rw [hp, degree_mul, degree_X_sub_C, Polynomial.degree_eq_natDegree p_ne_0] at h_deg
  -- âŠ¢ False
  norm_cast at h_deg
  -- âŠ¢ False
  linarith
  -- ğŸ‰ no goals
#align module.End.has_eigenvalue_of_is_root Module.End.hasEigenvalue_of_isRoot

theorem hasEigenvalue_iff_isRoot : f.HasEigenvalue Î¼ â†” (minpoly K f).IsRoot Î¼ :=
  âŸ¨isRoot_of_hasEigenvalue, hasEigenvalue_of_isRootâŸ©
#align module.End.has_eigenvalue_iff_is_root Module.End.hasEigenvalue_iff_isRoot

/-- An endomorphism of a finite-dimensional vector space has finitely many eigenvalues. -/
noncomputable instance (f : End K V) : Fintype f.Eigenvalues :=
  Set.Finite.fintype <| show {Î¼ | eigenspace f Î¼ â‰  âŠ¥}.Finite by
    have h : minpoly K f â‰  0 := minpoly.ne_zero f.isIntegral
    -- âŠ¢ Set.Finite {Î¼ | eigenspace f Î¼ â‰  âŠ¥}
    convert (minpoly K f).rootSet_finite K
    -- âŠ¢ {Î¼ | eigenspace f Î¼ â‰  âŠ¥} = rootSet (minpoly K f) K
    ext Î¼
    -- âŠ¢ Î¼ âˆˆ {Î¼ | eigenspace f Î¼ â‰  âŠ¥} â†” Î¼ âˆˆ rootSet (minpoly K f) K
    -- Porting note: was the below, but this applied unwanted simp lemmas
    -- ```
    -- classical simp [Polynomial.rootSet_def, Polynomial.mem_roots h, â† hasEigenvalue_iff_isRoot,
    --   HasEigenvalue]
    -- ```
    rw [Set.mem_setOf_eq, â† HasEigenvalue, hasEigenvalue_iff_isRoot, mem_rootSet_of_ne h, IsRoot,
      coe_aeval_eq_eval]

end End

end Module
