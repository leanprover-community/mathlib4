/-
Copyright (c) 2020 Alexander Bentkamp. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp
-/
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.FieldTheory.IsAlgClosed.Spectrum

#align_import linear_algebra.eigenspace.is_alg_closed from "leanprover-community/mathlib"@"6b0169218d01f2837d79ea2784882009a0da1aa1"

/-!
# Eigenvectors and eigenvalues over algebraically closed fields.

* Every linear operator on a vector space over an algebraically closed field has an eigenvalue.
* The generalized eigenvectors span the entire vector space.

## References

* [Sheldon Axler, *Linear Algebra Done Right*][axler2015]
* https://en.wikipedia.org/wiki/Eigenvalues_and_eigenvectors

## Tags

eigenspace, eigenvector, eigenvalue, eigen
-/


universe u v w

namespace Module

namespace End

open FiniteDimensional

variable {K : Type v} {V : Type w} [Field K] [AddCommGroup V] [Module K V]

-- This is Lemma 5.21 of [axler2015], although we are no longer following that proof.
/-- Every linear operator on a vector space over an algebraically closed field has
    an eigenvalue. -/
theorem exists_eigenvalue [IsAlgClosed K] [FiniteDimensional K V] [Nontrivial V] (f : End K V) :
    ‚àÉ c : K, f.HasEigenvalue c := by
  simp_rw [hasEigenvalue_iff_mem_spectrum]
  -- ‚ä¢ ‚àÉ c, c ‚àà spectrum K f
  exact spectrum.nonempty_of_isAlgClosed_of_finiteDimensional K f
  -- üéâ no goals
#align module.End.exists_eigenvalue Module.End.exists_eigenvalue

noncomputable instance [IsAlgClosed K] [FiniteDimensional K V] [Nontrivial V] (f : End K V) :
    Inhabited f.Eigenvalues :=
  ‚ü®‚ü®f.exists_eigenvalue.choose, f.exists_eigenvalue.choose_spec‚ü©‚ü©

/-- The generalized eigenvectors span the entire vector space (Lemma 8.21 of [axler2015]). -/
theorem iSup_generalizedEigenspace_eq_top [IsAlgClosed K] [FiniteDimensional K V] (f : End K V) :
    ‚®Ü (Œº : K) (k : ‚Ñï), f.generalizedEigenspace Œº k = ‚ä§ := by
  -- We prove the claim by strong induction on the dimension of the vector space.
  induction' h_dim : finrank K V using Nat.strong_induction_on with n ih generalizing V
  -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
  cases' n with n
  -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
  -- If the vector space is 0-dimensional, the result is trivial.
  ¬∑ rw [‚Üê top_le_iff]
    -- ‚ä¢ ‚ä§ ‚â§ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k
    simp only [finrank_eq_zero.1 (Eq.trans (finrank_top _ _) h_dim), bot_le]
    -- üéâ no goals
  -- Otherwise the vector space is nontrivial.
  ¬∑ haveI : Nontrivial V := finrank_pos_iff.1 (by rw [h_dim]; apply Nat.zero_lt_succ)
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- Hence, `f` has an eigenvalue `Œº‚ÇÄ`.
    obtain ‚ü®Œº‚ÇÄ, hŒº‚ÇÄ‚ü© : ‚àÉ Œº‚ÇÄ, f.HasEigenvalue Œº‚ÇÄ := exists_eigenvalue f
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- We define `ES` to be the generalized eigenspace
    let ES := f.generalizedEigenspace Œº‚ÇÄ (finrank K V)
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- and `ER` to be the generalized eigenrange.
    let ER := f.generalizedEigenrange Œº‚ÇÄ (finrank K V)
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- `f` maps `ER` into itself.
    have h_f_ER : ‚àÄ x : V, x ‚àà ER ‚Üí f x ‚àà ER := fun x hx =>
      map_generalizedEigenrange_le (Submodule.mem_map_of_mem hx)
    -- Therefore, we can define the restriction `f'` of `f` to `ER`.
    let f' : End K ER := f.restrict h_f_ER
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- The dimension of `ES` is positive
    have h_dim_ES_pos : 0 < finrank K ES := by
      dsimp only
      rw [h_dim]
      apply pos_finrank_generalizedEigenspace_of_hasEigenvalue hŒº‚ÇÄ (Nat.zero_lt_succ n)
    -- and the dimensions of `ES` and `ER` add up to `finrank K V`.
    have h_dim_add : finrank K ER + finrank K ES = finrank K V := by
      apply LinearMap.finrank_range_add_finrank_ker
    -- Therefore the dimension `ER` mus be smaller than `finrank K V`.
    have h_dim_ER : finrank K ER < n.succ := by linarith
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- This allows us to apply the induction hypothesis on `ER`:
    have ih_ER : ‚®Ü (Œº : K) (k : ‚Ñï), f'.generalizedEigenspace Œº k = ‚ä§ :=
      ih (finrank K ER) h_dim_ER f' rfl
    -- The induction hypothesis gives us a statement about subspaces of `ER`. We can transfer this
    -- to a statement about subspaces of `V` via `submodule.subtype`:
    have ih_ER' : ‚®Ü (Œº : K) (k : ‚Ñï), (f'.generalizedEigenspace Œº k).map ER.subtype = ER := by
      simp only [(Submodule.map_iSup _ _).symm, ih_ER, Submodule.map_subtype_top ER]
    -- Moreover, every generalized eigenspace of `f'` is contained in the corresponding generalized
    -- eigenspace of `f`.
    have hff' :
      ‚àÄ Œº k, (f'.generalizedEigenspace Œº k).map ER.subtype ‚â§ f.generalizedEigenspace Œº k := by
      intros
      rw [generalizedEigenspace_restrict]
      apply Submodule.map_comap_le
    -- It follows that `ER` is contained in the span of all generalized eigenvectors.
    have hER : ER ‚â§ ‚®Ü (Œº : K) (k : ‚Ñï), f.generalizedEigenspace Œº k := by
      rw [‚Üê ih_ER']
      exact iSup‚ÇÇ_mono hff'
    -- `ES` is contained in this span by definition.
    have hES : ES ‚â§ ‚®Ü (Œº : K) (k : ‚Ñï), f.generalizedEigenspace Œº k :=
      le_trans (le_iSup (fun k => f.generalizedEigenspace Œº‚ÇÄ k) (finrank K V))
        (le_iSup (fun Œº : K => ‚®Ü k : ‚Ñï, f.generalizedEigenspace Œº k) Œº‚ÇÄ)
    -- Moreover, we know that `ER` and `ES` are disjoint.
    have h_disjoint : Disjoint ER ES := generalized_eigenvec_disjoint_range_ker f Œº‚ÇÄ
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    -- Since the dimensions of `ER` and `ES` add up to the dimension of `V`, it follows that the
    -- span of all generalized eigenvectors is all of `V`.
    show ‚®Ü (Œº : K) (k : ‚Ñï), f.generalizedEigenspace Œº k = ‚ä§
    -- ‚ä¢ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k = ‚ä§
    ¬∑ rw [‚Üê top_le_iff, ‚Üê Submodule.eq_top_of_disjoint ER ES h_dim_add h_disjoint]
      -- ‚ä¢ ER ‚äî ES ‚â§ ‚®Ü (Œº : K) (k : ‚Ñï), ‚Üë(generalizedEigenspace f Œº) k
      apply sup_le hER hES
      -- üéâ no goals
#align module.End.supr_generalized_eigenspace_eq_top Module.End.iSup_generalizedEigenspace_eq_top

end End

end Module
