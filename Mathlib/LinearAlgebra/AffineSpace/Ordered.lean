/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Algebra.CharP.Invertible
import Mathlib.Algebra.Order.Module.Synonym
import Mathlib.LinearAlgebra.AffineSpace.Midpoint
import Mathlib.LinearAlgebra.AffineSpace.Slope

/-!
# Ordered modules as affine spaces

In this file we prove some theorems about `slope` and `lineMap` in the case when the module `E`
acting on the codomain `PE` of a function is an ordered module over its domain `k`. We also prove
inequalities that can be used to link convexity of a function on an interval to monotonicity of the
slope, see section docstring below for details.

## Implementation notes

We do not introduce the notion of ordered affine spaces (yet?). Instead, we prove various theorems
for an ordered module interpreted as an affine space.

## Tags

affine space, ordered module, slope
-/


open AffineMap

variable {k E PE : Type*}

/-!
### Monotonicity of `lineMap`

In this section we prove that `lineMap a b r` is monotone (strictly or not) in its arguments if
other arguments belong to specific domains.
-/


section OrderedRing

variable [Ring k] [PartialOrder k] [IsOrderedRing k]
  [AddCommGroup E] [PartialOrder E] [IsOrderedAddMonoid E] [Module k E] [IsStrictOrderedModule k E]
variable {a a' b b' : E} {r r' : k}

theorem lineMap_mono_left (ha : a â‰¤ a') (hr : r â‰¤ 1) : lineMap a b r â‰¤ lineMap a' b r := by
  simp only [lineMap_apply_module]
  gcongr
  exact sub_nonneg.2 hr

theorem lineMap_strict_mono_left (ha : a < a') (hr : r < 1) : lineMap a b r < lineMap a' b r := by
  simp only [lineMap_apply_module]
  gcongr
  exact sub_pos.2 hr

omit [IsOrderedRing k] in
theorem lineMap_mono_right (hb : b â‰¤ b') (hr : 0 â‰¤ r) : lineMap a b r â‰¤ lineMap a b' r := by
  simp only [lineMap_apply_module]
  gcongr

omit [IsOrderedRing k] in
theorem lineMap_strict_mono_right (hb : b < b') (hr : 0 < r) : lineMap a b r < lineMap a b' r := by
  simp only [lineMap_apply_module]; gcongr

theorem lineMap_mono_endpoints (ha : a â‰¤ a') (hb : b â‰¤ b') (hâ‚€ : 0 â‰¤ r) (hâ‚ : r â‰¤ 1) :
    lineMap a b r â‰¤ lineMap a' b' r :=
  (lineMap_mono_left ha hâ‚).trans (lineMap_mono_right hb hâ‚€)

theorem lineMap_strict_mono_endpoints (ha : a < a') (hb : b < b') (hâ‚€ : 0 â‰¤ r) (hâ‚ : r â‰¤ 1) :
    lineMap a b r < lineMap a' b' r := by
  rcases hâ‚€.eq_or_lt with (rfl | hâ‚€); Â· simpa
  exact (lineMap_mono_left ha.le hâ‚).trans_lt (lineMap_strict_mono_right hb hâ‚€)

variable [PosSMulReflectLT k E]

theorem lineMap_lt_lineMap_iff_of_lt (h : r < r') : lineMap a b r < lineMap a b r' â†” a < b := by
  simp only [lineMap_apply_module]
  rw [â† lt_sub_iff_add_lt, add_sub_assoc, â† sub_lt_iff_lt_add', â† sub_smul, â† sub_smul,
    sub_sub_sub_cancel_left, smul_lt_smul_iff_of_pos_left (sub_pos.2 h)]

theorem left_lt_lineMap_iff_lt (h : 0 < r) : a < lineMap a b r â†” a < b :=
  Iff.trans (by rw [lineMap_apply_zero]) (lineMap_lt_lineMap_iff_of_lt h)

theorem lineMap_lt_left_iff_lt (h : 0 < r) : lineMap a b r < a â†” b < a :=
  left_lt_lineMap_iff_lt (E := Eáµ’áµˆ) h

theorem lineMap_lt_right_iff_lt (h : r < 1) : lineMap a b r < b â†” a < b :=
  Iff.trans (by rw [lineMap_apply_one]) (lineMap_lt_lineMap_iff_of_lt h)

theorem right_lt_lineMap_iff_lt (h : r < 1) : b < lineMap a b r â†” b < a :=
  lineMap_lt_right_iff_lt (E := Eáµ’áµˆ) h

end OrderedRing

section LinearOrderedRing

variable [Ring k] [LinearOrder k] [IsStrictOrderedRing k]
  [AddCommGroup E] [PartialOrder E] [IsOrderedAddMonoid E] [Module k E] [IsStrictOrderedModule k E]
  {a a' b b' : E} {r r' : k}

theorem lineMap_le_lineMap_iff_of_lt' (h : a < b) : lineMap a b r â‰¤ lineMap a b r' â†” r â‰¤ r' := by
  simp only [lineMap_apply_module']
  rw [add_le_add_iff_right, smul_le_smul_iff_of_pos_right (sub_pos.mpr h)]

theorem left_le_lineMap_iff_nonneg (h : a < b) : a â‰¤ lineMap a b r â†” 0 â‰¤ r := by
  rw [â† lineMap_le_lineMap_iff_of_lt' h, lineMap_apply_zero,]

theorem lineMap_le_left_iff_nonpos (h : a < b) : lineMap a b r â‰¤ a â†” r â‰¤ 0 := by
  rw [â† lineMap_le_lineMap_iff_of_lt' h, lineMap_apply_zero]

theorem right_le_lineMap_iff_one_le (h : a < b) : b â‰¤ lineMap a b r â†” 1 â‰¤ r := by
  rw [â† lineMap_le_lineMap_iff_of_lt' h, lineMap_apply_one]

theorem lineMap_le_right_iff_le_one (h : a < b) : lineMap a b r â‰¤ b â†” r â‰¤ 1 := by
  rw [â† lineMap_le_lineMap_iff_of_lt' h, lineMap_apply_one]

theorem lineMap_lt_lineMap_iff_of_lt' (h : a < b) : lineMap a b r < lineMap a b r' â†” r < r' := by
  simp only [lineMap_apply_module']
  rw [add_lt_add_iff_right, smul_lt_smul_iff_of_pos_right (sub_pos.mpr h)]

theorem left_lt_lineMap_iff_pos (h : a < b) : a < lineMap a b r â†” 0 < r := by
  rw [â† lineMap_lt_lineMap_iff_of_lt' h, lineMap_apply_zero]

theorem lineMap_lt_left_iff_neg (h : a < b) : lineMap a b r < a â†” r < 0 := by
  rw [â† lineMap_lt_lineMap_iff_of_lt' h, lineMap_apply_zero]

theorem right_lt_lineMap_iff_one_lt (h : a < b) : b < lineMap a b r â†” 1 < r  := by
  rw [â† lineMap_lt_lineMap_iff_of_lt' h, lineMap_apply_one]

theorem lineMap_lt_right_iff_lt_one (h : a < b) : lineMap a b r < b â†” r < 1 := by
  rw [â† lineMap_lt_lineMap_iff_of_lt' h, lineMap_apply_one]

theorem midpoint_le_midpoint [Invertible (2 : k)] (ha : a â‰¤ a') (hb : b â‰¤ b') :
    midpoint k a b â‰¤ midpoint k a' b' :=
  lineMap_mono_endpoints ha hb (invOf_nonneg.2 zero_le_two) <| invOf_le_one one_le_two

end LinearOrderedRing

section LinearOrderedField

variable [Field k] [LinearOrder k] [IsStrictOrderedRing k]
  [AddCommGroup E] [PartialOrder E] [IsOrderedAddMonoid E]
variable [Module k E] [IsStrictOrderedModule k E] [PosSMulReflectLE k E]

section

variable {a b : E} {r r' : k}

theorem lineMap_le_lineMap_iff_of_lt (h : r < r') : lineMap a b r â‰¤ lineMap a b r' â†” a â‰¤ b := by
  simp only [lineMap_apply_module]
  rw [â† le_sub_iff_add_le, add_sub_assoc, â† sub_le_iff_le_add', â† sub_smul, â† sub_smul,
    sub_sub_sub_cancel_left, smul_le_smul_iff_of_pos_left (sub_pos.2 h)]

theorem left_le_lineMap_iff_le (h : 0 < r) : a â‰¤ lineMap a b r â†” a â‰¤ b :=
  Iff.trans (by rw [lineMap_apply_zero]) (lineMap_le_lineMap_iff_of_lt h)

@[simp]
theorem left_le_midpoint : a â‰¤ midpoint k a b â†” a â‰¤ b :=
  left_le_lineMap_iff_le <| inv_pos.2 zero_lt_two

theorem lineMap_le_left_iff_le (h : 0 < r) : lineMap a b r â‰¤ a â†” b â‰¤ a :=
  left_le_lineMap_iff_le (E := Eáµ’áµˆ) h

@[simp]
theorem midpoint_le_left : midpoint k a b â‰¤ a â†” b â‰¤ a :=
  lineMap_le_left_iff_le <| inv_pos.2 zero_lt_two

theorem lineMap_le_right_iff_le (h : r < 1) : lineMap a b r â‰¤ b â†” a â‰¤ b :=
  Iff.trans (by rw [lineMap_apply_one]) (lineMap_le_lineMap_iff_of_lt h)

@[simp]
theorem midpoint_le_right : midpoint k a b â‰¤ b â†” a â‰¤ b := lineMap_le_right_iff_le two_inv_lt_one

theorem right_le_lineMap_iff_le (h : r < 1) : b â‰¤ lineMap a b r â†” b â‰¤ a :=
  lineMap_le_right_iff_le (E := Eáµ’áµˆ) h

@[simp]
theorem right_le_midpoint : b â‰¤ midpoint k a b â†” b â‰¤ a := right_le_lineMap_iff_le two_inv_lt_one

end

/-!
### Convexity and slope

Given an interval `[a, b]` and a point `c âˆˆ (a, b)`, `c = lineMap a b r`, there are a few ways to
say that the point `(c, f c)` is above/below the segment `[(a, f a), (b, f b)]`:

* compare `f c` to `lineMap (f a) (f b) r`;
* compare `slope f a c` to `slope f a b`;
* compare `slope f c b` to `slope f a b`;
* compare `slope f a c` to `slope f c b`.

In this section we prove equivalence of these four approaches. In order to make the statements more
readable, we introduce local notation `c = lineMap a b r`. Then we prove lemmas like

```
lemma map_le_lineMap_iff_slope_le_slope_left (h : 0 < r * (b - a)) :
    f c â‰¤ lineMap (f a) (f b) r â†” slope f a c â‰¤ slope f a b :=
```

For each inequality between `f c` and `lineMap (f a) (f b) r` we provide 3 lemmas:

* `*_left` relates it to an inequality on `slope f a c` and `slope f a b`;
* `*_right` relates it to an inequality on `slope f a b` and `slope f c b`;
* no-suffix version relates it to an inequality on `slope f a c` and `slope f c b`.

These inequalities can be used to restate `convexOn` in terms of monotonicity of the slope.
-/


variable {f : k â†’ E} {a b r : k}

local notation "c" => lineMap a b r

section
omit [IsStrictOrderedRing k]

/-- Given `c = lineMap a b r`, `a < c`, the point `(c, f c)` is non-strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a c â‰¤ slope f a b`. -/
theorem map_le_lineMap_iff_slope_le_slope_left (h : 0 < r * (b - a)) :
    f c â‰¤ lineMap (f a) (f b) r â†” slope f a c â‰¤ slope f a b := by
  rw [lineMap_apply, lineMap_apply, slope, slope, vsub_eq_sub, vsub_eq_sub, vsub_eq_sub,
    vadd_eq_add, vadd_eq_add, smul_eq_mul, add_sub_cancel_right, smul_sub, smul_sub, smul_sub,
    sub_le_iff_le_add, mul_inv_rev, mul_smul, mul_smul, â† smul_sub, â† smul_sub, â† smul_add,
    smul_smul, â† mul_inv_rev, inv_smul_le_iff_of_pos h, smul_smul,
    mul_inv_cancel_rightâ‚€ (right_ne_zero_of_mul h.ne'), smul_add,
    smul_inv_smulâ‚€ (left_ne_zero_of_mul h.ne')]

/-- Given `c = lineMap a b r`, `a < c`, the point `(c, f c)` is non-strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f a b â‰¤ slope f a c`. -/
theorem lineMap_le_map_iff_slope_le_slope_left (h : 0 < r * (b - a)) :
    lineMap (f a) (f b) r â‰¤ f c â†” slope f a b â‰¤ slope f a c :=
  map_le_lineMap_iff_slope_le_slope_left (E := Eáµ’áµˆ) (f := f) (a := a) (b := b) (r := r) h

/-- Given `c = lineMap a b r`, `a < c`, the point `(c, f c)` is strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a c < slope f a b`. -/
theorem map_lt_lineMap_iff_slope_lt_slope_left (h : 0 < r * (b - a)) :
    f c < lineMap (f a) (f b) r â†” slope f a c < slope f a b :=
  lt_iff_lt_of_le_iff_le' (lineMap_le_map_iff_slope_le_slope_left h)
    (map_le_lineMap_iff_slope_le_slope_left h)

/-- Given `c = lineMap a b r`, `a < c`, the point `(c, f c)` is strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f a b < slope f a c`. -/
theorem lineMap_lt_map_iff_slope_lt_slope_left (h : 0 < r * (b - a)) :
    lineMap (f a) (f b) r < f c â†” slope f a b < slope f a c :=
  map_lt_lineMap_iff_slope_lt_slope_left (E := Eáµ’áµˆ) (f := f) (a := a) (b := b) (r := r) h

/-- Given `c = lineMap a b r`, `c < b`, the point `(c, f c)` is non-strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a b â‰¤ slope f c b`. -/
theorem map_le_lineMap_iff_slope_le_slope_right (h : 0 < (1 - r) * (b - a)) :
    f c â‰¤ lineMap (f a) (f b) r â†” slope f a b â‰¤ slope f c b := by
  rw [â† lineMap_apply_one_sub, â† lineMap_apply_one_sub _ _ r]
  revert h; generalize 1 - r = r'; clear! r; intro h
  simp_rw [lineMap_apply, slope, vsub_eq_sub, vadd_eq_add, smul_eq_mul]
  rw [sub_add_eq_sub_sub_swap, sub_self, zero_sub, neg_mul_eq_mul_neg, neg_sub,
    le_inv_smul_iff_of_pos h, smul_smul, mul_inv_cancel_rightâ‚€, le_sub_comm, â† neg_sub (f b),
    smul_neg, neg_add_eq_sub]
  Â· exact right_ne_zero_of_mul h.ne'

/-- Given `c = lineMap a b r`, `c < b`, the point `(c, f c)` is non-strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f c b â‰¤ slope f a b`. -/
theorem lineMap_le_map_iff_slope_le_slope_right (h : 0 < (1 - r) * (b - a)) :
    lineMap (f a) (f b) r â‰¤ f c â†” slope f c b â‰¤ slope f a b :=
  map_le_lineMap_iff_slope_le_slope_right (E := Eáµ’áµˆ) (f := f) (a := a) (b := b) (r := r) h

/-- Given `c = lineMap a b r`, `c < b`, the point `(c, f c)` is strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a b < slope f c b`. -/
theorem map_lt_lineMap_iff_slope_lt_slope_right (h : 0 < (1 - r) * (b - a)) :
    f c < lineMap (f a) (f b) r â†” slope f a b < slope f c b :=
  lt_iff_lt_of_le_iff_le' (lineMap_le_map_iff_slope_le_slope_right h)
    (map_le_lineMap_iff_slope_le_slope_right h)

/-- Given `c = lineMap a b r`, `c < b`, the point `(c, f c)` is strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f c b < slope f a b`. -/
theorem lineMap_lt_map_iff_slope_lt_slope_right (h : 0 < (1 - r) * (b - a)) :
    lineMap (f a) (f b) r < f c â†” slope f c b < slope f a b :=
  map_lt_lineMap_iff_slope_lt_slope_right (E := Eáµ’áµˆ) (f := f) (a := a) (b := b) (r := r) h

end

/-- Given `c = lineMap a b r`, `a < c < b`, the point `(c, f c)` is non-strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a c â‰¤ slope f c b`. -/
theorem map_le_lineMap_iff_slope_le_slope (hab : a < b) (hâ‚€ : 0 < r) (hâ‚ : r < 1) :
    f c â‰¤ lineMap (f a) (f b) r â†” slope f a c â‰¤ slope f c b := by
  rw [map_le_lineMap_iff_slope_le_slope_left (mul_pos hâ‚€ (sub_pos.2 hab)), â†
    lineMap_slope_lineMap_slope_lineMap f a b r, right_le_lineMap_iff_le hâ‚]

/-- Given `c = lineMap a b r`, `a < c < b`, the point `(c, f c)` is non-strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f c b â‰¤ slope f a c`. -/
theorem lineMap_le_map_iff_slope_le_slope (hab : a < b) (hâ‚€ : 0 < r) (hâ‚ : r < 1) :
    lineMap (f a) (f b) r â‰¤ f c â†” slope f c b â‰¤ slope f a c :=
  map_le_lineMap_iff_slope_le_slope (E := Eáµ’áµˆ) hab hâ‚€ hâ‚

/-- Given `c = lineMap a b r`, `a < c < b`, the point `(c, f c)` is strictly below the
segment `[(a, f a), (b, f b)]` if and only if `slope f a c < slope f c b`. -/
theorem map_lt_lineMap_iff_slope_lt_slope (hab : a < b) (hâ‚€ : 0 < r) (hâ‚ : r < 1) :
    f c < lineMap (f a) (f b) r â†” slope f a c < slope f c b :=
  lt_iff_lt_of_le_iff_le' (lineMap_le_map_iff_slope_le_slope hab hâ‚€ hâ‚)
    (map_le_lineMap_iff_slope_le_slope hab hâ‚€ hâ‚)

/-- Given `c = lineMap a b r`, `a < c < b`, the point `(c, f c)` is strictly above the
segment `[(a, f a), (b, f b)]` if and only if `slope f c b < slope f a c`. -/
theorem lineMap_lt_map_iff_slope_lt_slope (hab : a < b) (hâ‚€ : 0 < r) (hâ‚ : r < 1) :
    lineMap (f a) (f b) r < f c â†” slope f c b < slope f a c :=
  map_lt_lineMap_iff_slope_lt_slope (E := Eáµ’áµˆ) hab hâ‚€ hâ‚

end LinearOrderedField


lemma slope_pos_iff {ð•œ} [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
    {f : ð•œ â†’ ð•œ} {xâ‚€ b : ð•œ} (hb : xâ‚€ < b) :
    0 < slope f xâ‚€ b â†” f xâ‚€ < f b := by
  simp [slope, hb]

lemma slope_pos_iff_gt {ð•œ} [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
    {f : ð•œ â†’ ð•œ} {xâ‚€ b : ð•œ} (hb : b < xâ‚€) :
    0 < slope f xâ‚€ b â†” f b < f xâ‚€ := by
  rw [slope_comm, slope_pos_iff hb]

lemma pos_of_slope_pos {ð•œ} [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
    {f : ð•œ â†’ ð•œ} {xâ‚€ b : ð•œ}
    (hb : xâ‚€ < b) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : 0 < f b := by
  simp_all [slope]

lemma neg_of_slope_pos {ð•œ} [Field ð•œ] [LinearOrder ð•œ] [IsStrictOrderedRing ð•œ]
    {f : ð•œ â†’ ð•œ} {xâ‚€ b : ð•œ}
    (hb : b < xâ‚€) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : f b < 0 := by
  rwa [slope_pos_iff_gt, hf] at hbf
  exact hb
