/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.AffineSpace.Independent
import Mathlib.LinearAlgebra.Basis

#align_import linear_algebra.affine_space.basis from "leanprover-community/mathlib"@"2de9c37fa71dde2f1c6feff19876dd6a7b1519f0"

/-!
# Affine bases and barycentric coordinates

Suppose `P` is an affine space modelled on the module `V` over the ring `k`, and `p : Î¹ â†’ P` is an
affine-independent family of points spanning `P`. Given this data, each point `q : P` may be written
uniquely as an affine combination: `q = wâ‚€ pâ‚€ + wâ‚ pâ‚ + â‹¯` for some (finitely-supported) weights
`wáµ¢`. For each `i : Î¹`, we thus have an affine map `P â†’áµƒ[k] k`, namely `q â†¦ wáµ¢`. This family of
maps is known as the family of barycentric coordinates. It is defined in this file.

## The construction

Fixing `i : Î¹`, and allowing `j : Î¹` to range over the values `j â‰  i`, we obtain a basis `báµ¢` of `V`
defined by `báµ¢ j = p j -áµ¥ p i`. Let `fáµ¢ j : V â†’â‚—[k] k` be the corresponding dual basis and let
`fáµ¢ = âˆ‘ j, fáµ¢ j : V â†’â‚—[k] k` be the corresponding "sum of all coordinates" form. Then the `i`th
barycentric coordinate of `q : P` is `1 - fáµ¢ (q -áµ¥ p i)`.

## Main definitions

 * `AffineBasis`: a structure representing an affine basis of an affine space.
 * `AffineBasis.coord`: the map `P â†’áµƒ[k] k` corresponding to `i : Î¹`.
 * `AffineBasis.coord_apply_eq`: the behaviour of `AffineBasis.coord i` on `p i`.
 * `AffineBasis.coord_apply_ne`: the behaviour of `AffineBasis.coord i` on `p j` when `j â‰  i`.
 * `AffineBasis.coord_apply`: the behaviour of `AffineBasis.coord i` on `p j` for general `j`.
 * `AffineBasis.coord_apply_combination`: the characterisation of `AffineBasis.coord i` in terms
    of affine combinations, i.e., `AffineBasis.coord i (wâ‚€ pâ‚€ + wâ‚ pâ‚ + â‹¯) = wáµ¢`.

## TODO

 * Construct the affine equivalence between `P` and `{ f : Î¹ â†’â‚€ k | f.sum = 1 }`.

-/


open Affine BigOperators

open Set

universe uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

/-- An affine basis is a family of affine-independent points whose span is the top subspace. -/
structure AffineBasis (Î¹ : Type uâ‚) (k : Type uâ‚‚) {V : Type uâ‚ƒ} (P : Type uâ‚„) [AddCommGroup V]
  [AffineSpace V P] [Ring k] [Module k V] where
  protected toFun : Î¹ â†’ P
  protected ind' : AffineIndependent k toFun
  protected tot' : affineSpan k (range toFun) = âŠ¤
#align affine_basis AffineBasis

variable {Î¹ Î¹' k V P : Type*} [AddCommGroup V] [AffineSpace V P]

namespace AffineBasis

section Ring

variable [Ring k] [Module k V] (b : AffineBasis Î¹ k P) {s : Finset Î¹} {i j : Î¹} (e : Î¹ â‰ƒ Î¹')

/-- The unique point in a single-point space is the simplest example of an affine basis. -/
instance : Inhabited (AffineBasis PUnit k PUnit) :=
  âŸ¨âŸ¨id, affineIndependent_of_subsingleton k id, by simpâŸ©âŸ©
                                                   -- ğŸ‰ no goals

instance funLike : FunLike (AffineBasis Î¹ k P) Î¹ fun _ => P where
  coe := AffineBasis.toFun
  coe_injective' f g h := by cases f; cases g; congr
                             -- âŠ¢ { toFun := toFunâœ, ind' := ind'âœ, tot' := tot'âœ } = g
                                      -- âŠ¢ { toFun := toFunâœÂ¹, ind' := ind'âœÂ¹, tot' := tot'âœÂ¹ } = { toFun := toFunâœ, in â€¦
                                               -- ğŸ‰ no goals
#align affine_basis.fun_like AffineBasis.funLike

@[ext]
theorem ext {bâ‚ bâ‚‚ : AffineBasis Î¹ k P} (h : (bâ‚ : Î¹ â†’ P) = bâ‚‚) : bâ‚ = bâ‚‚ :=
  FunLike.coe_injective h
#align affine_basis.ext AffineBasis.ext

theorem ind : AffineIndependent k b :=
  b.ind'
#align affine_basis.ind AffineBasis.ind

theorem tot : affineSpan k (range b) = âŠ¤ :=
  b.tot'
#align affine_basis.tot AffineBasis.tot

protected theorem nonempty : Nonempty Î¹ :=
  not_isEmpty_iff.mp fun hÎ¹ => by
    simpa only [@range_eq_empty _ _ hÎ¹, AffineSubspace.span_empty, bot_ne_top] using b.tot
    -- ğŸ‰ no goals
#align affine_basis.nonempty AffineBasis.nonempty

/-- Composition of an affine basis and an equivalence of index types. -/
def reindex (e : Î¹ â‰ƒ Î¹') : AffineBasis Î¹' k P :=
  âŸ¨b âˆ˜ e.symm, b.ind.comp_embedding e.symm.toEmbedding, by
    rw [e.symm.surjective.range_comp]
    -- âŠ¢ affineSpan k (range â†‘b) = âŠ¤
    exact b.3âŸ©
    -- ğŸ‰ no goals
#align affine_basis.reindex AffineBasis.reindex

@[simp, norm_cast]
theorem coe_reindex : â‡‘(b.reindex e) = b âˆ˜ e.symm :=
  rfl
#align affine_basis.coe_reindex AffineBasis.coe_reindex

@[simp]
theorem reindex_apply (i' : Î¹') : b.reindex e i' = b (e.symm i') :=
  rfl
#align affine_basis.reindex_apply AffineBasis.reindex_apply

@[simp]
theorem reindex_refl : b.reindex (Equiv.refl _) = b :=
  ext rfl
#align affine_basis.reindex_refl AffineBasis.reindex_refl

/-- Given an affine basis for an affine space `P`, if we single out one member of the family, we
obtain a linear basis for the model space `V`.

The linear basis corresponding to the singled-out member `i : Î¹` is indexed by `{j : Î¹ // j â‰  i}`
and its `j`th element is `b j -áµ¥ b i`. (See `basisOf_apply`.) -/
noncomputable def basisOf (i : Î¹) : Basis { j : Î¹ // j â‰  i } k V :=
  Basis.mk ((affineIndependent_iff_linearIndependent_vsub k b i).mp b.ind)
    (by
      suffices
        Submodule.span k (range fun j : { x // x â‰  i } => b â†‘j -áµ¥ b i) = vectorSpan k (range b) by
        rw [this, â† direction_affineSpan, b.tot, AffineSubspace.direction_top]
      conv_rhs => rw [â† image_univ]
      -- âŠ¢ Submodule.span k (range fun j => â†‘b â†‘j -áµ¥ â†‘b i) = vectorSpan k (â†‘b '' univ)
      rw [vectorSpan_image_eq_span_vsub_set_right_ne k b (mem_univ i)]
      -- âŠ¢ Submodule.span k (range fun j => â†‘b â†‘j -áµ¥ â†‘b i) = Submodule.span k ((fun x = â€¦
      congr
      -- âŠ¢ (range fun j => â†‘b â†‘j -áµ¥ â†‘b i) = (fun x => x -áµ¥ â†‘b i) '' (â†‘b '' (univ \ {i}))
      ext v
      -- âŠ¢ (v âˆˆ range fun j => â†‘b â†‘j -áµ¥ â†‘b i) â†” v âˆˆ (fun x => x -áµ¥ â†‘b i) '' (â†‘b '' (uni â€¦
      simp)
      -- ğŸ‰ no goals
#align affine_basis.basis_of AffineBasis.basisOf

@[simp]
theorem basisOf_apply (i : Î¹) (j : { j : Î¹ // j â‰  i }) : b.basisOf i j = b â†‘j -áµ¥ b i := by
  simp [basisOf]
  -- ğŸ‰ no goals
#align affine_basis.basis_of_apply AffineBasis.basisOf_apply

@[simp]
theorem basisOf_reindex (i : Î¹') :
    (b.reindex e).basisOf i =
      (b.basisOf <| e.symm i).reindex (e.subtypeEquiv fun _ => e.eq_symm_apply.not) := by
  ext j
  -- âŠ¢ â†‘(basisOf (reindex b e) i) j = â†‘(Basis.reindex (basisOf b (â†‘e.symm i)) (Equi â€¦
  simp
  -- ğŸ‰ no goals
#align affine_basis.basis_of_reindex AffineBasis.basisOf_reindex

/-- The `i`th barycentric coordinate of a point. -/
noncomputable def coord (i : Î¹) : P â†’áµƒ[k] k where
  toFun q := 1 - (b.basisOf i).sumCoords (q -áµ¥ b i)
  linear := -(b.basisOf i).sumCoords
  map_vadd' q v := by
    dsimp only
    -- âŠ¢ 1 - â†‘(Basis.sumCoords (basisOf b i)) (v +áµ¥ q -áµ¥ â†‘b i) = â†‘(-Basis.sumCoords ( â€¦
    rw [vadd_vsub_assoc, LinearMap.map_add, vadd_eq_add, LinearMap.neg_apply,
      sub_add_eq_sub_sub_swap, add_comm, sub_eq_add_neg]
#align affine_basis.coord AffineBasis.coord

@[simp]
theorem linear_eq_sumCoords (i : Î¹) : (b.coord i).linear = -(b.basisOf i).sumCoords :=
  rfl
#align affine_basis.linear_eq_sum_coords AffineBasis.linear_eq_sumCoords

@[simp]
theorem coord_reindex (i : Î¹') : (b.reindex e).coord i = b.coord (e.symm i) := by
  ext
  -- âŠ¢ â†‘(coord (reindex b e) i) pâœ = â†‘(coord b (â†‘e.symm i)) pâœ
  classical simp [AffineBasis.coord]
  -- ğŸ‰ no goals
#align affine_basis.coord_reindex AffineBasis.coord_reindex

@[simp]
theorem coord_apply_eq (i : Î¹) : b.coord i (b i) = 1 := by
  simp only [coord, Basis.coe_sumCoords, LinearEquiv.map_zero, LinearEquiv.coe_coe, sub_zero,
    AffineMap.coe_mk, Finsupp.sum_zero_index, vsub_self]
#align affine_basis.coord_apply_eq AffineBasis.coord_apply_eq

@[simp]
theorem coord_apply_ne (h : i â‰  j) : b.coord i (b j) = 0 := by
  -- Porting note:
  -- in mathlib3 we didn't need to given the `fun j => j â‰  i` argument to `Subtype.coe_mk`,
  -- but I don't think we can complain: this proof was over-golfed.
  rw [coord, AffineMap.coe_mk, â† @Subtype.coe_mk _ (fun j => j â‰  i) j h.symm, â† b.basisOf_apply,
    Basis.sumCoords_self_apply, sub_self]
#align affine_basis.coord_apply_ne AffineBasis.coord_apply_ne

theorem coord_apply [DecidableEq Î¹] (i j : Î¹) : b.coord i (b j) = if i = j then 1 else 0 := by
  cases' eq_or_ne i j with h h <;> simp [h]
  -- âŠ¢ â†‘(coord b i) (â†‘b j) = if i = j then 1 else 0
                                   -- ğŸ‰ no goals
                                   -- ğŸ‰ no goals
#align affine_basis.coord_apply AffineBasis.coord_apply

@[simp]
theorem coord_apply_combination_of_mem (hi : i âˆˆ s) {w : Î¹ â†’ k} (hw : s.sum w = 1) :
    b.coord i (s.affineCombination k b w) = w i := by
  classical simp only [coord_apply, hi, Finset.affineCombination_eq_linear_combination, if_true,
      mul_boole, hw, Function.comp_apply, smul_eq_mul, s.sum_ite_eq,
      s.map_affineCombination b w hw]
#align affine_basis.coord_apply_combination_of_mem AffineBasis.coord_apply_combination_of_mem

@[simp]
theorem coord_apply_combination_of_not_mem (hi : i âˆ‰ s) {w : Î¹ â†’ k} (hw : s.sum w = 1) :
    b.coord i (s.affineCombination k b w) = 0 := by
  classical simp only [coord_apply, hi, Finset.affineCombination_eq_linear_combination, if_false,
      mul_boole, hw, Function.comp_apply, smul_eq_mul, s.sum_ite_eq,
      s.map_affineCombination b w hw]
#align affine_basis.coord_apply_combination_of_not_mem AffineBasis.coord_apply_combination_of_not_mem

@[simp]
theorem sum_coord_apply_eq_one [Fintype Î¹] (q : P) : âˆ‘ i, b.coord i q = 1 := by
  have hq : q âˆˆ affineSpan k (range b) := by
    rw [b.tot]
    exact AffineSubspace.mem_top k V q
  obtain âŸ¨w, hw, rflâŸ© := eq_affineCombination_of_mem_affineSpan_of_fintype hq
  -- âŠ¢ âˆ‘ i : Î¹, â†‘(coord b i) (â†‘(Finset.affineCombination k Finset.univ â†‘b) w) = 1
  convert hw
  -- âŠ¢ â†‘(coord b xâœ) (â†‘(Finset.affineCombination k Finset.univ â†‘b) w) = w xâœ
  exact b.coord_apply_combination_of_mem (Finset.mem_univ _) hw
  -- ğŸ‰ no goals
#align affine_basis.sum_coord_apply_eq_one AffineBasis.sum_coord_apply_eq_one

@[simp]
theorem affineCombination_coord_eq_self [Fintype Î¹] (q : P) :
    (Finset.univ.affineCombination k b fun i => b.coord i q) = q := by
  have hq : q âˆˆ affineSpan k (range b) := by
    rw [b.tot]
    exact AffineSubspace.mem_top k V q
  obtain âŸ¨w, hw, rflâŸ© := eq_affineCombination_of_mem_affineSpan_of_fintype hq
  -- âŠ¢ (â†‘(Finset.affineCombination k Finset.univ â†‘b) fun i => â†‘(coord b i) (â†‘(Finse â€¦
  congr
  -- âŠ¢ (fun i => â†‘(coord b i) (â†‘(Finset.affineCombination k Finset.univ â†‘b) w)) = w
  ext i
  -- âŠ¢ â†‘(coord b i) (â†‘(Finset.affineCombination k Finset.univ â†‘b) w) = w i
  exact b.coord_apply_combination_of_mem (Finset.mem_univ i) hw
  -- ğŸ‰ no goals
#align affine_basis.affine_combination_coord_eq_self AffineBasis.affineCombination_coord_eq_self

/-- A variant of `AffineBasis.affineCombination_coord_eq_self` for the special case when the
affine space is a module so we can talk about linear combinations. -/
@[simp]
theorem linear_combination_coord_eq_self [Fintype Î¹] (b : AffineBasis Î¹ k V) (v : V) :
    âˆ‘ i, b.coord i v â€¢ b i = v := by
  have hb := b.affineCombination_coord_eq_self v
  -- âŠ¢ âˆ‘ i : Î¹, â†‘(coord b i) v â€¢ â†‘b i = v
  rwa [Finset.univ.affineCombination_eq_linear_combination _ _ (b.sum_coord_apply_eq_one v)] at hb
  -- ğŸ‰ no goals
#align affine_basis.linear_combination_coord_eq_self AffineBasis.linear_combination_coord_eq_self

theorem ext_elem [Finite Î¹] {qâ‚ qâ‚‚ : P} (h : âˆ€ i, b.coord i qâ‚ = b.coord i qâ‚‚) : qâ‚ = qâ‚‚ := by
  cases nonempty_fintype Î¹
  -- âŠ¢ qâ‚ = qâ‚‚
  rw [â† b.affineCombination_coord_eq_self qâ‚, â† b.affineCombination_coord_eq_self qâ‚‚]
  -- âŠ¢ (â†‘(Finset.affineCombination k Finset.univ â†‘b) fun i => â†‘(coord b i) qâ‚) = â†‘( â€¦
  simp only [h]
  -- ğŸ‰ no goals
#align affine_basis.ext_elem AffineBasis.ext_elem

@[simp]
theorem coe_coord_of_subsingleton_eq_one [Subsingleton Î¹] (i : Î¹) : (b.coord i : P â†’ k) = 1 := by
  ext q
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  have hp : (range b).Subsingleton := by
    rw [â† image_univ]
    apply Subsingleton.image
    apply subsingleton_of_subsingleton
  haveI := AffineSubspace.subsingleton_of_subsingleton_span_eq_top hp b.tot
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  let s : Finset Î¹ := {i}
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  have hi : i âˆˆ s := by simp
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  have hw : s.sum (Function.const Î¹ (1 : k)) = 1 := by simp
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  have hq : q = s.affineCombination k b (Function.const Î¹ (1 : k)) := by simp
  -- âŠ¢ â†‘(coord b i) q = OfNat.ofNat 1 q
  rw [Pi.one_apply, hq, b.coord_apply_combination_of_mem hi hw, Function.const_apply]
  -- ğŸ‰ no goals
#align affine_basis.coe_coord_of_subsingleton_eq_one AffineBasis.coe_coord_of_subsingleton_eq_one

theorem surjective_coord [Nontrivial Î¹] (i : Î¹) : Function.Surjective <| b.coord i := by
  classical
    intro x
    obtain âŸ¨j, hijâŸ© := exists_ne i
    let s : Finset Î¹ := {i, j}
    have hi : i âˆˆ s := by simp
    have _ : j âˆˆ s := by simp
    let w : Î¹ â†’ k := fun j' => if j' = i then x else 1 - x
    have hw : s.sum w = 1 := by
      -- Porting note: previously this subgoal worked just by:
      -- simp [hij, Finset.sum_ite, Finset.filter_insert, Finset.filter_eq']
      -- I'm not sure why `simp` can not successfully use `Finset.filter_eq'`.
      simp [Finset.sum_ite, Finset.filter_insert, hij]
      erw [Finset.filter_eq']
      simp [hij.symm]
    use s.affineCombination k b w
    simp [b.coord_apply_combination_of_mem hi hw]
#align affine_basis.surjective_coord AffineBasis.surjective_coord

/-- Barycentric coordinates as an affine map. -/
noncomputable def coords : P â†’áµƒ[k] Î¹ â†’ k where
  toFun q i := b.coord i q
  linear :=
    { toFun := fun v i => -(b.basisOf i).sumCoords v
      map_add' := fun v w => by
        ext i
        -- âŠ¢ (fun v i => -â†‘(Basis.sumCoords (basisOf b i)) v) (v + w) i = ((fun v i => -â†‘ â€¦
        simp only [LinearMap.map_add, Pi.add_apply, neg_add]
        -- ğŸ‰ no goals
      map_smul' := fun t v => by
        ext i
        -- âŠ¢ AddHom.toFun { toFun := fun v i => -â†‘(Basis.sumCoords (basisOf b i)) v, map_ â€¦
        simp only [LinearMap.map_smul, Pi.smul_apply, smul_neg, RingHom.id_apply, mul_neg] }
        -- ğŸ‰ no goals
  map_vadd' p v := by
    ext i
    -- âŠ¢ (fun q i => â†‘(coord b i) q) (v +áµ¥ p) i = (â†‘{ toAddHom := { toFun := fun v i  â€¦
    -- Porting note:
    -- mathlib3 proof was:
    -- simp only [linear_eq_sumCoords, LinearMap.coe_mk, LinearMap.neg_apply, Pi.vadd_apply',
    --   AffineMap.map_vadd]
    -- but now we need to `dsimp` before `AffineMap.map_vadd` works.
    rw [LinearMap.coe_mk, Pi.vadd_apply']
    -- âŠ¢ (fun q i => â†‘(coord b i) q) (v +áµ¥ p) i = â†‘{ toFun := fun v i => -â†‘(Basis.sum â€¦
    dsimp
    -- âŠ¢ â†‘(coord b i) (v +áµ¥ p) = (-Finsupp.sum (â†‘(basisOf b i).repr v) fun x => id) + â€¦
    rw [AffineMap.map_vadd, linear_eq_sumCoords,
        LinearMap.neg_apply]
    simp only [ne_eq, Basis.coe_sumCoords, vadd_eq_add]
    -- ğŸ‰ no goals
#align affine_basis.coords AffineBasis.coords

@[simp]
theorem coords_apply (q : P) (i : Î¹) : b.coords q i = b.coord i q :=
  rfl
#align affine_basis.coords_apply AffineBasis.coords_apply

end Ring

section DivisionRing

variable [DivisionRing k] [Module k V]

@[simp]
theorem coord_apply_centroid [CharZero k] (b : AffineBasis Î¹ k P) {s : Finset Î¹} {i : Î¹}
    (hi : i âˆˆ s) : b.coord i (s.centroid k b) = (s.card : k)â»Â¹ := by
  rw [Finset.centroid,
    b.coord_apply_combination_of_mem hi (s.sum_centroidWeights_eq_one_of_nonempty _ âŸ¨i, hiâŸ©),
    Finset.centroidWeights, Function.const_apply]
#align affine_basis.coord_apply_centroid AffineBasis.coord_apply_centroid

theorem exists_affine_subbasis {t : Set P} (ht : affineSpan k t = âŠ¤) :
    âˆƒ (s : _) (_ : s âŠ† t) (b : AffineBasis (â†¥s) k P), â‡‘b = ((â†‘) : s â†’ P) := by
  obtain âŸ¨s, hst, h_tot, h_indâŸ© := exists_affineIndependent k V t
  -- âŠ¢ âˆƒ s x b, â†‘b = Subtype.val
  refine' âŸ¨s, hst, âŸ¨(â†‘), h_ind, _âŸ©, rflâŸ©
  -- âŠ¢ affineSpan k (range Subtype.val) = âŠ¤
  rw [Subtype.range_coe, h_tot, ht]
  -- ğŸ‰ no goals
#align affine_basis.exists_affine_subbasis AffineBasis.exists_affine_subbasis

variable (k V P)

theorem exists_affineBasis : âˆƒ (s : Set P) (b : AffineBasis (â†¥s) k P), â‡‘b = ((â†‘) : s â†’ P) :=
  let âŸ¨s, _, hsâŸ© := exists_affine_subbasis (AffineSubspace.span_univ k V P)
  âŸ¨s, hsâŸ©
#align affine_basis.exists_affine_basis AffineBasis.exists_affineBasis

end DivisionRing

end AffineBasis
