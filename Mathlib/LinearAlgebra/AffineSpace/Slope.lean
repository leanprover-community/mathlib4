/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.LinearAlgebra.AffineSpace.AffineMap
import Mathlib.Tactic.FieldSimp

/-!
# Slope of a function

In this file we define the slope of a function `f : k â†’ PE` taking values in an affine space over
`k` and prove some basic theorems about `slope`. The `slope` function naturally appears in the Mean
Value Theorem, and in the proof of the fact that a function with nonnegative second derivative on an
interval is convex on this interval.

## Tags

affine space, slope
-/

open AffineMap

variable {k E PE : Type*} [Field k] [AddCommGroup E] [Module k E] [AddTorsor E PE]

/-- `slope f a b = (b - a)â»Â¹ â€¢ (f b -áµ¥ f a)` is the slope of a function `f` on the interval
`[a, b]`. Note that `slope f a a = 0`, not the derivative of `f` at `a`. -/
def slope (f : k â†’ PE) (a b : k) : E :=
  (b - a)â»Â¹ â€¢ (f b -áµ¥ f a)

theorem slope_fun_def (f : k â†’ PE) : slope f = fun a b => (b - a)â»Â¹ â€¢ (f b -áµ¥ f a) :=
  rfl

theorem slope_def_field (f : k â†’ k) (a b : k) : slope f a b = (f b - f a) / (b - a) :=
  (div_eq_inv_mul _ _).symm

theorem slope_fun_def_field (f : k â†’ k) (a : k) : slope f a = fun b => (f b - f a) / (b - a) :=
  (div_eq_inv_mul _ _).symm

@[simp]
theorem slope_same (f : k â†’ PE) (a : k) : (slope f a a : E) = 0 := by
  rw [slope, sub_self, inv_zero, zero_smul]

theorem slope_def_module (f : k â†’ E) (a b : k) : slope f a b = (b - a)â»Â¹ â€¢ (f b - f a) :=
  rfl

@[simp]
theorem sub_smul_slope (f : k â†’ PE) (a b : k) : (b - a) â€¢ slope f a b = f b -áµ¥ f a := by
  rcases eq_or_ne a b with (rfl | hne)
  Â· rw [sub_self, zero_smul, vsub_self]
  Â· rw [slope, smul_inv_smulâ‚€ (sub_ne_zero.2 hne.symm)]

theorem sub_smul_slope_vadd (f : k â†’ PE) (a b : k) : (b - a) â€¢ slope f a b +áµ¥ f a = f b := by
  rw [sub_smul_slope, vsub_vadd]

@[simp]
theorem slope_vadd_const (f : k â†’ E) (c : PE) : (slope fun x => f x +áµ¥ c) = slope f := by
  ext a b
  simp only [slope, vadd_vsub_vadd_cancel_right, vsub_eq_sub]

@[simp]
theorem slope_sub_smul (f : k â†’ E) {a b : k} (h : a â‰  b) :
    slope (fun x => (x - a) â€¢ f x) a b = f b := by
  simp [slope, inv_smul_smulâ‚€ (sub_ne_zero.2 h.symm)]

theorem eq_of_slope_eq_zero {f : k â†’ PE} {a b : k} (h : slope f a b = (0 : E)) : f a = f b := by
  rw [â† sub_smul_slope_vadd f a b, h, smul_zero, zero_vadd]

theorem AffineMap.slope_comp {F PF : Type*} [AddCommGroup F] [Module k F] [AddTorsor F PF]
    (f : PE â†’áµƒ[k] PF) (g : k â†’ PE) (a b : k) : slope (f âˆ˜ g) a b = f.linear (slope g a b) := by
  simp only [slope, (Â· âˆ˜ Â·), f.linear.map_smul, f.linearMap_vsub]

theorem LinearMap.slope_comp {F : Type*} [AddCommGroup F] [Module k F] (f : E â†’â‚—[k] F) (g : k â†’ E)
    (a b : k) : slope (f âˆ˜ g) a b = f (slope g a b) :=
  f.toAffineMap.slope_comp g a b

theorem slope_comm (f : k â†’ PE) (a b : k) : slope f a b = slope f b a := by
  rw [slope, slope, â† neg_vsub_eq_vsub_rev, smul_neg, â† neg_smul, neg_inv, neg_sub]

@[simp] lemma slope_neg (f : k â†’ E) (x y : k) : slope (fun t â†¦ -f t) x y = -slope f x y := by
  simp only [slope_def_module, neg_sub_neg, â† smul_neg, neg_sub]

@[simp] lemma slope_neg_fun (f : k â†’ E) : slope (-f) = -slope f := by
  ext x y; exact slope_neg f x y

/-- `slope f a c` is a linear combination of `slope f a b` and `slope f b c`. This version
explicitly provides coefficients. If `a â‰  c`, then the sum of the coefficients is `1`, so it is
actually an affine combination, see `lineMap_slope_slope_sub_div_sub`. -/
theorem sub_div_sub_smul_slope_add_sub_div_sub_smul_slope (f : k â†’ PE) (a b c : k) :
    ((b - a) / (c - a)) â€¢ slope f a b + ((c - b) / (c - a)) â€¢ slope f b c = slope f a c := by
  by_cases hab : a = b
  Â· subst hab
    rw [sub_self, zero_div, zero_smul, zero_add]
    by_cases hac : a = c
    Â· simp [hac]
    Â· rw [div_self (sub_ne_zero.2 <| Ne.symm hac), one_smul]
  by_cases hbc : b = c
  Â· subst hbc
    simp [sub_ne_zero.2 (Ne.symm hab)]
  rw [add_comm]
  simp_rw [slope, div_eq_inv_mul, mul_smul, â† smul_add,
    smul_inv_smulâ‚€ (sub_ne_zero.2 <| Ne.symm hab), smul_inv_smulâ‚€ (sub_ne_zero.2 <| Ne.symm hbc),
    vsub_add_vsub_cancel]

/-- `slope f a c` is an affine combination of `slope f a b` and `slope f b c`. This version uses
`lineMap` to express this property. -/
theorem lineMap_slope_slope_sub_div_sub (f : k â†’ PE) (a b c : k) (h : a â‰  c) :
    lineMap (slope f a b) (slope f b c) ((c - b) / (c - a)) = slope f a c := by
  field_simp [sub_ne_zero.2 h.symm, â† sub_div_sub_smul_slope_add_sub_div_sub_smul_slope f a b c,
    lineMap_apply_module]

/-- `slope f a b` is an affine combination of `slope f a (lineMap a b r)` and
`slope f (lineMap a b r) b`. We use `lineMap` to express this property. -/
theorem lineMap_slope_lineMap_slope_lineMap (f : k â†’ PE) (a b r : k) :
    lineMap (slope f (lineMap a b r) b) (slope f a (lineMap a b r)) r = slope f a b := by
  obtain rfl | hab : a = b âˆ¨ a â‰  b := Classical.em _; Â· simp
  rw [slope_comm _ a, slope_comm _ a, slope_comm _ _ b]
  convert lineMap_slope_slope_sub_div_sub f b (lineMap a b r) a hab.symm using 2
  rw [lineMap_apply_ring, eq_div_iff (sub_ne_zero.2 hab), sub_mul, one_mul, mul_sub, â† sub_sub,
    sub_sub_cancel]

lemma slope_pos_iff {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : xâ‚€ < b) :
    0 < slope f xâ‚€ b â†” f xâ‚€ < f b := by
  simp [slope, hb]

lemma slope_pos_iff_gt {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : b < xâ‚€) :
    0 < slope f xâ‚€ b â†” f b < f xâ‚€ := by
  rw [slope_comm, slope_pos_iff hb]

lemma pos_of_slope_pos {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ}
    (hb : xâ‚€ < b) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : 0 < f b := by
  simp_all [slope, hf]

lemma neg_of_slope_pos {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ}
    (hb : b < xâ‚€) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : f b < 0 := by
  rwa [slope_pos_iff_gt, hf] at hbf
  exact hb
