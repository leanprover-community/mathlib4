/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers
-/
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Algebra.Order.Module.Defs
import Mathlib.LinearAlgebra.BilinearMap
import Mathlib.LinearAlgebra.Pi
import Mathlib.LinearAlgebra.Prod
import Mathlib.Tactic.Abel
import Mathlib.Algebra.AddTorsor.Basic
import Mathlib.LinearAlgebra.AffineSpace.Defs
/-!
# Affine maps

This file defines affine maps.

## Main definitions

* `AffineMap` is the type of affine maps between two affine spaces with the same ring `k`.  Various
  basic examples of affine maps are defined, including `const`, `id`, `lineMap` and `homothety`.

## Notation

* `P1 ‚Üí·µÉ[k] P2` is a notation for `AffineMap k P1 P2`;
* `AffineSpace V P`: a localized notation for `AddTorsor V P` defined in
  `LinearAlgebra.AffineSpace.Basic`.

## Implementation notes

`outParam` is used in the definition of `[AddTorsor V P]` to make `V` an implicit argument
(deduced from `P`) in most cases. As for modules, `k` is an explicit argument rather than implied by
`P` or `V`.

This file only provides purely algebraic definitions and results. Those depending on analysis or
topology are defined elsewhere; see `Analysis.Normed.Affine.AddTorsor` and
`Topology.Algebra.Affine`.

## References

* https://en.wikipedia.org/wiki/Affine_space
* https://en.wikipedia.org/wiki/Principal_homogeneous_space
-/

open Affine

/-- An `AffineMap k P1 P2` (notation: `P1 ‚Üí·µÉ[k] P2`) is a map from `P1` to `P2` that
induces a corresponding linear map from `V1` to `V2`. -/
structure AffineMap (k : Type*) {V1 : Type*} (P1 : Type*) {V2 : Type*} (P2 : Type*) [Ring k]
  [AddCommGroup V1] [Module k V1] [AffineSpace V1 P1] [AddCommGroup V2] [Module k V2]
  [AffineSpace V2 P2] where
  /-- The underlying function between the affine spaces `P1` and `P2`. -/
  toFun : P1 ‚Üí P2
  /-- The linear map between the corresponding vector spaces `V1` and `V2`.
  This represents how the affine map acts on differences of points. -/
  linear : V1 ‚Üí‚Çó[k] V2
  map_vadd' : ‚àÄ (p : P1) (v : V1), toFun (v +·µ• p) = linear v +·µ• toFun p

/-- An `AffineMap k P1 P2` (notation: `P1 ‚Üí·µÉ[k] P2`) is a map from `P1` to `P2` that
induces a corresponding linear map from `V1` to `V2`. -/
notation:25 P1 " ‚Üí·µÉ[" k:25 "] " P2:0 => AffineMap k P1 P2

instance AffineMap.instFunLike (k : Type*) {V1 : Type*} (P1 : Type*) {V2 : Type*} (P2 : Type*)
    [Ring k] [AddCommGroup V1] [Module k V1] [AffineSpace V1 P1] [AddCommGroup V2] [Module k V2]
    [AffineSpace V2 P2] : FunLike (P1 ‚Üí·µÉ[k] P2) P1 P2 where
  coe := AffineMap.toFun
  coe_injective' := fun ‚ü®f, f_linear, f_add‚ü© ‚ü®g, g_linear, g_add‚ü© => fun (h : f = g) => by
    obtain ‚ü®p‚ü© := (AddTorsor.nonempty : Nonempty P1)
    congr with v
    apply vadd_right_cancel (f p)
    rw [‚Üê f_add, h, ‚Üê g_add]

namespace LinearMap

variable {k : Type*} {V‚ÇÅ : Type*} {V‚ÇÇ : Type*} [Ring k] [AddCommGroup V‚ÇÅ] [Module k V‚ÇÅ]
  [AddCommGroup V‚ÇÇ] [Module k V‚ÇÇ] (f : V‚ÇÅ ‚Üí‚Çó[k] V‚ÇÇ)

/-- Reinterpret a linear map as an affine map. -/
def toAffineMap : V‚ÇÅ ‚Üí·µÉ[k] V‚ÇÇ where
  toFun := f
  linear := f
  map_vadd' p v := f.map_add v p

@[simp]
theorem coe_toAffineMap : ‚áëf.toAffineMap = f :=
  rfl

@[simp]
theorem toAffineMap_linear : f.toAffineMap.linear = f :=
  rfl

end LinearMap

namespace AffineMap

variable {k : Type*} {V1 : Type*} {P1 : Type*} {V2 : Type*} {P2 : Type*} {V3 : Type*}
  {P3 : Type*} {V4 : Type*} {P4 : Type*} [Ring k] [AddCommGroup V1] [Module k V1]
  [AffineSpace V1 P1] [AddCommGroup V2] [Module k V2] [AffineSpace V2 P2] [AddCommGroup V3]
  [Module k V3] [AffineSpace V3 P3] [AddCommGroup V4] [Module k V4] [AffineSpace V4 P4]

/-- Constructing an affine map and coercing back to a function
produces the same map. -/
@[simp]
theorem coe_mk (f : P1 ‚Üí P2) (linear add) : ((mk f linear add : P1 ‚Üí·µÉ[k] P2) : P1 ‚Üí P2) = f :=
  rfl

/-- `toFun` is the same as the result of coercing to a function. -/
@[simp]
theorem toFun_eq_coe (f : P1 ‚Üí·µÉ[k] P2) : f.toFun = ‚áëf :=
  rfl

/-- An affine map on the result of adding a vector to a point produces
the same result as the linear map applied to that vector, added to the
affine map applied to that point. -/
@[simp]
theorem map_vadd (f : P1 ‚Üí·µÉ[k] P2) (p : P1) (v : V1) : f (v +·µ• p) = f.linear v +·µ• f p :=
  f.map_vadd' p v

/-- The linear map on the result of subtracting two points is the
result of subtracting the result of the affine map on those two
points. -/
@[simp]
theorem linearMap_vsub (f : P1 ‚Üí·µÉ[k] P2) (p1 p2 : P1) : f.linear (p1 -·µ• p2) = f p1 -·µ• f p2 := by
  conv_rhs => rw [‚Üê vsub_vadd p1 p2, map_vadd, vadd_vsub]

/-- Two affine maps are equal if they coerce to the same function. -/
@[ext]
theorem ext {f g : P1 ‚Üí·µÉ[k] P2} (h : ‚àÄ p, f p = g p) : f = g :=
  DFunLike.ext _ _ h

theorem coeFn_injective : @Function.Injective (P1 ‚Üí·µÉ[k] P2) (P1 ‚Üí P2) (‚áë) :=
  DFunLike.coe_injective

protected theorem congr_arg (f : P1 ‚Üí·µÉ[k] P2) {x y : P1} (h : x = y) : f x = f y :=
  congr_arg _ h

protected theorem congr_fun {f g : P1 ‚Üí·µÉ[k] P2} (h : f = g) (x : P1) : f x = g x :=
  h ‚ñ∏ rfl

/-- Two affine maps are equal if they have equal linear maps and are equal at some point. -/
theorem ext_linear {f g : P1 ‚Üí·µÉ[k] P2} (h‚ÇÅ : f.linear = g.linear) {p : P1} (h‚ÇÇ : f p = g p) :
    f = g := by
  ext q
  have hgl : g.linear (q -·µ• p) = toFun g ((q -·µ• p) +·µ• q) -·µ• toFun g q := by simp
  have := f.map_vadd' q (q -·µ• p)
  rw [h‚ÇÅ, hgl, toFun_eq_coe, map_vadd, linearMap_vsub, h‚ÇÇ] at this
  simpa

/-- Two affine maps are equal if they have equal linear maps and are equal at some point. -/
theorem ext_linear_iff {f g : P1 ‚Üí·µÉ[k] P2} : f = g ‚Üî (f.linear = g.linear) ‚àß (‚àÉ p, f p = g p) :=
  ‚ü®fun h ‚Ü¶ ‚ü®congrArg _ h, by inhabit P1; exact default, by rw [h]‚ü©,
  fun h ‚Ü¶ Exists.casesOn h.2 fun _ hp ‚Ü¶ ext_linear h.1 hp‚ü©

variable (k P1)

/-- The constant function as an `AffineMap`. -/
def const (p : P2) : P1 ‚Üí·µÉ[k] P2 where
  toFun := Function.const P1 p
  linear := 0
  map_vadd' _ _ :=
    letI : AddAction V2 P2 := inferInstance
    by simp

@[simp]
theorem coe_const (p : P2) : ‚áë(const k P1 p) = Function.const P1 p :=
  rfl

@[simp]
theorem const_apply (p : P2) (q : P1) : (const k P1 p) q = p := rfl

@[simp]
theorem const_linear (p : P2) : (const k P1 p).linear = 0 :=
  rfl

variable {k P1}

theorem linear_eq_zero_iff_exists_const (f : P1 ‚Üí·µÉ[k] P2) :
    f.linear = 0 ‚Üî ‚àÉ q, f = const k P1 q := by
  refine ‚ü®fun h => ?_, fun h => ?_‚ü©
  ¬∑ use f (Classical.arbitrary P1)
    ext
    rw [coe_const, Function.const_apply, ‚Üê @vsub_eq_zero_iff_eq V2, ‚Üê f.linearMap_vsub, h,
      LinearMap.zero_apply]
  ¬∑ rcases h with ‚ü®q, rfl‚ü©
    exact const_linear k P1 q

instance nonempty : Nonempty (P1 ‚Üí·µÉ[k] P2) :=
  (AddTorsor.nonempty : Nonempty P2).map <| const k P1

/-- Construct an affine map by verifying the relation between the map and its linear part at one
base point. Namely, this function takes a map `f : P‚ÇÅ ‚Üí P‚ÇÇ`, a linear map `f' : V‚ÇÅ ‚Üí‚Çó[k] V‚ÇÇ`, and
a point `p` such that for any other point `p'` we have `f p' = f' (p' -·µ• p) +·µ• f p`. -/
def mk' (f : P1 ‚Üí P2) (f' : V1 ‚Üí‚Çó[k] V2) (p : P1) (h : ‚àÄ p' : P1, f p' = f' (p' -·µ• p) +·µ• f p) :
    P1 ‚Üí·µÉ[k] P2 where
  toFun := f
  linear := f'
  map_vadd' p' v := by rw [h, h p', vadd_vsub_assoc, f'.map_add, vadd_vadd]

@[simp]
theorem coe_mk' (f : P1 ‚Üí P2) (f' : V1 ‚Üí‚Çó[k] V2) (p h) : ‚áë(mk' f f' p h) = f :=
  rfl

@[simp]
theorem mk'_linear (f : P1 ‚Üí P2) (f' : V1 ‚Üí‚Çó[k] V2) (p h) : (mk' f f' p h).linear = f' :=
  rfl

section SMul

variable {R : Type*} [Monoid R] [DistribMulAction R V2] [SMulCommClass k R V2]
/-- The space of affine maps to a module inherits an `R`-action from the action on its codomain. -/
instance mulAction : MulAction R (P1 ‚Üí·µÉ[k] V2) where
  smul c f := ‚ü®c ‚Ä¢ ‚áëf, c ‚Ä¢ f.linear, fun p v => by simp [smul_add]‚ü©
  one_smul _ := ext fun _ => one_smul _ _
  mul_smul _ _ _ := ext fun _ => mul_smul _ _ _

@[simp, norm_cast]
theorem coe_smul (c : R) (f : P1 ‚Üí·µÉ[k] V2) : ‚áë(c ‚Ä¢ f) = c ‚Ä¢ ‚áëf :=
  rfl

@[simp]
theorem smul_linear (t : R) (f : P1 ‚Üí·µÉ[k] V2) : (t ‚Ä¢ f).linear = t ‚Ä¢ f.linear :=
  rfl

instance isCentralScalar [DistribMulAction R·µê·µí·µñ V2] [IsCentralScalar R V2] :
    IsCentralScalar R (P1 ‚Üí·µÉ[k] V2) where
  op_smul_eq_smul _r _x := ext fun _ => op_smul_eq_smul _ _

end SMul

instance : Zero (P1 ‚Üí·µÉ[k] V2) where zero := ‚ü®0, 0, fun _ _ => (zero_vadd _ _).symm‚ü©

instance : Add (P1 ‚Üí·µÉ[k] V2) where
  add f g := ‚ü®f + g, f.linear + g.linear, fun p v => by simp [add_add_add_comm]‚ü©

instance : Sub (P1 ‚Üí·µÉ[k] V2) where
  sub f g := ‚ü®f - g, f.linear - g.linear, fun p v => by simp [sub_add_sub_comm]‚ü©

instance : Neg (P1 ‚Üí·µÉ[k] V2) where
  neg f := ‚ü®-f, -f.linear, fun p v => by simp [add_comm, map_vadd f]‚ü©

@[simp, norm_cast]
theorem coe_zero : ‚áë(0 : P1 ‚Üí·µÉ[k] V2) = 0 :=
  rfl

@[simp, norm_cast]
theorem coe_add (f g : P1 ‚Üí·µÉ[k] V2) : ‚áë(f + g) = f + g :=
  rfl

@[simp, norm_cast]
theorem coe_neg (f : P1 ‚Üí·µÉ[k] V2) : ‚áë(-f) = -f :=
  rfl

@[simp, norm_cast]
theorem coe_sub (f g : P1 ‚Üí·µÉ[k] V2) : ‚áë(f - g) = f - g :=
  rfl

@[simp]
theorem zero_linear : (0 : P1 ‚Üí·µÉ[k] V2).linear = 0 :=
  rfl

@[simp]
theorem add_linear (f g : P1 ‚Üí·µÉ[k] V2) : (f + g).linear = f.linear + g.linear :=
  rfl

@[simp]
theorem sub_linear (f g : P1 ‚Üí·µÉ[k] V2) : (f - g).linear = f.linear - g.linear :=
  rfl

@[simp]
theorem neg_linear (f : P1 ‚Üí·µÉ[k] V2) : (-f).linear = -f.linear :=
  rfl

/-- The set of affine maps to a vector space is an additive commutative group. -/
instance : AddCommGroup (P1 ‚Üí·µÉ[k] V2) :=
  coeFn_injective.addCommGroup _ coe_zero coe_add coe_neg coe_sub (fun _ _ => coe_smul _ _)
    fun _ _ => coe_smul _ _

/-- The space of affine maps from `P1` to `P2` is an affine space over the space of affine maps
from `P1` to the vector space `V2` corresponding to `P2`. -/
instance : AffineSpace (P1 ‚Üí·µÉ[k] V2) (P1 ‚Üí·µÉ[k] P2) where
  vadd f g :=
    ‚ü®fun p => f p +·µ• g p, f.linear + g.linear,
      fun p v => by simp [vadd_vadd, add_right_comm]‚ü©
  zero_vadd f := ext fun p => zero_vadd _ (f p)
  add_vadd f‚ÇÅ f‚ÇÇ f‚ÇÉ := ext fun p => add_vadd (f‚ÇÅ p) (f‚ÇÇ p) (f‚ÇÉ p)
  vsub f g :=
    ‚ü®fun p => f p -·µ• g p, f.linear - g.linear, fun p v => by
      simp [vsub_vadd_eq_vsub_sub, vadd_vsub_assoc, sub_add_eq_add_sub]‚ü©
  vsub_vadd' f g := ext fun p => vsub_vadd (f p) (g p)
  vadd_vsub' f g := ext fun p => vadd_vsub (f p) (g p)

@[simp]
theorem vadd_apply (f : P1 ‚Üí·µÉ[k] V2) (g : P1 ‚Üí·µÉ[k] P2) (p : P1) : (f +·µ• g) p = f p +·µ• g p :=
  rfl

@[simp]
theorem vadd_linear (f : P1 ‚Üí·µÉ[k] V2) (g : P1 ‚Üí·µÉ[k] P2) : (f +·µ• g).linear = f.linear + g.linear :=
  rfl

@[simp]
theorem vsub_apply (f g : P1 ‚Üí·µÉ[k] P2) (p : P1) : (f -·µ• g : P1 ‚Üí·µÉ[k] V2) p = f p -·µ• g p :=
  rfl

@[simp]
theorem vsub_linear (f g : P1 ‚Üí·µÉ[k] P2) : (f -·µ• g).linear = f.linear - g.linear :=
  rfl

/-- `Prod.fst` as an `AffineMap`. -/
def fst : P1 √ó P2 ‚Üí·µÉ[k] P1 where
  toFun := Prod.fst
  linear := LinearMap.fst k V1 V2
  map_vadd' _ _ := rfl

@[simp]
theorem coe_fst : ‚áë(fst : P1 √ó P2 ‚Üí·µÉ[k] P1) = Prod.fst :=
  rfl

@[simp]
theorem fst_linear : (fst : P1 √ó P2 ‚Üí·µÉ[k] P1).linear = LinearMap.fst k V1 V2 :=
  rfl

/-- `Prod.snd` as an `AffineMap`. -/
def snd : P1 √ó P2 ‚Üí·µÉ[k] P2 where
  toFun := Prod.snd
  linear := LinearMap.snd k V1 V2
  map_vadd' _ _ := rfl

@[simp]
theorem coe_snd : ‚áë(snd : P1 √ó P2 ‚Üí·µÉ[k] P2) = Prod.snd :=
  rfl

@[simp]
theorem snd_linear : (snd : P1 √ó P2 ‚Üí·µÉ[k] P2).linear = LinearMap.snd k V1 V2 :=
  rfl

variable (k P1)
/-- Identity map as an affine map. -/
nonrec def id : P1 ‚Üí·µÉ[k] P1 where
  toFun := id
  linear := LinearMap.id
  map_vadd' _ _ := rfl

/-- The identity affine map acts as the identity. -/
@[simp, norm_cast]
theorem coe_id : ‚áë(id k P1) = _root_.id :=
  rfl

@[simp]
theorem id_linear : (id k P1).linear = LinearMap.id :=
  rfl

variable {P1}

/-- The identity affine map acts as the identity. -/
theorem id_apply (p : P1) : id k P1 p = p :=
  rfl

variable {k}

instance : Inhabited (P1 ‚Üí·µÉ[k] P1) :=
  ‚ü®id k P1‚ü©

/-- Composition of affine maps. -/
@[simps linear]
def comp (f : P2 ‚Üí·µÉ[k] P3) (g : P1 ‚Üí·µÉ[k] P2) : P1 ‚Üí·µÉ[k] P3 where
  toFun := f ‚àò g
  linear := f.linear.comp g.linear
  map_vadd' := by
    intro p v
    rw [Function.comp_apply, g.map_vadd, f.map_vadd]
    rfl

/-- Composition of affine maps acts as applying the two functions. -/
@[simp]
theorem coe_comp (f : P2 ‚Üí·µÉ[k] P3) (g : P1 ‚Üí·µÉ[k] P2) : ‚áë(f.comp g) = f ‚àò g :=
  rfl

/-- Composition of affine maps acts as applying the two functions. -/
theorem comp_apply (f : P2 ‚Üí·µÉ[k] P3) (g : P1 ‚Üí·µÉ[k] P2) (p : P1) : f.comp g p = f (g p) :=
  rfl

@[simp]
theorem comp_id (f : P1 ‚Üí·µÉ[k] P2) : f.comp (id k P1) = f :=
  ext fun _ => rfl

@[simp]
theorem id_comp (f : P1 ‚Üí·µÉ[k] P2) : (id k P2).comp f = f :=
  ext fun _ => rfl

theorem comp_assoc (f‚ÇÉ‚ÇÑ : P3 ‚Üí·µÉ[k] P4) (f‚ÇÇ‚ÇÉ : P2 ‚Üí·µÉ[k] P3) (f‚ÇÅ‚ÇÇ : P1 ‚Üí·µÉ[k] P2) :
    (f‚ÇÉ‚ÇÑ.comp f‚ÇÇ‚ÇÉ).comp f‚ÇÅ‚ÇÇ = f‚ÇÉ‚ÇÑ.comp (f‚ÇÇ‚ÇÉ.comp f‚ÇÅ‚ÇÇ) :=
  rfl

instance : Monoid (P1 ‚Üí·µÉ[k] P1) where
  one := id k P1
  mul := comp
  one_mul := id_comp
  mul_one := comp_id
  mul_assoc := comp_assoc

@[simp]
theorem coe_mul (f g : P1 ‚Üí·µÉ[k] P1) : ‚áë(f * g) = f ‚àò g :=
  rfl

@[simp]
theorem coe_one : ‚áë(1 : P1 ‚Üí·µÉ[k] P1) = _root_.id :=
  rfl

/-- `AffineMap.linear` on endomorphisms is a `MonoidHom`. -/
@[simps]
def linearHom : (P1 ‚Üí·µÉ[k] P1) ‚Üí* V1 ‚Üí‚Çó[k] V1 where
  toFun := linear
  map_one' := rfl
  map_mul' _ _ := rfl

@[simp]
theorem linear_injective_iff (f : P1 ‚Üí·µÉ[k] P2) :
    Function.Injective f.linear ‚Üî Function.Injective f := by
  obtain ‚ü®p‚ü© := (inferInstance : Nonempty P1)
  have h : ‚áëf.linear = (Equiv.vaddConst (f p)).symm ‚àò f ‚àò Equiv.vaddConst p := by
    ext v
    simp [f.map_vadd]
  rw [h, Equiv.comp_injective, Equiv.injective_comp]

@[simp]
theorem linear_surjective_iff (f : P1 ‚Üí·µÉ[k] P2) :
    Function.Surjective f.linear ‚Üî Function.Surjective f := by
  obtain ‚ü®p‚ü© := (inferInstance : Nonempty P1)
  have h : ‚áëf.linear = (Equiv.vaddConst (f p)).symm ‚àò f ‚àò Equiv.vaddConst p := by
    ext v
    simp [f.map_vadd]
  rw [h, Equiv.comp_surjective, Equiv.surjective_comp]

@[simp]
theorem linear_bijective_iff (f : P1 ‚Üí·µÉ[k] P2) :
    Function.Bijective f.linear ‚Üî Function.Bijective f :=
  and_congr f.linear_injective_iff f.linear_surjective_iff

theorem image_vsub_image {s t : Set P1} (f : P1 ‚Üí·µÉ[k] P2) :
    f '' s -·µ• f '' t = f.linear '' (s -·µ• t) := by
  ext v
  simp only [Set.mem_vsub, Set.mem_image,
    exists_exists_and_eq_and, ‚Üê f.linearMap_vsub]
  grind

/-- The product of two affine maps is an affine map. -/
@[simps linear]
def prod (f : P1 ‚Üí·µÉ[k] P2) (g : P1 ‚Üí·µÉ[k] P3) : P1 ‚Üí·µÉ[k] P2 √ó P3 where
  toFun := Pi.prod f g
  linear := f.linear.prod g.linear
  map_vadd' := by simp

theorem coe_prod (f : P1 ‚Üí·µÉ[k] P2) (g : P1 ‚Üí·µÉ[k] P3) : prod f g = Pi.prod f g :=
  rfl

@[simp]
theorem prod_apply (f : P1 ‚Üí·µÉ[k] P2) (g : P1 ‚Üí·µÉ[k] P3) (p : P1) : prod f g p = (f p, g p) :=
  rfl

/-- `Prod.map` of two affine maps. -/
@[simps linear]
def prodMap (f : P1 ‚Üí·µÉ[k] P2) (g : P3 ‚Üí·µÉ[k] P4) : P1 √ó P3 ‚Üí·µÉ[k] P2 √ó P4 where
  toFun := Prod.map f g
  linear := f.linear.prodMap g.linear
  map_vadd' := by simp

theorem coe_prodMap (f : P1 ‚Üí·µÉ[k] P2) (g : P3 ‚Üí·µÉ[k] P4) : ‚áë(f.prodMap g) = Prod.map f g :=
  rfl

@[simp]
theorem prodMap_apply (f : P1 ‚Üí·µÉ[k] P2) (g : P3 ‚Üí·µÉ[k] P4) (x) : f.prodMap g x = (f x.1, g x.2) :=
  rfl

/-! ### Definition of `AffineMap.lineMap` and lemmas about it -/

/-- The affine map from `k` to `P1` sending `0` to `p‚ÇÄ` and `1` to `p‚ÇÅ`. -/
def lineMap (p‚ÇÄ p‚ÇÅ : P1) : k ‚Üí·µÉ[k] P1 :=
  ((LinearMap.id : k ‚Üí‚Çó[k] k).smulRight (p‚ÇÅ -·µ• p‚ÇÄ)).toAffineMap +·µ• const k k p‚ÇÄ

theorem coe_lineMap (p‚ÇÄ p‚ÇÅ : P1) : (lineMap p‚ÇÄ p‚ÇÅ : k ‚Üí P1) = fun c => c ‚Ä¢ (p‚ÇÅ -·µ• p‚ÇÄ) +·µ• p‚ÇÄ :=
  rfl

theorem lineMap_apply (p‚ÇÄ p‚ÇÅ : P1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ c = c ‚Ä¢ (p‚ÇÅ -·µ• p‚ÇÄ) +·µ• p‚ÇÄ :=
  rfl

theorem lineMap_apply_module' (p‚ÇÄ p‚ÇÅ : V1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ c = c ‚Ä¢ (p‚ÇÅ - p‚ÇÄ) + p‚ÇÄ :=
  rfl

theorem lineMap_apply_module (p‚ÇÄ p‚ÇÅ : V1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ c = (1 - c) ‚Ä¢ p‚ÇÄ + c ‚Ä¢ p‚ÇÅ := by
  simp [lineMap_apply_module', smul_sub, sub_smul]; abel

theorem lineMap_apply_ring' (a b c : k) : lineMap a b c = c * (b - a) + a :=
  rfl

theorem lineMap_apply_ring (a b c : k) : lineMap a b c = (1 - c) * a + c * b :=
  lineMap_apply_module a b c

theorem lineMap_vadd_apply (p : P1) (v : V1) (c : k) : lineMap p (v +·µ• p) c = c ‚Ä¢ v +·µ• p := by
  rw [lineMap_apply, vadd_vsub]

@[simp]
theorem lineMap_linear (p‚ÇÄ p‚ÇÅ : P1) :
    (lineMap p‚ÇÄ p‚ÇÅ : k ‚Üí·µÉ[k] P1).linear = LinearMap.id.smulRight (p‚ÇÅ -·µ• p‚ÇÄ) :=
  add_zero _

theorem lineMap_same_apply (p : P1) (c : k) : lineMap p p c = p := by
  simp [lineMap_apply]

@[simp]
theorem lineMap_same (p : P1) : lineMap p p = const k k p :=
  ext <| lineMap_same_apply p

@[simp]
theorem lineMap_apply_zero (p‚ÇÄ p‚ÇÅ : P1) : lineMap p‚ÇÄ p‚ÇÅ (0 : k) = p‚ÇÄ := by
  simp [lineMap_apply]

@[simp]
theorem lineMap_apply_one (p‚ÇÄ p‚ÇÅ : P1) : lineMap p‚ÇÄ p‚ÇÅ (1 : k) = p‚ÇÅ := by
  simp [lineMap_apply]

@[simp]
theorem lineMap_eq_lineMap_iff [NoZeroSMulDivisors k V1] {p‚ÇÄ p‚ÇÅ : P1} {c‚ÇÅ c‚ÇÇ : k} :
    lineMap p‚ÇÄ p‚ÇÅ c‚ÇÅ = lineMap p‚ÇÄ p‚ÇÅ c‚ÇÇ ‚Üî p‚ÇÄ = p‚ÇÅ ‚à® c‚ÇÅ = c‚ÇÇ := by
  rw [lineMap_apply, lineMap_apply, ‚Üê @vsub_eq_zero_iff_eq V1, vadd_vsub_vadd_cancel_right, ‚Üê
    sub_smul, smul_eq_zero, sub_eq_zero, vsub_eq_zero_iff_eq, or_comm, eq_comm]

@[simp]
theorem lineMap_eq_left_iff [NoZeroSMulDivisors k V1] {p‚ÇÄ p‚ÇÅ : P1} {c : k} :
    lineMap p‚ÇÄ p‚ÇÅ c = p‚ÇÄ ‚Üî p‚ÇÄ = p‚ÇÅ ‚à® c = 0 := by
  rw [‚Üê @lineMap_eq_lineMap_iff k V1, lineMap_apply_zero]

@[simp]
theorem lineMap_eq_right_iff [NoZeroSMulDivisors k V1] {p‚ÇÄ p‚ÇÅ : P1} {c : k} :
    lineMap p‚ÇÄ p‚ÇÅ c = p‚ÇÅ ‚Üî p‚ÇÄ = p‚ÇÅ ‚à® c = 1 := by
  rw [‚Üê @lineMap_eq_lineMap_iff k V1, lineMap_apply_one]

variable (k) in
theorem lineMap_injective [NoZeroSMulDivisors k V1] {p‚ÇÄ p‚ÇÅ : P1} (h : p‚ÇÄ ‚â† p‚ÇÅ) :
    Function.Injective (lineMap p‚ÇÄ p‚ÇÅ : k ‚Üí P1) := fun _c‚ÇÅ _c‚ÇÇ hc =>
  (lineMap_eq_lineMap_iff.mp hc).resolve_left h

@[simp]
theorem apply_lineMap (f : P1 ‚Üí·µÉ[k] P2) (p‚ÇÄ p‚ÇÅ : P1) (c : k) :
    f (lineMap p‚ÇÄ p‚ÇÅ c) = lineMap (f p‚ÇÄ) (f p‚ÇÅ) c := by
  simp [lineMap_apply]

@[simp]
theorem comp_lineMap (f : P1 ‚Üí·µÉ[k] P2) (p‚ÇÄ p‚ÇÅ : P1) :
    f.comp (lineMap p‚ÇÄ p‚ÇÅ) = lineMap (f p‚ÇÄ) (f p‚ÇÅ) :=
  ext <| f.apply_lineMap p‚ÇÄ p‚ÇÅ

@[simp]
theorem fst_lineMap (p‚ÇÄ p‚ÇÅ : P1 √ó P2) (c : k) : (lineMap p‚ÇÄ p‚ÇÅ c).1 = lineMap p‚ÇÄ.1 p‚ÇÅ.1 c :=
  fst.apply_lineMap p‚ÇÄ p‚ÇÅ c

@[simp]
theorem snd_lineMap (p‚ÇÄ p‚ÇÅ : P1 √ó P2) (c : k) : (lineMap p‚ÇÄ p‚ÇÅ c).2 = lineMap p‚ÇÄ.2 p‚ÇÅ.2 c :=
  snd.apply_lineMap p‚ÇÄ p‚ÇÅ c

theorem lineMap_symm (p‚ÇÄ p‚ÇÅ : P1) :
    lineMap p‚ÇÄ p‚ÇÅ = (lineMap p‚ÇÅ p‚ÇÄ).comp (lineMap (1 : k) (0 : k)) := by
  simp

@[simp]
theorem lineMap_apply_one_sub (p‚ÇÄ p‚ÇÅ : P1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ (1 - c) = lineMap p‚ÇÅ p‚ÇÄ c := by
  rw [lineMap_symm p‚ÇÄ, comp_apply]
  congr
  simp [lineMap_apply]

@[simp]
theorem lineMap_vsub_left (p‚ÇÄ p‚ÇÅ : P1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ c -·µ• p‚ÇÄ = c ‚Ä¢ (p‚ÇÅ -·µ• p‚ÇÄ) :=
  vadd_vsub _ _

@[simp]
theorem left_vsub_lineMap (p‚ÇÄ p‚ÇÅ : P1) (c : k) : p‚ÇÄ -·µ• lineMap p‚ÇÄ p‚ÇÅ c = c ‚Ä¢ (p‚ÇÄ -·µ• p‚ÇÅ) := by
  rw [‚Üê neg_vsub_eq_vsub_rev, lineMap_vsub_left, ‚Üê smul_neg, neg_vsub_eq_vsub_rev]

@[simp]
theorem lineMap_vsub_right (p‚ÇÄ p‚ÇÅ : P1) (c : k) : lineMap p‚ÇÄ p‚ÇÅ c -·µ• p‚ÇÅ = (1 - c) ‚Ä¢ (p‚ÇÄ -·µ• p‚ÇÅ) := by
  rw [‚Üê lineMap_apply_one_sub, lineMap_vsub_left]

@[simp]
theorem right_vsub_lineMap (p‚ÇÄ p‚ÇÅ : P1) (c : k) : p‚ÇÅ -·µ• lineMap p‚ÇÄ p‚ÇÅ c = (1 - c) ‚Ä¢ (p‚ÇÅ -·µ• p‚ÇÄ) := by
  rw [‚Üê lineMap_apply_one_sub, left_vsub_lineMap]

theorem lineMap_vadd_lineMap (v‚ÇÅ v‚ÇÇ : V1) (p‚ÇÅ p‚ÇÇ : P1) (c : k) :
    lineMap v‚ÇÅ v‚ÇÇ c +·µ• lineMap p‚ÇÅ p‚ÇÇ c = lineMap (v‚ÇÅ +·µ• p‚ÇÅ) (v‚ÇÇ +·µ• p‚ÇÇ) c :=
  ((fst : V1 √ó P1 ‚Üí·µÉ[k] V1) +·µ• (snd : V1 √ó P1 ‚Üí·µÉ[k] P1)).apply_lineMap (v‚ÇÅ, p‚ÇÅ) (v‚ÇÇ, p‚ÇÇ) c

theorem lineMap_vsub_lineMap (p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : P1) (c : k) :
    lineMap p‚ÇÅ p‚ÇÇ c -·µ• lineMap p‚ÇÉ p‚ÇÑ c = lineMap (p‚ÇÅ -·µ• p‚ÇÉ) (p‚ÇÇ -·µ• p‚ÇÑ) c :=
  ((fst : P1 √ó P1 ‚Üí·µÉ[k] P1) -·µ• (snd : P1 √ó P1 ‚Üí·µÉ[k] P1)).apply_lineMap (_, _) (_, _) c

@[simp] lemma lineMap_lineMap_right (p‚ÇÄ p‚ÇÅ : P1) (c d : k) :
    lineMap p‚ÇÄ (lineMap p‚ÇÄ p‚ÇÅ c) d = lineMap p‚ÇÄ p‚ÇÅ (d * c) := by simp [lineMap_apply, mul_smul]

@[simp] lemma lineMap_lineMap_left (p‚ÇÄ p‚ÇÅ : P1) (c d : k) :
    lineMap (lineMap p‚ÇÄ p‚ÇÅ c) p‚ÇÅ d = lineMap p‚ÇÄ p‚ÇÅ (1 - (1 - d) * (1 - c)) := by
  simp_rw [lineMap_apply_one_sub, ‚Üê lineMap_apply_one_sub p‚ÇÅ, lineMap_lineMap_right]

lemma lineMap_mono [LinearOrder k] [Preorder V1] [AddRightMono V1] [SMulPosMono k V1]
    {p‚ÇÄ p‚ÇÅ : V1} (h : p‚ÇÄ ‚â§ p‚ÇÅ) :
    Monotone (lineMap (k := k) p‚ÇÄ p‚ÇÅ) := by
  intro x y hxy
  suffices x ‚Ä¢ (p‚ÇÅ - p‚ÇÄ) ‚â§ y ‚Ä¢ (p‚ÇÅ - p‚ÇÄ) by simpa [lineMap]
  gcongr
  simpa

lemma lineMap_anti [LinearOrder k] [Preorder V1] [AddLeftMono V1] [SMulPosMono k V1]
    {p‚ÇÄ p‚ÇÅ : V1} (h : p‚ÇÅ ‚â§ p‚ÇÄ) :
    Antitone (lineMap (k := k) p‚ÇÄ p‚ÇÅ) := by
  intro x y hxy
  suffices y ‚Ä¢ (p‚ÇÅ - p‚ÇÄ) ‚â§ x ‚Ä¢ (p‚ÇÅ - p‚ÇÄ) by simpa [lineMap]
  rw [‚Üê neg_le_neg_iff, ‚Üê smul_neg, ‚Üê smul_neg]
  gcongr
  simpa

/-- Decomposition of an affine map in the special case when the point space and vector space
are the same. -/
theorem decomp (f : V1 ‚Üí·µÉ[k] V2) : (f : V1 ‚Üí V2) = ‚áëf.linear + fun _ => f 0 := by
  ext x
  calc
    f x = f.linear x +·µ• f 0 := by rw [‚Üê f.map_vadd, vadd_eq_add, add_zero]
    _ = (f.linear + fun _ : V1 => f 0) x := rfl

/-- Decomposition of an affine map in the special case when the point space and vector space
are the same. -/
theorem decomp' (f : V1 ‚Üí·µÉ[k] V2) : (f.linear : V1 ‚Üí V2) = ‚áëf - fun _ => f 0 := by
  rw [decomp]
  simp only [LinearMap.map_zero, Pi.add_apply, add_sub_cancel_right, zero_add]

theorem image_uIcc {k : Type*} [Field k] [LinearOrder k] [IsStrictOrderedRing k]
    (f : k ‚Üí·µÉ[k] k) (a b : k) :
    f '' Set.uIcc a b = Set.uIcc (f a) (f b) := by
  have : ‚áëf = (fun x => x + f 0) ‚àò fun x => x * (f 1 - f 0) := by
    ext x
    change f x = x ‚Ä¢ (f 1 -·µ• f 0) +·µ• f 0
    rw [‚Üê f.linearMap_vsub, ‚Üê f.linear.map_smul, ‚Üê f.map_vadd]
    simp only [vsub_eq_sub, add_zero, mul_one, vadd_eq_add, sub_zero, smul_eq_mul]
  rw [this, Set.image_comp]
  simp only [Set.image_add_const_uIcc, Set.image_mul_const_uIcc, Function.comp_apply]

section

variable {Œπ : Type*} {V : Œπ ‚Üí Type*} {P : Œπ ‚Üí Type*} [‚àÄ i, AddCommGroup (V i)]
  [‚àÄ i, Module k (V i)] [‚àÄ i, AddTorsor (V i) (P i)]

/-- Evaluation at a point as an affine map. -/
def proj (i : Œπ) : (‚àÄ i : Œπ, P i) ‚Üí·µÉ[k] P i where
  toFun f := f i
  linear := @LinearMap.proj k Œπ _ V _ _ i
  map_vadd' _ _ := rfl

@[simp]
theorem proj_apply (i : Œπ) (f : ‚àÄ i, P i) : @proj k _ Œπ V P _ _ _ i f = f i :=
  rfl

@[simp]
theorem proj_linear (i : Œπ) : (@proj k _ Œπ V P _ _ _ i).linear = @LinearMap.proj k Œπ _ V _ _ i :=
  rfl

theorem pi_lineMap_apply (f g : ‚àÄ i, P i) (c : k) (i : Œπ) :
    lineMap f g c i = lineMap (f i) (g i) c :=
  (proj i : (‚àÄ i, P i) ‚Üí·µÉ[k] P i).apply_lineMap f g c

end

end AffineMap

namespace AffineMap

variable {R k V1 P1 V2 P2 V3 P3 : Type*}

section Ring

variable [Ring k] [AddCommGroup V1] [AffineSpace V1 P1] [AddCommGroup V2] [AffineSpace V2 P2]
variable [AddCommGroup V3] [AffineSpace V3 P3] [Module k V1] [Module k V2] [Module k V3]

section DistribMulAction

variable [Monoid R] [DistribMulAction R V2] [SMulCommClass k R V2]

/-- The space of affine maps to a module inherits an `R`-action from the action on its codomain. -/
instance distribMulAction : DistribMulAction R (P1 ‚Üí·µÉ[k] V2) where
  smul_add _ _ _ := ext fun _ => smul_add _ _ _
  smul_zero _ := ext fun _ => smul_zero _

end DistribMulAction

section Module

variable [Semiring R] [Module R V2] [SMulCommClass k R V2]

/-- The space of affine maps taking values in an `R`-module is an `R`-module. -/
instance : Module R (P1 ‚Üí·µÉ[k] V2) :=
  { AffineMap.distribMulAction with
    add_smul := fun _ _ _ => ext fun _ => add_smul _ _ _
    zero_smul := fun _ => ext fun _ => zero_smul _ _ }

variable (R)

/-- The space of affine maps between two modules is linearly equivalent to the product of the
domain with the space of linear maps, by taking the value of the affine map at `(0 : V1)` and the
linear part.

See note [bundled maps over different rings] -/
@[simps]
def toConstProdLinearMap : (V1 ‚Üí·µÉ[k] V2) ‚âÉ‚Çó[R] V2 √ó (V1 ‚Üí‚Çó[k] V2) where
  toFun f := ‚ü®f 0, f.linear‚ü©
  invFun p := p.2.toAffineMap + const k V1 p.1
  left_inv f := by
    ext
    rw [f.decomp]
    simp
  right_inv := by
    rintro ‚ü®v, f‚ü©
    ext <;> simp [const_linear]
  map_add' := by simp
  map_smul' := by simp

end Module

section Pi

variable {Œπ : Type*} {œÜv œÜp : Œπ ‚Üí Type*} [(i : Œπ) ‚Üí AddCommGroup (œÜv i)]
  [(i : Œπ) ‚Üí Module k (œÜv i)] [(i : Œπ) ‚Üí AffineSpace (œÜv i) (œÜp i)]
/-- `pi` construction for affine maps. From a family of affine maps it produces an affine
map into a family of affine spaces.

This is the affine version of `LinearMap.pi`.
-/
@[simps linear]
def pi (f : (i : Œπ) ‚Üí (P1 ‚Üí·µÉ[k] œÜp i)) : P1 ‚Üí·µÉ[k] ((i : Œπ) ‚Üí œÜp i) where
  toFun m a := f a m
  linear := LinearMap.pi (fun a ‚Ü¶ (f a).linear)
  map_vadd' _ _ := funext fun _ ‚Ü¶ map_vadd _ _ _

--fp for when the image is a dependent AffineSpace œÜp i, fv for when the
--image is a Module œÜv i, f' for when the image isn't dependent.
variable (fp : (i : Œπ) ‚Üí (P1 ‚Üí·µÉ[k] œÜp i)) (fv : (i : Œπ) ‚Üí (P1 ‚Üí·µÉ[k] œÜv i))
  (f' : Œπ ‚Üí P1 ‚Üí·µÉ[k] P2)

@[simp]
theorem pi_apply (c : P1) (i : Œπ) : pi fp c i = fp i c :=
  rfl

theorem pi_comp (g : P3 ‚Üí·µÉ[k] P1) : (pi fp).comp g = pi (fun i => (fp i).comp g) :=
  rfl

theorem pi_eq_zero : pi fv = 0 ‚Üî ‚àÄ i, fv i = 0 := by
  simp only [AffineMap.ext_iff, funext_iff, pi_apply]
  exact forall_comm

theorem pi_zero : pi (fun _ ‚Ü¶ 0 : (i : Œπ) ‚Üí P1 ‚Üí·µÉ[k] œÜv i) = 0 := by
  ext; rfl

theorem proj_pi (i : Œπ) : (proj i).comp (pi fp) = fp i :=
  ext fun _ => rfl
section Ext

variable [Finite Œπ] [DecidableEq Œπ] {f g : ((i : Œπ) ‚Üí œÜv i) ‚Üí·µÉ[k] P2}

/-- Two affine maps from a Pi-type of modules `(i : Œπ) ‚Üí œÜv i` are equal if they are equal in their
  operation on `Pi.single` and at zero. Analogous to `LinearMap.pi_ext`. See also `pi_ext_nonempty`,
  which instead of agreement at zero requires `Nonempty Œπ`. -/
theorem pi_ext_zero (h : ‚àÄ i x, f (Pi.single i x) = g (Pi.single i x)) (h‚ÇÇ : f 0 = g 0) :
    f = g := by
  apply ext_linear
  ¬∑ apply LinearMap.pi_ext
    intro i x
    have s‚ÇÅ := h i x
    have s‚ÇÇ := f.map_vadd 0 (Pi.single i x)
    have s‚ÇÉ := g.map_vadd 0 (Pi.single i x)
    rw [vadd_eq_add, add_zero] at s‚ÇÇ s‚ÇÉ
    replace h‚ÇÇ := h i 0
    simp only [Pi.single_zero] at h‚ÇÇ
    rwa [s‚ÇÇ, s‚ÇÉ, h‚ÇÇ, vadd_right_cancel_iff] at s‚ÇÅ
  ¬∑ exact h‚ÇÇ

/-- Two affine maps from a Pi-type of modules `(i : Œπ) ‚Üí œÜv i` are equal if they are equal in their
  operation on `Pi.single` and `Œπ` is nonempty.  Analogous to `LinearMap.pi_ext`. See also
  `pi_ext_zero`, which instead of `Nonempty Œπ` requires agreement at 0. -/
theorem pi_ext_nonempty [Nonempty Œπ] (h : ‚àÄ i x, f (Pi.single i x) = g (Pi.single i x)) :
    f = g := by
  apply pi_ext_zero h
  inhabit Œπ
  rw [‚Üê Pi.single_zero default]
  apply h

/-- This is used as the ext lemma instead of `AffineMap.pi_ext_nonempty` for reasons explained in
note [partially-applied ext lemmas]. Analogous to `LinearMap.pi_ext'` -/
@[ext (iff := false)]
theorem pi_ext_nonempty' [Nonempty Œπ] (h : ‚àÄ i, f.comp (LinearMap.single _ _ i).toAffineMap =
    g.comp (LinearMap.single _ _ i).toAffineMap) : f = g := by
  refine pi_ext_nonempty fun i x => ?_
  convert AffineMap.congr_fun (h i) x

end Ext

end Pi

end Ring

section CommRing

variable [CommRing k] [AddCommGroup V1] [AffineSpace V1 P1] [AddCommGroup V2]
variable [Module k V1] [Module k V2]

/-- `homothety c r` is the homothety (also known as dilation) about `c` with scale factor `r`. -/
def homothety (c : P1) (r : k) : P1 ‚Üí·µÉ[k] P1 :=
  r ‚Ä¢ (id k P1 -·µ• const k P1 c) +·µ• const k P1 c

theorem homothety_def (c : P1) (r : k) :
    homothety c r = r ‚Ä¢ (id k P1 -·µ• const k P1 c) +·µ• const k P1 c :=
  rfl

theorem coe_homothety (c : P1) (r : k) : homothety c r = fun p => r ‚Ä¢ (p -·µ• c) +·µ• c :=
  rfl

theorem homothety_apply (c : P1) (r : k) (p : P1) : homothety c r p = r ‚Ä¢ (p -·µ• c : V1) +·µ• c :=
  rfl

@[simp]
theorem homothety_linear (c : P1) (r : k) : (homothety c r).linear = r ‚Ä¢ LinearMap.id := by
  simp [homothety]

theorem homothety_eq_lineMap (c : P1) (r : k) (p : P1) : homothety c r p = lineMap c p r :=
  rfl

@[simp]
theorem homothety_one (c : P1) : homothety c (1 : k) = id k P1 := by
  ext p
  simp [homothety_apply]

@[simp]
theorem homothety_apply_same (c : P1) (r : k) : homothety c r c = c :=
  lineMap_same_apply c r

theorem homothety_mul_apply (c : P1) (r‚ÇÅ r‚ÇÇ : k) (p : P1) :
    homothety c (r‚ÇÅ * r‚ÇÇ) p = homothety c r‚ÇÅ (homothety c r‚ÇÇ p) := by
  simp only [homothety_apply, mul_smul, vadd_vsub]

theorem homothety_mul (c : P1) (r‚ÇÅ r‚ÇÇ : k) :
    homothety c (r‚ÇÅ * r‚ÇÇ) = (homothety c r‚ÇÅ).comp (homothety c r‚ÇÇ) :=
  ext <| homothety_mul_apply c r‚ÇÅ r‚ÇÇ

@[simp]
theorem homothety_zero (c : P1) : homothety c (0 : k) = const k P1 c := by
  ext p
  simp [homothety_apply]

@[simp]
theorem homothety_add (c : P1) (r‚ÇÅ r‚ÇÇ : k) :
    homothety c (r‚ÇÅ + r‚ÇÇ) = r‚ÇÅ ‚Ä¢ (id k P1 -·µ• const k P1 c) +·µ• homothety c r‚ÇÇ := by
  simp only [homothety_def, add_smul, vadd_vadd]

/-- `homothety` as a multiplicative monoid homomorphism. -/
def homothetyHom (c : P1) : k ‚Üí* P1 ‚Üí·µÉ[k] P1 where
  toFun := homothety c
  map_one' := homothety_one c
  map_mul' := homothety_mul c

@[simp]
theorem coe_homothetyHom (c : P1) : ‚áë(homothetyHom c : k ‚Üí* _) = homothety c :=
  rfl

/-- `homothety` as an affine map. -/
def homothetyAffine (c : P1) : k ‚Üí·µÉ[k] P1 ‚Üí·µÉ[k] P1 :=
  ‚ü®homothety c, (LinearMap.lsmul k _).flip (id k P1 -·µ• const k P1 c),
    Function.swap (homothety_add c)‚ü©

@[simp]
theorem coe_homothetyAffine (c : P1) : ‚áë(homothetyAffine c : k ‚Üí·µÉ[k] _) = homothety c :=
  rfl

end CommRing

end AffineMap

section

variable {ùïú E F : Type*} [Ring ùïú] [AddCommGroup E] [AddCommGroup F] [Module ùïú E] [Module ùïú F]

/-- Applying an affine map to an affine combination of two points yields an affine combination of
the images. -/
theorem Convex.combo_affine_apply {x y : E} {a b : ùïú} {f : E ‚Üí·µÉ[ùïú] F} (h : a + b = 1) :
    f (a ‚Ä¢ x + b ‚Ä¢ y) = a ‚Ä¢ f x + b ‚Ä¢ f y := by
  simp only [Convex.combo_eq_smul_sub_add h, ‚Üê vsub_eq_sub]
  exact f.apply_lineMap _ _ _

end
