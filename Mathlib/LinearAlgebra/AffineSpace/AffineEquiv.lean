/-
Copyright (c) 2020 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.LinearAlgebra.AffineSpace.AffineMap
import Mathlib.LinearAlgebra.GeneralLinearGroup
import Mathlib.Algebra.Invertible

#align_import linear_algebra.affine_space.affine_equiv from "leanprover-community/mathlib"@"bd1fc183335ea95a9519a1630bcf901fe9326d83"

/-!
# Affine equivalences

In this file we define `AffineEquiv k P‚ÇÅ P‚ÇÇ` (notation: `P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ`) to be the type of affine
equivalences between `P‚ÇÅ` and `P‚ÇÇ`, i.e., equivalences such that both forward and inverse maps are
affine maps.

We define the following equivalences:

* `AffineEquiv.refl k P`: the identity map as an `AffineEquiv`;

* `e.symm`: the inverse map of an `AffineEquiv` as an `AffineEquiv`;

* `e.trans e'`: composition of two `AffineEquiv`s; note that the order follows `mathlib`'s
  `CategoryTheory` convention (apply `e`, then `e'`), not the convention used in function
  composition and compositions of bundled morphisms.

We equip `AffineEquiv k P P` with a `Group` structure with multiplication corresponding to
composition in `AffineEquiv.group`.

## Tags

affine space, affine equivalence
-/

open Function Set

open Affine

/-- An affine equivalence is an equivalence between affine spaces such that both forward
and inverse maps are affine.

We define it using an `Equiv` for the map and a `LinearEquiv` for the linear part in order
to allow affine equivalences with good definitional equalities. -/
--@[nolint has_nonempty_instance]
structure AffineEquiv (k P‚ÇÅ P‚ÇÇ : Type*) {V‚ÇÅ V‚ÇÇ : Type*} [Ring k] [AddCommGroup V‚ÇÅ] [Module k V‚ÇÅ]
  [AddTorsor V‚ÇÅ P‚ÇÅ] [AddCommGroup V‚ÇÇ] [Module k V‚ÇÇ] [AddTorsor V‚ÇÇ P‚ÇÇ] extends P‚ÇÅ ‚âÉ P‚ÇÇ where
  linear : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ
  map_vadd' : ‚àÄ (p : P‚ÇÅ) (v : V‚ÇÅ), toEquiv (v +·µ• p) = linear v +·µ• toEquiv p
#align affine_equiv AffineEquiv

notation:25 P‚ÇÅ " ‚âÉ·µÉ[" k:25 "] " P‚ÇÇ:0 => AffineEquiv k P‚ÇÅ P‚ÇÇ

variable {k P‚ÇÅ P‚ÇÇ P‚ÇÉ P‚ÇÑ V‚ÇÅ V‚ÇÇ V‚ÇÉ V‚ÇÑ : Type*} [Ring k] [AddCommGroup V‚ÇÅ] [Module k V‚ÇÅ]
  [AddTorsor V‚ÇÅ P‚ÇÅ] [AddCommGroup V‚ÇÇ] [Module k V‚ÇÇ] [AddTorsor V‚ÇÇ P‚ÇÇ] [AddCommGroup V‚ÇÉ]
  [Module k V‚ÇÉ] [AddTorsor V‚ÇÉ P‚ÇÉ] [AddCommGroup V‚ÇÑ] [Module k V‚ÇÑ] [AddTorsor V‚ÇÑ P‚ÇÑ]

namespace AffineEquiv

/-- Reinterpret an `AffineEquiv` as an `AffineMap`. -/
@[coe]
def toAffineMap (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ :=
  { e with }
#align affine_equiv.to_affine_map AffineEquiv.toAffineMap

@[simp]
theorem toAffineMap_mk (f : P‚ÇÅ ‚âÉ P‚ÇÇ) (f' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) (h) :
    toAffineMap (mk f f' h) = ‚ü®f, f', h‚ü© :=
  rfl
#align affine_equiv.to_affine_map_mk AffineEquiv.toAffineMap_mk

@[simp]
theorem linear_toAffineMap (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.toAffineMap.linear = e.linear :=
  rfl
#align affine_equiv.linear_to_affine_map AffineEquiv.linear_toAffineMap

theorem toAffineMap_injective : Injective (toAffineMap : (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) ‚Üí P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ) := by
  rintro ‚ü®e, el, h‚ü© ‚ü®e', el', h'‚ü© H
  -- ‚ä¢ { toEquiv := e, linear := el, map_vadd' := h } = { toEquiv := e', linear :=  ‚Ä¶
  -- porting note: added `()`s and `AffineMap.mk.injEq`
  simp only [(toAffineMap_mk), (AffineMap.mk.injEq), Equiv.coe_inj,
    LinearEquiv.toLinearMap_inj] at H
  congr
  -- ‚ä¢ e = e'
  exacts [H.1, H.2]
  -- üéâ no goals
#align affine_equiv.to_affine_map_injective AffineEquiv.toAffineMap_injective

@[simp]
theorem toAffineMap_inj {e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ} : e.toAffineMap = e'.toAffineMap ‚Üî e = e' :=
  toAffineMap_injective.eq_iff
#align affine_equiv.to_affine_map_inj AffineEquiv.toAffineMap_inj

instance equivLike : EquivLike (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) P‚ÇÅ P‚ÇÇ where
  coe f := f.toFun
  inv f := f.invFun
  left_inv f := f.left_inv
  right_inv f := f.right_inv
  coe_injective' _ _ h _ := toAffineMap_injective (FunLike.coe_injective h)
#align affine_equiv.equiv_like AffineEquiv.equivLike

instance : CoeFun (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) fun _ => P‚ÇÅ ‚Üí P‚ÇÇ :=
  FunLike.hasCoeToFun

instance : CoeOut (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (P‚ÇÅ ‚âÉ P‚ÇÇ) :=
  ‚ü®AffineEquiv.toEquiv‚ü©

@[simp]
theorem map_vadd (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (p : P‚ÇÅ) (v : V‚ÇÅ) : e (v +·µ• p) = e.linear v +·µ• e p :=
  e.map_vadd' p v
#align affine_equiv.map_vadd AffineEquiv.map_vadd

@[simp]
theorem coe_toEquiv (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : ‚áëe.toEquiv = e :=
  rfl
#align affine_equiv.coe_to_equiv AffineEquiv.coe_toEquiv

instance : Coe (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ) :=
  ‚ü®toAffineMap‚ü©

@[simp]
theorem coe_toAffineMap (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : (e.toAffineMap : P‚ÇÅ ‚Üí P‚ÇÇ) = (e : P‚ÇÅ ‚Üí P‚ÇÇ) :=
  rfl
#align affine_equiv.coe_to_affine_map AffineEquiv.coe_toAffineMap

@[norm_cast, simp]
theorem coe_coe (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : ((e : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ) : P‚ÇÅ ‚Üí P‚ÇÇ) = e :=
  rfl
#align affine_equiv.coe_coe AffineEquiv.coe_coe

@[simp]
theorem coe_linear (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : (e : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ).linear = e.linear :=
  rfl
#align affine_equiv.coe_linear AffineEquiv.coe_linear

@[ext]
theorem ext {e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ} (h : ‚àÄ x, e x = e' x) : e = e' :=
  FunLike.ext _ _ h
#align affine_equiv.ext AffineEquiv.ext

theorem coeFn_injective : @Injective (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (P‚ÇÅ ‚Üí P‚ÇÇ) (‚áë) :=
  FunLike.coe_injective
#align affine_equiv.coe_fn_injective AffineEquiv.coeFn_injective

@[norm_cast]
-- Porting note: removed `simp`: proof is `simp only [FunLike.coe_fn_eq]`
theorem coeFn_inj {e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ} : (e : P‚ÇÅ ‚Üí P‚ÇÇ) = e' ‚Üî e = e' :=
  coeFn_injective.eq_iff
#align affine_equiv.coe_fn_inj AffineEquiv.coeFn_inj

theorem toEquiv_injective : Injective (toEquiv : (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) ‚Üí P‚ÇÅ ‚âÉ P‚ÇÇ) := fun _ _ H =>
  ext <| Equiv.ext_iff.1 H
#align affine_equiv.to_equiv_injective AffineEquiv.toEquiv_injective

@[simp]
theorem toEquiv_inj {e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ} : e.toEquiv = e'.toEquiv ‚Üî e = e' :=
  toEquiv_injective.eq_iff
#align affine_equiv.to_equiv_inj AffineEquiv.toEquiv_inj

@[simp]
theorem coe_mk (e : P‚ÇÅ ‚âÉ P‚ÇÇ) (e' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) (h) : ((‚ü®e, e', h‚ü© : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : P‚ÇÅ ‚Üí P‚ÇÇ) = e :=
  rfl
#align affine_equiv.coe_mk AffineEquiv.coe_mk

/-- Construct an affine equivalence by verifying the relation between the map and its linear part at
one base point. Namely, this function takes a map `e : P‚ÇÅ ‚Üí P‚ÇÇ`, a linear equivalence
`e' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ`, and a point `p` such that for any other point `p'` we have
`e p' = e' (p' -·µ• p) +·µ• e p`. -/
def mk' (e : P‚ÇÅ ‚Üí P‚ÇÇ) (e' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) (p : P‚ÇÅ) (h : ‚àÄ p' : P‚ÇÅ, e p' = e' (p' -·µ• p) +·µ• e p) :
    P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ where
  toFun := e
  invFun := fun q' : P‚ÇÇ => e'.symm (q' -·µ• e p) +·µ• p
  -- Porting note: `simp` needs `()`
  left_inv p' := by simp [h p', (vadd_vsub), (vsub_vadd)]
                    -- üéâ no goals
  -- Porting note: `simp` needs `()`
  right_inv q' := by simp [h (e'.symm (q' -·µ• e p) +·µ• p), (vadd_vsub), (vsub_vadd)]
                     -- üéâ no goals
  linear := e'
  -- Porting note: `simp` needs `()`
  map_vadd' p' v := by simp [h p', h (v +·µ• p'), (vadd_vsub_assoc), (vadd_vadd)]
                       -- üéâ no goals
#align affine_equiv.mk' AffineEquiv.mk'

@[simp]
theorem coe_mk' (e : P‚ÇÅ ‚âÉ P‚ÇÇ) (e' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) (p h) : ‚áë(mk' e e' p h) = e :=
  rfl
#align affine_equiv.coe_mk' AffineEquiv.coe_mk'

@[simp]
theorem linear_mk' (e : P‚ÇÅ ‚âÉ P‚ÇÇ) (e' : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) (p h) : (mk' e e' p h).linear = e' :=
  rfl
#align affine_equiv.linear_mk' AffineEquiv.linear_mk'

/-- Inverse of an affine equivalence as an affine equivalence. -/
@[symm]
def symm (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÅ where
  toEquiv := e.toEquiv.symm
  linear := e.linear.symm
  map_vadd' p v :=
    e.toEquiv.symm.apply_eq_iff_eq_symm_apply.2 <| by
      rw [Equiv.symm_symm, e.map_vadd' ((Equiv.symm e.toEquiv) p) ((LinearEquiv.symm e.linear) v),
        LinearEquiv.apply_symm_apply, Equiv.apply_symm_apply]
#align affine_equiv.symm AffineEquiv.symm

@[simp]
theorem symm_toEquiv (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.toEquiv.symm = e.symm.toEquiv :=
  rfl
#align affine_equiv.symm_to_equiv AffineEquiv.symm_toEquiv

@[simp]
theorem symm_linear (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.linear.symm = e.symm.linear :=
  rfl
#align affine_equiv.symm_linear AffineEquiv.symm_linear

/-- See Note [custom simps projection] -/
def Simps.apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : P‚ÇÅ ‚Üí P‚ÇÇ :=
  e
#align affine_equiv.simps.apply AffineEquiv.Simps.apply

/-- See Note [custom simps projection] -/
def Simps.symm_apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : P‚ÇÇ ‚Üí P‚ÇÅ :=
  e.symm
#align affine_equiv.simps.symm_apply AffineEquiv.Simps.symm_apply

initialize_simps_projections AffineEquiv (toEquiv_toFun ‚Üí apply, toEquiv_invFun ‚Üí symm_apply,
  linear ‚Üí linear, as_prefix linear, -toEquiv)

protected theorem bijective (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : Bijective e :=
  e.toEquiv.bijective
#align affine_equiv.bijective AffineEquiv.bijective

protected theorem surjective (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : Surjective e :=
  e.toEquiv.surjective
#align affine_equiv.surjective AffineEquiv.surjective

protected theorem injective (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : Injective e :=
  e.toEquiv.injective
#align affine_equiv.injective AffineEquiv.injective

/-- Bijective affine maps are affine isomorphisms. -/
@[simps! linear apply]
noncomputable def ofBijective {œÜ : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ} (hœÜ : Function.Bijective œÜ) : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ :=
  { Equiv.ofBijective _ hœÜ with
    linear := LinearEquiv.ofBijective œÜ.linear (œÜ.linear_bijective_iff.mpr hœÜ)
    map_vadd' := œÜ.map_vadd }
#align affine_equiv.of_bijective AffineEquiv.ofBijective

theorem ofBijective.symm_eq {œÜ : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÇ} (hœÜ : Function.Bijective œÜ) :
    (ofBijective hœÜ).symm.toEquiv = (Equiv.ofBijective _ hœÜ).symm :=
  rfl
#align affine_equiv.of_bijective.symm_eq AffineEquiv.ofBijective.symm_eq

@[simp]
theorem range_eq (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : range e = univ :=
  e.surjective.range_eq
#align affine_equiv.range_eq AffineEquiv.range_eq

@[simp]
theorem apply_symm_apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (p : P‚ÇÇ) : e (e.symm p) = p :=
  e.toEquiv.apply_symm_apply p
#align affine_equiv.apply_symm_apply AffineEquiv.apply_symm_apply

@[simp]
theorem symm_apply_apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (p : P‚ÇÅ) : e.symm (e p) = p :=
  e.toEquiv.symm_apply_apply p
#align affine_equiv.symm_apply_apply AffineEquiv.symm_apply_apply

theorem apply_eq_iff_eq_symm_apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) {p‚ÇÅ p‚ÇÇ} : e p‚ÇÅ = p‚ÇÇ ‚Üî p‚ÇÅ = e.symm p‚ÇÇ :=
  e.toEquiv.apply_eq_iff_eq_symm_apply
#align affine_equiv.apply_eq_iff_eq_symm_apply AffineEquiv.apply_eq_iff_eq_symm_apply

-- Porting note: removed `simp`, proof is `by simp only [@EmbeddingLike.apply_eq_iff_eq]`
theorem apply_eq_iff_eq (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) {p‚ÇÅ p‚ÇÇ : P‚ÇÅ} : e p‚ÇÅ = e p‚ÇÇ ‚Üî p‚ÇÅ = p‚ÇÇ :=
  e.toEquiv.apply_eq_iff_eq
#align affine_equiv.apply_eq_iff_eq AffineEquiv.apply_eq_iff_eq

@[simp]
theorem image_symm (f : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (s : Set P‚ÇÇ) : f.symm '' s = f ‚Åª¬π' s :=
  f.symm.toEquiv.image_eq_preimage _
#align affine_equiv.image_symm AffineEquiv.image_symm

@[simp]
theorem preimage_symm (f : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (s : Set P‚ÇÅ) : f.symm ‚Åª¬π' s = f '' s :=
  (f.symm.image_symm _).symm
#align affine_equiv.preimage_symm AffineEquiv.preimage_symm

variable (k P‚ÇÅ)

/-- Identity map as an `AffineEquiv`. -/
-- @[refl] -- Porting note: removed attribute
def refl : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ where
  toEquiv := Equiv.refl P‚ÇÅ
  linear := LinearEquiv.refl k V‚ÇÅ
  map_vadd' _ _ := rfl
#align affine_equiv.refl AffineEquiv.refl

@[simp]
theorem coe_refl : ‚áë(refl k P‚ÇÅ) = id :=
  rfl
#align affine_equiv.coe_refl AffineEquiv.coe_refl

@[simp]
theorem coe_refl_to_affineMap : ‚Üë(refl k P‚ÇÅ) = AffineMap.id k P‚ÇÅ :=
  rfl
#align affine_equiv.coe_refl_to_affine_map AffineEquiv.coe_refl_to_affineMap

@[simp]
theorem refl_apply (x : P‚ÇÅ) : refl k P‚ÇÅ x = x :=
  rfl
#align affine_equiv.refl_apply AffineEquiv.refl_apply

@[simp]
theorem toEquiv_refl : (refl k P‚ÇÅ).toEquiv = Equiv.refl P‚ÇÅ :=
  rfl
#align affine_equiv.to_equiv_refl AffineEquiv.toEquiv_refl

@[simp]
theorem linear_refl : (refl k P‚ÇÅ).linear = LinearEquiv.refl k V‚ÇÅ :=
  rfl
#align affine_equiv.linear_refl AffineEquiv.linear_refl

@[simp]
theorem symm_refl : (refl k P‚ÇÅ).symm = refl k P‚ÇÅ :=
  rfl
#align affine_equiv.symm_refl AffineEquiv.symm_refl

variable {k P‚ÇÅ}

/-- Composition of two `AffineEquiv`alences, applied left to right. -/
@[trans]
def trans (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (e' : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÉ) : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÉ where
  toEquiv := e.toEquiv.trans e'.toEquiv
  linear := e.linear.trans e'.linear
  map_vadd' p v := by
    -- porting note: added `()`
    simp only [LinearEquiv.trans_apply, (coe_toEquiv), (¬∑ ‚àò ¬∑), Equiv.coe_trans, (map_vadd)]
    -- üéâ no goals
#align affine_equiv.trans AffineEquiv.trans

@[simp]
theorem coe_trans (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (e' : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÉ) : ‚áë(e.trans e') = e' ‚àò e :=
  rfl
#align affine_equiv.coe_trans AffineEquiv.coe_trans

@[simp]
theorem coe_trans_to_affineMap (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (e' : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÉ) :
    (e.trans e' : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÉ) = (e' : P‚ÇÇ ‚Üí·µÉ[k] P‚ÇÉ).comp e :=
  rfl
#align affine_equiv.coe_trans_to_affine_map AffineEquiv.coe_trans_to_affineMap

@[simp]
theorem trans_apply (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (e' : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÉ) (p : P‚ÇÅ) : e.trans e' p = e' (e p) :=
  rfl
#align affine_equiv.trans_apply AffineEquiv.trans_apply

theorem trans_assoc (e‚ÇÅ : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (e‚ÇÇ : P‚ÇÇ ‚âÉ·µÉ[k] P‚ÇÉ) (e‚ÇÉ : P‚ÇÉ ‚âÉ·µÉ[k] P‚ÇÑ) :
    (e‚ÇÅ.trans e‚ÇÇ).trans e‚ÇÉ = e‚ÇÅ.trans (e‚ÇÇ.trans e‚ÇÉ) :=
  ext fun _ => rfl
#align affine_equiv.trans_assoc AffineEquiv.trans_assoc

@[simp]
theorem trans_refl (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.trans (refl k P‚ÇÇ) = e :=
  ext fun _ => rfl
#align affine_equiv.trans_refl AffineEquiv.trans_refl

@[simp]
theorem refl_trans (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : (refl k P‚ÇÅ).trans e = e :=
  ext fun _ => rfl
#align affine_equiv.refl_trans AffineEquiv.refl_trans

@[simp]
theorem self_trans_symm (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.trans e.symm = refl k P‚ÇÅ :=
  ext e.symm_apply_apply
#align affine_equiv.self_trans_symm AffineEquiv.self_trans_symm

@[simp]
theorem symm_trans_self (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) : e.symm.trans e = refl k P‚ÇÇ :=
  ext e.apply_symm_apply
#align affine_equiv.symm_trans_self AffineEquiv.symm_trans_self

@[simp]
theorem apply_lineMap (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÇ) (a b : P‚ÇÅ) (c : k) :
    e (AffineMap.lineMap a b c) = AffineMap.lineMap (e a) (e b) c :=
  e.toAffineMap.apply_lineMap a b c
#align affine_equiv.apply_line_map AffineEquiv.apply_lineMap

instance group : Group (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) where
  one := refl k P‚ÇÅ
  mul e e' := e'.trans e
  inv := symm
  mul_assoc e‚ÇÅ e‚ÇÇ e‚ÇÉ := trans_assoc _ _ _
  one_mul := trans_refl
  mul_one := refl_trans
  mul_left_inv := self_trans_symm
#align affine_equiv.group AffineEquiv.group

theorem one_def : (1 : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) = refl k P‚ÇÅ :=
  rfl
#align affine_equiv.one_def AffineEquiv.one_def

@[simp]
theorem coe_one : ‚áë(1 : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) = id :=
  rfl
#align affine_equiv.coe_one AffineEquiv.coe_one

theorem mul_def (e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) : e * e' = e'.trans e :=
  rfl
#align affine_equiv.mul_def AffineEquiv.mul_def

@[simp]
theorem coe_mul (e e' : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) : ‚áë(e * e') = e ‚àò e' :=
  rfl
#align affine_equiv.coe_mul AffineEquiv.coe_mul

theorem inv_def (e : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) : e‚Åª¬π = e.symm :=
  rfl
#align affine_equiv.inv_def AffineEquiv.inv_def

/-- `AffineEquiv.linear` on automorphisms is a `MonoidHom`. -/
@[simps]
def linearHom : (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) ‚Üí* V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÅ where
  toFun := linear
  map_one' := rfl
  map_mul' _ _ := rfl
#align affine_equiv.linear_hom AffineEquiv.linearHom

/-- The group of `AffineEquiv`s are equivalent to the group of units of `AffineMap`.

This is the affine version of `LinearMap.GeneralLinearGroup.generalLinearEquiv`. -/
@[simps]
def equivUnitsAffineMap : (P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ) ‚âÉ* (P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÅ)À£ where
  toFun e :=
    { val := e, inv := e.symm,
      val_inv := congr_arg toAffineMap e.symm_trans_self
      inv_val := congr_arg toAffineMap e.self_trans_symm }
  invFun u :=
    { toFun := (u : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÅ)
      invFun := (‚Üëu‚Åª¬π : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÅ)
      left_inv := AffineMap.congr_fun u.inv_mul
      right_inv := AffineMap.congr_fun u.mul_inv
      linear :=
        LinearMap.GeneralLinearGroup.generalLinearEquiv _ _ <| Units.map AffineMap.linearHom u
      map_vadd' := fun _ _ => (u : P‚ÇÅ ‚Üí·µÉ[k] P‚ÇÅ).map_vadd _ _ }
  left_inv _ := AffineEquiv.ext fun _ => rfl
  right_inv _ := Units.ext <| AffineMap.ext fun _ => rfl
  map_mul' _ _ := rfl
#align affine_equiv.equiv_units_affine_map AffineEquiv.equivUnitsAffineMap

variable (k)

/-- The map `v ‚Ü¶ v +·µ• b` as an affine equivalence between a module `V` and an affine space `P` with
tangent space `V`. -/
@[simps! linear apply symm_apply]
def vaddConst (b : P‚ÇÅ) : V‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ where
  toEquiv := Equiv.vaddConst b
  linear := LinearEquiv.refl _ _
  map_vadd' _ _ := add_vadd _ _ _
#align affine_equiv.vadd_const AffineEquiv.vaddConst

/-- `p' ‚Ü¶ p -·µ• p'` as an equivalence. -/
def constVSub (p : P‚ÇÅ) : P‚ÇÅ ‚âÉ·µÉ[k] V‚ÇÅ where
  toEquiv := Equiv.constVSub p
  linear := LinearEquiv.neg k
  -- porting note: added `coe_constVSub` and `()`s
  map_vadd' p' v := by simp [(Equiv.coe_constVSub), (vsub_vadd_eq_vsub_sub), neg_add_eq_sub]
                       -- üéâ no goals
#align affine_equiv.const_vsub AffineEquiv.constVSub

@[simp]
theorem coe_constVSub (p : P‚ÇÅ) : ‚áë(constVSub k p) = (¬∑ -·µ• ¬∑) p :=
  rfl
#align affine_equiv.coe_const_vsub AffineEquiv.coe_constVSub

@[simp]
theorem coe_constVSub_symm (p : P‚ÇÅ) : ‚áë(constVSub k p).symm = fun v : V‚ÇÅ => -v +·µ• p :=
  rfl
#align affine_equiv.coe_const_vsub_symm AffineEquiv.coe_constVSub_symm

variable (P‚ÇÅ)

/-- The map `p ‚Ü¶ v +·µ• p` as an affine automorphism of an affine space.

Note that there is no need for an `AffineMap.constVAdd` as it is always an equivalence.
This is roughly to `DistribMulAction.toLinearEquiv` as `+·µ•` is to `‚Ä¢`. -/
@[simps! apply linear]
def constVAdd (v : V‚ÇÅ) : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ where
  toEquiv := Equiv.constVAdd P‚ÇÅ v
  linear := LinearEquiv.refl _ _
  map_vadd' _ _ := vadd_comm _ _ _
#align affine_equiv.const_vadd AffineEquiv.constVAdd

@[simp]
theorem constVAdd_zero : constVAdd k P‚ÇÅ 0 = AffineEquiv.refl _ _ :=
  ext <| zero_vadd _
#align affine_equiv.const_vadd_zero AffineEquiv.constVAdd_zero

@[simp]
theorem constVAdd_add (v w : V‚ÇÅ) :
    constVAdd k P‚ÇÅ (v + w) = (constVAdd k P‚ÇÅ w).trans (constVAdd k P‚ÇÅ v) :=
  ext <| add_vadd _ _
#align affine_equiv.const_vadd_add AffineEquiv.constVAdd_add

@[simp]
theorem constVAdd_symm (v : V‚ÇÅ) : (constVAdd k P‚ÇÅ v).symm = constVAdd k P‚ÇÅ (-v) :=
  ext fun _ => rfl
#align affine_equiv.const_vadd_symm AffineEquiv.constVAdd_symm

/-- A more bundled version of `AffineEquiv.constVAdd`. -/
@[simps]
def constVAddHom : Multiplicative V‚ÇÅ ‚Üí* P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ where
  toFun v := constVAdd k P‚ÇÅ (Multiplicative.toAdd v)
  map_one' := constVAdd_zero _ _
  map_mul' := constVAdd_add _ P‚ÇÅ
#align affine_equiv.const_vadd_hom AffineEquiv.constVAddHom

theorem constVAdd_nsmul (n : ‚Ñï) (v : V‚ÇÅ) : constVAdd k P‚ÇÅ (n ‚Ä¢ v) = constVAdd k P‚ÇÅ v ^ n :=
  (constVAddHom k P‚ÇÅ).map_pow _ _
#align affine_equiv.const_vadd_nsmul AffineEquiv.constVAdd_nsmul

theorem constVAdd_zsmul (z : ‚Ñ§) (v : V‚ÇÅ) : constVAdd k P‚ÇÅ (z ‚Ä¢ v) = constVAdd k P‚ÇÅ v ^ z :=
  (constVAddHom k P‚ÇÅ).map_zpow _ _
#align affine_equiv.const_vadd_zsmul AffineEquiv.constVAdd_zsmul

section Homothety

variable {R V P : Type*} [CommRing R] [AddCommGroup V] [Module R V] [AffineSpace V P]

/-- Fixing a point in affine space, homothety about this point gives a group homomorphism from (the
centre of) the units of the scalars into the group of affine equivalences. -/
def homothetyUnitsMulHom (p : P) : RÀ£ ‚Üí* P ‚âÉ·µÉ[R] P :=
  equivUnitsAffineMap.symm.toMonoidHom.comp <| Units.map (AffineMap.homothetyHom p)
#align affine_equiv.homothety_units_mul_hom AffineEquiv.homothetyUnitsMulHom

@[simp]
theorem coe_homothetyUnitsMulHom_apply (p : P) (t : RÀ£) :
    (homothetyUnitsMulHom p t : P ‚Üí P) = AffineMap.homothety p (t : R) :=
  rfl
#align affine_equiv.coe_homothety_units_mul_hom_apply AffineEquiv.coe_homothetyUnitsMulHom_apply

@[simp]
theorem coe_homothetyUnitsMulHom_apply_symm (p : P) (t : RÀ£) :
    ((homothetyUnitsMulHom p t).symm : P ‚Üí P) = AffineMap.homothety p (‚Üët‚Åª¬π : R) :=
  rfl
#align affine_equiv.coe_homothety_units_mul_hom_apply_symm AffineEquiv.coe_homothetyUnitsMulHom_apply_symm

@[simp]
theorem coe_homothetyUnitsMulHom_eq_homothetyHom_coe (p : P) :
    ((‚Üë) : (P ‚âÉ·µÉ[R] P) ‚Üí P ‚Üí·µÉ[R] P) ‚àò homothetyUnitsMulHom p =
      AffineMap.homothetyHom p ‚àò ((‚Üë) : RÀ£ ‚Üí R) :=
  funext fun _ => rfl
#align affine_equiv.coe_homothety_units_mul_hom_eq_homothety_hom_coe AffineEquiv.coe_homothetyUnitsMulHom_eq_homothetyHom_coe

end Homothety

variable {P‚ÇÅ}

open Function

/-- Point reflection in `x` as a permutation. -/
def pointReflection (x : P‚ÇÅ) : P‚ÇÅ ‚âÉ·µÉ[k] P‚ÇÅ :=
  (constVSub k x).trans (vaddConst k x)
#align affine_equiv.point_reflection AffineEquiv.pointReflection

theorem pointReflection_apply (x y : P‚ÇÅ) : pointReflection k x y = x -·µ• y +·µ• x :=
  rfl
#align affine_equiv.point_reflection_apply AffineEquiv.pointReflection_apply

@[simp]
theorem pointReflection_symm (x : P‚ÇÅ) : (pointReflection k x).symm = pointReflection k x :=
  toEquiv_injective <| Equiv.pointReflection_symm x
#align affine_equiv.point_reflection_symm AffineEquiv.pointReflection_symm

@[simp]
theorem toEquiv_pointReflection (x : P‚ÇÅ) :
    (pointReflection k x).toEquiv = Equiv.pointReflection x :=
  rfl
#align affine_equiv.to_equiv_point_reflection AffineEquiv.toEquiv_pointReflection

@[simp]
theorem pointReflection_self (x : P‚ÇÅ) : pointReflection k x x = x :=
  vsub_vadd _ _
#align affine_equiv.point_reflection_self AffineEquiv.pointReflection_self

theorem pointReflection_involutive (x : P‚ÇÅ) : Involutive (pointReflection k x : P‚ÇÅ ‚Üí P‚ÇÅ) :=
  Equiv.pointReflection_involutive x
#align affine_equiv.point_reflection_involutive AffineEquiv.pointReflection_involutive

set_option linter.deprecated false in
/-- `x` is the only fixed point of `pointReflection x`. This lemma requires
`x + x = y + y ‚Üî x = y`. There is no typeclass to use here, so we add it as an explicit argument. -/
theorem pointReflection_fixed_iff_of_injective_bit0 {x y : P‚ÇÅ} (h : Injective (bit0 : V‚ÇÅ ‚Üí V‚ÇÅ)) :
    pointReflection k x y = y ‚Üî y = x :=
  Equiv.pointReflection_fixed_iff_of_injective_bit0 h
#align affine_equiv.point_reflection_fixed_iff_of_injective_bit0 AffineEquiv.pointReflection_fixed_iff_of_injective_bit0

set_option linter.deprecated false in
theorem injective_pointReflection_left_of_injective_bit0 (h : Injective (bit0 : V‚ÇÅ ‚Üí V‚ÇÅ)) (y : P‚ÇÅ) :
    Injective fun x : P‚ÇÅ => pointReflection k x y :=
  Equiv.injective_pointReflection_left_of_injective_bit0 h y
#align affine_equiv.injective_point_reflection_left_of_injective_bit0 AffineEquiv.injective_pointReflection_left_of_injective_bit0

theorem injective_pointReflection_left_of_module [Invertible (2 : k)] :
    ‚àÄ y, Injective fun x : P‚ÇÅ => pointReflection k x y :=
  injective_pointReflection_left_of_injective_bit0 k fun x y h => by
    rwa [bit0, bit0, ‚Üê two_smul k x, ‚Üê two_smul k y,
      (isUnit_of_invertible (2 : k)).smul_left_cancel] at h
#align affine_equiv.injective_point_reflection_left_of_module AffineEquiv.injective_pointReflection_left_of_module

theorem pointReflection_fixed_iff_of_module [Invertible (2 : k)] {x y : P‚ÇÅ} :
    pointReflection k x y = y ‚Üî y = x :=
  ((injective_pointReflection_left_of_module k y).eq_iff' (pointReflection_self k y)).trans eq_comm
#align affine_equiv.point_reflection_fixed_iff_of_module AffineEquiv.pointReflection_fixed_iff_of_module

end AffineEquiv

namespace LinearEquiv

/-- Interpret a linear equivalence between modules as an affine equivalence. -/
def toAffineEquiv (e : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) : V‚ÇÅ ‚âÉ·µÉ[k] V‚ÇÇ where
  toEquiv := e.toEquiv
  linear := e
  map_vadd' p v := e.map_add v p
#align linear_equiv.to_affine_equiv LinearEquiv.toAffineEquiv

@[simp]
theorem coe_toAffineEquiv (e : V‚ÇÅ ‚âÉ‚Çó[k] V‚ÇÇ) : ‚áëe.toAffineEquiv = e :=
  rfl
#align linear_equiv.coe_to_affine_equiv LinearEquiv.coe_toAffineEquiv

end LinearEquiv

namespace AffineMap

open AffineEquiv

theorem lineMap_vadd (v v' : V‚ÇÅ) (p : P‚ÇÅ) (c : k) :
    lineMap v v' c +·µ• p = lineMap (v +·µ• p) (v' +·µ• p) c :=
  (vaddConst k p).apply_lineMap v v' c
#align affine_map.line_map_vadd AffineMap.lineMap_vadd

theorem lineMap_vsub (p‚ÇÅ p‚ÇÇ p‚ÇÉ : P‚ÇÅ) (c : k) :
    lineMap p‚ÇÅ p‚ÇÇ c -·µ• p‚ÇÉ = lineMap (p‚ÇÅ -·µ• p‚ÇÉ) (p‚ÇÇ -·µ• p‚ÇÉ) c :=
  (vaddConst k p‚ÇÉ).symm.apply_lineMap p‚ÇÅ p‚ÇÇ c
#align affine_map.line_map_vsub AffineMap.lineMap_vsub

theorem vsub_lineMap (p‚ÇÅ p‚ÇÇ p‚ÇÉ : P‚ÇÅ) (c : k) :
    p‚ÇÅ -·µ• lineMap p‚ÇÇ p‚ÇÉ c = lineMap (p‚ÇÅ -·µ• p‚ÇÇ) (p‚ÇÅ -·µ• p‚ÇÉ) c :=
  (constVSub k p‚ÇÅ).apply_lineMap p‚ÇÇ p‚ÇÉ c
#align affine_map.vsub_line_map AffineMap.vsub_lineMap

theorem vadd_lineMap (v : V‚ÇÅ) (p‚ÇÅ p‚ÇÇ : P‚ÇÅ) (c : k) :
    v +·µ• lineMap p‚ÇÅ p‚ÇÇ c = lineMap (v +·µ• p‚ÇÅ) (v +·µ• p‚ÇÇ) c :=
  (constVAdd k P‚ÇÅ v).apply_lineMap p‚ÇÅ p‚ÇÇ c
#align affine_map.vadd_line_map AffineMap.vadd_lineMap

variable {R' : Type*} [CommRing R'] [Module R' V‚ÇÅ]

theorem homothety_neg_one_apply (c p : P‚ÇÅ) : homothety c (-1 : R') p = pointReflection R' c p := by
  -- porting note: added `()`, `_`, and `neg_vsub_eq_vsub_rev`
  simp [(homothety_apply), pointReflection_apply _, (neg_vsub_eq_vsub_rev)]
  -- üéâ no goals
#align affine_map.homothety_neg_one_apply AffineMap.homothety_neg_one_apply

end AffineMap
