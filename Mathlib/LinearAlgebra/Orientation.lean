/-
Copyright (c) 2021 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers
-/
import Mathlib.LinearAlgebra.Ray
import Mathlib.LinearAlgebra.Determinant

#align_import linear_algebra.orientation from "leanprover-community/mathlib"@"ce11c3c2a285bbe6937e26d9792fda4e51f3fe1a"

/-!
# Orientations of modules

This file defines orientations of modules.

## Main definitions

* `Orientation` is a type synonym for `Module.Ray` for the case where the module is that of
alternating maps from a module to its underlying ring.  An orientation may be associated with an
alternating map or with a basis.

* `Module.Oriented` is a type class for a choice of orientation of a module that is considered
the positive orientation.

## Implementation notes

`Orientation` is defined for an arbitrary index type, but the main intended use case is when
that index type is a `Fintype` and there exists a basis of the same cardinality.

## References

* https://en.wikipedia.org/wiki/Orientation_(vector_space)

-/


noncomputable section

open BigOperators

section OrderedCommSemiring

variable (R : Type*) [StrictOrderedCommSemiring R]

variable (M : Type*) [AddCommMonoid M] [Module R M]

variable {N : Type*} [AddCommMonoid N] [Module R N]

variable (Œπ : Type*)

/-- An orientation of a module, intended to be used when `Œπ` is a `Fintype` with the same
cardinality as a basis. -/
abbrev Orientation := Module.Ray R (AlternatingMap R M R Œπ)
#align orientation Orientation

/-- A type class fixing an orientation of a module. -/
class Module.Oriented where
  /-- Fix a positive orientation. -/
  positiveOrientation : Orientation R M Œπ
#align module.oriented Module.Oriented

export Module.Oriented (positiveOrientation)

variable {R M}

/-- An equivalence between modules implies an equivalence between orientations. -/
def Orientation.map (e : M ‚âÉ‚Çó[R] N) : Orientation R M Œπ ‚âÉ Orientation R N Œπ :=
  Module.Ray.map <| AlternatingMap.domLCongr R R Œπ R e
#align orientation.map Orientation.map

@[simp]
theorem Orientation.map_apply (e : M ‚âÉ‚Çó[R] N) (v : AlternatingMap R M R Œπ) (hv : v ‚â† 0) :
    Orientation.map Œπ e (rayOfNeZero _ v hv) =
      rayOfNeZero _ (v.compLinearMap e.symm) (mt (v.compLinearEquiv_eq_zero_iff e.symm).mp hv) :=
  rfl
#align orientation.map_apply Orientation.map_apply

@[simp]
theorem Orientation.map_refl : (Orientation.map Œπ <| LinearEquiv.refl R M) = Equiv.refl _ := by
  rw [Orientation.map, AlternatingMap.domLCongr_refl, Module.Ray.map_refl]
  -- üéâ no goals
#align orientation.map_refl Orientation.map_refl

@[simp]
theorem Orientation.map_symm (e : M ‚âÉ‚Çó[R] N) :
    (Orientation.map Œπ e).symm = Orientation.map Œπ e.symm := rfl
#align orientation.map_symm Orientation.map_symm

/-- A module is canonically oriented with respect to an empty index type. -/
instance (priority := 100) IsEmpty.oriented [Nontrivial R] [IsEmpty Œπ] : Module.Oriented R M Œπ
    where positiveOrientation :=
    rayOfNeZero R (AlternatingMap.constLinearEquivOfIsEmpty 1) <|
      AlternatingMap.constLinearEquivOfIsEmpty.injective.ne (by exact one_ne_zero)
                                                                -- üéâ no goals
#align is_empty.oriented IsEmpty.oriented

@[simp]
theorem Orientation.map_positiveOrientation_of_isEmpty [Nontrivial R] [IsEmpty Œπ] (f : M ‚âÉ‚Çó[R] N) :
    Orientation.map Œπ f positiveOrientation = positiveOrientation := rfl
#align orientation.map_positive_orientation_of_is_empty Orientation.map_positiveOrientation_of_isEmpty

@[simp]
theorem Orientation.map_of_isEmpty [IsEmpty Œπ] (x : Orientation R M Œπ) (f : M ‚âÉ‚Çó[R] M) :
    Orientation.map Œπ f x = x := by
  induction' x using Module.Ray.ind with g hg
  -- ‚ä¢ ‚Üë(map Œπ f) (rayOfNeZero R g hg) = rayOfNeZero R g hg
  rw [Orientation.map_apply]
  -- ‚ä¢ rayOfNeZero R (AlternatingMap.compLinearMap g ‚Üë(LinearEquiv.symm f)) (_ : ¬¨A ‚Ä¶
  congr
  -- ‚ä¢ AlternatingMap.compLinearMap g ‚Üë(LinearEquiv.symm f) = g
  ext i
  -- ‚ä¢ ‚Üë(AlternatingMap.compLinearMap g ‚Üë(LinearEquiv.symm f)) i = ‚Üëg i
  rw [AlternatingMap.compLinearMap_apply]
  -- ‚ä¢ (‚Üëg fun i_1 => ‚Üë‚Üë(LinearEquiv.symm f) (i i_1)) = ‚Üëg i
  congr
  -- ‚ä¢ (fun i_1 => ‚Üë‚Üë(LinearEquiv.symm f) (i i_1)) = i
  simp only [LinearEquiv.coe_coe, eq_iff_true_of_subsingleton]
  -- üéâ no goals
#align orientation.map_of_is_empty Orientation.map_of_isEmpty

end OrderedCommSemiring

section OrderedCommRing

variable {R : Type*} [StrictOrderedCommRing R]

variable {M N : Type*} [AddCommGroup M] [AddCommGroup N] [Module R M] [Module R N]

@[simp]
protected theorem Orientation.map_neg {Œπ : Type*} (f : M ‚âÉ‚Çó[R] N) (x : Orientation R M Œπ) :
    Orientation.map Œπ f (-x) = -Orientation.map Œπ f x :=
  Module.Ray.map_neg _ x
#align orientation.map_neg Orientation.map_neg

namespace Basis

variable {Œπ : Type*}

/-- The value of `Orientation.map` when the index type has the cardinality of a basis, in terms
of `f.det`. -/
theorem map_orientation_eq_det_inv_smul [Finite Œπ] (e : Basis Œπ R M) (x : Orientation R M Œπ)
    (f : M ‚âÉ‚Çó[R] M) : Orientation.map Œπ f x = (LinearEquiv.det f)‚Åª¬π ‚Ä¢ x := by
  cases nonempty_fintype Œπ
  -- ‚ä¢ ‚Üë(Orientation.map Œπ f) x = (‚ÜëLinearEquiv.det f)‚Åª¬π ‚Ä¢ x
  letI := Classical.decEq Œπ
  -- ‚ä¢ ‚Üë(Orientation.map Œπ f) x = (‚ÜëLinearEquiv.det f)‚Åª¬π ‚Ä¢ x
  induction' x using Module.Ray.ind with g hg
  -- ‚ä¢ ‚Üë(Orientation.map Œπ f) (rayOfNeZero R g hg) = (‚ÜëLinearEquiv.det f)‚Åª¬π ‚Ä¢ rayOf ‚Ä¶
  rw [Orientation.map_apply, smul_rayOfNeZero, ray_eq_iff, Units.smul_def,
    (g.compLinearMap f.symm).eq_smul_basis_det e, g.eq_smul_basis_det e,
    AlternatingMap.compLinearMap_apply, AlternatingMap.smul_apply,
    show (fun i ‚Ü¶ (LinearEquiv.symm f).toLinearMap (e i)) = (LinearEquiv.symm f).toLinearMap ‚àò e
    by rfl, Basis.det_comp, Basis.det_self, mul_one, smul_eq_mul, mul_comm, mul_smul,
    LinearEquiv.coe_inv_det]
#align basis.map_orientation_eq_det_inv_smul Basis.map_orientation_eq_det_inv_smul

variable [Fintype Œπ] [DecidableEq Œπ]

/-- The orientation given by a basis. -/
protected def orientation [Nontrivial R] (e : Basis Œπ R M) : Orientation R M Œπ :=
  rayOfNeZero R _ e.det_ne_zero
#align basis.orientation Basis.orientation

theorem orientation_map [Nontrivial R] (e : Basis Œπ R M) (f : M ‚âÉ‚Çó[R] N) :
    (e.map f).orientation = Orientation.map Œπ f e.orientation := by
  simp_rw [Basis.orientation, Orientation.map_apply, Basis.det_map']
  -- üéâ no goals
#align basis.orientation_map Basis.orientation_map

/-- The orientation given by a basis derived using `units_smul`, in terms of the product of those
units. -/
theorem orientation_unitsSMul [Nontrivial R] (e : Basis Œπ R M) (w : Œπ ‚Üí Units R) :
    (e.unitsSMul w).orientation = (‚àè i, w i)‚Åª¬π ‚Ä¢ e.orientation := by
  rw [Basis.orientation, Basis.orientation, smul_rayOfNeZero, ray_eq_iff,
    e.det.eq_smul_basis_det (e.unitsSMul w), det_unitsSMul_self, Units.smul_def, smul_smul]
  norm_cast
  -- ‚ä¢ SameRay R (det (unitsSMul e w)) (‚Üë((‚àè i : Œπ, w i)‚Åª¬π * ‚àè i : Œπ, w i) ‚Ä¢ det (u ‚Ä¶
  simp
  -- ‚ä¢ SameRay R (det (unitsSMul e w)) (det (unitsSMul e w))
  exact SameRay.rfl
  -- üéâ no goals
#align basis.orientation_units_smul Basis.orientation_unitsSMul

@[simp]
theorem orientation_isEmpty [Nontrivial R] [IsEmpty Œπ] (b : Basis Œπ R M) :
    b.orientation = positiveOrientation := by
  rw [Basis.orientation]
  -- ‚ä¢ rayOfNeZero R (det b) (_ : det b ‚â† 0) = positiveOrientation
  congr
  -- ‚ä¢ det b = ‚ÜëAlternatingMap.constLinearEquivOfIsEmpty 1
  exact b.det_isEmpty
  -- üéâ no goals
#align basis.orientation_is_empty Basis.orientation_isEmpty

end Basis

end OrderedCommRing

section LinearOrderedCommRing

variable {R : Type*} [LinearOrderedCommRing R]

variable {M : Type*} [AddCommGroup M] [Module R M]

variable {Œπ : Type*}

namespace Orientation

/-- A module `M` over a linearly ordered commutative ring has precisely two "orientations" with
respect to an empty index type. (Note that these are only orientations of `M` of in the conventional
mathematical sense if `M` is zero-dimensional.) -/
theorem eq_or_eq_neg_of_isEmpty [Nontrivial R] [IsEmpty Œπ] (o : Orientation R M Œπ) :
    o = positiveOrientation ‚à® o = -positiveOrientation := by
  induction' o using Module.Ray.ind with x hx
  -- ‚ä¢ rayOfNeZero R x hx = positiveOrientation ‚à® rayOfNeZero R x hx = -positiveOri ‚Ä¶
  dsimp [positiveOrientation]
  -- ‚ä¢ rayOfNeZero R x hx = rayOfNeZero R (AlternatingMap.constOfIsEmpty R M Œπ 1) ( ‚Ä¶
  simp only [ray_eq_iff, sameRay_neg_swap]
  -- ‚ä¢ SameRay R x (AlternatingMap.constOfIsEmpty R M Œπ 1) ‚à® SameRay R x (-Alternat ‚Ä¶
  rw [sameRay_or_sameRay_neg_iff_not_linearIndependent]
  -- ‚ä¢ ¬¨LinearIndependent R ![x, AlternatingMap.constOfIsEmpty R M Œπ 1]
  intro h
  -- ‚ä¢ False
  set f : AlternatingMap R M R Œπ ‚âÉ‚Çó[R] R := AlternatingMap.constLinearEquivOfIsEmpty.symm
  -- ‚ä¢ False
  have H : LinearIndependent R ![f x, 1] := by
    convert h.map' f.toLinearMap f.ker
    ext i
    fin_cases i <;> simp
  rw [linearIndependent_iff'] at H
  -- ‚ä¢ False
  simpa using H Finset.univ ![1, -f x] (by simp [Fin.sum_univ_succ]) 0 (by simp)
  -- üéâ no goals
#align orientation.eq_or_eq_neg_of_is_empty Orientation.eq_or_eq_neg_of_isEmpty

end Orientation

namespace Basis

variable [Fintype Œπ] [DecidableEq Œπ]

/-- The orientations given by two bases are equal if and only if the determinant of one basis
with respect to the other is positive. -/
theorem orientation_eq_iff_det_pos (e‚ÇÅ e‚ÇÇ : Basis Œπ R M) :
    e‚ÇÅ.orientation = e‚ÇÇ.orientation ‚Üî 0 < e‚ÇÅ.det e‚ÇÇ :=
  calc
    e‚ÇÅ.orientation = e‚ÇÇ.orientation ‚Üî SameRay R e‚ÇÅ.det e‚ÇÇ.det := ray_eq_iff _ _
    _ ‚Üî SameRay R (e‚ÇÅ.det e‚ÇÇ ‚Ä¢ e‚ÇÇ.det) e‚ÇÇ.det := by rw [‚Üê e‚ÇÅ.det.eq_smul_basis_det e‚ÇÇ]
                                                    -- üéâ no goals
    _ ‚Üî 0 < e‚ÇÅ.det e‚ÇÇ := sameRay_smul_left_iff_of_ne e‚ÇÇ.det_ne_zero (e‚ÇÅ.isUnit_det e‚ÇÇ).ne_zero

#align basis.orientation_eq_iff_det_pos Basis.orientation_eq_iff_det_pos

/-- Given a basis, any orientation equals the orientation given by that basis or its negation. -/
theorem orientation_eq_or_eq_neg (e : Basis Œπ R M) (x : Orientation R M Œπ) :
    x = e.orientation ‚à® x = -e.orientation := by
  induction' x using Module.Ray.ind with x hx
  -- ‚ä¢ rayOfNeZero R x hx = Basis.orientation e ‚à® rayOfNeZero R x hx = -Basis.orien ‚Ä¶
  rw [‚Üê x.map_basis_ne_zero_iff e] at hx
  -- ‚ä¢ rayOfNeZero R x hx‚úù = Basis.orientation e ‚à® rayOfNeZero R x hx‚úù = -Basis.ori ‚Ä¶
  rwa [Basis.orientation, ray_eq_iff, neg_rayOfNeZero, ray_eq_iff, x.eq_smul_basis_det e,
    sameRay_neg_smul_left_iff_of_ne e.det_ne_zero hx, sameRay_smul_left_iff_of_ne e.det_ne_zero hx,
    lt_or_lt_iff_ne, ne_comm]
#align basis.orientation_eq_or_eq_neg Basis.orientation_eq_or_eq_neg

/-- Given a basis, an orientation equals the negation of that given by that basis if and only
if it does not equal that given by that basis. -/
theorem orientation_ne_iff_eq_neg (e : Basis Œπ R M) (x : Orientation R M Œπ) :
    x ‚â† e.orientation ‚Üî x = -e.orientation :=
  ‚ü®fun h => (e.orientation_eq_or_eq_neg x).resolve_left h, fun h =>
    h.symm ‚ñ∏ (Module.Ray.ne_neg_self e.orientation).symm‚ü©
#align basis.orientation_ne_iff_eq_neg Basis.orientation_ne_iff_eq_neg

/-- Composing a basis with a linear equiv gives the same orientation if and only if the
determinant is positive. -/
theorem orientation_comp_linearEquiv_eq_iff_det_pos (e : Basis Œπ R M) (f : M ‚âÉ‚Çó[R] M) :
    (e.map f).orientation = e.orientation ‚Üî 0 < LinearMap.det (f : M ‚Üí‚Çó[R] M) := by
  rw [orientation_map, e.map_orientation_eq_det_inv_smul, units_inv_smul, units_smul_eq_self_iff,
    LinearEquiv.coe_det]
#align basis.orientation_comp_linear_equiv_eq_iff_det_pos Basis.orientation_comp_linearEquiv_eq_iff_det_pos

/-- Composing a basis with a linear equiv gives the negation of that orientation if and only if
the determinant is negative. -/
theorem orientation_comp_linearEquiv_eq_neg_iff_det_neg (e : Basis Œπ R M) (f : M ‚âÉ‚Çó[R] M) :
    (e.map f).orientation = -e.orientation ‚Üî LinearMap.det (f : M ‚Üí‚Çó[R] M) < 0 := by
  rw [orientation_map, e.map_orientation_eq_det_inv_smul, units_inv_smul, units_smul_eq_neg_iff,
    LinearEquiv.coe_det]
#align basis.orientation_comp_linear_equiv_eq_neg_iff_det_neg Basis.orientation_comp_linearEquiv_eq_neg_iff_det_neg

/-- Negating a single basis vector (represented using `units_smul`) negates the corresponding
orientation. -/
@[simp]
theorem orientation_neg_single [Nontrivial R] (e : Basis Œπ R M) (i : Œπ) :
    (e.unitsSMul (Function.update 1 i (-1))).orientation = -e.orientation := by
  rw [orientation_unitsSMul, Finset.prod_update_of_mem (Finset.mem_univ _)]
  -- ‚ä¢ (-1 * ‚àè x in Finset.univ \ {i}, OfNat.ofNat 1 x)‚Åª¬π ‚Ä¢ Basis.orientation e = - ‚Ä¶
  simp
  -- üéâ no goals
#align basis.orientation_neg_single Basis.orientation_neg_single

/-- Given a basis and an orientation, return a basis giving that orientation: either the original
basis, or one constructed by negating a single (arbitrary) basis vector. -/
def adjustToOrientation [Nontrivial R] [Nonempty Œπ] (e : Basis Œπ R M) (x : Orientation R M Œπ) :
    Basis Œπ R M :=
  haveI := Classical.decEq (Orientation R M Œπ)
  if e.orientation = x then e else e.unitsSMul (Function.update 1 (Classical.arbitrary Œπ) (-1))
#align basis.adjust_to_orientation Basis.adjustToOrientation

/-- `adjust_to_orientation` gives a basis with the required orientation. -/
@[simp]
theorem orientation_adjustToOrientation [Nontrivial R] [Nonempty Œπ] (e : Basis Œπ R M)
    (x : Orientation R M Œπ) : (e.adjustToOrientation x).orientation = x := by
  rw [adjustToOrientation]
  -- ‚ä¢ Basis.orientation (if Basis.orientation e = x then e else unitsSMul e (Funct ‚Ä¶
  split_ifs with h
  -- ‚ä¢ Basis.orientation e = x
  ¬∑ exact h
    -- üéâ no goals
  ¬∑ rw [orientation_neg_single, eq_comm, ‚Üê orientation_ne_iff_eq_neg, ne_comm]
    -- ‚ä¢ Basis.orientation e ‚â† x
    exact h
    -- üéâ no goals
#align basis.orientation_adjust_to_orientation Basis.orientation_adjustToOrientation

/-- Every basis vector from `adjust_to_orientation` is either that from the original basis or its
negation. -/
theorem adjustToOrientation_apply_eq_or_eq_neg [Nontrivial R] [Nonempty Œπ] (e : Basis Œπ R M)
    (x : Orientation R M Œπ) (i : Œπ) :
    e.adjustToOrientation x i = e i ‚à® e.adjustToOrientation x i = -e i := by
  rw [adjustToOrientation]
  -- ‚ä¢ ‚Üë(if Basis.orientation e = x then e else unitsSMul e (Function.update 1 (Cla ‚Ä¶
  split_ifs with h
  -- ‚ä¢ ‚Üëe i = ‚Üëe i ‚à® ‚Üëe i = -‚Üëe i
  ¬∑ simp
    -- üéâ no goals
  ¬∑ by_cases hi : i = Classical.arbitrary Œπ <;> simp [unitsSMul_apply, hi]
    -- ‚ä¢ ‚Üë(unitsSMul e (Function.update 1 (Classical.arbitrary Œπ) (-1))) i = ‚Üëe i ‚à® ‚Üë ‚Ä¶
                                                -- üéâ no goals
                                                -- üéâ no goals
#align basis.adjust_to_orientation_apply_eq_or_eq_neg Basis.adjustToOrientation_apply_eq_or_eq_neg

theorem det_adjustToOrientation [Nontrivial R] [Nonempty Œπ] (e : Basis Œπ R M)
    (x : Orientation R M Œπ) :
    (e.adjustToOrientation x).det = e.det ‚à® (e.adjustToOrientation x).det = -e.det := by
  dsimp [Basis.adjustToOrientation]
  -- ‚ä¢ det (if Basis.orientation e = x then e else unitsSMul e (Function.update 1 ( ‚Ä¶
  split_ifs
  -- ‚ä¢ det e = det e ‚à® det e = -det e
  ¬∑ left
    -- ‚ä¢ det e = det e
    rfl
    -- üéâ no goals
  ¬∑ right
    -- ‚ä¢ det (unitsSMul e (Function.update 1 (Classical.arbitrary Œπ) (-1))) = -det e
    simp [e.det_unitsSMul, ‚Üê Units.coe_prod, Finset.prod_update_of_mem]
    -- ‚ä¢ -1 ‚Ä¢ det e = -det e
    ext
    -- ‚ä¢ ‚Üë(-1 ‚Ä¢ det e) x‚úù = ‚Üë(-det e) x‚úù
    simp
    -- üéâ no goals
#align basis.det_adjust_to_orientation Basis.det_adjustToOrientation

@[simp]
theorem abs_det_adjustToOrientation [Nontrivial R] [Nonempty Œπ] (e : Basis Œπ R M)
    (x : Orientation R M Œπ) (v : Œπ ‚Üí M) : |(e.adjustToOrientation x).det v| = |e.det v| := by
  cases' e.det_adjustToOrientation x with h h <;> simp [h]
  -- ‚ä¢ |‚Üë(det (adjustToOrientation e x)) v| = |‚Üë(det e) v|
                                                  -- üéâ no goals
                                                  -- üéâ no goals
#align basis.abs_det_adjust_to_orientation Basis.abs_det_adjustToOrientation

end Basis

end LinearOrderedCommRing

section LinearOrderedField

variable {R : Type*} [LinearOrderedField R]

variable {M : Type*} [AddCommGroup M] [Module R M]

variable {Œπ : Type*}

namespace Orientation

variable [Fintype Œπ] [_i : FiniteDimensional R M]

open FiniteDimensional

/-- If the index type has cardinality equal to the finite dimension, any two orientations are
equal or negations. -/
theorem eq_or_eq_neg (x‚ÇÅ x‚ÇÇ : Orientation R M Œπ) (h : Fintype.card Œπ = finrank R M) :
    x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ := by
  have e := (finBasis R M).reindex (Fintype.equivFinOfCardEq h).symm
  -- ‚ä¢ x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ
  letI := Classical.decEq Œπ
  -- ‚ä¢ x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ
  -- Porting note: this needs to be made explicit for the simp below
  have orientation_neg_neg :
    ‚àÄ f : Basis Œπ R M, - -Basis.orientation f = Basis.orientation f := by simp
  rcases e.orientation_eq_or_eq_neg x‚ÇÅ with (h‚ÇÅ | h‚ÇÅ) <;>
  -- ‚ä¢ x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ
    rcases e.orientation_eq_or_eq_neg x‚ÇÇ with (h‚ÇÇ | h‚ÇÇ) <;> simp [h‚ÇÅ, h‚ÇÇ, orientation_neg_neg]
    -- ‚ä¢ x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ
    -- ‚ä¢ x‚ÇÅ = x‚ÇÇ ‚à® x‚ÇÅ = -x‚ÇÇ
                                                            -- üéâ no goals
                                                            -- üéâ no goals
                                                            -- üéâ no goals
                                                            -- üéâ no goals
#align orientation.eq_or_eq_neg Orientation.eq_or_eq_neg

/-- If the index type has cardinality equal to the finite dimension, an orientation equals the
negation of another orientation if and only if they are not equal. -/
theorem ne_iff_eq_neg (x‚ÇÅ x‚ÇÇ : Orientation R M Œπ) (h : Fintype.card Œπ = finrank R M) :
    x‚ÇÅ ‚â† x‚ÇÇ ‚Üî x‚ÇÅ = -x‚ÇÇ :=
  ‚ü®fun hn => (eq_or_eq_neg x‚ÇÅ x‚ÇÇ h).resolve_left hn, fun he =>
    he.symm ‚ñ∏ (Module.Ray.ne_neg_self x‚ÇÇ).symm‚ü©
#align orientation.ne_iff_eq_neg Orientation.ne_iff_eq_neg

/-- The value of `Orientation.map` when the index type has cardinality equal to the finite
dimension, in terms of `f.det`. -/
theorem map_eq_det_inv_smul (x : Orientation R M Œπ) (f : M ‚âÉ‚Çó[R] M)
  (h : Fintype.card Œπ = finrank R M) : Orientation.map Œπ f x = (LinearEquiv.det f)‚Åª¬π ‚Ä¢ x :=
  haveI e := (finBasis R M).reindex (Fintype.equivFinOfCardEq h).symm
  e.map_orientation_eq_det_inv_smul x f
#align orientation.map_eq_det_inv_smul Orientation.map_eq_det_inv_smul

/-- If the index type has cardinality equal to the finite dimension, composing an alternating
map with the same linear equiv on each argument gives the same orientation if and only if the
determinant is positive. -/
theorem map_eq_iff_det_pos (x : Orientation R M Œπ) (f : M ‚âÉ‚Çó[R] M)
    (h : Fintype.card Œπ = finrank R M) :
    Orientation.map Œπ f x = x ‚Üî 0 < LinearMap.det (f : M ‚Üí‚Çó[R] M) := by
  cases isEmpty_or_nonempty Œπ
  -- ‚ä¢ ‚Üë(map Œπ f) x = x ‚Üî 0 < ‚ÜëLinearMap.det ‚Üëf
  ¬∑ have H : finrank R M = 0 := by
      refine' h.symm.trans _
      convert @Fintype.card_of_isEmpty Œπ _
    simp [LinearMap.det_eq_one_of_finrank_eq_zero H]
    -- üéâ no goals
  rw [map_eq_det_inv_smul _ _ h, units_inv_smul, units_smul_eq_self_iff, LinearEquiv.coe_det]
  -- üéâ no goals
#align orientation.map_eq_iff_det_pos Orientation.map_eq_iff_det_pos

/-- If the index type has cardinality equal to the finite dimension, composing an alternating
map with the same linear equiv on each argument gives the negation of that orientation if and
only if the determinant is negative. -/
theorem map_eq_neg_iff_det_neg (x : Orientation R M Œπ) (f : M ‚âÉ‚Çó[R] M)
    (h : Fintype.card Œπ = finrank R M) :
    Orientation.map Œπ f x = -x ‚Üî LinearMap.det (f : M ‚Üí‚Çó[R] M) < 0 := by
  cases isEmpty_or_nonempty Œπ
  -- ‚ä¢ ‚Üë(map Œπ f) x = -x ‚Üî ‚ÜëLinearMap.det ‚Üëf < 0
  ¬∑ have H : finrank R M = 0 := by
      refine' h.symm.trans _
      convert @Fintype.card_of_isEmpty Œπ _
    simp [LinearMap.det_eq_one_of_finrank_eq_zero H, Module.Ray.ne_neg_self x]
    -- üéâ no goals
  have H : 0 < finrank R M := by
    rw [‚Üê h]
    exact Fintype.card_pos
  haveI : FiniteDimensional R M := finiteDimensional_of_finrank H
  -- ‚ä¢ ‚Üë(map Œπ f) x = -x ‚Üî ‚ÜëLinearMap.det ‚Üëf < 0
  rw [map_eq_det_inv_smul _ _ h, units_inv_smul, units_smul_eq_neg_iff, LinearEquiv.coe_det]
  -- üéâ no goals
#align orientation.map_eq_neg_iff_det_neg Orientation.map_eq_neg_iff_det_neg

/-- If the index type has cardinality equal to the finite dimension, a basis with the given
orientation. -/
def someBasis [Nonempty Œπ] [DecidableEq Œπ] (x : Orientation R M Œπ)
    (h : Fintype.card Œπ = finrank R M) : Basis Œπ R M :=
  ((finBasis R M).reindex (Fintype.equivFinOfCardEq h).symm).adjustToOrientation x
#align orientation.some_basis Orientation.someBasis

/-- `some_basis` gives a basis with the required orientation. -/
@[simp]
theorem someBasis_orientation [Nonempty Œπ] [DecidableEq Œπ] (x : Orientation R M Œπ)
    (h : Fintype.card Œπ = finrank R M) : (x.someBasis h).orientation = x :=
  Basis.orientation_adjustToOrientation _ _
#align orientation.some_basis_orientation Orientation.someBasis_orientation

end Orientation

end LinearOrderedField
