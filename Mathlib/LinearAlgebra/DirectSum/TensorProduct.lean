/-
Copyright (c) 2018 Kenny Lau. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kenny Lau, Mario Carneiro, Eric Wieser
-/
import Mathlib.LinearAlgebra.TensorProduct
import Mathlib.Algebra.DirectSum.Module

#align_import linear_algebra.direct_sum.tensor_product from "leanprover-community/mathlib"@"9b9d125b7be0930f564a68f1d73ace10cf46064d"
/-!
# Tensor products of direct sums

This file shows that taking `TensorProduct`s commutes with taking `DirectSum`s in both arguments.

## Main results

* `TensorProduct.directSum`
* `TensorProduct.directSumLeft`
* `TensorProduct.directSumRight`
-/

universe u v‚ÇÅ v‚ÇÇ w‚ÇÅ w‚ÇÅ' w‚ÇÇ w‚ÇÇ'

section Ring

namespace TensorProduct

open TensorProduct

open DirectSum

open LinearMap

attribute [local ext] TensorProduct.ext

variable (R : Type u) [CommRing R]

variable {Œπ‚ÇÅ : Type v‚ÇÅ} {Œπ‚ÇÇ : Type v‚ÇÇ}

variable [DecidableEq Œπ‚ÇÅ] [DecidableEq Œπ‚ÇÇ]

variable (M‚ÇÅ : Œπ‚ÇÅ ‚Üí Type w‚ÇÅ) (M‚ÇÅ' : Type w‚ÇÅ') (M‚ÇÇ : Œπ‚ÇÇ ‚Üí Type w‚ÇÇ) (M‚ÇÇ' : Type w‚ÇÇ')

variable [‚àÄ i‚ÇÅ, AddCommGroup (M‚ÇÅ i‚ÇÅ)] [AddCommGroup M‚ÇÅ']

variable [‚àÄ i‚ÇÇ, AddCommGroup (M‚ÇÇ i‚ÇÇ)] [AddCommGroup M‚ÇÇ']

variable [‚àÄ i‚ÇÅ, Module R (M‚ÇÅ i‚ÇÅ)] [Module R M‚ÇÅ'] [‚àÄ i‚ÇÇ, Module R (M‚ÇÇ i‚ÇÇ)] [Module R M‚ÇÇ']


/-- The linear equivalence `(‚®Å i‚ÇÅ, M‚ÇÅ i‚ÇÅ) ‚äó (‚®Å i‚ÇÇ, M‚ÇÇ i‚ÇÇ) ‚âÉ (‚®Å i‚ÇÅ, ‚®Å i‚ÇÇ, M‚ÇÅ i‚ÇÅ ‚äó M‚ÇÇ i‚ÇÇ)`, i.e.
"tensor product distributes over direct sum". -/
protected def directSum :
    ((‚®Å i‚ÇÅ, M‚ÇÅ i‚ÇÅ) ‚äó[R] ‚®Å i‚ÇÇ, M‚ÇÇ i‚ÇÇ) ‚âÉ‚Çó[R] ‚®Å i : Œπ‚ÇÅ √ó Œπ‚ÇÇ, M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2 := by
  -- porting note: entirely rewritten to allow unification to happen one step at a time
  refine LinearEquiv.ofLinear (R := R) (R‚ÇÇ := R) ?toFun ?invFun ?left ?right
  ¬∑ refine lift ?_
    -- ‚ä¢ (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) ‚Üí‚Çó[R] (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ) ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), M‚ÇÅ i. ‚Ä¶
    refine DirectSum.toModule R _ _ fun i‚ÇÅ => ?_
    -- ‚ä¢ M‚ÇÅ i‚ÇÅ ‚Üí‚Çó[R] (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ) ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), M‚ÇÅ i.fst ‚äó[R] M‚ÇÇ i.snd
    refine @LinearMap.flip R _ R _ R _ R _ _ _ _ _ _ _ _ _ _ _ _ _ _ ?_
    -- ‚ä¢ (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ) ‚Üí‚Çó[R] M‚ÇÅ i‚ÇÅ ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), M‚ÇÅ i.fst ‚äó[R] M‚ÇÇ i.snd
    refine DirectSum.toModule R _ _ fun i‚ÇÇ => LinearMap.flip <| ?_
    -- ‚ä¢ M‚ÇÅ i‚ÇÅ ‚Üí‚Çó[R] M‚ÇÇ i‚ÇÇ ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), M‚ÇÅ i.fst ‚äó[R] M‚ÇÇ i.snd
    refine curry ?_
    -- ‚ä¢ M‚ÇÅ i‚ÇÅ ‚äó[R] M‚ÇÇ i‚ÇÇ ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), M‚ÇÅ i.fst ‚äó[R] M‚ÇÇ i.snd
    exact DirectSum.lof R (Œπ‚ÇÅ √ó Œπ‚ÇÇ) (fun i => M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2) (i‚ÇÅ, i‚ÇÇ)
    -- üéâ no goals
  ¬∑ refine DirectSum.toModule R _ _ fun i => ?_
    -- ‚ä¢ M‚ÇÅ i.fst ‚äó[R] M‚ÇÇ i.snd ‚Üí‚Çó[R] (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) ‚äó[R] ‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ
    exact map (DirectSum.lof R _ M‚ÇÅ i.1) (DirectSum.lof R _ M‚ÇÇ i.2)
    -- üéâ no goals
  ¬∑ refine DirectSum.linearMap_ext R fun ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© => ?_
    -- ‚ä¢ comp (comp (lift (toModule R Œπ‚ÇÅ ((‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ) ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ √ó Œπ‚ÇÇ), ‚Ä¶
    refine TensorProduct.ext ?_
    -- ‚ä¢ compr‚ÇÇ (mk R (M‚ÇÅ (i‚ÇÅ, i‚ÇÇ).fst) (M‚ÇÇ (i‚ÇÅ, i‚ÇÇ).snd)) (comp (comp (lift (toModul ‚Ä¶
    refine LinearMap.ext‚ÇÇ fun m‚ÇÅ m‚ÇÇ => ?_
    -- ‚ä¢ ‚Üë(‚Üë(compr‚ÇÇ (mk R (M‚ÇÅ (i‚ÇÅ, i‚ÇÇ).fst) (M‚ÇÇ (i‚ÇÅ, i‚ÇÇ).snd)) (comp (comp (lift (toM ‚Ä¶
    -- porting note: seems much nicer than the `repeat` lean 3 proof.
    simp only [compr‚ÇÇ_apply, comp_apply, id_apply, mk_apply, DirectSum.toModule_lof, map_tmul,
        lift.tmul, flip_apply, curry_apply]
  ¬∑ -- `(_)` prevents typeclass search timing out on problems that can be solved immediately by
    -- unification
    refine TensorProduct.ext ?_
    -- ‚ä¢ compr‚ÇÇ (mk R (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ)) (comp (toModule R (Œπ ‚Ä¶
    refine @DirectSum.linearMap_ext R _ _ _ _ _ _ _ _ (_) _ _ fun i‚ÇÅ => ?_
    -- ‚ä¢ comp (compr‚ÇÇ (mk R (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ)) (comp (toModul ‚Ä¶
    refine @LinearMap.ext _ _ _ _ _ _ _ _ (_) (_) _ _ _ fun x‚ÇÅ => ?_
    -- ‚ä¢ ‚Üë(comp (compr‚ÇÇ (mk R (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ)) (comp (toMod ‚Ä¶
    refine @DirectSum.linearMap_ext R _ _ _ _ _ _ _ _ (_) _ _ fun i‚ÇÇ => ?_
    -- ‚ä¢ comp (‚Üë(comp (compr‚ÇÇ (mk R (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ)) (comp  ‚Ä¶
    refine LinearMap.ext fun x‚ÇÇ => ?_
    -- ‚ä¢ ‚Üë(comp (‚Üë(comp (compr‚ÇÇ (mk R (‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) (‚®Å (i‚ÇÇ : Œπ‚ÇÇ), M‚ÇÇ i‚ÇÇ)) (com ‚Ä¶
    -- porting note: seems much nicer than the `repeat` lean 3 proof.
    simp only [compr‚ÇÇ_apply, comp_apply, id_apply, mk_apply, DirectSum.toModule_lof, map_tmul,
        lift.tmul, flip_apply, curry_apply]
  /- was:

    refine'
      LinearEquiv.ofLinear
        (lift <|
          DirectSum.toModule R _ _ fun i‚ÇÅ => LinearMap.flip <| DirectSum.toModule R _ _ fun i‚ÇÇ =>
                LinearMap.flip <| curry <|
                  DirectSum.lof R (Œπ‚ÇÅ √ó Œπ‚ÇÇ) (fun i => M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2) (i‚ÇÅ, i‚ÇÇ))
        (DirectSum.toModule R _ _ fun i => map (DirectSum.lof R _ _ _) (DirectSum.lof R _ _ _)) _
        _ <;>
    [ext ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© x‚ÇÅ x‚ÇÇ : 4, ext i‚ÇÅ i‚ÇÇ x‚ÇÅ x‚ÇÇ : 5]
  repeat'
    first
      |rw [compr‚ÇÇ_apply]|rw [comp_apply]|rw [id_apply]|rw [mk_apply]|rw [DirectSum.toModule_lof]
      |rw [map_tmul]|rw [lift.tmul]|rw [flip_apply]|rw [curry_apply]
  -/

/- alternative with explicit types:
  refine'
      LinearEquiv.ofLinear
        (lift <|
          DirectSum.toModule
            (R := R) (M := M‚ÇÅ) (N := (‚®Å i‚ÇÇ, M‚ÇÇ i‚ÇÇ) ‚Üí‚Çó[R] ‚®Å i : Œπ‚ÇÅ √ó Œπ‚ÇÇ, M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2)
            (œÜ := fun i‚ÇÅ => LinearMap.flip <|
              DirectSum.toModule (R := R) (M := M‚ÇÇ) (N := ‚®Å i : Œπ‚ÇÅ √ó Œπ‚ÇÇ, M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2)
              (œÜ := fun i‚ÇÇ => LinearMap.flip <| curry <|
                  DirectSum.lof R (Œπ‚ÇÅ √ó Œπ‚ÇÇ) (fun i => M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2) (i‚ÇÅ, i‚ÇÇ))))
        (DirectSum.toModule
          (R := R)
          (M := fun i : Œπ‚ÇÅ √ó Œπ‚ÇÇ => M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2)
          (N := (‚®Å i‚ÇÅ, M‚ÇÅ i‚ÇÅ) ‚äó[R] ‚®Å i‚ÇÇ, M‚ÇÇ i‚ÇÇ)
          (œÜ := fun i : Œπ‚ÇÅ √ó Œπ‚ÇÇ => map (DirectSum.lof R _ M‚ÇÅ i.1) (DirectSum.lof R _ M‚ÇÇ i.2))) _
        _ <;>
    [ext ‚ü®i‚ÇÅ, i‚ÇÇ‚ü© x‚ÇÅ x‚ÇÇ : 4, ext i‚ÇÅ i‚ÇÇ x‚ÇÅ x‚ÇÇ : 5]
  repeat'
    first
      |rw [compr‚ÇÇ_apply]|rw [comp_apply]|rw [id_apply]|rw [mk_apply]|rw [DirectSum.toModule_lof]
      |rw [map_tmul]|rw [lift.tmul]|rw [flip_apply]|rw [curry_apply]
-/
#align tensor_product.direct_sum TensorProduct.directSum

/-- Tensor products distribute over a direct sum on the left . -/
def directSumLeft : (‚®Å i‚ÇÅ, M‚ÇÅ i‚ÇÅ) ‚äó[R] M‚ÇÇ' ‚âÉ‚Çó[R] ‚®Å i, M‚ÇÅ i ‚äó[R] M‚ÇÇ' :=
  LinearEquiv.ofLinear
    (lift <|
      DirectSum.toModule R _ _ fun i =>
        (mk R _ _).compr‚ÇÇ <| DirectSum.lof R Œπ‚ÇÅ (fun i => M‚ÇÅ i ‚äó[R] M‚ÇÇ') _)
    (DirectSum.toModule R _ _ fun i => rTensor _ (DirectSum.lof R Œπ‚ÇÅ _ _))
    (DirectSum.linearMap_ext R fun i =>
      TensorProduct.ext <|
        LinearMap.ext‚ÇÇ fun m‚ÇÅ m‚ÇÇ => by
          dsimp only [comp_apply, compr‚ÇÇ_apply, id_apply, mk_apply]
          -- ‚ä¢ ‚Üë(lift (toModule R Œπ‚ÇÅ (M‚ÇÇ' ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ), M‚ÇÅ i ‚äó[R] M‚ÇÇ') fun i => compr‚ÇÇ  ‚Ä¶
          simp_rw [DirectSum.toModule_lof, rTensor_tmul, lift.tmul, DirectSum.toModule_lof,
            compr‚ÇÇ_apply, mk_apply])
    (TensorProduct.ext <|
      DirectSum.linearMap_ext R fun i =>
        LinearMap.ext‚ÇÇ fun m‚ÇÅ m‚ÇÇ => by
          dsimp only [comp_apply, compr‚ÇÇ_apply, id_apply, mk_apply]
          -- ‚ä¢ ‚Üë(toModule R Œπ‚ÇÅ ((‚®Å (i‚ÇÅ : Œπ‚ÇÅ), M‚ÇÅ i‚ÇÅ) ‚äó[R] M‚ÇÇ') fun i => rTensor M‚ÇÇ' (lof R  ‚Ä¶
          simp_rw [lift.tmul, DirectSum.toModule_lof, compr‚ÇÇ_apply,
            mk_apply, DirectSum.toModule_lof, rTensor_tmul])
#align tensor_product.direct_sum_left TensorProduct.directSumLeft

/-- Tensor products distribute over a direct sum on the right. -/
def directSumRight : (M‚ÇÅ' ‚äó[R] ‚®Å i, M‚ÇÇ i) ‚âÉ‚Çó[R] ‚®Å i, M‚ÇÅ' ‚äó[R] M‚ÇÇ i :=
  TensorProduct.comm R _ _ ‚â™‚â´‚Çó directSumLeft R M‚ÇÇ M‚ÇÅ' ‚â™‚â´‚Çó
    DFinsupp.mapRange.linearEquiv fun _ => TensorProduct.comm R _ _
#align tensor_product.direct_sum_right TensorProduct.directSumRight

variable {M‚ÇÅ M‚ÇÅ' M‚ÇÇ M‚ÇÇ'}

@[simp]
theorem directSum_lof_tmul_lof (i‚ÇÅ : Œπ‚ÇÅ) (m‚ÇÅ : M‚ÇÅ i‚ÇÅ) (i‚ÇÇ : Œπ‚ÇÇ) (m‚ÇÇ : M‚ÇÇ i‚ÇÇ) :
    TensorProduct.directSum R M‚ÇÅ M‚ÇÇ (DirectSum.lof R Œπ‚ÇÅ M‚ÇÅ i‚ÇÅ m‚ÇÅ ‚äó‚Çú DirectSum.lof R Œπ‚ÇÇ M‚ÇÇ i‚ÇÇ m‚ÇÇ) =
      DirectSum.lof R (Œπ‚ÇÅ √ó Œπ‚ÇÇ) (fun i => M‚ÇÅ i.1 ‚äó[R] M‚ÇÇ i.2) (i‚ÇÅ, i‚ÇÇ) (m‚ÇÅ ‚äó‚Çú m‚ÇÇ) := by
  simp [TensorProduct.directSum]
  -- üéâ no goals
#align tensor_product.direct_sum_lof_tmul_lof TensorProduct.directSum_lof_tmul_lof

@[simp]
theorem directSumLeft_tmul_lof (i : Œπ‚ÇÅ) (x : M‚ÇÅ i) (y : M‚ÇÇ') :
    directSumLeft R M‚ÇÅ M‚ÇÇ' (DirectSum.lof R _ _ i x ‚äó‚Çú[R] y) =
    DirectSum.lof R _ _ i (x ‚äó‚Çú[R] y) := by
  dsimp only [directSumLeft, LinearEquiv.ofLinear_apply, lift.tmul]
  -- ‚ä¢ ‚Üë(‚Üë(toModule R Œπ‚ÇÅ (M‚ÇÇ' ‚Üí‚Çó[R] ‚®Å (i : Œπ‚ÇÅ), M‚ÇÅ i ‚äó[R] M‚ÇÇ') fun i => compr‚ÇÇ (mk  ‚Ä¶
  rw [DirectSum.toModule_lof R i]
  -- ‚ä¢ ‚Üë(‚Üë(compr‚ÇÇ (mk R (M‚ÇÅ i) M‚ÇÇ') (lof R Œπ‚ÇÅ (fun i => M‚ÇÅ i ‚äó[R] M‚ÇÇ') i)) x) y = ‚Üë ‚Ä¶
  rfl
  -- üéâ no goals
#align tensor_product.direct_sum_left_tmul_lof TensorProduct.directSumLeft_tmul_lof

@[simp]
theorem directSumLeft_symm_lof_tmul (i : Œπ‚ÇÅ) (x : M‚ÇÅ i) (y : M‚ÇÇ') :
    (directSumLeft R M‚ÇÅ M‚ÇÇ').symm (DirectSum.lof R _ _ i (x ‚äó‚Çú[R] y)) =
      DirectSum.lof R _ _ i x ‚äó‚Çú[R] y := by
  rw [LinearEquiv.symm_apply_eq, directSumLeft_tmul_lof]
  -- üéâ no goals
#align tensor_product.direct_sum_left_symm_lof_tmul TensorProduct.directSumLeft_symm_lof_tmul

@[simp]
theorem directSumRight_tmul_lof (x : M‚ÇÅ') (i : Œπ‚ÇÇ) (y : M‚ÇÇ i) :
    directSumRight R M‚ÇÅ' M‚ÇÇ (x ‚äó‚Çú[R] DirectSum.lof R _ _ i y) =
    DirectSum.lof R _ _ i (x ‚äó‚Çú[R] y) := by
  dsimp only [directSumRight, LinearEquiv.trans_apply, TensorProduct.comm_tmul]
  -- ‚ä¢ ‚Üë(DFinsupp.mapRange.linearEquiv fun x => TensorProduct.comm R (M‚ÇÇ x) M‚ÇÅ') (‚Üë ‚Ä¶
  rw [directSumLeft_tmul_lof]
  -- ‚ä¢ ‚Üë(DFinsupp.mapRange.linearEquiv fun x => TensorProduct.comm R (M‚ÇÇ x) M‚ÇÅ') (‚Üë ‚Ä¶
  exact DFinsupp.mapRange_single (hf := fun _ => rfl)
  -- üéâ no goals
#align tensor_product.direct_sum_right_tmul_lof TensorProduct.directSumRight_tmul_lof

@[simp]
theorem directSumRight_symm_lof_tmul (x : M‚ÇÅ') (i : Œπ‚ÇÇ) (y : M‚ÇÇ i) :
    (directSumRight R M‚ÇÅ' M‚ÇÇ).symm (DirectSum.lof R _ _ i (x ‚äó‚Çú[R] y)) =
      x ‚äó‚Çú[R] DirectSum.lof R _ _ i y := by
  rw [LinearEquiv.symm_apply_eq, directSumRight_tmul_lof]
  -- üéâ no goals
#align tensor_product.direct_sum_right_symm_lof_tmul TensorProduct.directSumRight_symm_lof_tmul

end TensorProduct

end Ring
