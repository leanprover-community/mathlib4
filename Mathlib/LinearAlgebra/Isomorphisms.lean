/-
Copyright (c) 2017 Johannes H√∂lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johannes H√∂lzl, Mario Carneiro, Kevin Buzzard, Yury Kudryashov
-/
import Mathlib.LinearAlgebra.Quotient

#align_import linear_algebra.isomorphisms from "leanprover-community/mathlib"@"2738d2ca56cbc63be80c3bd48e9ed90ad94e947d"

/-!
# Isomorphism theorems for modules.

* The Noether's first, second, and third isomorphism theorems for modules are proved as
  `LinearMap.quotKerEquivRange`, `LinearMap.quotientInfEquivSupQuotient` and
  `Submodule.quotientQuotientEquivQuotient`.

-/

universe u v

variable {R M M‚ÇÇ M‚ÇÉ : Type*}

variable [Ring R] [AddCommGroup M] [AddCommGroup M‚ÇÇ] [AddCommGroup M‚ÇÉ]

variable [Module R M] [Module R M‚ÇÇ] [Module R M‚ÇÉ]

variable (f : M ‚Üí‚Çó[R] M‚ÇÇ)

/-! The first and second isomorphism theorems for modules. -/

namespace LinearMap

open Submodule

section IsomorphismLaws

/-- The first isomorphism law for modules. The quotient of `M` by the kernel of `f` is linearly
equivalent to the range of `f`. -/
noncomputable def quotKerEquivRange : (M ‚ß∏ LinearMap.ker f) ‚âÉ‚Çó[R] LinearMap.range f :=
  (LinearEquiv.ofInjective (f.ker.liftQ f <| le_rfl) <|
        ker_eq_bot.mp <| Submodule.ker_liftQ_eq_bot _ _ _ (le_refl (LinearMap.ker f))).trans
    (LinearEquiv.ofEq _ _ <| Submodule.range_liftQ _ _ _)
#align linear_map.quot_ker_equiv_range LinearMap.quotKerEquivRange

/-- The first isomorphism theorem for surjective linear maps. -/
noncomputable def quotKerEquivOfSurjective (f : M ‚Üí‚Çó[R] M‚ÇÇ) (hf : Function.Surjective f) :
    (M ‚ß∏ LinearMap.ker f) ‚âÉ‚Çó[R] M‚ÇÇ :=
  f.quotKerEquivRange.trans (LinearEquiv.ofTop (LinearMap.range f) (LinearMap.range_eq_top.2 hf))
#align linear_map.quot_ker_equiv_of_surjective LinearMap.quotKerEquivOfSurjective

@[simp]
theorem quotKerEquivRange_apply_mk (x : M) :
    (f.quotKerEquivRange (Submodule.Quotient.mk x) : M‚ÇÇ) = f x :=
  rfl
#align linear_map.quot_ker_equiv_range_apply_mk LinearMap.quotKerEquivRange_apply_mk

@[simp]
theorem quotKerEquivRange_symm_apply_image (x : M) (h : f x ‚àà LinearMap.range f) :
    f.quotKerEquivRange.symm ‚ü®f x, h‚ü© = f.ker.mkQ x :=
  f.quotKerEquivRange.symm_apply_apply (f.ker.mkQ x)
#align linear_map.quot_ker_equiv_range_symm_apply_image LinearMap.quotKerEquivRange_symm_apply_image

-- Porting note: breaking up original definition of quotientInfToSupQuotient to avoid timing out
/-- Linear map from `p` to `p+p'/p'` where `p p'` are submodules of `R` -/
@[reducible]
def subToSupQuotient (p p' : Submodule R M) :
    { x // x ‚àà p } ‚Üí‚Çó[R] { x // x ‚àà p ‚äî p' } ‚ß∏ comap (Submodule.subtype (p ‚äî p')) p' :=
  (comap (p ‚äî p').subtype p').mkQ.comp (Submodule.ofLe le_sup_left)

-- Porting note: breaking up original definition of quotientInfToSupQuotient to avoid timing out
theorem comap_leq_ker_subToSupQuotient (p p' : Submodule R M) :
    comap (Submodule.subtype p) (p ‚äì p') ‚â§ ker (subToSupQuotient p p') := by
  rw [LinearMap.ker_comp, Submodule.ofLe, comap_codRestrict, ker_mkQ, map_comap_subtype]
  -- ‚ä¢ comap (Submodule.subtype p) (p ‚äì p') ‚â§ comap (Submodule.subtype p) ((p ‚äî p') ‚Ä¶
  exact comap_mono (inf_le_inf_right _ le_sup_left)
  -- üéâ no goals

/-- Canonical linear map from the quotient `p/(p ‚à© p')` to `(p+p')/p'`, mapping `x + (p ‚à© p')`
to `x + p'`, where `p` and `p'` are submodules of an ambient module.
-/
def quotientInfToSupQuotient (p p' : Submodule R M) :
    (‚Ü•p) ‚ß∏ (comap p.subtype (p ‚äì p')) ‚Üí‚Çó[R] (‚Ü•(p ‚äî p')) ‚ß∏ (comap (p ‚äî p').subtype p') :=
   (comap p.subtype (p ‚äì p')).liftQ (subToSupQuotient p p') (comap_leq_ker_subToSupQuotient p p')
#align linear_map.quotient_inf_to_sup_quotient LinearMap.quotientInfToSupQuotient

-- Porting note: breaking up original definition of quotientInfEquivSupQuotient to avoid timing out
theorem quotientInfEquivSupQuotient_injective (p p' : Submodule R M) :
    Function.Injective (quotientInfToSupQuotient p p') := by
  rw [‚Üê ker_eq_bot, quotientInfToSupQuotient, ker_liftQ_eq_bot]
  -- ‚ä¢ ker (subToSupQuotient p p') ‚â§ comap (Submodule.subtype p) (p ‚äì p')
  rw [ker_comp, ker_mkQ]
  -- ‚ä¢ comap (ofLe (_ : p ‚â§ p ‚äî p')) (comap (Submodule.subtype (p ‚äî p')) p') ‚â§ coma ‚Ä¶
  exact fun ‚ü®x, hx1‚ü© hx2 => ‚ü®hx1, hx2‚ü©
  -- üéâ no goals

-- Porting note: breaking up original definition of quotientInfEquivSupQuotient to avoid timing out
theorem quotientInfEquivSupQuotient_surjective (p p' : Submodule R M) :
    Function.Surjective (quotientInfToSupQuotient p p') := by
  rw [‚Üê range_eq_top, quotientInfToSupQuotient, range_liftQ, eq_top_iff']
  -- ‚ä¢ ‚àÄ (x : { x // x ‚àà p ‚äî p' } ‚ß∏ comap (Submodule.subtype (p ‚äî p')) p'), x ‚àà ran ‚Ä¶
  rintro ‚ü®x, hx‚ü©; rcases mem_sup.1 hx with ‚ü®y, hy, z, hz, rfl‚ü©
  -- ‚ä¢ Quot.mk Setoid.r { val := x, property := hx } ‚àà range (subToSupQuotient p p')
                  -- ‚ä¢ Quot.mk Setoid.r { val := y + z, property := hx } ‚àà range (subToSupQuotient  ‚Ä¶
  use ‚ü®y, hy‚ü©; apply (Submodule.Quotient.eq _).2
  -- ‚ä¢ ‚Üë(subToSupQuotient p p') { val := y, property := hy } = Quot.mk Setoid.r { v ‚Ä¶
               -- ‚ä¢ ‚Üë(ofLe (_ : p ‚â§ p ‚äî p')) { val := y, property := hy } - { val := y + z, prop ‚Ä¶
  simp only [mem_comap, map_sub, coeSubtype, coe_ofLe, sub_add_cancel', neg_mem_iff, hz]
  -- üéâ no goals

/--
Second Isomorphism Law : the canonical map from `p/(p ‚à© p')` to `(p+p')/p'` as a linear isomorphism.
-/
noncomputable def quotientInfEquivSupQuotient (p p' : Submodule R M) :
    (p ‚ß∏ comap p.subtype (p ‚äì p')) ‚âÉ‚Çó[R] _ ‚ß∏ comap (p ‚äî p').subtype p' :=
  LinearEquiv.ofBijective (quotientInfToSupQuotient p p')
    ‚ü®quotientInfEquivSupQuotient_injective p p', quotientInfEquivSupQuotient_surjective p p'‚ü©
#align linear_map.quotient_inf_equiv_sup_quotient LinearMap.quotientInfEquivSupQuotient

-- @[simp]
-- Porting note: `simp` affects the type arguments of `FunLike.coe`, so this theorem can't be
--               a simp theorem anymore, even if it has high priority.
theorem coe_quotientInfToSupQuotient (p p' : Submodule R M) :
    ‚áë(quotientInfToSupQuotient p p') = quotientInfEquivSupQuotient p p' :=
  rfl
#align linear_map.coe_quotient_inf_to_sup_quotient LinearMap.coe_quotientInfToSupQuotient

@[simp]
theorem quotientInfEquivSupQuotient_apply_mk (p p' : Submodule R M) (x : p) :
    let map := ofLe (le_sup_left : p ‚â§ p ‚äî p')
    quotientInfEquivSupQuotient p p' (Submodule.Quotient.mk x) =
      @Submodule.Quotient.mk R (p ‚äî p' : Submodule R M) _ _ _ (comap (p ‚äî p').subtype p') (map x) :=
  rfl
#align linear_map.quotient_inf_equiv_sup_quotient_apply_mk LinearMap.quotientInfEquivSupQuotient_apply_mk

theorem quotientInfEquivSupQuotient_symm_apply_left (p p' : Submodule R M) (x : ‚Ü•(p ‚äî p'))
    (hx : (x : M) ‚àà p) :
    (quotientInfEquivSupQuotient p p').symm (Submodule.Quotient.mk x) =
      Submodule.Quotient.mk ‚ü®x, hx‚ü© :=
  (LinearEquiv.symm_apply_eq _).2 <| by
    -- Porting note: Was `simp`.
    rw [quotientInfEquivSupQuotient_apply_mk, ofLe_apply]
    -- üéâ no goals
#align linear_map.quotient_inf_equiv_sup_quotient_symm_apply_left LinearMap.quotientInfEquivSupQuotient_symm_apply_left


-- @[simp] -- Porting note: simp can prove this
theorem quotientInfEquivSupQuotient_symm_apply_eq_zero_iff {p p' : Submodule R M} {x : ‚Ü•(p ‚äî p')} :
    (quotientInfEquivSupQuotient p p').symm (Submodule.Quotient.mk x) = 0 ‚Üî (x : M) ‚àà p' :=
  (LinearEquiv.symm_apply_eq _).trans <| by
    -- Porting note: Was `simp`.
    rw [_root_.map_zero, Quotient.mk_eq_zero, mem_comap, Submodule.coeSubtype]
    -- üéâ no goals
#align linear_map.quotient_inf_equiv_sup_quotient_symm_apply_eq_zero_iff LinearMap.quotientInfEquivSupQuotient_symm_apply_eq_zero_iff

theorem quotientInfEquivSupQuotient_symm_apply_right (p p' : Submodule R M) {x : ‚Ü•(p ‚äî p')}
    (hx : (x : M) ‚àà p') : (quotientInfEquivSupQuotient p p').symm (Submodule.Quotient.mk x)
    = 0 :=
  quotientInfEquivSupQuotient_symm_apply_eq_zero_iff.2 hx
#align linear_map.quotient_inf_equiv_sup_quotient_symm_apply_right LinearMap.quotientInfEquivSupQuotient_symm_apply_right

end IsomorphismLaws

end LinearMap

/-! The third isomorphism theorem for modules. -/

namespace Submodule

variable (S T : Submodule R M) (h : S ‚â§ T)

/-- The map from the third isomorphism theorem for modules: `(M / S) / (T / S) ‚Üí M / T`. -/
def quotientQuotientEquivQuotientAux (h : S ‚â§ T) : (M ‚ß∏ S) ‚ß∏ T.map S.mkQ ‚Üí‚Çó[R] M ‚ß∏ T :=
  liftQ _ (mapQ S T LinearMap.id h)
    (by
      rintro _ ‚ü®x, hx, rfl‚ü©
      -- ‚ä¢ ‚Üë(mkQ S) x ‚àà LinearMap.ker (mapQ S T LinearMap.id h)
      rw [LinearMap.mem_ker, mkQ_apply, mapQ_apply]
      -- ‚ä¢ Quotient.mk (‚ÜëLinearMap.id x) = 0
      exact (Quotient.mk_eq_zero _).mpr hx)
      -- üéâ no goals
#align submodule.quotient_quotient_equiv_quotient_aux Submodule.quotientQuotientEquivQuotientAux

@[simp]
theorem quotientQuotientEquivQuotientAux_mk (x : M ‚ß∏ S) :
    quotientQuotientEquivQuotientAux S T h (Quotient.mk x) = mapQ S T LinearMap.id h x :=
  liftQ_apply _ _ _
#align submodule.quotient_quotient_equiv_quotient_aux_mk Submodule.quotientQuotientEquivQuotientAux_mk

-- @[simp] -- Porting note: simp can prove this
theorem quotientQuotientEquivQuotientAux_mk_mk (x : M) :
    quotientQuotientEquivQuotientAux S T h (Quotient.mk (Quotient.mk x)) = Quotient.mk x := by simp
                                                                                               -- üéâ no goals
#align submodule.quotient_quotient_equiv_quotient_aux_mk_mk Submodule.quotientQuotientEquivQuotientAux_mk_mk

/-- **Noether's third isomorphism theorem** for modules: `(M / S) / (T / S) ‚âÉ M / T`. -/
def quotientQuotientEquivQuotient : ((M ‚ß∏ S) ‚ß∏ T.map S.mkQ) ‚âÉ‚Çó[R] M ‚ß∏ T :=
  { quotientQuotientEquivQuotientAux S T h with
    toFun := quotientQuotientEquivQuotientAux S T h
    invFun := mapQ _ _ (mkQ S) (le_comap_map _ _)
    left_inv := fun x => Quotient.inductionOn' x fun x => Quotient.inductionOn' x fun x => by simp
                                                                                              -- üéâ no goals
    right_inv := fun x => Quotient.inductionOn' x fun x => by simp }
                                                              -- üéâ no goals
#align submodule.quotient_quotient_equiv_quotient Submodule.quotientQuotientEquivQuotient

/-- Corollary of the third isomorphism theorem: `[S : T] [M : S] = [M : T]` -/
theorem card_quotient_mul_card_quotient (S T : Submodule R M) (hST : T ‚â§ S)
    [DecidablePred fun x => x ‚àà S.map T.mkQ] [Fintype (M ‚ß∏ S)] [Fintype (M ‚ß∏ T)] :
    Fintype.card (S.map T.mkQ) * Fintype.card (M ‚ß∏ S) = Fintype.card (M ‚ß∏ T) := by
  rw [Submodule.card_eq_card_quotient_mul_card (map T.mkQ S),
    Fintype.card_eq.mpr ‚ü®(quotientQuotientEquivQuotient T S hST).toEquiv‚ü©]
#align submodule.card_quotient_mul_card_quotient Submodule.card_quotient_mul_card_quotient

end Submodule
