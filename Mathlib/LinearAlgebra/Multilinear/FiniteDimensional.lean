/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.LinearAlgebra.Multilinear.Basic
import Mathlib.LinearAlgebra.FreeModule.Finite.Matrix

#align_import linear_algebra.multilinear.finite_dimensional from "leanprover-community/mathlib"@"ce11c3c2a285bbe6937e26d9792fda4e51f3fe1a"

/-! # Multilinear maps over finite dimensional spaces

The main results are that multilinear maps over finitely-generated, free modules are
finitely-generated and free.

* `Module.Finite.multilinearMap`
* `Module.Free.multilinearMap`

We do not put this in `LinearAlgebra.Multilinear.Basic` to avoid making the imports too large
there.
-/


namespace MultilinearMap

variable {Œπ R M‚ÇÇ : Type*} {M‚ÇÅ : Œπ ‚Üí Type*}

variable [Finite Œπ]

variable [CommRing R] [AddCommGroup M‚ÇÇ] [Module R M‚ÇÇ]

variable [Module.Finite R M‚ÇÇ] [Module.Free R M‚ÇÇ]

-- Porting note: split out from `free_and_finite` because of inscrutable typeclass errors
private theorem free_and_finite_fin (n : ‚Ñï) (N : Fin n ‚Üí Type*) [‚àÄ i, AddCommGroup (N i)]
    [‚àÄ i, Module R (N i)] [‚àÄ i, Module.Finite R (N i)] [‚àÄ i, Module.Free R (N i)] :
    Module.Free R (MultilinearMap R N M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R N M‚ÇÇ) := by
  induction' n with n ih
  -- ‚ä¢ Module.Free R (MultilinearMap R N M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R N  ‚Ä¶
  ¬∑ haveI : IsEmpty (Fin Nat.zero) := inferInstanceAs (IsEmpty (Fin 0))
    -- ‚ä¢ Module.Free R (MultilinearMap R N M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R N  ‚Ä¶
    exact
      ‚ü®Module.Free.of_equiv (constLinearEquivOfIsEmpty R N M‚ÇÇ),
        Module.Finite.equiv (constLinearEquivOfIsEmpty R N M‚ÇÇ)‚ü©
  ¬∑ suffices
      Module.Free R (N 0 ‚Üí‚Çó[R] MultilinearMap R (fun i : Fin n => N i.succ) M‚ÇÇ) ‚àß
        Module.Finite R (N 0 ‚Üí‚Çó[R] MultilinearMap R (fun i : Fin n => N i.succ) M‚ÇÇ) by
      cases this
      exact
        ‚ü®Module.Free.of_equiv (multilinearCurryLeftEquiv R N M‚ÇÇ),
          Module.Finite.equiv (multilinearCurryLeftEquiv R N M‚ÇÇ)‚ü©
    cases ih fun i => N i.succ
    -- ‚ä¢ Module.Free R (N 0 ‚Üí‚Çó[R] MultilinearMap R (fun i => N (Fin.succ i)) M‚ÇÇ) ‚àß Mo ‚Ä¶
    exact ‚ü®Module.Free.linearMap _ _ _, Module.Finite.linearMap _ _‚ü©
    -- üéâ no goals

variable [‚àÄ i, AddCommGroup (M‚ÇÅ i)] [‚àÄ i, Module R (M‚ÇÅ i)]

variable [‚àÄ i, Module.Finite R (M‚ÇÅ i)] [‚àÄ i, Module.Free R (M‚ÇÅ i)]

-- the induction requires us to show both at once
private theorem free_and_finite :
    Module.Free R (MultilinearMap R M‚ÇÅ M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R M‚ÇÅ M‚ÇÇ) := by
  cases nonempty_fintype Œπ
  -- ‚ä¢ Module.Free R (MultilinearMap R M‚ÇÅ M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R M ‚Ä¶
  have := @free_and_finite_fin R M‚ÇÇ _ _ _ _ _ (Fintype.card Œπ)
    (fun x => M‚ÇÅ ((Fintype.equivFin Œπ).symm x))
  cases' this with l r
  -- ‚ä¢ Module.Free R (MultilinearMap R M‚ÇÅ M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R M ‚Ä¶
  have e := domDomCongrLinearEquiv' R M‚ÇÅ M‚ÇÇ (Fintype.equivFin Œπ)
  -- ‚ä¢ Module.Free R (MultilinearMap R M‚ÇÅ M‚ÇÇ) ‚àß Module.Finite R (MultilinearMap R M ‚Ä¶
  exact ‚ü®Module.Free.of_equiv e.symm, Module.Finite.equiv e.symm‚ü©
  -- üéâ no goals

instance _root_.Module.Finite.multilinearMap : Module.Finite R (MultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  free_and_finite.2
#align module.finite.multilinear_map Module.Finite.multilinearMap

instance _root_.Module.Free.multilinearMap : Module.Free R (MultilinearMap R M‚ÇÅ M‚ÇÇ) :=
  free_and_finite.1
#align module.free.multilinear_map Module.Free.multilinearMap

end MultilinearMap
