/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.LocallyConvex.Basic

#align_import analysis.locally_convex.balanced_core_hull from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Balanced Core and Balanced Hull

## Main definitions

* `balancedCore`: The largest balanced subset of a set `s`.
* `balancedHull`: The smallest balanced superset of a set `s`.

## Main statements

* `balancedCore_eq_iInter`: Characterization of the balanced core as an intersection over subsets.
* `nhds_basis_closed_balanced`: The closed balanced sets form a basis of the neighborhood filter.

## Implementation details

The balanced core and hull are implemented differently: for the core we take the obvious definition
of the union over all balanced sets that are contained in `s`, whereas for the hull, we take the
union over `r â€¢ s`, for `r` the scalars with `â€–râ€– â‰¤ 1`. We show that `balancedHull` has the
defining properties of a hull in `Balanced.balancedHull_subset_of_subset` and `subset_balancedHull`.
For the core we need slightly stronger assumptions to obtain a characterization as an intersection,
this is `balancedCore_eq_iInter`.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

balanced
-/


open Set Pointwise Topology Filter

variable {ğ•œ E Î¹ : Type*}

section balancedHull

section SeminormedRing

variable [SeminormedRing ğ•œ]

section SMul

variable (ğ•œ) [SMul ğ•œ E] {s t : Set E} {x : E}

/-- The largest balanced subset of `s`. -/
def balancedCore (s : Set E) :=
  â‹ƒâ‚€ { t : Set E | Balanced ğ•œ t âˆ§ t âŠ† s }
#align balanced_core balancedCore

/-- Helper definition to prove `balanced_core_eq_iInter`-/
def balancedCoreAux (s : Set E) :=
  â‹‚ (r : ğ•œ) (_ : 1 â‰¤ â€–râ€–), r â€¢ s
#align balanced_core_aux balancedCoreAux

/-- The smallest balanced superset of `s`. -/
def balancedHull (s : Set E) :=
  â‹ƒ (r : ğ•œ) (_ : â€–râ€– â‰¤ 1), r â€¢ s
#align balanced_hull balancedHull

variable {ğ•œ}

theorem balancedCore_subset (s : Set E) : balancedCore ğ•œ s âŠ† s :=
  sUnion_subset fun _ ht => ht.2
#align balanced_core_subset balancedCore_subset

theorem balancedCore_empty : balancedCore ğ•œ (âˆ… : Set E) = âˆ… :=
  eq_empty_of_subset_empty (balancedCore_subset _)
#align balanced_core_empty balancedCore_empty

theorem mem_balancedCore_iff : x âˆˆ balancedCore ğ•œ s â†” âˆƒ t, Balanced ğ•œ t âˆ§ t âŠ† s âˆ§ x âˆˆ t := by
  simp_rw [balancedCore, mem_sUnion, mem_setOf_eq, and_assoc]
#align mem_balanced_core_iff mem_balancedCore_iff

theorem smul_balancedCore_subset (s : Set E) {a : ğ•œ} (ha : â€–aâ€– â‰¤ 1) :
    a â€¢ balancedCore ğ•œ s âŠ† balancedCore ğ•œ s := by
  rintro x âŸ¨y, hy, rflâŸ©
  rw [mem_balancedCore_iff] at hy
  rcases hy with âŸ¨t, ht1, ht2, hyâŸ©
  exact âŸ¨t, âŸ¨ht1, ht2âŸ©, ht1 a ha (smul_mem_smul_set hy)âŸ©
#align smul_balanced_core_subset smul_balancedCore_subset

theorem balancedCore_balanced (s : Set E) : Balanced ğ•œ (balancedCore ğ•œ s) := fun _ =>
  smul_balancedCore_subset s
#align balanced_core_balanced balancedCore_balanced

/-- The balanced core of `t` is maximal in the sense that it contains any balanced subset
`s` of `t`. -/
theorem Balanced.subset_balancedCore_of_subset (hs : Balanced ğ•œ s) (h : s âŠ† t) :
    s âŠ† balancedCore ğ•œ t :=
  subset_sUnion_of_mem âŸ¨hs, hâŸ©
#align balanced.subset_core_of_subset Balanced.subset_balancedCore_of_subset

theorem mem_balancedCoreAux_iff : x âˆˆ balancedCoreAux ğ•œ s â†” âˆ€ r : ğ•œ, 1 â‰¤ â€–râ€– â†’ x âˆˆ r â€¢ s :=
  mem_iInterâ‚‚
#align mem_balanced_core_aux_iff mem_balancedCoreAux_iff

theorem mem_balancedHull_iff : x âˆˆ balancedHull ğ•œ s â†” âˆƒ r : ğ•œ, â€–râ€– â‰¤ 1 âˆ§ x âˆˆ r â€¢ s := by
  simp [balancedHull]
#align mem_balanced_hull_iff mem_balancedHull_iff

/-- The balanced hull of `s` is minimal in the sense that it is contained in any balanced superset
`t` of `s`. -/
theorem Balanced.balancedHull_subset_of_subset (ht : Balanced ğ•œ t) (h : s âŠ† t) :
    balancedHull ğ•œ s âŠ† t := by
  intros x hx
  obtain âŸ¨r, hr, y, hy, rflâŸ© := mem_balancedHull_iff.1 hx
  exact ht.smul_mem hr (h hy)
#align balanced.hull_subset_of_subset Balanced.balancedHull_subset_of_subset

end SMul

section Module

variable [AddCommGroup E] [Module ğ•œ E] {s : Set E}

theorem balancedCore_zero_mem (hs : (0 : E) âˆˆ s) : (0 : E) âˆˆ balancedCore ğ•œ s :=
  mem_balancedCore_iff.2 âŸ¨0, balanced_zero, zero_subset.2 hs, Set.zero_mem_zeroâŸ©
#align balanced_core_zero_mem balancedCore_zero_mem

theorem balancedCore_nonempty_iff : (balancedCore ğ•œ s).Nonempty â†” (0 : E) âˆˆ s :=
  âŸ¨fun h => zero_subset.1 <| (zero_smul_set h).superset.trans <|
    (balancedCore_balanced s (0 : ğ•œ) <| norm_zero.trans_le zero_le_one).trans <|
      balancedCore_subset _,
    fun h => âŸ¨0, balancedCore_zero_mem hâŸ©âŸ©
#align balanced_core_nonempty_iff balancedCore_nonempty_iff

variable (ğ•œ)

theorem subset_balancedHull [NormOneClass ğ•œ] {s : Set E} : s âŠ† balancedHull ğ•œ s := fun _ hx =>
  mem_balancedHull_iff.2 âŸ¨1, norm_one.le, _, hx, one_smul _ _âŸ©
#align subset_balanced_hull subset_balancedHull

variable {ğ•œ}

theorem balancedHull.balanced (s : Set E) : Balanced ğ•œ (balancedHull ğ•œ s) := by
  intro a ha
  simp_rw [balancedHull, smul_set_iUnionâ‚‚, subset_def, mem_iUnionâ‚‚]
  rintro x âŸ¨r, hr, hxâŸ©
  rw [â† smul_assoc] at hx
  exact âŸ¨a â€¢ r, (SeminormedRing.norm_mul _ _).trans (mul_le_one ha (norm_nonneg r) hr), hxâŸ©
#align balanced_hull.balanced balancedHull.balanced

end Module

end SeminormedRing

section NormedField

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s t : Set E}

@[simp]
theorem balancedCoreAux_empty : balancedCoreAux ğ•œ (âˆ… : Set E) = âˆ… := by
  simp_rw [balancedCoreAux, iInterâ‚‚_eq_empty_iff, smul_set_empty]
  exact fun _ => âŸ¨1, norm_one.ge, not_mem_empty _âŸ©
#align balanced_core_aux_empty balancedCoreAux_empty

theorem balancedCoreAux_subset (s : Set E) : balancedCoreAux ğ•œ s âŠ† s := fun x hx => by
  simpa only [one_smul] using mem_balancedCoreAux_iff.1 hx 1 norm_one.ge
#align balanced_core_aux_subset balancedCoreAux_subset

theorem balancedCoreAux_balanced (h0 : (0 : E) âˆˆ balancedCoreAux ğ•œ s) :
    Balanced ğ•œ (balancedCoreAux ğ•œ s) := by
  rintro a ha x âŸ¨y, hy, rflâŸ©
  obtain rfl | h := eq_or_ne a 0
  Â· simp_rw [zero_smul, h0]
  rw [mem_balancedCoreAux_iff] at hy âŠ¢
  intro r hr
  have h'' : 1 â‰¤ â€–aâ»Â¹ â€¢ râ€– := by
    rw [norm_smul, norm_inv]
    exact one_le_mul_of_one_le_of_one_le (one_le_inv (norm_pos_iff.mpr h) ha) hr
  have h' := hy (aâ»Â¹ â€¢ r) h''
  rwa [smul_assoc, mem_inv_smul_set_iffâ‚€ h] at h'
#align balanced_core_aux_balanced balancedCoreAux_balanced

theorem balancedCoreAux_maximal (h : t âŠ† s) (ht : Balanced ğ•œ t) : t âŠ† balancedCoreAux ğ•œ s := by
  refine fun x hx => mem_balancedCoreAux_iff.2 fun r hr => ?_
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp <| zero_lt_one.trans_le hr)]
  refine h (ht.smul_mem ?_ hx)
  rw [norm_inv]
  exact inv_le_one hr
#align balanced_core_aux_maximal balancedCoreAux_maximal

theorem balancedCore_subset_balancedCoreAux : balancedCore ğ•œ s âŠ† balancedCoreAux ğ•œ s :=
  balancedCoreAux_maximal (balancedCore_subset s) (balancedCore_balanced s)
#align balanced_core_subset_balanced_core_aux balancedCore_subset_balancedCoreAux

theorem balancedCore_eq_iInter (hs : (0 : E) âˆˆ s) :
    balancedCore ğ•œ s = â‹‚ (r : ğ•œ) (_ : 1 â‰¤ â€–râ€–), r â€¢ s := by
  refine balancedCore_subset_balancedCoreAux.antisymm ?_
  refine (balancedCoreAux_balanced ?_).subset_balancedCore_of_subset (balancedCoreAux_subset s)
  exact balancedCore_subset_balancedCoreAux (balancedCore_zero_mem hs)
#align balanced_core_eq_Inter balancedCore_eq_iInter

theorem subset_balancedCore (ht : (0 : E) âˆˆ t) (hst : âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ s âŠ† t) :
    s âŠ† balancedCore ğ•œ t := by
  rw [balancedCore_eq_iInter ht]
  refine subset_iInterâ‚‚ fun a ha => ?_
  rw [â† smul_inv_smulâ‚€ (norm_pos_iff.mp <| zero_lt_one.trans_le ha) s]
  refine smul_set_mono (hst _ ?_)
  rw [norm_inv]
  exact inv_le_one ha
#align subset_balanced_core subset_balancedCore

end NormedField

end balancedHull

/-! ### Topological properties -/


section Topology

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [ContinuousSMul ğ•œ E] {U : Set E}

protected theorem IsClosed.balancedCore (hU : IsClosed U) : IsClosed (balancedCore ğ•œ U) := by
  by_cases h : (0 : E) âˆˆ U
  Â· rw [balancedCore_eq_iInter h]
    refine isClosed_iInter fun a => ?_
    refine isClosed_iInter fun ha => ?_
    have ha' := lt_of_lt_of_le zero_lt_one ha
    rw [norm_pos_iff] at ha'
    exact isClosedMap_smul_of_ne_zero ha' U hU
  Â· have : balancedCore ğ•œ U = âˆ… := by
      contrapose! h
      exact balancedCore_nonempty_iff.mp h
    rw [this]
    exact isClosed_empty
#align is_closed.balanced_core IsClosed.balancedCore

theorem balancedCore_mem_nhds_zero (hU : U âˆˆ ğ“ (0 : E)) : balancedCore ğ•œ U âˆˆ ğ“ (0 : E) := by
  -- Getting neighborhoods of the origin for `0 : ğ•œ` and `0 : E`
  obtain âŸ¨r, V, hr, hV, hrVUâŸ© : âˆƒ (r : â„) (V : Set E),
      0 < r âˆ§ V âˆˆ ğ“ (0 : E) âˆ§ âˆ€ (c : ğ•œ) (y : E), â€–câ€– < r â†’ y âˆˆ V â†’ c â€¢ y âˆˆ U := by
    have h : Filter.Tendsto (fun x : ğ•œ Ã— E => x.fst â€¢ x.snd) (ğ“ (0, 0)) (ğ“ 0) :=
      continuous_smul.tendsto' (0, 0) _ (smul_zero _)
    simpa only [â† Prod.exists', â† Prod.forall', â† and_imp, â† and_assoc, exists_prop] using
      h.basis_left (NormedAddCommGroup.nhds_zero_basis_norm_lt.prod_nhds (ğ“ _).basis_sets) U hU
  rcases NormedField.exists_norm_lt ğ•œ hr with âŸ¨y, hyâ‚€, hyrâŸ©
  rw [norm_pos_iff] at hyâ‚€
  have : y â€¢ V âˆˆ ğ“ (0 : E) := (set_smul_mem_nhds_zero_iff hyâ‚€).mpr hV
  -- It remains to show that `y â€¢ V âŠ† balancedCore ğ•œ U`
  refine Filter.mem_of_superset this (subset_balancedCore (mem_of_mem_nhds hU) fun a ha => ?_)
  rw [smul_smul]
  rintro _ âŸ¨z, hz, rflâŸ©
  refine hrVU _ _ ?_ hz
  rw [norm_mul, â† one_mul r]
  exact mul_lt_mul' ha hyr (norm_nonneg y) one_pos
#align balanced_core_mem_nhds_zero balancedCore_mem_nhds_zero

variable (ğ•œ E)

theorem nhds_basis_balanced :
    (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ ğ“ (0 : E) âˆ§ Balanced ğ•œ s) id :=
  Filter.hasBasis_self.mpr fun s hs =>
    âŸ¨balancedCore ğ•œ s, balancedCore_mem_nhds_zero hs, balancedCore_balanced s,
      balancedCore_subset sâŸ©
#align nhds_basis_balanced nhds_basis_balanced

theorem nhds_basis_closed_balanced [RegularSpace E] :
    (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ ğ“ (0 : E) âˆ§ IsClosed s âˆ§ Balanced ğ•œ s) id := by
  refine
    (closed_nhds_basis 0).to_hasBasis (fun s hs => ?_) fun s hs => âŸ¨s, âŸ¨hs.1, hs.2.1âŸ©, rfl.subsetâŸ©
  refine âŸ¨balancedCore ğ•œ s, âŸ¨balancedCore_mem_nhds_zero hs.1, ?_âŸ©, balancedCore_subset sâŸ©
  exact âŸ¨hs.2.balancedCore, balancedCore_balanced sâŸ©
#align nhds_basis_closed_balanced nhds_basis_closed_balanced

end Topology
