/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.Convex.Hull
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.Normed.MulAction
import Mathlib.Topology.Bornology.Absorbs

/-!
# Local convexity

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a â€¢ s âŠ† s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `Absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `Absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `Balanced`: A set `s` is balanced if `a â€¢ s âŠ† s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/


open Set

open Pointwise Topology

variable {ğ•œ ğ• E F : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

section SeminormedRing

variable [SeminormedRing ğ•œ]

section SMul

variable [SMul ğ•œ E] {s A B : Set E}

variable (ğ•œ) in
/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def Balanced (A : Set E) :=
  âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ A âŠ† A

lemma absorbs_iff_norm : Absorbs ğ•œ A B â†” âˆƒ r, âˆ€ c : ğ•œ, r â‰¤ â€–câ€– â†’ B âŠ† c â€¢ A :=
  Filter.atTop_basis.cobounded_of_norm.eventually_iff.trans <| by simp only [true_and]; rfl

alias âŸ¨_, Absorbs.of_normâŸ© := absorbs_iff_norm

lemma Absorbs.exists_pos (h : Absorbs ğ•œ A B) : âˆƒ r > 0, âˆ€ c : ğ•œ, r â‰¤ â€–câ€– â†’ B âŠ† c â€¢ A :=
  let âŸ¨r, hrâ‚, hrâŸ© := (Filter.atTop_basis' 1).cobounded_of_norm.eventually_iff.1 h
  âŸ¨r, one_pos.trans_le hrâ‚, hrâŸ©

theorem balanced_iff_smul_mem : Balanced ğ•œ s â†” âˆ€ â¦ƒa : ğ•œâ¦„, â€–aâ€– â‰¤ 1 â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ a â€¢ x âˆˆ s :=
  forallâ‚‚_congr fun _a _ha => smul_set_subset_iff

alias âŸ¨Balanced.smul_mem, _âŸ© := balanced_iff_smul_mem

theorem balanced_iff_closedBall_smul : Balanced ğ•œ s â†” Metric.closedBall (0 : ğ•œ) 1 â€¢ s âŠ† s := by
  simp [balanced_iff_smul_mem, smul_subset_iff]

@[simp]
theorem balanced_empty : Balanced ğ•œ (âˆ… : Set E) := fun _ _ => by rw [smul_set_empty]

@[simp]
theorem balanced_univ : Balanced ğ•œ (univ : Set E) := fun _a _ha => subset_univ _

theorem Balanced.union (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆª B) := fun _a ha =>
  smul_set_union.subset.trans <| union_subset_union (hA _ ha) <| hB _ ha

theorem Balanced.inter (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆ© B) := fun _a ha =>
  smul_set_inter_subset.trans <| inter_subset_inter (hA _ ha) <| hB _ ha

theorem balanced_iUnion {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹ƒ i, f i) :=
  fun _a ha => (smul_set_iUnion _ _).subset.trans <| iUnion_mono fun _ => h _ _ ha

theorem balanced_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹ƒ (i) (j), f i j) :=
  balanced_iUnion fun _ => balanced_iUnion <| h _

theorem Balanced.sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, Balanced ğ•œ s) : Balanced ğ•œ (â‹‚â‚€ S) :=
  fun _ _ => (smul_set_sInter_subset ..).trans (fun _ _ => by aesop)

theorem balanced_iInter {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹‚ i, f i) :=
  fun _a ha => (smul_set_iInter_subset _ _).trans <| iInter_mono fun _ => h _ _ ha

theorem balanced_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹‚ (i) (j), f i j) :=
  balanced_iInter fun _ => balanced_iInter <| h _

theorem Balanced.mulActionHom_preimage [SMul ğ•œ F] {s : Set F} (hs : Balanced ğ•œ s)
    (f : E â†’[ğ•œ] F) : Balanced ğ•œ (f â»Â¹' s) := fun a ha x âŸ¨y,âŸ¨hyâ‚,hyâ‚‚âŸ©âŸ© => by
  rw [mem_preimage, â† hyâ‚‚, map_smul]
  exact hs a ha (smul_mem_smul_set hyâ‚)

variable [SMul ğ• E] [SMulCommClass ğ•œ ğ• E]

theorem Balanced.smul (a : ğ•) (hs : Balanced ğ•œ s) : Balanced ğ•œ (a â€¢ s) := fun _b hb =>
  (smul_comm _ _ _).subset.trans <| smul_set_mono <| hs _ hb

end SMul

section Module

variable [AddCommGroup E] [Module ğ•œ E] {s t : Set E}

theorem Balanced.neg : Balanced ğ•œ s â†’ Balanced ğ•œ (-s) :=
  forallâ‚‚_imp fun _ _ h => (smul_set_neg _ _).subset.trans <| neg_subset_neg.2 h

@[simp]
theorem balanced_neg : Balanced ğ•œ (-s) â†” Balanced ğ•œ s :=
  âŸ¨fun h â†¦ neg_neg s â–¸ h.neg, fun h â†¦ h.negâŸ©

theorem Balanced.neg_mem_iff [NormOneClass ğ•œ] (h : Balanced ğ•œ s) {x : E} : -x âˆˆ s â†” x âˆˆ s :=
  âŸ¨fun hx â†¦ by simpa using h.smul_mem (a := -1) (by simp) hx,
    fun hx â†¦ by simpa using h.smul_mem (a := -1) (by simp) hxâŸ©

theorem Balanced.neg_eq [NormOneClass ğ•œ] (h : Balanced ğ•œ s) : -s = s :=
  Set.ext fun _ â†¦ h.neg_mem_iff

theorem Balanced.add (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s + t) := fun _a ha =>
  (smul_add _ _ _).subset.trans <| add_subset_add (hs _ ha) <| ht _ ha

theorem Balanced.sub (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s - t) := by
  simp_rw [sub_eq_add_neg]
  exact hs.add ht.neg

theorem balanced_zero : Balanced ğ•œ (0 : Set E) := fun _a _ha => (smul_zero _).subset

end Module

end SeminormedRing

section NormedDivisionRing

variable [NormedDivisionRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s t : Set E}

theorem absorbs_iff_eventually_nhdsNE_zero :
    Absorbs ğ•œ s t â†” âˆ€á¶  c : ğ•œ in ğ“[â‰ ] 0, MapsTo (c â€¢ Â·) t s := by
  rw [absorbs_iff_eventually_cobounded_mapsTo, â† Filter.inv_coboundedâ‚€]; rfl

alias âŸ¨Absorbs.eventually_nhdsNE_zero, _âŸ© := absorbs_iff_eventually_nhdsNE_zero

theorem absorbent_iff_eventually_nhdsNE_zero :
    Absorbent ğ•œ s â†” âˆ€ x : E, âˆ€á¶  c : ğ•œ in ğ“[â‰ ] 0, c â€¢ x âˆˆ s :=
  forall_congr' fun x â†¦ by simp only [absorbs_iff_eventually_nhdsNE_zero, mapsTo_singleton]

alias âŸ¨Absorbent.eventually_nhdsNE_zero, _âŸ© := absorbent_iff_eventually_nhdsNE_zero

theorem absorbs_iff_eventually_nhds_zero (hâ‚€ : 0 âˆˆ s) :
    Absorbs ğ•œ s t â†” âˆ€á¶  c : ğ•œ in ğ“ 0, MapsTo (c â€¢ Â·) t s := by
  rw [â† nhdsNE_sup_pure, Filter.eventually_sup, Filter.eventually_pure,
    â† absorbs_iff_eventually_nhdsNE_zero, and_iff_left]
  intro x _
  simpa only [zero_smul]

theorem Absorbs.eventually_nhds_zero (h : Absorbs ğ•œ s t) (hâ‚€ : 0 âˆˆ s) :
    âˆ€á¶  c : ğ•œ in ğ“ 0, MapsTo (c â€¢ Â·) t s :=
  (absorbs_iff_eventually_nhds_zero hâ‚€).1 h

variable [NormedRing ğ•] [Module ğ•œ ğ•] [NormSMulClass ğ•œ ğ•] [SMulWithZero ğ• E] [IsScalarTower ğ•œ ğ• E]
  {a b : ğ•œ} {x : E}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
theorem Balanced.smul_mono (hs : Balanced ğ• s) {a : ğ•} (h : â€–aâ€– â‰¤ â€–bâ€–) : a â€¢ s âŠ† b â€¢ s := by
  obtain rfl | hb := eq_or_ne b 0
  Â· rw [norm_zero, norm_le_zero_iff] at h
    simp only [h, â† image_smul, zero_smul, Subset.rfl]
  Â· calc
      a â€¢ s = b â€¢ (bâ»Â¹ â€¢ a) â€¢ s := by rw [smul_assoc, smul_inv_smulâ‚€ hb]
      _ âŠ† b â€¢ s := smul_set_mono <| hs _ <| by
        rw [norm_smul, norm_inv, â† div_eq_inv_mul]
        exact div_le_one_of_leâ‚€ h (norm_nonneg _)

theorem Balanced.smul_mem_mono [SMulCommClass ğ• ğ•œ E] (hs : Balanced ğ• s) {b : ğ•}
    (ha : a â€¢ x âˆˆ s) (hba : â€–bâ€– â‰¤ â€–aâ€–) : b â€¢ x âˆˆ s := by
  rcases eq_or_ne a 0 with rfl | haâ‚€
  Â· simp_all
  Â· calc
      (aâ»Â¹ â€¢ b) â€¢ a â€¢ x âˆˆ s := by
        refine hs.smul_mem ?_ ha
        rw [norm_smul, norm_inv, â† div_eq_inv_mul]
        exact div_le_one_of_leâ‚€ hba (norm_nonneg _)
      (aâ»Â¹ â€¢ b) â€¢ a â€¢ x = b â€¢ x := by rw [smul_comm, smul_assoc, smul_inv_smulâ‚€ haâ‚€]

theorem Balanced.subset_smul (hs : Balanced ğ•œ s) (ha : 1 â‰¤ â€–aâ€–) : s âŠ† a â€¢ s := by
  rw [â† @norm_one ğ•œ] at ha; simpa using hs.smul_mono ha

theorem Balanced.smul_congr (hs : Balanced ğ•œ s) (h : â€–aâ€– = â€–bâ€–) : a â€¢ s = b â€¢ s :=
  (hs.smul_mono h.le).antisymm (hs.smul_mono h.ge)

theorem Balanced.smul_eq (hs : Balanced ğ•œ s) (ha : â€–aâ€– = 1) : a â€¢ s = s :=
  (hs _ ha.le).antisymm <| hs.subset_smul ha.ge

/-- A balanced set absorbs itself. -/
theorem Balanced.absorbs_self (hs : Balanced ğ•œ s) : Absorbs ğ•œ s s :=
  .of_norm âŸ¨1, fun _ => hs.subset_smulâŸ©

end NormedDivisionRing

section NormedField

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s A : Set E} {x : E} {a b : ğ•œ}

theorem Balanced.smul_mem_iff (hs : Balanced ğ•œ s) (h : â€–aâ€– = â€–bâ€–) : a â€¢ x âˆˆ s â†” b â€¢ x âˆˆ s :=
  âŸ¨(hs.smul_mem_mono Â· h.ge), (hs.smul_mem_mono Â· h.le)âŸ©

variable [TopologicalSpace E] [ContinuousSMul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
theorem absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : Absorbent ğ•œ A :=
  absorbent_iff_inv_smul.2 fun x â†¦ Filter.tendsto_invâ‚€_cobounded.smul tendsto_const_nhds <| by
    rwa [zero_smul]

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
theorem Balanced.zero_insert_interior (hA : Balanced ğ•œ A) :
    Balanced ğ•œ (insert 0 (interior A)) := by
  intro a ha
  obtain rfl | h := eq_or_ne a 0
  Â· rw [zero_smul_set]
    exacts [subset_union_left, âŸ¨0, Or.inl rflâŸ©]
  Â· rw [â† image_smul, image_insert_eq, smul_zero]
    apply insert_subset_insert
    exact ((isOpenMap_smulâ‚€ h).mapsTo_interior <| hA.smul_mem ha).image_subset

/-- The interior of a balanced set is balanced if it contains the origin. -/
protected theorem Balanced.interior (hA : Balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) :
    Balanced ğ•œ (interior A) := by
  rw [â† insert_eq_self.2 h]
  exact hA.zero_insert_interior

protected theorem Balanced.closure (hA : Balanced ğ•œ A) : Balanced ğ•œ (closure A) := fun _a ha =>
  (image_closure_subset_closure_image <| continuous_const_smul _).trans <|
    closure_mono <| hA _ ha

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [PartialOrder ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s : Set E}

protected theorem Balanced.convexHull (hs : Balanced ğ•œ s) : Balanced ğ•œ (convexHull ğ•œ s) := by
  suffices Convex ğ•œ { x | âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ x âˆˆ convexHull ğ•œ s } by
    rw [balanced_iff_smul_mem] at hs âŠ¢
    refine fun a ha x hx => convexHull_min ?_ this hx a ha
    exact fun y hy a ha => subset_convexHull ğ•œ s (hs ha hy)
  intro x hx y hy u v hu hv huv a ha
  rw [smul_add, â† smul_comm u, â† smul_comm v]
  exact convex_convexHull ğ•œ s (hx a ha) (hy a ha) hu hv huv

end NontriviallyNormedField

section Real

variable [AddCommGroup E] [Module â„ E] {s : Set E}

theorem balanced_iff_neg_mem (hs : Convex â„ s) : Balanced â„ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ -x âˆˆ s := by
  refine âŸ¨fun h x => h.neg_mem_iff.2, fun h a ha => smul_set_subset_iff.2 fun x hx => ?_âŸ©
  rw [Real.norm_eq_abs, abs_le] at ha
  rw [show a = -((1 - a) / 2) + (a - -1) / 2 by ring, add_smul, neg_smul, â† smul_neg]
  exact hs (h hx) hx (div_nonneg (sub_nonneg_of_le ha.2) zero_le_two)
    (div_nonneg (sub_nonneg_of_le ha.1) zero_le_two) (by ring)

end Real
