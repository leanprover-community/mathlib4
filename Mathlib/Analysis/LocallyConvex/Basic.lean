/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.Convex.Hull
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.Topology.Bornology.Absorbs

#align_import analysis.locally_convex.basic from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Local convexity

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a â€¢ s âŠ† s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `Absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `Absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `Balanced`: A set `s` is balanced if `a â€¢ s âŠ† s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/


open Set

open Pointwise Topology

variable {ğ•œ ğ• E : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

section SeminormedRing

variable [SeminormedRing ğ•œ]

section SMul

variable [SMul ğ•œ E] {s t u v A B : Set E}
variable (ğ•œ)

/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def Balanced (A : Set E) :=
  âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ A âŠ† A
#align balanced Balanced

variable {ğ•œ}

lemma absorbs_iff_norm : Absorbs ğ•œ A B â†” âˆƒ r, âˆ€ c : ğ•œ, r â‰¤ â€–câ€– â†’ B âŠ† c â€¢ A :=
  Filter.atTop_basis.cobounded_of_norm.eventually_iff.trans <| by simp only [true_and]; rfl

alias âŸ¨_, Absorbs.of_normâŸ© := absorbs_iff_norm

lemma Absorbs.exists_pos (h : Absorbs ğ•œ A B) : âˆƒ r > 0, âˆ€ c : ğ•œ, r â‰¤ â€–câ€– â†’ B âŠ† c â€¢ A :=
  let âŸ¨r, hrâ‚, hrâŸ© := (Filter.atTop_basis' 1).cobounded_of_norm.eventually_iff.1 h
  âŸ¨r, one_pos.trans_le hrâ‚, hrâŸ©

theorem balanced_iff_smul_mem : Balanced ğ•œ s â†” âˆ€ â¦ƒa : ğ•œâ¦„, â€–aâ€– â‰¤ 1 â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ a â€¢ x âˆˆ s :=
  forallâ‚‚_congr fun _a _ha => smul_set_subset_iff
#align balanced_iff_smul_mem balanced_iff_smul_mem

alias âŸ¨Balanced.smul_mem, _âŸ© := balanced_iff_smul_mem
#align balanced.smul_mem Balanced.smul_mem

theorem balanced_iff_closedBall_smul : Balanced ğ•œ s â†” Metric.closedBall (0 : ğ•œ) 1 â€¢ s âŠ† s := by
  simp [balanced_iff_smul_mem, smul_subset_iff]

@[simp]
theorem balanced_empty : Balanced ğ•œ (âˆ… : Set E) := fun _ _ => by rw [smul_set_empty]
#align balanced_empty balanced_empty

@[simp]
theorem balanced_univ : Balanced ğ•œ (univ : Set E) := fun _a _ha => subset_univ _
#align balanced_univ balanced_univ

theorem Balanced.union (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆª B) := fun _a ha =>
  smul_set_union.subset.trans <| union_subset_union (hA _ ha) <| hB _ ha
#align balanced.union Balanced.union

theorem Balanced.inter (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆ© B) := fun _a ha =>
  smul_set_inter_subset.trans <| inter_subset_inter (hA _ ha) <| hB _ ha
#align balanced.inter Balanced.inter

theorem balanced_iUnion {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹ƒ i, f i) :=
  fun _a ha => (smul_set_iUnion _ _).subset.trans <| iUnion_mono fun _ => h _ _ ha
#align balanced_Union balanced_iUnion

theorem balanced_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹ƒ (i) (j), f i j) :=
  balanced_iUnion fun _ => balanced_iUnion <| h _
#align balanced_Unionâ‚‚ balanced_iUnionâ‚‚

theorem balanced_iInter {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹‚ i, f i) :=
  fun _a ha => (smul_set_iInter_subset _ _).trans <| iInter_mono fun _ => h _ _ ha
#align balanced_Inter balanced_iInter

theorem balanced_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹‚ (i) (j), f i j) :=
  balanced_iInter fun _ => balanced_iInter <| h _
#align balanced_Interâ‚‚ balanced_iInterâ‚‚

variable [SMul ğ• E] [SMulCommClass ğ•œ ğ• E]

theorem Balanced.smul (a : ğ•) (hs : Balanced ğ•œ s) : Balanced ğ•œ (a â€¢ s) := fun _b hb =>
  (smul_comm _ _ _).subset.trans <| smul_set_mono <| hs _ hb
#align balanced.smul Balanced.smul

end SMul

section Module

variable [AddCommGroup E] [Module ğ•œ E] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : Set E}

theorem Balanced.neg : Balanced ğ•œ s â†’ Balanced ğ•œ (-s) :=
  forallâ‚‚_imp fun _ _ h => (smul_set_neg _ _).subset.trans <| neg_subset_neg.2 h
#align balanced.neg Balanced.neg

@[simp]
theorem balanced_neg : Balanced ğ•œ (-s) â†” Balanced ğ•œ s :=
  âŸ¨fun h â†¦ neg_neg s â–¸ h.neg, fun h â†¦ h.negâŸ©

theorem Balanced.neg_mem_iff [NormOneClass ğ•œ] (h : Balanced ğ•œ s) {x : E} : -x âˆˆ s â†” x âˆˆ s :=
  âŸ¨fun hx â†¦ by simpa using h.smul_mem (a := -1) (by simp) hx,
    fun hx â†¦ by simpa using h.smul_mem (a := -1) (by simp) hxâŸ©
#align balanced.neg_mem_iff Balanced.neg_mem_iff

theorem Balanced.neg_eq [NormOneClass ğ•œ] (h : Balanced ğ•œ s) : -s = s :=
  Set.ext fun _ â†¦ h.neg_mem_iff

theorem Balanced.add (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s + t) := fun _a ha =>
  (smul_add _ _ _).subset.trans <| add_subset_add (hs _ ha) <| ht _ ha
#align balanced.add Balanced.add

theorem Balanced.sub (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s - t) := by
  simp_rw [sub_eq_add_neg]
  exact hs.add ht.neg
#align balanced.sub Balanced.sub

theorem balanced_zero : Balanced ğ•œ (0 : Set E) := fun _a _ha => (smul_zero _).subset
#align balanced_zero balanced_zero

end Module

end SeminormedRing

section NormedDivisionRing

variable [NormedDivisionRing ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s t : Set E} {x : E} {a b : ğ•œ}

theorem absorbs_iff_eventually_nhdsWithin_zero :
    Absorbs ğ•œ s t â†” âˆ€á¶  c : ğ•œ in ğ“[â‰ ] 0, MapsTo (c â€¢ Â·) t s := by
  rw [absorbs_iff_eventually_cobounded_mapsTo, â† Filter.inv_coboundedâ‚€]; rfl

alias âŸ¨Absorbs.eventually_nhdsWithin_zero, _âŸ© := absorbs_iff_eventually_nhdsWithin_zero

theorem absorbent_iff_eventually_nhdsWithin_zero :
    Absorbent ğ•œ s â†” âˆ€ x : E, âˆ€á¶  c : ğ•œ in ğ“[â‰ ] 0, c â€¢ x âˆˆ s :=
  forall_congr' fun x â†¦ by simp only [absorbs_iff_eventually_nhdsWithin_zero, mapsTo_singleton]

alias âŸ¨Absorbent.eventually_nhdsWithin_zero, _âŸ© := absorbent_iff_eventually_nhdsWithin_zero

theorem absorbs_iff_eventually_nhds_zero (hâ‚€ : 0 âˆˆ s) :
    Absorbs ğ•œ s t â†” âˆ€á¶  c : ğ•œ in ğ“ 0, MapsTo (c â€¢ Â·) t s := by
  rw [â† nhdsWithin_compl_singleton_sup_pure, Filter.eventually_sup, Filter.eventually_pure,
    â† absorbs_iff_eventually_nhdsWithin_zero, and_iff_left]
  intro x _
  simpa only [zero_smul]

theorem Absorbs.eventually_nhds_zero (h : Absorbs ğ•œ s t) (hâ‚€ : 0 âˆˆ s) :
    âˆ€á¶  c : ğ•œ in ğ“ 0, MapsTo (c â€¢ Â·) t s :=
  (absorbs_iff_eventually_nhds_zero hâ‚€).1 h

end NormedDivisionRing

section NormedField

variable [NormedField ğ•œ] [NormedRing ğ•] [NormedSpace ğ•œ ğ•] [AddCommGroup E] [Module ğ•œ E]
  [SMulWithZero ğ• E] [IsScalarTower ğ•œ ğ• E] {s t u v A B : Set E} {x : E} {a b : ğ•œ}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
theorem Balanced.smul_mono (hs : Balanced ğ• s) {a : ğ•} {b : ğ•œ} (h : â€–aâ€– â‰¤ â€–bâ€–) : a â€¢ s âŠ† b â€¢ s := by
  obtain rfl | hb := eq_or_ne b 0
  Â· rw [norm_zero, norm_le_zero_iff] at h
    simp only [h, â† image_smul, zero_smul, Subset.rfl]
  Â· calc
      a â€¢ s = b â€¢ (bâ»Â¹ â€¢ a) â€¢ s := by rw [smul_assoc, smul_inv_smulâ‚€ hb]
      _ âŠ† b â€¢ s := smul_set_mono <| hs _ <| by
        rw [norm_smul, norm_inv, â† div_eq_inv_mul]
        exact div_le_one_of_le h (norm_nonneg _)
#align balanced.smul_mono Balanced.smul_mono

theorem Balanced.smul_mem_mono [SMulCommClass ğ• ğ•œ E] (hs : Balanced ğ• s) {a : ğ•œ} {b : ğ•}
    (ha : a â€¢ x âˆˆ s) (hba : â€–bâ€– â‰¤ â€–aâ€–) : b â€¢ x âˆˆ s := by
  rcases eq_or_ne a 0 with rfl | haâ‚€
  Â· simp_all
  Â· calc
      b â€¢ x = (aâ»Â¹ â€¢ b) â€¢ a â€¢ x := by rw [smul_comm, smul_assoc, smul_inv_smulâ‚€ haâ‚€]
      _ âˆˆ s := by
        refine hs.smul_mem ?_ ha
        rw [norm_smul, norm_inv, â† div_eq_inv_mul]
        exact div_le_one_of_le hba (norm_nonneg _)

theorem Balanced.subset_smul (hA : Balanced ğ•œ A) (ha : 1 â‰¤ â€–aâ€–) : A âŠ† a â€¢ A := by
  rw [â† @norm_one ğ•œ] at ha; simpa using hA.smul_mono ha
#align balanced.subset_smul Balanced.subset_smul

theorem Balanced.smul_congr (hs : Balanced ğ•œ A) (h : â€–aâ€– = â€–bâ€–) : a â€¢ A = b â€¢ A :=
  (hs.smul_mono h.le).antisymm (hs.smul_mono h.ge)

theorem Balanced.smul_eq (hA : Balanced ğ•œ A) (ha : â€–aâ€– = 1) : a â€¢ A = A :=
  (hA _ ha.le).antisymm <| hA.subset_smul ha.ge
#align balanced.smul_eq Balanced.smul_eq

/-- A balanced set absorbs itself. -/
theorem Balanced.absorbs_self (hA : Balanced ğ•œ A) : Absorbs ğ•œ A A :=
  .of_norm âŸ¨1, fun _ => hA.subset_smulâŸ©
#align balanced.absorbs_self Balanced.absorbs_self

theorem Balanced.smul_mem_iff (hs : Balanced ğ•œ s) (h : â€–aâ€– = â€–bâ€–) : a â€¢ x âˆˆ s â†” b â€¢ x âˆˆ s :=
  âŸ¨(hs.smul_mem_mono Â· h.ge), (hs.smul_mem_mono Â· h.le)âŸ©
#align balanced.mem_smul_iff Balanced.smul_mem_iff

@[deprecated (since := "2024-02-02")] alias Balanced.mem_smul_iff := Balanced.smul_mem_iff

variable [TopologicalSpace E] [ContinuousSMul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
theorem absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : Absorbent ğ•œ A :=
  absorbent_iff_inv_smul.2 fun x â†¦ Filter.tendsto_invâ‚€_cobounded.smul tendsto_const_nhds <| by
    rwa [zero_smul]
#align absorbent_nhds_zero absorbent_nhds_zero

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
theorem Balanced.zero_insert_interior (hA : Balanced ğ•œ A) :
    Balanced ğ•œ (insert 0 (interior A)) := by
  intro a ha
  obtain rfl | h := eq_or_ne a 0
  Â· rw [zero_smul_set]
    exacts [subset_union_left, âŸ¨0, Or.inl rflâŸ©]
  Â· rw [â† image_smul, image_insert_eq, smul_zero]
    apply insert_subset_insert
    exact ((isOpenMap_smulâ‚€ h).mapsTo_interior <| hA.smul_mem ha).image_subset
#align balanced_zero_union_interior Balanced.zero_insert_interior

@[deprecated Balanced.zero_insert_interior]
theorem balanced_zero_union_interior (hA : Balanced ğ•œ A) : Balanced ğ•œ ((0 : Set E) âˆª interior A) :=
  hA.zero_insert_interior

/-- The interior of a balanced set is balanced if it contains the origin. -/
protected theorem Balanced.interior (hA : Balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) :
    Balanced ğ•œ (interior A) := by
  rw [â† insert_eq_self.2 h]
  exact hA.zero_insert_interior
#align balanced.interior Balanced.interior

protected theorem Balanced.closure (hA : Balanced ğ•œ A) : Balanced ğ•œ (closure A) := fun _a ha =>
  (image_closure_subset_closure_image <| continuous_const_smul _).trans <|
    closure_mono <| hA _ ha
#align balanced.closure Balanced.closure

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s : Set E}

@[deprecated Absorbent.zero_mem (since := "2024-02-02")]
theorem Absorbent.zero_mem' (hs : Absorbent ğ•œ s) : (0 : E) âˆˆ s := hs.zero_mem

variable [Module â„ E] [SMulCommClass â„ ğ•œ E]

protected theorem Balanced.convexHull (hs : Balanced ğ•œ s) : Balanced ğ•œ (convexHull â„ s) := by
  suffices Convex â„ { x | âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ x âˆˆ convexHull â„ s } by
    rw [balanced_iff_smul_mem] at hs âŠ¢
    refine fun a ha x hx => convexHull_min ?_ this hx a ha
    exact fun y hy a ha => subset_convexHull â„ s (hs ha hy)
  intro x hx y hy u v hu hv huv a ha
  simp only [smul_add, â† smul_comm]
  exact convex_convexHull â„ s (hx a ha) (hy a ha) hu hv huv
#align balanced_convex_hull_of_balanced Balanced.convexHull

@[deprecated (since := "2024-02-02")] alias balanced_convexHull_of_balanced := Balanced.convexHull

end NontriviallyNormedField

section Real

variable [AddCommGroup E] [Module â„ E] {s : Set E}

theorem balanced_iff_neg_mem (hs : Convex â„ s) : Balanced â„ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ -x âˆˆ s := by
  refine âŸ¨fun h x => h.neg_mem_iff.2, fun h a ha => smul_set_subset_iff.2 fun x hx => ?_âŸ©
  rw [Real.norm_eq_abs, abs_le] at ha
  rw [show a = -((1 - a) / 2) + (a - -1) / 2 by ring, add_smul, neg_smul, â† smul_neg]
  exact hs (h hx) hx (div_nonneg (sub_nonneg_of_le ha.2) zero_le_two)
    (div_nonneg (sub_nonneg_of_le ha.1) zero_le_two) (by ring)
#align balanced_iff_neg_mem balanced_iff_neg_mem

end Real
