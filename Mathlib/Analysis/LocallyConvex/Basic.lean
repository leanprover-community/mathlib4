/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.Convex.Hull
import Mathlib.Analysis.NormedSpace.Basic

#align_import analysis.locally_convex.basic from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Local convexity

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a â€¢ s âŠ† s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `Absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `Absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `Balanced`: A set `s` is balanced if `a â€¢ s âŠ† s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/


open Set

open Pointwise Topology

variable {ğ•œ ğ• E : Type*} {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*}

section SeminormedRing

variable [SeminormedRing ğ•œ]

section SMul

variable (ğ•œ) [SMul ğ•œ E]

/-- A set `A` absorbs another set `B` if `B` is contained in all scalings of `A` by elements of
sufficiently large norm. -/
def Absorbs (A B : Set E) :=
  âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ â€–aâ€– â†’ B âŠ† a â€¢ A
#align absorbs Absorbs

variable {ğ•œ} {s t u v A B : Set E}

@[simp]
theorem absorbs_empty {s : Set E} : Absorbs ğ•œ s (âˆ… : Set E) :=
  âŸ¨1, one_pos, fun _a _ha => Set.empty_subset _âŸ©
#align absorbs_empty absorbs_empty

theorem Absorbs.mono (hs : Absorbs ğ•œ s u) (hst : s âŠ† t) (hvu : v âŠ† u) : Absorbs ğ•œ t v :=
  let âŸ¨r, hr, hâŸ© := hs
  âŸ¨r, hr, fun _a ha => hvu.trans <| (h _ ha).trans <| smul_set_mono hstâŸ©
#align absorbs.mono Absorbs.mono

theorem Absorbs.mono_left (hs : Absorbs ğ•œ s u) (h : s âŠ† t) : Absorbs ğ•œ t u :=
  hs.mono h Subset.rfl
#align absorbs.mono_left Absorbs.mono_left

theorem Absorbs.mono_right (hs : Absorbs ğ•œ s u) (h : v âŠ† u) : Absorbs ğ•œ s v :=
  hs.mono Subset.rfl h
#align absorbs.mono_right Absorbs.mono_right

theorem Absorbs.union (hu : Absorbs ğ•œ s u) (hv : Absorbs ğ•œ s v) : Absorbs ğ•œ s (u âˆª v) := by
  obtain âŸ¨a, ha, huâŸ© := hu
  obtain âŸ¨b, _hb, hvâŸ© := hv
  exact
    âŸ¨max a b, lt_max_of_lt_left ha, fun c hc =>
      union_subset (hu _ <| le_of_max_le_left hc) (hv _ <| le_of_max_le_right hc)âŸ©
#align absorbs.union Absorbs.union

@[simp]
theorem absorbs_union : Absorbs ğ•œ s (u âˆª v) â†” Absorbs ğ•œ s u âˆ§ Absorbs ğ•œ s v :=
  âŸ¨fun h => âŸ¨h.mono_right <| subset_union_left _ _, h.mono_right <| subset_union_right _ _âŸ©,
    fun h => h.1.union h.2âŸ©
#align absorbs_union absorbs_union

theorem absorbs_iUnion_finset {Î¹ : Type*} {t : Finset Î¹} {f : Î¹ â†’ Set E} :
    Absorbs ğ•œ s (â‹ƒ i âˆˆ t, f i) â†” âˆ€ i âˆˆ t, Absorbs ğ•œ s (f i) := by
  classical
    induction' t using Finset.induction_on with i t _ht hi
    Â· simp only [Finset.not_mem_empty, Set.iUnion_false, Set.iUnion_empty, absorbs_empty,
        IsEmpty.forall_iff, imp_true_iff]
    rw [Finset.set_biUnion_insert, absorbs_union, hi]
    constructor <;> intro h
    Â· refine' fun _ hi' => (Finset.mem_insert.mp hi').elim _ (h.2 _)
      exact fun hi'' => by
        rw [hi'']
        exact h.1
    exact âŸ¨h i (Finset.mem_insert_self i t), fun i' hi' => h i' (Finset.mem_insert_of_mem hi')âŸ©
#align absorbs_Union_finset absorbs_iUnion_finset

theorem Set.Finite.absorbs_iUnion {Î¹ : Type*} {s : Set E} {t : Set Î¹} {f : Î¹ â†’ Set E}
    (hi : t.Finite) : Absorbs ğ•œ s (â‹ƒ i âˆˆ t, f i) â†” âˆ€ i âˆˆ t, Absorbs ğ•œ s (f i) := by
  lift t to Finset Î¹ using hi
  simp only [Finset.mem_coe]
  exact absorbs_iUnion_finset
#align set.finite.absorbs_Union Set.Finite.absorbs_iUnion

variable (ğ•œ)

/-- A set is absorbent if it absorbs every singleton. -/
def Absorbent (A : Set E) :=
  âˆ€ x, âˆƒ r, 0 < r âˆ§ âˆ€ a : ğ•œ, r â‰¤ â€–aâ€– â†’ x âˆˆ a â€¢ A
#align absorbent Absorbent

variable {ğ•œ}

theorem Absorbent.subset (hA : Absorbent ğ•œ A) (hAB : A âŠ† B) : Absorbent ğ•œ B := by
  refine' forall_imp (fun x => _) hA
  exact Exists.imp fun r => And.imp_right <| forallâ‚‚_imp fun a _ha hx => Set.smul_set_mono hAB hx
#align absorbent.subset Absorbent.subset

theorem absorbent_iff_forall_absorbs_singleton : Absorbent ğ•œ A â†” âˆ€ x, Absorbs ğ•œ A {x} := by
  simp_rw [Absorbs, Absorbent, singleton_subset_iff]
#align absorbent_iff_forall_absorbs_singleton absorbent_iff_forall_absorbs_singleton

theorem Absorbent.absorbs (hs : Absorbent ğ•œ s) {x : E} : Absorbs ğ•œ s {x} :=
  absorbent_iff_forall_absorbs_singleton.1 hs _
#align absorbent.absorbs Absorbent.absorbs

theorem absorbent_iff_nonneg_lt :
    Absorbent ğ•œ A â†” âˆ€ x, âˆƒ r, 0 â‰¤ r âˆ§ âˆ€ â¦ƒa : ğ•œâ¦„, r < â€–aâ€– â†’ x âˆˆ a â€¢ A :=
  forall_congr' fun _x =>
    âŸ¨fun âŸ¨r, hr, hxâŸ© => âŸ¨r, hr.le, fun a ha => hx a ha.leâŸ©, fun âŸ¨r, hr, hxâŸ© =>
      âŸ¨r + 1, add_pos_of_nonneg_of_pos hr zero_lt_one, fun _a ha =>
        hx ((lt_add_of_pos_right r zero_lt_one).trans_le ha)âŸ©âŸ©
#align absorbent_iff_nonneg_lt absorbent_iff_nonneg_lt

theorem Absorbent.absorbs_finite {s : Set E} (hs : Absorbent ğ•œ s) {v : Set E} (hv : v.Finite) :
    Absorbs ğ•œ s v := by
  rw [â† Set.biUnion_of_singleton v]
  exact hv.absorbs_iUnion.mpr fun _ _ => hs.absorbs
#align absorbent.absorbs_finite Absorbent.absorbs_finite

variable (ğ•œ)

/-- A set `A` is balanced if `a â€¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def Balanced (A : Set E) :=
  âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ A âŠ† A
#align balanced Balanced

variable {ğ•œ}

theorem balanced_iff_smul_mem : Balanced ğ•œ s â†” âˆ€ â¦ƒa : ğ•œâ¦„, â€–aâ€– â‰¤ 1 â†’ âˆ€ â¦ƒx : Eâ¦„, x âˆˆ s â†’ a â€¢ x âˆˆ s :=
  forallâ‚‚_congr fun _a _ha => smul_set_subset_iff
#align balanced_iff_smul_mem balanced_iff_smul_mem

alias balanced_iff_smul_mem â†” Balanced.smul_mem _
#align balanced.smul_mem Balanced.smul_mem

@[simp]
theorem balanced_empty : Balanced ğ•œ (âˆ… : Set E) := fun _ _ => by rw [smul_set_empty]
#align balanced_empty balanced_empty

@[simp]
theorem balanced_univ : Balanced ğ•œ (univ : Set E) := fun _a _ha => subset_univ _
#align balanced_univ balanced_univ

theorem Balanced.union (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆª B) := fun _a ha =>
  smul_set_union.subset.trans <| union_subset_union (hA _ ha) <| hB _ ha
#align balanced.union Balanced.union

theorem Balanced.inter (hA : Balanced ğ•œ A) (hB : Balanced ğ•œ B) : Balanced ğ•œ (A âˆ© B) := fun _a ha =>
  smul_set_inter_subset.trans <| inter_subset_inter (hA _ ha) <| hB _ ha
#align balanced.inter Balanced.inter

theorem balanced_iUnion {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹ƒ i, f i) :=
  fun _a ha => (smul_set_Union _ _).subset.trans <| iUnion_mono fun _ => h _ _ ha
#align balanced_Union balanced_iUnion

theorem balanced_iUnionâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹ƒ (i) (j), f i j) :=
  balanced_iUnion fun _ => balanced_iUnion <| h _
#align balanced_Unionâ‚‚ balanced_iUnionâ‚‚

theorem balanced_iInter {f : Î¹ â†’ Set E} (h : âˆ€ i, Balanced ğ•œ (f i)) : Balanced ğ•œ (â‹‚ i, f i) :=
  fun _a ha => (smul_set_iInter_subset _ _).trans <| iInter_mono fun _ => h _ _ ha
#align balanced_Inter balanced_iInter

theorem balanced_iInterâ‚‚ {f : âˆ€ i, Îº i â†’ Set E} (h : âˆ€ i j, Balanced ğ•œ (f i j)) :
    Balanced ğ•œ (â‹‚ (i) (j), f i j) :=
  balanced_iInter fun _ => balanced_iInter <| h _
#align balanced_Interâ‚‚ balanced_iInterâ‚‚

variable [SMul ğ• E] [SMulCommClass ğ•œ ğ• E]

theorem Balanced.smul (a : ğ•) (hs : Balanced ğ•œ s) : Balanced ğ•œ (a â€¢ s) := fun _b hb =>
  (smul_comm _ _ _).subset.trans <| smul_set_mono <| hs _ hb
#align balanced.smul Balanced.smul

end SMul

section Module

variable [AddCommGroup E] [Module ğ•œ E] {s sâ‚ sâ‚‚ t tâ‚ tâ‚‚ : Set E}

theorem Absorbs.neg : Absorbs ğ•œ s t â†’ Absorbs ğ•œ (-s) (-t) :=
  Exists.imp fun _r =>
    And.imp_right <| forallâ‚‚_imp fun _ _ h => (neg_subset_neg.2 h).trans (smul_set_neg _ _).superset
#align absorbs.neg Absorbs.neg

theorem Balanced.neg : Balanced ğ•œ s â†’ Balanced ğ•œ (-s) :=
  forallâ‚‚_imp fun _ _ h => (smul_set_neg _ _).subset.trans <| neg_subset_neg.2 h
#align balanced.neg Balanced.neg

theorem Absorbs.add : Absorbs ğ•œ sâ‚ tâ‚ â†’ Absorbs ğ•œ sâ‚‚ tâ‚‚ â†’ Absorbs ğ•œ (sâ‚ + sâ‚‚) (tâ‚ + tâ‚‚) :=
  fun âŸ¨râ‚, hrâ‚, hâ‚âŸ© âŸ¨râ‚‚, _hrâ‚‚, hâ‚‚âŸ© =>
  âŸ¨max râ‚ râ‚‚, lt_max_of_lt_left hrâ‚, fun _a ha =>
    (add_subset_add (hâ‚ _ <| le_of_max_le_left ha) <| hâ‚‚ _ <| le_of_max_le_right ha).trans
      (smul_add _ _ _).supersetâŸ©
#align absorbs.add Absorbs.add

theorem Balanced.add (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s + t) := fun _a ha =>
  (smul_add _ _ _).subset.trans <| add_subset_add (hs _ ha) <| ht _ ha
#align balanced.add Balanced.add

theorem Absorbs.sub (hâ‚ : Absorbs ğ•œ sâ‚ tâ‚) (hâ‚‚ : Absorbs ğ•œ sâ‚‚ tâ‚‚) :
    Absorbs ğ•œ (sâ‚ - sâ‚‚) (tâ‚ - tâ‚‚) := by
  simp_rw [sub_eq_add_neg]
  exact hâ‚.add hâ‚‚.neg
#align absorbs.sub Absorbs.sub

theorem Balanced.sub (hs : Balanced ğ•œ s) (ht : Balanced ğ•œ t) : Balanced ğ•œ (s - t) := by
  simp_rw [sub_eq_add_neg]
  exact hs.add ht.neg
#align balanced.sub Balanced.sub

theorem balanced_zero : Balanced ğ•œ (0 : Set E) := fun _a _ha => (smul_zero _).subset
#align balanced_zero balanced_zero

end Module

end SeminormedRing

section NormedField

variable [NormedField ğ•œ] [NormedRing ğ•] [NormedSpace ğ•œ ğ•] [AddCommGroup E] [Module ğ•œ E]
  [SMulWithZero ğ• E] [IsScalarTower ğ•œ ğ• E] {s t u v A B : Set E} {x : E} {a b : ğ•œ}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
theorem Balanced.smul_mono (hs : Balanced ğ• s) {a : ğ•} {b : ğ•œ} (h : â€–aâ€– â‰¤ â€–bâ€–) : a â€¢ s âŠ† b â€¢ s := by
  obtain rfl | hb := eq_or_ne b 0
  Â· rw [norm_zero] at h
    rw [norm_eq_zero.1 (h.antisymm <| norm_nonneg _)]
    obtain rfl | h := s.eq_empty_or_nonempty
    Â· simp_rw [smul_set_empty]; rfl
    Â· simp_rw [zero_smul_set h]; rfl
  rintro _ âŸ¨x, hx, rflâŸ©
  refine' âŸ¨bâ»Â¹ â€¢ a â€¢ x, _, smul_inv_smulâ‚€ hb _âŸ©
  rw [â† smul_assoc]
  refine' hs _ _ (smul_mem_smul_set hx)
  rw [norm_smul, norm_inv, â† div_eq_inv_mul]
  exact div_le_one_of_le h (norm_nonneg _)
#align balanced.smul_mono Balanced.smul_mono

/-- A balanced set absorbs itself. -/
theorem Balanced.absorbs_self (hA : Balanced ğ•œ A) : Absorbs ğ•œ A A := by
  refine' âŸ¨1, zero_lt_one, fun a ha x hx => _âŸ©
  rw [mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.1 <| zero_lt_one.trans_le ha)]
  refine' hA aâ»Â¹ _ (smul_mem_smul_set hx)
  rw [norm_inv]
  exact inv_le_one ha
#align balanced.absorbs_self Balanced.absorbs_self

theorem Balanced.subset_smul (hA : Balanced ğ•œ A) (ha : 1 â‰¤ â€–aâ€–) : A âŠ† a â€¢ A := by
  refine' (subset_set_smul_iffâ‚€ _).2 (hA aâ»Â¹ _)
  Â· rintro rfl
    rw [norm_zero] at ha
    exact zero_lt_one.not_le ha
  Â· rw [norm_inv]
    exact inv_le_one ha
#align balanced.subset_smul Balanced.subset_smul

theorem Balanced.smul_eq (hA : Balanced ğ•œ A) (ha : â€–aâ€– = 1) : a â€¢ A = A :=
  (hA _ ha.le).antisymm <| hA.subset_smul ha.ge
#align balanced.smul_eq Balanced.smul_eq

theorem Balanced.mem_smul_iff (hs : Balanced ğ•œ s) (h : â€–aâ€– = â€–bâ€–) : a â€¢ x âˆˆ s â†” b â€¢ x âˆˆ s := by
  obtain rfl | hb := eq_or_ne b 0
  Â· rw [norm_zero, norm_eq_zero] at h
    rw [h]
  have ha : a â‰  0 := norm_ne_zero_iff.1 (ne_of_eq_of_ne h <| norm_ne_zero_iff.2 hb)
  constructor <;> intro h' <;> [rw [â† inv_mul_cancel_rightâ‚€ ha b];
      rw [â† inv_mul_cancel_rightâ‚€ hb a]] <;>
    Â· rw [â† smul_eq_mul, smul_assoc]
      refine' hs.smul_mem _ h'
      simp [â† h, ha]
#align balanced.mem_smul_iff Balanced.mem_smul_iff

theorem Balanced.neg_mem_iff (hs : Balanced ğ•œ s) : -x âˆˆ s â†” x âˆˆ s := by
  convert hs.mem_smul_iff (x := x) (norm_neg 1) using 0;
  simp only [neg_smul, one_smul ğ•œ x]
#align balanced.neg_mem_iff Balanced.neg_mem_iff

theorem Absorbs.inter (hs : Absorbs ğ•œ s u) (ht : Absorbs ğ•œ t u) : Absorbs ğ•œ (s âˆ© t) u := by
  obtain âŸ¨a, ha, hsâŸ© := hs
  obtain âŸ¨b, _hb, htâŸ© := ht
  have h : 0 < max a b := lt_max_of_lt_left ha
  refine' âŸ¨max a b, lt_max_of_lt_left ha, fun c hc => _âŸ©
  rw [smul_set_interâ‚€ (norm_pos_iff.1 <| h.trans_le hc)]
  exact subset_inter (hs _ <| le_of_max_le_left hc) (ht _ <| le_of_max_le_right hc)
#align absorbs.inter Absorbs.inter

@[simp]
theorem absorbs_inter : Absorbs ğ•œ (s âˆ© t) u â†” Absorbs ğ•œ s u âˆ§ Absorbs ğ•œ t u :=
  âŸ¨fun h => âŸ¨h.mono_left <| inter_subset_left _ _, h.mono_left <| inter_subset_right _ _âŸ©, fun h =>
    h.1.inter h.2âŸ©
#align absorbs_inter absorbs_inter

theorem absorbent_univ : Absorbent ğ•œ (univ : Set E) := by
  refine' fun x => âŸ¨1, zero_lt_one, fun a ha => _âŸ©
  rw [smul_set_univâ‚€ (norm_pos_iff.1 <| zero_lt_one.trans_le ha)]
  exact trivial
#align absorbent_univ absorbent_univ

variable [TopologicalSpace E] [ContinuousSMul ğ•œ E]

/-- Every neighbourhood of the origin is absorbent. -/
theorem absorbent_nhds_zero (hA : A âˆˆ ğ“ (0 : E)) : Absorbent ğ•œ A := by
  intro x
  obtain âŸ¨w, hwâ‚, hwâ‚‚, hwâ‚ƒâŸ© := mem_nhds_iff.mp hA
  have hc : Continuous fun t : ğ•œ => t â€¢ x := continuous_id.smul continuous_const
  obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© :=
    Metric.isOpen_iff.mp (hwâ‚‚.preimage hc) 0 (by rwa [mem_preimage, zero_smul])
  have hrâ‚ƒ := inv_pos.mpr (half_pos hrâ‚)
  refine' âŸ¨(r / 2)â»Â¹, hrâ‚ƒ, fun a haâ‚ => _âŸ©
  have haâ‚‚ : 0 < â€–aâ€– := hrâ‚ƒ.trans_le haâ‚
  refine' (mem_smul_set_iff_inv_smul_memâ‚€ (norm_pos_iff.mp haâ‚‚) _ _).2 (hwâ‚ <| hrâ‚‚ _)
  rw [Metric.mem_ball, dist_zero_right, norm_inv]
  calc
    â€–aâ€–â»Â¹ â‰¤ r / 2 := (inv_le (half_pos hrâ‚) haâ‚‚).mp haâ‚
    _ < r := half_lt_self hrâ‚
#align absorbent_nhds_zero absorbent_nhds_zero

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
theorem balanced_zero_union_interior (hA : Balanced ğ•œ A) :
    Balanced ğ•œ ((0 : Set E) âˆª interior A) := by
  intro a ha
  obtain rfl | h := eq_or_ne a 0
  Â· rw [zero_smul_set]
    exacts [subset_union_left _ _, âŸ¨0, Or.inl rflâŸ©]
  Â· rw [â† image_smul, image_union]
    apply union_subset_union
    Â· rw [image_zero, smul_zero]
      rfl
    Â· calc
        a â€¢ interior A âŠ† interior (a â€¢ A) := (isOpenMap_smulâ‚€ h).image_interior_subset A
        _ âŠ† interior A := interior_mono (hA _ ha)
#align balanced_zero_union_interior balanced_zero_union_interior

/-- The interior of a balanced set is balanced if it contains the origin. -/
theorem Balanced.interior (hA : Balanced ğ•œ A) (h : (0 : E) âˆˆ interior A) :
    Balanced ğ•œ (interior A) := by
  rw [â† union_eq_self_of_subset_left (singleton_subset_iff.2 h)]
  exact balanced_zero_union_interior hA
#align balanced.interior Balanced.interior

theorem Balanced.closure (hA : Balanced ğ•œ A) : Balanced ğ•œ (closure A) := fun _a ha =>
  (image_closure_subset_closure_image <| continuous_id.const_smul _).trans <|
    closure_mono <| hA _ ha
#align balanced.closure Balanced.closure

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] {s : Set E}

theorem absorbs_zero_iff : Absorbs ğ•œ s 0 â†” (0 : E) âˆˆ s := by
  refine' âŸ¨_, fun h => âŸ¨1, zero_lt_one, fun a _ => zero_subset.2 <| zero_mem_smul_set hâŸ©âŸ©
  rintro âŸ¨r, hr, hâŸ©
  obtain âŸ¨a, haâŸ© := NormedSpace.exists_lt_norm ğ•œ ğ•œ r
  have := h _ ha.le
  rwa [zero_subset, zero_mem_smul_set_iff] at this
  exact norm_ne_zero_iff.1 (hr.trans ha).ne'
#align absorbs_zero_iff absorbs_zero_iff

theorem Absorbent.zero_mem (hs : Absorbent ğ•œ s) : (0 : E) âˆˆ s :=
  absorbs_zero_iff.1 <| absorbent_iff_forall_absorbs_singleton.1 hs _
#align absorbent.zero_mem Absorbent.zero_mem

variable [Module â„ E] [SMulCommClass â„ ğ•œ E]

theorem balanced_convexHull_of_balanced (hs : Balanced ğ•œ s) : Balanced ğ•œ (convexHull â„ s) := by
  suffices Convex â„ { x | âˆ€ a : ğ•œ, â€–aâ€– â‰¤ 1 â†’ a â€¢ x âˆˆ convexHull â„ s } by
    rw [balanced_iff_smul_mem] at hs âŠ¢
    refine' fun a ha x hx => convexHull_min _ this hx a ha
    exact fun y hy a ha => subset_convexHull â„ s (hs ha hy)
  intro x hx y hy u v hu hv huv a ha
  simp only [smul_add, â† smul_comm]
  exact convex_convexHull â„ s (hx a ha) (hy a ha) hu hv huv
#align balanced_convex_hull_of_balanced balanced_convexHull_of_balanced

end NontriviallyNormedField

section Real

variable [AddCommGroup E] [Module â„ E] {s : Set E}

theorem balanced_iff_neg_mem (hs : Convex â„ s) : Balanced â„ s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ -x âˆˆ s := by
  refine' âŸ¨fun h x => h.neg_mem_iff.2, fun h a ha => smul_set_subset_iff.2 fun x hx => _âŸ©
  rw [Real.norm_eq_abs, abs_le] at ha
  rw [show a = -((1 - a) / 2) + (a - -1) / 2 by ring, add_smul, neg_smul, â† smul_neg]
  exact
    hs (h hx) hx (div_nonneg (sub_nonneg_of_le ha.2) zero_le_two)
      (div_nonneg (sub_nonneg_of_le ha.1) zero_le_two) (by ring)
#align balanced_iff_neg_mem balanced_iff_neg_mem

end Real
