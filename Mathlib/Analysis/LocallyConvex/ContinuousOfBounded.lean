/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.LocallyConvex.Bounded
import Mathlib.Analysis.RCLike.Basic

#align_import analysis.locally_convex.continuous_of_bounded from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Continuity and Von Neumann boundedness

This files proves that for `E` and `F` two topological vector spaces over `â„` or `â„‚`,
if `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous
(this is `LinearMap.continuous_of_locally_bounded`).

We keep this file separate from `Analysis/LocallyConvex/Bounded` in order not to import
`Analysis/NormedSpace/RCLike` there, because defining the strong topology on the space of
continuous linear maps will require importing `Analysis/LocallyConvex/Bounded` in
`Analysis/NormedSpace/OperatorNorm`.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

-/


open TopologicalSpace Bornology Filter Topology Pointwise

variable {ğ•œ ğ•œ' E F : Type*}
variable [AddCommGroup E] [UniformSpace E] [UniformAddGroup E]
variable [AddCommGroup F] [UniformSpace F]

section NontriviallyNormedField

variable [UniformAddGroup F]
variable [NontriviallyNormedField ğ•œ] [Module ğ•œ E] [Module ğ•œ F] [ContinuousSMul ğ•œ E]

/-- Construct a continuous linear map from a linear map `f : E â†’â‚—[ğ•œ] F` and the existence of a
neighborhood of zero that gets mapped into a bounded set in `F`. -/
def LinearMap.clmOfExistsBoundedImage (f : E â†’â‚—[ğ•œ] F)
    (h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ (f '' V)) : E â†’L[ğ•œ] F :=
  âŸ¨f, by
    -- It suffices to show that `f` is continuous at `0`.
    refine continuous_of_continuousAt_zero f ?_
    rw [continuousAt_def, f.map_zero]
    intro U hU
    -- Continuity means that `U âˆˆ ğ“ 0` implies that `f â»Â¹' U âˆˆ ğ“ 0`.
    rcases h with âŸ¨V, hV, hâŸ©
    rcases (h hU).exists_pos with âŸ¨r, hr, hâŸ©
    rcases NormedField.exists_lt_norm ğ•œ r with âŸ¨x, hxâŸ©
    specialize h x hx.le
    -- After unfolding all the definitions, we know that `f '' V âŠ† x â€¢ U`. We use this to show the
    -- inclusion `xâ»Â¹ â€¢ V âŠ† fâ»Â¹' U`.
    have x_ne := norm_pos_iff.mp (hr.trans hx)
    have : xâ»Â¹ â€¢ V âŠ† f â»Â¹' U :=
      calc
        xâ»Â¹ â€¢ V âŠ† xâ»Â¹ â€¢ f â»Â¹' (f '' V) := Set.smul_set_mono (Set.subset_preimage_image (â‡‘f) V)
        _ âŠ† xâ»Â¹ â€¢ f â»Â¹' (x â€¢ U) := Set.smul_set_mono (Set.preimage_mono h)
        _ = f â»Â¹' (xâ»Â¹ â€¢ x â€¢ U) := by
          ext
          simp only [Set.mem_inv_smul_set_iffâ‚€ x_ne, Set.mem_preimage, LinearMap.map_smul]
        _ âŠ† f â»Â¹' U := by rw [inv_smul_smulâ‚€ x_ne _]
    -- Using this inclusion, it suffices to show that `xâ»Â¹ â€¢ V` is in `ğ“ 0`, which is trivial.
    refine mem_of_superset ?_ this
    convert set_smul_mem_nhds_smul hV (inv_ne_zero x_ne)
    exact (smul_zero _).symmâŸ©
#align linear_map.clm_of_exists_bounded_image LinearMap.clmOfExistsBoundedImage

theorem LinearMap.clmOfExistsBoundedImage_coe {f : E â†’â‚—[ğ•œ] F}
    {h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ (f '' V)} :
    (f.clmOfExistsBoundedImage h : E â†’â‚—[ğ•œ] F) = f :=
  rfl
#align linear_map.clm_of_exists_bounded_image_coe LinearMap.clmOfExistsBoundedImage_coe

@[simp]
theorem LinearMap.clmOfExistsBoundedImage_apply {f : E â†’â‚—[ğ•œ] F}
    {h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ (f '' V)} {x : E} :
    f.clmOfExistsBoundedImage h x = f x :=
  rfl
#align linear_map.clm_of_exists_bounded_image_apply LinearMap.clmOfExistsBoundedImage_apply

end NontriviallyNormedField

section RCLike

open TopologicalSpace Bornology

variable [FirstCountableTopology E]
variable [RCLike ğ•œ] [Module ğ•œ E] [ContinuousSMul ğ•œ E]
variable [RCLike ğ•œ'] [Module ğ•œ' F] [ContinuousSMul ğ•œ' F]
variable {Ïƒ : ğ•œ â†’+* ğ•œ'}

theorem LinearMap.continuousAt_zero_of_locally_bounded (f : E â†’â‚›â‚—[Ïƒ] F)
    (hf : âˆ€ s, IsVonNBounded ğ•œ s â†’ IsVonNBounded ğ•œ' (f '' s)) : ContinuousAt f 0 := by
  -- Assume that f is not continuous at 0
  by_contra h
  -- We use a decreasing balanced basis for 0 : E and a balanced basis for 0 : F
  -- and reformulate non-continuity in terms of these bases
  rcases (nhds_basis_balanced ğ•œ E).exists_antitone_subbasis with âŸ¨b, bE1, bEâŸ©
  simp only [_root_.id] at bE
  have bE' : (ğ“ (0 : E)).HasBasis (fun x : â„• => x â‰  0) fun n : â„• => (n : ğ•œ)â»Â¹ â€¢ b n := by
    refine bE.1.to_hasBasis ?_ ?_
    Â· intro n _
      use n + 1
      simp only [Ne, Nat.succ_ne_zero, not_false_iff, Nat.cast_add, Nat.cast_one, true_and_iff]
      -- `b (n + 1) âŠ† b n` follows from `Antitone`.
      have h : b (n + 1) âŠ† b n := bE.2 (by simp)
      refine _root_.trans ?_ h
      rintro y âŸ¨x, hx, hyâŸ©
      -- Since `b (n + 1)` is balanced `(n+1)â»Â¹ b (n + 1) âŠ† b (n + 1)`
      rw [â† hy]
      refine (bE1 (n + 1)).2.smul_mem ?_ hx
      have h' : 0 < (n : â„) + 1 := n.cast_add_one_pos
      rw [norm_inv, â† Nat.cast_one, â† Nat.cast_add, RCLike.norm_natCast, Nat.cast_add,
        Nat.cast_one, inv_le h' zero_lt_one]
      simp
    intro n hn
    -- The converse direction follows from continuity of the scalar multiplication
    have hcont : ContinuousAt (fun x : E => (n : ğ•œ) â€¢ x) 0 :=
      (continuous_const_smul (n : ğ•œ)).continuousAt
    simp only [ContinuousAt, map_zero, smul_zero] at hcont
    rw [bE.1.tendsto_left_iff] at hcont
    rcases hcont (b n) (bE1 n).1 with âŸ¨i, _, hiâŸ©
    refine âŸ¨i, trivial, fun x hx => âŸ¨(n : ğ•œ) â€¢ x, hi hx, ?_âŸ©âŸ©
    simp [â† mul_smul, hn]
  rw [ContinuousAt, map_zero, bE'.tendsto_iff (nhds_basis_balanced ğ•œ' F)] at h
  push_neg at h
  rcases h with âŸ¨V, âŸ¨hV, -âŸ©, hâŸ©
  simp only [_root_.id, forall_true_left] at h
  -- There exists `u : â„• â†’ E` such that for all `n : â„•` we have `u n âˆˆ nâ»Â¹ â€¢ b n` and `f (u n) âˆ‰ V`
  choose! u hu hu' using h
  -- The sequence `(fun n â†¦ n â€¢ u n)` converges to `0`
  have h_tendsto : Tendsto (fun n : â„• => (n : ğ•œ) â€¢ u n) atTop (ğ“ (0 : E)) := by
    apply bE.tendsto
    intro n
    by_cases h : n = 0
    Â· rw [h, Nat.cast_zero, zero_smul]
      exact mem_of_mem_nhds (bE.1.mem_of_mem <| by trivial)
    rcases hu n h with âŸ¨y, hy, hu1âŸ©
    convert hy
    rw [â† hu1, â† mul_smul]
    simp only [h, mul_inv_cancel, Ne, Nat.cast_eq_zero, not_false_iff, one_smul]
  -- The image `(fun n â†¦ n â€¢ u n)` is von Neumann bounded:
  have h_bounded : IsVonNBounded ğ•œ (Set.range fun n : â„• => (n : ğ•œ) â€¢ u n) :=
    h_tendsto.cauchySeq.totallyBounded_range.isVonNBounded ğ•œ
  -- Since `range u` is bounded, `V` absorbs it
  rcases (hf _ h_bounded hV).exists_pos with âŸ¨r, hr, h'âŸ©
  cases' exists_nat_gt r with n hn
  -- We now find a contradiction between `f (u n) âˆ‰ V` and the absorbing property
  have h1 : r â‰¤ â€–(n : ğ•œ')â€– := by
    rw [RCLike.norm_natCast]
    exact hn.le
  have hn' : 0 < â€–(n : ğ•œ')â€– := lt_of_lt_of_le hr h1
  rw [norm_pos_iff, Ne, Nat.cast_eq_zero] at hn'
  have h'' : f (u n) âˆˆ V := by
    simp only [Set.image_subset_iff] at h'
    specialize h' (n : ğ•œ') h1 (Set.mem_range_self n)
    simp only [Set.mem_preimage, LinearMap.map_smulâ‚›â‚—, map_natCast] at h'
    rcases h' with âŸ¨y, hy, h'âŸ©
    apply_fun fun y : F => (n : ğ•œ')â»Â¹ â€¢ y at h'
    simp only [hn', inv_smul_smulâ‚€, Ne, Nat.cast_eq_zero, not_false_iff] at h'
    rwa [â† h']
  exact hu' n hn' h''
#align linear_map.continuous_at_zero_of_locally_bounded LinearMap.continuousAt_zero_of_locally_bounded

/-- If `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous. -/
theorem LinearMap.continuous_of_locally_bounded [UniformAddGroup F] (f : E â†’â‚›â‚—[Ïƒ] F)
    (hf : âˆ€ s, IsVonNBounded ğ•œ s â†’ IsVonNBounded ğ•œ' (f '' s)) : Continuous f :=
  (uniformContinuous_of_continuousAt_zero f <| f.continuousAt_zero_of_locally_bounded hf).continuous
#align linear_map.continuous_of_locally_bounded LinearMap.continuous_of_locally_bounded

end RCLike
