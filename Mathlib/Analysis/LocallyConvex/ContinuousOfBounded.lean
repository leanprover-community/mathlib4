/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.LocallyConvex.Bounded
public import Mathlib.Analysis.SpecificLimits.Normed

/-!
# Continuity and Von Neumann boundedness

This file proves that for two topological vector spaces `E` and `F` over nontrivially normed fields,
if `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous
(this is `LinearMap.continuous_of_locally_bounded`).

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

-/

@[expose] public section


open TopologicalSpace Bornology Filter Topology Pointwise

variable {ğ•œ ğ•œ' E F : Type*}
variable [AddCommGroup E] [TopologicalSpace E] [IsTopologicalAddGroup E]
variable [AddCommGroup F] [TopologicalSpace F]

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [Module ğ•œ E] [ContinuousSMul ğ•œ E]
variable [NormedField ğ•œ'] [Module ğ•œ' F]
variable {Ïƒ : ğ•œ â†’+* ğ•œ'} [RingHomIsometric Ïƒ]

/-- Construct a continuous linear map from a linear map `f : E â†’â‚›â‚—[Ïƒ] F` and the existence of a
neighborhood of zero that gets mapped into a bounded set in `F`. -/
def LinearMap.clmOfExistsBoundedImage [IsTopologicalAddGroup F] (f : E â†’â‚›â‚—[Ïƒ] F)
    (h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ' (f '' V)) : E â†’SL[Ïƒ] F :=
  âŸ¨f, by
    -- It suffices to show that `f` is continuous at `0`.
    refine continuous_of_continuousAt_zero f ?_
    rw [continuousAt_def, f.map_zero]
    intro U hU
    -- Continuity means that `U âˆˆ ğ“ 0` implies that `f â»Â¹' U âˆˆ ğ“ 0`.
    rcases h with âŸ¨V, hV, hâŸ©
    rcases (h hU).exists_pos with âŸ¨r, hr, hâŸ©
    rcases NormedField.exists_lt_norm ğ•œ r with âŸ¨x, hxâŸ©
    specialize h (Ïƒ x) (by simpa using hx.le)
    -- After unfolding all the definitions, we know that `f '' V âŠ† Ïƒ x â€¢ U`. We use this to show the
    -- inclusion `xâ»Â¹ â€¢ V âŠ† fâ»Â¹' U`.
    have x_ne := norm_pos_iff.mp (hr.trans hx)
    have : xâ»Â¹ â€¢ V âŠ† f â»Â¹' U :=
      calc
        xâ»Â¹ â€¢ V âŠ† xâ»Â¹ â€¢ f â»Â¹' (f '' V) := Set.smul_set_mono (Set.subset_preimage_image (â‡‘f) V)
        _ âŠ† xâ»Â¹ â€¢ f â»Â¹' (Ïƒ x â€¢ U) := Set.smul_set_mono (Set.preimage_mono h)
        _ = f â»Â¹' U := by rw [x_ne.isUnit.preimage_smul_setâ‚›â‚— _, inv_smul_smulâ‚€ x_ne _]
    -- Using this inclusion, it suffices to show that `xâ»Â¹ â€¢ V` is in `ğ“ 0`, which is trivial.
    refine mem_of_superset ?_ this
    rwa [set_smul_mem_nhds_zero_iff (inv_ne_zero x_ne)]âŸ©

theorem LinearMap.clmOfExistsBoundedImage_coe [IsTopologicalAddGroup F] {f : E â†’â‚›â‚—[Ïƒ] F}
    {h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ' (f '' V)} :
    (f.clmOfExistsBoundedImage h : E â†’â‚›â‚—[Ïƒ] F) = f :=
  rfl

@[simp]
theorem LinearMap.clmOfExistsBoundedImage_apply [IsTopologicalAddGroup F] {f : E â†’â‚›â‚—[Ïƒ] F}
    {h : âˆƒ V âˆˆ ğ“ (0 : E), Bornology.IsVonNBounded ğ•œ' (f '' V)} {x : E} :
    f.clmOfExistsBoundedImage h x = f x :=
  rfl

variable [FirstCountableTopology E]

theorem LinearMap.continuousAt_zero_of_locally_bounded (f : E â†’â‚›â‚—[Ïƒ] F)
    (hf : âˆ€ s, IsVonNBounded ğ•œ s â†’ IsVonNBounded ğ•œ' (f '' s)) : ContinuousAt f 0 := by
  -- We pick `c : ğ•œ` nonzero of norm `< 1`.
  obtain âŸ¨c, hc0, hc1âŸ© := NormedField.exists_norm_lt_one ğ•œ
  have c_ne := norm_pos_iff.mp hc0
  -- We use a fast decreasing balanced basis for 0 : E, and reformulate continuity in terms of
  -- this basis
  rcases (nhds_basis_balanced ğ•œ E).exists_antitone_subbasis with âŸ¨b, bE1, bEâŸ©
  simp only [_root_.id] at bE
  have bE' : (ğ“ (0 : E)).HasBasis (fun _ â†¦ True) (fun n : â„• â†¦ (c ^ n) â€¢ b n) := by
    refine bE.1.to_hasBasis' ?_ ?_
    Â· intro n _
      use n
      exact âŸ¨trivial, (bE1 n).2 _ (by grw [norm_pow, hc1, one_pow])âŸ©
    Â· intro n _
      simpa using smul_mem_nhds_smulâ‚€ (pow_ne_zero n c_ne) (bE1 n).1
  simp_rw [ContinuousAt, map_zero, bE'.tendsto_left_iff, true_and, Set.MapsTo]
  -- Assume that f is not continuous at 0
  by_contra! h
  rcases h with âŸ¨V, hV, hâŸ©
  -- There exists `u : â„• â†’ E` such that for all `n : â„•` we have `u n âˆˆ c ^ n â€¢ b n` and
  -- `f (u n) âˆ‰ V`, with `V` some neighborhood of `0` in `F`.
  choose! u hu hu' using h
  -- The sequence `fun n â†¦ c ^ (-n) â€¢ u n` converges to `0`
  have h_tendsto : Tendsto (fun n : â„• => (c ^ n)â»Â¹ â€¢ u n) atTop (ğ“ (0 : E)) := by
    apply bE.tendsto
    intro n
    simpa only [Set.mem_smul_set_iff_inv_smul_memâ‚€ (pow_ne_zero n c_ne)] using hu n
  -- The range of `fun n â†¦ c ^ (-n) â€¢ u n` is von Neumann bounded
  have h_bounded : IsVonNBounded ğ•œ (Set.range fun n : â„• => (c ^ n)â»Â¹ â€¢ u n) :=
    h_tendsto.isVonNBounded_range ğ•œ
  -- Hence, by assumption, the range of `fun n â†¦ (Ïƒ c) ^ (-n) â€¢ f (u n)` is von Neumann bounded
  specialize hf _ h_bounded
  simp only [â† Set.range_comp', LinearMap.map_smulâ‚›â‚—, map_invâ‚€, map_pow] at hf
  -- Since `fun n â†¦ (Ïƒ c) ^ n` tends to zero, this implies that `f âˆ˜ u` converges to zero.
  have : Tendsto (f âˆ˜ u) atTop (ğ“ 0) := by
    have : Tendsto (fun n â†¦ Ïƒ c ^ n) atTop (ğ“ 0) :=
      tendsto_pow_atTop_nhds_zero_of_norm_lt_one (by simpa using hc1)
    have := hf.smul_tendsto_zero (.of_forall fun n â†¦ Set.mem_range_self n) this
    exact this.congr fun n â†¦ by simp [c_ne]
  -- But this is a contradiction.
  refine frequently_false (atTop : Filter â„•) <| Eventually.frequently ?_
  filter_upwards [this.eventually_mem hV] using hu'

/-- If `E` is first countable, then every locally bounded linear map `E â†’â‚›â‚—[Ïƒ] F` is continuous. -/
theorem LinearMap.continuous_of_locally_bounded [IsTopologicalAddGroup F] (f : E â†’â‚›â‚—[Ïƒ] F)
    (hf : âˆ€ s, IsVonNBounded ğ•œ s â†’ IsVonNBounded ğ•œ' (f '' s)) : Continuous f :=
  continuous_of_continuousAt_zero f (f.continuousAt_zero_of_locally_bounded hf)

end NontriviallyNormedField
