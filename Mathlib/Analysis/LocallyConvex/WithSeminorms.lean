/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Anatole Dedecker
-/
import Mathlib.Analysis.LocallyConvex.Bounded
import Mathlib.Analysis.Seminorm
import Mathlib.Topology.Algebra.Equicontinuity
import Mathlib.Topology.MetricSpace.Equicontinuity
import Mathlib.Topology.Algebra.FilterBasis
import Mathlib.Topology.Algebra.Module.LocallyConvex

#align_import analysis.locally_convex.with_seminorms from "leanprover-community/mathlib"@"b31173ee05c911d61ad6a05bd2196835c932e0ec"

/-!
# Topology induced by a family of seminorms

## Main definitions

* `SeminormFamily.basisSets`: The set of open seminorm balls for a family of seminorms.
* `SeminormFamily.moduleFilterBasis`: A module filter basis formed by the open balls.
* `Seminorm.IsBounded`: A linear map `f : E â†’â‚—[ğ•œ] F` is bounded iff every seminorm in `F` can be
bounded by a finite number of seminorms in `E`.

## Main statements

* `WithSeminorms.toLocallyConvexSpace`: A space equipped with a family of seminorms is locally
convex.
* `WithSeminorms.firstCountable`: A space is first countable if it's topology is induced by a
countable family of seminorms.

## Continuity of semilinear maps

If `E` and `F` are topological vector space with the topology induced by a family of seminorms, then
we have a direct method to prove that a linear map is continuous:
* `Seminorm.continuous_from_bounded`: A bounded linear map `f : E â†’â‚—[ğ•œ] F` is continuous.

If the topology of a space `E` is induced by a family of seminorms, then we can characterize von
Neumann boundedness in terms of that seminorm family. Together with
`LinearMap.continuous_of_locally_bounded` this gives general criterion for continuity.

* `WithSeminorms.isVonNBounded_iff_finset_seminorm_bounded`
* `WithSeminorms.isVonNBounded_iff_seminorm_bounded`
* `WithSeminorms.image_isVonNBounded_iff_finset_seminorm_bounded`
* `WithSeminorms.image_isVonNBounded_iff_seminorm_bounded`

## Tags

seminorm, locally convex
-/


open NormedField Set Seminorm TopologicalSpace Filter List

open NNReal Pointwise Topology Uniformity

variable {ğ•œ ğ•œâ‚‚ ğ• ğ•â‚‚ E F G Î¹ Î¹' : Type*}

section FilterBasis

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable (ğ•œ E Î¹)

/-- An abbreviation for indexed families of seminorms. This is mainly to allow for dot-notation. -/
abbrev SeminormFamily :=
  Î¹ â†’ Seminorm ğ•œ E
#align seminorm_family SeminormFamily

variable {ğ•œ E Î¹}

namespace SeminormFamily

/-- The sets of a filter basis for the neighborhood filter of 0. -/
def basisSets (p : SeminormFamily ğ•œ E Î¹) : Set (Set E) :=
  â‹ƒ (s : Finset Î¹) (r) (_ : 0 < r), singleton (ball (s.sup p) (0 : E) r)
#align seminorm_family.basis_sets SeminormFamily.basisSets

variable (p : SeminormFamily ğ•œ E Î¹)

theorem basisSets_iff {U : Set E} :
    U âˆˆ p.basisSets â†” âˆƒ (i : Finset Î¹) (r : â„), 0 < r âˆ§ U = ball (i.sup p) 0 r := by
  simp only [basisSets, mem_iUnion, exists_prop, mem_singleton_iff]
#align seminorm_family.basis_sets_iff SeminormFamily.basisSets_iff

theorem basisSets_mem (i : Finset Î¹) {r : â„} (hr : 0 < r) : (i.sup p).ball 0 r âˆˆ p.basisSets :=
  (basisSets_iff _).mpr âŸ¨i, _, hr, rflâŸ©
#align seminorm_family.basis_sets_mem SeminormFamily.basisSets_mem

theorem basisSets_singleton_mem (i : Î¹) {r : â„} (hr : 0 < r) : (p i).ball 0 r âˆˆ p.basisSets :=
  (basisSets_iff _).mpr âŸ¨{i}, _, hr, by rw [Finset.sup_singleton]âŸ©
#align seminorm_family.basis_sets_singleton_mem SeminormFamily.basisSets_singleton_mem

theorem basisSets_nonempty [Nonempty Î¹] : p.basisSets.Nonempty := by
  let i := Classical.arbitrary Î¹
  refine nonempty_def.mpr âŸ¨(p i).ball 0 1, ?_âŸ©
  exact p.basisSets_singleton_mem i zero_lt_one
#align seminorm_family.basis_sets_nonempty SeminormFamily.basisSets_nonempty

theorem basisSets_intersect (U V : Set E) (hU : U âˆˆ p.basisSets) (hV : V âˆˆ p.basisSets) :
    âˆƒ z âˆˆ p.basisSets, z âŠ† U âˆ© V := by
  classical
    rcases p.basisSets_iff.mp hU with âŸ¨s, râ‚, hrâ‚, hUâŸ©
    rcases p.basisSets_iff.mp hV with âŸ¨t, râ‚‚, hrâ‚‚, hVâŸ©
    use ((s âˆª t).sup p).ball 0 (min râ‚ râ‚‚)
    refine âŸ¨p.basisSets_mem (s âˆª t) (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©), ?_âŸ©
    rw [hU, hV, ball_finset_sup_eq_iInter _ _ _ (lt_min_iff.mpr âŸ¨hrâ‚, hrâ‚‚âŸ©),
      ball_finset_sup_eq_iInter _ _ _ hrâ‚, ball_finset_sup_eq_iInter _ _ _ hrâ‚‚]
    exact
      Set.subset_inter
        (Set.iInterâ‚‚_mono' fun i hi =>
          âŸ¨i, Finset.subset_union_left hi, ball_mono <| min_le_left _ _âŸ©)
        (Set.iInterâ‚‚_mono' fun i hi =>
          âŸ¨i, Finset.subset_union_right hi, ball_mono <| min_le_right _ _âŸ©)
#align seminorm_family.basis_sets_intersect SeminormFamily.basisSets_intersect

theorem basisSets_zero (U) (hU : U âˆˆ p.basisSets) : (0 : E) âˆˆ U := by
  rcases p.basisSets_iff.mp hU with âŸ¨Î¹', r, hr, hUâŸ©
  rw [hU, mem_ball_zero, map_zero]
  exact hr
#align seminorm_family.basis_sets_zero SeminormFamily.basisSets_zero

theorem basisSets_add (U) (hU : U âˆˆ p.basisSets) :
    âˆƒ V âˆˆ p.basisSets, V + V âŠ† U := by
  rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©
  use (s.sup p).ball 0 (r / 2)
  refine âŸ¨p.basisSets_mem s (div_pos hr zero_lt_two), ?_âŸ©
  refine Set.Subset.trans (ball_add_ball_subset (s.sup p) (r / 2) (r / 2) 0 0) ?_
  rw [hU, add_zero, add_halves']
#align seminorm_family.basis_sets_add SeminormFamily.basisSets_add

theorem basisSets_neg (U) (hU' : U âˆˆ p.basisSets) :
    âˆƒ V âˆˆ p.basisSets, V âŠ† (fun x : E => -x) â»Â¹' U := by
  rcases p.basisSets_iff.mp hU' with âŸ¨s, r, _, hUâŸ©
  rw [hU, neg_preimage, neg_ball (s.sup p), neg_zero]
  exact âŸ¨U, hU', Eq.subset hUâŸ©
#align seminorm_family.basis_sets_neg SeminormFamily.basisSets_neg

/-- The `addGroupFilterBasis` induced by the filter basis `Seminorm.basisSets`. -/
protected def addGroupFilterBasis [Nonempty Î¹] : AddGroupFilterBasis E :=
  addGroupFilterBasisOfComm p.basisSets p.basisSets_nonempty p.basisSets_intersect p.basisSets_zero
    p.basisSets_add p.basisSets_neg
#align seminorm_family.add_group_filter_basis SeminormFamily.addGroupFilterBasis

theorem basisSets_smul_right (v : E) (U : Set E) (hU : U âˆˆ p.basisSets) :
    âˆ€á¶  x : ğ•œ in ğ“ 0, x â€¢ v âˆˆ U := by
  rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©
  rw [hU, Filter.eventually_iff]
  simp_rw [(s.sup p).mem_ball_zero, map_smul_eq_mul]
  by_cases h : 0 < (s.sup p) v
  Â· simp_rw [(lt_div_iff h).symm]
    rw [â† _root_.ball_zero_eq]
    exact Metric.ball_mem_nhds 0 (div_pos hr h)
  simp_rw [le_antisymm (not_lt.mp h) (apply_nonneg _ v), mul_zero, hr]
  exact IsOpen.mem_nhds isOpen_univ (mem_univ 0)
#align seminorm_family.basis_sets_smul_right SeminormFamily.basisSets_smul_right

variable [Nonempty Î¹]

theorem basisSets_smul (U) (hU : U âˆˆ p.basisSets) :
    âˆƒ V âˆˆ ğ“ (0 : ğ•œ), âˆƒ W âˆˆ p.addGroupFilterBasis.sets, V â€¢ W âŠ† U := by
  rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©
  refine âŸ¨Metric.ball 0 âˆšr, Metric.ball_mem_nhds 0 (Real.sqrt_pos.mpr hr), ?_âŸ©
  refine âŸ¨(s.sup p).ball 0 âˆšr, p.basisSets_mem s (Real.sqrt_pos.mpr hr), ?_âŸ©
  refine Set.Subset.trans (ball_smul_ball (s.sup p) âˆšr âˆšr) ?_
  rw [hU, Real.mul_self_sqrt (le_of_lt hr)]
#align seminorm_family.basis_sets_smul SeminormFamily.basisSets_smul

theorem basisSets_smul_left (x : ğ•œ) (U : Set E) (hU : U âˆˆ p.basisSets) :
    âˆƒ V âˆˆ p.addGroupFilterBasis.sets, V âŠ† (fun y : E => x â€¢ y) â»Â¹' U := by
  rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©
  rw [hU]
  by_cases h : x â‰  0
  Â· rw [(s.sup p).smul_ball_preimage 0 r x h, smul_zero]
    use (s.sup p).ball 0 (r / â€–xâ€–)
    exact âŸ¨p.basisSets_mem s (div_pos hr (norm_pos_iff.mpr h)), Subset.rflâŸ©
  refine âŸ¨(s.sup p).ball 0 r, p.basisSets_mem s hr, ?_âŸ©
  simp only [not_ne_iff.mp h, Set.subset_def, mem_ball_zero, hr, mem_univ, map_zero, imp_true_iff,
    preimage_const_of_mem, zero_smul]
#align seminorm_family.basis_sets_smul_left SeminormFamily.basisSets_smul_left

/-- The `moduleFilterBasis` induced by the filter basis `Seminorm.basisSets`. -/
protected def moduleFilterBasis : ModuleFilterBasis ğ•œ E where
  toAddGroupFilterBasis := p.addGroupFilterBasis
  smul' := p.basisSets_smul _
  smul_left' := p.basisSets_smul_left
  smul_right' := p.basisSets_smul_right
#align seminorm_family.module_filter_basis SeminormFamily.moduleFilterBasis

theorem filter_eq_iInf (p : SeminormFamily ğ•œ E Î¹) :
    p.moduleFilterBasis.toFilterBasis.filter = â¨… i, (ğ“ 0).comap (p i) := by
  refine le_antisymm (le_iInf fun i => ?_) ?_
  Â· rw [p.moduleFilterBasis.toFilterBasis.hasBasis.le_basis_iff
        (Metric.nhds_basis_ball.comap _)]
    intro Îµ hÎµ
    refine âŸ¨(p i).ball 0 Îµ, ?_, ?_âŸ©
    Â· rw [â† (Finset.sup_singleton : _ = p i)]
      exact p.basisSets_mem {i} hÎµ
    Â· rw [id, (p i).ball_zero_eq_preimage_ball]
  Â· rw [p.moduleFilterBasis.toFilterBasis.hasBasis.ge_iff]
    rintro U (hU : U âˆˆ p.basisSets)
    rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, rflâŸ©
    rw [id, Seminorm.ball_finset_sup_eq_iInter _ _ _ hr, s.iInter_mem_sets]
    exact fun i _ =>
      Filter.mem_iInf_of_mem i
        âŸ¨Metric.ball 0 r, Metric.ball_mem_nhds 0 hr,
          Eq.subset (p i).ball_zero_eq_preimage_ball.symmâŸ©
#align seminorm_family.filter_eq_infi SeminormFamily.filter_eq_iInf

end SeminormFamily

end FilterBasis

section Bounded

namespace Seminorm

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [NormedField ğ•œâ‚‚] [AddCommGroup F] [Module ğ•œâ‚‚ F]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]

-- Todo: This should be phrased entirely in terms of the von Neumann bornology.
/-- The proposition that a linear map is bounded between spaces with families of seminorms. -/
def IsBounded (p : Î¹ â†’ Seminorm ğ•œ E) (q : Î¹' â†’ Seminorm ğ•œâ‚‚ F) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : Prop :=
  âˆ€ i, âˆƒ s : Finset Î¹, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ s.sup p
#align seminorm.is_bounded Seminorm.IsBounded

theorem isBounded_const (Î¹' : Type*) [Nonempty Î¹'] {p : Î¹ â†’ Seminorm ğ•œ E} {q : Seminorm ğ•œâ‚‚ F}
    (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    IsBounded p (fun _ : Î¹' => q) f â†” âˆƒ (s : Finset Î¹) (C : â„â‰¥0), q.comp f â‰¤ C â€¢ s.sup p := by
  simp only [IsBounded, forall_const]
#align seminorm.is_bounded_const Seminorm.isBounded_const

theorem const_isBounded (Î¹ : Type*) [Nonempty Î¹] {p : Seminorm ğ•œ E} {q : Î¹' â†’ Seminorm ğ•œâ‚‚ F}
    (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : IsBounded (fun _ : Î¹ => p) q f â†” âˆ€ i, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ p := by
  constructor <;> intro h i
  Â· rcases h i with âŸ¨s, C, hâŸ©
    exact âŸ¨C, le_trans h (smul_le_smul (Finset.sup_le fun _ _ => le_rfl) le_rfl)âŸ©
  use {Classical.arbitrary Î¹}
  simp only [h, Finset.sup_singleton]
#align seminorm.const_is_bounded Seminorm.const_isBounded

theorem isBounded_sup {p : Î¹ â†’ Seminorm ğ•œ E} {q : Î¹' â†’ Seminorm ğ•œâ‚‚ F} {f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F}
    (hf : IsBounded p q f) (s' : Finset Î¹') :
    âˆƒ (C : â„â‰¥0) (s : Finset Î¹), (s'.sup q).comp f â‰¤ C â€¢ s.sup p := by
  classical
    obtain rfl | _ := s'.eq_empty_or_nonempty
    Â· exact âŸ¨1, âˆ…, by simp [Seminorm.bot_eq_zero]âŸ©
    choose fâ‚› fC hf using hf
    use s'.card â€¢ s'.sup fC, Finset.biUnion s' fâ‚›
    have hs : âˆ€ i : Î¹', i âˆˆ s' â†’ (q i).comp f â‰¤ s'.sup fC â€¢ (Finset.biUnion s' fâ‚›).sup p := by
      intro i hi
      refine (hf i).trans (smul_le_smul ?_ (Finset.le_sup hi))
      exact Finset.sup_mono (Finset.subset_biUnion_of_mem fâ‚› hi)
    refine (comp_mono f (finset_sup_le_sum q s')).trans ?_
    simp_rw [â† pullback_apply, map_sum, pullback_apply]
    refine (Finset.sum_le_sum hs).trans ?_
    rw [Finset.sum_const, smul_assoc]
#align seminorm.is_bounded_sup Seminorm.isBounded_sup

end Seminorm

end Bounded

section Topology

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Nonempty Î¹]

/-- The proposition that the topology of `E` is induced by a family of seminorms `p`. -/
structure WithSeminorms (p : SeminormFamily ğ•œ E Î¹) [topology : TopologicalSpace E] : Prop where
  topology_eq_withSeminorms : topology = p.moduleFilterBasis.topology
#align with_seminorms WithSeminorms

theorem WithSeminorms.withSeminorms_eq {p : SeminormFamily ğ•œ E Î¹} [t : TopologicalSpace E]
    (hp : WithSeminorms p) : t = p.moduleFilterBasis.topology :=
  hp.1
#align with_seminorms.with_seminorms_eq WithSeminorms.withSeminorms_eq

variable [TopologicalSpace E]
variable {p : SeminormFamily ğ•œ E Î¹}

theorem WithSeminorms.topologicalAddGroup (hp : WithSeminorms p) : TopologicalAddGroup E := by
  rw [hp.withSeminorms_eq]
  exact AddGroupFilterBasis.isTopologicalAddGroup _
#align with_seminorms.topological_add_group WithSeminorms.topologicalAddGroup

theorem WithSeminorms.continuousSMul (hp : WithSeminorms p) : ContinuousSMul ğ•œ E := by
  rw [hp.withSeminorms_eq]
  exact ModuleFilterBasis.continuousSMul _

theorem WithSeminorms.hasBasis (hp : WithSeminorms p) :
    (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ p.basisSets) id := by
  rw [congr_fun (congr_arg (@nhds E) hp.1) 0]
  exact AddGroupFilterBasis.nhds_zero_hasBasis _
#align with_seminorms.has_basis WithSeminorms.hasBasis

theorem WithSeminorms.hasBasis_zero_ball (hp : WithSeminorms p) :
    (ğ“ (0 : E)).HasBasis
    (fun sr : Finset Î¹ Ã— â„ => 0 < sr.2) fun sr => (sr.1.sup p).ball 0 sr.2 := by
  refine âŸ¨fun V => ?_âŸ©
  simp only [hp.hasBasis.mem_iff, SeminormFamily.basisSets_iff, Prod.exists]
  constructor
  Â· rintro âŸ¨-, âŸ¨s, r, hr, rflâŸ©, hVâŸ©
    exact âŸ¨s, r, hr, hVâŸ©
  Â· rintro âŸ¨s, r, hr, hVâŸ©
    exact âŸ¨_, âŸ¨s, r, hr, rflâŸ©, hVâŸ©
#align with_seminorms.has_basis_zero_ball WithSeminorms.hasBasis_zero_ball

theorem WithSeminorms.hasBasis_ball (hp : WithSeminorms p) {x : E} :
    (ğ“ (x : E)).HasBasis
    (fun sr : Finset Î¹ Ã— â„ => 0 < sr.2) fun sr => (sr.1.sup p).ball x sr.2 := by
  have : TopologicalAddGroup E := hp.topologicalAddGroup
  rw [â† map_add_left_nhds_zero]
  convert hp.hasBasis_zero_ball.map (x + Â·) using 1
  ext sr : 1
  -- Porting note: extra type ascriptions needed on `0`
  have : (sr.fst.sup p).ball (x +áµ¥ (0 : E)) sr.snd = x +áµ¥ (sr.fst.sup p).ball 0 sr.snd :=
    Eq.symm (Seminorm.vadd_ball (sr.fst.sup p))
  rwa [vadd_eq_add, add_zero] at this
#align with_seminorms.has_basis_ball WithSeminorms.hasBasis_ball

/-- The `x`-neighbourhoods of a space whose topology is induced by a family of seminorms
are exactly the sets which contain seminorm balls around `x`. -/
theorem WithSeminorms.mem_nhds_iff (hp : WithSeminorms p) (x : E) (U : Set E) :
    U âˆˆ ğ“ x â†” âˆƒ s : Finset Î¹, âˆƒ r > 0, (s.sup p).ball x r âŠ† U := by
  rw [hp.hasBasis_ball.mem_iff, Prod.exists]
#align with_seminorms.mem_nhds_iff WithSeminorms.mem_nhds_iff

/-- The open sets of a space whose topology is induced by a family of seminorms
are exactly the sets which contain seminorm balls around all of their points. -/
theorem WithSeminorms.isOpen_iff_mem_balls (hp : WithSeminorms p) (U : Set E) :
    IsOpen U â†” âˆ€ x âˆˆ U, âˆƒ s : Finset Î¹, âˆƒ r > 0, (s.sup p).ball x r âŠ† U := by
  simp_rw [â† WithSeminorms.mem_nhds_iff hp _ U, isOpen_iff_mem_nhds]
#align with_seminorms.is_open_iff_mem_balls WithSeminorms.isOpen_iff_mem_balls

/- Note that through the following lemmas, one also immediately has that separating families
of seminorms induce Tâ‚‚ and Tâ‚ƒ topologies by `TopologicalAddGroup.t2Space`
and `TopologicalAddGroup.t3Space` -/
/-- A separating family of seminorms induces a Tâ‚ topology. -/
theorem WithSeminorms.T1_of_separating (hp : WithSeminorms p)
    (h : âˆ€ x, x â‰  0 â†’ âˆƒ i, p i x â‰  0) : T1Space E := by
  have := hp.topologicalAddGroup
  refine TopologicalAddGroup.t1Space _ ?_
  rw [â† isOpen_compl_iff, hp.isOpen_iff_mem_balls]
  rintro x (hx : x â‰  0)
  cases' h x hx with i pi_nonzero
  refine âŸ¨{i}, p i x, by positivity, subset_compl_singleton_iff.mpr ?_âŸ©
  rw [Finset.sup_singleton, mem_ball, zero_sub, map_neg_eq_map, not_lt]
#align with_seminorms.t1_of_separating WithSeminorms.T1_of_separating

/-- A family of seminorms inducing a Tâ‚ topology is separating. -/
theorem WithSeminorms.separating_of_T1 [T1Space E] (hp : WithSeminorms p) (x : E) (hx : x â‰  0) :
    âˆƒ i, p i x â‰  0 := by
  have := ((t1Space_TFAE E).out 0 9).mp (inferInstanceAs <| T1Space E)
  by_contra! h
  refine hx (this ?_)
  rw [hp.hasBasis_zero_ball.specializes_iff]
  rintro âŸ¨s, râŸ© (hr : 0 < r)
  simp only [ball_finset_sup_eq_iInter _ _ _ hr, mem_iInterâ‚‚, mem_ball_zero, h, hr, forall_true_iff]
#align with_seminorms.separating_of_t1 WithSeminorms.separating_of_T1

/-- A family of seminorms is separating iff it induces a Tâ‚ topology. -/
theorem WithSeminorms.separating_iff_T1 (hp : WithSeminorms p) :
    (âˆ€ x, x â‰  0 â†’ âˆƒ i, p i x â‰  0) â†” T1Space E := by
  refine âŸ¨WithSeminorms.T1_of_separating hp, ?_âŸ©
  intro
  exact WithSeminorms.separating_of_T1 hp
#align with_seminorms.separating_iff_t1 WithSeminorms.separating_iff_T1

end Topology

section Tendsto

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Nonempty Î¹] [TopologicalSpace E]
variable {p : SeminormFamily ğ•œ E Î¹}

/-- Convergence along filters for `WithSeminorms`.

Variant with `Finset.sup`. -/
theorem WithSeminorms.tendsto_nhds' (hp : WithSeminorms p) (u : F â†’ E) {f : Filter F} (yâ‚€ : E) :
    Filter.Tendsto u f (ğ“ yâ‚€) â†”
    âˆ€ (s : Finset Î¹) (Îµ), 0 < Îµ â†’ âˆ€á¶  x in f, s.sup p (u x - yâ‚€) < Îµ := by
  simp [hp.hasBasis_ball.tendsto_right_iff]
#align with_seminorms.tendsto_nhds' WithSeminorms.tendsto_nhds'

/-- Convergence along filters for `WithSeminorms`. -/
theorem WithSeminorms.tendsto_nhds (hp : WithSeminorms p) (u : F â†’ E) {f : Filter F} (yâ‚€ : E) :
    Filter.Tendsto u f (ğ“ yâ‚€) â†” âˆ€ i Îµ, 0 < Îµ â†’ âˆ€á¶  x in f, p i (u x - yâ‚€) < Îµ := by
  rw [hp.tendsto_nhds' u yâ‚€]
  exact
    âŸ¨fun h i => by simpa only [Finset.sup_singleton] using h {i}, fun h s Îµ hÎµ =>
      (s.eventually_all.2 fun i _ => h i Îµ hÎµ).mono fun _ => finset_sup_apply_lt hÎµâŸ©
#align with_seminorms.tendsto_nhds WithSeminorms.tendsto_nhds

variable [SemilatticeSup F] [Nonempty F]

/-- Limit `â†’ âˆ` for `WithSeminorms`. -/
theorem WithSeminorms.tendsto_nhds_atTop (hp : WithSeminorms p) (u : F â†’ E) (yâ‚€ : E) :
    Filter.Tendsto u Filter.atTop (ğ“ yâ‚€) â†”
    âˆ€ i Îµ, 0 < Îµ â†’ âˆƒ xâ‚€, âˆ€ x, xâ‚€ â‰¤ x â†’ p i (u x - yâ‚€) < Îµ := by
  rw [hp.tendsto_nhds u yâ‚€]
  exact forallâ‚ƒ_congr fun _ _ _ => Filter.eventually_atTop
#align with_seminorms.tendsto_nhds_at_top WithSeminorms.tendsto_nhds_atTop

end Tendsto

section TopologicalAddGroup

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [Nonempty Î¹]

section TopologicalSpace

variable [t : TopologicalSpace E]

theorem SeminormFamily.withSeminorms_of_nhds [TopologicalAddGroup E] (p : SeminormFamily ğ•œ E Î¹)
    (h : ğ“ (0 : E) = p.moduleFilterBasis.toFilterBasis.filter) : WithSeminorms p := by
  refine
    âŸ¨TopologicalAddGroup.ext inferInstance p.addGroupFilterBasis.isTopologicalAddGroup ?_âŸ©
  rw [AddGroupFilterBasis.nhds_zero_eq]
  exact h
#align seminorm_family.with_seminorms_of_nhds SeminormFamily.withSeminorms_of_nhds

theorem SeminormFamily.withSeminorms_of_hasBasis [TopologicalAddGroup E] (p : SeminormFamily ğ•œ E Î¹)
    (h : (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ p.basisSets) id) : WithSeminorms p :=
  p.withSeminorms_of_nhds <|
    Filter.HasBasis.eq_of_same_basis h p.addGroupFilterBasis.toFilterBasis.hasBasis
#align seminorm_family.with_seminorms_of_has_basis SeminormFamily.withSeminorms_of_hasBasis

theorem SeminormFamily.withSeminorms_iff_nhds_eq_iInf [TopologicalAddGroup E]
    (p : SeminormFamily ğ•œ E Î¹) : WithSeminorms p â†” (ğ“ (0 : E)) = â¨… i, (ğ“ 0).comap (p i) := by
  rw [â† p.filter_eq_iInf]
  refine âŸ¨fun h => ?_, p.withSeminorms_of_nhdsâŸ©
  rw [h.topology_eq_withSeminorms]
  exact AddGroupFilterBasis.nhds_zero_eq _
#align seminorm_family.with_seminorms_iff_nhds_eq_infi SeminormFamily.withSeminorms_iff_nhds_eq_iInf

/-- The topology induced by a family of seminorms is exactly the infimum of the ones induced by
each seminorm individually. We express this as a characterization of `WithSeminorms p`. -/
theorem SeminormFamily.withSeminorms_iff_topologicalSpace_eq_iInf [TopologicalAddGroup E]
    (p : SeminormFamily ğ•œ E Î¹) :
    WithSeminorms p â†”
      t = â¨… i, (p i).toSeminormedAddCommGroup.toUniformSpace.toTopologicalSpace := by
  rw [p.withSeminorms_iff_nhds_eq_iInf,
    TopologicalAddGroup.ext_iff inferInstance (topologicalAddGroup_iInf fun i => inferInstance),
    nhds_iInf]
  congrm _ = â¨… i, ?_
  exact @comap_norm_nhds_zero _ (p i).toSeminormedAddGroup
#align seminorm_family.with_seminorms_iff_topological_space_eq_infi SeminormFamily.withSeminorms_iff_topologicalSpace_eq_iInf

theorem WithSeminorms.continuous_seminorm {p : SeminormFamily ğ•œ E Î¹} (hp : WithSeminorms p)
    (i : Î¹) : Continuous (p i) := by
  have := hp.topologicalAddGroup
  rw [p.withSeminorms_iff_topologicalSpace_eq_iInf.mp hp]
  exact continuous_iInf_dom (@continuous_norm _ (p i).toSeminormedAddGroup)
#align with_seminorms.continuous_seminorm WithSeminorms.continuous_seminorm

end TopologicalSpace

/-- The uniform structure induced by a family of seminorms is exactly the infimum of the ones
induced by each seminorm individually. We express this as a characterization of
`WithSeminorms p`. -/
theorem SeminormFamily.withSeminorms_iff_uniformSpace_eq_iInf [u : UniformSpace E]
    [UniformAddGroup E] (p : SeminormFamily ğ•œ E Î¹) :
    WithSeminorms p â†” u = â¨… i, (p i).toSeminormedAddCommGroup.toUniformSpace := by
  rw [p.withSeminorms_iff_nhds_eq_iInf,
    UniformAddGroup.ext_iff inferInstance (uniformAddGroup_iInf fun i => inferInstance),
    UniformSpace.toTopologicalSpace_iInf, nhds_iInf]
  congrm _ = â¨… i, ?_
  exact @comap_norm_nhds_zero _ (p i).toAddGroupSeminorm.toSeminormedAddGroup
#align seminorm_family.with_seminorms_iff_uniform_space_eq_infi SeminormFamily.withSeminorms_iff_uniformSpace_eq_iInf

end TopologicalAddGroup

section NormedSpace

/-- The topology of a `NormedSpace ğ•œ E` is induced by the seminorm `normSeminorm ğ•œ E`. -/
theorem norm_withSeminorms (ğ•œ E) [NormedField ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] :
    WithSeminorms fun _ : Fin 1 => normSeminorm ğ•œ E := by
  let p : SeminormFamily ğ•œ E (Fin 1) := fun _ => normSeminorm ğ•œ E
  refine
    âŸ¨SeminormedAddCommGroup.toTopologicalAddGroup.ext
        p.addGroupFilterBasis.isTopologicalAddGroup ?_âŸ©
  refine Filter.HasBasis.eq_of_same_basis Metric.nhds_basis_ball ?_
  rw [â† ball_normSeminorm ğ•œ E]
  refine
    Filter.HasBasis.to_hasBasis p.addGroupFilterBasis.nhds_zero_hasBasis ?_ fun r hr =>
      âŸ¨(normSeminorm ğ•œ E).ball 0 r, p.basisSets_singleton_mem 0 hr, rfl.subsetâŸ©
  rintro U (hU : U âˆˆ p.basisSets)
  rcases p.basisSets_iff.mp hU with âŸ¨s, r, hr, hUâŸ©
  use r, hr
  rw [hU, id]
  by_cases h : s.Nonempty
  Â· rw [Finset.sup_const h]
  rw [Finset.not_nonempty_iff_eq_empty.mp h, Finset.sup_empty, ball_bot _ hr]
  exact Set.subset_univ _
#align norm_with_seminorms norm_withSeminorms

end NormedSpace

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Nonempty Î¹]
variable {p : SeminormFamily ğ•œ E Î¹}
variable [TopologicalSpace E]

theorem WithSeminorms.isVonNBounded_iff_finset_seminorm_bounded {s : Set E} (hp : WithSeminorms p) :
    Bornology.IsVonNBounded ğ•œ s â†” âˆ€ I : Finset Î¹, âˆƒ r > 0, âˆ€ x âˆˆ s, I.sup p x < r := by
  rw [hp.hasBasis.isVonNBounded_iff]
  constructor
  Â· intro h I
    simp only [id] at h
    specialize h ((I.sup p).ball 0 1) (p.basisSets_mem I zero_lt_one)
    rcases h.exists_pos with âŸ¨r, hr, hâŸ©
    cases' NormedField.exists_lt_norm ğ•œ r with a ha
    specialize h a (le_of_lt ha)
    rw [Seminorm.smul_ball_zero (norm_pos_iff.1 <| hr.trans ha), mul_one] at h
    refine âŸ¨â€–aâ€–, lt_trans hr ha, ?_âŸ©
    intro x hx
    specialize h hx
    exact (Finset.sup I p).mem_ball_zero.mp h
  intro h s' hs'
  rcases p.basisSets_iff.mp hs' with âŸ¨I, r, hr, hs'âŸ©
  rw [id, hs']
  rcases h I with âŸ¨r', _, h'âŸ©
  simp_rw [â† (I.sup p).mem_ball_zero] at h'
  refine Absorbs.mono_right ?_ h'
  exact (Finset.sup I p).ball_zero_absorbs_ball_zero hr

set_option linter.uppercaseLean3 false in
#align with_seminorms.is_vonN_bounded_iff_finset_seminorm_bounded WithSeminorms.isVonNBounded_iff_finset_seminorm_bounded

theorem WithSeminorms.image_isVonNBounded_iff_finset_seminorm_bounded (f : G â†’ E) {s : Set G}
    (hp : WithSeminorms p) :
    Bornology.IsVonNBounded ğ•œ (f '' s) â†”
      âˆ€ I : Finset Î¹, âˆƒ r > 0, âˆ€ x âˆˆ s, I.sup p (f x) < r := by
  simp_rw [hp.isVonNBounded_iff_finset_seminorm_bounded, Set.forall_mem_image]

set_option linter.uppercaseLean3 false in
#align with_seminorms.image_is_vonN_bounded_iff_finset_seminorm_bounded WithSeminorms.image_isVonNBounded_iff_finset_seminorm_bounded

theorem WithSeminorms.isVonNBounded_iff_seminorm_bounded {s : Set E} (hp : WithSeminorms p) :
    Bornology.IsVonNBounded ğ•œ s â†” âˆ€ i : Î¹, âˆƒ r > 0, âˆ€ x âˆˆ s, p i x < r := by
  rw [hp.isVonNBounded_iff_finset_seminorm_bounded]
  constructor
  Â· intro hI i
    convert hI {i}
    rw [Finset.sup_singleton]
  intro hi I
  by_cases hI : I.Nonempty
  Â· choose r hr h using hi
    have h' : 0 < I.sup' hI r := by
      rcases hI with âŸ¨i, hiâŸ©
      exact lt_of_lt_of_le (hr i) (Finset.le_sup' r hi)
    refine âŸ¨I.sup' hI r, h', fun x hx => finset_sup_apply_lt h' fun i hi => ?_âŸ©
    refine lt_of_lt_of_le (h i x hx) ?_
    simp only [Finset.le_sup'_iff, exists_prop]
    exact âŸ¨i, hi, (Eq.refl _).leâŸ©
  simp only [Finset.not_nonempty_iff_eq_empty.mp hI, Finset.sup_empty, coe_bot, Pi.zero_apply,
    exists_prop]
  exact âŸ¨1, zero_lt_one, fun _ _ => zero_lt_oneâŸ©

set_option linter.uppercaseLean3 false in
#align with_seminorms.is_vonN_bounded_iff_seminorm_bounded WithSeminorms.isVonNBounded_iff_seminorm_bounded

theorem WithSeminorms.image_isVonNBounded_iff_seminorm_bounded (f : G â†’ E) {s : Set G}
    (hp : WithSeminorms p) :
    Bornology.IsVonNBounded ğ•œ (f '' s) â†” âˆ€ i : Î¹, âˆƒ r > 0, âˆ€ x âˆˆ s, p i (f x) < r := by
  simp_rw [hp.isVonNBounded_iff_seminorm_bounded, Set.forall_mem_image]

set_option linter.uppercaseLean3 false in
#align with_seminorms.image_is_vonN_bounded_iff_seminorm_bounded WithSeminorms.image_isVonNBounded_iff_seminorm_bounded

end NontriviallyNormedField

-- TODO: the names in this section are not very predictable
section continuous_of_bounded

namespace Seminorm

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [NormedField ğ•] [Module ğ• E]
variable [NontriviallyNormedField ğ•œâ‚‚] [AddCommGroup F] [Module ğ•œâ‚‚ F]
variable [NormedField ğ•â‚‚] [Module ğ•â‚‚ F]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]
variable {Ï„â‚â‚‚ : ğ• â†’+* ğ•â‚‚} [RingHomIsometric Ï„â‚â‚‚]
variable [Nonempty Î¹] [Nonempty Î¹']

theorem continuous_of_continuous_comp {q : SeminormFamily ğ•â‚‚ F Î¹'} [TopologicalSpace E]
    [TopologicalAddGroup E] [TopologicalSpace F] (hq : WithSeminorms q)
    (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : âˆ€ i, Continuous ((q i).comp f)) : Continuous f := by
  have : TopologicalAddGroup F := hq.topologicalAddGroup
  refine continuous_of_continuousAt_zero f ?_
  simp_rw [ContinuousAt, f.map_zero, q.withSeminorms_iff_nhds_eq_iInf.mp hq, Filter.tendsto_iInf,
    Filter.tendsto_comap_iff]
  intro i
  convert (hf i).continuousAt.tendsto
  exact (map_zero _).symm
#align seminorm.continuous_of_continuous_comp Seminorm.continuous_of_continuous_comp

theorem continuous_iff_continuous_comp {q : SeminormFamily ğ•œâ‚‚ F Î¹'} [TopologicalSpace E]
    [TopologicalAddGroup E] [TopologicalSpace F] (hq : WithSeminorms q) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    Continuous f â†” âˆ€ i, Continuous ((q i).comp f) :=
    -- Porting note: if we *don't* use dot notation for `Continuous.comp`, Lean tries to show
    -- continuity of `((q i).comp f) âˆ˜ id` because it doesn't see that `((q i).comp f)` is
    -- actually a composition of functions.
  âŸ¨fun h i => (hq.continuous_seminorm i).comp h, continuous_of_continuous_comp hq fâŸ©
#align seminorm.continuous_iff_continuous_comp Seminorm.continuous_iff_continuous_comp

theorem continuous_from_bounded {p : SeminormFamily ğ• E Î¹} {q : SeminormFamily ğ•â‚‚ F Î¹'}
    {_ : TopologicalSpace E} (hp : WithSeminorms p) {_ : TopologicalSpace F} (hq : WithSeminorms q)
    (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : Seminorm.IsBounded p q f) : Continuous f := by
  have : TopologicalAddGroup E := hp.topologicalAddGroup
  refine continuous_of_continuous_comp hq _ fun i => ?_
  rcases hf i with âŸ¨s, C, hCâŸ©
  rw [â† Seminorm.finset_sup_smul] at hC
  -- Note: we deduce continuouty of `s.sup (C â€¢ p)` from that of `âˆ‘ i âˆˆ s, C â€¢ p i`.
  -- The reason is that there is no `continuous_finset_sup`, and even if it were we couldn't
  -- really use it since `â„` is not an `OrderBot`.
  refine Seminorm.continuous_of_le ?_ (hC.trans <| Seminorm.finset_sup_le_sum _ _)
  change Continuous (fun x â†¦ Seminorm.coeFnAddMonoidHom _ _ (âˆ‘ i âˆˆ s, C â€¢ p i) x)
  simp_rw [map_sum, Finset.sum_apply]
  exact (continuous_finset_sum _ fun i _ â†¦ (hp.continuous_seminorm i).const_smul (C : â„))
#align seminorm.continuous_from_bounded Seminorm.continuous_from_bounded

theorem cont_withSeminorms_normedSpace (F) [SeminormedAddCommGroup F] [NormedSpace ğ•â‚‚ F]
    [TopologicalSpace E] {p : Î¹ â†’ Seminorm ğ• E} (hp : WithSeminorms p)
    (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : âˆƒ (s : Finset Î¹) (C : â„â‰¥0), (normSeminorm ğ•â‚‚ F).comp f â‰¤ C â€¢ s.sup p) :
    Continuous f := by
  rw [â† Seminorm.isBounded_const (Fin 1)] at hf
  exact continuous_from_bounded hp (norm_withSeminorms ğ•â‚‚ F) f hf
#align seminorm.cont_with_seminorms_normed_space Seminorm.cont_withSeminorms_normedSpace

theorem cont_normedSpace_to_withSeminorms (E) [SeminormedAddCommGroup E] [NormedSpace ğ• E]
    [TopologicalSpace F] {q : Î¹ â†’ Seminorm ğ•â‚‚ F} (hq : WithSeminorms q)
    (f : E â†’â‚›â‚—[Ï„â‚â‚‚] F) (hf : âˆ€ i : Î¹, âˆƒ C : â„â‰¥0, (q i).comp f â‰¤ C â€¢ normSeminorm ğ• E) :
    Continuous f := by
  rw [â† Seminorm.const_isBounded (Fin 1)] at hf
  exact continuous_from_bounded (norm_withSeminorms ğ• E) hq f hf
#align seminorm.cont_normed_space_to_with_seminorms Seminorm.cont_normedSpace_to_withSeminorms

/-- Let `E` and `F` be two topological vector spaces over a `NontriviallyNormedField`, and assume
that the topology of `F` is generated by some family of seminorms `q`. For a family `f` of linear
maps from `E` to `F`, the following are equivalent:
* `f` is equicontinuous at `0`.
* `f` is equicontinuous.
* `f` is uniformly equicontinuous.
* For each `q i`, the family of seminorms `k â†¦ (q i) âˆ˜ (f k)` is bounded by some continuous
  seminorm `p` on `E`.
* For each `q i`, the seminorm `âŠ” k, (q i) âˆ˜ (f k)` is well-defined and continuous.

In particular, if you can determine all continuous seminorms on `E`, that gives you a complete
characterization of equicontinuity for linear maps from `E` to `F`. For example `E` and `F` are
both normed spaces, you get `NormedSpace.equicontinuous_TFAE`. -/
protected theorem _root_.WithSeminorms.equicontinuous_TFAE {Îº : Type*}
    {q : SeminormFamily ğ•œâ‚‚ F Î¹'} [UniformSpace E] [UniformAddGroup E] [u : UniformSpace F]
    [hu : UniformAddGroup F] (hq : WithSeminorms q) [ContinuousSMul ğ•œ E]
    (f : Îº â†’ E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : TFAE
    [ EquicontinuousAt ((â†‘) âˆ˜ f) 0,
      Equicontinuous ((â†‘) âˆ˜ f),
      UniformEquicontinuous ((â†‘) âˆ˜ f),
      âˆ€ i, âˆƒ p : Seminorm ğ•œ E, Continuous p âˆ§ âˆ€ k, (q i).comp (f k) â‰¤ p,
      âˆ€ i, BddAbove (range fun k â†¦ (q i).comp (f k)) âˆ§ Continuous (â¨† k, (q i).comp (f k)) ] := by
  -- We start by reducing to the case where the target is a seminormed space
  rw [q.withSeminorms_iff_uniformSpace_eq_iInf.mp hq, uniformEquicontinuous_iInf_rng,
      equicontinuous_iInf_rng, equicontinuousAt_iInf_rng]
  refine forall_tfae [_, _, _, _, _] fun i â†¦ ?_
  let _ : SeminormedAddCommGroup F := (q i).toSeminormedAddCommGroup
  clear u hu hq
  -- Now we can prove the equivalence in this setting
  simp only [List.map]
  tfae_have 1 â†’ 3
  Â· exact uniformEquicontinuous_of_equicontinuousAt_zero f
  tfae_have 3 â†’ 2
  Â· exact UniformEquicontinuous.equicontinuous
  tfae_have 2 â†’ 1
  Â· exact fun H â†¦ H 0
  tfae_have 3 â†’ 5
  Â· intro H
    have : âˆ€á¶  x in ğ“ 0, âˆ€ k, q i (f k x) â‰¤ 1 := by
      filter_upwards [Metric.equicontinuousAt_iff_right.mp (H.equicontinuous 0) 1 one_pos]
        with x hx k
      simpa using (hx k).le
    have bdd : BddAbove (range fun k â†¦ (q i).comp (f k)) :=
      Seminorm.bddAbove_of_absorbent (absorbent_nhds_zero this)
        (fun x hx â†¦ âŸ¨1, forall_mem_range.mpr hxâŸ©)
    rw [â† Seminorm.coe_iSup_eq bdd]
    refine âŸ¨bdd, Seminorm.continuous' (r := 1) ?_âŸ©
    filter_upwards [this] with x hx
    simpa only [closedBall_iSup bdd _ one_pos, mem_iInter, mem_closedBall_zero] using hx
  tfae_have 5 â†’ 4
  Â· exact fun H â†¦ âŸ¨â¨† k, (q i).comp (f k), Seminorm.coe_iSup_eq H.1 â–¸ H.2, le_ciSup H.1âŸ©
  tfae_have 4 â†’ 1 -- This would work over any `NormedField`
  Â· intro âŸ¨p, hp, hfpâŸ©
    exact Metric.equicontinuousAt_of_continuity_modulus p (map_zero p â–¸ hp.tendsto 0) _ <|
      eventually_of_forall fun x k â†¦ by simpa using hfp k x
  tfae_finish

theorem _root_.WithSeminorms.uniformEquicontinuous_iff_exists_continuous_seminorm {Îº : Type*}
    {q : SeminormFamily ğ•œâ‚‚ F Î¹'} [UniformSpace E] [UniformAddGroup E] [u : UniformSpace F]
    [hu : UniformAddGroup F] (hq : WithSeminorms q) [ContinuousSMul ğ•œ E]
    (f : Îº â†’ E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    UniformEquicontinuous ((â†‘) âˆ˜ f) â†”
    âˆ€ i, âˆƒ p : Seminorm ğ•œ E, Continuous p âˆ§ âˆ€ k, (q i).comp (f k) â‰¤ p :=
  (hq.equicontinuous_TFAE f).out 2 3

theorem _root_.WithSeminorms.uniformEquicontinuous_iff_bddAbove_and_continuous_iSup {Îº : Type*}
    {q : SeminormFamily ğ•œâ‚‚ F Î¹'} [UniformSpace E] [UniformAddGroup E] [u : UniformSpace F]
    [hu : UniformAddGroup F] (hq : WithSeminorms q) [ContinuousSMul ğ•œ E]
    (f : Îº â†’ E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    UniformEquicontinuous ((â†‘) âˆ˜ f) â†” âˆ€ i,
    BddAbove (range fun k â†¦ (q i).comp (f k)) âˆ§
      Continuous (â¨† k, (q i).comp (f k)) :=
  (hq.equicontinuous_TFAE f).out 2 4

end Seminorm

section Congr

namespace WithSeminorms

variable [Nonempty Î¹] [Nonempty Î¹']
variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [NormedField ğ•œâ‚‚] [AddCommGroup F] [Module ğ•œâ‚‚ F]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]

/-- Two families of seminorms `p` and `q` on the same space generate the same topology
if each `p i` is bounded by some `C â€¢ Finset.sup s q` and vice-versa.

We formulate these boundedness assumptions as `Seminorm.IsBounded q p LinearMap.id` (and
vice-versa) to reuse the API. Furthermore, we don't actually state it as an equality of topologies
but as a way to deduce `WithSeminorms q` from `WithSeminorms p`, since this should be more
useful in practice. -/
protected theorem congr {p : SeminormFamily ğ•œ E Î¹} {q : SeminormFamily ğ•œ E Î¹'}
    [t : TopologicalSpace E] (hp : WithSeminorms p) (hpq : Seminorm.IsBounded p q LinearMap.id)
    (hqp : Seminorm.IsBounded q p LinearMap.id) : WithSeminorms q := by
  constructor
  rw [hp.topology_eq_withSeminorms]
  clear hp t
  refine le_antisymm ?_ ?_ <;>
  rw [â† continuous_id_iff_le] <;>
  refine continuous_from_bounded (.mk (topology := _) rfl) (.mk (topology := _) rfl)
    LinearMap.id (by assumption)

protected theorem finset_sups {p : SeminormFamily ğ•œ E Î¹} [TopologicalSpace E]
    (hp : WithSeminorms p) : WithSeminorms (fun s : Finset Î¹ â†¦ s.sup p) := by
  refine hp.congr ?_ ?_
  Â· intro s
    refine âŸ¨s, 1, ?_âŸ©
    rw [one_smul]
    rfl
  Â· intro i
    refine âŸ¨{{i}}, 1, ?_âŸ©
    rw [Finset.sup_singleton, Finset.sup_singleton, one_smul]
    rfl

protected theorem partial_sups [Preorder Î¹] [LocallyFiniteOrderBot Î¹] {p : SeminormFamily ğ•œ E Î¹}
    [TopologicalSpace E] (hp : WithSeminorms p) : WithSeminorms (fun i â†¦ (Finset.Iic i).sup p) := by
  refine hp.congr ?_ ?_
  Â· intro i
    refine âŸ¨Finset.Iic i, 1, ?_âŸ©
    rw [one_smul]
    rfl
  Â· intro i
    refine âŸ¨{i}, 1, ?_âŸ©
    rw [Finset.sup_singleton, one_smul]
    exact (Finset.le_sup (Finset.mem_Iic.mpr le_rfl) : p i â‰¤ (Finset.Iic i).sup p)

protected theorem congr_equiv {p : SeminormFamily ğ•œ E Î¹} [t : TopologicalSpace E]
    (hp : WithSeminorms p) (e : Î¹' â‰ƒ Î¹) : WithSeminorms (p âˆ˜ e) := by
  refine hp.congr ?_ ?_ <;>
  intro i <;>
  [use {e i}, 1; use {e.symm i}, 1] <;>
  simp

end WithSeminorms

end Congr

end continuous_of_bounded

section bounded_of_continuous

namespace Seminorm

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
  [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]
  {p : SeminormFamily ğ•œ E Î¹}

/-- In a semi-`NormedSpace`, a continuous seminorm is zero on elements of norm `0`. -/
lemma map_eq_zero_of_norm_zero (q : Seminorm ğ•œ F)
    (hq : Continuous q) {x : F} (hx : â€–xâ€– = 0) : q x = 0 :=
  (map_zero q) â–¸
    ((specializes_iff_mem_closure.mpr <| mem_closure_zero_iff_norm.mpr hx).map hq).eq.symm

/-- Let `F` be a semi-`NormedSpace` over a `NontriviallyNormedField`, and let `q` be a
seminorm on `F`. If `q` is continuous, then it is uniformly controlled by the norm, that is there
is some `C > 0` such that `âˆ€ x, q x â‰¤ C * â€–xâ€–`.
The continuity ensures boundedness on a ball of some radius `Îµ`. The nontriviality of the
norm is then used to rescale any element into an element of norm in `[Îµ/C, Îµ[`, thus with a
controlled image by `q`. The control of `q` at the original element follows by rescaling. -/
lemma bound_of_continuous_normedSpace (q : Seminorm ğ•œ F)
    (hq : Continuous q) : âˆƒ C, 0 < C âˆ§ (âˆ€ x : F, q x â‰¤ C * â€–xâ€–) := by
  have hq' : Tendsto q (ğ“ 0) (ğ“ 0) := map_zero q â–¸ hq.tendsto 0
  rcases NormedAddCommGroup.nhds_zero_basis_norm_lt.mem_iff.mp (hq' <| Iio_mem_nhds one_pos)
    with âŸ¨Îµ, Îµ_pos, hÎµâŸ©
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  have : 0 < â€–câ€– / Îµ := by positivity
  refine âŸ¨â€–câ€– / Îµ, this, fun x â†¦ ?_âŸ©
  by_cases hx : â€–xâ€– = 0
  Â· rw [hx, mul_zero]
    exact le_of_eq (map_eq_zero_of_norm_zero q hq hx)
  Â· refine (normSeminorm ğ•œ F).bound_of_shell q Îµ_pos hc (fun x hle hlt â†¦ ?_) hx
    refine (le_of_lt <| show q x < _ from hÎµ hlt).trans ?_
    rwa [â† div_le_iff' this, one_div_div]

/-- Let `E` be a topological vector space (over a `NontriviallyNormedField`) whose topology is
generated by some family of seminorms `p`, and let `q` be a seminorm on `E`. If `q` is continuous,
then it is uniformly controlled by *finitely many* seminorms of `p`, that is there
is some finset `s` of the index set and some `C > 0` such that `q â‰¤ C â€¢ s.sup p`. -/
lemma bound_of_continuous [Nonempty Î¹] [t : TopologicalSpace E] (hp : WithSeminorms p)
    (q : Seminorm ğ•œ E) (hq : Continuous q) :
    âˆƒ s : Finset Î¹, âˆƒ C : â„â‰¥0, C â‰  0 âˆ§ q â‰¤ C â€¢ s.sup p := by
  -- The continuity of `q` gives us a finset `s` and a real `Îµ > 0`
  -- such that `hÎµ : (s.sup p).ball 0 Îµ âŠ† q.ball 0 1`.
  rcases hp.hasBasis.mem_iff.mp (ball_mem_nhds hq one_pos) with âŸ¨V, hV, hÎµâŸ©
  rcases p.basisSets_iff.mp hV with âŸ¨s, Îµ, Îµ_pos, rflâŸ©
  -- Now forget that `E` already had a topology and view it as the (semi)normed space
  -- `(E, s.sup p)`.
  clear hp hq t
  let _ : SeminormedAddCommGroup E := (s.sup p).toSeminormedAddCommGroup
  let _ : NormedSpace ğ•œ E := { norm_smul_le := fun a b â†¦ le_of_eq (map_smul_eq_mul (s.sup p) a b) }
  -- The inclusion `hÎµ` tells us exactly that `q` is *still* continuous for this new topology
  have : Continuous q :=
    Seminorm.continuous (r := 1) (mem_of_superset (Metric.ball_mem_nhds _ Îµ_pos) hÎµ)
  -- Hence we can conclude by applying `bound_of_continuous_normedSpace`.
  rcases bound_of_continuous_normedSpace q this with âŸ¨C, C_pos, hCâŸ©
  exact âŸ¨s, âŸ¨C, C_pos.leâŸ©, fun H â†¦ C_pos.ne.symm (congr_arg NNReal.toReal H), hCâŸ©
  -- Note that the key ingredient for this proof is that, by scaling arguments hidden in
  -- `Seminorm.continuous`, we only have to look at the `q`-ball of radius one, and the `s` we get
  -- from that will automatically work for all other radii.

end Seminorm

end bounded_of_continuous

section LocallyConvexSpace

open LocallyConvexSpace

variable [Nonempty Î¹] [NormedField ğ•œ] [NormedSpace â„ ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Module â„ E]
  [IsScalarTower â„ ğ•œ E] [TopologicalSpace E]

theorem WithSeminorms.toLocallyConvexSpace {p : SeminormFamily ğ•œ E Î¹} (hp : WithSeminorms p) :
    LocallyConvexSpace â„ E := by
  have := hp.topologicalAddGroup
  apply ofBasisZero â„ E id fun s => s âˆˆ p.basisSets
  Â· rw [hp.1, AddGroupFilterBasis.nhds_eq _, AddGroupFilterBasis.N_zero]
    exact FilterBasis.hasBasis _
  Â· intro s hs
    change s âˆˆ Set.iUnion _ at hs
    simp_rw [Set.mem_iUnion, Set.mem_singleton_iff] at hs
    rcases hs with âŸ¨I, r, _, rflâŸ©
    exact convex_ball _ _ _
#align with_seminorms.to_locally_convex_space WithSeminorms.toLocallyConvexSpace

end LocallyConvexSpace

section NormedSpace

variable (ğ•œ) [NormedField ğ•œ] [NormedSpace â„ ğ•œ] [SeminormedAddCommGroup E]

/-- Not an instance since `ğ•œ` can't be inferred. See `NormedSpace.toLocallyConvexSpace` for a
slightly weaker instance version. -/
theorem NormedSpace.toLocallyConvexSpace' [NormedSpace ğ•œ E] [Module â„ E] [IsScalarTower â„ ğ•œ E] :
    LocallyConvexSpace â„ E :=
  (norm_withSeminorms ğ•œ E).toLocallyConvexSpace
#align normed_space.to_locally_convex_space' NormedSpace.toLocallyConvexSpace'

/-- See `NormedSpace.toLocallyConvexSpace'` for a slightly stronger version which is not an
instance. -/
instance NormedSpace.toLocallyConvexSpace [NormedSpace â„ E] : LocallyConvexSpace â„ E :=
  NormedSpace.toLocallyConvexSpace' â„
#align normed_space.to_locally_convex_space NormedSpace.toLocallyConvexSpace

end NormedSpace

section TopologicalConstructions

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [NormedField ğ•œâ‚‚] [AddCommGroup F] [Module ğ•œâ‚‚ F]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]

/-- The family of seminorms obtained by composing each seminorm by a linear map. -/
def SeminormFamily.comp (q : SeminormFamily ğ•œâ‚‚ F Î¹) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : SeminormFamily ğ•œ E Î¹ :=
  fun i => (q i).comp f
#align seminorm_family.comp SeminormFamily.comp

theorem SeminormFamily.comp_apply (q : SeminormFamily ğ•œâ‚‚ F Î¹) (i : Î¹) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    q.comp f i = (q i).comp f :=
  rfl
#align seminorm_family.comp_apply SeminormFamily.comp_apply

theorem SeminormFamily.finset_sup_comp (q : SeminormFamily ğ•œâ‚‚ F Î¹) (s : Finset Î¹)
    (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) : (s.sup q).comp f = s.sup (q.comp f) := by
  ext x
  rw [Seminorm.comp_apply, Seminorm.finset_sup_apply, Seminorm.finset_sup_apply]
  rfl
#align seminorm_family.finset_sup_comp SeminormFamily.finset_sup_comp

variable [TopologicalSpace F]

theorem LinearMap.withSeminorms_induced [hÎ¹ : Nonempty Î¹] {q : SeminormFamily ğ•œâ‚‚ F Î¹}
    (hq : WithSeminorms q) (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) :
    WithSeminorms (topology := induced f inferInstance) (q.comp f) := by
  have := hq.topologicalAddGroup
  let _ : TopologicalSpace E := induced f inferInstance
  have : TopologicalAddGroup E := topologicalAddGroup_induced f
  rw [(q.comp f).withSeminorms_iff_nhds_eq_iInf, nhds_induced, map_zero,
    q.withSeminorms_iff_nhds_eq_iInf.mp hq, Filter.comap_iInf]
  refine iInf_congr fun i => ?_
  exact Filter.comap_comap
#align linear_map.with_seminorms_induced LinearMap.withSeminorms_induced

theorem Inducing.withSeminorms [hÎ¹ : Nonempty Î¹] {q : SeminormFamily ğ•œâ‚‚ F Î¹} (hq : WithSeminorms q)
    [TopologicalSpace E] {f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F} (hf : Inducing f) : WithSeminorms (q.comp f) := by
  rw [hf.induced]
  exact f.withSeminorms_induced hq
#align inducing.with_seminorms Inducing.withSeminorms

/-- (Disjoint) union of seminorm families. -/
protected def SeminormFamily.sigma {Îº : Î¹ â†’ Type*} (p : (i : Î¹) â†’ SeminormFamily ğ•œ E (Îº i)) :
    SeminormFamily ğ•œ E ((i : Î¹) Ã— Îº i) :=
  fun âŸ¨i, kâŸ© => p i k

theorem withSeminorms_iInf {Îº : Î¹ â†’ Type*} [Nonempty ((i : Î¹) Ã— Îº i)] [âˆ€ i, Nonempty (Îº i)]
    {p : (i : Î¹) â†’ SeminormFamily ğ•œ E (Îº i)} {t : Î¹ â†’ TopologicalSpace E}
    [âˆ€ i, @TopologicalAddGroup E (t i) _] (hp : âˆ€ i, WithSeminorms (topology := t i) (p i)) :
    WithSeminorms (topology := â¨… i, t i) (SeminormFamily.sigma p) := by
  have : @TopologicalAddGroup E (â¨… i, t i) _ := topologicalAddGroup_iInf (fun i â†¦ inferInstance)
  simp_rw [@SeminormFamily.withSeminorms_iff_topologicalSpace_eq_iInf _ _ _ _ _ _ _ (_)] at hp âŠ¢
  rw [iInf_sigma]
  exact iInf_congr hp

end TopologicalConstructions

section TopologicalProperties

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Nonempty Î¹] [Countable Î¹]
variable {p : SeminormFamily ğ•œ E Î¹}
variable [TopologicalSpace E]

/-- If the topology of a space is induced by a countable family of seminorms, then the topology
is first countable. -/
theorem WithSeminorms.first_countable (hp : WithSeminorms p) :
    FirstCountableTopology E := by
  have := hp.topologicalAddGroup
  let _ : UniformSpace E := TopologicalAddGroup.toUniformSpace E
  have : UniformAddGroup E := comm_topologicalAddGroup_is_uniform
  have : (ğ“ (0 : E)).IsCountablyGenerated := by
    rw [p.withSeminorms_iff_nhds_eq_iInf.mp hp]
    exact Filter.iInf.isCountablyGenerated _
  have : (uniformity E).IsCountablyGenerated := UniformAddGroup.uniformity_countably_generated
  exact UniformSpace.firstCountableTopology E
#align with_seminorms.first_countable WithSeminorms.first_countable

end TopologicalProperties
