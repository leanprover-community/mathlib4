/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Semicontinuous
import Mathlib.Topology.Baire.Lemmas

/-!
# Barrelled spaces and the Banach-Steinhaus theorem / Uniform Boundedness Principle

This files defines barrelled spaces over a `NontriviallyNormedField`, and proves the
Banach-Steinhaus theorem for maps from a barrelled space to a space equipped with a family
of seminorms generating the topology (i.e `WithSeminorms q` for some family of seminorms `q`).

The more standard Banach-Steinhaus theorem for normed spaces is then deduced from that in
`Mathlib/Analysis/Normed/Operator/BanachSteinhaus.lean`.

## Main definitions

* `BarrelledSpace`: a topological vector space `E` is said to be **barrelled** if all lower
  semicontinuous seminorms on `E` are actually continuous. See the implementation details below for
  more comments on this definition.
* `WithSeminorms.continuousLinearMapOfTendsto`: fix `E` a barrelled space and `F` a TVS
  satisfying `WithSeminorms q` for some `q`. Given a sequence of continuous linear maps from
  `E` to `F` that converges pointwise to a function `f : E â†’ F`, this bundles `f` as a
  continuous linear map using the Banach-Steinhaus theorem.

## Main theorems

* `BaireSpace.instBarrelledSpace`: any TVS that is also a `BaireSpace` is barrelled. In
  particular, this applies to Banach spaces and FrÃ©chet spaces.
* `WithSeminorms.banach_steinhaus`: the **Banach-Steinhaus** theorem, also called
  **Uniform Boundedness Principle**. Fix `E` a barrelled space and `F` a TVS satisfying
  `WithSeminorms q` for some `q`. Any family `ğ“• : Î¹ â†’ E â†’L[ğ•œ] F` of continuous linear maps
  that is pointwise bounded is (uniformly) equicontinuous. Here, pointwise bounded means that
  for all `k` and `x`, the family of real numbers `i â†¦ q k (ğ“• i x)` is bounded above,
  which is equivalent to requiring that `ğ“•` is pointwise Von Neumann bounded
  (see `WithSeminorms.image_isVonNBounded_iff_seminorm_bounded`).

## Implementation details

Barrelled spaces are defined in Bourbaki as locally convex spaces where barrels (aka closed
balanced absorbing convex sets) are neighborhoods of zero. One can then show that barrels in a
locally convex space are exactly closed unit balls of lower semicontinuous seminorms, hence that a
locally convex space is barrelled iff any lower semicontinuous seminorm is continuous.

The problem with this definition is the local convexity, which is essential to prove that the
barrel definition is equivalent to the seminorm definition, because we can essentially only
use `LocallyConvexSpace` over `â„` or `â„‚` (which is indeed the setup in which Bourbaki does most
of the theory). Since we can easily prove the normed space version over any
`NontriviallyNormedField`, this wouldn't make for a very satisfying "generalization".

Fortunately, it turns out that using the seminorm definition directly solves all problems,
since it is exactly what we need for the proof. One could then expect to need the barrel
characterization to prove that Baire TVS are barrelled, but the proof is actually easier to do
with the seminorm characterization!

## TODO

* define barrels and prove that a locally convex space is barrelled iff all barrels are
  neighborhoods of zero.

## References

* [N. Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

banach-steinhaus, uniform boundedness, equicontinuity
-/

open Filter Topology Set ContinuousLinearMap

section defs

/-- A topological vector space `E` is said to be **barrelled** if all lower semicontinuous
seminorms on `E` are actually continuous. This is not the usual definition for TVS over `â„` or `â„‚`,
but this has the big advantage of working and giving sensible results over *any*
`NontriviallyNormedField`. In particular, the Banach-Steinhaus theorem holds for maps between such
a space and any space whose topology is generated by a family of seminorms. -/
class BarrelledSpace (ğ•œ E : Type*) [SeminormedRing ğ•œ] [AddGroup E] [SMul ğ•œ E]
    [TopologicalSpace E] : Prop where
  /-- In a barrelled space, all lower semicontinuous seminorms on `E` are actually continuous. -/
  continuous_of_lowerSemicontinuous : âˆ€ p : Seminorm ğ•œ E, LowerSemicontinuous p â†’ Continuous p

theorem Seminorm.continuous_of_lowerSemicontinuous {ğ•œ E : Type*} [AddGroup E] [SMul ğ•œ E]
    [SeminormedRing ğ•œ] [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Seminorm ğ•œ E)
    (hp : LowerSemicontinuous p) : Continuous p :=
  BarrelledSpace.continuous_of_lowerSemicontinuous p hp

theorem Seminorm.continuous_iSup
    {Î¹ : Sort*} {ğ•œ E : Type*} [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Î¹ â†’ Seminorm ğ•œ E)
    (hp : âˆ€ i, Continuous (p i)) (bdd : BddAbove (range p)) :
    Continuous (â¨† i, p i) := by
  rw [â† Seminorm.coe_iSup_eq bdd]
  refine Seminorm.continuous_of_lowerSemicontinuous _ ?_
  rw [Seminorm.coe_iSup_eq bdd]
  rw [Seminorm.bddAbove_range_iff] at bdd
  convert lowerSemicontinuous_ciSup (f := fun i x â†¦ p i x) bdd (fun i â†¦ (hp i).lowerSemicontinuous)
  exact iSup_apply

end defs

section TVS_anyField

variable {Î± Î¹ Îº ğ•œâ‚ ğ•œâ‚‚ E F : Type*} [Nonempty Îº] [NontriviallyNormedField ğ•œâ‚]
    [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]
    [AddCommGroup E] [AddCommGroup F] [Module ğ•œâ‚ E] [Module ğ•œâ‚‚ F]

/-- Any TVS over a `NontriviallyNormedField` that is also a Baire space is barrelled. In
particular, this applies to Banach spaces and FrÃ©chet spaces. -/
instance BaireSpace.instBarrelledSpace [TopologicalSpace E] [IsTopologicalAddGroup E]
    [ContinuousConstSMul ğ•œâ‚ E] [BaireSpace E] :
    BarrelledSpace ğ•œâ‚ E where
  continuous_of_lowerSemicontinuous := by
    -- Let `p` be a lower-semicontinuous seminorm on `E`.
    intro p hp
    -- Consider the family of all `p`-closed-balls with integer radius.
    -- By lower semicontinuity, each of these closed balls is indeed closed...
    have hâ‚ : âˆ€ n : â„•, IsClosed (p.closedBall (0 : E) n) := fun n â†¦ by
      simpa [p.closedBall_zero_eq] using hp.isClosed_preimage n
    -- ... and clearly they cover the whole space.
    have hâ‚‚ : (â‹ƒ n : â„•, p.closedBall (0 : E) n) = univ :=
      eq_univ_of_forall fun x â†¦ mem_iUnion.mpr (exists_nat_ge <| p (x - 0))
    -- Hence, one of them has nonempty interior. Let `n : â„•` be its radius, and fix `x` an
    -- interior point.
    rcases nonempty_interior_of_iUnion_of_closed hâ‚ hâ‚‚ with âŸ¨n, âŸ¨x, hxnâŸ©âŸ©
    -- To show that `p` is continuous, we will show that the `p`-closed-ball of
    -- radius `2*n` is a neighborhood of zero.
    refine Seminorm.continuous' (r := n + n) ?_
    rw [p.closedBall_zero_eq] at hxn âŠ¢
    have hxn' : p x â‰¤ n := by convert interior_subset hxn
    -- By definition, we have `p x' â‰¤ n` for `x'` sufficiently close to `x`.
    -- In other words, `p (x + y) â‰¤ n` for `y` sufficiently close to `0`.
    rw [mem_interior_iff_mem_nhds, â† map_add_left_nhds_zero] at hxn
    -- Hence, for `y` sufficiently close to `0`, we have
    -- `p y = p (x + y - x) â‰¤ p (x + y) + p x â‰¤ 2*n`
    filter_upwards [hxn] with y hy
    calc p y = p (x + y - x) := by rw [add_sub_cancel_left]
      _ â‰¤ p (x + y) + p x := map_sub_le_add _ _ _
      _ â‰¤ n + n := add_le_add hy hxn'

namespace WithSeminorms

variable [UniformSpace E] [UniformSpace F] [IsUniformAddGroup E] [IsUniformAddGroup F]
    [ContinuousSMul ğ•œâ‚ E] [BarrelledSpace ğ•œâ‚ E] {ğ“• : Î¹ â†’ E â†’SL[Ïƒâ‚â‚‚] F}
    {q : SeminormFamily ğ•œâ‚‚ F Îº} (hq : WithSeminorms q)
include hq

/-- The **Banach-Steinhaus** theorem, or **Uniform Boundedness Principle**, for maps from a
barrelled space to any space whose topology is generated by a family of seminorms. Use
`WithSeminorms.equicontinuous_TFAE` and `Seminorm.bound_of_continuous` to get explicit bounds on
the seminorms from equicontinuity. -/
protected theorem banach_steinhaus (H : âˆ€ k x, BddAbove (range fun i â†¦ q k (ğ“• i x))) :
    UniformEquicontinuous ((â†‘) âˆ˜ ğ“•) := by
  -- We just have to prove that `âŠ” i, (q k) âˆ˜ (ğ“• i)` is a (well-defined) continuous seminorm
  -- for all `k`.
  refine (hq.uniformEquicontinuous_iff_bddAbove_and_continuous_iSup (toLinearMap âˆ˜ ğ“•)).mpr ?_
  intro k
  -- By assumption the supremum `âŠ” i, q k (ğ“• i x)` is well-defined for all `x`, hence the
  -- supremum `âŠ” i, (q k) âˆ˜ (ğ“• i)` is well defined in the lattice of seminorms.
  have : BddAbove (range fun i â†¦ (q k).comp (ğ“• i).toLinearMap) := by
    rw [Seminorm.bddAbove_range_iff]
    exact H k
  -- By definition of the lattice structure on seminorms, `âŠ” i, (q k) âˆ˜ (ğ“• i)` is the *pointwise*
  -- supremum of the continuous seminorms `(q k) âˆ˜ (ğ“• i)`. Since `E` is barrelled, this supremum
  -- is continuous.
  exact âŸ¨this, Seminorm.continuous_iSup _
    (fun i â†¦ (hq.continuous_seminorm k).comp (ğ“• i).continuous) thisâŸ©

variable [ContinuousSMul ğ•œâ‚‚ F]

/-- Given a sequence of continuous linear maps which converges pointwise and for which the
domain is barrelled, the Banach-Steinhaus theorem is used to guarantee that the limit map
is a *continuous* linear map as well.

This actually works for any *countably generated* filter instead of `atTop : Filter â„•`,
but the proof ultimately goes back to sequences. -/
protected def continuousLinearMapOfTendsto (hq : WithSeminorms q)
    [T2Space F] {l : Filter Î±} [l.IsCountablyGenerated]
    [l.NeBot] (g : Î± â†’ E â†’SL[Ïƒâ‚â‚‚] F) {f : E â†’ F} (h : Tendsto (fun n x â†¦ g n x) l (ğ“ f)) :
    E â†’SL[Ïƒâ‚â‚‚] F where
  toLinearMap := linearMapOfTendsto _ _ h
  cont := by
    -- Since the filter `l` is countably generated and nontrivial, we can find a sequence
    -- `u : â„• â†’ Î±` that tends to `l`. By considering `g âˆ˜ u` instead of `g`, we can thus assume
    -- that `Î± = â„•` and `l = atTop`
    rcases l.exists_seq_tendsto with âŸ¨u, huâŸ©
    -- We claim that the limit is continuous because it's a limit of an equicontinuous family.
    -- By the Banach-Steinhaus theorem, this equicontinuity will follow from pointwise boundedness.
    refine (h.comp hu).continuous_of_equicontinuous (hq.banach_steinhaus ?_).equicontinuous
    -- For `k` and `x` fixed, we need to show that `(i : â„•) â†¦ q k (g i x)` is bounded.
    intro k x
    -- This follows from the fact that this sequences converges (to `q k (f x)`) by hypothesis and
    -- continuity of `q k`.
    rw [tendsto_pi_nhds] at h
    exact (((hq.continuous_seminorm k).tendsto _).comp <| (h x).comp hu).bddAbove_range
end WithSeminorms

end TVS_anyField
