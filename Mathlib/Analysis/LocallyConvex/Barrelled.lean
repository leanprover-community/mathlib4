/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Semicontinuous
import Mathlib.Topology.MetricSpace.Baire

/-!
# Barrelled spaces
-/

open Filter Topology Set ContinuousLinearMap

section defs

/-- A topological vector space `E` is said to be "barrelled" if all lower semicontinuous seminorms
on `E` are actually continuous. This is not the usual definition for TVS over `â„` or `â„‚`, but this
has the big advantage of working and giving sensible results over *any* `NontriviallyNormedField`.
In particular, the Banach-Steinhaus theorem holds for maps between such a space and any space whose
topology is generated by a family of seminorms. -/
class BarrelledSpace (ğ•œ E : Type _) [SeminormedRing ğ•œ] [AddGroup E] [SMul ğ•œ E]
    [TopologicalSpace E] : Prop where
  continuous_of_lowerSemicontinuous : âˆ€ p : Seminorm ğ•œ E, LowerSemicontinuous p â†’ Continuous p

theorem Seminorm.continuous_of_lowerSemicontinuous {ğ•œ E : Type _} [AddGroup E] [SMul ğ•œ E]
    [SeminormedRing ğ•œ] [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Seminorm ğ•œ E)
    (hp : LowerSemicontinuous p) : Continuous p :=
  BarrelledSpace.continuous_of_lowerSemicontinuous p hp

theorem Seminorm.continuous_iSup {Î¹ ğ•œ E : Type _} [NormedField ğ•œ]  [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Î¹ â†’ Seminorm ğ•œ E)
    (hp : âˆ€ i, Continuous (p i)) (bdd : BddAbove (range p)) :
    Continuous (â¨† i, p i) := by
  rw [â† Seminorm.coe_iSup_eq bdd]
  refine Seminorm.continuous_of_lowerSemicontinuous _ ?_
  rw [Seminorm.coe_iSup_eq bdd]
  rw [Seminorm.bddAbove_range_iff] at bdd
  convert lowerSemicontinuous_ciSup (f := fun i x â†¦ p i x) bdd (fun i â†¦ (hp i).lowerSemicontinuous)
  exact iSup_apply

end defs

section TVS_anyField

variable {Î± Î¹ Îº ğ•œâ‚ ğ•œâ‚‚ E F : Type _} [Nonempty Îº] [NontriviallyNormedField ğ•œâ‚]
    [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]
    [AddCommGroup E] [AddCommGroup F] [Module ğ•œâ‚ E] [Module ğ•œâ‚‚ F]

/-- Any TVS over a `NontriviallyNormedField` that is also a Baire space is barrelled. In
particular, this applies to Banach spaces and FrÃ©chet spaces. -/
instance [TopologicalSpace E] [TopologicalAddGroup E] [ContinuousConstSMul ğ•œâ‚ E] [BaireSpace E] :
    BarrelledSpace ğ•œâ‚ E where
  continuous_of_lowerSemicontinuous := by
    -- Let `p` be a lower-semicontinuous seminorm on `E`.
    intro p hp
    -- Consider the family of all `p`-closed-balls with integer radius.
    -- By lower semicontinuity, each of these closed balls is indeed closed...
    have hâ‚ : âˆ€ n : â„•, IsClosed (p.closedBall (0 : E) n) := fun n â†¦ by
      simpa [p.closedBall_zero_eq] using hp.isClosed_preimage n
    -- ... and clearly they cover the whole space.
    have hâ‚‚ : (â‹ƒ n : â„•, p.closedBall (0 : E) n) = univ :=
      eq_univ_of_forall fun x â†¦ mem_iUnion.mpr (exists_nat_ge <| p (x - 0))
    -- Hence, one of them has nonempty interior. Let `n : â„•` be its radius, and fix `x` an
    -- interior point.
    rcases nonempty_interior_of_iUnion_of_closed hâ‚ hâ‚‚ with âŸ¨n, âŸ¨x, hxnâŸ©âŸ©
    -- To show that `p` is continuous, we will show that the `p`-closed-ball of
    -- radius `2*n` is a neighborhood of zero.
    refine Seminorm.continuous' (r := 2*n) ?_
    rw [p.closedBall_zero_eq] at hxn âŠ¢
    have hxn' : p x â‰¤ n := by convert interior_subset hxn
    -- By definition, we have `p x' â‰¤ n` for `x'` sufficiently close to `x`.
    -- In other words, `p (x + y) â‰¤ n` for `y` sufficiently close to `0`.
    rw [mem_interior_iff_mem_nhds, â† map_add_left_nhds_zero] at hxn
    -- Hence, for `y` sufficiently close to `0`, we have
    -- `p y = p(x + y - x) â‰¤ p (x + y) + p x â‰¤ 2*n`
    filter_upwards [hxn] with y hy
    calc p y = p (x + y - x) := by rw [add_sub_cancel']
      _ â‰¤ p (x + y) + p x := map_sub_le_add _ _ _
      _ â‰¤ n + n := add_le_add hy hxn'
      _ = 2*n := by ring

namespace WithSeminorms

variable [UniformSpace E] [UniformSpace F] [UniformAddGroup E] [UniformAddGroup F]
    [ContinuousSMul ğ•œâ‚ E] [ContinuousSMul ğ•œâ‚‚ F] [BarrelledSpace ğ•œâ‚ E] {ğ“• : Î¹ â†’ E â†’SL[Ïƒâ‚â‚‚] F}
    {q : SeminormFamily ğ•œâ‚‚ F Îº} (hq : WithSeminorms q)

/-- The **Banach-Steinhaus** theorem for maps from a barrelled space to any space whose topology is
generated by a family of seminorms. Use `WithSeminorms.equicontinuous_TFAE` and
`Seminorm.bound_of_continuous` to get explicit bounds on the seminorms from equicontinuity. -/
protected theorem banach_steinhaus (H : âˆ€ k x, BddAbove (range fun i â†¦ q k (ğ“• i x))) :
    UniformEquicontinuous ((â†‘) âˆ˜ ğ“•) := by
  -- We just have to prove that `âŠ” i, (q k) âˆ˜ (ğ“• i)` is a (well-defined) continuous seminorm
  -- for all `k`.
  refine (hq.uniformEquicontinuous_iff_bddAbove_and_continuous_iSup ((toLinearMap) âˆ˜ ğ“•)).mpr ?_
  intro k
  -- By assumption the supremum `âŠ” i, (q k) âˆ˜ (ğ“• i)` is well-defined at each point, hence this
  -- is indeed a seminorm
  have : BddAbove (range fun i â†¦ (q k).comp (ğ“• i).toLinearMap) := by
    rw [Seminorm.bddAbove_range_iff]
    exact H k
  -- And since it is a supremum of continuous seminorms and `E` is barrelled, it is continuous
  exact âŸ¨this, Seminorm.continuous_iSup _
    (fun i â†¦ (hq.continuous_seminorm k).comp (ğ“• i).continuous) thisâŸ©

/-- Given a sequence of continuous linear maps which converges pointwise and for which the
domain is barrelled, the Banach-Steinhaus theorem is used to guarantee that the limit map
is a *continuous* linear map as well.

This actually works for any *countably generated* filter instead of `AtTop : Filter â„•`,
but the proof ultimately goes back to sequences. -/
protected def continuousLinearMapOfTendsto [T2Space F] {l : Filter Î±} [l.IsCountablyGenerated]
    [l.NeBot] (g : Î± â†’ E â†’SL[Ïƒâ‚â‚‚] F) {f : E â†’ F} (h : Tendsto (fun n x â†¦ g n x) l (ğ“ f)) :
    E â†’SL[Ïƒâ‚â‚‚] F where
  toFun := f
  map_add' := (linearMapOfTendsto _ _ h).map_add'
  map_smul' := (linearMapOfTendsto _ _ h).map_smul'
  cont := by
    rcases l.exists_seq_tendsto with âŸ¨u, huâŸ©
    refine (h.comp hu).continuous_of_equicontinuous_at (hq.banach_steinhaus ?_).equicontinuous
    intro k x
    rw [tendsto_pi_nhds] at h
    exact (((hq.continuous_seminorm k).tendsto _).comp <| (h x).comp hu).bddAbove_range
end WithSeminorms

end TVS_anyField
