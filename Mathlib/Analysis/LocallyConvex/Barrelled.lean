/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Semicontinuous
import Mathlib.Topology.MetricSpace.Baire

/-!
# Barrelled spaces
-/

open Bornology Set ContinuousLinearMap

/-- A topological vector space `E` is said to be "barrelled" if all lower semicontinuous seminorms
on `E` are actually continuous. This is not the usual definition for TVS over `â„` or `â„‚`, but this
has the big advantage of working and giving sensible results over *any* `NontriviallyNormedField`.
In particular, the Banach-Steinhaus theorem holds for maps between such a space and any space whose
topology is generated by a family of seminorms. -/
class BarrelledSpace (ğ•œ E : Type _) [SeminormedRing ğ•œ] [AddGroup E] [SMul ğ•œ E]
    [TopologicalSpace E] : Prop where
  continuous_of_lowerSemicontinuous : âˆ€ p : Seminorm ğ•œ E, LowerSemicontinuous p â†’ Continuous p

/-- Any TVS over a `NontriviallyNormedField` that is also a Baire space is barrelled. In
particular, this applies to Banach spaces and FrÃ©chet spaces. -/
instance {ğ•œ E : Type _} [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [TopologicalAddGroup E] [ContinuousConstSMul ğ•œ E] [BaireSpace E] :
    BarrelledSpace ğ•œ E where
  continuous_of_lowerSemicontinuous := by
    intro p hp
    have hâ‚ : âˆ€ n : â„•, IsClosed (p.closedBall (0 : E) n) := fun n â†¦ by
      simpa [p.closedBall_zero_eq] using hp.isClosed_preimage n
    have hâ‚‚ : (â‹ƒ n : â„•, p.closedBall (0 : E) n) = univ :=
      eq_univ_of_forall fun x â†¦ mem_iUnion.mpr (exists_nat_ge <| p (x - 0))
    rcases nonempty_interior_of_iUnion_of_closed hâ‚ hâ‚‚ with âŸ¨n, âŸ¨x, hxnâŸ©âŸ©
    refine Seminorm.continuous' (show (0 : â„) < 2*n+1 by positivity) ?_
    rw [p.closedBall_zero_eq] at hxn âŠ¢
    have hxn' : p x â‰¤ n := by convert interior_subset hxn
    -- `hxn` says that `âˆ€á¶  y in ğ“ 0, p (x + y) â‰¤ n`.
    rw [mem_interior_iff_mem_nhds, â† map_add_left_nhds_zero] at hxn
    filter_upwards [hxn] with y hy
    calc p y = p (x + y - x) := by rw [add_sub_cancel']
      _ â‰¤ p (x + y) + p x := map_sub_le_add _ _ _
      _ â‰¤ n + n := add_le_add hy hxn'
      _ â‰¤ 2*n + 1 := by linarith

theorem Seminorm.continuous_of_lowerSemicontinuous {ğ•œ E : Type _} [AddGroup E] [SMul ğ•œ E]
    [SeminormedRing ğ•œ] [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Seminorm ğ•œ E)
    (hp : LowerSemicontinuous p) : Continuous p :=
  BarrelledSpace.continuous_of_lowerSemicontinuous p hp

theorem Seminorm.continuous_iSup {Î¹ ğ•œ E : Type _} [NormedField ğ•œ]  [AddCommGroup E] [Module ğ•œ E]
    [TopologicalSpace E] [BarrelledSpace ğ•œ E] (p : Î¹ â†’ Seminorm ğ•œ E)
    (hp : âˆ€ i, Continuous (p i)) (bdd : BddAbove (range p)) :
    Continuous (â¨† i, p i : Seminorm ğ•œ E) := by
  refine Seminorm.continuous_of_lowerSemicontinuous _ ?_
  rw [Seminorm.coe_iSup_eq bdd]
  rw [Seminorm.bddAbove_range_iff] at bdd
  convert lowerSemicontinuous_ciSup (f := fun i x â†¦ p i x) bdd (fun i â†¦ (hp i).lowerSemicontinuous)
  exact iSup_apply

theorem WithSeminorms.banach_steinhaus {Î¹ Îº ğ•œâ‚ ğ•œâ‚‚ E F : Type _} [Nonempty Îº]
    [NontriviallyNormedField ğ•œâ‚] [NontriviallyNormedField ğ•œâ‚‚]
    {Ïƒâ‚â‚‚ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]
    [AddCommGroup E] [AddCommGroup F] [Module ğ•œâ‚ E] [Module ğ•œâ‚‚ F] [UniformSpace E]
    [UniformSpace F] [UniformAddGroup E] [UniformAddGroup F] [ContinuousSMul ğ•œâ‚ E]
    [ContinuousSMul ğ•œâ‚‚ F] [BarrelledSpace ğ•œâ‚ E]
    {q : SeminormFamily ğ•œâ‚‚ F Îº} {ğ“• : Î¹ â†’ E â†’SL[Ïƒâ‚â‚‚] F}
    (hq : WithSeminorms q) (H : âˆ€ k x, BddAbove (range fun i â†¦ q k (ğ“• i x))) :
    UniformEquicontinuous ((â†‘) âˆ˜ ğ“•) := by
  refine (hq.uniformEquicontinuous_iff_bddAbove_and_continuous_iSup ((toLinearMap) âˆ˜ ğ“•)).mpr ?_
  intro k
  have : BddAbove (range fun i â†¦ (q k).comp (ğ“• i).toLinearMap) := by
    rw [Seminorm.bddAbove_range_iff]
    exact H k
  simp only [Function.comp_apply, â† Seminorm.coe_iSup_eq this]
  exact âŸ¨this, Seminorm.continuous_iSup _
    (fun i â†¦ (hq.continuous_seminorm k).comp (ğ“• i).continuous) thisâŸ©
