/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.GroupTheory.GroupAction.Pointwise
import Mathlib.Analysis.LocallyConvex.Basic
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Seminorm
import Mathlib.Topology.Bornology.Basic
import Mathlib.Topology.Algebra.UniformGroup
import Mathlib.Topology.UniformSpace.Cauchy
import Mathlib.Topology.Algebra.Module.Basic

#align_import analysis.locally_convex.bounded from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Von Neumann Boundedness

This file defines natural or von Neumann bounded sets and proves elementary properties.

## Main declarations

* `Bornology.IsVonNBounded`: A set `s` is von Neumann-bounded if every neighborhood of zero
absorbs `s`.
* `Bornology.vonNBornology`: The bornology made of the von Neumann-bounded sets.

## Main results

* `Bornology.IsVonNBounded.of_topologicalSpace_le`: A coarser topology admits more
von Neumann-bounded sets.
* `Bornology.IsVonNBounded.image`: A continuous linear image of a bounded set is bounded.
* `Bornology.isVonNBounded_iff_smul_tendsto_zero`: Given any sequence `Îµ` of scalars which tends
  to `ğ“[â‰ ] 0`, we have that a set `S` is bounded if and only if for any sequence `x : â„• â†’ S`,
  `Îµ â€¢ x` tends to 0. This shows that bounded sets are completely determined by sequences, which is
  the key fact for proving that sequential continuity implies continuity for linear maps defined on
  a bornological space

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

-/


variable {ğ•œ ğ•œ' E E' F Î¹ : Type*}

open Set Filter Function
open scoped Topology Pointwise

set_option linter.uppercaseLean3 false

namespace Bornology

section SeminormedRing

section Zero

variable (ğ•œ)
variable [SeminormedRing ğ•œ] [SMul ğ•œ E] [Zero E]
variable [TopologicalSpace E]

/-- A set `s` is von Neumann bounded if every neighborhood of 0 absorbs `s`. -/
def IsVonNBounded (s : Set E) : Prop :=
  âˆ€ â¦ƒVâ¦„, V âˆˆ ğ“ (0 : E) â†’ Absorbs ğ•œ V s
#align bornology.is_vonN_bounded Bornology.IsVonNBounded

variable (E)

@[simp]
theorem isVonNBounded_empty : IsVonNBounded ğ•œ (âˆ… : Set E) := fun _ _ => Absorbs.empty
#align bornology.is_vonN_bounded_empty Bornology.isVonNBounded_empty

variable {ğ•œ E}

theorem isVonNBounded_iff (s : Set E) : IsVonNBounded ğ•œ s â†” âˆ€ V âˆˆ ğ“ (0 : E), Absorbs ğ•œ V s :=
  Iff.rfl
#align bornology.is_vonN_bounded_iff Bornology.isVonNBounded_iff

theorem _root_.Filter.HasBasis.isVonNBounded_iff {q : Î¹ â†’ Prop} {s : Î¹ â†’ Set E} {A : Set E}
    (h : (ğ“ (0 : E)).HasBasis q s) : IsVonNBounded ğ•œ A â†” âˆ€ i, q i â†’ Absorbs ğ•œ (s i) A := by
  refine âŸ¨fun hA i hi => hA (h.mem_of_mem hi), fun hA V hV => ?_âŸ©
  rcases h.mem_iff.mp hV with âŸ¨i, hi, hVâŸ©
  exact (hA i hi).mono_left hV
#align filter.has_basis.is_vonN_bounded_basis_iff Filter.HasBasis.isVonNBounded_iff

@[deprecated] -- since 2024-01-12
alias _root_.Filter.HasBasis.isVonNBounded_basis_iff := Filter.HasBasis.isVonNBounded_iff

/-- Subsets of bounded sets are bounded. -/
theorem IsVonNBounded.subset {sâ‚ sâ‚‚ : Set E} (h : sâ‚ âŠ† sâ‚‚) (hsâ‚‚ : IsVonNBounded ğ•œ sâ‚‚) :
    IsVonNBounded ğ•œ sâ‚ := fun _ hV => (hsâ‚‚ hV).mono_right h
#align bornology.is_vonN_bounded.subset Bornology.IsVonNBounded.subset

/-- The union of two bounded sets is bounded. -/
theorem IsVonNBounded.union {sâ‚ sâ‚‚ : Set E} (hsâ‚ : IsVonNBounded ğ•œ sâ‚) (hsâ‚‚ : IsVonNBounded ğ•œ sâ‚‚) :
    IsVonNBounded ğ•œ (sâ‚ âˆª sâ‚‚) := fun _ hV => (hsâ‚ hV).union (hsâ‚‚ hV)
#align bornology.is_vonN_bounded.union Bornology.IsVonNBounded.union

end Zero

section ContinuousAdd

variable [SeminormedRing ğ•œ] [AddZeroClass E] [TopologicalSpace E] [ContinuousAdd E]
  [DistribSMul ğ•œ E] {s t : Set E}

protected theorem IsVonNBounded.add (hs : IsVonNBounded ğ•œ s) (ht : IsVonNBounded ğ•œ t) :
    IsVonNBounded ğ•œ (s + t) := fun U hU â†¦ by
  rcases exists_open_nhds_zero_add_subset hU with âŸ¨V, hVo, hV, hVUâŸ©
  exact ((hs <| hVo.mem_nhds hV).add (ht <| hVo.mem_nhds hV)).mono_left hVU

end ContinuousAdd

section TopologicalAddGroup

variable [SeminormedRing ğ•œ] [AddGroup E] [TopologicalSpace E] [TopologicalAddGroup E]
  [DistribMulAction ğ•œ E] {s t : Set E}

protected theorem IsVonNBounded.neg (hs : IsVonNBounded ğ•œ s) : IsVonNBounded ğ•œ (-s) := fun U hU â†¦ by
  rw [â† neg_neg U]
  exact (hs <| neg_mem_nhds_zero _ hU).neg_neg

@[simp]
theorem isVonNBounded_neg : IsVonNBounded ğ•œ (-s) â†” IsVonNBounded ğ•œ s :=
  âŸ¨fun h â†¦ neg_neg s â–¸ h.neg, fun h â†¦ h.negâŸ©

alias âŸ¨IsVonNBounded.of_neg, _âŸ© := isVonNBounded_neg

protected theorem IsVonNBounded.sub (hs : IsVonNBounded ğ•œ s) (ht : IsVonNBounded ğ•œ t) :
    IsVonNBounded ğ•œ (s - t) := by
  rw [sub_eq_add_neg]
  exact hs.add ht.neg

end TopologicalAddGroup

end SeminormedRing

section MultipleTopologies

variable [SeminormedRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]

/-- If a topology `t'` is coarser than `t`, then any set `s` that is bounded with respect to
`t` is bounded with respect to `t'`. -/
theorem IsVonNBounded.of_topologicalSpace_le {t t' : TopologicalSpace E} (h : t â‰¤ t') {s : Set E}
    (hs : @IsVonNBounded ğ•œ E _ _ _ t s) : @IsVonNBounded ğ•œ E _ _ _ t' s := fun _ hV =>
  hs <| (le_iff_nhds t t').mp h 0 hV
#align bornology.is_vonN_bounded.of_topological_space_le Bornology.IsVonNBounded.of_topologicalSpace_le

end MultipleTopologies

lemma isVonNBounded_iff_tendsto_smallSets_nhds {ğ•œ E : Type*} [NormedDivisionRing ğ•œ]
    [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] {S : Set E} :
    IsVonNBounded ğ•œ S â†” Tendsto (Â· â€¢ S : ğ•œ â†’ Set E) (ğ“ 0) (ğ“ 0).smallSets := by
  rw [tendsto_smallSets_iff]
  refine forallâ‚‚_congr fun V hV â†¦ ?_
  simp only [absorbs_iff_eventually_nhds_zero (mem_of_mem_nhds hV), mapsTo', image_smul]

alias âŸ¨IsVonNBounded.tendsto_smallSets_nhds, _âŸ© := isVonNBounded_iff_tendsto_smallSets_nhds

lemma isVonNBounded_pi_iff {ğ•œ Î¹ : Type*} {E : Î¹ â†’ Type*} [NormedDivisionRing ğ•œ]
    [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)]
    {S : Set (âˆ€ i, E i)} : IsVonNBounded ğ•œ S â†” âˆ€ i, IsVonNBounded ğ•œ (eval i '' S) := by
  simp_rw [isVonNBounded_iff_tendsto_smallSets_nhds, nhds_pi, Filter.pi, smallSets_iInf,
    smallSets_comap_eq_comap_image, tendsto_iInf, tendsto_comap_iff, Function.comp,
    â† image_smul, image_image]; rfl

section Image

variable {ğ•œâ‚ ğ•œâ‚‚ : Type*} [NormedDivisionRing ğ•œâ‚] [NormedDivisionRing ğ•œâ‚‚] [AddCommGroup E]
  [Module ğ•œâ‚ E] [AddCommGroup F] [Module ğ•œâ‚‚ F] [TopologicalSpace E] [TopologicalSpace F]

/-- A continuous linear image of a bounded set is bounded. -/
theorem IsVonNBounded.image {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚} [RingHomSurjective Ïƒ] [RingHomIsometric Ïƒ] {s : Set E}
    (hs : IsVonNBounded ğ•œâ‚ s) (f : E â†’SL[Ïƒ] F) : IsVonNBounded ğ•œâ‚‚ (f '' s) := by
  have Ïƒ_iso : Isometry Ïƒ := AddMonoidHomClass.isometry_of_norm Ïƒ fun x => RingHomIsometric.is_iso
  have : map Ïƒ (ğ“ 0) = ğ“ 0 := by
    rw [Ïƒ_iso.embedding.map_nhds_eq, Ïƒ.surjective.range_eq, nhdsWithin_univ, map_zero]
  have hfâ‚€ : Tendsto f (ğ“ 0) (ğ“ 0) := f.continuous.tendsto' 0 0 (map_zero f)
  simp only [isVonNBounded_iff_tendsto_smallSets_nhds, â† this, tendsto_map'_iff] at hs âŠ¢
  simpa only [comp_def, image_smul_setâ‚›â‚— _ _ Ïƒ f] using hfâ‚€.image_smallSets.comp hs
#align bornology.is_vonN_bounded.image Bornology.IsVonNBounded.image

end Image

section sequence

variable {ğ• : Type*} [NormedField ğ•œ] [NontriviallyNormedField ğ•] [AddCommGroup E] [Module ğ•œ E]
  [Module ğ• E] [TopologicalSpace E] [ContinuousSMul ğ• E]

theorem IsVonNBounded.smul_tendsto_zero {S : Set E} {Îµ : Î¹ â†’ ğ•œ} {x : Î¹ â†’ E} {l : Filter Î¹}
    (hS : IsVonNBounded ğ•œ S) (hxS : âˆ€á¶  n in l, x n âˆˆ S) (hÎµ : Tendsto Îµ l (ğ“ 0)) :
    Tendsto (Îµ â€¢ x) l (ğ“ 0) :=
  (hS.tendsto_smallSets_nhds.comp hÎµ).of_smallSets <| hxS.mono fun _ â†¦ smul_mem_smul_set
#align bornology.is_vonN_bounded.smul_tendsto_zero Bornology.IsVonNBounded.smul_tendsto_zero

theorem isVonNBounded_of_smul_tendsto_zero {Îµ : Î¹ â†’ ğ•} {l : Filter Î¹} [l.NeBot]
    (hÎµ : âˆ€á¶  n in l, Îµ n â‰  0) {S : Set E}
    (H : âˆ€ x : Î¹ â†’ E, (âˆ€ n, x n âˆˆ S) â†’ Tendsto (Îµ â€¢ x) l (ğ“ 0)) : IsVonNBounded ğ• S := by
  rw [(nhds_basis_balanced ğ• E).isVonNBounded_iff]
  by_contra! H'
  rcases H' with âŸ¨V, âŸ¨hV, hVbâŸ©, hVSâŸ©
  have : âˆ€á¶  n in l, âˆƒ x : S, Îµ n â€¢ (x : E) âˆ‰ V := by
    filter_upwards [hÎµ] with n hn
    rw [absorbs_iff_norm] at hVS
    push_neg at hVS
    rcases hVS â€–(Îµ n)â»Â¹â€– with âŸ¨a, haÎµ, haSâŸ©
    rcases Set.not_subset.mp haS with âŸ¨x, hxS, hxâŸ©
    refine âŸ¨âŸ¨x, hxSâŸ©, fun hnx => ?_âŸ©
    rw [â† Set.mem_inv_smul_set_iffâ‚€ hn] at hnx
    exact hx (hVb.smul_mono haÎµ hnx)
  rcases this.choice with âŸ¨x, hxâŸ©
  refine Filter.frequently_false l (Filter.Eventually.frequently ?_)
  filter_upwards [hx,
    (H (_ âˆ˜ x) fun n => (x n).2).eventually (eventually_mem_set.mpr hV)] using fun n => id
#align bornology.is_vonN_bounded_of_smul_tendsto_zero Bornology.isVonNBounded_of_smul_tendsto_zero

/-- Given any sequence `Îµ` of scalars which tends to `ğ“[â‰ ] 0`, we have that a set `S` is bounded
  if and only if for any sequence `x : â„• â†’ S`, `Îµ â€¢ x` tends to 0. This actually works for any
  indexing type `Î¹`, but in the special case `Î¹ = â„•` we get the important fact that convergent
  sequences fully characterize bounded sets. -/
theorem isVonNBounded_iff_smul_tendsto_zero {Îµ : Î¹ â†’ ğ•} {l : Filter Î¹} [l.NeBot]
    (hÎµ : Tendsto Îµ l (ğ“[â‰ ] 0)) {S : Set E} :
    IsVonNBounded ğ• S â†” âˆ€ x : Î¹ â†’ E, (âˆ€ n, x n âˆˆ S) â†’ Tendsto (Îµ â€¢ x) l (ğ“ 0) :=
  âŸ¨fun hS x hxS => hS.smul_tendsto_zero (eventually_of_forall hxS) (le_trans hÎµ nhdsWithin_le_nhds),
    isVonNBounded_of_smul_tendsto_zero (by exact hÎµ self_mem_nhdsWithin)âŸ©
#align bornology.is_vonN_bounded_iff_smul_tendsto_zero Bornology.isVonNBounded_iff_smul_tendsto_zero

end sequence

section NormedField

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [TopologicalSpace E] [ContinuousSMul ğ•œ E]

/-- Singletons are bounded. -/
theorem isVonNBounded_singleton (x : E) : IsVonNBounded ğ•œ ({x} : Set E) := fun _ hV =>
  (absorbent_nhds_zero hV).absorbs
#align bornology.is_vonN_bounded_singleton Bornology.isVonNBounded_singleton

section ContinuousAdd

variable [ContinuousAdd E] {s t : Set E}

protected theorem IsVonNBounded.vadd (hs : IsVonNBounded ğ•œ s) (x : E) :
    IsVonNBounded ğ•œ (x +áµ¥ s) := by
  rw [â† singleton_vadd]
  -- TODO: dot notation timeouts in the next line
  exact IsVonNBounded.add (isVonNBounded_singleton x) hs

@[simp]
theorem isVonNBounded_vadd (x : E) : IsVonNBounded ğ•œ (x +áµ¥ s) â†” IsVonNBounded ğ•œ s :=
  âŸ¨fun h â†¦ by simpa using h.vadd (-x), fun h â†¦ h.vadd xâŸ©

theorem IsVonNBounded.of_add_right (hst : IsVonNBounded ğ•œ (s + t)) (hs : s.Nonempty) :
    IsVonNBounded ğ•œ t :=
  let âŸ¨x, hxâŸ© := hs
  (isVonNBounded_vadd x).mp <| hst.subset <| image_subset_image2_right hx

theorem IsVonNBounded.of_add_left (hst : IsVonNBounded ğ•œ (s + t)) (ht : t.Nonempty) :
    IsVonNBounded ğ•œ s :=
  ((add_comm s t).subst hst).of_add_right ht

theorem isVonNBounded_add_of_nonempty (hs : s.Nonempty) (ht : t.Nonempty) :
    IsVonNBounded ğ•œ (s + t) â†” IsVonNBounded ğ•œ s âˆ§ IsVonNBounded ğ•œ t :=
  âŸ¨fun h â†¦ âŸ¨h.of_add_left ht, h.of_add_right hsâŸ©, and_imp.2 IsVonNBounded.addâŸ©

theorem isVonNBounded_add :
    IsVonNBounded ğ•œ (s + t) â†” s = âˆ… âˆ¨ t = âˆ… âˆ¨ IsVonNBounded ğ•œ s âˆ§ IsVonNBounded ğ•œ t := by
  rcases s.eq_empty_or_nonempty with rfl | hs; Â· simp
  rcases t.eq_empty_or_nonempty with rfl | ht; Â· simp
  simp [hs.ne_empty, ht.ne_empty, isVonNBounded_add_of_nonempty hs ht]

@[simp]
theorem isVonNBounded_add_self : IsVonNBounded ğ•œ (s + s) â†” IsVonNBounded ğ•œ s := by
  rcases s.eq_empty_or_nonempty with rfl | hs <;> simp [isVonNBounded_add_of_nonempty, *]

theorem IsVonNBounded.of_sub_left (hst : IsVonNBounded ğ•œ (s - t)) (ht : t.Nonempty) :
    IsVonNBounded ğ•œ s :=
  ((sub_eq_add_neg s t).subst hst).of_add_left ht.neg

end ContinuousAdd

section TopologicalAddGroup

variable [TopologicalAddGroup E] {s t : Set E}

theorem IsVonNBounded.of_sub_right (hst : IsVonNBounded ğ•œ (s - t)) (hs : s.Nonempty) :
    IsVonNBounded ğ•œ t :=
  (((sub_eq_add_neg s t).subst hst).of_add_right hs).of_neg

theorem isVonNBounded_sub_of_nonempty (hs : s.Nonempty) (ht : t.Nonempty) :
    IsVonNBounded ğ•œ (s - t) â†” IsVonNBounded ğ•œ s âˆ§ IsVonNBounded ğ•œ t := by
  simp [sub_eq_add_neg, isVonNBounded_add_of_nonempty, hs, ht]

theorem isVonNBounded_sub :
    IsVonNBounded ğ•œ (s - t) â†” s = âˆ… âˆ¨ t = âˆ… âˆ¨ IsVonNBounded ğ•œ s âˆ§ IsVonNBounded ğ•œ t := by
  simp [sub_eq_add_neg, isVonNBounded_add]

end TopologicalAddGroup

/-- The union of all bounded set is the whole space. -/
theorem isVonNBounded_covers : â‹ƒâ‚€ setOf (IsVonNBounded ğ•œ) = (Set.univ : Set E) :=
  Set.eq_univ_iff_forall.mpr fun x =>
    Set.mem_sUnion.mpr âŸ¨{x}, isVonNBounded_singleton _, Set.mem_singleton _âŸ©
#align bornology.is_vonN_bounded_covers Bornology.isVonNBounded_covers

variable (ğ•œ E)

-- See note [reducible non-instances]
/-- The von Neumann bornology defined by the von Neumann bounded sets.

Note that this is not registered as an instance, in order to avoid diamonds with the
metric bornology. -/
abbrev vonNBornology : Bornology E :=
  Bornology.ofBounded (setOf (IsVonNBounded ğ•œ)) (isVonNBounded_empty ğ•œ E)
    (fun _ hs _ ht => hs.subset ht) (fun _ hs _ => hs.union) isVonNBounded_singleton
#align bornology.vonN_bornology Bornology.vonNBornology

variable {E}

@[simp]
theorem isBounded_iff_isVonNBounded {s : Set E} :
    @IsBounded _ (vonNBornology ğ•œ E) s â†” IsVonNBounded ğ•œ s :=
  isBounded_ofBounded_iff _
#align bornology.is_bounded_iff_is_vonN_bounded Bornology.isBounded_iff_isVonNBounded

end NormedField

end Bornology

section UniformAddGroup

variable (ğ•œ) [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [UniformSpace E] [UniformAddGroup E] [ContinuousSMul ğ•œ E]

theorem TotallyBounded.isVonNBounded {s : Set E} (hs : TotallyBounded s) :
    Bornology.IsVonNBounded ğ•œ s := by
  rw [totallyBounded_iff_subset_finite_iUnion_nhds_zero] at hs
  intro U hU
  have h : Filter.Tendsto (fun x : E Ã— E => x.fst + x.snd) (ğ“ (0, 0)) (ğ“ ((0 : E) + (0 : E))) :=
    tendsto_add
  rw [add_zero] at h
  have h' := (nhds_basis_balanced ğ•œ E).prod (nhds_basis_balanced ğ•œ E)
  simp_rw [â† nhds_prod_eq, id] at h'
  rcases h.basis_left h' U hU with âŸ¨x, hx, h''âŸ©
  rcases hs x.snd hx.2.1 with âŸ¨t, ht, hsâŸ©
  refine Absorbs.mono_right ?_ hs
  rw [ht.absorbs_biUnion]
  have hx_fstsnd : x.fst + x.snd âŠ† U := add_subset_iff.mpr fun z1 hz1 z2 hz2 â†¦
    h'' <| mk_mem_prod hz1 hz2
  refine fun y _ => Absorbs.mono_left ?_ hx_fstsnd
  -- TODO: with dot notation, Lean timeouts on the next line. Why?
  exact Absorbent.vadd_absorbs (absorbent_nhds_zero hx.1.1) hx.2.2.absorbs_self
#align totally_bounded.is_vonN_bounded TotallyBounded.isVonNBounded

end UniformAddGroup

section VonNBornologyEqMetric

namespace NormedSpace

section NormedField

variable (ğ•œ)
variable [NormedField ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem isVonNBounded_of_isBounded {s : Set E} (h : Bornology.IsBounded s) :
    Bornology.IsVonNBounded ğ•œ s := by
  rcases h.subset_ball 0 with âŸ¨r, hrâŸ©
  rw [Metric.nhds_basis_ball.isVonNBounded_iff]
  rw [â† ball_normSeminorm ğ•œ E] at hr âŠ¢
  exact fun Îµ hÎµ â†¦ ((normSeminorm ğ•œ E).ball_zero_absorbs_ball_zero hÎµ).mono_right hr

variable (E)

theorem isVonNBounded_ball (r : â„) : Bornology.IsVonNBounded ğ•œ (Metric.ball (0 : E) r) :=
  isVonNBounded_of_isBounded _ Metric.isBounded_ball
#align normed_space.is_vonN_bounded_ball NormedSpace.isVonNBounded_ball

theorem isVonNBounded_closedBall (r : â„) :
    Bornology.IsVonNBounded ğ•œ (Metric.closedBall (0 : E) r) :=
  isVonNBounded_of_isBounded _ Metric.isBounded_closedBall
#align normed_space.is_vonN_bounded_closed_ball NormedSpace.isVonNBounded_closedBall

end NormedField

variable (ğ•œ)
variable [NontriviallyNormedField ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

theorem isVonNBounded_iff {s : Set E} : Bornology.IsVonNBounded ğ•œ s â†” Bornology.IsBounded s := by
  refine âŸ¨fun h â†¦ ?_, isVonNBounded_of_isBounded _âŸ©
  rcases (h (Metric.ball_mem_nhds 0 zero_lt_one)).exists_pos with âŸ¨Ï, hÏ, hÏballâŸ©
  rcases NormedField.exists_lt_norm ğ•œ Ï with âŸ¨a, haâŸ©
  specialize hÏball a ha.le
  rw [â† ball_normSeminorm ğ•œ E, Seminorm.smul_ball_zero (norm_pos_iff.1 <| hÏ.trans ha),
    ball_normSeminorm] at hÏball
  exact Metric.isBounded_ball.subset hÏball
#align normed_space.is_vonN_bounded_iff NormedSpace.isVonNBounded_iff

theorem isVonNBounded_iff' {s : Set E} :
    Bornology.IsVonNBounded ğ•œ s â†” âˆƒ r : â„, âˆ€ x âˆˆ s, â€–xâ€– â‰¤ r := by
  rw [NormedSpace.isVonNBounded_iff, isBounded_iff_forall_norm_le]
#align normed_space.is_vonN_bounded_iff' NormedSpace.isVonNBounded_iff'

theorem image_isVonNBounded_iff {Î± : Type*} {f : Î± â†’ E} {s : Set Î±} :
    Bornology.IsVonNBounded ğ•œ (f '' s) â†” âˆƒ r : â„, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ r := by
  simp_rw [isVonNBounded_iff', Set.forall_mem_image]
#align normed_space.image_is_vonN_bounded_iff NormedSpace.image_isVonNBounded_iff

/-- In a normed space, the von Neumann bornology (`Bornology.vonNBornology`) is equal to the
metric bornology. -/
theorem vonNBornology_eq : Bornology.vonNBornology ğ•œ E = PseudoMetricSpace.toBornology := by
  rw [Bornology.ext_iff_isBounded]
  intro s
  rw [Bornology.isBounded_iff_isVonNBounded]
  exact isVonNBounded_iff _
#align normed_space.vonN_bornology_eq NormedSpace.vonNBornology_eq

theorem isBounded_iff_subset_smul_ball {s : Set E} :
    Bornology.IsBounded s â†” âˆƒ a : ğ•œ, s âŠ† a â€¢ Metric.ball (0 : E) 1 := by
  rw [â† isVonNBounded_iff ğ•œ]
  constructor
  Â· intro h
    rcases (h (Metric.ball_mem_nhds 0 zero_lt_one)).exists_pos with âŸ¨Ï, _, hÏballâŸ©
    rcases NormedField.exists_lt_norm ğ•œ Ï with âŸ¨a, haâŸ©
    exact âŸ¨a, hÏball a ha.leâŸ©
  Â· rintro âŸ¨a, haâŸ©
    exact ((isVonNBounded_ball ğ•œ E 1).image (a â€¢ (1 : E â†’L[ğ•œ] E))).subset ha
#align normed_space.is_bounded_iff_subset_smul_ball NormedSpace.isBounded_iff_subset_smul_ball

theorem isBounded_iff_subset_smul_closedBall {s : Set E} :
    Bornology.IsBounded s â†” âˆƒ a : ğ•œ, s âŠ† a â€¢ Metric.closedBall (0 : E) 1 := by
  constructor
  Â· rw [isBounded_iff_subset_smul_ball ğ•œ]
    exact Exists.imp fun a ha => ha.trans <| Set.smul_set_mono <| Metric.ball_subset_closedBall
  Â· rw [â† isVonNBounded_iff ğ•œ]
    rintro âŸ¨a, haâŸ©
    exact ((isVonNBounded_closedBall ğ•œ E 1).image (a â€¢ (1 : E â†’L[ğ•œ] E))).subset ha
#align normed_space.is_bounded_iff_subset_smul_closed_ball NormedSpace.isBounded_iff_subset_smul_closedBall

end NormedSpace

end VonNBornologyEqMetric
