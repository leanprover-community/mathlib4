/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Topology.Algebra.Module.PointwiseConvergence
public import Mathlib.Analysis.LocallyConvex.WithSeminorms
public import Mathlib.Analysis.LocallyConvex.StrongTopology

/-!
# The topology of pointwise convergence is locally convex

We prove that the topology of pointwise convergence is induced by a family of seminorms and
that it is locally convex in the topological sense

* `PointwiseConvergenceCLM.seminorm`: the seminorms on `E â†’SLâ‚šâ‚œ[Ïƒ] F` given by `A â†¦ â€–A xâ€–` for fixed
`x : E`.
* `PointwiseConvergenceCLM.withSeminorm`: the topology is induced by the seminorms.
* `PointwiseConvergenceCLM.instLocallyConvexSpace`: `E â†’SLâ‚šâ‚œ[Ïƒ] F` is locally convex.

-/

@[expose] public section

variable {R ğ•œâ‚ ğ•œâ‚‚ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚]
  {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚} {E F : Type*}
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œâ‚ E]

namespace PointwiseConvergenceCLM

section NormedSpace

variable [NormedAddCommGroup F] [NormedSpace ğ•œâ‚‚ F]

/-- The family of seminorms that induce the topology of pointwise convergence, namely `â€–A xâ€–` for
all `x : E`. -/
protected def seminorm (x : E) : Seminorm ğ•œâ‚‚ (E â†’SLâ‚šâ‚œ[Ïƒ] F) where
  toFun A := â€–A xâ€–
  map_zero' := by simp
  add_le' A B := by simpa only using norm_add_le _ _
  neg' A := by simp
  smul' r A := by simp [norm_smul]

variable (Ïƒ E F) in
/-- The family of seminorms that induce the topology of pointwise convergence, namely `â€–A xâ€–` for
all `x : E`. -/
protected abbrev seminormFamily : SeminormFamily ğ•œâ‚‚ (E â†’SLâ‚šâ‚œ[Ïƒ] F) E :=
  PointwiseConvergenceCLM.seminorm

variable (Ïƒ E F) in
/-- The coercion `E â†’SLâ‚šâ‚œ[Ïƒ] F` to `E â†’ F` as a linear map.

The topology on `E â†’SLâ‚šâ‚œ[Ïƒ] F` is induced by this map. -/
def inducingFn : (E â†’SLâ‚šâ‚œ[Ïƒ] F) â†’â‚—[ğ•œâ‚‚] (E â†’ F) where
  toFun f := f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

variable (Ïƒ E F) in
theorem isInducing_inducingFn : Topology.IsInducing (inducingFn Ïƒ E F) :=
  (PointwiseConvergenceCLM.isEmbedding_coeFn Ïƒ E F).isInducing

lemma withSeminorms : WithSeminorms (PointwiseConvergenceCLM.seminormFamily Ïƒ E F) :=
  let e : E â‰ƒ (Î£ _ : E, Fin 1) := .symm <| .sigmaUnique _ _
  (isInducing_inducingFn Ïƒ E F).withSeminorms <| withSeminorms_pi (fun _ â†¦ norm_withSeminorms ğ•œâ‚‚ F)
    |>.congr_equiv e

end NormedSpace

section IsTopologicalAddGroup

variable [AddCommGroup F] [TopologicalSpace F] [IsTopologicalAddGroup F] [Module ğ•œâ‚‚ F]
  [Semiring R] [PartialOrder R]
  [Module R F] [ContinuousConstSMul R F] [LocallyConvexSpace R F] [SMulCommClass ğ•œâ‚‚ R F]

instance : LocallyConvexSpace R (E â†’SLâ‚šâ‚œ[Ïƒ] F) :=
  UniformConvergenceCLM.locallyConvexSpace R {(s : Set E) | Set.Finite s} âŸ¨âˆ…, Set.finite_emptyâŸ©
    (directedOn_of_sup_mem fun _ _ => Set.Finite.union)

end IsTopologicalAddGroup

end PointwiseConvergenceCLM
