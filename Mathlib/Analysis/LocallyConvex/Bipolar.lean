/-
Copyright (c) 2025 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/

import Mathlib.Analysis.LocallyConvex.Polar
import Mathlib.Analysis.LocallyConvex.WeakDual
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.Analysis.NormedSpace.HahnBanach.Separation

/-!

# Bipolar Theorem

## Main statements

- `LinearMap.flip_polar_polar_eq`: The Bipolar Theorem: The bipolar of a set coincides with its
  closed absolutely convex hull.

## References

* [Conway, *A course in functional analysis*][conway1990]

## Tags

bipolar, locally convex space
-/

variable {ğ•œ E F : Type*}

namespace LinearMap

section

variable [NontriviallyNormedField ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]

variable {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (s : Set E)

example (h : SeparatingLeft B) : B.polar_gc.closureOperator (âˆ… : Set E) = {0} := by
  simp only [GaloisConnection.closureOperator_apply, Function.comp_apply, polar_empty,
    OrderDual.ofDual_toDual, (B.flip.polar_univ h)]

end


section RCLike

variable [RCLike ğ•œ] [AddCommGroup E] [AddCommGroup F]
variable [Module ğ•œ E] [Module ğ•œ F]

variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

variable [Module â„ E] [IsScalarTower â„ ğ•œ E]

/-
The Bipolar Theorem: The bipolar of a set coincides with its closed absolutely convex hull.
[Conway, *A course in functional analysis*, Chapter V. 1.8][conway1990]
-/
open scoped ComplexConjugate
theorem flip_polar_polar_eq {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} {s : Set E} [Nonempty s] :
    B.flip.polar (B.polar s) = closedAbsConvexHull (E := WeakBilin B) ğ•œ s := by
  refine subset_antisymm ?_ <| closedAbsConvexHull_min (E := WeakBilin B)
    (subset_bipolar B s) (polar_absConvex _) (polar_isClosed B.flip _)
  rw [â† Set.compl_subset_compl]
  -- Let `x` be an element not in `(closedAbsConvexHull ğ•œ) s`
  intro x hx
  -- Use the Geometric Hahn-Banach theorem to obtain a function `f` and a constant `u` separating
  -- `(closedAbsConvexHull ğ•œ) s` and `x`
  obtain âŸ¨f, âŸ¨u, âŸ¨hfâ‚, hfâ‚‚âŸ©âŸ©âŸ© :=
    RCLike.geometric_hahn_banach_closed_point (ğ•œ := ğ•œ) (E := WeakBilin B)
      absConvex_convexClosedHull.2 isClosed_closedAbsConvexHull hx
  -- `0` is in `(closedAbsConvexHull ğ•œ) s` so `u` must be strictly positive
  have e3 : RCLike.re (f 0) < u :=
    (hfâ‚ 0) (absConvexHull_subset_closedAbsConvexHull zero_mem_absConvexHull)
  rw [map_zero, map_zero] at e3
  -- Rescale `f` as `g` in order that for all `a` in `(closedAbsConvexHull ğ•œ) s` `Re (g a) < 1`
  set g := (1/u : â„) â€¢ f with fg
  have fg2 : u â€¢ g = f := by
    rw [fg, one_div, â† smul_assoc, smul_eq_mul, mul_inv_cancelâ‚€ (ne_of_lt e3).symm, one_smul]
  have hgâ‚ {a : _} (ha : a âˆˆ (closedAbsConvexHull (E := WeakBilin B) ğ•œ) s) :
    RCLike.re (g a) < 1 := by
    rw [fg, ContinuousLinearMap.coe_smul', Pi.smul_apply, RCLike.smul_re, one_div,
      â† (inv_mul_cancelâ‚€ (lt_iff_le_and_ne.mp e3).2.symm)]
    exact mul_lt_mul_of_pos_left ((hfâ‚ _) ha) (inv_pos_of_pos e3)
  -- The dual embedding is surjective, let `fâ‚€` be the element of `F` corresponding to `g`
  obtain âŸ¨fâ‚€, hfâ‚€âŸ© := B.dualEmbedding_surjective g
  -- Then, by construction, `fâ‚€` is in the polar of `s`
  have hgâ‚ƒ : fâ‚€ âˆˆ (B.polar (E := WeakBilin B) s) := by
    simp only [â† hfâ‚€, WeakBilin.eval, coe_mk, AddHom.coe_mk, ContinuousLinearMap.coe_mk'] at hgâ‚
    intro xâ‚‚ hxâ‚‚
    let l := conj (B xâ‚‚ fâ‚€) / â€–B xâ‚‚ fâ‚€â€–
    have lnorm : â€–lâ€– â‰¤ 1 := by
      rw [norm_div, RCLike.norm_conj, norm_algebraMap', norm_norm]
      exact div_self_le_one _
    have i1 : RCLike.re ((B.flip fâ‚€) (l â€¢ xâ‚‚)) â‰¤ 1 := le_of_lt <| hgâ‚
      <| balanced_iff_smul_mem.mp absConvex_convexClosedHull.1 lnorm
        (subset_closedAbsConvexHull hxâ‚‚)
    rwa [CompatibleSMul.map_smul, smul_eq_mul, mul_comm, â† mul_div_assoc, LinearMap.flip_apply,
      RCLike.mul_conj, sq, mul_self_div_self, RCLike.ofReal_re] at i1
  -- and `1 < Re (B x fâ‚€)`
  have one_lt_x_fâ‚€ : 1 < RCLike.re (B x fâ‚€) := by
    rw [â† one_lt_inv_mulâ‚€ e3] at hfâ‚‚
    suffices uâ»Â¹ * RCLike.re (f x) = RCLike.re ((B x) fâ‚€) by exact lt_of_lt_of_eq hfâ‚‚ this
    rw [â† RCLike.re_ofReal_mul]
    congr
    simp only [map_invâ‚€, â† fg2, â† hfâ‚€, WeakBilin.eval, coe_mk, AddHom.coe_mk,
      ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_mk', Pi.smul_apply,
      Algebra.mul_smul_comm]
    rw [â† smul_eq_mul, â† smul_assoc]
    norm_cast
    have unz : u â‰  0 := (ne_of_lt e3).symm
    aesop
  -- From which it follows that `x` can't be in the bipolar of `s`
  by_contra hc
  rw [Set.mem_compl_iff, not_not] at hc
  exact ((lt_iff_le_not_ge.mp one_lt_x_fâ‚€).2)
    (Preorder.le_trans (RCLike.re ((B x) fâ‚€)) â€–(B x) fâ‚€â€– 1
      (RCLike.re_le_norm ((B x) fâ‚€)) (hc fâ‚€ hgâ‚ƒ))

/-
This fails when `s` is empty. Indeed, `closedAbsConvexHull (E := WeakBilin B) ğ•œ s` is the empty set,
but `B.polar_gc.closureOperator s` equals `{0}` when `B` is left separating.
-/
lemma closureOperator_polar_gc_nonempty {s : Set E} [Nonempty s] :
    B.polar_gc.closureOperator s = closedAbsConvexHull (E := WeakBilin B) ğ•œ s := by
  simp [flip_polar_polar_eq]

end RCLike

end LinearMap
