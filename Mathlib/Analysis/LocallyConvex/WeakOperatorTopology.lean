/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.NormedSpace.HahnBanach.SeparatingDual

/-!
# The weak operator topology

This file defines a type copy of `E â†’L[ğ•œ] F` (where `E` and `F` are topological vector spaces)
which is endowed with the weak operator topology (WOT) rather than the topology of bounded
convergence (which is the usual one induced by the operator norm in the normed setting).
The WOT is defined as the coarsest topology such that the functional `fun A => y (A x)` is
continuous for any `x : E` and `y : F â†’L[ğ•œ] ğ•œ`. Equivalently, a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `y (f a x)` tends to `y (A x)` along the same filter.

Basic non-topological properties of `E â†’L[ğ•œ] F` (such as the module structure) are copied over to
the type copy.

We also prove that the WOT is induced by the family of seminorms `â€–y (A x)â€–` for `x : E` and
`y : F â†’L[ğ•œ] ğ•œ`.

## Main declarations

* `ContinuousLinearMapWOT ğ•œ E F`: The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator
  topology.
* `ContinuousLinearMapWOT.tendsto_iff_forall_dual_apply_tendsto`: a function `f` tends to
  `A : E â†’WOT[ğ•œ] F` along filter `l` iff `y ((f a) x)` tends to `y (A x)` along the same filter.
* `ContinuousLinearMap.toWOT`: the inclusion map from `E â†’L[ğ•œ] F` to the type copy
* `ContinuousLinearMap.continuous_toWOT`: the inclusion map is continuous, i.e. the WOT is coarser
  than the norm topology.
* `ContinuousLinearMapWOT.withSeminorms`: the WOT is induced by the family of seminorms
  `â€–y (A x)â€–` for `x : E` and `y : F â†’L[ğ•œ] ğ•œ`.

## Notation

* The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology is denoted by
  `E â†’WOT[ğ•œ] F`.
* We locally use the notation `Fâ‹†` for `F â†’L[ğ•œ] ğ•œ`.

## Implementation notes

In most of the literature, the WOT is defined on maps between Banach spaces. Here, we only assume
that the domain and codomains are topological vector spaces over a normed field.
-/

open Topology

/-- The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology, denoted as
`E â†’WOT[ğ•œ] F`. -/
@[irreducible]
def ContinuousLinearMapWOT (ğ•œ : Type*) (E : Type*) (F : Type*) [Semiring ğ•œ] [AddCommGroup E]
    [TopologicalSpace E] [Module ğ•œ E] [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F] :=
  E â†’L[ğ•œ] F

@[inherit_doc]
notation:25 E " â†’WOT[" ğ•œ "] " F => ContinuousLinearMapWOT ğ•œ E F

namespace ContinuousLinearMapWOT

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [NormedField ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F]

local notation X "â‹†" => X â†’L[ğ•œ] ğ•œ

/-!
### Basic properties common with `E â†’L[ğ•œ] F`

The section copies basic non-topological properties of `E â†’L[ğ•œ] F` over to `E â†’WOT[ğ•œ] F`, such as
the module structure, `FunLike`, etc.
-/
section Basic

/-
Warning : Due to the irreducibility of `ContinuousLinearMapWOT`, one has to be careful when
declaring instances with data. For example, adding
```
unseal ContinuousLinearMapWOT in
instance instAddCommMonoid [ContinuousAdd F] : AddCommMonoid (E â†’WOT[ğ•œ] F) :=
  inferInstanceAs <| AddCommMonoid (E â†’L[ğ•œ] F)
```
would cause the following to fail :
```
example [IsTopologicalAddGroup F] :
  (instAddCommMonoid : AddCommMonoid (E â†’WOT[ğ•œ] F)) =
    instAddCommGroup.toAddCommMonoid := rfl
```
-/

unseal ContinuousLinearMapWOT in
instance instAddCommGroup [IsTopologicalAddGroup F] : AddCommGroup (E â†’WOT[ğ•œ] F) :=
  inferInstanceAs <| AddCommGroup (E â†’L[ğ•œ] F)

unseal ContinuousLinearMapWOT in
instance instModule [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F] : Module ğ•œ (E â†’WOT[ğ•œ] F) :=
  inferInstanceAs <| Module ğ•œ (E â†’L[ğ•œ] F)

variable (ğ•œ) (E) (F) [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]

unseal ContinuousLinearMapWOT in
/-- The linear equivalence that sends a continuous linear map to the type copy endowed with the
weak operator topology. -/
def _root_.ContinuousLinearMap.toWOT :
    (E â†’L[ğ•œ] F) â‰ƒâ‚—[ğ•œ] (E â†’WOT[ğ•œ] F) :=
  LinearEquiv.refl ğ•œ _

variable {ğ•œ} {E} {F}

instance instFunLike : FunLike (E â†’WOT[ğ•œ] F) E F where
  coe f :=  ((ContinuousLinearMap.toWOT ğ•œ E F).symm f : E â†’ F)
  coe_injective' := by intro; simp

instance instContinuousLinearMapClass : ContinuousLinearMapClass (E â†’WOT[ğ•œ] F) ğ•œ E F where
  map_add f x y := by simp only [DFunLike.coe]; simp
  map_smulâ‚›â‚— f r x := by simp only [DFunLike.coe]; simp
  map_continuous f := ContinuousLinearMap.continuous ((ContinuousLinearMap.toWOT ğ•œ E F).symm f)

lemma _root_.ContinuousLinearMap.toWOT_apply {A : E â†’L[ğ•œ] F} {x : E} :
    ((ContinuousLinearMap.toWOT ğ•œ E F) A) x = A x := rfl

unseal ContinuousLinearMapWOT in
lemma ext {A B : E â†’WOT[ğ•œ] F} (h : âˆ€ x, A x = B x) : A = B := ContinuousLinearMap.ext h

unseal ContinuousLinearMapWOT in
lemma ext_iff {A B : E â†’WOT[ğ•œ] F} : A = B â†” âˆ€ x, A x = B x := ContinuousLinearMap.ext_iff

-- This `ext` lemma is set at a lower priority than the default of 1000, so that the
-- version with an inner product (`ContinuousLinearMapWOT.ext_inner`) takes precedence
-- in the case of Hilbert spaces.
@[ext 900]
lemma ext_dual [H : SeparatingDual ğ•œ F] {A B : E â†’WOT[ğ•œ] F}
    (h : âˆ€ x (y : Fâ‹†), y (A x) = y (B x)) : A = B := by
  simp_rw [ext_iff, â† (separatingDual_iff_injective.mp H).eq_iff, LinearMap.ext_iff]
  exact h

@[simp] lemma zero_apply (x : E) : (0 : E â†’WOT[ğ•œ] F) x = 0 := by simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma add_apply {f g : E â†’WOT[ğ•œ] F} (x : E) : (f + g) x = f x + g x := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma sub_apply {f g : E â†’WOT[ğ•œ] F} (x : E) : (f - g) x = f x - g x := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma neg_apply {f : E â†’WOT[ğ•œ] F} (x : E) : (-f) x = -(f x) := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma smul_apply {f : E â†’WOT[ğ•œ] F} (c : ğ•œ) (x : E) : (c â€¢ f) x = c â€¢ (f x) := by
  simp only [DFunLike.coe]; rfl

end Basic

/-!
### The topology of `E â†’WOT[ğ•œ] F`

The section endows `E â†’WOT[ğ•œ] F` with the weak operator topology and shows the basic properties
of this topology. In particular, we show that it is a topological vector space.
-/
section Topology

variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]

variable (ğ•œ) (E) (F) in
/-- The function that induces the topology on `E â†’WOT[ğ•œ] F`, namely the function that takes
an `A` and maps it to `fun âŸ¨x, yâŸ© => y (A x)` in `E Ã— Fâ‹† â†’ ğ•œ`, bundled as a linear map to make
it easier to prove that it is a TVS. -/
def inducingFn : (E â†’WOT[ğ•œ] F) â†’â‚—[ğ•œ] (E Ã— Fâ‹† â†’ ğ•œ) where
  toFun := fun A âŸ¨x, yâŸ© => y (A x)
  map_add' := fun x y => by ext; simp
  map_smul' := fun x y => by ext; simp

@[simp]
lemma inducingFn_apply {f : E â†’WOT[ğ•œ] F} {x : E} {y : Fâ‹†} :
    inducingFn ğ•œ E F f (x, y) = y (f x) :=
  rfl

/-- The weak operator topology is the coarsest topology such that `fun A => y (A x)` is
continuous for all `x, y`. -/
instance instTopologicalSpace : TopologicalSpace (E â†’WOT[ğ•œ] F) :=
  .induced (inducingFn _ _ _) Pi.topologicalSpace

@[fun_prop]
lemma continuous_inducingFn : Continuous (inducingFn ğ•œ E F) :=
  continuous_induced_dom

lemma continuous_dual_apply (x : E) (y : Fâ‹†) : Continuous fun (A : E â†’WOT[ğ•œ] F) => y (A x) := by
  refine (continuous_pi_iff.mp continuous_inducingFn) âŸ¨x, yâŸ©

@[fun_prop]
lemma continuous_of_dual_apply_continuous {Î± : Type*} [TopologicalSpace Î±] {g : Î± â†’ E â†’WOT[ğ•œ] F}
    (h : âˆ€ x (y : Fâ‹†), Continuous fun a => y (g a x)) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr fun p => h p.1 p.2)

lemma isInducing_inducingFn : IsInducing (inducingFn ğ•œ E F) := âŸ¨rflâŸ©

lemma isEmbedding_inducingFn [SeparatingDual ğ•œ F] : IsEmbedding (inducingFn ğ•œ E F) := by
  refine Function.Injective.isEmbedding_induced fun A B hAB => ?_
  rw [ContinuousLinearMapWOT.ext_dual_iff]
  simpa [funext_iff] using hAB

@[deprecated (since := "2024-10-26")]
alias embedding_inducingFn := isEmbedding_inducingFn

open Filter in
/-- The defining property of the weak operator topology: a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `y (f a x)` tends to `y (A x)` along the same filter. -/
lemma tendsto_iff_forall_dual_apply_tendsto {Î± : Type*} {l : Filter Î±} {f : Î± â†’ E â†’WOT[ğ•œ] F}
    {A : E â†’WOT[ğ•œ] F} :
    Tendsto f l (ğ“ A) â†” âˆ€ x (y : Fâ‹†), Tendsto (fun a => y (f a x)) l (ğ“ (y (A x))) := by
  simp [isInducing_inducingFn.tendsto_nhds_iff, tendsto_pi_nhds]

lemma le_nhds_iff_forall_dual_apply_le_nhds {l : Filter (E â†’WOT[ğ•œ] F)} {A : E â†’WOT[ğ•œ] F} :
    l â‰¤ ğ“ A â†” âˆ€ x (y : Fâ‹†), l.map (fun T => y (T x)) â‰¤ ğ“ (y (A x)) :=
  tendsto_iff_forall_dual_apply_tendsto (f := id)

instance instT3Space [SeparatingDual ğ•œ F] : T3Space (E â†’WOT[ğ•œ] F) := isEmbedding_inducingFn.t3Space

instance instContinuousAdd : ContinuousAdd (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)
instance instContinuousNeg : ContinuousNeg (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)
instance instContinuousSMul : ContinuousSMul ğ•œ (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)

#adaptation_note /-- 2025-03-29 lean4#7717 Needed to add these instances explicitly to avoid a
limitation with parent instance inference. TODO(kmill): fix this. -/
instance instIsTopologicalAddGroup : IsTopologicalAddGroup (E â†’WOT[ğ•œ] F) where
  toContinuousAdd := inferInstance
  toContinuousNeg := inferInstance

instance instUniformSpace : UniformSpace (E â†’WOT[ğ•œ] F) := .comap (inducingFn ğ•œ E F) inferInstance

instance instIsUniformAddGroup : IsUniformAddGroup (E â†’WOT[ğ•œ] F) := .comap (inducingFn ğ•œ E F)

@[deprecated (since := "2025-03-31")] alias instUniformAddGroup :=
  ContinuousLinearMapWOT.instIsUniformAddGroup

end Topology

/-! ### The WOT is induced by a family of seminorms -/
section Seminorms

variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]

/-- The family of seminorms that induce the weak operator topology, namely `â€–y (A x)â€–` for
all `x` and `y`. -/
def seminorm (x : E) (y : Fâ‹†) : Seminorm ğ•œ (E â†’WOT[ğ•œ] F) where
  toFun A := â€–y (A x)â€–
  map_zero' := by simp
  add_le' A B := by simpa using norm_add_le _ _
  neg' A := by simp
  smul' r A := by simp

variable (ğ•œ) (E) (F) in
/-- The family of seminorms that induce the weak operator topology, namely `â€–y (A x)â€–` for
all `x` and `y`. -/
def seminormFamily : SeminormFamily ğ•œ (E â†’WOT[ğ•œ] F) (E Ã— Fâ‹†) :=
  fun âŸ¨x, yâŸ© => seminorm x y

lemma withSeminorms : WithSeminorms (seminormFamily ğ•œ E F) :=
  let e : E Ã— Fâ‹† â‰ƒ (Î£ _ : E Ã— Fâ‹†, Fin 1) := .symm <| .sigmaUnique _ _
  have : Nonempty (Î£ _ : E Ã— Fâ‹†, Fin 1) := e.symm.nonempty
  isInducing_inducingFn.withSeminorms <| withSeminorms_pi (fun _ â†¦ norm_withSeminorms ğ•œ ğ•œ)
    |>.congr_equiv e

lemma hasBasis_seminorms : (ğ“ (0 : E â†’WOT[ğ•œ] F)).HasBasis (seminormFamily ğ•œ E F).basisSets id :=
  withSeminorms.hasBasis

instance instLocallyConvexSpace [NormedSpace â„ ğ•œ] [Module â„ (E â†’WOT[ğ•œ] F)]
    [IsScalarTower â„ ğ•œ (E â†’WOT[ğ•œ] F)] :
    LocallyConvexSpace â„ (E â†’WOT[ğ•œ] F) :=
  withSeminorms.toLocallyConvexSpace

end Seminorms

section toWOT_continuous

variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F] [ContinuousSMul ğ•œ E]

/-- The weak operator topology is coarser than the bounded convergence topology, i.e. the inclusion
map is continuous. -/
@[continuity, fun_prop]
lemma ContinuousLinearMap.continuous_toWOT :
    Continuous (ContinuousLinearMap.toWOT ğ•œ E F) :=
  ContinuousLinearMapWOT.continuous_of_dual_apply_continuous fun x y â†¦
    y.cont.comp <| continuous_eval_const x

/-- The inclusion map from `E â†’[ğ•œ] F` to `E â†’WOT[ğ•œ] F`, bundled as a continuous linear map. -/
def ContinuousLinearMap.toWOTCLM : (E â†’L[ğ•œ] F) â†’L[ğ•œ] (E â†’WOT[ğ•œ] F) :=
  âŸ¨LinearEquiv.toLinearMap (ContinuousLinearMap.toWOT ğ•œ E F), ContinuousLinearMap.continuous_toWOTâŸ©

end toWOT_continuous

end ContinuousLinearMapWOT
