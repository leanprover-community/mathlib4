/-
Copyright (c) 2026 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
module

public import Mathlib.Analysis.Asymptotics.TVS
public import Mathlib.Analysis.LocallyConvex.WithSeminorms

/-!
# TODO
-/

@[expose] public section

open scoped NNReal
open Filter

variable {Î¹ Îº Î± ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F]
variable {f fâ‚ fâ‚‚ : Î± â†’ E} {g gâ‚ gâ‚‚ : Î± â†’ F} {l : Filter Î±}

namespace PolynormableSpace

variable [PolynormableSpace ğ•œ E] [PolynormableSpace ğ•œ F]

theorem isBigOTVS_iff_le :
    f =O[ğ•œ; l] g â†” âˆ€ p : Seminorm ğ•œ E, Continuous p â†’ âˆƒ q : Seminorm ğ•œ F,
      Continuous q âˆ§ p âˆ˜ f â‰¤á¶ [l] q âˆ˜ g := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  rw [(PolynormableSpace.hasBasis_zero_ball ğ•œ E).isBigOTVS_iff
      (PolynormableSpace.hasBasis_zero_ball ğ•œ F)]
  constructor <;>
  intro H p p_cont <;>
  obtain âŸ¨q, q_cont, hqâŸ© := H p p_cont <;>
  refine âŸ¨â€–câ€–â‚Š â€¢ q, q_cont.const_smul _, hq.mono fun x hx â†¦ ?_âŸ©
  Â· suffices (p (f x)).toNNReal â‰¤ â€–câ€–â‚‘ * (q (g x)).toNNReal by
      simpa (discharger := positivity) [NNReal.smul_def, â† Real.toNNReal_le_toNNReal_iff,
        â† ENNReal.coe_le_coe, Real.toNNReal_mul]
    calc  â†‘(p (f x)).toNNReal
      _ â‰¤ egauge ğ•œ (p.ball 0 1) (f x) := p.le_egauge_ball_one _
      _ â‰¤ egauge ğ•œ (q.ball 0 1) (g x) := hx
      _ â‰¤ â€–câ€–â‚‘ * (q (g x)).toNNReal := q.egauge_ball_one_le_of_one_lt_norm hc _
  Â· calc  egauge ğ•œ (p.ball 0 1) (f x)
      _ â‰¤ â€–câ€–â‚‘ * (p (f x)).toNNReal := p.egauge_ball_one_le_of_one_lt_norm hc _
      _ â‰¤ â€–câ€–â‚‘ * (q (g x)).toNNReal := by gcongr; exact hx
      _ = ((â€–câ€–â‚Š â€¢ q) (g x)).toNNReal := by
            simp [NNReal.smul_def, Real.toNNReal_mul, enorm_eq_nnnorm]
      _ â‰¤ egauge ğ•œ ((â€–câ€–â‚Š â€¢ q).ball 0 1) (g x) := (â€–câ€–â‚Š â€¢ q).le_egauge_ball_one _

theorem isBigOTVS_iff :
    f =O[ğ•œ; l] g â†” âˆ€ p : Seminorm ğ•œ E, Continuous p â†’ âˆƒ q : Seminorm ğ•œ F,
      Continuous q âˆ§ (p âˆ˜ f) =O[l] (q âˆ˜ g) := by
  simp_rw [isBigOTVS_iff_le, Filter.EventuallyLE]
  congrm âˆ€ p p_cont, ?_
  constructor <;> rintro âŸ¨q, q_cont, hqâŸ©
  Â· exact âŸ¨q, q_cont, .of_bound' <| by simpa (discharger := positivity) [abs_of_nonneg]âŸ©
  Â· rw [Asymptotics.isBigO_iff'] at hq
    rcases hq with âŸ¨C, C_pos, hCâŸ©
    simp (discharger := positivity) only [Function.comp_apply, Real.norm_of_nonneg] at hC
    refine âŸ¨C.toNNReal â€¢ q, q_cont.const_smul _, ?_âŸ©
    simpa [NNReal.smul_def, C_pos.le]

theorem isLittleOTVS_iff_le :
    f =o[ğ•œ; l] g â†” âˆ€ p : Seminorm ğ•œ E, Continuous p â†’ âˆƒ q : Seminorm ğ•œ F,
      Continuous q âˆ§ âˆ€ Îµ : â„â‰¥0, Îµ â‰  0 â†’ p âˆ˜ f â‰¤á¶ [l] (Îµ â€¢ q) âˆ˜ g := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  rw [(PolynormableSpace.hasBasis_zero_ball ğ•œ E).isLittleOTVS_iff
      (PolynormableSpace.hasBasis_zero_ball ğ•œ F)]
  constructor <;>
  intro H p p_cont <;>
  obtain âŸ¨q, q_cont, hqâŸ© := H p p_cont <;>
  refine âŸ¨â€–câ€–â‚Š â€¢ q, q_cont.const_smul _, fun Îµ hÎµ â†¦ (hq Îµ hÎµ).mono fun x hx â†¦ ?_âŸ©
  Â· suffices (p (f x)).toNNReal â‰¤ Îµ * â€–câ€–â‚‘ * (q (g x)).toNNReal by
      simpa (discharger := positivity) [NNReal.smul_def, â† Real.toNNReal_le_toNNReal_iff,
        â† ENNReal.coe_le_coe, Real.toNNReal_mul, mul_assoc]
    calc  â†‘(p (f x)).toNNReal
      _ â‰¤ egauge ğ•œ (p.ball 0 1) (f x) := p.le_egauge_ball_one _
      _ â‰¤ Îµ * egauge ğ•œ (q.ball 0 1) (g x) := hx
      _ â‰¤ Îµ * â€–câ€–â‚‘ * (q (g x)).toNNReal := by
            grw [mul_assoc, q.egauge_ball_one_le_of_one_lt_norm hc _]
  Â· calc  egauge ğ•œ (p.ball 0 1) (f x)
      _ â‰¤ â€–câ€–â‚‘ * (p (f x)).toNNReal := p.egauge_ball_one_le_of_one_lt_norm hc _
      _ â‰¤ â€–câ€–â‚‘ * ((Îµ â€¢ q) (g x)).toNNReal := by gcongr; exact hx
      _ = Îµ * ((â€–câ€–â‚Š â€¢ q) (g x)).toNNReal := by
            simp [NNReal.smul_def, Real.toNNReal_mul, enorm_eq_nnnorm, â† mul_assoc, mul_comm]
      _ â‰¤ Îµ * egauge ğ•œ ((â€–câ€–â‚Š â€¢ q).ball 0 1) (g x) := by
            grw [(â€–câ€–â‚Š â€¢ q).le_egauge_ball_one _]

theorem isLittleOTVS_iff :
    f =o[ğ•œ; l] g â†” âˆ€ p : Seminorm ğ•œ E, Continuous p â†’ âˆƒ q : Seminorm ğ•œ F,
      Continuous q âˆ§ (p âˆ˜ f) =o[l] (q âˆ˜ g) := by
  simp_rw [isLittleOTVS_iff_le, Filter.EventuallyLE, Asymptotics.isLittleO_iff]
  congrm âˆ€ p p_cont, âˆƒ q, _ âˆ§ ?_
  constructor <;> intro H Îµ hÎµ
  Â· have : (âŸ¨Îµ, hÎµ.leâŸ© : â„â‰¥0) â‰  0 := by simpa [â† NNReal.coe_ne_zero] using hÎµ.ne'
    simpa (discharger := positivity) [abs_of_nonneg] using H âŸ¨Îµ, hÎµ.leâŸ© this
  Â· simp (discharger := positivity) only [Function.comp_apply, Real.norm_of_nonneg] at H
    exact @H Îµ (by positivity)

end PolynormableSpace

namespace WithSeminorms

variable {p : SeminormFamily ğ•œ E Î¹} {q : SeminormFamily ğ•œ F Îº}

theorem isBigOTVS_iff_le (hp : WithSeminorms p) (hq : WithSeminorms q) :
    f =O[ğ•œ; l] g â†” âˆ€ i : Î¹, âˆƒ s : Finset Îº, âˆƒ C : â„â‰¥0, p i âˆ˜ f â‰¤á¶ [l] ((C â€¢ s.sup q) âˆ˜ g) := by
  have := hp.toPolynormableSpace
  have := hq.toPolynormableSpace
  rw [PolynormableSpace.isBigOTVS_iff_le]
  constructor <;> intro H
  Â· intro i
    obtain âŸ¨r, r_cont, hrâŸ© := H (p i) (hp.continuous_seminorm i)
    obtain âŸ¨s, C, -, hCâŸ© := Seminorm.bound_of_continuous hq r r_cont
    exact âŸ¨s, C, hr.mono fun x hx â†¦ hx.trans (hC _)âŸ©
  Â· intro r r_cont
    refine Seminorm.induction_of_continuous hp ?_ ?_ ?_ ?_ ?_ r_cont
    Â· intro i
      obtain âŸ¨s, C, hCâŸ© := H i
      refine âŸ¨_, ?_, hCâŸ©
      exact (Seminorm.continuous_finset_sup fun i _ â†¦ hq.continuous_seminorm i).const_smul _
    Â· exact âŸ¨0, continuous_zero, .rflâŸ©
    Â· rintro râ‚ râ‚‚ âŸ¨sâ‚, sâ‚_cont, hâ‚âŸ© âŸ¨sâ‚‚, sâ‚‚_cont, hâ‚‚âŸ©
      use sâ‚ âŠ” sâ‚‚, by fun_prop
      filter_upwards [hâ‚, hâ‚‚] with x hâ‚ hâ‚‚ using sup_le_sup hâ‚ hâ‚‚
    Â· rintro râ‚ râ‚‚ h âŸ¨s, s_cont, hsâŸ©
      exact âŸ¨s, s_cont, hs.mono fun x â†¦ (h _).transâŸ©
    Â· rintro r C âŸ¨s, s_cont, hsâŸ©
      refine âŸ¨C â€¢ s, s_cont.const_smul _, hs.mono fun x hx â†¦ ?_âŸ©
      apply _root_.smul_le_smul le_rfl hx <;> simp

theorem isBigOTVS_iff (hp : WithSeminorms p) (hq : WithSeminorms q) :
    f =O[ğ•œ; l] g â†” âˆ€ i : Î¹, âˆƒ s : Finset Îº, (p i âˆ˜ f) =O[l] ((s.sup q : Seminorm ğ•œ F) âˆ˜ g) := by
  simp_rw [hp.isBigOTVS_iff_le hq, Filter.EventuallyLE]
  congrm âˆ€ i, âˆƒ s, ?_
  constructor
  Â· rintro âŸ¨C, hCâŸ©
    exact .of_bound C <| by simpa (discharger := positivity) [abs_of_nonneg]
  Â· rw [Asymptotics.isBigO_iff']
    rintro âŸ¨C, C_pos, hCâŸ©
    simp (discharger := positivity) only [Function.comp_apply, Real.norm_of_nonneg] at hC
    refine âŸ¨C.toNNReal, ?_âŸ©
    simpa [NNReal.smul_def, C_pos.le]

theorem isLittleOTVS_iff_le (hp : WithSeminorms p) (hq : WithSeminorms q) :
    f =o[ğ•œ; l] g â†”
      âˆ€ i : Î¹, âˆƒ s : Finset Îº, âˆ€ Îµ : â„â‰¥0, Îµ â‰  0 â†’ p i âˆ˜ f â‰¤á¶ [l] ((Îµ â€¢ s.sup q) âˆ˜ g) := by
  have := hp.toPolynormableSpace
  have := hq.toPolynormableSpace
  rw [PolynormableSpace.isLittleOTVS_iff_le]
  constructor <;> intro H
  Â· intro i
    obtain âŸ¨r, r_cont, hrâŸ© := H (p i) (hp.continuous_seminorm i)
    obtain âŸ¨s, C, C_ne, hCâŸ© := Seminorm.bound_of_continuous hq r r_cont
    refine âŸ¨s, fun Îµ Îµ_pos â†¦ (hr (Îµ/C) (by positivity)).mono fun x hx â†¦ ?_âŸ©
    simp only [Function.comp_apply, Seminorm.le_def, Seminorm.smul_apply] at hx hC âŠ¢
    grw [hx, hC _, â† mul_smul, div_mul_cancelâ‚€ _ C_ne]
  Â· intro r r_cont
    refine Seminorm.induction_of_continuous hp ?_ ?_ ?_ ?_ ?_ r_cont
    Â· intro i
      obtain âŸ¨s, hsâŸ© := H i
      exact âŸ¨_, Seminorm.continuous_finset_sup fun i _ â†¦ hq.continuous_seminorm i, hsâŸ©
    Â· exact âŸ¨0, continuous_zero, fun _ _ â†¦ by simpa using .rflâŸ©
    Â· rintro râ‚ râ‚‚ âŸ¨sâ‚, sâ‚_cont, hâ‚âŸ© âŸ¨sâ‚‚, sâ‚‚_cont, hâ‚‚âŸ©
      refine âŸ¨sâ‚ âŠ” sâ‚‚, by fun_prop, fun Îµ Îµ_ne â†¦ ?_âŸ©
      rw [Seminorm.smul_sup]
      filter_upwards [hâ‚ Îµ Îµ_ne, hâ‚‚ Îµ Îµ_ne] with x hâ‚ hâ‚‚ using sup_le_sup hâ‚ hâ‚‚
    Â· rintro râ‚ râ‚‚ h âŸ¨s, s_cont, hsâŸ©
      exact âŸ¨s, s_cont, fun Îµ Îµ_pos â†¦ (hs Îµ Îµ_pos).mono fun x â†¦ (h _).transâŸ©
    Â· rintro r C âŸ¨s, s_cont, hsâŸ©
      refine âŸ¨C â€¢ s, s_cont.const_smul _, fun Îµ Îµ_pos â†¦ (hs Îµ Îµ_pos).mono fun x hx â†¦ ?_âŸ©
      rw [smul_comm]
      exact _root_.smul_le_smul le_rfl hx (by positivity) (apply_nonneg _ _)

theorem isLittleOTVS_iff (hp : WithSeminorms p) (hq : WithSeminorms q) :
    f =o[ğ•œ; l] g â†” âˆ€ i : Î¹, âˆƒ s : Finset Îº, (p i âˆ˜ f) =o[l] ((s.sup q : Seminorm ğ•œ F) âˆ˜ g) := by
  simp_rw [hp.isLittleOTVS_iff_le hq, Filter.EventuallyLE, Asymptotics.isLittleO_iff]
  congrm âˆ€ i, âˆƒ s, ?_
  constructor <;> intro H Îµ hÎµ
  Â· have : (âŸ¨Îµ, hÎµ.leâŸ© : â„â‰¥0) â‰  0 := by simpa [â† NNReal.coe_ne_zero] using hÎµ.ne'
    simpa (discharger := positivity) [abs_of_nonneg] using H âŸ¨Îµ, hÎµ.leâŸ© this
  Â· simp (discharger := positivity) only [Function.comp_apply, Real.norm_of_nonneg] at H
    exact @H Îµ (by positivity)

end WithSeminorms

end
