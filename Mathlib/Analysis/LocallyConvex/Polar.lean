/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Kalle KytÃ¶lÃ¤
-/
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.LinearAlgebra.SesquilinearForm.Basic
import Mathlib.Topology.Algebra.Module.WeakBilin
import Mathlib.Analysis.LocallyConvex.AbsConvex
import Mathlib.Analysis.Normed.Module.Convex

/-!
# Polar set

In this file we define the polar set. There are different notions of the polar, we will define the
*absolute polar*. The advantage over the real polar is that we can define the absolute polar for
any bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, where `ğ•œ` is a normed commutative ring and
`E` and `F` are modules over `ğ•œ`.

## Main definitions

* `LinearMap.polar`: The polar of a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`.

## Main statements

* `LinearMap.polar_eq_iInter`: The polar as an intersection.
* `LinearMap.subset_bipolar`: The polar is a subset of the bipolar.
* `LinearMap.polar_isClosed`: The polar is closed in the weak topology induced by `B.flip`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

polar
-/

variable {ğ•œ E F : Type*}

open Topology

namespace LinearMap

section NormedRing

variable [NormedCommRing ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- The (absolute) polar of `s : Set E` is given by the set of all `y : F` such that `â€–B x yâ€– â‰¤ 1`
for all `x âˆˆ s`. -/
def polar (s : Set E) : Set F :=
  { y : F | âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 }

theorem polar_mem_iff (s : Set E) (y : F) : y âˆˆ B.polar s â†” âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 :=
  Iff.rfl

theorem polar_mem (s : Set E) (y : F) (hy : y âˆˆ B.polar s) : âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 :=
  hy

theorem polar_eq_biInter_preimage (s : Set E) :
    B.polar s = â‹‚ x âˆˆ s, ((B x) â»Â¹' Metric.closedBall (0 : ğ•œ) 1) := by aesop

-- TODO: this theorem is abusing defeq between F and WeakBilin B.flip
theorem polar_isClosed (s : Set E) : IsClosed (X := WeakBilin B.flip) (B.polar s) := by
  rw [polar_eq_biInter_preimage]
  exact isClosed_biInter
    fun _ _ â†¦ Metric.isClosed_closedBall.preimage (WeakBilin.eval_continuous B.flip _)

@[simp]
theorem zero_mem_polar (s : Set E) : (0 : F) âˆˆ B.polar s := fun _ _ => by
  simp only [map_zero, norm_zero, zero_le_one]

theorem polar_nonempty (s : Set E) : Set.Nonempty (B.polar s) := by
  use 0
  exact zero_mem_polar B s

theorem polar_eq_iInter {s : Set E} : B.polar s = â‹‚ x âˆˆ s, { y : F | â€–B x yâ€– â‰¤ 1 } := by
  ext
  simp only [polar_mem_iff, Set.mem_iInter, Set.mem_setOf_eq]

/-- The map `B.polar : Set E â†’ Set F` forms an order-reversing Galois connection with
`B.flip.polar : Set F â†’ Set E`. We use `OrderDual.toDual` and `OrderDual.ofDual` to express
that `polar` is order-reversing. -/
theorem polar_gc :
    GaloisConnection (OrderDual.toDual âˆ˜ B.polar) (B.flip.polar âˆ˜ OrderDual.ofDual) := fun _ _ =>
  âŸ¨fun h _ hx _ hy => h hy _ hx, fun h _ hx _ hy => h hy _ hxâŸ©

@[simp]
theorem polar_iUnion {Î¹} {s : Î¹ â†’ Set E} : B.polar (â‹ƒ i, s i) = â‹‚ i, B.polar (s i) :=
  B.polar_gc.l_iSup

@[simp]
theorem polar_union {s t : Set E} : B.polar (s âˆª t) = B.polar s âˆ© B.polar t :=
  B.polar_gc.l_sup

theorem polar_antitone : Antitone (B.polar : Set E â†’ Set F) :=
  B.polar_gc.monotone_l

@[simp]
theorem polar_empty : B.polar âˆ… = Set.univ :=
  B.polar_gc.l_bot

@[simp]
theorem polar_singleton {a : E} : B.polar {a} = { y | â€–B a yâ€– â‰¤ 1 } := le_antisymm
  (fun _ hy => hy _ rfl)
  (fun y hy => (polar_mem_iff _ _ _).mp (fun _ hb => by rw [Set.mem_singleton_iff.mp hb]; exact hy))

theorem mem_polar_singleton {x : E} (y : F) : y âˆˆ B.polar {x} â†” â€–B x yâ€– â‰¤ 1 := by
  simp only [polar_singleton, Set.mem_setOf_eq]

theorem polar_zero : B.polar ({0} : Set E) = Set.univ := by
  simp only [polar_singleton, map_zero, zero_apply, norm_zero, zero_le_one, Set.setOf_true]

theorem subset_bipolar (s : Set E) : s âŠ† B.flip.polar (B.polar s) := fun x hx y hy => by
  rw [B.flip_apply]
  exact hy x hx

@[simp]
theorem tripolar_eq_polar (s : Set E) : B.polar (B.flip.polar (B.polar s)) = B.polar s :=
  (B.polar_antitone (B.subset_bipolar s)).antisymm (subset_bipolar B.flip (B.polar s))

@[deprecated (since := "2025-10-06")] alias polar_weak_closed := polar_isClosed

theorem sInter_polar_finite_subset_eq_polar (s : Set E) :
    â‹‚â‚€ (B.polar '' { F | F.Finite âˆ§ F âŠ† s }) = B.polar s := by
  ext x
  simp only [Set.sInter_image, Set.mem_setOf_eq, Set.mem_iInter, and_imp]
  refine âŸ¨fun hx a ha â†¦ ?_, fun hx F _ hFâ‚‚ => polar_antitone _ hFâ‚‚ hxâŸ©
  simpa [mem_polar_singleton] using hx _ (Set.finite_singleton a) (Set.singleton_subset_iff.mpr ha)

end NormedRing

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

theorem polar_univ (h : SeparatingRight B) : B.polar Set.univ = {(0 : F)} := by
  rw [Set.eq_singleton_iff_unique_mem]
  refine âŸ¨by simp only [zero_mem_polar], fun y hy => h _ fun x => ?_âŸ©
  refine norm_le_zero_iff.mp (le_of_forall_gt_imp_ge_of_dense fun Îµ hÎµ => ?_)
  rcases NormedField.exists_norm_lt ğ•œ hÎµ with âŸ¨c, hc, hcÎµâŸ©
  calc
    â€–B x yâ€– = â€–câ€– * â€–B (câ»Â¹ â€¢ x) yâ€– := by
      rw [B.map_smul, LinearMap.smul_apply, Algebra.id.smul_eq_mul, norm_mul, norm_inv,
        mul_inv_cancel_leftâ‚€ hc.ne']
    _ â‰¤ Îµ * 1 := by gcongr; exact hy _ trivial
    _ = Îµ := mul_one _

theorem polar_subMulAction {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) :
    B.polar m = { y | âˆ€ x âˆˆ m, B x y = 0 } := by
  ext y
  constructor
  Â· intro hy x hx
    obtain âŸ¨r, hrâŸ© := NormedField.exists_lt_norm ğ•œ â€–B x yâ€–â»Â¹
    contrapose! hr
    rw [â† one_div, le_div_iffâ‚€ (norm_pos_iff.2 hr)]
    simpa using hy _ (SMulMemClass.smul_mem r hx)
  Â· intro h x hx
    simp [h x hx]

/-- The polar of a set closed under scalar multiplication as a submodule -/
def polarSubmodule {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) : Submodule ğ•œ F :=
  .copy (â¨… x âˆˆ m, LinearMap.ker (B x)) (B.polar m) <| by ext; simp [polar_subMulAction]

end NontriviallyNormedField

end LinearMap

namespace StrongDual

section

variable (R M : Type*) [SeminormedCommRing R] [TopologicalSpace M] [AddCommGroup M] [Module R M]

theorem dualPairing_separatingLeft : (topDualPairing R M).SeparatingLeft := by
  rw [LinearMap.separatingLeft_iff_ker_eq_bot, LinearMap.ker_eq_bot]
  exact ContinuousLinearMap.coe_injective

@[deprecated (since := "2025-08-12")] alias NormedSpace.dualPairing_separatingLeft :=
  dualPairing_separatingLeft

end

section

/-- Given a subset `s` in a monoid `M` (over a commutative ring `R`), the polar `polar R s` is the
subset of `StrongDual R M` consisting of those functionals which evaluate to something of norm at
most one at all points `z âˆˆ s`. -/
def polar (R : Type*) [NormedCommRing R] {M : Type*} [AddCommMonoid M]
    [TopologicalSpace M] [Module R M] : Set M â†’ Set (StrongDual R M) :=
  (topDualPairing R M).flip.polar

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar := polar

/-- Given a subset `s` in a monoid `M` (over a field `ğ•œ`) closed under scalar multiplication,
the polar `polarSubmodule ğ•œ s` is the submodule of `StrongDual ğ•œ M` consisting of those functionals
which evaluate to zero at all points `z âˆˆ s`. -/
def polarSubmodule (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] {M : Type*} [AddCommMonoid M]
    [TopologicalSpace M] [Module ğ•œ M] {S : Type*} [SetLike S M] [SMulMemClass S ğ•œ M] (m : S) :
    Submodule ğ•œ (StrongDual ğ•œ M) := (topDualPairing ğ•œ M).flip.polarSubmodule m

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polarSubmodule := polarSubmodule

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable {E : Type*} [AddCommMonoid E] [TopologicalSpace E] [Module ğ•œ E]

lemma polarSubmodule_eq_polar (m : SubMulAction ğ•œ E) :
    (polarSubmodule ğ•œ m : Set (StrongDual ğ•œ E)) = polar ğ•œ m := rfl

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polarSubmodule_eq_polar :=
  polarSubmodule_eq_polar

theorem mem_polar_iff {x' : StrongDual ğ•œ E} (s : Set E) : x' âˆˆ polar ğ•œ s â†” âˆ€ z âˆˆ s, â€–x' zâ€– â‰¤ 1 :=
  Iff.rfl

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.mem_polar_iff := mem_polar_iff

lemma polarSubmodule_eq_setOf {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) :
    polarSubmodule ğ•œ m = { y : StrongDual ğ•œ E | âˆ€ x âˆˆ m, y x = 0 } :=
  (topDualPairing ğ•œ E).flip.polar_subMulAction _

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polarSubmodule_eq_setOf :=
  polarSubmodule_eq_setOf

lemma mem_polarSubmodule {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S)
    (y : StrongDual ğ•œ E) : y âˆˆ polarSubmodule ğ•œ m â†” âˆ€ x âˆˆ m, y x = 0 :=
  propext_iff.mp congr($(polarSubmodule_eq_setOf ğ•œ m) y)

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.mem_polarSubmodule :=
  mem_polarSubmodule

@[simp]
theorem zero_mem_polar (s : Set E) : (0 : StrongDual ğ•œ E) âˆˆ polar ğ•œ s :=
  LinearMap.zero_mem_polar _ s

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.zero_mem_polar := zero_mem_polar

theorem polar_nonempty (s : Set E) : Set.Nonempty (polar ğ•œ s) :=
  LinearMap.polar_nonempty _ _

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar_nonempty := polar_nonempty

open Set

@[simp]
theorem polar_empty : polar ğ•œ (âˆ… : Set E) = Set.univ :=
  LinearMap.polar_empty _

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar_empty := polar_empty

@[simp]
theorem polar_singleton {a : E} : polar ğ•œ {a} = { x | â€–x aâ€– â‰¤ 1 } := by
  simp only [polar, LinearMap.polar_singleton, LinearMap.flip_apply, topDualPairing_apply]

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar_singleton := polar_singleton

theorem mem_polar_singleton {a : E} (y : StrongDual ğ•œ E) : y âˆˆ polar ğ•œ {a} â†” â€–y aâ€– â‰¤ 1 := by
  simp only [polar_singleton, mem_setOf_eq]

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.mem_polar_singleton :=
  mem_polar_singleton

theorem polar_zero : polar ğ•œ ({0} : Set E) = Set.univ :=
  LinearMap.polar_zero _

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar_zero := polar_zero

end

section

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable {E : Type*} [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]

open Set

@[simp]
theorem polar_univ : polar ğ•œ (univ : Set E) = {(0 : StrongDual ğ•œ E)} :=
  (topDualPairing ğ•œ E).flip.polar_univ
    (LinearMap.flip_separatingRight.mpr (dualPairing_separatingLeft ğ•œ E))

@[deprecated (since := "2025-08-12")] alias _root_.NormedSpace.polar_univ := polar_univ

end

end StrongDual
