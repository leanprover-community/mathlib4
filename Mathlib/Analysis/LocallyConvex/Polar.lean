/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Kalle KytÃ¶lÃ¤
-/
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.LinearAlgebra.SesquilinearForm
import Mathlib.Topology.Algebra.Module.WeakDual

#align_import analysis.locally_convex.polar from "leanprover-community/mathlib"@"bcfa726826abd57587355b4b5b7e78ad6527b7e4"

/-!
# Polar set

In this file we define the polar set. There are different notions of the polar, we will define the
*absolute polar*. The advantage over the real polar is that we can define the absolute polar for
any bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`, where `ğ•œ` is a normed commutative ring and
`E` and `F` are modules over `ğ•œ`.

## Main definitions

* `LinearMap.polar`: The polar of a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ`.

## Main statements

* `LinearMap.polar_eq_iInter`: The polar as an intersection.
* `LinearMap.subset_bipolar`: The polar is a subset of the bipolar.
* `LinearMap.polar_weak_closed`: The polar is closed in the weak topology induced by `B.flip`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

polar
-/


variable {ğ•œ E F : Type*}

open Topology

namespace LinearMap

section NormedRing

variable [NormedCommRing ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

/-- The (absolute) polar of `s : Set E` is given by the set of all `y : F` such that `â€–B x yâ€– â‰¤ 1`
for all `x âˆˆ s`. -/
def polar (s : Set E) : Set F :=
  { y : F | âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 }
#align linear_map.polar LinearMap.polar

theorem polar_mem_iff (s : Set E) (y : F) : y âˆˆ B.polar s â†” âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 :=
  Iff.rfl
#align linear_map.polar_mem_iff LinearMap.polar_mem_iff

theorem polar_mem (s : Set E) (y : F) (hy : y âˆˆ B.polar s) : âˆ€ x âˆˆ s, â€–B x yâ€– â‰¤ 1 :=
  hy
#align linear_map.polar_mem LinearMap.polar_mem

@[simp]
theorem zero_mem_polar (s : Set E) : (0 : F) âˆˆ B.polar s := fun _ _ => by
  simp only [map_zero, norm_zero, zero_le_one]
#align linear_map.zero_mem_polar LinearMap.zero_mem_polar

theorem polar_eq_iInter {s : Set E} : B.polar s = â‹‚ x âˆˆ s, { y : F | â€–B x yâ€– â‰¤ 1 } := by
  ext
  simp only [polar_mem_iff, Set.mem_iInter, Set.mem_setOf_eq]
#align linear_map.polar_eq_Inter LinearMap.polar_eq_iInter

/-- The map `B.polar : Set E â†’ Set F` forms an order-reversing Galois connection with
`B.flip.polar : Set F â†’ Set E`. We use `OrderDual.toDual` and `OrderDual.ofDual` to express
that `polar` is order-reversing. -/
theorem polar_gc :
    GaloisConnection (OrderDual.toDual âˆ˜ B.polar) (B.flip.polar âˆ˜ OrderDual.ofDual) := fun _ _ =>
  âŸ¨fun h _ hx _ hy => h hy _ hx, fun h _ hx _ hy => h hy _ hxâŸ©
#align linear_map.polar_gc LinearMap.polar_gc

@[simp]
theorem polar_iUnion {Î¹} {s : Î¹ â†’ Set E} : B.polar (â‹ƒ i, s i) = â‹‚ i, B.polar (s i) :=
  B.polar_gc.l_iSup
#align linear_map.polar_Union LinearMap.polar_iUnion

@[simp]
theorem polar_union {s t : Set E} : B.polar (s âˆª t) = B.polar s âˆ© B.polar t :=
  B.polar_gc.l_sup
#align linear_map.polar_union LinearMap.polar_union

theorem polar_antitone : Antitone (B.polar : Set E â†’ Set F) :=
  B.polar_gc.monotone_l
#align linear_map.polar_antitone LinearMap.polar_antitone

@[simp]
theorem polar_empty : B.polar âˆ… = Set.univ :=
  B.polar_gc.l_bot
#align linear_map.polar_empty LinearMap.polar_empty

@[simp]
theorem polar_zero : B.polar ({0} : Set E) = Set.univ := by
  refine Set.eq_univ_iff_forall.mpr fun y x hx => ?_
  rw [Set.mem_singleton_iff.mp hx, map_zero, LinearMap.zero_apply, norm_zero]
  exact zero_le_one
#align linear_map.polar_zero LinearMap.polar_zero

theorem subset_bipolar (s : Set E) : s âŠ† B.flip.polar (B.polar s) := fun x hx y hy => by
  rw [B.flip_apply]
  exact hy x hx
#align linear_map.subset_bipolar LinearMap.subset_bipolar

@[simp]
theorem tripolar_eq_polar (s : Set E) : B.polar (B.flip.polar (B.polar s)) = B.polar s :=
  (B.polar_antitone (B.subset_bipolar s)).antisymm (subset_bipolar B.flip (B.polar s))
#align linear_map.tripolar_eq_polar LinearMap.tripolar_eq_polar

/-- The polar set is closed in the weak topology induced by `B.flip`. -/
theorem polar_weak_closed (s : Set E) : IsClosed[WeakBilin.instTopologicalSpace B.flip]
    (B.polar s) := by
  rw [polar_eq_iInter]
  refine isClosed_iInter fun x => isClosed_iInter fun _ => ?_
  exact isClosed_le (WeakBilin.eval_continuous B.flip x).norm continuous_const
#align linear_map.polar_weak_closed LinearMap.polar_weak_closed

end NormedRing

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]


variable (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ)

theorem polar_univ (h : SeparatingRight B) : B.polar Set.univ = {(0 : F)} := by
  rw [Set.eq_singleton_iff_unique_mem]
  refine âŸ¨by simp only [zero_mem_polar], fun y hy => h _ fun x => ?_âŸ©
  refine norm_le_zero_iff.mp (le_of_forall_le_of_dense fun Îµ hÎµ => ?_)
  rcases NormedField.exists_norm_lt ğ•œ hÎµ with âŸ¨c, hc, hcÎµâŸ©
  calc
    â€–B x yâ€– = â€–câ€– * â€–B (câ»Â¹ â€¢ x) yâ€– := by
      rw [B.map_smul, LinearMap.smul_apply, Algebra.id.smul_eq_mul, norm_mul, norm_inv,
        mul_inv_cancel_leftâ‚€ hc.ne']
    _ â‰¤ Îµ * 1 := by gcongr; exact hy _ trivial
    _ = Îµ := mul_one _
#align linear_map.polar_univ LinearMap.polar_univ

end NontriviallyNormedField

end LinearMap
