/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Topology.Algebra.Module.StrongTopology
import Mathlib.Analysis.Normed.Module.FiniteDimension
/-!
# Montel spaces

-/

open Filter Topology Set ContinuousLinearMap Bornology

section definition

variable {ğ•œ E F : Type*}
variable [SeminormedRing ğ•œ] [Zero E] [SMul ğ•œ E]
  [TopologicalSpace E]

class MontelSpace (ğ•œ E : Type*) [SeminormedRing ğ•œ] [Zero E] [SMul ğ•œ E]
    [TopologicalSpace E] : Prop where
  heine_borel : âˆ€ (s : Set E) (_ : IsClosed s) (_ : IsVonNBounded ğ•œ s), IsCompact s

namespace MontelSpace

variable (ğ•œ) in
theorem isCompact_of_isClosed_isVonNBounded [hm : MontelSpace ğ•œ E] {s : Set E}
    (h_closed : IsClosed s) (h_bounded : IsVonNBounded ğ•œ s) : IsCompact s :=
  hm.heine_borel s h_closed h_bounded

end MontelSpace

end definition

section Normed

namespace MontelSpace

variable {ğ•œ E F : Type*}
variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace ğ•œ]
  [hM : MontelSpace ğ•œ E]

theorem finiteDimensional : FiniteDimensional ğ•œ E :=
  FiniteDimensional.of_isCompact_closedBallâ‚€ ğ•œ zero_lt_one
    (isCompact_of_isClosed_isVonNBounded ğ•œ Metric.isClosed_closedBall
      (NormedSpace.isVonNBounded_closedBall _ _ _) )

end MontelSpace

end Normed

variable {ğ•œâ‚ ğ•œâ‚‚ : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚] {Ïƒ : ğ•œâ‚ â†’+* ğ•œâ‚‚}
variable {E F : Type*}
  [AddCommGroup E] [Module ğ•œâ‚ E]
  [UniformSpace E]
  [AddCommGroup F] [Module ğ•œâ‚‚ F]
  [TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousSMul ğ•œâ‚‚ F]

open CompactConvergenceCLM

variable (Ïƒ E F) in
/-- The linear equivalence that sends a continuous linear map to the type copy endowed with the
weak operator topology.

This definition is only used to prove the continuous linear equivalence. -/
private def _root_.LinearEquiv.toCompactConvergenceCLM :
    (E â†’SL[Ïƒ] F) â‰ƒâ‚—[ğ•œâ‚‚] E â†’SL_c[Ïƒ] F :=
  LinearEquiv.refl ğ•œâ‚‚ _

variable [IsUniformAddGroup E] [ContinuousSMul ğ•œâ‚ E]

variable (Ïƒ E F) in
/-- If `E` is a Montel space, then the strong topology on `E â†’L[ğ•œ] F` coincides with the topology
of compact convergence.

We realize this equality in terms of a continuous linear equivalence between the type synonyms.

This is Proposition 34.5 in Treves - Topological vector spaces, distributions, and kernels. -/
def _root_.ContinuousLinearEquiv.toCompactConvergenceCLM [T1Space E] [MontelSpace ğ•œâ‚ E] :
    (E â†’SL[Ïƒ] F) â‰ƒL[ğ•œâ‚‚] E â†’SL_c[Ïƒ] F where
  __ := LinearEquiv.toCompactConvergenceCLM Ïƒ E F
  continuous_toFun := by
    simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, LinearEquiv.coe_coe, continuous_def]
    intro s hs
    apply hs.mono
    apply UniformConvergenceCLM.topologicalSpace_mono
    intro x hx
    exact hx.totallyBounded.isVonNBounded ğ•œâ‚
  continuous_invFun := by
    apply continuous_of_continuousAt_zero (LinearEquiv.toCompactConvergenceCLM Ïƒ E F).symm
    rw [ContinuousAt, _root_.map_zero, CompactConvergenceCLM.hasBasis_nhds_zero.tendsto_iff
      ContinuousLinearMap.hasBasis_nhds_zero]
    rintro âŸ¨a, bâŸ© âŸ¨ha, hbâŸ©
    use âŸ¨closure a, bâŸ©
    exact âŸ¨âŸ¨MontelSpace.isCompact_of_isClosed_isVonNBounded ğ•œâ‚ isClosed_closure
      ha.closure, hbâŸ©, fun _ hf _ hx â†¦ hf _ (subset_closure hx)âŸ©

@[simp]
theorem _root_.ContinuousLinearEquiv.toCompactConvergenceCLM_apply [T1Space E] [MontelSpace ğ•œâ‚ E]
    (f : E â†’SL[Ïƒ] F) (x : E) : ContinuousLinearEquiv.toCompactConvergenceCLM Ïƒ E F f x = f x := rfl

@[simp]
theorem _root_.ContinuousLinearEquiv.toCompactConvergenceCLM_symm_apply [T1Space E]
    [MontelSpace ğ•œâ‚ E] (f : E â†’SL_c[Ïƒ] F) (x : E) :
    (ContinuousLinearEquiv.toCompactConvergenceCLM Ïƒ E F).symm f x = f x := rfl
