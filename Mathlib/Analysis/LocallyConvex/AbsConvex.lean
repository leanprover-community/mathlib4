/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.Convex.Gauge

/-!
# Absolutely convex sets

A set is called absolutely convex or disked if it is convex and balanced.
The importance of absolutely convex sets comes from the fact that every locally convex
topological vector space has a basis consisting of absolutely convex sets.

## Main definitions

* `gaugeSeminormFamily`: the seminorm family induced by all open absolutely convex neighborhoods
of zero.

## Main statements

* `with_gaugeSeminormFamily`: the topology of a locally convex space is induced by the family
`gaugeSeminormFamily`.

## TODO

* Define the disked hull

## Tags

disks, convex, balanced
-/


open NormedField Set

open NNReal Pointwise Topology

variable {ğ•œ E F G Î¹ : Type*}

section NontriviallyNormedField

variable (ğ•œ E) {s : Set E}
variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [Module â„ E] [SMulCommClass â„ ğ•œ E]
variable [TopologicalSpace E] [LocallyConvexSpace â„ E] [ContinuousSMul ğ•œ E]

theorem nhds_basis_abs_convex :
    (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ ğ“ (0 : E) âˆ§ Balanced ğ•œ s âˆ§ Convex â„ s) id := by
  refine
    (LocallyConvexSpace.convex_basis_zero â„ E).to_hasBasis (fun s hs => ?_) fun s hs =>
      âŸ¨s, âŸ¨hs.1, hs.2.2âŸ©, rfl.subsetâŸ©
  refine âŸ¨convexHull â„ (balancedCore ğ•œ s), ?_, convexHull_min (balancedCore_subset s) hs.2âŸ©
  refine âŸ¨Filter.mem_of_superset (balancedCore_mem_nhds_zero hs.1) (subset_convexHull â„ _), ?_âŸ©
  refine âŸ¨(balancedCore_balanced s).convexHull, ?_âŸ©
  exact convex_convexHull â„ (balancedCore ğ•œ s)

variable [ContinuousSMul â„ E] [TopologicalAddGroup E]

theorem nhds_basis_abs_convex_open :
    (ğ“ (0 : E)).HasBasis (fun s => (0 : E) âˆˆ s âˆ§ IsOpen s âˆ§ Balanced ğ•œ s âˆ§ Convex â„ s) id := by
  refine (nhds_basis_abs_convex ğ•œ E).to_hasBasis ?_ ?_
  Â· rintro s âŸ¨hs_nhds, hs_balanced, hs_convexâŸ©
    refine âŸ¨interior s, ?_, interior_subsetâŸ©
    exact
      âŸ¨mem_interior_iff_mem_nhds.mpr hs_nhds, isOpen_interior,
        hs_balanced.interior (mem_interior_iff_mem_nhds.mpr hs_nhds), hs_convex.interiorâŸ©
  rintro s âŸ¨hs_zero, hs_open, hs_balanced, hs_convexâŸ©
  exact âŸ¨s, âŸ¨hs_open.mem_nhds hs_zero, hs_balanced, hs_convexâŸ©, rfl.subsetâŸ©

end NontriviallyNormedField

section AbsolutelyConvexSets

variable [TopologicalSpace E] [AddCommMonoid E] [Zero E] [SeminormedRing ğ•œ]
variable [SMul ğ•œ E] [SMul â„ E]
variable (ğ•œ E)

/-- The type of absolutely convex open sets. -/
def AbsConvexOpenSets :=
  { s : Set E // (0 : E) âˆˆ s âˆ§ IsOpen s âˆ§ Balanced ğ•œ s âˆ§ Convex â„ s }

noncomputable instance AbsConvexOpenSets.instCoeTC : CoeTC (AbsConvexOpenSets ğ•œ E) (Set E) :=
  âŸ¨Subtype.valâŸ©

namespace AbsConvexOpenSets

variable {ğ•œ E}

theorem coe_zero_mem (s : AbsConvexOpenSets ğ•œ E) : (0 : E) âˆˆ (s : Set E) :=
  s.2.1

theorem coe_isOpen (s : AbsConvexOpenSets ğ•œ E) : IsOpen (s : Set E) :=
  s.2.2.1

theorem coe_nhds (s : AbsConvexOpenSets ğ•œ E) : (s : Set E) âˆˆ ğ“ (0 : E) :=
  s.coe_isOpen.mem_nhds s.coe_zero_mem

theorem coe_balanced (s : AbsConvexOpenSets ğ•œ E) : Balanced ğ•œ (s : Set E) :=
  s.2.2.2.1

theorem coe_convex (s : AbsConvexOpenSets ğ•œ E) : Convex â„ (s : Set E) :=
  s.2.2.2.2

end AbsConvexOpenSets

instance AbsConvexOpenSets.instNonempty : Nonempty (AbsConvexOpenSets ğ•œ E) := by
  rw [â† exists_true_iff_nonempty]
  dsimp only [AbsConvexOpenSets]
  rw [Subtype.exists]
  exact âŸ¨Set.univ, âŸ¨mem_univ 0, isOpen_univ, balanced_univ, convex_univâŸ©, trivialâŸ©

end AbsolutelyConvexSets

variable [RCLike ğ•œ]
variable [AddCommGroup E] [TopologicalSpace E]
variable [Module ğ•œ E] [Module â„ E] [IsScalarTower â„ ğ•œ E]
variable [ContinuousSMul â„ E]
variable (ğ•œ E)

/-- The family of seminorms defined by the gauges of absolute convex open sets. -/
noncomputable def gaugeSeminormFamily : SeminormFamily ğ•œ E (AbsConvexOpenSets ğ•œ E) := fun s =>
  gaugeSeminorm s.coe_balanced s.coe_convex (absorbent_nhds_zero s.coe_nhds)

variable {ğ•œ E}

theorem gaugeSeminormFamily_ball (s : AbsConvexOpenSets ğ•œ E) :
    (gaugeSeminormFamily ğ•œ E s).ball 0 1 = (s : Set E) := by
  dsimp only [gaugeSeminormFamily]
  rw [Seminorm.ball_zero_eq]
  simp_rw [gaugeSeminorm_toFun]
  exact gauge_lt_one_eq_self_of_isOpen s.coe_convex s.coe_zero_mem s.coe_isOpen

variable [TopologicalAddGroup E] [ContinuousSMul ğ•œ E]
variable [SMulCommClass â„ ğ•œ E] [LocallyConvexSpace â„ E]

/-- The topology of a locally convex space is induced by the gauge seminorm family. -/
theorem with_gaugeSeminormFamily : WithSeminorms (gaugeSeminormFamily ğ•œ E) := by
  refine SeminormFamily.withSeminorms_of_hasBasis _ ?_
  refine (nhds_basis_abs_convex_open ğ•œ E).to_hasBasis (fun s hs => ?_) fun s hs => ?_
  Â· refine âŸ¨s, âŸ¨?_, rfl.subsetâŸ©âŸ©
    convert (gaugeSeminormFamily _ _).basisSets_singleton_mem âŸ¨s, hsâŸ© one_pos
    rw [gaugeSeminormFamily_ball, Subtype.coe_mk]
  refine âŸ¨s, âŸ¨?_, rfl.subsetâŸ©âŸ©
  rw [SeminormFamily.basisSets_iff] at hs
  rcases hs with âŸ¨t, r, hr, rflâŸ©
  rw [Seminorm.ball_finset_sup_eq_iInter _ _ _ hr]
  -- We have to show that the intersection contains zero, is open, balanced, and convex
  refine
    âŸ¨mem_iInterâ‚‚.mpr fun _ _ => by simp [Seminorm.mem_ball_zero, hr],
      isOpen_biInter_finset fun S _ => ?_,
      balanced_iInterâ‚‚ fun _ _ => Seminorm.balanced_ball_zero _ _,
      convex_iInterâ‚‚ fun _ _ => Seminorm.convex_ball _ _ _âŸ©
  -- The only nontrivial part is to show that the ball is open
  have hr' : r = â€–(r : ğ•œ)â€– * 1 := by simp [abs_of_pos hr]
  have hr'' : (r : ğ•œ) â‰  0 := by simp [hr.ne']
  rw [hr', â† Seminorm.smul_ball_zero hr'', gaugeSeminormFamily_ball]
  exact S.coe_isOpen.smulâ‚€ hr''
