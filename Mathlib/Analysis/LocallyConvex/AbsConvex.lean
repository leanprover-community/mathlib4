/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Convex.TotallyBounded
import Mathlib.Analysis.LocallyConvex.Bounded

/-!
# Absolutely convex sets

A set `s` in an commutative monoid `E` is called absolutely convex or disked if it is convex and
balanced. The importance of absolutely convex sets comes from the fact that every locally convex
topological vector space has a basis consisting of absolutely convex sets.

## Main definitions

* `absConvexHull`: the absolutely convex hull of a set `s` is the smallest absolutely convex set
  containing `s`;
* `closedAbsConvexHull`: the closed absolutely convex hull of a set `s` is the smallest absolutely
  convex set containing `s`;

## Main statements

* `absConvexHull_eq_convexHull_balancedHull`: when the locally convex space is a module, the
  absolutely convex hull of a set `s` equals the convex hull of the balanced hull of `s`;
* `convexHull_union_neg_eq_absConvexHull`: the convex hull of `s âˆª -s` is the absolutely convex hull
  of `s`;
* `closedAbsConvexHull_closure_eq_closedAbsConvexHull` : the closed absolutely convex hull of the
  closure of `s` equals the closed absolutely convex hull of `s`;

## Implementation notes

Mathlib's definition of `Convex` requires the scalars to be an `OrderedSemiring` whereas the
definition of `Balanced` requires the scalars to be a `SeminormedRing`. Mathlib doesn't currently
have a concept of a semi-normed ordered ring, so we define a set as `AbsConvex` if it is balanced
over a `SeminormedRing` `ğ•œ` and convex over `â„`, assuming `IsScalarTower â„ ğ•œ E` and
`SMulCommClass â„ ğ•œ E` where required.

## Tags

disks, convex, balanced
-/

open NormedField Set

open NNReal Pointwise Topology

variable {ğ•œ E : Type*}

section AbsolutelyConvex

variable (ğ•œ) [SeminormedRing ğ•œ] [SMul ğ•œ E] [SMul â„ E] [AddCommMonoid E]
/-- A set is absolutely convex if it is balanced and convex. Mathlib's definition of `Convex`
requires the scalars to be an `OrderedSemiring` whereas the definition of `Balanced` requires the
scalars to be a `SeminormedRing`. Mathlib doesn't currently have a concept of a semi-normed ordered
ring, so we define a set as `AbsConvex` if it is balanced over a `SeminormedRing` `ğ•œ` and convex
over `â„`. -/
def AbsConvex (s : Set E) : Prop := Balanced ğ•œ s âˆ§ Convex â„ s

variable {ğ•œ}

theorem AbsConvex.empty : AbsConvex ğ•œ (âˆ… : Set E) := âŸ¨balanced_empty, convex_emptyâŸ©

theorem AbsConvex.univ : AbsConvex ğ•œ (univ : Set E) := âŸ¨balanced_univ, convex_univâŸ©

theorem AbsConvex.inter {s t : Set E} (hs : AbsConvex ğ•œ s) (ht : AbsConvex ğ•œ t) :
    AbsConvex ğ•œ (s âˆ© t) := âŸ¨hs.1.inter ht.1, hs.2.inter ht.2âŸ©

theorem AbsConvex.sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, AbsConvex ğ•œ s) : AbsConvex ğ•œ (â‹‚â‚€ S) :=
  âŸ¨.sInter fun s hs => (h s hs).1, convex_sInter fun s hs => (h s hs).2âŸ©

theorem AbsConvex.iInter {Î¹ : Sort*} {s : Î¹ â†’ Set E} (h : âˆ€ i, AbsConvex ğ•œ (s i)) :
    AbsConvex ğ•œ (â‹‚ i, s i) :=
  sInter_range s â–¸ AbsConvex.sInter <| forall_mem_range.2 h

theorem AbsConvex.iInterâ‚‚ {Î¹ : Sort*} {Îº : Î¹ â†’ Sort*} {f : âˆ€ i, Îº i â†’ Set E}
    (h : âˆ€ i j, AbsConvex ğ•œ (f i j)) : AbsConvex ğ•œ (â‹‚ (i) (j), f i j) :=
  AbsConvex.iInter fun _  => (AbsConvex.iInter fun _ => h _ _)

variable (ğ•œ)

/-- The absolute convex hull of a set `s` is the minimal absolute convex set that includes `s`. -/
@[simps! isClosed]
def absConvexHull : ClosureOperator (Set E) :=
  .ofCompletePred (AbsConvex ğ•œ) fun _ â†¦ .sInter

variable {ğ•œ} {s : Set E}

theorem subset_absConvexHull : s âŠ† absConvexHull ğ•œ s :=
  (absConvexHull ğ•œ).le_closure s

theorem absConvex_absConvexHull : AbsConvex ğ•œ (absConvexHull ğ•œ s) :=
  (absConvexHull ğ•œ).isClosed_closure s

theorem balanced_absConvexHull : Balanced ğ•œ (absConvexHull ğ•œ s) :=
  absConvex_absConvexHull.1

theorem convex_absConvexHull : Convex â„ (absConvexHull ğ•œ s) :=
  absConvex_absConvexHull.2

variable (ğ•œ s) in
theorem absConvexHull_eq_iInter :
    absConvexHull ğ•œ s = â‹‚ (t : Set E) (_ : s âŠ† t) (_ : AbsConvex ğ•œ t), t := by
  simp [absConvexHull, iInter_subtype, iInter_and]

variable {t : Set E} {x : E}

theorem mem_absConvexHull_iff : x âˆˆ absConvexHull ğ•œ s â†” âˆ€ t, s âŠ† t â†’ AbsConvex ğ•œ t â†’ x âˆˆ t := by
  simp_rw [absConvexHull_eq_iInter, mem_iInter]

theorem absConvexHull_min : s âŠ† t â†’ AbsConvex ğ•œ t â†’ absConvexHull ğ•œ s âŠ† t :=
  (absConvexHull ğ•œ).closure_min

theorem AbsConvex.absConvexHull_subset_iff (ht : AbsConvex ğ•œ t) : absConvexHull ğ•œ s âŠ† t â†” s âŠ† t :=
  (show (absConvexHull ğ•œ).IsClosed t from ht).closure_le_iff

@[mono, gcongr]
theorem absConvexHull_mono (hst : s âŠ† t) : absConvexHull ğ•œ s âŠ† absConvexHull ğ•œ t :=
  (absConvexHull ğ•œ).monotone hst

lemma absConvexHull_eq_self : absConvexHull ğ•œ s = s â†” AbsConvex ğ•œ s :=
  (absConvexHull ğ•œ).isClosed_iff.symm

alias âŸ¨_, AbsConvex.absConvexHull_eqâŸ© := absConvexHull_eq_self

@[simp]
theorem absConvexHull_univ : absConvexHull ğ•œ (univ : Set E) = univ :=
  ClosureOperator.closure_top (absConvexHull ğ•œ)

@[simp]
theorem absConvexHull_empty : absConvexHull ğ•œ (âˆ… : Set E) = âˆ… :=
  AbsConvex.empty.absConvexHull_eq

@[simp]
theorem absConvexHull_eq_empty : absConvexHull ğ•œ s = âˆ… â†” s = âˆ… := by
  constructor
  Â· intro h
    rw [â† Set.subset_empty_iff, â† h]
    exact subset_absConvexHull
  Â· rintro rfl
    exact absConvexHull_empty

@[simp]
theorem absConvexHull_nonempty : (absConvexHull ğ•œ s).Nonempty â†” s.Nonempty := by
  rw [nonempty_iff_ne_empty, nonempty_iff_ne_empty, Ne, Ne]
  exact not_congr absConvexHull_eq_empty

protected alias âŸ¨_, Set.Nonempty.absConvexHullâŸ© := absConvexHull_nonempty

variable [TopologicalSpace E]

theorem absConvex_closed_sInter {S : Set (Set E)} (h : âˆ€ s âˆˆ S, AbsConvex ğ•œ s âˆ§ IsClosed s) :
    AbsConvex ğ•œ (â‹‚â‚€ S) âˆ§ IsClosed (â‹‚â‚€ S) :=
  âŸ¨AbsConvex.sInter (fun s hs => (h s hs).1), isClosed_sInter fun _ hs => (h _ hs).2âŸ©

variable (ğ•œ) in
/-- The absolutely convex closed hull of a set `s` is the minimal absolutely convex closed set that
includes `s`. -/
@[simps! isClosed]
def closedAbsConvexHull : ClosureOperator (Set E) :=
  .ofCompletePred (fun s => AbsConvex ğ•œ s âˆ§ IsClosed s) fun _ â†¦ absConvex_closed_sInter

theorem absConvex_convexClosedHull {s : Set E} :
    AbsConvex ğ•œ (closedAbsConvexHull ğ•œ s) := ((closedAbsConvexHull ğ•œ).isClosed_closure s).1

theorem isClosed_closedAbsConvexHull {s : Set E} :
    IsClosed (closedAbsConvexHull ğ•œ s) := ((closedAbsConvexHull ğ•œ).isClosed_closure s).2

theorem subset_closedAbsConvexHull {s : Set E} : s âŠ† closedAbsConvexHull ğ•œ s :=
  (closedAbsConvexHull ğ•œ).le_closure s

theorem closure_subset_closedAbsConvexHull {s : Set E} : closure s âŠ† closedAbsConvexHull ğ•œ s :=
  closure_minimal subset_closedAbsConvexHull isClosed_closedAbsConvexHull

theorem closedAbsConvexHull_min {s t : Set E} (hst : s âŠ† t) (h_conv : AbsConvex ğ•œ t)
    (h_closed : IsClosed t) : closedAbsConvexHull ğ•œ s âŠ† t :=
  (closedAbsConvexHull ğ•œ).closure_min hst âŸ¨h_conv, h_closedâŸ©

theorem absConvexHull_subset_closedAbsConvexHull {s : Set E} :
    (absConvexHull ğ•œ) s âŠ† (closedAbsConvexHull ğ•œ) s :=
  absConvexHull_min subset_closedAbsConvexHull absConvex_convexClosedHull

@[simp]
theorem closedAbsConvexHull_closure_eq_closedAbsConvexHull {s : Set E} :
    closedAbsConvexHull ğ•œ (closure s) = closedAbsConvexHull ğ•œ s :=
  subset_antisymm (by simpa using ((closedAbsConvexHull ğ•œ).monotone
      (closure_subset_closedAbsConvexHull (ğ•œ := ğ•œ) (E := E))))
    ((closedAbsConvexHull ğ•œ).monotone subset_closure)

end AbsolutelyConvex

section NormedField

variable [NormedField ğ•œ]
  [AddCommGroup E] [Module â„ E] [Module ğ•œ E] [TopologicalSpace E]
  [IsTopologicalAddGroup E] [ContinuousSMul â„ E] [ContinuousSMul ğ•œ E]

theorem AbsConvex.closure {s : Set E} (hs : AbsConvex ğ•œ s) : AbsConvex ğ•œ (closure s) :=
  âŸ¨Balanced.closure hs.1, Convex.closure hs.2âŸ©

theorem closedAbsConvexHull_eq_closure_absConvexHull {s : Set E} :
    closedAbsConvexHull ğ•œ s = closure (absConvexHull ğ•œ s) := subset_antisymm
  (closedAbsConvexHull_min (subset_trans (subset_absConvexHull) subset_closure)
    (AbsConvex.closure absConvex_absConvexHull) isClosed_closure)
  (closure_minimal absConvexHull_subset_closedAbsConvexHull isClosed_closedAbsConvexHull)

end NormedField

section NontriviallyNormedField

variable (ğ•œ E)
variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
variable [Module â„ E] [SMulCommClass â„ ğ•œ E]
variable [TopologicalSpace E] [LocallyConvexSpace â„ E] [ContinuousSMul ğ•œ E]

theorem nhds_hasBasis_absConvex :
    (ğ“ (0 : E)).HasBasis (fun s : Set E => s âˆˆ ğ“ (0 : E) âˆ§ AbsConvex ğ•œ s) id := by
  refine
    (LocallyConvexSpace.convex_basis_zero â„ E).to_hasBasis (fun s hs => ?_) fun s hs =>
      âŸ¨s, âŸ¨hs.1, hs.2.2âŸ©, rfl.subsetâŸ©
  refine âŸ¨convexHull â„ (balancedCore ğ•œ s), ?_, convexHull_min (balancedCore_subset s) hs.2âŸ©
  refine âŸ¨Filter.mem_of_superset (balancedCore_mem_nhds_zero hs.1) (subset_convexHull â„ _), ?_âŸ©
  refine âŸ¨(balancedCore_balanced s).convexHull, ?_âŸ©
  exact convex_convexHull â„ (balancedCore ğ•œ s)

variable [ContinuousSMul â„ E] [IsTopologicalAddGroup E]

theorem nhds_hasBasis_absConvex_open :
    (ğ“ (0 : E)).HasBasis (fun s => (0 : E) âˆˆ s âˆ§ IsOpen s âˆ§ AbsConvex ğ•œ s) id := by
  refine (nhds_hasBasis_absConvex ğ•œ E).to_hasBasis ?_ ?_
  Â· rintro s âŸ¨hs_nhds, hs_balanced, hs_convexâŸ©
    refine âŸ¨interior s, ?_, interior_subsetâŸ©
    exact
      âŸ¨mem_interior_iff_mem_nhds.mpr hs_nhds, isOpen_interior,
        hs_balanced.interior (mem_interior_iff_mem_nhds.mpr hs_nhds), hs_convex.interiorâŸ©
  rintro s âŸ¨hs_zero, hs_open, hs_balanced, hs_convexâŸ©
  exact âŸ¨s, âŸ¨hs_open.mem_nhds hs_zero, hs_balanced, hs_convexâŸ©, rfl.subsetâŸ©

end NontriviallyNormedField

section

variable (ğ•œ) [NontriviallyNormedField ğ•œ]
variable [AddCommGroup E] [Module â„ E] [Module ğ•œ E]

theorem absConvexHull_add_subset {s t : Set E} :
    absConvexHull ğ•œ (s + t) âŠ† absConvexHull ğ•œ s + absConvexHull ğ•œ t :=
  absConvexHull_min (add_subset_add subset_absConvexHull subset_absConvexHull)
    âŸ¨Balanced.add balanced_absConvexHull balanced_absConvexHull,
      Convex.add convex_absConvexHull convex_absConvexHullâŸ©

theorem absConvexHull_eq_convexHull_balancedHull [SMulCommClass â„ ğ•œ E] {s : Set E} :
    absConvexHull ğ•œ s = convexHull â„ (balancedHull ğ•œ s) := le_antisymm
  (absConvexHull_min
    ((subset_convexHull â„ s).trans (convexHull_mono (subset_balancedHull ğ•œ)))
      âŸ¨Balanced.convexHull (balancedHull.balanced s), convex_convexHull ..âŸ©)
  (convexHull_min (balanced_absConvexHull.balancedHull_subset_of_subset subset_absConvexHull)
      convex_absConvexHull)

/-- In general, equality doesn't hold here - e.g. consider `s := {(-1, 1), (1, 1)}` in `â„Â²`. -/
theorem balancedHull_convexHull_subseteq_absConvexHull {s : Set E} :
    balancedHull ğ•œ (convexHull â„ s) âŠ† absConvexHull ğ•œ s :=
  balanced_absConvexHull.balancedHull_subset_of_subset
    (convexHull_min subset_absConvexHull convex_absConvexHull)

end

section

variable [AddCommGroup E] [Module â„ E]

lemma balancedHull_subset_convexHull_union_neg {s : Set E} :
    balancedHull â„ s âŠ† convexHull â„ (s âˆª -s) := by
  intro a ha
  obtain âŸ¨r, hr, y, hy, rflâŸ© := mem_balancedHull_iff.1 ha
  apply segment_subset_convexHull (mem_union_left (-s) hy) (mem_union_right _ (neg_mem_neg.mpr hy))
  have : 0 â‰¤ 1 + r := neg_le_iff_add_nonneg'.mp (neg_le_of_abs_le hr)
  have : 0 â‰¤ 1 - r := sub_nonneg.2 (le_of_abs_le hr)
  refine âŸ¨(1 + r)/2, (1 - r)/2, by positivity, by positivity, by ring, ?_âŸ©
  rw [smul_neg, â† sub_eq_add_neg, â† sub_smul]
  apply congrFun (congrArg HSMul.hSMul _) y
  ring_nf

@[simp]
theorem convexHull_union_neg_eq_absConvexHull {s : Set E} :
    convexHull â„ (s âˆª -s) = absConvexHull â„ s := by
  rw [absConvexHull_eq_convexHull_balancedHull]
  exact le_antisymm (convexHull_mono (union_subset (subset_balancedHull â„)
    (fun _ _ => by rw [mem_balancedHull_iff]; use -1; simp_all)))
    (by
      rw [â† Convex.convexHull_eq (convex_convexHull â„ (s âˆª -s))]
      exact convexHull_mono balancedHull_subset_convexHull_union_neg)

variable (E ğ•œ) {s : Set E}
variable [NontriviallyNormedField ğ•œ] [Module ğ•œ E] [SMulCommClass â„ ğ•œ E]
variable [UniformSpace E] [IsUniformAddGroup E] [lcs : LocallyConvexSpace â„ E] [ContinuousSMul â„ E]

-- TVS II.25 Prop3
theorem totallyBounded_absConvexHull (hs : TotallyBounded s) :
    TotallyBounded (absConvexHull â„ s) := by
  rw [â† convexHull_union_neg_eq_absConvexHull]
  apply totallyBounded_convexHull
  rw [totallyBounded_union]
  exact âŸ¨hs, totallyBounded_neg hsâŸ©

end

lemma zero_mem_absConvexHull {s : Set E} [SeminormedRing ğ•œ] [AddCommGroup E] [Module â„ E]
    [Module ğ•œ E] [Nonempty s] : 0 âˆˆ absConvexHull ğ•œ s :=
  balanced_absConvexHull.zero_mem (Nonempty.mono subset_absConvexHull Set.Nonempty.of_subtype)

/-- [Bourbaki, *Topological Vector Spaces*, III Â§1.6][bourbaki1987] -/
theorem isCompact_closedAbsConvexHull_of_totallyBounded {E : Type*} [AddCommGroup E] [Module â„ E]
    [UniformSpace E] [IsUniformAddGroup E] [ContinuousSMul â„ E] [LocallyConvexSpace â„ E]
    [QuasiCompleteSpace â„ E] {s : Set E} (ht : TotallyBounded s) :
    IsCompact (closedAbsConvexHull â„ s) := by
  rw [closedAbsConvexHull_eq_closure_absConvexHull]
  exact isCompact_closure_of_totallyBounded_quasiComplete (ğ•œ := â„)
    (totallyBounded_absConvexHull E ht)
