/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Algebra.Module.WeakBilin
import Mathlib.Data.Finsupp.Order
import Mathlib.LinearAlgebra.Dual.Lemmas

/-!
# Weak Dual in Topological Vector Spaces

We prove that the weak topology induced by a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ` is locally
convex and we explicitly give a neighborhood basis in terms of the family of seminorms
`fun x => â€–B x yâ€–` for `y : F`.

## Main definitions

* `LinearMap.toSeminorm`: turn a linear form `f : E â†’â‚—[ğ•œ] ğ•œ` into a seminorm `fun x => â€–f xâ€–`.
* `LinearMap.toSeminormFamily`: turn a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ` into a map
  `F â†’ Seminorm ğ•œ E`.

## Main statements

* `LinearMap.hasBasis_weakBilin`: the seminorm balls of `B.toSeminormFamily` form a
  neighborhood basis of `0` in the weak topology.
* `LinearMap.toSeminormFamily.withSeminorms`: the topology of a weak space is induced by the
  family of seminorms `B.toSeminormFamily`.
* `WeakBilin.locallyConvexSpace`: a space endowed with a weak topology is locally convex.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]
* [Rudin, *Functional Analysis*][rudin1991]

## Tags

weak dual, seminorm
-/


variable {ğ•œ E F : Type*}

open Topology

section BilinForm

namespace LinearMap

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]

/-- Construct a seminorm from a linear form `f : E â†’â‚—[ğ•œ] ğ•œ` over a normed field `ğ•œ` by
`fun x => â€–f xâ€–` -/
def toSeminorm (f : E â†’â‚—[ğ•œ] ğ•œ) : Seminorm ğ•œ E :=
  (normSeminorm ğ•œ ğ•œ).comp f

theorem coe_toSeminorm {f : E â†’â‚—[ğ•œ] ğ•œ} : â‡‘f.toSeminorm = fun x => â€–f xâ€– :=
  rfl

@[simp]
theorem toSeminorm_apply {f : E â†’â‚—[ğ•œ] ğ•œ} {x : E} : f.toSeminorm x = â€–f xâ€– :=
  rfl

theorem toSeminorm_ball_zero {f : E â†’â‚—[ğ•œ] ğ•œ} {r : â„} :
    Seminorm.ball f.toSeminorm 0 r = { x : E | â€–f xâ€– < r } := by
  simp only [Seminorm.ball_zero_eq, toSeminorm_apply]

theorem toSeminorm_comp (f : F â†’â‚—[ğ•œ] ğ•œ) (g : E â†’â‚—[ğ•œ] F) :
    f.toSeminorm.comp g = (f.comp g).toSeminorm := by
  ext
  simp only [Seminorm.comp_apply, toSeminorm_apply, coe_comp, Function.comp_apply]

/-- Construct a family of seminorms from a bilinear form. -/
def toSeminormFamily (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : SeminormFamily ğ•œ E F := fun y =>
  (B.flip y).toSeminorm

@[simp]
theorem toSeminormFamily_apply {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} {x y} : (B.toSeminormFamily y) x = â€–B x yâ€– :=
  rfl

variable {Î¹ ğ•œ E F : Type*}

open Topology TopologicalSpace
open scoped NNReal

section

section TopologicalRing

variable [Finite Î¹] [Field ğ•œ] [tğ•œ : TopologicalSpace ğ•œ] [IsTopologicalRing ğ•œ]
  [AddCommGroup E] [Module ğ•œ E] [T0Space ğ•œ]

theorem mem_span_iff_continuous_of_finite {f : Î¹ â†’ E â†’â‚—[ğ•œ] ğ•œ} (Ï† : E â†’â‚—[ğ•œ] ğ•œ) :
    Ï† âˆˆ Submodule.span ğ•œ (Set.range f) â†”
    Continuous[â¨… i, induced (f i) tğ•œ, tğ•œ] Ï† := by
  let _ := â¨… i, induced (f i) tğ•œ
  constructor
  Â· exact Submodule.span_induction
      (Set.forall_mem_range.mpr fun i â†¦ continuous_iInf_dom continuous_induced_dom) continuous_zero
      (fun _ _ _ _ â†¦ .add) (fun c _ _ h â†¦ h.const_smul c)
  Â· intro Ï†_cont
    refine mem_span_of_iInf_ker_le_ker fun x hx â†¦ ?_
    simp_rw [Submodule.mem_iInf, LinearMap.mem_ker] at hx âŠ¢
    have : Inseparable x 0 := by
      -- Maybe missing lemmas about `Inseparable`?
      simp_rw [Inseparable, nhds_iInf, nhds_induced, hx, map_zero]
    simpa only [map_zero] using (this.map Ï†_cont).eq

end TopologicalRing

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]

theorem mem_span_iff_continuous {f : Î¹ â†’ E â†’â‚—[ğ•œ] ğ•œ} (Ï† : E â†’â‚—[ğ•œ] ğ•œ) :
    Ï† âˆˆ Submodule.span ğ•œ (Set.range f) â†”
    Continuous[â¨… i, induced (f i) inferInstance, inferInstance] Ï† := by
  letI tğ•œ : TopologicalSpace ğ•œ := inferInstance
  let t := â¨… i, induced (f i) tğ•œ
  constructor
  -- TODO: is it worth factoring the first implication with `mem_span_iff_continuous_of_finite`?
  Â· exact Submodule.span_induction
      (Set.forall_mem_range.mpr fun i â†¦ continuous_iInf_dom continuous_induced_dom) continuous_zero
      (fun _ _ _ _ â†¦ .add) (fun c _ _ h â†¦ h.const_smul c)
  Â· intro Ï†_cont
    obtain âŸ¨s, hsâŸ© : âˆƒ s : Finset Î¹, Continuous[â¨… i : s, induced (f i) tğ•œ, tğ•œ] Ï† := by
      -- The following should be golfable by using/developping better API
      have : Ï† â»Â¹' (Metric.ball 0 1) âˆˆ ğ“ 0 :=
        Ï†_cont.continuousAt.tendsto (map_zero Ï† â–¸ Metric.ball_mem_nhds (0 : ğ•œ) one_pos)
      rw [nhds_iInf, Filter.mem_iInf_finite] at this
      rcases this with âŸ¨s, hsâŸ©
      use s
      let t' := â¨… i : s, induced (f i) tğ•œ
      have : IsTopologicalAddGroup E :=
        topologicalAddGroup_iInf fun _ â†¦ topologicalAddGroup_induced _
      have : ContinuousSMul ğ•œ E :=
        continuousSMul_iInf fun _ â†¦ continuousSMul_induced _
      rw [Seminorm.continuous_iff_continuous_comp (norm_withSeminorms ğ•œ ğ•œ), forall_const]
      refine Seminorm.continuous (r := 1) ?_
      rwa [nhds_iInf, Seminorm.ball_comp, ball_normSeminorm, iInf_subtype, map_zero]
    rw [â† LinearMap.mem_span_iff_continuous_of_finite] at hs
    exact Submodule.span_mono (Set.range_comp_subset_range _ _) hs

theorem mem_span_iff_bound [Nonempty Î¹] {f : Î¹ â†’ E â†’â‚—[ğ•œ] ğ•œ} (Ï† : E â†’â‚—[ğ•œ] ğ•œ) :
    Ï† âˆˆ Submodule.span ğ•œ (Set.range f) â†”
    âˆƒ s : Finset Î¹, âˆƒ c : â„â‰¥0, (normSeminorm ğ•œ ğ•œ).comp Ï† â‰¤
      c â€¢ (s.sup fun i â†¦ (normSeminorm ğ•œ ğ•œ).comp (f i)) := by
  letI tğ•œ : TopologicalSpace ğ•œ := inferInstance
  let t := â¨… i, induced (f i) tğ•œ
  have : IsTopologicalAddGroup E := topologicalAddGroup_iInf fun _ â†¦ topologicalAddGroup_induced _
  have : WithSeminorms (fun i â†¦ (normSeminorm ğ•œ ğ•œ).comp (f i)) := by
    simp_rw [SeminormFamily.withSeminorms_iff_nhds_eq_iInf, nhds_iInf, nhds_induced, map_zero,
      â† comap_norm_nhds_zero (E := ğ•œ), Filter.comap_comap]
    rfl
  rw [LinearMap.mem_span_iff_continuous]
  constructor <;> intro H
  Â· rw [Seminorm.continuous_iff_continuous_comp (norm_withSeminorms ğ•œ ğ•œ), forall_const] at H
    rcases Seminorm.bound_of_continuous this _ H with âŸ¨s, C, -, hCâŸ©
    exact âŸ¨s, C, hCâŸ©
  Â· exact Seminorm.cont_withSeminorms_normedSpace _ this _ H

example [AddCommGroup F] [Module ğ•œ F] {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (f : E â†’â‚—[ğ•œ] ğ•œ) :
    f âˆˆ Submodule.span ğ•œ (Set.range (B.flip)) â†”
    âˆƒ s : Finset F, âˆƒ (Î³ : NNReal), f.toSeminorm â‰¤ Î³ â€¢ (s.sup B.toSeminormFamily) := by
  apply mem_span_iff_bound f

end NontriviallyNormedField

end

end LinearMap

end BilinForm

section Topology

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]

theorem LinearMap.weakBilin_withSeminorms (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    WithSeminorms (LinearMap.toSeminormFamily B : F â†’ Seminorm ğ•œ (WeakBilin B)) :=
  let e : F â‰ƒ (Î£ _ : F, Fin 1) := .symm <| .sigmaUnique _ _
  have : Nonempty (Î£ _ : F, Fin 1) := e.symm.nonempty
  withSeminorms_induced (withSeminorms_pi (fun _ â†¦ norm_withSeminorms ğ•œ ğ•œ))
    (LinearMap.ltoFun ğ•œ F ğ•œ âˆ˜â‚— B : (WeakBilin B) â†’â‚—[ğ•œ] (F â†’ ğ•œ)) |>.congr_equiv e

theorem LinearMap.hasBasis_weakBilin (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    (ğ“ (0 : WeakBilin B)).HasBasis B.toSeminormFamily.basisSets _root_.id :=
  LinearMap.weakBilin_withSeminorms B |>.hasBasis

end Topology

section LocallyConvex

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]
variable [NormedSpace â„ ğ•œ] [Module â„ E] [IsScalarTower â„ ğ•œ E]

instance WeakBilin.locallyConvexSpace {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} :
    LocallyConvexSpace â„ (WeakBilin B) :=
  B.weakBilin_withSeminorms.toLocallyConvexSpace

end LocallyConvex
