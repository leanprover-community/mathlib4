/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Algebra.Module.WeakBilin
import Mathlib.Data.Finsupp.Order
import Mathlib.LinearAlgebra.Dual.Lemmas

/-!
# Weak Dual in Topological Vector Spaces

We prove that the weak topology induced by a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ` is locally
convex and we explicitly give a neighborhood basis in terms of the family of seminorms
`fun x => â€–B x yâ€–` for `y : F`.

## Main definitions

* `LinearMap.toSeminorm`: turn a linear form `f : E â†’â‚—[ğ•œ] ğ•œ` into a seminorm `fun x => â€–f xâ€–`.
* `LinearMap.toSeminormFamily`: turn a bilinear form `B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ` into a map
  `F â†’ Seminorm ğ•œ E`.

## Main statements

* `LinearMap.hasBasis_weakBilin`: the seminorm balls of `B.toSeminormFamily` form a
  neighborhood basis of `0` in the weak topology.
* `LinearMap.toSeminormFamily.withSeminorms`: the topology of a weak space is induced by the
  family of seminorms `B.toSeminormFamily`.
* `WeakBilin.locallyConvexSpace`: a space endowed with a weak topology is locally convex.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]
* [Rudin, *Functional Analysis*][rudin1991]

## Tags

weak dual, seminorm
-/


variable {ğ•œ E F : Type*}

open Topology

section BilinForm

namespace LinearMap

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]

/-- Construct a seminorm from a linear form `f : E â†’â‚—[ğ•œ] ğ•œ` over a normed field `ğ•œ` by
`fun x => â€–f xâ€–` -/
def toSeminorm (f : E â†’â‚—[ğ•œ] ğ•œ) : Seminorm ğ•œ E :=
  (normSeminorm ğ•œ ğ•œ).comp f

theorem coe_toSeminorm {f : E â†’â‚—[ğ•œ] ğ•œ} : â‡‘f.toSeminorm = fun x => â€–f xâ€– :=
  rfl

@[simp]
theorem toSeminorm_apply {f : E â†’â‚—[ğ•œ] ğ•œ} {x : E} : f.toSeminorm x = â€–f xâ€– :=
  rfl

theorem toSeminorm_ball_zero {f : E â†’â‚—[ğ•œ] ğ•œ} {r : â„} :
    Seminorm.ball f.toSeminorm 0 r = { x : E | â€–f xâ€– < r } := by
  simp only [Seminorm.ball_zero_eq, toSeminorm_apply]

theorem toSeminorm_comp (f : F â†’â‚—[ğ•œ] ğ•œ) (g : E â†’â‚—[ğ•œ] F) :
    f.toSeminorm.comp g = (f.comp g).toSeminorm := by
  ext
  simp only [Seminorm.comp_apply, toSeminorm_apply, coe_comp, Function.comp_apply]

/-- Construct a family of seminorms from a bilinear form. -/
def toSeminormFamily (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) : SeminormFamily ğ•œ E F := fun y =>
  (B.flip y).toSeminorm

@[simp]
theorem toSeminormFamily_apply {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} {x y} : (B.toSeminormFamily y) x = â€–B x yâ€– :=
  rfl

theorem functional_mem_span_iff {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (s : Finset F) (f : E â†’â‚—[ğ•œ] ğ•œ) :
    f âˆˆ Submodule.span ğ•œ (Set.range (B.flip âˆ˜ Subtype.val : s â†’ E â†’â‚—[ğ•œ] ğ•œ)) â†”
    âˆƒ Î³, âˆ€ (x : E), â€–f xâ€– â‰¤ Î³ * ((s.sup B.toSeminormFamily) x) := by
  constructor
  Â· intro h
    rw [â† Set.image_univ, Finsupp.mem_span_image_iff_linearCombination] at h
    obtain âŸ¨l, hl1, hl2âŸ© := h
    use (l.sum fun i d â†¦ â€–dâ€–)
    intro x
    rw [â† hl2, Finsupp.linearCombination_apply, finsupp_sum_apply,
      (Finsupp.sum_mul ((s.sup B.toSeminormFamily) x) l)]
    have e4' (i : s) : (B.toSeminormFamily i) x â‰¤ (s.sup B.toSeminormFamily) x :=
      Seminorm.le_finset_sup_apply (Finset.coe_mem i)
    have e4 (d : ğ•œ) (i : s) :
        â€–d * ((B.flip âˆ˜ Subtype.val) i) xâ€– â‰¤ â€–dâ€– * ((s.sup B.toSeminormFamily) x) := by
      rw [norm_mul]
      exact mul_le_mul_of_nonneg_left (e4' i) (norm_nonneg d)
    have e6 : (l.sum fun i d â†¦ â€–d * ((B.flip âˆ˜ Subtype.val) i) xâ€–) â‰¤
        (l.sum fun i d â†¦ (â€–dâ€– * ((s.sup B.toSeminormFamily) x))) :=
      Finsupp.sum_le_sum (Î± := ğ•œ) (Î² := â„) (fun i _ => e4 (l i) i)
    apply le_trans (norm_sum_le _ _)
    exact (le_trans e6 (Preorder.le_refl (l.sum fun i d â†¦ â€–dâ€– * (s.sup B.toSeminormFamily) x)))
  Â· intro âŸ¨Î³, hÎ³âŸ©
    apply mem_span_of_iInf_ker_le_ker
    intro x hx
    rw [mem_ker, â† norm_le_zero_iff]
    convert (hÎ³ x)
    rw [Submodule.mem_iInf, Subtype.forall] at hx
    have e1 : (s.sup B.toSeminormFamily) x = 0 := by
      rw [le_antisymm_iff]
      constructor
      Â· apply Seminorm.finset_sup_apply_le (Preorder.le_refl 0)
        intro i his
        rw [toSeminormFamily_apply, norm_le_zero_iff]
        exact hx _ his
      Â· exact apply_nonneg (s.sup B.toSeminormFamily) x
    simp_all only [mul_zero]

end LinearMap

end BilinForm

section Topology

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]

theorem LinearMap.weakBilin_withSeminorms (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    WithSeminorms (LinearMap.toSeminormFamily B : F â†’ Seminorm ğ•œ (WeakBilin B)) :=
  let e : F â‰ƒ (Î£ _ : F, Fin 1) := .symm <| .sigmaUnique _ _
  have : Nonempty (Î£ _ : F, Fin 1) := e.symm.nonempty
  withSeminorms_induced (withSeminorms_pi (fun _ â†¦ norm_withSeminorms ğ•œ ğ•œ))
    (LinearMap.ltoFun ğ•œ F ğ•œ âˆ˜â‚— B : (WeakBilin B) â†’â‚—[ğ•œ] (F â†’ ğ•œ)) |>.congr_equiv e

theorem LinearMap.hasBasis_weakBilin (B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ) :
    (ğ“ (0 : WeakBilin B)).HasBasis B.toSeminormFamily.basisSets _root_.id :=
  LinearMap.weakBilin_withSeminorms B |>.hasBasis

end Topology

section LocallyConvex

variable [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F]
variable [NormedSpace â„ ğ•œ] [Module â„ E] [IsScalarTower â„ ğ•œ E]

instance WeakBilin.locallyConvexSpace {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} :
    LocallyConvexSpace â„ (WeakBilin B) :=
  B.weakBilin_withSeminorms.toLocallyConvexSpace

end LocallyConvex
