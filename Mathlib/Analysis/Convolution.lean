/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Function.LocallyIntegrable
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.MeasureTheory.Group.Prod
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Convolution of functions

This file defines the convolution on two functions, i.e. `x ‚Ü¶ ‚à´ f(t)g(x - t) ‚àÇt`.
In the general case, these functions can be vector-valued, and have an arbitrary (additive)
group as domain. We use a continuous bilinear operation `L` on these function values as
"multiplication". The domain must be equipped with a Haar measure `Œº`
(though many individual results have weaker conditions on `Œº`).

For many applications we can take `L = ContinuousLinearMap.lsmul ‚Ñù ‚Ñù` or
`L = ContinuousLinearMap.mul ‚Ñù ‚Ñù`.

We also define `ConvolutionExists` and `ConvolutionExistsAt` to state that the convolution is
well-defined (everywhere or at a single point). These conditions are needed for pointwise
computations (e.g. `ConvolutionExistsAt.distrib_add`), but are generally not strong enough for any
local (or global) properties of the convolution. For this we need stronger assumptions on `f`
and/or `g`, and generally if we impose stronger conditions on one of the functions, we can impose
weaker conditions on the other.
We have proven many of the properties of the convolution assuming one of these functions
has compact support (in which case the other function only needs to be locally integrable).
We still need to prove the properties for other pairs of conditions (e.g. both functions are
rapidly decreasing)

# Design Decisions

We use a bilinear map `L` to "multiply" the two functions in the integrand.
This generality has several advantages

* This allows us to compute the total derivative of the convolution, in case the functions are
  multivariate. The total derivative is again a convolution, but where the codomains of the
  functions can be higher-dimensional. See `HasCompactSupport.hasFDerivAt_convolution_right`.
* This allows us to use `@[to_additive]` everywhere (which would not be possible if we would use
  `mul`/`smul` in the integral, since `@[to_additive]` will incorrectly also try to additivize
  those definitions).
* We need to support the case where at least one of the functions is vector-valued, but if we use
  `smul` to multiply the functions, that would be an asymmetric definition.

# Main Definitions
* `MeasureTheory.convolution f g L Œº x = (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº`
  is the convolution of `f` and `g` w.r.t. the continuous bilinear map `L` and measure `Œº`.
* `MeasureTheory.ConvolutionExistsAt f g x L Œº` states that the convolution `(f ‚ãÜ[L, Œº] g) x`
  is well-defined (i.e. the integral exists).
* `MeasureTheory.ConvolutionExists f g L Œº` states that the convolution `f ‚ãÜ[L, Œº] g`
  is well-defined at each point.

# Main Results
* `HasCompactSupport.hasFDerivAt_convolution_right` and
  `HasCompactSupport.hasFDerivAt_convolution_left`: we can compute the total derivative
  of the convolution as a convolution with the total derivative of the right (left) function.
* `HasCompactSupport.contDiff_convolution_right` and
  `HasCompactSupport.contDiff_convolution_left`: the convolution is `ùíû‚Åø` if one of the functions
  is `ùíû‚Åø` with compact support and the other function in locally integrable.

Versions of these statements for functions depending on a parameter are also given.

* `MeasureTheory.convolution_tendsto_right`: Given a sequence of nonnegative normalized functions
  whose support tends to a small neighborhood around `0`, the convolution tends to the right
  argument. This is specialized to bump functions in `ContDiffBump.convolution_tendsto_right`.

# Notation
The following notations are localized in the locale `Convolution`:
* `f ‚ãÜ[L, Œº] g` for the convolution. Note: you have to use parentheses to apply the convolution
  to an argument: `(f ‚ãÜ[L, Œº] g) x`.
* `f ‚ãÜ[L] g := f ‚ãÜ[L, volume] g`
* `f ‚ãÜ g := f ‚ãÜ[lsmul ‚Ñù ‚Ñù] g`

# To do
* Existence and (uniform) continuity of the convolution if
  one of the maps is in `‚Ñí^p` and the other in `‚Ñí^q` with `1 / p + 1 / q = 1`.
  This might require a generalization of `MeasureTheory.MemLp.smul` where `smul` is generalized
  to a continuous bilinear map.
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255K)
* The convolution is an `AEStronglyMeasurable` function
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255I).
* Prove properties about the convolution if both functions are rapidly decreasing.
* Use `@[to_additive]` everywhere (this likely requires changes in `to_additive`)
-/
open Set Function Filter MeasureTheory MeasureTheory.Measure TopologicalSpace

open Bornology ContinuousLinearMap Metric Topology
open scoped Pointwise NNReal Filter

universe uùïú uG uE uE' uE'' uF uF' uF'' uP

variable {ùïú : Type uùïú} {G : Type uG} {E : Type uE} {E' : Type uE'} {E'' : Type uE''} {F : Type uF}
  {F' : Type uF'} {F'' : Type uF''} {P : Type uP}

variable [NormedAddCommGroup E] [NormedAddCommGroup E'] [NormedAddCommGroup E'']
  [NormedAddCommGroup F] {f f' : G ‚Üí E} {g g' : G ‚Üí E'} {x x' : G} {y y' : E}

namespace MeasureTheory
section NontriviallyNormedField

variable [NontriviallyNormedField ùïú]
variable [NormedSpace ùïú E] [NormedSpace ùïú E'] [NormedSpace ùïú E''] [NormedSpace ùïú F]
variable (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

section NoMeasurability

variable [AddGroup G] [TopologicalSpace G]

theorem convolution_integrand_bound_right_of_le_of_subset {C : ‚Ñù} (hC : ‚àÄ i, ‚Äñg i‚Äñ ‚â§ C) {x t : G}
    {s u : Set G} (hx : x ‚àà s) (hu : -tsupport g + s ‚äÜ u) :
    ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ u.indicator (fun t => ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * C) t := by
  -- Porting note: had to add `f := _`
  refine le_indicator (f := fun t ‚Ü¶ ‚ÄñL (f t) (g (x - t))‚Äñ) (fun t _ => ?_) (fun t ht => ?_) t
  ¬∑ apply_rules [L.le_of_opNorm‚ÇÇ_le_of_le, le_rfl]
  ¬∑ have : x - t ‚àâ support g := by
      refine mt (fun hxt => hu ?_) ht
      refine ‚ü®_, Set.neg_mem_neg.mpr (subset_closure hxt), _, hx, ?_‚ü©
      simp only [neg_sub, sub_add_cancel]
    simp only [notMem_support.mp this, (L _).map_zero, norm_zero, le_rfl]

theorem _root_.HasCompactSupport.convolution_integrand_bound_right_of_subset
    (hcg : HasCompactSupport g) (hg : Continuous g)
    {x t : G} {s u : Set G} (hx : x ‚àà s) (hu : -tsupport g + s ‚äÜ u) :
    ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ u.indicator (fun t => ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚®Ü i, ‚Äñg i‚Äñ) t := by
  refine convolution_integrand_bound_right_of_le_of_subset _ (fun i => ?_) hx hu
  exact le_ciSup (hg.norm.bddAbove_range_of_hasCompactSupport hcg.norm) _

theorem _root_.HasCompactSupport.convolution_integrand_bound_right (hcg : HasCompactSupport g)
    (hg : Continuous g) {x t : G} {s : Set G} (hx : x ‚àà s) :
    ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ (-tsupport g + s).indicator (fun t => ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚®Ü i, ‚Äñg i‚Äñ) t :=
  hcg.convolution_integrand_bound_right_of_subset L hg hx Subset.rfl

theorem _root_.Continuous.convolution_integrand_fst [ContinuousSub G] (hg : Continuous g) (t : G) :
    Continuous fun x => L (f t) (g (x - t)) :=
  L.continuous‚ÇÇ.comp‚ÇÇ continuous_const <| hg.comp <| continuous_id.sub continuous_const

theorem _root_.HasCompactSupport.convolution_integrand_bound_left (hcf : HasCompactSupport f)
    (hf : Continuous f) {x t : G} {s : Set G} (hx : x ‚àà s) :
    ‚ÄñL (f (x - t)) (g t)‚Äñ ‚â§
      (-tsupport f + s).indicator (fun t => (‚ÄñL‚Äñ * ‚®Ü i, ‚Äñf i‚Äñ) * ‚Äñg t‚Äñ) t := by
  convert hcf.convolution_integrand_bound_right L.flip hf hx using 1
  simp_rw [L.opNorm_flip, mul_right_comm]

end NoMeasurability

section Measurability
variable [MeasurableSpace G] {Œº ŒΩ : Measure G}

/-- The convolution of `f` and `g` exists at `x` when the function `t ‚Ü¶ L (f t) (g (x - t))` is
integrable. There are various conditions on `f` and `g` to prove this. -/
def ConvolutionExistsAt [Sub G] (f : G ‚Üí E) (g : G ‚Üí E') (x : G) (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
    (Œº : Measure G := by volume_tac) : Prop :=
  Integrable (fun t => L (f t) (g (x - t))) Œº

/-- The convolution of `f` and `g` exists when the function `t ‚Ü¶ L (f t) (g (x - t))` is integrable
for all `x : G`. There are various conditions on `f` and `g` to prove this. -/
def ConvolutionExists [Sub G] (f : G ‚Üí E) (g : G ‚Üí E') (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
    (Œº : Measure G := by volume_tac) : Prop :=
  ‚àÄ x : G, ConvolutionExistsAt f g x L Œº

section ConvolutionExists

variable {L} in
theorem ConvolutionExistsAt.integrable [Sub G] {x : G} (h : ConvolutionExistsAt f g x L Œº) :
    Integrable (fun t => L (f t) (g (x - t))) Œº :=
  h

section Group

variable [AddGroup G]

theorem AEStronglyMeasurable.convolution_integrand' [MeasurableAdd‚ÇÇ G]
    [MeasurableNeg G] (hf : AEStronglyMeasurable f ŒΩ)
    (hg : AEStronglyMeasurable g <| map (fun p : G √ó G => p.1 - p.2) (Œº.prod ŒΩ)) :
    AEStronglyMeasurable (fun p : G √ó G => L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
  L.aestronglyMeasurable_comp‚ÇÇ hf.comp_snd <| hg.comp_measurable measurable_sub

section

variable [MeasurableAdd G] [MeasurableNeg G]

theorem AEStronglyMeasurable.convolution_integrand_snd'
    (hf : AEStronglyMeasurable f Œº) {x : G}
    (hg : AEStronglyMeasurable g <| map (fun t => x - t) Œº) :
    AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Œº :=
  L.aestronglyMeasurable_comp‚ÇÇ hf <| hg.comp_measurable <| measurable_id.const_sub x

theorem AEStronglyMeasurable.convolution_integrand_swap_snd' {x : G}
    (hf : AEStronglyMeasurable f <| map (fun t => x - t) Œº) (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (fun t => L (f (x - t)) (g t)) Œº :=
  L.aestronglyMeasurable_comp‚ÇÇ (hf.comp_measurable <| measurable_id.const_sub x) hg

/-- A sufficient condition to prove that `f ‚ãÜ[L, Œº] g` exists.
We assume that `f` is integrable on a set `s` and `g` is bounded and ae strongly measurable
on `x‚ÇÄ - s` (note that both properties hold if `g` is continuous with compact support). -/
theorem _root_.BddAbove.convolutionExistsAt' {x‚ÇÄ : G} {s : Set G}
    (hbg : BddAbove ((fun i => ‚Äñg i‚Äñ) '' ((fun t => -t + x‚ÇÄ) ‚Åª¬π' s))) (hs : MeasurableSet s)
    (h2s : (support fun t => L (f t) (g (x‚ÇÄ - t))) ‚äÜ s) (hf : IntegrableOn f s Œº)
    (hmg : AEStronglyMeasurable g <| map (fun t => x‚ÇÄ - t) (Œº.restrict s)) :
    ConvolutionExistsAt f g x‚ÇÄ L Œº := by
  rw [ConvolutionExistsAt]
  rw [‚Üê integrableOn_iff_integrable_of_support_subset h2s]
  set s' := (fun t => -t + x‚ÇÄ) ‚Åª¬π' s
  have : ‚àÄ·µê t : G ‚àÇŒº.restrict s,
      ‚ÄñL (f t) (g (x‚ÇÄ - t))‚Äñ ‚â§ s.indicator (fun t => ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚®Ü i : s', ‚Äñg i‚Äñ) t := by
    filter_upwards
    refine le_indicator (fun t ht => ?_) fun t ht => ?_
    ¬∑ apply_rules [L.le_of_opNorm‚ÇÇ_le_of_le, le_rfl]
      refine (le_ciSup_set hbg <| mem_preimage.mpr ?_)
      rwa [neg_sub, sub_add_cancel]
    ¬∑ have : t ‚àâ support fun t => L (f t) (g (x‚ÇÄ - t)) := mt (fun h => h2s h) ht
      rw [notMem_support.mp this, norm_zero]
  refine Integrable.mono' ?_ ?_ this
  ¬∑ rw [integrable_indicator_iff hs]; exact ((hf.norm.const_mul _).mul_const _).integrableOn
  ¬∑ exact hf.aestronglyMeasurable.convolution_integrand_snd' L hmg

/-- If `‚Äñf‚Äñ *[Œº] ‚Äñg‚Äñ` exists, then `f *[L, Œº] g` exists. -/
theorem ConvolutionExistsAt.of_norm' {x‚ÇÄ : G}
    (h : ConvolutionExistsAt (fun x => ‚Äñf x‚Äñ) (fun x => ‚Äñg x‚Äñ) x‚ÇÄ (mul ‚Ñù ‚Ñù) Œº)
    (hmf : AEStronglyMeasurable f Œº) (hmg : AEStronglyMeasurable g <| map (fun t => x‚ÇÄ - t) Œº) :
    ConvolutionExistsAt f g x‚ÇÄ L Œº := by
  refine (h.const_mul ‚ÄñL‚Äñ).mono'
    (hmf.convolution_integrand_snd' L hmg) (Eventually.of_forall fun x => ?_)
  rw [mul_apply', ‚Üê mul_assoc]
  apply L.le_opNorm‚ÇÇ

@[deprecated (since := "2025-02-07")]
alias ConvolutionExistsAt.ofNorm' := ConvolutionExistsAt.of_norm'

end

section Left

variable [MeasurableAdd‚ÇÇ G] [MeasurableNeg G] [SFinite Œº] [IsAddRightInvariant Œº]

theorem AEStronglyMeasurable.convolution_integrand_snd (hf : AEStronglyMeasurable f Œº)
    (hg : AEStronglyMeasurable g Œº) (x : G) :
    AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Œº :=
  hf.convolution_integrand_snd' L <|
    hg.mono_ac <| (quasiMeasurePreserving_sub_left_of_right_invariant Œº x).absolutelyContinuous

theorem AEStronglyMeasurable.convolution_integrand_swap_snd
    (hf : AEStronglyMeasurable f Œº) (hg : AEStronglyMeasurable g Œº) (x : G) :
    AEStronglyMeasurable (fun t => L (f (x - t)) (g t)) Œº :=
  (hf.mono_ac
        (quasiMeasurePreserving_sub_left_of_right_invariant Œº
            x).absolutelyContinuous).convolution_integrand_swap_snd'
    L hg

/-- If `‚Äñf‚Äñ *[Œº] ‚Äñg‚Äñ` exists, then `f *[L, Œº] g` exists. -/
theorem ConvolutionExistsAt.of_norm {x‚ÇÄ : G}
    (h : ConvolutionExistsAt (fun x => ‚Äñf x‚Äñ) (fun x => ‚Äñg x‚Äñ) x‚ÇÄ (mul ‚Ñù ‚Ñù) Œº)
    (hmf : AEStronglyMeasurable f Œº) (hmg : AEStronglyMeasurable g Œº) :
    ConvolutionExistsAt f g x‚ÇÄ L Œº :=
  h.of_norm' L hmf <|
    hmg.mono_ac (quasiMeasurePreserving_sub_left_of_right_invariant Œº x‚ÇÄ).absolutelyContinuous

@[deprecated (since := "2025-02-07")]
alias ConvolutionExistsAt.ofNorm := ConvolutionExistsAt.of_norm

end Left

section Right

variable [MeasurableAdd‚ÇÇ G] [MeasurableNeg G] [SFinite Œº] [IsAddRightInvariant Œº] [SFinite ŒΩ]

theorem AEStronglyMeasurable.convolution_integrand (hf : AEStronglyMeasurable f ŒΩ)
    (hg : AEStronglyMeasurable g Œº) :
    AEStronglyMeasurable (fun p : G √ó G => L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
  hf.convolution_integrand' L <|
    hg.mono_ac (quasiMeasurePreserving_sub_of_right_invariant Œº ŒΩ).absolutelyContinuous

theorem Integrable.convolution_integrand (hf : Integrable f ŒΩ) (hg : Integrable g Œº) :
    Integrable (fun p : G √ó G => L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) := by
  have h_meas : AEStronglyMeasurable (fun p : G √ó G => L (f p.2) (g (p.1 - p.2))) (Œº.prod ŒΩ) :=
    hf.aestronglyMeasurable.convolution_integrand L hg.aestronglyMeasurable
  have h2_meas : AEStronglyMeasurable (fun y : G => ‚à´ x : G, ‚ÄñL (f y) (g (x - y))‚Äñ ‚àÇŒº) ŒΩ :=
    h_meas.prod_swap.norm.integral_prod_right'
  simp_rw [integrable_prod_iff' h_meas]
  refine ‚ü®Eventually.of_forall fun t => (L (f t)).integrable_comp (hg.comp_sub_right t), ?_‚ü©
  refine Integrable.mono' ?_ h2_meas
      (Eventually.of_forall fun t => (?_ : _ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚à´ x, ‚Äñg (x - t)‚Äñ ‚àÇŒº))
  ¬∑ simp only [integral_sub_right_eq_self (‚Äñg ¬∑‚Äñ)]
    exact (hf.norm.const_mul _).mul_const _
  ¬∑ simp_rw [‚Üê integral_const_mul]
    rw [Real.norm_of_nonneg (by positivity)]
    exact integral_mono_of_nonneg (Eventually.of_forall fun t => norm_nonneg _)
      ((hg.comp_sub_right t).norm.const_mul _) (Eventually.of_forall fun t => L.le_opNorm‚ÇÇ _ _)

theorem Integrable.ae_convolution_exists (hf : Integrable f ŒΩ) (hg : Integrable g Œº) :
    ‚àÄ·µê x ‚àÇŒº, ConvolutionExistsAt f g x L ŒΩ :=
  ((integrable_prod_iff <|
          hf.aestronglyMeasurable.convolution_integrand L hg.aestronglyMeasurable).mp <|
      hf.convolution_integrand L hg).1

end Right

variable [TopologicalSpace G] [IsTopologicalAddGroup G] [BorelSpace G]

theorem _root_.HasCompactSupport.convolutionExistsAt {x‚ÇÄ : G}
    (h : HasCompactSupport fun t => L (f t) (g (x‚ÇÄ - t))) (hf : LocallyIntegrable f Œº)
    (hg : Continuous g) : ConvolutionExistsAt f g x‚ÇÄ L Œº := by
  let u := (Homeomorph.neg G).trans (Homeomorph.addRight x‚ÇÄ)
  let v := (Homeomorph.neg G).trans (Homeomorph.addLeft x‚ÇÄ)
  apply ((u.isCompact_preimage.mpr h).bddAbove_image hg.norm.continuousOn).convolutionExistsAt' L
    isClosed_closure.measurableSet subset_closure (hf.integrableOn_isCompact h)
  have A : AEStronglyMeasurable (g ‚àò v)
      (Œº.restrict (tsupport fun t : G => L (f t) (g (x‚ÇÄ - t)))) := by
    apply (hg.comp v.continuous).continuousOn.aestronglyMeasurable_of_isCompact h
    exact (isClosed_tsupport _).measurableSet
  convert ((v.continuous.measurable.measurePreserving
      (Œº.restrict (tsupport fun t => L (f t) (g (x‚ÇÄ - t))))).aestronglyMeasurable_comp_iff
    v.measurableEmbedding).1 A
  ext x
  simp only [v, Homeomorph.neg, sub_eq_add_neg, val_toAddUnits_apply, Homeomorph.trans_apply,
    Equiv.neg_apply, Equiv.toFun_as_coe, Homeomorph.homeomorph_mk_coe, Equiv.coe_fn_mk,
    Homeomorph.coe_addLeft]

theorem _root_.HasCompactSupport.convolutionExists_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Œº) (hg : Continuous g) : ConvolutionExists f g L Œº := by
  intro x‚ÇÄ
  refine HasCompactSupport.convolutionExistsAt L ?_ hf hg
  refine (hcg.comp_homeomorph (Homeomorph.subLeft x‚ÇÄ)).mono ?_
  refine fun t => mt fun ht : g (x‚ÇÄ - t) = 0 => ?_
  simp_rw [ht, (L _).map_zero]

theorem _root_.HasCompactSupport.convolutionExists_left_of_continuous_right
    (hcf : HasCompactSupport f) (hf : LocallyIntegrable f Œº) (hg : Continuous g) :
    ConvolutionExists f g L Œº := by
  intro x‚ÇÄ
  refine HasCompactSupport.convolutionExistsAt L ?_ hf hg
  refine hcf.mono ?_
  refine fun t => mt fun ht : f t = 0 => ?_
  simp_rw [ht, L.map_zero‚ÇÇ]

end Group

section CommGroup

variable [AddCommGroup G]

section MeasurableGroup

variable [MeasurableNeg G] [IsAddLeftInvariant Œº]

/-- A sufficient condition to prove that `f ‚ãÜ[L, Œº] g` exists.
We assume that the integrand has compact support and `g` is bounded on this support (note that
both properties hold if `g` is continuous with compact support). We also require that `f` is
integrable on the support of the integrand, and that both functions are strongly measurable.

This is a variant of `BddAbove.convolutionExistsAt'` in an abelian group with a left-invariant
measure. This allows us to state the boundedness and measurability of `g` in a more natural way. -/
theorem _root_.BddAbove.convolutionExistsAt [MeasurableAdd‚ÇÇ G] [SFinite Œº] {x‚ÇÄ : G} {s : Set G}
    (hbg : BddAbove ((fun i => ‚Äñg i‚Äñ) '' ((fun t => x‚ÇÄ - t) ‚Åª¬π' s))) (hs : MeasurableSet s)
    (h2s : (support fun t => L (f t) (g (x‚ÇÄ - t))) ‚äÜ s) (hf : IntegrableOn f s Œº)
    (hmg : AEStronglyMeasurable g Œº) : ConvolutionExistsAt f g x‚ÇÄ L Œº := by
  refine BddAbove.convolutionExistsAt' L ?_ hs h2s hf ?_
  ¬∑ simp_rw [‚Üê sub_eq_neg_add, hbg]
  ¬∑ have : AEStronglyMeasurable g (map (fun t : G => x‚ÇÄ - t) Œº) :=
      hmg.mono_ac (quasiMeasurePreserving_sub_left_of_right_invariant Œº x‚ÇÄ).absolutelyContinuous
    apply this.mono_measure
    exact map_mono restrict_le_self (measurable_const.sub measurable_id')

variable {L} [MeasurableAdd G] [IsNegInvariant Œº]

theorem convolutionExistsAt_flip :
    ConvolutionExistsAt g f x L.flip Œº ‚Üî ConvolutionExistsAt f g x L Œº := by
  simp_rw [ConvolutionExistsAt, ‚Üê integrable_comp_sub_left (fun t => L (f t) (g (x - t))) x,
    sub_sub_cancel, flip_apply]

theorem ConvolutionExistsAt.integrable_swap (h : ConvolutionExistsAt f g x L Œº) :
    Integrable (fun t => L (f (x - t)) (g t)) Œº := by
  convert h.comp_sub_left x
  simp_rw [sub_sub_self]

theorem convolutionExistsAt_iff_integrable_swap :
    ConvolutionExistsAt f g x L Œº ‚Üî Integrable (fun t => L (f (x - t)) (g t)) Œº :=
  convolutionExistsAt_flip.symm

end MeasurableGroup

variable [TopologicalSpace G] [IsTopologicalAddGroup G] [BorelSpace G]
variable [IsAddLeftInvariant Œº] [IsNegInvariant Œº]

theorem _root_.HasCompactSupport.convolutionExists_left
    (hcf : HasCompactSupport f) (hf : Continuous f)
    (hg : LocallyIntegrable g Œº) : ConvolutionExists f g L Œº := fun x‚ÇÄ =>
  convolutionExistsAt_flip.mp <| hcf.convolutionExists_right L.flip hg hf x‚ÇÄ

@[deprecated (since := "2025-02-06")]
alias _root_.HasCompactSupport.convolutionExistsLeft := HasCompactSupport.convolutionExists_left

theorem _root_.HasCompactSupport.convolutionExists_right_of_continuous_left
    (hcg : HasCompactSupport g) (hf : Continuous f) (hg : LocallyIntegrable g Œº) :
    ConvolutionExists f g L Œº := fun x‚ÇÄ =>
  convolutionExistsAt_flip.mp <| hcg.convolutionExists_left_of_continuous_right L.flip hg hf x‚ÇÄ

@[deprecated (since := "2025-02-06")]
alias _root_.HasCompactSupport.convolutionExistsRightOfContinuousLeft :=
  HasCompactSupport.convolutionExists_right_of_continuous_left

end CommGroup

end ConvolutionExists

variable [NormedSpace ‚Ñù F]

/-- The convolution of two functions `f` and `g` with respect to a continuous bilinear map `L` and
measure `Œº`. It is defined to be `(f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº`. -/
noncomputable def convolution [Sub G] (f : G ‚Üí E) (g : G ‚Üí E') (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
    (Œº : Measure G := by volume_tac) : G ‚Üí F := fun x =>
  ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº

/-- The convolution of two functions with respect to a bilinear operation `L` and a measure `Œº`. -/
scoped[Convolution] notation:67 f " ‚ãÜ[" L:67 ", " Œº:67 "] " g:66 => convolution f g L Œº

/-- The convolution of two functions with respect to a bilinear operation `L` and the volume. -/
scoped[Convolution]
  notation:67 f " ‚ãÜ[" L:67 "]" g:66 => convolution f g L MeasureSpace.volume

/-- The convolution of two real-valued functions with respect to volume. -/
scoped[Convolution]
  notation:67 f " ‚ãÜ " g:66 =>
    convolution f g (ContinuousLinearMap.lsmul ‚Ñù ‚Ñù) MeasureSpace.volume

open scoped Convolution

theorem convolution_def [Sub G] : (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f t) (g (x - t)) ‚àÇŒº :=
  rfl

/-- The definition of convolution where the bilinear operator is scalar multiplication.
Note: it often helps the elaborator to give the type of the convolution explicitly. -/
theorem convolution_lsmul [Sub G] {f : G ‚Üí ùïú} {g : G ‚Üí F} :
    (f ‚ãÜ[lsmul ùïú ùïú, Œº] g : G ‚Üí F) x = ‚à´ t, f t ‚Ä¢ g (x - t) ‚àÇŒº :=
  rfl

/-- The definition of convolution where the bilinear operator is multiplication. -/
theorem convolution_mul [Sub G] [NormedSpace ‚Ñù ùïú] {f : G ‚Üí ùïú} {g : G ‚Üí ùïú} :
    (f ‚ãÜ[mul ùïú ùïú, Œº] g) x = ‚à´ t, f t * g (x - t) ‚àÇŒº :=
  rfl

section Group

variable {L} [AddGroup G]

theorem smul_convolution [SMulCommClass ‚Ñù ùïú F] {y : ùïú} : y ‚Ä¢ f ‚ãÜ[L, Œº] g = y ‚Ä¢ (f ‚ãÜ[L, Œº] g) := by
  ext; simp only [Pi.smul_apply, convolution_def, ‚Üê integral_smul, L.map_smul‚ÇÇ]

theorem convolution_smul [SMulCommClass ‚Ñù ùïú F] {y : ùïú} : f ‚ãÜ[L, Œº] y ‚Ä¢ g = y ‚Ä¢ (f ‚ãÜ[L, Œº] g) := by
  ext; simp only [Pi.smul_apply, convolution_def, ‚Üê integral_smul, (L _).map_smul]

@[simp]
theorem zero_convolution : 0 ‚ãÜ[L, Œº] g = 0 := by
  ext
  simp_rw [convolution_def, Pi.zero_apply, L.map_zero‚ÇÇ, integral_zero]

@[simp]
theorem convolution_zero : f ‚ãÜ[L, Œº] 0 = 0 := by
  ext
  simp_rw [convolution_def, Pi.zero_apply, (L _).map_zero, integral_zero]

theorem ConvolutionExistsAt.distrib_add {x : G} (hfg : ConvolutionExistsAt f g x L Œº)
    (hfg' : ConvolutionExistsAt f g' x L Œº) :
    (f ‚ãÜ[L, Œº] (g + g')) x = (f ‚ãÜ[L, Œº] g) x + (f ‚ãÜ[L, Œº] g') x := by
  simp only [convolution_def, (L _).map_add, Pi.add_apply, integral_add hfg hfg']

theorem ConvolutionExists.distrib_add (hfg : ConvolutionExists f g L Œº)
    (hfg' : ConvolutionExists f g' L Œº) : f ‚ãÜ[L, Œº] (g + g') = f ‚ãÜ[L, Œº] g + f ‚ãÜ[L, Œº] g' := by
  ext x
  exact (hfg x).distrib_add (hfg' x)

theorem ConvolutionExistsAt.add_distrib {x : G} (hfg : ConvolutionExistsAt f g x L Œº)
    (hfg' : ConvolutionExistsAt f' g x L Œº) :
    ((f + f') ‚ãÜ[L, Œº] g) x = (f ‚ãÜ[L, Œº] g) x + (f' ‚ãÜ[L, Œº] g) x := by
  simp only [convolution_def, L.map_add‚ÇÇ, Pi.add_apply, integral_add hfg hfg']

theorem ConvolutionExists.add_distrib (hfg : ConvolutionExists f g L Œº)
    (hfg' : ConvolutionExists f' g L Œº) : (f + f') ‚ãÜ[L, Œº] g = f ‚ãÜ[L, Œº] g + f' ‚ãÜ[L, Œº] g := by
  ext x
  exact (hfg x).add_distrib (hfg' x)

theorem convolution_mono_right {f g g' : G ‚Üí ‚Ñù} (hfg : ConvolutionExistsAt f g x (lsmul ‚Ñù ‚Ñù) Œº)
    (hfg' : ConvolutionExistsAt f g' x (lsmul ‚Ñù ‚Ñù) Œº) (hf : ‚àÄ x, 0 ‚â§ f x) (hg : ‚àÄ x, g x ‚â§ g' x) :
    (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x ‚â§ (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g') x := by
  apply integral_mono hfg hfg'
  simp only [lsmul_apply, Algebra.id.smul_eq_mul]
  intro t
  apply mul_le_mul_of_nonneg_left (hg _) (hf _)

theorem convolution_mono_right_of_nonneg {f g g' : G ‚Üí ‚Ñù}
    (hfg' : ConvolutionExistsAt f g' x (lsmul ‚Ñù ‚Ñù) Œº) (hf : ‚àÄ x, 0 ‚â§ f x) (hg : ‚àÄ x, g x ‚â§ g' x)
    (hg' : ‚àÄ x, 0 ‚â§ g' x) : (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x ‚â§ (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g') x := by
  by_cases H : ConvolutionExistsAt f g x (lsmul ‚Ñù ‚Ñù) Œº
  ¬∑ exact convolution_mono_right H hfg' hf hg
  have : (f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g) x = 0 := integral_undef H
  rw [this]
  exact integral_nonneg fun y => mul_nonneg (hf y) (hg' (x - y))

variable (L)

theorem convolution_congr [MeasurableAdd‚ÇÇ G] [MeasurableNeg G] [SFinite Œº]
    [IsAddRightInvariant Œº] (h1 : f =·µê[Œº] f') (h2 : g =·µê[Œº] g') : f ‚ãÜ[L, Œº] g = f' ‚ãÜ[L, Œº] g' := by
  ext x
  apply integral_congr_ae
  exact (h1.prodMk <| h2.comp_tendsto
    (quasiMeasurePreserving_sub_left_of_right_invariant Œº x).tendsto_ae).fun_comp ‚Üøfun x y ‚Ü¶ L x y

theorem support_convolution_subset_swap : support (f ‚ãÜ[L, Œº] g) ‚äÜ support g + support f := by
  intro x h2x
  by_contra hx
  apply h2x
  simp_rw [Set.mem_add, ‚Üê exists_and_left, not_exists, not_and_or, notMem_support] at hx
  rw [convolution_def]
  convert integral_zero G F using 2
  ext t
  rcases hx (x - t) t with (h | h | h)
  ¬∑ rw [h, (L _).map_zero]
  ¬∑ rw [h, L.map_zero‚ÇÇ]
  ¬∑ exact (h <| sub_add_cancel x t).elim

section

variable [MeasurableAdd‚ÇÇ G] [MeasurableNeg G] [SFinite Œº] [IsAddRightInvariant Œº]

theorem Integrable.integrable_convolution (hf : Integrable f Œº)
    (hg : Integrable g Œº) : Integrable (f ‚ãÜ[L, Œº] g) Œº :=
  (hf.convolution_integrand L hg).integral_prod_left

end

variable [TopologicalSpace G]
variable [IsTopologicalAddGroup G]

protected theorem _root_.HasCompactSupport.convolution [T2Space G] (hcf : HasCompactSupport f)
    (hcg : HasCompactSupport g) : HasCompactSupport (f ‚ãÜ[L, Œº] g) :=
  (hcg.isCompact.add hcf).of_isClosed_subset isClosed_closure <|
    closure_minimal
      ((support_convolution_subset_swap L).trans <| add_subset_add subset_closure subset_closure)
      (hcg.isCompact.add hcf).isClosed

variable [BorelSpace G] [TopologicalSpace P]

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in a subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem continuousOn_convolution_right_with_param {g : P ‚Üí G ‚Üí E'} {s : Set P} {k : Set G}
    (hk : IsCompact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
    (hf : LocallyIntegrable f Œº) (hg : ContinuousOn (‚Üøg) (s √óÀ¢ univ)) :
    ContinuousOn (fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) := by
  /- First get rid of the case where the space is not locally compact. Then `g` vanishes everywhere
  and the conclusion is trivial. -/
  by_cases H : ‚àÄ p ‚àà s, ‚àÄ x, g p x = 0
  ¬∑ apply (continuousOn_const (c := 0)).congr
    rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
    apply integral_eq_zero_of_ae (Eventually.of_forall (fun y ‚Ü¶ ?_))
    simp [H p hp _]
  have : LocallyCompactSpace G := by
    push_neg at H
    rcases H with ‚ü®p, hp, x, hx‚ü©
    have A : support (g p) ‚äÜ k := support_subset_iff'.2 (fun y hy ‚Ü¶ hgs p y hp hy)
    have B : Continuous (g p) := by
      refine hg.comp_continuous (.prodMk_right _) fun x => ?_
      simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hp
    rcases eq_zero_or_locallyCompactSpace_of_support_subset_isCompact_of_addGroup hk A B with H|H
    ¬∑ simp [H] at hx
    ¬∑ exact H
  /- Since `G` is locally compact, one may thicken `k` a little bit into a larger compact set
  `(-k) + t`, outside of which all functions that appear in the convolution vanish. Then we can
  apply a continuity statement for integrals depending on a parameter, with respect to
  locally integrable functions and compactly supported continuous functions. -/
  rintro ‚ü®q‚ÇÄ, x‚ÇÄ‚ü© ‚ü®hq‚ÇÄ, -‚ü©
  obtain ‚ü®t, t_comp, ht‚ü© : ‚àÉ t, IsCompact t ‚àß t ‚àà ùìù x‚ÇÄ := exists_compact_mem_nhds x‚ÇÄ
  let k' : Set G := (-k) +·µ• t
  have k'_comp : IsCompact k' := IsCompact.vadd_set hk.neg t_comp
  let g' : (P √ó G) ‚Üí G ‚Üí E' := fun p x ‚Ü¶ g p.1 (p.2 - x)
  let s' : Set (P √ó G) := s √óÀ¢ t
  have A : ContinuousOn g'.uncurry (s' √óÀ¢ univ) := by
    have : g'.uncurry = g.uncurry ‚àò (fun w ‚Ü¶ (w.1.1, w.1.2 - w.2)) := by ext y; rfl
    rw [this]
    refine hg.comp (by fun_prop) ?_
    simp +contextual [s', MapsTo]
  have B : ContinuousOn (fun a ‚Ü¶ ‚à´ x, L (f x) (g' a x) ‚àÇŒº) s' := by
    apply continuousOn_integral_bilinear_of_locally_integrable_of_compact_support L k'_comp A _
      (hf.integrableOn_isCompact k'_comp)
    rintro ‚ü®p, x‚ü© y ‚ü®hp, hx‚ü© hy
    apply hgs p _ hp
    contrapose! hy
    exact ‚ü®y - x, by simpa using hy, x, hx, by simp‚ü©
  apply ContinuousWithinAt.mono_of_mem_nhdsWithin (B (q‚ÇÄ, x‚ÇÄ) ‚ü®hq‚ÇÄ, mem_of_mem_nhds ht‚ü©)
  exact mem_nhdsWithin_prod_iff.2 ‚ü®s, self_mem_nhdsWithin, t, nhdsWithin_le_nhds ht, Subset.rfl‚ü©

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in an open subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of compositions with an additional continuous map. -/
theorem continuousOn_convolution_right_with_param_comp {s : Set P} {v : P ‚Üí G}
    (hv : ContinuousOn v s) {g : P ‚Üí G ‚Üí E'} {k : Set G} (hk : IsCompact k)
    (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0) (hf : LocallyIntegrable f Œº)
    (hg : ContinuousOn (‚Üøg) (s √óÀ¢ univ)) : ContinuousOn (fun x => (f ‚ãÜ[L, Œº] g x) (v x)) s := by
  apply
    (continuousOn_convolution_right_with_param L hk hgs hf hg).comp (continuousOn_id.prodMk hv)
  intro x hx
  simp only [hx, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

/-- The convolution is continuous if one function is locally integrable and the other has compact
support and is continuous. -/
theorem _root_.HasCompactSupport.continuous_convolution_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Œº) (hg : Continuous g) : Continuous (f ‚ãÜ[L, Œº] g) := by
  rw [continuous_iff_continuousOn_univ]
  let g' : G ‚Üí G ‚Üí E' := fun _ q => g q
  have : ContinuousOn (‚Üøg') (univ √óÀ¢ univ) := (hg.comp continuous_snd).continuousOn
  exact continuousOn_convolution_right_with_param_comp L
    (continuous_iff_continuousOn_univ.1 continuous_id) hcg
    (fun p x _ hx => image_eq_zero_of_notMem_tsupport hx) hf this

/-- The convolution is continuous if one function is integrable and the other is bounded and
continuous. -/
theorem _root_.BddAbove.continuous_convolution_right_of_integrable
    [FirstCountableTopology G] [SecondCountableTopologyEither G E']
    (hbg : BddAbove (range fun x => ‚Äñg x‚Äñ)) (hf : Integrable f Œº) (hg : Continuous g) :
    Continuous (f ‚ãÜ[L, Œº] g) := by
  refine continuous_iff_continuousAt.mpr fun x‚ÇÄ => ?_
  have : ‚àÄ·∂† x in ùìù x‚ÇÄ, ‚àÄ·µê t : G ‚àÇŒº, ‚ÄñL (f t) (g (x - t))‚Äñ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ * ‚®Ü i, ‚Äñg i‚Äñ := by
    filter_upwards with x; filter_upwards with t
    apply_rules [L.le_of_opNorm‚ÇÇ_le_of_le, le_rfl, le_ciSup hbg (x - t)]
  refine continuousAt_of_dominated ?_ this ?_ ?_
  ¬∑ exact Eventually.of_forall fun x =>
      hf.aestronglyMeasurable.convolution_integrand_snd' L hg.aestronglyMeasurable
  ¬∑ exact (hf.norm.const_mul _).mul_const _
  ¬∑ exact Eventually.of_forall fun t => (L.continuous‚ÇÇ.comp‚ÇÇ continuous_const <|
      hg.comp <| continuous_id.sub continuous_const).continuousAt

end Group

section CommGroup

variable [AddCommGroup G]

theorem support_convolution_subset : support (f ‚ãÜ[L, Œº] g) ‚äÜ support f + support g :=
  (support_convolution_subset_swap L).trans (add_comm _ _).subset

variable [IsAddLeftInvariant Œº] [IsNegInvariant Œº]

section Measurable

variable [MeasurableNeg G]
variable [MeasurableAdd G]

/-- Commutativity of convolution -/
theorem convolution_flip : g ‚ãÜ[L.flip, Œº] f = f ‚ãÜ[L, Œº] g := by
  ext1 x
  simp_rw [convolution_def]
  rw [‚Üê integral_sub_left_eq_self _ Œº x]
  simp_rw [sub_sub_self, flip_apply]

/-- The symmetric definition of convolution. -/
theorem convolution_eq_swap : (f ‚ãÜ[L, Œº] g) x = ‚à´ t, L (f (x - t)) (g t) ‚àÇŒº := by
  rw [‚Üê convolution_flip]; rfl

/-- The symmetric definition of convolution where the bilinear operator is scalar multiplication. -/
theorem convolution_lsmul_swap {f : G ‚Üí ùïú} {g : G ‚Üí F} :
    (f ‚ãÜ[lsmul ùïú ùïú, Œº] g : G ‚Üí F) x = ‚à´ t, f (x - t) ‚Ä¢ g t ‚àÇŒº :=
  convolution_eq_swap _

/-- The symmetric definition of convolution where the bilinear operator is multiplication. -/
theorem convolution_mul_swap [NormedSpace ‚Ñù ùïú] {f : G ‚Üí ùïú} {g : G ‚Üí ùïú} :
    (f ‚ãÜ[mul ùïú ùïú, Œº] g) x = ‚à´ t, f (x - t) * g t ‚àÇŒº :=
  convolution_eq_swap _

/-- The convolution of two even functions is also even. -/
theorem convolution_neg_of_neg_eq (h1 : ‚àÄ·µê x ‚àÇŒº, f (-x) = f x) (h2 : ‚àÄ·µê x ‚àÇŒº, g (-x) = g x) :
    (f ‚ãÜ[L, Œº] g) (-x) = (f ‚ãÜ[L, Œº] g) x :=
  calc
    ‚à´ t : G, (L (f t)) (g (-x - t)) ‚àÇŒº = ‚à´ t : G, (L (f (-t))) (g (x + t)) ‚àÇŒº := by
      apply integral_congr_ae
      filter_upwards [h1, (eventually_add_left_iff Œº x).2 h2] with t ht h't
      simp_rw [ht, ‚Üê h't, neg_add']
    _ = ‚à´ t : G, (L (f t)) (g (x - t)) ‚àÇŒº := by
      rw [‚Üê integral_neg_eq_self]
      simp only [neg_neg, ‚Üê sub_eq_add_neg]

end Measurable

variable [TopologicalSpace G]
variable [IsTopologicalAddGroup G]
variable [BorelSpace G]

theorem _root_.HasCompactSupport.continuous_convolution_left
    (hcf : HasCompactSupport f) (hf : Continuous f) (hg : LocallyIntegrable g Œº) :
    Continuous (f ‚ãÜ[L, Œº] g) := by
  rw [‚Üê convolution_flip]
  exact hcf.continuous_convolution_right L.flip hg hf

theorem _root_.BddAbove.continuous_convolution_left_of_integrable
    [FirstCountableTopology G] [SecondCountableTopologyEither G E]
    (hbf : BddAbove (range fun x => ‚Äñf x‚Äñ)) (hf : Continuous f) (hg : Integrable g Œº) :
    Continuous (f ‚ãÜ[L, Œº] g) := by
  rw [‚Üê convolution_flip]
  exact hbf.continuous_convolution_right_of_integrable L.flip hg hf

end CommGroup

section NormedAddCommGroup

variable [SeminormedAddCommGroup G]

/-- Compute `(f ‚ãÜ g) x‚ÇÄ` if the support of the `f` is within `Metric.ball 0 R`, and `g` is constant
on `Metric.ball x‚ÇÄ R`.

We can simplify the RHS further if we assume `f` is integrable, but also if `L = (‚Ä¢)` or more
generally if `L` has an `AntilipschitzWith`-condition. -/
theorem convolution_eq_right' {x‚ÇÄ : G} {R : ‚Ñù} (hf : support f ‚äÜ ball (0 : G) R)
    (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, g x = g x‚ÇÄ) : (f ‚ãÜ[L, Œº] g) x‚ÇÄ = ‚à´ t, L (f t) (g x‚ÇÄ) ‚àÇŒº := by
  have h2 : ‚àÄ t, L (f t) (g (x‚ÇÄ - t)) = L (f t) (g x‚ÇÄ) := fun t ‚Ü¶ by
    by_cases ht : t ‚àà support f
    ¬∑ have h2t := hf ht
      rw [mem_ball_zero_iff] at h2t
      specialize hg (x‚ÇÄ - t)
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, ‚Üê sub_eq_add_neg] at hg
      rw [hg h2t]
    ¬∑ rw [notMem_support] at ht
      simp_rw [ht, L.map_zero‚ÇÇ]
  simp_rw [convolution_def, h2]

variable [BorelSpace G] [SecondCountableTopology G]
variable [IsAddLeftInvariant Œº] [SFinite Œº]

/-- Approximate `(f ‚ãÜ g) x‚ÇÄ` if the support of the `f` is bounded within a ball, and `g` is near
`g x‚ÇÄ` on a ball with the same radius around `x‚ÇÄ`. See `dist_convolution_le` for a special case.

We can simplify the second argument of `dist` further if we add some extra type-classes on `E`
and `ùïú` or if `L` is scalar multiplication. -/
theorem dist_convolution_le' {x‚ÇÄ : G} {R Œµ : ‚Ñù} {z‚ÇÄ : E'} (hŒµ : 0 ‚â§ Œµ) (hif : Integrable f Œº)
    (hf : support f ‚äÜ ball (0 : G) R) (hmg : AEStronglyMeasurable g Œº)
    (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, dist (g x) z‚ÇÄ ‚â§ Œµ) :
    dist ((f ‚ãÜ[L, Œº] g : G ‚Üí F) x‚ÇÄ) (‚à´ t, L (f t) z‚ÇÄ ‚àÇŒº) ‚â§ (‚ÄñL‚Äñ * ‚à´ x, ‚Äñf x‚Äñ ‚àÇŒº) * Œµ := by
  have hfg : ConvolutionExistsAt f g x‚ÇÄ L Œº := by
    refine BddAbove.convolutionExistsAt L ?_ Metric.isOpen_ball.measurableSet (Subset.trans ?_ hf)
      hif.integrableOn hmg
    swap; ¬∑ refine fun t => mt fun ht : f t = 0 => ?_; simp_rw [ht, L.map_zero‚ÇÇ]
    rw [bddAbove_def]
    refine ‚ü®‚Äñz‚ÇÄ‚Äñ + Œµ, ?_‚ü©
    rintro _ ‚ü®x, hx, rfl‚ü©
    refine norm_le_norm_add_const_of_dist_le (hg x ?_)
    rwa [mem_ball_iff_norm, norm_sub_rev, ‚Üê mem_ball_zero_iff]
  have h2 : ‚àÄ t, dist (L (f t) (g (x‚ÇÄ - t))) (L (f t) z‚ÇÄ) ‚â§ ‚ÄñL (f t)‚Äñ * Œµ := by
    intro t; by_cases ht : t ‚àà support f
    ¬∑ have h2t := hf ht
      rw [mem_ball_zero_iff] at h2t
      specialize hg (x‚ÇÄ - t)
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, ‚Üê sub_eq_add_neg] at hg
      refine ((L (f t)).dist_le_opNorm _ _).trans ?_
      exact mul_le_mul_of_nonneg_left (hg h2t) (norm_nonneg _)
    ¬∑ rw [notMem_support] at ht
      simp_rw [ht, L.map_zero‚ÇÇ, L.map_zero, norm_zero, zero_mul, dist_self]
      rfl
  simp_rw [convolution_def]
  simp_rw [dist_eq_norm] at h2 ‚ä¢
  rw [‚Üê integral_sub hfg.integrable]; swap; ¬∑ exact (L.flip z‚ÇÄ).integrable_comp hif
  refine (norm_integral_le_of_norm_le ((L.integrable_comp hif).norm.mul_const Œµ)
    (Eventually.of_forall h2)).trans ?_
  rw [integral_mul_const]
  refine mul_le_mul_of_nonneg_right ?_ hŒµ
  have h3 : ‚àÄ t, ‚ÄñL (f t)‚Äñ ‚â§ ‚ÄñL‚Äñ * ‚Äñf t‚Äñ := by
    intro t
    exact L.le_opNorm (f t)
  refine (integral_mono (L.integrable_comp hif).norm (hif.norm.const_mul _) h3).trans_eq ?_
  rw [integral_const_mul]

variable [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù E'] [CompleteSpace E']

/-- Approximate `f ‚ãÜ g` if the support of the `f` is bounded within a ball, and `g` is near `g x‚ÇÄ`
on a ball with the same radius around `x‚ÇÄ`.

This is a special case of `dist_convolution_le'` where `L` is `(‚Ä¢)`, `f` has integral 1 and `f` is
nonnegative. -/
theorem dist_convolution_le {f : G ‚Üí ‚Ñù} {x‚ÇÄ : G} {R Œµ : ‚Ñù} {z‚ÇÄ : E'} (hŒµ : 0 ‚â§ Œµ)
    (hf : support f ‚äÜ ball (0 : G) R) (hnf : ‚àÄ x, 0 ‚â§ f x) (hintf : ‚à´ x, f x ‚àÇŒº = 1)
    (hmg : AEStronglyMeasurable g Œº) (hg : ‚àÄ x ‚àà ball x‚ÇÄ R, dist (g x) z‚ÇÄ ‚â§ Œµ) :
    dist ((f ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g : G ‚Üí E') x‚ÇÄ) z‚ÇÄ ‚â§ Œµ := by
  have hif : Integrable f Œº := integrable_of_integral_eq_one hintf
  convert (dist_convolution_le' (lsmul ‚Ñù ‚Ñù) hŒµ hif hf hmg hg).trans _
  ¬∑ simp_rw [lsmul_apply, integral_smul_const, hintf, one_smul]
  ¬∑ simp_rw [Real.norm_of_nonneg (hnf _), hintf, mul_one]
    exact (mul_le_mul_of_nonneg_right opNorm_lsmul_le hŒµ).trans_eq (one_mul Œµ)

/-- `(œÜ i ‚ãÜ g i) (k i)` tends to `z‚ÇÄ` as `i` tends to some filter `l` if
* `œÜ` is a sequence of nonnegative functions with integral `1` as `i` tends to `l`;
* The support of `œÜ` tends to small neighborhoods around `(0 : G)` as `i` tends to `l`;
* `g i` is `mu`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `z‚ÇÄ` as `(i, x)` tends to `l √óÀ¢ ùìù x‚ÇÄ`;
* `k i` tends to `x‚ÇÄ`.

See also `ContDiffBump.convolution_tendsto_right`.
-/
theorem convolution_tendsto_right {Œπ} {g : Œπ ‚Üí G ‚Üí E'} {l : Filter Œπ} {x‚ÇÄ : G} {z‚ÇÄ : E'}
    {œÜ : Œπ ‚Üí G ‚Üí ‚Ñù} {k : Œπ ‚Üí G} (hnœÜ : ‚àÄ·∂† i in l, ‚àÄ x, 0 ‚â§ œÜ i x)
    (hiœÜ : ‚àÄ·∂† i in l, ‚à´ x, œÜ i x ‚àÇŒº = 1)
    -- todo: we could weaken this to "the integral tends to 1"
    (hœÜ : Tendsto (fun n => support (œÜ n)) l (ùìù 0).smallSets)
    (hmg : ‚àÄ·∂† i in l, AEStronglyMeasurable (g i) Œº) (hcg : Tendsto (uncurry g) (l √óÀ¢ ùìù x‚ÇÄ) (ùìù z‚ÇÄ))
    (hk : Tendsto k l (ùìù x‚ÇÄ)) :
    Tendsto (fun i : Œπ => (œÜ i ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] g i : G ‚Üí E') (k i)) l (ùìù z‚ÇÄ) := by
  simp_rw [tendsto_smallSets_iff] at hœÜ
  rw [Metric.tendsto_nhds] at hcg ‚ä¢
  simp_rw [Metric.eventually_prod_nhds_iff] at hcg
  intro Œµ hŒµ
  have h2Œµ : 0 < Œµ / 3 := div_pos hŒµ (by norm_num)
  obtain ‚ü®p, hp, Œ¥, hŒ¥, hgŒ¥‚ü© := hcg _ h2Œµ
  dsimp only [uncurry] at hgŒ¥
  have h2k := hk.eventually (ball_mem_nhds x‚ÇÄ <| half_pos hŒ¥)
  have h2œÜ := hœÜ (ball (0 : G) _) <| ball_mem_nhds _ (half_pos hŒ¥)
  filter_upwards [hp, h2k, h2œÜ, hnœÜ, hiœÜ, hmg] with i hpi hki hœÜi hnœÜi hiœÜi hmgi
  have hgi : dist (g i (k i)) z‚ÇÄ < Œµ / 3 := hgŒ¥ hpi (hki.trans <| half_lt_self hŒ¥)
  have h1 : ‚àÄ x' ‚àà ball (k i) (Œ¥ / 2), dist (g i x') (g i (k i)) ‚â§ Œµ / 3 + Œµ / 3 := by
    intro x' hx'
    refine (dist_triangle_right _ _ _).trans (add_le_add (hgŒ¥ hpi ?_).le hgi.le)
    exact ((dist_triangle _ _ _).trans_lt (add_lt_add hx'.out hki)).trans_eq (add_halves Œ¥)
  have := dist_convolution_le (add_pos h2Œµ h2Œµ).le hœÜi hnœÜi hiœÜi hmgi h1
  refine ((dist_triangle _ _ _).trans_lt (add_lt_add_of_le_of_lt this hgi)).trans_eq ?_
  field_simp; ring_nf

end NormedAddCommGroup

end Measurability

end NontriviallyNormedField

open scoped Convolution

section RCLike
variable [RCLike ùïú]
variable [NormedSpace ùïú E]
variable [NormedSpace ùïú E']
variable [NormedSpace ùïú E'']
variable [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
variable {n : ‚Ñï‚àû}
variable [MeasurableSpace G] {Œº ŒΩ : Measure G}
variable (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

section Assoc
variable [CompleteSpace F]
variable [NormedAddCommGroup F'] [NormedSpace ‚Ñù F'] [NormedSpace ùïú F'] [CompleteSpace F']
variable [NormedAddCommGroup F''] [NormedSpace ‚Ñù F''] [NormedSpace ùïú F''] [CompleteSpace F'']
variable {k : G ‚Üí E''}
variable (L‚ÇÇ : F ‚ÜíL[ùïú] E'' ‚ÜíL[ùïú] F')
variable (L‚ÇÉ : E ‚ÜíL[ùïú] F'' ‚ÜíL[ùïú] F')
variable (L‚ÇÑ : E' ‚ÜíL[ùïú] E'' ‚ÜíL[ùïú] F'')
variable [AddGroup G]
variable [SFinite Œº] [SFinite ŒΩ] [IsAddRightInvariant Œº]

theorem integral_convolution [MeasurableAdd‚ÇÇ G] [MeasurableNeg G] [NormedSpace ‚Ñù E]
    [NormedSpace ‚Ñù E'] [CompleteSpace E] [CompleteSpace E'] (hf : Integrable f ŒΩ)
    (hg : Integrable g Œº) : ‚à´ x, (f ‚ãÜ[L, ŒΩ] g) x ‚àÇŒº = L (‚à´ x, f x ‚àÇŒΩ) (‚à´ x, g x ‚àÇŒº) := by
  refine (integral_integral_swap (by apply hf.convolution_integrand L hg)).trans ?_
  simp_rw [integral_comp_comm _ (hg.comp_sub_right _), integral_sub_right_eq_self]
  exact (L.flip (‚à´ x, g x ‚àÇŒº)).integral_comp_comm hf

variable [MeasurableAdd‚ÇÇ G] [IsAddRightInvariant ŒΩ] [MeasurableNeg G]

/-- Convolution is associative. This has a weak but inconvenient integrability condition.
See also `MeasureTheory.convolution_assoc`. -/
theorem convolution_assoc' (hL : ‚àÄ (x : E) (y : E') (z : E''), L‚ÇÇ (L x y) z = L‚ÇÉ x (L‚ÇÑ y z))
    {x‚ÇÄ : G} (hfg : ‚àÄ·µê y ‚àÇŒº, ConvolutionExistsAt f g y L ŒΩ)
    (hgk : ‚àÄ·µê x ‚àÇŒΩ, ConvolutionExistsAt g k x L‚ÇÑ Œº)
    (hi : Integrable (uncurry fun x y => (L‚ÇÉ (f y)) ((L‚ÇÑ (g (x - y))) (k (x‚ÇÄ - x)))) (Œº.prod ŒΩ)) :
    ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ = (f ‚ãÜ[L‚ÇÉ, ŒΩ] g ‚ãÜ[L‚ÇÑ, Œº] k) x‚ÇÄ :=
  calc
    ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ = ‚à´ t, L‚ÇÇ (‚à´ s, L (f s) (g (t - s)) ‚àÇŒΩ) (k (x‚ÇÄ - t)) ‚àÇŒº := rfl
    _ = ‚à´ t, ‚à´ s, L‚ÇÇ (L (f s) (g (t - s))) (k (x‚ÇÄ - t)) ‚àÇŒΩ ‚àÇŒº :=
      (integral_congr_ae (hfg.mono fun t ht => ((L‚ÇÇ.flip (k (x‚ÇÄ - t))).integral_comp_comm ht).symm))
    _ = ‚à´ t, ‚à´ s, L‚ÇÉ (f s) (L‚ÇÑ (g (t - s)) (k (x‚ÇÄ - t))) ‚àÇŒΩ ‚àÇŒº := by simp_rw [hL]
    _ = ‚à´ s, ‚à´ t, L‚ÇÉ (f s) (L‚ÇÑ (g (t - s)) (k (x‚ÇÄ - t))) ‚àÇŒº ‚àÇŒΩ := by rw [integral_integral_swap hi]
    _ = ‚à´ s, ‚à´ u, L‚ÇÉ (f s) (L‚ÇÑ (g u) (k (x‚ÇÄ - s - u))) ‚àÇŒº ‚àÇŒΩ := by
      congr; ext t
      rw [eq_comm, ‚Üê integral_sub_right_eq_self _ t]
      simp_rw [sub_sub_sub_cancel_right]
    _ = ‚à´ s, L‚ÇÉ (f s) (‚à´ u, L‚ÇÑ (g u) (k (x‚ÇÄ - s - u)) ‚àÇŒº) ‚àÇŒΩ := by
      refine integral_congr_ae ?_
      refine ((quasiMeasurePreserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono fun t ht => ?_
      exact (L‚ÇÉ (f t)).integral_comp_comm ht
    _ = (f ‚ãÜ[L‚ÇÉ, ŒΩ] g ‚ãÜ[L‚ÇÑ, Œº] k) x‚ÇÄ := rfl

/-- Convolution is associative. This requires that
* all maps are a.e. strongly measurable w.r.t one of the measures
* `f ‚ãÜ[L, ŒΩ] g` exists almost everywhere
* `‚Äñg‚Äñ ‚ãÜ[Œº] ‚Äñk‚Äñ` exists almost everywhere
* `‚Äñf‚Äñ ‚ãÜ[ŒΩ] (‚Äñg‚Äñ ‚ãÜ[Œº] ‚Äñk‚Äñ)` exists at `x‚ÇÄ` -/
theorem convolution_assoc (hL : ‚àÄ (x : E) (y : E') (z : E''), L‚ÇÇ (L x y) z = L‚ÇÉ x (L‚ÇÑ y z)) {x‚ÇÄ : G}
    (hf : AEStronglyMeasurable f ŒΩ) (hg : AEStronglyMeasurable g Œº) (hk : AEStronglyMeasurable k Œº)
    (hfg : ‚àÄ·µê y ‚àÇŒº, ConvolutionExistsAt f g y L ŒΩ)
    (hgk : ‚àÄ·µê x ‚àÇŒΩ, ConvolutionExistsAt (fun x => ‚Äñg x‚Äñ) (fun x => ‚Äñk x‚Äñ) x (mul ‚Ñù ‚Ñù) Œº)
    (hfgk :
      ConvolutionExistsAt (fun x => ‚Äñf x‚Äñ) ((fun x => ‚Äñg x‚Äñ) ‚ãÜ[mul ‚Ñù ‚Ñù, Œº] fun x => ‚Äñk x‚Äñ) x‚ÇÄ
        (mul ‚Ñù ‚Ñù) ŒΩ) :
    ((f ‚ãÜ[L, ŒΩ] g) ‚ãÜ[L‚ÇÇ, Œº] k) x‚ÇÄ = (f ‚ãÜ[L‚ÇÉ, ŒΩ] g ‚ãÜ[L‚ÇÑ, Œº] k) x‚ÇÄ := by
  refine convolution_assoc' L L‚ÇÇ L‚ÇÉ L‚ÇÑ hL hfg (hgk.mono fun x hx => hx.of_norm L‚ÇÑ hg hk) ?_
  -- the following is similar to `Integrable.convolution_integrand`
  have h_meas :
    AEStronglyMeasurable (uncurry fun x y => L‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x))))
      (Œº.prod ŒΩ) := by
    refine L‚ÇÉ.aestronglyMeasurable_comp‚ÇÇ hf.comp_snd ?_
    refine L‚ÇÑ.aestronglyMeasurable_comp‚ÇÇ hg.comp_fst ?_
    refine (hk.mono_ac ?_).comp_measurable
      ((measurable_const.sub measurable_snd).sub measurable_fst)
    refine QuasiMeasurePreserving.absolutelyContinuous ?_
    refine QuasiMeasurePreserving.prod_of_left
      ((measurable_const.sub measurable_snd).sub measurable_fst) (Eventually.of_forall fun y => ?_)
    dsimp only
    exact quasiMeasurePreserving_sub_left_of_right_invariant Œº _
  have h2_meas :
    AEStronglyMeasurable (fun y => ‚à´ x, ‚ÄñL‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x)))‚Äñ ‚àÇŒº) ŒΩ :=
    h_meas.prod_swap.norm.integral_prod_right'
  have h3 : map (fun z : G √ó G => (z.1 - z.2, z.2)) (Œº.prod ŒΩ) = Œº.prod ŒΩ :=
    (measurePreserving_sub_prod Œº ŒΩ).map_eq
  suffices Integrable (uncurry fun x y => L‚ÇÉ (f y) (L‚ÇÑ (g x) (k (x‚ÇÄ - y - x)))) (Œº.prod ŒΩ) by
    rw [‚Üê h3] at this
    convert this.comp_measurable (measurable_sub.prodMk measurable_snd)
    ext ‚ü®x, y‚ü©
    simp +unfoldPartialApp only [uncurry, Function.comp_apply,
      sub_sub_sub_cancel_right]
  simp_rw [integrable_prod_iff' h_meas]
  refine ‚ü®((quasiMeasurePreserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono fun t ht =>
    (L‚ÇÉ (f t)).integrable_comp <| ht.of_norm L‚ÇÑ hg hk, ?_‚ü©
  refine (hfgk.const_mul (‚ÄñL‚ÇÉ‚Äñ * ‚ÄñL‚ÇÑ‚Äñ)).mono' h2_meas
    (((quasiMeasurePreserving_sub_left_of_right_invariant ŒΩ x‚ÇÄ).ae hgk).mono fun t ht => ?_)
  simp_rw [convolution_def, mul_apply', mul_mul_mul_comm ‚ÄñL‚ÇÉ‚Äñ ‚ÄñL‚ÇÑ‚Äñ, ‚Üê integral_const_mul]
  rw [Real.norm_of_nonneg (by positivity)]
  refine integral_mono_of_nonneg (Eventually.of_forall fun t => norm_nonneg _)
    ((ht.const_mul _).const_mul _) (Eventually.of_forall fun s => ?_)
  simp only [‚Üê mul_assoc ‚ÄñL‚ÇÑ‚Äñ]
  apply_rules [ContinuousLinearMap.le_of_opNorm‚ÇÇ_le_of_le, le_rfl]

end Assoc

variable [NormedAddCommGroup G] [BorelSpace G]

theorem convolution_precompR_apply {g : G ‚Üí E'' ‚ÜíL[ùïú] E'} (hf : LocallyIntegrable f Œº)
    (hcg : HasCompactSupport g) (hg : Continuous g) (x‚ÇÄ : G) (x : E'') :
    (f ‚ãÜ[L.precompR E'', Œº] g) x‚ÇÄ x = (f ‚ãÜ[L, Œº] fun a => g a x) x‚ÇÄ := by
  have := hcg.convolutionExists_right (L.precompR E'' :) hf hg x‚ÇÄ
  simp_rw [convolution_def, ContinuousLinearMap.integral_apply this]
  rfl

variable [NormedSpace ùïú G] [SFinite Œº] [IsAddLeftInvariant Œº]

/-- Compute the total derivative of `f ‚ãÜ g` if `g` is `C^1` with compact support and `f` is locally
integrable. To write down the total derivative as a convolution, we use
`ContinuousLinearMap.precompR`. -/
theorem _root_.HasCompactSupport.hasFDerivAt_convolution_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Œº) (hg : ContDiff ùïú 1 g) (x‚ÇÄ : G) :
    HasFDerivAt (f ‚ãÜ[L, Œº] g) ((f ‚ãÜ[L.precompR G, Œº] fderiv ùïú g) x‚ÇÄ) x‚ÇÄ := by
  rcases hcg.eq_zero_or_finiteDimensional ùïú hg.continuous with (rfl | fin_dim)
  ¬∑ have : fderiv ùïú (0 : G ‚Üí E') = 0 := fderiv_const (0 : E')
    simp only [this, convolution_zero, Pi.zero_apply]
    exact hasFDerivAt_const (0 : F) x‚ÇÄ
  have : ProperSpace G := FiniteDimensional.proper_rclike ùïú G
  set L' := L.precompR G
  have h1 : ‚àÄ·∂† x in ùìù x‚ÇÄ, AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Œº :=
    Eventually.of_forall
      (hf.aestronglyMeasurable.convolution_integrand_snd L hg.continuous.aestronglyMeasurable)
  have h2 : ‚àÄ x, AEStronglyMeasurable (fun t => L' (f t) (fderiv ùïú g (x - t))) Œº :=
    hf.aestronglyMeasurable.convolution_integrand_snd L'
      (hg.continuous_fderiv le_rfl).aestronglyMeasurable
  have h3 : ‚àÄ x t, HasFDerivAt (fun x => g (x - t)) (fderiv ùïú g (x - t)) x := fun x t ‚Ü¶ by
    simpa using
      (hg.differentiable le_rfl).differentiableAt.hasFDerivAt.comp x
        ((hasFDerivAt_id x).sub (hasFDerivAt_const t x))
  let K' := -tsupport (fderiv ùïú g) + closedBall x‚ÇÄ 1
  have hK' : IsCompact K' := (hcg.fderiv ùïú).neg.add (isCompact_closedBall x‚ÇÄ 1)
  apply hasFDerivAt_integral_of_dominated_of_fderiv_le zero_lt_one h1 _ (h2 x‚ÇÄ)
  ¬∑ filter_upwards with t x hx using
      (hcg.fderiv ùïú).convolution_integrand_bound_right L' (hg.continuous_fderiv le_rfl)
        (ball_subset_closedBall hx)
  ¬∑ rw [integrable_indicator_iff hK'.measurableSet]
    exact ((hf.integrableOn_isCompact hK').norm.const_mul _).mul_const _
  ¬∑ exact Eventually.of_forall fun t x _ => (L _).hasFDerivAt.comp x (h3 x t)
  ¬∑ exact hcg.convolutionExists_right L hf hg.continuous x‚ÇÄ

theorem _root_.HasCompactSupport.hasFDerivAt_convolution_left [IsNegInvariant Œº]
    (hcf : HasCompactSupport f) (hf : ContDiff ùïú 1 f) (hg : LocallyIntegrable g Œº) (x‚ÇÄ : G) :
    HasFDerivAt (f ‚ãÜ[L, Œº] g) ((fderiv ùïú f ‚ãÜ[L.precompL G, Œº] g) x‚ÇÄ) x‚ÇÄ := by
  simp +singlePass only [‚Üê convolution_flip]
  exact hcf.hasFDerivAt_convolution_right L.flip hg hf x‚ÇÄ

end RCLike

section Real

/-! The one-variable case -/

variable [RCLike ùïú]
variable [NormedSpace ùïú E]
variable [NormedSpace ùïú E']
variable [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
variable {f‚ÇÄ : ùïú ‚Üí E} {g‚ÇÄ : ùïú ‚Üí E'}
variable {n : ‚Ñï‚àû}
variable (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
variable {Œº : Measure ùïú}
variable [IsAddLeftInvariant Œº] [SFinite Œº]

theorem _root_.HasCompactSupport.hasDerivAt_convolution_right (hf : LocallyIntegrable f‚ÇÄ Œº)
    (hcg : HasCompactSupport g‚ÇÄ) (hg : ContDiff ùïú 1 g‚ÇÄ) (x‚ÇÄ : ùïú) :
    HasDerivAt (f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) ((f‚ÇÄ ‚ãÜ[L, Œº] deriv g‚ÇÄ) x‚ÇÄ) x‚ÇÄ := by
  convert (hcg.hasFDerivAt_convolution_right L hf hg x‚ÇÄ).hasDerivAt using 1
  rw [convolution_precompR_apply L hf (hcg.fderiv ùïú) (hg.continuous_fderiv le_rfl)]
  rfl

theorem _root_.HasCompactSupport.hasDerivAt_convolution_left [IsNegInvariant Œº]
    (hcf : HasCompactSupport f‚ÇÄ) (hf : ContDiff ùïú 1 f‚ÇÄ) (hg : LocallyIntegrable g‚ÇÄ Œº) (x‚ÇÄ : ùïú) :
    HasDerivAt (f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) ((deriv f‚ÇÄ ‚ãÜ[L, Œº] g‚ÇÄ) x‚ÇÄ) x‚ÇÄ := by
  simp +singlePass only [‚Üê convolution_flip]
  exact hcf.hasDerivAt_convolution_right L.flip hg hf x‚ÇÄ

end Real

section WithParam

variable [RCLike ùïú] [NormedSpace ùïú E] [NormedSpace ùïú E'] [NormedSpace ùïú E''] [NormedSpace ‚Ñù F]
  [NormedSpace ùïú F] [MeasurableSpace G] [NormedAddCommGroup G] [BorelSpace G]
  [NormedSpace ùïú G] [NormedAddCommGroup P] [NormedSpace ùïú P] {Œº : Measure G}
  (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)

/-- The derivative of the convolution `f * g` is given by `f * Dg`, when `f` is locally integrable
and `g` is `C^1` and compactly supported. Version where `g` depends on an additional parameter in an
open subset `s` of a parameter space `P` (and the compact support `k` is independent of the
parameter in `s`). -/
theorem hasFDerivAt_convolution_right_with_param {g : P ‚Üí G ‚Üí E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
    (hf : LocallyIntegrable f Œº) (hg : ContDiffOn ùïú 1 (‚Üøg) (s √óÀ¢ univ)) (q‚ÇÄ : P √ó G)
    (hq‚ÇÄ : q‚ÇÄ.1 ‚àà s) :
    HasFDerivAt (fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2)
      ((f ‚ãÜ[L.precompR (P √ó G), Œº] fun x : G => fderiv ùïú (‚Üøg) (q‚ÇÄ.1, x)) q‚ÇÄ.2) q‚ÇÄ := by
  let g' := fderiv ùïú ‚Üøg
  have A : ‚àÄ p ‚àà s, Continuous (g p) := fun p hp ‚Ü¶ by
    refine hg.continuousOn.comp_continuous (.prodMk_right _) fun x => ?_
    simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hp
  have A' : ‚àÄ q : P √ó G, q.1 ‚àà s ‚Üí s √óÀ¢ univ ‚àà ùìù q := fun q hq ‚Ü¶ by
    apply (hs.prod isOpen_univ).mem_nhds
    simpa only [mem_prod, mem_univ, and_true] using hq
  -- The derivative of `g` vanishes away from `k`.
  have g'_zero : ‚àÄ p x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g' (p, x) = 0 := by
    intro p x hp hx
    refine (hasFDerivAt_zero_of_eventually_const 0 ?_).fderiv
    have M2 : k·∂ú ‚àà ùìù x := hk.isClosed.isOpen_compl.mem_nhds hx
    have M1 : s ‚àà ùìù p := hs.mem_nhds hp
    rw [nhds_prod_eq]
    filter_upwards [prod_mem_prod M1 M2]
    rintro ‚ü®p, y‚ü© ‚ü®hp, hy‚ü©
    exact hgs p y hp hy
  /- We find a small neighborhood of `{q‚ÇÄ.1} √ó k` on which the derivative is uniformly bounded. This
    follows from the continuity at all points of the compact set `k`. -/
  obtain ‚ü®Œµ, C, Œµpos, h‚ÇÄŒµ, hŒµ‚ü© :
      ‚àÉ Œµ C, 0 < Œµ ‚àß ball q‚ÇÄ.1 Œµ ‚äÜ s ‚àß ‚àÄ p x, ‚Äñp - q‚ÇÄ.1‚Äñ < Œµ ‚Üí ‚Äñg' (p, x)‚Äñ ‚â§ C := by
    have A : IsCompact ({q‚ÇÄ.1} √óÀ¢ k) := isCompact_singleton.prod hk
    obtain ‚ü®t, kt, t_open, ht‚ü© : ‚àÉ t, {q‚ÇÄ.1} √óÀ¢ k ‚äÜ t ‚àß IsOpen t ‚àß IsBounded (g' '' t) := by
      have B : ContinuousOn g' (s √óÀ¢ univ) :=
        hg.continuousOn_fderiv_of_isOpen (hs.prod isOpen_univ) le_rfl
      apply exists_isOpen_isBounded_image_of_isCompact_of_continuousOn A (hs.prod isOpen_univ) _ B
      simp only [prod_subset_prod_iff, hq‚ÇÄ, singleton_subset_iff, subset_univ, and_self_iff,
        true_or]
    obtain ‚ü®Œµ, Œµpos, hŒµ, h'Œµ‚ü© :
      ‚àÉ Œµ : ‚Ñù, 0 < Œµ ‚àß thickening Œµ ({q‚ÇÄ.fst} √óÀ¢ k) ‚äÜ t ‚àß ball q‚ÇÄ.1 Œµ ‚äÜ s := by
      obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ : ‚Ñù, 0 < Œµ ‚àß thickening Œµ (({q‚ÇÄ.fst} : Set P) √óÀ¢ k) ‚äÜ t :=
        A.exists_thickening_subset_open t_open kt
      obtain ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü© : ‚àÉ Œ¥ : ‚Ñù, 0 < Œ¥ ‚àß ball q‚ÇÄ.1 Œ¥ ‚äÜ s := Metric.isOpen_iff.1 hs _ hq‚ÇÄ
      refine ‚ü®min Œµ Œ¥, lt_min Œµpos Œ¥pos, ?_, ?_‚ü©
      ¬∑ exact Subset.trans (thickening_mono (min_le_left _ _) _) hŒµ
      ¬∑ exact Subset.trans (ball_subset_ball (min_le_right _ _)) hŒ¥
    obtain ‚ü®C, Cpos, hC‚ü© : ‚àÉ C, 0 < C ‚àß g' '' t ‚äÜ closedBall 0 C := ht.subset_closedBall_lt 0 0
    refine ‚ü®Œµ, C, Œµpos, h'Œµ, fun p x hp => ?_‚ü©
    have hps : p ‚àà s := h'Œµ (mem_ball_iff_norm.2 hp)
    by_cases hx : x ‚àà k
    ¬∑ have H : (p, x) ‚àà t := by
        apply hŒµ
        refine mem_thickening_iff.2 ‚ü®(q‚ÇÄ.1, x), ?_, ?_‚ü©
        ¬∑ simp only [hx, singleton_prod, mem_image, Prod.mk_inj, eq_self_iff_true, true_and,
            exists_eq_right]
        ¬∑ rw [‚Üê dist_eq_norm] at hp
          simpa only [Prod.dist_eq, Œµpos, dist_self, max_lt_iff, and_true] using hp
      have : g' (p, x) ‚àà closedBall (0 : P √ó G ‚ÜíL[ùïú] E') C := hC (mem_image_of_mem _ H)
      rwa [mem_closedBall_zero_iff] at this
    ¬∑ have : g' (p, x) = 0 := g'_zero _ _ hps hx
      rw [this]
      simpa only [norm_zero] using Cpos.le
  /- Now, we wish to apply a theorem on differentiation of integrals. For this, we need to check
    trivial measurability or integrability assumptions (in `I1`, `I2`, `I3`), as well as a uniform
    integrability assumption over the derivative (in `I4` and `I5`) and pointwise differentiability
    in `I6`. -/
  have I1 :
    ‚àÄ·∂† x : P √ó G in ùìù q‚ÇÄ, AEStronglyMeasurable (fun a : G => L (f a) (g x.1 (x.2 - a))) Œº := by
    filter_upwards [A' q‚ÇÄ hq‚ÇÄ]
    rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
    refine (HasCompactSupport.convolutionExists_right L ?_ hf (A _ hp) _).1
    apply hk.of_isClosed_subset (isClosed_tsupport _)
    exact closure_minimal (support_subset_iff'.2 fun z hz => hgs _ _ hp hz) hk.isClosed
  have I2 : Integrable (fun a : G => L (f a) (g q‚ÇÄ.1 (q‚ÇÄ.2 - a))) Œº := by
    have M : HasCompactSupport (g q‚ÇÄ.1) := HasCompactSupport.intro hk fun x hx => hgs q‚ÇÄ.1 x hq‚ÇÄ hx
    apply M.convolutionExists_right L hf (A q‚ÇÄ.1 hq‚ÇÄ) q‚ÇÄ.2
  have I3 : AEStronglyMeasurable (fun a : G => (L (f a)).comp (g' (q‚ÇÄ.fst, q‚ÇÄ.snd - a))) Œº := by
    have T : HasCompactSupport fun y => g' (q‚ÇÄ.1, y) :=
      HasCompactSupport.intro hk fun x hx => g'_zero q‚ÇÄ.1 x hq‚ÇÄ hx
    apply (HasCompactSupport.convolutionExists_right (L.precompR (P √ó G) :) T hf _ q‚ÇÄ.2).1
    have : ContinuousOn g' (s √óÀ¢ univ) :=
      hg.continuousOn_fderiv_of_isOpen (hs.prod isOpen_univ) le_rfl
    apply this.comp_continuous (.prodMk_right _)
    intro x
    simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hq‚ÇÄ
  set K' := (-k + {q‚ÇÄ.2} : Set G) with K'_def
  have hK' : IsCompact K' := hk.neg.add isCompact_singleton
  obtain ‚ü®U, U_open, K'U, hU‚ü© : ‚àÉ U, IsOpen U ‚àß K' ‚äÜ U ‚àß IntegrableOn f U Œº :=
    hf.integrableOn_nhds_isCompact hK'
  obtain ‚ü®Œ¥, Œ¥pos, Œ¥Œµ, hŒ¥‚ü© : ‚àÉ Œ¥, (0 : ‚Ñù) < Œ¥ ‚àß Œ¥ ‚â§ Œµ ‚àß K' + ball 0 Œ¥ ‚äÜ U := by
    obtain ‚ü®V, V_mem, hV‚ü© : ‚àÉ V ‚àà ùìù (0 : G), K' + V ‚äÜ U :=
      compact_open_separated_add_right hK' U_open K'U
    rcases Metric.mem_nhds_iff.1 V_mem with ‚ü®Œ¥, Œ¥pos, hŒ¥‚ü©
    refine ‚ü®min Œ¥ Œµ, lt_min Œ¥pos Œµpos, min_le_right Œ¥ Œµ, ?_‚ü©
    exact (add_subset_add_left ((ball_subset_ball (min_le_left _ _)).trans hŒ¥)).trans hV
  letI := ContinuousLinearMap.hasOpNorm (ùïú := ùïú) (ùïú‚ÇÇ := ùïú) (E := E)
    (F := (P √ó G ‚ÜíL[ùïú] E') ‚ÜíL[ùïú] P √ó G ‚ÜíL[ùïú] F) (œÉ‚ÇÅ‚ÇÇ := RingHom.id ùïú)
  let bound : G ‚Üí ‚Ñù := indicator U fun t => ‚Äñ(L.precompR (P √ó G))‚Äñ * ‚Äñf t‚Äñ * C
  have I4 : ‚àÄ·µê a : G ‚àÇŒº, ‚àÄ x : P √ó G, dist x q‚ÇÄ < Œ¥ ‚Üí
      ‚ÄñL.precompR (P √ó G) (f a) (g' (x.fst, x.snd - a))‚Äñ ‚â§ bound a := by
    filter_upwards with a x hx
    rw [Prod.dist_eq, dist_eq_norm, dist_eq_norm] at hx
    have : (-tsupport fun a => g' (x.1, a)) + ball q‚ÇÄ.2 Œ¥ ‚äÜ U := by
      apply Subset.trans _ hŒ¥
      rw [K'_def, add_assoc]
      apply add_subset_add
      ¬∑ rw [neg_subset_neg]
        refine closure_minimal (support_subset_iff'.2 fun z hz => ?_) hk.isClosed
        apply g'_zero x.1 z (h‚ÇÄŒµ _) hz
        rw [mem_ball_iff_norm]
        exact ((le_max_left _ _).trans_lt hx).trans_le Œ¥Œµ
      ¬∑ simp only [add_ball, thickening_singleton, zero_vadd, subset_rfl]
    apply convolution_integrand_bound_right_of_le_of_subset _ _ _ this
    ¬∑ intro y
      exact hŒµ _ _ (((le_max_left _ _).trans_lt hx).trans_le Œ¥Œµ)
    ¬∑ rw [mem_ball_iff_norm]
      exact (le_max_right _ _).trans_lt hx
  have I5 : Integrable bound Œº := by
    rw [integrable_indicator_iff U_open.measurableSet]
    exact (hU.norm.const_mul _).mul_const _
  have I6 : ‚àÄ·µê a : G ‚àÇŒº, ‚àÄ x : P √ó G, dist x q‚ÇÄ < Œ¥ ‚Üí
      HasFDerivAt (fun x : P √ó G => L (f a) (g x.1 (x.2 - a)))
        ((L (f a)).comp (g' (x.fst, x.snd - a))) x := by
    filter_upwards with a x hx
    apply (L _).hasFDerivAt.comp x
    have N : s √óÀ¢ univ ‚àà ùìù (x.1, x.2 - a) := by
      apply A'
      apply h‚ÇÄŒµ
      rw [Prod.dist_eq] at hx
      exact lt_of_lt_of_le (lt_of_le_of_lt (le_max_left _ _) hx) Œ¥Œµ
    have Z := ((hg.differentiableOn le_rfl).differentiableAt N).hasFDerivAt
    have Z' :
        HasFDerivAt (fun x : P √ó G => (x.1, x.2 - a)) (ContinuousLinearMap.id ùïú (P √ó G)) x := by
      have : (fun x : P √ó G => (x.1, x.2 - a)) = _root_.id - fun x => (0, a) := by
        ext x <;> simp only [Pi.sub_apply, _root_.id, Prod.fst_sub, sub_zero, Prod.snd_sub]
      rw [this]
      exact (hasFDerivAt_id x).sub_const (0, a)
    exact Z.comp x Z'
  exact hasFDerivAt_integral_of_dominated_of_fderiv_le Œ¥pos I1 I2 I3 I4 I5 I6

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`).
In this version, all the types belong to the same universe (to get an induction working in the
proof). Use instead `contDiffOn_convolution_right_with_param`, which removes this restriction. -/
theorem contDiffOn_convolution_right_with_param_aux {G : Type uP} {E' : Type uP} {F : Type uP}
    {P : Type uP} [NormedAddCommGroup E'] [NormedAddCommGroup F] [NormedSpace ùïú E']
    [NormedSpace ‚Ñù F] [NormedSpace ùïú F] [MeasurableSpace G]
    {Œº : Measure G}
    [NormedAddCommGroup G] [BorelSpace G] [NormedSpace ùïú G] [NormedAddCommGroup P] [NormedSpace ùïú P]
    {f : G ‚Üí E} {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F) {g : P ‚Üí G ‚Üí E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
    (hf : LocallyIntegrable f Œº) (hg : ContDiffOn ùïú n (‚Üøg) (s √óÀ¢ univ)) :
    ContDiffOn ùïú n (fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) := by
  /- We have a formula for the derivation of `f * g`, which is of the same form, thanks to
    `hasFDerivAt_convolution_right_with_param`. Therefore, we can prove the result by induction on
    `n` (but for this we need the spaces at the different steps of the induction to live in the same
    universe, which is why we make the assumption in the lemma that all the relevant spaces
    come from the same universe). -/
  induction n using ENat.nat_induction generalizing g E' F with
  | h0 =>
    rw [WithTop.coe_zero, contDiffOn_zero] at hg ‚ä¢
    exact continuousOn_convolution_right_with_param L hk hgs hf hg
  | hsuc n ih =>
    simp only [Nat.succ_eq_add_one, Nat.cast_add, Nat.cast_one, WithTop.coe_add,
      WithTop.coe_natCast, WithTop.coe_one] at hg ‚ä¢
    let f' : P ‚Üí G ‚Üí P √ó G ‚ÜíL[ùïú] F := fun p a =>
      (f ‚ãÜ[L.precompR (P √ó G), Œº] fun x : G => fderiv ùïú (uncurry g) (p, x)) a
    have A : ‚àÄ q‚ÇÄ : P √ó G, q‚ÇÄ.1 ‚àà s ‚Üí
        HasFDerivAt (fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2) (f' q‚ÇÄ.1 q‚ÇÄ.2) q‚ÇÄ :=
      hasFDerivAt_convolution_right_with_param L hs hk hgs hf hg.one_of_succ
    rw [contDiffOn_succ_iff_fderiv_of_isOpen (hs.prod (@isOpen_univ G _))] at hg ‚ä¢
    refine ‚ü®?_, by simp, ?_‚ü©
    ¬∑ rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
      exact (A (p, x) hp).differentiableAt.differentiableWithinAt
    ¬∑ suffices H : ContDiffOn ùïú n (‚Üøf') (s √óÀ¢ univ) by
        apply H.congr
        rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
        exact (A (p, x) hp).fderiv
      have B : ‚àÄ (p : P) (x : G), p ‚àà s ‚Üí x ‚àâ k ‚Üí fderiv ùïú (uncurry g) (p, x) = 0 := by
        intro p x hp hx
        apply (hasFDerivAt_zero_of_eventually_const (0 : E') _).fderiv
        have M2 : k·∂ú ‚àà ùìù x := IsOpen.mem_nhds hk.isClosed.isOpen_compl hx
        have M1 : s ‚àà ùìù p := hs.mem_nhds hp
        rw [nhds_prod_eq]
        filter_upwards [prod_mem_prod M1 M2]
        rintro ‚ü®p, y‚ü© ‚ü®hp, hy‚ü©
        exact hgs p y hp hy
      apply ih (L.precompR (P √ó G) :) B
      convert hg.2.2
  | htop ih =>
    rw [contDiffOn_infty] at hg ‚ä¢
    exact fun n ‚Ü¶ ih n L hgs (hg n)

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem contDiffOn_convolution_right_with_param {f : G ‚Üí E} {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F)
    {g : P ‚Üí G ‚Üí E'} {s : Set P} {k : Set G} (hs : IsOpen s) (hk : IsCompact k)
    (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0) (hf : LocallyIntegrable f Œº)
    (hg : ContDiffOn ùïú n (‚Üøg) (s √óÀ¢ univ)) :
    ContDiffOn ùïú n (fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2) (s √óÀ¢ univ) := by
  /- The result is known when all the universes are the same, from
    `contDiffOn_convolution_right_with_param_aux`. We reduce to this situation by pushing
    everything through `ULift` continuous linear equivalences. -/
  let eG : Type max uG uE' uF uP := ULift.{max uE' uF uP} G
  borelize eG
  let eE' : Type max uE' uG uF uP := ULift.{max uG uF uP} E'
  let eF : Type max uF uG uE' uP := ULift.{max uG uE' uP} F
  let eP : Type max uP uG uE' uF := ULift.{max uG uE' uF} P
  let isoG : eG ‚âÉL[ùïú] G := ContinuousLinearEquiv.ulift
  let isoE' : eE' ‚âÉL[ùïú] E' := ContinuousLinearEquiv.ulift
  let isoF : eF ‚âÉL[ùïú] F := ContinuousLinearEquiv.ulift
  let isoP : eP ‚âÉL[ùïú] P := ContinuousLinearEquiv.ulift
  let ef := f ‚àò isoG
  let eŒº : Measure eG := Measure.map isoG.symm Œº
  let eg : eP ‚Üí eG ‚Üí eE' := fun ep ex => isoE'.symm (g (isoP ep) (isoG ex))
  let eL :=
    ContinuousLinearMap.comp
      ((ContinuousLinearEquiv.arrowCongr isoE' isoF).symm : (E' ‚ÜíL[ùïú] F) ‚ÜíL[ùïú] eE' ‚ÜíL[ùïú] eF) L
  let R := fun q : eP √ó eG => (ef ‚ãÜ[eL, eŒº] eg q.1) q.2
  have R_contdiff : ContDiffOn ùïú n R ((isoP ‚Åª¬π' s) √óÀ¢ univ) := by
    have hek : IsCompact (isoG ‚Åª¬π' k) := isoG.toHomeomorph.isClosedEmbedding.isCompact_preimage hk
    have hes : IsOpen (isoP ‚Åª¬π' s) := isoP.continuous.isOpen_preimage _ hs
    refine contDiffOn_convolution_right_with_param_aux eL hes hek ?_ ?_ ?_
    ¬∑ intro p x hp hx
      simp only [eg, (¬∑ ‚àò ¬∑), ContinuousLinearEquiv.prodCongr_apply, LinearIsometryEquiv.coe_coe,
        ContinuousLinearEquiv.map_eq_zero_iff]
      exact hgs _ _ hp hx
    ¬∑ exact (locallyIntegrable_map_homeomorph isoG.symm.toHomeomorph).2 hf
    ¬∑ apply isoE'.symm.contDiff.comp_contDiffOn
      apply hg.comp (isoP.prodCongr isoG).contDiff.contDiffOn
      rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
      simpa only [mem_preimage, ContinuousLinearEquiv.prodCongr_apply, prodMk_mem_set_prod_eq,
        mem_univ, and_true] using hp
  have A : ContDiffOn ùïú n (isoF ‚àò R ‚àò (isoP.prodCongr isoG).symm) (s √óÀ¢ univ) := by
    apply isoF.contDiff.comp_contDiffOn
    apply R_contdiff.comp (ContinuousLinearEquiv.contDiff _).contDiffOn
    rintro ‚ü®p, x‚ü© ‚ü®hp, -‚ü©
    simpa only [mem_preimage, mem_prod, mem_univ, and_true, ContinuousLinearEquiv.prodCongr_symm,
      ContinuousLinearEquiv.prodCongr_apply, ContinuousLinearEquiv.apply_symm_apply] using hp
  have : isoF ‚àò R ‚àò (isoP.prodCongr isoG).symm = fun q : P √ó G => (f ‚ãÜ[L, Œº] g q.1) q.2 := by
    apply funext
    rintro ‚ü®p, x‚ü©
    simp only [LinearIsometryEquiv.coe_coe, (¬∑ ‚àò ¬∑), ContinuousLinearEquiv.prodCongr_symm,
      ContinuousLinearEquiv.prodCongr_apply]
    simp only [R, convolution, coe_comp', ContinuousLinearEquiv.coe_coe, (¬∑ ‚àò ¬∑)]
    rw [IsClosedEmbedding.integral_map, ‚Üê isoF.integral_comp_comm]
    ¬∑ rfl
    ¬∑ exact isoG.symm.toHomeomorph.isClosedEmbedding
  simp_rw [this] at A
  exact A

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with an additional `C^n` function. -/
theorem contDiffOn_convolution_right_with_param_comp {n : ‚Ñï‚àû} (L : E ‚ÜíL[ùïú] E' ‚ÜíL[ùïú] F) {s : Set P}
    {v : P ‚Üí G} (hv : ContDiffOn ùïú n v s) {f : G ‚Üí E} {g : P ‚Üí G ‚Üí E'} {k : Set G} (hs : IsOpen s)
    (hk : IsCompact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0) (hf : LocallyIntegrable f Œº)
    (hg : ContDiffOn ùïú n (‚Üøg) (s √óÀ¢ univ)) : ContDiffOn ùïú n (fun x => (f ‚ãÜ[L, Œº] g x) (v x)) s := by
  apply (contDiffOn_convolution_right_with_param L hs hk hgs hf hg).comp (contDiffOn_id.prodMk hv)
  intro x hx
  simp only [hx, mem_preimage, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem contDiffOn_convolution_left_with_param [Œº.IsAddLeftInvariant] [Œº.IsNegInvariant]
    (L : E' ‚ÜíL[ùïú] E ‚ÜíL[ùïú] F) {f : G ‚Üí E} {n : ‚Ñï‚àû} {g : P ‚Üí G ‚Üí E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0)
    (hf : LocallyIntegrable f Œº) (hg : ContDiffOn ùïú n (‚Üøg) (s √óÀ¢ univ)) :
    ContDiffOn ùïú n (fun q : P √ó G => (g q.1 ‚ãÜ[L, Œº] f) q.2) (s √óÀ¢ univ) := by
  simpa only [convolution_flip] using contDiffOn_convolution_right_with_param L.flip hs hk hgs hf hg

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with additional `C^n` functions. -/
theorem contDiffOn_convolution_left_with_param_comp [Œº.IsAddLeftInvariant] [Œº.IsNegInvariant]
    (L : E' ‚ÜíL[ùïú] E ‚ÜíL[ùïú] F) {s : Set P} {n : ‚Ñï‚àû} {v : P ‚Üí G} (hv : ContDiffOn ùïú n v s) {f : G ‚Üí E}
    {g : P ‚Üí G ‚Üí E'} {k : Set G} (hs : IsOpen s) (hk : IsCompact k)
    (hgs : ‚àÄ p, ‚àÄ x, p ‚àà s ‚Üí x ‚àâ k ‚Üí g p x = 0) (hf : LocallyIntegrable f Œº)
    (hg : ContDiffOn ùïú n (‚Üøg) (s √óÀ¢ univ)) : ContDiffOn ùïú n (fun x => (g x ‚ãÜ[L, Œº] f) (v x)) s := by
  apply (contDiffOn_convolution_left_with_param L hs hk hgs hf hg).comp (contDiffOn_id.prodMk hv)
  intro x hx
  simp only [hx, mem_preimage, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

theorem _root_.HasCompactSupport.contDiff_convolution_right {n : ‚Ñï‚àû} (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Œº) (hg : ContDiff ùïú n g) : ContDiff ùïú n (f ‚ãÜ[L, Œº] g) := by
  rcases exists_compact_iff_hasCompactSupport.2 hcg with ‚ü®k, hk, h'k‚ü©
  rw [‚Üê contDiffOn_univ]
  exact contDiffOn_convolution_right_with_param_comp L contDiffOn_id isOpen_univ hk
    (fun p x _ hx => h'k x hx) hf (hg.comp contDiff_snd).contDiffOn

theorem _root_.HasCompactSupport.contDiff_convolution_left [Œº.IsAddLeftInvariant] [Œº.IsNegInvariant]
    {n : ‚Ñï‚àû} (hcf : HasCompactSupport f) (hf : ContDiff ùïú n f) (hg : LocallyIntegrable g Œº) :
    ContDiff ùïú n (f ‚ãÜ[L, Œº] g) := by
  rw [‚Üê convolution_flip]
  exact hcf.contDiff_convolution_right L.flip hg hf

end WithParam

section Nonneg

variable [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù E'] [NormedSpace ‚Ñù F]

/-- The forward convolution of two functions `f` and `g` on `‚Ñù`, with respect to a continuous
bilinear map `L` and measure `ŒΩ`. It is defined to be the function mapping `x` to
`‚à´ t in 0..x, L (f t) (g (x - t)) ‚àÇŒΩ` if `0 < x`, and 0 otherwise. -/
noncomputable def posConvolution (f : ‚Ñù ‚Üí E) (g : ‚Ñù ‚Üí E') (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F)
    (ŒΩ : Measure ‚Ñù := by volume_tac) : ‚Ñù ‚Üí F :=
  indicator (Ioi (0 : ‚Ñù)) fun x => ‚à´ t in (0)..x, L (f t) (g (x - t)) ‚àÇŒΩ

theorem posConvolution_eq_convolution_indicator (f : ‚Ñù ‚Üí E) (g : ‚Ñù ‚Üí E') (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F)
    (ŒΩ : Measure ‚Ñù := by volume_tac) [NoAtoms ŒΩ] :
    posConvolution f g L ŒΩ = convolution (indicator (Ioi 0) f) (indicator (Ioi 0) g) L ŒΩ := by
  ext1 x
  rw [convolution, posConvolution, indicator]
  split_ifs with h
  ¬∑ rw [intervalIntegral.integral_of_le (le_of_lt h), integral_Ioc_eq_integral_Ioo, ‚Üê
      integral_indicator (measurableSet_Ioo : MeasurableSet (Ioo 0 x))]
    congr 1 with t : 1
    have : t ‚â§ 0 ‚à® t ‚àà Ioo 0 x ‚à® x ‚â§ t := by
      rcases le_or_gt t 0 with (h | h)
      ¬∑ exact Or.inl h
      ¬∑ rcases lt_or_ge t x with (h' | h')
        exacts [Or.inr (Or.inl ‚ü®h, h'‚ü©), Or.inr (Or.inr h')]
    rcases this with (ht | ht | ht)
    ¬∑ rw [indicator_of_notMem (notMem_Ioo_of_le ht), indicator_of_notMem (notMem_Ioi.mpr ht),
        ContinuousLinearMap.map_zero, ContinuousLinearMap.zero_apply]
    ¬∑ rw [indicator_of_mem ht, indicator_of_mem (mem_Ioi.mpr ht.1),
          indicator_of_mem (mem_Ioi.mpr <| sub_pos.mpr ht.2)]
    ¬∑ rw [indicator_of_notMem (notMem_Ioo_of_ge ht),
          indicator_of_notMem (notMem_Ioi.mpr (sub_nonpos_of_le ht)),
          ContinuousLinearMap.map_zero]
  ¬∑ convert (integral_zero ‚Ñù F).symm with t
    by_cases ht : 0 < t
    ¬∑ rw [indicator_of_notMem (_ : x - t ‚àâ Ioi 0), ContinuousLinearMap.map_zero]
      rw [notMem_Ioi] at h ‚ä¢
      exact sub_nonpos.mpr (h.trans ht.le)
    ¬∑ rw [indicator_of_notMem (mem_Ioi.not.mpr ht), ContinuousLinearMap.map_zero,
       ContinuousLinearMap.zero_apply]

theorem integrable_posConvolution {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí E'} {Œº ŒΩ : Measure ‚Ñù} [SFinite Œº]
    [SFinite ŒΩ] [IsAddRightInvariant Œº] [NoAtoms ŒΩ] (hf : IntegrableOn f (Ioi 0) ŒΩ)
    (hg : IntegrableOn g (Ioi 0) Œº) (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F) :
    Integrable (posConvolution f g L ŒΩ) Œº := by
  rw [‚Üê integrable_indicator_iff (measurableSet_Ioi : MeasurableSet (Ioi (0 : ‚Ñù)))] at hf hg
  rw [posConvolution_eq_convolution_indicator f g L ŒΩ]
  exact (hf.convolution_integrand L hg).integral_prod_left

/-- The integral over `Ioi 0` of a forward convolution of two functions is equal to the product
of their integrals over this set. (Compare `integral_convolution` for the two-sided convolution.) -/
theorem integral_posConvolution [CompleteSpace E] [CompleteSpace E'] [CompleteSpace F]
    {Œº ŒΩ : Measure ‚Ñù}
    [SFinite Œº] [SFinite ŒΩ] [IsAddRightInvariant Œº] [NoAtoms ŒΩ] {f : ‚Ñù ‚Üí E} {g : ‚Ñù ‚Üí E'}
    (hf : IntegrableOn f (Ioi 0) ŒΩ) (hg : IntegrableOn g (Ioi 0) Œº) (L : E ‚ÜíL[‚Ñù] E' ‚ÜíL[‚Ñù] F) :
    ‚à´ x : ‚Ñù in Ioi 0, ‚à´ t : ‚Ñù in (0)..x, L (f t) (g (x - t)) ‚àÇŒΩ ‚àÇŒº =
      L (‚à´ x : ‚Ñù in Ioi 0, f x ‚àÇŒΩ) (‚à´ x : ‚Ñù in Ioi 0, g x ‚àÇŒº) := by
  rw [‚Üê integrable_indicator_iff measurableSet_Ioi] at hf hg
  simp_rw [‚Üê integral_indicator measurableSet_Ioi]
  convert integral_convolution L hf hg using 4 with x
  apply posConvolution_eq_convolution_indicator

end Nonneg
end MeasureTheory
