/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Basic
public import Mathlib.Analysis.Calculus.ParametricIntegral
public import Mathlib.MeasureTheory.Integral.Prod
public import Mathlib.MeasureTheory.Function.LocallyIntegrable
public import Mathlib.MeasureTheory.Group.Integral
public import Mathlib.MeasureTheory.Group.Prod
public import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Convolution of functions

This file defines the convolution on two functions, i.e. `x â†¦ âˆ« f(t)g(x - t) âˆ‚t`.
In the general case, these functions can be vector-valued, and have an arbitrary (additive)
group as domain. We use a continuous bilinear operation `L` on these function values as
"multiplication". The domain must be equipped with a Haar measure `Î¼`
(though many individual results have weaker conditions on `Î¼`).

For many applications we can take `L = ContinuousLinearMap.lsmul â„ â„` or
`L = ContinuousLinearMap.mul â„ â„`.

We also define `ConvolutionExists` and `ConvolutionExistsAt` to state that the convolution is
well-defined (everywhere or at a single point). These conditions are needed for pointwise
computations (e.g. `ConvolutionExistsAt.distrib_add`), but are generally not strong enough for any
local (or global) properties of the convolution. For this we need stronger assumptions on `f`
and/or `g`, and generally if we impose stronger conditions on one of the functions, we can impose
weaker conditions on the other.
We have proven many of the properties of the convolution assuming one of these functions
has compact support (in which case the other function only needs to be locally integrable).
We still need to prove the properties for other pairs of conditions (e.g. both functions are
rapidly decreasing)

## Design Decisions

We use a bilinear map `L` to "multiply" the two functions in the integrand.
This generality has several advantages

* This allows us to compute the total derivative of the convolution, in case the functions are
  multivariate. The total derivative is again a convolution, but where the codomains of the
  functions can be higher-dimensional. See `HasCompactSupport.hasFDerivAt_convolution_right`.
* This allows us to use `@[to_additive]` everywhere (which would not be possible if we would use
  `mul`/`smul` in the integral, since `@[to_additive]` will incorrectly also try to additivize
  those definitions).
* We need to support the case where at least one of the functions is vector-valued, but if we use
  `smul` to multiply the functions, that would be an asymmetric definition.

## Main Definitions
* `MeasureTheory.convolution f g L Î¼ x = (f â‹†[L, Î¼] g) x = âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼`
  is the convolution of `f` and `g` w.r.t. the continuous bilinear map `L` and measure `Î¼`.
* `MeasureTheory.ConvolutionExistsAt f g x L Î¼` states that the convolution `(f â‹†[L, Î¼] g) x`
  is well-defined (i.e. the integral exists).
* `MeasureTheory.ConvolutionExists f g L Î¼` states that the convolution `f â‹†[L, Î¼] g`
  is well-defined at each point.

## Main Results
* `HasCompactSupport.hasFDerivAt_convolution_right` and
  `HasCompactSupport.hasFDerivAt_convolution_left`: we can compute the total derivative
  of the convolution as a convolution with the total derivative of the right (left) function.
* `HasCompactSupport.contDiff_convolution_right` and
  `HasCompactSupport.contDiff_convolution_left`: the convolution is `ğ’â¿` if one of the functions
  is `ğ’â¿` with compact support and the other function in locally integrable.

Versions of these statements for functions depending on a parameter are also given.

* `MeasureTheory.convolution_tendsto_right`: Given a sequence of nonnegative normalized functions
  whose support tends to a small neighborhood around `0`, the convolution tends to the right
  argument. This is specialized to bump functions in `ContDiffBump.convolution_tendsto_right`.

## Notation
The following notations are localized in the scope `Convolution`:
* `f â‹†[L, Î¼] g` for the convolution. Note: you have to use parentheses to apply the convolution
  to an argument: `(f â‹†[L, Î¼] g) x`.
* `f â‹†[L] g := f â‹†[L, volume] g`
* `f â‹† g := f â‹†[lsmul â„ â„] g`

## To do
* Existence and (uniform) continuity of the convolution if
  one of the maps is in `â„’^p` and the other in `â„’^q` with `1 / p + 1 / q = 1`.
  This might require a generalization of `MeasureTheory.MemLp.smul` where `smul` is generalized
  to a continuous bilinear map.
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255K)
* The convolution is an `AEStronglyMeasurable` function
  (see e.g. [Fremlin, *Measure Theory* (volume 2)][fremlin_vol2], 255I).
* Prove properties about the convolution if both functions are rapidly decreasing.
* Use `@[to_additive]` everywhere (this likely requires changes in `to_additive`)
-/

@[expose] public section
open Set Function Filter MeasureTheory MeasureTheory.Measure TopologicalSpace

open Bornology ContinuousLinearMap Metric Topology
open scoped Pointwise NNReal Filter

universe uğ•œ uG uE uE' uE'' uF uF' uF'' uP

variable {ğ•œ : Type uğ•œ} {G : Type uG} {E : Type uE} {E' : Type uE'} {E'' : Type uE''} {F : Type uF}
  {F' : Type uF'} {F'' : Type uF''} {P : Type uP}

variable [NormedAddCommGroup E] [NormedAddCommGroup E'] [NormedAddCommGroup E'']
  [NormedAddCommGroup F] {f f' : G â†’ E} {g g' : G â†’ E'} {x x' : G} {y y' : E}

namespace MeasureTheory
section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ]
variable [NormedSpace ğ•œ E] [NormedSpace ğ•œ E'] [NormedSpace ğ•œ E''] [NormedSpace ğ•œ F]
variable (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)

section NoMeasurability

variable [AddGroup G] [TopologicalSpace G]

theorem convolution_integrand_bound_right_of_le_of_subset {C : â„} (hC : âˆ€ i, â€–g iâ€– â‰¤ C) {x t : G}
    {s u : Set G} (hx : x âˆˆ s) (hu : -tsupport g + s âŠ† u) :
    â€–L (f t) (g (x - t))â€– â‰¤ u.indicator (fun t => â€–Lâ€– * â€–f tâ€– * C) t := by
  -- Porting note: had to add `f := _`
  refine le_indicator (f := fun t â†¦ â€–L (f t) (g (x - t))â€–) (fun t _ => ?_) (fun t ht => ?_) t
  Â· apply_rules [L.le_of_opNormâ‚‚_le_of_le, le_rfl]
  Â· have : x - t âˆ‰ support g := by
      refine mt (fun hxt => hu ?_) ht
      refine âŸ¨_, Set.neg_mem_neg.mpr (subset_closure hxt), _, hx, ?_âŸ©
      simp only [neg_sub, sub_add_cancel]
    simp only [notMem_support.mp this, (L _).map_zero, norm_zero, le_rfl]

theorem _root_.HasCompactSupport.convolution_integrand_bound_right_of_subset
    (hcg : HasCompactSupport g) (hg : Continuous g)
    {x t : G} {s u : Set G} (hx : x âˆˆ s) (hu : -tsupport g + s âŠ† u) :
    â€–L (f t) (g (x - t))â€– â‰¤ u.indicator (fun t => â€–Lâ€– * â€–f tâ€– * â¨† i, â€–g iâ€–) t := by
  refine convolution_integrand_bound_right_of_le_of_subset _ (fun i => ?_) hx hu
  exact le_ciSup (hg.norm.bddAbove_range_of_hasCompactSupport hcg.norm) _

theorem _root_.HasCompactSupport.convolution_integrand_bound_right (hcg : HasCompactSupport g)
    (hg : Continuous g) {x t : G} {s : Set G} (hx : x âˆˆ s) :
    â€–L (f t) (g (x - t))â€– â‰¤ (-tsupport g + s).indicator (fun t => â€–Lâ€– * â€–f tâ€– * â¨† i, â€–g iâ€–) t :=
  hcg.convolution_integrand_bound_right_of_subset L hg hx Subset.rfl

theorem _root_.Continuous.convolution_integrand_fst [ContinuousSub G] (hg : Continuous g) (t : G) :
    Continuous fun x => L (f t) (g (x - t)) :=
  L.continuousâ‚‚.compâ‚‚ continuous_const <| by fun_prop

theorem _root_.HasCompactSupport.convolution_integrand_bound_left (hcf : HasCompactSupport f)
    (hf : Continuous f) {x t : G} {s : Set G} (hx : x âˆˆ s) :
    â€–L (f (x - t)) (g t)â€– â‰¤
      (-tsupport f + s).indicator (fun t => (â€–Lâ€– * â¨† i, â€–f iâ€–) * â€–g tâ€–) t := by
  convert hcf.convolution_integrand_bound_right L.flip hf hx using 1
  simp_rw [L.opNorm_flip, mul_right_comm]

end NoMeasurability

section Measurability
variable [MeasurableSpace G] {Î¼ Î½ : Measure G}

/-- The convolution of `f` and `g` exists at `x` when the function `t â†¦ L (f t) (g (x - t))` is
integrable. There are various conditions on `f` and `g` to prove this. -/
def ConvolutionExistsAt [Sub G] (f : G â†’ E) (g : G â†’ E') (x : G) (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
    (Î¼ : Measure G := by volume_tac) : Prop :=
  Integrable (fun t => L (f t) (g (x - t))) Î¼

/-- The convolution of `f` and `g` exists when the function `t â†¦ L (f t) (g (x - t))` is integrable
for all `x : G`. There are various conditions on `f` and `g` to prove this. -/
def ConvolutionExists [Sub G] (f : G â†’ E) (g : G â†’ E') (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
    (Î¼ : Measure G := by volume_tac) : Prop :=
  âˆ€ x : G, ConvolutionExistsAt f g x L Î¼

section ConvolutionExists

variable {L} in
theorem ConvolutionExistsAt.integrable [Sub G] {x : G} (h : ConvolutionExistsAt f g x L Î¼) :
    Integrable (fun t => L (f t) (g (x - t))) Î¼ :=
  h

section Group

variable [AddGroup G]

theorem AEStronglyMeasurable.convolution_integrand' [MeasurableAddâ‚‚ G]
    [MeasurableNeg G] (hf : AEStronglyMeasurable f Î½)
    (hg : AEStronglyMeasurable g <| map (fun p : G Ã— G => p.1 - p.2) (Î¼.prod Î½)) :
    AEStronglyMeasurable (fun p : G Ã— G => L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î½) :=
  L.aestronglyMeasurable_compâ‚‚ hf.comp_snd <| hg.comp_measurable measurable_sub

section

variable [MeasurableAdd G] [MeasurableNeg G]

theorem AEStronglyMeasurable.convolution_integrand_snd'
    (hf : AEStronglyMeasurable f Î¼) {x : G}
    (hg : AEStronglyMeasurable g <| map (fun t => x - t) Î¼) :
    AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Î¼ :=
  L.aestronglyMeasurable_compâ‚‚ hf <| hg.comp_measurable <| measurable_id.const_sub x

theorem AEStronglyMeasurable.convolution_integrand_swap_snd' {x : G}
    (hf : AEStronglyMeasurable f <| map (fun t => x - t) Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun t => L (f (x - t)) (g t)) Î¼ :=
  L.aestronglyMeasurable_compâ‚‚ (hf.comp_measurable <| measurable_id.const_sub x) hg

/-- A sufficient condition to prove that `f â‹†[L, Î¼] g` exists.
We assume that `f` is integrable on a set `s` and `g` is bounded and ae strongly measurable
on `xâ‚€ - s` (note that both properties hold if `g` is continuous with compact support). -/
theorem _root_.BddAbove.convolutionExistsAt' {xâ‚€ : G} {s : Set G}
    (hbg : BddAbove ((fun i => â€–g iâ€–) '' ((fun t => -t + xâ‚€) â»Â¹' s))) (hs : MeasurableSet s)
    (h2s : (support fun t => L (f t) (g (xâ‚€ - t))) âŠ† s) (hf : IntegrableOn f s Î¼)
    (hmg : AEStronglyMeasurable g <| map (fun t => xâ‚€ - t) (Î¼.restrict s)) :
    ConvolutionExistsAt f g xâ‚€ L Î¼ := by
  rw [ConvolutionExistsAt]
  rw [â† integrableOn_iff_integrable_of_support_subset h2s]
  set s' := (fun t => -t + xâ‚€) â»Â¹' s
  have : âˆ€áµ t : G âˆ‚Î¼.restrict s,
      â€–L (f t) (g (xâ‚€ - t))â€– â‰¤ s.indicator (fun t => â€–Lâ€– * â€–f tâ€– * â¨† i : s', â€–g iâ€–) t := by
    filter_upwards
    refine le_indicator (fun t ht => ?_) fun t ht => ?_
    Â· apply_rules [L.le_of_opNormâ‚‚_le_of_le, le_rfl]
      refine (le_ciSup_set hbg <| mem_preimage.mpr ?_)
      rwa [neg_sub, sub_add_cancel]
    Â· have : t âˆ‰ support fun t => L (f t) (g (xâ‚€ - t)) := mt (fun h => h2s h) ht
      rw [notMem_support.mp this, norm_zero]
  refine Integrable.mono' ?_ ?_ this
  Â· rw [integrable_indicator_iff hs]; exact ((hf.norm.const_mul _).mul_const _).integrableOn
  Â· exact hf.aestronglyMeasurable.convolution_integrand_snd' L hmg

/-- If `â€–fâ€– *[Î¼] â€–gâ€–` exists, then `f *[L, Î¼] g` exists. -/
theorem ConvolutionExistsAt.of_norm' {xâ‚€ : G}
    (h : ConvolutionExistsAt (fun x => â€–f xâ€–) (fun x => â€–g xâ€–) xâ‚€ (mul â„ â„) Î¼)
    (hmf : AEStronglyMeasurable f Î¼) (hmg : AEStronglyMeasurable g <| map (fun t => xâ‚€ - t) Î¼) :
    ConvolutionExistsAt f g xâ‚€ L Î¼ := by
  refine (h.const_mul â€–Lâ€–).mono'
    (hmf.convolution_integrand_snd' L hmg) (Eventually.of_forall fun x => ?_)
  rw [mul_apply', â† mul_assoc]
  apply L.le_opNormâ‚‚

end

section Left

variable [MeasurableAddâ‚‚ G] [MeasurableNeg G] [SFinite Î¼] [IsAddRightInvariant Î¼]

theorem AEStronglyMeasurable.convolution_integrand_snd (hf : AEStronglyMeasurable f Î¼)
    (hg : AEStronglyMeasurable g Î¼) (x : G) :
    AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Î¼ :=
  hf.convolution_integrand_snd' L <|
    hg.mono_ac <| (quasiMeasurePreserving_sub_left_of_right_invariant Î¼ x).absolutelyContinuous

theorem AEStronglyMeasurable.convolution_integrand_swap_snd
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) (x : G) :
    AEStronglyMeasurable (fun t => L (f (x - t)) (g t)) Î¼ :=
  (hf.mono_ac
        (quasiMeasurePreserving_sub_left_of_right_invariant Î¼
            x).absolutelyContinuous).convolution_integrand_swap_snd'
    L hg

/-- If `â€–fâ€– *[Î¼] â€–gâ€–` exists, then `f *[L, Î¼] g` exists. -/
theorem ConvolutionExistsAt.of_norm {xâ‚€ : G}
    (h : ConvolutionExistsAt (fun x => â€–f xâ€–) (fun x => â€–g xâ€–) xâ‚€ (mul â„ â„) Î¼)
    (hmf : AEStronglyMeasurable f Î¼) (hmg : AEStronglyMeasurable g Î¼) :
    ConvolutionExistsAt f g xâ‚€ L Î¼ :=
  h.of_norm' L hmf <|
    hmg.mono_ac (quasiMeasurePreserving_sub_left_of_right_invariant Î¼ xâ‚€).absolutelyContinuous

end Left

section Right

variable [MeasurableAddâ‚‚ G] [MeasurableNeg G] [SFinite Î¼] [IsAddRightInvariant Î¼] [SFinite Î½]

theorem AEStronglyMeasurable.convolution_integrand (hf : AEStronglyMeasurable f Î½)
    (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun p : G Ã— G => L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î½) :=
  hf.convolution_integrand' L <|
    hg.mono_ac (quasiMeasurePreserving_sub_of_right_invariant Î¼ Î½).absolutelyContinuous

theorem Integrable.convolution_integrand (hf : Integrable f Î½) (hg : Integrable g Î¼) :
    Integrable (fun p : G Ã— G => L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î½) := by
  have h_meas : AEStronglyMeasurable (fun p : G Ã— G => L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î½) :=
    hf.aestronglyMeasurable.convolution_integrand L hg.aestronglyMeasurable
  have h2_meas : AEStronglyMeasurable (fun y : G => âˆ« x : G, â€–L (f y) (g (x - y))â€– âˆ‚Î¼) Î½ :=
    h_meas.prod_swap.norm.integral_prod_right'
  simp_rw [integrable_prod_iff' h_meas]
  refine âŸ¨Eventually.of_forall fun t => (L (f t)).integrable_comp (hg.comp_sub_right t), ?_âŸ©
  refine Integrable.mono' ?_ h2_meas
      (Eventually.of_forall fun t => (?_ : _ â‰¤ â€–Lâ€– * â€–f tâ€– * âˆ« x, â€–g (x - t)â€– âˆ‚Î¼))
  Â· simp only [integral_sub_right_eq_self (â€–g Â·â€–)]
    fun_prop
  Â· simp_rw [â† integral_const_mul]
    rw [Real.norm_of_nonneg (by positivity)]
    exact integral_mono_of_nonneg (Eventually.of_forall fun t => norm_nonneg _)
      ((hg.comp_sub_right t).norm.const_mul _) (Eventually.of_forall fun t => L.le_opNormâ‚‚ _ _)

theorem Integrable.ae_convolution_exists (hf : Integrable f Î½) (hg : Integrable g Î¼) :
    âˆ€áµ x âˆ‚Î¼, ConvolutionExistsAt f g x L Î½ :=
  ((integrable_prod_iff <|
          hf.aestronglyMeasurable.convolution_integrand L hg.aestronglyMeasurable).mp <|
      hf.convolution_integrand L hg).1

end Right

variable [TopologicalSpace G] [IsTopologicalAddGroup G] [BorelSpace G]

theorem _root_.HasCompactSupport.convolutionExistsAt {xâ‚€ : G}
    (h : HasCompactSupport fun t => L (f t) (g (xâ‚€ - t))) (hf : LocallyIntegrable f Î¼)
    (hg : Continuous g) : ConvolutionExistsAt f g xâ‚€ L Î¼ := by
  let u := (Homeomorph.neg G).trans (Homeomorph.addRight xâ‚€)
  let v := (Homeomorph.neg G).trans (Homeomorph.addLeft xâ‚€)
  apply ((u.isCompact_preimage.mpr h).bddAbove_image hg.norm.continuousOn).convolutionExistsAt' L
    isClosed_closure.measurableSet subset_closure (hf.integrableOn_isCompact h)
  have A : AEStronglyMeasurable (g âˆ˜ v)
      (Î¼.restrict (tsupport fun t : G => L (f t) (g (xâ‚€ - t)))) := by
    apply (hg.comp v.continuous).continuousOn.aestronglyMeasurable_of_isCompact h
    exact (isClosed_tsupport _).measurableSet
  convert ((v.continuous.measurable.measurePreserving
      (Î¼.restrict (tsupport fun t => L (f t) (g (xâ‚€ - t))))).aestronglyMeasurable_comp_iff
    v.measurableEmbedding).1 A
  ext x
  simp only [v, Homeomorph.neg, sub_eq_add_neg, val_toAddUnits_apply, Homeomorph.trans_apply,
    Equiv.neg_apply, Homeomorph.homeomorph_mk_coe, Homeomorph.coe_addLeft]

theorem _root_.HasCompactSupport.convolutionExists_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Î¼) (hg : Continuous g) : ConvolutionExists f g L Î¼ := by
  intro xâ‚€
  refine HasCompactSupport.convolutionExistsAt L ?_ hf hg
  refine (hcg.comp_homeomorph (Homeomorph.subLeft xâ‚€)).mono ?_
  refine fun t => mt fun ht : g (xâ‚€ - t) = 0 => ?_
  simp_rw [ht, (L _).map_zero]

theorem _root_.HasCompactSupport.convolutionExists_left_of_continuous_right
    (hcf : HasCompactSupport f) (hf : LocallyIntegrable f Î¼) (hg : Continuous g) :
    ConvolutionExists f g L Î¼ := by
  intro xâ‚€
  refine HasCompactSupport.convolutionExistsAt L ?_ hf hg
  refine hcf.mono ?_
  refine fun t => mt fun ht : f t = 0 => ?_
  simp_rw [ht, L.map_zeroâ‚‚]

end Group

section CommGroup

variable [AddCommGroup G]

section MeasurableGroup

variable [MeasurableNeg G] [IsAddLeftInvariant Î¼]

/-- A sufficient condition to prove that `f â‹†[L, Î¼] g` exists.
We assume that the integrand has compact support and `g` is bounded on this support (note that
both properties hold if `g` is continuous with compact support). We also require that `f` is
integrable on the support of the integrand, and that both functions are strongly measurable.

This is a variant of `BddAbove.convolutionExistsAt'` in an abelian group with a left-invariant
measure. This allows us to state the boundedness and measurability of `g` in a more natural way. -/
theorem _root_.BddAbove.convolutionExistsAt [MeasurableAddâ‚‚ G] [SFinite Î¼] {xâ‚€ : G} {s : Set G}
    (hbg : BddAbove ((fun i => â€–g iâ€–) '' ((fun t => xâ‚€ - t) â»Â¹' s))) (hs : MeasurableSet s)
    (h2s : (support fun t => L (f t) (g (xâ‚€ - t))) âŠ† s) (hf : IntegrableOn f s Î¼)
    (hmg : AEStronglyMeasurable g Î¼) : ConvolutionExistsAt f g xâ‚€ L Î¼ := by
  refine BddAbove.convolutionExistsAt' L ?_ hs h2s hf ?_
  Â· simp_rw [â† sub_eq_neg_add, hbg]
  Â· have : AEStronglyMeasurable g (map (fun t : G => xâ‚€ - t) Î¼) :=
      hmg.mono_ac (quasiMeasurePreserving_sub_left_of_right_invariant Î¼ xâ‚€).absolutelyContinuous
    apply this.mono_measure
    exact map_mono restrict_le_self (measurable_const.sub measurable_id')

variable {L} [MeasurableAdd G] [IsNegInvariant Î¼]

theorem convolutionExistsAt_flip :
    ConvolutionExistsAt g f x L.flip Î¼ â†” ConvolutionExistsAt f g x L Î¼ := by
  simp_rw [ConvolutionExistsAt, â† integrable_comp_sub_left (fun t => L (f t) (g (x - t))) x,
    sub_sub_cancel, flip_apply]

theorem ConvolutionExistsAt.integrable_swap (h : ConvolutionExistsAt f g x L Î¼) :
    Integrable (fun t => L (f (x - t)) (g t)) Î¼ := by
  convert h.comp_sub_left x
  simp_rw [sub_sub_self]

theorem convolutionExistsAt_iff_integrable_swap :
    ConvolutionExistsAt f g x L Î¼ â†” Integrable (fun t => L (f (x - t)) (g t)) Î¼ :=
  convolutionExistsAt_flip.symm

end MeasurableGroup

variable [TopologicalSpace G] [IsTopologicalAddGroup G] [BorelSpace G]
variable [IsAddLeftInvariant Î¼] [IsNegInvariant Î¼]

theorem _root_.HasCompactSupport.convolutionExists_left
    (hcf : HasCompactSupport f) (hf : Continuous f)
    (hg : LocallyIntegrable g Î¼) : ConvolutionExists f g L Î¼ := fun xâ‚€ =>
  convolutionExistsAt_flip.mp <| hcf.convolutionExists_right L.flip hg hf xâ‚€

theorem _root_.HasCompactSupport.convolutionExists_right_of_continuous_left
    (hcg : HasCompactSupport g) (hf : Continuous f) (hg : LocallyIntegrable g Î¼) :
    ConvolutionExists f g L Î¼ := fun xâ‚€ =>
  convolutionExistsAt_flip.mp <| hcg.convolutionExists_left_of_continuous_right L.flip hg hf xâ‚€

end CommGroup

end ConvolutionExists

variable [NormedSpace â„ F]

/-- The convolution of two functions `f` and `g` with respect to a continuous bilinear map `L` and
measure `Î¼`. It is defined to be `(f â‹†[L, Î¼] g) x = âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼`. -/
noncomputable def convolution [Sub G] (f : G â†’ E) (g : G â†’ E') (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
    (Î¼ : Measure G := by volume_tac) : G â†’ F := fun x =>
  âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼

/-- The convolution of two functions with respect to a bilinear operation `L` and a measure `Î¼`. -/
scoped[Convolution] notation:67 f " â‹†[" L:67 ", " Î¼:67 "] " g:66 => convolution f g L Î¼

/-- The convolution of two functions with respect to a bilinear operation `L` and the volume. -/
scoped[Convolution]
  notation:67 f " â‹†[" L:67 "] " g:66 => convolution f g L MeasureSpace.volume

/-- The convolution of two real-valued functions with respect to volume. -/
scoped[Convolution]
  notation:67 f " â‹† " g:66 =>
    convolution f g (ContinuousLinearMap.lsmul â„ â„) MeasureSpace.volume

open scoped Convolution

theorem convolution_def [Sub G] : (f â‹†[L, Î¼] g) x = âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼ :=
  rfl

/-- The definition of convolution where the bilinear operator is scalar multiplication.
Note: it often helps the elaborator to give the type of the convolution explicitly. -/
theorem convolution_lsmul [Sub G] {f : G â†’ ğ•œ} {g : G â†’ F} :
    (f â‹†[lsmul ğ•œ ğ•œ, Î¼] g : G â†’ F) x = âˆ« t, f t â€¢ g (x - t) âˆ‚Î¼ :=
  rfl

/-- The definition of convolution where the bilinear operator is multiplication. -/
theorem convolution_mul [Sub G] [NormedSpace â„ ğ•œ] {f : G â†’ ğ•œ} {g : G â†’ ğ•œ} :
    (f â‹†[mul ğ•œ ğ•œ, Î¼] g) x = âˆ« t, f t * g (x - t) âˆ‚Î¼ :=
  rfl

section Group

variable {L} [AddGroup G]

theorem smul_convolution [SMulCommClass â„ ğ•œ F] {y : ğ•œ} : y â€¢ f â‹†[L, Î¼] g = y â€¢ (f â‹†[L, Î¼] g) := by
  ext; simp only [Pi.smul_apply, convolution_def, â† integral_smul, L.map_smulâ‚‚]

theorem convolution_smul [SMulCommClass â„ ğ•œ F] {y : ğ•œ} : f â‹†[L, Î¼] y â€¢ g = y â€¢ (f â‹†[L, Î¼] g) := by
  ext; simp only [Pi.smul_apply, convolution_def, â† integral_smul, (L _).map_smul]

@[simp]
theorem zero_convolution : 0 â‹†[L, Î¼] g = 0 := by
  ext
  simp_rw [convolution_def, Pi.zero_apply, L.map_zeroâ‚‚, integral_zero]

@[simp]
theorem convolution_zero : f â‹†[L, Î¼] 0 = 0 := by
  ext
  simp_rw [convolution_def, Pi.zero_apply, (L _).map_zero, integral_zero]

theorem ConvolutionExistsAt.distrib_add {x : G} (hfg : ConvolutionExistsAt f g x L Î¼)
    (hfg' : ConvolutionExistsAt f g' x L Î¼) :
    (f â‹†[L, Î¼] (g + g')) x = (f â‹†[L, Î¼] g) x + (f â‹†[L, Î¼] g') x := by
  simp only [convolution_def, (L _).map_add, Pi.add_apply, integral_add hfg hfg']

theorem ConvolutionExists.distrib_add (hfg : ConvolutionExists f g L Î¼)
    (hfg' : ConvolutionExists f g' L Î¼) : f â‹†[L, Î¼] (g + g') = f â‹†[L, Î¼] g + f â‹†[L, Î¼] g' := by
  ext x
  exact (hfg x).distrib_add (hfg' x)

theorem ConvolutionExistsAt.add_distrib {x : G} (hfg : ConvolutionExistsAt f g x L Î¼)
    (hfg' : ConvolutionExistsAt f' g x L Î¼) :
    ((f + f') â‹†[L, Î¼] g) x = (f â‹†[L, Î¼] g) x + (f' â‹†[L, Î¼] g) x := by
  simp only [convolution_def, L.map_addâ‚‚, Pi.add_apply, integral_add hfg hfg']

theorem ConvolutionExists.add_distrib (hfg : ConvolutionExists f g L Î¼)
    (hfg' : ConvolutionExists f' g L Î¼) : (f + f') â‹†[L, Î¼] g = f â‹†[L, Î¼] g + f' â‹†[L, Î¼] g := by
  ext x
  exact (hfg x).add_distrib (hfg' x)

theorem convolution_mono_right {f g g' : G â†’ â„} (hfg : ConvolutionExistsAt f g x (lsmul â„ â„) Î¼)
    (hfg' : ConvolutionExistsAt f g' x (lsmul â„ â„) Î¼) (hf : âˆ€ x, 0 â‰¤ f x) (hg : âˆ€ x, g x â‰¤ g' x) :
    (f â‹†[lsmul â„ â„, Î¼] g) x â‰¤ (f â‹†[lsmul â„ â„, Î¼] g') x := by
  apply integral_mono hfg hfg'
  simp only [lsmul_apply, smul_eq_mul]
  intro t
  apply mul_le_mul_of_nonneg_left (hg _) (hf _)

theorem convolution_mono_right_of_nonneg {f g g' : G â†’ â„}
    (hfg' : ConvolutionExistsAt f g' x (lsmul â„ â„) Î¼) (hf : âˆ€ x, 0 â‰¤ f x) (hg : âˆ€ x, g x â‰¤ g' x)
    (hg' : âˆ€ x, 0 â‰¤ g' x) : (f â‹†[lsmul â„ â„, Î¼] g) x â‰¤ (f â‹†[lsmul â„ â„, Î¼] g') x := by
  by_cases H : ConvolutionExistsAt f g x (lsmul â„ â„) Î¼
  Â· exact convolution_mono_right H hfg' hf hg
  have : (f â‹†[lsmul â„ â„, Î¼] g) x = 0 := integral_undef H
  rw [this]
  exact integral_nonneg fun y => mul_nonneg (hf y) (hg' (x - y))

variable (L)

theorem convolution_congr [MeasurableAddâ‚‚ G] [MeasurableNeg G] [SFinite Î¼]
    [IsAddRightInvariant Î¼] (h1 : f =áµ[Î¼] f') (h2 : g =áµ[Î¼] g') : f â‹†[L, Î¼] g = f' â‹†[L, Î¼] g' := by
  ext x
  apply integral_congr_ae
  exact (h1.prodMk <| h2.comp_tendsto
    (quasiMeasurePreserving_sub_left_of_right_invariant Î¼ x).tendsto_ae).fun_comp â†¿fun x y â†¦ L x y

theorem support_convolution_subset_swap : support (f â‹†[L, Î¼] g) âŠ† support g + support f := by
  intro x h2x
  by_contra hx
  apply h2x
  simp_rw [Set.mem_add, â† exists_and_left, not_exists, not_and_or, notMem_support] at hx
  rw [convolution_def]
  convert integral_zero G F using 2
  ext t
  rcases hx (x - t) t with (h | h | h)
  Â· rw [h, (L _).map_zero]
  Â· rw [h, L.map_zeroâ‚‚]
  Â· exact (h <| sub_add_cancel x t).elim

section

variable [MeasurableAddâ‚‚ G] [MeasurableNeg G] [SFinite Î¼] [IsAddRightInvariant Î¼]

theorem Integrable.integrable_convolution (hf : Integrable f Î¼)
    (hg : Integrable g Î¼) : Integrable (f â‹†[L, Î¼] g) Î¼ :=
  (hf.convolution_integrand L hg).integral_prod_left

end

variable [TopologicalSpace G]
variable [IsTopologicalAddGroup G]

protected theorem _root_.HasCompactSupport.convolution [T2Space G] (hcf : HasCompactSupport f)
    (hcg : HasCompactSupport g) : HasCompactSupport (f â‹†[L, Î¼] g) :=
  (hcg.isCompact.add hcf).of_isClosed_subset isClosed_closure <|
    closure_minimal
      ((support_convolution_subset_swap L).trans <| add_subset_add subset_closure subset_closure)
      (hcg.isCompact.add hcf).isClosed

variable [BorelSpace G] [TopologicalSpace P]

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in a subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem continuousOn_convolution_right_with_param {g : P â†’ G â†’ E'} {s : Set P} {k : Set G}
    (hk : IsCompact k) (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0)
    (hf : LocallyIntegrable f Î¼) (hg : ContinuousOn â†¿g (s Ã—Ë¢ univ)) :
    ContinuousOn (fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2) (s Ã—Ë¢ univ) := by
  /- First get rid of the case where the space is not locally compact. Then `g` vanishes everywhere
  and the conclusion is trivial. -/
  by_cases! H : âˆ€ p âˆˆ s, âˆ€ x, g p x = 0
  Â· apply (continuousOn_const (c := 0)).congr
    rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
    apply integral_eq_zero_of_ae (Eventually.of_forall (fun y â†¦ ?_))
    simp [H p hp _]
  have : LocallyCompactSpace G := by
    rcases H with âŸ¨p, hp, x, hxâŸ©
    have A : support (g p) âŠ† k := support_subset_iff'.2 (fun y hy â†¦ hgs p y hp hy)
    have B : Continuous (g p) := by
      refine hg.comp_continuous (.prodMk_right _) fun x => ?_
      simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hp
    rcases eq_zero_or_locallyCompactSpace_of_support_subset_isCompact_of_addGroup hk A B with H | H
    Â· simp [H] at hx
    Â· exact H
  /- Since `G` is locally compact, one may thicken `k` a little bit into a larger compact set
  `(-k) + t`, outside of which all functions that appear in the convolution vanish. Then we can
  apply a continuity statement for integrals depending on a parameter, with respect to
  locally integrable functions and compactly supported continuous functions. -/
  rintro âŸ¨qâ‚€, xâ‚€âŸ© âŸ¨hqâ‚€, -âŸ©
  obtain âŸ¨t, t_comp, htâŸ© : âˆƒ t, IsCompact t âˆ§ t âˆˆ ğ“ xâ‚€ := exists_compact_mem_nhds xâ‚€
  let k' : Set G := (-k) +áµ¥ t
  have k'_comp : IsCompact k' := IsCompact.vadd_set hk.neg t_comp
  let g' : (P Ã— G) â†’ G â†’ E' := fun p x â†¦ g p.1 (p.2 - x)
  let s' : Set (P Ã— G) := s Ã—Ë¢ t
  have A : ContinuousOn g'.uncurry (s' Ã—Ë¢ univ) := by
    have : g'.uncurry = g.uncurry âˆ˜ (fun w â†¦ (w.1.1, w.1.2 - w.2)) := by ext y; rfl
    rw [this]
    refine hg.comp (by fun_prop) ?_
    simp +contextual [s', MapsTo]
  have B : ContinuousOn (fun a â†¦ âˆ« x, L (f x) (g' a x) âˆ‚Î¼) s' := by
    apply continuousOn_integral_bilinear_of_locally_integrable_of_compact_support L k'_comp A _
      (hf.integrableOn_isCompact k'_comp)
    rintro âŸ¨p, xâŸ© y âŸ¨hp, hxâŸ© hy
    apply hgs p _ hp
    contrapose! hy
    exact âŸ¨y - x, by simpa using hy, x, hx, by simpâŸ©
  apply ContinuousWithinAt.mono_of_mem_nhdsWithin (B (qâ‚€, xâ‚€) âŸ¨hqâ‚€, mem_of_mem_nhds htâŸ©)
  exact mem_nhdsWithin_prod_iff.2 âŸ¨s, self_mem_nhdsWithin, t, nhdsWithin_le_nhds ht, Subset.rflâŸ©

/-- The convolution `f * g` is continuous if `f` is locally integrable and `g` is continuous and
compactly supported. Version where `g` depends on an additional parameter in an open subset `s` of
a parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of compositions with an additional continuous map. -/
theorem continuousOn_convolution_right_with_param_comp {s : Set P} {v : P â†’ G}
    (hv : ContinuousOn v s) {g : P â†’ G â†’ E'} {k : Set G} (hk : IsCompact k)
    (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0) (hf : LocallyIntegrable f Î¼)
    (hg : ContinuousOn â†¿g (s Ã—Ë¢ univ)) : ContinuousOn (fun x => (f â‹†[L, Î¼] g x) (v x)) s := by
  apply
    (continuousOn_convolution_right_with_param L hk hgs hf hg).comp (continuousOn_id.prodMk hv)
  intro x hx
  simp only [hx, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

/-- The convolution is continuous if one function is locally integrable and the other has compact
support and is continuous. -/
theorem _root_.HasCompactSupport.continuous_convolution_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Î¼) (hg : Continuous g) : Continuous (f â‹†[L, Î¼] g) := by
  rw [â† continuousOn_univ]
  let g' : G â†’ G â†’ E' := fun _ q => g q
  have : ContinuousOn â†¿g' (univ Ã—Ë¢ univ) := (hg.comp continuous_snd).continuousOn
  exact continuousOn_convolution_right_with_param_comp L
    (continuousOn_univ.2 continuous_id) hcg
    (fun p x _ hx => image_eq_zero_of_notMem_tsupport hx) hf this

/-- The convolution is continuous if one function is integrable and the other is bounded and
continuous. -/
theorem _root_.BddAbove.continuous_convolution_right_of_integrable
    [FirstCountableTopology G] [SecondCountableTopologyEither G E']
    (hbg : BddAbove (range fun x => â€–g xâ€–)) (hf : Integrable f Î¼) (hg : Continuous g) :
    Continuous (f â‹†[L, Î¼] g) := by
  refine continuous_iff_continuousAt.mpr fun xâ‚€ => ?_
  have : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ t : G âˆ‚Î¼, â€–L (f t) (g (x - t))â€– â‰¤ â€–Lâ€– * â€–f tâ€– * â¨† i, â€–g iâ€– := by
    filter_upwards with x; filter_upwards with t
    apply_rules [L.le_of_opNormâ‚‚_le_of_le, le_rfl, le_ciSup hbg (x - t)]
  refine continuousAt_of_dominated ?_ this (by fun_prop) ?_
  Â· exact Eventually.of_forall fun x =>
      hf.aestronglyMeasurable.convolution_integrand_snd' L hg.aestronglyMeasurable
  Â· filter_upwards with t; fun_prop

end Group

section CommGroup

variable [AddCommGroup G]

theorem support_convolution_subset : support (f â‹†[L, Î¼] g) âŠ† support f + support g :=
  (support_convolution_subset_swap L).trans (add_comm _ _).subset

variable [IsAddLeftInvariant Î¼] [IsNegInvariant Î¼]

section Measurable

variable [MeasurableNeg G]
variable [MeasurableAdd G]

/-- Commutativity of convolution -/
theorem convolution_flip : g â‹†[L.flip, Î¼] f = f â‹†[L, Î¼] g := by
  ext1 x
  simp_rw [convolution_def]
  rw [â† integral_sub_left_eq_self _ Î¼ x]
  simp_rw [sub_sub_self, flip_apply]

/-- The symmetric definition of convolution. -/
theorem convolution_eq_swap : (f â‹†[L, Î¼] g) x = âˆ« t, L (f (x - t)) (g t) âˆ‚Î¼ := by
  rw [â† convolution_flip]; rfl

/-- The symmetric definition of convolution where the bilinear operator is scalar multiplication. -/
theorem convolution_lsmul_swap {f : G â†’ ğ•œ} {g : G â†’ F} :
    (f â‹†[lsmul ğ•œ ğ•œ, Î¼] g : G â†’ F) x = âˆ« t, f (x - t) â€¢ g t âˆ‚Î¼ :=
  convolution_eq_swap _

/-- The symmetric definition of convolution where the bilinear operator is multiplication. -/
theorem convolution_mul_swap [NormedSpace â„ ğ•œ] {f : G â†’ ğ•œ} {g : G â†’ ğ•œ} :
    (f â‹†[mul ğ•œ ğ•œ, Î¼] g) x = âˆ« t, f (x - t) * g t âˆ‚Î¼ :=
  convolution_eq_swap _

/-- The convolution of two even functions is also even. -/
theorem convolution_neg_of_neg_eq (h1 : âˆ€áµ x âˆ‚Î¼, f (-x) = f x) (h2 : âˆ€áµ x âˆ‚Î¼, g (-x) = g x) :
    (f â‹†[L, Î¼] g) (-x) = (f â‹†[L, Î¼] g) x :=
  calc
    âˆ« t : G, (L (f t)) (g (-x - t)) âˆ‚Î¼ = âˆ« t : G, (L (f (-t))) (g (x + t)) âˆ‚Î¼ := by
      apply integral_congr_ae
      filter_upwards [h1, (eventually_add_left_iff Î¼ x).2 h2] with t ht h't
      simp_rw [ht, â† h't, neg_add']
    _ = âˆ« t : G, (L (f t)) (g (x - t)) âˆ‚Î¼ := by
      rw [â† integral_neg_eq_self]
      simp only [neg_neg, â† sub_eq_add_neg]

end Measurable

variable [TopologicalSpace G]
variable [IsTopologicalAddGroup G]
variable [BorelSpace G]

theorem _root_.HasCompactSupport.continuous_convolution_left
    (hcf : HasCompactSupport f) (hf : Continuous f) (hg : LocallyIntegrable g Î¼) :
    Continuous (f â‹†[L, Î¼] g) := by
  rw [â† convolution_flip]
  exact hcf.continuous_convolution_right L.flip hg hf

theorem _root_.BddAbove.continuous_convolution_left_of_integrable
    [FirstCountableTopology G] [SecondCountableTopologyEither G E]
    (hbf : BddAbove (range fun x => â€–f xâ€–)) (hf : Continuous f) (hg : Integrable g Î¼) :
    Continuous (f â‹†[L, Î¼] g) := by
  rw [â† convolution_flip]
  exact hbf.continuous_convolution_right_of_integrable L.flip hg hf

end CommGroup

section NormedAddCommGroup

variable [SeminormedAddCommGroup G]

/-- Compute `(f â‹† g) xâ‚€` if the support of the `f` is within `Metric.ball 0 R`, and `g` is constant
on `Metric.ball xâ‚€ R`.

We can simplify the RHS further if we assume `f` is integrable, but also if `L = (â€¢)` or more
generally if `L` has an `AntilipschitzWith`-condition. -/
theorem convolution_eq_right' {xâ‚€ : G} {R : â„} (hf : support f âŠ† ball (0 : G) R)
    (hg : âˆ€ x âˆˆ ball xâ‚€ R, g x = g xâ‚€) : (f â‹†[L, Î¼] g) xâ‚€ = âˆ« t, L (f t) (g xâ‚€) âˆ‚Î¼ := by
  have h2 : âˆ€ t, L (f t) (g (xâ‚€ - t)) = L (f t) (g xâ‚€) := fun t â†¦ by
    by_cases ht : t âˆˆ support f
    Â· have h2t := hf ht
      rw [mem_ball_zero_iff] at h2t
      specialize hg (xâ‚€ - t)
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, â† sub_eq_add_neg] at hg
      rw [hg h2t]
    Â· rw [notMem_support] at ht
      simp_rw [ht, L.map_zeroâ‚‚]
  simp_rw [convolution_def, h2]

variable [BorelSpace G] [SecondCountableTopology G]
variable [IsAddLeftInvariant Î¼] [SFinite Î¼]

/-- Approximate `(f â‹† g) xâ‚€` if the support of the `f` is bounded within a ball, and `g` is near
`g xâ‚€` on a ball with the same radius around `xâ‚€`. See `dist_convolution_le` for a special case.

We can simplify the second argument of `dist` further if we add some extra type-classes on `E`
and `ğ•œ` or if `L` is scalar multiplication. -/
theorem dist_convolution_le' {xâ‚€ : G} {R Îµ : â„} {zâ‚€ : E'} (hÎµ : 0 â‰¤ Îµ) (hif : Integrable f Î¼)
    (hf : support f âŠ† ball (0 : G) R) (hmg : AEStronglyMeasurable g Î¼)
    (hg : âˆ€ x âˆˆ ball xâ‚€ R, dist (g x) zâ‚€ â‰¤ Îµ) :
    dist ((f â‹†[L, Î¼] g : G â†’ F) xâ‚€) (âˆ« t, L (f t) zâ‚€ âˆ‚Î¼) â‰¤ (â€–Lâ€– * âˆ« x, â€–f xâ€– âˆ‚Î¼) * Îµ := by
  have hfg : ConvolutionExistsAt f g xâ‚€ L Î¼ := by
    refine BddAbove.convolutionExistsAt L ?_ Metric.isOpen_ball.measurableSet (Subset.trans ?_ hf)
      hif.integrableOn hmg
    swap; Â· refine fun t => mt fun ht : f t = 0 => ?_; simp_rw [ht, L.map_zeroâ‚‚]
    rw [bddAbove_def]
    refine âŸ¨â€–zâ‚€â€– + Îµ, ?_âŸ©
    rintro _ âŸ¨x, hx, rflâŸ©
    refine norm_le_norm_add_const_of_dist_le (hg x ?_)
    rwa [mem_ball_iff_norm, norm_sub_rev, â† mem_ball_zero_iff]
  have h2 : âˆ€ t, dist (L (f t) (g (xâ‚€ - t))) (L (f t) zâ‚€) â‰¤ â€–L (f t)â€– * Îµ := by
    intro t; by_cases ht : t âˆˆ support f
    Â· have h2t := hf ht
      rw [mem_ball_zero_iff] at h2t
      specialize hg (xâ‚€ - t)
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, â† sub_eq_add_neg] at hg
      refine ((L (f t)).dist_le_opNorm _ _).trans ?_
      exact mul_le_mul_of_nonneg_left (hg h2t) (norm_nonneg _)
    Â· rw [notMem_support] at ht
      simp_rw [ht, L.map_zeroâ‚‚, L.map_zero, norm_zero, zero_mul, dist_self]
      rfl
  simp_rw [convolution_def]
  simp_rw [dist_eq_norm] at h2 âŠ¢
  rw [â† integral_sub hfg.integrable]; swap; Â· exact (L.flip zâ‚€).integrable_comp hif
  refine (norm_integral_le_of_norm_le ((L.integrable_comp hif).norm.mul_const Îµ)
    (Eventually.of_forall h2)).trans ?_
  rw [integral_mul_const]
  refine mul_le_mul_of_nonneg_right ?_ hÎµ
  have h3 : âˆ€ t, â€–L (f t)â€– â‰¤ â€–Lâ€– * â€–f tâ€– := by
    intro t
    exact L.le_opNorm (f t)
  refine (integral_mono (L.integrable_comp hif).norm (hif.norm.const_mul _) h3).trans_eq ?_
  rw [integral_const_mul]

variable [NormedSpace â„ E] [NormedSpace â„ E'] [CompleteSpace E']

/-- Approximate `f â‹† g` if the support of the `f` is bounded within a ball, and `g` is near `g xâ‚€`
on a ball with the same radius around `xâ‚€`.

This is a special case of `dist_convolution_le'` where `L` is `(â€¢)`, `f` has integral 1 and `f` is
nonnegative. -/
theorem dist_convolution_le {f : G â†’ â„} {xâ‚€ : G} {R Îµ : â„} {zâ‚€ : E'} (hÎµ : 0 â‰¤ Îµ)
    (hf : support f âŠ† ball (0 : G) R) (hnf : âˆ€ x, 0 â‰¤ f x) (hintf : âˆ« x, f x âˆ‚Î¼ = 1)
    (hmg : AEStronglyMeasurable g Î¼) (hg : âˆ€ x âˆˆ ball xâ‚€ R, dist (g x) zâ‚€ â‰¤ Îµ) :
    dist ((f â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) zâ‚€ â‰¤ Îµ := by
  have hif : Integrable f Î¼ := integrable_of_integral_eq_one hintf
  convert (dist_convolution_le' (lsmul â„ â„) hÎµ hif hf hmg hg).trans _
  Â· simp_rw [lsmul_apply, integral_smul_const, hintf, one_smul]
  Â· simp_rw [Real.norm_of_nonneg (hnf _), hintf, mul_one]
    exact (mul_le_mul_of_nonneg_right opNorm_lsmul_le hÎµ).trans_eq (one_mul Îµ)

/-- `(Ï† i â‹† g i) (k i)` tends to `zâ‚€` as `i` tends to some filter `l` if
* `Ï†` is a sequence of nonnegative functions with integral `1` as `i` tends to `l`;
* The support of `Ï†` tends to small neighborhoods around `(0 : G)` as `i` tends to `l`;
* `g i` is `mu`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `zâ‚€` as `(i, x)` tends to `l Ã—Ë¢ ğ“ xâ‚€`;
* `k i` tends to `xâ‚€`.

See also `ContDiffBump.convolution_tendsto_right`.
-/
theorem convolution_tendsto_right {Î¹} {g : Î¹ â†’ G â†’ E'} {l : Filter Î¹} {xâ‚€ : G} {zâ‚€ : E'}
    {Ï† : Î¹ â†’ G â†’ â„} {k : Î¹ â†’ G} (hnÏ† : âˆ€á¶  i in l, âˆ€ x, 0 â‰¤ Ï† i x)
    (hiÏ† : âˆ€á¶  i in l, âˆ« x, Ï† i x âˆ‚Î¼ = 1)
    -- todo: we could weaken this to "the integral tends to 1"
    (hÏ† : Tendsto (fun n => support (Ï† n)) l (ğ“ 0).smallSets)
    (hmg : âˆ€á¶  i in l, AEStronglyMeasurable (g i) Î¼) (hcg : Tendsto (uncurry g) (l Ã—Ë¢ ğ“ xâ‚€) (ğ“ zâ‚€))
    (hk : Tendsto k l (ğ“ xâ‚€)) :
    Tendsto (fun i : Î¹ => (Ï† i â‹†[lsmul â„ â„, Î¼] g i : G â†’ E') (k i)) l (ğ“ zâ‚€) := by
  simp_rw [tendsto_smallSets_iff] at hÏ†
  rw [Metric.tendsto_nhds] at hcg âŠ¢
  simp_rw [Metric.eventually_prod_nhds_iff] at hcg
  intro Îµ hÎµ
  have h2Îµ : 0 < Îµ / 3 := div_pos hÎµ (by simp)
  obtain âŸ¨p, hp, Î´, hÎ´, hgÎ´âŸ© := hcg _ h2Îµ
  dsimp only [uncurry] at hgÎ´
  have h2k := hk.eventually (ball_mem_nhds xâ‚€ <| half_pos hÎ´)
  have h2Ï† := hÏ† (ball (0 : G) _) <| ball_mem_nhds _ (half_pos hÎ´)
  filter_upwards [hp, h2k, h2Ï†, hnÏ†, hiÏ†, hmg] with i hpi hki hÏ†i hnÏ†i hiÏ†i hmgi
  have hgi : dist (g i (k i)) zâ‚€ < Îµ / 3 := hgÎ´ hpi (hki.trans <| half_lt_self hÎ´)
  have h1 : âˆ€ x' âˆˆ ball (k i) (Î´ / 2), dist (g i x') (g i (k i)) â‰¤ Îµ / 3 + Îµ / 3 := by
    intro x' hx'
    refine (dist_triangle_right _ _ _).trans (add_le_add (hgÎ´ hpi ?_).le hgi.le)
    exact ((dist_triangle _ _ _).trans_lt (add_lt_add hx'.out hki)).trans_eq (add_halves Î´)
  have := dist_convolution_le (add_pos h2Îµ h2Îµ).le hÏ†i hnÏ†i hiÏ†i hmgi h1
  refine ((dist_triangle _ _ _).trans_lt (add_lt_add_of_le_of_lt this hgi)).trans_eq ?_
  ring

end NormedAddCommGroup

end Measurability

end NontriviallyNormedField

open scoped Convolution

section RCLike
variable [RCLike ğ•œ]
variable [NormedSpace ğ•œ E]
variable [NormedSpace ğ•œ E']
variable [NormedSpace ğ•œ E'']
variable [NormedSpace â„ F] [NormedSpace ğ•œ F]
variable {n : â„•âˆ}
variable [MeasurableSpace G] {Î¼ Î½ : Measure G}
variable (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)

section Assoc
variable [CompleteSpace F]
variable [NormedAddCommGroup F'] [NormedSpace â„ F'] [NormedSpace ğ•œ F'] [CompleteSpace F']
variable [NormedAddCommGroup F''] [NormedSpace â„ F''] [NormedSpace ğ•œ F''] [CompleteSpace F'']
variable {k : G â†’ E''}
variable (Lâ‚‚ : F â†’L[ğ•œ] E'' â†’L[ğ•œ] F')
variable (Lâ‚ƒ : E â†’L[ğ•œ] F'' â†’L[ğ•œ] F')
variable (Lâ‚„ : E' â†’L[ğ•œ] E'' â†’L[ğ•œ] F'')
variable [AddGroup G]
variable [SFinite Î¼] [SFinite Î½] [IsAddRightInvariant Î¼]

theorem integral_convolution [MeasurableAddâ‚‚ G] [MeasurableNeg G] [NormedSpace â„ E]
    [NormedSpace â„ E'] [CompleteSpace E] [CompleteSpace E'] (hf : Integrable f Î½)
    (hg : Integrable g Î¼) : âˆ« x, (f â‹†[L, Î½] g) x âˆ‚Î¼ = L (âˆ« x, f x âˆ‚Î½) (âˆ« x, g x âˆ‚Î¼) := by
  refine (integral_integral_swap (by apply hf.convolution_integrand L hg)).trans ?_
  simp_rw [integral_comp_comm _ (hg.comp_sub_right _), integral_sub_right_eq_self]
  exact (L.flip (âˆ« x, g x âˆ‚Î¼)).integral_comp_comm hf

variable [MeasurableAddâ‚‚ G] [IsAddRightInvariant Î½] [MeasurableNeg G]

/-- Convolution is associative. This has a weak but inconvenient integrability condition.
See also `MeasureTheory.convolution_assoc`. -/
theorem convolution_assoc' (hL : âˆ€ (x : E) (y : E') (z : E''), Lâ‚‚ (L x y) z = Lâ‚ƒ x (Lâ‚„ y z))
    {xâ‚€ : G} (hfg : âˆ€áµ y âˆ‚Î¼, ConvolutionExistsAt f g y L Î½)
    (hgk : âˆ€áµ x âˆ‚Î½, ConvolutionExistsAt g k x Lâ‚„ Î¼)
    (hi : Integrable (uncurry fun x y => (Lâ‚ƒ (f y)) ((Lâ‚„ (g (x - y))) (k (xâ‚€ - x)))) (Î¼.prod Î½)) :
    ((f â‹†[L, Î½] g) â‹†[Lâ‚‚, Î¼] k) xâ‚€ = (f â‹†[Lâ‚ƒ, Î½] g â‹†[Lâ‚„, Î¼] k) xâ‚€ :=
  calc
    ((f â‹†[L, Î½] g) â‹†[Lâ‚‚, Î¼] k) xâ‚€ = âˆ« t, Lâ‚‚ (âˆ« s, L (f s) (g (t - s)) âˆ‚Î½) (k (xâ‚€ - t)) âˆ‚Î¼ := rfl
    _ = âˆ« t, âˆ« s, Lâ‚‚ (L (f s) (g (t - s))) (k (xâ‚€ - t)) âˆ‚Î½ âˆ‚Î¼ :=
      (integral_congr_ae (hfg.mono fun t ht => ((Lâ‚‚.flip (k (xâ‚€ - t))).integral_comp_comm ht).symm))
    _ = âˆ« t, âˆ« s, Lâ‚ƒ (f s) (Lâ‚„ (g (t - s)) (k (xâ‚€ - t))) âˆ‚Î½ âˆ‚Î¼ := by simp_rw [hL]
    _ = âˆ« s, âˆ« t, Lâ‚ƒ (f s) (Lâ‚„ (g (t - s)) (k (xâ‚€ - t))) âˆ‚Î¼ âˆ‚Î½ := by rw [integral_integral_swap hi]
    _ = âˆ« s, âˆ« u, Lâ‚ƒ (f s) (Lâ‚„ (g u) (k (xâ‚€ - s - u))) âˆ‚Î¼ âˆ‚Î½ := by
      congr; ext t
      rw [eq_comm, â† integral_sub_right_eq_self _ t]
      simp_rw [sub_sub_sub_cancel_right]
    _ = âˆ« s, Lâ‚ƒ (f s) (âˆ« u, Lâ‚„ (g u) (k (xâ‚€ - s - u)) âˆ‚Î¼) âˆ‚Î½ := by
      refine integral_congr_ae ?_
      refine ((quasiMeasurePreserving_sub_left_of_right_invariant Î½ xâ‚€).ae hgk).mono fun t ht => ?_
      exact (Lâ‚ƒ (f t)).integral_comp_comm ht
    _ = (f â‹†[Lâ‚ƒ, Î½] g â‹†[Lâ‚„, Î¼] k) xâ‚€ := rfl

/-- Convolution is associative. This requires that
* all maps are a.e. strongly measurable w.r.t. one of the measures
* `f â‹†[L, Î½] g` exists almost everywhere
* `â€–gâ€– â‹†[Î¼] â€–kâ€–` exists almost everywhere
* `â€–fâ€– â‹†[Î½] (â€–gâ€– â‹†[Î¼] â€–kâ€–)` exists at `xâ‚€` -/
theorem convolution_assoc (hL : âˆ€ (x : E) (y : E') (z : E''), Lâ‚‚ (L x y) z = Lâ‚ƒ x (Lâ‚„ y z)) {xâ‚€ : G}
    (hf : AEStronglyMeasurable f Î½) (hg : AEStronglyMeasurable g Î¼) (hk : AEStronglyMeasurable k Î¼)
    (hfg : âˆ€áµ y âˆ‚Î¼, ConvolutionExistsAt f g y L Î½)
    (hgk : âˆ€áµ x âˆ‚Î½, ConvolutionExistsAt (fun x => â€–g xâ€–) (fun x => â€–k xâ€–) x (mul â„ â„) Î¼)
    (hfgk :
      ConvolutionExistsAt (fun x => â€–f xâ€–) ((fun x => â€–g xâ€–) â‹†[mul â„ â„, Î¼] fun x => â€–k xâ€–) xâ‚€
        (mul â„ â„) Î½) :
    ((f â‹†[L, Î½] g) â‹†[Lâ‚‚, Î¼] k) xâ‚€ = (f â‹†[Lâ‚ƒ, Î½] g â‹†[Lâ‚„, Î¼] k) xâ‚€ := by
  refine convolution_assoc' L Lâ‚‚ Lâ‚ƒ Lâ‚„ hL hfg (hgk.mono fun x hx => hx.of_norm Lâ‚„ hg hk) ?_
  -- the following is similar to `Integrable.convolution_integrand`
  have h_meas :
    AEStronglyMeasurable (uncurry fun x y => Lâ‚ƒ (f y) (Lâ‚„ (g x) (k (xâ‚€ - y - x))))
      (Î¼.prod Î½) := by
    refine Lâ‚ƒ.aestronglyMeasurable_compâ‚‚ hf.comp_snd ?_
    refine Lâ‚„.aestronglyMeasurable_compâ‚‚ hg.comp_fst ?_
    refine (hk.mono_ac ?_).comp_measurable (by fun_prop)
    refine QuasiMeasurePreserving.absolutelyContinuous ?_
    refine QuasiMeasurePreserving.prod_of_left (by fun_prop) (Eventually.of_forall fun y => ?_)
    dsimp only
    exact quasiMeasurePreserving_sub_left_of_right_invariant Î¼ _
  have h2_meas :
      AEStronglyMeasurable (fun y => âˆ« x, â€–Lâ‚ƒ (f y) (Lâ‚„ (g x) (k (xâ‚€ - y - x)))â€– âˆ‚Î¼) Î½ :=
    h_meas.prod_swap.norm.integral_prod_right'
  have h3 : map (fun z : G Ã— G => (z.1 - z.2, z.2)) (Î¼.prod Î½) = Î¼.prod Î½ :=
    (measurePreserving_sub_prod Î¼ Î½).map_eq
  suffices Integrable (uncurry fun x y => Lâ‚ƒ (f y) (Lâ‚„ (g x) (k (xâ‚€ - y - x)))) (Î¼.prod Î½) by
    rw [â† h3] at this
    convert this.comp_measurable (measurable_sub.prodMk measurable_snd)
    ext âŸ¨x, yâŸ©
    simp +unfoldPartialApp only [uncurry, Function.comp_apply,
      sub_sub_sub_cancel_right]
  simp_rw [integrable_prod_iff' h_meas]
  refine âŸ¨((quasiMeasurePreserving_sub_left_of_right_invariant Î½ xâ‚€).ae hgk).mono fun t ht =>
    (Lâ‚ƒ (f t)).integrable_comp <| ht.of_norm Lâ‚„ hg hk, ?_âŸ©
  refine (hfgk.const_mul (â€–Lâ‚ƒâ€– * â€–Lâ‚„â€–)).mono' h2_meas
    (((quasiMeasurePreserving_sub_left_of_right_invariant Î½ xâ‚€).ae hgk).mono fun t ht => ?_)
  simp_rw [convolution_def, mul_apply', mul_mul_mul_comm â€–Lâ‚ƒâ€– â€–Lâ‚„â€–, â† integral_const_mul]
  rw [Real.norm_of_nonneg (by positivity)]
  refine integral_mono_of_nonneg (Eventually.of_forall fun t => norm_nonneg _)
    ((ht.const_mul _).const_mul _) (Eventually.of_forall fun s => ?_)
  simp only [â† mul_assoc â€–Lâ‚„â€–]
  apply_rules [ContinuousLinearMap.le_of_opNormâ‚‚_le_of_le, le_rfl]

end Assoc

variable [NormedAddCommGroup G] [BorelSpace G]

theorem convolution_precompR_apply {g : G â†’ E'' â†’L[ğ•œ] E'} (hf : LocallyIntegrable f Î¼)
    (hcg : HasCompactSupport g) (hg : Continuous g) (xâ‚€ : G) (x : E'') :
    (f â‹†[L.precompR E'', Î¼] g) xâ‚€ x = (f â‹†[L, Î¼] fun a => g a x) xâ‚€ := by
  have := hcg.convolutionExists_right (L.precompR E'' :) hf hg xâ‚€
  simp_rw [convolution_def, ContinuousLinearMap.integral_apply this]
  rfl

variable [NormedSpace ğ•œ G] [SFinite Î¼] [IsAddLeftInvariant Î¼]

/-- Compute the total derivative of `f â‹† g` if `g` is `C^1` with compact support and `f` is locally
integrable. To write down the total derivative as a convolution, we use
`ContinuousLinearMap.precompR`. -/
theorem _root_.HasCompactSupport.hasFDerivAt_convolution_right (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Î¼) (hg : ContDiff ğ•œ 1 g) (xâ‚€ : G) :
    HasFDerivAt (f â‹†[L, Î¼] g) ((f â‹†[L.precompR G, Î¼] fderiv ğ•œ g) xâ‚€) xâ‚€ := by
  rcases hcg.eq_zero_or_finiteDimensional ğ•œ hg.continuous with (rfl | fin_dim)
  Â· have : fderiv ğ•œ (0 : G â†’ E') = 0 := fderiv_const (0 : E')
    simp only [this, convolution_zero, Pi.zero_apply]
    exact hasFDerivAt_const (0 : F) xâ‚€
  have : ProperSpace G := FiniteDimensional.proper_rclike ğ•œ G
  set L' := L.precompR G
  have h1 : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (fun t => L (f t) (g (x - t))) Î¼ :=
    Eventually.of_forall
      (hf.aestronglyMeasurable.convolution_integrand_snd L hg.continuous.aestronglyMeasurable)
  have h2 : âˆ€ x, AEStronglyMeasurable (fun t => L' (f t) (fderiv ğ•œ g (x - t))) Î¼ :=
    hf.aestronglyMeasurable.convolution_integrand_snd L'
      (hg.continuous_fderiv one_ne_zero).aestronglyMeasurable
  have h3 : âˆ€ x t, HasFDerivAt (fun x => g (x - t)) (fderiv ğ•œ g (x - t)) x := fun x t â†¦ by
    simpa using
      (hg.differentiable one_ne_zero).differentiableAt.hasFDerivAt.comp x
        ((hasFDerivAt_id x).sub (hasFDerivAt_const t x))
  let K' := -tsupport (fderiv ğ•œ g) + closedBall xâ‚€ 1
  have hK' : IsCompact K' := (hcg.fderiv ğ•œ).isCompact.neg.add (isCompact_closedBall xâ‚€ 1)
  apply hasFDerivAt_integral_of_dominated_of_fderiv_le (ball_mem_nhds _ zero_lt_one) h1 _ (h2 xâ‚€)
  Â· filter_upwards with t x hx using
      (hcg.fderiv ğ•œ).convolution_integrand_bound_right L' (hg.continuous_fderiv one_ne_zero)
        (ball_subset_closedBall hx)
  Â· rw [integrable_indicator_iff hK'.measurableSet]
    exact ((hf.integrableOn_isCompact hK').norm.const_mul _).mul_const _
  Â· exact Eventually.of_forall fun t x _ => (L _).hasFDerivAt.comp x (h3 x t)
  Â· exact hcg.convolutionExists_right L hf hg.continuous xâ‚€

theorem _root_.HasCompactSupport.hasFDerivAt_convolution_left [IsNegInvariant Î¼]
    (hcf : HasCompactSupport f) (hf : ContDiff ğ•œ 1 f) (hg : LocallyIntegrable g Î¼) (xâ‚€ : G) :
    HasFDerivAt (f â‹†[L, Î¼] g) ((fderiv ğ•œ f â‹†[L.precompL G, Î¼] g) xâ‚€) xâ‚€ := by
  simp +singlePass only [â† convolution_flip]
  exact hcf.hasFDerivAt_convolution_right L.flip hg hf xâ‚€

end RCLike

section Real

/-! The one-variable case -/

variable [RCLike ğ•œ]
variable [NormedSpace ğ•œ E]
variable [NormedSpace ğ•œ E']
variable [NormedSpace â„ F] [NormedSpace ğ•œ F]
variable {fâ‚€ : ğ•œ â†’ E} {gâ‚€ : ğ•œ â†’ E'}
variable {n : â„•âˆ}
variable (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
variable {Î¼ : Measure ğ•œ}
variable [IsAddLeftInvariant Î¼] [SFinite Î¼]

theorem _root_.HasCompactSupport.hasDerivAt_convolution_right (hf : LocallyIntegrable fâ‚€ Î¼)
    (hcg : HasCompactSupport gâ‚€) (hg : ContDiff ğ•œ 1 gâ‚€) (xâ‚€ : ğ•œ) :
    HasDerivAt (fâ‚€ â‹†[L, Î¼] gâ‚€) ((fâ‚€ â‹†[L, Î¼] deriv gâ‚€) xâ‚€) xâ‚€ := by
  convert (hcg.hasFDerivAt_convolution_right L hf hg xâ‚€).hasDerivAt using 1
  rw [convolution_precompR_apply L hf (hcg.fderiv ğ•œ) (hg.continuous_fderiv one_ne_zero)]
  rfl

theorem _root_.HasCompactSupport.hasDerivAt_convolution_left [IsNegInvariant Î¼]
    (hcf : HasCompactSupport fâ‚€) (hf : ContDiff ğ•œ 1 fâ‚€) (hg : LocallyIntegrable gâ‚€ Î¼) (xâ‚€ : ğ•œ) :
    HasDerivAt (fâ‚€ â‹†[L, Î¼] gâ‚€) ((deriv fâ‚€ â‹†[L, Î¼] gâ‚€) xâ‚€) xâ‚€ := by
  simp +singlePass only [â† convolution_flip]
  exact hcf.hasDerivAt_convolution_right L.flip hg hf xâ‚€

end Real

section WithParam

variable [RCLike ğ•œ] [NormedSpace ğ•œ E] [NormedSpace ğ•œ E'] [NormedSpace ğ•œ E''] [NormedSpace â„ F]
  [NormedSpace ğ•œ F] [MeasurableSpace G] [NormedAddCommGroup G] [BorelSpace G]
  [NormedSpace ğ•œ G] [NormedAddCommGroup P] [NormedSpace ğ•œ P] {Î¼ : Measure G}
  (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)

/-- The derivative of the convolution `f * g` is given by `f * Dg`, when `f` is locally integrable
and `g` is `C^1` and compactly supported. Version where `g` depends on an additional parameter in an
open subset `s` of a parameter space `P` (and the compact support `k` is independent of the
parameter in `s`). -/
theorem hasFDerivAt_convolution_right_with_param {g : P â†’ G â†’ E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0)
    (hf : LocallyIntegrable f Î¼) (hg : ContDiffOn ğ•œ 1 â†¿g (s Ã—Ë¢ univ)) (qâ‚€ : P Ã— G)
    (hqâ‚€ : qâ‚€.1 âˆˆ s) :
    HasFDerivAt (fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2)
      ((f â‹†[L.precompR (P Ã— G), Î¼] fun x : G => fderiv ğ•œ â†¿g (qâ‚€.1, x)) qâ‚€.2) qâ‚€ := by
  let g' := fderiv ğ•œ â†¿g
  have A : âˆ€ p âˆˆ s, Continuous (g p) := fun p hp â†¦ by
    refine hg.continuousOn.comp_continuous (.prodMk_right _) fun x => ?_
    simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hp
  have A' : âˆ€ q : P Ã— G, q.1 âˆˆ s â†’ s Ã—Ë¢ univ âˆˆ ğ“ q := fun q hq â†¦ by
    apply (hs.prod isOpen_univ).mem_nhds
    simpa only [mem_prod, mem_univ, and_true] using hq
  -- The derivative of `g` vanishes away from `k`.
  have g'_zero : âˆ€ p x, p âˆˆ s â†’ x âˆ‰ k â†’ g' (p, x) = 0 := by
    intro p x hp hx
    refine (hasFDerivAt_zero_of_eventually_const 0 ?_).fderiv
    have M2 : ká¶œ âˆˆ ğ“ x := hk.isClosed.isOpen_compl.mem_nhds hx
    have M1 : s âˆˆ ğ“ p := hs.mem_nhds hp
    rw [nhds_prod_eq]
    filter_upwards [prod_mem_prod M1 M2]
    rintro âŸ¨p, yâŸ© âŸ¨hp, hyâŸ©
    exact hgs p y hp hy
  /- We find a small neighborhood of `{qâ‚€.1} Ã— k` on which the derivative is uniformly bounded. This
    follows from the continuity at all points of the compact set `k`. -/
  obtain âŸ¨Îµ, C, Îµpos, hâ‚€Îµ, hÎµâŸ© :
      âˆƒ Îµ C, 0 < Îµ âˆ§ ball qâ‚€.1 Îµ âŠ† s âˆ§ âˆ€ p x, â€–p - qâ‚€.1â€– < Îµ â†’ â€–g' (p, x)â€– â‰¤ C := by
    have A : IsCompact ({qâ‚€.1} Ã—Ë¢ k) := isCompact_singleton.prod hk
    obtain âŸ¨t, kt, t_open, htâŸ© : âˆƒ t, {qâ‚€.1} Ã—Ë¢ k âŠ† t âˆ§ IsOpen t âˆ§ IsBounded (g' '' t) := by
      have B : ContinuousOn g' (s Ã—Ë¢ univ) :=
        hg.continuousOn_fderiv_of_isOpen (hs.prod isOpen_univ) le_rfl
      apply exists_isOpen_isBounded_image_of_isCompact_of_continuousOn A (hs.prod isOpen_univ) _ B
      simp only [prod_subset_prod_iff, hqâ‚€, singleton_subset_iff, subset_univ, and_self_iff,
        true_or]
    obtain âŸ¨Îµ, Îµpos, hÎµ, h'ÎµâŸ© :
      âˆƒ Îµ : â„, 0 < Îµ âˆ§ thickening Îµ ({qâ‚€.fst} Ã—Ë¢ k) âŠ† t âˆ§ ball qâ‚€.1 Îµ âŠ† s := by
      obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ Îµ : â„, 0 < Îµ âˆ§ thickening Îµ (({qâ‚€.fst} : Set P) Ã—Ë¢ k) âŠ† t :=
        A.exists_thickening_subset_open t_open kt
      obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ Î´ : â„, 0 < Î´ âˆ§ ball qâ‚€.1 Î´ âŠ† s := Metric.isOpen_iff.1 hs _ hqâ‚€
      refine âŸ¨min Îµ Î´, lt_min Îµpos Î´pos, ?_, ?_âŸ©
      Â· exact Subset.trans (thickening_mono (min_le_left _ _) _) hÎµ
      Â· exact Subset.trans (ball_subset_ball (min_le_right _ _)) hÎ´
    obtain âŸ¨C, Cpos, hCâŸ© : âˆƒ C, 0 < C âˆ§ g' '' t âŠ† closedBall 0 C := ht.subset_closedBall_lt 0 0
    refine âŸ¨Îµ, C, Îµpos, h'Îµ, fun p x hp => ?_âŸ©
    have hps : p âˆˆ s := h'Îµ (mem_ball_iff_norm.2 hp)
    by_cases hx : x âˆˆ k
    Â· have H : (p, x) âˆˆ t := by
        apply hÎµ
        refine mem_thickening_iff.2 âŸ¨(qâ‚€.1, x), ?_, ?_âŸ©
        Â· simp only [hx, singleton_prod, mem_image, Prod.mk_inj, true_and, exists_eq_right]
        Â· rw [â† dist_eq_norm] at hp
          simpa only [Prod.dist_eq, Îµpos, dist_self, max_lt_iff, and_true] using hp
      have : g' (p, x) âˆˆ closedBall (0 : P Ã— G â†’L[ğ•œ] E') C := hC (mem_image_of_mem _ H)
      rwa [mem_closedBall_zero_iff] at this
    Â· have : g' (p, x) = 0 := g'_zero _ _ hps hx
      rw [this]
      simpa only [norm_zero] using Cpos.le
  /- Now, we wish to apply a theorem on differentiation of integrals. For this, we need to check
    trivial measurability or integrability assumptions (in `I1`, `I2`, `I3`), as well as a uniform
    integrability assumption over the derivative (in `I4` and `I5`) and pointwise differentiability
    in `I6`. -/
  have I1 :
    âˆ€á¶  x : P Ã— G in ğ“ qâ‚€, AEStronglyMeasurable (fun a : G => L (f a) (g x.1 (x.2 - a))) Î¼ := by
    filter_upwards [A' qâ‚€ hqâ‚€]
    rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
    refine (HasCompactSupport.convolutionExists_right L ?_ hf (A _ hp) _).1
    apply hk.of_isClosed_subset (isClosed_tsupport _)
    exact closure_minimal (support_subset_iff'.2 fun z hz => hgs _ _ hp hz) hk.isClosed
  have I2 : Integrable (fun a : G => L (f a) (g qâ‚€.1 (qâ‚€.2 - a))) Î¼ := by
    have M : HasCompactSupport (g qâ‚€.1) := HasCompactSupport.intro hk fun x hx => hgs qâ‚€.1 x hqâ‚€ hx
    apply M.convolutionExists_right L hf (A qâ‚€.1 hqâ‚€) qâ‚€.2
  have I3 : AEStronglyMeasurable (fun a : G => (L (f a)).comp (g' (qâ‚€.fst, qâ‚€.snd - a))) Î¼ := by
    have T : HasCompactSupport fun y => g' (qâ‚€.1, y) :=
      HasCompactSupport.intro hk fun x hx => g'_zero qâ‚€.1 x hqâ‚€ hx
    apply (HasCompactSupport.convolutionExists_right (L.precompR (P Ã— G) :) T hf _ qâ‚€.2).1
    have : ContinuousOn g' (s Ã—Ë¢ univ) :=
      hg.continuousOn_fderiv_of_isOpen (hs.prod isOpen_univ) le_rfl
    apply this.comp_continuous (.prodMk_right _)
    intro x
    simpa only [prodMk_mem_set_prod_eq, mem_univ, and_true] using hqâ‚€
  set K' := (-k + {qâ‚€.2} : Set G) with K'_def
  have hK' : IsCompact K' := hk.neg.add isCompact_singleton
  obtain âŸ¨U, U_open, K'U, hUâŸ© : âˆƒ U, IsOpen U âˆ§ K' âŠ† U âˆ§ IntegrableOn f U Î¼ :=
    hf.integrableOn_nhds_isCompact hK'
  obtain âŸ¨Î´, Î´pos, Î´Îµ, hÎ´âŸ© : âˆƒ Î´, (0 : â„) < Î´ âˆ§ Î´ â‰¤ Îµ âˆ§ K' + ball 0 Î´ âŠ† U := by
    obtain âŸ¨V, V_mem, hVâŸ© : âˆƒ V âˆˆ ğ“ (0 : G), K' + V âŠ† U :=
      compact_open_separated_add_right hK' U_open K'U
    rcases Metric.mem_nhds_iff.1 V_mem with âŸ¨Î´, Î´pos, hÎ´âŸ©
    refine âŸ¨min Î´ Îµ, lt_min Î´pos Îµpos, min_le_right Î´ Îµ, ?_âŸ©
    exact (add_subset_add_left ((ball_subset_ball (min_le_left _ _)).trans hÎ´)).trans hV
  letI := ContinuousLinearMap.hasOpNorm (ğ•œ := ğ•œ) (ğ•œâ‚‚ := ğ•œ) (E := E)
    (F := (P Ã— G â†’L[ğ•œ] E') â†’L[ğ•œ] P Ã— G â†’L[ğ•œ] F) (Ïƒâ‚â‚‚ := RingHom.id ğ•œ)
  let bound : G â†’ â„ := indicator U fun t => â€–(L.precompR (P Ã— G))â€– * â€–f tâ€– * C
  have I4 : âˆ€áµ a : G âˆ‚Î¼, âˆ€ x : P Ã— G, dist x qâ‚€ < Î´ â†’
      â€–L.precompR (P Ã— G) (f a) (g' (x.fst, x.snd - a))â€– â‰¤ bound a := by
    filter_upwards with a x hx
    rw [Prod.dist_eq, dist_eq_norm, dist_eq_norm] at hx
    have : (-tsupport fun a => g' (x.1, a)) + ball qâ‚€.2 Î´ âŠ† U := by
      apply Subset.trans _ hÎ´
      rw [K'_def, add_assoc]
      apply add_subset_add
      Â· rw [neg_subset_neg]
        refine closure_minimal (support_subset_iff'.2 fun z hz => ?_) hk.isClosed
        apply g'_zero x.1 z (hâ‚€Îµ _) hz
        rw [mem_ball_iff_norm]
        exact ((le_max_left _ _).trans_lt hx).trans_le Î´Îµ
      Â· simp only [add_ball, thickening_singleton, zero_vadd, subset_rfl]
    apply convolution_integrand_bound_right_of_le_of_subset _ _ _ this
    Â· intro y
      exact hÎµ _ _ (((le_max_left _ _).trans_lt hx).trans_le Î´Îµ)
    Â· rw [mem_ball_iff_norm]
      exact (le_max_right _ _).trans_lt hx
  have I5 : Integrable bound Î¼ := by
    rw [integrable_indicator_iff U_open.measurableSet]
    exact (hU.norm.const_mul _).mul_const _
  have I6 : âˆ€áµ a : G âˆ‚Î¼, âˆ€ x : P Ã— G, dist x qâ‚€ < Î´ â†’
      HasFDerivAt (fun x : P Ã— G => L (f a) (g x.1 (x.2 - a)))
        ((L (f a)).comp (g' (x.fst, x.snd - a))) x := by
    filter_upwards with a x hx
    apply (L _).hasFDerivAt.comp x
    have N : s Ã—Ë¢ univ âˆˆ ğ“ (x.1, x.2 - a) := by
      apply A'
      apply hâ‚€Îµ
      rw [Prod.dist_eq] at hx
      exact lt_of_lt_of_le (lt_of_le_of_lt (le_max_left _ _) hx) Î´Îµ
    have Z := ((hg.differentiableOn one_ne_zero).differentiableAt N).hasFDerivAt
    have Z' :
        HasFDerivAt (fun x : P Ã— G => (x.1, x.2 - a)) (ContinuousLinearMap.id ğ•œ (P Ã— G)) x := by
      have : (fun x : P Ã— G => (x.1, x.2 - a)) = _root_.id - fun x => (0, a) := by
        ext x <;> simp only [Pi.sub_apply, _root_.id, Prod.fst_sub, sub_zero, Prod.snd_sub]
      rw [this]
      exact (hasFDerivAt_id x).sub_const (0, a)
    exact Z.comp x Z'
  exact hasFDerivAt_integral_of_dominated_of_fderiv_le (ball_mem_nhds _ Î´pos) I1 I2 I3 I4 I5 I6

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`).
In this version, all the types belong to the same universe (to get an induction working in the
proof). Use instead `contDiffOn_convolution_right_with_param`, which removes this restriction. -/
theorem contDiffOn_convolution_right_with_param_aux {G : Type uP} {E' : Type uP} {F : Type uP}
    {P : Type uP} [NormedAddCommGroup E'] [NormedAddCommGroup F] [NormedSpace ğ•œ E']
    [NormedSpace â„ F] [NormedSpace ğ•œ F] [MeasurableSpace G]
    {Î¼ : Measure G}
    [NormedAddCommGroup G] [BorelSpace G] [NormedSpace ğ•œ G] [NormedAddCommGroup P] [NormedSpace ğ•œ P]
    {f : G â†’ E} {n : â„•âˆ} (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {g : P â†’ G â†’ E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0)
    (hf : LocallyIntegrable f Î¼) (hg : ContDiffOn ğ•œ n â†¿g (s Ã—Ë¢ univ)) :
    ContDiffOn ğ•œ n (fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2) (s Ã—Ë¢ univ) := by
  /- We have a formula for the derivation of `f * g`, which is of the same form, thanks to
    `hasFDerivAt_convolution_right_with_param`. Therefore, we can prove the result by induction on
    `n` (but for this we need the spaces at the different steps of the induction to live in the same
    universe, which is why we make the assumption in the lemma that all the relevant spaces
    come from the same universe). -/
  induction n using ENat.nat_induction generalizing g E' F with
  | zero =>
    rw [WithTop.coe_zero, contDiffOn_zero] at hg âŠ¢
    exact continuousOn_convolution_right_with_param L hk hgs hf hg
  | succ n ih =>
    simp only [Nat.succ_eq_add_one, Nat.cast_add, Nat.cast_one, WithTop.coe_add,
      WithTop.coe_natCast, WithTop.coe_one] at hg âŠ¢
    let f' : P â†’ G â†’ P Ã— G â†’L[ğ•œ] F := fun p a =>
      (f â‹†[L.precompR (P Ã— G), Î¼] fun x : G => fderiv ğ•œ (uncurry g) (p, x)) a
    have A : âˆ€ qâ‚€ : P Ã— G, qâ‚€.1 âˆˆ s â†’
        HasFDerivAt (fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2) (f' qâ‚€.1 qâ‚€.2) qâ‚€ :=
      hasFDerivAt_convolution_right_with_param L hs hk hgs hf hg.one_of_succ
    rw [contDiffOn_succ_iff_fderiv_of_isOpen (hs.prod (@isOpen_univ G _))] at hg âŠ¢
    refine âŸ¨?_, by simp, ?_âŸ©
    Â· rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
      exact (A (p, x) hp).differentiableAt.differentiableWithinAt
    Â· suffices H : ContDiffOn ğ•œ n â†¿f' (s Ã—Ë¢ univ) by
        apply H.congr
        rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
        exact (A (p, x) hp).fderiv
      have B : âˆ€ (p : P) (x : G), p âˆˆ s â†’ x âˆ‰ k â†’ fderiv ğ•œ (uncurry g) (p, x) = 0 := by
        intro p x hp hx
        apply (hasFDerivAt_zero_of_eventually_const (0 : E') _).fderiv
        have M2 : ká¶œ âˆˆ ğ“ x := IsOpen.mem_nhds hk.isClosed.isOpen_compl hx
        have M1 : s âˆˆ ğ“ p := hs.mem_nhds hp
        rw [nhds_prod_eq]
        filter_upwards [prod_mem_prod M1 M2]
        rintro âŸ¨p, yâŸ© âŸ¨hp, hyâŸ©
        exact hgs p y hp hy
      apply ih (L.precompR (P Ã— G) :) B
      convert hg.2.2
  | top ih =>
    rw [contDiffOn_infty] at hg âŠ¢
    exact fun n â†¦ ih n L hgs (hg n)

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem contDiffOn_convolution_right_with_param {f : G â†’ E} {n : â„•âˆ} (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
    {g : P â†’ G â†’ E'} {s : Set P} {k : Set G} (hs : IsOpen s) (hk : IsCompact k)
    (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0) (hf : LocallyIntegrable f Î¼)
    (hg : ContDiffOn ğ•œ n â†¿g (s Ã—Ë¢ univ)) :
    ContDiffOn ğ•œ n (fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2) (s Ã—Ë¢ univ) := by
  /- The result is known when all the universes are the same, from
    `contDiffOn_convolution_right_with_param_aux`. We reduce to this situation by pushing
    everything through `ULift` continuous linear equivalences. -/
  let eG : Type max uG uE' uF uP := ULift.{max uE' uF uP} G
  borelize eG
  let eE' : Type max uE' uG uF uP := ULift.{max uG uF uP} E'
  let eF : Type max uF uG uE' uP := ULift.{max uG uE' uP} F
  let eP : Type max uP uG uE' uF := ULift.{max uG uE' uF} P
  let isoG : eG â‰ƒL[ğ•œ] G := ContinuousLinearEquiv.ulift
  let isoE' : eE' â‰ƒL[ğ•œ] E' := ContinuousLinearEquiv.ulift
  let isoF : eF â‰ƒL[ğ•œ] F := ContinuousLinearEquiv.ulift
  let isoP : eP â‰ƒL[ğ•œ] P := ContinuousLinearEquiv.ulift
  let ef := f âˆ˜ isoG
  let eÎ¼ : Measure eG := Measure.map isoG.symm Î¼
  let eg : eP â†’ eG â†’ eE' := fun ep ex => isoE'.symm (g (isoP ep) (isoG ex))
  let eL :=
    ContinuousLinearMap.comp
      ((ContinuousLinearEquiv.arrowCongr isoE' isoF).symm : (E' â†’L[ğ•œ] F) â†’L[ğ•œ] eE' â†’L[ğ•œ] eF) L
  let R := fun q : eP Ã— eG => (ef â‹†[eL, eÎ¼] eg q.1) q.2
  have R_contdiff : ContDiffOn ğ•œ n R ((isoP â»Â¹' s) Ã—Ë¢ univ) := by
    have hek : IsCompact (isoG â»Â¹' k) := isoG.toHomeomorph.isClosedEmbedding.isCompact_preimage hk
    have hes : IsOpen (isoP â»Â¹' s) := isoP.continuous.isOpen_preimage _ hs
    refine contDiffOn_convolution_right_with_param_aux eL hes hek ?_ ?_ ?_
    Â· intro p x hp hx
      simp only [eg,
        ContinuousLinearEquiv.map_eq_zero_iff]
      exact hgs _ _ hp hx
    Â· exact (locallyIntegrable_map_homeomorph isoG.symm.toHomeomorph).2 hf
    Â· apply isoE'.symm.contDiff.comp_contDiffOn
      apply hg.comp (isoP.prodCongr isoG).contDiff.contDiffOn
      rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
      simpa only [mem_preimage, ContinuousLinearEquiv.prodCongr_apply, prodMk_mem_set_prod_eq,
        mem_univ, and_true] using hp
  have A : ContDiffOn ğ•œ n (isoF âˆ˜ R âˆ˜ (isoP.prodCongr isoG).symm) (s Ã—Ë¢ univ) := by
    apply isoF.contDiff.comp_contDiffOn
    apply R_contdiff.comp (ContinuousLinearEquiv.contDiff _).contDiffOn
    rintro âŸ¨p, xâŸ© âŸ¨hp, -âŸ©
    simpa only [mem_preimage, mem_prod, mem_univ, and_true, ContinuousLinearEquiv.prodCongr_symm,
      ContinuousLinearEquiv.prodCongr_apply, ContinuousLinearEquiv.apply_symm_apply] using hp
  have : isoF âˆ˜ R âˆ˜ (isoP.prodCongr isoG).symm = fun q : P Ã— G => (f â‹†[L, Î¼] g q.1) q.2 := by
    apply funext
    rintro âŸ¨p, xâŸ©
    simp only [(Â· âˆ˜ Â·), ContinuousLinearEquiv.prodCongr_symm, ContinuousLinearEquiv.prodCongr_apply]
    simp only [R, convolution]
    rw [IsClosedEmbedding.integral_map, â† isoF.integral_comp_comm]
    Â· rfl
    Â· exact isoG.symm.toHomeomorph.isClosedEmbedding
  simp_rw [this] at A
  exact A

/-- The convolution `f * g` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with an additional `C^n` function. -/
theorem contDiffOn_convolution_right_with_param_comp {n : â„•âˆ} (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {s : Set P}
    {v : P â†’ G} (hv : ContDiffOn ğ•œ n v s) {f : G â†’ E} {g : P â†’ G â†’ E'} {k : Set G} (hs : IsOpen s)
    (hk : IsCompact k) (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0) (hf : LocallyIntegrable f Î¼)
    (hg : ContDiffOn ğ•œ n â†¿g (s Ã—Ë¢ univ)) : ContDiffOn ğ•œ n (fun x => (f â‹†[L, Î¼] g x) (v x)) s := by
  apply (contDiffOn_convolution_right_with_param L hs hk hgs hf hg).comp (contDiffOn_id.prodMk hv)
  intro x hx
  simp only [hx, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`). -/
theorem contDiffOn_convolution_left_with_param [Î¼.IsAddLeftInvariant] [Î¼.IsNegInvariant]
    (L : E' â†’L[ğ•œ] E â†’L[ğ•œ] F) {f : G â†’ E} {n : â„•âˆ} {g : P â†’ G â†’ E'} {s : Set P} {k : Set G}
    (hs : IsOpen s) (hk : IsCompact k) (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0)
    (hf : LocallyIntegrable f Î¼) (hg : ContDiffOn ğ•œ n â†¿g (s Ã—Ë¢ univ)) :
    ContDiffOn ğ•œ n (fun q : P Ã— G => (g q.1 â‹†[L, Î¼] f) q.2) (s Ã—Ë¢ univ) := by
  simpa only [convolution_flip] using contDiffOn_convolution_right_with_param L.flip hs hk hgs hf hg

/-- The convolution `g * f` is `C^n` when `f` is locally integrable and `g` is `C^n` and compactly
supported. Version where `g` depends on an additional parameter in an open subset `s` of a
parameter space `P` (and the compact support `k` is independent of the parameter in `s`),
given in terms of composition with additional `C^n` functions. -/
theorem contDiffOn_convolution_left_with_param_comp [Î¼.IsAddLeftInvariant] [Î¼.IsNegInvariant]
    (L : E' â†’L[ğ•œ] E â†’L[ğ•œ] F) {s : Set P} {n : â„•âˆ} {v : P â†’ G} (hv : ContDiffOn ğ•œ n v s) {f : G â†’ E}
    {g : P â†’ G â†’ E'} {k : Set G} (hs : IsOpen s) (hk : IsCompact k)
    (hgs : âˆ€ p, âˆ€ x, p âˆˆ s â†’ x âˆ‰ k â†’ g p x = 0) (hf : LocallyIntegrable f Î¼)
    (hg : ContDiffOn ğ•œ n â†¿g (s Ã—Ë¢ univ)) : ContDiffOn ğ•œ n (fun x => (g x â‹†[L, Î¼] f) (v x)) s := by
  apply (contDiffOn_convolution_left_with_param L hs hk hgs hf hg).comp (contDiffOn_id.prodMk hv)
  intro x hx
  simp only [hx, prodMk_mem_set_prod_eq, mem_univ, and_self_iff, _root_.id]

theorem _root_.HasCompactSupport.contDiff_convolution_right {n : â„•âˆ} (hcg : HasCompactSupport g)
    (hf : LocallyIntegrable f Î¼) (hg : ContDiff ğ•œ n g) : ContDiff ğ•œ n (f â‹†[L, Î¼] g) := by
  rcases exists_compact_iff_hasCompactSupport.2 hcg with âŸ¨k, hk, h'kâŸ©
  rw [â† contDiffOn_univ]
  exact contDiffOn_convolution_right_with_param_comp L contDiffOn_id isOpen_univ hk
    (fun p x _ hx => h'k x hx) hf (hg.comp contDiff_snd).contDiffOn

theorem _root_.HasCompactSupport.contDiff_convolution_left [Î¼.IsAddLeftInvariant] [Î¼.IsNegInvariant]
    {n : â„•âˆ} (hcf : HasCompactSupport f) (hf : ContDiff ğ•œ n f) (hg : LocallyIntegrable g Î¼) :
    ContDiff ğ•œ n (f â‹†[L, Î¼] g) := by
  rw [â† convolution_flip]
  exact hcf.contDiff_convolution_right L.flip hg hf

end WithParam

section Nonneg

variable [NormedSpace â„ E] [NormedSpace â„ E'] [NormedSpace â„ F]

/-- The forward convolution of two functions `f` and `g` on `â„`, with respect to a continuous
bilinear map `L` and measure `Î½`. It is defined to be the function mapping `x` to
`âˆ« t in 0..x, L (f t) (g (x - t)) âˆ‚Î½` if `0 < x`, and 0 otherwise. -/
noncomputable def posConvolution (f : â„ â†’ E) (g : â„ â†’ E') (L : E â†’L[â„] E' â†’L[â„] F)
    (Î½ : Measure â„ := by volume_tac) : â„ â†’ F :=
  indicator (Ioi (0 : â„)) fun x => âˆ« t in 0..x, L (f t) (g (x - t)) âˆ‚Î½

theorem posConvolution_eq_convolution_indicator (f : â„ â†’ E) (g : â„ â†’ E') (L : E â†’L[â„] E' â†’L[â„] F)
    (Î½ : Measure â„ := by volume_tac) [NoAtoms Î½] :
    posConvolution f g L Î½ = convolution (indicator (Ioi 0) f) (indicator (Ioi 0) g) L Î½ := by
  ext1 x
  rw [convolution, posConvolution, indicator]
  split_ifs with h
  Â· rw [intervalIntegral.integral_of_le (le_of_lt h), integral_Ioc_eq_integral_Ioo, â†
      integral_indicator (measurableSet_Ioo : MeasurableSet (Ioo 0 x))]
    congr 1 with t : 1
    have : t â‰¤ 0 âˆ¨ t âˆˆ Ioo 0 x âˆ¨ x â‰¤ t := by
      rcases le_or_gt t 0 with (h | h)
      Â· exact Or.inl h
      Â· rcases lt_or_ge t x with (h' | h')
        exacts [Or.inr (Or.inl âŸ¨h, h'âŸ©), Or.inr (Or.inr h')]
    rcases this with (ht | ht | ht)
    Â· rw [indicator_of_notMem (notMem_Ioo_of_le ht), indicator_of_notMem (notMem_Ioi.mpr ht),
        map_zero, ContinuousLinearMap.zero_apply]
    Â· rw [indicator_of_mem ht, indicator_of_mem (mem_Ioi.mpr ht.1),
          indicator_of_mem (mem_Ioi.mpr <| sub_pos.mpr ht.2)]
    Â· rw [indicator_of_notMem (notMem_Ioo_of_ge ht),
          indicator_of_notMem (notMem_Ioi.mpr (sub_nonpos_of_le ht)), map_zero]
  Â· convert (integral_zero â„ F).symm with t
    by_cases ht : 0 < t
    Â· rw [indicator_of_notMem (_ : x - t âˆ‰ Ioi 0), map_zero]
      rw [notMem_Ioi] at h âŠ¢
      exact sub_nonpos.mpr (h.trans ht.le)
    Â· rw [indicator_of_notMem (mem_Ioi.not.mpr ht), map_zero, ContinuousLinearMap.zero_apply]

theorem integrable_posConvolution {f : â„ â†’ E} {g : â„ â†’ E'} {Î¼ Î½ : Measure â„} [SFinite Î¼]
    [SFinite Î½] [IsAddRightInvariant Î¼] [NoAtoms Î½] (hf : IntegrableOn f (Ioi 0) Î½)
    (hg : IntegrableOn g (Ioi 0) Î¼) (L : E â†’L[â„] E' â†’L[â„] F) :
    Integrable (posConvolution f g L Î½) Î¼ := by
  rw [â† integrable_indicator_iff (measurableSet_Ioi : MeasurableSet (Ioi (0 : â„)))] at hf hg
  rw [posConvolution_eq_convolution_indicator f g L Î½]
  exact (hf.convolution_integrand L hg).integral_prod_left

/-- The integral over `Ioi 0` of a forward convolution of two functions is equal to the product
of their integrals over this set. (Compare `integral_convolution` for the two-sided convolution.) -/
theorem integral_posConvolution [CompleteSpace E] [CompleteSpace E'] [CompleteSpace F]
    {Î¼ Î½ : Measure â„}
    [SFinite Î¼] [SFinite Î½] [IsAddRightInvariant Î¼] [NoAtoms Î½] {f : â„ â†’ E} {g : â„ â†’ E'}
    (hf : IntegrableOn f (Ioi 0) Î½) (hg : IntegrableOn g (Ioi 0) Î¼) (L : E â†’L[â„] E' â†’L[â„] F) :
    âˆ« x : â„ in Ioi 0, âˆ« t : â„ in 0..x, L (f t) (g (x - t)) âˆ‚Î½ âˆ‚Î¼ =
      L (âˆ« x : â„ in Ioi 0, f x âˆ‚Î½) (âˆ« x : â„ in Ioi 0, g x âˆ‚Î¼) := by
  rw [â† integrable_indicator_iff measurableSet_Ioi] at hf hg
  simp_rw [â† integral_indicator measurableSet_Ioi]
  convert integral_convolution L hf hg using 4 with x
  apply posConvolution_eq_convolution_indicator

end Nonneg
end MeasureTheory
