/-
Copyright (c) 2024 Lawrence Wu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Lawrence Wu
-/
import Mathlib.Analysis.Fourier.Inversion

/-!
# Mellin inversion formula

We derive the Mellin inversion formula as a consequence of the Fourier inversion formula.

## Main results
- `mellin_inversion`: The inverse Mellin transform of the Mellin transform applied to `x > 0` is x.
-/

open Real Complex Set MeasureTheory

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

open scoped FourierTransform

private theorem rexp_neg_deriv_aux :
    âˆ€ x âˆˆ univ, HasDerivWithinAt (rexp âˆ˜ Neg.neg) (-rexp (-x)) univ x :=
  fun x _ â†¦ mul_neg_one (rexp (-x)) â–¸
    ((Real.hasDerivAt_exp (-x)).comp x (hasDerivAt_neg x)).hasDerivWithinAt

private theorem rexp_neg_image_aux : rexp âˆ˜ Neg.neg '' univ = Ioi 0 := by
  rw [Set.image_comp, Set.image_univ_of_surjective neg_surjective, Set.image_univ, Real.range_exp]

private theorem rexp_neg_injOn_aux : univ.InjOn (rexp âˆ˜ Neg.neg) :=
  Real.exp_injective.injOn.comp neg_injective.injOn (univ.mapsTo_univ _)

private theorem rexp_cexp_aux (x : â„) (s : â„‚) (f : E) :
    rexp (-x) â€¢ cexp (-â†‘x) ^ (s - 1) â€¢ f = cexp (-s * â†‘x) â€¢ f := by
  show (rexp (-x) : â„‚) â€¢ _ = _ â€¢ f
  rw [â† smul_assoc, smul_eq_mul]
  push_cast
  conv in cexp _ * _ => lhs; rw [â† cpow_one (cexp _)]
  rw [â† cpow_add _ _ (Complex.exp_ne_zero _), cpow_def_of_ne_zero (Complex.exp_ne_zero _),
    Complex.log_exp (by norm_num; exact pi_pos) (by simpa using pi_nonneg)]
  ring_nf

theorem mellin_eq_fourierIntegral (f : â„ â†’ E) {s : â„‚} :
    mellin f s = ğ“• (fun (u : â„) â†¦ (Real.exp (-s.re * u) â€¢ f (Real.exp (-u)))) (s.im / (2 * Ï€)) :=
  calc
    mellin f s
      = âˆ« (u : â„), Complex.exp (-s * u) â€¢ f (Real.exp (-u)) := by
      rw [mellin, â† rexp_neg_image_aux, integral_image_eq_integral_abs_deriv_smul
        MeasurableSet.univ rexp_neg_deriv_aux rexp_neg_injOn_aux]
      simp [rexp_cexp_aux]
    _ = âˆ« (u : â„), Complex.exp (â†‘(-2 * Ï€ * (u * (s.im / (2 * Ï€)))) * I) â€¢
        (Real.exp (-s.re * u) â€¢ f (Real.exp (-u))) := by
      congr
      ext u
      trans Complex.exp (-s.im * u * I) â€¢ (Real.exp (-s.re * u) â€¢ f (Real.exp (-u)))
      Â· conv => lhs; rw [â† re_add_im s]
        rw [neg_add, add_mul, Complex.exp_add, mul_comm, â† smul_eq_mul, smul_assoc]
        norm_cast
        push_cast
        ring_nf
      congr
      rw [mul_comm (-s.im : â„‚) (u : â„‚), mul_comm (-2 * Ï€)]
      have : 2 * (Ï€ : â„‚) â‰  0 := by norm_num; exact pi_ne_zero
      field_simp
    _ = ğ“• (fun (u : â„) â†¦ (Real.exp (-s.re * u) â€¢ f (Real.exp (-u)))) (s.im / (2 * Ï€)) := by
      simp [fourierIntegral_eq']

theorem mellinInv_eq_fourierIntegralInv (Ïƒ : â„) (f : â„‚ â†’ E) {x : â„} (hx : 0 < x) :
    mellinInv Ïƒ f x =
    (x : â„‚) ^ (-Ïƒ : â„‚) â€¢ ğ“•â» (fun (y : â„) â†¦ f (Ïƒ + 2 * Ï€ * y * I)) (-Real.log x) := calc
  mellinInv Ïƒ f x
    = (x : â„‚) ^ (-Ïƒ : â„‚) â€¢
      (âˆ« (y : â„), Complex.exp (2 * Ï€ * (y * (-Real.log x)) * I) â€¢ f (Ïƒ + 2 * Ï€ * y * I)) := by
    rw [mellinInv, one_div, â† abs_of_pos (show 0 < (2 * Ï€)â»Â¹ by norm_num; exact pi_pos)]
    have hx0 : (x : â„‚) â‰  0 := ofReal_ne_zero.mpr (ne_of_gt hx)
    simp_rw [neg_add, cpow_add _ _ hx0, mul_smul, integral_smul]
    rw [smul_comm, â† Measure.integral_comp_mul_left]
    congr! 3
    rw [cpow_def_of_ne_zero hx0, â† Complex.ofReal_log hx.le]
    push_cast
    ring_nf
  _ = (x : â„‚) ^ (-Ïƒ : â„‚) â€¢ ğ“•â» (fun (y : â„) â†¦ f (Ïƒ + 2 * Ï€ * y * I)) (-Real.log x) := by
    simp [fourierIntegralInv_eq']

variable [CompleteSpace E]

/-- The inverse Mellin transform of the Mellin transform applied to `x > 0` is x. -/
theorem mellin_inversion (Ïƒ : â„) (f : â„ â†’ E) {x : â„} (hx : 0 < x) (hf : MellinConvergent f Ïƒ)
    (hFf : VerticalIntegrable (mellin f) Ïƒ) (hfx : ContinuousAt f x) :
    mellinInv Ïƒ (mellin f) x = f x := by
  let g := fun (u : â„) => Real.exp (-Ïƒ * u) â€¢ f (Real.exp (-u))
  replace hf : Integrable g := by
    rw [MellinConvergent, â† rexp_neg_image_aux, integrableOn_image_iff_integrableOn_abs_deriv_smul
      MeasurableSet.univ rexp_neg_deriv_aux rexp_neg_injOn_aux] at hf
    replace hf : Integrable fun (x : â„) â†¦ cexp (-â†‘Ïƒ * â†‘x) â€¢ f (rexp (-x)) := by
      simpa [rexp_cexp_aux] using hf
    norm_cast at hf
  replace hFf : Integrable (ğ“• g) := by
    have h2Ï€ : 2 * Ï€ â‰  0 := by norm_num; exact pi_ne_zero
    have : Integrable (ğ“• (fun u â†¦ rexp (-(Ïƒ * u)) â€¢ f (rexp (-u)))) := by
      simpa [mellin_eq_fourierIntegral, mul_div_cancel_rightâ‚€ _ h2Ï€] using hFf.comp_mul_right' h2Ï€
    simp_rw [neg_mul_eq_neg_mul] at this
    exact this
  replace hfx : ContinuousAt g (-Real.log x) := by
    refine ContinuousAt.smul (by fun_prop) (ContinuousAt.comp ?_ (by fun_prop))
    simpa [Real.exp_log hx] using hfx
  calc
    mellinInv Ïƒ (mellin f) x
      = mellinInv Ïƒ (fun s â†¦ ğ“• g (s.im / (2 * Ï€))) x := by
      simp [g, mellinInv, mellin_eq_fourierIntegral]
    _ = (x : â„‚) ^ (-Ïƒ : â„‚) â€¢ g (-Real.log x) := by
      rw [mellinInv_eq_fourierIntegralInv _ _ hx, â† hf.fourier_inversion hFf hfx]
      simp [mul_div_cancel_leftâ‚€ _ (show 2 * Ï€ â‰  0 by norm_num; exact pi_ne_zero)]
    _ = (x : â„‚) ^ (-Ïƒ : â„‚) â€¢ rexp (Ïƒ * Real.log x) â€¢ f (rexp (Real.log x)) := by simp [g]
    _ = f x := by
      norm_cast
      rw [mul_comm Ïƒ, â† rpow_def_of_pos hx, Real.exp_log hx, â† Complex.ofReal_cpow hx.le]
      norm_cast
      rw [â† smul_assoc, smul_eq_mul, Real.rpow_neg hx.le,
        inv_mul_cancel (ne_of_gt (rpow_pos_of_pos hx Ïƒ)), one_smul]
