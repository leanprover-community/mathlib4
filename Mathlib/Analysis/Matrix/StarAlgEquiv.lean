/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Algebra.Algebra.Subalgebra.Basic
public import Mathlib.Algebra.Order.Ring.Star
public import Mathlib.Algebra.Star.UnitaryStarAlgAut
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.LinearAlgebra.Matrix.GeneralLinearGroup.Defs
public import Mathlib.LinearAlgebra.UnitaryGroup
public import Mathlib.Analysis.LocallyConvex.SeparatingDual
public import Mathlib.Topology.Algebra.Algebra.Equiv

import Mathlib.Algebra.Central.Basic
import Mathlib.Algebra.Central.Matrix
import Mathlib.Analysis.InnerProductSpace.StarOrder
import Mathlib.Analysis.Matrix.Order
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.LinearAlgebra.GeneralLinearGroup.AlgEquiv

/-!

# Star algebra equivalences on matrices are unitarily inner

-/

open scoped ComplexOrder

variable {ğ•œ A B F : Type*} [RCLike ğ•œ] [Ring A] [Algebra ğ•œ A] [StarRing A] [StarModule ğ•œ A]
  [PartialOrder A] [StarOrderedRing A] [Star B] [FunLike F B A] [StarHomClass F B A]

/-- Given â‹†-homomorphisms `f` and `g`, where the centralizer of the range of `f` is trivial,
`f` and `g` differ by a unit iff they differ by a unitary. -/
public theorem StarHom.coe_eq_units_conjugate_iff_coe_eq_unitary_conjugate
    (f g : F) (hf : Subalgebra.centralizer ğ•œ (Set.range f) = âŠ¥) :
    (âˆƒ (x : AË£), â‡‘g = fun b â†¦ â†‘x * f b * â†‘xâ»Â¹) â†”
    âˆƒ (u : unitary A), â‡‘g = fun b â†¦ u * f b * (star u : A) := by
  refine âŸ¨fun âŸ¨y, hyâŸ© â†¦ ?_, fun âŸ¨u, huâŸ© â†¦ âŸ¨Unitary.toUnits u, huâŸ©âŸ©
  nontriviality A
  have (x : B) : star (g x) = g (star x) := map_star _ _ |>.symm
  simp_rw [hy, star_mul] at this
  replace this (x : B) : star (y : A) * (y : A) * star (f x) * â†‘yâ»Â¹ = star (f x) * star â†‘y := by
    simp_rw [mul_assoc, â† mul_assoc (y : A), â† map_star f, â† this, â† mul_assoc,
      â† star_mul, Units.inv_mul, mul_one, map_star, star_mul]
  replace this (x : B) : Commute (f x) (star â†‘y * y) := by
    specialize this (star x)
    simp only [map_star, star_star] at this
    simp_rw [Commute, SemiconjBy, â† mul_assoc, â† this, mul_assoc, Units.inv_mul, mul_one]
  replace this (x : A) (hx : x âˆˆ Set.range f) : Commute x (star â†‘y * y) :=
    have âŸ¨a, haâŸ© := hx
    ha â–¸ this _
  simp_rw [Commute, SemiconjBy, â† Subalgebra.mem_centralizer_iff ğ•œ, hf] at this
  obtain âŸ¨Î±, hÎ±âŸ© := this
  simp only [AlgHom.toRingHom_eq_coe, Algebra.toRingHom_ofId, Algebra.algebraMap_eq_smul_one] at hÎ±
  have this : IsUnit (star (y : A) * y) := isUnit_iff_exists.mpr
    âŸ¨yâ»Â¹ * star ((yâ»Â¹ : AË£) : A), by simp [â† mul_assoc, â† star_mul, mul_assoc _ _ (star (y : A))]âŸ©
  have thisÎ± : Î± = RCLike.re Î± := by
    have this10 := by simpa [IsSelfAdjoint, â† hÎ±] using IsSelfAdjoint.star_mul_self (y : A)
    rwa [(smul_left_injective _ one_ne_zero).eq_iff, RCLike.conj_eq_iff_re, eq_comm] at this10
  have thisÎ±' : Î± â‰  0 := fun h â†¦ by simp [h, â† hÎ±] at this
  have this2 : 0 â‰¤ Î± := by
    rw [thisÎ±, RCLike.ofReal_nonneg]
    by_contra! this2
    exact one_ne_zero <| (IsUnit.mk0 _ thisÎ±').smul_eq_zero.mp (thisÎ±.symm â–¸ le_antisymm
      (smul_zero (RCLike.re Î± : ğ•œ) (A := A) â–¸ smul_le_smul_of_nonpos_left zero_le_one
        (by simpa using this2.le))
      (thisÎ± â–¸ hÎ± â–¸ star_mul_self_nonneg (y : A)))
  replace this2 := RCLike.ofReal_pos.mp <| thisÎ± â–¸ (lt_of_le_of_ne' this2 thisÎ±')
  have thisU : y * star (y : A) = Î± â€¢ (1 : A) := by simp [â† Units.mul_left_inj y, mul_assoc, â† hÎ±]
  set Î±a := (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ)
  have isU : Î±a â€¢ (y : A) âˆˆ unitary A := by
    simp_rw [Unitary.mem_iff, star_smul, RCLike.star_def, smul_mul_smul, Î±a, RCLike.conj_ofReal,
      â† RCLike.ofReal_mul, â† Real.rpow_add this2, â† hÎ±, thisU]
    norm_num
    nth_rw 2 [thisÎ±]
    simp [smul_smul, â† RCLike.ofReal_mul, â† Real.rpow_add_one (NeZero.of_pos this2).out]
  set U : unitary A := âŸ¨_, isUâŸ©
  have Uinv : ((((RCLike.re Î± : â„) ^ ((1 / 2 : â„)) : â„) : ğ•œ) â€¢ ((yâ»Â¹ : AË£) : A)) =
      (Uâ»Â¹ : unitary A) := by
    rw [â† neg_neg (1 / 2 : â„), Real.rpow_neg_eq_inv_rpow, Real.inv_rpow this2.le]
    set Î±' : ğ•œË£ := Units.mk0 Î±a <| by
      simp only [one_div, ne_eq, map_eq_zero, Î±a]
      rw [Real.rpow_eq_zero this2.le (by simp)]
      exact ne_of_gt this2
    rw [RCLike.ofReal_inv, show â†‘(RCLike.re Î± ^ (-(1 / 2 : â„))) = Î±a by rfl]
    have := by simpa only [Units.val_smul] using congr(($(Units.smul_inv Î±' y) : A))
    rw [show Î±' â€¢ y = Unitary.toUnits U by ext; simp [Î±', Î±a, U]] at this
    rw [show ((Uâ»Â¹ : unitary A) : A) = ((Unitary.toUnits U)â»Â¹ : AË£) by rfl, this]
    congr
  use U
  rw [â† Unitary.coe_star, Unitary.star_eq_inv, â† Uinv]
  simp [Î±a, Algebra.smul_mul_assoc, U, smul_smul, â† RCLike.ofReal_mul, â† Real.rpow_add this2, hy]

section
open Matrix
variable {n : Type*} [Fintype n]

-- TODO: wait for other PR
proof_wanted Matrix.AlgEquiv.coe_eq_conjugate {m : Type*} [Fintype m] [DecidableEq m]
    [DecidableEq n] {K : Type*} [Field K] (f : Matrix m m K â‰ƒâ‚[K] Matrix n n K) :
    âˆƒ (U : Matrix n m K) (V : Matrix m n K) (hUV : U * V = 1), â‡‘f = fun x â†¦ U * x * V

-- TODO: change `Matrix` to any central and simple finite algebra
-- and then also add the `AlgHom` version of this
-- and then also move this file outside of the `Matrix` folder
public theorem AlgEquiv.eq_mulSemiringActionToAlgEquiv_conjAct [DecidableEq n] {K : Type*} [Field K]
    (f : Matrix n n K â‰ƒâ‚[K] Matrix n n K) :
    âˆƒ U : GL n K, f = MulSemiringAction.toAlgEquiv K (G := ConjAct (GL n K)) _ U := by
  obtain âŸ¨U, hUâŸ© := ((toLinAlgEquiv'.symm.trans f).trans toLinAlgEquiv').eq_linearEquivConjAlgEquiv
  use GeneralLinearGroup.toLin.symm (.ofLinearEquiv U)
  ext1 x
  have := by simpa using congr((toLinAlgEquiv'.trans $hU).trans toLinAlgEquiv'.symm x)
  simp only [this, LinearMap.toMatrixAlgEquiv', toLinAlgEquiv', AlgEquiv.ofLinearEquiv_symm,
    LinearMap.toMatrix'_symm, AlgEquiv.ofLinearEquiv_apply, LinearEquiv.conjAlgEquiv_apply,
    LinearMap.toMatrix'_comp, LinearMap.toMatrix'_toLin', â† mul_assoc,
    MulSemiringAction.toAlgEquiv_apply, ConjAct.units_smul_def, coe_units_inv]
  congr
  refine (inv_eq_right_inv ?_).symm
  simp [ConjAct.ofConjAct, GeneralLinearGroup.toLin, LinearMap.GeneralLinearGroup.ofLinearEquiv,
    LinearMap.toMatrixAlgEquiv', â† LinearMap.toMatrix'_comp]

open ComplexOrder MatrixOrder

-- TODO: change `Matrix` to any central, simple and star-ordered finite algebra
-- and then also add the `StarAlgHom` version of this
public theorem StarAlgEquiv.eq_unitaryConjStarAlgAut [DecidableEq n]
    (f : Matrix n n ğ•œ â‰ƒâ‹†â‚[ğ•œ] Matrix n n ğ•œ) :
    âˆƒ U : unitaryGroup n ğ•œ, f = Unitary.conjStarAlgAut ğ•œ _ U := by
  obtain âŸ¨g, hgâŸ© := f.toAlgEquiv.eq_mulSemiringActionToAlgEquiv_conjAct
  have := StarHom.coe_eq_units_conjugate_iff_coe_eq_unitary_conjugate (ğ•œ := ğ•œ) 1 f (by simp)
  obtain âŸ¨U, hUâŸ© := this.mp âŸ¨g, congr($hg)âŸ©
  exact âŸ¨U, StarAlgEquiv.ext <| congrFun hUâŸ©

end

theorem ContinuousLinearEquiv.eq_comp_toContinuousLinearMap_symm
    {Râ‚ Râ‚‚ Râ‚ƒ Mâ‚ Mâ‚‚ Mâ‚ƒ : Type*} [Semiring Râ‚] [Semiring Râ‚‚] [Semiring Râ‚ƒ] [AddCommMonoid Mâ‚]
    [AddCommMonoid Mâ‚‚] [AddCommMonoid Mâ‚ƒ] {module_Mâ‚ : Module Râ‚ Mâ‚} {module_Mâ‚‚ : Module Râ‚‚ Mâ‚‚}
    {module_Mâ‚ƒ : Module Râ‚ƒ Mâ‚ƒ} [TopologicalSpace Mâ‚] [TopologicalSpace Mâ‚‚] [TopologicalSpace Mâ‚ƒ]
    {Ïƒâ‚â‚‚ : Râ‚ â†’+* Râ‚‚} {Ïƒâ‚‚â‚ : Râ‚‚ â†’+* Râ‚} {Ïƒâ‚â‚ƒ : Râ‚ â†’+* Râ‚ƒ}
    {Ïƒâ‚‚â‚ƒ : Râ‚‚ â†’+* Râ‚ƒ} {reâ‚â‚‚ : RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚} {reâ‚‚â‚ : RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚}
    [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ] {eâ‚â‚‚ : Mâ‚ â‰ƒSL[Ïƒâ‚â‚‚] Mâ‚‚} [RingHomCompTriple Ïƒâ‚‚â‚ Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ]
    (f : Mâ‚‚ â†’SL[Ïƒâ‚‚â‚ƒ] Mâ‚ƒ) (g : Mâ‚ â†’SL[Ïƒâ‚â‚ƒ] Mâ‚ƒ) :
    f = g.comp eâ‚â‚‚.symm.toContinuousLinearMap â†” f.comp eâ‚â‚‚.toContinuousLinearMap = g := by
  aesop

/-- Interpret a `ContinuousAlgHom` as a `ContinuousLinearMap`. -/
def ContinuousAlgHom.toContinuousLinearMap {R A B : Type*} [CommSemiring R] [Semiring A]
    [TopologicalSpace A] [Semiring B] [TopologicalSpace B] [Algebra R A] [Algebra R B]
    (e : A â†’A[R] B) : A â†’L[R] B :=
  { e with map_smul' := by simp }

/-- Interpret a `ContinuousAlgEquiv` as a `ContinuousLinearMap`. -/
abbrev ContinuousAlgEquiv.toContinuousLinearMap {R A B : Type*} [CommSemiring R] [Semiring A]
    [TopologicalSpace A] [Semiring B] [TopologicalSpace B] [Algebra R A] [Algebra R B]
    (e : A â‰ƒA[R] B) : A â†’L[R] B := e.toContinuousAlgHom.toContinuousLinearMap

@[simp] theorem ContinuousAlgEquiv.coe_toContinuousLinearMap {R A B : Type*} [CommSemiring R]
    [Semiring A] [TopologicalSpace A] [Semiring B] [TopologicalSpace B] [Algebra R A] [Algebra R B]
    (e : A â‰ƒA[R] B) : â‡‘e.toContinuousLinearMap = e := rfl

open ContinuousLinearMap

theorem ContinuousAlgEquiv.coe_eq_conjugate {ğ•œ V W : Type*} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup V] [NormedAddCommGroup W] [NormedSpace ğ•œ V] [NormedSpace ğ•œ W]
    [SeparatingDual ğ•œ V] [SeparatingDual ğ•œ W] [CompleteSpace V] [CompleteSpace W]
    (f : (V â†’L[ğ•œ] V) â‰ƒA[ğ•œ] (W â†’L[ğ•œ] W)) :
    âˆƒ U : V â‰ƒL[ğ•œ] W, â‡‘f = fun x â†¦ U âˆ˜L x âˆ˜L U.symm := by
  /- basically copied the same proof as the linear one -/
  by_cases! hV : Subsingleton V
  Â· by_cases! hV : Subsingleton W
    Â· use { toLinearEquiv := 0, continuous_invFun := by fun_prop }
      exact Subsingleton.allEq _ _
    simpa using congr(f $(Subsingleton.allEq 0 1))
  simp_rw [funext_iff, â† comp_assoc, ContinuousLinearEquiv.eq_comp_toContinuousLinearMap_symm]
  obtain âŸ¨u, huâŸ© := exists_ne (0 : V)
  obtain âŸ¨v, huvâŸ© := SeparatingDual.exists_ne_zero (R := ğ•œ) hu
  obtain âŸ¨z, hzâŸ© : âˆƒ z : W, Â¬ f (smulRight v u) z = (0 : W â†’L[ğ•œ] W) z := by
    rw [â† not_forall, â† ContinuousLinearMap.ext_iff, EmbeddingLike.map_eq_zero_iff,
      ContinuousLinearMap.ext_iff]
    exact not_forall.mpr âŸ¨u, huv.isUnit.smul_eq_zero.not.mpr huâŸ©
  set T := ContinuousLinearMap.apply' _ (.id ğ•œ) z âˆ˜L f.toContinuousLinearMap âˆ˜L smulRightL ğ•œ _ _ v
  have hT x : T x = f (smulRight v x) z := rfl
  have this A x : T (A x) = f A (T x) := by
    simp only [hT, â† ContinuousLinearMap.mul_apply, â† map_mul]
    congr; ext; simp
  have surj : Function.Surjective T := fun w â†¦ by
    obtain âŸ¨d, hdâŸ© := SeparatingDual.exists_eq_one (R := ğ•œ) hz
    exact âŸ¨f.symm (smulRight d w) u, by simp [T, this, hd]âŸ©
  have inj : Function.Injective T := fun x y hxy â†¦ by
    have h_smul : smulRightL ğ•œ _ _ v x = smulRightL ğ•œ _ _ v y := by
      apply f.injective <| ContinuousLinearMap.ext fun z â†¦ ?_
      obtain âŸ¨w, rflâŸ© := surj z
      simp [â† this, hxy]
    simpa [huv.isUnit.smul_left_cancel] using congr((fun f â†¦ f u) $h_smul)
  exact âŸ¨.ofBijective T ((LinearMapClass.ker_eq_bot _).mpr inj)
    (LinearMap.range_eq_top_of_surjective T surj), fun A â†¦ (ContinuousLinearMap.ext <| this A).symmâŸ©

/-- Interpret a â‹†-algebra equivalence as a continuous algebra equivalence when it is continuous. -/
abbrev StarAlgEquiv.toContinuousAlgEquiv {R A B : Type*} [CommSemiring R] [Semiring A]
    [TopologicalSpace A] [Semiring B] [TopologicalSpace B] [Algebra R A] [Algebra R B]
    [Star R] [Star A] [Star B] (e : A â‰ƒâ‹†â‚[R] B) (he : Continuous e) (he' : Continuous e.symm) :
    A â‰ƒA[R] B :=
  { e.toAlgEquiv with continuous_toFun := he, continuous_invFun := he' }

@[simp] theorem StarAlgEquiv.coe_toContinuousAlgEquiv {R A B : Type*} [CommSemiring R] [Semiring A]
    [TopologicalSpace A] [Semiring B] [TopologicalSpace B] [Algebra R A] [Algebra R B]
    [Star R] [Star A] [Star B] (e : A â‰ƒâ‹†â‚[R] B) (he : Continuous e) (he' : Continuous e.symm) :
    â‡‘(e.toContinuousAlgEquiv he he') = e := rfl

theorem StarAlgEquiv.eq_unitaryConjStarAlgAut_symm_unitaryLinearIsometryEquiv
    {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„‚ V] [CompleteSpace V]
    (f : (V â†’L[â„‚] V) â‰ƒâ‹†â‚[â„‚] (V â†’L[â„‚] V)) (hf : Continuous f) (hf' : Continuous f.symm) :
    âˆƒ U : V â‰ƒâ‚—áµ¢[â„‚] V, f = Unitary.conjStarAlgAut â„‚ _
      ((Unitary.linearIsometryEquiv (ğ•œ := â„‚)).symm U) := by
  obtain âŸ¨g, hgâŸ© := f.toContinuousAlgEquiv hf hf' |>.coe_eq_conjugate
  obtain âŸ¨U, hUâŸ© := StarHom.coe_eq_units_conjugate_iff_coe_eq_unitary_conjugate (ğ•œ := â„‚)
    1 f (by simp) |>.mp âŸ¨g.toUnit, congr($hg)âŸ©
  exact âŸ¨Unitary.linearIsometryEquiv U, StarAlgEquiv.ext <| congrFun hUâŸ©

theorem ContinuousLinearEquiv.isometry_iff_adjoint_eq_symm
    {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W] (e : V â‰ƒL[ğ•œ] W) :
    Isometry e â†” adjoint e.toContinuousLinearMap = e.symm.toContinuousLinearMap := by
  simp_rw [AddMonoidHomClass.isometry_iff_norm, â† coe_coe, norm_map_iff_adjoint_comp_self]
  refine âŸ¨fun h â†¦ ContinuousLinearMap.ext fun x â†¦ by simpa using congr($h (e.symm x)), fun h â†¦ ?_âŸ©
  simp [h, one_def]

/-- can't do this inline, it times out -/
noncomputable abbrev aux_isometry
    {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W) :
    V â‰ƒL[ğ•œ] W where
  toFun := (Î±' â€¢ e.toContinuousLinearMap).toLinearMap
  invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).toLinearMap
  left_inv := by
    simp only [coe_smul, Function.leftInverse_iff_comp, funext_iff, Function.comp_apply,
      LinearMap.smul_apply, coe_coe, ContinuousLinearEquiv.coe_coe, map_smul, smul_smul, hÎ±2, id_eq]
    simp_rw [â† ContinuousLinearEquiv.coe_coe, â† comp_apply, he]
    simp [smul_smul, hÎ±]
  right_inv := by
    simp only [coe_smul, Function.rightInverse_iff_comp, funext_iff, Function.comp_apply,
      LinearMap.smul_apply, coe_coe, map_smul, ContinuousLinearEquiv.coe_coe, smul_smul, hÎ±2, id_eq]
    simp_rw [â† ContinuousLinearEquiv.coe_coe, â† comp_apply, he']
    simp [smul_smul, hÎ±]
  map_add' := by simp
  map_smul' := by simp
  continuous_toFun := (Î±' â€¢ e.toContinuousLinearMap).continuous
  continuous_invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).continuous

theorem adjoint_aux_isometry
    {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    adjoint (aux_isometry e hÎ± hÎ±2 he he').toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap.adjoint := by
  ext x
  apply ext_inner_left ğ•œ fun y â†¦ ?_
  simp [aux_isometry, adjoint_inner_right, inner_smul_left, inner_smul_right, hÎ±a]

/-- can't do this inline either, it times out -/
noncomputable abbrev aux_isometry'
    {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    V â‰ƒâ‚—áµ¢[ğ•œ] W where
  __ := aux_isometry e hÎ± hÎ±2 he he' |>.toLinearEquiv
  norm_map' _ := by
    have heI : Isometry (aux_isometry e hÎ± hÎ±2 he he') := by
      rw [ContinuousLinearEquiv.isometry_iff_adjoint_eq_symm]
      exact adjoint_aux_isometry e hÎ± hÎ±2 he he' hÎ±a
    simpa using heI.norm_map_of_map_zero (by simp) _

theorem coe_aux_isometry' {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
    [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (aux_isometry' e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap := rfl

theorem coe_symm_aux_isometry' {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
    [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (aux_isometry' e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.symm.toContinuousLinearMap =
      Î±'â»Â¹ â€¢ e.symm.toContinuousLinearMap := by
  ext y
  apply (aux_isometry' e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.injective
  simp [smul_smul, inv_mul_cancelâ‚€ (a := Î±') (by grind)]

theorem LinearMap.IsSymmetric.isSymmetric_smul_iff {V : Type*} [NormedAddCommGroup V]
    [InnerProductSpace ğ•œ V] {f : V â†’â‚—[ğ•œ] V} (hf : f.IsSymmetric) (hf' : f â‰  0) (Î± : ğ•œ) :
    (Î± â€¢ f).IsSymmetric â†” starRingEnd ğ•œ Î± = Î± := by
  refine âŸ¨fun h â†¦ ?_, hf.smulâŸ©
  simp only [IsSymmetric, smul_apply, inner_smul_left, inner_smul_right, hf _ _,
    mul_eq_mul_right_iff, forall_or_left] at h
  have : f = 0 â†” âˆ€ x y, inner ğ•œ x (f y) = inner ğ•œ x ((0 : V â†’â‚—[ğ•œ] V) y) := by
    rw [forall_comm]
    simp_rw [LinearMap.ext_iff, â† ext_iff_inner_left ğ•œ]
  simp_rw [â† (by simpa using this), hf', or_false] at h
  exact h

theorem ContinuousLinearMap.IsPositive.isPositive_smul_iff {V : Type*} [NormedAddCommGroup V]
    [InnerProductSpace ğ•œ V] {f : V â†’L[ğ•œ] V} (hf : f.IsPositive) (hf' : f â‰  0) (Î± : ğ•œ) :
    (Î± â€¢ f).IsPositive â†” 0 â‰¤ Î± := by
  simp only [IsPositive, coe_smul, hf.isSymmetric.isSymmetric_smul_iff (by exact_mod_cast hf'),
    reApplyInnerSelf, coe_smul', Pi.smul_apply, inner_smul_left]
  refine âŸ¨fun âŸ¨h1, h2âŸ© â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· have : (RCLike.re Î± : ğ•œ) = Î± := RCLike.conj_eq_iff_re.mp h1
    apply RCLike.re_nonneg_of_nonneg h1 |>.mp
    rw [h1, â† this] at h2
    simp only [RCLike.re_ofReal_mul, mul_nonneg_iff] at h2
    have := by simpa [reApplyInnerSelf] using fun x â†¦ hf.2 x
    simp only [this, and_true, forall_or_left] at h2
    obtain (h | h) := h2
    Â· exact h
    Â· rw [forall_and_left] at h
      have := hf.isSymmetric.inner_map_self_eq_zero.not.mpr (by exact_mod_cast hf')
      simp_rw [RCLike.ext_iff (K := ğ•œ), forall_and] at this
      simp only [coe_coe, map_zero] at this
      have this' := by simpa using hf.isSymmetric.im_inner_apply_self
      simp_rw [this', forall_true_iff, and_true] at this
      grind
  Â· rw [RCLike.nonneg_iff] at h
    simp_all only [ne_eq, RCLike.conj_eq_iff_im, RCLike.mul_re, RCLike.conj_re, RCLike.conj_im,
      neg_zero, zero_mul, sub_zero, true_and]
    intro x
    exact mul_nonneg h.1 (hf.2 _)

set_option maxHeartbeats 200400 in
-- :FIXME: slow proof
theorem ContinuousStarAlgEquiv.coe_eq_conjugate'
    {V W : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
    [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]
    (f : (V â†’L[ğ•œ] V) â‰ƒâ‹†â‚[ğ•œ] (W â†’L[ğ•œ] W)) (hf : Continuous f) (hf' : Continuous f.symm) :
    âˆƒ U : V â‰ƒâ‚—áµ¢[ğ•œ] W, â‡‘f =
      fun x â†¦ U.toContinuousLinearEquiv âˆ˜L x âˆ˜L U.symm.toContinuousLinearEquiv := by
  obtain âŸ¨y, hyâŸ© := f.toContinuousAlgEquiv hf hf' |>.coe_eq_conjugate
  by_cases! hV : Subsingleton V
  Â· by_cases! hV : Subsingleton W
    Â· use { toLinearEquiv := 0, norm_map' _ := by simp [Subsingleton.eq_zero] }
      exact Subsingleton.allEq _ _
    simpa using congr(f $(Subsingleton.allEq 0 1))
  have (x : V â†’L[ğ•œ] V) : adjoint (f x) = f (adjoint x) := map_star _ _ |>.symm
  simp_rw [(StarAlgEquiv.coe_toContinuousAlgEquiv _ hf _ â–¸ hy), adjoint_comp] at this
  replace this (x : V â†’L[ğ•œ] V) : adjoint y.toContinuousLinearMap âˆ˜L y.toContinuousLinearMap âˆ˜L
      adjoint x âˆ˜L y.symm.toContinuousLinearMap = adjoint x âˆ˜L
        adjoint y.toContinuousLinearMap := by
    simp_rw [â† this x, â† comp_assoc, â† adjoint_comp]
    simp
  replace this (x : V â†’L[ğ•œ] V) : Commute x (adjoint y.toContinuousLinearMap âˆ˜L y) := by
    specialize this (adjoint x)
    simp only [adjoint_adjoint] at this
    simp_rw [Commute, SemiconjBy, mul_def, â† comp_assoc, â† this, comp_assoc]
    simp
  replace this :
      (adjoint y.toContinuousLinearMap âˆ˜L y) âˆˆ Subalgebra.centralizer ğ•œ (âŠ¤ : Set (V â†’L[ğ•œ] V)) := by
    rw [Subalgebra.mem_centralizer_iff]
    exact fun _ _ â†¦ this _
  simp only [Set.top_eq_univ, Subalgebra.centralizer_univ, Algebra.IsCentral.center_eq_bot] at this
  obtain âŸ¨Î±, hÎ±âŸ© := this
  simp only [AlgHom.toRingHom_eq_coe, Algebra.toRingHom_ofId, Algebra.algebraMap_eq_smul_one] at hÎ±
  have this : IsUnit (adjoint y.toContinuousLinearMap âˆ˜L y) := isUnit_iff_exists.mpr
    âŸ¨y.symm âˆ˜L adjoint y.symm.toContinuousLinearMap, by
        simp [mul_def, â† comp_assoc, comp_assoc _ _ (adjoint y.toContinuousLinearMap),
          â† adjoint_comp, one_def, comp_assoc _ y.toContinuousLinearMap]âŸ©
  have thisÎ± : Î± = RCLike.re Î± := by
    have this10 := by simpa [IsSelfAdjoint, â† hÎ±, one_def, star_eq_adjoint] using
      IsSelfAdjoint.adjoint_conj (IsSelfAdjoint.one (W â†’L[ğ•œ] W)) y.toContinuousLinearMap
    rwa [â† one_def, (smul_left_injective ğ•œ one_ne_zero).eq_iff, RCLike.conj_eq_iff_re,
      eq_comm] at this10
  have thisÎ±' : Î± â‰  0 := fun h â†¦ by simp [h, â† hÎ±] at this
  have this2 : 0 â‰¤ Î± := by
    have this1 := thisÎ±.symm â–¸ (nonneg_iff_isPositive _ |>.mpr
      (thisÎ± â–¸ hÎ± â–¸ isPositive_adjoint_comp_self y.toContinuousLinearMap))
    rw [â† ContinuousLinearMap.IsPositive.isPositive_smul_iff (V := V) isPositive_one]
    Â· exact (nonneg_iff_isPositive _).mp this1
    Â· exact one_ne_zero' (V â†’L[ğ•œ] V)
  replace this2 := RCLike.ofReal_pos.mp <| thisÎ± â–¸ (lt_of_le_of_ne' this2 thisÎ±')
  have thisU : y.toContinuousLinearMap âˆ˜L adjoint y.toContinuousLinearMap =
      Î± â€¢ ContinuousLinearMap.id ğ•œ _ := by
        have := by simpa [one_def, comp_assoc] using congr($hÎ± âˆ˜L y.symm.toContinuousLinearMap)
        ext
        apply_fun y.symm using y.symm.injective
        simp [â† this]
  set Î±a := (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ)
  have Î±a2 : Î±a * Î±a = Î±â»Â¹ := by
    simp_rw [Î±a, â† RCLike.ofReal_mul, â† Real.rpow_add this2]
    rw [thisÎ±]
    norm_num
    simp [Real.rpow_neg_one]
  set U := aux_isometry' y thisÎ±' Î±a2 hÎ±.symm thisU (by simp [Î±a])
  use U
  have la : Î±aâ»Â¹ * Î±a = 1 := by
    simp only [one_div, Î±a]
    exact inv_mul_cancelâ‚€ (by
      simp only [ne_eq, map_eq_zero]
      rw [Real.rpow_eq_zero this2.le (by simp)]
      exact ne_of_gt this2)
  simp [U, coe_aux_isometry', coe_symm_aux_isometry', smul_smul, la, â† hy]
