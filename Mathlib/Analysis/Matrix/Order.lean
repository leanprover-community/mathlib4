/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Instances
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.LinearAlgebra.Matrix.HermitianFunctionalCalculus
import Mathlib.LinearAlgebra.Matrix.PosDef

/-!
# The partial order on matrices

This file constructs the partial order and star ordered instances on matrices on `ğ•œ`.
This allows us to use more general results from Câ‹†-algebras, like `CFC.sqrt`.

## Main results

* `Matrix.instPartialOrder`: the partial order on matrices given by `x â‰¤ y := (y - x).PosSemidef`.
* `Matrix.PosSemidef.dotProduct_mulVec_zero_iff`: for a positive semi-definite matrix `A`,
we have `xâ‹† A x = 0` iff `A x = 0`.
* `Matrix.PosDef.matrixInnerProductSpace`: the inner product on matrices induced by a
positive definite matrix `M`: `âŸªx, yâŸ« = (y * M * xá´´).trace`.

## Implementation notes

Note that the partial order instance is scoped to `MatrixOrder`.
Please `open scoped MatrixOrder` to use this.
-/

variable {ğ•œ n : Type*} [RCLike ğ•œ] [Fintype n]

open scoped ComplexOrder
open Matrix

namespace Matrix

section PartialOrder

/-- The preorder on matrices given by `A â‰¤ B := (B - A).PosSemidef`. -/
abbrev instPreOrder : Preorder (Matrix n n ğ•œ) where
  le A B := (B - A).PosSemidef
  le_refl A := sub_self A â–¸ PosSemidef.zero
  le_trans A B C hâ‚ hâ‚‚ := sub_add_sub_cancel C B A â–¸ hâ‚‚.add hâ‚

scoped[MatrixOrder] attribute [instance] Matrix.instPreOrder

open MatrixOrder

lemma le_iff {A B : Matrix n n ğ•œ} : A â‰¤ B â†” (B - A).PosSemidef := Iff.rfl

lemma nonneg_iff_posSemidef {A : Matrix n n ğ•œ} : 0 â‰¤ A â†” A.PosSemidef := by rw [le_iff, sub_zero]

protected alias âŸ¨LE.le.posSemidef, PosSemidef.nonnegâŸ© := nonneg_iff_posSemidef

attribute [aesop safe forward (rule_sets := [CStarAlgebra])] PosSemidef.nonneg

/-- The partial order on matrices given by `A â‰¤ B := (B - A).PosSemidef`. -/
abbrev instPartialOrder : PartialOrder (Matrix n n ğ•œ) where
  le_antisymm A B hâ‚ hâ‚‚ := by
    rw [â† sub_eq_zero, â† hâ‚‚.trace_eq_zero_iff]
    have := neg_nonneg.mp <| trace_neg (A - B) â–¸ neg_sub A B â–¸ hâ‚.trace_nonneg
    exact le_antisymm this hâ‚‚.trace_nonneg

scoped[MatrixOrder] attribute [instance] Matrix.instPartialOrder

lemma instIsOrderedAddMonoid : IsOrderedAddMonoid (Matrix n n ğ•œ) where
  add_le_add_left _ _ _ _ := by rwa [le_iff, add_sub_add_left_eq_sub]

scoped[MatrixOrder] attribute [instance] Matrix.instIsOrderedAddMonoid

lemma instNonnegSpectrumClass : NonnegSpectrumClass â„ (Matrix n n ğ•œ) where
  quasispectrum_nonneg_of_nonneg A hA := by
    classical
    simp only [quasispectrum_eq_spectrum_union_zero â„ A, Set.union_singleton, Set.mem_insert_iff,
      forall_eq_or_imp, le_refl, true_and]
    intro x hx
    obtain âŸ¨i, rflâŸ© := Set.ext_iff.mp
      hA.posSemidef.1.spectrum_real_eq_range_eigenvalues x |>.mp hx
    exact hA.posSemidef.eigenvalues_nonneg _

scoped[MatrixOrder] attribute [instance] instNonnegSpectrumClass

lemma instStarOrderedRing : StarOrderedRing (Matrix n n ğ•œ) :=
  .of_nonneg_iff' add_le_add_left fun A â†¦
    âŸ¨fun hA â†¦ by
      classical
      obtain âŸ¨X, hX, -, rflâŸ© :=
        sub_zero A â–¸ CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts hA.isHermitian
          (QuasispectrumRestricts.nnreal_of_nonneg hA.nonneg)
      exact âŸ¨X, hX.star_eq.symm â–¸ rflâŸ©,
    fun âŸ¨A, hAâŸ© => hA â–¸ (posSemidef_conjTranspose_mul_self A).nonnegâŸ©

scoped[MatrixOrder] attribute [instance] instStarOrderedRing

end PartialOrder

open scoped MatrixOrder

namespace PosSemidef

section sqrtDeprecated

variable [DecidableEq n] {A : Matrix n n ğ•œ} (hA : PosSemidef A)

/-- The positive semidefinite square root of a positive semidefinite matrix -/
@[deprecated CFC.sqrt (since := "2025-09-22")]
noncomputable def sqrt : Matrix n n ğ•œ :=
  hA.1.eigenvectorUnitary.1 * diagonal ((â†‘) âˆ˜ (âˆšÂ·) âˆ˜ hA.1.eigenvalues) *
  (star hA.1.eigenvectorUnitary : Matrix n n ğ•œ)

@[deprecated CFC.sqrt_nonneg (since := "2025-09-22")]
lemma posSemidef_sqrt : PosSemidef (CFC.sqrt A) := CFC.sqrt_nonneg A |>.posSemidef

include hA

@[deprecated CFC.sq_sqrt (since := "2025-09-22")]
lemma sq_sqrt : (CFC.sqrt A) ^ 2 = A := CFC.sq_sqrt A

@[deprecated CFC.sqrt_mul_sqrt_self (since := "2025-09-22")]
lemma sqrt_mul_self : CFC.sqrt A * CFC.sqrt A = A := CFC.sqrt_mul_sqrt_self A

@[deprecated CFC.sq_eq_sq_iff (since := "2025-09-24")]
lemma sq_eq_sq_iff {B : Matrix n n ğ•œ} (hB : PosSemidef B) : A ^ 2 = B ^ 2 â†” A = B :=
  CFC.sq_eq_sq_iff A B

@[deprecated (since := "2025-09-24")] alias âŸ¨eq_of_sq_eq_sq, _âŸ© := CFC.sq_eq_sq_iff

@[deprecated CFC.sqrt_sq (since := "2025-09-22")]
lemma sqrt_sq : CFC.sqrt (A ^ 2) = A := CFC.sqrt_sq A

@[deprecated CFC.sqrt_eq_iff (since := "2025-09-23")]
lemma eq_sqrt_iff_sq_eq {B : Matrix n n ğ•œ} (hB : PosSemidef B) : A = CFC.sqrt B â†” A ^ 2 = B := by
  rw [eq_comm, CFC.sqrt_eq_iff B A hB.nonneg hA.nonneg, sq]

@[deprecated CFC.sqrt_eq_iff (since := "2025-09-23")]
lemma sqrt_eq_iff_eq_sq {B : Matrix n n ğ•œ} (hB : PosSemidef B) : CFC.sqrt A = B â†” A = B ^ 2 := by
  simpa [eq_comm, sq] using CFC.sqrt_eq_iff A B hA.nonneg hB.nonneg

@[deprecated CFC.sqrt_eq_zero_iff (since := "2025-09-22")]
lemma sqrt_eq_zero_iff : CFC.sqrt A = 0 â†” A = 0 := CFC.sqrt_eq_zero_iff A

@[deprecated CFC.sqrt_eq_one_iff (since := "2025-09-23")]
lemma sqrt_eq_one_iff : CFC.sqrt A = 1 â†” A = 1 := CFC.sqrt_eq_one_iff A

@[deprecated CFC.isUnit_sqrt_iff (since := "2025-09-22")]
lemma isUnit_sqrt_iff : IsUnit (CFC.sqrt A) â†” IsUnit A := CFC.isUnit_sqrt_iff A

lemma inv_sqrt : (CFC.sqrt A)â»Â¹ = CFC.sqrt Aâ»Â¹ := by
  rw [eq_comm, CFC.sqrt_eq_iff _ _  hA.inv.nonneg (CFC.sqrt_nonneg A).posSemidef.inv.nonneg, â† sq,
    inv_pow', CFC.sq_sqrt A]

end sqrtDeprecated

/-- For `A` positive semidefinite, we have `xâ‹† A x = 0` iff `A x = 0`. -/
theorem dotProduct_mulVec_zero_iff {A : Matrix n n ğ•œ} (hA : PosSemidef A) (x : n â†’ ğ•œ) :
    star x â¬áµ¥ A *áµ¥ x = 0 â†” A *áµ¥ x = 0 := by
  classical
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h â–¸ dotProduct_zero _âŸ©
  obtain âŸ¨B, rflâŸ© := CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hA.nonneg
  simp_rw [â† Matrix.mulVec_mulVec, dotProduct_mulVec _ _ (B *áµ¥ x), star_eq_conjTranspose,
    vecMul_conjTranspose, star_star, dotProduct_star_self_eq_zero] at h âŠ¢
  rw [h, mulVec_zero]

/-- For `A` positive semidefinite, we have `xâ‹† A x = 0` iff `A x = 0` (linear maps version). -/
theorem toLinearMapâ‚‚'_zero_iff [DecidableEq n]
    {A : Matrix n n ğ•œ} (hA : PosSemidef A) (x : n â†’ ğ•œ) :
    Matrix.toLinearMapâ‚‚' ğ•œ A (star x) x = 0 â†” A *áµ¥ x = 0 := by
  simpa only [toLinearMapâ‚‚'_apply'] using hA.dotProduct_mulVec_zero_iff x

end PosSemidef

/-- A matrix is positive semidefinite if and only if it has the form `Bá´´ * B` for some `B`. -/
@[deprecated CStarAlgebra.nonneg_iff_eq_star_mul_self (since := "2025-09-22")]
lemma posSemidef_iff_eq_conjTranspose_mul_self {A : Matrix n n ğ•œ} :
    PosSemidef A â†” âˆƒ (B : Matrix n n ğ•œ), A = Bá´´ * B := by
  classical
  exact nonneg_iff_posSemidef (A := A) |>.eq â–¸ CStarAlgebra.nonneg_iff_eq_star_mul_self

@[deprecated (since := "2025-05-07")]
alias posSemidef_iff_eq_transpose_mul_self := CStarAlgebra.nonneg_iff_eq_star_mul_self

theorem posSemidef_iff_isHermitian_and_spectrum_nonneg [DecidableEq n] {A : Matrix n n ğ•œ} :
    A.PosSemidef â†” A.IsHermitian âˆ§ spectrum ğ•œ A âŠ† {a : ğ•œ | 0 â‰¤ a} := by
  refine âŸ¨fun h => âŸ¨h.isHermitian, fun a => ?_âŸ©, fun âŸ¨h1, h2âŸ© => ?_âŸ©
  Â· simp only [h.isHermitian.spectrum_eq_image_range, Set.mem_image, Set.mem_range,
      exists_exists_eq_and, Set.mem_setOf_eq, forall_exists_index]
    rintro i rfl
    exact_mod_cast h.eigenvalues_nonneg _
  Â· rw [h1.posSemidef_iff_eigenvalues_nonneg]
    intro i
    simpa [h1.spectrum_eq_image_range] using @h2 (h1.eigenvalues i)

@[deprecated commute_iff_mul_nonneg (since := "2025-09-23")]
theorem PosSemidef.commute_iff {A B : Matrix n n ğ•œ} (hA : A.PosSemidef) (hB : B.PosSemidef) :
    Commute A B â†” (A * B).PosSemidef := by
  classical
  exact nonneg_iff_posSemidef (A := A * B).eq â–¸ commute_iff_mul_nonneg hA.nonneg hB.nonneg

/-- A positive semi-definite matrix is positive definite if and only if it is invertible. -/
@[grind =]
theorem PosSemidef.posDef_iff_isUnit [DecidableEq n] {x : Matrix n n ğ•œ}
    (hx : x.PosSemidef) : x.PosDef â†” IsUnit x := by
  refine âŸ¨fun h => h.isUnit, fun h => âŸ¨hx.1, fun v hv => ?_âŸ©âŸ©
  obtain âŸ¨y, rflâŸ© := CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hx.nonneg
  simp_rw [dotProduct_mulVec, â† vecMul_vecMul, star_eq_conjTranspose, â† star_mulVec,
    â† dotProduct_mulVec, dotProduct_star_self_pos_iff]
  contrapose! hv
  rw [â† map_eq_zero_iff (f := (yá´´ * y).mulVecLin) (mulVec_injective_iff_isUnit.mpr h),
    mulVecLin_apply, â† mulVec_mulVec, hv, mulVec_zero]

theorem isStrictlyPositive_iff_posDef [DecidableEq n] {x : Matrix n n ğ•œ} :
    IsStrictlyPositive x â†” x.PosDef :=
  âŸ¨fun h => h.nonneg.posSemidef.posDef_iff_isUnit.mpr h.isUnit,
  fun h => h.isUnit.isStrictlyPositive h.posSemidef.nonnegâŸ©

alias âŸ¨IsStrictlyPositive.posDef, PosDef.isStrictlyPositiveâŸ© := isStrictlyPositive_iff_posDef

attribute [aesop safe forward (rule_sets := [CStarAlgebra])] PosDef.isStrictlyPositive

@[deprecated IsStrictlyPositive.commute_iff (since := "2025-09-26")]
theorem PosDef.commute_iff {A B : Matrix n n ğ•œ} (hA : A.PosDef) (hB : B.PosDef) :
    Commute A B â†” (A * B).PosDef := by
  classical
  rw [hA.isStrictlyPositive.commute_iff hB.isStrictlyPositive, isStrictlyPositive_iff_posDef]

@[deprecated IsStrictlyPositive.sqrt (since := "2025-09-26")]
lemma PosDef.posDef_sqrt [DecidableEq n] {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    PosDef (CFC.sqrt M) := hM.isStrictlyPositive.sqrt.posDef

section kronecker
variable {m : Type*} [Fintype m]

open scoped Kronecker

/-- The kronecker product of two positive semi-definite matrices is positive semi-definite. -/
theorem PosSemidef.kronecker {x : Matrix n n ğ•œ} {y : Matrix m m ğ•œ}
    (hx : x.PosSemidef) (hy : y.PosSemidef) : (x âŠ—â‚– y).PosSemidef := by
  classical
  obtain âŸ¨a, rflâŸ© := CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hx.nonneg
  obtain âŸ¨b, rflâŸ© := CStarAlgebra.nonneg_iff_eq_star_mul_self.mp hy.nonneg
  simpa [mul_kronecker_mul, â† conjTranspose_kronecker, star_eq_conjTranspose] using
    posSemidef_conjTranspose_mul_self _

open Matrix in
/-- The kronecker of two positive definite matrices is positive definite. -/
theorem PosDef.kronecker {x : Matrix n n ğ•œ} {y : Matrix m m ğ•œ}
    (hx : x.PosDef) (hy : y.PosDef) : (x âŠ—â‚– y).PosDef := by
  classical
  exact hx.posSemidef.kronecker hy.posSemidef |>.posDef_iff_isUnit.mpr <|
    hx.isUnit.kronecker hy.isUnit

end kronecker

/--
A matrix is positive definite if and only if it has the form `Bá´´ * B` for some invertible `B`.
-/
@[deprecated CStarAlgebra.isStrictlyPositive_iff_eq_star_mul_self (since := "2025-09-28")]
lemma posDef_iff_eq_conjTranspose_mul_self [DecidableEq n] {A : Matrix n n ğ•œ} :
    PosDef A â†” âˆƒ B : Matrix n n ğ•œ, IsUnit B âˆ§ A = Bá´´ * B :=
  isStrictlyPositive_iff_posDef.symm.trans CStarAlgebra.isStrictlyPositive_iff_eq_star_mul_self

@[deprecated (since := "2025-08-07")] alias PosDef.posDef_iff_eq_conjTranspose_mul_self :=
  CStarAlgebra.isStrictlyPositive_iff_eq_star_mul_self

/-- A positive definite matrix `M` induces a norm on `Matrix n n ğ•œ`:
`â€–xâ€– = sqrt (x * M * xá´´).trace`. -/
noncomputable def PosDef.matrixNormedAddCommGroup {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    NormedAddCommGroup (Matrix n n ğ•œ) :=
  letI : InnerProductSpace.Core ğ•œ (Matrix n n ğ•œ) :=
  { inner x y := (y * M * xá´´).trace
    conj_inner_symm _ _ := by
      simp only [mul_assoc, starRingEnd_apply, â† trace_conjTranspose, conjTranspose_mul,
        conjTranspose_conjTranspose, hM.isHermitian.eq]
    re_inner_nonneg x := RCLike.nonneg_iff.mp
      (hM.posSemidef.mul_mul_conjTranspose_same x).trace_nonneg |>.1
    add_left := by simp [mul_add]
    smul_left := by simp
    definite x hx := by
      classical
      obtain âŸ¨y, hy, rflâŸ© := CStarAlgebra.isStrictlyPositive_iff_eq_star_mul_self.mp
        hM.isStrictlyPositive
      rw [â† mul_assoc, â† conjTranspose_conjTranspose x, star_eq_conjTranspose, â† conjTranspose_mul,
        conjTranspose_conjTranspose, mul_assoc, trace_conjTranspose_mul_self_eq_zero_iff] at hx
      lift y to (Matrix n n ğ•œ)Ë£ using hy
      simpa [â† mul_assoc] using congr(yâ»Â¹ * $hx) }
  this.toNormedAddCommGroup

/-- A positive definite matrix `M` induces an inner product on `Matrix n n ğ•œ`:
`âŸªx, yâŸ« = (y * M * xá´´).trace`. -/
def PosDef.matrixInnerProductSpace {M : Matrix n n ğ•œ} (hM : M.PosDef) :
    letI : NormedAddCommGroup (Matrix n n ğ•œ) := hM.matrixNormedAddCommGroup
    InnerProductSpace ğ•œ (Matrix n n ğ•œ) :=
  InnerProductSpace.ofCore _

end Matrix
