/-
Copyright (c) 2025 Newell Jensen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Newell Jensen
-/
module

public import Mathlib.Topology.MetricSpace.Thickening

/-!
# Delone sets

A **Delone set** `D ‚äÜ X` in a metric space is a set which is both:

* **uniformly discrete**: there exists a sufficiently small scale at which
  distinct points of `D` are separated.
* **relatively dense**: there exists a sufficiently large scale at which
  every point of `X` is close to some point of `D`.

In metric terms, this means that there exist constants `r > 0` and `R > 0`
such that distinct points of `D` are at distance at least `r`, and every point
of `X` lies within distance `R` of `D`.

In this file, these notions are formulated in terms of **metric entourages**
(i.e. relations belonging to the uniformity), rather than directly using
metric inequalities. This makes the theory compatible with the general
uniform-space infrastructure.

Delone sets arise in discrete geometry, crystallography, quasicrystals,
aperiodic order, and tiling theory.

## Main definitions

* `UniformlyDiscrete (D : Set X)`
  Existence of an entourage separating distinct points of `D`.

* `RelativelyDense (D : Set X)`
  Existence of an entourage whose image of `D` covers the whole space.

* `DeloneSet X`
  A structure bundling a uniformly discrete and relatively dense set.

## Basic properties

* Canonical radii: `DeloneSet.coveringRadius`, `DeloneSet.packingRadius`,
  with corresponding bounds `dist_le_coveringRadius` and `le_dist_of_mem_ne`.
* `DeloneSet.dist_pos_of_ne`: distinct points lie at positive distance.
* `DeloneSet.subset_ball_singleton`: small balls contain at most one point of a Delone set.
* `DeloneSet.map`: Delone sets are preserved by isometries (with `map_id`, `map_comp`, `map_symm`).

## TODO

The definition `distLT` is a temporary metric-derived entourage used to
phrase the theory of Delone sets in terms of relations / entourages.

Once mathlib provides a canonical family of entourages indexed by a quantitative
parameter (e.g. `‚Ñù‚â•0`) for metric spaces, this definition should be removed and
all occurrences of `distLT Œµ` replaced by the corresponding canonical entourage.
-/

@[expose] public section

open scoped Uniformity

variable {X Y : Type*} [MetricSpace X] [MetricSpace Y]

namespace Metric

/-- The metric `Œµ`-entourage as a relation (a set of pairs). -/
def distLT (Œµ : ‚Ñù) : SetRel X X :=
  {p : X √ó X | dist p.1 p.2 < Œµ}

/-- If `Œµ > 0`, then the metric entourage `distLT Œµ` belongs to the uniformity. -/
lemma distLT_mem_uniformity {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : distLT (X := X) Œµ ‚àà ùì§ X := by
  refine (mem_uniformity_dist).2 ?_
  refine ‚ü®Œµ, hŒµ, ?_‚ü©
  exact fun ‚¶Éa b‚¶Ñ a‚ÇÅ ‚Ü¶ a‚ÇÅ

/-- A set `D` is uniformly discrete if some metric entourage separates
distinct points of `D`. -/
def UniformlyDiscrete (D : Set X) : Prop :=
  ‚àÉ r > 0, ‚àÄ ‚¶Éx y‚¶Ñ, x ‚àà D ‚Üí y ‚àà D ‚Üí x ‚â† y ‚Üí (x, y) ‚àâ distLT (X := X) r

/-- A set `D` is relatively dense if some metric entourage covers the
whole space from `D`. -/
def RelativelyDense (D : Set X) : Prop :=
  ‚àÉ R > 0, ‚àÄ x : X, ‚àÉ y ‚àà D, (x, y) ‚àà distLT (X := X) R

/-- If `D ‚äÜ E` and `E` is uniformly discrete, then so is `D`. -/
lemma UniformlyDiscrete.mono {D E : Set X} (hDE : D ‚äÜ E) :
    UniformlyDiscrete E ‚Üí UniformlyDiscrete D := by
  rintro ‚ü®r, hr, hsep‚ü©
  refine ‚ü®r, hr, fun x y hx hy hne ‚Ü¶ ?_‚ü©
  exact hsep (hDE hx) (hDE hy) hne

/-- If `D ‚äÜ E` and `D` is relatively dense, then so is `E`. -/
lemma RelativelyDense.mono {D E : Set X} (hDE : D ‚äÜ E) :
    RelativelyDense D ‚Üí RelativelyDense E := by
  rintro ‚ü®R, hR, hcov‚ü©
  refine ‚ü®R, hR, fun x ‚Ü¶ ?_‚ü©
  obtain ‚ü®y, hyD, hxy‚ü© := hcov x
  exact ‚ü®y, hDE hyD, hxy‚ü©

lemma RelativelyDense.cthickening_eq_univ
    {X : Type*} [MetricSpace X] {D : Set X} :
    RelativelyDense D ‚Üí ‚àÉ R > 0, cthickening R D = Set.univ := by
  rintro ‚ü®R, hRpos, hcov‚ü©
  refine ‚ü®R, hRpos, ?_‚ü©
  ext x; constructor
  ¬∑ intro _; trivial
  ¬∑ intro _; obtain ‚ü®y, hyD, hxy‚ü© := hcov x
    have : dist x y ‚â§ R := by
      simpa [distLT] using (le_of_lt hxy)
    exact mem_cthickening_of_dist_le x y R D hyD this

end Metric

namespace Delone

open Metric

/-- A **Delone set** in a metric space: uniformly discrete and relatively dense. -/
structure DeloneSet (X : Type*) [MetricSpace X] where
  /-- The underlying set of a Delone set. -/
  (carrier : Set X)
  /-- Uniform discreteness: distinct points of the set are separated by a
  sufficiently small entourage. -/
  (uniformlyDiscrete : UniformlyDiscrete carrier)
  /-- Relative denseness: every point of the space is related by a bounded
  entourage to the set. -/
  (relativelyDense : RelativelyDense carrier)

attribute [simp] DeloneSet.carrier

namespace DeloneSet

/-- A Delone set is nonempty. -/
lemma nonempty [Nonempty X] (D : DeloneSet X) : Nonempty D.carrier := by
  obtain ‚ü®_, _, hcov‚ü© := D.relativelyDense
  obtain ‚ü®x‚ü© := (inferInstance : Nonempty X)
  obtain ‚ü®y, hyD, _‚ü© := hcov x
  exact ‚ü®y, hyD‚ü©

/-- The **covering radius** of a Delone set: a chosen constant `R > 0` such that every
point of the ambient space lies within distance `R` of some point of the set. -/
noncomputable def coveringRadius (D : DeloneSet X) : ‚Ñù :=
  Classical.choose D.relativelyDense

lemma coveringRadius_pos (D : DeloneSet X) : 0 < D.coveringRadius :=
  (Classical.choose_spec D.relativelyDense).1

lemma dist_le_coveringRadius (D : DeloneSet X) (x : X) :
    ‚àÉ y ‚àà D.carrier, dist x y ‚â§ D.coveringRadius := by
  obtain ‚ü®y, hy, hxy‚ü© := (Classical.choose_spec D.relativelyDense).2 x
  refine ‚ü®y, hy, ?_‚ü©
  simpa [distLT] using (le_of_lt hxy)

/-- The **packing radius** of a Delone set: a chosen constant `r > 0` such that any
two distinct points of the set are at distance at least `r`. -/
noncomputable def packingRadius (D : DeloneSet X) : ‚Ñù :=
  Classical.choose D.uniformlyDiscrete

lemma packingRadius_pos (D : DeloneSet X) : 0 < D.packingRadius :=
  (Classical.choose_spec D.uniformlyDiscrete).1

lemma le_dist_of_mem_ne (D : DeloneSet X) {x y : X}
    (hx : x ‚àà D.carrier) (hy : y ‚àà D.carrier) (hne : x ‚â† y) :
    D.packingRadius ‚â§ dist x y := by
  have hnot :
      (x, y) ‚àâ distLT (X := X) D.packingRadius :=
    (Classical.choose_spec D.uniformlyDiscrete).2 hx hy hne
  simpa [distLT] using (le_of_not_gt hnot)

lemma dist_pos_of_ne {D : DeloneSet X} {x y : X}
    (hx : x ‚àà D.carrier) (hy : y ‚àà D.carrier) (hne : x ‚â† y) :
    0 < dist x y :=
  lt_of_lt_of_le D.packingRadius_pos <| D.le_dist_of_mem_ne hx hy hne

/-- For a Delone set `D`, there exists a radius `r > 0` such that, for any
`z ‚àà D`, the open ball `ball z r` contains at most one point of the Delone set. -/
lemma subset_ball_singleton (D : DeloneSet X) :
    ‚àÉ r > 0, ‚àÄ ‚¶Éx y z‚¶Ñ, x ‚àà D.carrier ‚Üí y ‚àà D.carrier ‚Üí z ‚àà D.carrier ‚Üí
    x ‚àà ball z r ‚Üí y ‚àà ball z r ‚Üí x = y := by
  refine ‚ü®D.packingRadius / 2, half_pos D.packingRadius_pos, ?_‚ü©
  intro x y z hx hy hz hxz hyz
  by_contra hne
  have hlt : dist x y < D.packingRadius := by
    have hsum_lt : dist x z + dist z y < D.packingRadius := by
      simpa [add_halves] using (add_lt_add hxz <| mem_ball'.mp hyz)
    exact lt_of_le_of_lt (dist_triangle x z y) hsum_lt
  exact (not_lt_of_ge <| D.le_dist_of_mem_ne hx hy hne) hlt

/-- The image of a Delone set under an isometry is a Delone set. -/
def map (f : X ‚âÉ·µ¢ Y) (D : DeloneSet X) : DeloneSet Y := {
  carrier := f '' D.carrier
  uniformlyDiscrete := by
    refine ‚ü®D.packingRadius, D.packingRadius_pos, ?_‚ü©
    rintro y y' ‚ü®x, hx, rfl‚ü© ‚ü®x', hx', rfl‚ü© hne
    simpa [f.dist_eq, distLT] using D.le_dist_of_mem_ne hx hx' (by grind)
  relativelyDense := by
    obtain ‚ü®R, hR, hcov‚ü© := D.relativelyDense
    refine ‚ü®R, hR, ?_‚ü©
    intro y; obtain ‚ü®x, hx, hxy‚ü© := hcov (f.symm y)
    refine ‚ü®f x, ‚ü®x, hx, rfl‚ü©, ?_‚ü©
    have hthis : dist (f.symm y) x < R := by
      simpa [distLT] using hxy
    have hdist : dist y (f x) = dist (f.symm y) x := by
      simpa using (f.dist_eq (f.symm y) x)
    simpa [distLT, hdist] using hthis
}

@[ext] lemma ext {D E : DeloneSet X} (h : D.carrier = E.carrier) : D = E := by
  cases D; cases E; cases h; rfl

lemma map_id (D : DeloneSet X) : D.map (IsometryEquiv.refl X) = D := by
  ext x; constructor
  ¬∑ rintro ‚ü®y, hyD, rfl‚ü©; exact hyD
  ¬∑ intro hx; exact ‚ü®x, hx, rfl‚ü©

lemma map_comp {Z : Type*} [MetricSpace Z] (D : DeloneSet X) (f : X ‚âÉ·µ¢ Y) (g : Y ‚âÉ·µ¢ Z) :
    D.map (f.trans g) = (D.map f).map g := by
  ext z; constructor
  ¬∑ rintro ‚ü®x, hxD, rfl‚ü©; exact ‚ü®f x, ‚ü®x, hxD, rfl‚ü©, rfl‚ü©
  ¬∑ rintro ‚ü®y, ‚ü®x, hxD, rfl‚ü©, rfl‚ü©; exact ‚ü®x, hxD, rfl‚ü©

lemma map_symm (D : DeloneSet X) (f : X ‚âÉ·µ¢ Y) : (D.map f).map f.symm = D := by
  ext x; constructor
  ¬∑ rintro ‚ü®y, ‚ü®x‚ÇÄ, hx‚ÇÄD, rfl‚ü©, rfl‚ü©; simpa
  ¬∑ intro hx; exact ‚ü®f x, ‚ü®x, hx, rfl‚ü©, by simp‚ü©

end DeloneSet

end Delone
