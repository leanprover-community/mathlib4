/-
Copyright (c) 2025 Newell Jensen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Newell Jensen
-/
module

public import Mathlib.Topology.MetricSpace.Thickening

/-!
# Delone sets

A **Delone set** `D ‚äÜ X` in a metric space is a set which is both:

* **uniformly discrete**: there exists a sufficiently small scale at which
  distinct points of `D` are separated.
* **relatively dense**: there exists a sufficiently large scale at which
  every point of `X` is close to some point of `D`.

In metric terms, this means that there exist constants `r > 0` and `R > 0`
such that distinct points of `D` are at distance at least `r`, and every point
of `X` lies within distance `R` of `D`.

In this file, these notions are formulated in terms of **metric entourages**
(i.e. relations belonging to the uniformity), rather than directly using
metric inequalities. This makes the theory compatible with the general
uniform-space infrastructure.

Delone sets arise in discrete geometry, crystallography, quasicrystals,
aperiodic order, and tiling theory.

## Main definitions

* `UniformlyDiscreteWith D r`
  A set is uniformly discrete *with radius `r`*.

* `RelativelyDenseWith D R`
  A set is relatively dense *with radius `R`*.

* `IsUniformlyDiscrete D`, `IsRelativelyDense D`
  Existential versions.

* `DeloneSet X`
  Bundles a set together with explicit radii witnessing uniform discreteness
  and relative denseness.

## Basic properties

* Canonical radii: `DeloneSet.packingRadius`, `DeloneSet.coveringRadius`.
* Bounds: `dist_le_coveringRadius`, `le_dist_of_mem_ne`.
* `dist_pos_of_ne`: distinct points lie at positive distance.
* `subset_ball_singleton`: small balls contain at most one point of a Delone set.
* `map`: Delone sets are preserved by isometries.

## TODO

The definition `distLT` is a temporary metric-derived entourage used to
phrase the theory of Delone sets in terms of relations / entourages.

Once mathlib provides a canonical family of entourages indexed by a quantitative
parameter (e.g. `‚Ñù‚â•0`) for metric spaces, this definition should be removed and
all occurrences of `distLT Œµ` replaced by the corresponding canonical entourage.
-/

@[expose] public section

open scoped Uniformity

variable {X Y : Type*} [MetricSpace X] [MetricSpace Y]

namespace Metric

/-- The metric `Œµ`-entourage as a relation (a set of pairs). -/
def distLT (Œµ : ‚Ñù) : SetRel X X := {p : X √ó X | dist p.1 p.2 < Œµ}

/-- If `Œµ > 0`, then the metric entourage `distLT Œµ` belongs to the uniformity. -/
lemma distLT_mem_uniformity {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : distLT Œµ ‚àà ùì§ X := by
  exact (mem_uniformity_dist).2 ‚ü®Œµ, hŒµ, fun ‚¶Éa b‚¶Ñ a‚ÇÅ ‚Ü¶ a‚ÇÅ‚ü©

/-- A set `D` is uniformly discrete *with radius `r`* if
distinct points of `D` are separated by the entourage `distLT r`. -/
def UniformlyDiscreteWith (D : Set X) (r : ‚Ñù) : Prop :=
  0 < r ‚àß ‚àÄ ‚¶Éx y‚¶Ñ, x ‚àà D ‚Üí y ‚àà D ‚Üí x ‚â† y ‚Üí (x, y) ‚àâ distLT r

/-- A set `D` is relatively dense *with radius `R`* if
every point of `X` is related to `D` by `distLT R`. -/
def RelativelyDenseWith (D : Set X) (R : ‚Ñù) : Prop :=
  0 < R ‚àß ‚àÄ x : X, ‚àÉ y ‚àà D, (x, y) ‚àà distLT R

/-- A set is uniformly discrete if it is uniformly discrete with some radius. -/
def IsUniformlyDiscrete (D : Set X) : Prop :=
  ‚àÉ r : ‚Ñù, UniformlyDiscreteWith D r

/-- A set is relatively dense if it is relatively dense with some radius. -/
def IsRelativelyDense (D : Set X) : Prop :=
  ‚àÉ R : ‚Ñù, RelativelyDenseWith D R

/-- Monotonicity of uniform discreteness. -/
lemma UniformlyDiscreteWith.mono {D E : Set X} {r : ‚Ñù} (hDE : D ‚äÜ E) :
    UniformlyDiscreteWith E r ‚Üí UniformlyDiscreteWith D r := by
  rintro ‚ü®hr, h‚ü©
  exact ‚ü®hr, fun x y hx hy hne ‚Ü¶ h (hDE hx) (hDE hy) hne‚ü©

/-- Monotonicity of relative denseness. -/
lemma RelativelyDenseWith.mono {D E : Set X} {R : ‚Ñù} (hDE : D ‚äÜ E) :
    RelativelyDenseWith D R ‚Üí RelativelyDenseWith E R := by
  rintro ‚ü®hR, hcov‚ü©
  refine ‚ü®hR, fun x ‚Ü¶ ?_‚ü©
  obtain ‚ü®y, hyD, hxy‚ü© := hcov x
  exact ‚ü®y, hDE hyD, hxy‚ü©

/-- Relative denseness implies the thickening covers the whole space. -/
lemma RelativelyDenseWith.cthickening_eq_univ {X : Type*} [MetricSpace X] {D : Set X} {R : ‚Ñù} :
    RelativelyDenseWith D R ‚Üí cthickening R D = Set.univ := by
  rintro ‚ü®hRpos, hcov‚ü©
  ext x; constructor
  ¬∑ intro _; trivial
  ¬∑ intro _; obtain ‚ü®y, hyD, hxy‚ü© := hcov x
    have : dist x y ‚â§ R := by
      simpa [distLT] using (le_of_lt hxy)
    exact mem_cthickening_of_dist_le x y R D hyD this

end Metric

namespace Delone

open Metric

/-- A **Delone set** consists of a set together with explicit radii witnessing
uniform discreteness and relative denseness. -/
structure DeloneSet (X : Type*) [MetricSpace X] where
  /-- The underlying set. -/
  carrier : Set X
  /-- A radius witnessing uniform discreteness. -/
  r : ‚Ñù
  hr : UniformlyDiscreteWith carrier r
  /-- A radius witnessing relative denseness. -/
  R : ‚Ñù
  hR : RelativelyDenseWith carrier R

attribute [simp] DeloneSet.carrier

namespace DeloneSet

/-- The packing radius. -/
def packingRadius (D : DeloneSet X) : ‚Ñù := D.r

lemma packingRadius_pos (D : DeloneSet X) : 0 < D.packingRadius := D.hr.1

/-- The covering radius. -/
def coveringRadius (D : DeloneSet X) : ‚Ñù := D.R

lemma coveringRadius_pos (D : DeloneSet X) : 0 < D.coveringRadius := D.hR.1

/-- A Delone set is nonempty. -/
lemma nonempty [Nonempty X] (D : DeloneSet X) : Nonempty D.carrier := by
  obtain ‚ü®x‚ü© := (inferInstance : Nonempty X)
  obtain ‚ü®y, hyD, _‚ü© := D.hR.2 x
  exact ‚ü®y, hyD‚ü©

/-- Every point is within `coveringRadius` of the Delone set. -/
lemma dist_le_coveringRadius (D : DeloneSet X) (x : X) :
    ‚àÉ y ‚àà D.carrier, dist x y ‚â§ D.coveringRadius := by
  obtain ‚ü®y, hy, hxy‚ü© := D.hR.2 x
  exact ‚ü®y, hy, le_of_lt hxy‚ü©

/-- Any two distinct points of a Delone set are at distance at least `packingRadius`. -/
lemma le_dist_of_mem_ne (D : DeloneSet X) {x y : X}
    (hx : x ‚àà D.carrier) (hy : y ‚àà D.carrier) (hne : x ‚â† y) :
    D.packingRadius ‚â§ dist x y :=
  not_lt.mp <| D.hr.2 hx hy hne

/-- Distinct points in a Delone set lie at positive distance. -/
lemma dist_pos_of_ne {D : DeloneSet X} {x y : X}
    (hx : x ‚àà D.carrier) (hy : y ‚àà D.carrier) (hne : x ‚â† y) :
    0 < dist x y :=
lt_of_lt_of_le D.packingRadius_pos <| D.le_dist_of_mem_ne hx hy hne

/-- For a Delone set `D`, there exists `r > 0` such that
for any `z ‚àà D`, the ball `ball z r` contains at most one point of `D`. -/
lemma subset_ball_singleton (D : DeloneSet X) :
    ‚àÉ r > 0, ‚àÄ ‚¶Éx y z‚¶Ñ, x ‚àà D.carrier ‚Üí y ‚àà D.carrier ‚Üí z ‚àà D.carrier ‚Üí
      x ‚àà ball z r ‚Üí y ‚àà ball z r ‚Üí x = y := by
  refine ‚ü®D.packingRadius / 2, half_pos D.packingRadius_pos, ?_‚ü©
  intro x y z hx hy hz hxz hyz
  by_contra hne
  have hlt : dist x y < D.packingRadius := by
    have hsum_lt :
        dist x z + dist z y < D.packingRadius := by
      simpa [add_halves] using
        (add_lt_add hxz <| mem_ball'.mp hyz)
    exact lt_of_le_of_lt (dist_triangle x z y) hsum_lt
  exact (not_lt_of_ge <| D.le_dist_of_mem_ne hx hy hne) hlt

/-- Isometries preserve Delone sets. -/
def map (f : X ‚âÉ·µ¢ Y) (D : DeloneSet X) : DeloneSet Y := {
  carrier := f '' D.carrier
  r := D.r
  hr := by
    refine ‚ü®D.hr.1, ?_‚ü©
    rintro x y ‚ü®x‚ÇÄ, hx‚ÇÄ, rfl‚ü© ‚ü®y‚ÇÄ, hy‚ÇÄ, rfl‚ü© hne
    have : (x‚ÇÄ, y‚ÇÄ) ‚àâ distLT D.r := D.hr.2 hx‚ÇÄ hy‚ÇÄ ?_
    ¬∑ simpa [distLT, f.dist_eq] using this
    ¬∑ grind
  R := D.R
  hR := by
    refine ‚ü®D.hR.1, fun y ‚Ü¶ ?_‚ü©
    obtain ‚ü®x, hx, hxR‚ü© := D.hR.2 (f.symm y)
    refine ‚ü®f x, ‚ü®x, hx, rfl‚ü©, ?_‚ü©
    have hxR' : dist (f.symm y) x < D.R := by
      simpa [distLT] using hxR
    have hdist : dist y (f x) = dist (f.symm y) x := by
      simpa using (f.dist_eq (f.symm y) x)
    simpa [distLT, hdist] using hxR'
}

/-- Extensionality for `DeloneSet`: equality of carrier and radii. -/
@[ext] lemma ext {D E : DeloneSet X} (h_carrier : D.carrier = E.carrier)
    (h_r : D.r = E.r) (h_R : D.R = E.R) : D = E := by
  cases D; cases E; cases h_carrier; cases h_r; cases h_R; rfl

lemma map_id (D : DeloneSet X) : D.map (IsometryEquiv.refl X) = D := by
  apply ext
  ¬∑ ext x; constructor
    ¬∑ rintro ‚ü®y, hyD, rfl‚ü©; simpa using hyD
    ¬∑ intro hx; exact ‚ü®x, hx, rfl‚ü©
  ¬∑ rfl
  ¬∑ rfl

lemma map_comp {Z : Type*} [MetricSpace Z]
    (D : DeloneSet X) (f : X ‚âÉ·µ¢ Y) (g : Y ‚âÉ·µ¢ Z) :
    D.map (f.trans g) = (D.map f).map g := by
  apply ext
  ¬∑ ext z; constructor
    ¬∑ rintro ‚ü®x, hxD, rfl‚ü©
      exact ‚ü®f x, ‚ü®x, hxD, rfl‚ü©, rfl‚ü©
    ¬∑ rintro ‚ü®y, ‚ü®x, hxD, rfl‚ü©, rfl‚ü©
      exact ‚ü®x, hxD, rfl‚ü©
  ¬∑ rfl
  ¬∑ rfl

lemma map_symm (D : DeloneSet X) (f : X ‚âÉ·µ¢ Y) :
    (D.map f).map f.symm = D := by
  apply ext
  ¬∑ ext x; constructor
    ¬∑ rintro ‚ü®y, ‚ü®x‚ÇÄ, hx‚ÇÄD, rfl‚ü©, rfl‚ü©; simpa
    ¬∑ intro hx; exact ‚ü®f x, ‚ü®x, hx, rfl‚ü©, by simp‚ü©
  ¬∑ rfl
  ¬∑ rfl

end DeloneSet

end Delone
