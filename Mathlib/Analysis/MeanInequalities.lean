/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, S√©bastien Gou√´zel, R√©my Degenne
-/
import Mathlib.Algebra.BigOperators.Expect
import Mathlib.Analysis.Convex.Jensen
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal
import Mathlib.Data.Real.ConjExponents

/-!
# Mean value inequalities

In this file we prove several inequalities for finite sums, including AM-GM inequality,
HM-GM inequality, Young's inequality, H√∂lder inequality, and Minkowski inequality. Versions for
integrals of some of these inequalities are available in `MeasureTheory.MeanInequalities`.

## Main theorems

### AM-GM inequality:

The inequality says that the geometric mean of a tuple of non-negative numbers is less than or equal
to their arithmetic mean. We prove the weighted version of this inequality: if $w$ and $z$
are two non-negative vectors and $\sum_{i\in s} w_i=1$, then
$$
\prod_{i\in s} z_i^{w_i} ‚â§ \sum_{i\in s} w_iz_i.
$$
The classical version is a special case of this inequality for $w_i=\frac{1}{n}$.

We prove a few versions of this inequality. Each of the following lemmas comes in two versions:
a version for real-valued non-negative functions is in the `Real` namespace, and a version for
`NNReal`-valued functions is in the `NNReal` namespace.

- `geom_mean_le_arith_mean_weighted` : weighted version for functions on `Finset`s;
- `geom_mean_le_arith_mean2_weighted` : weighted version for two numbers;
- `geom_mean_le_arith_mean3_weighted` : weighted version for three numbers;
- `geom_mean_le_arith_mean4_weighted` : weighted version for four numbers.


### HM-GM inequality:

The inequality says that the harmonic mean of a tuple of positive numbers is less than or equal
to their geometric mean. We prove the weighted version of this inequality: if $w$ and $z$
are two positive vectors and $\sum_{i\in s} w_i=1$, then
$$
1/(\sum_{i\in s} w_i/z_i) ‚â§ \prod_{i\in s} z_i^{w_i}
$$
The classical version is proven as a special case of this inequality for $w_i=\frac{1}{n}$.

The inequalities are proven only for real valued positive functions on `Finset`s, and namespaced in
`Real`. The weighted version follows as a corollary of the weighted AM-GM inequality.

### Young's inequality

Young's inequality says that for non-negative numbers `a`, `b`, `p`, `q` such that
$\frac{1}{p}+\frac{1}{q}=1$ we have
$$
ab ‚â§ \frac{a^p}{p} + \frac{b^q}{q}.
$$

This inequality is a special case of the AM-GM inequality. It is then used to prove H√∂lder's
inequality (see below).

### H√∂lder's inequality

The inequality says that for two conjugate exponents `p` and `q` (i.e., for two positive numbers
such that $\frac{1}{p}+\frac{1}{q}=1$) and any two non-negative vectors their inner product is
less than or equal to the product of the $L_p$ norm of the first vector and the $L_q$ norm of the
second vector:
$$
\sum_{i\in s} a_ib_i ‚â§ \sqrt[p]{\sum_{i\in s} a_i^p}\sqrt[q]{\sum_{i\in s} b_i^q}.
$$

We give versions of this result in `‚Ñù`, `‚Ñù‚â•0` and `‚Ñù‚â•0‚àû`.

There are at least two short proofs of this inequality. In our proof we prenormalize both vectors,
then apply Young's inequality to each $a_ib_i$. Another possible proof would be to deduce this
inequality from the generalized mean inequality for well-chosen vectors and weights.

### Minkowski's inequality

The inequality says that for `p ‚â• 1` the function
$$
\|a\|_p=\sqrt[p]{\sum_{i\in s} a_i^p}
$$
satisfies the triangle inequality $\|a+b\|_p\le \|a\|_p+\|b\|_p$.

We give versions of this result in `Real`, `‚Ñù‚â•0` and `‚Ñù‚â•0‚àû`.

We deduce this inequality from H√∂lder's inequality. Namely, H√∂lder inequality implies that $\|a\|_p$
is the maximum of the inner product $\sum_{i\in s}a_ib_i$ over `b` such that $\|b\|_q\le 1$. Now
Minkowski's inequality follows from the fact that the maximum value of the sum of two functions is
less than or equal to the sum of the maximum values of the summands.

## TODO

- each inequality `A ‚â§ B` should come with a theorem `A = B ‚Üî _`; one of the ways to prove them
  is to define `StrictConvexOn` functions.
- generalized mean inequality with any `p ‚â§ q`, including negative numbers;
- prove that the power mean tends to the geometric mean as the exponent tends to zero.

-/


universe u v

open Finset NNReal ENNReal
open scoped BigOperators

noncomputable section

variable {Œπ : Type u} (s : Finset Œπ)

section GeomMeanLEArithMean

/-! ### AM-GM inequality -/


namespace Real

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for real-valued nonnegative functions. -/
theorem geom_mean_le_arith_mean_weighted (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i ‚àà s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) :
    ‚àè i ‚àà s, z i ^ w i ‚â§ ‚àë i ‚àà s, w i * z i := by
  -- If some number `z i` equals zero and has non-zero weight, then LHS is 0 and RHS is nonnegative.
  by_cases A : ‚àÉ i ‚àà s, z i = 0 ‚àß w i ‚â† 0
  ¬∑ rcases A with ‚ü®i, his, hzi, hwi‚ü©
    rw [prod_eq_zero his]
    ¬∑ exact sum_nonneg fun j hj => mul_nonneg (hw j hj) (hz j hj)
    ¬∑ rw [hzi]
      exact zero_rpow hwi
  -- If all numbers `z i` with non-zero weight are positive, then we apply Jensen's inequality
  -- for `exp` and numbers `log (z i)` with weights `w i`.
  ¬∑ simp only [not_exists, not_and, Ne, Classical.not_not] at A
    have := convexOn_exp.map_sum_le hw hw' fun i _ => Set.mem_univ <| log (z i)
    simp only [exp_sum, (¬∑ ‚àò ¬∑), smul_eq_mul, mul_comm (w _) (log _)] at this
    convert this using 1 <;> [apply prod_congr rfl;apply sum_congr rfl] <;> intro i hi
    ¬∑ cases' eq_or_lt_of_le (hz i hi) with hz hz
      ¬∑ simp [A i hi hz.symm]
      ¬∑ exact rpow_def_of_pos hz _
    ¬∑ cases' eq_or_lt_of_le (hz i hi) with hz hz
      ¬∑ simp [A i hi hz.symm]
      ¬∑ rw [exp_log hz]

/-- **AM-GM inequality**: The **geometric mean is less than or equal to the arithmetic mean. --/
theorem geom_mean_le_arith_mean {Œπ : Type*} (s : Finset Œπ) (w : Œπ ‚Üí ‚Ñù) (z : Œπ ‚Üí ‚Ñù)
    (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i) (hw' : 0 < ‚àë i ‚àà s, w i) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) :
    (‚àè i ‚àà s, z i ^ w i) ^ (‚àë i ‚àà s, w i)‚Åª¬π  ‚â§  (‚àë i ‚àà s, w i * z i) / (‚àë i ‚àà s, w i) := by
  convert geom_mean_le_arith_mean_weighted s (fun i => (w i) / ‚àë i ‚àà s, w i) z ?_ ?_ hz using 2
  ¬∑ rw [‚Üê finset_prod_rpow _ _ (fun i hi => rpow_nonneg (hz _ hi) _) _]
    refine Finset.prod_congr rfl (fun _ ih => ?_)
    rw [div_eq_mul_inv, rpow_mul (hz _ ih)]
  ¬∑ simp_rw [div_eq_mul_inv, mul_assoc, mul_comm, ‚Üê mul_assoc, ‚Üê Finset.sum_mul, mul_comm]
  ¬∑ exact fun _ hi => div_nonneg (hw _ hi) (le_of_lt hw')
  ¬∑ simp_rw [div_eq_mul_inv, ‚Üê Finset.sum_mul]
    exact mul_inv_cancel‚ÇÄ (by linarith)

theorem geom_mean_weighted_of_constant (w z : Œπ ‚Üí ‚Ñù) (x : ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i ‚àà s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) (hx : ‚àÄ i ‚àà s, w i ‚â† 0 ‚Üí z i = x) :
    ‚àè i ‚àà s, z i ^ w i = x :=
  calc
    ‚àè i ‚àà s, z i ^ w i = ‚àè i ‚àà s, x ^ w i := by
      refine prod_congr rfl fun i hi => ?_
      rcases eq_or_ne (w i) 0 with h‚ÇÄ | h‚ÇÄ
      ¬∑ rw [h‚ÇÄ, rpow_zero, rpow_zero]
      ¬∑ rw [hx i hi h‚ÇÄ]
    _ = x := by
      rw [‚Üê rpow_sum_of_nonneg _ hw, hw', rpow_one]
      have : (‚àë i ‚àà s, w i) ‚â† 0 := by
        rw [hw']
        exact one_ne_zero
      obtain ‚ü®i, his, hi‚ü© := exists_ne_zero_of_sum_ne_zero this
      rw [‚Üê hx i his hi]
      exact hz i his

theorem arith_mean_weighted_of_constant (w z : Œπ ‚Üí ‚Ñù) (x : ‚Ñù) (hw' : ‚àë i ‚àà s, w i = 1)
    (hx : ‚àÄ i ‚àà s, w i ‚â† 0 ‚Üí z i = x) : ‚àë i ‚àà s, w i * z i = x :=
  calc
    ‚àë i ‚àà s, w i * z i = ‚àë i ‚àà s, w i * x := by
      refine sum_congr rfl fun i hi => ?_
      rcases eq_or_ne (w i) 0 with hwi | hwi
      ¬∑ rw [hwi, zero_mul, zero_mul]
      ¬∑ rw [hx i hi hwi]
    _ = x := by rw [‚Üê sum_mul, hw', one_mul]

theorem geom_mean_eq_arith_mean_weighted_of_constant (w z : Œπ ‚Üí ‚Ñù) (x : ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i ‚àà s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) (hx : ‚àÄ i ‚àà s, w i ‚â† 0 ‚Üí z i = x) :
    ‚àè i ‚àà s, z i ^ w i = ‚àë i ‚àà s, w i * z i := by
  rw [geom_mean_weighted_of_constant, arith_mean_weighted_of_constant] <;> assumption

end Real

namespace NNReal

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for `NNReal`-valued functions. -/
theorem geom_mean_le_arith_mean_weighted (w z : Œπ ‚Üí ‚Ñù‚â•0) (hw' : ‚àë i ‚àà s, w i = 1) :
    (‚àè i ‚àà s, z i ^ (w i : ‚Ñù)) ‚â§ ‚àë i ‚àà s, w i * z i :=
  mod_cast
    Real.geom_mean_le_arith_mean_weighted _ _ _ (fun i _ => (w i).coe_nonneg)
      (by assumption_mod_cast) fun i _ => (z i).coe_nonneg

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for two `NNReal` numbers. -/
theorem geom_mean_le_arith_mean2_weighted (w‚ÇÅ w‚ÇÇ p‚ÇÅ p‚ÇÇ : ‚Ñù‚â•0) :
    w‚ÇÅ + w‚ÇÇ = 1 ‚Üí p‚ÇÅ ^ (w‚ÇÅ : ‚Ñù) * p‚ÇÇ ^ (w‚ÇÇ : ‚Ñù) ‚â§ w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one] using
    geom_mean_le_arith_mean_weighted univ ![w‚ÇÅ, w‚ÇÇ] ![p‚ÇÅ, p‚ÇÇ]

theorem geom_mean_le_arith_mean3_weighted (w‚ÇÅ w‚ÇÇ w‚ÇÉ p‚ÇÅ p‚ÇÇ p‚ÇÉ : ‚Ñù‚â•0) :
    w‚ÇÅ + w‚ÇÇ + w‚ÇÉ = 1 ‚Üí
      p‚ÇÅ ^ (w‚ÇÅ : ‚Ñù) * p‚ÇÇ ^ (w‚ÇÇ : ‚Ñù) * p‚ÇÉ ^ (w‚ÇÉ : ‚Ñù) ‚â§ w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ + w‚ÇÉ * p‚ÇÉ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one, ‚Üê add_assoc,
    mul_assoc] using geom_mean_le_arith_mean_weighted univ ![w‚ÇÅ, w‚ÇÇ, w‚ÇÉ] ![p‚ÇÅ, p‚ÇÇ, p‚ÇÉ]

theorem geom_mean_le_arith_mean4_weighted (w‚ÇÅ w‚ÇÇ w‚ÇÉ w‚ÇÑ p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : ‚Ñù‚â•0) :
    w‚ÇÅ + w‚ÇÇ + w‚ÇÉ + w‚ÇÑ = 1 ‚Üí
      p‚ÇÅ ^ (w‚ÇÅ : ‚Ñù) * p‚ÇÇ ^ (w‚ÇÇ : ‚Ñù) * p‚ÇÉ ^ (w‚ÇÉ : ‚Ñù) * p‚ÇÑ ^ (w‚ÇÑ : ‚Ñù) ‚â§
        w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ + w‚ÇÉ * p‚ÇÉ + w‚ÇÑ * p‚ÇÑ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one, ‚Üê add_assoc,
    mul_assoc] using geom_mean_le_arith_mean_weighted univ ![w‚ÇÅ, w‚ÇÇ, w‚ÇÉ, w‚ÇÑ] ![p‚ÇÅ, p‚ÇÇ, p‚ÇÉ, p‚ÇÑ]

end NNReal

namespace Real

theorem geom_mean_le_arith_mean2_weighted {w‚ÇÅ w‚ÇÇ p‚ÇÅ p‚ÇÇ : ‚Ñù} (hw‚ÇÅ : 0 ‚â§ w‚ÇÅ) (hw‚ÇÇ : 0 ‚â§ w‚ÇÇ)
    (hp‚ÇÅ : 0 ‚â§ p‚ÇÅ) (hp‚ÇÇ : 0 ‚â§ p‚ÇÇ) (hw : w‚ÇÅ + w‚ÇÇ = 1) : p‚ÇÅ ^ w‚ÇÅ * p‚ÇÇ ^ w‚ÇÇ ‚â§ w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ :=
  NNReal.geom_mean_le_arith_mean2_weighted ‚ü®w‚ÇÅ, hw‚ÇÅ‚ü© ‚ü®w‚ÇÇ, hw‚ÇÇ‚ü© ‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© ‚ü®p‚ÇÇ, hp‚ÇÇ‚ü© <|
    NNReal.coe_inj.1 <| by assumption

theorem geom_mean_le_arith_mean3_weighted {w‚ÇÅ w‚ÇÇ w‚ÇÉ p‚ÇÅ p‚ÇÇ p‚ÇÉ : ‚Ñù} (hw‚ÇÅ : 0 ‚â§ w‚ÇÅ) (hw‚ÇÇ : 0 ‚â§ w‚ÇÇ)
    (hw‚ÇÉ : 0 ‚â§ w‚ÇÉ) (hp‚ÇÅ : 0 ‚â§ p‚ÇÅ) (hp‚ÇÇ : 0 ‚â§ p‚ÇÇ) (hp‚ÇÉ : 0 ‚â§ p‚ÇÉ) (hw : w‚ÇÅ + w‚ÇÇ + w‚ÇÉ = 1) :
    p‚ÇÅ ^ w‚ÇÅ * p‚ÇÇ ^ w‚ÇÇ * p‚ÇÉ ^ w‚ÇÉ ‚â§ w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ + w‚ÇÉ * p‚ÇÉ :=
  NNReal.geom_mean_le_arith_mean3_weighted ‚ü®w‚ÇÅ, hw‚ÇÅ‚ü© ‚ü®w‚ÇÇ, hw‚ÇÇ‚ü© ‚ü®w‚ÇÉ, hw‚ÇÉ‚ü© ‚ü®p‚ÇÅ, hp‚ÇÅ‚ü© ‚ü®p‚ÇÇ, hp‚ÇÇ‚ü©
      ‚ü®p‚ÇÉ, hp‚ÇÉ‚ü© <|
    NNReal.coe_inj.1 hw

theorem geom_mean_le_arith_mean4_weighted {w‚ÇÅ w‚ÇÇ w‚ÇÉ w‚ÇÑ p‚ÇÅ p‚ÇÇ p‚ÇÉ p‚ÇÑ : ‚Ñù} (hw‚ÇÅ : 0 ‚â§ w‚ÇÅ)
    (hw‚ÇÇ : 0 ‚â§ w‚ÇÇ) (hw‚ÇÉ : 0 ‚â§ w‚ÇÉ) (hw‚ÇÑ : 0 ‚â§ w‚ÇÑ) (hp‚ÇÅ : 0 ‚â§ p‚ÇÅ) (hp‚ÇÇ : 0 ‚â§ p‚ÇÇ) (hp‚ÇÉ : 0 ‚â§ p‚ÇÉ)
    (hp‚ÇÑ : 0 ‚â§ p‚ÇÑ) (hw : w‚ÇÅ + w‚ÇÇ + w‚ÇÉ + w‚ÇÑ = 1) :
    p‚ÇÅ ^ w‚ÇÅ * p‚ÇÇ ^ w‚ÇÇ * p‚ÇÉ ^ w‚ÇÉ * p‚ÇÑ ^ w‚ÇÑ ‚â§ w‚ÇÅ * p‚ÇÅ + w‚ÇÇ * p‚ÇÇ + w‚ÇÉ * p‚ÇÉ + w‚ÇÑ * p‚ÇÑ :=
  NNReal.geom_mean_le_arith_mean4_weighted ‚ü®w‚ÇÅ, hw‚ÇÅ‚ü© ‚ü®w‚ÇÇ, hw‚ÇÇ‚ü© ‚ü®w‚ÇÉ, hw‚ÇÉ‚ü© ‚ü®w‚ÇÑ, hw‚ÇÑ‚ü© ‚ü®p‚ÇÅ, hp‚ÇÅ‚ü©
      ‚ü®p‚ÇÇ, hp‚ÇÇ‚ü© ‚ü®p‚ÇÉ, hp‚ÇÉ‚ü© ‚ü®p‚ÇÑ, hp‚ÇÑ‚ü© <|
    NNReal.coe_inj.1 <| by assumption

end Real

end GeomMeanLEArithMean

section HarmMeanLEGeomMean

/-! ### HM-GM inequality -/

namespace Real

/-- **HM-GM inequality**: The harmonic mean is less than or equal to the geometric mean, weighted
version for real-valued nonnegative functions. -/
theorem harm_mean_le_geom_mean_weighted (w z : Œπ ‚Üí ‚Ñù) (hs : s.Nonempty) (hw : ‚àÄ i ‚àà s, 0 < w i)
    (hw' : ‚àë i in s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 < z i) :
    (‚àë i in s, w i / z i)‚Åª¬π ‚â§ ‚àè i in s, z i ^ w i  := by
    have : ‚àè i in s, (1 / z) i ^ w i ‚â§ ‚àë i in s, w i * (1 / z) i :=
      geom_mean_le_arith_mean_weighted s w (1/z) (fun i hi ‚Ü¶ le_of_lt (hw i hi)) hw'
      (fun i hi ‚Ü¶ one_div_nonneg.2 (le_of_lt (hz i hi)))
    have p_pos : 0 < ‚àè i in s, (z i)‚Åª¬π ^ w i :=
      prod_pos fun i hi => rpow_pos_of_pos (inv_pos.2 (hz i hi)) _
    have s_pos : 0 < ‚àë i in s, w i * (z i)‚Åª¬π :=
      sum_pos (fun i hi => mul_pos (hw i hi) (inv_pos.2 (hz i hi))) hs
    norm_num at this
    rw [‚Üê inv_le_inv‚ÇÄ s_pos p_pos] at this
    apply le_trans this
    have p_pos‚ÇÇ : 0 < (‚àè i in s, (z i) ^ w i)‚Åª¬π :=
      inv_pos.2 (prod_pos fun i hi => rpow_pos_of_pos ((hz i hi)) _ )
    rw [‚Üê inv_inv (‚àè i in s, z i ^ w i), inv_le_inv‚ÇÄ p_pos p_pos‚ÇÇ, ‚Üê Finset.prod_inv_distrib]
    gcongr
    ¬∑ exact fun i hi ‚Ü¶ inv_nonneg.mpr (Real.rpow_nonneg (le_of_lt (hz i hi)) _)
    ¬∑ rw [Real.inv_rpow]; apply fun i hi ‚Ü¶ le_of_lt (hz i hi); assumption


/-- **HM-GM inequality**: The **harmonic mean is less than or equal to the geometric mean. --/
theorem harm_mean_le_geom_mean {Œπ : Type*} (s : Finset Œπ) (hs : s.Nonempty) (w : Œπ ‚Üí ‚Ñù)
    (z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 < w i) (hw' : 0 < ‚àë i in s, w i) (hz : ‚àÄ i ‚àà s, 0 < z i) :
    (‚àë i in s, w i) / (‚àë i in s, w i / z i) ‚â§ (‚àè i in s, z i ^ w i) ^ (‚àë i in s, w i)‚Åª¬π := by
  have := harm_mean_le_geom_mean_weighted s (fun i => (w i) / ‚àë i in s, w i) z hs ?_ ?_ hz
  ¬∑ simp only at this
    set n := ‚àë i in s, w i
    nth_rw 1 [div_eq_mul_inv, (show n = (n‚Åª¬π)‚Åª¬π by norm_num), ‚Üê mul_inv, Finset.mul_sum _ _ n‚Åª¬π]
    simp_rw [inv_mul_eq_div n ((w _)/(z _)), div_right_comm _ _ n]
    convert this
    rw [‚Üê Real.finset_prod_rpow s _ (fun i hi ‚Ü¶ Real.rpow_nonneg (le_of_lt <| hz i hi) _)]
    refine Finset.prod_congr rfl (fun i hi => ?_)
    rw [‚Üê Real.rpow_mul (le_of_lt <| hz i hi) (w _) n‚Åª¬π, div_eq_mul_inv (w _) n]
  ¬∑ exact fun i hi ‚Ü¶ div_pos (hw i hi) hw'
  ¬∑ simp_rw [div_eq_mul_inv (w _) (‚àë i in s, w i), ‚Üê Finset.sum_mul _ _ (‚àë i in s, w i)‚Åª¬π]
    exact mul_inv_cancel‚ÇÄ hw'.ne'

end Real

end HarmMeanLEGeomMean


section Young

/-! ### Young's inequality -/


namespace Real

/-- **Young's inequality**, a version for nonnegative real numbers. -/
theorem young_inequality_of_nonneg {a b p q : ‚Ñù} (ha : 0 ‚â§ a) (hb : 0 ‚â§ b)
    (hpq : p.IsConjExponent q) : a * b ‚â§ a ^ p / p + b ^ q / q := by
  simpa [‚Üê rpow_mul, ha, hb, hpq.ne_zero, hpq.symm.ne_zero, _root_.div_eq_inv_mul] using
    geom_mean_le_arith_mean2_weighted hpq.inv_nonneg hpq.symm.inv_nonneg
      (rpow_nonneg ha p) (rpow_nonneg hb q) hpq.inv_add_inv_conj

/-- **Young's inequality**, a version for arbitrary real numbers. -/
theorem young_inequality (a b : ‚Ñù) {p q : ‚Ñù} (hpq : p.IsConjExponent q) :
    a * b ‚â§ |a| ^ p / p + |b| ^ q / q :=
  calc
    a * b ‚â§ |a * b| := le_abs_self (a * b)
    _ = |a| * |b| := abs_mul a b
    _ ‚â§ |a| ^ p / p + |b| ^ q / q :=
      Real.young_inequality_of_nonneg (abs_nonneg a) (abs_nonneg b) hpq

end Real

namespace NNReal

/-- **Young's inequality**, `‚Ñù‚â•0` version. We use `{p q : ‚Ñù‚â•0}` in order to avoid constructing
witnesses of `0 ‚â§ p` and `0 ‚â§ q` for the denominators. -/
theorem young_inequality (a b : ‚Ñù‚â•0) {p q : ‚Ñù‚â•0} (hpq : p.IsConjExponent q) :
    a * b ‚â§ a ^ (p : ‚Ñù) / p + b ^ (q : ‚Ñù) / q :=
  Real.young_inequality_of_nonneg a.coe_nonneg b.coe_nonneg hpq.coe

/-- **Young's inequality**, `‚Ñù‚â•0` version with real conjugate exponents. -/
theorem young_inequality_real (a b : ‚Ñù‚â•0) {p q : ‚Ñù} (hpq : p.IsConjExponent q) :
    a * b ‚â§ a ^ p / Real.toNNReal p + b ^ q / Real.toNNReal q := by
  simpa [Real.coe_toNNReal, hpq.nonneg, hpq.symm.nonneg] using young_inequality a b hpq.toNNReal

end NNReal

namespace ENNReal

/-- **Young's inequality**, `‚Ñù‚â•0‚àû` version with real conjugate exponents. -/
theorem young_inequality (a b : ‚Ñù‚â•0‚àû) {p q : ‚Ñù} (hpq : p.IsConjExponent q) :
    a * b ‚â§ a ^ p / ENNReal.ofReal p + b ^ q / ENNReal.ofReal q := by
  by_cases h : a = ‚ä§ ‚à® b = ‚ä§
  ¬∑ refine le_trans le_top (le_of_eq ?_)
    repeat rw [div_eq_mul_inv]
    cases' h with h h <;> rw [h] <;> simp [h, hpq.pos, hpq.symm.pos]
  push_neg at h
  -- if a ‚â† ‚ä§ and b ‚â† ‚ä§, use the nnreal version: nnreal.young_inequality_real
  rw [‚Üê coe_toNNReal h.left, ‚Üê coe_toNNReal h.right, ‚Üê coe_mul, ‚Üê coe_rpow_of_nonneg _ hpq.nonneg,
    ‚Üê coe_rpow_of_nonneg _ hpq.symm.nonneg, ENNReal.ofReal, ENNReal.ofReal, ‚Üê
    @coe_div (Real.toNNReal p) _ (by simp [hpq.pos]), ‚Üê
    @coe_div (Real.toNNReal q) _ (by simp [hpq.symm.pos]), ‚Üê coe_add, coe_le_coe]
  exact NNReal.young_inequality_real a.toNNReal b.toNNReal hpq

end ENNReal

end Young

section HoelderMinkowski

/-! ### H√∂lder's and Minkowski's inequalities -/


namespace NNReal

private theorem inner_le_Lp_mul_Lp_of_norm_le_one (f g : Œπ ‚Üí ‚Ñù‚â•0) {p q : ‚Ñù}
    (hpq : p.IsConjExponent q) (hf : ‚àë i ‚àà s, f i ^ p ‚â§ 1) (hg : ‚àë i ‚àà s, g i ^ q ‚â§ 1) :
    ‚àë i ‚àà s, f i * g i ‚â§ 1 := by
  have hp : 0 < p.toNNReal := zero_lt_one.trans hpq.toNNReal.one_lt
  have hq : 0 < q.toNNReal := zero_lt_one.trans hpq.toNNReal.symm.one_lt
  calc
    ‚àë i ‚àà s, f i * g i ‚â§ ‚àë i ‚àà s, (f i ^ p / Real.toNNReal p + g i ^ q / Real.toNNReal q) :=
      Finset.sum_le_sum fun i _ => young_inequality_real (f i) (g i) hpq
    _ = (‚àë i ‚àà s, f i ^ p) / Real.toNNReal p + (‚àë i ‚àà s, g i ^ q) / Real.toNNReal q := by
      rw [sum_add_distrib, sum_div, sum_div]
    _ ‚â§ 1 / Real.toNNReal p + 1 / Real.toNNReal q := by
      refine add_le_add ?_ ?_ <;> rwa [div_le_iff‚ÇÄ, div_mul_cancel‚ÇÄ] <;> positivity
    _ = 1 := by simp_rw [one_div, hpq.toNNReal.inv_add_inv_conj]

private theorem inner_le_Lp_mul_Lp_of_norm_eq_zero (f g : Œπ ‚Üí ‚Ñù‚â•0) {p q : ‚Ñù}
    (hpq : p.IsConjExponent q) (hf : ‚àë i ‚àà s, f i ^ p = 0) :
    ‚àë i ‚àà s, f i * g i ‚â§ (‚àë i ‚àà s, f i ^ p) ^ (1 / p) * (‚àë i ‚àà s, g i ^ q) ^ (1 / q) := by
  simp only [hf, hpq.ne_zero, one_div, sum_eq_zero_iff, zero_rpow, zero_mul,
    inv_eq_zero, Ne, not_false_iff, le_zero_iff, mul_eq_zero]
  intro i his
  left
  rw [sum_eq_zero_iff] at hf
  exact (rpow_eq_zero_iff.mp (hf i his)).left

/-- **H√∂lder inequality**: The scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with `‚Ñù‚â•0`-valued functions. -/
theorem inner_le_Lp_mul_Lq (f g : Œπ ‚Üí ‚Ñù‚â•0) {p q : ‚Ñù} (hpq : p.IsConjExponent q) :
    ‚àë i ‚àà s, f i * g i ‚â§ (‚àë i ‚àà s, f i ^ p) ^ (1 / p) * (‚àë i ‚àà s, g i ^ q) ^ (1 / q) := by
  obtain hf | hf := eq_zero_or_pos (‚àë i ‚àà s, f i ^ p)
  ¬∑ exact inner_le_Lp_mul_Lp_of_norm_eq_zero s f g hpq hf
  obtain hg | hg := eq_zero_or_pos (‚àë i ‚àà s, g i ^ q)
  ¬∑ calc
      ‚àë i ‚àà s, f i * g i = ‚àë i ‚àà s, g i * f i := by
        congr with i
        rw [mul_comm]
      _ ‚â§ (‚àë i ‚àà s, g i ^ q) ^ (1 / q) * (‚àë i ‚àà s, f i ^ p) ^ (1 / p) :=
        (inner_le_Lp_mul_Lp_of_norm_eq_zero s g f hpq.symm hg)
      _ = (‚àë i ‚àà s, f i ^ p) ^ (1 / p) * (‚àë i ‚àà s, g i ^ q) ^ (1 / q) := mul_comm _ _
  let f' i := f i / (‚àë i ‚àà s, f i ^ p) ^ (1 / p)
  let g' i := g i / (‚àë i ‚àà s, g i ^ q) ^ (1 / q)
  suffices (‚àë i ‚àà s, f' i * g' i) ‚â§ 1 by
    simp_rw [f', g', div_mul_div_comm, ‚Üê sum_div] at this
    rwa [div_le_iff‚ÇÄ, one_mul] at this
    -- TODO: We are missing a positivity  extension here
    exact mul_pos (rpow_pos hf) (rpow_pos hg)
  refine inner_le_Lp_mul_Lp_of_norm_le_one s f' g' hpq (le_of_eq ?_) (le_of_eq ?_)
  ¬∑ simp_rw [f', div_rpow, ‚Üê sum_div, ‚Üê rpow_mul, one_div, inv_mul_cancel‚ÇÄ hpq.ne_zero, rpow_one,
      div_self hf.ne']
  ¬∑ simp_rw [g', div_rpow, ‚Üê sum_div, ‚Üê rpow_mul, one_div, inv_mul_cancel‚ÇÄ hpq.symm.ne_zero,
      rpow_one, div_self hg.ne']

/-- **Weighted H√∂lder inequality**. -/
lemma inner_le_weight_mul_Lp (s : Finset Œπ) {p : ‚Ñù} (hp : 1 ‚â§ p) (w f : Œπ ‚Üí ‚Ñù‚â•0) :
    ‚àë i ‚àà s, w i * f i ‚â§ (‚àë i ‚àà s, w i) ^ (1 - p‚Åª¬π) * (‚àë i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π := by
  obtain rfl | hp := hp.eq_or_lt
  ¬∑ simp
  calc
    _ = ‚àë i ‚àà s, w i ^ (1 - p‚Åª¬π) * (w i ^ p‚Åª¬π * f i) := ?_
    _ ‚â§ (‚àë i ‚àà s, (w i ^ (1 - p‚Åª¬π)) ^ (1 - p‚Åª¬π)‚Åª¬π) ^ (1 / (1 - p‚Åª¬π)‚Åª¬π) *
          (‚àë i ‚àà s, (w i ^ p‚Åª¬π * f i) ^ p) ^ (1 / p) :=
        inner_le_Lp_mul_Lq _ _ _ (.symm ‚ü®hp, by simp‚ü©)
    _ = _ := ?_
  ¬∑ congr with i
    rw [‚Üê mul_assoc, ‚Üê rpow_of_add_eq _ one_ne_zero, rpow_one]
    simp
  ¬∑ have hp‚ÇÄ : p ‚â† 0 := by positivity
    have hp‚ÇÅ : 1 - p‚Åª¬π ‚â† 0 := by simp [sub_eq_zero, hp.ne']
    simp [mul_rpow, div_inv_eq_mul, one_mul, one_div, hp‚ÇÄ, hp‚ÇÅ]

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_hasSum`. -/
theorem inner_le_Lp_mul_Lq_tsum {f g : Œπ ‚Üí ‚Ñù‚â•0} {p q : ‚Ñù} (hpq : p.IsConjExponent q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    (Summable fun i => f i * g i) ‚àß
      ‚àë' i, f i * g i ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q) := by
  have H‚ÇÅ : ‚àÄ s : Finset Œπ,
      ‚àë i ‚àà s, f i * g i ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q) := by
    intro s
    refine le_trans (inner_le_Lp_mul_Lq s f g hpq) (mul_le_mul ?_ ?_ bot_le bot_le)
    ¬∑ rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr hpq.pos)]
      exact sum_le_tsum _ (fun _ _ => zero_le _) hf
    ¬∑ rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr hpq.symm.pos)]
      exact sum_le_tsum _ (fun _ _ => zero_le _) hg
  have bdd : BddAbove (Set.range fun s => ‚àë i ‚àà s, f i * g i) := by
    refine ‚ü®(‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q), ?_‚ü©
    rintro a ‚ü®s, rfl‚ü©
    exact H‚ÇÅ s
  have H‚ÇÇ : Summable _ := (hasSum_of_isLUB _ (isLUB_ciSup bdd)).summable
  exact ‚ü®H‚ÇÇ, tsum_le_of_sum_le H‚ÇÇ H‚ÇÅ‚ü©

theorem summable_mul_of_Lp_Lq {f g : Œπ ‚Üí ‚Ñù‚â•0} {p q : ‚Ñù} (hpq : p.IsConjExponent q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    Summable fun i => f i * g i :=
  (inner_le_Lp_mul_Lq_tsum hpq hf hg).1

theorem inner_le_Lp_mul_Lq_tsum' {f g : Œπ ‚Üí ‚Ñù‚â•0} {p q : ‚Ñù} (hpq : p.IsConjExponent q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    ‚àë' i, f i * g i ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q) :=
  (inner_le_Lp_mul_Lq_tsum hpq hf hg).2

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are not already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_tsum`. -/
theorem inner_le_Lp_mul_Lq_hasSum {f g : Œπ ‚Üí ‚Ñù‚â•0} {A B : ‚Ñù‚â•0} {p q : ‚Ñù}
    (hpq : p.IsConjExponent q) (hf : HasSum (fun i => f i ^ p) (A ^ p))
    (hg : HasSum (fun i => g i ^ q) (B ^ q)) : ‚àÉ C, C ‚â§ A * B ‚àß HasSum (fun i => f i * g i) C := by
  obtain ‚ü®H‚ÇÅ, H‚ÇÇ‚ü© := inner_le_Lp_mul_Lq_tsum hpq hf.summable hg.summable
  have hA : A = (‚àë' i : Œπ, f i ^ p) ^ (1 / p) := by rw [hf.tsum_eq, rpow_inv_rpow_self hpq.ne_zero]
  have hB : B = (‚àë' i : Œπ, g i ^ q) ^ (1 / q) := by
    rw [hg.tsum_eq, rpow_inv_rpow_self hpq.symm.ne_zero]
  refine ‚ü®‚àë' i, f i * g i, ?_, ?_‚ü©
  ¬∑ simpa [hA, hB] using H‚ÇÇ
  ¬∑ simpa only [rpow_self_rpow_inv hpq.ne_zero] using H‚ÇÅ.hasSum

/-- For `1 ‚â§ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `‚Ñù‚â•0`-valued functions.
-/
theorem rpow_sum_le_const_mul_sum_rpow (f : Œπ ‚Üí ‚Ñù‚â•0) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, f i) ^ p ‚â§ (#s : ‚Ñù‚â•0) ^ (p - 1) * ‚àë i ‚àà s, f i ^ p := by
  cases' eq_or_lt_of_le hp with hp hp
  ¬∑ simp [‚Üê hp]
  let q : ‚Ñù := p / (p - 1)
  have hpq : p.IsConjExponent q := .conjExponent hp
  have hp‚ÇÅ : 1 / p * p = 1 := one_div_mul_cancel hpq.ne_zero
  have hq : 1 / q * p = p - 1 := by
    rw [‚Üê hpq.div_conj_eq_sub_one]
    ring
  simpa only [NNReal.mul_rpow, ‚Üê NNReal.rpow_mul, hp‚ÇÅ, hq, one_mul, one_rpow, rpow_one,
    Pi.one_apply, sum_const, Nat.smul_one_eq_cast] using
    NNReal.rpow_le_rpow (inner_le_Lp_mul_Lq s 1 f hpq.symm) hpq.nonneg

/-- The `L_p` seminorm of a vector `f` is the greatest value of the inner product
`‚àë i ‚àà s, f i * g i` over functions `g` of `L_q` seminorm less than or equal to one. -/
theorem isGreatest_Lp (f : Œπ ‚Üí ‚Ñù‚â•0) {p q : ‚Ñù} (hpq : p.IsConjExponent q) :
    IsGreatest ((fun g : Œπ ‚Üí ‚Ñù‚â•0 => ‚àë i ‚àà s, f i * g i) '' { g | ‚àë i ‚àà s, g i ^ q ‚â§ 1 })
      ((‚àë i ‚àà s, f i ^ p) ^ (1 / p)) := by
  constructor
  ¬∑ use fun i => f i ^ p / f i / (‚àë i ‚àà s, f i ^ p) ^ (1 / q)
    obtain hf | hf := eq_zero_or_pos (‚àë i ‚àà s, f i ^ p)
    ¬∑ simp [hf, hpq.ne_zero, hpq.symm.ne_zero]
    ¬∑ have A : p + q - q ‚â† 0 := by simp [hpq.ne_zero]
      have B : ‚àÄ y : ‚Ñù‚â•0, y * y ^ p / y = y ^ p := by
        refine fun y => mul_div_cancel_left_of_imp fun h => ?_
        simp [h, hpq.ne_zero]
      simp only [Set.mem_setOf_eq, div_rpow, ‚Üê sum_div, ‚Üê rpow_mul,
        div_mul_cancel‚ÇÄ _ hpq.symm.ne_zero, rpow_one, div_le_iff‚ÇÄ hf, one_mul, hpq.mul_eq_add, ‚Üê
        rpow_sub' A, add_sub_cancel_right, le_refl, true_and, ‚Üê mul_div_assoc, B]
      rw [div_eq_iff, ‚Üê rpow_add hf.ne', one_div, one_div, hpq.inv_add_inv_conj, rpow_one]
      simpa [hpq.symm.ne_zero] using hf.ne'
  ¬∑ rintro _ ‚ü®g, hg, rfl‚ü©
    apply le_trans (inner_le_Lp_mul_Lq s f g hpq)
    simpa only [mul_one] using
      mul_le_mul_left' (NNReal.rpow_le_one hg (le_of_lt hpq.symm.one_div_pos)) _

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `NNReal`-valued functions. -/
theorem Lp_add_le (f g : Œπ ‚Üí ‚Ñù‚â•0) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, (f i + g i) ^ p) ^ (1 / p) ‚â§
      (‚àë i ‚àà s, f i ^ p) ^ (1 / p) + (‚àë i ‚àà s, g i ^ p) ^ (1 / p) := by
  -- The result is trivial when `p = 1`, so we can assume `1 < p`.
  rcases eq_or_lt_of_le hp with (rfl | hp)
  ¬∑ simp [Finset.sum_add_distrib]
  have hpq := Real.IsConjExponent.conjExponent hp
  have := isGreatest_Lp s (f + g) hpq
  simp only [Pi.add_apply, add_mul, sum_add_distrib] at this
  rcases this.1 with ‚ü®œÜ, hœÜ, H‚ü©
  rw [‚Üê H]
  exact
    add_le_add ((isGreatest_Lp s f hpq).2 ‚ü®œÜ, hœÜ, rfl‚ü©) ((isGreatest_Lp s g hpq).2 ‚ü®œÜ, hœÜ, rfl‚ü©)

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `NNReal`-valued functions. For an alternative version, convenient if the
infinite sums are already expressed as `p`-th powers, see `Lp_add_le_hasSum_of_nonneg`. -/
theorem Lp_add_le_tsum {f g : Œπ ‚Üí ‚Ñù‚â•0} {p : ‚Ñù} (hp : 1 ‚â§ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) :
    (Summable fun i => (f i + g i) ^ p) ‚àß
      (‚àë' i, (f i + g i) ^ p) ^ (1 / p) ‚â§
        (‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p) := by
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp
  have H‚ÇÅ : ‚àÄ s : Finset Œπ,
      (‚àë i ‚àà s, (f i + g i) ^ p) ‚â§
        ((‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p)) ^ p := by
    intro s
    rw [one_div, ‚Üê NNReal.rpow_inv_le_iff pos, ‚Üê one_div]
    refine le_trans (Lp_add_le s f g hp) (add_le_add ?_ ?_) <;>
        rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr pos)] <;>
      refine sum_le_tsum _ (fun _ _ => zero_le _) ?_
    exacts [hf, hg]
  have bdd : BddAbove (Set.range fun s => ‚àë i ‚àà s, (f i + g i) ^ p) := by
    refine ‚ü®((‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p)) ^ p, ?_‚ü©
    rintro a ‚ü®s, rfl‚ü©
    exact H‚ÇÅ s
  have H‚ÇÇ : Summable _ := (hasSum_of_isLUB _ (isLUB_ciSup bdd)).summable
  refine ‚ü®H‚ÇÇ, ?_‚ü©
  rw [one_div, NNReal.rpow_inv_le_iff pos, ‚Üê one_div]
  exact tsum_le_of_sum_le H‚ÇÇ H‚ÇÅ

theorem summable_Lp_add {f g : Œπ ‚Üí ‚Ñù‚â•0} {p : ‚Ñù} (hp : 1 ‚â§ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) : Summable fun i => (f i + g i) ^ p :=
  (Lp_add_le_tsum hp hf hg).1

theorem Lp_add_le_tsum' {f g : Œπ ‚Üí ‚Ñù‚â•0} {p : ‚Ñù} (hp : 1 ‚â§ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) :
    (‚àë' i, (f i + g i) ^ p) ^ (1 / p) ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p) :=
  (Lp_add_le_tsum hp hf hg).2

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `NNReal`-valued functions. For an alternative version, convenient if the
infinite sums are not already expressed as `p`-th powers, see `Lp_add_le_tsum_of_nonneg`. -/
theorem Lp_add_le_hasSum {f g : Œπ ‚Üí ‚Ñù‚â•0} {A B : ‚Ñù‚â•0} {p : ‚Ñù} (hp : 1 ‚â§ p)
    (hf : HasSum (fun i => f i ^ p) (A ^ p)) (hg : HasSum (fun i => g i ^ p) (B ^ p)) :
    ‚àÉ C, C ‚â§ A + B ‚àß HasSum (fun i => (f i + g i) ^ p) (C ^ p) := by
  have hp' : p ‚â† 0 := (lt_of_lt_of_le zero_lt_one hp).ne'
  obtain ‚ü®H‚ÇÅ, H‚ÇÇ‚ü© := Lp_add_le_tsum hp hf.summable hg.summable
  have hA : A = (‚àë' i : Œπ, f i ^ p) ^ (1 / p) := by rw [hf.tsum_eq, rpow_inv_rpow_self hp']
  have hB : B = (‚àë' i : Œπ, g i ^ p) ^ (1 / p) := by rw [hg.tsum_eq, rpow_inv_rpow_self hp']
  refine ‚ü®(‚àë' i, (f i + g i) ^ p) ^ (1 / p), ?_, ?_‚ü©
  ¬∑ simpa [hA, hB] using H‚ÇÇ
  ¬∑ simpa only [rpow_self_rpow_inv hp'] using H‚ÇÅ.hasSum

end NNReal

namespace Real

variable (f g : Œπ ‚Üí ‚Ñù) {p q : ‚Ñù}

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with real-valued functions. -/
theorem inner_le_Lp_mul_Lq (hpq : IsConjExponent p q) :
    ‚àë i ‚àà s, f i * g i ‚â§ (‚àë i ‚àà s, |f i| ^ p) ^ (1 / p) * (‚àë i ‚àà s, |g i| ^ q) ^ (1 / q) := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.inner_le_Lp_mul_Lq s (fun i => ‚ü®_, abs_nonneg (f i)‚ü©) (fun i => ‚ü®_, abs_nonneg (g i)‚ü©)
        hpq)
  push_cast at this
  refine le_trans (sum_le_sum fun i _ => ?_) this
  simp only [‚Üê abs_mul, le_abs_self]

/-- For `1 ‚â§ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `‚Ñù`-valued functions. -/
theorem rpow_sum_le_const_mul_sum_rpow (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, |f i|) ^ p ‚â§ (#s : ‚Ñù) ^ (p - 1) * ‚àë i ‚àà s, |f i| ^ p := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.rpow_sum_le_const_mul_sum_rpow s (fun i => ‚ü®_, abs_nonneg (f i)‚ü©) hp)
  push_cast at this
  exact this

-- for some reason `exact_mod_cast` can't replace this argument
/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `Real`-valued functions. -/
theorem Lp_add_le (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, |f i + g i| ^ p) ^ (1 / p) ‚â§
      (‚àë i ‚àà s, |f i| ^ p) ^ (1 / p) + (‚àë i ‚àà s, |g i| ^ p) ^ (1 / p) := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.Lp_add_le s (fun i => ‚ü®_, abs_nonneg (f i)‚ü©) (fun i => ‚ü®_, abs_nonneg (g i)‚ü©) hp)
  push_cast at this
  refine le_trans (rpow_le_rpow ?_ (sum_le_sum fun i _ => ?_) ?_) this <;>
    simp [sum_nonneg, rpow_nonneg, abs_nonneg, le_trans zero_le_one hp, abs_add,
      rpow_le_rpow]

variable {f g}

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with real-valued nonnegative functions. -/
theorem inner_le_Lp_mul_Lq_of_nonneg (hpq : IsConjExponent p q) (hf : ‚àÄ i ‚àà s, 0 ‚â§ f i)
    (hg : ‚àÄ i ‚àà s, 0 ‚â§ g i) :
    ‚àë i ‚àà s, f i * g i ‚â§ (‚àë i ‚àà s, f i ^ p) ^ (1 / p) * (‚àë i ‚àà s, g i ^ q) ^ (1 / q) := by
  convert inner_le_Lp_mul_Lq s f g hpq using 3 <;> apply sum_congr rfl <;> intro i hi <;>
    simp only [abs_of_nonneg, hf i hi, hg i hi]

/-- **Weighted H√∂lder inequality**. -/
lemma inner_le_weight_mul_Lp_of_nonneg (s : Finset Œπ) {p : ‚Ñù} (hp : 1 ‚â§ p) (w f : Œπ ‚Üí ‚Ñù)
    (hw : ‚àÄ i, 0 ‚â§ w i) (hf : ‚àÄ i, 0 ‚â§ f i) :
    ‚àë i ‚àà s, w i * f i ‚â§ (‚àë i ‚àà s, w i) ^ (1 - p‚Åª¬π) * (‚àë i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π := by
  lift w to Œπ ‚Üí ‚Ñù‚â•0 using hw
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf
  beta_reduce at *
  norm_cast at *
  exact NNReal.inner_le_weight_mul_Lp _ hp _ _

/-- **Weighted H√∂lder inequality** in terms of `Finset.expect`. -/
lemma compact_inner_le_weight_mul_Lp_of_nonneg (s : Finset Œπ) {p : ‚Ñù} (hp : 1 ‚â§ p) {w f : Œπ ‚Üí ‚Ñù}
    (hw : ‚àÄ i, 0 ‚â§ w i) (hf : ‚àÄ i, 0 ‚â§ f i) :
    ùîº i ‚àà s, w i * f i ‚â§ (ùîº i ‚àà s, w i) ^ (1 - p‚Åª¬π) * (ùîº i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π := by
  simp_rw [expect_eq_sum_div_card]
  rw [div_rpow, div_rpow, div_mul_div_comm, ‚Üê rpow_add', sub_add_cancel, rpow_one]
  ¬∑ gcongr
    exact inner_le_weight_mul_Lp_of_nonneg s hp _ _ hw hf
  any_goals simp
  ¬∑ exact sum_nonneg fun i _ ‚Ü¶ by have := hw i; have := hf i; positivity
  ¬∑ exact sum_nonneg fun i _ ‚Ü¶ by have := hw i; positivity

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `‚Ñù`-valued functions.
For an alternative version, convenient if the infinite sums are already expressed as `p`-th powers,
see `inner_le_Lp_mul_Lq_hasSum_of_nonneg`. -/
theorem inner_le_Lp_mul_Lq_tsum_of_nonneg (hpq : p.IsConjExponent q) (hf : ‚àÄ i, 0 ‚â§ f i)
    (hg : ‚àÄ i, 0 ‚â§ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    (Summable fun i => f i * g i) ‚àß
      ‚àë' i, f i * g i ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q) := by
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf
  lift g to Œπ ‚Üí ‚Ñù‚â•0 using hg
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast at *
  exact NNReal.inner_le_Lp_mul_Lq_tsum hpq hf_sum hg_sum

theorem summable_mul_of_Lp_Lq_of_nonneg (hpq : p.IsConjExponent q) (hf : ‚àÄ i, 0 ‚â§ f i)
    (hg : ‚àÄ i, 0 ‚â§ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    Summable fun i => f i * g i :=
  (inner_le_Lp_mul_Lq_tsum_of_nonneg hpq hf hg hf_sum hg_sum).1

theorem inner_le_Lp_mul_Lq_tsum_of_nonneg' (hpq : p.IsConjExponent q) (hf : ‚àÄ i, 0 ‚â§ f i)
    (hg : ‚àÄ i, 0 ‚â§ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    ‚àë' i, f i * g i ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) * (‚àë' i, g i ^ q) ^ (1 / q) :=
  (inner_le_Lp_mul_Lq_tsum_of_nonneg hpq hf hg hf_sum hg_sum).2

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are not already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_tsum_of_nonneg`. -/
theorem inner_le_Lp_mul_Lq_hasSum_of_nonneg (hpq : p.IsConjExponent q) {A B : ‚Ñù} (hA : 0 ‚â§ A)
    (hB : 0 ‚â§ B) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i)
    (hf_sum : HasSum (fun i => f i ^ p) (A ^ p)) (hg_sum : HasSum (fun i => g i ^ q) (B ^ q)) :
    ‚àÉ C : ‚Ñù, 0 ‚â§ C ‚àß C ‚â§ A * B ‚àß HasSum (fun i => f i * g i) C := by
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf
  lift g to Œπ ‚Üí ‚Ñù‚â•0 using hg
  lift A to ‚Ñù‚â•0 using hA
  lift B to ‚Ñù‚â•0 using hB
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast at hf_sum hg_sum
  obtain ‚ü®C, hC, H‚ü© := NNReal.inner_le_Lp_mul_Lq_hasSum hpq hf_sum hg_sum
  refine ‚ü®C, C.prop, hC, ?_‚ü©
  norm_cast

/-- For `1 ‚â§ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with nonnegative `‚Ñù`-valued
functions. -/
theorem rpow_sum_le_const_mul_sum_rpow_of_nonneg (hp : 1 ‚â§ p) (hf : ‚àÄ i ‚àà s, 0 ‚â§ f i) :
    (‚àë i ‚àà s, f i) ^ p ‚â§ (#s : ‚Ñù) ^ (p - 1) * ‚àë i ‚àà s, f i ^ p := by
  convert rpow_sum_le_const_mul_sum_rpow s f hp using 2 <;> apply sum_congr rfl <;> intro i hi <;>
    simp only [abs_of_nonneg, hf i hi]

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `‚Ñù`-valued nonnegative
functions. -/
theorem Lp_add_le_of_nonneg (hp : 1 ‚â§ p) (hf : ‚àÄ i ‚àà s, 0 ‚â§ f i) (hg : ‚àÄ i ‚àà s, 0 ‚â§ g i) :
    (‚àë i ‚àà s, (f i + g i) ^ p) ^ (1 / p) ‚â§
      (‚àë i ‚àà s, f i ^ p) ^ (1 / p) + (‚àë i ‚àà s, g i ^ p) ^ (1 / p) := by
  convert Lp_add_le s f g hp using 2 <;> [skip;congr 1;congr 1] <;> apply sum_congr rfl <;>
      intro i hi <;>
    simp only [abs_of_nonneg, hf i hi, hg i hi, add_nonneg]

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `‚Ñù`-valued functions. For an alternative version, convenient if the infinite
sums are already expressed as `p`-th powers, see `Lp_add_le_hasSum_of_nonneg`. -/
theorem Lp_add_le_tsum_of_nonneg (hp : 1 ‚â§ p) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    (Summable fun i => (f i + g i) ^ p) ‚àß
      (‚àë' i, (f i + g i) ^ p) ^ (1 / p) ‚â§
        (‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p) := by
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf
  lift g to Œπ ‚Üí ‚Ñù‚â•0 using hg
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast0 at *
  exact NNReal.Lp_add_le_tsum hp hf_sum hg_sum

theorem summable_Lp_add_of_nonneg (hp : 1 ‚â§ p) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    Summable fun i => (f i + g i) ^ p :=
  (Lp_add_le_tsum_of_nonneg hp hf hg hf_sum hg_sum).1

theorem Lp_add_le_tsum_of_nonneg' (hp : 1 ‚â§ p) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    (‚àë' i, (f i + g i) ^ p) ^ (1 / p) ‚â§ (‚àë' i, f i ^ p) ^ (1 / p) + (‚àë' i, g i ^ p) ^ (1 / p) :=
  (Lp_add_le_tsum_of_nonneg hp hf hg hf_sum hg_sum).2

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `‚Ñù`-valued functions. For an alternative version, convenient if the infinite
sums are not already expressed as `p`-th powers, see `Lp_add_le_tsum_of_nonneg`. -/
theorem Lp_add_le_hasSum_of_nonneg (hp : 1 ‚â§ p) (hf : ‚àÄ i, 0 ‚â§ f i) (hg : ‚àÄ i, 0 ‚â§ g i) {A B : ‚Ñù}
    (hA : 0 ‚â§ A) (hB : 0 ‚â§ B) (hfA : HasSum (fun i => f i ^ p) (A ^ p))
    (hgB : HasSum (fun i => g i ^ p) (B ^ p)) :
    ‚àÉ C, 0 ‚â§ C ‚àß C ‚â§ A + B ‚àß HasSum (fun i => (f i + g i) ^ p) (C ^ p) := by
  lift f to Œπ ‚Üí ‚Ñù‚â•0 using hf
  lift g to Œπ ‚Üí ‚Ñù‚â•0 using hg
  lift A to ‚Ñù‚â•0 using hA
  lift B to ‚Ñù‚â•0 using hB
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at hfA hgB
  norm_cast at hfA hgB
  obtain ‚ü®C, hC‚ÇÅ, hC‚ÇÇ‚ü© := NNReal.Lp_add_le_hasSum hp hfA hgB
  use C
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce
  norm_cast
  exact ‚ü®zero_le _, hC‚ÇÅ, hC‚ÇÇ‚ü©

end Real

namespace ENNReal

variable (f g : Œπ ‚Üí ‚Ñù‚â•0‚àû) {p q : ‚Ñù}

/-- **H√∂lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with `‚Ñù‚â•0‚àû`-valued functions. -/
theorem inner_le_Lp_mul_Lq (hpq : p.IsConjExponent q) :
    ‚àë i ‚àà s, f i * g i ‚â§ (‚àë i ‚àà s, f i ^ p) ^ (1 / p) * (‚àë i ‚àà s, g i ^ q) ^ (1 / q) := by
  by_cases H : (‚àë i ‚àà s, f i ^ p) ^ (1 / p) = 0 ‚à® (‚àë i ‚àà s, g i ^ q) ^ (1 / q) = 0
  ¬∑ replace H : (‚àÄ i ‚àà s, f i = 0) ‚à® ‚àÄ i ‚àà s, g i = 0 := by
      simpa [ENNReal.rpow_eq_zero_iff, hpq.pos, hpq.symm.pos, asymm hpq.pos, asymm hpq.symm.pos,
        sum_eq_zero_iff_of_nonneg] using H
    have : ‚àÄ i ‚àà s, f i * g i = 0 := fun i hi => by cases' H with H H <;> simp [H i hi]
    simp [sum_eq_zero this]
  push_neg at H
  by_cases H' : (‚àë i ‚àà s, f i ^ p) ^ (1 / p) = ‚ä§ ‚à® (‚àë i ‚àà s, g i ^ q) ^ (1 / q) = ‚ä§
  ¬∑ cases' H' with H' H' <;> simp [H', -one_div, -sum_eq_zero_iff, -rpow_eq_zero_iff, H]
  replace H' : (‚àÄ i ‚àà s, f i ‚â† ‚ä§) ‚àß ‚àÄ i ‚àà s, g i ‚â† ‚ä§ := by
    simpa [ENNReal.rpow_eq_top_iff, asymm hpq.pos, asymm hpq.symm.pos, hpq.pos, hpq.symm.pos,
      ENNReal.sum_eq_top, not_or] using H'
  have := ENNReal.coe_le_coe.2 (@NNReal.inner_le_Lp_mul_Lq _ s (fun i => ENNReal.toNNReal (f i))
    (fun i => ENNReal.toNNReal (g i)) _ _ hpq)
  simp [ENNReal.coe_rpow_of_nonneg, hpq.pos.le, hpq.one_div_pos.le, hpq.symm.pos.le,
    hpq.symm.one_div_pos.le] at this
  convert this using 1 <;> [skip; congr 2] <;> [skip; skip; simp; skip; simp] <;>
    ¬∑ refine Finset.sum_congr rfl fun i hi => ?_
      simp [H'.1 i hi, H'.2 i hi, -WithZero.coe_mul]

/-- **Weighted H√∂lder inequality**. -/
lemma inner_le_weight_mul_Lp_of_nonneg (s : Finset Œπ) {p : ‚Ñù} (hp : 1 ‚â§ p) (w f : Œπ ‚Üí ‚Ñù‚â•0‚àû) :
    ‚àë i ‚àà s, w i * f i ‚â§ (‚àë i ‚àà s, w i) ^ (1 - p‚Åª¬π) * (‚àë i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π := by
  obtain rfl | hp := hp.eq_or_lt
  ¬∑ simp
  have hp‚ÇÄ : 0 < p := by positivity
  have hp‚ÇÅ : p‚Åª¬π < 1 := inv_lt_one_of_one_lt‚ÇÄ hp
  by_cases H : (‚àë i ‚àà s, w i) ^ (1 - p‚Åª¬π) = 0 ‚à® (‚àë i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π = 0
  ¬∑ replace H : (‚àÄ i ‚àà s, w i = 0) ‚à® ‚àÄ i ‚àà s, w i = 0 ‚à® f i = 0 := by
      simpa [hp‚ÇÄ, hp‚ÇÅ, hp‚ÇÄ.not_lt, hp‚ÇÅ.not_lt, sum_eq_zero_iff_of_nonneg] using H
    have (i) (hi : i ‚àà s) : w i * f i = 0 := by cases' H with H H <;> simp [H i hi]
    simp [sum_eq_zero this]
  push_neg at H
  by_cases H' : (‚àë i ‚àà s, w i) ^ (1 - p‚Åª¬π) = ‚ä§ ‚à® (‚àë i ‚àà s, w i * f i ^ p) ^ p‚Åª¬π = ‚ä§
  ¬∑ cases' H' with H' H' <;> simp [H', -one_div, -sum_eq_zero_iff, -rpow_eq_zero_iff, H]
  replace H' : (‚àÄ i ‚àà s, w i ‚â† ‚ä§) ‚àß ‚àÄ i ‚àà s, w i * f i ^ p ‚â† ‚ä§ := by
    simpa [rpow_eq_top_iff,hp‚ÇÄ, hp‚ÇÅ, hp‚ÇÄ.not_lt, hp‚ÇÅ.not_lt, sum_eq_top, not_or] using H'
  have := coe_le_coe.2 <| NNReal.inner_le_weight_mul_Lp s hp.le (fun i ‚Ü¶ ENNReal.toNNReal (w i))
    fun i ‚Ü¶ ENNReal.toNNReal (f i)
  rw [coe_mul] at this
  simp_rw [coe_rpow_of_nonneg _ <| inv_nonneg.2 hp‚ÇÄ.le, coe_finset_sum, ‚Üê ENNReal.toNNReal_rpow,
    ‚Üê ENNReal.toNNReal_mul, sum_congr rfl fun i hi ‚Ü¶ coe_toNNReal (H'.2 i hi)] at this
  simp [ENNReal.coe_rpow_of_nonneg, hp‚ÇÄ.le, hp‚ÇÅ.le] at this
  convert this using 2 with i hi
  ¬∑ obtain hw | hw := eq_or_ne (w i) 0
    ¬∑ simp [hw]
    rw [coe_toNNReal (H'.1 _ hi), coe_toNNReal]
    simpa [mul_eq_top, hw, hp‚ÇÄ, hp‚ÇÄ.not_lt, H'.1 _ hi] using H'.2 _ hi
  ¬∑ convert rfl with i hi
    exact coe_toNNReal (H'.1 _ hi)

/-- For `1 ‚â§ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `‚Ñù‚â•0‚àû`-valued functions.
-/
theorem rpow_sum_le_const_mul_sum_rpow (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, f i) ^ p ‚â§ (card s : ‚Ñù‚â•0‚àû) ^ (p - 1) * ‚àë i ‚àà s, f i ^ p := by
  cases' eq_or_lt_of_le hp with hp hp
  ¬∑ simp [‚Üê hp]
  let q : ‚Ñù := p / (p - 1)
  have hpq : p.IsConjExponent q := .conjExponent hp
  have hp‚ÇÅ : 1 / p * p = 1 := one_div_mul_cancel hpq.ne_zero
  have hq : 1 / q * p = p - 1 := by
    rw [‚Üê hpq.div_conj_eq_sub_one]
    ring
  simpa only [ENNReal.mul_rpow_of_nonneg _ _ hpq.nonneg, ‚Üê ENNReal.rpow_mul, hp‚ÇÅ, hq, coe_one,
    one_mul, one_rpow, rpow_one, Pi.one_apply, sum_const, Nat.smul_one_eq_cast] using
    ENNReal.rpow_le_rpow (inner_le_Lp_mul_Lq s 1 f hpq.symm) hpq.nonneg

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `‚Ñù‚â•0‚àû` valued nonnegative
functions. -/
theorem Lp_add_le (hp : 1 ‚â§ p) :
    (‚àë i ‚àà s, (f i + g i) ^ p) ^ (1 / p) ‚â§
      (‚àë i ‚àà s, f i ^ p) ^ (1 / p) + (‚àë i ‚àà s, g i ^ p) ^ (1 / p) := by
  by_cases H' : (‚àë i ‚àà s, f i ^ p) ^ (1 / p) = ‚ä§ ‚à® (‚àë i ‚àà s, g i ^ p) ^ (1 / p) = ‚ä§
  ¬∑ cases' H' with H' H' <;> simp [H', -one_div]
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp
  replace H' : (‚àÄ i ‚àà s, f i ‚â† ‚ä§) ‚àß ‚àÄ i ‚àà s, g i ‚â† ‚ä§ := by
    simpa [ENNReal.rpow_eq_top_iff, asymm pos, pos, ENNReal.sum_eq_top, not_or] using H'
  have :=
    ENNReal.coe_le_coe.2
      (@NNReal.Lp_add_le _ s (fun i => ENNReal.toNNReal (f i)) (fun i => ENNReal.toNNReal (g i)) _
        hp)
  push_cast [ENNReal.coe_rpow_of_nonneg, le_of_lt pos, le_of_lt (one_div_pos.2 pos)] at this
  convert this using 2 <;> [skip; congr 1; congr 1] <;>
    ¬∑ refine Finset.sum_congr rfl fun i hi => ?_
      simp [H'.1 i hi, H'.2 i hi]

end ENNReal

end HoelderMinkowski
