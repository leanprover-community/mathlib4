/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, SÃ©bastien GouÃ«zel, RÃ©my Degenne
-/
import Mathlib.Algebra.BigOperators.Expect
import Mathlib.Algebra.BigOperators.Field
import Mathlib.Analysis.Convex.Jensen
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal
import Mathlib.Data.Real.ConjExponents

/-!
# Mean value inequalities

In this file we prove several inequalities for finite sums, including AM-GM inequality,
HM-GM inequality, Young's inequality, HÃ¶lder inequality, and Minkowski inequality. Versions for
integrals of some of these inequalities are available in
`Mathlib/MeasureTheory/Integral/MeanInequalities.lean`.

## Main theorems

### AM-GM inequality:

The inequality says that the geometric mean of a tuple of non-negative numbers is less than or equal
to their arithmetic mean. We prove the weighted version of this inequality: if $w$ and $z$
are two non-negative vectors and $\sum_{i\in s} w_i=1$, then
$$
\prod_{i\in s} z_i^{w_i} â‰¤ \sum_{i\in s} w_iz_i.
$$
The classical version is a special case of this inequality for $w_i=\frac{1}{n}$.

We prove a few versions of this inequality. Each of the following lemmas comes in two versions:
a version for real-valued non-negative functions is in the `Real` namespace, and a version for
`NNReal`-valued functions is in the `NNReal` namespace.

- `geom_mean_le_arith_mean_weighted` : weighted version for functions on `Finset`s;
- `geom_mean_le_arith_mean2_weighted` : weighted version for two numbers;
- `geom_mean_le_arith_mean3_weighted` : weighted version for three numbers;
- `geom_mean_le_arith_mean4_weighted` : weighted version for four numbers.


### HM-GM inequality:

The inequality says that the harmonic mean of a tuple of positive numbers is less than or equal
to their geometric mean. We prove the weighted version of this inequality: if $w$ and $z$
are two positive vectors and $\sum_{i\in s} w_i=1$, then
$$
1/(\sum_{i\in s} w_i/z_i) â‰¤ \prod_{i\in s} z_i^{w_i}
$$
The classical version is proven as a special case of this inequality for $w_i=\frac{1}{n}$.

The inequalities are proven only for real valued positive functions on `Finset`s, and namespaced in
`Real`. The weighted version follows as a corollary of the weighted AM-GM inequality.

### Young's inequality

Young's inequality says that for non-negative numbers `a`, `b`, `p`, `q` such that
$\frac{1}{p}+\frac{1}{q}=1$ we have
$$
ab â‰¤ \frac{a^p}{p} + \frac{b^q}{q}.
$$

This inequality is a special case of the AM-GM inequality. It is then used to prove HÃ¶lder's
inequality (see below).

### HÃ¶lder's inequality

The inequality says that for two conjugate exponents `p` and `q` (i.e., for two positive numbers
such that $\frac{1}{p}+\frac{1}{q}=1$) and any two non-negative vectors their inner product is
less than or equal to the product of the $L_p$ norm of the first vector and the $L_q$ norm of the
second vector:
$$
\sum_{i\in s} a_ib_i â‰¤ \sqrt[p]{\sum_{i\in s} a_i^p}\sqrt[q]{\sum_{i\in s} b_i^q}.
$$

We give versions of this result in `â„`, `â„â‰¥0` and `â„â‰¥0âˆ`.

There are at least two short proofs of this inequality. In our proof we prenormalize both vectors,
then apply Young's inequality to each $a_ib_i$. Another possible proof would be to deduce this
inequality from the generalized mean inequality for well-chosen vectors and weights.

### Minkowski's inequality

The inequality says that for `p â‰¥ 1` the function
$$
\|a\|_p=\sqrt[p]{\sum_{i\in s} a_i^p}
$$
satisfies the triangle inequality $\|a+b\|_p\le \|a\|_p+\|b\|_p$.

We give versions of this result in `Real`, `â„â‰¥0` and `â„â‰¥0âˆ`.

We deduce this inequality from HÃ¶lder's inequality. Namely, HÃ¶lder inequality implies that $\|a\|_p$
is the maximum of the inner product $\sum_{i\in s}a_ib_i$ over `b` such that $\|b\|_q\le 1$. Now
Minkowski's inequality follows from the fact that the maximum value of the sum of two functions is
less than or equal to the sum of the maximum values of the summands.

## TODO

- each inequality `A â‰¤ B` should come with a theorem `A = B â†” _`; one of the ways to prove them
  is to define `StrictConvexOn` functions.
- generalized mean inequality with any `p â‰¤ q`, including negative numbers;
- prove that the power mean tends to the geometric mean as the exponent tends to zero.

-/


universe u v

open Finset NNReal ENNReal
open scoped BigOperators

noncomputable section

variable {Î¹ : Type u} (s : Finset Î¹)

section GeomMeanLEArithMean

/-! ### AM-GM inequality -/


namespace Real

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for real-valued nonnegative functions. -/
theorem geom_mean_le_arith_mean_weighted (w z : Î¹ â†’ â„) (hw : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) :
    âˆ i âˆˆ s, z i ^ w i â‰¤ âˆ‘ i âˆˆ s, w i * z i := by
  -- If some number `z i` equals zero and has non-zero weight, then LHS is 0 and RHS is nonnegative.
  by_cases A : âˆƒ i âˆˆ s, z i = 0 âˆ§ w i â‰  0
  Â· rcases A with âŸ¨i, his, hzi, hwiâŸ©
    rw [prod_eq_zero his]
    Â· exact sum_nonneg fun j hj => mul_nonneg (hw j hj) (hz j hj)
    Â· rw [hzi]
      exact zero_rpow hwi
  -- If all numbers `z i` with non-zero weight are positive, then we apply Jensen's inequality
  -- for `exp` and numbers `log (z i)` with weights `w i`.
  Â· simp only [not_exists, not_and, Ne, Classical.not_not] at A
    have := convexOn_exp.map_sum_le hw hw' fun i _ => Set.mem_univ <| log (z i)
    simp only [exp_sum, smul_eq_mul, mul_comm (w _) (log _)] at this
    convert this using 1 <;> [apply prod_congr rfl;apply sum_congr rfl] <;> intro i hi
    Â· rcases eq_or_lt_of_le (hz i hi) with hz | hz
      Â· simp [A i hi hz.symm]
      Â· exact rpow_def_of_pos hz _
    Â· rcases eq_or_lt_of_le (hz i hi) with hz | hz
      Â· simp [A i hi hz.symm]
      Â· rw [exp_log hz]

/-- **AM-GM inequality**: The **geometric mean is less than or equal to the arithmetic mean. -/
theorem geom_mean_le_arith_mean {Î¹ : Type*} (s : Finset Î¹) (w : Î¹ â†’ â„) (z : Î¹ â†’ â„)
    (hw : âˆ€ i âˆˆ s, 0 â‰¤ w i) (hw' : 0 < âˆ‘ i âˆˆ s, w i) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) :
    (âˆ i âˆˆ s, z i ^ w i) ^ (âˆ‘ i âˆˆ s, w i)â»Â¹  â‰¤  (âˆ‘ i âˆˆ s, w i * z i) / (âˆ‘ i âˆˆ s, w i) := by
  convert geom_mean_le_arith_mean_weighted s (fun i => (w i) / âˆ‘ i âˆˆ s, w i) z ?_ ?_ hz using 2
  Â· rw [â† finset_prod_rpow _ _ (fun i hi => rpow_nonneg (hz _ hi) _) _]
    refine Finset.prod_congr rfl (fun _ ih => ?_)
    rw [div_eq_mul_inv, rpow_mul (hz _ ih)]
  Â· simp_rw [div_eq_mul_inv, mul_assoc, mul_comm, â† mul_assoc, â† Finset.sum_mul, mul_comm]
  Â· exact fun _ hi => div_nonneg (hw _ hi) (le_of_lt hw')
  Â· simp_rw [div_eq_mul_inv, â† Finset.sum_mul]
    exact mul_inv_cancelâ‚€ (by linarith)

theorem geom_mean_weighted_of_constant (w z : Î¹ â†’ â„) (x : â„) (hw : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) (hx : âˆ€ i âˆˆ s, w i â‰  0 â†’ z i = x) :
    âˆ i âˆˆ s, z i ^ w i = x :=
  calc
    âˆ i âˆˆ s, z i ^ w i = âˆ i âˆˆ s, x ^ w i := by
      refine prod_congr rfl fun i hi => ?_
      rcases eq_or_ne (w i) 0 with hâ‚€ | hâ‚€
      Â· rw [hâ‚€, rpow_zero, rpow_zero]
      Â· rw [hx i hi hâ‚€]
    _ = x := by
      rw [â† rpow_sum_of_nonneg _ hw, hw', rpow_one]
      have : (âˆ‘ i âˆˆ s, w i) â‰  0 := by
        rw [hw']
        exact one_ne_zero
      obtain âŸ¨i, his, hiâŸ© := exists_ne_zero_of_sum_ne_zero this
      rw [â† hx i his hi]
      exact hz i his

theorem arith_mean_weighted_of_constant (w z : Î¹ â†’ â„) (x : â„) (hw' : âˆ‘ i âˆˆ s, w i = 1)
    (hx : âˆ€ i âˆˆ s, w i â‰  0 â†’ z i = x) : âˆ‘ i âˆˆ s, w i * z i = x :=
  calc
    âˆ‘ i âˆˆ s, w i * z i = âˆ‘ i âˆˆ s, w i * x := by
      refine sum_congr rfl fun i hi => ?_
      rcases eq_or_ne (w i) 0 with hwi | hwi
      Â· rw [hwi, zero_mul, zero_mul]
      Â· rw [hx i hi hwi]
    _ = x := by rw [â† sum_mul, hw', one_mul]

theorem geom_mean_eq_arith_mean_weighted_of_constant (w z : Î¹ â†’ â„) (x : â„) (hw : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) (hx : âˆ€ i âˆˆ s, w i â‰  0 â†’ z i = x) :
    âˆ i âˆˆ s, z i ^ w i = âˆ‘ i âˆˆ s, w i * z i := by
  rw [geom_mean_weighted_of_constant, arith_mean_weighted_of_constant] <;> assumption

/-- **AM-GM inequality - equality condition**: This theorem provides the equality condition for the
*positive* weighted version of the AM-GM inequality for real-valued nonnegative functions. -/
theorem geom_mean_eq_arith_mean_weighted_iff' (w z : Î¹ â†’ â„) (hw : âˆ€ i âˆˆ s, 0 < w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) :
    âˆ i âˆˆ s, z i ^ w i = âˆ‘ i âˆˆ s, w i * z i â†” âˆ€ j âˆˆ s, z j = âˆ‘ i âˆˆ s, w i * z i := by
  by_cases A : âˆƒ i âˆˆ s, z i = 0 âˆ§ w i â‰  0
  Â· rcases A with âŸ¨i, his, hzi, hwiâŸ©
    rw [prod_eq_zero his]
    Â· constructor
      Â· intro h
        rw [â† h]
        intro j hj
        apply eq_zero_of_ne_zero_of_mul_left_eq_zero (ne_of_lt (hw j hj)).symm
        apply (sum_eq_zero_iff_of_nonneg ?_).mp h.symm j hj
        exact fun i hi => (mul_nonneg_iff_of_pos_left (hw i hi)).mpr (hz i hi)
      Â· intro h
        convert h i his
        exact hzi.symm
    Â· rw [hzi]
      exact zero_rpow hwi
  Â· simp only [not_exists, not_and] at A
    have hz' := fun i h => lt_of_le_of_ne (hz i h) (fun a => (A i h a.symm) (ne_of_gt (hw i h)))
    have := strictConvexOn_exp.map_sum_eq_iff hw hw' fun i _ => Set.mem_univ <| log (z i)
    simp only [exp_sum, smul_eq_mul, mul_comm (w _) (log _)] at this
    convert this using 1
    Â· apply Eq.congr <;>
      [apply prod_congr rfl; apply sum_congr rfl] <;>
      intro i hi <;>
      simp only [exp_mul, exp_log (hz' i hi)]
    Â· constructor <;> intro h j hj
      Â· rw [â† arith_mean_weighted_of_constant s w _ (log (z j)) hw' fun i _ => congrFun rfl]
        apply sum_congr rfl
        intro x hx
        simp only [mul_comm, h j hj, h x hx]
      Â· rw [â† arith_mean_weighted_of_constant s w _ (z j) hw' fun i _ => congrFun rfl]
        apply sum_congr rfl
        intro x hx
        simp only [log_injOn_pos (hz' j hj) (hz' x hx), h j hj, h x hx]

/-- **AM-GM inequality - equality condition**: This theorem provides the equality condition for the
weighted version of the AM-GM inequality for real-valued nonnegative functions. -/
theorem geom_mean_eq_arith_mean_weighted_iff (w z : Î¹ â†’ â„) (hw : âˆ€ i âˆˆ s, 0 â‰¤ w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) :
    âˆ i âˆˆ s, z i ^ w i = âˆ‘ i âˆˆ s, w i * z i â†” âˆ€ j âˆˆ s, w j â‰  0 â†’ z j = âˆ‘ i âˆˆ s, w i * z i := by
  have h (i) (_ : i âˆˆ s) : w i * z i â‰  0 â†’ w i â‰  0 := by apply left_ne_zero_of_mul
  have h' (i) (_ : i âˆˆ s) : z i ^ w i â‰  1 â†’ w i â‰  0 := by
    by_contra!
    obtain âŸ¨h1, h2âŸ© := this
    simp only [h2, rpow_zero, ne_self_iff_false] at h1
  rw [â† sum_filter_of_ne h, â† prod_filter_of_ne h', geom_mean_eq_arith_mean_weighted_iff']
  Â· simp
  Â· simp +contextual [(hw _ _).lt_iff_ne']
  Â· rwa [sum_filter_ne_zero]
  Â· simp_all only [ne_eq, mul_eq_zero, not_or, not_false_eq_true, implies_true, mem_filter]

/-- **AM-GM inequality - strict inequality condition**: This theorem provides the strict inequality
condition for the *positive* weighted version of the AM-GM inequality for real-valued nonnegative
functions. -/
theorem geom_mean_lt_arith_mean_weighted_iff_of_pos (w z : Î¹ â†’ â„) (hw : âˆ€ i âˆˆ s, 0 < w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 â‰¤ z i) :
    âˆ i âˆˆ s, z i ^ w i < âˆ‘ i âˆˆ s, w i * z i â†” âˆƒ j âˆˆ s, âˆƒ k âˆˆ s, z j â‰  z k:= by
  constructor
  Â· intro h
    by_contra! h_contra
    rw [(geom_mean_eq_arith_mean_weighted_iff' s w z hw hw' hz).mpr ?_] at h
    Â· exact (lt_self_iff_false _).mp h
    Â· intro j hjs
      rw [â† arith_mean_weighted_of_constant s w (fun _ => z j) (z j) hw' fun _ _ => congrFun rfl]
      apply sum_congr rfl (fun x a => congrArg (HMul.hMul (w x)) (h_contra j hjs x a))
  Â· rintro âŸ¨j, hjs, k, hks, hzjkâŸ©
    have := geom_mean_le_arith_mean_weighted s w z (fun i a => le_of_lt (hw i a)) hw' hz
    by_contra! h
    apply le_antisymm this at h
    apply (geom_mean_eq_arith_mean_weighted_iff' s w z hw hw' hz).mp at h
    simp only [h j hjs, h k hks, ne_eq, not_true_eq_false] at hzjk

end Real

namespace NNReal

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for `NNReal`-valued functions. -/
theorem geom_mean_le_arith_mean_weighted (w z : Î¹ â†’ â„â‰¥0) (hw' : âˆ‘ i âˆˆ s, w i = 1) :
    (âˆ i âˆˆ s, z i ^ (w i : â„)) â‰¤ âˆ‘ i âˆˆ s, w i * z i :=
  mod_cast
    Real.geom_mean_le_arith_mean_weighted _ _ _ (fun i _ => (w i).coe_nonneg)
      (by assumption_mod_cast) fun i _ => (z i).coe_nonneg

/-- **AM-GM inequality**: The geometric mean is less than or equal to the arithmetic mean, weighted
version for two `NNReal` numbers. -/
theorem geom_mean_le_arith_mean2_weighted (wâ‚ wâ‚‚ pâ‚ pâ‚‚ : â„â‰¥0) :
    wâ‚ + wâ‚‚ = 1 â†’ pâ‚ ^ (wâ‚ : â„) * pâ‚‚ ^ (wâ‚‚ : â„) â‰¤ wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one] using
    geom_mean_le_arith_mean_weighted univ ![wâ‚, wâ‚‚] ![pâ‚, pâ‚‚]

theorem geom_mean_le_arith_mean3_weighted (wâ‚ wâ‚‚ wâ‚ƒ pâ‚ pâ‚‚ pâ‚ƒ : â„â‰¥0) :
    wâ‚ + wâ‚‚ + wâ‚ƒ = 1 â†’
      pâ‚ ^ (wâ‚ : â„) * pâ‚‚ ^ (wâ‚‚ : â„) * pâ‚ƒ ^ (wâ‚ƒ : â„) â‰¤ wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ + wâ‚ƒ * pâ‚ƒ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one, â† add_assoc,
    mul_assoc] using geom_mean_le_arith_mean_weighted univ ![wâ‚, wâ‚‚, wâ‚ƒ] ![pâ‚, pâ‚‚, pâ‚ƒ]

theorem geom_mean_le_arith_mean4_weighted (wâ‚ wâ‚‚ wâ‚ƒ wâ‚„ pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : â„â‰¥0) :
    wâ‚ + wâ‚‚ + wâ‚ƒ + wâ‚„ = 1 â†’
      pâ‚ ^ (wâ‚ : â„) * pâ‚‚ ^ (wâ‚‚ : â„) * pâ‚ƒ ^ (wâ‚ƒ : â„) * pâ‚„ ^ (wâ‚„ : â„) â‰¤
        wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ + wâ‚ƒ * pâ‚ƒ + wâ‚„ * pâ‚„ := by
  simpa only [Fin.prod_univ_succ, Fin.sum_univ_succ, Finset.prod_empty, Finset.sum_empty,
    Finset.univ_eq_empty, Fin.cons_succ, Fin.cons_zero, add_zero, mul_one, â† add_assoc,
    mul_assoc] using geom_mean_le_arith_mean_weighted univ ![wâ‚, wâ‚‚, wâ‚ƒ, wâ‚„] ![pâ‚, pâ‚‚, pâ‚ƒ, pâ‚„]

end NNReal

namespace Real

theorem geom_mean_le_arith_mean2_weighted {wâ‚ wâ‚‚ pâ‚ pâ‚‚ : â„} (hwâ‚ : 0 â‰¤ wâ‚) (hwâ‚‚ : 0 â‰¤ wâ‚‚)
    (hpâ‚ : 0 â‰¤ pâ‚) (hpâ‚‚ : 0 â‰¤ pâ‚‚) (hw : wâ‚ + wâ‚‚ = 1) : pâ‚ ^ wâ‚ * pâ‚‚ ^ wâ‚‚ â‰¤ wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ :=
  NNReal.geom_mean_le_arith_mean2_weighted âŸ¨wâ‚, hwâ‚âŸ© âŸ¨wâ‚‚, hwâ‚‚âŸ© âŸ¨pâ‚, hpâ‚âŸ© âŸ¨pâ‚‚, hpâ‚‚âŸ© <|
    NNReal.coe_inj.1 <| by assumption

theorem geom_mean_le_arith_mean3_weighted {wâ‚ wâ‚‚ wâ‚ƒ pâ‚ pâ‚‚ pâ‚ƒ : â„} (hwâ‚ : 0 â‰¤ wâ‚) (hwâ‚‚ : 0 â‰¤ wâ‚‚)
    (hwâ‚ƒ : 0 â‰¤ wâ‚ƒ) (hpâ‚ : 0 â‰¤ pâ‚) (hpâ‚‚ : 0 â‰¤ pâ‚‚) (hpâ‚ƒ : 0 â‰¤ pâ‚ƒ) (hw : wâ‚ + wâ‚‚ + wâ‚ƒ = 1) :
    pâ‚ ^ wâ‚ * pâ‚‚ ^ wâ‚‚ * pâ‚ƒ ^ wâ‚ƒ â‰¤ wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ + wâ‚ƒ * pâ‚ƒ :=
  NNReal.geom_mean_le_arith_mean3_weighted âŸ¨wâ‚, hwâ‚âŸ© âŸ¨wâ‚‚, hwâ‚‚âŸ© âŸ¨wâ‚ƒ, hwâ‚ƒâŸ© âŸ¨pâ‚, hpâ‚âŸ© âŸ¨pâ‚‚, hpâ‚‚âŸ©
      âŸ¨pâ‚ƒ, hpâ‚ƒâŸ© <|
    NNReal.coe_inj.1 hw

theorem geom_mean_le_arith_mean4_weighted {wâ‚ wâ‚‚ wâ‚ƒ wâ‚„ pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : â„} (hwâ‚ : 0 â‰¤ wâ‚)
    (hwâ‚‚ : 0 â‰¤ wâ‚‚) (hwâ‚ƒ : 0 â‰¤ wâ‚ƒ) (hwâ‚„ : 0 â‰¤ wâ‚„) (hpâ‚ : 0 â‰¤ pâ‚) (hpâ‚‚ : 0 â‰¤ pâ‚‚) (hpâ‚ƒ : 0 â‰¤ pâ‚ƒ)
    (hpâ‚„ : 0 â‰¤ pâ‚„) (hw : wâ‚ + wâ‚‚ + wâ‚ƒ + wâ‚„ = 1) :
    pâ‚ ^ wâ‚ * pâ‚‚ ^ wâ‚‚ * pâ‚ƒ ^ wâ‚ƒ * pâ‚„ ^ wâ‚„ â‰¤ wâ‚ * pâ‚ + wâ‚‚ * pâ‚‚ + wâ‚ƒ * pâ‚ƒ + wâ‚„ * pâ‚„ :=
  NNReal.geom_mean_le_arith_mean4_weighted âŸ¨wâ‚, hwâ‚âŸ© âŸ¨wâ‚‚, hwâ‚‚âŸ© âŸ¨wâ‚ƒ, hwâ‚ƒâŸ© âŸ¨wâ‚„, hwâ‚„âŸ© âŸ¨pâ‚, hpâ‚âŸ©
      âŸ¨pâ‚‚, hpâ‚‚âŸ© âŸ¨pâ‚ƒ, hpâ‚ƒâŸ© âŸ¨pâ‚„, hpâ‚„âŸ© <|
    NNReal.coe_inj.1 <| by assumption

/-- As an example application of AM-GM we prove that the **Motzkin polynomial** is nonnegative.
This bivariate polynomial cannot be written as a sum of squares. -/
lemma motzkin_polynomial_nonneg (x y : â„) :
    0 â‰¤ x ^ 4 * y ^ 2 + x ^ 2 * y ^ 4 - 3 * x ^ 2 * y ^ 2 + 1 := by
  have nnâ‚ : 0 â‰¤ x ^ 4 * y ^ 2 := by positivity
  have nnâ‚‚ : 0 â‰¤ x ^ 2 * y ^ 4 := by positivity
  have key := geom_mean_le_arith_mean3_weighted (by simp) (by simp) (by simp)
    nnâ‚ nnâ‚‚ zero_le_one (add_thirds 1)
  rw [one_rpow, mul_one, â† mul_rpow nnâ‚ nnâ‚‚, â† mul_add, â† mul_add,
    show x ^ 4 * y ^ 2 * (x ^ 2 * y ^ 4) = (x ^ 2) ^ 3 * (y ^ 2) ^ 3 by ring,
    mul_rpow (by positivity) (by positivity),
    â† rpow_natCast _ 3, â† rpow_mul (sq_nonneg x), â† rpow_natCast _ 3, â† rpow_mul (sq_nonneg y),
    show ((3 : â„•) * ((1 : â„) / 3)) = 1 by simp, rpow_one, rpow_one] at key
  linarith

end Real

end GeomMeanLEArithMean

section HarmMeanLEGeomMean

/-! ### HM-GM inequality -/

namespace Real

/-- **HM-GM inequality**: The harmonic mean is less than or equal to the geometric mean, weighted
version for real-valued nonnegative functions. -/
theorem harm_mean_le_geom_mean_weighted (w z : Î¹ â†’ â„) (hs : s.Nonempty) (hw : âˆ€ i âˆˆ s, 0 < w i)
    (hw' : âˆ‘ i âˆˆ s, w i = 1) (hz : âˆ€ i âˆˆ s, 0 < z i) :
    (âˆ‘ i âˆˆ s, w i / z i)â»Â¹ â‰¤ âˆ i âˆˆ s, z i ^ w i  := by
    have : âˆ i âˆˆ s, (1 / z) i ^ w i â‰¤ âˆ‘ i âˆˆ s, w i * (1 / z) i :=
      geom_mean_le_arith_mean_weighted s w (1/z) (fun i hi â†¦ le_of_lt (hw i hi)) hw'
      (fun i hi â†¦ one_div_nonneg.2 (le_of_lt (hz i hi)))
    have p_pos : 0 < âˆ i âˆˆ s, (z i)â»Â¹ ^ w i :=
      prod_pos fun i hi => rpow_pos_of_pos (inv_pos.2 (hz i hi)) _
    have s_pos : 0 < âˆ‘ i âˆˆ s, w i * (z i)â»Â¹ :=
      sum_pos (fun i hi => mul_pos (hw i hi) (inv_pos.2 (hz i hi))) hs
    norm_num at this
    rw [â† inv_le_invâ‚€ s_pos p_pos] at this
    apply le_trans this
    have p_posâ‚‚ : 0 < (âˆ i âˆˆ s, (z i) ^ w i)â»Â¹ :=
      inv_pos.2 (prod_pos fun i hi => rpow_pos_of_pos ((hz i hi)) _ )
    rw [â† inv_inv (âˆ i âˆˆ s, z i ^ w i), inv_le_invâ‚€ p_pos p_posâ‚‚, â† Finset.prod_inv_distrib]
    gcongr
    Â· exact fun i hi â†¦ inv_nonneg.mpr (Real.rpow_nonneg (le_of_lt (hz i hi)) _)
    Â· rw [Real.inv_rpow]; apply fun i hi â†¦ le_of_lt (hz i hi); assumption


/-- **HM-GM inequality**: The **harmonic mean is less than or equal to the geometric mean. -/
theorem harm_mean_le_geom_mean {Î¹ : Type*} (s : Finset Î¹) (hs : s.Nonempty) (w : Î¹ â†’ â„)
    (z : Î¹ â†’ â„) (hw : âˆ€ i âˆˆ s, 0 < w i) (hw' : 0 < âˆ‘ i âˆˆ s, w i) (hz : âˆ€ i âˆˆ s, 0 < z i) :
    (âˆ‘ i âˆˆ s, w i) / (âˆ‘ i âˆˆ s, w i / z i) â‰¤ (âˆ i âˆˆ s, z i ^ w i) ^ (âˆ‘ i âˆˆ s, w i)â»Â¹ := by
  have := harm_mean_le_geom_mean_weighted s (fun i => (w i) / âˆ‘ i âˆˆ s, w i) z hs ?_ ?_ hz
  Â· simp only at this
    set n := âˆ‘ i âˆˆ s, w i
    nth_rw 1 [div_eq_mul_inv, (show n = (nâ»Â¹)â»Â¹ by simp), â† mul_inv, Finset.mul_sum _ _ nâ»Â¹]
    simp_rw [inv_mul_eq_div n ((w _)/(z _)), div_right_comm _ _ n]
    convert this
    rw [â† Real.finset_prod_rpow s _ (fun i hi â†¦ Real.rpow_nonneg (le_of_lt <| hz i hi) _)]
    refine Finset.prod_congr rfl (fun i hi => ?_)
    rw [â† Real.rpow_mul (le_of_lt <| hz i hi) (w _) nâ»Â¹, div_eq_mul_inv (w _) n]
  Â· exact fun i hi â†¦ div_pos (hw i hi) hw'
  Â· simp_rw [div_eq_mul_inv (w _) (âˆ‘ i âˆˆ s, w i), â† Finset.sum_mul _ _ (âˆ‘ i âˆˆ s, w i)â»Â¹]
    exact mul_inv_cancelâ‚€ hw'.ne'

end Real

end HarmMeanLEGeomMean


section Young

/-! ### Young's inequality -/


namespace Real

/-- **Young's inequality**, a version for nonnegative real numbers. -/
theorem young_inequality_of_nonneg {a b p q : â„} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b)
    (hpq : p.HolderConjugate q) : a * b â‰¤ a ^ p / p + b ^ q / q := by
  simpa [â† rpow_mul, ha, hb, hpq.ne_zero, hpq.symm.ne_zero, _root_.div_eq_inv_mul] using
    geom_mean_le_arith_mean2_weighted hpq.inv_nonneg hpq.symm.inv_nonneg
      (rpow_nonneg ha p) (rpow_nonneg hb q) hpq.inv_add_inv_eq_one

/-- **Young's inequality**, a version for arbitrary real numbers. -/
theorem young_inequality (a b : â„) {p q : â„} (hpq : p.HolderConjugate q) :
    a * b â‰¤ |a| ^ p / p + |b| ^ q / q :=
  calc
    a * b â‰¤ |a * b| := le_abs_self (a * b)
    _ = |a| * |b| := abs_mul a b
    _ â‰¤ |a| ^ p / p + |b| ^ q / q :=
      Real.young_inequality_of_nonneg (abs_nonneg a) (abs_nonneg b) hpq

end Real

namespace NNReal

/-- **Young's inequality**, `â„â‰¥0` version. We use `{p q : â„â‰¥0}` in order to avoid constructing
witnesses of `0 â‰¤ p` and `0 â‰¤ q` for the denominators. -/
theorem young_inequality (a b : â„â‰¥0) {p q : â„â‰¥0} (hpq : p.HolderConjugate q) :
    a * b â‰¤ a ^ (p : â„) / p + b ^ (q : â„) / q :=
  Real.young_inequality_of_nonneg a.coe_nonneg b.coe_nonneg hpq.coe

/-- **Young's inequality**, `â„â‰¥0` version with real conjugate exponents. -/
theorem young_inequality_real (a b : â„â‰¥0) {p q : â„} (hpq : p.HolderConjugate q) :
    a * b â‰¤ a ^ p / Real.toNNReal p + b ^ q / Real.toNNReal q := by
  simpa [Real.coe_toNNReal, hpq.nonneg, hpq.symm.nonneg] using young_inequality a b hpq.toNNReal

end NNReal

namespace ENNReal

/-- **Young's inequality**, `â„â‰¥0âˆ` version with real conjugate exponents. -/
theorem young_inequality (a b : â„â‰¥0âˆ) {p q : â„} (hpq : p.HolderConjugate q) :
    a * b â‰¤ a ^ p / ENNReal.ofReal p + b ^ q / ENNReal.ofReal q := by
  by_cases h : a = âŠ¤ âˆ¨ b = âŠ¤
  Â· refine le_trans le_top (le_of_eq ?_)
    repeat rw [div_eq_mul_inv]
    rcases h with h | h <;> rw [h] <;> simp [hpq.pos, hpq.symm.pos]
  push_neg at h
  -- if a â‰  âŠ¤ and b â‰  âŠ¤, use the nnreal version: nnreal.young_inequality_real
  rw [â† coe_toNNReal h.left, â† coe_toNNReal h.right, â† coe_mul, â† coe_rpow_of_nonneg _ hpq.nonneg,
    â† coe_rpow_of_nonneg _ hpq.symm.nonneg, ENNReal.ofReal, ENNReal.ofReal, â†
    @coe_div (Real.toNNReal p) _ (by simp [hpq.pos]), â†
    @coe_div (Real.toNNReal q) _ (by simp [hpq.symm.pos]), â† coe_add, coe_le_coe]
  exact NNReal.young_inequality_real a.toNNReal b.toNNReal hpq

end ENNReal

end Young

section HoelderMinkowski

/-! ### HÃ¶lder's and Minkowski's inequalities -/


namespace NNReal

private theorem inner_le_Lp_mul_Lp_of_norm_le_one (f g : Î¹ â†’ â„â‰¥0) {p q : â„}
    (hpq : p.HolderConjugate q) (hf : âˆ‘ i âˆˆ s, f i ^ p â‰¤ 1) (hg : âˆ‘ i âˆˆ s, g i ^ q â‰¤ 1) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ 1 := by
  have hp : 0 < p.toNNReal := zero_lt_one.trans hpq.toNNReal.lt
  have hq : 0 < q.toNNReal := zero_lt_one.trans hpq.toNNReal.symm.lt
  calc
    âˆ‘ i âˆˆ s, f i * g i â‰¤ âˆ‘ i âˆˆ s, (f i ^ p / Real.toNNReal p + g i ^ q / Real.toNNReal q) :=
      Finset.sum_le_sum fun i _ => young_inequality_real (f i) (g i) hpq
    _ = (âˆ‘ i âˆˆ s, f i ^ p) / Real.toNNReal p + (âˆ‘ i âˆˆ s, g i ^ q) / Real.toNNReal q := by
      rw [sum_add_distrib, sum_div, sum_div]
    _ â‰¤ 1 / Real.toNNReal p + 1 / Real.toNNReal q := by
      refine add_le_add ?_ ?_ <;> rwa [div_le_iffâ‚€, div_mul_cancelâ‚€] <;> positivity
    _ = 1 := by simp_rw [one_div, hpq.toNNReal.inv_add_inv_eq_one]

private theorem inner_le_Lp_mul_Lp_of_norm_eq_zero (f g : Î¹ â†’ â„â‰¥0) {p q : â„}
    (hpq : p.HolderConjugate q) (hf : âˆ‘ i âˆˆ s, f i ^ p = 0) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) := by
  simp only [hf, hpq.ne_zero, one_div, sum_eq_zero_iff, zero_rpow, zero_mul,
    inv_eq_zero, Ne, not_false_iff, le_zero_iff, mul_eq_zero]
  intro i his
  left
  rw [sum_eq_zero_iff] at hf
  exact (rpow_eq_zero_iff.mp (hf i his)).left

/-- **HÃ¶lder inequality**: The scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with `â„â‰¥0`-valued functions. -/
theorem inner_le_Lp_mul_Lq (f g : Î¹ â†’ â„â‰¥0) {p q : â„} (hpq : p.HolderConjugate q) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) := by
  obtain hf | hf := eq_zero_or_pos (âˆ‘ i âˆˆ s, f i ^ p)
  Â· exact inner_le_Lp_mul_Lp_of_norm_eq_zero s f g hpq hf
  obtain hg | hg := eq_zero_or_pos (âˆ‘ i âˆˆ s, g i ^ q)
  Â· calc
      âˆ‘ i âˆˆ s, f i * g i = âˆ‘ i âˆˆ s, g i * f i := by
        congr with i
        rw [mul_comm]
      _ â‰¤ (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) * (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) :=
        (inner_le_Lp_mul_Lp_of_norm_eq_zero s g f hpq.symm hg)
      _ = (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) := mul_comm _ _
  let f' i := f i / (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p)
  let g' i := g i / (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q)
  suffices (âˆ‘ i âˆˆ s, f' i * g' i) â‰¤ 1 by
    simp_rw [f', g', div_mul_div_comm, â† sum_div] at this
    rwa [div_le_iffâ‚€, one_mul] at this
    -- TODO: We are missing a positivity  extension here
    exact mul_pos (rpow_pos hf) (rpow_pos hg)
  refine inner_le_Lp_mul_Lp_of_norm_le_one s f' g' hpq (le_of_eq ?_) (le_of_eq ?_)
  Â· simp_rw [f', div_rpow, â† sum_div, â† rpow_mul, one_div, inv_mul_cancelâ‚€ hpq.ne_zero, rpow_one,
      div_self hf.ne']
  Â· simp_rw [g', div_rpow, â† sum_div, â† rpow_mul, one_div, inv_mul_cancelâ‚€ hpq.symm.ne_zero,
      rpow_one, div_self hg.ne']

/-- **Weighted HÃ¶lder inequality**. -/
lemma inner_le_weight_mul_Lp (s : Finset Î¹) {p : â„} (hp : 1 â‰¤ p) (w f : Î¹ â†’ â„â‰¥0) :
    âˆ‘ i âˆˆ s, w i * f i â‰¤ (âˆ‘ i âˆˆ s, w i) ^ (1 - pâ»Â¹) * (âˆ‘ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ := by
  obtain rfl | hp := hp.eq_or_lt
  Â· simp
  calc
    _ = âˆ‘ i âˆˆ s, w i ^ (1 - pâ»Â¹) * (w i ^ pâ»Â¹ * f i) := ?_
    _ â‰¤ (âˆ‘ i âˆˆ s, (w i ^ (1 - pâ»Â¹)) ^ (1 - pâ»Â¹)â»Â¹) ^ (1 / (1 - pâ»Â¹)â»Â¹) *
          (âˆ‘ i âˆˆ s, (w i ^ pâ»Â¹ * f i) ^ p) ^ (1 / p) :=
        inner_le_Lp_mul_Lq _ _ _ (.symm <| Real.holderConjugate_iff.mpr âŸ¨hp, by simpâŸ©)
    _ = _ := ?_
  Â· congr with i
    rw [â† mul_assoc, â† rpow_of_add_eq _ one_ne_zero, rpow_one]
    simp
  Â· have hpâ‚€ : p â‰  0 := by positivity
    have hpâ‚ : 1 - pâ»Â¹ â‰  0 := by simp [sub_eq_zero, hp.ne']
    simp [mul_rpow, div_inv_eq_mul, one_mul, one_div, hpâ‚€, hpâ‚]

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_hasSum`. -/
theorem inner_le_Lp_mul_Lq_tsum {f g : Î¹ â†’ â„â‰¥0} {p q : â„} (hpq : p.HolderConjugate q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    (Summable fun i => f i * g i) âˆ§
      âˆ‘' i, f i * g i â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q) := by
  have Hâ‚ : âˆ€ s : Finset Î¹,
      âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q) := by
    intro s
    refine le_trans (inner_le_Lp_mul_Lq s f g hpq) (mul_le_mul ?_ ?_ bot_le bot_le)
    Â· rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr hpq.pos)]
      exact hf.sum_le_tsum _ (fun _ _ => zero_le _)
    Â· rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr hpq.symm.pos)]
      exact hg.sum_le_tsum _ (fun _ _ => zero_le _)
  have bdd : BddAbove (Set.range fun s => âˆ‘ i âˆˆ s, f i * g i) := by
    refine âŸ¨(âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q), ?_âŸ©
    rintro a âŸ¨s, rflâŸ©
    exact Hâ‚ s
  have Hâ‚‚ : Summable _ := (hasSum_of_isLUB _ (isLUB_ciSup bdd)).summable
  exact âŸ¨Hâ‚‚, Hâ‚‚.tsum_le_of_sum_le Hâ‚âŸ©

theorem summable_mul_of_Lp_Lq {f g : Î¹ â†’ â„â‰¥0} {p q : â„} (hpq : p.HolderConjugate q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    Summable fun i => f i * g i :=
  (inner_le_Lp_mul_Lq_tsum hpq hf hg).1

theorem inner_le_Lp_mul_Lq_tsum' {f g : Î¹ â†’ â„â‰¥0} {p q : â„} (hpq : p.HolderConjugate q)
    (hf : Summable fun i => f i ^ p) (hg : Summable fun i => g i ^ q) :
    âˆ‘' i, f i * g i â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q) :=
  (inner_le_Lp_mul_Lq_tsum hpq hf hg).2

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are not already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_tsum`. -/
theorem inner_le_Lp_mul_Lq_hasSum {f g : Î¹ â†’ â„â‰¥0} {A B : â„â‰¥0} {p q : â„}
    (hpq : p.HolderConjugate q) (hf : HasSum (fun i => f i ^ p) (A ^ p))
    (hg : HasSum (fun i => g i ^ q) (B ^ q)) : âˆƒ C, C â‰¤ A * B âˆ§ HasSum (fun i => f i * g i) C := by
  obtain âŸ¨Hâ‚, Hâ‚‚âŸ© := inner_le_Lp_mul_Lq_tsum hpq hf.summable hg.summable
  have hA : A = (âˆ‘' i : Î¹, f i ^ p) ^ (1 / p) := by rw [hf.tsum_eq, rpow_inv_rpow_self hpq.ne_zero]
  have hB : B = (âˆ‘' i : Î¹, g i ^ q) ^ (1 / q) := by
    rw [hg.tsum_eq, rpow_inv_rpow_self hpq.symm.ne_zero]
  refine âŸ¨âˆ‘' i, f i * g i, ?_, ?_âŸ©
  Â· simpa [hA, hB] using Hâ‚‚
  Â· simpa only [rpow_self_rpow_inv hpq.ne_zero] using Hâ‚.hasSum

/-- For `1 â‰¤ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `â„â‰¥0`-valued functions.
-/
theorem rpow_sum_le_const_mul_sum_rpow (f : Î¹ â†’ â„â‰¥0) {p : â„} (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, f i) ^ p â‰¤ (#s : â„â‰¥0) ^ (p - 1) * âˆ‘ i âˆˆ s, f i ^ p := by
  rcases eq_or_lt_of_le hp with hp | hp
  Â· simp [â† hp]
  let q : â„ := p / (p - 1)
  have hpq : p.HolderConjugate q := .conjExponent hp
  have hpâ‚ : 1 / p * p = 1 := one_div_mul_cancel hpq.ne_zero
  have hq : 1 / q * p = p - 1 := by
    rw [â† hpq.div_conj_eq_sub_one]
    ring
  simpa only [NNReal.mul_rpow, â† NNReal.rpow_mul, hpâ‚, hq, one_mul, one_rpow, rpow_one,
    Pi.one_apply, sum_const, Nat.smul_one_eq_cast] using
    NNReal.rpow_le_rpow (inner_le_Lp_mul_Lq s 1 f hpq.symm) hpq.nonneg

/-- The `L_p` seminorm of a vector `f` is the greatest value of the inner product
`âˆ‘ i âˆˆ s, f i * g i` over functions `g` of `L_q` seminorm less than or equal to one. -/
theorem isGreatest_Lp (f : Î¹ â†’ â„â‰¥0) {p q : â„} (hpq : p.HolderConjugate q) :
    IsGreatest ((fun g : Î¹ â†’ â„â‰¥0 => âˆ‘ i âˆˆ s, f i * g i) '' { g | âˆ‘ i âˆˆ s, g i ^ q â‰¤ 1 })
      ((âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p)) := by
  constructor
  Â· use fun i => f i ^ p / f i / (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / q)
    obtain hf | hf := eq_zero_or_pos (âˆ‘ i âˆˆ s, f i ^ p)
    Â· simp [hf, hpq.ne_zero, hpq.symm.ne_zero]
    Â· have A : p + q - q â‰  0 := by simp [hpq.ne_zero]
      have B : âˆ€ y : â„â‰¥0, y * y ^ p / y = y ^ p := by
        refine fun y => mul_div_cancel_left_of_imp fun h => ?_
        simp [h, hpq.ne_zero]
      simp only [Set.mem_setOf_eq, div_rpow, â† sum_div, â† rpow_mul,
        div_mul_cancelâ‚€ _ hpq.symm.ne_zero, rpow_one, div_le_iffâ‚€ hf, one_mul, hpq.mul_eq_add, â†
        rpow_sub' A, add_sub_cancel_right, le_refl, true_and, â† mul_div_assoc, B]
      rw [div_eq_iff, â† rpow_add hf.ne', one_div, one_div, hpq.inv_add_inv_eq_one, rpow_one]
      simpa [hpq.symm.ne_zero] using hf.ne'
  Â· rintro _ âŸ¨g, hg, rflâŸ©
    apply le_trans (inner_le_Lp_mul_Lq s f g hpq)
    simpa only [mul_one] using
      mul_le_mul_left' (NNReal.rpow_le_one hg (le_of_lt hpq.symm.one_div_pos)) _

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `NNReal`-valued functions. -/
theorem Lp_add_le (f g : Î¹ â†’ â„â‰¥0) {p : â„} (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, (f i + g i) ^ p) ^ (1 / p) â‰¤
      (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) + (âˆ‘ i âˆˆ s, g i ^ p) ^ (1 / p) := by
  -- The result is trivial when `p = 1`, so we can assume `1 < p`.
  rcases eq_or_lt_of_le hp with (rfl | hp)
  Â· simp [Finset.sum_add_distrib]
  have hpq := Real.HolderConjugate.conjExponent hp
  have := isGreatest_Lp s (f + g) hpq
  simp only [Pi.add_apply, add_mul, sum_add_distrib] at this
  rcases this.1 with âŸ¨Ï†, hÏ†, HâŸ©
  rw [â† H]
  exact
    add_le_add ((isGreatest_Lp s f hpq).2 âŸ¨Ï†, hÏ†, rflâŸ©) ((isGreatest_Lp s g hpq).2 âŸ¨Ï†, hÏ†, rflâŸ©)

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `NNReal`-valued functions. For an alternative version, convenient if the
infinite sums are already expressed as `p`-th powers, see `Lp_add_le_hasSum_of_nonneg`. -/
theorem Lp_add_le_tsum {f g : Î¹ â†’ â„â‰¥0} {p : â„} (hp : 1 â‰¤ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) :
    (Summable fun i => (f i + g i) ^ p) âˆ§
      (âˆ‘' i, (f i + g i) ^ p) ^ (1 / p) â‰¤
        (âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p) := by
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp
  have Hâ‚ : âˆ€ s : Finset Î¹,
      (âˆ‘ i âˆˆ s, (f i + g i) ^ p) â‰¤
        ((âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p)) ^ p := by
    intro s
    rw [one_div, â† NNReal.rpow_inv_le_iff pos, â† one_div]
    refine le_trans (Lp_add_le s f g hp) (add_le_add ?_ ?_) <;>
        rw [NNReal.rpow_le_rpow_iff (one_div_pos.mpr pos)] <;>
      refine Summable.sum_le_tsum _ (fun _ _ => zero_le _) ?_
    exacts [hf, hg]
  have bdd : BddAbove (Set.range fun s => âˆ‘ i âˆˆ s, (f i + g i) ^ p) := by
    refine âŸ¨((âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p)) ^ p, ?_âŸ©
    rintro a âŸ¨s, rflâŸ©
    exact Hâ‚ s
  have Hâ‚‚ : Summable _ := (hasSum_of_isLUB _ (isLUB_ciSup bdd)).summable
  refine âŸ¨Hâ‚‚, ?_âŸ©
  rw [one_div, NNReal.rpow_inv_le_iff pos, â† one_div]
  exact Hâ‚‚.tsum_le_of_sum_le Hâ‚

theorem summable_Lp_add {f g : Î¹ â†’ â„â‰¥0} {p : â„} (hp : 1 â‰¤ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) : Summable fun i => (f i + g i) ^ p :=
  (Lp_add_le_tsum hp hf hg).1

theorem Lp_add_le_tsum' {f g : Î¹ â†’ â„â‰¥0} {p : â„} (hp : 1 â‰¤ p) (hf : Summable fun i => f i ^ p)
    (hg : Summable fun i => g i ^ p) :
    (âˆ‘' i, (f i + g i) ^ p) ^ (1 / p) â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p) :=
  (Lp_add_le_tsum hp hf hg).2

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `NNReal`-valued functions. For an alternative version, convenient if the
infinite sums are not already expressed as `p`-th powers, see `Lp_add_le_tsum_of_nonneg`. -/
theorem Lp_add_le_hasSum {f g : Î¹ â†’ â„â‰¥0} {A B : â„â‰¥0} {p : â„} (hp : 1 â‰¤ p)
    (hf : HasSum (fun i => f i ^ p) (A ^ p)) (hg : HasSum (fun i => g i ^ p) (B ^ p)) :
    âˆƒ C, C â‰¤ A + B âˆ§ HasSum (fun i => (f i + g i) ^ p) (C ^ p) := by
  have hp' : p â‰  0 := (lt_of_lt_of_le zero_lt_one hp).ne'
  obtain âŸ¨Hâ‚, Hâ‚‚âŸ© := Lp_add_le_tsum hp hf.summable hg.summable
  have hA : A = (âˆ‘' i : Î¹, f i ^ p) ^ (1 / p) := by rw [hf.tsum_eq, rpow_inv_rpow_self hp']
  have hB : B = (âˆ‘' i : Î¹, g i ^ p) ^ (1 / p) := by rw [hg.tsum_eq, rpow_inv_rpow_self hp']
  refine âŸ¨(âˆ‘' i, (f i + g i) ^ p) ^ (1 / p), ?_, ?_âŸ©
  Â· simpa [hA, hB] using Hâ‚‚
  Â· simpa only [rpow_self_rpow_inv hp'] using Hâ‚.hasSum

end NNReal

namespace Real

variable (f g : Î¹ â†’ â„) {p q : â„}

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with real-valued functions. -/
theorem inner_le_Lp_mul_Lq (hpq : HolderConjugate p q) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘ i âˆˆ s, |f i| ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, |g i| ^ q) ^ (1 / q) := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.inner_le_Lp_mul_Lq s (fun i => âŸ¨_, abs_nonneg (f i)âŸ©) (fun i => âŸ¨_, abs_nonneg (g i)âŸ©)
        hpq)
  push_cast at this
  refine le_trans (sum_le_sum fun i _ => ?_) this
  simp only [â† abs_mul, le_abs_self]

/-- For `1 â‰¤ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `â„`-valued functions. -/
theorem rpow_sum_le_const_mul_sum_rpow (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, |f i|) ^ p â‰¤ (#s : â„) ^ (p - 1) * âˆ‘ i âˆˆ s, |f i| ^ p := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.rpow_sum_le_const_mul_sum_rpow s (fun i => âŸ¨_, abs_nonneg (f i)âŸ©) hp)
  push_cast at this
  exact this

-- for some reason `exact_mod_cast` can't replace this argument
/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `Real`-valued functions. -/
theorem Lp_add_le (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, |f i + g i| ^ p) ^ (1 / p) â‰¤
      (âˆ‘ i âˆˆ s, |f i| ^ p) ^ (1 / p) + (âˆ‘ i âˆˆ s, |g i| ^ p) ^ (1 / p) := by
  have :=
    NNReal.coe_le_coe.2
      (NNReal.Lp_add_le s (fun i => âŸ¨_, abs_nonneg (f i)âŸ©) (fun i => âŸ¨_, abs_nonneg (g i)âŸ©) hp)
  push_cast at this
  refine le_trans (rpow_le_rpow ?_ (sum_le_sum fun i _ => ?_) ?_) this <;>
    simp [sum_nonneg, rpow_nonneg, abs_nonneg, le_trans zero_le_one hp, abs_add,
      rpow_le_rpow]

variable {f g}

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with real-valued nonnegative functions. -/
theorem inner_le_Lp_mul_Lq_of_nonneg (hpq : HolderConjugate p q) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i)
    (hg : âˆ€ i âˆˆ s, 0 â‰¤ g i) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) := by
  convert inner_le_Lp_mul_Lq s f g hpq using 3 <;> apply sum_congr rfl <;> intro i hi <;>
    simp only [abs_of_nonneg, hf i hi, hg i hi]

/-- **Weighted HÃ¶lder inequality**. -/
lemma inner_le_weight_mul_Lp_of_nonneg (s : Finset Î¹) {p : â„} (hp : 1 â‰¤ p) (w f : Î¹ â†’ â„)
    (hw : âˆ€ i, 0 â‰¤ w i) (hf : âˆ€ i, 0 â‰¤ f i) :
    âˆ‘ i âˆˆ s, w i * f i â‰¤ (âˆ‘ i âˆˆ s, w i) ^ (1 - pâ»Â¹) * (âˆ‘ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ := by
  lift w to Î¹ â†’ â„â‰¥0 using hw
  lift f to Î¹ â†’ â„â‰¥0 using hf
  beta_reduce at *
  norm_cast at *
  exact NNReal.inner_le_weight_mul_Lp _ hp _ _

/-- **Weighted HÃ¶lder inequality** in terms of `Finset.expect`. -/
lemma compact_inner_le_weight_mul_Lp_of_nonneg (s : Finset Î¹) {p : â„} (hp : 1 â‰¤ p) {w f : Î¹ â†’ â„}
    (hw : âˆ€ i, 0 â‰¤ w i) (hf : âˆ€ i, 0 â‰¤ f i) :
    ğ”¼ i âˆˆ s, w i * f i â‰¤ (ğ”¼ i âˆˆ s, w i) ^ (1 - pâ»Â¹) * (ğ”¼ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ := by
  simp_rw [expect_eq_sum_div_card]
  rw [div_rpow, div_rpow, div_mul_div_comm, â† rpow_add', sub_add_cancel, rpow_one]
  Â· gcongr
    exact inner_le_weight_mul_Lp_of_nonneg s hp _ _ hw hf
  any_goals simp
  Â· exact sum_nonneg fun i _ â†¦ by have := hw i; have := hf i; positivity
  Â· exact sum_nonneg fun i _ â†¦ by have := hw i; positivity

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `â„`-valued functions.
For an alternative version, convenient if the infinite sums are already expressed as `p`-th powers,
see `inner_le_Lp_mul_Lq_hasSum_of_nonneg`. -/
theorem inner_le_Lp_mul_Lq_tsum_of_nonneg (hpq : p.HolderConjugate q) (hf : âˆ€ i, 0 â‰¤ f i)
    (hg : âˆ€ i, 0 â‰¤ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    (Summable fun i => f i * g i) âˆ§
      âˆ‘' i, f i * g i â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q) := by
  lift f to Î¹ â†’ â„â‰¥0 using hf
  lift g to Î¹ â†’ â„â‰¥0 using hg
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast at *
  exact NNReal.inner_le_Lp_mul_Lq_tsum hpq hf_sum hg_sum

theorem summable_mul_of_Lp_Lq_of_nonneg (hpq : p.HolderConjugate q) (hf : âˆ€ i, 0 â‰¤ f i)
    (hg : âˆ€ i, 0 â‰¤ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    Summable fun i => f i * g i :=
  (inner_le_Lp_mul_Lq_tsum_of_nonneg hpq hf hg hf_sum hg_sum).1

theorem inner_le_Lp_mul_Lq_tsum_of_nonneg' (hpq : p.HolderConjugate q) (hf : âˆ€ i, 0 â‰¤ f i)
    (hg : âˆ€ i, 0 â‰¤ g i) (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ q) :
    âˆ‘' i, f i * g i â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) * (âˆ‘' i, g i ^ q) ^ (1 / q) :=
  (inner_le_Lp_mul_Lq_tsum_of_nonneg hpq hf hg hf_sum hg_sum).2

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. A version for `NNReal`-valued
functions. For an alternative version, convenient if the infinite sums are not already expressed as
`p`-th powers, see `inner_le_Lp_mul_Lq_tsum_of_nonneg`. -/
theorem inner_le_Lp_mul_Lq_hasSum_of_nonneg (hpq : p.HolderConjugate q) {A B : â„} (hA : 0 â‰¤ A)
    (hB : 0 â‰¤ B) (hf : âˆ€ i, 0 â‰¤ f i) (hg : âˆ€ i, 0 â‰¤ g i)
    (hf_sum : HasSum (fun i => f i ^ p) (A ^ p)) (hg_sum : HasSum (fun i => g i ^ q) (B ^ q)) :
    âˆƒ C : â„, 0 â‰¤ C âˆ§ C â‰¤ A * B âˆ§ HasSum (fun i => f i * g i) C := by
  lift f to Î¹ â†’ â„â‰¥0 using hf
  lift g to Î¹ â†’ â„â‰¥0 using hg
  lift A to â„â‰¥0 using hA
  lift B to â„â‰¥0 using hB
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast at hf_sum hg_sum
  obtain âŸ¨C, hC, HâŸ© := NNReal.inner_le_Lp_mul_Lq_hasSum hpq hf_sum hg_sum
  refine âŸ¨C, C.prop, hC, ?_âŸ©
  norm_cast

/-- For `1 â‰¤ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with nonnegative `â„`-valued
functions. -/
theorem rpow_sum_le_const_mul_sum_rpow_of_nonneg (hp : 1 â‰¤ p) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) :
    (âˆ‘ i âˆˆ s, f i) ^ p â‰¤ (#s : â„) ^ (p - 1) * âˆ‘ i âˆˆ s, f i ^ p := by
  convert rpow_sum_le_const_mul_sum_rpow s f hp using 2 <;> apply sum_congr rfl <;> intro i hi <;>
    simp only [abs_of_nonneg, hf i hi]

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `â„`-valued nonnegative
functions. -/
theorem Lp_add_le_of_nonneg (hp : 1 â‰¤ p) (hf : âˆ€ i âˆˆ s, 0 â‰¤ f i) (hg : âˆ€ i âˆˆ s, 0 â‰¤ g i) :
    (âˆ‘ i âˆˆ s, (f i + g i) ^ p) ^ (1 / p) â‰¤
      (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) + (âˆ‘ i âˆˆ s, g i ^ p) ^ (1 / p) := by
  convert Lp_add_le s f g hp using 2 <;> [skip;congr 1;congr 1] <;> apply sum_congr rfl <;>
      intro i hi <;>
    simp only [abs_of_nonneg, hf i hi, hg i hi, add_nonneg]

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `â„`-valued functions. For an alternative version, convenient if the infinite
sums are already expressed as `p`-th powers, see `Lp_add_le_hasSum_of_nonneg`. -/
theorem Lp_add_le_tsum_of_nonneg (hp : 1 â‰¤ p) (hf : âˆ€ i, 0 â‰¤ f i) (hg : âˆ€ i, 0 â‰¤ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    (Summable fun i => (f i + g i) ^ p) âˆ§
      (âˆ‘' i, (f i + g i) ^ p) ^ (1 / p) â‰¤
        (âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p) := by
  lift f to Î¹ â†’ â„â‰¥0 using hf
  lift g to Î¹ â†’ â„â‰¥0 using hg
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at *
  norm_cast0 at *
  exact NNReal.Lp_add_le_tsum hp hf_sum hg_sum

theorem summable_Lp_add_of_nonneg (hp : 1 â‰¤ p) (hf : âˆ€ i, 0 â‰¤ f i) (hg : âˆ€ i, 0 â‰¤ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    Summable fun i => (f i + g i) ^ p :=
  (Lp_add_le_tsum_of_nonneg hp hf hg hf_sum hg_sum).1

theorem Lp_add_le_tsum_of_nonneg' (hp : 1 â‰¤ p) (hf : âˆ€ i, 0 â‰¤ f i) (hg : âˆ€ i, 0 â‰¤ g i)
    (hf_sum : Summable fun i => f i ^ p) (hg_sum : Summable fun i => g i ^ p) :
    (âˆ‘' i, (f i + g i) ^ p) ^ (1 / p) â‰¤ (âˆ‘' i, f i ^ p) ^ (1 / p) + (âˆ‘' i, g i ^ p) ^ (1 / p) :=
  (Lp_add_le_tsum_of_nonneg hp hf hg hf_sum hg_sum).2

/-- **Minkowski inequality**: the `L_p` seminorm of the infinite sum of two vectors is less than or
equal to the infinite sum of the `L_p`-seminorms of the summands, if these infinite sums both
exist. A version for `â„`-valued functions. For an alternative version, convenient if the infinite
sums are not already expressed as `p`-th powers, see `Lp_add_le_tsum_of_nonneg`. -/
theorem Lp_add_le_hasSum_of_nonneg (hp : 1 â‰¤ p) (hf : âˆ€ i, 0 â‰¤ f i) (hg : âˆ€ i, 0 â‰¤ g i) {A B : â„}
    (hA : 0 â‰¤ A) (hB : 0 â‰¤ B) (hfA : HasSum (fun i => f i ^ p) (A ^ p))
    (hgB : HasSum (fun i => g i ^ p) (B ^ p)) :
    âˆƒ C, 0 â‰¤ C âˆ§ C â‰¤ A + B âˆ§ HasSum (fun i => (f i + g i) ^ p) (C ^ p) := by
  lift f to Î¹ â†’ â„â‰¥0 using hf
  lift g to Î¹ â†’ â„â‰¥0 using hg
  lift A to â„â‰¥0 using hA
  lift B to â„â‰¥0 using hB
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce at hfA hgB
  norm_cast at hfA hgB
  obtain âŸ¨C, hCâ‚, hCâ‚‚âŸ© := NNReal.Lp_add_le_hasSum hp hfA hgB
  use C
  -- After https://github.com/leanprover/lean4/pull/2734, `norm_cast` needs help with beta reduction.
  beta_reduce
  norm_cast
  exact âŸ¨zero_le _, hCâ‚, hCâ‚‚âŸ©

end Real

namespace ENNReal

variable (f g : Î¹ â†’ â„â‰¥0âˆ) {p q : â„}

/-- **HÃ¶lder inequality**: the scalar product of two functions is bounded by the product of their
`L^p` and `L^q` norms when `p` and `q` are conjugate exponents. Version for sums over finite sets,
with `â„â‰¥0âˆ`-valued functions. -/
theorem inner_le_Lp_mul_Lq (hpq : p.HolderConjugate q) :
    âˆ‘ i âˆˆ s, f i * g i â‰¤ (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) * (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) := by
  by_cases H : (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) = 0 âˆ¨ (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) = 0
  Â· replace H : (âˆ€ i âˆˆ s, f i = 0) âˆ¨ âˆ€ i âˆˆ s, g i = 0 := by
      simpa [ENNReal.rpow_eq_zero_iff, hpq.pos, hpq.symm.pos, asymm hpq.pos, asymm hpq.symm.pos,
        sum_eq_zero_iff_of_nonneg] using H
    have : âˆ€ i âˆˆ s, f i * g i = 0 := fun i hi => by rcases H with H | H <;> simp [H i hi]
    simp [sum_eq_zero this]
  push_neg at H
  by_cases H' : (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) = âŠ¤ âˆ¨ (âˆ‘ i âˆˆ s, g i ^ q) ^ (1 / q) = âŠ¤
  Â· rcases H' with H' | H' <;> simp [H', -one_div, -sum_eq_zero_iff, -rpow_eq_zero_iff, H]
  replace H' : (âˆ€ i âˆˆ s, f i â‰  âŠ¤) âˆ§ âˆ€ i âˆˆ s, g i â‰  âŠ¤ := by
    simpa [ENNReal.rpow_eq_top_iff, asymm hpq.pos, asymm hpq.symm.pos, hpq.pos, hpq.symm.pos,
      ENNReal.sum_eq_top, not_or] using H'
  have := ENNReal.coe_le_coe.2 (@NNReal.inner_le_Lp_mul_Lq _ s (fun i => ENNReal.toNNReal (f i))
    (fun i => ENNReal.toNNReal (g i)) _ _ hpq)
  simp [ENNReal.coe_rpow_of_nonneg, hpq.pos.le, hpq.symm.pos.le] at this
  convert this using 1 <;> [skip; congr 2] <;> [skip; skip; simp; skip; simp] <;>
    Â· refine Finset.sum_congr rfl fun i hi => ?_
      simp [H'.1 i hi, H'.2 i hi, -WithZero.coe_mul]

/-- **Weighted HÃ¶lder inequality**. -/
lemma inner_le_weight_mul_Lp_of_nonneg (s : Finset Î¹) {p : â„} (hp : 1 â‰¤ p) (w f : Î¹ â†’ â„â‰¥0âˆ) :
    âˆ‘ i âˆˆ s, w i * f i â‰¤ (âˆ‘ i âˆˆ s, w i) ^ (1 - pâ»Â¹) * (âˆ‘ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ := by
  obtain rfl | hp := hp.eq_or_lt
  Â· simp
  have hpâ‚€ : 0 < p := by positivity
  have hpâ‚ : pâ»Â¹ < 1 := inv_lt_one_of_one_ltâ‚€ hp
  by_cases H : (âˆ‘ i âˆˆ s, w i) ^ (1 - pâ»Â¹) = 0 âˆ¨ (âˆ‘ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ = 0
  Â· replace H : (âˆ€ i âˆˆ s, w i = 0) âˆ¨ âˆ€ i âˆˆ s, w i = 0 âˆ¨ f i = 0 := by
      simpa [hpâ‚€, hpâ‚, hpâ‚€.not_gt, hpâ‚.not_gt, sum_eq_zero_iff_of_nonneg] using H
    have (i) (hi : i âˆˆ s) : w i * f i = 0 := by rcases H with H | H <;> simp [H i hi]
    simp [sum_eq_zero this]
  push_neg at H
  by_cases H' : (âˆ‘ i âˆˆ s, w i) ^ (1 - pâ»Â¹) = âŠ¤ âˆ¨ (âˆ‘ i âˆˆ s, w i * f i ^ p) ^ pâ»Â¹ = âŠ¤
  Â· rcases H' with H' | H' <;> simp [H', -one_div, -sum_eq_zero_iff, -rpow_eq_zero_iff, H]
  replace H' : (âˆ€ i âˆˆ s, w i â‰  âŠ¤) âˆ§ âˆ€ i âˆˆ s, w i * f i ^ p â‰  âŠ¤ := by
    simpa [rpow_eq_top_iff,hpâ‚€, hpâ‚, hpâ‚€.not_gt, hpâ‚.not_gt, sum_eq_top, not_or] using H'
  have := coe_le_coe.2 <| NNReal.inner_le_weight_mul_Lp s hp.le (fun i â†¦ ENNReal.toNNReal (w i))
    fun i â†¦ ENNReal.toNNReal (f i)
  rw [coe_mul] at this
  simp_rw [coe_rpow_of_nonneg _ <| inv_nonneg.2 hpâ‚€.le, coe_finset_sum, â† ENNReal.toNNReal_rpow,
    â† ENNReal.toNNReal_mul, sum_congr rfl fun i hi â†¦ coe_toNNReal (H'.2 i hi)] at this
  simp [ENNReal.coe_rpow_of_nonneg, hpâ‚.le] at this
  convert this using 2 with i hi
  Â· obtain hw | hw := eq_or_ne (w i) 0
    Â· simp [hw]
    rw [coe_toNNReal (H'.1 _ hi), coe_toNNReal]
    simpa [mul_eq_top, hw, hpâ‚€, hpâ‚€.not_gt, H'.1 _ hi] using H'.2 _ hi
  Â· convert rfl with i hi
    exact coe_toNNReal (H'.1 _ hi)

/-- For `1 â‰¤ p`, the `p`-th power of the sum of `f i` is bounded above by a constant times the
sum of the `p`-th powers of `f i`. Version for sums over finite sets, with `â„â‰¥0âˆ`-valued functions.
-/
theorem rpow_sum_le_const_mul_sum_rpow (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, f i) ^ p â‰¤ (card s : â„â‰¥0âˆ) ^ (p - 1) * âˆ‘ i âˆˆ s, f i ^ p := by
  rcases eq_or_lt_of_le hp with hp | hp
  Â· simp [â† hp]
  let q : â„ := p / (p - 1)
  have hpq : p.HolderConjugate q := .conjExponent hp
  have hpâ‚ : 1 / p * p = 1 := one_div_mul_cancel hpq.ne_zero
  have hq : 1 / q * p = p - 1 := by
    rw [â† hpq.div_conj_eq_sub_one]
    ring
  simpa only [ENNReal.mul_rpow_of_nonneg _ _ hpq.nonneg, â† ENNReal.rpow_mul, hpâ‚, hq, coe_one,
    one_mul, one_rpow, rpow_one, Pi.one_apply, sum_const, Nat.smul_one_eq_cast] using
    ENNReal.rpow_le_rpow (inner_le_Lp_mul_Lq s 1 f hpq.symm) hpq.nonneg

/-- **Minkowski inequality**: the `L_p` seminorm of the sum of two vectors is less than or equal
to the sum of the `L_p`-seminorms of the summands. A version for `â„â‰¥0âˆ` valued nonnegative
functions. -/
theorem Lp_add_le (hp : 1 â‰¤ p) :
    (âˆ‘ i âˆˆ s, (f i + g i) ^ p) ^ (1 / p) â‰¤
      (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) + (âˆ‘ i âˆˆ s, g i ^ p) ^ (1 / p) := by
  by_cases H' : (âˆ‘ i âˆˆ s, f i ^ p) ^ (1 / p) = âŠ¤ âˆ¨ (âˆ‘ i âˆˆ s, g i ^ p) ^ (1 / p) = âŠ¤
  Â· rcases H' with H' | H' <;> simp [H', -one_div]
  have pos : 0 < p := lt_of_lt_of_le zero_lt_one hp
  replace H' : (âˆ€ i âˆˆ s, f i â‰  âŠ¤) âˆ§ âˆ€ i âˆˆ s, g i â‰  âŠ¤ := by
    simpa [ENNReal.rpow_eq_top_iff, asymm pos, pos, ENNReal.sum_eq_top, not_or] using H'
  have :=
    ENNReal.coe_le_coe.2
      (@NNReal.Lp_add_le _ s (fun i => ENNReal.toNNReal (f i)) (fun i => ENNReal.toNNReal (g i)) _
        hp)
  push_cast [ENNReal.coe_rpow_of_nonneg, le_of_lt pos, le_of_lt (one_div_pos.2 pos)] at this
  convert this using 2 <;> [skip; congr 1; congr 1] <;>
    Â· refine Finset.sum_congr rfl fun i hi => ?_
      simp [H'.1 i hi, H'.2 i hi]

end ENNReal

end HoelderMinkowski
