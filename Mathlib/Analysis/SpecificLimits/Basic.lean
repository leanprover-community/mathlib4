/-
Copyright (c) 2017 Johannes HÃ¶lzl. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Johannes HÃ¶lzl, Yury Kudryashov, Patrick Massot
-/
import Mathlib.Algebra.Field.GeomSum
import Mathlib.Data.Nat.Factorial.BigOperators
import Mathlib.Order.Filter.AtTopBot.Archimedean
import Mathlib.Order.Iterate
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Topology.Algebra.InfiniteSum.Real
import Mathlib.Topology.Instances.EReal.Lemmas
import Mathlib.Topology.Instances.Rat

/-!
# A collection of specific limit computations

This file, by design, is independent of `NormedSpace` in the import hierarchy.  It contains
important specific limit computations in metric spaces, in ordered rings/fields, and in specific
instances of these such as `â„`, `â„â‰¥0` and `â„â‰¥0âˆ`.
-/

assert_not_exists Module.Basis NormedSpace

noncomputable section

open Set Function Filter Finset Metric Topology Nat uniformity NNReal ENNReal

variable {Î± : Type*} {Î² : Type*} {Î¹ : Type*}

theorem NNRat.tendsto_inv_atTop_nhds_zero_nat : Tendsto (fun n : â„• â†¦ (n : â„šâ‰¥0)â»Â¹) atTop (ğ“ 0) :=
  tendsto_inv_atTop_zero.comp tendsto_natCast_atTop_atTop

theorem NNRat.tendsto_algebraMap_inv_atTop_nhds_zero_nat (ğ•œ : Type*) [Semiring ğ•œ]
    [Algebra â„šâ‰¥0 ğ•œ] [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] :
    Tendsto (algebraMap â„šâ‰¥0 ğ•œ âˆ˜ fun n : â„• â†¦ (n : â„šâ‰¥0)â»Â¹) atTop (ğ“ 0) := by
  convert (continuous_algebraMap â„šâ‰¥0 ğ•œ).continuousAt.tendsto.comp
    tendsto_inv_atTop_nhds_zero_nat
  rw [map_zero]

theorem tendsto_inv_atTop_nhds_zero_nat {ğ•œ : Type*} [Semifield ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] :
    Tendsto (fun n : â„• â†¦ (n : ğ•œ)â»Â¹) atTop (ğ“ 0) := by
  convert NNRat.tendsto_algebraMap_inv_atTop_nhds_zero_nat ğ•œ
  simp

theorem tendsto_const_div_atTop_nhds_zero_nat {ğ•œ : Type*} [Semifield ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] [ContinuousMul ğ•œ] (C : ğ•œ) :
    Tendsto (fun n : â„• â†¦ C / n) atTop (ğ“ 0) := by
  simpa only [mul_zero, div_eq_mul_inv] using
    (tendsto_const_nhds (x := C)).mul tendsto_inv_atTop_nhds_zero_nat

theorem tendsto_one_div_atTop_nhds_zero_nat {ğ•œ : Type*} [Semifield ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] :
    Tendsto (fun n : â„• â†¦ 1 / (n : ğ•œ)) atTop (ğ“ 0) := by
  simp [tendsto_inv_atTop_nhds_zero_nat]

theorem EReal.tendsto_const_div_atTop_nhds_zero_nat {C : EReal} (h : C â‰  âŠ¥) (h' : C â‰  âŠ¤) :
    Tendsto (fun n : â„• â†¦ C / n) atTop (ğ“ 0) := by
  have : (fun n : â„• â†¦ C / n) = fun n : â„• â†¦ ((C.toReal / n : â„) : EReal) := by
    ext n
    nth_rw 1 [â† coe_toReal h' h, â† coe_coe_eq_natCast n, â† coe_div C.toReal n]
  rw [this, â† coe_zero, tendsto_coe]
  exact _root_.tendsto_const_div_atTop_nhds_zero_nat C.toReal

theorem tendsto_one_div_add_atTop_nhds_zero_nat {ğ•œ : Type*} [Semifield ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] :
    Tendsto (fun n : â„• â†¦ 1 / ((n : ğ•œ) + 1)) atTop (ğ“ 0) :=
  suffices Tendsto (fun n : â„• â†¦ 1 / (â†‘(n + 1) : ğ•œ)) atTop (ğ“ 0) by simpa
  (tendsto_add_atTop_iff_nat 1).2 tendsto_one_div_atTop_nhds_zero_nat

theorem tendsto_algebraMap_inv_atTop_nhds_zero_nat {ğ•œ : Type*} (A : Type*)
    [Semifield ğ•œ] [CharZero ğ•œ] [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ]
    [Semiring A] [Algebra ğ•œ A] [TopologicalSpace A] [ContinuousSMul ğ•œ A] :
    Tendsto (algebraMap ğ•œ A âˆ˜ fun n : â„• â†¦ (n : ğ•œ)â»Â¹) atTop (ğ“ 0) := by
  convert (continuous_algebraMap ğ•œ A).continuousAt.tendsto.comp tendsto_inv_atTop_nhds_zero_nat
  rw [map_zero]

/-- The limit of `n / (n + x)` is 1, for any constant `x` (valid in `â„` or any topological division
algebra over `â„šâ‰¥0`, e.g., `â„‚`). -/
theorem tendsto_natCast_div_add_atTop {ğ•œ : Type*} [DivisionSemiring ğ•œ] [TopologicalSpace ğ•œ]
    [CharZero ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] [IsTopologicalSemiring ğ•œ] [ContinuousInvâ‚€ ğ•œ] (x : ğ•œ) :
    Tendsto (fun n : â„• â†¦ (n : ğ•œ) / (n + x)) atTop (ğ“ 1) := by
  convert Tendsto.congr' ((eventually_ne_atTop 0).mp (Eventually.of_forall fun n hn â†¦ _)) _
  Â· exact fun n : â„• â†¦ 1 / (1 + x / n)
  Â· simp [Nat.cast_ne_zero.mpr hn, add_div']
  Â· have : ğ“ (1 : ğ•œ) = ğ“ (1 / (1 + x * (0 : ğ•œ))) := by
      rw [mul_zero, add_zero, div_one]
    rw [this]
    refine tendsto_const_nhds.div (tendsto_const_nhds.add ?_) (by simp)
    simp_rw [div_eq_mul_inv]
    refine tendsto_const_nhds.mul ?_
    have := ((continuous_algebraMap â„šâ‰¥0 ğ•œ).tendsto _).comp tendsto_inv_atTop_nhds_zero_nat
    rw [map_zero, Filter.tendsto_atTop'] at this
    refine Iff.mpr tendsto_atTop' ?_
    intros
    simp_all only [comp_apply, map_invâ‚€, map_natCast]

theorem tendsto_add_mul_div_add_mul_atTop_nhds {ğ•œ : Type*} [Semifield ğ•œ] [CharZero ğ•œ]
    [TopologicalSpace ğ•œ] [ContinuousSMul â„šâ‰¥0 ğ•œ] [IsTopologicalSemiring ğ•œ] [ContinuousInvâ‚€ ğ•œ]
    (a b c : ğ•œ) {d : ğ•œ} (hd : d â‰  0) :
    Tendsto (fun k : â„• â†¦ (a + c * k) / (b + d * k)) atTop (ğ“ (c / d)) := by
  apply Filter.Tendsto.congr'
  case fâ‚ => exact fun k â†¦ (a * (â†‘k)â»Â¹ + c) / (b * (â†‘k)â»Â¹ + d)
  Â· refine (eventually_ne_atTop 0).mp (Eventually.of_forall ?_)
    intro h hx
    dsimp
    field_simp (discharger := norm_cast)
  Â· apply Filter.Tendsto.div _ _ hd
    all_goals
      apply zero_add (_ : ğ•œ) â–¸ Filter.Tendsto.add_const _ _
      apply mul_zero (_ : ğ•œ) â–¸ Filter.Tendsto.const_mul _ _
      exact tendsto_inv_atTop_nhds_zero_nat

/-- For any positive `m : â„•`, `((n % m : â„•) : â„) / (n : â„)` tends to `0` as `n` tends to `âˆ`. -/
theorem tendsto_mod_div_atTop_nhds_zero_nat {m : â„•} (hm : 0 < m) :
    Tendsto (fun n : â„• => ((n % m : â„•) : â„) / (n : â„)) atTop (ğ“ 0) := by
  have h0 : âˆ€á¶  n : â„• in atTop, 0 â‰¤ (fun n : â„• => ((n % m : â„•) : â„)) n := by aesop
  exact tendsto_bdd_div_atTop_nhds_zero h0
    (.of_forall (fun n â†¦ cast_le.mpr (mod_lt n hm).le)) tendsto_natCast_atTop_atTop

/-- If `a â‰  0`, `(a * x + c)â»Â¹` tends to `0` as `x` tends to `âˆ`. -/
theorem tendsto_mul_add_inv_atTop_nhds_zero (a c : â„) (ha : a â‰  0) :
    Tendsto (fun x => (a * x + c)â»Â¹) atTop (ğ“ 0) := by
  obtain ha' | ha' := lt_or_gt_of_ne ha
  Â· exact tendsto_inv_atBot_zero.comp
      (tendsto_atBot_add_const_right _ c (tendsto_id.const_mul_atTop_of_neg ha'))
  Â· exact tendsto_inv_atTop_zero.comp
      (tendsto_atTop_add_const_right _ c (tendsto_id.const_mul_atTop ha'))

theorem Filter.EventuallyEq.div_mul_cancel {Î± G : Type*} [GroupWithZero G] {f g : Î± â†’ G}
    {l : Filter Î±} (hg : Tendsto g l (ğ“Ÿ {0}á¶œ)) : (fun x â†¦ f x / g x * g x) =á¶ [l] fun x â†¦ f x := by
  filter_upwards [hg.le_comap <| preimage_mem_comap (m := g) (mem_principal_self {0}á¶œ)] with x hx
  simp_all

/-- If `g` tends to `âˆ`, then eventually for all `x` we have `(f x / g x) * g x = f x`. -/
theorem Filter.EventuallyEq.div_mul_cancel_atTop {Î± K : Type*}
    [Semifield K] [LinearOrder K] [IsStrictOrderedRing K]
    {f g : Î± â†’ K} {l : Filter Î±} (hg : Tendsto g l atTop) :
    (fun x â†¦ f x / g x * g x) =á¶ [l] fun x â†¦ f x :=
  div_mul_cancel <| hg.mono_right <| le_principal_iff.mpr <|
    mem_of_superset (Ioi_mem_atTop 0) <| by simp

/-- If when `x` tends to `âˆ`, `g` tends to `âˆ` and `f x / g x` tends to a positive
  constant, then `f` tends to `âˆ`. -/
theorem Filter.Tendsto.num {Î± K : Type*} [Field K] [LinearOrder K] [IsStrictOrderedRing K]
    [TopologicalSpace K] [OrderTopology K]
    {f g : Î± â†’ K} {l : Filter Î±} (hg : Tendsto g l atTop) {a : K} (ha : 0 < a)
    (hlim : Tendsto (fun x => f x / g x) l (ğ“ a)) :
    Tendsto f l atTop :=
  (hlim.pos_mul_atTop ha hg).congr' (EventuallyEq.div_mul_cancel_atTop hg)

/-- If when `x` tends to `âˆ`, `g` tends to `âˆ` and `f x / g x` tends to a positive
  constant, then `f` tends to `âˆ`. -/
theorem Filter.Tendsto.den {Î± K : Type*} [Field K] [LinearOrder K] [IsStrictOrderedRing K]
    [TopologicalSpace K] [OrderTopology K]
    [ContinuousInv K] {f g : Î± â†’ K} {l : Filter Î±} (hf : Tendsto f l atTop) {a : K} (ha : 0 < a)
    (hlim : Tendsto (fun x => f x / g x) l (ğ“ a)) :
    Tendsto g l atTop :=
  have hlim' : Tendsto (fun x => g x / f x) l (ğ“ aâ»Â¹) := by
    simp_rw [â† inv_div (f _)]
    exact Filter.Tendsto.inv (f := fun x => f x / g x) hlim
  (hlim'.pos_mul_atTop (inv_pos_of_pos ha) hf).congr' (.div_mul_cancel_atTop hf)

/-- If when `x` tends to `âˆ`, `f x / g x` tends to a positive constant, then `f` tends to `âˆ` if
  and only if `g` tends to `âˆ`. -/
theorem Filter.Tendsto.num_atTop_iff_den_atTop {Î± K : Type*}
    [Field K] [LinearOrder K] [IsStrictOrderedRing K] [TopologicalSpace K]
    [OrderTopology K] [ContinuousInv K] {f g : Î± â†’ K} {l : Filter Î±} {a : K} (ha : 0 < a)
    (hlim : Tendsto (fun x => f x / g x) l (ğ“ a)) :
    Tendsto f l atTop â†” Tendsto g l atTop :=
  âŸ¨fun hf â†¦ hf.den ha hlim, fun hg â†¦ hg.num ha hlimâŸ©

/-! ### Powers -/


theorem tendsto_add_one_pow_atTop_atTop_of_pos
    [Semiring Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±] [Archimedean Î±] {r : Î±}
    (h : 0 < r) : Tendsto (fun n : â„• â†¦ (r + 1) ^ n) atTop atTop :=
  tendsto_atTop_atTop_of_monotone' (pow_right_monoâ‚€ <| le_add_of_nonneg_left h.le) <|
    not_bddAbove_iff.2 fun _ â†¦ Set.exists_range_iff.2 <| add_one_pow_unbounded_of_pos _ h

theorem tendsto_pow_atTop_atTop_of_one_lt
    [Semiring Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±] [ExistsAddOfLE Î±] [Archimedean Î±] {r : Î±}
    (h : 1 < r) : Tendsto (fun n : â„• â†¦ r ^ n) atTop atTop := by
  obtain âŸ¨r, r0, rflâŸ© := exists_pos_add_of_lt' h
  rw [add_comm]
  exact tendsto_add_one_pow_atTop_atTop_of_pos r0

theorem tendsto_pow_atTop_nhds_zero_of_lt_one {ğ•œ : Type*}
    [Semifield ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [ExistsAddOfLE ğ•œ] [Archimedean ğ•œ]
    [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] {r : ğ•œ} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) :
    Tendsto (fun n : â„• â†¦ r ^ n) atTop (ğ“ 0) :=
  hâ‚.eq_or_lt.elim
    (fun hr â†¦ (tendsto_add_atTop_iff_nat 1).mp <| by
      simp [_root_.pow_succ, â† hr])
    (fun hr â†¦
      have := (one_lt_invâ‚€ hr).2 hâ‚‚ |> tendsto_pow_atTop_atTop_of_one_lt
      (tendsto_inv_atTop_zero.comp this).congr fun n â†¦ by simp)

@[simp] theorem tendsto_pow_atTop_nhds_zero_iff {ğ•œ : Type*}
    [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [Archimedean ğ•œ]
    [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] {r : ğ•œ} :
    Tendsto (fun n : â„• â†¦ r ^ n) atTop (ğ“ 0) â†” |r| < 1 := by
  rw [tendsto_zero_iff_abs_tendsto_zero]
  refine âŸ¨fun h â†¦ by_contra (fun hr_le â†¦ ?_), fun h â†¦ ?_âŸ©
  Â· by_cases hr : 1 = |r|
    Â· replace h : Tendsto (fun n : â„• â†¦ |r| ^ n) atTop (ğ“ 0) := by simpa only [â† abs_pow, h]
      simp only [hr.symm, one_pow] at h
      exact zero_ne_one <| tendsto_nhds_unique h tendsto_const_nhds
    Â· apply @not_tendsto_nhds_of_tendsto_atTop ğ•œ â„• _ _ _ _ atTop _ (fun n â†¦ |r| ^ n) _ 0 _
      Â· refine (pow_right_strictMonoâ‚€ <| lt_of_le_of_ne (le_of_not_gt hr_le)
          hr).monotone.tendsto_atTop_atTop (fun b â†¦ ?_)
        obtain âŸ¨n, hnâŸ© := (pow_unbounded_of_one_lt b (lt_of_le_of_ne (le_of_not_gt hr_le) hr))
        exact âŸ¨n, le_of_lt hnâŸ©
      Â· simpa only [â† abs_pow]
  Â· simpa only [â† abs_pow] using (tendsto_pow_atTop_nhds_zero_of_lt_one (abs_nonneg r)) h

theorem tendsto_pow_atTop_nhdsWithin_zero_of_lt_one {ğ•œ : Type*}
    [Semifield ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [ExistsAddOfLE ğ•œ]
    [Archimedean ğ•œ] [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] {r : ğ•œ} (hâ‚ : 0 < r) (hâ‚‚ : r < 1) :
    Tendsto (fun n : â„• â†¦ r ^ n) atTop (ğ“[>] 0) :=
  tendsto_inf.2
    âŸ¨tendsto_pow_atTop_nhds_zero_of_lt_one hâ‚.le hâ‚‚,
      tendsto_principal.2 <| Eventually.of_forall fun _ â†¦ pow_pos hâ‚ _âŸ©

theorem uniformity_basis_dist_pow_of_lt_one {Î± : Type*} [PseudoMetricSpace Î±] {r : â„} (hâ‚€ : 0 < r)
    (hâ‚ : r < 1) :
    (uniformity Î±).HasBasis (fun _ : â„• â†¦ True) fun k â†¦ { p : Î± Ã— Î± | dist p.1 p.2 < r ^ k } :=
  Metric.mk_uniformity_basis (fun _ _ â†¦ pow_pos hâ‚€ _) fun _ Îµ0 â†¦
    (exists_pow_lt_of_lt_one Îµ0 hâ‚).imp fun _ hk â†¦ âŸ¨trivial, hk.leâŸ©

theorem geom_lt {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) {n : â„•} (hn : 0 < n)
    (h : âˆ€ k < n, c * u k < u (k + 1)) : c ^ n * u 0 < u n := by
  apply (monotone_mul_left_of_nonneg hc).seq_pos_lt_seq_of_le_of_lt hn _ _ h
  Â· simp
  Â· simp [_root_.pow_succ', mul_assoc, le_refl]

theorem geom_le {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) (n : â„•) (h : âˆ€ k < n, c * u k â‰¤ u (k + 1)) :
    c ^ n * u 0 â‰¤ u n := by
  apply (monotone_mul_left_of_nonneg hc).seq_le_seq n _ _ h <;>
    simp [_root_.pow_succ', mul_assoc, le_refl]

theorem lt_geom {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) {n : â„•} (hn : 0 < n)
    (h : âˆ€ k < n, u (k + 1) < c * u k) : u n < c ^ n * u 0 := by
  apply (monotone_mul_left_of_nonneg hc).seq_pos_lt_seq_of_lt_of_le hn _ h _
  Â· simp
  Â· simp [_root_.pow_succ', mul_assoc, le_refl]

theorem le_geom {u : â„• â†’ â„} {c : â„} (hc : 0 â‰¤ c) (n : â„•) (h : âˆ€ k < n, u (k + 1) â‰¤ c * u k) :
    u n â‰¤ c ^ n * u 0 := by
  apply (monotone_mul_left_of_nonneg hc).seq_le_seq n _ h _ <;>
    simp [_root_.pow_succ', mul_assoc, le_refl]

/-- If a sequence `v` of real numbers satisfies `k * v n â‰¤ v (n+1)` with `1 < k`,
then it goes to +âˆ. -/
theorem tendsto_atTop_of_geom_le {v : â„• â†’ â„} {c : â„} (hâ‚€ : 0 < v 0) (hc : 1 < c)
    (hu : âˆ€ n, c * v n â‰¤ v (n + 1)) : Tendsto v atTop atTop :=
  (tendsto_atTop_mono fun n â†¦ geom_le (zero_le_one.trans hc.le) n fun k _ â†¦ hu k) <|
    (tendsto_pow_atTop_atTop_of_one_lt hc).atTop_mul_const hâ‚€

theorem NNReal.tendsto_pow_atTop_nhds_zero_of_lt_one {r : â„â‰¥0} (hr : r < 1) :
    Tendsto (fun n : â„• â†¦ r ^ n) atTop (ğ“ 0) :=
  NNReal.tendsto_coe.1 <| by
    simp only [NNReal.coe_pow, NNReal.coe_zero,
      _root_.tendsto_pow_atTop_nhds_zero_of_lt_one r.coe_nonneg hr]

@[simp]
protected theorem NNReal.tendsto_pow_atTop_nhds_zero_iff {r : â„â‰¥0} :
    Tendsto (fun n : â„• => r ^ n) atTop (ğ“ 0) â†” r < 1 :=
  âŸ¨fun h => by simpa [coe_pow, coe_zero, abs_eq, coe_lt_one, val_eq_coe] using
    tendsto_pow_atTop_nhds_zero_iff.mp <| tendsto_coe.mpr h, tendsto_pow_atTop_nhds_zero_of_lt_oneâŸ©

theorem ENNReal.tendsto_pow_atTop_nhds_zero_of_lt_one {r : â„â‰¥0âˆ} (hr : r < 1) :
    Tendsto (fun n : â„• â†¦ r ^ n) atTop (ğ“ 0) := by
  rcases ENNReal.lt_iff_exists_coe.1 hr with âŸ¨r, rfl, hr'âŸ©
  rw [â† ENNReal.coe_zero]
  norm_cast at *
  apply NNReal.tendsto_pow_atTop_nhds_zero_of_lt_one hr

@[simp]
protected theorem ENNReal.tendsto_pow_atTop_nhds_zero_iff {r : â„â‰¥0âˆ} :
    Tendsto (fun n : â„• => r ^ n) atTop (ğ“ 0) â†” r < 1 := by
  refine âŸ¨fun h â†¦ ?_, tendsto_pow_atTop_nhds_zero_of_lt_oneâŸ©
  lift r to NNReal
  Â· refine fun hr â†¦ top_ne_zero (tendsto_nhds_unique (EventuallyEq.tendsto ?_) (hr â–¸ h))
    exact eventually_atTop.mpr âŸ¨1, fun _ hn â†¦ pow_eq_top_iff.mpr âŸ¨rfl, Nat.pos_iff_ne_zero.mp hnâŸ©âŸ©
  rw [â† coe_zero] at h
  norm_cast at h âŠ¢
  exact NNReal.tendsto_pow_atTop_nhds_zero_iff.mp h

@[simp]
protected theorem ENNReal.tendsto_pow_atTop_nhds_top_iff {r : â„â‰¥0âˆ} :
    Tendsto (fun n â†¦ r ^ n) atTop (ğ“ âˆ) â†” 1 < r := by
  refine âŸ¨?_, ?_âŸ©
  Â· contrapose!
    intro r_le_one h_tends
    specialize h_tends (Ioi_mem_nhds one_lt_top)
    simp only [Filter.mem_map, mem_atTop_sets, ge_iff_le, Set.mem_preimage, Set.mem_Ioi] at h_tends
    obtain âŸ¨n, hnâŸ© := h_tends
    exact lt_irrefl _ <| lt_of_lt_of_le (hn n le_rfl) <| pow_le_oneâ‚€ (zero_le _) r_le_one
  Â· intro r_gt_one
    have obs := @Tendsto.inv â„â‰¥0âˆ â„• _ _ _ (fun n â†¦ (râ»Â¹) ^ n) atTop 0
    simp only [ENNReal.tendsto_pow_atTop_nhds_zero_iff, inv_zero] at obs
    simpa [â† ENNReal.inv_pow] using obs <| ENNReal.inv_lt_one.mpr r_gt_one

lemma ENNReal.eq_zero_of_le_mul_pow {x r : â„â‰¥0âˆ} {Îµ : â„â‰¥0} (hr : r < 1)
    (h : âˆ€ n : â„•, x â‰¤ Îµ * r ^ n) : x = 0 := by
  rw [â† nonpos_iff_eq_zero]
  refine ge_of_tendsto' (f := fun (n : â„•) â†¦ Îµ * r ^ n) (x := atTop) ?_ h
  rw [â† mul_zero (Mâ‚€ := â„â‰¥0âˆ) (a := Îµ)]
  exact Tendsto.const_mul (tendsto_pow_atTop_nhds_zero_of_lt_one hr) (Or.inr coe_ne_top)

/-! ### Geometric series -/

section Geometric

theorem hasSum_geometric_of_lt_one {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) :
    HasSum (fun n : â„• â†¦ r ^ n) (1 - r)â»Â¹ :=
  have : r â‰  1 := ne_of_lt hâ‚‚
  have : Tendsto (fun n â†¦ (r ^ n - 1) * (r - 1)â»Â¹) atTop (ğ“ ((0 - 1) * (r - 1)â»Â¹)) :=
    ((tendsto_pow_atTop_nhds_zero_of_lt_one hâ‚ hâ‚‚).sub tendsto_const_nhds).mul tendsto_const_nhds
  (hasSum_iff_tendsto_nat_of_nonneg (pow_nonneg hâ‚) _).mpr <| by
    simp_all [neg_inv, geom_sum_eq, div_eq_mul_inv]

theorem summable_geometric_of_lt_one {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) :
    Summable fun n : â„• â†¦ r ^ n :=
  âŸ¨_, hasSum_geometric_of_lt_one hâ‚ hâ‚‚âŸ©


theorem tsum_geometric_of_lt_one {r : â„} (hâ‚ : 0 â‰¤ r) (hâ‚‚ : r < 1) : âˆ‘' n : â„•, r ^ n = (1 - r)â»Â¹ :=
  (hasSum_geometric_of_lt_one hâ‚ hâ‚‚).tsum_eq

theorem hasSum_geometric_two : HasSum (fun n : â„• â†¦ ((1 : â„) / 2) ^ n) 2 := by
  convert hasSum_geometric_of_lt_one _ _ <;> norm_num

theorem summable_geometric_two : Summable fun n : â„• â†¦ ((1 : â„) / 2) ^ n :=
  âŸ¨_, hasSum_geometric_twoâŸ©

theorem summable_geometric_two_encode {Î¹ : Type*} [Encodable Î¹] :
    Summable fun i : Î¹ â†¦ (1 / 2 : â„) ^ Encodable.encode i :=
  summable_geometric_two.comp_injective Encodable.encode_injective

theorem tsum_geometric_two : (âˆ‘' n : â„•, ((1 : â„) / 2) ^ n) = 2 :=
  hasSum_geometric_two.tsum_eq

theorem sum_geometric_two_le (n : â„•) : (âˆ‘ i âˆˆ range n, (1 / (2 : â„)) ^ i) â‰¤ 2 := by
  have : âˆ€ i, 0 â‰¤ (1 / (2 : â„)) ^ i := by
    intro i
    apply pow_nonneg
    norm_num
  convert summable_geometric_two.sum_le_tsum (range n) (fun i _ â†¦ this i)
  exact tsum_geometric_two.symm

theorem tsum_geometric_inv_two : (âˆ‘' n : â„•, (2 : â„)â»Â¹ ^ n) = 2 :=
  (inv_eq_one_div (2 : â„)).symm â–¸ tsum_geometric_two

/-- The sum of `2â»Â¹ ^ i` for `n â‰¤ i` equals `2 * 2â»Â¹ ^ n`. -/
theorem tsum_geometric_inv_two_ge (n : â„•) :
    (âˆ‘' i, ite (n â‰¤ i) ((2 : â„)â»Â¹ ^ i) 0) = 2 * 2â»Â¹ ^ n := by
  have A : Summable fun i : â„• â†¦ ite (n â‰¤ i) ((2â»Â¹ : â„) ^ i) 0 := by
    simpa only [â† piecewise_eq_indicator, one_div]
      using summable_geometric_two.indicator {i | n â‰¤ i}
  have B : ((Finset.range n).sum fun i : â„• â†¦ ite (n â‰¤ i) ((2â»Â¹ : â„) ^ i) 0) = 0 :=
    Finset.sum_eq_zero fun i hi â†¦
      ite_eq_right_iff.2 fun h â†¦ (lt_irrefl _ ((Finset.mem_range.1 hi).trans_le h)).elim
  simp only [â† Summable.sum_add_tsum_nat_add n A, B, if_true, zero_add, zero_le',
    le_add_iff_nonneg_left, pow_add, _root_.tsum_mul_right, tsum_geometric_inv_two]

theorem hasSum_geometric_two' (a : â„) : HasSum (fun n : â„• â†¦ a / 2 / 2 ^ n) a := by
  convert HasSum.mul_left (a / 2)
      (hasSum_geometric_of_lt_one (le_of_lt one_half_pos) one_half_lt_one) using 1
  Â· funext n
    simp only [one_div, inv_pow]
    rfl
  Â· norm_num

theorem summable_geometric_two' (a : â„) : Summable fun n : â„• â†¦ a / 2 / 2 ^ n :=
  âŸ¨a, hasSum_geometric_two' aâŸ©

theorem tsum_geometric_two' (a : â„) : âˆ‘' n : â„•, a / 2 / 2 ^ n = a :=
  (hasSum_geometric_two' a).tsum_eq

/-- **Sum of a Geometric Series** -/
theorem NNReal.hasSum_geometric {r : â„â‰¥0} (hr : r < 1) : HasSum (fun n : â„• â†¦ r ^ n) (1 - r)â»Â¹ := by
  apply NNReal.hasSum_coe.1
  push_cast
  rw [NNReal.coe_sub (le_of_lt hr)]
  exact hasSum_geometric_of_lt_one r.coe_nonneg hr

theorem NNReal.summable_geometric {r : â„â‰¥0} (hr : r < 1) : Summable fun n : â„• â†¦ r ^ n :=
  âŸ¨_, NNReal.hasSum_geometric hrâŸ©

theorem tsum_geometric_nnreal {r : â„â‰¥0} (hr : r < 1) : âˆ‘' n : â„•, r ^ n = (1 - r)â»Â¹ :=
  (NNReal.hasSum_geometric hr).tsum_eq

/-- The series `pow r` converges to `(1-r)â»Â¹`. For `r < 1` the RHS is a finite number,
and for `1 â‰¤ r` the RHS equals `âˆ`. -/
@[simp]
theorem ENNReal.tsum_geometric (r : â„â‰¥0âˆ) : âˆ‘' n : â„•, r ^ n = (1 - r)â»Â¹ := by
  rcases lt_or_ge r 1 with hr | hr
  Â· rcases ENNReal.lt_iff_exists_coe.1 hr with âŸ¨r, rfl, hr'âŸ©
    norm_cast at *
    convert ENNReal.tsum_coe_eq (NNReal.hasSum_geometric hr)
    rw [ENNReal.coe_inv <| ne_of_gt <| tsub_pos_iff_lt.2 hr, coe_sub, coe_one]
  Â· rw [tsub_eq_zero_iff_le.mpr hr, ENNReal.inv_zero, ENNReal.tsum_eq_iSup_nat, iSup_eq_top]
    refine fun a ha â†¦
      (ENNReal.exists_nat_gt (lt_top_iff_ne_top.1 ha)).imp fun n hn â†¦ lt_of_lt_of_le hn ?_
    calc
      (n : â„â‰¥0âˆ) = âˆ‘ i âˆˆ range n, 1 := by rw [sum_const, nsmul_one, card_range]
      _ â‰¤ âˆ‘ i âˆˆ range n, r ^ i := by gcongr; apply one_le_powâ‚€ hr

theorem ENNReal.tsum_geometric_add_one (r : â„â‰¥0âˆ) : âˆ‘' n : â„•, r ^ (n + 1) = r * (1 - r)â»Â¹ := by
  simp only [_root_.pow_succ', ENNReal.tsum_mul_left, ENNReal.tsum_geometric]

lemma ENNReal.tsum_two_zpow_neg_add_one :
    âˆ‘' m : â„•, 2 ^ (-1 - m : â„¤) = (1 : â„â‰¥0âˆ) := by
  simp_rw [neg_sub_left, ENNReal.zpow_neg, â† Nat.cast_one (R := â„¤), â† Nat.cast_add, zpow_natCast,
    ENNReal.inv_pow, ENNReal.tsum_geometric_add_one, one_sub_inv_two, inv_inv]
  exact ENNReal.inv_mul_cancel (Ne.symm (NeZero.ne' 2)) (Ne.symm top_ne_ofNat)

open Encodable

protected lemma ENNReal.tsum_geometric_two : âˆ‘' n, (2â»Â¹ : â„â‰¥0âˆ) ^ n = 2 := by simp

lemma ENNReal.tsum_geometric_two_encode_le_two {Î¹ : Type*} [Encodable Î¹] :
    âˆ‘' i : Î¹, (2â»Â¹ : â„â‰¥0âˆ) ^ encode i â‰¤ 2 :=
  (ENNReal.tsum_comp_le_tsum_of_injective encode_injective _).trans_eq ENNReal.tsum_geometric_two

lemma tsum_geometric_lt_top {r : â„â‰¥0âˆ} : âˆ‘' n, r ^ n < âˆ â†” r < 1 := by simp

lemma tsum_geometric_encode_lt_top {r : â„â‰¥0âˆ} (hr : r < 1) {Î¹ : Type*} [Encodable Î¹] :
    âˆ‘' i : Î¹, (r : â„â‰¥0âˆ) ^ encode i < âˆ :=
  (ENNReal.tsum_comp_le_tsum_of_injective encode_injective _).trans_lt <| by simpa

end Geometric

/-!
### Sequences with geometrically decaying distance in metric spaces

In this paragraph, we discuss sequences in metric spaces or emetric spaces for which the distance
between two consecutive terms decays geometrically. We show that such sequences are Cauchy
sequences, and bound their distances to the limit. We also discuss series with geometrically
decaying terms.
-/


section EdistLeGeometric

variable [PseudoEMetricSpace Î±] (r C : â„â‰¥0âˆ) (hr : r < 1) (hC : C â‰  âŠ¤) {f : â„• â†’ Î±}
  (hu : âˆ€ n, edist (f n) (f (n + 1)) â‰¤ C * r ^ n)

include hr hC hu in
/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, `C â‰  âˆ`, `r < 1`,
then `f` is a Cauchy sequence. -/
theorem cauchySeq_of_edist_le_geometric : CauchySeq f := by
  refine cauchySeq_of_edist_le_of_tsum_ne_top _ hu ?_
  rw [ENNReal.tsum_mul_left, ENNReal.tsum_geometric]
  have := (tsub_pos_iff_lt.2 hr).ne'
  finiteness

include hu in
/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
theorem edist_le_of_edist_le_geometric_of_tendsto {a : Î±} (ha : Tendsto f atTop (ğ“ a)) (n : â„•) :
    edist (f n) a â‰¤ C * r ^ n / (1 - r) := by
  convert edist_le_tsum_of_edist_le_of_tendsto _ hu ha _
  simp only [pow_add, ENNReal.tsum_mul_left, ENNReal.tsum_geometric, div_eq_mul_inv, mul_assoc]

include hu in
/-- If `edist (f n) (f (n+1))` is bounded by `C * r^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
theorem edist_le_of_edist_le_geometric_of_tendstoâ‚€ {a : Î±} (ha : Tendsto f atTop (ğ“ a)) :
    edist (f 0) a â‰¤ C / (1 - r) := by
  simpa only [_root_.pow_zero, mul_one] using edist_le_of_edist_le_geometric_of_tendsto r C hu ha 0

end EdistLeGeometric

section EdistLeGeometricTwo

variable [PseudoEMetricSpace Î±] (C : â„â‰¥0âˆ) (hC : C â‰  âŠ¤) {f : â„• â†’ Î±}
  (hu : âˆ€ n, edist (f n) (f (n + 1)) â‰¤ C / 2 ^ n) {a : Î±} (ha : Tendsto f atTop (ğ“ a))

include hC hu in
/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then `f` is a Cauchy sequence. -/
theorem cauchySeq_of_edist_le_geometric_two : CauchySeq f := by
  simp only [div_eq_mul_inv, ENNReal.inv_pow] at hu
  refine cauchySeq_of_edist_le_geometric 2â»Â¹ C ?_ hC hu
  simp

include hu ha in
/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f n` to the limit of `f` is bounded above by `2 * C * 2^-n`. -/
theorem edist_le_of_edist_le_geometric_two_of_tendsto (n : â„•) : edist (f n) a â‰¤ 2 * C / 2 ^ n := by
  simp only [div_eq_mul_inv, ENNReal.inv_pow] at *
  rw [mul_assoc, mul_comm]
  convert edist_le_of_edist_le_geometric_of_tendsto 2â»Â¹ C hu ha n using 1
  rw [ENNReal.one_sub_inv_two, div_eq_mul_inv, inv_inv]

include hu ha in
/-- If `edist (f n) (f (n+1))` is bounded by `C * 2^-n`, then the distance from
`f 0` to the limit of `f` is bounded above by `2 * C`. -/
theorem edist_le_of_edist_le_geometric_two_of_tendstoâ‚€ : edist (f 0) a â‰¤ 2 * C := by
  simpa only [_root_.pow_zero, div_eq_mul_inv, inv_one, mul_one] using
    edist_le_of_edist_le_geometric_two_of_tendsto C hu ha 0

end EdistLeGeometricTwo

section LeGeometric

variable [PseudoMetricSpace Î±] {r C : â„} {f : â„• â†’ Î±}

section
variable (hr : r < 1) (hu : âˆ€ n, dist (f n) (f (n + 1)) â‰¤ C * r ^ n)
include hr hu

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then `f` is a Cauchy sequence. -/
theorem aux_hasSum_of_le_geometric : HasSum (fun n : â„• â†¦ C * r ^ n) (C / (1 - r)) := by
  rcases sign_cases_of_C_mul_pow_nonneg fun n â†¦ dist_nonneg.trans (hu n) with (rfl | âŸ¨_, râ‚€âŸ©)
  Â· simp [hasSum_zero]
  Â· refine HasSum.mul_left C ?_
    simpa using hasSum_geometric_of_lt_one râ‚€ hr

variable (r C)

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then `f` is a Cauchy sequence.
Note that this lemma does not assume `0 â‰¤ C` or `0 â‰¤ r`. -/
theorem cauchySeq_of_le_geometric : CauchySeq f :=
  cauchySeq_of_dist_le_of_summable _ hu âŸ¨_, aux_hasSum_of_le_geometric hr huâŸ©

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f n` to the limit of `f` is bounded above by `C * r^n / (1 - r)`. -/
theorem dist_le_of_le_geometric_of_tendstoâ‚€ {a : Î±} (ha : Tendsto f atTop (ğ“ a)) :
    dist (f 0) a â‰¤ C / (1 - r) :=
  (aux_hasSum_of_le_geometric hr hu).tsum_eq â–¸
    dist_le_tsum_of_dist_le_of_tendstoâ‚€ _ hu âŸ¨_, aux_hasSum_of_le_geometric hr huâŸ© ha

/-- If `dist (f n) (f (n+1))` is bounded by `C * r^n`, `r < 1`, then the distance from
`f 0` to the limit of `f` is bounded above by `C / (1 - r)`. -/
theorem dist_le_of_le_geometric_of_tendsto {a : Î±} (ha : Tendsto f atTop (ğ“ a)) (n : â„•) :
    dist (f n) a â‰¤ C * r ^ n / (1 - r) := by
  have := aux_hasSum_of_le_geometric hr hu
  convert dist_le_tsum_of_dist_le_of_tendsto _ hu âŸ¨_, thisâŸ© ha n
  simp only [pow_add, mul_left_comm C, mul_div_right_comm]
  rw [mul_comm]
  exact (this.mul_left _).tsum_eq.symm

end

variable (huâ‚‚ : âˆ€ n, dist (f n) (f (n + 1)) â‰¤ C / 2 / 2 ^ n)
include huâ‚‚

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then `f` is a Cauchy sequence. -/
theorem cauchySeq_of_le_geometric_two : CauchySeq f :=
  cauchySeq_of_dist_le_of_summable _ huâ‚‚ <| âŸ¨_, hasSum_geometric_two' CâŸ©

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f 0` to the limit of `f` is bounded above by `C`. -/
theorem dist_le_of_le_geometric_two_of_tendstoâ‚€ {a : Î±} (ha : Tendsto f atTop (ğ“ a)) :
    dist (f 0) a â‰¤ C :=
  tsum_geometric_two' C â–¸ dist_le_tsum_of_dist_le_of_tendstoâ‚€ _ huâ‚‚ (summable_geometric_two' C) ha

/-- If `dist (f n) (f (n+1))` is bounded by `(C / 2) / 2^n`, then the distance from
`f n` to the limit of `f` is bounded above by `C / 2^n`. -/
theorem dist_le_of_le_geometric_two_of_tendsto {a : Î±} (ha : Tendsto f atTop (ğ“ a)) (n : â„•) :
    dist (f n) a â‰¤ C / 2 ^ n := by
  convert dist_le_tsum_of_dist_le_of_tendsto _ huâ‚‚ (summable_geometric_two' C) ha n
  simp only [add_comm n, pow_add, â† div_div]
  symm
  exact ((hasSum_geometric_two' C).div_const _).tsum_eq

end LeGeometric

/-! ### Summability tests based on comparison with geometric series -/


/-- A series whose terms are bounded by the terms of a converging geometric series converges. -/
theorem summable_one_div_pow_of_le {m : â„} {f : â„• â†’ â„•} (hm : 1 < m) (fi : âˆ€ i, i â‰¤ f i) :
    Summable fun i â†¦ 1 / m ^ f i := by
  refine .of_nonneg_of_le (fun a â†¦ by positivity) (fun a â†¦ ?_)
      (summable_geometric_of_lt_one (one_div_nonneg.mpr (zero_le_one.trans hm.le))
        ((one_div_lt (zero_lt_one.trans hm) zero_lt_one).mpr (one_div_one.le.trans_lt hm)))
  rw [div_pow, one_pow]
  refine (one_div_le_one_div ?_ ?_).mpr (pow_right_monoâ‚€ hm.le (fi a)) <;>
    exact pow_pos (zero_lt_one.trans hm) _

/-! ### Positive sequences with small sums on countable types -/


/-- For any positive `Îµ`, define on an encodable type a positive sequence with sum less than `Îµ` -/
def posSumOfEncodable {Îµ : â„} (hÎµ : 0 < Îµ) (Î¹) [Encodable Î¹] :
    { Îµ' : Î¹ â†’ â„ // (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒ c, HasSum Îµ' c âˆ§ c â‰¤ Îµ } := by
  let f n := Îµ / 2 / 2 ^ n
  have hf : HasSum f Îµ := hasSum_geometric_two' _
  have f0 : âˆ€ n, 0 < f n := fun n â†¦ div_pos (half_pos hÎµ) (pow_pos zero_lt_two _)
  refine âŸ¨f âˆ˜ Encodable.encode, fun i â†¦ f0 _, ?_âŸ©
  rcases hf.summable.comp_injective (@Encodable.encode_injective Î¹ _) with âŸ¨c, hgâŸ©
  refine âŸ¨c, hg, hasSum_le_inj _ (@Encodable.encode_injective Î¹ _) ?_ ?_ hg hfâŸ©
  Â· intro i _
    exact le_of_lt (f0 _)
  Â· intro n
    exact le_rfl

theorem Set.Countable.exists_pos_hasSum_le {Î¹ : Type*} {s : Set Î¹} (hs : s.Countable) {Îµ : â„}
    (hÎµ : 0 < Îµ) : âˆƒ Îµ' : Î¹ â†’ â„, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒ c, HasSum (fun i : s â†¦ Îµ' i) c âˆ§ c â‰¤ Îµ := by
  classical
  haveI := hs.toEncodable
  rcases posSumOfEncodable hÎµ s with âŸ¨f, hf0, âŸ¨c, hfc, hcÎµâŸ©âŸ©
  refine âŸ¨fun i â†¦ if h : i âˆˆ s then f âŸ¨i, hâŸ© else 1, fun i â†¦ ?_, âŸ¨c, ?_, hcÎµâŸ©âŸ©
  Â· conv_rhs => simp
    split_ifs
    exacts [hf0 _, zero_lt_one]
  Â· simpa only [Subtype.coe_prop, dif_pos, Subtype.coe_eta]

theorem Set.Countable.exists_pos_forall_sum_le {Î¹ : Type*} {s : Set Î¹} (hs : s.Countable) {Îµ : â„}
    (hÎµ : 0 < Îµ) : âˆƒ Îµ' : Î¹ â†’ â„,
    (âˆ€ i, 0 < Îµ' i) âˆ§ âˆ€ t : Finset Î¹, â†‘t âŠ† s â†’ âˆ‘ i âˆˆ t, Îµ' i â‰¤ Îµ := by
  classical
  rcases hs.exists_pos_hasSum_le hÎµ with âŸ¨Îµ', hpos, c, hÎµ'c, hcÎµâŸ©
  refine âŸ¨Îµ', hpos, fun t ht â†¦ ?_âŸ©
  rw [â† sum_subtype_of_mem _ ht]
  refine (sum_le_hasSum _ ?_ hÎµ'c).trans hcÎµ
  exact fun _ _ â†¦ (hpos _).le

namespace NNReal

theorem exists_pos_sum_of_countable {Îµ : â„â‰¥0} (hÎµ : Îµ â‰  0) (Î¹) [Countable Î¹] :
    âˆƒ Îµ' : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆƒ c, HasSum Îµ' c âˆ§ c < Îµ := by
  cases nonempty_encodable Î¹
  obtain âŸ¨a, a0, aÎµâŸ© := exists_between (pos_iff_ne_zero.2 hÎµ)
  obtain âŸ¨Îµ', hÎµ', c, hc, hcÎµâŸ© := posSumOfEncodable a0 Î¹
  exact
    âŸ¨fun i â†¦ âŸ¨Îµ' i, (hÎµ' i).leâŸ©, fun i â†¦ NNReal.coe_lt_coe.1 <| hÎµ' i,
      âŸ¨c, hasSum_le (fun i â†¦ (hÎµ' i).le) hasSum_zero hcâŸ©, NNReal.hasSum_coe.1 hc,
      aÎµ.trans_le' <| NNReal.coe_le_coe.1 hcÎµâŸ©

end NNReal

namespace ENNReal

theorem exists_pos_sum_of_countable {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (Î¹) [Countable Î¹] :
    âˆƒ Îµ' : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Îµ' i) âˆ§ (âˆ‘' i, (Îµ' i : â„â‰¥0âˆ)) < Îµ := by
  rcases exists_between (pos_iff_ne_zero.2 hÎµ) with âŸ¨r, h0r, hrÎµâŸ©
  rcases lt_iff_exists_coe.1 hrÎµ with âŸ¨x, rfl, _âŸ©
  rcases NNReal.exists_pos_sum_of_countable (coe_pos.1 h0r).ne' Î¹ with âŸ¨Îµ', hp, c, hc, hcrâŸ©
  exact âŸ¨Îµ', hp, (ENNReal.tsum_coe_eq hc).symm â–¸ lt_trans (coe_lt_coe.2 hcr) hrÎµâŸ©

theorem exists_pos_sum_of_countable' {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) (Î¹) [Countable Î¹] :
    âˆƒ Îµ' : Î¹ â†’ â„â‰¥0âˆ, (âˆ€ i, 0 < Îµ' i) âˆ§ âˆ‘' i, Îµ' i < Îµ :=
  let âŸ¨Î´, Î´pos, hÎ´âŸ© := exists_pos_sum_of_countable hÎµ Î¹
  âŸ¨fun i â†¦ Î´ i, fun i â†¦ ENNReal.coe_pos.2 (Î´pos i), hÎ´âŸ©

theorem exists_pos_tsum_mul_lt_of_countable {Îµ : â„â‰¥0âˆ} (hÎµ : Îµ â‰  0) {Î¹} [Countable Î¹] (w : Î¹ â†’ â„â‰¥0âˆ)
    (hw : âˆ€ i, w i â‰  âˆ) : âˆƒ Î´ : Î¹ â†’ â„â‰¥0, (âˆ€ i, 0 < Î´ i) âˆ§ (âˆ‘' i, (w i * Î´ i : â„â‰¥0âˆ)) < Îµ := by
  lift w to Î¹ â†’ â„â‰¥0 using hw
  rcases exists_pos_sum_of_countable hÎµ Î¹ with âŸ¨Î´', Hpos, HsumâŸ©
  have : âˆ€ i, 0 < max 1 (w i) := fun i â†¦ zero_lt_one.trans_le (le_max_left _ _)
  refine âŸ¨fun i â†¦ Î´' i / max 1 (w i), fun i â†¦ div_pos (Hpos _) (this i), ?_âŸ©
  refine lt_of_le_of_lt (ENNReal.tsum_le_tsum fun i â†¦ ?_) Hsum
  rw [coe_div (this i).ne']
  refine mul_le_of_le_div' ?_
  grw [â† le_max_right]

end ENNReal

/-!
### Factorial
-/


theorem factorial_tendsto_atTop : Tendsto Nat.factorial atTop atTop :=
  tendsto_atTop_atTop_of_monotone (fun _ _ â†¦ Nat.factorial_le) fun n â†¦ âŸ¨n, n.self_le_factorialâŸ©

theorem tendsto_factorial_div_pow_self_atTop :
    Tendsto (fun n â†¦ n ! / (n : â„) ^ n : â„• â†’ â„) atTop (ğ“ 0) :=
  tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds
    (tendsto_const_div_atTop_nhds_zero_nat 1)
    (Eventually.of_forall fun n â†¦
      div_nonneg (mod_cast n.factorial_pos.le)
        (pow_nonneg (mod_cast n.zero_le) _))
    (by
      refine (eventually_gt_atTop 0).mono fun n hn â†¦ ?_
      rcases Nat.exists_eq_succ_of_ne_zero hn.ne.symm with âŸ¨k, rflâŸ©
      rw [factorial_eq_prod_range_add_one, pow_eq_prod_const, div_eq_mul_inv, â† inv_eq_one_div,
        prod_natCast, Nat.cast_succ, â† Finset.prod_inv_distrib, â† prod_mul_distrib,
        Finset.prod_range_succ']
      simp only [one_mul, Nat.cast_add, zero_add, Nat.cast_one]
      refine
            mul_le_of_le_one_left (inv_nonneg.mpr <| mod_cast hn.le) (prod_le_one ?_ ?_) <;>
          intro x hx <;>
        rw [Finset.mem_range] at hx
      Â· positivity
      Â· refine (div_le_one <| mod_cast hn).mpr ?_
        norm_cast
        cutsat)

/-!
### Ceil and floor
-/


section

theorem tendsto_nat_floor_atTop {Î± : Type*}
    [Semiring Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±] [FloorSemiring Î±] :
    Tendsto (fun x : Î± â†¦ âŒŠxâŒ‹â‚Š) atTop atTop :=
  Nat.floor_mono.tendsto_atTop_atTop fun x â†¦ âŸ¨max 0 (x + 1), by simp [Nat.le_floor_iff]âŸ©

lemma tendsto_nat_ceil_atTop {Î± : Type*}
    [Semiring Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±] [FloorSemiring Î±] :
    Tendsto (fun x : Î± â†¦ âŒˆxâŒ‰â‚Š) atTop atTop := by
  refine Nat.ceil_mono.tendsto_atTop_atTop (fun x â†¦ âŸ¨x, ?_âŸ©)
  simp only [Nat.ceil_natCast, le_refl]

lemma tendsto_nat_floor_mul_atTop {Î± : Type _}
    [Semifield Î±] [LinearOrder Î±] [IsStrictOrderedRing Î±] [FloorSemiring Î±]
    [Archimedean Î±] (a : Î±) (ha : 0 < a) : Tendsto (fun (x : â„•) => âŒŠa * xâŒ‹â‚Š) atTop atTop :=
  Tendsto.comp tendsto_nat_floor_atTop
    <| Tendsto.const_mul_atTop ha tendsto_natCast_atTop_atTop

variable {R : Type*} [TopologicalSpace R] [Field R] [LinearOrder R] [IsStrictOrderedRing R]
  [OrderTopology R] [FloorRing R]

theorem tendsto_nat_floor_mul_div_atTop {a : R} (ha : 0 â‰¤ a) :
    Tendsto (fun x â†¦ (âŒŠa * xâŒ‹â‚Š : R) / x) atTop (ğ“ a) := by
  have A : Tendsto (fun x : R â†¦ a - xâ»Â¹) atTop (ğ“ (a - 0)) :=
    tendsto_const_nhds.sub tendsto_inv_atTop_zero
  rw [sub_zero] at A
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le' A tendsto_const_nhds
  Â· refine eventually_atTop.2 âŸ¨1, fun x hx â†¦ ?_âŸ©
    simp only [le_div_iffâ‚€ (zero_lt_one.trans_le hx), _root_.sub_mul,
      inv_mul_cancelâ‚€ (zero_lt_one.trans_le hx).ne']
    have := Nat.lt_floor_add_one (a * x)
    linarith
  Â· refine eventually_atTop.2 âŸ¨1, fun x hx â†¦ ?_âŸ©
    rw [div_le_iffâ‚€ (zero_lt_one.trans_le hx)]
    simp [Nat.floor_le (mul_nonneg ha (zero_le_one.trans hx))]

theorem tendsto_nat_floor_div_atTop : Tendsto (fun x â†¦ (âŒŠxâŒ‹â‚Š : R) / x) atTop (ğ“ 1) := by
  simpa using tendsto_nat_floor_mul_div_atTop (zero_le_one' R)

theorem tendsto_nat_ceil_mul_div_atTop {a : R} (ha : 0 â‰¤ a) :
    Tendsto (fun x â†¦ (âŒˆa * xâŒ‰â‚Š : R) / x) atTop (ğ“ a) := by
  have A : Tendsto (fun x : R â†¦ a + xâ»Â¹) atTop (ğ“ (a + 0)) :=
    tendsto_const_nhds.add tendsto_inv_atTop_zero
  rw [add_zero] at A
  apply tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds A
  Â· refine eventually_atTop.2 âŸ¨1, fun x hx â†¦ ?_âŸ©
    rw [le_div_iffâ‚€ (zero_lt_one.trans_le hx)]
    exact Nat.le_ceil _
  Â· refine eventually_atTop.2 âŸ¨1, fun x hx â†¦ ?_âŸ©
    simp [div_le_iffâ‚€ (zero_lt_one.trans_le hx), inv_mul_cancelâ‚€ (zero_lt_one.trans_le hx).ne',
      (Nat.ceil_lt_add_one (mul_nonneg ha (zero_le_one.trans hx))).le, add_mul]

theorem tendsto_nat_ceil_div_atTop : Tendsto (fun x â†¦ (âŒˆxâŒ‰â‚Š : R) / x) atTop (ğ“ 1) := by
  simpa using tendsto_nat_ceil_mul_div_atTop (zero_le_one' R)

lemma Nat.tendsto_div_const_atTop {n : â„•} (hn : n â‰  0) : Tendsto (Â· / n) atTop atTop := by
  rw [Tendsto, map_div_atTop_eq_nat n hn.bot_lt]

end

@[deprecated (since := "2025-10-27")]
alias tendsto_inverse_atTop_nhds_zero_nat := tendsto_inv_atTop_nhds_zero_nat

@[deprecated (since := "2025-10-27")]
alias NNReal.tendsto_inverse_atTop_nhds_zero_nat := tendsto_inv_atTop_nhds_zero_nat

@[deprecated (since := "2025-10-27")]
alias NNReal.tendsto_const_div_atTop_nhds_zero_nat := tendsto_const_div_atTop_nhds_zero_nat

@[deprecated (since := "2025-10-27")]
alias NNReal.tendsto_algebraMap_inverse_atTop_nhds_zero_nat :=
  tendsto_algebraMap_inv_atTop_nhds_zero_nat

@[deprecated (since := "2025-10-27")]
alias tendsto_algebraMap_inverse_atTop_nhds_zero_nat :=
  tendsto_algebraMap_inv_atTop_nhds_zero_nat

@[deprecated (since := "2025-10-27")]
protected alias Nat.tendsto_pow_atTop_atTop_of_one_lt := tendsto_pow_atTop_atTop_of_one_lt
