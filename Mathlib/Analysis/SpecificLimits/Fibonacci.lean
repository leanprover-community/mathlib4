/-
Copyright (c) 2025 Snir Broshi. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Snir Broshi
-/
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.NumberTheory.Real.GoldenRatio

/-!
# The ratio of consecutive Fibonacci numbers

We prove that the ratio of consecutive Fibonacci numbers tends to the golden ratio.
-/

open Nat Real Filter Tendsto
open scoped Topology goldenRatio

/-- The limit of `fib (n + 1) / fib n` as `n â†’ âˆ` is the golden ratio. -/
theorem tendsto_fib_succ_div_fib_atTop :
    Tendsto (fun n â†¦ (fib (n + 1) / fib n : â„)) atTop (ğ“ Ï†) := by
  have hâ‚ n : (fib (n + 1) / fib n : â„) = (Ï† - Ïˆ * (Ïˆ / Ï†) ^ n) / (1 - (Ïˆ / Ï†) ^ n) := by
    simp only [coe_fib_eq, pow_succ, div_pow]
    field_simp
  have hâ‚‚ := tendsto_pow_atTop_nhds_zero_of_abs_lt_one (r := Ïˆ / Ï†) <| by
    rw [abs_div, div_lt_one <| by positivity, abs_of_pos goldenRatio_pos, abs_lt]
    ring_nf
    bound
  rw [show Ï† = (Ï† - Ïˆ * 0) / (1 - 0) by ring, funext hâ‚]
  exact const_sub _ (const_mul _ hâ‚‚) |>.div (const_sub _ hâ‚‚) <| by simp

/-- The limit of `fib n / fib (n + 1)` as `n â†’ âˆ` is the negative conjugate of the golden ratio. -/
theorem tendsto_fib_div_fib_succ_atTop :
    Tendsto (fun n â†¦ (fib n / fib (n + 1) : â„)) atTop (ğ“ (-Ïˆ)) := by
  convert tendsto_fib_succ_div_fib_atTop.invâ‚€ (by positivity) using 2
  Â· rw [inv_div]
  Â· rw [inv_goldenRatio]
