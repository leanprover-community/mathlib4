/-
Copyright (c) 2019 Abhimanyu Pallavi Sudhir. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Abhimanyu Pallavi Sudhir, Violeta HernÃ¡ndez Palacios
-/
module

public import Mathlib.Algebra.Order.Ring.StandardPart
public import Mathlib.Analysis.SpecificLimits.Basic
public import Mathlib.Order.Filter.FilterProduct

/-!
# Construction of the hyperreal numbers as an ultraproduct of real sequences

We define the `Hyperreal` numbers as quotients of sequences `â„• â†’ â„` by an ultrafilter. These form
a field, and we prove some of their basic properties.

Note that most of the machinery that is usually defined for the specific purpose of non-standard
analysis (infinitesimal and infinite elements, standard parts) has been generealized to other
non-archimedean fields. In particular:

- `ArchimedeanClass` can be used to measure whether an element is infinitesimal (`0 < mk x`) or
  infinite (`mk x < 0`).
- `ArchimedeanClass.stdPart` generalizes the standard part function for a general ordered field.

## Todo

Use ÅoÅ›'s Theorem `FirstOrder.Language.Ultraproduct.sentence_realize` to formalize the transfer
principle on `Hyperreal`.
-/

@[expose] public section

open ArchimedeanClass Filter Germ Topology

/-- Hyperreal numbers on the ultrafilter extending the cofinite filter. -/
def Hyperreal : Type :=
  Germ (hyperfilter â„• : Filter â„•) â„

noncomputable section

#adaptation_note
/-- After nightly-2025-05-07 we had to remove `deriving Inhabited` on `Hyperreal` above,
as there is a new error about this instance having to be noncomputable, and `deriving` doesn't allow
for adding this! -/
namespace Hyperreal

@[inherit_doc] notation "â„*" => Hyperreal

instance : Field â„* :=
  inferInstanceAs (Field (Germ _ _))

instance : LinearOrder â„* :=
  inferInstanceAs (LinearOrder (Germ _ _))

instance : IsStrictOrderedRing â„* :=
  inferInstanceAs (IsStrictOrderedRing (Germ _ _))

/-- Natural embedding `â„ â†’ â„*`. -/
@[coe] def ofReal : â„ â†’ â„* := const

instance : CoeTC â„ â„* := âŸ¨ofRealâŸ©

@[simp, norm_cast]
theorem coe_eq_coe {x y : â„} : (x : â„*) = y â†” x = y :=
  Germ.const_inj

theorem coe_ne_coe {x y : â„} : (x : â„*) â‰  y â†” x â‰  y :=
  coe_eq_coe.not

@[simp, norm_cast]
theorem coe_eq_zero {x : â„} : (x : â„*) = 0 â†” x = 0 :=
  coe_eq_coe

@[simp, norm_cast]
theorem coe_eq_one {x : â„} : (x : â„*) = 1 â†” x = 1 :=
  coe_eq_coe

@[norm_cast]
theorem coe_ne_zero {x : â„} : (x : â„*) â‰  0 â†” x â‰  0 :=
  coe_ne_coe

@[norm_cast]
theorem coe_ne_one {x : â„} : (x : â„*) â‰  1 â†” x â‰  1 :=
  coe_ne_coe

@[simp, norm_cast]
theorem coe_one : â†‘(1 : â„) = (1 : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_zero : â†‘(0 : â„) = (0 : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_inv (x : â„) : â†‘xâ»Â¹ = (xâ»Â¹ : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_neg (x : â„) : â†‘(-x) = (-x : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_add (x y : â„) : â†‘(x + y) = (x + y : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_ofNat (n : â„•) [n.AtLeastTwo] :
    ((ofNat(n) : â„) : â„*) = OfNat.ofNat n :=
  rfl

@[simp, norm_cast]
theorem coe_mul (x y : â„) : â†‘(x * y) = (x * y : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_div (x y : â„) : â†‘(x / y) = (x / y : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_sub (x y : â„) : â†‘(x - y) = (x - y : â„*) :=
  rfl

@[simp, norm_cast]
theorem coe_le_coe {x y : â„} : (x : â„*) â‰¤ y â†” x â‰¤ y :=
  Germ.const_le_iff

@[simp, norm_cast]
theorem coe_lt_coe {x y : â„} : (x : â„*) < y â†” x < y :=
  Germ.const_lt_iff

@[simp, norm_cast]
theorem coe_nonneg {x : â„} : 0 â‰¤ (x : â„*) â†” 0 â‰¤ x :=
  coe_le_coe

@[simp, norm_cast]
theorem coe_pos {x : â„} : 0 < (x : â„*) â†” 0 < x :=
  coe_lt_coe

@[simp, norm_cast]
theorem coe_abs (x : â„) : ((|x| : â„) : â„*) = |â†‘x| :=
  const_abs x

@[simp, norm_cast]
theorem coe_max (x y : â„) : ((max x y : â„) : â„*) = max â†‘x â†‘y :=
  Germ.const_max _ _

@[simp, norm_cast]
theorem coe_min (x y : â„) : ((min x y : â„) : â„*) = min â†‘x â†‘y :=
  Germ.const_min _ _

/-- The canonical map `â„ â†’ â„*` as an `OrderRingHom`. -/
def coeRingHom : â„ â†’+*o â„* where
  toFun x := x
  map_zero' := rfl
  map_one' := rfl
  map_add' _ _ := rfl
  map_mul' _ _ := rfl
  monotone' _ _ := coe_le_coe.2

@[simp]
theorem archimedeanClassMk_coe_nonneg (x : â„) : 0 â‰¤ mk (x : â„*) :=
  mk_map_nonneg_of_archimedean coeRingHom x

@[simp]
theorem archimdeanClassMk_coe {x : â„} (hx : x â‰  0) : mk (x : â„*) = 0 :=
  mk_map_of_archimedean' coeRingHom hx

@[simp]
theorem stdPart_coe (x : â„) : stdPart (x : â„*) = x :=
  stdPart_of_archimedean coeRingHom x

/-! ### Basic constants -/

/-- Construct a hyperreal number from a sequence of real numbers. -/
def ofSeq (f : â„• â†’ â„) : â„* := (â†‘f : Germ (hyperfilter â„• : Filter â„•) â„)

theorem ofSeq_surjective : Function.Surjective ofSeq := Quot.exists_rep

theorem ofSeq_lt_ofSeq {f g : â„• â†’ â„} : ofSeq f < ofSeq g â†” âˆ€á¶  n in hyperfilter â„•, f n < g n :=
  Germ.coe_lt

theorem ofSeq_le_ofSeq {f g : â„• â†’ â„} : ofSeq f â‰¤ ofSeq g â†” âˆ€á¶  n in hyperfilter â„•, f n â‰¤ g n :=
  Germ.coe_le

/-- A sample infinite hyperreal Ï‰ = âŸ¦(0, 1, 2, 3, â‹¯)âŸ§. -/
def omega : â„* := ofSeq Nat.cast

@[inherit_doc] scoped notation "Ï‰" => Hyperreal.omega
recommended_spelling "omega" for "Ï‰" in [omega, Â«termÏ‰Â»]

theorem coe_lt_omega (r : â„) : r < Ï‰ := by
  apply ofSeq_lt_ofSeq.2 <| Filter.Eventually.filter_mono Nat.hyperfilter_le_atTop _
  obtain âŸ¨n, hnâŸ© := exists_nat_gt r
  rw [eventually_atTop]
  exact âŸ¨n, fun m hm â†¦ hn.trans_le (mod_cast hm)âŸ©

theorem omega_pos : 0 < Ï‰ :=
  coe_lt_omega 0

@[simp]
theorem omega_ne_zero : Ï‰ â‰  0 :=
  omega_pos.ne'

@[simp]
theorem abs_omega : |Ï‰| = Ï‰ :=
  abs_of_pos omega_pos

@[simp]
theorem archimedeanClassMk_omega_neg : mk Ï‰ < 0 :=
  fun n â†¦ by simpa using coe_lt_omega n

/-- A sample infinitesimal hyperreal Îµ = âŸ¦(0, 1, 1/2, 1/3, â‹¯)âŸ§. -/
def epsilon : â„* :=
  ofSeq fun n => nâ»Â¹

@[inherit_doc] scoped notation "Îµ" => Hyperreal.epsilon
recommended_spelling "epsilon" for "Îµ" in [epsilon, Â«termÎµÂ»]

@[simp]
theorem inv_omega : Ï‰â»Â¹ = Îµ :=
  rfl

@[simp]
theorem inv_epsilon : Îµâ»Â¹ = Ï‰ :=
  @inv_inv _ _ Ï‰

@[simp]
theorem epsilon_pos : 0 < Îµ :=
  inv_pos_of_pos omega_pos

@[simp]
theorem epsilon_ne_zero : Îµ â‰  0 :=
  epsilon_pos.ne'

@[simp]
theorem epsilon_mul_omega : Îµ * Ï‰ = 1 :=
  @inv_mul_cancelâ‚€ _ _ Ï‰ omega_ne_zero

@[simp]
theorem archimedeanClassMk_epsilon_pos : 0 < mk Îµ := by
  simp [â† inv_omega]

@[simp]
theorem stdPart_epsilon : stdPart Îµ = 0 :=
  stdPart_eq_zero.2 <| archimedeanClassMk_epsilon_pos.ne'

theorem epsilon_lt_of_pos {r : â„} : 0 < r â†’ Îµ < r :=
  lt_of_pos_of_archimedean coeRingHom archimedeanClassMk_epsilon_pos

theorem epsilon_lt_of_neg {r : â„} : r < 0 â†’ r < Îµ :=
  lt_of_neg_of_archimedean coeRingHom archimedeanClassMk_epsilon_pos

@[deprecated (since := "2026-01-05")]
alias epsilon_lt_pos := epsilon_lt_of_pos

/-!
### Some facts about `Tendsto`
-/

@[simp]
theorem tendsto_ofSeq {f : â„• â†’ â„} {lb : Filter â„} :
    (ofSeq f).Tendsto lb â†” Tendsto f (hyperfilter â„•) lb :=
  .rfl

theorem tendsto_iff_forall {x : â„*} {r : â„} :
    x.Tendsto (ğ“ r) â†” (âˆ€ s < r, s â‰¤ x) âˆ§ (âˆ€ s > r, x â‰¤ s) := by
  rcases ofSeq_surjective x with âŸ¨f, rflâŸ©
  rw [tendsto_ofSeq, (nhds_basis_Ioo _).tendsto_right_iff]
  simp_rw [Set.mem_Ioo, eventually_and, â† ofSeq_lt_ofSeq]
  refine âŸ¨fun H â†¦ âŸ¨fun s hs â†¦ ?_, fun s hs â†¦ ?_âŸ©, fun H âŸ¨s, tâŸ© âŸ¨hs, htâŸ© â†¦ âŸ¨?_, ?_âŸ©âŸ©
  Â· obtain âŸ¨t, htâŸ© := exists_gt r
    exact (H âŸ¨s, tâŸ© âŸ¨hs, htâŸ©).1.le
  Â· obtain âŸ¨t, htâŸ© := exists_lt r
    exact (H âŸ¨t, sâŸ© âŸ¨ht, hsâŸ©).2.le
  Â· obtain âŸ¨u, hu, hu'âŸ© := exists_between hs
    exact (coe_lt_coe.2 hu).trans_le (H.1 _ hu')
  Â· obtain âŸ¨u, hu, hu'âŸ© := exists_between ht
    exact (H.2 _ hu).trans_lt (coe_lt_coe.2 hu')

theorem archimedeanClassMk_nonneg_of_tendsto {x : â„*} {r : â„} (hx : x.Tendsto (ğ“ r)) :
    0 â‰¤ mk x := by
  rw [tendsto_iff_forall] at hx
  obtain âŸ¨s, hsâŸ© := exists_lt r
  obtain âŸ¨t, htâŸ© := exists_gt r
  exact mk_nonneg_of_le_of_le_of_archimedean coeRingHom (hx.1 s hs) (hx.2 t ht)

theorem stdPart_of_tendsto {x : â„*} {r : â„} (hx : x.Tendsto (ğ“ r)) : stdPart x = r := by
  rw [tendsto_iff_forall] at hx
  exact stdPart_eq coeRingHom hx.1 hx.2

theorem archimedeanClassMk_pos_of_tendsto {x : â„*} (hx : x.Tendsto (ğ“ 0)) : 0 < mk x := by
  apply (archimedeanClassMk_nonneg_of_tendsto hx).lt_of_ne'
  rw [â† stdPart_eq_zero, stdPart_of_tendsto hx]

@[deprecated archimedeanClassMk_pos_of_tendsto (since := "2026-01-05")]
theorem lt_of_tendsto_zero_of_pos {f : â„• â†’ â„} (hf : Tendsto f atTop (ğ“ 0)) :
    âˆ€ {r : â„}, 0 < r â†’ ofSeq f < (r : â„*) := fun hr â†¦
  ofSeq_lt_ofSeq.2 <| (hf.eventually <| gt_mem_nhds hr).filter_mono Nat.hyperfilter_le_atTop

set_option linter.deprecated false in
@[deprecated archimedeanClassMk_pos_of_tendsto (since := "2026-01-05")]
theorem neg_lt_of_tendsto_zero_of_pos {f : â„• â†’ â„} (hf : Tendsto f atTop (ğ“ 0)) :
    âˆ€ {r : â„}, 0 < r â†’ (-r : â„*) < ofSeq f := fun hr =>
  have hg := hf.neg
  neg_lt_of_neg_lt (by rw [neg_zero] at hg; exact lt_of_tendsto_zero_of_pos hg hr)

set_option linter.deprecated false in
@[deprecated archimedeanClassMk_pos_of_tendsto (since := "2026-01-05")]
theorem gt_of_tendsto_zero_of_neg {f : â„• â†’ â„} (hf : Tendsto f atTop (ğ“ 0)) :
    âˆ€ {r : â„}, r < 0 â†’ (r : â„*) < ofSeq f := fun {r} hr => by
  rw [â† neg_neg r, coe_neg]; exact neg_lt_of_tendsto_zero_of_pos hf (neg_pos.mpr hr)

theorem lt_of_tendsto_atTop {x : â„*} (r : â„) (hx : x.Tendsto atTop) : r < x := by
  rcases ofSeq_surjective x with âŸ¨f, rflâŸ©
  rw [tendsto_ofSeq] at hx
  exact ofSeq_lt_ofSeq.2 <| hx.eventually_mem (Ioi_mem_atTop r)

theorem lt_of_tendsto_atBot {x : â„*} (r : â„) (hx : x.Tendsto atBot) : x < r := by
  rcases ofSeq_surjective x with âŸ¨f, rflâŸ©
  rw [tendsto_ofSeq] at hx
  exact ofSeq_lt_ofSeq.2 <| hx.eventually_mem (Iio_mem_atBot r)

theorem archimedeanClassMk_neg_of_tendsto_atTop {x : â„*} (hx : x.Tendsto atTop) : mk x < 0 := by
  have : 0 < x := lt_of_tendsto_atTop 0 hx
  intro n
  simpa [abs_of_pos this] using lt_of_tendsto_atTop n hx

theorem archimedeanClassMk_neg_of_tendsto_atBot {x : â„*} (hx : x.Tendsto atBot) : mk x < 0 := by
  have : x < 0 := lt_of_tendsto_atBot 0 hx
  intro n
  simpa [abs_of_neg this, lt_neg] using lt_of_tendsto_atBot (-n) hx

theorem tendsto_atTop_iff {x : â„*} : x.Tendsto atTop â†” 0 < x âˆ§ mk x < 0 where
  mp h := âŸ¨lt_of_tendsto_atTop 0 h, archimedeanClassMk_neg_of_tendsto_atTop hâŸ©
  mpr h := by
    rcases ofSeq_surjective x with âŸ¨f, rflâŸ©
    rw [tendsto_ofSeq, tendsto_atTop]
    exact fun r â†¦ ofSeq_le_ofSeq.1 <|
      (lt_of_mk_lt_mk_of_nonneg (h.2.trans_le <| archimedeanClassMk_coe_nonneg r) h.1.le).le

theorem tendsto_atBot_iff {x : â„*} : x.Tendsto atBot â†” x < 0 âˆ§ mk x < 0 where
  mp h := âŸ¨lt_of_tendsto_atBot 0 h, archimedeanClassMk_neg_of_tendsto_atBot hâŸ©
  mpr h := by
    rcases ofSeq_surjective x with âŸ¨f, rflâŸ©
    rw [tendsto_ofSeq, tendsto_atBot]
    exact fun r â†¦ ofSeq_le_ofSeq.1 <|
      (lt_of_mk_lt_mk_of_nonpos (h.2.trans_le <| archimedeanClassMk_coe_nonneg r) h.1.le).le

end Hyperreal
end

/-
Porting note (https://github.com/leanprover-community/mathlib4/issues/11215): TODO: restore `positivity` plugin

namespace Tactic

open Positivity

private theorem hyperreal_coe_ne_zero {r : â„} : r â‰  0 â†’ (r : â„*) â‰  0 :=
  Hyperreal.coe_ne_zero.2

private theorem hyperreal_coe_nonneg {r : â„} : 0 â‰¤ r â†’ 0 â‰¤ (r : â„*) :=
  Hyperreal.coe_nonneg.2

private theorem hyperreal_coe_pos {r : â„} : 0 < r â†’ 0 < (r : â„*) :=
  Hyperreal.coe_pos.2

/-- Extension for the `positivity` tactic: cast from `â„` to `â„*`. -/
@[positivity]
unsafe def positivity_coe_real_hyperreal : expr â†’ tactic strictness
  | q(@coe _ _ $(inst) $(a)) => do
    unify inst q(@coeToLift _ _ Hyperreal.hasCoeT)
    let strictness_a â† core a
    match strictness_a with
      | positive p => positive <$> mk_app `` hyperreal_coe_pos [p]
      | nonnegative p => nonnegative <$> mk_app `` hyperreal_coe_nonneg [p]
      | nonzero p => nonzero <$> mk_app `` hyperreal_coe_ne_zero [p]
  | e =>
    pp e >>= fail âˆ˜ format.bracket "The expression " " is not of the form `(r : â„*)` for `r : â„`"

end Tactic
-/
