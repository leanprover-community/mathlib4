/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, S√©bastien Gou√´zel, R√©my Degenne
-/
import Mathlib.Analysis.Convex.Jensen
import Mathlib.Analysis.Convex.SpecificFunctions.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal
import Mathlib.Tactic.Positivity

#align_import analysis.mean_inequalities_pow from "leanprover-community/mathlib"@"ccdbfb6e5614667af5aa3ab2d50885e0ef44a46f"

/-!
# Mean value inequalities

In this file we prove several mean inequalities for finite sums. Versions for integrals of some of
these inequalities are available in `MeasureTheory.MeanInequalities`.

## Main theorems: generalized mean inequality

The inequality says that for two non-negative vectors $w$ and $z$ with $\sum_{i\in s} w_i=1$
and $p ‚â§ q$ we have
$$
\sqrt[p]{\sum_{i\in s} w_i z_i^p} ‚â§ \sqrt[q]{\sum_{i\in s} w_i z_i^q}.
$$

Currently we only prove this inequality for $p=1$. As in the rest of `Mathlib`, we provide
different theorems for natural exponents (`pow_arith_mean_le_arith_mean_pow`), integer exponents
(`zpow_arith_mean_le_arith_mean_zpow`), and real exponents (`rpow_arith_mean_le_arith_mean_rpow` and
`arith_mean_le_rpow_mean`). In the first two cases we prove
$$
\left(\sum_{i\in s} w_i z_i\right)^n ‚â§ \sum_{i\in s} w_i z_i^n
$$
in order to avoid using real exponents. For real exponents we prove both this and standard versions.

## TODO

- each inequality `A ‚â§ B` should come with a theorem `A = B ‚Üî _`; one of the ways to prove them
  is to define `StrictConvexOn` functions.
- generalized mean inequality with any `p ‚â§ q`, including negative numbers;
- prove that the power mean tends to the geometric mean as the exponent tends to zero.

-/


universe u v

open Finset

open Classical BigOperators NNReal ENNReal

noncomputable section

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

variable {Œπ : Type u} (s : Finset Œπ)

namespace Real

theorem pow_arith_mean_le_arith_mean_pow (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i in s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) (n : ‚Ñï) :
    (‚àë i in s, w i * z i) ^ n ‚â§ ‚àë i in s, w i * z i ^ n :=
  (convexOn_pow n).map_sum_le hw hw' hz
#align real.pow_arith_mean_le_arith_mean_pow Real.pow_arith_mean_le_arith_mean_pow

theorem pow_arith_mean_le_arith_mean_pow_of_even (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i in s, w i = 1) {n : ‚Ñï} (hn : Even n) :
    (‚àë i in s, w i * z i) ^ n ‚â§ ‚àë i in s, w i * z i ^ n :=
  hn.convexOn_pow.map_sum_le hw hw' fun _ _ => trivial
#align real.pow_arith_mean_le_arith_mean_pow_of_even Real.pow_arith_mean_le_arith_mean_pow_of_even

/-- Specific case of Jensen's inequality for sums of powers -/
theorem pow_sum_div_card_le_sum_pow {f : Œπ ‚Üí ‚Ñù} (n : ‚Ñï) (hf : ‚àÄ a ‚àà s, 0 ‚â§ f a) :
    (‚àë x in s, f x) ^ (n + 1) / (s.card : ‚Ñù) ^ n ‚â§ ‚àë x in s, f x ^ (n + 1) := by
  rcases s.eq_empty_or_nonempty with (rfl | hs)
  -- ‚ä¢ (‚àë x in ‚àÖ, f x) ^ (n + 1) / ‚Üë(card ‚àÖ) ^ n ‚â§ ‚àë x in ‚àÖ, f x ^ (n + 1)
  ¬∑ simp_rw [Finset.sum_empty, zero_pow' _ (Nat.succ_ne_zero n), zero_div]; rfl
    -- ‚ä¢ 0 ‚â§ 0
                                                                            -- üéâ no goals
  ¬∑ have hs0 : 0 < (s.card : ‚Ñù) := Nat.cast_pos.2 hs.card_pos
    -- ‚ä¢ (‚àë x in s, f x) ^ (n + 1) / ‚Üë(card s) ^ n ‚â§ ‚àë x in s, f x ^ (n + 1)
    suffices (‚àë x in s, f x / s.card) ^ (n + 1) ‚â§ ‚àë x in s, f x ^ (n + 1) / s.card by
      rwa [‚Üê Finset.sum_div, ‚Üê Finset.sum_div, div_pow, pow_succ' (s.card : ‚Ñù), ‚Üê div_div,
        div_le_iff hs0, div_mul, div_self hs0.ne', div_one] at this
    have :=
      @ConvexOn.map_sum_le ‚Ñù ‚Ñù ‚Ñù Œπ _ _ _ _ _ _ (Set.Ici 0) (fun x => x ^ (n + 1)) s
        (fun _ => 1 / s.card) ((‚Üë) ‚àò f) (convexOn_pow (n + 1)) ?_ ?_ fun i hi =>
        Set.mem_Ici.2 (hf i hi)
    ¬∑ simpa only [inv_mul_eq_div, one_div, Algebra.id.smul_eq_mul] using this
      -- üéâ no goals
    ¬∑ simp only [one_div, inv_nonneg, Nat.cast_nonneg, imp_true_iff]
      -- üéâ no goals
    ¬∑ simpa only [one_div, Finset.sum_const, nsmul_eq_mul] using mul_inv_cancel hs0.ne'
      -- üéâ no goals
#align real.pow_sum_div_card_le_sum_pow Real.pow_sum_div_card_le_sum_pow

theorem zpow_arith_mean_le_arith_mean_zpow (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i in s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 < z i) (m : ‚Ñ§) :
    (‚àë i in s, w i * z i) ^ m ‚â§ ‚àë i in s, w i * z i ^ m :=
  (convexOn_zpow m).map_sum_le hw hw' hz
#align real.zpow_arith_mean_le_arith_mean_zpow Real.zpow_arith_mean_le_arith_mean_zpow

theorem rpow_arith_mean_le_arith_mean_rpow (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i)
    (hw' : ‚àë i in s, w i = 1) (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p :=
  (convexOn_rpow hp).map_sum_le hw hw' hz
#align real.rpow_arith_mean_le_arith_mean_rpow Real.rpow_arith_mean_le_arith_mean_rpow

theorem arith_mean_le_rpow_mean (w z : Œπ ‚Üí ‚Ñù) (hw : ‚àÄ i ‚àà s, 0 ‚â§ w i) (hw' : ‚àë i in s, w i = 1)
    (hz : ‚àÄ i ‚àà s, 0 ‚â§ z i) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    ‚àë i in s, w i * z i ‚â§ (‚àë i in s, w i * z i ^ p) ^ (1 / p) := by
  have : 0 < p := by positivity
  -- ‚ä¢ ‚àë i in s, w i * z i ‚â§ (‚àë i in s, w i * z i ^ p) ^ (1 / p)
  rw [‚Üê rpow_le_rpow_iff _ _ this, ‚Üê rpow_mul, one_div_mul_cancel (ne_of_gt this), rpow_one]
  exact rpow_arith_mean_le_arith_mean_rpow s w z hw hw' hz hp
  all_goals
    apply_rules [sum_nonneg, rpow_nonneg_of_nonneg]
    intro i hi
    apply_rules [mul_nonneg, rpow_nonneg_of_nonneg, hw i hi, hz i hi]
#align real.arith_mean_le_rpow_mean Real.arith_mean_le_rpow_mean

end Real

namespace NNReal

/-- Weighted generalized mean inequality, version sums over finite sets, with `‚Ñù‚â•0`-valued
functions and natural exponent. -/
theorem pow_arith_mean_le_arith_mean_pow (w z : Œπ ‚Üí ‚Ñù‚â•0) (hw' : ‚àë i in s, w i = 1) (n : ‚Ñï) :
    (‚àë i in s, w i * z i) ^ n ‚â§ ‚àë i in s, w i * z i ^ n := by
  exact_mod_cast
    Real.pow_arith_mean_le_arith_mean_pow s _ _ (fun i _ => (w i).coe_nonneg)
      (by exact_mod_cast hw') (fun i _ => (z i).coe_nonneg) n
#align nnreal.pow_arith_mean_le_arith_mean_pow NNReal.pow_arith_mean_le_arith_mean_pow

theorem pow_sum_div_card_le_sum_pow (f : Œπ ‚Üí ‚Ñù‚â•0) (n : ‚Ñï) :
    (‚àë x in s, f x) ^ (n + 1) / (s.card : ‚Ñù) ^ n ‚â§ ‚àë x in s, f x ^ (n + 1) := by
  simpa only [‚Üê NNReal.coe_le_coe, NNReal.coe_sum, Nonneg.coe_div, NNReal.coe_pow] using
    @Real.pow_sum_div_card_le_sum_pow Œπ s (((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò f) n fun _ _ => NNReal.coe_nonneg _
#align nnreal.pow_sum_div_card_le_sum_pow NNReal.pow_sum_div_card_le_sum_pow

/-- Weighted generalized mean inequality, version for sums over finite sets, with `‚Ñù‚â•0`-valued
functions and real exponents. -/
theorem rpow_arith_mean_le_arith_mean_rpow (w z : Œπ ‚Üí ‚Ñù‚â•0) (hw' : ‚àë i in s, w i = 1) {p : ‚Ñù}
    (hp : 1 ‚â§ p) : (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p := by
  exact_mod_cast
    Real.rpow_arith_mean_le_arith_mean_rpow s _ _ (fun i _ => (w i).coe_nonneg)
      (by exact_mod_cast hw') (fun i _ => (z i).coe_nonneg) hp
#align nnreal.rpow_arith_mean_le_arith_mean_rpow NNReal.rpow_arith_mean_le_arith_mean_rpow

/-- Weighted generalized mean inequality, version for two elements of `‚Ñù‚â•0` and real exponents. -/
theorem rpow_arith_mean_le_arith_mean2_rpow (w‚ÇÅ w‚ÇÇ z‚ÇÅ z‚ÇÇ : ‚Ñù‚â•0) (hw' : w‚ÇÅ + w‚ÇÇ = 1) {p : ‚Ñù}
    (hp : 1 ‚â§ p) : (w‚ÇÅ * z‚ÇÅ + w‚ÇÇ * z‚ÇÇ) ^ p ‚â§ w‚ÇÅ * z‚ÇÅ ^ p + w‚ÇÇ * z‚ÇÇ ^ p := by
  have h := rpow_arith_mean_le_arith_mean_rpow univ ![w‚ÇÅ, w‚ÇÇ] ![z‚ÇÅ, z‚ÇÇ] ?_ hp
  -- ‚ä¢ (w‚ÇÅ * z‚ÇÅ + w‚ÇÇ * z‚ÇÇ) ^ p ‚â§ w‚ÇÅ * z‚ÇÅ ^ p + w‚ÇÇ * z‚ÇÇ ^ p
  ¬∑ simpa [Fin.sum_univ_succ] using h
    -- üéâ no goals
  ¬∑ simp [hw', Fin.sum_univ_succ]
    -- üéâ no goals
#align nnreal.rpow_arith_mean_le_arith_mean2_rpow NNReal.rpow_arith_mean_le_arith_mean2_rpow

/-- Unweighted mean inequality, version for two elements of `‚Ñù‚â•0` and real exponents. -/
theorem rpow_add_le_mul_rpow_add_rpow (z‚ÇÅ z‚ÇÇ : ‚Ñù‚â•0) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    (z‚ÇÅ + z‚ÇÇ) ^ p ‚â§ (2 : ‚Ñù‚â•0) ^ (p - 1) * (z‚ÇÅ ^ p + z‚ÇÇ ^ p) := by
  rcases eq_or_lt_of_le hp with (rfl | h'p)
  -- ‚ä¢ (z‚ÇÅ + z‚ÇÇ) ^ 1 ‚â§ 2 ^ (1 - 1) * (z‚ÇÅ ^ 1 + z‚ÇÇ ^ 1)
  ¬∑ simp only [rpow_one, sub_self, rpow_zero, one_mul]; rfl
    -- ‚ä¢ z‚ÇÅ + z‚ÇÇ ‚â§ z‚ÇÅ + z‚ÇÇ
                                                        -- üéâ no goals
  convert rpow_arith_mean_le_arith_mean2_rpow (1 / 2) (1 / 2) (2 * z‚ÇÅ) (2 * z‚ÇÇ) (add_halves 1) hp
    using 1
  ¬∑ simp only [one_div, inv_mul_cancel_left‚ÇÄ, Ne.def, mul_eq_zero, two_ne_zero, one_ne_zero,
      not_false_iff]
  ¬∑ have A : p - 1 ‚â† 0 := ne_of_gt (sub_pos.2 h'p)
    -- ‚ä¢ 2 ^ (p - 1) * (z‚ÇÅ ^ p + z‚ÇÇ ^ p) = 1 / 2 * (2 * z‚ÇÅ) ^ p + 1 / 2 * (2 * z‚ÇÇ) ^ p
    simp only [mul_rpow, rpow_sub' _ A, div_eq_inv_mul, rpow_one, mul_one]
    -- ‚ä¢ 2‚Åª¬π * 2 ^ p * (z‚ÇÅ ^ p + z‚ÇÇ ^ p) = 2‚Åª¬π * (2 ^ p * z‚ÇÅ ^ p) + 2‚Åª¬π * (2 ^ p * z‚ÇÇ ‚Ä¶
    ring
    -- üéâ no goals
#align nnreal.rpow_add_le_mul_rpow_add_rpow NNReal.rpow_add_le_mul_rpow_add_rpow

/-- Weighted generalized mean inequality, version for sums over finite sets, with `‚Ñù‚â•0`-valued
functions and real exponents. -/
theorem arith_mean_le_rpow_mean (w z : Œπ ‚Üí ‚Ñù‚â•0) (hw' : ‚àë i in s, w i = 1) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    ‚àë i in s, w i * z i ‚â§ (‚àë i in s, w i * z i ^ p) ^ (1 / p) := by
  exact_mod_cast
    Real.arith_mean_le_rpow_mean s _ _ (fun i _ => (w i).coe_nonneg) (by exact_mod_cast hw')
      (fun i _ => (z i).coe_nonneg) hp
#align nnreal.arith_mean_le_rpow_mean NNReal.arith_mean_le_rpow_mean

private theorem add_rpow_le_one_of_add_le_one {p : ‚Ñù} (a b : ‚Ñù‚â•0) (hab : a + b ‚â§ 1) (hp1 : 1 ‚â§ p) :
    a ^ p + b ^ p ‚â§ 1 := by
  have h_le_one : ‚àÄ x : ‚Ñù‚â•0, x ‚â§ 1 ‚Üí x ^ p ‚â§ x := fun x hx => rpow_le_self_of_le_one hx hp1
  -- ‚ä¢ a ^ p + b ^ p ‚â§ 1
  have ha : a ‚â§ 1 := (self_le_add_right a b).trans hab
  -- ‚ä¢ a ^ p + b ^ p ‚â§ 1
  have hb : b ‚â§ 1 := (self_le_add_left b a).trans hab
  -- ‚ä¢ a ^ p + b ^ p ‚â§ 1
  exact (add_le_add (h_le_one a ha) (h_le_one b hb)).trans hab
  -- üéâ no goals

theorem add_rpow_le_rpow_add {p : ‚Ñù} (a b : ‚Ñù‚â•0) (hp1 : 1 ‚â§ p) : a ^ p + b ^ p ‚â§ (a + b) ^ p := by
  have hp_pos : 0 < p := by positivity
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  by_cases h_zero : a + b = 0
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  ¬∑ simp [add_eq_zero_iff.mp h_zero, hp_pos.ne']
    -- üéâ no goals
  have h_nonzero : ¬¨(a = 0 ‚àß b = 0) := by rwa [add_eq_zero_iff] at h_zero
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  have h_add : a / (a + b) + b / (a + b) = 1 := by rw [div_add_div_same, div_self h_zero]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  have h := add_rpow_le_one_of_add_le_one (a / (a + b)) (b / (a + b)) h_add.le hp1
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  rw [div_rpow a (a + b), div_rpow b (a + b)] at h
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  have hab_0 : (a + b) ^ p ‚â† 0 := by simp [hp_pos, h_nonzero]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  have hab_0' : 0 < (a + b) ^ p := zero_lt_iff.mpr hab_0
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  have h_mul : (a + b) ^ p * (a ^ p / (a + b) ^ p + b ^ p / (a + b) ^ p) ‚â§ (a + b) ^ p := by
    nth_rw 4 [‚Üê mul_one ((a + b) ^ p)]
    exact (mul_le_mul_left hab_0').mpr h
  rwa [div_eq_mul_inv, div_eq_mul_inv, mul_add, mul_comm (a ^ p), mul_comm (b ^ p), ‚Üê mul_assoc, ‚Üê
    mul_assoc, mul_inv_cancel hab_0, one_mul, one_mul] at h_mul
#align nnreal.add_rpow_le_rpow_add NNReal.add_rpow_le_rpow_add

theorem rpow_add_rpow_le_add {p : ‚Ñù} (a b : ‚Ñù‚â•0) (hp1 : 1 ‚â§ p) :
    (a ^ p + b ^ p) ^ (1 / p) ‚â§ a + b := by
  rw [‚Üê @NNReal.le_rpow_one_div_iff _ _ (1 / p) (by simp [lt_of_lt_of_le zero_lt_one hp1])]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ (1 / (1 / p))
  rw [one_div_one_div]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  exact add_rpow_le_rpow_add _ _ hp1
  -- üéâ no goals
#align nnreal.rpow_add_rpow_le_add NNReal.rpow_add_rpow_le_add

theorem rpow_add_rpow_le {p q : ‚Ñù} (a b : ‚Ñù‚â•0) (hp_pos : 0 < p) (hpq : p ‚â§ q) :
    (a ^ q + b ^ q) ^ (1 / q) ‚â§ (a ^ p + b ^ p) ^ (1 / p) := by
  have h_rpow : ‚àÄ a : ‚Ñù‚â•0, a ^ q = (a ^ p) ^ (q / p) := fun a => by
    rw [‚Üê NNReal.rpow_mul, div_eq_inv_mul, ‚Üê mul_assoc, _root_.mul_inv_cancel hp_pos.ne.symm,
      one_mul]
  have h_rpow_add_rpow_le_add :
    ((a ^ p) ^ (q / p) + (b ^ p) ^ (q / p)) ^ (1 / (q / p)) ‚â§ a ^ p + b ^ p := by
    refine' rpow_add_rpow_le_add (a ^ p) (b ^ p) _
    rwa [one_le_div hp_pos]
  rw [h_rpow a, h_rpow b, NNReal.le_rpow_one_div_iff hp_pos, ‚Üê NNReal.rpow_mul, mul_comm,
    mul_one_div]
  rwa [one_div_div] at h_rpow_add_rpow_le_add
  -- üéâ no goals
#align nnreal.rpow_add_rpow_le NNReal.rpow_add_rpow_le

theorem rpow_add_le_add_rpow {p : ‚Ñù} (a b : ‚Ñù‚â•0) (hp : 0 ‚â§ p) (hp1 : p ‚â§ 1) :
    (a + b) ^ p ‚â§ a ^ p + b ^ p := by
  rcases hp.eq_or_lt with (rfl | hp_pos)
  -- ‚ä¢ (a + b) ^ 0 ‚â§ a ^ 0 + b ^ 0
  ¬∑ simp
    -- üéâ no goals
  have h := rpow_add_rpow_le a b hp_pos hp1
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  rw [one_div_one] at h
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  repeat' rw [NNReal.rpow_one] at h
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  exact (NNReal.le_rpow_one_div_iff hp_pos).mp h
  -- üéâ no goals
#align nnreal.rpow_add_le_add_rpow NNReal.rpow_add_le_add_rpow

end NNReal

namespace ENNReal

/-- Weighted generalized mean inequality, version for sums over finite sets, with `‚Ñù‚â•0‚àû`-valued
functions and real exponents. -/
theorem rpow_arith_mean_le_arith_mean_rpow (w z : Œπ ‚Üí ‚Ñù‚â•0‚àû) (hw' : ‚àë i in s, w i = 1) {p : ‚Ñù}
    (hp : 1 ‚â§ p) : (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p := by
  have hp_pos : 0 < p
  -- ‚ä¢ 0 < p
  positivity
  -- ‚ä¢ (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p
  have hp_nonneg : 0 ‚â§ p
  -- ‚ä¢ 0 ‚â§ p
  positivity
  -- ‚ä¢ (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p
  have hp_not_neg : ¬¨p < 0 := by simp [hp_nonneg]
  -- ‚ä¢ (‚àë i in s, w i * z i) ^ p ‚â§ ‚àë i in s, w i * z i ^ p
  have h_top_iff_rpow_top : ‚àÄ (i : Œπ), i ‚àà s ‚Üí (w i * z i = ‚ä§ ‚Üî w i * z i ^ p = ‚ä§) := by
    simp [ENNReal.mul_eq_top, hp_pos, hp_nonneg, hp_not_neg]
  refine' le_of_top_imp_top_of_toNNReal_le _ _
  -- ‚ä¢ (‚àë i in s, w i * z i) ^ p = ‚ä§ ‚Üí ‚àë i in s, w i * z i ^ p = ‚ä§
  ¬∑ -- first, prove `(‚àë i in s, w i * z i) ^ p = ‚ä§ ‚Üí ‚àë i in s, (w i * z i ^ p) = ‚ä§`
    rw [rpow_eq_top_iff, sum_eq_top_iff, sum_eq_top_iff]
    -- ‚ä¢ ‚àë i in s, w i * z i = 0 ‚àß p < 0 ‚à® (‚àÉ a, a ‚àà s ‚àß w a * z a = ‚ä§) ‚àß 0 < p ‚Üí ‚àÉ a ‚Ä¶
    intro h
    -- ‚ä¢ ‚àÉ a, a ‚àà s ‚àß w a * z a ^ p = ‚ä§
    simp only [and_false_iff, hp_not_neg, false_or_iff] at h
    -- ‚ä¢ ‚àÉ a, a ‚àà s ‚àß w a * z a ^ p = ‚ä§
    rcases h.left with ‚ü®a, H, ha‚ü©
    -- ‚ä¢ ‚àÉ a, a ‚àà s ‚àß w a * z a ^ p = ‚ä§
    use a, H
    -- ‚ä¢ w a * z a ^ p = ‚ä§
    rwa [‚Üê h_top_iff_rpow_top a H]
    -- üéâ no goals
  ¬∑ -- second, suppose both `(‚àë i in s, w i * z i) ^ p ‚â† ‚ä§` and `‚àë i in s, (w i * z i ^ p) ‚â† ‚ä§`,
    -- and prove `((‚àë i in s, w i * z i) ^ p).toNNReal ‚â§ (‚àë i in s, (w i * z i ^ p)).toNNReal`,
    -- by using `NNReal.rpow_arith_mean_le_arith_mean_rpow`.
    intro h_top_rpow_sum _
    -- ‚ä¢ ENNReal.toNNReal ((‚àë i in s, w i * z i) ^ p) ‚â§ ENNReal.toNNReal (‚àë i in s, w ‚Ä¶
    -- show hypotheses needed to put the `.toNNReal` inside the sums.
    have h_top : ‚àÄ a : Œπ, a ‚àà s ‚Üí w a * z a ‚â† ‚ä§ :=
      haveI h_top_sum : ‚àë i : Œπ in s, w i * z i ‚â† ‚ä§ := by
        intro h
        rw [h, top_rpow_of_pos hp_pos] at h_top_rpow_sum
        exact h_top_rpow_sum rfl
      fun a ha => (lt_top_of_sum_ne_top h_top_sum ha).ne
    have h_top_rpow : ‚àÄ a : Œπ, a ‚àà s ‚Üí w a * z a ^ p ‚â† ‚ä§ := by
      intro i hi
      specialize h_top i hi
      rwa [Ne.def, ‚Üê h_top_iff_rpow_top i hi]
    -- put the `.toNNReal` inside the sums.
    simp_rw [toNNReal_sum h_top_rpow, ‚Üê toNNReal_rpow, toNNReal_sum h_top, toNNReal_mul, ‚Üê
      toNNReal_rpow]
    -- use corresponding nnreal result
    refine'
      NNReal.rpow_arith_mean_le_arith_mean_rpow s (fun i => (w i).toNNReal)
        (fun i => (z i).toNNReal) _ hp
    -- verify the hypothesis `‚àë i in s, (w i).toNNReal = 1`, using `‚àë i in s, w i = 1` .
    have h_sum_nnreal : ‚àë i in s, w i = ‚Üë(‚àë i in s, (w i).toNNReal) := by
      rw [coe_finset_sum]
      refine' sum_congr rfl fun i hi => (coe_toNNReal _).symm
      refine' (lt_top_of_sum_ne_top _ hi).ne
      exact hw'.symm ‚ñ∏ ENNReal.one_ne_top
    rwa [‚Üê coe_eq_coe, ‚Üê h_sum_nnreal]
    -- üéâ no goals
#align ennreal.rpow_arith_mean_le_arith_mean_rpow ENNReal.rpow_arith_mean_le_arith_mean_rpow

/-- Weighted generalized mean inequality, version for two elements of `‚Ñù‚â•0‚àû` and real
exponents. -/
theorem rpow_arith_mean_le_arith_mean2_rpow (w‚ÇÅ w‚ÇÇ z‚ÇÅ z‚ÇÇ : ‚Ñù‚â•0‚àû) (hw' : w‚ÇÅ + w‚ÇÇ = 1) {p : ‚Ñù}
    (hp : 1 ‚â§ p) : (w‚ÇÅ * z‚ÇÅ + w‚ÇÇ * z‚ÇÇ) ^ p ‚â§ w‚ÇÅ * z‚ÇÅ ^ p + w‚ÇÇ * z‚ÇÇ ^ p := by
  have h := rpow_arith_mean_le_arith_mean_rpow univ ![w‚ÇÅ, w‚ÇÇ] ![z‚ÇÅ, z‚ÇÇ] ?_ hp
  -- ‚ä¢ (w‚ÇÅ * z‚ÇÅ + w‚ÇÇ * z‚ÇÇ) ^ p ‚â§ w‚ÇÅ * z‚ÇÅ ^ p + w‚ÇÇ * z‚ÇÇ ^ p
  ¬∑ simpa [Fin.sum_univ_succ] using h
    -- üéâ no goals
  ¬∑ simp [hw', Fin.sum_univ_succ]
    -- üéâ no goals
#align ennreal.rpow_arith_mean_le_arith_mean2_rpow ENNReal.rpow_arith_mean_le_arith_mean2_rpow

/-- Unweighted mean inequality, version for two elements of `‚Ñù‚â•0‚àû` and real exponents. -/
theorem rpow_add_le_mul_rpow_add_rpow (z‚ÇÅ z‚ÇÇ : ‚Ñù‚â•0‚àû) {p : ‚Ñù} (hp : 1 ‚â§ p) :
    (z‚ÇÅ + z‚ÇÇ) ^ p ‚â§ (2 : ‚Ñù‚â•0‚àû) ^ (p - 1) * (z‚ÇÅ ^ p + z‚ÇÇ ^ p) := by
  rcases eq_or_lt_of_le hp with (rfl | h'p)
  -- ‚ä¢ (z‚ÇÅ + z‚ÇÇ) ^ 1 ‚â§ 2 ^ (1 - 1) * (z‚ÇÅ ^ 1 + z‚ÇÇ ^ 1)
  ¬∑ simp only [rpow_one, sub_self, rpow_zero, one_mul, le_refl]
    -- üéâ no goals
  convert rpow_arith_mean_le_arith_mean2_rpow (1 / 2) (1 / 2) (2 * z‚ÇÅ) (2 * z‚ÇÇ)
      (ENNReal.add_halves 1) hp using 1
  ¬∑ simp [‚Üê mul_assoc, ENNReal.inv_mul_cancel two_ne_zero two_ne_top]
    -- üéâ no goals
  ¬∑ have _ : p - 1 ‚â† 0 := ne_of_gt (sub_pos.2 h'p)
    -- ‚ä¢ 2 ^ (p - 1) * (z‚ÇÅ ^ p + z‚ÇÇ ^ p) = 1 / 2 * (2 * z‚ÇÅ) ^ p + 1 / 2 * (2 * z‚ÇÇ) ^ p
    simp only [mul_rpow_of_nonneg _ _ (zero_le_one.trans hp), rpow_sub _ _ two_ne_zero two_ne_top,
      ENNReal.div_eq_inv_mul, rpow_one, mul_one]
    ring
    -- üéâ no goals
#align ennreal.rpow_add_le_mul_rpow_add_rpow ENNReal.rpow_add_le_mul_rpow_add_rpow

theorem add_rpow_le_rpow_add {p : ‚Ñù} (a b : ‚Ñù‚â•0‚àû) (hp1 : 1 ‚â§ p) : a ^ p + b ^ p ‚â§ (a + b) ^ p := by
  have hp_pos : 0 < p := by positivity
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  by_cases h_top : a + b = ‚ä§
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  ¬∑ rw [‚Üê @ENNReal.rpow_eq_top_iff_of_pos (a + b) p hp_pos] at h_top
    -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
    rw [h_top]
    -- ‚ä¢ a ^ p + b ^ p ‚â§ ‚ä§
    exact le_top
    -- üéâ no goals
  obtain ‚ü®ha_top, hb_top‚ü© := add_ne_top.mp h_top
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  lift a to ‚Ñù‚â•0 using ha_top
  -- ‚ä¢ ‚Üëa ^ p + b ^ p ‚â§ (‚Üëa + b) ^ p
  lift b to ‚Ñù‚â•0 using hb_top
  -- ‚ä¢ ‚Üëa ^ p + ‚Üëb ^ p ‚â§ (‚Üëa + ‚Üëb) ^ p
  simpa [‚Üê ENNReal.coe_rpow_of_nonneg _ hp_pos.le] using
    ENNReal.coe_le_coe.2 (NNReal.add_rpow_le_rpow_add a b hp1)
#align ennreal.add_rpow_le_rpow_add ENNReal.add_rpow_le_rpow_add

theorem rpow_add_rpow_le_add {p : ‚Ñù} (a b : ‚Ñù‚â•0‚àû) (hp1 : 1 ‚â§ p) :
    (a ^ p + b ^ p) ^ (1 / p) ‚â§ a + b := by
  rw [‚Üê @ENNReal.le_rpow_one_div_iff _ _ (1 / p) (by simp [lt_of_lt_of_le zero_lt_one hp1])]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ (1 / (1 / p))
  rw [one_div_one_div]
  -- ‚ä¢ a ^ p + b ^ p ‚â§ (a + b) ^ p
  exact add_rpow_le_rpow_add _ _ hp1
  -- üéâ no goals
#align ennreal.rpow_add_rpow_le_add ENNReal.rpow_add_rpow_le_add

theorem rpow_add_rpow_le {p q : ‚Ñù} (a b : ‚Ñù‚â•0‚àû) (hp_pos : 0 < p) (hpq : p ‚â§ q) :
    (a ^ q + b ^ q) ^ (1 / q) ‚â§ (a ^ p + b ^ p) ^ (1 / p) := by
  have h_rpow : ‚àÄ a : ‚Ñù‚â•0‚àû, a ^ q = (a ^ p) ^ (q / p) := fun a => by
    rw [‚Üê ENNReal.rpow_mul, _root_.mul_div_cancel' _ hp_pos.ne']
  have h_rpow_add_rpow_le_add :
    ((a ^ p) ^ (q / p) + (b ^ p) ^ (q / p)) ^ (1 / (q / p)) ‚â§ a ^ p + b ^ p := by
    refine' rpow_add_rpow_le_add (a ^ p) (b ^ p) _
    rwa [one_le_div hp_pos]
  rw [h_rpow a, h_rpow b, ENNReal.le_rpow_one_div_iff hp_pos, ‚Üê ENNReal.rpow_mul, mul_comm,
    mul_one_div]
  rwa [one_div_div] at h_rpow_add_rpow_le_add
  -- üéâ no goals
#align ennreal.rpow_add_rpow_le ENNReal.rpow_add_rpow_le

theorem rpow_add_le_add_rpow {p : ‚Ñù} (a b : ‚Ñù‚â•0‚àû) (hp : 0 ‚â§ p) (hp1 : p ‚â§ 1) :
    (a + b) ^ p ‚â§ a ^ p + b ^ p := by
  rcases hp.eq_or_lt with (rfl | hp_pos)
  -- ‚ä¢ (a + b) ^ 0 ‚â§ a ^ 0 + b ^ 0
  ¬∑ simp
    -- üéâ no goals
  have h := rpow_add_rpow_le a b hp_pos hp1
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  rw [one_div_one] at h
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  repeat' rw [ENNReal.rpow_one] at h
  -- ‚ä¢ (a + b) ^ p ‚â§ a ^ p + b ^ p
  exact (ENNReal.le_rpow_one_div_iff hp_pos).mp h
  -- üéâ no goals
#align ennreal.rpow_add_le_add_rpow ENNReal.rpow_add_le_add_rpow

end ENNReal
