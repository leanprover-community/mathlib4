/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity

/-!
# A collection of specific asymptotic results

This file contains specific lemmas about asymptotics which don't have their place in the general
theory developed in `Mathlib/Analysis/Asymptotics/Asymptotics.lean`.
-/


open Filter Asymptotics

open Topology

section NormedField

/-- If `f : ğ•œ â†’ E` is bounded in a punctured neighborhood of `a`, then `f(x) = o((x - a)â»Â¹)` as
`x â†’ a`, `x â‰  a`. -/
theorem Filter.IsBoundedUnder.isLittleO_sub_self_inv {ğ•œ E : Type*} [NormedField ğ•œ] [Norm E] {a : ğ•œ}
    {f : ğ•œ â†’ E} (h : IsBoundedUnder (Â· â‰¤ Â·) (ğ“[â‰ ] a) (norm âˆ˜ f)) :
    f =o[ğ“[â‰ ] a] fun x => (x - a)â»Â¹ := by
  refine (h.isBigO_const (one_ne_zero' â„)).trans_isLittleO (isLittleO_const_left.2 <| Or.inr ?_)
  simp only [Function.comp_def, norm_inv]
  exact (tendsto_norm_sub_self_nhdsNE a).inv_tendsto_nhdsGT_zero

end NormedField

section LinearOrderedField

variable {ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]

theorem pow_div_pow_eventuallyEq_atTop {p q : â„•} :
    (fun x : ğ•œ => x ^ p / x ^ q) =á¶ [atTop] fun x => x ^ ((p : â„¤) - q) := by
  apply (eventually_gt_atTop (0 : ğ•œ)).mono fun x hx => _
  intro x hx
  simp [zpow_subâ‚€ hx.ne']

theorem pow_div_pow_eventuallyEq_atBot {p q : â„•} :
    (fun x : ğ•œ => x ^ p / x ^ q) =á¶ [atBot] fun x => x ^ ((p : â„¤) - q) := by
  apply (eventually_lt_atBot (0 : ğ•œ)).mono fun x hx => _
  intro x hx
  simp [zpow_subâ‚€ hx.ne]

theorem tendsto_pow_div_pow_atTop_atTop {p q : â„•} (hpq : q < p) :
    Tendsto (fun x : ğ•œ => x ^ p / x ^ q) atTop atTop := by
  rw [tendsto_congr' pow_div_pow_eventuallyEq_atTop]
  apply tendsto_zpow_atTop_atTop
  omega

theorem tendsto_pow_div_pow_atTop_zero [TopologicalSpace ğ•œ] [OrderTopology ğ•œ] {p q : â„•}
    (hpq : p < q) : Tendsto (fun x : ğ•œ => x ^ p / x ^ q) atTop (ğ“ 0) := by
  rw [tendsto_congr' pow_div_pow_eventuallyEq_atTop]
  apply tendsto_zpow_atTop_zero
  omega

end LinearOrderedField

section NormedLinearOrderedField

variable {ğ•œ : Type*} [NormedField ğ•œ]

theorem Asymptotics.isLittleO_pow_pow_atTop_of_lt
    [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [OrderTopology ğ•œ] {p q : â„•} (hpq : p < q) :
    (fun x : ğ•œ => x ^ p) =o[atTop] fun x => x ^ q := by
  refine (isLittleO_iff_tendsto' ?_).mpr (tendsto_pow_div_pow_atTop_zero hpq)
  exact (eventually_gt_atTop 0).mono fun x hx hxq => (pow_ne_zero q hx.ne' hxq).elim

theorem Asymptotics.IsBigO.trans_tendsto_norm_atTop {Î± : Type*} {u v : Î± â†’ ğ•œ} {l : Filter Î±}
    (huv : u =O[l] v) (hu : Tendsto (fun x => â€–u xâ€–) l atTop) :
    Tendsto (fun x => â€–v xâ€–) l atTop := by
  rcases huv.exists_pos with âŸ¨c, hc, hcuvâŸ©
  rw [IsBigOWith] at hcuv
  convert Tendsto.atTop_div_const hc (tendsto_atTop_mono' l hcuv hu)
  rw [mul_div_cancel_leftâ‚€ _ hc.ne.symm]

end NormedLinearOrderedField

section Real

theorem Asymptotics.IsEquivalent.rpow {Î± : Type*} {u v : Î± â†’ â„} {l : Filter Î±}
    (hv : 0 â‰¤ v) (h : u ~[l] v) {r : â„} :
    u ^ r ~[l] v ^ r := by
  obtain âŸ¨Ï†, hÏ†, huÏ†vâŸ© := IsEquivalent.exists_eq_mul h
  rw [isEquivalent_iff_exists_eq_mul]
  have hÏ†r : Tendsto ((fun x â†¦ x ^ r) âˆ˜ Ï†) l (ğ“ 1) := by
    rw [â† Real.one_rpow r]
    exact Tendsto.comp (Real.continuousAt_rpow_const _ _ (by left; norm_num)) hÏ†
  use (Â· ^ r) âˆ˜ Ï†, hÏ†r
  conv => enter [3]; change fun x â†¦ Ï† x ^ r * v x ^ r
  filter_upwards [Tendsto.eventually_const_lt (zero_lt_one) hÏ†, huÏ†v] with x hÏ†_pos huv'
  simp [â† Real.mul_rpow (le_of_lt hÏ†_pos) (hv x), huv']

open Finset

theorem Asymptotics.IsLittleO.sum_range {Î± : Type*} [NormedAddCommGroup Î±] {f : â„• â†’ Î±} {g : â„• â†’ â„}
    (h : f =o[atTop] g) (hg : 0 â‰¤ g) (h'g : Tendsto (fun n => âˆ‘ i âˆˆ range n, g i) atTop atTop) :
    (fun n => âˆ‘ i âˆˆ range n, f i) =o[atTop] fun n => âˆ‘ i âˆˆ range n, g i := by
  have A : âˆ€ i, â€–g iâ€– = g i := fun i => Real.norm_of_nonneg (hg i)
  have B : âˆ€ n, â€–âˆ‘ i âˆˆ range n, g iâ€– = âˆ‘ i âˆˆ range n, g i := fun n => by
    rwa [Real.norm_eq_abs, abs_sum_of_nonneg']
  apply isLittleO_iff.2 fun Îµ Îµpos => _
  intro Îµ Îµpos
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, âˆ€ b : â„•, N â‰¤ b â†’ â€–f bâ€– â‰¤ Îµ / 2 * g b := by
    simpa only [A, eventually_atTop] using isLittleO_iff.mp h (half_pos Îµpos)
  have : (fun _ : â„• => âˆ‘ i âˆˆ range N, f i) =o[atTop] fun n : â„• => âˆ‘ i âˆˆ range n, g i := by
    apply isLittleO_const_left.2
    exact Or.inr (h'g.congr fun n => (B n).symm)
  filter_upwards [isLittleO_iff.1 this (half_pos Îµpos), Ici_mem_atTop N] with n hn Nn
  calc
    â€–âˆ‘ i âˆˆ range n, f iâ€– = â€–(âˆ‘ i âˆˆ range N, f i) + âˆ‘ i âˆˆ Ico N n, f iâ€– := by
      rw [sum_range_add_sum_Ico _ Nn]
    _ â‰¤ â€–âˆ‘ i âˆˆ range N, f iâ€– + â€–âˆ‘ i âˆˆ Ico N n, f iâ€– := norm_add_le _ _
    _ â‰¤ â€–âˆ‘ i âˆˆ range N, f iâ€– + âˆ‘ i âˆˆ Ico N n, Îµ / 2 * g i :=
      (add_le_add le_rfl (norm_sum_le_of_le _ fun i hi => hN _ (mem_Ico.1 hi).1))
    _ â‰¤ â€–âˆ‘ i âˆˆ range N, f iâ€– + âˆ‘ i âˆˆ range n, Îµ / 2 * g i := by
      gcongr
      Â· exact fun i _ _ â†¦ mul_nonneg (half_pos Îµpos).le (hg i)
      Â· rw [range_eq_Ico]
        exact Ico_subset_Ico (zero_le _) le_rfl
    _ â‰¤ Îµ / 2 * â€–âˆ‘ i âˆˆ range n, g iâ€– + Îµ / 2 * âˆ‘ i âˆˆ range n, g i := by rw [â† mul_sum]; gcongr
    _ = Îµ * â€–âˆ‘ i âˆˆ range n, g iâ€– := by
      simp only [B]
      ring

theorem Asymptotics.isLittleO_sum_range_of_tendsto_zero {Î± : Type*} [NormedAddCommGroup Î±]
    {f : â„• â†’ Î±} (h : Tendsto f atTop (ğ“ 0)) :
    (fun n => âˆ‘ i âˆˆ range n, f i) =o[atTop] fun n => (n : â„) := by
  have := ((isLittleO_one_iff â„).2 h).sum_range fun i => zero_le_one
  simp only [sum_const, card_range, Nat.smul_one_eq_cast] at this
  exact this tendsto_natCast_atTop_atTop

/-- The Cesaro average of a converging sequence converges to the same limit. -/
theorem Filter.Tendsto.cesaro_smul {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {u : â„• â†’ E}
    {l : E} (h : Tendsto u atTop (ğ“ l)) :
    Tendsto (fun n : â„• => (nâ»Â¹ : â„) â€¢ âˆ‘ i âˆˆ range n, u i) atTop (ğ“ l) := by
  rw [â† tendsto_sub_nhds_zero_iff, â† isLittleO_one_iff â„]
  have := Asymptotics.isLittleO_sum_range_of_tendsto_zero (tendsto_sub_nhds_zero_iff.2 h)
  apply ((isBigO_refl (fun n : â„• => (n : â„)â»Â¹) atTop).smul_isLittleO this).congr' _ _
  Â· filter_upwards [Ici_mem_atTop 1] with n npos
    have nposâ„ : (0 : â„) < n := Nat.cast_pos.2 npos
    simp only [smul_sub, sum_sub_distrib, sum_const, card_range, sub_right_inj]
    rw [â† Nat.cast_smul_eq_nsmul â„, smul_smul, inv_mul_cancelâ‚€ nposâ„.ne', one_smul]
  Â· filter_upwards [Ici_mem_atTop 1] with n npos
    have nposâ„ : (0 : â„) < n := Nat.cast_pos.2 npos
    rw [Algebra.id.smul_eq_mul, inv_mul_cancelâ‚€ nposâ„.ne']

/-- The Cesaro average of a converging sequence converges to the same limit. -/
theorem Filter.Tendsto.cesaro {u : â„• â†’ â„} {l : â„} (h : Tendsto u atTop (ğ“ l)) :
    Tendsto (fun n : â„• => (nâ»Â¹ : â„) * âˆ‘ i âˆˆ range n, u i) atTop (ğ“ l) :=
  h.cesaro_smul

end Real

section NormedLinearOrderedField

variable {R : Type*} [NormedField R] [LinearOrder R] [IsStrictOrderedRing R]
  [OrderTopology R] [FloorRing R]

theorem Asymptotics.isEquivalent_nat_floor :
    (fun (x : R) â†¦ â†‘âŒŠxâŒ‹â‚Š) ~[atTop] (fun x â†¦ x) := by
  refine isEquivalent_of_tendsto_one ?_ tendsto_nat_floor_div_atTop
  filter_upwards with x hx using by rw [hx, Nat.floor_zero, Nat.cast_eq_zero]

theorem Asymptotics.isEquivalent_nat_ceil :
    (fun (x : R) â†¦ â†‘âŒˆxâŒ‰â‚Š) ~[atTop] (fun x â†¦ x) := by
  refine isEquivalent_of_tendsto_one ?_ tendsto_nat_ceil_div_atTop
  filter_upwards with x hx using by rw [hx, Nat.ceil_zero, Nat.cast_eq_zero]

end NormedLinearOrderedField
