/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Asymptotics.Theta

/-!
# Asymptotic equivalence

In this file, we define the relation `IsEquivalent l u v`, which means that `u-v` is little o of
`v` along the filter `l`.

Unlike `Is(Little|Big)O` relations, this one requires `u` and `v` to have the same codomain `Œ≤`.
While the definition only requires `Œ≤` to be a `NormedAddCommGroup`, most interesting properties
require it to be a `NormedField`.

## Notations

We introduce the notation `u ~[l] v := IsEquivalent l u v`, which you can use by opening the
`Asymptotics` locale.

## Main results

If `Œ≤` is a `NormedAddCommGroup` :

- `_ ~[l] _` is an equivalence relation
- Equivalent statements for `u ~[l] const _ c` :
  - If `c ‚â† 0`, this is true iff `Tendsto u l (ùìù c)` (see `isEquivalent_const_iff_tendsto`)
  - For `c = 0`, this is true iff `u =·∂†[l] 0` (see `isEquivalent_zero_iff_eventually_zero`)

If `Œ≤` is a `NormedField` :

- Alternative characterization of the relation (see `isEquivalent_iff_exists_eq_mul`) :

  `u ~[l] v ‚Üî ‚àÉ (œÜ : Œ± ‚Üí Œ≤) (hœÜ : Tendsto œÜ l (ùìù 1)), u =·∂†[l] œÜ * v`

- Provided some non-vanishing hypothesis, this can be seen as `u ~[l] v ‚Üî Tendsto (u/v) l (ùìù 1)`
  (see `isEquivalent_iff_tendsto_one`)
- For any constant `c`, `u ~[l] v` implies `Tendsto u l (ùìù c) ‚Üî Tendsto v l (ùìù c)`
  (see `IsEquivalent.tendsto_nhds_iff`)
- `*` and `/` are compatible with `_ ~[l] _` (see `IsEquivalent.mul` and `IsEquivalent.div`)

If `Œ≤` is a `NormedLinearOrderedField` :

- If `u ~[l] v`, we have `Tendsto u l atTop ‚Üî Tendsto v l atTop`
  (see `IsEquivalent.tendsto_atTop_iff`)

## Implementation Notes

Note that `IsEquivalent` takes the parameters `(l : Filter Œ±) (u v : Œ± ‚Üí Œ≤)` in that order.
This is to enable `calc` support, as `calc` requires that the last two explicit arguments are `u v`.

-/


namespace Asymptotics

open Filter Function

open Topology

section NormedAddCommGroup

variable {Œ± Œ≤ : Type*} [NormedAddCommGroup Œ≤]

/-- Two functions `u` and `v` are said to be asymptotically equivalent along a filter `l`
  (denoted as `u ~[l] v` in the `Asymptotics` namespace)
  when `u x - v x = o(v x)` as `x` converges along `l`. -/
def IsEquivalent (l : Filter Œ±) (u v : Œ± ‚Üí Œ≤) :=
  (u - v) =o[l] v

@[inherit_doc] scoped notation:50 u " ~[" l:50 "] " v:50 => Asymptotics.IsEquivalent l u v

variable {u v w : Œ± ‚Üí Œ≤} {l : Filter Œ±}

theorem IsEquivalent.isLittleO (h : u ~[l] v) : (u - v) =o[l] v := h

nonrec theorem IsEquivalent.isBigO (h : u ~[l] v) : u =O[l] v :=
  (IsBigO.congr_of_sub h.isBigO.symm).mp (isBigO_refl _ _)

theorem IsEquivalent.isBigO_symm (h : u ~[l] v) : v =O[l] u := by
  convert h.isLittleO.right_isBigO_add
  simp

theorem IsEquivalent.isTheta (h : u ~[l] v) : u =Œò[l] v :=
  ‚ü®h.isBigO, h.isBigO_symm‚ü©

theorem IsEquivalent.isTheta_symm (h : u ~[l] v) : v =Œò[l] u :=
  ‚ü®h.isBigO_symm, h.isBigO‚ü©

@[refl]
theorem IsEquivalent.refl : u ~[l] u := by
  rw [IsEquivalent, sub_self]
  exact isLittleO_zero _ _

@[symm]
theorem IsEquivalent.symm (h : u ~[l] v) : v ~[l] u :=
  (h.isLittleO.trans_isBigO h.isBigO_symm).symm

@[trans]
theorem IsEquivalent.trans {l : Filter Œ±} {u v w : Œ± ‚Üí Œ≤} (huv : u ~[l] v) (hvw : v ~[l] w) :
    u ~[l] w :=
  (huv.isLittleO.trans_isBigO hvw.isBigO).triangle hvw.isLittleO

theorem IsEquivalent.congr_left {u v w : Œ± ‚Üí Œ≤} {l : Filter Œ±} (huv : u ~[l] v) (huw : u =·∂†[l] w) :
    w ~[l] v :=
  huv.congr' (huw.sub (EventuallyEq.refl _ _)) (EventuallyEq.refl _ _)

theorem IsEquivalent.congr_right {u v w : Œ± ‚Üí Œ≤} {l : Filter Œ±} (huv : u ~[l] v) (hvw : v =·∂†[l] w) :
    u ~[l] w :=
  (huv.symm.congr_left hvw).symm

theorem isEquivalent_zero_iff_eventually_zero : u ~[l] 0 ‚Üî u =·∂†[l] 0 := by
  rw [IsEquivalent, sub_zero]
  exact isLittleO_zero_right_iff

theorem isEquivalent_zero_iff_isBigO_zero : u ~[l] 0 ‚Üî u =O[l] (0 : Œ± ‚Üí Œ≤) := by
  refine ‚ü®IsEquivalent.isBigO, fun h ‚Ü¶ ?_‚ü©
  rw [isEquivalent_zero_iff_eventually_zero, eventuallyEq_iff_exists_mem]
  exact ‚ü®{ x : Œ± | u x = 0 }, isBigO_zero_right_iff.mp h, fun x hx ‚Ü¶ hx‚ü©

theorem isEquivalent_const_iff_tendsto {c : Œ≤} (h : c ‚â† 0) :
    u ~[l] const _ c ‚Üî Tendsto u l (ùìù c) := by
  simp +unfoldPartialApp only [IsEquivalent, const, isLittleO_const_iff h]
  constructor <;> intro h
  ¬∑ have := h.sub (tendsto_const_nhds (x := -c))
    simp only [Pi.sub_apply, sub_neg_eq_add, sub_add_cancel, zero_add] at this
    exact this
  ¬∑ have := h.sub (tendsto_const_nhds (x := c))
    rwa [sub_self] at this

theorem IsEquivalent.tendsto_const {c : Œ≤} (hu : u ~[l] const _ c) : Tendsto u l (ùìù c) := by
  rcases em <| c = 0 with rfl | h
  ¬∑ exact (tendsto_congr' <| isEquivalent_zero_iff_eventually_zero.mp hu).mpr tendsto_const_nhds
  ¬∑ exact (isEquivalent_const_iff_tendsto h).mp hu

theorem IsEquivalent.tendsto_nhds {c : Œ≤} (huv : u ~[l] v) (hu : Tendsto u l (ùìù c)) :
    Tendsto v l (ùìù c) := by
  by_cases h : c = 0
  ¬∑ subst c
    rw [‚Üê isLittleO_one_iff ‚Ñù] at hu ‚ä¢
    simpa using (huv.symm.isLittleO.trans hu).add hu
  ¬∑ rw [‚Üê isEquivalent_const_iff_tendsto h] at hu ‚ä¢
    exact huv.symm.trans hu

theorem IsEquivalent.tendsto_nhds_iff {c : Œ≤} (huv : u ~[l] v) :
    Tendsto u l (ùìù c) ‚Üî Tendsto v l (ùìù c) :=
  ‚ü®huv.tendsto_nhds, huv.symm.tendsto_nhds‚ü©

theorem IsEquivalent.add_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u + w ~[l] v := by
  simpa only [IsEquivalent, add_sub_right_comm] using huv.add hwv

theorem IsEquivalent.sub_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u - w ~[l] v := by
  simpa only [sub_eq_add_neg] using huv.add_isLittleO hwv.neg_left

theorem IsLittleO.add_isEquivalent (hu : u =o[l] w) (hv : v ~[l] w) : u + v ~[l] w :=
  add_comm v u ‚ñ∏ hv.add_isLittleO hu

theorem IsEquivalent.add_const_of_norm_tendsto_atTop {c : Œ≤}
    (huv : u ~[l] v) (hv : Tendsto (norm ‚àò v) l atTop) :
    (u ¬∑ + c) ~[l] v :=
  huv.add_isLittleO <| isLittleO_const_left.mpr (Or.inr hv)

theorem IsEquivalent.const_add_of_norm_tendsto_atTop {c : Œ≤}
    (huv : u ~[l] v) (hv : Tendsto (norm ‚àò v) l atTop) :
    (c + u ¬∑) ~[l] v :=
  (isLittleO_const_left.mpr (Or.inr hv)).add_isEquivalent huv

theorem IsLittleO.isEquivalent (huv : (u - v) =o[l] v) : u ~[l] v := huv

theorem IsEquivalent.neg (huv : u ~[l] v) : (fun x ‚Ü¶ -u x) ~[l] fun x ‚Ü¶ -v x := by
  rw [IsEquivalent]
  convert huv.isLittleO.neg_left.neg_right
  simp [neg_add_eq_sub]

end NormedAddCommGroup

open Asymptotics

section NormedField

variable {Œ± Œ≤ : Type*} [NormedField Œ≤] {u v : Œ± ‚Üí Œ≤} {l : Filter Œ±}

theorem isEquivalent_iff_exists_eq_mul :
    u ~[l] v ‚Üî ‚àÉ (œÜ : Œ± ‚Üí Œ≤) (_ : Tendsto œÜ l (ùìù 1)), u =·∂†[l] œÜ * v := by
  rw [IsEquivalent, isLittleO_iff_exists_eq_mul]
  constructor <;> rintro ‚ü®œÜ, hœÜ, h‚ü© <;> [refine ‚ü®œÜ + 1, ?_, ?_‚ü©; refine ‚ü®œÜ - 1, ?_, ?_‚ü©]
  ¬∑ conv in ùìù _ => rw [‚Üê zero_add (1 : Œ≤)]
    exact hœÜ.add tendsto_const_nhds
  ¬∑ convert h.fun_add (EventuallyEq.refl l v) <;> simp [add_mul]
  ¬∑ conv in ùìù _ => rw [‚Üê sub_self (1 : Œ≤)]
    exact hœÜ.sub tendsto_const_nhds
  ¬∑ convert h.fun_sub (EventuallyEq.refl l v); simp [sub_mul]

theorem IsEquivalent.exists_eq_mul (huv : u ~[l] v) :
    ‚àÉ (œÜ : Œ± ‚Üí Œ≤) (_ : Tendsto œÜ l (ùìù 1)), u =·∂†[l] œÜ * v :=
  isEquivalent_iff_exists_eq_mul.mp huv

theorem isEquivalent_of_tendsto_one (hz : ‚àÄ·∂† x in l, v x = 0 ‚Üí u x = 0)
    (huv : Tendsto (u / v) l (ùìù 1)) : u ~[l] v := by
  rw [isEquivalent_iff_exists_eq_mul]
  exact ‚ü®u / v, huv, hz.mono fun x hz' ‚Ü¶ (div_mul_cancel_of_imp hz').symm‚ü©

theorem isEquivalent_of_tendsto_one' (hz : ‚àÄ x, v x = 0 ‚Üí u x = 0) (huv : Tendsto (u / v) l (ùìù 1)) :
    u ~[l] v :=
  isEquivalent_of_tendsto_one (Eventually.of_forall hz) huv

theorem isEquivalent_iff_tendsto_one (hz : ‚àÄ·∂† x in l, v x ‚â† 0) :
    u ~[l] v ‚Üî Tendsto (u / v) l (ùìù 1) := by
  constructor
  ¬∑ intro hequiv
    have := hequiv.isLittleO.tendsto_div_nhds_zero
    simp only [Pi.sub_apply, sub_div] at this
    have key : Tendsto (fun x ‚Ü¶ v x / v x) l (ùìù 1) :=
      (tendsto_congr' <| hz.mono fun x hnz ‚Ü¶ @div_self _ _ (v x) hnz).mpr tendsto_const_nhds
    convert this.add key
    ¬∑ simp
    ¬∑ norm_num
  ¬∑ exact isEquivalent_of_tendsto_one (hz.mono fun x hnvz hz ‚Ü¶ (hnvz hz).elim)

end NormedField

section SMul

theorem IsEquivalent.smul {Œ± E ùïú : Type*} [NormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
    {a b : Œ± ‚Üí ùïú} {u v : Œ± ‚Üí E} {l : Filter Œ±} (hab : a ~[l] b) (huv : u ~[l] v) :
    (fun x ‚Ü¶ a x ‚Ä¢ u x) ~[l] fun x ‚Ü¶ b x ‚Ä¢ v x := by
  rcases hab.exists_eq_mul with ‚ü®œÜ, hœÜ, habœÜ‚ü©
  have : ((fun x ‚Ü¶ a x ‚Ä¢ u x) - (fun x ‚Ü¶ b x ‚Ä¢ v x)) =·∂†[l] fun x ‚Ü¶ b x ‚Ä¢ (œÜ x ‚Ä¢ u x - v x) := by
    convert (habœÜ.comp‚ÇÇ (¬∑ ‚Ä¢ ¬∑) <| EventuallyEq.refl _ u).fun_sub
      (EventuallyEq.refl _ fun x ‚Ü¶ b x ‚Ä¢ v x) using 1
    ext
    rw [Pi.mul_apply, mul_comm, mul_smul, ‚Üê smul_sub]
  refine (isLittleO_congr this.symm <| EventuallyEq.rfl).mp ((isBigO_refl b l).smul_isLittleO ?_)
  rcases huv.isBigO.exists_pos with ‚ü®C, hC, hCuv‚ü©
  rw [IsEquivalent] at *
  rw [isLittleO_iff] at *
  rw [IsBigOWith] at hCuv
  simp only [Metric.tendsto_nhds, dist_eq_norm] at hœÜ
  intro c hc
  specialize hœÜ (c / 2 / C) (div_pos (div_pos hc zero_lt_two) hC)
  specialize huv (div_pos hc zero_lt_two)
  refine hœÜ.mp (huv.mp <| hCuv.mono fun x hCuvx huvx hœÜx ‚Ü¶ ?_)
  have key :=
    calc
      ‚ÄñœÜ x - 1‚Äñ * ‚Äñu x‚Äñ ‚â§ c / 2 / C * ‚Äñu x‚Äñ := by gcongr
      _ ‚â§ c / 2 / C * (C * ‚Äñv x‚Äñ) := by gcongr
      _ = c / 2 * ‚Äñv x‚Äñ := by
        field_simp
  calc
    ‚Äñ((fun x : Œ± ‚Ü¶ œÜ x ‚Ä¢ u x) - v) x‚Äñ = ‚Äñ(œÜ x - 1) ‚Ä¢ u x + (u x - v x)‚Äñ := by
      simp [sub_smul, sub_add]
    _ ‚â§ ‚Äñ(œÜ x - 1) ‚Ä¢ u x‚Äñ + ‚Äñu x - v x‚Äñ := norm_add_le _ _
    _ = ‚ÄñœÜ x - 1‚Äñ * ‚Äñu x‚Äñ + ‚Äñu x - v x‚Äñ := by rw [norm_smul]
    _ ‚â§ c / 2 * ‚Äñv x‚Äñ + ‚Äñu x - v x‚Äñ := by gcongr
    _ ‚â§ c / 2 * ‚Äñv x‚Äñ + c / 2 * ‚Äñv x‚Äñ := by gcongr; exact huvx
    _ = c * ‚Äñv x‚Äñ := by ring

end SMul

section mul_inv

variable {Œ± Œπ Œ≤ : Type*} [NormedField Œ≤] {t u v w : Œ± ‚Üí Œ≤} {l : Filter Œ±}

protected theorem IsEquivalent.mul (htu : t ~[l] u) (hvw : v ~[l] w) : t * v ~[l] u * w :=
  htu.smul hvw

theorem IsEquivalent.listProd {L : List Œπ} {f g : Œπ ‚Üí Œ± ‚Üí Œ≤} (h : ‚àÄ i ‚àà L, f i ~[l] g i) :
    (fun x ‚Ü¶ (L.map (f ¬∑ x)).prod) ~[l] (fun x ‚Ü¶ (L.map (g ¬∑ x)).prod) := by
  induction L with
  | nil => simp [IsEquivalent.refl]
  | cons i L ihL =>
    simp only [List.forall_mem_cons, List.map_cons, List.prod_cons] at h ‚ä¢
    exact h.1.mul (ihL h.2)

theorem IsEquivalent.multisetProd {s : Multiset Œπ} {f g : Œπ ‚Üí Œ± ‚Üí Œ≤} (h : ‚àÄ i ‚àà s, f i ~[l] g i) :
    (fun x ‚Ü¶ (s.map (f ¬∑ x)).prod) ~[l] (fun x ‚Ü¶ (s.map (g ¬∑ x)).prod) := by
  obtain ‚ü®l, rfl‚ü© : ‚àÉ l : List Œπ, ‚Üël = s := Quotient.mk_surjective s
  exact listProd h

theorem IsEquivalent.finsetProd {s : Finset Œπ} {f g : Œπ ‚Üí Œ± ‚Üí Œ≤} (h : ‚àÄ i ‚àà s, f i ~[l] g i) :
    (‚àè i ‚àà s, f i ¬∑) ~[l] (‚àè i ‚àà s, g i ¬∑) :=
  multisetProd h

protected theorem IsEquivalent.inv (huv : u ~[l] v) : u‚Åª¬π ~[l] v‚Åª¬π := by
  rw [isEquivalent_iff_exists_eq_mul] at *
  rcases huv with ‚ü®œÜ, hœÜ, h‚ü©
  rw [‚Üê inv_one]
  refine ‚ü®fun x ‚Ü¶ (œÜ x)‚Åª¬π, Tendsto.inv‚ÇÄ hœÜ (by simp), ?_‚ü©
  convert h.fun_inv
  simp [mul_comm]

protected theorem IsEquivalent.div (htu : t ~[l] u) (hvw : v ~[l] w) :
    t / v ~[l] u / w := by
  simpa only [div_eq_mul_inv] using htu.mul hvw.inv

protected theorem IsEquivalent.pow (h : t ~[l] u) (n : ‚Ñï) : t ^ n ~[l] u ^ n := by
  induction n with
  | zero => simpa using IsEquivalent.refl
  | succ _ ih => simpa [pow_succ] using ih.mul h

protected theorem IsEquivalent.zpow (h : t ~[l] u) (z : ‚Ñ§) : t ^ z ~[l] u ^ z := by
  match z with
  | Int.ofNat _ => simpa using h.pow _
  | Int.negSucc _ => simpa using (h.pow _).inv

end mul_inv

section NormedLinearOrderedField

variable {Œ± Œ≤ : Type*} [NormedField Œ≤] [LinearOrder Œ≤] [IsStrictOrderedRing Œ≤]
  {u v : Œ± ‚Üí Œ≤} {l : Filter Œ±}

theorem IsEquivalent.tendsto_atTop [OrderTopology Œ≤] (huv : u ~[l] v) (hu : Tendsto u l atTop) :
    Tendsto v l atTop :=
  let ‚ü®œÜ, hœÜ, h‚ü© := huv.symm.exists_eq_mul
  Tendsto.congr' h.symm (mul_comm u œÜ ‚ñ∏ hu.atTop_mul_pos zero_lt_one hœÜ)

theorem IsEquivalent.tendsto_atTop_iff [OrderTopology Œ≤] (huv : u ~[l] v) :
    Tendsto u l atTop ‚Üî Tendsto v l atTop :=
  ‚ü®huv.tendsto_atTop, huv.symm.tendsto_atTop‚ü©

theorem IsEquivalent.tendsto_atBot [OrderTopology Œ≤] (huv : u ~[l] v) (hu : Tendsto u l atBot) :
    Tendsto v l atBot := by
  convert tendsto_neg_atTop_atBot.comp (huv.neg.tendsto_atTop <| tendsto_neg_atBot_atTop.comp hu)
  ext
  simp

theorem IsEquivalent.tendsto_atBot_iff [OrderTopology Œ≤] (huv : u ~[l] v) :
    Tendsto u l atBot ‚Üî Tendsto v l atBot :=
  ‚ü®huv.tendsto_atBot, huv.symm.tendsto_atBot‚ü©

end NormedLinearOrderedField

section Real

theorem IsEquivalent.add_add_of_nonneg {Œ± : Type*} {u v t w : Œ± ‚Üí ‚Ñù} {l : Filter Œ±}
    (hu : 0 ‚â§ v) (hw : 0 ‚â§ w) (htu : u ~[l] v) (hvw : t ~[l] w) :
    u + t ~[l] v + w := by
  simp only [IsEquivalent, add_sub_add_comm]
  change (fun x ‚Ü¶ (u - v) x + (t - w) x) =o[l] (fun x ‚Ü¶ v x + w x)
  conv => enter [3, x]; rw [‚Üê abs_eq_self.mpr (hu x), ‚Üê abs_eq_self.mpr (hw x)]
  simpa [‚Üê Real.norm_eq_abs] using .add_add htu hvw

end Real

end Asymptotics

open Filter Asymptotics

open Asymptotics

variable {Œ± Œ≤ Œ≤‚ÇÇ : Type*} [NormedAddCommGroup Œ≤] [Norm Œ≤‚ÇÇ] {l : Filter Œ±}

theorem Filter.EventuallyEq.isEquivalent {u v : Œ± ‚Üí Œ≤} (h : u =·∂†[l] v) : u ~[l] v :=
  IsEquivalent.congr_right (isLittleO_refl_left _ _) h

@[trans]
theorem Filter.EventuallyEq.trans_isEquivalent {f g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} (h : f =·∂†[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ ~[l] g‚ÇÇ) : f ~[l] g‚ÇÇ :=
  h.isEquivalent.trans h‚ÇÇ

namespace Asymptotics

instance transIsEquivalentIsEquivalent :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (IsEquivalent l) (IsEquivalent l) (IsEquivalent l) where
  trans := IsEquivalent.trans

instance transEventuallyEqIsEquivalent :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (EventuallyEq l) (IsEquivalent l) (IsEquivalent l) where
  trans := EventuallyEq.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_eventuallyEq {f g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} (h : f ~[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ =·∂†[l] g‚ÇÇ) : f ~[l] g‚ÇÇ :=
  h.trans h‚ÇÇ.isEquivalent

instance transIsEquivalentEventuallyEq :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (IsEquivalent l) (EventuallyEq l) (IsEquivalent l) where
  trans := IsEquivalent.trans_eventuallyEq

@[trans]
theorem IsEquivalent.trans_isBigO {f g‚ÇÅ : Œ± ‚Üí Œ≤} {g‚ÇÇ : Œ± ‚Üí Œ≤‚ÇÇ} (h : f ~[l] g‚ÇÅ) (h‚ÇÇ : g‚ÇÅ =O[l] g‚ÇÇ) :
    f =O[l] g‚ÇÇ :=
  IsBigO.trans h.isBigO h‚ÇÇ

instance transIsEquivalentIsBigO :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤‚ÇÇ) (IsEquivalent l) (IsBigO l) (IsBigO l) where
  trans := IsEquivalent.trans_isBigO

@[trans]
theorem IsBigO.trans_isEquivalent {f : Œ± ‚Üí Œ≤‚ÇÇ} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} (h : f =O[l] g‚ÇÅ) (h‚ÇÇ : g‚ÇÅ ~[l] g‚ÇÇ) :
    f =O[l] g‚ÇÇ :=
  IsBigO.trans h h‚ÇÇ.isBigO

instance transIsBigOIsEquivalent :
    @Trans (Œ± ‚Üí Œ≤‚ÇÇ) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (IsBigO l) (IsEquivalent l) (IsBigO l) where
  trans := IsBigO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isLittleO {f g‚ÇÅ : Œ± ‚Üí Œ≤} {g‚ÇÇ : Œ± ‚Üí Œ≤‚ÇÇ} (h : f ~[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ =o[l] g‚ÇÇ) : f =o[l] g‚ÇÇ :=
  IsBigO.trans_isLittleO h.isBigO h‚ÇÇ

instance transIsEquivalentIsLittleO :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤‚ÇÇ) (IsEquivalent l) (IsLittleO l) (IsLittleO l) where
  trans := IsEquivalent.trans_isLittleO

@[trans]
theorem IsLittleO.trans_isEquivalent {f : Œ± ‚Üí Œ≤‚ÇÇ} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} (h : f =o[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ ~[l] g‚ÇÇ) : f =o[l] g‚ÇÇ :=
  IsLittleO.trans_isBigO h h‚ÇÇ.isBigO

instance transIsLittleOIsEquivalent :
    @Trans (Œ± ‚Üí Œ≤‚ÇÇ) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (IsLittleO l) (IsEquivalent l) (IsLittleO l) where
  trans := IsLittleO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isTheta {f g‚ÇÅ : Œ± ‚Üí Œ≤} {g‚ÇÇ : Œ± ‚Üí Œ≤‚ÇÇ} (h : f ~[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ =Œò[l] g‚ÇÇ) : f =Œò[l] g‚ÇÇ :=
  IsTheta.trans h.isTheta h‚ÇÇ

instance transIsEquivalentIsTheta :
    @Trans (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤‚ÇÇ) (IsEquivalent l) (IsTheta l) (IsTheta l) where
  trans := IsEquivalent.trans_isTheta

@[trans]
theorem IsTheta.trans_isEquivalent {f : Œ± ‚Üí Œ≤‚ÇÇ} {g‚ÇÅ g‚ÇÇ : Œ± ‚Üí Œ≤} (h : f =Œò[l] g‚ÇÅ)
    (h‚ÇÇ : g‚ÇÅ ~[l] g‚ÇÇ) : f =Œò[l] g‚ÇÇ :=
  IsTheta.trans h h‚ÇÇ.isTheta

instance transIsThetaIsEquivalent :
    @Trans (Œ± ‚Üí Œ≤‚ÇÇ) (Œ± ‚Üí Œ≤) (Œ± ‚Üí Œ≤) (IsTheta l) (IsEquivalent l) (IsTheta l) where
  trans := IsTheta.trans_isEquivalent

theorem IsEquivalent.comp_tendsto {Œ±‚ÇÇ : Type*} {f g : Œ±‚ÇÇ ‚Üí Œ≤} {l' : Filter Œ±‚ÇÇ}
    (hfg : f ~[l'] g) {k : Œ± ‚Üí Œ±‚ÇÇ} (hk : Filter.Tendsto k l l') : (f ‚àò k) ~[l] (g ‚àò k) :=
  IsLittleO.comp_tendsto hfg hk

@[simp]
theorem isEquivalent_map {Œ±‚ÇÇ : Type*} {f g : Œ±‚ÇÇ ‚Üí Œ≤} {k : Œ± ‚Üí Œ±‚ÇÇ} :
    f ~[Filter.map k l] g ‚Üî (f ‚àò k) ~[l] (g ‚àò k) :=
  isLittleO_map

theorem IsEquivalent.mono {f g : Œ± ‚Üí Œ≤} {l' : Filter Œ±} (h : f ~[l'] g) (hl : l ‚â§ l') :
    f ~[l] g :=
  IsLittleO.mono h hl

end Asymptotics
