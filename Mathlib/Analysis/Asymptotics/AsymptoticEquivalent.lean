/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Asymptotics.Theta

/-!
# Asymptotic equivalence

In this file, we define the relation `IsEquivalent l u v`, which means that `u-v` is little o of
`v` along the filter `l`.

Unlike `Is(Little|Big)O` relations, this one requires `u` and `v` to have the same codomain `Î²`.
While the definition only requires `Î²` to be a `NormedAddCommGroup`, most interesting properties
require it to be a `NormedField`.

## Notations

We introduce the notation `u ~[l] v := IsEquivalent l u v`, which you can use by opening the
`Asymptotics` locale.

## Main results

If `Î²` is a `NormedAddCommGroup` :

- `_ ~[l] _` is an equivalence relation
- Equivalent statements for `u ~[l] const _ c` :
  - If `c â‰  0`, this is true iff `Tendsto u l (ğ“ c)` (see `isEquivalent_const_iff_tendsto`)
  - For `c = 0`, this is true iff `u =á¶ [l] 0` (see `isEquivalent_zero_iff_eventually_zero`)

If `Î²` is a `NormedField` :

- Alternative characterization of the relation (see `isEquivalent_iff_exists_eq_mul`) :

  `u ~[l] v â†” âˆƒ (Ï† : Î± â†’ Î²) (hÏ† : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v`

- Provided some non-vanishing hypothesis, this can be seen as `u ~[l] v â†” Tendsto (u/v) l (ğ“ 1)`
  (see `isEquivalent_iff_tendsto_one`)
- For any constant `c`, `u ~[l] v` implies `Tendsto u l (ğ“ c) â†” Tendsto v l (ğ“ c)`
  (see `IsEquivalent.tendsto_nhds_iff`)
- `*` and `/` are compatible with `_ ~[l] _` (see `IsEquivalent.mul` and `IsEquivalent.div`)

If `Î²` is a `NormedLinearOrderedField` :

- If `u ~[l] v`, we have `Tendsto u l atTop â†” Tendsto v l atTop`
  (see `IsEquivalent.tendsto_atTop_iff`)

## Implementation Notes

Note that `IsEquivalent` takes the parameters `(l : Filter Î±) (u v : Î± â†’ Î²)` in that order.
This is to enable `calc` support, as `calc` requires that the last two explicit arguments are `u v`.

-/


namespace Asymptotics

open Filter Function

open Topology

section NormedAddCommGroup

variable {Î± Î² : Type*} [NormedAddCommGroup Î²]

/-- Two functions `u` and `v` are said to be asymptotically equivalent along a filter `l`
  (denoted as `u ~[l] v` in the `Asymptotics` namespace)
  when `u x - v x = o(v x)` as `x` converges along `l`. -/
def IsEquivalent (l : Filter Î±) (u v : Î± â†’ Î²) :=
  (u - v) =o[l] v

@[inherit_doc] scoped notation:50 u " ~[" l:50 "] " v:50 => Asymptotics.IsEquivalent l u v

variable {u v w : Î± â†’ Î²} {l : Filter Î±}

theorem IsEquivalent.isLittleO (h : u ~[l] v) : (u - v) =o[l] v := h

nonrec theorem IsEquivalent.isBigO (h : u ~[l] v) : u =O[l] v :=
  (IsBigO.congr_of_sub h.isBigO.symm).mp (isBigO_refl _ _)

theorem IsEquivalent.isBigO_symm (h : u ~[l] v) : v =O[l] u := by
  convert h.isLittleO.right_isBigO_add
  simp

theorem IsEquivalent.isTheta (h : u ~[l] v) : u =Î˜[l] v :=
  âŸ¨h.isBigO, h.isBigO_symmâŸ©

theorem IsEquivalent.isTheta_symm (h : u ~[l] v) : v =Î˜[l] u :=
  âŸ¨h.isBigO_symm, h.isBigOâŸ©

@[refl]
theorem IsEquivalent.refl : u ~[l] u := by
  rw [IsEquivalent, sub_self]
  exact isLittleO_zero _ _

@[symm]
theorem IsEquivalent.symm (h : u ~[l] v) : v ~[l] u :=
  (h.isLittleO.trans_isBigO h.isBigO_symm).symm

@[trans]
theorem IsEquivalent.trans {l : Filter Î±} {u v w : Î± â†’ Î²} (huv : u ~[l] v) (hvw : v ~[l] w) :
    u ~[l] w :=
  (huv.isLittleO.trans_isBigO hvw.isBigO).triangle hvw.isLittleO

theorem IsEquivalent.congr_left {u v w : Î± â†’ Î²} {l : Filter Î±} (huv : u ~[l] v) (huw : u =á¶ [l] w) :
    w ~[l] v :=
  huv.congr' (huw.sub (EventuallyEq.refl _ _)) (EventuallyEq.refl _ _)

theorem IsEquivalent.congr_right {u v w : Î± â†’ Î²} {l : Filter Î±} (huv : u ~[l] v) (hvw : v =á¶ [l] w) :
    u ~[l] w :=
  (huv.symm.congr_left hvw).symm

theorem isEquivalent_zero_iff_eventually_zero : u ~[l] 0 â†” u =á¶ [l] 0 := by
  rw [IsEquivalent, sub_zero]
  exact isLittleO_zero_right_iff

theorem isEquivalent_zero_iff_isBigO_zero : u ~[l] 0 â†” u =O[l] (0 : Î± â†’ Î²) := by
  refine âŸ¨IsEquivalent.isBigO, fun h â†¦ ?_âŸ©
  rw [isEquivalent_zero_iff_eventually_zero, eventuallyEq_iff_exists_mem]
  exact âŸ¨{ x : Î± | u x = 0 }, isBigO_zero_right_iff.mp h, fun x hx â†¦ hxâŸ©

theorem isEquivalent_const_iff_tendsto {c : Î²} (h : c â‰  0) :
    u ~[l] const _ c â†” Tendsto u l (ğ“ c) := by
  simp +unfoldPartialApp only [IsEquivalent, const, isLittleO_const_iff h]
  constructor <;> intro h
  Â· have := h.sub (tendsto_const_nhds (x := -c))
    simp only [Pi.sub_apply, sub_neg_eq_add, sub_add_cancel, zero_add] at this
    exact this
  Â· have := h.sub (tendsto_const_nhds (x := c))
    rwa [sub_self] at this

theorem IsEquivalent.tendsto_const {c : Î²} (hu : u ~[l] const _ c) : Tendsto u l (ğ“ c) := by
  rcases em <| c = 0 with rfl | h
  Â· exact (tendsto_congr' <| isEquivalent_zero_iff_eventually_zero.mp hu).mpr tendsto_const_nhds
  Â· exact (isEquivalent_const_iff_tendsto h).mp hu

theorem IsEquivalent.tendsto_nhds {c : Î²} (huv : u ~[l] v) (hu : Tendsto u l (ğ“ c)) :
    Tendsto v l (ğ“ c) := by
  by_cases h : c = 0
  Â· subst c
    rw [â† isLittleO_one_iff â„] at hu âŠ¢
    simpa using (huv.symm.isLittleO.trans hu).add hu
  Â· rw [â† isEquivalent_const_iff_tendsto h] at hu âŠ¢
    exact huv.symm.trans hu

theorem IsEquivalent.tendsto_nhds_iff {c : Î²} (huv : u ~[l] v) :
    Tendsto u l (ğ“ c) â†” Tendsto v l (ğ“ c) :=
  âŸ¨huv.tendsto_nhds, huv.symm.tendsto_nhdsâŸ©

theorem IsEquivalent.add_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u + w ~[l] v := by
  simpa only [IsEquivalent, add_sub_right_comm] using huv.add hwv

theorem IsEquivalent.sub_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u - w ~[l] v := by
  simpa only [sub_eq_add_neg] using huv.add_isLittleO hwv.neg_left

theorem IsLittleO.add_isEquivalent (hu : u =o[l] w) (hv : v ~[l] w) : u + v ~[l] w :=
  add_comm v u â–¸ hv.add_isLittleO hu

theorem IsLittleO.isEquivalent (huv : (u - v) =o[l] v) : u ~[l] v := huv

theorem IsEquivalent.neg (huv : u ~[l] v) : (fun x â†¦ -u x) ~[l] fun x â†¦ -v x := by
  rw [IsEquivalent]
  convert huv.isLittleO.neg_left.neg_right
  simp [neg_add_eq_sub]

end NormedAddCommGroup

open Asymptotics

section NormedField

variable {Î± Î² : Type*} [NormedField Î²] {u v : Î± â†’ Î²} {l : Filter Î±}

theorem isEquivalent_iff_exists_eq_mul :
    u ~[l] v â†” âˆƒ (Ï† : Î± â†’ Î²) (_ : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v := by
  rw [IsEquivalent, isLittleO_iff_exists_eq_mul]
  constructor <;> rintro âŸ¨Ï†, hÏ†, hâŸ© <;> [refine âŸ¨Ï† + 1, ?_, ?_âŸ©; refine âŸ¨Ï† - 1, ?_, ?_âŸ©]
  Â· conv in ğ“ _ => rw [â† zero_add (1 : Î²)]
    exact hÏ†.add tendsto_const_nhds
  Â· convert h.add (EventuallyEq.refl l v) <;> simp [add_mul]
  Â· conv in ğ“ _ => rw [â† sub_self (1 : Î²)]
    exact hÏ†.sub tendsto_const_nhds
  Â· convert h.sub (EventuallyEq.refl l v); simp [sub_mul]

theorem IsEquivalent.exists_eq_mul (huv : u ~[l] v) :
    âˆƒ (Ï† : Î± â†’ Î²) (_ : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v :=
  isEquivalent_iff_exists_eq_mul.mp huv

theorem isEquivalent_of_tendsto_one (hz : âˆ€á¶  x in l, v x = 0 â†’ u x = 0)
    (huv : Tendsto (u / v) l (ğ“ 1)) : u ~[l] v := by
  rw [isEquivalent_iff_exists_eq_mul]
  exact âŸ¨u / v, huv, hz.mono fun x hz' â†¦ (div_mul_cancel_of_imp hz').symmâŸ©

theorem isEquivalent_of_tendsto_one' (hz : âˆ€ x, v x = 0 â†’ u x = 0) (huv : Tendsto (u / v) l (ğ“ 1)) :
    u ~[l] v :=
  isEquivalent_of_tendsto_one (Eventually.of_forall hz) huv

theorem isEquivalent_iff_tendsto_one (hz : âˆ€á¶  x in l, v x â‰  0) :
    u ~[l] v â†” Tendsto (u / v) l (ğ“ 1) := by
  constructor
  Â· intro hequiv
    have := hequiv.isLittleO.tendsto_div_nhds_zero
    simp only [Pi.sub_apply, sub_div] at this
    have key : Tendsto (fun x â†¦ v x / v x) l (ğ“ 1) :=
      (tendsto_congr' <| hz.mono fun x hnz â†¦ @div_self _ _ (v x) hnz).mpr tendsto_const_nhds
    convert this.add key
    Â· simp
    Â· norm_num
  Â· exact isEquivalent_of_tendsto_one (hz.mono fun x hnvz hz â†¦ (hnvz hz).elim)

end NormedField

section SMul

theorem IsEquivalent.smul {Î± E ğ•œ : Type*} [NormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {a b : Î± â†’ ğ•œ} {u v : Î± â†’ E} {l : Filter Î±} (hab : a ~[l] b) (huv : u ~[l] v) :
    (fun x â†¦ a x â€¢ u x) ~[l] fun x â†¦ b x â€¢ v x := by
  rcases hab.exists_eq_mul with âŸ¨Ï†, hÏ†, habÏ†âŸ©
  have : ((fun x â†¦ a x â€¢ u x) - (fun x â†¦ b x â€¢ v x)) =á¶ [l] fun x â†¦ b x â€¢ (Ï† x â€¢ u x - v x) := by
    convert (habÏ†.compâ‚‚ (Â· â€¢ Â·) <| EventuallyEq.refl _ u).sub
      (EventuallyEq.refl _ fun x â†¦ b x â€¢ v x) using 1
    ext
    rw [Pi.mul_apply, mul_comm, mul_smul, â† smul_sub]
  refine (isLittleO_congr this.symm <| EventuallyEq.rfl).mp ((isBigO_refl b l).smul_isLittleO ?_)
  rcases huv.isBigO.exists_pos with âŸ¨C, hC, hCuvâŸ©
  rw [IsEquivalent] at *
  rw [isLittleO_iff] at *
  rw [IsBigOWith] at hCuv
  simp only [Metric.tendsto_nhds, dist_eq_norm] at hÏ†
  intro c hc
  specialize hÏ† (c / 2 / C) (div_pos (div_pos hc zero_lt_two) hC)
  specialize huv (div_pos hc zero_lt_two)
  refine hÏ†.mp (huv.mp <| hCuv.mono fun x hCuvx huvx hÏ†x â†¦ ?_)
  have key :=
    calc
      â€–Ï† x - 1â€– * â€–u xâ€– â‰¤ c / 2 / C * â€–u xâ€– := by gcongr
      _ â‰¤ c / 2 / C * (C * â€–v xâ€–) := by gcongr
      _ = c / 2 * â€–v xâ€– := by
        field_simp [hC.ne.symm]
        ring
  calc
    â€–((fun x : Î± â†¦ Ï† x â€¢ u x) - v) xâ€– = â€–(Ï† x - 1) â€¢ u x + (u x - v x)â€– := by
      simp [sub_smul, sub_add]
    _ â‰¤ â€–(Ï† x - 1) â€¢ u xâ€– + â€–u x - v xâ€– := norm_add_le _ _
    _ = â€–Ï† x - 1â€– * â€–u xâ€– + â€–u x - v xâ€– := by rw [norm_smul]
    _ â‰¤ c / 2 * â€–v xâ€– + â€–u x - v xâ€– := by gcongr
    _ â‰¤ c / 2 * â€–v xâ€– + c / 2 * â€–v xâ€– := by gcongr; exact huvx
    _ = c * â€–v xâ€– := by ring

end SMul

section mul_inv

variable {Î± Î¹ Î² : Type*} [NormedField Î²] {t u v w : Î± â†’ Î²} {l : Filter Î±}

protected theorem IsEquivalent.mul (htu : t ~[l] u) (hvw : v ~[l] w) : t * v ~[l] u * w :=
  htu.smul hvw

theorem IsEquivalent.listProd {L : List Î¹} {f g : Î¹ â†’ Î± â†’ Î²} (h : âˆ€ i âˆˆ L, f i ~[l] g i) :
    (fun x â†¦ (L.map (f Â· x)).prod) ~[l] (fun x â†¦ (L.map (g Â· x)).prod) := by
  induction L with
  | nil => simp [IsEquivalent.refl]
  | cons i L ihL =>
    simp only [List.forall_mem_cons, List.map_cons, List.prod_cons] at h âŠ¢
    exact h.1.mul (ihL h.2)

theorem IsEquivalent.multisetProd {s : Multiset Î¹} {f g : Î¹ â†’ Î± â†’ Î²} (h : âˆ€ i âˆˆ s, f i ~[l] g i) :
    (fun x â†¦ (s.map (f Â· x)).prod) ~[l] (fun x â†¦ (s.map (g Â· x)).prod) := by
  obtain âŸ¨l, rflâŸ© : âˆƒ l : List Î¹, â†‘l = s := Quotient.mk_surjective s
  exact listProd h

theorem IsEquivalent.finsetProd {s : Finset Î¹} {f g : Î¹ â†’ Î± â†’ Î²} (h : âˆ€ i âˆˆ s, f i ~[l] g i) :
    (âˆ i âˆˆ s, f i Â·) ~[l] (âˆ i âˆˆ s, g i Â·) :=
  multisetProd h

protected theorem IsEquivalent.inv (huv : u ~[l] v) : (fun x â†¦ (u x)â»Â¹) ~[l] fun x â†¦ (v x)â»Â¹ := by
  rw [isEquivalent_iff_exists_eq_mul] at *
  rcases huv with âŸ¨Ï†, hÏ†, hâŸ©
  rw [â† inv_one]
  refine âŸ¨fun x â†¦ (Ï† x)â»Â¹, Tendsto.invâ‚€ hÏ† (by norm_num), ?_âŸ©
  convert h.inv
  simp [mul_comm]

protected theorem IsEquivalent.div (htu : t ~[l] u) (hvw : v ~[l] w) :
    (fun x â†¦ t x / v x) ~[l] fun x â†¦ u x / w x := by
  simpa only [div_eq_mul_inv] using htu.mul hvw.inv

protected theorem IsEquivalent.pow (h : t ~[l] u) (n : â„•) : t ^ n ~[l] u ^ n := by
  induction n with
  | zero => simpa using IsEquivalent.refl
  | succ _ ih => simpa [pow_succ] using ih.mul h

protected theorem IsEquivalent.zpow (h : t ~[l] u) (z : â„¤) : t ^ z ~[l] u ^ z := by
  match z with
  | Int.ofNat _ => simpa using h.pow _
  | Int.negSucc _ => simpa using (h.pow _).inv

end mul_inv

section NormedLinearOrderedField

variable {Î± Î² : Type*} [NormedField Î²] [LinearOrder Î²] [IsStrictOrderedRing Î²]
  {u v : Î± â†’ Î²} {l : Filter Î±}

theorem IsEquivalent.tendsto_atTop [OrderTopology Î²] (huv : u ~[l] v) (hu : Tendsto u l atTop) :
    Tendsto v l atTop :=
  let âŸ¨Ï†, hÏ†, hâŸ© := huv.symm.exists_eq_mul
  Tendsto.congr' h.symm (mul_comm u Ï† â–¸ hu.atTop_mul_pos zero_lt_one hÏ†)

theorem IsEquivalent.tendsto_atTop_iff [OrderTopology Î²] (huv : u ~[l] v) :
    Tendsto u l atTop â†” Tendsto v l atTop :=
  âŸ¨huv.tendsto_atTop, huv.symm.tendsto_atTopâŸ©

theorem IsEquivalent.tendsto_atBot [OrderTopology Î²] (huv : u ~[l] v) (hu : Tendsto u l atBot) :
    Tendsto v l atBot := by
  convert tendsto_neg_atTop_atBot.comp (huv.neg.tendsto_atTop <| tendsto_neg_atBot_atTop.comp hu)
  ext
  simp

theorem IsEquivalent.tendsto_atBot_iff [OrderTopology Î²] (huv : u ~[l] v) :
    Tendsto u l atBot â†” Tendsto v l atBot :=
  âŸ¨huv.tendsto_atBot, huv.symm.tendsto_atBotâŸ©

end NormedLinearOrderedField

end Asymptotics

open Filter Asymptotics

open Asymptotics

variable {Î± Î² Î²â‚‚ : Type*} [NormedAddCommGroup Î²] [Norm Î²â‚‚] {l : Filter Î±}

theorem Filter.EventuallyEq.isEquivalent {u v : Î± â†’ Î²} (h : u =á¶ [l] v) : u ~[l] v :=
  IsEquivalent.congr_right (isLittleO_refl_left _ _) h

@[trans]
theorem Filter.EventuallyEq.trans_isEquivalent {f gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =á¶ [l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f ~[l] gâ‚‚ :=
  h.isEquivalent.trans hâ‚‚

namespace Asymptotics

instance transIsEquivalentIsEquivalent :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (IsEquivalent l) (IsEquivalent l) (IsEquivalent l) where
  trans := IsEquivalent.trans

instance transEventuallyEqIsEquivalent :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (EventuallyEq l) (IsEquivalent l) (IsEquivalent l) where
  trans := EventuallyEq.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_eventuallyEq {f gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =á¶ [l] gâ‚‚) : f ~[l] gâ‚‚ :=
  h.trans hâ‚‚.isEquivalent

instance transIsEquivalentEventuallyEq :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (IsEquivalent l) (EventuallyEq l) (IsEquivalent l) where
  trans := IsEquivalent.trans_eventuallyEq

@[trans]
theorem IsEquivalent.trans_isBigO {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚) (hâ‚‚ : gâ‚ =O[l] gâ‚‚) :
    f =O[l] gâ‚‚ :=
  IsBigO.trans h.isBigO hâ‚‚

instance transIsEquivalentIsBigO :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsBigO l) (IsBigO l) where
  trans := IsEquivalent.trans_isBigO

@[trans]
theorem IsBigO.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =O[l] gâ‚) (hâ‚‚ : gâ‚ ~[l] gâ‚‚) :
    f =O[l] gâ‚‚ :=
  IsBigO.trans h hâ‚‚.isBigO

instance transIsBigOIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsBigO l) (IsEquivalent l) (IsBigO l) where
  trans := IsBigO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isLittleO {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =o[l] gâ‚‚) : f =o[l] gâ‚‚ :=
  IsBigO.trans_isLittleO h.isBigO hâ‚‚

instance transIsEquivalentIsLittleO :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsLittleO l) (IsLittleO l) where
  trans := IsEquivalent.trans_isLittleO

@[trans]
theorem IsLittleO.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =o[l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f =o[l] gâ‚‚ :=
  IsLittleO.trans_isBigO h hâ‚‚.isBigO

instance transIsLittleOIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsLittleO l) (IsEquivalent l) (IsLittleO l) where
  trans := IsLittleO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isTheta {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =Î˜[l] gâ‚‚) : f =Î˜[l] gâ‚‚ :=
  IsTheta.trans h.isTheta hâ‚‚

instance transIsEquivalentIsTheta :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsTheta l) (IsTheta l) where
  trans := IsEquivalent.trans_isTheta

@[trans]
theorem IsTheta.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =Î˜[l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f =Î˜[l] gâ‚‚ :=
  IsTheta.trans h hâ‚‚.isTheta

instance transIsThetaIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsTheta l) (IsEquivalent l) (IsTheta l) where
  trans := IsTheta.trans_isEquivalent

theorem IsEquivalent.comp_tendsto {Î±â‚‚ : Type*} {f g : Î±â‚‚ â†’ Î²} {l' : Filter Î±â‚‚}
    (hfg : f ~[l'] g) {k : Î± â†’ Î±â‚‚} (hk : Filter.Tendsto k l l') : (f âˆ˜ k) ~[l] (g âˆ˜ k) :=
  IsLittleO.comp_tendsto hfg hk

@[simp]
theorem isEquivalent_map {Î±â‚‚ : Type*} {f g : Î±â‚‚ â†’ Î²} {k : Î± â†’ Î±â‚‚} :
    f ~[Filter.map k l] g â†” (f âˆ˜ k) ~[l] (g âˆ˜ k) :=
  isLittleO_map

theorem IsEquivalent.mono {f g : Î± â†’ Î²} {l' : Filter Î±} (h : f ~[l'] g) (hl : l â‰¤ l') :
    f ~[l] g :=
  IsLittleO.mono h hl

end Asymptotics
