/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.Analysis.Asymptotics.Theta
import Mathlib.Analysis.Normed.Order.Basic

#align_import analysis.asymptotics.asymptotic_equivalent from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Asymptotic equivalence

In this file, we define the relation `IsEquivalent l u v`, which means that `u-v` is little o of
`v` along the filter `l`.

Unlike `Is(Little|Big)O` relations, this one requires `u` and `v` to have the same codomain `Î²`.
While the definition only requires `Î²` to be a `NormedAddCommGroup`, most interesting properties
require it to be a `NormedField`.

## Notations

We introduce the notation `u ~[l] v := IsEquivalent l u v`, which you can use by opening the
`Asymptotics` locale.

## Main results

If `Î²` is a `NormedAddCommGroup` :

- `_ ~[l] _` is an equivalence relation
- Equivalent statements for `u ~[l] const _ c` :
  - If `c â‰  0`, this is true iff `Tendsto u l (ğ“ c)` (see `isEquivalent_const_iff_tendsto`)
  - For `c = 0`, this is true iff `u =á¶ [l] 0` (see `isEquivalent_zero_iff_eventually_zero`)

If `Î²` is a `NormedField` :

- Alternative characterization of the relation (see `isEquivalent_iff_exists_eq_mul`) :

  `u ~[l] v â†” âˆƒ (Ï† : Î± â†’ Î²) (hÏ† : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v`

- Provided some non-vanishing hypothesis, this can be seen as `u ~[l] v â†” Tendsto (u/v) l (ğ“ 1)`
  (see `isEquivalent_iff_tendsto_one`)
- For any constant `c`, `u ~[l] v` implies `Tendsto u l (ğ“ c) â†” Tendsto v l (ğ“ c)`
  (see `IsEquivalent.tendsto_nhds_iff`)
- `*` and `/` are compatible with `_ ~[l] _` (see `IsEquivalent.mul` and `IsEquivalent.div`)

If `Î²` is a `NormedLinearOrderedField` :

- If `u ~[l] v`, we have `Tendsto u l atTop â†” Tendsto v l atTop`
  (see `IsEquivalent.tendsto_atTop_iff`)

## Implementation Notes

Note that `IsEquivalent` takes the parameters `(l : Filter Î±) (u v : Î± â†’ Î²)` in that order.
This is to enable `calc` support, as `calc` requires that the last two explicit arguments are `u v`.

-/


namespace Asymptotics

open Filter Function

open Topology

section NormedAddCommGroup

variable {Î± Î² : Type*} [NormedAddCommGroup Î²]

/-- Two functions `u` and `v` are said to be asymptotically equivalent along a filter `l` when
    `u x - v x = o(v x)` as `x` converges along `l`. -/
def IsEquivalent (l : Filter Î±) (u v : Î± â†’ Î²) :=
  (u - v) =o[l] v
#align asymptotics.is_equivalent Asymptotics.IsEquivalent

scoped notation:50 u " ~[" l:50 "] " v:50 => Asymptotics.IsEquivalent l u v

variable {u v w : Î± â†’ Î²} {l : Filter Î±}

theorem IsEquivalent.isLittleO (h : u ~[l] v) : (u - v) =o[l] v := h
#align asymptotics.is_equivalent.is_o Asymptotics.IsEquivalent.isLittleO

nonrec theorem IsEquivalent.isBigO (h : u ~[l] v) : u =O[l] v :=
  (IsBigO.congr_of_sub h.isBigO.symm).mp (isBigO_refl _ _)
set_option linter.uppercaseLean3 false in
#align asymptotics.is_equivalent.is_O Asymptotics.IsEquivalent.isBigO

theorem IsEquivalent.isBigO_symm (h : u ~[l] v) : v =O[l] u := by
  convert h.isLittleO.right_isBigO_add
  -- âŠ¢ u xâœ = (u - v) xâœ + v xâœ
  simp
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align asymptotics.is_equivalent.is_O_symm Asymptotics.IsEquivalent.isBigO_symm

theorem IsEquivalent.isTheta (h : u ~[l] v) : u =Î˜[l] v :=
  âŸ¨h.isBigO, h.isBigO_symmâŸ©

theorem IsEquivalent.isTheta_symm (h : u ~[l] v) : v =Î˜[l] u :=
  âŸ¨h.isBigO_symm, h.isBigOâŸ©

@[refl]
theorem IsEquivalent.refl : u ~[l] u := by
  rw [IsEquivalent, sub_self]
  -- âŠ¢ 0 =o[l] u
  exact isLittleO_zero _ _
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.refl Asymptotics.IsEquivalent.refl

@[symm]
theorem IsEquivalent.symm (h : u ~[l] v) : v ~[l] u :=
  (h.isLittleO.trans_isBigO h.isBigO_symm).symm
#align asymptotics.is_equivalent.symm Asymptotics.IsEquivalent.symm

@[trans]
theorem IsEquivalent.trans {l : Filter Î±} {u v w : Î± â†’ Î²} (huv : u ~[l] v) (hvw : v ~[l] w) :
    u ~[l] w :=
  (huv.isLittleO.trans_isBigO hvw.isBigO).triangle hvw.isLittleO
#align asymptotics.is_equivalent.trans Asymptotics.IsEquivalent.trans

theorem IsEquivalent.congr_left {u v w : Î± â†’ Î²} {l : Filter Î±} (huv : u ~[l] v) (huw : u =á¶ [l] w) :
    w ~[l] v :=
  huv.congr' (huw.sub (EventuallyEq.refl _ _)) (EventuallyEq.refl _ _)
#align asymptotics.is_equivalent.congr_left Asymptotics.IsEquivalent.congr_left

theorem IsEquivalent.congr_right {u v w : Î± â†’ Î²} {l : Filter Î±} (huv : u ~[l] v) (hvw : v =á¶ [l] w) :
    u ~[l] w :=
  (huv.symm.congr_left hvw).symm
#align asymptotics.is_equivalent.congr_right Asymptotics.IsEquivalent.congr_right

theorem isEquivalent_zero_iff_eventually_zero : u ~[l] 0 â†” u =á¶ [l] 0 := by
  rw [IsEquivalent, sub_zero]
  -- âŠ¢ u =o[l] 0 â†” u =á¶ [l] 0
  exact isLittleO_zero_right_iff
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent_zero_iff_eventually_zero Asymptotics.isEquivalent_zero_iff_eventually_zero

theorem isEquivalent_zero_iff_isBigO_zero : u ~[l] 0 â†” u =O[l] (0 : Î± â†’ Î²) := by
  refine' âŸ¨IsEquivalent.isBigO, fun h â†¦ _âŸ©
  -- âŠ¢ u ~[l] 0
  rw [isEquivalent_zero_iff_eventually_zero, eventuallyEq_iff_exists_mem]
  -- âŠ¢ âˆƒ s, s âˆˆ l âˆ§ Set.EqOn u 0 s
  exact âŸ¨{ x : Î± | u x = 0 }, isBigO_zero_right_iff.mp h, fun x hx â†¦ hxâŸ©
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align asymptotics.is_equivalent_zero_iff_is_O_zero Asymptotics.isEquivalent_zero_iff_isBigO_zero

theorem isEquivalent_const_iff_tendsto {c : Î²} (h : c â‰  0) :
    u ~[l] const _ c â†” Tendsto u l (ğ“ c) := by
  simp_rw [IsEquivalent, const, isLittleO_const_iff h]
  -- âŠ¢ Tendsto (u - fun x => c) l (ğ“ 0) â†” Tendsto u l (ğ“ c)
  constructor <;> intro h
  -- âŠ¢ Tendsto (u - fun x => c) l (ğ“ 0) â†’ Tendsto u l (ğ“ c)
                  -- âŠ¢ Tendsto u l (ğ“ c)
                  -- âŠ¢ Tendsto (u - fun x => c) l (ğ“ 0)
  Â· have := h.sub (tendsto_const_nhds (a := -c))
    -- âŠ¢ Tendsto u l (ğ“ c)
    simp only [Pi.sub_apply, sub_neg_eq_add, sub_add_cancel, zero_add] at this
    -- âŠ¢ Tendsto u l (ğ“ c)
    exact this
    -- ğŸ‰ no goals
  Â· have := h.sub (tendsto_const_nhds (a := c))
    -- âŠ¢ Tendsto (u - fun x => c) l (ğ“ 0)
    rwa [sub_self] at this
    -- ğŸ‰ no goals
#align asymptotics.is_equivalent_const_iff_tendsto Asymptotics.isEquivalent_const_iff_tendsto

theorem IsEquivalent.tendsto_const {c : Î²} (hu : u ~[l] const _ c) : Tendsto u l (ğ“ c) := by
  rcases em <| c = 0 with rfl | h
  -- âŠ¢ Tendsto u l (ğ“ 0)
  Â· exact (tendsto_congr' <| isEquivalent_zero_iff_eventually_zero.mp hu).mpr tendsto_const_nhds
    -- ğŸ‰ no goals
  Â· exact (isEquivalent_const_iff_tendsto h).mp hu
    -- ğŸ‰ no goals
#align asymptotics.is_equivalent.tendsto_const Asymptotics.IsEquivalent.tendsto_const

theorem IsEquivalent.tendsto_nhds {c : Î²} (huv : u ~[l] v) (hu : Tendsto u l (ğ“ c)) :
    Tendsto v l (ğ“ c) := by
  by_cases h : c = 0
  -- âŠ¢ Tendsto v l (ğ“ c)
  Â· subst c
    -- âŠ¢ Tendsto v l (ğ“ 0)
    rw [â† isLittleO_one_iff â„] at hu âŠ¢
    -- âŠ¢ v =o[l] fun _x => 1
    simpa using (huv.symm.isLittleO.trans hu).add hu
    -- ğŸ‰ no goals
  Â· rw [â† isEquivalent_const_iff_tendsto h] at hu âŠ¢
    -- âŠ¢ v ~[l] const Î± c
    exact huv.symm.trans hu
    -- ğŸ‰ no goals
#align asymptotics.is_equivalent.tendsto_nhds Asymptotics.IsEquivalent.tendsto_nhds

theorem IsEquivalent.tendsto_nhds_iff {c : Î²} (huv : u ~[l] v) :
    Tendsto u l (ğ“ c) â†” Tendsto v l (ğ“ c) :=
  âŸ¨huv.tendsto_nhds, huv.symm.tendsto_nhdsâŸ©
#align asymptotics.is_equivalent.tendsto_nhds_iff Asymptotics.IsEquivalent.tendsto_nhds_iff

theorem IsEquivalent.add_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u + w ~[l] v := by
  simpa only [IsEquivalent, add_sub_right_comm] using huv.add hwv
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.add_is_o Asymptotics.IsEquivalent.add_isLittleO

theorem IsEquivalent.sub_isLittleO (huv : u ~[l] v) (hwv : w =o[l] v) : u - w ~[l] v := by
  simpa only [sub_eq_add_neg] using huv.add_isLittleO hwv.neg_left
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.sub_is_o Asymptotics.IsEquivalent.sub_isLittleO

theorem IsLittleO.add_isEquivalent (hu : u =o[l] w) (hv : v ~[l] w) : u + v ~[l] w :=
  add_comm v u â–¸ hv.add_isLittleO hu
#align asymptotics.is_o.add_is_equivalent Asymptotics.IsLittleO.add_isEquivalent

theorem IsLittleO.isEquivalent (huv : (u - v) =o[l] v) : u ~[l] v := huv
#align asymptotics.is_o.is_equivalent Asymptotics.IsLittleO.isEquivalent

theorem IsEquivalent.neg (huv : u ~[l] v) : (fun x â†¦ -u x) ~[l] fun x â†¦ -v x := by
  rw [IsEquivalent]
  -- âŠ¢ ((fun x => -u x) - fun x => -v x) =o[l] fun x => -v x
  convert huv.isLittleO.neg_left.neg_right
  -- âŠ¢ ((fun x => -u x) - fun x => -v x) xâœ = -(u - v) xâœ
  simp [neg_add_eq_sub]
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.neg Asymptotics.IsEquivalent.neg

end NormedAddCommGroup

open Asymptotics

section NormedField

variable {Î± Î² : Type*} [NormedField Î²] {t u v w : Î± â†’ Î²} {l : Filter Î±}

theorem isEquivalent_iff_exists_eq_mul :
    u ~[l] v â†” âˆƒ (Ï† : Î± â†’ Î²) (_ : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v := by
  rw [IsEquivalent, isLittleO_iff_exists_eq_mul]
  -- âŠ¢ (âˆƒ Ï† _hÏ†, u - v =á¶ [l] Ï† * v) â†” âˆƒ Ï† x, u =á¶ [l] Ï† * v
  constructor <;> rintro âŸ¨Ï†, hÏ†, hâŸ© <;> [refine' âŸ¨Ï† + 1, _, _âŸ©; refine' âŸ¨Ï† - 1, _, _âŸ©]
  Â· conv in ğ“ _ => rw [â† zero_add (1 : Î²)]
    -- âŠ¢ Tendsto (Ï† + 1) l (ğ“ (0 + 1))
    exact hÏ†.add tendsto_const_nhds
    -- ğŸ‰ no goals
  Â· convert h.add (EventuallyEq.refl l v) <;> simp [add_mul]
    -- âŠ¢ u xâœ = (u - v) xâœ + v xâœ
                                              -- ğŸ‰ no goals
                                              -- ğŸ‰ no goals
  Â· conv in ğ“ _ => rw [â† sub_self (1 : Î²)]
    -- âŠ¢ Tendsto (Ï† - 1) l (ğ“ (1 - 1))
    exact hÏ†.sub tendsto_const_nhds
    -- ğŸ‰ no goals
  Â· convert h.sub (EventuallyEq.refl l v); simp [sub_mul]
    -- âŠ¢ ((Ï† - 1) * v) xâœ = (Ï† * v) xâœ - v xâœ
                                           -- ğŸ‰ no goals
#align asymptotics.is_equivalent_iff_exists_eq_mul Asymptotics.isEquivalent_iff_exists_eq_mul

theorem IsEquivalent.exists_eq_mul (huv : u ~[l] v) :
    âˆƒ (Ï† : Î± â†’ Î²) (_ : Tendsto Ï† l (ğ“ 1)), u =á¶ [l] Ï† * v :=
  isEquivalent_iff_exists_eq_mul.mp huv
#align asymptotics.is_equivalent.exists_eq_mul Asymptotics.IsEquivalent.exists_eq_mul

theorem isEquivalent_of_tendsto_one (hz : âˆ€á¶  x in l, v x = 0 â†’ u x = 0)
    (huv : Tendsto (u / v) l (ğ“ 1)) : u ~[l] v := by
  rw [isEquivalent_iff_exists_eq_mul]
  -- âŠ¢ âˆƒ Ï† x, u =á¶ [l] Ï† * v
  refine' âŸ¨u / v, huv, hz.mono fun x hz' â†¦ (div_mul_cancel_of_imp hz').symmâŸ©
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent_of_tendsto_one Asymptotics.isEquivalent_of_tendsto_one

theorem isEquivalent_of_tendsto_one' (hz : âˆ€ x, v x = 0 â†’ u x = 0) (huv : Tendsto (u / v) l (ğ“ 1)) :
    u ~[l] v :=
  isEquivalent_of_tendsto_one (eventually_of_forall hz) huv
#align asymptotics.is_equivalent_of_tendsto_one' Asymptotics.isEquivalent_of_tendsto_one'

theorem isEquivalent_iff_tendsto_one (hz : âˆ€á¶  x in l, v x â‰  0) :
    u ~[l] v â†” Tendsto (u / v) l (ğ“ 1) := by
  constructor
  -- âŠ¢ u ~[l] v â†’ Tendsto (u / v) l (ğ“ 1)
  Â· intro hequiv
    -- âŠ¢ Tendsto (u / v) l (ğ“ 1)
    have := hequiv.isLittleO.tendsto_div_nhds_zero
    -- âŠ¢ Tendsto (u / v) l (ğ“ 1)
    simp only [Pi.sub_apply, sub_div] at this
    -- âŠ¢ Tendsto (u / v) l (ğ“ 1)
    have key : Tendsto (fun x â†¦ v x / v x) l (ğ“ 1) :=
      (tendsto_congr' <| hz.mono fun x hnz â†¦ @div_self _ _ (v x) hnz).mpr tendsto_const_nhds
    convert this.add key
    -- âŠ¢ (u / v) xâœ = u xâœ / v xâœ - v xâœ / v xâœ + v xâœ / v xâœ
    Â· simp
      -- ğŸ‰ no goals
    Â· norm_num
      -- ğŸ‰ no goals
  Â· exact isEquivalent_of_tendsto_one (hz.mono fun x hnvz hz â†¦ (hnvz hz).elim)
    -- ğŸ‰ no goals
#align asymptotics.is_equivalent_iff_tendsto_one Asymptotics.isEquivalent_iff_tendsto_one

end NormedField

section Smul

theorem IsEquivalent.smul {Î± E ğ•œ : Type*} [NormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {a b : Î± â†’ ğ•œ} {u v : Î± â†’ E} {l : Filter Î±} (hab : a ~[l] b) (huv : u ~[l] v) :
    (fun x â†¦ a x â€¢ u x) ~[l] fun x â†¦ b x â€¢ v x := by
  rcases hab.exists_eq_mul with âŸ¨Ï†, hÏ†, habÏ†âŸ©
  -- âŠ¢ (fun x => a x â€¢ u x) ~[l] fun x => b x â€¢ v x
  have : ((fun x â†¦ a x â€¢ u x) - (fun x â†¦ b x â€¢ v x)) =á¶ [l] fun x â†¦ b x â€¢ (Ï† x â€¢ u x - v x) := by
    -- Porting note: `convert` has become too strong, so we need to specify `using 1`.
    convert (habÏ†.compâ‚‚ (Â· â€¢ Â·) <| EventuallyEq.refl _ u).sub
      (EventuallyEq.refl _ fun x â†¦ b x â€¢ v x) using 1
    ext
    rw [Pi.mul_apply, mul_comm, mul_smul, â† smul_sub]
  refine' (isLittleO_congr this.symm <| EventuallyEq.rfl).mp ((isBigO_refl b l).smul_isLittleO _)
  -- âŠ¢ (fun x => Ï† x â€¢ u x - v x) =o[l] fun x => v x
  rcases huv.isBigO.exists_pos with âŸ¨C, hC, hCuvâŸ©
  -- âŠ¢ (fun x => Ï† x â€¢ u x - v x) =o[l] fun x => v x
  rw [IsEquivalent] at *
  -- âŠ¢ (fun x => Ï† x â€¢ u x - v x) =o[l] fun x => v x
  rw [isLittleO_iff] at *
  -- âŠ¢ âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  rw [IsBigOWith] at hCuv
  -- âŠ¢ âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  simp only [Metric.tendsto_nhds, dist_eq_norm] at hÏ†
  -- âŠ¢ âˆ€ â¦ƒc : â„â¦„, 0 < c â†’ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  intro c hc
  -- âŠ¢ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  specialize hÏ† (c / 2 / C) (div_pos (div_pos hc zero_lt_two) hC)
  -- âŠ¢ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  specialize huv (div_pos hc zero_lt_two)
  -- âŠ¢ âˆ€á¶  (x : Î±) in l, â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  refine' hÏ†.mp (huv.mp <| hCuv.mono fun x hCuvx huvx hÏ†x â†¦ _)
  -- âŠ¢ â€–Ï† x â€¢ u x - v xâ€– â‰¤ c * â€–v xâ€–
  have key :=
    calc
      â€–Ï† x - 1â€– * â€–u xâ€– â‰¤ c / 2 / C * â€–u xâ€– := by gcongr
      _ â‰¤ c / 2 / C * (C * â€–v xâ€–) := by gcongr
      _ = c / 2 * â€–v xâ€– := by
        field_simp [hC.ne.symm]
        ring
  calc
    â€–((fun x : Î± â†¦ Ï† x â€¢ u x) - v) xâ€– = â€–(Ï† x - 1) â€¢ u x + (u x - v x)â€– := by
      simp [sub_smul, sub_add]
    _ â‰¤ â€–(Ï† x - 1) â€¢ u xâ€– + â€–u x - v xâ€– := (norm_add_le _ _)
    _ = â€–Ï† x - 1â€– * â€–u xâ€– + â€–u x - v xâ€– := by rw [norm_smul]
    _ â‰¤ c / 2 * â€–v xâ€– + â€–u x - v xâ€– := by gcongr
    _ â‰¤ c / 2 * â€–v xâ€– + c / 2 * â€–v xâ€– := by gcongr; exact huvx
    _ = c * â€–v xâ€– := by ring
#align asymptotics.is_equivalent.smul Asymptotics.IsEquivalent.smul

end Smul

section mul_inv

variable {Î± Î² : Type*} [NormedField Î²] {t u v w : Î± â†’ Î²} {l : Filter Î±}

theorem IsEquivalent.mul (htu : t ~[l] u) (hvw : v ~[l] w) : t * v ~[l] u * w :=
  htu.smul hvw
#align asymptotics.is_equivalent.mul Asymptotics.IsEquivalent.mul

theorem IsEquivalent.inv (huv : u ~[l] v) : (fun x â†¦ (u x)â»Â¹) ~[l] fun x â†¦ (v x)â»Â¹ := by
  rw [isEquivalent_iff_exists_eq_mul] at *
  -- âŠ¢ âˆƒ Ï† x, (fun x => (u x)â»Â¹) =á¶ [l] Ï† * fun x => (v x)â»Â¹
  rcases huv with âŸ¨Ï†, hÏ†, hâŸ©
  -- âŠ¢ âˆƒ Ï† x, (fun x => (u x)â»Â¹) =á¶ [l] Ï† * fun x => (v x)â»Â¹
  rw [â† inv_one]
  -- âŠ¢ âˆƒ Ï† x, (fun x => (u x)â»Â¹) =á¶ [l] Ï† * fun x => (v x)â»Â¹
  refine' âŸ¨fun x â†¦ (Ï† x)â»Â¹, Tendsto.invâ‚€ hÏ† (by norm_num), _âŸ©
  -- âŠ¢ (fun x => (u x)â»Â¹) =á¶ [l] (fun x => (Ï† x)â»Â¹) * fun x => (v x)â»Â¹
  convert h.inv
  -- âŠ¢ ((fun x => (Ï† x)â»Â¹) * fun x => (v x)â»Â¹) xâœ = ((Ï† * v) xâœ)â»Â¹
  simp [mul_comm]
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.inv Asymptotics.IsEquivalent.inv

theorem IsEquivalent.div (htu : t ~[l] u) (hvw : v ~[l] w) :
    (fun x â†¦ t x / v x) ~[l] fun x â†¦ u x / w x := by
  simpa only [div_eq_mul_inv] using htu.mul hvw.inv
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.div Asymptotics.IsEquivalent.div

end mul_inv

section NormedLinearOrderedField

variable {Î± Î² : Type*} [NormedLinearOrderedField Î²] {u v : Î± â†’ Î²} {l : Filter Î±}

theorem IsEquivalent.tendsto_atTop [OrderTopology Î²] (huv : u ~[l] v) (hu : Tendsto u l atTop) :
    Tendsto v l atTop :=
  let âŸ¨Ï†, hÏ†, hâŸ© := huv.symm.exists_eq_mul
  Tendsto.congr' h.symm (mul_comm u Ï† â–¸ hu.atTop_mul zero_lt_one hÏ†)
#align asymptotics.is_equivalent.tendsto_at_top Asymptotics.IsEquivalent.tendsto_atTop

theorem IsEquivalent.tendsto_atTop_iff [OrderTopology Î²] (huv : u ~[l] v) :
    Tendsto u l atTop â†” Tendsto v l atTop :=
  âŸ¨huv.tendsto_atTop, huv.symm.tendsto_atTopâŸ©
#align asymptotics.is_equivalent.tendsto_at_top_iff Asymptotics.IsEquivalent.tendsto_atTop_iff

theorem IsEquivalent.tendsto_atBot [OrderTopology Î²] (huv : u ~[l] v) (hu : Tendsto u l atBot) :
    Tendsto v l atBot := by
  convert tendsto_neg_atTop_atBot.comp (huv.neg.tendsto_atTop <| tendsto_neg_atBot_atTop.comp hu)
  -- âŠ¢ v = Neg.neg âˆ˜ fun x => -v x
  ext
  -- âŠ¢ v xâœ = (Neg.neg âˆ˜ fun x => -v x) xâœ
  simp
  -- ğŸ‰ no goals
#align asymptotics.is_equivalent.tendsto_at_bot Asymptotics.IsEquivalent.tendsto_atBot

theorem IsEquivalent.tendsto_atBot_iff [OrderTopology Î²] (huv : u ~[l] v) :
    Tendsto u l atBot â†” Tendsto v l atBot :=
  âŸ¨huv.tendsto_atBot, huv.symm.tendsto_atBotâŸ©
#align asymptotics.is_equivalent.tendsto_at_bot_iff Asymptotics.IsEquivalent.tendsto_atBot_iff

end NormedLinearOrderedField

end Asymptotics

open Filter Asymptotics

open Asymptotics

variable {Î± Î² Î²â‚‚ : Type*} [NormedAddCommGroup Î²] [Norm Î²â‚‚] {l : Filter Î±}

theorem Filter.EventuallyEq.isEquivalent {u v : Î± â†’ Î²} (h : u =á¶ [l] v) : u ~[l] v :=
  IsEquivalent.congr_right (isLittleO_refl_left _ _) h
#align filter.eventually_eq.is_equivalent Filter.EventuallyEq.isEquivalent

@[trans]
theorem Filter.EventuallyEq.trans_isEquivalent {f gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =á¶ [l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f ~[l] gâ‚‚ :=
  h.isEquivalent.trans hâ‚‚

namespace Asymptotics

instance transIsEquivalentIsEquivalent :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (IsEquivalent l) (IsEquivalent l) (IsEquivalent l) where
  trans := IsEquivalent.trans

instance transEventuallyEqIsEquivalent :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (EventuallyEq l) (IsEquivalent l) (IsEquivalent l) where
  trans := EventuallyEq.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_eventuallyEq {f gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =á¶ [l] gâ‚‚) : f ~[l] gâ‚‚ :=
  h.trans hâ‚‚.isEquivalent

instance transIsEquivalentEventuallyEq :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²) (IsEquivalent l) (EventuallyEq l) (IsEquivalent l) where
  trans := IsEquivalent.trans_eventuallyEq

@[trans]
theorem IsEquivalent.trans_isBigO {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚) (hâ‚‚ : gâ‚ =O[l] gâ‚‚) :
    f =O[l] gâ‚‚ :=
  IsBigO.trans h.isBigO hâ‚‚

instance transIsEquivalentIsBigO :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsBigO l) (IsBigO l) where
  trans := IsEquivalent.trans_isBigO

@[trans]
theorem IsBigO.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =O[l] gâ‚) (hâ‚‚ : gâ‚ ~[l] gâ‚‚) :
    f =O[l] gâ‚‚ :=
  IsBigO.trans h hâ‚‚.isBigO

instance transIsBigOIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsBigO l) (IsEquivalent l) (IsBigO l) where
  trans := IsBigO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isLittleO {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =o[l] gâ‚‚) : f =o[l] gâ‚‚ :=
  IsBigO.trans_isLittleO h.isBigO hâ‚‚

instance transIsEquivalentIsLittleO :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsLittleO l) (IsLittleO l) where
  trans := IsEquivalent.trans_isLittleO

@[trans]
theorem IsLittleO.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =o[l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f =o[l] gâ‚‚ :=
  IsLittleO.trans_isBigO h hâ‚‚.isBigO

instance transIsLittleOIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsLittleO l) (IsEquivalent l) (IsLittleO l) where
  trans := IsLittleO.trans_isEquivalent

@[trans]
theorem IsEquivalent.trans_isTheta {f gâ‚ : Î± â†’ Î²} {gâ‚‚ : Î± â†’ Î²â‚‚} (h : f ~[l] gâ‚)
    (hâ‚‚ : gâ‚ =Î˜[l] gâ‚‚) : f =Î˜[l] gâ‚‚ :=
  IsTheta.trans h.isTheta hâ‚‚

instance transIsEquivalentIsTheta :
    @Trans (Î± â†’ Î²) (Î± â†’ Î²) (Î± â†’ Î²â‚‚) (IsEquivalent l) (IsTheta l) (IsTheta l) where
  trans := IsEquivalent.trans_isTheta

@[trans]
theorem IsTheta.trans_isEquivalent {f : Î± â†’ Î²â‚‚} {gâ‚ gâ‚‚ : Î± â†’ Î²} (h : f =Î˜[l] gâ‚)
    (hâ‚‚ : gâ‚ ~[l] gâ‚‚) : f =Î˜[l] gâ‚‚ :=
  IsTheta.trans h hâ‚‚.isTheta

instance transIsThetaIsEquivalent :
    @Trans (Î± â†’ Î²â‚‚) (Î± â†’ Î²) (Î± â†’ Î²) (IsTheta l) (IsEquivalent l) (IsTheta l) where
  trans := IsTheta.trans_isEquivalent

end Asymptotics
