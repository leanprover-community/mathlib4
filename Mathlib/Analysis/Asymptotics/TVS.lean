/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Eric Wieser
-/
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.Analysis.Convex.EGauge
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Seminorm
import Mathlib.Tactic.Peel
import Mathlib.Topology.Instances.ENNReal

/-!
# Asymptotics in a Topological Vector Space

This file defines `Asymptotics.IsLittleOTVS` as a generalization of `Asymptotics.IsLittleO` from
normed spaces to topological spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

In a normed space, we can use balls of positive radius as both `U` and `V`,
thus reducing the definition to the classical one.

This frees the user from having to chose a canonical norm, at the expense of having to pick a
specific base ring.

This definition was added to the library in order to migrate FrÃ©chet derivatives
from normed vector spaces to topological vector spaces.
The definition is motivated by
https://en.wikipedia.org/wiki/Fr%C3%A9chet_derivative#Generalization_to_topological_vector_spaces
but the definition there doesn't work for topological vector spaces over general normed fields.
[This Zulip discussion](https://leanprover.zulipchat.com/#narrow/channel/116395-maths/topic/generalizing.20deriv.20to.20TVS)
led to the current choice of the definition.

It may be possible to generalize $f = O(g)$ and $f = \Theta(g)$ in a similar way,
but we don't need these definitions to redefine FrÃ©chet derivatives,
so formalization of these generalizations is left for later,
until someone will need it (e.g., to prove properties of the FrÃ©chet derivative over TVS).

## Main results

* `isLittleOTVS_iff_isLittleO`: the equivalence between these two definitions in the case of a
  normed space.

* `isLittleOTVS_iff_tendsto_inv_smul`: the equivalence to convergence of the ratio to zero
  in case of a topological vector space.

## TODO

Use this to redefine `HasFDerivAtFilter`, as the base ring is already chosen there, and this removes
the choice of norm being part of the statement.
-/

open Set Filter Asymptotics Metric
open scoped Topology Pointwise ENNReal NNReal

namespace Asymptotics

/-- `IsLittleOTVS ğ•œ f g l` is a generalization of `f =o[l] g` (`IsLittleO f g l`)
that works in topological `ğ•œ`-vector spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

We use an `ENNReal`-valued function `egauge` for the gauge,
so we unfold the definition of little o instead of reusing it. -/
def IsLittleOTVS (ğ•œ : Type*) {Î± E F : Type*}
    [NNNorm ğ•œ] [TopologicalSpace E] [TopologicalSpace F] [Zero E] [Zero F] [SMul ğ•œ E] [SMul ğ•œ F]
    (f : Î± â†’ E) (g : Î± â†’ F) (l : Filter Î±) : Prop :=
  âˆ€ U âˆˆ ğ“ (0 : E), âˆƒ V âˆˆ ğ“ (0 : F), âˆ€ Îµ â‰  (0 : â„â‰¥0),
    âˆ€á¶  x in l, egauge ğ•œ U (f x) â‰¤ Îµ * egauge ğ•œ V (g x)

variable {Î± Î² ğ•œ E F : Type*}

section TopologicalSpace

variable [NontriviallyNormedField ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F]

theorem _root_.Filter.HasBasis.isLittleOTVS_iff {Î¹E Î¹F : Sort*} {pE : Î¹E â†’ Prop} {pF : Î¹F â†’ Prop}
    {sE : Î¹E â†’ Set E} {sF : Î¹F â†’ Set F} (hE : HasBasis (ğ“ (0 : E)) pE sE)
    (hF : HasBasis (ğ“ (0 : F)) pF sF) {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±} :
    IsLittleOTVS ğ•œ f g l â†” âˆ€ i, pE i â†’ âˆƒ j, pF j âˆ§ âˆ€ Îµ â‰  (0 : â„â‰¥0),
      âˆ€á¶  x in l, egauge ğ•œ (sE i) (f x) â‰¤ Îµ * egauge ğ•œ (sF j) (g x) := by
  refine (hE.forall_iff ?_).trans <| forallâ‚‚_congr fun _ _ â†¦ hF.exists_iff ?_
  Â· rintro s t hsub âŸ¨V, hVâ‚€, hVâŸ©
    exact âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).mono fun x â†¦ le_trans <| egauge_anti _ hsub _âŸ©
  Â· refine fun s t hsub h Îµ hÎµ â†¦ (h Îµ hÎµ).mono fun x hx â†¦ hx.trans ?_
    gcongr

@[simp]
theorem isLittleOTVS_map {f : Î± â†’ E} {g : Î± â†’ F} {k : Î² â†’ Î±} {l : Filter Î²} :
    IsLittleOTVS ğ•œ f g (map k l) â†” IsLittleOTVS ğ•œ (f âˆ˜ k) (g âˆ˜ k) l := by
  simp [IsLittleOTVS]

protected lemma IsLittleOTVS.smul_left {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (h : IsLittleOTVS ğ•œ f g l) (c : Î± â†’ ğ•œ) :
    IsLittleOTVS ğ•œ (fun x â†¦ c x â€¢ f x) (fun x â†¦ c x â€¢ g x) l := by
  unfold IsLittleOTVS at *
  peel h with U hU V hV Îµ hÎµ x hx
  rw [egauge_smul_right, egauge_smul_right, mul_left_comm]
  Â· gcongr
  all_goals exact fun _ â†¦ Filter.nonempty_of_mem â€¹_â€º

lemma isLittleOTVS_one [ContinuousSMul ğ•œ E] {f : Î± â†’ E} {l : Filter Î±} :
    IsLittleOTVS ğ•œ f (1 : Î± â†’ ğ•œ) l â†” Tendsto f l (ğ“ 0) := by
  constructor
  Â· intro hf
    rw [(basis_sets _).isLittleOTVS_iff nhds_basis_ball] at hf
    rw [(nhds_basis_balanced ğ•œ E).tendsto_right_iff]
    rintro U âŸ¨hU, hUbâŸ©
    rcases hf U hU with âŸ¨r, hrâ‚€, hrâŸ©
    lift r to â„â‰¥0 using hrâ‚€.le
    norm_cast at hrâ‚€
    rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
    obtain âŸ¨Îµ, hÎµâ‚€, hÎµâŸ© : âˆƒ Îµ : â„â‰¥0, 0 < Îµ âˆ§ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) < 1 := by
      apply Eventually.exists_gt
      refine Continuous.tendsto' ?_ _ _ (by simp) |>.eventually_lt_const zero_lt_one
      fun_prop (disch := intros; first | apply ENNReal.coe_ne_top | positivity)
    filter_upwards [hr Îµ hÎµâ‚€.ne'] with x hx
    refine mem_of_egauge_lt_one hUb (hx.trans_lt ?_)
    calc
      (Îµ : â„â‰¥0âˆ) * egauge ğ•œ (ball (0 : ğ•œ) r) 1 â‰¤ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) := by
        rw [mul_div_assoc]
        gcongr
        simpa using egauge_ball_le_of_one_lt_norm (r := r) (x := (1 : ğ•œ)) hc (by simp)
      _ < 1 := â€¹_â€º
  Â· intro hf U hU
    refine âŸ¨ball 0 1, ball_mem_nhds _ one_pos, fun Îµ hÎµ â†¦ ?_âŸ©
    rcases NormedField.exists_norm_lt ğ•œ hÎµ.bot_lt with âŸ¨c, hcâ‚€, hcÎµâŸ©
    replace hcâ‚€ : c â‰  0 := by simpa using hcâ‚€
    filter_upwards [hf ((set_smul_mem_nhds_zero_iff hcâ‚€).2 hU)] with a ha
    calc
      egauge ğ•œ U (f a) â‰¤ â€–câ€–â‚Š := egauge_le_of_mem_smul ha
      _ â‰¤ Îµ := mod_cast hcÎµ.le
      _ â‰¤ Îµ * egauge ğ•œ (ball (0 : ğ•œ) 1) 1 := by
        apply le_mul_of_one_le_right'
        simpa using le_egauge_ball_one ğ•œ (1 : ğ•œ)

lemma IsLittleOTVS.tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (h : IsLittleOTVS ğ•œ g f l) : Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  rw [(basis_sets _).isLittleOTVS_iff nhds_basis_ball] at h
  rw [(nhds_basis_balanced ğ•œ E).tendsto_right_iff]
  rintro U âŸ¨hU, hUBâŸ©
  rcases h U hU with âŸ¨Îµ, hÎµâ‚€, hÎµâŸ©
  lift Îµ to â„â‰¥0 using hÎµâ‚€.le; norm_cast at hÎµâ‚€
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  filter_upwards [hÎµ (Îµ / 2 / â€–câ€–â‚Š) (ne_of_gt <| div_pos (half_pos hÎµâ‚€) (one_pos.trans hc))]
    with x hx
  refine mem_of_egauge_lt_one hUB ?_
  rw [id, egauge_smul_right (fun _ â†¦ Filter.nonempty_of_mem hU), nnnorm_inv]
  calc
    â†‘â€–f xâ€–â‚Šâ»Â¹ * egauge ğ•œ U (g x)
      â‰¤ (â†‘â€–f xâ€–â‚Š)â»Â¹ * (â†‘(Îµ / 2 / â€–câ€–â‚Š) * egauge ğ•œ (ball 0 Îµ) (f x)) :=
      mul_le_mul' ENNReal.coe_inv_le hx
    _ â‰¤ (â†‘â€–f xâ€–â‚Š)â»Â¹ * ((Îµ / 2 / â€–câ€–â‚Š) * (â€–câ€–â‚Š * â€–f xâ€–â‚Š / Îµ)) := by
      gcongr
      Â· refine ENNReal.coe_div_le.trans ?_; gcongr; apply ENNReal.coe_div_le
      Â· exact egauge_ball_le_of_one_lt_norm hc (.inl hÎµâ‚€.ne')
    _ = (â€–f xâ€–â‚Š / â€–f xâ€–â‚Š) * (Îµ / Îµ) * (â€–câ€–â‚Š / â€–câ€–â‚Š) * (1 / 2) := by
      simp only [div_eq_mul_inv, one_mul]; ring
    _ â‰¤ 1 * 1 * 1 * (1 / 2) := by gcongr <;> apply ENNReal.div_self_le_one
    _ < 1 := by norm_num

lemma isLittleOTVS_iff_tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (hâ‚€ : âˆ€á¶  x in l, f x = 0 â†’ g x = 0) :
    IsLittleOTVS ğ•œ g f l â†” Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  refine âŸ¨IsLittleOTVS.tendsto_inv_smul, fun h U hU â†¦ ?_âŸ©
  refine âŸ¨ball 0 1, ball_mem_nhds _ one_pos, fun Îµ hÎµ â†¦ ?_âŸ©
  rcases NormedField.exists_norm_lt ğ•œ hÎµ.bot_lt with âŸ¨c, hcâ‚€, hcÎµ : â€–câ€–â‚Š < ÎµâŸ©
  rw [norm_pos_iff] at hcâ‚€
  filter_upwards [hâ‚€, h <| (set_smul_mem_nhds_zero_iff hcâ‚€).2 hU]
    with x hxâ‚€ (hx : (f x)â»Â¹ â€¢ g x âˆˆ c â€¢ U)
  rcases eq_or_ne (f x) 0 with hfâ‚€ | hfâ‚€
  Â· simp [hxâ‚€ hfâ‚€, Filter.nonempty_of_mem hU]
  Â· rw [mem_smul_set_iff_inv_smul_memâ‚€ hcâ‚€, smul_smul] at hx
    refine (egauge_le_of_smul_mem_of_ne hx (by simp [*])).trans ?_
    simp_rw [nnnorm_mul, nnnorm_inv, mul_inv, inv_inv, ENNReal.coe_mul]
    gcongr
    apply le_egauge_ball_one

end TopologicalSpace

section NormedSpace

variable [NontriviallyNormedField ğ•œ]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

lemma isLittleOTVS_iff_isLittleO {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±} :
    IsLittleOTVS ğ•œ f g l â†” f =o[l] g := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hc : 1 < â€–câ€–â‚ŠâŸ©
  have hcâ‚€ : 0 < â€–câ€–â‚Š := one_pos.trans hc
  simp only [isLittleO_iff, nhds_basis_ball.isLittleOTVS_iff nhds_basis_ball]
  refine âŸ¨fun h Îµ hÎµ â†¦ ?_, fun h Îµ hÎµ â†¦ âŸ¨1, one_pos, fun Î´ hÎ´ â†¦ ?_âŸ©âŸ©
  Â· rcases h Îµ hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    lift Îµ to â„â‰¥0 using hÎµ.le; lift Î´ to â„â‰¥0 using hÎ´â‚€.le; norm_cast at hÎµ hÎ´â‚€
    filter_upwards [hÎ´ (Î´ / â€–câ€–â‚Š) (div_pos hÎ´â‚€ hcâ‚€).ne'] with x hx
    suffices (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ â€–g xâ€–â‚Š by
      rw [â† ENNReal.coe_div hÎµ.ne'] at this
      rw [â† div_le_iffâ‚€' (NNReal.coe_pos.2 hÎµ)]
      exact_mod_cast this
    calc
      (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ egauge ğ•œ (ball 0 Îµ) (f x) := div_le_egauge_ball ğ•œ _ _
      _ â‰¤ â†‘(Î´ / â€–câ€–â‚Š) * egauge ğ•œ (ball 0 â†‘Î´) (g x) := hx
      _ â‰¤ (Î´ / â€–câ€–â‚Š) * (â€–câ€–â‚Š * â€–g xâ€–â‚Š / Î´) := by
        gcongr
        exacts [ENNReal.coe_div_le, egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎ´â‚€)]
      _ = (Î´ / Î´) * (â€–câ€–â‚Š / â€–câ€–â‚Š) * â€–g xâ€–â‚Š := by simp only [div_eq_mul_inv]; ring
      _ â‰¤ 1 * 1 * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = â€–g xâ€–â‚Š := by simp
  Â· filter_upwards [@h â†‘(Îµ * Î´ / â€–câ€–â‚Š) (by positivity)] with x (hx : â€–f xâ€–â‚Š â‰¤ Îµ * Î´ / â€–câ€–â‚Š * â€–g xâ€–â‚Š)
    lift Îµ to â„â‰¥0 using hÎµ.le
    calc
      egauge ğ•œ (ball 0 Îµ) (f x) â‰¤ â€–câ€–â‚Š * â€–f xâ€–â‚Š / Îµ :=
        egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎµ)
      _ â‰¤ â€–câ€–â‚Š * (â†‘(Îµ * Î´ / â€–câ€–â‚Š) * â€–g xâ€–â‚Š) / Îµ := by gcongr; exact_mod_cast hx
      _ = (â€–câ€–â‚Š / â€–câ€–â‚Š) * (Îµ / Îµ) * Î´ * â€–g xâ€–â‚Š := by
        simp only [div_eq_mul_inv, ENNReal.coe_inv hcâ‚€.ne', ENNReal.coe_mul]; ring
      _ â‰¤ 1 * 1 * Î´ * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = Î´ * â€–g xâ€–â‚Š := by simp
      _ â‰¤ Î´ * egauge ğ•œ (ball 0 1) (g x) := by gcongr; apply le_egauge_ball_one

alias âŸ¨isLittleOTVS.isLittleO, IsLittle.isLittleOTVSâŸ© := isLittleOTVS_iff_isLittleO

end NormedSpace

end Asymptotics
