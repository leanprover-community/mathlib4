/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Eric Wieser
-/
import Mathlib.Analysis.Convex.EGauge
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Seminorm
import Mathlib.Tactic.Peel
import Mathlib.Topology.Instances.ENNReal.Lemmas
import Mathlib.Analysis.Asymptotics.Defs

/-!
# Asymptotics in a Topological Vector Space

This file defines `Asymptotics.IsLittleOTVS` as a generalization of `Asymptotics.IsLittleO` from
normed spaces to topological spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

In a normed space, we can use balls of positive radius as both `U` and `V`,
thus reducing the definition to the classical one.

This frees the user from having to chose a canonical norm, at the expense of having to pick a
specific base ring.
This is exactly the tradeoff we want in `HasFDerivAtFilter`,
as there the base ring is already chosen,
and this removes the choice of norm being part of the statement.

This definition was added to the library in order to migrate FrÃ©chet derivatives
from normed vector spaces to topological vector spaces.
The definition is motivated by
https://en.wikipedia.org/wiki/Fr%C3%A9chet_derivative#Generalization_to_topological_vector_spaces
but the definition there doesn't work for topological vector spaces over general normed fields.
[This Zulip discussion](https://leanprover.zulipchat.com/#narrow/channel/116395-maths/topic/generalizing.20deriv.20to.20TVS)
led to the current choice of the definition.

It may be possible to generalize $f = O(g)$ and $f = \Theta(g)$ in a similar way,
but we don't need these definitions to redefine FrÃ©chet derivatives,
so formalization of these generalizations is left for later,
until someone will need it (e.g., to prove properties of the FrÃ©chet derivative over TVS).

## Main results

* `isLittleOTVS_iff_isLittleO`: the equivalence between these two definitions in the case of a
  normed space.

* `isLittleOTVS_iff_tendsto_inv_smul`: the equivalence to convergence of the ratio to zero
  in case of a topological vector space.

-/

open Set Filter Asymptotics Metric
open scoped Topology Pointwise ENNReal NNReal

namespace Asymptotics

/-- `f =o[ğ•œ;l] g` (`IsLittleOTVS ğ•œ l f g`) is a generalization of `f =o[l] g` (`IsLittleO l f g`)
that works in topological `ğ•œ`-vector spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

We use an `ENNReal`-valued function `egauge` for the gauge,
so we unfold the definition of little o instead of reusing it. -/
def IsLittleOTVS (ğ•œ : Type*) {Î± E F : Type*}
    [NNNorm ğ•œ] [TopologicalSpace E] [TopologicalSpace F] [Zero E] [Zero F] [SMul ğ•œ E] [SMul ğ•œ F]
    (l : Filter Î±) (f : Î± â†’ E) (g : Î± â†’ F) : Prop :=
  âˆ€ U âˆˆ ğ“ (0 : E), âˆƒ V âˆˆ ğ“ (0 : F), âˆ€ Îµ â‰  (0 : â„â‰¥0),
    âˆ€á¶  x in l, egauge ğ•œ U (f x) â‰¤ Îµ * egauge ğ•œ V (g x)

@[inherit_doc]
notation:100 f " =o[" ğ•œ ";" l "] " g:100 => IsLittleOTVS ğ•œ l f g

variable {Î± Î² ğ•œ E F G : Type*}

section TopologicalSpace

variable [NontriviallyNormedField ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F]
  [AddCommGroup G] [TopologicalSpace G] [Module ğ•œ G]

section congr

variable {f fâ‚ fâ‚‚ : Î± â†’ E} {g gâ‚ gâ‚‚ : Î± â†’ F} {l : Filter Î±}

theorem isLittleOTVS_congr (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
    fâ‚ =o[ğ•œ;l] gâ‚ â†” fâ‚‚ =o[ğ•œ;l] gâ‚‚ := by
  simp only [IsLittleOTVS]
  refine forallâ‚‚_congr fun U hU => exists_congr fun V => and_congr_right fun hV =>
    forallâ‚‚_congr fun Îµ hÎµ => Filter.eventually_congr ?_
  filter_upwards [hf, hg] with _ eâ‚ eâ‚‚
  rw [eâ‚, eâ‚‚]

/-- A stronger version of `IsLittleOTVS.congr` that requires the functions only agree along the
filter. -/
theorem IsLittleOTVS.congr' (h : fâ‚ =o[ğ•œ;l] gâ‚) (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
    fâ‚‚ =o[ğ•œ;l] gâ‚‚ :=
  (isLittleOTVS_congr hf hg).mp h

theorem IsLittleOTVS.congr (h : fâ‚ =o[ğ•œ;l] gâ‚) (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
    fâ‚‚ =o[ğ•œ;l] gâ‚‚ :=
  h.congr' (univ_mem' hf) (univ_mem' hg)

theorem IsLittleOTVS.congr_left (h : fâ‚ =o[ğ•œ;l] g) (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) : fâ‚‚ =o[ğ•œ;l] g :=
  h.congr hf fun _ => rfl

theorem IsLittleOTVS.congr_right (h : f =o[ğ•œ;l] gâ‚) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) : f =o[ğ•œ;l] gâ‚‚ :=
  h.congr (fun _ => rfl) hg

end congr

variable {l lâ‚ lâ‚‚ : Filter Î±} {f : Î± â†’ E} {g : Î± â†’ F}

@[trans]
theorem IsLittleOTVS.trans {k : Î± â†’ G} (hfg : f =o[ğ•œ;l] g) (hgk : g =o[ğ•œ;l] k) : f =o[ğ•œ;l] k := by
  intros U hU
  obtain âŸ¨V, hV0, hVâŸ© := hfg U hU
  obtain âŸ¨W, hW0, hWâŸ© := hgk V hV0
  refine âŸ¨W, hW0, fun Îµ hÎµ => ?_âŸ©
  filter_upwards [hV Îµ hÎµ, hW 1 one_ne_zero] with a hfga hgka
  refine hfga.trans ?_
  gcongr
  simpa using hgka

instance transIsLittleOTVSIsLittleOTVS :
    @Trans (Î± â†’ E) (Î± â†’ F) (Î± â†’ G) (Â· =o[ğ•œ;l] Â·) (Â· =o[ğ•œ;l] Â·) (Â· =o[ğ•œ;l] Â·) where
  trans := IsLittleOTVS.trans

theorem _root_.Filter.HasBasis.isLittleOTVS_iff {Î¹E Î¹F : Sort*} {pE : Î¹E â†’ Prop} {pF : Î¹F â†’ Prop}
    {sE : Î¹E â†’ Set E} {sF : Î¹F â†’ Set F} (hE : HasBasis (ğ“ (0 : E)) pE sE)
    (hF : HasBasis (ğ“ (0 : F)) pF sF) :
    f =o[ğ•œ;l] g â†” âˆ€ i, pE i â†’ âˆƒ j, pF j âˆ§ âˆ€ Îµ â‰  (0 : â„â‰¥0),
      âˆ€á¶  x in l, egauge ğ•œ (sE i) (f x) â‰¤ Îµ * egauge ğ•œ (sF j) (g x) := by
  refine (hE.forall_iff ?_).trans <| forallâ‚‚_congr fun _ _ â†¦ hF.exists_iff ?_
  Â· rintro s t hsub âŸ¨V, hVâ‚€, hVâŸ©
    exact âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).mono fun x â†¦ le_trans <| egauge_anti _ hsub _âŸ©
  Â· refine fun s t hsub h Îµ hÎµ â†¦ (h Îµ hÎµ).mono fun x hx â†¦ hx.trans ?_
    gcongr

theorem isLittleOTVS_iff_smallSets :
    f =o[ğ•œ; l] g â†” âˆ€ U âˆˆ ğ“ 0, âˆ€á¶  V in (ğ“ 0).smallSets, âˆ€ Îµ â‰  (0 : â„â‰¥0),
      âˆ€á¶  x in l, egauge ğ•œ U (f x) â‰¤ Îµ * egauge ğ•œ V (g x) :=
  forallâ‚‚_congr fun U hU â†¦ .symm <| eventually_smallSets' fun Vâ‚ Vâ‚‚ hV hVâ‚‚ Îµ hÎµ â†¦
    (hVâ‚‚ Îµ hÎµ).mono fun x hx â†¦ hx.trans <| by gcongr

alias âŸ¨IsLittleOTVS.eventually_smallSets, _âŸ© := isLittleOTVS_iff_smallSets

@[simp]
theorem isLittleOTVS_map {k : Î² â†’ Î±} {l : Filter Î²} :
    f =o[ğ•œ; map k l] g â†” (f âˆ˜ k) =o[ğ•œ;l] (g âˆ˜ k) := by
  simp [IsLittleOTVS]

lemma IsLittleOTVS.mono (hf : f =o[ğ•œ;lâ‚] g) (h : lâ‚‚ â‰¤ lâ‚) : f =o[ğ•œ;lâ‚‚] g :=
  fun U hU => let âŸ¨V, hV0, hVâŸ© := hf U hU; âŸ¨V, hV0, fun Îµ hÎµ => (hV Îµ hÎµ).filter_mono hâŸ©

lemma IsLittleOTVS.comp_tendsto {k : Î² â†’ Î±} {lb : Filter Î²} (h : f =o[ğ•œ; l] g)
    (hk : Tendsto k lb l) : (f âˆ˜ k) =o[ğ•œ; lb] (g âˆ˜ k) :=
  isLittleOTVS_map.mp (h.mono hk)

lemma isLittleOTVS_sup : f =o[ğ•œ; lâ‚ âŠ” lâ‚‚] g â†” f =o[ğ•œ; lâ‚] g âˆ§ f =o[ğ•œ; lâ‚‚] g := by
  simp only [isLittleOTVS_iff_smallSets, â† forall_and, â† eventually_and, eventually_sup]

lemma IsLittleOTVS.sup (hfâ‚ : f =o[ğ•œ; lâ‚] g) (hfâ‚‚ : f =o[ğ•œ; lâ‚‚] g) : f =o[ğ•œ; lâ‚ âŠ” lâ‚‚] g :=
  isLittleOTVS_sup.mpr âŸ¨hfâ‚, hfâ‚‚âŸ©

@[simp]
lemma IsLittleOTVS.zero (g : Î± â†’ F) (l : Filter Î±) : (0 : Î± â†’ E) =o[ğ•œ;l] g := by
  intros U hU
  simpa [egauge_zero_right _ (Filter.nonempty_of_mem hU)] using âŸ¨univ, by simpâŸ©

lemma isLittleOTVS_insert [TopologicalSpace Î±] {x : Î±} {s : Set Î±} (h : f x = 0) :
    f =o[ğ•œ;(ğ“[insert x s] x)] g â†” f =o[ğ•œ;(ğ“[s] x)] g := by
  rw [nhdsWithin_insert, isLittleOTVS_sup, and_iff_right]
  exact .congr' (.zero g _) h.symm .rfl

lemma IsLittleOTVS.insert [TopologicalSpace Î±] {x : Î±} {s : Set Î±}
    (h : f =o[ğ•œ;(ğ“[s] x)] g) (hf : f x = 0) :
    f =o[ğ•œ;(ğ“[insert x s] x)] g :=
  (isLittleOTVS_insert hf).2 h

@[simp]
lemma IsLittleOTVS.bot : f =o[ğ•œ;âŠ¥] g :=
  fun u hU => âŸ¨univ, by simpâŸ©

theorem IsLittleOTVS.add [IsTopologicalAddGroup E] [ContinuousSMul ğ•œ E]
    {fâ‚ fâ‚‚ : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (hâ‚ : fâ‚ =o[ğ•œ; l] g) (hâ‚‚ : fâ‚‚ =o[ğ•œ; l] g) : (fâ‚ + fâ‚‚) =o[ğ•œ; l] g := by
  rw [(nhds_basis_balanced ğ•œ E).add_self.isLittleOTVS_iff (basis_sets _)]
  rintro U âŸ¨hU, hUbâŸ©
  rcases ((hâ‚.eventually_smallSets U hU).and (hâ‚‚.eventually_smallSets U hU)).exists_mem_of_smallSets
    with âŸ¨V, hV, hVfâ‚, hVfâ‚‚âŸ©
  refine âŸ¨V, hV, fun Îµ hÎµ â†¦ ?_âŸ©
  filter_upwards [hVfâ‚ Îµ hÎµ, hVfâ‚‚ Îµ hÎµ] with x hxâ‚ hxâ‚‚
  exact (egauge_add_add_le hUb hUb _ _).trans (max_le hxâ‚ hxâ‚‚)

protected lemma IsLittleOTVS.smul_left (h : f =o[ğ•œ;l] g) (c : Î± â†’ ğ•œ) :
    (fun x â†¦ c x â€¢ f x) =o[ğ•œ;l] (fun x â†¦ c x â€¢ g x) := by
  unfold IsLittleOTVS at *
  peel h with U hU V hV Îµ hÎµ x hx
  rw [egauge_smul_right, egauge_smul_right, mul_left_comm]
  Â· gcongr
  all_goals exact fun _ â†¦ Filter.nonempty_of_mem â€¹_â€º

lemma isLittleOTVS_one [ContinuousSMul ğ•œ E] : f =o[ğ•œ;l] (1 : Î± â†’ ğ•œ) â†” Tendsto f l (ğ“ 0) := by
  constructor
  Â· intro hf
    rw [(basis_sets _).isLittleOTVS_iff nhds_basis_ball] at hf
    rw [(nhds_basis_balanced ğ•œ E).tendsto_right_iff]
    rintro U âŸ¨hU, hUbâŸ©
    rcases hf U hU with âŸ¨r, hrâ‚€, hrâŸ©
    lift r to â„â‰¥0 using hrâ‚€.le
    norm_cast at hrâ‚€
    rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
    obtain âŸ¨Îµ, hÎµâ‚€, hÎµâŸ© : âˆƒ Îµ : â„â‰¥0, 0 < Îµ âˆ§ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) < 1 := by
      apply Eventually.exists_gt
      refine Continuous.tendsto' ?_ _ _ (by simp) |>.eventually_lt_const zero_lt_one
      fun_prop (disch := intros; first | apply ENNReal.coe_ne_top | positivity)
    filter_upwards [hr Îµ hÎµâ‚€.ne'] with x hx
    refine mem_of_egauge_lt_one hUb (hx.trans_lt ?_)
    calc
      (Îµ : â„â‰¥0âˆ) * egauge ğ•œ (ball (0 : ğ•œ) r) 1 â‰¤ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) := by
        rw [mul_div_assoc]
        gcongr
        simpa using egauge_ball_le_of_one_lt_norm (r := r) (x := (1 : ğ•œ)) hc (by simp)
      _ < 1 := â€¹_â€º
  Â· intro hf U hU
    refine âŸ¨ball 0 1, ball_mem_nhds _ one_pos, fun Îµ hÎµ â†¦ ?_âŸ©
    rcases NormedField.exists_norm_lt ğ•œ hÎµ.bot_lt with âŸ¨c, hcâ‚€, hcÎµâŸ©
    replace hcâ‚€ : c â‰  0 := by simpa using hcâ‚€
    filter_upwards [hf ((set_smul_mem_nhds_zero_iff hcâ‚€).2 hU)] with a ha
    calc
      egauge ğ•œ U (f a) â‰¤ â€–câ€–â‚Š := egauge_le_of_mem_smul ha
      _ â‰¤ Îµ := mod_cast hcÎµ.le
      _ â‰¤ Îµ * egauge ğ•œ (ball (0 : ğ•œ) 1) 1 := by
        apply le_mul_of_one_le_right'
        simpa using le_egauge_ball_one ğ•œ (1 : ğ•œ)

lemma IsLittleOTVS.tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E} (h : g =o[ğ•œ;l] f) :
    Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  rw [â† isLittleOTVS_one (ğ•œ := ğ•œ)]
  intro U hU
  rcases h.smul_left fâ»Â¹ U hU with âŸ¨V, hVâ‚€, hVâŸ©
  refine âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).mono fun x hx â†¦ hx.trans ?_âŸ©
  by_cases hxâ‚€ : f x = 0 <;> simp [hxâ‚€, egauge_zero_right _ (Filter.nonempty_of_mem hVâ‚€)]

lemma isLittleOTVS_iff_tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (hâ‚€ : âˆ€á¶  x in l, f x = 0 â†’ g x = 0) :
    g =o[ğ•œ;l] f â†” Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  refine âŸ¨IsLittleOTVS.tendsto_inv_smul, fun h â†¦ ?_âŸ©
  refine (((isLittleOTVS_one (ğ•œ := ğ•œ)).mpr h).smul_left f).congr' (hâ‚€.mono fun x hx â†¦ ?_) (by simp)
  by_cases h : f x = 0 <;> simp [h, hx]

end TopologicalSpace

section NormedSpace

variable [NontriviallyNormedField ğ•œ]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

lemma isLittleOTVS_iff_isLittleO {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±} :
    f =o[ğ•œ;l] g â†” f =o[l] g := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hc : 1 < â€–câ€–â‚ŠâŸ©
  have hcâ‚€ : 0 < â€–câ€–â‚Š := one_pos.trans hc
  simp only [isLittleO_iff, nhds_basis_ball.isLittleOTVS_iff nhds_basis_ball]
  refine âŸ¨fun h Îµ hÎµ â†¦ ?_, fun h Îµ hÎµ â†¦ âŸ¨1, one_pos, fun Î´ hÎ´ â†¦ ?_âŸ©âŸ©
  Â· rcases h Îµ hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    lift Îµ to â„â‰¥0 using hÎµ.le; lift Î´ to â„â‰¥0 using hÎ´â‚€.le; norm_cast at hÎµ hÎ´â‚€
    filter_upwards [hÎ´ (Î´ / â€–câ€–â‚Š) (div_pos hÎ´â‚€ hcâ‚€).ne'] with x hx
    suffices (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ â€–g xâ€–â‚Š by
      rw [â† ENNReal.coe_div hÎµ.ne'] at this
      rw [â† div_le_iffâ‚€' (NNReal.coe_pos.2 hÎµ)]
      exact_mod_cast this
    calc
      (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ egauge ğ•œ (ball 0 Îµ) (f x) := div_le_egauge_ball ğ•œ _ _
      _ â‰¤ â†‘(Î´ / â€–câ€–â‚Š) * egauge ğ•œ (ball 0 â†‘Î´) (g x) := hx
      _ â‰¤ (Î´ / â€–câ€–â‚Š) * (â€–câ€–â‚Š * â€–g xâ€–â‚Š / Î´) := by
        gcongr
        exacts [ENNReal.coe_div_le, egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎ´â‚€)]
      _ = (Î´ / Î´) * (â€–câ€–â‚Š / â€–câ€–â‚Š) * â€–g xâ€–â‚Š := by simp only [div_eq_mul_inv]; ring
      _ â‰¤ 1 * 1 * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = â€–g xâ€–â‚Š := by simp
  Â· filter_upwards [@h â†‘(Îµ * Î´ / â€–câ€–â‚Š) (by positivity)] with x (hx : â€–f xâ€–â‚Š â‰¤ Îµ * Î´ / â€–câ€–â‚Š * â€–g xâ€–â‚Š)
    lift Îµ to â„â‰¥0 using hÎµ.le
    calc
      egauge ğ•œ (ball 0 Îµ) (f x) â‰¤ â€–câ€–â‚Š * â€–f xâ€–â‚Š / Îµ :=
        egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎµ)
      _ â‰¤ â€–câ€–â‚Š * (â†‘(Îµ * Î´ / â€–câ€–â‚Š) * â€–g xâ€–â‚Š) / Îµ := by gcongr; exact_mod_cast hx
      _ = (â€–câ€–â‚Š / â€–câ€–â‚Š) * (Îµ / Îµ) * Î´ * â€–g xâ€–â‚Š := by
        simp only [div_eq_mul_inv, ENNReal.coe_inv hcâ‚€.ne', ENNReal.coe_mul]; ring
      _ â‰¤ 1 * 1 * Î´ * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = Î´ * â€–g xâ€–â‚Š := by simp
      _ â‰¤ Î´ * egauge ğ•œ (ball 0 1) (g x) := by gcongr; apply le_egauge_ball_one

alias âŸ¨isLittleOTVS.isLittleO, IsLittleO.isLittleOTVSâŸ© := isLittleOTVS_iff_isLittleO

end NormedSpace

end Asymptotics
