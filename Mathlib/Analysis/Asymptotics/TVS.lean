/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Eric Wieser
-/
import Mathlib.Analysis.Convex.EGauge
import Mathlib.Analysis.LocallyConvex.BalancedCoreHull
import Mathlib.Analysis.Seminorm
import Mathlib.Tactic.Peel
import Mathlib.Topology.Instances.ENNReal.Lemmas
import Mathlib.Analysis.Asymptotics.Defs
import Mathlib.Topology.Algebra.Module.LinearMapPiProd

/-!
# Asymptotics in a Topological Vector Space

This file defines `Asymptotics.IsLittleOTVS` and `Asymptotics.IsBigOTVS`
as generalizations of `Asymptotics.IsLittleO` and `Asymptotics.IsBigO`
from normed spaces to topological spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ (resp., $f = O(g)$)
if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$
(resp, $\operatorname{gauge}_{K, U} (f(x)) = O(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

In a normed space, we can use balls of positive radius as both `U` and `V`,
thus reducing the definition to the classical one.

This frees the user from having to chose a canonical norm, at the expense of having to pick a
specific base field.
This is exactly the tradeoff we want in `HasFDerivAtFilter`,
as there the base field is already chosen,
and this removes the choice of norm being part of the statement.

These definitions were added to the library in order to migrate FrÃ©chet derivatives
from normed vector spaces to topological vector spaces.
The definitions are motivated by
https://en.wikipedia.org/wiki/Fr%C3%A9chet_derivative#Generalization_to_topological_vector_spaces
but the definition there doesn't work for topological vector spaces over general normed fields.
[This Zulip discussion](https://leanprover.zulipchat.com/#narrow/channel/116395-maths/topic/generalizing.20deriv.20to.20TVS)
led to the current choice of the definition of `Asymptotics.IsLittleOTVS`,
and `Asymptotics.IsBigOTVS` was defined in a similar manner.

## Main results

* `isLittleOTVS_iff_isLittleO`: the equivalence between these two definitions in the case of a
  normed space.

* `isLittleOTVS_iff_tendsto_inv_smul`: the equivalence to convergence of the ratio to zero
  in case of a topological vector space.

## TODO

- Add `Asymptotics.IsThetaTVS` and `Asymptotics.IsEquivalentTVS`.
- Prove equivalence of `IsBigOTVS` and `IsBigO`.
- Prove a version of `Asymptotics.isBigO_One` for `IsBigOTVS`.

-/

open Set Filter Asymptotics Metric
open scoped Topology Pointwise ENNReal NNReal

namespace Asymptotics

section Defs

variable (ğ•œ : Type*) {Î± E F : Type*}
  [ENorm ğ•œ] [TopologicalSpace E] [TopologicalSpace F] [Zero E] [Zero F] [SMul ğ•œ E] [SMul ğ•œ F]

/-- `f =o[ğ•œ; l] g` (`IsLittleOTVS ğ•œ l f g`) is a generalization of `f =o[l] g` (`IsLittleO l f g`)
that works in topological `ğ•œ`-vector spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `K`,
we say that $f = o(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) = o(\operatorname{gauge}_{K, V} (g(x)))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.

We use an `ENNReal`-valued function `egauge` for the gauge,
so we unfold the definition of little o instead of reusing it. -/
@[mk_iff]
structure IsLittleOTVS (l : Filter Î±) (f : Î± â†’ E) (g : Î± â†’ F) : Prop where
  exists_eventuallyLE_mul : âˆ€ U âˆˆ ğ“ (0 : E), âˆƒ V âˆˆ ğ“ (0 : F), âˆ€ Îµ â‰  (0 : â„â‰¥0),
    (fun x â†¦ egauge ğ•œ U (f x)) â‰¤á¶ [l] (fun x â†¦ Îµ * egauge ğ•œ V (g x))

@[inherit_doc]
notation:100 f " =o[" ğ•œ "; " l "] " g:100 => IsLittleOTVS ğ•œ l f g

/-- `f =O[ğ•œ; l] g` (`IsBigOTVS ğ•œ l f g`) is a generalization of `f =O[l] g` (`IsBigO l f g`)
that works in topological `ğ•œ`-vector spaces.

Given two functions `f` and `g` taking values in topological vector spaces
over a normed field `ğ•œ`,
we say that $f = O(g)$ if for any neighborhood of zero `U` in the codomain of `f`
there exists a neighborhood of zero `V` in the codomain of `g`
such that $\operatorname{gauge}_{K, U} (f(x)) \le \operatorname{gauge}_{K, V} (g(x))$,
where $\operatorname{gauge}_{K, U}(y) = \inf \{â€–câ€– \mid y âˆˆ c â€¢ U\}$.
-/
@[mk_iff]
structure IsBigOTVS (l : Filter Î±) (f : Î± â†’ E) (g : Î± â†’ F) : Prop where
  exists_eventuallyLE : âˆ€ U âˆˆ ğ“ (0 : E), âˆƒ V âˆˆ ğ“ (0 : F),
    (egauge ğ•œ U <| f Â·) â‰¤á¶ [l] (egauge ğ•œ V <| g Â·)

@[inherit_doc]
notation:100 f " =O[" ğ•œ "; " l "] " g:100 => IsBigOTVS ğ•œ l f g

end Defs

variable {Î± Î² ğ•œ E F G : Type*}

section TopologicalSpace

variable [NontriviallyNormedField ğ•œ]
  [AddCommGroup E] [TopologicalSpace E] [Module ğ•œ E]
  [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F]
  [AddCommGroup G] [TopologicalSpace G] [Module ğ•œ G]

section congr

variable {f fâ‚ fâ‚‚ : Î± â†’ E} {g gâ‚ gâ‚‚ : Î± â†’ F} {l : Filter Î±}

theorem isLittleOTVS_iff_tendsto_div :
    f =o[ğ•œ; l] g â†” âˆ€ U âˆˆ ğ“ 0, âˆƒ V âˆˆ ğ“ 0,
      Tendsto (fun x â†¦ egauge ğ•œ U (f x) / egauge ğ•œ V (g x)) l (ğ“ 0) := by
  simp only [isLittleOTVS_iff, â† ENNReal.coe_zero, ENNReal.nhds_coe, â† NNReal.bot_eq_zero,
    (nhds_bot_basis_Iic.map _).tendsto_right_iff]
  simp +contextual [ENNReal.div_le_iff_le_mul, pos_iff_ne_zero, EventuallyLE]

alias âŸ¨IsLittleOTVS.tendsto_div, IsLittleOTVS.of_tendsto_divâŸ© := isLittleOTVS_iff_tendsto_div

/-- A version of `IsLittleOTVS.exists_eventuallyLE_mul`
where `Îµ` is quantified over `â„â‰¥0âˆ` instead of `â„â‰¥0`. -/
theorem IsLittleOTVS.exists_eventuallyLE_mul_ennreal (h : f =o[ğ•œ; l] g) {U : Set E} (hU : U âˆˆ ğ“ 0) :
    âˆƒ V âˆˆ ğ“ (0 : F), âˆ€ Îµ â‰  0, (fun x â†¦ egauge ğ•œ U (f x)) â‰¤á¶ [l] (fun x â†¦ Îµ * egauge ğ•œ V (g x)) := by
  obtain âŸ¨V, hVâ‚€, hVâŸ© := h.exists_eventuallyLE_mul U hU
  refine âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ ?_âŸ©
  cases Îµ with
  | top => exact (hV 1 one_ne_zero).trans <| .of_forall fun _ â†¦ mul_le_mul_right' le_top _
  | coe Îµ => exact hV Îµ (mod_cast hÎµ)

theorem isLittleOTVS_congr (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
    fâ‚ =o[ğ•œ; l] gâ‚ â†” fâ‚‚ =o[ğ•œ; l] gâ‚‚ := by
  simp only [isLittleOTVS_iff_tendsto_div]
  peel with U hU V hV
  exact tendsto_congr' (hf.compâ‚‚ (egauge _ _ Â· / egauge _ _ Â·) hg)

/-- A stronger version of `IsLittleOTVS.congr` that requires the functions only agree along the
filter. -/
theorem IsLittleOTVS.congr' (h : fâ‚ =o[ğ•œ; l] gâ‚) (hf : fâ‚ =á¶ [l] fâ‚‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
    fâ‚‚ =o[ğ•œ; l] gâ‚‚ :=
  (isLittleOTVS_congr hf hg).mp h

theorem IsLittleOTVS.congr (h : fâ‚ =o[ğ•œ; l] gâ‚) (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) :
    fâ‚‚ =o[ğ•œ; l] gâ‚‚ :=
  h.congr' (univ_mem' hf) (univ_mem' hg)

theorem IsLittleOTVS.congr_left (h : fâ‚ =o[ğ•œ; l] g) (hf : âˆ€ x, fâ‚ x = fâ‚‚ x) : fâ‚‚ =o[ğ•œ; l] g :=
  h.congr hf fun _ â†¦ rfl

theorem IsLittleOTVS.congr_right (h : f =o[ğ•œ; l] gâ‚) (hg : âˆ€ x, gâ‚ x = gâ‚‚ x) : f =o[ğ•œ; l] gâ‚‚ :=
  h.congr (fun _ â†¦ rfl) hg

end congr

variable {l lâ‚ lâ‚‚ : Filter Î±} {f : Î± â†’ E} {g : Î± â†’ F}

theorem IsLittleOTVS.isBigOTVS (h : f =o[ğ•œ; l] g) : f =O[ğ•œ; l] g := by
  refine âŸ¨fun U hU â†¦ ?_âŸ©
  rcases h.1 U hU with âŸ¨V, hVâ‚€, hVâŸ©
  use V, hVâ‚€
  simpa using hV 1 one_ne_zero

@[trans]
theorem IsBigOTVS.trans {k : Î± â†’ G} (hfg : f =O[ğ•œ; l] g) (hgk : g =O[ğ•œ; l] k) : f =O[ğ•œ; l] k := by
  refine âŸ¨fun U hUâ‚€ â†¦ ?_âŸ©
  obtain âŸ¨V, hVâ‚€, hVâŸ© := hfg.1 U hUâ‚€
  obtain âŸ¨W, hWâ‚€, hWâŸ© := hgk.1 V hVâ‚€
  refine âŸ¨W, hWâ‚€, ?_âŸ©
  filter_upwards [hV, hW] with x hxâ‚ hxâ‚‚ using hxâ‚.trans hxâ‚‚

instance instTransIsBigOTVSIsBigOTVS :
    @Trans (Î± â†’ E) (Î± â†’ F) (Î± â†’ G) (IsBigOTVS ğ•œ l) (IsBigOTVS ğ•œ l) (IsBigOTVS ğ•œ l) where
  trans := IsBigOTVS.trans

theorem IsLittleOTVS.trans_isBigOTVS {k : Î± â†’ G} (hfg : f =o[ğ•œ; l] g) (hgk : g =O[ğ•œ; l] k) :
    f =o[ğ•œ; l] k := by
  refine âŸ¨fun U hUâ‚€ â†¦ ?_âŸ©
  obtain âŸ¨V, hVâ‚€, hVâŸ© := hfg.1 U hUâ‚€
  obtain âŸ¨W, hWâ‚€, hWâŸ© := hgk.1 V hVâ‚€
  refine âŸ¨W, hWâ‚€, fun Îµ hÎµ â†¦ ?_âŸ©
  filter_upwards [hV Îµ hÎµ, hW] with x hxâ‚ hxâ‚‚ using hxâ‚.trans <| by gcongr

instance instTransIsLittleOTVSIsBigOTVS :
    @Trans (Î± â†’ E) (Î± â†’ F) (Î± â†’ G) (IsLittleOTVS ğ•œ l) (IsBigOTVS ğ•œ l) (IsLittleOTVS ğ•œ l) where
  trans := IsLittleOTVS.trans_isBigOTVS

theorem IsBigOTVS.trans_isLittleOTVS {k : Î± â†’ G} (hfg : f =O[ğ•œ; l] g) (hgk : g =o[ğ•œ; l] k) :
    f =o[ğ•œ; l] k := by
  refine âŸ¨fun U hUâ‚€ â†¦ ?_âŸ©
  obtain âŸ¨V, hVâ‚€, hVâŸ© := hfg.1 U hUâ‚€
  obtain âŸ¨W, hWâ‚€, hWâŸ© := hgk.1 V hVâ‚€
  refine âŸ¨W, hWâ‚€, fun Îµ hÎµ â†¦ ?_âŸ©
  filter_upwards [hV, hW Îµ hÎµ] with x hxâ‚ hxâ‚‚ using hxâ‚.trans hxâ‚‚

instance instTransIsBigOTVSIsLittleOTVS :
    @Trans (Î± â†’ E) (Î± â†’ F) (Î± â†’ G) (IsBigOTVS ğ•œ l) (IsLittleOTVS ğ•œ l) (IsLittleOTVS ğ•œ l) where
  trans := IsBigOTVS.trans_isLittleOTVS

@[trans]
theorem IsLittleOTVS.trans {k : Î± â†’ G} (hfg : f =o[ğ•œ; l] g) (hgk : g =o[ğ•œ; l] k) : f =o[ğ•œ; l] k :=
  hfg.trans_isBigOTVS hgk.isBigOTVS

instance instTransIsLittleOTVSIsLittleOTVS :
    @Trans (Î± â†’ E) (Î± â†’ F) (Î± â†’ G) (IsLittleOTVS ğ•œ l) (IsLittleOTVS ğ•œ l) (IsLittleOTVS ğ•œ l) where
  trans := IsLittleOTVS.trans

protected theorem _root_.Filter.HasBasis.isLittleOTVS_iff
    {Î¹E Î¹F : Sort*} {pE : Î¹E â†’ Prop} {pF : Î¹F â†’ Prop}
    {sE : Î¹E â†’ Set E} {sF : Î¹F â†’ Set F} (hE : HasBasis (ğ“ (0 : E)) pE sE)
    (hF : HasBasis (ğ“ (0 : F)) pF sF) :
    f =o[ğ•œ; l] g â†” âˆ€ i, pE i â†’ âˆƒ j, pF j âˆ§ âˆ€ Îµ â‰  (0 : â„â‰¥0),
      âˆ€á¶  x in l, egauge ğ•œ (sE i) (f x) â‰¤ Îµ * egauge ğ•œ (sF j) (g x) := by
  rw [isLittleOTVS_iff]
  refine (hE.forall_iff ?_).trans <| forallâ‚‚_congr fun _ _ â†¦ hF.exists_iff ?_
  Â· rintro s t hsub âŸ¨V, hVâ‚€, hVâŸ©
    exact âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).mono fun x â†¦ le_trans <| egauge_anti _ hsub _âŸ©
  Â· refine fun s t hsub h Îµ hÎµ â†¦ (h Îµ hÎµ).mono fun x hx â†¦ hx.trans ?_
    simp only
    gcongr

protected theorem _root_.Filter.HasBasis.isBigOTVS_iff
    {Î¹E Î¹F : Sort*} {pE : Î¹E â†’ Prop} {pF : Î¹F â†’ Prop}
    {sE : Î¹E â†’ Set E} {sF : Î¹F â†’ Set F} (hE : HasBasis (ğ“ (0 : E)) pE sE)
    (hF : HasBasis (ğ“ (0 : F)) pF sF) :
    f =O[ğ•œ; l] g â†” âˆ€ i, pE i â†’ âˆƒ j, pF j âˆ§
      âˆ€á¶  x in l, egauge ğ•œ (sE i) (f x) â‰¤ egauge ğ•œ (sF j) (g x) := by
  rw [isBigOTVS_iff]
  refine (hE.forall_iff ?_).trans <| forallâ‚‚_congr fun _ _ â†¦ hF.exists_iff ?_
  Â· rintro s t hsub âŸ¨V, hVâ‚€, hVâŸ©
    exact âŸ¨V, hVâ‚€, hV.mono fun x â†¦ le_trans <| egauge_anti _ hsub _âŸ©
  Â· exact fun s t hsub h â†¦ h.mono fun x hx â†¦ hx.trans <| egauge_anti ğ•œ hsub (g x)

theorem isLittleOTVS_iff_smallSets :
    f =o[ğ•œ; l] g â†” âˆ€ U âˆˆ ğ“ 0, âˆ€á¶  V in (ğ“ 0).smallSets, âˆ€ Îµ â‰  (0 : â„â‰¥0),
      âˆ€á¶  x in l, egauge ğ•œ U (f x) â‰¤ Îµ * egauge ğ•œ V (g x) :=
  (isLittleOTVS_iff ..).trans <| forallâ‚‚_congr fun U hU â†¦ .symm <|
    eventually_smallSets' fun Vâ‚ Vâ‚‚ hV hVâ‚‚ Îµ hÎµ â†¦ (hVâ‚‚ Îµ hÎµ).mono fun x hx â†¦ hx.trans <| by gcongr

alias âŸ¨IsLittleOTVS.eventually_smallSets, _âŸ© := isLittleOTVS_iff_smallSets

theorem isBigOTVS_iff_smallSets :
    f =O[ğ•œ; l] g â†” âˆ€ U âˆˆ ğ“ 0, âˆ€á¶  V in (ğ“ 0).smallSets,
      âˆ€á¶  x in l, egauge ğ•œ U (f x) â‰¤ egauge ğ•œ V (g x) :=
  (isBigOTVS_iff ..).trans <| forallâ‚‚_congr fun U hU â†¦ .symm <|
    eventually_smallSets' fun Vâ‚ Vâ‚‚ hV hVâ‚‚ â†¦ hVâ‚‚.mono fun x hx â†¦ hx.trans <| by gcongr

alias âŸ¨IsBigOTVS.eventually_smallSets, _âŸ© := isBigOTVS_iff_smallSets

@[simp]
theorem isLittleOTVS_map {k : Î² â†’ Î±} {l : Filter Î²} :
    f =o[ğ•œ; map k l] g â†” (f âˆ˜ k) =o[ğ•œ; l] (g âˆ˜ k) := by
  simp [isLittleOTVS_iff, EventuallyLE]

@[simp]
theorem isBigOTVS_map {k : Î² â†’ Î±} {l : Filter Î²} :
    f =O[ğ•œ; map k l] g â†” (f âˆ˜ k) =O[ğ•œ; l] (g âˆ˜ k) := by
  simp [isBigOTVS_iff, EventuallyLE]

lemma IsLittleOTVS.mono (hf : f =o[ğ•œ; lâ‚] g) (h : lâ‚‚ â‰¤ lâ‚) : f =o[ğ•œ; lâ‚‚] g :=
  âŸ¨fun U hU â†¦ let âŸ¨V, hV0, hVâŸ© := hf.1 U hU; âŸ¨V, hV0, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).filter_mono hâŸ©âŸ©

lemma IsBigOTVS.mono (hf : f =O[ğ•œ; lâ‚] g) (h : lâ‚‚ â‰¤ lâ‚) : f =O[ğ•œ; lâ‚‚] g :=
  âŸ¨fun U hU â†¦ let âŸ¨V, hV0, hVâŸ© := hf.1 U hU; âŸ¨V, hV0, hV.filter_mono hâŸ©âŸ©

lemma IsLittleOTVS.comp_tendsto {k : Î² â†’ Î±} {lb : Filter Î²} (h : f =o[ğ•œ; l] g)
    (hk : Tendsto k lb l) : (f âˆ˜ k) =o[ğ•œ; lb] (g âˆ˜ k) :=
  isLittleOTVS_map.mp (h.mono hk)

lemma IsBigOTVS.comp_tendsto {k : Î² â†’ Î±} {lb : Filter Î²} (h : f =O[ğ•œ; l] g)
    (hk : Tendsto k lb l) : (f âˆ˜ k) =O[ğ•œ; lb] (g âˆ˜ k) :=
  isBigOTVS_map.mp (h.mono hk)

lemma isLittleOTVS_sup : f =o[ğ•œ; lâ‚ âŠ” lâ‚‚] g â†” f =o[ğ•œ; lâ‚] g âˆ§ f =o[ğ•œ; lâ‚‚] g := by
  simp only [isLittleOTVS_iff_smallSets, â† forall_and, â† eventually_and, eventually_sup]

lemma IsLittleOTVS.sup (hfâ‚ : f =o[ğ•œ; lâ‚] g) (hfâ‚‚ : f =o[ğ•œ; lâ‚‚] g) : f =o[ğ•œ; lâ‚ âŠ” lâ‚‚] g :=
  isLittleOTVS_sup.mpr âŸ¨hfâ‚, hfâ‚‚âŸ©

lemma _root_.ContinuousLinearMap.isBigOTVS_id {l : Filter E} (f : E â†’L[ğ•œ] F) : f =O[ğ•œ; l] id :=
  âŸ¨fun U hU â†¦ âŸ¨f â»Â¹' U, (map_continuous f).tendsto' 0 0 (map_zero f) hU, .of_forall <|
    (mapsTo_preimage f U).egauge_le ğ•œ fâŸ©âŸ©

lemma _root_.ContinuousLinearMap.isBigOTVS_comp (g : E â†’L[ğ•œ] F) : (g âˆ˜ f) =O[ğ•œ; l] f :=
  g.isBigOTVS_id.comp_tendsto tendsto_top

lemma _root_.ContinuousLinearMap.isBigOTVS_fun_comp (g : E â†’L[ğ•œ] F) : (g <| f Â·) =O[ğ•œ; l] f :=
  g.isBigOTVS_comp

@[simp]
lemma IsLittleOTVS.zero (g : Î± â†’ F) (l : Filter Î±) : (0 : Î± â†’ E) =o[ğ•œ; l] g := by
  refine âŸ¨fun U hU â†¦ ?_âŸ©
  use univ
  simp [egauge_zero_right _ (Filter.nonempty_of_mem hU), EventuallyLE]

lemma isLittleOTVS_insert [TopologicalSpace Î±] {x : Î±} {s : Set Î±} (h : f x = 0) :
    f =o[ğ•œ; ğ“[insert x s] x] g â†” f =o[ğ•œ; (ğ“[s] x)] g := by
  rw [nhdsWithin_insert, isLittleOTVS_sup, and_iff_right]
  exact .congr' (.zero g _) h.symm .rfl

lemma IsLittleOTVS.insert [TopologicalSpace Î±] {x : Î±} {s : Set Î±}
    (h : f =o[ğ•œ; ğ“[s] x] g) (hf : f x = 0) :
    f =o[ğ•œ; ğ“[insert x s] x] g :=
  (isLittleOTVS_insert hf).2 h

@[simp]
lemma IsLittleOTVS.bot : f =o[ğ•œ; âŠ¥] g :=
  âŸ¨fun u hU â†¦ âŸ¨univ, by simp [EventuallyLE]âŸ©âŸ©

theorem IsLittleOTVS.prodMk [ContinuousSMul ğ•œ E] [ContinuousSMul ğ•œ F] {k : Î± â†’ G}
    (hf : f =o[ğ•œ; l] k) (hg : g =o[ğ•œ; l] k) : (fun x â†¦ (f x, g x)) =o[ğ•œ; l] k := by
  rw [((nhds_basis_balanced ğ•œ E).prod_nhds (nhds_basis_balanced ğ•œ F)).isLittleOTVS_iff
    (basis_sets _)]
  rintro âŸ¨U, VâŸ© âŸ¨âŸ¨hU, hUbâŸ©, hV, hVbâŸ©
  rcases ((hf.eventually_smallSets U hU).and (hg.eventually_smallSets V hV)).exists_mem_of_smallSets
    with âŸ¨W, hW, hWf, hWgâŸ©
  refine âŸ¨W, hW, fun Îµ hÎµ â†¦ ?_âŸ©
  filter_upwards [hWf Îµ hÎµ, hWg Îµ hÎµ] with x hfx hgx
  simp [egauge_prod_mk, *]

protected theorem IsLittleOTVS.fst {f : Î± â†’ E Ã— F} {g : Î± â†’ G} (h : f =o[ğ•œ; l] g) :
    (f Â· |>.fst) =o[ğ•œ; l] g :=
  ContinuousLinearMap.fst ğ•œ E F |>.isBigOTVS_comp |>.trans_isLittleOTVS h

protected theorem IsLittleOTVS.snd {f : Î± â†’ E Ã— F} {g : Î± â†’ G} (h : f =o[ğ•œ; l] g) :
    (f Â· |>.snd) =o[ğ•œ; l] g :=
  ContinuousLinearMap.snd ğ•œ E F |>.isBigOTVS_comp |>.trans_isLittleOTVS h

@[simp]
theorem isLittleOTVS_prodMk_left [ContinuousSMul ğ•œ E] [ContinuousSMul ğ•œ F] {k : Î± â†’ G} :
    (fun x â†¦ (f x, g x)) =o[ğ•œ; l] k â†” f =o[ğ•œ; l] k âˆ§ g =o[ğ•œ; l] k :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ h.elim .prodMkâŸ©

theorem IsBigOTVS.prodMk [ContinuousSMul ğ•œ E] [ContinuousSMul ğ•œ F] {k : Î± â†’ G}
    (hf : f =O[ğ•œ; l] k) (hg : g =O[ğ•œ; l] k) : (fun x â†¦ (f x, g x)) =O[ğ•œ; l] k := by
  rw [((nhds_basis_balanced ğ•œ E).prod_nhds (nhds_basis_balanced ğ•œ F)).isBigOTVS_iff (basis_sets _)]
  rintro âŸ¨U, VâŸ© âŸ¨âŸ¨hU, hUbâŸ©, hV, hVbâŸ©
  rcases ((hf.eventually_smallSets U hU).and (hg.eventually_smallSets V hV)).exists_mem_of_smallSets
    with âŸ¨W, hW, hWf, hWgâŸ©
  refine âŸ¨W, hW, ?_âŸ©
  filter_upwards [hWf, hWg] with x hfx hgx
  simp [egauge_prod_mk, *]

protected theorem IsBigOTVS.fst {f : Î± â†’ E Ã— F} {g : Î± â†’ G} (h : f =O[ğ•œ; l] g) :
    (f Â· |>.fst) =O[ğ•œ; l] g :=
  ContinuousLinearMap.fst ğ•œ E F |>.isBigOTVS_comp |>.trans h

protected theorem IsBigOTVS.snd {f : Î± â†’ E Ã— F} {g : Î± â†’ G} (h : f =O[ğ•œ; l] g) :
    (f Â· |>.snd) =O[ğ•œ; l] g :=
  ContinuousLinearMap.snd ğ•œ E F |>.isBigOTVS_comp |>.trans h

@[simp]
theorem isBigOTVS_prodMk_left [ContinuousSMul ğ•œ E] [ContinuousSMul ğ•œ F] {k : Î± â†’ G} :
    (fun x â†¦ (f x, g x)) =O[ğ•œ; l] k â†” f =O[ğ•œ; l] k âˆ§ g =O[ğ•œ; l] k :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ h.elim .prodMkâŸ©

theorem IsLittleOTVS.add [ContinuousAdd E] [ContinuousSMul ğ•œ E]
    {fâ‚ fâ‚‚ : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (hâ‚ : fâ‚ =o[ğ•œ; l] g) (hâ‚‚ : fâ‚‚ =o[ğ•œ; l] g) : (fâ‚ + fâ‚‚) =o[ğ•œ; l] g :=
  ContinuousLinearMap.fst ğ•œ E E + ContinuousLinearMap.snd ğ•œ E E |>.isBigOTVS_comp
    |>.trans_isLittleOTVS <| hâ‚.prodMk hâ‚‚

theorem IsBigOTVS.add [ContinuousAdd E] [ContinuousSMul ğ•œ E]
    {fâ‚ fâ‚‚ : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (hâ‚ : fâ‚ =O[ğ•œ; l] g) (hâ‚‚ : fâ‚‚ =O[ğ•œ; l] g) : (fâ‚ + fâ‚‚) =O[ğ•œ; l] g :=
  ContinuousLinearMap.fst ğ•œ E E + ContinuousLinearMap.snd ğ•œ E E |>.isBigOTVS_comp
    |>.trans <| hâ‚.prodMk hâ‚‚

protected theorem IsLittleOTVS.pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousSMul ğ•œ (E i)]
    {f : âˆ€ i, Î± â†’ E i} (h : âˆ€ i, f i =o[ğ•œ; l] g) : (fun x i â†¦ f i x) =o[ğ•œ; l] g := by
  have := hasBasis_pi fun i â†¦ nhds_basis_balanced ğ•œ (E i)
  rw [â† nhds_pi, â† Pi.zero_def] at this
  simp only [this.isLittleOTVS_iff (basis_sets _), forall_and, Prod.forall, id]
  rintro I U âŸ¨hIf, hU, UbâŸ©
  have := fun i hi â†¦ (h i).eventually_smallSets (U i) (hU i hi)
  rcases (hIf.eventually_all.mpr this).exists_mem_of_smallSets with âŸ¨V, hVâ‚€, hVâŸ©
  refine âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ ?_âŸ©
  refine (hIf.eventually_all.mpr (hV Â· Â· Îµ hÎµ)).mono fun x hx â†¦ ?_
  simpa only [id, egauge_pi hIf Ub, iSupâ‚‚_le_iff]

theorem IsLittleOTVS.proj {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] {f : Î± â†’ âˆ€ i, E i}
    (h : f =o[ğ•œ; l] g) (i : Î¹) : (f Â· i) =o[ğ•œ; l] g :=
  ContinuousLinearMap.proj i |>.isBigOTVS_fun_comp |>.trans_isLittleOTVS h

theorem isLittleOTVS_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousSMul ğ•œ (E i)]
    {f : Î± â†’ âˆ€ i, E i} : f =o[ğ•œ; l] g â†” âˆ€ i, (f Â· i) =o[ğ•œ; l] g :=
  âŸ¨.proj, .piâŸ©

protected theorem IsBigOTVS.pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousSMul ğ•œ (E i)]
    {f : âˆ€ i, Î± â†’ E i} (h : âˆ€ i, f i =O[ğ•œ; l] g) : (fun x i â†¦ f i x) =O[ğ•œ; l] g := by
  have := hasBasis_pi fun i â†¦ nhds_basis_balanced ğ•œ (E i)
  rw [â† nhds_pi, â† Pi.zero_def] at this
  simp only [this.isBigOTVS_iff (basis_sets _), forall_and, Prod.forall, id]
  rintro I U âŸ¨hIf, hU, UbâŸ©
  have := fun i hi â†¦ (h i).eventually_smallSets (U i) (hU i hi)
  rcases (hIf.eventually_all.mpr this).exists_mem_of_smallSets with âŸ¨V, hVâ‚€, hVâŸ©
  use V, hVâ‚€
  refine (hIf.eventually_all.mpr hV).mono fun x hx â†¦ ?_
  simpa only [id, egauge_pi hIf Ub, iSupâ‚‚_le_iff]

theorem IsBigOTVS.proj {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] {f : Î± â†’ âˆ€ i, E i}
    (h : f =O[ğ•œ; l] g) (i : Î¹) : (f Â· i) =O[ğ•œ; l] g :=
  ContinuousLinearMap.proj i |>.isBigOTVS_fun_comp |>.trans h

theorem isBigOTVS_pi {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)]
    [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousSMul ğ•œ (E i)]
    {f : Î± â†’ âˆ€ i, E i} : f =O[ğ•œ; l] g â†” âˆ€ i, (f Â· i) =O[ğ•œ; l] g :=
  âŸ¨.proj, .piâŸ©

protected lemma IsLittleOTVS.smul_left (h : f =o[ğ•œ; l] g) (c : Î± â†’ ğ•œ) :
    (fun x â†¦ c x â€¢ f x) =o[ğ•œ; l] (fun x â†¦ c x â€¢ g x) := by
  simp only [isLittleOTVS_iff] at *
  peel h with U hU V hV Îµ hÎµ x hx
  simp only at *
  rw [egauge_smul_right, egauge_smul_right, mul_left_comm]
  Â· gcongr
  all_goals exact fun _ â†¦ Filter.nonempty_of_mem â€¹_â€º

lemma isLittleOTVS_one [ContinuousSMul ğ•œ E] : f =o[ğ•œ; l] (1 : Î± â†’ ğ•œ) â†” Tendsto f l (ğ“ 0) := by
  constructor
  Â· intro hf
    rw [(basis_sets _).isLittleOTVS_iff nhds_basis_ball] at hf
    rw [(nhds_basis_balanced ğ•œ E).tendsto_right_iff]
    rintro U âŸ¨hU, hUbâŸ©
    rcases hf U hU with âŸ¨r, hrâ‚€, hrâŸ©
    lift r to â„â‰¥0 using hrâ‚€.le
    norm_cast at hrâ‚€
    rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
    obtain âŸ¨Îµ, hÎµâ‚€, hÎµâŸ© : âˆƒ Îµ : â„â‰¥0, 0 < Îµ âˆ§ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) < 1 := by
      apply Eventually.exists_gt
      refine Continuous.tendsto' ?_ _ _ (by simp) |>.eventually_lt_const zero_lt_one
      fun_prop (disch := intros; first | apply ENNReal.coe_ne_top | positivity)
    filter_upwards [hr Îµ hÎµâ‚€.ne'] with x hx
    refine mem_of_egauge_lt_one hUb (hx.trans_lt ?_)
    calc
      (Îµ : â„â‰¥0âˆ) * egauge ğ•œ (ball (0 : ğ•œ) r) 1 â‰¤ (Îµ * â€–câ€–â‚Š / r : â„â‰¥0âˆ) := by
        rw [mul_div_assoc]
        gcongr
        simpa using egauge_ball_le_of_one_lt_norm (r := r) (x := (1 : ğ•œ)) hc (by simp)
      _ < 1 := â€¹_â€º
  Â· simp only [isLittleOTVS_iff]
    intro hf U hU
    refine âŸ¨ball 0 1, ball_mem_nhds _ one_pos, fun Îµ hÎµ â†¦ ?_âŸ©
    rcases NormedField.exists_norm_lt ğ•œ hÎµ.bot_lt with âŸ¨c, hcâ‚€, hcÎµâŸ©
    replace hcâ‚€ : c â‰  0 := by simpa using hcâ‚€
    filter_upwards [hf ((set_smul_mem_nhds_zero_iff hcâ‚€).2 hU)] with a ha
    calc
      egauge ğ•œ U (f a) â‰¤ â€–câ€–â‚Š := egauge_le_of_mem_smul ha
      _ â‰¤ Îµ := mod_cast hcÎµ.le
      _ â‰¤ Îµ * egauge ğ•œ (ball (0 : ğ•œ) 1) 1 := by
        apply le_mul_of_one_le_right'
        simpa using le_egauge_ball_one ğ•œ (1 : ğ•œ)

lemma IsLittleOTVS.tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E}
    (h : g =o[ğ•œ; l] f) : Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  rw [â† isLittleOTVS_one (ğ•œ := ğ•œ), isLittleOTVS_iff]
  intro U hU
  rcases (h.smul_left fâ»Â¹).1 U hU with âŸ¨V, hVâ‚€, hVâŸ©
  refine âŸ¨V, hVâ‚€, fun Îµ hÎµ â†¦ (hV Îµ hÎµ).mono fun x hx â†¦ hx.trans ?_âŸ©
  by_cases hxâ‚€ : f x = 0 <;> simp [hxâ‚€, egauge_zero_right _ (Filter.nonempty_of_mem hVâ‚€)]

lemma isLittleOTVS_iff_tendsto_inv_smul [ContinuousSMul ğ•œ E] {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (hâ‚€ : âˆ€á¶  x in l, f x = 0 â†’ g x = 0) :
    g =o[ğ•œ; l] f â†” Tendsto (fun x â†¦ (f x)â»Â¹ â€¢ g x) l (ğ“ 0) := by
  refine âŸ¨IsLittleOTVS.tendsto_inv_smul, fun h â†¦ ?_âŸ©
  refine (((isLittleOTVS_one (ğ•œ := ğ•œ)).mpr h).smul_left f).congr' (hâ‚€.mono fun x hx â†¦ ?_) (by simp)
  by_cases h : f x = 0 <;> simp [h, hx]

variable (ğ•œ) in
/-- If `f` converges along `l` to a finite limit `x`, then `f =O[ğ•œ, l] 1`. -/
lemma Filter.Tendsto.isBigOTVS_one [ContinuousAdd E] [ContinuousSMul ğ•œ E] {x : E}
    (h : Tendsto f l (ğ“ x)) : f =O[ğ•œ; l] (fun _ â†¦ 1 : Î± â†’ ğ•œ) := by
  replace h : Tendsto (f Â· - x) l (ğ“ 0) := by
    simpa [sub_eq_add_neg] using h.add (tendsto_const_nhds (x := -x))
  rw [(nhds_basis_balanced ğ•œ E).add_self.isBigOTVS_iff nhds_basis_ball]
  rintro U âŸ¨hUâ‚€, hUbâŸ©
  obtain âŸ¨r, hrâ‚€, hrâ‚, hrâŸ© : âˆƒ r : â„â‰¥0, 0 < r âˆ§ r â‰¤ 1 âˆ§ (r : â„â‰¥0âˆ) â‰¤ (egauge ğ•œ U x)â»Â¹ := by
    apply Eventually.exists_gt
    refine .and (eventually_le_nhds one_pos) ?_
    refine (ENNReal.tendsto_coe.mpr tendsto_id).eventually_le_const ?_
    suffices âˆƒ c : ğ•œ, x âˆˆ c â€¢ U by simpa [egauge_eq_top]
    simpa using (absorbent_nhds_zero (ğ•œ := ğ•œ) hUâ‚€ x).exists
  use r, by positivity
  filter_upwards [h.eventually_mem hUâ‚€] with a ha
  calc
    egauge ğ•œ (U + U) (f a) â‰¤ max (egauge ğ•œ U (f a - x)) (egauge ğ•œ U x) := by
      simpa using egauge_add_add_le hUb hUb (f a - x) x
    _ â‰¤ (r : â„â‰¥0âˆ)â»Â¹ := by
      apply max_le
      Â· refine (egauge_le_one _ ha).trans ?_
        simp [one_le_invâ‚€ hrâ‚€, hrâ‚]
      Â· rwa [ENNReal.le_inv_iff_le_inv]
    _ â‰¤ egauge ğ•œ (ball (0 : ğ•œ) _) 1 := by simpa using div_le_egauge_ball ğ•œ r (1 : ğ•œ)

end TopologicalSpace

section NormedSpace

variable [NontriviallyNormedField ğ•œ]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

lemma isLittleOTVS_iff_isLittleO {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±} :
    f =o[ğ•œ; l] g â†” f =o[l] g := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hc : 1 < â€–câ€–â‚ŠâŸ©
  have hcâ‚€ : 0 < â€–câ€–â‚Š := one_pos.trans hc
  simp only [isLittleO_iff, nhds_basis_ball.isLittleOTVS_iff nhds_basis_ball]
  refine âŸ¨fun h Îµ hÎµ â†¦ ?_, fun h Îµ hÎµ â†¦ âŸ¨1, one_pos, fun Î´ hÎ´ â†¦ ?_âŸ©âŸ©
  Â· rcases h Îµ hÎµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    lift Îµ to â„â‰¥0 using hÎµ.le; lift Î´ to â„â‰¥0 using hÎ´â‚€.le; norm_cast at hÎµ hÎ´â‚€
    filter_upwards [hÎ´ (Î´ / â€–câ€–â‚Š) (div_pos hÎ´â‚€ hcâ‚€).ne'] with x hx
    suffices (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ â€–g xâ€–â‚Š by
      rw [â† ENNReal.coe_div hÎµ.ne'] at this
      rw [â† div_le_iffâ‚€' (NNReal.coe_pos.2 hÎµ)]
      exact_mod_cast this
    calc
      (â€–f xâ€–â‚Š / Îµ : â„â‰¥0âˆ) â‰¤ egauge ğ•œ (ball 0 Îµ) (f x) := div_le_egauge_ball ğ•œ _ _
      _ â‰¤ â†‘(Î´ / â€–câ€–â‚Š) * egauge ğ•œ (ball 0 â†‘Î´) (g x) := hx
      _ â‰¤ (Î´ / â€–câ€–â‚Š) * (â€–câ€–â‚Š * â€–g xâ€–â‚Š / Î´) := by
        gcongr
        exacts [ENNReal.coe_div_le, egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎ´â‚€)]
      _ = (Î´ / Î´) * (â€–câ€–â‚Š / â€–câ€–â‚Š) * â€–g xâ€–â‚Š := by simp only [div_eq_mul_inv]; ring
      _ â‰¤ 1 * 1 * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = â€–g xâ€–â‚Š := by simp
  Â· filter_upwards [@h â†‘(Îµ * Î´ / â€–câ€–â‚Š) (by positivity)] with x (hx : â€–f xâ€–â‚Š â‰¤ Îµ * Î´ / â€–câ€–â‚Š * â€–g xâ€–â‚Š)
    lift Îµ to â„â‰¥0 using hÎµ.le
    calc
      egauge ğ•œ (ball 0 Îµ) (f x) â‰¤ â€–câ€–â‚Š * â€–f xâ€–â‚Š / Îµ :=
        egauge_ball_le_of_one_lt_norm hc (.inl <| ne_of_gt hÎµ)
      _ â‰¤ â€–câ€–â‚Š * (â†‘(Îµ * Î´ / â€–câ€–â‚Š) * â€–g xâ€–â‚Š) / Îµ := by gcongr; exact_mod_cast hx
      _ = (â€–câ€–â‚Š / â€–câ€–â‚Š) * (Îµ / Îµ) * Î´ * â€–g xâ€–â‚Š := by
        simp only [div_eq_mul_inv, ENNReal.coe_inv hcâ‚€.ne', ENNReal.coe_mul]; ring
      _ â‰¤ 1 * 1 * Î´ * â€–g xâ€–â‚Š := by gcongr <;> exact ENNReal.div_self_le_one
      _ = Î´ * â€–g xâ€–â‚Š := by simp
      _ â‰¤ Î´ * egauge ğ•œ (ball 0 1) (g x) := by gcongr; apply le_egauge_ball_one

alias âŸ¨isLittleOTVS.isLittleO, IsLittleO.isLittleOTVSâŸ© := isLittleOTVS_iff_isLittleO

end NormedSpace

end Asymptotics
