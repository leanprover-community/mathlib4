/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.Defs
import Mathlib.Analysis.Normed.Group.Bounded
import Mathlib.Analysis.Normed.Group.InfiniteSum
import Mathlib.Analysis.Normed.MulAction
import Mathlib.Topology.PartialHomeomorph

/-!
# Further basic lemmas about asymptotics

-/

open Set Topology Filter NNReal

namespace Asymptotics


variable {Î± : Type*} {Î² : Type*} {E : Type*} {F : Type*} {G : Type*} {E' : Type*}
  {F' : Type*} {G' : Type*} {E'' : Type*} {F'' : Type*} {G'' : Type*} {E''' : Type*}
  {R : Type*} {R' : Type*} {ğ•œ : Type*} {ğ•œ' : Type*}

variable [Norm E] [Norm F] [Norm G]
variable [SeminormedAddCommGroup E'] [SeminormedAddCommGroup F'] [SeminormedAddCommGroup G']
  [NormedAddCommGroup E''] [NormedAddCommGroup F''] [NormedAddCommGroup G''] [SeminormedRing R]
  [SeminormedAddGroup E''']
  [SeminormedRing R']

variable [NormedDivisionRing ğ•œ] [NormedDivisionRing ğ•œ']
variable {c c' câ‚ câ‚‚ : â„} {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G}
variable {f' : Î± â†’ E'} {g' : Î± â†’ F'} {k' : Î± â†’ G'}
variable {f'' : Î± â†’ E''} {g'' : Î± â†’ F''} {k'' : Î± â†’ G''}
variable {l l' : Filter Î±}
@[simp]
theorem isBigOWith_principal {s : Set Î±} : IsBigOWith c (ğ“Ÿ s) f g â†” âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ c * â€–g xâ€– := by
  rw [IsBigOWith_def, eventually_principal]

theorem isBigO_principal {s : Set Î±} : f =O[ğ“Ÿ s] g â†” âˆƒ c, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ c * â€–g xâ€– := by
  simp_rw [isBigO_iff, eventually_principal]

@[simp]
theorem isLittleO_principal {s : Set Î±} : f'' =o[ğ“Ÿ s] g' â†” âˆ€ x âˆˆ s, f'' x = 0 := by
  refine âŸ¨fun h x hx â†¦ norm_le_zero_iff.1 ?_, fun h â†¦ ?_âŸ©
  Â· simp only [isLittleO_iff] at h
    have : Tendsto (fun c : â„ => c * â€–g' xâ€–) (ğ“[>] 0) (ğ“ 0) :=
      ((continuous_id.mul continuous_const).tendsto' _ _ (zero_mul _)).mono_left
        inf_le_left
    apply le_of_tendsto_of_tendsto tendsto_const_nhds this
    apply eventually_nhdsWithin_iff.2 (Eventually.of_forall (fun c hc â†¦ ?_))
    exact eventually_principal.1 (h hc) x hx
  Â· apply (isLittleO_zero g' _).congr' ?_ EventuallyEq.rfl
    exact fun x hx â†¦ (h x hx).symm

@[simp]
theorem isBigOWith_top : IsBigOWith c âŠ¤ f g â†” âˆ€ x, â€–f xâ€– â‰¤ c * â€–g xâ€– := by
  rw [IsBigOWith_def, eventually_top]

@[simp]
theorem isBigO_top : f =O[âŠ¤] g â†” âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–g xâ€– := by
  simp_rw [isBigO_iff, eventually_top]

@[simp]
theorem isLittleO_top : f'' =o[âŠ¤] g' â†” âˆ€ x, f'' x = 0 := by
  simp only [â† principal_univ, isLittleO_principal, mem_univ, forall_true_left]

section

variable (F)
variable [One F] [NormOneClass F]

theorem isBigOWith_const_one (c : E) (l : Filter Î±) :
    IsBigOWith â€–câ€– l (fun _x : Î± => c) fun _x => (1 : F) := by simp [isBigOWith_iff]

theorem isBigO_const_one (c : E) (l : Filter Î±) : (fun _x : Î± => c) =O[l] fun _x => (1 : F) :=
  (isBigOWith_const_one F c l).isBigO

theorem isLittleO_const_iff_isLittleO_one {c : F''} (hc : c â‰  0) :
    (f =o[l] fun _x => c) â†” f =o[l] fun _x => (1 : F) :=
  âŸ¨fun h => h.trans_isBigOWith (isBigOWith_const_one _ _ _) (norm_pos_iff.2 hc),
   fun h => h.trans_isBigO <| isBigO_const_const _ hc _âŸ©

@[simp]
theorem isLittleO_one_iff {f : Î± â†’ E'''} : f =o[l] (fun _x => 1 : Î± â†’ F) â†” Tendsto f l (ğ“ 0) := by
  simp only [isLittleO_iff, norm_one, mul_one, Metric.nhds_basis_closedBall.tendsto_right_iff,
    Metric.mem_closedBall, dist_zero_right]

@[simp]
theorem isBigO_one_iff : f =O[l] (fun _x => 1 : Î± â†’ F) â†”
    IsBoundedUnder (Â· â‰¤ Â·) l fun x => â€–f xâ€– := by
  simp only [isBigO_iff, norm_one, mul_one, IsBoundedUnder, IsBounded, eventually_map]

alias âŸ¨_, _root_.Filter.IsBoundedUnder.isBigO_oneâŸ© := isBigO_one_iff

@[simp]
theorem isLittleO_one_left_iff : (fun _x => 1 : Î± â†’ F) =o[l] f â†” Tendsto (fun x => â€–f xâ€–) l atTop :=
  calc
    (fun _x => 1 : Î± â†’ F) =o[l] f â†” âˆ€ n : â„•, âˆ€á¶  x in l, â†‘n * â€–(1 : F)â€– â‰¤ â€–f xâ€– :=
      isLittleO_iff_nat_mul_le_aux <| Or.inl fun _x => by simp only [norm_one, zero_le_one]
    _ â†” âˆ€ n : â„•, True â†’ âˆ€á¶  x in l, â€–f xâ€– âˆˆ Ici (n : â„) := by
      simp only [norm_one, mul_one, true_imp_iff, mem_Ici]
    _ â†” Tendsto (fun x => â€–f xâ€–) l atTop :=
      atTop_hasCountableBasis_of_archimedean.1.tendsto_right_iff.symm

theorem _root_.Filter.Tendsto.isBigO_one {c : E'} (h : Tendsto f' l (ğ“ c)) :
    f' =O[l] (fun _x => 1 : Î± â†’ F) :=
  h.norm.isBoundedUnder_le.isBigO_one F

theorem IsBigO.trans_tendsto_nhds (hfg : f =O[l] g') {y : F'} (hg : Tendsto g' l (ğ“ y)) :
    f =O[l] (fun _x => 1 : Î± â†’ F) :=
  hfg.trans <| hg.isBigO_one F

/-- The condition `f = O[ğ“[â‰ ] a] 1` is equivalent to `f = O[ğ“ a] 1`. -/
lemma isBigO_one_nhds_ne_iff [TopologicalSpace Î±] {a : Î±} :
    f =O[ğ“[â‰ ] a] (fun _ â†¦ 1 : Î± â†’ F) â†” f =O[ğ“ a] (fun _ â†¦ 1 : Î± â†’ F) := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h.mono nhdsWithin_le_nhdsâŸ©
  simp only [isBigO_one_iff, IsBoundedUnder, IsBounded, eventually_map] at h âŠ¢
  obtain âŸ¨c, hcâŸ© := h
  use max c â€–f aâ€–
  filter_upwards [eventually_nhdsWithin_iff.mp hc] with b hb
  rcases eq_or_ne b a with rfl | hb'
  Â· apply le_max_right
  Â· exact (hb hb').trans (le_max_left ..)

end

theorem isLittleO_const_iff {c : F''} (hc : c â‰  0) :
    (f'' =o[l] fun _x => c) â†” Tendsto f'' l (ğ“ 0) :=
  (isLittleO_const_iff_isLittleO_one â„ hc).trans (isLittleO_one_iff _)

theorem isLittleO_id_const {c : F''} (hc : c â‰  0) : (fun x : E'' => x) =o[ğ“ 0] fun _x => c :=
  (isLittleO_const_iff hc).mpr (continuous_id.tendsto 0)

theorem _root_.Filter.IsBoundedUnder.isBigO_const (h : IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f))
    {c : F''} (hc : c â‰  0) : f =O[l] fun _x => c :=
  (h.isBigO_one â„).trans (isBigO_const_const _ hc _)

theorem isBigO_const_of_tendsto {y : E''} (h : Tendsto f'' l (ğ“ y)) {c : F''} (hc : c â‰  0) :
    f'' =O[l] fun _x => c :=
  h.norm.isBoundedUnder_le.isBigO_const hc

theorem IsBigO.isBoundedUnder_le {c : F} (h : f =O[l] fun _x => c) :
    IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f) :=
  let âŸ¨c', hc'âŸ© := h.bound
  âŸ¨c' * â€–câ€–, eventually_map.2 hc'âŸ©

theorem isBigO_const_of_ne {c : F''} (hc : c â‰  0) :
    (f =O[l] fun _x => c) â†” IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f) :=
  âŸ¨fun h => h.isBoundedUnder_le, fun h => h.isBigO_const hcâŸ©

theorem isBigO_const_iff {c : F''} : (f'' =O[l] fun _x => c) â†”
    (c = 0 â†’ f'' =á¶ [l] 0) âˆ§ IsBoundedUnder (Â· â‰¤ Â·) l fun x => â€–f'' xâ€– := by
  refine âŸ¨fun h => âŸ¨fun hc => isBigO_zero_right_iff.1 (by rwa [â† hc]), h.isBoundedUnder_leâŸ©, ?_âŸ©
  rintro âŸ¨hcf, hfâŸ©
  rcases eq_or_ne c 0 with (hc | hc)
  exacts [(hcf hc).trans_isBigO (isBigO_zero _ _), hf.isBigO_const hc]

theorem isBigO_iff_isBoundedUnder_le_div (h : âˆ€á¶  x in l, g'' x â‰  0) :
    f =O[l] g'' â†” IsBoundedUnder (Â· â‰¤ Â·) l fun x => â€–f xâ€– / â€–g'' xâ€– := by
  simp only [isBigO_iff, IsBoundedUnder, IsBounded, eventually_map]
  exact
    exists_congr fun c =>
      eventually_congr <| h.mono fun x hx => (div_le_iffâ‚€ <| norm_pos_iff.2 hx).symm

/-- `(fun x â†¦ c) =O[l] f` if and only if `f` is bounded away from zero. -/
theorem isBigO_const_left_iff_pos_le_norm {c : E''} (hc : c â‰  0) :
    (fun _x => c) =O[l] f' â†” âˆƒ b, 0 < b âˆ§ âˆ€á¶  x in l, b â‰¤ â€–f' xâ€– := by
  constructor
  Â· intro h
    rcases h.exists_pos with âŸ¨C, hCâ‚€, hCâŸ©
    refine âŸ¨â€–câ€– / C, div_pos (norm_pos_iff.2 hc) hCâ‚€, ?_âŸ©
    exact hC.bound.mono fun x => (div_le_iffâ‚€' hCâ‚€).2
  Â· rintro âŸ¨b, hbâ‚€, hbâŸ©
    refine IsBigO.of_bound (â€–câ€– / b) (hb.mono fun x hx => ?_)
    rw [div_mul_eq_mul_div, mul_div_assoc]
    exact le_mul_of_one_le_right (norm_nonneg _) ((one_le_div hbâ‚€).2 hx)

theorem IsBigO.trans_tendsto (hfg : f'' =O[l] g'') (hg : Tendsto g'' l (ğ“ 0)) :
    Tendsto f'' l (ğ“ 0) :=
  (isLittleO_one_iff â„).1 <| hfg.trans_isLittleO <| (isLittleO_one_iff â„).2 hg

theorem IsLittleO.trans_tendsto (hfg : f'' =o[l] g'') (hg : Tendsto g'' l (ğ“ 0)) :
    Tendsto f'' l (ğ“ 0) :=
  hfg.isBigO.trans_tendsto hg

lemma isLittleO_id_one [One F''] [NeZero (1 : F'')] : (fun x : E'' => x) =o[ğ“ 0] (1 : E'' â†’ F'') :=
  isLittleO_id_const one_ne_zero

theorem continuousAt_iff_isLittleO {Î± : Type*} {E : Type*} [NormedRing E] [NormOneClass E]
    [TopologicalSpace Î±] {f : Î± â†’ E} {x : Î±} :
    (ContinuousAt f x) â†” (fun (y : Î±) â†¦ f y - f x) =o[ğ“ x] (fun (_ : Î±) â†¦ (1 : E)) := by
  simp [ContinuousAt, â† tendsto_sub_nhds_zero_iff]

/-! ### Multiplication -/

theorem IsBigO.of_pow {f : Î± â†’ ğ•œ} {g : Î± â†’ R} {n : â„•} (hn : n â‰  0) (h : (f ^ n) =O[l] (g ^ n)) :
    f =O[l] g := by
  rcases h.exists_pos with âŸ¨C, _hCâ‚€, hCâŸ©
  obtain âŸ¨c : â„, hcâ‚€ : 0 â‰¤ c, hc : C â‰¤ c ^ nâŸ© :=
    ((eventually_ge_atTop _).and <| (tendsto_pow_atTop hn).eventually_ge_atTop C).exists
  exact (hC.of_pow hn hc hcâ‚€).isBigO

/-! ### Scalar multiplication -/

section SMulConst

variable [Module R E'] [IsBoundedSMul R E']

theorem IsBigOWith.const_smul_self (c' : R) :
    IsBigOWith (â€–c'â€–) l (fun x => c' â€¢ f' x) f' :=
  isBigOWith_of_le' _ fun _ => norm_smul_le _ _

theorem IsBigO.const_smul_self (c' : R) : (fun x => c' â€¢ f' x) =O[l] f' :=
  (IsBigOWith.const_smul_self _).isBigO

theorem IsBigOWith.const_smul_left (h : IsBigOWith c l f' g) (c' : R) :
    IsBigOWith (â€–c'â€– * c) l (fun x => c' â€¢ f' x) g :=
  .trans (.const_smul_self _) h (norm_nonneg _)

theorem IsBigO.const_smul_left (h : f' =O[l] g) (c : R) : (c â€¢ f') =O[l] g :=
  let âŸ¨_b, hbâŸ© := h.isBigOWith
  (hb.const_smul_left _).isBigO

theorem IsLittleO.const_smul_left (h : f' =o[l] g) (c : R) : (c â€¢ f') =o[l] g :=
  (IsBigO.const_smul_self _).trans_isLittleO h

variable [Module ğ•œ E'] [NormSMulClass ğ•œ E']

theorem isBigO_const_smul_left {c : ğ•œ} (hc : c â‰  0) : (fun x => c â€¢ f' x) =O[l] g â†” f' =O[l] g := by
  have cne0 : â€–câ€– â‰  0 := norm_ne_zero_iff.mpr hc
  rw [â† isBigO_norm_left]
  simp only [norm_smul]
  rw [isBigO_const_mul_left_iff cne0, isBigO_norm_left]

theorem isLittleO_const_smul_left {c : ğ•œ} (hc : c â‰  0) :
    (fun x => c â€¢ f' x) =o[l] g â†” f' =o[l] g := by
  have cne0 : â€–câ€– â‰  0 := norm_ne_zero_iff.mpr hc
  rw [â† isLittleO_norm_left]
  simp only [norm_smul]
  rw [isLittleO_const_mul_left_iff cne0, isLittleO_norm_left]

theorem isBigO_const_smul_right {c : ğ•œ} (hc : c â‰  0) :
    (f =O[l] fun x => c â€¢ f' x) â†” f =O[l] f' := by
  have cne0 : â€–câ€– â‰  0 := norm_ne_zero_iff.mpr hc
  rw [â† isBigO_norm_right]
  simp only [norm_smul]
  rw [isBigO_const_mul_right_iff cne0, isBigO_norm_right]

theorem isLittleO_const_smul_right {c : ğ•œ} (hc : c â‰  0) :
    (f =o[l] fun x => c â€¢ f' x) â†” f =o[l] f' := by
  have cne0 : â€–câ€– â‰  0 := norm_ne_zero_iff.mpr hc
  rw [â† isLittleO_norm_right]
  simp only [norm_smul]
  rw [isLittleO_const_mul_right_iff cne0, isLittleO_norm_right]

end SMulConst

section SMul

variable [Module R E'] [IsBoundedSMul R E'] [Module ğ•œ' F'] [NormSMulClass ğ•œ' F']
variable {kâ‚ : Î± â†’ R} {kâ‚‚ : Î± â†’ ğ•œ'}

theorem IsBigOWith.smul (hâ‚ : IsBigOWith c l kâ‚ kâ‚‚) (hâ‚‚ : IsBigOWith c' l f' g') :
    IsBigOWith (c * c') l (fun x => kâ‚ x â€¢ f' x) fun x => kâ‚‚ x â€¢ g' x := by
  simp only [IsBigOWith_def] at *
  filter_upwards [hâ‚, hâ‚‚] with _ hxâ‚ hxâ‚‚
  apply le_trans (norm_smul_le _ _)
  convert mul_le_mul hxâ‚ hxâ‚‚ (norm_nonneg _) (le_trans (norm_nonneg _) hxâ‚) using 1
  rw [norm_smul, mul_mul_mul_comm]

theorem IsBigO.smul (hâ‚ : kâ‚ =O[l] kâ‚‚) (hâ‚‚ : f' =O[l] g') :
    (fun x => kâ‚ x â€¢ f' x) =O[l] fun x => kâ‚‚ x â€¢ g' x := by
  obtain âŸ¨câ‚, hâ‚âŸ© := hâ‚.isBigOWith
  obtain âŸ¨câ‚‚, hâ‚‚âŸ© := hâ‚‚.isBigOWith
  exact (hâ‚.smul hâ‚‚).isBigO

theorem IsBigO.smul_isLittleO (hâ‚ : kâ‚ =O[l] kâ‚‚) (hâ‚‚ : f' =o[l] g') :
    (fun x => kâ‚ x â€¢ f' x) =o[l] fun x => kâ‚‚ x â€¢ g' x := by
  simp only [IsLittleO_def] at *
  intro c cpos
  rcases hâ‚.exists_pos with âŸ¨c', c'pos, hc'âŸ©
  exact (hc'.smul (hâ‚‚ (div_pos cpos c'pos))).congr_const (mul_div_cancelâ‚€ _ (ne_of_gt c'pos))

theorem IsLittleO.smul_isBigO (hâ‚ : kâ‚ =o[l] kâ‚‚) (hâ‚‚ : f' =O[l] g') :
    (fun x => kâ‚ x â€¢ f' x) =o[l] fun x => kâ‚‚ x â€¢ g' x := by
  simp only [IsLittleO_def] at *
  intro c cpos
  rcases hâ‚‚.exists_pos with âŸ¨c', c'pos, hc'âŸ©
  exact ((hâ‚ (div_pos cpos c'pos)).smul hc').congr_const (div_mul_cancelâ‚€ _ (ne_of_gt c'pos))

theorem IsLittleO.smul (hâ‚ : kâ‚ =o[l] kâ‚‚) (hâ‚‚ : f' =o[l] g') :
    (fun x => kâ‚ x â€¢ f' x) =o[l] fun x => kâ‚‚ x â€¢ g' x :=
  hâ‚.smul_isBigO hâ‚‚.isBigO

end SMul

section Prod
variable {Î¹ : Type*}

theorem IsBigO.listProd {L : List Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ}
    (hf : âˆ€ i âˆˆ L, f i =O[l] g i) :
    (fun x â†¦ (L.map (f Â· x)).prod) =O[l] (fun x â†¦ (L.map (g Â· x)).prod) := by
  induction L with
  | nil => simp [isBoundedUnder_const]
  | cons i L ihL =>
    simp only [List.map_cons, List.prod_cons, List.forall_mem_cons] at hf âŠ¢
    exact hf.1.mul (ihL hf.2)

theorem IsBigO.multisetProd {R ğ•œ : Type*} [SeminormedCommRing R] [NormedField ğ•œ]
    {s : Multiset Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ} (hf : âˆ€ i âˆˆ s, f i =O[l] g i) :
    (fun x â†¦ (s.map (f Â· x)).prod) =O[l] (fun x â†¦ (s.map (g Â· x)).prod) := by
  obtain âŸ¨l, rflâŸ© : âˆƒ l : List Î¹, â†‘l = s := Quotient.mk_surjective s
  exact mod_cast IsBigO.listProd hf

theorem IsBigO.finsetProd {R ğ•œ : Type*} [SeminormedCommRing R] [NormedField ğ•œ]
    {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ}
    (hf : âˆ€ i âˆˆ s, f i =O[l] g i) : (âˆ i âˆˆ s, f i Â·) =O[l] (âˆ i âˆˆ s, g i Â·) :=
  .multisetProd hf

theorem IsLittleO.listProd {L : List Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ}
    (hâ‚ : âˆ€ i âˆˆ L, f i =O[l] g i) (hâ‚‚ : âˆƒ i âˆˆ L, f i =o[l] g i) :
    (fun x â†¦ (L.map (f Â· x)).prod) =o[l] (fun x â†¦ (L.map (g Â· x)).prod) := by
  induction L with
  | nil => simp at hâ‚‚
  | cons i L ihL =>
    simp only [List.map_cons, List.prod_cons, List.forall_mem_cons, List.exists_mem_cons_iff]
      at hâ‚ hâ‚‚ âŠ¢
    cases hâ‚‚ with
    | inl hi => exact hi.mul_isBigO <| .listProd hâ‚.2
    | inr hL => exact hâ‚.1.mul_isLittleO <| ihL hâ‚.2 hL

theorem IsLittleO.multisetProd {R ğ•œ : Type*} [SeminormedCommRing R] [NormedField ğ•œ]
    {s : Multiset Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ} (hâ‚ : âˆ€ i âˆˆ s, f i =O[l] g i)
    (hâ‚‚ : âˆƒ i âˆˆ s, f i =o[l] g i) :
    (fun x â†¦ (s.map (f Â· x)).prod) =o[l] (fun x â†¦ (s.map (g Â· x)).prod) := by
  obtain âŸ¨l, rflâŸ© : âˆƒ l : List Î¹, â†‘l = s := Quotient.mk_surjective s
  exact mod_cast IsLittleO.listProd hâ‚ hâ‚‚

theorem IsLittleO.finsetProd {R ğ•œ : Type*} [SeminormedCommRing R] [NormedField ğ•œ]
    {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ R} {g : Î¹ â†’ Î± â†’ ğ•œ} (hâ‚ : âˆ€ i âˆˆ s, f i =O[l] g i)
    (hâ‚‚ : âˆƒ i âˆˆ s, f i =o[l] g i) : (âˆ i âˆˆ s, f i Â·) =o[l] (âˆ i âˆˆ s, g i Â·) :=
  .multisetProd hâ‚ hâ‚‚

end Prod

/-! ### Relation between `f = o(g)` and `f / g â†’ 0` -/

theorem IsLittleO.tendsto_div_nhds_zero {f g : Î± â†’ ğ•œ} (h : f =o[l] g) :
    Tendsto (fun x => f x / g x) l (ğ“ 0) :=
  (isLittleO_one_iff ğ•œ).mp <| by
    calc
      (fun x => f x / g x) =o[l] fun x => g x / g x := by
        simpa only [div_eq_mul_inv] using h.mul_isBigO (isBigO_refl _ _)
      _ =O[l] fun _x => (1 : ğ•œ) := isBigO_of_le _ fun x => by simp [div_self_le_one]

theorem IsLittleO.tendsto_inv_smul_nhds_zero [Module ğ•œ E'] [NormSMulClass ğ•œ E']
    {f : Î± â†’ E'} {g : Î± â†’ ğ•œ}
    {l : Filter Î±} (h : f =o[l] g) : Tendsto (fun x => (g x)â»Â¹ â€¢ f x) l (ğ“ 0) := by
  simpa only [div_eq_inv_mul, â† norm_inv, â† norm_smul, â† tendsto_zero_iff_norm_tendsto_zero] using
    h.norm_norm.tendsto_div_nhds_zero

theorem isLittleO_iff_tendsto' {f g : Î± â†’ ğ•œ} (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0) :
    f =o[l] g â†” Tendsto (fun x => f x / g x) l (ğ“ 0) :=
  âŸ¨IsLittleO.tendsto_div_nhds_zero, fun h =>
    (((isLittleO_one_iff _).mpr h).mul_isBigO (isBigO_refl g l)).congr'
      (hgf.mono fun _x => div_mul_cancel_of_imp) (Eventually.of_forall fun _x => one_mul _)âŸ©

theorem isLittleO_iff_tendsto {f g : Î± â†’ ğ•œ} (hgf : âˆ€ x, g x = 0 â†’ f x = 0) :
    f =o[l] g â†” Tendsto (fun x => f x / g x) l (ğ“ 0) :=
  isLittleO_iff_tendsto' (Eventually.of_forall hgf)

alias âŸ¨_, isLittleO_of_tendsto'âŸ© := isLittleO_iff_tendsto'

alias âŸ¨_, isLittleO_of_tendstoâŸ© := isLittleO_iff_tendsto

theorem isLittleO_const_left_of_ne {c : E''} (hc : c â‰  0) :
    (fun _x => c) =o[l] g â†” Tendsto (fun x => â€–g xâ€–) l atTop := by
  simp only [â† isLittleO_one_left_iff â„]
  exact âŸ¨(isBigO_const_const (1 : â„) hc l).trans_isLittleO,
    (isBigO_const_one â„ c l).trans_isLittleOâŸ©

@[simp]
theorem isLittleO_const_left {c : E''} :
    (fun _x => c) =o[l] g'' â†” c = 0 âˆ¨ Tendsto (norm âˆ˜ g'') l atTop := by
  rcases eq_or_ne c 0 with (rfl | hc)
  Â· simp only [isLittleO_zero, true_or]
  Â· simp only [hc, false_or, isLittleO_const_left_of_ne hc]; rfl

@[simp 1001] -- Porting note: increase priority so that this triggers before `isLittleO_const_left`
theorem isLittleO_const_const_iff [NeBot l] {d : E''} {c : F''} :
    ((fun _x => d) =o[l] fun _x => c) â†” d = 0 := by
  have : Â¬Tendsto (Function.const Î± â€–câ€–) l atTop :=
    not_tendsto_atTop_of_tendsto_nhds tendsto_const_nhds
  simp only [isLittleO_const_left, or_iff_left_iff_imp]
  exact fun h => (this h).elim

@[simp]
theorem isLittleO_pure {x} : f'' =o[pure x] g'' â†” f'' x = 0 :=
  calc
    f'' =o[pure x] g'' â†” (fun _y : Î± => f'' x) =o[pure x] fun _ => g'' x := isLittleO_congr rfl rfl
    _ â†” f'' x = 0 := isLittleO_const_const_iff

theorem isLittleO_const_id_cobounded (c : F'') :
    (fun _ => c) =o[Bornology.cobounded E''] id :=
  isLittleO_const_left.2 <| .inr tendsto_norm_cobounded_atTop

theorem isLittleO_const_id_atTop (c : E'') : (fun _x : â„ => c) =o[atTop] id :=
  isLittleO_const_left.2 <| Or.inr tendsto_abs_atTop_atTop

theorem isLittleO_const_id_atBot (c : E'') : (fun _x : â„ => c) =o[atBot] id :=
  isLittleO_const_left.2 <| Or.inr tendsto_abs_atBot_atTop

/-! ### Equivalent definitions of the form `âˆƒ Ï†, u =á¶ [l] Ï† * v` in a `NormedField`. -/

section ExistsMulEq

variable {u v : Î± â†’ ğ•œ}

/-- If `â€–Ï†â€–` is eventually bounded by `c`, and `u =á¶ [l] Ï† * v`, then we have `IsBigOWith c u v l`.
This does not require any assumptions on `c`, which is why we keep this version along with
`IsBigOWith_iff_exists_eq_mul`. -/
theorem isBigOWith_of_eq_mul {u v : Î± â†’ R} (Ï† : Î± â†’ R) (hÏ† : âˆ€á¶  x in l, â€–Ï† xâ€– â‰¤ c)
    (h : u =á¶ [l] Ï† * v) :
    IsBigOWith c l u v := by
  simp only [IsBigOWith_def]
  refine h.symm.rw (fun x a => â€–aâ€– â‰¤ c * â€–v xâ€–) (hÏ†.mono fun x hx => ?_)
  simp only [Pi.mul_apply]
  refine (norm_mul_le _ _).trans ?_
  gcongr

theorem isBigOWith_iff_exists_eq_mul (hc : 0 â‰¤ c) :
    IsBigOWith c l u v â†” âˆƒ Ï† : Î± â†’ ğ•œ, (âˆ€á¶  x in l, â€–Ï† xâ€– â‰¤ c) âˆ§ u =á¶ [l] Ï† * v := by
  constructor
  Â· intro h
    use fun x => u x / v x
    refine âŸ¨Eventually.mono h.bound fun y hy => ?_, h.eventually_mul_div_cancel.symmâŸ©
    simpa using div_le_of_le_mulâ‚€ (norm_nonneg _) hc hy
  Â· rintro âŸ¨Ï†, hÏ†, hâŸ©
    exact isBigOWith_of_eq_mul Ï† hÏ† h

theorem IsBigOWith.exists_eq_mul (h : IsBigOWith c l u v) (hc : 0 â‰¤ c) :
    âˆƒ Ï† : Î± â†’ ğ•œ, (âˆ€á¶  x in l, â€–Ï† xâ€– â‰¤ c) âˆ§ u =á¶ [l] Ï† * v :=
  (isBigOWith_iff_exists_eq_mul hc).mp h

theorem isBigO_iff_exists_eq_mul :
    u =O[l] v â†” âˆƒ Ï† : Î± â†’ ğ•œ, l.IsBoundedUnder (Â· â‰¤ Â·) (norm âˆ˜ Ï†) âˆ§ u =á¶ [l] Ï† * v := by
  constructor
  Â· rintro h
    rcases h.exists_nonneg with âŸ¨c, hnnc, hcâŸ©
    rcases hc.exists_eq_mul hnnc with âŸ¨Ï†, hÏ†, huvÏ†âŸ©
    exact âŸ¨Ï†, âŸ¨c, hÏ†âŸ©, huvÏ†âŸ©
  Â· rintro âŸ¨Ï†, âŸ¨c, hÏ†âŸ©, huvÏ†âŸ©
    exact isBigO_iff_isBigOWith.2 âŸ¨c, isBigOWith_of_eq_mul Ï† hÏ† huvÏ†âŸ©

alias âŸ¨IsBigO.exists_eq_mul, _âŸ© := isBigO_iff_exists_eq_mul

theorem isLittleO_iff_exists_eq_mul :
    u =o[l] v â†” âˆƒ Ï† : Î± â†’ ğ•œ, Tendsto Ï† l (ğ“ 0) âˆ§ u =á¶ [l] Ï† * v := by
  constructor
  Â· exact fun h => âŸ¨fun x => u x / v x, h.tendsto_div_nhds_zero, h.eventually_mul_div_cancel.symmâŸ©
  Â· simp only [IsLittleO_def]
    rintro âŸ¨Ï†, hÏ†, huvÏ†âŸ© c hpos
    rw [NormedAddCommGroup.tendsto_nhds_zero] at hÏ†
    exact isBigOWith_of_eq_mul _ ((hÏ† c hpos).mono fun x => le_of_lt) huvÏ†

alias âŸ¨IsLittleO.exists_eq_mul, _âŸ© := isLittleO_iff_exists_eq_mul

end ExistsMulEq

/-! ### Miscellaneous lemmas -/

theorem div_isBoundedUnder_of_isBigO {Î± : Type*} {l : Filter Î±} {f g : Î± â†’ ğ•œ} (h : f =O[l] g) :
    IsBoundedUnder (Â· â‰¤ Â·) l fun x => â€–f x / g xâ€– := by
  obtain âŸ¨c, hâ‚€, hcâŸ© := h.exists_nonneg
  refine âŸ¨c, eventually_map.2 (hc.bound.mono fun x hx => ?_)âŸ©
  rw [norm_div]
  exact div_le_of_le_mulâ‚€ (norm_nonneg _) hâ‚€ hx

theorem isBigO_iff_div_isBoundedUnder {Î± : Type*} {l : Filter Î±} {f g : Î± â†’ ğ•œ}
    (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0) :
    f =O[l] g â†” IsBoundedUnder (Â· â‰¤ Â·) l fun x => â€–f x / g xâ€– := by
  refine âŸ¨div_isBoundedUnder_of_isBigO, fun h => ?_âŸ©
  obtain âŸ¨c, hcâŸ© := h
  simp only [eventually_map, norm_div] at hc
  refine IsBigO.of_bound c (hc.mp <| hgf.mono fun x hxâ‚ hxâ‚‚ => ?_)
  by_cases hgx : g x = 0
  Â· simp [hxâ‚ hgx, hgx]
  Â· exact (div_le_iffâ‚€ (norm_pos_iff.2 hgx)).mp hxâ‚‚

theorem isBigO_of_div_tendsto_nhds {Î± : Type*} {l : Filter Î±} {f g : Î± â†’ ğ•œ}
    (hgf : âˆ€á¶  x in l, g x = 0 â†’ f x = 0) (c : ğ•œ) (H : Filter.Tendsto (f / g) l (ğ“ c)) :
    f =O[l] g :=
  (isBigO_iff_div_isBoundedUnder hgf).2 <| H.norm.isBoundedUnder_le

theorem IsLittleO.tendsto_zero_of_tendsto {Î± E ğ•œ : Type*} [NormedAddCommGroup E] [NormedField ğ•œ]
    {u : Î± â†’ E} {v : Î± â†’ ğ•œ} {l : Filter Î±} {y : ğ•œ} (huv : u =o[l] v) (hv : Tendsto v l (ğ“ y)) :
    Tendsto u l (ğ“ 0) := by
  suffices h : u =o[l] fun _x => (1 : ğ•œ) by
    rwa [isLittleO_one_iff] at h
  exact huv.trans_isBigO (hv.isBigO_one ğ•œ)

theorem isLittleO_pow_pow {m n : â„•} (h : m < n) : (fun x : ğ•œ => x ^ n) =o[ğ“ 0] fun x => x ^ m := by
  rcases lt_iff_exists_add.1 h with âŸ¨p, hp0 : 0 < p, rflâŸ©
  suffices (fun x : ğ•œ => x ^ m * x ^ p) =o[ğ“ 0] fun x => x ^ m * 1 ^ p by
    simpa only [pow_add, one_pow, mul_one]
  exact IsBigO.mul_isLittleO (isBigO_refl _ _)
    (IsLittleO.pow ((isLittleO_one_iff _).2 tendsto_id) hp0)

theorem isLittleO_norm_pow_norm_pow {m n : â„•} (h : m < n) :
    (fun x : E' => â€–xâ€– ^ n) =o[ğ“ 0] fun x => â€–xâ€– ^ m :=
  (isLittleO_pow_pow h).comp_tendsto tendsto_norm_zero

theorem isLittleO_pow_id {n : â„•} (h : 1 < n) : (fun x : ğ•œ => x ^ n) =o[ğ“ 0] fun x => x := by
  convert isLittleO_pow_pow h (ğ•œ := ğ•œ)
  simp only [pow_one]

theorem isLittleO_norm_pow_id {n : â„•} (h : 1 < n) :
    (fun x : E' => â€–xâ€– ^ n) =o[ğ“ 0] fun x => x := by
  have := @isLittleO_norm_pow_norm_pow E' _ _ _ h
  simp only [pow_one] at this
  exact isLittleO_norm_right.mp this

theorem IsBigO.eq_zero_of_norm_pow_within {f : E'' â†’ F''} {s : Set E''} {xâ‚€ : E''} {n : â„•}
    (h : f =O[ğ“[s] xâ‚€] fun x => â€–x - xâ‚€â€– ^ n) (hxâ‚€ : xâ‚€ âˆˆ s) (hn : n â‰  0) : f xâ‚€ = 0 :=
  mem_of_mem_nhdsWithin hxâ‚€ h.eq_zero_imp <| by simp_rw [sub_self, norm_zero, zero_pow hn]

theorem IsBigO.eq_zero_of_norm_pow {f : E'' â†’ F''} {xâ‚€ : E''} {n : â„•}
    (h : f =O[ğ“ xâ‚€] fun x => â€–x - xâ‚€â€– ^ n) (hn : n â‰  0) : f xâ‚€ = 0 := by
  rw [â† nhdsWithin_univ] at h
  exact h.eq_zero_of_norm_pow_within (mem_univ _) hn

theorem isLittleO_pow_sub_pow_sub (xâ‚€ : E') {n m : â„•} (h : n < m) :
    (fun x => â€–x - xâ‚€â€– ^ m) =o[ğ“ xâ‚€] fun x => â€–x - xâ‚€â€– ^ n :=
  haveI : Tendsto (fun x => â€–x - xâ‚€â€–) (ğ“ xâ‚€) (ğ“ 0) := by
    apply tendsto_norm_zero.comp
    rw [â† sub_self xâ‚€]
    exact tendsto_id.sub tendsto_const_nhds
  (isLittleO_pow_pow h).comp_tendsto this

theorem isLittleO_pow_sub_sub (xâ‚€ : E') {m : â„•} (h : 1 < m) :
    (fun x => â€–x - xâ‚€â€– ^ m) =o[ğ“ xâ‚€] fun x => x - xâ‚€ := by
  simpa only [isLittleO_norm_right, pow_one] using isLittleO_pow_sub_pow_sub xâ‚€ h

theorem IsBigOWith.right_le_sub_of_lt_one {fâ‚ fâ‚‚ : Î± â†’ E'} (h : IsBigOWith c l fâ‚ fâ‚‚) (hc : c < 1) :
    IsBigOWith (1 / (1 - c)) l fâ‚‚ fun x => fâ‚‚ x - fâ‚ x :=
  IsBigOWith.of_bound <|
    mem_of_superset h.bound fun x hx => by
      simp only [mem_setOf_eq] at hx âŠ¢
      rw [mul_comm, one_div, â† div_eq_mul_inv, le_div_iffâ‚€, mul_sub, mul_one, mul_comm]
      Â· exact le_trans (sub_le_sub_left hx _) (norm_sub_norm_le _ _)
      Â· exact sub_pos.2 hc

theorem IsBigOWith.right_le_add_of_lt_one {fâ‚ fâ‚‚ : Î± â†’ E'} (h : IsBigOWith c l fâ‚ fâ‚‚) (hc : c < 1) :
    IsBigOWith (1 / (1 - c)) l fâ‚‚ fun x => fâ‚ x + fâ‚‚ x :=
  (h.neg_right.right_le_sub_of_lt_one hc).neg_right.of_neg_left.congr rfl (fun _ â†¦ rfl) fun x â†¦ by
    rw [neg_sub, sub_neg_eq_add]

theorem IsLittleO.right_isBigO_sub {fâ‚ fâ‚‚ : Î± â†’ E'} (h : fâ‚ =o[l] fâ‚‚) :
    fâ‚‚ =O[l] fun x => fâ‚‚ x - fâ‚ x :=
  ((h.def' one_half_pos).right_le_sub_of_lt_one one_half_lt_one).isBigO

theorem IsLittleO.right_isBigO_add {fâ‚ fâ‚‚ : Î± â†’ E'} (h : fâ‚ =o[l] fâ‚‚) :
    fâ‚‚ =O[l] fun x => fâ‚ x + fâ‚‚ x :=
  ((h.def' one_half_pos).right_le_add_of_lt_one one_half_lt_one).isBigO

theorem IsLittleO.right_isBigO_add' {fâ‚ fâ‚‚ : Î± â†’ E'} (h : fâ‚ =o[l] fâ‚‚) :
    fâ‚‚ =O[l] (fâ‚‚ + fâ‚) :=
  add_comm fâ‚ fâ‚‚ â–¸ h.right_isBigO_add

/-- If `f x = O(g x)` along `cofinite`, then there exists a positive constant `C` such that
`â€–f xâ€– â‰¤ C * â€–g xâ€–` whenever `g x â‰  0`. -/
theorem bound_of_isBigO_cofinite (h : f =O[cofinite] g'') :
    âˆƒ C > 0, âˆ€ â¦ƒxâ¦„, g'' x â‰  0 â†’ â€–f xâ€– â‰¤ C * â€–g'' xâ€– := by
  rcases h.exists_pos with âŸ¨C, Câ‚€, hCâŸ©
  rw [IsBigOWith_def, eventually_cofinite] at hC
  rcases (hC.toFinset.image fun x => â€–f xâ€– / â€–g'' xâ€–).exists_le with âŸ¨C', hC'âŸ©
  have : âˆ€ x, C * â€–g'' xâ€– < â€–f xâ€– â†’ â€–f xâ€– / â€–g'' xâ€– â‰¤ C' := by simpa using hC'
  refine âŸ¨max C C', lt_max_iff.2 (Or.inl Câ‚€), fun x hâ‚€ => ?_âŸ©
  rw [max_mul_of_nonneg _ _ (norm_nonneg _), le_max_iff, or_iff_not_imp_left, not_le]
  exact fun hx => (div_le_iffâ‚€ (norm_pos_iff.2 hâ‚€)).1 (this _ hx)

theorem isBigO_cofinite_iff (h : âˆ€ x, g'' x = 0 â†’ f'' x = 0) :
    f'' =O[cofinite] g'' â†” âˆƒ C, âˆ€ x, â€–f'' xâ€– â‰¤ C * â€–g'' xâ€– := by
  classical
  exact âŸ¨fun h' =>
    let âŸ¨C, _Câ‚€, hCâŸ© := bound_of_isBigO_cofinite h'
    âŸ¨C, fun x => if hx : g'' x = 0 then by simp [h _ hx, hx] else hC hxâŸ©,
    fun h => (isBigO_top.2 h).mono le_topâŸ©

theorem bound_of_isBigO_nat_atTop {f : â„• â†’ E} {g'' : â„• â†’ E''} (h : f =O[atTop] g'') :
    âˆƒ C > 0, âˆ€ â¦ƒxâ¦„, g'' x â‰  0 â†’ â€–f xâ€– â‰¤ C * â€–g'' xâ€– :=
  bound_of_isBigO_cofinite <| by rwa [Nat.cofinite_eq_atTop]

theorem isBigO_nat_atTop_iff {f : â„• â†’ E''} {g : â„• â†’ F''} (h : âˆ€ x, g x = 0 â†’ f x = 0) :
    f =O[atTop] g â†” âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–g xâ€– := by
  rw [â† Nat.cofinite_eq_atTop, isBigO_cofinite_iff h]

theorem isBigO_one_nat_atTop_iff {f : â„• â†’ E''} :
    f =O[atTop] (fun _n => 1 : â„• â†’ â„) â†” âˆƒ C, âˆ€ n, â€–f nâ€– â‰¤ C :=
  Iff.trans (isBigO_nat_atTop_iff fun _ h => (one_ne_zero h).elim) <| by
    simp only [norm_one, mul_one]

theorem IsBigO.nat_of_atTop {f : â„• â†’ E''} {g : â„• â†’ F''} (hfg : f =O[atTop] g)
    {l : Filter â„•} (h : âˆ€á¶  n in l, g n = 0 â†’ f n = 0) : f =O[l] g := by
  obtain âŸ¨C, hC_pos, hCâŸ© := bound_of_isBigO_nat_atTop hfg
  refine isBigO_iff.mpr âŸ¨C, ?_âŸ©
  filter_upwards [h] with x h
  by_cases hf : f x = 0
  Â· simp [hf, hC_pos]
  exact hC fun a â†¦ hf (h a)

theorem isBigOWith_pi {Î¹ : Type*} [Fintype Î¹] {E' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E' i)]
    {f : Î± â†’ âˆ€ i, E' i} {C : â„} (hC : 0 â‰¤ C) :
    IsBigOWith C l f g' â†” âˆ€ i, IsBigOWith C l (fun x => f x i) g' := by
  have : âˆ€ x, 0 â‰¤ C * â€–g' xâ€– := fun x => mul_nonneg hC (norm_nonneg _)
  simp only [isBigOWith_iff, pi_norm_le_iff_of_nonneg (this _), eventually_all]

@[simp]
theorem isBigO_pi {Î¹ : Type*} [Fintype Î¹] {E' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E' i)]
    {f : Î± â†’ âˆ€ i, E' i} : f =O[l] g' â†” âˆ€ i, (fun x => f x i) =O[l] g' := by
  simp only [isBigO_iff_eventually_isBigOWith, â† eventually_all]
  exact eventually_congr (eventually_atTop.2 âŸ¨0, fun c => isBigOWith_piâŸ©)

@[simp]
theorem isLittleO_pi {Î¹ : Type*} [Fintype Î¹] {E' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E' i)]
    {f : Î± â†’ âˆ€ i, E' i} : f =o[l] g' â†” âˆ€ i, (fun x => f x i) =o[l] g' := by
  simp +contextual only [IsLittleO_def, isBigOWith_pi, le_of_lt]
  exact âŸ¨fun h i c hc => h hc i, fun h c hc i => h i hcâŸ©

theorem IsBigO.natCast_atTop {R : Type*} [Semiring R] [PartialOrder R] [IsStrictOrderedRing R]
    [Archimedean R]
    {f : R â†’ E} {g : R â†’ F} (h : f =O[atTop] g) :
    (fun (n : â„•) => f n) =O[atTop] (fun n => g n) :=
  IsBigO.comp_tendsto h tendsto_natCast_atTop_atTop

theorem IsLittleO.natCast_atTop {R : Type*} [Semiring R] [PartialOrder R] [IsStrictOrderedRing R]
    [Archimedean R]
    {f : R â†’ E} {g : R â†’ F} (h : f =o[atTop] g) :
    (fun (n : â„•) => f n) =o[atTop] (fun n => g n) :=
  IsLittleO.comp_tendsto h tendsto_natCast_atTop_atTop

theorem isBigO_atTop_iff_eventually_exists {Î± : Type*} [SemilatticeSup Î±] [Nonempty Î±]
    {f : Î± â†’ E} {g : Î± â†’ F} : f =O[atTop] g â†” âˆ€á¶  nâ‚€ in atTop, âˆƒ c, âˆ€ n â‰¥ nâ‚€, â€–f nâ€– â‰¤ c * â€–g nâ€– := by
  rw [isBigO_iff, exists_eventually_atTop]

theorem isBigO_atTop_iff_eventually_exists_pos {Î± : Type*}
    [SemilatticeSup Î±] [Nonempty Î±] {f : Î± â†’ G} {g : Î± â†’ G'} :
    f =O[atTop] g â†” âˆ€á¶  nâ‚€ in atTop, âˆƒ c > 0, âˆ€ n â‰¥ nâ‚€, c * â€–f nâ€– â‰¤ â€–g nâ€– := by
  simp_rw [isBigO_iff'', â† exists_prop, Subtype.exists', exists_eventually_atTop]

lemma isBigO_mul_iff_isBigO_div {f g h : Î± â†’ ğ•œ} (hf : âˆ€á¶  x in l, f x â‰  0) :
    (fun x â†¦ f x * g x) =O[l] h â†” g =O[l] (fun x â†¦ h x / f x) := by
  rw [isBigO_iff', isBigO_iff']
  refine âŸ¨fun âŸ¨c, hc, HâŸ© â†¦ âŸ¨c, hc, ?_âŸ©, fun âŸ¨c, hc, HâŸ© â†¦ âŸ¨c, hc, ?_âŸ©âŸ© <;>
  Â· refine H.congr <| Eventually.mp hf <| Eventually.of_forall fun x hx â†¦ ?_
    rw [norm_mul, norm_div, â† mul_div_assoc, le_div_iffâ‚€' (norm_pos_iff.mpr hx)]

end Asymptotics

open Asymptotics

theorem summable_of_isBigO {Î¹ E} [SeminormedAddCommGroup E] [CompleteSpace E]
    {f : Î¹ â†’ E} {g : Î¹ â†’ â„} (hg : Summable g) (h : f =O[cofinite] g) : Summable f :=
  let âŸ¨_, hCâŸ© := h.isBigOWith
  .of_norm_bounded_eventually (hg.abs.mul_left _) hC.bound

theorem summable_of_isBigO_nat {E} [SeminormedAddCommGroup E] [CompleteSpace E]
    {f : â„• â†’ E} {g : â„• â†’ â„} (hg : Summable g) (h : f =O[atTop] g) : Summable f :=
  summable_of_isBigO hg <| Nat.cofinite_eq_atTop.symm â–¸ h

lemma Asymptotics.IsBigO.comp_summable_norm {Î¹ E F : Type*}
    [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] {f : E â†’ F} {g : Î¹ â†’ E}
    (hf : f =O[ğ“ 0] id) (hg : Summable (â€–g Â·â€–)) : Summable (â€–f <| g Â·â€–) :=
  summable_of_isBigO hg <| hf.norm_norm.comp_tendsto <|
    tendsto_zero_iff_norm_tendsto_zero.2 hg.tendsto_cofinite_zero

namespace PartialHomeomorph

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
variable {E : Type*} [Norm E] {F : Type*} [Norm F]

/-- Transfer `IsBigOWith` over a `PartialHomeomorph`. -/
theorem isBigOWith_congr (e : PartialHomeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target) {f : Î² â†’ E}
    {g : Î² â†’ F} {C : â„} : IsBigOWith C (ğ“ b) f g â†” IsBigOWith C (ğ“ (e.symm b)) (f âˆ˜ e) (g âˆ˜ e) :=
  âŸ¨fun h =>
    h.comp_tendsto <| by
      have := e.continuousAt (e.map_target hb)
      rwa [ContinuousAt, e.rightInvOn hb] at this,
    fun h =>
    (h.comp_tendsto (e.continuousAt_symm hb)).congr' rfl
      ((e.eventually_right_inverse hb).mono fun _ hx => congr_arg f hx)
      ((e.eventually_right_inverse hb).mono fun _ hx => congr_arg g hx)âŸ©

/-- Transfer `IsBigO` over a `PartialHomeomorph`. -/
theorem isBigO_congr (e : PartialHomeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target) {f : Î² â†’ E}
    {g : Î² â†’ F} : f =O[ğ“ b] g â†” (f âˆ˜ e) =O[ğ“ (e.symm b)] (g âˆ˜ e) := by
  simp only [IsBigO_def]
  exact exists_congr fun C => e.isBigOWith_congr hb

/-- Transfer `IsLittleO` over a `PartialHomeomorph`. -/
theorem isLittleO_congr (e : PartialHomeomorph Î± Î²) {b : Î²} (hb : b âˆˆ e.target) {f : Î² â†’ E}
    {g : Î² â†’ F} : f =o[ğ“ b] g â†” (f âˆ˜ e) =o[ğ“ (e.symm b)] (g âˆ˜ e) := by
  simp only [IsLittleO_def]
  exact forallâ‚‚_congr fun c _hc => e.isBigOWith_congr hb

end PartialHomeomorph

namespace Homeomorph

variable {Î± : Type*} {Î² : Type*} [TopologicalSpace Î±] [TopologicalSpace Î²]
variable {E : Type*} [Norm E] {F : Type*} [Norm F]

open Asymptotics

/-- Transfer `IsBigOWith` over a `Homeomorph`. -/
theorem isBigOWith_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} {C : â„} :
    IsBigOWith C (ğ“ b) f g â†” IsBigOWith C (ğ“ (e.symm b)) (f âˆ˜ e) (g âˆ˜ e) :=
  e.toPartialHomeomorph.isBigOWith_congr trivial

/-- Transfer `IsBigO` over a `Homeomorph`. -/
theorem isBigO_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} :
    f =O[ğ“ b] g â†” (f âˆ˜ e) =O[ğ“ (e.symm b)] (g âˆ˜ e) := by
  simp only [IsBigO_def]
  exact exists_congr fun C => e.isBigOWith_congr

/-- Transfer `IsLittleO` over a `Homeomorph`. -/
theorem isLittleO_congr (e : Î± â‰ƒâ‚œ Î²) {b : Î²} {f : Î² â†’ E} {g : Î² â†’ F} :
    f =o[ğ“ b] g â†” (f âˆ˜ e) =o[ğ“ (e.symm b)] (g âˆ˜ e) := by
  simp only [IsLittleO_def]
  exact forallâ‚‚_congr fun c _hc => e.isBigOWith_congr

end Homeomorph

namespace ContinuousOn

variable {Î± E F : Type*} [TopologicalSpace Î±] {s : Set Î±} {f : Î± â†’ E} {c : F}

section IsBigO

variable [SeminormedAddGroup E] [Norm F]

protected theorem isBigOWith_principal
    (hf : ContinuousOn f s) (hs : IsCompact s) (hc : â€–câ€– â‰  0) :
    IsBigOWith (sSup (Norm.norm '' (f '' s)) / â€–câ€–) (ğ“Ÿ s) f fun _ => c := by
  rw [isBigOWith_principal, div_mul_cancelâ‚€ _ hc]
  exact fun x hx â†¦ hs.image_of_continuousOn hf |>.image continuous_norm
   |>.isLUB_sSup (Set.image_nonempty.mpr <| Set.image_nonempty.mpr âŸ¨x, hxâŸ©)
   |>.left <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx

protected theorem isBigO_principal (hf : ContinuousOn f s) (hs : IsCompact s)
    (hc : â€–câ€– â‰  0) : f =O[ğ“Ÿ s] fun _ => c :=
  (hf.isBigOWith_principal hs hc).isBigO

end IsBigO

section IsBigORev

variable [NormedAddGroup E] [SeminormedAddGroup F]

protected theorem isBigOWith_rev_principal
    (hf : ContinuousOn f s) (hs : IsCompact s) (hC : âˆ€ i âˆˆ s, f i â‰  0) (c : F) :
    IsBigOWith (â€–câ€– / sInf (Norm.norm '' (f '' s))) (ğ“Ÿ s) (fun _ => c) f := by
  refine isBigOWith_principal.mpr fun x hx â†¦ ?_
  rw [mul_comm_div]
  replace hs := hs.image_of_continuousOn hf |>.image continuous_norm
  have h_sInf := hs.isGLB_sInf <| Set.image_nonempty.mpr <| Set.image_nonempty.mpr âŸ¨x, hxâŸ©
  refine le_mul_of_one_le_right (norm_nonneg c) <| (one_le_div ?_).mpr <|
    h_sInf.1 <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx
  obtain âŸ¨_, âŸ¨x, hx, hCxâŸ©, hnormCxâŸ© := hs.sInf_mem h_sInf.nonempty
  rw [â† hnormCx, â† hCx]
  exact (norm_ne_zero_iff.mpr (hC x hx)).symm.lt_of_le (norm_nonneg _)

protected theorem isBigO_rev_principal (hf : ContinuousOn f s)
    (hs : IsCompact s) (hC : âˆ€ i âˆˆ s, f i â‰  0) (c : F) : (fun _ => c) =O[ğ“Ÿ s] f :=
  (hf.isBigOWith_rev_principal hs hC c).isBigO

end IsBigORev

end ContinuousOn

/-- The (scalar) product of a sequence that tends to zero with a bounded one also tends to zero. -/
lemma NormedField.tendsto_zero_smul_of_tendsto_zero_of_bounded {Î¹ ğ•œ ğ”¸ : Type*}
    [NormedDivisionRing ğ•œ] [NormedAddCommGroup ğ”¸] [Module ğ•œ ğ”¸] [IsBoundedSMul ğ•œ ğ”¸] {l : Filter Î¹}
    {Îµ : Î¹ â†’ ğ•œ} {f : Î¹ â†’ ğ”¸} (hÎµ : Tendsto Îµ l (ğ“ 0)) (hf : IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f)) :
    Tendsto (Îµ â€¢ f) l (ğ“ 0) := by
  rw [â† isLittleO_one_iff ğ•œ] at hÎµ âŠ¢
  simpa using IsLittleO.smul_isBigO hÎµ (hf.isBigO_const (one_ne_zero : (1 : ğ•œ) â‰  0))
