/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.Defs
import Mathlib.Analysis.Normed.Group.Bounded
import Mathlib.Analysis.Normed.Group.InfiniteSum
import Mathlib.Analysis.Normed.MulAction
import Mathlib.Topology.PartialHomeomorph

/-!
# Further basic lemmas about asymptotics

-/

open Set Topology Filter NNReal

namespace Asymptotics


variable {Œ± : Type*} {Œ≤ : Type*} {E : Type*} {F : Type*} {G : Type*} {E' : Type*}
  {F' : Type*} {G' : Type*} {E'' : Type*} {F'' : Type*} {G'' : Type*} {E''' : Type*}
  {R : Type*} {R' : Type*} {ùïú : Type*} {ùïú' : Type*}

variable [Norm E] [Norm F] [Norm G]
variable [SeminormedAddCommGroup E'] [SeminormedAddCommGroup F'] [SeminormedAddCommGroup G']
  [NormedAddCommGroup E''] [NormedAddCommGroup F''] [NormedAddCommGroup G''] [SeminormedRing R]
  [SeminormedAddGroup E''']
  [SeminormedRing R']

variable [NormedDivisionRing ùïú] [NormedDivisionRing ùïú']
variable {c c' c‚ÇÅ c‚ÇÇ : ‚Ñù} {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} {k : Œ± ‚Üí G}
variable {f' : Œ± ‚Üí E'} {g' : Œ± ‚Üí F'} {k' : Œ± ‚Üí G'}
variable {f'' : Œ± ‚Üí E''} {g'' : Œ± ‚Üí F''} {k'' : Œ± ‚Üí G''}
variable {l l' : Filter Œ±}
@[simp]
theorem isBigOWith_principal {s : Set Œ±} : IsBigOWith c (ùìü s) f g ‚Üî ‚àÄ x ‚àà s, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ := by
  rw [IsBigOWith_def, eventually_principal]

theorem isBigO_principal {s : Set Œ±} : f =O[ùìü s] g ‚Üî ‚àÉ c, ‚àÄ x ‚àà s, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ := by
  simp_rw [isBigO_iff, eventually_principal]

@[simp]
theorem isLittleO_principal {s : Set Œ±} : f'' =o[ùìü s] g' ‚Üî ‚àÄ x ‚àà s, f'' x = 0 := by
  refine ‚ü®fun h x hx ‚Ü¶ norm_le_zero_iff.1 ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ simp only [isLittleO_iff] at h
    have : Tendsto (fun c : ‚Ñù ‚Ü¶ c * ‚Äñg' x‚Äñ) (ùìù[>] 0) (ùìù 0) :=
      ((continuous_id.mul continuous_const).tendsto' _ _ (zero_mul _)).mono_left
        inf_le_left
    apply le_of_tendsto_of_tendsto tendsto_const_nhds this
    apply eventually_nhdsWithin_iff.2 (Eventually.of_forall (fun c hc ‚Ü¶ ?_))
    exact eventually_principal.1 (h hc) x hx
  ¬∑ apply (isLittleO_zero g' _).congr' ?_ EventuallyEq.rfl
    exact fun x hx ‚Ü¶ (h x hx).symm

@[simp]
theorem isBigOWith_top : IsBigOWith c ‚ä§ f g ‚Üî ‚àÄ x, ‚Äñf x‚Äñ ‚â§ c * ‚Äñg x‚Äñ := by
  rw [IsBigOWith_def, eventually_top]

@[simp]
theorem isBigO_top : f =O[‚ä§] g ‚Üî ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ := by
  simp_rw [isBigO_iff, eventually_top]

@[simp]
theorem isLittleO_top : f'' =o[‚ä§] g' ‚Üî ‚àÄ x, f'' x = 0 := by
  simp only [‚Üê principal_univ, isLittleO_principal, mem_univ, forall_true_left]

section

variable (F)
variable [One F] [NormOneClass F]

theorem isBigOWith_const_one (c : E) (l : Filter Œ±) :
    IsBigOWith ‚Äñc‚Äñ l (fun _x : Œ± ‚Ü¶ c) fun _x ‚Ü¶ (1 : F) := by simp [isBigOWith_iff]

theorem isBigO_const_one (c : E) (l : Filter Œ±) : (fun _x : Œ± ‚Ü¶ c) =O[l] fun _x ‚Ü¶ (1 : F) :=
  (isBigOWith_const_one F c l).isBigO

theorem isLittleO_const_iff_isLittleO_one {c : F''} (hc : c ‚â† 0) :
    (f =o[l] fun _x ‚Ü¶ c) ‚Üî f =o[l] fun _x ‚Ü¶ (1 : F) :=
  ‚ü®fun h ‚Ü¶ h.trans_isBigOWith (isBigOWith_const_one _ _ _) (norm_pos_iff.2 hc),
   fun h ‚Ü¶ h.trans_isBigO <| isBigO_const_const _ hc _‚ü©

@[simp]
theorem isLittleO_one_iff {f : Œ± ‚Üí E'''} : f =o[l] (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) ‚Üî Tendsto f l (ùìù 0) := by
  simp only [isLittleO_iff, norm_one, mul_one, Metric.nhds_basis_closedBall.tendsto_right_iff,
    Metric.mem_closedBall, dist_zero_right]

@[simp]
theorem isBigO_one_iff : f =O[l] (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) ‚Üî
    IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x ‚Ü¶ ‚Äñf x‚Äñ := by
  simp only [isBigO_iff, norm_one, mul_one, IsBoundedUnder, IsBounded, eventually_map]

alias ‚ü®_, _root_.Filter.IsBoundedUnder.isBigO_one‚ü© := isBigO_one_iff

@[simp]
theorem isLittleO_one_left_iff : (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) =o[l] f ‚Üî Tendsto (fun x ‚Ü¶ ‚Äñf x‚Äñ) l atTop :=
  calc
    (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) =o[l] f ‚Üî ‚àÄ n : ‚Ñï, ‚àÄ·∂† x in l, ‚Üën * ‚Äñ(1 : F)‚Äñ ‚â§ ‚Äñf x‚Äñ :=
      isLittleO_iff_nat_mul_le_aux <| Or.inl fun _x ‚Ü¶ by simp only [norm_one, zero_le_one]
    _ ‚Üî ‚àÄ n : ‚Ñï, True ‚Üí ‚àÄ·∂† x in l, ‚Äñf x‚Äñ ‚àà Ici (n : ‚Ñù) := by
      simp only [norm_one, mul_one, true_imp_iff, mem_Ici]
    _ ‚Üî Tendsto (fun x ‚Ü¶ ‚Äñf x‚Äñ) l atTop :=
      atTop_hasCountableBasis_of_archimedean.1.tendsto_right_iff.symm

theorem _root_.Filter.Tendsto.isBigO_one {c : E'} (h : Tendsto f' l (ùìù c)) :
    f' =O[l] (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) :=
  h.norm.isBoundedUnder_le.isBigO_one F

theorem IsBigO.trans_tendsto_nhds (hfg : f =O[l] g') {y : F'} (hg : Tendsto g' l (ùìù y)) :
    f =O[l] (fun _x ‚Ü¶ 1 : Œ± ‚Üí F) :=
  hfg.trans <| hg.isBigO_one F

/-- The condition `f = O[ùìù[‚â†] a] 1` is equivalent to `f = O[ùìù a] 1`. -/
lemma isBigO_one_nhds_ne_iff [TopologicalSpace Œ±] {a : Œ±} :
    f =O[ùìù[‚â†] a] (fun _ ‚Ü¶ 1 : Œ± ‚Üí F) ‚Üî f =O[ùìù a] (fun _ ‚Ü¶ 1 : Œ± ‚Üí F) := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ h.mono nhdsWithin_le_nhds‚ü©
  simp only [isBigO_one_iff, IsBoundedUnder, IsBounded, eventually_map] at h ‚ä¢
  obtain ‚ü®c, hc‚ü© := h
  use max c ‚Äñf a‚Äñ
  filter_upwards [eventually_nhdsWithin_iff.mp hc] with b hb
  rcases eq_or_ne b a with rfl | hb'
  ¬∑ apply le_max_right
  ¬∑ exact (hb hb').trans (le_max_left ..)

end

theorem isLittleO_const_iff {c : F''} (hc : c ‚â† 0) :
    (f'' =o[l] fun _x ‚Ü¶ c) ‚Üî Tendsto f'' l (ùìù 0) :=
  (isLittleO_const_iff_isLittleO_one ‚Ñù hc).trans (isLittleO_one_iff _)

theorem isLittleO_id_const {c : F''} (hc : c ‚â† 0) : (fun x : E'' ‚Ü¶ x) =o[ùìù 0] fun _x ‚Ü¶ c :=
  (isLittleO_const_iff hc).mpr (continuous_id.tendsto 0)

theorem _root_.Filter.IsBoundedUnder.isBigO_const (h : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f))
    {c : F''} (hc : c ‚â† 0) : f =O[l] fun _x ‚Ü¶ c :=
  (h.isBigO_one ‚Ñù).trans (isBigO_const_const _ hc _)

theorem isBigO_const_of_tendsto {y : E''} (h : Tendsto f'' l (ùìù y)) {c : F''} (hc : c ‚â† 0) :
    f'' =O[l] fun _x ‚Ü¶ c :=
  h.norm.isBoundedUnder_le.isBigO_const hc

theorem IsBigO.isBoundedUnder_le {c : F} (h : f =O[l] fun _x ‚Ü¶ c) :
    IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f) :=
  let ‚ü®c', hc'‚ü© := h.bound
  ‚ü®c' * ‚Äñc‚Äñ, eventually_map.2 hc'‚ü©

theorem isBigO_const_of_ne {c : F''} (hc : c ‚â† 0) :
    (f =O[l] fun _x ‚Ü¶ c) ‚Üî IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f) :=
  ‚ü®fun h ‚Ü¶ h.isBoundedUnder_le, fun h ‚Ü¶ h.isBigO_const hc‚ü©

theorem isBigO_const_iff {c : F''} : (f'' =O[l] fun _x ‚Ü¶ c) ‚Üî
    (c = 0 ‚Üí f'' =·∂†[l] 0) ‚àß IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x ‚Ü¶ ‚Äñf'' x‚Äñ := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®fun hc ‚Ü¶ isBigO_zero_right_iff.1 (by rwa [‚Üê hc]), h.isBoundedUnder_le‚ü©, ?_‚ü©
  rintro ‚ü®hcf, hf‚ü©
  rcases eq_or_ne c 0 with (hc | hc)
  exacts [(hcf hc).trans_isBigO (isBigO_zero _ _), hf.isBigO_const hc]

theorem isBigO_iff_isBoundedUnder_le_div (h : ‚àÄ·∂† x in l, g'' x ‚â† 0) :
    f =O[l] g'' ‚Üî IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x ‚Ü¶ ‚Äñf x‚Äñ / ‚Äñg'' x‚Äñ := by
  simp only [isBigO_iff, IsBoundedUnder, IsBounded, eventually_map]
  exact
    exists_congr fun c ‚Ü¶
      eventually_congr <| h.mono fun x hx ‚Ü¶ (div_le_iff‚ÇÄ <| norm_pos_iff.2 hx).symm

/-- `(fun x ‚Ü¶ c) =O[l] f` if and only if `f` is bounded away from zero. -/
theorem isBigO_const_left_iff_pos_le_norm {c : E''} (hc : c ‚â† 0) :
    (fun _x ‚Ü¶ c) =O[l] f' ‚Üî ‚àÉ b, 0 < b ‚àß ‚àÄ·∂† x in l, b ‚â§ ‚Äñf' x‚Äñ := by
  constructor
  ¬∑ intro h
    rcases h.exists_pos with ‚ü®C, hC‚ÇÄ, hC‚ü©
    refine ‚ü®‚Äñc‚Äñ / C, div_pos (norm_pos_iff.2 hc) hC‚ÇÄ, ?_‚ü©
    exact hC.bound.mono fun x ‚Ü¶ (div_le_iff‚ÇÄ' hC‚ÇÄ).2
  ¬∑ rintro ‚ü®b, hb‚ÇÄ, hb‚ü©
    refine IsBigO.of_bound (‚Äñc‚Äñ / b) (hb.mono fun x hx ‚Ü¶ ?_)
    rw [div_mul_eq_mul_div, mul_div_assoc]
    exact le_mul_of_one_le_right (norm_nonneg _) ((one_le_div hb‚ÇÄ).2 hx)

theorem IsBigO.trans_tendsto (hfg : f'' =O[l] g'') (hg : Tendsto g'' l (ùìù 0)) :
    Tendsto f'' l (ùìù 0) :=
  (isLittleO_one_iff ‚Ñù).1 <| hfg.trans_isLittleO <| (isLittleO_one_iff ‚Ñù).2 hg

theorem IsLittleO.trans_tendsto (hfg : f'' =o[l] g'') (hg : Tendsto g'' l (ùìù 0)) :
    Tendsto f'' l (ùìù 0) :=
  hfg.isBigO.trans_tendsto hg

lemma isLittleO_id_one [One F''] [NeZero (1 : F'')] : (fun x : E'' ‚Ü¶ x) =o[ùìù 0] (1 : E'' ‚Üí F'') :=
  isLittleO_id_const one_ne_zero

theorem continuousAt_iff_isLittleO {Œ± : Type*} {E : Type*} [NormedRing E] [NormOneClass E]
    [TopologicalSpace Œ±] {f : Œ± ‚Üí E} {x : Œ±} :
    (ContinuousAt f x) ‚Üî (fun (y : Œ±) ‚Ü¶ f y - f x) =o[ùìù x] (fun (_ : Œ±) ‚Ü¶ (1 : E)) := by
  simp [ContinuousAt, ‚Üê tendsto_sub_nhds_zero_iff]

/-! ### Multiplication -/

theorem IsBigO.of_pow {f : Œ± ‚Üí ùïú} {g : Œ± ‚Üí R} {n : ‚Ñï} (hn : n ‚â† 0) (h : (f ^ n) =O[l] (g ^ n)) :
    f =O[l] g := by
  rcases h.exists_pos with ‚ü®C, _hC‚ÇÄ, hC‚ü©
  obtain ‚ü®c : ‚Ñù, hc‚ÇÄ : 0 ‚â§ c, hc : C ‚â§ c ^ n‚ü© :=
    ((eventually_ge_atTop _).and <| (tendsto_pow_atTop hn).eventually_ge_atTop C).exists
  exact (hC.of_pow hn hc hc‚ÇÄ).isBigO

/-! ### Scalar multiplication -/

section SMulConst

variable [Module R E'] [IsBoundedSMul R E']

theorem IsBigOWith.const_smul_self (c' : R) :
    IsBigOWith (‚Äñc'‚Äñ) l (fun x ‚Ü¶ c' ‚Ä¢ f' x) f' :=
  isBigOWith_of_le' _ fun _ ‚Ü¶ norm_smul_le _ _

theorem IsBigO.const_smul_self (c' : R) : (fun x ‚Ü¶ c' ‚Ä¢ f' x) =O[l] f' :=
  (IsBigOWith.const_smul_self _).isBigO

theorem IsBigOWith.const_smul_left (h : IsBigOWith c l f' g) (c' : R) :
    IsBigOWith (‚Äñc'‚Äñ * c) l (fun x ‚Ü¶ c' ‚Ä¢ f' x) g :=
  .trans (.const_smul_self _) h (norm_nonneg _)

theorem IsBigO.const_smul_left (h : f' =O[l] g) (c : R) : (c ‚Ä¢ f') =O[l] g :=
  let ‚ü®_b, hb‚ü© := h.isBigOWith
  (hb.const_smul_left _).isBigO

theorem IsLittleO.const_smul_left (h : f' =o[l] g) (c : R) : (c ‚Ä¢ f') =o[l] g :=
  (IsBigO.const_smul_self _).trans_isLittleO h

variable [Module ùïú E'] [NormSMulClass ùïú E']

theorem isBigO_const_smul_left {c : ùïú} (hc : c ‚â† 0) : (fun x ‚Ü¶ c ‚Ä¢ f' x) =O[l] g ‚Üî f' =O[l] g := by
  have cne0 : ‚Äñc‚Äñ ‚â† 0 := norm_ne_zero_iff.mpr hc
  rw [‚Üê isBigO_norm_left]
  simp only [norm_smul]
  rw [isBigO_const_mul_left_iff cne0, isBigO_norm_left]

theorem isLittleO_const_smul_left {c : ùïú} (hc : c ‚â† 0) :
    (fun x ‚Ü¶ c ‚Ä¢ f' x) =o[l] g ‚Üî f' =o[l] g := by
  have cne0 : ‚Äñc‚Äñ ‚â† 0 := norm_ne_zero_iff.mpr hc
  rw [‚Üê isLittleO_norm_left]
  simp only [norm_smul]
  rw [isLittleO_const_mul_left_iff cne0, isLittleO_norm_left]

theorem isBigO_const_smul_right {c : ùïú} (hc : c ‚â† 0) :
    (f =O[l] fun x ‚Ü¶ c ‚Ä¢ f' x) ‚Üî f =O[l] f' := by
  have cne0 : ‚Äñc‚Äñ ‚â† 0 := norm_ne_zero_iff.mpr hc
  rw [‚Üê isBigO_norm_right]
  simp only [norm_smul]
  rw [isBigO_const_mul_right_iff cne0, isBigO_norm_right]

theorem isLittleO_const_smul_right {c : ùïú} (hc : c ‚â† 0) :
    (f =o[l] fun x ‚Ü¶ c ‚Ä¢ f' x) ‚Üî f =o[l] f' := by
  have cne0 : ‚Äñc‚Äñ ‚â† 0 := norm_ne_zero_iff.mpr hc
  rw [‚Üê isLittleO_norm_right]
  simp only [norm_smul]
  rw [isLittleO_const_mul_right_iff cne0, isLittleO_norm_right]

end SMulConst

section SMul

variable [Module R E'] [IsBoundedSMul R E'] [Module ùïú' F'] [NormSMulClass ùïú' F']
variable {k‚ÇÅ : Œ± ‚Üí R} {k‚ÇÇ : Œ± ‚Üí ùïú'}

theorem IsBigOWith.smul (h‚ÇÅ : IsBigOWith c l k‚ÇÅ k‚ÇÇ) (h‚ÇÇ : IsBigOWith c' l f' g') :
    IsBigOWith (c * c') l (fun x ‚Ü¶ k‚ÇÅ x ‚Ä¢ f' x) fun x ‚Ü¶ k‚ÇÇ x ‚Ä¢ g' x := by
  simp only [IsBigOWith_def] at *
  filter_upwards [h‚ÇÅ, h‚ÇÇ] with _ hx‚ÇÅ hx‚ÇÇ
  apply le_trans (norm_smul_le _ _)
  convert mul_le_mul hx‚ÇÅ hx‚ÇÇ (norm_nonneg _) (le_trans (norm_nonneg _) hx‚ÇÅ) using 1
  rw [norm_smul, mul_mul_mul_comm]

theorem IsBigO.smul (h‚ÇÅ : k‚ÇÅ =O[l] k‚ÇÇ) (h‚ÇÇ : f' =O[l] g') :
    (fun x ‚Ü¶ k‚ÇÅ x ‚Ä¢ f' x) =O[l] fun x ‚Ü¶ k‚ÇÇ x ‚Ä¢ g' x := by
  obtain ‚ü®c‚ÇÅ, h‚ÇÅ‚ü© := h‚ÇÅ.isBigOWith
  obtain ‚ü®c‚ÇÇ, h‚ÇÇ‚ü© := h‚ÇÇ.isBigOWith
  exact (h‚ÇÅ.smul h‚ÇÇ).isBigO

theorem IsBigO.smul_isLittleO (h‚ÇÅ : k‚ÇÅ =O[l] k‚ÇÇ) (h‚ÇÇ : f' =o[l] g') :
    (fun x ‚Ü¶ k‚ÇÅ x ‚Ä¢ f' x) =o[l] fun x ‚Ü¶ k‚ÇÇ x ‚Ä¢ g' x := by
  simp only [IsLittleO_def] at *
  intro c cpos
  rcases h‚ÇÅ.exists_pos with ‚ü®c', c'pos, hc'‚ü©
  exact (hc'.smul (h‚ÇÇ (div_pos cpos c'pos))).congr_const (mul_div_cancel‚ÇÄ _ (ne_of_gt c'pos))

theorem IsLittleO.smul_isBigO (h‚ÇÅ : k‚ÇÅ =o[l] k‚ÇÇ) (h‚ÇÇ : f' =O[l] g') :
    (fun x ‚Ü¶ k‚ÇÅ x ‚Ä¢ f' x) =o[l] fun x ‚Ü¶ k‚ÇÇ x ‚Ä¢ g' x := by
  simp only [IsLittleO_def] at *
  intro c cpos
  rcases h‚ÇÇ.exists_pos with ‚ü®c', c'pos, hc'‚ü©
  exact ((h‚ÇÅ (div_pos cpos c'pos)).smul hc').congr_const (div_mul_cancel‚ÇÄ _ (ne_of_gt c'pos))

theorem IsLittleO.smul (h‚ÇÅ : k‚ÇÅ =o[l] k‚ÇÇ) (h‚ÇÇ : f' =o[l] g') :
    (fun x ‚Ü¶ k‚ÇÅ x ‚Ä¢ f' x) =o[l] fun x ‚Ü¶ k‚ÇÇ x ‚Ä¢ g' x :=
  h‚ÇÅ.smul_isBigO h‚ÇÇ.isBigO

end SMul

section Prod
variable {Œπ : Type*}

theorem IsBigO.listProd {L : List Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú}
    (hf : ‚àÄ i ‚àà L, f i =O[l] g i) :
    (fun x ‚Ü¶ (L.map (f ¬∑ x)).prod) =O[l] (fun x ‚Ü¶ (L.map (g ¬∑ x)).prod) := by
  induction L with
  | nil => simp [isBoundedUnder_const]
  | cons i L ihL =>
    simp only [List.map_cons, List.prod_cons, List.forall_mem_cons] at hf ‚ä¢
    exact hf.1.mul (ihL hf.2)

theorem IsBigO.multisetProd {R ùïú : Type*} [SeminormedCommRing R] [NormedField ùïú]
    {s : Multiset Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú} (hf : ‚àÄ i ‚àà s, f i =O[l] g i) :
    (fun x ‚Ü¶ (s.map (f ¬∑ x)).prod) =O[l] (fun x ‚Ü¶ (s.map (g ¬∑ x)).prod) := by
  obtain ‚ü®l, rfl‚ü© : ‚àÉ l : List Œπ, ‚Üël = s := Quotient.mk_surjective s
  exact mod_cast IsBigO.listProd hf

theorem IsBigO.finsetProd {R ùïú : Type*} [SeminormedCommRing R] [NormedField ùïú]
    {s : Finset Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú}
    (hf : ‚àÄ i ‚àà s, f i =O[l] g i) : (‚àè i ‚àà s, f i ¬∑) =O[l] (‚àè i ‚àà s, g i ¬∑) :=
  .multisetProd hf

theorem IsLittleO.listProd {L : List Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú}
    (h‚ÇÅ : ‚àÄ i ‚àà L, f i =O[l] g i) (h‚ÇÇ : ‚àÉ i ‚àà L, f i =o[l] g i) :
    (fun x ‚Ü¶ (L.map (f ¬∑ x)).prod) =o[l] (fun x ‚Ü¶ (L.map (g ¬∑ x)).prod) := by
  induction L with
  | nil => simp at h‚ÇÇ
  | cons i L ihL =>
    simp only [List.map_cons, List.prod_cons, List.forall_mem_cons, List.exists_mem_cons_iff]
      at h‚ÇÅ h‚ÇÇ ‚ä¢
    cases h‚ÇÇ with
    | inl hi => exact hi.mul_isBigO <| .listProd h‚ÇÅ.2
    | inr hL => exact h‚ÇÅ.1.mul_isLittleO <| ihL h‚ÇÅ.2 hL

theorem IsLittleO.multisetProd {R ùïú : Type*} [SeminormedCommRing R] [NormedField ùïú]
    {s : Multiset Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú} (h‚ÇÅ : ‚àÄ i ‚àà s, f i =O[l] g i)
    (h‚ÇÇ : ‚àÉ i ‚àà s, f i =o[l] g i) :
    (fun x ‚Ü¶ (s.map (f ¬∑ x)).prod) =o[l] (fun x ‚Ü¶ (s.map (g ¬∑ x)).prod) := by
  obtain ‚ü®l, rfl‚ü© : ‚àÉ l : List Œπ, ‚Üël = s := Quotient.mk_surjective s
  exact mod_cast IsLittleO.listProd h‚ÇÅ h‚ÇÇ

theorem IsLittleO.finsetProd {R ùïú : Type*} [SeminormedCommRing R] [NormedField ùïú]
    {s : Finset Œπ} {f : Œπ ‚Üí Œ± ‚Üí R} {g : Œπ ‚Üí Œ± ‚Üí ùïú} (h‚ÇÅ : ‚àÄ i ‚àà s, f i =O[l] g i)
    (h‚ÇÇ : ‚àÉ i ‚àà s, f i =o[l] g i) : (‚àè i ‚àà s, f i ¬∑) =o[l] (‚àè i ‚àà s, g i ¬∑) :=
  .multisetProd h‚ÇÅ h‚ÇÇ

end Prod

/-! ### Relation between `f = o(g)` and `f / g ‚Üí 0` -/

theorem IsLittleO.tendsto_div_nhds_zero {f g : Œ± ‚Üí ùïú} (h : f =o[l] g) :
    Tendsto (fun x ‚Ü¶ f x / g x) l (ùìù 0) :=
  (isLittleO_one_iff ùïú).mp <| by
    calc
      (fun x ‚Ü¶ f x / g x) =o[l] fun x ‚Ü¶ g x / g x := by
        simpa only [div_eq_mul_inv] using h.mul_isBigO (isBigO_refl _ _)
      _ =O[l] fun _x ‚Ü¶ (1 : ùïú) := isBigO_of_le _ fun x ‚Ü¶ by simp [div_self_le_one]

theorem IsLittleO.tendsto_inv_smul_nhds_zero [Module ùïú E'] [NormSMulClass ùïú E']
    {f : Œ± ‚Üí E'} {g : Œ± ‚Üí ùïú}
    {l : Filter Œ±} (h : f =o[l] g) : Tendsto (fun x ‚Ü¶ (g x)‚Åª¬π ‚Ä¢ f x) l (ùìù 0) := by
  simpa only [div_eq_inv_mul, ‚Üê norm_inv, ‚Üê norm_smul, ‚Üê tendsto_zero_iff_norm_tendsto_zero] using
    h.norm_norm.tendsto_div_nhds_zero

theorem isLittleO_iff_tendsto' {f g : Œ± ‚Üí ùïú} (hgf : ‚àÄ·∂† x in l, g x = 0 ‚Üí f x = 0) :
    f =o[l] g ‚Üî Tendsto (fun x ‚Ü¶ f x / g x) l (ùìù 0) :=
  ‚ü®IsLittleO.tendsto_div_nhds_zero, fun h ‚Ü¶
    (((isLittleO_one_iff _).mpr h).mul_isBigO (isBigO_refl g l)).congr'
      (hgf.mono fun _x ‚Ü¶ div_mul_cancel_of_imp) (Eventually.of_forall fun _x ‚Ü¶ one_mul _)‚ü©

theorem isLittleO_iff_tendsto {f g : Œ± ‚Üí ùïú} (hgf : ‚àÄ x, g x = 0 ‚Üí f x = 0) :
    f =o[l] g ‚Üî Tendsto (fun x ‚Ü¶ f x / g x) l (ùìù 0) :=
  isLittleO_iff_tendsto' (Eventually.of_forall hgf)

alias ‚ü®_, isLittleO_of_tendsto'‚ü© := isLittleO_iff_tendsto'

alias ‚ü®_, isLittleO_of_tendsto‚ü© := isLittleO_iff_tendsto

theorem isLittleO_const_left_of_ne {c : E''} (hc : c ‚â† 0) :
    (fun _x ‚Ü¶ c) =o[l] g ‚Üî Tendsto (fun x ‚Ü¶ ‚Äñg x‚Äñ) l atTop := by
  simp only [‚Üê isLittleO_one_left_iff ‚Ñù]
  exact ‚ü®(isBigO_const_const (1 : ‚Ñù) hc l).trans_isLittleO,
    (isBigO_const_one ‚Ñù c l).trans_isLittleO‚ü©

@[simp]
theorem isLittleO_const_left {c : E''} :
    (fun _x ‚Ü¶ c) =o[l] g'' ‚Üî c = 0 ‚à® Tendsto (norm ‚àò g'') l atTop := by
  rcases eq_or_ne c 0 with (rfl | hc)
  ¬∑ simp only [isLittleO_zero, true_or]
  ¬∑ simp only [hc, false_or, isLittleO_const_left_of_ne hc]; rfl

@[simp 1001] -- Porting note: increase priority so that this triggers before `isLittleO_const_left`
theorem isLittleO_const_const_iff [NeBot l] {d : E''} {c : F''} :
    ((fun _x ‚Ü¶ d) =o[l] fun _x ‚Ü¶ c) ‚Üî d = 0 := by
  have : ¬¨Tendsto (Function.const Œ± ‚Äñc‚Äñ) l atTop :=
    not_tendsto_atTop_of_tendsto_nhds tendsto_const_nhds
  simp only [isLittleO_const_left, or_iff_left_iff_imp]
  exact fun h ‚Ü¶ (this h).elim

@[simp]
theorem isLittleO_pure {x} : f'' =o[pure x] g'' ‚Üî f'' x = 0 :=
  calc
    f'' =o[pure x] g'' ‚Üî (fun _y : Œ± ‚Ü¶ f'' x) =o[pure x] fun _ ‚Ü¶ g'' x := isLittleO_congr rfl rfl
    _ ‚Üî f'' x = 0 := isLittleO_const_const_iff

theorem isLittleO_const_id_cobounded (c : F'') :
    (fun _ ‚Ü¶ c) =o[Bornology.cobounded E''] id :=
  isLittleO_const_left.2 <| .inr tendsto_norm_cobounded_atTop

theorem isLittleO_const_id_atTop (c : E'') : (fun _x : ‚Ñù ‚Ü¶ c) =o[atTop] id :=
  isLittleO_const_left.2 <| Or.inr tendsto_abs_atTop_atTop

theorem isLittleO_const_id_atBot (c : E'') : (fun _x : ‚Ñù ‚Ü¶ c) =o[atBot] id :=
  isLittleO_const_left.2 <| Or.inr tendsto_abs_atBot_atTop

/-! ### Equivalent definitions of the form `‚àÉ œÜ, u =·∂†[l] œÜ * v` in a `NormedField`. -/

section ExistsMulEq

variable {u v : Œ± ‚Üí ùïú}

/-- If `‚ÄñœÜ‚Äñ` is eventually bounded by `c`, and `u =·∂†[l] œÜ * v`, then we have `IsBigOWith c u v l`.
This does not require any assumptions on `c`, which is why we keep this version along with
`IsBigOWith_iff_exists_eq_mul`. -/
theorem isBigOWith_of_eq_mul {u v : Œ± ‚Üí R} (œÜ : Œ± ‚Üí R) (hœÜ : ‚àÄ·∂† x in l, ‚ÄñœÜ x‚Äñ ‚â§ c)
    (h : u =·∂†[l] œÜ * v) :
    IsBigOWith c l u v := by
  simp only [IsBigOWith_def]
  refine h.symm.rw (fun x a ‚Ü¶ ‚Äña‚Äñ ‚â§ c * ‚Äñv x‚Äñ) (hœÜ.mono fun x hx ‚Ü¶ ?_)
  simp only [Pi.mul_apply]
  refine (norm_mul_le _ _).trans ?_
  gcongr

theorem isBigOWith_iff_exists_eq_mul (hc : 0 ‚â§ c) :
    IsBigOWith c l u v ‚Üî ‚àÉ œÜ : Œ± ‚Üí ùïú, (‚àÄ·∂† x in l, ‚ÄñœÜ x‚Äñ ‚â§ c) ‚àß u =·∂†[l] œÜ * v := by
  constructor
  ¬∑ intro h
    use fun x ‚Ü¶ u x / v x
    refine ‚ü®Eventually.mono h.bound fun y hy ‚Ü¶ ?_, h.eventually_mul_div_cancel.symm‚ü©
    simpa using div_le_of_le_mul‚ÇÄ (norm_nonneg _) hc hy
  ¬∑ rintro ‚ü®œÜ, hœÜ, h‚ü©
    exact isBigOWith_of_eq_mul œÜ hœÜ h

theorem IsBigOWith.exists_eq_mul (h : IsBigOWith c l u v) (hc : 0 ‚â§ c) :
    ‚àÉ œÜ : Œ± ‚Üí ùïú, (‚àÄ·∂† x in l, ‚ÄñœÜ x‚Äñ ‚â§ c) ‚àß u =·∂†[l] œÜ * v :=
  (isBigOWith_iff_exists_eq_mul hc).mp h

theorem isBigO_iff_exists_eq_mul :
    u =O[l] v ‚Üî ‚àÉ œÜ : Œ± ‚Üí ùïú, l.IsBoundedUnder (¬∑ ‚â§ ¬∑) (norm ‚àò œÜ) ‚àß u =·∂†[l] œÜ * v := by
  constructor
  ¬∑ rintro h
    rcases h.exists_nonneg with ‚ü®c, hnnc, hc‚ü©
    rcases hc.exists_eq_mul hnnc with ‚ü®œÜ, hœÜ, huvœÜ‚ü©
    exact ‚ü®œÜ, ‚ü®c, hœÜ‚ü©, huvœÜ‚ü©
  ¬∑ rintro ‚ü®œÜ, ‚ü®c, hœÜ‚ü©, huvœÜ‚ü©
    exact isBigO_iff_isBigOWith.2 ‚ü®c, isBigOWith_of_eq_mul œÜ hœÜ huvœÜ‚ü©

alias ‚ü®IsBigO.exists_eq_mul, _‚ü© := isBigO_iff_exists_eq_mul

theorem isLittleO_iff_exists_eq_mul :
    u =o[l] v ‚Üî ‚àÉ œÜ : Œ± ‚Üí ùïú, Tendsto œÜ l (ùìù 0) ‚àß u =·∂†[l] œÜ * v := by
  constructor
  ¬∑ exact fun h ‚Ü¶ ‚ü®fun x ‚Ü¶ u x / v x, h.tendsto_div_nhds_zero, h.eventually_mul_div_cancel.symm‚ü©
  ¬∑ simp only [IsLittleO_def]
    rintro ‚ü®œÜ, hœÜ, huvœÜ‚ü© c hpos
    rw [NormedAddCommGroup.tendsto_nhds_zero] at hœÜ
    exact isBigOWith_of_eq_mul _ ((hœÜ c hpos).mono fun x ‚Ü¶ le_of_lt) huvœÜ

alias ‚ü®IsLittleO.exists_eq_mul, _‚ü© := isLittleO_iff_exists_eq_mul

end ExistsMulEq

/-! ### Miscellaneous lemmas -/

theorem div_isBoundedUnder_of_isBigO {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ùïú} (h : f =O[l] g) :
    IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x ‚Ü¶ ‚Äñf x / g x‚Äñ := by
  obtain ‚ü®c, h‚ÇÄ, hc‚ü© := h.exists_nonneg
  refine ‚ü®c, eventually_map.2 (hc.bound.mono fun x hx ‚Ü¶ ?_)‚ü©
  rw [norm_div]
  exact div_le_of_le_mul‚ÇÄ (norm_nonneg _) h‚ÇÄ hx

theorem isBigO_iff_div_isBoundedUnder {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ùïú}
    (hgf : ‚àÄ·∂† x in l, g x = 0 ‚Üí f x = 0) :
    f =O[l] g ‚Üî IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x ‚Ü¶ ‚Äñf x / g x‚Äñ := by
  refine ‚ü®div_isBoundedUnder_of_isBigO, fun h ‚Ü¶ ?_‚ü©
  obtain ‚ü®c, hc‚ü© := h
  simp only [eventually_map, norm_div] at hc
  refine IsBigO.of_bound c (hc.mp <| hgf.mono fun x hx‚ÇÅ hx‚ÇÇ ‚Ü¶ ?_)
  by_cases hgx : g x = 0
  ¬∑ simp [hx‚ÇÅ hgx, hgx]
  ¬∑ exact (div_le_iff‚ÇÄ (norm_pos_iff.2 hgx)).mp hx‚ÇÇ

theorem isBigO_of_div_tendsto_nhds {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ùïú}
    (hgf : ‚àÄ·∂† x in l, g x = 0 ‚Üí f x = 0) (c : ùïú) (H : Filter.Tendsto (f / g) l (ùìù c)) :
    f =O[l] g :=
  (isBigO_iff_div_isBoundedUnder hgf).2 <| H.norm.isBoundedUnder_le

theorem IsLittleO.tendsto_zero_of_tendsto {Œ± E ùïú : Type*} [NormedAddCommGroup E] [NormedField ùïú]
    {u : Œ± ‚Üí E} {v : Œ± ‚Üí ùïú} {l : Filter Œ±} {y : ùïú} (huv : u =o[l] v) (hv : Tendsto v l (ùìù y)) :
    Tendsto u l (ùìù 0) := by
  suffices h : u =o[l] fun _x ‚Ü¶ (1 : ùïú) by
    rwa [isLittleO_one_iff] at h
  exact huv.trans_isBigO (hv.isBigO_one ùïú)

theorem isLittleO_pow_pow {m n : ‚Ñï} (h : m < n) : (fun x : ùïú ‚Ü¶ x ^ n) =o[ùìù 0] fun x ‚Ü¶ x ^ m := by
  rcases lt_iff_exists_add.1 h with ‚ü®p, hp0 : 0 < p, rfl‚ü©
  suffices (fun x : ùïú ‚Ü¶ x ^ m * x ^ p) =o[ùìù 0] fun x ‚Ü¶ x ^ m * 1 ^ p by
    simpa only [pow_add, one_pow, mul_one]
  exact IsBigO.mul_isLittleO (isBigO_refl _ _)
    (IsLittleO.pow ((isLittleO_one_iff _).2 tendsto_id) hp0)

theorem isLittleO_norm_pow_norm_pow {m n : ‚Ñï} (h : m < n) :
    (fun x : E' ‚Ü¶ ‚Äñx‚Äñ ^ n) =o[ùìù 0] fun x ‚Ü¶ ‚Äñx‚Äñ ^ m :=
  (isLittleO_pow_pow h).comp_tendsto tendsto_norm_zero

theorem isLittleO_pow_id {n : ‚Ñï} (h : 1 < n) : (fun x : ùïú ‚Ü¶ x ^ n) =o[ùìù 0] fun x ‚Ü¶ x := by
  convert isLittleO_pow_pow h (ùïú := ùïú)
  simp only [pow_one]

theorem isLittleO_norm_pow_id {n : ‚Ñï} (h : 1 < n) :
    (fun x : E' ‚Ü¶ ‚Äñx‚Äñ ^ n) =o[ùìù 0] fun x ‚Ü¶ x := by
  have := @isLittleO_norm_pow_norm_pow E' _ _ _ h
  simp only [pow_one] at this
  exact isLittleO_norm_right.mp this

theorem IsBigO.eq_zero_of_norm_pow_within {f : E'' ‚Üí F''} {s : Set E''} {x‚ÇÄ : E''} {n : ‚Ñï}
    (h : f =O[ùìù[s] x‚ÇÄ] fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ n) (hx‚ÇÄ : x‚ÇÄ ‚àà s) (hn : n ‚â† 0) : f x‚ÇÄ = 0 :=
  mem_of_mem_nhdsWithin hx‚ÇÄ h.eq_zero_imp <| by simp_rw [sub_self, norm_zero, zero_pow hn]

theorem IsBigO.eq_zero_of_norm_pow {f : E'' ‚Üí F''} {x‚ÇÄ : E''} {n : ‚Ñï}
    (h : f =O[ùìù x‚ÇÄ] fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ n) (hn : n ‚â† 0) : f x‚ÇÄ = 0 := by
  rw [‚Üê nhdsWithin_univ] at h
  exact h.eq_zero_of_norm_pow_within (mem_univ _) hn

theorem isLittleO_pow_sub_pow_sub (x‚ÇÄ : E') {n m : ‚Ñï} (h : n < m) :
    (fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ m) =o[ùìù x‚ÇÄ] fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ n :=
  haveI : Tendsto (fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ) (ùìù x‚ÇÄ) (ùìù 0) := by
    apply tendsto_norm_zero.comp
    rw [‚Üê sub_self x‚ÇÄ]
    exact tendsto_id.sub tendsto_const_nhds
  (isLittleO_pow_pow h).comp_tendsto this

theorem isLittleO_pow_sub_sub (x‚ÇÄ : E') {m : ‚Ñï} (h : 1 < m) :
    (fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ m) =o[ùìù x‚ÇÄ] fun x ‚Ü¶ x - x‚ÇÄ := by
  simpa only [isLittleO_norm_right, pow_one] using isLittleO_pow_sub_pow_sub x‚ÇÄ h

theorem IsBigOWith.right_le_sub_of_lt_one {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí E'} (h : IsBigOWith c l f‚ÇÅ f‚ÇÇ) (hc : c < 1) :
    IsBigOWith (1 / (1 - c)) l f‚ÇÇ fun x ‚Ü¶ f‚ÇÇ x - f‚ÇÅ x :=
  IsBigOWith.of_bound <|
    mem_of_superset h.bound fun x hx ‚Ü¶ by
      simp only [mem_setOf_eq] at hx ‚ä¢
      rw [mul_comm, one_div, ‚Üê div_eq_mul_inv, le_div_iff‚ÇÄ, mul_sub, mul_one, mul_comm]
      ¬∑ exact le_trans (sub_le_sub_left hx _) (norm_sub_norm_le _ _)
      ¬∑ exact sub_pos.2 hc

theorem IsBigOWith.right_le_add_of_lt_one {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí E'} (h : IsBigOWith c l f‚ÇÅ f‚ÇÇ) (hc : c < 1) :
    IsBigOWith (1 / (1 - c)) l f‚ÇÇ fun x ‚Ü¶ f‚ÇÅ x + f‚ÇÇ x :=
  (h.neg_right.right_le_sub_of_lt_one hc).neg_right.of_neg_left.congr rfl (fun _ ‚Ü¶ rfl) fun x ‚Ü¶ by
    rw [neg_sub, sub_neg_eq_add]

theorem IsLittleO.right_isBigO_sub {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí E'} (h : f‚ÇÅ =o[l] f‚ÇÇ) :
    f‚ÇÇ =O[l] fun x ‚Ü¶ f‚ÇÇ x - f‚ÇÅ x :=
  ((h.def' one_half_pos).right_le_sub_of_lt_one one_half_lt_one).isBigO

theorem IsLittleO.right_isBigO_add {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí E'} (h : f‚ÇÅ =o[l] f‚ÇÇ) :
    f‚ÇÇ =O[l] fun x ‚Ü¶ f‚ÇÅ x + f‚ÇÇ x :=
  ((h.def' one_half_pos).right_le_add_of_lt_one one_half_lt_one).isBigO

theorem IsLittleO.right_isBigO_add' {f‚ÇÅ f‚ÇÇ : Œ± ‚Üí E'} (h : f‚ÇÅ =o[l] f‚ÇÇ) :
    f‚ÇÇ =O[l] (f‚ÇÇ + f‚ÇÅ) :=
  add_comm f‚ÇÅ f‚ÇÇ ‚ñ∏ h.right_isBigO_add

/-- If `f x = O(g x)` along `cofinite`, then there exists a positive constant `C` such that
`‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ` whenever `g x ‚â† 0`. -/
theorem bound_of_isBigO_cofinite (h : f =O[cofinite] g'') :
    ‚àÉ C > 0, ‚àÄ ‚¶Éx‚¶Ñ, g'' x ‚â† 0 ‚Üí ‚Äñf x‚Äñ ‚â§ C * ‚Äñg'' x‚Äñ := by
  rcases h.exists_pos with ‚ü®C, C‚ÇÄ, hC‚ü©
  rw [IsBigOWith_def, eventually_cofinite] at hC
  rcases (hC.toFinset.image fun x ‚Ü¶ ‚Äñf x‚Äñ / ‚Äñg'' x‚Äñ).exists_le with ‚ü®C', hC'‚ü©
  have : ‚àÄ x, C * ‚Äñg'' x‚Äñ < ‚Äñf x‚Äñ ‚Üí ‚Äñf x‚Äñ / ‚Äñg'' x‚Äñ ‚â§ C' := by simpa using hC'
  refine ‚ü®max C C', lt_max_iff.2 (Or.inl C‚ÇÄ), fun x h‚ÇÄ ‚Ü¶ ?_‚ü©
  rw [max_mul_of_nonneg _ _ (norm_nonneg _), le_max_iff, or_iff_not_imp_left, not_le]
  exact fun hx ‚Ü¶ (div_le_iff‚ÇÄ (norm_pos_iff.2 h‚ÇÄ)).1 (this _ hx)

theorem isBigO_cofinite_iff (h : ‚àÄ x, g'' x = 0 ‚Üí f'' x = 0) :
    f'' =O[cofinite] g'' ‚Üî ‚àÉ C, ‚àÄ x, ‚Äñf'' x‚Äñ ‚â§ C * ‚Äñg'' x‚Äñ := by
  classical
  exact ‚ü®fun h' ‚Ü¶
    let ‚ü®C, _C‚ÇÄ, hC‚ü© := bound_of_isBigO_cofinite h'
    ‚ü®C, fun x ‚Ü¶ if hx : g'' x = 0 then by simp [h _ hx, hx] else hC hx‚ü©,
    fun h ‚Ü¶ (isBigO_top.2 h).mono le_top‚ü©

theorem bound_of_isBigO_nat_atTop {f : ‚Ñï ‚Üí E} {g'' : ‚Ñï ‚Üí E''} (h : f =O[atTop] g'') :
    ‚àÉ C > 0, ‚àÄ ‚¶Éx‚¶Ñ, g'' x ‚â† 0 ‚Üí ‚Äñf x‚Äñ ‚â§ C * ‚Äñg'' x‚Äñ :=
  bound_of_isBigO_cofinite <| by rwa [Nat.cofinite_eq_atTop]

theorem isBigO_nat_atTop_iff {f : ‚Ñï ‚Üí E''} {g : ‚Ñï ‚Üí F''} (h : ‚àÄ x, g x = 0 ‚Üí f x = 0) :
    f =O[atTop] g ‚Üî ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ := by
  rw [‚Üê Nat.cofinite_eq_atTop, isBigO_cofinite_iff h]

theorem isBigO_one_nat_atTop_iff {f : ‚Ñï ‚Üí E''} :
    f =O[atTop] (fun _n ‚Ü¶ 1 : ‚Ñï ‚Üí ‚Ñù) ‚Üî ‚àÉ C, ‚àÄ n, ‚Äñf n‚Äñ ‚â§ C :=
  Iff.trans (isBigO_nat_atTop_iff fun _ h ‚Ü¶ (one_ne_zero h).elim) <| by
    simp only [norm_one, mul_one]

theorem IsBigO.nat_of_atTop {f : ‚Ñï ‚Üí E''} {g : ‚Ñï ‚Üí F''} (hfg : f =O[atTop] g)
    {l : Filter ‚Ñï} (h : ‚àÄ·∂† n in l, g n = 0 ‚Üí f n = 0) : f =O[l] g := by
  obtain ‚ü®C, hC_pos, hC‚ü© := bound_of_isBigO_nat_atTop hfg
  refine isBigO_iff.mpr ‚ü®C, ?_‚ü©
  filter_upwards [h] with x h
  by_cases hf : f x = 0
  ¬∑ simp [hf, hC_pos]
  exact hC fun a ‚Ü¶ hf (h a)

theorem isBigOWith_pi {Œπ : Type*} [Fintype Œπ] {E' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (E' i)]
    {f : Œ± ‚Üí ‚àÄ i, E' i} {C : ‚Ñù} (hC : 0 ‚â§ C) :
    IsBigOWith C l f g' ‚Üî ‚àÄ i, IsBigOWith C l (fun x ‚Ü¶ f x i) g' := by
  have : ‚àÄ x, 0 ‚â§ C * ‚Äñg' x‚Äñ := fun x ‚Ü¶ mul_nonneg hC (norm_nonneg _)
  simp only [isBigOWith_iff, pi_norm_le_iff_of_nonneg (this _), eventually_all]

@[simp]
theorem isBigO_pi {Œπ : Type*} [Fintype Œπ] {E' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (E' i)]
    {f : Œ± ‚Üí ‚àÄ i, E' i} : f =O[l] g' ‚Üî ‚àÄ i, (fun x ‚Ü¶ f x i) =O[l] g' := by
  simp only [isBigO_iff_eventually_isBigOWith, ‚Üê eventually_all]
  exact eventually_congr (eventually_atTop.2 ‚ü®0, fun c ‚Ü¶ isBigOWith_pi‚ü©)

@[simp]
theorem isLittleO_pi {Œπ : Type*} [Fintype Œπ] {E' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (E' i)]
    {f : Œ± ‚Üí ‚àÄ i, E' i} : f =o[l] g' ‚Üî ‚àÄ i, (fun x ‚Ü¶ f x i) =o[l] g' := by
  simp +contextual only [IsLittleO_def, isBigOWith_pi, le_of_lt]
  exact ‚ü®fun h i c hc ‚Ü¶ h hc i, fun h c hc i ‚Ü¶ h i hc‚ü©

theorem IsBigO.natCast_atTop {R : Type*} [Semiring R] [PartialOrder R] [IsStrictOrderedRing R]
    [Archimedean R]
    {f : R ‚Üí E} {g : R ‚Üí F} (h : f =O[atTop] g) :
    (fun (n : ‚Ñï) ‚Ü¶ f n) =O[atTop] (fun n ‚Ü¶ g n) :=
  IsBigO.comp_tendsto h tendsto_natCast_atTop_atTop

theorem IsLittleO.natCast_atTop {R : Type*} [Semiring R] [PartialOrder R] [IsStrictOrderedRing R]
    [Archimedean R]
    {f : R ‚Üí E} {g : R ‚Üí F} (h : f =o[atTop] g) :
    (fun (n : ‚Ñï) ‚Ü¶ f n) =o[atTop] (fun n ‚Ü¶ g n) :=
  IsLittleO.comp_tendsto h tendsto_natCast_atTop_atTop

theorem isBigO_atTop_iff_eventually_exists {Œ± : Type*} [SemilatticeSup Œ±] [Nonempty Œ±]
    {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} : f =O[atTop] g ‚Üî ‚àÄ·∂† n‚ÇÄ in atTop, ‚àÉ c, ‚àÄ n ‚â• n‚ÇÄ, ‚Äñf n‚Äñ ‚â§ c * ‚Äñg n‚Äñ := by
  rw [isBigO_iff, exists_eventually_atTop]

theorem isBigO_atTop_iff_eventually_exists_pos {Œ± : Type*}
    [SemilatticeSup Œ±] [Nonempty Œ±] {f : Œ± ‚Üí G} {g : Œ± ‚Üí G'} :
    f =O[atTop] g ‚Üî ‚àÄ·∂† n‚ÇÄ in atTop, ‚àÉ c > 0, ‚àÄ n ‚â• n‚ÇÄ, c * ‚Äñf n‚Äñ ‚â§ ‚Äñg n‚Äñ := by
  simp_rw [isBigO_iff'', ‚Üê exists_prop, Subtype.exists', exists_eventually_atTop]

lemma isBigO_mul_iff_isBigO_div {f g h : Œ± ‚Üí ùïú} (hf : ‚àÄ·∂† x in l, f x ‚â† 0) :
    (fun x ‚Ü¶ f x * g x) =O[l] h ‚Üî g =O[l] (fun x ‚Ü¶ h x / f x) := by
  rw [isBigO_iff', isBigO_iff']
  refine ‚ü®fun ‚ü®c, hc, H‚ü© ‚Ü¶ ‚ü®c, hc, ?_‚ü©, fun ‚ü®c, hc, H‚ü© ‚Ü¶ ‚ü®c, hc, ?_‚ü©‚ü© <;>
  ¬∑ refine H.congr <| Eventually.mp hf <| Eventually.of_forall fun x hx ‚Ü¶ ?_
    rw [norm_mul, norm_div, ‚Üê mul_div_assoc, le_div_iff‚ÇÄ' (norm_pos_iff.mpr hx)]

end Asymptotics

open Asymptotics

theorem summable_of_isBigO {Œπ E} [SeminormedAddCommGroup E] [CompleteSpace E]
    {f : Œπ ‚Üí E} {g : Œπ ‚Üí ‚Ñù} (hg : Summable g) (h : f =O[cofinite] g) : Summable f :=
  let ‚ü®_, hC‚ü© := h.isBigOWith
  .of_norm_bounded_eventually (hg.abs.mul_left _) hC.bound

theorem summable_of_isBigO_nat {E} [SeminormedAddCommGroup E] [CompleteSpace E]
    {f : ‚Ñï ‚Üí E} {g : ‚Ñï ‚Üí ‚Ñù} (hg : Summable g) (h : f =O[atTop] g) : Summable f :=
  summable_of_isBigO hg <| Nat.cofinite_eq_atTop.symm ‚ñ∏ h

lemma Asymptotics.IsBigO.comp_summable_norm {Œπ E F : Type*}
    [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] {f : E ‚Üí F} {g : Œπ ‚Üí E}
    (hf : f =O[ùìù 0] id) (hg : Summable (‚Äñg ¬∑‚Äñ)) : Summable (‚Äñf <| g ¬∑‚Äñ) :=
  summable_of_isBigO hg <| hf.norm_norm.comp_tendsto <|
    tendsto_zero_iff_norm_tendsto_zero.2 hg.tendsto_cofinite_zero

namespace PartialHomeomorph

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
variable {E : Type*} [Norm E] {F : Type*} [Norm F]

/-- Transfer `IsBigOWith` over a `PartialHomeomorph`. -/
theorem isBigOWith_congr (e : PartialHomeomorph Œ± Œ≤) {b : Œ≤} (hb : b ‚àà e.target) {f : Œ≤ ‚Üí E}
    {g : Œ≤ ‚Üí F} {C : ‚Ñù} : IsBigOWith C (ùìù b) f g ‚Üî IsBigOWith C (ùìù (e.symm b)) (f ‚àò e) (g ‚àò e) :=
  ‚ü®fun h ‚Ü¶
    h.comp_tendsto <| by
      have := e.continuousAt (e.map_target hb)
      rwa [ContinuousAt, e.rightInvOn hb] at this,
    fun h ‚Ü¶
    (h.comp_tendsto (e.continuousAt_symm hb)).congr' rfl
      ((e.eventually_right_inverse hb).mono fun _ hx ‚Ü¶ congr_arg f hx)
      ((e.eventually_right_inverse hb).mono fun _ hx ‚Ü¶ congr_arg g hx)‚ü©

/-- Transfer `IsBigO` over a `PartialHomeomorph`. -/
theorem isBigO_congr (e : PartialHomeomorph Œ± Œ≤) {b : Œ≤} (hb : b ‚àà e.target) {f : Œ≤ ‚Üí E}
    {g : Œ≤ ‚Üí F} : f =O[ùìù b] g ‚Üî (f ‚àò e) =O[ùìù (e.symm b)] (g ‚àò e) := by
  simp only [IsBigO_def]
  exact exists_congr fun C ‚Ü¶ e.isBigOWith_congr hb

/-- Transfer `IsLittleO` over a `PartialHomeomorph`. -/
theorem isLittleO_congr (e : PartialHomeomorph Œ± Œ≤) {b : Œ≤} (hb : b ‚àà e.target) {f : Œ≤ ‚Üí E}
    {g : Œ≤ ‚Üí F} : f =o[ùìù b] g ‚Üî (f ‚àò e) =o[ùìù (e.symm b)] (g ‚àò e) := by
  simp only [IsLittleO_def]
  exact forall‚ÇÇ_congr fun c _hc ‚Ü¶ e.isBigOWith_congr hb

end PartialHomeomorph

namespace Homeomorph

variable {Œ± : Type*} {Œ≤ : Type*} [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
variable {E : Type*} [Norm E] {F : Type*} [Norm F]

open Asymptotics

/-- Transfer `IsBigOWith` over a `Homeomorph`. -/
theorem isBigOWith_congr (e : Œ± ‚âÉ‚Çú Œ≤) {b : Œ≤} {f : Œ≤ ‚Üí E} {g : Œ≤ ‚Üí F} {C : ‚Ñù} :
    IsBigOWith C (ùìù b) f g ‚Üî IsBigOWith C (ùìù (e.symm b)) (f ‚àò e) (g ‚àò e) :=
  e.toPartialHomeomorph.isBigOWith_congr trivial

/-- Transfer `IsBigO` over a `Homeomorph`. -/
theorem isBigO_congr (e : Œ± ‚âÉ‚Çú Œ≤) {b : Œ≤} {f : Œ≤ ‚Üí E} {g : Œ≤ ‚Üí F} :
    f =O[ùìù b] g ‚Üî (f ‚àò e) =O[ùìù (e.symm b)] (g ‚àò e) := by
  simp only [IsBigO_def]
  exact exists_congr fun C ‚Ü¶ e.isBigOWith_congr

/-- Transfer `IsLittleO` over a `Homeomorph`. -/
theorem isLittleO_congr (e : Œ± ‚âÉ‚Çú Œ≤) {b : Œ≤} {f : Œ≤ ‚Üí E} {g : Œ≤ ‚Üí F} :
    f =o[ùìù b] g ‚Üî (f ‚àò e) =o[ùìù (e.symm b)] (g ‚àò e) := by
  simp only [IsLittleO_def]
  exact forall‚ÇÇ_congr fun c _hc ‚Ü¶ e.isBigOWith_congr

end Homeomorph

namespace ContinuousOn

variable {Œ± E F : Type*} [TopologicalSpace Œ±] {s : Set Œ±} {f : Œ± ‚Üí E} {c : F}

section IsBigO

variable [SeminormedAddGroup E] [Norm F]

protected theorem isBigOWith_principal
    (hf : ContinuousOn f s) (hs : IsCompact s) (hc : ‚Äñc‚Äñ ‚â† 0) :
    IsBigOWith (sSup (Norm.norm '' (f '' s)) / ‚Äñc‚Äñ) (ùìü s) f fun _ ‚Ü¶ c := by
  rw [isBigOWith_principal, div_mul_cancel‚ÇÄ _ hc]
  exact fun x hx ‚Ü¶ hs.image_of_continuousOn hf |>.image continuous_norm
   |>.isLUB_sSup (Set.image_nonempty.mpr <| Set.image_nonempty.mpr ‚ü®x, hx‚ü©)
   |>.left <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx

protected theorem isBigO_principal (hf : ContinuousOn f s) (hs : IsCompact s)
    (hc : ‚Äñc‚Äñ ‚â† 0) : f =O[ùìü s] fun _ ‚Ü¶ c :=
  (hf.isBigOWith_principal hs hc).isBigO

end IsBigO

section IsBigORev

variable [NormedAddGroup E] [SeminormedAddGroup F]

protected theorem isBigOWith_rev_principal
    (hf : ContinuousOn f s) (hs : IsCompact s) (hC : ‚àÄ i ‚àà s, f i ‚â† 0) (c : F) :
    IsBigOWith (‚Äñc‚Äñ / sInf (Norm.norm '' (f '' s))) (ùìü s) (fun _ ‚Ü¶ c) f := by
  refine isBigOWith_principal.mpr fun x hx ‚Ü¶ ?_
  rw [mul_comm_div]
  replace hs := hs.image_of_continuousOn hf |>.image continuous_norm
  have h_sInf := hs.isGLB_sInf <| Set.image_nonempty.mpr <| Set.image_nonempty.mpr ‚ü®x, hx‚ü©
  refine le_mul_of_one_le_right (norm_nonneg c) <| (one_le_div ?_).mpr <|
    h_sInf.1 <| Set.mem_image_of_mem _ <| Set.mem_image_of_mem _ hx
  obtain ‚ü®_, ‚ü®x, hx, hCx‚ü©, hnormCx‚ü© := hs.sInf_mem h_sInf.nonempty
  rw [‚Üê hnormCx, ‚Üê hCx]
  exact (norm_ne_zero_iff.mpr (hC x hx)).symm.lt_of_le (norm_nonneg _)

protected theorem isBigO_rev_principal (hf : ContinuousOn f s)
    (hs : IsCompact s) (hC : ‚àÄ i ‚àà s, f i ‚â† 0) (c : F) : (fun _ ‚Ü¶ c) =O[ùìü s] f :=
  (hf.isBigOWith_rev_principal hs hC c).isBigO

end IsBigORev

end ContinuousOn

/-- The (scalar) product of a sequence that tends to zero with a bounded one also tends to zero. -/
lemma NormedField.tendsto_zero_smul_of_tendsto_zero_of_bounded {Œπ ùïú ùî∏ : Type*}
    [NormedDivisionRing ùïú] [NormedAddCommGroup ùî∏] [Module ùïú ùî∏] [IsBoundedSMul ùïú ùî∏] {l : Filter Œπ}
    {Œµ : Œπ ‚Üí ùïú} {f : Œπ ‚Üí ùî∏} (hŒµ : Tendsto Œµ l (ùìù 0)) (hf : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f)) :
    Tendsto (Œµ ‚Ä¢ f) l (ùìù 0) := by
  rw [‚Üê isLittleO_one_iff ùïú] at hŒµ ‚ä¢
  simpa using IsLittleO.smul_isBigO hŒµ (hf.isBigO_const (one_ne_zero : (1 : ùïú) ‚â† 0))
