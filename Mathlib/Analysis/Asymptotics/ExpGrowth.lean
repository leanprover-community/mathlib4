/-
Copyright (c) 2025 Damien Thomine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Damien Thomine
-/
import Mathlib.Analysis.Asymptotics.LinearGrowth
import Mathlib.Analysis.SpecialFunctions.Log.ENNRealLogExp

/-!
# Exponential growth

This file defines the exponential growth of a sequence `u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû`. This notion comes in two
versions, using a `liminf` and a `limsup` respectively.

## Main definitions

- `expGrowthInf`, `expGrowthSup`: respectively, `liminf` and `limsup` of `log (u n) / n`.
- `expGrowthInfTopHom`, `expGrowthSupBotHom`: the functions `expGrowthInf`, `expGrowthSup`
as homomorphisms preserving finitary `Inf`/`Sup` respectively.

## Tags

asymptotics, exponential
-/

namespace ExpGrowth

open ENNReal EReal Filter Function LinearGrowth
open scoped Topology

/-! ### Definition -/

/-- Lower exponential growth of a sequence of extended nonnegative real numbers. -/
noncomputable def expGrowthInf (u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) : EReal := liminf (fun n ‚Ü¶ log (u n) / n) atTop

/-- Upper exponential growth of a sequence of extended nonnegative real numbers. -/
noncomputable def expGrowthSup (u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) : EReal := limsup (fun n ‚Ü¶ log (u n) / n) atTop

lemma expGrowthInf_def {u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} :
    expGrowthInf u = linearGrowthInf (log ‚àò u) := by
  rfl

lemma expGrowthSup_def {u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} :
    expGrowthSup u = linearGrowthSup (log ‚àò u) := by
  rfl

/-! ### Basic properties -/

section basic_properties

variable {u v : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} {a : EReal} {b : ‚Ñù‚â•0‚àû}

lemma expGrowthInf_congr (h : u =·∂†[atTop] v) :
    expGrowthInf u = expGrowthInf v :=
  liminf_congr (h.mono fun _ uv ‚Ü¶ uv ‚ñ∏ rfl)

lemma expGrowthSup_congr (h : u =·∂†[atTop] v) :
    expGrowthSup u = expGrowthSup v :=
  limsup_congr (h.mono fun _ uv ‚Ü¶ uv ‚ñ∏ rfl)

lemma expGrowthInf_eventually_monotone (h : u ‚â§·∂†[atTop] v) :
    expGrowthInf u ‚â§ expGrowthInf v :=
  liminf_le_liminf (h.mono fun n uv ‚Ü¶ monotone_div_right_of_nonneg n.cast_nonneg' (log_monotone uv))

lemma expGrowthInf_monotone : Monotone expGrowthInf :=
  fun _ _ uv ‚Ü¶ expGrowthInf_eventually_monotone (Eventually.of_forall uv)

lemma expGrowthSup_eventually_monotone (h : u ‚â§·∂†[atTop] v) :
    expGrowthSup u ‚â§ expGrowthSup v :=
  limsup_le_limsup (h.mono fun n uv ‚Ü¶ monotone_div_right_of_nonneg n.cast_nonneg' (log_monotone uv))

lemma expGrowthSup_monotone : Monotone expGrowthSup :=
  fun _ _ uv ‚Ü¶ expGrowthSup_eventually_monotone (Eventually.of_forall uv)

lemma expGrowthInf_le_expGrowthSup : expGrowthInf u ‚â§ expGrowthSup u := liminf_le_limsup

lemma expGrowthInf_le_expGrowthSup_of_frequently_le (h : ‚àÉ·∂† n in atTop, u n ‚â§ v n) :
    expGrowthInf u ‚â§ expGrowthSup v :=
  liminf_le_limsup_of_frequently_le <| h.mono fun n u_v ‚Ü¶ by gcongr

lemma expGrowthInf_le_iff :
    expGrowthInf u ‚â§ a ‚Üî ‚àÄ b > a, ‚àÉ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (b * n) := by
  rw [expGrowthInf, liminf_le_iff']
  refine forall‚ÇÇ_congr fun b _ ‚Ü¶ frequently_congr (eventually_atTop.2 ‚ü®1, fun n _ ‚Ü¶ ?_‚ü©)
  rw [div_le_iff_le_mul (by norm_cast) (natCast_ne_top n), ‚Üê log_exp (n * b), mul_comm _ b]
  exact logOrderIso.le_iff_le

lemma le_expGrowthInf_iff :
    a ‚â§ expGrowthInf u ‚Üî ‚àÄ b < a, ‚àÄ·∂† n : ‚Ñï in atTop, exp (b * n) ‚â§ u n := by
  rw [expGrowthInf, le_liminf_iff']
  refine forall‚ÇÇ_congr fun b _ ‚Ü¶ eventually_congr (eventually_atTop.2 ‚ü®1, fun n _ ‚Ü¶ ?_‚ü©)
  nth_rw 1 [le_div_iff_mul_le (by norm_cast) (natCast_ne_top n), ‚Üê log_exp (b * n)]
  exact logOrderIso.le_iff_le

lemma expGrowthSup_le_iff :
    expGrowthSup u ‚â§ a ‚Üî ‚àÄ b > a, ‚àÄ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (b * n) := by
  rw [expGrowthSup, limsup_le_iff']
  refine forall‚ÇÇ_congr fun b _ ‚Ü¶ eventually_congr (eventually_atTop.2 ‚ü®1, fun n _ ‚Ü¶ ?_‚ü©)
  rw [div_le_iff_le_mul (by norm_cast) (natCast_ne_top n), ‚Üê log_exp (n * b), mul_comm _ b]
  exact logOrderIso.le_iff_le

lemma le_expGrowthSup_iff :
    a ‚â§ expGrowthSup u ‚Üî ‚àÄ b < a, ‚àÉ·∂† n : ‚Ñï in atTop, exp (b * n) ‚â§ u n := by
  rw [expGrowthSup, le_limsup_iff']
  refine forall‚ÇÇ_congr fun b _ ‚Ü¶ frequently_congr (eventually_atTop.2 ‚ü®1, fun n _ ‚Ü¶ ?_‚ü©)
  nth_rw 1 [le_div_iff_mul_le (by norm_cast) (natCast_ne_top n), ‚Üê log_exp (b * n)]
  exact logOrderIso.le_iff_le

/- Forward direction of `expGrowthInf_le_iff`. -/
lemma frequently_le_exp (h : expGrowthInf u < a) :
    ‚àÉ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (a * n) :=
  expGrowthInf_le_iff.1 (le_refl (expGrowthInf u)) a h

/- Forward direction of `le_expGrowthInf_iff`. -/
lemma eventually_exp_le (h : a < expGrowthInf u) :
    ‚àÄ·∂† n : ‚Ñï in atTop, exp (a * n) ‚â§ u n :=
  le_expGrowthInf_iff.1 (le_refl (expGrowthInf u)) a h

/- Forward direction of `expGrowthSup_le_iff`. -/
lemma eventually_le_exp (h : expGrowthSup u < a) :
    ‚àÄ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (a * n) :=
  expGrowthSup_le_iff.1 (le_refl (expGrowthSup u)) a h

/- Forward direction of `le_expGrowthSup_iff`. -/
lemma frequently_exp_le (h : a < expGrowthSup u) :
    ‚àÉ·∂† n : ‚Ñï in atTop, exp (a * n) ‚â§ u n :=
  le_expGrowthSup_iff.1 (le_refl (expGrowthSup u)) a h

lemma _root_.Frequently.expGrowthInf_le (h : ‚àÉ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (a * n)) :
    expGrowthInf u ‚â§ a := by
  apply expGrowthInf_le_iff.2 fun c c_u ‚Ü¶ h.mono fun n hn ‚Ü¶ hn.trans ?_
  gcongr

lemma _root_.Eventually.le_expGrowthInf (h : ‚àÄ·∂† n : ‚Ñï in atTop, exp (a * n) ‚â§ u n) :
    a ‚â§ expGrowthInf u :=
  le_expGrowthInf_iff.2 fun c c_u ‚Ü¶ h.mono fun n hn ‚Ü¶ hn.trans' <| by gcongr

lemma _root_.Eventually.expGrowthSup_le (h : ‚àÄ·∂† n : ‚Ñï in atTop, u n ‚â§ exp (a * n)) :
    expGrowthSup u ‚â§ a :=
  expGrowthSup_le_iff.2 fun c c_u ‚Ü¶ h.mono fun n hn ‚Ü¶ hn.trans <| by gcongr

lemma _root_.Frequently.le_expGrowthSup (h : ‚àÉ·∂† n : ‚Ñï in atTop, exp (a * n) ‚â§ u n) :
    a ‚â§ expGrowthSup u :=
  le_expGrowthSup_iff.2 fun c c_u ‚Ü¶ h.mono fun n hn ‚Ü¶ hn.trans' <| by gcongr

/-! ### Special cases -/

lemma expGrowthSup_zero : expGrowthSup 0 = ‚ä• := by
  rw [‚Üê linearGrowthSup_bot, expGrowthSup_def]
  congr 1
  ext _
  rw [comp_apply, Pi.zero_apply, Pi.bot_apply, log_zero]

lemma expGrowthInf_zero : expGrowthInf 0 = ‚ä• := by
  apply le_bot_iff.1
  rw [‚Üê expGrowthSup_zero]
  exact expGrowthInf_le_expGrowthSup

lemma expGrowthInf_top : expGrowthInf ‚ä§ = ‚ä§ := by
  rw [‚Üê linearGrowthInf_top, expGrowthInf_def]
  rfl

lemma expGrowthSup_top : expGrowthSup ‚ä§ = ‚ä§ := by
  apply top_le_iff.1
  rw [‚Üê expGrowthInf_top]
  exact expGrowthInf_le_expGrowthSup

lemma expGrowthInf_const (h : b ‚â† 0) (h' : b ‚â† ‚àû) : expGrowthInf (fun _ ‚Ü¶ b) = 0 :=
  (tendsto_const_div_atTop_nhds_zero_nat (fun k ‚Ü¶ h (log_eq_bot_iff.1 k))
    (fun k ‚Ü¶ h' (log_eq_top_iff.1 k))).liminf_eq

lemma expGrowthSup_const (h : b ‚â† 0) (h' : b ‚â† ‚àû) : expGrowthSup (fun _ ‚Ü¶ b) = 0 :=
  (tendsto_const_div_atTop_nhds_zero_nat (fun k ‚Ü¶ h (log_eq_bot_iff.1 k))
    (fun k ‚Ü¶ h' (log_eq_top_iff.1 k))).limsup_eq

lemma expGrowthInf_pow : expGrowthInf (fun n ‚Ü¶ b ^ n) = log b := by
  rw [expGrowthInf, ‚Üê liminf_const (f := atTop (Œ± := ‚Ñï)) (log b)]
  refine liminf_congr (eventually_atTop.2 ‚ü®1, fun n n_1 ‚Ü¶ ?_‚ü©)
  rw [EReal.div_eq_iff (natCast_ne_bot n) (natCast_ne_top n)
    (zero_lt_one.trans_le (Nat.one_le_cast.2 n_1)).ne.symm, log_pow, mul_comm]

lemma expGrowthSup_pow : expGrowthSup (fun n ‚Ü¶ b ^ n) = log b := by
  rw [expGrowthSup, ‚Üê limsup_const (f := atTop (Œ± := ‚Ñï)) (log b)]
  refine limsup_congr (eventually_atTop.2 ‚ü®1, fun n n_1 ‚Ü¶ ?_‚ü©)
  rw [EReal.div_eq_iff (natCast_ne_bot n) (natCast_ne_top n)
    (zero_lt_one.trans_le (Nat.one_le_cast.2 n_1)).ne.symm, log_pow, mul_comm]

lemma expGrowthInf_exp : expGrowthInf (fun n ‚Ü¶ exp (a * n)) = a :=
  le_antisymm (Frequently.expGrowthInf_le (Frequently.of_forall fun _ ‚Ü¶ le_refl _))
    (Eventually.le_expGrowthInf (Eventually.of_forall fun _ ‚Ü¶ le_refl _))

lemma expGrowthSup_exp : expGrowthSup (fun n ‚Ü¶ exp (a * n)) = a :=
  le_antisymm (Eventually.expGrowthSup_le (Eventually.of_forall fun _ ‚Ü¶ le_refl _))
    (Frequently.le_expGrowthSup (Frequently.of_forall fun _ ‚Ü¶ le_refl _))

/-! ### Multiplication and inversion -/

lemma le_expGrowthInf_mul :
    expGrowthInf u + expGrowthInf v ‚â§ expGrowthInf (u * v) := by
  refine le_liminf_add.trans_eq (liminf_congr (Eventually.of_forall fun n ‚Ü¶ ?_))
  rw [Pi.add_apply, Pi.mul_apply, ‚Üê add_div_of_nonneg_right n.cast_nonneg', log_mul_add]

/-- See `expGrowthInf_mul_le'` for a version with swapped argument `u` and `v`. -/
lemma expGrowthInf_mul_le (h : expGrowthSup u ‚â† ‚ä• ‚à® expGrowthInf v ‚â† ‚ä§)
    (h' : expGrowthSup u ‚â† ‚ä§ ‚à® expGrowthInf v ‚â† ‚ä•) :
    expGrowthInf (u * v) ‚â§ expGrowthSup u + expGrowthInf v := by
  refine (liminf_add_le h h').trans_eq' (liminf_congr (Eventually.of_forall fun n ‚Ü¶ ?_))
  rw [Pi.add_apply, Pi.mul_apply, ‚Üê add_div_of_nonneg_right n.cast_nonneg', log_mul_add]

/-- See `expGrowthInf_mul_le` for a version with swapped argument `u` and `v`. -/
lemma expGrowthInf_mul_le' (h : expGrowthInf u ‚â† ‚ä• ‚à® expGrowthSup v ‚â† ‚ä§)
    (h' : expGrowthInf u ‚â† ‚ä§ ‚à® expGrowthSup v ‚â† ‚ä•) :
    expGrowthInf (u * v) ‚â§ expGrowthInf u + expGrowthSup v := by
  rw [mul_comm, add_comm]
  exact expGrowthInf_mul_le h'.symm h.symm

/-- See `le_expGrowthSup_mul'` for a version with swapped argument `u` and `v`. -/
lemma le_expGrowthSup_mul : expGrowthSup u + expGrowthInf v ‚â§ expGrowthSup (u * v) := by
  refine le_limsup_add.trans_eq (limsup_congr (Eventually.of_forall fun n ‚Ü¶ ?_))
  rw [Pi.add_apply, Pi.mul_apply, log_mul_add, add_div_of_nonneg_right n.cast_nonneg']

/-- See `le_expGrowthSup_mul` for a version with swapped argument `u` and `v`. -/
lemma le_expGrowthSup_mul' : expGrowthInf u + expGrowthSup v ‚â§ expGrowthSup (u * v) := by
  rw [mul_comm, add_comm]
  exact le_expGrowthSup_mul

lemma expGrowthSup_mul_le (h : expGrowthSup u ‚â† ‚ä• ‚à® expGrowthSup v ‚â† ‚ä§)
    (h' : expGrowthSup u ‚â† ‚ä§ ‚à® expGrowthSup v ‚â† ‚ä•) :
    expGrowthSup (u * v) ‚â§ expGrowthSup u + expGrowthSup v := by
  refine (limsup_add_le h h').trans_eq' (limsup_congr (Eventually.of_forall fun n ‚Ü¶ ?_))
  rw [Pi.add_apply, Pi.mul_apply, log_mul_add, add_div_of_nonneg_right n.cast_nonneg']

lemma expGrowthInf_inv : expGrowthInf u‚Åª¬π = - expGrowthSup u := by
  rw [expGrowthSup, ‚Üê liminf_neg]
  refine liminf_congr (Eventually.of_forall fun n ‚Ü¶ ?_)
  rw [Pi.neg_apply, Pi.inv_apply, div_eq_mul_inv, div_eq_mul_inv, ‚Üê EReal.neg_mul, log_inv]

lemma expGrowthSup_inv : expGrowthSup u‚Åª¬π = - expGrowthInf u := by
  rw [expGrowthInf, ‚Üê limsup_neg]
  refine limsup_congr (Eventually.of_forall fun n ‚Ü¶ ?_)
  rw [Pi.neg_apply, Pi.inv_apply, div_eq_mul_inv, div_eq_mul_inv, ‚Üê EReal.neg_mul, log_inv]

/-! ### Comparison -/

-- Bound on `expGrowthInf` under a `IsBigO` hypothesis. However, `‚Ñù‚â•0‚àû` is not normed, so the
-- `IsBigO` property is spelt out.
lemma expGrowthInf_le_of_eventually_le (hb : b ‚â† ‚àû) (h : ‚àÄ·∂† n in atTop, u n ‚â§ b * v n) :
    expGrowthInf u ‚â§ expGrowthInf v := by
  apply (expGrowthInf_eventually_monotone h).trans
  rcases eq_zero_or_pos b with rfl | b_pos
  ¬∑ simp only [zero_mul, ‚Üê Pi.zero_def, expGrowthInf_zero, bot_le]
  ¬∑ apply (expGrowthInf_mul_le _ _).trans_eq <;> rw [expGrowthSup_const b_pos.ne' hb]
    ¬∑ exact zero_add (expGrowthInf v)
    ¬∑ exact .inl zero_ne_bot
    ¬∑ exact .inl zero_ne_top

-- Bound on `expGrowthSup` under a `IsBigO` hypothesis. However, `‚Ñù‚â•0‚àû` is not normed, so the
-- `IsBigO` property is spelt out.
lemma expGrowthSup_le_of_eventually_le (hb : b ‚â† ‚àû) (h : ‚àÄ·∂† n in atTop, u n ‚â§ b * v n) :
    expGrowthSup u ‚â§ expGrowthSup v := by
  apply (expGrowthSup_eventually_monotone h).trans
  rcases eq_zero_or_pos b with rfl | b_pos
  ¬∑ simp only [zero_mul, ‚Üê Pi.zero_def, expGrowthSup_zero, bot_le]
  ¬∑ apply (expGrowthSup_mul_le _ _).trans_eq <;> rw [expGrowthSup_const b_pos.ne' hb]
    ¬∑ exact zero_add (expGrowthSup v)
    ¬∑ exact .inl zero_ne_bot
    ¬∑ exact .inl zero_ne_top

lemma expGrowthInf_of_eventually_ge (hb : b ‚â† 0) (h : ‚àÄ·∂† n in atTop, b * u n ‚â§ v n) :
    expGrowthInf u ‚â§ expGrowthInf v := by
  apply (expGrowthInf_eventually_monotone h).trans' (le_expGrowthInf_mul.trans' _)
  rcases eq_top_or_lt_top b with rfl | b_top
  ¬∑ rw [‚Üê Pi.top_def, expGrowthInf_top]
    exact le_add_of_nonneg_left le_top
  ¬∑ rw [expGrowthInf_const hb b_top.ne, zero_add]

lemma expGrowthSup_of_eventually_ge (hb : b ‚â† 0) (h : ‚àÄ·∂† n in atTop, b * u n ‚â§ v n) :
    expGrowthSup u ‚â§ expGrowthSup v := by
  apply (expGrowthSup_eventually_monotone h).trans' (le_expGrowthSup_mul'.trans' _)
  rcases eq_top_or_lt_top b with rfl | b_top
  ¬∑ exact expGrowthInf_top ‚ñ∏ le_add_of_nonneg_left le_top
  ¬∑ rw [expGrowthInf_const hb b_top.ne, zero_add]

/-! ### Infimum and supremum -/

lemma expGrowthInf_inf : expGrowthInf (u ‚äì v) = expGrowthInf u ‚äì expGrowthInf v := by
  rw [expGrowthInf, expGrowthInf, expGrowthInf, ‚Üê liminf_min]
  refine liminf_congr (Eventually.of_forall fun n ‚Ü¶ ?_)
  rw [Pi.inf_apply, log_monotone.map_min]
  exact (monotone_div_right_of_nonneg n.cast_nonneg').map_min

/-- Lower exponential growth as an `InfTopHom`. -/
noncomputable def expGrowthInfTopHom : InfTopHom (‚Ñï ‚Üí ‚Ñù‚â•0‚àû) EReal where
  toFun := expGrowthInf
  map_inf' _ _ := expGrowthInf_inf
  map_top' := expGrowthInf_top

lemma expGrowthInf_biInf {Œ± : Type*} (u : Œ± ‚Üí ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) {s : Set Œ±} (hs : s.Finite) :
    expGrowthInf (‚®Ö x ‚àà s, u x) = ‚®Ö x ‚àà s, expGrowthInf (u x) := by
  have := map_finset_inf expGrowthInfTopHom hs.toFinset u
  simpa only [expGrowthInfTopHom, InfTopHom.coe_mk, InfHom.coe_mk, Finset.inf_eq_iInf,
    hs.mem_toFinset, comp_apply]

lemma expGrowthInf_iInf {Œπ : Type*} [Finite Œπ] (u : Œπ ‚Üí ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) :
    expGrowthInf (‚®Ö i, u i) = ‚®Ö i, expGrowthInf (u i) := by
  rw [‚Üê iInf_univ, expGrowthInf_biInf u Set.finite_univ, iInf_univ]

lemma expGrowthSup_sup : expGrowthSup (u ‚äî v) = expGrowthSup u ‚äî expGrowthSup v := by
  rw [expGrowthSup, expGrowthSup, expGrowthSup, ‚Üê limsup_max]
  refine limsup_congr (Eventually.of_forall fun n ‚Ü¶ ?_)
  rw [Pi.sup_apply, log_monotone.map_max]
  exact (monotone_div_right_of_nonneg n.cast_nonneg').map_max

/-- Upper exponential growth as a `SupBotHom`. -/
noncomputable def expGrowthSupBotHom : SupBotHom (‚Ñï ‚Üí ‚Ñù‚â•0‚àû) EReal where
  toFun := expGrowthSup
  map_sup' _ _ := expGrowthSup_sup
  map_bot' := expGrowthSup_zero

lemma expGrowthSup_biSup {Œ± : Type*} (u : Œ± ‚Üí ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) {s : Set Œ±} (hs : s.Finite) :
    expGrowthSup (‚®Ü x ‚àà s, u x) = ‚®Ü x ‚àà s, expGrowthSup (u x) := by
  have := map_finset_sup expGrowthSupBotHom hs.toFinset u
  simpa only [expGrowthSupBotHom, SupBotHom.coe_mk, SupHom.coe_mk, Finset.sup_eq_iSup,
    hs.mem_toFinset, comp_apply]

lemma expGrowthSup_iSup {Œπ : Type*} [Finite Œπ] (u : Œπ ‚Üí ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) :
    expGrowthSup (‚®Ü i, u i) = ‚®Ü i, expGrowthSup (u i) := by
  rw [‚Üê iSup_univ, expGrowthSup_biSup u Set.finite_univ, iSup_univ]

/-! ### Addition -/

lemma le_expGrowthInf_add : expGrowthInf u ‚äî expGrowthInf v ‚â§ expGrowthInf (u + v) :=
  sup_le (expGrowthInf_monotone le_self_add) (expGrowthInf_monotone le_add_self)

lemma expGrowthSup_add : expGrowthSup (u + v) = expGrowthSup u ‚äî expGrowthSup v := by
  rw [‚Üê expGrowthSup_sup]
  apply le_antisymm
  ¬∑ refine expGrowthSup_le_of_eventually_le (b := 2) ofNat_ne_top (Eventually.of_forall fun n ‚Ü¶ ?_)
    rw [Pi.sup_apply u v n, Pi.add_apply u v n, two_mul]
    exact add_le_add (le_max_left (u n) (v n)) (le_max_right (u n) (v n))
  ¬∑ refine expGrowthSup_monotone fun n ‚Ü¶ ?_
    exact sup_le (self_le_add_right (u n) (v n)) (self_le_add_left (v n) (u n))

-- By lemma `expGrowthSup_add`, `expGrowthSup` is an `AddMonoidHom` from `‚Ñï ‚Üí ‚Ñù‚â•0‚àû` to
-- `Tropical EReal·µí·µà`. Lemma `expGrowthSup_sum` is exactly `Finset.trop_inf`. We prove it from
-- scratch to reduce imports.
lemma expGrowthSup_sum {Œ± : Type*} (u : Œ± ‚Üí ‚Ñï ‚Üí ‚Ñù‚â•0‚àû) (s : Finset Œ±) :
    expGrowthSup (‚àë x ‚àà s, u x) = ‚®Ü x ‚àà s, expGrowthSup (u x) := by
  classical
  induction s using Finset.induction_on with
  | empty => rw [Finset.sum_empty, ‚Üê Finset.iSup_coe, Finset.coe_empty, iSup_emptyset,
    expGrowthSup_zero]
  | insert a t a_t ha => rw [Finset.sum_insert a_t, expGrowthSup_add, ‚Üê Finset.iSup_coe,
    Finset.coe_insert a t, iSup_insert, Finset.iSup_coe, ha]

end basic_properties

/-! ### Composition -/

section composition

variable {u : ‚Ñï ‚Üí ‚Ñù‚â•0‚àû} {v : ‚Ñï ‚Üí ‚Ñï}

lemma le_expGrowthInf_comp (hu : 1 ‚â§·∂†[atTop] u) (hv : Tendsto v atTop atTop) :
    (linearGrowthInf fun n ‚Ü¶ v n : EReal) * expGrowthInf u ‚â§ expGrowthInf (u ‚àò v) := by
  apply le_linearGrowthInf_comp (hu.mono fun n h ‚Ü¶ ?_) hv
  rw [Pi.one_apply] at h
  rwa [Pi.zero_apply, zero_le_log_iff]

lemma expGrowthSup_comp_le (hu : ‚àÉ·∂† n in atTop, 1 ‚â§ u n)
    (hv‚ÇÄ : (linearGrowthSup fun n ‚Ü¶ v n : EReal) ‚â† 0)
    (hv‚ÇÅ : (linearGrowthSup fun n ‚Ü¶ v n : EReal) ‚â† ‚ä§) (hv‚ÇÇ : Tendsto v atTop atTop) :
    expGrowthSup (u ‚àò v) ‚â§ (linearGrowthSup fun n ‚Ü¶ v n : EReal) * expGrowthSup u := by
  apply linearGrowthSup_comp_le (u := log ‚àò u) (hu.mono fun n h ‚Ü¶ ?_) hv‚ÇÄ hv‚ÇÅ hv‚ÇÇ
  rwa [comp_apply, zero_le_log_iff]

/-! ### Monotone sequences -/

lemma _root_.Monotone.expGrowthInf_nonneg (h : Monotone u) (h' : u ‚â† 0) :
    0 ‚â§ expGrowthInf u := by
  apply (log_monotone.comp h).linearGrowthInf_nonneg
  simp only [ne_eq, funext_iff, comp_apply, Pi.bot_apply, log_eq_bot_iff, Pi.zero_apply] at h' ‚ä¢
  exact h'

lemma _root_.Monotone.expGrowthSup_nonneg (h : Monotone u) (h' : u ‚â† 0) :
    0 ‚â§ expGrowthSup u :=
  (h.expGrowthInf_nonneg h').trans expGrowthInf_le_expGrowthSup

lemma expGrowthInf_comp_nonneg (h : Monotone u) (h' : u ‚â† 0) (hv : Tendsto v atTop atTop) :
    0 ‚â§ expGrowthInf (u ‚àò v) := by
  apply linearGrowthInf_comp_nonneg (u := log ‚àò u) (log_monotone.comp h) _ hv
  simp only [ne_eq, funext_iff, comp_apply, Pi.bot_apply, log_eq_bot_iff, Pi.zero_apply] at h' ‚ä¢
  exact h'

lemma expGrowthSup_comp_nonneg (h : Monotone u) (h' : u ‚â† 0) (hv : Tendsto v atTop atTop) :
    0 ‚â§ expGrowthSup (u ‚àò v) :=
  (expGrowthInf_comp_nonneg h h' hv).trans expGrowthInf_le_expGrowthSup

lemma _root_.Monotone.expGrowthInf_comp_le (h : Monotone u)
    (hv‚ÇÄ : (linearGrowthSup fun n ‚Ü¶ v n : EReal) ‚â† 0)
    (hv‚ÇÅ : (linearGrowthSup fun n ‚Ü¶ v n : EReal) ‚â† ‚ä§) :
    expGrowthInf (u ‚àò v) ‚â§ (linearGrowthSup fun n ‚Ü¶ v n : EReal) * expGrowthInf u :=
  (log_monotone.comp h).linearGrowthInf_comp_le hv‚ÇÄ hv‚ÇÅ

lemma _root_.Monotone.le_expGrowthSup_comp (h : Monotone u)
    (hv : (linearGrowthInf fun n ‚Ü¶ v n : EReal) ‚â† 0) :
    (linearGrowthInf fun n ‚Ü¶ v n : EReal) * expGrowthSup u ‚â§ expGrowthSup (u ‚àò v) :=
  (log_monotone.comp h).le_linearGrowthSup_comp hv

lemma _root_.Monotone.expGrowthInf_comp {a : EReal} (h : Monotone u)
    (hv : Tendsto (fun n ‚Ü¶ (v n : EReal) / n) atTop (ùìù a)) (ha : a ‚â† 0) (ha' : a ‚â† ‚ä§) :
    expGrowthInf (u ‚àò v) = a * expGrowthInf u :=
  (log_monotone.comp h).linearGrowthInf_comp hv ha ha'

lemma _root_.Monotone.expGrowthSup_comp {a : EReal} (h : Monotone u)
    (hv : Tendsto (fun n ‚Ü¶ (v n : EReal) / n) atTop (ùìù a)) (ha : a ‚â† 0) (ha' : a ‚â† ‚ä§) :
    expGrowthSup (u ‚àò v) = a * expGrowthSup u :=
  (log_monotone.comp h).linearGrowthSup_comp hv ha ha'

lemma _root_.Monotone.expGrowthInf_comp_mul {m : ‚Ñï} (h : Monotone u) (hm : m ‚â† 0) :
    expGrowthInf (fun n ‚Ü¶ u (m * n)) = m * expGrowthInf u :=
  (log_monotone.comp h).linearGrowthInf_comp_mul hm

lemma _root_.Monotone.expGrowthSup_comp_mul {m : ‚Ñï} (h : Monotone u) (hm : m ‚â† 0) :
    expGrowthSup (fun n ‚Ü¶ u (m * n)) = m * expGrowthSup u :=
  (log_monotone.comp h).linearGrowthSup_comp_mul hm

end composition

end ExpGrowth
