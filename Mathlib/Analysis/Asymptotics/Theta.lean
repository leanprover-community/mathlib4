/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.Lemmas
import Mathlib.Analysis.Normed.Module.Basic

/-!
# Asymptotic equivalence up to a constant

In this file we define `Asymptotics.IsTheta l f g` (notation: `f =Î˜[l] g`) as
`f =O[l] g âˆ§ g =O[l] f`, then prove basic properties of this equivalence relation.
-/


open Filter

open Topology

namespace Asymptotics


variable {Î± : Type*} {Î² : Type*} {E : Type*} {F : Type*} {G : Type*} {E' : Type*}
  {F' : Type*} {G' : Type*} {E'' : Type*} {F'' : Type*} {G'' : Type*} {R : Type*}
  {R' : Type*} {ğ•œ : Type*} {ğ•œ' : Type*}

variable [Norm E] [Norm F] [Norm G]
variable [SeminormedAddCommGroup E'] [SeminormedAddCommGroup F'] [SeminormedAddCommGroup G']
  [NormedAddCommGroup E''] [NormedAddCommGroup F''] [NormedAddCommGroup G''] [SeminormedRing R]
  [SeminormedRing R']

variable [NormedField ğ•œ] [NormedField ğ•œ']
variable {c c' câ‚ câ‚‚ : â„} {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G}
variable {f' : Î± â†’ E'} {g' : Î± â†’ F'} {k' : Î± â†’ G'}
variable {f'' : Î± â†’ E''} {g'' : Î± â†’ F''}
variable {l l' : Filter Î±}

/-- We say that `f` is `Î˜(g)` along a filter `l` (notation: `f =Î˜[l] g`) if `f =O[l] g` and
`g =O[l] f`. -/
def IsTheta (l : Filter Î±) (f : Î± â†’ E) (g : Î± â†’ F) : Prop :=
  IsBigO l f g âˆ§ IsBigO l g f

@[inherit_doc]
notation:100 f " =Î˜[" l "] " g:100 => IsTheta l f g

theorem IsBigO.antisymm (hâ‚ : f =O[l] g) (hâ‚‚ : g =O[l] f) : f =Î˜[l] g :=
  âŸ¨hâ‚, hâ‚‚âŸ©

lemma IsTheta.isBigO (h : f =Î˜[l] g) : f =O[l] g := h.1

lemma IsTheta.isBigO_symm (h : f =Î˜[l] g) : g =O[l] f := h.2

@[refl]
theorem isTheta_refl (f : Î± â†’ E) (l : Filter Î±) : f =Î˜[l] f :=
  âŸ¨isBigO_refl _ _, isBigO_refl _ _âŸ©

theorem isTheta_rfl : f =Î˜[l] f :=
  isTheta_refl _ _

@[symm]
nonrec theorem IsTheta.symm (h : f =Î˜[l] g) : g =Î˜[l] f :=
  h.symm

theorem isTheta_comm : f =Î˜[l] g â†” g =Î˜[l] f :=
  âŸ¨fun h â†¦ h.symm, fun h â†¦ h.symmâŸ©

@[trans]
theorem IsTheta.trans {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g) (hâ‚‚ : g =Î˜[l] k) :
    f =Î˜[l] k :=
  âŸ¨hâ‚.1.trans hâ‚‚.1, hâ‚‚.2.trans hâ‚.2âŸ©

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F') (Î³ := Î± â†’ G) (IsTheta l) (IsTheta l) (IsTheta l) :=
  âŸ¨IsTheta.transâŸ©

@[trans]
theorem IsBigO.trans_isTheta {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =O[l] g)
    (hâ‚‚ : g =Î˜[l] k) : f =O[l] k :=
  hâ‚.trans hâ‚‚.1

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F') (Î³ := Î± â†’ G) (IsBigO l) (IsTheta l) (IsBigO l) :=
  âŸ¨IsBigO.trans_isThetaâŸ©

@[trans]
theorem IsTheta.trans_isBigO {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g)
    (hâ‚‚ : g =O[l] k) : f =O[l] k :=
  hâ‚.1.trans hâ‚‚

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F') (Î³ := Î± â†’ G) (IsTheta l) (IsBigO l) (IsBigO l) :=
  âŸ¨IsTheta.trans_isBigOâŸ©

@[trans]
theorem IsLittleO.trans_isTheta {f : Î± â†’ E} {g : Î± â†’ F} {k : Î± â†’ G'} (hâ‚ : f =o[l] g)
    (hâ‚‚ : g =Î˜[l] k) : f =o[l] k :=
  hâ‚.trans_isBigO hâ‚‚.1

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F') (Î³ := Î± â†’ G') (IsLittleO l) (IsTheta l) (IsLittleO l) :=
  âŸ¨IsLittleO.trans_isThetaâŸ©

@[trans]
theorem IsTheta.trans_isLittleO {f : Î± â†’ E} {g : Î± â†’ F'} {k : Î± â†’ G} (hâ‚ : f =Î˜[l] g)
    (hâ‚‚ : g =o[l] k) : f =o[l] k :=
  hâ‚.1.trans_isLittleO hâ‚‚

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F') (Î³ := Î± â†’ G) (IsTheta l) (IsLittleO l) (IsLittleO l) :=
  âŸ¨IsTheta.trans_isLittleOâŸ©

@[trans]
theorem IsTheta.trans_eventuallyEq {f : Î± â†’ E} {gâ‚ gâ‚‚ : Î± â†’ F} (h : f =Î˜[l] gâ‚) (hg : gâ‚ =á¶ [l] gâ‚‚) :
    f =Î˜[l] gâ‚‚ :=
  âŸ¨h.1.trans_eventuallyEq hg, hg.symm.trans_isBigO h.2âŸ©

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ F) (Î³ := Î± â†’ F) (IsTheta l) (EventuallyEq l) (IsTheta l) :=
  âŸ¨IsTheta.trans_eventuallyEqâŸ©

@[trans]
theorem _root_.Filter.EventuallyEq.trans_isTheta {fâ‚ fâ‚‚ : Î± â†’ E} {g : Î± â†’ F} (hf : fâ‚ =á¶ [l] fâ‚‚)
    (h : fâ‚‚ =Î˜[l] g) : fâ‚ =Î˜[l] g :=
  âŸ¨hf.trans_isBigO h.1, h.2.trans_eventuallyEq hf.symmâŸ©

instance : Trans (Î± := Î± â†’ E) (Î² := Î± â†’ E) (Î³ := Î± â†’ F) (EventuallyEq l) (IsTheta l) (IsTheta l) :=
  âŸ¨EventuallyEq.trans_isThetaâŸ©

lemma _root_.Filter.EventuallyEq.isTheta {f g : Î± â†’ E} (h : f =á¶ [l] g) : f =Î˜[l] g :=
  h.trans_isTheta isTheta_rfl

@[simp]
theorem isTheta_bot : f =Î˜[âŠ¥] g := by simp [IsTheta]

@[simp]
theorem isTheta_norm_left : (fun x â†¦ â€–f' xâ€–) =Î˜[l] g â†” f' =Î˜[l] g := by simp [IsTheta]

@[simp]
theorem isTheta_norm_right : (f =Î˜[l] fun x â†¦ â€–g' xâ€–) â†” f =Î˜[l] g' := by simp [IsTheta]

alias âŸ¨IsTheta.of_norm_left, IsTheta.norm_leftâŸ© := isTheta_norm_left

alias âŸ¨IsTheta.of_norm_right, IsTheta.norm_rightâŸ© := isTheta_norm_right

theorem IsTheta.of_norm_eventuallyEq_norm (h : (fun x â†¦ â€–f xâ€–) =á¶ [l] fun x â†¦ â€–g xâ€–) : f =Î˜[l] g :=
  âŸ¨.of_bound' h.le, .of_bound' h.symm.leâŸ©

@[deprecated (since := "2025-01-03")]
alias isTheta_of_norm_eventuallyEq := IsTheta.of_norm_eventuallyEq_norm

theorem IsTheta.of_norm_eventuallyEq {g : Î± â†’ â„} (h : (fun x â†¦ â€–f' xâ€–) =á¶ [l] g) : f' =Î˜[l] g :=
  of_norm_eventuallyEq_norm <| h.mono fun x hx â†¦ by simp only [â† hx, norm_norm]

@[deprecated (since := "2025-01-03")]
alias isTheta_of_norm_eventuallyEq' := IsTheta.of_norm_eventuallyEq

theorem IsTheta.isLittleO_congr_left (h : f' =Î˜[l] g') : f' =o[l] k â†” g' =o[l] k :=
  âŸ¨h.symm.trans_isLittleO, h.trans_isLittleOâŸ©

theorem IsTheta.isLittleO_congr_right (h : g' =Î˜[l] k') : f =o[l] g' â†” f =o[l] k' :=
  âŸ¨fun H â†¦ H.trans_isTheta h, fun H â†¦ H.trans_isTheta h.symmâŸ©

theorem IsTheta.isBigO_congr_left (h : f' =Î˜[l] g') : f' =O[l] k â†” g' =O[l] k :=
  âŸ¨h.symm.trans_isBigO, h.trans_isBigOâŸ©

theorem IsTheta.isBigO_congr_right (h : g' =Î˜[l] k') : f =O[l] g' â†” f =O[l] k' :=
  âŸ¨fun H â†¦ H.trans_isTheta h, fun H â†¦ H.trans_isTheta h.symmâŸ©

lemma IsTheta.isTheta_congr_left (h : f' =Î˜[l] g') : f' =Î˜[l] k â†” g' =Î˜[l] k :=
  h.isBigO_congr_left.and h.isBigO_congr_right

lemma IsTheta.isTheta_congr_right (h : f' =Î˜[l] g') : k =Î˜[l] f' â†” k =Î˜[l] g' :=
  h.isBigO_congr_right.and h.isBigO_congr_left

theorem IsTheta.mono (h : f =Î˜[l] g) (hl : l' â‰¤ l) : f =Î˜[l'] g :=
  âŸ¨h.1.mono hl, h.2.mono hlâŸ©

theorem IsTheta.sup (h : f' =Î˜[l] g') (h' : f' =Î˜[l'] g') : f' =Î˜[l âŠ” l'] g' :=
  âŸ¨h.1.sup h'.1, h.2.sup h'.2âŸ©

@[simp]
theorem isTheta_sup : f' =Î˜[l âŠ” l'] g' â†” f' =Î˜[l] g' âˆ§ f' =Î˜[l'] g' :=
  âŸ¨fun h â†¦ âŸ¨h.mono le_sup_left, h.mono le_sup_rightâŸ©, fun h â†¦ h.1.sup h.2âŸ©

theorem IsTheta.eq_zero_iff (h : f'' =Î˜[l] g'') : âˆ€á¶  x in l, f'' x = 0 â†” g'' x = 0 :=
  h.1.eq_zero_imp.mp <| h.2.eq_zero_imp.mono fun _ â†¦ Iff.intro

theorem IsTheta.tendsto_zero_iff (h : f'' =Î˜[l] g'') :
    Tendsto f'' l (ğ“ 0) â†” Tendsto g'' l (ğ“ 0) := by
  simp only [â† isLittleO_one_iff â„, h.isLittleO_congr_left]

theorem IsTheta.tendsto_norm_atTop_iff (h : f' =Î˜[l] g') :
    Tendsto (norm âˆ˜ f') l atTop â†” Tendsto (norm âˆ˜ g') l atTop := by
  simp only [Function.comp_def, â† isLittleO_const_left_of_ne (one_ne_zero' â„),
    h.isLittleO_congr_right]

theorem IsTheta.isBoundedUnder_le_iff (h : f' =Î˜[l] g') :
    IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f') â†” IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ g') := by
  simp only [â† isBigO_const_of_ne (one_ne_zero' â„), h.isBigO_congr_left]

theorem IsTheta.smul [NormedSpace ğ•œ E'] [NormedSpace ğ•œ' F'] {fâ‚ : Î± â†’ ğ•œ} {fâ‚‚ : Î± â†’ ğ•œ'} {gâ‚ : Î± â†’ E'}
    {gâ‚‚ : Î± â†’ F'} (hf : fâ‚ =Î˜[l] fâ‚‚) (hg : gâ‚ =Î˜[l] gâ‚‚) :
    (fun x â†¦ fâ‚ x â€¢ gâ‚ x) =Î˜[l] fun x â†¦ fâ‚‚ x â€¢ gâ‚‚ x :=
  âŸ¨hf.1.smul hg.1, hf.2.smul hg.2âŸ©

theorem IsTheta.mul {fâ‚ fâ‚‚ : Î± â†’ ğ•œ} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ'} (hâ‚ : fâ‚ =Î˜[l] gâ‚) (hâ‚‚ : fâ‚‚ =Î˜[l] gâ‚‚) :
    (fun x â†¦ fâ‚ x * fâ‚‚ x) =Î˜[l] fun x â†¦ gâ‚ x * gâ‚‚ x :=
  hâ‚.smul hâ‚‚

theorem IsTheta.listProd {Î¹ : Type*} {L : List Î¹} {f : Î¹ â†’ Î± â†’ ğ•œ} {g : Î¹ â†’ Î± â†’ ğ•œ'}
    (h : âˆ€ i âˆˆ L, f i =Î˜[l] g i) :
    (fun x â†¦ (L.map (f Â· x)).prod) =Î˜[l] (fun x â†¦ (L.map (g Â· x)).prod) :=
  âŸ¨.listProd fun i hi â†¦ (h i hi).isBigO, .listProd fun i hi â†¦ (h i hi).symm.isBigOâŸ©

theorem IsTheta.multisetProd {Î¹ : Type*} {s : Multiset Î¹} {f : Î¹ â†’ Î± â†’ ğ•œ} {g : Î¹ â†’ Î± â†’ ğ•œ'}
    (h : âˆ€ i âˆˆ s, f i =Î˜[l] g i) :
    (fun x â†¦ (s.map (f Â· x)).prod) =Î˜[l] (fun x â†¦ (s.map (g Â· x)).prod) :=
  âŸ¨.multisetProd fun i hi â†¦ (h i hi).isBigO, .multisetProd fun i hi â†¦ (h i hi).symm.isBigOâŸ©

theorem IsTheta.finsetProd {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ Î± â†’ ğ•œ} {g : Î¹ â†’ Î± â†’ ğ•œ'}
    (h : âˆ€ i âˆˆ s, f i =Î˜[l] g i) : (âˆ i âˆˆ s, f i Â·) =Î˜[l] (âˆ i âˆˆ s, g i Â·) :=
  âŸ¨.finsetProd fun i hi â†¦ (h i hi).isBigO, .finsetProd fun i hi â†¦ (h i hi).symm.isBigOâŸ©

theorem IsTheta.inv {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) :
    (fun x â†¦ (f x)â»Â¹) =Î˜[l] fun x â†¦ (g x)â»Â¹ :=
  âŸ¨h.2.inv_rev h.1.eq_zero_imp, h.1.inv_rev h.2.eq_zero_impâŸ©

@[simp]
theorem isTheta_inv {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} :
    ((fun x â†¦ (f x)â»Â¹) =Î˜[l] fun x â†¦ (g x)â»Â¹) â†” f =Î˜[l] g :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, IsTheta.invâŸ©

theorem IsTheta.div {fâ‚ fâ‚‚ : Î± â†’ ğ•œ} {gâ‚ gâ‚‚ : Î± â†’ ğ•œ'} (hâ‚ : fâ‚ =Î˜[l] gâ‚) (hâ‚‚ : fâ‚‚ =Î˜[l] gâ‚‚) :
    (fun x â†¦ fâ‚ x / fâ‚‚ x) =Î˜[l] fun x â†¦ gâ‚ x / gâ‚‚ x := by
  simpa only [div_eq_mul_inv] using hâ‚.mul hâ‚‚.inv

theorem IsTheta.pow {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) (n : â„•) :
    (fun x â†¦ f x ^ n) =Î˜[l] fun x â†¦ g x ^ n :=
  âŸ¨h.1.pow n, h.2.pow nâŸ©

theorem IsTheta.zpow {f : Î± â†’ ğ•œ} {g : Î± â†’ ğ•œ'} (h : f =Î˜[l] g) (n : â„¤) :
    (fun x â†¦ f x ^ n) =Î˜[l] fun x â†¦ g x ^ n := by
  cases n
  Â· simpa only [Int.ofNat_eq_coe, zpow_natCast] using h.pow _
  Â· simpa only [zpow_negSucc] using (h.pow _).inv

theorem isTheta_const_const {câ‚ : E''} {câ‚‚ : F''} (hâ‚ : câ‚ â‰  0) (hâ‚‚ : câ‚‚ â‰  0) :
    (fun _ : Î± â†¦ câ‚) =Î˜[l] fun _ â†¦ câ‚‚ :=
  âŸ¨isBigO_const_const _ hâ‚‚ _, isBigO_const_const _ hâ‚ _âŸ©

@[simp]
theorem isTheta_const_const_iff [NeBot l] {câ‚ : E''} {câ‚‚ : F''} :
    ((fun _ : Î± â†¦ câ‚) =Î˜[l] fun _ â†¦ câ‚‚) â†” (câ‚ = 0 â†” câ‚‚ = 0) := by
  simpa only [IsTheta, isBigO_const_const_iff, â† iff_def] using Iff.comm

@[simp]
theorem isTheta_zero_left : (fun _ â†¦ (0 : E')) =Î˜[l] g'' â†” g'' =á¶ [l] 0 := by
  simp only [IsTheta, isBigO_zero, isBigO_zero_right_iff, true_and]

@[simp]
theorem isTheta_zero_right : (f'' =Î˜[l] fun _ â†¦ (0 : F')) â†” f'' =á¶ [l] 0 :=
  isTheta_comm.trans isTheta_zero_left

theorem isTheta_const_smul_left [NormedSpace ğ•œ E'] {c : ğ•œ} (hc : c â‰  0) :
    (fun x â†¦ c â€¢ f' x) =Î˜[l] g â†” f' =Î˜[l] g :=
  and_congr (isBigO_const_smul_left hc) (isBigO_const_smul_right hc)

alias âŸ¨IsTheta.of_const_smul_left, IsTheta.const_smul_leftâŸ© := isTheta_const_smul_left

theorem isTheta_const_smul_right [NormedSpace ğ•œ F'] {c : ğ•œ} (hc : c â‰  0) :
    (f =Î˜[l] fun x â†¦ c â€¢ g' x) â†” f =Î˜[l] g' :=
  and_congr (isBigO_const_smul_right hc) (isBigO_const_smul_left hc)

alias âŸ¨IsTheta.of_const_smul_right, IsTheta.const_smul_rightâŸ© := isTheta_const_smul_right

theorem isTheta_const_mul_left {c : ğ•œ} {f : Î± â†’ ğ•œ} (hc : c â‰  0) :
    (fun x â†¦ c * f x) =Î˜[l] g â†” f =Î˜[l] g := by
  simpa only [â† smul_eq_mul] using isTheta_const_smul_left hc

alias âŸ¨IsTheta.of_const_mul_left, IsTheta.const_mul_leftâŸ© := isTheta_const_mul_left

theorem isTheta_const_mul_right {c : ğ•œ} {g : Î± â†’ ğ•œ} (hc : c â‰  0) :
    (f =Î˜[l] fun x â†¦ c * g x) â†” f =Î˜[l] g := by
  simpa only [â† smul_eq_mul] using isTheta_const_smul_right hc

alias âŸ¨IsTheta.of_const_mul_right, IsTheta.const_mul_rightâŸ© := isTheta_const_mul_right

theorem IsLittleO.right_isTheta_add {fâ‚ fâ‚‚ : Î± â†’ E'} (h : fâ‚ =o[l] fâ‚‚) :
    fâ‚‚ =Î˜[l] (fâ‚ + fâ‚‚) :=
  âŸ¨h.right_isBigO_add, h.add_isBigO (isBigO_refl _ _)âŸ©

theorem IsLittleO.right_isTheta_add' {fâ‚ fâ‚‚ : Î± â†’ E'} (h : fâ‚ =o[l] fâ‚‚) :
    fâ‚‚ =Î˜[l] (fâ‚‚ + fâ‚) :=
  add_comm fâ‚ fâ‚‚ â–¸ h.right_isTheta_add

lemma IsTheta.add_isLittleO {fâ‚ fâ‚‚ : Î± â†’ E'} {g : Î± â†’ F}
    (hÎ˜ : fâ‚ =Î˜[l] g) (ho : fâ‚‚ =o[l] g) : (fâ‚ + fâ‚‚) =Î˜[l] g :=
  (ho.trans_isTheta hÎ˜.symm).right_isTheta_add'.symm.trans hÎ˜

lemma IsLittleO.add_isTheta {fâ‚ fâ‚‚ : Î± â†’ E'} {g : Î± â†’ F}
    (ho : fâ‚ =o[l] g) (hÎ˜ : fâ‚‚ =Î˜[l] g) : (fâ‚ + fâ‚‚) =Î˜[l] g :=
  add_comm fâ‚ fâ‚‚ â–¸ hÎ˜.add_isLittleO ho

section

variable {f : Î± Ã— Î² â†’ E} {g : Î± Ã— Î² â†’ F} {l' : Filter Î²}

protected theorem IsTheta.fiberwise_right :
    f =Î˜[l Ã—Ë¢ l'] g â†’ âˆ€á¶  x in l, (f âŸ¨x, Â·âŸ©) =Î˜[l'] (g âŸ¨x, Â·âŸ©) := by
  simp only [IsTheta, eventually_and]
  exact fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ âŸ¨hâ‚.fiberwise_right, hâ‚‚.fiberwise_rightâŸ©

protected theorem IsTheta.fiberwise_left :
    f =Î˜[l Ã—Ë¢ l'] g â†’ âˆ€á¶  y in l', (f âŸ¨Â·, yâŸ©) =Î˜[l] (g âŸ¨Â·, yâŸ©) := by
  simp only [IsTheta, eventually_and]
  exact fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ âŸ¨hâ‚.fiberwise_left, hâ‚‚.fiberwise_leftâŸ©

end

section

variable (l' : Filter Î²)

protected theorem IsTheta.comp_fst : f =Î˜[l] g â†’ (f âˆ˜ Prod.fst) =Î˜[l Ã—Ë¢ l'] (g âˆ˜ Prod.fst) := by
  simp only [IsTheta, eventually_and]
  exact fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ âŸ¨hâ‚.comp_fst l', hâ‚‚.comp_fst l'âŸ©

protected theorem IsTheta.comp_snd : f =Î˜[l] g â†’ (f âˆ˜ Prod.snd) =Î˜[l' Ã—Ë¢ l] (g âˆ˜ Prod.snd) := by
  simp only [IsTheta, eventually_and]
  exact fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ âŸ¨hâ‚.comp_snd l', hâ‚‚.comp_snd l'âŸ©

end

end Asymptotics

namespace ContinuousOn

variable {Î± E F : Type*} [NormedAddGroup E] [SeminormedAddGroup F] [TopologicalSpace Î±]
  {s : Set Î±} {f : Î± â†’ E} {c : F}

protected theorem isTheta_principal
    (hf : ContinuousOn f s) (hs : IsCompact s) (hc : â€–câ€– â‰  0) (hC : âˆ€ i âˆˆ s, f i â‰  0) :
    f =Î˜[ğ“Ÿ s] fun _ => c :=
  âŸ¨hf.isBigO_principal hs hc, hf.isBigO_rev_principal hs hC câŸ©

end ContinuousOn
