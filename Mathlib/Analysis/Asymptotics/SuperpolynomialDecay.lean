/-
Copyright (c) 2021 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma
-/
import Mathlib.Algebra.Polynomial.Eval.Defs
import Mathlib.Analysis.Asymptotics.Lemmas

/-!
# Super-Polynomial Function Decay

This file defines a predicate `Asymptotics.SuperpolynomialDecay f` for a function satisfying
  one of following equivalent definitions (The definition is in terms of the first condition):

* `x ^ n * f` tends to `ùìù 0` for all (or sufficiently large) naturals `n`
* `|x ^ n * f|` tends to `ùìù 0` for all naturals `n` (`superpolynomialDecay_iff_abs_tendsto_zero`)
* `|x ^ n * f|` is bounded for all naturals `n` (`superpolynomialDecay_iff_abs_isBoundedUnder`)
* `f` is `o(x ^ c)` for all integers `c` (`superpolynomialDecay_iff_isLittleO`)
* `f` is `O(x ^ c)` for all integers `c` (`superpolynomialDecay_iff_isBigO`)

These conditions are all equivalent to conditions in terms of polynomials, replacing `x ^ c` with
  `p(x)` or `p(x)‚Åª¬π` as appropriate, since asymptotically `p(x)` behaves like `X ^ p.natDegree`.
These further equivalences are not proven in mathlib but would be good future projects.

The definition of superpolynomial decay for `f : Œ± ‚Üí Œ≤` is relative to a parameter `k : Œ± ‚Üí Œ≤`.
Super-polynomial decay then means `f x` decays faster than `(k x) ^ c` for all integers `c`.
Equivalently `f x` decays faster than `p.eval (k x)` for all polynomials `p : Œ≤[X]`.
The definition is also relative to a filter `l : Filter Œ±` where the decay rate is compared.

When the map `k` is given by `n ‚Ü¶ ‚Üën : ‚Ñï ‚Üí ‚Ñù` this defines negligible functions:
https://en.wikipedia.org/wiki/Negligible_function

When the map `k` is given by `(r‚ÇÅ,...,r‚Çô) ‚Ü¶ r‚ÇÅ*...*r‚Çô : ‚Ñù‚Åø ‚Üí ‚Ñù` this is equivalent
  to the definition of rapidly decreasing functions given here:
https://ncatlab.org/nlab/show/rapidly+decreasing+function

# Main Theorems

* `SuperpolynomialDecay.polynomial_mul` says that if `f(x)` is negligible,
    then so is `p(x) * f(x)` for any polynomial `p`.
* `superpolynomialDecay_iff_zpow_tendsto_zero` gives an equivalence between definitions in terms
    of decaying faster than `k(x) ^ n` for all naturals `n` or `k(x) ^ c` for all integer `c`.
-/


namespace Asymptotics

open Topology Polynomial

open Filter

/-- `f` has superpolynomial decay in parameter `k` along filter `l` if
  `k ^ n * f` tends to zero at `l` for all naturals `n` -/
def SuperpolynomialDecay {Œ± Œ≤ : Type*} [TopologicalSpace Œ≤] [CommSemiring Œ≤] (l : Filter Œ±)
    (k : Œ± ‚Üí Œ≤) (f : Œ± ‚Üí Œ≤) :=
  ‚àÄ n : ‚Ñï, Tendsto (fun a : Œ± => k a ^ n * f a) l (ùìù 0)

variable {Œ± Œ≤ : Type*} {l : Filter Œ±} {k : Œ± ‚Üí Œ≤} {f g g' : Œ± ‚Üí Œ≤}

section CommSemiring

variable [TopologicalSpace Œ≤] [CommSemiring Œ≤]

theorem SuperpolynomialDecay.congr' (hf : SuperpolynomialDecay l k f) (hfg : f =·∂†[l] g) :
    SuperpolynomialDecay l k g := fun z =>
  (hf z).congr' (EventuallyEq.mul (EventuallyEq.refl l _) hfg)

theorem SuperpolynomialDecay.congr (hf : SuperpolynomialDecay l k f) (hfg : ‚àÄ x, f x = g x) :
    SuperpolynomialDecay l k g := fun z =>
  (hf z).congr fun x => (congr_arg fun a => k x ^ z * a) <| hfg x

@[simp]
theorem superpolynomialDecay_zero (l : Filter Œ±) (k : Œ± ‚Üí Œ≤) : SuperpolynomialDecay l k 0 :=
  fun z => by simpa only [Pi.zero_apply, mul_zero] using tendsto_const_nhds

theorem SuperpolynomialDecay.add [ContinuousAdd Œ≤] (hf : SuperpolynomialDecay l k f)
    (hg : SuperpolynomialDecay l k g) : SuperpolynomialDecay l k (f + g) := fun z => by
  simpa only [mul_add, add_zero, Pi.add_apply] using (hf z).add (hg z)

theorem SuperpolynomialDecay.mul [ContinuousMul Œ≤] (hf : SuperpolynomialDecay l k f)
    (hg : SuperpolynomialDecay l k g) : SuperpolynomialDecay l k (f * g) := fun z => by
  simpa only [mul_assoc, one_mul, mul_zero, pow_zero] using (hf z).mul (hg 0)

theorem SuperpolynomialDecay.mul_const [ContinuousMul Œ≤] (hf : SuperpolynomialDecay l k f) (c : Œ≤) :
    SuperpolynomialDecay l k fun n => f n * c := fun z => by
  simpa only [‚Üê mul_assoc, zero_mul] using Tendsto.mul_const c (hf z)

theorem SuperpolynomialDecay.const_mul [ContinuousMul Œ≤] (hf : SuperpolynomialDecay l k f) (c : Œ≤) :
    SuperpolynomialDecay l k fun n => c * f n :=
  (hf.mul_const c).congr fun _ => mul_comm _ _

theorem SuperpolynomialDecay.param_mul (hf : SuperpolynomialDecay l k f) :
    SuperpolynomialDecay l k (k * f) := fun z =>
  tendsto_nhds.2 fun s hs hs0 =>
    l.sets_of_superset ((tendsto_nhds.1 (hf <| z + 1)) s hs hs0) fun x hx => by
      simpa only [Set.mem_preimage, Pi.mul_apply, ‚Üê mul_assoc, ‚Üê pow_succ] using hx

theorem SuperpolynomialDecay.mul_param (hf : SuperpolynomialDecay l k f) :
    SuperpolynomialDecay l k (f * k) :=
  hf.param_mul.congr fun _ => mul_comm _ _

theorem SuperpolynomialDecay.param_pow_mul (hf : SuperpolynomialDecay l k f) (n : ‚Ñï) :
    SuperpolynomialDecay l k (k ^ n * f) := by
  induction n with
  | zero => simpa only [one_mul, pow_zero] using hf
  | succ n hn => simpa only [pow_succ', mul_assoc] using hn.param_mul

theorem SuperpolynomialDecay.mul_param_pow (hf : SuperpolynomialDecay l k f) (n : ‚Ñï) :
    SuperpolynomialDecay l k (f * k ^ n) :=
  (hf.param_pow_mul n).congr fun _ => mul_comm _ _

theorem SuperpolynomialDecay.polynomial_mul [ContinuousAdd Œ≤] [ContinuousMul Œ≤]
    (hf : SuperpolynomialDecay l k f) (p : Œ≤[X]) :
    SuperpolynomialDecay l k fun x => (p.eval <| k x) * f x :=
  Polynomial.induction_on' p (fun p q hp hq => by simpa [add_mul] using hp.add hq) fun n c => by
    simpa [mul_assoc] using (hf.param_pow_mul n).const_mul c

theorem SuperpolynomialDecay.mul_polynomial [ContinuousAdd Œ≤] [ContinuousMul Œ≤]
    (hf : SuperpolynomialDecay l k f) (p : Œ≤[X]) :
    SuperpolynomialDecay l k fun x => f x * (p.eval <| k x) :=
  (hf.polynomial_mul p).congr fun _ => mul_comm _ _

end CommSemiring

section OrderedCommSemiring

variable [TopologicalSpace Œ≤] [CommSemiring Œ≤] [PartialOrder Œ≤] [IsOrderedRing Œ≤] [OrderTopology Œ≤]

theorem SuperpolynomialDecay.trans_eventuallyLE (hk : 0 ‚â§·∂†[l] k) (hg : SuperpolynomialDecay l k g)
    (hg' : SuperpolynomialDecay l k g') (hfg : g ‚â§·∂†[l] f) (hfg' : f ‚â§·∂†[l] g') :
    SuperpolynomialDecay l k f := fun z =>
  tendsto_of_tendsto_of_tendsto_of_le_of_le' (hg z) (hg' z)
    (by filter_upwards [hfg, hk] with x hx (hx' : 0 ‚â§ k x) using by gcongr)
    (by filter_upwards [hfg', hk] with x hx (hx' : 0 ‚â§ k x) using by gcongr)

end OrderedCommSemiring

section LinearOrderedCommRing

variable [TopologicalSpace Œ≤] [CommRing Œ≤] [LinearOrder Œ≤] [IsStrictOrderedRing Œ≤] [OrderTopology Œ≤]
variable (l k f)

theorem superpolynomialDecay_iff_abs_tendsto_zero :
    SuperpolynomialDecay l k f ‚Üî ‚àÄ n : ‚Ñï, Tendsto (fun a : Œ± => |k a ^ n * f a|) l (ùìù 0) :=
  ‚ü®fun h z => (tendsto_zero_iff_abs_tendsto_zero _).1 (h z), fun h z =>
    (tendsto_zero_iff_abs_tendsto_zero _).2 (h z)‚ü©

theorem superpolynomialDecay_iff_superpolynomialDecay_abs :
    SuperpolynomialDecay l k f ‚Üî SuperpolynomialDecay l (fun a => |k a|) fun a => |f a| :=
  (superpolynomialDecay_iff_abs_tendsto_zero l k f).trans
    (by simp_rw [SuperpolynomialDecay, abs_mul, abs_pow])

variable {l k f}

theorem SuperpolynomialDecay.trans_eventually_abs_le (hf : SuperpolynomialDecay l k f)
    (hfg : abs ‚àò g ‚â§·∂†[l] abs ‚àò f) : SuperpolynomialDecay l k g := by
  rw [superpolynomialDecay_iff_abs_tendsto_zero] at hf ‚ä¢
  refine fun z =>
    tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds (hf z)
      (Eventually.of_forall fun x => abs_nonneg _) (hfg.mono fun x hx => ?_)
  calc
    |k x ^ z * g x| = |k x ^ z| * |g x| := abs_mul (k x ^ z) (g x)
    _ ‚â§ |k x ^ z| * |f x| := by gcongr _ * ?_; exact hx
    _ = |k x ^ z * f x| := (abs_mul (k x ^ z) (f x)).symm

theorem SuperpolynomialDecay.trans_abs_le (hf : SuperpolynomialDecay l k f)
    (hfg : ‚àÄ x, |g x| ‚â§ |f x|) : SuperpolynomialDecay l k g :=
  hf.trans_eventually_abs_le (Eventually.of_forall hfg)

end LinearOrderedCommRing

section Field

variable [TopologicalSpace Œ≤] [Field Œ≤] (l k f)

theorem superpolynomialDecay_mul_const_iff [ContinuousMul Œ≤] {c : Œ≤} (hc0 : c ‚â† 0) :
    (SuperpolynomialDecay l k fun n => f n * c) ‚Üî SuperpolynomialDecay l k f :=
  ‚ü®fun h => (h.mul_const c‚Åª¬π).congr fun x => by simp [mul_assoc, mul_inv_cancel‚ÇÄ hc0], fun h =>
    h.mul_const c‚ü©

theorem superpolynomialDecay_const_mul_iff [ContinuousMul Œ≤] {c : Œ≤} (hc0 : c ‚â† 0) :
    (SuperpolynomialDecay l k fun n => c * f n) ‚Üî SuperpolynomialDecay l k f :=
  ‚ü®fun h => (h.const_mul c‚Åª¬π).congr fun x => by simp [‚Üê mul_assoc, inv_mul_cancel‚ÇÄ hc0], fun h =>
    h.const_mul c‚ü©

variable {l k f}

end Field

section LinearOrderedField

variable [TopologicalSpace Œ≤] [Field Œ≤] [LinearOrder Œ≤] [IsStrictOrderedRing Œ≤] [OrderTopology Œ≤]
variable (f)

theorem superpolynomialDecay_iff_abs_isBoundedUnder (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k f ‚Üî
    ‚àÄ z : ‚Ñï, IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun a : Œ± => |k a ^ z * f a| := by
  refine
    ‚ü®fun h z => Tendsto.isBoundedUnder_le (Tendsto.abs (h z)), fun h =>
      (superpolynomialDecay_iff_abs_tendsto_zero l k f).2 fun z => ?_‚ü©
  obtain ‚ü®m, hm‚ü© := h (z + 1)
  have h1 : Tendsto (fun _ : Œ± => (0 : Œ≤)) l (ùìù 0) := tendsto_const_nhds
  have h2 : Tendsto (fun a : Œ± => |(k a)‚Åª¬π| * m) l (ùìù 0) :=
    zero_mul m ‚ñ∏
      Tendsto.mul_const m ((tendsto_zero_iff_abs_tendsto_zero _).1 hk.inv_tendsto_atTop)
  refine
    tendsto_of_tendsto_of_tendsto_of_le_of_le' h1 h2 (Eventually.of_forall fun x => abs_nonneg _)
      ((eventually_map.1 hm).mp ?_)
  refine (hk.eventually_ne_atTop 0).mono fun x hk0 hx => ?_
  refine Eq.trans_le ?_ (mul_le_mul_of_nonneg_left hx <| abs_nonneg (k x)‚Åª¬π)
  rw [‚Üê abs_mul, ‚Üê mul_assoc, pow_succ', ‚Üê mul_assoc, inv_mul_cancel‚ÇÄ hk0, one_mul]

theorem superpolynomialDecay_iff_zpow_tendsto_zero (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k f ‚Üî ‚àÄ z : ‚Ñ§, Tendsto (fun a : Œ± => k a ^ z * f a) l (ùìù 0) := by
  refine ‚ü®fun h z => ?_, fun h n => by simpa only [zpow_natCast] using h (n : ‚Ñ§)‚ü©
  by_cases hz : 0 ‚â§ z
  ¬∑ unfold Tendsto
    lift z to ‚Ñï using hz
    simpa using h z
  ¬∑ have : Tendsto (fun a => k a ^ z) l (ùìù 0) :=
      Tendsto.comp (tendsto_zpow_atTop_zero (not_le.1 hz)) hk
    have h : Tendsto f l (ùìù 0) := by simpa using h 0
    exact zero_mul (0 : Œ≤) ‚ñ∏ this.mul h

variable {f}

theorem SuperpolynomialDecay.param_zpow_mul (hk : Tendsto k l atTop)
    (hf : SuperpolynomialDecay l k f) (z : ‚Ñ§) :
    SuperpolynomialDecay l k fun a => k a ^ z * f a := by
  rw [superpolynomialDecay_iff_zpow_tendsto_zero _ hk] at hf ‚ä¢
  refine fun z' => (hf <| z' + z).congr' ((hk.eventually_ne_atTop 0).mono fun x hx => ?_)
  simp [zpow_add‚ÇÄ hx, mul_assoc, Pi.mul_apply]

theorem SuperpolynomialDecay.mul_param_zpow (hk : Tendsto k l atTop)
    (hf : SuperpolynomialDecay l k f) (z : ‚Ñ§) : SuperpolynomialDecay l k fun a => f a * k a ^ z :=
  (hf.param_zpow_mul hk z).congr fun _ => mul_comm _ _

theorem SuperpolynomialDecay.inv_param_mul (hk : Tendsto k l atTop)
    (hf : SuperpolynomialDecay l k f) : SuperpolynomialDecay l k (k‚Åª¬π * f) := by
  simpa using hf.param_zpow_mul hk (-1)

theorem SuperpolynomialDecay.param_inv_mul (hk : Tendsto k l atTop)
    (hf : SuperpolynomialDecay l k f) : SuperpolynomialDecay l k (f * k‚Åª¬π) :=
  (hf.inv_param_mul hk).congr fun _ => mul_comm _ _

variable (f)

theorem superpolynomialDecay_param_mul_iff (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k (k * f) ‚Üî SuperpolynomialDecay l k f :=
  ‚ü®fun h =>
    (h.inv_param_mul hk).congr'
      ((hk.eventually_ne_atTop 0).mono fun x hx => by simp [‚Üê mul_assoc, inv_mul_cancel‚ÇÄ hx]),
    fun h => h.param_mul‚ü©

theorem superpolynomialDecay_mul_param_iff (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k (f * k) ‚Üî SuperpolynomialDecay l k f := by
  simpa [mul_comm k] using superpolynomialDecay_param_mul_iff f hk

theorem superpolynomialDecay_param_pow_mul_iff (hk : Tendsto k l atTop) (n : ‚Ñï) :
    SuperpolynomialDecay l k (k ^ n * f) ‚Üî SuperpolynomialDecay l k f := by
  induction n with
  | zero => simp
  | succ n hn =>
    simpa [pow_succ, ‚Üê mul_comm k, mul_assoc,
      superpolynomialDecay_param_mul_iff (k ^ n * f) hk] using hn

theorem superpolynomialDecay_mul_param_pow_iff (hk : Tendsto k l atTop) (n : ‚Ñï) :
    SuperpolynomialDecay l k (f * k ^ n) ‚Üî SuperpolynomialDecay l k f := by
  simpa [mul_comm f] using superpolynomialDecay_param_pow_mul_iff f hk n

variable {f}

end LinearOrderedField

section NormedLinearOrderedField

variable [NormedField Œ≤]
variable (l k f)

theorem superpolynomialDecay_iff_norm_tendsto_zero :
    SuperpolynomialDecay l k f ‚Üî ‚àÄ n : ‚Ñï, Tendsto (fun a : Œ± => ‚Äñk a ^ n * f a‚Äñ) l (ùìù 0) :=
  ‚ü®fun h z => tendsto_zero_iff_norm_tendsto_zero.1 (h z), fun h z =>
    tendsto_zero_iff_norm_tendsto_zero.2 (h z)‚ü©

theorem superpolynomialDecay_iff_superpolynomialDecay_norm :
    SuperpolynomialDecay l k f ‚Üî SuperpolynomialDecay l (fun a => ‚Äñk a‚Äñ) fun a => ‚Äñf a‚Äñ :=
  (superpolynomialDecay_iff_norm_tendsto_zero l k f).trans (by simp [SuperpolynomialDecay])

variable {l k}
variable [LinearOrder Œ≤] [IsStrictOrderedRing Œ≤] [OrderTopology Œ≤]

theorem superpolynomialDecay_iff_isBigO (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k f ‚Üî ‚àÄ z : ‚Ñ§, f =O[l] fun a : Œ± => k a ^ z := by
  refine (superpolynomialDecay_iff_zpow_tendsto_zero f hk).trans ?_
  have hk0 : ‚àÄ·∂† x in l, k x ‚â† 0 := hk.eventually_ne_atTop 0
  refine ‚ü®fun h z => ?_, fun h z => ?_‚ü©
  ¬∑ refine isBigO_of_div_tendsto_nhds (hk0.mono fun x hx hxz ‚Ü¶ absurd hxz (zpow_ne_zero _ hx)) 0 ?_
    have : (fun a : Œ± => k a ^ z)‚Åª¬π = fun a : Œ± => k a ^ (-z) := funext fun x => by simp
    rw [div_eq_mul_inv, mul_comm f, this]
    exact h (-z)
  ¬∑ suffices (fun a : Œ± => k a ^ z * f a) =O[l] fun a : Œ± => (k a)‚Åª¬π from
      IsBigO.trans_tendsto this hk.inv_tendsto_atTop
    refine ((isBigO_refl (fun a => k a ^ z) l).mul (h (-(z + 1)))).trans ?_
    refine .of_bound' <| hk0.mono fun a ha0 => ?_
    simp [‚Üê zpow_add‚ÇÄ ha0]

theorem superpolynomialDecay_iff_isLittleO (hk : Tendsto k l atTop) :
    SuperpolynomialDecay l k f ‚Üî ‚àÄ z : ‚Ñ§, f =o[l] fun a : Œ± => k a ^ z := by
  refine ‚ü®fun h z => ?_, fun h => (superpolynomialDecay_iff_isBigO f hk).2 fun z => (h z).isBigO‚ü©
  have hk0 : ‚àÄ·∂† x in l, k x ‚â† 0 := hk.eventually_ne_atTop 0
  have : (fun _ : Œ± => (1 : Œ≤)) =o[l] k :=
    isLittleO_of_tendsto' (hk0.mono fun x hkx hkx' => absurd hkx' hkx)
      (by simpa using hk.inv_tendsto_atTop)
  have : f =o[l] fun x : Œ± => k x * k x ^ (z - 1) := by
    simpa using this.mul_isBigO ((superpolynomialDecay_iff_isBigO f hk).1 h <| z - 1)
  refine this.trans_isBigO <| IsBigO.of_bound' <| hk0.mono fun x hkx => le_of_eq ?_
  simp [‚Üê zpow_one_add‚ÇÄ hkx]

end NormedLinearOrderedField

end Asymptotics
