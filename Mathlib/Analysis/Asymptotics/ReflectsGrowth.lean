/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.Theta

/-!
# Families with linear ordered asymptotics

We say that a function `g : Î¹ â†’ Î²` with linearly ordered codomain
*reflects growth rate* of a family of functions `f : Î¹ â†’ Î± â†’ E` along a filter `l : Filter Î±` if

- `f i =o[l] f j` whenever `g i < g j`;
- `f i =O[l] f j` whenever `g i â‰¤ g j`
  (the actual definition assumes `g i = g j`
  because the case `g i < g j` follows from the previous assumption);
- `âˆƒá¶  x in l, f i x â‰  0`.

The name is motivated by the following properties
of a triple `(f, g, l)` satisfying the above axioms.

- `f i =o[l] f j â†” g i < g j`, see `Asymptotics.ReflectsGrowth.isLittleO`;
- `f i =O[l] f j â†” g i â‰¤ g j`, see `Asymptotics.ReflectsGrowth.isBigO`;
- `f i =Î˜[l] f j â†” g i = g j`, see `Asymptotics.ReflectsGrowth.isTheta`.

## Keywords

asymptotics
-/

open Function Filter Bornology
open scoped Topology

namespace Asymptotics

section

variable {Î¹ Î± Î² E : Type*} [LinearOrder Î²] [NormedAddCommGroup E]

/-- A function `g : Î¹ â†’ Î²` with linear ordered codomain
*reflects growth* of a family of functions `f : Î¹ â†’ Î± â†’ E` along a filter `l`, if

- `f i =o[l] f j` iff `g i < g j`,
- `f i =O[l] f j` iff `g i â‰¤ g j`,
- none of `f i` is eventually equal to zero at `l`.

The actual definition assumes only two implications
-/
structure ReflectsGrowth (f : Î¹ â†’ Î± â†’ E) (g : Î¹ â†’ Î²) (l : Filter Î±) : Prop where
  isLittleO_of_lt {i j} : g i < g j â†’ f i =o[l] f j
  isBigO_of_eq {i j} : g i = g j â†’ f i =O[l] f j
  frequently_ne {i} : âˆƒá¶  x in l, f i x â‰  0

namespace ReflectsGrowth

variable {f : Î¹ â†’ Î± â†’ E} {g : Î¹ â†’ Î²} {l : Filter Î±} {i j : Î¹}

lemma isBigO (h : ReflectsGrowth f g l) : f i =O[l] f j â†” g i â‰¤ g j:=
  âŸ¨fun hO â†¦ not_lt.1 fun hlt â†¦ (h.isLittleO_of_lt hlt).not_isBigO h.frequently_ne hO, fun hle â†¦
    hle.eq_or_lt.elim h.isBigO_of_eq fun hij â†¦ (h.isLittleO_of_lt hij).isBigOâŸ©

lemma isLittleO (h : ReflectsGrowth f g l) : f i =o[l] f j â†” g i < g j := by
  cases lt_or_le (g i) (g j) with
  | inl hij => simp [hij, h.isLittleO_of_lt]
  | inr hji => simp [hji.not_lt, (h.isBigO.2 hji).not_isLittleO h.frequently_ne]

lemma isTheta (h : ReflectsGrowth f g l) : f i =Î˜[l] f j â†” g i = g j := by
  simp only [IsTheta, h.isBigO, le_antisymm_iff]

lemma congr_right {Î³ : Type*} [LinearOrder Î³] {g' : Î¹ â†’ Î³} (h : ReflectsGrowth f g l)
    (hg : âˆ€ {i j}, g i < g j â†” g' i < g' j) : ReflectsGrowth f g' l where
  isLittleO_of_lt hij := h.isLittleO.2 <| hg.2 hij
  isBigO_of_eq hij := h.isBigO.2 <| not_lt.1 <| mt hg.1 hij.not_gt
  frequently_ne := h.frequently_ne

lemma congr_left_isTheta {E' : Type*} [NormedAddCommGroup E'] {f' : Î¹ â†’ Î± â†’ E'}
    (h : ReflectsGrowth f g l) (hf : âˆ€ {i}, f i =Î˜[l] f' i) : ReflectsGrowth f' g l where
  isLittleO_of_lt := hf.isLittleO_congr_left.1 âˆ˜ hf.isLittleO_congr_right.1 âˆ˜ h.isLittleO.2
  isBigO_of_eq := hf.isBigO_congr_left.1 âˆ˜ hf.isBigO_congr_right.1 âˆ˜ h.isBigO_of_eq
  frequently_ne := (hf.eq_zero_iff.and_frequently h.frequently_ne).mono fun _ h â†¦ mt h.1.2 h.2

lemma congr_left {f' : Î¹ â†’ Î± â†’ E} (h : ReflectsGrowth f g l) (hf : âˆ€ {i}, f i =á¶ [l] f' i) :
    ReflectsGrowth f' g l :=
  h.congr_left_isTheta hf.isTheta

protected lemma comp {Î¹' : Type*} (h : ReflectsGrowth f g l) (u : Î¹' â†’ Î¹) :
    ReflectsGrowth (f âˆ˜ u) (g âˆ˜ u) l :=
  { h with }

end ReflectsGrowth

variable [Zero Î¹] [Zero Î²]

/-- A zero homomorphism `g : ZeroHom Î¹ Î²` with linear ordered codomain
*reflects growth* of a family of functions `f : Î¹ â†’ Î± â†’ E` along a filter `l`, if

- `g` reflects growth of `f` along `l` in the sense of `Asymptotics.ReflectsGrowth`,
- `g 0 = 0`,
- `f 0 =Î˜[l] 1`.
-/
structure ReflectsGrowthâ‚€ (f : Î¹ â†’ Î± â†’ E) (g : ZeroHom Î¹ Î²) (l : Filter Î±)
    extends ReflectsGrowth f g l : Prop where
  isTheta_map_zero_one : f 0 =Î˜[l] (1 : Î± â†’ â„)

namespace ReflectsGrowthâ‚€

variable {f : Î¹ â†’ Î± â†’ E} {g : ZeroHom Î¹ Î²} {l : Filter Î±} {i j : Î¹}

lemma isTheta_one_right (h : ReflectsGrowthâ‚€ f g l) {a} : f a =Î˜[l] (1 : Î± â†’ â„) â†” g a = 0 := by
  simp_rw [â† h.isTheta_map_zero_one.isTheta_congr_right, h.isTheta, map_zero]

lemma isBigO_one_right (h : ReflectsGrowthâ‚€ f g l) {a} : f a =O[l] (1 : Î± â†’ â„) â†” g a â‰¤ 0 := by
  simp_rw [â† h.isTheta_map_zero_one.isBigO_congr_right, h.isBigO, map_zero]

lemma isBigO_one_left (h : ReflectsGrowthâ‚€ f g l) {a} : (1 : Î± â†’ â„) =O[l] f a â†” 0 â‰¤ g a := by
  simp_rw [â† h.isTheta_map_zero_one.isBigO_congr_left, h.isBigO, map_zero]

lemma isLittleO_one_right (h : ReflectsGrowthâ‚€ f g l) {a} :
    f a =o[l] (1 : Î± â†’ â„) â†” g a < 0 := by
  simp_rw [â† h.isTheta_map_zero_one.isLittleO_congr_right, h.isLittleO, map_zero]

lemma isLittleO_one_left (h : ReflectsGrowthâ‚€ f g l) {a} :
    (1 : Î± â†’ â„) =o[l] f a â†” 0 < g a := by
  simp_rw [â† h.isTheta_map_zero_one.isLittleO_congr_left, h.isLittleO, map_zero]

lemma isBoundedUnder_le_norm (h : ReflectsGrowthâ‚€ f g l) {a} :
    IsBoundedUnder (Â· â‰¤ Â·) l (â€–f a Â·â€–) â†” g a â‰¤ 0 :=
  (isBigO_one_iff _).symm.trans h.isBigO_one_right

lemma tendsto_zero (h : ReflectsGrowthâ‚€ f g l) {a} : Tendsto (f a) l (ð“ 0) â†” g a < 0 :=
  (isLittleO_one_iff â„).symm.trans h.isLittleO_one_right

lemma tendsto_norm_atTop (h : ReflectsGrowthâ‚€ f g l) {a} :
    Tendsto (â€–f a Â·â€–) l atTop â†” 0 < g a :=
  (isLittleO_one_left_iff _).symm.trans h.isLittleO_one_left

protected lemma comp {Î¹' : Type*} [Zero Î¹'] (h : ReflectsGrowthâ‚€ f g l) (u : ZeroHom Î¹' Î¹) :
    ReflectsGrowthâ‚€ (f âˆ˜ u) (ZeroHom.comp (g : ZeroHom Î¹ Î²) u) l where
  toReflectsGrowth := h.toReflectsGrowth.comp u
  isTheta_map_zero_one := by simp [h.isTheta_map_zero_one]

lemma congr_right {Î³ : Type*} [LinearOrder Î³] [Zero Î³] {g' : ZeroHom Î¹ Î³}
    (h : ReflectsGrowthâ‚€ f g l) (hg : âˆ€ {i j}, g i < g j â†” g' i < g' j) :
    ReflectsGrowthâ‚€ f g' l where
  toReflectsGrowth := h.1.congr_right hg
  __ := h

end ReflectsGrowthâ‚€

end

section

variable {G Î± H E : Type*} [AddGroup G] [LinearOrderedAddCommGroup H] [NormedAddCommGroup E]
  {f : G â†’ Î± â†’ E} {g : G â†’+ H} {l : Filter Î±}

/-- -/
structure ReflectsGrowthAddMul (f : G â†’ Î± â†’ E) (g : G â†’+ H) (l : Filter Î±) : Prop where
  eventuallyEq_norm_map_add (a b : G) : (â€–f (a + b) Â·â€–) =á¶ [l] fun x â†¦ â€–f a xâ€– * â€–f b xâ€–
  tendsto_of_pos {a : G} (ha : 0 < g a) : Tendsto (â€–f a Â·â€–) l atTop
  isBigO_of_eq_zero {a : G} (ha : g a = 0) : (1 : Î± â†’ â„) =O[l] f a
  [neBot : l.NeBot]
  eventually_ne {a : G} : âˆ€á¶  x in l, f a x â‰  0

namespace ReflectsGrowthAddMul

lemma eventuallyEq_norm_map_zero (h : ReflectsGrowthAddMul f g l) : (â€–f 0 Â·â€–) =á¶ [l] 1 := by
  rcases (h.isBigO_of_eq_zero (map_zero g)).bound with âŸ¨C, hCâŸ©
  filter_upwards [h.eventuallyEq_norm_map_add 0 0, hC] with x hxâ‚€ hxâ‚
  have : â€–f 0 xâ€– â‰  0 := fun h â†¦ by simp [h, one_pos.not_le] at hxâ‚
  rwa [add_zero, left_eq_mulâ‚€ this] at hxâ‚€

lemma isTheta_map_zero_one (h : ReflectsGrowthAddMul f g l) : f 0 =Î˜[l] (1 : Î± â†’ â„) :=
  h.eventuallyEq_norm_map_zero.isTheta.of_norm_left

lemma reflectsGrowthâ‚€ (h : ReflectsGrowthAddMul f g l) : ReflectsGrowthâ‚€ f (g : ZeroHom G H) l where
  isLittleO_of_lt {a b} hlt := .of_norm_norm <|
    calc
      (â€–f a Â·â€–) = (1 * â€–f a Â·â€–) := (one_mul _).symm
      _ =o[l] fun x â†¦ â€–f (b - a) xâ€– * â€–f a xâ€– := by
        refine .mul_isBigO ?_ (isBigO_refl _ _)
        simp only [isLittleO_one_left_iff, norm_norm]
        exact h.tendsto_of_pos <| by rwa [map_sub, sub_pos]
      _ =á¶ [l] (â€–f b Â·â€–) := by simpa using (h.eventuallyEq_norm_map_add (b - a) a).symm
  isBigO_of_eq {a b} (heq : g a = g b) := .of_norm_norm <|
    calc
      (â€–f a Â·â€–) = (1 * â€–f a Â·â€–) := (one_mul _).symm
      _ =O[l] fun x â†¦ â€–f (b - a) xâ€– * â€–f a xâ€– :=
        (h.isBigO_of_eq_zero <| by simp [heq]).norm_right.mul (isBigO_refl _ _)
      _ =á¶ [l] (â€–f b Â·â€–) := by simpa using (h.eventuallyEq_norm_map_add (b - a) a).symm
  frequently_ne := have := h.neBot; h.eventually_ne.frequently
  isTheta_map_zero_one := h.isTheta_map_zero_one

lemma isLittleO (h : ReflectsGrowthAddMul f g l) {a b} : f a =o[l] f b â†” g a < g b :=
  h.reflectsGrowthâ‚€.isLittleO

lemma isBigO (h : ReflectsGrowthAddMul f g l) {a b} : f a =O[l] f b â†” g a â‰¤ g b :=
  h.reflectsGrowthâ‚€.isBigO

lemma isTheta (h : ReflectsGrowthAddMul f g l) {a b} : f a =Î˜[l] f b â†” g a = g b :=
  h.reflectsGrowthâ‚€.isTheta

lemma isTheta_one_right (h : ReflectsGrowthAddMul f g l) {a} : f a =Î˜[l] (1 : Î± â†’ â„) â†” g a = 0 :=
  h.reflectsGrowthâ‚€.isTheta_one_right

lemma isBigO_one_right (h : ReflectsGrowthAddMul f g l) {a} : f a =O[l] (1 : Î± â†’ â„) â†” g a â‰¤ 0 :=
  h.reflectsGrowthâ‚€.isBigO_one_right

lemma isBigO_one_left (h : ReflectsGrowthAddMul f g l) {a} : (1 : Î± â†’ â„) =O[l] f a â†” 0 â‰¤ g a :=
  h.reflectsGrowthâ‚€.isBigO_one_left

lemma isLittleO_one_right (h : ReflectsGrowthAddMul f g l) {a} : f a =o[l] (1 : Î± â†’ â„) â†” g a < 0 :=
  h.reflectsGrowthâ‚€.isLittleO_one_right

lemma isLittleO_one_left (h : ReflectsGrowthAddMul f g l) {a} : (1 : Î± â†’ â„) =o[l] f a â†” 0 < g a :=
  h.reflectsGrowthâ‚€.isLittleO_one_left

lemma isBoundedUnder_le_norm (h : ReflectsGrowthAddMul f g l) {a} :
    IsBoundedUnder (Â· â‰¤ Â·) l (â€–f a Â·â€–) â†” g a â‰¤ 0 :=
  h.reflectsGrowthâ‚€.isBoundedUnder_le_norm

lemma tendsto_zero (h : ReflectsGrowthAddMul f g l) {a} : Tendsto (f a) l (ð“ 0) â†” g a < 0 :=
  h.reflectsGrowthâ‚€.tendsto_zero

lemma tendsto_norm_atTop (h : ReflectsGrowthAddMul f g l) {a} :
    Tendsto (â€–f a Â·â€–) l atTop â†” 0 < g a :=
  (isLittleO_one_left_iff _).symm.trans h.isLittleO_one_left

lemma comp_tendsto {Î² : Type*} {l' : Filter Î²} {u : Î² â†’ Î±} [NeBot l']
    (hl : ReflectsGrowthAddMul f g l) (hu : Tendsto u l' l) :
    ReflectsGrowthAddMul (fun x y â†¦ f x (u y)) g l' where
  eventuallyEq_norm_map_add a b := hu.eventually (hl.eventuallyEq_norm_map_add a b)
  tendsto_of_pos hpos := (hl.tendsto_of_pos hpos).comp hu
  isBigO_of_eq_zero hzero := (hl.isBigO_of_eq_zero hzero).comp_tendsto hu
  eventually_ne := hu.eventually hl.eventually_ne

lemma mono (hl : ReflectsGrowthAddMul f g l) {l'} [NeBot l'] (hle : l' â‰¤ l) :
    ReflectsGrowthAddMul f g l' := hl.comp_tendsto hle

end ReflectsGrowthAddMul

lemma reflectsGrowthAddMul_zpow_cobounded {ð•œ : Type*} [NontriviallyNormedField ð•œ] :
    ReflectsGrowthAddMul (fun (n : â„¤) (x : ð•œ) â†¦ x ^ n) (AddMonoidHom.id â„¤) (cobounded ð•œ) where
  eventuallyEq_norm_map_add _ _ := (eventually_ne_cobounded 0).mono fun x hx â†¦ by
    simp only [zpow_addâ‚€ hx, norm_mul]
  tendsto_of_pos h := by
    simpa only [norm_zpow] using (tendsto_zpow_atTop_atTop h).comp tendsto_norm_cobounded_atTop
  isBigO_of_eq_zero _ := by simp_all [isBoundedUnder_const]
  eventually_ne := (eventually_ne_cobounded 0).mono fun _ â†¦ zpow_ne_zero _

lemma reflectsGrowthAddMul_zpow_atTop :
    ReflectsGrowthAddMul (fun (n : â„¤) (x : â„) â†¦ x ^ n) (AddMonoidHom.id â„¤) atTop :=
  reflectsGrowthAddMul_zpow_cobounded.mono (by simp)

lemma reflectsGrowthAddMul_zpow_atBot :
    ReflectsGrowthAddMul (fun (n : â„¤) (x : â„) â†¦ x ^ n) (AddMonoidHom.id â„¤) atBot :=
  reflectsGrowthAddMul_zpow_cobounded.mono (by simp)

@[simp]
lemma isLittleO_zpow_zpow_atTop {m n : â„¤} : (Â· ^ m : â„ â†’ â„) =o[atTop] (Â· ^ n) â†” m < n :=
  reflectsGrowthAddMul_zpow_atTop.isLittleO

lemma reflectsGrowthâ‚€_pow_atTop : ReflectsGrowthâ‚€ (fun (n : â„•) (x : â„) â†¦ x ^ n) (.id _) atTop := by
  simpa [comp_def]
    using (reflectsGrowthAddMul_zpow_atTop.reflectsGrowthâ‚€.comp
      (Nat.castAddMonoidHom â„¤ : ZeroHom â„• â„¤)).congr_right (g' := .id _) Nat.cast_lt
