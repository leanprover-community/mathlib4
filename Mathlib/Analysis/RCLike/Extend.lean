/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/
module

public import Mathlib.Algebra.Algebra.RestrictScalars
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.LinearAlgebra.Dual.Defs

/-!
# Extending an `â„`-linear functional to a `ğ•œ`-linear functional

In this file we provide a way to extend a (optionally, continuous) `â„`-linear map to a (continuous)
`ğ•œ`-linear map in a way that bounds the norm by the norm of the original map, when `ğ•œ` is either
`â„` (the extension is trivial) or `â„‚`. We formulate the extension uniformly, by assuming `RCLike ğ•œ`.

We motivate the form of the extension as follows. Note that `fc : F â†’â‚—[ğ•œ] ğ•œ` is determined fully by
`re fc`: for all `x : F`, `fc (I â€¢ x) = I * fc x`, so `im (fc x) = -re (fc (I â€¢ x))`. Therefore,
given an `fr : F â†’â‚—[â„] â„`, we define `fc x = fr x - fr (I â€¢ x) * I`.

In `Mathlib/Analysis/Normed/Module/RCLike/Extend.lean` we show that this extension is isometric.
This is separate to avoid importing material about the operator norm into files about more
elementary properties, like locally convex spaces.

## Main definitions

* `LinearMap.extendRCLike`
* `ContinuousLinearMap.extendRCLike`

-/

@[expose] public section

open RCLike

open ComplexConjugate

variable {ğ•œ : Type*} [RCLike ğ•œ] {F : Type*}
namespace Module.Dual

variable [AddCommGroup F] [Module â„ F] [Module ğ•œ F] [IsScalarTower â„ ğ•œ F]

/-- Extend `fr : Dual â„ F` to `Dual ğ•œ F` in a way that will also be continuous and have its norm
(as a continuous linear map) equal to `â€–frâ€–` when `fr` is itself continuous on a normed space. -/
noncomputable def extendRCLike (fr : Dual â„ F) : Dual ğ•œ F :=
  letI fc : F â†’ ğ•œ := fun x => (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x)
  have add (x y) : fc (x + y) = fc x + fc y := by
    simp only [fc, smul_add, map_add, mul_add]
    abel
  have A (c : â„) (x : F) : (fr ((c : ğ•œ) â€¢ x) : ğ•œ) = (c : ğ•œ) * (fr x : ğ•œ) := by simp
  have smul_â„ (c : â„) (x : F) : fc ((c : ğ•œ) â€¢ x) = (c : ğ•œ) * fc x := by
    simp only [fc, A, smul_comm I, mul_comm I, mul_sub, mul_assoc]
  have smul_I (x : F) : fc ((I : ğ•œ) â€¢ x) = (I : ğ•œ) * fc x := by
    obtain (h | h) := @I_mul_I_ax ğ•œ _
    Â· simp [fc, h]
    Â· simp [fc, mul_sub, â† mul_assoc, smul_smul, h, add_comm]
  have smul_ğ•œ (c : ğ•œ) (x : F) : fc (c â€¢ x) = c â€¢ fc x := by
    rw [â† re_add_im c]
    simp only [add_smul, â† smul_smul, add, smul_â„, smul_I, â† mul_assoc, smul_eq_mul, add_mul]
  { toFun := fc
    map_add' := add
    map_smul' := smul_ğ•œ }

theorem extendRCLike_apply (fr : Dual â„ F) (x : F) :
    fr.extendRCLike x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

@[simp]
theorem re_extendRCLike_apply (fr : Dual â„ F) (x : F) : re (fr.extendRCLike x : ğ•œ) = fr x := by
  simp only [extendRCLike_apply, map_sub, zero_mul, mul_zero, sub_zero, rclike_simps]

@[simp]
lemma im_extendRCLike_apply (g : Dual â„ F) (x : F) :
    im ((extendRCLike g) x : ğ•œ) = - g ((I : ğ•œ) â€¢ x) := by
  obtain (h | h) := RCLike.I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  all_goals simp [h, extendRCLike_apply]

theorem norm_extendRCLike_apply_sq (fr : Dual â„ F) (x : F) :
    â€–(fr.extendRCLike x : ğ•œ)â€– ^ 2 = fr (conj (fr.extendRCLike x : ğ•œ) â€¢ x) := calc
  â€–(fr.extendRCLike x : ğ•œ)â€– ^ 2 = re (conj (fr.extendRCLike x) * fr.extendRCLike x : ğ•œ) := by
    rw [RCLike.conj_mul, â† ofReal_pow, ofReal_re]
  _ = fr (conj (fr.extendRCLike x : ğ•œ) â€¢ x) := by
    rw [â† smul_eq_mul, â† map_smul, re_extendRCLike_apply]

/-- The extension `Module.Dual.extendRCLike` as a linear equivalence between the algebraic duals. -/
@[simps -isSimp apply symm_apply]
noncomputable def extendRCLikeâ‚— : Dual â„ F â‰ƒâ‚—[â„] Dual ğ•œ F where
  toFun := extendRCLike (ğ•œ := ğ•œ)
  invFun f := RCLike.reLm.comp (f.restrictScalars â„)
  left_inv f := by ext; simp
  right_inv f := by ext; apply RCLike.ext <;> simp
  map_add' := by intros; ext; simp [extendRCLike_apply]; ring
  map_smul' := by intros; ext; simp [extendRCLike_apply, real_smul_eq_coe_mul]; ring

end Module.Dual

namespace StrongDual

variable [TopologicalSpace F] [AddCommGroup F] [Module ğ•œ F] [ContinuousConstSMul ğ•œ F]
variable [Module â„ F] [IsScalarTower â„ ğ•œ F]

/-- Extend `fr : StrongDual â„ F` to `StrongDual ğ•œ F`.

It would be possible to use `LinearMap.mkContinuous` here, but we would need to know that the
continuity of `fr` implies it has bounded norm and we want to avoid that dependency here.

Norm properties of this extension can be found in
`Mathlib/Analysis/Normed/Module/RCLike/Extend.lean`. -/
noncomputable def extendRCLike (fr : StrongDual â„ F) : StrongDual ğ•œ F where
  __ := Module.Dual.extendRCLike fr.toLinearMap
  cont := show Continuous fun x â†¦ (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) by fun_prop

theorem extendRCLike_apply (fr : StrongDual â„ F) (x : F) :
    fr.extendRCLike x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

@[simp]
lemma re_extendRCLike_apply (g : StrongDual â„ F) (x : F) :
    re ((extendRCLike g) x : ğ•œ) = g x := by
  simp [extendRCLike_apply]

@[deprecated (since := "2026-01-28")] alias _root_.RCLike.re_extendToğ•œâ‚— := re_extendRCLike_apply

@[simp]
lemma im_extendRCLike_apply (g : StrongDual â„ F) (x : F) :
    re ((extendRCLike g) x : ğ•œ) = g x := by
  simp [extendRCLike_apply]

/-- The extension `StrongDual.extendRCLike` as a linear equivalence between the algebraic duals.

When `F` is a normed space, this can be upgraded to an *isometric* linear equivalence, see
`StrondDual.extendRCLikeâ‚—áµ¢`. -/
@[simps -isSimp apply symm_apply]
noncomputable def extendRCLikeâ‚— : StrongDual â„ F â‰ƒâ‚—[â„] StrongDual ğ•œ F where
  toFun := StrongDual.extendRCLike (ğ•œ := ğ•œ)
  invFun f := RCLike.reCLM.comp (f.restrictScalars â„)
  left_inv f := by ext; simp
  right_inv f := by ext; apply RCLike.ext <;> simp [extendRCLike_apply]
  map_add' := by intros; ext; simp [extendRCLike_apply]; ring
  map_smul' := by intros; ext; simp [extendRCLike_apply, real_smul_eq_coe_mul]; ring

@[deprecated (since := "2026-01-28")] alias _root_.RCLike.extendToğ•œâ‚— := extendRCLikeâ‚—

end StrongDual

namespace LinearMap

open Module.Dual

@[deprecated (since := "2026-01-28")] alias extendToğ•œ' := extendRCLike
@[deprecated (since := "2026-01-28")] alias extendToğ•œ'_apply := extendRCLike_apply
@[deprecated (since := "2026-01-28")] alias extendToğ•œ'_apply_re := re_extendRCLike_apply
@[deprecated (since := "2026-01-28")] alias norm_extendToğ•œ'_apply_sq := norm_extendRCLike_apply_sq
@[deprecated (since := "2026-01-28")] alias extendToğ•œ := extendRCLike
@[deprecated (since := "2026-01-28")] alias extendToğ•œ_apply := extendRCLike_apply

end LinearMap

namespace ContinuousLinearMap

open StrongDual

@[deprecated (since := "2026-01-28")] alias extendToğ•œ' := extendRCLike
@[deprecated (since := "2026-01-28")] alias extendToğ•œ'_apply := extendRCLike_apply
@[deprecated (since := "2026-01-28")] alias extendToğ•œ := extendRCLike
@[deprecated (since := "2026-01-28")] alias extendToğ•œ_apply := extendRCLike_apply

end ContinuousLinearMap
