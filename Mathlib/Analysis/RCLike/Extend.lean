/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/
import Mathlib.Algebra.Algebra.RestrictScalars
import Mathlib.Analysis.RCLike.Basic
import Mathlib.LinearAlgebra.Dual.Defs

/-!
# Extending an `â„`-linear functional to a `ğ•œ`-linear functional

In this file we provide a way to extend a (optionally, continuous) `â„`-linear map to a (continuous)
`ğ•œ`-linear map in a way that bounds the norm by the norm of the original map, when `ğ•œ` is either
`â„` (the extension is trivial) or `â„‚`. We formulate the extension uniformly, by assuming `RCLike ğ•œ`.

We motivate the form of the extension as follows. Note that `fc : F â†’â‚—[ğ•œ] ğ•œ` is determined fully by
`re fc`: for all `x : F`, `fc (I â€¢ x) = I * fc x`, so `im (fc x) = -re (fc (I â€¢ x))`. Therefore,
given an `fr : F â†’â‚—[â„] â„`, we define `fc x = fr x - fr (I â€¢ x) * I`.

In `Mathlib/Analysis/Normed/Module/RCLike/Extend.lean` we show that this extension is isometric.
This is separate to avoid importing material about the operator norm into files about more
elementary properties, like locally convex spaces.

## Main definitions

* `LinearMap.extendToğ•œ`
* `ContinuousLinearMap.extendToğ•œ`

## Implementation details

For convenience, the main definitions above operate in terms of `RestrictScalars â„ ğ•œ F`.
Alternate forms which operate on `[IsScalarTower â„ ğ•œ F]` instead are provided with a primed name.

-/


open RCLike

open ComplexConjugate

variable {ğ•œ : Type*} [RCLike ğ•œ] {F : Type*}
namespace LinearMap

open Module

section ScalarTower

variable [AddCommGroup F] [Module â„ F] [Module ğ•œ F] [IsScalarTower â„ ğ•œ F]

/-- Extend `fr : Dual â„ F` to `Dual ğ•œ F` in a way that will also be continuous and have its norm
(as a continuous linear map) equal to `â€–frâ€–` when `fr` is itself continuous on a normed space. -/
noncomputable def extendToğ•œ' (fr : Dual â„ F) : Dual ğ•œ F :=
  letI fc : F â†’ ğ•œ := fun x => (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x)
  have add (x y) : fc (x + y) = fc x + fc y := by
    simp only [fc, smul_add, map_add, mul_add]
    abel
  have A (c : â„) (x : F) : (fr ((c : ğ•œ) â€¢ x) : ğ•œ) = (c : ğ•œ) * (fr x : ğ•œ) := by simp
  have smul_â„ (c : â„) (x : F) : fc ((c : ğ•œ) â€¢ x) = (c : ğ•œ) * fc x := by
    simp only [fc, A, smul_comm I, mul_comm I, mul_sub, mul_assoc]
  have smul_I (x : F) : fc ((I : ğ•œ) â€¢ x) = (I : ğ•œ) * fc x := by
    obtain (h | h) := @I_mul_I_ax ğ•œ _
    Â· simp [fc, h]
    Â· simp [fc, mul_sub, â† mul_assoc, smul_smul, h, add_comm]
  have smul_ğ•œ (c : ğ•œ) (x : F) : fc (c â€¢ x) = c â€¢ fc x := by
    rw [â† re_add_im c]
    simp only [add_smul, â† smul_smul, add, smul_â„, smul_I, â† mul_assoc, smul_eq_mul, add_mul]
  { toFun := fc
    map_add' := add
    map_smul' := smul_ğ•œ }

theorem extendToğ•œ'_apply (fr : Dual â„ F) (x : F) :
    fr.extendToğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

@[simp]
theorem extendToğ•œ'_apply_re (fr : Dual â„ F) (x : F) : re (fr.extendToğ•œ' x : ğ•œ) = fr x := by
  simp only [extendToğ•œ'_apply, map_sub, zero_mul, mul_zero, sub_zero, rclike_simps]

theorem norm_extendToğ•œ'_apply_sq (fr : Dual â„ F) (x : F) :
    â€–(fr.extendToğ•œ' x : ğ•œ)â€– ^ 2 = fr (conj (fr.extendToğ•œ' x : ğ•œ) â€¢ x) := calc
  â€–(fr.extendToğ•œ' x : ğ•œ)â€– ^ 2 = re (conj (fr.extendToğ•œ' x) * fr.extendToğ•œ' x : ğ•œ) := by
    rw [RCLike.conj_mul, â† ofReal_pow, ofReal_re]
  _ = fr (conj (fr.extendToğ•œ' x : ğ•œ) â€¢ x) := by
    rw [â† smul_eq_mul, â† map_smul, extendToğ•œ'_apply_re]

end ScalarTower

section RestrictScalars

variable [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

instance : NormedSpace ğ•œ (RestrictScalars â„ ğ•œ F) :=
  inferInstanceAs (NormedSpace ğ•œ F)

/-- Extend `fr : Dual â„ (RestrictScalars â„ ğ•œ F)` to `Dual ğ•œ F`. -/
noncomputable def extendToğ•œ (fr : Dual â„ (RestrictScalars â„ ğ•œ F)) : Dual ğ•œ F :=
  fr.extendToğ•œ'

theorem extendToğ•œ_apply (fr : RestrictScalars â„ ğ•œ F â†’â‚—[â„] â„) (x : F) :
    fr.extendToğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

end RestrictScalars

end LinearMap

namespace ContinuousLinearMap

variable [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

section ScalarTower

variable [NormedSpace â„ F] [IsScalarTower â„ ğ•œ F]

/-- Extend `fr : StrongDual â„ F` to `StrongDual ğ•œ F`.

It would be possible to use `LinearMap.mkContinuous` here, but we would need to know that the
continuity of `fr` implies it has bounded norm and we want to avoid that dependency here.

Norm properties of this extension can be found in
`Mathlib/Analysis/Normed/Module/RCLike/Extend.lean`. -/
noncomputable def extendToğ•œ' (fr : StrongDual â„ F) : StrongDual ğ•œ F where
  __ := fr.toLinearMap.extendToğ•œ'
  cont := show Continuous fun x â†¦ (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) by fun_prop

theorem extendToğ•œ'_apply (fr : StrongDual â„ F) (x : F) :
    fr.extendToğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

end ScalarTower

/-- Extend `fr : StrongDual â„ (RestrictScalars â„ ğ•œ F)` to `StrongDual ğ•œ F`. -/
noncomputable def extendToğ•œ (fr : StrongDual â„ (RestrictScalars â„ ğ•œ F)) :
    StrongDual ğ•œ F := fr.extendToğ•œ'

theorem extendToğ•œ_apply (fr : StrongDual â„ (RestrictScalars â„ ğ•œ F)) (x : F) :
    fr.extendToğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl

end ContinuousLinearMap
