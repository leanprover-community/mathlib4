/-
Copyright (c) 2026 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Analysis.SpecialFunctions.Pow.Complex

import Mathlib.Analysis.SpecialFunctions.Pow.Real

/-!
# Square root on `RCLike`

This file contains the definitions `Complex.sqrt` and `RCLike.sqrt` and builds basic API.
-/

@[expose] public section

variable {ğ•œ : Type*} [RCLike ğ•œ]

open ComplexOrder

/-- The square root of a complex number. -/
noncomputable abbrev Complex.sqrt (a : â„‚) : â„‚ := a ^ (2â»Â¹ : â„‚)

theorem Complex.sqrt_def (a : â„‚) :
    a.sqrt = âˆš((â€–aâ€– + a.re) / 2) + (if a.im < 0 then -1 else 1) * âˆš((â€–aâ€– - a.re) / 2) * I := by
  rw [â† cpow_inv_two_re]
  by_cases! h : 0 â‰¤ a.im
  Â· simp [â† cpow_inv_two_im_eq_sqrt h, h.not_gt]
  simp only [re_add_im, â†“reduceIte, h, neg_one_mul, â† ofReal_neg, â† cpow_inv_two_im_eq_neg_sqrt h]

/-- The square root on `RCLike`. -/
noncomputable def RCLike.sqrt (a : ğ•œ) : ğ•œ :=
  if h : im (I : ğ•œ) = 1 then (complexRingEquiv h).symm (complexRingEquiv h a).sqrt
  else âˆš(re a)

theorem RCLike.re_sqrt (a : ğ•œ) : re (sqrt a) = âˆš((â€–aâ€– + re a) / 2) := by
  rw [sqrt]
  split_ifs with h
  Â· convert Complex.cpow_inv_two_re (complexRingEquiv h a)
    Â· simp
    Â· simpa [eq_comm] using norm_to_complex h a
    simp
  rw [â† show re a = a by grind [I_eq_zero_or_im_I_eq_one, re_add_im]]
  by_cases! ha' : 0 â‰¤ re a
  Â· simp [abs_of_nonneg ha']
  simp [abs_of_nonpos ha'.le, Real.sqrt_eq_zero', ha'.le]

theorem RCLike.sqrt_def (a : ğ•œ) :
    sqrt a = âˆš((â€–aâ€– + re a) / 2) + (if im a < 0 then -1 else 1) * âˆš((â€–aâ€– - re a) / 2) * I := by
  rw [â† re_sqrt]
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· simp [h, sqrt]
  by_cases! ha : 0 â‰¤ im a
  Â· simp only [sqrt, h, â†“reduceDIte, complexRingEquiv_apply, complexRingEquiv_symm_apply, map_add,
      ofReal_re, mul_re, I_re, mul_zero, ofReal_im, mul_one, sub_self, add_zero, ha.not_gt,
      â†“reduceIte, Nat.ofNat_nonneg, Real.sqrt_div', map_divâ‚€, one_mul, add_right_inj,
      mul_eq_mul_right_iff]
    rw [Complex.cpow_inv_two_im_eq_sqrt (by simpa)]
    simp [h]
  simp only [ha, â†“reduceIte, sqrt, h, â†“reduceDIte, complexRingEquiv_apply,
    complexRingEquiv_symm_apply, map_add, ofReal_re, mul_re, I_re, mul_zero, ofReal_im, mul_one,
    sub_self, add_zero, Nat.ofNat_nonneg, Real.sqrt_div', map_divâ‚€, neg_mul, add_right_inj]
  rw [Complex.cpow_inv_two_im_eq_neg_sqrt (by simpa)]
  simp [h]

theorem Complex.re_sqrt_ofReal (a : â„) :
    (sqrt (a : â„‚)).re = âˆša := by
  simp only [cpow_inv_two_re, norm_real, Real.norm_eq_abs, ofReal_re]
  grind

theorem RCLike.re_sqrt_ofReal (a : â„) :
    re (sqrt (a : ğ•œ)) = âˆša := by
  aesop (add simp [sqrt, Complex.re_sqrt_ofReal])

@[simp] theorem RCLike.sqrt_real (a : â„) :
    sqrt a = âˆša := by simp [â† re_sqrt_ofReal (ğ•œ := â„)]

@[simp] theorem RCLike.sqrt_complex (a : â„‚) :
    sqrt a = a.sqrt := by simp [sqrt]

theorem Complex.sqrt_of_nonneg {a : â„‚} (ha : 0 â‰¤ a) :
    a.sqrt = âˆša.re := by
  obtain âŸ¨Î± : â„, hÎ±, rflâŸ© := RCLike.nonneg_iff_exists_ofReal.mp ha
  simp only [coe_algebraMap, ofReal_re]
  rw [â† re_add_im (Î± : â„‚).sqrt, re_sqrt_ofReal]
  simp [sqrt, cpow_inv_two_im_eq_sqrt, abs_of_nonneg hÎ±]

theorem RCLike.sqrt_symm_complexRingEquiv {a : â„‚} (h : im (I : ğ•œ) = 1) :
    sqrt ((complexRingEquiv h).symm a) = (complexRingEquiv h).symm a.sqrt := by
  aesop (add simp [sqrt])

theorem RCLike.sqrt_complexRingEquiv {a : ğ•œ} (h : im (I : ğ•œ) = 1) :
    (complexRingEquiv h a).sqrt = complexRingEquiv h (sqrt a) := by
  aesop (add simp [sqrt])

theorem RCLike.sqrt_of_nonneg {a : ğ•œ} (ha : 0 â‰¤ a) :
    sqrt a = âˆš(re a) := by
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· simp [h, sqrt]
  apply_fun complexRingEquiv h
  rw [â† sqrt_complexRingEquiv h, Complex.sqrt_of_nonneg (by simpa)]
  simp

theorem Complex.sqrt_neg_of_nonneg {a : â„‚} (ha : 0 â‰¤ a) :
    (-a).sqrt = I * a.sqrt := by
  obtain âŸ¨Î±, hÎ±, rflâŸ© := RCLike.nonneg_iff_exists_ofReal.mp ha
  rw [Complex.sqrt_of_nonneg ha]
  simp only [coe_algebraMap, ofReal_re]
  rw [â† re_add_im (-(Î± : â„‚)).sqrt]
  simp [sqrt, cpow_inv_two_im_eq_sqrt, abs_of_nonneg hÎ±, cpow_inv_two_re, mul_comm]

theorem RCLike.sqrt_neg_of_nonneg {a : ğ•œ} (ha : 0 â‰¤ a) :
    sqrt (-a) = I * sqrt a := by
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· simp [h, sqrt, Real.sqrt_eq_zero', nonneg_iff.mp ha]
  apply_fun complexRingEquiv h
  rw [â† sqrt_complexRingEquiv h, map_neg, Complex.sqrt_neg_of_nonneg (by simpa),
    map_mul, â† sqrt_complexRingEquiv h]
  simp [h]

theorem Complex.sqrt_neg_one : sqrt (-1) = I := by
  simp [sqrt_neg_of_nonneg (a := 1) (by simp)]

theorem RCLike.sqrt_neg_one : sqrt (-1) = (I : ğ•œ) := by
  rw [sqrt_neg_of_nonneg (by simp)]
  simp [sqrt]

theorem Complex.sqrt_I : sqrt (I : â„‚) = âˆš2â»Â¹ * (1 + I) := by
  rw [sqrt, â† re_add_im (I ^ 2â»Â¹), cpow_inv_two_im_eq_sqrt (by simp), cpow_inv_two_re]
  simp [mul_add]

theorem Complex.sqrt_neg_I : sqrt (-I : â„‚) = âˆš2â»Â¹ * (1 - I) := by
  rw [sqrt, â† re_add_im ((-I) ^ 2â»Â¹), cpow_inv_two_im_eq_neg_sqrt (by simp), cpow_inv_two_re]
  simp [mul_sub, â† sub_eq_add_neg]
