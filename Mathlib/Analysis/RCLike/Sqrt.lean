/-
Copyright (c) 2026 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Analysis.SpecialFunctions.Pow.Complex

import Mathlib.Analysis.SpecialFunctions.Pow.Real
public import Mathlib.LinearAlgebra.Basis.VectorSpace

/-!
# Square root on `RCLike`

This file contains the definitions `Complex.sqrt` and `RCLike.sqrt` and builds basic API.
-/

@[expose] public section

variable {ğ•œ : Type*} [RCLike ğ•œ]

open ComplexOrder

/-- The square root of a complex number. -/
noncomputable def Complex.sqrt (a : â„‚) : â„‚ := a ^ (2â»Â¹ : â„‚)

@[simp] theorem Complex.sqrt_zero : (0 : â„‚).sqrt = 0 := by simp [sqrt]
@[simp] theorem Complex.sqrt_one : (1 : â„‚).sqrt = 1 := by simp [sqrt]

theorem Complex.sqrt_eq_real_add_ite {a : â„‚} :
    a.sqrt = âˆš((â€–aâ€– + a.re) / 2) + (if 0 â‰¤ a.im then 1 else -1) * âˆš((â€–aâ€– - a.re) / 2) * I := by
  rw [â† cpow_inv_two_re, sqrt]
  by_cases! h : 0 â‰¤ a.im
  Â· simp [â† cpow_inv_two_im_eq_sqrt h, h]
  simp only [re_add_im, â†“reduceIte, h.not_ge, neg_one_mul, â† ofReal_neg,
    â† cpow_inv_two_im_eq_neg_sqrt h]

/-- The square root on `RCLike`. -/
noncomputable def RCLike.sqrt (a : ğ•œ) : ğ•œ := map â„‚ ğ•œ (map ğ•œ â„‚ a).sqrt

theorem RCLike.sqrt_eq_ite {a : ğ•œ} :
    sqrt a = if h : im (I : ğ•œ) = 1 then (complexRingEquiv h).symm (complexRingEquiv h a).sqrt
      else âˆš(re a) := by
  rw [sqrt, eq_comm]
  split_ifs with h
  Â· simp
  have : (I : ğ•œ) = 0 := by grind [I_eq_zero_or_im_I_eq_one]
  simp_all only [Complex.sqrt, im_eq_zero this, map_apply, add_zero, re_to_complex, im_to_complex,
    mul_zero, algebraMap.coe_inj, Complex.cpow_inv_two_re]
  by_cases! ha' : 0 â‰¤ re a
  Â· simp [abs_of_nonneg ha', â† two_mul]
  simp [abs_of_nonpos ha'.le, Real.sqrt_eq_zero', ha'.le]

theorem RCLike.sqrt_eq_real_add_ite {a : ğ•œ} :
    sqrt a = âˆš((â€–aâ€– + re a) / 2) + (if 0 â‰¤ im a then 1 else -1) * âˆš((â€–aâ€– - re a) / 2) * I := by
  rw [sqrt, Complex.sqrt_eq_real_add_ite]
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· rw [â† re_add_im a]
    simp [h, im_eq_zero]
  aesop

@[simp] theorem RCLike.sqrt_zero : sqrt (0 : ğ•œ) = 0 := by simp [sqrt]
@[simp] theorem RCLike.sqrt_one : sqrt (1 : ğ•œ) = 1 := by simp [sqrt]

theorem Complex.re_sqrt_ofReal {a : â„} :
    (sqrt (a : â„‚)).re = âˆša := by
  simp only [cpow_inv_two_re, norm_real, Real.norm_eq_abs, ofReal_re, Complex.sqrt]
  grind

theorem RCLike.re_sqrt_ofReal {a : â„} :
    re (sqrt (a : ğ•œ)) = âˆša := by
  aesop (add simp [sqrt, Complex.re_sqrt_ofReal])

@[simp] theorem RCLike.sqrt_real {a : â„} :
    sqrt a = âˆša := by simp [â† re_sqrt_ofReal (ğ•œ := â„)]

@[simp] theorem RCLike.sqrt_complex {a : â„‚} :
    sqrt a = a.sqrt := by simp [sqrt]

theorem Complex.sqrt_of_nonneg {a : â„‚} (ha : 0 â‰¤ a) :
    a.sqrt = âˆša.re := by
  obtain âŸ¨Î± : â„, hÎ±, rflâŸ© := RCLike.nonneg_iff_exists_ofReal.mp ha
  simp only [coe_algebraMap, ofReal_re]
  rw [â† re_add_im (Î± : â„‚).sqrt, re_sqrt_ofReal]
  simp [sqrt, cpow_inv_two_im_eq_sqrt, abs_of_nonneg hÎ±]

theorem RCLike.sqrt_map {ğ•œ' : Type*} [RCLike ğ•œ'] {a : ğ•œ} (h : im (I : ğ•œ) = im (I : ğ•œ')) :
    sqrt (map ğ•œ ğ•œ' a) = map ğ•œ ğ•œ' (sqrt a) := by
  have := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  have := I_eq_zero_or_im_I_eq_one (K := ğ•œ')
  aesop (add simp [RCLike.sqrt, im_eq_zero])

theorem Complex.sqrt_map {a : ğ•œ} (h : RCLike.im (RCLike.I : ğ•œ) = 1) :
    (RCLike.map ğ•œ â„‚ a).sqrt = RCLike.map ğ•œ â„‚ (RCLike.sqrt a) := by
  aesop (add simp [RCLike.sqrt])

theorem RCLike.sqrt_of_nonneg {a : ğ•œ} (ha : 0 â‰¤ a) :
    sqrt a = âˆš(re a) := by
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· simp [h, sqrt_eq_ite]
  rw [sqrt_eq_ite, dif_pos h, RingEquiv.symm_apply_eq, Complex.sqrt_of_nonneg (by simpa)]
  simp

theorem Complex.sqrt_neg_of_nonneg {a : â„‚} (ha : 0 â‰¤ a) :
    (-a).sqrt = I * a.sqrt := by
  obtain âŸ¨Î±, hÎ±, rflâŸ© := RCLike.nonneg_iff_exists_ofReal.mp ha
  rw [Complex.sqrt_of_nonneg ha]
  simp only [coe_algebraMap, ofReal_re]
  rw [â† re_add_im (-(Î± : â„‚)).sqrt]
  simp [sqrt, cpow_inv_two_im_eq_sqrt, abs_of_nonneg hÎ±, cpow_inv_two_re, mul_comm]

theorem RCLike.sqrt_neg_of_nonneg {a : ğ•œ} (ha : 0 â‰¤ a) :
    sqrt (-a) = I * sqrt a := by
  obtain (h | h) := I_eq_zero_or_im_I_eq_one (K := ğ•œ)
  Â· simp [h, sqrt_eq_ite, Real.sqrt_eq_zero', nonneg_iff.mp ha]
  rw [sqrt_eq_ite, dif_pos h, RingEquiv.symm_apply_eq, map_neg,
    Complex.sqrt_neg_of_nonneg (by simpa)]
  simp [h, sqrt, map_mul]

theorem Complex.sqrt_neg_one : sqrt (-1) = I := by
  simp [sqrt_neg_of_nonneg (a := 1) (by simp)]

theorem RCLike.sqrt_neg_one : sqrt (-1) = (I : ğ•œ) := by
  simp [sqrt_neg_of_nonneg (a := (1 : ğ•œ)) (by simp)]

theorem Complex.sqrt_I : sqrt (I : â„‚) = âˆš2â»Â¹ * (1 + I) := by
  rw [sqrt, â† re_add_im (I ^ 2â»Â¹), cpow_inv_two_im_eq_sqrt (by simp), cpow_inv_two_re]
  simp [mul_add]

theorem Complex.sqrt_neg_I : sqrt (-I : â„‚) = âˆš2â»Â¹ * (1 - I) := by
  rw [sqrt, â† re_add_im ((-I) ^ 2â»Â¹), cpow_inv_two_im_eq_neg_sqrt (by simp), cpow_inv_two_re]
  simp [mul_sub, â† sub_eq_add_neg]

theorem RCLike.sqrt_I : sqrt (I : ğ•œ) = âˆš2â»Â¹ * (1 - I) * I := by
  rw [sqrt_eq_ite]
  split_ifs with h
  Â· simp_rw [RingEquiv.symm_apply_eq, map_mul]
    simp [h, mul_assoc, mul_add, add_comm, Complex.sqrt_I, add_mul]
  grind [I_eq_zero_or_im_I_eq_one]

theorem RCLike.sqrt_neg_I : sqrt (-I : ğ•œ) = âˆš2â»Â¹ * (1 + I) * -I := by
  rw [sqrt_eq_ite]
  split_ifs with h
  Â· simp_rw [RingEquiv.symm_apply_eq, map_mul]
    simp [h, mul_assoc, add_comm, Complex.sqrt_neg_I, neg_mul, mul_add, add_mul, mul_sub,
      mul_comm Complex.I, â† sub_eq_add_neg]
  grind [I_eq_zero_or_im_I_eq_one]
