/-
Copyright (c) 2020 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import Mathlib.Analysis.RCLike.Real
import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Analysis.Normed.Operator.ContinuousLinearMap

/-!
# Continuous linear maps between `RCLike K` and `‚Ñù`

This file realizes the real and imaginary parts as continuous linear maps, as well as the
`‚Ñù ‚Üí K` coercion as a linear isometry. It also realizes conjugation as an `‚Ñù`-linear isometry
equivalence from `K` to itself.

These are not included in earlier files to avoid importing material involving the operator norm.
-/

open scoped ComplexConjugate
namespace RCLike

variable {K : Type*} [RCLike K]

lemma lipschitzWith_re : LipschitzWith 1 (re (K := K)) := by
  simpa using AddMonoidHomClass.lipschitz_of_bound reLm 1
    fun _ ‚Ü¶ (by simpa using norm_re_le_norm _)

lemma lipschitzWith_im : LipschitzWith 1 (im (K := K)) := by
  simpa using AddMonoidHomClass.lipschitz_of_bound imLm 1
    fun _ ‚Ü¶ (by simpa using norm_im_le_norm _)

@[continuity, fun_prop]
theorem continuous_re : Continuous (re : K ‚Üí ‚Ñù) :=
  lipschitzWith_re.continuous

@[continuity, fun_prop]
theorem continuous_im : Continuous (im : K ‚Üí ‚Ñù) :=
  lipschitzWith_im.continuous

/-- The real part in an `RCLike` field, as a continuous linear map. -/
noncomputable def reCLM : StrongDual ‚Ñù K where
  __ := reLm

@[simp, rclike_simps, norm_cast]
theorem reCLM_coe : ((reCLM : StrongDual ‚Ñù K) : K ‚Üí‚Çó[‚Ñù] ‚Ñù) = reLm :=
  rfl

@[simp, rclike_simps]
theorem reCLM_apply : ((reCLM : StrongDual ‚Ñù K) : K ‚Üí ‚Ñù) = re :=
  rfl

/-- The imaginary part in an `RCLike` field, as a continuous linear map. -/
noncomputable def imCLM : StrongDual ‚Ñù K where
  __ := imLm

@[simp, rclike_simps, norm_cast]
theorem imCLM_coe : ((imCLM : StrongDual ‚Ñù K) : K ‚Üí‚Çó[‚Ñù] ‚Ñù) = imLm :=
  rfl

@[simp, rclike_simps]
theorem imCLM_apply : ((imCLM : StrongDual ‚Ñù K) : K ‚Üí ‚Ñù) = im :=
  rfl

/-- Conjugate as a linear isometry -/
noncomputable def conjLIE : K ‚âÉ‚Çó·µ¢[‚Ñù] K :=
  ‚ü®conjAe.toLinearEquiv, norm_conj‚ü©

@[simp, rclike_simps]
theorem conjLIE_apply : (conjLIE : K ‚Üí K) = conj :=
  rfl

/-- Conjugate as a continuous linear equivalence -/
noncomputable def conjCLE : K ‚âÉL[‚Ñù] K :=
  @conjLIE K _

@[simp, rclike_simps]
theorem conjCLE_coe : (@conjCLE K _).toLinearEquiv = conjAe.toLinearEquiv :=
  rfl

@[simp, rclike_simps]
theorem conjCLE_apply : (conjCLE : K ‚Üí K) = conj :=
  rfl

instance (priority := 100) : ContinuousStar K :=
  ‚ü®conjLIE.continuous‚ü©

@[continuity, fun_prop]
theorem continuous_conj : Continuous (conj : K ‚Üí K) :=
  continuous_star

/-- The ‚Ñù ‚Üí K coercion, as a linear isometry -/
noncomputable def ofRealLI : ‚Ñù ‚Üí‚Çó·µ¢[‚Ñù] K where
  toLinearMap := ofRealAm.toLinearMap
  norm_map' := norm_ofReal

@[simp, rclike_simps]
theorem ofRealLI_apply : (ofRealLI : ‚Ñù ‚Üí K) = ofReal :=
  rfl

/-- The `‚Ñù ‚Üí K` coercion, as a continuous linear map -/
noncomputable def ofRealCLM : ‚Ñù ‚ÜíL[‚Ñù] K :=
  ofRealLI.toContinuousLinearMap

@[simp, rclike_simps]
theorem ofRealCLM_coe : (@ofRealCLM K _ : ‚Ñù ‚Üí‚Çó[‚Ñù] K) = ofRealAm.toLinearMap :=
  rfl

@[simp, rclike_simps]
theorem ofRealCLM_apply : (ofRealCLM : ‚Ñù ‚Üí K) = ofReal :=
  rfl

@[continuity, fun_prop]
theorem continuous_ofReal : Continuous (ofReal : ‚Ñù ‚Üí K) :=
  ofRealLI.continuous

@[continuity]
theorem continuous_normSq : Continuous (normSq : K ‚Üí ‚Ñù) :=
  (continuous_re.mul continuous_re).add (continuous_im.mul continuous_im)

theorem lipschitzWith_ofReal : LipschitzWith 1 (ofReal : ‚Ñù ‚Üí K) :=
  ofRealLI.lipschitz

/-- The natural `‚Ñù`-linear isometry equivalence between `ùïú` satisfying `RCLike ùïú` and `‚Ñù` when
`RCLike.I = 0`. -/
@[simps]
noncomputable def realLinearIsometryEquiv (h : I = (0 : K)) : K ‚âÉ‚Çó·µ¢[‚Ñù] ‚Ñù where
  map_smul' := smul_re
  norm_map' z := by rw [‚Üê re_add_im z]; simp [- re_add_im, h]
  __ := realRingEquiv h

end RCLike
