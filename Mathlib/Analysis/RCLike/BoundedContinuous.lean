/-
Copyright (c) 2025 Jakob Stiefel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob Stiefel
-/
import Mathlib.Algebra.Algebra.Subalgebra.Tower
import Mathlib.Analysis.NormedSpace.OperatorNorm.NNNorm
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Topology.ContinuousMap.Bounded.Star

/-! # Results on bounded continuous functions with `RCLike` values -/

open Filter Real RCLike BoundedContinuousFunction

open scoped Topology

variable (ğ•œ E : Type*) [RCLike ğ•œ] [PseudoEMetricSpace E]

namespace RCLike

/-- On a star subalgebra of bounded continuous functions, the operations "restrict scalars to â„"
and "forget that a bounded continuous function is a bounded" commute. -/
theorem restrict_toContinuousMap_eq_toContinuousMapStar_restrict
    {A : StarSubalgebra ğ•œ (E â†’áµ‡ ğ•œ)} :
    ((A.restrictScalars â„).comap
    (AlgHom.compLeftContinuousBounded â„ ofRealAm lipschitzWith_ofReal)).map (toContinuousMapâ‚ â„) =
    ((A.map (toContinuousMapStarâ‚ ğ•œ)).restrictScalars â„).comap
    (ofRealAm.compLeftContinuous â„ continuous_ofReal) := by
  ext g
  simp only [Subalgebra.mem_map, Subalgebra.mem_comap, Subalgebra.mem_restrictScalars,
    StarSubalgebra.mem_toSubalgebra, toContinuousMapâ‚_apply, StarSubalgebra.mem_map]
  constructor
  Â· intro âŸ¨x, hxA, hxgâŸ©
    use (@ofRealAm ğ•œ _).compLeftContinuousBounded â„ lipschitzWith_ofReal x, hxA
    ext a
    simp only [toContinuousMapStarâ‚_apply_apply, AlgHom.compLeftContinuousBounded_apply_apply,
      ofRealAm_coe, AlgHom.compLeftContinuous_apply_apply, algebraMap.coe_inj]
    exact DFunLike.congr_fun hxg a
  Â· intro âŸ¨x, hxA, hxgâŸ©
    have hg_apply (a : E) := DFunLike.congr_fun hxg a
    simp only [toContinuousMapStarâ‚_apply_apply, AlgHom.compLeftContinuous_apply_apply,
      ofRealAm_coe] at hg_apply
    have h_comp_eq : (@ofRealAm ğ•œ _).compLeftContinuousBounded â„ lipschitzWith_ofReal
        (x.comp reCLM (@reCLM ğ•œ _).lipschitz) = x := by
      ext a
      simp [hg_apply]
    use x.comp reCLM (@reCLM ğ•œ _).lipschitz
    refine âŸ¨by rwa [h_comp_eq], ?_âŸ©
    ext a
    simp [hg_apply]

end RCLike
