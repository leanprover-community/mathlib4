/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.MeasureTheory.Function.Jacobian
import Mathlib.MeasureTheory.Measure.Lebesgue.Complex
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

#align_import analysis.special_functions.polar_coord from "leanprover-community/mathlib"@"8f9fea08977f7e450770933ee6abb20733b47c92"

/-!
# Polar coordinates

We define polar coordinates, as a partial homeomorphism in `‚Ñù^2` between `‚Ñù^2 - (-‚àû, 0]` and
`(0, +‚àû) √ó (-œÄ, œÄ)`. Its inverse is given by `(r, Œ∏) ‚Ü¶ (r cos Œ∏, r sin Œ∏)`.

It satisfies the following change of variables formula (see `integral_comp_polarCoord_symm`):
`‚à´ p in polarCoord.target, p.1 ‚Ä¢ f (polarCoord.symm p) = ‚à´ p, f p`

-/

noncomputable section Real

open Real Set MeasureTheory

open scoped Real Topology

/-- The polar coordinates partial homeomorphism in `‚Ñù^2`, mapping `(r cos Œ∏, r sin Œ∏)` to `(r, Œ∏)`.
It is a homeomorphism between `‚Ñù^2 - (-‚àû, 0]` and `(0, +‚àû) √ó (-œÄ, œÄ)`. -/
@[simps]
def polarCoord : PartialHomeomorph (‚Ñù √ó ‚Ñù) (‚Ñù √ó ‚Ñù) where
  toFun q := (‚àö(q.1 ^ 2 + q.2 ^ 2), Complex.arg (Complex.equivRealProd.symm q))
  invFun p := (p.1 * cos p.2, p.1 * sin p.2)
  source := {q | 0 < q.1} ‚à™ {q | q.2 ‚â† 0}
  target := Ioi (0 : ‚Ñù) √óÀ¢ Ioo (-œÄ) œÄ
  map_target' := by
    rintro ‚ü®r, Œ∏‚ü© ‚ü®hr, hŒ∏‚ü©
    dsimp at hr hŒ∏
    rcases eq_or_ne Œ∏ 0 with (rfl | h'Œ∏)
    ¬∑ simpa using hr
    ¬∑ right
      simp at hr
      simpa only [ne_of_gt hr, Ne, mem_setOf_eq, mul_eq_zero, false_or_iff,
        sin_eq_zero_iff_of_lt_of_lt hŒ∏.1 hŒ∏.2] using h'Œ∏
  map_source' := by
    rintro ‚ü®x, y‚ü© hxy
    simp only [prod_mk_mem_set_prod_eq, mem_Ioi, sqrt_pos, mem_Ioo, Complex.neg_pi_lt_arg,
      true_and_iff, Complex.arg_lt_pi_iff]
    constructor
    ¬∑ cases' hxy with hxy hxy
      ¬∑ dsimp at hxy; linarith [sq_pos_of_ne_zero hxy.ne', sq_nonneg y]
      ¬∑ linarith [sq_nonneg x, sq_pos_of_ne_zero hxy]
    ¬∑ cases' hxy with hxy hxy
      ¬∑ exact Or.inl (le_of_lt hxy)
      ¬∑ exact Or.inr hxy
  right_inv' := by
    rintro ‚ü®r, Œ∏‚ü© ‚ü®hr, hŒ∏‚ü©
    dsimp at hr hŒ∏
    simp only [Prod.mk.inj_iff]
    constructor
    ¬∑ conv_rhs => rw [‚Üê sqrt_sq (le_of_lt hr), ‚Üê one_mul (r ^ 2), ‚Üê sin_sq_add_cos_sq Œ∏]
      congr 1
      ring
    ¬∑ convert Complex.arg_mul_cos_add_sin_mul_I hr ‚ü®hŒ∏.1, hŒ∏.2.le‚ü©
      simp only [Complex.equivRealProd_symm_apply, Complex.ofReal_mul, Complex.ofReal_cos,
        Complex.ofReal_sin]
      ring
  left_inv' := by
    rintro ‚ü®x, y‚ü© _
    have A : ‚àö(x ^ 2 + y ^ 2) = Complex.abs (x + y * Complex.I) := by
      rw [Complex.abs_apply, Complex.normSq_add_mul_I]
    have Z := Complex.abs_mul_cos_add_sin_mul_I (x + y * Complex.I)
    simp only [‚Üê Complex.ofReal_cos, ‚Üê Complex.ofReal_sin, mul_add, ‚Üê Complex.ofReal_mul, ‚Üê
      mul_assoc] at Z
    simp [A]
  open_target := isOpen_Ioi.prod isOpen_Ioo
  open_source :=
    (isOpen_lt continuous_const continuous_fst).union
      (isOpen_ne_fun continuous_snd continuous_const)
  continuousOn_invFun :=
    ((continuous_fst.mul (continuous_cos.comp continuous_snd)).prod_mk
        (continuous_fst.mul (continuous_sin.comp continuous_snd))).continuousOn
  continuousOn_toFun := by
    apply ((continuous_fst.pow 2).add (continuous_snd.pow 2)).sqrt.continuousOn.prod
    have A : MapsTo Complex.equivRealProd.symm ({q : ‚Ñù √ó ‚Ñù | 0 < q.1} ‚à™ {q : ‚Ñù √ó ‚Ñù | q.2 ‚â† 0})
        Complex.slitPlane := by
      rintro ‚ü®x, y‚ü© hxy; simpa only using hxy
    refine ContinuousOn.comp (f := Complex.equivRealProd.symm)
      (g := Complex.arg) (fun z hz => ?_) ?_ A
    ¬∑ exact (Complex.continuousAt_arg hz).continuousWithinAt
    ¬∑ exact Complex.equivRealProdCLM.symm.continuous.continuousOn
#align polar_coord polarCoord

theorem hasFDerivAt_polarCoord_symm (p : ‚Ñù √ó ‚Ñù) :
    HasFDerivAt polarCoord.symm
      (LinearMap.toContinuousLinearMap (Matrix.toLin (Basis.finTwoProd ‚Ñù) (Basis.finTwoProd ‚Ñù)
        !![cos p.2, -p.1 * sin p.2; sin p.2, p.1 * cos p.2])) p := by
  rw [Matrix.toLin_finTwoProd_toContinuousLinearMap]
  convert HasFDerivAt.prod (ùïú := ‚Ñù)
    (hasFDerivAt_fst.mul ((hasDerivAt_cos p.2).comp_hasFDerivAt p hasFDerivAt_snd))
    (hasFDerivAt_fst.mul ((hasDerivAt_sin p.2).comp_hasFDerivAt p hasFDerivAt_snd)) using 2 <;>
  simp [smul_smul, add_comm, neg_mul, smul_neg, neg_smul _ (ContinuousLinearMap.snd ‚Ñù ‚Ñù ‚Ñù)]
#align has_fderiv_at_polar_coord_symm hasFDerivAt_polarCoord_symm

-- Porting note: this instance is needed but not automatically synthesised
instance : Measure.IsAddHaarMeasure volume (G := ‚Ñù √ó ‚Ñù) :=
  Measure.prod.instIsAddHaarMeasure _ _

theorem polarCoord_source_ae_eq_univ : polarCoord.source =·µê[volume] univ := by
  have A : polarCoord.source·∂ú ‚äÜ LinearMap.ker (LinearMap.snd ‚Ñù ‚Ñù ‚Ñù) := by
    intro x hx
    simp only [polarCoord_source, compl_union, mem_inter_iff, mem_compl_iff, mem_setOf_eq, not_lt,
      Classical.not_not] at hx
    exact hx.2
  have B : volume (LinearMap.ker (LinearMap.snd ‚Ñù ‚Ñù ‚Ñù) : Set (‚Ñù √ó ‚Ñù)) = 0 := by
    apply Measure.addHaar_submodule
    rw [Ne, LinearMap.ker_eq_top]
    intro h
    have : (LinearMap.snd ‚Ñù ‚Ñù ‚Ñù) (0, 1) = (0 : ‚Ñù √ó ‚Ñù ‚Üí‚Çó[‚Ñù] ‚Ñù) (0, 1) := by rw [h]
    simp at this
  simp only [ae_eq_univ]
  exact le_antisymm ((measure_mono A).trans (le_of_eq B)) bot_le
#align polar_coord_source_ae_eq_univ polarCoord_source_ae_eq_univ

theorem integral_comp_polarCoord_symm {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    (f : ‚Ñù √ó ‚Ñù ‚Üí E) :
    (‚à´ p in polarCoord.target, p.1 ‚Ä¢ f (polarCoord.symm p)) = ‚à´ p, f p := by
  set B : ‚Ñù √ó ‚Ñù ‚Üí ‚Ñù √ó ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù √ó ‚Ñù := fun p =>
    LinearMap.toContinuousLinearMap (Matrix.toLin (Basis.finTwoProd ‚Ñù) (Basis.finTwoProd ‚Ñù)
      !![cos p.2, -p.1 * sin p.2; sin p.2, p.1 * cos p.2])
  have A : ‚àÄ p ‚àà polarCoord.symm.source, HasFDerivAt polarCoord.symm (B p) p := fun p _ =>
    hasFDerivAt_polarCoord_symm p
  have B_det : ‚àÄ p, (B p).det = p.1 := by
    intro p
    conv_rhs => rw [‚Üê one_mul p.1, ‚Üê cos_sq_add_sin_sq p.2]
    simp only [B, neg_mul, LinearMap.det_toContinuousLinearMap, LinearMap.det_toLin,
      Matrix.det_fin_two_of, sub_neg_eq_add]
    ring
  symm
  calc
    ‚à´ p, f p = ‚à´ p in polarCoord.source, f p := by
      rw [‚Üê integral_univ]
      apply setIntegral_congr_set_ae
      exact polarCoord_source_ae_eq_univ.symm
    _ = ‚à´ p in polarCoord.target, abs (B p).det ‚Ä¢ f (polarCoord.symm p) := by
      apply integral_target_eq_integral_abs_det_fderiv_smul volume A
    _ = ‚à´ p in polarCoord.target, p.1 ‚Ä¢ f (polarCoord.symm p) := by
      apply setIntegral_congr polarCoord.open_target.measurableSet fun x hx => ?_
      rw [B_det, abs_of_pos]
      exact hx.1
#align integral_comp_polar_coord_symm integral_comp_polarCoord_symm

end Real

noncomputable section Complex

namespace Complex

open scoped Real

/-- The polar coordinates partial homeomorphism in `‚ÑÇ`, mapping `r (cos Œ∏ + I * sin Œ∏)` to `(r, Œ∏)`.
It is a homeomorphism between `‚ÑÇ - ‚Ñù‚â§0` and `(0, +‚àû) √ó (-œÄ, œÄ)`. -/
protected noncomputable def polarCoord : PartialHomeomorph ‚ÑÇ (‚Ñù √ó ‚Ñù) :=
  equivRealProdCLM.toHomeomorph.transPartialHomeomorph polarCoord

protected theorem polarCoord_apply (a : ‚ÑÇ) :
    Complex.polarCoord a = (Complex.abs a, Complex.arg a) := by
  simp_rw [Complex.abs_def, Complex.normSq_apply, ‚Üê pow_two]
  rfl

protected theorem polarCoord_source : Complex.polarCoord.source = slitPlane := rfl

protected theorem polarCoord_target :
    Complex.polarCoord.target = Set.Ioi (0 : ‚Ñù) √óÀ¢ Set.Ioo (-œÄ) œÄ := rfl

@[simp]
protected theorem polarCoord_symm_apply (p : ‚Ñù √ó ‚Ñù) :
    Complex.polarCoord.symm p = p.1 * (Real.cos p.2 + Real.sin p.2 * Complex.I) := by
  simp [Complex.polarCoord, equivRealProdCLM_symm_apply, mul_add, mul_assoc]

theorem polardCoord_symm_abs (p : ‚Ñù √ó ‚Ñù) :
    Complex.abs (Complex.polarCoord.symm p) = |p.1| := by simp

protected theorem integral_comp_polarCoord_symm {E : Type*} [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] (f : ‚ÑÇ ‚Üí E) :
    (‚à´ p in polarCoord.target, p.1 ‚Ä¢ f (Complex.polarCoord.symm p)) = ‚à´ p, f p := by
  rw [‚Üê (Complex.volume_preserving_equiv_real_prod.symm).integral_comp
    measurableEquivRealProd.symm.measurableEmbedding, ‚Üê integral_comp_polarCoord_symm]
  rfl

end Complex
