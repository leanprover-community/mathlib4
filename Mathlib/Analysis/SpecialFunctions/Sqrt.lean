/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.Deriv.Pow

/-!
# Smoothness of `Real.sqrt`

In this file we prove that `Real.sqrt` is infinitely smooth at all points `x ‚â† 0` and provide some
dot-notation lemmas.

## Tags

sqrt, differentiable
-/


open Set

open scoped Topology

namespace Real

/-- Local homeomorph between `(0, +‚àû)` and `(0, +‚àû)` with `toFun = (¬∑ ^ 2)` and
`invFun = Real.sqrt`. -/
noncomputable def sqPartialHomeomorph : PartialHomeomorph ‚Ñù ‚Ñù where
  toFun x := x ^ 2
  invFun := (‚àö¬∑)
  source := Ioi 0
  target := Ioi 0
  map_source' _ h := mem_Ioi.2 (pow_pos (mem_Ioi.1 h) _)
  map_target' _ h := mem_Ioi.2 (sqrt_pos.2 h)
  left_inv' _ h := sqrt_sq (le_of_lt h)
  right_inv' _ h := sq_sqrt (le_of_lt h)
  open_source := isOpen_Ioi
  open_target := isOpen_Ioi
  continuousOn_toFun := (continuous_pow 2).continuousOn
  continuousOn_invFun := continuousOn_id.sqrt

theorem deriv_sqrt_aux {x : ‚Ñù} (hx : x ‚â† 0) :
    HasStrictDerivAt (‚àö¬∑) (1 / (2 * ‚àöx)) x ‚àß ‚àÄ n, ContDiffAt ‚Ñù n (‚àö¬∑) x := by
  rcases hx.lt_or_gt with hx | hx
  ¬∑ rw [sqrt_eq_zero_of_nonpos hx.le, mul_zero, div_zero]
    have : (‚àö¬∑) =·∂†[ùìù x] fun _ ‚Ü¶ 0 := (gt_mem_nhds hx).mono fun x hx ‚Ü¶ sqrt_eq_zero_of_nonpos hx.le
    exact
      ‚ü®(hasStrictDerivAt_const x (0 : ‚Ñù)).congr_of_eventuallyEq this.symm, fun n ‚Ü¶
        contDiffAt_const.congr_of_eventuallyEq this‚ü©
  ¬∑ have : ‚Üë2 * ‚àöx ^ (2 - 1) ‚â† 0 := by simp [(sqrt_pos.2 hx).ne', @two_ne_zero ‚Ñù]
    constructor
    ¬∑ simpa using sqPartialHomeomorph.hasStrictDerivAt_symm hx this (hasStrictDerivAt_pow 2 _)
    ¬∑ exact fun n ‚Ü¶ sqPartialHomeomorph.contDiffAt_symm_deriv this hx (hasDerivAt_pow 2 (‚àöx))
        (contDiffAt_id.pow 2)

theorem hasStrictDerivAt_sqrt {x : ‚Ñù} (hx : x ‚â† 0) : HasStrictDerivAt (‚àö¬∑) (1 / (2 * ‚àöx)) x :=
  (deriv_sqrt_aux hx).1

@[fun_prop]
theorem contDiffAt_sqrt {x : ‚Ñù} {n : WithTop ‚Ñï‚àû} (hx : x ‚â† 0) : ContDiffAt ‚Ñù n (‚àö¬∑) x :=
  (deriv_sqrt_aux hx).2 n

theorem hasDerivAt_sqrt {x : ‚Ñù} (hx : x ‚â† 0) : HasDerivAt (‚àö¬∑) (1 / (2 * ‚àöx)) x :=
  (hasStrictDerivAt_sqrt hx).hasDerivAt

end Real

open Real

section deriv

variable {f : ‚Ñù ‚Üí ‚Ñù} {s : Set ‚Ñù} {f' x : ‚Ñù}

theorem HasDerivWithinAt.sqrt (hf : HasDerivWithinAt f f' s x) (hx : f x ‚â† 0) :
    HasDerivWithinAt (fun y ‚Ü¶ ‚àö(f y)) (f' / (2 * ‚àö(f x))) s x := by
  simpa only [(¬∑ ‚àò ¬∑), div_eq_inv_mul, mul_one] using
    (hasDerivAt_sqrt hx).comp_hasDerivWithinAt x hf

theorem HasDerivAt.sqrt (hf : HasDerivAt f f' x) (hx : f x ‚â† 0) :
    HasDerivAt (fun y ‚Ü¶ ‚àö(f y)) (f' / (2 * ‚àö(f x))) x := by
  simpa only [(¬∑ ‚àò ¬∑), div_eq_inv_mul, mul_one] using (hasDerivAt_sqrt hx).comp x hf

theorem HasStrictDerivAt.sqrt (hf : HasStrictDerivAt f f' x) (hx : f x ‚â† 0) :
    HasStrictDerivAt (fun t ‚Ü¶ ‚àö(f t)) (f' / (2 * ‚àö(f x))) x := by
  simpa only [(¬∑ ‚àò ¬∑), div_eq_inv_mul, mul_one] using (hasStrictDerivAt_sqrt hx).comp x hf

theorem derivWithin_sqrt (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0)
    (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x ‚Ü¶ ‚àö(f x)) s x = derivWithin f s x / (2 * ‚àö(f x)) :=
  (hf.hasDerivWithinAt.sqrt hx).derivWithin hxs

@[simp]
theorem deriv_sqrt (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    deriv (fun x ‚Ü¶ ‚àö(f x)) x = deriv f x / (2 * ‚àö(f x)) :=
  (hf.hasDerivAt.sqrt hx).deriv

end deriv

section fderiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚Ñù} {n : WithTop ‚Ñï‚àû}
  {s : Set E} {x : E} {f' : E ‚ÜíL[‚Ñù] ‚Ñù}

theorem HasFDerivAt.sqrt (hf : HasFDerivAt f f' x) (hx : f x ‚â† 0) :
    HasFDerivAt (fun y ‚Ü¶ ‚àö(f y)) ((1 / (2 * ‚àö(f x))) ‚Ä¢ f') x :=
  (hasDerivAt_sqrt hx).comp_hasFDerivAt x hf

theorem HasStrictFDerivAt.sqrt (hf : HasStrictFDerivAt f f' x) (hx : f x ‚â† 0) :
    HasStrictFDerivAt (fun y ‚Ü¶ ‚àö(f y)) ((1 / (2 * ‚àö(f x))) ‚Ä¢ f') x :=
  (hasStrictDerivAt_sqrt hx).comp_hasStrictFDerivAt x hf

theorem HasFDerivWithinAt.sqrt (hf : HasFDerivWithinAt f f' s x) (hx : f x ‚â† 0) :
    HasFDerivWithinAt (fun y ‚Ü¶ ‚àö(f y)) ((1 / (2 * ‚àö(f x))) ‚Ä¢ f') s x :=
  (hasDerivAt_sqrt hx).comp_hasFDerivWithinAt x hf

@[fun_prop]
theorem DifferentiableWithinAt.sqrt (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0) :
    DifferentiableWithinAt ‚Ñù (fun y ‚Ü¶ ‚àö(f y)) s x :=
  (hf.hasFDerivWithinAt.sqrt hx).differentiableWithinAt

@[fun_prop]
theorem DifferentiableAt.sqrt (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    DifferentiableAt ‚Ñù (fun y ‚Ü¶ ‚àö(f y)) x :=
  (hf.hasFDerivAt.sqrt hx).differentiableAt

@[fun_prop]
theorem DifferentiableOn.sqrt (hf : DifferentiableOn ‚Ñù f s) (hs : ‚àÄ x ‚àà s, f x ‚â† 0) :
    DifferentiableOn ‚Ñù (fun y ‚Ü¶ ‚àö(f y)) s := fun x hx ‚Ü¶ (hf x hx).sqrt (hs x hx)

@[fun_prop]
theorem Differentiable.sqrt (hf : Differentiable ‚Ñù f) (hs : ‚àÄ x, f x ‚â† 0) :
    Differentiable ‚Ñù fun y ‚Ü¶ ‚àö(f y) := fun x ‚Ü¶ (hf x).sqrt (hs x)

theorem fderivWithin_sqrt (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0)
    (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    fderivWithin ‚Ñù (fun x ‚Ü¶ ‚àö(f x)) s x = (1 / (2 * ‚àö(f x))) ‚Ä¢ fderivWithin ‚Ñù f s x :=
  (hf.hasFDerivWithinAt.sqrt hx).fderivWithin hxs

@[simp]
theorem fderiv_sqrt (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    fderiv ‚Ñù (fun x ‚Ü¶ ‚àö(f x)) x = (1 / (2 * ‚àö(f x))) ‚Ä¢ fderiv ‚Ñù f x :=
  (hf.hasFDerivAt.sqrt hx).fderiv

@[fun_prop]
theorem ContDiffAt.sqrt (hf : ContDiffAt ‚Ñù n f x) (hx : f x ‚â† 0) :
    ContDiffAt ‚Ñù n (fun y ‚Ü¶ ‚àö(f y)) x :=
  (contDiffAt_sqrt hx).comp x hf

@[fun_prop]
theorem ContDiffWithinAt.sqrt (hf : ContDiffWithinAt ‚Ñù n f s x) (hx : f x ‚â† 0) :
    ContDiffWithinAt ‚Ñù n (fun y ‚Ü¶ ‚àö(f y)) s x :=
  (contDiffAt_sqrt hx).comp_contDiffWithinAt x hf

@[fun_prop]
theorem ContDiffOn.sqrt (hf : ContDiffOn ‚Ñù n f s) (hs : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContDiffOn ‚Ñù n (fun y ‚Ü¶ ‚àö(f y)) s := fun x hx ‚Ü¶ (hf x hx).sqrt (hs x hx)

@[fun_prop]
theorem ContDiff.sqrt (hf : ContDiff ‚Ñù n f) (h : ‚àÄ x, f x ‚â† 0) : ContDiff ‚Ñù n fun y ‚Ü¶ ‚àö(f y) :=
  contDiff_iff_contDiffAt.2 fun x ‚Ü¶ hf.contDiffAt.sqrt (h x)

end fderiv
