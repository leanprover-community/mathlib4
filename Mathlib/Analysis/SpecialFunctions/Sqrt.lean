/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.Deriv.Pow

/-!
# Smoothness of `Real.sqrt`

In this file we prove that `Real.sqrt` is infinitely smooth at all points `x â‰  0` and provide some
dot-notation lemmas.

## Tags

sqrt, differentiable
-/


open Set

open scoped Topology

namespace Real

/-- Local homeomorph between `(0, +âˆž)` and `(0, +âˆž)` with `toFun = (Â· ^ 2)` and
`invFun = Real.sqrt`. -/
noncomputable def sqPartialHomeomorph : PartialHomeomorph â„ â„ where
  toFun x := x ^ 2
  invFun := (âˆšÂ·)
  source := Ioi 0
  target := Ioi 0
  map_source' _ h := mem_Ioi.2 (pow_pos (mem_Ioi.1 h) _)
  map_target' _ h := mem_Ioi.2 (sqrt_pos.2 h)
  left_inv' _ h := sqrt_sq (le_of_lt h)
  right_inv' _ h := sq_sqrt (le_of_lt h)
  open_source := isOpen_Ioi
  open_target := isOpen_Ioi
  continuousOn_toFun := (continuous_pow 2).continuousOn
  continuousOn_invFun := continuousOn_id.sqrt

theorem deriv_sqrt_aux {x : â„} (hx : x â‰  0) :
    HasStrictDerivAt (âˆšÂ·) (1 / (2 * âˆšx)) x âˆ§ âˆ€ n, ContDiffAt â„ n (âˆšÂ·) x := by
  rcases hx.lt_or_gt with hx | hx
  Â· rw [sqrt_eq_zero_of_nonpos hx.le, mul_zero, div_zero]
    have : (âˆšÂ·) =á¶ [ð“ x] fun _ => 0 := (gt_mem_nhds hx).mono fun x hx => sqrt_eq_zero_of_nonpos hx.le
    exact
      âŸ¨(hasStrictDerivAt_const x (0 : â„)).congr_of_eventuallyEq this.symm, fun n =>
        contDiffAt_const.congr_of_eventuallyEq thisâŸ©
  Â· have : â†‘2 * âˆšx ^ (2 - 1) â‰  0 := by simp [(sqrt_pos.2 hx).ne', @two_ne_zero â„]
    constructor
    Â· simpa using sqPartialHomeomorph.hasStrictDerivAt_symm hx this (hasStrictDerivAt_pow 2 _)
    Â· exact fun n => sqPartialHomeomorph.contDiffAt_symm_deriv this hx (hasDerivAt_pow 2 (âˆšx))
        (contDiffAt_id.pow 2)

theorem hasStrictDerivAt_sqrt {x : â„} (hx : x â‰  0) : HasStrictDerivAt (âˆšÂ·) (1 / (2 * âˆšx)) x :=
  (deriv_sqrt_aux hx).1

@[fun_prop]
theorem contDiffAt_sqrt {x : â„} {n : WithTop â„•âˆž} (hx : x â‰  0) : ContDiffAt â„ n (âˆšÂ·) x :=
  (deriv_sqrt_aux hx).2 n

theorem hasDerivAt_sqrt {x : â„} (hx : x â‰  0) : HasDerivAt (âˆšÂ·) (1 / (2 * âˆšx)) x :=
  (hasStrictDerivAt_sqrt hx).hasDerivAt

end Real

open Real

section deriv

variable {f : â„ â†’ â„} {s : Set â„} {f' x : â„}

theorem HasDerivWithinAt.sqrt (hf : HasDerivWithinAt f f' s x) (hx : f x â‰  0) :
    HasDerivWithinAt (fun y => âˆš(f y)) (f' / (2 * âˆš(f x))) s x := by
  simpa only [(Â· âˆ˜ Â·), div_eq_inv_mul, mul_one] using
    (hasDerivAt_sqrt hx).comp_hasDerivWithinAt x hf

theorem HasDerivAt.sqrt (hf : HasDerivAt f f' x) (hx : f x â‰  0) :
    HasDerivAt (fun y => âˆš(f y)) (f' / (2 * âˆš(f x))) x := by
  simpa only [(Â· âˆ˜ Â·), div_eq_inv_mul, mul_one] using (hasDerivAt_sqrt hx).comp x hf

theorem HasStrictDerivAt.sqrt (hf : HasStrictDerivAt f f' x) (hx : f x â‰  0) :
    HasStrictDerivAt (fun t => âˆš(f t)) (f' / (2 * âˆš(f x))) x := by
  simpa only [(Â· âˆ˜ Â·), div_eq_inv_mul, mul_one] using (hasStrictDerivAt_sqrt hx).comp x hf

theorem derivWithin_sqrt (hf : DifferentiableWithinAt â„ f s x) (hx : f x â‰  0)
    (hxs : UniqueDiffWithinAt â„ s x) :
    derivWithin (fun x => âˆš(f x)) s x = derivWithin f s x / (2 * âˆš(f x)) :=
  (hf.hasDerivWithinAt.sqrt hx).derivWithin hxs

@[simp]
theorem deriv_sqrt (hf : DifferentiableAt â„ f x) (hx : f x â‰  0) :
    deriv (fun x => âˆš(f x)) x = deriv f x / (2 * âˆš(f x)) :=
  (hf.hasDerivAt.sqrt hx).deriv

end deriv

section fderiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {f : E â†’ â„} {n : WithTop â„•âˆž}
  {s : Set E} {x : E} {f' : E â†’L[â„] â„}

theorem HasFDerivAt.sqrt (hf : HasFDerivAt f f' x) (hx : f x â‰  0) :
    HasFDerivAt (fun y => âˆš(f y)) ((1 / (2 * âˆš(f x))) â€¢ f') x :=
  (hasDerivAt_sqrt hx).comp_hasFDerivAt x hf

theorem HasStrictFDerivAt.sqrt (hf : HasStrictFDerivAt f f' x) (hx : f x â‰  0) :
    HasStrictFDerivAt (fun y => âˆš(f y)) ((1 / (2 * âˆš(f x))) â€¢ f') x :=
  (hasStrictDerivAt_sqrt hx).comp_hasStrictFDerivAt x hf

theorem HasFDerivWithinAt.sqrt (hf : HasFDerivWithinAt f f' s x) (hx : f x â‰  0) :
    HasFDerivWithinAt (fun y => âˆš(f y)) ((1 / (2 * âˆš(f x))) â€¢ f') s x :=
  (hasDerivAt_sqrt hx).comp_hasFDerivWithinAt x hf

@[fun_prop]
theorem DifferentiableWithinAt.sqrt (hf : DifferentiableWithinAt â„ f s x) (hx : f x â‰  0) :
    DifferentiableWithinAt â„ (fun y => âˆš(f y)) s x :=
  (hf.hasFDerivWithinAt.sqrt hx).differentiableWithinAt

@[fun_prop]
theorem DifferentiableAt.sqrt (hf : DifferentiableAt â„ f x) (hx : f x â‰  0) :
    DifferentiableAt â„ (fun y => âˆš(f y)) x :=
  (hf.hasFDerivAt.sqrt hx).differentiableAt

@[fun_prop]
theorem DifferentiableOn.sqrt (hf : DifferentiableOn â„ f s) (hs : âˆ€ x âˆˆ s, f x â‰  0) :
    DifferentiableOn â„ (fun y => âˆš(f y)) s := fun x hx => (hf x hx).sqrt (hs x hx)

@[fun_prop]
theorem Differentiable.sqrt (hf : Differentiable â„ f) (hs : âˆ€ x, f x â‰  0) :
    Differentiable â„ fun y => âˆš(f y) := fun x => (hf x).sqrt (hs x)

theorem fderivWithin_sqrt (hf : DifferentiableWithinAt â„ f s x) (hx : f x â‰  0)
    (hxs : UniqueDiffWithinAt â„ s x) :
    fderivWithin â„ (fun x => âˆš(f x)) s x = (1 / (2 * âˆš(f x))) â€¢ fderivWithin â„ f s x :=
  (hf.hasFDerivWithinAt.sqrt hx).fderivWithin hxs

@[simp]
theorem fderiv_sqrt (hf : DifferentiableAt â„ f x) (hx : f x â‰  0) :
    fderiv â„ (fun x => âˆš(f x)) x = (1 / (2 * âˆš(f x))) â€¢ fderiv â„ f x :=
  (hf.hasFDerivAt.sqrt hx).fderiv

@[fun_prop]
theorem ContDiffAt.sqrt (hf : ContDiffAt â„ n f x) (hx : f x â‰  0) :
    ContDiffAt â„ n (fun y => âˆš(f y)) x :=
  (contDiffAt_sqrt hx).comp x hf

@[fun_prop]
theorem ContDiffWithinAt.sqrt (hf : ContDiffWithinAt â„ n f s x) (hx : f x â‰  0) :
    ContDiffWithinAt â„ n (fun y => âˆš(f y)) s x :=
  (contDiffAt_sqrt hx).comp_contDiffWithinAt x hf

@[fun_prop]
theorem ContDiffOn.sqrt (hf : ContDiffOn â„ n f s) (hs : âˆ€ x âˆˆ s, f x â‰  0) :
    ContDiffOn â„ n (fun y => âˆš(f y)) s := fun x hx => (hf x hx).sqrt (hs x hx)

@[fun_prop]
theorem ContDiff.sqrt (hf : ContDiff â„ n f) (h : âˆ€ x, f x â‰  0) : ContDiff â„ n fun y => âˆš(f y) :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.sqrt (h x)

end fderiv
