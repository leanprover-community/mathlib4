/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
module

public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.Deriv
public import Mathlib.Analysis.Calculus.LogDeriv
public import Mathlib.Analysis.Meromorphic.Basic
public import Mathlib.Analysis.SpecialFunctions.Complex.Log
public import Mathlib.Analysis.SpecialFunctions.ExpDeriv

/-!
# Differentiability of the complex `log` function

-/

@[expose] public section

assert_not_exists IsConformalMap Conformal

open Set Filter

open scoped Real Topology

namespace Complex

theorem isOpenMap_exp : IsOpenMap exp :=
  isOpenMap_of_hasStrictDerivAt hasStrictDerivAt_exp exp_ne_zero

theorem hasStrictDerivAt_log {x : â„‚} (h : x âˆˆ slitPlane) : HasStrictDerivAt log xâ»Â¹ x :=
  have h0 : x â‰  0 := slitPlane_ne_zero h
  expOpenPartialHomeomorph.hasStrictDerivAt_symm h h0 <| by
    simpa [exp_log h0] using hasStrictDerivAt_exp (log x)

lemma hasDerivAt_log {z : â„‚} (hz : z âˆˆ slitPlane) : HasDerivAt log zâ»Â¹ z :=
  HasStrictDerivAt.hasDerivAt <| hasStrictDerivAt_log hz

@[fun_prop]
lemma differentiableAt_log {z : â„‚} (hz : z âˆˆ slitPlane) : DifferentiableAt â„‚ log z :=
  (hasDerivAt_log hz).differentiableAt

@[fun_prop]
theorem hasStrictFDerivAt_log_real {x : â„‚} (h : x âˆˆ slitPlane) :
    HasStrictFDerivAt log (xâ»Â¹ â€¢ (1 : â„‚ â†’L[â„] â„‚)) x :=
  (hasStrictDerivAt_log h).complexToReal_fderiv

theorem contDiffAt_log {x : â„‚} (h : x âˆˆ slitPlane) {n : WithTop â„•âˆ} : ContDiffAt â„‚ n log x :=
  expOpenPartialHomeomorph.contDiffAt_symm_deriv (exp_ne_zero <| log x) h (hasDerivAt_exp _)
    contDiff_exp.contDiffAt

end Complex

section LogDeriv

open Complex Filter

open scoped Topology

variable {Î± : Type*} [TopologicalSpace Î±] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

theorem HasStrictFDerivAt.clog {f : E â†’ â„‚} {f' : StrongDual â„‚ E} {x : E}
    (hâ‚ : HasStrictFDerivAt f f' x) (hâ‚‚ : f x âˆˆ slitPlane) :
    HasStrictFDerivAt (fun t => log (f t)) ((f x)â»Â¹ â€¢ f') x :=
  (hasStrictDerivAt_log hâ‚‚).comp_hasStrictFDerivAt x hâ‚

theorem HasStrictDerivAt.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} (hâ‚ : HasStrictDerivAt f f' x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasStrictDerivAt (fun t => log (f t)) (f' / f x) x := by
  rw [div_eq_inv_mul]; exact (hasStrictDerivAt_log hâ‚‚).comp x hâ‚

theorem HasStrictDerivAt.clog_real {f : â„ â†’ â„‚} {x : â„} {f' : â„‚} (hâ‚ : HasStrictDerivAt f f' x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasStrictDerivAt (fun t => log (f t)) (f' / f x) x := by
  simpa only [div_eq_inv_mul] using (hasStrictFDerivAt_log_real hâ‚‚).comp_hasStrictDerivAt x hâ‚

theorem HasFDerivAt.clog {f : E â†’ â„‚} {f' : StrongDual â„‚ E} {x : E} (hâ‚ : HasFDerivAt f f' x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasFDerivAt (fun t => log (f t)) ((f x)â»Â¹ â€¢ f') x :=
  (hasStrictDerivAt_log hâ‚‚).hasDerivAt.comp_hasFDerivAt x hâ‚

theorem HasDerivAt.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} (hâ‚ : HasDerivAt f f' x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasDerivAt (fun t => log (f t)) (f' / f x) x := by
  rw [div_eq_inv_mul]; exact (hasStrictDerivAt_log hâ‚‚).hasDerivAt.comp x hâ‚

theorem HasDerivAt.clog_real {f : â„ â†’ â„‚} {x : â„} {f' : â„‚} (hâ‚ : HasDerivAt f f' x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasDerivAt (fun t => log (f t)) (f' / f x) x := by
  simpa only [div_eq_inv_mul] using
    (hasStrictFDerivAt_log_real hâ‚‚).hasFDerivAt.comp_hasDerivAt x hâ‚

theorem DifferentiableAt.clog {f : E â†’ â„‚} {x : E} (hâ‚ : DifferentiableAt â„‚ f x)
    (hâ‚‚ : f x âˆˆ slitPlane) : DifferentiableAt â„‚ (fun t => log (f t)) x :=
  (hâ‚.hasFDerivAt.clog hâ‚‚).differentiableAt

theorem HasFDerivWithinAt.clog {f : E â†’ â„‚} {f' : StrongDual â„‚ E} {s : Set E} {x : E}
    (hâ‚ : HasFDerivWithinAt f f' s x) (hâ‚‚ : f x âˆˆ slitPlane) :
    HasFDerivWithinAt (fun t => log (f t)) ((f x)â»Â¹ â€¢ f') s x :=
  (hasStrictDerivAt_log hâ‚‚).hasDerivAt.comp_hasFDerivWithinAt x hâ‚

theorem HasDerivWithinAt.clog {f : â„‚ â†’ â„‚} {f' x : â„‚} {s : Set â„‚} (hâ‚ : HasDerivWithinAt f f' s x)
    (hâ‚‚ : f x âˆˆ slitPlane) : HasDerivWithinAt (fun t => log (f t)) (f' / f x) s x := by
  rw [div_eq_inv_mul]
  exact (hasStrictDerivAt_log hâ‚‚).hasDerivAt.comp_hasDerivWithinAt x hâ‚

theorem HasDerivWithinAt.clog_real {f : â„ â†’ â„‚} {s : Set â„} {x : â„} {f' : â„‚}
    (hâ‚ : HasDerivWithinAt f f' s x) (hâ‚‚ : f x âˆˆ slitPlane) :
    HasDerivWithinAt (fun t => log (f t)) (f' / f x) s x := by
  simpa only [div_eq_inv_mul] using
    (hasStrictFDerivAt_log_real hâ‚‚).hasFDerivAt.comp_hasDerivWithinAt x hâ‚

theorem DifferentiableWithinAt.clog {f : E â†’ â„‚} {s : Set E} {x : E}
    (hâ‚ : DifferentiableWithinAt â„‚ f s x) (hâ‚‚ : f x âˆˆ slitPlane) :
    DifferentiableWithinAt â„‚ (fun t => log (f t)) s x :=
  (hâ‚.hasFDerivWithinAt.clog hâ‚‚).differentiableWithinAt

theorem DifferentiableOn.clog {f : E â†’ â„‚} {s : Set E} (hâ‚ : DifferentiableOn â„‚ f s)
    (hâ‚‚ : âˆ€ x âˆˆ s, f x âˆˆ slitPlane) : DifferentiableOn â„‚ (fun t => log (f t)) s :=
  fun x hx => (hâ‚ x hx).clog (hâ‚‚ x hx)

theorem Differentiable.clog {f : E â†’ â„‚} (hâ‚ : Differentiable â„‚ f)
    (hâ‚‚ : âˆ€ x, f x âˆˆ slitPlane) : Differentiable â„‚ fun t => log (f t) := fun x =>
  (hâ‚ x).clog (hâ‚‚ x)

/-- The derivative of `log âˆ˜ f` is the logarithmic derivative provided `f` is differentiable and
we are on the slitPlane. -/
lemma Complex.deriv_log_comp_eq_logDeriv {f : â„‚ â†’ â„‚} {x : â„‚} (hâ‚ : DifferentiableAt â„‚ f x)
    (hâ‚‚ : f x âˆˆ Complex.slitPlane) : deriv (Complex.log âˆ˜ f) x = logDeriv f x := by
  have A := (HasDerivAt.clog hâ‚.hasDerivAt hâ‚‚).deriv
  rw [â† hâ‚.hasDerivAt.deriv] at A
  simp only [logDeriv, Pi.div_apply, â† A, Function.comp_def]

protected theorem MeromorphicOn.logDeriv {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ]
    [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [CompleteSpace ğ•œ']
    {f : ğ•œ â†’ ğ•œ'} {s : Set ğ•œ} (h : MeromorphicOn f s) : MeromorphicOn (logDeriv f) s :=
  h.deriv.div h

protected theorem Meromorphic.logDeriv {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ]
    [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [CompleteSpace ğ•œ']
    {f : ğ•œ â†’ ğ•œ'} (h : Meromorphic f) : Meromorphic (logDeriv f) :=
  h.deriv.div h

end LogDeriv
