/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.Calculus.Inverse
import Mathlib.Analysis.SpecialFunctions.Complex.Log
import Mathlib.Analysis.SpecialFunctions.ExpDeriv

#align_import analysis.special_functions.complex.log_deriv from "leanprover-community/mathlib"@"6a5c85000ab93fe5dcfdf620676f614ba8e18c26"

/-!
# Differentiability of the complex `log` function

-/


open Set Filter

open scoped Real Topology

namespace Complex

theorem isOpenMap_exp : IsOpenMap exp :=
  open_map_of_strict_deriv hasStrictDerivAt_exp exp_ne_zero
#align complex.is_open_map_exp Complex.isOpenMap_exp

/-- `Complex.exp` as a `LocalHomeomorph` with `source = {z | -œÄ < im z < œÄ}` and
`target = {z | 0 < re z} ‚à™ {z | im z ‚â† 0}`. This definition is used to prove that `Complex.log`
is complex differentiable at all points but the negative real semi-axis. -/
noncomputable def expLocalHomeomorph : LocalHomeomorph ‚ÑÇ ‚ÑÇ :=
  LocalHomeomorph.ofContinuousOpen
    { toFun := exp
      invFun := log
      source := {z : ‚ÑÇ | z.im ‚àà Ioo (-œÄ) œÄ}
      target := {z : ‚ÑÇ | 0 < z.re} ‚à™ {z : ‚ÑÇ | z.im ‚â† 0}
      map_source' := by
        rintro ‚ü®x, y‚ü© ‚ü®h‚ÇÅ : -œÄ < y, h‚ÇÇ : y < œÄ‚ü©
        -- ‚ä¢ exp { re := x, im := y } ‚àà {z | 0 < z.re} ‚à™ {z | z.im ‚â† 0}
        refine' (not_or_of_imp fun hz => _).symm
        -- ‚ä¢ exp { re := x, im := y } ‚àà {z | 0 < z.re}
        obtain rfl : y = 0 := by
          rw [exp_im] at hz
          simpa [(Real.exp_pos _).ne', Real.sin_eq_zero_iff_of_lt_of_lt h‚ÇÅ h‚ÇÇ] using hz
        rw [mem_setOf_eq, ‚Üê ofReal_def, exp_ofReal_re]
        -- ‚ä¢ 0 < Real.exp x
        exact Real.exp_pos x
        -- üéâ no goals
      map_target' := fun z h => by
        simp only [mem_setOf, log_im, mem_Ioo, neg_pi_lt_arg, arg_lt_pi_iff, true_and]
        -- ‚ä¢ 0 ‚â§ z.re ‚à® z.im ‚â† 0
        exact h.imp_left le_of_lt
        -- üéâ no goals
      left_inv' := fun x hx => log_exp hx.1 (le_of_lt hx.2)
      right_inv' := fun x hx => exp_log <| by rintro rfl; simp [lt_irrefl] at hx }
                                              -- ‚ä¢ False
                                                          -- üéâ no goals
    continuous_exp.continuousOn isOpenMap_exp (isOpen_Ioo.preimage continuous_im)
#align complex.exp_local_homeomorph Complex.expLocalHomeomorph

theorem hasStrictDerivAt_log {x : ‚ÑÇ} (h : 0 < x.re ‚à® x.im ‚â† 0) : HasStrictDerivAt log x‚Åª¬π x :=
  have h0 : x ‚â† 0 := by rintro rfl; simp [lt_irrefl] at h
                        -- ‚ä¢ False
                                    -- üéâ no goals
  expLocalHomeomorph.hasStrictDerivAt_symm h h0 <| by
    simpa [exp_log h0] using hasStrictDerivAt_exp (log x)
    -- üéâ no goals
#align complex.has_strict_deriv_at_log Complex.hasStrictDerivAt_log

theorem hasStrictFDerivAt_log_real {x : ‚ÑÇ} (h : 0 < x.re ‚à® x.im ‚â† 0) :
    HasStrictFDerivAt log (x‚Åª¬π ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚Ñù] ‚ÑÇ)) x :=
  (hasStrictDerivAt_log h).complexToReal_fderiv
#align complex.has_strict_fderiv_at_log_real Complex.hasStrictFDerivAt_log_real

theorem contDiffAt_log {x : ‚ÑÇ} (h : 0 < x.re ‚à® x.im ‚â† 0) {n : ‚Ñï‚àû} : ContDiffAt ‚ÑÇ n log x :=
  expLocalHomeomorph.contDiffAt_symm_deriv (exp_ne_zero <| log x) h (hasDerivAt_exp _)
    contDiff_exp.contDiffAt
#align complex.cont_diff_at_log Complex.contDiffAt_log

end Complex

section LogDeriv

open Complex Filter

open scoped Topology

variable {Œ± : Type*} [TopologicalSpace Œ±] {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

theorem HasStrictFDerivAt.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {x : E} (h‚ÇÅ : HasStrictFDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasStrictFDerivAt (fun t => log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
  (hasStrictDerivAt_log h‚ÇÇ).comp_hasStrictFDerivAt x h‚ÇÅ
#align has_strict_fderiv_at.clog HasStrictFDerivAt.clog

theorem HasStrictDerivAt.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} (h‚ÇÅ : HasStrictDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasStrictDerivAt (fun t => log (f t)) (f' / f x) x := by
  rw [div_eq_inv_mul]; exact (hasStrictDerivAt_log h‚ÇÇ).comp x h‚ÇÅ
  -- ‚ä¢ HasStrictDerivAt (fun t => log (f t)) ((f x)‚Åª¬π * f') x
                       -- üéâ no goals
#align has_strict_deriv_at.clog HasStrictDerivAt.clog

theorem HasStrictDerivAt.clog_real {f : ‚Ñù ‚Üí ‚ÑÇ} {x : ‚Ñù} {f' : ‚ÑÇ} (h‚ÇÅ : HasStrictDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasStrictDerivAt (fun t => log (f t)) (f' / f x) x := by
  simpa only [div_eq_inv_mul] using (hasStrictFDerivAt_log_real h‚ÇÇ).comp_hasStrictDerivAt x h‚ÇÅ
  -- üéâ no goals
#align has_strict_deriv_at.clog_real HasStrictDerivAt.clog_real

theorem HasFDerivAt.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {x : E} (h‚ÇÅ : HasFDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasFDerivAt (fun t => log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
  (hasStrictDerivAt_log h‚ÇÇ).hasDerivAt.comp_hasFDerivAt x h‚ÇÅ
#align has_fderiv_at.clog HasFDerivAt.clog

theorem HasDerivAt.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} (h‚ÇÅ : HasDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasDerivAt (fun t => log (f t)) (f' / f x) x := by
  rw [div_eq_inv_mul]; exact (hasStrictDerivAt_log h‚ÇÇ).hasDerivAt.comp x h‚ÇÅ
  -- ‚ä¢ HasDerivAt (fun t => log (f t)) ((f x)‚Åª¬π * f') x
                       -- üéâ no goals
#align has_deriv_at.clog HasDerivAt.clog

theorem HasDerivAt.clog_real {f : ‚Ñù ‚Üí ‚ÑÇ} {x : ‚Ñù} {f' : ‚ÑÇ} (h‚ÇÅ : HasDerivAt f f' x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasDerivAt (fun t => log (f t)) (f' / f x) x := by
  simpa only [div_eq_inv_mul] using
    (hasStrictFDerivAt_log_real h‚ÇÇ).hasFDerivAt.comp_hasDerivAt x h‚ÇÅ
#align has_deriv_at.clog_real HasDerivAt.clog_real

theorem DifferentiableAt.clog {f : E ‚Üí ‚ÑÇ} {x : E} (h‚ÇÅ : DifferentiableAt ‚ÑÇ f x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : DifferentiableAt ‚ÑÇ (fun t => log (f t)) x :=
  (h‚ÇÅ.hasFDerivAt.clog h‚ÇÇ).differentiableAt
#align differentiable_at.clog DifferentiableAt.clog

theorem HasFDerivWithinAt.clog {f : E ‚Üí ‚ÑÇ} {f' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ} {s : Set E} {x : E}
    (h‚ÇÅ : HasFDerivWithinAt f f' s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
    HasFDerivWithinAt (fun t => log (f t)) ((f x)‚Åª¬π ‚Ä¢ f') s x :=
  (hasStrictDerivAt_log h‚ÇÇ).hasDerivAt.comp_hasFDerivWithinAt x h‚ÇÅ
#align has_fderiv_within_at.clog HasFDerivWithinAt.clog

theorem HasDerivWithinAt.clog {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} {s : Set ‚ÑÇ} (h‚ÇÅ : HasDerivWithinAt f f' s x)
    (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) : HasDerivWithinAt (fun t => log (f t)) (f' / f x) s x := by
  rw [div_eq_inv_mul]
  -- ‚ä¢ HasDerivWithinAt (fun t => log (f t)) ((f x)‚Åª¬π * f') s x
  exact (hasStrictDerivAt_log h‚ÇÇ).hasDerivAt.comp_hasDerivWithinAt x h‚ÇÅ
  -- üéâ no goals
#align has_deriv_within_at.clog HasDerivWithinAt.clog

theorem HasDerivWithinAt.clog_real {f : ‚Ñù ‚Üí ‚ÑÇ} {s : Set ‚Ñù} {x : ‚Ñù} {f' : ‚ÑÇ}
    (h‚ÇÅ : HasDerivWithinAt f f' s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
    HasDerivWithinAt (fun t => log (f t)) (f' / f x) s x := by
  simpa only [div_eq_inv_mul] using
    (hasStrictFDerivAt_log_real h‚ÇÇ).hasFDerivAt.comp_hasDerivWithinAt x h‚ÇÅ
#align has_deriv_within_at.clog_real HasDerivWithinAt.clog_real

theorem DifferentiableWithinAt.clog {f : E ‚Üí ‚ÑÇ} {s : Set E} {x : E}
    (h‚ÇÅ : DifferentiableWithinAt ‚ÑÇ f s x) (h‚ÇÇ : 0 < (f x).re ‚à® (f x).im ‚â† 0) :
    DifferentiableWithinAt ‚ÑÇ (fun t => log (f t)) s x :=
  (h‚ÇÅ.hasFDerivWithinAt.clog h‚ÇÇ).differentiableWithinAt
#align differentiable_within_at.clog DifferentiableWithinAt.clog

theorem DifferentiableOn.clog {f : E ‚Üí ‚ÑÇ} {s : Set E} (h‚ÇÅ : DifferentiableOn ‚ÑÇ f s)
    (h‚ÇÇ : ‚àÄ x ‚àà s, 0 < (f x).re ‚à® (f x).im ‚â† 0) : DifferentiableOn ‚ÑÇ (fun t => log (f t)) s :=
  fun x hx => (h‚ÇÅ x hx).clog (h‚ÇÇ x hx)
#align differentiable_on.clog DifferentiableOn.clog

theorem Differentiable.clog {f : E ‚Üí ‚ÑÇ} (h‚ÇÅ : Differentiable ‚ÑÇ f)
    (h‚ÇÇ : ‚àÄ x, 0 < (f x).re ‚à® (f x).im ‚â† 0) : Differentiable ‚ÑÇ fun t => log (f t) := fun x =>
  (h‚ÇÅ x).clog (h‚ÇÇ x)
#align differentiable.clog Differentiable.clog

end LogDeriv

