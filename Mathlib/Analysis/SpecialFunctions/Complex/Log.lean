/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Complex.Arg
import Mathlib.Analysis.SpecialFunctions.Log.Basic

#align_import analysis.special_functions.complex.log from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# The complex `log` function

Basic properties, relationship with `exp`.
-/


noncomputable section

namespace Complex

open Set Filter Bornology

open scoped Real Topology ComplexConjugate

/-- Inverse of the `exp` function. Returns values such that `(log x).im > - œÄ` and `(log x).im ‚â§ œÄ`.
  `log 0 = 0`-/
-- Porting note: @[pp_nodot] does not exist in mathlib4
noncomputable def log (x : ‚ÑÇ) : ‚ÑÇ :=
  x.abs.log + arg x * I
#align complex.log Complex.log

theorem log_re (x : ‚ÑÇ) : x.log.re = x.abs.log := by simp [log]
#align complex.log_re Complex.log_re

theorem log_im (x : ‚ÑÇ) : x.log.im = x.arg := by simp [log]
#align complex.log_im Complex.log_im

theorem neg_pi_lt_log_im (x : ‚ÑÇ) : -œÄ < (log x).im := by simp only [log_im, neg_pi_lt_arg]
#align complex.neg_pi_lt_log_im Complex.neg_pi_lt_log_im

theorem log_im_le_pi (x : ‚ÑÇ) : (log x).im ‚â§ œÄ := by simp only [log_im, arg_le_pi]
#align complex.log_im_le_pi Complex.log_im_le_pi

theorem exp_log {x : ‚ÑÇ} (hx : x ‚â† 0) : exp (log x) = x := by
  rw [log, exp_add_mul_I, ‚Üê ofReal_sin, sin_arg, ‚Üê ofReal_cos, cos_arg hx, ‚Üê ofReal_exp,
    Real.exp_log (abs.pos hx), mul_add, ofReal_div, ofReal_div,
    mul_div_cancel‚ÇÄ _ (ofReal_ne_zero.2 <| abs.ne_zero hx), ‚Üê mul_assoc,
    mul_div_cancel‚ÇÄ _ (ofReal_ne_zero.2 <| abs.ne_zero hx), re_add_im]
#align complex.exp_log Complex.exp_log

@[simp]
theorem range_exp : Set.range exp = {0}·∂ú :=
  Set.ext fun x =>
    ‚ü®by
      rintro ‚ü®x, rfl‚ü©
      exact exp_ne_zero x, fun hx => ‚ü®log x, exp_log hx‚ü©‚ü©
#align complex.range_exp Complex.range_exp

theorem log_exp {x : ‚ÑÇ} (hx‚ÇÅ : -œÄ < x.im) (hx‚ÇÇ : x.im ‚â§ œÄ) : log (exp x) = x := by
  rw [log, abs_exp, Real.log_exp, exp_eq_exp_re_mul_sin_add_cos, ‚Üê ofReal_exp,
    arg_mul_cos_add_sin_mul_I (Real.exp_pos _) ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©, re_add_im]
#align complex.log_exp Complex.log_exp

theorem exp_inj_of_neg_pi_lt_of_le_pi {x y : ‚ÑÇ} (hx‚ÇÅ : -œÄ < x.im) (hx‚ÇÇ : x.im ‚â§ œÄ) (hy‚ÇÅ : -œÄ < y.im)
    (hy‚ÇÇ : y.im ‚â§ œÄ) (hxy : exp x = exp y) : x = y := by
  rw [‚Üê log_exp hx‚ÇÅ hx‚ÇÇ, ‚Üê log_exp hy‚ÇÅ hy‚ÇÇ, hxy]
#align complex.exp_inj_of_neg_pi_lt_of_le_pi Complex.exp_inj_of_neg_pi_lt_of_le_pi

theorem ofReal_log {x : ‚Ñù} (hx : 0 ‚â§ x) : (x.log : ‚ÑÇ) = log x :=
  Complex.ext (by rw [log_re, ofReal_re, abs_of_nonneg hx])
    (by rw [ofReal_im, log_im, arg_ofReal_of_nonneg hx])
#align complex.of_real_log Complex.ofReal_log

@[simp, norm_cast]
lemma natCast_log {n : ‚Ñï} : Real.log n = log n := ofReal_natCast n ‚ñ∏ ofReal_log n.cast_nonneg

@[simp]
lemma ofNat_log {n : ‚Ñï} [n.AtLeastTwo] :
    Real.log (no_index (OfNat.ofNat n)) = log (OfNat.ofNat n) :=
  natCast_log

theorem log_ofReal_re (x : ‚Ñù) : (log (x : ‚ÑÇ)).re = Real.log x := by simp [log_re]
#align complex.log_of_real_re Complex.log_ofReal_re

theorem log_ofReal_mul {r : ‚Ñù} (hr : 0 < r) {x : ‚ÑÇ} (hx : x ‚â† 0) :
    log (r * x) = Real.log r + log x := by
  replace hx := Complex.abs.ne_zero_iff.mpr hx
  simp_rw [log, map_mul, abs_ofReal, arg_real_mul _ hr, abs_of_pos hr, Real.log_mul hr.ne' hx,
    ofReal_add, add_assoc]
#align complex.log_of_real_mul Complex.log_ofReal_mul

theorem log_mul_ofReal (r : ‚Ñù) (hr : 0 < r) (x : ‚ÑÇ) (hx : x ‚â† 0) :
    log (x * r) = Real.log r + log x := by rw [mul_comm, log_ofReal_mul hr hx]
#align complex.log_mul_of_real Complex.log_mul_ofReal

lemma log_mul_eq_add_log_iff {x y : ‚ÑÇ} (hx‚ÇÄ : x ‚â† 0) (hy‚ÇÄ : y ‚â† 0) :
    log (x * y) = log x + log y ‚Üî arg x + arg y ‚àà Set.Ioc (-œÄ) œÄ := by
  refine ext_iff.trans <| Iff.trans ?_ <| arg_mul_eq_add_arg_iff hx‚ÇÄ hy‚ÇÄ
  simp_rw [add_re, add_im, log_re, log_im, AbsoluteValue.map_mul,
    Real.log_mul (abs.ne_zero hx‚ÇÄ) (abs.ne_zero hy‚ÇÄ), true_and]

alias ‚ü®_, log_mul‚ü© := log_mul_eq_add_log_iff

@[simp]
theorem log_zero : log 0 = 0 := by simp [log]
#align complex.log_zero Complex.log_zero

@[simp]
theorem log_one : log 1 = 0 := by simp [log]
#align complex.log_one Complex.log_one

theorem log_neg_one : log (-1) = œÄ * I := by simp [log]
#align complex.log_neg_one Complex.log_neg_one

theorem log_I : log I = œÄ / 2 * I := by simp [log]
set_option linter.uppercaseLean3 false in
#align complex.log_I Complex.log_I

theorem log_neg_I : log (-I) = -(œÄ / 2) * I := by simp [log]
set_option linter.uppercaseLean3 false in
#align complex.log_neg_I Complex.log_neg_I

theorem log_conj_eq_ite (x : ‚ÑÇ) : log (conj x) = if x.arg = œÄ then log x else conj (log x) := by
  simp_rw [log, abs_conj, arg_conj, map_add, map_mul, conj_ofReal]
  split_ifs with hx
  ¬∑ rw [hx]
  simp_rw [ofReal_neg, conj_I, mul_neg, neg_mul]
#align complex.log_conj_eq_ite Complex.log_conj_eq_ite

theorem log_conj (x : ‚ÑÇ) (h : x.arg ‚â† œÄ) : log (conj x) = conj (log x) := by
  rw [log_conj_eq_ite, if_neg h]
#align complex.log_conj Complex.log_conj

theorem log_inv_eq_ite (x : ‚ÑÇ) : log x‚Åª¬π = if x.arg = œÄ then -conj (log x) else -log x := by
  by_cases hx : x = 0
  ¬∑ simp [hx]
  rw [inv_def, log_mul_ofReal, Real.log_inv, ofReal_neg, ‚Üê sub_eq_neg_add, log_conj_eq_ite]
  ¬∑ simp_rw [log, map_add, map_mul, conj_ofReal, conj_I, normSq_eq_abs, Real.log_pow,
      Nat.cast_two, ofReal_mul, neg_add, mul_neg, neg_neg]
    norm_num; rw [two_mul] -- Porting note: added to simplify `‚Üë2`
    split_ifs
    ¬∑ rw [add_sub_right_comm, sub_add_cancel_left]
    ¬∑ rw [add_sub_right_comm, sub_add_cancel_left]
  ¬∑ rwa [inv_pos, Complex.normSq_pos]
  ¬∑ rwa [map_ne_zero]
#align complex.log_inv_eq_ite Complex.log_inv_eq_ite

theorem log_inv (x : ‚ÑÇ) (hx : x.arg ‚â† œÄ) : log x‚Åª¬π = -log x := by rw [log_inv_eq_ite, if_neg hx]
#align complex.log_inv Complex.log_inv

theorem two_pi_I_ne_zero : (2 * œÄ * I : ‚ÑÇ) ‚â† 0 := by norm_num [Real.pi_ne_zero, I_ne_zero]
set_option linter.uppercaseLean3 false in
#align complex.two_pi_I_ne_zero Complex.two_pi_I_ne_zero

theorem exp_eq_one_iff {x : ‚ÑÇ} : exp x = 1 ‚Üî ‚àÉ n : ‚Ñ§, x = n * (2 * œÄ * I) := by
  constructor
  ¬∑ intro h
    rcases existsUnique_add_zsmul_mem_Ioc Real.two_pi_pos x.im (-œÄ) with ‚ü®n, hn, -‚ü©
    use -n
    rw [Int.cast_neg, neg_mul, eq_neg_iff_add_eq_zero]
    have : (x + n * (2 * œÄ * I)).im ‚àà Set.Ioc (-œÄ) œÄ := by simpa [two_mul, mul_add] using hn
    rw [‚Üê log_exp this.1 this.2, exp_periodic.int_mul n, h, log_one]
  ¬∑ rintro ‚ü®n, rfl‚ü©
    exact (exp_periodic.int_mul n).eq.trans exp_zero
#align complex.exp_eq_one_iff Complex.exp_eq_one_iff

theorem exp_eq_exp_iff_exp_sub_eq_one {x y : ‚ÑÇ} : exp x = exp y ‚Üî exp (x - y) = 1 := by
  rw [exp_sub, div_eq_one_iff_eq (exp_ne_zero _)]
#align complex.exp_eq_exp_iff_exp_sub_eq_one Complex.exp_eq_exp_iff_exp_sub_eq_one

theorem exp_eq_exp_iff_exists_int {x y : ‚ÑÇ} : exp x = exp y ‚Üî ‚àÉ n : ‚Ñ§, x = y + n * (2 * œÄ * I) := by
  simp only [exp_eq_exp_iff_exp_sub_eq_one, exp_eq_one_iff, sub_eq_iff_eq_add']
#align complex.exp_eq_exp_iff_exists_int Complex.exp_eq_exp_iff_exists_int

@[simp]
theorem countable_preimage_exp {s : Set ‚ÑÇ} : (exp ‚Åª¬π' s).Countable ‚Üî s.Countable := by
  refine ‚ü®fun hs => ?_, fun hs => ?_‚ü©
  ¬∑ refine ((hs.image exp).insert 0).mono ?_
    rw [Set.image_preimage_eq_inter_range, range_exp, ‚Üê Set.diff_eq, ‚Üê Set.union_singleton,
        Set.diff_union_self]
    exact Set.subset_union_left _ _
  ¬∑ rw [‚Üê Set.biUnion_preimage_singleton]
    refine hs.biUnion fun z hz => ?_
    rcases em (‚àÉ w, exp w = z) with (‚ü®w, rfl‚ü© | hne)
    ¬∑ simp only [Set.preimage, Set.mem_singleton_iff, exp_eq_exp_iff_exists_int, Set.setOf_exists]
      exact Set.countable_iUnion fun m => Set.countable_singleton _
    ¬∑ push_neg at hne
      simp [Set.preimage, hne]
#align complex.countable_preimage_exp Complex.countable_preimage_exp

alias ‚ü®_, _root_.Set.Countable.preimage_cexp‚ü© := countable_preimage_exp
#align set.countable.preimage_cexp Set.Countable.preimage_cexp

theorem tendsto_log_nhdsWithin_im_neg_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto log (ùìù[{ z : ‚ÑÇ | z.im < 0 }] z) (ùìù <| Real.log (abs z) - œÄ * I) := by
  convert
    (continuous_ofReal.continuousAt.comp_continuousWithinAt
            (continuous_abs.continuousWithinAt.log _)).tendsto.add
      (((continuous_ofReal.tendsto _).comp <|
            tendsto_arg_nhdsWithin_im_neg_of_re_neg_of_im_zero hre him).mul
        tendsto_const_nhds) using 1
  ¬∑ simp [sub_eq_add_neg]
  ¬∑ lift z to ‚Ñù using him
    simpa using hre.ne
#align complex.tendsto_log_nhds_within_im_neg_of_re_neg_of_im_zero Complex.tendsto_log_nhdsWithin_im_neg_of_re_neg_of_im_zero

theorem continuousWithinAt_log_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
    ContinuousWithinAt log { z : ‚ÑÇ | 0 ‚â§ z.im } z := by
  convert
    (continuous_ofReal.continuousAt.comp_continuousWithinAt
            (continuous_abs.continuousWithinAt.log _)).tendsto.add
      ((continuous_ofReal.continuousAt.comp_continuousWithinAt <|
            continuousWithinAt_arg_of_re_neg_of_im_zero hre him).mul
        tendsto_const_nhds) using 1
  lift z to ‚Ñù using him
  simpa using hre.ne
#align complex.continuous_within_at_log_of_re_neg_of_im_zero Complex.continuousWithinAt_log_of_re_neg_of_im_zero

theorem tendsto_log_nhdsWithin_im_nonneg_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto log (ùìù[{ z : ‚ÑÇ | 0 ‚â§ z.im }] z) (ùìù <| Real.log (abs z) + œÄ * I) := by
  simpa only [log, arg_eq_pi_iff.2 ‚ü®hre, him‚ü©] using
    (continuousWithinAt_log_of_re_neg_of_im_zero hre him).tendsto
#align complex.tendsto_log_nhds_within_im_nonneg_of_re_neg_of_im_zero Complex.tendsto_log_nhdsWithin_im_nonneg_of_re_neg_of_im_zero

@[simp]
theorem map_exp_comap_re_atBot : map exp (comap re atBot) = ùìù[‚â†] 0 := by
  rw [‚Üê comap_exp_nhds_zero, map_comap, range_exp, nhdsWithin]
#align complex.map_exp_comap_re_at_bot Complex.map_exp_comap_re_atBot

-- Adaptation note: nightly-2024-04-01
-- The simpNF linter now times out on this lemma.
-- See https://github.com/leanprover-community/mathlib4/issues/12226
@[simp, nolint simpNF]
theorem map_exp_comap_re_atTop : map exp (comap re atTop) = cobounded ‚ÑÇ := by
  rw [‚Üê comap_exp_cobounded, map_comap, range_exp, inf_eq_left, le_principal_iff]
  exact eventually_ne_cobounded _
#align complex.map_exp_comap_re_at_top Complex.map_exp_comap_re_atTop

end Complex

section LogDeriv

open Complex Filter

open Topology

variable {Œ± : Type*}

theorem continuousAt_clog {x : ‚ÑÇ} (h : x ‚àà slitPlane) : ContinuousAt log x := by
  refine ContinuousAt.add ?_ ?_
  ¬∑ refine continuous_ofReal.continuousAt.comp ?_
    refine (Real.continuousAt_log ?_).comp Complex.continuous_abs.continuousAt
    exact Complex.abs.ne_zero_iff.mpr <| slitPlane_ne_zero h
  ¬∑ have h_cont_mul : Continuous fun x : ‚ÑÇ => x * I := continuous_id'.mul continuous_const
    refine h_cont_mul.continuousAt.comp (continuous_ofReal.continuousAt.comp ?_)
    exact continuousAt_arg h
#align continuous_at_clog continuousAt_clog

theorem _root_.Filter.Tendsto.clog {l : Filter Œ±} {f : Œ± ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h : Tendsto f l (ùìù x))
    (hx : x ‚àà slitPlane) : Tendsto (fun t => log (f t)) l (ùìù <| log x) :=
  (continuousAt_clog hx).tendsto.comp h
#align filter.tendsto.clog Filter.Tendsto.clog

variable [TopologicalSpace Œ±]

nonrec
theorem _root_.ContinuousAt.clog {f : Œ± ‚Üí ‚ÑÇ} {x : Œ±} (h‚ÇÅ : ContinuousAt f x)
    (h‚ÇÇ : f x ‚àà slitPlane) : ContinuousAt (fun t => log (f t)) x :=
  h‚ÇÅ.clog h‚ÇÇ
#align continuous_at.clog ContinuousAt.clog

nonrec
theorem _root_.ContinuousWithinAt.clog {f : Œ± ‚Üí ‚ÑÇ} {s : Set Œ±} {x : Œ±}
    (h‚ÇÅ : ContinuousWithinAt f s x) (h‚ÇÇ : f x ‚àà slitPlane) :
    ContinuousWithinAt (fun t => log (f t)) s x :=
  h‚ÇÅ.clog h‚ÇÇ
#align continuous_within_at.clog ContinuousWithinAt.clog

nonrec
theorem _root_.ContinuousOn.clog {f : Œ± ‚Üí ‚ÑÇ} {s : Set Œ±} (h‚ÇÅ : ContinuousOn f s)
    (h‚ÇÇ : ‚àÄ x ‚àà s, f x ‚àà slitPlane) : ContinuousOn (fun t => log (f t)) s := fun x hx =>
  (h‚ÇÅ x hx).clog (h‚ÇÇ x hx)
#align continuous_on.clog ContinuousOn.clog

nonrec
theorem _root_.Continuous.clog {f : Œ± ‚Üí ‚ÑÇ} (h‚ÇÅ : Continuous f)
    (h‚ÇÇ : ‚àÄ x, f x ‚àà slitPlane) : Continuous fun t => log (f t) :=
  continuous_iff_continuousAt.2 fun x => h‚ÇÅ.continuousAt.clog (h‚ÇÇ x)
#align continuous.clog Continuous.clog

end LogDeriv
