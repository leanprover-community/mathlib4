/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Complex.Arg
import Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# The complex `log` function

Basic properties, relationship with `exp`.
-/


noncomputable section

namespace Complex

open Set Filter Bornology

open scoped Real Topology ComplexConjugate

/-- Inverse of the `exp` function. Returns values such that `(log x).im > - Ï€` and `(log x).im â‰¤ Ï€`.
  `log 0 = 0` -/
@[pp_nodot]
noncomputable def log (x : â„‚) : â„‚ :=
  Real.log â€–xâ€– + arg x * I

theorem log_re (x : â„‚) : x.log.re = Real.log â€–xâ€– := by simp [log]

theorem log_im (x : â„‚) : x.log.im = x.arg := by simp [log]

theorem neg_pi_lt_log_im (x : â„‚) : -Ï€ < (log x).im := by simp only [log_im, neg_pi_lt_arg]

theorem log_im_le_pi (x : â„‚) : (log x).im â‰¤ Ï€ := by simp only [log_im, arg_le_pi]

theorem exp_log {x : â„‚} (hx : x â‰  0) : exp (log x) = x := by
  rw [log, exp_add_mul_I, â† ofReal_sin, sin_arg, â† ofReal_cos, cos_arg hx, â† ofReal_exp,
    Real.exp_log (norm_pos_iff.mpr hx), mul_add, ofReal_div, ofReal_div,
    mul_div_cancelâ‚€ _ (ofReal_ne_zero.2 <| norm_ne_zero_iff.mpr hx), â† mul_assoc,
    mul_div_cancelâ‚€ _ (ofReal_ne_zero.2 <| norm_ne_zero_iff.mpr hx), re_add_im]

@[simp]
theorem range_exp : Set.range exp = {0}á¶œ :=
  Set.ext fun x =>
    âŸ¨by
      rintro âŸ¨x, rflâŸ©
      exact exp_ne_zero x, fun hx => âŸ¨log x, exp_log hxâŸ©âŸ©

theorem log_exp {x : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚ : x.im â‰¤ Ï€) : log (exp x) = x := by
  rw [log, norm_exp, Real.log_exp, exp_eq_exp_re_mul_sin_add_cos, â† ofReal_exp,
    arg_mul_cos_add_sin_mul_I (Real.exp_pos _) âŸ¨hxâ‚, hxâ‚‚âŸ©, re_add_im]

theorem exp_inj_of_neg_pi_lt_of_le_pi {x y : â„‚} (hxâ‚ : -Ï€ < x.im) (hxâ‚‚ : x.im â‰¤ Ï€) (hyâ‚ : -Ï€ < y.im)
    (hyâ‚‚ : y.im â‰¤ Ï€) (hxy : exp x = exp y) : x = y := by
  rw [â† log_exp hxâ‚ hxâ‚‚, â† log_exp hyâ‚ hyâ‚‚, hxy]

theorem exp_mul_I_injOn : Set.InjOn (fun Î¸ : â„ â†¦ exp (Î¸ * I)) (Ioc (-Ï€) Ï€) := by
  intro x hx y hy he
  suffices x * I = y * I by aesop
  refine exp_inj_of_neg_pi_lt_of_le_pi ?_ ?_ ?_ ?_ he
  all_goals aesop

theorem ofReal_log {x : â„} (hx : 0 â‰¤ x) : (x.log : â„‚) = log x :=
  Complex.ext (by rw [log_re, ofReal_re, Complex.norm_of_nonneg hx])
    (by rw [ofReal_im, log_im, arg_ofReal_of_nonneg hx])

@[simp, norm_cast]
lemma natCast_log {n : â„•} : Real.log n = log n := ofReal_natCast n â–¸ ofReal_log n.cast_nonneg

@[simp]
lemma ofNat_log {n : â„•} [n.AtLeastTwo] :
    Real.log ofNat(n) = log (OfNat.ofNat n) :=
  natCast_log

theorem log_ofReal_re (x : â„) : (log (x : â„‚)).re = Real.log x := by simp [log_re]

theorem log_ofReal_mul {r : â„} (hr : 0 < r) {x : â„‚} (hx : x â‰  0) :
    log (r * x) = Real.log r + log x := by
  replace hx := norm_ne_zero_iff.mpr hx
  simp_rw [log, norm_mul, norm_real, arg_real_mul _ hr, Real.norm_of_nonneg hr.le,
    Real.log_mul hr.ne' hx, ofReal_add, add_assoc]

theorem log_mul_ofReal (r : â„) (hr : 0 < r) (x : â„‚) (hx : x â‰  0) :
    log (x * r) = Real.log r + log x := by rw [mul_comm, log_ofReal_mul hr hx]

lemma log_mul_eq_add_log_iff {x y : â„‚} (hxâ‚€ : x â‰  0) (hyâ‚€ : y â‰  0) :
    log (x * y) = log x + log y â†” arg x + arg y âˆˆ Set.Ioc (-Ï€) Ï€ := by
  refine Complex.ext_iff.trans <| Iff.trans ?_ <| arg_mul_eq_add_arg_iff hxâ‚€ hyâ‚€
  simp_rw [add_re, add_im, log_re, log_im, norm_mul,
    Real.log_mul (norm_ne_zero_iff.mpr hxâ‚€) (norm_ne_zero_iff.mpr hyâ‚€), true_and]

alias âŸ¨_, log_mulâŸ© := log_mul_eq_add_log_iff

@[simp]
theorem log_zero : log 0 = 0 := by simp [log]

@[simp]
theorem log_one : log 1 = 0 := by simp [log]

/-- This holds true for all `x : â„‚` because of the junk values `0 / 0 = 0` and `log 0 = 0`. -/
@[simp] lemma log_div_self (x : â„‚) : log (x / x) = 0 := by simp [log]

theorem log_neg_one : log (-1) = Ï€ * I := by simp [log]

theorem log_I : log I = Ï€ / 2 * I := by simp [log]

theorem log_neg_I : log (-I) = -(Ï€ / 2) * I := by simp [log]

theorem log_conj_eq_ite (x : â„‚) : log (conj x) = if x.arg = Ï€ then log x else conj (log x) := by
  simp_rw [log, norm_conj, arg_conj, map_add, map_mul, conj_ofReal]
  split_ifs with hx
  Â· rw [hx]
  simp_rw [ofReal_neg, conj_I, mul_neg, neg_mul]

theorem log_conj (x : â„‚) (h : x.arg â‰  Ï€) : log (conj x) = conj (log x) := by
  rw [log_conj_eq_ite, if_neg h]

theorem log_inv_eq_ite (x : â„‚) : log xâ»Â¹ = if x.arg = Ï€ then -conj (log x) else -log x := by
  by_cases hx : x = 0
  Â· simp [hx]
  rw [inv_def, log_mul_ofReal, Real.log_inv, ofReal_neg, â† sub_eq_neg_add, log_conj_eq_ite]
  Â· simp_rw [log, map_add, map_mul, conj_ofReal, conj_I, normSq_eq_norm_sq, Real.log_pow,
      Nat.cast_two, ofReal_mul, neg_add, mul_neg, neg_neg]
    norm_num; rw [two_mul] -- Porting note: added to simplify `â†‘2`
    split_ifs
    Â· rw [add_sub_right_comm, sub_add_cancel_left]
    Â· rw [add_sub_right_comm, sub_add_cancel_left]
  Â· rwa [inv_pos, Complex.normSq_pos]
  Â· rwa [map_ne_zero]

theorem log_inv (x : â„‚) (hx : x.arg â‰  Ï€) : log xâ»Â¹ = -log x := by rw [log_inv_eq_ite, if_neg hx]

theorem two_pi_I_ne_zero : (2 * Ï€ * I : â„‚) â‰  0 := by norm_num [Real.pi_ne_zero, I_ne_zero]

theorem exp_eq_one_iff {x : â„‚} : exp x = 1 â†” âˆƒ n : â„¤, x = n * (2 * Ï€ * I) := by
  constructor
  Â· intro h
    rcases existsUnique_add_zsmul_mem_Ioc Real.two_pi_pos x.im (-Ï€) with âŸ¨n, hn, -âŸ©
    use -n
    rw [Int.cast_neg, neg_mul, eq_neg_iff_add_eq_zero]
    have : (x + n * (2 * Ï€ * I)).im âˆˆ Set.Ioc (-Ï€) Ï€ := by simpa [two_mul, mul_add] using hn
    rw [â† log_exp this.1 this.2, exp_periodic.int_mul n, h, log_one]
  Â· rintro âŸ¨n, rflâŸ©
    exact (exp_periodic.int_mul n).eq.trans exp_zero

theorem exp_eq_exp_iff_exp_sub_eq_one {x y : â„‚} : exp x = exp y â†” exp (x - y) = 1 := by
  rw [exp_sub, div_eq_one_iff_eq (exp_ne_zero _)]

theorem exp_eq_exp_iff_exists_int {x y : â„‚} : exp x = exp y â†” âˆƒ n : â„¤, x = y + n * (2 * Ï€ * I) := by
  simp only [exp_eq_exp_iff_exp_sub_eq_one, exp_eq_one_iff, sub_eq_iff_eq_add']

theorem log_exp_exists (z : â„‚) :
    âˆƒ n : â„¤, log (exp z) = z + n * (2 * Ï€ * I) := by
  rw [â† exp_eq_exp_iff_exists_int, exp_log]
  exact exp_ne_zero z

@[simp]
theorem countable_preimage_exp {s : Set â„‚} : (exp â»Â¹' s).Countable â†” s.Countable := by
  refine âŸ¨fun hs => ?_, fun hs => ?_âŸ©
  Â· refine ((hs.image exp).insert 0).mono ?_
    rw [Set.image_preimage_eq_inter_range, range_exp, â† Set.diff_eq, â† Set.union_singleton,
        Set.diff_union_self]
    exact Set.subset_union_left
  Â· rw [â† Set.biUnion_preimage_singleton]
    refine hs.biUnion fun z hz => ?_
    rcases em (âˆƒ w, exp w = z) with (âŸ¨w, rflâŸ© | hne)
    Â· simp only [Set.preimage, Set.mem_singleton_iff, exp_eq_exp_iff_exists_int, Set.setOf_exists]
      exact Set.countable_iUnion fun m => Set.countable_singleton _
    Â· push_neg at hne
      simp [Set.preimage, hne]

alias âŸ¨_, _root_.Set.Countable.preimage_cexpâŸ© := countable_preimage_exp

theorem tendsto_log_nhdsWithin_im_neg_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto log (ğ“[{ z : â„‚ | z.im < 0 }] z) (ğ“ <| Real.log â€–zâ€– - Ï€ * I) := by
  convert
    (continuous_ofReal.continuousAt.comp_continuousWithinAt
            (continuous_norm.continuousWithinAt.log _)).tendsto.add
      (((continuous_ofReal.tendsto _).comp <|
            tendsto_arg_nhdsWithin_im_neg_of_re_neg_of_im_zero hre him).mul
        tendsto_const_nhds) using 1
  Â· simp [sub_eq_add_neg]
  Â· lift z to â„ using him
    simpa using hre.ne

theorem continuousWithinAt_log_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
    ContinuousWithinAt log { z : â„‚ | 0 â‰¤ z.im } z := by
  convert
    (continuous_ofReal.continuousAt.comp_continuousWithinAt
            (continuous_norm.continuousWithinAt.log _)).tendsto.add
      ((continuous_ofReal.continuousAt.comp_continuousWithinAt <|
            continuousWithinAt_arg_of_re_neg_of_im_zero hre him).mul
        tendsto_const_nhds) using 1
  lift z to â„ using him
  simpa using hre.ne

theorem tendsto_log_nhdsWithin_im_nonneg_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto log (ğ“[{ z : â„‚ | 0 â‰¤ z.im }] z) (ğ“ <| Real.log â€–zâ€– + Ï€ * I) := by
  simpa only [log, arg_eq_pi_iff.2 âŸ¨hre, himâŸ©] using
    (continuousWithinAt_log_of_re_neg_of_im_zero hre him).tendsto

@[simp]
theorem map_exp_comap_re_atBot : map exp (comap re atBot) = ğ“[â‰ ] 0 := by
  rw [â† comap_exp_nhds_zero, map_comap, range_exp, nhdsWithin]

@[simp]
theorem map_exp_comap_re_atTop : map exp (comap re atTop) = cobounded â„‚ := by
  rw [â† comap_exp_cobounded, map_comap, range_exp, inf_eq_left, le_principal_iff]
  exact eventually_ne_cobounded _

end Complex

section LogDeriv

open Complex Filter

open Topology

variable {Î± : Type*}

theorem continuousAt_clog {x : â„‚} (h : x âˆˆ slitPlane) : ContinuousAt log x := by
  refine ContinuousAt.add ?_ ?_
  Â· refine continuous_ofReal.continuousAt.comp ?_
    refine (Real.continuousAt_log ?_).comp continuous_norm.continuousAt
    exact norm_ne_zero_iff.mpr <| slitPlane_ne_zero h
  Â· have h_cont_mul : Continuous fun x : â„‚ => x * I := continuous_id'.mul continuous_const
    refine h_cont_mul.continuousAt.comp (continuous_ofReal.continuousAt.comp ?_)
    exact continuousAt_arg h

theorem _root_.Filter.Tendsto.clog {l : Filter Î±} {f : Î± â†’ â„‚} {x : â„‚} (h : Tendsto f l (ğ“ x))
    (hx : x âˆˆ slitPlane) : Tendsto (fun t => log (f t)) l (ğ“ <| log x) :=
  (continuousAt_clog hx).tendsto.comp h

variable [TopologicalSpace Î±]

nonrec
theorem _root_.ContinuousAt.clog {f : Î± â†’ â„‚} {x : Î±} (hâ‚ : ContinuousAt f x)
    (hâ‚‚ : f x âˆˆ slitPlane) : ContinuousAt (fun t => log (f t)) x :=
  hâ‚.clog hâ‚‚

nonrec
theorem _root_.ContinuousWithinAt.clog {f : Î± â†’ â„‚} {s : Set Î±} {x : Î±}
    (hâ‚ : ContinuousWithinAt f s x) (hâ‚‚ : f x âˆˆ slitPlane) :
    ContinuousWithinAt (fun t => log (f t)) s x :=
  hâ‚.clog hâ‚‚

nonrec
theorem _root_.ContinuousOn.clog {f : Î± â†’ â„‚} {s : Set Î±} (hâ‚ : ContinuousOn f s)
    (hâ‚‚ : âˆ€ x âˆˆ s, f x âˆˆ slitPlane) : ContinuousOn (fun t => log (f t)) s := fun x hx =>
  (hâ‚ x hx).clog (hâ‚‚ x hx)

nonrec
theorem _root_.Continuous.clog {f : Î± â†’ â„‚} (hâ‚ : Continuous f)
    (hâ‚‚ : âˆ€ x, f x âˆˆ slitPlane) : Continuous fun t => log (f t) :=
  continuous_iff_continuousAt.2 fun x => hâ‚.continuousAt.clog (hâ‚‚ x)

end LogDeriv

section tsum_tprod

variable {Î± Î¹ : Type*}

open Real

lemma Real.hasProd_of_hasSum_log {f : Î¹ â†’ â„} (hfn : âˆ€ n, 0 < f n) {a : â„}
    (hf : HasSum (fun n => log (f n)) a) : HasProd f (rexp a) :=
  hf.rexp.congr (by simp [exp_log, hfn])

lemma Real.multipliable_of_summable_log (f : Î¹ â†’ â„) (hfn : âˆ€ n, 0 < f n)
    (hf : Summable fun n => log (f n)) : Multipliable f :=
  âŸ¨_, Real.hasProd_of_hasSum_log hfn hf.hasSumâŸ©

/-- The exponential of a infinite sum of real logs (which converges absolutely) is an infinite
product. -/
lemma Real.rexp_tsum_eq_tprod (f : Î¹ â†’ â„) (hfn : âˆ€ n, 0 < f n)
    (hf : Summable fun n => log (f n)) : rexp (âˆ‘' n : Î¹, log (f n)) = âˆ' n : Î¹, f n :=
  (Real.hasProd_of_hasSum_log hfn hf.hasSum).tprod_eq.symm

open Complex

lemma Complex.hasProd_of_hasSum_log (f : Î¹ â†’ â„‚) (hfn : âˆ€ n, f n â‰  0) {a : â„‚}
    (hf : HasSum (fun n => log (f n)) a) : HasProd (fun b â†¦ f b) (cexp a) :=
    hf.cexp.congr (by simp [exp_log, hfn])

lemma Complex.multipliable_of_summable_log (f : Î¹ â†’ â„‚) (hfn : âˆ€ n, f n â‰  0)
    (hf : Summable fun n => log (f n)) : Multipliable fun b â†¦ f b  :=
   âŸ¨_, Complex.hasProd_of_hasSum_log _ hfn hf.hasSumâŸ©

/-- The exponential of a infinite sum of comples logs (which converges absolutely) is an infinite
product. -/
lemma Complex.cexp_tsum_eq_tprod (f : Î¹ â†’  â„‚) (hfn : âˆ€ n, f n â‰  0)
    (hf : Summable fun n => log (f n)) : (cexp ((âˆ‘' n : Î¹, log (f n )))) = âˆ' n : Î¹, f n  :=
  (Complex.hasProd_of_hasSum_log _ hfn hf.hasSum).tprod_eq.symm

end tsum_tprod

open Complex in
theorem Set.Countable.preimage_circleMap {s : Set â„‚} (hs : s.Countable) (c : â„‚) {R : â„}
    (hR : R â‰  0) : (circleMap c R â»Â¹' s).Countable :=
  show (((â†‘) : â„ â†’ â„‚) â»Â¹' ((Â· * I) â»Â¹'
      (exp â»Â¹' ((R * Â·) â»Â¹' ((c + Â·) â»Â¹' s))))).Countable from
    (((hs.preimage (add_right_injective _)).preimage <|
      mul_right_injectiveâ‚€ <| ofReal_ne_zero.2 hR).preimage_cexp.preimage <|
        mul_left_injectiveâ‚€ I_ne_zero).preimage ofReal_injective
