/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Angle
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Inverse

/-!
# The argument of a complex number.

We define `arg : ‚ÑÇ ‚Üí ‚Ñù`, returning a real number in the range (-œÄ, œÄ],
such that for `x ‚â† 0`, `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
while `arg 0` defaults to `0`
-/

open Filter Metric Set
open scoped ComplexConjugate Real Topology

namespace Complex
variable {a x z : ‚ÑÇ}

/-- `arg` returns values in the range (-œÄ, œÄ], such that for `x ‚â† 0`,
  `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
  `arg 0` defaults to `0` -/
noncomputable def arg (x : ‚ÑÇ) : ‚Ñù :=
  if 0 ‚â§ x.re then Real.arcsin (x.im / ‚Äñx‚Äñ)
  else if 0 ‚â§ x.im then Real.arcsin ((-x).im / ‚Äñx‚Äñ) + œÄ else Real.arcsin ((-x).im / ‚Äñx‚Äñ) - œÄ

theorem sin_arg (x : ‚ÑÇ) : Real.sin (arg x) = x.im / ‚Äñx‚Äñ := by
  unfold arg; split_ifs <;>
    simp [sub_eq_add_neg, arg, Real.sin_arcsin (abs_le.1 (abs_im_div_norm_le_one x)).1
      (abs_le.1 (abs_im_div_norm_le_one x)).2, Real.sin_add, neg_div, Real.arcsin_neg, Real.sin_neg]

theorem cos_arg {x : ‚ÑÇ} (hx : x ‚â† 0) : Real.cos (arg x) = x.re / ‚Äñx‚Äñ := by
  rw [arg]
  split_ifs with h‚ÇÅ h‚ÇÇ
  ¬∑ rw [Real.cos_arcsin]
    field_simp [Real.sqrt_sq, (norm_pos_iff.mpr hx).le, *]
  ¬∑ rw [Real.cos_add_pi, Real.cos_arcsin]
    field_simp [Real.sqrt_div (sq_nonneg _), Real.sqrt_sq_eq_abs,
      _root_.abs_of_neg (not_le.1 h‚ÇÅ), *]
  ¬∑ rw [Real.cos_sub_pi, Real.cos_arcsin]
    field_simp [Real.sqrt_div (sq_nonneg _), Real.sqrt_sq_eq_abs,
      _root_.abs_of_neg (not_le.1 h‚ÇÅ), *]

@[simp]
theorem norm_mul_exp_arg_mul_I (x : ‚ÑÇ) : ‚Äñx‚Äñ * exp (arg x * I) = x := by
  rcases eq_or_ne x 0 with (rfl | hx)
  ¬∑ simp
  ¬∑ have : ‚Äñx‚Äñ ‚â† 0 := norm_ne_zero_iff.mpr hx
    apply Complex.ext <;> field_simp [sin_arg, cos_arg hx, this, mul_comm ‚Äñx‚Äñ]

@[simp]
theorem norm_mul_cos_add_sin_mul_I (x : ‚ÑÇ) : (‚Äñx‚Äñ * (cos (arg x) + sin (arg x) * I) : ‚ÑÇ) = x := by
  rw [‚Üê exp_mul_I, norm_mul_exp_arg_mul_I]

@[simp]
lemma norm_mul_cos_arg (x : ‚ÑÇ) : ‚Äñx‚Äñ * Real.cos (arg x) = x.re := by
  simpa [-norm_mul_cos_add_sin_mul_I] using congr_arg re (norm_mul_cos_add_sin_mul_I x)

@[simp]
lemma norm_mul_sin_arg (x : ‚ÑÇ) : ‚Äñx‚Äñ * Real.sin (arg x) = x.im := by
  simpa [-norm_mul_cos_add_sin_mul_I] using congr_arg im (norm_mul_cos_add_sin_mul_I x)

theorem norm_eq_one_iff (z : ‚ÑÇ) : ‚Äñz‚Äñ = 1 ‚Üî ‚àÉ Œ∏ : ‚Ñù, exp (Œ∏ * I) = z := by
  refine ‚ü®fun hz => ‚ü®arg z, ?_‚ü©, ?_‚ü©
  ¬∑ calc
      exp (arg z * I) = ‚Äñz‚Äñ * exp (arg z * I) := by rw [hz, ofReal_one, one_mul]
      _ = z :=norm_mul_exp_arg_mul_I z
  ¬∑ rintro ‚ü®Œ∏, rfl‚ü©
    exact Complex.norm_exp_ofReal_mul_I Œ∏

@[deprecated (since := "2025-02-16")] alias abs_mul_exp_arg_mul_I := norm_mul_exp_arg_mul_I
@[deprecated (since := "2025-02-16")] alias abs_mul_cos_add_sin_mul_I := norm_mul_cos_add_sin_mul_I
@[deprecated (since := "2025-02-16")] alias abs_mul_cos_arg := norm_mul_cos_arg
@[deprecated (since := "2025-02-16")] alias abs_mul_sin_arg := norm_mul_sin_arg
@[deprecated (since := "2025-02-16")] alias abs_eq_one_iff := norm_eq_one_iff

@[simp]
theorem range_exp_mul_I : (Set.range fun x : ‚Ñù => exp (x * I)) = Metric.sphere 0 1 := by
  ext x
  simp only [mem_sphere_zero_iff_norm, norm_eq_one_iff, Set.mem_range]

theorem arg_mul_cos_add_sin_mul_I {r : ‚Ñù} (hr : 0 < r) {Œ∏ : ‚Ñù} (hŒ∏ : Œ∏ ‚àà Set.Ioc (-œÄ) œÄ) :
    arg (r * (cos Œ∏ + sin Œ∏ * I)) = Œ∏ := by
  simp only [arg, norm_mul, norm_cos_add_sin_mul_I, Complex.norm_of_nonneg hr.le, mul_one]
  simp only [re_ofReal_mul, im_ofReal_mul, neg_im, ‚Üê ofReal_cos, ‚Üê ofReal_sin, ‚Üê
    mk_eq_add_mul_I, neg_div, mul_div_cancel_left‚ÇÄ _ hr.ne', mul_nonneg_iff_right_nonneg_of_pos hr]
  by_cases h‚ÇÅ : Œ∏ ‚àà Set.Icc (-(œÄ / 2)) (œÄ / 2)
  ¬∑ rw [if_pos]
    exacts [Real.arcsin_sin' h‚ÇÅ, Real.cos_nonneg_of_mem_Icc h‚ÇÅ]
  ¬∑ rw [Set.mem_Icc, not_and_or, not_le, not_le] at h‚ÇÅ
    rcases h‚ÇÅ with h‚ÇÅ | h‚ÇÅ
    ¬∑ replace hŒ∏ := hŒ∏.1
      have hcos : Real.cos Œ∏ < 0 := by
        rw [‚Üê neg_pos, ‚Üê Real.cos_add_pi]
        refine Real.cos_pos_of_mem_Ioo ‚ü®?_, ?_‚ü© <;> linarith
      have hsin : Real.sin Œ∏ < 0 := Real.sin_neg_of_neg_of_neg_pi_lt (by linarith) hŒ∏
      rw [if_neg, if_neg, ‚Üê Real.sin_add_pi, Real.arcsin_sin, add_sub_cancel_right] <;> [linarith;
        linarith; exact hsin.not_ge; exact hcos.not_ge]
    ¬∑ replace hŒ∏ := hŒ∏.2
      have hcos : Real.cos Œ∏ < 0 := Real.cos_neg_of_pi_div_two_lt_of_lt h‚ÇÅ (by linarith)
      have hsin : 0 ‚â§ Real.sin Œ∏ := Real.sin_nonneg_of_mem_Icc ‚ü®by linarith, hŒ∏‚ü©
      rw [if_neg, if_pos, ‚Üê Real.sin_sub_pi, Real.arcsin_sin, sub_add_cancel] <;> [linarith;
        linarith; exact hsin; exact hcos.not_ge]

theorem arg_cos_add_sin_mul_I {Œ∏ : ‚Ñù} (hŒ∏ : Œ∏ ‚àà Set.Ioc (-œÄ) œÄ) : arg (cos Œ∏ + sin Œ∏ * I) = Œ∏ := by
  rw [‚Üê one_mul (_ + _), ‚Üê ofReal_one, arg_mul_cos_add_sin_mul_I zero_lt_one hŒ∏]

lemma arg_exp_mul_I (Œ∏ : ‚Ñù) :
    arg (exp (Œ∏ * I)) = toIocMod (mul_pos two_pos Real.pi_pos) (-œÄ) Œ∏ := by
  convert arg_cos_add_sin_mul_I (Œ∏ := toIocMod (mul_pos two_pos Real.pi_pos) (-œÄ) Œ∏) _ using 2
  ¬∑ rw [‚Üê exp_mul_I, eq_sub_of_add_eq <| toIocMod_add_toIocDiv_zsmul _ _ Œ∏, ofReal_sub,
      ofReal_zsmul, ofReal_mul, ofReal_ofNat, exp_mul_I_periodic.sub_zsmul_eq]
  ¬∑ convert toIocMod_mem_Ioc _ _ _
    ring

@[simp]
theorem arg_zero : arg 0 = 0 := by simp [arg, le_refl]

theorem ext_norm_arg {x y : ‚ÑÇ} (h‚ÇÅ : ‚Äñx‚Äñ = ‚Äñy‚Äñ) (h‚ÇÇ : x.arg = y.arg) : x = y := by
  rw [‚Üê norm_mul_exp_arg_mul_I x, ‚Üê norm_mul_exp_arg_mul_I y, h‚ÇÅ, h‚ÇÇ]

theorem ext_norm_arg_iff {x y : ‚ÑÇ} : x = y ‚Üî ‚Äñx‚Äñ = ‚Äñy‚Äñ ‚àß arg x = arg y :=
  ‚ü®fun h => h ‚ñ∏ ‚ü®rfl, rfl‚ü©, and_imp.2 ext_norm_arg‚ü©

@[deprecated (since := "2025-02-16")] alias ext_abs_arg := ext_norm_arg
@[deprecated (since := "2025-02-16")] alias ext_abs_arg_iff := ext_norm_arg_iff

theorem arg_mem_Ioc (z : ‚ÑÇ) : arg z ‚àà Set.Ioc (-œÄ) œÄ := by
  have hœÄ : 0 < œÄ := Real.pi_pos
  rcases eq_or_ne z 0 with (rfl | hz)
  ¬∑ simp [hœÄ, hœÄ.le]
  rcases existsUnique_add_zsmul_mem_Ioc Real.two_pi_pos (arg z) (-œÄ) with ‚ü®N, hN, -‚ü©
  rw [two_mul, neg_add_cancel_left, ‚Üê two_mul, zsmul_eq_mul] at hN
  rw [‚Üê norm_mul_cos_add_sin_mul_I z, ‚Üê cos_add_int_mul_two_pi _ N, ‚Üê sin_add_int_mul_two_pi _ N]
  have := arg_mul_cos_add_sin_mul_I (norm_pos_iff.mpr hz) hN
  push_cast at this
  rwa [this]

@[simp]
theorem range_arg : Set.range arg = Set.Ioc (-œÄ) œÄ :=
  (Set.range_subset_iff.2 arg_mem_Ioc).antisymm fun _ hx => ‚ü®_, arg_cos_add_sin_mul_I hx‚ü©

theorem arg_le_pi (x : ‚ÑÇ) : arg x ‚â§ œÄ :=
  (arg_mem_Ioc x).2

theorem neg_pi_lt_arg (x : ‚ÑÇ) : -œÄ < arg x :=
  (arg_mem_Ioc x).1

theorem abs_arg_le_pi (z : ‚ÑÇ) : |arg z| ‚â§ œÄ :=
  abs_le.2 ‚ü®(neg_pi_lt_arg z).le, arg_le_pi z‚ü©

@[simp]
theorem arg_nonneg_iff {z : ‚ÑÇ} : 0 ‚â§ arg z ‚Üî 0 ‚â§ z.im := by
  rcases eq_or_ne z 0 with (rfl | h‚ÇÄ); ¬∑ simp
  calc
    0 ‚â§ arg z ‚Üî 0 ‚â§ Real.sin (arg z) :=
      ‚ü®fun h => Real.sin_nonneg_of_mem_Icc ‚ü®h, arg_le_pi z‚ü©, by
        contrapose!
        intro h
        exact Real.sin_neg_of_neg_of_neg_pi_lt h (neg_pi_lt_arg _)‚ü©
    _ ‚Üî _ := by rw [sin_arg, le_div_iff‚ÇÄ (norm_pos_iff.mpr h‚ÇÄ), zero_mul]

@[simp]
theorem arg_neg_iff {z : ‚ÑÇ} : arg z < 0 ‚Üî z.im < 0 :=
  lt_iff_lt_of_le_iff_le arg_nonneg_iff

theorem arg_real_mul (x : ‚ÑÇ) {r : ‚Ñù} (hr : 0 < r) : arg (r * x) = arg x := by
  rcases eq_or_ne x 0 with (rfl | hx); ¬∑ rw [mul_zero]
  conv_lhs =>
    rw [‚Üê norm_mul_cos_add_sin_mul_I x, ‚Üê mul_assoc, ‚Üê ofReal_mul,
      arg_mul_cos_add_sin_mul_I (mul_pos hr (norm_pos_iff.mpr hx)) x.arg_mem_Ioc]

theorem arg_mul_real {r : ‚Ñù} (hr : 0 < r) (x : ‚ÑÇ) : arg (x * r) = arg x :=
  mul_comm x r ‚ñ∏ arg_real_mul x hr

theorem arg_eq_arg_iff {x y : ‚ÑÇ} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    arg x = arg y ‚Üî (‚Äñy‚Äñ / ‚Äñx‚Äñ : ‚ÑÇ) * x = y := by
  simp only [ext_norm_arg_iff, norm_mul, norm_div, norm_real, norm_norm,
    div_mul_cancel‚ÇÄ _ (norm_ne_zero_iff.mpr hx), eq_self_iff_true, true_and]
  rw [‚Üê ofReal_div, arg_real_mul]
  exact div_pos (norm_pos_iff.mpr hy) (norm_pos_iff.mpr hx)

@[simp] lemma arg_one : arg 1 = 0 := by simp [arg, zero_le_one]

/-- This holds true for all `x : ‚ÑÇ` because of the junk values `0 / 0 = 0` and `arg 0 = 0`. -/
@[simp] lemma arg_div_self (x : ‚ÑÇ) : arg (x / x) = 0 := by
  obtain rfl | hx := eq_or_ne x 0 <;> simp [*]

@[simp]
theorem arg_neg_one : arg (-1) = œÄ := by simp [arg, le_refl, not_le.2 (zero_lt_one' ‚Ñù)]

@[simp]
theorem arg_I : arg I = œÄ / 2 := by simp [arg, le_refl]

@[simp]
theorem arg_neg_I : arg (-I) = -(œÄ / 2) := by simp [arg, le_refl]

@[simp]
theorem tan_arg (x : ‚ÑÇ) : Real.tan (arg x) = x.im / x.re := by
  by_cases h : x = 0
  ¬∑ simp only [h, zero_div, Complex.zero_im, Complex.arg_zero, Real.tan_zero, Complex.zero_re]
  rw [Real.tan_eq_sin_div_cos, sin_arg, cos_arg h,
    div_div_div_cancel_right‚ÇÄ (norm_ne_zero_iff.mpr h)]

theorem arg_ofReal_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) : arg x = 0 := by simp [arg, hx]

@[simp, norm_cast]
lemma natCast_arg {n : ‚Ñï} : arg n = 0 :=
  ofReal_natCast n ‚ñ∏ arg_ofReal_of_nonneg n.cast_nonneg

@[simp]
lemma ofNat_arg {n : ‚Ñï} [n.AtLeastTwo] : arg ofNat(n) = 0 :=
  natCast_arg

theorem arg_eq_zero_iff {z : ‚ÑÇ} : arg z = 0 ‚Üî 0 ‚â§ z.re ‚àß z.im = 0 := by
  refine ‚ü®fun h => ?_, ?_‚ü©
  ¬∑ rw [‚Üê norm_mul_cos_add_sin_mul_I z, h]
    simp [norm_nonneg]
  ¬∑ obtain ‚ü®x, y‚ü© := z
    rintro ‚ü®h, rfl : y = 0‚ü©
    exact arg_ofReal_of_nonneg h

open ComplexOrder in
lemma arg_eq_zero_iff_zero_le {z : ‚ÑÇ} : arg z = 0 ‚Üî 0 ‚â§ z := by
  rw [arg_eq_zero_iff, eq_comm, nonneg_iff]

theorem arg_eq_pi_iff {z : ‚ÑÇ} : arg z = œÄ ‚Üî z.re < 0 ‚àß z.im = 0 := by
  by_cases h‚ÇÄ : z = 0
  ¬∑ simp [h‚ÇÄ, lt_irrefl, Real.pi_ne_zero.symm]
  constructor
  ¬∑ intro h
    rw [‚Üê norm_mul_cos_add_sin_mul_I z, h]
    simp [h‚ÇÄ]
  ¬∑ obtain ‚ü®x, y‚ü© := z
    rintro ‚ü®h : x < 0, rfl : y = 0‚ü©
    rw [‚Üê arg_neg_one, ‚Üê arg_real_mul (-1) (neg_pos.2 h)]
    simp [‚Üê ofReal_def]

open ComplexOrder in
lemma arg_eq_pi_iff_lt_zero {z : ‚ÑÇ} : arg z = œÄ ‚Üî z < 0 := arg_eq_pi_iff

theorem arg_lt_pi_iff {z : ‚ÑÇ} : arg z < œÄ ‚Üî 0 ‚â§ z.re ‚à® z.im ‚â† 0 := by
  rw [(arg_le_pi z).lt_iff_ne, not_iff_comm, not_or, not_le, Classical.not_not, arg_eq_pi_iff]

theorem arg_ofReal_of_neg {x : ‚Ñù} (hx : x < 0) : arg x = œÄ :=
  arg_eq_pi_iff.2 ‚ü®hx, rfl‚ü©

theorem arg_eq_pi_div_two_iff {z : ‚ÑÇ} : arg z = œÄ / 2 ‚Üî z.re = 0 ‚àß 0 < z.im := by
  by_cases h‚ÇÄ : z = 0; ¬∑ simp [h‚ÇÄ, lt_irrefl, Real.pi_div_two_pos.ne]
  constructor
  ¬∑ intro h
    rw [‚Üê norm_mul_cos_add_sin_mul_I z, h]
    simp [h‚ÇÄ]
  ¬∑ obtain ‚ü®x, y‚ü© := z
    rintro ‚ü®rfl : x = 0, hy : 0 < y‚ü©
    rw [‚Üê arg_I, ‚Üê arg_real_mul I hy, ofReal_mul', I_re, I_im, mul_zero, mul_one]

theorem arg_eq_neg_pi_div_two_iff {z : ‚ÑÇ} : arg z = -(œÄ / 2) ‚Üî z.re = 0 ‚àß z.im < 0 := by
  by_cases h‚ÇÄ : z = 0; ¬∑ simp [h‚ÇÄ, lt_irrefl, Real.pi_ne_zero]
  constructor
  ¬∑ intro h
    rw [‚Üê norm_mul_cos_add_sin_mul_I z, h]
    simp [h‚ÇÄ]
  ¬∑ obtain ‚ü®x, y‚ü© := z
    rintro ‚ü®rfl : x = 0, hy : y < 0‚ü©
    rw [‚Üê arg_neg_I, ‚Üê arg_real_mul (-I) (neg_pos.2 hy), mk_eq_add_mul_I]
    simp

theorem arg_of_re_nonneg {x : ‚ÑÇ} (hx : 0 ‚â§ x.re) : arg x = Real.arcsin (x.im / ‚Äñx‚Äñ) :=
  if_pos hx

theorem arg_of_re_neg_of_im_nonneg {x : ‚ÑÇ} (hx_re : x.re < 0) (hx_im : 0 ‚â§ x.im) :
    arg x = Real.arcsin ((-x).im / ‚Äñx‚Äñ) + œÄ := by
  simp only [arg, hx_re.not_ge, hx_im, if_true, if_false]

theorem arg_of_re_neg_of_im_neg {x : ‚ÑÇ} (hx_re : x.re < 0) (hx_im : x.im < 0) :
    arg x = Real.arcsin ((-x).im / ‚Äñx‚Äñ) - œÄ := by
  simp only [arg, hx_re.not_ge, hx_im.not_ge, if_false]

theorem arg_of_im_nonneg_of_ne_zero {z : ‚ÑÇ} (h‚ÇÅ : 0 ‚â§ z.im) (h‚ÇÇ : z ‚â† 0) :
    arg z = Real.arccos (z.re / ‚Äñz‚Äñ) := by
  rw [‚Üê cos_arg h‚ÇÇ, Real.arccos_cos (arg_nonneg_iff.2 h‚ÇÅ) (arg_le_pi _)]

theorem arg_of_im_pos {z : ‚ÑÇ} (hz : 0 < z.im) : arg z = Real.arccos (z.re / ‚Äñz‚Äñ) :=
  arg_of_im_nonneg_of_ne_zero hz.le fun h => hz.ne' <| h.symm ‚ñ∏ rfl

theorem arg_of_im_neg {z : ‚ÑÇ} (hz : z.im < 0) : arg z = -Real.arccos (z.re / ‚Äñz‚Äñ) := by
  have h‚ÇÄ : z ‚â† 0 := mt (congr_arg im) hz.ne
  rw [‚Üê cos_arg h‚ÇÄ, ‚Üê Real.cos_neg, Real.arccos_cos, neg_neg]
  exacts [neg_nonneg.2 (arg_neg_iff.2 hz).le, neg_le.2 (neg_pi_lt_arg z).le]

theorem arg_conj (x : ‚ÑÇ) : arg (conj x) = if arg x = œÄ then œÄ else -arg x := by
  simp_rw [arg_eq_pi_iff, arg, neg_im, conj_im, conj_re, norm_conj, neg_div, neg_neg,
    Real.arcsin_neg]
  rcases lt_trichotomy x.re 0 with (hr | hr | hr) <;>
    rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  ¬∑ simp [hr, hr.not_ge, hi.le, hi.ne, not_le.2 hi, add_comm]
  ¬∑ simp [hr, hr.not_ge, hi]
  ¬∑ simp [hr, hr.not_ge, hi.ne.symm, hi.le, not_le.2 hi, sub_eq_neg_add]
  ¬∑ simp [hr]
  ¬∑ simp [hr]
  ¬∑ simp [hr]
  ¬∑ simp [hr, hr.le, hi.ne]
  ¬∑ simp [hr, hr.le, hr.le.not_gt]
  ¬∑ simp [hr, hr.le, hr.le.not_gt]

theorem arg_inv (x : ‚ÑÇ) : arg x‚Åª¬π = if arg x = œÄ then œÄ else -arg x := by
  rw [‚Üê arg_conj, inv_def, mul_comm]
  by_cases hx : x = 0
  ¬∑ simp [hx]
  ¬∑ exact arg_real_mul (conj x) (by simp [hx])

@[simp] lemma abs_arg_inv (x : ‚ÑÇ) : |x‚Åª¬π.arg| = |x.arg| := by rw [arg_inv]; split_ifs <;> simp [*]

-- TODO: Replace the next two lemmas by general facts about periodic functions
lemma norm_eq_one_iff' : ‚Äñx‚Äñ = 1 ‚Üî ‚àÉ Œ∏ ‚àà Set.Ioc (-œÄ) œÄ, exp (Œ∏ * I) = x := by
  rw [norm_eq_one_iff]
  constructor
  ¬∑ rintro ‚ü®Œ∏, rfl‚ü©
    refine ‚ü®toIocMod (mul_pos two_pos Real.pi_pos) (-œÄ) Œ∏, ?_, ?_‚ü©
    ¬∑ convert toIocMod_mem_Ioc _ _ _
      ring
    ¬∑ rw [eq_sub_of_add_eq <| toIocMod_add_toIocDiv_zsmul _ _ Œ∏, ofReal_sub,
      ofReal_zsmul, ofReal_mul, ofReal_ofNat, exp_mul_I_periodic.sub_zsmul_eq]
  ¬∑ rintro ‚ü®Œ∏, _, rfl‚ü©
    exact ‚ü®Œ∏, rfl‚ü©

@[deprecated (since := "2025-02-16")] alias abs_eq_one_iff' := norm_eq_one_iff'

lemma image_exp_Ioc_eq_sphere : (fun Œ∏ : ‚Ñù ‚Ü¶ exp (Œ∏ * I)) '' Set.Ioc (-œÄ) œÄ = sphere 0 1 := by
  ext; simpa using norm_eq_one_iff'.symm

theorem arg_le_pi_div_two_iff {z : ‚ÑÇ} : arg z ‚â§ œÄ / 2 ‚Üî 0 ‚â§ re z ‚à® im z < 0 := by
  rcases le_or_gt 0 (re z) with hre | hre
  ¬∑ simp only [hre, arg_of_re_nonneg hre, Real.arcsin_le_pi_div_two, true_or]
  simp only [hre.not_ge, false_or]
  rcases le_or_gt 0 (im z) with him | him
  ¬∑ simp only [him.not_gt]
    rw [iff_false, not_le, arg_of_re_neg_of_im_nonneg hre him, ‚Üê sub_lt_iff_lt_add, half_sub,
      Real.neg_pi_div_two_lt_arcsin, neg_im, neg_div, neg_lt_neg_iff, div_lt_one, ‚Üê
      abs_of_nonneg him, abs_im_lt_norm]
    exacts [hre.ne, norm_pos_iff.mpr <| ne_of_apply_ne re hre.ne]
  ¬∑ simp only [him]
    rw [iff_true, arg_of_re_neg_of_im_neg hre him]
    exact (sub_le_self _ Real.pi_pos.le).trans (Real.arcsin_le_pi_div_two _)

theorem neg_pi_div_two_le_arg_iff {z : ‚ÑÇ} : -(œÄ / 2) ‚â§ arg z ‚Üî 0 ‚â§ re z ‚à® 0 ‚â§ im z := by
  rcases le_or_gt 0 (re z) with hre | hre
  ¬∑ simp only [hre, arg_of_re_nonneg hre, Real.neg_pi_div_two_le_arcsin, true_or]
  simp only [hre.not_ge, false_or]
  rcases le_or_gt 0 (im z) with him | him
  ¬∑ simp only [him]
    rw [iff_true, arg_of_re_neg_of_im_nonneg hre him]
    exact (Real.neg_pi_div_two_le_arcsin _).trans (le_add_of_nonneg_right Real.pi_pos.le)
  ¬∑ simp only [him.not_ge]
    rw [iff_false, not_le, arg_of_re_neg_of_im_neg hre him, sub_lt_iff_lt_add', ‚Üê
      sub_eq_add_neg, sub_half, Real.arcsin_lt_pi_div_two, div_lt_one, neg_im, ‚Üê abs_of_neg him,
      abs_im_lt_norm]
    exacts [hre.ne, norm_pos_iff.mpr <| ne_of_apply_ne re hre.ne]

lemma neg_pi_div_two_lt_arg_iff {z : ‚ÑÇ} : -(œÄ / 2) < arg z ‚Üî 0 < re z ‚à® 0 ‚â§ im z := by
  rw [lt_iff_le_and_ne, neg_pi_div_two_le_arg_iff, ne_comm, Ne, arg_eq_neg_pi_div_two_iff]
  rcases lt_trichotomy z.re 0 with hre | hre | hre
  ¬∑ simp [hre.ne, hre.not_ge, hre.not_gt]
  ¬∑ simp [hre]
  ¬∑ simp [hre, hre.le, hre.ne']

lemma arg_lt_pi_div_two_iff {z : ‚ÑÇ} : arg z < œÄ / 2 ‚Üî 0 < re z ‚à® im z < 0 ‚à® z = 0 := by
  rw [lt_iff_le_and_ne, arg_le_pi_div_two_iff, Ne, arg_eq_pi_div_two_iff]
  rcases lt_trichotomy z.re 0 with hre | hre | hre
  ¬∑ have : z ‚â† 0 := by simp [Complex.ext_iff, hre.ne]
    simp [hre.ne, hre.not_ge, hre.not_gt, this]
  ¬∑ have : z = 0 ‚Üî z.im = 0 := by simp [Complex.ext_iff, hre]
    simp [hre, this, or_comm, le_iff_eq_or_lt]
  ¬∑ simp [hre, hre.le, hre.ne']

@[simp]
theorem abs_arg_le_pi_div_two_iff {z : ‚ÑÇ} : |arg z| ‚â§ œÄ / 2 ‚Üî 0 ‚â§ re z := by
  rw [abs_le, arg_le_pi_div_two_iff, neg_pi_div_two_le_arg_iff, ‚Üê or_and_left, ‚Üê not_le,
    and_not_self_iff, or_false]

@[simp]
theorem abs_arg_lt_pi_div_two_iff {z : ‚ÑÇ} : |arg z| < œÄ / 2 ‚Üî 0 < re z ‚à® z = 0 := by
  rw [abs_lt, arg_lt_pi_div_two_iff, neg_pi_div_two_lt_arg_iff, ‚Üê or_and_left]
  rcases eq_or_ne z 0 with hz | hz
  ¬∑ simp [hz]
  ¬∑ simp_rw [hz, or_false, ‚Üê not_lt, not_and_self_iff, or_false]

@[simp]
theorem arg_conj_coe_angle (x : ‚ÑÇ) : (arg (conj x) : Real.Angle) = -arg x := by
  by_cases h : arg x = œÄ <;> simp [arg_conj, h]

@[simp]
theorem arg_inv_coe_angle (x : ‚ÑÇ) : (arg x‚Åª¬π : Real.Angle) = -arg x := by
  by_cases h : arg x = œÄ <;> simp [arg_inv, h]

theorem arg_neg_eq_arg_sub_pi_of_im_pos {x : ‚ÑÇ} (hi : 0 < x.im) : arg (-x) = arg x - œÄ := by
  rw [arg_of_im_pos hi, arg_of_im_neg (show (-x).im < 0 from Left.neg_neg_iff.2 hi)]
  simp [neg_div, Real.arccos_neg]

theorem arg_neg_eq_arg_add_pi_of_im_neg {x : ‚ÑÇ} (hi : x.im < 0) : arg (-x) = arg x + œÄ := by
  rw [arg_of_im_neg hi, arg_of_im_pos (show 0 < (-x).im from Left.neg_pos_iff.2 hi)]
  simp [neg_div, Real.arccos_neg, add_comm, ‚Üê sub_eq_add_neg]

theorem arg_neg_eq_arg_sub_pi_iff {x : ‚ÑÇ} :
    arg (-x) = arg x - œÄ ‚Üî 0 < x.im ‚à® x.im = 0 ‚àß x.re < 0 := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  ¬∑ simp [hi, hi.ne, hi.not_gt, arg_neg_eq_arg_add_pi_of_im_neg, sub_eq_add_neg, ‚Üê
      add_eq_zero_iff_eq_neg, Real.pi_ne_zero]
  ¬∑ rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    ¬∑ rw [arg_ofReal_of_neg hr, ‚Üê ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le]
      simp [hr]
    ¬∑ simp [hr, hi, Real.pi_ne_zero]
    ¬∑ rw [arg_ofReal_of_nonneg hr.le, ‚Üê ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr)]
      simp [hr.not_gt, ‚Üê add_eq_zero_iff_eq_neg, Real.pi_ne_zero]
  ¬∑ simp [hi, arg_neg_eq_arg_sub_pi_of_im_pos]

theorem arg_neg_eq_arg_add_pi_iff {x : ‚ÑÇ} :
    arg (-x) = arg x + œÄ ‚Üî x.im < 0 ‚à® x.im = 0 ‚àß 0 < x.re := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  ¬∑ simp [hi, arg_neg_eq_arg_add_pi_of_im_neg]
  ¬∑ rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    ¬∑ rw [arg_ofReal_of_neg hr, ‚Üê ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le]
      simp [hr.not_gt, ‚Üê two_mul, Real.pi_ne_zero]
    ¬∑ simp [hr, hi, Real.pi_ne_zero.symm]
    ¬∑ rw [arg_ofReal_of_nonneg hr.le, ‚Üê ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr)]
      simp [hr]
  ¬∑ simp [hi, hi.ne.symm, hi.not_gt, arg_neg_eq_arg_sub_pi_of_im_pos, sub_eq_add_neg, ‚Üê
      add_eq_zero_iff_neg_eq, Real.pi_ne_zero]

theorem arg_neg_coe_angle {x : ‚ÑÇ} (hx : x ‚â† 0) : (arg (-x) : Real.Angle) = arg x + œÄ := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  ¬∑ rw [arg_neg_eq_arg_add_pi_of_im_neg hi, Real.Angle.coe_add]
  ¬∑ rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    ¬∑ rw [arg_ofReal_of_neg hr, ‚Üê ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le, ‚Üê
        Real.Angle.coe_add, ‚Üê two_mul, Real.Angle.coe_two_pi, Real.Angle.coe_zero]
    ¬∑ exact False.elim (hx (ext hr hi))
    ¬∑ rw [arg_ofReal_of_nonneg hr.le, ‚Üê ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr),
        Real.Angle.coe_zero, zero_add]
  ¬∑ rw [arg_neg_eq_arg_sub_pi_of_im_pos hi, Real.Angle.coe_sub, Real.Angle.sub_coe_pi_eq_add_coe_pi]

theorem arg_mul_cos_add_sin_mul_I_eq_toIocMod {r : ‚Ñù} (hr : 0 < r) (Œ∏ : ‚Ñù) :
    arg (r * (cos Œ∏ + sin Œ∏ * I)) = toIocMod Real.two_pi_pos (-œÄ) Œ∏ := by
  have hi : toIocMod Real.two_pi_pos (-œÄ) Œ∏ ‚àà Set.Ioc (-œÄ) œÄ := by
    convert toIocMod_mem_Ioc _ _ Œ∏
    ring
  convert arg_mul_cos_add_sin_mul_I hr hi using 3
  simp [toIocMod, cos_sub_int_mul_two_pi, sin_sub_int_mul_two_pi]

theorem arg_cos_add_sin_mul_I_eq_toIocMod (Œ∏ : ‚Ñù) :
    arg (cos Œ∏ + sin Œ∏ * I) = toIocMod Real.two_pi_pos (-œÄ) Œ∏ := by
  rw [‚Üê one_mul (_ + _), ‚Üê ofReal_one, arg_mul_cos_add_sin_mul_I_eq_toIocMod zero_lt_one]

theorem arg_mul_cos_add_sin_mul_I_sub {r : ‚Ñù} (hr : 0 < r) (Œ∏ : ‚Ñù) :
    arg (r * (cos Œ∏ + sin Œ∏ * I)) - Œ∏ = 2 * œÄ * ‚åä(œÄ - Œ∏) / (2 * œÄ)‚åã := by
  rw [arg_mul_cos_add_sin_mul_I_eq_toIocMod hr, toIocMod_sub_self, toIocDiv_eq_neg_floor,
    zsmul_eq_mul]
  ring_nf

theorem arg_cos_add_sin_mul_I_sub (Œ∏ : ‚Ñù) :
    arg (cos Œ∏ + sin Œ∏ * I) - Œ∏ = 2 * œÄ * ‚åä(œÄ - Œ∏) / (2 * œÄ)‚åã := by
  rw [‚Üê one_mul (_ + _), ‚Üê ofReal_one, arg_mul_cos_add_sin_mul_I_sub zero_lt_one]

theorem arg_mul_cos_add_sin_mul_I_coe_angle {r : ‚Ñù} (hr : 0 < r) (Œ∏ : Real.Angle) :
    (arg (r * (Real.Angle.cos Œ∏ + Real.Angle.sin Œ∏ * I)) : Real.Angle) = Œ∏ := by
  induction' Œ∏ using Real.Angle.induction_on with Œ∏
  rw [Real.Angle.cos_coe, Real.Angle.sin_coe, Real.Angle.angle_eq_iff_two_pi_dvd_sub]
  use ‚åä(œÄ - Œ∏) / (2 * œÄ)‚åã
  exact mod_cast arg_mul_cos_add_sin_mul_I_sub hr Œ∏

theorem arg_cos_add_sin_mul_I_coe_angle (Œ∏ : Real.Angle) :
    (arg (Real.Angle.cos Œ∏ + Real.Angle.sin Œ∏ * I) : Real.Angle) = Œ∏ := by
  rw [‚Üê one_mul (_ + _), ‚Üê ofReal_one, arg_mul_cos_add_sin_mul_I_coe_angle zero_lt_one]

theorem arg_mul_coe_angle {x y : ‚ÑÇ} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    (arg (x * y) : Real.Angle) = arg x + arg y := by
  convert arg_mul_cos_add_sin_mul_I_coe_angle (mul_pos (norm_pos_iff.mpr hx) (norm_pos_iff.mpr hy))
      (arg x + arg y : Real.Angle) using 3
  simp_rw [‚Üê Real.Angle.coe_add, Real.Angle.sin_coe, Real.Angle.cos_coe, ofReal_cos, ofReal_sin,
    cos_add_sin_I, ofReal_add, add_mul, exp_add, ofReal_mul]
  rw [mul_assoc, mul_comm (exp _), ‚Üê mul_assoc (‚Äñy‚Äñ : ‚ÑÇ), norm_mul_exp_arg_mul_I, mul_comm y, ‚Üê
    mul_assoc, norm_mul_exp_arg_mul_I]

theorem arg_div_coe_angle {x y : ‚ÑÇ} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    (arg (x / y) : Real.Angle) = arg x - arg y := by
  rw [div_eq_mul_inv, arg_mul_coe_angle hx (inv_ne_zero hy), arg_inv_coe_angle, sub_eq_add_neg]

theorem arg_pow_coe_angle (x : ‚ÑÇ) (n : ‚Ñï) :
    (arg (x ^ n) : Real.Angle) = n ‚Ä¢ (arg x : Real.Angle) := by
  obtain rfl | x0 := eq_or_ne x 0
  ¬∑ by_cases n0 : n = 0 <;> simp [n0]
  ¬∑ induction n with
    | zero => simp [x0]
    | succ n ih => rw [pow_succ, arg_mul_coe_angle (pow_ne_zero n x0) x0, ih, succ_nsmul]

theorem arg_zpow_coe_angle (x : ‚ÑÇ) (n : ‚Ñ§) :
    (arg (x ^ n) : Real.Angle) = n ‚Ä¢ (arg x : Real.Angle) := by
  match n with
  | Int.ofNat m => simp [arg_pow_coe_angle]
  | Int.negSucc m => simp [arg_pow_coe_angle]

@[simp]
theorem arg_coe_angle_toReal_eq_arg (z : ‚ÑÇ) : (arg z : Real.Angle).toReal = arg z := by
  rw [Real.Angle.toReal_coe_eq_self_iff_mem_Ioc]
  exact arg_mem_Ioc _

theorem arg_coe_angle_eq_iff_eq_toReal {z : ‚ÑÇ} {Œ∏ : Real.Angle} :
    (arg z : Real.Angle) = Œ∏ ‚Üî arg z = Œ∏.toReal := by
  rw [‚Üê Real.Angle.toReal_inj, arg_coe_angle_toReal_eq_arg]

@[simp]
theorem arg_coe_angle_eq_iff {x y : ‚ÑÇ} : (arg x : Real.Angle) = arg y ‚Üî arg x = arg y := by
  simp_rw [‚Üê Real.Angle.toReal_inj, arg_coe_angle_toReal_eq_arg]

lemma arg_mul_eq_add_arg_iff {x y : ‚ÑÇ} (hx‚ÇÄ : x ‚â† 0) (hy‚ÇÄ : y ‚â† 0) :
    (x * y).arg = x.arg + y.arg ‚Üî arg x + arg y ‚àà Set.Ioc (-œÄ) œÄ := by
  rw [‚Üê arg_coe_angle_toReal_eq_arg, arg_mul_coe_angle hx‚ÇÄ hy‚ÇÄ, ‚Üê Real.Angle.coe_add,
      Real.Angle.toReal_coe_eq_self_iff_mem_Ioc]

alias ‚ü®_, arg_mul‚ü© := arg_mul_eq_add_arg_iff

section slitPlane

open ComplexOrder in
/-- An alternative description of the slit plane as consisting of nonzero complex numbers
whose argument is not œÄ. -/
lemma mem_slitPlane_iff_arg {z : ‚ÑÇ} : z ‚àà slitPlane ‚Üî z.arg ‚â† œÄ ‚àß z ‚â† 0 := by
  simp only [mem_slitPlane_iff_not_le_zero, le_iff_lt_or_eq, ne_eq, arg_eq_pi_iff_lt_zero, not_or]

lemma slitPlane_arg_ne_pi {z : ‚ÑÇ} (hz : z ‚àà slitPlane) : z.arg ‚â† Real.pi :=
  (mem_slitPlane_iff_arg.mp hz).1

end slitPlane

section Continuity

theorem arg_eq_nhds_of_re_pos (hx : 0 < x.re) : arg =·∂†[ùìù x] fun x => Real.arcsin (x.im / ‚Äñx‚Äñ) :=
  ((continuous_re.tendsto _).eventually (lt_mem_nhds hx)).mono fun _ hy => arg_of_re_nonneg hy.le

theorem arg_eq_nhds_of_re_neg_of_im_pos (hx_re : x.re < 0) (hx_im : 0 < x.im) :
    arg =·∂†[ùìù x] fun x => Real.arcsin ((-x).im / ‚Äñx‚Äñ) + œÄ := by
  suffices h_forall_nhds : ‚àÄ·∂† y : ‚ÑÇ in ùìù x, y.re < 0 ‚àß 0 < y.im from
    h_forall_nhds.mono fun y hy => arg_of_re_neg_of_im_nonneg hy.1 hy.2.le
  refine IsOpen.eventually_mem ?_ (‚ü®hx_re, hx_im‚ü© : x.re < 0 ‚àß 0 < x.im)
  exact
    IsOpen.and (isOpen_lt continuous_re continuous_zero) (isOpen_lt continuous_zero continuous_im)

theorem arg_eq_nhds_of_re_neg_of_im_neg (hx_re : x.re < 0) (hx_im : x.im < 0) :
    arg =·∂†[ùìù x] fun x => Real.arcsin ((-x).im / ‚Äñx‚Äñ) - œÄ := by
  suffices h_forall_nhds : ‚àÄ·∂† y : ‚ÑÇ in ùìù x, y.re < 0 ‚àß y.im < 0 from
    h_forall_nhds.mono fun y hy => arg_of_re_neg_of_im_neg hy.1 hy.2
  refine IsOpen.eventually_mem ?_ (‚ü®hx_re, hx_im‚ü© : x.re < 0 ‚àß x.im < 0)
  exact
    IsOpen.and (isOpen_lt continuous_re continuous_zero) (isOpen_lt continuous_im continuous_zero)

theorem arg_eq_nhds_of_im_pos (hz : 0 < im z) : arg =·∂†[ùìù z] fun x => Real.arccos (x.re / ‚Äñx‚Äñ) :=
  ((continuous_im.tendsto _).eventually (lt_mem_nhds hz)).mono fun _ => arg_of_im_pos

theorem arg_eq_nhds_of_im_neg (hz : im z < 0) : arg =·∂†[ùìù z] fun x => -Real.arccos (x.re / ‚Äñx‚Äñ) :=
  ((continuous_im.tendsto _).eventually (gt_mem_nhds hz)).mono fun _ => arg_of_im_neg

theorem continuousAt_arg (h : x ‚àà slitPlane) : ContinuousAt arg x := by
  have h‚ÇÄ : ‚Äñx‚Äñ ‚â† 0 := by
    rw [norm_ne_zero_iff]
    exact slitPlane_ne_zero h
  rw [mem_slitPlane_iff, ‚Üê lt_or_lt_iff_ne] at h
  rcases h with (hx_re | hx_im | hx_im)
  exacts [(Real.continuousAt_arcsin.comp
          (continuous_im.continuousAt.div continuous_norm.continuousAt h‚ÇÄ)).congr
      (arg_eq_nhds_of_re_pos hx_re).symm,
    (Real.continuous_arccos.continuousAt.comp
            (continuous_re.continuousAt.div continuous_norm.continuousAt h‚ÇÄ)).neg.congr
      (arg_eq_nhds_of_im_neg hx_im).symm,
    (Real.continuous_arccos.continuousAt.comp
          (continuous_re.continuousAt.div continuous_norm.continuousAt h‚ÇÄ)).congr
      (arg_eq_nhds_of_im_pos hx_im).symm]

theorem tendsto_arg_nhdsWithin_im_neg_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto arg (ùìù[{ z : ‚ÑÇ | z.im < 0 }] z) (ùìù (-œÄ)) := by
  suffices H : Tendsto (fun x : ‚ÑÇ => Real.arcsin ((-x).im / ‚Äñx‚Äñ) - œÄ)
      (ùìù[{ z : ‚ÑÇ | z.im < 0 }] z) (ùìù (-œÄ)) by
    refine H.congr' ?_
    have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0 := continuous_re.tendsto z (gt_mem_nhds hre)
    filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds this] with _ him hre
    rw [arg, if_neg hre.not_ge, if_neg him.not_ge]
  convert (Real.continuousAt_arcsin.comp_continuousWithinAt
    ((continuous_im.continuousAt.comp_continuousWithinAt continuousWithinAt_neg).div
      continuous_norm.continuousWithinAt _)
    ).sub_const œÄ using 1
  ¬∑ simp [him]
  ¬∑ lift z to ‚Ñù using him
    simpa using hre.ne

theorem continuousWithinAt_arg_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0) (him : z.im = 0) :
    ContinuousWithinAt arg { z : ‚ÑÇ | 0 ‚â§ z.im } z := by
  have : arg =·∂†[ùìù[{ z : ‚ÑÇ | 0 ‚â§ z.im }] z] fun x => Real.arcsin ((-x).im / ‚Äñx‚Äñ) + œÄ := by
    have : ‚àÄ·∂† x : ‚ÑÇ in ùìù z, x.re < 0 := continuous_re.tendsto z (gt_mem_nhds hre)
    filter_upwards [self_mem_nhdsWithin (s := { z : ‚ÑÇ | 0 ‚â§ z.im }),
      mem_nhdsWithin_of_mem_nhds this] with _ him hre
    rw [arg, if_neg hre.not_ge, if_pos him]
  refine ContinuousWithinAt.congr_of_eventuallyEq ?_ this ?_
  ¬∑ refine
      (Real.continuousAt_arcsin.comp_continuousWithinAt
            ((continuous_im.continuousAt.comp_continuousWithinAt continuousWithinAt_neg).div
              continuous_norm.continuousWithinAt ?_)).add
        tendsto_const_nhds
    lift z to ‚Ñù using him
    simpa using hre.ne
  ¬∑ rw [arg, if_neg hre.not_ge, if_pos him.ge]

theorem tendsto_arg_nhdsWithin_im_nonneg_of_re_neg_of_im_zero {z : ‚ÑÇ} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto arg (ùìù[{ z : ‚ÑÇ | 0 ‚â§ z.im }] z) (ùìù œÄ) := by
  simpa only [arg_eq_pi_iff.2 ‚ü®hre, him‚ü©] using
    (continuousWithinAt_arg_of_re_neg_of_im_zero hre him).tendsto

theorem continuousAt_arg_coe_angle (h : x ‚â† 0) : ContinuousAt ((‚Üë) ‚àò arg : ‚ÑÇ ‚Üí Real.Angle) x := by
  by_cases hs : x ‚àà slitPlane
  ¬∑ exact Real.Angle.continuous_coe.continuousAt.comp (continuousAt_arg hs)
  ¬∑ rw [‚Üê Function.comp_id (((‚Üë) : ‚Ñù ‚Üí Real.Angle) ‚àò arg),
      (funext_iff.2 fun _ => (neg_neg _).symm : (id : ‚ÑÇ ‚Üí ‚ÑÇ) = Neg.neg ‚àò Neg.neg), ‚Üê
      Function.comp_assoc]
    refine ContinuousAt.comp ?_ continuous_neg.continuousAt
    suffices ContinuousAt (Function.update (((‚Üë) ‚àò arg) ‚àò Neg.neg : ‚ÑÇ ‚Üí Real.Angle) 0 œÄ) (-x) by
      rwa [continuousAt_update_of_ne (neg_ne_zero.2 h)] at this
    have ha :
      Function.update (((‚Üë) ‚àò arg) ‚àò Neg.neg : ‚ÑÇ ‚Üí Real.Angle) 0 œÄ = fun z =>
        (arg z : Real.Angle) + œÄ := by
      rw [Function.update_eq_iff]
      exact ‚ü®by simp, fun z hz => arg_neg_coe_angle hz‚ü©
    rw [ha]
    replace hs := mem_slitPlane_iff.mpr.mt hs
    push_neg at hs
    refine
      (Real.Angle.continuous_coe.continuousAt.comp (continuousAt_arg (Or.inl ?_))).add
        continuousAt_const
    rw [neg_re, neg_pos]
    exact hs.1.lt_of_ne fun h0 => h (Complex.ext_iff.2 ‚ü®h0, hs.2‚ü©)

end Continuity

end Complex
