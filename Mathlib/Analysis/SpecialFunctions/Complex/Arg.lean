/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Angle
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Inverse

/-!
# The argument of a complex number.

We define `arg : â„‚ â†’ â„`, returning a real number in the range (-Ï€, Ï€],
such that for `x â‰  0`, `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
while `arg 0` defaults to `0`
-/

open Filter Metric Set
open scoped ComplexConjugate Real Topology

namespace Complex
variable {a x z : â„‚}

/-- `arg` returns values in the range (-Ï€, Ï€], such that for `x â‰  0`,
  `sin (arg x) = x.im / x.abs` and `cos (arg x) = x.re / x.abs`,
  `arg 0` defaults to `0` -/
noncomputable def arg (x : â„‚) : â„ :=
  if 0 â‰¤ x.re then Real.arcsin (x.im / abs x)
  else if 0 â‰¤ x.im then Real.arcsin ((-x).im / abs x) + Ï€ else Real.arcsin ((-x).im / abs x) - Ï€

theorem sin_arg (x : â„‚) : Real.sin (arg x) = x.im / abs x := by
  unfold arg; split_ifs <;>
    simp [sub_eq_add_neg, arg,
      Real.sin_arcsin (abs_le.1 (abs_im_div_abs_le_one x)).1 (abs_le.1 (abs_im_div_abs_le_one x)).2,
      Real.sin_add, neg_div, Real.arcsin_neg, Real.sin_neg]

theorem cos_arg {x : â„‚} (hx : x â‰  0) : Real.cos (arg x) = x.re / abs x := by
  rw [arg]
  split_ifs with hâ‚ hâ‚‚
  Â· rw [Real.cos_arcsin]
    field_simp [Real.sqrt_sq, (abs.pos hx).le, *]
  Â· rw [Real.cos_add_pi, Real.cos_arcsin]
    field_simp [Real.sqrt_div (sq_nonneg _), Real.sqrt_sq_eq_abs,
      _root_.abs_of_neg (not_le.1 hâ‚), *]
  Â· rw [Real.cos_sub_pi, Real.cos_arcsin]
    field_simp [Real.sqrt_div (sq_nonneg _), Real.sqrt_sq_eq_abs,
      _root_.abs_of_neg (not_le.1 hâ‚), *]

@[simp]
theorem abs_mul_exp_arg_mul_I (x : â„‚) : â†‘(abs x) * exp (arg x * I) = x := by
  rcases eq_or_ne x 0 with (rfl | hx)
  Â· simp
  Â· have : abs x â‰  0 := abs.ne_zero hx
    apply Complex.ext <;> field_simp [sin_arg, cos_arg hx, this, mul_comm (abs x)]

@[simp]
theorem abs_mul_cos_add_sin_mul_I (x : â„‚) : (abs x * (cos (arg x) + sin (arg x) * I) : â„‚) = x := by
  rw [â† exp_mul_I, abs_mul_exp_arg_mul_I]

@[simp]
lemma abs_mul_cos_arg (x : â„‚) : abs x * Real.cos (arg x) = x.re := by
  simpa [-abs_mul_cos_add_sin_mul_I] using congr_arg re (abs_mul_cos_add_sin_mul_I x)

@[simp]
lemma abs_mul_sin_arg (x : â„‚) : abs x * Real.sin (arg x) = x.im := by
  simpa [-abs_mul_cos_add_sin_mul_I] using congr_arg im (abs_mul_cos_add_sin_mul_I x)

theorem abs_eq_one_iff (z : â„‚) : abs z = 1 â†” âˆƒ Î¸ : â„, exp (Î¸ * I) = z := by
  refine âŸ¨fun hz => âŸ¨arg z, ?_âŸ©, ?_âŸ©
  Â· calc
      exp (arg z * I) = abs z * exp (arg z * I) := by rw [hz, ofReal_one, one_mul]
      _ = z := abs_mul_exp_arg_mul_I z

  Â· rintro âŸ¨Î¸, rflâŸ©
    exact Complex.abs_exp_ofReal_mul_I Î¸

@[simp]
theorem range_exp_mul_I : (Set.range fun x : â„ => exp (x * I)) = Metric.sphere 0 1 := by
  ext x
  simp only [mem_sphere_zero_iff_norm, norm_eq_abs, abs_eq_one_iff, Set.mem_range]

theorem arg_mul_cos_add_sin_mul_I {r : â„} (hr : 0 < r) {Î¸ : â„} (hÎ¸ : Î¸ âˆˆ Set.Ioc (-Ï€) Ï€) :
    arg (r * (cos Î¸ + sin Î¸ * I)) = Î¸ := by
  simp only [arg, map_mul, abs_cos_add_sin_mul_I, Complex.abs_of_nonneg hr.le, mul_one]
  simp only [re_ofReal_mul, im_ofReal_mul, neg_im, â† ofReal_cos, â† ofReal_sin, â†
    mk_eq_add_mul_I, neg_div, mul_div_cancel_leftâ‚€ _ hr.ne', mul_nonneg_iff_right_nonneg_of_pos hr]
  by_cases hâ‚ : Î¸ âˆˆ Set.Icc (-(Ï€ / 2)) (Ï€ / 2)
  Â· rw [if_pos]
    exacts [Real.arcsin_sin' hâ‚, Real.cos_nonneg_of_mem_Icc hâ‚]
  Â· rw [Set.mem_Icc, not_and_or, not_le, not_le] at hâ‚
    rcases hâ‚ with hâ‚ | hâ‚
    Â· replace hÎ¸ := hÎ¸.1
      have hcos : Real.cos Î¸ < 0 := by
        rw [â† neg_pos, â† Real.cos_add_pi]
        refine Real.cos_pos_of_mem_Ioo âŸ¨?_, ?_âŸ© <;> linarith
      have hsin : Real.sin Î¸ < 0 := Real.sin_neg_of_neg_of_neg_pi_lt (by linarith) hÎ¸
      rw [if_neg, if_neg, â† Real.sin_add_pi, Real.arcsin_sin, add_sub_cancel_right] <;> [linarith;
        linarith; exact hsin.not_le; exact hcos.not_le]
    Â· replace hÎ¸ := hÎ¸.2
      have hcos : Real.cos Î¸ < 0 := Real.cos_neg_of_pi_div_two_lt_of_lt hâ‚ (by linarith)
      have hsin : 0 â‰¤ Real.sin Î¸ := Real.sin_nonneg_of_mem_Icc âŸ¨by linarith, hÎ¸âŸ©
      rw [if_neg, if_pos, â† Real.sin_sub_pi, Real.arcsin_sin, sub_add_cancel] <;> [linarith;
        linarith; exact hsin; exact hcos.not_le]

theorem arg_cos_add_sin_mul_I {Î¸ : â„} (hÎ¸ : Î¸ âˆˆ Set.Ioc (-Ï€) Ï€) : arg (cos Î¸ + sin Î¸ * I) = Î¸ := by
  rw [â† one_mul (_ + _), â† ofReal_one, arg_mul_cos_add_sin_mul_I zero_lt_one hÎ¸]

lemma arg_exp_mul_I (Î¸ : â„) :
    arg (exp (Î¸ * I)) = toIocMod (mul_pos two_pos Real.pi_pos) (-Ï€) Î¸ := by
  convert arg_cos_add_sin_mul_I (Î¸ := toIocMod (mul_pos two_pos Real.pi_pos) (-Ï€) Î¸) _ using 2
  Â· rw [â† exp_mul_I, eq_sub_of_add_eq <| toIocMod_add_toIocDiv_zsmul _ _ Î¸, ofReal_sub,
      ofReal_zsmul, ofReal_mul, ofReal_ofNat, exp_mul_I_periodic.sub_zsmul_eq]
  Â· convert toIocMod_mem_Ioc _ _ _
    ring

@[simp]
theorem arg_zero : arg 0 = 0 := by simp [arg, le_refl]

theorem ext_abs_arg {x y : â„‚} (hâ‚ : abs x = abs y) (hâ‚‚ : x.arg = y.arg) : x = y := by
  rw [â† abs_mul_exp_arg_mul_I x, â† abs_mul_exp_arg_mul_I y, hâ‚, hâ‚‚]

theorem ext_abs_arg_iff {x y : â„‚} : x = y â†” abs x = abs y âˆ§ arg x = arg y :=
  âŸ¨fun h => h â–¸ âŸ¨rfl, rflâŸ©, and_imp.2 ext_abs_argâŸ©

theorem arg_mem_Ioc (z : â„‚) : arg z âˆˆ Set.Ioc (-Ï€) Ï€ := by
  have hÏ€ : 0 < Ï€ := Real.pi_pos
  rcases eq_or_ne z 0 with (rfl | hz)
  Â· simp [hÏ€, hÏ€.le]
  rcases existsUnique_add_zsmul_mem_Ioc Real.two_pi_pos (arg z) (-Ï€) with âŸ¨N, hN, -âŸ©
  rw [two_mul, neg_add_cancel_left, â† two_mul, zsmul_eq_mul] at hN
  rw [â† abs_mul_cos_add_sin_mul_I z, â† cos_add_int_mul_two_pi _ N, â† sin_add_int_mul_two_pi _ N]
  have := arg_mul_cos_add_sin_mul_I (abs.pos hz) hN
  push_cast at this
  rwa [this]

@[simp]
theorem range_arg : Set.range arg = Set.Ioc (-Ï€) Ï€ :=
  (Set.range_subset_iff.2 arg_mem_Ioc).antisymm fun _ hx => âŸ¨_, arg_cos_add_sin_mul_I hxâŸ©

theorem arg_le_pi (x : â„‚) : arg x â‰¤ Ï€ :=
  (arg_mem_Ioc x).2

theorem neg_pi_lt_arg (x : â„‚) : -Ï€ < arg x :=
  (arg_mem_Ioc x).1

theorem abs_arg_le_pi (z : â„‚) : |arg z| â‰¤ Ï€ :=
  abs_le.2 âŸ¨(neg_pi_lt_arg z).le, arg_le_pi zâŸ©

@[simp]
theorem arg_nonneg_iff {z : â„‚} : 0 â‰¤ arg z â†” 0 â‰¤ z.im := by
  rcases eq_or_ne z 0 with (rfl | hâ‚€); Â· simp
  calc
    0 â‰¤ arg z â†” 0 â‰¤ Real.sin (arg z) :=
      âŸ¨fun h => Real.sin_nonneg_of_mem_Icc âŸ¨h, arg_le_pi zâŸ©, by
        contrapose!
        intro h
        exact Real.sin_neg_of_neg_of_neg_pi_lt h (neg_pi_lt_arg _)âŸ©
    _ â†” _ := by rw [sin_arg, le_div_iffâ‚€ (abs.pos hâ‚€), zero_mul]

@[simp]
theorem arg_neg_iff {z : â„‚} : arg z < 0 â†” z.im < 0 :=
  lt_iff_lt_of_le_iff_le arg_nonneg_iff

theorem arg_real_mul (x : â„‚) {r : â„} (hr : 0 < r) : arg (r * x) = arg x := by
  rcases eq_or_ne x 0 with (rfl | hx); Â· rw [mul_zero]
  conv_lhs =>
    rw [â† abs_mul_cos_add_sin_mul_I x, â† mul_assoc, â† ofReal_mul,
      arg_mul_cos_add_sin_mul_I (mul_pos hr (abs.pos hx)) x.arg_mem_Ioc]

theorem arg_mul_real {r : â„} (hr : 0 < r) (x : â„‚) : arg (x * r) = arg x :=
  mul_comm x r â–¸ arg_real_mul x hr

theorem arg_eq_arg_iff {x y : â„‚} (hx : x â‰  0) (hy : y â‰  0) :
    arg x = arg y â†” (abs y / abs x : â„‚) * x = y := by
  simp only [ext_abs_arg_iff, map_mul, map_divâ‚€, abs_ofReal, Complex.abs_abs,
    div_mul_cancelâ‚€ _ (abs.ne_zero hx), eq_self_iff_true, true_and]
  rw [â† ofReal_div, arg_real_mul]
  exact div_pos (abs.pos hy) (abs.pos hx)

@[simp] lemma arg_one : arg 1 = 0 := by simp [arg, zero_le_one]

/-- This holds true for all `x : â„‚` because of the junk values `0 / 0 = 0` and `arg 0 = 0`. -/
@[simp] lemma arg_div_self (x : â„‚) : arg (x / x) = 0 := by
  obtain rfl | hx := eq_or_ne x 0 <;> simp [*]

@[simp]
theorem arg_neg_one : arg (-1) = Ï€ := by simp [arg, le_refl, not_le.2 (zero_lt_one' â„)]

@[simp]
theorem arg_I : arg I = Ï€ / 2 := by simp [arg, le_refl]

@[simp]
theorem arg_neg_I : arg (-I) = -(Ï€ / 2) := by simp [arg, le_refl]

@[simp]
theorem tan_arg (x : â„‚) : Real.tan (arg x) = x.im / x.re := by
  by_cases h : x = 0
  Â· simp only [h, zero_div, Complex.zero_im, Complex.arg_zero, Real.tan_zero, Complex.zero_re]
  rw [Real.tan_eq_sin_div_cos, sin_arg, cos_arg h, div_div_div_cancel_rightâ‚€ (abs.ne_zero h)]

theorem arg_ofReal_of_nonneg {x : â„} (hx : 0 â‰¤ x) : arg x = 0 := by simp [arg, hx]

@[simp, norm_cast]
lemma natCast_arg {n : â„•} : arg n = 0 :=
  ofReal_natCast n â–¸ arg_ofReal_of_nonneg n.cast_nonneg

@[simp]
lemma ofNat_arg {n : â„•} [n.AtLeastTwo] : arg ofNat(n) = 0 :=
  natCast_arg

theorem arg_eq_zero_iff {z : â„‚} : arg z = 0 â†” 0 â‰¤ z.re âˆ§ z.im = 0 := by
  refine âŸ¨fun h => ?_, ?_âŸ©
  Â· rw [â† abs_mul_cos_add_sin_mul_I z, h]
    simp [abs.nonneg]
  Â· cases' z with x y
    rintro âŸ¨h, rfl : y = 0âŸ©
    exact arg_ofReal_of_nonneg h

open ComplexOrder in
lemma arg_eq_zero_iff_zero_le {z : â„‚} : arg z = 0 â†” 0 â‰¤ z := by
  rw [arg_eq_zero_iff, eq_comm, nonneg_iff]

theorem arg_eq_pi_iff {z : â„‚} : arg z = Ï€ â†” z.re < 0 âˆ§ z.im = 0 := by
  by_cases hâ‚€ : z = 0
  Â· simp [hâ‚€, lt_irrefl, Real.pi_ne_zero.symm]
  constructor
  Â· intro h
    rw [â† abs_mul_cos_add_sin_mul_I z, h]
    simp [hâ‚€]
  Â· cases' z with x y
    rintro âŸ¨h : x < 0, rfl : y = 0âŸ©
    rw [â† arg_neg_one, â† arg_real_mul (-1) (neg_pos.2 h)]
    simp [â† ofReal_def]

open ComplexOrder in
lemma arg_eq_pi_iff_lt_zero {z : â„‚} : arg z = Ï€ â†” z < 0 := arg_eq_pi_iff

theorem arg_lt_pi_iff {z : â„‚} : arg z < Ï€ â†” 0 â‰¤ z.re âˆ¨ z.im â‰  0 := by
  rw [(arg_le_pi z).lt_iff_ne, not_iff_comm, not_or, not_le, Classical.not_not, arg_eq_pi_iff]

theorem arg_ofReal_of_neg {x : â„} (hx : x < 0) : arg x = Ï€ :=
  arg_eq_pi_iff.2 âŸ¨hx, rflâŸ©

theorem arg_eq_pi_div_two_iff {z : â„‚} : arg z = Ï€ / 2 â†” z.re = 0 âˆ§ 0 < z.im := by
  by_cases hâ‚€ : z = 0; Â· simp [hâ‚€, lt_irrefl, Real.pi_div_two_pos.ne]
  constructor
  Â· intro h
    rw [â† abs_mul_cos_add_sin_mul_I z, h]
    simp [hâ‚€]
  Â· cases' z with x y
    rintro âŸ¨rfl : x = 0, hy : 0 < yâŸ©
    rw [â† arg_I, â† arg_real_mul I hy, ofReal_mul', I_re, I_im, mul_zero, mul_one]

theorem arg_eq_neg_pi_div_two_iff {z : â„‚} : arg z = -(Ï€ / 2) â†” z.re = 0 âˆ§ z.im < 0 := by
  by_cases hâ‚€ : z = 0; Â· simp [hâ‚€, lt_irrefl, Real.pi_ne_zero]
  constructor
  Â· intro h
    rw [â† abs_mul_cos_add_sin_mul_I z, h]
    simp [hâ‚€]
  Â· cases' z with x y
    rintro âŸ¨rfl : x = 0, hy : y < 0âŸ©
    rw [â† arg_neg_I, â† arg_real_mul (-I) (neg_pos.2 hy), mk_eq_add_mul_I]
    simp

theorem arg_of_re_nonneg {x : â„‚} (hx : 0 â‰¤ x.re) : arg x = Real.arcsin (x.im / abs x) :=
  if_pos hx

theorem arg_of_re_neg_of_im_nonneg {x : â„‚} (hx_re : x.re < 0) (hx_im : 0 â‰¤ x.im) :
    arg x = Real.arcsin ((-x).im / abs x) + Ï€ := by
  simp only [arg, hx_re.not_le, hx_im, if_true, if_false]

theorem arg_of_re_neg_of_im_neg {x : â„‚} (hx_re : x.re < 0) (hx_im : x.im < 0) :
    arg x = Real.arcsin ((-x).im / abs x) - Ï€ := by
  simp only [arg, hx_re.not_le, hx_im.not_le, if_false]

theorem arg_of_im_nonneg_of_ne_zero {z : â„‚} (hâ‚ : 0 â‰¤ z.im) (hâ‚‚ : z â‰  0) :
    arg z = Real.arccos (z.re / abs z) := by
  rw [â† cos_arg hâ‚‚, Real.arccos_cos (arg_nonneg_iff.2 hâ‚) (arg_le_pi _)]

theorem arg_of_im_pos {z : â„‚} (hz : 0 < z.im) : arg z = Real.arccos (z.re / abs z) :=
  arg_of_im_nonneg_of_ne_zero hz.le fun h => hz.ne' <| h.symm â–¸ rfl

theorem arg_of_im_neg {z : â„‚} (hz : z.im < 0) : arg z = -Real.arccos (z.re / abs z) := by
  have hâ‚€ : z â‰  0 := mt (congr_arg im) hz.ne
  rw [â† cos_arg hâ‚€, â† Real.cos_neg, Real.arccos_cos, neg_neg]
  exacts [neg_nonneg.2 (arg_neg_iff.2 hz).le, neg_le.2 (neg_pi_lt_arg z).le]

theorem arg_conj (x : â„‚) : arg (conj x) = if arg x = Ï€ then Ï€ else -arg x := by
  simp_rw [arg_eq_pi_iff, arg, neg_im, conj_im, conj_re, abs_conj, neg_div, neg_neg,
    Real.arcsin_neg]
  rcases lt_trichotomy x.re 0 with (hr | hr | hr) <;>
    rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  Â· simp [hr, hr.not_le, hi.le, hi.ne, not_le.2 hi, add_comm]
  Â· simp [hr, hr.not_le, hi]
  Â· simp [hr, hr.not_le, hi.ne.symm, hi.le, not_le.2 hi, sub_eq_neg_add]
  Â· simp [hr]
  Â· simp [hr]
  Â· simp [hr]
  Â· simp [hr, hr.le, hi.ne]
  Â· simp [hr, hr.le, hr.le.not_lt]
  Â· simp [hr, hr.le, hr.le.not_lt]

theorem arg_inv (x : â„‚) : arg xâ»Â¹ = if arg x = Ï€ then Ï€ else -arg x := by
  rw [â† arg_conj, inv_def, mul_comm]
  by_cases hx : x = 0
  Â· simp [hx]
  Â· exact arg_real_mul (conj x) (by simp [hx])

@[simp] lemma abs_arg_inv (x : â„‚) : |xâ»Â¹.arg| = |x.arg| := by rw [arg_inv]; split_ifs <;> simp [*]

-- TODO: Replace the next two lemmas by general facts about periodic functions
lemma abs_eq_one_iff' : abs x = 1 â†” âˆƒ Î¸ âˆˆ Set.Ioc (-Ï€) Ï€, exp (Î¸ * I) = x := by
  rw [abs_eq_one_iff]
  constructor
  Â· rintro âŸ¨Î¸, rflâŸ©
    refine âŸ¨toIocMod (mul_pos two_pos Real.pi_pos) (-Ï€) Î¸, ?_, ?_âŸ©
    Â· convert toIocMod_mem_Ioc _ _ _
      ring
    Â· rw [eq_sub_of_add_eq <| toIocMod_add_toIocDiv_zsmul _ _ Î¸, ofReal_sub,
      ofReal_zsmul, ofReal_mul, ofReal_ofNat, exp_mul_I_periodic.sub_zsmul_eq]
  Â· rintro âŸ¨Î¸, _, rflâŸ©
    exact âŸ¨Î¸, rflâŸ©

lemma image_exp_Ioc_eq_sphere : (fun Î¸ : â„ â†¦ exp (Î¸ * I)) '' Set.Ioc (-Ï€) Ï€ = sphere 0 1 := by
  ext; simpa using abs_eq_one_iff'.symm

theorem arg_le_pi_div_two_iff {z : â„‚} : arg z â‰¤ Ï€ / 2 â†” 0 â‰¤ re z âˆ¨ im z < 0 := by
  rcases le_or_lt 0 (re z) with hre | hre
  Â· simp only [hre, arg_of_re_nonneg hre, Real.arcsin_le_pi_div_two, true_or]
  simp only [hre.not_le, false_or]
  rcases le_or_lt 0 (im z) with him | him
  Â· simp only [him.not_lt]
    rw [iff_false, not_le, arg_of_re_neg_of_im_nonneg hre him, â† sub_lt_iff_lt_add, half_sub,
      Real.neg_pi_div_two_lt_arcsin, neg_im, neg_div, neg_lt_neg_iff, div_lt_one, â†
      abs_of_nonneg him, abs_im_lt_abs]
    exacts [hre.ne, abs.pos <| ne_of_apply_ne re hre.ne]
  Â· simp only [him]
    rw [iff_true, arg_of_re_neg_of_im_neg hre him]
    exact (sub_le_self _ Real.pi_pos.le).trans (Real.arcsin_le_pi_div_two _)

theorem neg_pi_div_two_le_arg_iff {z : â„‚} : -(Ï€ / 2) â‰¤ arg z â†” 0 â‰¤ re z âˆ¨ 0 â‰¤ im z := by
  rcases le_or_lt 0 (re z) with hre | hre
  Â· simp only [hre, arg_of_re_nonneg hre, Real.neg_pi_div_two_le_arcsin, true_or]
  simp only [hre.not_le, false_or]
  rcases le_or_lt 0 (im z) with him | him
  Â· simp only [him]
    rw [iff_true, arg_of_re_neg_of_im_nonneg hre him]
    exact (Real.neg_pi_div_two_le_arcsin _).trans (le_add_of_nonneg_right Real.pi_pos.le)
  Â· simp only [him.not_le]
    rw [iff_false, not_le, arg_of_re_neg_of_im_neg hre him, sub_lt_iff_lt_add', â†
      sub_eq_add_neg, sub_half, Real.arcsin_lt_pi_div_two, div_lt_one, neg_im, â† abs_of_neg him,
      abs_im_lt_abs]
    exacts [hre.ne, abs.pos <| ne_of_apply_ne re hre.ne]

lemma neg_pi_div_two_lt_arg_iff {z : â„‚} : -(Ï€ / 2) < arg z â†” 0 < re z âˆ¨ 0 â‰¤ im z := by
  rw [lt_iff_le_and_ne, neg_pi_div_two_le_arg_iff, ne_comm, Ne, arg_eq_neg_pi_div_two_iff]
  rcases lt_trichotomy z.re 0 with hre | hre | hre
  Â· simp [hre.ne, hre.not_le, hre.not_lt]
  Â· simp [hre]
  Â· simp [hre, hre.le, hre.ne']

lemma arg_lt_pi_div_two_iff {z : â„‚} : arg z < Ï€ / 2 â†” 0 < re z âˆ¨ im z < 0 âˆ¨ z = 0 := by
  rw [lt_iff_le_and_ne, arg_le_pi_div_two_iff, Ne, arg_eq_pi_div_two_iff]
  rcases lt_trichotomy z.re 0 with hre | hre | hre
  Â· have : z â‰  0 := by simp [Complex.ext_iff, hre.ne]
    simp [hre.ne, hre.not_le, hre.not_lt, this]
  Â· have : z = 0 â†” z.im = 0 := by simp [Complex.ext_iff, hre]
    simp [hre, this, or_comm, le_iff_eq_or_lt]
  Â· simp [hre, hre.le, hre.ne']

@[simp]
theorem abs_arg_le_pi_div_two_iff {z : â„‚} : |arg z| â‰¤ Ï€ / 2 â†” 0 â‰¤ re z := by
  rw [abs_le, arg_le_pi_div_two_iff, neg_pi_div_two_le_arg_iff, â† or_and_left, â† not_le,
    and_not_self_iff, or_false]

@[simp]
theorem abs_arg_lt_pi_div_two_iff {z : â„‚} : |arg z| < Ï€ / 2 â†” 0 < re z âˆ¨ z = 0 := by
  rw [abs_lt, arg_lt_pi_div_two_iff, neg_pi_div_two_lt_arg_iff, â† or_and_left]
  rcases eq_or_ne z 0 with hz | hz
  Â· simp [hz]
  Â· simp_rw [hz, or_false, â† not_lt, not_and_self_iff, or_false]

@[simp]
theorem arg_conj_coe_angle (x : â„‚) : (arg (conj x) : Real.Angle) = -arg x := by
  by_cases h : arg x = Ï€ <;> simp [arg_conj, h]

@[simp]
theorem arg_inv_coe_angle (x : â„‚) : (arg xâ»Â¹ : Real.Angle) = -arg x := by
  by_cases h : arg x = Ï€ <;> simp [arg_inv, h]

theorem arg_neg_eq_arg_sub_pi_of_im_pos {x : â„‚} (hi : 0 < x.im) : arg (-x) = arg x - Ï€ := by
  rw [arg_of_im_pos hi, arg_of_im_neg (show (-x).im < 0 from Left.neg_neg_iff.2 hi)]
  simp [neg_div, Real.arccos_neg]

theorem arg_neg_eq_arg_add_pi_of_im_neg {x : â„‚} (hi : x.im < 0) : arg (-x) = arg x + Ï€ := by
  rw [arg_of_im_neg hi, arg_of_im_pos (show 0 < (-x).im from Left.neg_pos_iff.2 hi)]
  simp [neg_div, Real.arccos_neg, add_comm, â† sub_eq_add_neg]

theorem arg_neg_eq_arg_sub_pi_iff {x : â„‚} :
    arg (-x) = arg x - Ï€ â†” 0 < x.im âˆ¨ x.im = 0 âˆ§ x.re < 0 := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  Â· simp [hi, hi.ne, hi.not_lt, arg_neg_eq_arg_add_pi_of_im_neg, sub_eq_add_neg, â†
      add_eq_zero_iff_eq_neg, Real.pi_ne_zero]
  Â· rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    Â· rw [arg_ofReal_of_neg hr, â† ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le]
      simp [hr]
    Â· simp [hr, hi, Real.pi_ne_zero]
    Â· rw [arg_ofReal_of_nonneg hr.le, â† ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr)]
      simp [hr.not_lt, â† add_eq_zero_iff_eq_neg, Real.pi_ne_zero]
  Â· simp [hi, arg_neg_eq_arg_sub_pi_of_im_pos]

theorem arg_neg_eq_arg_add_pi_iff {x : â„‚} :
    arg (-x) = arg x + Ï€ â†” x.im < 0 âˆ¨ x.im = 0 âˆ§ 0 < x.re := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  Â· simp [hi, arg_neg_eq_arg_add_pi_of_im_neg]
  Â· rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    Â· rw [arg_ofReal_of_neg hr, â† ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le]
      simp [hr.not_lt, â† two_mul, Real.pi_ne_zero]
    Â· simp [hr, hi, Real.pi_ne_zero.symm]
    Â· rw [arg_ofReal_of_nonneg hr.le, â† ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr)]
      simp [hr]
  Â· simp [hi, hi.ne.symm, hi.not_lt, arg_neg_eq_arg_sub_pi_of_im_pos, sub_eq_add_neg, â†
      add_eq_zero_iff_neg_eq, Real.pi_ne_zero]

theorem arg_neg_coe_angle {x : â„‚} (hx : x â‰  0) : (arg (-x) : Real.Angle) = arg x + Ï€ := by
  rcases lt_trichotomy x.im 0 with (hi | hi | hi)
  Â· rw [arg_neg_eq_arg_add_pi_of_im_neg hi, Real.Angle.coe_add]
  Â· rw [(ext rfl hi : x = x.re)]
    rcases lt_trichotomy x.re 0 with (hr | hr | hr)
    Â· rw [arg_ofReal_of_neg hr, â† ofReal_neg, arg_ofReal_of_nonneg (Left.neg_pos_iff.2 hr).le, â†
        Real.Angle.coe_add, â† two_mul, Real.Angle.coe_two_pi, Real.Angle.coe_zero]
    Â· exact False.elim (hx (ext hr hi))
    Â· rw [arg_ofReal_of_nonneg hr.le, â† ofReal_neg, arg_ofReal_of_neg (Left.neg_neg_iff.2 hr),
        Real.Angle.coe_zero, zero_add]
  Â· rw [arg_neg_eq_arg_sub_pi_of_im_pos hi, Real.Angle.coe_sub, Real.Angle.sub_coe_pi_eq_add_coe_pi]

theorem arg_mul_cos_add_sin_mul_I_eq_toIocMod {r : â„} (hr : 0 < r) (Î¸ : â„) :
    arg (r * (cos Î¸ + sin Î¸ * I)) = toIocMod Real.two_pi_pos (-Ï€) Î¸ := by
  have hi : toIocMod Real.two_pi_pos (-Ï€) Î¸ âˆˆ Set.Ioc (-Ï€) Ï€ := by
    convert toIocMod_mem_Ioc _ _ Î¸
    ring
  convert arg_mul_cos_add_sin_mul_I hr hi using 3
  simp [toIocMod, cos_sub_int_mul_two_pi, sin_sub_int_mul_two_pi]

theorem arg_cos_add_sin_mul_I_eq_toIocMod (Î¸ : â„) :
    arg (cos Î¸ + sin Î¸ * I) = toIocMod Real.two_pi_pos (-Ï€) Î¸ := by
  rw [â† one_mul (_ + _), â† ofReal_one, arg_mul_cos_add_sin_mul_I_eq_toIocMod zero_lt_one]

theorem arg_mul_cos_add_sin_mul_I_sub {r : â„} (hr : 0 < r) (Î¸ : â„) :
    arg (r * (cos Î¸ + sin Î¸ * I)) - Î¸ = 2 * Ï€ * âŒŠ(Ï€ - Î¸) / (2 * Ï€)âŒ‹ := by
  rw [arg_mul_cos_add_sin_mul_I_eq_toIocMod hr, toIocMod_sub_self, toIocDiv_eq_neg_floor,
    zsmul_eq_mul]
  ring_nf

theorem arg_cos_add_sin_mul_I_sub (Î¸ : â„) :
    arg (cos Î¸ + sin Î¸ * I) - Î¸ = 2 * Ï€ * âŒŠ(Ï€ - Î¸) / (2 * Ï€)âŒ‹ := by
  rw [â† one_mul (_ + _), â† ofReal_one, arg_mul_cos_add_sin_mul_I_sub zero_lt_one]

theorem arg_mul_cos_add_sin_mul_I_coe_angle {r : â„} (hr : 0 < r) (Î¸ : Real.Angle) :
    (arg (r * (Real.Angle.cos Î¸ + Real.Angle.sin Î¸ * I)) : Real.Angle) = Î¸ := by
  induction' Î¸ using Real.Angle.induction_on with Î¸
  rw [Real.Angle.cos_coe, Real.Angle.sin_coe, Real.Angle.angle_eq_iff_two_pi_dvd_sub]
  use âŒŠ(Ï€ - Î¸) / (2 * Ï€)âŒ‹
  exact mod_cast arg_mul_cos_add_sin_mul_I_sub hr Î¸

theorem arg_cos_add_sin_mul_I_coe_angle (Î¸ : Real.Angle) :
    (arg (Real.Angle.cos Î¸ + Real.Angle.sin Î¸ * I) : Real.Angle) = Î¸ := by
  rw [â† one_mul (_ + _), â† ofReal_one, arg_mul_cos_add_sin_mul_I_coe_angle zero_lt_one]

theorem arg_mul_coe_angle {x y : â„‚} (hx : x â‰  0) (hy : y â‰  0) :
    (arg (x * y) : Real.Angle) = arg x + arg y := by
  convert arg_mul_cos_add_sin_mul_I_coe_angle (mul_pos (abs.pos hx) (abs.pos hy))
      (arg x + arg y : Real.Angle) using
    3
  simp_rw [â† Real.Angle.coe_add, Real.Angle.sin_coe, Real.Angle.cos_coe, ofReal_cos, ofReal_sin,
    cos_add_sin_I, ofReal_add, add_mul, exp_add, ofReal_mul]
  rw [mul_assoc, mul_comm (exp _), â† mul_assoc (abs y : â„‚), abs_mul_exp_arg_mul_I, mul_comm y, â†
    mul_assoc, abs_mul_exp_arg_mul_I]

theorem arg_div_coe_angle {x y : â„‚} (hx : x â‰  0) (hy : y â‰  0) :
    (arg (x / y) : Real.Angle) = arg x - arg y := by
  rw [div_eq_mul_inv, arg_mul_coe_angle hx (inv_ne_zero hy), arg_inv_coe_angle, sub_eq_add_neg]

@[simp]
theorem arg_coe_angle_toReal_eq_arg (z : â„‚) : (arg z : Real.Angle).toReal = arg z := by
  rw [Real.Angle.toReal_coe_eq_self_iff_mem_Ioc]
  exact arg_mem_Ioc _

theorem arg_coe_angle_eq_iff_eq_toReal {z : â„‚} {Î¸ : Real.Angle} :
    (arg z : Real.Angle) = Î¸ â†” arg z = Î¸.toReal := by
  rw [â† Real.Angle.toReal_inj, arg_coe_angle_toReal_eq_arg]

@[simp]
theorem arg_coe_angle_eq_iff {x y : â„‚} : (arg x : Real.Angle) = arg y â†” arg x = arg y := by
  simp_rw [â† Real.Angle.toReal_inj, arg_coe_angle_toReal_eq_arg]

lemma arg_mul_eq_add_arg_iff {x y : â„‚} (hxâ‚€ : x â‰  0) (hyâ‚€ : y â‰  0) :
    (x * y).arg = x.arg + y.arg â†” arg x + arg y âˆˆ Set.Ioc (-Ï€) Ï€ := by
  rw [â† arg_coe_angle_toReal_eq_arg, arg_mul_coe_angle hxâ‚€ hyâ‚€, â† Real.Angle.coe_add,
      Real.Angle.toReal_coe_eq_self_iff_mem_Ioc]

alias âŸ¨_, arg_mulâŸ© := arg_mul_eq_add_arg_iff

section slitPlane

open ComplexOrder in
/-- An alternative description of the slit plane as consisting of nonzero complex numbers
whose argument is not Ï€. -/
lemma mem_slitPlane_iff_arg {z : â„‚} : z âˆˆ slitPlane â†” z.arg â‰  Ï€ âˆ§ z â‰  0 := by
  simp only [mem_slitPlane_iff_not_le_zero, le_iff_lt_or_eq, ne_eq, arg_eq_pi_iff_lt_zero, not_or]

lemma slitPlane_arg_ne_pi {z : â„‚} (hz : z âˆˆ slitPlane) : z.arg â‰  Real.pi :=
  (mem_slitPlane_iff_arg.mp hz).1

end slitPlane

section Continuity

theorem arg_eq_nhds_of_re_pos (hx : 0 < x.re) : arg =á¶ [ğ“ x] fun x => Real.arcsin (x.im / abs x) :=
  ((continuous_re.tendsto _).eventually (lt_mem_nhds hx)).mono fun _ hy => arg_of_re_nonneg hy.le

theorem arg_eq_nhds_of_re_neg_of_im_pos (hx_re : x.re < 0) (hx_im : 0 < x.im) :
    arg =á¶ [ğ“ x] fun x => Real.arcsin ((-x).im / abs x) + Ï€ := by
  suffices h_forall_nhds : âˆ€á¶  y : â„‚ in ğ“ x, y.re < 0 âˆ§ 0 < y.im from
    h_forall_nhds.mono fun y hy => arg_of_re_neg_of_im_nonneg hy.1 hy.2.le
  refine IsOpen.eventually_mem ?_ (âŸ¨hx_re, hx_imâŸ© : x.re < 0 âˆ§ 0 < x.im)
  exact
    IsOpen.and (isOpen_lt continuous_re continuous_zero) (isOpen_lt continuous_zero continuous_im)

theorem arg_eq_nhds_of_re_neg_of_im_neg (hx_re : x.re < 0) (hx_im : x.im < 0) :
    arg =á¶ [ğ“ x] fun x => Real.arcsin ((-x).im / abs x) - Ï€ := by
  suffices h_forall_nhds : âˆ€á¶  y : â„‚ in ğ“ x, y.re < 0 âˆ§ y.im < 0 from
    h_forall_nhds.mono fun y hy => arg_of_re_neg_of_im_neg hy.1 hy.2
  refine IsOpen.eventually_mem ?_ (âŸ¨hx_re, hx_imâŸ© : x.re < 0 âˆ§ x.im < 0)
  exact
    IsOpen.and (isOpen_lt continuous_re continuous_zero) (isOpen_lt continuous_im continuous_zero)

theorem arg_eq_nhds_of_im_pos (hz : 0 < im z) : arg =á¶ [ğ“ z] fun x => Real.arccos (x.re / abs x) :=
  ((continuous_im.tendsto _).eventually (lt_mem_nhds hz)).mono fun _ => arg_of_im_pos

theorem arg_eq_nhds_of_im_neg (hz : im z < 0) : arg =á¶ [ğ“ z] fun x => -Real.arccos (x.re / abs x) :=
  ((continuous_im.tendsto _).eventually (gt_mem_nhds hz)).mono fun _ => arg_of_im_neg

theorem continuousAt_arg (h : x âˆˆ slitPlane) : ContinuousAt arg x := by
  have hâ‚€ : abs x â‰  0 := by
    rw [abs.ne_zero_iff]
    exact slitPlane_ne_zero h
  rw [mem_slitPlane_iff, â† lt_or_lt_iff_ne] at h
  rcases h with (hx_re | hx_im | hx_im)
  exacts [(Real.continuousAt_arcsin.comp
          (continuous_im.continuousAt.div continuous_abs.continuousAt hâ‚€)).congr
      (arg_eq_nhds_of_re_pos hx_re).symm,
    (Real.continuous_arccos.continuousAt.comp
            (continuous_re.continuousAt.div continuous_abs.continuousAt hâ‚€)).neg.congr
      (arg_eq_nhds_of_im_neg hx_im).symm,
    (Real.continuous_arccos.continuousAt.comp
          (continuous_re.continuousAt.div continuous_abs.continuousAt hâ‚€)).congr
      (arg_eq_nhds_of_im_pos hx_im).symm]

theorem tendsto_arg_nhdsWithin_im_neg_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto arg (ğ“[{ z : â„‚ | z.im < 0 }] z) (ğ“ (-Ï€)) := by
  suffices H : Tendsto (fun x : â„‚ => Real.arcsin ((-x).im / abs x) - Ï€)
      (ğ“[{ z : â„‚ | z.im < 0 }] z) (ğ“ (-Ï€)) by
    refine H.congr' ?_
    have : âˆ€á¶  x : â„‚ in ğ“ z, x.re < 0 := continuous_re.tendsto z (gt_mem_nhds hre)
    filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds this] with _ him hre
    rw [arg, if_neg hre.not_le, if_neg him.not_le]
  convert (Real.continuousAt_arcsin.comp_continuousWithinAt
    ((continuous_im.continuousAt.comp_continuousWithinAt continuousWithinAt_neg).div
      continuous_abs.continuousWithinAt _)
    ).sub_const Ï€ using 1
  Â· simp [him]
  Â· lift z to â„ using him
    simpa using hre.ne

theorem continuousWithinAt_arg_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0) (him : z.im = 0) :
    ContinuousWithinAt arg { z : â„‚ | 0 â‰¤ z.im } z := by
  have : arg =á¶ [ğ“[{ z : â„‚ | 0 â‰¤ z.im }] z] fun x => Real.arcsin ((-x).im / abs x) + Ï€ := by
    have : âˆ€á¶  x : â„‚ in ğ“ z, x.re < 0 := continuous_re.tendsto z (gt_mem_nhds hre)
    filter_upwards [self_mem_nhdsWithin (s := { z : â„‚ | 0 â‰¤ z.im }),
      mem_nhdsWithin_of_mem_nhds this] with _ him hre
    rw [arg, if_neg hre.not_le, if_pos him]
  refine ContinuousWithinAt.congr_of_eventuallyEq ?_ this ?_
  Â· refine
      (Real.continuousAt_arcsin.comp_continuousWithinAt
            ((continuous_im.continuousAt.comp_continuousWithinAt continuousWithinAt_neg).div
              continuous_abs.continuousWithinAt ?_)).add
        tendsto_const_nhds
    lift z to â„ using him
    simpa using hre.ne
  Â· rw [arg, if_neg hre.not_le, if_pos him.ge]

theorem tendsto_arg_nhdsWithin_im_nonneg_of_re_neg_of_im_zero {z : â„‚} (hre : z.re < 0)
    (him : z.im = 0) : Tendsto arg (ğ“[{ z : â„‚ | 0 â‰¤ z.im }] z) (ğ“ Ï€) := by
  simpa only [arg_eq_pi_iff.2 âŸ¨hre, himâŸ©] using
    (continuousWithinAt_arg_of_re_neg_of_im_zero hre him).tendsto

theorem continuousAt_arg_coe_angle (h : x â‰  0) : ContinuousAt ((â†‘) âˆ˜ arg : â„‚ â†’ Real.Angle) x := by
  by_cases hs : x âˆˆ slitPlane
  Â· exact Real.Angle.continuous_coe.continuousAt.comp (continuousAt_arg hs)
  Â· rw [â† Function.comp_id (((â†‘) : â„ â†’ Real.Angle) âˆ˜ arg),
      (funext_iff.2 fun _ => (neg_neg _).symm : (id : â„‚ â†’ â„‚) = Neg.neg âˆ˜ Neg.neg), â†
      Function.comp_assoc]
    refine ContinuousAt.comp ?_ continuous_neg.continuousAt
    suffices ContinuousAt (Function.update (((â†‘) âˆ˜ arg) âˆ˜ Neg.neg : â„‚ â†’ Real.Angle) 0 Ï€) (-x) by
      rwa [continuousAt_update_of_ne (neg_ne_zero.2 h)] at this
    have ha :
      Function.update (((â†‘) âˆ˜ arg) âˆ˜ Neg.neg : â„‚ â†’ Real.Angle) 0 Ï€ = fun z =>
        (arg z : Real.Angle) + Ï€ := by
      rw [Function.update_eq_iff]
      exact âŸ¨by simp, fun z hz => arg_neg_coe_angle hzâŸ©
    rw [ha]
    replace hs := mem_slitPlane_iff.mpr.mt hs
    push_neg at hs
    refine
      (Real.Angle.continuous_coe.continuousAt.comp (continuousAt_arg (Or.inl ?_))).add
        continuousAt_const
    rw [neg_re, neg_pos]
    exact hs.1.lt_of_ne fun h0 => h (Complex.ext_iff.2 âŸ¨h0, hs.2âŸ©)

end Continuity

end Complex
