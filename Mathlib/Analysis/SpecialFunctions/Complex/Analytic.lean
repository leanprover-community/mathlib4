/-
Copyright (c) 2024 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Analysis.Analytic.Composition
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.SpecialFunctions.Complex.LogDeriv

/-!
# Various complex special functions are analytic

`log`, and `cpow` are analytic, since they are differentiable.
-/

open Complex Set
open scoped Topology

variable {E : Type} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]
variable {f g : E ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {x : E} {s : Set E}

/-- `log` is analytic away from nonpositive reals -/
@[fun_prop]
theorem analyticAt_clog (m : z ‚àà slitPlane) : AnalyticAt ‚ÑÇ log z := by
  rw [analyticAt_iff_eventually_differentiableAt]
  filter_upwards [isOpen_slitPlane.eventually_mem m]
  intro z m
  exact differentiableAt_id.clog m

/-- `log` is analytic away from nonpositive reals -/
@[fun_prop]
theorem AnalyticAt.clog (fa : AnalyticAt ‚ÑÇ f x) (m : f x ‚àà slitPlane) :
    AnalyticAt ‚ÑÇ (fun z ‚Ü¶ log (f z)) x :=
  (analyticAt_clog m).comp fa

theorem AnalyticWithinAt.clog (fa : AnalyticWithinAt ‚ÑÇ f s x) (m : f x ‚àà slitPlane) :
    AnalyticWithinAt ‚ÑÇ (fun z ‚Ü¶ log (f z)) s x :=
  (analyticAt_clog m).comp_analyticWithinAt fa

/-- `log` is analytic away from nonpositive reals -/
theorem AnalyticOnNhd.clog (fs : AnalyticOnNhd ‚ÑÇ f s) (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) :
    AnalyticOnNhd ‚ÑÇ (fun z ‚Ü¶ log (f z)) s :=
  fun z n ‚Ü¶ (analyticAt_clog (m z n)).comp (fs z n)

theorem AnalyticOn.clog (fs : AnalyticOn ‚ÑÇ f s) (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) :
    AnalyticOn ‚ÑÇ (fun z ‚Ü¶ log (f z)) s :=
  fun z n ‚Ü¶ (analyticAt_clog (m z n)).analyticWithinAt.comp (fs z n) m

/-- `f z ^ g z` is analytic if `f z` is not a nonpositive real -/
theorem AnalyticWithinAt.cpow (fa : AnalyticWithinAt ‚ÑÇ f s x) (ga : AnalyticWithinAt ‚ÑÇ g s x)
    (m : f x ‚àà slitPlane) : AnalyticWithinAt ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) s x := by
  have e : (fun z ‚Ü¶ f z ^ g z) =·∂†[ùìù[insert x s] x] fun z ‚Ü¶ exp (log (f z) * g z) := by
    filter_upwards [(fa.continuousWithinAt_insert.eventually_ne (slitPlane_ne_zero m))]
    intro z fz
    simp only [fz, cpow_def, if_false]
  apply AnalyticWithinAt.congr_of_eventuallyEq_insert _ e
  exact ((fa.clog m).mul ga).cexp

/-- `f z ^ g z` is analytic if `f z` is not a nonpositive real -/
@[fun_prop]
theorem AnalyticAt.cpow (fa : AnalyticAt ‚ÑÇ f x) (ga : AnalyticAt ‚ÑÇ g x)
    (m : f x ‚àà slitPlane) : AnalyticAt ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) x := by
  rw [‚Üê analyticWithinAt_univ] at fa ga ‚ä¢
  exact fa.cpow ga m

/-- `f z ^ g z` is analytic if `f z` avoids nonpositive reals -/
theorem AnalyticOn.cpow (fs : AnalyticOn ‚ÑÇ f s) (gs : AnalyticOn ‚ÑÇ g s)
    (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) : AnalyticOn ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) s :=
  fun z n ‚Ü¶ (fs z n).cpow (gs z n) (m z n)

/-- `f z ^ g z` is analytic if `f z` avoids nonpositive reals -/
theorem AnalyticOnNhd.cpow (fs : AnalyticOnNhd ‚ÑÇ f s) (gs : AnalyticOnNhd ‚ÑÇ g s)
    (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) : AnalyticOnNhd ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) s :=
  fun z n ‚Ü¶ (fs z n).cpow (gs z n) (m z n)

section ReOfReal

variable {f : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚Ñù} {x : ‚Ñù}

@[fun_prop]
lemma AnalyticAt.re_ofReal (hf : AnalyticAt ‚ÑÇ f x) :
    AnalyticAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).re) x :=
  (Complex.reCLM.analyticAt _).comp (hf.restrictScalars.comp (Complex.ofRealCLM.analyticAt _))

@[fun_prop]
lemma AnalyticAt.im_ofReal (hf : AnalyticAt ‚ÑÇ f x) :
    AnalyticAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).im) x :=
  (Complex.imCLM.analyticAt _).comp (hf.restrictScalars.comp (Complex.ofRealCLM.analyticAt _))

lemma AnalyticWithinAt.re_ofReal (hf : AnalyticWithinAt ‚ÑÇ f (ofReal '' s) x) :
    AnalyticWithinAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).re) s x :=
  ((Complex.reCLM.analyticWithinAt _ _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticWithinAt _ _) (mapsTo_image ofReal s)

lemma AnalyticWithinAt.im_ofReal (hf : AnalyticWithinAt ‚ÑÇ f (ofReal '' s) x) :
    AnalyticWithinAt ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).im) s x :=
  ((Complex.imCLM.analyticWithinAt _ _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticWithinAt _ _) (mapsTo_image ofReal s)

lemma AnalyticOn.re_ofReal (hf : AnalyticOn ‚ÑÇ f (ofReal '' s)) :
    AnalyticOn ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).re) s :=
  ((Complex.reCLM.analyticOn _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticOn _) (mapsTo_image ofReal s)

lemma AnalyticOn.im_ofReal (hf : AnalyticOn ‚ÑÇ f (ofReal '' s)) :
    AnalyticOn ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).im) s :=
  ((Complex.imCLM.analyticOn _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticOn _) (mapsTo_image ofReal s)

lemma AnalyticOnNhd.re_ofReal (hf : AnalyticOnNhd ‚ÑÇ f (ofReal '' s)) :
    AnalyticOnNhd ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).re) s :=
  ((Complex.reCLM.analyticOnNhd _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticOnNhd _) (mapsTo_image ofReal s)

lemma AnalyticOnNhd.im_ofReal (hf : AnalyticOnNhd ‚ÑÇ f (ofReal '' s)) :
    AnalyticOnNhd ‚Ñù (fun x : ‚Ñù ‚Ü¶ (f x).im) s :=
  ((Complex.imCLM.analyticOnNhd _).comp hf.restrictScalars (mapsTo_image f _)).comp
    (Complex.ofRealCLM.analyticOnNhd _) (mapsTo_image ofReal s)

end ReOfReal

section Real

variable {f : ‚Ñù ‚Üí ‚Ñù} {s : Set ‚Ñù} {x : ‚Ñù}

@[fun_prop]
lemma analyticAt_log (hx : 0 < x) : AnalyticAt ‚Ñù Real.log x := by
  have : Real.log = fun x : ‚Ñù ‚Ü¶ (Complex.log x).re := by ext x; exact (Complex.log_ofReal_re x).symm
  rw [this]
  refine AnalyticAt.re_ofReal <| analyticAt_clog ?_
  simp [hx]

lemma analyticOnNhd_log : AnalyticOnNhd ‚Ñù Real.log (Set.Ioi 0) := fun _ hx ‚Ü¶ analyticAt_log hx

lemma analyticOn_log : AnalyticOn ‚Ñù Real.log (Set.Ioi 0) := analyticOnNhd_log.analyticOn

@[fun_prop]
lemma AnalyticAt.log (fa : AnalyticAt ‚Ñù f x) (m : 0 < f x) :
    AnalyticAt ‚Ñù (fun z ‚Ü¶ Real.log (f z)) x :=
  (analyticAt_log m).comp fa

lemma AnalyticWithinAt.log (fa : AnalyticWithinAt ‚Ñù f s x) (m : 0 < f x) :
    AnalyticWithinAt ‚Ñù (fun z ‚Ü¶ Real.log (f z)) s x :=
  (analyticAt_log m).comp_analyticWithinAt fa

lemma AnalyticOnNhd.log (fs : AnalyticOnNhd ‚Ñù f s) (m : ‚àÄ x ‚àà s, 0 < f x) :
    AnalyticOnNhd ‚Ñù (fun z ‚Ü¶ Real.log (f z)) s :=
  fun z n ‚Ü¶ (analyticAt_log (m z n)).comp (fs z n)

lemma AnalyticOn.log (fs : AnalyticOn ‚Ñù f s) (m : ‚àÄ x ‚àà s, 0 < f x) :
    AnalyticOn ‚Ñù (fun z ‚Ü¶ Real.log (f z)) s :=
  fun z n ‚Ü¶ (analyticAt_log (m z n)).analyticWithinAt.comp (fs z n) m

end Real
