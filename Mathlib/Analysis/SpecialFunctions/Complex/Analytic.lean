/-
Copyright (c) 2024 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Analysis.Analytic.Composition
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.SpecialFunctions.Complex.LogDeriv

/-!
# Various complex special functions are analytic

`exp`, `log`, and `cpow` are analytic, since they are differentiable.
-/

open Complex Set
open scoped Topology

variable {E : Type} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]
variable {f g : E ‚Üí ‚ÑÇ} {z : ‚ÑÇ} {x : E} {s : Set E}

/-- `exp` is entire -/
theorem analyticOn_cexp : AnalyticOn ‚ÑÇ exp univ := by
  rw [analyticOn_univ_iff_differentiable]; exact differentiable_exp

/-- `exp` is analytic at any point -/
theorem analyticAt_cexp : AnalyticAt ‚ÑÇ exp z :=
  analyticOn_cexp z (mem_univ _)

/-- `exp ‚àò f` is analytic -/
theorem AnalyticAt.cexp (fa : AnalyticAt ‚ÑÇ f x) : AnalyticAt ‚ÑÇ (fun z ‚Ü¶ exp (f z)) x :=
  analyticAt_cexp.comp fa

/-- `exp ‚àò f` is analytic -/
theorem AnalyticOn.cexp (fs : AnalyticOn ‚ÑÇ f s) : AnalyticOn ‚ÑÇ (fun z ‚Ü¶ exp (f z)) s :=
  fun z n ‚Ü¶ analyticAt_cexp.comp (fs z n)

/-- `log` is analytic away from nonpositive reals -/
theorem analyticAt_clog (m : z ‚àà slitPlane) : AnalyticAt ‚ÑÇ log z := by
  rw [analyticAt_iff_eventually_differentiableAt]
  filter_upwards [isOpen_slitPlane.eventually_mem m]
  intro z m
  exact differentiableAt_id.clog m

/-- `log` is analytic away from nonpositive reals -/
theorem AnalyticAt.clog (fa : AnalyticAt ‚ÑÇ f x) (m : f x ‚àà slitPlane) :
    AnalyticAt ‚ÑÇ (fun z ‚Ü¶ log (f z)) x :=
  (analyticAt_clog m).comp fa

/-- `log` is analytic away from nonpositive reals -/
theorem AnalyticOn.clog (fs : AnalyticOn ‚ÑÇ f s) (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) :
    AnalyticOn ‚ÑÇ (fun z ‚Ü¶ log (f z)) s :=
  fun z n ‚Ü¶ (analyticAt_clog (m z n)).comp (fs z n)

/-- `f z ^ g z` is analytic if `f z` is not a nonpositive real -/
theorem AnalyticAt.cpow (fa : AnalyticAt ‚ÑÇ f x) (ga : AnalyticAt ‚ÑÇ g x)
    (m : f x ‚àà slitPlane) : AnalyticAt ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) x := by
  have e : (fun z ‚Ü¶ f z ^ g z) =·∂†[ùìù x] fun z ‚Ü¶ exp (log (f z) * g z) := by
    filter_upwards [(fa.continuousAt.eventually_ne (slitPlane_ne_zero m))]
    intro z fz
    simp only [fz, cpow_def, if_false]
  rw [analyticAt_congr e]
  exact ((fa.clog m).mul ga).cexp

/-- `f z ^ g z` is analytic if `f z` avoids nonpositive reals -/
theorem AnalyticOn.cpow (fs : AnalyticOn ‚ÑÇ f s) (gs : AnalyticOn ‚ÑÇ g s)
    (m : ‚àÄ z ‚àà s, f z ‚àà slitPlane) : AnalyticOn ‚ÑÇ (fun z ‚Ü¶ f z ^ g z) s :=
  fun z n ‚Ü¶ (fs z n).cpow (gs z n) (m z n)
