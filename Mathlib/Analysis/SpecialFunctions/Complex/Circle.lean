/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.SpecialFunctions.Complex.Log

#align_import analysis.special_functions.complex.circle from "leanprover-community/mathlib"@"f333194f5ecd1482191452c5ea60b37d4d6afa08"

/-!
# Maps on the unit circle

In this file we prove some basic lemmas about `expMapCircle` and the restriction of `Complex.arg`
to the unit circle. These two maps define a local equivalence between `circle` and `‚Ñù`, see
`circle.argLocalEquiv` and `circle.argEquiv`, that sends the whole circle to `(-œÄ, œÄ]`.
-/


open Complex Function Set

open Real

namespace circle

theorem injective_arg : Injective fun z : circle => arg z := fun z w h =>
  Subtype.ext <| ext_abs_arg ((abs_coe_circle z).trans (abs_coe_circle w).symm) h
#align circle.injective_arg circle.injective_arg

@[simp]
theorem arg_eq_arg {z w : circle} : arg z = arg w ‚Üî z = w :=
  injective_arg.eq_iff
#align circle.arg_eq_arg circle.arg_eq_arg

end circle

theorem arg_expMapCircle {x : ‚Ñù} (h‚ÇÅ : -œÄ < x) (h‚ÇÇ : x ‚â§ œÄ) : arg (expMapCircle x) = x := by
  rw [expMapCircle_apply, exp_mul_I, arg_cos_add_sin_mul_I ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©]
  -- üéâ no goals
#align arg_exp_map_circle arg_expMapCircle

@[simp]
theorem expMapCircle_arg (z : circle) : expMapCircle (arg z) = z :=
  circle.injective_arg <| arg_expMapCircle (neg_pi_lt_arg _) (arg_le_pi _)
#align exp_map_circle_arg expMapCircle_arg

namespace circle

/-- `Complex.arg ‚àò (‚Üë)` and `expMapCircle` define a local equivalence between `circle` and `‚Ñù`
with `source = Set.univ` and `target = Set.Ioc (-œÄ) œÄ`. -/
@[simps (config := { fullyApplied := false })]
noncomputable def argLocalEquiv : LocalEquiv circle ‚Ñù where
  toFun := arg ‚àò (‚Üë)
  invFun := expMapCircle
  source := univ
  target := Ioc (-œÄ) œÄ
  map_source' _ _ := ‚ü®neg_pi_lt_arg _, arg_le_pi _‚ü©
  map_target' := mapsTo_univ _ _
  left_inv' z _ := expMapCircle_arg z
  right_inv' _ hx := arg_expMapCircle hx.1 hx.2
#align circle.arg_local_equiv circle.argLocalEquiv

/-- `Complex.arg` and `expMapCircle` define an equivalence between `circle` and `(-œÄ, œÄ]`. -/
@[simps (config := { fullyApplied := false })]
noncomputable def argEquiv : circle ‚âÉ Ioc (-œÄ) œÄ where
  toFun z := ‚ü®arg z, neg_pi_lt_arg _, arg_le_pi _‚ü©
  invFun := expMapCircle ‚àò (‚Üë)
  left_inv _ := argLocalEquiv.left_inv trivial
  right_inv x := Subtype.ext <| argLocalEquiv.right_inv x.2
#align circle.arg_equiv circle.argEquiv

end circle

theorem leftInverse_expMapCircle_arg : LeftInverse expMapCircle (arg ‚àò (‚Üë)) :=
  expMapCircle_arg
#align left_inverse_exp_map_circle_arg leftInverse_expMapCircle_arg

theorem invOn_arg_expMapCircle : InvOn (arg ‚àò (‚Üë)) expMapCircle (Ioc (-œÄ) œÄ) univ :=
  circle.argLocalEquiv.symm.invOn
#align inv_on_arg_exp_map_circle invOn_arg_expMapCircle

theorem surjOn_expMapCircle_neg_pi_pi : SurjOn expMapCircle (Ioc (-œÄ) œÄ) univ :=
  circle.argLocalEquiv.symm.surjOn
#align surj_on_exp_map_circle_neg_pi_pi surjOn_expMapCircle_neg_pi_pi

theorem expMapCircle_eq_expMapCircle {x y : ‚Ñù} :
    expMapCircle x = expMapCircle y ‚Üî ‚àÉ m : ‚Ñ§, x = y + m * (2 * œÄ) := by
  rw [Subtype.ext_iff, expMapCircle_apply, expMapCircle_apply, exp_eq_exp_iff_exists_int]
  -- ‚ä¢ (‚àÉ n, ‚Üëx * I = ‚Üëy * I + ‚Üën * (2 * ‚ÜëœÄ * I)) ‚Üî ‚àÉ m, x = y + ‚Üëm * (2 * œÄ)
  refine' exists_congr fun n => _
  -- ‚ä¢ ‚Üëx * I = ‚Üëy * I + ‚Üën * (2 * ‚ÜëœÄ * I) ‚Üî x = y + ‚Üën * (2 * œÄ)
  rw [‚Üê mul_assoc, ‚Üê add_mul, mul_left_inj' I_ne_zero]
  -- ‚ä¢ ‚Üëx = ‚Üëy + ‚Üën * (2 * ‚ÜëœÄ) ‚Üî x = y + ‚Üën * (2 * œÄ)
  norm_cast
  -- üéâ no goals
#align exp_map_circle_eq_exp_map_circle expMapCircle_eq_expMapCircle

theorem periodic_expMapCircle : Periodic expMapCircle (2 * œÄ) := fun z =>
  expMapCircle_eq_expMapCircle.2 ‚ü®1, by rw [Int.cast_one, one_mul]‚ü©
                                        -- üéâ no goals
#align periodic_exp_map_circle periodic_expMapCircle

@[simp]
theorem expMapCircle_two_pi : expMapCircle (2 * œÄ) = 1 :=
  periodic_expMapCircle.eq.trans expMapCircle_zero
#align exp_map_circle_two_pi expMapCircle_two_pi

theorem expMapCircle_sub_two_pi (x : ‚Ñù) : expMapCircle (x - 2 * œÄ) = expMapCircle x :=
  periodic_expMapCircle.sub_eq x
#align exp_map_circle_sub_two_pi expMapCircle_sub_two_pi

theorem expMapCircle_add_two_pi (x : ‚Ñù) : expMapCircle (x + 2 * œÄ) = expMapCircle x :=
  periodic_expMapCircle x
#align exp_map_circle_add_two_pi expMapCircle_add_two_pi

/-- `expMapCircle`, applied to a `Real.Angle`. -/
noncomputable def Real.Angle.expMapCircle (Œ∏ : Real.Angle) : circle :=
  periodic_expMapCircle.lift Œ∏
#align real.angle.exp_map_circle Real.Angle.expMapCircle

@[simp]
theorem Real.Angle.expMapCircle_coe (x : ‚Ñù) : Real.Angle.expMapCircle x = _root_.expMapCircle x :=
  rfl
#align real.angle.exp_map_circle_coe Real.Angle.expMapCircle_coe

theorem Real.Angle.coe_expMapCircle (Œ∏ : Real.Angle) :
    (Œ∏.expMapCircle : ‚ÑÇ) = Œ∏.cos + Œ∏.sin * I := by
  induction Œ∏ using Real.Angle.induction_on
  -- ‚ä¢ ‚Üë(expMapCircle ‚Üëx‚úù) = ‚Üë(cos ‚Üëx‚úù) + ‚Üë(sin ‚Üëx‚úù) * I
  simp [Complex.exp_mul_I]
  -- üéâ no goals
#align real.angle.coe_exp_map_circle Real.Angle.coe_expMapCircle

@[simp]
theorem Real.Angle.expMapCircle_zero : Real.Angle.expMapCircle 0 = 1 := by
  rw [‚Üê Real.Angle.coe_zero, Real.Angle.expMapCircle_coe, _root_.expMapCircle_zero]
  -- üéâ no goals
#align real.angle.exp_map_circle_zero Real.Angle.expMapCircle_zero

@[simp]
theorem Real.Angle.expMapCircle_neg (Œ∏ : Real.Angle) :
    Real.Angle.expMapCircle (-Œ∏) = (Real.Angle.expMapCircle Œ∏)‚Åª¬π := by
  induction Œ∏ using Real.Angle.induction_on
  -- ‚ä¢ expMapCircle (-‚Üëx‚úù) = (expMapCircle ‚Üëx‚úù)‚Åª¬π
  simp_rw [‚Üê Real.Angle.coe_neg, Real.Angle.expMapCircle_coe, _root_.expMapCircle_neg]
  -- üéâ no goals
#align real.angle.exp_map_circle_neg Real.Angle.expMapCircle_neg

@[simp]
theorem Real.Angle.expMapCircle_add (Œ∏‚ÇÅ Œ∏‚ÇÇ : Real.Angle) : Real.Angle.expMapCircle (Œ∏‚ÇÅ + Œ∏‚ÇÇ) =
    Real.Angle.expMapCircle Œ∏‚ÇÅ * Real.Angle.expMapCircle Œ∏‚ÇÇ := by
  induction Œ∏‚ÇÅ using Real.Angle.induction_on
  -- ‚ä¢ expMapCircle (‚Üëx‚úù + Œ∏‚ÇÇ) = expMapCircle ‚Üëx‚úù * expMapCircle Œ∏‚ÇÇ
  induction Œ∏‚ÇÇ using Real.Angle.induction_on
  -- ‚ä¢ expMapCircle (‚Üëx‚úù¬π + ‚Üëx‚úù) = expMapCircle ‚Üëx‚úù¬π * expMapCircle ‚Üëx‚úù
  exact _root_.expMapCircle_add _ _
  -- üéâ no goals
#align real.angle.exp_map_circle_add Real.Angle.expMapCircle_add

@[simp]
theorem Real.Angle.arg_expMapCircle (Œ∏ : Real.Angle) :
    (arg (Real.Angle.expMapCircle Œ∏) : Real.Angle) = Œ∏ := by
  induction Œ∏ using Real.Angle.induction_on
  -- ‚ä¢ ‚Üë(arg ‚Üë(expMapCircle ‚Üëx‚úù)) = ‚Üëx‚úù
  rw [Real.Angle.expMapCircle_coe, expMapCircle_apply, exp_mul_I, ‚Üê ofReal_cos, ‚Üê ofReal_sin, ‚Üê
    Real.Angle.cos_coe, ‚Üê Real.Angle.sin_coe, arg_cos_add_sin_mul_I_coe_angle]
#align real.angle.arg_exp_map_circle Real.Angle.arg_expMapCircle
