/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.SpecialFunctions.Gamma.Basic
import Mathlib.Analysis.SpecialFunctions.PolarCoord
import Mathlib.Analysis.Convex.Complex
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.Analysis.Fourier.PoissonSummation

#align_import analysis.special_functions.gaussian from "leanprover-community/mathlib"@"7982767093ae38cba236487f9c9dd9cd99f63c16"

/-!
# Gaussian integral

We prove various versions of the formula for the Gaussian integral:
* `integral_gaussian`: for real `b` we have `‚à´ x:‚Ñù, exp (-b * x^2) = sqrt (œÄ / b)`.
* `integral_gaussian_complex`: for complex `b` with `0 < re b` we have
  `‚à´ x:‚Ñù, exp (-b * x^2) = (œÄ / b) ^ (1 / 2)`.
* `integral_gaussian_Ioi` and `integral_gaussian_complex_Ioi`: variants for integrals over `Ioi 0`.
* `Complex.Gamma_one_half_eq`: the formula `Œì (1 / 2) = ‚àöœÄ`.

We also prove, more generally, that the Fourier transform of the Gaussian is another Gaussian:

* `integral_cexp_neg_mul_sq_add_const`: for all complex `b` and `c` with `0 < re b` we have
  `‚à´ (x : ‚Ñù), exp (-b * (x + c) ^ 2) = (œÄ / b) ^ (1 / 2)`.
* `fourier_transform_gaussian`: for all complex `b` and `t` with `0 < re b`, we have
  `‚à´ x:‚Ñù, exp (I * t * x) * exp (-b * x^2) = (œÄ / b) ^ (1 / 2) * exp (-t ^ 2 / (4 * b))`.
* `fourier_transform_gaussian_pi`: a variant with `b` and `t` scaled to give a more symmetric
  statement, and formulated in terms of the Fourier transform operator `ùìï`.

As an application, in `Real.tsum_exp_neg_mul_int_sq` and `cCmplex.tsum_exp_neg_mul_int_sq`, we use
Poisson summation to prove the identity
`‚àë' (n : ‚Ñ§), exp (-œÄ * a * n ^ 2) = 1 / a ^ (1 / 2) * ‚àë' (n : ‚Ñ§), exp (-œÄ / a * n ^ 2)`
for positive real `a`, or complex `a` with positive real part. (See also
`NumberTheory.ModularForms.JacobiTheta`.)
-/

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

noncomputable section

open Real Set MeasureTheory Filter Asymptotics

open scoped Real Topology FourierTransform

open Complex hiding exp continuous_exp abs_of_nonneg sq_abs

theorem exp_neg_mul_rpow_isLittleO_exp_neg {p b : ‚Ñù} (hb : 0 < b) (hp : 1 < p) :
    (fun x : ‚Ñù => exp (- b * x ^ p)) =o[atTop] fun x : ‚Ñù => exp (-x) := by
  rw [isLittleO_exp_comp_exp_comp]
  suffices Tendsto (fun x => x * (b * x ^ (p - 1) + -1)) atTop atTop by
    refine Tendsto.congr' ?_ this
    refine eventuallyEq_of_mem (Ioi_mem_atTop (0 : ‚Ñù)) (fun x hx => ?_)
    rw [rpow_sub_one (ne_of_gt hx)]
    field_simp [(by exact ne_of_gt hx : x ‚â† 0)]
    ring
  apply Tendsto.atTop_mul_atTop tendsto_id
  refine tendsto_atTop_add_const_right atTop (-1 : ‚Ñù) ?_
  exact Tendsto.const_mul_atTop hb (tendsto_rpow_atTop (by linarith))

theorem exp_neg_mul_sq_isLittleO_exp_neg {b : ‚Ñù} (hb : 0 < b) :
    (fun x : ‚Ñù => exp (-b * x ^ 2)) =o[atTop] fun x : ‚Ñù => exp (-x) := by
  simp_rw [‚Üê rpow_two]
  exact exp_neg_mul_rpow_isLittleO_exp_neg hb one_lt_two
#align exp_neg_mul_sq_is_o_exp_neg exp_neg_mul_sq_isLittleO_exp_neg

theorem rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg (s : ‚Ñù) {b p : ‚Ñù} (hp : 1 < p) (hb : 0 < b) :
    (fun x : ‚Ñù => x ^ s * exp (- b * x ^ p)) =o[atTop] fun x : ‚Ñù => exp (-(1 / 2) * x) := by
  apply ((isBigO_refl (fun x : ‚Ñù => x ^ s) atTop).mul_isLittleO
      (exp_neg_mul_rpow_isLittleO_exp_neg hb hp)).trans
  simpa only [mul_comm] using Real.Gamma_integrand_isLittleO s

theorem rpow_mul_exp_neg_mul_sq_isLittleO_exp_neg {b : ‚Ñù} (hb : 0 < b) (s : ‚Ñù) :
    (fun x : ‚Ñù => x ^ s * exp (-b * x ^ 2)) =o[atTop] fun x : ‚Ñù => exp (-(1 / 2) * x) := by
  simp_rw [‚Üê rpow_two]
  exact rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg s one_lt_two hb
#align rpow_mul_exp_neg_mul_sq_is_o_exp_neg rpow_mul_exp_neg_mul_sq_isLittleO_exp_neg

theorem integrableOn_rpow_mul_exp_neg_rpow {p s : ‚Ñù} (hs : -1 < s) (hp : 1 ‚â§ p) :
    IntegrableOn (fun x : ‚Ñù => x ^ s * exp (- x ^ p)) (Ioi 0) := by
  obtain hp | hp := le_iff_lt_or_eq.mp hp
  ¬∑ have h_exp : ‚àÄ x, ContinuousAt (fun x => exp (- x)) x :=
        fun x => (by exact continuousAt_neg : ContinuousAt (fun x => -x) x).exp
    rw [‚Üê Ioc_union_Ioi_eq_Ioi (zero_le_one : (0 : ‚Ñù) ‚â§ 1), integrableOn_union]
    constructor
    ¬∑ rw [‚Üê integrableOn_Icc_iff_integrableOn_Ioc]
      refine IntegrableOn.mul_continuousOn ?_ ?_ isCompact_Icc
      ¬∑ refine (intervalIntegrable_iff_integrable_Icc_of_le zero_le_one).mp ?_
        exact intervalIntegral.intervalIntegrable_rpow' hs
      ¬∑ intro x _
        change ContinuousWithinAt ((fun x => exp (- x)) ‚àò (fun x => x ^ p)) (Icc 0 1) x
        refine ContinuousAt.comp_continuousWithinAt (h_exp _) ?_
        exact continuousWithinAt_id.rpow_const (Or.inr (le_of_lt (lt_trans zero_lt_one hp)))
    ¬∑ have h_rpow : ‚àÄ (x r : ‚Ñù), x ‚àà Ici 1 ‚Üí ContinuousWithinAt (fun x => x ^ r) (Ici 1) x := by
        intro _ _ hx
        refine continuousWithinAt_id.rpow_const (Or.inl ?_)
        exact ne_of_gt (lt_of_lt_of_le zero_lt_one hx)
      refine integrable_of_isBigO_exp_neg (by norm_num : (0:‚Ñù) < 1 / 2)
        (ContinuousOn.mul (fun x hx => h_rpow x s hx) (fun x hx => ?_)) (IsLittleO.isBigO ?_)
      ¬∑ change ContinuousWithinAt ((fun x => exp (- x)) ‚àò (fun x => x ^ p)) (Ici 1) x
        exact ContinuousAt.comp_continuousWithinAt (h_exp _) (h_rpow x p hx)
      ¬∑ convert rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg s hp (by norm_num : (0:‚Ñù) < 1) using 3
        rw [neg_mul, one_mul]
  ¬∑ simp_rw [‚Üê hp, Real.rpow_one]
    convert Real.GammaIntegral_convergent (by linarith : 0 < s + 1) using 2
    rw [add_sub_cancel, mul_comm]

theorem integrableOn_rpow_mul_exp_neg_mul_rpow {p s b : ‚Ñù} (hs : -1 < s) (hp : 1 ‚â§ p) (hb : 0 < b) :
    IntegrableOn (fun x : ‚Ñù => x ^ s * exp (- b * x ^ p)) (Ioi 0) := by
  have hib : 0 < b ^ (-p‚Åª¬π) := rpow_pos_of_pos hb _
  suffices IntegrableOn (fun x ‚Ü¶ (b ^ (-p‚Åª¬π)) ^ s * (x ^ s * exp (-x ^ p))) (Ioi 0) by
    rw [show 0 = b ^ (-p‚Åª¬π) * 0 by rw [mul_zero], ‚Üê integrableOn_Ioi_comp_mul_left_iff _ _ hib]
    refine this.congr_fun (fun _ hx => ?_) measurableSet_Ioi
    rw [‚Üê mul_assoc, mul_rpow, mul_rpow, ‚Üê rpow_mul (z := p), neg_mul, neg_mul, inv_mul_cancel,
      rpow_neg_one, mul_inv_cancel_left‚ÇÄ]
    all_goals linarith [mem_Ioi.mp hx]
  refine Integrable.const_mul ?_ _
  rw [‚Üê integrableOn_def]
  exact integrableOn_rpow_mul_exp_neg_rpow hs hp

theorem integrableOn_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
    IntegrableOn (fun x : ‚Ñù => x ^ s * exp (-b * x ^ 2)) (Ioi 0) := by
  simp_rw [‚Üê rpow_two]
  exact integrableOn_rpow_mul_exp_neg_mul_rpow hs one_le_two hb
#align integrable_on_rpow_mul_exp_neg_mul_sq integrableOn_rpow_mul_exp_neg_mul_sq

theorem integrable_rpow_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) {s : ‚Ñù} (hs : -1 < s) :
    Integrable fun x : ‚Ñù => x ^ s * exp (-b * x ^ 2) := by
  rw [‚Üê integrableOn_univ, ‚Üê @Iio_union_Ici _ _ (0 : ‚Ñù), integrableOn_union,
    integrableOn_Ici_iff_integrableOn_Ioi]
  refine' ‚ü®_, integrableOn_rpow_mul_exp_neg_mul_sq hb hs‚ü©
  rw [‚Üê (Measure.measurePreserving_neg (volume : Measure ‚Ñù)).integrableOn_comp_preimage
      (Homeomorph.neg ‚Ñù).measurableEmbedding]
  simp only [Function.comp, neg_sq, neg_preimage, preimage_neg_Iio, neg_neg, neg_zero]
  apply Integrable.mono' (integrableOn_rpow_mul_exp_neg_mul_sq hb hs)
  ¬∑ apply Measurable.aestronglyMeasurable
    exact (measurable_id'.neg.pow measurable_const).mul
      ((measurable_id'.pow measurable_const).const_mul (-b)).exp
  ¬∑ have : MeasurableSet (Ioi (0 : ‚Ñù)) := measurableSet_Ioi
    filter_upwards [ae_restrict_mem this] with x hx
    have h'x : 0 ‚â§ x := le_of_lt hx
    rw [Real.norm_eq_abs, abs_mul, abs_of_nonneg (exp_pos _).le]
    apply mul_le_mul_of_nonneg_right _ (exp_pos _).le
    simpa [abs_of_nonneg h'x] using abs_rpow_le_abs_rpow (-x) s
#align integrable_rpow_mul_exp_neg_mul_sq integrable_rpow_mul_exp_neg_mul_sq

theorem integrable_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) : Integrable fun x : ‚Ñù => exp (-b * x ^ 2) :=
  by simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : ‚Ñù) < 0)
#align integrable_exp_neg_mul_sq integrable_exp_neg_mul_sq

theorem integrableOn_Ioi_exp_neg_mul_sq_iff {b : ‚Ñù} :
    IntegrableOn (fun x : ‚Ñù => exp (-b * x ^ 2)) (Ioi 0) ‚Üî 0 < b := by
  refine' ‚ü®fun h => _, fun h => (integrable_exp_neg_mul_sq h).integrableOn‚ü©
  by_contra' hb
  have : ‚à´‚Åª _ : ‚Ñù in Ioi 0, 1 ‚â§ ‚à´‚Åª x : ‚Ñù in Ioi 0, ‚Äñexp (-b * x ^ 2)‚Äñ‚Çä := by
    apply lintegral_mono (fun x ‚Ü¶ _)
    simp only [neg_mul, ENNReal.one_le_coe_iff, ‚Üê toNNReal_one, toNNReal_le_iff_le_coe,
      Real.norm_of_nonneg (exp_pos _).le, coe_nnnorm, one_le_exp_iff, Right.nonneg_neg_iff]
    exact fun x ‚Ü¶ mul_nonpos_of_nonpos_of_nonneg hb (sq_nonneg x)
  simpa using this.trans_lt h.2
#align integrable_on_Ioi_exp_neg_mul_sq_iff integrableOn_Ioi_exp_neg_mul_sq_iff

theorem integrable_exp_neg_mul_sq_iff {b : ‚Ñù} :
    (Integrable fun x : ‚Ñù => exp (-b * x ^ 2)) ‚Üî 0 < b :=
  ‚ü®fun h => integrableOn_Ioi_exp_neg_mul_sq_iff.mp h.integrableOn, integrable_exp_neg_mul_sq‚ü©
#align integrable_exp_neg_mul_sq_iff integrable_exp_neg_mul_sq_iff

theorem integrable_mul_exp_neg_mul_sq {b : ‚Ñù} (hb : 0 < b) :
    Integrable fun x : ‚Ñù => x * exp (-b * x ^ 2) := by
  simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by norm_num : (-1 : ‚Ñù) < 1)
#align integrable_mul_exp_neg_mul_sq integrable_mul_exp_neg_mul_sq

theorem norm_cexp_neg_mul_sq (b : ‚ÑÇ) (x : ‚Ñù) :
    ‚ÄñComplex.exp (-b * (x : ‚ÑÇ) ^ 2)‚Äñ = exp (-b.re * x ^ 2) := by
  rw [Complex.norm_eq_abs, Complex.abs_exp, ‚ÜêofReal_pow, mul_comm (-b) _, ofReal_mul_re, neg_re,
    mul_comm]
#align norm_cexp_neg_mul_sq norm_cexp_neg_mul_sq

theorem integrable_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
    Integrable fun x : ‚Ñù => cexp (-b * (x : ‚ÑÇ) ^ 2) := by
  refine' ‚ü®(Complex.continuous_exp.comp
    (continuous_const.mul (continuous_ofReal.pow 2))).aestronglyMeasurable, _‚ü©
  rw [‚Üê hasFiniteIntegral_norm_iff]
  simp_rw [norm_cexp_neg_mul_sq]
  exact (integrable_exp_neg_mul_sq hb).2
#align integrable_cexp_neg_mul_sq integrable_cexp_neg_mul_sq

theorem integrable_mul_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
    Integrable fun x : ‚Ñù => ‚Üëx * cexp (-b * (x : ‚ÑÇ) ^ 2) := by
  refine' ‚ü®(continuous_ofReal.mul (Complex.continuous_exp.comp _)).aestronglyMeasurable, _‚ü©
  ¬∑ exact continuous_const.mul (continuous_ofReal.pow 2)
  have := (integrable_mul_exp_neg_mul_sq hb).hasFiniteIntegral
  rw [‚Üê hasFiniteIntegral_norm_iff] at this ‚ä¢
  convert this
  rw [norm_mul, norm_mul, norm_cexp_neg_mul_sq b, Complex.norm_eq_abs, abs_ofReal, Real.norm_eq_abs,
    norm_of_nonneg (exp_pos _).le]
#align integrable_mul_cexp_neg_mul_sq integrable_mul_cexp_neg_mul_sq

theorem integral_mul_cexp_neg_mul_sq {b : ‚ÑÇ} (hb : 0 < b.re) :
    ‚à´ r : ‚Ñù in Ioi 0, (r : ‚ÑÇ) * cexp (-b * (r : ‚ÑÇ) ^ 2) = (2 * b)‚Åª¬π := by
  have hb' : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  have A : ‚àÄ x : ‚ÑÇ, HasDerivAt (fun x => -(2 * b)‚Åª¬π * cexp (-b * x ^ 2))
    (x * cexp (-b * x ^ 2)) x := by
    intro x
    convert ((hasDerivAt_pow 2 x).const_mul (-b)).cexp.const_mul (-(2 * b)‚Åª¬π) using 1
    field_simp [hb']
    ring
  have B : Tendsto (fun y : ‚Ñù ‚Ü¶ -(2 * b)‚Åª¬π * cexp (-b * (y : ‚ÑÇ) ^ 2))
    atTop (ùìù (-(2 * b)‚Åª¬π * 0)) := by
    refine' Tendsto.const_mul _ (tendsto_zero_iff_norm_tendsto_zero.mpr _)
    simp_rw [norm_cexp_neg_mul_sq b]
    exact tendsto_exp_atBot.comp (Tendsto.neg_const_mul_atTop (neg_lt_zero.2 hb)
        (tendsto_pow_atTop two_ne_zero))
  convert integral_Ioi_of_hasDerivAt_of_tendsto' (fun x _ => (A ‚Üëx).comp_ofReal)
    (integrable_mul_cexp_neg_mul_sq hb).integrableOn B using 1
  simp only [mul_zero, ofReal_zero, zero_pow', Ne.def, bit0_eq_zero, Nat.one_ne_zero,
    not_false_iff, Complex.exp_zero, mul_one, sub_neg_eq_add, zero_add]
#align integral_mul_cexp_neg_mul_sq integral_mul_cexp_neg_mul_sq

/-- The *square* of the Gaussian integral `‚à´ x:‚Ñù, exp (-b * x^2)` is equal to `œÄ / b`. -/
theorem integral_gaussian_sq_complex {b : ‚ÑÇ} (hb : 0 < b.re) :
    (‚à´ x : ‚Ñù, cexp (-b * (x : ‚ÑÇ) ^ 2)) ^ 2 = œÄ / b := by
  /- We compute `(‚à´ exp (-b x^2))^2` as an integral over `‚Ñù^2`, and then make a polar change
  of coordinates. We are left with `‚à´ r * exp (-b r^2)`, which has been computed in
  `integral_mul_cexp_neg_mul_sq` using the fact that this function has an obvious primitive. -/
  calc
    (‚à´ x : ‚Ñù, cexp (-b * (x : ‚ÑÇ) ^ 2)) ^ 2 =
        ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (-b * (p.1 : ‚ÑÇ) ^ 2) * cexp (-b * (p.2 : ‚ÑÇ) ^ 2) :=
      by rw [pow_two, ‚Üê integral_prod_mul]; rfl
    _ = ‚à´ p : ‚Ñù √ó ‚Ñù, cexp (-b * ((p.1 : ‚ÑÇ)^ 2 + (p.2 : ‚ÑÇ) ^ 2)) := by
      congr
      ext1 p
      rw [‚Üê Complex.exp_add, mul_add]
    _ = ‚à´ p in polarCoord.target, p.1 ‚Ä¢
        cexp (-b * ((p.1 * Complex.cos p.2) ^ 2 + (p.1 * Complex.sin p.2) ^ 2)) := by
      rw [‚Üê integral_comp_polarCoord_symm]
      simp only [polarCoord_symm_apply, ofReal_mul, ofReal_cos, ofReal_sin]
    _ = (‚à´ r in Ioi (0 : ‚Ñù), r * cexp (-b * (r : ‚ÑÇ) ^ 2)) * ‚à´ Œ∏ in Ioo (-œÄ) œÄ, 1 := by
      rw [‚Üê set_integral_prod_mul]
      congr with p : 1
      rw [mul_one]
      congr
      conv_rhs => rw [‚Üê one_mul ((p.1 : ‚ÑÇ) ^ 2), ‚Üê sin_sq_add_cos_sq (p.2 : ‚ÑÇ)]
      ring
    _ = ‚ÜëœÄ / b := by
      have : 0 ‚â§ œÄ + œÄ := by linarith [Real.pi_pos]
      simp only [integral_const, Measure.restrict_apply', measurableSet_Ioo, univ_inter, volume_Ioo,
        sub_neg_eq_add, ENNReal.toReal_ofReal, this]
      rw [‚Üê two_mul, real_smul, mul_one, ofReal_mul, ofReal_ofNat, integral_mul_cexp_neg_mul_sq hb]
      field_simp [(by contrapose! hb; rw [hb, zero_re] : b ‚â† 0)]
      ring
#align integral_gaussian_sq_complex integral_gaussian_sq_complex

theorem integral_gaussian (b : ‚Ñù) : ‚à´ x : ‚Ñù, exp (-b * x ^ 2) = sqrt (œÄ / b) := by
  -- First we deal with the crazy case where `b ‚â§ 0`: then both sides vanish.
  rcases le_or_lt b 0 with (hb | hb)
  ¬∑ rw [integral_undef, sqrt_eq_zero_of_nonpos]
    ¬∑ exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb
    ¬∑ simpa only [not_lt, integrable_exp_neg_mul_sq_iff] using hb
  -- Assume now `b > 0`. Then both sides are non-negative and their squares agree.
  refine' (sq_eq_sq _ (sqrt_nonneg _)).1 _
  ¬∑ exact integral_nonneg fun x => (exp_pos _).le
  rw [‚Üê ofReal_inj, ofReal_pow, ‚Üê coe_algebraMap, IsROrC.algebraMap_eq_ofReal, ‚Üê integral_ofReal,
    sq_sqrt (div_pos pi_pos hb).le, ‚Üê IsROrC.algebraMap_eq_ofReal, coe_algebraMap, ofReal_div]
  convert integral_gaussian_sq_complex (by rwa [ofReal_re] : 0 < (b : ‚ÑÇ).re) with _ x
  rw [ofReal_exp, ofReal_mul, ofReal_pow, ofReal_neg]
#align integral_gaussian integral_gaussian

theorem continuousAt_gaussian_integral (b : ‚ÑÇ) (hb : 0 < re b) :
    ContinuousAt (fun c : ‚ÑÇ => ‚à´ x : ‚Ñù, cexp (-c * (x : ‚ÑÇ) ^ 2)) b := by
  let f : ‚ÑÇ ‚Üí ‚Ñù ‚Üí ‚ÑÇ := fun (c : ‚ÑÇ) (x : ‚Ñù) => cexp (-c * (x : ‚ÑÇ) ^ 2)
  obtain ‚ü®d, hd, hd'‚ü© := exists_between hb
  have f_meas : ‚àÄ c : ‚ÑÇ, AEStronglyMeasurable (f c) volume := fun c => by
    apply Continuous.aestronglyMeasurable
    exact Complex.continuous_exp.comp (continuous_const.mul (continuous_ofReal.pow 2))
  have f_cts : ‚àÄ x : ‚Ñù, ContinuousAt (fun c => f c x) b := fun x =>
    (Complex.continuous_exp.comp (continuous_id'.neg.mul continuous_const)).continuousAt
  have f_le_bd : ‚àÄ·∂† c : ‚ÑÇ in ùìù b, ‚àÄ·µê x : ‚Ñù, ‚Äñf c x‚Äñ ‚â§ exp (-d * x ^ 2) := by
    refine' eventually_of_mem ((continuous_re.isOpen_preimage _ isOpen_Ioi).mem_nhds hd') _
    refine' fun c hc => ae_of_all _ fun x => _
    rw [norm_cexp_neg_mul_sq, exp_le_exp]
    exact mul_le_mul_of_nonneg_right (neg_le_neg (le_of_lt hc)) (sq_nonneg _)
  exact
    continuousAt_of_dominated (eventually_of_forall f_meas) f_le_bd (integrable_exp_neg_mul_sq hd)
      (ae_of_all _ f_cts)
#align continuous_at_gaussian_integral continuousAt_gaussian_integral

theorem integral_gaussian_complex {b : ‚ÑÇ} (hb : 0 < re b) :
    ‚à´ x : ‚Ñù, cexp (-b * (x : ‚ÑÇ) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) := by
  have nv : ‚àÄ {b : ‚ÑÇ}, 0 < re b ‚Üí b ‚â† 0 := by intro b hb; contrapose! hb; rw [hb]; simp
  apply
    (convex_halfspace_re_gt 0).isPreconnected.eq_of_sq_eq ?_ ?_ (fun c hc => ?_) (fun {c} hc => ?_)
      (by simp : 0 < re (1 : ‚ÑÇ)) ?_ hb
  ¬∑ -- integral is continuous
    exact ContinuousAt.continuousOn continuousAt_gaussian_integral
  ¬∑ -- `(œÄ / b) ^ (1 / 2 : ‚ÑÇ)` is continuous
    refine'
      ContinuousAt.continuousOn fun b hb =>
        (continuousAt_cpow_const (Or.inl _)).comp (continuousAt_const.div continuousAt_id (nv hb))
    rw [div_re, ofReal_im, ofReal_re, zero_mul, zero_div, add_zero]
    exact div_pos (mul_pos pi_pos hb) (normSq_pos.mpr (nv hb))
  ¬∑ -- equality at 1
    have : ‚àÄ x : ‚Ñù, cexp (-(1 : ‚ÑÇ) * (x : ‚ÑÇ) ^ 2) = exp (-(1 : ‚Ñù) * x ^ 2) := by
      intro x
      simp only [ofReal_exp, neg_mul, one_mul, ofReal_neg, ofReal_pow]
    simp_rw [this, ‚Üê coe_algebraMap, IsROrC.algebraMap_eq_ofReal, integral_ofReal,
      ‚Üê IsROrC.algebraMap_eq_ofReal, coe_algebraMap]
    conv_rhs =>
      congr
      ¬∑ rw [‚Üê ofReal_one, ‚Üê ofReal_div]
      ¬∑ rw [‚Üê ofReal_one, ‚Üê ofReal_ofNat, ‚Üê ofReal_div]
    rw [‚Üê ofReal_cpow, ofReal_inj]
    convert integral_gaussian (1 : ‚Ñù) using 1
    ¬∑ rw [sqrt_eq_rpow]
    ¬∑ rw [div_one]; exact pi_pos.le
  ¬∑ -- squares of both sides agree
    dsimp only [Pi.pow_apply]
    rw [integral_gaussian_sq_complex hc, sq]
    conv_lhs => rw [‚Üê cpow_one (‚ÜëœÄ / c)]
    rw [‚Üê cpow_add _ _ (div_ne_zero (ofReal_ne_zero.mpr pi_ne_zero) (nv hc))]
    norm_num
  ¬∑ -- RHS doesn't vanish
    rw [Ne.def, cpow_eq_zero_iff, not_and_or]
    exact Or.inl (div_ne_zero (ofReal_ne_zero.mpr pi_ne_zero) (nv hc))
#align integral_gaussian_complex integral_gaussian_complex

-- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`, for complex `b`.
theorem integral_gaussian_complex_Ioi {b : ‚ÑÇ} (hb : 0 < re b) :
    ‚à´ x : ‚Ñù in Ioi 0, cexp (-b * (x : ‚ÑÇ) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) / 2 := by
  have full_integral := integral_gaussian_complex hb
  have : MeasurableSet (Ioi (0 : ‚Ñù)) := measurableSet_Ioi
  rw [‚Üê integral_add_compl this (integrable_cexp_neg_mul_sq hb), compl_Ioi] at full_integral
  suffices ‚à´ x : ‚Ñù in Iic 0, cexp (-b * (x : ‚ÑÇ) ^ 2) = ‚à´ x : ‚Ñù in Ioi 0, cexp (-b * (x : ‚ÑÇ) ^ 2) by
    rw [this, ‚Üê mul_two] at full_integral
    rwa [eq_div_iff]; exact two_ne_zero
  have : ‚àÄ c : ‚Ñù, ‚à´ x in (0 : ‚Ñù)..c, cexp (-b * (x : ‚ÑÇ) ^ 2) =
      ‚à´ x in -c..0, cexp (-b * (x : ‚ÑÇ) ^ 2) := by
    intro c
    have := intervalIntegral.integral_comp_sub_left (a := 0) (b := c)
      (fun x => cexp (-b * (x : ‚ÑÇ) ^ 2)) 0
    simpa [zero_sub, neg_sq, neg_zero] using this
  have t1 :=
    intervalIntegral_tendsto_integral_Ioi 0 (integrable_cexp_neg_mul_sq hb).integrableOn tendsto_id
  have t2 :
    Tendsto (fun c : ‚Ñù => ‚à´ x : ‚Ñù in (0 : ‚Ñù)..c, cexp (-b * (x : ‚ÑÇ) ^ 2)) atTop
      (ùìù (‚à´ x : ‚Ñù in Iic 0, cexp (-b * (x : ‚ÑÇ) ^ 2))) := by
    simp_rw [this]
    refine' intervalIntegral_tendsto_integral_Iic _ _ tendsto_neg_atTop_atBot
    apply (integrable_cexp_neg_mul_sq hb).integrableOn
  exact tendsto_nhds_unique t2 t1
#align integral_gaussian_complex_Ioi integral_gaussian_complex_Ioi

-- The Gaussian integral on the half-line, `‚à´ x in Ioi 0, exp (-b * x^2)`, for real `b`.
theorem integral_gaussian_Ioi (b : ‚Ñù) :
    ‚à´ x in Ioi (0 : ‚Ñù), exp (-b * x ^ 2) = sqrt (œÄ / b) / 2 := by
  rcases le_or_lt b 0 with (hb | hb)
  ¬∑ rw [integral_undef, sqrt_eq_zero_of_nonpos, zero_div]
    exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb
    rwa [‚Üê IntegrableOn, integrableOn_Ioi_exp_neg_mul_sq_iff, not_lt]
  rw [‚Üê IsROrC.ofReal_inj (K := ‚ÑÇ), ‚Üê integral_ofReal, ‚Üê IsROrC.algebraMap_eq_ofReal,
    coe_algebraMap]
  convert integral_gaussian_complex_Ioi (by rwa [ofReal_re] : 0 < (b : ‚ÑÇ).re)
  ¬∑ simp
  ¬∑ rw [sqrt_eq_rpow, ‚Üê ofReal_div, ofReal_div, ofReal_cpow]
    norm_num
    exact (div_pos pi_pos hb).le
#align integral_gaussian_Ioi integral_gaussian_Ioi

/-- The special-value formula `Œì(1/2) = ‚àöœÄ`, which is equivalent to the Gaussian integral. -/
theorem Real.Gamma_one_half_eq : Real.Gamma (1 / 2) = sqrt œÄ := by
  rw [Gamma_eq_integral one_half_pos, ‚Üê integral_comp_rpow_Ioi_of_pos zero_lt_two]
  convert congr_arg (fun x : ‚Ñù => 2 * x) (integral_gaussian_Ioi 1) using 1
  ¬∑ rw [‚Üê integral_mul_left]
    refine' set_integral_congr measurableSet_Ioi fun x hx => _
    dsimp only
    have : (x ^ (2 : ‚Ñù)) ^ (1 / (2 : ‚Ñù) - 1) = x‚Åª¬π := by
      rw [‚Üê rpow_mul (le_of_lt hx)]
      norm_num
      rw [rpow_neg (le_of_lt hx), rpow_one]
    rw [smul_eq_mul, this]
    field_simp [(ne_of_lt (show 0 < x from hx)).symm]
    norm_num; ring
  ¬∑ rw [div_one, ‚Üê mul_div_assoc, mul_comm, mul_div_cancel _ (two_ne_zero' ‚Ñù)]
set_option linter.uppercaseLean3 false in
#align real.Gamma_one_half_eq Real.Gamma_one_half_eq

/-- The special-value formula `Œì(1/2) = ‚àöœÄ`, which is equivalent to the Gaussian integral. -/
theorem Complex.Gamma_one_half_eq : Complex.Gamma (1 / 2) = (œÄ : ‚ÑÇ) ^ (1 / 2 : ‚ÑÇ) := by
  convert congr_arg ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) Real.Gamma_one_half_eq
  ¬∑ simpa only [one_div, ofReal_inv, ofReal_ofNat] using Gamma_ofReal (1 / 2)
  ¬∑ rw [sqrt_eq_rpow, ofReal_cpow pi_pos.le, ofReal_div, ofReal_ofNat, ofReal_one]
set_option linter.uppercaseLean3 false in
#align complex.Gamma_one_half_eq Complex.Gamma_one_half_eq

namespace GaussianFourier

/-! ## Fourier transform of the Gaussian integral
-/


open intervalIntegral

open scoped Real

variable {b : ‚ÑÇ}

/-- The integral of the Gaussian function over the vertical edges of a rectangle
with vertices at `(¬±T, 0)` and `(¬±T, c)`.  -/
def verticalIntegral (b : ‚ÑÇ) (c T : ‚Ñù) : ‚ÑÇ :=
  ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, I * (cexp (-b * (T + y * I) ^ 2) - cexp (-b * (T - y * I) ^ 2))
#align gaussian_fourier.vertical_integral GaussianFourier.verticalIntegral

/-- Explicit formula for the norm of the Gaussian function along the vertical
edges. -/
theorem norm_cexp_neg_mul_sq_add_mul_I (b : ‚ÑÇ) (c T : ‚Ñù) :
    ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ = exp (-(b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2)) := by
  rw [Complex.norm_eq_abs, Complex.abs_exp, neg_mul, neg_re, ‚Üê re_add_im b]
  simp only [sq, re_add_im, mul_re, mul_im, add_re, add_im, ofReal_re, ofReal_im, I_re, I_im]
  ring_nf
set_option linter.uppercaseLean3 false in
#align gaussian_fourier.norm_cexp_neg_mul_sq_add_mul_I GaussianFourier.norm_cexp_neg_mul_sq_add_mul_I

theorem norm_cexp_neg_mul_sq_add_mul_I' (hb : b.re ‚â† 0) (c T : ‚Ñù) :
    ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ =
      exp (-(b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re + b.re))) := by
  have :
    b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2 =
      b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re + b.re) :=
    by field_simp; ring
  rw [norm_cexp_neg_mul_sq_add_mul_I, this]
set_option linter.uppercaseLean3 false in
#align gaussian_fourier.norm_cexp_neg_mul_sq_add_mul_I' GaussianFourier.norm_cexp_neg_mul_sq_add_mul_I'

theorem verticalIntegral_norm_le (hb : 0 < b.re) (c : ‚Ñù) {T : ‚Ñù} (hT : 0 ‚â§ T) :
    ‚ÄñverticalIntegral b c T‚Äñ ‚â§
      (2 : ‚Ñù) * |c| * exp (-(b.re * T ^ 2 - (2 : ‚Ñù) * |b.im| * |c| * T - b.re * c ^ 2)) := by
  -- first get uniform bound for integrand
  have vert_norm_bound :
    ‚àÄ {T : ‚Ñù},
      0 ‚â§ T ‚Üí
        ‚àÄ {c y : ‚Ñù},
          |y| ‚â§ |c| ‚Üí
            ‚Äñcexp (-b * (T + y * I) ^ 2)‚Äñ ‚â§
              exp (-(b.re * T ^ 2 - (2 : ‚Ñù) * |b.im| * |c| * T - b.re * c ^ 2)) := by
    intro T hT c y hy
    rw [norm_cexp_neg_mul_sq_add_mul_I b, exp_le_exp, neg_le_neg_iff]
    refine' sub_le_sub (sub_le_sub (le_refl _) (mul_le_mul_of_nonneg_right _ hT)) _
    ¬∑ (conv_lhs => rw [mul_assoc]); (conv_rhs => rw [mul_assoc])
      refine' mul_le_mul_of_nonneg_left ((le_abs_self _).trans _) zero_le_two
      rw [abs_mul]
      exact mul_le_mul_of_nonneg_left hy (abs_nonneg _)
    ¬∑ refine' mul_le_mul_of_nonneg_left _ hb.le
      rwa [sq_le_sq]
  -- now main proof
  refine' (intervalIntegral.norm_integral_le_of_norm_le_const _).trans _
  pick_goal 3
  ¬∑ rw [sub_zero]
    conv_lhs => simp only [mul_comm _ |c|]
    conv_rhs =>
      conv =>
        congr
        rw [mul_comm]
      rw [mul_assoc]
  ¬∑ intro y hy
    have absy : |y| ‚â§ |c| := by
      rcases le_or_lt 0 c with (h | h)
      ¬∑ rw [uIoc_of_le h] at hy
        rw [abs_of_nonneg h, abs_of_pos hy.1]
        exact hy.2
      ¬∑ rw [uIoc_of_lt h] at hy
        rw [abs_of_neg h, abs_of_nonpos hy.2, neg_le_neg_iff]
        exact hy.1.le
    rw [norm_mul, Complex.norm_eq_abs, abs_I, one_mul, two_mul]
    refine' (norm_sub_le _ _).trans (add_le_add (vert_norm_bound hT absy) _)
    rw [‚Üê abs_neg y] at absy
    simpa only [neg_mul, ofReal_neg] using vert_norm_bound hT absy
#align gaussian_fourier.vertical_integral_norm_le GaussianFourier.verticalIntegral_norm_le

theorem tendsto_verticalIntegral (hb : 0 < b.re) (c : ‚Ñù) :
    Tendsto (verticalIntegral b c) atTop (ùìù 0) := by
  -- complete proof using squeeze theorem:
  rw [tendsto_zero_iff_norm_tendsto_zero]
  refine'
    tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds _
      (eventually_of_forall fun _ => norm_nonneg _)
      ((eventually_ge_atTop (0 : ‚Ñù)).mp
        (eventually_of_forall fun T hT => verticalIntegral_norm_le hb c hT))
  rw [(by ring : 0 = 2 * |c| * 0)]
  refine' (tendsto_exp_atBot.comp (tendsto_neg_atTop_atBot.comp _)).const_mul _
  apply tendsto_atTop_add_const_right
  simp_rw [sq, ‚Üê mul_assoc, ‚Üê sub_mul]
  refine' Tendsto.atTop_mul_atTop (tendsto_atTop_add_const_right _ _ _) tendsto_id
  exact (tendsto_const_mul_atTop_of_pos hb).mpr tendsto_id
#align gaussian_fourier.tendsto_vertical_integral GaussianFourier.tendsto_verticalIntegral

theorem integrable_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
    Integrable fun x : ‚Ñù => cexp (-b * (x + c * I) ^ 2) := by
  refine'
    ‚ü®(Complex.continuous_exp.comp
          (continuous_const.mul
            ((continuous_ofReal.add continuous_const).pow 2))).aestronglyMeasurable,
      _‚ü©
  rw [‚Üê hasFiniteIntegral_norm_iff]
  simp_rw [norm_cexp_neg_mul_sq_add_mul_I' hb.ne', neg_sub _ (c ^ 2 * _),
    sub_eq_add_neg _ (b.re * _), Real.exp_add]
  suffices Integrable fun x : ‚Ñù => exp (-(b.re * x ^ 2)) by
    exact (Integrable.comp_sub_right this (b.im * c / b.re)).hasFiniteIntegral.const_mul _
  simp_rw [‚Üê neg_mul]
  apply integrable_exp_neg_mul_sq hb
set_option linter.uppercaseLean3 false in
#align gaussian_fourier.integrable_cexp_neg_mul_sq_add_real_mul_I GaussianFourier.integrable_cexp_neg_mul_sq_add_real_mul_I

theorem integral_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
    ‚à´ x : ‚Ñù, cexp (-b * (x + c * I) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) := by
  refine'
    tendsto_nhds_unique
      (intervalIntegral_tendsto_integral (integrable_cexp_neg_mul_sq_add_real_mul_I hb c)
        tendsto_neg_atTop_atBot tendsto_id)
      _
  set I‚ÇÅ := fun T => ‚à´ x : ‚Ñù in -T..T, cexp (-b * (x + c * I) ^ 2) with HI‚ÇÅ
  let I‚ÇÇ := fun T : ‚Ñù => ‚à´ x : ‚Ñù in -T..T, cexp (-b * (x : ‚ÑÇ) ^ 2)
  let I‚ÇÑ := fun T : ‚Ñù => ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, cexp (-b * (T + y * I) ^ 2)
  let I‚ÇÖ := fun T : ‚Ñù => ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, cexp (-b * (-T + y * I) ^ 2)
  have C : ‚àÄ T : ‚Ñù, I‚ÇÇ T - I‚ÇÅ T + I * I‚ÇÑ T - I * I‚ÇÖ T = 0 := by
    intro T
    have :=
      integral_boundary_rect_eq_zero_of_differentiableOn (fun z => cexp (-b * z ^ 2)) (-T)
        (T + c * I)
        (by
          refine' Differentiable.differentiableOn (Differentiable.const_mul _ _).cexp
          exact differentiable_pow 2)
    simpa only [neg_im, ofReal_im, neg_zero, ofReal_zero, zero_mul, add_zero, neg_re,
      ofReal_re, add_re, mul_re, I_re, mul_zero, I_im, tsub_zero, add_im, mul_im,
      mul_one, zero_add, Algebra.id.smul_eq_mul, ofReal_neg] using this
  simp_rw [id.def, ‚Üê HI‚ÇÅ]
  have : I‚ÇÅ = fun T : ‚Ñù => I‚ÇÇ T + verticalIntegral b c T := by
    ext1 T
    specialize C T
    rw [sub_eq_zero] at C
    unfold verticalIntegral
    rw [integral_const_mul, intervalIntegral.integral_sub]
    ¬∑ simp_rw [(fun a b => by rw [sq]; ring_nf : ‚àÄ a b : ‚ÑÇ, (a - b * I) ^ 2 = (-a + b * I) ^ 2)]
      change I‚ÇÅ T = I‚ÇÇ T + I * (I‚ÇÑ T - I‚ÇÖ T)
      rw [mul_sub, ‚Üê C]
      abel
    all_goals apply Continuous.intervalIntegrable; continuity
  rw [this, ‚Üê add_zero ((œÄ / b : ‚ÑÇ) ^ (1 / 2 : ‚ÑÇ)), ‚Üê integral_gaussian_complex hb]
  refine' Tendsto.add _ (tendsto_verticalIntegral hb c)
  exact
    intervalIntegral_tendsto_integral (integrable_cexp_neg_mul_sq hb) tendsto_neg_atTop_atBot
      tendsto_id
set_option linter.uppercaseLean3 false in
#align gaussian_fourier.integral_cexp_neg_mul_sq_add_real_mul_I GaussianFourier.integral_cexp_neg_mul_sq_add_real_mul_I

theorem _root_.integral_cexp_neg_mul_sq_add_const (hb : 0 < b.re) (c : ‚ÑÇ) :
    ‚à´ x : ‚Ñù, cexp (-b * (x + c) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) := by
  rw [‚Üê re_add_im c]
  simp_rw [‚Üê add_assoc, ‚Üê ofReal_add]
  rw [integral_add_right_eq_self fun x : ‚Ñù => cexp (-b * (‚Üëx + ‚Üëc.im * I) ^ 2)]
  ¬∑ apply integral_cexp_neg_mul_sq_add_real_mul_I hb
#align integral_cexp_neg_mul_sq_add_const integral_cexp_neg_mul_sq_add_const

theorem _root_.fourier_transform_gaussian (hb : 0 < b.re) (t : ‚ÑÇ) :
    ‚à´ x : ‚Ñù, cexp (I * t * x) * cexp (-b * (x : ‚ÑÇ) ^ 2) =
      cexp (-t ^ 2 / (4 * b)) * (œÄ / b) ^ (1 / 2 : ‚ÑÇ) := by
  have : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  simp_rw [‚Üê Complex.exp_add]
  have : ‚àÄ x : ‚ÑÇ,
      I * t * x + -b * x ^ 2 = -t ^ 2 / ((4 : ‚ÑÇ) * b) + -b * (x + -I * t / 2 / b) ^ 2 := by
    intro x
    ring_nf
    rw [I_sq]
    field_simp; ring
  simp_rw [this, Complex.exp_add, integral_mul_left, integral_cexp_neg_mul_sq_add_const hb]
#align fourier_transform_gaussian fourier_transform_gaussian

theorem _root_.fourier_transform_gaussian_pi (hb : 0 < b.re) :
    (ùìï fun x : ‚Ñù => cexp (-œÄ * b * (x : ‚ÑÇ) ^ 2)) = fun t : ‚Ñù =>
      (1 : ‚ÑÇ) / b ^ (1 / 2 : ‚ÑÇ) * cexp (-œÄ / b * (t : ‚ÑÇ) ^ 2) := by
  ext1 t
  simp_rw [fourierIntegral_eq_integral_exp_smul, smul_eq_mul]
  have h1 : 0 < re (œÄ * b) := by rw [ofReal_mul_re]; exact mul_pos pi_pos hb
  have h2 : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  convert _root_.fourier_transform_gaussian h1 (-2 * œÄ * t) using 1
  ¬∑ congr 1 with x : 1
    congr 2
    any_goals push_cast; ring
  ¬∑ conv_lhs => rw [mul_comm]
    congr 2
    ¬∑ field_simp [ofReal_ne_zero.mpr pi_ne_zero]; ring
    ¬∑ rw [‚Üê div_div, div_self (ofReal_ne_zero.mpr pi_ne_zero), one_div, one_div b, inv_cpow]
      rw [Ne.def, arg_eq_pi_iff, not_and_or, not_lt]
      exact Or.inl hb.le
#align fourier_transform_gaussian_pi fourier_transform_gaussian_pi

end GaussianFourier

section GaussianPoisson

/-! ## Poisson summation applied to the Gaussian -/


variable {E : Type*} [NormedAddCommGroup E]

theorem tendsto_rpow_abs_mul_exp_neg_mul_sq_cocompact {a : ‚Ñù} (ha : 0 < a) (s : ‚Ñù) :
    Tendsto (fun x : ‚Ñù => |x| ^ s * rexp (-a * x ^ 2)) (cocompact ‚Ñù) (ùìù 0) := by
  conv in rexp _ => rw [‚Üê sq_abs]
  erw [cocompact_eq, ‚Üê comap_abs_atTop,
    @tendsto_comap'_iff _ _ _ (fun y => y ^ s * rexp (-a * y ^ 2)) _ _ _
      (mem_atTop_sets.mpr ‚ü®0, fun b hb => ‚ü®b, abs_of_nonneg hb‚ü©‚ü©)]
  exact
    (rpow_mul_exp_neg_mul_sq_isLittleO_exp_neg ha s).tendsto_zero_of_tendsto
      (tendsto_exp_atBot.comp <| tendsto_id.neg_const_mul_atTop (neg_lt_zero.mpr one_half_pos))
#align tendsto_rpow_abs_mul_exp_neg_mul_sq_cocompact tendsto_rpow_abs_mul_exp_neg_mul_sq_cocompact

theorem isLittleO_exp_neg_mul_sq_cocompact {a : ‚ÑÇ} (ha : 0 < a.re) (s : ‚Ñù) :
    (fun x : ‚Ñù => Complex.exp (-a * (x : ‚ÑÇ) ^ 2)) =o[cocompact ‚Ñù] fun x : ‚Ñù => |x| ^ s := by
  rw [‚Üê isLittleO_norm_left]
  simp_rw [norm_cexp_neg_mul_sq]
  apply isLittleO_of_tendsto'
  ¬∑ refine' Eventually.filter_mono cocompact_le_cofinite _
    refine' (eventually_cofinite_ne 0).mp (eventually_of_forall fun x hx h => _)
    exact ((rpow_pos_of_pos (abs_pos.mpr hx) _).ne' h).elim
  ¬∑ refine'
      Tendsto.congr' (Eventually.filter_mono cocompact_le_cofinite _)
        (tendsto_zero_iff_norm_tendsto_zero.mp <|
          tendsto_rpow_abs_mul_exp_neg_mul_sq_cocompact ha (-s))
    refine' (eventually_cofinite_ne 0).mp (eventually_of_forall fun x _ => _)
    dsimp only
    rw [norm_mul, norm_of_nonneg (rpow_nonneg_of_nonneg (abs_nonneg _) _), mul_comm,
      rpow_neg (abs_nonneg x), div_eq_mul_inv, norm_of_nonneg (exp_pos _).le]
#align is_o_exp_neg_mul_sq_cocompact isLittleO_exp_neg_mul_sq_cocompact

theorem Complex.tsum_exp_neg_mul_int_sq {a : ‚ÑÇ} (ha : 0 < a.re) :
    (‚àë' n : ‚Ñ§, cexp (-œÄ * a * (n : ‚ÑÇ) ^ 2)) =
      (1 : ‚ÑÇ) / a ^ (1 / 2 : ‚ÑÇ) * ‚àë' n : ‚Ñ§, cexp (-œÄ / a * (n : ‚ÑÇ) ^ 2) := by
  let f := fun x : ‚Ñù => cexp (-œÄ * a * (x : ‚ÑÇ) ^ 2)
  have h1 : 0 < (‚ÜëœÄ * a).re := by
    rw [ofReal_mul_re]
    exact mul_pos pi_pos ha
  have h2 : 0 < (‚ÜëœÄ / a).re := by
    rw [div_eq_mul_inv, ofReal_mul_re, inv_re]
    refine' mul_pos pi_pos (div_pos ha <| normSq_pos.mpr _)
    contrapose! ha
    rw [ha, zero_re]
  have f_bd : f =O[cocompact ‚Ñù] fun x => |x| ^ (-2 : ‚Ñù) := by
    convert (isLittleO_exp_neg_mul_sq_cocompact h1 (-2)).isBigO using 2
    dsimp only
    congr 1
    ring
  have Ff_bd : ùìï f =O[cocompact ‚Ñù] fun x => |x| ^ (-2 : ‚Ñù) := by
    rw [fourier_transform_gaussian_pi ha]
    convert (isLittleO_exp_neg_mul_sq_cocompact h2 (-2)).isBigO.const_mul_left
      ((1 : ‚ÑÇ) / a ^ (1 / 2 : ‚ÑÇ)) using 2
    congr 1
    ring_nf
  simpa only [fourier_transform_gaussian_pi ha, tsum_mul_left, Function.comp] using
    Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay
      (Complex.continuous_exp.comp (continuous_const.mul (continuous_ofReal.pow 2)) : Continuous f)
      one_lt_two f_bd Ff_bd
#align complex.tsum_exp_neg_mul_int_sq Complex.tsum_exp_neg_mul_int_sq

theorem Real.tsum_exp_neg_mul_int_sq {a : ‚Ñù} (ha : 0 < a) :
    (‚àë' n : ‚Ñ§, exp (-œÄ * a * (n : ‚Ñù) ^ 2)) =
      (1 : ‚Ñù) / a ^ (1 / 2 : ‚Ñù) * (‚àë' n : ‚Ñ§, exp (-œÄ / a * (n : ‚Ñù) ^ 2)) := by
  simpa only [‚Üê ofReal_inj, ofReal_mul, ofReal_tsum, ofReal_exp, ofReal_div, ofReal_pow,
    ofReal_int_cast, ofReal_neg, ofReal_cpow ha.le, ofReal_ofNat, ofReal_one] using
    Complex.tsum_exp_neg_mul_int_sq (by rwa [ofReal_re] : 0 < (a : ‚ÑÇ).re)
#align real.tsum_exp_neg_mul_int_sq Real.tsum_exp_neg_mul_int_sq

end GaussianPoisson
