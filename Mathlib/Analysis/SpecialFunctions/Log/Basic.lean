/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne
-/
import Mathlib.Analysis.SpecialFunctions.Exp
import Mathlib.Data.Nat.Factorization.Basic

#align_import analysis.special_functions.log.basic from "leanprover-community/mathlib"@"f23a09ce6d3f367220dc3cecad6b7eb69eb01690"

/-!
# Real logarithm

In this file we define `Real.log` to be the logarithm of a real number. As usual, we extend it from
its domain `(0, +âˆ)` to a globally defined function. We choose to do it so that `log 0 = 0` and
`log (-x) = log x`.

We prove some basic properties of this function and show that it is continuous.

## Tags

logarithm, continuity
-/

set_option autoImplicit true


open Set Filter Function

open Topology

noncomputable section

namespace Real

variable {x y : â„}

/-- The real logarithm function, equal to the inverse of the exponential for `x > 0`,
to `log |x|` for `x < 0`, and to `0` for `0`. We use this unconventional extension to
`(-âˆ, 0]` as it gives the formula `log (x * y) = log x + log y` for all nonzero `x` and `y`, and
the derivative of `log` is `1/x` away from `0`. -/
-- @[pp_nodot] -- Porting note: removed
noncomputable def log (x : â„) : â„ :=
  if hx : x = 0 then 0 else expOrderIso.symm âŸ¨|x|, abs_pos.2 hxâŸ©
#align real.log Real.log

theorem log_of_ne_zero (hx : x â‰  0) : log x = expOrderIso.symm âŸ¨|x|, abs_pos.2 hxâŸ© :=
  dif_neg hx
#align real.log_of_ne_zero Real.log_of_ne_zero

theorem log_of_pos (hx : 0 < x) : log x = expOrderIso.symm âŸ¨x, hxâŸ© := by
  rw [log_of_ne_zero hx.ne']
  -- âŠ¢ â†‘(OrderIso.symm expOrderIso) { val := |x|, property := (_ : 0 < |x|) } = â†‘(O â€¦
  congr
  -- âŠ¢ |x| = x
  exact abs_of_pos hx
  -- ğŸ‰ no goals
#align real.log_of_pos Real.log_of_pos

theorem exp_log_eq_abs (hx : x â‰  0) : exp (log x) = |x| := by
  rw [log_of_ne_zero hx, â† coe_expOrderIso_apply, OrderIso.apply_symm_apply, Subtype.coe_mk]
  -- ğŸ‰ no goals
#align real.exp_log_eq_abs Real.exp_log_eq_abs

theorem exp_log (hx : 0 < x) : exp (log x) = x := by
  rw [exp_log_eq_abs hx.ne']
  -- âŠ¢ |x| = x
  exact abs_of_pos hx
  -- ğŸ‰ no goals
#align real.exp_log Real.exp_log

theorem exp_log_of_neg (hx : x < 0) : exp (log x) = -x := by
  rw [exp_log_eq_abs (ne_of_lt hx)]
  -- âŠ¢ |x| = -x
  exact abs_of_neg hx
  -- ğŸ‰ no goals
#align real.exp_log_of_neg Real.exp_log_of_neg

theorem le_exp_log (x : â„) : x â‰¤ exp (log x) := by
  by_cases h_zero : x = 0
  -- âŠ¢ x â‰¤ exp (log x)
  Â· rw [h_zero, log, dif_pos rfl, exp_zero]
    -- âŠ¢ 0 â‰¤ 1
    exact zero_le_one
    -- ğŸ‰ no goals
  Â· rw [exp_log_eq_abs h_zero]
    -- âŠ¢ x â‰¤ |x|
    exact le_abs_self _
    -- ğŸ‰ no goals
#align real.le_exp_log Real.le_exp_log

@[simp]
theorem log_exp (x : â„) : log (exp x) = x :=
  exp_injective <| exp_log (exp_pos x)
#align real.log_exp Real.log_exp

theorem surjOn_log : SurjOn log (Ioi 0) univ := fun x _ => âŸ¨exp x, exp_pos x, log_exp xâŸ©
#align real.surj_on_log Real.surjOn_log

theorem log_surjective : Surjective log := fun x => âŸ¨exp x, log_exp xâŸ©
#align real.log_surjective Real.log_surjective

@[simp]
theorem range_log : range log = univ :=
  log_surjective.range_eq
#align real.range_log Real.range_log

@[simp]
theorem log_zero : log 0 = 0 :=
  dif_pos rfl
#align real.log_zero Real.log_zero

@[simp]
theorem log_one : log 1 = 0 :=
  exp_injective <| by rw [exp_log zero_lt_one, exp_zero]
                      -- ğŸ‰ no goals
#align real.log_one Real.log_one

@[simp]
theorem log_abs (x : â„) : log |x| = log x := by
  by_cases h : x = 0
  -- âŠ¢ log |x| = log x
  Â· simp [h]
    -- ğŸ‰ no goals
  Â· rw [â† exp_eq_exp, exp_log_eq_abs h, exp_log_eq_abs (abs_pos.2 h).ne', abs_abs]
    -- ğŸ‰ no goals
#align real.log_abs Real.log_abs

@[simp]
theorem log_neg_eq_log (x : â„) : log (-x) = log x := by rw [â† log_abs x, â† log_abs (-x), abs_neg]
                                                        -- ğŸ‰ no goals
#align real.log_neg_eq_log Real.log_neg_eq_log

theorem sinh_log {x : â„} (hx : 0 < x) : sinh (log x) = (x - xâ»Â¹) / 2 := by
  rw [sinh_eq, exp_neg, exp_log hx]
  -- ğŸ‰ no goals
#align real.sinh_log Real.sinh_log

theorem cosh_log {x : â„} (hx : 0 < x) : cosh (log x) = (x + xâ»Â¹) / 2 := by
  rw [cosh_eq, exp_neg, exp_log hx]
  -- ğŸ‰ no goals
#align real.cosh_log Real.cosh_log

theorem surjOn_log' : SurjOn log (Iio 0) univ := fun x _ =>
  âŸ¨-exp x, neg_lt_zero.2 <| exp_pos x, by rw [log_neg_eq_log, log_exp]âŸ©
                                          -- ğŸ‰ no goals
#align real.surj_on_log' Real.surjOn_log'

theorem log_mul (hx : x â‰  0) (hy : y â‰  0) : log (x * y) = log x + log y :=
  exp_injective <| by
    rw [exp_log_eq_abs (mul_ne_zero hx hy), exp_add, exp_log_eq_abs hx, exp_log_eq_abs hy, abs_mul]
    -- ğŸ‰ no goals
#align real.log_mul Real.log_mul

theorem log_div (hx : x â‰  0) (hy : y â‰  0) : log (x / y) = log x - log y :=
  exp_injective <| by
    rw [exp_log_eq_abs (div_ne_zero hx hy), exp_sub, exp_log_eq_abs hx, exp_log_eq_abs hy, abs_div]
    -- ğŸ‰ no goals
#align real.log_div Real.log_div

@[simp]
theorem log_inv (x : â„) : log xâ»Â¹ = -log x := by
  by_cases hx : x = 0; Â· simp [hx]
  -- âŠ¢ log xâ»Â¹ = -log x
                         -- ğŸ‰ no goals
  rw [â† exp_eq_exp, exp_log_eq_abs (inv_ne_zero hx), exp_neg, exp_log_eq_abs hx, abs_inv]
  -- ğŸ‰ no goals
#align real.log_inv Real.log_inv

theorem log_le_log (h : 0 < x) (hâ‚ : 0 < y) : log x â‰¤ log y â†” x â‰¤ y := by
  rw [â† exp_le_exp, exp_log h, exp_log hâ‚]
  -- ğŸ‰ no goals
#align real.log_le_log Real.log_le_log

@[gcongr]
theorem log_lt_log (hx : 0 < x) : x < y â†’ log x < log y := by
  intro h
  -- âŠ¢ log x < log y
  rwa [â† exp_lt_exp, exp_log hx, exp_log (lt_trans hx h)]
  -- ğŸ‰ no goals
#align real.log_lt_log Real.log_lt_log

@[gcongr]
theorem log_le_log' (hx : 0 < x) : x â‰¤ y â†’ log x â‰¤ log y := by
  intro hxy
  -- âŠ¢ log x â‰¤ log y
  cases hxy.eq_or_lt with
  | inl h_eq => simp [h_eq]
  | inr hlt => exact le_of_lt <| log_lt_log hx hlt

theorem log_lt_log_iff (hx : 0 < x) (hy : 0 < y) : log x < log y â†” x < y := by
  rw [â† exp_lt_exp, exp_log hx, exp_log hy]
  -- ğŸ‰ no goals
#align real.log_lt_log_iff Real.log_lt_log_iff

theorem log_le_iff_le_exp (hx : 0 < x) : log x â‰¤ y â†” x â‰¤ exp y := by rw [â† exp_le_exp, exp_log hx]
                                                                     -- ğŸ‰ no goals
#align real.log_le_iff_le_exp Real.log_le_iff_le_exp

theorem log_lt_iff_lt_exp (hx : 0 < x) : log x < y â†” x < exp y := by rw [â† exp_lt_exp, exp_log hx]
                                                                     -- ğŸ‰ no goals
#align real.log_lt_iff_lt_exp Real.log_lt_iff_lt_exp

theorem le_log_iff_exp_le (hy : 0 < y) : x â‰¤ log y â†” exp x â‰¤ y := by rw [â† exp_le_exp, exp_log hy]
                                                                     -- ğŸ‰ no goals
#align real.le_log_iff_exp_le Real.le_log_iff_exp_le

theorem lt_log_iff_exp_lt (hy : 0 < y) : x < log y â†” exp x < y := by rw [â† exp_lt_exp, exp_log hy]
                                                                     -- ğŸ‰ no goals
#align real.lt_log_iff_exp_lt Real.lt_log_iff_exp_lt

theorem log_pos_iff (hx : 0 < x) : 0 < log x â†” 1 < x := by
  rw [â† log_one]
  -- âŠ¢ log 1 < log x â†” 1 < x
  exact log_lt_log_iff zero_lt_one hx
  -- ğŸ‰ no goals
#align real.log_pos_iff Real.log_pos_iff

theorem log_pos (hx : 1 < x) : 0 < log x :=
  (log_pos_iff (lt_trans zero_lt_one hx)).2 hx
#align real.log_pos Real.log_pos

theorem log_pos_of_lt_neg_one (hx : x < -1) : 0 < log x := by
  rw [â†neg_neg x, log_neg_eq_log]
  -- âŠ¢ 0 < log (-x)
  have : 1 < -x := by linarith
  -- âŠ¢ 0 < log (-x)
  exact log_pos this
  -- ğŸ‰ no goals

theorem log_neg_iff (h : 0 < x) : log x < 0 â†” x < 1 := by
  rw [â† log_one]
  -- âŠ¢ log x < log 1 â†” x < 1
  exact log_lt_log_iff h zero_lt_one
  -- ğŸ‰ no goals
#align real.log_neg_iff Real.log_neg_iff

theorem log_neg (h0 : 0 < x) (h1 : x < 1) : log x < 0 :=
  (log_neg_iff h0).2 h1
#align real.log_neg Real.log_neg

theorem log_neg_of_lt_zero (h0 : x < 0) (h1 : -1 < x) : log x < 0 := by
  rw [â†neg_neg x, log_neg_eq_log]
  -- âŠ¢ log (-x) < 0
  have h0' : 0 < -x := by linarith
  -- âŠ¢ log (-x) < 0
  have h1' : -x < 1 := by linarith
  -- âŠ¢ log (-x) < 0
  exact log_neg h0' h1'
  -- ğŸ‰ no goals

theorem log_nonneg_iff (hx : 0 < x) : 0 â‰¤ log x â†” 1 â‰¤ x := by rw [â† not_lt, log_neg_iff hx, not_lt]
                                                              -- ğŸ‰ no goals
#align real.log_nonneg_iff Real.log_nonneg_iff

theorem log_nonneg (hx : 1 â‰¤ x) : 0 â‰¤ log x :=
  (log_nonneg_iff (zero_lt_one.trans_le hx)).2 hx
#align real.log_nonneg Real.log_nonneg

theorem log_nonpos_iff (hx : 0 < x) : log x â‰¤ 0 â†” x â‰¤ 1 := by rw [â† not_lt, log_pos_iff hx, not_lt]
                                                              -- ğŸ‰ no goals
#align real.log_nonpos_iff Real.log_nonpos_iff

theorem log_nonpos_iff' (hx : 0 â‰¤ x) : log x â‰¤ 0 â†” x â‰¤ 1 := by
  rcases hx.eq_or_lt with (rfl | hx)
  -- âŠ¢ log 0 â‰¤ 0 â†” 0 â‰¤ 1
  Â· simp [le_refl, zero_le_one]
    -- ğŸ‰ no goals
  exact log_nonpos_iff hx
  -- ğŸ‰ no goals
#align real.log_nonpos_iff' Real.log_nonpos_iff'

theorem log_nonpos (hx : 0 â‰¤ x) (h'x : x â‰¤ 1) : log x â‰¤ 0 :=
  (log_nonpos_iff' hx).2 h'x
#align real.log_nonpos Real.log_nonpos

theorem log_nat_cast_nonneg (n : â„•) : 0 â‰¤ log n := by
  by_cases hn : n = 0
  -- âŠ¢ 0 â‰¤ log â†‘n
  case pos => simp [hn]
  -- âŠ¢ 0 â‰¤ log â†‘n
  -- ğŸ‰ no goals
  case neg =>
    have : (1 : â„) â‰¤ n := by exact_mod_cast Nat.one_le_of_lt <| Nat.pos_of_ne_zero hn
    exact log_nonneg this

theorem log_neg_nat_cast_nonneg (n : â„•) : 0 â‰¤ log (-n) := by
  rw [â†log_neg_eq_log, neg_neg]
  -- âŠ¢ 0 â‰¤ log â†‘n
  exact log_nat_cast_nonneg _
  -- ğŸ‰ no goals

theorem log_int_cast_nonneg (n : â„¤) : 0 â‰¤ log n := by
  cases lt_trichotomy 0 n with
  | inl hn =>
      have : (1 : â„) â‰¤ n := by exact_mod_cast hn
      exact log_nonneg this
  | inr hn =>
      cases hn with
      | inl hn => simp [hn.symm]
      | inr hn =>
          have : (1 : â„) â‰¤ -n := by rw [â†neg_zero, â†lt_neg] at hn; exact_mod_cast hn
          rw [â†log_neg_eq_log]
          exact log_nonneg this

theorem strictMonoOn_log : StrictMonoOn log (Set.Ioi 0) := fun _ hx _ _ hxy => log_lt_log hx hxy
#align real.strict_mono_on_log Real.strictMonoOn_log

theorem strictAntiOn_log : StrictAntiOn log (Set.Iio 0) := by
  rintro x (hx : x < 0) y (hy : y < 0) hxy
  -- âŠ¢ log y < log x
  rw [â† log_abs y, â† log_abs x]
  -- âŠ¢ log |y| < log |x|
  refine' log_lt_log (abs_pos.2 hy.ne) _
  -- âŠ¢ |y| < |x|
  rwa [abs_of_neg hy, abs_of_neg hx, neg_lt_neg_iff]
  -- ğŸ‰ no goals
#align real.strict_anti_on_log Real.strictAntiOn_log

theorem log_injOn_pos : Set.InjOn log (Set.Ioi 0) :=
  strictMonoOn_log.injOn
#align real.log_inj_on_pos Real.log_injOn_pos

theorem log_lt_sub_one_of_pos (hx1 : 0 < x) (hx2 : x â‰  1) : log x < x - 1 := by
  have h : log x â‰  0
  -- âŠ¢ log x â‰  0
  Â· rwa [â† log_one, log_injOn_pos.ne_iff hx1]
    -- âŠ¢ 1 âˆˆ Ioi 0
    exact mem_Ioi.mpr zero_lt_one
    -- ğŸ‰ no goals
  linarith [add_one_lt_exp_of_nonzero h, exp_log hx1]
  -- ğŸ‰ no goals
#align real.log_lt_sub_one_of_pos Real.log_lt_sub_one_of_pos

theorem eq_one_of_pos_of_log_eq_zero {x : â„} (hâ‚ : 0 < x) (hâ‚‚ : log x = 0) : x = 1 :=
  log_injOn_pos (Set.mem_Ioi.2 hâ‚) (Set.mem_Ioi.2 zero_lt_one) (hâ‚‚.trans Real.log_one.symm)
#align real.eq_one_of_pos_of_log_eq_zero Real.eq_one_of_pos_of_log_eq_zero

theorem log_ne_zero_of_pos_of_ne_one {x : â„} (hx_pos : 0 < x) (hx : x â‰  1) : log x â‰  0 :=
  mt (eq_one_of_pos_of_log_eq_zero hx_pos) hx
#align real.log_ne_zero_of_pos_of_ne_one Real.log_ne_zero_of_pos_of_ne_one

@[simp]
theorem log_eq_zero {x : â„} : log x = 0 â†” x = 0 âˆ¨ x = 1 âˆ¨ x = -1 := by
  constructor
  -- âŠ¢ log x = 0 â†’ x = 0 âˆ¨ x = 1 âˆ¨ x = -1
  Â· intro h
    -- âŠ¢ x = 0 âˆ¨ x = 1 âˆ¨ x = -1
    rcases lt_trichotomy x 0 with (x_lt_zero | rfl | x_gt_zero)
    Â· refine' Or.inr (Or.inr (neg_eq_iff_eq_neg.mp _))
      -- âŠ¢ -x = 1
      rw [â† log_neg_eq_log x] at h
      -- âŠ¢ -x = 1
      exact eq_one_of_pos_of_log_eq_zero (neg_pos.mpr x_lt_zero) h
      -- ğŸ‰ no goals
    Â· exact Or.inl rfl
      -- ğŸ‰ no goals
    Â· exact Or.inr (Or.inl (eq_one_of_pos_of_log_eq_zero x_gt_zero h))
      -- ğŸ‰ no goals
  Â· rintro (rfl | rfl | rfl) <;> simp only [log_one, log_zero, log_neg_eq_log]
                                 -- ğŸ‰ no goals
                                 -- ğŸ‰ no goals
                                 -- ğŸ‰ no goals
#align real.log_eq_zero Real.log_eq_zero

theorem log_ne_zero {x : â„} : log x â‰  0 â†” x â‰  0 âˆ§ x â‰  1 âˆ§ x â‰  -1 := by
  simpa only [not_or] using log_eq_zero.not
  -- ğŸ‰ no goals
#align real.log_ne_zero Real.log_ne_zero

@[simp]
theorem log_pow (x : â„) (n : â„•) : log (x ^ n) = n * log x := by
  induction' n with n ih
  -- âŠ¢ log (x ^ Nat.zero) = â†‘Nat.zero * log x
  Â· simp
    -- ğŸ‰ no goals
  rcases eq_or_ne x 0 with (rfl | hx)
  -- âŠ¢ log (0 ^ Nat.succ n) = â†‘(Nat.succ n) * log 0
  Â· simp
    -- ğŸ‰ no goals
  rw [pow_succ', log_mul (pow_ne_zero _ hx) hx, ih, Nat.cast_succ, add_mul, one_mul]
  -- ğŸ‰ no goals
#align real.log_pow Real.log_pow

@[simp]
theorem log_zpow (x : â„) (n : â„¤) : log (x ^ n) = n * log x := by
  induction n
  -- âŠ¢ log (x ^ Int.ofNat aâœ) = â†‘(Int.ofNat aâœ) * log x
  Â· rw [Int.ofNat_eq_coe, zpow_ofNat, log_pow, Int.cast_ofNat]
    -- ğŸ‰ no goals
  rw [zpow_negSucc, log_inv, log_pow, Int.cast_negSucc, Nat.cast_add_one, neg_mul_eq_neg_mul]
  -- ğŸ‰ no goals
#align real.log_zpow Real.log_zpow

theorem log_sqrt {x : â„} (hx : 0 â‰¤ x) : log (sqrt x) = log x / 2 := by
  rw [eq_div_iff, mul_comm, â† Nat.cast_two, â† log_pow, sq_sqrt hx]
  -- âŠ¢ 2 â‰  0
  exact two_ne_zero
  -- ğŸ‰ no goals
#align real.log_sqrt Real.log_sqrt

theorem log_le_sub_one_of_pos {x : â„} (hx : 0 < x) : log x â‰¤ x - 1 := by
  rw [le_sub_iff_add_le]
  -- âŠ¢ log x + 1 â‰¤ x
  convert add_one_le_exp (log x)
  -- âŠ¢ x = exp (log x)
  rw [exp_log hx]
  -- ğŸ‰ no goals
#align real.log_le_sub_one_of_pos Real.log_le_sub_one_of_pos

/-- Bound for `|log x * x|` in the interval `(0, 1]`. -/
theorem abs_log_mul_self_lt (x : â„) (h1 : 0 < x) (h2 : x â‰¤ 1) : |log x * x| < 1 := by
  have : 0 < 1 / x := by simpa only [one_div, inv_pos] using h1
  -- âŠ¢ |log x * x| < 1
  replace := log_le_sub_one_of_pos this
  -- âŠ¢ |log x * x| < 1
  replace : log (1 / x) < 1 / x := by linarith
  -- âŠ¢ |log x * x| < 1
  rw [log_div one_ne_zero h1.ne', log_one, zero_sub, lt_div_iff h1] at this
  -- âŠ¢ |log x * x| < 1
  have aux : 0 â‰¤ -log x * x := by
    refine' mul_nonneg _ h1.le
    rw [â† log_inv]
    apply log_nonneg
    rw [â† le_inv h1 zero_lt_one, inv_one]
    exact h2
  rw [â† abs_of_nonneg aux, neg_mul, abs_neg] at this
  -- âŠ¢ |log x * x| < 1
  exact this
  -- ğŸ‰ no goals
#align real.abs_log_mul_self_lt Real.abs_log_mul_self_lt

/-- The real logarithm function tends to `+âˆ` at `+âˆ`. -/
theorem tendsto_log_atTop : Tendsto log atTop atTop :=
  tendsto_comp_exp_atTop.1 <| by simpa only [log_exp] using tendsto_id
                                 -- ğŸ‰ no goals
#align real.tendsto_log_at_top Real.tendsto_log_atTop

theorem tendsto_log_nhdsWithin_zero : Tendsto log (ğ“[â‰ ] 0) atBot := by
  rw [â† show _ = log from funext log_abs]
  -- âŠ¢ Tendsto (fun x => log |x|) (ğ“[{0}á¶œ] 0) atBot
  refine' Tendsto.comp (g := log) _ tendsto_abs_nhdsWithin_zero
  -- âŠ¢ Tendsto log (ğ“[Ioi 0] 0) atBot
  simpa [â† tendsto_comp_exp_atBot] using tendsto_id
  -- ğŸ‰ no goals
#align real.tendsto_log_nhds_within_zero Real.tendsto_log_nhdsWithin_zero

theorem continuousOn_log : ContinuousOn log {0}á¶œ := by
  simp only [continuousOn_iff_continuous_restrict, restrict]
  -- âŠ¢ Continuous fun x => log â†‘x
  conv in log _ => rw [log_of_ne_zero (show (x : â„) â‰  0 from x.2)]
  -- âŠ¢ Continuous fun x => â†‘(OrderIso.symm expOrderIso) { val := |â†‘x|, property :=  â€¦
  exact expOrderIso.symm.continuous.comp (continuous_subtype_val.norm.subtype_mk _)
  -- ğŸ‰ no goals
#align real.continuous_on_log Real.continuousOn_log

@[continuity]
theorem continuous_log : Continuous fun x : { x : â„ // x â‰  0 } => log x :=
  continuousOn_iff_continuous_restrict.1 <| continuousOn_log.mono fun _ => id
#align real.continuous_log Real.continuous_log

@[continuity]
theorem continuous_log' : Continuous fun x : { x : â„ // 0 < x } => log x :=
  continuousOn_iff_continuous_restrict.1 <| continuousOn_log.mono fun _ hx => ne_of_gt hx
#align real.continuous_log' Real.continuous_log'

theorem continuousAt_log (hx : x â‰  0) : ContinuousAt log x :=
  (continuousOn_log x hx).continuousAt <| isOpen_compl_singleton.mem_nhds hx
#align real.continuous_at_log Real.continuousAt_log

@[simp]
theorem continuousAt_log_iff : ContinuousAt log x â†” x â‰  0 := by
  refine' âŸ¨_, continuousAt_logâŸ©
  -- âŠ¢ ContinuousAt log x â†’ x â‰  0
  rintro h rfl
  -- âŠ¢ False
  exact not_tendsto_nhds_of_tendsto_atBot tendsto_log_nhdsWithin_zero _
    (h.tendsto.mono_left inf_le_left)
#align real.continuous_at_log_iff Real.continuousAt_log_iff

open BigOperators

theorem log_prod {Î± : Type*} (s : Finset Î±) (f : Î± â†’ â„) (hf : âˆ€ x âˆˆ s, f x â‰  0) :
    log (âˆ i in s, f i) = âˆ‘ i in s, log (f i) := by
  induction' s using Finset.cons_induction_on with a s ha ih
  -- âŠ¢ log (âˆ i in âˆ…, f i) = âˆ‘ i in âˆ…, log (f i)
  Â· simp
    -- ğŸ‰ no goals
  Â· rw [Finset.forall_mem_cons] at hf
    -- âŠ¢ log (âˆ i in Finset.cons a s ha, f i) = âˆ‘ i in Finset.cons a s ha, log (f i)
    simp [ih hf.2, log_mul hf.1 (Finset.prod_ne_zero_iff.2 hf.2)]
    -- ğŸ‰ no goals
#align real.log_prod Real.log_prod

-- Porting note: new theorem
protected theorem _root_.Finsupp.log_prod {Î± Î² : Type*} [Zero Î²] (f : Î± â†’â‚€ Î²) (g : Î± â†’ Î² â†’ â„)
    (hg : âˆ€ a, g a (f a) = 0 â†’ f a = 0) : log (f.prod g) = f.sum fun a b â†¦ log (g a b) :=
  log_prod _ _ fun _x hx hâ‚€ â†¦ Finsupp.mem_support_iff.1 hx <| hg _ hâ‚€

theorem log_nat_eq_sum_factorization (n : â„•) :
    log n = n.factorization.sum fun p t => t * log p := by
  rcases eq_or_ne n 0 with (rfl | hn)
  -- âŠ¢ log â†‘0 = Finsupp.sum (Nat.factorization 0) fun p t => â†‘t * log â†‘p
  Â· simp -- relies on junk values of `log` and `Nat.factorization`
    -- ğŸ‰ no goals
  Â· simp only [â† log_pow, â† Nat.cast_pow]
    -- âŠ¢ log â†‘n = Finsupp.sum (Nat.factorization n) fun p t => log â†‘(p ^ t)
    rw [â† Finsupp.log_prod, â† Nat.cast_finsupp_prod, Nat.factorization_prod_pow_eq_self hn]
    -- âŠ¢ âˆ€ (a : â„•), â†‘(a ^ â†‘(Nat.factorization n) a) = 0 â†’ â†‘(Nat.factorization n) a = 0
    intro p hp
    -- âŠ¢ â†‘(Nat.factorization n) p = 0
    rw [pow_eq_zero (Nat.cast_eq_zero.1 hp), Nat.factorization_zero_right]
    -- ğŸ‰ no goals
#align real.log_nat_eq_sum_factorization Real.log_nat_eq_sum_factorization

theorem tendsto_pow_log_div_mul_add_atTop (a b : â„) (n : â„•) (ha : a â‰  0) :
    Tendsto (fun x => log x ^ n / (a * x + b)) atTop (ğ“ 0) :=
  ((tendsto_div_pow_mul_exp_add_atTop a b n ha.symm).comp tendsto_log_atTop).congr' <| by
    filter_upwards [eventually_gt_atTop (0 : â„)] with x hx using by simp [exp_log hx]
    -- ğŸ‰ no goals
#align real.tendsto_pow_log_div_mul_add_at_top Real.tendsto_pow_log_div_mul_add_atTop

theorem isLittleO_pow_log_id_atTop {n : â„•} : (fun x => log x ^ n) =o[atTop] id := by
  rw [Asymptotics.isLittleO_iff_tendsto']
  -- âŠ¢ Tendsto (fun x => log x ^ n / id x) atTop (ğ“ 0)
  Â· simpa using tendsto_pow_log_div_mul_add_atTop 1 0 n one_ne_zero
    -- ğŸ‰ no goals
  filter_upwards [eventually_ne_atTop (0 : â„)] with x hâ‚ hâ‚‚ using (hâ‚ hâ‚‚).elim
  -- ğŸ‰ no goals
#align real.is_o_pow_log_id_at_top Real.isLittleO_pow_log_id_atTop

theorem isLittleO_log_id_atTop : log =o[atTop] id :=
  isLittleO_pow_log_id_atTop.congr_left fun _ => pow_one _
#align real.is_o_log_id_at_top Real.isLittleO_log_id_atTop

theorem isLittleO_const_log_atTop {c : â„} : (fun _ => c) =o[atTop] log := by
  refine Asymptotics.isLittleO_of_tendsto' ?_
    <| Tendsto.div_atTop (a := c) (by simp) tendsto_log_atTop
  filter_upwards [eventually_gt_atTop 1] with x hx
  -- âŠ¢ log x = 0 â†’ c = 0
  aesop (add safe forward log_pos)
  -- ğŸ‰ no goals

end Real

section Continuity

open Real

variable {Î± : Type*}

theorem Filter.Tendsto.log {f : Î± â†’ â„} {l : Filter Î±} {x : â„} (h : Tendsto f l (ğ“ x)) (hx : x â‰  0) :
    Tendsto (fun x => log (f x)) l (ğ“ (log x)) :=
  (continuousAt_log hx).tendsto.comp h
#align filter.tendsto.log Filter.Tendsto.log

variable [TopologicalSpace Î±] {f : Î± â†’ â„} {s : Set Î±} {a : Î±}

theorem Continuous.log (hf : Continuous f) (hâ‚€ : âˆ€ x, f x â‰  0) : Continuous fun x => log (f x) :=
  continuousOn_log.comp_continuous hf hâ‚€
#align continuous.log Continuous.log

nonrec theorem ContinuousAt.log (hf : ContinuousAt f a) (hâ‚€ : f a â‰  0) :
    ContinuousAt (fun x => log (f x)) a :=
  hf.log hâ‚€
#align continuous_at.log ContinuousAt.log

nonrec theorem ContinuousWithinAt.log (hf : ContinuousWithinAt f s a) (hâ‚€ : f a â‰  0) :
    ContinuousWithinAt (fun x => log (f x)) s a :=
  hf.log hâ‚€
#align continuous_within_at.log ContinuousWithinAt.log

theorem ContinuousOn.log (hf : ContinuousOn f s) (hâ‚€ : âˆ€ x âˆˆ s, f x â‰  0) :
    ContinuousOn (fun x => log (f x)) s := fun x hx => (hf x hx).log (hâ‚€ x hx)
#align continuous_on.log ContinuousOn.log

end Continuity

section TendstoCompAddSub

open Filter

namespace Real

theorem tendsto_log_comp_add_sub_log (y : â„) :
    Tendsto (fun x : â„ => log (x + y) - log x) atTop (ğ“ 0) := by
  have : Tendsto (fun x â†¦ 1 + y / x) atTop (ğ“ (1 + 0))
  -- âŠ¢ Tendsto (fun x => 1 + y / x) atTop (ğ“ (1 + 0))
  Â· exact tendsto_const_nhds.add (tendsto_const_nhds.div_atTop tendsto_id)
    -- ğŸ‰ no goals
  rw [â† comap_exp_nhds_exp, exp_zero, tendsto_comap_iff, â† add_zero (1 : â„)]
  -- âŠ¢ Tendsto (exp âˆ˜ fun x => log (x + y) - log x) atTop (ğ“ (1 + 0))
  refine' this.congr' _
  -- âŠ¢ (fun x => 1 + y / x) =á¶ [atTop] exp âˆ˜ fun x => log (x + y) - log x
  filter_upwards [eventually_gt_atTop (0 : â„), eventually_gt_atTop (-y)] with x hxâ‚€ hxy
  -- âŠ¢ 1 + y / x = (exp âˆ˜ fun x => log (x + y) - log x) x
  rw [comp_apply, exp_sub, exp_log, exp_log, one_add_div] <;> linarith
                                                              -- ğŸ‰ no goals
                                                              -- ğŸ‰ no goals
                                                              -- ğŸ‰ no goals
#align real.tendsto_log_comp_add_sub_log Real.tendsto_log_comp_add_sub_log

theorem tendsto_log_nat_add_one_sub_log : Tendsto (fun k : â„• => log (k + 1) - log k) atTop (ğ“ 0) :=
  (tendsto_log_comp_add_sub_log 1).comp tendsto_nat_cast_atTop_atTop
#align real.tendsto_log_nat_add_one_sub_log Real.tendsto_log_nat_add_one_sub_log

end Real

end TendstoCompAddSub

namespace Mathlib.Meta.Positivity
open Lean.Meta Qq

lemma log_nonneg_of_isNat (h : NormNum.IsNat e n) : 0 â‰¤ Real.log (e : â„) := by
  rw [NormNum.IsNat.to_eq h rfl]
  -- âŠ¢ 0 â‰¤ Real.log â†‘n
  exact Real.log_nat_cast_nonneg _
  -- ğŸ‰ no goals

lemma log_pos_of_isNat (h : NormNum.IsNat e n) (w : Nat.blt 1 n = true) : 0 < Real.log (e : â„) := by
  rw [NormNum.IsNat.to_eq h rfl]
  -- âŠ¢ 0 < Real.log â†‘n
  apply Real.log_pos
  -- âŠ¢ 1 < â†‘n
  simpa using w
  -- ğŸ‰ no goals

lemma log_nonneg_of_isNegNat (h : NormNum.IsInt e (.negOfNat n)) : 0 â‰¤ Real.log (e : â„) := by
  rw [NormNum.IsInt.neg_to_eq h rfl]
  -- âŠ¢ 0 â‰¤ Real.log (-â†‘n)
  exact Real.log_neg_nat_cast_nonneg _
  -- ğŸ‰ no goals

lemma log_pos_of_isNegNat (h : NormNum.IsInt e (.negOfNat n)) (w : Nat.blt 1 n = true) :
    0 < Real.log (e : â„) := by
  rw [NormNum.IsInt.neg_to_eq h rfl]
  -- âŠ¢ 0 < Real.log (-â†‘n)
  rw [Real.log_neg_eq_log]
  -- âŠ¢ 0 < Real.log â†‘n
  apply Real.log_pos
  -- âŠ¢ 1 < â†‘n
  simpa using w
  -- ğŸ‰ no goals

lemma log_pos_of_isRat :
    (NormNum.IsRat e n d) â†’ (decide ((1 : â„š) < n / d)) â†’ (0 < Real.log (e : â„))
  | âŸ¨inv, eqâŸ©, h => by
    rw [eq, invOf_eq_inv, â†div_eq_mul_inv]
    -- âŠ¢ 0 < Real.log (â†‘n / â†‘d)
    have : 1 < (n : â„) / d := by exact_mod_cast of_decide_eq_true h
    -- âŠ¢ 0 < Real.log (â†‘n / â†‘d)
    exact Real.log_pos this
    -- ğŸ‰ no goals

lemma log_pos_of_isRat_neg :
    (NormNum.IsRat e n d) â†’ (decide (n / d < (-1 : â„š))) â†’ (0 < Real.log (e : â„))
  | âŸ¨inv, eqâŸ©, h => by
    rw [eq, invOf_eq_inv, â†div_eq_mul_inv]
    -- âŠ¢ 0 < Real.log (â†‘n / â†‘d)
    have : (n : â„) / d < -1 := by exact_mod_cast of_decide_eq_true h
    -- âŠ¢ 0 < Real.log (â†‘n / â†‘d)
    exact Real.log_pos_of_lt_neg_one this
    -- ğŸ‰ no goals

lemma log_nz_of_isRat : (NormNum.IsRat e n d) â†’ (decide ((0 : â„š) < n / d))
    â†’ (decide (n / d < (1 : â„š))) â†’ (Real.log (e : â„) â‰  0)
  | âŸ¨inv, eqâŸ©, hâ‚, hâ‚‚ => by
    rw [eq, invOf_eq_inv, â†div_eq_mul_inv]
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    have hâ‚' : 0 < (n : â„) / d := by exact_mod_cast of_decide_eq_true hâ‚
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    have hâ‚‚' : (n : â„) / d < 1 := by exact_mod_cast of_decide_eq_true hâ‚‚
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    exact ne_of_lt <| Real.log_neg hâ‚' hâ‚‚'
    -- ğŸ‰ no goals

lemma log_nz_of_isRat_neg : (NormNum.IsRat e n d) â†’ (decide (n / d < (0 : â„š)))
      â†’ (decide ((-1 : â„š) < n / d)) â†’ (Real.log (e : â„) â‰  0)
  | âŸ¨inv, eqâŸ©, hâ‚, hâ‚‚ => by
    rw [eq, invOf_eq_inv, â†div_eq_mul_inv]
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    have hâ‚' : (n : â„) / d < 0 := by exact_mod_cast of_decide_eq_true hâ‚
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    have hâ‚‚' : -1 < (n : â„) / d := by exact_mod_cast of_decide_eq_true hâ‚‚
    -- âŠ¢ Real.log (â†‘n / â†‘d) â‰  0
    exact ne_of_lt <| Real.log_neg_of_lt_zero hâ‚' hâ‚‚'
    -- ğŸ‰ no goals

/-- Extension for the `positivity` tactic: `Real.log` of a natural number is always nonnegative. -/
@[positivity Real.log (Nat.cast _)]
def evalLogNatCast : PositivityExt where eval {_ _} _zÎ± _pÎ± e := do
  let .app (f : Q(â„ â†’ â„)) (.app _ (a : Q(â„•))) â† withReducible (whnf e) | throwError "not Real.log"
  guard <|â† withDefault <| withNewMCtxDepth <| isDefEq f q(Real.log)
  pure (.nonnegative (q(Real.log_nat_cast_nonneg $a) : Lean.Expr))

/-- Extension for the `positivity` tactic: `Real.log` of an integer is always nonnegative. -/
@[positivity Real.log (Int.cast _)]
def evalLogIntCast : PositivityExt where eval {_ _} _zÎ± _pÎ± e := do
  let .app (f : Q(â„ â†’ â„)) (.app _ (a : Q(â„¤))) â† withReducible (whnf e) | throwError "not Real.log"
  guard <|â† withDefault <| withNewMCtxDepth <| isDefEq f q(Real.log)
  pure (.nonnegative (q(Real.log_int_cast_nonneg $a) : Lean.Expr))

/-- Extension for the `positivity` tactic: `Real.log` of a numeric literal. -/
@[positivity Real.log _]
def evalLogNatLit : PositivityExt where eval {_ _} _zÎ± _pÎ± e := do
  let .app (f : Q(â„ â†’ â„)) (a : Q(â„)) â† withReducible (whnf e) | throwError "not Real.log"
  guard <|â† withDefault <| withNewMCtxDepth <| isDefEq f q(Real.log)
  match â†NormNum.derive a with
  | .isNat (_ : Q(AddMonoidWithOne â„)) lit p =>
    assumeInstancesCommute
    have p : Q(NormNum.IsNat $a $lit) := p
    if 1 < lit.natLit! then
      let p' : Q(Nat.blt 1 $lit = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.positive (q(log_pos_of_isNat $p $p') : Lean.Expr))
    else
      pure (.nonnegative (q(log_nonneg_of_isNat $p) : Lean.Expr))
  | .isNegNat _ lit p =>
    assumeInstancesCommute
    have p : Q(NormNum.IsInt $a (Int.negOfNat $lit)) := p
    if 1 < lit.natLit! then
      let p' : Q(Nat.blt 1 $lit = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.positive (q(log_pos_of_isNegNat $p $p') : Lean.Expr))
    else
      pure (.nonnegative (q(log_nonneg_of_isNegNat $p) : Lean.Expr))
  | .isRat (i : Q(DivisionRing â„)) q n d p =>
    assumeInstancesCommute
    have p : Q(by clear! Â«$iÂ»; exact NormNum.IsRat $a $n $d) := p
                  -- âŠ¢ Sort ?u.471342
                               -- ğŸ‰ no goals
    if 0 < q âˆ§ q < 1 then
      let wâ‚ : Q(decide ((0 : â„š) < $n / $d) = true) := (q(Eq.refl true) : Lean.Expr)
      let wâ‚‚ : Q(decide ($n / $d < (1 : â„š)) = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.nonzero (q(log_nz_of_isRat $p $wâ‚ $wâ‚‚) : Lean.Expr))
    else if 1 < q then
      let w : Q(decide ((1 : â„š) < $n / $d) = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.positive (q(log_pos_of_isRat $p $w) : Lean.Expr))
    else if -1 < q âˆ§ q < 0 then
      let wâ‚ : Q(decide ($n / $d < (0 : â„š)) = true) := (q(Eq.refl true) : Lean.Expr)
      let wâ‚‚ : Q(decide ((-1 : â„š) < $n / $d) = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.nonzero (q(log_nz_of_isRat_neg $p $wâ‚ $wâ‚‚) : Lean.Expr))
    else if q < -1 then
      let w : Q(decide ($n / $d < (-1 : â„š)) = true) := (q(Eq.refl true) : Lean.Expr)
      pure (.positive (q(log_pos_of_isRat_neg $p $w) : Lean.Expr))
    else
      failure
  | _ => failure

end Mathlib.Meta.Positivity
