/-
Copyright (c) 2022 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey, Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Data.Int.Log

#align_import analysis.special_functions.log.base from "leanprover-community/mathlib"@"f23a09ce6d3f367220dc3cecad6b7eb69eb01690"

/-!
# Real logarithm base `b`

In this file we define `Real.logb` to be the logarithm of a real number in a given base `b`. We
define this as the division of the natural logarithms of the argument and the base, so that we have
a globally defined function with `logb b 0 = 0`, `logb b (-x) = logb b x` `logb 0 x = 0` and
`logb (-b) x = logb b x`.

We prove some basic properties of this function and its relation to `rpow`.

## Tags

logarithm, continuity
-/


open Set Filter Function

open Topology

noncomputable section

namespace Real

variable {b x y : ‚Ñù}

/-- The real logarithm in a given base. As with the natural logarithm, we define `logb b x` to
be `logb b |x|` for `x < 0`, and `0` for `x = 0`.-/
-- @[pp_nodot] -- Porting note: removed
noncomputable def logb (b x : ‚Ñù) : ‚Ñù :=
  log x / log b
#align real.logb Real.logb

theorem log_div_log : log x / log b = logb b x :=
  rfl
#align real.log_div_log Real.log_div_log

@[simp]
theorem logb_zero : logb b 0 = 0 := by simp [logb]
                                       -- üéâ no goals
#align real.logb_zero Real.logb_zero

@[simp]
theorem logb_one : logb b 1 = 0 := by simp [logb]
                                      -- üéâ no goals
#align real.logb_one Real.logb_one

@[simp]
theorem logb_abs (x : ‚Ñù) : logb b |x| = logb b x := by rw [logb, logb, log_abs]
                                                       -- üéâ no goals
#align real.logb_abs Real.logb_abs

@[simp]
theorem logb_neg_eq_logb (x : ‚Ñù) : logb b (-x) = logb b x := by
  rw [‚Üê logb_abs x, ‚Üê logb_abs (-x), abs_neg]
  -- üéâ no goals
#align real.logb_neg_eq_logb Real.logb_neg_eq_logb

theorem logb_mul (hx : x ‚â† 0) (hy : y ‚â† 0) : logb b (x * y) = logb b x + logb b y := by
  simp_rw [logb, log_mul hx hy, add_div]
  -- üéâ no goals
#align real.logb_mul Real.logb_mul

theorem logb_div (hx : x ‚â† 0) (hy : y ‚â† 0) : logb b (x / y) = logb b x - logb b y := by
  simp_rw [logb, log_div hx hy, sub_div]
  -- üéâ no goals
#align real.logb_div Real.logb_div

@[simp]
theorem logb_inv (x : ‚Ñù) : logb b x‚Åª¬π = -logb b x := by simp [logb, neg_div]
                                                        -- üéâ no goals
#align real.logb_inv Real.logb_inv

theorem inv_logb (a b : ‚Ñù) : (logb a b)‚Åª¬π = logb b a := by simp_rw [logb, inv_div]
                                                           -- üéâ no goals
#align real.inv_logb Real.inv_logb

theorem inv_logb_mul_base {a b : ‚Ñù} (h‚ÇÅ : a ‚â† 0) (h‚ÇÇ : b ‚â† 0) (c : ‚Ñù) :
    (logb (a * b) c)‚Åª¬π = (logb a c)‚Åª¬π + (logb b c)‚Åª¬π := by
  simp_rw [inv_logb]; exact logb_mul h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ logb c (a * b) = logb c a + logb c b
                      -- üéâ no goals
#align real.inv_logb_mul_base Real.inv_logb_mul_base

theorem inv_logb_div_base {a b : ‚Ñù} (h‚ÇÅ : a ‚â† 0) (h‚ÇÇ : b ‚â† 0) (c : ‚Ñù) :
    (logb (a / b) c)‚Åª¬π = (logb a c)‚Åª¬π - (logb b c)‚Åª¬π := by
  simp_rw [inv_logb]; exact logb_div h‚ÇÅ h‚ÇÇ
  -- ‚ä¢ logb c (a / b) = logb c a - logb c b
                      -- üéâ no goals
#align real.inv_logb_div_base Real.inv_logb_div_base

theorem logb_mul_base {a b : ‚Ñù} (h‚ÇÅ : a ‚â† 0) (h‚ÇÇ : b ‚â† 0) (c : ‚Ñù) :
    logb (a * b) c = ((logb a c)‚Åª¬π + (logb b c)‚Åª¬π)‚Åª¬π := by rw [‚Üê inv_logb_mul_base h‚ÇÅ h‚ÇÇ c, inv_inv]
                                                           -- üéâ no goals
#align real.logb_mul_base Real.logb_mul_base

theorem logb_div_base {a b : ‚Ñù} (h‚ÇÅ : a ‚â† 0) (h‚ÇÇ : b ‚â† 0) (c : ‚Ñù) :
    logb (a / b) c = ((logb a c)‚Åª¬π - (logb b c)‚Åª¬π)‚Åª¬π := by rw [‚Üê inv_logb_div_base h‚ÇÅ h‚ÇÇ c, inv_inv]
                                                           -- üéâ no goals
#align real.logb_div_base Real.logb_div_base

theorem mul_logb {a b c : ‚Ñù} (h‚ÇÅ : b ‚â† 0) (h‚ÇÇ : b ‚â† 1) (h‚ÇÉ : b ‚â† -1) :
    logb a b * logb b c = logb a c := by
  unfold logb
  -- ‚ä¢ log b / log a * (log c / log b) = log c / log a
  rw [mul_comm, div_mul_div_cancel _ (log_ne_zero.mpr ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©)]
  -- üéâ no goals
#align real.mul_logb Real.mul_logb

theorem div_logb {a b c : ‚Ñù} (h‚ÇÅ : c ‚â† 0) (h‚ÇÇ : c ‚â† 1) (h‚ÇÉ : c ‚â† -1) :
    logb a c / logb b c = logb a b :=
  div_div_div_cancel_left' _ _ <| log_ne_zero.mpr ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©
#align real.div_logb Real.div_logb

section BPosAndNeOne

variable (b_pos : 0 < b) (b_ne_one : b ‚â† 1)

private theorem log_b_ne_zero : log b ‚â† 0 := by
  have b_ne_zero : b ‚â† 0; linarith
  -- ‚ä¢ b ‚â† 0
                          -- ‚ä¢ log b ‚â† 0
  have b_ne_minus_one : b ‚â† -1; linarith
  -- ‚ä¢ b ‚â† -1
                                -- ‚ä¢ log b ‚â† 0
  simp [b_ne_one, b_ne_zero, b_ne_minus_one]
  -- üéâ no goals

@[simp]
theorem logb_rpow : logb b (b ^ x) = x := by
  rw [logb, div_eq_iff, log_rpow b_pos]
  -- ‚ä¢ log b ‚â† 0
  exact log_b_ne_zero b_pos b_ne_one
  -- üéâ no goals
#align real.logb_rpow Real.logb_rpow

theorem rpow_logb_eq_abs (hx : x ‚â† 0) : b ^ logb b x = |x| := by
  apply log_injOn_pos
  simp only [Set.mem_Ioi]
  apply rpow_pos_of_pos b_pos
  -- ‚ä¢ |x| ‚àà Ioi 0
  simp only [abs_pos, mem_Ioi, Ne.def, hx, not_false_iff]
  -- ‚ä¢ log (b ^ logb b x) = log |x|
  rw [log_rpow b_pos, logb, log_abs]
  -- ‚ä¢ log x / log b * log b = log x
  field_simp [log_b_ne_zero b_pos b_ne_one]
  -- üéâ no goals
#align real.rpow_logb_eq_abs Real.rpow_logb_eq_abs

@[simp]
theorem rpow_logb (hx : 0 < x) : b ^ logb b x = x := by
  rw [rpow_logb_eq_abs b_pos b_ne_one hx.ne']
  -- ‚ä¢ |x| = x
  exact abs_of_pos hx
  -- üéâ no goals
#align real.rpow_logb Real.rpow_logb

theorem rpow_logb_of_neg (hx : x < 0) : b ^ logb b x = -x := by
  rw [rpow_logb_eq_abs b_pos b_ne_one (ne_of_lt hx)]
  -- ‚ä¢ |x| = -x
  exact abs_of_neg hx
  -- üéâ no goals
#align real.rpow_logb_of_neg Real.rpow_logb_of_neg

theorem surjOn_logb : SurjOn (logb b) (Ioi 0) univ := fun x _ =>
  ‚ü®rpow b x, rpow_pos_of_pos b_pos x, logb_rpow b_pos b_ne_one‚ü©
#align real.surj_on_logb Real.surjOn_logb

theorem logb_surjective : Surjective (logb b) := fun x => ‚ü®b ^ x, logb_rpow b_pos b_ne_one‚ü©
#align real.logb_surjective Real.logb_surjective

@[simp]
theorem range_logb : range (logb b) = univ :=
  (logb_surjective b_pos b_ne_one).range_eq
#align real.range_logb Real.range_logb

theorem surjOn_logb' : SurjOn (logb b) (Iio 0) univ := by
  intro x _
  -- ‚ä¢ x ‚àà logb b '' Iio 0
  use -b ^ x
  -- ‚ä¢ -b ^ x ‚àà Iio 0 ‚àß logb b (-b ^ x) = x
  constructor
  -- ‚ä¢ -b ^ x ‚àà Iio 0
  ¬∑ simp only [Right.neg_neg_iff, Set.mem_Iio]
    -- ‚ä¢ 0 < b ^ x
    apply rpow_pos_of_pos b_pos
    -- üéâ no goals
  ¬∑ rw [logb_neg_eq_logb, logb_rpow b_pos b_ne_one]
    -- üéâ no goals
#align real.surj_on_logb' Real.surjOn_logb'

end BPosAndNeOne

section OneLtB

variable (hb : 1 < b)

private theorem b_pos : 0 < b := by linarith
                                    -- üéâ no goals

-- Porting note: prime added to avoid clashing with `b_ne_one` further down the file
private theorem b_ne_one' : b ‚â† 1 := by linarith
                                        -- üéâ no goals

@[simp]
theorem logb_le_logb (h : 0 < x) (h‚ÇÅ : 0 < y) : logb b x ‚â§ logb b y ‚Üî x ‚â§ y := by
  rw [logb, logb, div_le_div_right (log_pos hb), log_le_log h h‚ÇÅ]
  -- üéâ no goals
#align real.logb_le_logb Real.logb_le_logb

theorem logb_lt_logb (hx : 0 < x) (hxy : x < y) : logb b x < logb b y := by
  rw [logb, logb, div_lt_div_right (log_pos hb)]
  -- ‚ä¢ log x < log y
  exact log_lt_log hx hxy
  -- üéâ no goals
#align real.logb_lt_logb Real.logb_lt_logb

@[simp]
theorem logb_lt_logb_iff (hx : 0 < x) (hy : 0 < y) : logb b x < logb b y ‚Üî x < y := by
  rw [logb, logb, div_lt_div_right (log_pos hb)]
  -- ‚ä¢ log x < log y ‚Üî x < y
  exact log_lt_log_iff hx hy
  -- üéâ no goals
#align real.logb_lt_logb_iff Real.logb_lt_logb_iff

theorem logb_le_iff_le_rpow (hx : 0 < x) : logb b x ‚â§ y ‚Üî x ‚â§ b ^ y := by
  rw [‚Üê rpow_le_rpow_left_iff hb, rpow_logb (b_pos hb) (b_ne_one' hb) hx]
  -- üéâ no goals
#align real.logb_le_iff_le_rpow Real.logb_le_iff_le_rpow

theorem logb_lt_iff_lt_rpow (hx : 0 < x) : logb b x < y ‚Üî x < b ^ y := by
  rw [‚Üê rpow_lt_rpow_left_iff hb, rpow_logb (b_pos hb) (b_ne_one' hb) hx]
  -- üéâ no goals
#align real.logb_lt_iff_lt_rpow Real.logb_lt_iff_lt_rpow

theorem le_logb_iff_rpow_le (hy : 0 < y) : x ‚â§ logb b y ‚Üî b ^ x ‚â§ y := by
  rw [‚Üê rpow_le_rpow_left_iff hb, rpow_logb (b_pos hb) (b_ne_one' hb) hy]
  -- üéâ no goals
#align real.le_logb_iff_rpow_le Real.le_logb_iff_rpow_le

theorem lt_logb_iff_rpow_lt (hy : 0 < y) : x < logb b y ‚Üî b ^ x < y := by
  rw [‚Üê rpow_lt_rpow_left_iff hb, rpow_logb (b_pos hb) (b_ne_one' hb) hy]
  -- üéâ no goals
#align real.lt_logb_iff_rpow_lt Real.lt_logb_iff_rpow_lt

theorem logb_pos_iff (hx : 0 < x) : 0 < logb b x ‚Üî 1 < x := by
  rw [‚Üê @logb_one b]
  -- ‚ä¢ logb b 1 < logb b x ‚Üî 1 < x
  rw [logb_lt_logb_iff hb zero_lt_one hx]
  -- üéâ no goals
#align real.logb_pos_iff Real.logb_pos_iff

theorem logb_pos (hx : 1 < x) : 0 < logb b x := by
  rw [logb_pos_iff hb (lt_trans zero_lt_one hx)]
  -- ‚ä¢ 1 < x
  exact hx
  -- üéâ no goals
#align real.logb_pos Real.logb_pos

theorem logb_neg_iff (h : 0 < x) : logb b x < 0 ‚Üî x < 1 := by
  rw [‚Üê logb_one]
  -- ‚ä¢ logb b x < logb ?m.16248 1 ‚Üî x < 1
  exact logb_lt_logb_iff hb h zero_lt_one
  -- üéâ no goals
#align real.logb_neg_iff Real.logb_neg_iff

theorem logb_neg (h0 : 0 < x) (h1 : x < 1) : logb b x < 0 :=
  (logb_neg_iff hb h0).2 h1
#align real.logb_neg Real.logb_neg

theorem logb_nonneg_iff (hx : 0 < x) : 0 ‚â§ logb b x ‚Üî 1 ‚â§ x := by
  rw [‚Üê not_lt, logb_neg_iff hb hx, not_lt]
  -- üéâ no goals
#align real.logb_nonneg_iff Real.logb_nonneg_iff

theorem logb_nonneg (hx : 1 ‚â§ x) : 0 ‚â§ logb b x :=
  (logb_nonneg_iff hb (zero_lt_one.trans_le hx)).2 hx
#align real.logb_nonneg Real.logb_nonneg

theorem logb_nonpos_iff (hx : 0 < x) : logb b x ‚â§ 0 ‚Üî x ‚â§ 1 := by
  rw [‚Üê not_lt, logb_pos_iff hb hx, not_lt]
  -- üéâ no goals
#align real.logb_nonpos_iff Real.logb_nonpos_iff

theorem logb_nonpos_iff' (hx : 0 ‚â§ x) : logb b x ‚â§ 0 ‚Üî x ‚â§ 1 := by
  rcases hx.eq_or_lt with (rfl | hx)
  -- ‚ä¢ logb b 0 ‚â§ 0 ‚Üî 0 ‚â§ 1
  ¬∑ simp [le_refl, zero_le_one]
    -- üéâ no goals
  exact logb_nonpos_iff hb hx
  -- üéâ no goals
#align real.logb_nonpos_iff' Real.logb_nonpos_iff'

theorem logb_nonpos (hx : 0 ‚â§ x) (h'x : x ‚â§ 1) : logb b x ‚â§ 0 :=
  (logb_nonpos_iff' hb hx).2 h'x
#align real.logb_nonpos Real.logb_nonpos

theorem strictMonoOn_logb : StrictMonoOn (logb b) (Set.Ioi 0) := fun _ hx _ _ hxy =>
  logb_lt_logb hb hx hxy
#align real.strict_mono_on_logb Real.strictMonoOn_logb

theorem strictAntiOn_logb : StrictAntiOn (logb b) (Set.Iio 0) := by
  rintro x (hx : x < 0) y (hy : y < 0) hxy
  -- ‚ä¢ logb b y < logb b x
  rw [‚Üê logb_abs y, ‚Üê logb_abs x]
  -- ‚ä¢ logb b |y| < logb b |x|
  refine' logb_lt_logb hb (abs_pos.2 hy.ne) _
  -- ‚ä¢ |y| < |x|
  rwa [abs_of_neg hy, abs_of_neg hx, neg_lt_neg_iff]
  -- üéâ no goals
#align real.strict_anti_on_logb Real.strictAntiOn_logb

theorem logb_injOn_pos : Set.InjOn (logb b) (Set.Ioi 0) :=
  (strictMonoOn_logb hb).injOn
#align real.logb_inj_on_pos Real.logb_injOn_pos

theorem eq_one_of_pos_of_logb_eq_zero (h‚ÇÅ : 0 < x) (h‚ÇÇ : logb b x = 0) : x = 1 :=
  logb_injOn_pos hb (Set.mem_Ioi.2 h‚ÇÅ) (Set.mem_Ioi.2 zero_lt_one) (h‚ÇÇ.trans Real.logb_one.symm)
#align real.eq_one_of_pos_of_logb_eq_zero Real.eq_one_of_pos_of_logb_eq_zero

theorem logb_ne_zero_of_pos_of_ne_one (hx_pos : 0 < x) (hx : x ‚â† 1) : logb b x ‚â† 0 :=
  mt (eq_one_of_pos_of_logb_eq_zero hb hx_pos) hx
#align real.logb_ne_zero_of_pos_of_ne_one Real.logb_ne_zero_of_pos_of_ne_one

theorem tendsto_logb_atTop : Tendsto (logb b) atTop atTop :=
  Tendsto.atTop_div_const (log_pos hb) tendsto_log_atTop
#align real.tendsto_logb_at_top Real.tendsto_logb_atTop

end OneLtB

section BPosAndBLtOne

variable (b_pos : 0 < b) (b_lt_one : b < 1)

private theorem b_ne_one : b ‚â† 1 := by linarith
                                       -- üéâ no goals

@[simp]
theorem logb_le_logb_of_base_lt_one (h : 0 < x) (h‚ÇÅ : 0 < y) : logb b x ‚â§ logb b y ‚Üî y ‚â§ x := by
  rw [logb, logb, div_le_div_right_of_neg (log_neg b_pos b_lt_one), log_le_log h‚ÇÅ h]
  -- üéâ no goals
#align real.logb_le_logb_of_base_lt_one Real.logb_le_logb_of_base_lt_one

theorem logb_lt_logb_of_base_lt_one (hx : 0 < x) (hxy : x < y) : logb b y < logb b x := by
  rw [logb, logb, div_lt_div_right_of_neg (log_neg b_pos b_lt_one)]
  -- ‚ä¢ log x < log y
  exact log_lt_log hx hxy
  -- üéâ no goals
#align real.logb_lt_logb_of_base_lt_one Real.logb_lt_logb_of_base_lt_one

@[simp]
theorem logb_lt_logb_iff_of_base_lt_one (hx : 0 < x) (hy : 0 < y) :
    logb b x < logb b y ‚Üî y < x := by
  rw [logb, logb, div_lt_div_right_of_neg (log_neg b_pos b_lt_one)]
  -- ‚ä¢ log y < log x ‚Üî y < x
  exact log_lt_log_iff hy hx
  -- üéâ no goals
#align real.logb_lt_logb_iff_of_base_lt_one Real.logb_lt_logb_iff_of_base_lt_one

theorem logb_le_iff_le_rpow_of_base_lt_one (hx : 0 < x) : logb b x ‚â§ y ‚Üî b ^ y ‚â§ x := by
  rw [‚Üê rpow_le_rpow_left_iff_of_base_lt_one b_pos b_lt_one, rpow_logb b_pos (b_ne_one b_lt_one) hx]
  -- üéâ no goals
#align real.logb_le_iff_le_rpow_of_base_lt_one Real.logb_le_iff_le_rpow_of_base_lt_one

theorem logb_lt_iff_lt_rpow_of_base_lt_one (hx : 0 < x) : logb b x < y ‚Üî b ^ y < x := by
  rw [‚Üê rpow_lt_rpow_left_iff_of_base_lt_one b_pos b_lt_one, rpow_logb b_pos (b_ne_one b_lt_one) hx]
  -- üéâ no goals
#align real.logb_lt_iff_lt_rpow_of_base_lt_one Real.logb_lt_iff_lt_rpow_of_base_lt_one

theorem le_logb_iff_rpow_le_of_base_lt_one (hy : 0 < y) : x ‚â§ logb b y ‚Üî y ‚â§ b ^ x := by
  rw [‚Üê rpow_le_rpow_left_iff_of_base_lt_one b_pos b_lt_one, rpow_logb b_pos (b_ne_one b_lt_one) hy]
  -- üéâ no goals
#align real.le_logb_iff_rpow_le_of_base_lt_one Real.le_logb_iff_rpow_le_of_base_lt_one

theorem lt_logb_iff_rpow_lt_of_base_lt_one (hy : 0 < y) : x < logb b y ‚Üî y < b ^ x := by
  rw [‚Üê rpow_lt_rpow_left_iff_of_base_lt_one b_pos b_lt_one, rpow_logb b_pos (b_ne_one b_lt_one) hy]
  -- üéâ no goals
#align real.lt_logb_iff_rpow_lt_of_base_lt_one Real.lt_logb_iff_rpow_lt_of_base_lt_one

theorem logb_pos_iff_of_base_lt_one (hx : 0 < x) : 0 < logb b x ‚Üî x < 1 := by
  rw [‚Üê @logb_one b, logb_lt_logb_iff_of_base_lt_one b_pos b_lt_one zero_lt_one hx]
  -- üéâ no goals
#align real.logb_pos_iff_of_base_lt_one Real.logb_pos_iff_of_base_lt_one

theorem logb_pos_of_base_lt_one (hx : 0 < x) (hx' : x < 1) : 0 < logb b x := by
  rw [logb_pos_iff_of_base_lt_one b_pos b_lt_one hx]
  -- ‚ä¢ x < 1
  exact hx'
  -- üéâ no goals
#align real.logb_pos_of_base_lt_one Real.logb_pos_of_base_lt_one

theorem logb_neg_iff_of_base_lt_one (h : 0 < x) : logb b x < 0 ‚Üî 1 < x := by
  rw [‚Üê @logb_one b, logb_lt_logb_iff_of_base_lt_one b_pos b_lt_one h zero_lt_one]
  -- üéâ no goals
#align real.logb_neg_iff_of_base_lt_one Real.logb_neg_iff_of_base_lt_one

theorem logb_neg_of_base_lt_one (h1 : 1 < x) : logb b x < 0 :=
  (logb_neg_iff_of_base_lt_one b_pos b_lt_one (lt_trans zero_lt_one h1)).2 h1
#align real.logb_neg_of_base_lt_one Real.logb_neg_of_base_lt_one

theorem logb_nonneg_iff_of_base_lt_one (hx : 0 < x) : 0 ‚â§ logb b x ‚Üî x ‚â§ 1 := by
  rw [‚Üê not_lt, logb_neg_iff_of_base_lt_one b_pos b_lt_one hx, not_lt]
  -- üéâ no goals
#align real.logb_nonneg_iff_of_base_lt_one Real.logb_nonneg_iff_of_base_lt_one

theorem logb_nonneg_of_base_lt_one (hx : 0 < x) (hx' : x ‚â§ 1) : 0 ‚â§ logb b x := by
  rw [logb_nonneg_iff_of_base_lt_one b_pos b_lt_one hx]
  -- ‚ä¢ x ‚â§ 1
  exact hx'
  -- üéâ no goals
#align real.logb_nonneg_of_base_lt_one Real.logb_nonneg_of_base_lt_one

theorem logb_nonpos_iff_of_base_lt_one (hx : 0 < x) : logb b x ‚â§ 0 ‚Üî 1 ‚â§ x := by
  rw [‚Üê not_lt, logb_pos_iff_of_base_lt_one b_pos b_lt_one hx, not_lt]
  -- üéâ no goals
#align real.logb_nonpos_iff_of_base_lt_one Real.logb_nonpos_iff_of_base_lt_one

theorem strictAntiOn_logb_of_base_lt_one : StrictAntiOn (logb b) (Set.Ioi 0) := fun _ hx _ _ hxy =>
  logb_lt_logb_of_base_lt_one b_pos b_lt_one hx hxy
#align real.strict_anti_on_logb_of_base_lt_one Real.strictAntiOn_logb_of_base_lt_one

theorem strictMonoOn_logb_of_base_lt_one : StrictMonoOn (logb b) (Set.Iio 0) := by
  rintro x (hx : x < 0) y (hy : y < 0) hxy
  -- ‚ä¢ logb b x < logb b y
  rw [‚Üê logb_abs y, ‚Üê logb_abs x]
  -- ‚ä¢ logb b |x| < logb b |y|
  refine' logb_lt_logb_of_base_lt_one b_pos b_lt_one (abs_pos.2 hy.ne) _
  -- ‚ä¢ |y| < |x|
  rwa [abs_of_neg hy, abs_of_neg hx, neg_lt_neg_iff]
  -- üéâ no goals
#align real.strict_mono_on_logb_of_base_lt_one Real.strictMonoOn_logb_of_base_lt_one

theorem logb_injOn_pos_of_base_lt_one : Set.InjOn (logb b) (Set.Ioi 0) :=
  (strictAntiOn_logb_of_base_lt_one b_pos b_lt_one).injOn
#align real.logb_inj_on_pos_of_base_lt_one Real.logb_injOn_pos_of_base_lt_one

theorem eq_one_of_pos_of_logb_eq_zero_of_base_lt_one (h‚ÇÅ : 0 < x) (h‚ÇÇ : logb b x = 0) : x = 1 :=
  logb_injOn_pos_of_base_lt_one b_pos b_lt_one (Set.mem_Ioi.2 h‚ÇÅ) (Set.mem_Ioi.2 zero_lt_one)
    (h‚ÇÇ.trans Real.logb_one.symm)
#align real.eq_one_of_pos_of_logb_eq_zero_of_base_lt_one Real.eq_one_of_pos_of_logb_eq_zero_of_base_lt_one

theorem logb_ne_zero_of_pos_of_ne_one_of_base_lt_one (hx_pos : 0 < x) (hx : x ‚â† 1) : logb b x ‚â† 0 :=
  mt (eq_one_of_pos_of_logb_eq_zero_of_base_lt_one b_pos b_lt_one hx_pos) hx
#align real.logb_ne_zero_of_pos_of_ne_one_of_base_lt_one Real.logb_ne_zero_of_pos_of_ne_one_of_base_lt_one

theorem tendsto_logb_atTop_of_base_lt_one : Tendsto (logb b) atTop atBot := by
  rw [tendsto_atTop_atBot]
  -- ‚ä¢ ‚àÄ (b_1 : ‚Ñù), ‚àÉ i, ‚àÄ (a : ‚Ñù), i ‚â§ a ‚Üí logb b a ‚â§ b_1
  intro e
  -- ‚ä¢ ‚àÉ i, ‚àÄ (a : ‚Ñù), i ‚â§ a ‚Üí logb b a ‚â§ e
  use 1 ‚äî b ^ e
  -- ‚ä¢ ‚àÄ (a : ‚Ñù), 1 ‚äî b ^ e ‚â§ a ‚Üí logb b a ‚â§ e
  intro a
  -- ‚ä¢ 1 ‚äî b ^ e ‚â§ a ‚Üí logb b a ‚â§ e
  simp only [and_imp, sup_le_iff]
  -- ‚ä¢ 1 ‚â§ a ‚Üí b ^ e ‚â§ a ‚Üí logb b a ‚â§ e
  intro ha
  -- ‚ä¢ b ^ e ‚â§ a ‚Üí logb b a ‚â§ e
  rw [logb_le_iff_le_rpow_of_base_lt_one b_pos b_lt_one]
  -- ‚ä¢ b ^ e ‚â§ a ‚Üí b ^ e ‚â§ a
  tauto
  -- ‚ä¢ 0 < a
  exact lt_of_lt_of_le zero_lt_one ha
  -- üéâ no goals
#align real.tendsto_logb_at_top_of_base_lt_one Real.tendsto_logb_atTop_of_base_lt_one

end BPosAndBLtOne

theorem floor_logb_nat_cast {b : ‚Ñï} {r : ‚Ñù} (hb : 1 < b) (hr : 0 ‚â§ r) :
    ‚åälogb b r‚åã = Int.log b r := by
  obtain rfl | hr := hr.eq_or_lt
  -- ‚ä¢ ‚åälogb (‚Üëb) 0‚åã = Int.log b 0
  ¬∑ rw [logb_zero, Int.log_zero_right, Int.floor_zero]
    -- üéâ no goals
  have hb1' : 1 < (b : ‚Ñù) := Nat.one_lt_cast.mpr hb
  -- ‚ä¢ ‚åälogb (‚Üëb) r‚åã = Int.log b r
  apply le_antisymm
  -- ‚ä¢ ‚åälogb (‚Üëb) r‚åã ‚â§ Int.log b r
  ¬∑ rw [‚Üê Int.zpow_le_iff_le_log hb hr, ‚Üê rpow_int_cast b]
    -- ‚ä¢ ‚Üëb ^ ‚Üë‚åälogb (‚Üëb) r‚åã ‚â§ r
    refine' le_of_le_of_eq _ (rpow_logb (zero_lt_one.trans hb1') hb1'.ne' hr)
    -- ‚ä¢ ‚Üëb ^ ‚Üë‚åälogb (‚Üëb) r‚åã ‚â§ ‚Üëb ^ logb (‚Üëb) r
    exact rpow_le_rpow_of_exponent_le hb1'.le (Int.floor_le _)
    -- üéâ no goals
  ¬∑ rw [Int.le_floor, le_logb_iff_rpow_le hb1' hr, rpow_int_cast]
    -- ‚ä¢ ‚Üëb ^ Int.log b r ‚â§ r
    exact Int.zpow_log_le_self hb hr
    -- üéâ no goals
#align real.floor_logb_nat_cast Real.floor_logb_nat_cast

theorem ceil_logb_nat_cast {b : ‚Ñï} {r : ‚Ñù} (hb : 1 < b) (hr : 0 ‚â§ r) :
    ‚åàlogb b r‚åâ = Int.clog b r := by
  obtain rfl | hr := hr.eq_or_lt
  -- ‚ä¢ ‚åàlogb (‚Üëb) 0‚åâ = Int.clog b 0
  ¬∑ rw [logb_zero, Int.clog_zero_right, Int.ceil_zero]
    -- üéâ no goals
  have hb1' : 1 < (b : ‚Ñù) := Nat.one_lt_cast.mpr hb
  -- ‚ä¢ ‚åàlogb (‚Üëb) r‚åâ = Int.clog b r
  apply le_antisymm
  -- ‚ä¢ ‚åàlogb (‚Üëb) r‚åâ ‚â§ Int.clog b r
  ¬∑ rw [Int.ceil_le, logb_le_iff_le_rpow hb1' hr, rpow_int_cast]
    -- ‚ä¢ r ‚â§ ‚Üëb ^ Int.clog b r
    refine' Int.self_le_zpow_clog hb r
    -- üéâ no goals
  ¬∑ rw [‚Üê Int.le_zpow_iff_clog_le hb hr, ‚Üê rpow_int_cast b]
    -- ‚ä¢ r ‚â§ ‚Üëb ^ ‚Üë‚åàlogb (‚Üëb) r‚åâ
    refine' (rpow_logb (zero_lt_one.trans hb1') hb1'.ne' hr).symm.trans_le _
    -- ‚ä¢ ‚Üëb ^ logb (‚Üëb) r ‚â§ ‚Üëb ^ ‚Üë‚åàlogb (‚Üëb) r‚åâ
    exact rpow_le_rpow_of_exponent_le hb1'.le (Int.le_ceil _)
    -- üéâ no goals
#align real.ceil_logb_nat_cast Real.ceil_logb_nat_cast

@[simp]
theorem logb_eq_zero : logb b x = 0 ‚Üî b = 0 ‚à® b = 1 ‚à® b = -1 ‚à® x = 0 ‚à® x = 1 ‚à® x = -1 := by
  simp_rw [logb, div_eq_zero_iff, log_eq_zero]
  -- ‚ä¢ (x = 0 ‚à® x = 1 ‚à® x = -1) ‚à® b = 0 ‚à® b = 1 ‚à® b = -1 ‚Üî b = 0 ‚à® b = 1 ‚à® b = -1 ‚à® ‚Ä¶
  tauto
  -- üéâ no goals
#align real.logb_eq_zero Real.logb_eq_zero

-- TODO add other limits and continuous API lemmas analogous to those in Log.lean
open BigOperators

theorem logb_prod {Œ± : Type*} (s : Finset Œ±) (f : Œ± ‚Üí ‚Ñù) (hf : ‚àÄ x ‚àà s, f x ‚â† 0) :
    logb b (‚àè i in s, f i) = ‚àë i in s, logb b (f i) := by
  classical
    induction' s using Finset.induction_on with a s ha ih
    ¬∑ simp
    simp only [Finset.mem_insert, forall_eq_or_imp] at hf
    simp [ha, ih hf.2, logb_mul hf.1 (Finset.prod_ne_zero_iff.2 hf.2)]
#align real.logb_prod Real.logb_prod

end Real
