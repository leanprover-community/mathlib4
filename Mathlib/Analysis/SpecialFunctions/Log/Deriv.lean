/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne
-/
module

public import Mathlib.Analysis.Calculus.Deriv.Pow
public import Mathlib.Analysis.Calculus.LogDeriv
public import Mathlib.Analysis.SpecialFunctions.Log.Basic
public import Mathlib.Analysis.SpecialFunctions.ExpDeriv
public import Mathlib.Analysis.Calculus.Deriv.MeanValue
public import Mathlib.Tactic.AdaptationNote

/-!
# Derivative and series expansion of real logarithm

In this file we prove that `Real.log` is infinitely smooth at all nonzero `x : ‚Ñù`. We also prove
that the series `‚àë' n : ‚Ñï, x ^ (n + 1) / (n + 1)` converges to `(-Real.log (1 - x))` for all
`x : ‚Ñù`, `|x| < 1`.

## Tags

logarithm, derivative
-/

public section


open Filter Finset Set

open scoped Topology ContDiff

namespace Real

variable {x : ‚Ñù}

theorem hasStrictDerivAt_log_of_pos (hx : 0 < x) : HasStrictDerivAt log x‚Åª¬π x := by
  have : HasStrictDerivAt log (exp <| log x)‚Åª¬π x :=
    (hasStrictDerivAt_exp <| log x).of_local_left_inverse (continuousAt_log hx.ne')
        (ne_of_gt <| exp_pos _) <|
      Eventually.mono (lt_mem_nhds hx) @exp_log
  rwa [exp_log hx] at this

theorem hasStrictDerivAt_log (hx : x ‚â† 0) : HasStrictDerivAt log x‚Åª¬π x := by
  rcases hx.lt_or_gt with hx | hx
  ¬∑ convert (hasStrictDerivAt_log_of_pos (neg_pos.mpr hx)).comp x (hasStrictDerivAt_neg x) using 1
    ¬∑ ext y; exact (log_neg_eq_log y).symm
    ¬∑ ring
  ¬∑ exact hasStrictDerivAt_log_of_pos hx

theorem hasDerivAt_log (hx : x ‚â† 0) : HasDerivAt log x‚Åª¬π x :=
  (hasStrictDerivAt_log hx).hasDerivAt

@[fun_prop] theorem differentiableAt_log (hx : x ‚â† 0) : DifferentiableAt ‚Ñù log x :=
  (hasDerivAt_log hx).differentiableAt

theorem differentiableOn_log : DifferentiableOn ‚Ñù log {0}·∂ú := fun _x hx =>
  (differentiableAt_log hx).differentiableWithinAt

@[simp]
theorem differentiableAt_log_iff : DifferentiableAt ‚Ñù log x ‚Üî x ‚â† 0 :=
  ‚ü®fun h => continuousAt_log_iff.1 h.continuousAt, differentiableAt_log‚ü©

theorem deriv_log (x : ‚Ñù) : deriv log x = x‚Åª¬π :=
  if hx : x = 0 then by
    rw [deriv_zero_of_not_differentiableAt (differentiableAt_log_iff.not_left.2 hx), hx, inv_zero]
  else (hasDerivAt_log hx).deriv

@[simp]
theorem deriv_log' : deriv log = Inv.inv :=
  funext deriv_log

theorem contDiffAt_log {n : WithTop ‚Ñï‚àû} {x : ‚Ñù} : ContDiffAt ‚Ñù n log x ‚Üî x ‚â† 0 := by
  refine ‚ü®fun h ‚Ü¶ continuousAt_log_iff.1 h.continuousAt, fun hx ‚Ü¶ ?_‚ü©
  have A y (hy : 0 < y) : ContDiffAt ‚Ñù n log y := by
    apply expPartialHomeomorph.contDiffAt_symm_deriv (f‚ÇÄ' := y) hy.ne' (by simpa)
    ¬∑ convert hasDerivAt_exp (log y)
      rw [exp_log hy]
    ¬∑ exact analyticAt_rexp.contDiffAt
  rcases hx.lt_or_gt with hx | hx
  ¬∑ have : ContDiffAt ‚Ñù n (log ‚àò (fun y ‚Ü¶ -y)) x := by
      apply ContDiffAt.comp
      ¬∑ apply A _ (Left.neg_pos_iff.mpr hx)
      apply contDiffAt_id.neg
    convert this
    ext x
    simp
  ¬∑ exact A x hx

@[fun_prop]
theorem contDiffOn_log {n : WithTop ‚Ñï‚àû} : ContDiffOn ‚Ñù n log {0}·∂ú := by
  intro x hx
  push _ ‚àà _ at hx
  exact (contDiffAt_log.2 hx).contDiffWithinAt

end Real

section LogDifferentiable

open Real

section deriv

variable {f : ‚Ñù ‚Üí ‚Ñù} {x f' : ‚Ñù} {s : Set ‚Ñù}

theorem HasDerivWithinAt.log (hf : HasDerivWithinAt f f' s x) (hx : f x ‚â† 0) :
    HasDerivWithinAt (fun y => log (f y)) (f' / f x) s x := by
  rw [div_eq_inv_mul]
  exact (hasDerivAt_log hx).comp_hasDerivWithinAt x hf

theorem HasDerivAt.log (hf : HasDerivAt f f' x) (hx : f x ‚â† 0) :
    HasDerivAt (fun y => log (f y)) (f' / f x) x := by
  rw [‚Üê hasDerivWithinAt_univ] at *
  exact hf.log hx

theorem HasStrictDerivAt.log (hf : HasStrictDerivAt f f' x) (hx : f x ‚â† 0) :
    HasStrictDerivAt (fun y => log (f y)) (f' / f x) x := by
  rw [div_eq_inv_mul]
  exact (hasStrictDerivAt_log hx).comp x hf

theorem derivWithin.log (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0)
    (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x => log (f x)) s x = derivWithin f s x / f x :=
  (hf.hasDerivWithinAt.log hx).derivWithin hxs

@[simp]
theorem deriv.log (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    deriv (fun x => log (f x)) x = deriv f x / f x :=
  (hf.hasDerivAt.log hx).deriv

/-- The derivative of `log ‚àò f` is the logarithmic derivative provided `f` is differentiable and
`f x  ‚â† 0`. -/
lemma Real.deriv_log_comp_eq_logDeriv {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù} (h‚ÇÅ : DifferentiableAt ‚Ñù f x)
    (h‚ÇÇ : f x ‚â† 0) : deriv (log ‚àò f) x = logDeriv f x := by
  simp only [logDeriv, Pi.div_apply, ‚Üê deriv.log h‚ÇÅ h‚ÇÇ, Function.comp_def]

end deriv

section fderiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚Ñù} {x : E}
  {f' : StrongDual ‚Ñù E} {s : Set E}

theorem HasFDerivWithinAt.log (hf : HasFDerivWithinAt f f' s x) (hx : f x ‚â† 0) :
    HasFDerivWithinAt (fun x => log (f x)) ((f x)‚Åª¬π ‚Ä¢ f') s x :=
  (hasDerivAt_log hx).comp_hasFDerivWithinAt x hf

theorem HasFDerivAt.log (hf : HasFDerivAt f f' x) (hx : f x ‚â† 0) :
    HasFDerivAt (fun x => log (f x)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
  (hasDerivAt_log hx).comp_hasFDerivAt x hf

theorem HasStrictFDerivAt.log (hf : HasStrictFDerivAt f f' x) (hx : f x ‚â† 0) :
    HasStrictFDerivAt (fun x => log (f x)) ((f x)‚Åª¬π ‚Ä¢ f') x :=
  (hasStrictDerivAt_log hx).comp_hasStrictFDerivAt x hf

theorem DifferentiableWithinAt.log (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0) :
    DifferentiableWithinAt ‚Ñù (fun x => log (f x)) s x :=
  (hf.hasFDerivWithinAt.log hx).differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.log (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    DifferentiableAt ‚Ñù (fun x => log (f x)) x :=
  (hf.hasFDerivAt.log hx).differentiableAt

@[fun_prop]
theorem ContDiffAt.log {n} (hf : ContDiffAt ‚Ñù n f x) (hx : f x ‚â† 0) :
    ContDiffAt ‚Ñù n (fun x => log (f x)) x :=
  (contDiffAt_log.2 hx).comp x hf

@[fun_prop]
theorem ContDiffWithinAt.log {n} (hf : ContDiffWithinAt ‚Ñù n f s x) (hx : f x ‚â† 0) :
    ContDiffWithinAt ‚Ñù n (fun x => log (f x)) s x :=
  (contDiffAt_log.2 hx).comp_contDiffWithinAt x hf

@[fun_prop]
theorem ContDiffOn.log {n} (hf : ContDiffOn ‚Ñù n f s) (hs : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContDiffOn ‚Ñù n (fun x => log (f x)) s := fun x hx => (hf x hx).log (hs x hx)

@[fun_prop]
theorem ContDiff.log {n} (hf : ContDiff ‚Ñù n f) (h : ‚àÄ x, f x ‚â† 0) :
    ContDiff ‚Ñù n fun x => log (f x) :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.log (h x)

@[fun_prop]
theorem DifferentiableOn.log (hf : DifferentiableOn ‚Ñù f s) (hx : ‚àÄ x ‚àà s, f x ‚â† 0) :
    DifferentiableOn ‚Ñù (fun x => log (f x)) s := fun x h => (hf x h).log (hx x h)

@[simp, fun_prop]
theorem Differentiable.log (hf : Differentiable ‚Ñù f) (hx : ‚àÄ x, f x ‚â† 0) :
    Differentiable ‚Ñù fun x => log (f x) := fun x => (hf x).log (hx x)

theorem fderivWithin.log (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0)
    (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    fderivWithin ‚Ñù (fun x => log (f x)) s x = (f x)‚Åª¬π ‚Ä¢ fderivWithin ‚Ñù f s x :=
  (hf.hasFDerivWithinAt.log hx).fderivWithin hxs

@[simp]
theorem fderiv.log (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0) :
    fderiv ‚Ñù (fun x => log (f x)) x = (f x)‚Åª¬π ‚Ä¢ fderiv ‚Ñù f x :=
  (hf.hasFDerivAt.log hx).fderiv

end fderiv

end LogDifferentiable

namespace Real

-- see https://github.com/leanprover-community/mathlib4/issues/29041
set_option linter.unusedSimpArgs false in
/-- A crude lemma estimating the difference between `log (1-x)` and its Taylor series at `0`,
where the main point of the bound is that it tends to `0`. The goal is to deduce the series
expansion of the logarithm, in `hasSum_pow_div_log_of_abs_lt_1`.

TODO: use one of generic theorems about Taylor's series to prove this estimate.
-/
theorem abs_log_sub_add_sum_range_le {x : ‚Ñù} (h : |x| < 1) (n : ‚Ñï) :
    |(‚àë i ‚àà range n, x ^ (i + 1) / (i + 1)) + log (1 - x)| ‚â§ |x| ^ (n + 1) / (1 - |x|) := by
  /- For the proof, we show that the derivative of the function to be estimated is small,
    and then apply the mean value inequality. -/
  let F : ‚Ñù ‚Üí ‚Ñù := fun x => (‚àë i ‚àà range n, x ^ (i + 1) / (i + 1)) + log (1 - x)
  let F' : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶ -x ^ n / (1 - x)
  -- Porting note: In `mathlib3`, the proof used `deriv`/`DifferentiableAt`. `simp` failed to
  -- compute `deriv`, so I changed the proof to use `HasDerivAt` instead
  -- First step: compute the derivative of `F`
  have A : ‚àÄ y ‚àà Ioo (-1 : ‚Ñù) 1, HasDerivAt F (F' y) y := fun y hy ‚Ü¶ by
    have : HasDerivAt F ((‚àë i ‚àà range n, ‚Üë(i + 1) * y ^ i / (‚Üëi + 1)) + (-1) / (1 - y)) y :=
      .add (.fun_sum fun i _ ‚Ü¶ (hasDerivAt_pow (i + 1) y).div_const ((i : ‚Ñù) + 1))
        (((hasDerivAt_id y).const_sub _).log <| sub_ne_zero.2 hy.2.ne')
    convert this using 1
    calc
      -y ^ n / (1 - y) = ‚àë i ‚àà Finset.range n, y ^ i + -1 / (1 - y) := by
        simp [field, geom_sum_eq hy.2.ne, sub_ne_zero.2 hy.2.ne, sub_ne_zero.2 hy.2.ne']
        ring
      _ = ‚àë i ‚àà Finset.range n, ‚Üë(i + 1) * y ^ i / (‚Üëi + 1) + -1 / (1 - y) := by
        congr with i
        rw [Nat.cast_succ, mul_div_cancel_left‚ÇÄ _ (Nat.cast_add_one_pos i).ne']
  -- second step: show that the derivative of `F` is small
  have B : ‚àÄ y ‚àà Icc (-|x|) |x|, |F' y| ‚â§ |x| ^ n / (1 - |x|) := fun y hy ‚Ü¶
    calc
      |F' y| = |y| ^ n / |1 - y| := by simp [F', abs_div]
      _ ‚â§ |x| ^ n / (1 - |x|) := by
        have : |y| ‚â§ |x| := abs_le.2 hy
        have : 1 - |x| ‚â§ |1 - y| := le_trans (by linarith [hy.2]) (le_abs_self _)
        gcongr
        exact sub_pos.2 h
  -- third step: apply the mean value inequality
  have C : ‚ÄñF x - F 0‚Äñ ‚â§ |x| ^ n / (1 - |x|) * ‚Äñx - 0‚Äñ := by
    refine Convex.norm_image_sub_le_of_norm_hasDerivWithin_le
      (fun y hy ‚Ü¶ (A _ ?_).hasDerivWithinAt) B (convex_Icc _ _) ?_ ?_
    ¬∑ exact Icc_subset_Ioo (neg_lt_neg h) h hy
    ¬∑ simp
    ¬∑ simp [le_abs_self x, neg_le.mp (neg_le_abs x)]
  -- fourth step: conclude by massaging the inequality of the third step
  simpa [F, div_mul_eq_mul_div, pow_succ] using C

-- see https://github.com/leanprover-community/mathlib4/issues/29041
set_option linter.unusedSimpArgs false in
/--
Compute the derivative of the difference between $\frac{1}{2} * \log(\frac{1+x}{1-x})$ and its
Taylor series at `0` up to order `n`. This is an auxiliary lemma for
`sum_range_sub_log_div_le` and `sum_range_le_log_div`.
Note that thanks to the geometric series, the derivative has a particularly simple form, and means
that it is more convenient to avoid Taylor's theorem.
-/
lemma hasDerivAt_half_log_one_add_div_one_sub_sub_sum_range
    {y : ‚Ñù} (n : ‚Ñï) (hy‚ÇÅ : -1 < y) (hy‚ÇÇ : y < 1) :
    HasDerivAt
      (fun x ‚Ü¶ 1 / 2 * log ((1 + x) / (1 - x)) - (‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1)))
      ((y ^ 2) ^ n / (1 - y ^ 2)) y := by
  refine ((((((hasDerivAt_id _).const_add _).div ((hasDerivAt_id _).const_sub _) (by grind)).log
          ?_).const_mul _).sub (HasDerivAt.fun_sum fun i hi ‚Ü¶ (hasDerivAt_pow _ _).div_const _))
        |>.congr_deriv ?_
  ¬∑ simp only [id_eq, div_ne_zero_iff, Pi.div_apply]; grind
  have : (‚àë i ‚àà range n, (2 * i + 1) * y ^ (2 * i) / (2 * i + 1)) =
      (‚àë i ‚àà range n, (y ^ 2) ^ i) := by
    congr with i
    simp [field, mul_comm, ‚Üê pow_mul]
  have hy‚ÇÉ : y ^ 2 ‚â† 1 := by simp [hy‚ÇÅ.ne', hy‚ÇÇ.ne]
  have hy‚ÇÑ : (1 - y) * (1 + y) = 1 - y ^ 2 := by ring
  simp [this, field, geom_sum_eq hy‚ÇÉ, hy‚ÇÑ, sub_ne_zero_of_ne, hy‚ÇÉ.symm]
  ring

/-- A lemma estimating the difference between $\frac{1}{2} * \log(\frac{1+x}{1-x})$ and its
Taylor series at `0`, where the bound tends to `0`. This bound is particularly useful for explicit
estimates of logarithms.

Note that thanks to the geometric series, the derivative has a particularly simple form, and means
that it is more convenient to avoid Taylor's theorem for this proof.
-/
lemma sum_range_sub_log_div_le {x : ‚Ñù} (h : |x| < 1) (n : ‚Ñï) :
    |1 / 2 * log ((1 + x) / (1 - x)) - ‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1)| ‚â§
      |x| ^ (2 * n + 1) / (1 - x ^ 2) := by
  let F (x : ‚Ñù) : ‚Ñù :=
    1 / 2 * log ((1 + x) / (1 - x)) - (‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1))
  let F' (y : ‚Ñù) : ‚Ñù := (y ^ 2) ^ n / (1 - y ^ 2)
  have hI : Icc (-|x|) |x| ‚äÜ Ioo (-1 : ‚Ñù) 1 := Icc_subset_Ioo (by simp [h]) h
  -- First step: compute the derivative of `F`
  have A : ‚àÄ y ‚àà Ioo (-1 : ‚Ñù) 1, HasDerivAt F (F' y) y := by
    intro y hy
    exact hasDerivAt_half_log_one_add_div_one_sub_sub_sum_range _ (by grind) (by grind)
  -- second step: show that the derivative of `F` is small
  have B : ‚àÄ y ‚àà Set.Icc (-|x|) |x|, ‚ÄñF' y‚Äñ ‚â§ |x| ^ (2 * n) / (1 - x ^ 2) := fun y hy ‚Ü¶ by
    have : y ^ 2 ‚â§ x ^ 2 := sq_le_sq.2 (abs_le.2 hy)
    calc
      ‚ÄñF' y‚Äñ = (y ^ 2) ^ n / |1 - y ^ 2| := by simp [F']
      _ = (y ^ 2) ^ n / (1 - y ^ 2) := by rw [abs_of_pos (by simpa [abs_lt] using hI hy)]
      _ ‚â§ (x ^ 2) ^ n / (1 - x ^ 2) := by gcongr ?_ ^ n / (1 - ?_); simpa [abs_lt] using h
      _ ‚â§ |x| ^ (2 * n) / (1 - x ^ 2) := by simp [pow_mul]
  -- third step: apply the mean value inequality
  have C : ‚ÄñF x - F 0‚Äñ ‚â§ |x| ^ (2 * n) / (1 - x ^ 2) * ‚Äñx - 0‚Äñ :=
    (convex_Icc (-|x|) |x|).norm_image_sub_le_of_norm_hasDerivWithin_le
      (fun y hy ‚Ü¶ (A _ (hI hy)).hasDerivWithinAt) B
      (by simp) (by simp [le_abs_self, neg_le, neg_le_abs x])
  -- fourth step: conclude by massaging the inequality of the third step
  simpa [F, pow_succ, div_mul_eq_mul_div] using C

/--
For `0 ‚â§ x < 1`, the partial sums of the series expansion of $\frac{1}{2} * \log(\frac{1+x}{1-x})$
at `0` form a lower bound for it. This shows that the absolute value in `sum_range_sub_log_div_le`
can be dropped, and gives explicit lower bounds for logarithms.
-/
lemma sum_range_le_log_div {x : ‚Ñù} (h‚ÇÄ : 0 ‚â§ x) (h : x < 1) (n : ‚Ñï) :
    ‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1) ‚â§ 1 / 2 * log ((1 + x) / (1 - x)) := by
  let F (x : ‚Ñù) : ‚Ñù :=
    1 / 2 * log ((1 + x) / (1 - x)) - (‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1))
  let F' (y : ‚Ñù) : ‚Ñù := (y ^ 2) ^ n / (1 - y ^ 2)
  -- First step: compute the derivative of `F`
  have A : ‚àÄ y ‚àà Icc 0 x, HasDerivAt F (F' y) y := by
    intro y hy
    exact hasDerivAt_half_log_one_add_div_one_sub_sub_sum_range _ (by grind) (by grind)
  -- It suffices to show that `F` is monotone on `[0, x]`
  suffices MonotoneOn F (Icc 0 x) by simpa [F] using this ‚ü®le_rfl, h‚ÇÄ‚ü© ‚ü®h‚ÇÄ, le_rfl‚ü© h‚ÇÄ
  -- Second step: show that the derivative of `F` is nonnegative; it has been computed already.
  refine monotoneOn_of_hasDerivWithinAt_nonneg (convex_Icc 0 x)
    (fun y hy ‚Ü¶ (A y hy).continuousAt.continuousWithinAt)
    (fun y hy ‚Ü¶ (A y (interior_subset hy)).hasDerivWithinAt) ?_
  intro y hy
  simp only [interior_Icc, Set.mem_Ioo] at hy
  have : 0 ‚â§ 1 - y ^ 2 := by calc
    0 ‚â§ 1 - x ^ 2 := by simp [abs_of_nonneg h‚ÇÄ, h.le]
    _ ‚â§ 1 - y ^ 2 := sub_le_sub_left (pow_le_pow_left‚ÇÄ hy.1.le hy.2.le 2) 1
  positivity

lemma log_div_le_sum_range_add {x : ‚Ñù} (h‚ÇÄ : 0 ‚â§ x) (h : x < 1) (n : ‚Ñï) :
    1 / 2 * log ((1 + x) / (1 - x)) ‚â§
      (‚àë i ‚àà range n, x ^ (2 * i + 1) / (2 * i + 1)) + x ^ (2 * n + 1) / (1 - x ^ 2) := by
  have h‚ÇÅ := sum_range_sub_log_div_le (by rwa [abs_of_nonneg h‚ÇÄ]) n
  rwa [abs_of_nonneg (sub_nonneg_of_le (sum_range_le_log_div h‚ÇÄ h n)), abs_of_nonneg h‚ÇÄ,
    sub_le_iff_le_add'] at h‚ÇÅ

/-- Power series expansion of the logarithm around `1`. -/
theorem hasSum_pow_div_log_of_abs_lt_one {x : ‚Ñù} (h : |x| < 1) :
    HasSum (fun n : ‚Ñï => x ^ (n + 1) / (n + 1)) (-log (1 - x)) := by
  rw [Summable.hasSum_iff_tendsto_nat]
  ¬∑ show Tendsto (fun n : ‚Ñï => ‚àë i ‚àà range n, x ^ (i + 1) / (i + 1)) atTop (ùìù (-log (1 - x)))
    rw [tendsto_iff_norm_sub_tendsto_zero]
    simp only [norm_eq_abs, sub_neg_eq_add]
    refine squeeze_zero (fun n => abs_nonneg _) (abs_log_sub_add_sum_range_le h) ?_
    suffices Tendsto (fun t : ‚Ñï => |x| ^ (t + 1) / (1 - |x|)) atTop (ùìù (|x| * 0 / (1 - |x|))) by
      simpa
    simp only [pow_succ']
    refine (tendsto_const_nhds.mul ?_).div_const _
    exact tendsto_pow_atTop_nhds_zero_of_lt_one (abs_nonneg _) h
  show Summable fun n : ‚Ñï => x ^ (n + 1) / (n + 1)
  refine .of_norm_bounded (summable_geometric_of_lt_one (abs_nonneg _) h) fun i => ?_
  calc
    ‚Äñx ^ (i + 1) / (i + 1)‚Äñ = |x| ^ (i + 1) / (i + 1) := by
      have : (0 : ‚Ñù) ‚â§ i + 1 := le_of_lt (Nat.cast_add_one_pos i)
      rw [norm_eq_abs, abs_div, ‚Üê pow_abs, abs_of_nonneg this]
    _ ‚â§ |x| ^ (i + 1) / (0 + 1) := by
      gcongr
      exact i.cast_nonneg
    _ ‚â§ |x| ^ i := by
      simpa [pow_succ] using mul_le_of_le_one_right (pow_nonneg (abs_nonneg x) i) (le_of_lt h)

/-- Power series expansion of `log(1 + x) - log(1 - x)` for `|x| < 1`. -/
theorem hasSum_log_sub_log_of_abs_lt_one {x : ‚Ñù} (h : |x| < 1) :
    HasSum (fun k : ‚Ñï => (2 : ‚Ñù) * (1 / (2 * k + 1)) * x ^ (2 * k + 1))
      (log (1 + x) - log (1 - x)) := by
  set term := fun n : ‚Ñï => -1 * ((-x) ^ (n + 1) / ((n : ‚Ñù) + 1)) + x ^ (n + 1) / (n + 1)
  have h_term_eq_goal :
      term ‚àò (2 * ¬∑) = fun k : ‚Ñï => 2 * (1 / (2 * k + 1)) * x ^ (2 * k + 1) := by
    ext n
    dsimp only [term, (¬∑ ‚àò ¬∑)]
    rw [Odd.neg_pow (‚ü®n, rfl‚ü© : Odd (2 * n + 1)) x]
    push_cast
    ring_nf
  rw [‚Üê h_term_eq_goal, (mul_right_injective‚ÇÄ (two_ne_zero' ‚Ñï)).hasSum_iff]
  ¬∑ have h‚ÇÅ := (hasSum_pow_div_log_of_abs_lt_one (Eq.trans_lt (abs_neg x) h)).mul_left (-1)
    convert h‚ÇÅ.add (hasSum_pow_div_log_of_abs_lt_one h) using 1
    ring_nf
  ¬∑ intro m hm
    rw [range_two_mul, Set.mem_setOf_eq, ‚Üê Nat.even_add_one] at hm
    dsimp [term]
    rw [Even.neg_pow hm, neg_one_mul, neg_add_cancel]

/-- Expansion of `log (1 + a‚Åª¬π)` as a series in powers of `1 / (2 * a + 1)`. -/
theorem hasSum_log_one_add_inv {a : ‚Ñù} (h : 0 < a) :
    HasSum (fun k : ‚Ñï => (2 : ‚Ñù) * (1 / (2 * k + 1)) * (1 / (2 * a + 1)) ^ (2 * k + 1))
      (log (1 + a‚Åª¬π)) := by
  have h‚ÇÅ : |1 / (2 * a + 1)| < 1 := by
    rw [abs_of_pos, div_lt_one]
    ¬∑ linarith
    ¬∑ linarith
    ¬∑ exact div_pos one_pos (by linarith)
  convert hasSum_log_sub_log_of_abs_lt_one h‚ÇÅ using 1
  have h‚ÇÇ : (2 : ‚Ñù) * a + 1 ‚â† 0 := by linarith
  have h‚ÇÉ := h.ne'
  rw [‚Üê log_div]
  ¬∑ congr
    simp [field]
    ring
  ¬∑ field_simp
    positivity
  ¬∑ simp [field, h‚ÇÉ]

/-- Expansion of `log (1 + a)` as a series in powers of `a / (a + 2)`. -/
theorem hasSum_log_one_add {a : ‚Ñù} (h : 0 ‚â§ a) :
    HasSum (fun k : ‚Ñï => (2 : ‚Ñù) * (1 / (2 * k + 1)) * (a / (a + 2)) ^ (2 * k + 1))
      (log (1 + a)) := by
  obtain (rfl | ha0) := eq_or_ne a 0
  ¬∑ simp [hasSum_zero]
  ¬∑ convert hasSum_log_one_add_inv (inv_pos.mpr (lt_of_le_of_ne h ha0.symm)) using 4
    all_goals simp [field, add_comm]

lemma le_log_one_add_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) : 2 * x / (x + 2) ‚â§ log (1 + x) := by
  convert le_hasSum (hasSum_log_one_add hx) 0 (by intros; positivity) using 1
  simp [field]

lemma lt_log_one_add_of_pos {x : ‚Ñù} (hx : 0 < x) : 2 * x / (x + 2) < log (1 + x) := by
  convert lt_hasSum (hasSum_log_one_add hx.le) 0 (by intros; positivity)
    1 (by positivity) (by positivity) using 1
  simp [field]

end Real
