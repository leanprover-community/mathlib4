/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.Topology.Algebra.InfiniteSum.Field

/-!
# Summability of logarithms

We give conditions under which the logarithms of a summble sequence is summable. We also use this
to relate summability of `f` to multipliability of `1 + f`.

-/

variable {Œπ : Type*}

open Filter Topology NNReal SummationFilter

namespace Complex
variable {f : Œπ ‚Üí ‚ÑÇ} {a : ‚ÑÇ}

lemma hasProd_of_hasSum_log (hfn : ‚àÄ i, f i ‚â† 0) (hf : HasSum (fun i ‚Ü¶ log (f i)) a) :
    HasProd f (exp a) :=
  hf.cexp.congr (by simp [exp_log, hfn])

lemma multipliable_of_summable_log (hf : Summable fun i ‚Ü¶ log (f i)) :
    Multipliable f := by
  by_cases! hfn : ‚àÉ n, f n = 0
  ¬∑ exact multipliable_of_exists_eq_zero hfn
  ¬∑ exact ‚ü®_, hasProd_of_hasSum_log hfn hf.hasSum‚ü©

/-- The exponential of a convergent sum of complex logs is the corresponding infinite product. -/
lemma cexp_tsum_eq_tprod (hfn : ‚àÄ i, f i ‚â† 0) (hf : Summable fun i ‚Ü¶ log (f i)) :
    cexp (‚àë' i, log (f i)) = ‚àè' i, f i :=
  (hasProd_of_hasSum_log hfn hf.hasSum).tprod_eq.symm

lemma summable_log_one_add_of_summable {f : Œπ ‚Üí ‚ÑÇ} (hf : Summable f) :
    Summable (fun i ‚Ü¶ log (1 + f i)) := by
  apply (hf.norm.mul_left (3 / 2)).of_norm_bounded_eventually
  filter_upwards [hf.norm.tendsto_cofinite_zero.eventually_le_const one_half_pos] with i hi
    using norm_log_one_add_half_le_self hi

protected lemma multipliable_one_add_of_summable (hf : Summable f) :
    Multipliable (fun i ‚Ü¶ 1 + f i) :=
  multipliable_of_summable_log (summable_log_one_add_of_summable hf)

end Complex

namespace Real
variable {f : Œπ ‚Üí ‚Ñù} {a : ‚Ñù}

lemma hasProd_of_hasSum_log (hfn : ‚àÄ i, 0 < f i) (hf : HasSum (fun i ‚Ü¶ log (f i)) a) :
    HasProd f (rexp a) :=
  hf.rexp.congr (by simp [exp_log, hfn])

lemma multipliable_of_summable_log (hfn : ‚àÄ i, 0 < f i) (hf : Summable fun i ‚Ü¶ log (f i)) :
    Multipliable f :=
  ‚ü®_, hasProd_of_hasSum_log hfn hf.hasSum‚ü©

/-- Alternate version of `Real.multipliable_of_summable_log` assuming only that positivity holds
eventually. -/
lemma multipliable_of_summable_log' (hfn : ‚àÄ·∂† i in cofinite, 0 < f i)
    (hf : Summable fun i ‚Ü¶ log (f i)) : Multipliable f := by
  have : Summable fun i ‚Ü¶ log (if 0 < f i then f i else 1) := by
    apply hf.congr_cofinite
    filter_upwards [hfn] with i hi using by simp [hi]
  have : Multipliable fun i ‚Ü¶ if 0 < f i then f i else 1 := by
    refine multipliable_of_summable_log (fun i ‚Ü¶ ?_) this
    split_ifs with h <;> simp [h]
  refine this.congr_cofinite‚ÇÄ (fun i ‚Ü¶ ?_) ?_
  ¬∑ split_ifs with h <;> simp [h, ne_of_gt]
  ¬∑ filter_upwards [hfn] with i hi using by simp [hi]

/-- The exponential of a convergent sum of real logs is the corresponding infinite product. -/
lemma rexp_tsum_eq_tprod (hfn : ‚àÄ i, 0 < f i) (hf : Summable fun i ‚Ü¶ log (f i)) :
    rexp (‚àë' i, log (f i)) = ‚àè' i, f i :=
  (hasProd_of_hasSum_log hfn hf.hasSum).tprod_eq.symm

open Complex in
lemma summable_log_one_add_of_summable (hf : Summable f) :
    Summable (fun i ‚Ü¶ log (1 + f i)) := by
  rw [‚Üê summable_ofReal]
  apply (Complex.summable_log_one_add_of_summable (summable_ofReal.mpr hf)).congr_cofinite
  filter_upwards [hf.tendsto_cofinite_zero.eventually_const_le neg_one_lt_zero] with i hi
  rw [ofReal_log, ofReal_add, ofReal_one]
  linarith

protected lemma multipliable_one_add_of_summable (hf : Summable f) :
    Multipliable (fun i ‚Ü¶ 1 + f i) := by
  refine multipliable_of_summable_log' ?_ (summable_log_one_add_of_summable hf)
  filter_upwards [hf.tendsto_cofinite_zero.eventually_const_lt neg_one_lt_zero] with i hi
  linarith

end Real

section NormedRing

lemma Multipliable.eventually_bounded_finset_prod {v : Œπ ‚Üí ‚Ñù} (hv : Multipliable v) :
    ‚àÉ r‚ÇÅ > 0, ‚àÉ s‚ÇÅ, ‚àÄ t, s‚ÇÅ ‚äÜ t ‚Üí ‚àè i ‚àà t, v i ‚â§ r‚ÇÅ := by
  obtain ‚ü®r‚ÇÅ, hr‚ÇÅ‚ü© := exists_gt (max 0 <| ‚àè' i, v i)
  rw [max_lt_iff] at hr‚ÇÅ
  have := hv.hasProd.eventually_le_const hr‚ÇÅ.2
  rw [unconditional, eventually_atTop] at this
  exact ‚ü®r‚ÇÅ, hr‚ÇÅ.1, this‚ü©

variable {R : Type*} [NormedCommRing R] [NormOneClass R] {f : Œπ ‚Üí R}

lemma multipliable_norm_one_add_of_summable_norm (hf : Summable fun i ‚Ü¶ ‚Äñf i‚Äñ) :
    Multipliable fun i ‚Ü¶ ‚Äñ1 + f i‚Äñ := by
  conv => enter [1, i]; rw [‚Üê sub_add_cancel ‚Äñ1 + f i‚Äñ 1, add_comm]
  refine Real.multipliable_one_add_of_summable <| hf.of_norm_bounded (fun i ‚Ü¶ ?_)
  simpa using abs_norm_sub_norm_le (1 + f i) 1

lemma Finset.norm_prod_one_add_sub_one_le (t : Finset Œπ) (f : Œπ ‚Üí R) :
    ‚Äñ‚àè i ‚àà t, (1 + f i) - 1‚Äñ ‚â§ Real.exp (‚àë i ‚àà t, ‚Äñf i‚Äñ) - 1 := by
  classical
  induction t using Finset.induction_on with
  | empty => simp
  | insert x t hx IH =>
    rw [Finset.prod_insert hx, Finset.sum_insert hx, Real.exp_add,
      show (1 + f x) * ‚àè i ‚àà t, (1 + f i) - 1 =
        (‚àè i ‚àà t, (1 + f i) - 1) + f x * ‚àè x ‚àà t, (1 + f x) by ring]
    refine (norm_add_le_of_le IH (norm_mul_le _ _)).trans ?_
    generalize h : Real.exp (‚àë i ‚àà t, ‚Äñf i‚Äñ) = A at ‚ä¢ IH
    rw [sub_add_eq_add_sub, sub_le_sub_iff_right]
    transitivity A + ‚Äñf x‚Äñ * A
    ¬∑ grw [norm_le_norm_sub_add (‚àè x ‚àà t, (1 + f x)) 1, IH, norm_one, sub_add_cancel]
    rw [‚Üê one_add_mul, add_comm]
    exact mul_le_mul_of_nonneg_right (Real.add_one_le_exp _) (h ‚ñ∏ Real.exp_nonneg _)

lemma prod_vanishing_of_summable_norm (hf : Summable fun i ‚Ü¶ ‚Äñf i‚Äñ) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ s‚ÇÇ, ‚àÄ t, Disjoint t s‚ÇÇ ‚Üí ‚Äñ‚àè i ‚àà t, (1 + f i) - 1‚Äñ < Œµ := by
  suffices ‚àÉ s, ‚àÄ t, Disjoint t s ‚Üí Real.exp (‚àë i ‚àà t, ‚Äñf i‚Äñ) - 1 < Œµ from
    this.imp fun s hs t ht ‚Ü¶ (t.norm_prod_one_add_sub_one_le _).trans_lt (hs t ht)
  suffices {x | Real.exp x - 1 < Œµ} ‚àà ùìù 0 from hf.vanishing this
  let f (x) := Real.exp x - 1
  have : Set.Iio Œµ ‚àà nhds (f 0) := by simpa [f] using Iio_mem_nhds hŒµ
  exact ContinuousAt.preimage_mem_nhds (by fun_prop) this

open Finset in
/-- In a complete normed ring, `‚àè' i, (1 + f i)` is convergent if the sum of real numbers
`‚àë' i, ‚Äñf i‚Äñ` is convergent. -/
lemma multipliable_one_add_of_summable [CompleteSpace R]
    (hf : Summable fun i ‚Ü¶ ‚Äñf i‚Äñ) : Multipliable fun i ‚Ü¶ (1 + f i) := by
  classical
  refine CompleteSpace.complete <| Metric.cauchy_iff.mpr ‚ü®by infer_instance, fun Œµ hŒµ ‚Ü¶ ?_‚ü©
  obtain ‚ü®r‚ÇÅ, hr‚ÇÅ, s‚ÇÅ, hs‚ÇÅ‚ü© :=
    (multipliable_norm_one_add_of_summable_norm hf).eventually_bounded_finset_prod
  obtain ‚ü®s‚ÇÇ, hs‚ÇÇ‚ü© := prod_vanishing_of_summable_norm hf (show 0 < Œµ / (2 * r‚ÇÅ) by positivity)
  simp only [unconditional, Filter.mem_map, mem_atTop_sets, ge_iff_le, le_eq_subset,
    Set.mem_preimage]
  let s := s‚ÇÅ ‚à™ s‚ÇÇ
  -- The idea here is that if `s` is a large enough finset, then the product over `s` is bounded
  -- by some `r`, and the product over finsets disjoint from `s` is within `Œµ / (2 * r)` of 1.
  -- From this it follows that the products over any two finsets containing `s` are within `Œµ` of
  -- each other.
  -- Here `s‚ÇÅ ‚äÜ s` guarantees that the product over `s` is bounded, and `s‚ÇÇ ‚äÜ s` guarantees that
  -- the product over terms not in `s` is small.
  refine ‚ü®Metric.ball (‚àè i ‚àà s, (1 + f i)) (Œµ / 2), ‚ü®s, fun b hb ‚Ü¶ ?_‚ü©, ?_‚ü©
  ¬∑ rw [‚Üê union_sdiff_of_subset hb, prod_union sdiff_disjoint.symm,
      Metric.mem_ball, dist_eq_norm_sub, ‚Üê mul_sub_one,
      show Œµ / 2 = r‚ÇÅ * (Œµ / (2 * r‚ÇÅ)) by field_simp]
    apply (norm_mul_le _ _).trans_lt
    refine lt_of_le_of_lt (b := r‚ÇÅ * ‚Äñ‚àè x ‚àà b \ s, (1 + f x) - 1‚Äñ) ?_ ?_
    ¬∑ refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
      exact (Finset.norm_prod_le _ _).trans (hs‚ÇÅ _ subset_union_left)
    ¬∑ refine mul_lt_mul_of_pos_left (hs‚ÇÇ _ ?_) hr‚ÇÅ
      simp [s, sdiff_union_distrib, disjoint_iff_inter_eq_empty]
  ¬∑ intro x hx y hy
    exact (dist_triangle_right _ _ (‚àè i ‚àà s, (1 + f i))).trans_lt (add_halves Œµ ‚ñ∏ add_lt_add hx hy)

lemma Summable.summable_log_norm_one_add (hu : Summable fun n ‚Ü¶ ‚Äñf n‚Äñ) :
    Summable fun i ‚Ü¶ Real.log ‚Äñ1 + f i‚Äñ := by
  suffices Summable (‚Äñ1 + f ¬∑‚Äñ - 1) from
    (Real.summable_log_one_add_of_summable this).congr (by simp)
  refine .of_norm (hu.of_nonneg_of_le (fun i ‚Ü¶ by positivity) fun i ‚Ü¶ ?_)
  simp only [Real.norm_eq_abs, abs_le]
  constructor
  ¬∑ simpa using norm_add_le (1 + f i) (-f i)
  ¬∑ simpa [add_comm] using norm_add_le (f i) 1

lemma tprod_one_add_ne_zero_of_summable [CompleteSpace R] [NormMulClass R]
    (hf : ‚àÄ i, 1 + f i ‚â† 0)
    (hu : Summable (‚Äñf ¬∑‚Äñ)) : ‚àè' i : Œπ, (1 + f i) ‚â† 0 := by
  rw [‚Üê norm_ne_zero_iff, Multipliable.norm_tprod]
  ¬∑ rw [‚Üê Real.rexp_tsum_eq_tprod (fun i ‚Ü¶ norm_pos_iff.mpr <| hf i) hu.summable_log_norm_one_add]
    apply Real.exp_ne_zero
  ¬∑ exact multipliable_one_add_of_summable hu

end NormedRing
