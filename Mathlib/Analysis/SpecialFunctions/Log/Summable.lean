/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/

import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds

/-!
# Summability of logarithms

We give conditions under which the logarithms of a summble sequence is summable. We also use this
to relate summability of `f` to multipliability of `1 + f`.

-/

variable {Î¹ : Type*}

open Filter Topology NNReal

namespace Complex
variable {f : Î¹ â†’ â„‚} {a : â„‚}

lemma hasProd_of_hasSum_log (hfn : âˆ€ i, f i â‰  0) (hf : HasSum (fun i â†¦ log (f i)) a) :
    HasProd f (exp a) :=
  hf.cexp.congr (by simp [exp_log, hfn])

lemma multipliable_of_summable_log (hf : Summable fun i â†¦ log (f i)) :
    Multipliable f := by
  by_cases hfn : âˆƒ n, f n = 0
  Â· exact multipliable_of_exists_eq_zero hfn
  Â· exact âŸ¨_, hasProd_of_hasSum_log (not_exists.mp hfn) hf.hasSumâŸ©

/-- The exponential of a convergent sum of complex logs is the corresponding infinite product. -/
lemma cexp_tsum_eq_tprod (hfn : âˆ€ i, f i â‰  0) (hf : Summable fun i â†¦ log (f i)) :
    cexp (âˆ‘' i, log (f i)) = âˆ' i, f i :=
  (hasProd_of_hasSum_log hfn hf.hasSum).tprod_eq.symm

lemma summable_log_one_add_of_summable {f : Î¹ â†’ â„‚} (hf : Summable f) :
    Summable (fun i â†¦ log (1 + f i)) := by
  apply (hf.norm.mul_left _).of_norm_bounded_eventually
  filter_upwards [hf.norm.tendsto_cofinite_zero.eventually_le_const one_half_pos] with i hi
    using norm_log_one_add_half_le_self hi

lemma tprod_one_add_ne_zero_of_summable {Î± : Type*} (x : Î±) {f : Î¹ â†’ Î± â†’ â„‚}
    (hf : âˆ€ i x, 1 + f i x â‰  0) (hu : âˆ€ x : Î±, Summable fun n => f n x) :
    (âˆ' i : Î¹, (1 + f i) x) â‰  0 := by
  simp only [Pi.add_apply, Pi.one_apply, ne_eq]
  rw [â† Complex.cexp_tsum_eq_tprod (f := fun n => 1 + f n x) (fun n => hf n x)]
  Â· simp only [exp_ne_zero, not_false_eq_true]
  Â· exact Complex.summable_log_one_add_of_summable (hu x)

protected lemma multipliable_one_add_of_summable (hf : Summable f) :
    Multipliable (fun i â†¦ 1 + f i) :=
  multipliable_of_summable_log (summable_log_one_add_of_summable hf)

end Complex

namespace Real
variable {f : Î¹ â†’ â„} {a : â„}

lemma hasProd_of_hasSum_log (hfn : âˆ€ i, 0 < f i) (hf : HasSum (fun i â†¦ log (f i)) a) :
    HasProd f (rexp a) :=
  hf.rexp.congr (by simp [exp_log, hfn])

lemma multipliable_of_summable_log (hfn : âˆ€ i, 0 < f i) (hf : Summable fun i â†¦ log (f i)) :
    Multipliable f :=
  âŸ¨_, hasProd_of_hasSum_log hfn hf.hasSumâŸ©

/-- Alternate version of `Real.multipliable_of_summable_log` assuming only that positivity holds
eventually. -/
lemma multipliable_of_summable_log' (hfn : âˆ€á¶  i in cofinite, 0 < f i)
    (hf : Summable fun i â†¦ log (f i)) : Multipliable f := by
  have : Summable fun i â†¦ log (if 0 < f i then f i else 1) := by
    apply hf.congr_cofinite
    filter_upwards [hfn] with i hi using by simp [hi]
  have : Multipliable fun i â†¦ if 0 < f i then f i else 1 := by
    refine multipliable_of_summable_log (fun i â†¦ ?_) this
    split_ifs with h <;> simp [h]
  refine this.congr_cofiniteâ‚€ (fun i â†¦ ?_) ?_
  Â· split_ifs with h <;> simp [h, ne_of_gt]
  Â· filter_upwards [hfn] with i hi using by simp [hi]

/-- The exponential of a convergent sum of real logs is the corresponding infinite product. -/
lemma rexp_tsum_eq_tprod (hfn : âˆ€ i, 0 < f i) (hf : Summable fun i â†¦ log (f i)) :
    rexp (âˆ‘' i, log (f i)) = âˆ' i, f i :=
  (hasProd_of_hasSum_log hfn hf.hasSum).tprod_eq.symm

open Complex in
lemma summable_log_one_add_of_summable (hf : Summable f) :
    Summable (fun i â†¦ log (1 + f i)) := by
  rw [â† summable_ofReal]
  apply (Complex.summable_log_one_add_of_summable (summable_ofReal.mpr hf)).congr_cofinite
  filter_upwards [hf.tendsto_cofinite_zero.eventually_const_le neg_one_lt_zero] with i hi
  rw [ofReal_log, ofReal_add, ofReal_one]
  linarith

protected lemma multipliable_one_add_of_summable (hf : Summable f) :
    Multipliable (fun i â†¦ 1 + f i) := by
  refine multipliable_of_summable_log' ?_ (summable_log_one_add_of_summable hf)
  filter_upwards [hf.tendsto_cofinite_zero.eventually_const_lt neg_one_lt_zero] with i hi
  linarith

end Real

section NormedRing

lemma Multipliable.eventually_bounded_finset_prod {v : Î¹ â†’ â„} (hv : Multipliable v) :
    âˆƒ râ‚ > 0, âˆƒ sâ‚, âˆ€ t, sâ‚ âŠ† t â†’ âˆ i âˆˆ t, v i â‰¤ râ‚ := by
  obtain âŸ¨râ‚, hrâ‚âŸ© := exists_gt (max 0 <| âˆ' i, v i)
  rw [max_lt_iff] at hrâ‚
  have := hv.hasProd.eventually_le_const hrâ‚.2
  rw [eventually_atTop] at this
  exact âŸ¨râ‚, hrâ‚.1, thisâŸ©

variable {R : Type*} [NormedCommRing R] [NormOneClass R] {f : Î¹ â†’ R}

lemma multipliable_norm_one_add_of_summable_norm (hf : Summable fun i â†¦ â€–f iâ€–) :
    Multipliable fun i â†¦ â€–1 + f iâ€– := by
  conv => enter [1, i]; rw [â† sub_add_cancel â€–1 + f iâ€– 1, add_comm]
  refine Real.multipliable_one_add_of_summable <| hf.of_norm_bounded (fun i â†¦ ?_)
  simpa using abs_norm_sub_norm_le (1 + f i) 1

lemma Finset.norm_prod_one_add_sub_one_le (t : Finset Î¹) (f : Î¹ â†’ R) :
    â€–âˆ i âˆˆ t, (1 + f i) - 1â€– â‰¤ Real.exp (âˆ‘ i âˆˆ t, â€–f iâ€–) - 1 := by
  classical
  induction t using Finset.induction_on with
  | empty => simp
  | insert x t hx IH =>
    rw [Finset.prod_insert hx, Finset.sum_insert hx, Real.exp_add,
      show (1 + f x) * âˆ i âˆˆ t, (1 + f i) - 1 =
        (âˆ i âˆˆ t, (1 + f i) - 1) + f x * âˆ x âˆˆ t, (1 + f x) by ring]
    refine (norm_add_le_of_le IH (norm_mul_le _ _)).trans ?_
    generalize h : Real.exp (âˆ‘ i âˆˆ t, â€–f iâ€–) = A at âŠ¢ IH
    rw [sub_add_eq_add_sub, sub_le_sub_iff_right]
    transitivity A + â€–f xâ€– * A
    Â· gcongr
      rw [â† sub_add_cancel (âˆ x âˆˆ t, (1 + f x)) 1]
      refine (norm_add_le _ _).trans <| (add_le_add_right IH _).trans ?_
      rw [norm_one, sub_add_cancel]
    rw [â† one_add_mul, add_comm]
    exact mul_le_mul_of_nonneg_right (Real.add_one_le_exp _) (h â–¸ Real.exp_nonneg _)

lemma prod_vanishing_of_summable_norm (hf : Summable fun i â†¦ â€–f iâ€–) {Îµ : â„} (hÎµ : 0 < Îµ) :
    âˆƒ sâ‚‚, âˆ€ t, Disjoint t sâ‚‚ â†’ â€–âˆ i âˆˆ t, (1 + f i) - 1â€– < Îµ := by
  suffices âˆƒ s, âˆ€ t, Disjoint t s â†’ Real.exp (âˆ‘ i âˆˆ t, â€–f iâ€–) - 1 < Îµ from
    this.imp fun s hs t ht â†¦ (t.norm_prod_one_add_sub_one_le _).trans_lt (hs t ht)
  suffices {x | Real.exp x - 1 < Îµ} âˆˆ ğ“ 0 from hf.vanishing this
  let f (x) := Real.exp x - 1
  have : Set.Iio Îµ âˆˆ nhds (f 0) := by simpa [f] using Iio_mem_nhds hÎµ
  exact ContinuousAt.preimage_mem_nhds (by fun_prop) this

open Finset in
/-- In a complete normed ring, `âˆ' i, (1 + f i)` is convergent if the sum of real numbers
`âˆ‘' i, â€–f iâ€–` is convergent. -/
lemma multipliable_one_add_of_summable [CompleteSpace R]
    (hf : Summable fun i â†¦ â€–f iâ€–) : Multipliable fun i â†¦ (1 + f i) := by
  classical
  refine CompleteSpace.complete <| Metric.cauchy_iff.mpr âŸ¨by infer_instance, fun Îµ hÎµ â†¦ ?_âŸ©
  obtain âŸ¨râ‚, hrâ‚, sâ‚, hsâ‚âŸ© :=
    (multipliable_norm_one_add_of_summable_norm hf).eventually_bounded_finset_prod
  obtain âŸ¨sâ‚‚, hsâ‚‚âŸ© := prod_vanishing_of_summable_norm hf (show 0 < Îµ / (2 * râ‚) by positivity)
  simp only [Filter.mem_map, mem_atTop_sets, ge_iff_le, le_eq_subset, Set.mem_preimage]
  let s := sâ‚ âˆª sâ‚‚
  -- The idea here is that if `s` is a large enough finset, then the product over `s` is bounded
  -- by some `r`, and the product over finsets disjoint from `s` is within `Îµ / (2 * r)` of 1.
  -- From this it follows that the products over any two finsets containing `s` are within `Îµ` of
  -- each other.
  -- Here `sâ‚ âŠ† s` guarantees that the product over `s` is bounded, and `sâ‚‚ âŠ† s` guarantees that
  -- the product over terms not in `s` is small.
  refine âŸ¨Metric.ball (âˆ i âˆˆ s, (1 + f i)) (Îµ / 2), âŸ¨s, fun b hb â†¦ ?_âŸ©, ?_âŸ©
  Â· rw [â† union_sdiff_of_subset hb, prod_union sdiff_disjoint.symm,
      Metric.mem_ball, dist_eq_norm_sub, â† mul_sub_one,
      show Îµ / 2 = râ‚ * (Îµ / (2 * râ‚)) by field_simp [hrâ‚]; ring]
    apply (norm_mul_le _ _).trans_lt
    refine lt_of_le_of_lt (b := râ‚ * â€–âˆ x âˆˆ b \ s, (1 + f x) - 1â€–) ?_ ?_
    Â· refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
      exact (Finset.norm_prod_le _ _).trans (hsâ‚ _ subset_union_left)
    Â· refine mul_lt_mul_of_pos_left (hsâ‚‚ _ ?_) hrâ‚
      simp [s, sdiff_union_distrib, disjoint_iff_inter_eq_empty]
  Â· intro x hx y hy
    exact (dist_triangle_right _ _ (âˆ i âˆˆ s, (1 + f i))).trans_lt (add_halves Îµ â–¸ add_lt_add hx hy)

end NormedRing
