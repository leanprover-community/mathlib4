/-
Copyright (c) 2021 Bolton Bailey. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bolton Bailey
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Real

#align_import analysis.special_functions.log.monotone from "leanprover-community/mathlib"@"0b9eaaa7686280fad8cce467f5c3c57ee6ce77f8"

/-!
# Logarithm Tonality

In this file we describe the tonality of the logarithm function when multiplied by functions of the
form `x ^ a`.

## Tags

logarithm, tonality
-/


open Set Filter Function

open Topology

noncomputable section

namespace Real

variable {x y : ‚Ñù}

theorem log_mul_self_monotoneOn : MonotoneOn (fun x : ‚Ñù => log x * x) { x | 1 ‚â§ x } := by
  -- TODO: can be strengthened to exp (-1) ‚â§ x
  simp only [MonotoneOn, mem_setOf_eq]
  -- ‚ä¢ ‚àÄ ‚¶Éa : ‚Ñù‚¶Ñ, 1 ‚â§ a ‚Üí ‚àÄ ‚¶Éb : ‚Ñù‚¶Ñ, 1 ‚â§ b ‚Üí a ‚â§ b ‚Üí log a * a ‚â§ log b * b
  intro x hex y hey hxy
  -- ‚ä¢ log x * x ‚â§ log y * y
  have x_pos : 0 < x := lt_of_lt_of_le zero_lt_one hex
  -- ‚ä¢ log x * x ‚â§ log y * y
  have y_pos : 0 < y := lt_of_lt_of_le zero_lt_one hey
  -- ‚ä¢ log x * x ‚â§ log y * y
  refine' mul_le_mul ((log_le_log x_pos y_pos).mpr hxy) hxy (le_of_lt x_pos) _
  -- ‚ä¢ 0 ‚â§ log y
  rwa [le_log_iff_exp_le y_pos, Real.exp_zero]
  -- üéâ no goals
#align real.log_mul_self_monotone_on Real.log_mul_self_monotoneOn

theorem log_div_self_antitoneOn : AntitoneOn (fun x : ‚Ñù => log x / x) { x | exp 1 ‚â§ x } := by
  simp only [AntitoneOn, mem_setOf_eq]
  -- ‚ä¢ ‚àÄ ‚¶Éa : ‚Ñù‚¶Ñ, exp 1 ‚â§ a ‚Üí ‚àÄ ‚¶Éb : ‚Ñù‚¶Ñ, exp 1 ‚â§ b ‚Üí a ‚â§ b ‚Üí log b / b ‚â§ log a / a
  intro x hex y hey hxy
  -- ‚ä¢ log y / y ‚â§ log x / x
  have x_pos : 0 < x := (exp_pos 1).trans_le hex
  -- ‚ä¢ log y / y ‚â§ log x / x
  have y_pos : 0 < y := (exp_pos 1).trans_le hey
  -- ‚ä¢ log y / y ‚â§ log x / x
  have hlogx : 1 ‚â§ log x := by rwa [le_log_iff_exp_le x_pos]
  -- ‚ä¢ log y / y ‚â§ log x / x
  have hyx : 0 ‚â§ y / x - 1 := by rwa [le_sub_iff_add_le, le_div_iff x_pos, zero_add, one_mul]
  -- ‚ä¢ log y / y ‚â§ log x / x
  rw [div_le_iff y_pos, ‚Üê sub_le_sub_iff_right (log x)]
  -- ‚ä¢ log y - log x ‚â§ log x / x * y - log x
  calc
    log y - log x = log (y / x) := by rw [log_div y_pos.ne' x_pos.ne']
    _ ‚â§ y / x - 1 := (log_le_sub_one_of_pos (div_pos y_pos x_pos))
    _ ‚â§ log x * (y / x - 1) := (le_mul_of_one_le_left hyx hlogx)
    _ = log x / x * y - log x := by ring
#align real.log_div_self_antitone_on Real.log_div_self_antitoneOn

theorem log_div_self_rpow_antitoneOn {a : ‚Ñù} (ha : 0 < a) :
    AntitoneOn (fun x : ‚Ñù => log x / x ^ a) { x | exp (1 / a) ‚â§ x } := by
  simp only [AntitoneOn, mem_setOf_eq]
  -- ‚ä¢ ‚àÄ ‚¶Éa_1 : ‚Ñù‚¶Ñ, exp (1 / a) ‚â§ a_1 ‚Üí ‚àÄ ‚¶Éb : ‚Ñù‚¶Ñ, exp (1 / a) ‚â§ b ‚Üí a_1 ‚â§ b ‚Üí log  ‚Ä¶
  intro x hex y _ hxy
  -- ‚ä¢ log y / y ^ a ‚â§ log x / x ^ a
  have x_pos : 0 < x := lt_of_lt_of_le (exp_pos (1 / a)) hex
  -- ‚ä¢ log y / y ^ a ‚â§ log x / x ^ a
  have y_pos : 0 < y := by linarith
  -- ‚ä¢ log y / y ^ a ‚â§ log x / x ^ a
  have x_nonneg : 0 ‚â§ x := le_trans (le_of_lt (exp_pos (1 / a))) hex
  -- ‚ä¢ log y / y ^ a ‚â§ log x / x ^ a
  have y_nonneg : 0 ‚â§ y := by linarith
  -- ‚ä¢ log y / y ^ a ‚â§ log x / x ^ a
  nth_rw 1 [‚Üê rpow_one y]
  -- ‚ä¢ log (y ^ 1) / y ^ a ‚â§ log x / x ^ a
  nth_rw 1 [‚Üê rpow_one x]
  -- ‚ä¢ log (y ^ 1) / y ^ a ‚â§ log (x ^ 1) / x ^ a
  rw [‚Üê div_self (ne_of_lt ha).symm, div_eq_mul_one_div a a, rpow_mul y_nonneg, rpow_mul x_nonneg,
    log_rpow (rpow_pos_of_pos y_pos a), log_rpow (rpow_pos_of_pos x_pos a), mul_div_assoc,
    mul_div_assoc, mul_le_mul_left (one_div_pos.mpr ha)]
  ¬∑ refine' log_div_self_antitoneOn _ _ _
    ¬∑ simp only [Set.mem_setOf_eq]
      -- ‚ä¢ exp 1 ‚â§ x ^ a
      convert rpow_le_rpow _ hex (le_of_lt ha) using 1
      -- ‚ä¢ exp 1 = exp (1 / a) ^ a
      rw [‚Üê exp_mul]
      -- ‚ä¢ exp 1 = exp (1 / a * a)
      simp only [Real.exp_eq_exp]
      -- ‚ä¢ 1 = 1 / a * a
      field_simp [(ne_of_lt ha).symm]
      -- ‚ä¢ 0 ‚â§ exp (1 / a)
      exact le_of_lt (exp_pos (1 / a))
      -- üéâ no goals
    ¬∑ simp only [Set.mem_setOf_eq]
      -- ‚ä¢ exp 1 ‚â§ y ^ a
      convert rpow_le_rpow _ (_root_.trans hex hxy) (le_of_lt ha) using 1
      -- ‚ä¢ exp 1 = exp (1 / a) ^ a
      rw [‚Üê exp_mul]
      -- ‚ä¢ exp 1 = exp (1 / a * a)
      simp only [Real.exp_eq_exp]
      -- ‚ä¢ 1 = 1 / a * a
      field_simp [(ne_of_lt ha).symm]
      -- ‚ä¢ 0 ‚â§ exp (1 / a)
      exact le_of_lt (exp_pos (1 / a))
      -- üéâ no goals
    exact rpow_le_rpow x_nonneg hxy (le_of_lt ha)
    -- üéâ no goals
#align real.log_div_self_rpow_antitone_on Real.log_div_self_rpow_antitoneOn

theorem log_div_sqrt_antitoneOn : AntitoneOn (fun x : ‚Ñù => log x / sqrt x) { x | exp 2 ‚â§ x } := by
  simp_rw [sqrt_eq_rpow]
  -- ‚ä¢ AntitoneOn (fun x => log x / x ^ (1 / 2)) {x | exp 2 ‚â§ x}
  convert @log_div_self_rpow_antitoneOn (1 / 2) (by norm_num)
  -- ‚ä¢ 2 = 1 / (1 / 2)
  norm_num
  -- üéâ no goals
#align real.log_div_sqrt_antitone_on Real.log_div_sqrt_antitoneOn

end Real
