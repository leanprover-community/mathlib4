/-
Copyright (c) 2023 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Analysis.SpecialFunctions.Pow.Asymptotics
import Mathlib.Analysis.Convex.Deriv

/-!
# The function `x ‚Ü¶ - x * log x`

The purpose of this file is to record basic analytic properties of the function `x ‚Ü¶ - x * log x`,
which is notably used in the theory of Shannon entropy.

## Main definitions

* `negMulLog`: the function `x ‚Ü¶ - x * log x` from `‚Ñù` to `‚Ñù`.

-/

open scoped Topology

namespace Real

lemma continuous_mul_log : Continuous fun x ‚Ü¶ x * log x := by
  rw [continuous_iff_continuousAt]
  intro x
  obtain hx | rfl := ne_or_eq x 0
  ¬∑ exact (continuous_id'.continuousAt).mul (continuousAt_log hx)
  rw [ContinuousAt, zero_mul]
  simp_rw [mul_comm _ (log _)]
  nth_rewrite 1 [‚Üê nhdsWithin_univ]
  have : (Set.univ : Set ‚Ñù) = Set.Iio 0 ‚à™ Set.Ioi 0 ‚à™ {0} := by ext; simp [em]
  rw [this, nhdsWithin_union, nhdsWithin_union]
  simp only [nhdsWithin_singleton, sup_le_iff, Filter.nonpos_iff, Filter.tendsto_sup]
  refine ‚ü®‚ü®tendsto_log_mul_self_nhds_zero_left, ?_‚ü©, ?_‚ü©
  ¬∑ simpa only [rpow_one] using tendsto_log_mul_rpow_nhds_zero zero_lt_one
  ¬∑ convert tendsto_pure_nhds (fun x ‚Ü¶ log x * x) 0
    simp only [log_zero, mul_zero]

lemma differentiableOn_mul_log : DifferentiableOn ‚Ñù (fun x ‚Ü¶ x * log x) {0}·∂ú :=
  differentiable_id'.differentiableOn.mul differentiableOn_log

lemma deriv_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : deriv (fun x ‚Ü¶ x * log x) x = log x + 1 := by
  rw [deriv_mul differentiableAt_id' (differentiableAt_log hx)]
  simp only [deriv_id'', one_mul, deriv_log', ne_eq, add_right_inj]
  exact mul_inv_cancel hx

lemma hasDerivAt_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : HasDerivAt (fun x ‚Ü¶ x * log x) (log x + 1) x := by
  rw [‚Üê deriv_mul_log hx, hasDerivAt_deriv_iff]
  refine DifferentiableOn.differentiableAt differentiableOn_mul_log ?_
  simp [hx]

open Filter in
lemma tendsto_deriv_mul_log_nhdsWithin_zero :
    Tendsto (deriv (fun x ‚Ü¶ x * log x)) (ùìù[>] 0) atBot := by
  have : (deriv (fun x ‚Ü¶ x * log x)) =·∂†[ùìù[>] 0] (fun x ‚Ü¶ log x + 1) := by
    apply eventuallyEq_nhdsWithin_of_eqOn
    intro x hx
    rw [deriv_mul_log]
    simp only [Set.mem_Ioi, ne_eq]
    exact ne_of_gt hx
  simp only [tendsto_congr' this, tendsto_atBot_add_const_right, tendsto_log_nhdsWithin_zero_right]

-- lemma isLittleO_nhds_zero_id_deriv_mul_log : id =o[ùìù[>] 0] (deriv (fun x ‚Ü¶ x * log x)) := by
--   sorry

/- helper lemma for `not_eventually_bounded_zero_mul_log` -/
private lemma NegMulLog.one_lt_log_sub_const_of_lt_exp_sub
    (x D : ‚Ñù) (hx : 0 < x) (h : x < rexp (D - 2)) :
    1 < |x.log - D| := by
  by_cases abs_eq : x.log - D < 0
  ¬∑ rw [abs_of_neg]
    have : x.log - D < -2 := sub_left_lt_of_lt_add ((log_lt_iff_lt_exp hx).mpr h)
    linarith
    exact abs_eq
  ¬∑ have h := log_lt_log hx h
    simp only [log_exp] at h
    rw [abs_of_pos (by linarith)]
    linarith

lemma not_eventually_bounded_derivative_zero_mul_log (D : ‚Ñù) :
    ¬¨ ‚àÄ·∂† (x : ‚Ñù) in ùìù 0, |x * x.log - x * D| ‚â§ |x| := by
  simp [eventually_nhdsWithin_iff, Metric.eventually_nhds_iff]
  intro x hx
  exists min (2‚Åª¬π * x) (exp (D - 2))
  have xhalf_gt0 : 0 < 2‚Åª¬π * x := by norm_num [hx]
  constructor
  ¬∑ rw [abs_of_nonneg (le_min (le_of_lt xhalf_gt0) (exp_nonneg (D - 2)))]
    apply min_lt_of_left_lt
    norm_num
    linarith
  ¬∑ have exists_pos : 0 < |min (2‚Åª¬π * x) (rexp (D - 2))| := by
      simp only [one_div, abs_pos]
      exact ne_of_gt (lt_min xhalf_gt0 (exp_pos (D - 2)))
    simp only [‚Üê mul_sub, one_div, abs_mul]
    apply (lt_mul_iff_one_lt_right exists_pos).mpr
    by_cases min_l : (2‚Åª¬π * x) < (rexp (D - 2))
    ¬∑ rw [min_eq_left_of_lt min_l]
      exact NegMulLog.one_lt_log_sub_const_of_lt_exp_sub (2‚Åª¬π * x) D xhalf_gt0 min_l
    ¬∑ simp only [min_eq_right (le_of_not_lt min_l), log_exp, sub_sub_cancel_left, abs_neg,
        Nat.abs_ofNat, Nat.one_lt_ofNat]

-- TODO inline or refactor or find home
/- In the hypothesis, D stands for a hypothetical derivative `deriv f x0`. -/
lemma not_DifferentiableAt_of_not_eventuallly_bounded_derivative (f : ‚Ñù ‚Üí ‚Ñù) (x0 : ‚Ñù)
    (hf : ‚àÄ D, ¬¨ ‚àÄ·∂† (x : ‚Ñù) in ùìù 0, |f (x0 + x) - f x0 - x * D| ‚â§ |x|) :
    ¬¨ DifferentiableAt ‚Ñù f x0 := by
  intro h
  have := hasDerivAt_iff_isLittleO_nhds_zero.mp (DifferentiableAt.hasDerivAt h)
  simp only [zero_add, log_zero, mul_zero, sub_zero, smul_eq_mul] at this
  have := Asymptotics.IsLittleO.bound this
  simp only [norm_mul, norm_eq_abs] at this
  have := @this 1 (by norm_num)
  simp only [one_mul] at this
  have := hf (deriv f x0)
  contradiction

lemma not_DifferentiableAt_log_mul_zero :
    ¬¨ DifferentiableAt ‚Ñù (fun x ‚Ü¶ x * log x) 0 := by
  apply not_DifferentiableAt_of_not_eventuallly_bounded_derivative (fun x ‚Ü¶ x * log x) 0
  simp only [zero_add, log_zero, mul_zero, sub_zero]
  exact not_eventually_bounded_derivative_zero_mul_log

/-- Not differentiable, hence `deriv` has junk value zero. -/
lemma deriv_mul_log_zero : deriv (fun x ‚Ü¶ x * log x) 0 = 0 :=
  deriv_zero_of_not_differentiableAt not_DifferentiableAt_log_mul_zero

section NotContinuousAtOfTendstoNhdsWithin
-- TODO put elsewhere, generalize, maybe add other nhdsWithin variants?

open Filter

lemma not_continuousAt_of_tendsto_nhdsWithin_Ioi_atTop {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù}
    (hf : Tendsto f (ùìù[>] x) atTop) :
    ¬¨ ContinuousAt f x := fun h ‚Ü¶
  not_tendsto_nhds_of_tendsto_atTop hf _ (h.tendsto.mono_left inf_le_left)

lemma not_continuousAt_of_tendsto_nhdsWithin_Ioi_atBot {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù}
    (hf : Filter.Tendsto f (ùìù[>] x) Filter.atBot) :
    ¬¨ ContinuousAt f x := fun h ‚Ü¶
  not_tendsto_nhds_of_tendsto_atBot hf _ (h.tendsto.mono_left inf_le_left)

lemma not_continuousAt_of_tendsto_nhdsWithin_Iio_atTop {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù}
    (hf : Filter.Tendsto f (ùìù[<] x) Filter.atTop) :
    ¬¨ ContinuousAt f x := fun h ‚Ü¶
  not_tendsto_nhds_of_tendsto_atTop hf _ (h.tendsto.mono_left inf_le_left)

lemma not_continuousAt_of_tendsto_nhdsWithin_Iio_atBot {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù}
    (hf : Filter.Tendsto f (ùìù[<] x) Filter.atBot) :
    ¬¨ ContinuousAt f x := fun h ‚Ü¶
  not_tendsto_nhds_of_tendsto_atBot hf _ (h.tendsto.mono_left inf_le_left)

end NotContinuousAtOfTendstoNhdsWithin

lemma not_continuousAt_deriv_mul_log_zero :
    ¬¨ ContinuousAt (deriv (fun (x : ‚Ñù) ‚Ü¶ x * log x)) 0 :=
  not_continuousAt_of_tendsto_nhdsWithin_Ioi_atBot tendsto_deriv_mul_log_nhdsWithin_zero

lemma deriv2_mul_log (x : ‚Ñù) : deriv^[2] (fun x ‚Ü¶ x * log x) x = x‚Åª¬π := by
  simp only [Function.iterate_succ, Function.iterate_zero, Function.id_comp, Function.comp_apply]
  by_cases hx : x ‚â† 0
  ¬∑ suffices ‚àÄ·∂† y in (ùìù x), deriv (fun x ‚Ü¶ x * log x) y = log y + 1 by
      refine (Filter.EventuallyEq.deriv_eq this).trans ?_
      rw [deriv_add_const, deriv_log x]
    filter_upwards [eventually_ne_nhds hx] with y hy using deriv_mul_log hy
  ¬∑ rw [show x = 0 by simp_all only [ne_eq, Decidable.not_not], inv_zero]
    exact deriv_zero_of_not_differentiableAt
      (fun h ‚Ü¶ not_continuousAt_deriv_mul_log_zero h.continuousAt)

lemma strictConvexOn_mul_log : StrictConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) := by
  refine strictConvexOn_of_deriv2_pos (convex_Ici 0) (continuous_mul_log.continuousOn) ?_
  intro x hx
  simp only [Set.nonempty_Iio, interior_Ici', Set.mem_Ioi] at hx
  rw [deriv2_mul_log]
  positivity

lemma convexOn_mul_log : ConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) :=
  strictConvexOn_mul_log.convexOn

lemma mul_log_nonneg {x : ‚Ñù} (hx : 1 ‚â§ x) : 0 ‚â§ x * log x :=
  mul_nonneg (zero_le_one.trans hx) (log_nonneg hx)

lemma mul_log_nonpos {x : ‚Ñù} (hx‚ÇÄ : 0 ‚â§ x) (hx‚ÇÅ : x ‚â§ 1) : x * log x ‚â§ 0 :=
  mul_nonpos_of_nonneg_of_nonpos hx‚ÇÄ (log_nonpos hx‚ÇÄ hx‚ÇÅ)

section negMulLog

/-- The function `x ‚Ü¶ - x * log x` from `‚Ñù` to `‚Ñù`. -/
noncomputable def negMulLog (x : ‚Ñù) : ‚Ñù := - x * log x

lemma negMulLog_def : negMulLog = fun x ‚Ü¶ - x * log x := rfl

lemma negMulLog_eq_neg : negMulLog = fun x ‚Ü¶ - (x * log x) := by simp [negMulLog_def]

@[simp] lemma negMulLog_zero : negMulLog (0 : ‚Ñù) = 0 := by simp [negMulLog]

@[simp] lemma negMulLog_one : negMulLog (1 : ‚Ñù) = 0 := by simp [negMulLog]

lemma negMulLog_nonneg {x : ‚Ñù} (h1 : 0 ‚â§ x) (h2 : x ‚â§ 1) : 0 ‚â§ negMulLog x := by
  simpa only [negMulLog_eq_neg, neg_nonneg] using mul_log_nonpos h1 h2

lemma negMulLog_mul (x y : ‚Ñù) : negMulLog (x * y) = y * negMulLog x + x * negMulLog y := by
  simp only [negMulLog, neg_mul, neg_add_rev]
  by_cases hx : x = 0
  ¬∑ simp [hx]
  by_cases hy : y = 0
  ¬∑ simp [hy]
  rw [log_mul hx hy]
  ring

lemma continuous_negMulLog : Continuous negMulLog := by
  simpa only [negMulLog_eq_neg] using continuous_mul_log.neg

lemma differentiableOn_negMulLog : DifferentiableOn ‚Ñù negMulLog {0}·∂ú := by
  simpa only [negMulLog_eq_neg] using differentiableOn_mul_log.neg

lemma differentiableAt_negMulLog_iff {x : ‚Ñù} : DifferentiableAt ‚Ñù negMulLog x ‚Üî x ‚â† 0 := by
  constructor
  ¬∑ unfold negMulLog
    intro h eq0
    simp only [neg_mul, differentiableAt_neg_iff, eq0] at h
    exact not_DifferentiableAt_log_mul_zero h
  ¬∑ intro hx
    have : x ‚àà ({0} : Set ‚Ñù)·∂ú := by
      simp_all only [ne_eq, Set.mem_compl_iff, Set.mem_singleton_iff, not_false_eq_true]
    have := differentiableOn_negMulLog x this
    apply DifferentiableWithinAt.differentiableAt (s := {0}·∂ú)
    <;> simp_all only [ne_eq, Set.mem_compl_iff, Set.mem_singleton_iff, not_false_eq_true,
        compl_singleton_mem_nhds_iff]

lemma deriv_negMulLog {x : ‚Ñù} (hx : x ‚â† 0) : deriv negMulLog x = - log x - 1 := by
  rw [negMulLog_eq_neg, deriv.neg, deriv_mul_log hx]
  ring

lemma hasDerivAt_negMulLog {x : ‚Ñù} (hx : x ‚â† 0) : HasDerivAt negMulLog (- log x - 1) x := by
  rw [‚Üê deriv_negMulLog hx, hasDerivAt_deriv_iff]
  refine DifferentiableOn.differentiableAt differentiableOn_negMulLog ?_
  simp [hx]

lemma deriv2_negMulLog (x : ‚Ñù) : deriv^[2] negMulLog x = - x‚Åª¬π := by
  rw [negMulLog_eq_neg]
  have h := deriv2_mul_log
  simp only [Function.iterate_succ, Function.iterate_zero, Function.id_comp,
    Function.comp_apply, deriv.neg', differentiableAt_id', differentiableAt_log_iff, ne_eq] at h ‚ä¢
  rw [h]

lemma strictConcaveOn_negMulLog : StrictConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog := by
  simpa only [negMulLog_eq_neg] using strictConvexOn_mul_log.neg

lemma concaveOn_negMulLog : ConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog :=
  strictConcaveOn_negMulLog.concaveOn

end negMulLog

end Real
