/-
Copyright (c) 2023 R√©my Degenne. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: R√©my Degenne
-/
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Analysis.SpecialFunctions.Pow.Asymptotics

/-!
# The function `x ‚Ü¶ - x * log x`

The purpose of this file is to record basic analytic properties of the function `x ‚Ü¶ - x * log x`,
which is notably used in the theory of Shannon entropy.

## Main definitions

* `negMulLog`: the function `x ‚Ü¶ - x * log x` from `‚Ñù` to `‚Ñù`.

-/

open scoped Topology

namespace Real

lemma continuous_mul_log : Continuous fun x ‚Ü¶ x * log x := by
  rw [continuous_iff_continuousAt]
  intro x
  by_cases hx : x = 0
  swap; ¬∑ exact (continuous_id'.continuousAt).mul (continuousAt_log hx)
  rw [hx, ContinuousAt, zero_mul]
  suffices Filter.Tendsto (fun x ‚Ü¶ log x * x) (ùìù 0) (ùìù 0) by
    exact this.congr (fun x ‚Ü¶ by rw [mul_comm])
  nth_rewrite 1 [‚Üê nhdsWithin_univ]
  have : (Set.univ : Set ‚Ñù) = Set.Iio 0 ‚à™ Set.Ioi 0 ‚à™ {0} := by ext; simp [em]
  rw [this, nhdsWithin_union, nhdsWithin_union]
  simp only [nhdsWithin_singleton, sup_le_iff, Filter.nonpos_iff, Filter.tendsto_sup]
  refine ‚ü®‚ü®tendsto_log_mul_nhds_zero_left, ?_‚ü©, ?_‚ü©
  ¬∑ simpa only [rpow_one] using tendsto_log_mul_rpow_nhds_zero zero_lt_one
  ¬∑ convert tendsto_pure_nhds (fun x ‚Ü¶ log x * x) 0
    simp

lemma differentiableOn_mul_log : DifferentiableOn ‚Ñù (fun x ‚Ü¶ x * log x) {0}·∂ú :=
  differentiable_id'.differentiableOn.mul differentiableOn_log

lemma deriv_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : deriv (fun x ‚Ü¶ x * log x) x = log x + 1 := by
  rw [deriv_mul differentiableAt_id' (differentiableAt_log hx)]
  simp only [deriv_id'', one_mul, deriv_log', ne_eq, add_right_inj]
  exact mul_inv_cancel hx

lemma deriv2_mul_log {x : ‚Ñù} (hx : x ‚â† 0) : deriv^[2] (fun x ‚Ü¶ x * log x) x = x‚Åª¬π := by
  simp only [Function.iterate_succ, Function.iterate_zero, Function.comp.left_id,
    Function.comp_apply]
  suffices ‚àÄ·∂† y in (ùìù x), deriv (fun x ‚Ü¶ x * log x) y = log y + 1 by
    refine (Filter.EventuallyEq.deriv_eq this).trans ?_
    rw [deriv_add_const, deriv_log x]
  filter_upwards [eventually_ne_nhds hx] with y hy using deriv_mul_log hy

lemma strictConvexOn_mul_log : StrictConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) := by
  refine strictConvexOn_of_deriv2_pos (convex_Ici 0) (continuous_mul_log.continuousOn) ?_
  intro x hx
  simp only [Set.nonempty_Iio, interior_Ici', Set.mem_Ioi] at hx
  rw [deriv2_mul_log hx.ne']
  positivity

lemma convexOn_mul_log : ConvexOn ‚Ñù (Set.Ici (0 : ‚Ñù)) (fun x ‚Ü¶ x * log x) :=
  strictConvexOn_mul_log.convexOn

lemma mul_log_nonneg {x : ‚Ñù} (hx : 1 ‚â§ x) : 0 ‚â§ x * log x :=
  mul_nonneg (zero_le_one.trans hx) (log_nonneg hx)

section negMulLog

/-- The function `x ‚Ü¶ - x * log x` from `‚Ñù` to `‚Ñù`. -/
noncomputable def negMulLog (x : ‚Ñù) : ‚Ñù := - x * log x

lemma negMulLog_def : negMulLog = fun x ‚Ü¶ - x * log x := rfl

lemma negMulLog_eq_neg : negMulLog = fun x ‚Ü¶ - (x * log x) := by simp [negMulLog_def]

@[simp] lemma negMulLog_zero : negMulLog (0 : ‚Ñù) = 0 := by simp [negMulLog]

@[simp] lemma negMulLog_one : negMulLog (1 : ‚Ñù) = 0 := by simp [negMulLog]

lemma negMulLog_nonneg {x : ‚Ñù} (h1 : 0 ‚â§ x) (h2 : x ‚â§ 1) : 0 ‚â§ negMulLog x := by
  rw [negMulLog_eq_neg, neg_nonneg]
  exact mul_nonpos_of_nonneg_of_nonpos h1 (log_nonpos h1 h2)

lemma negMulLog_mul (x y : ‚Ñù) : negMulLog (x * y) = y * negMulLog x + x * negMulLog y := by
  simp only [negMulLog, neg_mul, neg_add_rev]
  by_cases hx : x = 0
  ¬∑ simp [hx]
  by_cases hy : y = 0
  ¬∑ simp [hy]
  rw [log_mul hx hy]
  ring

lemma continuous_negMulLog : Continuous negMulLog := by
  rw [negMulLog_eq_neg]
  exact continuous_mul_log.neg

lemma differentiableOn_negMulLog : DifferentiableOn ‚Ñù negMulLog {0}·∂ú := by
  rw [negMulLog_eq_neg]
  exact differentiableOn_mul_log.neg

lemma deriv_negMulLog {x : ‚Ñù} (hx : x ‚â† 0) : deriv negMulLog x = - log x - 1 := by
  rw [negMulLog_eq_neg, deriv.neg, deriv_mul_log hx]
  ring

lemma deriv2_negMulLog {x : ‚Ñù} (hx : x ‚â† 0) : deriv^[2] negMulLog x = - x‚Åª¬π := by
  rw [negMulLog_eq_neg]
  have h := deriv2_mul_log hx
  simp only [Function.iterate_succ, Function.iterate_zero, Function.comp.left_id,
    Function.comp_apply, deriv.neg', differentiableAt_id', differentiableAt_log_iff, ne_eq] at h ‚ä¢
  rw [h]

lemma strictConcaveOn_negMulLog : StrictConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog := by
  rw [negMulLog_eq_neg]
  exact strictConvexOn_mul_log.neg

lemma concaveOn_negMulLog : ConcaveOn ‚Ñù (Set.Ici (0 : ‚Ñù)) negMulLog :=
  strictConcaveOn_negMulLog.concaveOn

end negMulLog

end Real
