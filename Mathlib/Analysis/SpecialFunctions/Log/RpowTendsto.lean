/-
Copyright (c) 2025 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Real

/-!
# The logarithm as a limit of powers

This file shows that the logarithm can be expressed as a limit of powers, namely that
`p‚Åª¬π * (x ^ p - 1)` tends to `log x` as `p` tends to zero for positive `x`.

## Main declarations

* `tendstoUniformlyOn_rpow_sub_one_log`: `p‚Åª¬π * (x ^ p - 1)` tends uniformly to `log x` on
  compact subsets of `Ioi 0` as `p` tends to zero
* `tendsto_rpow_sub_one_log`: `p‚Åª¬π * (x ^ p - 1)`: the analogous statement for pointwise
  convergence.
-/

open scoped Topology
open Real Filter

open Set in
lemma tendstoUniformlyOn_rpow_sub_one_log {s : Set ‚Ñù} (hs : s ‚äÜ Ioi 0) (hs' : IsCompact s) :
    TendstoUniformlyOn (fun (p : ‚Ñù) (x : ‚Ñù) => p‚Åª¬π * (x ^ p - 1)) log (ùìù[>] 0) s := by
  rw [Metric.tendstoUniformlyOn_iff]
  intro Œµ hŒµ
  let pbound : ‚Ñù := Œµ / (sSup ((fun x => ‚Äñlog x‚Äñ ^ 2) '' s) + 1)
  have hxs : ‚àÄ x ‚àà s, x ‚â† 0 := by grind
  have sSup_nonneg : 0 ‚â§ sSup ((fun x => ‚Äñlog x‚Äñ ^ 2) '' s) := by
    refine Real.sSup_nonneg ?_
    grind [norm_nonneg, ‚Üê sq_nonneg]
  have sSup_nonneg' : 0 ‚â§ sSup ((fun x => ‚Äñlog x‚Äñ) '' s) := by
    refine Real.sSup_nonneg ?_
    grind [norm_nonneg, ‚Üê sq_nonneg]
  have pbound_pos : 0 < pbound := by positivity
  have h‚ÇÅ : ‚àÄ·∂† p : ‚Ñù in ùìù[>] 0, 0 < p := eventually_mem_of_tendsto_nhdsWithin fun ‚¶ÉU‚¶Ñ a => a
  have h‚ÇÇ : ‚àÄ·∂† p : ‚Ñù in ùìù[>] 0, p < pbound :=
    Eventually.filter_mono nhdsWithin_le_nhds <| eventually_lt_nhds pbound_pos
  have h‚ÇÉ : ‚àÄ·∂† p : ‚Ñù in ùìù[>] 0, p ‚â§ 1 / (sSup ((fun x => ‚Äñlog x‚Äñ) '' s) + 1) :=
    Eventually.filter_mono nhdsWithin_le_nhds <| eventually_le_nhds (by positivity)
  have hcont : ContinuousOn (fun x => ‚Äñlog x‚Äñ ^ 2) s := by
    fun_prop (disch := assumption)
  have hcont' : ContinuousOn (fun x => ‚Äñlog x‚Äñ) s := by
    fun_prop (disch := assumption)
  filter_upwards [h‚ÇÅ, h‚ÇÇ, h‚ÇÉ] with p hp‚ÇÅ hp‚ÇÇ hp‚ÇÉ
  have p_nonneg : 0 ‚â§ p := by grind
  intro x hx
  have hx' : ‚Äñp * log x‚Äñ ‚â§ 1 := calc
    _ = p * ‚Äñlog x‚Äñ := by grind [norm_mul, Real.norm_of_nonneg]
    _ ‚â§ 1 / (sSup ((fun y => ‚Äñlog y‚Äñ) '' s) + 1) * ‚Äñlog x‚Äñ := by gcongr
    _ ‚â§ 1 / (‚Äñlog x‚Äñ + 1) * ‚Äñlog x‚Äñ := by
        gcongr
        refine le_csSup ?_ (by grind)
        grind [IsCompact.bddAbove, ‚Üê IsCompact.image_of_continuousOn]
    _ = ‚Äñlog x‚Äñ / (‚Äñlog x‚Äñ + 1) := by grind
    _ ‚â§ 1 := by rw [div_le_one‚ÇÄ] <;> grind [norm_nonneg]
  have pinv_nonneg : 0 ‚â§ p‚Åª¬π := by grind [_root_.inv_nonneg]
  rw [dist_eq_norm']
  calc
    _ = ‚Äñp‚Åª¬π * ((x ^ p - 1) - p * log x)‚Äñ := by grind
    _ = p‚Åª¬π * ‚Äñ(rexp (p * log x) - 1) - p * log x‚Äñ := by
          simp only [norm_mul, Real.norm_of_nonneg (r := p‚Åª¬π) pinv_nonneg]
          congr
          rw [mul_comm, Real.exp_mul, Real.exp_log (by grind)]
    _ ‚â§ p‚Åª¬π * ‚Äñp * log x‚Äñ ^ 2 := by
          gcongr
          refine Real.norm_exp_sub_one_sub_id_le ?_
          simp only [hx']
    _ = p * ‚Äñlog x‚Äñ ^ 2 := by
          simp only [norm_mul]
          grind [Real.norm_of_nonneg]
    _ ‚â§ p * sSup ((fun x => ‚Äñlog x‚Äñ ^ 2) '' s) := by
          gcongr
          refine le_csSup ?_ (by grind)
          grind [IsCompact.bddAbove, ‚Üê IsCompact.image_of_continuousOn]
    _ ‚â§ p * (sSup ((fun x => ‚Äñlog x‚Äñ ^ 2) '' s) + 1) := by gcongr; grind
    _ < pbound * (sSup ((fun x => ‚Äñlog x‚Äñ ^ 2) '' s) + 1) := by gcongr
    _ = Œµ := by grind

lemma tendsto_rpow_sub_one_log {x : ‚Ñù} (hx : 0 < x) :
    Tendsto (fun p => p‚Åª¬π * (x ^ p - 1)) (ùìù[>] 0) (ùìù (log x)) :=
  TendstoUniformlyOn.tendsto_at (s := {x})
    (tendstoUniformlyOn_rpow_sub_one_log (by grind) isCompact_singleton) (by grind)
