/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.Algebra.Module.ZLattice.Summable
public import Mathlib.Analysis.Analytic.Binomial
public import Mathlib.Analysis.Complex.Liouville
public import Mathlib.Analysis.Complex.LocallyUniformLimit
public import Mathlib.Analysis.Meromorphic.Order
public import Mathlib.LinearAlgebra.Complex.FiniteDimensional
public import Mathlib.Tactic.NormNum.NatFactorial
public import Mathlib.Topology.Algebra.InfiniteSum.UniformOn
public import Mathlib.Topology.MetricSpace.ProperSpace.Lemmas

/-!

# Weierstrass `‚Ñò` functions

## Main definitions and results
- `PeriodPair.weierstrassP`: The Weierstrass `‚Ñò`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_weierstrassP`:
  The summands of `‚Ñò` sums to `‚Ñò` locally uniformly.
- `PeriodPair.differentiableOn_weierstrassP`: `‚Ñò` is differentiable away from the lattice points.
- `PeriodPair.weierstrassP_add_coe`: The Weierstrass `‚Ñò`-function is periodic.
- `PeriodPair.weierstrassP_neg`: The Weierstrass `‚Ñò`-function is even.

- `PeriodPair.derivWeierstrassP`:
  The derivative of the Weierstrass `‚Ñò`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_derivWeierstrassP`:
  The summands of `‚Ñò'` sums to `‚Ñò'` locally uniformly.
- `PeriodPair.differentiableOn_derivWeierstrassP`:
  `‚Ñò'` is differentiable away from the lattice points.
- `PeriodPair.derivWeierstrassP_add_coe`: `‚Ñò'` is periodic.
- `PeriodPair.weierstrassP_neg`: `‚Ñò'` is odd.
- `PeriodPair.deriv_weierstrassP`: `deriv ‚Ñò = ‚Ñò'`. This is true globally because of junk values.
- `PeriodPair.analyticOnNhd_weierstrassP`: `‚Ñò` is analytic away from the lattice points.
- `PeriodPair.meromorphic_weierstrassP`: `‚Ñò` is meromorphic on the whole plane.
- `PeriodPair.order_weierstrassP`: `‚Ñò` has a pole of order 2 at each of the lattice points.
- `PeriodPair.derivWeierstrassP_sq` : `‚Ñò'(z)¬≤ = 4 ‚Ñò(z)¬≥ - g‚ÇÇ ‚Ñò(z) - g‚ÇÉ`

## tags

Weierstrass p-functions, Weierstrass p functions

-/

@[expose] public section

open Module Filter
open scoped Topology Nat

noncomputable section

/-- A pair of `‚Ñù`-linearly independent complex numbers.
They span the period lattice in `lattice`,
and are the periods of the elliptic functions we shall construct. -/
structure PeriodPair : Type where
  /-- The first period in a `PeriodPair`. -/
  œâ‚ÇÅ : ‚ÑÇ
  /-- The second period in a `PeriodPair`. -/
  œâ‚ÇÇ : ‚ÑÇ
  indep : LinearIndependent ‚Ñù ![œâ‚ÇÅ, œâ‚ÇÇ]

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] (L : PeriodPair)

namespace PeriodPair

/-- The `‚Ñù`-basis of `‚ÑÇ` determined by a pair of periods. -/
protected def basis : Basis (Fin 2) ‚Ñù ‚ÑÇ :=
  basisOfLinearIndependentOfCardEqFinrank L.indep (by simp)

@[simp] lemma basis_zero : L.basis 0 = L.œâ‚ÇÅ := by simp [PeriodPair.basis]
@[simp] lemma basis_one : L.basis 1 = L.œâ‚ÇÇ := by simp [PeriodPair.basis]

/-- The lattice spanned by a pair of periods. -/
def lattice : Submodule ‚Ñ§ ‚ÑÇ := Submodule.span ‚Ñ§ {L.œâ‚ÇÅ, L.œâ‚ÇÇ}

lemma mem_lattice {L : PeriodPair} {x : ‚ÑÇ} :
    x ‚àà L.lattice ‚Üî ‚àÉ m n : ‚Ñ§, m * L.œâ‚ÇÅ + n * L.œâ‚ÇÇ = x := by
  simp only [lattice, Submodule.mem_span_pair, zsmul_eq_mul]

lemma œâ‚ÇÅ_mem_lattice : L.œâ‚ÇÅ ‚àà L.lattice := Submodule.subset_span (by simp)
lemma œâ‚ÇÇ_mem_lattice : L.œâ‚ÇÇ ‚àà L.lattice := Submodule.subset_span (by simp)

lemma mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice {L : PeriodPair} {Œ± Œ≤ : ‚Ñö} :
    Œ± * L.œâ‚ÇÅ + Œ≤ * L.œâ‚ÇÇ ‚àà L.lattice ‚Üî Œ±.den = 1 ‚àß Œ≤.den = 1 := by
  refine ‚ü®fun H ‚Ü¶ ?_, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®m, n, e‚ü© := mem_lattice.mp H
    have := LinearIndependent.pair_iff.mp L.indep (m - Œ±) (n - Œ≤)
      (by simpa using by linear_combination e)
    simp only [sub_eq_zero] at this
    norm_cast at this
    aesop
  ¬∑ lift Œ± to ‚Ñ§ using h‚ÇÅ
    lift Œ≤ to ‚Ñ§ using h‚ÇÇ
    simp only [Rat.cast_intCast, ‚Üê zsmul_eq_mul]
    exact add_mem (Submodule.smul_mem _ _ L.œâ‚ÇÅ_mem_lattice)
      (Submodule.smul_mem _ _ L.œâ‚ÇÇ_mem_lattice)

lemma œâ‚ÇÅ_div_two_notMem_lattice : L.œâ‚ÇÅ / 2 ‚àâ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice (Œ± := 1 / 2) (Œ≤ := 0)).not.mpr (by norm_num)

lemma œâ‚ÇÇ_div_two_notMem_lattice : L.œâ‚ÇÇ / 2 ‚àâ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice (Œ± := 0) (Œ≤ := 1 / 2)).not.mpr (by norm_num)

-- helper lemma to connect to the ZLattice API
lemma lattice_eq_span_range_basis :
    L.lattice = Submodule.span ‚Ñ§ (Set.range L.basis) := by
  have : Finset.univ (Œ± := Fin 2) = {0, 1} := rfl
  rw [lattice, ‚Üê Set.image_univ, ‚Üê Finset.coe_univ, this]
  simp [Set.image_insert_eq]

instance : DiscreteTopology L.lattice := L.lattice_eq_span_range_basis ‚ñ∏ inferInstance

instance : IsZLattice ‚Ñù L.lattice := by
  simp_rw [L.lattice_eq_span_range_basis]
  infer_instance

lemma isClosed_lattice : IsClosed (X := ‚ÑÇ) L.lattice :=
  @AddSubgroup.isClosed_of_discrete _ _ _ _ _ L.lattice.toAddSubgroup
    (inferInstanceAs (DiscreteTopology L.lattice))

lemma isClosed_of_subset_lattice {s : Set ‚ÑÇ} (hs : s ‚äÜ L.lattice) : IsClosed s := by
  convert L.isClosed_lattice.isClosedMap_subtype_val _
    (isClosed_discrete (Œ± := L.lattice) ((‚Üë) ‚Åª¬π' s))
  convert Set.image_preimage_eq_inter_range.symm using 1
  simpa

lemma isOpen_compl_lattice_diff {s : Set ‚ÑÇ} : IsOpen (L.lattice \ s)·∂ú :=
  (L.isClosed_of_subset_lattice Set.diff_subset).isOpen_compl

open scoped Topology in
lemma compl_lattice_diff_singleton_mem_nhds (x : ‚ÑÇ) : (‚ÜëL.lattice \ {x})·∂ú ‚àà ùìù x :=
  L.isOpen_compl_lattice_diff.mem_nhds (by simp)

instance : ProperSpace L.lattice := .of_isClosed L.isClosed_lattice

/-- The `‚Ñ§`-basis of the lattice determined by a pair of periods. -/
def latticeBasis : Basis (Fin 2) ‚Ñ§ L.lattice :=
  (Basis.span (v := ![L.œâ‚ÇÅ, L.œâ‚ÇÇ]) (L.indep.restrict_scalars' _)).map
    (.ofEq _ _ (by simp [lattice, Set.pair_comm L.œâ‚ÇÇ L.œâ‚ÇÅ]))

@[simp] lemma latticeBasis_zero : L.latticeBasis 0 = L.œâ‚ÇÅ := by simp [latticeBasis]
@[simp] lemma latticeBasis_one : L.latticeBasis 1 = L.œâ‚ÇÇ := by simp [latticeBasis]

@[simp] lemma finrank_lattice : finrank ‚Ñ§ L.lattice = 2 := finrank_eq_card_basis L.latticeBasis

/-- The equivalence from the lattice generated by a pair of periods to `‚Ñ§ √ó ‚Ñ§`. -/
def latticeEquivProd : L.lattice ‚âÉ‚Çó[‚Ñ§] ‚Ñ§ √ó ‚Ñ§ :=
  L.latticeBasis.repr ‚â™‚â´‚Çó Finsupp.linearEquivFunOnFinite _ _ _ ‚â™‚â´‚Çó .finTwoArrow ‚Ñ§ ‚Ñ§

lemma latticeEquiv_symm_apply (x : ‚Ñ§ √ó ‚Ñ§) :
    (L.latticeEquivProd.symm x).1 = x.1 * L.œâ‚ÇÅ + x.2 * L.œâ‚ÇÇ := by
  simp [latticeEquivProd, Finsupp.linearCombination]
  rfl

lemma hasSumLocallyUniformly_aux (f : L.lattice ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ)
    (u : ‚Ñù ‚Üí L.lattice ‚Üí ‚Ñù) (hu : ‚àÄ r > 0, Summable (u r))
    (hf : ‚àÄ r > 0, ‚àÄ·∂† R in atTop, ‚àÄ x, ‚Äñx‚Äñ < r ‚Üí ‚àÄ l : L.lattice, ‚Äñl.1‚Äñ = R ‚Üí ‚Äñf l x‚Äñ ‚â§ u r l) :
    HasSumLocallyUniformly f (‚àë' j, f j ¬∑) := by
  rw [hasSumLocallyUniformly_iff_tendstoLocallyUniformly, tendstoLocallyUniformly_iff_filter]
  intro x
  obtain ‚ü®r, hr, hr'‚ü© : ‚àÉ r, 0 < r ‚àß ùìù x ‚â§ ùìü (Metric.ball 0 r) :=
    ‚ü®‚Äñx‚Äñ + 1, by positivity, Filter.le_principal_iff.mpr (Metric.isOpen_ball.mem_nhds (by simp))‚ü©
  refine .mono_right ?_ hr'
  rw [‚Üê tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  refine tendstoUniformlyOn_tsum_of_cofinite_eventually (hu r hr) ?_
  obtain ‚ü®R, hR‚ü© := eventually_atTop.mp (hf r hr)
  refine (isCompact_iff_finite.mp (isCompact_closedBall (0 : L.lattice) R)).subset ?_
  intros l hl
  obtain ‚ü®s, hs, hs'‚ü© : ‚àÉ x, ‚Äñx‚Äñ < r ‚àß u r l < ‚Äñf l x‚Äñ := by simpa using hl
  simp only [Metric.mem_closedBall, dist_zero_right, AddSubgroupClass.coe_norm]
  contrapose! hs'
  exact hR _ hs'.le _ hs _ rfl

-- Only the asymptotics matter and `10` is just a convenient constant to pick.
lemma weierstrassP_bound (r : ‚Ñù) (hr : 0 < r) (s : ‚ÑÇ) (hs : ‚Äñs‚Äñ < r) (l : ‚ÑÇ) (h : 2 * r ‚â§ ‚Äñl‚Äñ) :
    ‚Äñ1 / (s - l) ^ 2 - 1 / l ^ 2‚Äñ ‚â§ 10 * r * ‚Äñl‚Äñ ^ (-3 : ‚Ñù) := by
  have : s ‚â† ‚Üël := by rintro rfl; linarith
  have : 0 < ‚Äñl‚Äñ := by linarith
  calc
    _ = ‚Äñ(‚Üël ^ 2 - (s - ‚Üël) ^ 2) / ((s - ‚Üël) ^ 2 * ‚Üël ^ 2)‚Äñ := by
      rw [div_sub_div, one_mul, mul_one]
      ¬∑ simpa [sub_eq_zero]
      ¬∑ simpa
    _ = ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / (‚Äñs - l‚Äñ ^ 2 * ‚Äñl‚Äñ ^ 2) := by simp
    _ ‚â§ ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / ((‚Äñl‚Äñ / 2) ^ 2 * ‚Äñl‚Äñ ^ 2) := by
      gcongr
      rw [norm_sub_rev]
      exact .trans (by linarith) (norm_sub_norm_le l s)
    _ = ‚Äñs * (2 * l - s)‚Äñ / (‚Äñl‚Äñ ^ 4 / 4) := by
      congr 1
      ¬∑ rw [sq_sub_sq]; simp [‚Üê sub_add, two_mul, sub_add_eq_add_sub]
      ¬∑ ring
    _ = (‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / (‚Äñl‚Äñ ^ 4 / 4) := by simp
    _ = (4 * ‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / ‚Äñl‚Äñ ^ 4 := by field
    _ ‚â§ (4 * r * (2.5 * ‚Äñl‚Äñ)) / ‚Äñl‚Äñ ^ 4 := by
      gcongr (4 * ?_ * ?_) / ‚Äñl‚Äñ ^ 4
      refine (norm_sub_le _ _).trans ?_
      simp only [Complex.norm_mul, Complex.norm_ofNat]
      linarith
    _ = 10 * r / ‚Äñl‚Äñ ^ 3 := by field
    _ = _ := by norm_cast

section weierstrassPExcept

/-- The Weierstrass `‚Ñò` function with the `l‚ÇÄ`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `‚Ñò[L - l‚ÇÄ]` in the namespace `PeriodPairs`. -/
def weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassPExcept]
scoped notation3 "‚Ñò[" L:max " - " l‚ÇÄ "]" => weierstrassPExcept L l‚ÇÄ

lemma hasSumLocallyUniformly_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    HasSumLocallyUniformly
      (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ if l.1 = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      ‚Ñò[L - l‚ÇÄ] := by
  refine L.hasSumLocallyUniformly_aux (u := (10 * ¬∑ * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simpa using show 0 ‚â§ 10 * r * (‚Äñ‚Üël‚Äñ ^ 3)‚Åª¬π by positivity
  ¬∑ exact weierstrassP_bound r hr s hs l h

lemma hasSum_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      (‚Ñò[L - l‚ÇÄ] z) :=
  (L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).hasSum

/- `weierstrassPExcept l‚ÇÄ` is differentiable on non-lattice points and `l‚ÇÄ`. -/
lemma differentiableOn_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ ‚Ñò[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine (L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).hasSumLocallyUniformlyOn.differentiableOn
    (.of_forall fun s ‚Ü¶ .fun_sum fun i hi ‚Ü¶ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  ¬∑ simp
  ¬∑ exact .sub (.div (by fun_prop) (by fun_prop) (by aesop (add simp sub_eq_zero))) (by fun_prop)

lemma weierstrassPExcept_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] (-z) = ‚Ñò[L - -l‚ÇÄ] z := by
  simp only [weierstrassPExcept]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  simp
  ring

@[simp] lemma weierstrassPExcept_zero (l‚ÇÄ : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] 0 = 0 := by simp [weierstrassPExcept]

end weierstrassPExcept

section weierstrassP

/-- The Weierstrass `‚Ñò` function. This has the notation `‚Ñò[L]` in the namespace `PeriodPairs`. -/
def weierstrassP (z : ‚ÑÇ) : ‚ÑÇ := ‚àë' l : L.lattice, (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassP] scoped notation3 "‚Ñò[" L "]" => weierstrassP L

lemma weierstrassPExcept_add (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] z + (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) = ‚Ñò[L] z := by
  trans ‚Ñò[L - l‚ÇÄ] z + ‚àë' i : L.lattice, if i = l‚ÇÄ.1 then (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) else 0
  ¬∑ simp
  rw [weierstrassPExcept, ‚Üê Summable.tsum_add]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *]
  ¬∑ exact ‚ü®_, L.hasSum_weierstrassPExcept _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma weierstrassPExcept_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] z = ‚Ñò[L] z + (1 / l‚ÇÄ.1 ^ 2 - 1 / (z - l‚ÇÄ.1) ^ 2) := by
  rw [‚Üê L.weierstrassPExcept_add l‚ÇÄ]
  abel

lemma weierstrassPExcept_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    ‚Ñò[L - l‚ÇÄ] = ‚Ñò[L] := by
  delta weierstrassPExcept weierstrassP
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this]

lemma hasSumLocallyUniformly_weierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ 1 / (z - ‚Üël) ^ 2 - 1 / l ^ 2) ‚Ñò[L] := by
  convert L.hasSumLocallyUniformly_weierstrassPExcept (L.œâ‚ÇÅ / 2) using 3 with l
  ¬∑ rw [if_neg]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_notMem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma hasSum_weierstrassP (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ (1 / (z - l) ^ 2 - 1 / l ^ 2)) (‚Ñò[L] z) :=
  L.hasSumLocallyUniformly_weierstrassP.hasSum

lemma differentiableOn_weierstrassP :
    DifferentiableOn ‚ÑÇ ‚Ñò[L] L.lattice·∂ú := by
  rw [‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  convert L.differentiableOn_weierstrassPExcept _
  simp [L.œâ‚ÇÅ_div_two_notMem_lattice]

@[simp]
lemma weierstrassP_neg (z : ‚ÑÇ) : ‚Ñò[L] (-z) = ‚Ñò[L] z := by
  simp only [weierstrassP]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr with l
  simp
  ring

lemma not_continuousAt_weierstrassP (x : ‚ÑÇ) (hx : x ‚àà L.lattice) : ¬¨ ContinuousAt ‚Ñò[L] x := by
  eta_expand
  simp_rw [‚Üê L.weierstrassPExcept_add ‚ü®x, hx‚ü©]
  intro H
  apply (NormedField.continuousAt_zpow (n := -2) (x := (0 : ‚ÑÇ))).not.mpr (by simp)
  simpa [Function.comp_def] using
    (((H.sub ((L.differentiableOn_weierstrassPExcept x).differentiableAt
      (L.compl_lattice_diff_singleton_mem_nhds x)).continuousAt).add
      (continuous_const (y := 1 / x ^ 2)).continuousAt).comp_of_eq
      (continuous_add_left x).continuousAt (add_zero _) :)

end weierstrassP

section derivWeierstrassPExcept

/-- The derivative of Weierstrass `‚Ñò` function with the `l‚ÇÄ`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `‚Ñò'[L - l‚ÇÄ]` in the namespace `PeriodPairs`. -/
def derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3

@[inherit_doc derivWeierstrassPExcept]
scoped notation3 "‚Ñò'[" L:max " - " l‚ÇÄ "]" => derivWeierstrassPExcept L l‚ÇÄ

lemma hasSumLocallyUniformly_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3)
      ‚Ñò'[L - l‚ÇÄ] := by
  refine L.hasSumLocallyUniformly_aux (u := fun _ ‚Ü¶ (16 * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simpa using show 0 ‚â§ ‚Äñ‚Üël‚Äñ ^ 3 by positivity
  have : s ‚â† ‚Üël := by rintro rfl; exfalso; linarith
  have : l ‚â† 0 := by rintro rfl; simp_all; linarith
  simp only [Complex.norm_div, norm_neg, Complex.norm_ofNat, norm_pow, AddSubgroupClass.coe_norm]
  rw [Real.rpow_neg (by positivity), ‚Üê div_eq_mul_inv, div_le_div_iff‚ÇÄ, norm_sub_rev]
  ¬∑ refine LE.le.trans_eq (b := 2 * (2 * ‚Äñl - s‚Äñ) ^ 3) ?_ (by ring)
    norm_cast
    gcongr
    refine le_trans ?_ (mul_le_mul le_rfl (norm_sub_norm_le _ _) (by linarith) (by linarith))
    norm_cast at *
    linarith
  ¬∑ exact pow_pos (by simpa [sub_eq_zero]) _
  ¬∑ exact Real.rpow_pos_of_pos (by simpa) _

lemma hasSum_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3) (‚Ñò'[L - l‚ÇÄ] z) :=
  (L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.tendsto_at
    (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ
    |>.tendstoLocallyUniformlyOn.differentiableOn
      (.of_forall fun s ‚Ü¶ .fun_sum fun i hi ‚Ü¶ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  ¬∑ simp
  refine .div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_
  have : x ‚â† i := by rintro rfl; simp_all
  simpa [sub_eq_zero]

lemma eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    Set.EqOn (deriv ‚Ñò[L - l‚ÇÄ]) ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine ((L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.deriv
    (.of_forall fun s ‚Ü¶ ?_) L.isOpen_compl_lattice_diff).unique ?_
  ¬∑ refine .fun_sum fun i hi ‚Ü¶ ?_
    split_ifs
    ¬∑ simp
    refine .sub (.div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_) (by fun_prop)
    have : x ‚â† i := by rintro rfl; simp_all
    simpa [sub_eq_zero]
  ¬∑ refine (L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.congr ?_
    intro s l hl
    simp only [Function.comp_apply]
    rw [deriv_fun_sum]
    ¬∑ congr with x
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      rw [deriv_fun_sub (.fun_div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop),
        deriv_const]
      simp_rw [‚Üê zpow_natCast, one_div, ‚Üê zpow_neg, Nat.cast_ofNat]
      rw [deriv_comp_sub_const (f := (¬∑ ^ (-2 : ‚Ñ§))), deriv_zpow]
      simp
      field_simp
    ¬∑ intros x hxs
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      exact .sub (.div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop)

@[simp] lemma deriv_weierstrassPExcept_same (l : ‚ÑÇ) : deriv ‚Ñò[L - l] l = ‚Ñò'[L - l] l :=
  L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept l (x := l) (by simp)

lemma derivWeierstrassPExcept_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] (-z) = - ‚Ñò'[L - (-l‚ÇÄ)] z := by
  simp only [derivWeierstrassPExcept]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub,
    ‚Üê div_neg, ‚Üê tsum_neg, apply_ite, neg_zero]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  ring

@[simp] lemma derivWeierstrassPExcept_zero_zero : ‚Ñò'[L - 0] 0 = 0 := by
  simpa [CharZero.eq_neg_self_iff] using L.derivWeierstrassPExcept_neg 0 0

end derivWeierstrassPExcept

section Periodicity

lemma derivWeierstrassPExcept_add_coe (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L - l‚ÇÄ] (z + l) = ‚Ñò'[L - (l‚ÇÄ - l)] z := by
  simp only [derivWeierstrassPExcept]
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub, eq_sub_iff_add_eq]

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassPExcept_add_coe_aux
    (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    Set.EqOn (‚Ñò[L - l‚ÇÄ] <| ¬∑ + l) (‚Ñò[L - (l‚ÇÄ - l)] ¬∑ + (1 / l‚ÇÄ ^ 2 - 1 / (l‚ÇÄ - ‚Üël) ^ 2))
      (L.lattice \ {l‚ÇÄ - l})·∂ú := by
  apply IsOpen.eqOn_of_deriv_eq (ùïú := ‚ÑÇ) L.isOpen_compl_lattice_diff
    ?_ ?_ ?_ ?_ (x := -(l / 2)) ?_ ?_
  ¬∑ refine (Set.Countable.isConnected_compl_of_one_lt_rank (by simp) ?_).2
    exact .mono sdiff_le (countable_of_Lindelof_of_discrete (X := L.lattice))
  ¬∑ refine (L.differentiableOn_weierstrassPExcept l‚ÇÄ).comp (f := (¬∑ + l.1)) (by fun_prop) ?_
    rintro x h‚ÇÅ ‚ü®h‚ÇÇ : x + l ‚àà _, h‚ÇÉ : x + l ‚â† l‚ÇÄ‚ü©
    exact h‚ÇÅ ‚ü®by simpa using sub_mem h‚ÇÇ l.2, by rintro rfl; simp at h‚ÇÉ‚ü©
  ¬∑ refine .add (L.differentiableOn_weierstrassPExcept _) (by simp)
  ¬∑ intro x hx
    simp only [deriv_add_const', deriv_comp_add_const]
    rw [L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept, L.derivWeierstrassPExcept_add_coe]
    ¬∑ simpa using hx
    ¬∑ simp only [Set.mem_compl_iff, Set.mem_diff, SetLike.mem_coe, Set.mem_singleton_iff, not_and,
        Decidable.not_not, eq_sub_iff_add_eq] at hx ‚ä¢
      exact fun H ‚Ü¶ hx (by simpa using sub_mem H l.2)
  ¬∑ simp [hl]
  ¬∑ rw [L.weierstrassPExcept_neg, L.weierstrassPExcept_def ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©,
      L.weierstrassPExcept_def ‚ü®_, neg_mem (sub_mem hl‚ÇÄ l.2)‚ü©, add_assoc]
    congr 2 <;> ring

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassP_add_coe_aux (z : ‚ÑÇ) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    ‚Ñò[L] (z + l) = ‚Ñò[L] z := by
  have hl0 : l ‚â† 0 := by rintro rfl; simp at hl
  by_cases hz : z ‚àà L.lattice
  ¬∑ have := L.weierstrassPExcept_add_coe_aux (z + l) (add_mem hz l.2) l hl (x := z) (by simp)
    dsimp at this
    rw [‚Üê L.weierstrassPExcept_add ‚ü®z + l, add_mem hz l.2‚ü©, this,
      ‚Üê L.weierstrassPExcept_add ‚ü®z, hz‚ü©]
    simp
    ring
  ¬∑ have := L.weierstrassPExcept_add_coe_aux 0 (zero_mem _) l hl (x := z) (by simp [hz])
    simp only [zero_sub, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, div_zero,
      even_two, Even.neg_pow, one_div] at this
    rw [‚Üê L.weierstrassPExcept_add 0, Submodule.coe_zero, this, ‚Üê L.weierstrassPExcept_add (-l)]
    simp
    ring

@[simp]
lemma weierstrassP_add_coe (z : ‚ÑÇ) (l : L.lattice) : ‚Ñò[L] (z + l) = ‚Ñò[L] z := by
  let G : AddSubgroup ‚ÑÇ :=
    { carrier := { z | (‚Ñò[L] <| ¬∑ + z) = ‚Ñò[L] }
      add_mem' := by simp_all [funext_iff, ‚Üê add_assoc]
      zero_mem' := by simp
      neg_mem' {z} hz := funext fun i ‚Ü¶ by conv_lhs => rw [‚Üê hz]; simp }
  have : L.lattice ‚â§ G.toIntSubmodule := by
    rw [lattice, Submodule.span_le]
    rintro _ (rfl | rfl)
    ¬∑ ext i
      exact L.weierstrassP_add_coe_aux _ ‚ü®_, L.œâ‚ÇÅ_mem_lattice‚ü© L.œâ‚ÇÅ_div_two_notMem_lattice
    ¬∑ ext i
      exact L.weierstrassP_add_coe_aux _ ‚ü®_, L.œâ‚ÇÇ_mem_lattice‚ü© L.œâ‚ÇÇ_div_two_notMem_lattice
  exact congr_fun (this l.2) _

lemma periodic_weierstrassP (l : L.lattice) : ‚Ñò[L].Periodic l :=
  (L.weierstrassP_add_coe ¬∑ l)

@[simp]
lemma weierstrassP_zero : ‚Ñò[L] 0 = 0 := by simp [weierstrassP]

@[simp]
lemma weierstrassP_coe (l : L.lattice) : ‚Ñò[L] l = 0 := by
  rw [‚Üê zero_add l.1, L.weierstrassP_add_coe, L.weierstrassP_zero]

@[simp]
lemma weierstrassP_sub_coe (z : ‚ÑÇ) (l : L.lattice) : ‚Ñò[L] (z - l) = ‚Ñò[L] z := by
  rw [‚Üê L.weierstrassP_add_coe _ l, sub_add_cancel]

end Periodicity

section derivWeierstrassP

/-- The derivative of Weierstrass `‚Ñò` function.
This has the notation `‚Ñò'[L]` in the namespace `PeriodPairs`. -/
def derivWeierstrassP (z : ‚ÑÇ) : ‚ÑÇ := - ‚àë' l : L.lattice, 2 / (z - l) ^ 3

@[inherit_doc weierstrassP] scoped notation3 "‚Ñò'[" L "]" => derivWeierstrassP L

lemma derivWeierstrassPExcept_sub (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] z - 2 / (z - l‚ÇÄ) ^ 3 = ‚Ñò'[L] z := by
  trans ‚Ñò'[L - l‚ÇÄ] z + ‚àë' i : L.lattice, if i.1 = l‚ÇÄ.1 then (- 2 / (z - l‚ÇÄ) ^ 3) else 0
  ¬∑ simp [sub_eq_add_neg, neg_div]
  rw [derivWeierstrassP, derivWeierstrassPExcept, ‚Üê Summable.tsum_add, ‚Üê tsum_neg]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *, neg_div]
  ¬∑ exact ‚ü®_, L.hasSum_derivWeierstrassPExcept _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma derivWeierstrassPExcept_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] z = ‚Ñò'[L] z + 2 / (z - l‚ÇÄ) ^ 3 := by
  rw [‚Üê L.derivWeierstrassPExcept_sub l‚ÇÄ, sub_add_cancel]

lemma derivWeierstrassPExcept_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    ‚Ñò'[L - l‚ÇÄ] = ‚Ñò'[L] := by
  delta derivWeierstrassPExcept derivWeierstrassP
  simp_rw [‚Üê tsum_neg]
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this, neg_div]

lemma hasSumLocallyUniformly_derivWeierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ - 2 / (z - l) ^ 3) ‚Ñò'[L] := by
  convert L.hasSumLocallyUniformly_derivWeierstrassPExcept (L.œâ‚ÇÅ / 2) using 3 with l z
  ¬∑ rw [if_neg, neg_div]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_notMem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma hasSum_derivWeierstrassP (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ - 2 / (z - l) ^ 3) (‚Ñò'[L] z) :=
  L.hasSumLocallyUniformly_derivWeierstrassP.tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassP :
    DifferentiableOn ‚ÑÇ ‚Ñò'[L] L.lattice·∂ú := by
  rw [‚Üê L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  convert L.differentiableOn_derivWeierstrassPExcept _
  simp [L.œâ‚ÇÅ_div_two_notMem_lattice]

@[simp]
lemma derivWeierstrassP_neg (z : ‚ÑÇ) : ‚Ñò'[L] (-z) = - ‚Ñò'[L] z := by
  simp only [derivWeierstrassP]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub, neg_neg,
    ‚Üê div_neg, ‚Üê tsum_neg]
  congr! with l
  ring

@[simp]
lemma derivWeierstrassP_add_coe (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L] (z + l) = ‚Ñò'[L] z := by
  simp only [derivWeierstrassP]
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [‚Üê tsum_neg, ‚Üê div_neg, Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub]

lemma periodic_derivWeierstrassP (l : L.lattice) : ‚Ñò'[L].Periodic l :=
  (L.derivWeierstrassP_add_coe ¬∑ l)

@[simp]
lemma derivWeierstrassP_zero : ‚Ñò'[L] 0 = 0 := by
  rw [‚Üê CharZero.eq_neg_self_iff, ‚Üê L.derivWeierstrassP_neg, neg_zero]

@[simp]
lemma derivWeierstrassP_coe (l : L.lattice) : ‚Ñò'[L] l = 0 := by
  rw [‚Üê zero_add l.1, L.derivWeierstrassP_add_coe, L.derivWeierstrassP_zero]

@[simp]
lemma derivWeierstrassP_sub_coe (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L] (z - l) = ‚Ñò'[L] z := by
  rw [‚Üê L.derivWeierstrassP_add_coe _ l, sub_add_cancel]

/-- `deriv ‚Ñò = ‚Ñò'`. This is true globally because of junk values. -/
@[simp] lemma deriv_weierstrassP : deriv ‚Ñò[L] = ‚Ñò'[L] := by
  ext x
  by_cases hx : x ‚àà L.lattice
  ¬∑ rw [deriv_zero_of_not_differentiableAt, L.derivWeierstrassP_coe ‚ü®x, hx‚ü©]
    exact fun H ‚Ü¶ L.not_continuousAt_weierstrassP x hx H.continuousAt
  ¬∑ rw [‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
      ‚Üê L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (L.œâ‚ÇÅ / 2) (x := x) (by simp [hx])]

end derivWeierstrassP

section AnalyticWeierstrassPExcept

/-- The sum `‚àë (l - x)‚Åª ≥` over `l ‚àà L`. This converges when `2 < r`, see `hasSum_sumInvPow`. -/
def sumInvPow (x : ‚ÑÇ) (r : ‚Ñï) : ‚ÑÇ := ‚àë' l : L.lattice, ((l - x) ^ r)‚Åª¬π

lemma hasSum_sumInvPow (x : ‚ÑÇ) {r : ‚Ñï} (hr : 2 < r) :
    HasSum (fun l : L.lattice ‚Ü¶ ((l - x) ^ r)‚Åª¬π) (L.sumInvPow x r) := by
  refine Summable.hasSum (.of_norm_bounded (ZLattice.summable_norm_sub_zpow _
    (-r) (by simpa) x) fun l ‚Ü¶ ?_)
  rw [‚Üê zpow_natCast, ‚Üê zpow_neg, ‚Üê norm_zpow]

/-- In the power series expansion of `‚Ñò(z) = ‚àë a·µ¢ (z - x)‚Å±` at some `x ‚àâ L`,
each `a·µ¢` can be written as an infinite sum over `l ‚àà L`.
This is the summand of this infinite sum with the `l‚ÇÄ`-th term omitted.
See `PeriodPair.coeff_weierstrassPExceptSeries`. -/
def weierstrassPExceptSummand (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  if l.1 = l‚ÇÄ then 0 else ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

/-- The power series expansion of `‚Ñò[L - l‚ÇÄ]` at `x`.
See `PeriodPair.hasFPowerSeriesOnBall_weierstrassPExcept`. -/
def weierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ if i = 0 then (‚Ñò[L - l‚ÇÄ] x) else (i + 1) *
    (L.sumInvPow x (i + 2) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 2))‚Åª¬π else 0)

lemma coeff_weierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) :
    (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i =
      ‚àë' l : L.lattice, L.weierstrassPExceptSummand l‚ÇÄ x i l := by
  delta weierstrassPExceptSummand weierstrassPExceptSeries
  cases i with
  | zero => simp [weierstrassPExcept, sub_sq_comm x, zpow_ofNat]
  | succ i =>
    split_ifs with hl‚ÇÄ
    ¬∑ trans (i + 2) * (L.sumInvPow x (i + 3) -
        ‚àë' l : L.lattice, if l = ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü© then (l‚ÇÄ - x) ^ (-‚Üë(i + 3) : ‚Ñ§) else 0)
      ¬∑ rw [FormalMultilinearSeries.coeff_ofScalars, tsum_ite_eq, zpow_neg, zpow_natCast]
        simp [add_assoc, one_add_one_eq_two]
      ¬∑ rw [sumInvPow, ‚Üê (hasSum_sumInvPow _ _ (by linarith)).summable.tsum_sub, ‚Üê tsum_mul_left]
        ¬∑ simp_rw [Subtype.ext_iff, zpow_neg]
          congr with l
          split_ifs with e
          ¬∑ simp only [e, zpow_natCast, sub_self, mul_zero]
          ¬∑ dsimp; norm_cast; ring
        ¬∑ exact summable_of_finite_support ((Set.finite_singleton ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©).subset (by simp_all))
    ¬∑ have h‚ÇÅ (l : L.lattice) : l.1 ‚â† l‚ÇÄ := fun e ‚Ü¶ hl‚ÇÄ (e ‚ñ∏ l.2)
      simp [h‚ÇÅ, tsum_mul_left, sumInvPow, add_assoc,
        one_add_one_eq_two, ‚Üê zpow_natCast, -neg_add_rev]

/--
In the power series expansion of `‚Ñò(z) = ‚àë·µ¢ a·µ¢ (z - x)‚Å±` at some `x ‚àâ L`,
each `a·µ¢` can be writen as a sum over `l ‚àà L`, i.e.
`a·µ¢ = ‚àë‚Çó, (i + 1) * (l - x)‚Åª‚Å±‚Åª¬≤` for `i ‚â† 0` and `a‚ÇÄ = ‚àë‚Çó, (l - x)‚Åª¬≤ - l‚Åª¬≤`.

We show that the double sum converges if `z` falls in a ball centered at `x` that doesn't touch `L`.
-/
-- We should be able to skip this computation via some general complex-analytic machinery but
-- they are missing at the moment.
-- Consider refactoring once we have developed more of the missing API.
lemma summable_weierstrassPExceptSummand (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.weierstrassPExceptSummand l‚ÇÄ x b c * (z - x) ^ b) := by
  -- We first find a `Œ∫ > 1`,
  -- such that the ball centered at `x` with radius `Œ∫ * ‚Äñz - x‚Äñ` does not touch `L`.
  obtain ‚ü®Œ∫, hŒ∫, hŒ∫'‚ü© : ‚àÉ Œ∫ : ‚Ñù, 1 < Œ∫ ‚àß ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ * Œ∫ < ‚Äñl - x‚Äñ := by
    obtain ‚ü®Œ∫, hŒ∫, hŒ∫'‚ü© := Metric.isOpen_iff.mp ((continuous_mul_right ‚Äñz - x‚Äñ).isOpen_preimage _
      (isClosedMap_dist x _
      (L.isClosed_of_subset_lattice (Set.diff_subset (t := {l‚ÇÄ})))).upperClosure.isOpen_compl) 1
      (by simpa [Complex.dist_eq, @forall_comm ‚Ñù, norm_sub_rev x] using hx)
    refine ‚ü®Œ∫ / 2 + 1, by simpa, fun l hl ‚Ü¶ ?_‚ü©
    have : ‚àÄ l ‚àà L.lattice, l ‚â† l‚ÇÄ ‚Üí (Œ∫ / 2 + 1) * ‚Äñz - x‚Äñ < dist x l := by
      simpa using @hŒ∫' (Œ∫ / 2 + 1) (by simp [div_lt_iff‚ÇÄ, abs_eq_self.mpr hŒ∫.le, hŒ∫])
    simpa only [Complex.dist_eq, norm_sub_rev x, mul_comm] using this _ l.2 hl
  -- We single out the degree zero term via this equiv.
  let e : ‚Ñï √ó L.lattice ‚âÉ L.lattice ‚äï (‚Ñï √ó L.lattice) :=
    (Equiv.prodCongrLeft fun _ ‚Ü¶ (Denumerable.eqv (Option ‚Ñï)).symm).trans optionProdEquiv
  rw [‚Üê e.symm.summable_iff]
  apply Summable.sum
  ¬∑ -- for the degree zero term, this is the usual summability of the definition of `‚Ñò`.
    simpa [weierstrassPExceptSummand, e, Function.comp_def, Function.uncurry, sub_sq_comm x,
      Denumerable.eqv] using (L.hasSum_weierstrassPExcept l‚ÇÄ x).summable
  ¬∑ -- for the remaining terms, we bound it by `(i + 2) Œ∫‚Åª‚Å± * ‚Äñl - x‚Äñ‚Åª¬≥ * ‚Äñz - x‚Äñ`.
    dsimp [e, Function.comp_def, Function.uncurry_def, Denumerable.eqv, weierstrassPExceptSummand]
    have H‚ÇÅ : Summable fun i : ‚Ñï ‚Ü¶ ((i + 2) * Œ∫ ^ (-i : ‚Ñ§)) := by
      have : |Œ∫‚Åª¬π| < 1 := by grind [abs_inv, inv_lt_one_iff‚ÇÄ]
      simpa [mul_comm] using ((Real.hasFPowerSeriesOnBall_ofScalars_mul_add_zero 1 2).hasSum
        (y := Œ∫‚Åª¬π) (by simpa [enorm_eq_nnnorm])).summable
    have H‚ÇÇ : Summable fun l : L.lattice ‚Ü¶ ‚Äñl - x‚Äñ ^ (-3 : ‚Ñ§) * ‚Äñz - x‚Äñ :=
      (ZLattice.summable_norm_sub_zpow _ _ (by simp) _).mul_right _
    refine (H‚ÇÅ.mul_of_nonneg H‚ÇÇ (by intro; positivity) (by intro; positivity)).of_norm_bounded ?_
    intro p
    split_ifs with hp
    ¬∑ simp only [zero_mul, norm_zero, zpow_neg, zpow_natCast, Int.reduceNeg]; positivity
    have hpx : ‚Äñp.2 - x‚Äñ ‚â† 0 := fun h ‚Ü¶ by
      obtain rfl : p.2 = x := by simpa [sub_eq_zero] using h
      simpa [(norm_nonneg _).not_gt] using hx p.2 hp
    obtain rfl | hxz := eq_or_ne z x
    ¬∑ simp
    calc
      _ = ‚Äñ(p.1 + 2 : ‚ÑÇ)‚Äñ * ‚Äñp.2 - x‚Äñ ^ (-3 - p.1 : ‚Ñ§) * ‚Äñz - x‚Äñ ^ (p.1 + 1) := by
        norm_num; ring_nf; simp
      _ = ‚Äñ(p.1 + 2 : ‚ÑÇ)‚Äñ * ((‚Äñ‚Üëp.2 - x‚Äñ / ‚Äñz - x‚Äñ) ^ p.1)‚Åª¬π * ((‚Äñp.2 - x‚Äñ ^ 3)‚Åª¬π * ‚Äñz - x‚Äñ) := by
        simp [hpx, zpow_sub‚ÇÄ, div_pow]; field
      _ ‚â§ (p.1 + 2) * (Œ∫ ^ p.1)‚Åª¬π * ((‚Äñp.2 - x‚Äñ ^ 3)‚Åª¬π * ‚Äñz - x‚Äñ) := by
        gcongr
        ¬∑ norm_cast
        ¬∑ exact (le_div_iff‚ÇÄ (by simpa [sub_eq_zero])).mpr ((mul_comm _ _).trans_le (hŒ∫' p.2 hp).le)
      _ = _ := by simp [zpow_ofNat]

lemma weierstrassPExcept_eq_tsum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    ‚Ñò[L - l‚ÇÄ] z = ‚àë' i : ‚Ñï, (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i := by
  trans ‚àë' (l : L.lattice) (i : ‚Ñï), if l.1 = l‚ÇÄ then 0 else
      ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0) * (z - x) ^ i
  ¬∑ delta weierstrassPExcept
    congr 1 with l
    split_ifs with h
    ¬∑ simp
    simpa [mul_comm] using ((Complex.one_div_sub_sq_sub_one_div_sq_hasFPowerSeriesOnBall_zero l x
      (by simpa [sub_eq_zero] using (norm_nonneg _).trans_lt (hx l h))).hasSum (y := z - x)
      (by simpa [enorm_eq_nnnorm] using hx _ h)).tsum_eq.symm
  trans ‚àë' (l : ‚Ü•L.lattice) (i : ‚Ñï), L.weierstrassPExceptSummand l‚ÇÄ x i l * (z - x) ^ i
  ¬∑ simp only [weierstrassPExceptSummand, ite_mul, zero_mul]
  ¬∑ simp_rw [coeff_weierstrassPExceptSeries, ‚Üê tsum_mul_right]
    apply Summable.tsum_comm
    exact L.summable_weierstrassPExceptSummand l‚ÇÄ z x hx

lemma weierstrassPExceptSeries_hasSum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i) (‚Ñò[L - l‚ÇÄ] z) := by
  refine (Summable.hasSum_iff ?_).mpr (L.weierstrassPExcept_eq_tsum l‚ÇÄ z x hx).symm
  simp_rw [coeff_weierstrassPExceptSeries, ‚Üê tsum_mul_right]
  exact (L.summable_weierstrassPExceptSummand l‚ÇÄ z x hx).prod

lemma hasFPowerSeriesOnBall_weierstrassPExcept (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò[L - l‚ÇÄ] (L.weierstrassPExceptSeries l‚ÇÄ x) x r := by
  constructor
  ¬∑ apply FormalMultilinearSeries.le_radius_of_tendsto (l := 0)
    convert tendsto_norm.comp (L.weierstrassPExceptSeries_hasSum l‚ÇÄ (x + r) x
      ?_).summable.tendsto_atTop_zero using 2 with i
    ¬∑ simp
    ¬∑ simp
    ¬∑ intro l hl
      simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©
  ¬∑ exact ENNReal.coe_pos.mpr hr0
  ¬∑ intro z hz
    replace hz : ‚Äñz‚Äñ < r := by simpa using hz
    have := L.weierstrassPExceptSeries_hasSum l‚ÇÄ (x + z) x
    simp only [add_sub_cancel_left] at this
    convert this (fun l hl ‚Ü¶ hz.trans (by simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©)) with i
    rw [weierstrassPExceptSeries, FormalMultilinearSeries.ofScalars_apply_eq,
      FormalMultilinearSeries.coeff_ofScalars, smul_eq_mul]

lemma hasFPowerSeriesAt_weierstrassPExcept (l : ‚ÑÇ) :
    HasFPowerSeriesAt ‚Ñò[L - l] (.ofScalars (ùïú := ‚ÑÇ) ‚ÑÇ fun i : ‚Ñï ‚Ü¶
      if i = 0 then ‚Ñò[L - l] l else (i + 1) * L.sumInvPow l (i + 2)) l := by
  obtain ‚ü®r, h‚ÇÅ, h‚ÇÇ‚ü© := Metric.nhds_basis_closedBall.mem_iff.mp
    (L.compl_lattice_diff_singleton_mem_nhds l)
  lift r to NNReal using h‚ÇÅ.le
  simpa [weierstrassPExceptSeries] using
    (L.hasFPowerSeriesOnBall_weierstrassPExcept l l r h‚ÇÅ h‚ÇÇ).hasFPowerSeriesAt

lemma analyticOnNhd_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) : AnalyticOnNhd ‚ÑÇ ‚Ñò[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú :=
  (L.differentiableOn_weierstrassPExcept l‚ÇÄ).analyticOnNhd L.isOpen_compl_lattice_diff

@[fun_prop]
lemma analyticAt_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) : AnalyticAt ‚ÑÇ ‚Ñò[L - l‚ÇÄ] l‚ÇÄ :=
  L.analyticOnNhd_weierstrassPExcept _ _ (by simp)

attribute [local simp] Nat.factorial_ne_zero in
lemma iteratedDeriv_weierstrassPExcept_self (l : ‚ÑÇ) {n : ‚Ñï} :
    iteratedDeriv n ‚Ñò[L - l] l =
      if n = 0 then ‚Ñò[L - l] l else (n + 1)! * L.sumInvPow l (n + 2) := by
  rw [‚Üê div_mul_cancel‚ÇÄ (a := iteratedDeriv _ _ _) (b := ‚Üën !) (by simp),
    ‚Üê eq_div_iff_mul_eq (by simp)]
  trans if n = 0 then ‚Ñò[L - l] l else (n + 1) * L.sumInvPow l (n + 2)
  ¬∑ simpa using congr($((L.analyticAt_weierstrassPExcept l).hasFPowerSeriesAt
      |>.eq_formalMultilinearSeries (L.hasFPowerSeriesAt_weierstrassPExcept l)).coeff n)
  ¬∑ cases n <;> simp [Nat.factorial_succ]; field

end AnalyticWeierstrassPExcept

section AnalyticderivWeierstrassPExcept

/-- The power series expansion of `‚Ñò'[L - l‚ÇÄ]` at `x`.
See `PeriodPair.hasFPowerSeriesOnBall_derivWeierstrassPExcept`. -/
def derivWeierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ (i + 1) * (i + 2) *
    (L.sumInvPow x (i + 3) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 3))‚Åª¬π else 0)

lemma hasFPowerSeriesOnBall_derivWeierstrassPExcept (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò'[L - l‚ÇÄ] (L.derivWeierstrassPExceptSeries l‚ÇÄ x) x r := by
  refine .congr ?_
    ((L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept l‚ÇÄ).mono (.trans ?_ hr))
  ¬∑ have := (L.hasFPowerSeriesOnBall_weierstrassPExcept l‚ÇÄ x r hr0 hr).fderiv
    convert (ContinuousLinearMap.apply ‚ÑÇ ‚ÑÇ (1 : ‚ÑÇ)).comp_hasFPowerSeriesOnBall this
    ext n
    simp [weierstrassPExceptSeries, derivWeierstrassPExceptSeries]
    ring
  ¬∑ simpa using Metric.ball_subset_closedBall

lemma hasFPowerSeriesAt_derivWeierstrassPExcept (l : ‚ÑÇ) :
    HasFPowerSeriesAt ‚Ñò'[L - l]
      (.ofScalars ‚ÑÇ fun i ‚Ü¶ (i + 1) * (i + 2) * L.sumInvPow l (i + 3)) l := by
  obtain ‚ü®r, h‚ÇÅ, h‚ÇÇ‚ü© := Metric.nhds_basis_closedBall.mem_iff.mp
    (L.compl_lattice_diff_singleton_mem_nhds l)
  simpa [derivWeierstrassPExceptSeries] using
    (L.hasFPowerSeriesOnBall_derivWeierstrassPExcept l l ‚ü®r, h‚ÇÅ.le‚ü© h‚ÇÅ h‚ÇÇ).hasFPowerSeriesAt

lemma analyticOnNhd_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    AnalyticOnNhd ‚ÑÇ ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú :=
  (L.differentiableOn_derivWeierstrassPExcept l‚ÇÄ).analyticOnNhd L.isOpen_compl_lattice_diff

@[fun_prop]
lemma analyticAt_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    AnalyticAt ‚ÑÇ ‚Ñò'[L - l‚ÇÄ] l‚ÇÄ :=
  L.analyticOnNhd_derivWeierstrassPExcept l‚ÇÄ _ (by simp)

lemma iteratedDeriv_derivWeierstrassPExcept_self (l : ‚ÑÇ) {n : ‚Ñï} :
    iteratedDeriv n ‚Ñò'[L - l] l = (n + 2)! * L.sumInvPow l (n + 3) := by
  have : iteratedDeriv n ‚Ñò'[L - l] l / n ! = (‚Üën + 1) * (‚Üën + 2) * L.sumInvPow l (n + 3) := by
    simpa using congr($((L.analyticAt_derivWeierstrassPExcept l).hasFPowerSeriesAt
      |>.eq_formalMultilinearSeries (L.hasFPowerSeriesAt_derivWeierstrassPExcept l)).coeff n)
  simp [div_eq_iff, Nat.factorial_ne_zero, Nat.factorial_succ] at this ‚ä¢
  grind

@[simp]
lemma deriv_derivWeierstrassPExcept_self (l : ‚ÑÇ) :
    deriv ‚Ñò'[L - l] l = 6 * L.sumInvPow l 4 := by
  simpa using L.iteratedDeriv_derivWeierstrassPExcept_self l (n := 1)

lemma analyticOnNhd_derivWeierstrassP : AnalyticOnNhd ‚ÑÇ ‚Ñò'[L] L.lattice·∂ú :=
  L.differentiableOn_derivWeierstrassP.analyticOnNhd L.isClosed_lattice.isOpen_compl

end AnalyticderivWeierstrassPExcept

section Analytic

/-- In the power series expansion of `‚Ñò(z) = ‚àë a·µ¢z‚Å±` at some `x ‚àâ L`,
each `a·µ¢` can be written as an infinite sum over `l ‚àà L`.
This is the summand of this infinite sum. See `PeriodPair.coeff_weierstrassPSeries`. -/
def weierstrassPSummand (x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

/-- The power series expansion of `‚Ñò` at `x`.
See `PeriodPair.hasFPowerSeriesOnBall_weierstrassP`. -/
def weierstrassPSeries (x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  .ofScalars _ fun i ‚Ü¶ if i = 0 then (‚Ñò[L] x) else (i + 1) * L.sumInvPow x (i + 2)

lemma weierstrassPExceptSeries_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.weierstrassPExceptSeries l‚ÇÄ = L.weierstrassPSeries := by
  delta weierstrassPSeries weierstrassPExceptSeries
  congr! with z i f
  ¬∑ rw [L.weierstrassPExcept_of_notMem _ hl‚ÇÄ]
  ¬∑ simp [hl‚ÇÄ]

lemma weierstrassPExceptSummand_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.weierstrassPExceptSummand l‚ÇÄ = L.weierstrassPSummand := by
  grind [weierstrassPSummand, weierstrassPExceptSummand]

lemma coeff_weierstrassPSeries (x : ‚ÑÇ) (i : ‚Ñï) :
    (L.weierstrassPSeries x).coeff i = ‚àë' l : L.lattice, L.weierstrassPSummand x i l := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    L.coeff_weierstrassPExceptSeries,
    ‚Üê L.weierstrassPExceptSummand_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma summable_weierstrassPSummand (z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.weierstrassPSummand x b c * (z - x) ^ b) := by
  simp_rw [‚Üê L.weierstrassPExceptSummand_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  refine L.summable_weierstrassPExceptSummand _ z x fun l hl ‚Ü¶ hx l

lemma weierstrassPSeries_hasSum (z x : ‚ÑÇ) (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.weierstrassPSeries x).coeff i * (z - x) ^ i) (‚Ñò[L] z) := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    ‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  exact L.weierstrassPExceptSeries_hasSum _ z x fun l hl ‚Ü¶ hx l

lemma hasFPowerSeriesOnBall_weierstrassP (x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ L.lattice·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò[L] (L.weierstrassPSeries x) x r := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    ‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  exact L.hasFPowerSeriesOnBall_weierstrassPExcept _ x r hr0
    (hr.trans (Set.compl_subset_compl.mpr Set.diff_subset))

lemma analyticOnNhd_weierstrassP : AnalyticOnNhd ‚ÑÇ ‚Ñò[L] L.lattice·∂ú :=
  L.differentiableOn_weierstrassP.analyticOnNhd L.isClosed_lattice.isOpen_compl

lemma ite_eq_one_sub_sq_mul_weierstrassP (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (z : ‚ÑÇ) :
    (if z = l‚ÇÄ then 1 else (z - l‚ÇÄ) ^ 2 * ‚Ñò[L] z) =
      (z - l‚ÇÄ) ^ 2 * ‚Ñò[L - l‚ÇÄ] z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2 := by
  grind [L.weierstrassPExcept_add ‚ü®_, hl‚ÇÄ‚ü©]

@[fun_prop]
lemma meromorphic_weierstrassP : Meromorphic ‚Ñò[L] := by
  intro x
  by_cases hx : x ‚àà L.lattice
  ¬∑ simp_rw [‚Üê funext <| L.weierstrassPExcept_add ‚ü®x, hx‚ü©]
    have := (analyticOnNhd_weierstrassPExcept L x x (by simp)).meromorphicAt
    fun_prop
  ¬∑ exact (L.analyticOnNhd_weierstrassP x hx).meromorphicAt

@[fun_prop]
lemma meromorphic_derivWeierstrassP : Meromorphic ‚Ñò'[L] := by
  rw [‚Üê deriv_weierstrassP]
  fun_prop

lemma order_weierstrassP (l‚ÇÄ : ‚ÑÇ) (h : l‚ÇÄ ‚àà L.lattice) :
    meromorphicOrderAt ‚Ñò[L] l‚ÇÄ = -2 := by
  trans ‚Üë(-2 : ‚Ñ§)
  ¬∑ rw [meromorphicOrderAt_eq_int_iff (L.meromorphic_weierstrassP l‚ÇÄ)]
    refine ‚ü®fun z ‚Ü¶ (z - l‚ÇÄ) ^ 2 * ‚Ñò[L - l‚ÇÄ] z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2, ?_, ?_, ?_‚ü©
    ¬∑ have : AnalyticAt ‚ÑÇ ‚Ñò[L - l‚ÇÄ] l‚ÇÄ := L.analyticOnNhd_weierstrassPExcept l‚ÇÄ l‚ÇÄ (by simp)
      suffices AnalyticAt ‚ÑÇ (fun z ‚Ü¶ (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2) l‚ÇÄ by fun_prop
      by_cases hl‚ÇÄ : l‚ÇÄ = 0
      ¬∑ simpa [hl‚ÇÄ] using analyticAt_const
      ¬∑ fun_prop (disch := simpa)
    ¬∑ simp
    ¬∑ filter_upwards [self_mem_nhdsWithin] with z (hz : _ ‚â† _)
      have : (z - l‚ÇÄ) ^ 2 ‚â† 0 := by simpa [sub_eq_zero]
      simp [‚Üê L.ite_eq_one_sub_sq_mul_weierstrassP l‚ÇÄ h,
        if_neg hz, inv_mul_cancel_left‚ÇÄ this, zpow_ofNat]
  ¬∑ norm_num

end Analytic

section Relation

/-- The Eisenstein series as a function on lattices.
It takes `L` to the sum `‚àë l‚Åª ≥` over `l ‚àà L`.
TODO: Establish connections with the `ModularForm` library. -/
def G (n : ‚Ñï) : ‚ÑÇ := ‚àë' l : L.lattice, (l ^ n)‚Åª¬π

@[simp]
lemma sumInvPow_zero : L.sumInvPow 0 = L.G := by
  ext; simp [sumInvPow, G]

lemma G_eq_zero_of_odd (n : ‚Ñï) (hn : Odd n) : L.G n = 0 := by
  rw [‚Üê CharZero.eq_neg_self_iff, G, ‚Üê tsum_neg, ‚Üê (Equiv.neg _).tsum_eq]
  congr with l
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, neg_inv, hn.neg_pow]

/-- The lattice invariant `g‚ÇÇ := 60 G‚ÇÑ`. -/
def g‚ÇÇ : ‚ÑÇ := 60 * L.G 4

/-- The lattice invariant `g‚ÇÉ := 140 G‚ÇÜ`. -/
def g‚ÇÉ : ‚ÑÇ := 140 * L.G 6

/-- (Implementation detail) The relation that `‚Ñò'` and `‚Ñò` satisfies.
We will show that this is constant zero. See `PeriodPair.relation_eq_zero` -/
private def relation (z : ‚ÑÇ) : ‚ÑÇ :=
  letI := Classical.propDecidable
  if z ‚àà L.lattice then 0 else ‚Ñò'[L] z ^ 2 - 4 * ‚Ñò[L] z ^ 3 + L.g‚ÇÇ * ‚Ñò[L] z + L.g‚ÇÉ

@[local fun_prop]
private lemma meromorphic_relation : Meromorphic L.relation := by
  have : Meromorphic fun z ‚Ü¶ ‚Ñò'[L] z ^ 2 - 4 * ‚Ñò[L] z ^ 3 + L.g‚ÇÇ * ‚Ñò[L] z + L.g‚ÇÉ := by fun_prop
  refine fun z ‚Ü¶ (this _).congr ?_
  filter_upwards [self_mem_nhdsWithin, mem_nhdsWithin_of_mem_nhds
    (L.compl_lattice_diff_singleton_mem_nhds _)] with w hw hw'
  rw [relation, if_neg (by simp_all)]

private lemma relation_mul_id_pow_six_eventuallyEq :
    (L.relation * id ^ 6) =·∂†[nhds 0] fun z ‚Ü¶
      (‚Ñò'[L - (0 : ‚ÑÇ)] z * z ^ 3 - 2) ^ 2 - 4 *
      (‚Ñò[L - (0 : ‚ÑÇ)] z * z ^ 2 + 1) ^ 3 + L.g‚ÇÇ *
      (‚Ñò[L - (0 : ‚ÑÇ)] z * z ^ 6 + z ^ 4) + L.g‚ÇÉ * z ^ 6 := by
  filter_upwards [L.compl_lattice_diff_singleton_mem_nhds _] with z hz
  by_cases hz0 : z = 0
  ¬∑ simp [hz0, relation]; norm_num
  replace hz : z ‚àâ L.lattice := by simp_all
  simp only [Pi.mul_apply, Pi.pow_apply, relation, ‚ÜìreduceIte, hz,
    ‚Üê ZeroMemClass.coe_zero L.lattice, L.derivWeierstrassPExcept_def, L.weierstrassPExcept_def]
  simp
  field

@[local fun_prop]
private lemma analyticAt_relation_mul_id_pow_six :
    AnalyticAt ‚ÑÇ (L.relation * id ^ 6) 0 := by
  refine .congr ?_ L.relation_mul_id_pow_six_eventuallyEq.symm
  fun_prop

@[local simp]
private lemma relation_neg (x) : L.relation (-x) = L.relation x := by
  classical simp [relation]

attribute [local fun_prop] AnalyticAt.contDiffAt in
private lemma iteratedDeriv_six_relation_mul_id_pow_six :
    iteratedDeriv 6 (L.relation * id ^ 6) 0 = 0 := by
  rw [L.relation_mul_id_pow_six_eventuallyEq.iteratedDeriv_eq]
  simp_rw [pow_succ (_ + _), pow_succ (_ - _), pow_zero, one_mul]
  simp (discharger := fun_prop) only [iteratedDeriv_fun_add, iteratedDeriv_fun_sub,
    iteratedDeriv_fun_mul, iteratedDeriv_const, iteratedDeriv_fun_pow_zero,
    iteratedDeriv_derivWeierstrassPExcept_self, iteratedDeriv_weierstrassPExcept_self]
  simp [Finset.sum_range_succ, L.G_eq_zero_of_odd 3 (by decide), g‚ÇÉ,
    show Nat.choose 6 4 = 15 by rfl, show Nat.choose 6 3 = 20 by rfl]
  ring

attribute [local fun_prop] AnalyticAt.contDiffAt in
private lemma analyticAt_relation_zero : AnalyticAt ‚ÑÇ L.relation 0 := by
  refine .of_meromorphicOrderAt_pos (one_pos.trans_le ?_) (by simp [relation])
  suffices 7 ‚â§ meromorphicOrderAt (L.relation * id ^ 6) 0 by
    rw [meromorphicOrderAt_mul (by fun_prop) (by fun_prop),
      meromorphicOrderAt_pow (by fun_prop)] at this
    rw [‚Üê WithTop.add_le_add_iff_right (z := 6) (by simp)]
    simpa [-add_le_add_iff_left_of_ne_top] using this
  rw [AnalyticAt.meromorphicOrderAt_eq (by fun_prop)]
  refine ENat.monotone_map_iff.mpr Nat.mono_cast
    ((natCast_le_analyticOrderAt_iff_iteratedDeriv_eq_zero (by fun_prop)).mpr fun i hi‚ÇÅ ‚Ü¶ ?_)
  by_cases hi‚ÇÇ : Odd i
  ¬∑ simpa [‚Üê CharZero.eq_neg_self_iff, hi‚ÇÇ, (show Even 6 by decide).neg_pow] using
      (iteratedDeriv_comp_neg i (L.relation * id ^ 6) 0 :)
  by_cases hi‚ÇÉ : i = 0
  ¬∑ simp [hi‚ÇÉ]
  by_cases hi‚ÇÑ : i = 6
  ¬∑ exact hi‚ÇÑ ‚ñ∏ L.iteratedDeriv_six_relation_mul_id_pow_six
  rw [L.relation_mul_id_pow_six_eventuallyEq.iteratedDeriv_eq]
  simp_rw [pow_succ (_ + _), pow_succ (_ - _), pow_zero, one_mul]
  simp (discharger := fun_prop) only [iteratedDeriv_fun_add, iteratedDeriv_fun_sub,
    iteratedDeriv_fun_mul, iteratedDeriv_const, iteratedDeriv_fun_pow_zero,
    iteratedDeriv_derivWeierstrassPExcept_self, iteratedDeriv_weierstrassPExcept_self]
  obtain rfl | rfl : i = 2 ‚à® i = 4 := by grind
  ¬∑ simp [Finset.sum_range_succ]
  ¬∑ simp [Finset.sum_range_succ, show Nat.choose 4 2 = 6 by rfl, g‚ÇÇ]; ring

@[local simp]
private lemma relation_add_coe (x : ‚ÑÇ) (l : L.lattice) :
    L.relation (x + l) = L.relation x := by
  simp only [relation, derivWeierstrassP_add_coe, weierstrassP_add_coe]
  congr 1
  simpa using (L.lattice.toAddSubgroup.add_mem_cancel_right (y := x) l.2)

@[local simp]
private lemma relation_sub_coe (x : ‚ÑÇ) (l : L.lattice) :
    L.relation (x - l) = L.relation x := by
  rw [‚Üê L.relation_add_coe _ l, sub_add_cancel]

private lemma analyticAt_relation (x : ‚ÑÇ) : AnalyticAt ‚ÑÇ L.relation x := by
  by_cases hx : x ‚àà L.lattice
  ¬∑ lift x to L.lattice using hx
    have := L.analyticAt_relation_zero
    rw [‚Üê sub_self x.1] at this
    convert this.comp (f := (¬∑ - x.1)) (by fun_prop)
    ext a
    simp
  ¬∑ have : AnalyticAt ‚ÑÇ (fun z ‚Ü¶ ‚Ñò'[L] z ^ 2 - 4 * ‚Ñò[L] z ^ 3 + L.g‚ÇÇ * ‚Ñò[L] z + L.g‚ÇÉ) x := by
      have := L.analyticOnNhd_derivWeierstrassP _ hx
      have := L.analyticOnNhd_weierstrassP _ hx
      fun_prop (disch := assumption)
    apply this.congr
    filter_upwards [L.isClosed_lattice.isOpen_compl.mem_nhds hx] with x hx
    simp_all [relation]

private lemma relation_eq_zero : L.relation = 0 := by
  ext x
  have : Differentiable ‚ÑÇ L.relation := fun x ‚Ü¶ (L.analyticAt_relation x).differentiableAt
  exact (this.apply_eq_apply_of_bounded (IsZLattice.isCompact_range_of_periodic L.lattice _
    this.continuous fun z w hw ‚Ü¶ by lift w to L.lattice using hw; simp).isBounded x 0).trans
    (if_pos (by simp))

/-- `‚Ñò'(z)¬≤ = 4 ‚Ñò(z)¬≥ - g‚ÇÇ ‚Ñò(z) - g‚ÇÉ` -/
lemma derivWeierstrassP_sq (z : ‚ÑÇ) (hz : z ‚àâ L.lattice) :
    ‚Ñò'[L] z ^ 2 = 4 * ‚Ñò[L] z ^ 3 - L.g‚ÇÇ * ‚Ñò[L] z - L.g‚ÇÉ := by
  simpa [sub_eq_zero, relation, hz, sub_add] using congr($L.relation_eq_zero z)

end Relation

end PeriodPair
