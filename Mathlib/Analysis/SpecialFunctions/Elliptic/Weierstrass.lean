/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.Algebra.Module.ZLattice.Summable
public import Mathlib.Analysis.Complex.LocallyUniformLimit
public import Mathlib.LinearAlgebra.Complex.FiniteDimensional
public import Mathlib.Topology.Algebra.InfiniteSum.UniformOn
public import Mathlib.Topology.MetricSpace.ProperSpace.Lemmas
public import Mathlib.Analysis.Normed.Module.Connected
public import Mathlib.Analysis.Analytic.Binomial
public import Mathlib.Analysis.Meromorphic.Order

/-!

# Weierstrass `â„˜` functions

## Main definitions and results
- `PeriodPair.weierstrassP`: The Weierstrass `â„˜`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_weierstrassP`:
  The summands of `â„˜` sums to `â„˜` locally uniformly.
- `PeriodPair.differentiableOn_weierstrassP`: `â„˜` is differentiable away from the lattice points.
- `PeriodPair.weierstrassP_add_coe`: The Weierstrass `â„˜`-function is periodic.
- `PeriodPair.weierstrassP_neg`: The Weierstrass `â„˜`-function is even.

- `PeriodPair.derivWeierstrassP`:
  The derivative of the Weierstrass `â„˜`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_derivWeierstrassP`:
  The summands of `â„˜'` sums to `â„˜'` locally uniformly.
- `PeriodPair.differentiableOn_derivWeierstrassP`:
  `â„˜'` is differentiable away from the lattice points.
- `PeriodPair.derivWeierstrassP_add_coe`: `â„˜'` is periodic.
- `PeriodPair.weierstrassP_neg`: `â„˜'` is odd.
- `PeriodPair.deriv_weierstrassP`: `deriv â„˜ = â„˜'`. This is true globally because of junk values.
- `PeriodPair.analyticOnNhd_weierstrassP`: `â„˜` is analytic away from the lattice points.
- `PeriodPair.meromorphic_weierstrassP`: `â„˜` is meromorphic on the whole plane.
- `PeriodPair.order_weierstrassP`: `â„˜` has a pole of order 2 at each of the lattice points.

## tags

Weierstrass p-functions, Weierstrass p functions

-/

@[expose] public section

open Module

noncomputable section

/-- A pair of `â„`-linearly independent complex numbers.
They span the period lattice in `lattice`,
and are the periods of the elliptic functions we shall construct. -/
structure PeriodPair : Type where
  /-- The first period in a `PeriodPair`. -/
  Ï‰â‚ : â„‚
  /-- The second period in a `PeriodPair`. -/
  Ï‰â‚‚ : â„‚
  indep : LinearIndependent â„ ![Ï‰â‚, Ï‰â‚‚]

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] (L : PeriodPair)

namespace PeriodPair

/-- The `â„`-basis of `â„‚` determined by a pair of periods. -/
protected def basis : Basis (Fin 2) â„ â„‚ :=
  basisOfLinearIndependentOfCardEqFinrank L.indep (by simp)

@[simp] lemma basis_zero : L.basis 0 = L.Ï‰â‚ := by simp [PeriodPair.basis]
@[simp] lemma basis_one : L.basis 1 = L.Ï‰â‚‚ := by simp [PeriodPair.basis]

/-- The lattice spanned by a pair of periods. -/
def lattice : Submodule â„¤ â„‚ := Submodule.span â„¤ {L.Ï‰â‚, L.Ï‰â‚‚}

lemma mem_lattice {L : PeriodPair} {x : â„‚} :
    x âˆˆ L.lattice â†” âˆƒ m n : â„¤, m * L.Ï‰â‚ + n * L.Ï‰â‚‚ = x := by
  simp only [lattice, Submodule.mem_span_pair, zsmul_eq_mul]

lemma Ï‰â‚_mem_lattice : L.Ï‰â‚ âˆˆ L.lattice := Submodule.subset_span (by simp)
lemma Ï‰â‚‚_mem_lattice : L.Ï‰â‚‚ âˆˆ L.lattice := Submodule.subset_span (by simp)

lemma mul_Ï‰â‚_add_mul_Ï‰â‚‚_mem_lattice {L : PeriodPair} {Î± Î² : â„š} :
    Î± * L.Ï‰â‚ + Î² * L.Ï‰â‚‚ âˆˆ L.lattice â†” Î±.den = 1 âˆ§ Î².den = 1 := by
  refine âŸ¨fun H â†¦ ?_, fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ ?_âŸ©
  Â· obtain âŸ¨m, n, eâŸ© := mem_lattice.mp H
    have := LinearIndependent.pair_iff.mp L.indep (m - Î±) (n - Î²)
      (by simpa using by linear_combination e)
    simp only [sub_eq_zero] at this
    norm_cast at this
    aesop
  Â· lift Î± to â„¤ using hâ‚
    lift Î² to â„¤ using hâ‚‚
    simp only [Rat.cast_intCast, â† zsmul_eq_mul]
    exact add_mem (Submodule.smul_mem _ _ L.Ï‰â‚_mem_lattice)
      (Submodule.smul_mem _ _ L.Ï‰â‚‚_mem_lattice)

lemma Ï‰â‚_div_two_notMem_lattice : L.Ï‰â‚ / 2 âˆ‰ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_Ï‰â‚_add_mul_Ï‰â‚‚_mem_lattice (Î± := 1 / 2) (Î² := 0)).not.mpr (by norm_num)

lemma Ï‰â‚‚_div_two_notMem_lattice : L.Ï‰â‚‚ / 2 âˆ‰ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_Ï‰â‚_add_mul_Ï‰â‚‚_mem_lattice (Î± := 0) (Î² := 1 / 2)).not.mpr (by norm_num)

-- helper lemma to connect to the ZLattice API
lemma lattice_eq_span_range_basis :
    L.lattice = Submodule.span â„¤ (Set.range L.basis) := by
  have : Finset.univ (Î± := Fin 2) = {0, 1} := rfl
  rw [lattice, â† Set.image_univ, â† Finset.coe_univ, this]
  simp [Set.image_insert_eq]

instance : DiscreteTopology L.lattice := L.lattice_eq_span_range_basis â–¸ inferInstance

instance : IsZLattice â„ L.lattice := by
  simp_rw [L.lattice_eq_span_range_basis]
  infer_instance

lemma isClosed_lattice : IsClosed (X := â„‚) L.lattice :=
  @AddSubgroup.isClosed_of_discrete _ _ _ _ _ L.lattice.toAddSubgroup
    (inferInstanceAs (DiscreteTopology L.lattice))

lemma isClosed_of_subset_lattice {s : Set â„‚} (hs : s âŠ† L.lattice) : IsClosed s := by
  convert L.isClosed_lattice.isClosedMap_subtype_val _
    (isClosed_discrete (Î± := L.lattice) ((â†‘) â»Â¹' s))
  convert Set.image_preimage_eq_inter_range.symm using 1
  simpa

lemma isOpen_compl_lattice_diff {s : Set â„‚} : IsOpen (L.lattice \ s)á¶œ :=
  (L.isClosed_of_subset_lattice Set.diff_subset).isOpen_compl

instance : ProperSpace L.lattice := .of_isClosed L.isClosed_lattice

/-- The `â„¤`-basis of the lattice determined by a pair of periods. -/
def latticeBasis : Basis (Fin 2) â„¤ L.lattice :=
  (Basis.span (v := ![L.Ï‰â‚, L.Ï‰â‚‚]) (L.indep.restrict_scalars' _)).map
    (.ofEq _ _ (by simp [lattice, Set.pair_comm L.Ï‰â‚‚ L.Ï‰â‚]))

@[simp] lemma latticeBasis_zero : L.latticeBasis 0 = L.Ï‰â‚ := by simp [latticeBasis]
@[simp] lemma latticeBasis_one : L.latticeBasis 1 = L.Ï‰â‚‚ := by simp [latticeBasis]

@[simp] lemma finrank_lattice : finrank â„¤ L.lattice = 2 := finrank_eq_card_basis L.latticeBasis

/-- The equivalence from the lattice generated by a pair of periods to `â„¤ Ã— â„¤`. -/
def latticeEquivProd : L.lattice â‰ƒâ‚—[â„¤] â„¤ Ã— â„¤ :=
  L.latticeBasis.repr â‰ªâ‰«â‚— Finsupp.linearEquivFunOnFinite _ _ _ â‰ªâ‰«â‚— .finTwoArrow â„¤ â„¤

lemma latticeEquiv_symm_apply (x : â„¤ Ã— â„¤) :
    (L.latticeEquivProd.symm x).1 = x.1 * L.Ï‰â‚ + x.2 * L.Ï‰â‚‚ := by
  simp [latticeEquivProd, Finsupp.linearCombination]
  rfl

open Topology Filter in
lemma hasSumLocallyUniformly_aux (f : L.lattice â†’ â„‚ â†’ â„‚)
    (u : â„ â†’ L.lattice â†’ â„) (hu : âˆ€ r > 0, Summable (u r))
    (hf : âˆ€ r > 0, âˆ€á¶  R in atTop, âˆ€ x, â€–xâ€– < r â†’ âˆ€ l : L.lattice, â€–l.1â€– = R â†’ â€–f l xâ€– â‰¤ u r l) :
    HasSumLocallyUniformly f (âˆ‘' j, f j Â·) := by
  rw [hasSumLocallyUniformly_iff_tendstoLocallyUniformly, tendstoLocallyUniformly_iff_filter]
  intro x
  obtain âŸ¨r, hr, hr'âŸ© : âˆƒ r, 0 < r âˆ§ ğ“ x â‰¤ ğ“Ÿ (Metric.ball 0 r) :=
    âŸ¨â€–xâ€– + 1, by positivity, Filter.le_principal_iff.mpr (Metric.isOpen_ball.mem_nhds (by simp))âŸ©
  refine .mono_right ?_ hr'
  rw [â† tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  refine tendstoUniformlyOn_tsum_of_cofinite_eventually (hu r hr) ?_
  obtain âŸ¨R, hRâŸ© := eventually_atTop.mp (hf r hr)
  refine (isCompact_iff_finite.mp (isCompact_closedBall (0 : L.lattice) R)).subset ?_
  intros l hl
  obtain âŸ¨s, hs, hs'âŸ© : âˆƒ x, â€–xâ€– < r âˆ§ u r l < â€–f l xâ€– := by simpa using hl
  simp only [Metric.mem_closedBall, dist_zero_right, AddSubgroupClass.coe_norm]
  contrapose! hs'
  exact hR _ hs'.le _ hs _ rfl

-- Only the asymptotics matter and `10` is just a convenient constant to pick.
lemma weierstrassP_bound (r : â„) (hr : 0 < r) (s : â„‚) (hs : â€–sâ€– < r) (l : â„‚) (h : 2 * r â‰¤ â€–lâ€–) :
    â€–1 / (s - l) ^ 2 - 1 / l ^ 2â€– â‰¤ 10 * r * â€–lâ€– ^ (-3 : â„) := by
  have : s â‰  â†‘l := by rintro rfl; linarith
  have : 0 < â€–lâ€– := by linarith
  calc
    _ = â€–(â†‘l ^ 2 - (s - â†‘l) ^ 2) / ((s - â†‘l) ^ 2 * â†‘l ^ 2)â€– := by
      rw [div_sub_div, one_mul, mul_one]
      Â· simpa [sub_eq_zero]
      Â· simpa
    _ = â€–l ^ 2 - (s - l) ^ 2â€– / (â€–s - lâ€– ^ 2 * â€–lâ€– ^ 2) := by simp
    _ â‰¤ â€–l ^ 2 - (s - l) ^ 2â€– / ((â€–lâ€– / 2) ^ 2 * â€–lâ€– ^ 2) := by
      gcongr
      rw [norm_sub_rev]
      exact .trans (by linarith) (norm_sub_norm_le l s)
    _ = â€–s * (2 * l - s)â€– / (â€–lâ€– ^ 4 / 4) := by
      congr 1
      Â· rw [sq_sub_sq]; simp [â† sub_add, two_mul, sub_add_eq_add_sub]
      Â· ring
    _ = (â€–sâ€– * â€–2 * l - sâ€–) / (â€–lâ€– ^ 4 / 4) := by simp
    _ = (4 * â€–sâ€– * â€–2 * l - sâ€–) / â€–lâ€– ^ 4 := by field
    _ â‰¤ (4 * r * (2.5 * â€–lâ€–)) / â€–lâ€– ^ 4 := by
      gcongr (4 * ?_ * ?_) / â€–lâ€– ^ 4
      refine (norm_sub_le _ _).trans ?_
      simp only [Complex.norm_mul, Complex.norm_ofNat]
      linarith
    _ = 10 * r / â€–lâ€– ^ 3 := by field
    _ = _ := by norm_cast

section weierstrassPExcept

/-- The Weierstrass `â„˜` function with the `lâ‚€`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `â„˜[L - lâ‚€]` in the namespace `PeriodPairs`. -/
def weierstrassPExcept (lâ‚€ : â„‚) (z : â„‚) : â„‚ :=
  âˆ‘' l : L.lattice, if l = lâ‚€ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassPExcept]
scoped notation3 "â„˜[" L:max " - " lâ‚€ "]" => weierstrassPExcept L lâ‚€

lemma hasSumLocallyUniformly_weierstrassPExcept (lâ‚€ : â„‚) :
    HasSumLocallyUniformly
      (fun (l : L.lattice) (z : â„‚) â†¦ if l.1 = lâ‚€ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      â„˜[L - lâ‚€] := by
  refine L.hasSumLocallyUniformly_aux (u := (10 * Â· * â€–Â·â€– ^ (-3 : â„))) _
    (fun _ _ â†¦ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr â†¦
    Filter.eventually_atTop.mpr âŸ¨2 * r, ?_âŸ©
  rintro _ h s hs l rfl
  split_ifs
  Â· simpa using show 0 â‰¤ 10 * r * (â€–â†‘lâ€– ^ 3)â»Â¹ by positivity
  Â· exact weierstrassP_bound r hr s hs l h

lemma hasSum_weierstrassPExcept (lâ‚€ : â„‚) (z : â„‚) :
    HasSum (fun l : L.lattice â†¦ if l = lâ‚€ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      (â„˜[L - lâ‚€] z) :=
  (L.hasSumLocallyUniformly_weierstrassPExcept lâ‚€).hasSum

/- `weierstrassPExcept lâ‚€` is differentiable on non-lattice points and `lâ‚€`. -/
lemma differentiableOn_weierstrassPExcept (lâ‚€ : â„‚) :
    DifferentiableOn â„‚ â„˜[L - lâ‚€] (L.lattice \ {lâ‚€})á¶œ := by
  refine (L.hasSumLocallyUniformly_weierstrassPExcept lâ‚€).hasSumLocallyUniformlyOn.differentiableOn
    (.of_forall fun s â†¦ .fun_sum fun i hi â†¦ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  Â· simp
  Â· exact .sub (.div (by fun_prop) (by fun_prop) (by aesop (add simp sub_eq_zero))) (by fun_prop)

lemma weierstrassPExcept_neg (lâ‚€ : â„‚) (z : â„‚) :
    â„˜[L - lâ‚€] (-z) = â„˜[L - -lâ‚€] z := by
  simp only [weierstrassPExcept]
  rw [â† (Equiv.neg L.lattice).tsum_eq]
  congr! 3 with l
  Â· simp [neg_eq_iff_eq_neg]
  simp
  ring

end weierstrassPExcept

section weierstrassP

/-- The Weierstrass `â„˜` function. This has the notation `â„˜[L]` in the namespace `PeriodPairs`. -/
def weierstrassP (z : â„‚) : â„‚ := âˆ‘' l : L.lattice, (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassP] scoped notation3 "â„˜[" L "]" => weierstrassP L

lemma weierstrassPExcept_add (lâ‚€ : L.lattice) (z : â„‚) :
    â„˜[L - lâ‚€] z + (1 / (z - lâ‚€.1) ^ 2 - 1 / lâ‚€.1 ^ 2) = â„˜[L] z := by
  trans â„˜[L - lâ‚€] z + âˆ‘' i : L.lattice, if i = lâ‚€.1 then (1 / (z - lâ‚€.1) ^ 2 - 1 / lâ‚€.1 ^ 2) else 0
  Â· simp
  rw [weierstrassPExcept, â† Summable.tsum_add]
  Â· congr with w; split_ifs <;> simp only [zero_add, add_zero, *]
  Â· exact âŸ¨_, L.hasSum_weierstrassPExcept _ _âŸ©
  Â· exact summable_of_finite_support ((Set.finite_singleton lâ‚€).subset (by simp_all))

lemma weierstrassPExcept_def (lâ‚€ : L.lattice) (z : â„‚) :
    â„˜[L - lâ‚€] z = â„˜[L] z + (1 / lâ‚€.1 ^ 2 - 1 / (z - lâ‚€.1) ^ 2) := by
  rw [â† L.weierstrassPExcept_add lâ‚€]
  abel

lemma weierstrassPExcept_of_notMem (lâ‚€ : â„‚) (hl : lâ‚€ âˆ‰ L.lattice) :
    â„˜[L - lâ‚€] = â„˜[L] := by
  delta weierstrassPExcept weierstrassP
  congr! 3 with z l
  have : l.1 â‰  lâ‚€ := by rintro rfl; simp at hl
  simp [this]

lemma hasSumLocallyUniformly_weierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : â„‚) â†¦ 1 / (z - â†‘l) ^ 2 - 1 / l ^ 2) â„˜[L] := by
  convert L.hasSumLocallyUniformly_weierstrassPExcept (L.Ï‰â‚ / 2) using 3 with l
  Â· rw [if_neg]; exact fun e â†¦ L.Ï‰â‚_div_two_notMem_lattice (e â–¸ l.2)
  Â· rw [L.weierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]

lemma hasSum_weierstrassP (z : â„‚) :
    HasSum (fun l : L.lattice â†¦ (1 / (z - l) ^ 2 - 1 / l ^ 2)) (â„˜[L] z) :=
  L.hasSumLocallyUniformly_weierstrassP.hasSum

lemma differentiableOn_weierstrassP :
    DifferentiableOn â„‚ â„˜[L] L.latticeá¶œ := by
  rw [â† L.weierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]
  convert L.differentiableOn_weierstrassPExcept _
  simp [L.Ï‰â‚_div_two_notMem_lattice]

@[simp]
lemma weierstrassP_neg (z : â„‚) : â„˜[L] (-z) = â„˜[L] z := by
  simp only [weierstrassP]
  rw [â† (Equiv.neg L.lattice).tsum_eq]
  congr with l
  simp
  ring

lemma not_continuousAt_weierstrassP (x : â„‚) (hx : x âˆˆ L.lattice) : Â¬ ContinuousAt â„˜[L] x := by
  eta_expand
  simp_rw [â† L.weierstrassPExcept_add âŸ¨x, hxâŸ©]
  intro H
  apply (NormedField.continuousAt_zpow (n := -2) (x := (0 : â„‚))).not.mpr (by simp)
  simpa [Function.comp_def] using
    (((H.sub ((L.differentiableOn_weierstrassPExcept x).differentiableAt (x := x)
      (L.isOpen_compl_lattice_diff.mem_nhds (by simp))).continuousAt).add
      (continuous_const (y := 1 / x ^ 2)).continuousAt).comp_of_eq
      (continuous_add_left x).continuousAt (add_zero _) :)

end weierstrassP

section derivWeierstrassPExcept

/-- The derivative of Weierstrass `â„˜` function with the `lâ‚€`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `â„˜'[L - lâ‚€]` in the namespace `PeriodPairs`. -/
def derivWeierstrassPExcept (lâ‚€ : â„‚) (z : â„‚) : â„‚ :=
  âˆ‘' l : L.lattice, if l.1 = lâ‚€ then 0 else -2 / (z - l) ^ 3

@[inherit_doc derivWeierstrassPExcept]
scoped notation3 "â„˜'[" L:max " - " lâ‚€ "]" => derivWeierstrassPExcept L lâ‚€

lemma hasSumLocallyUniformly_derivWeierstrassPExcept (lâ‚€ : â„‚) :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : â„‚) â†¦ if l.1 = lâ‚€ then 0 else -2 / (z - l) ^ 3)
      â„˜'[L - lâ‚€] := by
  refine L.hasSumLocallyUniformly_aux (u := fun _ â†¦ (16 * â€–Â·â€– ^ (-3 : â„))) _
    (fun _ _ â†¦ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr â†¦
    Filter.eventually_atTop.mpr âŸ¨2 * r, ?_âŸ©
  rintro _ h s hs l rfl
  split_ifs
  Â· simpa using show 0 â‰¤ â€–â†‘lâ€– ^ 3 by positivity
  have : s â‰  â†‘l := by rintro rfl; exfalso; linarith
  have : l â‰  0 := by rintro rfl; simp_all; linarith
  simp only [Complex.norm_div, norm_neg, Complex.norm_ofNat, norm_pow, AddSubgroupClass.coe_norm]
  rw [Real.rpow_neg (by positivity), â† div_eq_mul_inv, div_le_div_iffâ‚€, norm_sub_rev]
  Â· refine LE.le.trans_eq (b := 2 * (2 * â€–l - sâ€–) ^ 3) ?_ (by ring)
    norm_cast
    gcongr
    refine le_trans ?_ (mul_le_mul le_rfl (norm_sub_norm_le _ _) (by linarith) (by linarith))
    norm_cast at *
    linarith
  Â· exact pow_pos (by simpa [sub_eq_zero]) _
  Â· exact Real.rpow_pos_of_pos (by simpa) _

lemma hasSum_derivWeierstrassPExcept (lâ‚€ : â„‚) (z : â„‚) :
    HasSum (fun l : L.lattice â†¦ if l.1 = lâ‚€ then 0 else -2 / (z - l) ^ 3) (â„˜'[L - lâ‚€] z) :=
  (L.hasSumLocallyUniformly_derivWeierstrassPExcept lâ‚€).tendstoLocallyUniformlyOn.tendsto_at
    (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassPExcept (lâ‚€ : â„‚) :
    DifferentiableOn â„‚ â„˜'[L - lâ‚€] (L.lattice \ {lâ‚€})á¶œ := by
  refine L.hasSumLocallyUniformly_derivWeierstrassPExcept lâ‚€
    |>.tendstoLocallyUniformlyOn.differentiableOn
      (.of_forall fun s â†¦ .fun_sum fun i hi â†¦ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  Â· simp
  refine .div (by fun_prop) (by fun_prop) fun x hx â†¦ ?_
  have : x â‰  i := by rintro rfl; simp_all
  simpa [sub_eq_zero]

lemma eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (lâ‚€ : â„‚) :
    Set.EqOn (deriv â„˜[L - lâ‚€]) â„˜'[L - lâ‚€] (L.lattice \ {lâ‚€})á¶œ := by
  refine ((L.hasSumLocallyUniformly_weierstrassPExcept lâ‚€).tendstoLocallyUniformlyOn.deriv
    (.of_forall fun s â†¦ ?_) L.isOpen_compl_lattice_diff).unique ?_
  Â· refine .fun_sum fun i hi â†¦ ?_
    split_ifs
    Â· simp
    refine .sub (.div (by fun_prop) (by fun_prop) fun x hx â†¦ ?_) (by fun_prop)
    have : x â‰  i := by rintro rfl; simp_all
    simpa [sub_eq_zero]
  Â· refine (L.hasSumLocallyUniformly_derivWeierstrassPExcept lâ‚€).tendstoLocallyUniformlyOn.congr ?_
    intro s l hl
    simp only [Function.comp_apply]
    rw [deriv_fun_sum]
    Â· congr with x
      split_ifs with hlâ‚
      Â· simp
      have hlâ‚ : l - x â‰  0 := fun e â†¦ hlâ‚ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      rw [deriv_fun_sub (.fun_div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop),
        deriv_const]
      simp_rw [â† zpow_natCast, one_div, â† zpow_neg, Nat.cast_ofNat]
      rw [deriv_comp_sub_const (f := (Â· ^ (-2 : â„¤))), deriv_zpow]
      simp
      field_simp
    Â· intros x hxs
      split_ifs with hlâ‚
      Â· simp
      have hlâ‚ : l - x â‰  0 := fun e â†¦ hlâ‚ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      exact .sub (.div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop)

lemma derivWeierstrassPExcept_neg (lâ‚€ : â„‚) (z : â„‚) :
    â„˜'[L - lâ‚€] (-z) = - â„˜'[L - (-lâ‚€)] z := by
  simp only [derivWeierstrassPExcept]
  rw [â† (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub,
    â† div_neg, â† tsum_neg, apply_ite, neg_zero]
  congr! 3 with l
  Â· simp [neg_eq_iff_eq_neg]
  ring

end derivWeierstrassPExcept

section Periodicity

lemma derivWeierstrassPExcept_add_coe (lâ‚€ : â„‚) (z : â„‚) (l : L.lattice) :
    â„˜'[L - lâ‚€] (z + l) = â„˜'[L - (lâ‚€ - l)] z := by
  simp only [derivWeierstrassPExcept]
  rw [â† (Equiv.addRight l).tsum_eq]
  simp only [Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub, eq_sub_iff_add_eq]

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassPExcept_add_coe_aux
    (lâ‚€ : â„‚) (hlâ‚€ : lâ‚€ âˆˆ L.lattice) (l : L.lattice) (hl : l.1 / 2 âˆ‰ L.lattice) :
    Set.EqOn (â„˜[L - lâ‚€] <| Â· + l) (â„˜[L - (lâ‚€ - l)] Â· + (1 / lâ‚€ ^ 2 - 1 / (lâ‚€ - â†‘l) ^ 2))
      (L.lattice \ {lâ‚€ - l})á¶œ := by
  apply IsOpen.eqOn_of_deriv_eq (ğ•œ := â„‚) L.isOpen_compl_lattice_diff
    ?_ ?_ ?_ ?_ (x := -(l / 2)) ?_ ?_
  Â· refine (Set.Countable.isConnected_compl_of_one_lt_rank (by simp) ?_).2
    exact .mono sdiff_le (countable_of_Lindelof_of_discrete (X := L.lattice))
  Â· refine (L.differentiableOn_weierstrassPExcept lâ‚€).comp (f := (Â· + l.1)) (by fun_prop) ?_
    rintro x hâ‚ âŸ¨hâ‚‚ : x + l âˆˆ _, hâ‚ƒ : x + l â‰  lâ‚€âŸ©
    exact hâ‚ âŸ¨by simpa using sub_mem hâ‚‚ l.2, by rintro rfl; simp at hâ‚ƒâŸ©
  Â· refine .add (L.differentiableOn_weierstrassPExcept _) (by simp)
  Â· intro x hx
    simp only [deriv_add_const', deriv_comp_add_const]
    rw [L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept, L.derivWeierstrassPExcept_add_coe]
    Â· simpa using hx
    Â· simp only [Set.mem_compl_iff, Set.mem_diff, SetLike.mem_coe, Set.mem_singleton_iff, not_and,
        Decidable.not_not, eq_sub_iff_add_eq] at hx âŠ¢
      exact fun H â†¦ hx (by simpa using sub_mem H l.2)
  Â· simp [hl]
  Â· rw [L.weierstrassPExcept_neg, L.weierstrassPExcept_def âŸ¨lâ‚€, hlâ‚€âŸ©,
      L.weierstrassPExcept_def âŸ¨_, neg_mem (sub_mem hlâ‚€ l.2)âŸ©, add_assoc]
    congr 2 <;> ring

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassP_add_coe_aux (z : â„‚) (l : L.lattice) (hl : l.1 / 2 âˆ‰ L.lattice) :
    â„˜[L] (z + l) = â„˜[L] z := by
  have hl0 : l â‰  0 := by rintro rfl; simp at hl
  by_cases hz : z âˆˆ L.lattice
  Â· have := L.weierstrassPExcept_add_coe_aux (z + l) (add_mem hz l.2) l hl (x := z) (by simp)
    dsimp at this
    rw [â† L.weierstrassPExcept_add âŸ¨z + l, add_mem hz l.2âŸ©, this,
      â† L.weierstrassPExcept_add âŸ¨z, hzâŸ©]
    simp
    ring
  Â· have := L.weierstrassPExcept_add_coe_aux 0 (zero_mem _) l hl (x := z) (by simp [hz])
    simp only [zero_sub, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, div_zero,
      even_two, Even.neg_pow, one_div] at this
    rw [â† L.weierstrassPExcept_add 0, Submodule.coe_zero, this, â† L.weierstrassPExcept_add (-l)]
    simp
    ring

@[simp]
lemma weierstrassP_add_coe (z : â„‚) (l : L.lattice) : â„˜[L] (z + l) = â„˜[L] z := by
  let G : AddSubgroup â„‚ :=
    { carrier := { z | (â„˜[L] <| Â· + z) = â„˜[L] }
      add_mem' := by simp_all [funext_iff, â† add_assoc]
      zero_mem' := by simp
      neg_mem' {z} hz := funext fun i â†¦ by conv_lhs => rw [â† hz]; simp }
  have : L.lattice â‰¤ G.toIntSubmodule := by
    rw [lattice, Submodule.span_le]
    rintro _ (rfl | rfl)
    Â· ext i
      exact L.weierstrassP_add_coe_aux _ âŸ¨_, L.Ï‰â‚_mem_latticeâŸ© L.Ï‰â‚_div_two_notMem_lattice
    Â· ext i
      exact L.weierstrassP_add_coe_aux _ âŸ¨_, L.Ï‰â‚‚_mem_latticeâŸ© L.Ï‰â‚‚_div_two_notMem_lattice
  exact congr_fun (this l.2) _

lemma periodic_weierstrassP (l : L.lattice) : â„˜[L].Periodic l :=
  (L.weierstrassP_add_coe Â· l)

@[simp]
lemma weierstrassP_zero : â„˜[L] 0 = 0 := by simp [weierstrassP]

@[simp]
lemma weierstrassP_coe (l : L.lattice) : â„˜[L] l = 0 := by
  rw [â† zero_add l.1, L.weierstrassP_add_coe, L.weierstrassP_zero]

@[simp]
lemma weierstrassP_sub_coe (z : â„‚) (l : L.lattice) : â„˜[L] (z - l) = â„˜[L] z := by
  rw [â† L.weierstrassP_add_coe _ l, sub_add_cancel]

end Periodicity

section derivWeierstrassP

/-- The derivative of Weierstrass `â„˜` function.
This has the notation `â„˜'[L]` in the namespace `PeriodPairs`. -/
def derivWeierstrassP (z : â„‚) : â„‚ := - âˆ‘' l : L.lattice, 2 / (z - l) ^ 3

@[inherit_doc weierstrassP] scoped notation3 "â„˜'[" L "]" => derivWeierstrassP L

lemma derivWeierstrassPExcept_sub (lâ‚€ : L.lattice) (z : â„‚) :
    â„˜'[L - lâ‚€] z - 2 / (z - lâ‚€) ^ 3 = â„˜'[L] z := by
  trans â„˜'[L - lâ‚€] z + âˆ‘' i : L.lattice, if i.1 = lâ‚€.1 then (- 2 / (z - lâ‚€) ^ 3) else 0
  Â· simp [sub_eq_add_neg, neg_div]
  rw [derivWeierstrassP, derivWeierstrassPExcept, â† Summable.tsum_add, â† tsum_neg]
  Â· congr with w; split_ifs <;> simp only [zero_add, add_zero, *, neg_div]
  Â· exact âŸ¨_, L.hasSum_derivWeierstrassPExcept _ _âŸ©
  Â· exact summable_of_finite_support ((Set.finite_singleton lâ‚€).subset (by simp_all))

lemma derivWeierstrassPExcept_def (lâ‚€ : L.lattice) (z : â„‚) :
    â„˜'[L - lâ‚€] z = â„˜'[L] z + 2 / (z - lâ‚€) ^ 3 := by
  rw [â† L.derivWeierstrassPExcept_sub lâ‚€, sub_add_cancel]

lemma derivWeierstrassPExcept_of_notMem (lâ‚€ : â„‚) (hl : lâ‚€ âˆ‰ L.lattice) :
    â„˜'[L - lâ‚€] = â„˜'[L] := by
  delta derivWeierstrassPExcept derivWeierstrassP
  simp_rw [â† tsum_neg]
  congr! 3 with z l
  have : l.1 â‰  lâ‚€ := by rintro rfl; simp at hl
  simp [this, neg_div]

lemma hasSumLocallyUniformly_derivWeierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : â„‚) â†¦ - 2 / (z - l) ^ 3) â„˜'[L] := by
  convert L.hasSumLocallyUniformly_derivWeierstrassPExcept (L.Ï‰â‚ / 2) using 3 with l z
  Â· rw [if_neg, neg_div]; exact fun e â†¦ L.Ï‰â‚_div_two_notMem_lattice (e â–¸ l.2)
  Â· rw [L.derivWeierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]

lemma hasSum_derivWeierstrassP (z : â„‚) :
    HasSum (fun l : L.lattice â†¦ - 2 / (z - l) ^ 3) (â„˜'[L] z) :=
  L.hasSumLocallyUniformly_derivWeierstrassP.tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassP :
    DifferentiableOn â„‚ â„˜'[L] L.latticeá¶œ := by
  rw [â† L.derivWeierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]
  convert L.differentiableOn_derivWeierstrassPExcept _
  simp [L.Ï‰â‚_div_two_notMem_lattice]

@[simp]
lemma derivWeierstrassP_neg (z : â„‚) : â„˜'[L] (-z) = - â„˜'[L] z := by
  simp only [derivWeierstrassP]
  rw [â† (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub, neg_neg,
    â† div_neg, â† tsum_neg]
  congr! with l
  ring

@[simp]
lemma derivWeierstrassP_add_coe (z : â„‚) (l : L.lattice) :
    â„˜'[L] (z + l) = â„˜'[L] z := by
  simp only [derivWeierstrassP]
  rw [â† (Equiv.addRight l).tsum_eq]
  simp only [â† tsum_neg, â† div_neg, Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub]

lemma periodic_derivWeierstrassP (l : L.lattice) : â„˜'[L].Periodic l :=
  (L.derivWeierstrassP_add_coe Â· l)

@[simp]
lemma derivWeierstrassP_zero : â„˜'[L] 0 = 0 := by
  rw [â† CharZero.eq_neg_self_iff, â† L.derivWeierstrassP_neg, neg_zero]

@[simp]
lemma derivWeierstrassP_coe (l : L.lattice) : â„˜'[L] l = 0 := by
  rw [â† zero_add l.1, L.derivWeierstrassP_add_coe, L.derivWeierstrassP_zero]

@[simp]
lemma derivWeierstrassP_sub_coe (z : â„‚) (l : L.lattice) :
    â„˜'[L] (z - l) = â„˜'[L] z := by
  rw [â† L.derivWeierstrassP_add_coe _ l, sub_add_cancel]

/-- `deriv â„˜ = â„˜'`. This is true globally because of junk values. -/
@[simp] lemma deriv_weierstrassP : deriv â„˜[L] = â„˜'[L] := by
  ext x
  by_cases hx : x âˆˆ L.lattice
  Â· rw [deriv_zero_of_not_differentiableAt, L.derivWeierstrassP_coe âŸ¨x, hxâŸ©]
    exact fun H â†¦ L.not_continuousAt_weierstrassP x hx H.continuousAt
  Â· rw [â† L.weierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice,
      â† L.derivWeierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (L.Ï‰â‚ / 2) (x := x) (by simp [hx])]

end derivWeierstrassP

section AnalyticWeierstrassPExcept

/-- The sum `âˆ‘ (l - x)â»Ê³` over `l âˆˆ L`. This converges when `2 < r`, see `hasSum_sumInvPow`. -/
def sumInvPow (x : â„‚) (r : â„•) : â„‚ := âˆ‘' l : L.lattice, ((l - x) ^ r)â»Â¹

lemma hasSum_sumInvPow (x : â„‚) {r : â„•} (hr : 2 < r) :
    HasSum (fun l : L.lattice â†¦ ((l - x) ^ r)â»Â¹) (L.sumInvPow x r) := by
  refine Summable.hasSum (.of_norm_bounded (ZLattice.summable_norm_sub_zpow _
    (-r) (by simpa) x) fun l â†¦ ?_)
  rw [â† zpow_natCast, â† zpow_neg, â† norm_zpow]

/-- In the power series expansion of `â„˜(z) = âˆ‘ aáµ¢ (z - x)â±` at some `x âˆ‰ L`,
each `aáµ¢` can be written as an infinite sum over `l âˆˆ L`.
This is the summand of this infinite sum with the `lâ‚€`-th term omitted.
See `PeriodPair.coeff_weierstrassPExceptSeries`. -/
def weierstrassPExceptSummand (lâ‚€ x : â„‚) (i : â„•) (l : L.lattice) : â„‚ :=
  if l.1 = lâ‚€ then 0 else ((i + 1) * (l.1 - x) ^ (- â†‘(i + 2) : â„¤) - i.casesOn (l.1 ^ (-2 : â„¤)) 0)

/-- The power series exansion of `â„˜[L - lâ‚€]` at `x`.
See `PeriodPair.hasFPowerSeriesOnBall_weierstrassPExcept`. -/
def weierstrassPExceptSeries (lâ‚€ x : â„‚) : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i â†¦ i.casesOn (â„˜[L - lâ‚€] x) fun i â†¦ (i + 2) *
    (L.sumInvPow x (i + 3) - if lâ‚€ âˆˆ L.lattice then ((lâ‚€ - x) ^ (i + 3))â»Â¹ else 0)

lemma coeff_weierstrassPExceptSeries (lâ‚€ x : â„‚) (i : â„•) :
    (L.weierstrassPExceptSeries lâ‚€ x).coeff i =
      âˆ‘' l : L.lattice, L.weierstrassPExceptSummand lâ‚€ x i l := by
  delta weierstrassPExceptSummand weierstrassPExceptSeries
  cases i with
  | zero => simp [weierstrassPExcept, sub_sq_comm x, zpow_ofNat]
  | succ i =>
    split_ifs with hlâ‚€
    Â· trans (i + 2) * (L.sumInvPow x (i + 3) -
        âˆ‘' l : L.lattice, if l = âŸ¨lâ‚€, hlâ‚€âŸ© then (lâ‚€ - x) ^ (-â†‘(i + 3) : â„¤) else 0)
      Â· rw [FormalMultilinearSeries.coeff_ofScalars, tsum_ite_eq, zpow_neg, zpow_natCast]
      Â· rw [sumInvPow, â† (hasSum_sumInvPow _ _ (by linarith)).summable.tsum_sub, â† tsum_mul_left]
        Â· simp_rw [Subtype.ext_iff, zpow_neg]
          congr with l
          split_ifs with e
          Â· simp only [e, zpow_natCast, sub_self, mul_zero]
          Â· dsimp; norm_cast; ring
        Â· exact summable_of_finite_support ((Set.finite_singleton âŸ¨lâ‚€, hlâ‚€âŸ©).subset (by simp_all))
    Â· have hâ‚ (l : L.lattice) : l.1 â‰  lâ‚€ := fun e â†¦ hlâ‚€ (e â–¸ l.2)
      simp [hâ‚, tsum_mul_left, sumInvPow, add_assoc,
        one_add_one_eq_two, â† zpow_natCast, - neg_add_rev]

/--
In the power series expansion of `â„˜(z) = âˆ‘áµ¢ aáµ¢ (z - x)â±` at some `x âˆ‰ L`,
each `aáµ¢` can be writen as a sum over `l âˆˆ L`, i.e.
`aáµ¢ = âˆ‘â‚—, (i + 1) * (l - x)â»â±â»Â²` for `i â‰  0` and `aâ‚€ = âˆ‘â‚—, (l - x)â»Â² - lâ»Â²`.

We show that the double sum converges if `z` falls in a ball centered at `x` that doesn't touch `L`.
-/
-- We should be able to skip this computation via some general complex-analytic machinery but
-- they are missing at the moment.
-- Consider refactoring once we have developed more of the missing API.
lemma summable_weierstrassPExceptSummand (lâ‚€ z x : â„‚)
    (hx : âˆ€ l : L.lattice, l.1 â‰  lâ‚€ â†’ â€–z - xâ€– < â€–l - xâ€–) :
    Summable (Function.uncurry fun b c â†¦ L.weierstrassPExceptSummand lâ‚€ x b c * (z - x) ^ b) := by
  -- We first find a `Îº > 1`,
  -- such that the ball centered at `x` with radius `Îº * â€–z - xâ€–` does not touch `L`.
  obtain âŸ¨Îº, hÎº, hÎº'âŸ© : âˆƒ Îº : â„, 1 < Îº âˆ§ âˆ€ l : L.lattice, l.1 â‰  lâ‚€ â†’ â€–z - xâ€– * Îº < â€–l - xâ€– := by
    obtain âŸ¨Îº, hÎº, hÎº'âŸ© := Metric.isOpen_iff.mp ((continuous_mul_right â€–z - xâ€–).isOpen_preimage _
      (isClosedMap_dist x _
      (L.isClosed_of_subset_lattice (Set.diff_subset (t := {lâ‚€})))).upperClosure.isOpen_compl) 1
      (by simpa [Complex.dist_eq, @forall_comm â„, norm_sub_rev x] using hx)
    refine âŸ¨Îº / 2 + 1, by simpa, fun l hl â†¦ ?_âŸ©
    have : âˆ€ l âˆˆ L.lattice, l â‰  lâ‚€ â†’ (Îº / 2 + 1) * â€–z - xâ€– < dist x l := by
      simpa using @hÎº' (Îº / 2 + 1) (by simp [div_lt_iffâ‚€, abs_eq_self.mpr hÎº.le, hÎº])
    simpa only [Complex.dist_eq, norm_sub_rev x, mul_comm] using this _ l.2 hl
  -- We single out the degree zero term via this equiv.
  let e : â„• Ã— L.lattice â‰ƒ L.lattice âŠ• (â„• Ã— L.lattice) :=
    (Equiv.prodCongrLeft fun _ â†¦ (Denumerable.eqv (Option â„•)).symm).trans optionProdEquiv
  rw [â† e.symm.summable_iff]
  apply Summable.sum
  Â· -- for the degree zero term, this is the usual summability of the definition of `â„˜`.
    simpa [weierstrassPExceptSummand, e, Function.comp_def, Function.uncurry, sub_sq_comm x,
      Denumerable.eqv] using (L.hasSum_weierstrassPExcept lâ‚€ x).summable
  Â· -- for the remaining terms, we bound it by `(i + 2) Îºâ»â± * â€–l - xâ€–â»Â³ * â€–z - xâ€–`.
    dsimp [e, Function.comp_def, Function.uncurry_def, Denumerable.eqv, weierstrassPExceptSummand]
    have Hâ‚ : Summable fun i : â„• â†¦ ((i + 2) * Îº ^ (-i : â„¤)) := by
      have : |Îºâ»Â¹| < 1 := by grind [abs_inv, inv_lt_one_iffâ‚€]
      simpa [mul_comm] using ((Real.hasFPowerSeriesOnBall_ofScalars_mul_add_zero 1 2).hasSum
        (y := Îºâ»Â¹) (by simpa [enorm_eq_nnnorm])).summable
    have Hâ‚‚ : Summable fun l : L.lattice â†¦ â€–l - xâ€– ^ (-3 : â„¤) * â€–z - xâ€– :=
      (ZLattice.summable_norm_sub_zpow _ _ (by simp) _).mul_right _
    refine (Hâ‚.mul_of_nonneg Hâ‚‚ (by intro; positivity) (by intro; positivity)).of_norm_bounded ?_
    intro p
    split_ifs with hp
    Â· simp only [zero_mul, norm_zero, zpow_neg, zpow_natCast, Int.reduceNeg]; positivity
    have hpx : â€–p.2 - xâ€– â‰  0 := fun h â†¦ by
      obtain rfl : p.2 = x := by simpa [sub_eq_zero] using h
      simpa [(norm_nonneg _).not_gt] using hx p.2 hp
    obtain rfl | hxz := eq_or_ne z x
    Â· simp
    calc
      _ = â€–(p.1 + 2 : â„‚)â€– * â€–p.2 - xâ€– ^ (-3 - p.1 : â„¤) * â€–z - xâ€– ^ (p.1 + 1) := by
        norm_num; ring_nf; simp
      _ = â€–(p.1 + 2 : â„‚)â€– * ((â€–â†‘p.2 - xâ€– / â€–z - xâ€–) ^ p.1)â»Â¹ * ((â€–p.2 - xâ€– ^ 3)â»Â¹ * â€–z - xâ€–) := by
        simp [hpx, zpow_subâ‚€, div_pow]; field
      _ â‰¤ (p.1 + 2) * (Îº ^ p.1)â»Â¹ * ((â€–p.2 - xâ€– ^ 3)â»Â¹ * â€–z - xâ€–) := by
        gcongr
        Â· norm_cast
        Â· exact (le_div_iffâ‚€ (by simpa [sub_eq_zero])).mpr ((mul_comm _ _).trans_le (hÎº' p.2 hp).le)
      _ = _ := by simp [zpow_ofNat]

lemma weierstrassPExcept_eq_tsum (lâ‚€ z x : â„‚)
    (hx : âˆ€ l : L.lattice, l.1 â‰  lâ‚€ â†’ â€–z - xâ€– < â€–l - xâ€–) :
    â„˜[L - lâ‚€] z = âˆ‘' i : â„•, (L.weierstrassPExceptSeries lâ‚€ x).coeff i * (z - x) ^ i := by
  trans âˆ‘' (l : L.lattice) (i : â„•), if l.1 = lâ‚€ then 0 else
      ((i + 1) * (l.1 - x) ^ (- â†‘(i + 2) : â„¤) - i.casesOn (l.1 ^ (-2 : â„¤)) 0) * (z - x) ^ i
  Â· delta weierstrassPExcept
    congr 1 with l
    split_ifs with h
    Â· simp
    simpa [mul_comm] using ((Complex.one_div_sub_sq_sub_one_div_sq_hasFPowerSeriesOnBall_zero l x
      (by simpa [sub_eq_zero] using (norm_nonneg _).trans_lt (hx l h))).hasSum (y := z - x)
      (by simpa [enorm_eq_nnnorm] using hx _ h)).tsum_eq.symm
  trans âˆ‘' (l : â†¥L.lattice) (i : â„•), L.weierstrassPExceptSummand lâ‚€ x i l * (z - x) ^ i
  Â· simp only [weierstrassPExceptSummand, ite_mul, zero_mul]
  Â· simp_rw [coeff_weierstrassPExceptSeries, â† tsum_mul_right]
    apply Summable.tsum_comm
    exact L.summable_weierstrassPExceptSummand lâ‚€ z x hx

lemma weierstrassPExceptSeries_hasSum (lâ‚€ z x : â„‚)
    (hx : âˆ€ l : L.lattice, l.1 â‰  lâ‚€ â†’ â€–z - xâ€– < â€–l - xâ€–) :
    HasSum (fun i â†¦ (L.weierstrassPExceptSeries lâ‚€ x).coeff i * (z - x) ^ i) (â„˜[L - lâ‚€] z) := by
  refine (Summable.hasSum_iff ?_).mpr (L.weierstrassPExcept_eq_tsum lâ‚€ z x hx).symm
  simp_rw [coeff_weierstrassPExceptSeries, â† tsum_mul_right]
  exact (L.summable_weierstrassPExceptSummand lâ‚€ z x hx).prod

lemma hasFPowerSeriesOnBall_weierstrassPExcept (lâ‚€ x : â„‚) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r âŠ† (L.lattice \ {lâ‚€})á¶œ) :
    HasFPowerSeriesOnBall â„˜[L - lâ‚€] (L.weierstrassPExceptSeries lâ‚€ x) x r := by
  constructor
  Â· apply FormalMultilinearSeries.le_radius_of_tendsto (l := 0)
    convert tendsto_norm.comp (L.weierstrassPExceptSeries_hasSum lâ‚€ (x + r) x
      ?_).summable.tendsto_atTop_zero using 2 with i
    Â· simp
    Â· simp
    Â· intro l hl
      simpa using Set.subset_compl_comm.mp hr âŸ¨l.2, hlâŸ©
  Â· exact ENNReal.coe_pos.mpr hr0
  Â· intro z hz
    replace hz : â€–zâ€– < r := by simpa using hz
    have := L.weierstrassPExceptSeries_hasSum lâ‚€ (x + z) x
    simp only [add_sub_cancel_left] at this
    convert this (fun l hl â†¦ hz.trans (by simpa using Set.subset_compl_comm.mp hr âŸ¨l.2, hlâŸ©)) with i
    rw [weierstrassPExceptSeries, FormalMultilinearSeries.ofScalars_apply_eq,
      FormalMultilinearSeries.coeff_ofScalars, smul_eq_mul]

lemma analyticOnNhd_weierstrassPExcept (lâ‚€ : â„‚) : AnalyticOnNhd â„‚ â„˜[L - lâ‚€] (L.lattice \ {lâ‚€})á¶œ :=
  (L.differentiableOn_weierstrassPExcept lâ‚€).analyticOnNhd L.isOpen_compl_lattice_diff

end AnalyticWeierstrassPExcept

section AnalyticderivWeierstrassPExcept

/-- The power series exansion of `â„˜'[L - lâ‚€]` at `x`.
See `PeriodPair.hasFPowerSeriesOnBall_derivWeierstrassPExcept`. -/
def derivWeierstrassPExceptSeries (lâ‚€ x : â„‚) : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i â†¦ (i + 1) * (i + 2) *
    (L.sumInvPow x (i + 3) - if lâ‚€ âˆˆ L.lattice then ((lâ‚€ - x) ^ (i + 3))â»Â¹ else 0)

lemma hasFPowerSeriesOnBall_derivWeierstrassPExcept (lâ‚€ x : â„‚) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r âŠ† (L.lattice \ {lâ‚€})á¶œ) :
    HasFPowerSeriesOnBall â„˜'[L - lâ‚€] (L.derivWeierstrassPExceptSeries lâ‚€ x) x r := by
  refine .congr ?_
    ((L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept lâ‚€).mono (.trans ?_ hr))
  Â· have := (L.hasFPowerSeriesOnBall_weierstrassPExcept lâ‚€ x r hr0 hr).fderiv
    convert (ContinuousLinearMap.apply â„‚ â„‚ (1 : â„‚)).comp_hasFPowerSeriesOnBall this
    ext n
    simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, smul_eq_mul,
      ContinuousLinearMap.compFormalMultilinearSeries_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply, map_smul,
      ContinuousLinearMap.apply_apply, FormalMultilinearSeries.derivSeries_coeff_one, nsmul_eq_mul,
      Nat.cast_add, Nat.cast_one, mul_eq_mul_left_iff]
    left
    simp [weierstrassPExceptSeries, derivWeierstrassPExceptSeries, mul_assoc]
  Â· simpa using Metric.ball_subset_closedBall

lemma analyticOnNhd_derivWeierstrassPExcept (lâ‚€ : â„‚) :
    AnalyticOnNhd â„‚ â„˜'[L - lâ‚€] (L.lattice \ {lâ‚€})á¶œ :=
  (L.differentiableOn_derivWeierstrassPExcept lâ‚€).analyticOnNhd L.isOpen_compl_lattice_diff

lemma PeriodPairs.analyticOnNhd_derivWeierstrassP : AnalyticOnNhd â„‚ â„˜'[L] L.latticeá¶œ :=
  L.differentiableOn_derivWeierstrassP.analyticOnNhd L.isClosed_lattice.isOpen_compl

end AnalyticderivWeierstrassPExcept

section Analytic

/-- In the power series expansion of `â„˜(z) = âˆ‘ aáµ¢zâ±` at some `x âˆ‰ L`,
each `aáµ¢` can be written as an infinite sum over `l âˆˆ L`.
This is the summand of this infinite sum. See `PeriodPair.coeff_weierstrassPSeries`. -/
def weierstrassPSummand (x : â„‚) (i : â„•) (l : L.lattice) : â„‚ :=
  ((i + 1) * (l.1 - x) ^ (- â†‘(i + 2) : â„¤) - i.casesOn (l.1 ^ (-2 : â„¤)) 0)

/-- The power series exansion of `â„˜` at `x`. See `PeriodPair.hasFPowerSeriesOnBall_weierstrassP`. -/
def weierstrassPSeries (x : â„‚) : FormalMultilinearSeries â„‚ â„‚ â„‚ :=
  .ofScalars _ fun i â†¦ i.casesOn (â„˜[L] x) fun i â†¦ (i + 2) * L.sumInvPow x (i + 3)

lemma weierstrassPExceptSeries_of_notMem (lâ‚€ : â„‚) (hlâ‚€ : lâ‚€ âˆ‰ L.lattice) :
    L.weierstrassPExceptSeries lâ‚€ = L.weierstrassPSeries := by
  delta weierstrassPSeries weierstrassPExceptSeries
  congr! with z i f
  Â· rw [L.weierstrassPExcept_of_notMem _ hlâ‚€]
  Â· simp [hlâ‚€]

lemma weierstrassPExceptSummand_of_notMem (lâ‚€ : â„‚) (hlâ‚€ : lâ‚€ âˆ‰ L.lattice) :
    L.weierstrassPExceptSummand lâ‚€ = L.weierstrassPSummand := by
  grind [weierstrassPSummand, weierstrassPExceptSummand]

lemma coeff_weierstrassPSeries (x : â„‚) (i : â„•) :
    (L.weierstrassPSeries x).coeff i = âˆ‘' l : L.lattice, L.weierstrassPSummand x i l := by
  simp_rw [â† L.weierstrassPExceptSeries_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice,
    L.coeff_weierstrassPExceptSeries,
    â† L.weierstrassPExceptSummand_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]

lemma summable_weierstrassPSummand (z x : â„‚)
    (hx : âˆ€ l : L.lattice, â€–z - xâ€– < â€–l - xâ€–) :
    Summable (Function.uncurry fun b c â†¦ L.weierstrassPSummand x b c * (z - x) ^ b) := by
  simp_rw [â† L.weierstrassPExceptSummand_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]
  refine L.summable_weierstrassPExceptSummand _ z x fun l hl â†¦ hx l

lemma weierstrassPSeries_hasSum (z x : â„‚) (hx : âˆ€ l : L.lattice, â€–z - xâ€– < â€–l - xâ€–) :
    HasSum (fun i â†¦ (L.weierstrassPSeries x).coeff i * (z - x) ^ i) (â„˜[L] z) := by
  simp_rw [â† L.weierstrassPExceptSeries_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice,
    â† L.weierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]
  exact L.weierstrassPExceptSeries_hasSum _ z x fun l hl â†¦ hx l

lemma hasFPowerSeriesOnBall_weierstrassP (x : â„‚) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r âŠ† L.latticeá¶œ) :
    HasFPowerSeriesOnBall â„˜[L] (L.weierstrassPSeries x) x r := by
  simp_rw [â† L.weierstrassPExceptSeries_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice,
    â† L.weierstrassPExcept_of_notMem _ L.Ï‰â‚_div_two_notMem_lattice]
  exact L.hasFPowerSeriesOnBall_weierstrassPExcept _ x r hr0
    (hr.trans (Set.compl_subset_compl.mpr Set.diff_subset))

lemma analyticOnNhd_weierstrassP : AnalyticOnNhd â„‚ â„˜[L] L.latticeá¶œ :=
  L.differentiableOn_weierstrassP.analyticOnNhd L.isClosed_lattice.isOpen_compl

lemma ite_eq_one_sub_sq_mul_weierstrassP (lâ‚€ : â„‚) (hlâ‚€ : lâ‚€ âˆˆ L.lattice) (z : â„‚) :
    (if z = lâ‚€ then 1 else (z - lâ‚€) ^ 2 * â„˜[L] z) =
      (z - lâ‚€) ^ 2 * L.weierstrassPExcept lâ‚€ z + 1 - (z - lâ‚€) ^ 2 / lâ‚€ ^ 2 := by
  grind [L.weierstrassPExcept_add âŸ¨_, hlâ‚€âŸ©]

lemma meromorphic_weierstrassP : Meromorphic â„˜[L] := by
  intro x
  by_cases hx : x âˆˆ L.lattice
  Â· simp_rw [â† funext <| L.weierstrassPExcept_add âŸ¨x, hxâŸ©]
    have := (analyticOnNhd_weierstrassPExcept L x x (by simp)).meromorphicAt
    fun_prop
  Â· exact (L.analyticOnNhd_weierstrassP x hx).meromorphicAt

lemma order_weierstrassP (lâ‚€ : â„‚) (h : lâ‚€ âˆˆ L.lattice) :
    meromorphicOrderAt â„˜[L] lâ‚€ = -2 := by
  trans â†‘(-2 : â„¤)
  Â· rw [meromorphicOrderAt_eq_int_iff (L.meromorphic_weierstrassP lâ‚€)]
    refine âŸ¨fun z â†¦ (z - lâ‚€) ^ 2 * â„˜[L - lâ‚€] z + 1 - (z - lâ‚€) ^ 2 / lâ‚€ ^ 2, ?_, ?_, ?_âŸ©
    Â· have : AnalyticAt â„‚ â„˜[L - lâ‚€] lâ‚€ := L.analyticOnNhd_weierstrassPExcept lâ‚€ lâ‚€ (by simp)
      suffices AnalyticAt â„‚ (fun z â†¦ (z - lâ‚€) ^ 2 / lâ‚€ ^ 2) lâ‚€ by fun_prop
      by_cases hlâ‚€ : lâ‚€ = 0
      Â· simpa [hlâ‚€] using analyticAt_const
      Â· fun_prop (disch := simpa)
    Â· simp
    Â· filter_upwards [self_mem_nhdsWithin] with z (hz : _ â‰  _)
      have : (z - lâ‚€) ^ 2 â‰  0 := by simpa [sub_eq_zero]
      simp [â† L.ite_eq_one_sub_sq_mul_weierstrassP lâ‚€ h,
        if_neg hz, inv_mul_cancel_leftâ‚€ this, zpow_ofNat]
  Â· norm_num

end Analytic

end PeriodPair
