/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.Algebra.Module.ZLattice.Summable
public import Mathlib.Analysis.Complex.LocallyUniformLimit
public import Mathlib.LinearAlgebra.Complex.FiniteDimensional
public import Mathlib.Topology.Algebra.InfiniteSum.UniformOn
public import Mathlib.Analysis.Normed.Module.Connected
public import Mathlib.Analysis.Analytic.Binomial
public import Mathlib.Analysis.Meromorphic.Order

/-!

# Weierstrass `‚Ñò` functions

## Main definitions and results
- `PeriodPair.weierstrassP`: The Weierstrass `‚Ñò`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_weierstrassP`:
  The summands of `‚Ñò` sums to `‚Ñò` locally uniformly.
- `PeriodPair.differentiableOn_weierstrassP`: `‚Ñò` is differentiable away from the lattice points.
- `PeriodPair.weierstrassP_add_coe`: The Weierstrass `‚Ñò`-function is periodic.
- `PeriodPair.weierstrassP_neg`: The Weierstrass `‚Ñò`-function is even.

- `PeriodPair.derivWeierstrassP`:
  The derivative of the Weierstrass `‚Ñò`-function associated to a pair of periods.
- `PeriodPair.hasSumLocallyUniformly_derivWeierstrassP`:
  The summands of `‚Ñò'` sums to `‚Ñò'` locally uniformly.
- `PeriodPair.differentiableOn_derivWeierstrassP`:
  `‚Ñò'` is differentiable away from the lattice points.
- `PeriodPair.derivWeierstrassP_add_coe`: `‚Ñò'` is periodic.
- `PeriodPair.weierstrassP_neg`: `‚Ñò'` is odd.
- `PeriodPair.deriv_weierstrassP`: `deriv ‚Ñò = ‚Ñò'`. This is true globally because of junk values.

## tags

Weierstrass p-functions, Weierstrass p functions

-/

@[expose] public section

open Module

section

open scoped NNReal

theorem Complex.one_div_one_sub_cpow_hasFPowerSeriesOnBall_zero (a : ‚ÑÇ) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x) ^ a)
      (.ofScalars ‚ÑÇ fun n ‚Ü¶ Ring.choose (a + n - 1) n) 0 1 := by
  have H : ((binomialSeries ‚ÑÇ (-a)).compContinuousLinearMap (-1)) =
      .ofScalars ‚ÑÇ fun n ‚Ü¶ Ring.choose (a + n - 1) n := by
    ext n; simp [FormalMultilinearSeries.compContinuousLinearMap, binomialSeries, Ring.choose_neg,
      Units.smul_def, Int.coe_negOnePow_natCast, ‚Üê pow_add, ‚Üê mul_assoc]
  have : HasFPowerSeriesOnBall (fun x ‚Ü¶ (1 + x) ^ (-a)) (binomialSeries ‚ÑÇ (-a : ‚ÑÇ)) (-0) 1 := by
    simpa using one_add_cpow_hasFPowerSeriesOnBall_zero
  simpa [Complex.cpow_neg, Function.comp_def, ‚Üê sub_eq_add_neg, H] using
    this.compContinuousLinearMap (u := -1) (x := (0 : ‚ÑÇ))

theorem Complex.one_div_one_sub_pow_hasFPowerSeriesOnBall_zero (a : ‚Ñï) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x) ^ (a + 1))
      (.ofScalars ‚ÑÇ (ùïú := ‚ÑÇ) fun n ‚Ü¶ ‚Üë(Nat.choose (a + n) a)) 0 1 := by
  convert one_div_one_sub_cpow_hasFPowerSeriesOnBall_zero (a + 1) using 3 with z n
  ¬∑ norm_cast
  ¬∑ rw [eq_comm, add_right_comm, add_sub_cancel_right, ‚Üê Nat.cast_add,
      Ring.choose_natCast, Nat.choose_symm_add]

theorem Complex.one_div_sub_pow_hasFPowerSeriesOnBall_zero (a : ‚Ñï) {z : ‚ÑÇ} (hz : z ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (z - x) ^ (a + 1))
      (.ofScalars ‚ÑÇ (ùïú := ‚ÑÇ) fun n ‚Ü¶ (z ^ (n + a + 1))‚Åª¬π * ‚Üë(Nat.choose (a + n) a)) 0 ‚Äñz‚Äñ‚Çë := by
  have := one_div_one_sub_pow_hasFPowerSeriesOnBall_zero a
  rw [‚Üê map_zero (z‚Åª¬π ‚Ä¢ 1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ)] at this
  have := this.compContinuousLinearMap
  have H : 1 / ‚Äñ(z‚Åª¬π ‚Ä¢ 1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ)‚Äñ‚Çë = ‚Äñz‚Äñ‚Çë := by simp [enorm_smul, enorm_inv, hz]
  simp only [one_div, ContinuousLinearMap.coe_smul', H, Function.comp_def] at this
  convert (this.const_smul (c := (z ^ (a + 1))‚Åª¬π)).congr ?_ using 2
  ¬∑ ext n
    simp only [FormalMultilinearSeries.smul_apply, ContinuousMultilinearMap.smul_apply,
      FormalMultilinearSeries.compContinuousLinearMap_apply]
    simp [add_assoc, pow_add _ _ (a + 1), mul_assoc]
  ¬∑ intro w hw
    simp [‚Üê mul_inv_rev, ‚Üê mul_pow, sub_mul, mul_right_comm _ w, hz]

theorem Complex.one_div_sub_hasFPowerSeriesOnBall_zero {z : ‚ÑÇ} (hz : z ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (z - x)) (.ofScalars ‚ÑÇ fun n ‚Ü¶ (z ^ (n + 1))‚Åª¬π) 0 ‚Äñz‚Äñ‚Çë := by
  simpa using one_div_sub_pow_hasFPowerSeriesOnBall_zero (a := 0) hz

theorem Complex.one_div_sub_sq_hasFPowerSeriesOnBall_zero {z : ‚ÑÇ} (hz : z ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (z - x) ^ 2)
      (.ofScalars ‚ÑÇ fun n ‚Ü¶ (z ^ (n + 2))‚Åª¬π * (n + 1)) 0 ‚Äñz‚Äñ‚Çë := by
  simpa [add_comm 1] using one_div_sub_pow_hasFPowerSeriesOnBall_zero 1 hz

theorem Complex.one_div_one_sub_hasFPowerSeriesOnBall_zero :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x : ‚ÑÇ)) (.ofScalars (ùïú := ‚ÑÇ) ‚ÑÇ 1) 0 1 := by
  simpa using one_div_sub_hasFPowerSeriesOnBall_zero (z := 1)

theorem Complex.one_div_one_sub_sq_hasFPowerSeriesOnBall_zero :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x : ‚ÑÇ) ^ 2) (.ofScalars ‚ÑÇ fun n ‚Ü¶ (n + 1 : ‚ÑÇ)) 0 1 := by
  simpa using one_div_sub_sq_hasFPowerSeriesOnBall_zero (z := 1)

/-- `‚àë (ai + b) z‚Å± = (b - a) / (1 - z) + a / (1 - z)¬≤` -/
theorem Complex.hasFPowerSeriesOnBall_linear_zero (a b : ‚ÑÇ) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ (b - a) / (1 - x) + a / (1 - x) ^ 2)
      (.ofScalars ‚ÑÇ (a * ¬∑ + b)) 0 1 := by
  convert (one_div_one_sub_hasFPowerSeriesOnBall_zero.const_smul (c := b - a)).add
    (one_div_one_sub_sq_hasFPowerSeriesOnBall_zero.const_smul (c := a)) using 2
  ¬∑ simp [div_eq_mul_inv]
  ¬∑ ext; simp; ring

theorem Real.one_div_sub_pow_hasFPowerSeriesOnBall_zero (a : ‚Ñï) {r : ‚Ñù} (hr : r ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (r - x) ^ (a + 1))
      (.ofScalars ‚Ñù (ùïú := ‚Ñù) fun n ‚Ü¶ (r ^ (n + a + 1))‚Åª¬π * ‚Üë(Nat.choose (a + n) a)) 0 ‚Äñr‚Äñ‚Çë := by
  have := (Complex.one_div_sub_pow_hasFPowerSeriesOnBall_zero a (z := r)
    (by simpa)).restrictScalars (ùïú := ‚Ñù)
  rw [‚Üê Complex.ofRealCLM.map_zero] at this
  convert (Complex.reCLM.comp_hasFPowerSeriesOnBall this.compContinuousLinearMap) using 2
  ¬∑ simp [-Complex.inv_re, ‚Üê Complex.ofReal_pow, ‚Üê Complex.ofReal_inv, ‚Üê Complex.ofReal_sub]
  ¬∑ ext n
    simp [Function.comp_def, -FormalMultilinearSeries.apply_eq_prod_smul_coeff]
    simp [-Complex.inv_re, ‚Üê Complex.ofReal_pow, ‚Üê Complex.ofReal_inv]
  ¬∑ simp [enorm_eq_nnnorm]

theorem Real.one_div_sub_hasFPowerSeriesOnBall_zero {r : ‚Ñù} (hr : r ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (r - x)) (.ofScalars ‚Ñù fun n ‚Ü¶ (r ^ (n + 1))‚Åª¬π) 0 ‚Äñr‚Äñ‚Çë := by
  simpa using one_div_sub_pow_hasFPowerSeriesOnBall_zero (a := 0) hr

theorem Real.one_div_sub_sq_hasFPowerSeriesOnBall_zero {r : ‚Ñù} (hr : r ‚â† 0) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (r - x) ^ 2)
      (.ofScalars ‚Ñù fun n ‚Ü¶ (r ^ (n + 2))‚Åª¬π * (n + 1)) 0 ‚Äñr‚Äñ‚Çë := by
  simpa [add_comm 1] using one_div_sub_pow_hasFPowerSeriesOnBall_zero 1 hr

theorem Real.one_div_one_sub_hasFPowerSeriesOnBall_zero :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x)) (.ofScalars (ùïú := ‚Ñù) ‚Ñù 1) 0 1 := by
  simpa using one_div_sub_hasFPowerSeriesOnBall_zero (r := 1)

theorem Real.one_div_one_sub_sq_hasFPowerSeriesOnBall_zero :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ 1 / (1 - x) ^ 2) (.ofScalars ‚Ñù fun n ‚Ü¶ (n + 1 : ‚Ñù)) 0 1 := by
  simpa using one_div_sub_sq_hasFPowerSeriesOnBall_zero (r := 1)

/-- `‚àë (ai + b) z‚Å± = (b - a) / (1 - z) + a / (1 - z)¬≤` -/
theorem Real.hasFPowerSeriesOnBall_linear_zero (a b : ‚Ñù) :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ (b - a) / (1 - x) + a / (1 - x) ^ 2)
      (.ofScalars ‚Ñù (a * ¬∑ + b)) 0 1 := by
  convert (one_div_one_sub_hasFPowerSeriesOnBall_zero.const_smul (c := b - a)).add
    (one_div_one_sub_sq_hasFPowerSeriesOnBall_zero.const_smul (c := a)) using 2
  ¬∑ simp [div_eq_mul_inv]
  ¬∑ ext; simp; ring

lemma FormalMultilinearSeries.ofScalars_sub {ùïú : Type*} (E : Type*) [Field ùïú] [Ring E] [Algebra ùïú E]
    [TopologicalSpace E] [IsTopologicalRing E] (c c' : ‚Ñï ‚Üí ùïú) :
    ofScalars E (c - c') = ofScalars E c - ofScalars E c' := by ext; simp [ofScalars, sub_smul]

lemma Complex.one_div_sub_sq_sub_one_div_sq_hasFPowerSeriesOnBall_zero (w x : ‚ÑÇ) (hw : w ‚â† x) :
    HasFPowerSeriesOnBall (fun z ‚Ü¶ 1 / (z - w) ^ 2 - 1 / w ^ 2) (.ofScalars ‚ÑÇ
      fun i ‚Ü¶ (i + 1) * (w - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (w ^ (-2 : ‚Ñ§)) 0) x ‚Äñw - x‚Äñ‚Çë := by
  rw [‚Üê Pi.sub_def, ‚Üê Pi.sub_def, FormalMultilinearSeries.ofScalars_sub]
  refine .sub ?_ ?_
  ¬∑ simpa only [sub_sub_sub_cancel_right, zero_add, sub_sq_comm w, zpow_neg, zpow_natCast, mul_comm]
      using (Complex.one_div_sub_sq_hasFPowerSeriesOnBall_zero
        (z := w - x) (by simp [sub_eq_zero, hw])).comp_sub x
  ¬∑ convert hasFPowerSeriesOnBall_const.mono _ le_top
    ¬∑ ext (_|_) <;> simp [zpow_ofNat]
    ¬∑ simpa [sub_eq_zero]

end

noncomputable section

/-- A pair of `‚Ñù`-linearly independent complex numbers.
They span the period lattice in `lattice`,
and are the periods of the elliptic functions we shall construct. -/
structure PeriodPair : Type where
  /-- The first period in a `PeriodPair`. -/
  œâ‚ÇÅ : ‚ÑÇ
  /-- The second period in a `PeriodPair`. -/
  œâ‚ÇÇ : ‚ÑÇ
  indep : LinearIndependent ‚Ñù ![œâ‚ÇÅ, œâ‚ÇÇ]

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] (L : PeriodPair)

namespace PeriodPair

/-- The `‚Ñù`-basis of `‚ÑÇ` determined by a pair of periods. -/
protected def basis : Basis (Fin 2) ‚Ñù ‚ÑÇ :=
  basisOfLinearIndependentOfCardEqFinrank L.indep (by simp)

@[simp] lemma basis_zero : L.basis 0 = L.œâ‚ÇÅ := by simp [PeriodPair.basis]
@[simp] lemma basis_one : L.basis 1 = L.œâ‚ÇÇ := by simp [PeriodPair.basis]

/-- The lattice spanned by a pair of periods. -/
def lattice : Submodule ‚Ñ§ ‚ÑÇ := Submodule.span ‚Ñ§ {L.œâ‚ÇÅ, L.œâ‚ÇÇ}

lemma mem_lattice {L : PeriodPair} {x : ‚ÑÇ} :
    x ‚àà L.lattice ‚Üî ‚àÉ m n : ‚Ñ§, m * L.œâ‚ÇÅ + n * L.œâ‚ÇÇ = x := by
  simp only [lattice, Submodule.mem_span_pair, zsmul_eq_mul]

lemma œâ‚ÇÅ_mem_lattice : L.œâ‚ÇÅ ‚àà L.lattice := Submodule.subset_span (by simp)
lemma œâ‚ÇÇ_mem_lattice : L.œâ‚ÇÇ ‚àà L.lattice := Submodule.subset_span (by simp)

lemma mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice {L : PeriodPair} {Œ± Œ≤ : ‚Ñö} :
    Œ± * L.œâ‚ÇÅ + Œ≤ * L.œâ‚ÇÇ ‚àà L.lattice ‚Üî Œ±.den = 1 ‚àß Œ≤.den = 1 := by
  refine ‚ü®fun H ‚Ü¶ ?_, fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®m, n, e‚ü© := mem_lattice.mp H
    have := LinearIndependent.pair_iff.mp L.indep (m - Œ±) (n - Œ≤)
      (by simpa using by linear_combination e)
    simp only [sub_eq_zero] at this
    norm_cast at this
    aesop
  ¬∑ lift Œ± to ‚Ñ§ using h‚ÇÅ
    lift Œ≤ to ‚Ñ§ using h‚ÇÇ
    simp only [Rat.cast_intCast, ‚Üê zsmul_eq_mul]
    exact add_mem (Submodule.smul_mem _ _ L.œâ‚ÇÅ_mem_lattice)
      (Submodule.smul_mem _ _ L.œâ‚ÇÇ_mem_lattice)

lemma œâ‚ÇÅ_div_two_notMem_lattice : L.œâ‚ÇÅ / 2 ‚àâ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice (Œ± := 1/2) (Œ≤ := 0)).not.mpr (by norm_num)

lemma œâ‚ÇÇ_div_two_notMem_lattice : L.œâ‚ÇÇ / 2 ‚àâ L.lattice := by
  simpa [inv_mul_eq_div] using
    (L.mul_œâ‚ÇÅ_add_mul_œâ‚ÇÇ_mem_lattice (Œ± := 0) (Œ≤ := 1/2)).not.mpr (by norm_num)

-- helper lemma to connect to the ZLattice API
lemma lattice_eq_span_range_basis :
    L.lattice = Submodule.span ‚Ñ§ (Set.range L.basis) := by
  have : Finset.univ (Œ± := Fin 2) = {0, 1} := rfl
  rw [lattice, ‚Üê Set.image_univ, ‚Üê Finset.coe_univ, this]
  simp [Set.image_insert_eq]

instance : DiscreteTopology L.lattice := L.lattice_eq_span_range_basis ‚ñ∏ inferInstance

instance : IsZLattice ‚Ñù L.lattice := by
  simp_rw [L.lattice_eq_span_range_basis]
  infer_instance

lemma isClosed_lattice : IsClosed (X := ‚ÑÇ) L.lattice :=
  @AddSubgroup.isClosed_of_discrete _ _ _ _ _ L.lattice.toAddSubgroup
    (inferInstanceAs (DiscreteTopology L.lattice))

lemma isClosed_of_subset_lattice {s : Set ‚ÑÇ} (hs : s ‚äÜ L.lattice) : IsClosed s := by
  convert L.isClosed_lattice.isClosedMap_subtype_val _
    (isClosed_discrete (Œ± := L.lattice) ((‚Üë) ‚Åª¬π' s))
  convert Set.image_preimage_eq_inter_range.symm using 1
  simpa

lemma isOpen_compl_lattice_diff {s : Set ‚ÑÇ} : IsOpen (L.lattice \ s)·∂ú :=
  (L.isClosed_of_subset_lattice Set.diff_subset).isOpen_compl

instance : ProperSpace L.lattice := .of_isClosed L.isClosed_lattice

/-- The `‚Ñ§`-basis of the lattice determined by a pair of periods. -/
def latticeBasis : Basis (Fin 2) ‚Ñ§ L.lattice :=
  (Basis.span (v := ![L.œâ‚ÇÅ, L.œâ‚ÇÇ]) (L.indep.restrict_scalars' _)).map
    (.ofEq _ _ (by simp [lattice, Set.pair_comm L.œâ‚ÇÇ L.œâ‚ÇÅ]))

@[simp] lemma latticeBasis_zero : L.latticeBasis 0 = L.œâ‚ÇÅ := by simp [latticeBasis]
@[simp] lemma latticeBasis_one : L.latticeBasis 1 = L.œâ‚ÇÇ := by simp [latticeBasis]

@[simp] lemma finrank_lattice : finrank ‚Ñ§ L.lattice = 2 := finrank_eq_card_basis L.latticeBasis

/-- The equivalence from the lattice generated by a pair of periods to `‚Ñ§ √ó ‚Ñ§`. -/
def latticeEquivProd : L.lattice ‚âÉ‚Çó[‚Ñ§] ‚Ñ§ √ó ‚Ñ§ :=
  L.latticeBasis.repr ‚â™‚â´‚Çó Finsupp.linearEquivFunOnFinite _ _ _ ‚â™‚â´‚Çó .finTwoArrow ‚Ñ§ ‚Ñ§

lemma latticeEquiv_symm_apply (x : ‚Ñ§ √ó ‚Ñ§) :
    (L.latticeEquivProd.symm x).1 = x.1 * L.œâ‚ÇÅ + x.2 * L.œâ‚ÇÇ := by
  simp [latticeEquivProd, Finsupp.linearCombination]
  rfl

open Topology Filter in
lemma hasSumLocallyUniformly_aux (f : L.lattice ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ)
    (u : ‚Ñù ‚Üí L.lattice ‚Üí ‚Ñù) (hu : ‚àÄ r > 0, Summable (u r))
    (hf : ‚àÄ r > 0, ‚àÄ·∂† R in atTop, ‚àÄ x, ‚Äñx‚Äñ < r ‚Üí ‚àÄ l : L.lattice, ‚Äñl.1‚Äñ = R ‚Üí ‚Äñf l x‚Äñ ‚â§ u r l) :
    HasSumLocallyUniformly f (‚àë' j, f j ¬∑) := by
  rw [hasSumLocallyUniformly_iff_tendstoLocallyUniformly, tendstoLocallyUniformly_iff_filter]
  intro x
  obtain ‚ü®r, hr, hr'‚ü© : ‚àÉ r, 0 < r ‚àß ùìù x ‚â§ ùìü (Metric.ball 0 r) :=
    ‚ü®‚Äñx‚Äñ + 1, by positivity, Filter.le_principal_iff.mpr (Metric.isOpen_ball.mem_nhds (by simp))‚ü©
  refine .mono_right ?_ hr'
  rw [‚Üê tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  refine tendstoUniformlyOn_tsum_of_cofinite_eventually (hu r hr) ?_
  obtain ‚ü®R, hR‚ü© := eventually_atTop.mp (hf r hr)
  refine (isCompact_iff_finite.mp (isCompact_closedBall (0 : L.lattice) R)).subset ?_
  intros l hl
  obtain ‚ü®s, hs, hs'‚ü© : ‚àÉ x, ‚Äñx‚Äñ < r ‚àß u r l < ‚Äñf l x‚Äñ := by simpa using hl
  simp only [Metric.mem_closedBall, dist_zero_right, AddSubgroupClass.coe_norm]
  contrapose! hs'
  exact hR _ hs'.le _ hs _ rfl

-- Only the asymptotics matter and `10` is just a convenient constant to pick.
lemma weierstrassP_bound (r : ‚Ñù) (hr : r > 0) (s : ‚ÑÇ) (hs : ‚Äñs‚Äñ < r) (l : ‚ÑÇ) (h : 2 * r ‚â§ ‚Äñl‚Äñ) :
    ‚Äñ1 / (s - l) ^ 2 - 1 / l ^ 2‚Äñ ‚â§ 10 * r * ‚Äñl‚Äñ ^ (-3 : ‚Ñù) := by
  have : s ‚â† ‚Üël := by rintro rfl; exfalso; linarith
  have : 0 < ‚Äñl‚Äñ := by
    suffices l ‚â† 0 by simpa
    rintro rfl
    simp only [norm_zero] at h
    linarith
  calc
    _ = ‚Äñ(‚Üël ^ 2 - (s - ‚Üël) ^ 2) / ((s - ‚Üël) ^ 2 * ‚Üël ^ 2)‚Äñ := by
      rw [div_sub_div, one_mul, mul_one]
      ¬∑ simpa [sub_eq_zero]
      ¬∑ simpa
    _ = ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / (‚Äñs - l‚Äñ ^ 2 * ‚Äñl‚Äñ ^ 2) := by simp
    _ ‚â§ ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / ((‚Äñl‚Äñ / 2) ^ 2 * ‚Äñl‚Äñ ^ 2) := by
      gcongr
      rw [norm_sub_rev]
      exact .trans (by linarith) (norm_sub_norm_le l s)
    _ = ‚Äñs * (2 * l - s)‚Äñ / (‚Äñl‚Äñ ^ 4 / 4) := by
      congr 1
      ¬∑ rw [sq_sub_sq]; simp [‚Üê sub_add, two_mul, sub_add_eq_add_sub]
      ¬∑ ring
    _ = (‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / (‚Äñl‚Äñ ^ 4 / 4) := by simp
    _ = (4 * ‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / ‚Äñl‚Äñ ^ 4 := by field
    _ ‚â§ (4 * r * (2.5 * ‚Äñl‚Äñ)) / ‚Äñl‚Äñ ^ 4 := by
      gcongr (4 * ?_ * ?_) / ‚Äñl‚Äñ ^ 4
      refine (norm_sub_le _ _).trans ?_
      simp only [Complex.norm_mul, Complex.norm_ofNat]
      linarith
    _ = 10 * r / ‚Äñl‚Äñ ^ 3 := by field
    _ = _ := by norm_cast

section weierstrassPExcept

/-- The Weierstrass `‚Ñò` function with the `l‚ÇÄ`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `‚Ñò[L - l‚ÇÄ]` in the namespace `PeriodPairs`. -/
def weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassPExcept]
scoped notation3 "‚Ñò[" L:max " - " l‚ÇÄ "]" => weierstrassPExcept L l‚ÇÄ

lemma hasSumLocallyUniformly_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    HasSumLocallyUniformly
      (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ if l.1 = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      ‚Ñò[L - l‚ÇÄ] := by
  refine L.hasSumLocallyUniformly_aux (u := (10 * ¬∑ * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simpa using show 0 ‚â§ 10 * r * (‚Äñ‚Üël‚Äñ ^ 3)‚Åª¬π by positivity
  ¬∑ exact weierstrassP_bound r hr s hs l h

lemma hasSum_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      (‚Ñò[L - l‚ÇÄ] z) :=
  (L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).hasSum

/- `weierstrassPExcept l‚ÇÄ` is differentiable on non-lattice points and `l‚ÇÄ`. -/
lemma differentiableOn_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ ‚Ñò[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine (L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).hasSumLocallyUniformlyOn.differentiableOn
    (.of_forall fun s ‚Ü¶ .fun_sum fun i hi ‚Ü¶ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  ¬∑ simp
  ¬∑ exact .sub (.div (by fun_prop) (by fun_prop) (by aesop (add simp sub_eq_zero))) (by fun_prop)

lemma weierstrassPExcept_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] (-z) = ‚Ñò[L - -l‚ÇÄ] z := by
  simp only [weierstrassPExcept]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  simp
  ring

end weierstrassPExcept

section weierstrassP

/-- The Weierstrass `‚Ñò` function. This has the notation `‚Ñò[L]` in the namespace `PeriodPairs`. -/
def weierstrassP (z : ‚ÑÇ) : ‚ÑÇ := ‚àë' l : L.lattice, (1 / (z - l) ^ 2 - 1 / l ^ 2)

@[inherit_doc weierstrassP] scoped notation3 "‚Ñò[" L "]" => weierstrassP L

lemma weierstrassPExcept_add (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] z + (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) = ‚Ñò[L] z := by
  trans ‚Ñò[L - l‚ÇÄ] z + ‚àë' i : L.lattice, if i = l‚ÇÄ.1 then (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) else 0
  ¬∑ simp
  rw [weierstrassPExcept, ‚Üê Summable.tsum_add]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *]
  ¬∑ exact ‚ü®_, L.hasSum_weierstrassPExcept _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma weierstrassPExcept_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò[L - l‚ÇÄ] z = ‚Ñò[L] z + (1 / l‚ÇÄ.1 ^ 2 - 1 / (z - l‚ÇÄ.1) ^ 2) := by
  rw [‚Üê L.weierstrassPExcept_add l‚ÇÄ]
  abel

lemma weierstrassPExcept_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    ‚Ñò[L - l‚ÇÄ] = ‚Ñò[L] := by
  delta weierstrassPExcept weierstrassP
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this]

lemma hasSumLocallyUniformly_weierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ 1 / (z - ‚Üël) ^ 2 - 1 / l ^ 2) ‚Ñò[L] := by
  convert L.hasSumLocallyUniformly_weierstrassPExcept (L.œâ‚ÇÅ / 2) using 3 with l
  ¬∑ rw [if_neg]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_notMem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma hasSum_weierstrassP (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ (1 / (z - l) ^ 2 - 1 / l ^ 2)) (‚Ñò[L] z) :=
  L.hasSumLocallyUniformly_weierstrassP.hasSum

lemma differentiableOn_weierstrassP :
    DifferentiableOn ‚ÑÇ ‚Ñò[L] L.lattice·∂ú := by
  rw [‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  convert L.differentiableOn_weierstrassPExcept _
  simp [L.œâ‚ÇÅ_div_two_notMem_lattice]

@[simp]
lemma weierstrassP_neg (z : ‚ÑÇ) : ‚Ñò[L] (-z) = ‚Ñò[L] z := by
  simp only [weierstrassP]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr with l
  simp
  ring

lemma not_continuousAt_weierstrassP (x : ‚ÑÇ) (hx : x ‚àà L.lattice) : ¬¨ ContinuousAt ‚Ñò[L] x := by
  eta_expand
  simp_rw [‚Üê L.weierstrassPExcept_add ‚ü®x, hx‚ü©]
  intro H
  apply (NormedField.continuousAt_zpow (n := -2) (x := (0 : ‚ÑÇ))).not.mpr (by simp)
  simpa [Function.comp_def] using
    (((H.sub ((L.differentiableOn_weierstrassPExcept x).differentiableAt (x := x)
      (L.isOpen_compl_lattice_diff.mem_nhds (by simp))).continuousAt).add
      (continuous_const (y := 1 / x ^ 2)).continuousAt).comp_of_eq
      (continuous_add_left x).continuousAt (add_zero _):)

end weierstrassP

section derivWeierstrassPExcept

/-- The derivative of Weierstrass `‚Ñò` function with the `l‚ÇÄ`-term missing.
This is mainly a tool for calculations where one would want to omit a diverging term.
This has the notation `‚Ñò'[L - l‚ÇÄ]` in the namespace `PeriodPairs`. -/
def derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3

@[inherit_doc derivWeierstrassPExcept]
scoped notation3 "‚Ñò'[" L:max " - " l‚ÇÄ "]" => derivWeierstrassPExcept L l‚ÇÄ

lemma hasSumLocallyUniformly_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3)
      ‚Ñò'[L - l‚ÇÄ] := by
  refine L.hasSumLocallyUniformly_aux (u := fun _ ‚Ü¶ (16 * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (ZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simpa using show 0 ‚â§ ‚Äñ‚Üël‚Äñ ^ 3 by positivity
  have : s ‚â† ‚Üël := by rintro rfl; exfalso; linarith
  have : l ‚â† 0 := by rintro rfl; simp_all; linarith
  simp only [Complex.norm_div, norm_neg, Complex.norm_ofNat, norm_pow, AddSubgroupClass.coe_norm]
  rw [Real.rpow_neg (by positivity), ‚Üê div_eq_mul_inv, div_le_div_iff‚ÇÄ, norm_sub_rev]
  ¬∑ refine LE.le.trans_eq (b := 2 * (2 * ‚Äñl - s‚Äñ) ^ 3) ?_ (by ring)
    norm_cast
    gcongr
    refine le_trans ?_ (mul_le_mul le_rfl (norm_sub_norm_le _ _) (by linarith) (by linarith))
    norm_cast at *
    linarith
  ¬∑ exact pow_pos (by simpa [sub_eq_zero]) _
  ¬∑ exact Real.rpow_pos_of_pos (by simpa) _

lemma hasSum_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3) (‚Ñò'[L - l‚ÇÄ] z) :=
  (L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.tendsto_at
    (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ
    |>.tendstoLocallyUniformlyOn.differentiableOn
      (.of_forall fun s ‚Ü¶ .fun_sum fun i hi ‚Ü¶ ?_) L.isOpen_compl_lattice_diff
  split_ifs
  ¬∑ simp
  refine .div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_
  have : x ‚â† i := by rintro rfl; simp_all
  simpa [sub_eq_zero]

lemma eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    Set.EqOn (deriv ‚Ñò[L - l‚ÇÄ]) ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine ((L.hasSumLocallyUniformly_weierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.deriv
    (.of_forall fun s ‚Ü¶ ?_) L.isOpen_compl_lattice_diff).unique ?_
  ¬∑ refine .fun_sum fun i hi ‚Ü¶ ?_
    split_ifs
    ¬∑ simp
    refine .sub (.div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_) (by fun_prop)
    have : x ‚â† i := by rintro rfl; simp_all
    simpa [sub_eq_zero]
  ¬∑ refine (L.hasSumLocallyUniformly_derivWeierstrassPExcept l‚ÇÄ).tendstoLocallyUniformlyOn.congr ?_
    intro s l hl
    simp only [Function.comp_apply]
    rw [deriv_fun_sum]
    ¬∑ congr with x
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      rw [deriv_fun_sub (.fun_div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop),
        deriv_const]
      simp_rw [‚Üê zpow_natCast, one_div, ‚Üê zpow_neg, Nat.cast_ofNat]
      rw [deriv_comp_sub_const (f := (¬∑ ^ (-2 : ‚Ñ§))), deriv_zpow]
      simp
      field_simp
    ¬∑ intros x hxs
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      exact .sub (.div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop)

lemma derivWeierstrassPExcept_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] (-z) = - ‚Ñò'[L - (-l‚ÇÄ)] z := by
  simp only [derivWeierstrassPExcept]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub,
    ‚Üê div_neg, ‚Üê tsum_neg, apply_ite, neg_zero]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  ring

end derivWeierstrassPExcept

section Periodicity

lemma derivWeierstrassPExcept_add_coe (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L - l‚ÇÄ] (z + l) = ‚Ñò'[L - (l‚ÇÄ - l)] z := by
  simp only [derivWeierstrassPExcept]
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub, eq_sub_iff_add_eq]

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassPExcept_add_coe_aux
    (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    Set.EqOn (‚Ñò[L - l‚ÇÄ] <| ¬∑ + l) (‚Ñò[L - (l‚ÇÄ - l)] ¬∑ + (1 / l‚ÇÄ ^ 2 - 1 / (l‚ÇÄ - ‚Üël) ^ 2))
      (L.lattice \ {l‚ÇÄ - l})·∂ú := by
  apply IsOpen.eqOn_of_deriv_eq (ùïú := ‚ÑÇ) L.isOpen_compl_lattice_diff
    ?_ ?_ ?_ ?_ (x := - (l / 2)) ?_ ?_
  ¬∑ refine (Set.Countable.isConnected_compl_of_one_lt_rank (by simp) ?_).2
    exact .mono sdiff_le (countable_of_Lindelof_of_discrete (X := L.lattice))
  ¬∑ refine (L.differentiableOn_weierstrassPExcept l‚ÇÄ).comp (f := (¬∑ + l.1)) (by fun_prop) ?_
    rintro x h‚ÇÅ ‚ü®h‚ÇÇ : x + l ‚àà _, h‚ÇÉ : x + l ‚â† l‚ÇÄ‚ü©
    exact h‚ÇÅ ‚ü®by simpa using sub_mem h‚ÇÇ l.2, by rintro rfl; simp at h‚ÇÉ‚ü©
  ¬∑ refine .add (L.differentiableOn_weierstrassPExcept _) (by simp)
  ¬∑ intro x hx
    simp only [deriv_add_const', deriv_comp_add_const]
    rw [L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept, L.derivWeierstrassPExcept_add_coe]
    ¬∑ simpa using hx
    ¬∑ simp only [Set.mem_compl_iff, Set.mem_diff, SetLike.mem_coe, Set.mem_singleton_iff, not_and,
        Decidable.not_not, eq_sub_iff_add_eq] at hx ‚ä¢
      exact fun H ‚Ü¶ hx (by simpa using sub_mem H l.2)
  ¬∑ simp [hl]
  ¬∑ rw [L.weierstrassPExcept_neg, L.weierstrassPExcept_def ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©,
      L.weierstrassPExcept_def ‚ü®_, neg_mem (sub_mem hl‚ÇÄ l.2)‚ü©, add_assoc]
    congr 2 <;> ring

-- Subsumed by `weierstrassP_add_coe`
private lemma weierstrassP_add_coe_aux (z : ‚ÑÇ) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    ‚Ñò[L] (z + l) = ‚Ñò[L] z := by
  have hl0 : l ‚â† 0 := by rintro rfl; simp at hl
  by_cases hz : z ‚àà L.lattice
  ¬∑ have := L.weierstrassPExcept_add_coe_aux (z + l) (add_mem hz l.2) l hl (x := z) (by simp)
    dsimp at this
    rw [‚Üê L.weierstrassPExcept_add ‚ü®z + l, add_mem hz l.2‚ü©, this,
      ‚Üê L.weierstrassPExcept_add ‚ü®z, hz‚ü©]
    simp
    ring
  ¬∑ have := L.weierstrassPExcept_add_coe_aux 0 (zero_mem _) l hl (x := z) (by simp [hz])
    simp only [zero_sub, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, div_zero,
      even_two, Even.neg_pow, one_div] at this
    rw [‚Üê L.weierstrassPExcept_add 0, Submodule.coe_zero, this, ‚Üê L.weierstrassPExcept_add (-l)]
    simp
    ring

@[simp]
lemma weierstrassP_add_coe (z : ‚ÑÇ) (l : L.lattice) : ‚Ñò[L] (z + l) = ‚Ñò[L] z := by
  let G : AddSubgroup ‚ÑÇ :=
    { carrier := { z | (‚Ñò[L] <| ¬∑ + z) = ‚Ñò[L] }
      add_mem' := by simp_all [funext_iff, ‚Üê add_assoc]
      zero_mem' := by simp
      neg_mem' {z} hz := funext fun i ‚Ü¶ by conv_lhs => rw [‚Üê hz]; simp }
  have : L.lattice ‚â§ G.toIntSubmodule := by
    rw [lattice, Submodule.span_le]
    rintro _ (rfl|rfl)
    ¬∑ ext i
      exact L.weierstrassP_add_coe_aux _ ‚ü®_, L.œâ‚ÇÅ_mem_lattice‚ü© L.œâ‚ÇÅ_div_two_notMem_lattice
    ¬∑ ext i
      exact L.weierstrassP_add_coe_aux _ ‚ü®_, L.œâ‚ÇÇ_mem_lattice‚ü© L.œâ‚ÇÇ_div_two_notMem_lattice
  exact congr_fun (this l.2) _

lemma periodic_weierstrassP (l : L.lattice) : ‚Ñò[L].Periodic l :=
  (L.weierstrassP_add_coe ¬∑ l)

@[simp]
lemma weierstrassP_zero : ‚Ñò[L] 0 = 0 := by simp [weierstrassP]

@[simp]
lemma weierstrassP_coe (l : L.lattice) : ‚Ñò[L] l = 0 := by
  rw [‚Üê zero_add l.1, L.weierstrassP_add_coe, L.weierstrassP_zero]

@[simp]
lemma weierstrassP_sub_coe (z : ‚ÑÇ) (l : L.lattice) : ‚Ñò[L] (z - l) = ‚Ñò[L] z := by
  rw [‚Üê L.weierstrassP_add_coe _ l, sub_add_cancel]

end Periodicity

section derivWeierstrassP

/-- The derivative of Weierstrass `‚Ñò` function.
This has the notation `‚Ñò'[L]` in the namespace `PeriodPairs`. -/
def derivWeierstrassP (z : ‚ÑÇ) : ‚ÑÇ := - ‚àë' l : L.lattice, 2 / (z - l) ^ 3

@[inherit_doc weierstrassP] scoped notation3 "‚Ñò'[" L "]" => derivWeierstrassP L

lemma derivWeierstrassPExcept_sub (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] z - 2 / (z - l‚ÇÄ) ^ 3 = ‚Ñò'[L] z := by
  trans ‚Ñò'[L - l‚ÇÄ] z + ‚àë' i : L.lattice, if i.1 = l‚ÇÄ.1 then (- 2 / (z - l‚ÇÄ) ^ 3) else 0
  ¬∑ simp [sub_eq_add_neg, neg_div]
  rw [derivWeierstrassP, derivWeierstrassPExcept, ‚Üê Summable.tsum_add, ‚Üê tsum_neg]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *, neg_div]
  ¬∑ exact ‚ü®_, L.hasSum_derivWeierstrassPExcept _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma derivWeierstrassPExcept_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    ‚Ñò'[L - l‚ÇÄ] z = ‚Ñò'[L] z + 2 / (z - l‚ÇÄ) ^ 3 := by
  rw [‚Üê L.derivWeierstrassPExcept_sub l‚ÇÄ, sub_add_cancel]

lemma derivWeierstrassPExcept_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    ‚Ñò'[L - l‚ÇÄ] = ‚Ñò'[L] := by
  delta derivWeierstrassPExcept derivWeierstrassP
  simp_rw [‚Üê tsum_neg]
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this, neg_div]

lemma hasSumLocallyUniformly_derivWeierstrassP :
    HasSumLocallyUniformly (fun (l : L.lattice) (z : ‚ÑÇ) ‚Ü¶ - 2 / (z - l) ^ 3) ‚Ñò'[L] := by
  convert L.hasSumLocallyUniformly_derivWeierstrassPExcept (L.œâ‚ÇÅ / 2) using 3 with l z
  ¬∑ rw [if_neg, neg_div]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_notMem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma hasSum_derivWeierstrassP (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ - 2 / (z - l) ^ 3) (‚Ñò'[L] z) :=
  L.hasSumLocallyUniformly_derivWeierstrassP.tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma differentiableOn_derivWeierstrassP :
    DifferentiableOn ‚ÑÇ ‚Ñò'[L] L.lattice·∂ú := by
  rw [‚Üê L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  convert L.differentiableOn_derivWeierstrassPExcept _
  simp [L.œâ‚ÇÅ_div_two_notMem_lattice]

@[simp]
lemma derivWeierstrassP_neg (z : ‚ÑÇ) : ‚Ñò'[L] (-z) = - ‚Ñò'[L] z := by
  simp only [derivWeierstrassP]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub, neg_neg,
    ‚Üê div_neg, ‚Üê tsum_neg]
  congr! with l
  ring

@[simp]
lemma derivWeierstrassP_add_coe (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L] (z + l) = ‚Ñò'[L] z := by
  simp only [derivWeierstrassP]
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [‚Üê tsum_neg, ‚Üê div_neg, Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub]

lemma periodic_derivWeierstrassP (l : L.lattice) : ‚Ñò'[L].Periodic l :=
  (L.derivWeierstrassP_add_coe ¬∑ l)

@[simp]
lemma derivWeierstrassP_zero : ‚Ñò'[L] 0 = 0 := by
  rw [‚Üê CharZero.eq_neg_self_iff, ‚Üê L.derivWeierstrassP_neg, neg_zero]

@[simp]
lemma derivWeierstrassP_coe (l : L.lattice) : ‚Ñò'[L] l = 0 := by
  rw [‚Üê zero_add l.1, L.derivWeierstrassP_add_coe, L.derivWeierstrassP_zero]

@[simp]
lemma derivWeierstrassP_sub_coe (z : ‚ÑÇ) (l : L.lattice) :
    ‚Ñò'[L] (z - l) = ‚Ñò'[L] z := by
  rw [‚Üê L.derivWeierstrassP_add_coe _ l, sub_add_cancel]

/-- `deriv ‚Ñò = ‚Ñò'`. This is true globally because of junk values. -/
@[simp] lemma deriv_weierstrassP : deriv ‚Ñò[L] = ‚Ñò'[L] := by
  ext x
  by_cases hx : x ‚àà L.lattice
  ¬∑ rw [deriv_zero_of_not_differentiableAt, L.derivWeierstrassP_coe ‚ü®x, hx‚ü©]
    exact fun H ‚Ü¶ L.not_continuousAt_weierstrassP x hx H.continuousAt
  ¬∑ rw [‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
      ‚Üê L.derivWeierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
      L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept (L.œâ‚ÇÅ/2) (x := x) (by simp [hx])]

end derivWeierstrassP

section AnalyticweierstrassPExcept
def sumInvPow (x : ‚ÑÇ) (r : ‚Ñï) : ‚ÑÇ := ‚àë' l : L.lattice, ((l - x) ^ r)‚Åª¬π

def weierstrassPExceptSummand (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  if l.1 = l‚ÇÄ then 0 else ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

def weierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ i.casesOn (‚Ñò[L - l‚ÇÄ] x) fun i ‚Ü¶ (i + 2) *
    (L.sumInvPow x (i + 3) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 3))‚Åª¬π else 0)

lemma coeff_weierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) :
    (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i =
      ‚àë' l : L.lattice, L.weierstrassPExceptSummand l‚ÇÄ x i l := by
  delta weierstrassPExceptSummand
  rw [weierstrassPExceptSeries, FormalMultilinearSeries.coeff_ofScalars]
  cases i with
  | zero => simp [weierstrassPExcept, sub_sq_comm x, zpow_ofNat]
  | succ i =>
    dsimp
    split_ifs with hl‚ÇÄ
    ¬∑ trans (i + 2) * (L.sumInvPow x (i + 3) -
        ‚àë' l : L.lattice, if l = ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü© then (l‚ÇÄ - x) ^ (-‚Üë(i + 3) : ‚Ñ§) else 0)
      ¬∑ congr 2
        rw [tsum_ite_eq, zpow_neg, zpow_natCast]
      ¬∑ rw [sumInvPow, ‚Üê Summable.tsum_sub]
        ¬∑ rw [‚Üê tsum_mul_left]
          simp_rw [Subtype.ext_iff, zpow_neg]
          congr with l
          split_ifs with e
          ¬∑ simp only [e, zpow_natCast, sub_self, mul_zero]
          ¬∑ norm_cast; ring
        ¬∑ refine .of_norm_bounded (ZLattice.summable_norm_sub_zpow _ (-‚Üë(i + 3)) ?_ x) fun l ‚Ü¶ ?_
          ¬∑ simp; linarith
          ¬∑ rw [‚Üê zpow_natCast, ‚Üê zpow_neg, ‚Üê norm_zpow]
        ¬∑ exact summable_of_finite_support ((Set.finite_singleton ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©).subset (by simp_all))
    ¬∑ have (l : L.lattice) : l.1 ‚â† l‚ÇÄ := fun e ‚Ü¶ hl‚ÇÄ (e ‚ñ∏ l.2)
      simp only [this, if_false, sub_zero, tsum_mul_left, sumInvPow]
      congr 1
      simp [add_assoc, one_add_one_eq_two]

def _root_.optionProdEquiv {Œ± Œ≤ : Type*} : Option Œ± √ó Œ≤ ‚âÉ Œ≤ ‚äï Œ± √ó Œ≤ where
  toFun x := x.1.casesOn (.inl x.2) (fun a ‚Ü¶ .inr (a, x.2))
  invFun x := x.casesOn (Prod.mk none) (Prod.map some id)
  left_inv
  | (none, _) => rfl
  | (some _, _) => rfl
  right_inv
  | .inl _ => rfl
  | .inr (_, _) => rfl

lemma summable_weierstrassPExceptSummand (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.weierstrassPExceptSummand l‚ÇÄ x b c * (z - x) ^ b) := by
  obtain ‚ü®Œµ, hŒµ, hŒµ'‚ü© : ‚àÉ Œµ : ‚Ñù, 1 < Œµ ‚àß ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ * Œµ < ‚Äñl - x‚Äñ := by
    obtain ‚ü®Œ¥, hŒ¥, h‚ü© := Disjoint.exists_cthickenings (by
      simp only [Set.disjoint_iff_inter_eq_empty, Set.mem_diff, Set.mem_inter_iff, not_lt, not_and,
        Metric.mem_closedBall, Complex.dist_eq, Set.ext_iff, SetLike.mem_coe, Set.mem_singleton_iff,
        Set.mem_empty_iff_false, iff_false, Decidable.not_not, not_imp_comm (a := _ = _)] at hx ‚ä¢
      exact fun x h‚ÇÅ h‚ÇÇ ‚Ü¶ hx ‚ü®x, h‚ÇÇ‚ü© h‚ÇÅ) (isCompact_closedBall x ‚Äñz - x‚Äñ)
      (L.isClosed_of_subset_lattice (Set.diff_subset (t := {l‚ÇÄ})))
    by_cases hz : z = x
    ¬∑ refine ‚ü®2, one_lt_two, fun l hl ‚Ü¶ by simpa [hz] using hx l hl‚ü©
    have : 0 < ‚Äñz - x‚Äñ := by simp [sub_eq_zero, hz]
    refine ‚ü®Œ¥ / ‚Äñz - x‚Äñ + 1, by simpa using show 0 < Œ¥ / ‚Äñz - x‚Äñ by positivity, fun l hl ‚Ü¶ ?_‚ü©
    rw [mul_add, mul_one, mul_div, mul_div_cancel_left‚ÇÄ _ this.ne']
    rw [cthickening_closedBall hŒ¥.le (by positivity)] at h
    have := Set.subset_compl_iff_disjoint_left.mpr h (Metric.self_subset_cthickening _ ‚ü®l.2, hl‚ü©)
    simpa [Complex.dist_eq] using this
  let e : ‚Ñï √ó L.lattice ‚âÉ L.lattice ‚äï (‚Ñï √ó L.lattice) :=
    (Equiv.prodCongrLeft fun _ ‚Ü¶ (Denumerable.eqv (Option ‚Ñï)).symm).trans optionProdEquiv
  have he‚ÇÅ : e.symm ‚àò Sum.inl = Prod.mk 0 := rfl
  have he‚ÇÇ : e.symm ‚àò Sum.inr = Prod.map Nat.succ id := rfl
  rw [‚Üê e.symm.summable_iff]
  apply Summable.sum
  ¬∑ simp only [Function.comp_assoc, he‚ÇÅ, weierstrassPExceptSummand]
    simpa [Function.comp_def, sub_sq_comm x] using (L.hasSum_weierstrassPExcept l‚ÇÄ x).summable
  ¬∑ simp only [Function.comp_assoc, he‚ÇÇ, Function.uncurry_def]
    simp only [Function.comp_def, Prod.map_fst, Nat.succ_eq_add_one, Prod.map_snd, id_eq]
    refine Summable.of_norm_bounded (g := fun p : ‚Ñï √ó L.lattice ‚Ü¶
        ((p.1 + 2) * Œµ ^ (- p.1 : ‚Ñ§)) * (‚Äñp.2 - x‚Äñ ^ (-3 : ‚Ñ§) * ‚Äñz - x‚Äñ)) ?_ ?_
    ¬∑ refine Summable.mul_of_nonneg (f := fun p : ‚Ñï ‚Ü¶ ((p + 2) * Œµ ^ (- p : ‚Ñ§)))
        (g := fun p : L.lattice ‚Ü¶ ‚Äñp - x‚Äñ ^ (-3 : ‚Ñ§) * ‚Äñz - x‚Äñ) ?_ ?_ ?_ ?_
      ¬∑ have : |Œµ‚Åª¬π| < 1 := by
          rw [abs_inv, abs_eq_self.mpr (by positivity), inv_lt_one_iff‚ÇÄ]; exact .inr hŒµ
        convert ((Real.hasFPowerSeriesOnBall_linear_zero 1 2).hasSum (y := Œµ‚Åª¬π)
          (by simpa [enorm_eq_nnnorm])).summable
        simp [mul_comm]
      ¬∑ apply Summable.mul_right
        apply ZLattice.summable_norm_sub_zpow
        simp
      ¬∑ intro; positivity
      ¬∑ intro; positivity
    ¬∑ intro p
      simp only [weierstrassPExceptSummand]
      split_ifs with hp
      ¬∑ simp only [zero_mul, norm_zero, zpow_neg, zpow_natCast, Int.reduceNeg]; positivity
      simp only [zpow_neg, zpow_natCast, add_assoc]
      norm_num
      simp only [mul_assoc, add_assoc, one_add_one_eq_two]
      rw [pow_succ (n := p.1)]
      refine mul_le_mul ?_ ?_ (by positivity) (by positivity)
      ¬∑ norm_cast
      ¬∑ simp only [‚Üê mul_assoc]
        refine mul_le_mul ?_ le_rfl (by positivity) (by positivity)
        rw [pow_add, mul_inv_rev, mul_assoc, mul_comm ((Œµ ^ p.1)‚Åª¬π)]
        refine mul_le_mul le_rfl ?_ (by positivity) (by positivity)
        rw [‚Üê inv_pow, ‚Üê inv_pow, ‚Üê mul_pow]
        gcongr
        have : ‚Äñz - x‚Äñ * Œµ < ‚Äñp.2 - x‚Äñ := hŒµ' p.2 hp
        have h : 0 < ‚Äñp.2 - x‚Äñ := (show 0 ‚â§ ‚Äñz - x‚Äñ * Œµ by positivity).trans_lt this
        rw [inv_mul_le_iff‚ÇÄ h, le_mul_inv_iff‚ÇÄ (by positivity)]
        exact this.le

lemma weierstrassPExcept_eq_tsum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    ‚Ñò[L - l‚ÇÄ] z = ‚àë' i : ‚Ñï, (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i := by
  trans ‚àë' (l : L.lattice) (i : ‚Ñï), if l.1 = l‚ÇÄ then 0 else
      ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0) * (z - x) ^ i
  ¬∑ delta weierstrassPExcept
    congr 1 with l
    split_ifs with h
    ¬∑ simp
    convert ((Complex.one_div_sub_sq_sub_one_div_sq_hasFPowerSeriesOnBall_zero l x
      (by simpa [sub_eq_zero] using (norm_nonneg _).trans_lt (hx l h))).hasSum (y := z - x)
      (by simpa [enorm_eq_nnnorm] using hx _ h)).tsum_eq.symm with i
    ¬∑ simp
    ¬∑ simp [mul_comm]
  trans ‚àë' (l : ‚Ü•L.lattice) (i : ‚Ñï), L.weierstrassPExceptSummand l‚ÇÄ x i l * (z - x) ^ i
  ¬∑ simp only [weierstrassPExceptSummand, ite_mul, zero_mul]
  ¬∑ simp_rw [coeff_weierstrassPExceptSeries, ‚Üê tsum_mul_right]
    apply Summable.tsum_comm
    exact L.summable_weierstrassPExceptSummand l‚ÇÄ z x hx

lemma weierstrassPExceptSeries_hasSum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.weierstrassPExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i) (‚Ñò[L - l‚ÇÄ] z) := by
  refine (Summable.hasSum_iff ?_).mpr (L.weierstrassPExcept_eq_tsum l‚ÇÄ z x hx).symm
  simp_rw [coeff_weierstrassPExceptSeries, ‚Üê tsum_mul_right]
  exact (L.summable_weierstrassPExceptSummand l‚ÇÄ z x hx).prod

lemma hasFPowerSeriesOnBall_weierstrassPExcept (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò[L - l‚ÇÄ] (L.weierstrassPExceptSeries l‚ÇÄ x) x r := by
  constructor
  ¬∑ apply FormalMultilinearSeries.le_radius_of_tendsto (l := 0)
    convert tendsto_norm.comp (L.weierstrassPExceptSeries_hasSum l‚ÇÄ (x + r) x
      ?_).summable.tendsto_atTop_zero using 2 with i
    ¬∑ simp
    ¬∑ simp
    ¬∑ intro l hl
      simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©
  ¬∑ exact ENNReal.coe_pos.mpr hr0
  ¬∑ intro z hz
    replace hz : ‚Äñz‚Äñ < r := by simpa using hz
    have := L.weierstrassPExceptSeries_hasSum l‚ÇÄ (x + z) x
    simp only [add_sub_cancel_left] at this
    convert this (fun l hl ‚Ü¶ hz.trans (by simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©)) with i
    rw [weierstrassPExceptSeries, FormalMultilinearSeries.ofScalars_apply_eq,
      FormalMultilinearSeries.coeff_ofScalars]
    rfl

lemma analyticOnNhd_weierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    AnalyticOnNhd ‚ÑÇ ‚Ñò[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  intro x hx
  obtain ‚ü®Œµ, hŒµ, h‚ü© := Metric.isOpen_iff.mp L.isOpen_compl_lattice_diff x hx
  lift Œµ to NNReal using hŒµ.le
  exact ‚ü®L.weierstrassPExceptSeries l‚ÇÄ x, _, L.hasFPowerSeriesOnBall_weierstrassPExcept l‚ÇÄ x (Œµ / 2)
    (div_pos hŒµ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact hŒµ)).trans h)‚ü©

end AnalyticweierstrassPExcept

section AnalyticderivWeierstrassPExcept

def derivWeierstrassPExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ (i + 1) * (i + 2) *
    (L.sumInvPow x (i + 3) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 3))‚Åª¬π else 0)

lemma hasFPowerSeriesOnBall_derivWeierstrassPExcept (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò'[L - l‚ÇÄ] (L.derivWeierstrassPExceptSeries l‚ÇÄ x) x r := by
  refine .congr ?_
    ((L.eqOn_deriv_weierstrassPExcept_derivWeierstrassPExcept l‚ÇÄ).mono (.trans ?_ hr))
  ¬∑ have := (L.hasFPowerSeriesOnBall_weierstrassPExcept l‚ÇÄ x r hr0 hr).fderiv
    convert (ContinuousLinearMap.apply ‚ÑÇ ‚ÑÇ (1 : ‚ÑÇ)).comp_hasFPowerSeriesOnBall this
    ext n
    simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, smul_eq_mul,
      ContinuousLinearMap.compFormalMultilinearSeries_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply, map_smul,
      ContinuousLinearMap.apply_apply, FormalMultilinearSeries.derivSeries_coeff_one, nsmul_eq_mul,
      Nat.cast_add, Nat.cast_one, mul_eq_mul_left_iff]
    left
    simp [weierstrassPExceptSeries, derivWeierstrassPExceptSeries, mul_assoc]
  ¬∑ simpa using Metric.ball_subset_closedBall

lemma analyticOnNhd_derivWeierstrassPExcept (l‚ÇÄ : ‚ÑÇ) :
    AnalyticOnNhd ‚ÑÇ ‚Ñò'[L - l‚ÇÄ] (L.lattice \ {l‚ÇÄ})·∂ú := by
  intro x hx
  obtain ‚ü®Œµ, hŒµ, h‚ü© := Metric.isOpen_iff.mp L.isOpen_compl_lattice_diff x hx
  lift Œµ to NNReal using hŒµ.le
  exact ‚ü®L.derivWeierstrassPExceptSeries l‚ÇÄ x, _,
    L.hasFPowerSeriesOnBall_derivWeierstrassPExcept l‚ÇÄ x (Œµ / 2)
    (div_pos hŒµ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact hŒµ)).trans h)‚ü©

end AnalyticderivWeierstrassPExcept

section Analytic

def weierstrassPSummand (x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

def weierstrassPSeries (x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  .ofScalars _ fun i ‚Ü¶ i.casesOn (‚Ñò[L] x) fun i ‚Ü¶ (i + 2) * L.sumInvPow x (i + 3)

lemma weierstrassPExceptSeries_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.weierstrassPExceptSeries l‚ÇÄ = L.weierstrassPSeries := by
  delta weierstrassPSeries weierstrassPExceptSeries
  congr! with z i f
  ¬∑ rw [L.weierstrassPExcept_of_notMem _ hl‚ÇÄ]
  ¬∑ simp [hl‚ÇÄ]

lemma weierstrassPExceptSummand_of_notMem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.weierstrassPExceptSummand l‚ÇÄ = L.weierstrassPSummand := by
  delta weierstrassPSummand weierstrassPExceptSummand
  ext l z l'
  have : l'.1 ‚â† l‚ÇÄ := fun e ‚Ü¶ hl‚ÇÄ (e ‚ñ∏ l'.2)
  simp [this]

lemma coeff_weierstrassPSeries (x : ‚ÑÇ) (i : ‚Ñï) :
    (L.weierstrassPSeries x).coeff i = ‚àë' l : L.lattice, L.weierstrassPSummand x i l := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    L.coeff_weierstrassPExceptSeries,
    ‚Üê L.weierstrassPExceptSummand_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]

lemma summable_weierstrassPSummand (z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.weierstrassPSummand x b c * (z - x) ^ b) := by
  simp_rw [‚Üê L.weierstrassPExceptSummand_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  refine L.summable_weierstrassPExceptSummand _ z x fun l hl ‚Ü¶ hx l

lemma weierstrassPSeries_hasSum (z x : ‚ÑÇ) (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.weierstrassPSeries x).coeff i * (z - x) ^ i) (‚Ñò[L] z) := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    ‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  exact L.weierstrassPExceptSeries_hasSum _ z x fun l hl ‚Ü¶ hx l

lemma hasFPowerSeriesOnBall_weierstrassP (x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ L.lattice·∂ú) :
    HasFPowerSeriesOnBall ‚Ñò[L] (L.weierstrassPSeries x) x r := by
  simp_rw [‚Üê L.weierstrassPExceptSeries_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice,
    ‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  exact L.hasFPowerSeriesOnBall_weierstrassPExcept _ x r hr0
    (hr.trans (Set.compl_subset_compl.mpr Set.diff_subset))

lemma analyticOnNhd_weierstrassP : AnalyticOnNhd ‚ÑÇ ‚Ñò[L] L.lattice·∂ú := by
  simp_rw [‚Üê L.weierstrassPExcept_of_notMem _ L.œâ‚ÇÅ_div_two_notMem_lattice]
  refine (L.analyticOnNhd_weierstrassPExcept _).mono (Set.compl_subset_compl.mpr Set.diff_subset)

lemma ite_eq_one_sub_sq_mul_weierstrassP (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (z : ‚ÑÇ) :
  (if z = l‚ÇÄ then 1 else (z - l‚ÇÄ) ^ 2 * ‚Ñò[L] z) =
    (z - l‚ÇÄ) ^ 2 * L.weierstrassPExcept l‚ÇÄ z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2 := by
  split_ifs with h
  ¬∑ subst h
    simp
  rw [‚Üê L.weierstrassPExcept_add ‚ü®_, hl‚ÇÄ‚ü©, mul_add, mul_sub, add_sub_assoc,
    ‚Üê mul_div_assoc, mul_one, ‚Üê mul_div_assoc, mul_one, div_self]
  simpa [sub_eq_zero] using h

lemma meromorphicAt_weierstrassP (x : ‚ÑÇ) : MeromorphicAt ‚Ñò[L] x := by
  by_cases hx : x ‚àà L.lattice
  ¬∑ use 3
    simp_rw [‚Üê L.weierstrassPExcept_add ‚ü®x, hx‚ü©, smul_eq_mul, mul_add, mul_sub]
    refine .add (.mul (by fun_prop) (L.analyticOnNhd_weierstrassPExcept x x (fun e ‚Ü¶ e.2 rfl)))
      (.sub ?_ (by fun_prop))
    convert_to AnalyticAt ‚ÑÇ (fun z ‚Ü¶ z - x) x
    ¬∑ ext z
      by_cases h : z - x = 0
      ¬∑ simp [h]
      ¬∑ rw [pow_succ', one_div, mul_inv_cancel_right‚ÇÄ]
        simpa
    ¬∑ fun_prop
  ¬∑ exact (L.analyticOnNhd_weierstrassP x hx).meromorphicAt

lemma order_weierstrassP (l‚ÇÄ : ‚ÑÇ) (h : l‚ÇÄ ‚àà L.lattice) :
    meromorphicOrderAt ‚Ñò[L] l‚ÇÄ = -2 := by
  trans ‚Üë(-2 : ‚Ñ§)
  ¬∑ rw [meromorphicOrderAt_eq_int_iff (L.meromorphicAt_weierstrassP l‚ÇÄ)]
    refine ‚ü®fun z ‚Ü¶ (z - l‚ÇÄ) ^ 2 * ‚Ñò[L - l‚ÇÄ] z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2, ?_, ?_, ?_‚ü©
    ¬∑ refine .sub (.add (.mul (by fun_prop) ?_) (by fun_prop)) ?_
      ¬∑ exact L.analyticOnNhd_weierstrassPExcept l‚ÇÄ l‚ÇÄ (by simp)
      ¬∑ by_cases hl‚ÇÄ : l‚ÇÄ = 0
        ¬∑ simpa [hl‚ÇÄ] using analyticAt_const
        ¬∑ exact .div (by fun_prop) (by fun_prop) (by simpa)
    ¬∑ simp
    ¬∑ refine Filter.eventually_of_mem self_mem_nhdsWithin ?_
      rintro z (hz : _ ‚â† _)
      have : (z - l‚ÇÄ) ^ 2 ‚â† 0 := by simpa [sub_eq_zero]
      simp [‚Üê L.ite_eq_one_sub_sq_mul_weierstrassP l‚ÇÄ h,
        if_neg hz, inv_mul_cancel_left‚ÇÄ this, zpow_ofNat]
  ¬∑ norm_num

end Analytic
