/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.RingTheory.Polynomial.Bernstein
import Mathlib.Topology.ContinuousFunction.Polynomial
import Mathlib.Topology.ContinuousFunction.Compact

#align_import analysis.special_functions.bernstein from "leanprover-community/mathlib"@"2c1d8ca2812b64f88992a5294ea3dba144755cd1"

/-!
# Bernstein approximations and Weierstrass' theorem

We prove that the Bernstein approximations
```
âˆ‘ k : Fin (n+1), f (k/n : â„) * n.choose k * x^k * (1-x)^(n-k)
```
for a continuous function `f : C([0,1], â„)` converge uniformly to `f` as `n` tends to infinity.

Our proof follows [Richard Beals' *Analysis, an introduction*][beals-analysis], Â§7D.
The original proof, due to [Bernstein](bernstein1912) in 1912, is probabilistic,
and relies on Bernoulli's theorem,
which gives bounds for how quickly the observed frequencies in a
Bernoulli trial approach the underlying probability.

The proof here does not directly rely on Bernoulli's theorem,
but can also be given a probabilistic account.
* Consider a weighted coin which with probability `x` produces heads,
  and with probability `1-x` produces tails.
* The value of `bernstein n k x` is the probability that
  such a coin gives exactly `k` heads in a sequence of `n` tosses.
* If such an appearance of `k` heads results in a payoff of `f(k / n)`,
  the `n`-th Bernstein approximation for `f` evaluated at `x` is the expected payoff.
* The main estimate in the proof bounds the probability that
  the observed frequency of heads differs from `x` by more than some `Î´`,
  obtaining a bound of `(4 * n * Î´^2)â»Â¹`, irrespective of `x`.
* This ensures that for `n` large, the Bernstein approximation is (uniformly) close to the
  payoff function `f`.

(You don't need to think in these terms to follow the proof below: it's a giant `calc` block!)

This result proves Weierstrass' theorem that polynomials are dense in `C([0,1], â„)`,
although we defer an abstract statement of this until later.
-/

set_option linter.uppercaseLean3 false -- S

noncomputable section

open scoped Classical BigOperators BoundedContinuousFunction unitInterval

/-- The Bernstein polynomials, as continuous functions on `[0,1]`.
-/
def bernstein (n Î½ : â„•) : C(I, â„) :=
  (bernsteinPolynomial â„ n Î½).toContinuousMapOn I
#align bernstein bernstein

@[simp]
theorem bernstein_apply (n Î½ : â„•) (x : I) :
    bernstein n Î½ x = (n.choose Î½ : â„) * (x : â„) ^ Î½ * (1 - (x : â„)) ^ (n - Î½) := by
  dsimp [bernstein, Polynomial.toContinuousMapOn, Polynomial.toContinuousMap, bernsteinPolynomial]
  -- âŠ¢ Polynomial.eval (â†‘x) (â†‘(Nat.choose n Î½) * Polynomial.X ^ Î½ * (1 - Polynomial â€¦
  simp
  -- ğŸ‰ no goals
#align bernstein_apply bernstein_apply

theorem bernstein_nonneg {n Î½ : â„•} {x : I} : 0 â‰¤ bernstein n Î½ x := by
  simp only [bernstein_apply]
  -- âŠ¢ 0 â‰¤ â†‘(Nat.choose n Î½) * â†‘x ^ Î½ * (1 - â†‘x) ^ (n - Î½)
  have hâ‚ : (0:â„) â‰¤ x := by unit_interval
  -- âŠ¢ 0 â‰¤ â†‘(Nat.choose n Î½) * â†‘x ^ Î½ * (1 - â†‘x) ^ (n - Î½)
  have hâ‚‚ : (0:â„) â‰¤ 1 - x := by unit_interval
  -- âŠ¢ 0 â‰¤ â†‘(Nat.choose n Î½) * â†‘x ^ Î½ * (1 - â†‘x) ^ (n - Î½)
  positivity
  -- ğŸ‰ no goals
#align bernstein_nonneg bernstein_nonneg

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

@[positivity FunLike.coe _ _]
def evalBernstein : PositivityExt where eval {_ _} _zÎ± _pÎ± e := do
  let .app (.app _coe (.app (.app _ n) Î½)) x â† whnfR e | throwError "not bernstein polynomial"
  let p â† mkAppOptM ``bernstein_nonneg #[n, Î½, x]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

/-!
We now give a slight reformulation of `bernsteinPolynomial.variance`.
-/


namespace bernstein

/-- Send `k : Fin (n+1)` to the equally spaced points `k/n` in the unit interval.
-/
def z {n : â„•} (k : Fin (n + 1)) : I :=
  âŸ¨(k : â„) / n, by
    cases' n with n
    -- âŠ¢ â†‘â†‘k / â†‘Nat.zero âˆˆ I
    Â· norm_num
      -- ğŸ‰ no goals
    Â· have hâ‚ : 0 < (n.succ : â„) := by exact_mod_cast Nat.succ_pos _
      -- âŠ¢ â†‘â†‘k / â†‘(Nat.succ n) âˆˆ I
      have hâ‚‚ : â†‘k â‰¤ n.succ := by exact_mod_cast Fin.le_last k
      -- âŠ¢ â†‘â†‘k / â†‘(Nat.succ n) âˆˆ I
      rw [Set.mem_Icc, le_div_iff hâ‚, div_le_iff hâ‚]
      -- âŠ¢ 0 * â†‘(Nat.succ n) â‰¤ â†‘â†‘k âˆ§ â†‘â†‘k â‰¤ 1 * â†‘(Nat.succ n)
      norm_cast
      -- âŠ¢ 0 * Nat.succ n â‰¤ â†‘k âˆ§ â†‘k â‰¤ 1 * Nat.succ n
      simp [hâ‚‚]âŸ©
      -- ğŸ‰ no goals
#align bernstein.z bernstein.z

local postfix:90 "/â‚™" => z

theorem probability (n : â„•) (x : I) : (âˆ‘ k : Fin (n + 1), bernstein n k x) = 1 := by
  have := bernsteinPolynomial.sum â„ n
  -- âŠ¢ âˆ‘ k : Fin (n + 1), â†‘(bernstein n â†‘k) x = 1
  apply_fun fun p => Polynomial.aeval (x : â„) p at this
  -- âŠ¢ âˆ‘ k : Fin (n + 1), â†‘(bernstein n â†‘k) x = 1
  simp [AlgHom.map_sum, Finset.sum_range] at this
  -- âŠ¢ âˆ‘ k : Fin (n + 1), â†‘(bernstein n â†‘k) x = 1
  exact this
  -- ğŸ‰ no goals
#align bernstein.probability bernstein.probability

theorem variance {n : â„•} (h : 0 < (n : â„)) (x : I) :
    (âˆ‘ k : Fin (n + 1), (x - k/â‚™ : â„) ^ 2 * bernstein n k x) = (x : â„) * (1 - x) / n := by
  have h' : (n : â„) â‰  0 := ne_of_gt h
  -- âŠ¢ âˆ‘ k : Fin (n + 1), (â†‘x - â†‘(k/â‚™)) ^ 2 * â†‘(bernstein n â†‘k) x = â†‘x * (1 - â†‘x) / â€¦
  apply_fun fun x : â„ => x * n using GroupWithZero.mul_right_injective h'
  -- âŠ¢ (fun x => x * â†‘n) (âˆ‘ k : Fin (n + 1), (â†‘x - â†‘(k/â‚™)) ^ 2 * â†‘(bernstein n â†‘k)  â€¦
  apply_fun fun x : â„ => x * n using GroupWithZero.mul_right_injective h'
  -- âŠ¢ (fun x => x * â†‘n) ((fun x => x * â†‘n) (âˆ‘ k : Fin (n + 1), (â†‘x - â†‘(k/â‚™)) ^ 2 * â€¦
  dsimp
  -- âŠ¢ (âˆ‘ k : Fin (n + 1), (â†‘x - â†‘(k/â‚™)) ^ 2 * â†‘(bernstein n â†‘k) x) * â†‘n * â†‘n = â†‘x  â€¦
  conv_lhs => simp only [Finset.sum_mul, z]
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  conv_rhs => rw [div_mul_cancel _ h']
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  have := bernsteinPolynomial.variance â„ n
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  apply_fun fun p => Polynomial.aeval (x : â„) p at this
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  simp [AlgHom.map_sum, Finset.sum_range, â† Polynomial.nat_cast_mul] at this
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  convert this using 1
  -- âŠ¢ âˆ‘ x_1 : Fin (n + 1), (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n â€¦
  Â· congr 1; funext k
    -- âŠ¢ (fun x_1 => (â†‘x - â†‘â†‘x_1 / â†‘n) ^ 2 * â†‘(bernstein n â†‘x_1) x * â†‘n * â†‘n) = fun x â€¦
             -- âŠ¢ (â†‘x - â†‘â†‘k / â†‘n) ^ 2 * â†‘(bernstein n â†‘k) x * â†‘n * â†‘n = (â†‘n * â†‘x - â†‘â†‘k) ^ 2 *  â€¦
    rw [mul_comm _ (n : â„), mul_comm _ (n : â„), â† mul_assoc, â† mul_assoc]
    -- âŠ¢ â†‘n * â†‘n * (â†‘x - â†‘â†‘k / â†‘n) ^ 2 * â†‘(bernstein n â†‘k) x = (â†‘n * â†‘x - â†‘â†‘k) ^ 2 *  â€¦
    congr 1
    -- âŠ¢ â†‘n * â†‘n * (â†‘x - â†‘â†‘k / â†‘n) ^ 2 = (â†‘n * â†‘x - â†‘â†‘k) ^ 2
    field_simp [h]
    -- âŠ¢ â†‘n * â†‘n * (â†‘x * â†‘n - â†‘â†‘k) ^ 2 = (â†‘n * â†‘x - â†‘â†‘k) ^ 2 * â†‘n ^ 2
    ring
    -- ğŸ‰ no goals
  Â· ring
    -- ğŸ‰ no goals
#align bernstein.variance bernstein.variance

end bernstein

open bernstein

local postfix:1024 "/â‚™" => z

/-- The `n`-th approximation of a continuous function on `[0,1]` by Bernstein polynomials,
given by `âˆ‘ k, f (k/n) * bernstein n k x`.
-/
def bernsteinApproximation (n : â„•) (f : C(I, â„)) : C(I, â„) :=
  âˆ‘ k : Fin (n + 1), f k/â‚™ â€¢ bernstein n k
#align bernstein_approximation bernsteinApproximation

/-!
We now set up some of the basic machinery of the proof that the Bernstein approximations
converge uniformly.

A key player is the set `S f Îµ h n x`,
for some function `f : C(I, â„)`, `h : 0 < Îµ`, `n : â„•` and `x : I`.

This is the set of points `k` in `Fin (n+1)` such that
`k/n` is within `Î´` of `x`, where `Î´` is the modulus of uniform continuity for `f`,
chosen so `|f x - f y| < Îµ/2` when `|x - y| < Î´`.

We show that if `k âˆ‰ S`, then `1 â‰¤ Î´^-2 * (x - k/n)^2`.
-/


namespace bernsteinApproximation

@[simp]
theorem apply (n : â„•) (f : C(I, â„)) (x : I) :
    bernsteinApproximation n f x = âˆ‘ k : Fin (n + 1), f k/â‚™ * bernstein n k x := by
  simp [bernsteinApproximation]
  -- ğŸ‰ no goals
#align bernstein_approximation.apply bernsteinApproximation.apply

/-- The modulus of (uniform) continuity for `f`, chosen so `|f x - f y| < Îµ/2` when `|x - y| < Î´`.
-/
def Î´ (f : C(I, â„)) (Îµ : â„) (h : 0 < Îµ) : â„ :=
  f.modulus (Îµ / 2) (half_pos h)
#align bernstein_approximation.Î´ bernsteinApproximation.Î´

theorem Î´_pos {f : C(I, â„)} {Îµ : â„} {h : 0 < Îµ} : 0 < Î´ f Îµ h :=
  f.modulus_pos
#align bernstein_approximation.Î´_pos bernsteinApproximation.Î´_pos

/-- The set of points `k` so `k/n` is within `Î´` of `x`.
-/
def S (f : C(I, â„)) (Îµ : â„) (h : 0 < Îµ) (n : â„•) (x : I) : Finset (Fin (n + 1)) :=
  {k : Fin (n + 1) | dist k/â‚™ x < Î´ f Îµ h}.toFinset
#align bernstein_approximation.S bernsteinApproximation.S

/-- If `k âˆˆ S`, then `f(k/n)` is close to `f x`.
-/
theorem lt_of_mem_S {f : C(I, â„)} {Îµ : â„} {h : 0 < Îµ} {n : â„•} {x : I} {k : Fin (n + 1)}
    (m : k âˆˆ S f Îµ h n x) : |f k/â‚™ - f x| < Îµ / 2 := by
  apply f.dist_lt_of_dist_lt_modulus (Îµ / 2) (half_pos h)
  -- âŠ¢ dist k/â‚™ x < ContinuousMap.modulus f (Îµ / 2) (_ : 0 < Îµ / 2)
  -- Porting note: `simp` fails to apply `Set.mem_toFinset` on its own
  simpa [S, (Set.mem_toFinset)] using m
  -- ğŸ‰ no goals
#align bernstein_approximation.lt_of_mem_S bernsteinApproximation.lt_of_mem_S

/-- If `k âˆ‰ S`, then as `Î´ â‰¤ |x - k/n|`, we have the inequality `1 â‰¤ Î´^-2 * (x - k/n)^2`.
This particular formulation will be helpful later.
-/
theorem le_of_mem_S_compl {f : C(I, â„)} {Îµ : â„} {h : 0 < Îµ} {n : â„•} {x : I} {k : Fin (n + 1)}
    (m : k âˆˆ (S f Îµ h n x)á¶œ) : (1 : â„) â‰¤ Î´ f Îµ h ^ (-2 : â„¤) * ((x : â„) - k/â‚™) ^ 2 := by
  -- Porting note: added parentheses to help `simp`
  simp only [Finset.mem_compl, not_lt, (Set.mem_toFinset), Set.mem_setOf_eq, S] at m
  -- âŠ¢ 1 â‰¤ Î´ f Îµ h ^ (-2) * (â†‘x - â†‘k/â‚™) ^ 2
  rw [zpow_neg, â† div_eq_inv_mul, zpow_two, â† pow_two, one_le_div (pow_pos Î´_pos 2), sq_le_sq,
    abs_of_pos Î´_pos]
  rwa [dist_comm] at m
  -- ğŸ‰ no goals
#align bernstein_approximation.le_of_mem_S_compl bernsteinApproximation.le_of_mem_S_compl

end bernsteinApproximation

open bernsteinApproximation

open BoundedContinuousFunction

open Filter

open scoped Topology

/-- The Bernstein approximations
```
âˆ‘ k : Fin (n+1), f (k/n : â„) * n.choose k * x^k * (1-x)^(n-k)
```
for a continuous function `f : C([0,1], â„)` converge uniformly to `f` as `n` tends to infinity.

This is the proof given in [Richard Beals' *Analysis, an introduction*][beals-analysis], Â§7D,
and reproduced on wikipedia.
-/
theorem bernsteinApproximation_uniform (f : C(I, â„)) :
    Tendsto (fun n : â„• => bernsteinApproximation n f) atTop (ğ“ f) := by
  simp only [Metric.nhds_basis_ball.tendsto_right_iff, Metric.mem_ball, dist_eq_norm]
  -- âŠ¢ âˆ€ (i : â„), 0 < i â†’ âˆ€á¶  (x : â„•) in atTop, â€–bernsteinApproximation x f - fâ€– < i
  intro Îµ h
  -- âŠ¢ âˆ€á¶  (x : â„•) in atTop, â€–bernsteinApproximation x f - fâ€– < Îµ
  let Î´ := Î´ f Îµ h
  -- âŠ¢ âˆ€á¶  (x : â„•) in atTop, â€–bernsteinApproximation x f - fâ€– < Îµ
  have nhds_zero := tendsto_const_div_atTop_nhds_0_nat (2 * â€–fâ€– * Î´ ^ (-2 : â„¤))
  -- âŠ¢ âˆ€á¶  (x : â„•) in atTop, â€–bernsteinApproximation x f - fâ€– < Îµ
  filter_upwards [nhds_zero.eventually (gt_mem_nhds (half_pos h)), eventually_gt_atTop 0] with n nh
    npos'
  have npos : 0 < (n : â„) := by positivity
  -- âŠ¢ â€–bernsteinApproximation n f - fâ€– < Îµ
  have wâ‚‚ : 0 â‰¤ Î´ ^ (-2:â„¤) := zpow_neg_two_nonneg _ -- TODO: need a positivity extension for `zpow`
  -- âŠ¢ â€–bernsteinApproximation n f - fâ€– < Îµ
  -- As `[0,1]` is compact, it suffices to check the inequality pointwise.
  rw [ContinuousMap.norm_lt_iff _ h]
  -- âŠ¢ âˆ€ (x : â†‘I), â€–â†‘(bernsteinApproximation n f - f) xâ€– < Îµ
  intro x
  -- âŠ¢ â€–â†‘(bernsteinApproximation n f - f) xâ€– < Îµ
  -- The idea is to split up the sum over `k` into two sets,
  -- `S`, where `x - k/n < Î´`, and its complement.
  let S := S f Îµ h n x
  -- âŠ¢ â€–â†‘(bernsteinApproximation n f - f) xâ€– < Îµ
  calc
    |(bernsteinApproximation n f - f) x| = |bernsteinApproximation n f x - f x| := rfl
    _ = |bernsteinApproximation n f x - f x * 1| := by rw [mul_one]
    _ = |bernsteinApproximation n f x - f x * âˆ‘ k : Fin (n + 1), bernstein n k x| := by
      rw [bernstein.probability]
    _ = |âˆ‘ k : Fin (n + 1), (f k/â‚™ - f x) * bernstein n k x| := by
      simp [bernsteinApproximation, Finset.mul_sum, sub_mul]
    _ â‰¤ âˆ‘ k : Fin (n + 1), |(f k/â‚™ - f x) * bernstein n k x| := (Finset.abs_sum_le_sum_abs _ _)
    _ = âˆ‘ k : Fin (n + 1), |f k/â‚™ - f x| * bernstein n k x := by
      simp_rw [abs_mul, abs_eq_self.mpr bernstein_nonneg]
    _ = (âˆ‘ k in S, |f k/â‚™ - f x| * bernstein n k x) + âˆ‘ k in Sá¶œ, |f k/â‚™ - f x| * bernstein n k x :=
      (S.sum_add_sum_compl _).symm
    -- We'll now deal with the terms in `S` and the terms in `Sá¶œ` in separate calc blocks.
    _ < Îµ / 2 + Îµ / 2 :=
      (add_lt_add_of_le_of_lt ?_ ?_)
    _ = Îµ := add_halves Îµ
  Â· -- We now work on the terms in `S`: uniform continuity and `bernstein.probability`
    -- quickly give us a bound.
    calc
      âˆ‘ k in S, |f k/â‚™ - f x| * bernstein n k x â‰¤ âˆ‘ k in S, Îµ / 2 * bernstein n k x := by
        gcongr with _ m
        exact le_of_lt (lt_of_mem_S m)
      _ = Îµ / 2 * âˆ‘ k in S, bernstein n k x := by rw [Finset.mul_sum]
      -- In this step we increase the sum over `S` back to a sum over all of `Fin (n+1)`,
      -- so that we can use `bernstein.probability`.
      _ â‰¤ Îµ / 2 * âˆ‘ k : Fin (n + 1), bernstein n k x := by
        gcongr
        exact Finset.sum_le_univ_sum_of_nonneg fun k => bernstein_nonneg
      _ = Îµ / 2 := by rw [bernstein.probability, mul_one]
  Â· -- We now turn to working on `Sá¶œ`: we control the difference term just using `â€–fâ€–`,
    -- and then insert a `Î´^(-2) * (x - k/n)^2` factor
    -- (which is at least one because we are not in `S`).
    calc
      âˆ‘ k in Sá¶œ, |f k/â‚™ - f x| * bernstein n k x â‰¤ âˆ‘ k in Sá¶œ, 2 * â€–fâ€– * bernstein n k x := by
        gcongr
        apply f.dist_le_two_norm
      _ = 2 * â€–fâ€– * âˆ‘ k in Sá¶œ, bernstein n k x := by rw [Finset.mul_sum]
      _ â‰¤ 2 * â€–fâ€– * âˆ‘ k in Sá¶œ, Î´ ^ (-2 : â„¤) * ((x : â„) - k/â‚™) ^ 2 * bernstein n k x := by
        gcongr with _ m
        conv_lhs => rw [â† one_mul (bernstein _ _ _)]
        gcongr
        exact le_of_mem_S_compl m
      -- Again enlarging the sum from `Sá¶œ` to all of `Fin (n+1)`
      _ â‰¤ 2 * â€–fâ€– * âˆ‘ k : Fin (n + 1), Î´ ^ (-2 : â„¤) * ((x : â„) - k/â‚™) ^ 2 * bernstein n k x := by
        gcongr
        refine Finset.sum_le_univ_sum_of_nonneg <| fun k => ?_
        positivity
      _ = 2 * â€–fâ€– * Î´ ^ (-2 : â„¤) * âˆ‘ k : Fin (n + 1), ((x : â„) - k/â‚™) ^ 2 * bernstein n k x := by
        conv_rhs =>
          rw [mul_assoc, Finset.mul_sum]
          simp only [â† mul_assoc]
      -- `bernstein.variance` and `x âˆˆ [0,1]` gives the uniform bound
      _ = 2 * â€–fâ€– * Î´ ^ (-2 : â„¤) * x * (1 - x) / n := by rw [variance npos]; ring
      _ â‰¤ 2 * â€–fâ€– * Î´ ^ (-2 : â„¤) * 1 * 1 / n := by gcongr <;> unit_interval
      _ < Îµ / 2 := by simp only [mul_one]; exact nh
#align bernstein_approximation_uniform bernsteinApproximation_uniform
