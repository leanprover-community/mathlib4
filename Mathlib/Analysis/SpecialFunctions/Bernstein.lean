/-
Copyright (c) 2021 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison
-/
import Mathlib.Algebra.BigOperators.Field
import Mathlib.Analysis.Convex.Gauge
import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.RingTheory.Polynomial.Bernstein
import Mathlib.Topology.Algebra.Module.LocallyConvex
import Mathlib.Topology.ContinuousMap.Polynomial

/-!
# Bernstein approximations and Weierstrass' theorem

We prove that the Bernstein approximations
```
‚àë k : Fin (n+1), (n.choose k * x^k * (1-x)^(n-k)) ‚Ä¢ f (k/n : ‚Ñù)
```
for a continuous function `f : C([0,1], E)` taking values in a locally convex vector space
converge uniformly to `f` as `n` tends to infinity.
This statement directly applies to the cases when the codomain is a (semi)normed space
or, more generally, has a topology defined by a family of seminorms.

Our proof follows [Richard Beals' *Analysis, an introduction*][beals-analysis], ¬ß7D.
The original proof, due to [Bernstein](bernstein1912) in 1912, is probabilistic,
and relies on Bernoulli's theorem,
which gives bounds for how quickly the observed frequencies in a
Bernoulli trial approach the underlying probability.

The proof here does not directly rely on Bernoulli's theorem,
but can also be given a probabilistic account.
* Consider a weighted coin which with probability `x` produces heads,
  and with probability `1-x` produces tails.
* The value of `bernstein n k x` is the probability that
  such a coin gives exactly `k` heads in a sequence of `n` tosses.
* If such an appearance of `k` heads results in a payoff of `f(k / n)`,
  the `n`-th Bernstein approximation for `f` evaluated at `x` is the expected payoff.
* The main estimate in the proof bounds the probability that
  the observed frequency of heads differs from `x` by more than some `Œ¥`,
  obtaining a bound of `(4 * n * Œ¥^2)‚Åª¬π`, irrespective of `x`.
* This ensures that for `n` large, the Bernstein approximation is (uniformly) close to the
  payoff function `f`.

(You don't need to think in these terms to follow the proof below: it's a giant `calc` block!)

This result proves Weierstrass' theorem that polynomials are dense in `C([0,1], ‚Ñù)`,
although we defer an abstract statement of this until later.
-/

noncomputable section

open Filter
open scoped unitInterval Topology Uniformity

/-- The Bernstein polynomials, as continuous functions on `[0,1]`.
-/
def bernstein (n ŒΩ : ‚Ñï) : C(I, ‚Ñù) :=
  (bernsteinPolynomial ‚Ñù n ŒΩ).toContinuousMapOn I

theorem bernstein_apply (n ŒΩ : ‚Ñï) (x : I) :
    bernstein n ŒΩ x = (n.choose ŒΩ : ‚Ñù) * (x : ‚Ñù) ^ ŒΩ * (1 - (x : ‚Ñù)) ^ (n - ŒΩ) := by
  dsimp [bernstein, Polynomial.toContinuousMapOn, Polynomial.toContinuousMap, bernsteinPolynomial]
  simp

@[simp]
theorem bernstein_nonneg {n ŒΩ : ‚Ñï} {x : I} : 0 ‚â§ bernstein n ŒΩ x := by
  simp only [bernstein_apply]
  have h‚ÇÅ : (0 : ‚Ñù) ‚â§ x := by unit_interval
  have h‚ÇÇ : (0 : ‚Ñù) ‚â§ 1 - x := by unit_interval
  positivity

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension of the `positivity` tactic for Bernstein polynomials: they are always non-negative. -/
@[positivity DFunLike.coe (bernstein _ _) _]
def evalBernstein : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app (.app _coe (.app (.app _ n) ŒΩ)) x ‚Üê whnfR e | throwError "not bernstein polynomial"
  let p ‚Üê mkAppOptM ``bernstein_nonneg #[n, ŒΩ, x]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

/-!
We now give a slight reformulation of `bernsteinPolynomial.variance`.
-/


namespace bernstein

/-- Send `k : Fin (n+1)` to the equally spaced points `k/n` in the unit interval.
-/
def z {n : ‚Ñï} (k : Fin (n + 1)) : I :=
  ‚ü®(k : ‚Ñù) / n, by simp [div_nonneg, div_le_one_of_le‚ÇÄ, k.is_le]‚ü©

local postfix:90 "/‚Çô" => z

@[simp] lemma z_zero {n : ‚Ñï} : (0 : Fin (n + 1))/‚Çô = 0 := by simp [z]
@[simp] lemma z_last {n : ‚Ñï} (hn : n ‚â† 0) : .last n/‚Çô = 1 := by simp [z, hn]

@[simp]
theorem probability (n : ‚Ñï) (x : I) : (‚àë k : Fin (n + 1), bernstein n k x) = 1 := by
  have := bernsteinPolynomial.sum ‚Ñù n
  apply_fun fun p => Polynomial.aeval (x : ‚Ñù) p at this
  simp? [map_sum, Finset.sum_range] at this says
    simp only [Finset.sum_range, map_sum, Polynomial.coe_aeval_eq_eval, Polynomial.eval_one] at this
  exact this

theorem variance {n : ‚Ñï} (hn : n ‚â† 0) (x : I) :
    (‚àë k : Fin (n + 1), (x - k/‚Çô : ‚Ñù) ^ 2 * bernstein n k x) = (x : ‚Ñù) * (1 - x) / n := by
  convert congr(Polynomial.aeval (x : ‚Ñù) $(bernsteinPolynomial.variance ‚Ñù n) / n ^ 2) using 1
  ¬∑ simp [z, bernstein_apply, Finset.sum_range, bernsteinPolynomial]
    field_simp
    rw [‚Üê Finset.sum_div]
    field_simp
  ¬∑ simp
    field_simp

end bernstein

open bernstein

local postfix:1024 "/‚Çô" => z

variable {E : Type*} [AddCommGroup E] [TopologicalSpace E] [IsTopologicalAddGroup E]
  [Module ‚Ñù E] [ContinuousSMul ‚Ñù E]

/-- The `n`-th approximation of a continuous function on `[0,1]` by Bernstein polynomials,
given by `‚àë k, bernstein n k x ‚Ä¢ f (k/n)`.
-/
def bernsteinApproximation (n : ‚Ñï) (f : C(I, E)) : C(I, E) :=
  ‚àë k : Fin (n + 1), bernstein n k ‚Ä¢ .const _ (f k/‚Çô)

/-!
We now set up some of the basic machinery of the proof that the Bernstein approximations
converge uniformly.

A key player is the set `S f Œµ h n x`,
for some function `f : C(I, E)`, `h : 0 < Œµ`, `n : ‚Ñï` and `x : I`.

This is the set of points `k` in `Fin (n+1)` such that
`k/n` is within `Œ¥` of `x`, where `Œ¥` is the modulus of uniform continuity for `f`,
chosen so `‚Äñf x - f y‚Äñ < Œµ/2` when `|x - y| < Œ¥`.

We show that if `k ‚àâ S`, then `1 ‚â§ Œ¥^-2 * (x - k/n)^2`.
-/

namespace bernsteinApproximation

theorem apply (n : ‚Ñï) (f : C(I, E)) (x : I) :
    bernsteinApproximation n f x = ‚àë k : Fin (n + 1), bernstein n k x ‚Ä¢ f k/‚Çô := by
  simp [bernsteinApproximation]

@[simp]
theorem apply_zero (n : ‚Ñï) (f : C(I, E)) : bernsteinApproximation n f 0 = f 0 := by
  simp [apply, Fin.sum_univ_succ, bernstein_apply, z]

@[simp]
theorem apply_one {n : ‚Ñï} (hn : n ‚â† 0) (f : C(I, E)) : bernsteinApproximation n f 1 = f 1 := by
  simp [apply, Fin.sum_univ_castSucc, bernstein_apply, hn, Nat.sub_eq_zero_iff_le]

end bernsteinApproximation

open bernsteinApproximation

/-- The Bernstein approximations
```
‚àë k : Fin (n+1), f (k/n : ‚Ñù) * n.choose k * x^k * (1-x)^(n-k)
```
for a continuous function `f : C([0,1], ‚Ñù)` converge uniformly to `f` as `n` tends to infinity.

This is the proof given in [Richard Beals' *Analysis, an introduction*][beals-analysis], ¬ß7D,
and reproduced on wikipedia.
-/
theorem bernsteinApproximation_uniform [LocallyConvexSpace ‚Ñù E] (f : C(I, E)) :
    Tendsto (fun n : ‚Ñï => bernsteinApproximation n f) atTop (ùìù f) := by
  letI : UniformSpace E := IsTopologicalAddGroup.toUniformSpace E
  have : IsUniformAddGroup E := isUniformAddGroup_of_addCommGroup
  /- Topology on a locally convex TVS is given by a family of seminorms `‚Äñx‚Äñ_U = gauge U x`,
  where the open symmetric convex sets `U` form a basis of neighborhoods in this topology,
  and are the open unit balls for the corresponding seminorms.
  For technical reasons, we neither assume `U`s to be open, nor symmetric. -/
  suffices ‚àÄ U ‚àà ùìù (0 : E), Convex ‚Ñù U ‚Üí
      ‚àÄ·∂† n in atTop, ‚àÄ x : I, gauge U (bernsteinApproximation n f x - f x) < 1 by
    rw [(LocallyConvexSpace.convex_basis_zero ‚Ñù E).uniformity_of_nhds_zero_swapped
      |>.compactConvergenceUniformity_of_compact |> nhds_basis_uniformity |>.tendsto_right_iff]
    rintro U ‚ü®hU‚ÇÄ, hcU‚ü©
    filter_upwards [this U hU‚ÇÄ hcU] with n hn x
    exact gauge_lt_one_subset_self hcU (mem_of_mem_nhds hU‚ÇÄ) (absorbent_nhds_zero hU‚ÇÄ) (hn x)
  intro U hU‚ÇÄ hUc
  /- Choose a constant `C` such that `‚Äñf x - f y‚Äñ_U ‚â§ C` for all `x`, `y`.
  For a normed space, this would be twice the norm of `f`. -/
  obtain ‚ü®C, hC‚ü© : ‚àÉ C, ‚àÄ x y, gauge U (f x - f y) ‚â§ C := by
    have : Continuous fun (x, y) ‚Ü¶ gauge U (f x - f y) := by fun_prop (disch := assumption)
    simpa only [BddAbove, Set.Nonempty, mem_upperBounds, Set.forall_mem_range, Prod.forall]
      using isCompact_range this |>.bddAbove
  have hC‚ÇÄ : 0 ‚â§ C := le_trans (gauge_nonneg _) (hC 0 0)
  /- Use uniform continuity of `f` to hcoose `Œ¥ > 0` such that `‚Äñf x - f y‚Äñ_U < 1 / 2`
  whenever `dist x y < Œ¥`. -/
  obtain ‚ü®Œ¥, hŒ¥‚ÇÄ, hŒ¥‚ü© : ‚àÉ Œ¥ > 0, ‚àÄ x y : I, dist x y < Œ¥ ‚Üí gauge U (f x - f y) < 1 / 2 := by
    have := CompactSpace.uniformContinuous_of_continuous (map_continuous f)
    rw [Metric.uniformity_basis_dist.uniformContinuous_iff
      (basis_sets _).uniformity_of_nhds_zero_swapped] at this
    exact this {z | gauge U z < 1 / 2} <| tendsto_gauge_nhds_zero hU‚ÇÄ
      |>.eventually_lt_const <| by positivity
  -- Take `n ‚â† 0` such that `C / Œ¥ ^ 2 / n < 1 / 2`.
  have nhds_zero := tendsto_const_div_atTop_nhds_zero_nat (C / Œ¥ ^ 2)
  filter_upwards [nhds_zero.eventually_lt_const (half_pos one_pos), eventually_ne_atTop 0]
    with n nh hn‚ÇÄ x
  -- The idea is to split up the sum over `k` into two sets,
  -- `S`, where `x - k/n < Œ¥`, and its complement.
  set S : Finset (Fin (n + 1)) := {k : Fin (n + 1) | dist k/‚Çô x < Œ¥}
  calc
    gauge U (bernsteinApproximation n f x - f x)
      = gauge U (‚àë k : Fin (n + 1), bernstein n k x ‚Ä¢ (f k/‚Çô - f x)) := by
      simp [bernsteinApproximation.apply, smul_sub, ‚Üê Finset.sum_smul]
    _ ‚â§ ‚àë k : Fin (n + 1), gauge U (bernstein n k x ‚Ä¢ (f k/‚Çô - f x)) :=
      gauge_sum_le hUc (absorbent_nhds_zero hU‚ÇÄ) _ _
    _ = ‚àë k : Fin (n + 1), bernstein n k x * gauge U (f k/‚Çô - f x) := by
      simp only [gauge_smul_of_nonneg, bernstein_nonneg, smul_eq_mul]
    _ = (‚àë k ‚àà S, bernstein n k x * gauge U (f k/‚Çô - f x)) +
          ‚àë k ‚àà S·∂ú, bernstein n k x * gauge U (f k/‚Çô - f x) :=
      (S.sum_add_sum_compl _).symm
    -- We'll now deal with the terms in `S` and the terms in `S·∂ú` in separate calc blocks.
    _ < 1 / 2 + 1 / 2 := add_lt_add_of_le_of_lt ?_ ?_
    _ = 1 := add_halves 1
  ¬∑ -- We now work on the terms in `S`: uniform continuity and `bernstein.probability`
    -- quickly give us a bound.
    calc
      ‚àë k ‚àà S, bernstein n k x * gauge U (f k/‚Çô - f x) ‚â§ ‚àë k ‚àà S, bernstein n k x * (1 / 2) := by
        gcongr with k hk
        refine (hŒ¥ _ _ ?_).le
        simpa [S] using hk
      _ = 1 / 2 * ‚àë k ‚àà S, bernstein n k x := by rw [mul_comm, Finset.sum_mul]
      -- In this step we increase the sum over `S` back to a sum over all of `Fin (n+1)`,
      -- so that we can use `bernstein.probability`.
      _ ‚â§ 1 / 2 * ‚àë k : Fin (n + 1), bernstein n k x := by gcongr; exact S.subset_univ
      _ = 1 / 2 := by rw [bernstein.probability, mul_one]
  ¬∑ -- We now turn to working on `S·∂ú`: we control the difference term just using `C`,
    -- and then insert a `(x - k/n)^2 / Œ¥^2` factor
    -- (which is at least one because we are not in `S`).
    calc
      ‚àë k ‚àà S·∂ú, bernstein n k x * gauge U (f k/‚Çô - f x) ‚â§ ‚àë k ‚àà S·∂ú, C * bernstein n k x := by
        simp only [mul_comm (bernstein n _ x)]
        gcongr
        apply hC
      _ = C * ‚àë k ‚àà S·∂ú, bernstein n k x := by rw [Finset.mul_sum]
      _ ‚â§ C * ‚àë k ‚àà S·∂ú, ((x : ‚Ñù) - k/‚Çô) ^ 2 / Œ¥ ^ 2 * bernstein n k x := by
        gcongr with k hk
        conv_lhs => rw [‚Üê one_mul (bernstein _ _ _)]
        gcongr
        simpa [one_le_div‚ÇÄ, hŒ¥‚ÇÄ, sq_le_sq, S, abs_of_pos, ‚Üê Real.dist_eq, dist_comm (x : ‚Ñù)]
          using hk
      -- Again enlarging the sum from `S·∂ú` to all of `Fin (n+1)`
      _ ‚â§ C * ‚àë k : Fin (n + 1), ((x : ‚Ñù) - k/‚Çô) ^ 2 / Œ¥ ^ 2 * bernstein n k x := by
        gcongr; exact S·∂ú.subset_univ
      _ = C * (‚àë k : Fin (n + 1), ((x : ‚Ñù) - k/‚Çô) ^ 2 * bernstein n k x) / Œ¥ ^ 2 := by
        simp only [‚Üê mul_div_right_comm, ‚Üê mul_div_assoc, ‚Üê Finset.sum_div]
      -- `bernstein.variance` and `x ‚àà [0,1]` gives the uniform bound
      _ = C / Œ¥ ^ 2 * x * (1 - x) / n := by rw [variance hn‚ÇÄ]; ring
      _ ‚â§ C / Œ¥ ^ 2 * 1 * 1 / n := by gcongr <;> unit_interval
      _ < 1 / 2 := by simpa only [mul_one]
