/-
Copyright (c) 2021 Scott Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Scott Morrison
-/
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.RingTheory.Polynomial.Bernstein
import Mathlib.Topology.ContinuousFunction.Polynomial
import Mathlib.Topology.ContinuousFunction.Compact

#align_import analysis.special_functions.bernstein from "leanprover-community/mathlib"@"2c1d8ca2812b64f88992a5294ea3dba144755cd1"

/-!
# Bernstein approximations and Weierstrass' theorem

We prove that the Bernstein approximations
```
‚àë k : Fin (n+1), f (k/n : ‚Ñù) * n.choose k * x^k * (1-x)^(n-k)
```
for a continuous function `f : C([0,1], ‚Ñù)` converge uniformly to `f` as `n` tends to infinity.

Our proof follows [Richard Beals' *Analysis, an introduction*][beals-analysis], ¬ß7D.
The original proof, due to [Bernstein](bernstein1912) in 1912, is probabilistic,
and relies on Bernoulli's theorem,
which gives bounds for how quickly the observed frequencies in a
Bernoulli trial approach the underlying probability.

The proof here does not directly rely on Bernoulli's theorem,
but can also be given a probabilistic account.
* Consider a weighted coin which with probability `x` produces heads,
  and with probability `1-x` produces tails.
* The value of `bernstein n k x` is the probability that
  such a coin gives exactly `k` heads in a sequence of `n` tosses.
* If such an appearance of `k` heads results in a payoff of `f(k / n)`,
  the `n`-th Bernstein approximation for `f` evaluated at `x` is the expected payoff.
* The main estimate in the proof bounds the probability that
  the observed frequency of heads differs from `x` by more than some `Œ¥`,
  obtaining a bound of `(4 * n * Œ¥^2)‚Åª¬π`, irrespective of `x`.
* This ensures that for `n` large, the Bernstein approximation is (uniformly) close to the
  payoff function `f`.

(You don't need to think in these terms to follow the proof below: it's a giant `calc` block!)

This result proves Weierstrass' theorem that polynomials are dense in `C([0,1], ‚Ñù)`,
although we defer an abstract statement of this until later.
-/

set_option linter.uppercaseLean3 false -- S

noncomputable section

open scoped Classical BoundedContinuousFunction unitInterval

/-- The Bernstein polynomials, as continuous functions on `[0,1]`.
-/
def bernstein (n ŒΩ : ‚Ñï) : C(I, ‚Ñù) :=
  (bernsteinPolynomial ‚Ñù n ŒΩ).toContinuousMapOn I
#align bernstein bernstein

@[simp]
theorem bernstein_apply (n ŒΩ : ‚Ñï) (x : I) :
    bernstein n ŒΩ x = (n.choose ŒΩ : ‚Ñù) * (x : ‚Ñù) ^ ŒΩ * (1 - (x : ‚Ñù)) ^ (n - ŒΩ) := by
  dsimp [bernstein, Polynomial.toContinuousMapOn, Polynomial.toContinuousMap, bernsteinPolynomial]
  simp
#align bernstein_apply bernstein_apply

theorem bernstein_nonneg {n ŒΩ : ‚Ñï} {x : I} : 0 ‚â§ bernstein n ŒΩ x := by
  simp only [bernstein_apply]
  have h‚ÇÅ : (0:‚Ñù) ‚â§ x := by unit_interval
  have h‚ÇÇ : (0:‚Ñù) ‚â§ 1 - x := by unit_interval
  positivity
#align bernstein_nonneg bernstein_nonneg

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension of the `positivity` tactic for Bernstein polynomials: they are always non-negative. -/
@[positivity DFunLike.coe _ _]
def evalBernstein : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app (.app _coe (.app (.app _ n) ŒΩ)) x ‚Üê whnfR e | throwError "not bernstein polynomial"
  let p ‚Üê mkAppOptM ``bernstein_nonneg #[n, ŒΩ, x]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

/-!
We now give a slight reformulation of `bernsteinPolynomial.variance`.
-/


namespace bernstein

/-- Send `k : Fin (n+1)` to the equally spaced points `k/n` in the unit interval.
-/
def z {n : ‚Ñï} (k : Fin (n + 1)) : I :=
  ‚ü®(k : ‚Ñù) / n, by
    cases' n with n
    ¬∑ norm_num
    ¬∑ have h‚ÇÅ : 0 < (n.succ : ‚Ñù) := mod_cast Nat.succ_pos _
      have h‚ÇÇ : ‚Üëk ‚â§ n.succ := mod_cast Fin.le_last k
      rw [Set.mem_Icc, le_div_iff h‚ÇÅ, div_le_iff h‚ÇÅ]
      norm_cast
      simp [h‚ÇÇ]‚ü©
#align bernstein.z bernstein.z

local postfix:90 "/‚Çô" => z

theorem probability (n : ‚Ñï) (x : I) : (‚àë k : Fin (n + 1), bernstein n k x) = 1 := by
  have := bernsteinPolynomial.sum ‚Ñù n
  apply_fun fun p => Polynomial.aeval (x : ‚Ñù) p at this
  simp? [AlgHom.map_sum, Finset.sum_range] at this says
    simp only [Finset.sum_range, map_sum, Polynomial.coe_aeval_eq_eval, map_one] at this
  exact this
#align bernstein.probability bernstein.probability

theorem variance {n : ‚Ñï} (h : 0 < (n : ‚Ñù)) (x : I) :
    (‚àë k : Fin (n + 1), (x - k/‚Çô : ‚Ñù) ^ 2 * bernstein n k x) = (x : ‚Ñù) * (1 - x) / n := by
  have h' : (n : ‚Ñù) ‚â† 0 := ne_of_gt h
  apply_fun fun x : ‚Ñù => x * n using GroupWithZero.mul_right_injective h'
  apply_fun fun x : ‚Ñù => x * n using GroupWithZero.mul_right_injective h'
  dsimp
  conv_lhs => simp only [Finset.sum_mul, z]
  conv_rhs => rw [div_mul_cancel‚ÇÄ _ h']
  have := bernsteinPolynomial.variance ‚Ñù n
  apply_fun fun p => Polynomial.aeval (x : ‚Ñù) p at this
  simp? [AlgHom.map_sum, Finset.sum_range, ‚Üê Polynomial.natCast_mul] at this says
    simp only [nsmul_eq_mul, Finset.sum_range, map_sum, map_mul, map_pow, map_sub, map_natCast,
      Polynomial.aeval_X, Polynomial.coe_aeval_eq_eval, map_one] at this
  convert this using 1
  ¬∑ congr 1; funext k
    rw [mul_comm _ (n : ‚Ñù), mul_comm _ (n : ‚Ñù), ‚Üê mul_assoc, ‚Üê mul_assoc]
    congr 1
    field_simp [h]
    ring
  ¬∑ ring
#align bernstein.variance bernstein.variance

end bernstein

open bernstein

local postfix:1024 "/‚Çô" => z

/-- The `n`-th approximation of a continuous function on `[0,1]` by Bernstein polynomials,
given by `‚àë k, f (k/n) * bernstein n k x`.
-/
def bernsteinApproximation (n : ‚Ñï) (f : C(I, ‚Ñù)) : C(I, ‚Ñù) :=
  ‚àë k : Fin (n + 1), f k/‚Çô ‚Ä¢ bernstein n k
#align bernstein_approximation bernsteinApproximation

/-!
We now set up some of the basic machinery of the proof that the Bernstein approximations
converge uniformly.

A key player is the set `S f Œµ h n x`,
for some function `f : C(I, ‚Ñù)`, `h : 0 < Œµ`, `n : ‚Ñï` and `x : I`.

This is the set of points `k` in `Fin (n+1)` such that
`k/n` is within `Œ¥` of `x`, where `Œ¥` is the modulus of uniform continuity for `f`,
chosen so `|f x - f y| < Œµ/2` when `|x - y| < Œ¥`.

We show that if `k ‚àâ S`, then `1 ‚â§ Œ¥^-2 * (x - k/n)^2`.
-/


namespace bernsteinApproximation

@[simp]
theorem apply (n : ‚Ñï) (f : C(I, ‚Ñù)) (x : I) :
    bernsteinApproximation n f x = ‚àë k : Fin (n + 1), f k/‚Çô * bernstein n k x := by
  simp [bernsteinApproximation]
#align bernstein_approximation.apply bernsteinApproximation.apply

/-- The modulus of (uniform) continuity for `f`, chosen so `|f x - f y| < Œµ/2` when `|x - y| < Œ¥`.
-/
def Œ¥ (f : C(I, ‚Ñù)) (Œµ : ‚Ñù) (h : 0 < Œµ) : ‚Ñù :=
  f.modulus (Œµ / 2) (half_pos h)
#align bernstein_approximation.Œ¥ bernsteinApproximation.Œ¥

theorem Œ¥_pos {f : C(I, ‚Ñù)} {Œµ : ‚Ñù} {h : 0 < Œµ} : 0 < Œ¥ f Œµ h :=
  f.modulus_pos
#align bernstein_approximation.Œ¥_pos bernsteinApproximation.Œ¥_pos

/-- The set of points `k` so `k/n` is within `Œ¥` of `x`.
-/
def S (f : C(I, ‚Ñù)) (Œµ : ‚Ñù) (h : 0 < Œµ) (n : ‚Ñï) (x : I) : Finset (Fin (n + 1)) :=
  {k : Fin (n + 1) | dist k/‚Çô x < Œ¥ f Œµ h}.toFinset
#align bernstein_approximation.S bernsteinApproximation.S

/-- If `k ‚àà S`, then `f(k/n)` is close to `f x`.
-/
theorem lt_of_mem_S {f : C(I, ‚Ñù)} {Œµ : ‚Ñù} {h : 0 < Œµ} {n : ‚Ñï} {x : I} {k : Fin (n + 1)}
    (m : k ‚àà S f Œµ h n x) : |f k/‚Çô - f x| < Œµ / 2 := by
  apply f.dist_lt_of_dist_lt_modulus (Œµ / 2) (half_pos h)
  -- Porting note: `simp` fails to apply `Set.mem_toFinset` on its own
  simpa [S, (Set.mem_toFinset)] using m
#align bernstein_approximation.lt_of_mem_S bernsteinApproximation.lt_of_mem_S

/-- If `k ‚àâ S`, then as `Œ¥ ‚â§ |x - k/n|`, we have the inequality `1 ‚â§ Œ¥^-2 * (x - k/n)^2`.
This particular formulation will be helpful later.
-/
theorem le_of_mem_S_compl {f : C(I, ‚Ñù)} {Œµ : ‚Ñù} {h : 0 < Œµ} {n : ‚Ñï} {x : I} {k : Fin (n + 1)}
    (m : k ‚àà (S f Œµ h n x)·∂ú) : (1 : ‚Ñù) ‚â§ Œ¥ f Œµ h ^ (-2 : ‚Ñ§) * ((x : ‚Ñù) - k/‚Çô) ^ 2 := by
  -- Porting note: added parentheses to help `simp`
  simp only [Finset.mem_compl, not_lt, Set.mem_toFinset, Set.mem_setOf_eq, S] at m
  rw [zpow_neg, ‚Üê div_eq_inv_mul, zpow_two, ‚Üê pow_two, one_le_div (pow_pos Œ¥_pos 2), sq_le_sq,
    abs_of_pos Œ¥_pos]
  rwa [dist_comm] at m
#align bernstein_approximation.le_of_mem_S_compl bernsteinApproximation.le_of_mem_S_compl

end bernsteinApproximation

open bernsteinApproximation

open BoundedContinuousFunction

open Filter

open scoped Topology

/-- The Bernstein approximations
```
‚àë k : Fin (n+1), f (k/n : ‚Ñù) * n.choose k * x^k * (1-x)^(n-k)
```
for a continuous function `f : C([0,1], ‚Ñù)` converge uniformly to `f` as `n` tends to infinity.

This is the proof given in [Richard Beals' *Analysis, an introduction*][beals-analysis], ¬ß7D,
and reproduced on wikipedia.
-/
theorem bernsteinApproximation_uniform (f : C(I, ‚Ñù)) :
    Tendsto (fun n : ‚Ñï => bernsteinApproximation n f) atTop (ùìù f) := by
  simp only [Metric.nhds_basis_ball.tendsto_right_iff, Metric.mem_ball, dist_eq_norm]
  intro Œµ h
  let Œ¥ := Œ¥ f Œµ h
  have nhds_zero := tendsto_const_div_atTop_nhds_zero_nat (2 * ‚Äñf‚Äñ * Œ¥ ^ (-2 : ‚Ñ§))
  filter_upwards [nhds_zero.eventually (gt_mem_nhds (half_pos h)), eventually_gt_atTop 0] with n nh
    npos'
  have npos : 0 < (n : ‚Ñù) := by positivity
  -- As `[0,1]` is compact, it suffices to check the inequality pointwise.
  rw [ContinuousMap.norm_lt_iff _ h]
  intro x
  -- The idea is to split up the sum over `k` into two sets,
  -- `S`, where `x - k/n < Œ¥`, and its complement.
  let S := S f Œµ h n x
  calc
    |(bernsteinApproximation n f - f) x| = |bernsteinApproximation n f x - f x| := rfl
    _ = |bernsteinApproximation n f x - f x * 1| := by rw [mul_one]
    _ = |bernsteinApproximation n f x - f x * ‚àë k : Fin (n + 1), bernstein n k x| := by
      rw [bernstein.probability]
    _ = |‚àë k : Fin (n + 1), (f k/‚Çô - f x) * bernstein n k x| := by
      simp [bernsteinApproximation, Finset.mul_sum, sub_mul]
    _ ‚â§ ‚àë k : Fin (n + 1), |(f k/‚Çô - f x) * bernstein n k x| := Finset.abs_sum_le_sum_abs _ _
    _ = ‚àë k : Fin (n + 1), |f k/‚Çô - f x| * bernstein n k x := by
      simp_rw [abs_mul, abs_eq_self.mpr bernstein_nonneg]
    _ = (‚àë k ‚àà S, |f k/‚Çô - f x| * bernstein n k x) + ‚àë k ‚àà S·∂ú, |f k/‚Çô - f x| * bernstein n k x :=
      (S.sum_add_sum_compl _).symm
    -- We'll now deal with the terms in `S` and the terms in `S·∂ú` in separate calc blocks.
    _ < Œµ / 2 + Œµ / 2 :=
      (add_lt_add_of_le_of_lt ?_ ?_)
    _ = Œµ := add_halves Œµ
  ¬∑ -- We now work on the terms in `S`: uniform continuity and `bernstein.probability`
    -- quickly give us a bound.
    calc
      ‚àë k ‚àà S, |f k/‚Çô - f x| * bernstein n k x ‚â§ ‚àë k ‚àà S, Œµ / 2 * bernstein n k x := by
        gcongr with _ m
        exact le_of_lt (lt_of_mem_S m)
      _ = Œµ / 2 * ‚àë k ‚àà S, bernstein n k x := by rw [Finset.mul_sum]
      -- In this step we increase the sum over `S` back to a sum over all of `Fin (n+1)`,
      -- so that we can use `bernstein.probability`.
      _ ‚â§ Œµ / 2 * ‚àë k : Fin (n + 1), bernstein n k x := by
        gcongr
        exact Finset.sum_le_univ_sum_of_nonneg fun k => bernstein_nonneg
      _ = Œµ / 2 := by rw [bernstein.probability, mul_one]
  ¬∑ -- We now turn to working on `S·∂ú`: we control the difference term just using `‚Äñf‚Äñ`,
    -- and then insert a `Œ¥^(-2) * (x - k/n)^2` factor
    -- (which is at least one because we are not in `S`).
    calc
      ‚àë k ‚àà S·∂ú, |f k/‚Çô - f x| * bernstein n k x ‚â§ ‚àë k ‚àà S·∂ú, 2 * ‚Äñf‚Äñ * bernstein n k x := by
        gcongr
        apply f.dist_le_two_norm
      _ = 2 * ‚Äñf‚Äñ * ‚àë k ‚àà S·∂ú, bernstein n k x := by rw [Finset.mul_sum]
      _ ‚â§ 2 * ‚Äñf‚Äñ * ‚àë k ‚àà S·∂ú, Œ¥ ^ (-2 : ‚Ñ§) * ((x : ‚Ñù) - k/‚Çô) ^ 2 * bernstein n k x := by
        gcongr with _ m
        conv_lhs => rw [‚Üê one_mul (bernstein _ _ _)]
        gcongr
        exact le_of_mem_S_compl m
      -- Again enlarging the sum from `S·∂ú` to all of `Fin (n+1)`
      _ ‚â§ 2 * ‚Äñf‚Äñ * ‚àë k : Fin (n + 1), Œ¥ ^ (-2 : ‚Ñ§) * ((x : ‚Ñù) - k/‚Çô) ^ 2 * bernstein n k x := by
        gcongr
        refine Finset.sum_le_univ_sum_of_nonneg fun k => ?_
        positivity
      _ = 2 * ‚Äñf‚Äñ * Œ¥ ^ (-2 : ‚Ñ§) * ‚àë k : Fin (n + 1), ((x : ‚Ñù) - k/‚Çô) ^ 2 * bernstein n k x := by
        conv_rhs =>
          rw [mul_assoc, Finset.mul_sum]
          simp only [‚Üê mul_assoc]
      -- `bernstein.variance` and `x ‚àà [0,1]` gives the uniform bound
      _ = 2 * ‚Äñf‚Äñ * Œ¥ ^ (-2 : ‚Ñ§) * x * (1 - x) / n := by rw [variance npos]; ring
      _ ‚â§ 2 * ‚Äñf‚Äñ * Œ¥ ^ (-2 : ‚Ñ§) * 1 * 1 / n := by gcongr <;> unit_interval
      _ < Œµ / 2 := by simp only [mul_one]; exact nh
#align bernstein_approximation_uniform bernsteinApproximation_uniform
