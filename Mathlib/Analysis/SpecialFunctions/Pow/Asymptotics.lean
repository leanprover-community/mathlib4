/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne, David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal

/-!
# Limits and asymptotics of power functions at `+‚àû`

This file contains results about the limiting behaviour of power functions at `+‚àû`. For convenience
some results on asymptotics as `x ‚Üí 0` (those which are not just continuity statements) are also
located here.
-/


noncomputable section

open Real Topology NNReal ENNReal Filter ComplexConjugate Finset Set

/-!
## Limits at `+‚àû`
-/


section Limits

open Real Filter

/-- The function `x ^ y` tends to `+‚àû` at `+‚àû` for any positive real `y`. -/
theorem tendsto_rpow_atTop {y : ‚Ñù} (hy : 0 < y) : Tendsto (fun x : ‚Ñù => x ^ y) atTop atTop := by
  rw [(atTop_basis' 0).tendsto_right_iff]
  intro b hb
  filter_upwards [eventually_ge_atTop 0, eventually_ge_atTop (b ^ (1 / y))] with x hx‚ÇÄ hx
  simpa (disch := positivity) [Real.rpow_inv_le_iff_of_pos] using hx

/-- The function `x ^ (-y)` tends to `0` at `+‚àû` for any positive real `y`. -/
theorem tendsto_rpow_neg_atTop {y : ‚Ñù} (hy : 0 < y) : Tendsto (fun x : ‚Ñù => x ^ (-y)) atTop (ùìù 0) :=
  Tendsto.congr' (eventuallyEq_of_mem (Ioi_mem_atTop 0) fun _ hx => (rpow_neg (le_of_lt hx) y).symm)
    (tendsto_rpow_atTop hy).inv_tendsto_atTop

open Asymptotics in
lemma tendsto_rpow_atTop_of_base_lt_one (b : ‚Ñù) (hb‚ÇÄ : -1 < b) (hb‚ÇÅ : b < 1) :
    Tendsto (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) atTop (ùìù (0 : ‚Ñù)) := by
  rcases lt_trichotomy b 0 with hb | rfl | hb
  case inl => -- b < 0
    simp_rw [Real.rpow_def_of_nonpos hb.le, hb.ne, ite_false]
    rw [‚Üê isLittleO_const_iff (c := (1 : ‚Ñù)) one_ne_zero, (one_mul (1 : ‚Ñù)).symm]
    refine IsLittleO.mul_isBigO ?exp ?cos
    case exp =>
      rw [isLittleO_const_iff one_ne_zero]
      refine tendsto_exp_atBot.comp <| (tendsto_const_mul_atBot_of_neg ?_).mpr tendsto_id
      rw [‚Üê log_neg_eq_log, log_neg_iff (by linarith)]
      linarith
    case cos =>
      rw [isBigO_iff]
      exact ‚ü®1, Eventually.of_forall fun x => by simp [Real.abs_cos_le_one]‚ü©
  case inr.inl => -- b = 0
    refine Tendsto.mono_right ?_ (Iff.mpr pure_le_nhds_iff rfl)
    rw [tendsto_pure]
    filter_upwards [eventually_ne_atTop 0] with _ hx
    simp [hx]
  case inr.inr => -- b > 0
    simp_rw [Real.rpow_def_of_pos hb]
    refine tendsto_exp_atBot.comp <| (tendsto_const_mul_atBot_of_neg ?_).mpr tendsto_id
    exact (log_neg_iff hb).mpr hb‚ÇÅ

lemma tendsto_rpow_atBot_of_base_lt_one (b : ‚Ñù) (hb‚ÇÄ : 0 < b) (hb‚ÇÅ : b < 1) :
    Tendsto (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) atBot atTop := by
  simp_rw [Real.rpow_def_of_pos (by positivity : 0 < b)]
  refine tendsto_exp_atTop.comp <| (tendsto_const_mul_atTop_iff_neg <| tendsto_id (Œ± := ‚Ñù)).mpr ?_
  exact (log_neg_iff hb‚ÇÄ).mpr hb‚ÇÅ

lemma tendsto_rpow_atBot_of_base_gt_one (b : ‚Ñù) (hb : 1 < b) :
    Tendsto (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) atBot (ùìù 0) := by
  simp_rw [Real.rpow_def_of_pos (by positivity : 0 < b)]
  refine tendsto_exp_atBot.comp <| (tendsto_const_mul_atBot_of_pos ?_).mpr tendsto_id
  exact (log_pos_iff (by positivity)).mpr <| by aesop

@[deprecated (since := "2025-08-24")]
alias tendsto_rpow_atTop_of_base_gt_one := tendsto_rpow_atBot_of_base_gt_one

/-- The function `x ^ (a / (b * x + c))` tends to `1` at `+‚àû`, for any real numbers `a`, `b`, and
`c` such that `b` is nonzero. -/
theorem tendsto_rpow_div_mul_add (a b c : ‚Ñù) (hb : 0 ‚â† b) :
    Tendsto (fun x => x ^ (a / (b * x + c))) atTop (ùìù 1) := by
  refine
    Tendsto.congr' ?_
      ((tendsto_exp_nhds_zero_nhds_one.comp
            (by
              simpa only [mul_zero, pow_one] using
                (tendsto_const_nhds (x := a)).mul
                  (tendsto_div_pow_mul_exp_add_atTop b c 1 hb))).comp
        tendsto_log_atTop)
  apply eventuallyEq_of_mem (Ioi_mem_atTop (0 : ‚Ñù))
  intro x hx
  simp only [Set.mem_Ioi, Function.comp_apply] at hx ‚ä¢
  rw [exp_log hx, ‚Üê exp_log (rpow_pos_of_pos hx (a / (b * x + c))), log_rpow hx (a / (b * x + c))]
  field_simp

/-- The function `x ^ (1 / x)` tends to `1` at `+‚àû`. -/
theorem tendsto_rpow_div : Tendsto (fun x => x ^ ((1 : ‚Ñù) / x)) atTop (ùìù 1) := by
  convert tendsto_rpow_div_mul_add (1 : ‚Ñù) _ (0 : ‚Ñù) zero_ne_one
  ring

/-- The function `x ^ (-1 / x)` tends to `1` at `+‚àû`. -/
theorem tendsto_rpow_neg_div : Tendsto (fun x => x ^ (-(1 : ‚Ñù) / x)) atTop (ùìù 1) := by
  convert tendsto_rpow_div_mul_add (-(1 : ‚Ñù)) _ (0 : ‚Ñù) zero_ne_one
  ring

/-- The function `exp(x) / x ^ s` tends to `+‚àû` at `+‚àû`, for any real number `s`. -/
theorem tendsto_exp_div_rpow_atTop (s : ‚Ñù) : Tendsto (fun x : ‚Ñù => exp x / x ^ s) atTop atTop := by
  obtain ‚ü®n, hn‚ü© := archimedean_iff_nat_lt.1 Real.instArchimedean s
  refine tendsto_atTop_mono' _ ?_ (tendsto_exp_div_pow_atTop n)
  filter_upwards [eventually_gt_atTop (0 : ‚Ñù), eventually_ge_atTop (1 : ‚Ñù)] with x hx‚ÇÄ hx‚ÇÅ
  gcongr
  simpa using rpow_le_rpow_of_exponent_le hx‚ÇÅ hn.le

/-- The function `exp (b * x) / x ^ s` tends to `+‚àû` at `+‚àû`, for any real `s` and `b > 0`. -/
theorem tendsto_exp_mul_div_rpow_atTop (s : ‚Ñù) (b : ‚Ñù) (hb : 0 < b) :
    Tendsto (fun x : ‚Ñù => exp (b * x) / x ^ s) atTop atTop := by
  refine ((tendsto_rpow_atTop hb).comp (tendsto_exp_div_rpow_atTop (s / b))).congr' ?_
  filter_upwards [eventually_ge_atTop (0 : ‚Ñù)] with x hx‚ÇÄ
  simp [Real.div_rpow, (exp_pos x).le, rpow_nonneg, ‚Üê Real.rpow_mul, ‚Üê exp_mul,
    mul_comm x, hb.ne', *]

/-- The function `x ^ s * exp (-b * x)` tends to `0` at `+‚àû`, for any real `s` and `b > 0`. -/
theorem tendsto_rpow_mul_exp_neg_mul_atTop_nhds_zero (s : ‚Ñù) (b : ‚Ñù) (hb : 0 < b) :
    Tendsto (fun x : ‚Ñù => x ^ s * exp (-b * x)) atTop (ùìù 0) := by
  refine (tendsto_exp_mul_div_rpow_atTop s b hb).inv_tendsto_atTop.congr' ?_
  filter_upwards with x using by simp [exp_neg, inv_div, div_eq_mul_inv _ (exp _)]

nonrec theorem NNReal.tendsto_rpow_atTop {y : ‚Ñù} (hy : 0 < y) :
    Tendsto (fun x : ‚Ñù‚â•0 => x ^ y) atTop atTop := by
  rw [Filter.tendsto_atTop_atTop]
  intro b
  obtain ‚ü®c, hc‚ü© := tendsto_atTop_atTop.mp (tendsto_rpow_atTop hy) b
  use c.toNNReal
  intro a ha
  exact mod_cast hc a (Real.toNNReal_le_iff_le_coe.mp ha)

theorem ENNReal.tendsto_rpow_at_top {y : ‚Ñù} (hy : 0 < y) :
    Tendsto (fun x : ‚Ñù‚â•0‚àû => x ^ y) (ùìù ‚ä§) (ùìù ‚ä§) := by
  rw [ENNReal.tendsto_nhds_top_iff_nnreal]
  intro x
  obtain ‚ü®c, _, hc‚ü© :=
    (atTop_basis_Ioi.tendsto_iff atTop_basis_Ioi).mp (NNReal.tendsto_rpow_atTop hy) x trivial
  have hc' : Set.Ioi ‚Üëc ‚àà ùìù (‚ä§ : ‚Ñù‚â•0‚àû) := Ioi_mem_nhds ENNReal.coe_lt_top
  filter_upwards [hc'] with a ha
  by_cases ha' : a = ‚ä§
  ¬∑ simp [ha', hy]
  lift a to ‚Ñù‚â•0 using ha'
  simp only [Set.mem_Ioi, coe_lt_coe] at ha hc
  rw [‚Üê ENNReal.coe_rpow_of_nonneg _ hy.le]
  exact mod_cast hc a ha

end Limits

/-!
## Asymptotic results: `IsBigO`, `IsLittleO` and `IsTheta`
-/


namespace Complex

section

variable {Œ± : Type*} {l : Filter Œ±} {f g : Œ± ‚Üí ‚ÑÇ}

open Asymptotics

theorem isTheta_exp_arg_mul_im (hl : IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x => |(g x).im|) :
    (fun x => Real.exp (arg (f x) * im (g x))) =Œò[l] fun _ => (1 : ‚Ñù) := by
  rcases hl with ‚ü®b, hb‚ü©
  refine Real.isTheta_exp_comp_one.2 ‚ü®œÄ * b, ?_‚ü©
  rw [eventually_map] at hb ‚ä¢
  refine hb.mono fun x hx => ?_
  rw [abs_mul]
  exact mul_le_mul (abs_arg_le_pi _) hx (abs_nonneg _) Real.pi_pos.le

theorem isBigO_cpow_rpow (hl : IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x => |(g x).im|) :
    (fun x => f x ^ g x) =O[l] fun x => ‚Äñf x‚Äñ ^ (g x).re :=
  calc
    (fun x => f x ^ g x) =O[l]
        (show Œ± ‚Üí ‚Ñù from fun x => ‚Äñf x‚Äñ ^ (g x).re / Real.exp (arg (f x) * im (g x))) :=
      isBigO_of_le _ fun _ => (norm_cpow_le _ _).trans (le_abs_self _)
    _ =Œò[l] (show Œ± ‚Üí ‚Ñù from fun x => ‚Äñf x‚Äñ ^ (g x).re / (1 : ‚Ñù)) :=
      ((isTheta_refl _ _).div (isTheta_exp_arg_mul_im hl))
    _ =·∂†[l] (show Œ± ‚Üí ‚Ñù from fun x => ‚Äñf x‚Äñ ^ (g x).re) := by
      simp only [div_one, EventuallyEq.rfl]

theorem isTheta_cpow_rpow (hl_im : IsBoundedUnder (¬∑ ‚â§ ¬∑) l fun x => |(g x).im|)
    (hl : ‚àÄ·∂† x in l, f x = 0 ‚Üí re (g x) = 0 ‚Üí g x = 0) :
    (fun x => f x ^ g x) =Œò[l] fun x => ‚Äñf x‚Äñ ^ (g x).re :=
  calc
    (fun x => f x ^ g x) =Œò[l]
        (fun x => ‚Äñf x‚Äñ ^ (g x).re / Real.exp (arg (f x) * im (g x))) :=
      .of_norm_eventuallyEq <| hl.mono fun _ => norm_cpow_of_imp
    _ =Œò[l] fun x => ‚Äñf x‚Äñ ^ (g x).re / (1 : ‚Ñù) :=
      (isTheta_refl _ _).div (isTheta_exp_arg_mul_im hl_im)
    _ =·∂†[l] (fun x => ‚Äñf x‚Äñ ^ (g x).re) := by
      simp only [div_one, EventuallyEq.rfl]

theorem isTheta_cpow_const_rpow {b : ‚ÑÇ} (hl : b.re = 0 ‚Üí b ‚â† 0 ‚Üí ‚àÄ·∂† x in l, f x ‚â† 0) :
    (fun x => f x ^ b) =Œò[l] fun x => ‚Äñf x‚Äñ ^ b.re :=
  isTheta_cpow_rpow isBoundedUnder_const <| by
    simpa only [eventually_imp_distrib_right, not_imp_not, Imp.swap (a := b.re = 0)] using hl

end

end Complex

open Real

namespace Asymptotics

variable {Œ± : Type*} {r c : ‚Ñù} {l : Filter Œ±} {f g : Œ± ‚Üí ‚Ñù}

theorem IsBigOWith.rpow (h : IsBigOWith c l f g) (hc : 0 ‚â§ c) (hr : 0 ‚â§ r) (hg : 0 ‚â§·∂†[l] g) :
    IsBigOWith (c ^ r) l (fun x => f x ^ r) fun x => g x ^ r := by
  apply IsBigOWith.of_bound
  filter_upwards [hg, h.bound] with x hgx hx
  calc
    |f x ^ r| ‚â§ |f x| ^ r := abs_rpow_le_abs_rpow _ _
    _ ‚â§ (c * |g x|) ^ r := rpow_le_rpow (abs_nonneg _) hx hr
    _ = c ^ r * |g x ^ r| := by rw [mul_rpow hc (abs_nonneg _), abs_rpow_of_nonneg hgx]

theorem IsBigO.rpow (hr : 0 ‚â§ r) (hg : 0 ‚â§·∂†[l] g) (h : f =O[l] g) :
    (fun x => f x ^ r) =O[l] fun x => g x ^ r :=
  let ‚ü®_, hc, h'‚ü© := h.exists_nonneg
  (h'.rpow hc hr hg).isBigO

theorem IsTheta.rpow (hr : 0 ‚â§ r) (hf : 0 ‚â§·∂†[l] f) (hg : 0 ‚â§·∂†[l] g) (h : f =Œò[l] g) :
    (fun x => f x ^ r) =Œò[l] fun x => g x ^ r :=
  ‚ü®h.1.rpow hr hg, h.2.rpow hr hf‚ü©

theorem IsLittleO.rpow (hr : 0 < r) (hg : 0 ‚â§·∂†[l] g) (h : f =o[l] g) :
    (fun x => f x ^ r) =o[l] fun x => g x ^ r := by
  refine .of_isBigOWith fun c hc ‚Ü¶ ?_
  rw [‚Üê rpow_inv_rpow hc.le hr.ne']
  refine (h.forall_isBigOWith ?_).rpow ?_ ?_ hg <;> positivity

protected lemma IsBigO.sqrt (hfg : f =O[l] g) (hg : 0 ‚â§·∂†[l] g) :
    (fun x ‚Ü¶ ‚àö(f x)) =O[l] (fun x ‚Ü¶ ‚àö(g x)) := by
  simpa [Real.sqrt_eq_rpow] using hfg.rpow one_half_pos.le hg

protected lemma IsLittleO.sqrt (hfg : f =o[l] g) (hg : 0 ‚â§·∂†[l] g) :
    (fun x ‚Ü¶ ‚àö(f x)) =o[l] (fun x ‚Ü¶ ‚àö(g x)) := by
  simpa [Real.sqrt_eq_rpow] using hfg.rpow one_half_pos hg

protected lemma IsTheta.sqrt (hfg : f =Œò[l] g) (hf : 0 ‚â§·∂†[l] f) (hg : 0 ‚â§·∂†[l] g) :
    (Real.sqrt <| f ¬∑) =Œò[l] (Real.sqrt <| g ¬∑) :=
  ‚ü®hfg.1.sqrt hg, hfg.2.sqrt hf‚ü©

theorem isBigO_atTop_natCast_rpow_of_tendsto_div_rpow {ùïú : Type*} [RCLike ùïú] {g : ‚Ñï ‚Üí ùïú}
    {a : ùïú} {r : ‚Ñù} (hlim : Tendsto (fun n ‚Ü¶ g n / (n ^ r : ‚Ñù)) atTop (ùìù a)) :
    g =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r := by
  refine (isBigO_of_div_tendsto_nhds ?_ ‚Äña‚Äñ ?_).of_norm_left
  ¬∑ filter_upwards [eventually_ne_atTop 0] with _ h
    simp [Real.rpow_eq_zero_iff_of_nonneg, h]
  ¬∑ exact hlim.norm.congr fun n ‚Ü¶ by simp [abs_of_nonneg, show 0 ‚â§ (n : ‚Ñù) ^ r by positivity]

variable {E : Type*} [SeminormedRing E] (a b c : ‚Ñù)

theorem IsBigO.mul_atTop_rpow_of_isBigO_rpow {f g : ‚Ñù ‚Üí E}
    (hf : f =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ a) (hg : g =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ b)
    (h : a + b ‚â§ c) :
    (f * g) =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ c := by
  refine (hf.mul hg).trans (Eventually.isBigO ?_)
  filter_upwards [eventually_ge_atTop 1] with t ht
  rw [‚Üê Real.rpow_add (zero_lt_one.trans_le ht), Real.norm_of_nonneg (Real.rpow_nonneg
    (zero_le_one.trans ht) (a + b))]
  exact Real.rpow_le_rpow_of_exponent_le ht h

theorem IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow {f g : ‚Ñï ‚Üí E}
    (hf : f =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ a) (hg : g =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ b)
    (h : a + b ‚â§ c) :
    (f * g) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ c := by
  refine (hf.mul hg).trans (Eventually.isBigO ?_)
  filter_upwards [eventually_ge_atTop 1] with t ht
  replace ht : 1 ‚â§ (t : ‚Ñù) := Nat.one_le_cast.mpr ht
  rw [‚Üê Real.rpow_add (zero_lt_one.trans_le ht), Real.norm_of_nonneg (Real.rpow_nonneg
    (zero_le_one.trans ht) (a + b))]
  exact Real.rpow_le_rpow_of_exponent_le ht h

end Asymptotics

open Asymptotics

/-- `x ^ s = o(exp(b * x))` as `x ‚Üí ‚àû` for any real `s` and positive `b`. -/
theorem isLittleO_rpow_exp_pos_mul_atTop (s : ‚Ñù) {b : ‚Ñù} (hb : 0 < b) :
    (fun x : ‚Ñù => x ^ s) =o[atTop] fun x => exp (b * x) :=
  isLittleO_of_tendsto (fun _ h => absurd h (exp_pos _).ne') <| by
    simpa only [div_eq_mul_inv, exp_neg, neg_mul] using
      tendsto_rpow_mul_exp_neg_mul_atTop_nhds_zero s b hb

/-- `x ^ k = o(exp(b * x))` as `x ‚Üí ‚àû` for any integer `k` and positive `b`. -/
theorem isLittleO_zpow_exp_pos_mul_atTop (k : ‚Ñ§) {b : ‚Ñù} (hb : 0 < b) :
    (fun x : ‚Ñù => x ^ k) =o[atTop] fun x => exp (b * x) := by
  simpa only [Real.rpow_intCast] using isLittleO_rpow_exp_pos_mul_atTop k hb

/-- `x ^ k = o(exp(b * x))` as `x ‚Üí ‚àû` for any natural `k` and positive `b`. -/
theorem isLittleO_pow_exp_pos_mul_atTop (k : ‚Ñï) {b : ‚Ñù} (hb : 0 < b) :
    (fun x : ‚Ñù => x ^ k) =o[atTop] fun x => exp (b * x) := by
  simpa using isLittleO_zpow_exp_pos_mul_atTop k hb

/-- `x ^ s = o(exp x)` as `x ‚Üí ‚àû` for any real `s`. -/
theorem isLittleO_rpow_exp_atTop (s : ‚Ñù) : (fun x : ‚Ñù => x ^ s) =o[atTop] exp := by
  simpa only [one_mul] using isLittleO_rpow_exp_pos_mul_atTop s one_pos

/-- `exp (-a * x) = o(x ^ s)` as `x ‚Üí ‚àû`, for any positive `a` and real `s`. -/
theorem isLittleO_exp_neg_mul_rpow_atTop {a : ‚Ñù} (ha : 0 < a) (b : ‚Ñù) :
    IsLittleO atTop (fun x : ‚Ñù => exp (-a * x)) fun x : ‚Ñù => x ^ b := by
  apply isLittleO_of_tendsto'
  ¬∑ refine (eventually_gt_atTop 0).mono fun t ht h => ?_
    rw [rpow_eq_zero_iff_of_nonneg ht.le] at h
    exact (ht.ne' h.1).elim
  ¬∑ refine (tendsto_exp_mul_div_rpow_atTop (-b) a ha).inv_tendsto_atTop.congr' ?_
    refine (eventually_ge_atTop 0).mono fun t ht => ?_
    simp [field, Real.exp_neg, rpow_neg ht]

theorem isLittleO_log_rpow_atTop {r : ‚Ñù} (hr : 0 < r) : log =o[atTop] fun x => x ^ r :=
  calc
    log =O[atTop] fun x => r * log x := isBigO_self_const_mul hr.ne' _ _
    _ =·∂†[atTop] fun x => log (x ^ r) :=
      ((eventually_gt_atTop 0).mono fun _ hx => (log_rpow hx _).symm)
    _ =o[atTop] fun x => x ^ r := isLittleO_log_id_atTop.comp_tendsto (tendsto_rpow_atTop hr)

theorem isLittleO_log_rpow_rpow_atTop {s : ‚Ñù} (r : ‚Ñù) (hs : 0 < s) :
    (fun x => log x ^ r) =o[atTop] fun x => x ^ s :=
  let r' := max r 1
  have hr : 0 < r' := lt_max_iff.2 <| Or.inr one_pos
  have H : 0 < s / r' := div_pos hs hr
  calc
    (fun x => log x ^ r) =O[atTop] fun x => log x ^ r' :=
      .of_norm_eventuallyLE <| by
        filter_upwards [tendsto_log_atTop.eventually_ge_atTop 1] with x hx
        rw [Real.norm_of_nonneg (by positivity)]
        gcongr
        exacts [hx, le_max_left _ _]
    _ =o[atTop] fun x => (x ^ (s / r')) ^ r' :=
      ((isLittleO_log_rpow_atTop H).rpow hr <|
        (_root_.tendsto_rpow_atTop H).eventually <| eventually_ge_atTop 0)
    _ =·∂†[atTop] fun x => x ^ s :=
      (eventually_ge_atTop 0).mono fun x hx ‚Ü¶ by simp only [‚Üê rpow_mul hx, div_mul_cancel‚ÇÄ _ hr.ne']

theorem isLittleO_abs_log_rpow_rpow_nhdsGT_zero {s : ‚Ñù} (r : ‚Ñù) (hs : s < 0) :
    (fun x => |log x| ^ r) =o[ùìù[>] 0] fun x => x ^ s :=
  ((isLittleO_log_rpow_rpow_atTop r (neg_pos.2 hs)).comp_tendsto tendsto_inv_nhdsGT_zero).congr'
    (mem_of_superset (Icc_mem_nhdsGT one_pos) fun x hx => by
      simp [abs_of_nonpos, log_nonpos hx.1 hx.2])
    (eventually_mem_nhdsWithin.mono fun x hx => by
      rw [Function.comp_apply, inv_rpow hx.out.le, rpow_neg hx.out.le, inv_inv])

@[deprecated (since := "2025-03-02")]
alias isLittleO_abs_log_rpow_rpow_nhds_zero := isLittleO_abs_log_rpow_rpow_nhdsGT_zero

theorem isLittleO_log_rpow_nhdsGT_zero {r : ‚Ñù} (hr : r < 0) : log =o[ùìù[>] 0] fun x => x ^ r :=
  (isLittleO_abs_log_rpow_rpow_nhdsGT_zero 1 hr).neg_left.congr'
    (mem_of_superset (Icc_mem_nhdsGT one_pos) fun x hx => by
      simp [abs_of_nonpos (log_nonpos hx.1 hx.2)])
    .rfl

@[deprecated (since := "2025-03-02")]
alias isLittleO_log_rpow_nhds_zero := isLittleO_log_rpow_nhdsGT_zero

theorem tendsto_log_div_rpow_nhdsGT_zero {r : ‚Ñù} (hr : r < 0) :
    Tendsto (fun x => log x / x ^ r) (ùìù[>] 0) (ùìù 0) :=
  (isLittleO_log_rpow_nhdsGT_zero hr).tendsto_div_nhds_zero

@[deprecated (since := "2025-03-02")]
alias tendsto_log_div_rpow_nhds_zero := tendsto_log_div_rpow_nhdsGT_zero

theorem tendsto_log_mul_rpow_nhdsGT_zero {r : ‚Ñù} (hr : 0 < r) :
    Tendsto (fun x => log x * x ^ r) (ùìù[>] 0) (ùìù 0) :=
  (tendsto_log_div_rpow_nhdsGT_zero <| neg_lt_zero.2 hr).congr' <|
    eventually_mem_nhdsWithin.mono fun x hx => by rw [rpow_neg hx.out.le, div_inv_eq_mul]

@[deprecated (since := "2025-03-02")]
alias tendsto_log_mul_rpow_nhds_zero := tendsto_log_mul_rpow_nhdsGT_zero

lemma tendsto_log_mul_self_nhdsLT_zero : Filter.Tendsto (fun x ‚Ü¶ log x * x) (ùìù[<] 0) (ùìù 0) := by
  have h := tendsto_log_mul_rpow_nhdsGT_zero zero_lt_one
  simp only [Real.rpow_one] at h
  have h_eq : ‚àÄ x ‚àà Set.Iio 0, (-(fun x ‚Ü¶ log x * x) ‚àò (fun x ‚Ü¶ |x|)) x = log x * x := by
    simp only [Set.mem_Iio, Pi.neg_apply, Function.comp_apply, log_abs]
    intro x hx
    simp only [abs_of_nonpos hx.le, mul_neg, neg_neg]
  refine tendsto_nhdsWithin_congr h_eq ?_
  nth_rewrite 3 [‚Üê neg_zero]
  refine (h.comp (tendsto_abs_nhdsNE_zero.mono_left ?_)).neg
  refine nhdsWithin_mono 0 (fun x hx ‚Ü¶ ?_)
  simp only [Set.mem_Iio] at hx
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff, hx.ne, not_false_eq_true]

@[deprecated (since := "2025-03-02")]
alias tendsto_log_mul_self_nhds_zero_left := tendsto_log_mul_self_nhdsLT_zero
