/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne, David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Asymptotics

/-!
# Continuity of power functions

This file contains lemmas about continuity of the power functions on `‚ÑÇ`, `‚Ñù`, `‚Ñù‚â•0`, and `‚Ñù‚â•0‚àû`.
-/


noncomputable section

open Real Topology NNReal ENNReal Filter ComplexConjugate Finset Set

section CpowLimits

/-!
## Continuity for complex powers
-/


open Complex

variable {Œ± : Type*}

theorem zero_cpow_eq_nhds {b : ‚ÑÇ} (hb : b ‚â† 0) : (fun x : ‚ÑÇ => (0 : ‚ÑÇ) ^ x) =·∂†[ùìù b] 0 := by
  suffices ‚àÄ·∂† x : ‚ÑÇ in ùìù b, x ‚â† 0 from
    this.mono fun x hx ‚Ü¶ by
      dsimp only
      rw [zero_cpow hx, Pi.zero_apply]
  exact IsOpen.eventually_mem isOpen_ne hb

theorem cpow_eq_nhds {a b : ‚ÑÇ} (ha : a ‚â† 0) :
    (fun x => x ^ b) =·∂†[ùìù a] fun x => exp (log x * b) := by
  suffices ‚àÄ·∂† x : ‚ÑÇ in ùìù a, x ‚â† 0 from
    this.mono fun x hx ‚Ü¶ by
      dsimp only
      rw [cpow_def_of_ne_zero hx]
  exact IsOpen.eventually_mem isOpen_ne ha

theorem cpow_eq_nhds' {p : ‚ÑÇ √ó ‚ÑÇ} (hp_fst : p.fst ‚â† 0) :
    (fun x => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) := by
  suffices ‚àÄ·∂† x : ‚ÑÇ √ó ‚ÑÇ in ùìù p, x.1 ‚â† 0 from
    this.mono fun x hx ‚Ü¶ by
      dsimp only
      rw [cpow_def_of_ne_zero hx]
  refine IsOpen.eventually_mem ?_ hp_fst
  change IsOpen { x : ‚ÑÇ √ó ‚ÑÇ | x.1 = 0 }·∂ú
  rw [isOpen_compl_iff]
  exact isClosed_eq continuous_fst continuous_const

-- Continuity of `fun x => a ^ x`: union of these two lemmas is optimal.
theorem continuousAt_const_cpow {a b : ‚ÑÇ} (ha : a ‚â† 0) : ContinuousAt (fun x : ‚ÑÇ => a ^ x) b := by
  have cpow_eq : (fun x : ‚ÑÇ => a ^ x) = fun x => exp (log a * x) := by
    ext1 b
    rw [cpow_def_of_ne_zero ha]
  rw [cpow_eq]
  exact continuous_exp.continuousAt.comp (ContinuousAt.mul continuousAt_const continuousAt_id)

theorem continuousAt_const_cpow' {a b : ‚ÑÇ} (h : b ‚â† 0) : ContinuousAt (fun x : ‚ÑÇ => a ^ x) b := by
  by_cases ha : a = 0
  ¬∑ rw [ha, continuousAt_congr (zero_cpow_eq_nhds h)]
    exact continuousAt_const
  ¬∑ exact continuousAt_const_cpow ha

/-- The function `z ^ w` is continuous in `(z, w)` provided that `z` does not belong to the interval
`(-‚àû, 0]` on the real line. See also `Complex.continuousAt_cpow_zero_of_re_pos` for a version that
works for `z = 0` but assumes `0 < re w`. -/
theorem continuousAt_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp_fst : p.fst ‚àà slitPlane) :
    ContinuousAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2) p := by
  rw [continuousAt_congr (cpow_eq_nhds' <| slitPlane_ne_zero hp_fst)]
  refine continuous_exp.continuousAt.comp ?_
  exact
    ContinuousAt.mul
      (ContinuousAt.comp (continuousAt_clog hp_fst) continuous_fst.continuousAt)
      continuous_snd.continuousAt

theorem continuousAt_cpow_const {a b : ‚ÑÇ} (ha : a ‚àà slitPlane) :
    ContinuousAt (¬∑ ^ b) a :=
  Tendsto.comp (@continuousAt_cpow (a, b) ha) (continuousAt_id.prodMk continuousAt_const)

theorem Filter.Tendsto.cpow {l : Filter Œ±} {f g : Œ± ‚Üí ‚ÑÇ} {a b : ‚ÑÇ} (hf : Tendsto f l (ùìù a))
    (hg : Tendsto g l (ùìù b)) (ha : a ‚àà slitPlane) :
    Tendsto (fun x => f x ^ g x) l (ùìù (a ^ b)) :=
  (@continuousAt_cpow (a, b) ha).tendsto.comp (hf.prodMk_nhds hg)

theorem Filter.Tendsto.const_cpow {l : Filter Œ±} {f : Œ± ‚Üí ‚ÑÇ} {a b : ‚ÑÇ} (hf : Tendsto f l (ùìù b))
    (h : a ‚â† 0 ‚à® b ‚â† 0) : Tendsto (fun x => a ^ f x) l (ùìù (a ^ b)) := by
  cases h with
  | inl h => exact (continuousAt_const_cpow h).tendsto.comp hf
  | inr h => exact (continuousAt_const_cpow' h).tendsto.comp hf

variable [TopologicalSpace Œ±] {f g : Œ± ‚Üí ‚ÑÇ} {s : Set Œ±} {a : Œ±}

nonrec theorem ContinuousWithinAt.cpow (hf : ContinuousWithinAt f s a)
    (hg : ContinuousWithinAt g s a) (h0 : f a ‚àà slitPlane) :
    ContinuousWithinAt (fun x => f x ^ g x) s a :=
  hf.cpow hg h0

nonrec theorem ContinuousWithinAt.const_cpow {b : ‚ÑÇ} (hf : ContinuousWithinAt f s a)
    (h : b ‚â† 0 ‚à® f a ‚â† 0) : ContinuousWithinAt (fun x => b ^ f x) s a :=
  hf.const_cpow h

nonrec theorem ContinuousAt.cpow (hf : ContinuousAt f a) (hg : ContinuousAt g a)
    (h0 : f a ‚àà slitPlane) : ContinuousAt (fun x => f x ^ g x) a :=
  hf.cpow hg h0

nonrec theorem ContinuousAt.const_cpow {b : ‚ÑÇ} (hf : ContinuousAt f a) (h : b ‚â† 0 ‚à® f a ‚â† 0) :
    ContinuousAt (fun x => b ^ f x) a :=
  hf.const_cpow h

theorem ContinuousOn.cpow (hf : ContinuousOn f s) (hg : ContinuousOn g s)
    (h0 : ‚àÄ a ‚àà s, f a ‚àà slitPlane) : ContinuousOn (fun x => f x ^ g x) s := fun a ha =>
  (hf a ha).cpow (hg a ha) (h0 a ha)

theorem ContinuousOn.const_cpow {b : ‚ÑÇ} (hf : ContinuousOn f s) (h : b ‚â† 0 ‚à® ‚àÄ a ‚àà s, f a ‚â† 0) :
    ContinuousOn (fun x => b ^ f x) s := fun a ha => (hf a ha).const_cpow (h.imp id fun h => h a ha)

theorem Continuous.cpow (hf : Continuous f) (hg : Continuous g)
    (h0 : ‚àÄ a, f a ‚àà slitPlane) : Continuous fun x => f x ^ g x :=
  continuous_iff_continuousAt.2 fun a => hf.continuousAt.cpow hg.continuousAt (h0 a)

theorem Continuous.const_cpow {b : ‚ÑÇ} (hf : Continuous f) (h : b ‚â† 0 ‚à® ‚àÄ a, f a ‚â† 0) :
    Continuous fun x => b ^ f x :=
  continuous_iff_continuousAt.2 fun a => hf.continuousAt.const_cpow <| h.imp id fun h => h a

theorem ContinuousOn.cpow_const {b : ‚ÑÇ} (hf : ContinuousOn f s)
    (h : ‚àÄ a : Œ±, a ‚àà s ‚Üí f a ‚àà slitPlane) : ContinuousOn (fun x => f x ^ b) s :=
  hf.cpow continuousOn_const h

@[fun_prop]
lemma continuous_const_cpow (z : ‚ÑÇ) [NeZero z] : Continuous fun s : ‚ÑÇ ‚Ü¶ z ^ s :=
  continuous_id.const_cpow (.inl <| NeZero.ne z)

end CpowLimits

section RpowLimits

/-!
## Continuity for real powers
-/


namespace Real

theorem continuousAt_const_rpow {a b : ‚Ñù} (h : a ‚â† 0) : ContinuousAt (a ^ ¬∑) b := by
  simp only [rpow_def]
  refine Complex.continuous_re.continuousAt.comp ?_
  refine (continuousAt_const_cpow ?_).comp Complex.continuous_ofReal.continuousAt
  norm_cast

theorem continuousAt_const_rpow' {a b : ‚Ñù} (h : b ‚â† 0) : ContinuousAt (a ^ ¬∑) b := by
  simp only [rpow_def]
  refine Complex.continuous_re.continuousAt.comp ?_
  refine (continuousAt_const_cpow' ?_).comp Complex.continuous_ofReal.continuousAt
  norm_cast

theorem rpow_eq_nhds_of_neg {p : ‚Ñù √ó ‚Ñù} (hp_fst : p.fst < 0) :
    (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) * cos (x.2 * œÄ) := by
  suffices ‚àÄ·∂† x : ‚Ñù √ó ‚Ñù in ùìù p, x.1 < 0 from
    this.mono fun x hx ‚Ü¶ by
      dsimp only
      rw [rpow_def_of_neg hx]
  exact IsOpen.eventually_mem (isOpen_lt continuous_fst continuous_const) hp_fst

theorem rpow_eq_nhds_of_pos {p : ‚Ñù √ó ‚Ñù} (hp_fst : 0 < p.fst) :
    (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) := by
  suffices ‚àÄ·∂† x : ‚Ñù √ó ‚Ñù in ùìù p, 0 < x.1 from
    this.mono fun x hx ‚Ü¶ by
      dsimp only
      rw [rpow_def_of_pos hx]
  exact IsOpen.eventually_mem (isOpen_lt continuous_const continuous_fst) hp_fst

theorem continuousAt_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) :
    ContinuousAt (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) p := by
  rw [ne_iff_lt_or_gt] at hp
  cases hp with
  | inl hp =>
    rw [continuousAt_congr (rpow_eq_nhds_of_neg hp)]
    refine ContinuousAt.mul ?_ (continuous_cos.continuousAt.comp ?_)
    ¬∑ refine continuous_exp.continuousAt.comp (ContinuousAt.mul ?_ continuous_snd.continuousAt)
      refine (continuousAt_log ?_).comp continuous_fst.continuousAt
      exact hp.ne
    ¬∑ exact continuous_snd.continuousAt.mul continuousAt_const
  | inr hp =>
    rw [continuousAt_congr (rpow_eq_nhds_of_pos hp)]
    refine continuous_exp.continuousAt.comp (ContinuousAt.mul ?_ continuous_snd.continuousAt)
    refine (continuousAt_log ?_).comp continuous_fst.continuousAt
    exact hp.ne'

theorem continuousAt_rpow_of_pos (p : ‚Ñù √ó ‚Ñù) (hp : 0 < p.2) :
    ContinuousAt (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) p := by
  obtain ‚ü®x, y‚ü© := p
  dsimp only at hp
  obtain hx | rfl := ne_or_eq x 0
  ¬∑ exact continuousAt_rpow_of_ne (x, y) hx
  have A : Tendsto (fun p : ‚Ñù √ó ‚Ñù => exp (log p.1 * p.2)) (ùìù[‚â†] 0 √óÀ¢ ùìù y) (ùìù 0) :=
    tendsto_exp_atBot.comp
      ((tendsto_log_nhdsNE_zero.comp tendsto_fst).atBot_mul_pos hp tendsto_snd)
  have B : Tendsto (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) (ùìù[‚â†] 0 √óÀ¢ ùìù y) (ùìù 0) :=
    squeeze_zero_norm (fun p => abs_rpow_le_exp_log_mul p.1 p.2) A
  have C : Tendsto (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) (ùìù[{0}] 0 √óÀ¢ ùìù y) (pure 0) := by
    rw [nhdsWithin_singleton, tendsto_pure, pure_prod, eventually_map]
    exact (lt_mem_nhds hp).mono fun y hy => zero_rpow hy.ne'
  simpa only [‚Üê sup_prod, ‚Üê nhdsWithin_union, compl_union_self, nhdsWithin_univ, nhds_prod_eq,
    ContinuousAt, zero_rpow hp.ne'] using B.sup (C.mono_right (pure_le_nhds _))

theorem continuousAt_rpow (p : ‚Ñù √ó ‚Ñù) (h : p.1 ‚â† 0 ‚à® 0 < p.2) :
    ContinuousAt (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) p :=
  h.elim (fun h => continuousAt_rpow_of_ne p h) fun h => continuousAt_rpow_of_pos p h

@[fun_prop]
theorem continuousAt_rpow_const (x : ‚Ñù) (q : ‚Ñù) (h : x ‚â† 0 ‚à® 0 ‚â§ q) :
    ContinuousAt (fun x : ‚Ñù => x ^ q) x := by
  rw [le_iff_lt_or_eq, ‚Üê or_assoc] at h
  obtain h|rfl := h
  ¬∑ exact (continuousAt_rpow (x, q) h).comp‚ÇÇ continuousAt_id continuousAt_const
  ¬∑ simp_rw [rpow_zero]; exact continuousAt_const

@[fun_prop]
theorem continuous_rpow_const {q : ‚Ñù} (h : 0 ‚â§ q) : Continuous (fun x : ‚Ñù => x ^ q) :=
  continuous_iff_continuousAt.mpr fun x ‚Ü¶ continuousAt_rpow_const x q (.inr h)

@[fun_prop]
lemma continuous_const_rpow {a : ‚Ñù} (h : a ‚â† 0) : Continuous (fun x : ‚Ñù ‚Ü¶ a ^ x) :=
  continuous_iff_continuousAt.mpr fun _ ‚Ü¶ continuousAt_const_rpow h

end Real

section

variable {Œ± : Type*}

theorem Filter.Tendsto.rpow {l : Filter Œ±} {f g : Œ± ‚Üí ‚Ñù} {x y : ‚Ñù} (hf : Tendsto f l (ùìù x))
    (hg : Tendsto g l (ùìù y)) (h : x ‚â† 0 ‚à® 0 < y) : Tendsto (fun t => f t ^ g t) l (ùìù (x ^ y)) :=
  (Real.continuousAt_rpow (x, y) h).tendsto.comp (hf.prodMk_nhds hg)

theorem Filter.Tendsto.rpow_const {l : Filter Œ±} {f : Œ± ‚Üí ‚Ñù} {x p : ‚Ñù} (hf : Tendsto f l (ùìù x))
    (h : x ‚â† 0 ‚à® 0 ‚â§ p) : Tendsto (fun a => f a ^ p) l (ùìù (x ^ p)) :=
  if h0 : 0 = p then h0 ‚ñ∏ by simp [tendsto_const_nhds]
  else hf.rpow tendsto_const_nhds (h.imp id fun h' => h'.lt_of_ne h0)

variable [TopologicalSpace Œ±] {f g : Œ± ‚Üí ‚Ñù} {s : Set Œ±} {x : Œ±} {p : ‚Ñù}

nonrec theorem ContinuousAt.rpow (hf : ContinuousAt f x) (hg : ContinuousAt g x)
    (h : f x ‚â† 0 ‚à® 0 < g x) : ContinuousAt (fun t => f t ^ g t) x :=
  hf.rpow hg h

nonrec theorem ContinuousWithinAt.rpow (hf : ContinuousWithinAt f s x)
    (hg : ContinuousWithinAt g s x) (h : f x ‚â† 0 ‚à® 0 < g x) :
    ContinuousWithinAt (fun t => f t ^ g t) s x :=
  hf.rpow hg h

theorem ContinuousOn.rpow (hf : ContinuousOn f s) (hg : ContinuousOn g s)
    (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 0 < g x) : ContinuousOn (fun t => f t ^ g t) s := fun t ht =>
  (hf t ht).rpow (hg t ht) (h t ht)

theorem Continuous.rpow (hf : Continuous f) (hg : Continuous g) (h : ‚àÄ x, f x ‚â† 0 ‚à® 0 < g x) :
    Continuous fun x => f x ^ g x :=
  continuous_iff_continuousAt.2 fun x => hf.continuousAt.rpow hg.continuousAt (h x)

nonrec theorem ContinuousWithinAt.rpow_const (hf : ContinuousWithinAt f s x) (h : f x ‚â† 0 ‚à® 0 ‚â§ p) :
    ContinuousWithinAt (fun x => f x ^ p) s x :=
  hf.rpow_const h

nonrec theorem ContinuousAt.rpow_const (hf : ContinuousAt f x) (h : f x ‚â† 0 ‚à® 0 ‚â§ p) :
    ContinuousAt (fun x => f x ^ p) x :=
  hf.rpow_const h

theorem ContinuousOn.rpow_const (hf : ContinuousOn f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 0 ‚â§ p) :
    ContinuousOn (fun x => f x ^ p) s := fun x hx => (hf x hx).rpow_const (h x hx)

theorem Continuous.rpow_const (hf : Continuous f) (h : ‚àÄ x, f x ‚â† 0 ‚à® 0 ‚â§ p) :
    Continuous fun x => f x ^ p :=
  continuous_iff_continuousAt.2 fun x => hf.continuousAt.rpow_const (h x)

end

end RpowLimits

/-! ## Continuity results for `cpow`, part II

These results involve relating real and complex powers, so cannot be done higher up.
-/


section CpowLimits2

namespace Complex

/-- See also `continuousAt_cpow` and `Complex.continuousAt_cpow_of_re_pos`. -/
theorem continuousAt_cpow_zero_of_re_pos {z : ‚ÑÇ} (hz : 0 < z.re) :
    ContinuousAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2) (0, z) := by
  have hz‚ÇÄ : z ‚â† 0 := ne_of_apply_ne re hz.ne'
  rw [ContinuousAt, zero_cpow hz‚ÇÄ, tendsto_zero_iff_norm_tendsto_zero]
  refine squeeze_zero (fun _ => norm_nonneg _) (fun _ => norm_cpow_le _ _) ?_
  simp only [div_eq_mul_inv, ‚Üê Real.exp_neg]
  refine Tendsto.zero_mul_isBoundedUnder_le ?_ ?_
  ¬∑ convert
        (continuous_fst.norm.tendsto ((0 : ‚ÑÇ), z)).rpow
          ((continuous_re.comp continuous_snd).tendsto _) _ <;>
      simp [hz, Real.zero_rpow hz.ne']
  ¬∑ simp only [Function.comp_def, Real.norm_eq_abs, abs_of_pos (Real.exp_pos _)]
    rcases exists_gt |im z| with ‚ü®C, hC‚ü©
    refine ‚ü®Real.exp (œÄ * C), eventually_map.2 ?_‚ü©
    refine
      (((continuous_im.comp continuous_snd).abs.tendsto (_, z)).eventually (gt_mem_nhds hC)).mono
        fun z hz => Real.exp_le_exp.2 <| (neg_le_abs _).trans ?_
    rw [_root_.abs_mul]
    exact
      mul_le_mul (abs_le.2 ‚ü®(neg_pi_lt_arg _).le, arg_le_pi _‚ü©) hz.le (_root_.abs_nonneg _)
        Real.pi_pos.le

open ComplexOrder in
/-- See also `continuousAt_cpow` for a version that assumes `p.1 ‚â† 0` but makes no
assumptions about `p.2`. -/
theorem continuousAt_cpow_of_re_pos {p : ‚ÑÇ √ó ‚ÑÇ} (h‚ÇÅ : 0 ‚â§ p.1.re ‚à® p.1.im ‚â† 0) (h‚ÇÇ : 0 < p.2.re) :
    ContinuousAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2) p := by
  obtain ‚ü®z, w‚ü© := p
  rw [‚Üê not_lt_zero_iff, lt_iff_le_and_ne, not_and_or, Ne, Classical.not_not,
    not_le_zero_iff] at h‚ÇÅ
  rcases h‚ÇÅ with (h‚ÇÅ | (rfl : z = 0))
  exacts [continuousAt_cpow h‚ÇÅ, continuousAt_cpow_zero_of_re_pos h‚ÇÇ]

/-- See also `continuousAt_cpow_const` for a version that assumes `z ‚â† 0` but makes no
assumptions about `w`. -/
theorem continuousAt_cpow_const_of_re_pos {z w : ‚ÑÇ} (hz : 0 ‚â§ re z ‚à® im z ‚â† 0) (hw : 0 < re w) :
    ContinuousAt (fun x => x ^ w) z :=
  Tendsto.comp (@continuousAt_cpow_of_re_pos (z, w) hz hw)
    (continuousAt_id.prodMk continuousAt_const)

/-- Continuity of `(x, y) ‚Ü¶ x ^ y` as a function on `‚Ñù √ó ‚ÑÇ`. -/
theorem continuousAt_ofReal_cpow (x : ‚Ñù) (y : ‚ÑÇ) (h : 0 < y.re ‚à® x ‚â† 0) :
    ContinuousAt (fun p => (p.1 : ‚ÑÇ) ^ p.2 : ‚Ñù √ó ‚ÑÇ ‚Üí ‚ÑÇ) (x, y) := by
  rcases lt_trichotomy (0 : ‚Ñù) x with (hx | rfl | hx)
  ¬∑ -- x > 0 : easy case
    have : ContinuousAt (fun p => ‚ü®‚Üëp.1, p.2‚ü© : ‚Ñù √ó ‚ÑÇ ‚Üí ‚ÑÇ √ó ‚ÑÇ) (x, y) := by fun_prop
    refine (continuousAt_cpow (Or.inl ?_)).comp this
    rwa [ofReal_re]
  ¬∑ -- x = 0 : reduce to continuousAt_cpow_zero_of_re_pos
    have A : ContinuousAt (fun p => p.1 ^ p.2 : ‚ÑÇ √ó ‚ÑÇ ‚Üí ‚ÑÇ) ‚ü®‚Üë(0 : ‚Ñù), y‚ü© := by
      rw [ofReal_zero]
      apply continuousAt_cpow_zero_of_re_pos
      tauto
    have B : ContinuousAt (fun p => ‚ü®‚Üëp.1, p.2‚ü© : ‚Ñù √ó ‚ÑÇ ‚Üí ‚ÑÇ √ó ‚ÑÇ) ‚ü®0, y‚ü© := by fun_prop
    exact A.comp_of_eq B rfl
  ¬∑ -- x < 0 : difficult case
    suffices ContinuousAt (fun p => (-(p.1 : ‚ÑÇ)) ^ p.2 * exp (œÄ * I * p.2) : ‚Ñù √ó ‚ÑÇ ‚Üí ‚ÑÇ) (x, y) by
      refine this.congr (eventually_of_mem (prod_mem_nhds (Iio_mem_nhds hx) univ_mem) ?_)
      exact fun p hp => (ofReal_cpow_of_nonpos (le_of_lt hp.1) p.2).symm
    have A : ContinuousAt (fun p => ‚ü®-‚Üëp.1, p.2‚ü© : ‚Ñù √ó ‚ÑÇ ‚Üí ‚ÑÇ √ó ‚ÑÇ) (x, y) := by fun_prop
    apply ContinuousAt.mul
    ¬∑ refine (continuousAt_cpow (Or.inl ?_)).comp A
      rwa [neg_re, ofReal_re, neg_pos]
    ¬∑ exact (continuous_exp.comp (continuous_const.mul continuous_snd)).continuousAt

theorem continuousAt_ofReal_cpow_const (x : ‚Ñù) (y : ‚ÑÇ) (h : 0 < y.re ‚à® x ‚â† 0) :
    ContinuousAt (fun a => (a : ‚ÑÇ) ^ y : ‚Ñù ‚Üí ‚ÑÇ) x :=
  (continuousAt_ofReal_cpow x y h).comp‚ÇÇ_of_eq (by fun_prop) (by fun_prop) rfl

theorem continuous_ofReal_cpow_const {y : ‚ÑÇ} (hs : 0 < y.re) :
    Continuous (fun x => (x : ‚ÑÇ) ^ y : ‚Ñù ‚Üí ‚ÑÇ) :=
  continuous_iff_continuousAt.mpr fun x => continuousAt_ofReal_cpow_const x y (Or.inl hs)

end Complex

end CpowLimits2

/-! ## Limits and continuity for `‚Ñù‚â•0` powers -/


namespace NNReal

theorem continuousAt_rpow {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 < y) :
    ContinuousAt (fun p : ‚Ñù‚â•0 √ó ‚Ñù => p.1 ^ p.2) (x, y) := by
  have :
    (fun p : ‚Ñù‚â•0 √ó ‚Ñù => p.1 ^ p.2) =
      Real.toNNReal ‚àò (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) ‚àò fun p : ‚Ñù‚â•0 √ó ‚Ñù => (p.1.1, p.2) := by
    ext p
    simp only [coe_rpow, val_eq_coe, Function.comp_apply, coe_toNNReal', left_eq_sup]
    exact_mod_cast zero_le (p.1 ^ p.2)
  rw [this]
  refine continuous_real_toNNReal.continuousAt.comp (ContinuousAt.comp ?_ ?_)
  ¬∑ apply Real.continuousAt_rpow
    simpa using h
  ¬∑ fun_prop

theorem eventually_pow_one_div_le (x : ‚Ñù‚â•0) {y : ‚Ñù‚â•0} (hy : 1 < y) :
    ‚àÄ·∂† n : ‚Ñï in atTop, x ^ (1 / n : ‚Ñù) ‚â§ y := by
  obtain ‚ü®m, hm‚ü© := add_one_pow_unbounded_of_pos x (tsub_pos_of_lt hy)
  rw [tsub_add_cancel_of_le hy.le] at hm
  refine eventually_atTop.2 ‚ü®m + 1, fun n hn => ?_‚ü©
  simp only [one_div]
  simpa only [NNReal.rpow_inv_le_iff (Nat.cast_pos.2 <| m.succ_pos.trans_le hn),
    NNReal.rpow_natCast] using hm.le.trans (pow_right_mono‚ÇÄ hy.le (m.le_succ.trans hn))

end NNReal

open Filter

theorem Filter.Tendsto.nnrpow {Œ± : Type*} {f : Filter Œ±} {u : Œ± ‚Üí ‚Ñù‚â•0} {v : Œ± ‚Üí ‚Ñù} {x : ‚Ñù‚â•0}
    {y : ‚Ñù} (hx : Tendsto u f (ùìù x)) (hy : Tendsto v f (ùìù y)) (h : x ‚â† 0 ‚à® 0 < y) :
    Tendsto (fun a => u a ^ v a) f (ùìù (x ^ y)) :=
  Tendsto.comp (NNReal.continuousAt_rpow h) (hx.prodMk_nhds hy)

namespace NNReal

theorem continuousAt_rpow_const {x : ‚Ñù‚â•0} {y : ‚Ñù} (h : x ‚â† 0 ‚à® 0 ‚â§ y) :
    ContinuousAt (fun z => z ^ y) x :=
  h.elim (fun h => tendsto_id.nnrpow tendsto_const_nhds (Or.inl h)) fun h =>
    h.eq_or_lt.elim (fun h => h ‚ñ∏ by simp only [rpow_zero, continuousAt_const]) fun h =>
      tendsto_id.nnrpow tendsto_const_nhds (Or.inr h)

@[fun_prop]
theorem continuous_rpow_const {y : ‚Ñù} (h : 0 ‚â§ y) : Continuous fun x : ‚Ñù‚â•0 => x ^ y :=
  continuous_iff_continuousAt.2 fun _ => continuousAt_rpow_const (Or.inr h)

@[fun_prop]
theorem continuousOn_rpow_const_compl_zero {r : ‚Ñù} :
    ContinuousOn (fun z : ‚Ñù‚â•0 => z ^ r) {0}·∂ú :=
  fun _ h => ContinuousAt.continuousWithinAt <| NNReal.continuousAt_rpow_const (.inl h)

-- even though this follows from `ContinuousOn.mono` and the previous lemma, we include it for
-- automation purposes with `fun_prop`, because the side goal `0 ‚àâ s ‚à® 0 ‚â§ r` is often easy to check
@[fun_prop]
theorem continuousOn_rpow_const {r : ‚Ñù} {s : Set ‚Ñù‚â•0}
    (h : 0 ‚àâ s ‚à® 0 ‚â§ r) : ContinuousOn (fun z : ‚Ñù‚â•0 => z ^ r) s :=
  h.elim (fun _ ‚Ü¶ ContinuousOn.mono (s := {0}·∂ú) (by fun_prop) (by aesop))
    (NNReal.continuous_rpow_const ¬∑ |>.continuousOn)

end NNReal

/-! ## Continuity for `‚Ñù‚â•0‚àû` powers -/


namespace ENNReal

theorem eventually_pow_one_div_le {x : ‚Ñù‚â•0‚àû} (hx : x ‚â† ‚àû) {y : ‚Ñù‚â•0‚àû} (hy : 1 < y) :
    ‚àÄ·∂† n : ‚Ñï in atTop, x ^ (1 / n : ‚Ñù) ‚â§ y := by
  lift x to ‚Ñù‚â•0 using hx
  by_cases h : y = ‚àû
  ¬∑ exact Eventually.of_forall fun n => h.symm ‚ñ∏ le_top
  ¬∑ lift y to ‚Ñù‚â•0 using h
    have := NNReal.eventually_pow_one_div_le x (mod_cast hy : 1 < y)
    refine this.congr (Eventually.of_forall fun n => ?_)
    rw [‚Üê coe_rpow_of_nonneg x (by positivity : 0 ‚â§ (1 / n : ‚Ñù)), coe_le_coe]

private theorem continuousAt_rpow_const_of_pos {x : ‚Ñù‚â•0‚àû} {y : ‚Ñù} (h : 0 < y) :
    ContinuousAt (fun a : ‚Ñù‚â•0‚àû => a ^ y) x := by
  by_cases hx : x = ‚ä§
  ¬∑ rw [hx, ContinuousAt]
    convert ENNReal.tendsto_rpow_at_top h
    simp [h]
  lift x to ‚Ñù‚â•0 using hx
  rw [continuousAt_coe_iff]
  convert continuous_coe.continuousAt.comp (NNReal.continuousAt_rpow_const (Or.inr h.le)) using 1
  ext1 x
  simp [‚Üê coe_rpow_of_nonneg _ h.le]

@[continuity, fun_prop]
theorem continuous_rpow_const {y : ‚Ñù} : Continuous fun a : ‚Ñù‚â•0‚àû => a ^ y := by
  refine continuous_iff_continuousAt.2 fun x => ?_
  rcases lt_trichotomy (0 : ‚Ñù) y with (hy | rfl | hy)
  ¬∑ exact continuousAt_rpow_const_of_pos hy
  ¬∑ simp only [rpow_zero]
    exact continuousAt_const
  ¬∑ obtain ‚ü®z, hz‚ü© : ‚àÉ z, y = -z := ‚ü®-y, (neg_neg _).symm‚ü©
    have z_pos : 0 < z := by simpa [hz] using hy
    simp_rw [hz, rpow_neg]
    exact continuous_inv.continuousAt.comp (continuousAt_rpow_const_of_pos z_pos)

theorem tendsto_const_mul_rpow_nhds_zero_of_pos {c : ‚Ñù‚â•0‚àû} (hc : c ‚â† ‚àû) {y : ‚Ñù} (hy : 0 < y) :
    Tendsto (fun x : ‚Ñù‚â•0‚àû => c * x ^ y) (ùìù 0) (ùìù 0) := by
  convert ENNReal.Tendsto.const_mul (ENNReal.continuous_rpow_const.tendsto 0) _
  ¬∑ simp [hy]
  ¬∑ exact Or.inr hc

end ENNReal

theorem Filter.Tendsto.ennrpow_const {Œ± : Type*} {f : Filter Œ±} {m : Œ± ‚Üí ‚Ñù‚â•0‚àû} {a : ‚Ñù‚â•0‚àû} (r : ‚Ñù)
    (hm : Tendsto m f (ùìù a)) : Tendsto (fun x => m x ^ r) f (ùìù (a ^ r)) :=
  (ENNReal.continuous_rpow_const.tendsto a).comp hm
