/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.Analysis.SpecialFunctions.Complex.LogDeriv
import Mathlib.Analysis.Calculus.FDeriv.Extend
import Mathlib.Analysis.Calculus.Deriv.Prod
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

/-!
# Derivatives of power function on `‚ÑÇ`, `‚Ñù`, `‚Ñù‚â•0`, and `‚Ñù‚â•0‚àû`

We also prove differentiability and provide derivatives for the power functions `x ^ y`.
-/


noncomputable section

open scoped Real Topology NNReal ENNReal
open Filter

namespace Complex

theorem hasStrictFDerivAt_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp : p.1 ‚àà slitPlane) :
    HasStrictFDerivAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2)
      ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ ContinuousLinearMap.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
        (p.1 ^ p.2 * log p.1) ‚Ä¢ ContinuousLinearMap.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) p := by
  have A : p.1 ‚â† 0 := slitPlane_ne_zero hp
  have : (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) :=
    ((isOpen_ne.preimage continuous_fst).eventually_mem A).mono fun p hp =>
      cpow_def_of_ne_zero hp _
  rw [cpow_sub _ _ A, cpow_one, mul_div_left_comm, mul_smul, mul_smul]
  refine HasStrictFDerivAt.congr_of_eventuallyEq ?_ this.symm
  simpa only [cpow_def_of_ne_zero A, div_eq_mul_inv, mul_smul, add_comm, smul_add] using
    ((hasStrictFDerivAt_fst.clog hp).mul hasStrictFDerivAt_snd).cexp

theorem hasStrictFDerivAt_cpow' {x y : ‚ÑÇ} (hp : x ‚àà slitPlane) :
    HasStrictFDerivAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2)
      ((y * x ^ (y - 1)) ‚Ä¢ ContinuousLinearMap.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
        (x ^ y * log x) ‚Ä¢ ContinuousLinearMap.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) (x, y) :=
  @hasStrictFDerivAt_cpow (x, y) hp

theorem hasStrictDerivAt_const_cpow {x y : ‚ÑÇ} (h : x ‚â† 0 ‚à® y ‚â† 0) :
    HasStrictDerivAt (fun y => x ^ y) (x ^ y * log x) y := by
  rcases em (x = 0) with (rfl | hx)
  ¬∑ replace h := h.neg_resolve_left rfl
    rw [log_zero, mul_zero]
    refine (hasStrictDerivAt_const y 0).congr_of_eventuallyEq ?_
    exact (isOpen_ne.eventually_mem h).mono fun y hy => (zero_cpow hy).symm
  ¬∑ simpa only [cpow_def_of_ne_zero hx, mul_one] using
      ((hasStrictDerivAt_id y).const_mul (log x)).cexp

theorem hasFDerivAt_cpow {p : ‚ÑÇ √ó ‚ÑÇ} (hp : p.1 ‚àà slitPlane) :
    HasFDerivAt (fun x : ‚ÑÇ √ó ‚ÑÇ => x.1 ^ x.2)
      ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ ContinuousLinearMap.fst ‚ÑÇ ‚ÑÇ ‚ÑÇ +
        (p.1 ^ p.2 * log p.1) ‚Ä¢ ContinuousLinearMap.snd ‚ÑÇ ‚ÑÇ ‚ÑÇ) p :=
  (hasStrictFDerivAt_cpow hp).hasFDerivAt

end Complex

section fderiv

open Complex

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f g : E ‚Üí ‚ÑÇ} {f' g' : E ‚ÜíL[‚ÑÇ] ‚ÑÇ}
  {x : E} {s : Set E} {c : ‚ÑÇ}

theorem HasStrictFDerivAt.cpow (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x)
    (h0 : f x ‚àà slitPlane) : HasStrictFDerivAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Complex.log (f x)) ‚Ä¢ g') x :=
  (hasStrictFDerivAt_cpow (p := (f x, g x)) h0).comp x (hf.prodMk hg)

theorem HasStrictFDerivAt.const_cpow (hf : HasStrictFDerivAt f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasStrictFDerivAt (fun x => c ^ f x) ((c ^ f x * Complex.log c) ‚Ä¢ f') x :=
  (hasStrictDerivAt_const_cpow h0).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.cpow (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x)
    (h0 : f x ‚àà slitPlane) : HasFDerivAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Complex.log (f x)) ‚Ä¢ g') x := by
  convert (@Complex.hasFDerivAt_cpow ((fun x => (f x, g x)) x) h0).comp x (hf.prodMk hg)

theorem HasFDerivAt.const_cpow (hf : HasFDerivAt f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasFDerivAt (fun x => c ^ f x) ((c ^ f x * Complex.log c) ‚Ä¢ f') x :=
  (hasStrictDerivAt_const_cpow h0).hasDerivAt.comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.cpow (hf : HasFDerivWithinAt f f' s x) (hg : HasFDerivWithinAt g g' s x)
    (h0 : f x ‚àà slitPlane) : HasFDerivWithinAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Complex.log (f x)) ‚Ä¢ g') s x := by
  convert (@Complex.hasFDerivAt_cpow ((fun x => (f x, g x)) x) h0).comp_hasFDerivWithinAt x
    (hf.prodMk hg)

theorem HasFDerivWithinAt.const_cpow (hf : HasFDerivWithinAt f f' s x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasFDerivWithinAt (fun x => c ^ f x) ((c ^ f x * Complex.log c) ‚Ä¢ f') s x :=
  (hasStrictDerivAt_const_cpow h0).hasDerivAt.comp_hasFDerivWithinAt x hf

@[fun_prop]
theorem DifferentiableAt.cpow (hf : DifferentiableAt ‚ÑÇ f x) (hg : DifferentiableAt ‚ÑÇ g x)
    (h0 : f x ‚àà slitPlane) : DifferentiableAt ‚ÑÇ (fun x => f x ^ g x) x :=
  (hf.hasFDerivAt.cpow hg.hasFDerivAt h0).differentiableAt

@[fun_prop]
theorem DifferentiableAt.const_cpow (hf : DifferentiableAt ‚ÑÇ f x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    DifferentiableAt ‚ÑÇ (fun x => c ^ f x) x :=
  (hf.hasFDerivAt.const_cpow h0).differentiableAt

@[fun_prop]
theorem DifferentiableAt.cpow_const (hf : DifferentiableAt ‚ÑÇ f x) (h0 : f x ‚àà slitPlane) :
    DifferentiableAt ‚ÑÇ (fun x => f x ^ c) x :=
  hf.cpow (differentiableAt_const c) h0

@[fun_prop]
theorem DifferentiableWithinAt.cpow (hf : DifferentiableWithinAt ‚ÑÇ f s x)
    (hg : DifferentiableWithinAt ‚ÑÇ g s x) (h0 : f x ‚àà slitPlane) :
    DifferentiableWithinAt ‚ÑÇ (fun x => f x ^ g x) s x :=
  (hf.hasFDerivWithinAt.cpow hg.hasFDerivWithinAt h0).differentiableWithinAt

@[fun_prop]
theorem DifferentiableWithinAt.const_cpow (hf : DifferentiableWithinAt ‚ÑÇ f s x)
    (h0 : c ‚â† 0 ‚à® f x ‚â† 0) : DifferentiableWithinAt ‚ÑÇ (fun x => c ^ f x) s x :=
  (hf.hasFDerivWithinAt.const_cpow h0).differentiableWithinAt

@[fun_prop]
theorem DifferentiableWithinAt.cpow_const (hf : DifferentiableWithinAt ‚ÑÇ f s x)
    (h0 : f x ‚àà slitPlane) :
    DifferentiableWithinAt ‚ÑÇ (fun x => f x ^ c) s x :=
  hf.cpow (differentiableWithinAt_const c) h0

@[fun_prop]
theorem DifferentiableOn.cpow (hf : DifferentiableOn ‚ÑÇ f s) (hg : DifferentiableOn ‚ÑÇ g s)
    (h0 : Set.MapsTo f s slitPlane) : DifferentiableOn ‚ÑÇ (fun x ‚Ü¶ f x ^ g x) s :=
  fun x hx ‚Ü¶ (hf x hx).cpow (hg x hx) (h0 hx)

@[fun_prop]
theorem DifferentiableOn.const_cpow (hf : DifferentiableOn ‚ÑÇ f s)
    (h0 : c ‚â† 0 ‚à® ‚àÄ x ‚àà s, f x ‚â† 0) : DifferentiableOn ‚ÑÇ (fun x ‚Ü¶ c ^ f x) s :=
  fun x hx ‚Ü¶ (hf x hx).const_cpow (h0.imp_right fun h ‚Ü¶ h x hx)

@[fun_prop]
theorem DifferentiableOn.cpow_const (hf : DifferentiableOn ‚ÑÇ f s)
    (h0 : ‚àÄ x ‚àà s, f x ‚àà slitPlane) :
    DifferentiableOn ‚ÑÇ (fun x => f x ^ c) s :=
  hf.cpow (differentiableOn_const c) h0

@[fun_prop]
theorem Differentiable.cpow (hf : Differentiable ‚ÑÇ f) (hg : Differentiable ‚ÑÇ g)
    (h0 : ‚àÄ x, f x ‚àà slitPlane) : Differentiable ‚ÑÇ (fun x ‚Ü¶ f x ^ g x) :=
  fun x ‚Ü¶ (hf x).cpow (hg x) (h0 x)

@[fun_prop]
theorem Differentiable.const_cpow (hf : Differentiable ‚ÑÇ f)
    (h0 : c ‚â† 0 ‚à® ‚àÄ x, f x ‚â† 0) : Differentiable ‚ÑÇ (fun x ‚Ü¶ c ^ f x) :=
  fun x ‚Ü¶ (hf x).const_cpow (h0.imp_right fun h ‚Ü¶ h x)

@[fun_prop]
lemma differentiable_const_cpow_of_neZero (z : ‚ÑÇ) [NeZero z] :
    Differentiable ‚ÑÇ fun s : ‚ÑÇ ‚Ü¶ z ^ s :=
  differentiable_id.const_cpow (.inl <| NeZero.ne z)

@[fun_prop]
lemma differentiableAt_const_cpow_of_neZero (z : ‚ÑÇ) [NeZero z] (t : ‚ÑÇ) :
    DifferentiableAt ‚ÑÇ (fun s : ‚ÑÇ ‚Ü¶ z ^ s) t :=
  differentiableAt_id.const_cpow (.inl <| NeZero.ne z)

end fderiv

section deriv

open Complex

variable {f g : ‚ÑÇ ‚Üí ‚ÑÇ} {s : Set ‚ÑÇ} {f' g' x c : ‚ÑÇ}

/-- A private lemma that rewrites the output of lemmas like `HasFDerivAt.cpow` to the form
expected by lemmas like `HasDerivAt.cpow`. -/
private theorem aux : ((g x * f x ^ (g x - 1)) ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight f' +
    (f x ^ g x * log (f x)) ‚Ä¢ (1 : ‚ÑÇ ‚ÜíL[‚ÑÇ] ‚ÑÇ).smulRight g') 1 =
      g x * f x ^ (g x - 1) * f' + f x ^ g x * log (f x) * g' := by
  simp only [Algebra.id.smul_eq_mul, one_mul, ContinuousLinearMap.one_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.add_apply, Pi.smul_apply,
    ContinuousLinearMap.coe_smul']

nonrec theorem HasStrictDerivAt.cpow (hf : HasStrictDerivAt f f' x) (hg : HasStrictDerivAt g g' x)
    (h0 : f x ‚àà slitPlane) : HasStrictDerivAt (fun x => f x ^ g x)
      (g x * f x ^ (g x - 1) * f' + f x ^ g x * Complex.log (f x) * g') x := by
  simpa using (hf.cpow hg h0).hasStrictDerivAt

theorem HasStrictDerivAt.const_cpow (hf : HasStrictDerivAt f f' x) (h : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasStrictDerivAt (fun x => c ^ f x) (c ^ f x * Complex.log c * f') x :=
  (hasStrictDerivAt_const_cpow h).comp x hf

theorem Complex.hasStrictDerivAt_cpow_const (h : x ‚àà slitPlane) :
    HasStrictDerivAt (fun z : ‚ÑÇ => z ^ c) (c * x ^ (c - 1)) x := by
  simpa only [mul_zero, add_zero, mul_one] using
    (hasStrictDerivAt_id x).cpow (hasStrictDerivAt_const x c) h

theorem HasStrictDerivAt.cpow_const (hf : HasStrictDerivAt f f' x)
    (h0 : f x ‚àà slitPlane) :
    HasStrictDerivAt (fun x => f x ^ c) (c * f x ^ (c - 1) * f') x :=
  (Complex.hasStrictDerivAt_cpow_const h0).comp x hf

theorem HasDerivAt.cpow (hf : HasDerivAt f f' x) (hg : HasDerivAt g g' x)
    (h0 : f x ‚àà slitPlane) : HasDerivAt (fun x => f x ^ g x)
      (g x * f x ^ (g x - 1) * f' + f x ^ g x * Complex.log (f x) * g') x := by
  simpa only [aux] using (hf.hasFDerivAt.cpow hg h0).hasDerivAt

theorem HasDerivAt.const_cpow (hf : HasDerivAt f f' x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasDerivAt (fun x => c ^ f x) (c ^ f x * Complex.log c * f') x :=
  (hasStrictDerivAt_const_cpow h0).hasDerivAt.comp x hf

theorem HasDerivAt.cpow_const (hf : HasDerivAt f f' x) (h0 : f x ‚àà slitPlane) :
    HasDerivAt (fun x => f x ^ c) (c * f x ^ (c - 1) * f') x :=
  (Complex.hasStrictDerivAt_cpow_const h0).hasDerivAt.comp x hf

theorem HasDerivWithinAt.cpow (hf : HasDerivWithinAt f f' s x) (hg : HasDerivWithinAt g g' s x)
    (h0 : f x ‚àà slitPlane) : HasDerivWithinAt (fun x => f x ^ g x)
      (g x * f x ^ (g x - 1) * f' + f x ^ g x * Complex.log (f x) * g') s x := by
  simpa only [aux] using (hf.hasFDerivWithinAt.cpow hg h0).hasDerivWithinAt

theorem HasDerivWithinAt.const_cpow (hf : HasDerivWithinAt f f' s x) (h0 : c ‚â† 0 ‚à® f x ‚â† 0) :
    HasDerivWithinAt (fun x => c ^ f x) (c ^ f x * Complex.log c * f') s x :=
  (hasStrictDerivAt_const_cpow h0).hasDerivAt.comp_hasDerivWithinAt x hf

theorem HasDerivWithinAt.cpow_const (hf : HasDerivWithinAt f f' s x)
    (h0 : f x ‚àà slitPlane) :
    HasDerivWithinAt (fun x => f x ^ c) (c * f x ^ (c - 1) * f') s x :=
  (Complex.hasStrictDerivAt_cpow_const h0).hasDerivAt.comp_hasDerivWithinAt x hf

/-- Although `fun x => x ^ r` for fixed `r` is *not* complex-differentiable along the negative real
line, it is still real-differentiable, and the derivative is what one would formally expect.
See `hasDerivAt_ofReal_cpow_const` for an alternate formulation. -/
theorem hasDerivAt_ofReal_cpow_const' {x : ‚Ñù} (hx : x ‚â† 0) {r : ‚ÑÇ} (hr : r ‚â† -1) :
    HasDerivAt (fun y : ‚Ñù => (y : ‚ÑÇ) ^ (r + 1) / (r + 1)) (x ^ r) x := by
  rw [Ne, ‚Üê add_eq_zero_iff_eq_neg, ‚Üê Ne] at hr
  rcases lt_or_gt_of_ne hx.symm with (hx | hx)
  ¬∑ -- easy case : `0 < x`
    apply HasDerivAt.comp_ofReal (e := fun y => (y : ‚ÑÇ) ^ (r + 1) / (r + 1))
    convert HasDerivAt.div_const (ùïú := ‚ÑÇ) ?_ (r + 1) using 1
    ¬∑ exact (mul_div_cancel_right‚ÇÄ _ hr).symm
    ¬∑ convert HasDerivAt.cpow_const ?_ ?_ using 1
      ¬∑ rw [add_sub_cancel_right, mul_comm]; exact (mul_one _).symm
      ¬∑ exact hasDerivAt_id (x : ‚ÑÇ)
      ¬∑ simp [hx]
  ¬∑ -- harder case : `x < 0`
    have : ‚àÄ·∂† y : ‚Ñù in ùìù x,
        (y : ‚ÑÇ) ^ (r + 1) / (r + 1) = (-y : ‚ÑÇ) ^ (r + 1) * exp (œÄ * I * (r + 1)) / (r + 1) := by
      refine Filter.eventually_of_mem (Iio_mem_nhds hx) fun y hy => ?_
      rw [ofReal_cpow_of_nonpos (le_of_lt hy)]
    refine HasDerivAt.congr_of_eventuallyEq ?_ this
    rw [ofReal_cpow_of_nonpos (le_of_lt hx)]
    suffices HasDerivAt (fun y : ‚Ñù => (-‚Üëy) ^ (r + 1) * exp (‚ÜëœÄ * I * (r + 1)))
        ((r + 1) * (-‚Üëx) ^ r * exp (‚ÜëœÄ * I * r)) x by
      convert this.div_const (r + 1) using 1
      conv_rhs => rw [mul_assoc, mul_comm, mul_div_cancel_right‚ÇÄ _ hr]
    rw [mul_add ((œÄ : ‚ÑÇ) * _), mul_one, exp_add, exp_pi_mul_I, mul_comm (_ : ‚ÑÇ) (-1 : ‚ÑÇ),
      neg_one_mul]
    simp_rw [mul_neg, ‚Üê neg_mul, ‚Üê ofReal_neg]
    suffices HasDerivAt (fun y : ‚Ñù => (‚Üë(-y) : ‚ÑÇ) ^ (r + 1)) (-(r + 1) * ‚Üë(-x) ^ r) x by
      convert this.neg.mul_const _ using 1; ring
    suffices HasDerivAt (fun y : ‚Ñù => (y : ‚ÑÇ) ^ (r + 1)) ((r + 1) * ‚Üë(-x) ^ r) (-x) by
      convert @HasDerivAt.scomp ‚Ñù _ ‚ÑÇ _ _ x ‚Ñù _ _ _ _ _ _ _ _ this (hasDerivAt_neg x) using 1
      rw [real_smul, ofReal_neg 1, ofReal_one]; ring
    suffices HasDerivAt (fun y : ‚ÑÇ => y ^ (r + 1)) ((r + 1) * ‚Üë(-x) ^ r) ‚Üë(-x) by
      exact this.comp_ofReal
    conv in ‚Üë_ ^ _ => rw [(by ring : r = r + 1 - 1)]
    convert HasDerivAt.cpow_const ?_ ?_ using 1
    ¬∑ rw [add_sub_cancel_right, add_sub_cancel_right]; exact (mul_one _).symm
    ¬∑ exact hasDerivAt_id ((-x : ‚Ñù) : ‚ÑÇ)
    ¬∑ simp [hx]

@[deprecated (since := "2024-12-15")] alias hasDerivAt_ofReal_cpow := hasDerivAt_ofReal_cpow_const'

/-- An alternate formulation of `hasDerivAt_ofReal_cpow_const'`. -/
theorem hasDerivAt_ofReal_cpow_const {x : ‚Ñù} (hx : x ‚â† 0) {r : ‚ÑÇ} (hr : r ‚â† 0) :
    HasDerivAt (fun y : ‚Ñù => (y : ‚ÑÇ) ^ r) (r * x ^ (r - 1)) x := by
  have := HasDerivAt.const_mul r <| hasDerivAt_ofReal_cpow_const' hx
    (by rwa [ne_eq, sub_eq_neg_self])
  simpa [sub_add_cancel, mul_div_cancel‚ÇÄ _ hr] using this

/-- A version of `DifferentiableAt.cpow_const` for a real function. -/
theorem DifferentiableAt.ofReal_cpow_const {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù} (hf : DifferentiableAt ‚Ñù f x)
    (h0 : f x ‚â† 0) (h1 : c ‚â† 0) :
    DifferentiableAt ‚Ñù (fun (y : ‚Ñù) => (f y : ‚ÑÇ) ^ c) x :=
  (hasDerivAt_ofReal_cpow_const h0 h1).differentiableAt.comp x hf

theorem Complex.deriv_cpow_const (hx : x ‚àà Complex.slitPlane) :
    deriv (fun (x : ‚ÑÇ) ‚Ü¶ x ^ c) x = c * x ^ (c - 1) :=
  (hasStrictDerivAt_cpow_const hx).hasDerivAt.deriv

/-- A version of `Complex.deriv_cpow_const` for a real variable. -/
theorem Complex.deriv_ofReal_cpow_const {x : ‚Ñù} (hx : x ‚â† 0) (hc : c ‚â† 0) :
    deriv (fun x : ‚Ñù ‚Ü¶ (x : ‚ÑÇ) ^ c) x = c * x ^ (c - 1) :=
  (hasDerivAt_ofReal_cpow_const hx hc).deriv

theorem deriv_cpow_const (hf : DifferentiableAt ‚ÑÇ f x) (hx : f x ‚àà Complex.slitPlane) :
    deriv (fun (x : ‚ÑÇ) ‚Ü¶ f x ^ c) x = c * f x ^ (c - 1) * deriv f x :=
  (hf.hasDerivAt.cpow_const hx).deriv

theorem isTheta_deriv_ofReal_cpow_const_atTop {c : ‚ÑÇ} (hc : c ‚â† 0) :
    deriv (fun (x : ‚Ñù) => (x : ‚ÑÇ) ^ c) =Œò[atTop] fun x => x ^ (c.re - 1) := by
  calc
    _ =·∂†[atTop] fun x : ‚Ñù ‚Ü¶ c * x ^ (c - 1) := by
      filter_upwards [eventually_ne_atTop 0] with x hx using by rw [deriv_ofReal_cpow_const hx hc]
    _ =Œò[atTop] fun x : ‚Ñù ‚Ü¶ ‚Äñ(x : ‚ÑÇ) ^ (c - 1)‚Äñ :=
      (Asymptotics.IsTheta.of_norm_eventuallyEq EventuallyEq.rfl).const_mul_left hc
    _ =·∂†[atTop] fun x ‚Ü¶ x ^ (c.re - 1) := by
      filter_upwards [eventually_gt_atTop 0] with x hx
      rw [norm_cpow_eq_rpow_re_of_pos hx, sub_re, one_re]

theorem isBigO_deriv_ofReal_cpow_const_atTop (c : ‚ÑÇ) :
    deriv (fun (x : ‚Ñù) => (x : ‚ÑÇ) ^ c) =O[atTop] fun x => x ^ (c.re - 1) := by
  obtain rfl | hc := eq_or_ne c 0
  ¬∑ simp_rw [cpow_zero, deriv_const', Asymptotics.isBigO_zero]
  ¬∑ exact (isTheta_deriv_ofReal_cpow_const_atTop hc).1

end deriv

namespace Real

variable {x y z : ‚Ñù}

/-- `(x, y) ‚Ü¶ x ^ y` is strictly differentiable at `p : ‚Ñù √ó ‚Ñù` such that `0 < p.fst`. -/
theorem hasStrictFDerivAt_rpow_of_pos (p : ‚Ñù √ó ‚Ñù) (hp : 0 < p.1) :
    HasStrictFDerivAt (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2)
      ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ ContinuousLinearMap.fst ‚Ñù ‚Ñù ‚Ñù +
        (p.1 ^ p.2 * log p.1) ‚Ä¢ ContinuousLinearMap.snd ‚Ñù ‚Ñù ‚Ñù) p := by
  have : (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) :=
    (continuousAt_fst.eventually (lt_mem_nhds hp)).mono fun p hp => rpow_def_of_pos hp _
  refine HasStrictFDerivAt.congr_of_eventuallyEq ?_ this.symm
  convert ((hasStrictFDerivAt_fst.log hp.ne').fun_mul hasStrictFDerivAt_snd).exp using 1
  rw [rpow_sub_one hp.ne', ‚Üê rpow_def_of_pos hp, smul_add, smul_smul, mul_div_left_comm,
    div_eq_mul_inv, smul_smul, smul_smul, mul_assoc, add_comm]

/-- `(x, y) ‚Ü¶ x ^ y` is strictly differentiable at `p : ‚Ñù √ó ‚Ñù` such that `p.fst < 0`. -/
theorem hasStrictFDerivAt_rpow_of_neg (p : ‚Ñù √ó ‚Ñù) (hp : p.1 < 0) :
    HasStrictFDerivAt (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2)
      ((p.2 * p.1 ^ (p.2 - 1)) ‚Ä¢ ContinuousLinearMap.fst ‚Ñù ‚Ñù ‚Ñù +
        (p.1 ^ p.2 * log p.1 - exp (log p.1 * p.2) * sin (p.2 * œÄ) * œÄ) ‚Ä¢
          ContinuousLinearMap.snd ‚Ñù ‚Ñù ‚Ñù) p := by
  have : (fun x : ‚Ñù √ó ‚Ñù => x.1 ^ x.2) =·∂†[ùìù p] fun x => exp (log x.1 * x.2) * cos (x.2 * œÄ) :=
    (continuousAt_fst.eventually (gt_mem_nhds hp)).mono fun p hp => rpow_def_of_neg hp _
  refine HasStrictFDerivAt.congr_of_eventuallyEq ?_ this.symm
  convert ((hasStrictFDerivAt_fst.log hp.ne).fun_mul hasStrictFDerivAt_snd).exp.fun_mul
    (hasStrictFDerivAt_snd.mul_const œÄ).cos using 1
  simp_rw [rpow_sub_one hp.ne, smul_add, ‚Üê add_assoc, smul_smul, ‚Üê add_smul, ‚Üê mul_assoc,
    mul_comm (cos _), ‚Üê rpow_def_of_neg hp]
  rw [div_eq_mul_inv, add_comm]; congr 2 <;> ring

/-- The function `fun (x, y) => x ^ y` is infinitely smooth at `(x, y)` unless `x = 0`. -/
theorem contDiffAt_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) {n : WithTop ‚Ñï‚àû} :
    ContDiffAt ‚Ñù n (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) p := by
  rcases hp.lt_or_gt with hneg | hpos
  exacts
    [(((contDiffAt_fst.log hneg.ne).mul contDiffAt_snd).exp.mul
          (contDiffAt_snd.mul contDiffAt_const).cos).congr_of_eventuallyEq
      ((continuousAt_fst.eventually (gt_mem_nhds hneg)).mono fun p hp => rpow_def_of_neg hp _),
    ((contDiffAt_fst.log hpos.ne').mul contDiffAt_snd).exp.congr_of_eventuallyEq
      ((continuousAt_fst.eventually (lt_mem_nhds hpos)).mono fun p hp => rpow_def_of_pos hp _)]

theorem differentiableAt_rpow_of_ne (p : ‚Ñù √ó ‚Ñù) (hp : p.1 ‚â† 0) :
    DifferentiableAt ‚Ñù (fun p : ‚Ñù √ó ‚Ñù => p.1 ^ p.2) p :=
  (contDiffAt_rpow_of_ne p hp).differentiableAt le_rfl

theorem _root_.HasStrictDerivAt.rpow {f g : ‚Ñù ‚Üí ‚Ñù} {f' g' : ‚Ñù} (hf : HasStrictDerivAt f f' x)
    (hg : HasStrictDerivAt g g' x) (h : 0 < f x) : HasStrictDerivAt (fun x => f x ^ g x)
      (f' * g x * f x ^ (g x - 1) + g' * f x ^ g x * Real.log (f x)) x := by
  convert (hasStrictFDerivAt_rpow_of_pos ((fun x => (f x, g x)) x) h).comp_hasStrictDerivAt x
    (hf.prodMk hg) using 1
  simp [mul_assoc, mul_comm]

theorem hasStrictDerivAt_rpow_const_of_ne {x : ‚Ñù} (hx : x ‚â† 0) (p : ‚Ñù) :
    HasStrictDerivAt (fun x => x ^ p) (p * x ^ (p - 1)) x := by
  rcases hx.lt_or_gt with hx | hx
  ¬∑ have := (hasStrictFDerivAt_rpow_of_neg (x, p) hx).comp_hasStrictDerivAt x
      ((hasStrictDerivAt_id x).prodMk (hasStrictDerivAt_const x p))
    convert this using 1; simp
  ¬∑ simpa using (hasStrictDerivAt_id x).rpow (hasStrictDerivAt_const x p) hx

theorem hasStrictDerivAt_const_rpow {a : ‚Ñù} (ha : 0 < a) (x : ‚Ñù) :
    HasStrictDerivAt (fun x => a ^ x) (a ^ x * log a) x := by
  simpa using (hasStrictDerivAt_const _ _).rpow (hasStrictDerivAt_id x) ha

lemma differentiableAt_rpow_const_of_ne (p : ‚Ñù) {x : ‚Ñù} (hx : x ‚â† 0) :
    DifferentiableAt ‚Ñù (fun x => x ^ p) x :=
  (hasStrictDerivAt_rpow_const_of_ne hx p).differentiableAt

lemma differentiableOn_rpow_const (p : ‚Ñù) :
    DifferentiableOn ‚Ñù (fun x => (x : ‚Ñù) ^ p) {0}·∂ú :=
  fun _ hx => (Real.differentiableAt_rpow_const_of_ne p hx).differentiableWithinAt

/-- This lemma says that `fun x => a ^ x` is strictly differentiable for `a < 0`. Note that these
values of `a` are outside of the "official" domain of `a ^ x`, and we may redefine `a ^ x`
for negative `a` if some other definition will be more convenient. -/
theorem hasStrictDerivAt_const_rpow_of_neg {a x : ‚Ñù} (ha : a < 0) :
    HasStrictDerivAt (fun x => a ^ x) (a ^ x * log a - exp (log a * x) * sin (x * œÄ) * œÄ) x := by
  simpa using (hasStrictFDerivAt_rpow_of_neg (a, x) ha).comp_hasStrictDerivAt x
    ((hasStrictDerivAt_const _ _).prodMk (hasStrictDerivAt_id _))

end Real

namespace Real

variable {z x y : ‚Ñù}

theorem hasDerivAt_rpow_const {x p : ‚Ñù} (h : x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasDerivAt (fun x => x ^ p) (p * x ^ (p - 1)) x := by
  rcases ne_or_eq x 0 with (hx | rfl)
  ¬∑ exact (hasStrictDerivAt_rpow_const_of_ne hx _).hasDerivAt
  replace h : 1 ‚â§ p := h.neg_resolve_left rfl
  apply hasDerivAt_of_hasDerivAt_of_ne fun x hx =>
    (hasStrictDerivAt_rpow_const_of_ne hx p).hasDerivAt
  exacts [continuousAt_id.rpow_const (Or.inr (zero_le_one.trans h)),
    continuousAt_const.mul (continuousAt_id.rpow_const (Or.inr (sub_nonneg.2 h)))]

theorem differentiable_rpow_const {p : ‚Ñù} (hp : 1 ‚â§ p) : Differentiable ‚Ñù fun x : ‚Ñù => x ^ p :=
  fun _ => (hasDerivAt_rpow_const (Or.inr hp)).differentiableAt

theorem deriv_rpow_const {x p : ‚Ñù} (h : x ‚â† 0 ‚à® 1 ‚â§ p) :
    deriv (fun x : ‚Ñù => x ^ p) x = p * x ^ (p - 1) :=
  (hasDerivAt_rpow_const h).deriv

theorem deriv_rpow_const' {p : ‚Ñù} (h : 1 ‚â§ p) :
    (deriv fun x : ‚Ñù => x ^ p) = fun x => p * x ^ (p - 1) :=
  funext fun _ => deriv_rpow_const (Or.inr h)

theorem contDiffAt_rpow_const_of_ne {x p : ‚Ñù} {n : WithTop ‚Ñï‚àû} (h : x ‚â† 0) :
    ContDiffAt ‚Ñù n (fun x => x ^ p) x :=
  (contDiffAt_rpow_of_ne (x, p) h).comp x (contDiffAt_id.prodMk contDiffAt_const)

theorem contDiff_rpow_const_of_le {p : ‚Ñù} {n : ‚Ñï} (h : ‚Üën ‚â§ p) :
    ContDiff ‚Ñù n fun x : ‚Ñù => x ^ p := by
  induction' n with n ihn generalizing p
  ¬∑ exact contDiff_zero.2 (continuous_id.rpow_const fun x => Or.inr <| by simpa using h)
  ¬∑ have h1 : 1 ‚â§ p := le_trans (by simp) h
    rw [Nat.cast_succ, ‚Üê le_sub_iff_add_le] at h
    rw [show ((n + 1 : ‚Ñï) : WithTop ‚Ñï‚àû) = n + 1 from rfl,
      contDiff_succ_iff_deriv, deriv_rpow_const' h1]
    simp only [WithTop.natCast_ne_top, analyticOn_univ, IsEmpty.forall_iff, true_and]
    exact ‚ü®differentiable_rpow_const h1, contDiff_const.mul (ihn h)‚ü©

theorem contDiffAt_rpow_const_of_le {x p : ‚Ñù} {n : ‚Ñï} (h : ‚Üën ‚â§ p) :
    ContDiffAt ‚Ñù n (fun x : ‚Ñù => x ^ p) x :=
  (contDiff_rpow_const_of_le h).contDiffAt

theorem contDiffAt_rpow_const {x p : ‚Ñù} {n : ‚Ñï} (h : x ‚â† 0 ‚à® ‚Üën ‚â§ p) :
    ContDiffAt ‚Ñù n (fun x : ‚Ñù => x ^ p) x :=
  h.elim contDiffAt_rpow_const_of_ne contDiffAt_rpow_const_of_le

theorem hasStrictDerivAt_rpow_const {x p : ‚Ñù} (hx : x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasStrictDerivAt (fun x => x ^ p) (p * x ^ (p - 1)) x :=
  ContDiffAt.hasStrictDerivAt' (contDiffAt_rpow_const (by rwa [‚Üê Nat.cast_one] at hx))
    (hasDerivAt_rpow_const hx) le_rfl

end Real

section Differentiability

open Real

section fderiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f g : E ‚Üí ‚Ñù} {f' g' : E ‚ÜíL[‚Ñù] ‚Ñù}
  {x : E} {s : Set E} {c p : ‚Ñù} {n : WithTop ‚Ñï‚àû}

theorem HasFDerivWithinAt.rpow (hf : HasFDerivWithinAt f f' s x) (hg : HasFDerivWithinAt g g' s x)
    (h : 0 < f x) : HasFDerivWithinAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Real.log (f x)) ‚Ä¢ g') s x := by
  -- `by exact` to deal with tricky unification.
  exact (hasStrictFDerivAt_rpow_of_pos (f x, g x) h).hasFDerivAt.comp_hasFDerivWithinAt x
    (hf.prodMk hg)

theorem HasFDerivAt.rpow (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) (h : 0 < f x) :
    HasFDerivAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Real.log (f x)) ‚Ä¢ g') x := by
  exact (hasStrictFDerivAt_rpow_of_pos (f x, g x) h).hasFDerivAt.comp x (hf.prodMk hg)

theorem HasStrictFDerivAt.rpow (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x)
    (h : 0 < f x) : HasStrictFDerivAt (fun x => f x ^ g x)
      ((g x * f x ^ (g x - 1)) ‚Ä¢ f' + (f x ^ g x * Real.log (f x)) ‚Ä¢ g') x :=
  (hasStrictFDerivAt_rpow_of_pos (f x, g x) h).comp x (hf.prodMk hg)

@[fun_prop]
theorem DifferentiableWithinAt.rpow (hf : DifferentiableWithinAt ‚Ñù f s x)
    (hg : DifferentiableWithinAt ‚Ñù g s x) (h : f x ‚â† 0) :
    DifferentiableWithinAt ‚Ñù (fun x => f x ^ g x) s x := by
  -- `by exact` to deal with tricky unification.
  exact (differentiableAt_rpow_of_ne (f x, g x) h).comp_differentiableWithinAt x (hf.prodMk hg)

@[fun_prop]
theorem DifferentiableAt.rpow (hf : DifferentiableAt ‚Ñù f x) (hg : DifferentiableAt ‚Ñù g x)
    (h : f x ‚â† 0) : DifferentiableAt ‚Ñù (fun x => f x ^ g x) x := by
  -- `by exact` to deal with tricky unification.
  exact (differentiableAt_rpow_of_ne (f x, g x) h).comp x (hf.prodMk hg)

@[fun_prop]
theorem DifferentiableOn.rpow (hf : DifferentiableOn ‚Ñù f s) (hg : DifferentiableOn ‚Ñù g s)
    (h : ‚àÄ x ‚àà s, f x ‚â† 0) : DifferentiableOn ‚Ñù (fun x => f x ^ g x) s := fun x hx =>
  (hf x hx).rpow (hg x hx) (h x hx)

@[fun_prop]
theorem Differentiable.rpow (hf : Differentiable ‚Ñù f) (hg : Differentiable ‚Ñù g) (h : ‚àÄ x, f x ‚â† 0) :
    Differentiable ‚Ñù fun x => f x ^ g x := fun x => (hf x).rpow (hg x) (h x)

@[fun_prop]
theorem HasFDerivWithinAt.rpow_const (hf : HasFDerivWithinAt f f' s x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasFDerivWithinAt (fun x => f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') s x :=
  (hasDerivAt_rpow_const h).comp_hasFDerivWithinAt x hf

@[fun_prop]
theorem HasFDerivAt.rpow_const (hf : HasFDerivAt f f' x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasFDerivAt (fun x => f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') x :=
  (hasDerivAt_rpow_const h).comp_hasFDerivAt x hf

theorem HasStrictFDerivAt.rpow_const (hf : HasStrictFDerivAt f f' x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasStrictFDerivAt (fun x => f x ^ p) ((p * f x ^ (p - 1)) ‚Ä¢ f') x :=
  (hasStrictDerivAt_rpow_const h).comp_hasStrictFDerivAt x hf

@[fun_prop]
theorem DifferentiableWithinAt.rpow_const (hf : DifferentiableWithinAt ‚Ñù f s x)
    (h : f x ‚â† 0 ‚à® 1 ‚â§ p) : DifferentiableWithinAt ‚Ñù (fun x => f x ^ p) s x :=
  (hf.hasFDerivWithinAt.rpow_const h).differentiableWithinAt

@[simp]
theorem DifferentiableAt.rpow_const (hf : DifferentiableAt ‚Ñù f x) (h : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    DifferentiableAt ‚Ñù (fun x => f x ^ p) x :=
  (hf.hasFDerivAt.rpow_const h).differentiableAt

@[fun_prop]
theorem DifferentiableOn.rpow_const (hf : DifferentiableOn ‚Ñù f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0 ‚à® 1 ‚â§ p) :
    DifferentiableOn ‚Ñù (fun x => f x ^ p) s := fun x hx => (hf x hx).rpow_const (h x hx)

@[fun_prop]
theorem Differentiable.rpow_const (hf : Differentiable ‚Ñù f) (h : ‚àÄ x, f x ‚â† 0 ‚à® 1 ‚â§ p) :
    Differentiable ‚Ñù fun x => f x ^ p := fun x => (hf x).rpow_const (h x)

theorem HasFDerivWithinAt.const_rpow (hf : HasFDerivWithinAt f f' s x) (hc : 0 < c) :
    HasFDerivWithinAt (fun x => c ^ f x) ((c ^ f x * Real.log c) ‚Ä¢ f') s x :=
  (hasStrictDerivAt_const_rpow hc (f x)).hasDerivAt.comp_hasFDerivWithinAt x hf

theorem HasFDerivAt.const_rpow (hf : HasFDerivAt f f' x) (hc : 0 < c) :
    HasFDerivAt (fun x => c ^ f x) ((c ^ f x * Real.log c) ‚Ä¢ f') x :=
  (hasStrictDerivAt_const_rpow hc (f x)).hasDerivAt.comp_hasFDerivAt x hf

theorem HasStrictFDerivAt.const_rpow (hf : HasStrictFDerivAt f f' x) (hc : 0 < c) :
    HasStrictFDerivAt (fun x => c ^ f x) ((c ^ f x * Real.log c) ‚Ä¢ f') x :=
  (hasStrictDerivAt_const_rpow hc (f x)).comp_hasStrictFDerivAt x hf

@[fun_prop]
theorem ContDiffWithinAt.rpow (hf : ContDiffWithinAt ‚Ñù n f s x) (hg : ContDiffWithinAt ‚Ñù n g s x)
    (h : f x ‚â† 0) : ContDiffWithinAt ‚Ñù n (fun x => f x ^ g x) s x := by
  -- `by exact` to deal with tricky unification.
  exact (contDiffAt_rpow_of_ne (f x, g x) h).comp_contDiffWithinAt x (hf.prodMk hg)

@[fun_prop]
theorem ContDiffAt.rpow (hf : ContDiffAt ‚Ñù n f x) (hg : ContDiffAt ‚Ñù n g x) (h : f x ‚â† 0) :
    ContDiffAt ‚Ñù n (fun x => f x ^ g x) x := by
  -- `by exact` to deal with tricky unification.
  exact (contDiffAt_rpow_of_ne (f x, g x) h).comp x (hf.prodMk hg)

@[fun_prop]
theorem ContDiffOn.rpow (hf : ContDiffOn ‚Ñù n f s) (hg : ContDiffOn ‚Ñù n g s) (h : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContDiffOn ‚Ñù n (fun x => f x ^ g x) s := fun x hx => (hf x hx).rpow (hg x hx) (h x hx)

@[fun_prop]
theorem ContDiff.rpow (hf : ContDiff ‚Ñù n f) (hg : ContDiff ‚Ñù n g) (h : ‚àÄ x, f x ‚â† 0) :
    ContDiff ‚Ñù n fun x => f x ^ g x :=
  contDiff_iff_contDiffAt.mpr fun x => hf.contDiffAt.rpow hg.contDiffAt (h x)

@[fun_prop]
theorem ContDiffWithinAt.rpow_const_of_ne (hf : ContDiffWithinAt ‚Ñù n f s x) (h : f x ‚â† 0) :
    ContDiffWithinAt ‚Ñù n (fun x => f x ^ p) s x :=
  hf.rpow contDiffWithinAt_const h

@[fun_prop]
theorem ContDiffAt.rpow_const_of_ne (hf : ContDiffAt ‚Ñù n f x) (h : f x ‚â† 0) :
    ContDiffAt ‚Ñù n (fun x => f x ^ p) x :=
  hf.rpow contDiffAt_const h

@[fun_prop]
theorem ContDiffOn.rpow_const_of_ne (hf : ContDiffOn ‚Ñù n f s) (h : ‚àÄ x ‚àà s, f x ‚â† 0) :
    ContDiffOn ‚Ñù n (fun x => f x ^ p) s := fun x hx => (hf x hx).rpow_const_of_ne (h x hx)

@[fun_prop]
theorem ContDiff.rpow_const_of_ne (hf : ContDiff ‚Ñù n f) (h : ‚àÄ x, f x ‚â† 0) :
    ContDiff ‚Ñù n fun x => f x ^ p :=
  hf.rpow contDiff_const h

variable {m : ‚Ñï}

@[fun_prop]
theorem ContDiffWithinAt.rpow_const_of_le (hf : ContDiffWithinAt ‚Ñù m f s x) (h : ‚Üëm ‚â§ p) :
    ContDiffWithinAt ‚Ñù m (fun x => f x ^ p) s x :=
  (contDiffAt_rpow_const_of_le h).comp_contDiffWithinAt x hf

@[fun_prop]
theorem ContDiffAt.rpow_const_of_le (hf : ContDiffAt ‚Ñù m f x) (h : ‚Üëm ‚â§ p) :
    ContDiffAt ‚Ñù m (fun x => f x ^ p) x := by
  rw [‚Üê contDiffWithinAt_univ] at *; exact hf.rpow_const_of_le h

@[fun_prop]
theorem ContDiffOn.rpow_const_of_le (hf : ContDiffOn ‚Ñù m f s) (h : ‚Üëm ‚â§ p) :
    ContDiffOn ‚Ñù m (fun x => f x ^ p) s := fun x hx => (hf x hx).rpow_const_of_le h

@[fun_prop]
theorem ContDiff.rpow_const_of_le (hf : ContDiff ‚Ñù m f) (h : ‚Üëm ‚â§ p) :
    ContDiff ‚Ñù m fun x => f x ^ p :=
  contDiff_iff_contDiffAt.mpr fun _ => hf.contDiffAt.rpow_const_of_le h

end fderiv

section deriv

variable {f g : ‚Ñù ‚Üí ‚Ñù} {f' g' x y p : ‚Ñù} {s : Set ‚Ñù}

theorem HasDerivWithinAt.rpow (hf : HasDerivWithinAt f f' s x) (hg : HasDerivWithinAt g g' s x)
    (h : 0 < f x) : HasDerivWithinAt (fun x => f x ^ g x)
      (f' * g x * f x ^ (g x - 1) + g' * f x ^ g x * Real.log (f x)) s x := by
  convert (hf.hasFDerivWithinAt.rpow hg.hasFDerivWithinAt h).hasDerivWithinAt using 1
  dsimp; ring

theorem HasDerivAt.rpow (hf : HasDerivAt f f' x) (hg : HasDerivAt g g' x) (h : 0 < f x) :
    HasDerivAt (fun x => f x ^ g x)
      (f' * g x * f x ^ (g x - 1) + g' * f x ^ g x * Real.log (f x)) x := by
  rw [‚Üê hasDerivWithinAt_univ] at *
  exact hf.rpow hg h

theorem HasDerivWithinAt.rpow_const (hf : HasDerivWithinAt f f' s x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasDerivWithinAt (fun y => f y ^ p) (f' * p * f x ^ (p - 1)) s x := by
  convert (hasDerivAt_rpow_const hx).comp_hasDerivWithinAt x hf using 1
  ring

theorem HasDerivAt.rpow_const (hf : HasDerivAt f f' x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    HasDerivAt (fun y => f y ^ p) (f' * p * f x ^ (p - 1)) x := by
  rw [‚Üê hasDerivWithinAt_univ] at *
  exact hf.rpow_const hx

theorem derivWithin_rpow_const (hf : DifferentiableWithinAt ‚Ñù f s x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p)
    (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x => f x ^ p) s x = derivWithin f s x * p * f x ^ (p - 1) :=
  (hf.hasDerivWithinAt.rpow_const hx).derivWithin hxs

@[simp]
theorem deriv_rpow_const (hf : DifferentiableAt ‚Ñù f x) (hx : f x ‚â† 0 ‚à® 1 ‚â§ p) :
    deriv (fun x => f x ^ p) x = deriv f x * p * f x ^ (p - 1) :=
  (hf.hasDerivAt.rpow_const hx).deriv

theorem deriv_norm_ofReal_cpow (c : ‚ÑÇ) {t : ‚Ñù} (ht : 0 < t) :
    (deriv fun x : ‚Ñù ‚Ü¶ ‚Äñ(x : ‚ÑÇ) ^ c‚Äñ) t = c.re * t ^ (c.re - 1) := by
  rw [EventuallyEq.deriv_eq (f := fun x ‚Ü¶ x ^ c.re)]
  ¬∑ rw [Real.deriv_rpow_const (Or.inl ht.ne')]
  ¬∑ filter_upwards [eventually_gt_nhds ht] with x hx
    rw [Complex.norm_cpow_eq_rpow_re_of_pos hx]

lemma isTheta_deriv_rpow_const_atTop {p : ‚Ñù} (hp : p ‚â† 0) :
    deriv (fun (x : ‚Ñù) => x ^ p) =Œò[atTop] fun x => x ^ (p-1) := by
  calc deriv (fun (x : ‚Ñù) => x ^ p) =·∂†[atTop] fun x => p * x ^ (p - 1) := by
              filter_upwards [eventually_ne_atTop 0] with x hx
              rw [Real.deriv_rpow_const (Or.inl hx)]
       _ =Œò[atTop] fun x => x ^ (p-1) :=
              Asymptotics.IsTheta.const_mul_left hp Asymptotics.isTheta_rfl

lemma isBigO_deriv_rpow_const_atTop (p : ‚Ñù) :
    deriv (fun (x : ‚Ñù) => x ^ p) =O[atTop] fun x => x ^ (p-1) := by
  rcases eq_or_ne p 0 with rfl | hp
  case inl =>
    simp [zero_sub, Real.rpow_neg_one, Real.rpow_zero, deriv_const', Asymptotics.isBigO_zero]
  case inr =>
    exact (isTheta_deriv_rpow_const_atTop hp).1

end deriv

end Differentiability
