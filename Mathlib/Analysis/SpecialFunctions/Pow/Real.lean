/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, S√©bastien Gou√´zel,
  R√©my Degenne, David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Pow.Complex
import Qq

/-! # Power function on `‚Ñù`

We construct the power functions `x ^ y`, where `x` and `y` are real numbers.
-/


noncomputable section

open Real ComplexConjugate Finset Set

/-
## Definitions
-/
namespace Real
variable {x y z : ‚Ñù}

/-- The real power function `x ^ y`, defined as the real part of the complex power function.
For `x > 0`, it is equal to `exp (y log x)`. For `x = 0`, one sets `0 ^ 0=1` and `0 ^ y=0` for
`y ‚â† 0`. For `x < 0`, the definition is somewhat arbitrary as it depends on the choice of a complex
determination of the logarithm. With our conventions, it is equal to `exp (y log x) cos (œÄ y)`. -/
noncomputable def rpow (x y : ‚Ñù) :=
  ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re

noncomputable instance : Pow ‚Ñù ‚Ñù := ‚ü®rpow‚ü©

@[simp]
theorem rpow_eq_pow (x y : ‚Ñù) : rpow x y = x ^ y := rfl

theorem rpow_def (x y : ‚Ñù) : x ^ y = ((x : ‚ÑÇ) ^ (y : ‚ÑÇ)).re := rfl

theorem rpow_def_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) :
    x ^ y = if x = 0 then if y = 0 then 1 else 0 else exp (log x * y) := by
  simp only [rpow_def, Complex.cpow_def]; split_ifs <;>
  simp_all [(Complex.ofReal_log hx).symm, -Complex.ofReal_mul,
      (Complex.ofReal_mul _ _).symm, Complex.exp_ofReal_re, Complex.ofReal_eq_zero]

theorem rpow_def_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : x ^ y = exp (log x * y) := by
  rw [rpow_def_of_nonneg (le_of_lt hx), if_neg (ne_of_gt hx)]

theorem exp_mul (x y : ‚Ñù) : exp (x * y) = exp x ^ y := by rw [rpow_def_of_pos (exp_pos _), log_exp]

@[simp, norm_cast]
theorem rpow_intCast (x : ‚Ñù) (n : ‚Ñ§) : x ^ (n : ‚Ñù) = x ^ n := by
  simp only [rpow_def, ‚Üê Complex.ofReal_zpow, Complex.cpow_intCast, Complex.ofReal_intCast,
    Complex.ofReal_re]

@[deprecated (since := "2024-04-17")]
alias rpow_int_cast := rpow_intCast

@[simp, norm_cast]
theorem rpow_natCast (x : ‚Ñù) (n : ‚Ñï) : x ^ (n : ‚Ñù) = x ^ n := by simpa using rpow_intCast x n

@[deprecated (since := "2024-04-17")]
alias rpow_nat_cast := rpow_natCast

@[simp]
theorem exp_one_rpow (x : ‚Ñù) : exp 1 ^ x = exp x := by rw [‚Üê exp_mul, one_mul]

@[simp] lemma exp_one_pow (n : ‚Ñï) : exp 1 ^ n = exp n := by rw [‚Üê rpow_natCast, exp_one_rpow]

theorem rpow_eq_zero_iff_of_nonneg (hx : 0 ‚â§ x) : x ^ y = 0 ‚Üî x = 0 ‚àß y ‚â† 0 := by
  simp only [rpow_def_of_nonneg hx]
  split_ifs <;> simp [*, exp_ne_zero]

@[simp]
lemma rpow_eq_zero (hx : 0 ‚â§ x) (hy : y ‚â† 0) : x ^ y = 0 ‚Üî x = 0 := by
  simp [rpow_eq_zero_iff_of_nonneg, *]

@[simp]
lemma rpow_ne_zero (hx : 0 ‚â§ x) (hy : y ‚â† 0) : x ^ y ‚â† 0 ‚Üî x ‚â† 0 :=
  Real.rpow_eq_zero hx hy |>.not

open Real

theorem rpow_def_of_neg {x : ‚Ñù} (hx : x < 0) (y : ‚Ñù) : x ^ y = exp (log x * y) * cos (y * œÄ) := by
  rw [rpow_def, Complex.cpow_def, if_neg]
  ¬∑ have : Complex.log x * y = ‚Üë(log (-x) * y) + ‚Üë(y * œÄ) * Complex.I := by
      simp only [Complex.log, abs_of_neg hx, Complex.arg_ofReal_of_neg hx, Complex.abs_ofReal,
        Complex.ofReal_mul]
      ring
    rw [this, Complex.exp_add_mul_I, ‚Üê Complex.ofReal_exp, ‚Üê Complex.ofReal_cos, ‚Üê
      Complex.ofReal_sin, mul_add, ‚Üê Complex.ofReal_mul, ‚Üê mul_assoc, ‚Üê Complex.ofReal_mul,
      Complex.add_re, Complex.ofReal_re, Complex.mul_re, Complex.I_re, Complex.ofReal_im,
      Real.log_neg_eq_log]
    ring
  ¬∑ rw [Complex.ofReal_eq_zero]
    exact ne_of_lt hx

theorem rpow_def_of_nonpos {x : ‚Ñù} (hx : x ‚â§ 0) (y : ‚Ñù) :
    x ^ y = if x = 0 then if y = 0 then 1 else 0 else exp (log x * y) * cos (y * œÄ) := by
  split_ifs with h <;> simp [rpow_def, *]; exact rpow_def_of_neg (lt_of_le_of_ne hx h) _

@[bound]
theorem rpow_pos_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : 0 < x ^ y := by
  rw [rpow_def_of_pos hx]; apply exp_pos

@[simp]
theorem rpow_zero (x : ‚Ñù) : x ^ (0 : ‚Ñù) = 1 := by simp [rpow_def]

theorem rpow_zero_pos (x : ‚Ñù) : 0 < x ^ (0 : ‚Ñù) := by simp

@[simp]
theorem zero_rpow {x : ‚Ñù} (h : x ‚â† 0) : (0 : ‚Ñù) ^ x = 0 := by simp [rpow_def, *]

theorem zero_rpow_eq_iff {x : ‚Ñù} {a : ‚Ñù} : 0 ^ x = a ‚Üî x ‚â† 0 ‚àß a = 0 ‚à® x = 0 ‚àß a = 1 := by
  constructor
  ¬∑ intro hyp
    simp only [rpow_def, Complex.ofReal_zero] at hyp
    by_cases h : x = 0
    ¬∑ subst h
      simp only [Complex.one_re, Complex.ofReal_zero, Complex.cpow_zero] at hyp
      exact Or.inr ‚ü®rfl, hyp.symm‚ü©
    ¬∑ rw [Complex.zero_cpow (Complex.ofReal_ne_zero.mpr h)] at hyp
      exact Or.inl ‚ü®h, hyp.symm‚ü©
  ¬∑ rintro (‚ü®h, rfl‚ü© | ‚ü®rfl, rfl‚ü©)
    ¬∑ exact zero_rpow h
    ¬∑ exact rpow_zero _

theorem eq_zero_rpow_iff {x : ‚Ñù} {a : ‚Ñù} : a = 0 ^ x ‚Üî x ‚â† 0 ‚àß a = 0 ‚à® x = 0 ‚àß a = 1 := by
  rw [‚Üê zero_rpow_eq_iff, eq_comm]

@[simp]
theorem rpow_one (x : ‚Ñù) : x ^ (1 : ‚Ñù) = x := by simp [rpow_def]

@[simp]
theorem one_rpow (x : ‚Ñù) : (1 : ‚Ñù) ^ x = 1 := by simp [rpow_def]

theorem zero_rpow_le_one (x : ‚Ñù) : (0 : ‚Ñù) ^ x ‚â§ 1 := by
  by_cases h : x = 0 <;> simp [h, zero_le_one]

theorem zero_rpow_nonneg (x : ‚Ñù) : 0 ‚â§ (0 : ‚Ñù) ^ x := by
  by_cases h : x = 0 <;> simp [h, zero_le_one]

@[bound]
theorem rpow_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : 0 ‚â§ x ^ y := by
  rw [rpow_def_of_nonneg hx]; split_ifs <;>
    simp only [zero_le_one, le_refl, le_of_lt (exp_pos _)]

theorem abs_rpow_of_nonneg {x y : ‚Ñù} (hx_nonneg : 0 ‚â§ x) : |x ^ y| = |x| ^ y := by
  have h_rpow_nonneg : 0 ‚â§ x ^ y := Real.rpow_nonneg hx_nonneg _
  rw [abs_eq_self.mpr hx_nonneg, abs_eq_self.mpr h_rpow_nonneg]

@[bound]
theorem abs_rpow_le_abs_rpow (x y : ‚Ñù) : |x ^ y| ‚â§ |x| ^ y := by
  rcases le_or_lt 0 x with hx | hx
  ¬∑ rw [abs_rpow_of_nonneg hx]
  ¬∑ rw [abs_of_neg hx, rpow_def_of_neg hx, rpow_def_of_pos (neg_pos.2 hx), log_neg_eq_log, abs_mul,
      abs_of_pos (exp_pos _)]
    exact mul_le_of_le_one_right (exp_pos _).le (abs_cos_le_one _)

theorem abs_rpow_le_exp_log_mul (x y : ‚Ñù) : |x ^ y| ‚â§ exp (log x * y) := by
  refine (abs_rpow_le_abs_rpow x y).trans ?_
  by_cases hx : x = 0
  ¬∑ by_cases hy : y = 0 <;> simp [hx, hy, zero_le_one]
  ¬∑ rw [rpow_def_of_pos (abs_pos.2 hx), log_abs]

lemma rpow_inv_log (hx‚ÇÄ : 0 < x) (hx‚ÇÅ : x ‚â† 1) : x ^ (log x)‚Åª¬π = exp 1 := by
  rw [rpow_def_of_pos hx‚ÇÄ, mul_inv_cancel‚ÇÄ]
  exact log_ne_zero.2 ‚ü®hx‚ÇÄ.ne', hx‚ÇÅ, (hx‚ÇÄ.trans' <| by norm_num).ne'‚ü©

/-- See `Real.rpow_inv_log` for the equality when `x ‚â† 1` is strictly positive. -/
lemma rpow_inv_log_le_exp_one : x ^ (log x)‚Åª¬π ‚â§ exp 1 := by
  calc
    _ ‚â§ |x ^ (log x)‚Åª¬π| := le_abs_self _
    _ ‚â§ |x| ^ (log x)‚Åª¬π := abs_rpow_le_abs_rpow ..
  rw [‚Üê log_abs]
  obtain hx | hx := (abs_nonneg x).eq_or_gt
  ¬∑ simp [hx]
  ¬∑ rw [rpow_def_of_pos hx]
    gcongr
    exact mul_inv_le_one

theorem norm_rpow_of_nonneg {x y : ‚Ñù} (hx_nonneg : 0 ‚â§ x) : ‚Äñx ^ y‚Äñ = ‚Äñx‚Äñ ^ y := by
  simp_rw [Real.norm_eq_abs]
  exact abs_rpow_of_nonneg hx_nonneg

variable {w x y z : ‚Ñù}

theorem rpow_add (hx : 0 < x) (y z : ‚Ñù) : x ^ (y + z) = x ^ y * x ^ z := by
  simp only [rpow_def_of_pos hx, mul_add, exp_add]

theorem rpow_add' (hx : 0 ‚â§ x) (h : y + z ‚â† 0) : x ^ (y + z) = x ^ y * x ^ z := by
  rcases hx.eq_or_lt with (rfl | pos)
  ¬∑ rw [zero_rpow h, zero_eq_mul]
    have : y ‚â† 0 ‚à® z ‚â† 0 := not_and_or.1 fun ‚ü®hy, hz‚ü© => h <| hy.symm ‚ñ∏ hz.symm ‚ñ∏ zero_add 0
    exact this.imp zero_rpow zero_rpow
  ¬∑ exact rpow_add pos _ _

/-- Variant of `Real.rpow_add'` that avoids having to prove `y + z = w` twice. -/
lemma rpow_of_add_eq (hx : 0 ‚â§ x) (hw : w ‚â† 0) (h : y + z = w) : x ^ w = x ^ y * x ^ z := by
  rw [‚Üê h, rpow_add' hx]; rwa [h]

theorem rpow_add_of_nonneg (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 ‚â§ z) :
    x ^ (y + z) = x ^ y * x ^ z := by
  rcases hy.eq_or_lt with (rfl | hy)
  ¬∑ rw [zero_add, rpow_zero, one_mul]
  exact rpow_add' hx (ne_of_gt <| add_pos_of_pos_of_nonneg hy hz)

/-- For `0 ‚â§ x`, the only problematic case in the equality `x ^ y * x ^ z = x ^ (y + z)` is for
`x = 0` and `y + z = 0`, where the right hand side is `1` while the left hand side can vanish.
The inequality is always true, though, and given in this lemma. -/
theorem le_rpow_add {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ y * x ^ z ‚â§ x ^ (y + z) := by
  rcases le_iff_eq_or_lt.1 hx with (H | pos)
  ¬∑ by_cases h : y + z = 0
    ¬∑ simp only [H.symm, h, rpow_zero]
      calc
        (0 : ‚Ñù) ^ y * 0 ^ z ‚â§ 1 * 1 :=
          mul_le_mul (zero_rpow_le_one y) (zero_rpow_le_one z) (zero_rpow_nonneg z) zero_le_one
        _ = 1 := by simp

    ¬∑ simp [rpow_add', ‚Üê H, h]
  ¬∑ simp [rpow_add pos]

theorem rpow_sum_of_pos {Œπ : Type*} {a : ‚Ñù} (ha : 0 < a) (f : Œπ ‚Üí ‚Ñù) (s : Finset Œπ) :
    (a ^ ‚àë x ‚àà s, f x) = ‚àè x ‚àà s, a ^ f x :=
  map_sum (‚ü®‚ü®fun (x : ‚Ñù) => (a ^ x : ‚Ñù), rpow_zero a‚ü©, rpow_add ha‚ü© : ‚Ñù ‚Üí+ (Additive ‚Ñù)) f s

theorem rpow_sum_of_nonneg {Œπ : Type*} {a : ‚Ñù} (ha : 0 ‚â§ a) {s : Finset Œπ} {f : Œπ ‚Üí ‚Ñù}
    (h : ‚àÄ x ‚àà s, 0 ‚â§ f x) : (a ^ ‚àë x ‚àà s, f x) = ‚àè x ‚àà s, a ^ f x := by
  induction' s using Finset.cons_induction with i s hi ihs
  ¬∑ rw [sum_empty, Finset.prod_empty, rpow_zero]
  ¬∑ rw [forall_mem_cons] at h
    rw [sum_cons, prod_cons, ‚Üê ihs h.2, rpow_add_of_nonneg ha h.1 (sum_nonneg h.2)]

theorem rpow_neg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : x ^ (-y) = (x ^ y)‚Åª¬π := by
  simp only [rpow_def_of_nonneg hx]; split_ifs <;> simp_all [exp_neg]

theorem rpow_sub {x : ‚Ñù} (hx : 0 < x) (y z : ‚Ñù) : x ^ (y - z) = x ^ y / x ^ z := by
  simp only [sub_eq_add_neg, rpow_add hx, rpow_neg (le_of_lt hx), div_eq_mul_inv]

theorem rpow_sub' {x : ‚Ñù} (hx : 0 ‚â§ x) {y z : ‚Ñù} (h : y - z ‚â† 0) : x ^ (y - z) = x ^ y / x ^ z := by
  simp only [sub_eq_add_neg] at h ‚ä¢
  simp only [rpow_add' hx h, rpow_neg hx, div_eq_mul_inv]

protected theorem _root_.HasCompactSupport.rpow_const {Œ± : Type*} [TopologicalSpace Œ±] {f : Œ± ‚Üí ‚Ñù}
    (hf : HasCompactSupport f) {r : ‚Ñù} (hr : r ‚â† 0) : HasCompactSupport (fun x ‚Ü¶ f x ^ r) :=
  hf.comp_left (g := (¬∑ ^ r)) (Real.zero_rpow hr)

end Real

/-!
## Comparing real and complex powers
-/


namespace Complex

theorem ofReal_cpow {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) : ((x ^ y : ‚Ñù) : ‚ÑÇ) = (x : ‚ÑÇ) ^ (y : ‚ÑÇ) := by
  simp only [Real.rpow_def_of_nonneg hx, Complex.cpow_def, ofReal_eq_zero]; split_ifs <;>
    simp [Complex.ofReal_log hx]

theorem ofReal_cpow_of_nonpos {x : ‚Ñù} (hx : x ‚â§ 0) (y : ‚ÑÇ) :
    (x : ‚ÑÇ) ^ y = (-x : ‚ÑÇ) ^ y * exp (œÄ * I * y) := by
  rcases hx.eq_or_lt with (rfl | hlt)
  ¬∑ rcases eq_or_ne y 0 with (rfl | hy) <;> simp [*]
  have hne : (x : ‚ÑÇ) ‚â† 0 := ofReal_ne_zero.mpr hlt.ne
  rw [cpow_def_of_ne_zero hne, cpow_def_of_ne_zero (neg_ne_zero.2 hne), ‚Üê exp_add, ‚Üê add_mul, log,
    log, abs.map_neg, arg_ofReal_of_neg hlt, ‚Üê ofReal_neg,
    arg_ofReal_of_nonneg (neg_nonneg.2 hx), ofReal_zero, zero_mul, add_zero]

lemma cpow_ofReal (x : ‚ÑÇ) (y : ‚Ñù) :
    x ^ (y : ‚ÑÇ) = ‚Üë(abs x ^ y) * (Real.cos (arg x * y) + Real.sin (arg x * y) * I) := by
  rcases eq_or_ne x 0 with rfl | hx
  ¬∑ simp [ofReal_cpow le_rfl]
  ¬∑ rw [cpow_def_of_ne_zero hx, exp_eq_exp_re_mul_sin_add_cos, mul_comm (log x)]
    norm_cast
    rw [re_ofReal_mul, im_ofReal_mul, log_re, log_im, mul_comm y, mul_comm y, Real.exp_mul,
      Real.exp_log]
    rwa [abs.pos_iff]

lemma cpow_ofReal_re (x : ‚ÑÇ) (y : ‚Ñù) : (x ^ (y : ‚ÑÇ)).re = (abs x) ^ y * Real.cos (arg x * y) := by
  rw [cpow_ofReal]; generalize arg x * y = z; simp [Real.cos]

lemma cpow_ofReal_im (x : ‚ÑÇ) (y : ‚Ñù) : (x ^ (y : ‚ÑÇ)).im = (abs x) ^ y * Real.sin (arg x * y) := by
  rw [cpow_ofReal]; generalize arg x * y = z; simp [Real.sin]

theorem abs_cpow_of_ne_zero {z : ‚ÑÇ} (hz : z ‚â† 0) (w : ‚ÑÇ) :
    abs (z ^ w) = abs z ^ w.re / Real.exp (arg z * im w) := by
  rw [cpow_def_of_ne_zero hz, abs_exp, mul_re, log_re, log_im, Real.exp_sub,
    Real.rpow_def_of_pos (abs.pos hz)]

theorem abs_cpow_of_imp {z w : ‚ÑÇ} (h : z = 0 ‚Üí w.re = 0 ‚Üí w = 0) :
    abs (z ^ w) = abs z ^ w.re / Real.exp (arg z * im w) := by
  rcases ne_or_eq z 0 with (hz | rfl) <;> [exact abs_cpow_of_ne_zero hz w; rw [map_zero]]
  rcases eq_or_ne w.re 0 with hw | hw
  ¬∑ simp [hw, h rfl hw]
  ¬∑ rw [Real.zero_rpow hw, zero_div, zero_cpow, map_zero]
    exact ne_of_apply_ne re hw

theorem abs_cpow_le (z w : ‚ÑÇ) : abs (z ^ w) ‚â§ abs z ^ w.re / Real.exp (arg z * im w) := by
  by_cases h : z = 0 ‚Üí w.re = 0 ‚Üí w = 0
  ¬∑ exact (abs_cpow_of_imp h).le
  ¬∑ push_neg at h
    simp [h]

@[simp]
theorem abs_cpow_real (x : ‚ÑÇ) (y : ‚Ñù) : abs (x ^ (y : ‚ÑÇ)) = Complex.abs x ^ y := by
  rw [abs_cpow_of_imp] <;> simp

@[simp]
theorem abs_cpow_inv_nat (x : ‚ÑÇ) (n : ‚Ñï) : abs (x ^ (n‚Åª¬π : ‚ÑÇ)) = Complex.abs x ^ (n‚Åª¬π : ‚Ñù) := by
  rw [‚Üê abs_cpow_real]; simp [-abs_cpow_real]

theorem abs_cpow_eq_rpow_re_of_pos {x : ‚Ñù} (hx : 0 < x) (y : ‚ÑÇ) : abs (x ^ y) = x ^ y.re := by
  rw [abs_cpow_of_ne_zero (ofReal_ne_zero.mpr hx.ne'), arg_ofReal_of_nonneg hx.le,
    zero_mul, Real.exp_zero, div_one, abs_of_nonneg hx.le]

theorem abs_cpow_eq_rpow_re_of_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) {y : ‚ÑÇ} (hy : re y ‚â† 0) :
    abs (x ^ y) = x ^ re y := by
  rw [abs_cpow_of_imp] <;> simp [*, arg_ofReal_of_nonneg, _root_.abs_of_nonneg]

open Filter in
lemma norm_ofReal_cpow_eventually_eq_atTop (c : ‚ÑÇ) :
    (fun t : ‚Ñù ‚Ü¶ ‚Äñ(t : ‚ÑÇ) ^ c‚Äñ) =·∂†[atTop] fun t ‚Ü¶ t ^ c.re := by
  filter_upwards [eventually_gt_atTop 0] with t ht
  rw [Complex.norm_eq_abs, abs_cpow_eq_rpow_re_of_pos ht]

lemma norm_natCast_cpow_of_re_ne_zero (n : ‚Ñï) {s : ‚ÑÇ} (hs : s.re ‚â† 0) :
    ‚Äñ(n : ‚ÑÇ) ^ s‚Äñ = (n : ‚Ñù) ^ (s.re) := by
  rw [norm_eq_abs, ‚Üê ofReal_natCast, abs_cpow_eq_rpow_re_of_nonneg n.cast_nonneg hs]

lemma norm_natCast_cpow_of_pos {n : ‚Ñï} (hn : 0 < n) (s : ‚ÑÇ) :
    ‚Äñ(n : ‚ÑÇ) ^ s‚Äñ = (n : ‚Ñù) ^ (s.re) := by
  rw [norm_eq_abs, ‚Üê ofReal_natCast, abs_cpow_eq_rpow_re_of_pos (Nat.cast_pos.mpr hn) _]

lemma norm_natCast_cpow_pos_of_pos {n : ‚Ñï} (hn : 0 < n) (s : ‚ÑÇ) : 0 < ‚Äñ(n : ‚ÑÇ) ^ s‚Äñ :=
  (norm_natCast_cpow_of_pos hn _).symm ‚ñ∏ Real.rpow_pos_of_pos (Nat.cast_pos.mpr hn) _

theorem cpow_mul_ofReal_nonneg {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) (z : ‚ÑÇ) :
    (x : ‚ÑÇ) ^ (‚Üëy * z) = (‚Üë(x ^ y) : ‚ÑÇ) ^ z := by
  rw [cpow_mul, ofReal_cpow hx]
  ¬∑ rw [‚Üê ofReal_log hx, ‚Üê ofReal_mul, ofReal_im, neg_lt_zero]; exact Real.pi_pos
  ¬∑ rw [‚Üê ofReal_log hx, ‚Üê ofReal_mul, ofReal_im]; exact Real.pi_pos.le

end Complex

/-! ### Positivity extension -/

namespace Mathlib.Meta.Positivity
open Lean Meta Qq

/-- Extension for the `positivity` tactic: exponentiation by a real number is positive (namely 1)
when the exponent is zero. The other cases are done in `evalRpow`. -/
@[positivity (_ : ‚Ñù) ^ (0 : ‚Ñù)]
def evalRpowZero : PositivityExt where eval {u Œ±} _ _ e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q($a ^ (0 : ‚Ñù)) =>
    assertInstancesCommute
    pure (.positive q(Real.rpow_zero_pos $a))
  | _, _, _ => throwError "not Real.rpow"

/-- Extension for the `positivity` tactic: exponentiation by a real number is nonnegative when
the base is nonnegative and positive when the base is positive. -/
@[positivity (_ : ‚Ñù) ^ (_ : ‚Ñù)]
def evalRpow : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q($a ^ ($b : ‚Ñù)) =>
    let ra ‚Üê core q(inferInstance) q(inferInstance) a
    assertInstancesCommute
    match ra with
    | .positive pa =>
        pure (.positive q(Real.rpow_pos_of_pos $pa $b))
    | .nonnegative pa =>
        pure (.nonnegative q(Real.rpow_nonneg $pa $b))
    | _ => pure .none
  | _, _, _ => throwError "not Real.rpow"

end Mathlib.Meta.Positivity

/-!
## Further algebraic properties of `rpow`
-/


namespace Real

variable {x y z : ‚Ñù} {n : ‚Ñï}

theorem rpow_mul {x : ‚Ñù} (hx : 0 ‚â§ x) (y z : ‚Ñù) : x ^ (y * z) = (x ^ y) ^ z := by
  rw [‚Üê Complex.ofReal_inj, Complex.ofReal_cpow (rpow_nonneg hx _),
      Complex.ofReal_cpow hx, Complex.ofReal_mul, Complex.cpow_mul, Complex.ofReal_cpow hx] <;>
    simp only [(Complex.ofReal_mul _ _).symm, (Complex.ofReal_log hx).symm, Complex.ofReal_im,
      neg_lt_zero, pi_pos, le_of_lt pi_pos]

lemma rpow_pow_comm {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) (n : ‚Ñï) : (x ^ y) ^ n = (x ^ n) ^ y := by
  simp_rw [‚Üê rpow_natCast, ‚Üê rpow_mul hx, mul_comm y]

lemma rpow_zpow_comm {x : ‚Ñù} (hx : 0 ‚â§ x) (y : ‚Ñù) (n : ‚Ñ§) : (x ^ y) ^ n = (x ^ n) ^ y := by
  simp_rw [‚Üê rpow_intCast, ‚Üê rpow_mul hx, mul_comm y]

lemma rpow_add_intCast {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñ§) : x ^ (y + n) = x ^ y * x ^ n := by
  rw [rpow_def, rpow_def, Complex.ofReal_add,
    Complex.cpow_add _ _ (Complex.ofReal_ne_zero.mpr hx), Complex.ofReal_intCast,
    Complex.cpow_intCast, ‚Üê Complex.ofReal_zpow, mul_comm, Complex.re_ofReal_mul, mul_comm]

lemma rpow_add_natCast {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y + n) = x ^ y * x ^ n := by
  simpa using rpow_add_intCast hx y n

lemma rpow_sub_intCast {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y - n) = x ^ y / x ^ n := by
  simpa using rpow_add_intCast hx y (-n)

lemma rpow_sub_natCast {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y - n) = x ^ y / x ^ n := by
  simpa using rpow_sub_intCast hx y n

lemma rpow_add_intCast' (hx : 0 ‚â§ x) {n : ‚Ñ§} (h : y + n ‚â† 0) : x ^ (y + n) = x ^ y * x ^ n := by
  rw [rpow_add' hx h, rpow_intCast]

lemma rpow_add_natCast' (hx : 0 ‚â§ x) (h : y + n ‚â† 0) : x ^ (y + n) = x ^ y * x ^ n := by
  rw [rpow_add' hx h, rpow_natCast]

lemma rpow_sub_intCast' (hx : 0 ‚â§ x) {n : ‚Ñ§} (h : y - n ‚â† 0) : x ^ (y - n) = x ^ y / x ^ n := by
  rw [rpow_sub' hx h, rpow_intCast]

lemma rpow_sub_natCast' (hx : 0 ‚â§ x) (h : y - n ‚â† 0) : x ^ (y - n) = x ^ y / x ^ n := by
  rw [rpow_sub' hx h, rpow_natCast]

@[deprecated (since := "2024-08-28")] alias rpow_add_int := rpow_add_intCast
@[deprecated (since := "2024-08-28")] alias rpow_add_nat := rpow_add_natCast
@[deprecated (since := "2024-08-28")] alias rpow_sub_int := rpow_sub_intCast
@[deprecated (since := "2024-08-28")] alias rpow_sub_nat := rpow_sub_natCast
@[deprecated (since := "2024-08-28")] alias rpow_add_int' := rpow_add_intCast'
@[deprecated (since := "2024-08-28")] alias rpow_add_nat' := rpow_add_natCast'
@[deprecated (since := "2024-08-28")] alias rpow_sub_int' := rpow_sub_intCast'
@[deprecated (since := "2024-08-28")] alias rpow_sub_nat' := rpow_sub_natCast'

theorem rpow_add_one {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) : x ^ (y + 1) = x ^ y * x := by
  simpa using rpow_add_natCast hx y 1

theorem rpow_sub_one {x : ‚Ñù} (hx : x ‚â† 0) (y : ‚Ñù) : x ^ (y - 1) = x ^ y / x := by
  simpa using rpow_sub_natCast hx y 1

lemma rpow_add_one' (hx : 0 ‚â§ x) (h : y + 1 ‚â† 0) : x ^ (y + 1) = x ^ y * x := by
  rw [rpow_add' hx h, rpow_one]

lemma rpow_one_add' (hx : 0 ‚â§ x) (h : 1 + y ‚â† 0) : x ^ (1 + y) = x * x ^ y := by
  rw [rpow_add' hx h, rpow_one]

lemma rpow_sub_one' (hx : 0 ‚â§ x) (h : y - 1 ‚â† 0) : x ^ (y - 1) = x ^ y / x := by
  rw [rpow_sub' hx h, rpow_one]

lemma rpow_one_sub' (hx : 0 ‚â§ x) (h : 1 - y ‚â† 0) : x ^ (1 - y) = x / x ^ y := by
  rw [rpow_sub' hx h, rpow_one]

@[simp]
theorem rpow_two (x : ‚Ñù) : x ^ (2 : ‚Ñù) = x ^ 2 := by
  rw [‚Üê rpow_natCast]
  simp only [Nat.cast_ofNat]

theorem rpow_neg_one (x : ‚Ñù) : x ^ (-1 : ‚Ñù) = x‚Åª¬π := by
  suffices H : x ^ ((-1 : ‚Ñ§) : ‚Ñù) = x‚Åª¬π by rwa [Int.cast_neg, Int.cast_one] at H
  simp only [rpow_intCast, zpow_one, zpow_neg]

theorem mul_rpow (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) : (x * y) ^ z = x ^ z * y ^ z := by
  iterate 2 rw [Real.rpow_def_of_nonneg]; split_ifs with h_ifs <;> simp_all
  ¬∑ rw [log_mul ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫, add_mul, exp_add, rpow_def_of_pos (hy.lt_of_ne' ‚Äπ_‚Ä∫)]
  all_goals positivity

theorem inv_rpow (hx : 0 ‚â§ x) (y : ‚Ñù) : x‚Åª¬π ^ y = (x ^ y)‚Åª¬π := by
  simp only [‚Üê rpow_neg_one, ‚Üê rpow_mul hx, mul_comm]

theorem div_rpow (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (z : ‚Ñù) : (x / y) ^ z = x ^ z / y ^ z := by
  simp only [div_eq_mul_inv, mul_rpow hx (inv_nonneg.2 hy), inv_rpow hy]

theorem log_rpow {x : ‚Ñù} (hx : 0 < x) (y : ‚Ñù) : log (x ^ y) = y * log x := by
  apply exp_injective
  rw [exp_log (rpow_pos_of_pos hx y), ‚Üê exp_log hx, mul_comm, rpow_def_of_pos (exp_pos (log x)) y]

theorem mul_log_eq_log_iff {x y z : ‚Ñù} (hx : 0 < x) (hz : 0 < z) :
    y * log x = log z ‚Üî x ^ y = z :=
  ‚ü®fun h ‚Ü¶ log_injOn_pos (rpow_pos_of_pos hx _) hz <| log_rpow hx _ |>.trans h,
  by rintro rfl; rw [log_rpow hx]‚ü©

@[simp] lemma rpow_rpow_inv (hx : 0 ‚â§ x) (hy : y ‚â† 0) : (x ^ y) ^ y‚Åª¬π = x := by
  rw [‚Üê rpow_mul hx, mul_inv_cancel‚ÇÄ hy, rpow_one]

@[simp] lemma rpow_inv_rpow (hx : 0 ‚â§ x) (hy : y ‚â† 0) : (x ^ y‚Åª¬π) ^ y = x := by
  rw [‚Üê rpow_mul hx, inv_mul_cancel‚ÇÄ hy, rpow_one]

theorem pow_rpow_inv_natCast (hx : 0 ‚â§ x) (hn : n ‚â† 0) : (x ^ n) ^ (n‚Åª¬π : ‚Ñù) = x := by
  have hn0 : (n : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.2 hn
  rw [‚Üê rpow_natCast, ‚Üê rpow_mul hx, mul_inv_cancel‚ÇÄ hn0, rpow_one]

theorem rpow_inv_natCast_pow (hx : 0 ‚â§ x) (hn : n ‚â† 0) : (x ^ (n‚Åª¬π : ‚Ñù)) ^ n = x := by
  have hn0 : (n : ‚Ñù) ‚â† 0 := Nat.cast_ne_zero.2 hn
  rw [‚Üê rpow_natCast, ‚Üê rpow_mul hx, inv_mul_cancel‚ÇÄ hn0, rpow_one]

lemma rpow_natCast_mul (hx : 0 ‚â§ x) (n : ‚Ñï) (z : ‚Ñù) : x ^ (n * z) = (x ^ n) ^ z := by
  rw [rpow_mul hx, rpow_natCast]

lemma rpow_mul_natCast (hx : 0 ‚â§ x) (y : ‚Ñù) (n : ‚Ñï) : x ^ (y * n) = (x ^ y) ^ n := by
  rw [rpow_mul hx, rpow_natCast]

lemma rpow_intCast_mul (hx : 0 ‚â§ x) (n : ‚Ñ§) (z : ‚Ñù) : x ^ (n * z) = (x ^ n) ^ z := by
  rw [rpow_mul hx, rpow_intCast]

lemma rpow_mul_intCast (hx : 0 ‚â§ x) (y : ‚Ñù) (n : ‚Ñ§) : x ^ (y * n) = (x ^ y) ^ n := by
  rw [rpow_mul hx, rpow_intCast]

/-! Note: lemmas about `(‚àè i ‚àà s, f i ^ r)` such as `Real.finset_prod_rpow` are proved
in `Mathlib/Analysis/SpecialFunctions/Pow/NNReal.lean` instead. -/

/-!
## Order and monotonicity
-/


@[gcongr, bound]
theorem rpow_lt_rpow (hx : 0 ‚â§ x) (hxy : x < y) (hz : 0 < z) : x ^ z < y ^ z := by
  rw [le_iff_eq_or_lt] at hx; cases' hx with hx hx
  ¬∑ rw [‚Üê hx, zero_rpow (ne_of_gt hz)]
    exact rpow_pos_of_pos (by rwa [‚Üê hx] at hxy) _
  ¬∑ rw [rpow_def_of_pos hx, rpow_def_of_pos (lt_trans hx hxy), exp_lt_exp]
    exact mul_lt_mul_of_pos_right (log_lt_log hx hxy) hz

theorem strictMonoOn_rpow_Ici_of_exponent_pos {r : ‚Ñù} (hr : 0 < r) :
    StrictMonoOn (fun (x : ‚Ñù) => x ^ r) (Set.Ici 0) :=
  fun _ ha _ _ hab => rpow_lt_rpow ha hab hr

@[gcongr, bound]
theorem rpow_le_rpow {x y z : ‚Ñù} (h : 0 ‚â§ x) (h‚ÇÅ : x ‚â§ y) (h‚ÇÇ : 0 ‚â§ z) : x ^ z ‚â§ y ^ z := by
  rcases eq_or_lt_of_le h‚ÇÅ with (rfl | h‚ÇÅ'); ¬∑ rfl
  rcases eq_or_lt_of_le h‚ÇÇ with (rfl | h‚ÇÇ'); ¬∑ simp
  exact le_of_lt (rpow_lt_rpow h h‚ÇÅ' h‚ÇÇ')

theorem monotoneOn_rpow_Ici_of_exponent_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) :
    MonotoneOn (fun (x : ‚Ñù) => x ^ r) (Set.Ici 0) :=
  fun _ ha _ _ hab => rpow_le_rpow ha hab hr

lemma rpow_lt_rpow_of_neg (hx : 0 < x) (hxy : x < y) (hz : z < 0) : y ^ z < x ^ z := by
  have := hx.trans hxy
  rw [‚Üê inv_lt_inv‚ÇÄ, ‚Üê rpow_neg, ‚Üê rpow_neg]
  on_goal 1 => refine rpow_lt_rpow ?_ hxy (neg_pos.2 hz)
  all_goals positivity

lemma rpow_le_rpow_of_nonpos (hx : 0 < x) (hxy : x ‚â§ y) (hz : z ‚â§ 0) : y ^ z ‚â§ x ^ z := by
  have := hx.trans_le hxy
  rw [‚Üê inv_le_inv‚ÇÄ, ‚Üê rpow_neg, ‚Üê rpow_neg]
  on_goal 1 => refine rpow_le_rpow ?_ hxy (neg_nonneg.2 hz)
  all_goals positivity

theorem rpow_lt_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z < y ^ z ‚Üî x < y :=
  ‚ü®lt_imp_lt_of_le_imp_le fun h => rpow_le_rpow hy h (le_of_lt hz), fun h => rpow_lt_rpow hx h hz‚ü©

theorem rpow_le_rpow_iff (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z ‚â§ y ^ z ‚Üî x ‚â§ y :=
  le_iff_le_iff_lt_iff_lt.2 <| rpow_lt_rpow_iff hy hx hz

lemma rpow_lt_rpow_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) : x ^ z < y ^ z ‚Üî y < x :=
  ‚ü®lt_imp_lt_of_le_imp_le fun h ‚Ü¶ rpow_le_rpow_of_nonpos hx h hz.le,
    fun h ‚Ü¶ rpow_lt_rpow_of_neg hy h hz‚ü©

lemma rpow_le_rpow_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) : x ^ z ‚â§ y ^ z ‚Üî y ‚â§ x :=
  le_iff_le_iff_lt_iff_lt.2 <| rpow_lt_rpow_iff_of_neg hy hx hz

lemma le_rpow_inv_iff_of_pos (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ‚â§ y ^ z‚Åª¬π ‚Üî x ^ z ‚â§ y := by
  rw [‚Üê rpow_le_rpow_iff hx _ hz, rpow_inv_rpow] <;> positivity

lemma rpow_inv_le_iff_of_pos (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z‚Åª¬π ‚â§ y ‚Üî x ‚â§ y ^ z := by
  rw [‚Üê rpow_le_rpow_iff _ hy hz, rpow_inv_rpow] <;> positivity

lemma lt_rpow_inv_iff_of_pos (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x < y ^ z‚Åª¬π ‚Üî x ^ z < y :=
  lt_iff_lt_of_le_iff_le <| rpow_inv_le_iff_of_pos hy hx hz

lemma rpow_inv_lt_iff_of_pos (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : 0 < z) : x ^ z‚Åª¬π < y ‚Üî x < y ^ z :=
  lt_iff_lt_of_le_iff_le <| le_rpow_inv_iff_of_pos hy hx hz

theorem le_rpow_inv_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) :
    x ‚â§ y ^ z‚Åª¬π ‚Üî y ‚â§ x ^ z := by
  rw [‚Üê rpow_le_rpow_iff_of_neg _ hx hz, rpow_inv_rpow _ hz.ne] <;> positivity

theorem lt_rpow_inv_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) :
    x < y ^ z‚Åª¬π ‚Üî y < x ^ z := by
  rw [‚Üê rpow_lt_rpow_iff_of_neg _ hx hz, rpow_inv_rpow _ hz.ne] <;> positivity

theorem rpow_inv_lt_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) :
    x ^ z‚Åª¬π < y ‚Üî y ^ z < x := by
  rw [‚Üê rpow_lt_rpow_iff_of_neg hy _ hz, rpow_inv_rpow _ hz.ne] <;> positivity

theorem rpow_inv_le_iff_of_neg (hx : 0 < x) (hy : 0 < y) (hz : z < 0) :
    x ^ z‚Åª¬π ‚â§ y ‚Üî y ^ z ‚â§ x := by
  rw [‚Üê rpow_le_rpow_iff_of_neg hy _ hz, rpow_inv_rpow _ hz.ne] <;> positivity

theorem rpow_lt_rpow_of_exponent_lt (hx : 1 < x) (hyz : y < z) : x ^ y < x ^ z := by
  repeat' rw [rpow_def_of_pos (lt_trans zero_lt_one hx)]
  rw [exp_lt_exp]; exact mul_lt_mul_of_pos_left hyz (log_pos hx)

@[gcongr]
theorem rpow_le_rpow_of_exponent_le (hx : 1 ‚â§ x) (hyz : y ‚â§ z) : x ^ y ‚â§ x ^ z := by
  repeat' rw [rpow_def_of_pos (lt_of_lt_of_le zero_lt_one hx)]
  rw [exp_le_exp]; exact mul_le_mul_of_nonneg_left hyz (log_nonneg hx)

theorem rpow_lt_rpow_of_exponent_neg {x y z : ‚Ñù} (hy : 0 < y) (hxy : y < x) (hz : z < 0) :
    x ^ z < y ^ z := by
  have hx : 0 < x := hy.trans hxy
  rw [‚Üê neg_neg z, Real.rpow_neg (le_of_lt hx) (-z), Real.rpow_neg (le_of_lt hy) (-z),
      inv_lt_inv‚ÇÄ (rpow_pos_of_pos hx _) (rpow_pos_of_pos hy _)]
  exact Real.rpow_lt_rpow (by positivity) hxy <| neg_pos_of_neg hz

theorem strictAntiOn_rpow_Ioi_of_exponent_neg {r : ‚Ñù} (hr : r < 0) :
    StrictAntiOn (fun (x : ‚Ñù) => x ^ r) (Set.Ioi 0) :=
  fun _ ha _ _ hab => rpow_lt_rpow_of_exponent_neg ha hab hr

theorem rpow_le_rpow_of_exponent_nonpos {x y : ‚Ñù} (hy : 0 < y) (hxy : y ‚â§ x) (hz : z ‚â§ 0) :
    x ^ z ‚â§ y ^ z := by
  rcases ne_or_eq z 0 with hz_zero | rfl
  case inl =>
    rcases ne_or_eq x y with hxy' | rfl
    case inl =>
      exact le_of_lt <| rpow_lt_rpow_of_exponent_neg hy (Ne.lt_of_le (id (Ne.symm hxy')) hxy)
        (Ne.lt_of_le hz_zero hz)
    case inr => simp
  case inr => simp

theorem antitoneOn_rpow_Ioi_of_exponent_nonpos {r : ‚Ñù} (hr : r ‚â§ 0) :
    AntitoneOn (fun (x : ‚Ñù) => x ^ r) (Set.Ioi 0) :=
  fun _ ha _ _ hab => rpow_le_rpow_of_exponent_nonpos ha hab hr

@[simp]
theorem rpow_le_rpow_left_iff (hx : 1 < x) : x ^ y ‚â§ x ^ z ‚Üî y ‚â§ z := by
  have x_pos : 0 < x := lt_trans zero_lt_one hx
  rw [‚Üê log_le_log_iff (rpow_pos_of_pos x_pos y) (rpow_pos_of_pos x_pos z), log_rpow x_pos,
    log_rpow x_pos, mul_le_mul_right (log_pos hx)]

@[simp]
theorem rpow_lt_rpow_left_iff (hx : 1 < x) : x ^ y < x ^ z ‚Üî y < z := by
  rw [lt_iff_not_le, rpow_le_rpow_left_iff hx, lt_iff_not_le]

theorem rpow_lt_rpow_of_exponent_gt (hx0 : 0 < x) (hx1 : x < 1) (hyz : z < y) : x ^ y < x ^ z := by
  repeat' rw [rpow_def_of_pos hx0]
  rw [exp_lt_exp]; exact mul_lt_mul_of_neg_left hyz (log_neg hx0 hx1)

theorem rpow_le_rpow_of_exponent_ge (hx0 : 0 < x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y) : x ^ y ‚â§ x ^ z := by
  repeat' rw [rpow_def_of_pos hx0]
  rw [exp_le_exp]; exact mul_le_mul_of_nonpos_left hyz (log_nonpos (le_of_lt hx0) hx1)

@[simp]
theorem rpow_le_rpow_left_iff_of_base_lt_one (hx0 : 0 < x) (hx1 : x < 1) :
    x ^ y ‚â§ x ^ z ‚Üî z ‚â§ y := by
  rw [‚Üê log_le_log_iff (rpow_pos_of_pos hx0 y) (rpow_pos_of_pos hx0 z), log_rpow hx0, log_rpow hx0,
    mul_le_mul_right_of_neg (log_neg hx0 hx1)]

@[simp]
theorem rpow_lt_rpow_left_iff_of_base_lt_one (hx0 : 0 < x) (hx1 : x < 1) :
    x ^ y < x ^ z ‚Üî z < y := by
  rw [lt_iff_not_le, rpow_le_rpow_left_iff_of_base_lt_one hx0 hx1, lt_iff_not_le]

theorem rpow_lt_one {x z : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x < 1) (hz : 0 < z) : x ^ z < 1 := by
  rw [‚Üê one_rpow z]
  exact rpow_lt_rpow hx1 hx2 hz

theorem rpow_le_one {x z : ‚Ñù} (hx1 : 0 ‚â§ x) (hx2 : x ‚â§ 1) (hz : 0 ‚â§ z) : x ^ z ‚â§ 1 := by
  rw [‚Üê one_rpow z]
  exact rpow_le_rpow hx1 hx2 hz

theorem rpow_lt_one_of_one_lt_of_neg {x z : ‚Ñù} (hx : 1 < x) (hz : z < 0) : x ^ z < 1 := by
  convert rpow_lt_rpow_of_exponent_lt hx hz
  exact (rpow_zero x).symm

theorem rpow_le_one_of_one_le_of_nonpos {x z : ‚Ñù} (hx : 1 ‚â§ x) (hz : z ‚â§ 0) : x ^ z ‚â§ 1 := by
  convert rpow_le_rpow_of_exponent_le hx hz
  exact (rpow_zero x).symm

theorem one_lt_rpow {x z : ‚Ñù} (hx : 1 < x) (hz : 0 < z) : 1 < x ^ z := by
  rw [‚Üê one_rpow z]
  exact rpow_lt_rpow zero_le_one hx hz

theorem one_le_rpow {x z : ‚Ñù} (hx : 1 ‚â§ x) (hz : 0 ‚â§ z) : 1 ‚â§ x ^ z := by
  rw [‚Üê one_rpow z]
  exact rpow_le_rpow zero_le_one hx hz

theorem one_lt_rpow_of_pos_of_lt_one_of_neg (hx1 : 0 < x) (hx2 : x < 1) (hz : z < 0) :
    1 < x ^ z := by
  convert rpow_lt_rpow_of_exponent_gt hx1 hx2 hz
  exact (rpow_zero x).symm

theorem one_le_rpow_of_pos_of_le_one_of_nonpos (hx1 : 0 < x) (hx2 : x ‚â§ 1) (hz : z ‚â§ 0) :
    1 ‚â§ x ^ z := by
  convert rpow_le_rpow_of_exponent_ge hx1 hx2 hz
  exact (rpow_zero x).symm

theorem rpow_lt_one_iff_of_pos (hx : 0 < x) : x ^ y < 1 ‚Üî 1 < x ‚àß y < 0 ‚à® x < 1 ‚àß 0 < y := by
  rw [rpow_def_of_pos hx, exp_lt_one_iff, mul_neg_iff, log_pos_iff hx.le, log_neg_iff hx]

theorem rpow_lt_one_iff (hx : 0 ‚â§ x) :
    x ^ y < 1 ‚Üî x = 0 ‚àß y ‚â† 0 ‚à® 1 < x ‚àß y < 0 ‚à® x < 1 ‚àß 0 < y := by
  rcases hx.eq_or_lt with (rfl | hx)
  ¬∑ rcases _root_.em (y = 0) with (rfl | hy) <;> simp [*, lt_irrefl, zero_lt_one]
  ¬∑ simp [rpow_lt_one_iff_of_pos hx, hx.ne.symm]

theorem rpow_lt_one_iff' {x y : ‚Ñù} (hx : 0 ‚â§ x) (hy : 0 < y) :
    x ^ y < 1 ‚Üî x < 1 := by
  rw [‚Üê Real.rpow_lt_rpow_iff hx zero_le_one hy, Real.one_rpow]

theorem one_lt_rpow_iff_of_pos (hx : 0 < x) : 1 < x ^ y ‚Üî 1 < x ‚àß 0 < y ‚à® x < 1 ‚àß y < 0 := by
  rw [rpow_def_of_pos hx, one_lt_exp_iff, mul_pos_iff, log_pos_iff hx.le, log_neg_iff hx]

theorem one_lt_rpow_iff (hx : 0 ‚â§ x) : 1 < x ^ y ‚Üî 1 < x ‚àß 0 < y ‚à® 0 < x ‚àß x < 1 ‚àß y < 0 := by
  rcases hx.eq_or_lt with (rfl | hx)
  ¬∑ rcases _root_.em (y = 0) with (rfl | hy) <;> simp [*, lt_irrefl, (zero_lt_one' ‚Ñù).not_lt]
  ¬∑ simp [one_lt_rpow_iff_of_pos hx, hx]

/-- This is a more general but less convenient version of `rpow_le_rpow_of_exponent_ge`.
This version allows `x = 0`, so it explicitly forbids `x = y = 0`, `z ‚â† 0`. -/
theorem rpow_le_rpow_of_exponent_ge_of_imp (hx0 : 0 ‚â§ x) (hx1 : x ‚â§ 1) (hyz : z ‚â§ y)
    (h : x = 0 ‚Üí y = 0 ‚Üí z = 0) :
    x ^ y ‚â§ x ^ z := by
  rcases eq_or_lt_of_le hx0 with (rfl | hx0')
  ¬∑ rcases eq_or_ne y 0 with rfl | hy0
    ¬∑ rw [h rfl rfl]
    ¬∑ rw [zero_rpow hy0]
      apply zero_rpow_nonneg
  ¬∑ exact rpow_le_rpow_of_exponent_ge hx0' hx1 hyz

/-- This version of `rpow_le_rpow_of_exponent_ge` allows `x = 0` but requires `0 ‚â§ z`.
See also `rpow_le_rpow_of_exponent_ge_of_imp` for the most general version. -/
theorem rpow_le_rpow_of_exponent_ge' (hx0 : 0 ‚â§ x) (hx1 : x ‚â§ 1) (hz : 0 ‚â§ z) (hyz : z ‚â§ y) :
    x ^ y ‚â§ x ^ z :=
  rpow_le_rpow_of_exponent_ge_of_imp hx0 hx1 hyz fun _ hy ‚Ü¶ le_antisymm (hyz.trans_eq hy) hz

lemma rpow_max {x y p : ‚Ñù} (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hp : 0 ‚â§ p) :
    (max x y) ^ p = max (x ^ p) (y ^ p) := by
  rcases le_total x y with hxy | hxy
  ¬∑ rw [max_eq_right hxy, max_eq_right (rpow_le_rpow hx hxy hp)]
  ¬∑ rw [max_eq_left hxy, max_eq_left (rpow_le_rpow hy hxy hp)]

theorem self_le_rpow_of_le_one (h‚ÇÅ : 0 ‚â§ x) (h‚ÇÇ : x ‚â§ 1) (h‚ÇÉ : y ‚â§ 1) : x ‚â§ x ^ y := by
  simpa only [rpow_one]
    using rpow_le_rpow_of_exponent_ge_of_imp h‚ÇÅ h‚ÇÇ h‚ÇÉ fun _ ‚Ü¶ (absurd ¬∑ one_ne_zero)

theorem self_le_rpow_of_one_le (h‚ÇÅ : 1 ‚â§ x) (h‚ÇÇ : 1 ‚â§ y) : x ‚â§ x ^ y := by
  simpa only [rpow_one] using rpow_le_rpow_of_exponent_le h‚ÇÅ h‚ÇÇ

theorem rpow_le_self_of_le_one (h‚ÇÅ : 0 ‚â§ x) (h‚ÇÇ : x ‚â§ 1) (h‚ÇÉ : 1 ‚â§ y) : x ^ y ‚â§ x := by
  simpa only [rpow_one]
    using rpow_le_rpow_of_exponent_ge_of_imp h‚ÇÅ h‚ÇÇ h‚ÇÉ fun _ ‚Ü¶ (absurd ¬∑ (one_pos.trans_le h‚ÇÉ).ne')

theorem rpow_le_self_of_one_le (h‚ÇÅ : 1 ‚â§ x) (h‚ÇÇ : y ‚â§ 1) : x ^ y ‚â§ x := by
  simpa only [rpow_one] using rpow_le_rpow_of_exponent_le h‚ÇÅ h‚ÇÇ

theorem self_lt_rpow_of_lt_one (h‚ÇÅ : 0 < x) (h‚ÇÇ : x < 1) (h‚ÇÉ : y < 1) : x < x ^ y := by
  simpa only [rpow_one] using rpow_lt_rpow_of_exponent_gt h‚ÇÅ h‚ÇÇ h‚ÇÉ

theorem self_lt_rpow_of_one_lt (h‚ÇÅ : 1 < x) (h‚ÇÇ : 1 < y) : x < x ^ y := by
  simpa only [rpow_one] using rpow_lt_rpow_of_exponent_lt h‚ÇÅ h‚ÇÇ

theorem rpow_lt_self_of_lt_one (h‚ÇÅ : 0 < x) (h‚ÇÇ : x < 1) (h‚ÇÉ : 1 < y) : x ^ y < x := by
  simpa only [rpow_one] using rpow_lt_rpow_of_exponent_gt h‚ÇÅ h‚ÇÇ h‚ÇÉ

theorem rpow_lt_self_of_one_lt (h‚ÇÅ : 1 < x) (h‚ÇÇ : y < 1) : x ^ y < x := by
  simpa only [rpow_one] using rpow_lt_rpow_of_exponent_lt h‚ÇÅ h‚ÇÇ

theorem rpow_left_injOn {x : ‚Ñù} (hx : x ‚â† 0) : InjOn (fun y : ‚Ñù => y ^ x) { y : ‚Ñù | 0 ‚â§ y } := by
  rintro y hy z hz (hyz : y ^ x = z ^ x)
  rw [‚Üê rpow_one y, ‚Üê rpow_one z, ‚Üê mul_inv_cancel‚ÇÄ hx, rpow_mul hy, rpow_mul hz, hyz]

lemma rpow_left_inj (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : z ‚â† 0) : x ^ z = y ^ z ‚Üî x = y :=
  (rpow_left_injOn hz).eq_iff hx hy

lemma rpow_inv_eq (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : z ‚â† 0) : x ^ z‚Åª¬π = y ‚Üî x = y ^ z := by
  rw [‚Üê rpow_left_inj _ hy hz, rpow_inv_rpow hx hz]; positivity

lemma eq_rpow_inv (hx : 0 ‚â§ x) (hy : 0 ‚â§ y) (hz : z ‚â† 0) : x = y ^ z‚Åª¬π ‚Üî x ^ z = y := by
  rw [‚Üê rpow_left_inj hx _ hz, rpow_inv_rpow hy hz]; positivity

theorem le_rpow_iff_log_le (hx : 0 < x) (hy : 0 < y) : x ‚â§ y ^ z ‚Üî log x ‚â§ z * log y := by
  rw [‚Üê log_le_log_iff hx (rpow_pos_of_pos hy z), log_rpow hy]

lemma le_pow_iff_log_le (hx : 0 < x) (hy : 0 < y) : x ‚â§ y ^ n ‚Üî log x ‚â§ n * log y :=
  rpow_natCast _ _ ‚ñ∏ le_rpow_iff_log_le hx hy

lemma le_zpow_iff_log_le {n : ‚Ñ§} (hx : 0 < x) (hy : 0 < y) : x ‚â§ y ^ n ‚Üî log x ‚â§ n * log y :=
  rpow_intCast _ _ ‚ñ∏ le_rpow_iff_log_le hx hy

lemma le_rpow_of_log_le (hy : 0 < y) (h : log x ‚â§ z * log y) : x ‚â§ y ^ z := by
  obtain hx | hx := le_or_lt x 0
  ¬∑ exact hx.trans (rpow_pos_of_pos hy _).le
  ¬∑ exact (le_rpow_iff_log_le hx hy).2 h

lemma le_pow_of_log_le (hy : 0 < y) (h : log x ‚â§ n * log y) : x ‚â§ y ^ n :=
  rpow_natCast _ _ ‚ñ∏ le_rpow_of_log_le hy h

lemma le_zpow_of_log_le {n : ‚Ñ§} (hy : 0 < y) (h : log x ‚â§ n * log y) : x ‚â§ y ^ n :=
  rpow_intCast _ _ ‚ñ∏ le_rpow_of_log_le hy h

theorem lt_rpow_iff_log_lt (hx : 0 < x) (hy : 0 < y) : x < y ^ z ‚Üî log x < z * log y := by
  rw [‚Üê log_lt_log_iff hx (rpow_pos_of_pos hy z), log_rpow hy]

lemma lt_pow_iff_log_lt (hx : 0 < x) (hy : 0 < y) : x < y ^ n ‚Üî log x < n * log y :=
  rpow_natCast _ _ ‚ñ∏ lt_rpow_iff_log_lt hx hy

lemma lt_zpow_iff_log_lt {n : ‚Ñ§} (hx : 0 < x) (hy : 0 < y) : x < y ^ n ‚Üî log x < n * log y :=
  rpow_intCast _ _ ‚ñ∏ lt_rpow_iff_log_lt hx hy

lemma lt_rpow_of_log_lt (hy : 0 < y) (h : log x < z * log y) : x < y ^ z := by
  obtain hx | hx := le_or_lt x 0
  ¬∑ exact hx.trans_lt (rpow_pos_of_pos hy _)
  ¬∑ exact (lt_rpow_iff_log_lt hx hy).2 h

lemma lt_pow_of_log_lt (hy : 0 < y) (h : log x < n * log y) : x < y ^ n :=
  rpow_natCast _ _ ‚ñ∏ lt_rpow_of_log_lt hy h

lemma lt_zpow_of_log_lt {n : ‚Ñ§} (hy : 0 < y) (h : log x < n * log y) : x < y ^ n :=
  rpow_intCast _ _ ‚ñ∏ lt_rpow_of_log_lt hy h

lemma rpow_le_iff_le_log (hx : 0 < x) (hy : 0 < y) : x ^ z ‚â§ y ‚Üî z * log x ‚â§ log y := by
  rw [‚Üê log_le_log_iff (rpow_pos_of_pos hx _) hy, log_rpow hx]

lemma pow_le_iff_le_log (hx : 0 < x) (hy : 0 < y) : x ^ n ‚â§ y ‚Üî n * log x ‚â§ log y := by
  rw [‚Üê rpow_le_iff_le_log hx hy, rpow_natCast]

lemma zpow_le_iff_le_log {n : ‚Ñ§} (hx : 0 < x) (hy : 0 < y) : x ^ n ‚â§ y ‚Üî n * log x ‚â§ log y := by
  rw [‚Üê rpow_le_iff_le_log hx hy, rpow_intCast]

lemma le_log_of_rpow_le (hx : 0 < x) (h : x ^ z ‚â§ y) : z * log x ‚â§ log y :=
  log_rpow hx _ ‚ñ∏ log_le_log (by positivity) h

lemma le_log_of_pow_le (hx : 0 < x) (h : x ^ n ‚â§ y) : n * log x ‚â§ log y :=
  le_log_of_rpow_le hx (rpow_natCast _ _ ‚ñ∏ h)

lemma le_log_of_zpow_le {n : ‚Ñ§} (hx : 0 < x) (h : x ^ n ‚â§ y) : n * log x ‚â§ log y :=
  le_log_of_rpow_le hx (rpow_intCast _ _ ‚ñ∏ h)

lemma rpow_le_of_le_log (hy : 0 < y) (h : log x ‚â§ z * log y) : x ‚â§ y ^ z := by
  obtain hx | hx := le_or_lt x 0
  ¬∑ exact hx.trans (rpow_pos_of_pos hy _).le
  ¬∑ exact (le_rpow_iff_log_le hx hy).2 h

lemma pow_le_of_le_log (hy : 0 < y) (h : log x ‚â§ n * log y) : x ‚â§ y ^ n :=
  rpow_natCast _ _ ‚ñ∏ rpow_le_of_le_log hy h

lemma zpow_le_of_le_log {n : ‚Ñ§} (hy : 0 < y) (h : log x ‚â§ n * log y) : x ‚â§ y ^ n :=
  rpow_intCast _ _ ‚ñ∏ rpow_le_of_le_log hy h

lemma rpow_lt_iff_lt_log (hx : 0 < x) (hy : 0 < y) : x ^ z < y ‚Üî z * log x < log y := by
  rw [‚Üê log_lt_log_iff (rpow_pos_of_pos hx _) hy, log_rpow hx]

lemma pow_lt_iff_lt_log (hx : 0 < x) (hy : 0 < y) : x ^ n < y ‚Üî n * log x < log y := by
  rw [‚Üê rpow_lt_iff_lt_log hx hy, rpow_natCast]

lemma zpow_lt_iff_lt_log {n : ‚Ñ§} (hx : 0 < x) (hy : 0 < y) : x ^ n < y ‚Üî n * log x < log y := by
  rw [‚Üê rpow_lt_iff_lt_log hx hy, rpow_intCast]

lemma lt_log_of_rpow_lt (hx : 0 < x) (h : x ^ z < y) : z * log x < log y :=
  log_rpow hx _ ‚ñ∏ log_lt_log (by positivity) h

lemma lt_log_of_pow_lt (hx : 0 < x) (h : x ^ n < y) : n * log x < log y :=
  lt_log_of_rpow_lt hx (rpow_natCast _ _ ‚ñ∏ h)

lemma lt_log_of_zpow_lt {n : ‚Ñ§} (hx : 0 < x) (h : x ^ n < y) : n * log x < log y :=
  lt_log_of_rpow_lt hx (rpow_intCast _ _ ‚ñ∏ h)

lemma rpow_lt_of_lt_log (hy : 0 < y) (h : log x < z * log y) : x < y ^ z := by
  obtain hx | hx := le_or_lt x 0
  ¬∑ exact hx.trans_lt (rpow_pos_of_pos hy _)
  ¬∑ exact (lt_rpow_iff_log_lt hx hy).2 h

lemma pow_lt_of_lt_log (hy : 0 < y) (h : log x < n * log y) : x < y ^ n :=
  rpow_natCast _ _ ‚ñ∏ rpow_lt_of_lt_log hy h

lemma zpow_lt_of_lt_log {n : ‚Ñ§} (hy : 0 < y) (h : log x < n * log y) : x < y ^ n :=
  rpow_intCast _ _ ‚ñ∏ rpow_lt_of_lt_log hy h

theorem rpow_le_one_iff_of_pos (hx : 0 < x) : x ^ y ‚â§ 1 ‚Üî 1 ‚â§ x ‚àß y ‚â§ 0 ‚à® x ‚â§ 1 ‚àß 0 ‚â§ y := by
  rw [rpow_def_of_pos hx, exp_le_one_iff, mul_nonpos_iff, log_nonneg_iff hx, log_nonpos_iff hx.le]

/-- Bound for `|log x * x ^ t|` in the interval `(0, 1]`, for positive real `t`. -/
theorem abs_log_mul_self_rpow_lt (x t : ‚Ñù) (h1 : 0 < x) (h2 : x ‚â§ 1) (ht : 0 < t) :
    |log x * x ^ t| < 1 / t := by
  rw [lt_div_iff‚ÇÄ ht]
  have := abs_log_mul_self_lt (x ^ t) (rpow_pos_of_pos h1 t) (rpow_le_one h1.le h2 ht.le)
  rwa [log_rpow h1, mul_assoc, abs_mul, abs_of_pos ht, mul_comm] at this

/-- `log x` is bounded above by a multiple of every power of `x` with positive exponent. -/
lemma log_le_rpow_div {x Œµ : ‚Ñù} (hx : 0 ‚â§ x) (hŒµ : 0 < Œµ) : log x ‚â§ x ^ Œµ / Œµ := by
  rcases hx.eq_or_lt with rfl | h
  ¬∑ rw [log_zero, zero_rpow hŒµ.ne', zero_div]
  rw [le_div_iff‚ÇÄ' hŒµ]
  exact (log_rpow h Œµ).symm.trans_le <| (log_le_sub_one_of_pos <| rpow_pos_of_pos h Œµ).trans
    (sub_one_lt _).le

/-- The (real) logarithm of a natural number `n` is bounded by a multiple of every power of `n`
with positive exponent. -/
lemma log_natCast_le_rpow_div (n : ‚Ñï) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : log n ‚â§ n ^ Œµ / Œµ :=
  log_le_rpow_div n.cast_nonneg hŒµ

lemma strictMono_rpow_of_base_gt_one {b : ‚Ñù} (hb : 1 < b) :
    StrictMono (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) := by
  simp_rw [Real.rpow_def_of_pos (zero_lt_one.trans hb)]
  exact exp_strictMono.comp <| StrictMono.const_mul strictMono_id <| Real.log_pos hb

lemma monotone_rpow_of_base_ge_one {b : ‚Ñù} (hb : 1 ‚â§ b) :
    Monotone (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) := by
  rcases lt_or_eq_of_le hb with hb | rfl
  case inl => exact (strictMono_rpow_of_base_gt_one hb).monotone
  case inr => intro _ _ _; simp

lemma strictAnti_rpow_of_base_lt_one {b : ‚Ñù} (hb‚ÇÄ : 0 < b) (hb‚ÇÅ : b < 1) :
    StrictAnti (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) := by
  simp_rw [Real.rpow_def_of_pos hb‚ÇÄ]
  exact exp_strictMono.comp_strictAnti <| StrictMono.const_mul_of_neg strictMono_id
      <| Real.log_neg hb‚ÇÄ hb‚ÇÅ

lemma antitone_rpow_of_base_le_one {b : ‚Ñù} (hb‚ÇÄ : 0 < b) (hb‚ÇÅ : b ‚â§ 1) :
    Antitone (b ^ ¬∑ : ‚Ñù ‚Üí ‚Ñù) := by
  rcases lt_or_eq_of_le hb‚ÇÅ with hb‚ÇÅ | rfl
  case inl => exact (strictAnti_rpow_of_base_lt_one hb‚ÇÄ hb‚ÇÅ).antitone
  case inr => intro _ _ _; simp

lemma rpow_right_inj (hx‚ÇÄ : 0 < x) (hx‚ÇÅ : x ‚â† 1) : x ^ y = x ^ z ‚Üî y = z := by
  refine ‚ü®fun H ‚Ü¶ ?_, fun H ‚Ü¶ by rw [H]‚ü©
  rcases hx‚ÇÅ.lt_or_lt with h | h
  ¬∑ exact (strictAnti_rpow_of_base_lt_one hx‚ÇÄ h).injective H
  ¬∑ exact (strictMono_rpow_of_base_gt_one h).injective H

/-- Guessing rule for the `bound` tactic: when trying to prove `x ^ y ‚â§ x ^ z`, we can either assume
`1 ‚â§ x` or `0 < x ‚â§ 1`. -/
@[bound] lemma rpow_le_rpow_of_exponent_le_or_ge {x y z : ‚Ñù}
    (h : 1 ‚â§ x ‚àß y ‚â§ z ‚à® 0 < x ‚àß x ‚â§ 1 ‚àß z ‚â§ y) : x ^ y ‚â§ x ^ z := by
  rcases h with ‚ü®x1, yz‚ü© | ‚ü®x0, x1, zy‚ü©
  ¬∑ exact Real.rpow_le_rpow_of_exponent_le x1 yz
  ¬∑ exact Real.rpow_le_rpow_of_exponent_ge x0 x1 zy

end Real

namespace Complex

lemma norm_prime_cpow_le_one_half (p : Nat.Primes) {s : ‚ÑÇ} (hs : 1 < s.re) :
    ‚Äñ(p : ‚ÑÇ) ^ (-s)‚Äñ ‚â§ 1 / 2 := by
  rw [norm_natCast_cpow_of_re_ne_zero p <| by rw [neg_re]; linarith only [hs]]
  refine (Real.rpow_le_rpow_of_nonpos zero_lt_two (Nat.cast_le.mpr p.prop.two_le) <|
    by rw [neg_re]; linarith only [hs]).trans ?_
  rw [one_div, ‚Üê Real.rpow_neg_one]
  exact Real.rpow_le_rpow_of_exponent_le one_le_two <| (neg_lt_neg hs).le

lemma one_sub_prime_cpow_ne_zero {p : ‚Ñï} (hp : p.Prime) {s : ‚ÑÇ} (hs : 1 < s.re) :
    1 - (p : ‚ÑÇ) ^ (-s) ‚â† 0 := by
  refine sub_ne_zero_of_ne fun H ‚Ü¶ ?_
  have := norm_prime_cpow_le_one_half ‚ü®p, hp‚ü© hs
  simp only at this
  rw [‚Üê H, norm_one] at this
  norm_num at this

lemma norm_natCast_cpow_le_norm_natCast_cpow_of_pos {n : ‚Ñï} (hn : 0 < n) {w z : ‚ÑÇ}
    (h : w.re ‚â§ z.re) :
    ‚Äñ(n : ‚ÑÇ) ^ w‚Äñ ‚â§ ‚Äñ(n : ‚ÑÇ) ^ z‚Äñ := by
  simp_rw [norm_natCast_cpow_of_pos hn]
  exact Real.rpow_le_rpow_of_exponent_le (by exact_mod_cast hn) h

lemma norm_natCast_cpow_le_norm_natCast_cpow_iff {n : ‚Ñï} (hn : 1 < n) {w z : ‚ÑÇ} :
    ‚Äñ(n : ‚ÑÇ) ^ w‚Äñ ‚â§ ‚Äñ(n : ‚ÑÇ) ^ z‚Äñ ‚Üî w.re ‚â§ z.re := by
  simp_rw [norm_natCast_cpow_of_pos (Nat.zero_lt_of_lt hn),
    Real.rpow_le_rpow_left_iff (Nat.one_lt_cast.mpr hn)]

lemma norm_log_natCast_le_rpow_div (n : ‚Ñï) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) : ‚Äñlog n‚Äñ ‚â§ n ^ Œµ / Œµ := by
  rcases n.eq_zero_or_pos with rfl | h
  ¬∑ rw [Nat.cast_zero, Nat.cast_zero, log_zero, norm_zero, Real.zero_rpow hŒµ.ne', zero_div]
  rw [norm_eq_abs, ‚Üê natCast_log, abs_ofReal,
    _root_.abs_of_nonneg <| Real.log_nonneg <| by exact_mod_cast Nat.one_le_of_lt h.lt]
  exact Real.log_natCast_le_rpow_div n hŒµ

end Complex

namespace  Asymptotics

open Filter

open Topology in
theorem isBigO_atTop_natCast_rpow_of_tendsto_div_rpow {ùïú : Type*} [RCLike ùïú] {g : ‚Ñï ‚Üí ùïú}
    {a : ùïú} {r : ‚Ñù} (hlim : Tendsto (fun n ‚Ü¶ g n / (n ^ r : ‚Ñù)) atTop (ùìù a)) :
    g =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ r := by
  refine isBigO_norm_left.mp <| isBigO_of_div_tendsto_nhds ?_ ‚Äña‚Äñ ?_
  ¬∑ filter_upwards [eventually_ne_atTop 0] with _ h
    simp [Real.rpow_eq_zero_iff_of_nonneg, h]
  ¬∑ have := Function.comp_def _ _ ‚ñ∏ tendsto_norm.comp hlim
    simpa [norm_div, _root_.abs_of_nonneg (Real.rpow_nonneg (Nat.cast_nonneg _) _)] using this

variable {E : Type*} [SeminormedRing E] (a b c : ‚Ñù)

theorem IsBigO.mul_atTop_rpow_of_isBigO_rpow {f g : ‚Ñù ‚Üí E}
    (hf : f =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ a) (hg : g =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ b)
    (h : a + b ‚â§ c) :
    (f * g) =O[atTop] fun t ‚Ü¶ (t : ‚Ñù) ^ c := by
  refine (hf.mul hg).trans (Eventually.isBigO ?_)
  filter_upwards [eventually_ge_atTop 1] with t ht
  rw [‚Üê Real.rpow_add (zero_lt_one.trans_le ht), Real.norm_of_nonneg (Real.rpow_nonneg
    (zero_le_one.trans ht) (a + b))]
  exact Real.rpow_le_rpow_of_exponent_le ht h

theorem IsBigO.mul_atTop_rpow_natCast_of_isBigO_rpow {f g : ‚Ñï ‚Üí E}
    (hf : f =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ a) (hg : g =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ b)
    (h : a + b ‚â§ c) :
    (f * g) =O[atTop] fun n ‚Ü¶ (n : ‚Ñù) ^ c := by
  refine (hf.mul hg).trans (Eventually.isBigO ?_)
  filter_upwards [eventually_ge_atTop 1] with t ht
  replace ht : 1 ‚â§ (t : ‚Ñù) := Nat.one_le_cast.mpr ht
  rw [‚Üê Real.rpow_add (zero_lt_one.trans_le ht), Real.norm_of_nonneg (Real.rpow_nonneg
    (zero_le_one.trans ht) (a + b))]
  exact Real.rpow_le_rpow_of_exponent_le ht h

end Asymptotics

/-!
## Square roots of reals
-/


namespace Real

variable {z x y : ‚Ñù}

section Sqrt

theorem sqrt_eq_rpow (x : ‚Ñù) : ‚àöx = x ^ (1 / (2 : ‚Ñù)) := by
  obtain h | h := le_or_lt 0 x
  ¬∑ rw [‚Üê mul_self_inj_of_nonneg (sqrt_nonneg _) (rpow_nonneg h _), mul_self_sqrt h, ‚Üê sq,
      ‚Üê rpow_natCast, ‚Üê rpow_mul h]
    norm_num
  ¬∑ have : 1 / (2 : ‚Ñù) * œÄ = œÄ / (2 : ‚Ñù) := by ring
    rw [sqrt_eq_zero_of_nonpos h.le, rpow_def_of_neg h, this, cos_pi_div_two, mul_zero]

theorem rpow_div_two_eq_sqrt {x : ‚Ñù} (r : ‚Ñù) (hx : 0 ‚â§ x) : x ^ (r / 2) = ‚àöx ^ r := by
  rw [sqrt_eq_rpow, ‚Üê rpow_mul hx]
  congr
  ring

end Sqrt

end Real

namespace Complex

lemma cpow_inv_two_re (x : ‚ÑÇ) : (x ^ (2‚Åª¬π : ‚ÑÇ)).re = sqrt ((abs x + x.re) / 2) := by
  rw [‚Üê ofReal_ofNat, ‚Üê ofReal_inv, cpow_ofReal_re, ‚Üê div_eq_mul_inv, ‚Üê one_div,
    ‚Üê Real.sqrt_eq_rpow, cos_half, ‚Üê sqrt_mul, ‚Üê mul_div_assoc, mul_add, mul_one, abs_mul_cos_arg]
  exacts [abs.nonneg _, (neg_pi_lt_arg _).le, arg_le_pi _]

lemma cpow_inv_two_im_eq_sqrt {x : ‚ÑÇ} (hx : 0 ‚â§ x.im) :
    (x ^ (2‚Åª¬π : ‚ÑÇ)).im = sqrt ((abs x - x.re) / 2) := by
  rw [‚Üê ofReal_ofNat, ‚Üê ofReal_inv, cpow_ofReal_im, ‚Üê div_eq_mul_inv, ‚Üê one_div,
    ‚Üê Real.sqrt_eq_rpow, sin_half_eq_sqrt, ‚Üê sqrt_mul (abs.nonneg _), ‚Üê mul_div_assoc, mul_sub,
    mul_one, abs_mul_cos_arg]
  ¬∑ rwa [arg_nonneg_iff]
  ¬∑ linarith [pi_pos, arg_le_pi x]

lemma cpow_inv_two_im_eq_neg_sqrt {x : ‚ÑÇ} (hx : x.im < 0) :
    (x ^ (2‚Åª¬π : ‚ÑÇ)).im = -sqrt ((abs x - x.re) / 2) := by
  rw [‚Üê ofReal_ofNat, ‚Üê ofReal_inv, cpow_ofReal_im, ‚Üê div_eq_mul_inv, ‚Üê one_div,
    ‚Üê Real.sqrt_eq_rpow, sin_half_eq_neg_sqrt, mul_neg, ‚Üê sqrt_mul (abs.nonneg _),
    ‚Üê mul_div_assoc, mul_sub, mul_one, abs_mul_cos_arg]
  ¬∑ linarith [pi_pos, neg_pi_lt_arg x]
  ¬∑ exact (arg_neg_iff.2 hx).le

lemma abs_cpow_inv_two_im (x : ‚ÑÇ) : |(x ^ (2‚Åª¬π : ‚ÑÇ)).im| = sqrt ((abs x - x.re) / 2) := by
  rw [‚Üê ofReal_ofNat, ‚Üê ofReal_inv, cpow_ofReal_im, ‚Üê div_eq_mul_inv, ‚Üê one_div,
    ‚Üê Real.sqrt_eq_rpow, _root_.abs_mul, _root_.abs_of_nonneg (sqrt_nonneg _), abs_sin_half,
    ‚Üê sqrt_mul (abs.nonneg _), ‚Üê mul_div_assoc, mul_sub, mul_one, abs_mul_cos_arg]

open scoped ComplexOrder in
lemma inv_natCast_cpow_ofReal_pos {n : ‚Ñï} (hn : n ‚â† 0) (x : ‚Ñù) :
    0 < ((n : ‚ÑÇ) ^ (x : ‚ÑÇ))‚Åª¬π := by
  refine RCLike.inv_pos_of_pos ?_
  rw [show (n : ‚ÑÇ) ^ (x : ‚ÑÇ) = (n : ‚Ñù) ^ (x : ‚ÑÇ) from rfl, ‚Üê ofReal_cpow n.cast_nonneg']
  positivity

end Complex

section Tactics

/-!
## Tactic extensions for real powers
-/
namespace Mathlib.Meta.NormNum

open Lean.Meta Qq

theorem isNat_rpow_pos {a b : ‚Ñù} {nb ne : ‚Ñï}
    (pb : IsNat b nb) (pe' : IsNat (a ^ nb) ne) :
    IsNat (a ^ b) ne := by
  rwa [pb.out, rpow_natCast]

theorem isNat_rpow_neg {a b : ‚Ñù} {nb ne : ‚Ñï}
    (pb : IsInt b (Int.negOfNat nb)) (pe' : IsNat (a ^ (Int.negOfNat nb)) ne) :
    IsNat (a ^ b) ne := by
  rwa [pb.out, Real.rpow_intCast]

theorem isInt_rpow_pos {a b : ‚Ñù} {nb ne : ‚Ñï}
    (pb : IsNat b nb) (pe' : IsInt (a ^ nb) (Int.negOfNat ne)) :
    IsInt (a ^ b) (Int.negOfNat ne) := by
  rwa [pb.out, rpow_natCast]

theorem isInt_rpow_neg {a b : ‚Ñù} {nb ne : ‚Ñï}
    (pb : IsInt b (Int.negOfNat nb)) (pe' : IsInt (a ^ (Int.negOfNat nb)) (Int.negOfNat ne)) :
    IsInt (a ^ b) (Int.negOfNat ne) := by
  rwa [pb.out, Real.rpow_intCast]

theorem isRat_rpow_pos {a b : ‚Ñù} {nb : ‚Ñï}
    {num : ‚Ñ§} {den : ‚Ñï}
    (pb : IsNat b nb) (pe' : IsRat (a ^ nb) num den) :
    IsRat (a ^ b) num den := by
  rwa [pb.out, rpow_natCast]

theorem isRat_rpow_neg {a b : ‚Ñù} {nb : ‚Ñï}
    {num : ‚Ñ§} {den : ‚Ñï}
    (pb : IsInt b (Int.negOfNat nb)) (pe' : IsRat (a ^ (Int.negOfNat nb)) num den) :
    IsRat (a ^ b) num den := by
  rwa [pb.out, Real.rpow_intCast]

#adaptation_note
/--
Since https://github.com/leanprover/lean4/pull/5338,
the unused variable linter can not see usages of variables in
`haveI' : ‚ãØ =Q ‚ãØ := ‚ü®‚ü©` clauses, so generates many false positives.
-/
set_option linter.unusedVariables false

/-- Evaluates expressions of the form `a ^ b` when `a` and `b` are both reals. -/
@[norm_num (_ : ‚Ñù) ^ (_ : ‚Ñù)]
def evalRPow : NormNumExt where eval {u Œ±} e := do
  let .app (.app f (a : Q(‚Ñù))) (b : Q(‚Ñù)) ‚Üê Lean.Meta.whnfR e | failure
  guard <|‚Üê withNewMCtxDepth <| isDefEq f q(HPow.hPow (Œ± := ‚Ñù) (Œ≤ := ‚Ñù))
  haveI' : u =QL 0 := ‚ü®‚ü©
  haveI' : $Œ± =Q ‚Ñù := ‚ü®‚ü©
  haveI' h : $e =Q $a ^ $b := ‚ü®‚ü©
  h.check
  let (rb : Result b) ‚Üê derive (Œ± := q(‚Ñù)) b
  match rb with
  | .isBool .. | .isRat _ .. => failure
  | .isNat sŒ≤ nb pb =>
    match ‚Üê derive q($a ^ $nb) with
    | .isBool .. => failure
    | .isNat sŒ±' ne' pe' =>
      assumeInstancesCommute
      haveI' : $sŒ±' =Q AddGroupWithOne.toAddMonoidWithOne := ‚ü®‚ü©
      return .isNat sŒ±' ne' q(isNat_rpow_pos $pb $pe')
    | .isNegNat sŒ±' ne' pe' =>
      assumeInstancesCommute
      return .isNegNat sŒ±' ne' q(isInt_rpow_pos $pb $pe')
    | .isRat sŒ±' qe' nume' dene' pe' =>
      assumeInstancesCommute
      return .isRat sŒ±' qe' nume' dene' q(isRat_rpow_pos $pb $pe')
  | .isNegNat sŒ≤ nb pb =>
    match ‚Üê derive q($a ^ (-($nb : ‚Ñ§))) with
    | .isBool .. => failure
    | .isNat sŒ±' ne' pe' =>
      assumeInstancesCommute
      return .isNat sŒ±' ne' q(isNat_rpow_neg $pb $pe')
    | .isNegNat sŒ±' ne' pe' =>
      let _ := q(Real.instRing)
      assumeInstancesCommute
      return .isNegNat sŒ±' ne' q(isInt_rpow_neg $pb $pe')
    | .isRat sŒ±' qe' nume' dene' pe' =>
      assumeInstancesCommute
      return .isRat sŒ±' qe' nume' dene' q(isRat_rpow_neg $pb $pe')

end Mathlib.Meta.NormNum

end Tactics
