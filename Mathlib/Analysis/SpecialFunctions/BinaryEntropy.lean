/-
Copyright (c) 2023 Adomas Baliuka. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Adomas Baliuka
-/
import Mathlib.Analysis.SpecialFunctions.Log.NegMulLog
import Mathlib.Analysis.Convex.SpecificFunctions.Basic

/-!
# Properties of Shannon q-ary entropy and binary entropy functions

The [binary entropy function](https://en.wikipedia.org/wiki/Binary_entropy_function)
`binEntropy p := - p * log p - (1 - p) * log (1 - p)`
is the Shannon entropy of a Bernoulli random variable with success probability `p`.

More generally, the q-ary entropy function is the Shannon entropy of the random variable
with possible outcomes `{1, ..., q}`, where outcome `1` has probability `1 - p`
and all other outcomes are equally likely.

`qaryEntropy (q : ‚Ñï) (p : ‚Ñù) := p * log (q - 1) - p * log p - (1 - p) * log (1 - p)`

This file assumes that entropy is measured in Nats, hence the use of natural logarithms.
Most lemmas are also valid using a logarithm in a different base.

## Main declarations

* `Real.binEntropy`: the binary entropy function
* `Real.qaryEntropy`: the `q`-ary entropy function

## Main results

The functions are also defined outside the interval `Icc 0 1` due to `log x = log |x|`.

* They are continuous everywhere (`binEntropy_continuous` and `qaryEntropy_continuous`).
* They are differentiable everywhere except at points `0` or `1`
  (`hasDerivAt_binEntropy` and `hasDerivAt_qaryEntropy`).
  In addition, due to junk values, `deriv binEntropy p = log (1 - p) - log p`
  holds everywhere (`deriv_binEntropy`).
* they are strictly increasing on `Icc 0 (1 - 1/q))`
  (`qaryEntropy_strictMonoOn`, `binEntropy_strictMonoOn`)
  and strictly decreasing on `Icc (1 - 1/q) 1`
  (`binEntropy_strictAntiOn` and `qaryEntropy_strictAntiOn`).
* they are strictly concave on `Icc 0 1`
  (`strictConcaveOn_qaryEntropy` and `strictConcave_binEntropy`).

## Tags

entropy, Shannon, binary, nit, nepit
-/

namespace Real
variable {q : ‚Ñï} {p : ‚Ñù}

/-! ### Binary entropy -/

/-- The [binary entropy function](https://en.wikipedia.org/wiki/Binary_entropy_function)
`binEntropy p := - p * log p - (1-p) * log (1 - p)`
is the Shannon entropy of a Bernoulli random variable with success probability `p`. -/
@[pp_nodot] noncomputable def binEntropy (p : ‚Ñù) : ‚Ñù := p * log p‚Åª¬π + (1 - p) * log (1 - p)‚Åª¬π

@[simp] lemma binEntropy_zero : binEntropy 0 = 0 := by simp [binEntropy]
@[simp] lemma binEntropy_one : binEntropy 1 = 0 := by simp [binEntropy]
@[simp] lemma binEntropy_two_inv : binEntropy 2‚Åª¬π = log 2 := by norm_num [binEntropy]; simp; ring

lemma binEntropy_eq_negMulLog_add_negMulLog_one_sub (p : ‚Ñù) :
    binEntropy p = negMulLog p + negMulLog (1 - p) := by simp [binEntropy, negMulLog, ‚Üê neg_mul]

lemma binEntropy_eq_negMulLog_add_negMulLog_one_sub' :
    binEntropy = fun p ‚Ü¶ negMulLog p + negMulLog (1 - p) :=
  funext binEntropy_eq_negMulLog_add_negMulLog_one_sub

/-- `binEntropy` is symmetric about 1/2. -/
@[simp] lemma binEntropy_one_sub (p : ‚Ñù) : binEntropy (1 - p) = binEntropy p := by
  simp [binEntropy, add_comm]

/-- `binEntropy` is symmetric about 1/2. -/
lemma binEntropy_two_inv_add (p : ‚Ñù) : binEntropy (2‚Åª¬π + p) = binEntropy (2‚Åª¬π - p) := by
  rw [‚Üê binEntropy_one_sub]; ring_nf

lemma binEntropy_pos (hp‚ÇÄ : 0 < p) (hp‚ÇÅ : p < 1) : 0 < binEntropy p := by
  unfold binEntropy
  have : 0 < 1 - p := sub_pos.2 hp‚ÇÅ
  have : 0 < log p‚Åª¬π := log_pos <| (one_lt_inv‚ÇÄ hp‚ÇÄ).2 hp‚ÇÅ
  have : 0 < log (1 - p)‚Åª¬π := log_pos <| (one_lt_inv‚ÇÄ ‚Äπ_‚Ä∫).2 (sub_lt_self _ hp‚ÇÄ)
  positivity

lemma binEntropy_nonneg (hp‚ÇÄ : 0 ‚â§ p) (hp‚ÇÅ : p ‚â§ 1) : 0 ‚â§ binEntropy p := by
  obtain rfl | hp‚ÇÄ := hp‚ÇÄ.eq_or_lt
  ¬∑ simp
  obtain rfl | hp‚ÇÅ := hp‚ÇÅ.eq_or_lt
  ¬∑ simp
  exact (binEntropy_pos hp‚ÇÄ hp‚ÇÅ).le

/-- Outside the usual range of `binEntropy`, it is negative. This is due to `log p = log |p|`. -/
lemma binEntropy_neg_of_neg (hp : p < 0) : binEntropy p < 0 := by
  rw [binEntropy, log_inv, log_inv]
  suffices -p * log p < (1 - p) * log (1 - p) by linarith
  by_cases hp' : p < -1
  ¬∑ have : log p < log (1 - p) := by
      rw [‚Üê log_neg_eq_log]
      exact log_lt_log (Left.neg_pos_iff.mpr hp) (by linarith)
    nlinarith [log_pos_of_lt_neg_one hp']
  ¬∑ have : -p * log p ‚â§ 0 := by
      wlog h : -1 < p
      ¬∑ simp only [show p = -1 by linarith, log_neg_eq_log, log_one, le_refl, mul_zero]
      ¬∑ nlinarith [log_neg_of_lt_zero hp h]
    nlinarith [(log_pos (by linarith) : 0 < log (1 - p))]

/-- Outside the usual range of `binEntropy`, it is negative. This is due to `log p = log |p|`. -/
lemma binEntropy_nonpos_of_nonpos (hp : p ‚â§ 0) : binEntropy p ‚â§ 0 := by
  obtain rfl | hp := hp.eq_or_lt
  ¬∑ simp
  ¬∑ exact (binEntropy_neg_of_neg hp).le

/-- Outside the usual range of `binEntropy`, it is negative. This is due to `log p = log |p|` -/
lemma binEntropy_neg_of_one_lt (hp : 1 < p) : binEntropy p < 0 := by
  rw [‚Üê binEntropy_one_sub]; exact binEntropy_neg_of_neg (sub_neg.2 hp)

/-- Outside the usual range of `binEntropy`, it is negative. This is due to `log p = log |p|` -/
lemma binEntropy_nonpos_of_one_le (hp : 1 ‚â§ p) : binEntropy p ‚â§ 0 := by
  rw [‚Üê binEntropy_one_sub]; exact binEntropy_nonpos_of_nonpos (sub_nonpos.2 hp)

lemma binEntropy_eq_zero : binEntropy p = 0 ‚Üî p = 0 ‚à® p = 1 := by
  refine ‚ü®fun h ‚Ü¶ ?_, by rintro (rfl | rfl) <;> simp‚ü©
  contrapose! h
  obtain hp‚ÇÄ | hp‚ÇÄ := h.1.lt_or_gt
  ¬∑ exact (binEntropy_neg_of_neg hp‚ÇÄ).ne
  obtain hp‚ÇÅ | hp‚ÇÅ := h.2.lt_or_gt.symm
  ¬∑ exact (binEntropy_neg_of_one_lt hp‚ÇÅ).ne
  ¬∑ exact (binEntropy_pos hp‚ÇÄ hp‚ÇÅ).ne'

/-- For probability `p ‚â† 0.5`, `binEntropy p < log 2`. -/
lemma binEntropy_lt_log_two : binEntropy p < log 2 ‚Üî p ‚â† 2‚Åª¬π := by
  refine ‚ü®?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rintro h rfl
    simp at h
  wlog hp : p < 2‚Åª¬π
  ¬∑ have hp : 1 - p < 2‚Åª¬π := by
      rw [sub_lt_comm]; norm_num at *; linarith +splitNe
    rw [‚Üê binEntropy_one_sub]
    exact this hp.ne hp
  obtain hp‚ÇÄ | hp‚ÇÄ := le_or_gt p 0
  ¬∑ exact (binEntropy_nonpos_of_nonpos hp‚ÇÄ).trans_lt <| log_pos <| by simp
  have hp‚ÇÅ : 0 < 1 - p := sub_pos.2 <| hp.trans <| by norm_num
  calc
  _ < log (p * p‚Åª¬π + (1 - p) * (1 - p)‚Åª¬π) :=
    strictConcaveOn_log_Ioi.2 (inv_pos.2 hp‚ÇÄ) (inv_pos.2 hp‚ÇÅ)
      (by simpa [eq_sub_iff_add_eq, ‚Üê two_mul, mul_comm, mul_eq_one_iff_eq_inv‚ÇÄ]) hp‚ÇÄ hp‚ÇÅ (by simp)
  _ = log 2 := by rw [mul_inv_cancel‚ÇÄ, mul_inv_cancel‚ÇÄ, one_add_one_eq_two] <;> positivity

lemma binEntropy_le_log_two : binEntropy p ‚â§ log 2 := by
  obtain rfl | hp := eq_or_ne p 2‚Åª¬π
  ¬∑ simp
  ¬∑ exact (binEntropy_lt_log_two.2 hp).le

lemma binEntropy_eq_log_two : binEntropy p = log 2 ‚Üî p = 2‚Åª¬π := by
  rw [‚Üê binEntropy_le_log_two.not_lt_iff_eq, binEntropy_lt_log_two, not_ne_iff]

/-- Binary entropy is continuous everywhere.
This is due to definition of `Real.log` for negative numbers. -/
@[fun_prop] lemma binEntropy_continuous : Continuous binEntropy := by
  rw [binEntropy_eq_negMulLog_add_negMulLog_one_sub']; fun_prop

@[fun_prop] lemma differentiableAt_binEntropy (hp‚ÇÄ : p ‚â† 0) (hp‚ÇÅ : p ‚â† 1) :
    DifferentiableAt ‚Ñù binEntropy p := by
  rw [ne_comm, ‚Üê sub_ne_zero] at hp‚ÇÅ
  unfold binEntropy
  simp only [log_inv, mul_neg]
  fun_prop (disch := assumption)

lemma differentiableAt_binEntropy_iff_ne_zero_one :
    DifferentiableAt ‚Ñù binEntropy p ‚Üî p ‚â† 0 ‚àß p ‚â† 1 := by
  refine ‚ü®fun h ‚Ü¶ ‚ü®?_, ?_‚ü©, fun h ‚Ü¶ differentiableAt_binEntropy h.1 h.2‚ü©
    <;> rintro rfl <;> unfold binEntropy at h
  ¬∑ rw [DifferentiableAt.fun_add_iff_left] at h
    ¬∑ simp [log_inv, mul_neg, ‚Üê neg_mul, ‚Üê negMulLog_def, differentiableAt_negMulLog_iff] at h
    ¬∑ fun_prop (disch := simp)
  ¬∑ rw [DifferentiableAt.fun_add_iff_right, differentiableAt_iff_comp_const_sub (b := 1)] at h
    ¬∑ simp [log_inv, mul_neg, ‚Üê neg_mul, ‚Üê negMulLog_def, differentiableAt_negMulLog_iff] at h
    ¬∑ fun_prop (disch := simp)

set_option push_neg.use_distrib true in
/-- Binary entropy has derivative `log (1 - p) - log p`.
It's not differentiable at `0` or `1` but the junk values of `deriv` and `log` coincide there. -/
lemma deriv_binEntropy (p : ‚Ñù) : deriv binEntropy p = log (1 - p) - log p := by
  by_cases hp : p ‚â† 0 ‚àß p ‚â† 1
  ¬∑ obtain ‚ü®hp‚ÇÄ, hp‚ÇÅ‚ü© := hp
    rw [ne_comm, ‚Üê sub_ne_zero] at hp‚ÇÅ
    rw [binEntropy_eq_negMulLog_add_negMulLog_one_sub', deriv_fun_add, deriv_comp_const_sub,
      deriv_negMulLog hp‚ÇÄ, deriv_negMulLog hp‚ÇÅ]
    ¬∑ ring
    all_goals fun_prop (disch := assumption)
  -- pathological case where `deriv = 0` since `binEntropy` is not differentiable there
  ¬∑ rw [deriv_zero_of_not_differentiableAt (differentiableAt_binEntropy_iff_ne_zero_one.not.2 hp)]
    push_neg at hp
    obtain rfl | rfl := hp <;> simp

/-! ### `q`-ary entropy -/

/-- Shannon q-ary Entropy function (measured in Nats, i.e., using natural logs).

It's the Shannon entropy of a random variable with possible outcomes {1, ..., q}
where outcome `1` has probability `1 - p` and all other outcomes are equally likely.

The usual domain of definition is p ‚àà [0,1], i.e., input is a probability.

This is a generalization of the binary entropy function `binEntropy`. -/
@[pp_nodot] noncomputable def qaryEntropy (q : ‚Ñï) (p : ‚Ñù) : ‚Ñù := p * log (q - 1 : ‚Ñ§) + binEntropy p

@[simp] lemma qaryEntropy_zero (q : ‚Ñï) : qaryEntropy q 0 = 0 := by simp [qaryEntropy]
@[simp] lemma qaryEntropy_one (q : ‚Ñï) : qaryEntropy q 1 = log (q - 1 : ‚Ñ§) := by simp [qaryEntropy]
@[simp] lemma qaryEntropy_two : qaryEntropy 2 = binEntropy := by ext; simp [qaryEntropy]

lemma qaryEntropy_pos (hp‚ÇÄ : 0 < p) (hp‚ÇÅ : p < 1) : 0 < qaryEntropy q p := by
  unfold qaryEntropy
  have := binEntropy_pos hp‚ÇÄ hp‚ÇÅ
  positivity

lemma qaryEntropy_nonneg (hp‚ÇÄ : 0 ‚â§ p) (hp‚ÇÅ : p ‚â§ 1) : 0 ‚â§ qaryEntropy q p := by
  obtain rfl | hp‚ÇÄ := hp‚ÇÄ.eq_or_lt
  ¬∑ simp
  obtain rfl | hp‚ÇÅ := hp‚ÇÅ.eq_or_lt
  ¬∑ simpa [qaryEntropy, -Int.cast_sub] using log_intCast_nonneg _
  exact (qaryEntropy_pos hp‚ÇÄ hp‚ÇÅ).le

/-- Outside the usual range of `qaryEntropy`, it is negative. This is due to `log p = log |p|`. -/
lemma qaryEntropy_neg_of_neg (hp : p < 0) : qaryEntropy q p < 0 :=
  add_neg_of_nonpos_of_neg (mul_nonpos_of_nonpos_of_nonneg hp.le (log_intCast_nonneg _))
    (binEntropy_neg_of_neg hp)

/-- Outside the usual range of `qaryEntropy`, it is negative. This is due to `log p = log |p|`. -/
lemma qaryEntropy_nonpos_of_nonpos (hp : p ‚â§ 0) : qaryEntropy q p ‚â§ 0 :=
  add_nonpos (mul_nonpos_of_nonpos_of_nonneg hp (log_intCast_nonneg _))
    (binEntropy_nonpos_of_nonpos hp)

/-- The q-ary entropy function is continuous everywhere.
This is due to definition of `Real.log` for negative numbers. -/
@[fun_prop] lemma qaryEntropy_continuous : Continuous (qaryEntropy q) := by
  unfold qaryEntropy; fun_prop

@[fun_prop] lemma differentiableAt_qaryEntropy (hp‚ÇÄ : p ‚â† 0) (hp‚ÇÅ : p ‚â† 1) :
    DifferentiableAt ‚Ñù (qaryEntropy q) p := by unfold qaryEntropy; fun_prop (disch := assumption)

lemma deriv_qaryEntropy (hp‚ÇÄ : p ‚â† 0) (hp‚ÇÅ : p ‚â† 1) :
    deriv (qaryEntropy q) p = log (q - 1) + log (1 - p) - log p := by
  unfold qaryEntropy
  rw [deriv_fun_add]
  ¬∑ simp only [Int.cast_sub, Int.cast_natCast, Int.cast_one, differentiableAt_fun_id,
      deriv_mul_const, deriv_id'', one_mul, deriv_binEntropy, add_sub_assoc]
  all_goals fun_prop (disch := assumption)

/-- Binary entropy has derivative `log (1 - p) - log p`. -/
lemma hasDerivAt_binEntropy (hp‚ÇÄ : p ‚â† 0) (hp‚ÇÅ : p ‚â† 1) :
    HasDerivAt binEntropy (log (1 - p) - log p) p :=
  deriv_binEntropy _ ‚ñ∏ (differentiableAt_binEntropy hp‚ÇÄ hp‚ÇÅ).hasDerivAt

lemma hasDerivAt_qaryEntropy (hp‚ÇÄ : p ‚â† 0) (hp‚ÇÅ : p ‚â† 1) :
    HasDerivAt (qaryEntropy q) (log (q - 1) + log (1 - p) - log p) p :=
  deriv_qaryEntropy hp‚ÇÄ hp‚ÇÅ ‚ñ∏ (differentiableAt_qaryEntropy hp‚ÇÄ hp‚ÇÅ).hasDerivAt

open Filter Topology Set

private lemma tendsto_log_one_sub_sub_log_nhdsGT_atAtop :
    Tendsto (fun p ‚Ü¶ log (1 - p) - log p) (ùìù[>] 0) atTop := by
  apply Filter.tendsto_atTop_add_left_of_le' (ùìù[>] 0) (log (1/2) : ‚Ñù)
  ¬∑ have h‚ÇÅ : (0 : ‚Ñù) < 1 / 2 := by simp
    filter_upwards [Ioc_mem_nhdsGT h‚ÇÅ] with p hx
    gcongr
    linarith [hx.2]
  ¬∑ apply tendsto_neg_atTop_iff.mpr tendsto_log_nhdsGT_zero

private lemma tendsto_log_one_sub_sub_log_nhdsLT_one_atBot :
    Tendsto (fun p ‚Ü¶ log (1 - p) - log p) (ùìù[<] 1) atBot := by
  apply Filter.tendsto_atBot_add_right_of_ge' (ùìù[<] 1) (-log (1 - 2‚Åª¬π))
  ¬∑ have : Tendsto log (ùìù[>] 0) atBot := Real.tendsto_log_nhdsGT_zero
    apply Tendsto.comp (f := (1 - ¬∑)) (g := log) this
    have contF : Continuous ((1 : ‚Ñù) - ¬∑) := continuous_sub_left 1
    have : MapsTo ((1 : ‚Ñù) - ¬∑) (Iio 1) (Ioi 0) := by
      intro p hx
      simp_all only [mem_Iio, mem_Ioi, sub_pos]
    convert ContinuousWithinAt.tendsto_nhdsWithin (x :=(1 : ‚Ñù)) contF.continuousWithinAt this
    exact Eq.symm (sub_eq_zero_of_eq rfl)
  ¬∑ have h‚ÇÅ : (1 : ‚Ñù) - (2 : ‚Ñù)‚Åª¬π < 1 := by norm_num
    filter_upwards [Ico_mem_nhdsLT h‚ÇÅ] with p hx
    gcongr
    exact hx.1

lemma not_continuousAt_deriv_qaryEntropy_one :
    ¬¨ContinuousAt (deriv (qaryEntropy q)) 1 := by
  have tendstoBot : Tendsto (fun p ‚Ü¶ log (q - 1) + log (1 - p) - log p) (ùìù[<] 1) atBot := by
    have : (fun p ‚Ü¶ log (q - 1) + log (1 - p) - log p)
      = (fun p ‚Ü¶ log (q - 1) + (log (1 - p) - log p)) := by
      ext
      ring
    rw [this]
    apply tendsto_atBot_add_const_left
    exact tendsto_log_one_sub_sub_log_nhdsLT_one_atBot
  apply not_continuousAt_of_tendsto (Filter.Tendsto.congr' _ tendstoBot) nhdsWithin_le_nhds
  ¬∑ simp only [disjoint_nhds_atBot_iff, not_isBot, not_false_eq_true]
  filter_upwards [Ioo_mem_nhdsLT (show 1 - 2‚Åª¬π < (1 : ‚Ñù) by norm_num)]
  intros
  apply (deriv_qaryEntropy _ _).symm
  ¬∑ simp_all only [mem_Ioo, ne_eq]
    linarith [show (1 : ‚Ñù) = 2‚Åª¬π + 2‚Åª¬π by norm_num]
  ¬∑ simp_all only [mem_Ioo, ne_eq]
    linarith [two_inv_lt_one (Œ± := ‚Ñù)]

lemma not_continuousAt_deriv_qaryEntropy_zero :
    ¬¨ContinuousAt (deriv (qaryEntropy q)) 0 := by
  have tendstoTop : Tendsto (fun p ‚Ü¶ log (q - 1) + log (1 - p) - log p) (ùìù[>] 0) atTop := by
    have : (fun p ‚Ü¶ log (q - 1) + log (1 - p) - log p)
        = (fun p ‚Ü¶ log (q - 1) + (log (1 - p) - log p)) := by ext; ring
    rw [this]
    exact tendsto_atTop_add_const_left _ _ tendsto_log_one_sub_sub_log_nhdsGT_atAtop
  apply not_continuousAt_of_tendsto (Filter.Tendsto.congr' _ tendstoTop) nhdsWithin_le_nhds
  ¬∑ simp only [disjoint_nhds_atTop_iff, not_isTop, not_false_eq_true]
  filter_upwards [Ioo_mem_nhdsGT (show (0 : ‚Ñù) < 2‚Åª¬π by norm_num)]
  intros
  apply (deriv_qaryEntropy _ _).symm
  ¬∑ simp_all only [mem_Ioo, ne_eq]
    linarith
  ¬∑ simp_all only [mem_Ioo, ne_eq]
    linarith [two_inv_lt_one (Œ± := ‚Ñù)]

/-- Second derivative of q-ary entropy. -/
lemma deriv2_qaryEntropy :
    deriv^[2] (qaryEntropy q) p = -1 / (p * (1 - p)) := by
  simp only [Function.iterate_succ, Function.iterate_zero, Function.id_comp, Function.comp_apply]
  by_cases is_x_where_nondiff : p ‚â† 0 ‚àß p ‚â† 1  -- normal case
  ¬∑ obtain ‚ü®xne0, xne1‚ü© := is_x_where_nondiff
    suffices ‚àÄ·∂† y in (ùìù p),
        deriv (fun p ‚Ü¶ (qaryEntropy q) p) y = log (q - 1) + log (1 - y) - log y by
      refine (Filter.EventuallyEq.deriv_eq this).trans ?_
      rw [deriv_fun_sub ?_ (differentiableAt_log xne0)]
      ¬∑ rw [deriv.log differentiableAt_fun_id xne0]
        simp only [deriv_id'', one_div]
        ¬∑ have {q : ‚Ñù} (p : ‚Ñù) : DifferentiableAt ‚Ñù (fun p => q - p) p := by fun_prop
          have d_oneminus (p : ‚Ñù) : deriv (fun (y : ‚Ñù) ‚Ü¶ 1 - y) p = -1 := by
            rw [deriv_const_sub 1, deriv_id'']
          simp [field, sub_ne_zero_of_ne xne1.symm, this, d_oneminus]
          ring
      ¬∑ apply DifferentiableAt.add
        simp only [differentiableAt_const]
        exact DifferentiableAt.log (by fun_prop) (sub_ne_zero.mpr xne1.symm)
    filter_upwards [eventually_ne_nhds xne0, eventually_ne_nhds xne1]
      with y xne0 h2 using deriv_qaryEntropy xne0 h2
  -- Pathological case where we use junk value (because function not differentiable)
  ¬∑ have : p = 0 ‚à® p = 1 := Decidable.or_iff_not_not_and_not.mpr is_x_where_nondiff
    rw [deriv_zero_of_not_differentiableAt]
    ¬∑ simp_all only [ne_eq, not_and, Decidable.not_not]
      cases this <;> simp_all only [
        mul_zero, one_ne_zero, zero_ne_one, sub_zero, mul_one, div_zero, sub_self]
    ¬∑ intro h
      have contAt := h.continuousAt
      cases this <;> simp_all [
        not_continuousAt_deriv_qaryEntropy_zero, not_continuousAt_deriv_qaryEntropy_one]

lemma deriv2_binEntropy : deriv^[2] binEntropy p = -1 / (p * (1 - p)) :=
  qaryEntropy_two ‚ñ∏ deriv2_qaryEntropy

/-! ### Strict monotonicity of entropy -/

/-- Qary entropy is strictly increasing in the interval [0, 1 - q‚Åª¬π]. -/
lemma qaryEntropy_strictMonoOn (qLe2 : 2 ‚â§ q) :
    StrictMonoOn (qaryEntropy q) (Icc 0 (1 - 1/q)) := by
  intro p1 hp1 p2 hp2 p1le2
  apply strictMonoOn_of_deriv_pos (convex_Icc 0 (1 - 1/(q : ‚Ñù))) _ _ hp1 hp2 p1le2
  ¬∑ exact qaryEntropy_continuous.continuousOn
  ¬∑ intro p hp
    have : 2 ‚â§ (q : ‚Ñù) := Nat.ofNat_le_cast.mpr qLe2
    have zero_le_qinv : 0 < (q : ‚Ñù)‚Åª¬π := by positivity
    have : 0 < 1 - p := by
      simp only [sub_pos]
      have p_lt_1_minus_qinv : p < 1 - (q : ‚Ñù)‚Åª¬π := by
        simp_all only [inv_pos, interior_Icc, mem_Ioo, one_div]
      linarith
    simp only [one_div, interior_Icc, mem_Ioo] at hp
    rw [deriv_qaryEntropy (by linarith)]
    ¬∑ simp
      rw [‚Üê log_mul (by linarith) (by linarith)]
      apply Real.strictMonoOn_log (mem_Ioi.mpr hp.1)
      ¬∑ simp_all only [mem_Ioi, mul_pos_iff_of_pos_left, show 0 < (q : ‚Ñù) - 1 by linarith]
      ¬∑ have qpos : 0 < (q : ‚Ñù) := by positivity
        have : q * p < q - 1 := by
          convert (mul_lt_mul_left qpos).2 hp.2 using 1
          simp only [mul_sub, mul_one, isUnit_iff_ne_zero, ne_eq, ne_of_gt qpos, not_false_eq_true,
            IsUnit.mul_inv_cancel]
        linarith
    exact (ne_of_gt (lt_add_neg_iff_lt.mp this : p < 1)).symm

/-- Qary entropy is strictly decreasing in the interval [1 - q‚Åª¬π, 1]. -/
lemma qaryEntropy_strictAntiOn (qLe2 : 2 ‚â§ q) :
    StrictAntiOn (qaryEntropy q) (Icc (1 - 1/q) 1) := by
  intro p1 hp1 p2 hp2 p1le2
  apply strictAntiOn_of_deriv_neg (convex_Icc (1 - 1/(q : ‚Ñù)) 1) _ _ hp1 hp2 p1le2
  ¬∑ exact qaryEntropy_continuous.continuousOn
  ¬∑ intro p hp
    have : 2 ‚â§ (q : ‚Ñù) := Nat.ofNat_le_cast.mpr qLe2
    have qinv_lt_1 : (q : ‚Ñù)‚Åª¬π < 1 := inv_lt_one_of_one_lt‚ÇÄ (by linarith)
    have zero_lt_1_sub_p : 0 < 1 - p := by simp_all only [sub_pos, interior_Icc, mem_Ioo]
    simp only [one_div, interior_Icc, mem_Ioo] at hp
    rw [deriv_qaryEntropy (by linarith)]
    ¬∑ simp
      rw [‚Üê log_mul (by linarith) (by linarith)]
      apply Real.strictMonoOn_log (mem_Ioi.mpr (show 0 < (‚Üëq - 1) * (1 - p) by nlinarith))
      ¬∑ simp_all only [mem_Ioi]
        linarith
      ¬∑ have qpos : 0 < (q : ‚Ñù) := by positivity
        ring_nf
        simp only [add_lt_iff_neg_right, neg_add_lt_iff_lt_add, add_zero, gt_iff_lt]
        have : (q : ‚Ñù) - 1 < p * q := by
          have tmp := mul_lt_mul_of_pos_right hp.1 qpos
          simp at tmp
          have : (q : ‚Ñù) ‚â† 0 := (ne_of_lt qpos).symm
          have asdfasfd : (1 - (q : ‚Ñù)‚Åª¬π) * ‚Üëq = q - 1 := by calc (1 - (q : ‚Ñù)‚Åª¬π) * ‚Üëq
            _ = q - (q : ‚Ñù)‚Åª¬π * (q : ‚Ñù) := by ring
            _ = q - 1 := by simp_all only [ne_eq, isUnit_iff_ne_zero,
              not_false_eq_true, IsUnit.inv_mul_cancel]
          rwa [asdfasfd] at tmp
        nlinarith
    exact (ne_of_gt (lt_add_neg_iff_lt.mp zero_lt_1_sub_p : p < 1)).symm

/-- Binary entropy is strictly increasing in interval [0, 1/2]. -/
lemma binEntropy_strictMonoOn : StrictMonoOn binEntropy (Icc 0 2‚Åª¬π) := by
  rw [show Icc (0 : ‚Ñù) 2‚Åª¬π = Icc 0 (1 - 1/2) by norm_num, ‚Üê qaryEntropy_two]
  exact qaryEntropy_strictMonoOn (by rfl)

/-- Binary entropy is strictly decreasing in interval [1/2, 1]. -/
lemma binEntropy_strictAntiOn : StrictAntiOn binEntropy (Icc 2‚Åª¬π 1) := by
  rw [show (Icc (2‚Åª¬π : ‚Ñù) 1) = Icc (1/2) 1 by norm_num, ‚Üê qaryEntropy_two]
  convert qaryEntropy_strictAntiOn (by rfl) using 1
  norm_num

/-! ### Strict concavity of entropy -/

lemma strictConcaveOn_qaryEntropy : StrictConcaveOn ‚Ñù (Icc 0 1) (qaryEntropy q) := by
  apply strictConcaveOn_of_deriv2_neg (convex_Icc 0 1) qaryEntropy_continuous.continuousOn
  intro p hp
  rw [deriv2_qaryEntropy]
  ¬∑ simp_all only [interior_Icc, mem_Ioo]
    apply div_neg_of_neg_of_pos
    ¬∑ norm_num [show 0 < log 2 by positivity]
    ¬∑ simp_all only [mul_pos_iff_of_pos_left, sub_pos]

lemma strictConcave_binEntropy : StrictConcaveOn ‚Ñù (Icc 0 1) binEntropy :=
  qaryEntropy_two ‚ñ∏ strictConcaveOn_qaryEntropy

end Real
