/-
Copyright (c) 2025 Jakob Stiefel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jakob Stiefel
-/
import Mathlib.Analysis.SpecialFunctions.MulExpNegMulSq
import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.MeasureTheory.Integral.BoundedContinuousFunction
import Mathlib.MeasureTheory.Integral.DominatedConvergence
import Mathlib.MeasureTheory.Measure.RegularityCompacts
import Mathlib.Topology.ContinuousMap.StoneWeierstrass

/-!
# Properties of the integral of `mulExpNegMulSq`

The mapping `mulExpNegMulSq` can be used to transform a function `g : E â†’ â„` into a bounded
function `mulExpNegMulSq Îµ âˆ˜ g : E â†’ â„ = fun x => g x * Real.exp (-Îµ * g x * g x)`. This file
contains results on the integral of `mulExpNegMulSq g Îµ` with respect to a finite measure `P`.

## Lemmas

- `tendsto_integral_mulExpNegMulSq_comp`: By the dominated convergence theorem and
  `mulExpNegMulSq_abs_le_norm`, the integral of `mulExpNegMulSq Îµ âˆ˜ g` with respect to a
  finite measure `P` converges to the integral of `g`, as `Îµ â†’ 0`;
- `tendsto_integral_mul_one_add_inv_smul_sq_pow`: The integral of `mulExpNegMulSq Îµ âˆ˜ g` with
  respect to a finite measure `P` can be approximated by the integral of the sequence approximating
  the exponential function, `fun x => (g * (1 + (n : â„)â»Â¹ â€¢ -(Îµ â€¢ g * g)) ^ n) x`. This allows to
  transfer properties of a subalgebra of functions containing `g` to the function
  `mulExpNegMulSq Îµ âˆ˜ g`, see e.g. `integral_mulExpNegMulSq_comp_eq`.

## Main Result

`dist_integral_mulExpNegMulSq_comp_le`: For a subalgebra of functions `A`, if for any `g âˆˆ A` the
integral with respect to two finite measures `P, P'` coincide, then the difference of the integrals
of `mulExpNegMulSq Îµ âˆ˜ g` with respect to `P, P'` is bounded by `6 * âˆšÎµ`.
This is a key ingredient in the proof of theorem `ext_of_forall_mem_subalgebra_integral_eq`, where
it is shown that a subalgebra of functions that separates points separates finite measures.
-/

open MeasureTheory Real NNReal ENNReal BoundedContinuousFunction Filter

open scoped Topology

variable {E : Type*} [TopologicalSpace E] [MeasurableSpace E] [BorelSpace E]
    {P : Measure E} [IsFiniteMeasure P] {Îµ : â„}

theorem integrable_mulExpNegMulSq_comp (f : C(E, â„)) (hÎµ : 0 < Îµ) :
    Integrable (fun x => mulExpNegMulSq Îµ (f x)) P := by
  apply integrable P âŸ¨âŸ¨fun x => mulExpNegMulSq Îµ (f x), by fun_propâŸ©, âŸ¨2 * (âˆšÎµ)â»Â¹, _âŸ©âŸ©
  exact fun x y => dist_mulExpNegMulSq_le_two_mul_sqrt hÎµ (f x) (f y)

theorem integrable_mulExpNegMulSq_comp_restrict_of_isCompact {K : Set E} (hK : IsCompact K)
    (hKmeas : MeasurableSet K) (g : C(E, â„)) :
    Integrable (fun x => mulExpNegMulSq Îµ (g x)) (P.restrict K) :=
  g.continuous.mulExpNegMulSq.continuousOn.integrableOn_compact' hK hKmeas

/-- The integral of `mulExpNegMulSq Îµ âˆ˜ g` with respect to a finite measure `P` converges to the
integral of `g`, as `Îµ â†’ 0` from above. -/
theorem tendsto_integral_mulExpNegMulSq_comp (g : E â†’áµ‡ â„) :
    Tendsto (fun Îµ => âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P) (ğ“[>] 0) (ğ“ (âˆ« x, g x âˆ‚P)) := by
  apply tendsto_of_seq_tendsto
  intro u hu
  obtain âŸ¨N, huposâŸ© := eventually_atTop.mp (tendsto_nhdsWithin_iff.mp hu).2
  apply tendsto_integral_filter_of_norm_le_const ?h_meas ?h_bound ?h_lim
  Â· exact Eventually.of_forall (fun n => g.continuous.mulExpNegMulSq.aestronglyMeasurable)
  Â· use norm g
    rw [eventually_atTop]
    use N
    intro n hn
    exact Eventually.of_forall
      (fun _ => abs_mulExpNegMulSq_comp_le_norm g (le_of_lt (Set.mem_Ioi.mp (hupos n hn))))
  Â· exact Eventually.of_forall (fun _ => (tendsto_nhdsWithin_of_tendsto_nhds
        tendsto_mulExpNegMulSq).comp hu)

/-- The integral of `mulExpNegMulSq Îµ âˆ˜ g` with respect to a finite measure `P` can be
approximated by the integral of the sequence approximating the exponential function. -/
theorem tendsto_integral_mul_one_add_inv_smul_sq_pow (g : E â†’áµ‡ â„) (hÎµ : 0 < Îµ) :
    Tendsto (fun (n : â„•) => âˆ« x, (g * (1 + (n : â„)â»Â¹ â€¢ -(Îµ â€¢ g * g)) ^ n) x âˆ‚ P)
    atTop (ğ“ (âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P)) := by
  apply tendsto_integral_filter_of_norm_le_const ?h_meas ?h_bound ?h_lim
  Â· apply Eventually.of_forall
    exact fun n => StronglyMeasurable.aestronglyMeasurable (Continuous.stronglyMeasurable
        (Continuous.mul g.continuous ((1 + ((n : â„)â»Â¹ â€¢ -(Îµ â€¢ g * g))) ^ n).continuous))
  Â· obtain âŸ¨N, hgNâŸ© := exists_nat_gt (Îµ * (norm g * norm g))
    use norm g
    rw [eventually_atTop]
    use N
    intro n hn
    have hnpos : 0 < (n : â„) := by
      apply lt_of_lt_of_le (lt_of_le_of_lt _ hgN) (Nat.cast_le.mpr hn)
      exact (mul_nonneg (le_of_lt hÎµ) (mul_self_nonneg (norm g)))
    apply Eventually.of_forall
    intro x
    simp only [smul_neg, BoundedContinuousFunction.coe_mul, Pi.mul_apply, pow_apply,
      BoundedContinuousFunction.coe_add, BoundedContinuousFunction.coe_one, coe_neg,
      BoundedContinuousFunction.coe_smul, smul_eq_mul, Pi.add_apply, Pi.one_apply, Pi.neg_apply,
      norm_mul, norm_eq_abs, norm_pow]
    refine (mul_le_mul_of_nonneg_right (norm_coe_le_norm g x) (pow_nonneg (abs_nonneg _) n)).trans
      <| mul_le_of_le_one_right (norm_nonneg _) ?_
    apply pow_le_oneâ‚€ (abs_nonneg _)
    rw [mul_assoc, inv_mul_eq_div, abs_le]
    refine âŸ¨?_, (add_le_iff_nonpos_right 1).mpr (Left.neg_nonpos_iff.mpr
      (div_nonneg (mul_nonneg (le_of_lt hÎµ) (mul_self_nonneg (g x))) (le_of_lt hnpos)))âŸ©
    apply le_trans (by linarith) (sub_nonneg_of_le ((div_le_one hnpos).mpr _))
    apply le_trans (le_trans _ (le_of_lt hgN)) (Nat.cast_le.mpr hn)
    apply mul_le_mul (Preorder.le_refl Îµ) _ (mul_self_nonneg (g x)) (le_of_lt hÎµ)
    rw [â† abs_le_iff_mul_self_le, abs_norm]
    exact norm_coe_le_norm g x
  Â· apply Eventually.of_forall
    intro x
    apply Tendsto.const_mul (g x)
    simp [mul_assoc, inv_mul_eq_div, â† neg_div]
    exact tendsto_one_add_div_pow_exp (-(Îµ * (g x * g x)))

@[deprecated (since := "2025-05-22")]
alias tendsto_integral_mul_one_plus_inv_smul_sq_pow := tendsto_integral_mul_one_add_inv_smul_sq_pow

theorem integral_mulExpNegMulSq_comp_eq {P' : Measure E} [IsFiniteMeasure P']
    {A : Subalgebra â„ (E â†’áµ‡ â„)} (hÎµ : 0 < Îµ)
    (heq : âˆ€ g âˆˆ A, âˆ« x, (g : E â†’ â„) x âˆ‚P = âˆ« x, (g : E â†’ â„) x âˆ‚P') {g : E â†’áµ‡ â„} (hgA : g âˆˆ A) :
    âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P = âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P' := by
  have one_add_inv_mul_mem (n : â„•) : g * (1 + (n : â„)â»Â¹ â€¢ -(Îµ â€¢ g * g)) ^ n âˆˆ A := by
    apply Subalgebra.mul_mem A hgA (Subalgebra.pow_mem A _ n)
    apply Subalgebra.add_mem A (Subalgebra.one_mem A) (Subalgebra.smul_mem A _ nâ»Â¹)
    exact Subalgebra.neg_mem A (Subalgebra.mul_mem A (Subalgebra.smul_mem A hgA Îµ) hgA)
  have limP : Tendsto (fun n : â„• => âˆ« x, (g * (1 + (n : â„)â»Â¹ â€¢ -(Îµ â€¢ g * g)) ^ n) x âˆ‚P) atTop
      (ğ“ (âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P')) := by
    rw [funext fun n => heq _ (one_add_inv_mul_mem n)]
    exact tendsto_integral_mul_one_add_inv_smul_sq_pow g hÎµ
  exact tendsto_nhds_unique
    (tendsto_integral_mul_one_add_inv_smul_sq_pow g hÎµ) limP

theorem abs_integral_sub_setIntegral_mulExpNegMulSq_comp_lt (f : C(E, â„))
    {K : Set E} (hK : MeasurableSet K) (hÎµ : 0 < Îµ) (hKP : P Ká¶œ < Îµ.toNNReal) :
    |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P| < âˆšÎµ := by
  apply lt_of_le_of_lt (norm_integral_sub_setIntegral_le
    (Eventually.of_forall (fun _ => abs_mulExpNegMulSq_le hÎµ)) hK
    (integrable_mulExpNegMulSq_comp f hÎµ))
  rw [mul_inv_lt_iffâ‚€ (sqrt_pos_of_pos hÎµ), mul_self_sqrt (le_of_lt hÎµ)]
  exact toReal_lt_of_lt_ofReal hKP

theorem abs_setIntegral_mulExpNegMulSq_comp_sub_le_mul_measure {K : Set E} (hK : IsCompact K)
    (hKmeas : MeasurableSet K) (f g : C(E, â„)) {Î´ : â„} (hÎµ : 0 < Îµ)
    (hfg : âˆ€ x âˆˆ K, |g x - f x| < Î´) :
    |âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P|
      â‰¤ Î´ * (P K).toReal := by
  rw [â† (integral_sub (integrable_mulExpNegMulSq_comp_restrict_of_isCompact hK hKmeas g)
      (integrable_mulExpNegMulSq_comp_restrict_of_isCompact hK hKmeas f)), â† norm_eq_abs]
  exact norm_setIntegral_le_of_norm_le_const hK.measure_lt_top
    (fun x hxK => le_trans (dist_mulExpNegMulSq_le_dist hÎµ) (hfg x hxK).le)

variable {E : Type*} [MeasurableSpace E] [PseudoEMetricSpace E] [BorelSpace E] [CompleteSpace E]
    [SecondCountableTopology E]
    {P P' : Measure E} [IsFiniteMeasure P] [IsFiniteMeasure P']

/-- If for any `g âˆˆ A` the integrals with respect to two finite measures `P, P'` coincide, then the
difference of the integrals of `mulExpNegMulSq Îµ âˆ˜ g` with respect to `P, P'` is bounded by
`6 * âˆšÎµ`. -/
theorem dist_integral_mulExpNegMulSq_comp_le (f : E â†’áµ‡ â„)
    {A : Subalgebra â„ (E â†’áµ‡ â„)} (hA : (A.map (toContinuousMapâ‚ â„)).SeparatesPoints)
    (heq : âˆ€ g âˆˆ A, âˆ« x, (g : E â†’ â„) x âˆ‚P = âˆ« x, (g : E â†’ â„) x âˆ‚P') (hÎµ : 0 < Îµ) :
    |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'| â‰¤ 6 * âˆšÎµ := by
  -- if both measures are zero, the result is trivial
  by_cases hPP' : P = 0 âˆ§ P' = 0
  Â· simp only [hPP', integral_zero_measure, sub_self, abs_zero, Nat.ofNat_pos,
    mul_nonneg_iff_of_pos_left, (le_of_lt (sqrt_pos_of_pos hÎµ))]
  let const : â„ := (max (P.real Set.univ) (P'.real Set.univ))
  have pos_of_measure : 0 < const := by
    rw [not_and_or] at hPP'
    rcases hPP' with hP0 | hP'0
    Â· exact lt_max_of_lt_left
        (toReal_pos ((Measure.measure_univ_ne_zero).mpr hP0) (measure_ne_top P Set.univ))
    Â· exact lt_max_of_lt_right
        (toReal_pos ((Measure.measure_univ_ne_zero).mpr hP'0) (measure_ne_top P' Set.univ))
  -- obtain K, a compact and closed set, which covers E up to a small area of measure at most Îµ
  -- w.r.t. both P and P'
  obtain âŸ¨KP, _, hKPco, hKPcl, hKPâŸ© := MeasurableSet.exists_isCompact_isClosed_diff_lt
    (MeasurableSet.univ) (measure_ne_top P Set.univ) (ne_of_gt (ofReal_pos.mpr hÎµ))
  obtain âŸ¨KP', _, hKP'co, hKP'cl, hKP'âŸ© := MeasurableSet.exists_isCompact_isClosed_diff_lt
    (MeasurableSet.univ) (measure_ne_top P' Set.univ) (ne_of_gt (ofReal_pos.mpr hÎµ))
  let K := KP âˆª KP'
  have hKco := IsCompact.union hKPco hKP'co
  have hKcl := IsClosed.union hKPcl hKP'cl
  simp [â† Set.compl_eq_univ_diff] at hKP hKP'
  have hKPbound : P (KP âˆª KP')á¶œ < Îµ.toNNReal := lt_of_le_of_lt
        (measure_mono (Set.compl_subset_compl_of_subset (Set.subset_union_left))) hKP
  have hKP'bound : P' (KP âˆª KP')á¶œ < Îµ.toNNReal := lt_of_le_of_lt
        (measure_mono (Set.compl_subset_compl_of_subset (Set.subset_union_right))) hKP'
  -- Stone-Weierstrass approximation of f on K
  obtain âŸ¨g', hg'A, hg'approxâŸ© :=
      ContinuousMap.exists_mem_subalgebra_near_continuous_of_isCompact_of_separatesPoints
      hA f hKco (Left.mul_pos (sqrt_pos_of_pos hÎµ) (inv_pos_of_pos pos_of_measure))
  simp only [Subalgebra.mem_map] at hg'A
  let g := hg'A.choose
  have hgA : g âˆˆ A := hg'A.choose_spec.1
  have hgapprox : âˆ€ x âˆˆ K, â€–g x - f xâ€– < âˆšÎµ * constâ»Â¹ := by
    rw [â† coe_toContinuousMapâ‚ â„ g, hg'A.choose_spec.2]
    exact hg'approx
  -- collect the results needed in the decomposition at the end of the proof
  have line1 : |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P
      - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P| < âˆšÎµ :=
    abs_integral_sub_setIntegral_mulExpNegMulSq_comp_lt
      f (IsClosed.measurableSet hKcl) hÎµ hKPbound
  have line3 : |âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P
      - âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P| < âˆšÎµ := by
    rw [abs_sub_comm]
    exact (abs_integral_sub_setIntegral_mulExpNegMulSq_comp_lt
      g (IsClosed.measurableSet hKcl) hÎµ hKPbound)
  have line5 : |âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P'
      - âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P'| < âˆšÎµ :=
    (abs_integral_sub_setIntegral_mulExpNegMulSq_comp_lt
      g (IsClosed.measurableSet hKcl) hÎµ hKP'bound)
  have line7 : |âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P'
      - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'| < âˆšÎµ := by
    rw [abs_sub_comm]
    exact (abs_integral_sub_setIntegral_mulExpNegMulSq_comp_lt
      f (IsClosed.measurableSet hKcl) hÎµ hKP'bound)
  have line2 : |âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P
      - âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P| â‰¤ âˆšÎµ := by
    rw [abs_sub_comm]
    apply le_trans (abs_setIntegral_mulExpNegMulSq_comp_sub_le_mul_measure hKco
      (IsClosed.measurableSet hKcl) f g hÎµ hgapprox)
    rw [mul_assoc]
    apply mul_le_of_le_one_right (le_of_lt (sqrt_pos_of_pos hÎµ))
    apply inv_mul_le_one_of_leâ‚€ (le_max_of_le_left _) (le_of_lt pos_of_measure)
    exact (toReal_le_toReal (measure_ne_top P _) (measure_ne_top P _)).mpr
        (measure_mono (Set.subset_univ _))
  have line6 : |âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P'
      - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P'| â‰¤ âˆšÎµ := by
    apply le_trans (abs_setIntegral_mulExpNegMulSq_comp_sub_le_mul_measure hKco
      (IsClosed.measurableSet hKcl) f g hÎµ hgapprox)
    rw [mul_assoc]
    apply mul_le_of_le_one_right (le_of_lt (sqrt_pos_of_pos hÎµ))
    apply inv_mul_le_one_of_leâ‚€ (le_max_of_le_right _) (le_of_lt pos_of_measure)
    exact (toReal_le_toReal (measure_ne_top P' _) (measure_ne_top P' _)).mpr
        (measure_mono (Set.subset_univ _))
  have line4 : |âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P
      - âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P'| = 0 := by
    rw [abs_eq_zero, sub_eq_zero]
    exact integral_mulExpNegMulSq_comp_eq hÎµ heq hgA
  calc
      |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'|
    â‰¤ |âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P|
      + |âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P - âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P|
      + |âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P - âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P|
      + |âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P - âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P'|
      + |âˆ« x, mulExpNegMulSq Îµ (g x) âˆ‚P' - âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P'|
      + |âˆ« x in K, mulExpNegMulSq Îµ (g x) âˆ‚P' - âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P'|
      + |âˆ« x in K, mulExpNegMulSq Îµ (f x) âˆ‚P' - âˆ« x, mulExpNegMulSq Îµ (f x) âˆ‚P'| :=
        @dist_triangle8 â„ _ _ _ _ _ _ _ _ _
  _ â‰¤ 6 * âˆšÎµ := by linarith
