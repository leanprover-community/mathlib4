/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.JapaneseBracket
import Mathlib.Analysis.SpecialFunctions.Integrals
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.MeasureTheory.Measure.Lebesgue.Integral

/-!
# Evaluation of specific improper integrals

This file contains some integrability results, and evaluations of integrals, over `â„` or over
half-infinite intervals in `â„`.

## See also

- `Mathlib.Analysis.SpecialFunctions.Integrals` -- integrals over finite intervals
- `Mathlib.Analysis.SpecialFunctions.Gaussian` -- integral of `exp (-x ^ 2)`
- `Mathlib.Analysis.SpecialFunctions.JapaneseBracket`-- integrability of `(1+â€–xâ€–)^(-r)`.
-/


open Real Set Filter MeasureTheory intervalIntegral

open scoped Topology

theorem integrableOn_exp_Iic (c : â„) : IntegrableOn exp (Iic c) := by
  refine
    integrableOn_Iic_of_intervalIntegral_norm_bounded (exp c) c
      (fun y => intervalIntegrable_exp.1) tendsto_id
      (eventually_of_mem (Iic_mem_atBot 0) fun y _ => ?_)
  simp_rw [norm_of_nonneg (exp_pos _).le, integral_exp, sub_le_self_iff]
  exact (exp_pos _).le

theorem integral_exp_Iic (c : â„) : âˆ« x : â„ in Iic c, exp x = exp c := by
  refine
    tendsto_nhds_unique
      (intervalIntegral_tendsto_integral_Iic _ (integrableOn_exp_Iic _) tendsto_id) ?_
  simp_rw [integral_exp, show ğ“ (exp c) = ğ“ (exp c - 0) by rw [sub_zero]]
  exact tendsto_exp_atBot.const_sub _

theorem integral_exp_Iic_zero : âˆ« x : â„ in Iic 0, exp x = 1 :=
  exp_zero â–¸ integral_exp_Iic 0

theorem integral_exp_neg_Ioi (c : â„) : (âˆ« x : â„ in Ioi c, exp (-x)) = exp (-c) := by
  simpa only [integral_comp_neg_Ioi] using integral_exp_Iic (-c)

theorem integral_exp_neg_Ioi_zero : (âˆ« x : â„ in Ioi 0, exp (-x)) = 1 := by
  simpa only [neg_zero, exp_zero] using integral_exp_neg_Ioi 0

/-- If `0 < c`, then `(fun t : â„ â†¦ t ^ a)` is integrable on `(c, âˆ)` for all `a < -1`. -/
theorem integrableOn_Ioi_rpow_of_lt {a : â„} (ha : a < -1) {c : â„} (hc : 0 < c) :
    IntegrableOn (fun t : â„ => t ^ a) (Ioi c) := by
  have hd : âˆ€ x âˆˆ Ici c, HasDerivAt (fun t => t ^ (a + 1) / (a + 1)) (x ^ a) x := by
    intro x hx
    convert (hasDerivAt_rpow_const (Or.inl (hc.trans_le hx).ne')).div_const _ using 1
    field_simp [show a + 1 â‰  0 from ne_of_lt (by linarith), mul_comm]
  have ht : Tendsto (fun t => t ^ (a + 1) / (a + 1)) atTop (ğ“ (0 / (a + 1))) := by
    apply Tendsto.div_const
    simpa only [neg_neg] using tendsto_rpow_neg_atTop (by linarith : 0 < -(a + 1))
  exact
    integrableOn_Ioi_deriv_of_nonneg' hd (fun t ht => rpow_nonneg (hc.trans ht).le a) ht

theorem integrableOn_Ioi_rpow_iff {s t : â„} (ht : 0 < t) :
    IntegrableOn (fun x â†¦ x ^ s) (Ioi t) â†” s < -1 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ integrableOn_Ioi_rpow_of_lt h htâŸ©
  contrapose! h
  intro H
  have H' : IntegrableOn (fun x â†¦ x ^ s) (Ioi (max 1 t)) :=
    H.mono (Set.Ioi_subset_Ioi (le_max_right _ _)) le_rfl
  have : IntegrableOn (fun x â†¦ xâ»Â¹) (Ioi (max 1 t)) := by
    apply H'.mono' measurable_inv.aestronglyMeasurable
    filter_upwards [ae_restrict_mem measurableSet_Ioi] with x hx
    have x_one : 1 â‰¤ x := ((le_max_left _ _).trans_lt (mem_Ioi.1 hx)).le
    simp only [norm_inv, Real.norm_eq_abs, abs_of_nonneg (zero_le_one.trans x_one)]
    rw [â† Real.rpow_neg_one x]
    exact Real.rpow_le_rpow_of_exponent_le x_one h
  exact not_IntegrableOn_Ioi_inv this

theorem integrableAtFilter_rpow_atTop_iff {s : â„} :
    IntegrableAtFilter (fun x : â„ â†¦ x ^ s) atTop â†” s < -1 := by
  refine âŸ¨fun âŸ¨t, ht, hintâŸ© â†¦ ?_, fun h â†¦
    âŸ¨Set.Ioi 1, Ioi_mem_atTop 1, (integrableOn_Ioi_rpow_iff zero_lt_one).mpr hâŸ©âŸ©
  obtain âŸ¨a, haâŸ© := mem_atTop_sets.mp ht
  refine (integrableOn_Ioi_rpow_iff (zero_lt_one.trans_le (le_max_right a 1))).mp ?_
  exact hint.mono_set <| fun x hx â†¦ ha _ <| (le_max_left a 1).trans hx.le

/-- The real power function with any exponent is not integrable on `(0, +âˆ)`. -/
theorem not_integrableOn_Ioi_rpow (s : â„) : Â¬ IntegrableOn (fun x â†¦ x ^ s) (Ioi (0 : â„)) := by
  intro h
  rcases le_or_lt s (-1) with hs|hs
  Â· have : IntegrableOn (fun x â†¦ x ^ s) (Ioo (0 : â„) 1) := h.mono Ioo_subset_Ioi_self le_rfl
    rw [integrableOn_Ioo_rpow_iff zero_lt_one] at this
    exact hs.not_lt this
  Â· have : IntegrableOn (fun x â†¦ x ^ s) (Ioi (1 : â„)) := h.mono (Ioi_subset_Ioi zero_le_one) le_rfl
    rw [integrableOn_Ioi_rpow_iff zero_lt_one] at this
    exact hs.not_lt this

theorem setIntegral_Ioi_zero_rpow (s : â„) : âˆ« x in Ioi (0 : â„), x ^ s = 0 :=
  MeasureTheory.integral_undef (not_integrableOn_Ioi_rpow s)

theorem integral_Ioi_rpow_of_lt {a : â„} (ha : a < -1) {c : â„} (hc : 0 < c) :
    âˆ« t : â„ in Ioi c, t ^ a = -c ^ (a + 1) / (a + 1) := by
  have hd : âˆ€ x âˆˆ Ici c, HasDerivAt (fun t => t ^ (a + 1) / (a + 1)) (x ^ a) x := by
    intro x hx
    convert (hasDerivAt_rpow_const (p := a + 1) (Or.inl (hc.trans_le hx).ne')).div_const _ using 1
    field_simp [show a + 1 â‰  0 from ne_of_lt (by linarith), mul_comm]
  have ht : Tendsto (fun t => t ^ (a + 1) / (a + 1)) atTop (ğ“ (0 / (a + 1))) := by
    apply Tendsto.div_const
    simpa only [neg_neg] using tendsto_rpow_neg_atTop (by linarith : 0 < -(a + 1))
  convert integral_Ioi_of_hasDerivAt_of_tendsto' hd (integrableOn_Ioi_rpow_of_lt ha hc) ht using 1
  simp only [neg_div, zero_div, zero_sub]

theorem integrableOn_Ioi_norm_cpow_of_lt {a : â„‚} (ha : a.re < -1) {c : â„} (hc : 0 < c) :
    IntegrableOn (fun t : â„ â†¦ â€–(t : â„‚) ^ aâ€–) (Ioi c) := by
  refine (integrableOn_Ioi_rpow_of_lt ha hc).congr_fun (fun x hx => ?_) measurableSet_Ioi
  rw [Complex.norm_cpow_eq_rpow_re_of_pos (hc.trans hx)]

theorem integrableOn_Ioi_cpow_of_lt {a : â„‚} (ha : a.re < -1) {c : â„} (hc : 0 < c) :
    IntegrableOn (fun t : â„ => (t : â„‚) ^ a) (Ioi c) := by
  refine (integrable_norm_iff ?_).mp <| integrableOn_Ioi_norm_cpow_of_lt ha hc
  refine ContinuousOn.aestronglyMeasurable (fun t ht â†¦ ?_) measurableSet_Ioi
  exact (Complex.continuousAt_ofReal_cpow_const _ _ (Or.inr (hc.trans ht).ne')).continuousWithinAt

theorem integrableOn_Ioi_norm_cpow_iff {s : â„‚} {t : â„} (ht : 0 < t) :
    IntegrableOn (fun x : â„ â†¦ â€–(x : â„‚) ^ sâ€–) (Ioi t) â†” s.re < -1 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ integrableOn_Ioi_norm_cpow_of_lt h htâŸ©
  refine (integrableOn_Ioi_rpow_iff ht).mp <| h.congr_fun (fun a ha â†¦ ?_) measurableSet_Ioi
  rw [Complex.norm_cpow_eq_rpow_re_of_pos (ht.trans ha)]

theorem integrableOn_Ioi_cpow_iff {s : â„‚} {t : â„} (ht : 0 < t) :
    IntegrableOn (fun x : â„ â†¦ (x : â„‚) ^ s) (Ioi t) â†” s.re < -1 :=
  âŸ¨fun h â†¦ (integrableOn_Ioi_norm_cpow_iff ht).mp h.norm, fun h â†¦ integrableOn_Ioi_cpow_of_lt h htâŸ©

theorem integrableOn_Ioi_deriv_ofReal_cpow {s : â„‚} {t : â„} (ht : 0 < t) (hs : s.re < 0) :
    IntegrableOn (deriv fun x : â„ â†¦ (x : â„‚) ^ s) (Set.Ioi t) := by
  have h : IntegrableOn (fun x : â„ â†¦ s * x ^ (s - 1)) (Set.Ioi t) := by
    refine (integrableOn_Ioi_cpow_of_lt ?_ ht).const_mul _
    rwa [Complex.sub_re, Complex.one_re, sub_lt_iff_lt_add, neg_add_cancel]
  refine h.congr_fun (fun x hx â†¦ ?_) measurableSet_Ioi
  rw [Complex.deriv_ofReal_cpow_const (ht.trans hx).ne' (fun h â†¦ (Complex.zero_re â–¸ h â–¸ hs).false)]

theorem integrableOn_Ioi_deriv_norm_ofReal_cpow {s : â„‚} {t : â„} (ht : 0 < t) (hs : s.re â‰¤ 0):
    IntegrableOn (deriv fun x : â„ â†¦ â€–(x : â„‚) ^ sâ€–) (Set.Ioi t) := by
  rw [integrableOn_congr_fun (fun x hx â†¦ by
    rw [deriv_norm_ofReal_cpow _ (ht.trans hx)]) measurableSet_Ioi]
  obtain hs | hs := eq_or_lt_of_le hs
  Â· simp_rw [hs, zero_mul]
    exact integrableOn_zero
  Â· replace hs : s.re - 1 < - 1 := by rwa [sub_lt_iff_lt_add, neg_add_cancel]
    exact (integrableOn_Ioi_rpow_of_lt hs ht).const_mul s.re

/-- The complex power function with any exponent is not integrable on `(0, +âˆ)`. -/
theorem not_integrableOn_Ioi_cpow (s : â„‚) :
    Â¬ IntegrableOn (fun x : â„ â†¦ (x : â„‚) ^ s) (Ioi (0 : â„)) := by
  intro h
  rcases le_or_lt s.re (-1) with hs|hs
  Â· have : IntegrableOn (fun x : â„ â†¦ (x : â„‚) ^ s) (Ioo (0 : â„) 1) :=
      h.mono Ioo_subset_Ioi_self le_rfl
    rw [integrableOn_Ioo_cpow_iff zero_lt_one] at this
    exact hs.not_lt this
  Â· have : IntegrableOn (fun x : â„ â†¦ (x : â„‚) ^ s) (Ioi 1) :=
      h.mono (Ioi_subset_Ioi zero_le_one) le_rfl
    rw [integrableOn_Ioi_cpow_iff zero_lt_one] at this
    exact hs.not_lt this

theorem setIntegral_Ioi_zero_cpow (s : â„‚) : âˆ« x in Ioi (0 : â„), (x : â„‚) ^ s = 0 :=
  MeasureTheory.integral_undef (not_integrableOn_Ioi_cpow s)

theorem integral_Ioi_cpow_of_lt {a : â„‚} (ha : a.re < -1) {c : â„} (hc : 0 < c) :
    (âˆ« t : â„ in Ioi c, (t : â„‚) ^ a) = -(c : â„‚) ^ (a + 1) / (a + 1) := by
  refine
    tendsto_nhds_unique
      (intervalIntegral_tendsto_integral_Ioi c (integrableOn_Ioi_cpow_of_lt ha hc) tendsto_id) ?_
  suffices
    Tendsto (fun x : â„ => ((x : â„‚) ^ (a + 1) - (c : â„‚) ^ (a + 1)) / (a + 1)) atTop
      (ğ“ <| -c ^ (a + 1) / (a + 1)) by
    refine this.congr' ((eventually_gt_atTop 0).mp (Eventually.of_forall fun x hx => ?_))
    dsimp only
    rw [integral_cpow, id]
    refine Or.inr âŸ¨?_, not_mem_uIcc_of_lt hc hxâŸ©
    apply_fun Complex.re
    rw [Complex.neg_re, Complex.one_re]
    exact ha.ne
  simp_rw [â† zero_sub, sub_div]
  refine (Tendsto.div_const ?_ _).sub_const _
  rw [tendsto_zero_iff_norm_tendsto_zero]
  refine
    (tendsto_rpow_neg_atTop (by linarith : 0 < -(a.re + 1))).congr'
      ((eventually_gt_atTop 0).mp (Eventually.of_forall fun x hx => ?_))
  simp_rw [neg_neg, Complex.norm_cpow_eq_rpow_re_of_pos hx, Complex.add_re, Complex.one_re]

theorem integrable_inv_one_add_sq : Integrable fun (x : â„) â†¦ (1 + x ^ 2)â»Â¹ := by
  suffices Integrable fun (x : â„) â†¦ (1 + â€–xâ€– ^ 2) ^ ((-2 : â„) / 2) by simpa [rpow_neg_one]
  exact integrable_rpow_neg_one_add_norm_sq (by simp)

@[simp]
theorem integral_Iic_inv_one_add_sq {i : â„} :
    âˆ« (x : â„) in Set.Iic i, (1 + x ^ 2)â»Â¹ = arctan i + (Ï€ / 2) :=
  integral_Iic_of_hasDerivAt_of_tendsto' (fun x _ => hasDerivAt_arctan' x)
    integrable_inv_one_add_sq.integrableOn (tendsto_nhds_of_tendsto_nhdsWithin tendsto_arctan_atBot)
    |>.trans (sub_neg_eq_add _ _)

@[simp]
theorem integral_Ioi_inv_one_add_sq {i : â„} :
    âˆ« (x : â„) in Set.Ioi i, (1 + x ^ 2)â»Â¹ = (Ï€ / 2) - arctan i :=
  integral_Ioi_of_hasDerivAt_of_tendsto' (fun x _ => hasDerivAt_arctan' x)
    integrable_inv_one_add_sq.integrableOn (tendsto_nhds_of_tendsto_nhdsWithin tendsto_arctan_atTop)

@[simp]
theorem integral_univ_inv_one_add_sq : âˆ« (x : â„), (1 + x ^ 2)â»Â¹ = Ï€ :=
  (by ring : Ï€ = (Ï€ / 2) - (-(Ï€ / 2))) â–¸ integral_of_hasDerivAt_of_tendsto hasDerivAt_arctan'
    integrable_inv_one_add_sq (tendsto_nhds_of_tendsto_nhdsWithin tendsto_arctan_atBot)
    (tendsto_nhds_of_tendsto_nhdsWithin tendsto_arctan_atTop)
