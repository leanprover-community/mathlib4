/-
Copyright (c) 2022 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.ArctanDeriv

#align_import analysis.special_functions.trigonometric.bounds from "leanprover-community/mathlib"@"2c1d8ca2812b64f88992a5294ea3dba144755cd1"

/-!
# Polynomial bounds for trigonometric functions

## Main statements

This file contains upper and lower bounds for real trigonometric functions in terms
of polynomials. See `Trigonometric.Basic` for more elementary inequalities, establishing
the ranges of these functions, and their monotonicity in suitable intervals.

Here we prove the following:

* `sin_lt`: for `x > 0` we have `sin x < x`.
* `sin_gt_sub_cube`: For `0 < x ‚â§ 1` we have `x - x ^ 3 / 4 < sin x`.
* `lt_tan`: for `0 < x < œÄ/2` we have `x < tan x`.
* `cos_le_one_div_sqrt_sq_add_one` and `cos_lt_one_div_sqrt_sq_add_one`: for
  `-3 * œÄ / 2 ‚â§ x ‚â§ 3 * œÄ / 2`, we have `cos x ‚â§ 1 / sqrt (x ^ 2 + 1)`, with strict inequality if
  `x ‚â† 0`. (This bound is not quite optimal, but not far off)

## Tags

sin, cos, tan, angle
-/


noncomputable section

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

open Set

namespace Real

open scoped Real

/-- For 0 < x, we have sin x < x. -/
theorem sin_lt {x : ‚Ñù} (h : 0 < x) : sin x < x := by
  cases' lt_or_le 1 x with h' h'
  -- ‚ä¢ sin x < x
  ¬∑ exact (sin_le_one x).trans_lt h'
    -- üéâ no goals
  have hx : |x| = x := abs_of_nonneg h.le
  -- ‚ä¢ sin x < x
  have := le_of_abs_le (sin_bound <| show |x| ‚â§ 1 by rwa [hx])
  -- ‚ä¢ sin x < x
  rw [sub_le_iff_le_add', hx] at this
  -- ‚ä¢ sin x < x
  apply this.trans_lt
  -- ‚ä¢ x - x ^ 3 / 6 + x ^ 4 * (5 / 96) < x
  rw [sub_add, sub_lt_self_iff, sub_pos, div_eq_mul_inv (x ^ 3)]
  -- ‚ä¢ x ^ 4 * (5 / 96) < x ^ 3 * 6‚Åª¬π
  refine' mul_lt_mul' _ (by norm_num) (by norm_num) (pow_pos h 3)
  -- ‚ä¢ x ^ 4 ‚â§ x ^ 3
  apply pow_le_pow_of_le_one h.le h'
  -- ‚ä¢ 3 ‚â§ 4
  norm_num
  -- üéâ no goals
#align real.sin_lt Real.sin_lt

/-- For 0 < x ‚â§ 1 we have x - x ^ 3 / 4 < sin x.

This is also true for x > 1, but it's nontrivial for x just above 1. This inequality is not
tight; the tighter inequality is sin x > x - x ^ 3 / 6 for all x > 0, but this inequality has
a simpler proof. -/
theorem sin_gt_sub_cube {x : ‚Ñù} (h : 0 < x) (h' : x ‚â§ 1) : x - x ^ 3 / 4 < sin x := by
  have hx : |x| = x := abs_of_nonneg h.le
  -- ‚ä¢ x - x ^ 3 / 4 < sin x
  have := neg_le_of_abs_le (sin_bound <| show |x| ‚â§ 1 by rwa [hx])
  -- ‚ä¢ x - x ^ 3 / 4 < sin x
  rw [le_sub_iff_add_le, hx] at this
  -- ‚ä¢ x - x ^ 3 / 4 < sin x
  refine' lt_of_lt_of_le _ this
  -- ‚ä¢ x - x ^ 3 / 4 < -(x ^ 4 * (5 / 96)) + (x - x ^ 3 / 6)
  have : x ^ 3 / ‚Üë4 - x ^ 3 / ‚Üë6 = x ^ 3 * 12‚Åª¬π := by norm_num [div_eq_mul_inv, ‚Üê mul_sub]
  -- ‚ä¢ x - x ^ 3 / 4 < -(x ^ 4 * (5 / 96)) + (x - x ^ 3 / 6)
  rw [add_comm, sub_add, sub_neg_eq_add, sub_lt_sub_iff_left, ‚Üê lt_sub_iff_add_lt', this]
  -- ‚ä¢ x ^ 4 * (5 / 96) < x ^ 3 * 12‚Åª¬π
  refine' mul_lt_mul' _ (by norm_num) (by norm_num) (pow_pos h 3)
  -- ‚ä¢ x ^ 4 ‚â§ x ^ 3
  apply pow_le_pow_of_le_one h.le h'
  -- ‚ä¢ 3 ‚â§ 4
  norm_num
  -- üéâ no goals
#align real.sin_gt_sub_cube Real.sin_gt_sub_cube

/-- The derivative of `tan x - x` is `1/(cos x)^2 - 1` away from the zeroes of cos. -/
theorem deriv_tan_sub_id (x : ‚Ñù) (h : cos x ‚â† 0) :
    deriv (fun y : ‚Ñù => tan y - y) x = 1 / cos x ^ 2 - 1 :=
  HasDerivAt.deriv <| by simpa using (hasDerivAt_tan h).add (hasDerivAt_id x).neg
                         -- üéâ no goals
#align real.deriv_tan_sub_id Real.deriv_tan_sub_id

/-- For all `0 < x < œÄ/2` we have `x < tan x`.

This is proved by checking that the function `tan x - x` vanishes
at zero and has non-negative derivative. -/
theorem lt_tan {x : ‚Ñù} (h1 : 0 < x) (h2 : x < œÄ / 2) : x < tan x := by
  let U := Ico 0 (œÄ / 2)
  -- ‚ä¢ x < tan x
  have intU : interior U = Ioo 0 (œÄ / 2) := interior_Ico
  -- ‚ä¢ x < tan x
  have half_pi_pos : 0 < œÄ / 2 := div_pos pi_pos two_pos
  -- ‚ä¢ x < tan x
  have cos_pos : ‚àÄ {y : ‚Ñù}, y ‚àà U ‚Üí 0 < cos y := by
    intro y hy
    exact cos_pos_of_mem_Ioo (Ico_subset_Ioo_left (neg_lt_zero.mpr half_pi_pos) hy)
  have sin_pos : ‚àÄ {y : ‚Ñù}, y ‚àà interior U ‚Üí 0 < sin y := by
    intro y hy
    rw [intU] at hy
    exact sin_pos_of_mem_Ioo (Ioo_subset_Ioo_right (div_le_self pi_pos.le one_le_two) hy)
  have tan_cts_U : ContinuousOn tan U := by
    apply ContinuousOn.mono continuousOn_tan
    intro z hz
    simp only [mem_setOf_eq]
    exact (cos_pos hz).ne'
  have tan_minus_id_cts : ContinuousOn (fun y : ‚Ñù => tan y - y) U := tan_cts_U.sub continuousOn_id
  -- ‚ä¢ x < tan x
  have deriv_pos : ‚àÄ y : ‚Ñù, y ‚àà interior U ‚Üí 0 < deriv (fun y' : ‚Ñù => tan y' - y') y := by
    intro y hy
    have := cos_pos (interior_subset hy)
    simp only [deriv_tan_sub_id y this.ne', one_div, gt_iff_lt, sub_pos]
    norm_cast
    have bd2 : cos y ^ 2 < 1 := by
      apply lt_of_le_of_ne y.cos_sq_le_one
      rw [cos_sq']
      simpa only [Ne.def, sub_eq_self, pow_eq_zero_iff, Nat.succ_pos'] using (sin_pos hy).ne'
    rwa [one_div, lt_inv, inv_one]
    ¬∑ exact zero_lt_one
    simpa only [sq, mul_self_pos] using this.ne'
  have mono := Convex.strictMonoOn_of_deriv_pos (convex_Ico 0 (œÄ / 2)) tan_minus_id_cts deriv_pos
  -- ‚ä¢ x < tan x
  have zero_in_U : (0 : ‚Ñù) ‚àà U := by rwa [left_mem_Ico]
  -- ‚ä¢ x < tan x
  have x_in_U : x ‚àà U := ‚ü®h1.le, h2‚ü©
  -- ‚ä¢ x < tan x
  simpa only [tan_zero, sub_zero, sub_pos] using mono zero_in_U x_in_U h1
  -- üéâ no goals
#align real.lt_tan Real.lt_tan

theorem le_tan {x : ‚Ñù} (h1 : 0 ‚â§ x) (h2 : x < œÄ / 2) : x ‚â§ tan x := by
  rcases eq_or_lt_of_le h1 with (rfl | h1')
  -- ‚ä¢ 0 ‚â§ tan 0
  ¬∑ rw [tan_zero]
    -- üéâ no goals
  ¬∑ exact le_of_lt (lt_tan h1' h2)
    -- üéâ no goals
#align real.le_tan Real.le_tan

theorem cos_lt_one_div_sqrt_sq_add_one {x : ‚Ñù} (hx1 : -(3 * œÄ / 2) ‚â§ x) (hx2 : x ‚â§ 3 * œÄ / 2)
    (hx3 : x ‚â† 0) : cos x < ‚Üë1 / sqrt (x ^ 2 + 1) := by
  suffices ‚àÄ {y : ‚Ñù} (_ : 0 < y) (_ : y ‚â§ 3 * œÄ / 2), cos y < ‚Üë1 / sqrt (y ^ 2 + 1) by
    rcases lt_or_lt_iff_ne.mpr hx3.symm with ‚ü®h‚ü©
    ¬∑ exact this h hx2
    ¬∑ convert this (by linarith : 0 < -x) (by linarith) using 1
      ¬∑ rw [cos_neg]
      ¬∑ rw [neg_sq]
  intro y hy1 hy2
  -- ‚ä¢ cos y < 1 / sqrt (y ^ 2 + 1)
  have hy3 : ‚Üë0 < y ^ 2 + 1 := by linarith [sq_nonneg y]
  -- ‚ä¢ cos y < 1 / sqrt (y ^ 2 + 1)
  rcases lt_or_le y (œÄ / 2) with (hy2' | hy1')
  -- ‚ä¢ cos y < 1 / sqrt (y ^ 2 + 1)
  ¬∑ -- Main case : `0 < y < œÄ / 2`
    have hy4 : 0 < cos y := cos_pos_of_mem_Ioo ‚ü®by linarith, hy2'‚ü©
    -- ‚ä¢ cos y < 1 / sqrt (y ^ 2 + 1)
    rw [‚Üê abs_of_nonneg (cos_nonneg_of_mem_Icc ‚ü®by linarith, hy2'.le‚ü©), ‚Üê
      abs_of_nonneg (one_div_nonneg.mpr (sqrt_nonneg _)), ‚Üê sq_lt_sq, div_pow, one_pow,
      sq_sqrt hy3.le, lt_one_div (pow_pos hy4 _) hy3, ‚Üê inv_one_add_tan_sq hy4.ne', one_div,
      inv_inv, add_comm, add_lt_add_iff_left, sq_lt_sq, abs_of_pos hy1,
      abs_of_nonneg (tan_nonneg_of_nonneg_of_le_pi_div_two hy1.le hy2'.le)]
    exact Real.lt_tan hy1 hy2'
    -- üéâ no goals
  ¬∑ -- Easy case : `œÄ / 2 ‚â§ y ‚â§ 3 * œÄ / 2`
    refine' lt_of_le_of_lt _ (one_div_pos.mpr <| sqrt_pos_of_pos hy3)
    -- ‚ä¢ cos y ‚â§ 0
    exact cos_nonpos_of_pi_div_two_le_of_le hy1' (by linarith [pi_pos])
    -- üéâ no goals
#align real.cos_lt_one_div_sqrt_sq_add_one Real.cos_lt_one_div_sqrt_sq_add_one

theorem cos_le_one_div_sqrt_sq_add_one {x : ‚Ñù} (hx1 : -(3 * œÄ / 2) ‚â§ x) (hx2 : x ‚â§ 3 * œÄ / 2) :
    cos x ‚â§ ‚Üë1 / sqrt (x ^ 2 + 1) := by
  rcases eq_or_ne x 0 with (rfl | hx3)
  -- ‚ä¢ cos 0 ‚â§ 1 / sqrt (0 ^ 2 + 1)
  ¬∑ simp
    -- üéâ no goals
  ¬∑ exact (cos_lt_one_div_sqrt_sq_add_one hx1 hx2 hx3).le
    -- üéâ no goals
#align real.cos_le_one_div_sqrt_sq_add_one Real.cos_le_one_div_sqrt_sq_add_one

end Real
