/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Arctan
import Mathlib.Analysis.SpecialFunctions.Trigonometric.ComplexDeriv

/-!
# Derivatives of the `tan` and `arctan` functions.

Continuity and derivatives of the tangent and arctangent functions.
-/


noncomputable section

namespace Real

open Set Filter

open scoped Topology Real

theorem hasStrictDerivAt_tan {x : ‚Ñù} (h : cos x ‚â† 0) : HasStrictDerivAt tan (1 / cos x ^ 2) x :=
  mod_cast (Complex.hasStrictDerivAt_tan (by exact mod_cast h)).real_of_complex

theorem hasDerivAt_tan {x : ‚Ñù} (h : cos x ‚â† 0) : HasDerivAt tan (1 / cos x ^ 2) x :=
  mod_cast (Complex.hasDerivAt_tan (by exact mod_cast h)).real_of_complex

theorem tendsto_abs_tan_of_cos_eq_zero {x : ‚Ñù} (hx : cos x = 0) :
    Tendsto (fun x ‚Ü¶ abs (tan x)) (ùìù[‚â†] x) atTop := by
  have hx : Complex.cos x = 0 := mod_cast hx
  simp only [‚Üê Real.norm_eq_abs, ‚Üê Complex.norm_real, Complex.ofReal_tan]
  refine (Complex.tendsto_norm_tan_of_cos_eq_zero hx).comp ?_
  refine Tendsto.inf Complex.continuous_ofReal.continuousAt ?_
  exact tendsto_principal_principal.2 fun y ‚Ü¶ mt Complex.ofReal_inj.1

theorem tendsto_abs_tan_atTop (k : ‚Ñ§) :
    Tendsto (fun x ‚Ü¶ abs (tan x)) (ùìù[‚â†] ((2 * k + 1) * œÄ / 2)) atTop :=
  tendsto_abs_tan_of_cos_eq_zero <| cos_eq_zero_iff.2 ‚ü®k, rfl‚ü©

theorem continuousAt_tan {x : ‚Ñù} : ContinuousAt tan x ‚Üî cos x ‚â† 0 := by
  refine ‚ü®fun hc h‚ÇÄ ‚Ü¶ ?_, fun h ‚Ü¶ (hasDerivAt_tan h).continuousAt‚ü©
  exact not_tendsto_nhds_of_tendsto_atTop (tendsto_abs_tan_of_cos_eq_zero h‚ÇÄ) _
    (hc.norm.tendsto.mono_left inf_le_left)

theorem differentiableAt_tan {x : ‚Ñù} : DifferentiableAt ‚Ñù tan x ‚Üî cos x ‚â† 0 :=
  ‚ü®fun h ‚Ü¶ continuousAt_tan.1 h.continuousAt, fun h ‚Ü¶ (hasDerivAt_tan h).differentiableAt‚ü©

@[simp]
theorem deriv_tan (x : ‚Ñù) : deriv tan x = 1 / cos x ^ 2 :=
  if h : cos x = 0 then by
    have : ¬¨DifferentiableAt ‚Ñù tan x := mt differentiableAt_tan.1 (Classical.not_not.2 h)
    simp [deriv_zero_of_not_differentiableAt this, h, sq]
  else (hasDerivAt_tan h).deriv

@[simp]
theorem contDiffAt_tan {n : WithTop ‚Ñï‚àû} {x : ‚Ñù} : ContDiffAt ‚Ñù n tan x ‚Üî cos x ‚â† 0 :=
  ‚ü®fun h ‚Ü¶ continuousAt_tan.1 h.continuousAt, fun h ‚Ü¶
    (Complex.contDiffAt_tan.2 <| mod_cast h).real_of_complex‚ü©

theorem hasDerivAt_tan_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-(œÄ / 2) : ‚Ñù) (œÄ / 2)) :
    HasDerivAt tan (1 / cos x ^ 2) x :=
  hasDerivAt_tan (cos_pos_of_mem_Ioo h).ne'

theorem differentiableAt_tan_of_mem_Ioo {x : ‚Ñù} (h : x ‚àà Ioo (-(œÄ / 2) : ‚Ñù) (œÄ / 2)) :
    DifferentiableAt ‚Ñù tan x :=
  (hasDerivAt_tan_of_mem_Ioo h).differentiableAt

theorem hasStrictDerivAt_arctan (x : ‚Ñù) : HasStrictDerivAt arctan (1 / (1 + x ^ 2)) x := by
  have A : cos (arctan x) ‚â† 0 := (cos_arctan_pos x).ne'
  simpa [cos_sq_arctan] using
    tanPartialHomeomorph.hasStrictDerivAt_symm trivial (by simpa) (hasStrictDerivAt_tan A)

theorem hasDerivAt_arctan (x : ‚Ñù) : HasDerivAt arctan (1 / (1 + x ^ 2)) x :=
  (hasStrictDerivAt_arctan x).hasDerivAt

theorem hasDerivAt_arctan' (x : ‚Ñù) : HasDerivAt arctan (1 + x ^ 2)‚Åª¬π x :=
  one_div (1 + x ^ 2) ‚ñ∏ hasDerivAt_arctan x

theorem differentiableAt_arctan (x : ‚Ñù) : DifferentiableAt ‚Ñù arctan x :=
  (hasDerivAt_arctan x).differentiableAt

theorem differentiable_arctan : Differentiable ‚Ñù arctan :=
  differentiableAt_arctan

@[simp]
theorem deriv_arctan : deriv arctan = fun (x : ‚Ñù) ‚Ü¶ 1 / (1 + x ^ 2) :=
  funext fun x ‚Ü¶ (hasDerivAt_arctan x).deriv

theorem contDiff_arctan {n : WithTop ‚Ñï‚àû} : ContDiff ‚Ñù n arctan :=
  contDiff_iff_contDiffAt.2 fun x ‚Ü¶
    have : cos (arctan x) ‚â† 0 := (cos_arctan_pos x).ne'
    tanPartialHomeomorph.contDiffAt_symm_deriv (by simpa) trivial (hasDerivAt_tan this)
      (contDiffAt_tan.2 this)

end Real

section

/-!
### Lemmas for derivatives of the composition of `Real.arctan` with a differentiable function

In this section we register lemmas for the derivatives of the composition of `Real.arctan` with a
differentiable function, for standalone use and use with `simp`. -/


open Real

section deriv

variable {f : ‚Ñù ‚Üí ‚Ñù} {f' x : ‚Ñù} {s : Set ‚Ñù}

theorem HasStrictDerivAt.arctan (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x ‚Ü¶ arctan (f x)) (1 / (1 + f x ^ 2) * f') x :=
  (Real.hasStrictDerivAt_arctan (f x)).comp x hf

theorem HasDerivAt.arctan (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x ‚Ü¶ arctan (f x)) (1 / (1 + f x ^ 2) * f') x :=
  (Real.hasDerivAt_arctan (f x)).comp x hf

theorem HasDerivWithinAt.arctan (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x ‚Ü¶ arctan (f x)) (1 / (1 + f x ^ 2) * f') s x :=
  (Real.hasDerivAt_arctan (f x)).comp_hasDerivWithinAt x hf

theorem derivWithin_arctan (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x ‚Ü¶ arctan (f x)) s x = 1 / (1 + f x ^ 2) * derivWithin f s x :=
  hf.hasDerivWithinAt.arctan.derivWithin hxs

@[simp]
theorem deriv_arctan (hc : DifferentiableAt ‚Ñù f x) :
    deriv (fun x ‚Ü¶ arctan (f x)) x = 1 / (1 + f x ^ 2) * deriv f x :=
  hc.hasDerivAt.arctan.deriv

end deriv

section fderiv

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚Ñù} {f' : E ‚ÜíL[‚Ñù] ‚Ñù} {x : E}
  {s : Set E} {n : ‚Ñï‚àû}

theorem HasStrictFDerivAt.arctan (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x ‚Ü¶ arctan (f x)) ((1 / (1 + f x ^ 2)) ‚Ä¢ f') x :=
  (hasStrictDerivAt_arctan (f x)).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.arctan (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun x ‚Ü¶ arctan (f x)) ((1 / (1 + f x ^ 2)) ‚Ä¢ f') x :=
  (hasDerivAt_arctan (f x)).comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.arctan (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x ‚Ü¶ arctan (f x)) ((1 / (1 + f x ^ 2)) ‚Ä¢ f') s x :=
  (hasDerivAt_arctan (f x)).comp_hasFDerivWithinAt x hf

theorem fderivWithin_arctan (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    fderivWithin ‚Ñù (fun x ‚Ü¶ arctan (f x)) s x = (1 / (1 + f x ^ 2)) ‚Ä¢ fderivWithin ‚Ñù f s x :=
  hf.hasFDerivWithinAt.arctan.fderivWithin hxs

@[simp]
theorem fderiv_arctan (hc : DifferentiableAt ‚Ñù f x) :
    fderiv ‚Ñù (fun x ‚Ü¶ arctan (f x)) x = (1 / (1 + f x ^ 2)) ‚Ä¢ fderiv ‚Ñù f x :=
  hc.hasFDerivAt.arctan.fderiv

theorem DifferentiableWithinAt.arctan (hf : DifferentiableWithinAt ‚Ñù f s x) :
    DifferentiableWithinAt ‚Ñù (fun x ‚Ü¶ Real.arctan (f x)) s x :=
  hf.hasFDerivWithinAt.arctan.differentiableWithinAt

@[simp]
theorem DifferentiableAt.arctan (hc : DifferentiableAt ‚Ñù f x) :
    DifferentiableAt ‚Ñù (fun x ‚Ü¶ arctan (f x)) x :=
  hc.hasFDerivAt.arctan.differentiableAt

theorem DifferentiableOn.arctan (hc : DifferentiableOn ‚Ñù f s) :
    DifferentiableOn ‚Ñù (fun x ‚Ü¶ arctan (f x)) s := fun x h ‚Ü¶ (hc x h).arctan

@[simp]
theorem Differentiable.arctan (hc : Differentiable ‚Ñù f) : Differentiable ‚Ñù fun x ‚Ü¶ arctan (f x) :=
  fun x ‚Ü¶ (hc x).arctan

theorem ContDiffAt.arctan (h : ContDiffAt ‚Ñù n f x) : ContDiffAt ‚Ñù n (fun x ‚Ü¶ arctan (f x)) x :=
  contDiff_arctan.contDiffAt.comp x h

theorem ContDiff.arctan (h : ContDiff ‚Ñù n f) : ContDiff ‚Ñù n fun x ‚Ü¶ arctan (f x) :=
  contDiff_arctan.comp h

theorem ContDiffWithinAt.arctan (h : ContDiffWithinAt ‚Ñù n f s x) :
    ContDiffWithinAt ‚Ñù n (fun x ‚Ü¶ arctan (f x)) s x :=
  contDiff_arctan.comp_contDiffWithinAt h

theorem ContDiffOn.arctan (h : ContDiffOn ‚Ñù n f s) : ContDiffOn ‚Ñù n (fun x ‚Ü¶ arctan (f x)) s :=
  contDiff_arctan.comp_contDiffOn h

end fderiv

end
