/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Complex

#align_import analysis.special_functions.trigonometric.arctan from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# The `arctan` function.

Inequalities, identities and `Real.tan` as a `PartialHomeomorph` between `(-(Ï€ / 2), Ï€ / 2)`
and the whole line.

The result of `arctan x + arctan y` is given by `arctan_add`, `arctan_add_eq_add_pi` or
`arctan_add_eq_sub_pi` depending on whether `x * y < 1` and `0 < x`. As an application of
`arctan_add` we give four Machin-like formulas (linear combinations of arctangents equal to
`Ï€ / 4 = arctan 1`), including John Machin's original one at
`four_mul_arctan_inv_5_sub_arctan_inv_239`.
-/


noncomputable section

namespace Real

open Set Filter

open scoped Topology Real

theorem tan_add {x y : â„}
    (h : ((âˆ€ k : â„¤, x â‰  (2 * k + 1) * Ï€ / 2) âˆ§ âˆ€ l : â„¤, y â‰  (2 * l + 1) * Ï€ / 2) âˆ¨
      (âˆƒ k : â„¤, x = (2 * k + 1) * Ï€ / 2) âˆ§ âˆƒ l : â„¤, y = (2 * l + 1) * Ï€ / 2) :
    tan (x + y) = (tan x + tan y) / (1 - tan x * tan y) := by
  simpa only [â† Complex.ofReal_inj, Complex.ofReal_sub, Complex.ofReal_add, Complex.ofReal_div,
    Complex.ofReal_mul, Complex.ofReal_tan] using
    @Complex.tan_add (x : â„‚) (y : â„‚) (by convert h <;> norm_cast)
#align real.tan_add Real.tan_add

theorem tan_add' {x y : â„}
    (h : (âˆ€ k : â„¤, x â‰  (2 * k + 1) * Ï€ / 2) âˆ§ âˆ€ l : â„¤, y â‰  (2 * l + 1) * Ï€ / 2) :
    tan (x + y) = (tan x + tan y) / (1 - tan x * tan y) :=
  tan_add (Or.inl h)
#align real.tan_add' Real.tan_add'

theorem tan_two_mul {x : â„} : tan (2 * x) = 2 * tan x / (1 - tan x ^ 2) := by
  have := @Complex.tan_two_mul x
  norm_cast at *
#align real.tan_two_mul Real.tan_two_mul

theorem tan_int_mul_pi_div_two (n : â„¤) : tan (n * Ï€ / 2) = 0 :=
  tan_eq_zero_iff.mpr (by use n)
#align real.tan_int_mul_pi_div_two Real.tan_int_mul_pi_div_two

theorem continuousOn_tan : ContinuousOn tan {x | cos x â‰  0} := by
  suffices ContinuousOn (fun x => sin x / cos x) {x | cos x â‰  0} by
    have h_eq : (fun x => sin x / cos x) = tan := by ext1 x; rw [tan_eq_sin_div_cos]
    rwa [h_eq] at this
  exact continuousOn_sin.div continuousOn_cos fun x => id
#align real.continuous_on_tan Real.continuousOn_tan

@[continuity]
theorem continuous_tan : Continuous fun x : {x | cos x â‰  0} => tan x :=
  continuousOn_iff_continuous_restrict.1 continuousOn_tan
#align real.continuous_tan Real.continuous_tan

theorem continuousOn_tan_Ioo : ContinuousOn tan (Ioo (-(Ï€ / 2)) (Ï€ / 2)) := by
  refine ContinuousOn.mono continuousOn_tan fun x => ?_
  simp only [and_imp, mem_Ioo, mem_setOf_eq, Ne]
  rw [cos_eq_zero_iff]
  rintro hx_gt hx_lt âŸ¨r, hxr_eqâŸ©
  rcases le_or_lt 0 r with h | h
  Â· rw [lt_iff_not_ge] at hx_lt
    refine hx_lt ?_
    rw [hxr_eq, â† one_mul (Ï€ / 2), mul_div_assoc, ge_iff_le, mul_le_mul_right (half_pos pi_pos)]
    simp [h]
  Â· rw [lt_iff_not_ge] at hx_gt
    refine hx_gt ?_
    rw [hxr_eq, â† one_mul (Ï€ / 2), mul_div_assoc, ge_iff_le, neg_mul_eq_neg_mul,
      mul_le_mul_right (half_pos pi_pos)]
    have hr_le : r â‰¤ -1 := by rwa [Int.lt_iff_add_one_le, â† le_neg_iff_add_nonpos_right] at h
    rw [â† le_sub_iff_add_le, mul_comm, â† le_div_iff]
    Â· set_option tactic.skipAssignedInstances false in norm_num
      rw [â† Int.cast_one, â† Int.cast_neg]; norm_cast
    Â· exact zero_lt_two
#align real.continuous_on_tan_Ioo Real.continuousOn_tan_Ioo

theorem surjOn_tan : SurjOn tan (Ioo (-(Ï€ / 2)) (Ï€ / 2)) univ :=
  have := neg_lt_self pi_div_two_pos
  continuousOn_tan_Ioo.surjOn_of_tendsto (nonempty_Ioo.2 this)
    (by rw [tendsto_comp_coe_Ioo_atBot this]; exact tendsto_tan_neg_pi_div_two)
    (by rw [tendsto_comp_coe_Ioo_atTop this]; exact tendsto_tan_pi_div_two)
#align real.surj_on_tan Real.surjOn_tan

theorem tan_surjective : Function.Surjective tan := fun _ => surjOn_tan.subset_range trivial
#align real.tan_surjective Real.tan_surjective

theorem image_tan_Ioo : tan '' Ioo (-(Ï€ / 2)) (Ï€ / 2) = univ :=
  univ_subset_iff.1 surjOn_tan
#align real.image_tan_Ioo Real.image_tan_Ioo

/-- `Real.tan` as an `OrderIso` between `(-(Ï€ / 2), Ï€ / 2)` and `â„`. -/
def tanOrderIso : Ioo (-(Ï€ / 2)) (Ï€ / 2) â‰ƒo â„ :=
  (strictMonoOn_tan.orderIso _ _).trans <|
    (OrderIso.setCongr _ _ image_tan_Ioo).trans OrderIso.Set.univ
#align real.tan_order_iso Real.tanOrderIso

/-- Inverse of the `tan` function, returns values in the range `-Ï€ / 2 < arctan x` and
`arctan x < Ï€ / 2` -/
-- @[pp_nodot] -- Porting note: removed
noncomputable def arctan (x : â„) : â„ :=
  tanOrderIso.symm x
#align real.arctan Real.arctan

@[simp]
theorem tan_arctan (x : â„) : tan (arctan x) = x :=
  tanOrderIso.apply_symm_apply x
#align real.tan_arctan Real.tan_arctan

theorem arctan_mem_Ioo (x : â„) : arctan x âˆˆ Ioo (-(Ï€ / 2)) (Ï€ / 2) :=
  Subtype.coe_prop _
#align real.arctan_mem_Ioo Real.arctan_mem_Ioo

@[simp]
theorem range_arctan : range arctan = Ioo (-(Ï€ / 2)) (Ï€ / 2) :=
  ((EquivLike.surjective _).range_comp _).trans Subtype.range_coe
#align real.range_arctan Real.range_arctan

theorem arctan_tan {x : â„} (hxâ‚ : -(Ï€ / 2) < x) (hxâ‚‚ : x < Ï€ / 2) : arctan (tan x) = x :=
  Subtype.ext_iff.1 <| tanOrderIso.symm_apply_apply âŸ¨x, hxâ‚, hxâ‚‚âŸ©
#align real.arctan_tan Real.arctan_tan

theorem cos_arctan_pos (x : â„) : 0 < cos (arctan x) :=
  cos_pos_of_mem_Ioo <| arctan_mem_Ioo x
#align real.cos_arctan_pos Real.cos_arctan_pos

theorem cos_sq_arctan (x : â„) : cos (arctan x) ^ 2 = 1 / (1 + x ^ 2) := by
  rw_mod_cast [one_div, â† inv_one_add_tan_sq (cos_arctan_pos x).ne', tan_arctan]
#align real.cos_sq_arctan Real.cos_sq_arctan

theorem sin_arctan (x : â„) : sin (arctan x) = x / âˆš(1 + x ^ 2) := by
  rw_mod_cast [â† tan_div_sqrt_one_add_tan_sq (cos_arctan_pos x), tan_arctan]
#align real.sin_arctan Real.sin_arctan

theorem cos_arctan (x : â„) : cos (arctan x) = 1 / âˆš(1 + x ^ 2) := by
  rw_mod_cast [one_div, â† inv_sqrt_one_add_tan_sq (cos_arctan_pos x), tan_arctan]
#align real.cos_arctan Real.cos_arctan

theorem arctan_lt_pi_div_two (x : â„) : arctan x < Ï€ / 2 :=
  (arctan_mem_Ioo x).2
#align real.arctan_lt_pi_div_two Real.arctan_lt_pi_div_two

theorem neg_pi_div_two_lt_arctan (x : â„) : -(Ï€ / 2) < arctan x :=
  (arctan_mem_Ioo x).1
#align real.neg_pi_div_two_lt_arctan Real.neg_pi_div_two_lt_arctan

theorem arctan_eq_arcsin (x : â„) : arctan x = arcsin (x / âˆš(1 + x ^ 2)) :=
  Eq.symm <| arcsin_eq_of_sin_eq (sin_arctan x) (mem_Icc_of_Ioo <| arctan_mem_Ioo x)
#align real.arctan_eq_arcsin Real.arctan_eq_arcsin

theorem arcsin_eq_arctan {x : â„} (h : x âˆˆ Ioo (-(1 : â„)) 1) :
    arcsin x = arctan (x / âˆš(1 - x ^ 2)) := by
  rw_mod_cast [arctan_eq_arcsin, div_pow, sq_sqrt, one_add_div, div_div, â† sqrt_mul,
    mul_div_cancelâ‚€, sub_add_cancel, sqrt_one, div_one] <;> simp at h <;> nlinarith [h.1, h.2]
#align real.arcsin_eq_arctan Real.arcsin_eq_arctan

@[simp]
theorem arctan_zero : arctan 0 = 0 := by simp [arctan_eq_arcsin]
#align real.arctan_zero Real.arctan_zero

@[mono]
theorem arctan_strictMono : StrictMono arctan := tanOrderIso.symm.strictMono

theorem arctan_injective : arctan.Injective := arctan_strictMono.injective

@[simp]
theorem arctan_eq_zero_iff {x : â„} : arctan x = 0 â†” x = 0 :=
  .trans (by rw [arctan_zero]) arctan_injective.eq_iff

theorem tendsto_arctan_atTop : Tendsto arctan atTop (ğ“[<] (Ï€ / 2)) :=
  tendsto_Ioo_atTop.mp tanOrderIso.symm.tendsto_atTop

theorem tendsto_arctan_atBot : Tendsto arctan atBot (ğ“[>] (-(Ï€ / 2))) :=
  tendsto_Ioo_atBot.mp tanOrderIso.symm.tendsto_atBot

theorem arctan_eq_of_tan_eq {x y : â„} (h : tan x = y) (hx : x âˆˆ Ioo (-(Ï€ / 2)) (Ï€ / 2)) :
    arctan y = x :=
  injOn_tan (arctan_mem_Ioo _) hx (by rw [tan_arctan, h])
#align real.arctan_eq_of_tan_eq Real.arctan_eq_of_tan_eq

@[simp]
theorem arctan_one : arctan 1 = Ï€ / 4 :=
  arctan_eq_of_tan_eq tan_pi_div_four <| by constructor <;> linarith [pi_pos]
#align real.arctan_one Real.arctan_one

@[simp]
theorem arctan_neg (x : â„) : arctan (-x) = -arctan x := by simp [arctan_eq_arcsin, neg_div]
#align real.arctan_neg Real.arctan_neg

theorem arctan_eq_arccos {x : â„} (h : 0 â‰¤ x) : arctan x = arccos (âˆš(1 + x ^ 2))â»Â¹ := by
  rw [arctan_eq_arcsin, arccos_eq_arcsin]; swap; Â· exact inv_nonneg.2 (sqrt_nonneg _)
  congr 1
  rw_mod_cast [â† sqrt_inv, sq_sqrt, â† one_div, one_sub_div, add_sub_cancel_left, sqrt_div,
    sqrt_sq h]
  all_goals positivity
#align real.arctan_eq_arccos Real.arctan_eq_arccos

-- The junk values for `arccos` and `sqrt` make this true even for `1 < x`.
theorem arccos_eq_arctan {x : â„} (h : 0 < x) : arccos x = arctan (âˆš(1 - x ^ 2) / x) := by
  rw [arccos, eq_comm]
  refine arctan_eq_of_tan_eq ?_ âŸ¨?_, ?_âŸ©
  Â· rw_mod_cast [tan_pi_div_two_sub, tan_arcsin, inv_div]
  Â· linarith only [arcsin_le_pi_div_two x, pi_pos]
  Â· linarith only [arcsin_pos.2 h]
#align real.arccos_eq_arctan Real.arccos_eq_arctan

theorem arctan_inv_of_pos {x : â„} (h : 0 < x) : arctan xâ»Â¹ = Ï€ / 2 - arctan x := by
  rw [â† arctan_tan (x := _ - _), tan_pi_div_two_sub, tan_arctan]
  Â· norm_num
    exact (arctan_lt_pi_div_two x).trans (half_lt_self_iff.mpr pi_pos)
  Â· rw [sub_lt_self_iff, â† arctan_zero]
    exact tanOrderIso.symm.strictMono h

theorem arctan_inv_of_neg {x : â„} (h : x < 0) : arctan xâ»Â¹ = -(Ï€ / 2) - arctan x := by
  have := arctan_inv_of_pos (neg_pos.mpr h)
  rwa [inv_neg, arctan_neg, neg_eq_iff_eq_neg, neg_sub', arctan_neg, neg_neg] at this

section ArctanAdd

lemma arctan_ne_mul_pi_div_two {x : â„} : âˆ€ (k : â„¤), arctan x â‰  (2 * k + 1) * Ï€ / 2 := by
  by_contra!
  obtain âŸ¨k, hâŸ© := this
  obtain âŸ¨lb, ubâŸ© := arctan_mem_Ioo x
  rw [h, neg_eq_neg_one_mul, mul_div_assoc, mul_lt_mul_right (by positivity)] at lb
  rw [h, â† one_mul (Ï€ / 2), mul_div_assoc, mul_lt_mul_right (by positivity)] at ub
  norm_cast at lb ub; change -1 < _ at lb; omega

lemma arctan_add_arctan_lt_pi_div_two {x y : â„} (h : x * y < 1) : arctan x + arctan y < Ï€ / 2 := by
  cases' le_or_lt y 0 with hy hy
  Â· rw [â† add_zero (Ï€ / 2), â† arctan_zero]
    exact add_lt_add_of_lt_of_le (arctan_lt_pi_div_two _) (tanOrderIso.symm.monotone hy)
  Â· rw [â† lt_div_iff hy, â† inv_eq_one_div] at h
    replace h : arctan x < arctan yâ»Â¹ := tanOrderIso.symm.strictMono h
    rwa [arctan_inv_of_pos hy, lt_tsub_iff_right] at h

theorem arctan_add {x y : â„} (h : x * y < 1) :
    arctan x + arctan y = arctan ((x + y) / (1 - x * y)) := by
  rw [â† arctan_tan (x := _ + _)]
  Â· congr
    conv_rhs => rw [â† tan_arctan x, â† tan_arctan y]
    exact tan_add' âŸ¨arctan_ne_mul_pi_div_two, arctan_ne_mul_pi_div_twoâŸ©
  Â· rw [neg_lt, neg_add, â† arctan_neg, â† arctan_neg]
    rw [â† neg_mul_neg] at h
    exact arctan_add_arctan_lt_pi_div_two h
  Â· exact arctan_add_arctan_lt_pi_div_two h

theorem arctan_add_eq_add_pi {x y : â„} (h : 1 < x * y) (hx : 0 < x) :
    arctan x + arctan y = arctan ((x + y) / (1 - x * y)) + Ï€ := by
  have hy : 0 < y := by
    have := mul_pos_iff.mp (zero_lt_one.trans h)
    simpa [hx, hx.asymm]
  have k := arctan_add (mul_inv x y â–¸ inv_lt_one h)
  rw [arctan_inv_of_pos hx, arctan_inv_of_pos hy, show _ + _ = Ï€ - (arctan x + arctan y) by ring,
    sub_eq_iff_eq_add, â† sub_eq_iff_eq_add', sub_eq_add_neg, â† arctan_neg, add_comm] at k
  convert k.symm using 3
  field_simp
  rw [show -x + -y = -(x + y) by ring, show x * y - 1 = -(1 - x * y) by ring, neg_div_neg_eq]

theorem arctan_add_eq_sub_pi {x y : â„} (h : 1 < x * y) (hx : x < 0) :
    arctan x + arctan y = arctan ((x + y) / (1 - x * y)) - Ï€ := by
  rw [â† neg_mul_neg] at h
  have k := arctan_add_eq_add_pi h (neg_pos.mpr hx)
  rw [show _ / _ = -((x + y) / (1 - x * y)) by ring, â† neg_inj] at k
  simp only [arctan_neg, neg_add, neg_neg, â† sub_eq_add_neg _ Ï€] at k
  exact k

theorem two_mul_arctan {x : â„} (hâ‚ : -1 < x) (hâ‚‚ : x < 1) :
    2 * arctan x = arctan (2 * x / (1 - x ^ 2)) := by
  rw [two_mul, arctan_add (by nlinarith)]; congr 1; ring

theorem two_mul_arctan_add_pi {x : â„} (h : 1 < x) :
    2 * arctan x = arctan (2 * x / (1 - x ^ 2)) + Ï€ := by
  rw [two_mul, arctan_add_eq_add_pi (by nlinarith) (by linarith)]; congr 2; ring

theorem two_mul_arctan_sub_pi {x : â„} (h : x < -1) :
    2 * arctan x = arctan (2 * x / (1 - x ^ 2)) - Ï€ := by
  rw [two_mul, arctan_add_eq_sub_pi (by nlinarith) (by linarith)]; congr 2; ring

theorem arctan_inv_2_add_arctan_inv_3 : arctan 2â»Â¹ + arctan 3â»Â¹ = Ï€ / 4 := by
  rw [arctan_add] <;> norm_num

theorem two_mul_arctan_inv_2_sub_arctan_inv_7 : 2 * arctan 2â»Â¹ - arctan 7â»Â¹ = Ï€ / 4 := by
  rw [two_mul_arctan, â† arctan_one, sub_eq_iff_eq_add, arctan_add] <;> norm_num

theorem two_mul_arctan_inv_3_add_arctan_inv_7 : 2 * arctan 3â»Â¹ + arctan 7â»Â¹ = Ï€ / 4 := by
  rw [two_mul_arctan, arctan_add] <;> norm_num

/-- **John Machin's 1706 formula**, which he used to compute Ï€ to 100 decimal places. -/
theorem four_mul_arctan_inv_5_sub_arctan_inv_239 : 4 * arctan 5â»Â¹ - arctan 239â»Â¹ = Ï€ / 4 := by
  rw [show 4 * arctan _ = 2 * (2 * _) by ring, two_mul_arctan, two_mul_arctan, â† arctan_one,
    sub_eq_iff_eq_add, arctan_add] <;> norm_num

end ArctanAdd

@[continuity]
theorem continuous_arctan : Continuous arctan :=
  continuous_subtype_val.comp tanOrderIso.toHomeomorph.continuous_invFun
#align real.continuous_arctan Real.continuous_arctan

theorem continuousAt_arctan {x : â„} : ContinuousAt arctan x :=
  continuous_arctan.continuousAt
#align real.continuous_at_arctan Real.continuousAt_arctan

/-- `Real.tan` as a `PartialHomeomorph` between `(-(Ï€ / 2), Ï€ / 2)` and the whole line. -/
def tanPartialHomeomorph : PartialHomeomorph â„ â„ where
  toFun := tan
  invFun := arctan
  source := Ioo (-(Ï€ / 2)) (Ï€ / 2)
  target := univ
  map_source' := mapsTo_univ _ _
  map_target' y _ := arctan_mem_Ioo y
  left_inv' _ hx := arctan_tan hx.1 hx.2
  right_inv' y _ := tan_arctan y
  open_source := isOpen_Ioo
  open_target := isOpen_univ
  continuousOn_toFun := continuousOn_tan_Ioo
  continuousOn_invFun := continuous_arctan.continuousOn
#align real.tan_local_homeomorph Real.tanPartialHomeomorph

@[simp]
theorem coe_tanPartialHomeomorph : â‡‘tanPartialHomeomorph = tan :=
  rfl
#align real.coe_tan_local_homeomorph Real.coe_tanPartialHomeomorph

@[simp]
theorem coe_tanPartialHomeomorph_symm : â‡‘tanPartialHomeomorph.symm = arctan :=
  rfl
#align real.coe_tan_local_homeomorph_symm Real.coe_tanPartialHomeomorph_symm

end Real
