/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Calculus.IteratedDeriv.WithinZpow
import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
import Mathlib.Analysis.Complex.IntegerCompl
import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Analysis.PSeries
import Mathlib.Analysis.SpecialFunctions.Trigonometric.EulerSineProd
import Mathlib.Analysis.NormedSpace.MultipliableUniformlyOn
import Mathlib.NumberTheory.ModularForms.EisensteinSeries.Summable
import Mathlib.Topology.Algebra.InfiniteSum.TsumUniformlyOn

/-!
# Cotangent

This file contains lemmas about the cotangent function, including useful series expansions.
In particular, we prove that
`œÄ * cot (œÄ * z) = œÄ * I - 2 * œÄ * I * ‚àë' n : ‚Ñï, Complex.exp (2 * œÄ * I * z) ^ n`
as well as the infinite sum representation of cotangent (also known as the Mittag-Leffler
expansion): `œÄ * cot (œÄ * z) = 1 / z + ‚àë' n : ‚Ñï+, (1 / (z - n) + 1 / (z + n))`.
-/

open Real Complex

open scoped UpperHalfPlane

local notation "‚ÑÇ_‚Ñ§" => integerComplement

local notation "‚Ñç‚Çí" => UpperHalfPlane.upperHalfPlaneSet

lemma Complex.cot_eq_exp_ratio (z : ‚ÑÇ) :
    cot z = (Complex.exp (2 * I * z) + 1) / (I * (1 - Complex.exp (2 * I * z))) := by
  rw [Complex.cot, Complex.sin, Complex.cos]
  have h1 : exp (z * I) + exp (-z * I) = exp (-(z * I)) * (exp (2 * I * z) + 1) := by
    rw [mul_add, ‚Üê Complex.exp_add]
    simp only [mul_one]
    ring_nf
  have h2 : (exp (-z * I) - exp (z * I)) = exp (-(z * I)) * ((1 - exp (2 * I * z))) := by
    ring_nf
    rw [mul_assoc, ‚Üê Complex.exp_add]
    ring_nf
  rw [h1, h2]
  field_simp

/- The version one probably wants to use more. -/
lemma Complex.cot_pi_eq_exp_ratio (z : ‚ÑÇ) :
    cot (œÄ * z) = (Complex.exp (2 * œÄ * I * z) + 1) / (I * (1 - Complex.exp (2 * œÄ * I * z))) := by
  rw [cot_eq_exp_ratio (œÄ * z)]
  ring_nf

/- This is the version one probably wants, which is why the pi's are there. -/
theorem pi_mul_cot_pi_q_exp (z : ‚Ñç) :
    œÄ * cot (œÄ * z) = œÄ * I - 2 * œÄ * I * ‚àë' n : ‚Ñï, Complex.exp (2 * œÄ * I * z) ^ n := by
  have h1 : œÄ * ((exp (2 * œÄ * I * z) + 1) / (I * (1 - exp (2 * œÄ * I * z)))) =
      -œÄ * I * ((exp (2 * œÄ * I * z) + 1) * (1 / (1 - exp (2 * œÄ * I * z)))) := by
    simp only [div_mul_eq_div_mul_one_div, div_I, one_div, neg_mul, mul_neg, neg_inj]
    ring
  rw [cot_pi_eq_exp_ratio, h1, one_div, (tsum_geometric_of_norm_lt_one
    (UpperHalfPlane.norm_exp_two_pi_I_lt_one z)).symm, add_comm, geom_series_mul_one_add
      (Complex.exp (2 * œÄ * I * (z : ‚ÑÇ))) (UpperHalfPlane.norm_exp_two_pi_I_lt_one _)]
  ring

section MittagLeffler

open Filter Function

open scoped Topology BigOperators Nat Complex

variable {x : ‚ÑÇ} {Z : Set ‚ÑÇ}

/-- The main term in the infinite product for sine. -/
noncomputable abbrev sineTerm (x : ‚ÑÇ) (n : ‚Ñï) : ‚ÑÇ := -x ^ 2 / (n + 1) ^ 2

lemma sineTerm_ne_zero {x : ‚ÑÇ} (hx : x ‚àà ‚ÑÇ_‚Ñ§) (n : ‚Ñï) : 1 + sineTerm x n ‚â† 0 := by
  simp only [sineTerm, ne_eq]
  rw [add_eq_zero_iff_eq_neg, neg_div', eq_div_iff]
  ¬∑ have := (integerComplement_pow_two_ne_pow_two hx (n + 1 : ‚Ñ§))
    aesop
  ¬∑ simp [Nat.cast_add_one_ne_zero n]

lemma tendsto_euler_sin_prod' (h0 : x ‚â† 0) :
    Tendsto (fun n : ‚Ñï ‚Ü¶ ‚àè i ‚àà Finset.range n, (1 + sineTerm x i)) atTop
    (ùìù (sin (œÄ * x) / (œÄ * x))) := by
  rw [show (sin (œÄ * x) / (œÄ * x)) = sin (œÄ * x) * (1 / (œÄ * x)) by ring]
  apply (Filter.Tendsto.mul_const (b := 1 / (œÄ * x)) (tendsto_euler_sin_prod x)).congr
  exact fun n ‚Ü¶ by field_simp; rfl

lemma multipliable_sineTerm (x : ‚ÑÇ) : Multipliable fun i ‚Ü¶ (1 + sineTerm x i) := by
  apply multipliable_one_add_of_summable
  have := summable_pow_div_add (x ^ 2) 2 1 Nat.one_lt_two
  simpa [sineTerm] using this

lemma euler_sineTerm_tprod (hx : x ‚àà ‚ÑÇ_‚Ñ§) :
    ‚àè' i : ‚Ñï, (1 + sineTerm x i) = Complex.sin (œÄ * x) / (œÄ * x) := by
  rw [‚Üê Multipliable.hasProd_iff (multipliable_sineTerm x),
    Multipliable.hasProd_iff_tendsto_nat (multipliable_sineTerm x)]
  exact tendsto_euler_sin_prod' (integerComplement.ne_zero hx)

private lemma sineTerm_bound_aux (hZ : IsCompact Z) :
    ‚àÉ u : ‚Ñï ‚Üí ‚Ñù, Summable u ‚àß ‚àÄ j z, z ‚àà Z ‚Üí ‚ÄñsineTerm z j‚Äñ ‚â§ u j := by
  have hf : ContinuousOn (fun x : ‚ÑÇ => ‚Äñ-x ^ 2‚Äñ) Z := by
    fun_prop
  obtain ‚ü®s, hs‚ü© := bddAbove_def.mp (IsCompact.bddAbove_image hZ hf)
  refine ‚ü®fun n : ‚Ñï => ‚Äñ(s : ‚ÑÇ) / (n + 1) ^ 2‚Äñ, ?_, ?_‚ü©
  ¬∑ simpa using summable_pow_div_add (s : ‚ÑÇ) 2 1 (Nat.one_lt_two)
  ¬∑ simp only [norm_neg, norm_pow, Set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iff‚ÇÇ, sineTerm, norm_div, norm_real, norm_eq_abs] at *
    intro n x hx
    gcongr
    apply le_trans (hs x hx) (le_abs_self s)

lemma multipliableUniformlyOn_euler_sin_prod_on_compact (hZC : IsCompact Z) :
    MultipliableUniformlyOn (fun n : ‚Ñï => fun z : ‚ÑÇ => (1 + sineTerm z n)) {Z} := by
  obtain ‚ü®u, hu, hu2‚ü© := sineTerm_bound_aux hZC
  refine Summable.multipliableUniformlyOn_nat_one_add hZC hu ?_ ?_
  ¬∑ filter_upwards with n z hz using hu2 n z hz
  ¬∑ fun_prop

lemma HasProdUniformlyOn_sineTerm_prod_on_compact (hZ2 : Z ‚äÜ ‚ÑÇ_‚Ñ§)
    (hZC : IsCompact Z) :
    HasProdUniformlyOn (fun n : ‚Ñï => fun z : ‚ÑÇ => (1 + sineTerm z n))
    (fun x => (Complex.sin (‚ÜëœÄ * x) / (‚ÜëœÄ * x))) {Z} := by
  apply (multipliableUniformlyOn_euler_sin_prod_on_compact hZC).hasProdUniformlyOn.congr_right
  exact fun s hs x hx => euler_sineTerm_tprod (by aesop)

lemma HasProdLocallyUniformlyOn_euler_sin_prod :
    HasProdLocallyUniformlyOn (fun n : ‚Ñï => fun z : ‚ÑÇ => (1 + sineTerm z n))
    (fun x => (Complex.sin (œÄ * x) / (œÄ * x))) ‚ÑÇ_‚Ñ§ := by
  apply hasProdLocallyUniformlyOn_of_forall_compact isOpen_compl_range_intCast
  exact fun _ hZ hZC => HasProdUniformlyOn_sineTerm_prod_on_compact hZ hZC

/-- `sin œÄ z` is non vanishing on the complement of the integers in `‚ÑÇ`. -/
theorem sin_pi_mul_ne_zero (hx : x ‚àà ‚ÑÇ_‚Ñ§) : Complex.sin (œÄ * x) ‚â† 0 := by
  apply Complex.sin_ne_zero_iff.2
  intro k
  nth_rw 2 [mul_comm]
  exact Injective.ne (mul_right_injective‚ÇÄ (ofReal_ne_zero.mpr Real.pi_ne_zero)) (by aesop)

lemma cot_pi_mul_contDiffWithinAt (k : ‚Ñï‚àû) (hx : x ‚àà ‚ÑÇ_‚Ñ§) :
    ContDiffWithinAt ‚ÑÇ k (fun x ‚Ü¶ (‚ÜëœÄ * x).cot) ‚Ñç‚Çí x := by
  simp_rw [Complex.cot, Complex.cos, Complex.sin]
  exact ContDiffWithinAt.div (by fun_prop) (by fun_prop) (sin_pi_mul_ne_zero hx)

lemma tendsto_logDeriv_euler_sin_div (hx : x ‚àà ‚ÑÇ_‚Ñ§) :
    Tendsto (fun n : ‚Ñï ‚Ü¶ logDeriv (fun z ‚Ü¶ ‚àè j ‚àà Finset.range n, (1 + sineTerm z j)) x)
        atTop (ùìù <| logDeriv (fun t ‚Ü¶ (Complex.sin (œÄ * t) / (œÄ * t))) x) := by
  refine logDeriv_tendsto (isOpen_compl_range_intCast) ‚ü®x, hx‚ü©
      HasProdLocallyUniformlyOn_euler_sin_prod.tendstoLocallyUniformlyOn_finsetRange ?_ ?_
  ¬∑ filter_upwards with n using by fun_prop
  ¬∑ simp only [ne_eq, div_eq_zero_iff, mul_eq_zero, ofReal_eq_zero, not_or]
    exact ‚ü®sin_pi_mul_ne_zero hx, Real.pi_ne_zero, integerComplement.ne_zero hx‚ü©

lemma logDeriv_sin_div_eq_cot (hz : x ‚àà ‚ÑÇ_‚Ñ§) :
    logDeriv (fun t ‚Ü¶ (Complex.sin (œÄ * t) / (œÄ * t))) x = œÄ * cot (œÄ * x) - 1 / x := by
  have : (fun t ‚Ü¶ (Complex.sin (œÄ * t)/ (œÄ * t))) = fun z ‚Ü¶
    (Complex.sin ‚àò fun t ‚Ü¶ œÄ * t) z / (œÄ * z) := by simp
  rw [this, logDeriv_div _ (by apply sin_pi_mul_ne_zero hz) ?_
    (DifferentiableAt.comp _ (Complex.differentiableAt_sin) (by fun_prop)) (by fun_prop),
    logDeriv_comp (Complex.differentiableAt_sin) (by fun_prop), Complex.logDeriv_sin,
    deriv_const_mul _ (by fun_prop), deriv_id'', logDeriv_const_mul, logDeriv_id']
  ¬∑ field_simp
  ¬∑ simp
  ¬∑ simp only [ne_eq, mul_eq_zero, ofReal_eq_zero, not_or]
    exact ‚ü®Real.pi_ne_zero, integerComplement.ne_zero hz‚ü©

/-- The term in the infinite sum expansion of cot. -/
noncomputable abbrev cotTerm (x : ‚ÑÇ) (n : ‚Ñï) : ‚ÑÇ := 1 / (x - (n + 1)) + 1 / (x + (n + 1))

lemma logDeriv_sineTerm_eq_cotTerm (hx : x ‚àà ‚ÑÇ_‚Ñ§) (i : ‚Ñï) :
    logDeriv (fun (z : ‚ÑÇ) ‚Ü¶ 1 + sineTerm z i) x = cotTerm x i := by
  have h1 := integerComplement_add_ne_zero hx (i + 1)
  have h2 : ((x : ‚ÑÇ) - (i + 1)) ‚â† 0 := by
    simpa [sub_eq_add_neg] using integerComplement_add_ne_zero hx (-(i + 1))
  have h3 : (i+1)^2 + -x^2 ‚â† 0 := by
      have := (integerComplement_pow_two_ne_pow_two hx ((i + 1) : ‚Ñ§))
      rw [‚Üê sub_eq_add_neg, sub_ne_zero]
      aesop
  simp only [Int.cast_add, Int.cast_natCast, Int.cast_one, ne_eq, sineTerm, logDeriv_apply,
    deriv_const_add', deriv_div_const, deriv.fun_neg', differentiableAt_fun_id, deriv_fun_pow,
    Nat.cast_ofNat, Nat.add_one_sub_one, pow_one, deriv_id'', mul_one, cotTerm, one_div] at *
  field_simp
  ring

lemma logDeriv_prod_sineTerm_eq_sum_cotTerm (hx : x ‚àà ‚ÑÇ_‚Ñ§) (n : ‚Ñï) :
    logDeriv (fun (z : ‚ÑÇ) ‚Ü¶ ‚àè j ‚àà Finset.range n, (1 + sineTerm z j)) x =
    ‚àë j ‚àà Finset.range n, cotTerm x j := by
  rw [logDeriv_prod]
  ¬∑ simp_rw [logDeriv_sineTerm_eq_cotTerm hx]
  ¬∑ exact fun i _ ‚Ü¶ sineTerm_ne_zero hx i
  ¬∑ fun_prop

lemma tendsto_logDeriv_euler_cot_sub (hx : x ‚àà ‚ÑÇ_‚Ñ§) :
    Tendsto (fun n : ‚Ñï => ‚àë j ‚àà Finset.range n, cotTerm x j) atTop
    (ùìù <| œÄ * cot (œÄ * x)- 1 / x) := by
  simp_rw [‚Üê logDeriv_sin_div_eq_cot hx, ‚Üê logDeriv_prod_sineTerm_eq_sum_cotTerm hx]
  simpa using tendsto_logDeriv_euler_sin_div hx

lemma cotTerm_identity (hz : x ‚àà ‚ÑÇ_‚Ñ§) (n : ‚Ñï) :
    cotTerm x n = 2 * x * (1 / ((x + (n + 1)) * (x - (n + 1)))) := by
  simp only [cotTerm]
  rw [one_div_add_one_div]
  ¬∑ ring
  ¬∑ simpa [sub_eq_add_neg] using integerComplement_add_ne_zero hz (-(n + 1) : ‚Ñ§)
  ¬∑ simpa using (integerComplement_add_ne_zero hz ((n : ‚Ñ§) + 1))

lemma Summable_cotTerm (hz : x ‚àà ‚ÑÇ_‚Ñ§) : Summable fun n ‚Ü¶ cotTerm x n := by
  rw [funext fun n ‚Ü¶ cotTerm_identity hz n]
  apply Summable.mul_left
  suffices Summable fun i : ‚Ñï ‚Ü¶ (x - (‚Üëi : ‚ÑÇ))‚Åª¬π * (x + (‚Üëi : ‚ÑÇ))‚Åª¬π by
    rw [‚Üê summable_nat_add_iff 1] at this
    simpa using this
  suffices Summable fun i : ‚Ñ§ ‚Ü¶ (x - (‚Üëi : ‚ÑÇ))‚Åª¬π * (x + (‚Üëi : ‚ÑÇ))‚Åª¬π by
    apply this.comp_injective CharZero.cast_injective
  apply (EisensteinSeries.summable_linear_sub_mul_linear_add x 1 1).congr
  simp [mul_comm]

lemma cot_series_rep' (hz : x ‚àà ‚ÑÇ_‚Ñ§) : œÄ * cot (œÄ * x) - 1 / x =
    ‚àë' n : ‚Ñï, (1 / (x - (n + 1)) + 1 / (x + (n + 1))) := by
  rw [HasSum.tsum_eq]
  apply (Summable.hasSum_iff_tendsto_nat (Summable_cotTerm hz)).mpr
    (tendsto_logDeriv_euler_cot_sub hz)

/-- The cotangent infinite sum representation. -/
theorem cot_series_rep (hz : x ‚àà ‚ÑÇ_‚Ñ§) :
    œÄ * cot (œÄ * x) = 1 / x + ‚àë' n : ‚Ñï+, (1 / (x - n) + 1 / (x + n)) := by
  have h0 := tsum_pnat_eq_tsum_succ (f := fun n ‚Ü¶ 1 / (x - n) + 1 / (x + n))
  have h1 := cot_series_rep' hz
  simp only [one_div, Nat.cast_add, Nat.cast_one] at *
  rw [h0, ‚Üê h1]
  ring

end MittagLeffler

section iteratedDeriv

open Set UpperHalfPlane

open scoped Nat

variable (k : ‚Ñï)

private lemma contDiffOn_inv_linear (d : ‚Ñ§) : ContDiffOn ‚ÑÇ k (fun z : ‚ÑÇ ‚Ü¶ 1 / (z + d)) ‚ÑÇ_‚Ñ§ := by
  simpa using ContDiffOn.inv (by fun_prop) (fun x hx ‚Ü¶ integerComplement_add_ne_zero hx d)

lemma eqOn_iteratedDeriv_cotTerm (d : ‚Ñï) : EqOn (iteratedDeriv k (fun z ‚Ü¶ cotTerm z d))
    (fun z ‚Ü¶ (-1)^ k * k ! * ((z + (d + 1))^ (-1 - k : ‚Ñ§) + (z - (d + 1)) ^ (-1 - k : ‚Ñ§))) ‚ÑÇ_‚Ñ§ := by
  intro z hz
  rw [‚Üê Pi.add_def, iteratedDeriv_add]
  ¬∑ have h2 := iter_deriv_inv_linear_sub k 1 ((d + 1 : ‚ÑÇ))
    have h3 := iter_deriv_inv_linear k 1 (d + 1 : ‚ÑÇ)
    simp only [one_div, one_mul, one_pow, mul_one, Int.reduceNeg, iteratedDeriv_eq_iterate] at *
    rw [h2, h3]
    ring
  ¬∑ simpa [sub_eq_add_neg] using (contDiffOn_inv_linear k (-(d + 1))).contDiffAt
      ((isOpen_compl_range_intCast).mem_nhds hz)
  ¬∑ simpa using (contDiffOn_inv_linear k (d + 1)).contDiffAt
      ((isOpen_compl_range_intCast).mem_nhds hz)

lemma eqOn_iteratedDerivWithin_cotTerm_integerComplement (d : ‚Ñï) :
    EqOn
      (iteratedDerivWithin k (fun z ‚Ü¶ cotTerm z d) ‚ÑÇ_‚Ñ§)
      (fun z ‚Ü¶ (-1) ^ k * k ! * ((z + (d + 1)) ^ (-1 - k : ‚Ñ§) + (z - (d + 1)) ^ (-1 - k : ‚Ñ§)))
      ‚ÑÇ_‚Ñ§ := by
  apply Set.EqOn.trans (iteratedDerivWithin_of_isOpen isOpen_compl_range_intCast)
  exact eqOn_iteratedDeriv_cotTerm ..

lemma eqOn_iteratedDerivWithin_cotTerm_upperHalfPlaneSet (d : ‚Ñï) :
    EqOn
      (iteratedDerivWithin k (fun z ‚Ü¶ cotTerm z d) ‚Ñç‚Çí)
      (fun z ‚Ü¶ (-1) ^ k * k ! * ((z + (d + 1)) ^ (-1 - k : ‚Ñ§) + (z - (d + 1)) ^ (-1 - k : ‚Ñ§)))
      ‚Ñç‚Çí := by
  apply Set.EqOn.trans (upperHalfPlane_inter_integerComplement ‚ñ∏
    iteratedDerivWithin_congr_right_of_isOpen (fun z ‚Ü¶ cotTerm z d) k
    isOpen_upperHalfPlaneSet (isOpen_compl_range_intCast))
  intro z hz
  simpa using eqOn_iteratedDerivWithin_cotTerm_integerComplement k d
    (coe_mem_integerComplement ‚ü®z, hz‚ü©)

open EisensteinSeries in
private noncomputable abbrev cotTermUpperBound (A B : ‚Ñù) (hB : 0 < B) (a : ‚Ñï) :=
  k ! * (2 * (r (‚ü®‚ü®A, B‚ü©, by simp [hB]‚ü©) ^ (-1 - k : ‚Ñ§)) * ‚Äñ((a + 1) ^ (-1 - k : ‚Ñ§) : ‚Ñù)‚Äñ)

private lemma summable_cotTermUpperBound (A B : ‚Ñù) (hB : 0 < B) {k : ‚Ñï} (hk : 1 ‚â§ k) :
    Summable fun a : ‚Ñï ‚Ü¶ cotTermUpperBound k A B hB a := by
  simp_rw [‚Üê mul_assoc]
  apply Summable.mul_left
  conv => enter [1, n]; rw [show (-1 - k : ‚Ñ§) = -(1 + k :) by cutsat, zpow_neg, zpow_natCast];
          enter [1, 1, 1]; norm_cast
  rw [summable_norm_iff, summable_nat_add_iff (f := fun n : ‚Ñï ‚Ü¶ ((n : ‚Ñù) ^ (1 + k))‚Åª¬π)]
  exact summable_nat_pow_inv.mpr <| by cutsat

open EisensteinSeries in
private lemma iteratedDerivWithin_cotTerm_bounded_uniformly
    {k : ‚Ñï} {K : Set ‚ÑÇ} (hK : K ‚äÜ ‚Ñç‚Çí) (A B : ‚Ñù) (hB : 0 < B)
    (HABK : inclusion hK '' univ ‚äÜ verticalStrip A B) (n : ‚Ñï) {a : ‚ÑÇ} (ha : a ‚àà K) :
    ‚ÄñiteratedDerivWithin k (fun z ‚Ü¶ cotTerm z n) ‚Ñç‚Çí a‚Äñ ‚â§ cotTermUpperBound k A B hB n := by
  simp only [eqOn_iteratedDerivWithin_cotTerm_upperHalfPlaneSet k n (hK ha), Complex.norm_mul,
    norm_pow, norm_neg,norm_one, one_pow, Complex.norm_natCast, one_mul, cotTermUpperBound,
    Int.reduceNeg, norm_zpow, Real.norm_eq_abs, two_mul, add_mul]
  gcongr
  have h1 := summand_bound_of_mem_verticalStrip (k := k + 1) (by norm_cast; cutsat) ![1, n + 1] hB
      (z := ‚ü®a, (hK ha)‚ü©) (A := A) (by aesop)
  have h2 := abs_norm_eq_max_natAbs_neg n ‚ñ∏ (summand_bound_of_mem_verticalStrip (k := k + 1)
    (by norm_cast; cutsat) ![1, -(n + 1)] hB (z := ‚ü®a, (hK ha)‚ü©) (A := A) (by aesop))
  simp only [Fin.isValue, Matrix.cons_val_zero, Int.cast_one, coe_mk_subtype, one_mul,
    Matrix.cons_val_one, Matrix.cons_val_fin_one, Int.cast_add, Int.cast_natCast, neg_add_rev,
    abs_norm_eq_max_natAbs, Int.reduceNeg, Int.cast_neg, sub_eq_add_neg, ge_iff_le] at h1 h2 ‚ä¢
  refine le_trans (norm_add_le _ _) (add_le_add ?_ ?_) <;>
    {simp only [Int.reduceNeg, norm_zpow]; norm_cast at h1 h2 ‚ä¢}

lemma summableLocallyUniformlyOn_iteratedDerivWithin_cotTerm {k : ‚Ñï} (hk : 1 ‚â§ k) :
    SummableLocallyUniformlyOn (fun n ‚Ü¶ iteratedDerivWithin k (fun z ‚Ü¶ cotTerm z n) ‚Ñç‚Çí) ‚Ñç‚Çí := by
  apply SummableLocallyUniformlyOn_of_locally_bounded isOpen_upperHalfPlaneSet
  intro K hK hKc
  obtain ‚ü®A, B, hB, HABK‚ü© := subset_verticalStrip_of_isCompact
    ((isCompact_iff_isCompact_univ.mp hKc).image_of_continuousOn
    (continuous_inclusion hK |>.continuousOn))
  exact ‚ü®cotTermUpperBound k A B hB, summable_cotTermUpperBound A B hB hk,
    iteratedDerivWithin_cotTerm_bounded_uniformly hK A B hB HABK‚ü©

lemma differentiableOn_iteratedDerivWithin_cotTerm (n l : ‚Ñï) :
    DifferentiableOn ‚ÑÇ (iteratedDerivWithin l (fun z ‚Ü¶ cotTerm z n) ‚Ñç‚Çí) ‚Ñç‚Çí := by
  suffices DifferentiableOn ‚ÑÇ (fun z : ‚ÑÇ ‚Ü¶ (-1) ^ l * l ! * ((z + (n + 1)) ^ (-1 - l : ‚Ñ§) +
    (z - (n + 1)) ^ (-1 - l : ‚Ñ§))) ‚Ñç‚Çí by
    exact this.congr fun z hz ‚Ü¶ eqOn_iteratedDerivWithin_cotTerm_upperHalfPlaneSet l n hz
  apply DifferentiableOn.const_mul
  apply DifferentiableOn.add <;> refine DifferentiableOn.zpow (by fun_prop) <| .inl fun x hx ‚Ü¶ ?_
  ¬∑ simpa [add_eq_zero_iff_neg_eq'] using (UpperHalfPlane.ne_int ‚ü®x, hx‚ü© (-(n + 1))).symm
  ¬∑ simpa [sub_eq_zero] using (UpperHalfPlane.ne_int ‚ü®x, hx‚ü© (n + 1))

private lemma aux_summable_add {k : ‚Ñï} (hk : 1 ‚â§ k) (x : ‚ÑÇ) :
  Summable fun (n : ‚Ñï) ‚Ü¶ (x + (n + 1)) ^ (-1 - k : ‚Ñ§) := by
  apply ((summable_nat_add_iff 1).mpr (summable_int_iff_summable_nat_and_neg.mp
        (EisensteinSeries.linear_right_summable x 1 (k := k + 1) (by cutsat))).1).congr
  simp [‚Üê zpow_neg, sub_eq_add_neg]

private lemma aux_summable_sub {k : ‚Ñï} (hk : 1 ‚â§ k) (x : ‚ÑÇ) :
  Summable fun (n : ‚Ñï) ‚Ü¶ (x - (n + 1)) ^ (-1 - k : ‚Ñ§) := by
  apply ((summable_nat_add_iff 1).mpr (summable_int_iff_summable_nat_and_neg.mp
        (EisensteinSeries.linear_right_summable x 1 (k := k + 1) (by cutsat))).2).congr
  simp [‚Üê zpow_neg, sub_eq_add_neg]

variable {z : ‚ÑÇ}

-- We have this auxiliary ugly version on the lhs so the rhs looks nicer.
private lemma aux_iteratedDeriv_tsum_cotTerm {k : ‚Ñï} (hk : 1 ‚â§ k) (hz : z ‚àà ‚Ñç‚Çí) :
    (-1) ^ k * (k !) * z ^ (-1 - k : ‚Ñ§) +
      iteratedDerivWithin k (fun z ‚Ü¶ ‚àë' n : ‚Ñï, cotTerm z n) ‚Ñç‚Çí z =
    (-1) ^ k * k ! * ‚àë' n : ‚Ñ§, (z + n) ^ (-1 - k : ‚Ñ§) := by
  rw [iteratedDerivWithin_tsum k isOpen_upperHalfPlaneSet hz
    (fun t ht ‚Ü¶ Summable_cotTerm (coe_mem_integerComplement ‚ü®t, ht‚ü©))
    (fun l hl hl2 ‚Ü¶ summableLocallyUniformlyOn_iteratedDerivWithin_cotTerm  hl)
    (fun n l z hl hz ‚Ü¶ (differentiableOn_iteratedDerivWithin_cotTerm n l).differentiableAt
    (isOpen_upperHalfPlaneSet.mem_nhds hz))]
  conv =>
    enter [1, 2, 1, n]
    rw [eqOn_iteratedDerivWithin_cotTerm_upperHalfPlaneSet k n (by simp [hz])]
  rw [tsum_of_add_one_of_neg_add_one (by simpa using aux_summable_add hk z)
    (by simpa [sub_eq_add_neg] using aux_summable_sub hk z),
    tsum_mul_left, Summable.tsum_add (aux_summable_add hk z) (aux_summable_sub hk z)]
  push_cast
  ring_nf

lemma iteratedDerivWithin_cot_sub_inv_eq_add_mul_tsum {k : ‚Ñï} (hk : 1 ‚â§ k) (hz : z ‚àà ‚Ñç‚Çí) :
    iteratedDerivWithin k (fun x : ‚ÑÇ ‚Ü¶ œÄ * cot (œÄ * x) - 1 / x) ‚Ñç‚Çí z =
    -(-1) ^ k * k ! * (z ^ (-1 - k : ‚Ñ§)) + (-1) ^ k * k ! * ‚àë' n : ‚Ñ§, (z + n) ^ (-1 - k : ‚Ñ§) := by
  simp only [‚Üê aux_iteratedDeriv_tsum_cotTerm hk hz, one_div, neg_mul, neg_add_cancel_left]
  refine iteratedDerivWithin_congr (fun z hz ‚Ü¶ ?_) hz
  simpa [cotTerm] using (cot_series_rep' (UpperHalfPlane.coe_mem_integerComplement ‚ü®z, hz‚ü©))

private lemma iteratedDerivWithin_cot_pi_mul_sub_inv {z : ‚ÑÇ} (hz : z ‚àà ‚Ñç‚Çí) :
    iteratedDerivWithin k (fun x : ‚ÑÇ ‚Ü¶ œÄ * cot (œÄ * x) - 1 / x) ‚Ñç‚Çí z =
    (iteratedDerivWithin k (fun x : ‚ÑÇ ‚Ü¶ œÄ * cot (œÄ * x)) ‚Ñç‚Çí z) -
    (-1) ^ k * k ! * (z ^ (-1 - k : ‚Ñ§)) := by
  simp_rw [sub_eq_add_neg]
  rw [iteratedDerivWithin_fun_add hz isOpen_upperHalfPlaneSet.uniqueDiffOn]
  ¬∑ simpa [iteratedDerivWithin_fun_neg] using iteratedDerivWithin_one_div k
      isOpen_upperHalfPlaneSet hz
  ¬∑ exact ContDiffWithinAt.smul (by fun_prop) (cot_pi_mul_contDiffWithinAt k
      (UpperHalfPlane.coe_mem_integerComplement ‚ü®z, hz‚ü©))
  ¬∑ simp only [one_div]
    apply ContDiffWithinAt.neg
    exact ContDiffWithinAt.inv (by fun_prop) (ne_zero ‚ü®z, hz‚ü©)

lemma iteratedDerivWithin_cot_pi_mul_eq_mul_tsum_zpow {k : ‚Ñï} (hk : 1 ‚â§ k) {z : ‚ÑÇ} (hz : z ‚àà ‚Ñç‚Çí) :
    iteratedDerivWithin k (fun x : ‚ÑÇ ‚Ü¶ œÄ * cot (œÄ * x)) ‚Ñç‚Çí z =
    (-1) ^ k * k ! * ‚àë' n : ‚Ñ§, (z + n) ^ (-1 - k : ‚Ñ§):= by
  have h0 := iteratedDerivWithin_cot_pi_mul_sub_inv k hz
  rw [iteratedDerivWithin_cot_sub_inv_eq_add_mul_tsum  hk hz, add_comm] at h0
  rw [‚Üê add_left_inj (-(-1) ^ k * k ! * z ^ (-1 - k : ‚Ñ§)), h0]
  ring

/-- The series expansion of the iterated derivative of `œÄ cot (œÄ z)`. -/
theorem iteratedDerivWithin_cot_pi_mul_eq_mul_tsum_div_pow {k : ‚Ñï} (hk : 1 ‚â§ k) {z : ‚ÑÇ}
    (hz : z ‚àà ‚Ñç‚Çí) :
    iteratedDerivWithin k (fun x : ‚ÑÇ ‚Ü¶ œÄ * cot (œÄ * x)) ‚Ñç‚Çí z =
      (-1) ^ k * k ! * ‚àë' n : ‚Ñ§, 1 / (z + n) ^ (k + 1) := by
  convert iteratedDerivWithin_cot_pi_mul_eq_mul_tsum_zpow hk hz with n
  rw [show (-1 - k : ‚Ñ§) = -(k + 1 :) by norm_cast; cutsat, zpow_neg_coe_of_pos _ (by cutsat),
    one_div]

end iteratedDeriv
