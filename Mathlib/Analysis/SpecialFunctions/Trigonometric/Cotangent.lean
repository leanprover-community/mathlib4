/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Complex.UpperHalfPlane.Exp
import Mathlib.Analysis.Complex.IntegerCompl
import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Analysis.PSeries
import Mathlib.Analysis.SpecialFunctions.Trigonometric.EulerSineProd
import Mathlib.Analysis.NormedSpace.MultipliableUniformlyOn
import Mathlib.NumberTheory.ModularForms.EisensteinSeries.Summable

/-!
# Cotangent

This file contains lemmas about the cotangent function, including useful series expansions.
In particular, we prove that
`Ï€ * cot (Ï€ * z) = Ï€ * I - 2 * Ï€ * I * âˆ‘' n : â„•, Complex.exp (2 * Ï€ * I * z) ^ n`
as well as the infinite sum representation of cotangent (also known as the Mittag-Leffler
expansion): `Ï€ * cot (Ï€ * z) = 1 / z + âˆ‘' n : â„•+, (1 / ((z : â„‚) - n) + 1 / (z + n))`.
-/

open Real Complex

open scoped UpperHalfPlane

lemma Complex.cot_eq_exp_ratio (z : â„‚) :
    cot z = (Complex.exp (2 * I * z) + 1) / (I * (1 - Complex.exp (2 * I * z))) := by
  rw [Complex.cot, Complex.sin, Complex.cos]
  field_simp
  have h1 : exp (z * I) + exp (-(z * I)) = exp (-(z * I)) * (exp (2 * I * z) + 1) := by
    rw [mul_add, â† Complex.exp_add]
    simp only [mul_one, add_left_inj]
    ring_nf
  have h2 : (exp (-(z * I)) - exp (z * I)) * I = exp (-(z * I)) * (I * (1 - exp (2 * I * z))) := by
    ring_nf
    rw [mul_assoc, â† Complex.exp_add]
    ring_nf
  rw [h1, h2, mul_div_mul_left _ _ (Complex.exp_ne_zero _)]

/- The version one probably wants to use more. -/
lemma Complex.cot_pi_eq_exp_ratio (z : â„‚) :
    cot (Ï€ * z) = (Complex.exp (2 * Ï€ * I * z) + 1) / (I * (1 - Complex.exp (2 * Ï€ * I * z))) := by
  rw [cot_eq_exp_ratio (Ï€ * z)]
  ring_nf

/- This is the version one probably wants, which is why the pi's are there. -/
theorem pi_mul_cot_pi_q_exp (z : â„) :
    Ï€ * cot (Ï€ * z) = Ï€ * I - 2 * Ï€ * I * âˆ‘' n : â„•, Complex.exp (2 * Ï€ * I * z) ^ n := by
  have h1 : Ï€ * ((exp (2 * Ï€ * I * z) + 1) / (I * (1 - exp (2 * Ï€ * I * z)))) =
      -Ï€ * I * ((exp (2 * Ï€ * I * z) + 1) * (1 / (1 - exp (2 * Ï€ * I * z)))) := by
    simp only [div_mul_eq_div_mul_one_div, div_I, one_div, neg_mul, mul_neg, neg_inj]
    ring
  rw [cot_pi_eq_exp_ratio, h1, one_div, (tsum_geometric_of_norm_lt_one
    (UpperHalfPlane.norm_exp_two_pi_I_lt_one z)).symm, add_comm, geom_series_mul_one_add
      (Complex.exp (2 * Ï€ * I * (z : â„‚))) (UpperHalfPlane.norm_exp_two_pi_I_lt_one _)]
  ring

section MittagLeffler

open Filter Function

open scoped Topology BigOperators Nat Complex

local notation "â„‚_â„¤" => integerComplement

variable {x : â„‚} {Z : Set â„‚}

/-- The main term in the infinite product for sine. -/
noncomputable abbrev sineTerm (x : â„‚) (n : â„•) : â„‚ := -x ^ 2 / (n + 1) ^ 2

lemma sineTerm_ne_zero {x : â„‚} (hx : x âˆˆ â„‚_â„¤) (n : â„•) : 1 + sineTerm x n â‰  0 := by
  simp only [sineTerm, ne_eq]
  rw [add_eq_zero_iff_eq_neg, neg_div', eq_div_iff]
  Â· have := (integerComplement_pow_two_ne_pow_two hx (n + 1 : â„¤))
    aesop
  Â· simp [Nat.cast_add_one_ne_zero n]

theorem tendsto_euler_sin_prod' (h0 : x â‰  0) :
    Tendsto (fun n : â„• â†¦ âˆ i âˆˆ Finset.range n, (1 + sineTerm x i)) atTop
    (ğ“ (sin (Ï€ * x) / (Ï€ * x))) := by
  rw [show (sin (Ï€ * x) / (Ï€ * x)) = sin (Ï€ * x) * (1 / (Ï€ * x)) by ring]
  apply (Filter.Tendsto.mul_const (b := 1 / (Ï€ * x)) (tendsto_euler_sin_prod x)).congr
  exact fun n â†¦ by field_simp [sineTerm, Real.pi_ne_zero, sub_eq_add_neg]

theorem multipliable_sineTerm (x : â„‚) : Multipliable fun i â†¦ (1 + sineTerm x i) := by
  apply multipliable_one_add_of_summable
  have := summable_pow_div_add (x ^ 2) 2 1 Nat.one_lt_two
  simpa [sineTerm] using this

lemma euler_sineTerm_tprod (hx : x âˆˆ â„‚_â„¤) :
    âˆ' i : â„•, (1 + sineTerm x i) = Complex.sin (Ï€ * x) / (Ï€ * x) := by
  rw [â† Multipliable.hasProd_iff (multipliable_sineTerm x) ,
    Multipliable.hasProd_iff_tendsto_nat (multipliable_sineTerm x)]
  exact tendsto_euler_sin_prod' (integerComplement.ne_zero hx)

private lemma sineTerm_bound_aux (hZ : IsCompact Z) :
    âˆƒ u : â„• â†’ â„, Summable u âˆ§ âˆ€ j z, z âˆˆ Z â†’ â€–sineTerm z jâ€– â‰¤ u j := by
  have hf : ContinuousOn (fun x : â„‚ => â€–-x ^ 2â€–) Z := by
    fun_prop
  obtain âŸ¨s, hsâŸ© := bddAbove_def.mp (IsCompact.bddAbove_image hZ hf)
  refine âŸ¨fun n : â„• => â€–(s : â„‚) / (n + 1) ^ 2â€–, ?_, ?_âŸ©
  Â· simpa using summable_pow_div_add (s : â„‚) 2 1 (Nat.one_lt_two)
  Â· simp only [norm_neg, norm_pow, Set.mem_image, forall_exists_index, and_imp,
      forall_apply_eq_imp_iffâ‚‚, sineTerm, norm_div, norm_real, norm_eq_abs] at *
    intro n x hx
    gcongr
    apply le_trans (hs x hx) (le_abs_self s)

theorem multipliableUniformlyOn_euler_sin_prod_on_compact (hZC : IsCompact Z) :
    MultipliableUniformlyOn (fun n : â„• => fun z : â„‚ => (1 + sineTerm z n)) {Z} := by
  obtain âŸ¨u, hu, hu2âŸ© := sineTerm_bound_aux hZC
  refine Summable.multipliableUniformlyOn_nat_one_add hZC hu ?_ ?_
  Â· filter_upwards with n z hz using hu2 n z hz
  Â· fun_prop

theorem HasProdUniformlyOn_sineTerm_prod_on_compact (hZ2 : Z âŠ† â„‚_â„¤)
    (hZC : IsCompact Z) :
    HasProdUniformlyOn (fun n : â„• => fun z : â„‚ => (1 + sineTerm z n))
    (fun x => (Complex.sin (â†‘Ï€ * x) / (â†‘Ï€ * x))) {Z} := by
  apply (multipliableUniformlyOn_euler_sin_prod_on_compact hZC).hasProdUniformlyOn.congr_right
  exact fun s hs x hx => euler_sineTerm_tprod (by aesop)

theorem HasProdLocallyUniformlyOn_euler_sin_prod :
    HasProdLocallyUniformlyOn (fun n : â„• => fun z : â„‚ => (1 + sineTerm z n))
    (fun x => (Complex.sin (Ï€ * x) / (Ï€ * x))) â„‚_â„¤ := by
  apply hasProdLocallyUniformlyOn_of_forall_compact isOpen_compl_range_intCast
  exact fun _ hZ hZC => HasProdUniformlyOn_sineTerm_prod_on_compact hZ hZC

theorem sin_pi_z_ne_zero (hz : x âˆˆ â„‚_â„¤) : Complex.sin (Ï€ * x) â‰  0 := by
  apply Complex.sin_ne_zero_iff.2
  intro k
  nth_rw 2 [mul_comm]
  exact Injective.ne (mul_right_injectiveâ‚€ (ofReal_ne_zero.mpr Real.pi_ne_zero)) (by aesop)

theorem tendsto_logDeriv_euler_sin_div (hx : x âˆˆ â„‚_â„¤) :
    Tendsto (fun n : â„• â†¦ logDeriv (fun z â†¦ âˆ j âˆˆ Finset.range n, (1 + sineTerm z j)) x)
        atTop (ğ“ <| logDeriv (fun t â†¦ (Complex.sin (Ï€ * t) / (Ï€ * t))) x) := by
  refine logDeriv_tendsto (isOpen_compl_range_intCast) âŸ¨x, hxâŸ©
      HasProdLocallyUniformlyOn_euler_sin_prod.tendstoLocallyUniformlyOn_finsetRange ?_ ?_
  Â· filter_upwards with n using by fun_prop
  Â· simp only [ne_eq, div_eq_zero_iff, mul_eq_zero, ofReal_eq_zero, not_or]
    exact âŸ¨sin_pi_z_ne_zero hx, Real.pi_ne_zero , integerComplement.ne_zero hxâŸ©

theorem logDeriv_sin_div_eq_cot (hz : x âˆˆ â„‚_â„¤) :
    logDeriv (fun t â†¦ (Complex.sin (Ï€ * t) / (Ï€ * t))) x = Ï€ * cot (Ï€ * x) - 1 / x := by
  have : (fun t â†¦ (Complex.sin (Ï€ * t)/ (Ï€ * t))) = fun z â†¦
    (Complex.sin âˆ˜ fun t â†¦ Ï€ * t) z / (Ï€ * z) := by simp
  rw [this, logDeriv_div _ (by apply sin_pi_z_ne_zero hz) ?_
    (DifferentiableAt.comp _ (Complex.differentiableAt_sin) (by fun_prop)) (by fun_prop),
    logDeriv_comp (Complex.differentiableAt_sin) (by fun_prop), Complex.logDeriv_sin,
    deriv_const_mul _ (by fun_prop), deriv_id'', logDeriv_const_mul, logDeriv_id']
  Â· field_simp [mul_comm]
  Â· simpa using Real.pi_ne_zero
  Â· simp only [ne_eq, mul_eq_zero, ofReal_eq_zero, not_or]
    exact âŸ¨Real.pi_ne_zero, integerComplement.ne_zero hzâŸ©

/-- The term in the infinite sum expansion of cot. -/
noncomputable abbrev cotTerm (x : â„‚) (n : â„•) : â„‚ := 1 / (x - (n + 1)) + 1 / (x + (n + 1))

theorem logDeriv_sineTerm_eq_cotTerm (hx : x âˆˆ â„‚_â„¤) (i : â„•) :
    logDeriv (fun (z : â„‚) â†¦ 1 + sineTerm z i) x = cotTerm x i := by
  have h1 := integerComplement_add_ne_zero hx (i + 1)
  have h2 : ((x : â„‚) - (i + 1)) â‰  0 := by
    simpa [sub_eq_add_neg] using integerComplement_add_ne_zero hx (-(i + 1))
  have h3 : (i+1)^2 + -x^2 â‰  0 := by
      have := (integerComplement_pow_two_ne_pow_two hx ((i + 1) : â„¤))
      rw [â† sub_eq_add_neg, sub_ne_zero]
      aesop
  simp only [Int.cast_add, Int.cast_natCast, Int.cast_one, ne_eq, sineTerm, logDeriv_apply,
    deriv_const_add', deriv_div_const, deriv.fun_neg', differentiableAt_fun_id, deriv_fun_pow,
    Nat.cast_ofNat, Nat.add_one_sub_one, pow_one, deriv_id'', mul_one, cotTerm, one_div] at *
  field_simp [Nat.cast_add_one_ne_zero i]
  ring

lemma logDeriv_prod_sineTerm_eq_sum_cotTerm (hx : x âˆˆ â„‚_â„¤) (n : â„•) :
    logDeriv (fun (z : â„‚) â†¦ âˆ j âˆˆ Finset.range n, (1 + sineTerm z j)) x =
    âˆ‘ j âˆˆ Finset.range n, cotTerm x j := by
  rw [logDeriv_prod]
  Â· simp_rw [logDeriv_sineTerm_eq_cotTerm hx]
  Â· exact fun i _ â†¦ sineTerm_ne_zero hx i
  Â· fun_prop

theorem tendsto_logDeriv_euler_cot_sub (hx : x âˆˆ â„‚_â„¤) :
    Tendsto (fun n : â„• => âˆ‘ j âˆˆ Finset.range n, cotTerm x j) atTop
    (ğ“ <| Ï€ * cot (Ï€ * x)- 1 / x) := by
  simp_rw [â† logDeriv_sin_div_eq_cot hx, â† logDeriv_prod_sineTerm_eq_sum_cotTerm hx]
  simpa using tendsto_logDeriv_euler_sin_div hx

lemma cotTerm_identity (hz : x âˆˆ â„‚_â„¤) (n : â„•) :
    cotTerm x n = 2 * x * (1 / ((x + (n + 1)) * (x - (n + 1)))) := by
  simp only [cotTerm]
  rw [one_div_add_one_div]
  Â· ring
  Â· simpa [sub_eq_add_neg] using integerComplement_add_ne_zero hz (-(n + 1) : â„¤)
  Â· simpa using (integerComplement_add_ne_zero hz ((n : â„¤) + 1))

theorem Summable_cotTerm (hz : x âˆˆ â„‚_â„¤) : Summable fun n â†¦ cotTerm x n := by
  rw [funext fun n â†¦ cotTerm_identity hz n]
  apply Summable.mul_left
  suffices Summable fun i : â„• â†¦ (x - (â†‘i : â„‚))â»Â¹ * (x + (â†‘i : â„‚))â»Â¹ by
    rw [â† summable_nat_add_iff 1] at this
    simpa using this
  suffices Summable fun i : â„¤ â†¦ (x - (â†‘i : â„‚))â»Â¹ * (x + (â†‘i : â„‚))â»Â¹ by
    apply this.comp_injective CharZero.cast_injective
  apply (EisensteinSeries.summable_linear_sub_mul_linear_add x 1 1).congr
  simp [mul_comm]

theorem cot_series_rep' (hz : x âˆˆ â„‚_â„¤) : Ï€ * cot (Ï€ * x) - 1 / x =
    âˆ‘' n : â„•, (1 / (x - (n + 1)) + 1 / (x + (n + 1))) := by
  rw [HasSum.tsum_eq]
  apply (Summable.hasSum_iff_tendsto_nat (Summable_cotTerm hz)).mpr
    (tendsto_logDeriv_euler_cot_sub hz)

/-- The cotangent infinite sum representation. -/
theorem cot_series_rep (hz : x âˆˆ â„‚_â„¤) :
    Ï€ * cot (Ï€ * x) = 1 / x + âˆ‘' n : â„•+, (1 / (x - n) + 1 / (x + n)) := by
  have h0 := tsum_pnat_eq_tsum_succ fun n â†¦ 1 / (x - n) + 1 / (x + n)
  have h1 := cot_series_rep' hz
  simp only [one_div, Nat.cast_add, Nat.cast_one] at *
  rw [h0, â† h1]
  ring

end MittagLeffler
