/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle SÃ¶nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Complex
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

/-!
# Complex trigonometric functions

Basic facts and derivatives for the complex trigonometric functions.
-/


noncomputable section

namespace Complex

open Set Filter

open scoped Real

theorem hasStrictDerivAt_tan {x : â„‚} (h : cos x â‰  0) : HasStrictDerivAt tan (1 / cos x ^ 2) x := by
  convert (hasStrictDerivAt_sin x).div (hasStrictDerivAt_cos x) h using 1
  rw_mod_cast [â† sin_sq_add_cos_sq x]
  ring

theorem hasDerivAt_tan {x : â„‚} (h : cos x â‰  0) : HasDerivAt tan (1 / cos x ^ 2) x :=
  (hasStrictDerivAt_tan h).hasDerivAt

open scoped Topology

theorem tendsto_norm_tan_of_cos_eq_zero {x : â„‚} (hx : cos x = 0) :
    Tendsto (fun x => â€–tan xâ€–) (ğ“[â‰ ] x) atTop := by
  simp only [tan_eq_sin_div_cos, norm_div]
  have A : sin x â‰  0 := fun h => by simpa [*, sq] using sin_sq_add_cos_sq x
  have B : Tendsto cos (ğ“[â‰ ] x) (ğ“[â‰ ] 0) :=
    hx â–¸ (hasDerivAt_cos x).tendsto_nhdsNE (neg_ne_zero.2 A)
  exact continuous_sin.continuousWithinAt.norm.pos_mul_atTop (norm_pos_iff.2 A)
    (tendsto_norm_nhdsNE_zero.comp B).inv_tendsto_nhdsGT_zero

theorem tendsto_norm_tan_atTop (k : â„¤) :
    Tendsto (fun x => â€–tan xâ€–) (ğ“[â‰ ] ((2 * k + 1) * Ï€ / 2 : â„‚)) atTop :=
  tendsto_norm_tan_of_cos_eq_zero <| cos_eq_zero_iff.2 âŸ¨k, rflâŸ©

@[simp]
theorem continuousAt_tan {x : â„‚} : ContinuousAt tan x â†” cos x â‰  0 := by
  refine âŸ¨fun hc hâ‚€ => ?_, fun h => (hasDerivAt_tan h).continuousAtâŸ©
  exact not_tendsto_nhds_of_tendsto_atTop (tendsto_norm_tan_of_cos_eq_zero hâ‚€) _
    (hc.norm.tendsto.mono_left inf_le_left)

@[simp]
theorem differentiableAt_tan {x : â„‚} : DifferentiableAt â„‚ tan x â†” cos x â‰  0 :=
  âŸ¨fun h => continuousAt_tan.1 h.continuousAt, fun h => (hasDerivAt_tan h).differentiableAtâŸ©

@[simp]
theorem deriv_tan (x : â„‚) : deriv tan x = 1 / cos x ^ 2 :=
  if h : cos x = 0 then by
    have : Â¬DifferentiableAt â„‚ tan x := mt differentiableAt_tan.1 (Classical.not_not.2 h)
    simp [deriv_zero_of_not_differentiableAt this, h, sq]
  else (hasDerivAt_tan h).deriv

@[simp]
theorem contDiffAt_tan {x : â„‚} {n : WithTop â„•âˆ} : ContDiffAt â„‚ n tan x â†” cos x â‰  0 :=
  âŸ¨fun h => continuousAt_tan.1 h.continuousAt, contDiff_sin.contDiffAt.div contDiff_cos.contDiffAtâŸ©

end Complex
