/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
module

public import Mathlib.Analysis.Complex.Trigonometric
public import Mathlib.LinearAlgebra.Complex.Module
public import Mathlib.RingTheory.Polynomial.Chebyshev

/-!
# Multiple angle formulas in terms of Chebyshev polynomials

This file gives the trigonometric characterizations of Chebyshev polynomials, for the real
(`Real.cos`) and complex (`Complex.cos`) cosine and the real (`Real.cosh`) and complex
(`Complex.cosh`) hyperbolic cosine.
-/

public section


namespace Polynomial.Chebyshev

open Polynomial

variable {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]

@[simp, norm_cast]
theorem complex_ofReal_eval_T : ‚àÄ (x : ‚Ñù) n, (((T ‚Ñù n).eval x : ‚Ñù) : ‚ÑÇ) = (T ‚ÑÇ n).eval (x : ‚ÑÇ) :=
  @algebraMap_eval_T ‚Ñù ‚ÑÇ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_U : ‚àÄ (x : ‚Ñù) n, (((U ‚Ñù n).eval x : ‚Ñù) : ‚ÑÇ) = (U ‚ÑÇ n).eval (x : ‚ÑÇ) :=
  @algebraMap_eval_U ‚Ñù ‚ÑÇ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_C : ‚àÄ (x : ‚Ñù) n, (((C ‚Ñù n).eval x : ‚Ñù) : ‚ÑÇ) = (C ‚ÑÇ n).eval (x : ‚ÑÇ) :=
  @algebraMap_eval_C ‚Ñù ‚ÑÇ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_S : ‚àÄ (x : ‚Ñù) n, (((S ‚Ñù n).eval x : ‚Ñù) : ‚ÑÇ) = (S ‚ÑÇ n).eval (x : ‚ÑÇ) :=
  @algebraMap_eval_S ‚Ñù ‚ÑÇ _ _ _

/-! ### Complex versions -/

section Complex

open Complex

variable (Œ∏ : ‚ÑÇ)

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cos Œ∏` to the
value `cos (n * Œ∏)`. -/
@[simp]
theorem T_complex_cos (n : ‚Ñ§) : (T ‚ÑÇ n).eval (cos Œ∏) = cos (n * Œ∏) := by
  induction n using Polynomial.Chebyshev.induct with
  | zero => simp
  | one => simp
  | add_two n ih1 ih2 =>
    simp only [T_add_two, eval_sub, eval_mul, eval_X, eval_ofNat, ih1, ih2, sub_eq_iff_eq_add,
      cos_add_cos]
    push_cast
    ring_nf
  | neg_add_one n ih1 ih2 =>
    simp only [T_sub_one, eval_sub, eval_mul, eval_X, eval_ofNat, ih1, ih2, sub_eq_iff_eq_add',
      cos_add_cos]
    push_cast
    ring_nf

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cos Œ∏` to the
value `sin ((n + 1) * Œ∏) / sin Œ∏`. -/
@[simp]
theorem U_complex_cos (n : ‚Ñ§) : (U ‚ÑÇ n).eval (cos Œ∏) * sin Œ∏ = sin ((n + 1) * Œ∏) := by
  induction n using Polynomial.Chebyshev.induct with
  | zero => simp
  | one => simp [one_add_one_eq_two, sin_two_mul]; ring
  | add_two n ih1 ih2 =>
    simp only [U_add_two, eval_sub, eval_mul, eval_X, eval_ofNat, sub_mul,
      mul_assoc, ih1, ih2, sub_eq_iff_eq_add, sin_add_sin]
    push_cast
    ring_nf
  | neg_add_one n ih1 ih2 =>
    simp only [U_sub_one, eval_sub, eval_mul, eval_X, eval_ofNat, sub_mul,
      mul_assoc, ih1, ih2, sub_eq_iff_eq_add', sin_add_sin]
    push_cast
    ring_nf

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vieta‚ÄìLucas polynomial) evaluates on
`2 * cos Œ∏` to the value `2 * cos (n * Œ∏)`. -/
@[simp]
theorem C_two_mul_complex_cos (n : ‚Ñ§) : (C ‚ÑÇ n).eval (2 * cos Œ∏) = 2 * cos (n * Œ∏) := by
  simp [C_eq_two_mul_T_comp_half_mul_X]

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vieta‚ÄìFibonacci polynomial)
evaluates on `2 * cos Œ∏` to the value `sin ((n + 1) * Œ∏) / sin Œ∏`. -/
@[simp]
theorem S_two_mul_complex_cos (n : ‚Ñ§) : (S ‚ÑÇ n).eval (2 * cos Œ∏) * sin Œ∏ = sin ((n + 1) * Œ∏) := by
  simp [S_eq_U_comp_half_mul_X]

set_option linter.style.whitespace false in -- manual alignment is not recognised
/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cosh Œ∏` to the
value `cosh (n * Œ∏)`. -/
@[simp]
theorem T_complex_cosh (n : ‚Ñ§) : (T ‚ÑÇ n).eval (cosh Œ∏) = cosh (n * Œ∏) := calc
  (T ‚ÑÇ n).eval (cosh Œ∏)
  _ = (T ‚ÑÇ n).eval (cos (Œ∏ * I))        := by rw [cos_mul_I]
  _ = cos (n * (Œ∏ * I))                 := T_complex_cos (Œ∏ * I) n
  _ = cos (n * Œ∏ * I)                   := by rw [mul_assoc]
  _ = cosh (n * Œ∏)                      := cos_mul_I (n * Œ∏)

set_option linter.style.whitespace false in -- manual alignment is not recognised
/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cosh Œ∏` to the
value `sinh ((n + 1) * Œ∏) / sinh Œ∏`. -/
@[simp]
theorem U_complex_cosh (n : ‚Ñ§) : (U ‚ÑÇ n).eval (cosh Œ∏) * sinh Œ∏ = sinh ((n + 1) * Œ∏) := calc
  (U ‚ÑÇ n).eval (cosh Œ∏) * sinh Œ∏
  _ = (U ‚ÑÇ n).eval (cos (Œ∏ * I)) * sin (Œ∏ * I) * (-I)   := by simp [cos_mul_I, sin_mul_I, mul_assoc]
  _ = sin ((n + 1) * (Œ∏ * I)) * (-I)                    := by rw [U_complex_cos]
  _ = sin ((n + 1) * Œ∏ * I) * (-I)                      := by rw [mul_assoc]
  _ = sinh ((n + 1) * Œ∏)                                := by
    rw [sin_mul_I ((n + 1) * Œ∏), mul_assoc, mul_neg, I_mul_I, neg_neg, mul_one]

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vieta‚ÄìLucas polynomial) evaluates on
`2 * cosh Œ∏` to the value `2 * cosh (n * Œ∏)`. -/
@[simp]
theorem C_two_mul_complex_cosh (n : ‚Ñ§) : (C ‚ÑÇ n).eval (2 * cosh Œ∏) = 2 * cosh (n * Œ∏) := by
  simp [C_eq_two_mul_T_comp_half_mul_X]

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vieta‚ÄìFibonacci polynomial)
evaluates on `2 * cosh Œ∏` to the value `sinh ((n + 1) * Œ∏) / sinh Œ∏`. -/
@[simp]
theorem S_two_mul_complex_cosh (n : ‚Ñ§) : (S ‚ÑÇ n).eval (2 * cosh Œ∏) * sinh Œ∏ =
    sinh ((n + 1) * Œ∏) := by
  simp [S_eq_U_comp_half_mul_X]

end Complex

/-! ### Real versions -/

section Real

open Real

variable (Œ∏ : ‚Ñù) (n : ‚Ñ§)

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cos Œ∏` to the
value `cos (n * Œ∏)`. -/
@[simp]
theorem T_real_cos : (T ‚Ñù n).eval (cos Œ∏) = cos (n * Œ∏) := mod_cast T_complex_cos Œ∏ n

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cos Œ∏` to the
value `sin ((n + 1) * Œ∏) / sin Œ∏`. -/
@[simp]
theorem U_real_cos : (U ‚Ñù n).eval (cos Œ∏) * sin Œ∏ = sin ((n + 1) * Œ∏) :=
  mod_cast U_complex_cos Œ∏ n

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vieta‚ÄìLucas polynomial) evaluates on
`2 * cos Œ∏` to the value `2 * cos (n * Œ∏)`. -/
@[simp]
theorem C_two_mul_real_cos : (C ‚Ñù n).eval (2 * cos Œ∏) = 2 * cos (n * Œ∏) :=
  mod_cast C_two_mul_complex_cos Œ∏ n

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vieta‚ÄìFibonacci polynomial)
evaluates on `2 * cos Œ∏` to the value `sin ((n + 1) * Œ∏) / sin Œ∏`. -/
@[simp]
theorem S_two_mul_real_cos : (S ‚Ñù n).eval (2 * cos Œ∏) * sin Œ∏ = sin ((n + 1) * Œ∏) :=
  mod_cast S_two_mul_complex_cos Œ∏ n

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cosh Œ∏` to the
value `cosh (n * Œ∏)`. -/
@[simp]
theorem T_real_cosh (n : ‚Ñ§) : (T ‚Ñù n).eval (cosh Œ∏) = cosh (n * Œ∏) := mod_cast T_complex_cosh Œ∏ n

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cosh Œ∏` to the
value `sinh ((n + 1) * Œ∏) / sinh Œ∏`. -/
@[simp]
theorem U_real_cosh (n : ‚Ñ§) : (U ‚Ñù n).eval (cosh Œ∏) * sinh Œ∏ = sinh ((n + 1) * Œ∏) :=
  mod_cast U_complex_cosh Œ∏ n

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vieta‚ÄìLucas polynomial) evaluates on
`2 * cosh Œ∏` to the value `2 * cosh (n * Œ∏)`. -/
@[simp]
theorem C_two_mul_real_cosh (n : ‚Ñ§) : (C ‚Ñù n).eval (2 * cosh Œ∏) = 2 * cosh (n * Œ∏) :=
  mod_cast C_two_mul_complex_cosh Œ∏ n

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vieta‚ÄìFibonacci polynomial)
evaluates on `2 * cosh Œ∏` to the value `sinh ((n + 1) * Œ∏) / sinh Œ∏`. -/
@[simp]
theorem S_two_mul_real_cosh (n : ‚Ñ§) : (S ‚Ñù n).eval (2 * cosh Œ∏) * sinh Œ∏ = sinh ((n + 1) * Œ∏) :=
  mod_cast S_two_mul_complex_cosh Œ∏ n

end Real

section Derivative

variable (ùîΩ) [Field ùîΩ] [CharZero ùîΩ]

theorem iterate_derivative_T_real_eval_one (n : ‚Ñ§) (k : ‚Ñï) :
    (derivative^[k] (T ùîΩ n)).eval 1 =
      (‚àè l ‚àà Finset.range k, (n ^ 2 - l ^ 2)) / (‚àè l ‚àà Finset.range k, (2 * l + 1)) := by
  rw [eq_div_iff (Nat.cast_ne_zero.mpr (by positivity)), mul_comm, iterate_derivative_T_eval_one]

theorem iterate_derivative_U_real_eval_one (n : ‚Ñ§) (k : ‚Ñï) :
    (derivative^[k] (U ùîΩ n)).eval 1 =
      ((‚àè l ‚àà Finset.range k, ((n + 1) ^ 2 - (l + 1) ^ 2) : ‚Ñ§) * (n + 1)) /
      (‚àè l ‚àà Finset.range k, (2 * l + 3)) := by
  rw [eq_div_iff (Nat.cast_ne_zero.mpr (by positivity)), mul_comm, iterate_derivative_U_eval_one]

theorem iterate_derivative_T_real_eval_one_dvd (n : ‚Ñ§) (k : ‚Ñï) :
    ‚àè l ‚àà Finset.range k, (2 * l + 1 : ‚Ñ§) ‚à£ ‚àè l ‚àà Finset.range k, (n ^ 2 - l ^ 2) := by
  apply dvd_of_mul_right_eq
  convert iterate_derivative_T_eval_one n k
  simp

theorem iterate_derivative_U_real_eval_one_dvd (n : ‚Ñ§) (k : ‚Ñï) :
    ‚àè l ‚àà Finset.range k, (2 * l + 3 : ‚Ñ§) ‚à£
      (‚àè l ‚àà Finset.range k, ((n + 1) ^ 2 - (l + 1) ^ 2)) * (n + 1) := by
  apply dvd_of_mul_right_eq
  convert iterate_derivative_U_eval_one n k
  simp

theorem derivative_U_real_eval_one (n : ‚Ñ§) :
    (derivative (U ‚Ñù n)).eval 1 = ((n + 2) * (n + 1) * n) / 3 :=
  eq_div_of_mul_eq ((NeZero.ne' 3).symm) ((mul_comm ..).trans (derivative_U_eval_one (R := ‚Ñù) n))

theorem derivative_U_real_eval_one_dvd (n : ‚Ñ§) :
    3 ‚à£ (n + 2) * (n + 1) * n :=
  dvd_of_mul_right_eq _ (derivative_U_eval_one (R := ‚Ñ§) n)

end Derivative

end Polynomial.Chebyshev
