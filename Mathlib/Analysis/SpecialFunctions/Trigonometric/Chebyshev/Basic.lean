/-
Copyright (c) 2020 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
module

public import Mathlib.Analysis.Complex.Trigonometric
public import Mathlib.LinearAlgebra.Complex.Module
public import Mathlib.RingTheory.Polynomial.Chebyshev

/-!
# Multiple angle formulas in terms of Chebyshev polynomials

This file gives the trigonometric characterizations of Chebyshev polynomials, for the real
(`Real.cos`) and complex (`Complex.cos`) cosine and the real (`Real.cosh`) and complex
(`Complex.cosh`) hyperbolic cosine.
-/

public section


namespace Polynomial.Chebyshev

open Polynomial

variable {R A : Type*} [CommRing R] [CommRing A] [Algebra R A]

@[simp, norm_cast]
theorem complex_ofReal_eval_T : âˆ€ (x : â„) n, (((T â„ n).eval x : â„) : â„‚) = (T â„‚ n).eval (x : â„‚) :=
  @algebraMap_eval_T â„ â„‚ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_U : âˆ€ (x : â„) n, (((U â„ n).eval x : â„) : â„‚) = (U â„‚ n).eval (x : â„‚) :=
  @algebraMap_eval_U â„ â„‚ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_C : âˆ€ (x : â„) n, (((C â„ n).eval x : â„) : â„‚) = (C â„‚ n).eval (x : â„‚) :=
  @algebraMap_eval_C â„ â„‚ _ _ _

@[simp, norm_cast]
theorem complex_ofReal_eval_S : âˆ€ (x : â„) n, (((S â„ n).eval x : â„) : â„‚) = (S â„‚ n).eval (x : â„‚) :=
  @algebraMap_eval_S â„ â„‚ _ _ _

/-! ### Complex versions -/

section Complex

open Complex

variable (Î¸ : â„‚)

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cos Î¸` to the
value `cos (n * Î¸)`. -/
@[simp]
theorem T_complex_cos (n : â„¤) : (T â„‚ n).eval (cos Î¸) = cos (n * Î¸) := by
  induction n using Polynomial.Chebyshev.induct with
  | zero => simp
  | one => simp
  | add_two n ih1 ih2 =>
    simp only [T_add_two, eval_sub, eval_mul, eval_X, eval_ofNat, ih1, ih2, sub_eq_iff_eq_add,
      cos_add_cos]
    push_cast
    ring_nf
  | neg_add_one n ih1 ih2 =>
    simp only [T_sub_one, eval_sub, eval_mul, eval_X, eval_ofNat, ih1, ih2, sub_eq_iff_eq_add',
      cos_add_cos]
    push_cast
    ring_nf

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cos Î¸` to the
value `sin ((n + 1) * Î¸) / sin Î¸`. -/
@[simp]
theorem U_complex_cos (n : â„¤) : (U â„‚ n).eval (cos Î¸) * sin Î¸ = sin ((n + 1) * Î¸) := by
  induction n using Polynomial.Chebyshev.induct with
  | zero => simp
  | one => simp [one_add_one_eq_two, sin_two_mul]; ring
  | add_two n ih1 ih2 =>
    simp only [U_add_two, eval_sub, eval_mul, eval_X, eval_ofNat, sub_mul,
      mul_assoc, ih1, ih2, sub_eq_iff_eq_add, sin_add_sin]
    push_cast
    ring_nf
  | neg_add_one n ih1 ih2 =>
    simp only [U_sub_one, eval_sub, eval_mul, eval_X, eval_ofNat, sub_mul,
      mul_assoc, ih1, ih2, sub_eq_iff_eq_add', sin_add_sin]
    push_cast
    ring_nf

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vietaâ€“Lucas polynomial) evaluates on
`2 * cos Î¸` to the value `2 * cos (n * Î¸)`. -/
@[simp]
theorem C_two_mul_complex_cos (n : â„¤) : (C â„‚ n).eval (2 * cos Î¸) = 2 * cos (n * Î¸) := by
  simp [C_eq_two_mul_T_comp_half_mul_X]

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vietaâ€“Fibonacci polynomial)
evaluates on `2 * cos Î¸` to the value `sin ((n + 1) * Î¸) / sin Î¸`. -/
@[simp]
theorem S_two_mul_complex_cos (n : â„¤) : (S â„‚ n).eval (2 * cos Î¸) * sin Î¸ = sin ((n + 1) * Î¸) := by
  simp [S_eq_U_comp_half_mul_X]

set_option linter.style.whitespace false in -- manual alignment is not recognised
/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cosh Î¸` to the
value `cosh (n * Î¸)`. -/
@[simp]
theorem T_complex_cosh (n : â„¤) : (T â„‚ n).eval (cosh Î¸) = cosh (n * Î¸) := calc
  (T â„‚ n).eval (cosh Î¸)
  _ = (T â„‚ n).eval (cos (Î¸ * I))        := by rw [cos_mul_I]
  _ = cos (n * (Î¸ * I))                 := T_complex_cos (Î¸ * I) n
  _ = cos (n * Î¸ * I)                   := by rw [mul_assoc]
  _ = cosh (n * Î¸)                      := cos_mul_I (n * Î¸)

set_option linter.style.whitespace false in -- manual alignment is not recognised
/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cosh Î¸` to the
value `sinh ((n + 1) * Î¸) / sinh Î¸`. -/
@[simp]
theorem U_complex_cosh (n : â„¤) : (U â„‚ n).eval (cosh Î¸) * sinh Î¸ = sinh ((n + 1) * Î¸) := calc
  (U â„‚ n).eval (cosh Î¸) * sinh Î¸
  _ = (U â„‚ n).eval (cos (Î¸ * I)) * sin (Î¸ * I) * (-I)   := by simp [cos_mul_I, sin_mul_I, mul_assoc]
  _ = sin ((n + 1) * (Î¸ * I)) * (-I)                    := by rw [U_complex_cos]
  _ = sin ((n + 1) * Î¸ * I) * (-I)                      := by rw [mul_assoc]
  _ = sinh ((n + 1) * Î¸)                                := by
    rw [sin_mul_I ((n + 1) * Î¸), mul_assoc, mul_neg, I_mul_I, neg_neg, mul_one]

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vietaâ€“Lucas polynomial) evaluates on
`2 * cosh Î¸` to the value `2 * cosh (n * Î¸)`. -/
@[simp]
theorem C_two_mul_complex_cosh (n : â„¤) : (C â„‚ n).eval (2 * cosh Î¸) = 2 * cosh (n * Î¸) := by
  simp [C_eq_two_mul_T_comp_half_mul_X]

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vietaâ€“Fibonacci polynomial)
evaluates on `2 * cosh Î¸` to the value `sinh ((n + 1) * Î¸) / sinh Î¸`. -/
@[simp]
theorem S_two_mul_complex_cosh (n : â„¤) : (S â„‚ n).eval (2 * cosh Î¸) * sinh Î¸ =
    sinh ((n + 1) * Î¸) := by
  simp [S_eq_U_comp_half_mul_X]

end Complex

/-! ### Real versions -/

section Real

open Real

variable (Î¸ : â„) (n : â„¤)

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cos Î¸` to the
value `cos (n * Î¸)`. -/
@[simp]
theorem T_real_cos : (T â„ n).eval (cos Î¸) = cos (n * Î¸) := mod_cast T_complex_cos Î¸ n

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cos Î¸` to the
value `sin ((n + 1) * Î¸) / sin Î¸`. -/
@[simp]
theorem U_real_cos : (U â„ n).eval (cos Î¸) * sin Î¸ = sin ((n + 1) * Î¸) :=
  mod_cast U_complex_cos Î¸ n

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vietaâ€“Lucas polynomial) evaluates on
`2 * cos Î¸` to the value `2 * cos (n * Î¸)`. -/
@[simp]
theorem C_two_mul_real_cos : (C â„ n).eval (2 * cos Î¸) = 2 * cos (n * Î¸) :=
  mod_cast C_two_mul_complex_cos Î¸ n

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vietaâ€“Fibonacci polynomial)
evaluates on `2 * cos Î¸` to the value `sin ((n + 1) * Î¸) / sin Î¸`. -/
@[simp]
theorem S_two_mul_real_cos : (S â„ n).eval (2 * cos Î¸) * sin Î¸ = sin ((n + 1) * Î¸) :=
  mod_cast S_two_mul_complex_cos Î¸ n

/-- The `n`-th Chebyshev polynomial of the first kind evaluates on `cosh Î¸` to the
value `cosh (n * Î¸)`. -/
@[simp]
theorem T_real_cosh (n : â„¤) : (T â„ n).eval (cosh Î¸) = cosh (n * Î¸) := mod_cast T_complex_cosh Î¸ n

/-- The `n`-th Chebyshev polynomial of the second kind evaluates on `cosh Î¸` to the
value `sinh ((n + 1) * Î¸) / sinh Î¸`. -/
@[simp]
theorem U_real_cosh (n : â„¤) : (U â„ n).eval (cosh Î¸) * sinh Î¸ = sinh ((n + 1) * Î¸) :=
  mod_cast U_complex_cosh Î¸ n

/-- The `n`-th rescaled Chebyshev polynomial of the first kind (Vietaâ€“Lucas polynomial) evaluates on
`2 * cosh Î¸` to the value `2 * cosh (n * Î¸)`. -/
@[simp]
theorem C_two_mul_real_cosh (n : â„¤) : (C â„ n).eval (2 * cosh Î¸) = 2 * cosh (n * Î¸) :=
  mod_cast C_two_mul_complex_cosh Î¸ n

/-- The `n`-th rescaled Chebyshev polynomial of the second kind (Vietaâ€“Fibonacci polynomial)
evaluates on `2 * cosh Î¸` to the value `sinh ((n + 1) * Î¸) / sinh Î¸`. -/
@[simp]
theorem S_two_mul_real_cosh (n : â„¤) : (S â„ n).eval (2 * cosh Î¸) * sinh Î¸ = sinh ((n + 1) * Î¸) :=
  mod_cast S_two_mul_complex_cosh Î¸ n

end Real

section Derivative

variable (ğ”½) [Field ğ”½]

theorem iterate_derivative_T_real_eval_one (n : â„¤) (k : â„•) :
    (derivative^[k] (T â„ n)).eval 1 =
      (âˆ l âˆˆ Finset.range k, (n ^ 2 - l ^ 2)) / (âˆ l âˆˆ Finset.range k, (2 * l + 1)) := by
  have h := iterate_derivative_T_eval_one (R := â„) n k
  push_cast at h âŠ¢
  refine eq_div_of_mul_eq (Finset.prod_ne_zero_iff.mpr (fun l hl => ?_)) ((mul_comm ..).trans h)
  norm_cast

theorem iterate_derivative_U_real_eval_one (n : â„¤) (k : â„•) :
    (derivative^[k] (U â„ n)).eval 1 =
      ((âˆ l âˆˆ Finset.range k, ((n + 1) ^ 2 - (l + 1) ^ 2) : â„¤) * (n + 1)) /
      (âˆ l âˆˆ Finset.range k, (2 * l + 3)) := by
  have h := iterate_derivative_U_eval_one (R := â„) n k
  push_cast at h âŠ¢
  refine eq_div_of_mul_eq (Finset.prod_ne_zero_iff.mpr (fun l hl => ?_)) ((mul_comm ..).trans h)
  norm_cast

theorem iterate_derivative_T_real_eval_one_dvd (n : â„¤) (k : â„•) :
    âˆ l âˆˆ Finset.range k, (2 * l + 1 : â„¤) âˆ£ âˆ l âˆˆ Finset.range k, (n ^ 2 - l ^ 2) := by
  have h := iterate_derivative_T_eval_one (R := â„¤) n k
  push_cast at h
  apply dvd_of_mul_right_eq _ h

theorem iterate_derivative_U_real_eval_one_dvd (n : â„¤) (k : â„•) :
    âˆ l âˆˆ Finset.range k, (2 * l + 3 : â„¤) âˆ£
      (âˆ l âˆˆ Finset.range k, ((n + 1) ^ 2 - (l + 1) ^ 2)) * (n + 1) := by
  have h := iterate_derivative_U_eval_one (R := â„¤) n k
  push_cast at h
  apply dvd_of_mul_right_eq _ h

theorem derivative_U_real_eval_one (n : â„¤) :
    (derivative (U â„ n)).eval 1 = ((n + 2) * (n + 1) * n) / 3 :=
  eq_div_of_mul_eq ((NeZero.ne' 3).symm) ((mul_comm ..).trans (derivative_U_eval_one (R := â„) n))

theorem derivative_U_real_eval_one_dvd (n : â„¤) :
    3 âˆ£ (n + 2) * (n + 1) * n :=
  dvd_of_mul_right_eq _ (derivative_U_eval_one (R := â„¤) n)

end Derivative

end Polynomial.Chebyshev
