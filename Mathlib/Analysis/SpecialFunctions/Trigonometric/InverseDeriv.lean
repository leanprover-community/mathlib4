/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Inverse
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv

/-!
# derivatives of the inverse trigonometric functions

Derivatives of `arcsin` and `arccos`.
-/

noncomputable section

open scoped Topology Filter Real ContDiff
open Set

namespace Real

section Arcsin

theorem deriv_arcsin_aux {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
    HasStrictDerivAt arcsin (1 / ‚àö(1 - x ^ 2)) x ‚àß ContDiffAt ‚Ñù œâ arcsin x := by
  rcases h‚ÇÅ.lt_or_gt with h‚ÇÅ | h‚ÇÅ
  ¬∑ have : 1 - x ^ 2 < 0 := by nlinarith [h‚ÇÅ]
    rw [sqrt_eq_zero'.2 this.le, div_zero]
    have : arcsin =·∂†[ùìù x] fun _ => -(œÄ / 2) :=
      (gt_mem_nhds h‚ÇÅ).mono fun y hy => arcsin_of_le_neg_one hy.le
    exact ‚ü®(hasStrictDerivAt_const x _).congr_of_eventuallyEq this.symm,
      contDiffAt_const.congr_of_eventuallyEq this‚ü©
  rcases h‚ÇÇ.lt_or_gt with h‚ÇÇ | h‚ÇÇ
  ¬∑ have : 0 < ‚àö(1 - x ^ 2) := sqrt_pos.2 (by nlinarith [h‚ÇÅ, h‚ÇÇ])
    simp only [‚Üê cos_arcsin, one_div] at this ‚ä¢
    exact ‚ü®sinPartialHomeomorph.hasStrictDerivAt_symm ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© this.ne' (hasStrictDerivAt_sin _),
      sinPartialHomeomorph.contDiffAt_symm_deriv this.ne' ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© (hasDerivAt_sin _)
        contDiff_sin.contDiffAt‚ü©
  ¬∑ have : 1 - x ^ 2 < 0 := by nlinarith [h‚ÇÇ]
    rw [sqrt_eq_zero'.2 this.le, div_zero]
    have : arcsin =·∂†[ùìù x] fun _ => œÄ / 2 := (lt_mem_nhds h‚ÇÇ).mono fun y hy => arcsin_of_one_le hy.le
    exact ‚ü®(hasStrictDerivAt_const x _).congr_of_eventuallyEq this.symm,
      contDiffAt_const.congr_of_eventuallyEq this‚ü©

theorem hasStrictDerivAt_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
    HasStrictDerivAt arcsin (1 / ‚àö(1 - x ^ 2)) x :=
  (deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).1

theorem hasDerivAt_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
    HasDerivAt arcsin (1 / ‚àö(1 - x ^ 2)) x :=
  (hasStrictDerivAt_arcsin h‚ÇÅ h‚ÇÇ).hasDerivAt

theorem contDiffAt_arcsin {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : WithTop ‚Ñï‚àû} :
    ContDiffAt ‚Ñù n arcsin x :=
  (deriv_arcsin_aux h‚ÇÅ h‚ÇÇ).2.of_le le_top

theorem hasDerivWithinAt_arcsin_Ici {x : ‚Ñù} (h : x ‚â† -1) :
    HasDerivWithinAt arcsin (1 / ‚àö(1 - x ^ 2)) (Ici x) x := by
  rcases eq_or_ne x 1 with (rfl | h')
  ¬∑ convert (hasDerivWithinAt_const (1 : ‚Ñù) _ (œÄ / 2)).congr _ _ <;>
      simp +contextual [arcsin_of_one_le]
  ¬∑ exact (hasDerivAt_arcsin h h').hasDerivWithinAt

theorem hasDerivWithinAt_arcsin_Iic {x : ‚Ñù} (h : x ‚â† 1) :
    HasDerivWithinAt arcsin (1 / ‚àö(1 - x ^ 2)) (Iic x) x := by
  rcases em (x = -1) with (rfl | h')
  ¬∑ convert (hasDerivWithinAt_const (-1 : ‚Ñù) _ (-(œÄ / 2))).congr _ _ <;>
      simp +contextual [arcsin_of_le_neg_one]
  ¬∑ exact (hasDerivAt_arcsin h' h).hasDerivWithinAt

theorem differentiableWithinAt_arcsin_Ici {x : ‚Ñù} :
    DifferentiableWithinAt ‚Ñù arcsin (Ici x) x ‚Üî x ‚â† -1 := by
  refine ‚ü®?_, fun h => (hasDerivWithinAt_arcsin_Ici h).differentiableWithinAt‚ü©
  rintro h rfl
  have : sin ‚àò arcsin =·∂†[ùìù[‚â•] (-1 : ‚Ñù)] id := by
    filter_upwards [Icc_mem_nhdsGE (neg_lt_self zero_lt_one)] with x using sin_arcsin'
  have := h.hasDerivWithinAt.sin.congr_of_eventuallyEq this.symm (by simp)
  simpa using (uniqueDiffOn_Ici _ _ left_mem_Ici).eq_deriv _ this (hasDerivWithinAt_id _ _)

theorem differentiableWithinAt_arcsin_Iic {x : ‚Ñù} :
    DifferentiableWithinAt ‚Ñù arcsin (Iic x) x ‚Üî x ‚â† 1 := by
  refine ‚ü®fun h => ?_, fun h => (hasDerivWithinAt_arcsin_Iic h).differentiableWithinAt‚ü©
  rw [‚Üê neg_neg x, ‚Üê image_neg_Ici] at h
  have := (h.comp (-x) differentiableWithinAt_id.fun_neg (mapsTo_image _ _)).fun_neg
  simpa [(¬∑ ‚àò ¬∑), differentiableWithinAt_arcsin_Ici] using this

theorem differentiableAt_arcsin {x : ‚Ñù} : DifferentiableAt ‚Ñù arcsin x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
  ‚ü®fun h => ‚ü®differentiableWithinAt_arcsin_Ici.1 h.differentiableWithinAt,
      differentiableWithinAt_arcsin_Iic.1 h.differentiableWithinAt‚ü©,
    fun h => (hasDerivAt_arcsin h.1 h.2).differentiableAt‚ü©

@[simp]
theorem deriv_arcsin : deriv arcsin = fun x => 1 / ‚àö(1 - x ^ 2) := by
  funext x
  by_cases h : x ‚â† -1 ‚àß x ‚â† 1
  ¬∑ exact (hasDerivAt_arcsin h.1 h.2).deriv
  ¬∑ rw [deriv_zero_of_not_differentiableAt (mt differentiableAt_arcsin.1 h)]
    simp only [not_and_or, Ne, Classical.not_not] at h
    rcases h with (rfl | rfl) <;> simp

theorem differentiableOn_arcsin : DifferentiableOn ‚Ñù arcsin {-1, 1}·∂ú := fun _x hx =>
  (differentiableAt_arcsin.2
      ‚ü®fun h => hx (Or.inl h), fun h => hx (Or.inr h)‚ü©).differentiableWithinAt

theorem contDiffOn_arcsin {n : WithTop ‚Ñï‚àû} : ContDiffOn ‚Ñù n arcsin {-1, 1}·∂ú := fun _x hx =>
  (contDiffAt_arcsin (mt Or.inl hx) (mt Or.inr hx)).contDiffWithinAt

theorem contDiffAt_arcsin_iff {x : ‚Ñù} {n : WithTop ‚Ñï‚àû} :
    ContDiffAt ‚Ñù n arcsin x ‚Üî n = 0 ‚à® x ‚â† -1 ‚àß x ‚â† 1 :=
  ‚ü®fun h => or_iff_not_imp_left.2 fun hn => differentiableAt_arcsin.1 <| h.differentiableAt <|
      ENat.one_le_iff_ne_zero_withTop.mpr hn,
    fun h => h.elim (fun hn => hn.symm ‚ñ∏ (contDiff_zero.2 continuous_arcsin).contDiffAt) fun hx =>
      contDiffAt_arcsin hx.1 hx.2‚ü©

end Arcsin

section Arccos

theorem hasStrictDerivAt_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
    HasStrictDerivAt arccos (-(1 / ‚àö(1 - x ^ 2))) x :=
  (hasStrictDerivAt_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

theorem hasDerivAt_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) :
    HasDerivAt arccos (-(1 / ‚àö(1 - x ^ 2))) x :=
  (hasDerivAt_arcsin h‚ÇÅ h‚ÇÇ).const_sub (œÄ / 2)

theorem contDiffAt_arccos {x : ‚Ñù} (h‚ÇÅ : x ‚â† -1) (h‚ÇÇ : x ‚â† 1) {n : WithTop ‚Ñï‚àû} :
    ContDiffAt ‚Ñù n arccos x :=
  contDiffAt_const.sub (contDiffAt_arcsin h‚ÇÅ h‚ÇÇ)

theorem hasDerivWithinAt_arccos_Ici {x : ‚Ñù} (h : x ‚â† -1) :
    HasDerivWithinAt arccos (-(1 / ‚àö(1 - x ^ 2))) (Ici x) x :=
  (hasDerivWithinAt_arcsin_Ici h).const_sub _

theorem hasDerivWithinAt_arccos_Iic {x : ‚Ñù} (h : x ‚â† 1) :
    HasDerivWithinAt arccos (-(1 / ‚àö(1 - x ^ 2))) (Iic x) x :=
  (hasDerivWithinAt_arcsin_Iic h).const_sub _

theorem differentiableWithinAt_arccos_Ici {x : ‚Ñù} :
    DifferentiableWithinAt ‚Ñù arccos (Ici x) x ‚Üî x ‚â† -1 :=
  (differentiableWithinAt_const_sub_iff _).trans differentiableWithinAt_arcsin_Ici

theorem differentiableWithinAt_arccos_Iic {x : ‚Ñù} :
    DifferentiableWithinAt ‚Ñù arccos (Iic x) x ‚Üî x ‚â† 1 :=
  (differentiableWithinAt_const_sub_iff _).trans differentiableWithinAt_arcsin_Iic

theorem differentiableAt_arccos {x : ‚Ñù} : DifferentiableAt ‚Ñù arccos x ‚Üî x ‚â† -1 ‚àß x ‚â† 1 :=
  (differentiableAt_const _).sub_iff_right.trans differentiableAt_arcsin

@[simp]
theorem deriv_arccos : deriv arccos = fun x => -(1 / ‚àö(1 - x ^ 2)) :=
  funext fun x => (deriv_const_sub _).trans <| by simp only [deriv_arcsin]

theorem differentiableOn_arccos : DifferentiableOn ‚Ñù arccos {-1, 1}·∂ú :=
  differentiableOn_arcsin.const_sub _

theorem contDiffOn_arccos {n : WithTop ‚Ñï‚àû} : ContDiffOn ‚Ñù n arccos {-1, 1}·∂ú :=
  contDiffOn_const.sub contDiffOn_arcsin

theorem contDiffAt_arccos_iff {x : ‚Ñù} {n : WithTop ‚Ñï‚àû} :
    ContDiffAt ‚Ñù n arccos x ‚Üî n = 0 ‚à® x ‚â† -1 ‚àß x ‚â† 1 := by
  refine Iff.trans ‚ü®fun h => ?_, fun h => ?_‚ü© contDiffAt_arcsin_iff <;>
    simpa [arccos] using (contDiffAt_const (c := œÄ / 2)).sub h

end Arccos

end Real
