/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
module

public import Mathlib.Order.Monotone.Odd
public import Mathlib.Analysis.Calculus.LogDeriv
public import Mathlib.Analysis.SpecialFunctions.ExpDeriv
public import Mathlib.Analysis.SpecialFunctions.Trigonometric.Basic
public import Mathlib.Analysis.Calculus.Deriv.MeanValue

/-!
# Differentiability of hyperbolic trigonometric functions

## Main statements

The differentiability of the hyperbolic trigonometric functions is proved, and their derivatives are
computed.

## Tags

sinh, cosh, tanh
-/

@[expose] public section

noncomputable section

open scoped Asymptotics Topology Filter
open Set

namespace Complex

/-- The complex hyperbolic sine function is everywhere strictly differentiable, with the derivative
`cosh x`. -/
theorem hasStrictDerivAt_sinh (x : ‚ÑÇ) : HasStrictDerivAt sinh (cosh x) x := by
  simp only [cosh, div_eq_mul_inv]
  convert ((hasStrictDerivAt_exp x).sub (hasStrictDerivAt_id x).fun_neg.cexp).mul_const (2 : ‚ÑÇ)‚Åª¬π
    using 1
  rw [id, mul_neg_one, sub_eq_add_neg, neg_neg]

/-- The complex hyperbolic sine function is everywhere differentiable, with the derivative
`cosh x`. -/
theorem hasDerivAt_sinh (x : ‚ÑÇ) : HasDerivAt sinh (cosh x) x :=
  (hasStrictDerivAt_sinh x).hasDerivAt

theorem isEquivalent_sinh : sinh ~[ùìù 0] id := by simpa using (hasDerivAt_sinh 0).isLittleO

@[fun_prop]
theorem contDiff_sinh {n} : ContDiff ‚ÑÇ n sinh :=
  (contDiff_exp.sub contDiff_neg.cexp).div_const _

@[simp]
theorem differentiable_sinh : Differentiable ‚ÑÇ sinh := fun x => (hasDerivAt_sinh x).differentiableAt

@[simp]
theorem differentiableAt_sinh {x : ‚ÑÇ} : DifferentiableAt ‚ÑÇ sinh x :=
  differentiable_sinh x

/-- The function `Complex.sinh` is complex analytic. -/
@[fun_prop]
lemma analyticAt_sinh {x : ‚ÑÇ} : AnalyticAt ‚ÑÇ sinh x :=
  contDiff_sinh.contDiffAt.analyticAt

/-- The function `Complex.sinh` is complex analytic. -/
lemma analyticWithinAt_sinh {x : ‚ÑÇ} {s : Set ‚ÑÇ} : AnalyticWithinAt ‚ÑÇ sinh s x :=
  contDiff_sinh.contDiffWithinAt.analyticWithinAt

/-- The function `Complex.sinh` is complex analytic. -/
theorem analyticOnNhd_sinh {s : Set ‚ÑÇ} : AnalyticOnNhd ‚ÑÇ sinh s :=
  fun _ _ ‚Ü¶ analyticAt_sinh

/-- The function `Complex.sinh` is complex analytic. -/
lemma analyticOn_sinh {s : Set ‚ÑÇ} : AnalyticOn ‚ÑÇ sinh s :=
  contDiff_sinh.contDiffOn.analyticOn

@[simp]
theorem deriv_sinh : deriv sinh = cosh :=
  funext fun x => (hasDerivAt_sinh x).deriv

/-- The complex hyperbolic cosine function is everywhere strictly differentiable, with the
derivative `sinh x`. -/
theorem hasStrictDerivAt_cosh (x : ‚ÑÇ) : HasStrictDerivAt cosh (sinh x) x := by
  simp only [sinh, div_eq_mul_inv]
  convert ((hasStrictDerivAt_exp x).add (hasStrictDerivAt_id x).fun_neg.cexp).mul_const (2 : ‚ÑÇ)‚Åª¬π
    using 1
  rw [id, mul_neg_one, sub_eq_add_neg]

/-- The complex hyperbolic cosine function is everywhere differentiable, with the derivative
`sinh x`. -/
theorem hasDerivAt_cosh (x : ‚ÑÇ) : HasDerivAt cosh (sinh x) x :=
  (hasStrictDerivAt_cosh x).hasDerivAt

@[fun_prop]
theorem contDiff_cosh {n} : ContDiff ‚ÑÇ n cosh :=
  (contDiff_exp.add contDiff_neg.cexp).div_const _

@[simp]
theorem differentiable_cosh : Differentiable ‚ÑÇ cosh := fun x => (hasDerivAt_cosh x).differentiableAt

@[simp]
theorem differentiableAt_cosh {x : ‚ÑÇ} : DifferentiableAt ‚ÑÇ cosh x :=
  differentiable_cosh x

/-- The function `Complex.cosh` is complex analytic. -/
@[fun_prop]
lemma analyticAt_cosh {x : ‚ÑÇ} : AnalyticAt ‚ÑÇ cosh x :=
  contDiff_cosh.contDiffAt.analyticAt

/-- The function `Complex.cosh` is complex analytic. -/
lemma analyticWithinAt_cosh {x : ‚ÑÇ} {s : Set ‚ÑÇ} : AnalyticWithinAt ‚ÑÇ cosh s x :=
  contDiff_cosh.contDiffWithinAt.analyticWithinAt

/-- The function `Complex.cosh` is complex analytic. -/
theorem analyticOnNhd_cosh {s : Set ‚ÑÇ} : AnalyticOnNhd ‚ÑÇ cosh s :=
  fun _ _ ‚Ü¶ analyticAt_cosh

/-- The function `Complex.cosh` is complex analytic. -/
lemma analyticOn_cosh {s : Set ‚ÑÇ} : AnalyticOn ‚ÑÇ cosh s :=
  contDiff_cosh.contDiffOn.analyticOn

@[simp]
theorem deriv_cosh : deriv cosh = sinh :=
  funext fun x => (hasDerivAt_cosh x).deriv

end Complex

section

/-! ### Simp lemmas for derivatives of `fun x => Complex.cos (f x)` etc., `f : ‚ÑÇ ‚Üí ‚ÑÇ` -/

variable {f : ‚ÑÇ ‚Üí ‚ÑÇ} {f' x : ‚ÑÇ} {s : Set ‚ÑÇ}

/-! #### `Complex.cosh` -/

theorem HasStrictDerivAt.ccosh (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) * f') x :=
  (Complex.hasStrictDerivAt_cosh (f x)).comp x hf

theorem HasDerivAt.ccosh (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) * f') x :=
  (Complex.hasDerivAt_cosh (f x)).comp x hf

theorem HasDerivWithinAt.ccosh (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) * f') s x :=
  (Complex.hasDerivAt_cosh (f x)).comp_hasDerivWithinAt x hf

theorem derivWithin_ccosh (hf : DifferentiableWithinAt ‚ÑÇ f s x) (hxs : UniqueDiffWithinAt ‚ÑÇ s x) :
    derivWithin (fun x => Complex.cosh (f x)) s x = Complex.sinh (f x) * derivWithin f s x :=
  hf.hasDerivWithinAt.ccosh.derivWithin hxs

@[simp]
theorem deriv_ccosh (hc : DifferentiableAt ‚ÑÇ f x) :
    deriv (fun x => Complex.cosh (f x)) x = Complex.sinh (f x) * deriv f x :=
  hc.hasDerivAt.ccosh.deriv

/-! #### `Complex.sinh` -/

theorem HasStrictDerivAt.csinh (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) * f') x :=
  (Complex.hasStrictDerivAt_sinh (f x)).comp x hf

theorem HasDerivAt.csinh (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) * f') x :=
  (Complex.hasDerivAt_sinh (f x)).comp x hf

theorem HasDerivWithinAt.csinh (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) * f') s x :=
  (Complex.hasDerivAt_sinh (f x)).comp_hasDerivWithinAt x hf

theorem derivWithin_csinh (hf : DifferentiableWithinAt ‚ÑÇ f s x) (hxs : UniqueDiffWithinAt ‚ÑÇ s x) :
    derivWithin (fun x => Complex.sinh (f x)) s x = Complex.cosh (f x) * derivWithin f s x :=
  hf.hasDerivWithinAt.csinh.derivWithin hxs

@[simp]
theorem deriv_csinh (hc : DifferentiableAt ‚ÑÇ f x) :
    deriv (fun x => Complex.sinh (f x)) x = Complex.cosh (f x) * deriv f x :=
  hc.hasDerivAt.csinh.deriv

end

section

/-! ### Simp lemmas for derivatives of `fun x => Complex.cos (f x)` etc., `f : E ‚Üí ‚ÑÇ` -/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : E ‚Üí ‚ÑÇ} {f' : StrongDual ‚ÑÇ E}
  {x : E} {s : Set E}

/-! #### `Complex.cosh` -/

theorem HasStrictFDerivAt.ccosh (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) ‚Ä¢ f') x :=
  (Complex.hasStrictDerivAt_cosh (f x)).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.ccosh (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) ‚Ä¢ f') x :=
  (Complex.hasDerivAt_cosh (f x)).comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.ccosh (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => Complex.cosh (f x)) (Complex.sinh (f x) ‚Ä¢ f') s x :=
  (Complex.hasDerivAt_cosh (f x)).comp_hasFDerivWithinAt x hf

theorem DifferentiableWithinAt.ccosh (hf : DifferentiableWithinAt ‚ÑÇ f s x) :
    DifferentiableWithinAt ‚ÑÇ (fun x => Complex.cosh (f x)) s x :=
  hf.hasFDerivWithinAt.ccosh.differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.ccosh (hc : DifferentiableAt ‚ÑÇ f x) :
    DifferentiableAt ‚ÑÇ (fun x => Complex.cosh (f x)) x :=
  hc.hasFDerivAt.ccosh.differentiableAt

theorem DifferentiableOn.ccosh (hc : DifferentiableOn ‚ÑÇ f s) :
    DifferentiableOn ‚ÑÇ (fun x => Complex.cosh (f x)) s := fun x h => (hc x h).ccosh

@[simp, fun_prop]
theorem Differentiable.ccosh (hc : Differentiable ‚ÑÇ f) :
    Differentiable ‚ÑÇ fun x => Complex.cosh (f x) := fun x => (hc x).ccosh

theorem fderivWithin_ccosh (hf : DifferentiableWithinAt ‚ÑÇ f s x) (hxs : UniqueDiffWithinAt ‚ÑÇ s x) :
    fderivWithin ‚ÑÇ (fun x => Complex.cosh (f x)) s x = Complex.sinh (f x) ‚Ä¢ fderivWithin ‚ÑÇ f s x :=
  hf.hasFDerivWithinAt.ccosh.fderivWithin hxs

@[simp]
theorem fderiv_ccosh (hc : DifferentiableAt ‚ÑÇ f x) :
    fderiv ‚ÑÇ (fun x => Complex.cosh (f x)) x = Complex.sinh (f x) ‚Ä¢ fderiv ‚ÑÇ f x :=
  hc.hasFDerivAt.ccosh.fderiv

theorem ContDiff.ccosh {n} (h : ContDiff ‚ÑÇ n f) : ContDiff ‚ÑÇ n fun x => Complex.cosh (f x) :=
  Complex.contDiff_cosh.comp h

theorem ContDiffAt.ccosh {n} (hf : ContDiffAt ‚ÑÇ n f x) :
    ContDiffAt ‚ÑÇ n (fun x => Complex.cosh (f x)) x :=
  Complex.contDiff_cosh.contDiffAt.comp x hf

theorem ContDiffOn.ccosh {n} (hf : ContDiffOn ‚ÑÇ n f s) :
    ContDiffOn ‚ÑÇ n (fun x => Complex.cosh (f x)) s :=
  Complex.contDiff_cosh.comp_contDiffOn hf

theorem ContDiffWithinAt.ccosh {n} (hf : ContDiffWithinAt ‚ÑÇ n f s x) :
    ContDiffWithinAt ‚ÑÇ n (fun x => Complex.cosh (f x)) s x :=
  Complex.contDiff_cosh.contDiffAt.comp_contDiffWithinAt x hf

/-! #### `Complex.sinh` -/

theorem HasStrictFDerivAt.csinh (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) ‚Ä¢ f') x :=
  (Complex.hasStrictDerivAt_sinh (f x)).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.csinh (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) ‚Ä¢ f') x :=
  (Complex.hasDerivAt_sinh (f x)).comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.csinh (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => Complex.sinh (f x)) (Complex.cosh (f x) ‚Ä¢ f') s x :=
  (Complex.hasDerivAt_sinh (f x)).comp_hasFDerivWithinAt x hf

theorem DifferentiableWithinAt.csinh (hf : DifferentiableWithinAt ‚ÑÇ f s x) :
    DifferentiableWithinAt ‚ÑÇ (fun x => Complex.sinh (f x)) s x :=
  hf.hasFDerivWithinAt.csinh.differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.csinh (hc : DifferentiableAt ‚ÑÇ f x) :
    DifferentiableAt ‚ÑÇ (fun x => Complex.sinh (f x)) x :=
  hc.hasFDerivAt.csinh.differentiableAt

theorem DifferentiableOn.csinh (hc : DifferentiableOn ‚ÑÇ f s) :
    DifferentiableOn ‚ÑÇ (fun x => Complex.sinh (f x)) s := fun x h => (hc x h).csinh

@[simp, fun_prop]
theorem Differentiable.csinh (hc : Differentiable ‚ÑÇ f) :
    Differentiable ‚ÑÇ fun x => Complex.sinh (f x) := fun x => (hc x).csinh

theorem fderivWithin_csinh (hf : DifferentiableWithinAt ‚ÑÇ f s x) (hxs : UniqueDiffWithinAt ‚ÑÇ s x) :
    fderivWithin ‚ÑÇ (fun x => Complex.sinh (f x)) s x = Complex.cosh (f x) ‚Ä¢ fderivWithin ‚ÑÇ f s x :=
  hf.hasFDerivWithinAt.csinh.fderivWithin hxs

@[simp]
theorem fderiv_csinh (hc : DifferentiableAt ‚ÑÇ f x) :
    fderiv ‚ÑÇ (fun x => Complex.sinh (f x)) x = Complex.cosh (f x) ‚Ä¢ fderiv ‚ÑÇ f x :=
  hc.hasFDerivAt.csinh.fderiv

theorem ContDiff.csinh {n} (h : ContDiff ‚ÑÇ n f) : ContDiff ‚ÑÇ n fun x => Complex.sinh (f x) :=
  Complex.contDiff_sinh.comp h

theorem ContDiffAt.csinh {n} (hf : ContDiffAt ‚ÑÇ n f x) :
    ContDiffAt ‚ÑÇ n (fun x => Complex.sinh (f x)) x :=
  Complex.contDiff_sinh.contDiffAt.comp x hf

theorem ContDiffOn.csinh {n} (hf : ContDiffOn ‚ÑÇ n f s) :
    ContDiffOn ‚ÑÇ n (fun x => Complex.sinh (f x)) s :=
  Complex.contDiff_sinh.comp_contDiffOn hf

theorem ContDiffWithinAt.csinh {n} (hf : ContDiffWithinAt ‚ÑÇ n f s x) :
    ContDiffWithinAt ‚ÑÇ n (fun x => Complex.sinh (f x)) s x :=
  Complex.contDiff_sinh.contDiffAt.comp_contDiffWithinAt x hf

end

namespace Real

variable {x y z : ‚Ñù}

theorem hasStrictDerivAt_sinh (x : ‚Ñù) : HasStrictDerivAt sinh (cosh x) x :=
  (Complex.hasStrictDerivAt_sinh x).real_of_complex

theorem hasDerivAt_sinh (x : ‚Ñù) : HasDerivAt sinh (cosh x) x :=
  (Complex.hasDerivAt_sinh x).real_of_complex

theorem isEquivalent_sinh : sinh ~[ùìù 0] id := by simpa using (hasDerivAt_sinh 0).isLittleO

@[fun_prop]
theorem contDiff_sinh {n} : ContDiff ‚Ñù n sinh :=
  Complex.contDiff_sinh.real_of_complex

@[simp]
theorem differentiable_sinh : Differentiable ‚Ñù sinh := fun x => (hasDerivAt_sinh x).differentiableAt

@[simp]
theorem differentiableAt_sinh : DifferentiableAt ‚Ñù sinh x :=
  differentiable_sinh x

/-- The function `Real.sinh` is real analytic. -/
@[fun_prop]
lemma analyticAt_sinh : AnalyticAt ‚Ñù sinh x :=
  contDiff_sinh.contDiffAt.analyticAt

/-- The function `Real.sinh` is real analytic. -/
lemma analyticWithinAt_sinh {s : Set ‚Ñù} : AnalyticWithinAt ‚Ñù sinh s x :=
  contDiff_sinh.contDiffWithinAt.analyticWithinAt

/-- The function `Real.sinh` is real analytic. -/
theorem analyticOnNhd_sinh {s : Set ‚Ñù} : AnalyticOnNhd ‚Ñù sinh s :=
  fun _ _ ‚Ü¶ analyticAt_sinh

/-- The function `Real.sinh` is real analytic. -/
lemma analyticOn_sinh {s : Set ‚Ñù} : AnalyticOn ‚Ñù sinh s :=
  contDiff_sinh.contDiffOn.analyticOn

@[simp]
theorem deriv_sinh : deriv sinh = cosh :=
  funext fun x => (hasDerivAt_sinh x).deriv

theorem hasStrictDerivAt_cosh (x : ‚Ñù) : HasStrictDerivAt cosh (sinh x) x :=
  (Complex.hasStrictDerivAt_cosh x).real_of_complex

theorem hasDerivAt_cosh (x : ‚Ñù) : HasDerivAt cosh (sinh x) x :=
  (Complex.hasDerivAt_cosh x).real_of_complex

@[fun_prop]
theorem contDiff_cosh {n} : ContDiff ‚Ñù n cosh :=
  Complex.contDiff_cosh.real_of_complex

@[simp]
theorem differentiable_cosh : Differentiable ‚Ñù cosh := fun x => (hasDerivAt_cosh x).differentiableAt

@[simp]
theorem differentiableAt_cosh : DifferentiableAt ‚Ñù cosh x :=
  differentiable_cosh x

/-- The function `Real.cosh` is real analytic. -/
@[fun_prop]
lemma analyticAt_cosh : AnalyticAt ‚Ñù cosh x :=
  contDiff_cosh.contDiffAt.analyticAt

/-- The function `Real.cosh` is real analytic. -/
lemma analyticWithinAt_cosh {s : Set ‚Ñù} : AnalyticWithinAt ‚Ñù cosh s x :=
  contDiff_cosh.contDiffWithinAt.analyticWithinAt

/-- The function `Real.cosh` is real analytic. -/
theorem analyticOnNhd_cosh {s : Set ‚Ñù} : AnalyticOnNhd ‚Ñù cosh s :=
  fun _ _ ‚Ü¶ analyticAt_cosh

/-- The function `Real.cosh` is real analytic. -/
lemma analyticOn_cosh {s : Set ‚Ñù} : AnalyticOn ‚Ñù cosh s :=
  contDiff_cosh.contDiffOn.analyticOn

@[simp]
theorem deriv_cosh : deriv cosh = sinh :=
  funext fun x => (hasDerivAt_cosh x).deriv

/-- `sinh` is strictly monotone. -/
theorem sinh_strictMono : StrictMono sinh :=
  strictMono_of_deriv_pos <| by rw [Real.deriv_sinh]; exact cosh_pos

/-- `sinh` is injective, `‚àÄ a b, sinh a = sinh b ‚Üí a = b`. -/
theorem sinh_injective : Function.Injective sinh :=
  sinh_strictMono.injective

@[simp]
theorem sinh_inj : sinh x = sinh y ‚Üî x = y :=
  sinh_injective.eq_iff

@[simp]
theorem sinh_le_sinh : sinh x ‚â§ sinh y ‚Üî x ‚â§ y :=
  sinh_strictMono.le_iff_le

@[simp]
theorem sinh_lt_sinh : sinh x < sinh y ‚Üî x < y :=
  sinh_strictMono.lt_iff_lt

@[simp] lemma sinh_eq_zero : sinh x = 0 ‚Üî x = 0 := by rw [‚Üê @sinh_inj x, sinh_zero]

lemma sinh_ne_zero : sinh x ‚â† 0 ‚Üî x ‚â† 0 := sinh_eq_zero.not

@[simp]
theorem sinh_pos_iff : 0 < sinh x ‚Üî 0 < x := by simpa only [sinh_zero] using @sinh_lt_sinh 0 x

@[simp]
theorem sinh_nonpos_iff : sinh x ‚â§ 0 ‚Üî x ‚â§ 0 := by simpa only [sinh_zero] using @sinh_le_sinh x 0

@[simp]
theorem sinh_neg_iff : sinh x < 0 ‚Üî x < 0 := by simpa only [sinh_zero] using @sinh_lt_sinh x 0

@[simp]
theorem sinh_nonneg_iff : 0 ‚â§ sinh x ‚Üî 0 ‚â§ x := by simpa only [sinh_zero] using @sinh_le_sinh 0 x

theorem abs_sinh (x : ‚Ñù) : |sinh x| = sinh |x| := by
  cases le_total x 0 <;> simp [abs_of_nonneg, abs_of_nonpos, *]

theorem cosh_strictMonoOn : StrictMonoOn cosh (Ici 0) :=
  strictMonoOn_of_deriv_pos (convex_Ici _) continuous_cosh.continuousOn fun x hx => by
    rw [interior_Ici, mem_Ioi] at hx; rwa [deriv_cosh, sinh_pos_iff]

@[simp]
theorem cosh_le_cosh : cosh x ‚â§ cosh y ‚Üî |x| ‚â§ |y| :=
  cosh_abs x ‚ñ∏ cosh_abs y ‚ñ∏ cosh_strictMonoOn.le_iff_le (abs_nonneg x) (abs_nonneg y)

@[simp]
theorem cosh_lt_cosh : cosh x < cosh y ‚Üî |x| < |y| :=
  lt_iff_lt_of_le_iff_le cosh_le_cosh

@[simp]
theorem one_le_cosh (x : ‚Ñù) : 1 ‚â§ cosh x :=
  cosh_zero ‚ñ∏ cosh_le_cosh.2 (by simp only [_root_.abs_zero, _root_.abs_nonneg])

@[simp]
theorem one_lt_cosh : 1 < cosh x ‚Üî x ‚â† 0 :=
  cosh_zero ‚ñ∏ cosh_lt_cosh.trans (by simp only [_root_.abs_zero, abs_pos])

theorem sinh_sub_id_strictMono : StrictMono fun x => sinh x - x := by
  refine strictMono_of_odd_strictMonoOn_nonneg (fun x => by simp; abel) ?_
  refine strictMonoOn_of_deriv_pos (convex_Ici _) ?_ fun x hx => ?_
  ¬∑ exact (continuous_sinh.sub continuous_id).continuousOn
  ¬∑ rw [interior_Ici, mem_Ioi] at hx
    rw [deriv_fun_sub, deriv_sinh, deriv_id'', sub_pos, one_lt_cosh]
    exacts [hx.ne', differentiableAt_sinh, differentiableAt_id]

@[simp]
theorem self_le_sinh_iff : x ‚â§ sinh x ‚Üî 0 ‚â§ x :=
  calc
    x ‚â§ sinh x ‚Üî sinh 0 - 0 ‚â§ sinh x - x := by simp
    _ ‚Üî 0 ‚â§ x := sinh_sub_id_strictMono.le_iff_le

@[simp]
theorem sinh_le_self_iff : sinh x ‚â§ x ‚Üî x ‚â§ 0 :=
  calc
    sinh x ‚â§ x ‚Üî sinh x - x ‚â§ sinh 0 - 0 := by simp
    _ ‚Üî x ‚â§ 0 := sinh_sub_id_strictMono.le_iff_le

@[simp]
theorem self_lt_sinh_iff : x < sinh x ‚Üî 0 < x :=
  lt_iff_lt_of_le_iff_le sinh_le_self_iff

@[simp]
theorem sinh_lt_self_iff : sinh x < x ‚Üî x < 0 :=
  lt_iff_lt_of_le_iff_le self_le_sinh_iff

end Real

section iteratedDeriv

/-! ### Simp lemmas for iterated derivatives of `sinh` and `cosh`. -/

namespace Complex

@[simp]
theorem iteratedDeriv_add_one_sinh (n : ‚Ñï) :
    iteratedDeriv (n + 1) sinh = iteratedDeriv n cosh := by
  induction n with
  | zero => simp
  | succ n ih =>
    rw [iteratedDeriv_succ, ih, iteratedDeriv_succ]

@[simp]
theorem iteratedDeriv_add_one_cosh (n : ‚Ñï) :
    iteratedDeriv (n + 1) cosh = iteratedDeriv n sinh := by
  induction n with
  | zero => ext; simp
  | succ n ih =>
    rw [iteratedDeriv_succ, ih, iteratedDeriv_succ]

@[simp]
theorem iteratedDeriv_even_sinh (n : ‚Ñï) :
    iteratedDeriv (2 * n) sinh = sinh := by
  induction n with
  | zero => simp
  | succ n ih => simp_all [mul_add]

@[simp]
theorem iteratedDeriv_even_cosh (n : ‚Ñï) :
    iteratedDeriv (2 * n) cosh = cosh := by
  induction n with
  | zero => simp
  | succ n ih => simp_all [mul_add]

theorem iteratedDeriv_odd_sinh (n : ‚Ñï) :
    iteratedDeriv (2 * n + 1) sinh = cosh := by simp

theorem iteratedDeriv_odd_cosh (n : ‚Ñï) :
    iteratedDeriv (2 * n + 1) cosh = sinh := by simp

theorem differentiable_iteratedDeriv_sinh (n : ‚Ñï) :
    Differentiable ‚ÑÇ (iteratedDeriv n sinh) :=
  match n with
  | 0 => by simp
  | 1 => by simp
  | n + 2 => by simp [differentiable_iteratedDeriv_sinh]

theorem differentiable_iteratedDeriv_cosh (n : ‚Ñï) :
    Differentiable ‚ÑÇ (iteratedDeriv n cosh) :=
  match n with
  | 0 => by simp
  | 1 => by simp
  | n + 2 => by simp [differentiable_iteratedDeriv_cosh]

end Complex

namespace Real

@[simp]
theorem iteratedDeriv_add_one_sinh (n : ‚Ñï) :
    iteratedDeriv (n + 1) sinh = iteratedDeriv n cosh := by
  induction n with
  | zero => simp
  | succ n ih =>
    rw [iteratedDeriv_succ, ih, iteratedDeriv_succ]

@[simp]
theorem iteratedDeriv_add_one_cosh (n : ‚Ñï) :
    iteratedDeriv (n + 1) cosh = iteratedDeriv n sinh := by
  induction n with
  | zero => ext; simp
  | succ n ih =>
    rw [iteratedDeriv_succ, ih, iteratedDeriv_succ]

@[simp]
theorem iteratedDeriv_even_sinh (n : ‚Ñï) :
    iteratedDeriv (2 * n) sinh = sinh := by
  induction n with
  | zero => simp
  | succ n ih => simp_all [mul_add]

@[simp]
theorem iteratedDeriv_even_cosh (n : ‚Ñï) :
    iteratedDeriv (2 * n) cosh = cosh := by
  induction n with
  | zero => simp
  | succ n ih => simp_all [mul_add]

theorem iteratedDeriv_odd_sinh (n : ‚Ñï) :
    iteratedDeriv (2 * n + 1) sinh = cosh := by simp

theorem iteratedDeriv_odd_cosh (n : ‚Ñï) :
    iteratedDeriv (2 * n + 1) cosh = sinh := by simp

theorem differentiable_iteratedDeriv_sinh (n : ‚Ñï) :
    Differentiable ‚Ñù (iteratedDeriv n sinh) :=
  match n with
  | 0 => by simp
  | 1 => by simp
  | n + 2 => by simp [differentiable_iteratedDeriv_sinh]

theorem differentiable_iteratedDeriv_cosh (n : ‚Ñï) :
    Differentiable ‚Ñù (iteratedDeriv n cosh) :=
  match n with
  | 0 => by simp
  | 1 => by simp
  | n + 2 => by simp [differentiable_iteratedDeriv_cosh]

@[simp]
theorem iteratedDerivWithin_sinh_Icc (n : ‚Ñï) {a b : ‚Ñù} (h : a < b) {x : ‚Ñù} (hx : x ‚àà Icc a b) :
    iteratedDerivWithin n sinh (Icc a b) x = iteratedDeriv n sinh x :=
  iteratedDerivWithin_eq_iteratedDeriv (uniqueDiffOn_Icc h) contDiff_sinh.contDiffAt hx

@[simp]
theorem iteratedDerivWithin_cosh_Icc (n : ‚Ñï) {a b : ‚Ñù} (h : a < b) {x : ‚Ñù} (hx : x ‚àà Icc a b) :
    iteratedDerivWithin n cosh (Icc a b) x = iteratedDeriv n cosh x :=
  iteratedDerivWithin_eq_iteratedDeriv (uniqueDiffOn_Icc h) contDiff_cosh.contDiffAt hx

@[simp]
theorem iteratedDerivWithin_sinh_Ioo (n : ‚Ñï) {a b x : ‚Ñù} (hx : x ‚àà Ioo a b) :
    iteratedDerivWithin n sinh (Ioo a b) x = iteratedDeriv n sinh x :=
  iteratedDerivWithin_eq_iteratedDeriv (uniqueDiffOn_Ioo a b) contDiff_sinh.contDiffAt hx

@[simp]
theorem iteratedDerivWithin_cosh_Ioo (n : ‚Ñï) {a b x : ‚Ñù} (hx : x ‚àà Ioo a b) :
    iteratedDerivWithin n cosh (Ioo a b) x = iteratedDeriv n cosh x :=
  iteratedDerivWithin_eq_iteratedDeriv (uniqueDiffOn_Ioo a b) contDiff_cosh.contDiffAt hx

end Real

end iteratedDeriv

section

/-! ### Simp lemmas for derivatives of `fun x => Real.cos (f x)` etc., `f : ‚Ñù ‚Üí ‚Ñù` -/

variable {f : ‚Ñù ‚Üí ‚Ñù} {f' x : ‚Ñù} {s : Set ‚Ñù}

/-! #### `Real.cosh` -/

theorem HasStrictDerivAt.cosh (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x => Real.cosh (f x)) (Real.sinh (f x) * f') x :=
  (Real.hasStrictDerivAt_cosh (f x)).comp x hf

theorem HasDerivAt.cosh (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x => Real.cosh (f x)) (Real.sinh (f x) * f') x :=
  (Real.hasDerivAt_cosh (f x)).comp x hf

theorem HasDerivWithinAt.cosh (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x => Real.cosh (f x)) (Real.sinh (f x) * f') s x :=
  (Real.hasDerivAt_cosh (f x)).comp_hasDerivWithinAt x hf

theorem derivWithin_cosh (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x => Real.cosh (f x)) s x = Real.sinh (f x) * derivWithin f s x :=
  hf.hasDerivWithinAt.cosh.derivWithin hxs

@[simp]
theorem deriv_cosh (hc : DifferentiableAt ‚Ñù f x) :
    deriv (fun x => Real.cosh (f x)) x = Real.sinh (f x) * deriv f x :=
  hc.hasDerivAt.cosh.deriv

/-! #### `Real.sinh` -/

theorem HasStrictDerivAt.sinh (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x => Real.sinh (f x)) (Real.cosh (f x) * f') x :=
  (Real.hasStrictDerivAt_sinh (f x)).comp x hf

theorem HasDerivAt.sinh (hf : HasDerivAt f f' x) :
    HasDerivAt (fun x => Real.sinh (f x)) (Real.cosh (f x) * f') x :=
  (Real.hasDerivAt_sinh (f x)).comp x hf

theorem HasDerivWithinAt.sinh (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x => Real.sinh (f x)) (Real.cosh (f x) * f') s x :=
  (Real.hasDerivAt_sinh (f x)).comp_hasDerivWithinAt x hf

theorem derivWithin_sinh (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    derivWithin (fun x => Real.sinh (f x)) s x = Real.cosh (f x) * derivWithin f s x :=
  hf.hasDerivWithinAt.sinh.derivWithin hxs

@[simp]
theorem deriv_sinh (hc : DifferentiableAt ‚Ñù f x) :
    deriv (fun x => Real.sinh (f x)) x = Real.cosh (f x) * deriv f x :=
  hc.hasDerivAt.sinh.deriv

end

section

/-! ### Simp lemmas for derivatives of `fun x => Real.cos (f x)` etc., `f : E ‚Üí ‚Ñù` -/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {f : E ‚Üí ‚Ñù} {f' : StrongDual ‚Ñù E}
  {x : E} {s : Set E}

/-! #### `Real.cosh` -/

theorem HasStrictFDerivAt.cosh (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => Real.cosh (f x)) (Real.sinh (f x) ‚Ä¢ f') x :=
  (Real.hasStrictDerivAt_cosh (f x)).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.cosh (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun x => Real.cosh (f x)) (Real.sinh (f x) ‚Ä¢ f') x :=
  (Real.hasDerivAt_cosh (f x)).comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.cosh (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => Real.cosh (f x)) (Real.sinh (f x) ‚Ä¢ f') s x :=
  (Real.hasDerivAt_cosh (f x)).comp_hasFDerivWithinAt x hf

theorem DifferentiableWithinAt.cosh (hf : DifferentiableWithinAt ‚Ñù f s x) :
    DifferentiableWithinAt ‚Ñù (fun x => Real.cosh (f x)) s x :=
  hf.hasFDerivWithinAt.cosh.differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.cosh (hc : DifferentiableAt ‚Ñù f x) :
    DifferentiableAt ‚Ñù (fun x => Real.cosh (f x)) x :=
  hc.hasFDerivAt.cosh.differentiableAt

theorem DifferentiableOn.cosh (hc : DifferentiableOn ‚Ñù f s) :
    DifferentiableOn ‚Ñù (fun x => Real.cosh (f x)) s := fun x h => (hc x h).cosh

@[simp, fun_prop]
theorem Differentiable.cosh (hc : Differentiable ‚Ñù f) : Differentiable ‚Ñù fun x => Real.cosh (f x) :=
  fun x => (hc x).cosh

theorem fderivWithin_cosh (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    fderivWithin ‚Ñù (fun x => Real.cosh (f x)) s x = Real.sinh (f x) ‚Ä¢ fderivWithin ‚Ñù f s x :=
  hf.hasFDerivWithinAt.cosh.fderivWithin hxs

@[simp]
theorem fderiv_cosh (hc : DifferentiableAt ‚Ñù f x) :
    fderiv ‚Ñù (fun x => Real.cosh (f x)) x = Real.sinh (f x) ‚Ä¢ fderiv ‚Ñù f x :=
  hc.hasFDerivAt.cosh.fderiv

theorem ContDiff.cosh {n} (h : ContDiff ‚Ñù n f) : ContDiff ‚Ñù n fun x => Real.cosh (f x) :=
  Real.contDiff_cosh.comp h

theorem ContDiffAt.cosh {n} (hf : ContDiffAt ‚Ñù n f x) :
    ContDiffAt ‚Ñù n (fun x => Real.cosh (f x)) x :=
  Real.contDiff_cosh.contDiffAt.comp x hf

theorem ContDiffOn.cosh {n} (hf : ContDiffOn ‚Ñù n f s) :
    ContDiffOn ‚Ñù n (fun x => Real.cosh (f x)) s :=
  Real.contDiff_cosh.comp_contDiffOn hf

theorem ContDiffWithinAt.cosh {n} (hf : ContDiffWithinAt ‚Ñù n f s x) :
    ContDiffWithinAt ‚Ñù n (fun x => Real.cosh (f x)) s x :=
  Real.contDiff_cosh.contDiffAt.comp_contDiffWithinAt x hf

/-! #### `Real.sinh` -/

theorem HasStrictFDerivAt.sinh (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => Real.sinh (f x)) (Real.cosh (f x) ‚Ä¢ f') x :=
  (Real.hasStrictDerivAt_sinh (f x)).comp_hasStrictFDerivAt x hf

theorem HasFDerivAt.sinh (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun x => Real.sinh (f x)) (Real.cosh (f x) ‚Ä¢ f') x :=
  (Real.hasDerivAt_sinh (f x)).comp_hasFDerivAt x hf

theorem HasFDerivWithinAt.sinh (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => Real.sinh (f x)) (Real.cosh (f x) ‚Ä¢ f') s x :=
  (Real.hasDerivAt_sinh (f x)).comp_hasFDerivWithinAt x hf

theorem DifferentiableWithinAt.sinh (hf : DifferentiableWithinAt ‚Ñù f s x) :
    DifferentiableWithinAt ‚Ñù (fun x => Real.sinh (f x)) s x :=
  hf.hasFDerivWithinAt.sinh.differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.sinh (hc : DifferentiableAt ‚Ñù f x) :
    DifferentiableAt ‚Ñù (fun x => Real.sinh (f x)) x :=
  hc.hasFDerivAt.sinh.differentiableAt

theorem DifferentiableOn.sinh (hc : DifferentiableOn ‚Ñù f s) :
    DifferentiableOn ‚Ñù (fun x => Real.sinh (f x)) s := fun x h => (hc x h).sinh

@[simp, fun_prop]
theorem Differentiable.sinh (hc : Differentiable ‚Ñù f) : Differentiable ‚Ñù fun x => Real.sinh (f x) :=
  fun x => (hc x).sinh

theorem fderivWithin_sinh (hf : DifferentiableWithinAt ‚Ñù f s x) (hxs : UniqueDiffWithinAt ‚Ñù s x) :
    fderivWithin ‚Ñù (fun x => Real.sinh (f x)) s x = Real.cosh (f x) ‚Ä¢ fderivWithin ‚Ñù f s x :=
  hf.hasFDerivWithinAt.sinh.fderivWithin hxs

@[simp]
theorem fderiv_sinh (hc : DifferentiableAt ‚Ñù f x) :
    fderiv ‚Ñù (fun x => Real.sinh (f x)) x = Real.cosh (f x) ‚Ä¢ fderiv ‚Ñù f x :=
  hc.hasFDerivAt.sinh.fderiv

theorem ContDiff.sinh {n} (h : ContDiff ‚Ñù n f) : ContDiff ‚Ñù n fun x => Real.sinh (f x) :=
  Real.contDiff_sinh.comp h

theorem ContDiffAt.sinh {n} (hf : ContDiffAt ‚Ñù n f x) :
    ContDiffAt ‚Ñù n (fun x => Real.sinh (f x)) x :=
  Real.contDiff_sinh.contDiffAt.comp x hf

theorem ContDiffOn.sinh {n} (hf : ContDiffOn ‚Ñù n f s) :
    ContDiffOn ‚Ñù n (fun x => Real.sinh (f x)) s :=
  Real.contDiff_sinh.comp_contDiffOn hf

theorem ContDiffWithinAt.sinh {n} (hf : ContDiffWithinAt ‚Ñù n f s x) :
    ContDiffWithinAt ‚Ñù n (fun x => Real.sinh (f x)) s x :=
  Real.contDiff_sinh.contDiffAt.comp_contDiffWithinAt x hf

section LogDeriv

@[simp]
theorem Complex.logDeriv_cosh : logDeriv (Complex.cosh) = Complex.tanh := by
  ext
  rw [logDeriv, Complex.deriv_cosh, Pi.div_apply, Complex.tanh]

@[simp]
theorem Real.logDeriv_cosh : logDeriv (Real.cosh) = Real.tanh := by
  ext
  rw [logDeriv, Real.deriv_cosh, Pi.div_apply, Real.tanh_eq_sinh_div_cosh]

end LogDeriv

end

namespace Mathlib.Meta.Positivity
open Lean Meta Qq

private alias ‚ü®_, sinh_pos_of_pos‚ü© := Real.sinh_pos_iff
private alias ‚ü®_, sinh_nonneg_of_nonneg‚ü© := Real.sinh_nonneg_iff
private alias ‚ü®_, sinh_ne_zero_of_ne_zero‚ü© := Real.sinh_ne_zero

/-- Extension for the `positivity` tactic: `Real.sinh` is positive/nonnegative/nonzero if its input
is. -/
@[positivity Real.sinh _]
meta def evalSinh : PositivityExt where eval {u Œ±} _ _ e := do
  let zŒ± : Q(Zero ‚Ñù) := q(inferInstance)
  let pŒ± : Q(PartialOrder ‚Ñù) := q(inferInstance)
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(Real.sinh $a) =>
    assumeInstancesCommute
    match ‚Üê core zŒ± pŒ± a with
    | .positive pa => return .positive q(sinh_pos_of_pos $pa)
    | .nonnegative pa => return .nonnegative q(sinh_nonneg_of_nonneg $pa)
    | .nonzero pa => return .nonzero q(sinh_ne_zero_of_ne_zero $pa)
    | _ => return .none
  | _, _, _ => throwError "not Real.sinh"

example (x : ‚Ñù) (hx : 0 < x) : 0 < x.sinh := by positivity
example (x : ‚Ñù) (hx : 0 ‚â§ x) : 0 ‚â§ x.sinh := by positivity
example (x : ‚Ñù) (hx : x ‚â† 0) : x.sinh ‚â† 0 := by positivity

end Mathlib.Meta.Positivity
