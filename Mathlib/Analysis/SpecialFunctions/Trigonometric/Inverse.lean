/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Basic
import Mathlib.Topology.Algebra.Order.ProjIcc

#align_import analysis.special_functions.trigonometric.inverse from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Inverse trigonometric functions.

See also `Analysis.SpecialFunctions.Trigonometric.Arctan` for the inverse tan function.
(This is delayed as it is easier to set up after developing complex trigonometric functions.)

Basic inequalities on trigonometric functions.
-/


noncomputable section

open Classical Topology Filter

open Set Filter

open Real

namespace Real

/-- Inverse of the `sin` function, returns values in the range `-œÄ / 2 ‚â§ arcsin x ‚â§ œÄ / 2`.
It defaults to `-œÄ / 2` on `(-‚àû, -1)` and to `œÄ / 2` to `(1, ‚àû)`. -/
-- @[pp_nodot] Porting note: not implemented
noncomputable def arcsin : ‚Ñù ‚Üí ‚Ñù :=
  Subtype.val ‚àò IccExtend (neg_le_self zero_le_one) sinOrderIso.symm
#align real.arcsin Real.arcsin

theorem arcsin_mem_Icc (x : ‚Ñù) : arcsin x ‚àà Icc (-(œÄ / 2)) (œÄ / 2) :=
  Subtype.coe_prop _
#align real.arcsin_mem_Icc Real.arcsin_mem_Icc

@[simp]
theorem range_arcsin : range arcsin = Icc (-(œÄ / 2)) (œÄ / 2) := by
  rw [arcsin, range_comp Subtype.val]
  -- ‚ä¢ Subtype.val '' range (IccExtend arcsin.proof_2 ‚Üë(OrderIso.symm sinOrderIso)) ‚Ä¶
  simp [Icc]
  -- üéâ no goals
#align real.range_arcsin Real.range_arcsin

theorem arcsin_le_pi_div_two (x : ‚Ñù) : arcsin x ‚â§ œÄ / 2 :=
  (arcsin_mem_Icc x).2
#align real.arcsin_le_pi_div_two Real.arcsin_le_pi_div_two

theorem neg_pi_div_two_le_arcsin (x : ‚Ñù) : -(œÄ / 2) ‚â§ arcsin x :=
  (arcsin_mem_Icc x).1
#align real.neg_pi_div_two_le_arcsin Real.neg_pi_div_two_le_arcsin

theorem arcsin_projIcc (x : ‚Ñù) : arcsin (projIcc (-1) 1 (neg_le_self zero_le_one) x) = arcsin x :=
  by rw [arcsin, Function.comp_apply, IccExtend_val, Function.comp_apply, IccExtend,
        Function.comp_apply]
#align real.arcsin_proj_Icc Real.arcsin_projIcc

theorem sin_arcsin' {x : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) : sin (arcsin x) = x := by
  simpa [arcsin, IccExtend_of_mem _ _ hx, -OrderIso.apply_symm_apply] using
    Subtype.ext_iff.1 (sinOrderIso.apply_symm_apply ‚ü®x, hx‚ü©)
#align real.sin_arcsin' Real.sin_arcsin'

theorem sin_arcsin {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : sin (arcsin x) = x :=
  sin_arcsin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
#align real.sin_arcsin Real.sin_arcsin

theorem arcsin_sin' {x : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) : arcsin (sin x) = x :=
  injOn_sin (arcsin_mem_Icc _) hx <| by rw [sin_arcsin (neg_one_le_sin _) (sin_le_one _)]
                                        -- üéâ no goals
#align real.arcsin_sin' Real.arcsin_sin'

theorem arcsin_sin {x : ‚Ñù} (hx‚ÇÅ : -(œÄ / 2) ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ / 2) : arcsin (sin x) = x :=
  arcsin_sin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©
#align real.arcsin_sin Real.arcsin_sin

theorem strictMonoOn_arcsin : StrictMonoOn arcsin (Icc (-1) 1) :=
  (Subtype.strictMono_coe _).comp_strictMonoOn <|
    sinOrderIso.symm.strictMono.strictMonoOn_IccExtend _
#align real.strict_mono_on_arcsin Real.strictMonoOn_arcsin

theorem monotone_arcsin : Monotone arcsin :=
  (Subtype.mono_coe _).comp <| sinOrderIso.symm.monotone.IccExtend _
#align real.monotone_arcsin Real.monotone_arcsin

theorem injOn_arcsin : InjOn arcsin (Icc (-1) 1) :=
  strictMonoOn_arcsin.injOn
#align real.inj_on_arcsin Real.injOn_arcsin

theorem arcsin_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
    arcsin x = arcsin y ‚Üî x = y :=
  injOn_arcsin.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©
#align real.arcsin_inj Real.arcsin_inj

@[continuity]
theorem continuous_arcsin : Continuous arcsin :=
  continuous_subtype_val.comp sinOrderIso.symm.continuous.Icc_extend'
#align real.continuous_arcsin Real.continuous_arcsin

theorem continuousAt_arcsin {x : ‚Ñù} : ContinuousAt arcsin x :=
  continuous_arcsin.continuousAt
#align real.continuous_at_arcsin Real.continuousAt_arcsin

theorem arcsin_eq_of_sin_eq {x y : ‚Ñù} (h‚ÇÅ : sin x = y) (h‚ÇÇ : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin y = x := by
  subst y
  -- ‚ä¢ arcsin (sin x) = x
  exact injOn_sin (arcsin_mem_Icc _) h‚ÇÇ (sin_arcsin' (sin_mem_Icc x))
  -- üéâ no goals
#align real.arcsin_eq_of_sin_eq Real.arcsin_eq_of_sin_eq

@[simp]
theorem arcsin_zero : arcsin 0 = 0 :=
  arcsin_eq_of_sin_eq sin_zero ‚ü®neg_nonpos.2 pi_div_two_pos.le, pi_div_two_pos.le‚ü©
#align real.arcsin_zero Real.arcsin_zero

@[simp]
theorem arcsin_one : arcsin 1 = œÄ / 2 :=
  arcsin_eq_of_sin_eq sin_pi_div_two <| right_mem_Icc.2 (neg_le_self pi_div_two_pos.le)
#align real.arcsin_one Real.arcsin_one

theorem arcsin_of_one_le {x : ‚Ñù} (hx : 1 ‚â§ x) : arcsin x = œÄ / 2 := by
  rw [‚Üê arcsin_projIcc, projIcc_of_right_le _ hx, Subtype.coe_mk, arcsin_one]
  -- üéâ no goals
#align real.arcsin_of_one_le Real.arcsin_of_one_le

theorem arcsin_neg_one : arcsin (-1) = -(œÄ / 2) :=
  arcsin_eq_of_sin_eq (by rw [sin_neg, sin_pi_div_two]) <|
                          -- üéâ no goals
    left_mem_Icc.2 (neg_le_self pi_div_two_pos.le)
#align real.arcsin_neg_one Real.arcsin_neg_one

theorem arcsin_of_le_neg_one {x : ‚Ñù} (hx : x ‚â§ -1) : arcsin x = -(œÄ / 2) := by
  rw [‚Üê arcsin_projIcc, projIcc_of_le_left _ hx, Subtype.coe_mk, arcsin_neg_one]
  -- üéâ no goals
#align real.arcsin_of_le_neg_one Real.arcsin_of_le_neg_one

@[simp]
theorem arcsin_neg (x : ‚Ñù) : arcsin (-x) = -arcsin x := by
  cases' le_total x (-1) with hx‚ÇÅ hx‚ÇÅ
  -- ‚ä¢ arcsin (-x) = -arcsin x
  ¬∑ rw [arcsin_of_le_neg_one hx‚ÇÅ, neg_neg, arcsin_of_one_le (le_neg.2 hx‚ÇÅ)]
    -- üéâ no goals
  cases' le_total 1 x with hx‚ÇÇ hx‚ÇÇ
  -- ‚ä¢ arcsin (-x) = -arcsin x
  ¬∑ rw [arcsin_of_one_le hx‚ÇÇ, arcsin_of_le_neg_one (neg_le_neg hx‚ÇÇ)]
    -- üéâ no goals
  refine' arcsin_eq_of_sin_eq _ _
  -- ‚ä¢ sin (-arcsin x) = -x
  ¬∑ rw [sin_neg, sin_arcsin hx‚ÇÅ hx‚ÇÇ]
    -- üéâ no goals
  ¬∑ exact ‚ü®neg_le_neg (arcsin_le_pi_div_two _), neg_le.2 (neg_pi_div_two_le_arcsin _)‚ü©
    -- üéâ no goals
#align real.arcsin_neg Real.arcsin_neg

theorem arcsin_le_iff_le_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x ‚â§ y ‚Üî x ‚â§ sin y := by
  rw [‚Üê arcsin_sin' hy, strictMonoOn_arcsin.le_iff_le hx (sin_mem_Icc _), arcsin_sin' hy]
  -- üéâ no goals
#align real.arcsin_le_iff_le_sin Real.arcsin_le_iff_le_sin

theorem arcsin_le_iff_le_sin' {x y : ‚Ñù} (hy : y ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x ‚â§ y ‚Üî x ‚â§ sin y := by
  cases' le_total x (-1) with hx‚ÇÅ hx‚ÇÅ
  -- ‚ä¢ arcsin x ‚â§ y ‚Üî x ‚â§ sin y
  ¬∑ simp [arcsin_of_le_neg_one hx‚ÇÅ, hy.1, hx‚ÇÅ.trans (neg_one_le_sin _)]
    -- üéâ no goals
  cases' lt_or_le 1 x with hx‚ÇÇ hx‚ÇÇ
  -- ‚ä¢ arcsin x ‚â§ y ‚Üî x ‚â§ sin y
  ¬∑ simp [arcsin_of_one_le hx‚ÇÇ.le, hy.2.not_le, (sin_le_one y).trans_lt hx‚ÇÇ]
    -- üéâ no goals
  exact arcsin_le_iff_le_sin ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© (mem_Icc_of_Ico hy)
  -- üéâ no goals
#align real.arcsin_le_iff_le_sin' Real.arcsin_le_iff_le_sin'

theorem le_arcsin_iff_sin_le {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
    x ‚â§ arcsin y ‚Üî sin x ‚â§ y := by
  rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg,
    arcsin_le_iff_le_sin ‚ü®neg_le_neg hy.2, neg_le.2 hy.1‚ü© ‚ü®neg_le_neg hx.2, neg_le.2 hx.1‚ü©, sin_neg,
    neg_le_neg_iff]
#align real.le_arcsin_iff_sin_le Real.le_arcsin_iff_sin_le

theorem le_arcsin_iff_sin_le' {x y : ‚Ñù} (hx : x ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
    x ‚â§ arcsin y ‚Üî sin x ‚â§ y := by
  rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg, arcsin_le_iff_le_sin' ‚ü®neg_le_neg hx.2, neg_lt.2 hx.1‚ü©,
    sin_neg, neg_le_neg_iff]
#align real.le_arcsin_iff_sin_le' Real.le_arcsin_iff_sin_le'

theorem arcsin_lt_iff_lt_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x < y ‚Üî x < sin y :=
  not_le.symm.trans <| (not_congr <| le_arcsin_iff_sin_le hy hx).trans not_le
#align real.arcsin_lt_iff_lt_sin Real.arcsin_lt_iff_lt_sin

theorem arcsin_lt_iff_lt_sin' {x y : ‚Ñù} (hy : y ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x < y ‚Üî x < sin y :=
  not_le.symm.trans <| (not_congr <| le_arcsin_iff_sin_le' hy).trans not_le
#align real.arcsin_lt_iff_lt_sin' Real.arcsin_lt_iff_lt_sin'

theorem lt_arcsin_iff_sin_lt {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
    x < arcsin y ‚Üî sin x < y :=
  not_le.symm.trans <| (not_congr <| arcsin_le_iff_le_sin hy hx).trans not_le
#align real.lt_arcsin_iff_sin_lt Real.lt_arcsin_iff_sin_lt

theorem lt_arcsin_iff_sin_lt' {x y : ‚Ñù} (hx : x ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
    x < arcsin y ‚Üî sin x < y :=
  not_le.symm.trans <| (not_congr <| arcsin_le_iff_le_sin' hx).trans not_le
#align real.lt_arcsin_iff_sin_lt' Real.lt_arcsin_iff_sin_lt'

theorem arcsin_eq_iff_eq_sin {x y : ‚Ñù} (hy : y ‚àà Ioo (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x = y ‚Üî x = sin y := by
  simp only [le_antisymm_iff, arcsin_le_iff_le_sin' (mem_Ico_of_Ioo hy),
    le_arcsin_iff_sin_le' (mem_Ioc_of_Ioo hy)]
#align real.arcsin_eq_iff_eq_sin Real.arcsin_eq_iff_eq_sin

@[simp]
theorem arcsin_nonneg {x : ‚Ñù} : 0 ‚â§ arcsin x ‚Üî 0 ‚â§ x :=
  (le_arcsin_iff_sin_le' ‚ü®neg_lt_zero.2 pi_div_two_pos, pi_div_two_pos.le‚ü©).trans <| by
    rw [sin_zero]
    -- üéâ no goals
#align real.arcsin_nonneg Real.arcsin_nonneg

@[simp]
theorem arcsin_nonpos {x : ‚Ñù} : arcsin x ‚â§ 0 ‚Üî x ‚â§ 0 :=
  neg_nonneg.symm.trans <| arcsin_neg x ‚ñ∏ arcsin_nonneg.trans neg_nonneg
#align real.arcsin_nonpos Real.arcsin_nonpos

@[simp]
theorem arcsin_eq_zero_iff {x : ‚Ñù} : arcsin x = 0 ‚Üî x = 0 := by simp [le_antisymm_iff]
                                                                -- üéâ no goals
#align real.arcsin_eq_zero_iff Real.arcsin_eq_zero_iff

@[simp]
theorem zero_eq_arcsin_iff {x} : 0 = arcsin x ‚Üî x = 0 :=
  eq_comm.trans arcsin_eq_zero_iff
#align real.zero_eq_arcsin_iff Real.zero_eq_arcsin_iff

@[simp]
theorem arcsin_pos {x : ‚Ñù} : 0 < arcsin x ‚Üî 0 < x :=
  lt_iff_lt_of_le_iff_le arcsin_nonpos
#align real.arcsin_pos Real.arcsin_pos

@[simp]
theorem arcsin_lt_zero {x : ‚Ñù} : arcsin x < 0 ‚Üî x < 0 :=
  lt_iff_lt_of_le_iff_le arcsin_nonneg
#align real.arcsin_lt_zero Real.arcsin_lt_zero

@[simp]
theorem arcsin_lt_pi_div_two {x : ‚Ñù} : arcsin x < œÄ / 2 ‚Üî x < 1 :=
  (arcsin_lt_iff_lt_sin' (right_mem_Ioc.2 <| neg_lt_self pi_div_two_pos)).trans <| by
    rw [sin_pi_div_two]
    -- üéâ no goals
#align real.arcsin_lt_pi_div_two Real.arcsin_lt_pi_div_two

@[simp]
theorem neg_pi_div_two_lt_arcsin {x : ‚Ñù} : -(œÄ / 2) < arcsin x ‚Üî -1 < x :=
  (lt_arcsin_iff_sin_lt' <| left_mem_Ico.2 <| neg_lt_self pi_div_two_pos).trans <| by
    rw [sin_neg, sin_pi_div_two]
    -- üéâ no goals
#align real.neg_pi_div_two_lt_arcsin Real.neg_pi_div_two_lt_arcsin

@[simp]
theorem arcsin_eq_pi_div_two {x : ‚Ñù} : arcsin x = œÄ / 2 ‚Üî 1 ‚â§ x :=
  ‚ü®fun h => not_lt.1 fun h' => (arcsin_lt_pi_div_two.2 h').ne h, arcsin_of_one_le‚ü©
#align real.arcsin_eq_pi_div_two Real.arcsin_eq_pi_div_two

@[simp]
theorem pi_div_two_eq_arcsin {x} : œÄ / 2 = arcsin x ‚Üî 1 ‚â§ x :=
  eq_comm.trans arcsin_eq_pi_div_two
#align real.pi_div_two_eq_arcsin Real.pi_div_two_eq_arcsin

@[simp]
theorem pi_div_two_le_arcsin {x} : œÄ / 2 ‚â§ arcsin x ‚Üî 1 ‚â§ x :=
  (arcsin_le_pi_div_two x).le_iff_eq.trans pi_div_two_eq_arcsin
#align real.pi_div_two_le_arcsin Real.pi_div_two_le_arcsin

@[simp]
theorem arcsin_eq_neg_pi_div_two {x : ‚Ñù} : arcsin x = -(œÄ / 2) ‚Üî x ‚â§ -1 :=
  ‚ü®fun h => not_lt.1 fun h' => (neg_pi_div_two_lt_arcsin.2 h').ne' h, arcsin_of_le_neg_one‚ü©
#align real.arcsin_eq_neg_pi_div_two Real.arcsin_eq_neg_pi_div_two

@[simp]
theorem neg_pi_div_two_eq_arcsin {x} : -(œÄ / 2) = arcsin x ‚Üî x ‚â§ -1 :=
  eq_comm.trans arcsin_eq_neg_pi_div_two
#align real.neg_pi_div_two_eq_arcsin Real.neg_pi_div_two_eq_arcsin

@[simp]
theorem arcsin_le_neg_pi_div_two {x} : arcsin x ‚â§ -(œÄ / 2) ‚Üî x ‚â§ -1 :=
  (neg_pi_div_two_le_arcsin x).le_iff_eq.trans arcsin_eq_neg_pi_div_two
#align real.arcsin_le_neg_pi_div_two Real.arcsin_le_neg_pi_div_two

@[simp]
theorem pi_div_four_le_arcsin {x} : œÄ / 4 ‚â§ arcsin x ‚Üî sqrt 2 / 2 ‚â§ x := by
  rw [‚Üê sin_pi_div_four, le_arcsin_iff_sin_le']
  -- ‚ä¢ œÄ / 4 ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)
  have := pi_pos
  -- ‚ä¢ œÄ / 4 ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)
  constructor <;> linarith
  -- ‚ä¢ -(œÄ / 2) < œÄ / 4
                  -- üéâ no goals
                  -- üéâ no goals
#align real.pi_div_four_le_arcsin Real.pi_div_four_le_arcsin

theorem mapsTo_sin_Ioo : MapsTo sin (Ioo (-(œÄ / 2)) (œÄ / 2)) (Ioo (-1) 1) := fun x h => by
  rwa [mem_Ioo, ‚Üê arcsin_lt_pi_div_two, ‚Üê neg_pi_div_two_lt_arcsin, arcsin_sin h.1.le h.2.le]
  -- üéâ no goals
#align real.maps_to_sin_Ioo Real.mapsTo_sin_Ioo

/-- `Real.sin` as a `LocalHomeomorph` between `(-œÄ / 2, œÄ / 2)` and `(-1, 1)`. -/
@[simp]
def sinLocalHomeomorph : LocalHomeomorph ‚Ñù ‚Ñù where
  toFun := sin
  invFun := arcsin
  source := Ioo (-(œÄ / 2)) (œÄ / 2)
  target := Ioo (-1) 1
  map_source' := mapsTo_sin_Ioo
  map_target' _ hy := ‚ü®neg_pi_div_two_lt_arcsin.2 hy.1, arcsin_lt_pi_div_two.2 hy.2‚ü©
  left_inv' _ hx := arcsin_sin hx.1.le hx.2.le
  right_inv' _ hy := sin_arcsin hy.1.le hy.2.le
  open_source := isOpen_Ioo
  open_target := isOpen_Ioo
  continuous_toFun := continuous_sin.continuousOn
  continuous_invFun := continuous_arcsin.continuousOn
#align real.sin_local_homeomorph Real.sinLocalHomeomorph

theorem cos_arcsin_nonneg (x : ‚Ñù) : 0 ‚â§ cos (arcsin x) :=
  cos_nonneg_of_mem_Icc ‚ü®neg_pi_div_two_le_arcsin _, arcsin_le_pi_div_two _‚ü©
#align real.cos_arcsin_nonneg Real.cos_arcsin_nonneg

-- The junk values for `arcsin` and `sqrt` make this true even outside `[-1, 1]`.
theorem cos_arcsin (x : ‚Ñù) : cos (arcsin x) = sqrt (1 - x ^ 2) := by
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
                         -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
  ¬∑ rw [not_le] at hx‚ÇÅ
    -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
    rw [arcsin_of_le_neg_one hx‚ÇÅ.le, cos_neg, cos_pi_div_two, sqrt_eq_zero_of_nonpos]
    -- ‚ä¢ 1 - x ^ 2 ‚â§ 0
    nlinarith
    -- üéâ no goals
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
                        -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
  ¬∑ rw [not_le] at hx‚ÇÇ
    -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
    rw [arcsin_of_one_le hx‚ÇÇ.le, cos_pi_div_two, sqrt_eq_zero_of_nonpos]
    -- ‚ä¢ 1 - x ^ 2 ‚â§ 0
    nlinarith
    -- üéâ no goals
  have : sin (arcsin x) ^ 2 + cos (arcsin x) ^ 2 = 1 := sin_sq_add_cos_sq (arcsin x)
  -- ‚ä¢ cos (arcsin x) = sqrt (1 - x ^ 2)
  rw [‚Üê eq_sub_iff_add_eq', ‚Üê sqrt_inj (sq_nonneg _) (sub_nonneg.2 (sin_sq_le_one (arcsin x))), sq,
    sqrt_mul_self (cos_arcsin_nonneg _)] at this
  rw [this, sin_arcsin hx‚ÇÅ hx‚ÇÇ]
  -- üéâ no goals
#align real.cos_arcsin Real.cos_arcsin

-- The junk values for `arcsin` and `sqrt` make this true even outside `[-1, 1]`.
theorem tan_arcsin (x : ‚Ñù) : tan (arcsin x) = x / sqrt (1 - x ^ 2) := by
  rw [tan_eq_sin_div_cos, cos_arcsin]
  -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
                         -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
  ¬∑ have h : sqrt (1 - x ^ 2) = 0 := sqrt_eq_zero_of_nonpos (by nlinarith)
    -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
    rw [h]
    -- ‚ä¢ sin (arcsin x) / 0 = x / 0
    simp
    -- üéâ no goals
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
                        -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
  ¬∑ have h : sqrt (1 - x ^ 2) = 0 := sqrt_eq_zero_of_nonpos (by nlinarith)
    -- ‚ä¢ sin (arcsin x) / sqrt (1 - x ^ 2) = x / sqrt (1 - x ^ 2)
    rw [h]
    -- ‚ä¢ sin (arcsin x) / 0 = x / 0
    simp
    -- üéâ no goals
  rw [sin_arcsin hx‚ÇÅ hx‚ÇÇ]
  -- üéâ no goals
#align real.tan_arcsin Real.tan_arcsin

/-- Inverse of the `cos` function, returns values in the range `0 ‚â§ arccos x` and `arccos x ‚â§ œÄ`.
  It defaults to `œÄ` on `(-‚àû, -1)` and to `0` to `(1, ‚àû)`. -/
-- @[pp_nodot] Porting note: not implemented
noncomputable def arccos (x : ‚Ñù) : ‚Ñù :=
  œÄ / 2 - arcsin x
#align real.arccos Real.arccos

theorem arccos_eq_pi_div_two_sub_arcsin (x : ‚Ñù) : arccos x = œÄ / 2 - arcsin x :=
  rfl
#align real.arccos_eq_pi_div_two_sub_arcsin Real.arccos_eq_pi_div_two_sub_arcsin

theorem arcsin_eq_pi_div_two_sub_arccos (x : ‚Ñù) : arcsin x = œÄ / 2 - arccos x := by simp [arccos]
                                                                                    -- üéâ no goals
#align real.arcsin_eq_pi_div_two_sub_arccos Real.arcsin_eq_pi_div_two_sub_arccos

theorem arccos_le_pi (x : ‚Ñù) : arccos x ‚â§ œÄ := by
  unfold arccos; linarith [neg_pi_div_two_le_arcsin x]
  -- ‚ä¢ œÄ / 2 - arcsin x ‚â§ œÄ
                 -- üéâ no goals
#align real.arccos_le_pi Real.arccos_le_pi

theorem arccos_nonneg (x : ‚Ñù) : 0 ‚â§ arccos x := by
  unfold arccos; linarith [arcsin_le_pi_div_two x]
  -- ‚ä¢ 0 ‚â§ œÄ / 2 - arcsin x
                 -- üéâ no goals
#align real.arccos_nonneg Real.arccos_nonneg

@[simp]
theorem arccos_pos {x : ‚Ñù} : 0 < arccos x ‚Üî x < 1 := by simp [arccos]
                                                        -- üéâ no goals
#align real.arccos_pos Real.arccos_pos

theorem cos_arccos {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : cos (arccos x) = x := by
  rw [arccos, cos_pi_div_two_sub, sin_arcsin hx‚ÇÅ hx‚ÇÇ]
  -- üéâ no goals
#align real.cos_arccos Real.cos_arccos

theorem arccos_cos {x : ‚Ñù} (hx‚ÇÅ : 0 ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ) : arccos (cos x) = x := by
  rw [arccos, ‚Üê sin_pi_div_two_sub, arcsin_sin] <;> simp [sub_eq_add_neg] <;> linarith
                                                    -- üéâ no goals
                                                    -- ‚ä¢ x ‚â§ œÄ
                                                    -- ‚ä¢ 0 ‚â§ x
                                                                              -- üéâ no goals
                                                                              -- üéâ no goals
#align real.arccos_cos Real.arccos_cos

theorem strictAntiOn_arccos : StrictAntiOn arccos (Icc (-1) 1) := fun _ hx _ hy h =>
  sub_lt_sub_left (strictMonoOn_arcsin hx hy h) _
#align real.strict_anti_on_arccos Real.strictAntiOn_arccos

theorem arccos_injOn : InjOn arccos (Icc (-1) 1) :=
  strictAntiOn_arccos.injOn
#align real.arccos_inj_on Real.arccos_injOn

theorem arccos_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
    arccos x = arccos y ‚Üî x = y :=
  arccos_injOn.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©
#align real.arccos_inj Real.arccos_inj

@[simp]
theorem arccos_zero : arccos 0 = œÄ / 2 := by simp [arccos]
                                             -- üéâ no goals
#align real.arccos_zero Real.arccos_zero

@[simp]
theorem arccos_one : arccos 1 = 0 := by simp [arccos]
                                        -- üéâ no goals
#align real.arccos_one Real.arccos_one

@[simp]
theorem arccos_neg_one : arccos (-1) = œÄ := by simp [arccos, add_halves]
                                               -- üéâ no goals
#align real.arccos_neg_one Real.arccos_neg_one

@[simp]
theorem arccos_eq_zero {x} : arccos x = 0 ‚Üî 1 ‚â§ x := by simp [arccos, sub_eq_zero]
                                                        -- üéâ no goals
#align real.arccos_eq_zero Real.arccos_eq_zero

@[simp]
theorem arccos_eq_pi_div_two {x} : arccos x = œÄ / 2 ‚Üî x = 0 := by simp [arccos]
                                                                  -- üéâ no goals
#align real.arccos_eq_pi_div_two Real.arccos_eq_pi_div_two

@[simp]
theorem arccos_eq_pi {x} : arccos x = œÄ ‚Üî x ‚â§ -1 := by
  rw [arccos, sub_eq_iff_eq_add, ‚Üê sub_eq_iff_eq_add', div_two_sub_self, neg_pi_div_two_eq_arcsin]
  -- üéâ no goals
#align real.arccos_eq_pi Real.arccos_eq_pi

theorem arccos_neg (x : ‚Ñù) : arccos (-x) = œÄ - arccos x := by
  rw [‚Üê add_halves œÄ, arccos, arcsin_neg, arccos, add_sub_assoc, sub_sub_self, sub_neg_eq_add]
  -- üéâ no goals
#align real.arccos_neg Real.arccos_neg

theorem arccos_of_one_le {x : ‚Ñù} (hx : 1 ‚â§ x) : arccos x = 0 := by
  rw [arccos, arcsin_of_one_le hx, sub_self]
  -- üéâ no goals
#align real.arccos_of_one_le Real.arccos_of_one_le

theorem arccos_of_le_neg_one {x : ‚Ñù} (hx : x ‚â§ -1) : arccos x = œÄ := by
  rw [arccos, arcsin_of_le_neg_one hx, sub_neg_eq_add, add_halves']
  -- üéâ no goals
#align real.arccos_of_le_neg_one Real.arccos_of_le_neg_one

-- The junk values for `arccos` and `sqrt` make this true even outside `[-1, 1]`.
theorem sin_arccos (x : ‚Ñù) : sin (arccos x) = sqrt (1 - x ^ 2) := by
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
                         -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
  ¬∑ rw [not_le] at hx‚ÇÅ
    -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
    rw [arccos_of_le_neg_one hx‚ÇÅ.le, sin_pi, sqrt_eq_zero_of_nonpos]
    -- ‚ä¢ 1 - x ^ 2 ‚â§ 0
    nlinarith
    -- üéâ no goals
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
                        -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
  ¬∑ rw [not_le] at hx‚ÇÇ
    -- ‚ä¢ sin (arccos x) = sqrt (1 - x ^ 2)
    rw [arccos_of_one_le hx‚ÇÇ.le, sin_zero, sqrt_eq_zero_of_nonpos]
    -- ‚ä¢ 1 - x ^ 2 ‚â§ 0
    nlinarith
    -- üéâ no goals
  rw [arccos_eq_pi_div_two_sub_arcsin, sin_pi_div_two_sub, cos_arcsin]
  -- üéâ no goals
#align real.sin_arccos Real.sin_arccos

@[simp]
theorem arccos_le_pi_div_two {x} : arccos x ‚â§ œÄ / 2 ‚Üî 0 ‚â§ x := by simp [arccos]
                                                                  -- üéâ no goals
#align real.arccos_le_pi_div_two Real.arccos_le_pi_div_two

@[simp]
theorem arccos_lt_pi_div_two {x : ‚Ñù} : arccos x < œÄ / 2 ‚Üî 0 < x := by simp [arccos]
                                                                      -- üéâ no goals
#align real.arccos_lt_pi_div_two Real.arccos_lt_pi_div_two

@[simp]
theorem arccos_le_pi_div_four {x} : arccos x ‚â§ œÄ / 4 ‚Üî sqrt 2 / 2 ‚â§ x := by
  rw [arccos, ‚Üê pi_div_four_le_arcsin]
  -- ‚ä¢ œÄ / 2 - arcsin x ‚â§ œÄ / 4 ‚Üî œÄ / 4 ‚â§ arcsin x
  constructor <;>
  -- ‚ä¢ œÄ / 2 - arcsin x ‚â§ œÄ / 4 ‚Üí œÄ / 4 ‚â§ arcsin x
    ¬∑ intro
      -- ‚ä¢ œÄ / 4 ‚â§ arcsin x
      -- ‚ä¢ œÄ / 2 - arcsin x ‚â§ œÄ / 4
      -- üéâ no goals
      linarith
      -- üéâ no goals
#align real.arccos_le_pi_div_four Real.arccos_le_pi_div_four

@[continuity]
theorem continuous_arccos : Continuous arccos :=
  continuous_const.sub continuous_arcsin
#align real.continuous_arccos Real.continuous_arccos

-- The junk values for `arccos` and `sqrt` make this true even outside `[-1, 1]`.
theorem tan_arccos (x : ‚Ñù) : tan (arccos x) = sqrt (1 - x ^ 2) / x := by
  rw [arccos, tan_pi_div_two_sub, tan_arcsin, inv_div]
  -- üéâ no goals
#align real.tan_arccos Real.tan_arccos

-- The junk values for `arccos` and `sqrt` make this true even for `1 < x`.
theorem arccos_eq_arcsin {x : ‚Ñù} (h : 0 ‚â§ x) : arccos x = arcsin (sqrt (1 - x ^ 2)) :=
  (arcsin_eq_of_sin_eq (sin_arccos _)
      ‚ü®(Left.neg_nonpos_iff.2 (div_nonneg pi_pos.le (by norm_num))).trans (arccos_nonneg _),
                                                        -- üéâ no goals
        arccos_le_pi_div_two.2 h‚ü©).symm
#align real.arccos_eq_arcsin Real.arccos_eq_arcsin

-- The junk values for `arcsin` and `sqrt` make this true even for `1 < x`.
theorem arcsin_eq_arccos {x : ‚Ñù} (h : 0 ‚â§ x) : arcsin x = arccos (sqrt (1 - x ^ 2)) := by
  rw [eq_comm, ‚Üê cos_arcsin]
  -- ‚ä¢ arccos (cos (arcsin x)) = arcsin x
  exact
    arccos_cos (arcsin_nonneg.2 h)
      ((arcsin_le_pi_div_two _).trans (div_le_self pi_pos.le one_le_two))
#align real.arcsin_eq_arccos Real.arcsin_eq_arccos

end Real
