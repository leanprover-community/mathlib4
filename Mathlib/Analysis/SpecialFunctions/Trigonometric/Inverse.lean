/-
Copyright (c) 2018 Chris Hughes. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Hughes, Abhimanyu Pallavi Sudhir, Jean Lo, Calle S√∂nne, Benjamin Davidson
-/
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Basic
import Mathlib.Topology.Order.ProjIcc

/-!
# Inverse trigonometric functions.

See also `Analysis.SpecialFunctions.Trigonometric.Arctan` for the inverse tan function.
(This is delayed as it is easier to set up after developing complex trigonometric functions.)

Basic inequalities on trigonometric functions.
-/


noncomputable section

open Topology Filter Set Filter Real

namespace Real
variable {x y : ‚Ñù}

/-- Inverse of the `sin` function, returns values in the range `-œÄ / 2 ‚â§ arcsin x ‚â§ œÄ / 2`.
It defaults to `-œÄ / 2` on `(-‚àû, -1)` and to `œÄ / 2` to `(1, ‚àû)`. -/
@[pp_nodot]
noncomputable def arcsin : ‚Ñù ‚Üí ‚Ñù :=
  Subtype.val ‚àò IccExtend (neg_le_self zero_le_one) sinOrderIso.symm

theorem arcsin_mem_Icc (x : ‚Ñù) : arcsin x ‚àà Icc (-(œÄ / 2)) (œÄ / 2) :=
  Subtype.coe_prop _

@[simp]
theorem range_arcsin : range arcsin = Icc (-(œÄ / 2)) (œÄ / 2) := by
  rw [arcsin, range_comp Subtype.val]
  simp [Icc]

theorem arcsin_le_pi_div_two (x : ‚Ñù) : arcsin x ‚â§ œÄ / 2 :=
  (arcsin_mem_Icc x).2

theorem neg_pi_div_two_le_arcsin (x : ‚Ñù) : -(œÄ / 2) ‚â§ arcsin x :=
  (arcsin_mem_Icc x).1

theorem arcsin_projIcc (x : ‚Ñù) :
    arcsin (projIcc (-1) 1 (neg_le_self zero_le_one) x) = arcsin x := by
  rw [arcsin, Function.comp_apply, IccExtend_val, Function.comp_apply, IccExtend,
        Function.comp_apply]

theorem sin_arcsin' {x : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) : sin (arcsin x) = x := by
  simpa [arcsin, IccExtend_of_mem _ _ hx, -OrderIso.apply_symm_apply] using
    Subtype.ext_iff.1 (sinOrderIso.apply_symm_apply ‚ü®x, hx‚ü©)

theorem sin_arcsin {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : sin (arcsin x) = x :=
  sin_arcsin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©

theorem arcsin_sin' {x : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) : arcsin (sin x) = x :=
  injOn_sin (arcsin_mem_Icc _) hx <| by rw [sin_arcsin (neg_one_le_sin _) (sin_le_one _)]

theorem arcsin_sin {x : ‚Ñù} (hx‚ÇÅ : -(œÄ / 2) ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ / 2) : arcsin (sin x) = x :=
  arcsin_sin' ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü©

theorem strictMonoOn_arcsin : StrictMonoOn arcsin (Icc (-1) 1) :=
  (Subtype.strictMono_coe _).comp_strictMonoOn <|
    sinOrderIso.symm.strictMono.strictMonoOn_IccExtend _

@[gcongr]
theorem arcsin_lt_arcsin {x y : ‚Ñù} (hx : -1 ‚â§ x) (hlt : x < y) (hy : y ‚â§ 1) :
    arcsin x < arcsin y :=
  strictMonoOn_arcsin ‚ü®hx, hlt.le.trans hy‚ü© ‚ü®hx.trans hlt.le, hy‚ü© hlt

theorem monotone_arcsin : Monotone arcsin :=
  (Subtype.mono_coe _).comp <| sinOrderIso.symm.monotone.IccExtend _

@[gcongr]
theorem arcsin_le_arcsin {x y : ‚Ñù} (h : x ‚â§ y) : arcsin x ‚â§ arcsin y := monotone_arcsin h

theorem injOn_arcsin : InjOn arcsin (Icc (-1) 1) :=
  strictMonoOn_arcsin.injOn

theorem arcsin_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
    arcsin x = arcsin y ‚Üî x = y :=
  injOn_arcsin.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©

@[continuity, fun_prop]
theorem continuous_arcsin : Continuous arcsin :=
  continuous_subtype_val.comp sinOrderIso.symm.continuous.Icc_extend'

@[fun_prop]
theorem continuousAt_arcsin {x : ‚Ñù} : ContinuousAt arcsin x :=
  continuous_arcsin.continuousAt

theorem arcsin_eq_of_sin_eq {x y : ‚Ñù} (h‚ÇÅ : sin x = y) (h‚ÇÇ : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin y = x := by
  subst y
  exact injOn_sin (arcsin_mem_Icc _) h‚ÇÇ (sin_arcsin' (sin_mem_Icc x))

@[simp]
theorem arcsin_zero : arcsin 0 = 0 :=
  arcsin_eq_of_sin_eq sin_zero ‚ü®neg_nonpos.2 pi_div_two_pos.le, pi_div_two_pos.le‚ü©

@[simp]
theorem arcsin_one : arcsin 1 = œÄ / 2 :=
  arcsin_eq_of_sin_eq sin_pi_div_two <| right_mem_Icc.2 (neg_le_self pi_div_two_pos.le)

theorem arcsin_of_one_le {x : ‚Ñù} (hx : 1 ‚â§ x) : arcsin x = œÄ / 2 := by
  rw [‚Üê arcsin_projIcc, projIcc_of_right_le _ hx, Subtype.coe_mk, arcsin_one]

theorem arcsin_neg_one : arcsin (-1) = -(œÄ / 2) :=
  arcsin_eq_of_sin_eq (by rw [sin_neg, sin_pi_div_two]) <|
    left_mem_Icc.2 (neg_le_self pi_div_two_pos.le)

theorem arcsin_of_le_neg_one {x : ‚Ñù} (hx : x ‚â§ -1) : arcsin x = -(œÄ / 2) := by
  rw [‚Üê arcsin_projIcc, projIcc_of_le_left _ hx, Subtype.coe_mk, arcsin_neg_one]

@[simp]
theorem arcsin_neg (x : ‚Ñù) : arcsin (-x) = -arcsin x := by
  rcases le_total x (-1) with hx‚ÇÅ | hx‚ÇÅ
  ¬∑ rw [arcsin_of_le_neg_one hx‚ÇÅ, neg_neg, arcsin_of_one_le (le_neg.2 hx‚ÇÅ)]
  rcases le_total 1 x with hx‚ÇÇ | hx‚ÇÇ
  ¬∑ rw [arcsin_of_one_le hx‚ÇÇ, arcsin_of_le_neg_one (neg_le_neg hx‚ÇÇ)]
  refine arcsin_eq_of_sin_eq ?_ ?_
  ¬∑ rw [sin_neg, sin_arcsin hx‚ÇÅ hx‚ÇÇ]
  ¬∑ exact ‚ü®neg_le_neg (arcsin_le_pi_div_two _), neg_le.2 (neg_pi_div_two_le_arcsin _)‚ü©

theorem arcsin_le_iff_le_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x ‚â§ y ‚Üî x ‚â§ sin y := by
  rw [‚Üê arcsin_sin' hy, strictMonoOn_arcsin.le_iff_le hx (sin_mem_Icc _), arcsin_sin' hy]

theorem arcsin_le_iff_le_sin' {x y : ‚Ñù} (hy : y ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x ‚â§ y ‚Üî x ‚â§ sin y := by
  rcases le_total x (-1) with hx‚ÇÅ | hx‚ÇÅ
  ¬∑ simp [arcsin_of_le_neg_one hx‚ÇÅ, hy.1, hx‚ÇÅ.trans (neg_one_le_sin _)]
  rcases lt_or_ge 1 x with hx‚ÇÇ | hx‚ÇÇ
  ¬∑ simp [arcsin_of_one_le hx‚ÇÇ.le, hy.2.not_ge, (sin_le_one y).trans_lt hx‚ÇÇ]
  exact arcsin_le_iff_le_sin ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© (mem_Icc_of_Ico hy)

theorem le_arcsin_iff_sin_le {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
    x ‚â§ arcsin y ‚Üî sin x ‚â§ y := by
  rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg,
    arcsin_le_iff_le_sin ‚ü®neg_le_neg hy.2, neg_le.2 hy.1‚ü© ‚ü®neg_le_neg hx.2, neg_le.2 hx.1‚ü©, sin_neg,
    neg_le_neg_iff]

theorem le_arcsin_iff_sin_le' {x y : ‚Ñù} (hx : x ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
    x ‚â§ arcsin y ‚Üî sin x ‚â§ y := by
  rw [‚Üê neg_le_neg_iff, ‚Üê arcsin_neg, arcsin_le_iff_le_sin' ‚ü®neg_le_neg hx.2, neg_lt.2 hx.1‚ü©,
    sin_neg, neg_le_neg_iff]

theorem arcsin_lt_iff_lt_sin {x y : ‚Ñù} (hx : x ‚àà Icc (-1 : ‚Ñù) 1) (hy : y ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x < y ‚Üî x < sin y :=
  not_le.symm.trans <| (not_congr <| le_arcsin_iff_sin_le hy hx).trans not_le

theorem arcsin_lt_iff_lt_sin' {x y : ‚Ñù} (hy : y ‚àà Ioc (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x < y ‚Üî x < sin y :=
  not_le.symm.trans <| (not_congr <| le_arcsin_iff_sin_le' hy).trans not_le

theorem lt_arcsin_iff_sin_lt {x y : ‚Ñù} (hx : x ‚àà Icc (-(œÄ / 2)) (œÄ / 2)) (hy : y ‚àà Icc (-1 : ‚Ñù) 1) :
    x < arcsin y ‚Üî sin x < y :=
  not_le.symm.trans <| (not_congr <| arcsin_le_iff_le_sin hy hx).trans not_le

theorem lt_arcsin_iff_sin_lt' {x y : ‚Ñù} (hx : x ‚àà Ico (-(œÄ / 2)) (œÄ / 2)) :
    x < arcsin y ‚Üî sin x < y :=
  not_le.symm.trans <| (not_congr <| arcsin_le_iff_le_sin' hx).trans not_le

theorem arcsin_eq_iff_eq_sin {x y : ‚Ñù} (hy : y ‚àà Ioo (-(œÄ / 2)) (œÄ / 2)) :
    arcsin x = y ‚Üî x = sin y := by
  simp only [le_antisymm_iff, arcsin_le_iff_le_sin' (mem_Ico_of_Ioo hy),
    le_arcsin_iff_sin_le' (mem_Ioc_of_Ioo hy)]

@[simp]
theorem arcsin_nonneg {x : ‚Ñù} : 0 ‚â§ arcsin x ‚Üî 0 ‚â§ x :=
  (le_arcsin_iff_sin_le' ‚ü®neg_lt_zero.2 pi_div_two_pos, pi_div_two_pos.le‚ü©).trans <| by
    rw [sin_zero]

@[simp]
theorem arcsin_nonpos {x : ‚Ñù} : arcsin x ‚â§ 0 ‚Üî x ‚â§ 0 :=
  neg_nonneg.symm.trans <| arcsin_neg x ‚ñ∏ arcsin_nonneg.trans neg_nonneg

@[simp]
theorem arcsin_eq_zero_iff {x : ‚Ñù} : arcsin x = 0 ‚Üî x = 0 := by simp [le_antisymm_iff]

@[simp]
theorem zero_eq_arcsin_iff {x} : 0 = arcsin x ‚Üî x = 0 :=
  eq_comm.trans arcsin_eq_zero_iff

@[simp]
theorem arcsin_pos {x : ‚Ñù} : 0 < arcsin x ‚Üî 0 < x :=
  lt_iff_lt_of_le_iff_le arcsin_nonpos

@[simp]
theorem arcsin_lt_zero {x : ‚Ñù} : arcsin x < 0 ‚Üî x < 0 :=
  lt_iff_lt_of_le_iff_le arcsin_nonneg

@[simp]
theorem arcsin_lt_pi_div_two {x : ‚Ñù} : arcsin x < œÄ / 2 ‚Üî x < 1 :=
  (arcsin_lt_iff_lt_sin' (right_mem_Ioc.2 <| neg_lt_self pi_div_two_pos)).trans <| by
    rw [sin_pi_div_two]

@[simp]
theorem neg_pi_div_two_lt_arcsin {x : ‚Ñù} : -(œÄ / 2) < arcsin x ‚Üî -1 < x :=
  (lt_arcsin_iff_sin_lt' <| left_mem_Ico.2 <| neg_lt_self pi_div_two_pos).trans <| by
    rw [sin_neg, sin_pi_div_two]

@[simp]
theorem arcsin_eq_pi_div_two {x : ‚Ñù} : arcsin x = œÄ / 2 ‚Üî 1 ‚â§ x :=
  ‚ü®fun h => not_lt.1 fun h' => (arcsin_lt_pi_div_two.2 h').ne h, arcsin_of_one_le‚ü©

@[simp]
theorem pi_div_two_eq_arcsin {x} : œÄ / 2 = arcsin x ‚Üî 1 ‚â§ x :=
  eq_comm.trans arcsin_eq_pi_div_two

@[simp]
theorem pi_div_two_le_arcsin {x} : œÄ / 2 ‚â§ arcsin x ‚Üî 1 ‚â§ x :=
  (arcsin_le_pi_div_two x).le_iff_eq.trans pi_div_two_eq_arcsin

@[simp]
theorem arcsin_eq_neg_pi_div_two {x : ‚Ñù} : arcsin x = -(œÄ / 2) ‚Üî x ‚â§ -1 :=
  ‚ü®fun h => not_lt.1 fun h' => (neg_pi_div_two_lt_arcsin.2 h').ne' h, arcsin_of_le_neg_one‚ü©

@[simp]
theorem neg_pi_div_two_eq_arcsin {x} : -(œÄ / 2) = arcsin x ‚Üî x ‚â§ -1 :=
  eq_comm.trans arcsin_eq_neg_pi_div_two

@[simp]
theorem arcsin_le_neg_pi_div_two {x} : arcsin x ‚â§ -(œÄ / 2) ‚Üî x ‚â§ -1 :=
  (neg_pi_div_two_le_arcsin x).le_iff_eq.trans arcsin_eq_neg_pi_div_two

@[simp]
theorem pi_div_four_le_arcsin {x} : œÄ / 4 ‚â§ arcsin x ‚Üî ‚àö2 / 2 ‚â§ x := by
  rw [‚Üê sin_pi_div_four, le_arcsin_iff_sin_le']
  have := pi_pos
  constructor <;> linarith

theorem mapsTo_sin_Ioo : MapsTo sin (Ioo (-(œÄ / 2)) (œÄ / 2)) (Ioo (-1) 1) := fun x h => by
  rwa [mem_Ioo, ‚Üê arcsin_lt_pi_div_two, ‚Üê neg_pi_div_two_lt_arcsin, arcsin_sin h.1.le h.2.le]

/-- `Real.sin` as a `PartialHomeomorph` between `(-œÄ / 2, œÄ / 2)` and `(-1, 1)`. -/
@[simp]
def sinPartialHomeomorph : PartialHomeomorph ‚Ñù ‚Ñù where
  toFun := sin
  invFun := arcsin
  source := Ioo (-(œÄ / 2)) (œÄ / 2)
  target := Ioo (-1) 1
  map_source' := mapsTo_sin_Ioo
  map_target' _ hy := ‚ü®neg_pi_div_two_lt_arcsin.2 hy.1, arcsin_lt_pi_div_two.2 hy.2‚ü©
  left_inv' _ hx := arcsin_sin hx.1.le hx.2.le
  right_inv' _ hy := sin_arcsin hy.1.le hy.2.le
  open_source := isOpen_Ioo
  open_target := isOpen_Ioo
  continuousOn_toFun := continuous_sin.continuousOn
  continuousOn_invFun := continuous_arcsin.continuousOn

theorem cos_arcsin_nonneg (x : ‚Ñù) : 0 ‚â§ cos (arcsin x) :=
  cos_nonneg_of_mem_Icc ‚ü®neg_pi_div_two_le_arcsin _, arcsin_le_pi_div_two _‚ü©

-- The junk values for `arcsin` and `sqrt` make this true even outside `[-1, 1]`.
theorem cos_arcsin (x : ‚Ñù) : cos (arcsin x) = ‚àö(1 - x ^ 2) := by
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  ¬∑ rw [not_le] at hx‚ÇÅ
    rw [arcsin_of_le_neg_one hx‚ÇÅ.le, cos_neg, cos_pi_div_two, sqrt_eq_zero_of_nonpos]
    nlinarith
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  ¬∑ rw [not_le] at hx‚ÇÇ
    rw [arcsin_of_one_le hx‚ÇÇ.le, cos_pi_div_two, sqrt_eq_zero_of_nonpos]
    nlinarith
  have : sin (arcsin x) ^ 2 + cos (arcsin x) ^ 2 = 1 := sin_sq_add_cos_sq (arcsin x)
  rw [‚Üê eq_sub_iff_add_eq', ‚Üê sqrt_inj (sq_nonneg _) (sub_nonneg.2 (sin_sq_le_one (arcsin x))), sq,
    sqrt_mul_self (cos_arcsin_nonneg _)] at this
  rw [this, sin_arcsin hx‚ÇÅ hx‚ÇÇ]

-- The junk values for `arcsin` and `sqrt` make this true even outside `[-1, 1]`.
theorem tan_arcsin (x : ‚Ñù) : tan (arcsin x) = x / ‚àö(1 - x ^ 2) := by
  rw [tan_eq_sin_div_cos, cos_arcsin]
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  ¬∑ have h : ‚àö(1 - x ^ 2) = 0 := sqrt_eq_zero_of_nonpos (by nlinarith)
    rw [h]
    simp
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  ¬∑ have h : ‚àö(1 - x ^ 2) = 0 := sqrt_eq_zero_of_nonpos (by nlinarith)
    rw [h]
    simp
  rw [sin_arcsin hx‚ÇÅ hx‚ÇÇ]

/-- Inverse of the `cos` function, returns values in the range `0 ‚â§ arccos x` and `arccos x ‚â§ œÄ`.
  It defaults to `œÄ` on `(-‚àû, -1)` and to `0` to `(1, ‚àû)`. -/
@[pp_nodot]
noncomputable def arccos (x : ‚Ñù) : ‚Ñù :=
  œÄ / 2 - arcsin x

theorem arccos_eq_pi_div_two_sub_arcsin (x : ‚Ñù) : arccos x = œÄ / 2 - arcsin x :=
  rfl

theorem arcsin_eq_pi_div_two_sub_arccos (x : ‚Ñù) : arcsin x = œÄ / 2 - arccos x := by simp [arccos]

theorem arccos_le_pi (x : ‚Ñù) : arccos x ‚â§ œÄ := by
  unfold arccos; linarith [neg_pi_div_two_le_arcsin x]

theorem arccos_nonneg (x : ‚Ñù) : 0 ‚â§ arccos x := by
  unfold arccos; linarith [arcsin_le_pi_div_two x]

@[simp]
theorem arccos_pos {x : ‚Ñù} : 0 < arccos x ‚Üî x < 1 := by simp [arccos]

theorem cos_arccos {x : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) : cos (arccos x) = x := by
  rw [arccos, cos_pi_div_two_sub, sin_arcsin hx‚ÇÅ hx‚ÇÇ]

theorem arccos_cos {x : ‚Ñù} (hx‚ÇÅ : 0 ‚â§ x) (hx‚ÇÇ : x ‚â§ œÄ) : arccos (cos x) = x := by
  rw [arccos, ‚Üê sin_pi_div_two_sub, arcsin_sin] <;> simp [sub_eq_add_neg] <;> linarith

lemma arccos_eq_of_eq_cos (hy‚ÇÄ : 0 ‚â§ y) (hy‚ÇÅ : y ‚â§ œÄ) (hxy : x = cos y) : arccos x = y := by
  rw [hxy, arccos_cos hy‚ÇÄ hy‚ÇÅ]

theorem strictAntiOn_arccos : StrictAntiOn arccos (Icc (-1) 1) := fun _ hx _ hy h =>
  sub_lt_sub_left (strictMonoOn_arcsin hx hy h) _

@[gcongr]
lemma arccos_lt_arccos {x y : ‚Ñù} (hx : -1 ‚â§ x) (hlt : x < y) (hy : y ‚â§ 1) :
    arccos y < arccos x := by
  unfold arccos; gcongr <;> assumption

@[gcongr]
lemma arccos_le_arccos {x y : ‚Ñù} (hlt : x ‚â§ y) : arccos y ‚â§ arccos x := by unfold arccos; gcongr

theorem antitone_arccos : Antitone arccos := fun _ _ ‚Ü¶ arccos_le_arccos

theorem arccos_injOn : InjOn arccos (Icc (-1) 1) :=
  strictAntiOn_arccos.injOn

theorem arccos_inj {x y : ‚Ñù} (hx‚ÇÅ : -1 ‚â§ x) (hx‚ÇÇ : x ‚â§ 1) (hy‚ÇÅ : -1 ‚â§ y) (hy‚ÇÇ : y ‚â§ 1) :
    arccos x = arccos y ‚Üî x = y :=
  arccos_injOn.eq_iff ‚ü®hx‚ÇÅ, hx‚ÇÇ‚ü© ‚ü®hy‚ÇÅ, hy‚ÇÇ‚ü©

@[simp]
theorem arccos_zero : arccos 0 = œÄ / 2 := by simp [arccos]

@[simp]
theorem arccos_one : arccos 1 = 0 := by simp [arccos]

@[simp]
theorem arccos_neg_one : arccos (-1) = œÄ := by simp [arccos, add_halves]

@[simp]
theorem arccos_eq_zero {x} : arccos x = 0 ‚Üî 1 ‚â§ x := by simp [arccos, sub_eq_zero]

@[simp]
theorem arccos_eq_pi_div_two {x} : arccos x = œÄ / 2 ‚Üî x = 0 := by simp [arccos]

@[simp]
theorem arccos_eq_pi {x} : arccos x = œÄ ‚Üî x ‚â§ -1 := by
  rw [arccos, sub_eq_iff_eq_add, ‚Üê sub_eq_iff_eq_add', div_two_sub_self, neg_pi_div_two_eq_arcsin]

theorem arccos_neg (x : ‚Ñù) : arccos (-x) = œÄ - arccos x := by
  rw [‚Üê add_halves œÄ, arccos, arcsin_neg, arccos, add_sub_assoc, sub_sub_self, sub_neg_eq_add]

theorem arccos_of_one_le {x : ‚Ñù} (hx : 1 ‚â§ x) : arccos x = 0 := by
  rw [arccos, arcsin_of_one_le hx, sub_self]

theorem arccos_of_le_neg_one {x : ‚Ñù} (hx : x ‚â§ -1) : arccos x = œÄ := by
  rw [arccos, arcsin_of_le_neg_one hx, sub_neg_eq_add, add_halves]

-- The junk values for `arccos` and `sqrt` make this true even outside `[-1, 1]`.
theorem sin_arccos (x : ‚Ñù) : sin (arccos x) = ‚àö(1 - x ^ 2) := by
  by_cases hx‚ÇÅ : -1 ‚â§ x; swap
  ¬∑ rw [not_le] at hx‚ÇÅ
    rw [arccos_of_le_neg_one hx‚ÇÅ.le, sin_pi, sqrt_eq_zero_of_nonpos]
    nlinarith
  by_cases hx‚ÇÇ : x ‚â§ 1; swap
  ¬∑ rw [not_le] at hx‚ÇÇ
    rw [arccos_of_one_le hx‚ÇÇ.le, sin_zero, sqrt_eq_zero_of_nonpos]
    nlinarith
  rw [arccos_eq_pi_div_two_sub_arcsin, sin_pi_div_two_sub, cos_arcsin]

@[simp]
theorem arccos_le_pi_div_two {x} : arccos x ‚â§ œÄ / 2 ‚Üî 0 ‚â§ x := by simp [arccos]

@[simp]
theorem arccos_lt_pi_div_two {x : ‚Ñù} : arccos x < œÄ / 2 ‚Üî 0 < x := by simp [arccos]

@[simp]
theorem arccos_le_pi_div_four {x} : arccos x ‚â§ œÄ / 4 ‚Üî ‚àö2 / 2 ‚â§ x := by
  rw [arccos, ‚Üê pi_div_four_le_arcsin]
  constructor <;>
    ¬∑ intro
      linarith

@[continuity, fun_prop]
theorem continuous_arccos : Continuous arccos :=
  continuous_const.sub continuous_arcsin

-- The junk values for `arccos` and `sqrt` make this true even outside `[-1, 1]`.
theorem tan_arccos (x : ‚Ñù) : tan (arccos x) = ‚àö(1 - x ^ 2) / x := by
  rw [arccos, tan_pi_div_two_sub, tan_arcsin, inv_div]

-- The junk values for `arccos` and `sqrt` make this true even for `1 < x`.
theorem arccos_eq_arcsin {x : ‚Ñù} (h : 0 ‚â§ x) : arccos x = arcsin (‚àö(1 - x ^ 2)) :=
  (arcsin_eq_of_sin_eq (sin_arccos _)
      ‚ü®(Left.neg_nonpos_iff.2 (div_nonneg pi_pos.le (by norm_num))).trans (arccos_nonneg _),
        arccos_le_pi_div_two.2 h‚ü©).symm

-- The junk values for `arcsin` and `sqrt` make this true even for `1 < x`.
theorem arcsin_eq_arccos {x : ‚Ñù} (h : 0 ‚â§ x) : arcsin x = arccos (‚àö(1 - x ^ 2)) := by
  rw [eq_comm, ‚Üê cos_arcsin]
  exact
    arccos_cos (arcsin_nonneg.2 h)
      ((arcsin_le_pi_div_two _).trans (div_le_self pi_pos.le one_le_two))

end Real

open Real

/-!
### Convenience dot notation lemmas
-/

namespace Filter.Tendsto

variable {Œ± : Type*} {l : Filter Œ±} {x : ‚Ñù} {f : Œ± ‚Üí ‚Ñù}

protected theorem arcsin (h : Tendsto f l (ùìù x)) : Tendsto (arcsin <| f ¬∑) l (ùìù (arcsin x)) :=
  (continuous_arcsin.tendsto _).comp h

theorem arcsin_nhdsLE (h : Tendsto f l (ùìù[‚â§] x)) :
    Tendsto (arcsin <| f ¬∑) l (ùìù[‚â§] (arcsin x)) := by
  refine ((continuous_arcsin.tendsto _).inf <| MapsTo.tendsto fun y hy ‚Ü¶ ?_).comp h
  exact monotone_arcsin hy

theorem arcsin_nhdsGE (h : Tendsto f l (ùìù[‚â•] x)) : Tendsto (arcsin <| f ¬∑) l (ùìù[‚â•] (arcsin x)) :=
  ((continuous_arcsin.tendsto _).inf <| MapsTo.tendsto fun _ ‚Ü¶ arcsin_le_arcsin).comp h

protected theorem arccos (h : Tendsto f l (ùìù x)) : Tendsto (arccos <| f ¬∑) l (ùìù (arccos x)) :=
  (continuous_arccos.tendsto _).comp h

theorem arccos_nhdsLE (h : Tendsto f l (ùìù[‚â§] x)) : Tendsto (arccos <| f ¬∑) l (ùìù[‚â•] (arccos x)) :=
  ((continuous_arccos.tendsto _).inf <| MapsTo.tendsto fun _ ‚Ü¶ arccos_le_arccos).comp h

theorem arccos_nhdsGE (h : Tendsto f l (ùìù[‚â•] x)) :
    Tendsto (arccos <| f ¬∑) l (ùìù[‚â§] (arccos x)) := by
  refine ((continuous_arccos.tendsto _).inf <| MapsTo.tendsto fun y hy ‚Ü¶ ?_).comp h
  simp only [mem_Ici, mem_Iic] at hy ‚ä¢
  exact antitone_arccos hy

end Filter.Tendsto

variable {X : Type*} [TopologicalSpace X] {f : X ‚Üí ‚Ñù} {s : Set X} {x : X}

protected nonrec theorem ContinuousWithinAt.arcsin (h : ContinuousWithinAt f s x) :
    ContinuousWithinAt (arcsin <| f ¬∑) s x :=
  h.arcsin

protected nonrec theorem ContinuousWithinAt.arccos (h : ContinuousWithinAt f s x) :
    ContinuousWithinAt (arccos <| f ¬∑) s x :=
  h.arccos

protected nonrec theorem ContinuousAt.arcsin (h : ContinuousAt f x) :
    ContinuousAt (arcsin <| f ¬∑) x :=
  h.arcsin

protected nonrec theorem ContinuousAt.arccos (h : ContinuousAt f x) :
    ContinuousAt (arccos <| f ¬∑) x :=
  h.arccos

protected theorem ContinuousOn.arcsin (h : ContinuousOn f s) : ContinuousOn (arcsin <| f ¬∑) s :=
  fun x hx ‚Ü¶ (h x hx).arcsin

protected theorem ContinuousOn.arccos (h : ContinuousOn f s) : ContinuousOn (arccos <| f ¬∑) s :=
  fun x hx ‚Ü¶ (h x hx).arccos

protected theorem Continuous.arcsin (h : Continuous f) : Continuous (arcsin <| f ¬∑) :=
  continuous_arcsin.comp h

protected theorem Continuous.arccos (h : Continuous f) : Continuous (arccos <| f ¬∑) :=
  continuous_arccos.comp h
