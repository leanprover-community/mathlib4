/-
Copyright (c) 2025 Mitchell Horner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mitchell Horner
-/
import Mathlib.Analysis.Convex.Deriv
import Mathlib.Data.Nat.Choose.Cast

/-!
# Pochhammer polynomials

This file proves analysis theorems for Pochhammer polynomials.

## Main definitions

* `Differentiable.descPochhammer_eval` is the proof that the descending Pochhammer polynomial
  `descPochhammer â„ n` is differentiable.

* `ConvexOn.descPochhammer_eval` is the proof that the descending Pochhammer polynomial
  `descPochhammer â„ n` is convex on `[n-1, âˆ)`.

* `descPochhammer_eval_le_sum_descFactorial` is a special case of **Jensen's inequality**
  for `Nat.descFactorial`.

* `descPochhammer_eval_div_factorial_le_sum_choose` is a special case of **Jensen's inequality**
  for `Nat.choose`.
-/


section DescPochhammer

variable {n : â„•} {ğ•œ : Type*} {k : ğ•œ} [NontriviallyNormedField ğ•œ]

lemma descPochhammer_eval_eq_prod_range (n : â„•) (k : ğ•œ) :
    (descPochhammer ğ•œ n).eval k = âˆ j âˆˆ Finset.range n, (k-j) := by
  induction' n with n ih
  Â· simp
  Â· simp [descPochhammer_succ_right, ih, â†Finset.prod_range_succ]

/-- `descPochhammer ğ•œ n` is differentiable. -/
theorem Differentiable.descPochhammer_eval : Differentiable ğ•œ (descPochhammer ğ•œ n).eval := by
  simp [descPochhammer_eval_eq_prod_range, Differentiable.finset_prod]

/-- `descPochhammer ğ•œ n` is continuous. -/
theorem Continuous.descPochhammer_eval : Continuous (descPochhammer ğ•œ n).eval := by
  exact Differentiable.descPochhammer_eval.continuous

lemma deriv_descPochhammer_eval_eq_sum_prod_range_erase (n : â„•) (k : ğ•œ) :
    deriv (descPochhammer ğ•œ n).eval k
      = âˆ‘ i âˆˆ Finset.range n, âˆ j âˆˆ (Finset.range n).erase i, (k-j) := by
  simp [descPochhammer_eval_eq_prod_range, deriv_finset_prod]

/-- `deriv (descPochhammer â„ n)` is monotone on `(n-1, âˆ)`. -/
lemma MonotoneOn.deriv_descPochhammer_eval (n : â„•) :
    MonotoneOn (deriv (descPochhammer â„ n).eval) (Set.Ioi (n-1 : â„)) := by
  induction' n with n ih
  Â· simp [monotoneOn_const]
  Â· intro a ha b hb hab
    rw [Set.mem_Ioi, Nat.cast_add_one, add_sub_cancel_right] at ha hb
    simp_rw [deriv_descPochhammer_eval_eq_sum_prod_range_erase]
    apply Finset.sum_le_sum; intro i hi
    apply Finset.prod_le_prod
    Â· intro j hj
      rw [Finset.mem_erase, Finset.mem_range] at hj
      apply sub_nonneg_of_le
      exact ha.le.trans' (mod_cast Nat.le_pred_of_lt hj.2)
    Â· intro j hj
      rwa [â†sub_le_sub_iff_right (j : â„)] at hab

/-- `descPochhammer â„ n` is convex on `[n-1, âˆ)`. -/
theorem ConvexOn.descPochhammer_eval (n : â„•) :
    ConvexOn â„ (Set.Ici (n-1 : â„)) (descPochhammer â„ n).eval := by
  rcases n.eq_zero_or_pos with heq | hpos
  Â· simp [heq, convexOn_const, convex_Ici]
  Â· apply MonotoneOn.convexOn_of_deriv (convex_Ici (n-1 : â„))
      Continuous.descPochhammer_eval.continuousOn
      Differentiable.descPochhammer_eval.differentiableOn
    rw [interior_Ici]
    exact MonotoneOn.deriv_descPochhammer_eval n

/-- `0` for `(-âˆ, n-1)` and `descPochhammer â„ n` for `[n-1, âˆ)` is convex on the real line. -/
lemma ConvexOn.ite_descPochhammer_eval (hn : 1 â‰¤ n) :
    ConvexOn â„ Set.univ (fun (x : â„) â†¦ if n-1 â‰¤ x then (descPochhammer â„ n).eval x else 0) := by
  refine âŸ¨convex_univ, fun x _ y _ a b ha hb hab â†¦ ?_âŸ©
  by_cases hx : n-1 â‰¤ x <;> by_cases hy : n-1 â‰¤ y
  Â· have hcombo : n-1 â‰¤ a*x+b*y := (le_min hx hy).trans (Convex.min_le_combo x y ha hb hab)
    simpa [hcombo, hx, hy] using (ConvexOn.descPochhammer_eval n).2 hx hy ha hb hab
  Â· have hcombo' : a*x+b*y â‰¤ a*x+b*(n-1) :=
      add_le_add_left (mul_le_mul_of_nonneg_left (le_of_not_le hy) hb) (a*x)
    by_cases hcombo : n-1 â‰¤ a*x+b*y
    Â· trans (descPochhammer â„ n).eval (a*x+b*(n-1))
      Â· simpa [hcombo] using MonotoneOn.descPochhammer_eval n hcombo (hcombo.trans hcombo') hcombo'
      Â· simpa [â†descPochhammer_eval_coe_nat_of_lt (Nat.sub_one_lt_of_lt hn), Nat.cast_pred hn,
          hx, hy] using (ConvexOn.descPochhammer_eval n).2 hx (le_refl (n-1 : â„)) ha hb hab
    Â· simpa [hcombo, hx, hy] using mul_nonneg ha (descPochhammer_nonneg hx)
  Â· have hcombo' : a*x+b*y â‰¤ a*(n-1)+b*y :=
      add_le_add_right (mul_le_mul_of_nonneg_left (le_of_not_le hx) ha) (b*y)
    by_cases hcombo : n-1 â‰¤ a*x+b*y
    Â· trans (descPochhammer â„ n).eval (a*(n-1)+b*y)
      Â· simpa [hcombo] using MonotoneOn.descPochhammer_eval n hcombo (hcombo.trans hcombo') hcombo'
      Â· simpa [â†descPochhammer_eval_coe_nat_of_lt (Nat.sub_one_lt_of_lt hn), Nat.cast_pred hn,
          hx, hy] using (ConvexOn.descPochhammer_eval n).2 (le_refl (n-1 : â„)) hy ha hb hab
    Â· simpa [hcombo, hx, hy] using mul_nonneg hb (descPochhammer_nonneg hy)
  Â· have hcombo : a*x+b*y â‰¤ n-1 :=
      (Convex.combo_le_max x y ha hb hab).trans (max_le (le_of_not_le hx) (le_of_not_le hy))
    rcases lt_or_eq_of_le hcombo with hlt | heq
    Â· simp [not_le_of_lt hlt, hx, hy]
    Â· simpa [heq, hx, hy, Nat.cast_pred hn] using
        (descPochhammer_eval_coe_nat_of_lt (Nat.sub_one_lt_of_lt hn)).le

private lemma ite_descPochhammer_eval_eq_descFactorial (k n : â„•) :
    (if (n-1 : â„) â‰¤ (k : â„) then (descPochhammer â„ n).eval (k : â„) else 0)
      = k.descFactorial n := by
  rw [descPochhammer_eval_eq_descFactorial â„ k n, ite_eq_left_iff, not_le, eq_comm,
    Nat.cast_eq_zero, Nat.descFactorial_eq_zero_iff_lt, â†@Nat.cast_lt â„]
  exact (sub_lt_self (n : â„) zero_lt_one).trans'

/-- Special case of **Jensen's inequality** for `Nat.descFactorial`. -/
theorem descPochhammer_eval_le_sum_descFactorial
    (hn : 1 â‰¤ n) {Î¹ : Type*} {t : Finset Î¹} (p : Î¹ â†’ â„•) (w : Î¹ â†’ â„)
    (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (h_avg : n-1 â‰¤ âˆ‘ i âˆˆ t, w i * p i) :
    (descPochhammer â„ n).eval (âˆ‘ i âˆˆ t, w i * p i)
      â‰¤ âˆ‘ i âˆˆ t, w i * (p i).descFactorial n := by
  let f : â„ â†’ â„ := fun (x : â„) â†¦ if n-1 â‰¤ x then (descPochhammer â„ n).eval x else 0
  suffices h_jensen : f (âˆ‘ i âˆˆ t, w i â€¢ p i) â‰¤ âˆ‘ i âˆˆ t, w i â€¢ f (p i) by
    simpa only [smul_eq_mul, f, h_avg, ite_descPochhammer_eval_eq_descFactorial] using h_jensen
  exact ConvexOn.map_sum_le (ConvexOn.ite_descPochhammer_eval hn) hâ‚€ hâ‚ (by simp)

/-- Special case of **Jensen's inequality** for `Nat.choose`. -/
theorem descPochhammer_eval_div_factorial_le_sum_choose
    (hn : 1 â‰¤ n) {Î¹ : Type*} {t : Finset Î¹} (p : Î¹ â†’ â„•) (w : Î¹ â†’ â„)
    (hâ‚€ : âˆ€ i âˆˆ t, 0 â‰¤ w i) (hâ‚ : âˆ‘ i âˆˆ t, w i = 1) (h_avg : n-1 â‰¤ âˆ‘ i âˆˆ t, w i * p i) :
    (descPochhammer â„ n).eval (âˆ‘ i âˆˆ t, w i * p i) / n.factorial
      â‰¤ âˆ‘ i âˆˆ t, w i * (p i).choose n := by
  simp_rw [Nat.cast_choose_eq_descPochhammer_div,
    mul_div, â†Finset.sum_div, descPochhammer_eval_eq_descFactorial]
  apply div_le_div_of_nonneg_right _ (Nat.cast_nonneg n.factorial)
  exact descPochhammer_eval_le_sum_descFactorial hn p w hâ‚€ hâ‚ h_avg

end DescPochhammer
