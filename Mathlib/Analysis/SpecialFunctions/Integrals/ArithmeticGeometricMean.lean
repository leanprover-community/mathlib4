/-
Copyright (c) 2026 Jeremy Tan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Tan
-/
module

public import Mathlib.Analysis.SpecialFunctions.ArithmeticGeometricMean
public import Mathlib.Analysis.SpecialFunctions.ImproperIntegrals

/-!
# An integral related to the arithmetic-geometric mean

For $a,b>0$ consider the improper integral
$$I(a,b) = \int_0^\infty \frac{dx}{\sqrt{(x^2+a^2)(x^2+b^2)}}.$$
Then making the substitution $x = t+\sqrt{t^2+ab}$ yields, after rearranging,
$$I(a,b) = \int_{-\infty}^\infty \frac{dx}{2\sqrt{(x^2+ab)(x^2+((a+b)/2)^2)}}
= I(\sqrt{ab},(a+b)/2).$$
As shown in `Mathlib/Analysis/SpecialFunctions/AGM/Basic.lean`, iterating this transformation drives
both arguments towards the arithmetic-geometric mean (AGM) of $a$ and $b$, so we have
$$I(a,b)=I(\operatorname{agm}(a,b),\operatorname{agm}(a,b))=\frac{\pi/2}{\operatorname{agm}(a,b)}$$
where the second equality follows from an elementary integral.
This establishes an important link between the AGM and complete elliptic integrals.
-/

@[expose] public section

open MeasureTheory Real

/-- The elliptic integral related to the arithmetic-geometric mean. -/
noncomputable def agmIntegral (a b : â„) : â„ :=
  âˆ« x in Set.Ioi 0, (âˆš((x ^ 2 + a ^ 2) * (x ^ 2 + b ^ 2)))â»Â¹

variable {a b : â„}

lemma integrableOn_agmIntegrand_Ioi_of_pos (ha : 0 < a) (hb : 0 < b) :
    IntegrableOn (fun x â†¦ (âˆš((x ^ 2 + a ^ 2) * (x ^ 2 + b ^ 2)))â»Â¹) (Set.Ioi 0) := by
  apply Integrable.mono' (g := fun x â†¦ (x ^ 2 + min a b ^ 2)â»Â¹)
  Â· have mnz : min a b â‰  0 := by positivity
    conv =>
      enter [1, x]
      rw [â† mul_div_cancelâ‚€ x mnz, mul_pow, â† mul_add_one, mul_inv, add_comm]
    exact ((integrable_inv_one_add_sq.comp_div mnz).const_mul _).restrict
  Â· exact (AEMeasurable.aestronglyMeasurable (by fun_prop)).restrict
  Â· refine Filter.Eventually.of_forall fun x â†¦ ?_
    rw [norm_of_nonneg (by positivity), inv_le_invâ‚€ (by positivity) (by positivity),
      â† sqrt_mul_self (show 0 â‰¤ x ^ 2 + min a b ^ 2 by positivity)]
    gcongr <;> grind

lemma agmIntegral_comm : agmIntegral a b = agmIntegral b a := by
  simp [agmIntegral, mul_comm]

/-- The integral diverges (and thus takes the junk value of 0 in Lean) when `a = 0`. -/
@[simp]
lemma agmIntegral_zero_left : agmIntegral 0 b = 0 := by
  have cong (x) (mx : x âˆˆ Set.Ioi 0) :
      (âˆš((x ^ 2 + 0 ^ 2) * (x ^ 2 + b ^ 2)))â»Â¹ = (x * âˆš(x ^ 2 + b ^ 2))â»Â¹ := by
    rw [sq 0, mul_zero, add_zero, sqrt_mul (sq_nonneg _), sqrt_sq mx.le]
  rw [agmIntegral, setIntegral_congr_fun measurableSet_Ioi cong]
  apply integral_undef (?_ : Â¬IntegrableOn _ _ _)
  by_contra! h
  replace h := h.mono_set (Set.Ioc_subset_Ioi_self (b := 1))
  replace h : IntegrableOn (fun a â†¦ aâ»Â¹ * (âˆš(1 + b ^ 2))â»Â¹) (Set.Ioc 0 1) := by
    apply h.mono' (measurable_inv.aestronglyMeasurable.restrict.mul_const _)
    refine ae_restrict_of_forall_mem measurableSet_Ioc fun x âŸ¨hxâ‚, hxâ‚‚âŸ© â†¦ ?_
    rw [â† mul_inv, norm_inv, norm_eq_abs, abs_of_nonneg (by positivity)]
    gcongr
    bound
  replace h : IntegrableOn _ _ _ := h.mul_const âˆš(1 + b ^ 2)
  conv at h =>
    enter [1, x]
    rw [inv_mul_cancel_rightâ‚€ (by positivity)]
  rw [â† intervalIntegrable_iff_integrableOn_Ioc_of_le zero_le_one] at h
  simp at h

/-- The integral diverges (and thus takes the junk value of 0 in Lean) when `b = 0`. -/
@[simp]
lemma agmIntegral_zero_right : agmIntegral a 0 = 0 := by
  rw [agmIntegral_comm, agmIntegral_zero_left]

/-- The limiting, special case of `agmIntegral`. -/
theorem agmIntegral_self (ha : 0 â‰¤ a) : agmIntegral a a = Ï€ / 2 / a := by
  rcases ha.eq_or_lt with rfl | ha; Â· simp
  unfold agmIntegral
  conv_lhs =>
    enter [2, x]
    rw [â† sq, sqrt_sq (by positivity)]
  have lâ‚ (x) (hx : x âˆˆ Set.Ioi 0) : HasDerivWithinAt (a * Â·) a (Set.Ioi 0) x :=
    (hasDerivAt_const_mul a).hasDerivWithinAt
  have lâ‚‚ : MonotoneOn (a * Â·) (Set.Ioi 0) :=
    (monotone_mul_left_of_nonneg ha.le).monotoneOn _
  rw [â† Set.image_const_mul_Ioi_zero ha,
    integral_image_eq_integral_deriv_smul_of_monotoneOn measurableSet_Ioi lâ‚ lâ‚‚]
  conv_lhs =>
    enter [2, x]
    rw [mul_pow, â† mul_add_one, mul_inv, smul_eq_mul, â† mul_assoc, â† div_eq_mul_inv a, sq,
      div_self_mul_self', add_comm]
  rw [integral_const_mul, integral_Ioi_inv_one_add_sq, arctan_zero]
  ring

/-- **Landen's transformation** for `agmIntegral`. -/
theorem agmIntegral_eq_agmIntegral_gm_am (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) :
    agmIntegral a b = agmIntegral âˆš(a * b) ((a + b) / 2) := by
  -- Handle degenerate cases
  rcases ha.eq_or_lt with rfl | ha; Â· simp
  rcases hb.eq_or_lt with rfl | hb; Â· simp
  -- Define prerequisites for the substitution
  let f (t : â„) := t + âˆš(t ^ 2 + a * b)
  let f' (t : â„) := f t / âˆš(t ^ 2 + a * b)
  let g (x : â„) := (âˆš((x ^ 2 + a ^ 2) * (x ^ 2 + b ^ 2)))â»Â¹
  have pf (t) : 0 < f t := by
    rcases le_or_gt 0 t with ht | ht; Â· positivity
    rw [â† neg_lt_iff_pos_add']
    nth_rw 1 [â† abs_of_nonpos ht.le, â† sqrt_sq_eq_abs, â† add_zero (t ^ 2)]
    gcongr
    positivity
  have df (t) : HasDerivAt f (f' t) t := by
    unfold f'
    rw [â† div_add_one (by positivity), add_comm]
    apply (hasDerivAt_id' _).fun_add
    rw [â† mul_div_mul_left _ _ two_ne_zero]
    refine ((HasDerivAt.add_const _) ?_).sqrt (by positivity)
    convert hasDerivAt_pow 2 t using 1
    ring
  have mf : StrictMono f := strictMono_of_hasDerivAt_pos df fun t â†¦ by positivity [pf t]
  have rf : f '' Set.univ = Set.Ioi 0 := by
    ext x
    rw [Set.image_univ, Set.mem_range, Set.mem_Ioi]
    refine âŸ¨fun âŸ¨t, htâŸ© â†¦ ht.symm â–¸ (pf _), fun hx â†¦ ?_âŸ©
    use (x ^ 2 - a * b) / (2 * x)
    unfold f
    rw [div_pow, div_add' _ (a * b) _ (by positivity),
      show ((x ^ 2 - a * b) ^ 2 + a * b * (2 * x) ^ 2) = (x ^ 2 + a * b) ^ 2 by ring, â† div_pow,
      sqrt_sq (by positivity), â† add_div, sub_add_add_cancel, â† two_mul,
      mul_div_mul_left _ _ two_ne_zero, sq, mul_self_div_self]
  -- Make the substitution, whose LHS is simply `agmIntegral a b`
  change âˆ« x in Set.Ioi 0, g x = _
  rw [â† rf, integral_image_eq_integral_deriv_smul_of_monotoneOn MeasurableSet.univ
    (fun t _ â†¦ hasDerivWithinAt_univ.mpr (df t)) (monotoneOn_univ.mpr mf.monotone) g]
  -- Simplify the post-substitution integral
  have rearrâ‚ (t) : (f t * (t - âˆš(t ^ 2 + a * b))) ^ 2 = (a * b) ^ 2 := by
    rw [â† sq_sub_sq, sq_sqrt (by positivity)]
    ring
  have rearrâ‚‚ (t) : f t ^ 2 + (t - âˆš(t ^ 2 + a * b)) ^ 2 = 4 * t ^ 2 + 2 * a * b := by
    rw [add_sq, sub_sq, sq_sqrt (by positivity)]
    ring
  have rearrâ‚ƒ (t) :
      (f t ^ 2 + a ^ 2) * (f t ^ 2 + b ^ 2) = (2 * f t) ^ 2 * (t ^ 2 + ((a + b) / 2) ^ 2) := by
    rw [show (f t ^ 2 + a ^ 2) * (f t ^ 2 + b ^ 2) =
      f t ^ 2 * (f t ^ 2 + a ^ 2 + b ^ 2) + (a * b) ^ 2 by ring, â† rearrâ‚ t, mul_pow, â† mul_add,
      add_right_comm, add_right_comm (f t ^ 2), rearrâ‚‚]
    ring
  unfold f' g
  conv_lhs =>
    enter [2, t]
    rw [rearrâ‚ƒ, sqrt_mul (sq_nonneg _), sqrt_sq (by positivity [pf t]), smul_eq_mul, mul_inv,
      div_eq_mul_inv, mul_mul_mul_comm, â† mul_inv, â† sqrt_mul (by positivity),
      â† div_eq_mul_inv (f t), â† div_div, div_div_cancel_left' (pf t).ne']
  rw [integral_const_mul, inv_mul_eq_div, div_eq_iff two_ne_zero, Measure.restrict_univ]
  -- The current LHS integral is an even integrand over all â„,
  -- so it is twice the integral over (0, âˆ), i.e. the RHS integral
  rw [mul_comm _ 2, agmIntegral, â† integral_comp_abs]
  congr! 4 with _ t
  rw [sq_abs, sq_sqrt (by positivity)]

lemma continuousOn_agmIntegral :
    ContinuousOn (fun p â†¦ agmIntegral p.1 p.2) (Set.Ioi 0 Ã—Ë¢ Set.Ioi 0) := by
  rintro âŸ¨a, bâŸ© âŸ¨ha : 0 < a, hb : 0 < bâŸ©
  apply continuousWithinAt_of_dominated (bound := fun x â†¦ (x ^ 2 + (min a b / 2) ^ 2)â»Â¹)
  Â· exact eventually_nhdsWithin_of_forall fun _ _ â†¦
      (AEMeasurable.aestronglyMeasurable (by fun_prop)).restrict
  Â· rw [eventually_nhdsWithin_iff, Metric.eventually_nhds_iff]
    refine âŸ¨min a b / 2, by positivity,
      fun (a', b') dp mp â†¦ ae_restrict_of_forall_mem measurableSet_Ioi fun t (ht : 0 < t) â†¦ ?_âŸ©
    simp_rw [Prod.dist_eq, max_lt_iff, dist_eq] at dp
    rw [norm_of_nonneg (by positivity), inv_le_invâ‚€ (by positivity) (by positivity),
      â† sqrt_mul_self (show 0 â‰¤ t ^ 2 + (min a b / 2) ^ 2 by positivity)]
    gcongr <;> grind
  Â· have mnz : min a b / 2 â‰  0 := by positivity
    conv =>
      enter [1, x]
      rw [â† mul_div_cancelâ‚€ x mnz, mul_pow, â† mul_add_one, mul_inv, add_comm]
    exact ((integrable_inv_one_add_sq.comp_div mnz).const_mul _).restrict
  Â· exact Filter.Eventually.of_forall fun _ â†¦ ContinuousWithinAt.invâ‚€ (by fun_prop) (by positivity)

lemma strictAntiOn_agmIntegral :
    StrictAntiOn (fun p â†¦ agmIntegral p.1 p.2) (Set.Ioi 0 Ã—Ë¢ Set.Ioi 0) := by
  rintro âŸ¨aâ‚, bâ‚âŸ© âŸ¨haâ‚ : 0 < aâ‚, hbâ‚ : 0 < bâ‚âŸ© âŸ¨aâ‚‚, bâ‚‚âŸ© âŸ¨haâ‚‚ : 0 < aâ‚‚, hbâ‚‚ : 0 < bâ‚‚âŸ© l
  simp only [Prod.lt_iff, agmIntegral] at l âŠ¢
  have intâ‚ := integrableOn_agmIntegrand_Ioi_of_pos haâ‚ hbâ‚
  have intâ‚‚ := integrableOn_agmIntegrand_Ioi_of_pos haâ‚‚ hbâ‚‚
  rw [â† sub_pos, â† integral_sub intâ‚ intâ‚‚]
  refine (integral_pos_iff_support_of_nonneg (fun x â†¦ ?_) (intâ‚.sub intâ‚‚)).mpr ?_
  Â· simp only [Pi.zero_apply, Pi.sub_apply, sub_nonneg]
    gcongr <;> grind
  Â· rw [Measure.restrict_apply' measurableSet_Ioi, Pi.sub_def]
    suffices Set.Ioi 0 âŠ† Function.support fun x â†¦
        (âˆš((x ^ 2 + aâ‚ ^ 2) * (x ^ 2 + bâ‚ ^ 2)))â»Â¹ - (âˆš((x ^ 2 + aâ‚‚ ^ 2) * (x ^ 2 + bâ‚‚ ^ 2)))â»Â¹ by
      simp [Set.inter_eq_right.mpr this]
    intro x (hx : 0 < x)
    rw [Function.mem_support]
    apply ne_of_gt
    rw [sub_pos]
    gcongr (âˆš(?_))â»Â¹
    obtain âŸ¨oâ‚, oâ‚‚âŸ© | âŸ¨oâ‚, oâ‚‚âŸ© := l
    Â· calc
        _ < (x ^ 2 + aâ‚‚ ^ 2) * (x ^ 2 + bâ‚ ^ 2) := by gcongr
        _ â‰¤ _ := by gcongr
    Â· calc
        _ â‰¤ (x ^ 2 + aâ‚‚ ^ 2) * (x ^ 2 + bâ‚ ^ 2) := by gcongr
        _ < _ := by gcongr

open NNReal

variable {a b : â„â‰¥0}

lemma agmIntegral_eq_agmIntegral_agmSequences {n : â„•} :
    agmIntegral a b = agmIntegral (agmSequences a b n).1 (agmSequences a b n).2 := by
  induction n with
  | zero =>
    rw [agmIntegral_eq_agmIntegral_gm_am (coe_nonneg a) (coe_nonneg b)]
    simp [agmSequences_zero]
  | succ n ih =>
    rw [ih, agmIntegral_eq_agmIntegral_gm_am (coe_nonneg _) (coe_nonneg _),
      agmSequences_succ', coe_sqrt, NNReal.coe_mul]
    rfl

open Filter Topology in
/-- The fundamental relation between `agmIntegral` and `agm`. -/
theorem agmIntegral_eq_pi_div_two_div_agm : agmIntegral a b = Ï€ / 2 / agm a b := by
  by_cases h : a = 0 âˆ¨ b = 0
  Â· obtain rfl | rfl := h <;> simp
  rw [not_or, â† Ne, â† pos_iff_ne_zero, â† Ne, â† pos_iff_ne_zero] at h
  let f (n : â„•) : â„ Ã— â„ := ((agmSequences a b n).1, (agmSequences a b n).2)
  let g (p : â„ Ã— â„) : â„ := agmIntegral p.1 p.2
  suffices Tendsto (g âˆ˜ f) atTop (ğ“ (agmIntegral (agm a b) (agm a b))) by
    rw [â† agmIntegral_self (coe_nonneg _)]
    refine tendsto_nhds_unique ?_ this
    change Tendsto (fun n â†¦ agmIntegral (agmSequences a b n).1 (agmSequences a b n).2) _ _
    simp [â† agmIntegral_eq_agmIntegral_agmSequences]
  have tt : Tendsto f atTop (ğ“ (agm a b, agm a b)) :=
    (Prod.tendsto_iff _ _).mpr
      âŸ¨tendsto_coe.mpr tendsto_agmSequences_fst_agm, tendsto_coe.mpr tendsto_agmSequences_snd_agmâŸ©
  refine (continuousOn_agmIntegral.continuousAt ?_).tendsto.comp tt
  exact prod_mem_nhds (Ioi_mem_nhds (agm_pos h.1 h.2)) (Ioi_mem_nhds (agm_pos h.1 h.2))

theorem agm_eq_pi_div_two_div_agmIntegral : agm a b = Ï€ / 2 / agmIntegral a b := by
  by_cases h : a = 0 âˆ¨ b = 0
  Â· obtain rfl | rfl := h <;> simp
  rw [not_or, â† Ne, â† pos_iff_ne_zero, â† Ne, â† pos_iff_ne_zero] at h
  have e := @agmIntegral_eq_pi_div_two_div_agm a b
  have agmp : (0 : â„) < agm a b := agm_pos h.1 h.2
  have aip : 0 < agmIntegral a b := by
    rw [e]
    positivity
  rwa [eq_div_iff aip.ne', mul_comm, â† eq_div_iff agmp.ne']

theorem strictMonoOn_agm : StrictMonoOn (fun p â†¦ agm p.1 p.2) (Set.Ioi 0 Ã—Ë¢ Set.Ioi 0) := by
  rintro âŸ¨aâ‚, bâ‚âŸ© âŸ¨haâ‚ : 0 < aâ‚, hbâ‚ : 0 < bâ‚âŸ© âŸ¨aâ‚‚, bâ‚‚âŸ© âŸ¨haâ‚‚ : 0 < aâ‚‚, hbâ‚‚ : 0 < bâ‚‚âŸ© l
  simp_rw [â† coe_lt_coe, agm_eq_pi_div_two_div_agmIntegral]
  apply div_lt_div_of_pos_left (by bound) ?_ ?_
  Â· rw [agmIntegral_eq_pi_div_two_div_agm]
    have agmp := agm_pos haâ‚‚ hbâ‚‚
    positivity
  Â· have hâ‚ : (aâ‚.toReal, bâ‚.toReal) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [haâ‚, hbâ‚]
    have hâ‚‚ : (aâ‚‚.toReal, bâ‚‚.toReal) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [haâ‚‚, hbâ‚‚]
    exact strictAntiOn_agmIntegral hâ‚ hâ‚‚ l

/-- The AGM is monotone in both arguments. -/
theorem monotone_agm : Monotone fun (p : â„â‰¥0 Ã— â„â‰¥0) â†¦ agm p.1 p.2 := by
  rintro âŸ¨aâ‚, bâ‚âŸ© âŸ¨aâ‚‚, bâ‚‚âŸ© âŸ¨lâ‚, lâ‚‚âŸ©
  dsimp only at lâ‚ lâ‚‚ âŠ¢
  by_cases hâ‚ : aâ‚ = 0 âˆ¨ bâ‚ = 0
  Â· obtain rfl | rfl := hâ‚ <;> simp
  rw [not_or, â† Ne, â† pos_iff_ne_zero, â† Ne, â† pos_iff_ne_zero] at hâ‚
  have hâ‚‚ : 0 < aâ‚‚ âˆ§ 0 < bâ‚‚ := âŸ¨hâ‚.1.trans_le lâ‚, hâ‚.2.trans_le lâ‚‚âŸ©
  have mâ‚ : (aâ‚, bâ‚) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [hâ‚.1, hâ‚.2]
  have mâ‚‚ : (aâ‚‚, bâ‚‚) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [hâ‚‚.1, hâ‚‚.2]
  apply strictMonoOn_agm.monotoneOn mâ‚ mâ‚‚ âŸ¨lâ‚, lâ‚‚âŸ©

lemma strictMono_agm_fst (hb : 0 < b) : StrictMono (agm Â· b) := fun a a' l â†¦ by
  dsimp only
  rcases eq_zero_or_pos a with rfl | ha
  Â· simp [agm_pos l hb]
  Â· have h : (a, b) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [ha, hb]
    have h' : (a', b) âˆˆ Set.Ioi 0 Ã—Ë¢ Set.Ioi 0 := by simp [ha.trans l, hb]
    apply strictMonoOn_agm h h' (Prod.mk_lt_mk_iff_left.mpr l)

lemma strictMono_agm_snd (ha : 0 < a) : StrictMono (agm a Â·) := by
  conv =>
    enter [1, b]
    rw [agm_comm]
  exact strictMono_agm_fst ha
