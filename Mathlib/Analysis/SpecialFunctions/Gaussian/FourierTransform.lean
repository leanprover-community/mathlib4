/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.SpecialFunctions.Gaussian.GaussianIntegral
import Mathlib.Analysis.Complex.CauchyIntegral
import Mathlib.MeasureTheory.Integral.Pi
import Mathlib.Analysis.Fourier.FourierTransform

/-!
# Fourier transform of the Gaussian

We prove that the Fourier transform of the Gaussian function is another Gaussian:

* `integral_cexp_quadratic`: general formula for `‚à´ (x : ‚Ñù), exp (b * x ^ 2 + c * x + d)`
* `fourierIntegral_gaussian`: for all complex `b` and `t` with `0 < re b`, we have
  `‚à´ x:‚Ñù, exp (I * t * x) * exp (-b * x^2) = (œÄ / b) ^ (1 / 2) * exp (-t ^ 2 / (4 * b))`.
* `fourierIntegral_gaussian_pi`: a variant with `b` and `t` scaled to give a more symmetric
  statement, and formulated in terms of the Fourier transform operator `ùìï`.

We also give versions of these formulas in finite-dimensional inner product spaces, see
`integral_cexp_neg_mul_sq_norm_add` and `fourierIntegral_gaussian_innerProductSpace`.

-/

/-!
## Fourier integral of Gaussian functions
-/

open Real Set MeasureTheory Filter Asymptotics intervalIntegral

open scoped Real Topology FourierTransform RealInnerProductSpace

open Complex hiding exp continuous_exp

noncomputable section

namespace GaussianFourier

variable {b : ‚ÑÇ}

/-- The integral of the Gaussian function over the vertical edges of a rectangle
with vertices at `(¬±T, 0)` and `(¬±T, c)`. -/
def verticalIntegral (b : ‚ÑÇ) (c T : ‚Ñù) : ‚ÑÇ :=
  ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, I * (cexp (-b * (T + y * I) ^ 2) - cexp (-b * (T - y * I) ^ 2))

/-- Explicit formula for the norm of the Gaussian function along the vertical
edges. -/
theorem norm_cexp_neg_mul_sq_add_mul_I (b : ‚ÑÇ) (c T : ‚Ñù) :
    ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ = exp (-(b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2)) := by
  rw [Complex.norm_exp, neg_mul, neg_re, ‚Üê re_add_im b]
  simp only [sq, re_add_im, mul_re, mul_im, add_re, add_im, ofReal_re, ofReal_im, I_re, I_im]
  ring_nf

theorem norm_cexp_neg_mul_sq_add_mul_I' (hb : b.re ‚â† 0) (c T : ‚Ñù) :
    ‚Äñcexp (-b * (T + c * I) ^ 2)‚Äñ =
      exp (-(b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re + b.re))) := by
  have :
    b.re * T ^ 2 - 2 * b.im * c * T - b.re * c ^ 2 =
      b.re * (T - b.im * c / b.re) ^ 2 - c ^ 2 * (b.im ^ 2 / b.re + b.re) := by
    field_simp; ring
  rw [norm_cexp_neg_mul_sq_add_mul_I, this]

theorem verticalIntegral_norm_le (hb : 0 < b.re) (c : ‚Ñù) {T : ‚Ñù} (hT : 0 ‚â§ T) :
    ‚ÄñverticalIntegral b c T‚Äñ ‚â§
      (2 : ‚Ñù) * |c| * exp (-(b.re * T ^ 2 - (2 : ‚Ñù) * |b.im| * |c| * T - b.re * c ^ 2)) := by
  -- first get uniform bound for integrand
  have vert_norm_bound :
    ‚àÄ {T : ‚Ñù},
      0 ‚â§ T ‚Üí
        ‚àÄ {c y : ‚Ñù},
          |y| ‚â§ |c| ‚Üí
            ‚Äñcexp (-b * (T + y * I) ^ 2)‚Äñ ‚â§
              exp (-(b.re * T ^ 2 - (2 : ‚Ñù) * |b.im| * |c| * T - b.re * c ^ 2)) := by
    intro T hT c y hy
    rw [norm_cexp_neg_mul_sq_add_mul_I b]
    gcongr exp (-(_ - ?_ * _ - _ * ?_))
    ¬∑ (conv_lhs => rw [mul_assoc]); (conv_rhs => rw [mul_assoc])
      gcongr _ * ?_
      refine (le_abs_self _).trans ?_
      rw [abs_mul]
      gcongr
    ¬∑ rwa [sq_le_sq]
  -- now main proof
  apply (intervalIntegral.norm_integral_le_of_norm_le_const _).trans
  ¬∑ rw [sub_zero]
    conv_lhs => simp only [mul_comm _ |c|]
    conv_rhs =>
      conv =>
        congr
        rw [mul_comm]
      rw [mul_assoc]
  ¬∑ intro y hy
    have absy : |y| ‚â§ |c| := by
      rcases le_or_gt 0 c with (h | h)
      ¬∑ rw [uIoc_of_le h] at hy
        rw [abs_of_nonneg h, abs_of_pos hy.1]
        exact hy.2
      ¬∑ rw [uIoc_of_ge h.le] at hy
        rw [abs_of_neg h, abs_of_nonpos hy.2, neg_le_neg_iff]
        exact hy.1.le
    rw [norm_mul, norm_I, one_mul, two_mul]
    refine (norm_sub_le _ _).trans (add_le_add (vert_norm_bound hT absy) ?_)
    rw [‚Üê abs_neg y] at absy
    simpa only [neg_mul, ofReal_neg] using vert_norm_bound hT absy

theorem tendsto_verticalIntegral (hb : 0 < b.re) (c : ‚Ñù) :
    Tendsto (verticalIntegral b c) atTop (ùìù 0) := by
  -- complete proof using squeeze theorem:
  rw [tendsto_zero_iff_norm_tendsto_zero]
  refine
    tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds ?_
      (Eventually.of_forall fun _ => norm_nonneg _)
      ((eventually_ge_atTop (0 : ‚Ñù)).mp
        (Eventually.of_forall fun T hT => verticalIntegral_norm_le hb c hT))
  rw [(by ring : 0 = 2 * |c| * 0)]
  refine (tendsto_exp_atBot.comp (tendsto_neg_atTop_atBot.comp ?_)).const_mul _
  apply tendsto_atTop_add_const_right
  simp_rw [sq, ‚Üê mul_assoc, ‚Üê sub_mul]
  refine Tendsto.atTop_mul_atTop‚ÇÄ (tendsto_atTop_add_const_right _ _ ?_) tendsto_id
  exact (tendsto_const_mul_atTop_of_pos hb).mpr tendsto_id

theorem integrable_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
    Integrable fun x : ‚Ñù => cexp (-b * (x + c * I) ^ 2) := by
  refine
    ‚ü®(Complex.continuous_exp.comp
          (continuous_const.mul
            ((continuous_ofReal.add continuous_const).pow 2))).aestronglyMeasurable,
      ?_‚ü©
  rw [‚Üê hasFiniteIntegral_norm_iff]
  simp_rw [norm_cexp_neg_mul_sq_add_mul_I' hb.ne', neg_sub _ (c ^ 2 * _),
    sub_eq_add_neg _ (b.re * _), Real.exp_add]
  suffices Integrable fun x : ‚Ñù => exp (-(b.re * x ^ 2)) by
    exact (Integrable.comp_sub_right this (b.im * c / b.re)).hasFiniteIntegral.const_mul _
  simp_rw [‚Üê neg_mul]
  apply integrable_exp_neg_mul_sq hb

theorem integral_cexp_neg_mul_sq_add_real_mul_I (hb : 0 < b.re) (c : ‚Ñù) :
    ‚à´ x : ‚Ñù, cexp (-b * (x + c * I) ^ 2) = (œÄ / b) ^ (1 / 2 : ‚ÑÇ) := by
  refine
    tendsto_nhds_unique
      (intervalIntegral_tendsto_integral (integrable_cexp_neg_mul_sq_add_real_mul_I hb c)
        tendsto_neg_atTop_atBot tendsto_id)
      ?_
  set I‚ÇÅ := fun T => ‚à´ x : ‚Ñù in -T..T, cexp (-b * (x + c * I) ^ 2) with HI‚ÇÅ
  let I‚ÇÇ := fun T : ‚Ñù => ‚à´ x : ‚Ñù in -T..T, cexp (-b * (x : ‚ÑÇ) ^ 2)
  let I‚ÇÑ := fun T : ‚Ñù => ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, cexp (-b * (T + y * I) ^ 2)
  let I‚ÇÖ := fun T : ‚Ñù => ‚à´ y : ‚Ñù in (0 : ‚Ñù)..c, cexp (-b * (-T + y * I) ^ 2)
  have C : ‚àÄ T : ‚Ñù, I‚ÇÇ T - I‚ÇÅ T + I * I‚ÇÑ T - I * I‚ÇÖ T = 0 := by
    intro T
    have :=
      integral_boundary_rect_eq_zero_of_differentiableOn (fun z => cexp (-b * z ^ 2)) (-T)
        (T + c * I)
        (by
          refine Differentiable.differentiableOn (Differentiable.const_mul ?_ _).cexp
          exact differentiable_pow 2)
    simpa only [neg_im, ofReal_im, neg_zero, ofReal_zero, zero_mul, add_zero, neg_re,
      ofReal_re, add_re, mul_re, I_re, mul_zero, I_im, tsub_zero, add_im, mul_im,
      mul_one, zero_add, Algebra.id.smul_eq_mul, ofReal_neg] using this
  simp_rw [id, ‚Üê HI‚ÇÅ]
  have : I‚ÇÅ = fun T : ‚Ñù => I‚ÇÇ T + verticalIntegral b c T := by
    ext1 T
    specialize C T
    rw [sub_eq_zero] at C
    unfold verticalIntegral
    rw [intervalIntegral.integral_const_mul, intervalIntegral.integral_sub]
    ¬∑ simp_rw [(fun a b => by rw [sq]; ring_nf : ‚àÄ a b : ‚ÑÇ, (a - b * I) ^ 2 = (-a + b * I) ^ 2)]
      change I‚ÇÅ T = I‚ÇÇ T + I * (I‚ÇÑ T - I‚ÇÖ T)
      rw [mul_sub, ‚Üê C]
      abel
    all_goals apply Continuous.intervalIntegrable; continuity
  rw [this, ‚Üê add_zero ((œÄ / b : ‚ÑÇ) ^ (1 / 2 : ‚ÑÇ)), ‚Üê integral_gaussian_complex hb]
  refine Tendsto.add ?_ (tendsto_verticalIntegral hb c)
  exact
    intervalIntegral_tendsto_integral (integrable_cexp_neg_mul_sq hb) tendsto_neg_atTop_atBot
      tendsto_id

theorem _root_.integral_cexp_quadratic (hb : b.re < 0) (c d : ‚ÑÇ) :
    ‚à´ x : ‚Ñù,
      cexp (b * x ^ 2 + c * x + d) = (œÄ / -b) ^ (1 / 2 : ‚ÑÇ) * cexp (d - c ^ 2 / (4 * b)) := by
  have hb' : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  have h (x : ‚Ñù) : cexp (b * x ^ 2 + c * x + d) =
      cexp (- -b * (x + c / (2 * b)) ^ 2) * cexp (d - c ^ 2 / (4 * b)) := by
    simp_rw [‚Üê Complex.exp_add]
    congr 1
    field_simp
    ring_nf
  simp_rw [h, MeasureTheory.integral_mul_const]
  rw [‚Üê re_add_im (c / (2 * b))]
  simp_rw [‚Üê add_assoc, ‚Üê ofReal_add]
  rw [integral_add_right_eq_self fun a : ‚Ñù ‚Ü¶ cexp (- -b * (‚Üëa + ‚Üë(c / (2 * b)).im * I) ^ 2),
    integral_cexp_neg_mul_sq_add_real_mul_I ((neg_re b).symm ‚ñ∏ (neg_pos.mpr hb))]

lemma _root_.integrable_cexp_quadratic' (hb : b.re < 0) (c d : ‚ÑÇ) :
    Integrable (fun (x : ‚Ñù) ‚Ü¶ cexp (b * x ^ 2 + c * x + d)) := by
  have hb' : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  by_contra H
  simpa [hb', pi_ne_zero, Complex.exp_ne_zero, integral_undef H]
    using integral_cexp_quadratic hb c d

lemma _root_.integrable_cexp_quadratic (hb : 0 < b.re) (c d : ‚ÑÇ) :
    Integrable (fun (x : ‚Ñù) ‚Ü¶ cexp (-b * x ^ 2 + c * x + d)) := by
  have : (-b).re < 0 := by simpa using hb
  exact integrable_cexp_quadratic' this c d

theorem _root_.fourierIntegral_gaussian (hb : 0 < b.re) (t : ‚ÑÇ) :
    ‚à´ x : ‚Ñù, cexp (I * t * x) * cexp (-b * x ^ 2) =
    (œÄ / b) ^ (1 / 2 : ‚ÑÇ) * cexp (-t ^ 2 / (4 * b)) := by
  conv => enter [1, 2, x]; rw [‚Üê Complex.exp_add, add_comm, ‚Üê add_zero (-b * x ^ 2 + I * t * x)]
  rw [integral_cexp_quadratic (show (-b).re < 0 by rwa [neg_re, neg_lt_zero]), neg_neg, zero_sub,
    mul_neg, div_neg, neg_neg, mul_pow, I_sq, neg_one_mul, mul_comm]

theorem _root_.fourierIntegral_gaussian_pi' (hb : 0 < b.re) (c : ‚ÑÇ) :
    (ùìï fun x : ‚Ñù => cexp (-œÄ * b * x ^ 2 + 2 * œÄ * c * x)) = fun t : ‚Ñù =>
    1 / b ^ (1 / 2 : ‚ÑÇ) * cexp (-œÄ / b * (t + I * c) ^ 2) := by
  haveI : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
  have h : (-‚ÜëœÄ * b).re < 0 := by
    simpa only [neg_mul, neg_re, re_ofReal_mul, neg_lt_zero] using mul_pos pi_pos hb
  ext1 t
  simp_rw [fourierIntegral_real_eq_integral_exp_smul, smul_eq_mul, ‚Üê Complex.exp_add, ‚Üê add_assoc]
  have (x : ‚Ñù) : ‚Üë(-2 * œÄ * x * t) * I + -œÄ * b * x ^ 2 + 2 * œÄ * c * x =
    -œÄ * b * x ^ 2 + (-2 * œÄ * I * t + 2 * œÄ * c) * x + 0 := by push_cast; ring
  simp_rw [this, integral_cexp_quadratic h, neg_mul, neg_neg]
  congr 2
  ¬∑ rw [‚Üê div_div, div_self <| ofReal_ne_zero.mpr pi_ne_zero, one_div, inv_cpow, ‚Üê one_div]
    rw [Ne, arg_eq_pi_iff, not_and_or, not_lt]
    exact Or.inl hb.le
  ¬∑ field_simp
    ring_nf
    simp only [I_sq]
    ring

theorem _root_.fourierIntegral_gaussian_pi (hb : 0 < b.re) :
    (ùìï fun (x : ‚Ñù) ‚Ü¶ cexp (-œÄ * b * x ^ 2)) =
    fun t : ‚Ñù ‚Ü¶ 1 / b ^ (1 / 2 : ‚ÑÇ) * cexp (-œÄ / b * t ^ 2) := by
  simpa only [mul_zero, zero_mul, add_zero] using fourierIntegral_gaussian_pi' hb 0

section InnerProductSpace

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ‚Ñù V] [FiniteDimensional ‚Ñù V]
  [MeasurableSpace V] [BorelSpace V]

theorem integrable_cexp_neg_sum_mul_add {Œπ : Type*} [Fintype Œπ] {b : Œπ ‚Üí ‚ÑÇ}
    (hb : ‚àÄ i, 0 < (b i).re) (c : Œπ ‚Üí ‚ÑÇ) :
    Integrable (fun (v : Œπ ‚Üí ‚Ñù) ‚Ü¶ cexp (-‚àë i, b i * (v i : ‚ÑÇ) ^ 2 + ‚àë i, c i * v i)) := by
  simp_rw [‚Üê Finset.sum_neg_distrib, ‚Üê Finset.sum_add_distrib, Complex.exp_sum, ‚Üê neg_mul]
  apply Integrable.fintype_prod (f := fun i (v : ‚Ñù) ‚Ü¶ cexp (-b i * v ^ 2 + c i * v)) (fun i ‚Ü¶ ?_)
  convert integrable_cexp_quadratic (hb i) (c i) 0 using 3 with x
  simp only [add_zero]

theorem integrable_cexp_neg_mul_sum_add {Œπ : Type*} [Fintype Œπ] (hb : 0 < b.re) (c : Œπ ‚Üí ‚ÑÇ) :
    Integrable (fun (v : Œπ ‚Üí ‚Ñù) ‚Ü¶ cexp (-b * ‚àë i, (v i : ‚ÑÇ) ^ 2 + ‚àë i, c i * v i)) := by
  simp_rw [neg_mul, Finset.mul_sum]
  exact integrable_cexp_neg_sum_mul_add (fun _ ‚Ü¶ hb) c

theorem integrable_cexp_neg_mul_sq_norm_add_of_euclideanSpace
    {Œπ : Type*} [Fintype Œπ] (hb : 0 < b.re) (c : ‚ÑÇ) (w : EuclideanSpace ‚Ñù Œπ) :
    Integrable (fun (v : EuclideanSpace ‚Ñù Œπ) ‚Ü¶ cexp (-b * ‚Äñv‚Äñ ^ 2 + c * ‚ü™w, v‚ü´)) := by
  have := EuclideanSpace.volume_preserving_measurableEquiv Œπ
  rw [‚Üê MeasurePreserving.integrable_comp_emb this.symm (MeasurableEquiv.measurableEmbedding _)]
  simp only [neg_mul, Function.comp_def]
  convert integrable_cexp_neg_mul_sum_add hb (fun i ‚Ü¶ c * w i) using 3 with v
  simp only [EuclideanSpace.measurableEquiv, MeasurableEquiv.symm_mk, MeasurableEquiv.coe_mk,
    EuclideanSpace.norm_eq, Real.norm_eq_abs, sq_abs, PiLp.inner_apply,
    RCLike.inner_apply, conj_trivial, ofReal_sum, ofReal_mul, Finset.mul_sum, neg_mul,
    Finset.sum_neg_distrib, mul_assoc]
  norm_cast
  rw [sq_sqrt]
  ¬∑ simp [Finset.mul_sum, mul_comm]
  ¬∑ exact Finset.sum_nonneg (fun i _hi ‚Ü¶ by positivity)

/-- In a real inner product space, the complex exponential of minus the square of the norm plus
a scalar product is integrable. Useful when discussing the Fourier transform of a Gaussian. -/
theorem integrable_cexp_neg_mul_sq_norm_add (hb : 0 < b.re) (c : ‚ÑÇ) (w : V) :
    Integrable (fun (v : V) ‚Ü¶ cexp (-b * ‚Äñv‚Äñ ^ 2 + c * ‚ü™w, v‚ü´)) := by
  let e := (stdOrthonormalBasis ‚Ñù V).repr.symm
  rw [‚Üê e.measurePreserving.integrable_comp_emb e.toHomeomorph.measurableEmbedding]
  convert integrable_cexp_neg_mul_sq_norm_add_of_euclideanSpace
    hb c (e.symm w) with v
  simp only [neg_mul, Function.comp_apply, LinearIsometryEquiv.norm_map,
    LinearIsometryEquiv.symm_symm,
    LinearIsometryEquiv.inner_map_eq_flip]

theorem integral_cexp_neg_sum_mul_add {Œπ : Type*} [Fintype Œπ] {b : Œπ ‚Üí ‚ÑÇ}
    (hb : ‚àÄ i, 0 < (b i).re) (c : Œπ ‚Üí ‚ÑÇ) :
    ‚à´ v : Œπ ‚Üí ‚Ñù, cexp (-‚àë i, b i * (v i : ‚ÑÇ) ^ 2 + ‚àë i, c i * v i)
      = ‚àè i, (œÄ / b i) ^ (1 / 2 : ‚ÑÇ) * cexp (c i ^ 2 / (4 * b i)) := by
  simp_rw [‚Üê Finset.sum_neg_distrib, ‚Üê Finset.sum_add_distrib, Complex.exp_sum, ‚Üê neg_mul]
  rw [integral_fintype_prod_volume_eq_prod (f := fun i (v : ‚Ñù) ‚Ü¶ cexp (-b i * v ^ 2 + c i * v))]
  congr with i
  have : (-b i).re < 0 := by simpa using hb i
  convert integral_cexp_quadratic this (c i) 0 using 1 <;> simp [div_neg]

theorem integral_cexp_neg_mul_sum_add {Œπ : Type*} [Fintype Œπ] (hb : 0 < b.re) (c : Œπ ‚Üí ‚ÑÇ) :
    ‚à´ v : Œπ ‚Üí ‚Ñù, cexp (-b * ‚àë i, (v i : ‚ÑÇ) ^ 2 + ‚àë i, c i * v i)
      = (œÄ / b) ^ (Fintype.card Œπ / 2 : ‚ÑÇ) * cexp ((‚àë i, c i ^ 2) / (4 * b)) := by
  simp_rw [neg_mul, Finset.mul_sum, integral_cexp_neg_sum_mul_add (fun _ ‚Ü¶ hb) c, one_div,
    Finset.prod_mul_distrib, Finset.prod_const, ‚Üê cpow_nat_mul, ‚Üê Complex.exp_sum, Fintype.card,
    Finset.sum_div, div_eq_mul_inv]

theorem integral_cexp_neg_mul_sq_norm_add_of_euclideanSpace
    {Œπ : Type*} [Fintype Œπ] (hb : 0 < b.re) (c : ‚ÑÇ) (w : EuclideanSpace ‚Ñù Œπ) :
    ‚à´ v : EuclideanSpace ‚Ñù Œπ, cexp (-b * ‚Äñv‚Äñ ^ 2 + c * ‚ü™w, v‚ü´) =
      (œÄ / b) ^ (Fintype.card Œπ / 2 : ‚ÑÇ) * cexp (c ^ 2 * ‚Äñw‚Äñ ^ 2 / (4 * b)) := by
  have := (EuclideanSpace.volume_preserving_measurableEquiv Œπ).symm
  rw [‚Üê this.integral_comp (MeasurableEquiv.measurableEmbedding _)]
  simp only [neg_mul]
  convert integral_cexp_neg_mul_sum_add hb (fun i ‚Ü¶ c * w i) using 5 with _x y
  ¬∑ simp only [EuclideanSpace.coe_measurableEquiv_symm, EuclideanSpace.norm_eq, PiLp.toLp_apply,
      Real.norm_eq_abs, sq_abs, neg_mul, neg_inj, mul_eq_mul_left_iff]
    norm_cast
    left
    rw [sq_sqrt]
    exact Finset.sum_nonneg (fun i _hi ‚Ü¶ by positivity)
  ¬∑ simp [PiLp.inner_apply, EuclideanSpace.measurableEquiv, Finset.mul_sum, mul_assoc]
    simp_rw [mul_comm]
  ¬∑ simp only [EuclideanSpace.norm_eq, Real.norm_eq_abs, sq_abs, mul_pow, ‚Üê Finset.mul_sum]
    congr
    norm_cast
    rw [sq_sqrt]
    exact Finset.sum_nonneg (fun i _hi ‚Ü¶ by positivity)

theorem integral_cexp_neg_mul_sq_norm_add
    (hb : 0 < b.re) (c : ‚ÑÇ) (w : V) :
    ‚à´ v : V, cexp (-b * ‚Äñv‚Äñ ^ 2 + c * ‚ü™w, v‚ü´) =
      (œÄ / b) ^ (Module.finrank ‚Ñù V / 2 : ‚ÑÇ) * cexp (c ^ 2 * ‚Äñw‚Äñ ^ 2 / (4 * b)) := by
  let e := (stdOrthonormalBasis ‚Ñù V).repr.symm
  rw [‚Üê e.measurePreserving.integral_comp e.toHomeomorph.measurableEmbedding]
  convert integral_cexp_neg_mul_sq_norm_add_of_euclideanSpace
    hb c (e.symm w) <;> simp [LinearIsometryEquiv.inner_map_eq_flip]

theorem integral_cexp_neg_mul_sq_norm (hb : 0 < b.re) :
    ‚à´ v : V, cexp (-b * ‚Äñv‚Äñ ^ 2) = (œÄ / b) ^ (Module.finrank ‚Ñù V / 2 : ‚ÑÇ) := by
  simpa using integral_cexp_neg_mul_sq_norm_add hb 0 (0 : V)

theorem integral_rexp_neg_mul_sq_norm {b : ‚Ñù} (hb : 0 < b) :
    ‚à´ v : V, rexp (-b * ‚Äñv‚Äñ ^ 2) = (œÄ / b) ^ (Module.finrank ‚Ñù V / 2 : ‚Ñù) := by
  rw [‚Üê ofReal_inj]
  convert integral_cexp_neg_mul_sq_norm (show 0 < (b : ‚ÑÇ).re from hb) (V := V)
  ¬∑ change ofRealLI (‚à´ (v : V), rexp (-b * ‚Äñv‚Äñ ^ 2)) = ‚à´ (v : V), cexp (-‚Üëb * ‚Üë‚Äñv‚Äñ ^ 2)
    rw [‚Üê ofRealLI.integral_comp_comm]
    simp [ofRealLI]
  ¬∑ rw [‚Üê ofReal_div, ofReal_cpow (by positivity)]
    simp

theorem _root_.fourierIntegral_gaussian_innerProductSpace' (hb : 0 < b.re) (x w : V) :
    ùìï (fun v ‚Ü¶ cexp (-b * ‚Äñv‚Äñ ^ 2 + 2 * œÄ * Complex.I * ‚ü™x, v‚ü´)) w =
      (œÄ / b) ^ (Module.finrank ‚Ñù V / 2 : ‚ÑÇ) * cexp (-œÄ ^ 2 * ‚Äñx - w‚Äñ ^ 2 / b) := by
  simp only [neg_mul, fourierIntegral_eq', ofReal_neg, ofReal_mul, ofReal_ofNat,
    smul_eq_mul, ‚Üê Complex.exp_add, real_inner_comm w]
  convert integral_cexp_neg_mul_sq_norm_add hb (2 * œÄ * Complex.I) (x - w) using 3 with v
  ¬∑ congr 1
    simp [inner_sub_left]
    ring
  ¬∑ have : b ‚â† 0 := by contrapose! hb; rw [hb, zero_re]
    simp [mul_pow]
    ring

theorem _root_.fourierIntegral_gaussian_innerProductSpace (hb : 0 < b.re) (w : V) :
    ùìï (fun v ‚Ü¶ cexp (-b * ‚Äñv‚Äñ ^ 2)) w =
      (œÄ / b) ^ (Module.finrank ‚Ñù V / 2 : ‚ÑÇ) * cexp (-œÄ ^ 2 * ‚Äñw‚Äñ ^ 2 / b) := by
  simpa using fourierIntegral_gaussian_innerProductSpace' hb 0 w

end InnerProductSpace

end GaussianFourier
