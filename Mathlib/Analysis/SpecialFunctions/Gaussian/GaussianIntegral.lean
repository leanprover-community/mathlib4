/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.SpecialFunctions.Gamma.Basic
import Mathlib.Analysis.SpecialFunctions.PolarCoord
import Mathlib.Analysis.Complex.Convex
import Mathlib.Data.Nat.Factorial.DoubleFactorial

/-!
# Gaussian integral

We prove various versions of the formula for the Gaussian integral:
* `integral_gaussian`: for real `b` we have `âˆ« x:â„, exp (-b * x^2) = âˆš(Ï€ / b)`.
* `integral_gaussian_complex`: for complex `b` with `0 < re b` we have
  `âˆ« x:â„, exp (-b * x^2) = (Ï€ / b) ^ (1 / 2)`.
* `integral_gaussian_Ioi` and `integral_gaussian_complex_Ioi`: variants for integrals over `Ioi 0`.
* `Complex.Gamma_one_half_eq`: the formula `Î“ (1 / 2) = âˆšÏ€`.
-/

noncomputable section

open Real Set MeasureTheory Filter Asymptotics

open scoped Real Topology

open Complex hiding exp abs_of_nonneg

theorem exp_neg_mul_rpow_isLittleO_exp_neg {p b : â„} (hb : 0 < b) (hp : 1 < p) :
    (fun x : â„ â†¦ exp (- b * x ^ p)) =o[atTop] fun x : â„ â†¦ exp (-x) := by
  rw [isLittleO_exp_comp_exp_comp]
  suffices Tendsto (fun x â†¦ x * (b * x ^ (p - 1) + -1)) atTop atTop by
    refine Tendsto.congr' ?_ this
    refine eventuallyEq_of_mem (Ioi_mem_atTop (0 : â„)) (fun x hx â†¦ ?_)
    rw [mem_Ioi] at hx
    rw [rpow_sub_one hx.ne']
    field_simp [hx.ne']
    ring
  apply tendsto_id.atTop_mul_atTopâ‚€
  refine tendsto_atTop_add_const_right atTop (-1 : â„) ?_
  exact Tendsto.const_mul_atTop hb (tendsto_rpow_atTop (by linarith))

theorem exp_neg_mul_sq_isLittleO_exp_neg {b : â„} (hb : 0 < b) :
    (fun x : â„ â†¦ exp (-b * x ^ 2)) =o[atTop] fun x : â„ â†¦ exp (-x) := by
  simp_rw [â† rpow_two]
  exact exp_neg_mul_rpow_isLittleO_exp_neg hb one_lt_two

theorem rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg (s : â„) {b p : â„} (hp : 1 < p) (hb : 0 < b) :
    (fun x : â„ â†¦ x ^ s * exp (- b * x ^ p)) =o[atTop] fun x : â„ â†¦ exp (-(1 / 2) * x) := by
  apply ((isBigO_refl (fun x : â„ â†¦ x ^ s) atTop).mul_isLittleO
      (exp_neg_mul_rpow_isLittleO_exp_neg hb hp)).trans
  simpa only [mul_comm] using Real.Gamma_integrand_isLittleO s

theorem rpow_mul_exp_neg_mul_sq_isLittleO_exp_neg {b : â„} (hb : 0 < b) (s : â„) :
    (fun x : â„ â†¦ x ^ s * exp (-b * x ^ 2)) =o[atTop] fun x : â„ â†¦ exp (-(1 / 2) * x) := by
  simp_rw [â† rpow_two]
  exact rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg s one_lt_two hb

theorem integrableOn_rpow_mul_exp_neg_rpow {p s : â„} (hs : -1 < s) (hp : 1 â‰¤ p) :
    IntegrableOn (fun x : â„ â†¦ x ^ s * exp (- x ^ p)) (Ioi 0) := by
  obtain hp | hp := le_iff_lt_or_eq.mp hp
  Â· have h_exp : âˆ€ x, ContinuousAt (fun x â†¦ exp (- x)) x := fun x â†¦ continuousAt_neg.rexp
    rw [â† Ioc_union_Ioi_eq_Ioi zero_le_one, integrableOn_union]
    constructor
    Â· rw [â† integrableOn_Icc_iff_integrableOn_Ioc]
      refine IntegrableOn.mul_continuousOn ?_ ?_ isCompact_Icc
      Â· refine (intervalIntegrable_iff_integrableOn_Icc_of_le zero_le_one).mp ?_
        exact intervalIntegral.intervalIntegrable_rpow' hs
      Â· intro x _
        change ContinuousWithinAt ((fun x â†¦ exp (- x)) âˆ˜ (fun x â†¦ x ^ p)) (Icc 0 1) x
        refine ContinuousAt.comp_continuousWithinAt (h_exp _) ?_
        exact continuousWithinAt_id.rpow_const (Or.inr (le_of_lt (lt_trans zero_lt_one hp)))
    Â· have h_rpow : âˆ€ (x r : â„), x âˆˆ Ici 1 â†’ ContinuousWithinAt (fun x â†¦ x ^ r) (Ici 1) x := by
        intro _ _ hx
        refine continuousWithinAt_id.rpow_const (Or.inl ?_)
        exact ne_of_gt (lt_of_lt_of_le zero_lt_one hx)
      refine integrable_of_isBigO_exp_neg (by simp : (0 : â„) < 1 / 2)
        (ContinuousOn.mul (fun x hx â†¦ h_rpow x s hx) (fun x hx â†¦ ?_)) (IsLittleO.isBigO ?_)
      Â· change ContinuousWithinAt ((fun x â†¦ exp (- x)) âˆ˜ (fun x â†¦ x ^ p)) (Ici 1) x
        exact ContinuousAt.comp_continuousWithinAt (h_exp _) (h_rpow x p hx)
      Â· convert rpow_mul_exp_neg_mul_rpow_isLittleO_exp_neg s hp (by simp : (0 : â„) < 1) using 3
        rw [neg_mul, one_mul]
  Â· simp_rw [â† hp, Real.rpow_one]
    convert Real.GammaIntegral_convergent (by linarith : 0 < s + 1) using 2
    rw [add_sub_cancel_right, mul_comm]

theorem integrableOn_rpow_mul_exp_neg_mul_rpow {p s b : â„} (hs : -1 < s) (hp : 1 â‰¤ p) (hb : 0 < b) :
    IntegrableOn (fun x : â„ â†¦ x ^ s * exp (- b * x ^ p)) (Ioi 0) := by
  have hib : 0 < b ^ (-pâ»Â¹) := rpow_pos_of_pos hb _
  suffices IntegrableOn (fun x â†¦ (b ^ (-pâ»Â¹)) ^ s * (x ^ s * exp (-x ^ p))) (Ioi 0) by
    rw [show 0 = b ^ (-pâ»Â¹) * 0 by rw [mul_zero], â† integrableOn_Ioi_comp_mul_left_iff _ _ hib]
    refine this.congr_fun (fun _ hx â†¦ ?_) measurableSet_Ioi
    rw [â† mul_assoc, mul_rpow, mul_rpow, â† rpow_mul (z := p), neg_mul, neg_mul, inv_mul_cancelâ‚€,
      rpow_neg_one, mul_inv_cancel_leftâ‚€]
    all_goals linarith [mem_Ioi.mp hx]
  refine Integrable.const_mul ?_ _
  rw [â† IntegrableOn]
  exact integrableOn_rpow_mul_exp_neg_rpow hs hp

theorem integrableOn_rpow_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) {s : â„} (hs : -1 < s) :
    IntegrableOn (fun x : â„ â†¦ x ^ s * exp (-b * x ^ 2)) (Ioi 0) := by
  simp_rw [â† rpow_two]
  exact integrableOn_rpow_mul_exp_neg_mul_rpow hs one_le_two hb

theorem integrable_rpow_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) {s : â„} (hs : -1 < s) :
    Integrable fun x : â„ â†¦ x ^ s * exp (-b * x ^ 2) := by
  rw [â† integrableOn_univ, â† @Iio_union_Ici _ _ (0 : â„), integrableOn_union,
    integrableOn_Ici_iff_integrableOn_Ioi]
  refine âŸ¨?_, integrableOn_rpow_mul_exp_neg_mul_sq hb hsâŸ©
  rw [â† (Measure.measurePreserving_neg (volume : Measure â„)).integrableOn_comp_preimage
      (Homeomorph.neg â„).measurableEmbedding]
  simp only [Function.comp_def, neg_sq, neg_preimage, neg_Iio, neg_zero]
  apply Integrable.mono' (integrableOn_rpow_mul_exp_neg_mul_sq hb hs)
  Â· apply Measurable.aestronglyMeasurable
    exact (measurable_id'.neg.pow measurable_const).mul
      ((measurable_id'.pow measurable_const).const_mul (-b)).exp
  Â· have : MeasurableSet (Ioi (0 : â„)) := measurableSet_Ioi
    filter_upwards [ae_restrict_mem this] with x hx
    have h'x : 0 â‰¤ x := le_of_lt hx
    rw [Real.norm_eq_abs, abs_mul, abs_of_nonneg (exp_pos _).le]
    apply mul_le_mul_of_nonneg_right _ (exp_pos _).le
    simpa [abs_of_nonneg h'x] using abs_rpow_le_abs_rpow (-x) s

theorem integrable_exp_neg_mul_sq {b : â„} (hb : 0 < b) :
    Integrable fun x : â„ â†¦ exp (-b * x ^ 2) := by
  simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by simp : (-1 : â„) < 0)

theorem integrableOn_Ioi_exp_neg_mul_sq_iff {b : â„} :
    IntegrableOn (fun x : â„ â†¦ exp (-b * x ^ 2)) (Ioi 0) â†” 0 < b := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ (integrable_exp_neg_mul_sq h).integrableOnâŸ©
  by_contra! hb
  have : âˆ«â» _ : â„ in Ioi 0, 1 â‰¤ âˆ«â» x : â„ in Ioi 0, â€–exp (-b * x ^ 2)â€–â‚Š := by
    apply lintegral_mono (fun x â†¦ _)
    simp only [neg_mul, ENNReal.one_le_coe_iff, â† toNNReal_one, toNNReal_le_iff_le_coe,
      Real.norm_of_nonneg (exp_pos _).le, coe_nnnorm, one_le_exp_iff, Right.nonneg_neg_iff]
    exact fun x â†¦ mul_nonpos_of_nonpos_of_nonneg hb (sq_nonneg x)
  simpa using this.trans_lt h.2

theorem integrable_exp_neg_mul_sq_iff {b : â„} :
    (Integrable fun x : â„ â†¦ exp (-b * x ^ 2)) â†” 0 < b :=
  âŸ¨fun h â†¦ integrableOn_Ioi_exp_neg_mul_sq_iff.mp h.integrableOn, integrable_exp_neg_mul_sqâŸ©

theorem integrable_mul_exp_neg_mul_sq {b : â„} (hb : 0 < b) :
    Integrable fun x : â„ â†¦ x * exp (-b * x ^ 2) := by
  simpa using integrable_rpow_mul_exp_neg_mul_sq hb (by simp : (-1 : â„) < 1)

theorem norm_cexp_neg_mul_sq (b : â„‚) (x : â„) :
    â€–Complex.exp (-b * (x : â„‚) ^ 2)â€– = exp (-b.re * x ^ 2) := by
  rw [norm_exp, â† ofReal_pow, mul_comm (-b) _, re_ofReal_mul, neg_re, mul_comm]

theorem integrable_cexp_neg_mul_sq {b : â„‚} (hb : 0 < b.re) :
    Integrable fun x : â„ â†¦ cexp (-b * (x : â„‚) ^ 2) := by
  refine âŸ¨(Complex.continuous_exp.comp
    (continuous_const.mul (continuous_ofReal.pow 2))).aestronglyMeasurable, ?_âŸ©
  rw [â† hasFiniteIntegral_norm_iff]
  simp_rw [norm_cexp_neg_mul_sq]
  exact (integrable_exp_neg_mul_sq hb).2

theorem integrable_mul_cexp_neg_mul_sq {b : â„‚} (hb : 0 < b.re) :
    Integrable fun x : â„ â†¦ â†‘x * cexp (-b * (x : â„‚) ^ 2) := by
  refine âŸ¨(continuous_ofReal.mul (Complex.continuous_exp.comp ?_)).aestronglyMeasurable, ?_âŸ©
  Â· exact continuous_const.mul (continuous_ofReal.pow 2)
  have := (integrable_mul_exp_neg_mul_sq hb).hasFiniteIntegral
  rw [â† hasFiniteIntegral_norm_iff] at this âŠ¢
  convert this
  rw [norm_mul, norm_mul, norm_cexp_neg_mul_sq b, norm_real, norm_of_nonneg (exp_pos _).le]

theorem integral_mul_cexp_neg_mul_sq {b : â„‚} (hb : 0 < b.re) :
    âˆ« r : â„ in Ioi 0, (r : â„‚) * cexp (-b * (r : â„‚) ^ 2) = (2 * b)â»Â¹ := by
  have hb' : b â‰  0 := by contrapose! hb; rw [hb, zero_re]
  have A : âˆ€ x : â„‚, HasDerivAt (fun x â†¦ -(2 * b)â»Â¹ * cexp (-b * x ^ 2))
    (x * cexp (-b * x ^ 2)) x := by
    intro x
    convert ((hasDerivAt_pow 2 x).const_mul (-b)).cexp.const_mul (-(2 * b)â»Â¹) using 1
    field_simp [hb']
    ring
  have B : Tendsto (fun y : â„ â†¦ -(2 * b)â»Â¹ * cexp (-b * (y : â„‚) ^ 2))
    atTop (ğ“ (-(2 * b)â»Â¹ * 0)) := by
    refine Tendsto.const_mul _ (tendsto_zero_iff_norm_tendsto_zero.mpr ?_)
    simp_rw [norm_cexp_neg_mul_sq b]
    exact tendsto_exp_atBot.comp
      ((tendsto_pow_atTop two_ne_zero).const_mul_atTop_of_neg (neg_lt_zero.2 hb))
  convert integral_Ioi_of_hasDerivAt_of_tendsto' (fun x _ â†¦ (A â†‘x).comp_ofReal)
    (integrable_mul_cexp_neg_mul_sq hb).integrableOn B using 1
  simp only [mul_zero, ofReal_zero, zero_pow, Ne,
    not_false_iff, Complex.exp_zero, mul_one, sub_neg_eq_add, zero_add, reduceCtorEq]

/-- The *square* of the Gaussian integral `âˆ« x:â„, exp (-b * x^2)` is equal to `Ï€ / b`. -/
theorem integral_gaussian_sq_complex {b : â„‚} (hb : 0 < b.re) :
    (âˆ« x : â„, cexp (-b * (x : â„‚) ^ 2)) ^ 2 = Ï€ / b := by
  /- We compute `(âˆ« exp (-b x^2))^2` as an integral over `â„^2`, and then make a polar change
  of coordinates. We are left with `âˆ« r * exp (-b r^2)`, which has been computed in
  `integral_mul_cexp_neg_mul_sq` using the fact that this function has an obvious primitive. -/
  calc
    (âˆ« x : â„, cexp (-b * (x : â„‚) ^ 2)) ^ 2 =
        âˆ« p : â„ Ã— â„, cexp (-b * (p.1 : â„‚) ^ 2) * cexp (-b * (p.2 : â„‚) ^ 2) := by
      rw [pow_two, â† integral_prod_mul]; rfl
    _ = âˆ« p : â„ Ã— â„, cexp (-b * ((p.1 : â„‚)^ 2 + (p.2 : â„‚) ^ 2)) := by
      congr
      ext1 p
      rw [â† Complex.exp_add, mul_add]
    _ = âˆ« p in polarCoord.target, p.1 â€¢
        cexp (-b * ((p.1 * Complex.cos p.2) ^ 2 + (p.1 * Complex.sin p.2) ^ 2)) := by
      rw [â† integral_comp_polarCoord_symm]
      simp only [polarCoord_symm_apply, ofReal_mul, ofReal_cos, ofReal_sin]
    _ = (âˆ« r in Ioi (0 : â„), r * cexp (-b * (r : â„‚) ^ 2)) * âˆ« Î¸ in Ioo (-Ï€) Ï€, 1 := by
      rw [â† setIntegral_prod_mul]
      congr with p : 1
      rw [mul_one]
      congr
      conv_rhs => rw [â† one_mul ((p.1 : â„‚) ^ 2), â† sin_sq_add_cos_sq (p.2 : â„‚)]
      ring
    _ = â†‘Ï€ / b := by
      simp only [integral_const, MeasurableSet.univ, measureReal_restrict_apply,
        univ_inter, real_smul, mul_one, integral_mul_cexp_neg_mul_sq hb]
      rw [volume_real_Ioo_of_le (by linarith [pi_nonneg])]
      simp
      ring

theorem integral_gaussian (b : â„) : âˆ« x : â„, exp (-b * x ^ 2) = âˆš(Ï€ / b) := by
  -- First we deal with the crazy case where `b â‰¤ 0`: then both sides vanish.
  rcases le_or_gt b 0 with (hb | hb)
  Â· rw [integral_undef, sqrt_eq_zero_of_nonpos]
    Â· exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb
    Â· simpa only [not_lt, integrable_exp_neg_mul_sq_iff] using hb
  -- Assume now `b > 0`. Then both sides are non-negative and their squares agree.
  refine (sq_eq_sqâ‚€ (by positivity) (by positivity)).1 ?_
  rw [â† ofReal_inj, ofReal_pow, â† coe_algebraMap, RCLike.algebraMap_eq_ofReal, â† integral_ofReal,
    sq_sqrt (div_pos pi_pos hb).le, â† RCLike.algebraMap_eq_ofReal, coe_algebraMap, ofReal_div]
  convert integral_gaussian_sq_complex (by rwa [ofReal_re] : 0 < (b : â„‚).re) with _ x
  rw [ofReal_exp, ofReal_mul, ofReal_pow, ofReal_neg]

theorem continuousAt_gaussian_integral (b : â„‚) (hb : 0 < re b) :
    ContinuousAt (fun c : â„‚ â†¦ âˆ« x : â„, cexp (-c * (x : â„‚) ^ 2)) b := by
  let f : â„‚ â†’ â„ â†’ â„‚ := fun (c : â„‚) (x : â„) â†¦ cexp (-c * (x : â„‚) ^ 2)
  obtain âŸ¨d, hd, hd'âŸ© := exists_between hb
  have f_meas : âˆ€ c : â„‚, AEStronglyMeasurable (f c) volume := fun c â†¦ by
    apply Continuous.aestronglyMeasurable
    exact Complex.continuous_exp.comp (continuous_const.mul (continuous_ofReal.pow 2))
  have f_cts : âˆ€ x : â„, ContinuousAt (fun c â†¦ f c x) b := fun x â†¦
    (Complex.continuous_exp.comp (continuous_id'.neg.mul continuous_const)).continuousAt
  have f_le_bd : âˆ€á¶  c : â„‚ in ğ“ b, âˆ€áµ x : â„, â€–f c xâ€– â‰¤ exp (-d * x ^ 2) := by
    refine eventually_of_mem ((continuous_re.isOpen_preimage _ isOpen_Ioi).mem_nhds hd') ?_
    intro c hc; filter_upwards with x
    rw [norm_cexp_neg_mul_sq]
    gcongr
    exact le_of_lt hc
  exact
    continuousAt_of_dominated (Eventually.of_forall f_meas) f_le_bd (integrable_exp_neg_mul_sq hd)
      (ae_of_all _ f_cts)

theorem integral_gaussian_complex {b : â„‚} (hb : 0 < re b) :
    âˆ« x : â„, cexp (-b * (x : â„‚) ^ 2) = (Ï€ / b) ^ (1 / 2 : â„‚) := by
  have nv : âˆ€ {b : â„‚}, 0 < re b â†’ b â‰  0 := by intro b hb; contrapose! hb; rw [hb]; simp
  apply
    (convex_halfSpace_re_gt 0).isPreconnected.eq_of_sq_eq ?_ ?_ (fun c hc â†¦ ?_) (fun {c} hc â†¦ ?_)
      (by simp : 0 < re (1 : â„‚)) ?_ hb
  Â· -- integral is continuous
    exact continuousOn_of_forall_continuousAt continuousAt_gaussian_integral
  Â· -- `(Ï€ / b) ^ (1 / 2 : â„‚)` is continuous
    refine
      continuousOn_of_forall_continuousAt fun b hb â†¦
        (continuousAt_cpow_const (Or.inl ?_)).comp (continuousAt_const.div continuousAt_id (nv hb))
    rw [div_re, ofReal_im, ofReal_re, zero_mul, zero_div, add_zero]
    exact div_pos (mul_pos pi_pos hb) (normSq_pos.mpr (nv hb))
  Â· -- equality at 1
    have : âˆ€ x : â„, cexp (-(1 : â„‚) * (x : â„‚) ^ 2) = exp (-(1 : â„) * x ^ 2) := by
      intro x
      simp only [ofReal_exp, neg_mul, one_mul, ofReal_neg, ofReal_pow]
    simp_rw [this, â† coe_algebraMap, RCLike.algebraMap_eq_ofReal, integral_ofReal,
      â† RCLike.algebraMap_eq_ofReal, coe_algebraMap]
    conv_rhs =>
      congr
      Â· rw [â† ofReal_one, â† ofReal_div]
      Â· rw [â† ofReal_one, â† ofReal_ofNat, â† ofReal_div]
    rw [â† ofReal_cpow, ofReal_inj]
    Â· convert integral_gaussian (1 : â„) using 1
      rw [sqrt_eq_rpow]
    Â· rw [div_one]; exact pi_pos.le
  Â· -- squares of both sides agree
    dsimp only [Pi.pow_apply]
    rw [integral_gaussian_sq_complex hc, sq]
    conv_lhs => rw [â† cpow_one (â†‘Ï€ / c)]
    rw [â† cpow_add _ _ (div_ne_zero (ofReal_ne_zero.mpr pi_ne_zero) (nv hc))]
    norm_num
  Â· -- RHS doesn't vanish
    rw [Ne, cpow_eq_zero_iff, not_and_or]
    exact Or.inl (div_ne_zero (ofReal_ne_zero.mpr pi_ne_zero) (nv hc))

-- The Gaussian integral on the half-line, `âˆ« x in Ioi 0, exp (-b * x^2)`, for complex `b`.
theorem integral_gaussian_complex_Ioi {b : â„‚} (hb : 0 < re b) :
    âˆ« x : â„ in Ioi 0, cexp (-b * (x : â„‚) ^ 2) = (Ï€ / b) ^ (1 / 2 : â„‚) / 2 := by
  have full_integral := integral_gaussian_complex hb
  have : MeasurableSet (Ioi (0 : â„)) := measurableSet_Ioi
  rw [â† integral_add_compl this (integrable_cexp_neg_mul_sq hb), compl_Ioi] at full_integral
  suffices âˆ« x : â„ in Iic 0, cexp (-b * (x : â„‚) ^ 2) = âˆ« x : â„ in Ioi 0, cexp (-b * (x : â„‚) ^ 2) by
    rw [this, â† mul_two] at full_integral
    rwa [eq_div_iff]; exact two_ne_zero
  have : âˆ€ c : â„, âˆ« x in (0 : â„)..c, cexp (-b * (x : â„‚) ^ 2) =
      âˆ« x in -c..0, cexp (-b * (x : â„‚) ^ 2) := by
    intro c
    have := intervalIntegral.integral_comp_sub_left (a := 0) (b := c)
      (fun x â†¦ cexp (-b * (x : â„‚) ^ 2)) 0
    simpa [zero_sub, neg_sq, neg_zero] using this
  have t1 :=
    intervalIntegral_tendsto_integral_Ioi 0 (integrable_cexp_neg_mul_sq hb).integrableOn tendsto_id
  have t2 :
    Tendsto (fun c : â„ â†¦ âˆ« x : â„ in (0 : â„)..c, cexp (-b * (x : â„‚) ^ 2)) atTop
      (ğ“ (âˆ« x : â„ in Iic 0, cexp (-b * (x : â„‚) ^ 2))) := by
    simp_rw [this]
    refine intervalIntegral_tendsto_integral_Iic _ ?_ tendsto_neg_atTop_atBot
    apply (integrable_cexp_neg_mul_sq hb).integrableOn
  exact tendsto_nhds_unique t2 t1

-- The Gaussian integral on the half-line, `âˆ« x in Ioi 0, exp (-b * x^2)`, for real `b`.
theorem integral_gaussian_Ioi (b : â„) :
    âˆ« x in Ioi (0 : â„), exp (-b * x ^ 2) = âˆš(Ï€ / b) / 2 := by
  rcases le_or_gt b 0 with (hb | hb)
  Â· rw [integral_undef, sqrt_eq_zero_of_nonpos, zero_div]
    Â· exact div_nonpos_of_nonneg_of_nonpos pi_pos.le hb
    Â· rwa [â† IntegrableOn, integrableOn_Ioi_exp_neg_mul_sq_iff, not_lt]
  rw [â† RCLike.ofReal_inj (K := â„‚), â† integral_ofReal, â† RCLike.algebraMap_eq_ofReal,
    coe_algebraMap]
  convert integral_gaussian_complex_Ioi (by rwa [ofReal_re] : 0 < (b : â„‚).re)
  Â· simp
  Â· rw [sqrt_eq_rpow, â† ofReal_div, ofReal_div, ofReal_cpow]
    Â· norm_num
    Â· exact (div_pos pi_pos hb).le

/-- The special-value formula `Î“(1/2) = âˆšÏ€`, which is equivalent to the Gaussian integral. -/
theorem Real.Gamma_one_half_eq : Real.Gamma (1 / 2) = âˆšÏ€ := by
  rw [Gamma_eq_integral one_half_pos, â† integral_comp_rpow_Ioi_of_pos zero_lt_two]
  convert congr_arg (fun x : â„ â†¦ 2 * x) (integral_gaussian_Ioi 1) using 1
  Â· rw [â† integral_const_mul]
    refine setIntegral_congr_fun measurableSet_Ioi fun x hx â†¦ ?_
    dsimp only
    have : (x ^ (2 : â„)) ^ (1 / (2 : â„) - 1) = xâ»Â¹ := by
      rw [â† rpow_mul (le_of_lt hx)]
      norm_num
      rw [rpow_neg (le_of_lt hx), rpow_one]
    rw [smul_eq_mul, this]
    field_simp [(ne_of_lt (show 0 < x from hx)).symm]
    norm_num; ring
  Â· rw [div_one, â† mul_div_assoc, mul_comm, mul_div_cancel_rightâ‚€ _ (two_ne_zero' â„)]

/-- The special-value formula `Î“(1/2) = âˆšÏ€`, which is equivalent to the Gaussian integral. -/
theorem Complex.Gamma_one_half_eq : Complex.Gamma (1 / 2) = (Ï€ : â„‚) ^ (1 / 2 : â„‚) := by
  convert congr_arg ((â†‘) : â„ â†’ â„‚) Real.Gamma_one_half_eq
  Â· simpa only [one_div, ofReal_inv, ofReal_ofNat] using Gamma_ofReal (1 / 2)
  Â· rw [sqrt_eq_rpow, ofReal_cpow pi_pos.le, ofReal_div, ofReal_ofNat, ofReal_one]

open scoped Nat in
/-- The special-value formula `Î“(k + 1 + 1/2) = (2 * k + 1)â€¼ * âˆšÏ€ / (2 ^ (k + 1))` for half-integer
values of the gamma function in terms of `Nat.doubleFactorial`. -/
lemma Real.Gamma_nat_add_one_add_half (k : â„•) :
    Gamma (k + 1 + 1 / 2) = (2 * k + 1 : â„•)â€¼ * âˆšÏ€ / (2 ^ (k + 1)) := by
  induction k with
  | zero => simp [-one_div, add_comm (1 : â„), Gamma_add_one, Gamma_one_half_eq]; ring
  | succ k ih =>
    rw [add_right_comm, Gamma_add_one (by positivity), Nat.cast_add, Nat.cast_one, ih, Nat.mul_add]
    simp
    ring

open scoped Nat in
/-- The special-value formula `Î“(k + 1/2) = (2 * k - 1)â€¼ * âˆšÏ€ / (2 ^ k))` for half-integer
values of the gamma function in terms of `Nat.doubleFactorial`. -/
lemma Real.Gamma_nat_add_half (k : â„•) :
    Gamma (k + 1 / 2) = (2 * k - 1 : â„•)â€¼ * âˆšÏ€ / (2 ^ k) := by
  cases k with
  | zero => simp [- one_div, Gamma_one_half_eq]
  | succ k => simpa [-one_div, mul_add] using Gamma_nat_add_one_add_half k
