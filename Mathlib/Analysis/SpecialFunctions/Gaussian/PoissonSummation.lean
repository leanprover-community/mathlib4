/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.Analysis.SpecialFunctions.Gaussian.FourierTransform
import Mathlib.Analysis.Fourier.PoissonSummation

/-!
# Poisson summation applied to the Gaussian

In `Real.tsum_exp_neg_mul_int_sq` and `Complex.tsum_exp_neg_mul_int_sq`, we use Poisson summation
to prove the identity

`‚àë' (n : ‚Ñ§), exp (-œÄ * a * n ^ 2) = 1 / a ^ (1 / 2) * ‚àë' (n : ‚Ñ§), exp (-œÄ / a * n ^ 2)`

for positive real `a`, or complex `a` with positive real part. (See also
`NumberTheory.ModularForms.JacobiTheta`.)
-/

open Real Set MeasureTheory Filter Asymptotics intervalIntegral

open scoped Real Topology FourierTransform RealInnerProductSpace

open Complex hiding exp continuous_exp abs_of_nonneg sq_abs

noncomputable section

section GaussianPoisson

/-! First we show that Gaussian-type functions have rapid decay along `cocompact ‚Ñù`. -/

lemma rexp_neg_quadratic_isLittleO_rpow_atTop {a : ‚Ñù} (ha : a < 0) (b s : ‚Ñù) :
    (fun x ‚Ü¶ rexp (a * x ^ 2 + b * x)) =o[atTop] (¬∑ ^ s) := by
  suffices (fun x ‚Ü¶ rexp (a * x ^ 2 + b * x)) =o[atTop] (fun x ‚Ü¶ rexp (-x)) by
    refine this.trans ?_
    simpa only [neg_one_mul] using isLittleO_exp_neg_mul_rpow_atTop zero_lt_one s
  rw [isLittleO_exp_comp_exp_comp]
  have : (fun x ‚Ü¶ -x - (a * x ^ 2 + b * x)) = fun x ‚Ü¶ x * (-a * x - (b + 1)) := by
    ext1 x; ring_nf
  rw [this]
  exact tendsto_id.atTop_mul_atTop‚ÇÄ <| tendsto_atTop_add_const_right _ _ <|
    tendsto_id.const_mul_atTop (neg_pos.mpr ha)

lemma cexp_neg_quadratic_isLittleO_rpow_atTop {a : ‚ÑÇ} (ha : a.re < 0) (b : ‚ÑÇ) (s : ‚Ñù) :
    (fun x : ‚Ñù ‚Ü¶ cexp (a * x ^ 2 + b * x)) =o[atTop] (¬∑ ^ s) := by
  apply Asymptotics.IsLittleO.of_norm_left
  convert rexp_neg_quadratic_isLittleO_rpow_atTop ha b.re s with x
  simp_rw [Complex.norm_exp, add_re, ‚Üê ofReal_pow, mul_comm (_ : ‚ÑÇ) ‚Üë(_ : ‚Ñù),
      re_ofReal_mul, mul_comm _ (re _)]

lemma cexp_neg_quadratic_isLittleO_abs_rpow_cocompact {a : ‚ÑÇ} (ha : a.re < 0) (b : ‚ÑÇ) (s : ‚Ñù) :
    (fun x : ‚Ñù ‚Ü¶ cexp (a * x ^ 2 + b * x)) =o[cocompact ‚Ñù] (|¬∑| ^ s) := by
  rw [cocompact_eq_atBot_atTop, isLittleO_sup]
  constructor
  ¬∑ refine ((cexp_neg_quadratic_isLittleO_rpow_atTop ha (-b) s).comp_tendsto
      Filter.tendsto_neg_atBot_atTop).congr' (Eventually.of_forall fun x ‚Ü¶ ?_) ?_
    ¬∑ simp only [neg_mul, Function.comp_apply, ofReal_neg, neg_sq, mul_neg, neg_neg]
    ¬∑ refine (eventually_lt_atBot 0).mp (Eventually.of_forall fun x hx ‚Ü¶ ?_)
      simp only [Function.comp_apply, abs_of_neg hx]
  ¬∑ refine (cexp_neg_quadratic_isLittleO_rpow_atTop ha b s).congr' EventuallyEq.rfl ?_
    refine (eventually_gt_atTop 0).mp (Eventually.of_forall fun x hx ‚Ü¶ ?_)
    simp_rw [abs_of_pos hx]

theorem tendsto_rpow_abs_mul_exp_neg_mul_sq_cocompact {a : ‚Ñù} (ha : 0 < a) (s : ‚Ñù) :
    Tendsto (fun x : ‚Ñù => |x| ^ s * rexp (-a * x ^ 2)) (cocompact ‚Ñù) (ùìù 0) := by
  conv in rexp _ => rw [‚Üê sq_abs]
  rw [cocompact_eq_atBot_atTop, ‚Üê comap_abs_atTop]
  erw [tendsto_comap'_iff (m := fun y => y ^ s * rexp (-a * y ^ 2))
      (mem_atTop_sets.mpr ‚ü®0, fun b hb => ‚ü®b, abs_of_nonneg hb‚ü©‚ü©)]
  exact
    (rpow_mul_exp_neg_mul_sq_isLittleO_exp_neg ha s).tendsto_zero_of_tendsto
      (tendsto_exp_atBot.comp <| tendsto_id.const_mul_atTop_of_neg (neg_lt_zero.mpr one_half_pos))

theorem isLittleO_exp_neg_mul_sq_cocompact {a : ‚ÑÇ} (ha : 0 < a.re) (s : ‚Ñù) :
    (fun x : ‚Ñù => Complex.exp (-a * x ^ 2)) =o[cocompact ‚Ñù] fun x : ‚Ñù => |x| ^ s := by
  convert cexp_neg_quadratic_isLittleO_abs_rpow_cocompact (?_ : (-a).re < 0) 0 s using 1
  ¬∑ simp_rw [zero_mul, add_zero]
  ¬∑ rwa [neg_re, neg_lt_zero]

/-- Jacobi's theta-function transformation formula for the sum of `exp -Q(x)`, where `Q` is a
negative definite quadratic form. -/
theorem Complex.tsum_exp_neg_quadratic {a : ‚ÑÇ} (ha : 0 < a.re) (b : ‚ÑÇ) :
    (‚àë' n : ‚Ñ§, cexp (-œÄ * a * n ^ 2 + 2 * œÄ * b * n)) =
      1 / a ^ (1 / 2 : ‚ÑÇ) * ‚àë' n : ‚Ñ§, cexp (-œÄ / a * (n + I * b) ^ 2) := by
  let f : ‚Ñù ‚Üí ‚ÑÇ := fun x ‚Ü¶ cexp (-œÄ * a * x ^ 2 + 2 * œÄ * b * x)
  have hCf : Continuous f := by
    refine Complex.continuous_exp.comp (Continuous.add ?_ ?_)
    ¬∑ exact continuous_const.mul (Complex.continuous_ofReal.pow 2)
    ¬∑ exact continuous_const.mul Complex.continuous_ofReal
  have hFf : ùìï f = fun x : ‚Ñù ‚Ü¶ 1 / a ^ (1 / 2 : ‚ÑÇ) * cexp (-œÄ / a * (x + I * b) ^ 2) :=
    fourierIntegral_gaussian_pi' ha b
  have h1 : 0 < (‚ÜëœÄ * a).re := by
    rw [re_ofReal_mul]
    exact mul_pos pi_pos ha
  have h2 : 0 < (‚ÜëœÄ / a).re := by
    rw [div_eq_mul_inv, re_ofReal_mul, inv_re]
    refine mul_pos pi_pos (div_pos ha <| normSq_pos.mpr ?_)
    contrapose! ha
    rw [ha, zero_re]
  have f_bd : f =O[cocompact ‚Ñù] (fun x => |x| ^ (-2 : ‚Ñù)) := by
    convert (cexp_neg_quadratic_isLittleO_abs_rpow_cocompact ?_ _ (-2)).isBigO
    rwa [neg_mul, neg_re, neg_lt_zero]
  have Ff_bd : (ùìï f) =O[cocompact ‚Ñù] (fun x => |x| ^ (-2 : ‚Ñù)) := by
    rw [hFf]
    have : ‚àÄ (x : ‚Ñù), -‚ÜëœÄ / a * (‚Üëx + I * b) ^ 2 =
        -‚ÜëœÄ / a * x ^ 2 + (-2 * œÄ * I * b) / a * x + œÄ * b ^ 2 / a := by
      intro x; ring_nf; rw [I_sq]; ring
    simp_rw [this]
    conv => enter [2, x]; rw [Complex.exp_add, ‚Üê mul_assoc _ _ (Complex.exp _), mul_comm]
    refine ((cexp_neg_quadratic_isLittleO_abs_rpow_cocompact
      (?_) (-2 * ‚ÜëœÄ * I * b / a) (-2)).isBigO.const_mul_left _).const_mul_left _
    rwa [neg_div, neg_re, neg_lt_zero]
  convert Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay hCf one_lt_two f_bd Ff_bd 0 using 1
  ¬∑ simp only [f, zero_add, ofReal_intCast]
  ¬∑ rw [‚Üê tsum_mul_left]
    simp only [QuotientAddGroup.mk_zero, fourier_eval_zero, mul_one, hFf, ofReal_intCast]

theorem Complex.tsum_exp_neg_mul_int_sq {a : ‚ÑÇ} (ha : 0 < a.re) :
    (‚àë' n : ‚Ñ§, cexp (-œÄ * a * (n : ‚ÑÇ) ^ 2)) =
      1 / a ^ (1 / 2 : ‚ÑÇ) * ‚àë' n : ‚Ñ§, cexp (-œÄ / a * (n : ‚ÑÇ) ^ 2) := by
  simpa only [mul_zero, zero_mul, add_zero] using Complex.tsum_exp_neg_quadratic ha 0

theorem Real.tsum_exp_neg_mul_int_sq {a : ‚Ñù} (ha : 0 < a) :
    (‚àë' n : ‚Ñ§, exp (-œÄ * a * (n : ‚Ñù) ^ 2)) =
      (1 : ‚Ñù) / a ^ (1 / 2 : ‚Ñù) * (‚àë' n : ‚Ñ§, exp (-œÄ / a * (n : ‚Ñù) ^ 2)) := by
  simpa only [‚Üê ofReal_inj, ofReal_tsum, ofReal_exp, ofReal_mul, ofReal_neg, ofReal_pow,
    ofReal_intCast, ofReal_div, ofReal_one, ofReal_cpow ha.le, ofReal_ofNat, mul_zero, zero_mul,
    add_zero] using Complex.tsum_exp_neg_quadratic (by rwa [ofReal_re] : 0 < (a : ‚ÑÇ).re) 0

end GaussianPoisson
