/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Convolution
import Mathlib.Analysis.SpecialFunctions.Complex.LogBounds
import Mathlib.Analysis.SpecialFunctions.Trigonometric.EulerSineProd
import Mathlib.Analysis.SpecialFunctions.Gamma.BohrMollerup
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Analysis.Complex.CauchyIntegral

/-!
# The Beta function, and further properties of the Gamma function

In this file we define the Beta integral, relate Beta and Gamma functions, and prove some
refined properties of the Gamma function using these relations.

## Results on the Beta function

* `Complex.betaIntegral`: the Beta function `Œí(u, v)`, where `u`, `v` are complex with positive
  real part.
* `Complex.Gamma_mul_Gamma_eq_betaIntegral`: the formula
  `Gamma u * Gamma v = Gamma (u + v) * betaIntegral u v`.

## Results on the Gamma function

* `Complex.Gamma_ne_zero`: for all `s : ‚ÑÇ` with `s ‚àâ {-n : n ‚àà ‚Ñï}` we have `Œì s ‚â† 0`.
* `Complex.GammaSeq_tendsto_Gamma`: for all `s`, the limit as `n ‚Üí ‚àû` of the sequence
  `n ‚Ü¶ n ^ s * n! / (s * (s + 1) * ... * (s + n))` is `Œì(s)`.
* `Complex.Gamma_mul_Gamma_one_sub`: Euler's reflection formula
  `Gamma s * Gamma (1 - s) = œÄ / sin œÄ s`.
* `Complex.differentiable_one_div_Gamma`: the function `1 / Œì(s)` is differentiable everywhere.
* `Complex.Gamma_mul_Gamma_add_half`: Legendre's duplication formula
  `Gamma s * Gamma (s + 1 / 2) = Gamma (2 * s) * 2 ^ (1 - 2 * s) * ‚àöœÄ`.
* `Real.Gamma_ne_zero`, `Real.GammaSeq_tendsto_Gamma`,
  `Real.Gamma_mul_Gamma_one_sub`, `Real.Gamma_mul_Gamma_add_half`: real versions of the above.
-/


noncomputable section


open Filter intervalIntegral Set Real MeasureTheory

open scoped Nat Topology Real

section BetaIntegral

/-! ## The Beta function -/


namespace Complex

/-- The Beta function `Œí (u, v)`, defined as `‚à´ x:‚Ñù in 0..1, x ^ (u - 1) * (1 - x) ^ (v - 1)`. -/
noncomputable def betaIntegral (u v : ‚ÑÇ) : ‚ÑÇ :=
  ‚à´ x : ‚Ñù in 0..1, (x : ‚ÑÇ) ^ (u - 1) * (1 - (x : ‚ÑÇ)) ^ (v - 1)

/-- Auxiliary lemma for `betaIntegral_convergent`, showing convergence at the left endpoint. -/
theorem betaIntegral_convergent_left {u : ‚ÑÇ} (hu : 0 < re u) (v : ‚ÑÇ) :
    IntervalIntegrable (fun x =>
      (x : ‚ÑÇ) ^ (u - 1) * (1 - (x : ‚ÑÇ)) ^ (v - 1) : ‚Ñù ‚Üí ‚ÑÇ) volume 0 (1 / 2) := by
  apply IntervalIntegrable.mul_continuousOn
  ¬∑ refine intervalIntegral.intervalIntegrable_cpow' ?_
    rwa [sub_re, one_re, ‚Üê zero_sub, sub_lt_sub_iff_right]
  ¬∑ apply continuousOn_of_forall_continuousAt
    intro x hx
    rw [uIcc_of_le (by positivity : (0 : ‚Ñù) ‚â§ 1 / 2)] at hx
    apply ContinuousAt.cpow
    ¬∑ exact (continuous_const.sub continuous_ofReal).continuousAt
    ¬∑ exact continuousAt_const
    ¬∑ norm_cast
      exact ofReal_mem_slitPlane.2 <| by linarith only [hx.2]

/-- The Beta integral is convergent for all `u, v` of positive real part. -/
theorem betaIntegral_convergent {u v : ‚ÑÇ} (hu : 0 < re u) (hv : 0 < re v) :
    IntervalIntegrable (fun x =>
      (x : ‚ÑÇ) ^ (u - 1) * (1 - (x : ‚ÑÇ)) ^ (v - 1) : ‚Ñù ‚Üí ‚ÑÇ) volume 0 1 := by
  refine (betaIntegral_convergent_left hu v).trans ?_
  rw [IntervalIntegrable.iff_comp_neg]
  convert ((betaIntegral_convergent_left hv u).comp_add_right 1).symm using 1
  ¬∑ ext1 x
    conv_lhs => rw [mul_comm]
    congr 2 <;> ¬∑ push_cast; ring
  ¬∑ norm_num
  ¬∑ simp

theorem betaIntegral_symm (u v : ‚ÑÇ) : betaIntegral v u = betaIntegral u v := by
  simpa [betaIntegral, ‚Üê intervalIntegral.integral_symm, add_comm, mul_comm, sub_eq_add_neg]
    using intervalIntegral.integral_comp_mul_add (a := 0) (b := 1) (c := -1)
      (fun x : ‚Ñù => (x : ‚ÑÇ) ^ (u - 1) * (1 - (x : ‚ÑÇ)) ^ (v - 1)) neg_one_lt_zero.ne 1

theorem betaIntegral_eval_one_right {u : ‚ÑÇ} (hu : 0 < re u) : betaIntegral u 1 = 1 / u := by
  simp_rw [betaIntegral, sub_self, cpow_zero, mul_one]
  rw [integral_cpow (Or.inl _)]
  ¬∑ rw [ofReal_zero, ofReal_one, one_cpow, zero_cpow, sub_zero, sub_add_cancel]
    rw [sub_add_cancel]
    contrapose! hu; rw [hu, zero_re]
  ¬∑ rwa [sub_re, one_re, ‚Üê sub_pos, sub_neg_eq_add, sub_add_cancel]

theorem betaIntegral_scaled (s t : ‚ÑÇ) {a : ‚Ñù} (ha : 0 < a) :
    ‚à´ x in 0..a, (x : ‚ÑÇ) ^ (s - 1) * ((a : ‚ÑÇ) - x) ^ (t - 1) =
    (a : ‚ÑÇ) ^ (s + t - 1) * betaIntegral s t := by
  have ha' : (a : ‚ÑÇ) ‚â† 0 := ofReal_ne_zero.mpr ha.ne'
  rw [betaIntegral]
  have A : (a : ‚ÑÇ) ^ (s + t - 1) = a * ((a : ‚ÑÇ) ^ (s - 1) * (a : ‚ÑÇ) ^ (t - 1)) := by
    rw [(by abel : s + t - 1 = 1 + (s - 1) + (t - 1)), cpow_add _ _ ha', cpow_add 1 _ ha', cpow_one,
      mul_assoc]
  rw [A, mul_assoc, ‚Üê intervalIntegral.integral_const_mul, ‚Üê real_smul, ‚Üê zero_div a, ‚Üê
    div_self ha.ne', ‚Üê intervalIntegral.integral_comp_div _ ha.ne', zero_div]
  simp_rw [intervalIntegral.integral_of_le ha.le]
  refine setIntegral_congr_fun measurableSet_Ioc fun x hx => ?_
  rw [mul_mul_mul_comm]
  congr 1
  ¬∑ rw [‚Üê mul_cpow_ofReal_nonneg ha.le (div_pos hx.1 ha).le, ofReal_div, mul_div_cancel‚ÇÄ _ ha']
  ¬∑ rw [(by norm_cast : (1 : ‚ÑÇ) - ‚Üë(x / a) = ‚Üë(1 - x / a)), ‚Üê
      mul_cpow_ofReal_nonneg ha.le (sub_nonneg.mpr <| (div_le_one ha).mpr hx.2)]
    push_cast
    rw [mul_sub, mul_one, mul_div_cancel‚ÇÄ _ ha']

/-- Relation between Beta integral and Gamma function. -/
theorem Gamma_mul_Gamma_eq_betaIntegral {s t : ‚ÑÇ} (hs : 0 < re s) (ht : 0 < re t) :
    Gamma s * Gamma t = Gamma (s + t) * betaIntegral s t := by
  -- Note that we haven't proved (yet) that the Gamma function has no zeroes, so we can't formulate
  -- this as a formula for the Beta function.
  have conv_int := integral_posConvolution
    (GammaIntegral_convergent hs) (GammaIntegral_convergent ht) (ContinuousLinearMap.mul ‚Ñù ‚ÑÇ)
  simp_rw [ContinuousLinearMap.mul_apply'] at conv_int
  have hst : 0 < re (s + t) := by rw [add_re]; exact add_pos hs ht
  rw [Gamma_eq_integral hs, Gamma_eq_integral ht, Gamma_eq_integral hst, GammaIntegral,
    GammaIntegral, GammaIntegral, ‚Üê conv_int, ‚Üê MeasureTheory.integral_mul_const (betaIntegral _ _)]
  refine setIntegral_congr_fun measurableSet_Ioi fun x hx => ?_
  rw [mul_assoc, ‚Üê betaIntegral_scaled s t hx, ‚Üê intervalIntegral.integral_const_mul]
  congr 1 with y : 1
  push_cast
  suffices Complex.exp (-x) = Complex.exp (-y) * Complex.exp (-(x - y)) by rw [this]; ring
  rw [‚Üê Complex.exp_add]; congr 1; abel

/-- Recurrence formula for the Beta function. -/
theorem betaIntegral_recurrence {u v : ‚ÑÇ} (hu : 0 < re u) (hv : 0 < re v) :
    u * betaIntegral u (v + 1) = v * betaIntegral (u + 1) v := by
  -- NB: If we knew `Gamma (u + v + 1) ‚â† 0` this would be an easy consequence of
  -- `Gamma_mul_Gamma_eq_betaIntegral`; but we don't know that yet. We will prove it later, but
  -- this lemma is needed in the proof. So we give a (somewhat laborious) direct argument.
  let F : ‚Ñù ‚Üí ‚ÑÇ := fun x => (x : ‚ÑÇ) ^ u * (1 - (x : ‚ÑÇ)) ^ v
  have hu' : 0 < re (u + 1) := by rw [add_re, one_re]; positivity
  have hv' : 0 < re (v + 1) := by rw [add_re, one_re]; positivity
  have hc : ContinuousOn F (Icc 0 1) := by
    refine (continuousOn_of_forall_continuousAt fun x hx => ?_).mul
        (continuousOn_of_forall_continuousAt fun x hx => ?_)
    ¬∑ refine (continuousAt_cpow_const_of_re_pos (Or.inl ?_) hu).comp continuous_ofReal.continuousAt
      rw [ofReal_re]; exact hx.1
    ¬∑ refine (continuousAt_cpow_const_of_re_pos (Or.inl ?_) hv).comp
        (continuous_const.sub continuous_ofReal).continuousAt
      rw [sub_re, one_re, ofReal_re, sub_nonneg]
      exact hx.2
  have hder : ‚àÄ x : ‚Ñù, x ‚àà Ioo (0 : ‚Ñù) 1 ‚Üí
      HasDerivAt F (u * ((x : ‚ÑÇ) ^ (u - 1) * (1 - (x : ‚ÑÇ)) ^ v) -
        v * ((x : ‚ÑÇ) ^ u * (1 - (x : ‚ÑÇ)) ^ (v - 1))) x := by
    intro x hx
    have U : HasDerivAt (fun y : ‚ÑÇ => y ^ u) (u * (x : ‚ÑÇ) ^ (u - 1)) ‚Üëx := by
      have := @HasDerivAt.cpow_const _ _ _ u (hasDerivAt_id (x : ‚ÑÇ)) (Or.inl ?_)
      ¬∑ simp only [id_eq, mul_one] at this
        exact this
      ¬∑ rw [id_eq, ofReal_re]; exact hx.1
    have V : HasDerivAt (fun y : ‚ÑÇ => (1 - y) ^ v) (-v * (1 - (x : ‚ÑÇ)) ^ (v - 1)) ‚Üëx := by
      have A := @HasDerivAt.cpow_const _ _ _ v (hasDerivAt_id (1 - (x : ‚ÑÇ))) (Or.inl ?_)
      swap; ¬∑ rw [id, sub_re, one_re, ofReal_re, sub_pos]; exact hx.2
      simp_rw [id] at A
      have B : HasDerivAt (fun y : ‚ÑÇ => 1 - y) (-1) ‚Üëx := by
        apply HasDerivAt.const_sub; apply hasDerivAt_id
      convert HasDerivAt.comp (‚Üëx) A B using 1
      ring
    convert (U.mul V).comp_ofReal using 1
    ring
  have h_int := ((betaIntegral_convergent hu hv').const_mul u).sub
    ((betaIntegral_convergent hu' hv).const_mul v)
  rw [add_sub_cancel_right, add_sub_cancel_right] at h_int
  have int_ev := intervalIntegral.integral_eq_sub_of_hasDerivAt_of_le zero_le_one hc hder h_int
  have hF0 : F 0 = 0 := by
    simp only [F, mul_eq_zero, ofReal_zero, cpow_eq_zero_iff, Ne,
      true_and, sub_zero, one_cpow, one_ne_zero, or_false]
    contrapose! hu; rw [hu, zero_re]
  have hF1 : F 1 = 0 := by
    simp only [F, mul_eq_zero, ofReal_one, one_cpow, one_ne_zero, sub_self, cpow_eq_zero_iff,
      Ne, true_and, false_or]
    contrapose! hv; rw [hv, zero_re]
  rw [hF0, hF1, sub_zero, intervalIntegral.integral_sub, intervalIntegral.integral_const_mul,
    intervalIntegral.integral_const_mul] at int_ev
  ¬∑ rw [betaIntegral, betaIntegral, ‚Üê sub_eq_zero]
    convert int_ev <;> ring
  ¬∑ apply IntervalIntegrable.const_mul
    convert betaIntegral_convergent hu hv'; ring
  ¬∑ apply IntervalIntegrable.const_mul
    convert betaIntegral_convergent hu' hv; ring

/-- Explicit formula for the Beta function when second argument is a positive integer. -/
theorem betaIntegral_eval_nat_add_one_right {u : ‚ÑÇ} (hu : 0 < re u) (n : ‚Ñï) :
    betaIntegral u (n + 1) = n ! / ‚àè j ‚àà Finset.range (n + 1), (u + j) := by
  induction n generalizing u with
  | zero =>
    rw [Nat.cast_zero, zero_add, betaIntegral_eval_one_right hu, Nat.factorial_zero, Nat.cast_one]
    simp
  | succ n IH =>
    have := betaIntegral_recurrence hu (?_ : 0 < re n.succ)
    swap; ¬∑ rw [‚Üê ofReal_natCast, ofReal_re]; positivity
    rw [mul_comm u _, ‚Üê eq_div_iff] at this
    swap; ¬∑ contrapose! hu; rw [hu, zero_re]
    rw [this, Finset.prod_range_succ', Nat.cast_succ, IH]
    swap; ¬∑ rw [add_re, one_re]; positivity
    rw [Nat.factorial_succ, Nat.cast_mul, Nat.cast_add, Nat.cast_one, Nat.cast_zero, add_zero, ‚Üê
      mul_div_assoc, ‚Üê div_div]
    congr 3 with j : 1
    push_cast; abel

end Complex

end BetaIntegral

section LimitFormula

/-! ## The Euler limit formula -/


namespace Complex

/-- The sequence with `n`-th term `n ^ s * n! / (s * (s + 1) * ... * (s + n))`, for complex `s`.
We will show that this tends to `Œì(s)` as `n ‚Üí ‚àû`. -/
noncomputable def GammaSeq (s : ‚ÑÇ) (n : ‚Ñï) :=
  (n : ‚ÑÇ) ^ s * n ! / ‚àè j ‚àà Finset.range (n + 1), (s + j)

theorem GammaSeq_eq_betaIntegral_of_re_pos {s : ‚ÑÇ} (hs : 0 < re s) (n : ‚Ñï) :
    GammaSeq s n = (n : ‚ÑÇ) ^ s * betaIntegral s (n + 1) := by
  rw [GammaSeq, betaIntegral_eval_nat_add_one_right hs n, ‚Üê mul_div_assoc]

theorem GammaSeq_add_one_left (s : ‚ÑÇ) {n : ‚Ñï} (hn : n ‚â† 0) :
    GammaSeq (s + 1) n / s = n / (n + 1 + s) * GammaSeq s n := by
  conv_lhs => rw [GammaSeq, Finset.prod_range_succ, div_div]
  conv_rhs =>
    rw [GammaSeq, Finset.prod_range_succ', Nat.cast_zero, add_zero, div_mul_div_comm, ‚Üê mul_assoc,
      ‚Üê mul_assoc, mul_comm _ (Finset.prod _ _)]
  congr 3
  ¬∑ rw [cpow_add _ _ (Nat.cast_ne_zero.mpr hn), cpow_one, mul_comm]
  ¬∑ refine Finset.prod_congr (by rfl) fun x _ => ?_
    push_cast; ring
  ¬∑ abel

theorem GammaSeq_eq_approx_Gamma_integral {s : ‚ÑÇ} (hs : 0 < re s) {n : ‚Ñï} (hn : n ‚â† 0) :
    GammaSeq s n = ‚à´ x : ‚Ñù in 0..n, ‚Üë((1 - x / n) ^ n) * (x : ‚ÑÇ) ^ (s - 1) := by
  have : ‚àÄ x : ‚Ñù, x = x / n * n := by intro x; rw [div_mul_cancel‚ÇÄ]; exact Nat.cast_ne_zero.mpr hn
  conv_rhs => enter [1, x, 2, 1]; rw [this x]
  rw [GammaSeq_eq_betaIntegral_of_re_pos hs]
  have := intervalIntegral.integral_comp_div (a := 0) (b := n)
    (fun x => ‚Üë((1 - x) ^ n) * ‚Üë(x * ‚Üën) ^ (s - 1) : ‚Ñù ‚Üí ‚ÑÇ) (Nat.cast_ne_zero.mpr hn)
  dsimp only at this
  rw [betaIntegral, this, real_smul, zero_div, div_self, add_sub_cancel_right,
    ‚Üê intervalIntegral.integral_const_mul, ‚Üê intervalIntegral.integral_const_mul]
  swap; ¬∑ exact Nat.cast_ne_zero.mpr hn
  simp_rw [intervalIntegral.integral_of_le zero_le_one]
  refine setIntegral_congr_fun measurableSet_Ioc fun x hx => ?_
  push_cast
  have hn' : (n : ‚ÑÇ) ‚â† 0 := Nat.cast_ne_zero.mpr hn
  have A : (n : ‚ÑÇ) ^ s = (n : ‚ÑÇ) ^ (s - 1) * n := by
    conv_lhs => rw [(by ring : s = s - 1 + 1), cpow_add _ _ hn']
    simp
  have B : ((x : ‚ÑÇ) * ‚Üën) ^ (s - 1) = (x : ‚ÑÇ) ^ (s - 1) * (n : ‚ÑÇ) ^ (s - 1) := by
    rw [‚Üê ofReal_natCast,
      mul_cpow_ofReal_nonneg hx.1.le (Nat.cast_pos.mpr (Nat.pos_of_ne_zero hn)).le]
  rw [A, B, cpow_natCast]; ring

/-- The main technical lemma for `GammaSeq_tendsto_Gamma`, expressing the integral defining the
Gamma function for `0 < re s` as the limit of a sequence of integrals over finite intervals. -/
theorem approx_Gamma_integral_tendsto_Gamma_integral {s : ‚ÑÇ} (hs : 0 < re s) :
    Tendsto (fun n : ‚Ñï => ‚à´ x : ‚Ñù in 0..n, ((1 - x / n) ^ n : ‚Ñù) * (x : ‚ÑÇ) ^ (s - 1)) atTop
      (ùìù <| Gamma s) := by
  rw [Gamma_eq_integral hs]
  -- We apply dominated convergence to the following function, which we will show is uniformly
  -- bounded above by the Gamma integrand `exp (-x) * x ^ (re s - 1)`.
  let f : ‚Ñï ‚Üí ‚Ñù ‚Üí ‚ÑÇ := fun n =>
    indicator (Ioc 0 (n : ‚Ñù)) fun x : ‚Ñù => ((1 - x / n) ^ n : ‚Ñù) * (x : ‚ÑÇ) ^ (s - 1)
  -- integrability of f
  have f_ible : ‚àÄ n : ‚Ñï, Integrable (f n) (volume.restrict (Ioi 0)) := by
    intro n
    rw [integrable_indicator_iff (measurableSet_Ioc : MeasurableSet (Ioc (_ : ‚Ñù) _)), IntegrableOn,
      Measure.restrict_restrict_of_subset Ioc_subset_Ioi_self, ‚Üê IntegrableOn, ‚Üê
      intervalIntegrable_iff_integrableOn_Ioc_of_le (by positivity : (0 : ‚Ñù) ‚â§ n)]
    apply IntervalIntegrable.continuousOn_mul
    ¬∑ refine intervalIntegral.intervalIntegrable_cpow' ?_
      rwa [sub_re, one_re, ‚Üê zero_sub, sub_lt_sub_iff_right]
    ¬∑ apply Continuous.continuousOn
      continuity
  -- pointwise limit of f
  have f_tends : ‚àÄ x : ‚Ñù, x ‚àà Ioi (0 : ‚Ñù) ‚Üí
      Tendsto (fun n : ‚Ñï => f n x) atTop (ùìù <| ‚Üë(Real.exp (-x)) * (x : ‚ÑÇ) ^ (s - 1)) := by
    intro x hx
    apply Tendsto.congr'
    ¬∑ change ‚àÄ·∂† n : ‚Ñï in atTop, ‚Üë((1 - x / n) ^ n) * (x : ‚ÑÇ) ^ (s - 1) = f n x
      filter_upwards [eventually_ge_atTop ‚åàx‚åâ‚Çä] with n hn
      rw [Nat.ceil_le] at hn
      dsimp only [f]
      rw [indicator_of_mem]
      exact ‚ü®hx, hn‚ü©
    ¬∑ simp_rw [mul_comm]
      refine (Tendsto.comp (continuous_ofReal.tendsto _) ?_).const_mul _
      convert Real.tendsto_one_add_div_pow_exp (-x) using 1
      ext1 n
      rw [neg_div, ‚Üê sub_eq_add_neg]
  -- let `convert` identify the remaining goals
  convert tendsto_integral_of_dominated_convergence _ (fun n => (f_ible n).1)
    (Real.GammaIntegral_convergent hs) _
    ((ae_restrict_iff' measurableSet_Ioi).mpr (ae_of_all _ f_tends)) using 1
  -- limit of f is the integrand we want
  ¬∑ ext1 n
    rw [MeasureTheory.integral_indicator (measurableSet_Ioc : MeasurableSet (Ioc (_ : ‚Ñù) _)),
      intervalIntegral.integral_of_le (by positivity : 0 ‚â§ (n : ‚Ñù)),
      Measure.restrict_restrict_of_subset Ioc_subset_Ioi_self]
  -- f is uniformly bounded by the Gamma integrand
  ¬∑ intro n
    rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with x hx
    simp only [mem_Ioi, f] at hx ‚ä¢
    rcases lt_or_ge (n : ‚Ñù) x with (hxn | hxn)
    ¬∑ rw [indicator_of_notMem (notMem_Ioc_of_gt hxn), norm_zero,
        mul_nonneg_iff_right_nonneg_of_pos (exp_pos _)]
      exact rpow_nonneg (le_of_lt hx) _
    ¬∑ rw [indicator_of_mem (mem_Ioc.mpr ‚ü®mem_Ioi.mp hx, hxn‚ü©), norm_mul, Complex.norm_of_nonneg
          (pow_nonneg (sub_nonneg.mpr <| div_le_one_of_le‚ÇÄ hxn <| by positivity) _),
          norm_cpow_eq_rpow_re_of_pos hx, sub_re, one_re]
      gcongr
      exact one_sub_div_pow_le_exp_neg hxn

/-- Euler's limit formula for the complex Gamma function. -/
theorem GammaSeq_tendsto_Gamma (s : ‚ÑÇ) : Tendsto (GammaSeq s) atTop (ùìù <| Gamma s) := by
  suffices ‚àÄ m : ‚Ñï, -‚Üëm < re s ‚Üí Tendsto (GammaSeq s) atTop (ùìù <| GammaAux m s) by
    rw [Gamma]
    apply this
    rw [neg_lt]
    rcases lt_or_ge 0 (re s) with (hs | hs)
    ¬∑ exact (neg_neg_of_pos hs).trans_le (Nat.cast_nonneg _)
    ¬∑ refine (Nat.lt_floor_add_one _).trans_le ?_
      rw [sub_eq_neg_add, Nat.floor_add_one (neg_nonneg.mpr hs), Nat.cast_add_one]
  intro m
  induction m generalizing s with intro hs
  | zero => -- Base case: `0 < re s`, so Gamma is given by the integral formula
    rw [Nat.cast_zero, neg_zero] at hs
    rw [‚Üê Gamma_eq_GammaAux]
    ¬∑ refine Tendsto.congr' ?_ (approx_Gamma_integral_tendsto_Gamma_integral hs)
      refine (eventually_ne_atTop 0).mp (Eventually.of_forall fun n hn => ?_)
      exact (GammaSeq_eq_approx_Gamma_integral hs hn).symm
    ¬∑ rwa [Nat.cast_zero, neg_lt_zero]
  | succ m IH => -- Induction step: use recurrence formulae in `s` for Gamma and GammaSeq
    rw [Nat.cast_succ, neg_add, ‚Üê sub_eq_add_neg, sub_lt_iff_lt_add, ‚Üê one_re, ‚Üê add_re] at hs
    rw [GammaAux]
    have := @Tendsto.congr' _ _ _ ?_ _ _
      ((eventually_ne_atTop 0).mp (Eventually.of_forall fun n hn => ?_)) ((IH _ hs).div_const s)
    pick_goal 3; ¬∑ exact GammaSeq_add_one_left s hn -- doesn't work if inlined?
    conv at this => arg 1; intro n; rw [mul_comm]
    rwa [‚Üê mul_one (GammaAux m (s + 1) / s), tendsto_mul_iff_of_ne_zero _ (one_ne_zero' ‚ÑÇ)] at this
    simp_rw [add_assoc]
    exact tendsto_natCast_div_add_atTop (1 + s)

end Complex

end LimitFormula

section GammaReflection

/-! ## The reflection formula -/


namespace Complex

theorem GammaSeq_mul (z : ‚ÑÇ) {n : ‚Ñï} (hn : n ‚â† 0) :
    GammaSeq z n * GammaSeq (1 - z) n =
      n / (n + ‚Üë1 - z) * (‚Üë1 / (z * ‚àè j ‚àà Finset.range n, (‚Üë1 - z ^ 2 / ((j : ‚ÑÇ) + 1) ^ 2))) := by
  -- also true for n = 0 but we don't need it
  have aux : ‚àÄ a b c d : ‚ÑÇ, a * b * (c * d) = a * c * (b * d) := by intros; ring
  rw [GammaSeq, GammaSeq, div_mul_div_comm, aux, ‚Üê pow_two]
  have : (n : ‚ÑÇ) ^ z * (n : ‚ÑÇ) ^ (1 - z) = n := by
    rw [‚Üê cpow_add _ _ (Nat.cast_ne_zero.mpr hn), add_sub_cancel, cpow_one]
  rw [this, Finset.prod_range_succ', Finset.prod_range_succ, aux, ‚Üê Finset.prod_mul_distrib,
    Nat.cast_zero, add_zero, add_comm (1 - z) n, ‚Üê add_sub_assoc]
  have : ‚àÄ j : ‚Ñï, (z + ‚Üë(j + 1)) * (‚Üë1 - z + ‚Üëj) =
      ((j + 1) ^ 2 :) * (‚Üë1 - z ^ 2 / ((j : ‚ÑÇ) + 1) ^ 2) := by
    intro j
    push_cast
    have : (j : ‚ÑÇ) + 1 ‚â† 0 := by rw [‚Üê Nat.cast_succ, Nat.cast_ne_zero]; exact Nat.succ_ne_zero j
    field_simp; ring
  simp_rw [this]
  rw [Finset.prod_mul_distrib, ‚Üê Nat.cast_prod, Finset.prod_pow,
    Finset.prod_range_add_one_eq_factorial, Nat.cast_pow,
    (by intros; ring : ‚àÄ a b c d : ‚ÑÇ, a * b * (c * d) = a * (d * (b * c))), ‚Üê div_div,
    mul_div_cancel_right‚ÇÄ, ‚Üê div_div, mul_comm z _, mul_one_div]
  exact pow_ne_zero 2 (Nat.cast_ne_zero.mpr <| by positivity)

/-- Euler's reflection formula for the complex Gamma function. -/
theorem Gamma_mul_Gamma_one_sub (z : ‚ÑÇ) : Gamma z * Gamma (1 - z) = œÄ / sin (œÄ * z) := by
  have pi_ne : (œÄ : ‚ÑÇ) ‚â† 0 := Complex.ofReal_ne_zero.mpr pi_ne_zero
  by_cases hs : sin (‚ÜëœÄ * z) = 0
  ¬∑ -- first deal with silly case z = integer
    rw [hs, div_zero]
    rw [‚Üê neg_eq_zero, ‚Üê Complex.sin_neg, ‚Üê mul_neg, Complex.sin_eq_zero_iff, mul_comm] at hs
    obtain ‚ü®k, hk‚ü© := hs
    rw [mul_eq_mul_right_iff, eq_false (ofReal_ne_zero.mpr pi_pos.ne'), or_false,
      neg_eq_iff_eq_neg] at hk
    rw [hk]
    cases k
    ¬∑ rw [Int.ofNat_eq_coe, Int.cast_natCast, Complex.Gamma_neg_nat_eq_zero, zero_mul]
    ¬∑ rw [Int.cast_negSucc, neg_neg, Nat.cast_add, Nat.cast_one, add_comm, sub_add_cancel_left,
        Complex.Gamma_neg_nat_eq_zero, mul_zero]
  refine tendsto_nhds_unique ((GammaSeq_tendsto_Gamma z).mul (GammaSeq_tendsto_Gamma <| 1 - z)) ?_
  have : ‚ÜëœÄ / sin (‚ÜëœÄ * z) = 1 * (œÄ / sin (œÄ * z)) := by rw [one_mul]
  convert Tendsto.congr' ((eventually_ne_atTop 0).mp (Eventually.of_forall fun n hn =>
    (GammaSeq_mul z hn).symm)) (Tendsto.mul _ _)
  ¬∑ convert tendsto_natCast_div_add_atTop (1 - z) using 1; ext1 n; rw [add_sub_assoc]
  ¬∑ have : ‚ÜëœÄ / sin (‚ÜëœÄ * z) = 1 / (sin (œÄ * z) / œÄ) := by simp
    convert tendsto_const_nhds.div _ (div_ne_zero hs pi_ne)
    rw [‚Üê tendsto_mul_iff_of_ne_zero tendsto_const_nhds pi_ne, div_mul_cancel‚ÇÄ _ pi_ne]
    convert tendsto_euler_sin_prod z using 1
    ext1 n; rw [mul_comm, ‚Üê mul_assoc]

/-- The Gamma function does not vanish on `‚ÑÇ` (except at non-positive integers, where the function
is mathematically undefined and we set it to `0` by convention). -/
theorem Gamma_ne_zero {s : ‚ÑÇ} (hs : ‚àÄ m : ‚Ñï, s ‚â† -m) : Gamma s ‚â† 0 := by
  by_cases h_im : s.im = 0
  ¬∑ have : s = ‚Üës.re := by
      conv_lhs => rw [‚Üê Complex.re_add_im s]
      rw [h_im, ofReal_zero, zero_mul, add_zero]
    rw [this, Gamma_ofReal, ofReal_ne_zero]
    refine Real.Gamma_ne_zero fun n => ?_
    specialize hs n
    contrapose! hs
    rwa [this, ‚Üê ofReal_natCast, ‚Üê ofReal_neg, ofReal_inj]
  ¬∑ have : sin (‚ÜëœÄ * s) ‚â† 0 := by
      rw [Complex.sin_ne_zero_iff]
      intro k
      apply_fun im
      rw [im_ofReal_mul, ‚Üê ofReal_intCast, ‚Üê ofReal_mul, ofReal_im]
      exact mul_ne_zero Real.pi_pos.ne' h_im
    have A := div_ne_zero (ofReal_ne_zero.mpr Real.pi_pos.ne') this
    rw [‚Üê Complex.Gamma_mul_Gamma_one_sub s, mul_ne_zero_iff] at A
    exact A.1

theorem Gamma_eq_zero_iff (s : ‚ÑÇ) : Gamma s = 0 ‚Üî ‚àÉ m : ‚Ñï, s = -m := by
  constructor
  ¬∑ contrapose!; exact Gamma_ne_zero
  ¬∑ rintro ‚ü®m, rfl‚ü©; exact Gamma_neg_nat_eq_zero m

/-- A weaker, but easier-to-apply, version of `Complex.Gamma_ne_zero`. -/
theorem Gamma_ne_zero_of_re_pos {s : ‚ÑÇ} (hs : 0 < re s) : Gamma s ‚â† 0 := by
  refine Gamma_ne_zero fun m => ?_
  contrapose! hs
  simpa only [hs, neg_re, ‚Üê ofReal_natCast, ofReal_re, neg_nonpos] using Nat.cast_nonneg _

end Complex

namespace Real

/-- The sequence with `n`-th term `n ^ s * n! / (s * (s + 1) * ... * (s + n))`, for real `s`. We
will show that this tends to `Œì(s)` as `n ‚Üí ‚àû`. -/
noncomputable def GammaSeq (s : ‚Ñù) (n : ‚Ñï) :=
  (n : ‚Ñù) ^ s * n ! / ‚àè j ‚àà Finset.range (n + 1), (s + j)

/-- Euler's limit formula for the real Gamma function. -/
theorem GammaSeq_tendsto_Gamma (s : ‚Ñù) : Tendsto (GammaSeq s) atTop (ùìù <| Gamma s) := by
  suffices Tendsto ((‚Üë) ‚àò GammaSeq s : ‚Ñï ‚Üí ‚ÑÇ) atTop (ùìù <| Complex.Gamma s) by
    exact (Complex.continuous_re.tendsto (Complex.Gamma ‚Üës)).comp this
  convert Complex.GammaSeq_tendsto_Gamma s
  ext1 n
  dsimp only [GammaSeq, Function.comp_apply, Complex.GammaSeq]
  push_cast
  rw [Complex.ofReal_cpow n.cast_nonneg, Complex.ofReal_natCast]

/-- Euler's reflection formula for the real Gamma function. -/
theorem Gamma_mul_Gamma_one_sub (s : ‚Ñù) : Gamma s * Gamma (1 - s) = œÄ / sin (œÄ * s) := by
  simp_rw [‚Üê Complex.ofReal_inj, Complex.ofReal_div, Complex.ofReal_sin, Complex.ofReal_mul, ‚Üê
    Complex.Gamma_ofReal, Complex.ofReal_sub, Complex.ofReal_one]
  exact Complex.Gamma_mul_Gamma_one_sub s

end Real

end GammaReflection

section InvGamma

open scoped Real

namespace Complex

/-! ## The reciprocal Gamma function

We show that the reciprocal Gamma function `1 / Œì(s)` is entire. These lemmas show that (in this
case at least) mathlib's conventions for division by zero do actually give a mathematically useful
answer! (These results are useful in the theory of zeta and L-functions.) -/


/-- A reformulation of the Gamma recurrence relation which is true for `s = 0` as well. -/
theorem one_div_Gamma_eq_self_mul_one_div_Gamma_add_one (s : ‚ÑÇ) :
    (Gamma s)‚Åª¬π = s * (Gamma (s + 1))‚Åª¬π := by
  rcases ne_or_eq s 0 with (h | rfl)
  ¬∑ rw [Gamma_add_one s h, mul_inv, mul_inv_cancel_left‚ÇÄ h]
  ¬∑ rw [zero_add, Gamma_zero, inv_zero, zero_mul]

/-- The reciprocal of the Gamma function is differentiable everywhere
(including the points where Gamma itself is not). -/
theorem differentiable_one_div_Gamma : Differentiable ‚ÑÇ fun s : ‚ÑÇ => (Gamma s)‚Åª¬π := fun s ‚Ü¶ by
  rcases exists_nat_gt (-s.re) with ‚ü®n, hs‚ü©
  induction n generalizing s with
  | zero =>
    rw [Nat.cast_zero, neg_lt_zero] at hs
    suffices ‚àÄ m : ‚Ñï, s ‚â† -‚Üëm from (differentiableAt_Gamma _ this).inv (Gamma_ne_zero this)
    rintro m rfl
    apply hs.not_ge
    simp
  | succ n ihn =>
    rw [funext one_div_Gamma_eq_self_mul_one_div_Gamma_add_one]
    specialize ihn (s + 1) (by rwa [add_re, one_re, neg_add', sub_lt_iff_lt_add, ‚Üê Nat.cast_succ])
    exact differentiableAt_id.mul (ihn.comp s (f := fun s => s + 1) <|
      differentiableAt_id.add_const (1 : ‚ÑÇ))

lemma betaIntegral_eq_Gamma_mul_div (u v : ‚ÑÇ) (hu : 0 < u.re) (hv : 0 < v.re) :
    betaIntegral u v = Gamma u * Gamma v / Gamma (u + v) := by
  rw [Gamma_mul_Gamma_eq_betaIntegral hu hv,
      mul_div_cancel_left‚ÇÄ _ (Gamma_ne_zero_of_re_pos (add_pos hu hv))]

end Complex

end InvGamma

section Doubling

/-!
## The doubling formula for Gamma

We prove the doubling formula for arbitrary real or complex arguments, by analytic continuation from
the positive real case. (Knowing that `Œì‚Åª¬π` is analytic everywhere makes this much simpler, since we
do not have to do any special-case handling for the poles of `Œì`.)
-/


namespace Complex

theorem Gamma_mul_Gamma_add_half (s : ‚ÑÇ) :
    Gamma s * Gamma (s + 1 / 2) = Gamma (2 * s) * (2 : ‚ÑÇ) ^ (1 - 2 * s) * ‚Üë(‚àöœÄ) := by
  suffices (fun z => (Gamma z)‚Åª¬π * (Gamma (z + 1 / 2))‚Åª¬π) = fun z =>
      (Gamma (2 * z))‚Åª¬π * (2 : ‚ÑÇ) ^ (2 * z - 1) / ‚Üë(‚àöœÄ) by
    convert congr_arg Inv.inv (congr_fun this s) using 1
    ¬∑ rw [mul_inv, inv_inv, inv_inv]
    ¬∑ rw [div_eq_mul_inv, mul_inv, mul_inv, inv_inv, inv_inv, ‚Üê cpow_neg, neg_sub]
  have h1 : AnalyticOnNhd ‚ÑÇ (fun z : ‚ÑÇ => (Gamma z)‚Åª¬π * (Gamma (z + 1 / 2))‚Åª¬π) univ := by
    refine DifferentiableOn.analyticOnNhd ?_ isOpen_univ
    refine (differentiable_one_div_Gamma.mul ?_).differentiableOn
    exact differentiable_one_div_Gamma.comp (differentiable_id.add (differentiable_const _))
  have h2 : AnalyticOnNhd ‚ÑÇ
      (fun z => (Gamma (2 * z))‚Åª¬π * (2 : ‚ÑÇ) ^ (2 * z - 1) / ‚Üë(‚àöœÄ)) univ := by
    refine DifferentiableOn.analyticOnNhd ?_ isOpen_univ
    refine (Differentiable.mul ?_ (differentiable_const _)).differentiableOn
    apply Differentiable.mul
    ¬∑ exact differentiable_one_div_Gamma.comp (differentiable_id.const_mul _)
    ¬∑ refine fun t => DifferentiableAt.const_cpow ?_ (Or.inl two_ne_zero)
      exact DifferentiableAt.sub_const (differentiableAt_id.const_mul _) _
  have h3 : Tendsto ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ) (ùìù[‚â†] 1) (ùìù[‚â†] 1) := by
    rw [tendsto_nhdsWithin_iff]; constructor
    ¬∑ exact tendsto_nhdsWithin_of_tendsto_nhds continuous_ofReal.continuousAt
    ¬∑ exact eventually_nhdsWithin_iff.mpr (Eventually.of_forall fun t ht => ofReal_ne_one.mpr ht)
  refine AnalyticOnNhd.eq_of_frequently_eq h1 h2 (h3.frequently ?_)
  refine ((Eventually.filter_mono nhdsWithin_le_nhds) ?_).frequently
  refine (eventually_gt_nhds zero_lt_one).mp (Eventually.of_forall fun t ht => ?_)
  rw [‚Üê mul_inv, Gamma_ofReal, (by simp : (t : ‚ÑÇ) + 1 / 2 = ‚Üë(t + 1 / 2)), Gamma_ofReal, ‚Üê
    ofReal_mul, Gamma_mul_Gamma_add_half_of_pos ht, ofReal_mul, ofReal_mul, ‚Üê Gamma_ofReal,
    mul_inv, mul_inv, (by simp : 2 * (t : ‚ÑÇ) = ‚Üë(2 * t)), Gamma_ofReal,
    ofReal_cpow zero_le_two, show (2 : ‚Ñù) = (2 : ‚ÑÇ) by norm_cast, ‚Üê cpow_neg, ofReal_sub,
    ofReal_one, neg_sub, ‚Üê div_eq_mul_inv]

end Complex

namespace Real

open Complex

theorem Gamma_mul_Gamma_add_half (s : ‚Ñù) :
    Gamma s * Gamma (s + 1 / 2) = Gamma (2 * s) * (2 : ‚Ñù) ^ (1 - 2 * s) * ‚àöœÄ := by
  rw [‚Üê ofReal_inj]
  simpa only [‚Üê Gamma_ofReal, ofReal_cpow zero_le_two, ofReal_mul, ofReal_add, ofReal_div,
    ofReal_sub] using Complex.Gamma_mul_Gamma_add_half ‚Üës

end Real

end Doubling
