/-
Copyright (c) 2022 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.MeasureTheory.Integral.ExpDecay
import Mathlib.Analysis.MellinTransform

#align_import analysis.special_functions.gamma.basic from "leanprover-community/mathlib"@"cca40788df1b8755d5baf17ab2f27dacc2e17acb"

/-!
# The Gamma function

This file defines the `Œì` function (of a real or complex variable `s`). We define this by Euler's
integral `Œì(s) = ‚à´ x in Ioi 0, exp (-x) * x ^ (s - 1)` in the range where this integral converges
(i.e., for `0 < s` in the real case, and `0 < re s` in the complex case).

We show that this integral satisfies `Œì(1) = 1` and `Œì(s + 1) = s * Œì(s)`; hence we can define
`Œì(s)` for all `s` as the unique function satisfying this recurrence and agreeing with Euler's
integral in the convergence range. (If `s = -n` for `n ‚àà ‚Ñï`, then the function is undefined, and we
set it to be `0` by convention.)

## Gamma function: main statements (complex case)

* `Complex.Gamma`: the `Œì` function (of a complex variable).
* `Complex.Gamma_eq_integral`: for `0 < re s`, `Œì(s)` agrees with Euler's integral.
* `Complex.Gamma_add_one`: for all `s : ‚ÑÇ` with `s ‚â† 0`, we have `Œì (s + 1) = s Œì(s)`.
* `Complex.Gamma_nat_eq_factorial`: for all `n : ‚Ñï` we have `Œì (n + 1) = n!`.
* `Complex.differentiableAt_Gamma`: `Œì` is complex-differentiable at all `s : ‚ÑÇ` with
  `s ‚àâ {-n : n ‚àà ‚Ñï}`.

## Gamma function: main statements (real case)

* `Real.Gamma`: the `Œì` function (of a real variable).
* Real counterparts of all the properties of the complex Gamma function listed above:
  `Real.Gamma_eq_integral`, `Real.Gamma_add_one`, `Real.Gamma_nat_eq_factorial`,
  `Real.differentiableAt_Gamma`.

## Tags

Gamma
-/


noncomputable section

set_option linter.uppercaseLean3 false

open Filter intervalIntegral Set Real MeasureTheory Asymptotics

open scoped Nat Topology ComplexConjugate

namespace Real

/-- Asymptotic bound for the `Œì` function integrand. -/
theorem Gamma_integrand_isLittleO (s : ‚Ñù) :
    (fun x : ‚Ñù => exp (-x) * x ^ s) =o[atTop] fun x : ‚Ñù => exp (-(1 / 2) * x) := by
  refine isLittleO_of_tendsto (fun x hx => ?_) ?_
  ¬∑ exfalso; exact (exp_pos (-(1 / 2) * x)).ne' hx
  have : (fun x : ‚Ñù => exp (-x) * x ^ s / exp (-(1 / 2) * x)) =
      (fun x : ‚Ñù => exp (1 / 2 * x) / x ^ s)‚Åª¬π := by
    ext1 x
    field_simp [exp_ne_zero, exp_neg, ‚Üê Real.exp_add]
    left
    ring
  rw [this]
  exact (tendsto_exp_mul_div_rpow_atTop s (1 / 2) one_half_pos).inv_tendsto_atTop
#align real.Gamma_integrand_is_o Real.Gamma_integrand_isLittleO

/-- The Euler integral for the `Œì` function converges for positive real `s`. -/
theorem GammaIntegral_convergent {s : ‚Ñù} (h : 0 < s) :
    IntegrableOn (fun x : ‚Ñù => exp (-x) * x ^ (s - 1)) (Ioi 0) := by
  rw [‚Üê Ioc_union_Ioi_eq_Ioi (@zero_le_one ‚Ñù _ _ _ _), integrableOn_union]
  constructor
  ¬∑ rw [‚Üê integrableOn_Icc_iff_integrableOn_Ioc]
    refine IntegrableOn.continuousOn_mul continuousOn_id.neg.rexp ?_ isCompact_Icc
    refine (intervalIntegrable_iff_integrableOn_Icc_of_le zero_le_one).mp ?_
    exact intervalIntegrable_rpow' (by linarith)
  ¬∑ refine integrable_of_isBigO_exp_neg one_half_pos ?_ (Gamma_integrand_isLittleO _).isBigO
    refine continuousOn_id.neg.rexp.mul (continuousOn_id.rpow_const ?_)
    intro x hx
    exact Or.inl ((zero_lt_one : (0 : ‚Ñù) < 1).trans_le hx).ne'
#align real.Gamma_integral_convergent Real.GammaIntegral_convergent

end Real

namespace Complex

/- Technical note: In defining the Gamma integrand exp (-x) * x ^ (s - 1) for s complex, we have to
make a choice between ‚Üë(Real.exp (-x)), Complex.exp (‚Üë(-x)), and Complex.exp (-‚Üëx), all of which are
equal but not definitionally so. We use the first of these throughout. -/
/-- The integral defining the `Œì` function converges for complex `s` with `0 < re s`.

This is proved by reduction to the real case. -/
theorem GammaIntegral_convergent {s : ‚ÑÇ} (hs : 0 < s.re) :
    IntegrableOn (fun x => (-x).exp * x ^ (s - 1) : ‚Ñù ‚Üí ‚ÑÇ) (Ioi 0) := by
  constructor
  ¬∑ refine ContinuousOn.aestronglyMeasurable ?_ measurableSet_Ioi
    apply (continuous_ofReal.comp continuous_neg.rexp).continuousOn.mul
    apply ContinuousAt.continuousOn
    intro x hx
    have : ContinuousAt (fun x : ‚ÑÇ => x ^ (s - 1)) ‚Üëx :=
      continuousAt_cpow_const <| ofReal_mem_slitPlane.2 hx
    exact ContinuousAt.comp this continuous_ofReal.continuousAt
  ¬∑ rw [‚Üê hasFiniteIntegral_norm_iff]
    refine HasFiniteIntegral.congr (Real.GammaIntegral_convergent hs).2 ?_
    apply (ae_restrict_iff' measurableSet_Ioi).mpr
    filter_upwards with x hx
    rw [norm_eq_abs, map_mul, abs_of_nonneg <| le_of_lt <| exp_pos <| -x,
      abs_cpow_eq_rpow_re_of_pos hx _]
    simp
#align complex.Gamma_integral_convergent Complex.GammaIntegral_convergent

/-- Euler's integral for the `Œì` function (of a complex variable `s`), defined as
`‚à´ x in Ioi 0, exp (-x) * x ^ (s - 1)`.

See `Complex.GammaIntegral_convergent` for a proof of the convergence of the integral for
`0 < re s`. -/
def GammaIntegral (s : ‚ÑÇ) : ‚ÑÇ :=
  ‚à´ x in Ioi (0 : ‚Ñù), ‚Üë(-x).exp * ‚Üëx ^ (s - 1)
#align complex.Gamma_integral Complex.GammaIntegral

theorem GammaIntegral_conj (s : ‚ÑÇ) : GammaIntegral (conj s) = conj (GammaIntegral s) := by
  rw [GammaIntegral, GammaIntegral, ‚Üê integral_conj]
  refine setIntegral_congr measurableSet_Ioi fun x hx => ?_
  dsimp only
  rw [RingHom.map_mul, conj_ofReal, cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)),
    cpow_def_of_ne_zero (ofReal_ne_zero.mpr (ne_of_gt hx)), ‚Üê exp_conj, RingHom.map_mul, ‚Üê
    ofReal_log (le_of_lt hx), conj_ofReal, RingHom.map_sub, RingHom.map_one]
#align complex.Gamma_integral_conj Complex.GammaIntegral_conj

theorem GammaIntegral_ofReal (s : ‚Ñù) :
    GammaIntegral ‚Üës = ‚Üë(‚à´ x : ‚Ñù in Ioi 0, Real.exp (-x) * x ^ (s - 1)) := by
  have : ‚àÄ r : ‚Ñù, Complex.ofReal' r = @RCLike.ofReal ‚ÑÇ _ r := fun r => rfl
  rw [GammaIntegral]
  conv_rhs => rw [this, ‚Üê _root_.integral_ofReal]
  refine setIntegral_congr measurableSet_Ioi ?_
  intro x hx; dsimp only
  conv_rhs => rw [‚Üê this]
  rw [ofReal_mul, ofReal_cpow (mem_Ioi.mp hx).le]
  simp
#align complex.Gamma_integral_of_real Complex.GammaIntegral_ofReal

@[simp]
theorem GammaIntegral_one : GammaIntegral 1 = 1 := by
  simpa only [‚Üê ofReal_one, GammaIntegral_ofReal, ofReal_inj, sub_self, rpow_zero,
    mul_one] using integral_exp_neg_Ioi_zero
#align complex.Gamma_integral_one Complex.GammaIntegral_one

end Complex

/-! Now we establish the recurrence relation `Œì(s + 1) = s * Œì(s)` using integration by parts. -/


namespace Complex

section GammaRecurrence

/-- The indefinite version of the `Œì` function, `Œì(s, X) = ‚à´ x ‚àà 0..X, exp(-x) x ^ (s - 1)`. -/
def partialGamma (s : ‚ÑÇ) (X : ‚Ñù) : ‚ÑÇ :=
  ‚à´ x in (0)..X, (-x).exp * x ^ (s - 1)
#align complex.partial_Gamma Complex.partialGamma

theorem tendsto_partialGamma {s : ‚ÑÇ} (hs : 0 < s.re) :
    Tendsto (fun X : ‚Ñù => partialGamma s X) atTop (ùìù <| GammaIntegral s) :=
  intervalIntegral_tendsto_integral_Ioi 0 (GammaIntegral_convergent hs) tendsto_id
#align complex.tendsto_partial_Gamma Complex.tendsto_partialGamma

private theorem Gamma_integrand_interval_integrable (s : ‚ÑÇ) {X : ‚Ñù} (hs : 0 < s.re) (hX : 0 ‚â§ X) :
    IntervalIntegrable (fun x => (-x).exp * x ^ (s - 1) : ‚Ñù ‚Üí ‚ÑÇ) volume 0 X := by
  rw [intervalIntegrable_iff_integrableOn_Ioc_of_le hX]
  exact IntegrableOn.mono_set (GammaIntegral_convergent hs) Ioc_subset_Ioi_self

private theorem Gamma_integrand_deriv_integrable_A {s : ‚ÑÇ} (hs : 0 < s.re) {X : ‚Ñù} (hX : 0 ‚â§ X) :
    IntervalIntegrable (fun x => -((-x).exp * x ^ s) : ‚Ñù ‚Üí ‚ÑÇ) volume 0 X := by
  convert (Gamma_integrand_interval_integrable (s + 1) _ hX).neg
  ¬∑ simp only [ofReal_exp, ofReal_neg, add_sub_cancel_right]; rfl
  ¬∑ simp only [add_re, one_re]; linarith

private theorem Gamma_integrand_deriv_integrable_B {s : ‚ÑÇ} (hs : 0 < s.re) {Y : ‚Ñù} (hY : 0 ‚â§ Y) :
    IntervalIntegrable (fun x : ‚Ñù => (-x).exp * (s * x ^ (s - 1)) : ‚Ñù ‚Üí ‚ÑÇ) volume 0 Y := by
  have : (fun x => (-x).exp * (s * x ^ (s - 1)) : ‚Ñù ‚Üí ‚ÑÇ) =
      (fun x => s * ((-x).exp * x ^ (s - 1)) : ‚Ñù ‚Üí ‚ÑÇ) := by ext1; ring
  rw [this, intervalIntegrable_iff_integrableOn_Ioc_of_le hY]
  constructor
  ¬∑ refine (continuousOn_const.mul ?_).aestronglyMeasurable measurableSet_Ioc
    apply (continuous_ofReal.comp continuous_neg.rexp).continuousOn.mul
    apply ContinuousAt.continuousOn
    intro x hx
    refine (?_ : ContinuousAt (fun x : ‚ÑÇ => x ^ (s - 1)) _).comp continuous_ofReal.continuousAt
    exact continuousAt_cpow_const <| ofReal_mem_slitPlane.2 hx.1
  rw [‚Üê hasFiniteIntegral_norm_iff]
  simp_rw [norm_eq_abs, map_mul]
  refine (((Real.GammaIntegral_convergent hs).mono_set
    Ioc_subset_Ioi_self).hasFiniteIntegral.congr ?_).const_mul _
  rw [EventuallyEq, ae_restrict_iff']
  ¬∑ filter_upwards with x hx
    rw [abs_of_nonneg (exp_pos _).le, abs_cpow_eq_rpow_re_of_pos hx.1]
    simp
  ¬∑ exact measurableSet_Ioc

/-- The recurrence relation for the indefinite version of the `Œì` function. -/
theorem partialGamma_add_one {s : ‚ÑÇ} (hs : 0 < s.re) {X : ‚Ñù} (hX : 0 ‚â§ X) :
    partialGamma (s + 1) X = s * partialGamma s X - (-X).exp * X ^ s := by
  rw [partialGamma, partialGamma, add_sub_cancel_right]
  have F_der_I : ‚àÄ x : ‚Ñù, x ‚àà Ioo 0 X ‚Üí HasDerivAt (fun x => (-x).exp * x ^ s : ‚Ñù ‚Üí ‚ÑÇ)
      (-((-x).exp * x ^ s) + (-x).exp * (s * x ^ (s - 1))) x := by
    intro x hx
    have d1 : HasDerivAt (fun y : ‚Ñù => (-y).exp) (-(-x).exp) x := by
      simpa using (hasDerivAt_neg x).exp
    have d2 : HasDerivAt (fun y : ‚Ñù => (y : ‚ÑÇ) ^ s) (s * x ^ (s - 1)) x := by
      have t := @HasDerivAt.cpow_const _ _ _ s (hasDerivAt_id ‚Üëx) ?_
      ¬∑ simpa only [mul_one] using t.comp_ofReal
      ¬∑ exact ofReal_mem_slitPlane.2 hx.1
    simpa only [ofReal_neg, neg_mul] using d1.ofReal_comp.mul d2
  have cont := (continuous_ofReal.comp continuous_neg.rexp).mul (continuous_ofReal_cpow_const hs)
  have der_ible :=
    (Gamma_integrand_deriv_integrable_A hs hX).add (Gamma_integrand_deriv_integrable_B hs hX)
  have int_eval := integral_eq_sub_of_hasDerivAt_of_le hX cont.continuousOn F_der_I der_ible
  -- We are basically done here but manipulating the output into the right form is fiddly.
  apply_fun fun x : ‚ÑÇ => -x at int_eval
  rw [intervalIntegral.integral_add (Gamma_integrand_deriv_integrable_A hs hX)
      (Gamma_integrand_deriv_integrable_B hs hX),
    intervalIntegral.integral_neg, neg_add, neg_neg] at int_eval
  rw [eq_sub_of_add_eq int_eval, sub_neg_eq_add, neg_sub, add_comm, add_sub]
  have : (fun x => (-x).exp * (s * x ^ (s - 1)) : ‚Ñù ‚Üí ‚ÑÇ) =
      (fun x => s * (-x).exp * x ^ (s - 1) : ‚Ñù ‚Üí ‚ÑÇ) := by ext1; ring
  rw [this]
  have t := @integral_const_mul 0 X volume _ _ s fun x : ‚Ñù => (-x).exp * x ^ (s - 1)
  rw [‚Üê t, ofReal_zero, zero_cpow]
  ¬∑ rw [mul_zero, add_zero]; congr 2; ext1; ring
  ¬∑ contrapose! hs; rw [hs, zero_re]
#align complex.partial_Gamma_add_one Complex.partialGamma_add_one

/-- The recurrence relation for the `Œì` integral. -/
theorem GammaIntegral_add_one {s : ‚ÑÇ} (hs : 0 < s.re) :
    GammaIntegral (s + 1) = s * GammaIntegral s := by
  suffices Tendsto (s + 1).partialGamma atTop (ùìù <| s * GammaIntegral s) by
    refine tendsto_nhds_unique ?_ this
    apply tendsto_partialGamma; rw [add_re, one_re]; linarith
  have : (fun X : ‚Ñù => s * partialGamma s X - X ^ s * (-X).exp) =·∂†[atTop]
      (s + 1).partialGamma := by
    apply eventuallyEq_of_mem (Ici_mem_atTop (0 : ‚Ñù))
    intro X hX
    rw [partialGamma_add_one hs (mem_Ici.mp hX)]
    ring_nf
  refine Tendsto.congr' this ?_
  suffices Tendsto (fun X => -X ^ s * (-X).exp : ‚Ñù ‚Üí ‚ÑÇ) atTop (ùìù 0) by
    simpa using Tendsto.add (Tendsto.const_mul s (tendsto_partialGamma hs)) this
  rw [tendsto_zero_iff_norm_tendsto_zero]
  have :
      (fun e : ‚Ñù => ‚Äñ-(e : ‚ÑÇ) ^ s * (-e).exp‚Äñ) =·∂†[atTop] fun e : ‚Ñù => e ^ s.re * (-1 * e).exp := by
    refine eventuallyEq_of_mem (Ioi_mem_atTop 0) ?_
    intro x hx; dsimp only
    rw [norm_eq_abs, map_mul, abs.map_neg, abs_cpow_eq_rpow_re_of_pos hx,
      abs_of_nonneg (exp_pos (-x)).le, neg_mul, one_mul]
  exact (tendsto_congr' this).mpr (tendsto_rpow_mul_exp_neg_mul_atTop_nhds_zero _ _ zero_lt_one)
#align complex.Gamma_integral_add_one Complex.GammaIntegral_add_one

end GammaRecurrence

/-! Now we define `Œì(s)` on the whole complex plane, by recursion. -/


section GammaDef

/-- The `n`th function in this family is `Œì(s)` if `-n < s.re`, and junk otherwise. -/
noncomputable def GammaAux : ‚Ñï ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ
  | 0 => GammaIntegral
  | n + 1 => fun s : ‚ÑÇ => GammaAux n (s + 1) / s
#align complex.Gamma_aux Complex.GammaAux

theorem GammaAux_recurrence1 (s : ‚ÑÇ) (n : ‚Ñï) (h1 : -s.re < ‚Üën) :
    GammaAux n s = GammaAux n (s + 1) / s := by
  induction' n with n hn generalizing s
  ¬∑ simp only [Nat.zero_eq, CharP.cast_eq_zero, Left.neg_neg_iff] at h1
    dsimp only [GammaAux]; rw [GammaIntegral_add_one h1]
    rw [mul_comm, mul_div_cancel_right‚ÇÄ]; contrapose! h1; rw [h1]
    simp
  ¬∑ dsimp only [GammaAux]
    have hh1 : -(s + 1).re < n := by
      rw [Nat.cast_add, Nat.cast_one] at h1
      rw [add_re, one_re]; linarith
    rw [‚Üê hn (s + 1) hh1]
#align complex.Gamma_aux_recurrence1 Complex.GammaAux_recurrence1

theorem GammaAux_recurrence2 (s : ‚ÑÇ) (n : ‚Ñï) (h1 : -s.re < ‚Üën) :
    GammaAux n s = GammaAux (n + 1) s := by
  cases' n with n n
  ¬∑ simp only [Nat.zero_eq, CharP.cast_eq_zero, Left.neg_neg_iff] at h1
    dsimp only [GammaAux]
    rw [GammaIntegral_add_one h1, mul_div_cancel_left‚ÇÄ]
    rintro rfl
    rw [zero_re] at h1
    exact h1.false
  ¬∑ dsimp only [GammaAux]
    have : GammaAux n (s + 1 + 1) / (s + 1) = GammaAux n (s + 1) := by
      have hh1 : -(s + 1).re < n := by
        rw [Nat.cast_add, Nat.cast_one] at h1
        rw [add_re, one_re]; linarith
      rw [GammaAux_recurrence1 (s + 1) n hh1]
    rw [this]
#align complex.Gamma_aux_recurrence2 Complex.GammaAux_recurrence2

/-- The `Œì` function (of a complex variable `s`). -/
-- @[pp_nodot] -- Porting note: removed
irreducible_def Gamma (s : ‚ÑÇ) : ‚ÑÇ :=
  GammaAux ‚åä1 - s.re‚åã‚Çä s
#align complex.Gamma Complex.Gamma

theorem Gamma_eq_GammaAux (s : ‚ÑÇ) (n : ‚Ñï) (h1 : -s.re < ‚Üën) : Gamma s = GammaAux n s := by
  have u : ‚àÄ k : ‚Ñï, GammaAux (‚åä1 - s.re‚åã‚Çä + k) s = Gamma s := by
    intro k; induction' k with k hk
    ¬∑ simp [Gamma]
    ¬∑ rw [‚Üê hk, ‚Üê add_assoc]
      refine (GammaAux_recurrence2 s (‚åä1 - s.re‚åã‚Çä + k) ?_).symm
      rw [Nat.cast_add]
      have i0 := Nat.sub_one_lt_floor (1 - s.re)
      simp only [sub_sub_cancel_left] at i0
      refine lt_add_of_lt_of_nonneg i0 ?_
      rw [‚Üê Nat.cast_zero, Nat.cast_le]; exact Nat.zero_le k
  convert (u <| n - ‚åä1 - s.re‚åã‚Çä).symm; rw [Nat.add_sub_of_le]
  by_cases h : 0 ‚â§ 1 - s.re
  ¬∑ apply Nat.le_of_lt_succ
    exact_mod_cast lt_of_le_of_lt (Nat.floor_le h) (by linarith : 1 - s.re < n + 1)
  ¬∑ rw [Nat.floor_of_nonpos]
    ¬∑ omega
    ¬∑ linarith
#align complex.Gamma_eq_Gamma_aux Complex.Gamma_eq_GammaAux

/-- The recurrence relation for the `Œì` function. -/
theorem Gamma_add_one (s : ‚ÑÇ) (h2 : s ‚â† 0) : Gamma (s + 1) = s * Gamma s := by
  let n := ‚åä1 - s.re‚åã‚Çä
  have t1 : -s.re < n := by simpa only [sub_sub_cancel_left] using Nat.sub_one_lt_floor (1 - s.re)
  have t2 : -(s + 1).re < n := by rw [add_re, one_re]; linarith
  rw [Gamma_eq_GammaAux s n t1, Gamma_eq_GammaAux (s + 1) n t2, GammaAux_recurrence1 s n t1]
  field_simp
#align complex.Gamma_add_one Complex.Gamma_add_one

theorem Gamma_eq_integral {s : ‚ÑÇ} (hs : 0 < s.re) : Gamma s = GammaIntegral s :=
  Gamma_eq_GammaAux s 0 (by norm_cast; linarith)
#align complex.Gamma_eq_integral Complex.Gamma_eq_integral

@[simp]
theorem Gamma_one : Gamma 1 = 1 := by rw [Gamma_eq_integral] <;> simp
#align complex.Gamma_one Complex.Gamma_one

theorem Gamma_nat_eq_factorial (n : ‚Ñï) : Gamma (n + 1) = n ! := by
  induction' n with n hn
  ¬∑ simp
  ¬∑ rw [Gamma_add_one n.succ <| Nat.cast_ne_zero.mpr <| Nat.succ_ne_zero n]
    simp only [Nat.cast_succ, Nat.factorial_succ, Nat.cast_mul]; congr
#align complex.Gamma_nat_eq_factorial Complex.Gamma_nat_eq_factorial

@[simp]
theorem Gamma_ofNat_eq_factorial (n : ‚Ñï) [(n + 1).AtLeastTwo] :
    Gamma (no_index (OfNat.ofNat (n + 1) : ‚ÑÇ)) = n ! :=
  mod_cast Gamma_nat_eq_factorial (n : ‚Ñï)

/-- At `0` the Gamma function is undefined; by convention we assign it the value `0`. -/
@[simp]
theorem Gamma_zero : Gamma 0 = 0 := by
  simp_rw [Gamma, zero_re, sub_zero, Nat.floor_one, GammaAux, div_zero]
#align complex.Gamma_zero Complex.Gamma_zero

/-- At `-n` for `n ‚àà ‚Ñï`, the Gamma function is undefined; by convention we assign it the value 0. -/
theorem Gamma_neg_nat_eq_zero (n : ‚Ñï) : Gamma (-n) = 0 := by
  induction' n with n IH
  ¬∑ rw [Nat.cast_zero, neg_zero, Gamma_zero]
  ¬∑ have A : -(n.succ : ‚ÑÇ) ‚â† 0 := by
      rw [neg_ne_zero, Nat.cast_ne_zero]
      apply Nat.succ_ne_zero
    have : -(n : ‚ÑÇ) = -‚Üën.succ + 1 := by simp
    rw [this, Gamma_add_one _ A] at IH
    contrapose! IH
    exact mul_ne_zero A IH
#align complex.Gamma_neg_nat_eq_zero Complex.Gamma_neg_nat_eq_zero

theorem Gamma_conj (s : ‚ÑÇ) : Gamma (conj s) = conj (Gamma s) := by
  suffices ‚àÄ (n : ‚Ñï) (s : ‚ÑÇ), GammaAux n (conj s) = conj (GammaAux n s) by
    simp [Gamma, this]
  intro n
  induction' n with n IH
  ¬∑ rw [GammaAux]; exact GammaIntegral_conj
  ¬∑ intro s
    rw [GammaAux]
    dsimp only
    rw [div_eq_mul_inv _ s, RingHom.map_mul, conj_inv, ‚Üê div_eq_mul_inv]
    suffices conj s + 1 = conj (s + 1) by rw [this, IH]
    rw [RingHom.map_add, RingHom.map_one]
#align complex.Gamma_conj Complex.Gamma_conj

/-- Expresses the integral over `Ioi 0` of `t ^ (a - 1) * exp (-(r * t))` in terms of the Gamma
function, for complex `a`. -/
lemma integral_cpow_mul_exp_neg_mul_Ioi {a : ‚ÑÇ} {r : ‚Ñù} (ha : 0 < a.re) (hr : 0 < r) :
    ‚à´ (t : ‚Ñù) in Ioi 0, t ^ (a - 1) * exp (-(r * t)) = (1 / r) ^ a * Gamma a := by
  have aux : (1 / r : ‚ÑÇ) ^ a = 1 / r * (1 / r) ^ (a - 1) := by
    nth_rewrite 2 [‚Üê cpow_one (1 / r : ‚ÑÇ)]
    rw [‚Üê cpow_add _ _ (one_div_ne_zero <| ofReal_ne_zero.mpr hr.ne'), add_sub_cancel]
  calc
    _ = ‚à´ (t : ‚Ñù) in Ioi 0, (1 / r) ^ (a - 1) * (r * t) ^ (a - 1) * exp (-(r * t)) := by
      refine MeasureTheory.setIntegral_congr measurableSet_Ioi (fun x hx ‚Ü¶ ?_)
      rw [mem_Ioi] at hx
      rw [mul_cpow_ofReal_nonneg hr.le hx.le, ‚Üê mul_assoc, one_div, ‚Üê ofReal_inv,
        ‚Üê mul_cpow_ofReal_nonneg (inv_pos.mpr hr).le hr.le, ‚Üê ofReal_mul r‚Åª¬π, inv_mul_cancel hr.ne',
        ofReal_one, one_cpow, one_mul]
    _ = 1 / r * ‚à´ (t : ‚Ñù) in Ioi 0, (1 / r) ^ (a - 1) * t ^ (a - 1) * exp (-t) := by
      simp_rw [‚Üê ofReal_mul]
      rw [integral_comp_mul_left_Ioi (fun x ‚Ü¶ _ * x ^ (a - 1) * exp (-x)) _ hr, mul_zero,
        real_smul, ‚Üê one_div, ofReal_div, ofReal_one]
    _ = 1 / r * (1 / r : ‚ÑÇ) ^ (a - 1) * (‚à´ (t : ‚Ñù) in Ioi 0, t ^ (a - 1) * exp (-t)) := by
      simp_rw [‚Üê integral_mul_left, mul_assoc]
    _ = (1 / r) ^ a * Gamma a := by
      rw [aux, Gamma_eq_integral ha]
      congr 2 with x
      rw [ofReal_exp, ofReal_neg, mul_comm]

end GammaDef

/-! Now check that the `Œì` function is differentiable, wherever this makes sense. -/


section GammaHasDeriv

/-- Rewrite the Gamma integral as an example of a Mellin transform. -/
theorem GammaIntegral_eq_mellin : GammaIntegral = mellin fun x => ‚Üë(Real.exp (-x)) :=
  funext fun s => by simp only [mellin, GammaIntegral, smul_eq_mul, mul_comm]
#align complex.Gamma_integral_eq_mellin Complex.GammaIntegral_eq_mellin

/-- The derivative of the `Œì` integral, at any `s ‚àà ‚ÑÇ` with `1 < re s`, is given by the Mellin
transform of `log t * exp (-t)`. -/
theorem hasDerivAt_GammaIntegral {s : ‚ÑÇ} (hs : 0 < s.re) :
    HasDerivAt GammaIntegral (‚à´ t : ‚Ñù in Ioi 0, t ^ (s - 1) * (Real.log t * Real.exp (-t))) s := by
  rw [GammaIntegral_eq_mellin]
  convert (mellin_hasDerivAt_of_isBigO_rpow (E := ‚ÑÇ) _ _ (lt_add_one _) _ hs).2
  ¬∑ refine (Continuous.continuousOn ?_).locallyIntegrableOn measurableSet_Ioi
    exact continuous_ofReal.comp (Real.continuous_exp.comp continuous_neg)
  ¬∑ rw [‚Üê isBigO_norm_left]
    simp_rw [Complex.norm_eq_abs, abs_ofReal, ‚Üê Real.norm_eq_abs, isBigO_norm_left]
    simpa only [neg_one_mul] using (isLittleO_exp_neg_mul_rpow_atTop zero_lt_one _).isBigO
  ¬∑ simp_rw [neg_zero, rpow_zero]
    refine isBigO_const_of_tendsto (?_ : Tendsto _ _ (ùìù (1 : ‚ÑÇ))) one_ne_zero
    rw [(by simp : (1 : ‚ÑÇ) = Real.exp (-0))]
    exact (continuous_ofReal.comp (Real.continuous_exp.comp continuous_neg)).continuousWithinAt
#align complex.has_deriv_at_Gamma_integral Complex.hasDerivAt_GammaIntegral

theorem differentiableAt_GammaAux (s : ‚ÑÇ) (n : ‚Ñï) (h1 : 1 - s.re < n) (h2 : ‚àÄ m : ‚Ñï, s ‚â† -m) :
    DifferentiableAt ‚ÑÇ (GammaAux n) s := by
  induction' n with n hn generalizing s
  ¬∑ refine (hasDerivAt_GammaIntegral ?_).differentiableAt
    rw [Nat.cast_zero] at h1; linarith
  ¬∑ dsimp only [GammaAux]
    specialize hn (s + 1)
    have a : 1 - (s + 1).re < ‚Üën := by
      rw [Nat.cast_succ] at h1; rw [Complex.add_re, Complex.one_re]; linarith
    have b : ‚àÄ m : ‚Ñï, s + 1 ‚â† -m := by
      intro m; have := h2 (1 + m)
      contrapose! this
      rw [‚Üê eq_sub_iff_add_eq] at this
      simpa using this
    refine DifferentiableAt.div (DifferentiableAt.comp _ (hn a b) ?_) ?_ ?_
    ¬∑ rw [differentiableAt_add_const_iff (1 : ‚ÑÇ)]; exact differentiableAt_id
    ¬∑ exact differentiableAt_id
    ¬∑ simpa using h2 0
#align complex.differentiable_at_Gamma_aux Complex.differentiableAt_GammaAux

theorem differentiableAt_Gamma (s : ‚ÑÇ) (hs : ‚àÄ m : ‚Ñï, s ‚â† -m) : DifferentiableAt ‚ÑÇ Gamma s := by
  let n := ‚åä1 - s.re‚åã‚Çä + 1
  have hn : 1 - s.re < n := mod_cast Nat.lt_floor_add_one (1 - s.re)
  apply (differentiableAt_GammaAux s n hn hs).congr_of_eventuallyEq
  let S := {t : ‚ÑÇ | 1 - t.re < n}
  have : S ‚àà ùìù s := by
    rw [mem_nhds_iff]; use S
    refine ‚ü®Subset.rfl, ?_, hn‚ü©
    have : S = re ‚Åª¬π' Ioi (1 - n : ‚Ñù) := by
      ext; rw [preimage, Ioi, mem_setOf_eq, mem_setOf_eq, mem_setOf_eq]; exact sub_lt_comm
    rw [this]
    exact Continuous.isOpen_preimage continuous_re _ isOpen_Ioi
  apply eventuallyEq_of_mem this
  intro t ht; rw [mem_setOf_eq] at ht
  apply Gamma_eq_GammaAux; linarith
#align complex.differentiable_at_Gamma Complex.differentiableAt_Gamma

end GammaHasDeriv

/-- At `s = 0`, the Gamma function has a simple pole with residue 1. -/
theorem tendsto_self_mul_Gamma_nhds_zero : Tendsto (fun z : ‚ÑÇ => z * Gamma z) (ùìù[‚â†] 0) (ùìù 1) := by
  rw [show ùìù (1 : ‚ÑÇ) = ùìù (Gamma (0 + 1)) by simp only [zero_add, Complex.Gamma_one]]
  convert (Tendsto.mono_left _ nhdsWithin_le_nhds).congr'
    (eventuallyEq_of_mem self_mem_nhdsWithin Complex.Gamma_add_one)
  refine ContinuousAt.comp (g := Gamma) ?_ (continuous_id.add continuous_const).continuousAt
  refine (Complex.differentiableAt_Gamma _ fun m => ?_).continuousAt
  rw [zero_add, ‚Üê ofReal_natCast, ‚Üê ofReal_neg, ‚Üê ofReal_one, Ne, ofReal_inj]
  refine (lt_of_le_of_lt ?_ zero_lt_one).ne'
  exact neg_nonpos.mpr (Nat.cast_nonneg _)
#align complex.tendsto_self_mul_Gamma_nhds_zero Complex.tendsto_self_mul_Gamma_nhds_zero

end Complex

namespace Real

/-- The `Œì` function (of a real variable `s`). -/
-- @[pp_nodot] -- Porting note: removed
def Gamma (s : ‚Ñù) : ‚Ñù :=
  (Complex.Gamma s).re
#align real.Gamma Real.Gamma

theorem Gamma_eq_integral {s : ‚Ñù} (hs : 0 < s) :
    Gamma s = ‚à´ x in Ioi 0, exp (-x) * x ^ (s - 1) := by
  rw [Gamma, Complex.Gamma_eq_integral (by rwa [Complex.ofReal_re] : 0 < Complex.re s)]
  dsimp only [Complex.GammaIntegral]
  simp_rw [‚Üê Complex.ofReal_one, ‚Üê Complex.ofReal_sub]
  suffices ‚à´ x : ‚Ñù in Ioi 0, ‚Üë(exp (-x)) * (x : ‚ÑÇ) ^ ((s - 1 : ‚Ñù) : ‚ÑÇ) =
      ‚à´ x : ‚Ñù in Ioi 0, ((exp (-x) * x ^ (s - 1) : ‚Ñù) : ‚ÑÇ) by
    have cc : ‚àÄ r : ‚Ñù, Complex.ofReal' r = @RCLike.ofReal ‚ÑÇ _ r := fun r => rfl
    conv_lhs => rw [this]; enter [1, 2, x]; rw [cc]
    rw [_root_.integral_ofReal, ‚Üê cc, Complex.ofReal_re]
  refine setIntegral_congr measurableSet_Ioi fun x hx => ?_
  push_cast
  rw [Complex.ofReal_cpow (le_of_lt hx)]
  push_cast; rfl
#align real.Gamma_eq_integral Real.Gamma_eq_integral

theorem Gamma_add_one {s : ‚Ñù} (hs : s ‚â† 0) : Gamma (s + 1) = s * Gamma s := by
  simp_rw [Gamma]
  rw [Complex.ofReal_add, Complex.ofReal_one, Complex.Gamma_add_one, Complex.re_ofReal_mul]
  rwa [Complex.ofReal_ne_zero]
#align real.Gamma_add_one Real.Gamma_add_one

@[simp]
theorem Gamma_one : Gamma 1 = 1 := by
  rw [Gamma, Complex.ofReal_one, Complex.Gamma_one, Complex.one_re]
#align real.Gamma_one Real.Gamma_one

theorem _root_.Complex.Gamma_ofReal (s : ‚Ñù) : Complex.Gamma (s : ‚ÑÇ) = Gamma s := by
  rw [Gamma, eq_comm, ‚Üê Complex.conj_eq_iff_re, ‚Üê Complex.Gamma_conj, Complex.conj_ofReal]
#align complex.Gamma_of_real Complex.Gamma_ofReal

theorem Gamma_nat_eq_factorial (n : ‚Ñï) : Gamma (n + 1) = n ! := by
  rw [Gamma, Complex.ofReal_add, Complex.ofReal_natCast, Complex.ofReal_one,
    Complex.Gamma_nat_eq_factorial, ‚Üê Complex.ofReal_natCast, Complex.ofReal_re]
#align real.Gamma_nat_eq_factorial Real.Gamma_nat_eq_factorial

@[simp]
theorem Gamma_ofNat_eq_factorial (n : ‚Ñï) [(n + 1).AtLeastTwo] :
    Gamma (no_index (OfNat.ofNat (n + 1) : ‚Ñù)) = n ! :=
  mod_cast Gamma_nat_eq_factorial (n : ‚Ñï)

/-- At `0` the Gamma function is undefined; by convention we assign it the value `0`. -/
@[simp]
theorem Gamma_zero : Gamma 0 = 0 := by
  simpa only [‚Üê Complex.ofReal_zero, Complex.Gamma_ofReal, Complex.ofReal_inj] using
    Complex.Gamma_zero
#align real.Gamma_zero Real.Gamma_zero

/-- At `-n` for `n ‚àà ‚Ñï`, the Gamma function is undefined; by convention we assign it the value `0`.
-/
theorem Gamma_neg_nat_eq_zero (n : ‚Ñï) : Gamma (-n) = 0 := by
  simpa only [‚Üê Complex.ofReal_natCast, ‚Üê Complex.ofReal_neg, Complex.Gamma_ofReal,
    Complex.ofReal_eq_zero] using Complex.Gamma_neg_nat_eq_zero n
#align real.Gamma_neg_nat_eq_zero Real.Gamma_neg_nat_eq_zero

theorem Gamma_pos_of_pos {s : ‚Ñù} (hs : 0 < s) : 0 < Gamma s := by
  rw [Gamma_eq_integral hs]
  have : (Function.support fun x : ‚Ñù => exp (-x) * x ^ (s - 1)) ‚à© Ioi 0 = Ioi 0 := by
    rw [inter_eq_right]
    intro x hx
    rw [Function.mem_support]
    exact mul_ne_zero (exp_pos _).ne' (rpow_pos_of_pos hx _).ne'
  rw [setIntegral_pos_iff_support_of_nonneg_ae]
  ¬∑ rw [this, volume_Ioi, ‚Üê ENNReal.ofReal_zero]
    exact ENNReal.ofReal_lt_top
  ¬∑ refine eventually_of_mem (self_mem_ae_restrict measurableSet_Ioi) ?_
    exact fun x hx => (mul_pos (exp_pos _) (rpow_pos_of_pos hx _)).le
  ¬∑ exact GammaIntegral_convergent hs
#align real.Gamma_pos_of_pos Real.Gamma_pos_of_pos

theorem Gamma_nonneg_of_nonneg {s : ‚Ñù} (hs : 0 ‚â§ s) : 0 ‚â§ Gamma s := by
  obtain rfl | h := eq_or_lt_of_le hs
  ¬∑ rw [Gamma_zero]
  ¬∑ exact (Gamma_pos_of_pos h).le

open Complex in
/-- Expresses the integral over `Ioi 0` of `t ^ (a - 1) * exp (-(r * t))`, for positive real `r`,
in terms of the Gamma function. -/
lemma integral_rpow_mul_exp_neg_mul_Ioi {a r : ‚Ñù} (ha : 0 < a) (hr : 0 < r) :
    ‚à´ t : ‚Ñù in Ioi 0, t ^ (a - 1) * exp (-(r * t)) = (1 / r) ^ a * Gamma a := by
  rw [‚Üê ofReal_inj, ofReal_mul, ‚Üê Gamma_ofReal, ofReal_cpow (by positivity), ofReal_div]
  convert integral_cpow_mul_exp_neg_mul_Ioi (by rwa [ofReal_re] : 0 < (a : ‚ÑÇ).re) hr
  refine _root_.integral_ofReal.symm.trans <| setIntegral_congr measurableSet_Ioi (fun t ht ‚Ü¶ ?_)
  norm_cast
  rw [‚Üê ofReal_cpow (le_of_lt ht), RCLike.ofReal_mul]
  rfl

open Lean.Meta Qq Mathlib.Meta.Positivity in
/-- The `positivity` extension which identifies expressions of the form `Gamma a`. -/
@[positivity Gamma (_ : ‚Ñù)]
def _root_.Mathlib.Meta.Positivity.evalGamma : PositivityExt where eval {u Œ±} _zŒ± _pŒ± e := do
  match u, Œ±, e with
  | 0, ~q(‚Ñù), ~q(Gamma $a) =>
    match ‚Üê core q(inferInstance) q(inferInstance) a with
    | .positive pa =>
      assertInstancesCommute
      pure (.positive q(Gamma_pos_of_pos $pa))
    | .nonnegative pa =>
      assertInstancesCommute
      pure (.nonnegative q(Gamma_nonneg_of_nonneg $pa))
    | _ => pure .none
  | _, _, _ => throwError "failed to match on Gamma application"

/-- The Gamma function does not vanish on `‚Ñù` (except at non-positive integers, where the function
is mathematically undefined and we set it to `0` by convention). -/
theorem Gamma_ne_zero {s : ‚Ñù} (hs : ‚àÄ m : ‚Ñï, s ‚â† -m) : Gamma s ‚â† 0 := by
  suffices ‚àÄ {n : ‚Ñï}, -(n : ‚Ñù) < s ‚Üí Gamma s ‚â† 0 by
    apply this
    swap
    ¬∑ exact ‚åä-s‚åã‚Çä + 1
    rw [neg_lt, Nat.cast_add, Nat.cast_one]
    exact Nat.lt_floor_add_one _
  intro n
  induction' n with _ n_ih generalizing s
  ¬∑ intro hs
    refine (Gamma_pos_of_pos ?_).ne'
    rwa [Nat.cast_zero, neg_zero] at hs
  ¬∑ intro hs'
    have : Gamma (s + 1) ‚â† 0 := by
      apply n_ih
      ¬∑ intro m
        specialize hs (1 + m)
        contrapose! hs
        rw [‚Üê eq_sub_iff_add_eq] at hs
        rw [hs]
        push_cast
        ring
      ¬∑ rw [Nat.cast_add, Nat.cast_one, neg_add] at hs'
        linarith
    rw [Gamma_add_one, mul_ne_zero_iff] at this
    ¬∑ exact this.2
    ¬∑ simpa using hs 0
#align real.Gamma_ne_zero Real.Gamma_ne_zero

theorem Gamma_eq_zero_iff (s : ‚Ñù) : Gamma s = 0 ‚Üî ‚àÉ m : ‚Ñï, s = -m :=
  ‚ü®by contrapose!; exact Gamma_ne_zero, by rintro ‚ü®m, rfl‚ü©; exact Gamma_neg_nat_eq_zero m‚ü©
#align real.Gamma_eq_zero_iff Real.Gamma_eq_zero_iff

theorem differentiableAt_Gamma {s : ‚Ñù} (hs : ‚àÄ m : ‚Ñï, s ‚â† -m) : DifferentiableAt ‚Ñù Gamma s := by
  refine (Complex.differentiableAt_Gamma _ ?_).hasDerivAt.real_of_complex.differentiableAt
  simp_rw [‚Üê Complex.ofReal_natCast, ‚Üê Complex.ofReal_neg, Ne, Complex.ofReal_inj]
  exact hs
#align real.differentiable_at_Gamma Real.differentiableAt_Gamma

end Real
