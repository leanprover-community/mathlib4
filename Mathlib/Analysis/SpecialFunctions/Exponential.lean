/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Eric Wieser
-/
import Mathlib.Analysis.Normed.Algebra.Exponential
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Data.Complex.Exponential
import Mathlib.Topology.MetricSpace.CauSeqFilter

/-!
# Calculus results on exponential in a Banach algebra

In this file, we prove basic properties about the derivative of the exponential map `exp ğ•‚`
in a Banach algebra `ğ”¸` over a field `ğ•‚`. We keep them separate from the main file
`Analysis.Normed.Algebra.Exponential` in order to minimize dependencies.

## Main results

We prove most results for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `hasStrictFDerivAt_exp_zero_of_radius_pos` : `NormedSpace.exp ğ•‚` has strict FrÃ©chet derivative
  `1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero
  (see also `hasStrictDerivAt_exp_zero_of_radius_pos` for the case `ğ”¸ = ğ•‚`)
- `hasStrictFDerivAt_exp_of_lt_radius` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative,
  then given a point `x` in the disk of convergence, `NormedSpace.exp ğ•‚` has strict FrÃ©chet
  derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x
  (see also `hasStrictDerivAt_exp_of_lt_radius` for the case `ğ”¸ = ğ•‚`)
- `hasStrictFDerivAt_exp_smul_const_of_mem_ball`: even when `ğ”¸` is non-commutative,
  if we have an intermediate algebra `ğ•Š` which is commutative, the function
  `(u : ğ•Š) â†¦ NormedSpace.exp ğ•‚ (u â€¢ x)`, still has strict FrÃ©chet derivative
  `NormedSpace.exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x` at `t` if
  `t â€¢ x` is in the radius of convergence.

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `hasStrictFDerivAt_exp_zero` : `NormedSpace.exp ğ•‚` has strict FrÃ©chet derivative `1 : ğ”¸ â†’L[ğ•‚] ğ”¸`
  at zero (see also `hasStrictDerivAt_exp_zero` for the case `ğ”¸ = ğ•‚`)
- `hasStrictFDerivAt_exp` : if `ğ”¸` is commutative, then given any point `x`, `NormedSpace.exp ğ•‚`
  has strict FrÃ©chet derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at x
  (see also `hasStrictDerivAt_exp` for the case `ğ”¸ = ğ•‚`)
- `hasStrictFDerivAt_exp_smul_const`: even when `ğ”¸` is non-commutative, if we have
  an intermediate algebra `ğ•Š` which is commutative, the function
  `(u : ğ•Š) â†¦ NormedSpace.exp ğ•‚ (u â€¢ x)` still has strict FrÃ©chet derivative
  `NormedSpace.exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸).smulRight x` at `t`.

### Compatibility with `Real.exp` and `Complex.exp`

- `Complex.exp_eq_exp_â„‚` : `Complex.exp = NormedSpace.exp â„‚ â„‚`
- `Real.exp_eq_exp_â„` : `Real.exp = NormedSpace.exp â„ â„`

-/


open Filter RCLike ContinuousMultilinearMap NormedField NormedSpace Asymptotics

open scoped Nat Topology ENNReal

section AnyFieldAnyAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  [CompleteSpace ğ”¸]

/-- The exponential in a Banach algebra `ğ”¸` over a normed field `ğ•‚` has strict FrÃ©chet derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
theorem hasStrictFDerivAt_exp_zero_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictFDerivAt (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 := by
  convert (hasFPowerSeriesAt_exp_zero_of_radius_pos h).hasStrictFDerivAt
  ext x
  change x = expSeries ğ•‚ ğ”¸ 1 fun _ => x
  simp [expSeries_apply_eq, Nat.factorial]

/-- The exponential in a Banach algebra `ğ”¸` over a normed field `ğ•‚` has FrÃ©chet derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero, as long as it converges on a neighborhood of zero. -/
theorem hasFDerivAt_exp_zero_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasFDerivAt (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
  (hasStrictFDerivAt_exp_zero_of_radius_pos h).hasFDerivAt

end AnyFieldAnyAlgebra

section AnyFieldCommAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  [CompleteSpace ğ”¸]

/-- The exponential map in a commutative Banach algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has FrÃ©chet derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸`
at any point `x`in the disk of convergence. -/
theorem hasFDerivAt_exp_of_mem_ball [CharZero ğ•‚] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasFDerivAt (exp ğ•‚) (exp ğ•‚ x â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸)) x := by
  have hpos : 0 < (expSeries ğ•‚ ğ”¸).radius := (zero_le _).trans_lt hx
  rw [hasFDerivAt_iff_isLittleO_nhds_zero]
  suffices
    (fun h => exp ğ•‚ x * (exp ğ•‚ (0 + h) - exp ğ•‚ 0 - ContinuousLinearMap.id ğ•‚ ğ”¸ h)) =á¶ [ğ“ 0] fun h =>
      exp ğ•‚ (x + h) - exp ğ•‚ x - exp ğ•‚ x â€¢ ContinuousLinearMap.id ğ•‚ ğ”¸ h by
    refine (IsLittleO.const_mul_left ?_ _).congr' this (EventuallyEq.refl _ _)
    rw [â† hasFDerivAt_iff_isLittleO_nhds_zero]
    exact hasFDerivAt_exp_zero_of_radius_pos hpos
  have : âˆ€á¶  h in ğ“ (0 : ğ”¸), h âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius :=
    EMetric.ball_mem_nhds _ hpos
  filter_upwards [this] with _ hh
  rw [exp_add_of_mem_ball hx hh, exp_zero, zero_add, ContinuousLinearMap.id_apply, smul_eq_mul]
  ring

/-- The exponential map in a commutative Banach algebra `ğ”¸` over a normed field `ğ•‚` of
characteristic zero has strict FrÃ©chet derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸`
at any point `x` in the disk of convergence. -/
theorem hasStrictFDerivAt_exp_of_mem_ball [CharZero ğ•‚] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictFDerivAt (exp ğ•‚) (exp ğ•‚ x â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸)) x :=
  let âŸ¨_, hpâŸ© := analyticAt_exp_of_mem_ball x hx
  hp.hasFDerivAt.unique (hasFDerivAt_exp_of_mem_ball hx) â–¸ hp.hasStrictFDerivAt

end AnyFieldCommAlgebra

section deriv

variable {ğ•‚ : Type*} [NontriviallyNormedField ğ•‚] [CompleteSpace ğ•‚]

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`NormedSpace.exp ğ•‚ x` at any point `x` in the disk of convergence. -/
theorem hasStrictDerivAt_exp_of_mem_ball [CharZero ğ•‚] {x : ğ•‚}
    (hx : x âˆˆ EMetric.ball (0 : ğ•‚) (expSeries ğ•‚ ğ•‚).radius) :
    HasStrictDerivAt (exp ğ•‚) (exp ğ•‚ x) x := by
  simpa using (hasStrictFDerivAt_exp_of_mem_ball hx).hasStrictDerivAt

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`NormedSpace.exp ğ•‚ x` at any point `x` in the disk of convergence. -/
theorem hasDerivAt_exp_of_mem_ball [CharZero ğ•‚] {x : ğ•‚}
    (hx : x âˆˆ EMetric.ball (0 : ğ•‚) (expSeries ğ•‚ ğ•‚).radius) : HasDerivAt (exp ğ•‚) (exp ğ•‚ x) x :=
  (hasStrictDerivAt_exp_of_mem_ball hx).hasDerivAt

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has strict derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
theorem hasStrictDerivAt_exp_zero_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ•‚).radius) :
    HasStrictDerivAt (exp ğ•‚) (1 : ğ•‚) 0 :=
  (hasStrictFDerivAt_exp_zero_of_radius_pos h).hasStrictDerivAt

/-- The exponential map in a complete normed field `ğ•‚` of characteristic zero has derivative
`1` at zero, as long as it converges on a neighborhood of zero. -/
theorem hasDerivAt_exp_zero_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ•‚).radius) :
    HasDerivAt (exp ğ•‚) (1 : ğ•‚) 0 :=
  (hasStrictDerivAt_exp_zero_of_radius_pos h).hasDerivAt

end deriv

section RCLikeAnyAlgebra

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

/-- The exponential in a Banach algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict FrÃ©chet derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
theorem hasStrictFDerivAt_exp_zero : HasStrictFDerivAt (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
  hasStrictFDerivAt_exp_zero_of_radius_pos (expSeries_radius_pos ğ•‚ ğ”¸)

/-- The exponential in a Banach algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has FrÃ©chet derivative
`1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at zero. -/
theorem hasFDerivAt_exp_zero : HasFDerivAt (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) 0 :=
  hasStrictFDerivAt_exp_zero.hasFDerivAt

end RCLikeAnyAlgebra

section RCLikeCommAlgebra

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

/-- The exponential map in a commutative Banach algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict
FrÃ©chet derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
theorem hasStrictFDerivAt_exp {x : ğ”¸} : HasStrictFDerivAt (exp ğ•‚) (exp ğ•‚ x â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸)) x :=
  hasStrictFDerivAt_exp_of_mem_ball ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- The exponential map in a commutative Banach algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚` has
FrÃ©chet derivative `NormedSpace.exp ğ•‚ x â€¢ 1 : ğ”¸ â†’L[ğ•‚] ğ”¸` at any point `x`. -/
theorem hasFDerivAt_exp {x : ğ”¸} : HasFDerivAt (exp ğ•‚) (exp ğ•‚ x â€¢ (1 : ğ”¸ â†’L[ğ•‚] ğ”¸)) x :=
  hasStrictFDerivAt_exp.hasFDerivAt

end RCLikeCommAlgebra

section DerivRCLike

variable {ğ•‚ : Type*} [RCLike ğ•‚]

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `NormedSpace.exp ğ•‚ x`
at any point `x`. -/
theorem hasStrictDerivAt_exp {x : ğ•‚} : HasStrictDerivAt (exp ğ•‚) (exp ğ•‚ x) x :=
  hasStrictDerivAt_exp_of_mem_ball ((expSeries_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `NormedSpace.exp ğ•‚ x`
at any point `x`. -/
theorem hasDerivAt_exp {x : ğ•‚} : HasDerivAt (exp ğ•‚) (exp ğ•‚ x) x :=
  hasStrictDerivAt_exp.hasDerivAt

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has strict derivative `1` at zero. -/
theorem hasStrictDerivAt_exp_zero : HasStrictDerivAt (exp ğ•‚) (1 : ğ•‚) 0 :=
  hasStrictDerivAt_exp_zero_of_radius_pos (expSeries_radius_pos ğ•‚ ğ•‚)

/-- The exponential map in `ğ•‚ = â„` or `ğ•‚ = â„‚` has derivative `1` at zero. -/
theorem hasDerivAt_exp_zero : HasDerivAt (exp ğ•‚) (1 : ğ•‚) 0 :=
  hasStrictDerivAt_exp_zero.hasDerivAt

end DerivRCLike

theorem Complex.exp_eq_exp_â„‚ : Complex.exp = NormedSpace.exp â„‚ := by
  refine funext fun x => ?_
  rw [Complex.exp, exp_eq_tsum_div]
  have : CauSeq.IsComplete â„‚ norm := Complex.instIsComplete
  exact tendsto_nhds_unique x.exp'.tendsto_limit (expSeries_div_summable â„ x).hasSum.tendsto_sum_nat

theorem Real.exp_eq_exp_â„ : Real.exp = NormedSpace.exp â„ := by
  ext x; exact mod_cast congr_fun Complex.exp_eq_exp_â„‚ x

/-! ### Derivative of $\exp (ux)$ by $u$

Note that since for `x : ğ”¸` we have `NormedRing ğ”¸` not `NormedCommRing ğ”¸`, we cannot deduce
these results from `hasFDerivAt_exp_of_mem_ball` applied to the algebra `ğ”¸`.

One possible solution for that would be to apply `hasFDerivAt_exp_of_mem_ball` to the
commutative algebra `Algebra.elementalAlgebra ğ•Š x`. Unfortunately we don't have all the required
API, so we leave that to a future refactor (see https://github.com/leanprover-community/mathlib3/pull/19062 for discussion).

We could also go the other way around and deduce `hasFDerivAt_exp_of_mem_ball` from
`hasFDerivAt_exp_smul_const_of_mem_ball` applied to `ğ•Š := ğ”¸`, `x := (1 : ğ”¸)`, and `t := x`.
However, doing so would make the aforementioned `elementalAlgebra` refactor harder, so for now we
just prove these two lemmas independently.

A last strategy would be to deduce everything from the more general non-commutative case,
$$\frac{d}{dt}e^{x(t)} = \int_0^1 e^{sx(t)} \left(\frac{d}{dt}e^{x(t)}\right) e^{(1-s)x(t)} ds$$
but this is harder to prove, and typically is shown by going via these results first.

TODO: prove this result too!
-/


section exp_smul

variable {ğ•‚ ğ•Š ğ”¸ : Type*}
variable (ğ•‚)

open scoped Topology

open Asymptotics Filter

section MemBall

variable [NontriviallyNormedField ğ•‚] [CharZero ğ•‚]
variable [NormedCommRing ğ•Š] [NormedRing ğ”¸]
variable [NormedSpace ğ•‚ ğ•Š] [NormedAlgebra ğ•‚ ğ”¸] [Algebra ğ•Š ğ”¸] [ContinuousSMul ğ•Š ğ”¸]
variable [IsScalarTower ğ•‚ ğ•Š ğ”¸]
variable [CompleteSpace ğ”¸]

theorem hasFDerivAt_exp_smul_const_of_mem_ball (x : ğ”¸) (t : ğ•Š)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) t := by
  -- TODO: prove this via `hasFDerivAt_exp_of_mem_ball` using the commutative ring
  -- `Algebra.elementalAlgebra ğ•Š x`. See https://github.com/leanprover-community/mathlib3/pull/19062 for discussion.
  have hpos : 0 < (expSeries ğ•‚ ğ”¸).radius := (zero_le _).trans_lt htx
  rw [hasFDerivAt_iff_isLittleO_nhds_zero]
  suffices (fun (h : ğ•Š) => exp ğ•‚ (t â€¢ x) *
      (exp ğ•‚ ((0 + h) â€¢ x) - exp ğ•‚ ((0 : ğ•Š) â€¢ x) - ((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) h)) =á¶ [ğ“ 0]
        fun h =>
          exp ğ•‚ ((t + h) â€¢ x) - exp ğ•‚ (t â€¢ x) - (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) h by
    apply (IsLittleO.const_mul_left _ _).congr' this (EventuallyEq.refl _ _)
    rw [â† hasFDerivAt_iff_isLittleO_nhds_zero (f := fun u => exp ğ•‚ (u â€¢ x))
      (f' := (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) (x := 0)]
    have : HasFDerivAt (exp ğ•‚) (1 : ğ”¸ â†’L[ğ•‚] ğ”¸) ((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x 0) := by
      rw [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, zero_smul]
      exact hasFDerivAt_exp_zero_of_radius_pos hpos
    exact this.comp 0 ((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x).hasFDerivAt
  have : Tendsto (fun h : ğ•Š => h â€¢ x) (ğ“ 0) (ğ“ 0) := by
    rw [â† zero_smul ğ•Š x]
    exact tendsto_id.smul_const x
  have : âˆ€á¶  h in ğ“ (0 : ğ•Š), h â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius :=
    this.eventually (EMetric.ball_mem_nhds _ hpos)
  filter_upwards [this] with h hh
  have : Commute (t â€¢ x) (h â€¢ x) := ((Commute.refl x).smul_left t).smul_right h
  rw [add_smul t h, exp_add_of_commute_of_mem_ball this htx hh, zero_add, zero_smul, exp_zero,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply,
    ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.one_apply, smul_eq_mul, mul_sub_left_distrib, mul_sub_left_distrib, mul_one]

theorem hasFDerivAt_exp_smul_const_of_mem_ball' (x : ğ”¸) (t : ğ•Š)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x).smulRight (exp ğ•‚ (t â€¢ x))) t := by
  convert hasFDerivAt_exp_smul_const_of_mem_ball ğ•‚ _ _ htx using 1
  ext t'
  show Commute (t' â€¢ x) (exp ğ•‚ (t â€¢ x))
  exact (((Commute.refl x).smul_left t').smul_right t).exp_right ğ•‚

theorem hasStrictFDerivAt_exp_smul_const_of_mem_ball (x : ğ”¸) (t : ğ•Š)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) t :=
  let âŸ¨_, hpâŸ© := analyticAt_exp_of_mem_ball (t â€¢ x) htx
  have derivâ‚ : HasStrictFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x)) _ t :=
    hp.hasStrictFDerivAt.comp t ((ContinuousLinearMap.id ğ•‚ ğ•Š).smulRight x).hasStrictFDerivAt
  have derivâ‚‚ : HasFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x)) _ t :=
    hasFDerivAt_exp_smul_const_of_mem_ball ğ•‚ x t htx
  derivâ‚.hasFDerivAt.unique derivâ‚‚ â–¸ derivâ‚

theorem hasStrictFDerivAt_exp_smul_const_of_mem_ball' (x : ğ”¸) (t : ğ•Š)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x).smulRight (exp ğ•‚ (t â€¢ x))) t := by
  let âŸ¨_, _âŸ© := analyticAt_exp_of_mem_ball (t â€¢ x) htx
  convert hasStrictFDerivAt_exp_smul_const_of_mem_ball ğ•‚ _ _ htx using 1
  ext t'
  show Commute (t' â€¢ x) (exp ğ•‚ (t â€¢ x))
  exact (((Commute.refl x).smul_left t').smul_right t).exp_right ğ•‚

variable {ğ•‚}

theorem hasStrictDerivAt_exp_smul_const_of_mem_ball (x : ğ”¸) (t : ğ•‚)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t := by
  simpa using (hasStrictFDerivAt_exp_smul_const_of_mem_ball ğ•‚ x t htx).hasStrictDerivAt

theorem hasStrictDerivAt_exp_smul_const_of_mem_ball' (x : ğ”¸) (t : ğ•‚)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasStrictDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t := by
  simpa using (hasStrictFDerivAt_exp_smul_const_of_mem_ball' ğ•‚ x t htx).hasStrictDerivAt

theorem hasDerivAt_exp_smul_const_of_mem_ball (x : ğ”¸) (t : ğ•‚)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
  (hasStrictDerivAt_exp_smul_const_of_mem_ball x t htx).hasDerivAt

theorem hasDerivAt_exp_smul_const_of_mem_ball' (x : ğ”¸) (t : ğ•‚)
    (htx : t â€¢ x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
  (hasStrictDerivAt_exp_smul_const_of_mem_ball' x t htx).hasDerivAt

end MemBall

section RCLike

variable [RCLike ğ•‚]
variable [NormedCommRing ğ•Š] [NormedRing ğ”¸]
variable [NormedAlgebra ğ•‚ ğ•Š] [NormedAlgebra ğ•‚ ğ”¸] [Algebra ğ•Š ğ”¸] [ContinuousSMul ğ•Š ğ”¸]
variable [IsScalarTower ğ•‚ ğ•Š ğ”¸]
variable [CompleteSpace ğ”¸]

theorem hasFDerivAt_exp_smul_const (x : ğ”¸) (t : ğ•Š) :
    HasFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) t :=
  hasFDerivAt_exp_smul_const_of_mem_ball ğ•‚ _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasFDerivAt_exp_smul_const' (x : ğ”¸) (t : ğ•Š) :
    HasFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x).smulRight (exp ğ•‚ (t â€¢ x))) t :=
  hasFDerivAt_exp_smul_const_of_mem_ball' ğ•‚ _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasStrictFDerivAt_exp_smul_const (x : ğ”¸) (t : ğ•Š) :
    HasStrictFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (exp ğ•‚ (t â€¢ x) â€¢ (1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x) t :=
  hasStrictFDerivAt_exp_smul_const_of_mem_ball ğ•‚ _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasStrictFDerivAt_exp_smul_const' (x : ğ”¸) (t : ğ•Š) :
    HasStrictFDerivAt (fun u : ğ•Š => exp ğ•‚ (u â€¢ x))
      (((1 : ğ•Š â†’L[ğ•‚] ğ•Š).smulRight x).smulRight (exp ğ•‚ (t â€¢ x))) t :=
  hasStrictFDerivAt_exp_smul_const_of_mem_ball' ğ•‚ _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

variable {ğ•‚}

theorem hasStrictDerivAt_exp_smul_const (x : ğ”¸) (t : ğ•‚) :
    HasStrictDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
  hasStrictDerivAt_exp_smul_const_of_mem_ball _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasStrictDerivAt_exp_smul_const' (x : ğ”¸) (t : ğ•‚) :
    HasStrictDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
  hasStrictDerivAt_exp_smul_const_of_mem_ball' _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasDerivAt_exp_smul_const (x : ğ”¸) (t : ğ•‚) :
    HasDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (exp ğ•‚ (t â€¢ x) * x) t :=
  hasDerivAt_exp_smul_const_of_mem_ball _ _ <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem hasDerivAt_exp_smul_const' (x : ğ”¸) (t : ğ•‚) :
    HasDerivAt (fun u : ğ•‚ => exp ğ•‚ (u â€¢ x)) (x * exp ğ•‚ (t â€¢ x)) t :=
  hasDerivAt_exp_smul_const_of_mem_ball' _ _ <|
    (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

end RCLike

end exp_smul

section tsum_tprod

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

/-- If `f` has sum `a`, then `NormedSpace.exp âˆ˜ f` has product `NormedSpace.exp a`. -/
lemma HasSum.exp {Î¹ : Type*} {f : Î¹ â†’ ğ”¸} {a : ğ”¸} (h : HasSum f a) :
    HasProd (exp ğ•‚ âˆ˜ f) (exp ğ•‚ a) :=
  Tendsto.congr (fun s â†¦ exp_sum s f) <| Tendsto.exp h

end tsum_tprod
