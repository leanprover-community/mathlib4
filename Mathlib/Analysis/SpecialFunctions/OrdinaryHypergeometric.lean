/-
Copyright (c) 2024 Edward Watine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Edward Watine
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.SpecificLimits.RCLike

/-!
# Ordinary hypergeometric function in a Banach algebra

In this file, we define `ordinaryHypergeometric`, the _ordinary_ or _Gaussian_ hypergeometric
function in a topological algebra `ğ”¸` over a field `ğ•‚` given by: $$
_2\mathrm{F}_1(a\ b\ c : \mathbb{K}, x : \mathbb{A}) = \sum_{n=0}^{\infty}\frac{(a)_n(b)_n}{(c)_n}
\frac{x^n}{n!}   \,,
$$
with $(a)_n$ is the ascending Pochhammer symbol (see `ascPochhammer`).

This file contains the basic definitions over a general field `ğ•‚` and notation for `â‚‚Fâ‚`,
as well as showing that terms of the series are zero if any of the `(a b c : ğ•‚)` are sufficiently
large non-positive integers, rendering the series finite. In this file "sufficiently large" means
that `-n < a` for the `n`-th term, and similarly for `b` and `c`.

- `ordinaryHypergeometricSeries` is the `FormalMultilinearSeries` given above for some `(a b c : ğ•‚)`
- `ordinaryHypergeometric` is the sum of the series for some `(x : ğ”¸)`
- `ordinaryHypergeometricSeries_eq_zero_of_nonpos_int` shows that the `n`-th term of the series is
zero if any of the parameters are sufficiently large non-positive integers

## `[RCLike ğ•‚]`

If we have `[RCLike ğ•‚]`, then we show that the latter result is an iff, and hence prove that the
radius of convergence of the series is unity if the series is infinite, or `âŠ¤` otherwise.

- `ordinaryHypergeometricSeries_eq_zero_iff` is iff variant of
`ordinaryHypergeometricSeries_eq_zero_of_nonpos_int`
- `ordinaryHypergeometricSeries_radius_eq_one` proves that the radius of convergence of the
`ordinaryHypergeometricSeries` is unity under non-trivial parameters

## Notation

`â‚‚Fâ‚` is notation for `ordinaryHypergeometric`.

## References

See <https://en.wikipedia.org/wiki/Hypergeometric_function>.

## Tags

hypergeometric, gaussian, ordinary
-/

open Nat

section Field

variable {ğ•‚ : Type*} (ğ”¸ : Type*) [Field ğ•‚] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸]
  [TopologicalRing ğ”¸]

/-- `ordinaryHypergeometricSeries ğ”¸ (a b c : ğ•‚)` is a `FormalMultilinearSeries`.
Its sum is the `ordinaryHypergeometric` map. -/
noncomputable def ordinaryHypergeometricSeries (a b c : ğ•‚) : FormalMultilinearSeries ğ•‚ ğ”¸ ğ”¸ :=
  fun n => ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
    ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸

variable {ğ”¸} (a b c : ğ•‚)

/-- `ordinaryHypergeometric (a b c : ğ•‚) : ğ”¸ â†’ ğ”¸` is the ordinary hypergeometric map, defined as the
sum of the `FormalMultilinearSeries` `ordinaryHypergeometricSeries ğ”¸ a b c`.

Note that this takes the junk value `0` outside the radius of convergence.
-/
noncomputable def ordinaryHypergeometric (x : ğ”¸) : ğ”¸ :=
  (ordinaryHypergeometricSeries ğ”¸ a b c).sum x

@[inherit_doc]
notation "â‚‚Fâ‚" => ordinaryHypergeometric

theorem ordinaryHypergeometricSeries_apply_eq (x : ğ”¸) (n : â„•) :
    (ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => x) =
      ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
        ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n := by
  simp [ordinaryHypergeometricSeries]

/-- This naming follows the convention of `NormedSpace.expSeries_apply_eq'`. -/
theorem ordinaryHypergeometricSeries_apply_eq' (x : ğ”¸) :
    (fun n => ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => x) =
      fun n => ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
        ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n := by
  simp [ordinaryHypergeometricSeries]

theorem ordinaryHypergeometric_sum_eq (x : ğ”¸) : (ordinaryHypergeometricSeries ğ”¸ a b c).sum x =
    âˆ‘' n : â„•, ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a * (ascPochhammer ğ•‚ n).eval b *
      ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n :=
  tsum_congr fun n => ordinaryHypergeometricSeries_apply_eq a b c x n

theorem ordinaryHypergeometric_eq_tsum : â‚‚Fâ‚ a b c =
    fun (x : ğ”¸) => âˆ‘' n : â„•, ((n !â»Â¹ : ğ•‚) * (ascPochhammer ğ•‚ n).eval a *
      (ascPochhammer ğ•‚ n).eval b * ((ascPochhammer ğ•‚ n).eval c)â»Â¹ ) â€¢ x ^ n :=
  funext (ordinaryHypergeometric_sum_eq a b c)

theorem ordinaryHypergeometricSeries_apply_zero (n : â„•) :
    (ordinaryHypergeometricSeries ğ”¸ a b c n fun _ => 0) = Pi.single (f := fun _ => ğ”¸) 0 1 n := by
  rw [ordinaryHypergeometricSeries_apply_eq]
  cases n <;> simp

@[simp]
theorem ordinaryHypergeometric_zero : â‚‚Fâ‚ a b c (0 : ğ”¸) = 1 := by
  simp [ordinaryHypergeometric_eq_tsum, â† ordinaryHypergeometricSeries_apply_eq,
    ordinaryHypergeometricSeries_apply_zero]

@[simp]
theorem ordinaryHypergeometric_op [T2Space ğ”¸] (x : ğ”¸) :
    â‚‚Fâ‚ a b c (MulOpposite.op x) = MulOpposite.op (â‚‚Fâ‚ a b c x) := by
  simp [ordinaryHypergeometric, ordinaryHypergeometric_sum_eq, â† MulOpposite.op_pow,
    â† MulOpposite.op_smul, tsum_op]

@[simp]
theorem ordinaryHypergeometric_unop [T2Space ğ”¸] (x : ğ”¸áµáµ’áµ–) :
    â‚‚Fâ‚ a b c (MulOpposite.unop x) = MulOpposite.unop (â‚‚Fâ‚ a b c x) := by
  simp [ordinaryHypergeometric, ordinaryHypergeometric_sum_eq, â† MulOpposite.unop_pow,
     â† MulOpposite.unop_smul, tsum_unop]

theorem ordinaryHypergeometricSeries_symm :
    ordinaryHypergeometricSeries ğ”¸ a b c = ordinaryHypergeometricSeries ğ”¸ b a c := by
  ext
  simp [ordinaryHypergeometricSeries, mul_assoc, mul_left_comm]

/-- If any parameter to the series is a sufficiently large nonpositive integer, then the series
term is zero. -/
lemma ordinaryHypergeometricSeries_eq_zero_of_neg_nat {n k : â„•}
    (habc : k = -a âˆ¨ k = -b âˆ¨ k = -c) (hk : k < n) :
      ordinaryHypergeometricSeries ğ”¸ a b c n = 0 := by
  rw [ordinaryHypergeometricSeries]
  rcases habc with h | h | h
  all_goals
    ext
    simp [(ascPochhammer_eval_eq_zero_iff n _).2 âŸ¨k, hk, hâŸ©]

end Field

section RCLike

open Asymptotics Filter Real Set Nat

open scoped Topology

variable {ğ•‚ : Type*} (ğ”¸ : Type*) [RCLike ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  (a b c : ğ•‚)

theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ (-(k : ğ•‚)) b c).radius = âŠ¤ := by
  refine FormalMultilinearSeries.radius_eq_top_of_forall_image_add_eq_zero _ (1 + k) fun n â†¦ ?_
  exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat (-(k : ğ•‚)) b c (by aesop) (by omega)

theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚‚ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ a (-(k : ğ•‚)) c).radius = âŠ¤ := by
  rw [ordinaryHypergeometricSeries_symm]
  exact ordinaryHypergeometric_radius_top_of_neg_natâ‚ ğ”¸ a c

theorem ordinaryHypergeometric_radius_top_of_neg_natâ‚ƒ {k : â„•} :
    (ordinaryHypergeometricSeries ğ”¸ a b (-(k : ğ•‚))).radius = âŠ¤ := by
  refine FormalMultilinearSeries.radius_eq_top_of_forall_image_add_eq_zero _ (1 + k) fun n â†¦ ?_
  exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat a b (-(k : ğ•‚)) (by aesop) (by omega)

/-- An iff variation on `ordinaryHypergeometricSeries_eq_zero_of_nonpos_int` for `[RCLike ğ•‚]`. -/
lemma ordinaryHypergeometricSeries_eq_zero_iff (n : â„•) :
    ordinaryHypergeometricSeries ğ”¸ a b c n = 0 â†”
      âˆƒ k < n, k = -a âˆ¨ k = -b âˆ¨ k = -c := by
  refine âŸ¨fun h â†¦ ?_, fun zero â†¦ ?_âŸ©
  Â· rw [ordinaryHypergeometricSeries,
      smul_eq_zero (c:=(_ * (Polynomial.eval c (ascPochhammer ğ•‚ n))â»Â¹))
      (x:=ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸)] at h
    cases' h with h hm
    Â· simp only [_root_.mul_eq_zero, inv_eq_zero] at h
      rcases h with ((hn | h) | h) | h
      Â· simp [Nat.factorial_ne_zero] at hn
      all_goals
        obtain âŸ¨kn, hkn, hnâŸ© := (ascPochhammer_eval_eq_zero_iff _ _).1 h
        exact âŸ¨kn, hkn, by tautoâŸ©
    Â· rw [ContinuousMultilinearMap.ext_iff] at hm
      absurd hm
      push_neg
      exact âŸ¨fun _ â†¦ 1, by simpâŸ©
  Â· obtain âŸ¨_, h, hnâŸ© := zero
    exact ordinaryHypergeometricSeries_eq_zero_of_neg_nat a b c hn h

theorem ordinaryHypergeometricSeries_succ_norm_div_norm (n : â„•)
    (habc : âˆ€ kn < n, (â†‘kn â‰  -a âˆ§ â†‘kn â‰  -b âˆ§ â†‘kn â‰  -c)) :
      â€–ordinaryHypergeometricSeries ğ”¸ a b c (n+1)â€– / â€–ordinaryHypergeometricSeries ğ”¸ a b c nâ€– =
        â€–a + nâ€– * â€–b + nâ€– * â€–c + nâ€–â»Â¹ * â€–1 + (n : ğ•‚)â€–â»Â¹ := by
  simp [ordinaryHypergeometricSeries, factorial_succ, ascPochhammer_succ_eval]
  rw [norm_smul (x:=ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ (n + 1) ğ”¸),
    norm_smul (x:=ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸)]
  simp only [norm_mul, norm_inv, ContinuousMultilinearMap.norm_mkPiAlgebraFin, mul_one]
  have : â€–(n ! : ğ•‚)â€–â»Â¹ * â€–(n : ğ•‚) + 1â€–â»Â¹ * (â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€– * â€–a + nâ€–) *
    (â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€– * â€–b + nâ€–) *
    (â€–c + nâ€–â»Â¹ * â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹) /
    (â€–(n ! : ğ•‚)â€–â»Â¹ * â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€– *
    â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€– * â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹) =
    â€–(n ! : ğ•‚)â€–â»Â¹â»Â¹ * â€–(n ! : ğ•‚)â€–â»Â¹ * â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€– *
    â€–Polynomial.eval a (ascPochhammer ğ•‚ n)â€–â»Â¹ * â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€– *
    â€–Polynomial.eval b (ascPochhammer ğ•‚ n)â€–â»Â¹ * â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹â»Â¹ *
    â€–Polynomial.eval c (ascPochhammer ğ•‚ n)â€–â»Â¹ * â€–a + nâ€– * â€–b + nâ€– * â€–c + nâ€–â»Â¹ * â€–1 + (n : ğ•‚)â€–â»Â¹ :=
    by ring_nf
  rw [this, inv_inv, inv_inv]
  repeat rw [DivisionRing.mul_inv_cancel, one_mul]
  all_goals
    rw [norm_ne_zero_iff]
  any_goals
    apply (ascPochhammer_eval_eq_zero_iff n _).not.2
    push_neg
    exact fun kn hkn â†¦ by simp [habc kn hkn]
  exact cast_ne_zero.2 (factorial_ne_zero n)

/-- The ratio of successive terms of `ordinaryHypergeometricSeries` tends to one. This theorem
is used in the proof `ordinaryHypergeometric_ratio_tendsto_nhds_atTop`. -/
theorem ordinaryHypergeometricSeries_ratio_tendsto_one_atTop :
    Tendsto (fun k : â„• â†¦ (a + k) * (b + k) * (c + k)â»Â¹ * ((1 : ğ•‚) + k)â»Â¹) atTop (ğ“ 1) := by
  conv =>
    enter [1, n]
    rw [mul_assoc, â† mul_inv, â† div_eq_mul_inv, mul_div_mul_comm]
  apply (mul_one (1 : ğ•‚)) â–¸ Filter.Tendsto.mul <;>
  convert RCLike.tendsto_add_div_add_atTop_nhds _ _ (1 : ğ•‚) one_ne_zero <;> simp

/-- The ratio of successive terms of the sum `ordinaryHypergeometric a b c r` is `r`. This theorem
is used for the ratio test in `ordinaryHypergeometricSeries_radius_eq_one`. -/
theorem ordinaryHypergeometric_ratio_tendsto_nhds_atTop {r : â„} (hr : 0 < r)
    (habc : âˆ€ kn : â„•, â†‘kn â‰  -a âˆ§ â†‘kn â‰  -b âˆ§ â†‘kn â‰  -c) : Tendsto (fun n â†¦
      â€–â€–ordinaryHypergeometricSeries ğ”¸ a b c (n + 1)â€– * r ^ (n + 1)â€– /
        â€–â€–ordinaryHypergeometricSeries ğ”¸ a b c nâ€– * r ^ nâ€–) atTop (ğ“ r) := by
  simp_rw [â† norm_div, mul_div_mul_comm, pow_succ, mul_div_right_comm]
  apply Real.norm_of_nonneg (le_of_lt hr) â–¸ Filter.Tendsto.norm
  conv =>
    enter [1, n]
    rw [ordinaryHypergeometricSeries_succ_norm_div_norm, div_self (pow_ne_zero n hr.ne.symm),
      one_mul, â† norm_mul, â† norm_inv, â† norm_mul, â† norm_inv, â† norm_mul]
    rfl
    tactic => aesop
  apply (one_mul (_ : â„)) â–¸ Filter.Tendsto.mul_const _ _
  exact (norm_one (Î± := ğ•‚)) â–¸ Filter.Tendsto.norm
    (ordinaryHypergeometricSeries_ratio_tendsto_one_atTop a b c)

/-- The radius of convergence of `ordinaryHypergeometricSeries` is unity if none of the parameters
are non-positive integers. This proof uses a similar technique to
`formalMultilinearSeries_geometric_radius`. -/
theorem ordinaryHypergeometricSeries_radius_eq_one
    (habc : âˆ€ kn : â„•, â†‘kn â‰  -a âˆ§ â†‘kn â‰  -b âˆ§ â†‘kn â‰  -c) :
      (ordinaryHypergeometricSeries ğ”¸ a b c).radius = 1 := by
  apply le_antisymm <;> refine ENNReal.le_of_forall_nnreal_lt (fun r hr â†¦ ?_)
  Â· rw [â† ENNReal.coe_one, ENNReal.coe_le_coe]
    have := FormalMultilinearSeries.summable_norm_mul_pow _ hr
    contrapose! this
    apply not_summable_of_ratio_test_tendsto_gt_one this
    refine ordinaryHypergeometric_ratio_tendsto_nhds_atTop ğ”¸ a b c ?_ habc
    exact NNReal.coe_pos.2 (lt_trans zero_lt_one this)
  Â· rw [â† Nat.cast_one, ENNReal.coe_lt_natCast, Nat.cast_one] at hr
    by_cases hr' : r = 0
    Â· simp [hr']
    Â· apply FormalMultilinearSeries.le_radius_of_summable_norm
      apply summable_of_ratio_test_tendsto_lt_one hr
      Â· refine Eventually.of_forall (fun n â†¦ mul_ne_zero (norm_ne_zero_iff.2 ?_) ?_)
        Â· exact (ordinaryHypergeometricSeries_eq_zero_iff ğ”¸ a b c n).not.2 (by aesop)
        Â· exact (pow_ne_zero n <| NNReal.coe_ne_zero.2 hr')
      apply ordinaryHypergeometric_ratio_tendsto_nhds_atTop ğ”¸ a b c ?_ habc
      exact (Ne.intro hr').lt_of_le' (NNReal.coe_nonneg r)

end RCLike
