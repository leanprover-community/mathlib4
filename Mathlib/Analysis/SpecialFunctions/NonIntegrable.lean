/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.SpecialFunctions.Log.Deriv
import Mathlib.MeasureTheory.Integral.FundThmCalculus

#align_import analysis.special_functions.non_integrable from "leanprover-community/mathlib"@"55ec6e9af7d3e0043f57e394cb06a72f6275273e"

/-!
# Non integrable functions

In this file we prove that the derivative of a function that tends to infinity is not interval
integrable, see `not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_filter` and
`not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured`. Then we apply the
latter lemma to prove that the function `fun x => xâ»Â¹` is integrable on `a..b` if and only if
`a = b` or `0 âˆ‰ [a, b]`.

## Main results

* `not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured`: if `f` tends to infinity
  along `ğ“[â‰ ] c` and `f' = O(g)` along the same filter, then `g` is not interval integrable on any
  nontrivial integral `a..b`, `c âˆˆ [a, b]`.

* `not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_filter`: a version of
  `not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured` that works for one-sided
  neighborhoods;

* `not_intervalIntegrable_of_sub_inv_isBigO_punctured`: if `1 / (x - c) = O(f)` as `x â†’ c`, `x â‰  c`,
  then `f` is not interval integrable on any nontrivial interval `a..b`, `c âˆˆ [a, b]`;

* `intervalIntegrable_sub_inv_iff`, `intervalIntegrable_inv_iff`: integrability conditions for
  `(x - c)â»Â¹` and `xâ»Â¹`.

## Tags

integrable function
-/


open scoped MeasureTheory Topology Interval NNReal ENNReal

open MeasureTheory TopologicalSpace Set Filter Asymptotics intervalIntegral

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup F]

/-- If `f` is eventually differentiable along a nontrivial filter `l : Filter â„` that is generated
by convex sets, the norm of `f` tends to infinity along `l`, and `f' = O(g)` along `l`, where `f'`
is the derivative of `f`, then `g` is not integrable on any set `k` belonging to `l`.
Auxiliary version assuming that `E` is complete. -/
theorem not_integrableOn_of_tendsto_norm_atTop_of_deriv_isBigO_filter_aux
    [CompleteSpace E] {f : â„ â†’ E} {g : â„ â†’ F}
    {k : Set â„} (l : Filter â„) [NeBot l] [TendstoIxxClass Icc l l]
    (hl : k âˆˆ l) (hd : âˆ€á¶  x in l, DifferentiableAt â„ f x) (hf : Tendsto (fun x => â€–f xâ€–) l atTop)
    (hfg : deriv f =O[l] g) : Â¬IntegrableOn g k := by
  intro hgi
  obtain âŸ¨C, hCâ‚€, s, hsl, hsub, hfd, hgâŸ© :
    âˆƒ (C : â„) (_ : 0 â‰¤ C), âˆƒ s âˆˆ l, (âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, [[x, y]] âŠ† k) âˆ§
      (âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, âˆ€ z âˆˆ [[x, y]], DifferentiableAt â„ f z) âˆ§
        âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, âˆ€ z âˆˆ [[x, y]], â€–deriv f zâ€– â‰¤ C * â€–g zâ€– := by
    rcases hfg.exists_nonneg with âŸ¨C, Câ‚€, hCâŸ©
    have h : âˆ€á¶  x : â„ Ã— â„ in l.prod l,
        âˆ€ y âˆˆ [[x.1, x.2]], (DifferentiableAt â„ f y âˆ§ â€–deriv f yâ€– â‰¤ C * â€–g yâ€–) âˆ§ y âˆˆ k :=
      (tendsto_fst.uIcc tendsto_snd).eventually ((hd.and hC.bound).and hl).smallSets
    rcases mem_prod_self_iff.1 h with âŸ¨s, hsl, hsâŸ©
    simp only [prod_subset_iff, mem_setOf_eq] at hs
    exact âŸ¨C, Câ‚€, s, hsl, fun x hx y hy z hz => (hs x hx y hy z hz).2, fun x hx y hy z hz =>
      (hs x hx y hy z hz).1.1, fun x hx y hy z hz => (hs x hx y hy z hz).1.2âŸ©
  replace hgi : IntegrableOn (fun x â†¦ C * â€–g xâ€–) k := by exact hgi.norm.smul C
  obtain âŸ¨c, hc, d, hd, hltâŸ© : âˆƒ c âˆˆ s, âˆƒ d âˆˆ s, (â€–f câ€– + âˆ« y in k, C * â€–g yâ€–) < â€–f dâ€– := by
    rcases Filter.nonempty_of_mem hsl with âŸ¨c, hcâŸ©
    have : âˆ€á¶  x in l, (â€–f câ€– + âˆ« y in k, C * â€–g yâ€–) < â€–f xâ€– :=
      hf.eventually (eventually_gt_atTop _)
    exact âŸ¨c, hc, (this.and hsl).exists.imp fun d hd => âŸ¨hd.2, hd.1âŸ©âŸ©
  specialize hsub c hc d hd; specialize hfd c hc d hd
  replace hg : âˆ€ x âˆˆ Î™ c d, â€–deriv f xâ€– â‰¤ C * â€–g xâ€– :=
    fun z hz => hg c hc d hd z âŸ¨hz.1.le, hz.2âŸ©
  have hg_ae : âˆ€áµ x âˆ‚volume.restrict (Î™ c d), â€–deriv f xâ€– â‰¤ C * â€–g xâ€– :=
    (ae_restrict_mem measurableSet_uIoc).mono hg
  have hsub' : Î™ c d âŠ† k := Subset.trans Ioc_subset_Icc_self hsub
  have hfi : IntervalIntegrable (deriv f) volume c d := by
    rw [intervalIntegrable_iff]
    have : IntegrableOn (fun x â†¦ C * â€–g xâ€–) (Î™ c d) := IntegrableOn.mono hgi hsub' le_rfl
    exact Integrable.mono' this (aestronglyMeasurable_deriv _ _) hg_ae
  refine hlt.not_le (sub_le_iff_le_add'.1 ?_)
  calc
    â€–f dâ€– - â€–f câ€– â‰¤ â€–f d - f câ€– := norm_sub_norm_le _ _
    _ = â€–âˆ« x in c..d, deriv f xâ€– := congr_arg _ (integral_deriv_eq_sub hfd hfi).symm
    _ = â€–âˆ« x in Î™ c d, deriv f xâ€– := norm_integral_eq_norm_integral_Ioc _
    _ â‰¤ âˆ« x in Î™ c d, â€–deriv f xâ€– := norm_integral_le_integral_norm _
    _ â‰¤ âˆ« x in Î™ c d, C * â€–g xâ€– :=
      setIntegral_mono_on hfi.norm.def' (hgi.mono_set hsub') measurableSet_uIoc hg
    _ â‰¤ âˆ« x in k, C * â€–g xâ€– := by
      apply setIntegral_mono_set hgi
        (ae_of_all _ fun x => mul_nonneg hCâ‚€ (norm_nonneg _)) hsub'.eventuallyLE

theorem not_integrableOn_of_tendsto_norm_atTop_of_deriv_isBigO_filter
    {f : â„ â†’ E} {g : â„ â†’ F}
    {k : Set â„} (l : Filter â„) [NeBot l] [TendstoIxxClass Icc l l]
    (hl : k âˆˆ l) (hd : âˆ€á¶  x in l, DifferentiableAt â„ f x) (hf : Tendsto (fun x => â€–f xâ€–) l atTop)
    (hfg : deriv f =O[l] g) : Â¬IntegrableOn g k := by
  let a : E â†’â‚—áµ¢[â„] UniformSpace.Completion E := UniformSpace.Completion.toComplâ‚—áµ¢
  let f' := a âˆ˜ f
  have h'd : âˆ€á¶  x in l, DifferentiableAt â„ f' x := by
    filter_upwards [hd] with x hx using a.toContinuousLinearMap.differentiableAt.comp x hx
  have h'f : Tendsto (fun x => â€–f' xâ€–) l atTop := hf.congr (fun x â†¦ by simp [f'])
  have h'fg : deriv f' =O[l] g := by
    apply IsBigO.trans _ hfg
    rw [â† isBigO_norm_norm]
    suffices (fun x â†¦ â€–deriv f' xâ€–) =á¶ [l] (fun x â†¦ â€–deriv f xâ€–) by exact this.isBigO
    filter_upwards [hd] with x hx
    have : deriv f' x = a (deriv f x) := by
      rw [fderiv.comp_deriv x _ hx]
      Â· have : fderiv â„ a (f x) = a.toContinuousLinearMap := a.toContinuousLinearMap.fderiv
        simp only [this]
        rfl
      Â· exact a.toContinuousLinearMap.differentiableAt
    simp only [this]
    simp
  exact not_integrableOn_of_tendsto_norm_atTop_of_deriv_isBigO_filter_aux l hl h'd h'f h'fg

/-- If `f` is eventually differentiable along a nontrivial filter `l : Filter â„` that is generated
by convex sets, the norm of `f` tends to infinity along `l`, and `f' = O(g)` along `l`, where `f'`
is the derivative of `f`, then `g` is not integrable on any interval `a..b` such that
`[a, b] âˆˆ l`. -/
theorem not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_filter {f : â„ â†’ E} {g : â„ â†’ F}
    {a b : â„} (l : Filter â„) [NeBot l] [TendstoIxxClass Icc l l] (hl : [[a, b]] âˆˆ l)
    (hd : âˆ€á¶  x in l, DifferentiableAt â„ f x) (hf : Tendsto (fun x => â€–f xâ€–) l atTop)
    (hfg : deriv f =O[l] g) : Â¬IntervalIntegrable g volume a b := by
  rw [intervalIntegrable_iff']
  exact not_integrableOn_of_tendsto_norm_atTop_of_deriv_isBigO_filter _ hl hd hf hfg
set_option linter.uppercaseLean3 false in
#align not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_filter not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_filter

/-- If `a â‰  b`, `c âˆˆ [a, b]`, `f` is differentiable in the neighborhood of `c` within
`[a, b] \ {c}`, `â€–f xâ€– â†’ âˆ` as `x â†’ c` within `[a, b] \ {c}`, and `f' = O(g)` along
`ğ“[[a, b] \ {c}] c`, where `f'` is the derivative of `f`, then `g` is not interval integrable on
`a..b`. -/
theorem not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_within_diff_singleton
    {f : â„ â†’ E} {g : â„ â†’ F} {a b c : â„} (hne : a â‰  b) (hc : c âˆˆ [[a, b]])
    (h_deriv : âˆ€á¶  x in ğ“[[[a, b]] \ {c}] c, DifferentiableAt â„ f x)
    (h_infty : Tendsto (fun x => â€–f xâ€–) (ğ“[[[a, b]] \ {c}] c) atTop)
    (hg : deriv f =O[ğ“[[[a, b]] \ {c}] c] g) : Â¬IntervalIntegrable g volume a b := by
  obtain âŸ¨l, hl, hl', hle, hmemâŸ© :
    âˆƒ l : Filter â„, TendstoIxxClass Icc l l âˆ§ l.NeBot âˆ§ l â‰¤ ğ“ c âˆ§ [[a, b]] \ {c} âˆˆ l := by
    cases' (min_lt_max.2 hne).lt_or_lt c with hlt hlt
    Â· refine âŸ¨ğ“[<] c, inferInstance, inferInstance, inf_le_left, ?_âŸ©
      rw [â† Iic_diff_right]
      exact diff_mem_nhdsWithin_diff (Icc_mem_nhdsWithin_Iic âŸ¨hlt, hc.2âŸ©) _
    Â· refine âŸ¨ğ“[>] c, inferInstance, inferInstance, inf_le_left, ?_âŸ©
      rw [â† Ici_diff_left]
      exact diff_mem_nhdsWithin_diff (Icc_mem_nhdsWithin_Ici âŸ¨hc.1, hltâŸ©) _
  have : l â‰¤ ğ“[[[a, b]] \ {c}] c := le_inf hle (le_principal_iff.2 hmem)
  exact not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_filter l
    (mem_of_superset hmem diff_subset) (h_deriv.filter_mono this) (h_infty.mono_left this)
    (hg.mono this)
set_option linter.uppercaseLean3 false in
#align not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_within_diff_singleton not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_within_diff_singleton

/-- If `f` is differentiable in a punctured neighborhood of `c`, `â€–f xâ€– â†’ âˆ` as `x â†’ c` (more
formally, along the filter `ğ“[â‰ ] c`), and `f' = O(g)` along `ğ“[â‰ ] c`, where `f'` is the derivative
of `f`, then `g` is not interval integrable on any nontrivial interval `a..b` such that
`c âˆˆ [a, b]`. -/
theorem not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured {f : â„ â†’ E}
    {g : â„ â†’ F} {a b c : â„} (h_deriv : âˆ€á¶  x in ğ“[â‰ ] c, DifferentiableAt â„ f x)
    (h_infty : Tendsto (fun x => â€–f xâ€–) (ğ“[â‰ ] c) atTop) (hg : deriv f =O[ğ“[â‰ ] c] g) (hne : a â‰  b)
    (hc : c âˆˆ [[a, b]]) : Â¬IntervalIntegrable g volume a b :=
  have : ğ“[[[a, b]] \ {c}] c â‰¤ ğ“[â‰ ] c := nhdsWithin_mono _ inter_subset_right
  not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_within_diff_singleton hne hc
    (h_deriv.filter_mono this) (h_infty.mono_left this) (hg.mono this)
set_option linter.uppercaseLean3 false in
#align not_interval_integrable_of_tendsto_norm_at_top_of_deriv_is_O_punctured not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured

/-- If `f` grows in the punctured neighborhood of `c : â„` at least as fast as `1 / (x - c)`,
then it is not interval integrable on any nontrivial interval `a..b`, `c âˆˆ [a, b]`. -/
theorem not_intervalIntegrable_of_sub_inv_isBigO_punctured {f : â„ â†’ F} {a b c : â„}
    (hf : (fun x => (x - c)â»Â¹) =O[ğ“[â‰ ] c] f) (hne : a â‰  b) (hc : c âˆˆ [[a, b]]) :
    Â¬IntervalIntegrable f volume a b := by
  have A : âˆ€á¶  x in ğ“[â‰ ] c, HasDerivAt (fun x => Real.log (x - c)) (x - c)â»Â¹ x := by
    filter_upwards [self_mem_nhdsWithin] with x hx
    simpa using ((hasDerivAt_id x).sub_const c).log (sub_ne_zero.2 hx)
  have B : Tendsto (fun x => â€–Real.log (x - c)â€–) (ğ“[â‰ ] c) atTop := by
    refine tendsto_abs_atBot_atTop.comp (Real.tendsto_log_nhdsWithin_zero.comp ?_)
    rw [â† sub_self c]
    exact ((hasDerivAt_id c).sub_const c).tendsto_punctured_nhds one_ne_zero
  exact not_intervalIntegrable_of_tendsto_norm_atTop_of_deriv_isBigO_punctured
    (A.mono fun x hx => hx.differentiableAt) B
    (hf.congr' (A.mono fun x hx => hx.deriv.symm) EventuallyEq.rfl) hne hc
set_option linter.uppercaseLean3 false in
#align not_interval_integrable_of_sub_inv_is_O_punctured not_intervalIntegrable_of_sub_inv_isBigO_punctured

/-- The function `fun x => (x - c)â»Â¹` is integrable on `a..b` if and only if
`a = b` or `c âˆ‰ [a, b]`. -/
@[simp]
theorem intervalIntegrable_sub_inv_iff {a b c : â„} :
    IntervalIntegrable (fun x => (x - c)â»Â¹) volume a b â†” a = b âˆ¨ c âˆ‰ [[a, b]] := by
  constructor
  Â· refine fun h => or_iff_not_imp_left.2 fun hne hc => ?_
    exact not_intervalIntegrable_of_sub_inv_isBigO_punctured (isBigO_refl _ _) hne hc h
  Â· rintro (rfl | hâ‚€)
    Â· exact IntervalIntegrable.refl
    refine ((continuous_sub_right c).continuousOn.invâ‚€ ?_).intervalIntegrable
    exact fun x hx => sub_ne_zero.2 <| ne_of_mem_of_not_mem hx hâ‚€
#align interval_integrable_sub_inv_iff intervalIntegrable_sub_inv_iff

/-- The function `fun x => xâ»Â¹` is integrable on `a..b` if and only if
`a = b` or `0 âˆ‰ [a, b]`. -/
@[simp]
theorem intervalIntegrable_inv_iff {a b : â„} :
    IntervalIntegrable (fun x => xâ»Â¹) volume a b â†” a = b âˆ¨ (0 : â„) âˆ‰ [[a, b]] := by
  simp only [â† intervalIntegrable_sub_inv_iff, sub_zero]
#align interval_integrable_inv_iff intervalIntegrable_inv_iff

/-- The function `fun x â†¦ xâ»Â¹` is not integrable on any interval `[a, +âˆ)`. -/
theorem not_IntegrableOn_Ici_inv {a : â„} :
    Â¬ IntegrableOn (fun x => xâ»Â¹) (Ici a) := by
  have A : âˆ€á¶  x in atTop, HasDerivAt (fun x => Real.log x) xâ»Â¹ x := by
    filter_upwards [Ioi_mem_atTop 0] with x hx using Real.hasDerivAt_log (ne_of_gt hx)
  have B : Tendsto (fun x => â€–Real.log xâ€–) atTop atTop :=
    tendsto_norm_atTop_atTop.comp Real.tendsto_log_atTop
  exact not_integrableOn_of_tendsto_norm_atTop_of_deriv_isBigO_filter atTop (Ici_mem_atTop a)
    (A.mono (fun x hx â†¦ hx.differentiableAt)) B
    (Filter.EventuallyEq.isBigO (A.mono (fun x hx â†¦ hx.deriv)))

/-- The function `fun x â†¦ xâ»Â¹` is not integrable on any interval `(a, +âˆ)`. -/
theorem not_IntegrableOn_Ioi_inv {a : â„} :
    Â¬ IntegrableOn (Â·â»Â¹) (Ioi a) := by
  simpa only [IntegrableOn, restrict_Ioi_eq_restrict_Ici] using not_IntegrableOn_Ici_inv
