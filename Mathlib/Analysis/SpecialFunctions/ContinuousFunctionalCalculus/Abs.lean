/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jireh Loreaux
-/

import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Commute


/-!
# Absolute value defined via the continuous functional calculus

This file defines the absolute value via the non-unital continuous functional calculus
and provides basic API.

## Main declarations

+ `CFC.abs`: The absolute value as `abs a := CFC.sqrt (star a * a)`.

-/

variable {ğ•œ A : Type*}

open scoped NNReal
open CFC

namespace CFC

section NonUnital

section Real

variable [NonUnitalRing A] [StarRing A] [TopologicalSpace A]
  [Module â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]
  [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
  [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A]

/-- The absolute value defined via the non-unital continuous functional calculus. -/
noncomputable def abs (a : A) := sqrt (star a * a)

@[simp, grind =]
lemma abs_neg (a : A) : abs (-a) = abs a := by
  simp [abs]

@[simp, grind â†]
lemma abs_nonneg (a : A) : 0 â‰¤ abs a := sqrt_nonneg _

lemma abs_star (a : A) (ha : IsStarNormal a := by cfc_tac) : abs (star a) = abs a := by
  simp [abs, star_comm_self']

@[simp, grind =]
lemma abs_zero : abs (0 : A) = 0 := by
  simp [abs]

variable [IsTopologicalRing A] [T2Space A]

lemma abs_mul_abs (a : A) : abs a * abs a = star a * a :=
  sqrt_mul_sqrt_self _ <| star_mul_self_nonneg _

/- The hypotheses could be weakened to `Commute (star a * a) b`, but
in that case one should simply use `Commute.cfcâ‚™_nnreal` directly.

The point of this theorem is to have simpler hypotheses. -/
lemma _root_.Commute.cfcAbs_left {a b : A} (hâ‚ : Commute a b) (hâ‚‚ : Commute a (star b)) :
    Commute (abs a) b :=
  .cfcâ‚™_nnreal (by simp_all [hâ‚‚.star_left]) _

/- The hypotheses could be weakened to `Commute (star a * a) b`, but
in that case one should simply use `Commute.cfcâ‚™_nnreal` directly.

The point of this theorem is to have simpler hypotheses. -/
lemma _root_.Commute.cfcAbs_right {a b : A} (hâ‚ : Commute a b) (hâ‚‚ : Commute a (star b)) :
    Commute b (abs a) :=
  hâ‚.cfcAbs_left hâ‚‚ |>.symm

/- The hypotheses could be weakened to `Commute (star a * a) (star b * b)`, but
in that case one should simply use `Commute.cfcâ‚™_nnreal` (twice) directly.

The point of this theorem is to have simpler hypotheses. -/
lemma _root_.Commute.cfcAbs_cfcAbs {a b : A} (hâ‚ : Commute a b) (hâ‚‚ : Commute a (star b)) :
    Commute (abs a) (abs b) :=
  Commute.cfcâ‚™_nnreal (by simp_all [hâ‚‚.star_left]) _ |>.symm.cfcâ‚™_nnreal _ |>.symm

/-- Normal elements commute with their absolute value. -/
lemma commute_abs_self (a : A) (ha : IsStarNormal a := by cfc_tac) :
    Commute (abs a) a :=
  .cfcAbs_left (.refl a) ha.star_comm_self.symm

lemma _root_.Commute.cfcAbs_mul_eq {a b : A} (hâ‚ : Commute a b) (hâ‚‚ : Commute a (star b)) :
    abs (a * b) = abs a * abs b := by
  have hab := hâ‚.cfcAbs_cfcAbs hâ‚‚
  rw [abs, CFC.sqrt_eq_iff _ _ (star_mul_self_nonneg _)
    (hab.mul_nonneg (abs_nonneg a) (abs_nonneg b)), hab.eq, hab.mul_mul_mul_comm,
    abs_mul_abs, abs_mul_abs, star_mul, hâ‚‚.star_left.symm.mul_mul_mul_comm, hâ‚.eq]

lemma abs_mul_self (a : A) (ha : IsStarNormal a := by cfc_tac) :
    abs (a * a) = star a * a := by
  rw [Commute.cfcAbs_mul_eq (.refl a) ha.star_comm_self.symm, abs_mul_abs]

lemma abs_nnrpow_two (a : A) : abs a ^ (2 : â„â‰¥0) = star a * a := by
  simp only [abs_nonneg, nnrpow_two]
  apply abs_mul_abs

lemma abs_nnrpow_two_mul (a : A) (x : â„â‰¥0) :
    abs a ^ (2 * x) = (star a * a) ^ x := by rw [â† nnrpow_nnrpow, abs_nnrpow_two]

lemma abs_nnrpow (a : A) (x : â„â‰¥0) :
    abs a ^ x = (star a * a) ^ (x / 2) := by
  simp only [â† abs_nnrpow_two_mul, mul_div_left_comm, ne_eq, OfNat.ofNat_ne_zero,
    not_false_eq_true, div_self, mul_one]

@[grind =]
lemma abs_of_nonneg (a : A) (ha : 0 â‰¤ a := by cfc_tac) : abs a = a := by
  rw [abs, ha.star_eq, sqrt_mul_self a ha]

lemma abs_of_nonpos (a : A) (ha : a â‰¤ 0 := by cfc_tac) : abs a = -a := by
  simpa using abs_of_nonneg (-a)

lemma abs_eq_cfcâ‚™_norm (a : A) (ha : IsSelfAdjoint a := by cfc_tac) :
    abs a = cfcâ‚™ (â€–Â·â€–) a := by
  conv_lhs =>
    rw [abs, ha.star_eq, sqrt_eq_real_sqrt .., â† cfcâ‚™_id' â„ a, â† cfcâ‚™_mul .., â† cfcâ‚™_comp' ..]
  simp [â† sq, Real.sqrt_sq_eq_abs]

protected lemma posPart_add_negPart (a : A) (ha : IsSelfAdjoint a := by cfc_tac) :
    aâº + aâ» = abs a := by
  rw [CFC.posPart_def, CFC.negPart_def, â† cfcâ‚™_add .., abs_eq_cfcâ‚™_norm a ha]
  exact cfcâ‚™_congr fun x hx â†¦ posPart_add_negPart x

lemma abs_sub_self (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : abs a - a = 2 â€¢ aâ» := by
 simpa [two_smul] using
    congr($(CFC.posPart_add_negPart a) - $(CFC.posPart_sub_negPart a)).symm

lemma abs_add_self (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : abs a + a = 2 â€¢ aâº := by
  simpa [two_smul] using
    congr($(CFC.posPart_add_negPart a) + $(CFC.posPart_sub_negPart a)).symm

@[simp, grind =]
lemma cfcAbs_cfcAbs (a : A) : abs (abs a) = abs a := abs_of_nonneg ..

variable [StarModule â„ A]

@[simp, grind =]
lemma abs_smul_nonneg {R : Type*} [Semiring R] [SMulWithZero R â„â‰¥0] [SMul R A]
    [IsScalarTower R â„â‰¥0 A] (r : R) (a : A) :
    abs (r â€¢ a) = r â€¢ abs a := by
  suffices âˆ€ r : â„â‰¥0, abs (r â€¢ a) = r â€¢ abs a by simpa using this (r â€¢ 1)
  intro r
  rw [abs, sqrt_eq_iff _ _ (star_mul_self_nonneg _) (smul_nonneg (by positivity) (abs_nonneg _))]
  simp [mul_smul_comm, smul_mul_assoc, abs_mul_abs]

end Real

section RCLike

variable {p : A â†’ Prop} [RCLike ğ•œ]
  [NonUnitalRing A] [TopologicalSpace A] [Module ğ•œ A]
  [StarRing A] [PartialOrder A] [StarOrderedRing A]
  [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
  [NonUnitalContinuousFunctionalCalculus ğ•œ A p]

open ComplexOrder

lemma _root_.cfcâ‚™_norm_sq_nonneg (f : ğ•œ â†’ ğ•œ) (a : A) : 0 â‰¤ cfcâ‚™ (fun z â†¦ star (f z) * (f z)) a :=
  cfcâ‚™_nonneg fun _ _ â†¦ star_mul_self_nonneg _

lemma _root_.cfcâ‚™_norm_nonneg (f : ğ•œ â†’ ğ•œ) (a : A) : 0 â‰¤ cfcâ‚™ (â€–f Â·â€– : ğ•œ â†’ ğ•œ) a :=
  cfcâ‚™_nonneg fun _ _ â†¦ by simp

variable [Module â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]
  [NonnegSpectrumClass â„ A] [IsTopologicalRing A] [T2Space A]
  [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]

variable [StarModule ğ•œ A] [StarModule â„ A] [IsScalarTower â„ ğ•œ A] in
@[simp]
lemma abs_smul (r : ğ•œ) (a : A) : abs (r â€¢ a) = â€–râ€– â€¢ abs a := by
  trans abs (â€–râ€– â€¢ a)
  Â· simp only [abs, mul_smul_comm, smul_mul_assoc, star_smul, â† smul_assoc,
      RCLike.real_smul_eq_coe_smul (K := ğ•œ)]
    simp [-algebraMap_smul, â† smul_mul_assoc, â† mul_comm (starRingEnd _ _), RCLike.conj_mul, sq]
  Â· lift â€–râ€– to â„â‰¥0 using norm_nonneg _ with r
    simp [â† NNReal.smul_def]

variable (ğ•œ) in
lemma abs_eq_cfcâ‚™_coe_norm (a : A) (ha : p a := by cfc_tac) :
    abs a = cfcâ‚™ (fun z : ğ•œ â†¦ (â€–zâ€– : ğ•œ)) a := by
  rw [abs, sqrt_eq_iff _ _ (hb := cfcâ‚™_norm_nonneg _ _), â† cfcâ‚™_mul ..]
  conv_rhs => rw [â† cfcâ‚™_id' ğ•œ a, â† cfcâ‚™_star, â† cfcâ‚™_mul ..]
  simp [RCLike.conj_mul, sq]

lemma _root_.cfcâ‚™_comp_norm (f : ğ•œ â†’ ğ•œ) (a : A) (ha : p a := by cfc_tac)
    (hf : ContinuousOn f ((fun z â†¦ (â€–zâ€– : ğ•œ)) '' quasispectrum ğ•œ a) := by cfc_cont_tac) :
    cfcâ‚™ (f â€–Â·â€–) a = cfcâ‚™ f (abs a) := by
  obtain (hf0 | hf0) := em (f 0 = 0)
  Â· rw [cfcâ‚™_comp' f (â€–Â·â€–) a, â† abs_eq_cfcâ‚™_coe_norm _ a]
  Â· rw [cfcâ‚™_apply_of_not_map_zero _ hf0,
      cfcâ‚™_apply_of_not_map_zero _ (fun h â†¦ (hf0 <| by simpa using h).elim)]

lemma quasispectrum_abs (a : A) (ha : p a := by cfc_tac) :
    quasispectrum ğ•œ (abs a) = (fun z â†¦ (â€–zâ€– : ğ•œ)) '' quasispectrum ğ•œ a := by
  rw [abs_eq_cfcâ‚™_coe_norm ğ•œ a ha, cfcâ‚™_map_quasispectrum ..]

end RCLike

end NonUnital

section Unital

section Real

variable [Ring A] [StarRing A] [TopologicalSpace A] [Algebra â„ A]
  [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]
  [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A]
  [IsTopologicalRing A] [T2Space A]

lemma abs_eq_cfc_norm (a : A) (ha : IsSelfAdjoint a := by cfc_tac) :
    abs a = cfc (â€–Â·â€–) a := by
  rw [abs_eq_cfcâ‚™_norm _, cfcâ‚™_eq_cfc]

@[simp]
lemma abs_one : abs (1 : A) = 1 := by
  simp [abs]

variable [StarModule â„ A]

@[simp]
lemma abs_algebraMap_nnreal (x : â„â‰¥0) : abs (algebraMap â„â‰¥0 A x) = algebraMap â„â‰¥0 A x := by
  simp [Algebra.algebraMap_eq_smul_one]

@[simp]
lemma abs_natCast (n : â„•) : abs (n : A) = n := by
  simpa only [map_natCast, Nat.abs_cast] using abs_algebraMap_nnreal (n : â„â‰¥0)

@[simp]
lemma abs_ofNat (n : â„•) [n.AtLeastTwo] : abs (ofNat(n) : A) = ofNat(n) := by
  simpa using abs_natCast n

@[simp]
lemma abs_intCast (n : â„¤) : abs (n : A) = |n| := by
  cases n with
  | ofNat _ => simp
  | negSucc n =>
    rw [Int.cast_negSucc, abs_neg, abs_natCast, â† Int.cast_natCast]
    congr

end Real

section RCLike

variable {p : A â†’ Prop} [RCLike ğ•œ]
  [Ring A] [TopologicalSpace A] [StarRing A] [PartialOrder A]
  [StarOrderedRing A] [Algebra ğ•œ A]
  [ContinuousFunctionalCalculus ğ•œ A p]
  [Algebra â„ A] [NonnegSpectrumClass â„ A] [IsTopologicalRing A] [T2Space A]
  [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]

variable [StarModule ğ•œ A] [StarModule â„ A] [IsScalarTower â„ ğ•œ A] in
@[simp]
lemma abs_algebraMap (c : ğ•œ) : abs (algebraMap ğ•œ A c) = algebraMap â„ A â€–câ€– := by
  simp [Algebra.algebraMap_eq_smul_one]

lemma _root_.cfc_comp_norm (f : ğ•œ â†’ ğ•œ) (a : A) (ha : p a := by cfc_tac)
    (hf : ContinuousOn f ((fun z â†¦ (â€–zâ€– : ğ•œ)) '' spectrum ğ•œ a) := by cfc_cont_tac) :
    cfc (f â€–Â·â€–) a = cfc f (abs a) := by
  rw [abs_eq_cfcâ‚™_coe_norm ğ•œ a, cfcâ‚™_eq_cfc, â† cfc_comp' ..]

lemma abs_sq (a : A) : (abs a) ^ 2 = star a * a := by
  rw [sq, abs_mul_abs]

lemma spectrum_abs (a : A) (ha : p a := by cfc_tac) :
    spectrum ğ•œ (abs a) = (fun z â†¦ (â€–zâ€– : ğ•œ)) '' spectrum ğ•œ a := by
  rw [abs_eq_cfcâ‚™_coe_norm ğ•œ a, cfcâ‚™_eq_cfc, cfc_map_spectrum ..]

end RCLike

end Unital

section CStar

/- This section requires `A` to be a `CStarRing` -/

variable [NonUnitalNormedRing A] [StarRing A] [CStarRing A]
  [NormedSpace â„ A] [SMulCommClass â„ A A] [IsScalarTower â„ A A]
  [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
  [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A]

open CFC

@[simp, grind =]
lemma abs_eq_zero_iff {a : A} : abs a = 0 â†” a = 0 := by
  rw [CFC.abs, sqrt_eq_zero_iff _, CStarRing.star_mul_self_eq_zero_iff]

@[simp, grind =]
lemma norm_abs {a : A} : â€–abs aâ€– = â€–aâ€– := by
  rw [â† sq_eq_sqâ‚€ (norm_nonneg _) (norm_nonneg _), sq, sq, â† CStarRing.norm_star_mul_self,
    (abs_nonneg _).star_eq, CFC.abs_mul_abs, CStarRing.norm_star_mul_self]

end CStar

end CFC
