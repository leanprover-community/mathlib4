/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.SpecialFunctions.Exponential
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique
import Mathlib.Topology.ContinuousMap.ContinuousSqrt
import Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# The exponential and logarithm based on the continuous functional calculus

This file defines the logarithm via the continuous functional calculus (CFC) and builds its API.
This allows one to take logs of matrices, operators, elements of a Câ‹†-algebra, etc.

It also shows that exponentials defined via the continuous functional calculus are equal to
`NormedSpace.exp` (defined via power series) whenever the former are not junk values.

## Main declarations

+ `CFC.log`: the real log function based on the CFC, i.e. `cfc Real.log`
+ `CFC.exp_eq_normedSpace_exp`: exponentials based on the CFC are equal to exponentials based
  on power series.
+ `CFC.log_exp` and `CFC.exp_log`: `CFC.log` and `NormedSpace.exp â„` are inverses of each other.

## Implementation notes

Since `cfc Real.exp` and `cfc Complex.exp` are strictly less general than `NormedSpace.exp`
(defined via power series), we only give minimal API for these here in order to relate
`NormedSpace.exp` to functions defined via the CFC. In particular, we don't give separate
definitions for them.

## TODO

+ Show that `log (a * b) = log a + log b` whenever `a` and `b` commute (and the same for indexed
  products).
+ Relate `CFC.log` to `rpow`, `zpow`, `sqrt`, `inv`.
-/

open NormedSpace

section general_exponential
variable {ð•œ : Type*} {Î± : Type*} [RCLike ð•œ] [TopologicalSpace Î±] [CompactSpace Î±]

lemma NormedSpace.exp_continuousMap_eq (f : C(Î±, ð•œ)) :
    exp ð•œ f = (âŸ¨exp ð•œ âˆ˜ f, exp_continuous.comp f.continuousâŸ© : C(Î±, ð•œ)) := by
  ext a
  simp only [NormedSpace.exp, FormalMultilinearSeries.sum]
  have h_sum := NormedSpace.expSeries_summable (ð•‚ := ð•œ) f
  simp_rw [â† ContinuousMap.tsum_apply h_sum a, NormedSpace.expSeries_apply_eq]
  simp [NormedSpace.exp_eq_tsum]

end general_exponential

namespace CFC
section RCLikeNormed

variable {ð•œ : Type*} {A : Type*} [RCLike ð•œ] {p : A â†’ Prop} [NormedRing A]
  [StarRing A] [IsTopologicalRing A] [NormedAlgebra ð•œ A] [ContinuousFunctionalCalculus ð•œ A p]

open scoped ContinuousFunctionalCalculus in
lemma exp_eq_normedSpace_exp {a : A} (ha : p a := by cfc_tac) :
    cfc (exp ð•œ : ð•œ â†’ ð•œ) a = exp ð•œ a := by
  conv_rhs => rw [â† cfc_id ð•œ a ha, cfc_apply id a ha]
  have h := (cfcHom_isClosedEmbedding (R := ð•œ) (show p a from ha)).continuous
  have _ : ContinuousOn (exp ð•œ) (spectrum ð•œ a) := exp_continuous.continuousOn
  simp_rw [â† map_exp ð•œ _ h, cfc_apply (exp ð•œ) a ha]
  congr 1
  ext
  simp [exp_continuousMap_eq]

end RCLikeNormed

section RealNormed

variable {A : Type*} [NormedRing A] [StarRing A]
  [IsTopologicalRing A] [NormedAlgebra â„ A]
  [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]

lemma real_exp_eq_normedSpace_exp {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    cfc Real.exp a = exp â„ a :=
  Real.exp_eq_exp_â„ â–¸ exp_eq_normedSpace_exp ha

@[aesop safe apply (rule_sets := [CStarAlgebra])]
lemma _root_.IsSelfAdjoint.exp_nonneg {ð•œ : Type*} [Field ð•œ] [Algebra ð•œ A]
    [PartialOrder A] [StarOrderedRing A] {a : A} (ha : IsSelfAdjoint a) :
    0 â‰¤ exp ð•œ a := by
  rw [exp_eq_exp ð•œ â„, â† real_exp_eq_normedSpace_exp]
  exact cfc_nonneg fun x _ => Real.exp_nonneg x

end RealNormed

section ComplexNormed

variable {A : Type*} {p : A â†’ Prop} [NormedRing A] [StarRing A]
  [NormedAlgebra â„‚ A] [ContinuousFunctionalCalculus â„‚ A p]

lemma complex_exp_eq_normedSpace_exp {a : A} (ha : p a := by cfc_tac) :
    cfc Complex.exp a = exp â„‚ a :=
  Complex.exp_eq_exp_â„‚ â–¸ exp_eq_normedSpace_exp ha

end ComplexNormed


section real_log

open scoped ComplexOrder

variable {A : Type*} [NormedRing A] [StarRing A] [NormedAlgebra â„ A]
  [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]

/-- The real logarithm, defined via the continuous functional calculus. This can be used on
matrices, operators on a Hilbert space, elements of a Câ‹†-algebra, etc. -/
noncomputable def log (a : A) : A := cfc Real.log a

@[simp, grind]
protected lemma _root_.IsSelfAdjoint.log {a : A} : IsSelfAdjoint (log a) := cfc_predicate _ a

@[simp, grind] lemma log_zero : log (0 : A) = 0 := by simp [log]

@[simp, grind] lemma log_one : log (1 : A) = 0 := by simp [log]

@[simp, grind =]
lemma log_algebraMap {r : â„} : log (algebraMap â„ A r) = algebraMap â„ A (Real.log r) := by
  simp [log]

lemma log_smul {r : â„} (a : A) (haâ‚‚ : âˆ€ x âˆˆ spectrum â„ a, x â‰  0) (hr : r â‰  0)
    (haâ‚ : IsSelfAdjoint a := by cfc_tac) :
    log (r â€¢ a) = algebraMap â„ A (Real.log r) + log a := by
  rw [log, â† cfc_smul_id (R := â„) r a, â† cfc_comp Real.log (r â€¢ Â·) a, log]
  calc
    _ = cfc (fun z => Real.log r + Real.log z) a :=
      cfc_congr (Real.log_mul hr <| haâ‚‚ Â· Â·)
    _ = _ := by rw [cfc_const_add _ _ _]

@[grind =]
lemma log_smul' [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A] {r : â„} (a : A)
    (hr : 0 < r) (ha : IsStrictlyPositive a := by cfc_tac) :
    log (r â€¢ a) = algebraMap â„ A (Real.log r) + log a := by
  grind [log_smul]

lemma log_pow (n : â„•) (a : A) (haâ‚‚ : âˆ€ x âˆˆ spectrum â„ a, x â‰  0)
    (haâ‚ : IsSelfAdjoint a := by cfc_tac) : log (a ^ n) = n â€¢ log a := by
  have haâ‚‚' : ContinuousOn Real.log (spectrum â„ a) := by fun_prop (disch := assumption)
  have haâ‚‚'' : ContinuousOn Real.log ((Â· ^ n) '' spectrum â„ a)  := by fun_prop (disch := aesop)
  rw [log, â† cfc_pow_id (R := â„) a n haâ‚, â† cfc_comp' Real.log (Â· ^ n) a haâ‚‚'', log]
  simp_rw [Real.log_pow, â† Nat.cast_smul_eq_nsmul â„ n, cfc_const_mul (n : â„) Real.log a haâ‚‚']

@[grind =]
lemma log_pow' [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A] (n : â„•) (a : A)
    (ha : IsStrictlyPositive a := by cfc_tac) :
    log (a ^ n) = n â€¢ log a := by
  grind [log_pow]

open NormedSpace in
@[grind =]
lemma log_exp (a : A) (ha : IsSelfAdjoint a := by cfc_tac) : log (exp â„ a) = a := by
  have hcont : ContinuousOn Real.log (Real.exp '' spectrum â„ a) := by fun_prop (disch := simp)
  rw [log, â† real_exp_eq_normedSpace_exp, â† cfc_comp' Real.log Real.exp a hcont]
  simp [cfc_id' (R := â„) a]

open NormedSpace in
@[grind =]
lemma exp_log [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A] (a : A)
    (ha : IsStrictlyPositive a := by cfc_tac) : exp â„ (log a) = a := by
  have haâ‚‚ : âˆ€ x âˆˆ spectrum â„ a, x â‰  0 := by grind
  rw [â† real_exp_eq_normedSpace_exp .log, log, â† cfc_comp' Real.exp Real.log a (by fun_prop)]
  conv_rhs => rw [â† cfc_id (R := â„) a]
  refine cfc_congr fun x hx => ?_
  grind [Real.exp_log]

end real_log
end CFC
