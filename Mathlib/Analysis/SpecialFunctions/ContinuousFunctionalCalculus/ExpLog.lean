/-
Copyright (c) 2024 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/

import Mathlib.Analysis.NormedSpace.Star.ContinuousFunctionalCalculus.Instances
import Mathlib.Analysis.SpecialFunctions.Log.Basic
import Mathlib.Analysis.NormedSpace.Exponential

/-!
# The exp and log functions based on the continuous functional calculus

This file defines the log function via the continuous functional calculus and build its API.
This allows one to take logs of matrices, operators, elements of a C‚ãÜ-algebra, etc.

It also shows that exponentials defined via the CFC are equal to `NormedSpace.exp` (defined via
power series) whenever the former are not junk values.

## Main declarations

+ `CFC.log`: the real log function based on the CFC, i.e. `cfc Real.log`
+ `CFC.exp_eq_normedSpace_exp`: exponentials based on the CFC are equal to exponentials based
  on power series.
+ `CFC.log_exp` and `CFC.exp_log`: `CFC.log` and `NormedSpace.exp ‚Ñù` are inverses of each other.

## Implementation notes

Since `cfc Real.exp` and `cfc Complex.exp` are strictly less general than `NormedSpace.exp`
(defined via power series), we only give minimal API for these here in order to relate
`NormedSpace.exp` to functions defined via the CFC. In particular, we don't give separate
definitions for them.
-/

open NormedSpace

section general_exponential
variable {ùïú : Type*} {Œ± : Type*} [RCLike ùïú] [TopologicalSpace Œ±] [CompactSpace Œ±]

lemma NormedSpace.exp_continuousMap_eq (f : C(Œ±, ùïú)) :
    exp ùïú f = (‚ü®exp ùïú ‚àò f, Continuous.comp exp_continuous f.continuous‚ü© : C(Œ±, ùïú)) := by
  ext a
  simp only [Function.comp_apply, NormedSpace.exp, FormalMultilinearSeries.sum]
  have h_sum := NormedSpace.expSeries_summable (ùïÇ := ùïú) f
  simp_rw [‚Üê ContinuousMap.tsum_apply h_sum a, NormedSpace.expSeries_apply_eq]
  simp [NormedSpace.exp_eq_tsum]

end general_exponential

namespace CFC
section RCLikeNormed

variable {ùïú : Type*} {A : Type*} [RCLike ùïú] {p : A ‚Üí Prop} [PartialOrder A] [NormedRing A]
  [StarRing A] [StarOrderedRing A] [TopologicalRing A] [NormedAlgebra ùïú A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ùïú p]
  [UniqueContinuousFunctionalCalculus ùïú A]

lemma exp_eq_normedSpace_exp {a : A} (ha : p a) :
    cfc (exp ùïú : ùïú ‚Üí ùïú) a = exp ùïú a := by
  have h‚ÇÅ : a = cfc (R := ùïú) id a := (cfc_id ùïú a ha).symm
  conv_rhs => rw [h‚ÇÅ, cfc_apply id a ha]
  let œÜ := cfcHom (R := ùïú) ha
  have h‚ÇÇ : Continuous œÜ := (cfcHom_closedEmbedding ha).continuous
  have _ : ContinuousOn (exp ùïú) (spectrum ùïú a) := Continuous.continuousOn exp_continuous
  simp_rw [‚Üê map_exp ùïú œÜ h‚ÇÇ, cfc_apply (exp ùïú) a ha, œÜ]
  congr 1
  ext
  simp [exp_continuousMap_eq]

end RCLikeNormed

section RealNormed

variable {A : Type*} {p : A ‚Üí Prop} [PartialOrder A] [NormedRing A] [StarRing A] [StarOrderedRing A]
  [TopologicalRing A] [NormedAlgebra ‚Ñù A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ‚Ñù p]
  [UniqueContinuousFunctionalCalculus ‚Ñù A]

lemma real_exp_eq_normedSpace_exp {a : A} (ha : p a) :
    cfc Real.exp a = exp ‚Ñù a := by rw [Real.exp_eq_exp_‚Ñù]; exact exp_eq_normedSpace_exp ha

end RealNormed

section ComplexNormed

variable {A : Type*} {p : A ‚Üí Prop} [PartialOrder A] [NormedRing A] [StarRing A] [StarOrderedRing A]
  [TopologicalRing A] [NormedAlgebra ‚ÑÇ A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ‚ÑÇ p]
  [UniqueContinuousFunctionalCalculus ‚ÑÇ A]

lemma complex_exp_eq_normedSpace_exp {a : A} (ha : p a) :
    cfc Complex.exp a = exp ‚ÑÇ a := by rw [Complex.exp_eq_exp_‚ÑÇ]; exact exp_eq_normedSpace_exp ha

end ComplexNormed


section real_log

open scoped ComplexOrder

variable {A : Type*} [PartialOrder A] [NormedRing A] [StarRing A] [StarOrderedRing A]
  [TopologicalRing A] [NormedAlgebra ‚Ñù A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ‚Ñù (IsSelfAdjoint : A ‚Üí Prop)]
  [UniqueContinuousFunctionalCalculus ‚Ñù A]

noncomputable def log (a : A) : A := cfc Real.log a

lemma isSelfAdjoint_log {a : A} : IsSelfAdjoint (log a) := cfc_predicate _ a

lemma log_exp {a : A} (ha : IsSelfAdjoint a) : log (NormedSpace.exp ‚Ñù a) = a := by
  have hcont : ContinuousOn Real.log (Real.exp '' spectrum ‚Ñù a) := by
    refine ContinuousOn.log (continuousOn_id' _) fun x hx => ?_
    rw [Set.mem_image] at hx
    obtain ‚ü®z, hz‚ü© := hx
    rw [‚Üê hz.2]
    exact Real.exp_ne_zero z
  have hcomp : Real.log ‚àò Real.exp = id := by ext; simp
  rw [log, ‚Üê real_exp_eq_normedSpace_exp ha, ‚Üê cfc_comp Real.log Real.exp a ha hcont]
  rw [hcomp, cfc_id (R := ‚Ñù) a ha]

-- TODO: Relate the hypothesis to a notion of strict positivity
lemma exp_log {a : A} (ha‚ÇÅ : IsSelfAdjoint a := by cfc_tac) (ha‚ÇÇ : ‚àÄ x ‚àà spectrum ‚Ñù a, 0 < x) :
    NormedSpace.exp ‚Ñù (log a) = a := by
  have ha‚ÇÉ : ContinuousOn Real.log (spectrum ‚Ñù a) := by
    refine ContinuousOn.mono Real.continuousOn_log fun x hx => ?_
    rw [Set.mem_compl_singleton_iff]
    exact ne_of_gt <| ha‚ÇÇ x hx
  have hcont : ContinuousOn Real.exp (Real.log '' spectrum ‚Ñù a) := by fun_prop
  rw [‚Üê real_exp_eq_normedSpace_exp isSelfAdjoint_log,
      log, ‚Üê cfc_comp Real.exp Real.log a ha‚ÇÅ hcont ha‚ÇÉ]
  conv_rhs => rw [‚Üê cfc_id (R := ‚Ñù) a ha‚ÇÅ]
  refine cfc_congr ?_
  intro x hx
  simp only [Function.comp_apply, Real.exp_log (ha‚ÇÇ x hx), id_eq]

@[simp] lemma log_zero : log (0 : A) = 0 := by simp [log]

@[simp] lemma log_one : log (1 : A) = 0 := by simp [log]

@[simp]
lemma log_algebraMap {r : ‚Ñù} : log (algebraMap ‚Ñù A r) = algebraMap ‚Ñù A (Real.log r) := by
  simp [log]

-- TODO: Relate the hypothesis to a notion of strict positivity
lemma log_smul {r : ‚Ñù} {a : A} (ha‚ÇÅ : IsSelfAdjoint a := by cfc_tac)
    (ha‚ÇÇ : ‚àÄ x ‚àà spectrum ‚Ñù a, 0 < x) (hr : 0 < r) :
    log (r ‚Ä¢ a) = algebraMap ‚Ñù A (Real.log r) + log a := by
  have ha‚ÇÇ' : ContinuousOn Real.log (spectrum ‚Ñù a)  := by
    refine ContinuousOn.mono Real.continuousOn_log fun x hx => ?_
    rw [Set.mem_compl_singleton_iff]
    exact ne_of_gt (ha‚ÇÇ x hx)
  have ha‚ÇÇ'' : ContinuousOn Real.log ((r ‚Ä¢ ¬∑) '' spectrum ‚Ñù a)  := by
    refine ContinuousOn.mono Real.continuousOn_log fun x hx => ?_
    rw [Set.mem_compl_singleton_iff]
    rw [Set.mem_image] at hx
    obtain ‚ü®z, hz‚ü© := hx
    rw [‚Üê hz.2]
    have : 0 < r ‚Ä¢ z := (smul_pos_iff_of_pos_left hr).mpr (ha‚ÇÇ z hz.1)
    exact ne_of_gt this
  rw [log, ‚Üê cfc_smul_id (S := ‚Ñù) (R := ‚Ñù) r a ha‚ÇÅ, ‚Üê cfc_comp Real.log (r ‚Ä¢ ¬∑) a ha‚ÇÅ ha‚ÇÇ'', log]
  have hmain : Set.EqOn (Real.log ‚àò (r ‚Ä¢ ¬∑)) (fun z => Real.log r + Real.log z) (spectrum ‚Ñù a) := by
    intro x hx
    simp only [smul_eq_mul, Function.comp_apply]
    exact Real.log_mul (ne_of_gt hr) <| ne_of_gt (ha‚ÇÇ x hx)
  rw [cfc_congr hmain, cfc_const_add _ a _ ha‚ÇÅ ha‚ÇÇ']

-- TODO: Relate the hypothesis to a notion of strict positivity
lemma log_pow {n : ‚Ñï} {a : A} (ha‚ÇÅ : IsSelfAdjoint a := by cfc_tac)
    (ha‚ÇÇ : ‚àÄ x ‚àà spectrum ‚Ñù a, 0 < x) : log (a ^ n) = n ‚Ä¢ log a := by
  have ha‚ÇÇ' : ContinuousOn Real.log (spectrum ‚Ñù a) := by
    refine ContinuousOn.mono Real.continuousOn_log fun x hx => ?_
    rw [Set.mem_compl_singleton_iff]
    exact ne_of_gt (ha‚ÇÇ x hx)
  have ha‚ÇÇ'' : ContinuousOn Real.log ((¬∑ ^ n) '' spectrum ‚Ñù a)  := by
    refine ContinuousOn.mono Real.continuousOn_log fun x hx => ?_
    rw [Set.mem_compl_singleton_iff]
    rw [Set.mem_image] at hx
    obtain ‚ü®z, hz‚ü© := hx
    rw [‚Üê hz.2]
    exact ne_of_gt (pow_pos (ha‚ÇÇ z hz.1) n)
  rw [log, ‚Üê cfc_pow_id (R := ‚Ñù) a n ha‚ÇÅ, ‚Üê cfc_comp Real.log (¬∑ ^ n) a ha‚ÇÅ ha‚ÇÇ'', log]
  have hmain : Real.log ‚àò (¬∑ ^ n) = fun z => n ‚Ä¢ Real.log z := by ext; simp
  rw [hmain, cfc_smul n Real.log a ha‚ÇÇ']

end real_log
end CFC
