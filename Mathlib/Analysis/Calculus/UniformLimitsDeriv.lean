/-
Copyright (c) 2022 Kevin H. Wilson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin H. Wilson
-/
import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.Analysis.NormedSpace.RCLike
import Mathlib.Order.Filter.Curry

#align_import analysis.calculus.uniform_limits_deriv from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Swapping limits and derivatives via uniform convergence

The purpose of this file is to prove that the derivative of the pointwise limit of a sequence of
functions is the pointwise limit of the functions' derivatives when the derivatives converge
_uniformly_. The formal statement appears as `hasFDerivAt_of_tendstoLocallyUniformlyOn`.

## Main statements

* `uniformCauchySeqOnFilter_of_fderiv`: If
    1. `f : â„• â†’ E â†’ G` is a sequence of functions which have derivatives
       `f' : â„• â†’ E â†’ (E â†’L[ğ•œ] G)` on a neighborhood of `x`,
    2. the functions `f` converge at `x`, and
    3. the derivatives `f'` form a Cauchy sequence uniformly on a neighborhood of `x`,
  then the `f` form a Cauchy sequence _uniformly_ on a neighborhood of `x`
* `hasFDerivAt_of_tendstoUniformlyOnFilter` : Suppose (1), (2), and (3) above are true. Let
  `g` (resp. `g'`) be the limiting function of the `f` (resp. `g'`). Then `f'` is the derivative of
  `g` on a neighborhood of `x`
* `hasFDerivAt_of_tendstoUniformlyOn`: An often-easier-to-use version of the above theorem when
  *all* the derivatives exist and functions converge on a common open set and the derivatives
  converge uniformly there.

Each of the above statements also has variations that support `deriv` instead of `fderiv`.

## Implementation notes

Our technique for proving the main result is the famous "`Îµ / 3` proof." In words, you can find it
explained, for instance, at [this StackExchange post](https://math.stackexchange.com/questions/214218/uniform-convergence-of-derivatives-tao-14-2-7).
The subtlety is that we want to prove that the difference quotients of the `g` converge to the `g'`.
That is, we want to prove something like:

```
âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x), |y - x|â»Â¹ * |(g y - g x) - g' x (y - x)| < Îµ.
```

To do so, we will need to introduce a pair of quantifiers

```lean
âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x), |y - x|â»Â¹ * |(g y - g x) - g' x (y - x)| < Îµ.
```

So how do we write this in terms of filters? Well, the initial definition of the derivative is

```lean
tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (ğ“ x) (ğ“ 0)
```

There are two ways we might introduce `n`. We could do:

```lean
âˆ€á¶  (n : â„•) in atTop, Tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (ğ“ x) (ğ“ 0)
```

but this is equivalent to the quantifier order `âˆƒ N, âˆ€ n â‰¥ N, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y âˆˆ B_Î´(x)`,
which _implies_ our desired `âˆ€ âˆƒ âˆ€ âˆƒ âˆ€` but is _not_ equivalent to it. On the other hand, we might
try

```lean
Tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (atTop Ã—Ë¢ ğ“ x) (ğ“ 0)
```

but this is equivalent to the quantifier order `âˆ€ Îµ > 0, âˆƒ N, âˆƒ Î´ > 0, âˆ€ n â‰¥ N, âˆ€ y âˆˆ B_Î´(x)`, which
again _implies_ our desired `âˆ€ âˆƒ âˆ€ âˆƒ âˆ€` but is not equivalent to it.

So to get the quantifier order we want, we need to introduce a new filter construction, which we
call a "curried filter"

```lean
Tendsto (|y - x|â»Â¹ * |(g y - g x) - g' x (y - x)|) (atTop.curry (ğ“ x)) (ğ“ 0)
```

Then the above implications are `Filter.Tendsto.curry` and
`Filter.Tendsto.mono_left Filter.curry_le_prod`. We will use both of these deductions as part of
our proof.

We note that if you loosen the assumptions of the main theorem then the proof becomes quite a bit
easier. In particular, if you assume there is a common neighborhood `s` where all of the three
assumptions of `hasFDerivAt_of_tendstoUniformlyOnFilter` hold and that the `f'` are
continuous, then you can avoid the mean value theorem and much of the work around curried filters.

## Tags

uniform convergence, limits of derivatives
-/


open Filter

open scoped uniformity Filter Topology

section LimitsOfDerivatives

variable {Î¹ : Type*} {l : Filter Î¹} {E : Type*} [NormedAddCommGroup E] {ğ•œ : Type*} [RCLike ğ•œ]
  [NormedSpace ğ•œ E] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] {f : Î¹ â†’ E â†’ G}
  {g : E â†’ G} {f' : Î¹ â†’ E â†’ E â†’L[ğ•œ] G} {g' : E â†’ E â†’L[ğ•œ] G} {x : E}

/-- If a sequence of functions real or complex functions are eventually differentiable on a
neighborhood of `x`, they are Cauchy _at_ `x`, and their derivatives
are a uniform Cauchy sequence in a neighborhood of `x`, then the functions form a uniform Cauchy
sequence in a neighborhood of `x`. -/
theorem uniformCauchySeqOnFilter_of_fderiv (hf' : UniformCauchySeqOnFilter f' l (ğ“ x))
    (hf : âˆ€á¶  n : Î¹ Ã— E in l Ã—Ë¢ ğ“ x, HasFDerivAt (f n.1) (f' n.1 n.2) n.2)
    (hfg : Cauchy (map (fun n => f n x) l)) : UniformCauchySeqOnFilter f l (ğ“ x) := by
  letI : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ _
  rw [SeminormedAddGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_zero] at hf' âŠ¢
  suffices
    TendstoUniformlyOnFilter (fun (n : Î¹ Ã— Î¹) (z : E) => f n.1 z - f n.2 z - (f n.1 x - f n.2 x)) 0
        (l Ã—Ë¢ l) (ğ“ x) âˆ§
      TendstoUniformlyOnFilter (fun (n : Î¹ Ã— Î¹) (_ : E) => f n.1 x - f n.2 x) 0 (l Ã—Ë¢ l) (ğ“ x) by
    have := this.1.add this.2
    rw [add_zero] at this
    exact this.congr (by simp)
  constructor
  Â· -- This inequality follows from the mean value theorem. To apply it, we will need to shrink our
    -- neighborhood to small enough ball
    rw [Metric.tendstoUniformlyOnFilter_iff] at hf' âŠ¢
    intro Îµ hÎµ
    have := (tendsto_swap4_prod.eventually (hf.prod_mk hf)).diag_of_prod_right
    obtain âŸ¨a, b, c, d, eâŸ© := eventually_prod_iff.1 ((hf' Îµ hÎµ).and this)
    obtain âŸ¨R, hR, hR'âŸ© := Metric.nhds_basis_ball.eventually_iff.mp d
    let r := min 1 R
    have hr : 0 < r := by simp [r, hR]
    have hr' : âˆ€ â¦ƒy : Eâ¦„, y âˆˆ Metric.ball x r â†’ c y := fun y hy =>
      hR' (lt_of_lt_of_le (Metric.mem_ball.mp hy) (min_le_right _ _))
    have hxy : âˆ€ y : E, y âˆˆ Metric.ball x r â†’ â€–y - xâ€– < 1 := by
      intro y hy
      rw [Metric.mem_ball, dist_eq_norm] at hy
      exact lt_of_lt_of_le hy (min_le_left _ _)
    have hxyÎµ : âˆ€ y : E, y âˆˆ Metric.ball x r â†’ Îµ * â€–y - xâ€– < Îµ := by
      intro y hy
      exact (mul_lt_iff_lt_one_right hÎµ.lt).mpr (hxy y hy)
    -- With a small ball in hand, apply the mean value theorem
    refine
      eventually_prod_iff.mpr
        âŸ¨_, b, fun e : E => Metric.ball x r e,
          eventually_mem_set.mpr (Metric.nhds_basis_ball.mem_of_mem hr), fun {n} hn {y} hy => ?_âŸ©
    simp only [Pi.zero_apply, dist_zero_left] at e âŠ¢
    refine lt_of_le_of_lt ?_ (hxyÎµ y hy)
    exact
      Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le
        (fun y hy => ((e hn (hr' hy)).2.1.sub (e hn (hr' hy)).2.2).hasFDerivWithinAt)
        (fun y hy => (e hn (hr' hy)).1.le) (convex_ball x r) (Metric.mem_ball_self hr) hy
  Â· -- This is just `hfg` run through `eventually_prod_iff`
    refine Metric.tendstoUniformlyOnFilter_iff.mpr fun Îµ hÎµ => ?_
    obtain âŸ¨t, ht, ht'âŸ© := (Metric.cauchy_iff.mp hfg).2 Îµ hÎµ
    exact
      eventually_prod_iff.mpr
        âŸ¨fun n : Î¹ Ã— Î¹ => f n.1 x âˆˆ t âˆ§ f n.2 x âˆˆ t,
          eventually_prod_iff.mpr âŸ¨_, ht, _, ht, fun {n} hn {n'} hn' => âŸ¨hn, hn'âŸ©âŸ©,
          fun _ => True,
          by simp,
          fun {n} hn {y} _ => by simpa [norm_sub_rev, dist_eq_norm] using ht' _ hn.1 _ hn.2âŸ©
#align uniform_cauchy_seq_on_filter_of_fderiv uniformCauchySeqOnFilter_of_fderiv

/-- A variant of the second fundamental theorem of calculus (FTC-2): If a sequence of functions
between real or complex normed spaces are differentiable on a ball centered at `x`, they
form a Cauchy sequence _at_ `x`, and their derivatives are Cauchy uniformly on the ball, then the
functions form a uniform Cauchy sequence on the ball.

NOTE: The fact that we work on a ball is typically all that is necessary to work with power series
and Dirichlet series (our primary use case). However, this can be generalized by replacing the ball
with any connected, bounded, open set and replacing uniform convergence with local uniform
convergence. See `cauchy_map_of_uniformCauchySeqOn_fderiv`.
-/
theorem uniformCauchySeqOn_ball_of_fderiv {r : â„} (hf' : UniformCauchySeqOn f' l (Metric.ball x r))
    (hf : âˆ€ n : Î¹, âˆ€ y : E, y âˆˆ Metric.ball x r â†’ HasFDerivAt (f n) (f' n y) y)
    (hfg : Cauchy (map (fun n => f n x) l)) : UniformCauchySeqOn f l (Metric.ball x r) := by
  letI : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ _
  have : NeBot l := (cauchy_map_iff.1 hfg).1
  rcases le_or_lt r 0 with (hr | hr)
  Â· simp only [Metric.ball_eq_empty.2 hr, UniformCauchySeqOn, Set.mem_empty_iff_false,
      IsEmpty.forall_iff, eventually_const, imp_true_iff]
  rw [SeminormedAddGroup.uniformCauchySeqOn_iff_tendstoUniformlyOn_zero] at hf' âŠ¢
  suffices
    TendstoUniformlyOn (fun (n : Î¹ Ã— Î¹) (z : E) => f n.1 z - f n.2 z - (f n.1 x - f n.2 x)) 0
        (l Ã—Ë¢ l) (Metric.ball x r) âˆ§
      TendstoUniformlyOn (fun (n : Î¹ Ã— Î¹) (_ : E) => f n.1 x - f n.2 x) 0
        (l Ã—Ë¢ l) (Metric.ball x r) by
    have := this.1.add this.2
    rw [add_zero] at this
    refine this.congr ?_
    filter_upwards with n z _ using (by simp)
  constructor
  Â· -- This inequality follows from the mean value theorem
    rw [Metric.tendstoUniformlyOn_iff] at hf' âŠ¢
    intro Îµ hÎµ
    obtain âŸ¨q, hqpos, hqâŸ© : âˆƒ q : â„, 0 < q âˆ§ q * r < Îµ := by
      simp_rw [mul_comm]
      exact exists_pos_mul_lt hÎµ.lt r
    apply (hf' q hqpos.gt).mono
    intro n hn y hy
    simp_rw [dist_eq_norm, Pi.zero_apply, zero_sub, norm_neg] at hn âŠ¢
    have mvt :=
      Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le
        (fun z hz => ((hf n.1 z hz).sub (hf n.2 z hz)).hasFDerivWithinAt) (fun z hz => (hn z hz).le)
        (convex_ball x r) (Metric.mem_ball_self hr) hy
    refine lt_of_le_of_lt mvt ?_
    have : q * â€–y - xâ€– < q * r :=
      mul_lt_mul' rfl.le (by simpa only [dist_eq_norm] using Metric.mem_ball.mp hy) (norm_nonneg _)
        hqpos
    exact this.trans hq
  Â· -- This is just `hfg` run through `eventually_prod_iff`
    refine Metric.tendstoUniformlyOn_iff.mpr fun Îµ hÎµ => ?_
    obtain âŸ¨t, ht, ht'âŸ© := (Metric.cauchy_iff.mp hfg).2 Îµ hÎµ
    rw [eventually_prod_iff]
    refine âŸ¨fun n => f n x âˆˆ t, ht, fun n => f n x âˆˆ t, ht, ?_âŸ©
    intro n hn n' hn' z _
    rw [dist_eq_norm, Pi.zero_apply, zero_sub, norm_neg, â† dist_eq_norm]
    exact ht' _ hn _ hn'
#align uniform_cauchy_seq_on_ball_of_fderiv uniformCauchySeqOn_ball_of_fderiv

/-- If a sequence of functions between real or complex normed spaces are differentiable on a
preconnected open set, they form a Cauchy sequence _at_ `x`, and their derivatives are Cauchy
uniformly on the set, then the functions form a Cauchy sequence at any point in the set. -/
theorem cauchy_map_of_uniformCauchySeqOn_fderiv {s : Set E} (hs : IsOpen s) (h's : IsPreconnected s)
    (hf' : UniformCauchySeqOn f' l s) (hf : âˆ€ n : Î¹, âˆ€ y : E, y âˆˆ s â†’ HasFDerivAt (f n) (f' n y) y)
    {xâ‚€ x : E} (hxâ‚€ : xâ‚€ âˆˆ s) (hx : x âˆˆ s) (hfg : Cauchy (map (fun n => f n xâ‚€) l)) :
    Cauchy (map (fun n => f n x) l) := by
  have : NeBot l := (cauchy_map_iff.1 hfg).1
  let t := { y | y âˆˆ s âˆ§ Cauchy (map (fun n => f n y) l) }
  suffices H : s âŠ† t from (H hx).2
  have A : âˆ€ x Îµ, x âˆˆ t â†’ Metric.ball x Îµ âŠ† s â†’ Metric.ball x Îµ âŠ† t := fun x Îµ xt hx y hy =>
    âŸ¨hx hy,
      (uniformCauchySeqOn_ball_of_fderiv (hf'.mono hx) (fun n y hy => hf n y (hx hy))
            xt.2).cauchy_map
        hyâŸ©
  have open_t : IsOpen t := by
    rw [Metric.isOpen_iff]
    intro x hx
    rcases Metric.isOpen_iff.1 hs x hx.1 with âŸ¨Îµ, Îµpos, hÎµâŸ©
    exact âŸ¨Îµ, Îµpos, A x Îµ hx hÎµâŸ©
  have st_nonempty : (s âˆ© t).Nonempty := âŸ¨xâ‚€, hxâ‚€, âŸ¨hxâ‚€, hfgâŸ©âŸ©
  suffices H : closure t âˆ© s âŠ† t from h's.subset_of_closure_inter_subset open_t st_nonempty H
  rintro x âŸ¨xt, xsâŸ©
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ (Îµ : â„), Îµ > 0 âˆ§ Metric.ball x Îµ âŠ† s := Metric.isOpen_iff.1 hs x xs
  obtain âŸ¨y, yt, hxyâŸ© : âˆƒ (y : E), y âˆˆ t âˆ§ dist x y < Îµ / 2 :=
    Metric.mem_closure_iff.1 xt _ (half_pos Îµpos)
  have B : Metric.ball y (Îµ / 2) âŠ† Metric.ball x Îµ := by
    apply Metric.ball_subset_ball'; rw [dist_comm]; linarith
  exact A y (Îµ / 2) yt (B.trans hÎµ) (Metric.mem_ball.2 hxy)
#align cauchy_map_of_uniform_cauchy_seq_on_fderiv cauchy_map_of_uniformCauchySeqOn_fderiv

/-- If `f_n â†’ g` pointwise and the derivatives `(f_n)' â†’ h` _uniformly_ converge, then
in fact for a fixed `y`, the difference quotients `â€–z - yâ€–â»Â¹ â€¢ (f_n z - f_n y)` converge
_uniformly_ to `â€–z - yâ€–â»Â¹ â€¢ (g z - g y)` -/
theorem difference_quotients_converge_uniformly (hf' : TendstoUniformlyOnFilter f' g' l (ğ“ x))
    (hf : âˆ€á¶  n : Î¹ Ã— E in l Ã—Ë¢ ğ“ x, HasFDerivAt (f n.1) (f' n.1 n.2) n.2)
    (hfg : âˆ€á¶  y : E in ğ“ x, Tendsto (fun n => f n y) l (ğ“ (g y))) :
    TendstoUniformlyOnFilter (fun n : Î¹ => fun y : E => (â€–y - xâ€–â»Â¹ : ğ•œ) â€¢ (f n y - f n x))
      (fun y : E => (â€–y - xâ€–â»Â¹ : ğ•œ) â€¢ (g y - g x)) l (ğ“ x) := by
  let A : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ _
  rcases eq_or_ne l âŠ¥ with (hl | hl)
  Â· simp only [hl, TendstoUniformlyOnFilter, bot_prod, eventually_bot, imp_true_iff]
  haveI : NeBot l := âŸ¨hlâŸ©
  refine
    UniformCauchySeqOnFilter.tendstoUniformlyOnFilter_of_tendsto ?_
      ((hfg.and (eventually_const.mpr hfg.self_of_nhds)).mono fun y hy =>
        (hy.1.sub hy.2).const_smul _)
  rw [SeminormedAddGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_zero]
  rw [Metric.tendstoUniformlyOnFilter_iff]
  have hfg' := hf'.uniformCauchySeqOnFilter
  rw [SeminormedAddGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_zero] at hfg'
  rw [Metric.tendstoUniformlyOnFilter_iff] at hfg'
  intro Îµ hÎµ
  obtain âŸ¨q, hqpos, hqÎµâŸ© := exists_pos_rat_lt hÎµ
  specialize hfg' (q : â„) (by simp [hqpos])
  have := (tendsto_swap4_prod.eventually (hf.prod_mk hf)).diag_of_prod_right
  obtain âŸ¨a, b, c, d, eâŸ© := eventually_prod_iff.1 (hfg'.and this)
  obtain âŸ¨r, hr, hr'âŸ© := Metric.nhds_basis_ball.eventually_iff.mp d
  rw [eventually_prod_iff]
  refine
    âŸ¨_, b, fun e : E => Metric.ball x r e,
      eventually_mem_set.mpr (Metric.nhds_basis_ball.mem_of_mem hr), fun {n} hn {y} hy => ?_âŸ©
  simp only [Pi.zero_apply, dist_zero_left]
  rw [â† smul_sub, norm_smul, norm_inv, RCLike.norm_coe_norm]
  refine lt_of_le_of_lt ?_ hqÎµ
  by_cases hyz' : x = y; Â· simp [hyz', hqpos.le]
  have hyz : 0 < â€–y - xâ€– := by rw [norm_pos_iff]; intro hy'; exact hyz' (eq_of_sub_eq_zero hy').symm
  rw [inv_mul_le_iff hyz, mul_comm, sub_sub_sub_comm]
  simp only [Pi.zero_apply, dist_zero_left] at e
  refine
    Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le
      (fun y hy => ((e hn (hr' hy)).2.1.sub (e hn (hr' hy)).2.2).hasFDerivWithinAt)
      (fun y hy => (e hn (hr' hy)).1.le) (convex_ball x r) (Metric.mem_ball_self hr) hy
#align difference_quotients_converge_uniformly difference_quotients_converge_uniformly

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit at `x`.

In words the assumptions mean the following:
  * `hf'`: The `f'` converge "uniformly at" `x` to `g'`. This does not mean that the `f' n` even
    converge away from `x`!
  * `hf`: For all `(y, n)` with `y` sufficiently close to `x` and `n` sufficiently large, `f' n` is
    the derivative of `f n`
  * `hfg`: The `f n` converge pointwise to `g` on a neighborhood of `x` -/
theorem hasFDerivAt_of_tendstoUniformlyOnFilter [NeBot l]
    (hf' : TendstoUniformlyOnFilter f' g' l (ğ“ x))
    (hf : âˆ€á¶  n : Î¹ Ã— E in l Ã—Ë¢ ğ“ x, HasFDerivAt (f n.1) (f' n.1 n.2) n.2)
    (hfg : âˆ€á¶  y in ğ“ x, Tendsto (fun n => f n y) l (ğ“ (g y))) : HasFDerivAt g (g' x) x := by
  -- The proof strategy follows several steps:
  --   1. The quantifiers in the definition of the derivative are
  --      `âˆ€ Îµ > 0, âˆƒÎ´ > 0, âˆ€y âˆˆ B_Î´(x)`. We will introduce a quantifier in the middle:
  --      `âˆ€ Îµ > 0, âˆƒN, âˆ€n â‰¥ N, âˆƒÎ´ > 0, âˆ€y âˆˆ B_Î´(x)` which will allow us to introduce the `f(') n`
  --   2. The order of the quantifiers `hfg` are opposite to what we need. We will be able to swap
  --      the quantifiers using the uniform convergence assumption
  rw [hasFDerivAt_iff_tendsto]
  -- Introduce extra quantifier via curried filters
  suffices
    Tendsto (fun y : Î¹ Ã— E => â€–y.2 - xâ€–â»Â¹ * â€–g y.2 - g x - (g' x) (y.2 - x)â€–)
      (l.curry (ğ“ x)) (ğ“ 0) by
    rw [Metric.tendsto_nhds] at this âŠ¢
    intro Îµ hÎµ
    specialize this Îµ hÎµ
    rw [eventually_curry_iff] at this
    simp only at this
    exact (eventually_const.mp this).mono (by simp only [imp_self, forall_const])
  -- With the new quantifier in hand, we can perform the famous `Îµ/3` proof. Specifically,
  -- we will break up the limit (the difference functions minus the derivative go to 0) into 3:
  --   * The difference functions of the `f n` converge *uniformly* to the difference functions
  --     of the `g n`
  --   * The `f' n` are the derivatives of the `f n`
  --   * The `f' n` converge to `g'` at `x`
  conv =>
    congr
    ext
    rw [â† abs_norm, â† abs_inv, â† @RCLike.norm_ofReal ğ•œ _ _, RCLike.ofReal_inv, â† norm_smul]
  rw [â† tendsto_zero_iff_norm_tendsto_zero]
  have :
    (fun a : Î¹ Ã— E => (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (g a.2 - g x - (g' x) (a.2 - x))) =
      ((fun a : Î¹ Ã— E => (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (g a.2 - g x - (f a.1 a.2 - f a.1 x))) +
          fun a : Î¹ Ã— E =>
          (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (f a.1 a.2 - f a.1 x - ((f' a.1 x) a.2 - (f' a.1 x) x))) +
        fun a : Î¹ Ã— E => (â€–a.2 - xâ€–â»Â¹ : ğ•œ) â€¢ (f' a.1 x - g' x) (a.2 - x) := by
    ext; simp only [Pi.add_apply]; rw [â† smul_add, â† smul_add]; congr
    simp only [map_sub, sub_add_sub_cancel, ContinuousLinearMap.coe_sub', Pi.sub_apply]
    -- Porting note: added
    abel
  simp_rw [this]
  have : ğ“ (0 : G) = ğ“ (0 + 0 + 0) := by simp only [add_zero]
  rw [this]
  refine Tendsto.add (Tendsto.add ?_ ?_) ?_
  Â· simp only
    have := difference_quotients_converge_uniformly hf' hf hfg
    rw [Metric.tendstoUniformlyOnFilter_iff] at this
    rw [Metric.tendsto_nhds]
    intro Îµ hÎµ
    apply ((this Îµ hÎµ).filter_mono curry_le_prod).mono
    intro n hn
    rw [dist_eq_norm] at hn âŠ¢
    rw [â† smul_sub] at hn
    rwa [sub_zero]
  Â· -- (Almost) the definition of the derivatives
    rw [Metric.tendsto_nhds]
    intro Îµ hÎµ
    rw [eventually_curry_iff]
    refine hf.curry.mono fun n hn => ?_
    have := hn.self_of_nhds
    rw [hasFDerivAt_iff_tendsto, Metric.tendsto_nhds] at this
    refine (this Îµ hÎµ).mono fun y hy => ?_
    rw [dist_eq_norm] at hy âŠ¢
    simp only [sub_zero, map_sub, norm_mul, norm_inv, norm_norm] at hy âŠ¢
    rw [norm_smul, norm_inv, RCLike.norm_coe_norm]
    exact hy
  Â· -- hfg' after specializing to `x` and applying the definition of the operator norm
    refine Tendsto.mono_left ?_ curry_le_prod
    have h1 : Tendsto (fun n : Î¹ Ã— E => g' n.2 - f' n.1 n.2) (l Ã—Ë¢ ğ“ x) (ğ“ 0) := by
      rw [Metric.tendstoUniformlyOnFilter_iff] at hf'
      exact Metric.tendsto_nhds.mpr fun Îµ hÎµ => by simpa using hf' Îµ hÎµ
    have h2 : Tendsto (fun n : Î¹ => g' x - f' n x) l (ğ“ 0) := by
      rw [Metric.tendsto_nhds] at h1 âŠ¢
      exact fun Îµ hÎµ => (h1 Îµ hÎµ).curry.mono fun n hn => hn.self_of_nhds
    refine squeeze_zero_norm ?_
      (tendsto_zero_iff_norm_tendsto_zero.mp (tendsto_fst.comp (h2.prod_map tendsto_id)))
    intro n
    simp_rw [norm_smul, norm_inv, RCLike.norm_coe_norm]
    by_cases hx : x = n.2; Â· simp [hx]
    have hnx : 0 < â€–n.2 - xâ€– := by
      rw [norm_pos_iff]; intro hx'; exact hx (eq_of_sub_eq_zero hx').symm
    rw [inv_mul_le_iff hnx, mul_comm]
    simp only [Function.comp_apply, Prod.map_apply]
    rw [norm_sub_rev]
    exact (f' n.1 x - g' x).le_opNorm (n.2 - x)
#align has_fderiv_at_of_tendsto_uniformly_on_filter hasFDerivAt_of_tendstoUniformlyOnFilter

theorem hasFDerivAt_of_tendstoLocallyUniformlyOn [NeBot l] {s : Set E} (hs : IsOpen s)
    (hf' : TendstoLocallyUniformlyOn f' g' l s) (hf : âˆ€ n, âˆ€ x âˆˆ s, HasFDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x âˆˆ s, Tendsto (fun n => f n x) l (ğ“ (g x))) (hx : x âˆˆ s) :
    HasFDerivAt g (g' x) x := by
  have h1 : s âˆˆ ğ“ x := hs.mem_nhds hx
  have h3 : Set.univ Ã—Ë¢ s âˆˆ l Ã—Ë¢ ğ“ x := by simp only [h1, prod_mem_prod_iff, univ_mem, and_self_iff]
  have h4 : âˆ€á¶  n : Î¹ Ã— E in l Ã—Ë¢ ğ“ x, HasFDerivAt (f n.1) (f' n.1 n.2) n.2 :=
    eventually_of_mem h3 fun âŸ¨n, zâŸ© âŸ¨_, hzâŸ© => hf n z hz
  refine hasFDerivAt_of_tendstoUniformlyOnFilter ?_ h4 (eventually_of_mem h1 hfg)
  simpa [IsOpen.nhdsWithin_eq hs hx] using tendstoLocallyUniformlyOn_iff_filter.mp hf' x hx
#align has_fderiv_at_of_tendsto_locally_uniformly_on hasFDerivAt_of_tendstoLocallyUniformlyOn

/-- A slight variant of `hasFDerivAt_of_tendstoLocallyUniformlyOn` with the assumption stated
in terms of `DifferentiableOn` rather than `HasFDerivAt`. This makes a few proofs nicer in
complex analysis where holomorphicity is assumed but the derivative is not known a priori. -/
theorem hasFDerivAt_of_tendsto_locally_uniformly_on' [NeBot l] {s : Set E} (hs : IsOpen s)
    (hf' : TendstoLocallyUniformlyOn (fderiv ğ•œ âˆ˜ f) g' l s) (hf : âˆ€ n, DifferentiableOn ğ•œ (f n) s)
    (hfg : âˆ€ x âˆˆ s, Tendsto (fun n => f n x) l (ğ“ (g x))) (hx : x âˆˆ s) :
    HasFDerivAt g (g' x) x := by
  refine hasFDerivAt_of_tendstoLocallyUniformlyOn hs hf' (fun n z hz => ?_) hfg hx
  exact ((hf n z hz).differentiableAt (hs.mem_nhds hz)).hasFDerivAt
#align has_fderiv_at_of_tendsto_locally_uniformly_on' hasFDerivAt_of_tendsto_locally_uniformly_on'

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit on an open set containing `x`. -/
theorem hasFDerivAt_of_tendstoUniformlyOn [NeBot l] {s : Set E} (hs : IsOpen s)
    (hf' : TendstoUniformlyOn f' g' l s)
    (hf : âˆ€ n : Î¹, âˆ€ x : E, x âˆˆ s â†’ HasFDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x : E, x âˆˆ s â†’ Tendsto (fun n => f n x) l (ğ“ (g x))) :
    âˆ€ x : E, x âˆˆ s â†’ HasFDerivAt g (g' x) x := fun _ =>
  hasFDerivAt_of_tendstoLocallyUniformlyOn hs hf'.tendstoLocallyUniformlyOn hf hfg
#align has_fderiv_at_of_tendsto_uniformly_on hasFDerivAt_of_tendstoUniformlyOn

/-- `(d/dx) lim_{n â†’ âˆ} f n x = lim_{n â†’ âˆ} f' n x` when the `f' n` converge
_uniformly_ to their limit. -/
theorem hasFDerivAt_of_tendstoUniformly [NeBot l] (hf' : TendstoUniformly f' g' l)
    (hf : âˆ€ n : Î¹, âˆ€ x : E, HasFDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x : E, Tendsto (fun n => f n x) l (ğ“ (g x))) : âˆ€ x : E, HasFDerivAt g (g' x) x := by
  intro x
  have hf : âˆ€ n : Î¹, âˆ€ x : E, x âˆˆ Set.univ â†’ HasFDerivAt (f n) (f' n x) x := by simp [hf]
  have hfg : âˆ€ x : E, x âˆˆ Set.univ â†’ Tendsto (fun n => f n x) l (ğ“ (g x)) := by simp [hfg]
  have hf' : TendstoUniformlyOn f' g' l Set.univ := by rwa [tendstoUniformlyOn_univ]
  exact hasFDerivAt_of_tendstoUniformlyOn isOpen_univ hf' hf hfg x (Set.mem_univ x)
#align has_fderiv_at_of_tendsto_uniformly hasFDerivAt_of_tendstoUniformly

end LimitsOfDerivatives

section deriv

/-! ### `deriv` versions of above theorems

In this section, we provide `deriv` equivalents of the `fderiv` lemmas in the previous section.
-/


variable {Î¹ : Type*} {l : Filter Î¹} {ğ•œ : Type*} [RCLike ğ•œ] {G : Type*} [NormedAddCommGroup G]
  [NormedSpace ğ•œ G] {f : Î¹ â†’ ğ•œ â†’ G} {g : ğ•œ â†’ G} {f' : Î¹ â†’ ğ•œ â†’ G} {g' : ğ•œ â†’ G} {x : ğ•œ}

/-- If our derivatives converge uniformly, then the FrÃ©chet derivatives converge uniformly -/
theorem UniformCauchySeqOnFilter.one_smulRight {l' : Filter ğ•œ}
    (hf' : UniformCauchySeqOnFilter f' l l') :
    UniformCauchySeqOnFilter (fun n => fun z => (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight (f' n z)) l l' := by
  -- The tricky part of this proof is that operator norms are written in terms of `â‰¤` whereas
  -- metrics are written in terms of `<`. So we need to shrink `Îµ` utilizing the archimedean
  -- property of `â„`
  rw [SeminormedAddGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_zero,
    Metric.tendstoUniformlyOnFilter_iff] at hf' âŠ¢
  intro Îµ hÎµ
  obtain âŸ¨q, hq, hq'âŸ© := exists_between hÎµ.lt
  apply (hf' q hq).mono
  intro n hn
  refine lt_of_le_of_lt ?_ hq'
  simp only [dist_eq_norm, Pi.zero_apply, zero_sub, norm_neg] at hn âŠ¢
  refine ContinuousLinearMap.opNorm_le_bound _ hq.le ?_
  intro z
  simp only [ContinuousLinearMap.coe_sub', Pi.sub_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.one_apply]
  rw [â† smul_sub, norm_smul, mul_comm]
  gcongr
#align uniform_cauchy_seq_on_filter.one_smul_right UniformCauchySeqOnFilter.one_smulRight

theorem uniformCauchySeqOnFilter_of_deriv (hf' : UniformCauchySeqOnFilter f' l (ğ“ x))
    (hf : âˆ€á¶  n : Î¹ Ã— ğ•œ in l Ã—Ë¢ ğ“ x, HasDerivAt (f n.1) (f' n.1 n.2) n.2)
    (hfg : Cauchy (map (fun n => f n x) l)) : UniformCauchySeqOnFilter f l (ğ“ x) := by
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hf
  exact uniformCauchySeqOnFilter_of_fderiv hf'.one_smulRight hf hfg
#align uniform_cauchy_seq_on_filter_of_deriv uniformCauchySeqOnFilter_of_deriv

theorem uniformCauchySeqOn_ball_of_deriv {r : â„} (hf' : UniformCauchySeqOn f' l (Metric.ball x r))
    (hf : âˆ€ n : Î¹, âˆ€ y : ğ•œ, y âˆˆ Metric.ball x r â†’ HasDerivAt (f n) (f' n y) y)
    (hfg : Cauchy (map (fun n => f n x) l)) : UniformCauchySeqOn f l (Metric.ball x r) := by
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hf
  rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter] at hf'
  have hf' :
    UniformCauchySeqOn (fun n => fun z => (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight (f' n z)) l
      (Metric.ball x r) := by
    rw [uniformCauchySeqOn_iff_uniformCauchySeqOnFilter]
    exact hf'.one_smulRight
  exact uniformCauchySeqOn_ball_of_fderiv hf' hf hfg
#align uniform_cauchy_seq_on_ball_of_deriv uniformCauchySeqOn_ball_of_deriv

theorem hasDerivAt_of_tendstoUniformlyOnFilter [NeBot l]
    (hf' : TendstoUniformlyOnFilter f' g' l (ğ“ x))
    (hf : âˆ€á¶  n : Î¹ Ã— ğ•œ in l Ã—Ë¢ ğ“ x, HasDerivAt (f n.1) (f' n.1 n.2) n.2)
    (hfg : âˆ€á¶  y in ğ“ x, Tendsto (fun n => f n y) l (ğ“ (g y))) : HasDerivAt g (g' x) x := by
  -- The first part of the proof rewrites `hf` and the goal to be functions so that Lean
  -- can recognize them when we apply `hasFDerivAt_of_tendstoUniformlyOnFilter`
  let F' n z := (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight (f' n z)
  let G' z := (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight (g' z)
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hf âŠ¢
  -- Now we need to rewrite hf' in terms of `ContinuousLinearMap`s. The tricky part is that
  -- operator norms are written in terms of `â‰¤` whereas metrics are written in terms of `<`. So we
  -- need to shrink `Îµ` utilizing the archimedean property of `â„`
  have hf' : TendstoUniformlyOnFilter F' G' l (ğ“ x) := by
    rw [Metric.tendstoUniformlyOnFilter_iff] at hf' âŠ¢
    intro Îµ hÎµ
    obtain âŸ¨q, hq, hq'âŸ© := exists_between hÎµ.lt
    apply (hf' q hq).mono
    intro n hn
    refine lt_of_le_of_lt ?_ hq'
    simp only [dist_eq_norm] at hn âŠ¢
    refine ContinuousLinearMap.opNorm_le_bound _ hq.le ?_
    intro z
    simp only [F', G', ContinuousLinearMap.coe_sub', Pi.sub_apply,
      ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply]
    rw [â† smul_sub, norm_smul, mul_comm]
    gcongr
  exact hasFDerivAt_of_tendstoUniformlyOnFilter hf' hf hfg
#align has_deriv_at_of_tendsto_uniformly_on_filter hasDerivAt_of_tendstoUniformlyOnFilter

theorem hasDerivAt_of_tendstoLocallyUniformlyOn [NeBot l] {s : Set ğ•œ} (hs : IsOpen s)
    (hf' : TendstoLocallyUniformlyOn f' g' l s)
    (hf : âˆ€á¶  n in l, âˆ€ x âˆˆ s, HasDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x âˆˆ s, Tendsto (fun n => f n x) l (ğ“ (g x))) (hx : x âˆˆ s) : HasDerivAt g (g' x) x := by
  have h1 : s âˆˆ ğ“ x := hs.mem_nhds hx
  have h2 : âˆ€á¶  n : Î¹ Ã— ğ•œ in l Ã—Ë¢ ğ“ x, HasDerivAt (f n.1) (f' n.1 n.2) n.2 :=
    eventually_prod_iff.2 âŸ¨_, hf, fun x => x âˆˆ s, h1, fun {n} => idâŸ©
  refine hasDerivAt_of_tendstoUniformlyOnFilter ?_ h2 (eventually_of_mem h1 hfg)
  simpa [IsOpen.nhdsWithin_eq hs hx] using tendstoLocallyUniformlyOn_iff_filter.mp hf' x hx
#align has_deriv_at_of_tendsto_locally_uniformly_on hasDerivAt_of_tendstoLocallyUniformlyOn

/-- A slight variant of `hasDerivAt_of_tendstoLocallyUniformlyOn` with the assumption stated in
terms of `DifferentiableOn` rather than `HasDerivAt`. This makes a few proofs nicer in complex
analysis where holomorphicity is assumed but the derivative is not known a priori. -/
theorem hasDerivAt_of_tendsto_locally_uniformly_on' [NeBot l] {s : Set ğ•œ} (hs : IsOpen s)
    (hf' : TendstoLocallyUniformlyOn (deriv âˆ˜ f) g' l s)
    (hf : âˆ€á¶  n in l, DifferentiableOn ğ•œ (f n) s)
    (hfg : âˆ€ x âˆˆ s, Tendsto (fun n => f n x) l (ğ“ (g x))) (hx : x âˆˆ s) : HasDerivAt g (g' x) x := by
  refine hasDerivAt_of_tendstoLocallyUniformlyOn hs hf' ?_ hfg hx
  filter_upwards [hf] with n h z hz using ((h z hz).differentiableAt (hs.mem_nhds hz)).hasDerivAt
#align has_deriv_at_of_tendsto_locally_uniformly_on' hasDerivAt_of_tendsto_locally_uniformly_on'

theorem hasDerivAt_of_tendstoUniformlyOn [NeBot l] {s : Set ğ•œ} (hs : IsOpen s)
    (hf' : TendstoUniformlyOn f' g' l s)
    (hf : âˆ€á¶  n in l, âˆ€ x : ğ•œ, x âˆˆ s â†’ HasDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x : ğ•œ, x âˆˆ s â†’ Tendsto (fun n => f n x) l (ğ“ (g x))) :
    âˆ€ x : ğ•œ, x âˆˆ s â†’ HasDerivAt g (g' x) x := fun _ =>
  hasDerivAt_of_tendstoLocallyUniformlyOn hs hf'.tendstoLocallyUniformlyOn hf hfg
#align has_deriv_at_of_tendsto_uniformly_on hasDerivAt_of_tendstoUniformlyOn

theorem hasDerivAt_of_tendstoUniformly [NeBot l] (hf' : TendstoUniformly f' g' l)
    (hf : âˆ€á¶  n in l, âˆ€ x : ğ•œ, HasDerivAt (f n) (f' n x) x)
    (hfg : âˆ€ x : ğ•œ, Tendsto (fun n => f n x) l (ğ“ (g x))) : âˆ€ x : ğ•œ, HasDerivAt g (g' x) x := by
  intro x
  have hf : âˆ€á¶  n in l, âˆ€ x : ğ•œ, x âˆˆ Set.univ â†’ HasDerivAt (f n) (f' n x) x := by
    filter_upwards [hf] with n h x _ using h x
  have hfg : âˆ€ x : ğ•œ, x âˆˆ Set.univ â†’ Tendsto (fun n => f n x) l (ğ“ (g x)) := by simp [hfg]
  have hf' : TendstoUniformlyOn f' g' l Set.univ := by rwa [tendstoUniformlyOn_univ]
  exact hasDerivAt_of_tendstoUniformlyOn isOpen_univ hf' hf hfg x (Set.mem_univ x)
#align has_deriv_at_of_tendsto_uniformly hasDerivAt_of_tendstoUniformly

end deriv
