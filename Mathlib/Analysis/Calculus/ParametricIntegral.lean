/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Operations
public import Mathlib.Analysis.Calculus.MeanValue
public import Mathlib.Analysis.Calculus.TangentCone.Prod
public import Mathlib.MeasureTheory.Integral.DominatedConvergence

/-!
# Derivatives of integrals depending on parameters

A parametric integral is a function with shape `f = fun x : H â†¦ âˆ« a : Î±, F x a âˆ‚Î¼` for some
`F : H â†’ Î± â†’ E`, where `H` and `E` are normed spaces and `Î±` is a measured space with measure `Î¼`.

We already know from `continuous_of_dominated`
in `Mathlib/MeasureTheory/Integral/Bochner/Basic.lean` how
to guarantee that `f` is continuous using the dominated convergence theorem. In this file,
we want to express the derivative of `f` as the integral of the derivative of `F` with respect
to `x`.


## Main results

As explained above, all results express the derivative of a parametric integral as the integral of
a derivative. The variations come from the assumptions and from the different ways of expressing
derivative, especially FrÃ©chet derivatives vs elementary derivative of function of one real
variable.

* `hasFDerivAt_integral_of_dominated_loc_of_lip`: this version assumes that
  - `F x` is ae-measurable for x near `xâ‚€`,
  - `F xâ‚€` is integrable,
  - `fun x â†¦ F x a` has derivative `F' a : H â†’L[â„] E` at `xâ‚€` which is ae-measurable,
  - `fun x â†¦ F x a` is locally Lipschitz near `xâ‚€` for almost every `a`,
    with a Lipschitz bound which is integrable with respect to `a`.

  A subtle point is that the "near xâ‚€" in the last condition has to be uniform in `a`. This is
  controlled by a fixed neighborhood `s` of `xâ‚€`.

* `hasFDerivAt_integral_of_dominated_of_fderiv_le`: this version assumes `fun x â†¦ F x a` has
  derivative `F' x a` for `x` near `xâ‚€` and `F' x` is bounded by an integrable function independent
  from `x` near `xâ‚€`.

* `hasFDerivAt_integral_of_continuousOn_fderiv`: this version assumes that `F : H â†’ Î± â†’ E` is
  continuously differentiable in the first argument near `xâ‚€` in the sense that:
  - `F.uncurry : H Ã— Î± â†’ E` is continuous on `u Ã—Ë¢ k` for a neighbourhood `u` of `xâ‚€`,
  - `fun x â†¦ F x a` is differentiable on `u` for each parameter `a âˆˆ k`,
  - `fun (x, a) â†¦ fderiv ğ•œ (fun y â†¦ F y a) x` is continuous on `u Ã—Ë¢ k`.

  Here `k : Set Î±` is the domain of integration and is required to be compact, regarding some
  sufficiently compatible topology on `Î±`.

* `ContDiffOn.parametric_integral` : If `f.uncurry : H Ã— H' â†’ E` is `Câ¿` on `u Ã—Ë¢ k` for an open
  set `u` and a compact set `k`, then given any subset `sâ‚€` of `k` the parametric
  integral `fun x â†¦ âˆ« a in sâ‚€, f x a âˆ‚Î¼` is `Câ¿` on `u` too.

`hasDerivAt_integral_of_dominated_loc_of_lip` and
`hasDerivAt_integral_of_dominated_loc_of_deriv_le` are versions of the above two results that
assume `H = â„` or `H = â„‚` and use the high-school derivative `deriv` instead of FrÃ©chet derivative
`fderiv`.

## Tags
integral, derivative
-/

public section


noncomputable section

open TopologicalSpace MeasureTheory Filter Metric Set

open scoped Topology Filter

variable {Î± : Type*} [MeasurableSpace Î±] {Î¼ : Measure Î±}
  {ğ•œ : Type*} [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
  {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H]
  {H' : Type*} [NormedAddCommGroup H'] [NormedSpace ğ•œ H'] [MeasurableSpace H']
  [OpensMeasurableSpace H']

variable {F : H â†’ Î± â†’ E} {xâ‚€ : H} {bound : Î± â†’ â„} {s : Set H}

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming `F xâ‚€` is
integrable, `â€–F x a - F xâ‚€ aâ€– â‰¤ bound a * â€–x - xâ‚€â€–` for `x` in a ball around `xâ‚€` for ae `a` with
integrable Lipschitz bound `bound` (with a ball radius independent of `a`), and `F x` is
ae-measurable for `x` in the same ball. See `hasFDerivAt_integral_of_dominated_loc_of_lip` for a
slightly less general but usually more useful version. -/
theorem hasFDerivAt_integral_of_dominated_loc_of_lip' {F' : Î± â†’ H â†’L[ğ•œ] E} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€ x âˆˆ s, AEStronglyMeasurable (F x) Î¼) (hF_int : Integrable (F xâ‚€) Î¼)
    (hF'_meas : AEStronglyMeasurable F' Î¼)
    (h_lipsch : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, â€–F x a - F xâ‚€ aâ€– â‰¤ bound a * â€–x - xâ‚€â€–)
    (bound_integrable : Integrable (bound : Î± â†’ â„) Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, HasFDerivAt (F Â· a) (F' a) xâ‚€) :
    Integrable F' Î¼ âˆ§ HasFDerivAt (fun x â†¦ âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ := by
  obtain âŸ¨Îµ, Îµ_pos, hÎµâŸ© := Metric.mem_nhds_iff.1 hs
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos
  have nneg : âˆ€ x, 0 â‰¤ â€–x - xâ‚€â€–â»Â¹ := fun x â†¦ inv_nonneg.mpr (norm_nonneg _)
  set b : Î± â†’ â„ := fun a â†¦ |bound a|
  have b_int : Integrable b Î¼ := bound_integrable.norm
  have b_nonneg : âˆ€ a, 0 â‰¤ b a := fun a â†¦ abs_nonneg _
  replace h_lipsch : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F x a - F xâ‚€ aâ€– â‰¤ b a * â€–x - xâ‚€â€– :=
    h_lipsch.mono fun a ha x hx â†¦
      (ha x (hÎµ hx)).trans <| mul_le_mul_of_nonneg_right (le_abs_self _) (norm_nonneg _)
  have hF_int' : âˆ€ x âˆˆ ball xâ‚€ Îµ, Integrable (F x) Î¼ := fun x x_in â†¦ by
    have : âˆ€áµ a âˆ‚Î¼, â€–F xâ‚€ a - F x aâ€– â‰¤ Îµ * b a := by
      simp only [norm_sub_rev (F xâ‚€ _)]
      refine h_lipsch.mono fun a ha â†¦ (ha x x_in).trans ?_
      rw [mul_comm Îµ]
      rw [mem_ball, dist_eq_norm] at x_in
      exact mul_le_mul_of_nonneg_left x_in.le (b_nonneg _)
    exact integrable_of_norm_sub_le (hF_meas x (hÎµ x_in)) hF_int
      (bound_integrable.norm.const_mul Îµ) this
  have hF'_int : Integrable F' Î¼ :=
    have : âˆ€áµ a âˆ‚Î¼, â€–F' aâ€– â‰¤ b a := by
      apply (h_diff.and h_lipsch).mono
      rintro a âŸ¨ha_diff, ha_lipâŸ©
      exact ha_diff.le_of_lip' (b_nonneg a) (mem_of_superset (ball_mem_nhds _ Îµ_pos) <| ha_lip)
    b_int.mono' hF'_meas this
  refine âŸ¨hF'_int, ?_âŸ©
  /- Discard the trivial case where `E` is not complete, as all integrals vanish. -/
  by_cases hE : CompleteSpace E; swap
  Â· rcases subsingleton_or_nontrivial H with hH|hH
    Â· have : Subsingleton (H â†’L[ğ•œ] E) := inferInstance
      convert hasFDerivAt_of_subsingleton _ xâ‚€
    Â· have : Â¬(CompleteSpace (H â†’L[ğ•œ] E)) := by
        simpa [SeparatingDual.completeSpace_continuousLinearMap_iff] using hE
      simp only [integral, hE, â†“reduceDIte, this]
      exact hasFDerivAt_const 0 xâ‚€
  have h_ball : ball xâ‚€ Îµ âˆˆ ğ“ xâ‚€ := ball_mem_nhds xâ‚€ Îµ_pos
  have : âˆ€á¶  x in ğ“ xâ‚€, â€–x - xâ‚€â€–â»Â¹ * â€–((âˆ« a, F x a âˆ‚Î¼) - âˆ« a, F xâ‚€ a âˆ‚Î¼) - (âˆ« a, F' a âˆ‚Î¼) (x - xâ‚€)â€– =
      â€–âˆ« a, â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€)) âˆ‚Î¼â€– := by
    apply mem_of_superset (ball_mem_nhds _ Îµ_pos)
    intro x x_in; simp only
    rw [mem_setOf_eq, â† norm_smul_of_nonneg (nneg _), integral_smul, integral_sub, integral_sub,
      â† ContinuousLinearMap.integral_apply hF'_int]
    exacts [hF_int' x x_in, hF_int, (hF_int' x x_in).sub hF_int,
      hF'_int.apply_continuousLinearMap _]
  rw [hasFDerivAt_iff_tendsto, tendsto_congr' this, â† tendsto_zero_iff_norm_tendsto_zero, â†
    show (âˆ« a : Î±, â€–xâ‚€ - xâ‚€â€–â»Â¹ â€¢ (F xâ‚€ a - F xâ‚€ a - (F' a) (xâ‚€ - xâ‚€)) âˆ‚Î¼) = 0 by simp]
  apply tendsto_integral_filter_of_dominated_convergence
  Â· filter_upwards [h_ball] with _ x_in
    apply AEStronglyMeasurable.const_smul
    exact ((hF_meas _ (hÎµ x_in)).sub (hF_meas _ (hÎµ xâ‚€_in))).sub
      (hF'_meas.apply_continuousLinearMap _)
  Â· refine mem_of_superset h_ball fun x hx â†¦ ?_
    apply (h_diff.and h_lipsch).mono
    on_goal 1 => rintro a âŸ¨-, ha_boundâŸ©
    show â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€– â‰¤ b a + â€–F' aâ€–
    replace ha_bound : â€–F x a - F xâ‚€ aâ€– â‰¤ b a * â€–x - xâ‚€â€– := ha_bound x hx
    calc
      â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€– =
          â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a) - â€–x - xâ‚€â€–â»Â¹ â€¢ F' a (x - xâ‚€)â€– := by rw [smul_sub]
      _ â‰¤ â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a)â€– + â€–â€–x - xâ‚€â€–â»Â¹ â€¢ F' a (x - xâ‚€)â€– := norm_sub_le _ _
      _ = â€–x - xâ‚€â€–â»Â¹ * â€–F x a - F xâ‚€ aâ€– + â€–x - xâ‚€â€–â»Â¹ * â€–F' a (x - xâ‚€)â€– := by
        rw [norm_smul_of_nonneg, norm_smul_of_nonneg] <;> exact nneg _
      _ â‰¤ â€–x - xâ‚€â€–â»Â¹ * (b a * â€–x - xâ‚€â€–) + â€–x - xâ‚€â€–â»Â¹ * (â€–F' aâ€– * â€–x - xâ‚€â€–) := by
        gcongr; exact (F' a).le_opNorm _
      _ â‰¤ b a + â€–F' aâ€– := ?_
    simp only [â† div_eq_inv_mul]
    apply_rules [add_le_add, div_le_of_le_mulâ‚€] <;> first | rfl | positivity
  Â· exact b_int.add hF'_int.norm
  Â· apply h_diff.mono
    intro a ha
    suffices Tendsto (fun x â†¦ â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))) (ğ“ xâ‚€) (ğ“ 0) by simpa
    rw [tendsto_zero_iff_norm_tendsto_zero]
    have : (fun x â†¦ â€–x - xâ‚€â€–â»Â¹ * â€–F x a - F xâ‚€ a - F' a (x - xâ‚€)â€–) = fun x â†¦
        â€–â€–x - xâ‚€â€–â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))â€– := by
      ext x
      rw [norm_smul_of_nonneg (nneg _)]
    rwa [hasFDerivAt_iff_tendsto, this] at ha

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with a ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is ae-measurable
for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasFDerivAt_integral_of_dominated_loc_of_lip {F' : Î± â†’ H â†’L[ğ•œ] E}
    (hs : s âˆˆ ğ“ xâ‚€) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) Î¼)
    (hF_int : Integrable (F xâ‚€) Î¼) (hF'_meas : AEStronglyMeasurable F' Î¼)
    (h_lip : âˆ€áµ a âˆ‚Î¼, LipschitzOnWith (Real.nnabs <| bound a) (F Â· a) s)
    (bound_integrable : Integrable (bound : Î± â†’ â„) Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, HasFDerivAt (F Â· a) (F' a) xâ‚€) :
    Integrable F' Î¼ âˆ§ HasFDerivAt (fun x â†¦ âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ := by
  obtain âŸ¨Î´, Î´_pos, hÎ´âŸ© : âˆƒ Î´ > 0, âˆ€ x âˆˆ ball xâ‚€ Î´, AEStronglyMeasurable (F x) Î¼ âˆ§ x âˆˆ s :=
    eventually_nhds_iff_ball.mp (hF_meas.and hs)
  choose hÎ´_meas hÎ´Îµ using hÎ´
  replace h_lip : âˆ€áµ a : Î± âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Î´, â€–F x a - F xâ‚€ aâ€– â‰¤ |bound a| * â€–x - xâ‚€â€– :=
    h_lip.mono fun a lip x hx â†¦ lip.norm_sub_le (hÎ´Îµ x hx) (mem_of_mem_nhds hs)
  replace bound_integrable := bound_integrable.norm
  apply hasFDerivAt_integral_of_dominated_loc_of_lip' (ball_mem_nhds xâ‚€ Î´_pos) <;> assumption

open scoped Interval in
/-- Differentiation under integral of `x â†¦ âˆ« x in a..b, F x t` at a given point `xâ‚€ âˆˆ (a,b)`,
assuming `F xâ‚€` is integrable on `(a,b)`, that `x â†¦ F x t` is Lipschitz on a ball around `xâ‚€`
for almost every `t` (with a ball radius independent of `t`) with integrable Lipschitz bound,
and `F x` is a.e.-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasFDerivAt_integral_of_dominated_loc_of_lip_interval [NormedSpace â„ H] {Î¼ : Measure â„}
    {F : H â†’ â„ â†’ E} {F' : â„ â†’ H â†’L[â„] E} {a b : â„} {bound : â„ â†’ â„} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) <| Î¼.restrict (Î™ a b))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' <| Î¼.restrict (Î™ a b))
    (h_lip : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b),
      LipschitzOnWith (Real.nnabs <| bound t) (F Â· t) s)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b), HasFDerivAt (F Â· t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasFDerivAt (fun x â†¦ âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  simp_rw [AEStronglyMeasurable.aestronglyMeasurable_uIoc_iff, eventually_and] at hF_meas hF'_meas
  rw [ae_restrict_uIoc_iff] at h_lip h_diff
  have Hâ‚ :=
    hasFDerivAt_integral_of_dominated_loc_of_lip hs hF_meas.1 hF_int.1 hF'_meas.1 h_lip.1
      bound_integrable.1 h_diff.1
  have Hâ‚‚ :=
    hasFDerivAt_integral_of_dominated_loc_of_lip hs hF_meas.2 hF_int.2 hF'_meas.2 h_lip.2
      bound_integrable.2 h_diff.2
  exact âŸ¨âŸ¨Hâ‚.1, Hâ‚‚.1âŸ©, Hâ‚.2.sub Hâ‚‚.2âŸ©

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a ball around `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the ball radius is independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasFDerivAt_integral_of_dominated_of_fderiv_le {F' : H â†’ Î± â†’ H â†’L[ğ•œ] E} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) Î¼) (hF_int : Integrable (F xâ‚€) Î¼)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) Î¼)
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, â€–F' x aâ€– â‰¤ bound a)
    (bound_integrable : Integrable (bound : Î± â†’ â„) Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, HasFDerivAt (F Â· a) (F' x a) x) :
    HasFDerivAt (fun x â†¦ âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ := by
  letI : NormedSpace â„ H := NormedSpace.restrictScalars â„ ğ•œ H
  rcases Metric.mem_nhds_iff.1 hs with âŸ¨Îµ, Îµ_pos, hÎµâŸ©
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, HasFDerivAt (F Â· a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono fun a ha â†¦ ha xâ‚€ (hÎµ xâ‚€_in)
  have : âˆ€áµ a âˆ‚Î¼, LipschitzOnWith (Real.nnabs (bound a)) (F Â· a) (ball xâ‚€ Îµ) := by
    apply (h_diff.and h_bound).mono
    rintro a âŸ¨ha_deriv, ha_boundâŸ©
    refine (convex_ball _ _).lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
      (fun x x_in â†¦ (ha_deriv x (hÎµ x_in)).hasFDerivWithinAt) fun x x_in â†¦ ?_
    rw [â† NNReal.coe_le_coe, coe_nnnorm, Real.coe_nnabs]
    exact (ha_bound x (hÎµ x_in)).trans (le_abs_self _)
  apply (hasFDerivAt_integral_of_dominated_loc_of_lip (ball_mem_nhds xâ‚€ Îµ_pos)
    hF_meas hF_int hF'_meas this bound_integrable diff_xâ‚€).2

open scoped Interval in
/-- Differentiation under integral of `x â†¦ âˆ« x in a..b, F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable on `(a,b)`, `x â†¦ F x a` is differentiable on a ball around `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the ball radius is independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasFDerivAt_integral_of_dominated_of_fderiv_le'' [NormedSpace â„ H] {Î¼ : Measure â„}
    {F : H â†’ â„ â†’ E} {F' : H â†’ â„ â†’ H â†’L[â„] E} {a b : â„} {bound : â„ â†’ â„} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) <| Î¼.restrict (Î™ a b))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) <| Î¼.restrict (Î™ a b))
    (h_bound : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b), âˆ€ x âˆˆ s, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b), âˆ€ x âˆˆ s, HasFDerivAt (F Â· t) (F' x t) x) :
    HasFDerivAt (fun x â†¦ âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [ae_restrict_uIoc_iff] at h_diff h_bound
  simp_rw [AEStronglyMeasurable.aestronglyMeasurable_uIoc_iff, eventually_and] at hF_meas hF'_meas
  exact
    (hasFDerivAt_integral_of_dominated_of_fderiv_le hs hF_meas.1 hF_int.1 hF'_meas.1 h_bound.1
          bound_integrable.1 h_diff.1).sub
      (hasFDerivAt_integral_of_dominated_of_fderiv_le hs hF_meas.2 hF_int.2 hF'_meas.2 h_bound.2
        bound_integrable.2 h_diff.2)

/-- A convenient special case of `hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if there exist a neighbourhood `u` of `xâ‚€` and a compact set `k` such that `F.uncurry : H Ã— Î± â†’ E`
is continuous and continuously differentiable in the first argument on `u Ã—Ë¢ k`, then a derivative
of `fun x => âˆ« a in k, F x a âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« a in k, fderiv ğ•œ (fun x â†¦ F x a) xâ‚€ âˆ‚Î¼`. -/
theorem hasFDerivAt_integral_of_continuousOn_fderiv [TopologicalSpace Î±] [T2Space Î±]
    [OpensMeasurableSpace Î±] {F : H â†’ Î± â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {k : Set Î±}
    (hk : IsCompact k) (hk' : Î¼ k < âŠ¤) (hFâ‚ : ContinuousOn F.uncurry (u Ã—Ë¢ k))
    (hFâ‚‚ : âˆ€ a âˆˆ k, DifferentiableOn ğ•œ (fun x â†¦ F x a) u)
    (hFâ‚ƒ : ContinuousOn (fun x â†¦ fderiv ğ•œ (fun y â†¦ F y x.2) x.1) (u Ã—Ë¢ k)) :
    HasFDerivAt (fun x => âˆ« a in k, F x a âˆ‚Î¼)
      (âˆ« a in k, fderiv ğ•œ (fun x â†¦ F x a) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  -- wlog shrink u to an open neighbourhood
  wlog hu' : IsOpen u with h
  Â· have âŸ¨u', hu'âŸ© := _root_.mem_nhds_iff.1 hu
    exact h (hu'.2.1.mem_nhds hu'.2.2) hk hk' (hFâ‚.mono <| prod_mono_left hu'.1)
      (fun a ha â†¦ (hFâ‚‚ a ha).mono hu'.1) (hFâ‚ƒ.mono <| prod_mono_left hu'.1) hu'.2.1
  have hxu := mem_of_mem_nhds hu
  let F' := fun x : H Ã— Î± â†¦ â€–fderiv ğ•œ (fun y â†¦ F y x.2) x.1â€–
  have hF' : ContinuousOn F' _ := continuous_norm.comp_continuousOn hFâ‚ƒ
  -- via a compactness argument, find an Îµ > 0 such that F' is bounded on `ball xâ‚€ Îµ Ã— k`
  let âŸ¨Îµ, hÎµ, hÎµ', B, hBâŸ© :
      âˆƒ Îµ > 0, ball xâ‚€ Îµ âŠ† u âˆ§ âˆƒ B, âˆ€ x âˆˆ ball xâ‚€ Îµ Ã—Ë¢ k, F' x < B := by
    let âŸ¨B, hBâŸ© := (isCompact_singleton.prod hk).bddAbove_image <|
      hF'.mono <| prod_mono_left <| singleton_subset_iff.2 hxu
    have âŸ¨v, hv, hv'âŸ© := generalized_tube_lemma_left (s := {xâ‚€}) isCompact_singleton
      hk (s' := u) (n := F' â»Â¹' (Iio (B + 1))) (by
        refine nhdsSetWithin_mono_left ?_ <| hF'.preimage_mem_nhdsSetWithin_of_mem_nhdsSet
          (t := Iic B) (u := Iio (B + 1)) <| isOpen_Iio.mem_nhdsSet.2 (by simp)
        intro x hx
        exact âŸ¨prod_mono_left (by simp [hxu]) hx, mem_upperBounds.1 hB _ <| mem_image_of_mem _ hxâŸ©)
    rw [nhdsSetWithin_singleton, hu'.nhdsWithin_eq hxu] at hv
    have âŸ¨Îµ, hÎµ, hÎµ'âŸ© := Metric.mem_nhds_iff.1 (Filter.inter_mem hv (hu))
    exact âŸ¨Îµ, hÎµ, hÎµ'.trans inter_subset_right, B + 1,
      fun x hx â†¦ hv' <| prod_mono_left (hÎµ'.trans inter_subset_left) hxâŸ©
  -- now apply `hasFDerivAt_integral_of_dominated_of_fderiv_le` with the obtained Îµ and bound
  have hk'' : MeasurableSet k := hk.measurableSet
  have := isCompact_iff_compactSpace.1 hk
  have : IsFiniteMeasure (Measure.comap ((â†‘) : k â†’ Î±) Î¼) :=
    âŸ¨by simp [(MeasurableEmbedding.subtype_coe hk'').comap_apply, hk']âŸ©
  simp_rw [â† integral_subtype_comap hk'']
  refine hasFDerivAt_integral_of_dominated_of_fderiv_le (bound := fun _ â†¦ B)
    (F' := fun x (a : k) â†¦ fderiv ğ•œ (fun x â†¦ F x a) x) (ball_mem_nhds xâ‚€ hÎµ) ?_ ?_ ?_ ?_ ?_ ?_
  Â· refine eventually_nhds_iff.2 âŸ¨u, fun x hx â†¦ ?_, hu', hxuâŸ©
    refine Continuous.aestronglyMeasurable_of_compactSpace ?_
    exact (hFâ‚.uncurry_left x hx).comp_continuous (by fun_prop) (by simp)
  Â· refine integrableOn_univ.1 <| ContinuousOn.integrableOn_compact isCompact_univ <|
      continuousOn_univ.2 <| (hFâ‚.uncurry_left _ hxu).comp_continuous (by fun_prop) (by simp)
  Â· refine Continuous.aestronglyMeasurable_of_compactSpace ?_
    exact hFâ‚ƒ.comp_continuous (f := fun a : k â†¦ (xâ‚€, â†‘a)) (by fun_prop) fun a â†¦ âŸ¨hxu, a.2âŸ©
  Â· refine .of_forall fun a x hx â†¦ (hB (x, a) âŸ¨hx, a.2âŸ©).le
  Â· exact integrable_const _
  Â· refine .of_forall fun a x hx â†¦ ?_
    exact (DifferentiableOn.differentiableAt (hFâ‚‚ a <| a.2) (hu'.mem_nhds <| hÎµ' hx)).hasFDerivAt

/-- A convenient special case of `hasFDerivAt_integral_of_continuousOn_fderiv`:
if `f.uncurry : H Ã— H' â†’ E` is continuously differentiable on `u Ã—Ë¢ k` for a neighbourhood `u`
of `xâ‚€` and a nice compact set `k`, then a derivative of `fun x => âˆ« a in k, f x a âˆ‚Î¼` in `xâ‚€` can
be computed as `âˆ« a in k, fderiv ğ•œ (fun x â†¦ f x a) xâ‚€ âˆ‚Î¼`. -/
theorem hasFDerivAt_integral_of_contDiffOn {Î¼ : Measure H'} {f : H â†’ H' â†’ E} {xâ‚€ : H}
    {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {k : Set H'} (hk : IsCompact k) (hk' : Î¼ k < âŠ¤)
    (hk'' : UniqueDiffOn ğ•œ k) (hF : ContDiffOn ğ•œ 1 f.uncurry (u Ã—Ë¢ k)) :
    HasFDerivAt (fun x => âˆ« a in k, f x a âˆ‚Î¼) (âˆ« a in k, fderiv ğ•œ (fun x â†¦ f x a) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hu' : IsOpen u with h
  Â· have âŸ¨u', hu'âŸ© := _root_.mem_nhds_iff.1 hu
    exact h (hu'.2.1.mem_nhds hu'.2.2) hk hk' hk'' (hF.mono <| prod_mono_left hu'.1) hu'.2.1
  refine hasFDerivAt_integral_of_continuousOn_fderiv hu hk hk' hF.continuousOn (fun a ha â†¦
    hF.differentiableOn_one.comp (by fun_prop) fun x hx â†¦ (âŸ¨hx, haâŸ© : (x, a) âˆˆ _ Ã—Ë¢ _)) ?_
  refine .congr (f := fun x â†¦ (fderivWithin ğ•œ f.uncurry (u Ã—Ë¢ k) x).comp (.inl ğ•œ H H'))
      ?_ fun x hx â†¦ ?_
  Â· refine ((ContinuousLinearMap.compL ğ•œ H (H Ã— H') E).flip _).continuous.comp_continuousOn ?_
    exact hF.continuousOn_fderivWithin (hu'.uniqueDiffOn.prod hk'') le_rfl
  Â· rw [show (fun y â†¦ f y x.2) = (f.uncurry âˆ˜ fun y â†¦ (y, x.2)) by rfl]
    rw [â† fderivWithin_eq_fderiv (s := u) (hu'.uniqueDiffWithinAt hx.1) <| by
      refine DifferentiableOn.differentiableAt (s := u) ?_ (hu'.mem_nhds hx.1)
      exact ((hF.differentiableOn one_ne_zero).comp (by fun_prop) (fun y hy â†¦ âŸ¨hy, hx.2âŸ©))]
    rw [fderivWithin_comp _ (t := u Ã—Ë¢ k) (hF.differentiableOn (by simp) _ âŸ¨hx.1, hx.2âŸ©)
      (by fun_prop) (by exact fun y hy â†¦ âŸ¨hy, hx.2âŸ©) (hu'.uniqueDiffWithinAt hx.1)]
    congr
    exact (hasFDerivAt_prodMk_left _ x.2).hasFDerivWithinAt.fderivWithin
      (hu'.uniqueDiffWithinAt hx.1)

/-- Iterated differentiation under integral of `x â†¦ âˆ« F x a` on an open set `s`, assuming that each
function `x â†¦ F x a` has a Taylor series of order `n`, with uniform integrability conditions on
the successive derivatives. -/
theorem hasFTaylorSeriesOn_integral_of_le_bound {n : WithTop â„•âˆ} {bound : â„• â†’ Î± â†’ â„}
    {p : H â†’ Î± â†’ FormalMultilinearSeries ğ•œ H E} (hs : IsOpen s)
    (hF_meas : âˆ€ x âˆˆ s, âˆ€ (i : â„•), i â‰¤ n â†’ AEStronglyMeasurable (p x Â· i) Î¼)
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, âˆ€ (i : â„•), i â‰¤ n â†’ â€–p x a iâ€– â‰¤ bound i a)
    (bound_integrable : âˆ€ (i : â„•), i â‰¤ n â†’ Integrable (bound i) Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, HasFTaylorSeriesUpToOn n (F Â· a) (p Â· a) s) :
    HasFTaylorSeriesUpToOn n (fun x â†¦ âˆ« a, F x a âˆ‚Î¼) (fun x i â†¦ âˆ« a, p x a i âˆ‚Î¼) s := by
  constructor
  Â· intro x hx
    simp only [ContinuousMultilinearMap.curry0_apply, Matrix.zero_empty]
    rw [ContinuousMultilinearMap.integral_apply]
    Â· apply integral_congr_ae
      filter_upwards [h_diff] with a ha
      simp [â† ha.zero_eq x hx]
    Â· apply Integrable.mono' (bound_integrable 0 (by simp)) (hF_meas x hx 0 (by simp))
      filter_upwards [h_bound] with a ha using ha x hx 0 (by simp)
  Â· intro i hi x hx
    have h'i : (i + 1 : â„•) â‰¤ n := ENat.add_one_natCast_le_withTop_of_lt hi
    apply HasFDerivAt.hasFDerivWithinAt
    change HasFDerivAt (fun x â†¦ âˆ« a, p x a i âˆ‚Î¼)
      ((continuousMultilinearCurryLeftEquiv ğ•œ (fun i â†¦ H) E).toContinuousLinearEquiv
        (âˆ« a, p x a i.succ âˆ‚Î¼)) x
    -- next line should not be necessary...
    let A : NormedSpace â„ (H â†’L[ğ•œ] (H [Ã—i]â†’L[ğ•œ] E)) := ContinuousLinearMap.toNormedSpace
    rw [â† ContinuousLinearEquiv.integral_comp_comm]
    let G : H â†’ Î± â†’ (H [Ã—i]â†’L[ğ•œ] E) := fun x a â†¦ p x a i
    let G' : H â†’ Î± â†’ H â†’L[ğ•œ] (H [Ã—i]â†’L[ğ•œ] E) := fun x a â†¦
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun i â†¦ H) E) (p x a i.succ)
    change HasFDerivAt (fun x â†¦ âˆ« a, G x a âˆ‚Î¼) (âˆ« a, G' x a âˆ‚Î¼) x
    have s_mem : s âˆˆ ğ“ x := hs.mem_nhds hx
    apply hasFDerivAt_integral_of_dominated_of_fderiv_le (s := s) (bound := bound (i + 1)) s_mem
    Â· filter_upwards [s_mem] with y hy using hF_meas _ hy _ hi.le
    Â· apply Integrable.mono' (bound_integrable i hi.le) (hF_meas _ hx _ hi.le)
      filter_upwards [h_bound] with a ha using ha x hx i hi.le
    Â· apply Continuous.comp_aestronglyMeasurable (by fun_prop)
      exact hF_meas x hx i.succ h'i
    Â· filter_upwards [h_bound] with a ha y hy
      simp only [Nat.succ_eq_add_one, LinearIsometryEquiv.norm_map, G']
      apply ha _ hy _ h'i
    Â· apply bound_integrable _ h'i
    Â· filter_upwards [h_diff] with a ha y hy
      exact (ha.fderivWithin i hi y hy).hasFDerivAt (hs.mem_nhds hy)
  Â· intro i hi
    apply continuousOn_of_dominated (bound := bound i)
    Â· intro y hy
      exact hF_meas y hy i hi
    Â· intro y hy
      filter_upwards [h_bound] with a ha using ha y hy i hi
    Â· apply bound_integrable i hi
    Â· filter_upwards [h_diff] with a ha using ha.cont i hi

open ContinuousMultilinearMap in
theorem hasFTaylorSeriesOn_setIntegral_of_le_const
    {n : WithTop â„•âˆ} {C : â„• â†’ â„} {Î¼ : Measure H'}
    {p : H Ã— H' â†’ FormalMultilinearSeries ğ•œ (H Ã— H') E} (hs : IsOpen s)
    {t : Set H'} {F : H â†’ H' â†’ E} (ht : IsSeparable t) (tmeas : MeasurableSet t) (hmut : Î¼ t â‰  âŠ¤)
    (hF : HasFTaylorSeriesUpToOn n F.uncurry p (s Ã—Ë¢ t))
    (h_bound : âˆ€ x âˆˆ s, âˆ€ a âˆˆ t, âˆ€ (i : â„•), i â‰¤ n â†’ â€–p (x, a) iâ€– â‰¤ C i) :
    HasFTaylorSeriesUpToOn n (fun x â†¦ âˆ« a in t, F x a âˆ‚Î¼)
      (fun x i â†¦ âˆ« a in t, (p (x, a) i).compContinuousLinearMap
        (fun _ â†¦ ContinuousLinearMap.inl ğ•œ H H') âˆ‚Î¼) s := by
  apply hasFTaylorSeriesOn_integral_of_le_bound hs (bound := fun i a â†¦ C i * âˆ (j : Fin i), 1)
  Â· intro x hx i hi
    apply ContinuousOn.aestronglyMeasurable_of_isSeparable ?_ tmeas ht
    change ContinuousOn
      (fun y â†¦ compContinuousLinearMapL (fun i â†¦ ContinuousLinearMap.inl ğ•œ H H') (p (x, y) i)) t
    apply Continuous.comp_continuousOn (by fun_prop)
    apply (hF.cont i hi).comp (by fun_prop)
    intro w hw
    exact âŸ¨hx, hwâŸ©
  Â· apply ae_restrict_of_forall_mem tmeas (fun w hw â†¦ ?_)
    intro x hx i hi
    apply (ContinuousMultilinearMap.norm_compContinuousLinearMap_le _ _).trans
    have : â€–p (x, w) iâ€– â‰¤ C i := h_bound x hx w hw i hi
    gcongr
    Â· exact le_trans (by positivity) this
    Â· exact ContinuousLinearMap.norm_inl_le_one ğ•œ H H'
  Â· intro i hi
    exact integrableOn_const hmut
  Â· apply ae_restrict_of_forall_mem tmeas (fun w hw â†¦ ?_)
    let g : H â†’á´¬[ğ•œ] H Ã— H' :=
    { toFun := fun x â†¦ (x, w)
      linear := LinearMap.inl ğ•œ H H'
      map_vadd' p v := by simp
      cont := by fun_prop }
    apply (hF.comp_continuousAffineMap g).mono
    simp only [ContinuousAffineMap.coe_mk, AffineMap.coe_mk, g]
    grind

open ContinuousMultilinearMap in
/-- If `f.uncurry : H Ã— H' â†’ E` is `Câ¿` on `u Ã—Ë¢ k` for an open set `u` and a compact set `k`, then
given any subset `sâ‚€` of `k` the parametric integral `fun x â†¦ âˆ« a in sâ‚€, f x a âˆ‚Î¼`
is `Câ¿` on `u` too. -/
lemma ContDiffOn.parametric_integral
    {Î¼ : Measure H'} {f : H â†’ H' â†’ E} {u : Set H} (hu : IsOpen u)
    {sâ‚€ k : Set H'} (hk : IsCompact k) {n : â„•âˆ} (hsâ‚€ : sâ‚€ âŠ† k)
    (hf : ContDiffOn ğ•œ n f.uncurry (u Ã—Ë¢ k)) (musâ‚€ : Î¼ sâ‚€ â‰  âŠ¤) :
    ContDiffOn ğ•œ n (fun x â†¦ âˆ« a in sâ‚€, f x a âˆ‚Î¼) u := by
  /- Locally, this is already proved in `hasFTaylorSeriesOn_setIntegral_of_le_const` (which moreover
  gives a formula for the successive derivatives). To globalize, one covers the compact set `k`
  with finitely many sets on which the local property golds. Technically, this is more conveniently
  done using the induction principle `IsCompact.induction_on` in which one only needs to check
  the property locally, and invariance under binary union. -/
  intro x hx
  apply contDiffWithinAt_iff_forall_nat_le.2 (fun m hm â†¦ ?_)
  suffices âˆƒ s, k âˆ© k âŠ† s âˆ§ s âŠ† k âˆ§ MeasurableSet s âˆ§ âˆ€ t âŠ† s, MeasurableSet t â†’ Î¼ t â‰  âŠ¤ â†’
      ContDiffWithinAt ğ•œ m (fun x â†¦ âˆ« a in t, f x a âˆ‚Î¼) u x by
    rcases this with âŸ¨s, ks, sk, -, hsâŸ©
    rw [show s = k by grind] at hs
    have : ContDiffWithinAt ğ•œ m (fun x â†¦ âˆ« a in k âˆ© toMeasurable Î¼ sâ‚€, f x a âˆ‚Î¼) u x := by
      apply hs _ inter_subset_left (hk.measurableSet.inter (measurableSet_toMeasurable _ _))
      apply ((measure_mono inter_subset_right).trans_lt ?_).ne
      rw [measure_toMeasurable]
      exact musâ‚€.lt_top
    convert this using 3
    exact (Measure.restrict_inter_toMeasurable musâ‚€ hk.measurableSet hsâ‚€).symm
  apply IsCompact.induction_on (s := k)
    (p := fun sâ‚€ â†¦ âˆƒ s, k âˆ© sâ‚€ âŠ† s âˆ§ s âŠ† k âˆ§ MeasurableSet s âˆ§ âˆ€ t âŠ† s, MeasurableSet t â†’
      Î¼ t â‰  âŠ¤ â†’ ContDiffWithinAt ğ•œ m (fun x â†¦ âˆ« a in t, f x a âˆ‚Î¼) u x) hk
  Â· simp only [inter_empty, empty_subset, true_and]
    exact âŸ¨âˆ…, by simpa using contDiffWithinAt_constâŸ©
  Â· grind
  Â· -- check invariance of the property under binary union
    rintro s s' âŸ¨t, kt, tk, tmeas, htâŸ© âŸ¨t', kt', t'k, t'meas, ht'âŸ©
    refine âŸ¨t âˆª t', by grind, by grind, tmeas.union t'meas, fun v hv vmeas muv â†¦ ?_âŸ©
    let vâ‚ := v âˆ© t
    let vâ‚‚ := v \ vâ‚
    have vâ‚meas : MeasurableSet vâ‚ := vmeas.inter tmeas
    have vâ‚‚meas : MeasurableSet vâ‚‚ := vmeas.diff vâ‚meas
    have muvâ‚ : Î¼ vâ‚ â‰  âŠ¤ := ((measure_mono inter_subset_left).trans_lt muv.lt_top).ne
    have muvâ‚‚ : Î¼ vâ‚‚ â‰  âŠ¤ := ((measure_mono diff_subset).trans_lt muv.lt_top).ne
    have : ContDiffWithinAt ğ•œ m (fun x â†¦ âˆ« a in vâ‚, f x a âˆ‚Î¼ + âˆ« a in vâ‚‚, f x a âˆ‚Î¼) u x :=
      (ht vâ‚ inter_subset_right vâ‚meas muvâ‚).add (ht' vâ‚‚ (by grind) vâ‚‚meas muvâ‚‚)
    apply this.congr_of_mem (fun y hy â†¦ ?_) hx
    rw [show v = vâ‚ âˆª vâ‚‚ by grind, setIntegral_union disjoint_sdiff_left.symm vâ‚‚meas]
    Â· exact (hf.continuousOn.uncurry_left _ hy).integrableOn_of_subset_isCompact
        hk vâ‚meas (inter_subset_right.trans tk) muvâ‚
    Â· exact (hf.continuousOn.uncurry_left _ hy).integrableOn_of_subset_isCompact
        hk vâ‚‚meas (by grind only [= subset_def, = mem_diff, = mem_union]) muvâ‚‚
  -- check the property locally using `hasFTaylorSeriesOn_setIntegral_of_le_const`
  intro y hy
  obtain âŸ¨v, v_mem, p, hpâŸ© : âˆƒ v âˆˆ ğ“[insert (x, y) (u Ã—Ë¢ k)] (x, y), âˆƒ p,
    HasFTaylorSeriesUpToOn m (Function.uncurry f) p v := hf (x, y) âŸ¨hx, hyâŸ© m hm
  obtain âŸ¨u', u'_mem, k', k'_mem, u'_open,  k'meas, k'k, hk'v, hk'_boundâŸ© :
      âˆƒ u' âˆˆ ğ“ x, âˆƒ k' âˆˆ ğ“[k] y, IsOpen u' âˆ§ MeasurableSet k' âˆ§ k' âŠ† k âˆ§ u' Ã—Ë¢ k' âŠ† v
      âˆ§ âˆ€ N â‰¤ m, âˆ€ z âˆˆ u' Ã—Ë¢ k', â€–p z Nâ€– < 1 + â€–p (x, y) Nâ€– := by
    rw [show insert (x, y) (u Ã—Ë¢ k) = u Ã—Ë¢ k from insert_eq_of_mem (by exact âŸ¨hx, hyâŸ©)] at v_mem
    let v'' := â‹‚ N âˆˆ Finset.Iic m, {z | â€–p z Nâ€– < 1 + â€–p (x, y) Nâ€–}
    have : v'' âˆˆ ğ“[u Ã—Ë¢ k] (x, y) := by
      apply (Filter.biInter_finset_mem _).2 (fun i hi â†¦ ?_)
      apply nhdsWithin_le_of_mem v_mem
      have xyv : (x, y) âˆˆ v := mem_of_mem_nhdsWithin (by exact âŸ¨hx, hyâŸ©) v_mem
      have : ContinuousWithinAt (fun z â†¦ â€–p z iâ€–) v (x, y) :=
        (hp.cont i (by simpa using hi) (x, y) xyv).norm
      exact this.preimage_mem_nhdsWithin (Iio_mem_nhds (by linarith))
    have v'_mem : v âˆ© v'' âˆˆ ğ“[u Ã—Ë¢ k] (x, y) := by
      apply Filter.inter_mem v_mem this
    rw [nhdsWithin_prod_eq, Filter.mem_prod_iff, IsOpen.nhdsWithin_eq hu hx] at v'_mem
    rcases v'_mem with âŸ¨u', u'_mem, t', t'_mem, ht'âŸ©
    rw [mem_nhdsWithin] at t'_mem
    rcases t'_mem with âŸ¨t'', t''_open, t''_mem, ht''âŸ©
    rcases _root_.mem_nhds_iff.1 u'_mem with âŸ¨u'', hu'', u''_open, xu''âŸ©
    refine âŸ¨u'', u''_open.mem_nhds xu'', t'' âˆ© k, ?_, u''_open,
      t''_open.measurableSet.inter hk.measurableSet, inter_subset_right, ?_, ?_âŸ©
    Â· rw [inter_comm]
      exact inter_mem_nhdsWithin _ (t''_open.mem_nhds t''_mem)
    Â· exact Subset.trans (by gcongr) (ht'.trans inter_subset_left)
    Â· intro i hi z z_mem
      have : z âˆˆ v'' := by
        have : u'' Ã—Ë¢ (t'' âˆ© k) âŠ† v'' := Subset.trans (by gcongr) (ht'.trans inter_subset_right)
        exact this z_mem
      simp only [Finset.mem_Iic, mem_iInter, mem_setOf_eq, v''] at this
      exact this i hi
  refine âŸ¨k', k'_mem, k', inter_subset_right, k'k, k'meas, fun t tk' tmeas hmut â†¦ ?_âŸ©
  have : HasFTaylorSeriesUpToOn m (fun x â†¦ âˆ« a in t, f x a âˆ‚Î¼)
      (fun x i â†¦ âˆ« a in t, (p (x, a) i).compContinuousLinearMap
        (fun _ â†¦ ContinuousLinearMap.inl ğ•œ H H') âˆ‚Î¼) u' := by
    apply hasFTaylorSeriesOn_setIntegral_of_le_const u'_open (hk.isSeparable.mono (tk'.trans k'k))
      tmeas hmut (hp.mono (by grind)) (C := fun i â†¦ 1 + â€–p (x, y) iâ€–)
    intro x' hx' a ha i hi
    exact (hk'_bound i (mod_cast hi) (x', a) âŸ¨hx', tk' haâŸ©).le
  apply ContDiffWithinAt.mono_of_mem_nhdsWithin ?_ (nhdsWithin_le_nhds u'_mem)
  exact contDiffWithinAt_nat.2 âŸ¨u', nhdsWithin_le_nhds u'_mem, _, thisâŸ©

/-- If `f.uncurry : H Ã— H' â†’ E` is `Câ¿`, the parametric integral `fun x â†¦ âˆ« a in sâ‚€, f x a âˆ‚Î¼`
over a set `sâ‚€` contained in a compact set `k` is `Câ¿` too. -/
lemma ContDiff.parametric_integral {H' : Type*}
    [NormedAddCommGroup H'] [NormedSpace ğ•œ H'] [MeasurableSpace H'] [OpensMeasurableSpace H']
    {Î¼ : Measure H'} {f : H â†’ H' â†’ E} {k sâ‚€ : Set H'} (hk : IsCompact k) {n : â„•âˆ} (hsâ‚€ : sâ‚€ âŠ† k)
    (hf : ContDiff ğ•œ n f.uncurry) (musâ‚€ : Î¼ sâ‚€ â‰  âŠ¤) :
    ContDiff ğ•œ n (fun x â†¦ âˆ« a in sâ‚€, f x a âˆ‚Î¼) :=
  contDiffOn_univ.1 <| hf.contDiffOn.parametric_integral isOpen_univ hk hsâ‚€ musâ‚€

section

variable {F : ğ•œ â†’ Î± â†’ E} {xâ‚€ : ğ•œ} {s : Set ğ•œ}

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is
ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasDerivAt_integral_of_dominated_loc_of_lip {F' : Î± â†’ E} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) Î¼) (hF_int : Integrable (F xâ‚€) Î¼)
    (hF'_meas : AEStronglyMeasurable F' Î¼)
    (h_lipsch : âˆ€áµ a âˆ‚Î¼, LipschitzOnWith (Real.nnabs <| bound a) (F Â· a) s)
    (bound_integrable : Integrable (bound : Î± â†’ â„) Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, HasDerivAt (F Â· a) (F' a) xâ‚€) :
    Integrable F' Î¼ âˆ§ HasDerivAt (fun x â†¦ âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ := by
  set L : E â†’L[ğ•œ] ğ•œ â†’L[ğ•œ] E := ContinuousLinearMap.smulRightL ğ•œ ğ•œ E 1
  replace h_diff : âˆ€áµ a âˆ‚Î¼, HasFDerivAt (F Â· a) (L (F' a)) xâ‚€ :=
    h_diff.mono fun x hx â†¦ hx.hasFDerivAt
  have hm : AEStronglyMeasurable (L âˆ˜ F') Î¼ := L.continuous.comp_aestronglyMeasurable hF'_meas
  obtain âŸ¨hF'_int, keyâŸ© := hasFDerivAt_integral_of_dominated_loc_of_lip
    hs hF_meas hF_int hm h_lipsch bound_integrable h_diff
  replace hF'_int : Integrable F' Î¼ := by
    rw [â† integrable_norm_iff hm] at hF'_int
    simpa [L, (Â· âˆ˜ Â·), integrable_norm_iff hF'_meas] using hF'_int
  refine âŸ¨hF'_int, ?_âŸ©
  by_cases hE : CompleteSpace E; swap
  Â· simpa [integral, hE] using hasDerivAt_const xâ‚€ 0
  simp_rw [hasDerivAt_iff_hasFDerivAt] at h_diff âŠ¢
  simpa only [(Â· âˆ˜ Â·), ContinuousLinearMap.integral_comp_comm _ hF'_int] using key

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : â„`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on an interval around `xâ‚€` for ae `a`
(with interval radius independent of `a`) with derivative uniformly bounded by an integrable
function, and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
theorem hasDerivAt_integral_of_dominated_loc_of_deriv_le (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) Î¼) (hF_int : Integrable (F xâ‚€) Î¼)
    {F' : ğ•œ â†’ Î± â†’ E} (hF'_meas : AEStronglyMeasurable (F' xâ‚€) Î¼)
    (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, â€–F' x aâ€– â‰¤ bound a) (bound_integrable : Integrable bound Î¼)
    (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ s, HasDerivAt (F Â· a) (F' x a) x) :
    Integrable (F' xâ‚€) Î¼ âˆ§ HasDerivAt (fun n â†¦ âˆ« a, F n a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ := by
  rcases Metric.mem_nhds_iff.1 hs with âŸ¨Îµ, Îµ_pos, hÎµâŸ©
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, HasDerivAt (F Â· a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono fun a ha â†¦ ha xâ‚€ (hÎµ xâ‚€_in)
  have : âˆ€áµ a âˆ‚Î¼, LipschitzOnWith (Real.nnabs (bound a)) (fun x : ğ•œ â†¦ F x a) (ball xâ‚€ Îµ) := by
    apply (h_diff.and h_bound).mono
    rintro a âŸ¨ha_deriv, ha_boundâŸ©
    refine (convex_ball _ _).lipschitzOnWith_of_nnnorm_hasDerivWithin_le
      (fun x x_in â†¦ (ha_deriv x (hÎµ x_in)).hasDerivWithinAt) fun x x_in â†¦ ?_
    rw [â† NNReal.coe_le_coe, coe_nnnorm, Real.coe_nnabs]
    exact (ha_bound x (hÎµ x_in)).trans (le_abs_self _)
  exact hasDerivAt_integral_of_dominated_loc_of_lip (ball_mem_nhds xâ‚€ Îµ_pos) hF_meas hF_int
    hF'_meas this bound_integrable diff_xâ‚€

end
