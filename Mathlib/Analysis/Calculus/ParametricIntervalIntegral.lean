/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.ParametricIntegral

/-!
# Derivatives of interval integrals depending on parameters

In this file we restate theorems about derivatives of integrals depending on parameters for interval
integrals. -/

universe u

open TopologicalSpace MeasureTheory Filter Metric Set

open scoped Topology Filter Interval

variable {ğ•œ : Type*} [RCLike ğ•œ] {Î¼ : Measure â„} {E : Type*} [NormedAddCommGroup E]
  [NormedSpace â„ E] [NormedSpace ğ•œ E] {H : Type*} [NormedAddCommGroup H]
  [NormedSpace ğ•œ H] {a b Îµ : â„} {bound : â„ â†’ â„}

namespace intervalIntegral

/-- Differentiation under integral of `x â†¦ âˆ« t in a..b, F x t` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with a ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is ae-measurable
for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_loc_of_lip
    {F : H â†’ â„ â†’ E} {F' : â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lip : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) (ball xâ‚€ Îµ))
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasFDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lip h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasFDerivAt_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas h_lip
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a ball around `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the ball radius is independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_of_fderiv_le
    {F : H â†’ â„ â†’ E} {F' : H â†’ â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H} (Îµ_pos : 0 < Îµ)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, HasFDerivAt (fun x => F x t) (F' x t) x) :
    HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable
  simp only [intervalIntegral_eq_integral_uIoc]
  exact (hasFDerivAt_integral_of_dominated_of_fderiv_le Îµ_pos hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff).const_smul _

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is
ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_lip {F : ğ•œ â†’ â„ â†’ E} {F' : â„ â†’ E} {xâ‚€ : ğ•œ}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lipsch : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) (ball xâ‚€ Îµ))
    (bound_integrable : IntervalIntegrable (bound : â„ â†’ â„) Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lipsch h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas h_lipsch
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on an interval around `xâ‚€` for ae `a`
(with interval radius independent of `a`) with derivative uniformly bounded by an integrable
function, and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_deriv_le
    {F : ğ•œ â†’ â„ â†’ E} {F' : ğ•œ â†’ â„ â†’ E} {xâ‚€ : ğ•œ}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, HasDerivAt (fun x => F x t) (F' x t) x) :
    IntervalIntegrable (F' xâ‚€) Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_deriv_le Îµ_pos hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if there exists a neighbourhood `u` of `xâ‚€` such that `f : H Ã— â„ â†’ E` is continuous on
`u Ã—Ë¢ [[a, b]]` and differentiable on `u` in the first argument for every `t âˆˆ [[a, b]]` and that
derivative is continuous on `u Ã—Ë¢ [[a, b]]`, then a derivative of
`fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv ğ•œ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼`. -/
nonrec theorem hasFDerivAt_integral_of_continuousOn_fderiv [IsLocallyFiniteMeasure Î¼] [NoAtoms Î¼]
    {f : H Ã— â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hFâ‚ : ContinuousOn f (u Ã—Ë¢ [[a, b]]))
    (hFâ‚‚ : âˆ€ t âˆˆ [[a, b]], DifferentiableOn ğ•œ (fun x â†¦ f (x, t)) u)
    (hFâ‚ƒ : ContinuousOn (fun x â†¦ fderiv ğ•œ (fun y â†¦ f (y, x.2)) x.1) (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼)
      (âˆ« t in a..b, fderiv ğ•œ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a â‰¤ b with h
  Â· simp_rw [intervalIntegral.integral_symm b a]
    exact (h hu (uIcc_comm a b â–¸ hFâ‚) (uIcc_comm a b â–¸ hFâ‚‚) (uIcc_comm a b â–¸ hFâ‚ƒ)
      (le_of_not_ge hab)).neg
  simp_rw [intervalIntegral.integral_of_le hab, â† integral_Icc_eq_integral_Ioc, â† uIcc_of_le hab]
  exact hasFDerivAt_integral_of_continuousOn_fderiv (ğ•œ := ğ•œ) (Î¼ := Î¼) (H := H) (E := E)
    hu (k := [[a, b]]) isCompact_uIcc hFâ‚ hFâ‚‚ hFâ‚ƒ

/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if `f : H Ã— â„ â†’ E` is continuously differentiable on `u Ã—Ë¢ [[a, b]]` for a neighbourhood `u`
of `xâ‚€`, then a derivative of `fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼`. -/
nonrec theorem hasFDerivAt_integral_of_contDiffOn
    [IsLocallyFiniteMeasure Î¼] [NoAtoms Î¼] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {H : Type*} [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hF : ContDiffOn â„ 1 f (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼)
      (âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simpa only [intervalIntegral.integral_symm b a] using (h hu (uIcc_comm a b â–¸ hF) hab).neg
    Â· simp [hab, hasFDerivAt_const]
  wlog hu' : IsOpen u with h
  Â· have âŸ¨u', hu'âŸ© := _root_.mem_nhds_iff.1 hu
    exact h (hu'.2.1.mem_nhds hu'.2.2) (hF.mono <| prod_mono_left hu'.1) hab hu'.2.1
  refine hasFDerivAt_integral_of_continuousOn_fderiv hu hF.continuousOn
    (fun t ht â†¦ hF.differentiableOn_one.comp (by fun_prop) fun x hx â†¦ âŸ¨hx, htâŸ©) ?_
  refine .congr (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ Icc a b) x).comp (.inl â„ H â„))
      ?_ fun x hx â†¦ ?_
  Â· rw [uIcc_of_le hab.le] at hF âŠ¢
    refine ((ContinuousLinearMap.compL â„ H (H Ã— â„) E).flip
      (.inl â„ H â„)).continuous.comp_continuousOn ?_
    refine (hF.continuousOn_fderivWithin ?_ le_rfl)
    exact hu'.uniqueDiffOn.prod <| uniqueDiffOn_Icc hab
  Â· rw [show (fun y â†¦ f (y, x.2)) = (f âˆ˜ fun y â†¦ (y, x.2)) by rfl]
    rw [â† fderivWithin_eq_fderiv (s := u) (hu'.uniqueDiffWithinAt hx.1) <| by
      refine DifferentiableOn.differentiableAt (s := u) ?_ (hu'.mem_nhds hx.1)
      exact ((hF.differentiableOn le_rfl).comp (by fun_prop) (fun y hy â†¦ âŸ¨hy, hx.2âŸ©))]
    rw [uIcc_of_le hab.le] at hF hx
    rw [fderivWithin_comp _ (t := u Ã—Ë¢ Set.Icc a b) (hF.differentiableOn (by simp) _ âŸ¨hx.1, hx.2âŸ©)
      (by fun_prop) (by exact fun y hy â†¦ âŸ¨hy, hx.2âŸ©) (hu'.uniqueDiffWithinAt hx.1)]
    congr
    exact (hasFDerivAt_prodMk_left _ x.2).hasFDerivWithinAt.fderivWithin
      (hu'.uniqueDiffWithinAt hx.1)

lemma _root_.ContDiffOn.parametric_intervalIntegral {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]
    [NoAtoms Î¼] {E H : Type u} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {u : Set H} (hu : IsOpen u) {a b : â„} {n : â„•âˆ}
    (hf : ContDiffOn â„ n f (u Ã—Ë¢ [[a, b]])) :
    ContDiffOn â„ n (fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼) u := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h hu (uIcc_comm a b â–¸ hf) hab).neg
    Â· simp [hab, contDiffOn_const]
  revert E; change âˆ€ E : _, _
  refine ENat.nat_induction n ?_ ?_ ?_
  Â· intro E _ _ f
    simp_rw [WithTop.coe_zero, contDiffOn_zero]
    exact ContinuousOn.parametric_intervalIntegral
  Â· intro k h E _ _ f hf
    refine (contDiffOn_succ_iff_fderiv_of_isOpen (ğ•œ := â„) (n := k) hu).2 âŸ¨?_, by simp, ?_âŸ©
    Â· intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      exact h.differentiableAt.differentiableWithinAt
    Â· have := hf.fderivWithin (hu.uniqueDiffOn.prod <| uIcc_of_le hab.le â–¸ uniqueDiffOn_Icc hab)
        (m := k) le_rfl
      refine (h _ (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ [[a, b]]) x).comp (.inl â„ H â„))
        (by fun_prop)).congr ?_
      intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      rw [h.fderiv]
      refine intervalIntegral.integral_congr fun t ht â†¦ ?_
      rw [show (fun x â†¦ f (x, t)) = (f âˆ˜ fun x â†¦ (x, t)) by rfl]
      rw [â† fderivWithin_eq_fderiv (hu.uniqueDiffWithinAt hx) (((hf.differentiableOn (by simp)).comp
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©)).differentiableAt (hu.mem_nhds hx))]
      rw [fderivWithin_comp _ (t := u Ã—Ë¢ [[a, b]]) (hf.differentiableOn (by simp) _ âŸ¨hx, htâŸ©)
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©) (hu.uniqueDiffWithinAt hx)]
      congr
      exact (hasFDerivAt_prodMk_left x t).hasFDerivWithinAt.fderivWithin (hu.uniqueDiffWithinAt hx)
  Â· intro h E _ _ f hf
    exact contDiffOn_infty.2 fun n â†¦ h n E <| hf.of_le <| WithTop.coe_le_coe.2 le_top

lemma _root_.ContDiff.parametric_intervalIntegral {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]
    [NoAtoms Î¼] {E H : Type u} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {a b : â„} {n : â„•âˆ}
    (hf : ContDiff â„ n f) :
    ContDiff â„ n (fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼) :=
  contDiffOn_univ.1 <| ContDiffOn.parametric_intervalIntegral isOpen_univ hf.contDiffOn

end intervalIntegral
