/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Operations
public import Mathlib.Analysis.Calculus.ParametricIntegral

/-!
# Derivatives of interval integrals depending on parameters

In this file we restate theorems about derivatives of integrals depending on parameters for interval
integrals. In the real case, we also show that parametric integrals of Câ¿ functions are Câ¿. -/

universe u

public section


open TopologicalSpace MeasureTheory Filter Metric Set

open scoped Topology Filter Interval

variable {ğ•œ : Type*} [RCLike ğ•œ] {Î¼ : Measure â„} {E : Type*} [NormedAddCommGroup E]
  [NormedSpace â„ E] [NormedSpace ğ•œ E] {H : Type*} [NormedAddCommGroup H]
  [NormedSpace ğ•œ H] {s : Set H} {a b : â„} {bound : â„ â†’ â„}

namespace intervalIntegral

/-- Differentiation under integral of `x â†¦ âˆ« t in a..b, F x t` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a neighborhood of `xâ‚€` for ae `a`
(with a neighborhood independent of `a`) with integrable Lipschitz bound, and `F x` is ae-measurable
for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_loc_of_lip
    {F : H â†’ â„ â†’ E} {F' : â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H}
    (hs : s âˆˆ ğ“ xâ‚€) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lip : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) s)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasFDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lip h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasFDerivAt_integral_of_dominated_loc_of_lip hs hF_meas hF_int hF'_meas h_lip
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a neighborhood of `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the neighborhood independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_of_fderiv_le
    {F : H â†’ â„ â†’ E} {F' : H â†’ â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ s, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ s, HasFDerivAt (fun x => F x t) (F' x t) x) :
    HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable
  simp only [intervalIntegral_eq_integral_uIoc]
  exact (hasFDerivAt_integral_of_dominated_of_fderiv_le hs hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff).const_smul _

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a neighborhood of `xâ‚€` for ae `a`
(with a neighborhood independent of `a`) with integrable Lipschitz bound, and `F x` is
ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_lip {F : ğ•œ â†’ â„ â†’ E} {F' : â„ â†’ E} {xâ‚€ : ğ•œ}
    {s : Set ğ•œ} (hs : s âˆˆ ğ“ xâ‚€)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lipsch : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) s)
    (bound_integrable : IntervalIntegrable (bound : â„ â†’ â„) Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lipsch h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_lip hs hF_meas hF_int hF'_meas h_lipsch
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a neighborhood of `xâ‚€` for ae `a`
(with a neighborhood independent of `a`) with derivative uniformly bounded by an integrable
function, and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_deriv_le
    {F : ğ•œ â†’ â„ â†’ E} {F' : ğ•œ â†’ â„ â†’ E} {xâ‚€ : ğ•œ} {s : Set ğ•œ}
    (hs : s âˆˆ ğ“ xâ‚€) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ s, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ s, HasDerivAt (fun x => F x t) (F' x t) x) :
    IntervalIntegrable (F' xâ‚€) Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_deriv_le hs hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if there exists a neighbourhood `u` of `xâ‚€` such that `f.uncurry : H Ã— â„ â†’ E` is continuous on
`u Ã—Ë¢ [[a, b]]` and differentiable on `u` in the first argument for every `t âˆˆ [[a, b]]` and that
derivative is continuous on `u Ã—Ë¢ [[a, b]]`, then a derivative of
`fun x => âˆ« t in a..b, f x t âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv ğ•œ (fun x â†¦ f x t) xâ‚€ âˆ‚Î¼`. -/
theorem hasFDerivAt_integral_of_continuousOn_fderiv [IsLocallyFiniteMeasure Î¼] [NoAtoms Î¼]
    {f : H â†’ â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hFâ‚ : ContinuousOn f.uncurry (u Ã—Ë¢ [[a, b]]))
    (hFâ‚‚ : âˆ€ t âˆˆ [[a, b]], DifferentiableOn ğ•œ (fun x â†¦ f x t) u)
    (hFâ‚ƒ : ContinuousOn (fun x â†¦ fderiv ğ•œ (fun y â†¦ f y x.2) x.1) (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f x t âˆ‚Î¼)
      (âˆ« t in a..b, fderiv ğ•œ (fun x â†¦ f x t) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a â‰¤ b with h
  Â· simp_rw [integral_symm b a]
    exact (h hu (uIcc_comm a b â–¸ hFâ‚) (uIcc_comm a b â–¸ hFâ‚‚) (uIcc_comm a b â–¸ hFâ‚ƒ)
      (le_of_not_ge hab)).neg
  simp_rw [integral_of_le hab, â† integral_Icc_eq_integral_Ioc, â† uIcc_of_le hab]
  exact _root_.hasFDerivAt_integral_of_continuousOn_fderiv
    hu isCompact_uIcc isCompact_uIcc.measure_lt_top hFâ‚ hFâ‚‚ hFâ‚ƒ

/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if `f.uncurry : H Ã— â„ â†’ E` is continuously differentiable on `u Ã—Ë¢ [[a, b]]` for a neighbourhood `u`
of `xâ‚€`, then a derivative of `fun x => âˆ« t in a..b, f x t âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv â„ (fun x â†¦ f x t) xâ‚€ âˆ‚Î¼`. -/
theorem hasFDerivAt_integral_of_contDiffOn [IsLocallyFiniteMeasure Î¼] [NoAtoms Î¼] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E] {H : Type*} [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H â†’ â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hF : ContDiffOn â„ 1 f.uncurry (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f x t âˆ‚Î¼)
      (âˆ« t in a..b, fderiv â„ (fun x â†¦ f x t) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simpa only [integral_symm b a] using (h hu (uIcc_comm a b â–¸ hF) hab).neg
    Â· simp [hab, hasFDerivAt_const]
  simp_rw [integral_of_le hab.le, â† integral_Icc_eq_integral_Ioc, â† uIcc_of_le hab.le]
  exact _root_.hasFDerivAt_integral_of_contDiffOn hu isCompact_uIcc isCompact_uIcc.measure_lt_top
    (uIcc_of_le hab.le â–¸ uniqueDiffOn_Icc hab) hF

end intervalIntegral

/-- If `f.uncurry : H Ã— â„ â†’ E` is Câ¿ on `u Ã—Ë¢ [[a, b]]`, the parametric integral
`fun x â†¦ âˆ« t in a..b, f x t âˆ‚Î¼` is Câ¿ on `u` too. -/
lemma ContDiffOn.parametric_intervalIntegral {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]
    [NoAtoms Î¼] {E H : Type u} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H â†’ â„ â†’ E} {u : Set H} (hu : IsOpen u) {a b : â„} {n : â„•âˆ}
    (hf : ContDiffOn â„ n f.uncurry (u Ã—Ë¢ [[a, b]])) :
    ContDiffOn â„ n (fun x â†¦ âˆ« t in a..b, f x t âˆ‚Î¼) u := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h hu (uIcc_comm a b â–¸ hf) hab).neg
    Â· simp [hab, contDiffOn_const]
  simp_rw [intervalIntegral.integral_of_le hab.le, â† integral_Icc_eq_integral_Ioc,
    â† uIcc_of_le hab.le]
  apply hf.parametric_integral hu isCompact_uIcc le_rfl

/-- If `f : H Ã— â„ â†’ E` is Câ¿, the parametric integral
`fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼` is Câ¿ too. -/
lemma ContDiff.parametric_intervalIntegral {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]
    [NoAtoms Î¼] {E H : Type u} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {a b : â„} {n : â„•âˆ}
    (hf : ContDiff â„ n f) : ContDiff â„ n (fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼) :=
  contDiffOn_univ.1 <| hf.contDiffOn.parametric_intervalIntegral isOpen_univ
