/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Basic

/-!
# Derivatives of interval integrals depending on parameters

In this file we restate theorems about derivatives of integrals depending on parameters for interval
integrals. -/

universe u

open TopologicalSpace MeasureTheory Filter Metric

open scoped Topology Filter Interval

variable {ğ•œ : Type*} [RCLike ğ•œ] {Î¼ : Measure â„} {E : Type*} [NormedAddCommGroup E]
  [NormedSpace â„ E] [NormedSpace ğ•œ E] {H : Type*} [NormedAddCommGroup H]
  [NormedSpace ğ•œ H] {a b Îµ : â„} {bound : â„ â†’ â„}

namespace intervalIntegral

/-- Differentiation under integral of `x â†¦ âˆ« t in a..b, F x t` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with a ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is ae-measurable
for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_loc_of_lip
    {F : H â†’ â„ â†’ E} {F' : â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lip : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) (ball xâ‚€ Îµ))
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasFDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lip h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasFDerivAt_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas h_lip
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Differentiation under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€`, assuming
`F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on a ball around `xâ‚€` for ae `a` with
derivative norm uniformly bounded by an integrable function (the ball radius is independent of `a`),
and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasFDerivAt_integral_of_dominated_of_fderiv_le
    {F : H â†’ â„ â†’ E} {F' : H â†’ â„ â†’ H â†’L[ğ•œ] E} {xâ‚€ : H} (Îµ_pos : 0 < Îµ)
    (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, HasFDerivAt (fun x => F x t) (F' x t) x) :
    HasFDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable
  simp only [intervalIntegral_eq_integral_uIoc]
  exact (hasFDerivAt_integral_of_dominated_of_fderiv_le Îµ_pos hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff).const_smul _

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is locally Lipschitz on a ball around `xâ‚€` for ae `a`
(with ball radius independent of `a`) with integrable Lipschitz bound, and `F x` is
ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_lip {F : ğ•œ â†’ â„ â†’ E} {F' : â„ â†’ E} {xâ‚€ : ğ•œ}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable F' (Î¼.restrict (Î™ a b)))
    (h_lipsch : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’
      LipschitzOnWith (Real.nnabs <| bound t) (fun x => F x t) (ball xâ‚€ Îµ))
    (bound_integrable : IntervalIntegrable (bound : â„ â†’ â„) Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ HasDerivAt (fun x => F x t) (F' t) xâ‚€) :
    IntervalIntegrable F' Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_lipsch h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas h_lipsch
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

/-- Derivative under integral of `x â†¦ âˆ« F x a` at a given point `xâ‚€ : ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
assuming `F xâ‚€` is integrable, `x â†¦ F x a` is differentiable on an interval around `xâ‚€` for ae `a`
(with interval radius independent of `a`) with derivative uniformly bounded by an integrable
function, and `F x` is ae-measurable for `x` in a possibly smaller neighborhood of `xâ‚€`. -/
nonrec theorem hasDerivAt_integral_of_dominated_loc_of_deriv_le
    {F : ğ•œ â†’ â„ â†’ E} {F' : ğ•œ â†’ â„ â†’ E} {xâ‚€ : ğ•œ}
    (Îµ_pos : 0 < Îµ) (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, AEStronglyMeasurable (F x) (Î¼.restrict (Î™ a b)))
    (hF_int : IntervalIntegrable (F xâ‚€) Î¼ a b)
    (hF'_meas : AEStronglyMeasurable (F' xâ‚€) (Î¼.restrict (Î™ a b)))
    (h_bound : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, â€–F' x tâ€– â‰¤ bound t)
    (bound_integrable : IntervalIntegrable bound Î¼ a b)
    (h_diff : âˆ€áµ t âˆ‚Î¼, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, HasDerivAt (fun x => F x t) (F' x t) x) :
    IntervalIntegrable (F' xâ‚€) Î¼ a b âˆ§
      HasDerivAt (fun x => âˆ« t in a..b, F x t âˆ‚Î¼) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î¼) xâ‚€ := by
  rw [â† ae_restrict_iff' measurableSet_uIoc] at h_bound h_diff
  simp only [intervalIntegrable_iff] at hF_int bound_integrable âŠ¢
  simp only [intervalIntegral_eq_integral_uIoc]
  have := hasDerivAt_integral_of_dominated_loc_of_deriv_le Îµ_pos hF_meas hF_int hF'_meas h_bound
    bound_integrable h_diff
  exact âŸ¨this.1, this.2.const_smul _âŸ©

open Set in
/-- A convenient special case of `intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le`:
if `f : H Ã— â„ â†’ E` is continuously differentiable on `u Ã—Ë¢ [[a, b]]` for a neighbourhood `u`
of `xâ‚€`, then a derivative of `fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼` in `xâ‚€` can be computed as
`âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼`. -/
nonrec theorem hasFDerivAt_integral_of_contDiffOn
    {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼] {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] {H : Type*} [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {xâ‚€ : H} {u : Set H} (hu : u âˆˆ ğ“ xâ‚€) {a b : â„}
    (hF : ContDiffOn â„ 1 f (u Ã—Ë¢ [[a, b]])) :
    HasFDerivAt (fun x => âˆ« t in a..b, f (x, t) âˆ‚Î¼)
      (âˆ« t in a..b, fderiv â„ (fun x â†¦ f (x, t)) xâ‚€ âˆ‚Î¼) xâ‚€ := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h (Î¼ := Î¼) hu (Set.uIcc_comm a b â–¸ hF) hab).neg
    Â· simp [hab, hasFDerivAt_const]
  rw [uIcc_of_le hab.le] at hF
  replace âŸ¨u, hu, hxu, hFâŸ© : âˆƒ u, IsOpen u âˆ§ xâ‚€ âˆˆ u âˆ§ ContDiffOn â„ 1 f (u Ã—Ë¢ Icc a b) := by
    have âŸ¨u', hu'âŸ© := _root_.mem_nhds_iff.1 hu
    exact âŸ¨u', hu'.2.1, hu'.2.2, hF.mono <| prod_mono_left hu'.1âŸ©
  let F' := fun x : H Ã— â„ â†¦ fderiv â„ (fun y â†¦ f (y, x.2)) x.1
  have hF' : ContinuousOn F' (u Ã—Ë¢ Icc a b) := by
    refine .congr (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ Set.Icc a b) x).comp (.inl â„ H â„))
      ?_ fun x hx â†¦ ?_
    Â· refine ((ContinuousLinearMap.compL â„ H (H Ã— â„) E).flip
        (.inl â„ H â„)).continuous.comp_continuousOn ?_
      refine (hF.continuousOn_fderivWithin ?_ le_rfl)
      exact hu.uniqueDiffOn.prod <| uniqueDiffOn_Icc hab
    Â· dsimp [F']; rw [show (fun y â†¦ f (y, x.2)) = (f âˆ˜ fun y â†¦ (y, x.2)) by rfl]
      rw [â† fderivWithin_eq_fderiv (s := u) (hu.uniqueDiffWithinAt hx.1) <| by
        refine DifferentiableOn.differentiableAt (s := u) ?_ (hu.mem_nhds hx.1)
        exact ((hF.differentiableOn le_rfl).comp (by fun_prop) (fun y hy â†¦ âŸ¨hy, hx.2âŸ©))]
      rw [fderivWithin_comp _ (t := u Ã—Ë¢ Set.Icc a b) (hF.differentiableOn (by simp) _ âŸ¨hx.1, hx.2âŸ©)
        (by fun_prop) (by exact fun y hy â†¦ âŸ¨hy, hx.2âŸ©) (hu.uniqueDiffWithinAt hx.1)]
      congr
      exact (hasFDerivAt_prodMk_left _ x.2).hasFDerivWithinAt.fderivWithin
        (hu.uniqueDiffWithinAt hx.1)
  let F'' := fun x â†¦ â€–F' xâ€–
  have hF'' : ContinuousOn F'' _ := continuous_norm.comp_continuousOn hF'
  let âŸ¨Îµ, hÎµ, hÎµ', B, hBâŸ© :
      âˆƒ Îµ > 0, Metric.ball xâ‚€ Îµ âŠ† u âˆ§ âˆƒ B, âˆ€ x âˆˆ Metric.ball xâ‚€ Îµ Ã—Ë¢ Icc a b, F'' x < B := by
    let âŸ¨B, hBâŸ© := (isCompact_singleton.prod isCompact_Icc).bddAbove_image <|
      hF''.mono <| prod_mono_left <| singleton_subset_iff.2 hxu
    have âŸ¨v, hv, hv'âŸ© := generalized_tube_lemma_left (s := {xâ‚€}) isCompact_singleton
      (t := Icc a b) isCompact_Icc (s' := u) (n := F'' â»Â¹' (Iio (B + 1))) (by
        refine nhdsSetWithin_mono_left ?_ <| hF''.preimage_mem_nhdsSetWithin_of_mem_nhdsSet
          (t := Iic B) (u := Iio (B + 1)) <| isOpen_Iio.mem_nhdsSet.2 (by simp)
        intro x hx
        exact âŸ¨prod_mono_left (by simp [hxu]) hx, mem_upperBounds.1 hB _ <| mem_image_of_mem _ hxâŸ©)
    rw [nhdsSetWithin_singleton, hu.nhdsWithin_eq hxu] at hv
    have âŸ¨Îµ, hÎµ, hÎµ'âŸ© := Metric.mem_nhds_iff.1 (Filter.inter_mem hv (hu.mem_nhds hxu))
    exact âŸ¨Îµ, hÎµ, hÎµ'.trans inter_subset_right, B + 1,
      fun x hx â†¦ hv' <| prod_mono_left (hÎµ'.trans inter_subset_left) hxâŸ©
  refine intervalIntegral.hasFDerivAt_integral_of_dominated_of_fderiv_le (bound := fun _ â†¦ B)
    (F' := fun x t â†¦ fderiv â„ (fun x â†¦ f (x, t)) x) hÎµ ?_ ?_ ?_ ?_ ?_ ?_
  Â· refine eventually_nhds_iff.2 âŸ¨u, fun x hx â†¦ ?_, hu, hxuâŸ©
    refine ContinuousOn.aestronglyMeasurable ?_ measurableSet_uIoc
    refine .mono ?_ <| (uIoc_of_le hab.le).trans_le Ioc_subset_Icc_self
    exact hF.continuousOn.comp (by fun_prop) fun t ht â†¦ âŸ¨hx, htâŸ©
  Â· apply ContinuousOn.intervalIntegrable
    exact hF.continuousOn.comp (by fun_prop) fun t ht â†¦ âŸ¨hxu, uIcc_of_le hab.le â–¸ htâŸ©
  Â· refine ContinuousOn.aestronglyMeasurable ?_ measurableSet_uIoc
    refine .mono ?_ <| (uIoc_of_le hab.le).trans_le Ioc_subset_Icc_self
    exact hF'.comp (f := fun t â†¦ (xâ‚€, t)) (by fun_prop) fun t ht â†¦ âŸ¨hxu, htâŸ©
  Â· refine .of_forall fun t ht x hx â†¦ ?_
    exact (hB (x, t) âŸ¨hx, Ioc_subset_Icc_self <| uIoc_of_le hab.le â–¸ htâŸ©).le
  Â· exact intervalIntegrable_const
  Â· refine .of_forall fun t ht x hx â†¦ ?_
    refine (DifferentiableOn.differentiableAt ?_ (hu.mem_nhds <| hÎµ' hx)).hasFDerivAt
    exact hF.differentiableOn_one.comp (by fun_prop) fun x hx â†¦
      âŸ¨hx, Ioc_subset_Icc_self <| uIoc_of_le hab.le â–¸ htâŸ©

lemma _root_.ContDiffOn.parametric_intervalIntegral {Î¼ : Measure â„} [IsLocallyFiniteMeasure Î¼]
    [NoAtoms Î¼] {E H : Type u} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup H]
    [NormedSpace â„ H] {f : H Ã— â„ â†’ E} {u : Set H} (hu : IsOpen u) {a b : â„} {n : â„•âˆ}
    (hf : ContDiffOn â„ n f (u Ã—Ë¢ [[a, b]])) :
    ContDiffOn â„ n (fun x â†¦ âˆ« t in a..b, f (x, t) âˆ‚Î¼) u := by
  wlog hab : a < b with h
  Â· obtain hab | hab := lt_or_eq_of_le <| le_of_not_gt hab
    Â· simp_rw [intervalIntegral.integral_symm b a]
      exact (h hu (Set.uIcc_comm a b â–¸ hf) hab).neg
    Â· simp [hab, contDiffOn_const]
  revert E; change âˆ€ E : _, _
  refine ENat.nat_induction n ?_ ?_ ?_
  Â· intro E _ _ f
    simp_rw [WithTop.coe_zero, contDiffOn_zero]
    exact ContinuousOn.parametric_intervalIntegral
  Â· intro k h E _ _ f hf
    refine (contDiffOn_succ_iff_fderiv_of_isOpen (ğ•œ := â„) (n := k) hu).2 âŸ¨?_, by simp, ?_âŸ©
    Â· intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      exact h.differentiableAt.differentiableWithinAt
    Â· have := hf.fderivWithin (hu.uniqueDiffOn.prod <| Set.uIcc_of_le hab.le â–¸ uniqueDiffOn_Icc hab)
        (m := k) le_rfl
      refine (h _ (f := fun x â†¦ (fderivWithin â„ f (u Ã—Ë¢ [[a, b]]) x).comp (.inl â„ H â„))
        (by fun_prop)).congr ?_
      intro x hx
      have h := intervalIntegral.hasFDerivAt_integral_of_contDiffOn (Î¼ := Î¼)
        (hu.mem_nhds hx) (hf.of_le <| by simp)
      rw [h.fderiv]
      refine intervalIntegral.integral_congr fun t ht â†¦ ?_
      rw [show (fun x â†¦ f (x, t)) = (f âˆ˜ fun x â†¦ (x, t)) by rfl]
      rw [â† fderivWithin_eq_fderiv (hu.uniqueDiffWithinAt hx) (((hf.differentiableOn (by simp)).comp
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©)).differentiableAt (hu.mem_nhds hx))]
      rw [fderivWithin_comp _ (t := u Ã—Ë¢ [[a, b]]) (hf.differentiableOn (by simp) _ âŸ¨hx, htâŸ©)
        (by fun_prop) (fun x hx â†¦ âŸ¨hx, htâŸ©) (hu.uniqueDiffWithinAt hx)]
      congr
      exact (hasFDerivAt_prodMk_left x t).hasFDerivWithinAt.fderivWithin (hu.uniqueDiffWithinAt hx)
  Â· intro h E _ _ f hf
    exact contDiffOn_infty.2 fun n â†¦ h n E <| hf.of_le <| WithTop.coe_le_coe.2 le_top

end intervalIntegral
