/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.ContDiff.Defs

/-!
# One-dimensional iterated derivatives

We define the `n`-th derivative of a function `f : ğ•œ â†’ F` as a function
`iteratedDeriv n f : ğ•œ â†’ F`, as well as a version on domains `iteratedDerivWithin n f s : ğ•œ â†’ F`,
and prove their basic properties.

## Main definitions and results

Let `ğ•œ` be a nontrivially normed field, and `F` a normed vector space over `ğ•œ`. Let `f : ğ•œ â†’ F`.

* `iteratedDeriv n f` is the `n`-th derivative of `f`, seen as a function from `ğ•œ` to `F`.
  It is defined as the `n`-th FrÃ©chet derivative (which is a multilinear map) applied to the
  vector `(1, ..., 1)`, to take advantage of all the existing framework, but we show that it
  coincides with the naive iterative definition.
* `iteratedDeriv_eq_iterate` states that the `n`-th derivative of `f` is obtained by starting
  from `f` and differentiating it `n` times.
* `iteratedDerivWithin n f s` is the `n`-th derivative of `f` within the domain `s`. It only
  behaves well when `s` has the unique derivative property.
* `iteratedDerivWithin_eq_iterate` states that the `n`-th derivative of `f` in the domain `s` is
  obtained by starting from `f` and differentiating it `n` times within `s`. This only holds when
  `s` has the unique derivative property.

## Implementation details

The results are deduced from the corresponding results for the more general (multilinear) iterated
FrÃ©chet derivative. For this, we write `iteratedDeriv n f` as the composition of
`iteratedFDeriv ğ•œ n f` and a continuous linear equiv. As continuous linear equivs respect
differentiability and commute with differentiation, this makes it possible to prove readily that
the derivative of the `n`-th derivative is the `n+1`-th derivative in `iteratedDerivWithin_succ`,
by translating the corresponding result `iteratedFDerivWithin_succ_apply_left` for the
iterated FrÃ©chet derivative.
-/

noncomputable section

open scoped Topology
open Filter Asymptotics Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- The `n`-th iterated derivative of a function from `ğ•œ` to `F`, as a function from `ğ•œ` to `F`. -/
def iteratedDeriv (n : â„•) (f : ğ•œ â†’ F) (x : ğ•œ) : F :=
  (iteratedFDeriv ğ•œ n f x : (Fin n â†’ ğ•œ) â†’ F) fun _ : Fin n => 1

/-- The `n`-th iterated derivative of a function from `ğ•œ` to `F` within a set `s`, as a function
from `ğ•œ` to `F`. -/
def iteratedDerivWithin (n : â„•) (f : ğ•œ â†’ F) (s : Set ğ•œ) (x : ğ•œ) : F :=
  (iteratedFDerivWithin ğ•œ n f s x : (Fin n â†’ ğ•œ) â†’ F) fun _ : Fin n => 1

variable {n : â„•} {f : ğ•œ â†’ F} {s : Set ğ•œ} {x : ğ•œ}

theorem iteratedDerivWithin_univ : iteratedDerivWithin n f univ = iteratedDeriv n f := by
  ext x
  rw [iteratedDerivWithin, iteratedDeriv, iteratedFDerivWithin_univ]

theorem iteratedDerivWithin_eq_iteratedDeriv (hs : UniqueDiffOn ğ•œ s) (h : ContDiffAt ğ•œ n f x)
    (hx : x âˆˆ s) : iteratedDerivWithin n f s x = iteratedDeriv n f x := by
  rw [iteratedDerivWithin, iteratedDeriv, iteratedFDerivWithin_eq_iteratedFDeriv hs h hx]

/-! ### Properties of the iterated derivative within a set -/


theorem iteratedDerivWithin_eq_iteratedFDerivWithin : iteratedDerivWithin n f s x =
    (iteratedFDerivWithin ğ•œ n f s x : (Fin n â†’ ğ•œ) â†’ F) fun _ : Fin n => 1 :=
  rfl

/-- Write the iterated derivative as the composition of a continuous linear equiv and the iterated
FrÃ©chet derivative -/
theorem iteratedDerivWithin_eq_equiv_comp : iteratedDerivWithin n f s =
    (ContinuousMultilinearMap.piFieldEquiv ğ•œ (Fin n) F).symm âˆ˜ iteratedFDerivWithin ğ•œ n f s := by
  ext x; rfl

/-- Write the iterated FrÃ©chet derivative as the composition of a continuous linear equiv and the
iterated derivative. -/
theorem iteratedFDerivWithin_eq_equiv_comp :
    iteratedFDerivWithin ğ•œ n f s =
      ContinuousMultilinearMap.piFieldEquiv ğ•œ (Fin n) F âˆ˜ iteratedDerivWithin n f s := by
  rw [iteratedDerivWithin_eq_equiv_comp, â† Function.comp_assoc, LinearIsometryEquiv.self_comp_symm,
    Function.id_comp]

/-- The `n`-th FrÃ©chet derivative applied to a vector `(m 0, ..., m (n-1))` is the derivative
multiplied by the product of the `m i`s. -/
theorem iteratedFDerivWithin_apply_eq_iteratedDerivWithin_mul_prod {m : Fin n â†’ ğ•œ} :
    (iteratedFDerivWithin ğ•œ n f s x : (Fin n â†’ ğ•œ) â†’ F) m =
      (âˆ i, m i) â€¢ iteratedDerivWithin n f s x := by
  rw [iteratedDerivWithin_eq_iteratedFDerivWithin, â† ContinuousMultilinearMap.map_smul_univ]
  simp

theorem norm_iteratedFDerivWithin_eq_norm_iteratedDerivWithin :
    â€–iteratedFDerivWithin ğ•œ n f s xâ€– = â€–iteratedDerivWithin n f s xâ€– := by
  rw [iteratedDerivWithin_eq_equiv_comp, Function.comp_apply, LinearIsometryEquiv.norm_map]

@[simp]
theorem iteratedDerivWithin_zero : iteratedDerivWithin 0 f s = f := by
  ext x
  simp [iteratedDerivWithin]

@[simp]
theorem iteratedDerivWithin_one {x : ğ•œ} :
    iteratedDerivWithin 1 f s x = derivWithin f s x := by
  by_cases hsx : AccPt x (ğ“Ÿ s)
  Â· simp only [iteratedDerivWithin, iteratedFDerivWithin_one_apply hsx.uniqueDiffWithinAt,
      derivWithin]
  Â· simp [derivWithin_zero_of_not_accPt hsx, iteratedDerivWithin, iteratedFDerivWithin,
      fderivWithin_zero_of_not_accPt hsx]

/-- If the first `n` derivatives within a set of a function are continuous, and its first `n-1`
derivatives are differentiable, then the function is `C^n`. This is not an equivalence in general,
but this is an equivalence when the set has unique derivatives, see
`contDiffOn_iff_continuousOn_differentiableOn_deriv`. -/
theorem contDiffOn_of_continuousOn_differentiableOn_deriv {n : â„•âˆ}
    (Hcont : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (fun x => iteratedDerivWithin m f s x) s)
    (Hdiff : âˆ€ m : â„•, (m : â„•âˆ) < n â†’ DifferentiableOn ğ•œ (fun x => iteratedDerivWithin m f s x) s) :
    ContDiffOn ğ•œ n f s := by
  apply contDiffOn_of_continuousOn_differentiableOn
  Â· simpa only [iteratedFDerivWithin_eq_equiv_comp, LinearIsometryEquiv.comp_continuousOn_iff]
  Â· simpa only [iteratedFDerivWithin_eq_equiv_comp, LinearIsometryEquiv.comp_differentiableOn_iff]

/-- To check that a function is `n` times continuously differentiable, it suffices to check that its
first `n` derivatives are differentiable. This is slightly too strong as the condition we
require on the `n`-th derivative is differentiability instead of continuity, but it has the
advantage of avoiding the discussion of continuity in the proof (and for `n = âˆ` this is optimal).
-/
theorem contDiffOn_of_differentiableOn_deriv {n : â„•âˆ}
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ DifferentiableOn ğ•œ (iteratedDerivWithin m f s) s) :
    ContDiffOn ğ•œ n f s := by
  apply contDiffOn_of_differentiableOn
  simpa only [iteratedFDerivWithin_eq_equiv_comp, LinearIsometryEquiv.comp_differentiableOn_iff]

/-- On a set with unique derivatives, a `C^n` function has derivatives up to `n` which are
continuous. -/
theorem ContDiffOn.continuousOn_iteratedDerivWithin
    {n : WithTop â„•âˆ} {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : m â‰¤ n) (hs : UniqueDiffOn ğ•œ s) : ContinuousOn (iteratedDerivWithin m f s) s := by
  simpa only [iteratedDerivWithin_eq_equiv_comp, LinearIsometryEquiv.comp_continuousOn_iff] using
    h.continuousOn_iteratedFDerivWithin hmn hs

theorem ContDiffWithinAt.differentiableWithinAt_iteratedDerivWithin {n : WithTop â„•âˆ} {m : â„•}
    (h : ContDiffWithinAt ğ•œ n f s x) (hmn : m < n) (hs : UniqueDiffOn ğ•œ (insert x s)) :
    DifferentiableWithinAt ğ•œ (iteratedDerivWithin m f s) s x := by
  simpa only [iteratedDerivWithin_eq_equiv_comp,
    LinearIsometryEquiv.comp_differentiableWithinAt_iff] using
    h.differentiableWithinAt_iteratedFDerivWithin hmn hs

/-- On a set with unique derivatives, a `C^n` function has derivatives less than `n` which are
differentiable. -/
theorem ContDiffOn.differentiableOn_iteratedDerivWithin {n : WithTop â„•âˆ} {m : â„•}
    (h : ContDiffOn ğ•œ n f s) (hmn : m < n) (hs : UniqueDiffOn ğ•œ s) :
    DifferentiableOn ğ•œ (iteratedDerivWithin m f s) s := fun x hx =>
  (h x hx).differentiableWithinAt_iteratedDerivWithin hmn <| by rwa [insert_eq_of_mem hx]

/-- The property of being `C^n`, initially defined in terms of the FrÃ©chet derivative, can be
reformulated in terms of the one-dimensional derivative on sets with unique derivatives. -/
theorem contDiffOn_iff_continuousOn_differentiableOn_deriv {n : â„•âˆ} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†” (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (iteratedDerivWithin m f s) s) âˆ§
      âˆ€ m : â„•, (m : â„•âˆ) < n â†’ DifferentiableOn ğ•œ (iteratedDerivWithin m f s) s := by
  simp only [contDiffOn_iff_continuousOn_differentiableOn hs, iteratedFDerivWithin_eq_equiv_comp,
    LinearIsometryEquiv.comp_continuousOn_iff, LinearIsometryEquiv.comp_differentiableOn_iff]

/-- The property of being `C^n`, initially defined in terms of the FrÃ©chet derivative, can be
reformulated in terms of the one-dimensional derivative on sets with unique derivatives. -/
theorem contDiffOn_nat_iff_continuousOn_differentiableOn_deriv {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†” (âˆ€ m : â„•, m â‰¤ n â†’ ContinuousOn (iteratedDerivWithin m f s) s) âˆ§
      âˆ€ m : â„•, m < n â†’ DifferentiableOn ğ•œ (iteratedDerivWithin m f s) s := by
  rw [show n = ((n : â„•âˆ) : WithTop â„•âˆ) from rfl,
    contDiffOn_iff_continuousOn_differentiableOn_deriv hs]
  simp

/-- The `n+1`-th iterated derivative within a set with unique derivatives can be obtained by
differentiating the `n`-th iterated derivative. -/
theorem iteratedDerivWithin_succ {x : ğ•œ} :
    iteratedDerivWithin (n + 1) f s x = derivWithin (iteratedDerivWithin n f s) s x := by
  by_cases hxs : AccPt x (ğ“Ÿ s)
  Â· rw [iteratedDerivWithin_eq_iteratedFDerivWithin, iteratedFDerivWithin_succ_apply_left,
      iteratedFDerivWithin_eq_equiv_comp,
      LinearIsometryEquiv.comp_fderivWithin _ hxs.uniqueDiffWithinAt, derivWithin]
    change ((ContinuousMultilinearMap.mkPiRing ğ•œ (Fin n) ((fderivWithin ğ•œ
      (iteratedDerivWithin n f s) s x : ğ•œ â†’ F) 1) : (Fin n â†’ ğ•œ) â†’ F) fun _ : Fin n => 1) =
      (fderivWithin ğ•œ (iteratedDerivWithin n f s) s x : ğ•œ â†’ F) 1
    simp
  Â· simp [derivWithin_zero_of_not_accPt hxs, iteratedDerivWithin, iteratedFDerivWithin,
      fderivWithin_zero_of_not_accPt hxs]

/-- The `n`-th iterated derivative within a set with unique derivatives can be obtained by
iterating `n` times the differentiation operation. -/
theorem iteratedDerivWithin_eq_iterate {x : ğ•œ} :
    iteratedDerivWithin n f s x = (fun g : ğ•œ â†’ F => derivWithin g s)^[n] f x := by
  induction n generalizing x with
  | zero => simp
  | succ n IH =>
    rw [iteratedDerivWithin_succ, Function.iterate_succ']
    exact derivWithin_congr (fun y hy => IH) IH

/-- The `n+1`-th iterated derivative within a set with unique derivatives can be obtained by
taking the `n`-th derivative of the derivative. -/
theorem iteratedDerivWithin_succ' {x : ğ•œ} :
    iteratedDerivWithin (n + 1) f s x = (iteratedDerivWithin n (derivWithin f s) s) x := by
  rw [iteratedDerivWithin_eq_iterate, iteratedDerivWithin_eq_iterate]; rfl

/-! ### Properties of the iterated derivative on the whole space -/


theorem iteratedDeriv_eq_iteratedFDeriv :
    iteratedDeriv n f x = (iteratedFDeriv ğ•œ n f x : (Fin n â†’ ğ•œ) â†’ F) fun _ : Fin n => 1 :=
  rfl

/-- Write the iterated derivative as the composition of a continuous linear equiv and the iterated
FrÃ©chet derivative -/
theorem iteratedDeriv_eq_equiv_comp : iteratedDeriv n f =
    (ContinuousMultilinearMap.piFieldEquiv ğ•œ (Fin n) F).symm âˆ˜ iteratedFDeriv ğ•œ n f := by
  ext x; rfl

/-- Write the iterated FrÃ©chet derivative as the composition of a continuous linear equiv and the
iterated derivative. -/
theorem iteratedFDeriv_eq_equiv_comp : iteratedFDeriv ğ•œ n f =
    ContinuousMultilinearMap.piFieldEquiv ğ•œ (Fin n) F âˆ˜ iteratedDeriv n f := by
  rw [iteratedDeriv_eq_equiv_comp, â† Function.comp_assoc, LinearIsometryEquiv.self_comp_symm,
    Function.id_comp]

/-- The `n`-th FrÃ©chet derivative applied to a vector `(m 0, ..., m (n-1))` is the derivative
multiplied by the product of the `m i`s. -/
theorem iteratedFDeriv_apply_eq_iteratedDeriv_mul_prod {m : Fin n â†’ ğ•œ} :
    (iteratedFDeriv ğ•œ n f x : (Fin n â†’ ğ•œ) â†’ F) m = (âˆ i, m i) â€¢ iteratedDeriv n f x := by
  rw [iteratedDeriv_eq_iteratedFDeriv, â† ContinuousMultilinearMap.map_smul_univ]; simp

theorem norm_iteratedFDeriv_eq_norm_iteratedDeriv :
    â€–iteratedFDeriv ğ•œ n f xâ€– = â€–iteratedDeriv n f xâ€– := by
  rw [iteratedDeriv_eq_equiv_comp, Function.comp_apply, LinearIsometryEquiv.norm_map]

@[simp]
theorem iteratedDeriv_zero : iteratedDeriv 0 f = f := by ext x; simp [iteratedDeriv]

@[simp]
theorem iteratedDeriv_one : iteratedDeriv 1 f = deriv f := by ext x; simp [iteratedDeriv]

/-- The property of being `C^n`, initially defined in terms of the FrÃ©chet derivative, can be
reformulated in terms of the one-dimensional derivative. -/
theorem contDiff_iff_iteratedDeriv {n : â„•âˆ} : ContDiff ğ•œ n f â†”
    (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Continuous (iteratedDeriv m f)) âˆ§
      âˆ€ m : â„•, (m : â„•âˆ) < n â†’ Differentiable ğ•œ (iteratedDeriv m f) := by
  simp only [contDiff_iff_continuous_differentiable, iteratedFDeriv_eq_equiv_comp,
    LinearIsometryEquiv.comp_continuous_iff, LinearIsometryEquiv.comp_differentiable_iff]

/-- The property of being `C^n`, initially defined in terms of the FrÃ©chet derivative, can be
reformulated in terms of the one-dimensional derivative. -/
theorem contDiff_nat_iff_iteratedDeriv {n : â„•} : ContDiff ğ•œ n f â†”
    (âˆ€ m : â„•, m â‰¤ n â†’ Continuous (iteratedDeriv m f)) âˆ§
      âˆ€ m : â„•, m < n â†’ Differentiable ğ•œ (iteratedDeriv m f) := by
  rw [â† WithTop.coe_natCast, contDiff_iff_iteratedDeriv]
  simp

/-- To check that a function is `n` times continuously differentiable, it suffices to check that its
first `n` derivatives are differentiable. This is slightly too strong as the condition we
require on the `n`-th derivative is differentiability instead of continuity, but it has the
advantage of avoiding the discussion of continuity in the proof (and for `n = âˆ` this is optimal).
-/
theorem contDiff_of_differentiable_iteratedDeriv {n : â„•âˆ}
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Differentiable ğ•œ (iteratedDeriv m f)) : ContDiff ğ•œ n f :=
  contDiff_iff_iteratedDeriv.2 âŸ¨fun m hm => (h m hm).continuous, fun m hm => h m (le_of_lt hm)âŸ©

theorem ContDiff.continuous_iteratedDeriv {n : WithTop â„•âˆ} (m : â„•) (h : ContDiff ğ•œ n f)
    (hmn : m â‰¤ n) : Continuous (iteratedDeriv m f) :=
  (contDiff_iff_iteratedDeriv.1 (h.of_le hmn)).1 m le_rfl

@[fun_prop]
theorem ContDiff.continuous_iteratedDeriv' (m : â„•) (h : ContDiff ğ•œ m f) :
    Continuous (iteratedDeriv m f) :=
  ContDiff.continuous_iteratedDeriv m h (le_refl _)

theorem ContDiff.differentiable_iteratedDeriv {n : WithTop â„•âˆ} (m : â„•) (h : ContDiff ğ•œ n f)
    (hmn : m < n) : Differentiable ğ•œ (iteratedDeriv m f) :=
  (contDiff_iff_iteratedDeriv.1 (h.of_le (ENat.add_one_natCast_le_withTop_of_lt hmn))).2 m
    (mod_cast (lt_add_one m))

@[fun_prop]
theorem ContDiff.differentiable_iteratedDeriv' (m : â„•) (h : ContDiff ğ•œ (m + 1) f) :
    Differentiable ğ•œ (iteratedDeriv m f) :=
  h.differentiable_iteratedDeriv m (Nat.cast_lt.mpr m.lt_succ_self)

/-- The `n+1`-th iterated derivative can be obtained by differentiating the `n`-th
iterated derivative. -/
theorem iteratedDeriv_succ : iteratedDeriv (n + 1) f = deriv (iteratedDeriv n f) := by
  ext x
  rw [â† iteratedDerivWithin_univ, â† iteratedDerivWithin_univ, â† derivWithin_univ]
  exact iteratedDerivWithin_succ

/-- The `n`-th iterated derivative can be obtained by iterating `n` times the
differentiation operation. -/
theorem iteratedDeriv_eq_iterate : iteratedDeriv n f = deriv^[n] f := by
  ext x
  rw [â† iteratedDerivWithin_univ]
  convert iteratedDerivWithin_eq_iterate (F := F)
  simp [derivWithin_univ]

theorem iteratedDerivWithin_of_isOpen (hs : IsOpen s) :
    Set.EqOn (iteratedDerivWithin n f s) (iteratedDeriv n f) s := by
  intro x hx
  simp_rw [iteratedDerivWithin, iteratedDeriv,iteratedFDerivWithin_of_isOpen n hs hx]

theorem iteratedDerivWithin_congr_right_of_isOpen (f : ğ•œ â†’ F) (n : â„•) {s t : Set ğ•œ} (hs : IsOpen s)
    (ht : IsOpen t) : (s âˆ© t).EqOn (iteratedDerivWithin n f s) (iteratedDerivWithin n f t) := by
  intro r hr
  rw [iteratedDerivWithin_of_isOpen hs hr.1, iteratedDerivWithin_of_isOpen ht hr.2]

theorem iteratedDerivWithin_of_isOpen_eq_iterate (hs : IsOpen s) :
    EqOn (iteratedDerivWithin n f s) (deriv^[n] f) s := by
  apply Set.EqOn.trans (iteratedDerivWithin_of_isOpen hs)
  rw [iteratedDeriv_eq_iterate]
  exact Set.eqOn_refl _ _

/-- The `n+1`-th iterated derivative can be obtained by taking the `n`-th derivative of the
derivative. -/
theorem iteratedDeriv_succ' : iteratedDeriv (n + 1) f = iteratedDeriv n (deriv f) := by
  rw [iteratedDeriv_eq_iterate, iteratedDeriv_eq_iterate, Function.iterate_succ_apply]

lemma AnalyticAt.hasFPowerSeriesAt {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
    [CharZero ğ•œ] {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (h : AnalyticAt ğ•œ f x) :
    HasFPowerSeriesAt f
      (FormalMultilinearSeries.ofScalars ğ•œ (fun n â†¦ iteratedDeriv n f x / n.factorial)) x := by
  obtain âŸ¨p, hpâŸ© := h
  convert hp
  obtain âŸ¨r, hprâŸ© := hp
  ext n
  have h_fact_smul := hpr.factorial_smul 1
  simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, Finset.prod_const, Finset.card_univ,
    Fintype.card_fin, smul_eq_mul, nsmul_eq_mul, one_pow, one_mul] at h_fact_smul
  simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff,
    FormalMultilinearSeries.coeff_ofScalars, smul_eq_mul, mul_eq_mul_left_iff]
  left
  rw [div_eq_iff, mul_comm, h_fact_smul, â† iteratedDeriv_eq_iteratedFDeriv]
  norm_cast
  exact Nat.factorial_ne_zero _
