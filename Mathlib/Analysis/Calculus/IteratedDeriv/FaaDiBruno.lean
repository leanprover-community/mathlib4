/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# Iterated derivatives of compositions

In this file we specialize FaÃ  di Bruno's formula to one-dimensional domain
to deduce formulae for `iteratedDerivWithin k (g âˆ˜ f) s x` for `k = 2` and `k = 3`.

We use
- `vcomp` for lemmas about the composition of `g : E â†’ F` with `f : ğ•œ â†’ E`;
- `scomp` for lemmas about the composition of `g : ğ•œ â†’ E` with `f : ğ•œ â†’ ğ•œ`;
- `comp` for lemmas about the composition of `g : ğ•œ â†’ ğ•œ` with `f : ğ•œ â†’ ğ•œ`.

## TODO

- What `UniqueDiffOn` assumptions can be discarded?
- In case of dimension 1 (and, more generally, in case of symmetric iterated derivatives),
  some terms are equal.
  Add versions of FaÃ  di Bruno's formula that take the symmetries into account.
- Can we generalize `scomp`/`comp` to `f : ğ•œ â†’ ğ•œ'`,
  where `ğ•œ'` is a normed algebra over `ğ•œ`? E.g., `ğ•œ = â„`, `ğ•œ' = â„‚`.

Before starting to work on these TODOs, please contact Yury Kudryashov
who may have partial progress towards some of them.
-/

open Function Set
open scoped ContDiff

section vcomp

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {g : E â†’ F} {f : ğ•œ â†’ E} {s : Set ğ•œ} {t : Set E} {x : ğ•œ} {n : WithTop â„•âˆ} {i : â„•}

theorem iteratedDerivWithin_vcomp_eq_sum_orderedFinpartition
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) (hi : i â‰¤ n) :
    iteratedDerivWithin i (g âˆ˜ f) s x =
      âˆ‘ c : OrderedFinpartition i, iteratedFDerivWithin ğ•œ c.length g t (f x) fun j â†¦
        iteratedDerivWithin (c.partSize j) f s x := by
  simp only [iteratedDerivWithin, iteratedFDerivWithin_comp hg hf ht hs hx hst hi]
  simp [FormalMultilinearSeries.taylorComp, ftaylorSeriesWithin,
    OrderedFinpartition.applyOrderedFinpartition_apply, comp_def]

theorem iteratedDeriv_vcomp_eq_sum_orderedFinpartition
    (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x) (hi : i â‰¤ n) :
    iteratedDeriv i (g âˆ˜ f) x =
      âˆ‘ c : OrderedFinpartition i, iteratedFDeriv ğ•œ c.length g (f x) fun j â†¦
        iteratedDeriv (c.partSize j) f x := by
  simp only [â† iteratedDerivWithin_univ, â† iteratedFDerivWithin_univ]
  exact iteratedDerivWithin_vcomp_eq_sum_orderedFinpartition hg hf uniqueDiffOn_univ
    uniqueDiffOn_univ (mem_univ x) (mapsTo_univ f _) hi

theorem iteratedDerivWithin_vcomp_two
    (hg : ContDiffWithinAt ğ•œ 2 g t (f x)) (hf : ContDiffWithinAt ğ•œ 2 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 2 (g âˆ˜ f) s x =
      iteratedFDerivWithin ğ•œ 2 g t (f x) (fun _ â†¦ derivWithin f s x) +
      fderivWithin ğ•œ g t (f x) (iteratedDerivWithin 2 f s x) := by
  rw [iteratedDerivWithin_vcomp_eq_sum_orderedFinpartition hg hf ht hs hx hst le_rfl]
  simp only [â† (OrderedFinpartition.extendEquiv 1).sum_comp, Fintype.sum_sigma, Fintype.sum_unique,
    OrderedFinpartition.default_eq, OrderedFinpartition.atomic_length, Fintype.sum_option]
  have : (Fin.cons 1 (fun _ â†¦ 1) : Fin 2 â†’ â„•) = fun _ â†¦ 1 :=
    funext <| Fin.forall_fin_two.mpr âŸ¨rfl, rflâŸ©
  simp [OrderedFinpartition.extendEquiv, OrderedFinpartition.extend,
    OrderedFinpartition.extendLeft, OrderedFinpartition.extendMiddle, ht _ (hst hx),
    OrderedFinpartition.atomic, this]

theorem iteratedDeriv_vcomp_two (hg : ContDiffAt ğ•œ 2 g (f x)) (hf : ContDiffAt ğ•œ 2 f x) :
    iteratedDeriv 2 (g âˆ˜ f) x =
      iteratedFDeriv ğ•œ 2 g (f x) (fun _ â†¦ deriv f x) + fderiv ğ•œ g (f x) (iteratedDeriv 2 f x) := by
  simp only [â† iteratedDerivWithin_univ, â† iteratedFDerivWithin_univ,
    â† derivWithin_univ, â† fderivWithin_univ]
  exact iteratedDerivWithin_vcomp_two hg hf uniqueDiffOn_univ
    uniqueDiffOn_univ (mem_univ x) (mapsTo_univ f _)

theorem iteratedDerivWithin_vcomp_three
    (hg : ContDiffWithinAt ğ•œ 3 g t (f x)) (hf : ContDiffWithinAt ğ•œ 3 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 3 (g âˆ˜ f) s x =
      iteratedFDerivWithin ğ•œ 3 g t (f x) (fun _ â†¦ derivWithin f s x) +
      iteratedFDerivWithin ğ•œ 2 g t (f x) ![iteratedDerivWithin 2 f s x, derivWithin f s x] +
      2 â€¢ iteratedFDerivWithin ğ•œ 2 g t (f x) ![derivWithin f s x, iteratedDerivWithin 2 f s x] +
      fderivWithin ğ•œ g t (f x) (iteratedDerivWithin 3 f s x) := by
  rw [iteratedDerivWithin_vcomp_eq_sum_orderedFinpartition hg hf ht hs hx hst le_rfl]
  simp only [â† (OrderedFinpartition.extendEquiv 1).sum_comp,
    â† (OrderedFinpartition.extendEquiv 2).sum_comp, Fintype.sum_sigma,
    Fintype.sum_option, Nat.reduceAdd, OrderedFinpartition.extendEquiv_apply,
    OrderedFinpartition.extend_none, OrderedFinpartition.extend_some,
    OrderedFinpartition.extendMiddle_length, OrderedFinpartition.default_eq,
    OrderedFinpartition.atomic_length, OrderedFinpartition.extendLeft_length,
    Fintype.sum_unique, Fin.sum_univ_zero, Fin.sum_univ_two]
  simp? [add_assoc, two_smul, iteratedFDerivWithin_one_apply (ht _ <| hst hx)] says
    simp only [OrderedFinpartition.extendLeft_partSize, OrderedFinpartition.extendLeft_length,
      OrderedFinpartition.atomic_length, Nat.reduceAdd, OrderedFinpartition.atomic_partSize,
      Fin.isValue, OrderedFinpartition.extendMiddle_partSize, Fin.cons_zero, Fin.update_cons_zero,
      Fin.cons_one, Fin.default_eq_zero, OrderedFinpartition.extendMiddle_length, Fin.cons_update,
      Fin.succ_zero_eq_one, update_self, update_idem,
      iteratedFDerivWithin_one_apply (ht _ <| hst hx), add_assoc, two_smul]
  have (j) : (Fin.cons 1 (Fin.cons 1 fun _ â†¦ 1) : Fin 3 â†’ â„•) j = 1 := by
    fin_cases j <;> rfl
  congr <;> ext x <;> fin_cases x <;> simp [this]

theorem iteratedDeriv_vcomp_three (hg : ContDiffAt ğ•œ 3 g (f x)) (hf : ContDiffAt ğ•œ 3 f x) :
    iteratedDeriv 3 (g âˆ˜ f) x =
      iteratedFDeriv ğ•œ 3 g (f x) (fun _ â†¦ deriv f x) +
      iteratedFDeriv ğ•œ 2 g (f x) ![iteratedDeriv 2 f x, deriv f x] +
      2 â€¢ iteratedFDeriv ğ•œ 2 g (f x) ![deriv f x, iteratedDeriv 2 f x] +
      fderiv ğ•œ g (f x) (iteratedDeriv 3 f x) := by
  simp only [â† iteratedDerivWithin_univ, â† iteratedFDerivWithin_univ,
    â† derivWithin_univ, â† fderivWithin_univ]
  exact iteratedDerivWithin_vcomp_three hg hf uniqueDiffOn_univ
    uniqueDiffOn_univ (mem_univ x) (mapsTo_univ f _)

end vcomp

section scomp

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {g : ğ•œ â†’ E} {f : ğ•œ â†’ ğ•œ} {s : Set ğ•œ} {t : Set ğ•œ} {x : ğ•œ} {n : WithTop â„•âˆ} {i : â„•}

theorem iteratedDerivWithin_scomp_eq_sum_orderedFinpartition
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) (hi : i â‰¤ n) :
    iteratedDerivWithin i (g âˆ˜ f) s x =
      âˆ‘ c : OrderedFinpartition i,
        (âˆ j, iteratedDerivWithin (c.partSize j) f s x) â€¢
          iteratedDerivWithin c.length g t (f x) := by
  rw [iteratedDerivWithin_vcomp_eq_sum_orderedFinpartition hg hf ht hs hx hst hi]
  simp only [iteratedFDerivWithin_apply_eq_iteratedDerivWithin_mul_prod]

theorem iteratedDeriv_scomp_eq_sum_orderedFinpartition
    (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x) (hi : i â‰¤ n) :
    iteratedDeriv i (g âˆ˜ f) x =
      âˆ‘ c : OrderedFinpartition i,
        (âˆ j, iteratedDeriv (c.partSize j) f x) â€¢ iteratedDeriv c.length g (f x) := by
  rw [iteratedDeriv_vcomp_eq_sum_orderedFinpartition hg hf hi]
  simp only [iteratedFDeriv_apply_eq_iteratedDeriv_mul_prod]

theorem iteratedDerivWithin_scomp_two
    (hg : ContDiffWithinAt ğ•œ 2 g t (f x)) (hf : ContDiffWithinAt ğ•œ 2 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 2 (g âˆ˜ f) s x =
      derivWithin f s x ^ 2 â€¢ iteratedDerivWithin 2 g t (f x) +
      iteratedDerivWithin 2 f s x â€¢ derivWithin g t (f x) := by
  rw [iteratedDerivWithin_vcomp_two hg hf ht hs hx hst]
  simp [â† derivWithin_fderivWithin, iteratedFDerivWithin_apply_eq_iteratedDerivWithin_mul_prod]

theorem iteratedDeriv_scomp_two (hg : ContDiffAt ğ•œ 2 g (f x)) (hf : ContDiffAt ğ•œ 2 f x) :
    iteratedDeriv 2 (g âˆ˜ f) x
      = deriv f x ^ 2 â€¢ iteratedDeriv 2 g (f x) + iteratedDeriv 2 f x â€¢ deriv g (f x) := by
  simp only [â† iteratedDerivWithin_univ, â† derivWithin_univ]
  exact iteratedDerivWithin_scomp_two hg hf uniqueDiffOn_univ uniqueDiffOn_univ (mem_univ _)
    (mapsTo_univ _ _)

theorem iteratedDerivWithin_scomp_three
    (hg : ContDiffWithinAt ğ•œ 3 g t (f x)) (hf : ContDiffWithinAt ğ•œ 3 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 3 (g âˆ˜ f) s x =
      derivWithin f s x ^ 3 â€¢ iteratedDerivWithin 3 g t (f x) +
      3 â€¢ iteratedDerivWithin 2 f s x â€¢ derivWithin f s x â€¢ iteratedDerivWithin 2 g t (f x) +
      iteratedDerivWithin 3 f s x â€¢ derivWithin g t (f x) := by
  rw [iteratedDerivWithin_vcomp_three hg hf ht hs hx hst]
  simp? [â† derivWithin_fderivWithin, mul_smul, smul_comm (iteratedDerivWithin 2 f s x),
      iteratedFDerivWithin_apply_eq_iteratedDerivWithin_mul_prod] says
    simp only [iteratedFDerivWithin_apply_eq_iteratedDerivWithin_mul_prod, Finset.prod_const,
      Finset.card_univ, Fintype.card_fin, Fin.prod_univ_two, Fin.isValue, Matrix.cons_val_zero,
      Matrix.cons_val_one, Matrix.cons_val_fin_one, mul_smul,
      smul_comm (iteratedDerivWithin 2 f s x), â† derivWithin_fderivWithin,
      ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, add_left_inj]
  abel

theorem iteratedDeriv_scomp_three (hg : ContDiffAt ğ•œ 3 g (f x)) (hf : ContDiffAt ğ•œ 3 f x) :
    iteratedDeriv 3 (g âˆ˜ f) x =
      deriv f x ^ 3 â€¢ iteratedDeriv 3 g (f x) +
      3 â€¢ iteratedDeriv 2 f x â€¢ deriv f x â€¢ iteratedDeriv 2 g (f x) +
      iteratedDeriv 3 f x â€¢ deriv g (f x) := by
  simp only [â† iteratedDerivWithin_univ, â† derivWithin_univ]
  exact iteratedDerivWithin_scomp_three hg hf uniqueDiffOn_univ uniqueDiffOn_univ (mem_univ _)
    (mapsTo_univ _ _)

end scomp

section comp

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {g f : ğ•œ â†’ ğ•œ} {s t : Set ğ•œ} {x : ğ•œ} {n : WithTop â„•âˆ} {i : â„•}

theorem iteratedDerivWithin_comp_eq_sum_orderedFinpartition
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) (hi : i â‰¤ n) :
    iteratedDerivWithin i (g âˆ˜ f) s x =
      âˆ‘ c : OrderedFinpartition i,
        iteratedDerivWithin c.length g t (f x) * âˆ j, iteratedDerivWithin (c.partSize j) f s x := by
  rw [iteratedDerivWithin_scomp_eq_sum_orderedFinpartition hg hf ht hs hx hst hi]
  simp only [smul_eq_mul, mul_comm]

theorem iteratedDeriv_comp_eq_sum_orderedFinpartition
    (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x) (hi : i â‰¤ n) :
    iteratedDeriv i (g âˆ˜ f) x =
      âˆ‘ c : OrderedFinpartition i,
        iteratedDeriv c.length g (f x) * âˆ j, iteratedDeriv (c.partSize j) f x := by
  rw [iteratedDeriv_scomp_eq_sum_orderedFinpartition hg hf hi]
  simp only [smul_eq_mul, mul_comm]

theorem iteratedDerivWithin_comp_two
    (hg : ContDiffWithinAt ğ•œ 2 g t (f x)) (hf : ContDiffWithinAt ğ•œ 2 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 2 (g âˆ˜ f) s x =
      iteratedDerivWithin 2 g t (f x) * derivWithin f s x ^ 2 +
      derivWithin g t (f x) * iteratedDerivWithin 2 f s x := by
  rw [iteratedDerivWithin_scomp_two hg hf ht hs hx hst]
  simp only [smul_eq_mul, mul_comm]

theorem iteratedDeriv_comp_two (hg : ContDiffAt ğ•œ 2 g (f x)) (hf : ContDiffAt ğ•œ 2 f x) :
    iteratedDeriv 2 (g âˆ˜ f) x =
      iteratedDeriv 2 g (f x) * deriv f x ^ 2 + deriv g (f x) * iteratedDeriv 2 f x := by
  simp only [â† iteratedDerivWithin_univ, â† derivWithin_univ]
  exact iteratedDerivWithin_comp_two hg hf uniqueDiffOn_univ uniqueDiffOn_univ (mem_univ _)
    (mapsTo_univ _ _)

theorem iteratedDerivWithin_comp_three
    (hg : ContDiffWithinAt ğ•œ 3 g t (f x)) (hf : ContDiffWithinAt ğ•œ 3 f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    iteratedDerivWithin 3 (g âˆ˜ f) s x =
      iteratedDerivWithin 3 g t (f x) * derivWithin f s x ^ 3 +
      3 * iteratedDerivWithin 2 g t (f x) * iteratedDerivWithin 2 f s x * derivWithin f s x +
      derivWithin g t (f x) * iteratedDerivWithin 3 f s x := by
  rw [iteratedDerivWithin_scomp_three hg hf ht hs hx hst]
  simp only [nsmul_eq_mul, smul_eq_mul, Nat.cast_ofNat]
  ring

theorem iteratedDeriv_comp_three (hg : ContDiffAt ğ•œ 3 g (f x)) (hf : ContDiffAt ğ•œ 3 f x) :
    iteratedDeriv 3 (g âˆ˜ f) x =
      iteratedDeriv 3 g (f x) * deriv f x ^ 3 +
      3 * iteratedDeriv 2 g (f x) * iteratedDeriv 2 f x * deriv f x +
      deriv g (f x) * iteratedDeriv 3 f x := by
  simp only [â† iteratedDerivWithin_univ, â† derivWithin_univ]
  exact iteratedDerivWithin_comp_three hg hf uniqueDiffOn_univ uniqueDiffOn_univ (mem_univ _)
    (mapsTo_univ _ _)

end comp
