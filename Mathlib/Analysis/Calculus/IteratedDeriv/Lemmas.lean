/-
Copyright (c) 2023 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, Ruben Van de Velde
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Shift
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# One-dimensional iterated derivatives

This file contains a number of further results on `iteratedDerivWithin` that need more imports
than are available in `Mathlib/Analysis/Calculus/IteratedDeriv/Defs.lean`.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {R : Type*} [Semiring R] [Module R F] [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F]
  {n : â„•} {x : ğ•œ} {s : Set ğ•œ} (hx : x âˆˆ s) (h : UniqueDiffOn ğ•œ s) {f g : ğ•œ â†’ F}

theorem iteratedDerivWithin_add (hf : ContDiffOn ğ•œ n f s) (hg : ContDiffOn ğ•œ n g s) :
    iteratedDerivWithin n (f + g) s x =
      iteratedDerivWithin n f s x + iteratedDerivWithin n g s x := by
  simp_rw [iteratedDerivWithin, iteratedFDerivWithin_add_apply hf hg h hx,
    ContinuousMultilinearMap.add_apply]

theorem iteratedDerivWithin_congr (hfg : Set.EqOn f g s) :
    Set.EqOn (iteratedDerivWithin n f s) (iteratedDerivWithin n g s) s := by
  induction n generalizing f g with
  | zero => rwa [iteratedDerivWithin_zero]
  | succ n IH =>
    intro y hy
    have : UniqueDiffWithinAt ğ•œ s y := h.uniqueDiffWithinAt hy
    rw [iteratedDerivWithin_succ this, iteratedDerivWithin_succ this]
    exact derivWithin_congr (IH hfg) (IH hfg hy)

theorem iteratedDerivWithin_const_add (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c + f z) s x = iteratedDerivWithin n f s x := by
  obtain âŸ¨n, rflâŸ© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ' h hx, iteratedDerivWithin_succ' h hx]
  refine iteratedDerivWithin_congr h ?_ hx
  intro y hy
  exact derivWithin_const_add (h.uniqueDiffWithinAt hy) _

theorem iteratedDerivWithin_const_neg (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c - f z) s x = iteratedDerivWithin n (fun z => -f z) s x := by
  obtain âŸ¨n, rflâŸ© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ' h hx, iteratedDerivWithin_succ' h hx]
  refine iteratedDerivWithin_congr h ?_ hx
  intro y hy
  have : UniqueDiffWithinAt ğ•œ s y := h.uniqueDiffWithinAt hy
  rw [derivWithin.neg this]
  exact derivWithin_const_sub this _

theorem iteratedDerivWithin_const_smul (c : R) (hf : ContDiffOn ğ•œ n f s) :
    iteratedDerivWithin n (c â€¢ f) s x = c â€¢ iteratedDerivWithin n f s x := by
  simp_rw [iteratedDerivWithin]
  rw [iteratedFDerivWithin_const_smul_apply hf h hx]
  simp only [ContinuousMultilinearMap.smul_apply]

theorem iteratedDerivWithin_const_mul (c : ğ•œ) {f : ğ•œ â†’ ğ•œ} (hf : ContDiffOn ğ•œ n f s) :
    iteratedDerivWithin n (fun z => c * f z) s x = c * iteratedDerivWithin n f s x := by
  simpa using iteratedDerivWithin_const_smul (F := ğ•œ) hx h c hf

variable (f) in
theorem iteratedDerivWithin_neg :
    iteratedDerivWithin n (-f) s x = -iteratedDerivWithin n f s x := by
  rw [iteratedDerivWithin, iteratedDerivWithin, iteratedFDerivWithin_neg_apply h hx,
    ContinuousMultilinearMap.neg_apply]

variable (f) in
theorem iteratedDerivWithin_neg' :
    iteratedDerivWithin n (fun z => -f z) s x = -iteratedDerivWithin n f s x :=
  iteratedDerivWithin_neg hx h f

theorem iteratedDerivWithin_sub (hf : ContDiffOn ğ•œ n f s) (hg : ContDiffOn ğ•œ n g s) :
    iteratedDerivWithin n (f - g) s x =
      iteratedDerivWithin n f s x - iteratedDerivWithin n g s x := by
  rw [sub_eq_add_neg, sub_eq_add_neg, Pi.neg_def, iteratedDerivWithin_add hx h hf hg.neg,
    iteratedDerivWithin_neg' hx h]

theorem iteratedDeriv_const_smul {n : â„•} {f : ğ•œ â†’ F} (h : ContDiff ğ•œ n f) (c : ğ•œ) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n â€¢ iteratedDeriv n f (c * x) := by
  induction n with
  | zero => simp
  | succ n ih =>
    funext x
    have hâ‚€ : DifferentiableAt ğ•œ (iteratedDeriv n f) (c * x) :=
      h.differentiable_iteratedDeriv n (Nat.cast_lt.mpr n.lt_succ_self) |>.differentiableAt
    have hâ‚ : DifferentiableAt ğ•œ (fun x => iteratedDeriv n f (c * x)) x := by
      rw [â† Function.comp_def]
      apply DifferentiableAt.comp
      Â· exact h.differentiable_iteratedDeriv n (Nat.cast_lt.mpr n.lt_succ_self) |>.differentiableAt
      Â· exact differentiableAt_id'.const_mul _
    rw [iteratedDeriv_succ, ih h.of_succ, deriv_const_smul _ hâ‚, iteratedDeriv_succ,
      â† Function.comp_def, deriv.scomp x hâ‚€ (differentiableAt_id'.const_mul _),
      deriv_const_mul _ differentiableAt_id', deriv_id'', smul_smul, mul_one, pow_succ]

theorem iteratedDeriv_const_mul {n : â„•} {f : ğ•œ â†’ ğ•œ} (h : ContDiff ğ•œ n f) (c : ğ•œ) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n * iteratedDeriv n f (c * x) := by
  simpa only [smul_eq_mul] using iteratedDeriv_const_smul h c

lemma iteratedDeriv_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ -(f x)) a = -(iteratedDeriv n f a) := by
  simp_rw [â† iteratedDerivWithin_univ, iteratedDerivWithin_neg' (Set.mem_univ a) uniqueDiffOn_univ]

lemma iteratedDeriv_comp_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ f (-x)) a = (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-a) := by
  induction' n with n ih generalizing a
  Â· simp only [Nat.zero_eq, iteratedDeriv_zero, pow_zero, one_smul]
  Â· have ih' : iteratedDeriv n (fun x â†¦ f (-x)) = fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-x) :=
      funext ih
    rw [iteratedDeriv_succ, iteratedDeriv_succ, ih', pow_succ', neg_mul, one_mul,
      deriv_comp_neg (f := fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f x), deriv_const_smul',
      neg_smul]
