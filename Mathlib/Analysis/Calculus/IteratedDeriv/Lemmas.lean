/-
Copyright (c) 2023 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, Ruben Van de Velde
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Operations
public import Mathlib.Analysis.Calculus.Deriv.Mul
public import Mathlib.Analysis.Calculus.Deriv.Shift
public import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# One-dimensional iterated derivatives

This file contains a number of further results on `iteratedDerivWithin` that need more imports
than are available in `Mathlib/Analysis/Calculus/IteratedDeriv/Defs.lean`.
-/

public section

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {n : â„•} {x : ğ•œ} {s : Set ğ•œ} (hx : x âˆˆ s) (h : UniqueDiffOn ğ•œ s) {f g : ğ•œ â†’ F}
  -- For maximum generality, results about `smul` involve a second type besides `ğ•œ`,
  -- with varying hypotheses.
  -- * `R`: general type.
  {R : Type*} [DistribSMul R F] [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F]
  -- * `ğ•`: division semiring. (Addition in `ğ•` is not used, so the results would work with a
  -- `GroupWithZero` if we had a `DistribSMulWithZero` typeclass.)
  {ğ• : Type*} [DivisionSemiring ğ•] [Module ğ• F] [SMulCommClass ğ•œ ğ• F] [ContinuousConstSMul ğ• F]
  -- * `ğ”¸`: normed `ğ•œ`-algebra.
  {ğ”¸ : Type*} [NormedRing ğ”¸] [NormedAlgebra ğ•œ ğ”¸] [Module ğ”¸ F] [IsBoundedSMul ğ”¸ F]
    [IsScalarTower ğ•œ ğ”¸ F]
  -- * `ğ•œ'`: normed `ğ•œ`-division algebra.
  {ğ•œ' : Type*} [NormedDivisionRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
    [Module ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F] [ContinuousSMul ğ•œ' F]

section one_dimensional


open scoped Topology

section

theorem Filter.EventuallyEq.iteratedDerivWithin_eq (hfg : f =á¶ [ğ“[s] x] g) (hfg' : f x = g x) :
    iteratedDerivWithin n f s x = iteratedDerivWithin n g s x :=
  congr($(hfg.iteratedFDerivWithin_eq hfg' n) _)

theorem Filter.EventuallyEq.iteratedDerivWithin_eq_of_nhds_insert
    {ğ•œ F : Type*} [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F] (n : â„•) {f g : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}
    (hfg : f =á¶ [ğ“[insert x s] x] g) :
    iteratedDerivWithin n f s x = iteratedDerivWithin n g s x :=
  (hfg.filter_mono (by simp)).iteratedDerivWithin_eq (hfg.eq_of_nhdsWithin (by simp))

theorem iteratedDerivWithin_congr (hfg : Set.EqOn f g s) :
    Set.EqOn (iteratedDerivWithin n f s) (iteratedDerivWithin n g s) s :=
  fun _ hx â†¦ hfg.eventuallyEq_nhdsWithin.iteratedDerivWithin_eq (hfg hx)

include h hx in
theorem iteratedDerivWithin_add
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    iteratedDerivWithin n (f + g) s x =
      iteratedDerivWithin n f s x + iteratedDerivWithin n g s x := by
  simp_rw [iteratedDerivWithin, iteratedFDerivWithin_add_apply hf hg h hx,
    ContinuousMultilinearMap.add_apply]

include h hx in
theorem iteratedDerivWithin_fun_add
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    iteratedDerivWithin n (fun z â†¦ f z + g z) s x =
      iteratedDerivWithin n f s x + iteratedDerivWithin n g s x := by
  simpa using iteratedDerivWithin_add hx h hf hg

theorem iteratedDerivWithin_const_add (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c + f z) s x = iteratedDerivWithin n f s x := by
  obtain âŸ¨n, rflâŸ© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ', iteratedDerivWithin_succ']
  congr 1 with y
  exact derivWithin_const_add _

theorem iteratedDerivWithin_const_sub (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c - f z) s x = iteratedDerivWithin n (fun z => -f z) s x := by
  obtain âŸ¨n, rflâŸ© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ', iteratedDerivWithin_succ']
  congr 1 with y
  rw [derivWithin.fun_neg]
  exact derivWithin_const_sub _

include h hx in
theorem iteratedDerivWithin_const_smul (c : R) (hf : ContDiffWithinAt ğ•œ n f s x) :
    iteratedDerivWithin n (c â€¢ f) s x = c â€¢ iteratedDerivWithin n f s x := by
  simp_rw [iteratedDerivWithin]
  rw [iteratedFDerivWithin_const_smul_apply (a := c) hf h hx]
  simp only [ContinuousMultilinearMap.smul_apply]

include h hx in
theorem iteratedDerivWithin_fun_const_smul (c : R) (hf : ContDiffWithinAt ğ•œ n f s x) :
    iteratedDerivWithin n (fun w â†¦ c â€¢ f w) s x = c â€¢ iteratedDerivWithin n f s x :=
  iteratedDerivWithin_const_smul hx h c hf

/-- A variant of `iteratedDerivWithin_const_smul` without differentiability assumption when
the scalar multiplication is by division ring elements. -/
@[simp]
theorem iteratedDerivWithin_const_smul_field (c : ğ•) (f : ğ•œ â†’ F) :
    iteratedDerivWithin n (c â€¢ f) s x = c â€¢ iteratedDerivWithin n f s x := by
  induction n generalizing f x with
  | zero => simp
  | succ n IH =>
    simp_rw [iteratedDerivWithin_succ, funext (@IH Â· f), â† Pi.smul_def,
      derivWithin_const_smul_field]

include h hx in
theorem iteratedDerivWithin_const_mul (c : ğ”¸) {f : ğ•œ â†’ ğ”¸} (hf : ContDiffWithinAt ğ•œ n f s x) :
    iteratedDerivWithin n (fun z => c * f z) s x = c * iteratedDerivWithin n f s x :=
  iteratedDerivWithin_fun_const_smul hx h c hf

/-- A variant of `iteratedDerivWithin_fun_const_smul` without differentiability assumption when
the scalar multiplication is by division ring elements. -/
@[simp]
theorem iteratedDerivWithin_fun_const_smul_field (c : ğ•) (f : ğ•œ â†’ F) :
    iteratedDerivWithin n (fun z => c â€¢ f z) s x = c â€¢ iteratedDerivWithin n f s x :=
  iteratedDerivWithin_const_smul_field c f

@[simp]
theorem iteratedDerivWithin_const_mul_field (c : ğ•œ') (f : ğ•œ â†’ ğ•œ') :
    iteratedDerivWithin n (fun z => c * f z) s x = c * iteratedDerivWithin n f s x :=
  iteratedDerivWithin_fun_const_smul_field c f

include h hx in
theorem iteratedDerivWithin_smul_const {f : ğ•œ â†’ ğ”¸} (hf : ContDiffWithinAt ğ•œ n f s x) (v : F) :
    iteratedDerivWithin n (fun y â†¦ f y â€¢ v) s x = iteratedDerivWithin n f s x â€¢ v := by
  simp [iteratedDerivWithin, iteratedFDerivWithin_smul_const_apply hf h hx]

include h hx in
@[simp]
theorem iteratedDerivWithin_mul_const {f : ğ•œ â†’ ğ”¸} (hf : ContDiffWithinAt ğ•œ n f s x) (d : ğ”¸) :
    iteratedDerivWithin n (fun z â†¦ f z * d) s x = iteratedDerivWithin n f s x * d :=
  iteratedDerivWithin_smul_const hx h hf d

/-- A variant of `iteratedDerivWithin_mul_const` without differentiability assumption when
the scalar multiplication is by division ring elements. -/
@[simp]
theorem iteratedDerivWithin_mul_const_field (f : ğ•œ â†’ ğ•œ') (d : ğ•œ') :
    iteratedDerivWithin n (fun z â†¦ f z * d) s x = iteratedDerivWithin n f s x * d := by
  induction n generalizing f x with
  | zero => simp
  | succ n IH =>
    simp_rw [iteratedDerivWithin_succ, funext (@IH Â· f), derivWithin_mul_const_field]

variable (f) in
omit h hx in
@[simp]
theorem iteratedDerivWithin_neg :
    iteratedDerivWithin n (-f) s x = -iteratedDerivWithin n f s x := by
  induction n generalizing x with
  | zero => simp
  | succ n IH =>
    simp only [iteratedDerivWithin_succ]
    rw [â† derivWithin.neg]
    congr with y
    exact IH

variable (f) in
theorem iteratedDerivWithin_fun_neg :
    iteratedDerivWithin n (fun z => -f z) s x = -iteratedDerivWithin n f s x :=
  iteratedDerivWithin_neg f

include h hx

theorem iteratedDerivWithin_sub
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    iteratedDerivWithin n (f - g) s x =
      iteratedDerivWithin n f s x - iteratedDerivWithin n g s x := by
  rw [sub_eq_add_neg, sub_eq_add_neg, Pi.neg_def, iteratedDerivWithin_add hx h hf hg.neg,
    iteratedDerivWithin_fun_neg]

theorem iteratedDerivWithin_comp_const_smul (hf : ContDiffOn ğ•œ n f s) (c : ğ•œ)
    (hs : Set.MapsTo (c * Â·) s s) :
    iteratedDerivWithin n (fun x => f (c * x)) s x = c ^ n â€¢ iteratedDerivWithin n f s (c * x) := by
  induction n generalizing x with
  | zero => simp
  | succ n ih =>
    have hcx : c * x âˆˆ s := hs hx
    have hâ‚€ : s.EqOn
        (iteratedDerivWithin n (fun x â†¦ f (c * x)) s)
        (fun x => c ^ n â€¢ iteratedDerivWithin n f s (c * x)) :=
      fun x hx => ih hx hf.of_succ
    have hâ‚ : DifferentiableWithinAt ğ•œ (iteratedDerivWithin n f s) s (c * x) :=
      hf.differentiableOn_iteratedDerivWithin (Nat.cast_lt.mpr n.lt_succ_self) h _ hcx
    have hâ‚‚ : DifferentiableWithinAt ğ•œ (fun x => iteratedDerivWithin n f s (c * x)) s x := by
      rw [â† Function.comp_def]
      apply DifferentiableWithinAt.comp
      Â· exact hf.differentiableOn_iteratedDerivWithin (Nat.cast_lt.mpr n.lt_succ_self) h _ hcx
      Â· exact differentiableWithinAt_id'.const_mul _
      Â· exact hs
    rw [iteratedDerivWithin_succ, derivWithin_congr hâ‚€ (ih hx hf.of_succ),
      derivWithin_fun_const_smul (c ^ n) hâ‚‚, iteratedDerivWithin_succ,
      â† Function.comp_def,
      derivWithin.scomp x hâ‚ (differentiableWithinAt_id'.const_mul _) hs,
      derivWithin_const_mul _ differentiableWithinAt_id', derivWithin_id' _ _ (h _ hx),
      smul_smul, mul_one, pow_succ]

lemma iteratedDerivWithin_id :
    iteratedDerivWithin n id s x = if n = 0 then x else if n = 1 then 1 else 0 := by
  obtain (_ | n) := n
  Â· simp
  Â· rw [iteratedDerivWithin_succ', iteratedDerivWithin_congr (g := fun _ â†¦ 1) _ hx]
    Â· simp [iteratedDerivWithin_const]
    Â· exact fun y hy â†¦ derivWithin_id _ _ (h.uniqueDiffWithinAt hy)

lemma iteratedDerivWithin_fun_id :
    iteratedDerivWithin n (Â·) s x = if n = 0 then x else if n = 1 then 1 else 0 :=
  iteratedDerivWithin_id hx h

lemma iteratedDerivWithin_smul {f : ğ•œ â†’ ğ”¸} {g : ğ•œ â†’ F}
    (hf : ContDiffWithinAt ğ•œ (â†‘n) f s x) (hg : ContDiffWithinAt ğ•œ (â†‘n) g s x) :
    iteratedDerivWithin n (f â€¢ g) s x = âˆ‘ i âˆˆ .range (n + 1),
      n.choose i â€¢ iteratedDerivWithin i f s x â€¢ iteratedDerivWithin (n - i) g s x := by
  induction n generalizing f g with
  | zero => simp
  | succ n IH =>
    obtain âŸ¨U, hU, HâŸ© := ((hf.eventually (by simp)).and (hg.eventually (by simp))).exists_mem
    rw [iteratedDerivWithin_succ', Filter.EventuallyEq.iteratedDerivWithin_eq_of_nhds_insert
        (g := f â€¢ derivWithin g s + derivWithin f s â€¢ g)]
    Â· rw [Finset.sum_range_succ', iteratedDerivWithin_add hx h, IH, Finset.sum_range_succ', IH]
      Â· simp only [Nat.choose_succ_succ', add_smul, Finset.sum_add_distrib]
        nth_rw 3 [Finset.sum_range_succ]
        have : âˆ€ i âˆˆ Finset.range n, 1 â‰¤ n - i := by simp; lia
        simp +contextual [â† iteratedDerivWithin_succ', â† n.sub_sub, Nat.sub_add_cancel, this]
        abel
      all_goals clear IH H U hU; fun_prop (disch := simp_all)
    Â· filter_upwards [hf.eventually (by simp), hg.eventually (by simp)] with y hfy hgy
      rw [derivWithin_smul (hfy.differentiableWithinAt _) (hgy.differentiableWithinAt _)]
      all_goals simp

lemma iteratedDerivWithin_mul {f g : ğ•œ â†’ ğ”¸}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    iteratedDerivWithin n (f * g) s x = âˆ‘ i âˆˆ .range (n + 1),
      n.choose i * iteratedDerivWithin i f s x * iteratedDerivWithin (n - i) g s x := by
  simp [â† smul_eq_mul, iteratedDerivWithin_smul hx h hf hg]

theorem iteratedDerivWithin_pow (m : â„•) (k : â„•) :
    iteratedDerivWithin k (Â· ^ m) s x = m.descFactorial k * x ^ (m - k) := by
  induction m generalizing k with
  | zero => cases k <;> simp [iteratedDerivWithin_const]
  | succ i IH =>
    obtain (_ | k) := k
    Â· simp
    simp only [pow_succ]
    refine (iteratedDerivWithin_mul hx h (by fun_prop) (by fun_prop)).trans ?_
    have : ((i + 1).descFactorial (k + 1)) =
        (k + 1) * (i.descFactorial k) + (i.descFactorial (k + 1)) := by
      rw [Nat.succ_descFactorial_succ]
      cases le_or_gt k i <;> simp [Nat.descFactorial, â† add_mul, *]; lia
    obtain hik | hik := le_or_gt i k <;>
      simp +contextual [IH, iteratedDerivWithin_fun_id, h, hx, Finset.sum_range_succ,
        show âˆ€ x âˆˆ Finset.range k, k + 1 - x â‰  0 by simp; lia, -Nat.descFactorial_succ,
        show âˆ€ x âˆˆ Finset.range k, k + 1 - x â‰  1 by simp; lia, this,
        Nat.descFactorial_eq_zero_iff_lt.mpr, hik,
        show k < i â†’ i - k = (i - (k + 1) + 1) by lia]; ring

end

lemma iteratedDeriv_add (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (f + g) x = iteratedDeriv n f x + iteratedDeriv n g x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_add (Set.mem_univ _) uniqueDiffOn_univ hf hg

-- TODO: `@[to_fun]` generates the wrong name. Same for the various lemmas below.
lemma iteratedDeriv_fun_add (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (fun z â†¦ f z + g z) x = iteratedDeriv n f x + iteratedDeriv n g x :=
  iteratedDeriv_add hf hg

theorem iteratedDeriv_const_add (hn : 0 < n) (c : F) :
    iteratedDeriv n (fun z => c + f z) x = iteratedDeriv n f x := by
  simpa only [â† iteratedDerivWithin_univ] using iteratedDerivWithin_const_add hn c

theorem iteratedDeriv_const_sub (hn : 0 < n) (c : F) :
    iteratedDeriv n (fun z => c - f z) x = iteratedDeriv n (-f) x := by
  simpa only [â† iteratedDerivWithin_univ] using iteratedDerivWithin_const_sub hn c

@[simp]
lemma iteratedDeriv_fun_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ -(f x)) a = -(iteratedDeriv n f a) := by
  simpa only [â† iteratedDerivWithin_univ] using iteratedDerivWithin_neg f

@[simp]
lemma iteratedDeriv_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (-f) a = -(iteratedDeriv n f a) := by
  simpa only [â† iteratedDerivWithin_univ] using iteratedDerivWithin_neg f

lemma iteratedDeriv_sub (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (f - g) x = iteratedDeriv n f x - iteratedDeriv n g x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_sub (Set.mem_univ _) uniqueDiffOn_univ hf hg

lemma iteratedDeriv_fun_sub (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (fun z â†¦ f z - g z) x = iteratedDeriv n f x - iteratedDeriv n g x :=
  iteratedDeriv_sub hf hg

theorem iteratedDeriv_const_smul {n : â„•} {f : ğ•œ â†’ F} (h : ContDiffAt ğ•œ n f x) (c : R) :
    iteratedDeriv n (c â€¢ f) x = c â€¢ iteratedDeriv n f x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_const_smul (Set.mem_univ x) uniqueDiffOn_univ
      c (contDiffWithinAt_univ.mpr h)

theorem iteratedDeriv_fun_const_smul {n : â„•} {f : ğ•œ â†’ F} (h : ContDiffAt ğ•œ n f x) (c : R) :
    iteratedDeriv n (c â€¢ f Â·) x = c â€¢ iteratedDeriv n f x :=
  iteratedDeriv_const_smul h c

/-- A variant of `iteratedDeriv_const_smul` without differentiability assumption when
the scalar multiplication is by division ring elements. -/
@[simp]
theorem iteratedDeriv_const_smul_field {n : â„•} (c : ğ•) (f : ğ•œ â†’ F) :
    iteratedDeriv n (c â€¢ f) x = c â€¢ iteratedDeriv n f x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_const_smul_field (s := Set.univ) c f

/-- A variant of `iteratedDeriv_fun_const_smul` without differentiability assumption when
the scalar multiplication is by division ring elements. -/
@[simp]
theorem iteratedDeriv_fun_const_smul_field {n : â„•} (c : ğ•) (f : ğ•œ â†’ F) :
    iteratedDeriv n (c â€¢ f Â·) x = c â€¢ iteratedDeriv n f x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_fun_const_smul_field (s := Set.univ) c f

theorem iteratedDeriv_smul_const {f : ğ•œ â†’ ğ”¸} (hf : ContDiffAt ğ•œ n f x) (v : F) :
    iteratedDeriv n (fun y â†¦ f y â€¢ v) x = iteratedDeriv n f x â€¢ v := by
  simp [iteratedDeriv, iteratedFDeriv_smul_const_apply hf]

theorem iteratedDeriv_const_mul {n : â„•} {f : ğ•œ â†’ ğ”¸} (c : ğ”¸) (hf : ContDiffAt ğ•œ n f x) :
    iteratedDeriv n (c * f Â·) x = c * iteratedDeriv n f x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_const_mul (Set.mem_univ x) uniqueDiffOn_univ c hf

/-- A variant of `iteratedDeriv_const_mul` without differentiability assumption when
the multiplication is in a division ring. -/
@[simp]
theorem iteratedDeriv_const_mul_field {n : â„•} (c : ğ•œ') (f : ğ•œ â†’ ğ•œ') :
    iteratedDeriv n (c * f Â·) x = c * iteratedDeriv n f x := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_const_mul_field (s := .univ) c f

/-- A variant of `iteratedDeriv_mul_const` without differentiability assumption when
the multiplication is in a division ring. -/
@[simp]
theorem iteratedDeriv_mul_const_field {n : â„•} (f : ğ•œ â†’ ğ•œ') (c : ğ•œ') :
    iteratedDeriv n (f Â· * c) x = iteratedDeriv n f x * c := by
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_mul_const_field (s := .univ) f c

@[simp]
theorem iteratedDeriv_div_const {n : â„•} (f : ğ•œ â†’ ğ•œ') (c : ğ•œ') :
    iteratedDeriv n (f Â· / c) x = iteratedDeriv n f x / c := by
  simp [div_eq_mul_inv]

theorem iteratedDeriv_comp_const_smul {n : â„•} {f : ğ•œ â†’ F} (h : ContDiff ğ•œ n f) (c : ğ•œ) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n â€¢ iteratedDeriv n f (c * x) := by
  funext x
  simpa only [iteratedDerivWithin_univ] using
    iteratedDerivWithin_comp_const_smul (Set.mem_univ x) uniqueDiffOn_univ (contDiffOn_univ.mpr h)
      c (Set.mapsTo_univ _ _)

theorem iteratedDeriv_comp_const_mul {n : â„•} {f : ğ•œ â†’ ğ•œ} (h : ContDiff ğ•œ n f) (c : ğ•œ) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n * iteratedDeriv n f (c * x) := by
  simpa only [smul_eq_mul] using iteratedDeriv_comp_const_smul h c

lemma iteratedDeriv_comp_neg (n : â„•) (f : ğ•œ â†’ F) (a : ğ•œ) :
    iteratedDeriv n (fun x â†¦ f (-x)) a = (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-a) := by
  induction n generalizing a with
  | zero => simp only [iteratedDeriv_zero, pow_zero, one_smul]
  | succ n ih =>
    have ih' : iteratedDeriv n (fun x â†¦ f (-x)) = fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (-x) :=
      funext ih
    rw [iteratedDeriv_succ, iteratedDeriv_succ, ih', pow_succ', neg_mul, one_mul,
      deriv_comp_neg (f := fun x â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f x), deriv_fun_const_smul_field,
      neg_smul]

lemma iteratedDeriv_id {n : â„•} {x : ğ•œ} :
    iteratedDeriv n id x = if n = 0 then x else if n = 1 then 1 else 0 := by
  obtain (_ | _ | n) := n <;>
    simp [iteratedDeriv_succ', iteratedDeriv_const]

lemma iteratedDeriv_fun_id {n : â„•} {x : ğ•œ} :
    iteratedDeriv n (fun a â†¦ a) x = if n = 0 then x else if n = 1 then 1 else 0 :=
  iteratedDeriv_id

lemma iteratedDeriv_fun_id_zero :
    iteratedDeriv n (fun a â†¦ a) (0 : ğ•œ) = if n = 1 then 1 else 0 := by
  simp +contextual [iteratedDeriv_fun_id]

lemma iteratedDeriv_mul {f g : ğ•œ â†’ ğ”¸} (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (f * g) x = âˆ‘ i âˆˆ .range (n + 1),
      n.choose i * iteratedDeriv i f x * iteratedDeriv (n - i) g x := by
  simpa using iteratedDerivWithin_mul
    (Set.mem_univ x) uniqueDiffOn_univ hf.contDiffWithinAt hg.contDiffWithinAt

@[simp]
theorem iteratedDeriv_pow (m : â„•) (k : â„•) :
    iteratedDeriv k (Â· ^ m) x = m.descFactorial k * x ^ (m - k) := by
  simpa using iteratedDerivWithin_pow (Set.mem_univ x) uniqueDiffOn_univ m k

lemma iteratedDeriv_fun_mul {f g : ğ•œ â†’ ğ”¸} (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    iteratedDeriv n (fun x â†¦ f x * g x) x = âˆ‘ i âˆˆ .range (n + 1),
      n.choose i * iteratedDeriv i f x * iteratedDeriv (n - i) g x :=
  iteratedDeriv_mul hf hg

lemma iteratedDeriv_fun_pow_zero {n m : â„•} :
    iteratedDeriv n (Â· ^ m) (0 : ğ•œ) = if n = m then m.factorial else 0 := by
  obtain h | h | h := lt_trichotomy n m <;>
    simp_all [Nat.descFactorial_self, Nat.descFactorial_eq_zero_iff_lt.mpr, ne_of_lt, ne_of_gt]

lemma Filter.EventuallyEq.iteratedDeriv_eq (n : â„•) {f g : ğ•œ â†’ F} {x : ğ•œ} (hfg : f =á¶ [ğ“ x] g) :
    iteratedDeriv n f x = iteratedDeriv n g x := by
  simp only [â† iteratedDerivWithin_univ, iteratedDerivWithin_eq_iteratedFDerivWithin]
  rw [(hfg.filter_mono nhdsWithin_le_nhds).iteratedFDerivWithin_eq hfg.eq_of_nhds n]

lemma Set.EqOn.iteratedDeriv_of_isOpen (hfg : Set.EqOn f g s) (hs : IsOpen s) (n : â„•) :
    Set.EqOn (iteratedDeriv n f) (iteratedDeriv n g) s := by
  refine fun x hx â†¦ Filter.EventuallyEq.iteratedDeriv_eq n ?_
  filter_upwards [IsOpen.mem_nhds hs hx] with a ha
  exact hfg ha

end one_dimensional

/-!
### Invariance of iterated derivatives under translation
-/

section shift_invariance

variable (n : â„•) (f : ğ•œ â†’ F) (s : ğ•œ)

/-- The iterated derivative commutes with shifting the function by a constant on the left. -/
lemma iteratedDeriv_comp_const_add :
    iteratedDeriv n (fun z â†¦ f (s + z)) = fun t â†¦ iteratedDeriv n f (s + t) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
    simpa only [iteratedDeriv_succ, IH] using funext <| deriv_comp_const_add _ s

/-- The iterated derivative commutes with shifting the function by a constant on the right. -/
lemma iteratedDeriv_comp_add_const :
    iteratedDeriv n (fun z â†¦ f (z + s)) = fun t â†¦ iteratedDeriv n f (t + s) := by
  induction n with
  | zero => simp only [iteratedDeriv_zero]
  | succ n IH =>
    simpa only [iteratedDeriv_succ, IH] using funext <| deriv_comp_add_const _ s

lemma iteratedDeriv_comp_sub_const :
    iteratedDeriv n (fun z â†¦ f (z - s)) = fun t â†¦ iteratedDeriv n f (t - s) := by
  simp [sub_eq_add_neg, iteratedDeriv_comp_add_const]

lemma iteratedDeriv_comp_const_sub :
    iteratedDeriv n (fun z â†¦ f (s - z)) = fun t â†¦ (-1 : ğ•œ) ^ n â€¢ iteratedDeriv n f (s - t) := by
  simpa [funext_iff, neg_add_eq_sub, iteratedDeriv_comp_add_const] using
    iteratedDeriv_comp_neg n (fun z => f (z + s))

end shift_invariance

section sums

/-!
### Iterated derivatives of sums
-/
open Finset
variable {Î¹ : Type*} {n : â„•} {x : ğ•œ} {f : Î¹ â†’ ğ•œ â†’ F} {s : Finset Î¹}

lemma iteratedDeriv_sum {Î¹ : Type*} {n : â„•} {x : ğ•œ} {f : Î¹ â†’ ğ•œ â†’ F}
    {s : Finset Î¹} (hf : âˆ€ i âˆˆ s, ContDiffAt ğ•œ n (f i) x) :
    iteratedDeriv n (âˆ‘ i âˆˆ s, f i) x = âˆ‘ i âˆˆ s, iteratedDeriv n (f i) x := by
  classical
  induction s using Finset.induction_on with
  | empty => simp
  | insert i t hi IH =>
    rw [forall_mem_insert] at hf
    simp only [sum_insert hi, sum_fn] at IH âŠ¢
    rw [iteratedDeriv_add hf.1 (.sum hf.2), IH hf.2]

lemma iteratedDeriv_fun_sum (hf : âˆ€ i âˆˆ s, ContDiffAt ğ•œ n (f i) x) :
    iteratedDeriv n (fun z â†¦ âˆ‘ i âˆˆ s, f i z) x = âˆ‘ i âˆˆ s, iteratedDeriv n (f i) x :=
  by simpa [sum_fn] using iteratedDeriv_sum hf

end sums
