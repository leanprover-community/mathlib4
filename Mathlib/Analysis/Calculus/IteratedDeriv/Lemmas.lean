/-
Copyright (c) 2023 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck, Ruben Van de Velde
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# One-dimensional iterated derivatives

This file contains a number of further results on `iteratedDerivWithin` that need more imports
than are available in `Mathlib/Analysis/Calculus/IteratedDeriv/Defs.lean`.
-/

variable
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {R : Type*} [Semiring R] [Module R F] [SMulCommClass ùïú R F] [ContinuousConstSMul R F]
  {n : ‚Ñï} {x : ùïú} {s : Set ùïú} (hx : x ‚àà s) (h : UniqueDiffOn ùïú s) {f g : ùïú ‚Üí F}

theorem iteratedDerivWithin_add (hf : ContDiffOn ùïú n f s) (hg : ContDiffOn ùïú n g s) :
    iteratedDerivWithin n (f + g) s x =
      iteratedDerivWithin n f s x + iteratedDerivWithin n g s x := by
  simp_rw [iteratedDerivWithin, iteratedFDerivWithin_add_apply hf hg h hx,
    ContinuousMultilinearMap.add_apply]

theorem iteratedDerivWithin_congr (hfg : Set.EqOn f g s) :
    Set.EqOn (iteratedDerivWithin n f s) (iteratedDerivWithin n g s) s := by
  induction n generalizing f g with
  | zero => rwa [iteratedDerivWithin_zero]
  | succ n IH =>
    intro y hy
    have : UniqueDiffWithinAt ùïú s y := h.uniqueDiffWithinAt hy
    rw [iteratedDerivWithin_succ this, iteratedDerivWithin_succ this]
    exact derivWithin_congr (IH hfg) (IH hfg hy)

theorem iteratedDerivWithin_const_add (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c + f z) s x = iteratedDerivWithin n f s x := by
  obtain ‚ü®n, rfl‚ü© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ' h hx, iteratedDerivWithin_succ' h hx]
  refine iteratedDerivWithin_congr h ?_ hx
  intro y hy
  exact derivWithin_const_add (h.uniqueDiffWithinAt hy) _

theorem iteratedDerivWithin_const_neg (hn : 0 < n) (c : F) :
    iteratedDerivWithin n (fun z => c - f z) s x = iteratedDerivWithin n (fun z => -f z) s x := by
  obtain ‚ü®n, rfl‚ü© := n.exists_eq_succ_of_ne_zero hn.ne'
  rw [iteratedDerivWithin_succ' h hx, iteratedDerivWithin_succ' h hx]
  refine iteratedDerivWithin_congr h ?_ hx
  intro y hy
  have : UniqueDiffWithinAt ùïú s y := h.uniqueDiffWithinAt hy
  rw [derivWithin.neg this]
  exact derivWithin_const_sub this _

theorem iteratedDerivWithin_const_smul (c : R) (hf : ContDiffOn ùïú n f s) :
    iteratedDerivWithin n (c ‚Ä¢ f) s x = c ‚Ä¢ iteratedDerivWithin n f s x := by
  simp_rw [iteratedDerivWithin]
  rw [iteratedFDerivWithin_const_smul_apply hf h hx]
  simp only [ContinuousMultilinearMap.smul_apply]

theorem iteratedDerivWithin_const_mul (c : ùïú) {f : ùïú ‚Üí ùïú} (hf : ContDiffOn ùïú n f s) :
    iteratedDerivWithin n (fun z => c * f z) s x = c * iteratedDerivWithin n f s x := by
  simpa using iteratedDerivWithin_const_smul (F := ùïú) hx h c hf

theorem iteratedDerivWithin_neg (hf : ContDiffOn ùïú n f s) :
    iteratedDerivWithin n (-f) s x = -iteratedDerivWithin n f s x := by
  have := iteratedDerivWithin_const_smul hx h (-1) hf
  simpa only [neg_smul, one_smul]

theorem iteratedDerivWithin_neg' (hf : ContDiffOn ùïú n f s) :
    iteratedDerivWithin n (fun z => -f z) s x = -iteratedDerivWithin n f s x :=
  iteratedDerivWithin_neg hx h hf

theorem iteratedDerivWithin_sub (hf : ContDiffOn ùïú n f s) (hg : ContDiffOn ùïú n g s) :
    iteratedDerivWithin n (f - g) s x =
      iteratedDerivWithin n f s x - iteratedDerivWithin n g s x := by
  rw [sub_eq_add_neg, sub_eq_add_neg, Pi.neg_def, iteratedDerivWithin_add hx h hf hg.neg,
    iteratedDerivWithin_neg' hx h hg]

theorem iteratedDeriv_const_smul {n : ‚Ñï} {f : ùïú ‚Üí F} (h : ContDiff ùïú n f) (c : ùïú) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n ‚Ä¢ iteratedDeriv n f (c * x) := by
  induction n with
  | zero => simp
  | succ n ih =>
    funext x
    have h‚ÇÄ : DifferentiableAt ùïú (iteratedDeriv n f) (c * x) :=
      h.differentiable_iteratedDeriv n (Nat.cast_lt.mpr n.lt_succ_self) |>.differentiableAt
    have h‚ÇÅ : DifferentiableAt ùïú (fun x => iteratedDeriv n f (c * x)) x := by
      rw [‚Üê Function.comp_def]
      apply DifferentiableAt.comp
      ¬∑ exact h.differentiable_iteratedDeriv n (Nat.cast_lt.mpr n.lt_succ_self) |>.differentiableAt
      ¬∑ exact differentiableAt_id'.const_mul _
    rw [iteratedDeriv_succ, ih h.of_succ, deriv_const_smul _ h‚ÇÅ, iteratedDeriv_succ,
      ‚Üê Function.comp_def, deriv.scomp x h‚ÇÄ (differentiableAt_id'.const_mul _),
      deriv_const_mul _ differentiableAt_id', deriv_id'', smul_smul, mul_one, pow_succ']

theorem iteratedDeriv_const_mul {n : ‚Ñï} {f : ùïú ‚Üí ùïú} (h : ContDiff ùïú n f) (c : ùïú) :
    iteratedDeriv n (fun x => f (c * x)) = fun x => c ^ n * iteratedDeriv n f (c * x) := by
  simpa only [smul_eq_mul] using iteratedDeriv_const_smul h c
