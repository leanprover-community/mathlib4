/-
Copyright (c) 2020 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.Analysis.Calculus.Deriv.Inv

#align_import analysis.calculus.lhopital from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# L'HÃ´pital's rule for 0/0 indeterminate forms

In this file, we prove several forms of "L'HÃ´pital's rule" for computing 0/0
indeterminate forms. The proof of `HasDerivAt.lhopital_zero_right_on_Ioo`
is based on the one given in the corresponding
[Wikibooks](https://en.wikibooks.org/wiki/Calculus/L%27H%C3%B4pital%27s_Rule)
chapter, and all other statements are derived from this one by composing by
carefully chosen functions.

Note that the filter `f'/g'` tends to isn't required to be one of `ğ“ a`,
`atTop` or `atBot`. In fact, we give a slightly stronger statement by
allowing it to be any filter on `â„`.

Each statement is available in a `HasDerivAt` form and a `deriv` form, which
is denoted by each statement being in either the `HasDerivAt` or the `deriv`
namespace.

## Tags

L'HÃ´pital's rule, L'Hopital's rule
-/


open Filter Set

open scoped Filter Topology Pointwise

variable {a b : â„} (hab : a < b) {l : Filter â„} {f f' g g' : â„ â†’ â„}

/-!
## Interval-based versions

We start by proving statements where all conditions (derivability, `g' â‰  0`) have
to be satisfied on an explicitly-provided interval.
-/


namespace HasDerivAt

theorem lhopital_zero_right_on_Ioo (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x) (hg' : âˆ€ x âˆˆ Ioo a b, g' x â‰  0)
    (hfa : Tendsto f (ğ“[>] a) (ğ“ 0)) (hga : Tendsto g (ğ“[>] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  have sub : âˆ€ x âˆˆ Ioo a b, Ioo a x âŠ† Ioo a b := fun x hx =>
    Ioo_subset_Ioo (le_refl a) (le_of_lt hx.2)
  have hg : âˆ€ x âˆˆ Ioo a b, g x â‰  0 := by
    intro x hx h
    have : Tendsto g (ğ“[<] x) (ğ“ 0) := by
      rw [â† h, â† nhdsWithin_Ioo_eq_nhdsWithin_Iio hx.1]
      exact ((hgg' x hx).continuousAt.continuousWithinAt.mono <| sub x hx).tendsto
    obtain âŸ¨y, hyx, hyâŸ© : âˆƒ c âˆˆ Ioo a x, g' c = 0 :=
      exists_hasDerivAt_eq_zero' hx.1 hga this fun y hy => hgg' y <| sub x hx hy
    exact hg' y (sub x hx hyx) hy
  have : âˆ€ x âˆˆ Ioo a b, âˆƒ c âˆˆ Ioo a x, f x * g' c = g x * f' c := by
    intro x hx
    rw [â† sub_zero (f x), â† sub_zero (g x)]
    exact exists_ratio_hasDerivAt_eq_ratio_slope' g g' hx.1 f f' (fun y hy => hgg' y <| sub x hx hy)
      (fun y hy => hff' y <| sub x hx hy) hga hfa
      (tendsto_nhdsWithin_of_tendsto_nhds (hgg' x hx).continuousAt.tendsto)
      (tendsto_nhdsWithin_of_tendsto_nhds (hff' x hx).continuousAt.tendsto)
  choose! c hc using this
  have : âˆ€ x âˆˆ Ioo a b, ((fun x' => f' x' / g' x') âˆ˜ c) x = f x / g x := by
    intro x hx
    rcases hc x hx with âŸ¨hâ‚, hâ‚‚âŸ©
    field_simp [hg x hx, hg' (c x) ((sub x hx) hâ‚)]
    simp only [hâ‚‚]
    rw [mul_comm]
  have cmp : âˆ€ x âˆˆ Ioo a b, a < c x âˆ§ c x < x := fun x hx => (hc x hx).1
  rw [â† nhdsWithin_Ioo_eq_nhdsWithin_Ioi hab]
  apply tendsto_nhdsWithin_congr this
  apply hdiv.comp
  refine tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _
    (tendsto_of_tendsto_of_tendsto_of_le_of_le' tendsto_const_nhds
      (tendsto_nhdsWithin_of_tendsto_nhds tendsto_id) ?_ ?_) ?_
  all_goals
    apply eventually_nhdsWithin_of_forall
    intro x hx
    have := cmp x hx
    try simp
    linarith [this]
#align has_deriv_at.lhopital_zero_right_on_Ioo HasDerivAt.lhopital_zero_right_on_Ioo

theorem lhopital_zero_right_on_Ico (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x) (hcf : ContinuousOn f (Ico a b))
    (hcg : ContinuousOn g (Ico a b)) (hg' : âˆ€ x âˆˆ Ioo a b, g' x â‰  0) (hfa : f a = 0) (hga : g a = 0)
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  refine lhopital_zero_right_on_Ioo hab hff' hgg' hg' ?_ ?_ hdiv
  Â· rw [â† hfa, â† nhdsWithin_Ioo_eq_nhdsWithin_Ioi hab]
    exact ((hcf a <| left_mem_Ico.mpr hab).mono Ioo_subset_Ico_self).tendsto
  Â· rw [â† hga, â† nhdsWithin_Ioo_eq_nhdsWithin_Ioi hab]
    exact ((hcg a <| left_mem_Ico.mpr hab).mono Ioo_subset_Ico_self).tendsto
#align has_deriv_at.lhopital_zero_right_on_Ico HasDerivAt.lhopital_zero_right_on_Ico

theorem lhopital_zero_left_on_Ioo (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x) (hg' : âˆ€ x âˆˆ Ioo a b, g' x â‰  0)
    (hfb : Tendsto f (ğ“[<] b) (ğ“ 0)) (hgb : Tendsto g (ğ“[<] b) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[<] b) l) :
  Tendsto (fun x => f x / g x) (ğ“[<] b) l := by
  -- Here, we essentially compose by `Neg.neg`. The following is mostly technical details.
  have hdnf : âˆ€ x âˆˆ -Ioo a b, HasDerivAt (f âˆ˜ Neg.neg) (f' (-x) * -1) x := fun x hx =>
    comp x (hff' (-x) hx) (hasDerivAt_neg x)
  have hdng : âˆ€ x âˆˆ -Ioo a b, HasDerivAt (g âˆ˜ Neg.neg) (g' (-x) * -1) x := fun x hx =>
    comp x (hgg' (-x) hx) (hasDerivAt_neg x)
  rw [preimage_neg_Ioo] at hdnf
  rw [preimage_neg_Ioo] at hdng
  have := lhopital_zero_right_on_Ioo (neg_lt_neg hab) hdnf hdng (by
    intro x hx h
    apply hg' _ (by rw [â† preimage_neg_Ioo] at hx; exact hx)
    rwa [mul_comm, â† neg_eq_neg_one_mul, neg_eq_zero] at h)
    (hfb.comp tendsto_neg_nhdsWithin_Ioi_neg) (hgb.comp tendsto_neg_nhdsWithin_Ioi_neg)
    (by
      simp only [neg_div_neg_eq, mul_one, mul_neg]
      exact (tendsto_congr fun x => rfl).mp (hdiv.comp tendsto_neg_nhdsWithin_Ioi_neg))
  have := this.comp tendsto_neg_nhdsWithin_Iio
  unfold Function.comp at this
  simpa only [neg_neg]
#align has_deriv_at.lhopital_zero_left_on_Ioo HasDerivAt.lhopital_zero_left_on_Ioo

theorem lhopital_zero_left_on_Ioc (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x) (hcf : ContinuousOn f (Ioc a b))
    (hcg : ContinuousOn g (Ioc a b)) (hg' : âˆ€ x âˆˆ Ioo a b, g' x â‰  0) (hfb : f b = 0) (hgb : g b = 0)
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[<] b) l) :
    Tendsto (fun x => f x / g x) (ğ“[<] b) l := by
  refine lhopital_zero_left_on_Ioo hab hff' hgg' hg' ?_ ?_ hdiv
  Â· rw [â† hfb, â† nhdsWithin_Ioo_eq_nhdsWithin_Iio hab]
    exact ((hcf b <| right_mem_Ioc.mpr hab).mono Ioo_subset_Ioc_self).tendsto
  Â· rw [â† hgb, â† nhdsWithin_Ioo_eq_nhdsWithin_Iio hab]
    exact ((hcg b <| right_mem_Ioc.mpr hab).mono Ioo_subset_Ioc_self).tendsto
#align has_deriv_at.lhopital_zero_left_on_Ioc HasDerivAt.lhopital_zero_left_on_Ioc

theorem lhopital_zero_atTop_on_Ioi (hff' : âˆ€ x âˆˆ Ioi a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Ioi a, HasDerivAt g (g' x) x) (hg' : âˆ€ x âˆˆ Ioi a, g' x â‰  0)
    (hftop : Tendsto f atTop (ğ“ 0)) (hgtop : Tendsto g atTop (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) atTop l) : Tendsto (fun x => f x / g x) atTop l := by
  obtain âŸ¨a', haa', ha'âŸ© : âˆƒ a', a < a' âˆ§ 0 < a' := âŸ¨1 + max a 0,
    âŸ¨lt_of_le_of_lt (le_max_left a 0) (lt_one_add _),
      lt_of_le_of_lt (le_max_right a 0) (lt_one_add _)âŸ©âŸ©
  have fact1 : âˆ€ x : â„, x âˆˆ Ioo 0 a'â»Â¹ â†’ x â‰  0 := fun _ hx => (ne_of_lt hx.1).symm
  have fact2 : âˆ€ x âˆˆ Ioo 0 a'â»Â¹, a < xâ»Â¹ := fun _ hx => lt_trans haa' ((lt_inv ha' hx.1).mpr hx.2)
  have hdnf : âˆ€ x âˆˆ Ioo 0 a'â»Â¹, HasDerivAt (f âˆ˜ Inv.inv) (f' xâ»Â¹ * -(x ^ 2)â»Â¹) x := fun x hx =>
    comp x (hff' xâ»Â¹ <| fact2 x hx) (hasDerivAt_inv <| fact1 x hx)
  have hdng : âˆ€ x âˆˆ Ioo 0 a'â»Â¹, HasDerivAt (g âˆ˜ Inv.inv) (g' xâ»Â¹ * -(x ^ 2)â»Â¹) x := fun x hx =>
    comp x (hgg' xâ»Â¹ <| fact2 x hx) (hasDerivAt_inv <| fact1 x hx)
  have := lhopital_zero_right_on_Ioo (inv_pos.mpr ha') hdnf hdng
    (by
      intro x hx
      refine mul_ne_zero ?_ (neg_ne_zero.mpr <| inv_ne_zero <| pow_ne_zero _ <| fact1 x hx)
      exact hg' _ (fact2 x hx))
    (hftop.comp tendsto_inv_zero_atTop) (hgtop.comp tendsto_inv_zero_atTop)
    (by
      refine (tendsto_congr' ?_).mp (hdiv.comp tendsto_inv_zero_atTop)
      rw [eventuallyEq_iff_exists_mem]
      use Ioi 0, self_mem_nhdsWithin
      intro x hx
      unfold Function.comp
      simp only
      erw [mul_div_mul_right]
      exact neg_ne_zero.mpr (inv_ne_zero <| pow_ne_zero _ <| ne_of_gt hx))
  have := this.comp tendsto_inv_atTop_zero'
  unfold Function.comp at this
  simpa only [inv_inv]
#align has_deriv_at.lhopital_zero_at_top_on_Ioi HasDerivAt.lhopital_zero_atTop_on_Ioi

theorem lhopital_zero_atBot_on_Iio (hff' : âˆ€ x âˆˆ Iio a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€ x âˆˆ Iio a, HasDerivAt g (g' x) x) (hg' : âˆ€ x âˆˆ Iio a, g' x â‰  0)
    (hfbot : Tendsto f atBot (ğ“ 0)) (hgbot : Tendsto g atBot (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) atBot l) : Tendsto (fun x => f x / g x) atBot l := by
  -- Here, we essentially compose by `Neg.neg`. The following is mostly technical details.
  have hdnf : âˆ€ x âˆˆ -Iio a, HasDerivAt (f âˆ˜ Neg.neg) (f' (-x) * -1) x := fun x hx =>
    comp x (hff' (-x) hx) (hasDerivAt_neg x)
  have hdng : âˆ€ x âˆˆ -Iio a, HasDerivAt (g âˆ˜ Neg.neg) (g' (-x) * -1) x := fun x hx =>
    comp x (hgg' (-x) hx) (hasDerivAt_neg x)
  rw [preimage_neg_Iio] at hdnf
  rw [preimage_neg_Iio] at hdng
  have := lhopital_zero_atTop_on_Ioi hdnf hdng
    (by
      intro x hx h
      apply hg' _ (by rw [â† preimage_neg_Iio] at hx; exact hx)
      rwa [mul_comm, â† neg_eq_neg_one_mul, neg_eq_zero] at h)
    (hfbot.comp tendsto_neg_atTop_atBot) (hgbot.comp tendsto_neg_atTop_atBot)
    (by
      simp only [mul_one, mul_neg, neg_div_neg_eq]
      exact (tendsto_congr fun x => rfl).mp (hdiv.comp tendsto_neg_atTop_atBot))
  have := this.comp tendsto_neg_atBot_atTop
  unfold Function.comp at this
  simpa only [neg_neg]
#align has_deriv_at.lhopital_zero_at_bot_on_Iio HasDerivAt.lhopital_zero_atBot_on_Iio

end HasDerivAt

namespace deriv

theorem lhopital_zero_right_on_Ioo (hdf : DifferentiableOn â„ f (Ioo a b))
    (hg' : âˆ€ x âˆˆ Ioo a b, deriv g x â‰  0) (hfa : Tendsto f (ğ“[>] a) (ğ“ 0))
    (hga : Tendsto g (ğ“[>] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  have hdf : âˆ€ x âˆˆ Ioo a b, DifferentiableAt â„ f x := fun x hx =>
    (hdf x hx).differentiableAt (Ioo_mem_nhds hx.1 hx.2)
  have hdg : âˆ€ x âˆˆ Ioo a b, DifferentiableAt â„ g x := fun x hx =>
    by_contradiction fun h => hg' x hx (deriv_zero_of_not_differentiableAt h)
  exact HasDerivAt.lhopital_zero_right_on_Ioo hab (fun x hx => (hdf x hx).hasDerivAt)
    (fun x hx => (hdg x hx).hasDerivAt) hg' hfa hga hdiv
#align deriv.lhopital_zero_right_on_Ioo deriv.lhopital_zero_right_on_Ioo

theorem lhopital_zero_right_on_Ico (hdf : DifferentiableOn â„ f (Ioo a b))
    (hcf : ContinuousOn f (Ico a b)) (hcg : ContinuousOn g (Ico a b))
    (hg' : âˆ€ x âˆˆ Ioo a b, (deriv g) x â‰  0) (hfa : f a = 0) (hga : g a = 0)
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  refine lhopital_zero_right_on_Ioo hab hdf hg' ?_ ?_ hdiv
  Â· rw [â† hfa, â† nhdsWithin_Ioo_eq_nhdsWithin_Ioi hab]
    exact ((hcf a <| left_mem_Ico.mpr hab).mono Ioo_subset_Ico_self).tendsto
  Â· rw [â† hga, â† nhdsWithin_Ioo_eq_nhdsWithin_Ioi hab]
    exact ((hcg a <| left_mem_Ico.mpr hab).mono Ioo_subset_Ico_self).tendsto
#align deriv.lhopital_zero_right_on_Ico deriv.lhopital_zero_right_on_Ico

theorem lhopital_zero_left_on_Ioo (hdf : DifferentiableOn â„ f (Ioo a b))
    (hg' : âˆ€ x âˆˆ Ioo a b, (deriv g) x â‰  0) (hfb : Tendsto f (ğ“[<] b) (ğ“ 0))
    (hgb : Tendsto g (ğ“[<] b) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[<] b) l) :
    Tendsto (fun x => f x / g x) (ğ“[<] b) l := by
  have hdf : âˆ€ x âˆˆ Ioo a b, DifferentiableAt â„ f x := fun x hx =>
    (hdf x hx).differentiableAt (Ioo_mem_nhds hx.1 hx.2)
  have hdg : âˆ€ x âˆˆ Ioo a b, DifferentiableAt â„ g x := fun x hx =>
    by_contradiction fun h => hg' x hx (deriv_zero_of_not_differentiableAt h)
  exact HasDerivAt.lhopital_zero_left_on_Ioo hab (fun x hx => (hdf x hx).hasDerivAt)
    (fun x hx => (hdg x hx).hasDerivAt) hg' hfb hgb hdiv
#align deriv.lhopital_zero_left_on_Ioo deriv.lhopital_zero_left_on_Ioo

theorem lhopital_zero_atTop_on_Ioi (hdf : DifferentiableOn â„ f (Ioi a))
    (hg' : âˆ€ x âˆˆ Ioi a, (deriv g) x â‰  0) (hftop : Tendsto f atTop (ğ“ 0))
    (hgtop : Tendsto g atTop (ğ“ 0)) (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) atTop l) :
    Tendsto (fun x => f x / g x) atTop l := by
  have hdf : âˆ€ x âˆˆ Ioi a, DifferentiableAt â„ f x := fun x hx =>
    (hdf x hx).differentiableAt (Ioi_mem_nhds hx)
  have hdg : âˆ€ x âˆˆ Ioi a, DifferentiableAt â„ g x := fun x hx =>
    by_contradiction fun h => hg' x hx (deriv_zero_of_not_differentiableAt h)
  exact HasDerivAt.lhopital_zero_atTop_on_Ioi (fun x hx => (hdf x hx).hasDerivAt)
    (fun x hx => (hdg x hx).hasDerivAt) hg' hftop hgtop hdiv
#align deriv.lhopital_zero_at_top_on_Ioi deriv.lhopital_zero_atTop_on_Ioi

theorem lhopital_zero_atBot_on_Iio (hdf : DifferentiableOn â„ f (Iio a))
    (hg' : âˆ€ x âˆˆ Iio a, (deriv g) x â‰  0) (hfbot : Tendsto f atBot (ğ“ 0))
    (hgbot : Tendsto g atBot (ğ“ 0)) (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) atBot l) :
    Tendsto (fun x => f x / g x) atBot l := by
  have hdf : âˆ€ x âˆˆ Iio a, DifferentiableAt â„ f x := fun x hx =>
    (hdf x hx).differentiableAt (Iio_mem_nhds hx)
  have hdg : âˆ€ x âˆˆ Iio a, DifferentiableAt â„ g x := fun x hx =>
    by_contradiction fun h => hg' x hx (deriv_zero_of_not_differentiableAt h)
  exact HasDerivAt.lhopital_zero_atBot_on_Iio (fun x hx => (hdf x hx).hasDerivAt)
    (fun x hx => (hdg x hx).hasDerivAt) hg' hfbot hgbot hdiv
#align deriv.lhopital_zero_at_bot_on_Iio deriv.lhopital_zero_atBot_on_Iio

end deriv

/-!
## Generic versions

The following statements no longer any explicit interval, as they only require
conditions holding eventually.
-/


namespace HasDerivAt

/-- L'HÃ´pital's rule for approaching a real from the right, `HasDerivAt` version -/
theorem lhopital_zero_nhds_right (hff' : âˆ€á¶  x in ğ“[>] a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in ğ“[>] a, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in ğ“[>] a, g' x â‰  0)
    (hfa : Tendsto f (ğ“[>] a) (ğ“ 0)) (hga : Tendsto g (ğ“[>] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  rw [eventually_iff_exists_mem] at *
  rcases hff' with âŸ¨sâ‚, hsâ‚, hff'âŸ©
  rcases hgg' with âŸ¨sâ‚‚, hsâ‚‚, hgg'âŸ©
  rcases hg' with âŸ¨sâ‚ƒ, hsâ‚ƒ, hg'âŸ©
  let s := sâ‚ âˆ© sâ‚‚ âˆ© sâ‚ƒ
  have hs : s âˆˆ ğ“[>] a := inter_mem (inter_mem hsâ‚ hsâ‚‚) hsâ‚ƒ
  rw [mem_nhdsWithin_Ioi_iff_exists_Ioo_subset] at hs
  rcases hs with âŸ¨u, hau, huâŸ©
  refine lhopital_zero_right_on_Ioo hau ?_ ?_ ?_ hfa hga hdiv <;>
    intro x hx <;> apply_assumption <;>
    first | exact (hu hx).1.1 | exact (hu hx).1.2 | exact (hu hx).2
#align has_deriv_at.lhopital_zero_nhds_right HasDerivAt.lhopital_zero_nhds_right

/-- L'HÃ´pital's rule for approaching a real from the left, `HasDerivAt` version -/
theorem lhopital_zero_nhds_left (hff' : âˆ€á¶  x in ğ“[<] a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in ğ“[<] a, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in ğ“[<] a, g' x â‰  0)
    (hfa : Tendsto f (ğ“[<] a) (ğ“ 0)) (hga : Tendsto g (ğ“[<] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[<] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[<] a) l := by
  rw [eventually_iff_exists_mem] at *
  rcases hff' with âŸ¨sâ‚, hsâ‚, hff'âŸ©
  rcases hgg' with âŸ¨sâ‚‚, hsâ‚‚, hgg'âŸ©
  rcases hg' with âŸ¨sâ‚ƒ, hsâ‚ƒ, hg'âŸ©
  let s := sâ‚ âˆ© sâ‚‚ âˆ© sâ‚ƒ
  have hs : s âˆˆ ğ“[<] a := inter_mem (inter_mem hsâ‚ hsâ‚‚) hsâ‚ƒ
  rw [mem_nhdsWithin_Iio_iff_exists_Ioo_subset] at hs
  rcases hs with âŸ¨l, hal, hlâŸ©
  refine lhopital_zero_left_on_Ioo hal ?_ ?_ ?_ hfa hga hdiv <;> intro x hx <;> apply_assumption <;>
    first | exact (hl hx).1.1| exact (hl hx).1.2| exact (hl hx).2
#align has_deriv_at.lhopital_zero_nhds_left HasDerivAt.lhopital_zero_nhds_left

/-- L'HÃ´pital's rule for approaching a real, `HasDerivAt` version. This
  does not require anything about the situation at `a` -/
theorem lhopital_zero_nhds' (hff' : âˆ€á¶  x in ğ“[â‰ ] a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in ğ“[â‰ ] a, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in ğ“[â‰ ] a, g' x â‰  0)
    (hfa : Tendsto f (ğ“[â‰ ] a) (ğ“ 0)) (hga : Tendsto g (ğ“[â‰ ] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“[â‰ ] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[â‰ ] a) l := by
  simp only [â† Iio_union_Ioi, nhdsWithin_union, tendsto_sup, eventually_sup] at *
  exact âŸ¨lhopital_zero_nhds_left hff'.1 hgg'.1 hg'.1 hfa.1 hga.1 hdiv.1,
    lhopital_zero_nhds_right hff'.2 hgg'.2 hg'.2 hfa.2 hga.2 hdiv.2âŸ©
#align has_deriv_at.lhopital_zero_nhds' HasDerivAt.lhopital_zero_nhds'

/-- **L'HÃ´pital's rule** for approaching a real, `HasDerivAt` version -/
theorem lhopital_zero_nhds (hff' : âˆ€á¶  x in ğ“ a, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in ğ“ a, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in ğ“ a, g' x â‰  0)
    (hfa : Tendsto f (ğ“ a) (ğ“ 0)) (hga : Tendsto g (ğ“ a) (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) (ğ“ a) l) : Tendsto (fun x => f x / g x) (ğ“[â‰ ] a) l := by
  apply @lhopital_zero_nhds' _ _ _ f' _ g' <;>
    (first | apply eventually_nhdsWithin_of_eventually_nhds |
      apply tendsto_nhdsWithin_of_tendsto_nhds) <;> assumption
#align has_deriv_at.lhopital_zero_nhds HasDerivAt.lhopital_zero_nhds

/-- L'HÃ´pital's rule for approaching +âˆ, `HasDerivAt` version -/
theorem lhopital_zero_atTop (hff' : âˆ€á¶  x in atTop, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in atTop, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in atTop, g' x â‰  0)
    (hftop : Tendsto f atTop (ğ“ 0)) (hgtop : Tendsto g atTop (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) atTop l) : Tendsto (fun x => f x / g x) atTop l := by
  rw [eventually_iff_exists_mem] at *
  rcases hff' with âŸ¨sâ‚, hsâ‚, hff'âŸ©
  rcases hgg' with âŸ¨sâ‚‚, hsâ‚‚, hgg'âŸ©
  rcases hg' with âŸ¨sâ‚ƒ, hsâ‚ƒ, hg'âŸ©
  let s := sâ‚ âˆ© sâ‚‚ âˆ© sâ‚ƒ
  have hs : s âˆˆ atTop := inter_mem (inter_mem hsâ‚ hsâ‚‚) hsâ‚ƒ
  rw [mem_atTop_sets] at hs
  rcases hs with âŸ¨l, hlâŸ©
  have hl' : Ioi l âŠ† s := fun x hx => hl x (le_of_lt hx)
  refine lhopital_zero_atTop_on_Ioi ?_ ?_ (fun x hx => hg' x <| (hl' hx).2) hftop hgtop hdiv <;>
    intro x hx <;> apply_assumption <;> first | exact (hl' hx).1.1| exact (hl' hx).1.2
#align has_deriv_at.lhopital_zero_at_top HasDerivAt.lhopital_zero_atTop

/-- L'HÃ´pital's rule for approaching -âˆ, `HasDerivAt` version -/
theorem lhopital_zero_atBot (hff' : âˆ€á¶  x in atBot, HasDerivAt f (f' x) x)
    (hgg' : âˆ€á¶  x in atBot, HasDerivAt g (g' x) x) (hg' : âˆ€á¶  x in atBot, g' x â‰  0)
    (hfbot : Tendsto f atBot (ğ“ 0)) (hgbot : Tendsto g atBot (ğ“ 0))
    (hdiv : Tendsto (fun x => f' x / g' x) atBot l) : Tendsto (fun x => f x / g x) atBot l := by
  rw [eventually_iff_exists_mem] at *
  rcases hff' with âŸ¨sâ‚, hsâ‚, hff'âŸ©
  rcases hgg' with âŸ¨sâ‚‚, hsâ‚‚, hgg'âŸ©
  rcases hg' with âŸ¨sâ‚ƒ, hsâ‚ƒ, hg'âŸ©
  let s := sâ‚ âˆ© sâ‚‚ âˆ© sâ‚ƒ
  have hs : s âˆˆ atBot := inter_mem (inter_mem hsâ‚ hsâ‚‚) hsâ‚ƒ
  rw [mem_atBot_sets] at hs
  rcases hs with âŸ¨l, hlâŸ©
  have hl' : Iio l âŠ† s := fun x hx => hl x (le_of_lt hx)
  refine lhopital_zero_atBot_on_Iio ?_ ?_ (fun x hx => hg' x <| (hl' hx).2) hfbot hgbot hdiv <;>
    intro x hx <;> apply_assumption <;> first | exact (hl' hx).1.1| exact (hl' hx).1.2
#align has_deriv_at.lhopital_zero_at_bot HasDerivAt.lhopital_zero_atBot

end HasDerivAt

namespace deriv

/-- **L'HÃ´pital's rule** for approaching a real from the right, `deriv` version -/
theorem lhopital_zero_nhds_right (hdf : âˆ€á¶  x in ğ“[>] a, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x in ğ“[>] a, deriv g x â‰  0) (hfa : Tendsto f (ğ“[>] a) (ğ“ 0))
    (hga : Tendsto g (ğ“[>] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[>] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[>] a) l := by
  have hdg : âˆ€á¶  x in ğ“[>] a, DifferentiableAt â„ g x :=
    hg'.mp (eventually_of_forall fun _ hg' =>
      by_contradiction fun h => hg' (deriv_zero_of_not_differentiableAt h))
  have hdf' : âˆ€á¶  x in ğ“[>] a, HasDerivAt f (deriv f x) x :=
    hdf.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  have hdg' : âˆ€á¶  x in ğ“[>] a, HasDerivAt g (deriv g x) x :=
    hdg.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  exact HasDerivAt.lhopital_zero_nhds_right hdf' hdg' hg' hfa hga hdiv
#align deriv.lhopital_zero_nhds_right deriv.lhopital_zero_nhds_right

/-- **L'HÃ´pital's rule** for approaching a real from the left, `deriv` version -/
theorem lhopital_zero_nhds_left (hdf : âˆ€á¶  x in ğ“[<] a, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x in ğ“[<] a, deriv g x â‰  0) (hfa : Tendsto f (ğ“[<] a) (ğ“ 0))
    (hga : Tendsto g (ğ“[<] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[<] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[<] a) l := by
  have hdg : âˆ€á¶  x in ğ“[<] a, DifferentiableAt â„ g x :=
    hg'.mp (eventually_of_forall fun _ hg' =>
      by_contradiction fun h => hg' (deriv_zero_of_not_differentiableAt h))
  have hdf' : âˆ€á¶  x in ğ“[<] a, HasDerivAt f (deriv f x) x :=
    hdf.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  have hdg' : âˆ€á¶  x in ğ“[<] a, HasDerivAt g (deriv g x) x :=
    hdg.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  exact HasDerivAt.lhopital_zero_nhds_left hdf' hdg' hg' hfa hga hdiv
#align deriv.lhopital_zero_nhds_left deriv.lhopital_zero_nhds_left

/-- **L'HÃ´pital's rule** for approaching a real, `deriv` version. This
  does not require anything about the situation at `a` -/
theorem lhopital_zero_nhds' (hdf : âˆ€á¶  x in ğ“[â‰ ] a, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x in ğ“[â‰ ] a, deriv g x â‰  0) (hfa : Tendsto f (ğ“[â‰ ] a) (ğ“ 0))
    (hga : Tendsto g (ğ“[â‰ ] a) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“[â‰ ] a) l) :
    Tendsto (fun x => f x / g x) (ğ“[â‰ ] a) l := by
  simp only [â† Iio_union_Ioi, nhdsWithin_union, tendsto_sup, eventually_sup] at *
  exact âŸ¨lhopital_zero_nhds_left hdf.1 hg'.1 hfa.1 hga.1 hdiv.1,
    lhopital_zero_nhds_right hdf.2 hg'.2 hfa.2 hga.2 hdiv.2âŸ©
#align deriv.lhopital_zero_nhds' deriv.lhopital_zero_nhds'

/-- **L'HÃ´pital's rule** for approaching a real, `deriv` version -/
theorem lhopital_zero_nhds (hdf : âˆ€á¶  x in ğ“ a, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x in ğ“ a, deriv g x â‰  0) (hfa : Tendsto f (ğ“ a) (ğ“ 0)) (hga : Tendsto g (ğ“ a) (ğ“ 0))
    (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) (ğ“ a) l) :
    Tendsto (fun x => f x / g x) (ğ“[â‰ ] a) l := by
  apply lhopital_zero_nhds' <;>
    (first | apply eventually_nhdsWithin_of_eventually_nhds |
      apply tendsto_nhdsWithin_of_tendsto_nhds) <;> assumption
#align deriv.lhopital_zero_nhds deriv.lhopital_zero_nhds

/-- **L'HÃ´pital's rule** for approaching +âˆ, `deriv` version -/
theorem lhopital_zero_atTop (hdf : âˆ€á¶  x : â„ in atTop, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x : â„ in atTop, deriv g x â‰  0) (hftop : Tendsto f atTop (ğ“ 0))
    (hgtop : Tendsto g atTop (ğ“ 0)) (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) atTop l) :
    Tendsto (fun x => f x / g x) atTop l := by
  have hdg : âˆ€á¶  x in atTop, DifferentiableAt â„ g x := hg'.mp
    (eventually_of_forall fun _ hg' =>
      by_contradiction fun h => hg' (deriv_zero_of_not_differentiableAt h))
  have hdf' : âˆ€á¶  x in atTop, HasDerivAt f (deriv f x) x :=
    hdf.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  have hdg' : âˆ€á¶  x in atTop, HasDerivAt g (deriv g x) x :=
    hdg.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  exact HasDerivAt.lhopital_zero_atTop hdf' hdg' hg' hftop hgtop hdiv
#align deriv.lhopital_zero_at_top deriv.lhopital_zero_atTop

/-- **L'HÃ´pital's rule** for approaching -âˆ, `deriv` version -/
theorem lhopital_zero_atBot (hdf : âˆ€á¶  x : â„ in atBot, DifferentiableAt â„ f x)
    (hg' : âˆ€á¶  x : â„ in atBot, deriv g x â‰  0) (hfbot : Tendsto f atBot (ğ“ 0))
    (hgbot : Tendsto g atBot (ğ“ 0)) (hdiv : Tendsto (fun x => (deriv f) x / (deriv g) x) atBot l) :
    Tendsto (fun x => f x / g x) atBot l := by
  have hdg : âˆ€á¶  x in atBot, DifferentiableAt â„ g x :=
    hg'.mp (eventually_of_forall fun _ hg' =>
      by_contradiction fun h => hg' (deriv_zero_of_not_differentiableAt h))
  have hdf' : âˆ€á¶  x in atBot, HasDerivAt f (deriv f x) x :=
    hdf.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  have hdg' : âˆ€á¶  x in atBot, HasDerivAt g (deriv g x) x :=
    hdg.mp (eventually_of_forall fun _ => DifferentiableAt.hasDerivAt)
  exact HasDerivAt.lhopital_zero_atBot hdf' hdg' hg' hfbot hgbot hdiv
#align deriv.lhopital_zero_at_bot deriv.lhopital_zero_atBot

end deriv
