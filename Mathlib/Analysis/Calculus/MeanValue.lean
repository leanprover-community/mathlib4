/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.AffineMap
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.LocalExtr.Rolle
import Mathlib.Analysis.Convex.Normed
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Topology.LocallyConstant.Basic

/-!
# The mean value inequality and equalities

In this file we prove the following facts:

* `Convex.norm_image_sub_le_of_norm_deriv_le` : if `f` is differentiable on a convex set `s`
  and the norm of its derivative is bounded by `C`, then `f` is Lipschitz continuous on `s` with
  constant `C`; also a variant in which what is bounded by `C` is the norm of the difference of the
  derivative from a fixed linear map. This lemma and its versions are formulated using `RCLike`,
  so they work both for real and complex derivatives.

* `image_le_of*`, `image_norm_le_of_*` : several similar lemmas deducing `f x â‰¤ B x` or
  `â€–f xâ€– â‰¤ B x` from upper estimates on `f'` or `â€–f'â€–`, respectively. These lemmas differ by
  their assumptions:

  * `of_liminf_*` lemmas assume that limit inferior of some ratio is less than `B' x`;
  * `of_deriv_right_*`, `of_norm_deriv_right_*` lemmas assume that the right derivative
    or its norm is less than `B' x`;
  * `of_*_lt_*` lemmas assume a strict inequality whenever `f x = B x` or `â€–f xâ€– = B x`;
  * `of_*_le_*` lemmas assume a non-strict inequality everywhere on `[a, b)`;
  * name of a lemma ends with `'` if (1) it assumes that `B` is continuous on `[a, b]`
    and has a right derivative at every point of `[a, b)`, and (2) the lemma has
    a counterpart assuming that `B` is differentiable everywhere on `â„`

* `norm_image_sub_le_*_segment` : if derivative of `f` on `[a, b]` is bounded above
  by a constant `C`, then `â€–f x - f aâ€– â‰¤ C * â€–x - aâ€–`; several versions deal with
  right derivative and derivative within `[a, b]` (`HasDerivWithinAt` or `derivWithin`).

* `Convex.is_const_of_fderivWithin_eq_zero` : if a function has derivative `0` on a convex set `s`,
  then it is a constant on `s`.

* `exists_ratio_hasDerivAt_eq_ratio_slope` and `exists_ratio_deriv_eq_ratio_slope` :
  Cauchy's Mean Value Theorem.

* `exists_hasDerivAt_eq_slope` and `exists_deriv_eq_slope` : Lagrange's Mean Value Theorem.

* `domain_mvt` : Lagrange's Mean Value Theorem, applied to a segment in a convex domain.

* `Convex.image_sub_lt_mul_sub_of_deriv_lt`, `Convex.mul_sub_lt_image_sub_of_lt_deriv`,
  `Convex.image_sub_le_mul_sub_of_deriv_le`, `Convex.mul_sub_le_image_sub_of_le_deriv`,
  if `âˆ€ x, C (</â‰¤/>/â‰¥) (f' x)`, then `C * (y - x) (</â‰¤/>/â‰¥) (f y - f x)` whenever `x < y`.

* `monotoneOn_of_deriv_nonneg`, `antitoneOn_of_deriv_nonpos`,
  `strictMono_of_deriv_pos`, `strictAnti_of_deriv_neg` :
  if the derivative of a function is non-negative/non-positive/positive/negative, then
  the function is monotone/antitone/strictly monotone/strictly monotonically
  decreasing.

* `convexOn_of_deriv`, `convexOn_of_deriv2_nonneg` : if the derivative of a function
  is increasing or its second derivative is nonnegative, then the original function is convex.

* `hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt` : a C^1 function over the reals is
  strictly differentiable. (This is a corollary of the mean value inequality.)
-/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

open Metric Set Asymptotics ContinuousLinearMap Filter

open scoped Topology NNReal

/-! ### One-dimensional fencing inequalities -/


/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x âˆˆ [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary' {f f' : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) â‰¤ f' x`
    (hf' : âˆ€ x âˆˆ Ico a b, âˆ€ r, f' x < r â†’ âˆƒá¶  z in ğ“[>] x, slope f x z < r)
    {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, f x = B x â†’ f' x < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x := by
  change Icc a b âŠ† { x | f x â‰¤ B x }
  set s := { x | f x â‰¤ B x } âˆ© Icc a b
  have A : ContinuousOn (fun x => (f x, B x)) (Icc a b) := hf.prod hB
  have : IsClosed s := by
    simp only [s, inter_comm]
    exact A.preimage_isClosed_of_isClosed isClosed_Icc OrderClosedTopology.isClosed_le'
  apply this.Icc_subset_of_forall_exists_gt ha
  rintro x âŸ¨hxB : f x â‰¤ B x, xabâŸ© y hy
  rcases hxB.lt_or_eq with hxB | hxB
  Â· -- If `f x < B x`, then all we need is continuity of both sides
    refine nonempty_of_mem (inter_mem ?_ (Ioc_mem_nhdsGT hy))
    have : âˆ€á¶  x in ğ“[Icc a b] x, f x < B x :=
      A x (Ico_subset_Icc_self xab) (IsOpen.mem_nhds (isOpen_lt continuous_fst continuous_snd) hxB)
    have : âˆ€á¶  x in ğ“[>] x, f x < B x := nhdsWithin_le_of_mem (Icc_mem_nhdsGT_of_mem xab) this
    exact this.mono fun y => le_of_lt
  Â· rcases exists_between (bound x xab hxB) with âŸ¨r, hfr, hrBâŸ©
    specialize hf' x xab r hfr
    have HB : âˆ€á¶  z in ğ“[>] x, r < slope B x z :=
      (hasDerivWithinAt_iff_tendsto_slope' <| lt_irrefl x).1 (hB' x xab).Ioi_of_Ici
        (Ioi_mem_nhds hrB)
    obtain âŸ¨z, hfz, hzB, hzâŸ© : âˆƒ z, slope f x z < r âˆ§ r < slope B x z âˆ§ z âˆˆ Ioc x y :=
      hf'.and_eventually (HB.and (Ioc_mem_nhdsGT hy)) |>.exists
    refine âŸ¨z, ?_, hzâŸ©
    have := (hfz.trans hzB).le
    rwa [slope_def_field, slope_def_field, div_le_div_iff_of_pos_right (sub_pos.2 hz.1), hxB,
      sub_le_sub_iff_right] at this

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has derivative `B'` everywhere on `â„`;
* for each `x âˆˆ [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary {f f' : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) â‰¤ f' x`
    (hf' : âˆ€ x âˆˆ Ico a b, âˆ€ r, f' x < r â†’ âˆƒá¶  z in ğ“[>] x, slope f x z < r)
    {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : âˆ€ x, HasDerivAt B (B' x) x)
    (bound : âˆ€ x âˆˆ Ico a b, f x = B x â†’ f' x < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x âˆˆ [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by `B'`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_le_deriv_boundary {f : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b)) {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    -- `bound` actually says `liminf (f z - f x) / (z - x) â‰¤ B' x`
    (bound : âˆ€ x âˆˆ Ico a b, âˆ€ r, B' x < r â†’ âˆƒá¶  z in ğ“[>] x, slope f x z < r) :
    âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x := by
  have Hr : âˆ€ x âˆˆ Icc a b, âˆ€ r > 0, f x â‰¤ B x + r * (x - a) := fun x hx r hr => by
    apply image_le_of_liminf_slope_right_lt_deriv_boundary' hf bound
    Â· rwa [sub_self, mul_zero, add_zero]
    Â· exact hB.add (continuousOn_const.mul (continuousOn_id.sub continuousOn_const))
    Â· intro x hx
      exact (hB' x hx).add (((hasDerivWithinAt_id x (Ici x)).sub_const a).const_mul r)
    Â· intro x _ _
      rw [mul_one]
      exact (lt_add_iff_pos_right _).2 hr
    exact hx
  intro x hx
  have : ContinuousWithinAt (fun r => B x + r * (x - a)) (Ioi 0) 0 :=
    continuousWithinAt_const.add (continuousWithinAt_id.mul continuousWithinAt_const)
  convert continuousWithinAt_const.closure_le _ this (Hr x hx) using 1 <;> simp

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary' {f f' : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, f x = B x â†’ f' x < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has derivative `B'` everywhere on `â„`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary {f f' : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : âˆ€ x, HasDerivAt B (B' x) x)
    (bound : âˆ€ x âˆˆ Ico a b, f x = B x â†’ f' x < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x :=
  image_le_of_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a â‰¤ B a`;
* `B` has derivative `B'` everywhere on `â„`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x â‰¤ B' x` on `[a, b)`.

Then `f x â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_le_deriv_boundary {f f' : â„ â†’ â„} {a b : â„}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : f a â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, f' x â‰¤ B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ f x â‰¤ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary hf ha hB hB' fun x hx _ hr =>
    (hf' x hx).liminf_right_slope_le (lt_of_le_of_lt (bound x hx) hr)

/-! ### Vector-valued functions `f : â„ â†’ E` -/


section

variable {f : â„ â†’ E} {a b : â„}

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `â€–f aâ€– â‰¤ B a`;
* `B` has right derivative at every point of `[a, b)`;
* for each `x âˆˆ [a, b)` the right-side limit inferior of `(â€–f zâ€– - â€–f xâ€–) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `â€–f xâ€– = B x`.

Then `â€–f xâ€– â‰¤ B x` everywhere on `[a, b]`. -/
theorem image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary {E : Type*}
    [NormedAddCommGroup E] {f : â„ â†’ E} {f' : â„ â†’ â„} (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (â€–f zâ€– - â€–f xâ€–) / (z - x) â‰¤ f' x`
    (hf' : âˆ€ x âˆˆ Ico a b, âˆ€ r, f' x < r â†’ âˆƒá¶  z in ğ“[>] x, slope (norm âˆ˜ f) x z < r)
    {B B' : â„ â†’ â„} (ha : â€–f aâ€– â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f xâ€– = B x â†’ f' x < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ â€–f xâ€– â‰¤ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' (continuous_norm.comp_continuousOn hf) hf' ha hB
    hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `â€–f aâ€– â‰¤ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* the norm of `f'` is strictly less than `B'` whenever `â€–f xâ€– = B x`.

Then `â€–f xâ€– â‰¤ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary' {f' : â„ â†’ E}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : â€–f aâ€– â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f xâ€– = B x â†’ â€–f' xâ€– < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ â€–f xâ€– â‰¤ B x :=
  image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `â€–f aâ€– â‰¤ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `â„`;
* the norm of `f'` is strictly less than `B'` whenever `â€–f xâ€– = B x`.

Then `â€–f xâ€– â‰¤ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary {f' : â„ â†’ E}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : â€–f aâ€– â‰¤ B a) (hB : âˆ€ x, HasDerivAt B (B' x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f xâ€– = B x â†’ â€–f' xâ€– < B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ â€–f xâ€– â‰¤ B x :=
  image_norm_le_of_norm_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `â€–f aâ€– â‰¤ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* we have `â€–f' xâ€– â‰¤ B x` everywhere on `[a, b)`.

Then `â€–f xâ€– â‰¤ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary' {f' : â„ â†’ E}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : â€–f aâ€– â‰¤ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f' xâ€– â‰¤ B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ â€–f xâ€– â‰¤ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary (continuous_norm.comp_continuousOn hf) ha hB hB'
    fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le ((bound x hx).trans_lt hr)

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `â€–f aâ€– â‰¤ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `â„`;
* we have `â€–f' xâ€– â‰¤ B x` everywhere on `[a, b)`.

Then `â€–f xâ€– â‰¤ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary {f' : â„ â†’ E}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : â„ â†’ â„} (ha : â€–f aâ€– â‰¤ B a) (hB : âˆ€ x, HasDerivAt B (B' x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f' xâ€– â‰¤ B' x) : âˆ€ â¦ƒxâ¦„, x âˆˆ Icc a b â†’ â€–f xâ€– â‰¤ B x :=
  image_norm_le_of_norm_deriv_right_le_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- A function on `[a, b]` with the norm of the right derivative bounded by `C`
satisfies `â€–f x - f aâ€– â‰¤ C * (x - a)`. -/
theorem norm_image_sub_le_of_norm_deriv_right_le_segment {f' : â„ â†’ E} {C : â„}
    (hf : ContinuousOn f (Icc a b)) (hf' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f' xâ€– â‰¤ C) : âˆ€ x âˆˆ Icc a b, â€–f x - f aâ€– â‰¤ C * (x - a) := by
  let g x := f x - f a
  have hg : ContinuousOn g (Icc a b) := hf.sub continuousOn_const
  have hg' : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt g (f' x) (Ici x) x := by
    intro x hx
    simpa using (hf' x hx).sub (hasDerivWithinAt_const _ _ _)
  let B x := C * (x - a)
  have hB : âˆ€ x, HasDerivAt B C x := by
    intro x
    simpa using (hasDerivAt_const x C).mul ((hasDerivAt_id x).sub (hasDerivAt_const x a))
  convert image_norm_le_of_norm_deriv_right_le_deriv_boundary hg hg' _ hB bound
  simp only [g, B]; rw [sub_self, norm_zero, sub_self, mul_zero]

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `â€–f x - f aâ€– â‰¤ C * (x - a)`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment' {f' : â„ â†’ E} {C : â„}
    (hf : âˆ€ x âˆˆ Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x)
    (bound : âˆ€ x âˆˆ Ico a b, â€–f' xâ€– â‰¤ C) : âˆ€ x âˆˆ Icc a b, â€–f x - f aâ€– â‰¤ C * (x - a) := by
  refine
    norm_image_sub_le_of_norm_deriv_right_le_segment (fun x hx => (hf x hx).continuousWithinAt)
      (fun x hx => ?_) bound
  exact (hf x <| Ico_subset_Icc_self hx).mono_of_mem_nhdsWithin (Icc_mem_nhdsGE_of_mem hx)

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `â€–f x - f aâ€– â‰¤ C * (x - a)`, `derivWithin`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment {C : â„} (hf : DifferentiableOn â„ f (Icc a b))
    (bound : âˆ€ x âˆˆ Ico a b, â€–derivWithin f (Icc a b) xâ€– â‰¤ C) :
    âˆ€ x âˆˆ Icc a b, â€–f x - f aâ€– â‰¤ C * (x - a) := by
  refine norm_image_sub_le_of_norm_deriv_le_segment' ?_ bound
  exact fun x hx => (hf x hx).hasDerivWithinAt

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `â€–f 1 - f 0â€– â‰¤ C`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01' {f' : â„ â†’ E} {C : â„}
    (hf : âˆ€ x âˆˆ Icc (0 : â„) 1, HasDerivWithinAt f (f' x) (Icc (0 : â„) 1) x)
    (bound : âˆ€ x âˆˆ Ico (0 : â„) 1, â€–f' xâ€– â‰¤ C) : â€–f 1 - f 0â€– â‰¤ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment' hf bound 1 (right_mem_Icc.2 zero_le_one)

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `â€–f 1 - f 0â€– â‰¤ C`, `derivWithin` version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01 {C : â„}
    (hf : DifferentiableOn â„ f (Icc (0 : â„) 1))
    (bound : âˆ€ x âˆˆ Ico (0 : â„) 1, â€–derivWithin f (Icc (0 : â„) 1) xâ€– â‰¤ C) : â€–f 1 - f 0â€– â‰¤ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment hf bound 1 (right_mem_Icc.2 zero_le_one)

theorem constant_of_has_deriv_right_zero (hcont : ContinuousOn f (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f 0 (Ici x) x) : âˆ€ x âˆˆ Icc a b, f x = f a := by
  have : âˆ€ x âˆˆ Icc a b, â€–f x - f aâ€– â‰¤ 0 * (x - a) := fun x hx =>
    norm_image_sub_le_of_norm_deriv_right_le_segment hcont hderiv (fun _ _ => norm_zero.le) x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using this

theorem constant_of_derivWithin_zero (hdiff : DifferentiableOn â„ f (Icc a b))
    (hderiv : âˆ€ x âˆˆ Ico a b, derivWithin f (Icc a b) x = 0) : âˆ€ x âˆˆ Icc a b, f x = f a := by
  have H : âˆ€ x âˆˆ Ico a b, â€–derivWithin f (Icc a b) xâ€– â‰¤ 0 := by
    simpa only [norm_le_zero_iff] using fun x hx => hderiv x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using fun x hx =>
    norm_image_sub_le_of_norm_deriv_le_segment hdiff H x hx

variable {f' g : â„ â†’ E}

/-- If two continuous functions on `[a, b]` have the same right derivative and are equal at `a`,
  then they are equal everywhere on `[a, b]`. -/
theorem eq_of_has_deriv_right_eq (derivf : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (derivg : âˆ€ x âˆˆ Ico a b, HasDerivWithinAt g (f' x) (Ici x) x) (fcont : ContinuousOn f (Icc a b))
    (gcont : ContinuousOn g (Icc a b)) (hi : f a = g a) : âˆ€ y âˆˆ Icc a b, f y = g y := by
  simp only [â† @sub_eq_zero _ _ (f _)] at hi âŠ¢
  exact hi â–¸ constant_of_has_deriv_right_zero (fcont.sub gcont) fun y hy => by
    simpa only [sub_self] using (derivf y hy).sub (derivg y hy)

/-- If two differentiable functions on `[a, b]` have the same derivative within `[a, b]` everywhere
  on `[a, b)` and are equal at `a`, then they are equal everywhere on `[a, b]`. -/
theorem eq_of_derivWithin_eq (fdiff : DifferentiableOn â„ f (Icc a b))
    (gdiff : DifferentiableOn â„ g (Icc a b))
    (hderiv : EqOn (derivWithin f (Icc a b)) (derivWithin g (Icc a b)) (Ico a b)) (hi : f a = g a) :
    âˆ€ y âˆˆ Icc a b, f y = g y := by
  have A : âˆ€ y âˆˆ Ico a b, HasDerivWithinAt f (derivWithin f (Icc a b) y) (Ici y) y := fun y hy =>
    (fdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem_nhdsWithin
    (Icc_mem_nhdsGE_of_mem hy)
  have B : âˆ€ y âˆˆ Ico a b, HasDerivWithinAt g (derivWithin g (Icc a b) y) (Ici y) y := fun y hy =>
    (gdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem_nhdsWithin
    (Icc_mem_nhdsGE_of_mem hy)
  exact eq_of_has_deriv_right_eq A (fun y hy => (hderiv hy).symm â–¸ B y hy) fdiff.continuousOn
    gdiff.continuousOn hi

end

/-!
### Vector-valued functions `f : E â†’ G`

Theorems in this section work both for real and complex differentiable functions. We use assumptions
`[NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ] [NormedSpace ğ•œ E] [NormedSpace ğ•œ G]` to
achieve this result. For the domain `E` we also assume `[NormedSpace â„ E]` to have a notion
of a `Convex` set. -/

section

namespace Convex

variable {ğ•œ G : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
  [NormedSpace ğ•œ E] [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f g : E â†’ G} {C : â„} {s : Set E} {x y : E} {f' g' : E â†’ E â†’L[ğ•œ] G} {Ï† : E â†’L[ğ•œ] G}

instance (priority := 100) : PathConnectedSpace ğ•œ := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  infer_instance

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`, then
the function is `C`-Lipschitz. Version with `HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le
    (hf : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (bound : âˆ€ x âˆˆ s, â€–f' xâ€– â‰¤ C) (hs : Convex â„ s)
    (xs : x âˆˆ s) (ys : y âˆˆ s) : â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  letI : NormedSpace â„ G := RestrictScalars.normedSpace â„ ğ•œ G
  /- By composition with `AffineMap.lineMap x y`, we reduce to a statement for functions defined
    on `[0,1]`, for which it is proved in `norm_image_sub_le_of_norm_deriv_le_segment`.
    We just have to check the differentiability of the composition and bounds on its derivative,
    which is straightforward but tedious for lack of automation. -/
  set g := (AffineMap.lineMap x y : â„ â†’ E)
  have segm : MapsTo g (Icc 0 1 : Set â„) s := hs.mapsTo_lineMap xs ys
  have hD : âˆ€ t âˆˆ Icc (0 : â„) 1,
      HasDerivWithinAt (f âˆ˜ g) (f' (g t) (y - x)) (Icc 0 1) t := fun t ht => by
    simpa using ((hf (g t) (segm ht)).restrictScalars â„).comp_hasDerivWithinAt _
      AffineMap.hasDerivWithinAt_lineMap segm
  have bound : âˆ€ t âˆˆ Ico (0 : â„) 1, â€–f' (g t) (y - x)â€– â‰¤ C * â€–y - xâ€– := fun t ht =>
    le_of_opNorm_le _ (bound _ <| segm <| Ico_subset_Icc_self ht) _
  simpa [g] using norm_image_sub_le_of_norm_deriv_le_segment_01' hD bound

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `HasFDerivWithinAt` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasFDerivWithin_le {C : â„â‰¥0}
    (hf : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (bound : âˆ€ x âˆˆ s, â€–f' xâ€–â‚Š â‰¤ C)
    (hs : Convex â„ s) : LipschitzOnWith C f s := by
  rw [lipschitzOnWith_iff_norm_sub_le]
  intro x x_in y y_in
  exact hs.norm_image_sub_le_of_norm_hasFDerivWithin_le hf bound y_in x_in

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E â†’ G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : â„â‰¥0` larger than `â€–f' xâ€–â‚Š`, `f` is
`K`-Lipschitz on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt` for a version that claims
existence of `K` instead of an explicit estimate. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt (hs : Convex â„ s)
    {f : E â†’ G} (hder : âˆ€á¶  y in ğ“[s] x, HasFDerivWithinAt f (f' y) s y)
    (hcont : ContinuousWithinAt f' s x) (K : â„â‰¥0) (hK : â€–f' xâ€–â‚Š < K) :
    âˆƒ t âˆˆ ğ“[s] x, LipschitzOnWith K f t := by
  obtain âŸ¨Îµ, Îµ0, hÎµâŸ© : âˆƒ Îµ > 0,
      ball x Îµ âˆ© s âŠ† { y | HasFDerivWithinAt f (f' y) s y âˆ§ â€–f' yâ€–â‚Š < K } :=
    mem_nhdsWithin_iff.1 (hder.and <| hcont.nnnorm.eventually (gt_mem_nhds hK))
  rw [inter_comm] at hÎµ
  refine âŸ¨s âˆ© ball x Îµ, inter_mem_nhdsWithin _ (ball_mem_nhds _ Îµ0), ?_âŸ©
  exact
    (hs.inter (convex_ball _ _)).lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
      (fun y hy => (hÎµ hy).1.mono inter_subset_left) fun y hy => (hÎµ hy).2.le

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E â†’ G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : â„â‰¥0` larger than `â€–f' xâ€–â‚Š`, `f` is Lipschitz
on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt` for a version
with an explicit estimate on the Lipschitz constant. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt (hs : Convex â„ s) {f : E â†’ G}
    (hder : âˆ€á¶  y in ğ“[s] x, HasFDerivWithinAt f (f' y) s y) (hcont : ContinuousWithinAt f' s x) :
    âˆƒ K, âˆƒ t âˆˆ ğ“[s] x, LipschitzOnWith K f t :=
  (exists_gt _).imp <|
    hs.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt hder hcont

/-- The mean value theorem on a convex set: if the derivative of a function within this set is
bounded by `C`, then the function is `C`-Lipschitz. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le (hf : DifferentiableOn ğ•œ f s)
    (bound : âˆ€ x âˆˆ s, â€–fderivWithin ğ•œ f s xâ€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderivWithin` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderivWithin_le {C : â„â‰¥0} (hf : DifferentiableOn ğ•œ f s)
    (bound : âˆ€ x âˆˆ s, â€–fderivWithin ğ•œ f s xâ€–â‚Š â‰¤ C) (hs : Convex â„ s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le (hf : âˆ€ x âˆˆ s, DifferentiableAt ğ•œ f x)
    (bound : âˆ€ x âˆˆ s, â€–fderiv ğ•œ f xâ€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderiv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderiv_le {C : â„â‰¥0} (hf : âˆ€ x âˆˆ s, DifferentiableAt ğ•œ f x)
    (bound : âˆ€ x âˆˆ s, â€–fderiv ğ•œ f xâ€–â‚Š â‰¤ C) (hs : Convex â„ s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound

/-- The mean value theorem: if the derivative of a function is bounded by `C`, then the function is
`C`-Lipschitz. Version with `fderiv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_fderiv_le
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {f : E â†’ G}
    {C : â„â‰¥0} (hf : Differentiable ğ•œ f)
    (bound : âˆ€ x, â€–fderiv ğ•œ f xâ€–â‚Š â‰¤ C) : LipschitzWith C f := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  let A : NormedSpace â„ E := RestrictScalars.normedSpace â„ ğ•œ E
  rw [â† lipschitzOnWith_univ]
  exact lipschitzOnWith_of_nnnorm_fderiv_le (fun x _ â†¦ hf x) (fun x _ â†¦ bound x) convex_univ

/-- Variant of the mean value inequality on a convex set, using a bound on the difference between
the derivative and a fixed linear map, rather than a bound on the derivative itself. Version with
`HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (hf : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (bound : âˆ€ x âˆˆ s, â€–f' x - Ï†â€– â‰¤ C)
    (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) : â€–f y - f x - Ï† (y - x)â€– â‰¤ C * â€–y - xâ€– := by
  /- We subtract `Ï†` to define a new function `g` for which `g' = 0`, for which the previous theorem
    applies, `Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le`. Then, we just need to glue
    together the pieces, expressing back `f` in terms of `g`. -/
  let g y := f y - Ï† y
  have hg : âˆ€ x âˆˆ s, HasFDerivWithinAt g (f' x - Ï†) s x := fun x xs =>
    (hf x xs).sub Ï†.hasFDerivWithinAt
  calc
    â€–f y - f x - Ï† (y - x)â€– = â€–f y - f x - (Ï† y - Ï† x)â€– := by simp
    _ = â€–f y - Ï† y - (f x - Ï† x)â€– := by congr 1; abel
    _ = â€–g y - g xâ€– := by simp [g]
    _ â‰¤ C * â€–y - xâ€– := Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le hg bound hs xs ys

/-- Variant of the mean value inequality on a convex set. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le' (hf : DifferentiableOn ğ•œ f s)
    (bound : âˆ€ x âˆˆ s, â€–fderivWithin ğ•œ f s x - Ï†â€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f x - Ï† (y - x)â€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le' (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys

/-- Variant of the mean value inequality on a convex set. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le' (hf : âˆ€ x âˆˆ s, DifferentiableAt ğ•œ f x)
    (bound : âˆ€ x âˆˆ s, â€–fderiv ğ•œ f x - Ï†â€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f x - Ï† (y - x)â€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys

/-- If a function has zero FrÃ©chet derivative at every point of a convex set,
then it is a constant on this set. -/
theorem is_const_of_fderivWithin_eq_zero (hs : Convex â„ s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : âˆ€ x âˆˆ s, fderivWithin ğ•œ f s x = 0) (hx : x âˆˆ s) (hy : y âˆˆ s) : f x = f y := by
  have bound : âˆ€ x âˆˆ s, â€–fderivWithin ğ•œ f s xâ€– â‰¤ 0 := fun x hx => by
    simp only [hf' x hx, norm_zero, le_rfl]
  simpa only [(dist_eq_norm _ _).symm, zero_mul, dist_le_zero, eq_comm] using
    hs.norm_image_sub_le_of_norm_fderivWithin_le hf bound hx hy

theorem _root_.is_const_of_fderiv_eq_zero
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {f : E â†’ G}
    (hf : Differentiable ğ•œ f) (hf' : âˆ€ x, fderiv ğ•œ f x = 0)
    (x y : E) : f x = f y := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  let A : NormedSpace â„ E := RestrictScalars.normedSpace â„ ğ•œ E
  exact convex_univ.is_const_of_fderivWithin_eq_zero hf.differentiableOn
    (fun x _ => by rw [fderivWithin_univ]; exact hf' x) trivial trivial

/-- If two functions have equal FrÃ©chet derivatives at every point of a convex set, and are equal at
one point in that set, then they are equal on that set. -/
theorem eqOn_of_fderivWithin_eq (hs : Convex â„ s) (hf : DifferentiableOn ğ•œ f s)
    (hg : DifferentiableOn ğ•œ g s) (hs' : UniqueDiffOn ğ•œ s)
    (hf' : s.EqOn (fderivWithin ğ•œ f s) (fderivWithin ğ•œ g s)) (hx : x âˆˆ s) (hfgx : f x = g x) :
    s.EqOn f g := fun y hy => by
  suffices f x - g x = f y - g y by rwa [hfgx, sub_self, eq_comm, sub_eq_zero] at this
  refine hs.is_const_of_fderivWithin_eq_zero (hf.sub hg) (fun z hz => ?_) hx hy
  rw [fderivWithin_sub (hs' _ hz) (hf _ hz) (hg _ hz), sub_eq_zero, hf' hz]

/-- If `f` has zero derivative on an open set, then `f` is locally constant on `s`. -/
-- TODO: change the spelling once we have `IsLocallyConstantOn`.
theorem _root_.IsOpen.isOpen_inter_preimage_of_fderiv_eq_zero
    (hs : IsOpen s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (fderiv ğ•œ f) 0) (t : Set G) : IsOpen (s âˆ© f â»Â¹' t) := by
  refine Metric.isOpen_iff.mpr fun y âŸ¨hy, hy'âŸ© â†¦ ?_
  obtain âŸ¨r, hr, hâŸ© := Metric.isOpen_iff.mp hs y hy
  refine âŸ¨r, hr, Set.subset_inter h fun x hx â†¦ ?_âŸ©
  have := (convex_ball y r).is_const_of_fderivWithin_eq_zero (hf.mono h) ?_ hx (mem_ball_self hr)
  Â· simpa [this]
  Â· intro z hz
    simpa only [fderivWithin_of_isOpen Metric.isOpen_ball hz] using hf' (h hz)

theorem _root_.isLocallyConstant_of_fderiv_eq_zero (hâ‚ : Differentiable ğ•œ f)
    (hâ‚‚ : âˆ€ x, fderiv ğ•œ f x = 0) : IsLocallyConstant f := by
  simpa using isOpen_univ.isOpen_inter_preimage_of_fderiv_eq_zero hâ‚.differentiableOn fun _ _ â†¦ hâ‚‚ _

/-- If `f` has zero derivative on a connected open set, then `f` is constant on `s`. -/
theorem _root_.IsOpen.exists_is_const_of_fderiv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (fderiv ğ•œ f) 0) : âˆƒ a, âˆ€ x âˆˆ s, f x = a := by
  obtain (rfl|âŸ¨y, hyâŸ©) := s.eq_empty_or_nonempty
  Â· exact âŸ¨0, by simpâŸ©
  Â· refine âŸ¨f y, fun x hx â†¦ ?_âŸ©
    have hâ‚ := hs.isOpen_inter_preimage_of_fderiv_eq_zero hf hf' {f y}
    have hâ‚‚ := hf.continuousOn.comp_continuous continuous_subtype_val (fun x â†¦ x.2)
    by_contra hâ‚ƒ
    obtain âŸ¨t, ht, ht'âŸ© := (isClosed_singleton (x := f y)).preimage hâ‚‚
    have ht'' : âˆ€ a âˆˆ s, a âˆˆ t â†” f a â‰  f y := by simpa [Set.ext_iff] using ht'
    obtain âŸ¨z, Hâ‚, Hâ‚‚, Hâ‚ƒâŸ© := hs' _ _ hâ‚ ht (fun x h â†¦ by simp [h, ht'', eq_or_ne]) âŸ¨y, by simpaâŸ©
      âŸ¨x, by simp [ht'' _ hx, hx, hâ‚ƒ]âŸ©
    exact (ht'' _ Hâ‚).mp Hâ‚ƒ Hâ‚‚.2

theorem _root_.IsOpen.is_const_of_fderiv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (fderiv ğ•œ f) 0) {x y : E} (hx : x âˆˆ s) (hy : y âˆˆ s) : f x = f y := by
  obtain âŸ¨a, haâŸ© := hs.exists_is_const_of_fderiv_eq_zero hs' hf hf'
  rw [ha x hx, ha y hy]

theorem _root_.IsOpen.exists_eq_add_of_fderiv_eq (hs : IsOpen s) (hs' : IsPreconnected s)
    (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s)
    (hf' : s.EqOn (fderiv ğ•œ f) (fderiv ğ•œ g)) : âˆƒ a, s.EqOn f (g Â· + a) := by
  simp_rw [Set.EqOn, â† sub_eq_iff_eq_add']
  refine hs.exists_is_const_of_fderiv_eq_zero hs' (hf.sub hg) fun x hx â†¦ ?_
  rw [fderiv_sub (hf.differentiableAt (hs.mem_nhds hx)) (hg.differentiableAt (hs.mem_nhds hx)),
    hf' hx, sub_self, Pi.zero_apply]

/-- If two functions have equal FrÃ©chet derivatives at every point of a connected open set,
and are equal at one point in that set, then they are equal on that set. -/
theorem _root_.IsOpen.eqOn_of_fderiv_eq (hs : IsOpen s) (hs' : IsPreconnected s)
    (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s)
    (hf' : âˆ€ x âˆˆ s, fderiv ğ•œ f x = fderiv ğ•œ g x) (hx : x âˆˆ s) (hfgx : f x = g x) :
    s.EqOn f g := by
  obtain âŸ¨a, haâŸ© := hs.exists_eq_add_of_fderiv_eq hs' hf hg hf'
  obtain rfl := self_eq_add_right.mp (hfgx.symm.trans (ha hx))
  simpa using ha

theorem _root_.eq_of_fderiv_eq
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {f g : E â†’ G}
    (hf : Differentiable ğ•œ f) (hg : Differentiable ğ•œ g)
    (hf' : âˆ€ x, fderiv ğ•œ f x = fderiv ğ•œ g x) (x : E) (hfgx : f x = g x) : f = g := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  let A : NormedSpace â„ E := RestrictScalars.normedSpace â„ ğ•œ E
  suffices Set.univ.EqOn f g from funext fun x => this <| mem_univ x
  exact convex_univ.eqOn_of_fderivWithin_eq hf.differentiableOn hg.differentiableOn
    uniqueDiffOn_univ (fun x _ => by simpa using hf' _) (mem_univ _) hfgx

end Convex

namespace Convex

variable {ğ•œ G : Type*} [RCLike ğ•œ] [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f f' : ğ•œ â†’ G} {s : Set ğ•œ} {x y : ğ•œ}

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `HasDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasDerivWithin_le {C : â„}
    (hf : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (bound : âˆ€ x âˆˆ s, â€–f' xâ€– â‰¤ C) (hs : Convex â„ s)
    (xs : x âˆˆ s) (ys : y âˆˆ s) : â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– :=
  Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `HasDerivWithinAt` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasDerivWithin_le {C : â„â‰¥0} (hs : Convex â„ s)
    (hf : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) (bound : âˆ€ x âˆˆ s, â€–f' xâ€–â‚Š â‰¤ C) :
    LipschitzOnWith C f s :=
  Convex.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function within
this set is bounded by `C`, then the function is `C`-Lipschitz. Version with `derivWithin` -/
theorem norm_image_sub_le_of_norm_derivWithin_le {C : â„} (hf : DifferentiableOn ğ•œ f s)
    (bound : âˆ€ x âˆˆ s, â€–derivWithin f s xâ€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound xs
    ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `derivWithin` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_derivWithin_le {C : â„â‰¥0} (hs : Convex â„ s)
    (hf : DifferentiableOn ğ•œ f s) (bound : âˆ€ x âˆˆ s, â€–derivWithin f s xâ€–â‚Š â‰¤ C) :
    LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `deriv`. -/
theorem norm_image_sub_le_of_norm_deriv_le {C : â„} (hf : âˆ€ x âˆˆ s, DifferentiableAt ğ•œ f x)
    (bound : âˆ€ x âˆˆ s, â€–deriv f xâ€– â‰¤ C) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    â€–f y - f xâ€– â‰¤ C * â€–y - xâ€– :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `deriv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_deriv_le {C : â„â‰¥0} (hf : âˆ€ x âˆˆ s, DifferentiableAt ğ•œ f x)
    (bound : âˆ€ x âˆˆ s, â€–deriv f xâ€–â‚Š â‰¤ C) (hs : Convex â„ s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound

/-- The mean value theorem set in dimension 1: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `deriv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_deriv_le {C : â„â‰¥0} (hf : Differentiable ğ•œ f)
    (bound : âˆ€ x, â€–deriv f xâ€–â‚Š â‰¤ C) : LipschitzWith C f :=
  lipschitzOnWith_univ.1 <|
    convex_univ.lipschitzOnWith_of_nnnorm_deriv_le (fun x _ => hf x) fun x _ => bound x

/-- If `f : ğ•œ â†’ G`, `ğ•œ = R` or `ğ•œ = â„‚`, is differentiable everywhere and its derivative equal zero,
then it is a constant function. -/
theorem _root_.is_const_of_deriv_eq_zero (hf : Differentiable ğ•œ f) (hf' : âˆ€ x, deriv f x = 0)
    (x y : ğ•œ) : f x = f y :=
  is_const_of_fderiv_eq_zero hf (fun z => by ext; simp [â† deriv_fderiv, hf']) _ _

theorem _root_.IsOpen.isOpen_inter_preimage_of_deriv_eq_zero
    (hs : IsOpen s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (deriv f) 0) (t : Set G) : IsOpen (s âˆ© f â»Â¹' t) :=
  hs.isOpen_inter_preimage_of_fderiv_eq_zero hf
    (fun x hx â†¦ by ext; simp [â† deriv_fderiv, hf' hx]) t

theorem _root_.IsOpen.exists_is_const_of_deriv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (deriv f) 0) : âˆƒ a, âˆ€ x âˆˆ s, f x = a :=
  hs.exists_is_const_of_fderiv_eq_zero hs' hf (fun {x} hx â†¦ by ext; simp [â† deriv_fderiv, hf' hx])

theorem _root_.IsOpen.is_const_of_deriv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ğ•œ f s)
    (hf' : s.EqOn (deriv f) 0) {x y : ğ•œ} (hx : x âˆˆ s) (hy : y âˆˆ s) : f x = f y :=
  hs.is_const_of_fderiv_eq_zero hs' hf (fun a ha â†¦ by ext; simp [â† deriv_fderiv, hf' ha]) hx hy

theorem _root_.IsOpen.exists_eq_add_of_deriv_eq {f g : ğ•œ â†’ G} (hs : IsOpen s)
    (hs' : IsPreconnected s)
    (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s)
    (hf' : s.EqOn (deriv f) (deriv g)) : âˆƒ a, s.EqOn f (g Â· + a) :=
  hs.exists_eq_add_of_fderiv_eq hs' hf hg (fun x hx â†¦ by ext; simp [â† deriv_fderiv, hf' hx])

theorem _root_.IsOpen.eqOn_of_deriv_eq {f g : ğ•œ â†’ G} (hs : IsOpen s)
    (hs' : IsPreconnected s) (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s)
    (hf' : s.EqOn (deriv f) (deriv g)) (hx : x âˆˆ s) (hfgx : f x = g x) :
    s.EqOn f g :=
  hs.eqOn_of_fderiv_eq hs' hf hg (fun _ hx â†¦ ContinuousLinearMap.ext_ring (hf' hx)) hx hfgx

end Convex

end

/-! ### Functions `[a, b] â†’ â„`. -/

section Interval

-- Declare all variables here to make sure they come in a correct order
variable (f f' : â„ â†’ â„) {a b : â„} (hab : a < b) (hfc : ContinuousOn f (Icc a b))
  (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) (hfd : DifferentiableOn â„ f (Ioo a b))
  (g g' : â„ â†’ â„) (hgc : ContinuousOn g (Icc a b)) (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x)
  (hgd : DifferentiableOn â„ g (Ioo a b))

include hab hfc hff' hgc hgg' in
/-- Cauchy's **Mean Value Theorem**, `HasDerivAt` version. -/
theorem exists_ratio_hasDerivAt_eq_ratio_slope :
    âˆƒ c âˆˆ Ioo a b, (g b - g a) * f' c = (f b - f a) * g' c := by
  let h x := (g b - g a) * f x - (f b - f a) * g x
  have hI : h a = h b := by simp only [h]; ring
  let h' x := (g b - g a) * f' x - (f b - f a) * g' x
  have hhh' : âˆ€ x âˆˆ Ioo a b, HasDerivAt h (h' x) x := fun x hx =>
    ((hff' x hx).const_mul (g b - g a)).sub ((hgg' x hx).const_mul (f b - f a))
  have hhc : ContinuousOn h (Icc a b) :=
    (continuousOn_const.mul hfc).sub (continuousOn_const.mul hgc)
  rcases exists_hasDerivAt_eq_zero hab hhc hI hhh' with âŸ¨c, cmem, hcâŸ©
  exact âŸ¨c, cmem, sub_eq_zero.1 hcâŸ©

include hab in
/-- Cauchy's **Mean Value Theorem**, extended `HasDerivAt` version. -/
theorem exists_ratio_hasDerivAt_eq_ratio_slope' {lfa lga lfb lgb : â„}
    (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) (hgg' : âˆ€ x âˆˆ Ioo a b, HasDerivAt g (g' x) x)
    (hfa : Tendsto f (ğ“[>] a) (ğ“ lfa)) (hga : Tendsto g (ğ“[>] a) (ğ“ lga))
    (hfb : Tendsto f (ğ“[<] b) (ğ“ lfb)) (hgb : Tendsto g (ğ“[<] b) (ğ“ lgb)) :
    âˆƒ c âˆˆ Ioo a b, (lgb - lga) * f' c = (lfb - lfa) * g' c := by
  let h x := (lgb - lga) * f x - (lfb - lfa) * g x
  have hha : Tendsto h (ğ“[>] a) (ğ“ <| lgb * lfa - lfb * lga) := by
    have : Tendsto h (ğ“[>] a) (ğ“ <| (lgb - lga) * lfa - (lfb - lfa) * lga) :=
      (tendsto_const_nhds.mul hfa).sub (tendsto_const_nhds.mul hga)
    convert this using 2
    ring
  have hhb : Tendsto h (ğ“[<] b) (ğ“ <| lgb * lfa - lfb * lga) := by
    have : Tendsto h (ğ“[<] b) (ğ“ <| (lgb - lga) * lfb - (lfb - lfa) * lgb) :=
      (tendsto_const_nhds.mul hfb).sub (tendsto_const_nhds.mul hgb)
    convert this using 2
    ring
  let h' x := (lgb - lga) * f' x - (lfb - lfa) * g' x
  have hhh' : âˆ€ x âˆˆ Ioo a b, HasDerivAt h (h' x) x := by
    intro x hx
    exact ((hff' x hx).const_mul _).sub ((hgg' x hx).const_mul _)
  rcases exists_hasDerivAt_eq_zero' hab hha hhb hhh' with âŸ¨c, cmem, hcâŸ©
  exact âŸ¨c, cmem, sub_eq_zero.1 hcâŸ©

include hab hfc hff' in
/-- Lagrange's Mean Value Theorem, `HasDerivAt` version -/
theorem exists_hasDerivAt_eq_slope : âˆƒ c âˆˆ Ioo a b, f' c = (f b - f a) / (b - a) := by
  obtain âŸ¨c, cmem, hcâŸ© : âˆƒ c âˆˆ Ioo a b, (b - a) * f' c = (f b - f a) * 1 :=
    exists_ratio_hasDerivAt_eq_ratio_slope f f' hab hfc hff' id 1 continuousOn_id
      fun x _ => hasDerivAt_id x
  use c, cmem
  rwa [mul_one, mul_comm, â† eq_div_iff (sub_ne_zero.2 hab.ne')] at hc

include hab hfc hgc hgd hfd in
/-- Cauchy's Mean Value Theorem, `deriv` version. -/
theorem exists_ratio_deriv_eq_ratio_slope :
    âˆƒ c âˆˆ Ioo a b, (g b - g a) * deriv f c = (f b - f a) * deriv g c :=
  exists_ratio_hasDerivAt_eq_ratio_slope f (deriv f) hab hfc
    (fun x hx => ((hfd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt) g
    (deriv g) hgc fun x hx =>
    ((hgd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt

include hab in
/-- Cauchy's Mean Value Theorem, extended `deriv` version. -/
theorem exists_ratio_deriv_eq_ratio_slope' {lfa lga lfb lgb : â„}
    (hdf : DifferentiableOn â„ f <| Ioo a b) (hdg : DifferentiableOn â„ g <| Ioo a b)
    (hfa : Tendsto f (ğ“[>] a) (ğ“ lfa)) (hga : Tendsto g (ğ“[>] a) (ğ“ lga))
    (hfb : Tendsto f (ğ“[<] b) (ğ“ lfb)) (hgb : Tendsto g (ğ“[<] b) (ğ“ lgb)) :
    âˆƒ c âˆˆ Ioo a b, (lgb - lga) * deriv f c = (lfb - lfa) * deriv g c :=
  exists_ratio_hasDerivAt_eq_ratio_slope' _ _ hab _ _
    (fun x hx => ((hdf x hx).differentiableAt <| Ioo_mem_nhds hx.1 hx.2).hasDerivAt)
    (fun x hx => ((hdg x hx).differentiableAt <| Ioo_mem_nhds hx.1 hx.2).hasDerivAt) hfa hga hfb hgb

include hab hfc hfd in
/-- Lagrange's **Mean Value Theorem**, `deriv` version. -/
theorem exists_deriv_eq_slope : âˆƒ c âˆˆ Ioo a b, deriv f c = (f b - f a) / (b - a) :=
  exists_hasDerivAt_eq_slope f (deriv f) hab hfc fun x hx =>
    ((hfd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt

include hab hfc hfd in
/-- Lagrange's **Mean Value Theorem**, `deriv` version. -/
theorem exists_deriv_eq_slope' : âˆƒ c âˆˆ Ioo a b, deriv f c = slope f a b := by
  rw [slope_def_field]
  exact exists_deriv_eq_slope f hab hfc hfd

/-- A real function whose derivative tends to infinity from the right at a point is not
differentiable on the right at that point -/
theorem not_differentiableWithinAt_of_deriv_tendsto_atTop_Ioi (f : â„ â†’ â„) {a : â„}
    (hf : Tendsto (deriv f) (ğ“[>] a) atTop) : Â¬ DifferentiableWithinAt â„ f (Ioi a) a := by
  replace hf : Tendsto (derivWithin f (Ioi a)) (ğ“[>] a) atTop := by
    refine hf.congr' ?_
    filter_upwards [eventually_mem_nhdsWithin] with x hx
    have : Ioi a âˆˆ ğ“ x := by simp [â† mem_interior_iff_mem_nhds, hx]
    exact (derivWithin_of_mem_nhds this).symm
  by_cases hcont_at_a : ContinuousWithinAt f (Ici a) a
  case neg =>
    intro hcontra
    have := hcontra.continuousWithinAt
    rw [â† ContinuousWithinAt.diff_iff this] at hcont_at_a
    simp at hcont_at_a
  case pos =>
    intro hdiff
    replace hdiff := hdiff.hasDerivWithinAt
    rw [hasDerivWithinAt_iff_tendsto_slope, Set.diff_singleton_eq_self not_mem_Ioi_self] at hdiff
    have hâ‚€ : âˆ€á¶  b in ğ“[>] a,
        âˆ€ x âˆˆ Ioc a b, max (derivWithin f (Ioi a) a + 1) 0 < derivWithin f (Ioi a) x := by
      rw [(nhdsGT_basis a).eventually_iff]
      rw [(nhdsGT_basis a).tendsto_left_iff] at hf
      obtain âŸ¨b, hab, hbâŸ© := hf (Ioi (max (derivWithin f (Ioi a) a + 1) 0)) (Ioi_mem_atTop _)
      refine âŸ¨b, hab, fun x hx z hz => ?_âŸ©
      simp only [MapsTo, mem_Ioo, mem_Ioi, and_imp] at hb
      exact hb hz.1 <| hz.2.trans_lt hx.2
    have hâ‚ : âˆ€á¶  b in ğ“[>] a, slope f a b < derivWithin f (Ioi a) a + 1 := by
      rw [(nhds_basis_Ioo _).tendsto_right_iff] at hdiff
      specialize hdiff âŸ¨derivWithin f (Ioi a) a - 1, derivWithin f (Ioi a) a + 1âŸ© <| by simp
      filter_upwards [hdiff] with z hz using hz.2
    have hcontra : âˆ€á¶  _ in ğ“[>] a, False := by
      filter_upwards [hâ‚€, hâ‚, eventually_mem_nhdsWithin] with b hb hslope (hab : a < b)
      have hdiff' : DifferentiableOn â„ f (Ioc a b) := fun z hz => by
        refine DifferentiableWithinAt.mono (t := Ioi a) ?_ Ioc_subset_Ioi_self
        have : derivWithin f (Ioi a) z â‰  0 := ne_of_gt <| by
          simp_all only [mem_Ioo, and_imp, mem_Ioc, max_lt_iff]
        exact differentiableWithinAt_of_derivWithin_ne_zero this
      have hcont_Ioc : âˆ€ z âˆˆ Ioc a b, ContinuousWithinAt f (Icc a b) z := by
        intro z hz''
        refine (hdiff'.continuousOn z hz'').mono_of_mem_nhdsWithin ?_
        have hfinal : ğ“[Ioc a b] z = ğ“[Icc a b] z := by
          refine nhdsWithin_eq_nhdsWithin' (s := Ioi a) (Ioi_mem_nhds hz''.1) ?_
          simp only [Ioc_inter_Ioi, le_refl, sup_of_le_left]
          ext y
          exact âŸ¨fun h => âŸ¨mem_Icc_of_Ioc h, mem_of_mem_inter_left hâŸ©, fun âŸ¨H1, H2âŸ© => âŸ¨H2, H1.2âŸ©âŸ©
        rw [â† hfinal]
        exact self_mem_nhdsWithin
      have hcont : ContinuousOn f (Icc a b) := by
        intro z hz
        by_cases hz' : z = a
        Â· rw [hz']
          exact hcont_at_a.mono Icc_subset_Ici_self
        Â· exact hcont_Ioc z âŸ¨lt_of_le_of_ne hz.1 (Ne.symm hz'), hz.2âŸ©
      obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© :=
        exists_deriv_eq_slope' f hab hcont (hdiff'.mono (Ioo_subset_Ioc_self))
      specialize hb x âŸ¨hxâ‚.1, le_of_lt hxâ‚.2âŸ©
      replace hxâ‚‚ : derivWithin f (Ioi a) x = slope f a b := by
        have : Ioi a âˆˆ ğ“ x := by simp [â† mem_interior_iff_mem_nhds, hxâ‚.1]
        rwa [derivWithin_of_mem_nhds this]
      rw [hxâ‚‚, max_lt_iff] at hb
      linarith
    simp [Filter.eventually_false_iff_eq_bot, â† not_mem_closure_iff_nhdsWithin_eq_bot] at hcontra

/-- A real function whose derivative tends to minus infinity from the right at a point is not
differentiable on the right at that point -/
theorem not_differentiableWithinAt_of_deriv_tendsto_atBot_Ioi (f : â„ â†’ â„) {a : â„}
    (hf : Tendsto (deriv f) (ğ“[>] a) atBot) : Â¬ DifferentiableWithinAt â„ f (Ioi a) a := by
  intro h
  have hf' : Tendsto (deriv (-f)) (ğ“[>] a) atTop := by
    rw [Pi.neg_def, deriv.neg']
    exact tendsto_neg_atBot_atTop.comp hf
  exact not_differentiableWithinAt_of_deriv_tendsto_atTop_Ioi (-f) hf' h.neg

/-- A real function whose derivative tends to minus infinity from the left at a point is not
differentiable on the left at that point -/
theorem not_differentiableWithinAt_of_deriv_tendsto_atBot_Iio (f : â„ â†’ â„) {a : â„}
    (hf : Tendsto (deriv f) (ğ“[<] a) atBot) : Â¬ DifferentiableWithinAt â„ f (Iio a) a := by
  let f' := f âˆ˜ Neg.neg
  have hderiv : deriv f' =á¶ [ğ“[>] (-a)] -(deriv f âˆ˜ Neg.neg) := by
    rw [atBot_basis.tendsto_right_iff] at hf
    specialize hf (-1) trivial
    rw [(nhdsLT_basis a).eventually_iff] at hf
    rw [EventuallyEq, (nhdsGT_basis (-a)).eventually_iff]
    obtain âŸ¨b, hbâ‚, hbâ‚‚âŸ© := hf
    refine âŸ¨-b, by linarith, fun x hx => ?_âŸ©
    simp only [Pi.neg_apply, Function.comp_apply]
    suffices deriv f' x = deriv f (-x) * deriv (Neg.neg : â„ â†’ â„) x by simpa using this
    refine deriv_comp x (differentiableAt_of_deriv_ne_zero ?_) (by fun_prop)
    rw [mem_Ioo] at hx
    have hâ‚ : -x âˆˆ Ioo b a := âŸ¨by linarith, by linarithâŸ©
    have hâ‚‚ : deriv f (-x) â‰¤ -1 := hbâ‚‚ hâ‚
    exact ne_of_lt (by linarith)
  have hmain : Â¬ DifferentiableWithinAt â„ f' (Ioi (-a)) (-a) := by
    refine not_differentiableWithinAt_of_deriv_tendsto_atTop_Ioi f' <| Tendsto.congr' hderiv.symm ?_
    refine Tendsto.comp (g := -deriv f) ?_ tendsto_neg_nhdsGT_neg
    exact Tendsto.comp (g := Neg.neg) tendsto_neg_atBot_atTop hf
  intro h
  have : DifferentiableWithinAt â„ f' (Ioi (-a)) (-a) := by
    refine DifferentiableWithinAt.comp (g := f) (f := Neg.neg) (t := Iio a) (-a) ?_ ?_ ?_
    Â· simp [h]
    Â· fun_prop
    Â· intro x
      simp [neg_lt]
  exact hmain this

/-- A real function whose derivative tends to infinity from the left at a point is not
differentiable on the left at that point -/
theorem not_differentiableWithinAt_of_deriv_tendsto_atTop_Iio (f : â„ â†’ â„) {a : â„}
    (hf : Tendsto (deriv f) (ğ“[<] a) atTop) : Â¬ DifferentiableWithinAt â„ f (Iio a) a := by
  intro h
  have hf' : Tendsto (deriv (-f)) (ğ“[<] a) atBot := by
    rw [Pi.neg_def, deriv.neg']
    exact tendsto_neg_atTop_atBot.comp hf
  exact not_differentiableWithinAt_of_deriv_tendsto_atBot_Iio (-f) hf' h.neg

end Interval

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C < f'`, then
`f` grows faster than `C * x` on `D`, i.e., `C * (y - x) < f y - f x` whenever `x, y âˆˆ D`,
`x < y`. -/
theorem Convex.mul_sub_lt_image_sub_of_lt_deriv {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D)) {C}
    (hf'_gt : âˆ€ x âˆˆ interior D, C < deriv f x) :
    âˆ€áµ‰ (x âˆˆ D) (y âˆˆ D), x < y â†’ C * (y - x) < f y - f x := by
  intro x hx y hy hxy
  have hxyD : Icc x y âŠ† D := hD.ordConnected.out hx hy
  have hxyD' : Ioo x y âŠ† interior D :=
    subset_sUnion_of_mem âŸ¨isOpen_Ioo, Ioo_subset_Icc_self.trans hxyDâŸ©
  obtain âŸ¨a, a_mem, haâŸ© : âˆƒ a âˆˆ Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy (hf.mono hxyD) (hf'.mono hxyD')
  have : C < (f y - f x) / (y - x) := ha â–¸ hf'_gt _ (hxyD' a_mem)
  exact (lt_div_iffâ‚€ (sub_pos.2 hxy)).1 this

/-- Let `f : â„ â†’ â„` be a differentiable function. If `C < f'`, then `f` grows faster than
`C * x`, i.e., `C * (y - x) < f y - f x` whenever `x < y`. -/
theorem mul_sub_lt_image_sub_of_lt_deriv {f : â„ â†’ â„} (hf : Differentiable â„ f) {C}
    (hf'_gt : âˆ€ x, C < deriv f x) â¦ƒx yâ¦„ (hxy : x < y) : C * (y - x) < f y - f x :=
  convex_univ.mul_sub_lt_image_sub_of_lt_deriv hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => hf'_gt x) x trivial y trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C â‰¤ f'`, then
`f` grows at least as fast as `C * x` on `D`, i.e., `C * (y - x) â‰¤ f y - f x` whenever `x, y âˆˆ D`,
`x â‰¤ y`. -/
theorem Convex.mul_sub_le_image_sub_of_le_deriv {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D)) {C}
    (hf'_ge : âˆ€ x âˆˆ interior D, C â‰¤ deriv f x) :
    âˆ€áµ‰ (x âˆˆ D) (y âˆˆ D), x â‰¤ y â†’ C * (y - x) â‰¤ f y - f x := by
  intro x hx y hy hxy
  rcases eq_or_lt_of_le hxy with hxy' | hxy'
  Â· rw [hxy', sub_self, sub_self, mul_zero]
  have hxyD : Icc x y âŠ† D := hD.ordConnected.out hx hy
  have hxyD' : Ioo x y âŠ† interior D :=
    subset_sUnion_of_mem âŸ¨isOpen_Ioo, Ioo_subset_Icc_self.trans hxyDâŸ©
  obtain âŸ¨a, a_mem, haâŸ© : âˆƒ a âˆˆ Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy' (hf.mono hxyD) (hf'.mono hxyD')
  have : C â‰¤ (f y - f x) / (y - x) := ha â–¸ hf'_ge _ (hxyD' a_mem)
  exact (le_div_iffâ‚€ (sub_pos.2 hxy')).1 this

/-- Let `f : â„ â†’ â„` be a differentiable function. If `C â‰¤ f'`, then `f` grows at least as fast
as `C * x`, i.e., `C * (y - x) â‰¤ f y - f x` whenever `x â‰¤ y`. -/
theorem mul_sub_le_image_sub_of_le_deriv {f : â„ â†’ â„} (hf : Differentiable â„ f) {C}
    (hf'_ge : âˆ€ x, C â‰¤ deriv f x) â¦ƒx yâ¦„ (hxy : x â‰¤ y) : C * (y - x) â‰¤ f y - f x :=
  convex_univ.mul_sub_le_image_sub_of_le_deriv hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => hf'_ge x) x trivial y trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' < C`, then
`f` grows slower than `C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x, y âˆˆ D`,
`x < y`. -/
theorem Convex.image_sub_lt_mul_sub_of_deriv_lt {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D)) {C}
    (lt_hf' : âˆ€ x âˆˆ interior D, deriv f x < C) (x : â„) (hx : x âˆˆ D) (y : â„) (hy : y âˆˆ D)
    (hxy : x < y) : f y - f x < C * (y - x) :=
  have hf'_gt : âˆ€ x âˆˆ interior D, -C < deriv (fun y => -f y) x := fun x hx => by
    rw [deriv.neg, neg_lt_neg_iff]
    exact lt_hf' x hx
  by linarith [hD.mul_sub_lt_image_sub_of_lt_deriv hf.neg hf'.neg hf'_gt x hx y hy hxy]

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f' < C`, then `f` grows slower than
`C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x < y`. -/
theorem image_sub_lt_mul_sub_of_deriv_lt {f : â„ â†’ â„} (hf : Differentiable â„ f) {C}
    (lt_hf' : âˆ€ x, deriv f x < C) â¦ƒx yâ¦„ (hxy : x < y) : f y - f x < C * (y - x) :=
  convex_univ.image_sub_lt_mul_sub_of_deriv_lt hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => lt_hf' x) x trivial y trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' â‰¤ C`, then
`f` grows at most as fast as `C * x` on `D`, i.e., `f y - f x â‰¤ C * (y - x)` whenever `x, y âˆˆ D`,
`x â‰¤ y`. -/
theorem Convex.image_sub_le_mul_sub_of_deriv_le {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D)) {C}
    (le_hf' : âˆ€ x âˆˆ interior D, deriv f x â‰¤ C) (x : â„) (hx : x âˆˆ D) (y : â„) (hy : y âˆˆ D)
    (hxy : x â‰¤ y) : f y - f x â‰¤ C * (y - x) :=
  have hf'_ge : âˆ€ x âˆˆ interior D, -C â‰¤ deriv (fun y => -f y) x := fun x hx => by
    rw [deriv.neg, neg_le_neg_iff]
    exact le_hf' x hx
  by linarith [hD.mul_sub_le_image_sub_of_le_deriv hf.neg hf'.neg hf'_ge x hx y hy hxy]

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f' â‰¤ C`, then `f` grows at most as fast
as `C * x`, i.e., `f y - f x â‰¤ C * (y - x)` whenever `x â‰¤ y`. -/
theorem image_sub_le_mul_sub_of_deriv_le {f : â„ â†’ â„} (hf : Differentiable â„ f) {C}
    (le_hf' : âˆ€ x, deriv f x â‰¤ C) â¦ƒx yâ¦„ (hxy : x â‰¤ y) : f y - f x â‰¤ C * (y - x) :=
  convex_univ.image_sub_le_mul_sub_of_deriv_le hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => le_hf' x) x trivial y trivial hxy

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is positive, then
`f` is a strictly monotone function on `D`.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly positive. -/
theorem strictMonoOn_of_deriv_pos {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, 0 < deriv f x) : StrictMonoOn f D := by
  intro x hx y hy
  have : DifferentiableOn â„ f (interior D) := fun z hz =>
    (differentiableAt_of_deriv_ne_zero (hf' z hz).ne').differentiableWithinAt
  simpa only [zero_mul, sub_pos] using
    hD.mul_sub_lt_image_sub_of_lt_deriv hf this hf' x hx y hy

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is positive, then
`f` is a strictly monotone function.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly positive. -/
theorem strictMono_of_deriv_pos {f : â„ â†’ â„} (hf' : âˆ€ x, 0 < deriv f x) : StrictMono f :=
  strictMonoOn_univ.1 <| strictMonoOn_of_deriv_pos convex_univ (fun z _ =>
    (differentiableAt_of_deriv_ne_zero (hf' z).ne').differentiableWithinAt.continuousWithinAt)
    fun x _ => hf' x

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is strictly positive,
then `f` is a strictly monotone function on `D`. -/
lemma strictMonoOn_of_hasDerivWithinAt_pos {D : Set â„} (hD : Convex â„ D) {f f' : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'â‚€ : âˆ€ x âˆˆ interior D, 0 < f' x) : StrictMonoOn f D :=
  strictMonoOn_of_deriv_pos hD hf fun x hx â†¦ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'â‚€ _ hx

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is strictly positive, then
`f` is a strictly monotone function. -/
lemma strictMono_of_hasDerivAt_pos {f f' : â„ â†’ â„} (hf : âˆ€ x, HasDerivAt f (f' x) x)
    (hf' : âˆ€ x, 0 < f' x) : StrictMono f :=
  strictMono_of_deriv_pos fun x â†¦ by rw [(hf _).deriv]; exact hf' _

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonnegative, then
`f` is a monotone function on `D`. -/
theorem monotoneOn_of_deriv_nonneg {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D))
    (hf'_nonneg : âˆ€ x âˆˆ interior D, 0 â‰¤ deriv f x) : MonotoneOn f D := fun x hx y hy hxy => by
  simpa only [zero_mul, sub_nonneg] using
    hD.mul_sub_le_image_sub_of_le_deriv hf hf' hf'_nonneg x hx y hy hxy

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is nonnegative, then
`f` is a monotone function. -/
theorem monotone_of_deriv_nonneg {f : â„ â†’ â„} (hf : Differentiable â„ f) (hf' : âˆ€ x, 0 â‰¤ deriv f x) :
    Monotone f :=
  monotoneOn_univ.1 <|
    monotoneOn_of_deriv_nonneg convex_univ hf.continuous.continuousOn hf.differentiableOn fun x _ =>
      hf' x

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonnegative, then
`f` is a monotone function on `D`. -/
lemma monotoneOn_of_hasDerivWithinAt_nonneg {D : Set â„} (hD : Convex â„ D) {f f' : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'â‚€ : âˆ€ x âˆˆ interior D, 0 â‰¤ f' x) : MonotoneOn f D :=
  monotoneOn_of_deriv_nonneg hD hf (fun _ hx â†¦ (hf' _ hx).differentiableWithinAt) fun x hx â†¦ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'â‚€ _ hx

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is nonnegative, then
`f` is a monotone function. -/
lemma monotone_of_hasDerivAt_nonneg {f f' : â„ â†’ â„} (hf : âˆ€ x, HasDerivAt f (f' x) x)
    (hf' : 0 â‰¤ f') : Monotone f :=
  monotone_of_deriv_nonneg (fun _ â†¦ (hf _).differentiableAt) fun x â†¦ by
    rw [(hf _).deriv]; exact hf' _

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is negative, then
`f` is a strictly antitone function on `D`. -/
theorem strictAntiOn_of_deriv_neg {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, deriv f x < 0) : StrictAntiOn f D :=
  fun x hx y => by
  simpa only [zero_mul, sub_lt_zero] using
    hD.image_sub_lt_mul_sub_of_deriv_lt hf
      (fun z hz => (differentiableAt_of_deriv_ne_zero (hf' z hz).ne).differentiableWithinAt) hf' x
      hx y

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is negative, then
`f` is a strictly antitone function.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly negative. -/
theorem strictAnti_of_deriv_neg {f : â„ â†’ â„} (hf' : âˆ€ x, deriv f x < 0) : StrictAnti f :=
  strictAntiOn_univ.1 <| strictAntiOn_of_deriv_neg convex_univ
      (fun z _ =>
        (differentiableAt_of_deriv_ne_zero (hf' z).ne).differentiableWithinAt.continuousWithinAt)
      fun x _ => hf' x

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is strictly positive,
then `f` is a strictly monotone function on `D`. -/
lemma strictAntiOn_of_hasDerivWithinAt_neg {D : Set â„} (hD : Convex â„ D) {f f' : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'â‚€ : âˆ€ x âˆˆ interior D, f' x < 0) : StrictAntiOn f D :=
  strictAntiOn_of_deriv_neg hD hf fun x hx â†¦ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'â‚€ _ hx

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is strictly positive, then
`f` is a strictly monotone function. -/
lemma strictAnti_of_hasDerivAt_neg {f f' : â„ â†’ â„} (hf : âˆ€ x, HasDerivAt f (f' x) x)
    (hf' : âˆ€ x, f' x < 0) : StrictAnti f :=
  strictAnti_of_deriv_neg fun x â†¦ by rw [(hf _).deriv]; exact hf' _

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonpositive, then
`f` is an antitone function on `D`. -/
theorem antitoneOn_of_deriv_nonpos {D : Set â„} (hD : Convex â„ D) {f : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn â„ f (interior D))
    (hf'_nonpos : âˆ€ x âˆˆ interior D, deriv f x â‰¤ 0) : AntitoneOn f D := fun x hx y hy hxy => by
  simpa only [zero_mul, sub_nonpos] using
    hD.image_sub_le_mul_sub_of_deriv_le hf hf' hf'_nonpos x hx y hy hxy

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is nonpositive, then
`f` is an antitone function. -/
theorem antitone_of_deriv_nonpos {f : â„ â†’ â„} (hf : Differentiable â„ f) (hf' : âˆ€ x, deriv f x â‰¤ 0) :
    Antitone f :=
  antitoneOn_univ.1 <|
    antitoneOn_of_deriv_nonpos convex_univ hf.continuous.continuousOn hf.differentiableOn fun x _ =>
      hf' x

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonpositive, then
`f` is an antitone function on `D`. -/
lemma antitoneOn_of_hasDerivWithinAt_nonpos {D : Set â„} (hD : Convex â„ D) {f f' : â„ â†’ â„}
    (hf : ContinuousOn f D) (hf' : âˆ€ x âˆˆ interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'â‚€ : âˆ€ x âˆˆ interior D, f' x â‰¤ 0) : AntitoneOn f D :=
  antitoneOn_of_deriv_nonpos hD hf (fun _ hx â†¦ (hf' _ hx).differentiableWithinAt) fun x hx â†¦ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'â‚€ _ hx

/-- Let `f : â„ â†’ â„` be a differentiable function. If `f'` is nonpositive, then `f` is an antitone
function. -/
lemma antitone_of_hasDerivAt_nonpos {f f' : â„ â†’ â„} (hf : âˆ€ x, HasDerivAt f (f' x) x)
    (hf' : f' â‰¤ 0) : Antitone f :=
  antitone_of_deriv_nonpos (fun _ â†¦ (hf _).differentiableAt) fun x â†¦ by
    rw [(hf _).deriv]; exact hf' _

/-! ### Functions `f : E â†’ â„` -/

/-- Lagrange's **Mean Value Theorem**, applied to convex domains. -/
theorem domain_mvt {f : E â†’ â„} {s : Set E} {x y : E} {f' : E â†’ E â†’L[â„] â„}
    (hf : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (hs : Convex â„ s) (xs : x âˆˆ s) (ys : y âˆˆ s) :
    âˆƒ z âˆˆ segment â„ x y, f y - f x = f' z (y - x) := by
  -- Use `g = AffineMap.lineMap x y` to parametrize the segment
  set g : â„ â†’ E := fun t => AffineMap.lineMap x y t
  set I := Icc (0 : â„) 1
  have hsub : Ioo (0 : â„) 1 âŠ† I := Ioo_subset_Icc_self
  have hmaps : MapsTo g I s := hs.mapsTo_lineMap xs ys
  -- The one-variable function `f âˆ˜ g` has derivative `f' (g t) (y - x)` at each `t âˆˆ I`
  have hfg : âˆ€ t âˆˆ I, HasDerivWithinAt (f âˆ˜ g) (f' (g t) (y - x)) I t := fun t ht =>
    (hf _ (hmaps ht)).comp_hasDerivWithinAt t AffineMap.hasDerivWithinAt_lineMap hmaps
  -- apply 1-variable mean value theorem to pullback
  have hMVT : âˆƒ t âˆˆ Ioo (0 : â„) 1, f' (g t) (y - x) = (f (g 1) - f (g 0)) / (1 - 0) := by
    refine exists_hasDerivAt_eq_slope (f âˆ˜ g) _ (by norm_num) ?_ ?_
    Â· exact fun t Ht => (hfg t Ht).continuousWithinAt
    Â· exact fun t Ht => (hfg t <| hsub Ht).hasDerivAt (Icc_mem_nhds Ht.1 Ht.2)
  -- reinterpret on domain
  rcases hMVT with âŸ¨t, Ht, hMVT'âŸ©
  rw [segment_eq_image_lineMap, exists_mem_image]
  refine âŸ¨t, hsub Ht, ?_âŸ©
  simpa [g] using hMVT'.symm

section RCLike

/-!
### Vector-valued functions `f : E â†’ F`. Strict differentiability.

A `C^1` function is strictly differentiable, when the field is `â„` or `â„‚`. This follows from the
mean value inequality on balls, which is a particular case of the above results after restricting
the scalars to `â„`. Note that it does not make sense to talk of a convex set over `â„‚`, but balls
make sense and are enough. Many formulations of the mean value inequality could be generalized to
balls over `â„` or `â„‚`. For now, we only include the ones that we need.
-/

variable {ğ•œ : Type*} [RCLike ğ•œ] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] {H : Type*}
  [NormedAddCommGroup H] [NormedSpace ğ•œ H] {f : G â†’ H} {f' : G â†’ G â†’L[ğ•œ] H} {x : G}

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt
    (hder : âˆ€á¶  y in ğ“ x, HasFDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictFDerivAt f (f' x) x := by
  -- turn little-o definition of strict_fderiv into an epsilon-delta statement
  rw [hasStrictFDerivAt_iff_isLittleO, isLittleO_iff]
  refine fun c hc => Metric.eventually_nhds_iff_ball.mpr ?_
  -- the correct Îµ is the modulus of continuity of f'
  rcases Metric.mem_nhds_iff.mp (inter_mem hder (hcont <| ball_mem_nhds _ hc)) with âŸ¨Îµ, Îµ0, hÎµâŸ©
  refine âŸ¨Îµ, Îµ0, ?_âŸ©
  -- simplify formulas involving the product E Ã— E
  rintro âŸ¨a, bâŸ© h
  rw [â† ball_prod_same, prod_mk_mem_set_prod_eq] at h
  -- exploit the choice of Îµ as the modulus of continuity of f'
  have hf' : âˆ€ x' âˆˆ ball x Îµ, â€–f' x' - f' xâ€– â‰¤ c := fun x' H' => by
    rw [â† dist_eq_norm]
    exact le_of_lt (hÎµ H').2
  -- apply mean value theorem
  letI : NormedSpace â„ G := RestrictScalars.normedSpace â„ ğ•œ G
  refine (convex_ball _ _).norm_image_sub_le_of_norm_hasFDerivWithin_le' ?_ hf' h.2 h.1
  exact fun y hy => (hÎµ hy).1.hasFDerivWithinAt

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictDerivAt_of_hasDerivAt_of_continuousAt {f f' : ğ•œ â†’ G} {x : ğ•œ}
    (hder : âˆ€á¶  y in ğ“ x, HasDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictDerivAt f (f' x) x :=
  hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt (hder.mono fun _ hy => hy.hasFDerivAt) <|
    (smulRightL ğ•œ ğ•œ G 1).continuous.continuousAt.comp hcont

end RCLike
