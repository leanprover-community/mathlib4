/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.AffineMap
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.LocalExtr.Rolle
import Mathlib.Analysis.Convex.Normed
import Mathlib.Analysis.RCLike.Basic

#align_import analysis.calculus.mean_value from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# The mean value inequality and equalities

In this file we prove the following facts:

* `Convex.norm_image_sub_le_of_norm_deriv_le` : if `f` is differentiable on a convex set `s`
  and the norm of its derivative is bounded by `C`, then `f` is Lipschitz continuous on `s` with
  constant `C`; also a variant in which what is bounded by `C` is the norm of the difference of the
  derivative from a fixed linear map. This lemma and its versions are formulated using `RCLike`,
  so they work both for real and complex derivatives.

* `image_le_of*`, `image_norm_le_of_*` : several similar lemmas deducing `f x ‚â§ B x` or
  `‚Äñf x‚Äñ ‚â§ B x` from upper estimates on `f'` or `‚Äñf'‚Äñ`, respectively. These lemmas differ by
  their assumptions:

  * `of_liminf_*` lemmas assume that limit inferior of some ratio is less than `B' x`;
  * `of_deriv_right_*`, `of_norm_deriv_right_*` lemmas assume that the right derivative
    or its norm is less than `B' x`;
  * `of_*_lt_*` lemmas assume a strict inequality whenever `f x = B x` or `‚Äñf x‚Äñ = B x`;
  * `of_*_le_*` lemmas assume a non-strict inequality everywhere on `[a, b)`;
  * name of a lemma ends with `'` if (1) it assumes that `B` is continuous on `[a, b]`
    and has a right derivative at every point of `[a, b)`, and (2) the lemma has
    a counterpart assuming that `B` is differentiable everywhere on `‚Ñù`

* `norm_image_sub_le_*_segment` : if derivative of `f` on `[a, b]` is bounded above
  by a constant `C`, then `‚Äñf x - f a‚Äñ ‚â§ C * ‚Äñx - a‚Äñ`; several versions deal with
  right derivative and derivative within `[a, b]` (`HasDerivWithinAt` or `derivWithin`).

* `Convex.is_const_of_fderivWithin_eq_zero` : if a function has derivative `0` on a convex set `s`,
  then it is a constant on `s`.

* `exists_ratio_hasDerivAt_eq_ratio_slope` and `exists_ratio_deriv_eq_ratio_slope` :
  Cauchy's Mean Value Theorem.

* `exists_hasDerivAt_eq_slope` and `exists_deriv_eq_slope` : Lagrange's Mean Value Theorem.

* `domain_mvt` : Lagrange's Mean Value Theorem, applied to a segment in a convex domain.

* `Convex.image_sub_lt_mul_sub_of_deriv_lt`, `Convex.mul_sub_lt_image_sub_of_lt_deriv`,
  `Convex.image_sub_le_mul_sub_of_deriv_le`, `Convex.mul_sub_le_image_sub_of_le_deriv`,
  if `‚àÄ x, C (</‚â§/>/‚â•) (f' x)`, then `C * (y - x) (</‚â§/>/‚â•) (f y - f x)` whenever `x < y`.

* `monotoneOn_of_deriv_nonneg`, `antitoneOn_of_deriv_nonpos`,
  `strictMono_of_deriv_pos`, `strictAnti_of_deriv_neg` :
  if the derivative of a function is non-negative/non-positive/positive/negative, then
  the function is monotone/antitone/strictly monotone/strictly monotonically
  decreasing.

* `convexOn_of_deriv`, `convexOn_of_deriv2_nonneg` : if the derivative of a function
  is increasing or its second derivative is nonnegative, then the original function is convex.

* `hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt` : a C^1 function over the reals is
  strictly differentiable. (This is a corollary of the mean value inequality.)
-/


variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace ‚Ñù F]

open Metric Set Asymptotics ContinuousLinearMap Filter

open scoped Classical Topology NNReal

/-! ### One-dimensional fencing inequalities -/


/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x := by
  change Icc a b ‚äÜ { x | f x ‚â§ B x }
  set s := { x | f x ‚â§ B x } ‚à© Icc a b
  have A : ContinuousOn (fun x => (f x, B x)) (Icc a b) := hf.prod hB
  have : IsClosed s := by
    simp only [s, inter_comm]
    exact A.preimage_isClosed_of_isClosed isClosed_Icc OrderClosedTopology.isClosed_le'
  apply this.Icc_subset_of_forall_exists_gt ha
  rintro x ‚ü®hxB : f x ‚â§ B x, xab‚ü© y hy
  cases' hxB.lt_or_eq with hxB hxB
  ¬∑ -- If `f x < B x`, then all we need is continuity of both sides
    refine nonempty_of_mem (inter_mem ?_ (Ioc_mem_nhdsWithin_Ioi ‚ü®le_rfl, hy‚ü©))
    have : ‚àÄ·∂† x in ùìù[Icc a b] x, f x < B x :=
      A x (Ico_subset_Icc_self xab) (IsOpen.mem_nhds (isOpen_lt continuous_fst continuous_snd) hxB)
    have : ‚àÄ·∂† x in ùìù[>] x, f x < B x := nhdsWithin_le_of_mem (Icc_mem_nhdsWithin_Ioi xab) this
    exact this.mono fun y => le_of_lt
  ¬∑ rcases exists_between (bound x xab hxB) with ‚ü®r, hfr, hrB‚ü©
    specialize hf' x xab r hfr
    have HB : ‚àÄ·∂† z in ùìù[>] x, r < slope B x z :=
      (hasDerivWithinAt_iff_tendsto_slope' <| lt_irrefl x).1 (hB' x xab).Ioi_of_Ici
        (Ioi_mem_nhds hrB)
    obtain ‚ü®z, hfz, hzB, hz‚ü© : ‚àÉ z, slope f x z < r ‚àß r < slope B x z ‚àß z ‚àà Ioc x y :=
      (hf'.and_eventually (HB.and (Ioc_mem_nhdsWithin_Ioi ‚ü®le_rfl, hy‚ü©))).exists
    refine ‚ü®z, ?_, hz‚ü©
    have := (hfz.trans hzB).le
    rwa [slope_def_field, slope_def_field, div_le_div_right (sub_pos.2 hz.1), hxB,
      sub_le_sub_iff_right] at this
#align image_le_of_liminf_slope_right_lt_deriv_boundary' image_le_of_liminf_slope_right_lt_deriv_boundary'

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound
#align image_le_of_liminf_slope_right_lt_deriv_boundary image_le_of_liminf_slope_right_lt_deriv_boundary

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by `B'`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_le_deriv_boundary {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    -- `bound` actually says `liminf (f z - f x) / (z - x) ‚â§ B' x`
    (bound : ‚àÄ x ‚àà Ico a b, ‚àÄ r, B' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r) :
    ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x := by
  have Hr : ‚àÄ x ‚àà Icc a b, ‚àÄ r > 0, f x ‚â§ B x + r * (x - a) := fun x hx r hr => by
    apply image_le_of_liminf_slope_right_lt_deriv_boundary' hf bound
    ¬∑ rwa [sub_self, mul_zero, add_zero]
    ¬∑ exact hB.add (continuousOn_const.mul (continuousOn_id.sub continuousOn_const))
    ¬∑ intro x hx
      exact (hB' x hx).add (((hasDerivWithinAt_id x (Ici x)).sub_const a).const_mul r)
    ¬∑ intro x _ _
      rw [mul_one]
      exact (lt_add_iff_pos_right _).2 hr
    exact hx
  intro x hx
  have : ContinuousWithinAt (fun r => B x + r * (x - a)) (Ioi 0) 0 :=
    continuousWithinAt_const.add (continuousWithinAt_id.mul continuousWithinAt_const)
  convert continuousWithinAt_const.closure_le _ this (Hr x hx) using 1 <;> simp
#align image_le_of_liminf_slope_right_le_deriv_boundary image_le_of_liminf_slope_right_le_deriv_boundary

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_le hr) ha hB hB' bound
#align image_le_of_deriv_right_lt_deriv_boundary' image_le_of_deriv_right_lt_deriv_boundary'

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound
#align image_le_of_deriv_right_lt_deriv_boundary image_le_of_deriv_right_lt_deriv_boundary

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x ‚â§ B' x` on `[a, b)`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_le_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f' x ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary hf ha hB hB' fun x hx _ hr =>
    (hf' x hx).liminf_right_slope_le (lt_of_le_of_lt (bound x hx) hr)
#align image_le_of_deriv_right_le_deriv_boundary image_le_of_deriv_right_le_deriv_boundary

/-! ### Vector-valued functions `f : ‚Ñù ‚Üí E` -/


section

variable {f : ‚Ñù ‚Üí E} {a b : ‚Ñù}

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `B` has right derivative at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(‚Äñf z‚Äñ - ‚Äñf x‚Äñ) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary {E : Type*}
    [NormedAddCommGroup E] {f : ‚Ñù ‚Üí E} {f' : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (‚Äñf z‚Äñ - ‚Äñf x‚Äñ) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope (norm ‚àò f) x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' (continuous_norm.comp_continuousOn hf) hf' ha hB
    hB' bound
#align image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* the norm of `f'` is strictly less than `B'` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary' {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí ‚Äñf' x‚Äñ < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le hr) ha hB hB' bound
#align image_norm_le_of_norm_deriv_right_lt_deriv_boundary' image_norm_le_of_norm_deriv_right_lt_deriv_boundary'

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* the norm of `f'` is strictly less than `B'` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí ‚Äñf' x‚Äñ < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_norm_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound
#align image_norm_le_of_norm_deriv_right_lt_deriv_boundary image_norm_le_of_norm_deriv_right_lt_deriv_boundary

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* we have `‚Äñf' x‚Äñ ‚â§ B x` everywhere on `[a, b)`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary' {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary (continuous_norm.comp_continuousOn hf) ha hB hB'
    fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le ((bound x hx).trans_lt hr)
#align image_norm_le_of_norm_deriv_right_le_deriv_boundary' image_norm_le_of_norm_deriv_right_le_deriv_boundary'

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* we have `‚Äñf' x‚Äñ ‚â§ B x` everywhere on `[a, b)`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_norm_deriv_right_le_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound
#align image_norm_le_of_norm_deriv_right_le_deriv_boundary image_norm_le_of_norm_deriv_right_le_deriv_boundary

/-- A function on `[a, b]` with the norm of the right derivative bounded by `C`
satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`. -/
theorem norm_image_sub_le_of_norm_deriv_right_le_segment {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ C) : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  let g x := f x - f a
  have hg : ContinuousOn g (Icc a b) := hf.sub continuousOn_const
  have hg' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt g (f' x) (Ici x) x := by
    intro x hx
    simpa using (hf' x hx).sub (hasDerivWithinAt_const _ _ _)
  let B x := C * (x - a)
  have hB : ‚àÄ x, HasDerivAt B C x := by
    intro x
    simpa using (hasDerivAt_const x C).mul ((hasDerivAt_id x).sub (hasDerivAt_const x a))
  convert image_norm_le_of_norm_deriv_right_le_deriv_boundary hg hg' _ hB bound
  simp only [g, B]; rw [sub_self, norm_zero, sub_self, mul_zero]
#align norm_image_sub_le_of_norm_deriv_right_le_segment norm_image_sub_le_of_norm_deriv_right_le_segment

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ C) : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  refine
    norm_image_sub_le_of_norm_deriv_right_le_segment (fun x hx => (hf x hx).continuousWithinAt)
      (fun x hx => ?_) bound
  exact (hf x <| Ico_subset_Icc_self hx).mono_of_mem (Icc_mem_nhdsWithin_Ici hx)
#align norm_image_sub_le_of_norm_deriv_le_segment' norm_image_sub_le_of_norm_deriv_le_segment'

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`, `derivWithin`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment {C : ‚Ñù} (hf : DifferentiableOn ‚Ñù f (Icc a b))
    (bound : ‚àÄ x ‚àà Ico a b, ‚ÄñderivWithin f (Icc a b) x‚Äñ ‚â§ C) :
    ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  refine norm_image_sub_le_of_norm_deriv_le_segment' ?_ bound
  exact fun x hx => (hf x hx).hasDerivWithinAt
#align norm_image_sub_le_of_norm_deriv_le_segment norm_image_sub_le_of_norm_deriv_le_segment

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚Äñf 1 - f 0‚Äñ ‚â§ C`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà Icc (0 : ‚Ñù) 1, HasDerivWithinAt f (f' x) (Icc (0 : ‚Ñù) 1) x)
    (bound : ‚àÄ x ‚àà Ico (0 : ‚Ñù) 1, ‚Äñf' x‚Äñ ‚â§ C) : ‚Äñf 1 - f 0‚Äñ ‚â§ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment' hf bound 1 (right_mem_Icc.2 zero_le_one)
#align norm_image_sub_le_of_norm_deriv_le_segment_01' norm_image_sub_le_of_norm_deriv_le_segment_01'

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚Äñf 1 - f 0‚Äñ ‚â§ C`, `derivWithin` version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01 {C : ‚Ñù}
    (hf : DifferentiableOn ‚Ñù f (Icc (0 : ‚Ñù) 1))
    (bound : ‚àÄ x ‚àà Ico (0 : ‚Ñù) 1, ‚ÄñderivWithin f (Icc (0 : ‚Ñù) 1) x‚Äñ ‚â§ C) : ‚Äñf 1 - f 0‚Äñ ‚â§ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment hf bound 1 (right_mem_Icc.2 zero_le_one)
#align norm_image_sub_le_of_norm_deriv_le_segment_01 norm_image_sub_le_of_norm_deriv_le_segment_01

theorem constant_of_has_deriv_right_zero (hcont : ContinuousOn f (Icc a b))
    (hderiv : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f 0 (Ici x) x) : ‚àÄ x ‚àà Icc a b, f x = f a := by
  have : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ 0 * (x - a) := fun x hx =>
    norm_image_sub_le_of_norm_deriv_right_le_segment hcont hderiv (fun _ _ => norm_zero.le) x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using this
#align constant_of_has_deriv_right_zero constant_of_has_deriv_right_zero

theorem constant_of_derivWithin_zero (hdiff : DifferentiableOn ‚Ñù f (Icc a b))
    (hderiv : ‚àÄ x ‚àà Ico a b, derivWithin f (Icc a b) x = 0) : ‚àÄ x ‚àà Icc a b, f x = f a := by
  have H : ‚àÄ x ‚àà Ico a b, ‚ÄñderivWithin f (Icc a b) x‚Äñ ‚â§ 0 := by
    simpa only [norm_le_zero_iff] using fun x hx => hderiv x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using fun x hx =>
    norm_image_sub_le_of_norm_deriv_le_segment hdiff H x hx
#align constant_of_deriv_within_zero constant_of_derivWithin_zero

variable {f' g : ‚Ñù ‚Üí E}

/-- If two continuous functions on `[a, b]` have the same right derivative and are equal at `a`,
  then they are equal everywhere on `[a, b]`. -/
theorem eq_of_has_deriv_right_eq (derivf : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (derivg : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt g (f' x) (Ici x) x) (fcont : ContinuousOn f (Icc a b))
    (gcont : ContinuousOn g (Icc a b)) (hi : f a = g a) : ‚àÄ y ‚àà Icc a b, f y = g y := by
  simp only [‚Üê @sub_eq_zero _ _ (f _)] at hi ‚ä¢
  exact hi ‚ñ∏ constant_of_has_deriv_right_zero (fcont.sub gcont) fun y hy => by
    simpa only [sub_self] using (derivf y hy).sub (derivg y hy)
#align eq_of_has_deriv_right_eq eq_of_has_deriv_right_eq

/-- If two differentiable functions on `[a, b]` have the same derivative within `[a, b]` everywhere
  on `[a, b)` and are equal at `a`, then they are equal everywhere on `[a, b]`. -/
theorem eq_of_derivWithin_eq (fdiff : DifferentiableOn ‚Ñù f (Icc a b))
    (gdiff : DifferentiableOn ‚Ñù g (Icc a b))
    (hderiv : EqOn (derivWithin f (Icc a b)) (derivWithin g (Icc a b)) (Ico a b)) (hi : f a = g a) :
    ‚àÄ y ‚àà Icc a b, f y = g y := by
  have A : ‚àÄ y ‚àà Ico a b, HasDerivWithinAt f (derivWithin f (Icc a b) y) (Ici y) y := fun y hy =>
    (fdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem (Icc_mem_nhdsWithin_Ici hy)
  have B : ‚àÄ y ‚àà Ico a b, HasDerivWithinAt g (derivWithin g (Icc a b) y) (Ici y) y := fun y hy =>
    (gdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem (Icc_mem_nhdsWithin_Ici hy)
  exact
    eq_of_has_deriv_right_eq A (fun y hy => (hderiv hy).symm ‚ñ∏ B y hy) fdiff.continuousOn
      gdiff.continuousOn hi
#align eq_of_deriv_within_eq eq_of_derivWithin_eq

end

/-!
### Vector-valued functions `f : E ‚Üí G`

Theorems in this section work both for real and complex differentiable functions. We use assumptions
`[RCLike ùïú] [NormedSpace ùïú E] [NormedSpace ùïú G]` to achieve this result. For the domain `E` we
also assume `[NormedSpace ‚Ñù E]` to have a notion of a `Convex` set. -/

section

variable {ùïú G : Type*} [RCLike ùïú] [NormedSpace ùïú E] [NormedAddCommGroup G] [NormedSpace ùïú G]

namespace Convex

variable {f g : E ‚Üí G} {C : ‚Ñù} {s : Set E} {x y : E} {f' g' : E ‚Üí E ‚ÜíL[ùïú] G} {œÜ : E ‚ÜíL[ùïú] G}

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`, then
the function is `C`-Lipschitz. Version with `HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s)
    (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := by
  letI : NormedSpace ‚Ñù G := RestrictScalars.normedSpace ‚Ñù ùïú G
  /- By composition with `AffineMap.lineMap x y`, we reduce to a statement for functions defined
    on `[0,1]`, for which it is proved in `norm_image_sub_le_of_norm_deriv_le_segment`.
    We just have to check the differentiability of the composition and bounds on its derivative,
    which is straightforward but tedious for lack of automation. -/
  set g := (AffineMap.lineMap x y : ‚Ñù ‚Üí E)
  have segm : MapsTo g (Icc 0 1 : Set ‚Ñù) s := hs.mapsTo_lineMap xs ys
  have hD : ‚àÄ t ‚àà Icc (0 : ‚Ñù) 1,
      HasDerivWithinAt (f ‚àò g) (f' (g t) (y - x)) (Icc 0 1) t := fun t ht => by
    simpa using ((hf (g t) (segm ht)).restrictScalars ‚Ñù).comp_hasDerivWithinAt _
      AffineMap.hasDerivWithinAt_lineMap segm
  have bound : ‚àÄ t ‚àà Ico (0 : ‚Ñù) 1, ‚Äñf' (g t) (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := fun t ht =>
    le_of_opNorm_le _ (bound _ <| segm <| Ico_subset_Icc_self ht) _
  simpa [g] using norm_image_sub_le_of_norm_deriv_le_segment_01' hD bound
#align convex.norm_image_sub_le_of_norm_has_fderiv_within_le Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `HasFDerivWithinAt` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasFDerivWithin_le {C : ‚Ñù‚â•0}
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ‚Çä ‚â§ C)
    (hs : Convex ‚Ñù s) : LipschitzOnWith C f s := by
  rw [lipschitzOnWith_iff_norm_sub_le]
  intro x x_in y y_in
  exact hs.norm_image_sub_le_of_norm_hasFDerivWithin_le hf bound y_in x_in
#align convex.lipschitz_on_with_of_nnnorm_has_fderiv_within_le Convex.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E ‚Üí G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : ‚Ñù‚â•0` larger than `‚Äñf' x‚Äñ‚Çä`, `f` is
`K`-Lipschitz on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt` for a version that claims
existence of `K` instead of an explicit estimate. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt (hs : Convex ‚Ñù s)
    {f : E ‚Üí G} (hder : ‚àÄ·∂† y in ùìù[s] x, HasFDerivWithinAt f (f' y) s y)
    (hcont : ContinuousWithinAt f' s x) (K : ‚Ñù‚â•0) (hK : ‚Äñf' x‚Äñ‚Çä < K) :
    ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith K f t := by
  obtain ‚ü®Œµ, Œµ0, hŒµ‚ü© : ‚àÉ Œµ > 0,
      ball x Œµ ‚à© s ‚äÜ { y | HasFDerivWithinAt f (f' y) s y ‚àß ‚Äñf' y‚Äñ‚Çä < K } :=
    mem_nhdsWithin_iff.1 (hder.and <| hcont.nnnorm.eventually (gt_mem_nhds hK))
  rw [inter_comm] at hŒµ
  refine ‚ü®s ‚à© ball x Œµ, inter_mem_nhdsWithin _ (ball_mem_nhds _ Œµ0), ?_‚ü©
  exact
    (hs.inter (convex_ball _ _)).lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
      (fun y hy => (hŒµ hy).1.mono (inter_subset_left _ _)) fun y hy => (hŒµ hy).2.le
#align convex.exists_nhds_within_lipschitz_on_with_of_has_fderiv_within_at_of_nnnorm_lt Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E ‚Üí G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : ‚Ñù‚â•0` larger than `‚Äñf' x‚Äñ‚Çä`, `f` is Lipschitz
on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt` for a version
with an explicit estimate on the Lipschitz constant. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt (hs : Convex ‚Ñù s) {f : E ‚Üí G}
    (hder : ‚àÄ·∂† y in ùìù[s] x, HasFDerivWithinAt f (f' y) s y) (hcont : ContinuousWithinAt f' s x) :
    ‚àÉ K, ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith K f t :=
  (exists_gt _).imp <|
    hs.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt hder hcont
#align convex.exists_nhds_within_lipschitz_on_with_of_has_fderiv_within_at Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt

/-- The mean value theorem on a convex set: if the derivative of a function within this set is
bounded by `C`, then the function is `C`-Lipschitz. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys
#align convex.norm_image_sub_le_of_norm_fderiv_within_le Convex.norm_image_sub_le_of_norm_fderivWithin_le

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderivWithin` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderivWithin_le {C : ‚Ñù‚â•0} (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound
#align convex.lipschitz_on_with_of_nnnorm_fderiv_within_le Convex.lipschitzOnWith_of_nnnorm_fderivWithin_le

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys
#align convex.norm_image_sub_le_of_norm_fderiv_le Convex.norm_image_sub_le_of_norm_fderiv_le

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderiv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderiv_le {C : ‚Ñù‚â•0} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound
#align convex.lipschitz_on_with_of_nnnorm_fderiv_le Convex.lipschitzOnWith_of_nnnorm_fderiv_le

/-- The mean value theorem: if the derivative of a function is bounded by `C`, then the function is
`C`-Lipschitz. Version with `fderiv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_fderiv_le
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f : E ‚Üí G}
    {C : ‚Ñù‚â•0} (hf : Differentiable ùïú f)
    (bound : ‚àÄ x, ‚Äñfderiv ùïú f x‚Äñ‚Çä ‚â§ C) : LipschitzWith C f := by
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  rw [‚Üê lipschitzOn_univ]
  exact lipschitzOnWith_of_nnnorm_fderiv_le (fun x _ ‚Ü¶ hf x) (fun x _ ‚Ü¶ bound x) convex_univ

/-- Variant of the mean value inequality on a convex set, using a bound on the difference between
the derivative and a fixed linear map, rather than a bound on the derivative itself. Version with
`HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x - œÜ‚Äñ ‚â§ C)
    (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := by
  /- We subtract `œÜ` to define a new function `g` for which `g' = 0`, for which the previous theorem
    applies, `Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le`. Then, we just need to glue
    together the pieces, expressing back `f` in terms of `g`. -/
  let g y := f y - œÜ y
  have hg : ‚àÄ x ‚àà s, HasFDerivWithinAt g (f' x - œÜ) s x := fun x xs =>
    (hf x xs).sub œÜ.hasFDerivWithinAt
  calc
    ‚Äñf y - f x - œÜ (y - x)‚Äñ = ‚Äñf y - f x - (œÜ y - œÜ x)‚Äñ := by simp
    _ = ‚Äñf y - œÜ y - (f x - œÜ x)‚Äñ := by congr 1; abel
    _ = ‚Äñg y - g x‚Äñ := by simp
    _ ‚â§ C * ‚Äñy - x‚Äñ := Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le hg bound hs xs ys
#align convex.norm_image_sub_le_of_norm_has_fderiv_within_le' Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'

/-- Variant of the mean value inequality on a convex set. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le' (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x - œÜ‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le' (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys
#align convex.norm_image_sub_le_of_norm_fderiv_within_le' Convex.norm_image_sub_le_of_norm_fderivWithin_le'

/-- Variant of the mean value inequality on a convex set. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le' (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x - œÜ‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys
#align convex.norm_image_sub_le_of_norm_fderiv_le' Convex.norm_image_sub_le_of_norm_fderiv_le'

/-- If a function has zero Fr√©chet derivative at every point of a convex set,
then it is a constant on this set. -/
theorem is_const_of_fderivWithin_eq_zero (hs : Convex ‚Ñù s) (hf : DifferentiableOn ùïú f s)
    (hf' : ‚àÄ x ‚àà s, fderivWithin ùïú f s x = 0) (hx : x ‚àà s) (hy : y ‚àà s) : f x = f y := by
  have bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ ‚â§ 0 := fun x hx => by
    simp only [hf' x hx, norm_zero, le_rfl]
  simpa only [(dist_eq_norm _ _).symm, zero_mul, dist_le_zero, eq_comm] using
    hs.norm_image_sub_le_of_norm_fderivWithin_le hf bound hx hy
#align convex.is_const_of_fderiv_within_eq_zero Convex.is_const_of_fderivWithin_eq_zero

theorem _root_.is_const_of_fderiv_eq_zero
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f : E ‚Üí G}
    (hf : Differentiable ùïú f) (hf' : ‚àÄ x, fderiv ùïú f x = 0)
    (x y : E) : f x = f y := by
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  exact convex_univ.is_const_of_fderivWithin_eq_zero hf.differentiableOn
    (fun x _ => by rw [fderivWithin_univ]; exact hf' x) trivial trivial
#align is_const_of_fderiv_eq_zero is_const_of_fderiv_eq_zero

/-- If two functions have equal Fr√©chet derivatives at every point of a convex set, and are equal at
one point in that set, then they are equal on that set. -/
theorem eqOn_of_fderivWithin_eq (hs : Convex ‚Ñù s) (hf : DifferentiableOn ùïú f s)
    (hg : DifferentiableOn ùïú g s) (hs' : UniqueDiffOn ùïú s)
    (hf' : ‚àÄ x ‚àà s, fderivWithin ùïú f s x = fderivWithin ùïú g s x) (hx : x ‚àà s) (hfgx : f x = g x) :
    s.EqOn f g := fun y hy => by
  suffices f x - g x = f y - g y by rwa [hfgx, sub_self, eq_comm, sub_eq_zero] at this
  refine hs.is_const_of_fderivWithin_eq_zero (hf.sub hg) (fun z hz => ?_) hx hy
  rw [fderivWithin_sub (hs' _ hz) (hf _ hz) (hg _ hz), sub_eq_zero, hf' _ hz]
#align convex.eq_on_of_fderiv_within_eq Convex.eqOn_of_fderivWithin_eq

theorem _root_.eq_of_fderiv_eq
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f g : E ‚Üí G}
    (hf : Differentiable ùïú f) (hg : Differentiable ùïú g)
    (hf' : ‚àÄ x, fderiv ùïú f x = fderiv ùïú g x) (x : E) (hfgx : f x = g x) : f = g := by
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  suffices Set.univ.EqOn f g from funext fun x => this <| mem_univ x
  exact convex_univ.eqOn_of_fderivWithin_eq hf.differentiableOn hg.differentiableOn
    uniqueDiffOn_univ (fun x _ => by simpa using hf' _) (mem_univ _) hfgx
#align eq_of_fderiv_eq eq_of_fderiv_eq

end Convex

namespace Convex

variable {f f' : ùïú ‚Üí G} {s : Set ùïú} {x y : ùïú}

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `HasDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasDerivWithin_le {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s)
    (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs xs ys
#align convex.norm_image_sub_le_of_norm_has_deriv_within_le Convex.norm_image_sub_le_of_norm_hasDerivWithin_le

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `HasDerivWithinAt` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasDerivWithin_le {C : ‚Ñù‚â•0} (hs : Convex ‚Ñù s)
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ‚Çä ‚â§ C) :
    LipschitzOnWith C f s :=
  Convex.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs
#align convex.lipschitz_on_with_of_nnnorm_has_deriv_within_le Convex.lipschitzOnWith_of_nnnorm_hasDerivWithin_le

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function within
this set is bounded by `C`, then the function is `C`-Lipschitz. Version with `derivWithin` -/
theorem norm_image_sub_le_of_norm_derivWithin_le {C : ‚Ñù} (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñderivWithin f s x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound xs
    ys
#align convex.norm_image_sub_le_of_norm_deriv_within_le Convex.norm_image_sub_le_of_norm_derivWithin_le

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `derivWithin` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_derivWithin_le {C : ‚Ñù‚â•0} (hs : Convex ‚Ñù s)
    (hf : DifferentiableOn ùïú f s) (bound : ‚àÄ x ‚àà s, ‚ÄñderivWithin f s x‚Äñ‚Çä ‚â§ C) :
    LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound
#align convex.lipschitz_on_with_of_nnnorm_deriv_within_le Convex.lipschitzOnWith_of_nnnorm_derivWithin_le

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `deriv`. -/
theorem norm_image_sub_le_of_norm_deriv_le {C : ‚Ñù} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñderiv f x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound xs ys
#align convex.norm_image_sub_le_of_norm_deriv_le Convex.norm_image_sub_le_of_norm_deriv_le

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `deriv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_deriv_le {C : ‚Ñù‚â•0} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñderiv f x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound
#align convex.lipschitz_on_with_of_nnnorm_deriv_le Convex.lipschitzOnWith_of_nnnorm_deriv_le

/-- The mean value theorem set in dimension 1: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `deriv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_deriv_le {C : ‚Ñù‚â•0} (hf : Differentiable ùïú f)
    (bound : ‚àÄ x, ‚Äñderiv f x‚Äñ‚Çä ‚â§ C) : LipschitzWith C f :=
  lipschitzOn_univ.1 <|
    convex_univ.lipschitzOnWith_of_nnnorm_deriv_le (fun x _ => hf x) fun x _ => bound x
#align lipschitz_with_of_nnnorm_deriv_le lipschitzWith_of_nnnorm_deriv_le

/-- If `f : ùïú ‚Üí G`, `ùïú = R` or `ùïú = ‚ÑÇ`, is differentiable everywhere and its derivative equal zero,
then it is a constant function. -/
theorem _root_.is_const_of_deriv_eq_zero (hf : Differentiable ùïú f) (hf' : ‚àÄ x, deriv f x = 0)
    (x y : ùïú) : f x = f y :=
  is_const_of_fderiv_eq_zero hf (fun z => by ext; simp [‚Üê deriv_fderiv, hf']) _ _
#align is_const_of_deriv_eq_zero is_const_of_deriv_eq_zero

end Convex

end

/-! ### Functions `[a, b] ‚Üí ‚Ñù`. -/

section Interval

-- Declare all variables here to make sure they come in a correct order
variable (f f' : ‚Ñù ‚Üí ‚Ñù) {a b : ‚Ñù} (hab : a < b) (hfc : ContinuousOn f (Icc a b))
  (hff' : ‚àÄ x ‚àà Ioo a b, HasDerivAt f (f' x) x) (hfd : DifferentiableOn ‚Ñù f (Ioo a b))
  (g g' : ‚Ñù ‚Üí ‚Ñù) (hgc : ContinuousOn g (Icc a b)) (hgg' : ‚àÄ x ‚àà Ioo a b, HasDerivAt g (g' x) x)
  (hgd : DifferentiableOn ‚Ñù g (Ioo a b))

/-- Cauchy's **Mean Value Theorem**, `HasDerivAt` version. -/
theorem exists_ratio_hasDerivAt_eq_ratio_slope :
    ‚àÉ c ‚àà Ioo a b, (g b - g a) * f' c = (f b - f a) * g' c := by
  let h x := (g b - g a) * f x - (f b - f a) * g x
  have hI : h a = h b := by simp only [h]; ring
  let h' x := (g b - g a) * f' x - (f b - f a) * g' x
  have hhh' : ‚àÄ x ‚àà Ioo a b, HasDerivAt h (h' x) x := fun x hx =>
    ((hff' x hx).const_mul (g b - g a)).sub ((hgg' x hx).const_mul (f b - f a))
  have hhc : ContinuousOn h (Icc a b) :=
    (continuousOn_const.mul hfc).sub (continuousOn_const.mul hgc)
  rcases exists_hasDerivAt_eq_zero hab hhc hI hhh' with ‚ü®c, cmem, hc‚ü©
  exact ‚ü®c, cmem, sub_eq_zero.1 hc‚ü©
#align exists_ratio_has_deriv_at_eq_ratio_slope exists_ratio_hasDerivAt_eq_ratio_slope

/-- Cauchy's **Mean Value Theorem**, extended `HasDerivAt` version. -/
theorem exists_ratio_hasDerivAt_eq_ratio_slope' {lfa lga lfb lgb : ‚Ñù}
    (hff' : ‚àÄ x ‚àà Ioo a b, HasDerivAt f (f' x) x) (hgg' : ‚àÄ x ‚àà Ioo a b, HasDerivAt g (g' x) x)
    (hfa : Tendsto f (ùìù[>] a) (ùìù lfa)) (hga : Tendsto g (ùìù[>] a) (ùìù lga))
    (hfb : Tendsto f (ùìù[<] b) (ùìù lfb)) (hgb : Tendsto g (ùìù[<] b) (ùìù lgb)) :
    ‚àÉ c ‚àà Ioo a b, (lgb - lga) * f' c = (lfb - lfa) * g' c := by
  let h x := (lgb - lga) * f x - (lfb - lfa) * g x
  have hha : Tendsto h (ùìù[>] a) (ùìù <| lgb * lfa - lfb * lga) := by
    have : Tendsto h (ùìù[>] a) (ùìù <| (lgb - lga) * lfa - (lfb - lfa) * lga) :=
      (tendsto_const_nhds.mul hfa).sub (tendsto_const_nhds.mul hga)
    convert this using 2
    ring
  have hhb : Tendsto h (ùìù[<] b) (ùìù <| lgb * lfa - lfb * lga) := by
    have : Tendsto h (ùìù[<] b) (ùìù <| (lgb - lga) * lfb - (lfb - lfa) * lgb) :=
      (tendsto_const_nhds.mul hfb).sub (tendsto_const_nhds.mul hgb)
    convert this using 2
    ring
  let h' x := (lgb - lga) * f' x - (lfb - lfa) * g' x
  have hhh' : ‚àÄ x ‚àà Ioo a b, HasDerivAt h (h' x) x := by
    intro x hx
    exact ((hff' x hx).const_mul _).sub ((hgg' x hx).const_mul _)
  rcases exists_hasDerivAt_eq_zero' hab hha hhb hhh' with ‚ü®c, cmem, hc‚ü©
  exact ‚ü®c, cmem, sub_eq_zero.1 hc‚ü©
#align exists_ratio_has_deriv_at_eq_ratio_slope' exists_ratio_hasDerivAt_eq_ratio_slope'

/-- Lagrange's Mean Value Theorem, `HasDerivAt` version -/
theorem exists_hasDerivAt_eq_slope : ‚àÉ c ‚àà Ioo a b, f' c = (f b - f a) / (b - a) := by
  obtain ‚ü®c, cmem, hc‚ü© : ‚àÉ c ‚àà Ioo a b, (b - a) * f' c = (f b - f a) * 1 :=
    exists_ratio_hasDerivAt_eq_ratio_slope f f' hab hfc hff' id 1 continuousOn_id
      fun x _ => hasDerivAt_id x
  use c, cmem
  rwa [mul_one, mul_comm, ‚Üê eq_div_iff (sub_ne_zero.2 hab.ne')] at hc
#align exists_has_deriv_at_eq_slope exists_hasDerivAt_eq_slope

/-- Cauchy's Mean Value Theorem, `deriv` version. -/
theorem exists_ratio_deriv_eq_ratio_slope :
    ‚àÉ c ‚àà Ioo a b, (g b - g a) * deriv f c = (f b - f a) * deriv g c :=
  exists_ratio_hasDerivAt_eq_ratio_slope f (deriv f) hab hfc
    (fun x hx => ((hfd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt) g
    (deriv g) hgc fun x hx =>
    ((hgd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt
#align exists_ratio_deriv_eq_ratio_slope exists_ratio_deriv_eq_ratio_slope

/-- Cauchy's Mean Value Theorem, extended `deriv` version. -/
theorem exists_ratio_deriv_eq_ratio_slope' {lfa lga lfb lgb : ‚Ñù}
    (hdf : DifferentiableOn ‚Ñù f <| Ioo a b) (hdg : DifferentiableOn ‚Ñù g <| Ioo a b)
    (hfa : Tendsto f (ùìù[>] a) (ùìù lfa)) (hga : Tendsto g (ùìù[>] a) (ùìù lga))
    (hfb : Tendsto f (ùìù[<] b) (ùìù lfb)) (hgb : Tendsto g (ùìù[<] b) (ùìù lgb)) :
    ‚àÉ c ‚àà Ioo a b, (lgb - lga) * deriv f c = (lfb - lfa) * deriv g c :=
  exists_ratio_hasDerivAt_eq_ratio_slope' _ _ hab _ _
    (fun x hx => ((hdf x hx).differentiableAt <| Ioo_mem_nhds hx.1 hx.2).hasDerivAt)
    (fun x hx => ((hdg x hx).differentiableAt <| Ioo_mem_nhds hx.1 hx.2).hasDerivAt) hfa hga hfb hgb
#align exists_ratio_deriv_eq_ratio_slope' exists_ratio_deriv_eq_ratio_slope'

/-- Lagrange's **Mean Value Theorem**, `deriv` version. -/
theorem exists_deriv_eq_slope : ‚àÉ c ‚àà Ioo a b, deriv f c = (f b - f a) / (b - a) :=
  exists_hasDerivAt_eq_slope f (deriv f) hab hfc fun x hx =>
    ((hfd x hx).differentiableAt <| IsOpen.mem_nhds isOpen_Ioo hx).hasDerivAt
#align exists_deriv_eq_slope exists_deriv_eq_slope

end Interval

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C < f'`, then
`f` grows faster than `C * x` on `D`, i.e., `C * (y - x) < f y - f x` whenever `x, y ‚àà D`,
`x < y`. -/
theorem Convex.mul_sub_lt_image_sub_of_lt_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D)) {C}
    (hf'_gt : ‚àÄ x ‚àà interior D, C < deriv f x) :
    ‚àÄ·µâ (x ‚àà D) (y ‚àà D), x < y ‚Üí C * (y - x) < f y - f x := by
  intro x hx y hy hxy
  have hxyD : Icc x y ‚äÜ D := hD.ordConnected.out hx hy
  have hxyD' : Ioo x y ‚äÜ interior D :=
    subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hxyD‚ü©
  obtain ‚ü®a, a_mem, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy (hf.mono hxyD) (hf'.mono hxyD')
  have : C < (f y - f x) / (y - x) := ha ‚ñ∏ hf'_gt _ (hxyD' a_mem)
  exact (lt_div_iff (sub_pos.2 hxy)).1 this
#align convex.mul_sub_lt_image_sub_of_lt_deriv Convex.mul_sub_lt_image_sub_of_lt_deriv

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `C < f'`, then `f` grows faster than
`C * x`, i.e., `C * (y - x) < f y - f x` whenever `x < y`. -/
theorem mul_sub_lt_image_sub_of_lt_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) {C}
    (hf'_gt : ‚àÄ x, C < deriv f x) ‚¶Éx y‚¶Ñ (hxy : x < y) : C * (y - x) < f y - f x :=
  convex_univ.mul_sub_lt_image_sub_of_lt_deriv hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => hf'_gt x) x trivial y trivial hxy
#align mul_sub_lt_image_sub_of_lt_deriv mul_sub_lt_image_sub_of_lt_deriv

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `C ‚â§ f'`, then
`f` grows at least as fast as `C * x` on `D`, i.e., `C * (y - x) ‚â§ f y - f x` whenever `x, y ‚àà D`,
`x ‚â§ y`. -/
theorem Convex.mul_sub_le_image_sub_of_le_deriv {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D)) {C}
    (hf'_ge : ‚àÄ x ‚àà interior D, C ‚â§ deriv f x) :
    ‚àÄ·µâ (x ‚àà D) (y ‚àà D), x ‚â§ y ‚Üí C * (y - x) ‚â§ f y - f x := by
  intro x hx y hy hxy
  cases' eq_or_lt_of_le hxy with hxy' hxy'
  ¬∑ rw [hxy', sub_self, sub_self, mul_zero]
  have hxyD : Icc x y ‚äÜ D := hD.ordConnected.out hx hy
  have hxyD' : Ioo x y ‚äÜ interior D :=
    subset_sUnion_of_mem ‚ü®isOpen_Ioo, Ioo_subset_Icc_self.trans hxyD‚ü©
  obtain ‚ü®a, a_mem, ha‚ü© : ‚àÉ a ‚àà Ioo x y, deriv f a = (f y - f x) / (y - x) :=
    exists_deriv_eq_slope f hxy' (hf.mono hxyD) (hf'.mono hxyD')
  have : C ‚â§ (f y - f x) / (y - x) := ha ‚ñ∏ hf'_ge _ (hxyD' a_mem)
  exact (le_div_iff (sub_pos.2 hxy')).1 this
#align convex.mul_sub_le_image_sub_of_le_deriv Convex.mul_sub_le_image_sub_of_le_deriv

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `C ‚â§ f'`, then `f` grows at least as fast
as `C * x`, i.e., `C * (y - x) ‚â§ f y - f x` whenever `x ‚â§ y`. -/
theorem mul_sub_le_image_sub_of_le_deriv {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) {C}
    (hf'_ge : ‚àÄ x, C ‚â§ deriv f x) ‚¶Éx y‚¶Ñ (hxy : x ‚â§ y) : C * (y - x) ‚â§ f y - f x :=
  convex_univ.mul_sub_le_image_sub_of_le_deriv hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => hf'_ge x) x trivial y trivial hxy
#align mul_sub_le_image_sub_of_le_deriv mul_sub_le_image_sub_of_le_deriv

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' < C`, then
`f` grows slower than `C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x, y ‚àà D`,
`x < y`. -/
theorem Convex.image_sub_lt_mul_sub_of_deriv_lt {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D)) {C}
    (lt_hf' : ‚àÄ x ‚àà interior D, deriv f x < C) (x : ‚Ñù) (hx : x ‚àà D) (y : ‚Ñù) (hy : y ‚àà D)
    (hxy : x < y) : f y - f x < C * (y - x) :=
  have hf'_gt : ‚àÄ x ‚àà interior D, -C < deriv (fun y => -f y) x := fun x hx => by
    rw [deriv.neg, neg_lt_neg_iff]
    exact lt_hf' x hx
  by linarith [hD.mul_sub_lt_image_sub_of_lt_deriv hf.neg hf'.neg hf'_gt x hx y hy hxy]
#align convex.image_sub_lt_mul_sub_of_deriv_lt Convex.image_sub_lt_mul_sub_of_deriv_lt

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f' < C`, then `f` grows slower than
`C * x` on `D`, i.e., `f y - f x < C * (y - x)` whenever `x < y`. -/
theorem image_sub_lt_mul_sub_of_deriv_lt {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) {C}
    (lt_hf' : ‚àÄ x, deriv f x < C) ‚¶Éx y‚¶Ñ (hxy : x < y) : f y - f x < C * (y - x) :=
  convex_univ.image_sub_lt_mul_sub_of_deriv_lt hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => lt_hf' x) x trivial y trivial hxy
#align image_sub_lt_mul_sub_of_deriv_lt image_sub_lt_mul_sub_of_deriv_lt

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f' ‚â§ C`, then
`f` grows at most as fast as `C * x` on `D`, i.e., `f y - f x ‚â§ C * (y - x)` whenever `x, y ‚àà D`,
`x ‚â§ y`. -/
theorem Convex.image_sub_le_mul_sub_of_deriv_le {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D)) {C}
    (le_hf' : ‚àÄ x ‚àà interior D, deriv f x ‚â§ C) (x : ‚Ñù) (hx : x ‚àà D) (y : ‚Ñù) (hy : y ‚àà D)
    (hxy : x ‚â§ y) : f y - f x ‚â§ C * (y - x) :=
  have hf'_ge : ‚àÄ x ‚àà interior D, -C ‚â§ deriv (fun y => -f y) x := fun x hx => by
    rw [deriv.neg, neg_le_neg_iff]
    exact le_hf' x hx
  by linarith [hD.mul_sub_le_image_sub_of_le_deriv hf.neg hf'.neg hf'_ge x hx y hy hxy]
#align convex.image_sub_le_mul_sub_of_deriv_le Convex.image_sub_le_mul_sub_of_deriv_le

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f' ‚â§ C`, then `f` grows at most as fast
as `C * x`, i.e., `f y - f x ‚â§ C * (y - x)` whenever `x ‚â§ y`. -/
theorem image_sub_le_mul_sub_of_deriv_le {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) {C}
    (le_hf' : ‚àÄ x, deriv f x ‚â§ C) ‚¶Éx y‚¶Ñ (hxy : x ‚â§ y) : f y - f x ‚â§ C * (y - x) :=
  convex_univ.image_sub_le_mul_sub_of_deriv_le hf.continuous.continuousOn hf.differentiableOn
    (fun x _ => le_hf' x) x trivial y trivial hxy
#align image_sub_le_mul_sub_of_deriv_le image_sub_le_mul_sub_of_deriv_le

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is positive, then
`f` is a strictly monotone function on `D`.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly positive. -/
theorem strictMonoOn_of_deriv_pos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, 0 < deriv f x) : StrictMonoOn f D := by
  intro x hx y hy
  have : DifferentiableOn ‚Ñù f (interior D) := fun z hz =>
    (differentiableAt_of_deriv_ne_zero (hf' z hz).ne').differentiableWithinAt
  simpa only [zero_mul, sub_pos] using
    hD.mul_sub_lt_image_sub_of_lt_deriv hf this hf' x hx y hy
#align convex.strict_mono_on_of_deriv_pos strictMonoOn_of_deriv_pos

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is positive, then
`f` is a strictly monotone function.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly positive. -/
theorem strictMono_of_deriv_pos {f : ‚Ñù ‚Üí ‚Ñù} (hf' : ‚àÄ x, 0 < deriv f x) : StrictMono f :=
  strictMonoOn_univ.1 <| strictMonoOn_of_deriv_pos convex_univ (fun z _ =>
    (differentiableAt_of_deriv_ne_zero (hf' z).ne').differentiableWithinAt.continuousWithinAt)
    fun x _ => hf' x
#align strict_mono_of_deriv_pos strictMono_of_deriv_pos

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is strictly positive,
then `f` is a strictly monotone function on `D`. -/
lemma strictMonoOn_of_hasDerivWithinAt_pos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'‚ÇÄ : ‚àÄ x ‚àà interior D, 0 < f' x) : StrictMonoOn f D :=
  strictMonoOn_of_deriv_pos hD hf fun x hx ‚Ü¶ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'‚ÇÄ _ hx

@[deprecated (since := "2024-03-02")]
alias StrictMonoOn_of_hasDerivWithinAt_pos := strictMonoOn_of_hasDerivWithinAt_pos

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is strictly positive, then
`f` is a strictly monotone function. -/
lemma strictMono_of_hasDerivAt_pos {f f' : ‚Ñù ‚Üí ‚Ñù} (hf : ‚àÄ x, HasDerivAt f (f' x) x)
    (hf' : ‚àÄ x, 0 < f' x) : StrictMono f :=
  strictMono_of_deriv_pos fun x ‚Ü¶ by rw [(hf _).deriv]; exact hf' _

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonnegative, then
`f` is a monotone function on `D`. -/
theorem monotoneOn_of_deriv_nonneg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D))
    (hf'_nonneg : ‚àÄ x ‚àà interior D, 0 ‚â§ deriv f x) : MonotoneOn f D := fun x hx y hy hxy => by
  simpa only [zero_mul, sub_nonneg] using
    hD.mul_sub_le_image_sub_of_le_deriv hf hf' hf'_nonneg x hx y hy hxy
#align convex.monotone_on_of_deriv_nonneg monotoneOn_of_deriv_nonneg

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonnegative, then
`f` is a monotone function. -/
theorem monotone_of_deriv_nonneg {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) (hf' : ‚àÄ x, 0 ‚â§ deriv f x) :
    Monotone f :=
  monotoneOn_univ.1 <|
    monotoneOn_of_deriv_nonneg convex_univ hf.continuous.continuousOn hf.differentiableOn fun x _ =>
      hf' x
#align monotone_of_deriv_nonneg monotone_of_deriv_nonneg

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonnegative, then
`f` is a monotone function on `D`. -/
lemma monotoneOn_of_hasDerivWithinAt_nonneg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'‚ÇÄ : ‚àÄ x ‚àà interior D, 0 ‚â§ f' x) : MonotoneOn f D :=
  monotoneOn_of_deriv_nonneg hD hf (fun x hx ‚Ü¶ (hf' _ hx).differentiableWithinAt) fun x hx ‚Ü¶ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'‚ÇÄ _ hx

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonnegative, then
`f` is a monotone function. -/
lemma monotone_of_hasDerivAt_nonneg {f f' : ‚Ñù ‚Üí ‚Ñù} (hf : ‚àÄ x, HasDerivAt f (f' x) x)
    (hf' : 0 ‚â§ f') : Monotone f :=
  monotone_of_deriv_nonneg (fun x ‚Ü¶ (hf _).differentiableAt) fun x ‚Ü¶ by
    rw [(hf _).deriv]; exact hf' _

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is negative, then
`f` is a strictly antitone function on `D`. -/
theorem strictAntiOn_of_deriv_neg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, deriv f x < 0) : StrictAntiOn f D :=
  fun x hx y => by
  simpa only [zero_mul, sub_lt_zero] using
    hD.image_sub_lt_mul_sub_of_deriv_lt hf
      (fun z hz => (differentiableAt_of_deriv_ne_zero (hf' z hz).ne).differentiableWithinAt) hf' x
      hx y
#align convex.strict_anti_on_of_deriv_neg strictAntiOn_of_deriv_neg

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is negative, then
`f` is a strictly antitone function.
Note that we don't require differentiability explicitly as it already implied by the derivative
being strictly negative. -/
theorem strictAnti_of_deriv_neg {f : ‚Ñù ‚Üí ‚Ñù} (hf' : ‚àÄ x, deriv f x < 0) : StrictAnti f :=
  strictAntiOn_univ.1 <| strictAntiOn_of_deriv_neg convex_univ
      (fun z _ =>
        (differentiableAt_of_deriv_ne_zero (hf' z).ne).differentiableWithinAt.continuousWithinAt)
      fun x _ => hf' x
#align strict_anti_of_deriv_neg strictAnti_of_deriv_neg

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is strictly positive,
then `f` is a strictly monotone function on `D`. -/
lemma strictAntiOn_of_hasDerivWithinAt_neg {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'‚ÇÄ : ‚àÄ x ‚àà interior D, f' x < 0) : StrictAntiOn f D :=
  strictAntiOn_of_deriv_neg hD hf fun x hx ‚Ü¶ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'‚ÇÄ _ hx

@[deprecated (since := "2024-03-02")]
alias StrictAntiOn_of_hasDerivWithinAt_pos := strictAntiOn_of_hasDerivWithinAt_neg

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is strictly positive, then
`f` is a strictly monotone function. -/
lemma strictAnti_of_hasDerivAt_neg {f f' : ‚Ñù ‚Üí ‚Ñù} (hf : ‚àÄ x, HasDerivAt f (f' x) x)
    (hf' : ‚àÄ x, f' x < 0) : StrictAnti f :=
  strictAnti_of_deriv_neg fun x ‚Ü¶ by rw [(hf _).deriv]; exact hf' _

@[deprecated (since := "2024-03-02")]
alias strictAnti_of_hasDerivAt_pos := strictAnti_of_hasDerivAt_neg

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonpositive, then
`f` is an antitone function on `D`. -/
theorem antitoneOn_of_deriv_nonpos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : DifferentiableOn ‚Ñù f (interior D))
    (hf'_nonpos : ‚àÄ x ‚àà interior D, deriv f x ‚â§ 0) : AntitoneOn f D := fun x hx y hy hxy => by
  simpa only [zero_mul, sub_nonpos] using
    hD.image_sub_le_mul_sub_of_deriv_le hf hf' hf'_nonpos x hx y hy hxy
#align convex.antitone_on_of_deriv_nonpos antitoneOn_of_deriv_nonpos

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonpositive, then
`f` is an antitone function. -/
theorem antitone_of_deriv_nonpos {f : ‚Ñù ‚Üí ‚Ñù} (hf : Differentiable ‚Ñù f) (hf' : ‚àÄ x, deriv f x ‚â§ 0) :
    Antitone f :=
  antitoneOn_univ.1 <|
    antitoneOn_of_deriv_nonpos convex_univ hf.continuous.continuousOn hf.differentiableOn fun x _ =>
      hf' x
#align antitone_of_deriv_nonpos antitone_of_deriv_nonpos

/-- Let `f` be a function continuous on a convex (or, equivalently, connected) subset `D`
of the real line. If `f` is differentiable on the interior of `D` and `f'` is nonpositive, then
`f` is an antitone function on `D`. -/
lemma antitoneOn_of_hasDerivWithinAt_nonpos {D : Set ‚Ñù} (hD : Convex ‚Ñù D) {f f' : ‚Ñù ‚Üí ‚Ñù}
    (hf : ContinuousOn f D) (hf' : ‚àÄ x ‚àà interior D, HasDerivWithinAt f (f' x) (interior D) x)
    (hf'‚ÇÄ : ‚àÄ x ‚àà interior D, f' x ‚â§ 0) : AntitoneOn f D :=
  antitoneOn_of_deriv_nonpos hD hf (fun x hx ‚Ü¶ (hf' _ hx).differentiableWithinAt) fun x hx ‚Ü¶ by
    rw [deriv_eqOn isOpen_interior hf' hx]; exact hf'‚ÇÄ _ hx

/-- Let `f : ‚Ñù ‚Üí ‚Ñù` be a differentiable function. If `f'` is nonpositive, then `f` is an antitone
function. -/
lemma antitone_of_hasDerivAt_nonpos {f f' : ‚Ñù ‚Üí ‚Ñù} (hf : ‚àÄ x, HasDerivAt f (f' x) x)
    (hf' : f' ‚â§ 0) : Antitone f :=
  antitone_of_deriv_nonpos (fun x ‚Ü¶ (hf _).differentiableAt) fun x ‚Ü¶ by
    rw [(hf _).deriv]; exact hf' _

/-! ### Functions `f : E ‚Üí ‚Ñù` -/

/-- Lagrange's **Mean Value Theorem**, applied to convex domains. -/
theorem domain_mvt {f : E ‚Üí ‚Ñù} {s : Set E} {x y : E} {f' : E ‚Üí E ‚ÜíL[‚Ñù] ‚Ñù}
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚àÉ z ‚àà segment ‚Ñù x y, f y - f x = f' z (y - x) := by
  -- Use `g = AffineMap.lineMap x y` to parametrize the segment
  set g : ‚Ñù ‚Üí E := fun t => AffineMap.lineMap x y t
  set I := Icc (0 : ‚Ñù) 1
  have hsub : Ioo (0 : ‚Ñù) 1 ‚äÜ I := Ioo_subset_Icc_self
  have hmaps : MapsTo g I s := hs.mapsTo_lineMap xs ys
  -- The one-variable function `f ‚àò g` has derivative `f' (g t) (y - x)` at each `t ‚àà I`
  have hfg : ‚àÄ t ‚àà I, HasDerivWithinAt (f ‚àò g) (f' (g t) (y - x)) I t := fun t ht =>
    (hf _ (hmaps ht)).comp_hasDerivWithinAt t AffineMap.hasDerivWithinAt_lineMap hmaps
  -- apply 1-variable mean value theorem to pullback
  have hMVT : ‚àÉ t ‚àà Ioo (0 : ‚Ñù) 1, f' (g t) (y - x) = (f (g 1) - f (g 0)) / (1 - 0) := by
    refine exists_hasDerivAt_eq_slope (f ‚àò g) _ (by norm_num) ?_ ?_
    ¬∑ exact fun t Ht => (hfg t Ht).continuousWithinAt
    ¬∑ exact fun t Ht => (hfg t <| hsub Ht).hasDerivAt (Icc_mem_nhds Ht.1 Ht.2)
  -- reinterpret on domain
  rcases hMVT with ‚ü®t, Ht, hMVT'‚ü©
  rw [segment_eq_image_lineMap, exists_mem_image]
  refine ‚ü®t, hsub Ht, ?_‚ü©
  simpa [g] using hMVT'.symm
#align domain_mvt domain_mvt

section RCLike

/-!
### Vector-valued functions `f : E ‚Üí F`. Strict differentiability.

A `C^1` function is strictly differentiable, when the field is `‚Ñù` or `‚ÑÇ`. This follows from the
mean value inequality on balls, which is a particular case of the above results after restricting
the scalars to `‚Ñù`. Note that it does not make sense to talk of a convex set over `‚ÑÇ`, but balls
make sense and are enough. Many formulations of the mean value inequality could be generalized to
balls over `‚Ñù` or `‚ÑÇ`. For now, we only include the ones that we need.
-/

variable {ùïú : Type*} [RCLike ùïú] {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G] {H : Type*}
  [NormedAddCommGroup H] [NormedSpace ùïú H] {f : G ‚Üí H} {f' : G ‚Üí G ‚ÜíL[ùïú] H} {x : G}

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt
    (hder : ‚àÄ·∂† y in ùìù x, HasFDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictFDerivAt f (f' x) x := by
  -- turn little-o definition of strict_fderiv into an epsilon-delta statement
  refine isLittleO_iff.mpr fun c hc => Metric.eventually_nhds_iff_ball.mpr ?_
  -- the correct Œµ is the modulus of continuity of f'
  rcases Metric.mem_nhds_iff.mp (inter_mem hder (hcont <| ball_mem_nhds _ hc)) with ‚ü®Œµ, Œµ0, hŒµ‚ü©
  refine ‚ü®Œµ, Œµ0, ?_‚ü©
  -- simplify formulas involving the product E √ó E
  rintro ‚ü®a, b‚ü© h
  rw [‚Üê ball_prod_same, prod_mk_mem_set_prod_eq] at h
  -- exploit the choice of Œµ as the modulus of continuity of f'
  have hf' : ‚àÄ x' ‚àà ball x Œµ, ‚Äñf' x' - f' x‚Äñ ‚â§ c := fun x' H' => by
    rw [‚Üê dist_eq_norm]
    exact le_of_lt (hŒµ H').2
  -- apply mean value theorem
  letI : NormedSpace ‚Ñù G := RestrictScalars.normedSpace ‚Ñù ùïú G
  refine (convex_ball _ _).norm_image_sub_le_of_norm_hasFDerivWithin_le' ?_ hf' h.2 h.1
  exact fun y hy => (hŒµ hy).1.hasFDerivWithinAt
#align has_strict_fderiv_at_of_has_fderiv_at_of_continuous_at hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictDerivAt_of_hasDerivAt_of_continuousAt {f f' : ùïú ‚Üí G} {x : ùïú}
    (hder : ‚àÄ·∂† y in ùìù x, HasDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictDerivAt f (f' x) x :=
  hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt (hder.mono fun _ hy => hy.hasFDerivAt) <|
    (smulRightL ùïú ùïú G 1).continuous.continuousAt.comp hcont
#align has_strict_deriv_at_of_has_deriv_at_of_continuous_at hasStrictDerivAt_of_hasDerivAt_of_continuousAt

end RCLike
