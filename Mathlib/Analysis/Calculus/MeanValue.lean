/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.AffineMap
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Normed.Group.AddTorsor
import Mathlib.Analysis.Normed.Module.Convex
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Topology.Instances.RealVectorSpace
import Mathlib.Topology.LocallyConstant.Basic

/-!
# The mean value inequality and equalities

In this file we prove the following facts:

* `Convex.norm_image_sub_le_of_norm_deriv_le` : if `f` is differentiable on a convex set `s`
  and the norm of its derivative is bounded by `C`, then `f` is Lipschitz continuous on `s` with
  constant `C`; also a variant in which what is bounded by `C` is the norm of the difference of the
  derivative from a fixed linear map. This lemma and its versions are formulated using `RCLike`,
  so they work both for real and complex derivatives.

* `image_le_of*`, `image_norm_le_of_*` : several similar lemmas deducing `f x ‚â§ B x` or
  `‚Äñf x‚Äñ ‚â§ B x` from upper estimates on `f'` or `‚Äñf'‚Äñ`, respectively. These lemmas differ by
  their assumptions:

  * `of_liminf_*` lemmas assume that limit inferior of some ratio is less than `B' x`;
  * `of_deriv_right_*`, `of_norm_deriv_right_*` lemmas assume that the right derivative
    or its norm is less than `B' x`;
  * `of_*_lt_*` lemmas assume a strict inequality whenever `f x = B x` or `‚Äñf x‚Äñ = B x`;
  * `of_*_le_*` lemmas assume a non-strict inequality everywhere on `[a, b)`;
  * name of a lemma ends with `'` if (1) it assumes that `B` is continuous on `[a, b]`
    and has a right derivative at every point of `[a, b)`, and (2) the lemma has
    a counterpart assuming that `B` is differentiable everywhere on `‚Ñù`

* `norm_image_sub_le_*_segment` : if derivative of `f` on `[a, b]` is bounded above
  by a constant `C`, then `‚Äñf x - f a‚Äñ ‚â§ C * ‚Äñx - a‚Äñ`; several versions deal with
  right derivative and derivative within `[a, b]` (`HasDerivWithinAt` or `derivWithin`).

* `Convex.is_const_of_fderivWithin_eq_zero` : if a function has derivative `0` on a convex set `s`,
  then it is a constant on `s`.

* `hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt` : a C^1 function over the reals is
  strictly differentiable. (This is a corollary of the mean value inequality.)
-/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace ‚Ñù F]

open Metric Set Asymptotics ContinuousLinearMap Filter

open scoped Topology NNReal

/-! ### One-dimensional fencing inequalities -/


/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x := by
  change Icc a b ‚äÜ { x | f x ‚â§ B x }
  set s := { x | f x ‚â§ B x } ‚à© Icc a b
  have A : ContinuousOn (fun x => (f x, B x)) (Icc a b) := hf.prodMk hB
  have : IsClosed s := by
    simp only [s, inter_comm]
    exact A.preimage_isClosed_of_isClosed isClosed_Icc OrderClosedTopology.isClosed_le'
  apply this.Icc_subset_of_forall_exists_gt ha
  rintro x ‚ü®hxB : f x ‚â§ B x, xab‚ü© y hy
  rcases hxB.lt_or_eq with hxB | hxB
  ¬∑ -- If `f x < B x`, then all we need is continuity of both sides
    refine nonempty_of_mem (inter_mem ?_ (Ioc_mem_nhdsGT hy))
    have : ‚àÄ·∂† x in ùìù[Icc a b] x, f x < B x :=
      A x (Ico_subset_Icc_self xab) (IsOpen.mem_nhds (isOpen_lt continuous_fst continuous_snd) hxB)
    have : ‚àÄ·∂† x in ùìù[>] x, f x < B x := nhdsWithin_le_of_mem (Icc_mem_nhdsGT_of_mem xab) this
    exact this.mono fun y => le_of_lt
  ¬∑ rcases exists_between (bound x xab hxB) with ‚ü®r, hfr, hrB‚ü©
    specialize hf' x xab r hfr
    have HB : ‚àÄ·∂† z in ùìù[>] x, r < slope B x z :=
      (hasDerivWithinAt_iff_tendsto_slope' <| lt_irrefl x).1 (hB' x xab).Ioi_of_Ici
        (Ioi_mem_nhds hrB)
    obtain ‚ü®z, hfz, hzB, hz‚ü© : ‚àÉ z, slope f x z < r ‚àß r < slope B x z ‚àß z ‚àà Ioc x y :=
      hf'.and_eventually (HB.and (Ioc_mem_nhdsGT hy)) |>.exists
    refine ‚ü®z, ?_, hz‚ü©
    have := (hfz.trans hzB).le
    rwa [slope_def_field, slope_def_field, div_le_div_iff_of_pos_right (sub_pos.2 hz.1), hxB,
      sub_le_sub_iff_right] at this

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (f z - f x) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(f z - f x) / (z - x)`
  is bounded above by `B'`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_liminf_slope_right_le_deriv_boundary {f : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    -- `bound` actually says `liminf (f z - f x) / (z - x) ‚â§ B' x`
    (bound : ‚àÄ x ‚àà Ico a b, ‚àÄ r, B' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope f x z < r) :
    ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x := by
  have Hr : ‚àÄ x ‚àà Icc a b, ‚àÄ r > 0, f x ‚â§ B x + r * (x - a) := fun x hx r hr => by
    apply image_le_of_liminf_slope_right_lt_deriv_boundary' hf bound
    ¬∑ rwa [sub_self, mul_zero, add_zero]
    ¬∑ exact hB.add (continuousOn_const.mul (continuousOn_id.sub continuousOn_const))
    ¬∑ intro x hx
      exact (hB' x hx).add (((hasDerivWithinAt_id x (Ici x)).sub_const a).const_mul r)
    ¬∑ intro x _ _
      rw [mul_one]
      exact (lt_add_iff_pos_right _).2 hr
    exact hx
  intro x hx
  have : ContinuousWithinAt (fun r => B x + r * (x - a)) (Ioi 0) 0 :=
    continuousWithinAt_const.add (continuousWithinAt_id.mul continuousWithinAt_const)
  convert continuousWithinAt_const.closure_le _ this (Hr x hx) using 1 <;> simp

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary' {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x < B' x` whenever `f x = B x`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_lt_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f x = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `f a ‚â§ B a`;
* `B` has right derivative `B'` at every point of `[a, b)`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* we have `f' x ‚â§ B' x` on `[a, b)`.

Then `f x ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_le_of_deriv_right_le_deriv_boundary {f f' : ‚Ñù ‚Üí ‚Ñù} {a b : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : f a ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, f' x ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí f x ‚â§ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary hf ha hB hB' fun x hx _ hr =>
    (hf' x hx).liminf_right_slope_le (lt_of_le_of_lt (bound x hx) hr)

/-! ### Vector-valued functions `f : ‚Ñù ‚Üí E` -/


section

variable {f : ‚Ñù ‚Üí E} {a b : ‚Ñù}

/-- General fencing theorem for continuous functions with an estimate on the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `B` has right derivative at every point of `[a, b)`;
* for each `x ‚àà [a, b)` the right-side limit inferior of `(‚Äñf z‚Äñ - ‚Äñf x‚Äñ) / (z - x)`
  is bounded above by a function `f'`;
* we have `f' x < B' x` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. -/
theorem image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary {E : Type*}
    [NormedAddCommGroup E] {f : ‚Ñù ‚Üí E} {f' : ‚Ñù ‚Üí ‚Ñù} (hf : ContinuousOn f (Icc a b))
    -- `hf'` actually says `liminf (‚Äñf z‚Äñ - ‚Äñf x‚Äñ) / (z - x) ‚â§ f' x`
    (hf' : ‚àÄ x ‚àà Ico a b, ‚àÄ r, f' x < r ‚Üí ‚àÉ·∂† z in ùìù[>] x, slope (norm ‚àò f) x z < r)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí f' x < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_le_of_liminf_slope_right_lt_deriv_boundary' (continuous_norm.comp_continuousOn hf) hf' ha hB
    hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* the norm of `f'` is strictly less than `B'` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary' {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí ‚Äñf' x‚Äñ < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_liminf_right_slope_norm_lt_deriv_boundary hf
    (fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le hr) ha hB hB' bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* the norm of `f'` is strictly less than `B'` whenever `‚Äñf x‚Äñ = B x`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_lt_deriv_boundary {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf x‚Äñ = B x ‚Üí ‚Äñf' x‚Äñ < B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_norm_deriv_right_lt_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` and `B` have right derivatives `f'` and `B'` respectively at every point of `[a, b)`;
* we have `‚Äñf' x‚Äñ ‚â§ B x` everywhere on `[a, b)`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary' {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ContinuousOn B (Icc a b))
    (hB' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt B (B' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_le_of_liminf_slope_right_le_deriv_boundary (continuous_norm.comp_continuousOn hf) ha hB hB'
    fun x hx _ hr => (hf' x hx).liminf_right_slope_norm_le ((bound x hx).trans_lt hr)

/-- General fencing theorem for continuous functions with an estimate on the norm of the derivative.
Let `f` and `B` be continuous functions on `[a, b]` such that

* `‚Äñf a‚Äñ ‚â§ B a`;
* `f` has right derivative `f'` at every point of `[a, b)`;
* `B` has derivative `B'` everywhere on `‚Ñù`;
* we have `‚Äñf' x‚Äñ ‚â§ B x` everywhere on `[a, b)`.

Then `‚Äñf x‚Äñ ‚â§ B x` everywhere on `[a, b]`. We use one-sided derivatives in the assumptions
to make this theorem work for piecewise differentiable functions.
-/
theorem image_norm_le_of_norm_deriv_right_le_deriv_boundary {f' : ‚Ñù ‚Üí E}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    {B B' : ‚Ñù ‚Üí ‚Ñù} (ha : ‚Äñf a‚Äñ ‚â§ B a) (hB : ‚àÄ x, HasDerivAt B (B' x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ B' x) : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà Icc a b ‚Üí ‚Äñf x‚Äñ ‚â§ B x :=
  image_norm_le_of_norm_deriv_right_le_deriv_boundary' hf hf' ha
    (fun x _ => (hB x).continuousAt.continuousWithinAt) (fun x _ => (hB x).hasDerivWithinAt) bound

/-- A function on `[a, b]` with the norm of the right derivative bounded by `C`
satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`. -/
theorem norm_image_sub_le_of_norm_deriv_right_le_segment {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ContinuousOn f (Icc a b)) (hf' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ C) : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  let g x := f x - f a
  have hg : ContinuousOn g (Icc a b) := hf.sub continuousOn_const
  have hg' : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt g (f' x) (Ici x) x := by
    intro x hx
    simp [g, hf' x hx]
  let B x := C * (x - a)
  have hB : ‚àÄ x, HasDerivAt B C x := by
    intro x
    simpa using (hasDerivAt_const x C).mul ((hasDerivAt_id x).sub (hasDerivAt_const x a))
  convert image_norm_le_of_norm_deriv_right_le_deriv_boundary hg hg' _ hB bound
  simp only [g, B]; rw [sub_self, norm_zero, sub_self, mul_zero]

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x)
    (bound : ‚àÄ x ‚àà Ico a b, ‚Äñf' x‚Äñ ‚â§ C) : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  refine
    norm_image_sub_le_of_norm_deriv_right_le_segment (fun x hx => (hf x hx).continuousWithinAt)
      (fun x hx => ?_) bound
  exact (hf x <| Ico_subset_Icc_self hx).mono_of_mem_nhdsWithin (Icc_mem_nhdsGE_of_mem hx)

/-- A function on `[a, b]` with the norm of the derivative within `[a, b]`
bounded by `C` satisfies `‚Äñf x - f a‚Äñ ‚â§ C * (x - a)`, `derivWithin`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment {C : ‚Ñù} (hf : DifferentiableOn ‚Ñù f (Icc a b))
    (bound : ‚àÄ x ‚àà Ico a b, ‚ÄñderivWithin f (Icc a b) x‚Äñ ‚â§ C) :
    ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ C * (x - a) := by
  refine norm_image_sub_le_of_norm_deriv_le_segment' ?_ bound
  exact fun x hx => (hf x hx).hasDerivWithinAt

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚Äñf 1 - f 0‚Äñ ‚â§ C`, `HasDerivWithinAt`
version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01' {f' : ‚Ñù ‚Üí E} {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà Icc (0 : ‚Ñù) 1, HasDerivWithinAt f (f' x) (Icc (0 : ‚Ñù) 1) x)
    (bound : ‚àÄ x ‚àà Ico (0 : ‚Ñù) 1, ‚Äñf' x‚Äñ ‚â§ C) : ‚Äñf 1 - f 0‚Äñ ‚â§ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment' hf bound 1 (right_mem_Icc.2 zero_le_one)

/-- A function on `[0, 1]` with the norm of the derivative within `[0, 1]`
bounded by `C` satisfies `‚Äñf 1 - f 0‚Äñ ‚â§ C`, `derivWithin` version. -/
theorem norm_image_sub_le_of_norm_deriv_le_segment_01 {C : ‚Ñù}
    (hf : DifferentiableOn ‚Ñù f (Icc (0 : ‚Ñù) 1))
    (bound : ‚àÄ x ‚àà Ico (0 : ‚Ñù) 1, ‚ÄñderivWithin f (Icc (0 : ‚Ñù) 1) x‚Äñ ‚â§ C) : ‚Äñf 1 - f 0‚Äñ ‚â§ C := by
  simpa only [sub_zero, mul_one] using
    norm_image_sub_le_of_norm_deriv_le_segment hf bound 1 (right_mem_Icc.2 zero_le_one)

theorem constant_of_has_deriv_right_zero (hcont : ContinuousOn f (Icc a b))
    (hderiv : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f 0 (Ici x) x) : ‚àÄ x ‚àà Icc a b, f x = f a := by
  have : ‚àÄ x ‚àà Icc a b, ‚Äñf x - f a‚Äñ ‚â§ 0 * (x - a) := fun x hx =>
    norm_image_sub_le_of_norm_deriv_right_le_segment hcont hderiv (fun _ _ => norm_zero.le) x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using this

theorem constant_of_derivWithin_zero (hdiff : DifferentiableOn ‚Ñù f (Icc a b))
    (hderiv : ‚àÄ x ‚àà Ico a b, derivWithin f (Icc a b) x = 0) : ‚àÄ x ‚àà Icc a b, f x = f a := by
  have H : ‚àÄ x ‚àà Ico a b, ‚ÄñderivWithin f (Icc a b) x‚Äñ ‚â§ 0 := by
    simpa only [norm_le_zero_iff] using fun x hx => hderiv x hx
  simpa only [zero_mul, norm_le_zero_iff, sub_eq_zero] using fun x hx =>
    norm_image_sub_le_of_norm_deriv_le_segment hdiff H x hx

variable {f' g : ‚Ñù ‚Üí E}

/-- If two continuous functions on `[a, b]` have the same right derivative and are equal at `a`,
  then they are equal everywhere on `[a, b]`. -/
theorem eq_of_has_deriv_right_eq (derivf : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt f (f' x) (Ici x) x)
    (derivg : ‚àÄ x ‚àà Ico a b, HasDerivWithinAt g (f' x) (Ici x) x) (fcont : ContinuousOn f (Icc a b))
    (gcont : ContinuousOn g (Icc a b)) (hi : f a = g a) : ‚àÄ y ‚àà Icc a b, f y = g y := by
  simp only [‚Üê @sub_eq_zero _ _ (f _)] at hi ‚ä¢
  exact hi ‚ñ∏ constant_of_has_deriv_right_zero (fcont.sub gcont) fun y hy => by
    simpa only [sub_self] using (derivf y hy).sub (derivg y hy)

/-- If two differentiable functions on `[a, b]` have the same derivative within `[a, b]` everywhere
  on `[a, b)` and are equal at `a`, then they are equal everywhere on `[a, b]`. -/
theorem eq_of_derivWithin_eq (fdiff : DifferentiableOn ‚Ñù f (Icc a b))
    (gdiff : DifferentiableOn ‚Ñù g (Icc a b))
    (hderiv : EqOn (derivWithin f (Icc a b)) (derivWithin g (Icc a b)) (Ico a b)) (hi : f a = g a) :
    ‚àÄ y ‚àà Icc a b, f y = g y := by
  have A : ‚àÄ y ‚àà Ico a b, HasDerivWithinAt f (derivWithin f (Icc a b) y) (Ici y) y := fun y hy =>
    (fdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem_nhdsWithin
    (Icc_mem_nhdsGE_of_mem hy)
  have B : ‚àÄ y ‚àà Ico a b, HasDerivWithinAt g (derivWithin g (Icc a b) y) (Ici y) y := fun y hy =>
    (gdiff y (mem_Icc_of_Ico hy)).hasDerivWithinAt.mono_of_mem_nhdsWithin
    (Icc_mem_nhdsGE_of_mem hy)
  exact eq_of_has_deriv_right_eq A (fun y hy => (hderiv hy).symm ‚ñ∏ B y hy) fdiff.continuousOn
    gdiff.continuousOn hi

end

/-!
### Vector-valued functions `f : E ‚Üí G`

Theorems in this section work both for real and complex differentiable functions. We use assumptions
`[NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú] [NormedSpace ùïú E] [NormedSpace ùïú G]` to
achieve this result. For the domain `E` we also assume `[NormedSpace ‚Ñù E]` to have a notion
of a `Convex` set. -/

section

namespace Convex

variable {ùïú G : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  [NormedSpace ùïú E] [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f g : E ‚Üí G} {C : ‚Ñù} {s : Set E} {x y : E} {f' g' : E ‚Üí E ‚ÜíL[ùïú] G} {œÜ : E ‚ÜíL[ùïú] G}

instance (priority := 100) : PathConnectedSpace ùïú := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  infer_instance

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`, then
the function is `C`-Lipschitz. Version with `HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s)
    (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  letI : NormedSpace ‚Ñù G := RestrictScalars.normedSpace ‚Ñù ùïú G
  /- By composition with `AffineMap.lineMap x y`, we reduce to a statement for functions defined
    on `[0,1]`, for which it is proved in `norm_image_sub_le_of_norm_deriv_le_segment`.
    We just have to check the differentiability of the composition and bounds on its derivative,
    which is straightforward but tedious for lack of automation. -/
  set g := (AffineMap.lineMap x y : ‚Ñù ‚Üí E)
  have segm : MapsTo g (Icc 0 1 : Set ‚Ñù) s := hs.mapsTo_lineMap xs ys
  have hD : ‚àÄ t ‚àà Icc (0 : ‚Ñù) 1,
      HasDerivWithinAt (f ‚àò g) (f' (g t) (y - x)) (Icc 0 1) t := fun t ht => by
    simpa using ((hf (g t) (segm ht)).restrictScalars ‚Ñù).comp_hasDerivWithinAt _
      AffineMap.hasDerivWithinAt_lineMap segm
  have bound : ‚àÄ t ‚àà Ico (0 : ‚Ñù) 1, ‚Äñf' (g t) (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := fun t ht =>
    le_of_opNorm_le _ (bound _ <| segm <| Ico_subset_Icc_self ht) _
  simpa [g] using norm_image_sub_le_of_norm_deriv_le_segment_01' hD bound

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `HasFDerivWithinAt` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasFDerivWithin_le {C : ‚Ñù‚â•0}
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ‚Çä ‚â§ C)
    (hs : Convex ‚Ñù s) : LipschitzOnWith C f s := by
  rw [lipschitzOnWith_iff_norm_sub_le]
  intro x x_in y y_in
  exact hs.norm_image_sub_le_of_norm_hasFDerivWithin_le hf bound y_in x_in

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E ‚Üí G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : ‚Ñù‚â•0` larger than `‚Äñf' x‚Äñ‚Çä`, `f` is
`K`-Lipschitz on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt` for a version that claims
existence of `K` instead of an explicit estimate. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt (hs : Convex ‚Ñù s)
    {f : E ‚Üí G} (hder : ‚àÄ·∂† y in ùìù[s] x, HasFDerivWithinAt f (f' y) s y)
    (hcont : ContinuousWithinAt f' s x) (K : ‚Ñù‚â•0) (hK : ‚Äñf' x‚Äñ‚Çä < K) :
    ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith K f t := by
  obtain ‚ü®Œµ, Œµ0, hŒµ‚ü© : ‚àÉ Œµ > 0,
      ball x Œµ ‚à© s ‚äÜ { y | HasFDerivWithinAt f (f' y) s y ‚àß ‚Äñf' y‚Äñ‚Çä < K } :=
    mem_nhdsWithin_iff.1 (hder.and <| hcont.nnnorm.eventually (gt_mem_nhds hK))
  rw [inter_comm] at hŒµ
  refine ‚ü®s ‚à© ball x Œµ, inter_mem_nhdsWithin _ (ball_mem_nhds _ Œµ0), ?_‚ü©
  exact
    (hs.inter (convex_ball _ _)).lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
      (fun y hy => (hŒµ hy).1.mono inter_subset_left) fun y hy => (hŒµ hy).2.le

/-- Let `s` be a convex set in a real normed vector space `E`, let `f : E ‚Üí G` be a function
differentiable within `s` in a neighborhood of `x : E` with derivative `f'`. Suppose that `f'` is
continuous within `s` at `x`. Then for any number `K : ‚Ñù‚â•0` larger than `‚Äñf' x‚Äñ‚Çä`, `f` is Lipschitz
on some neighborhood of `x` within `s`. See also
`Convex.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt` for a version
with an explicit estimate on the Lipschitz constant. -/
theorem exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt (hs : Convex ‚Ñù s) {f : E ‚Üí G}
    (hder : ‚àÄ·∂† y in ùìù[s] x, HasFDerivWithinAt f (f' y) s y) (hcont : ContinuousWithinAt f' s x) :
    ‚àÉ K, ‚àÉ t ‚àà ùìù[s] x, LipschitzOnWith K f t :=
  (exists_gt _).imp <|
    hs.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt hder hcont

/-- The mean value theorem on a convex set: if the derivative of a function within this set is
bounded by `C`, then the function is `C`-Lipschitz. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderivWithin` and
`LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderivWithin_le {C : ‚Ñù‚â•0} (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt) bound

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys

/-- The mean value theorem on a convex set: if the derivative of a function is bounded by `C` on
`s`, then the function is `C`-Lipschitz on `s`. Version with `fderiv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_fderiv_le {C : ‚Ñù‚â•0} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound

/-- The mean value theorem: if the derivative of a function is bounded by `C`, then the function is
`C`-Lipschitz. Version with `fderiv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_fderiv_le
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f : E ‚Üí G}
    {C : ‚Ñù‚â•0} (hf : Differentiable ùïú f)
    (bound : ‚àÄ x, ‚Äñfderiv ùïú f x‚Äñ‚Çä ‚â§ C) : LipschitzWith C f := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  rw [‚Üê lipschitzOnWith_univ]
  exact lipschitzOnWith_of_nnnorm_fderiv_le (fun x _ ‚Ü¶ hf x) (fun x _ ‚Ü¶ bound x) convex_univ

/-- Variant of the mean value inequality on a convex set, using a bound on the difference between
the derivative and a fixed linear map, rather than a bound on the derivative itself. Version with
`HasFDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (hf : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x - œÜ‚Äñ ‚â§ C)
    (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ := by
  /- We subtract `œÜ` to define a new function `g` for which `g' = 0`, for which the previous theorem
    applies, `Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le`. Then, we just need to glue
    together the pieces, expressing back `f` in terms of `g`. -/
  let g y := f y - œÜ y
  have hg : ‚àÄ x ‚àà s, HasFDerivWithinAt g (f' x - œÜ) s x := fun x xs =>
    (hf x xs).sub œÜ.hasFDerivWithinAt
  calc
    ‚Äñf y - f x - œÜ (y - x)‚Äñ = ‚Äñf y - f x - (œÜ y - œÜ x)‚Äñ := by simp
    _ = ‚Äñf y - œÜ y - (f x - œÜ x)‚Äñ := by congr 1; abel
    _ = ‚Äñg y - g x‚Äñ := by simp [g]
    _ ‚â§ C * ‚Äñy - x‚Äñ := Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le hg bound hs xs ys

/-- Variant of the mean value inequality on a convex set. Version with `fderivWithin`. -/
theorem norm_image_sub_le_of_norm_fderivWithin_le' (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x - œÜ‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le' (fun x hx => (hf x hx).hasFDerivWithinAt) bound
    xs ys

/-- Variant of the mean value inequality on a convex set. Version with `fderiv`. -/
theorem norm_image_sub_le_of_norm_fderiv_le' (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñfderiv ùïú f x - œÜ‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x - œÜ (y - x)‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (fun x hx => (hf x hx).hasFDerivAt.hasFDerivWithinAt) bound xs ys

/-- If a function has zero Fr√©chet derivative at every point of a convex set,
then it is a constant on this set. -/
theorem is_const_of_fderivWithin_eq_zero (hs : Convex ‚Ñù s) (hf : DifferentiableOn ùïú f s)
    (hf' : ‚àÄ x ‚àà s, fderivWithin ùïú f s x = 0) (hx : x ‚àà s) (hy : y ‚àà s) : f x = f y := by
  have bound : ‚àÄ x ‚àà s, ‚ÄñfderivWithin ùïú f s x‚Äñ ‚â§ 0 := fun x hx => by
    simp only [hf' x hx, norm_zero, le_rfl]
  simpa only [(dist_eq_norm _ _).symm, zero_mul, dist_le_zero, eq_comm] using
    hs.norm_image_sub_le_of_norm_fderivWithin_le hf bound hx hy

theorem _root_.is_const_of_fderiv_eq_zero
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f : E ‚Üí G}
    (hf : Differentiable ùïú f) (hf' : ‚àÄ x, fderiv ùïú f x = 0)
    (x y : E) : f x = f y := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  exact convex_univ.is_const_of_fderivWithin_eq_zero hf.differentiableOn
    (fun x _ => by rw [fderivWithin_univ]; exact hf' x) trivial trivial

/-- If two functions have equal Fr√©chet derivatives at every point of a convex set, and are equal at
one point in that set, then they are equal on that set. -/
theorem eqOn_of_fderivWithin_eq (hs : Convex ‚Ñù s) (hf : DifferentiableOn ùïú f s)
    (hg : DifferentiableOn ùïú g s) (hs' : UniqueDiffOn ùïú s)
    (hf' : s.EqOn (fderivWithin ùïú f s) (fderivWithin ùïú g s)) (hx : x ‚àà s) (hfgx : f x = g x) :
    s.EqOn f g := fun y hy => by
  suffices f x - g x = f y - g y by rwa [hfgx, sub_self, eq_comm, sub_eq_zero] at this
  refine hs.is_const_of_fderivWithin_eq_zero (hf.sub hg) (fun z hz => ?_) hx hy
  rw [fderivWithin_sub (hs' _ hz) (hf _ hz) (hg _ hz), sub_eq_zero, hf' hz]

/-- If `f` has zero derivative on an open set, then `f` is locally constant on `s`. -/
-- TODO: change the spelling once we have `IsLocallyConstantOn`.
theorem _root_.IsOpen.isOpen_inter_preimage_of_fderiv_eq_zero
    (hs : IsOpen s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (fderiv ùïú f) 0) (t : Set G) : IsOpen (s ‚à© f ‚Åª¬π' t) := by
  refine Metric.isOpen_iff.mpr fun y ‚ü®hy, hy'‚ü© ‚Ü¶ ?_
  obtain ‚ü®r, hr, h‚ü© := Metric.isOpen_iff.mp hs y hy
  refine ‚ü®r, hr, Set.subset_inter h fun x hx ‚Ü¶ ?_‚ü©
  have := (convex_ball y r).is_const_of_fderivWithin_eq_zero (hf.mono h) ?_ hx (mem_ball_self hr)
  ¬∑ simpa [this]
  ¬∑ intro z hz
    simpa only [fderivWithin_of_isOpen Metric.isOpen_ball hz] using hf' (h hz)

theorem _root_.isLocallyConstant_of_fderiv_eq_zero (h‚ÇÅ : Differentiable ùïú f)
    (h‚ÇÇ : ‚àÄ x, fderiv ùïú f x = 0) : IsLocallyConstant f := by
  simpa using isOpen_univ.isOpen_inter_preimage_of_fderiv_eq_zero h‚ÇÅ.differentiableOn fun _ _ ‚Ü¶ h‚ÇÇ _

/-- If `f` has zero derivative on a connected open set, then `f` is constant on `s`. -/
theorem _root_.IsOpen.exists_is_const_of_fderiv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (fderiv ùïú f) 0) : ‚àÉ a, ‚àÄ x ‚àà s, f x = a := by
  obtain (rfl | ‚ü®y, hy‚ü©) := s.eq_empty_or_nonempty
  ¬∑ exact ‚ü®0, by simp‚ü©
  ¬∑ refine ‚ü®f y, fun x hx ‚Ü¶ ?_‚ü©
    have h‚ÇÅ := hs.isOpen_inter_preimage_of_fderiv_eq_zero hf hf' {f y}
    have h‚ÇÇ := hf.continuousOn.comp_continuous continuous_subtype_val (fun x ‚Ü¶ x.2)
    by_contra h‚ÇÉ
    obtain ‚ü®t, ht, ht'‚ü© := (isClosed_singleton (x := f y)).preimage h‚ÇÇ
    have ht'' : ‚àÄ a ‚àà s, a ‚àà t ‚Üî f a ‚â† f y := by simpa [Set.ext_iff] using ht'
    obtain ‚ü®z, H‚ÇÅ, H‚ÇÇ, H‚ÇÉ‚ü© := hs' _ _ h‚ÇÅ ht (fun x h ‚Ü¶ by simp [h, ht'', eq_or_ne]) ‚ü®y, by simpa‚ü©
      ‚ü®x, by simp [ht'' _ hx, hx, h‚ÇÉ]‚ü©
    exact (ht'' _ H‚ÇÅ).mp H‚ÇÉ H‚ÇÇ.2

theorem _root_.IsOpen.is_const_of_fderiv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (fderiv ùïú f) 0) {x y : E} (hx : x ‚àà s) (hy : y ‚àà s) : f x = f y := by
  obtain ‚ü®a, ha‚ü© := hs.exists_is_const_of_fderiv_eq_zero hs' hf hf'
  rw [ha x hx, ha y hy]

theorem _root_.IsOpen.exists_eq_add_of_fderiv_eq (hs : IsOpen s) (hs' : IsPreconnected s)
    (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s)
    (hf' : s.EqOn (fderiv ùïú f) (fderiv ùïú g)) : ‚àÉ a, s.EqOn f (g ¬∑ + a) := by
  simp_rw [Set.EqOn, ‚Üê sub_eq_iff_eq_add']
  refine hs.exists_is_const_of_fderiv_eq_zero hs' (hf.sub hg) fun x hx ‚Ü¶ ?_
  rw [fderiv_fun_sub (hf.differentiableAt (hs.mem_nhds hx)) (hg.differentiableAt (hs.mem_nhds hx)),
    hf' hx, sub_self, Pi.zero_apply]

/-- If two functions have equal Fr√©chet derivatives at every point of a connected open set,
and are equal at one point in that set, then they are equal on that set. -/
theorem _root_.IsOpen.eqOn_of_fderiv_eq (hs : IsOpen s) (hs' : IsPreconnected s)
    (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s)
    (hf' : ‚àÄ x ‚àà s, fderiv ùïú f x = fderiv ùïú g x) (hx : x ‚àà s) (hfgx : f x = g x) :
    s.EqOn f g := by
  obtain ‚ü®a, ha‚ü© := hs.exists_eq_add_of_fderiv_eq hs' hf hg hf'
  obtain rfl := left_eq_add.mp (hfgx.symm.trans (ha hx))
  simpa using ha

theorem _root_.eq_of_fderiv_eq
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E] {f g : E ‚Üí G}
    (hf : Differentiable ùïú f) (hg : Differentiable ùïú g)
    (hf' : ‚àÄ x, fderiv ùïú f x = fderiv ùïú g x) (x : E) (hfgx : f x = g x) : f = g := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  let A : NormedSpace ‚Ñù E := RestrictScalars.normedSpace ‚Ñù ùïú E
  suffices Set.univ.EqOn f g from funext fun x => this <| mem_univ x
  exact convex_univ.eqOn_of_fderivWithin_eq hf.differentiableOn hg.differentiableOn
    uniqueDiffOn_univ (fun x _ => by simpa using hf' _) (mem_univ _) hfgx

lemma isLittleO_pow_succ {x‚ÇÄ : E} {n : ‚Ñï} (hs : Convex ‚Ñù s) (hx‚ÇÄs : x‚ÇÄ ‚àà s)
    (hff' : ‚àÄ x ‚àà s, HasFDerivWithinAt f (f' x) s x) (hf' : f' =o[ùìù[s] x‚ÇÄ] fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ n) :
    (fun x ‚Ü¶ f x - f x‚ÇÄ) =o[ùìù[s] x‚ÇÄ] fun x ‚Ü¶ ‚Äñx - x‚ÇÄ‚Äñ ^ (n + 1) := by
  rw [Asymptotics.isLittleO_iff] at hf' ‚ä¢
  intro c hc
  simp_rw [norm_pow, pow_succ, ‚Üê mul_assoc, norm_norm]
  simp_rw [norm_pow, norm_norm] at hf'
  have : ‚àÄ·∂† x in ùìù[s] x‚ÇÄ, segment ‚Ñù x‚ÇÄ x ‚äÜ s ‚àß ‚àÄ y ‚àà segment ‚Ñù x‚ÇÄ x, ‚Äñf' y‚Äñ ‚â§ c * ‚Äñx - x‚ÇÄ‚Äñ ^ n := by
    have h1 : ‚àÄ·∂† x in ùìù[s] x‚ÇÄ, x ‚àà s := eventually_mem_nhdsWithin
    filter_upwards [h1, hs.eventually_nhdsWithin_segment hx‚ÇÄs (hf' hc)] with x hxs h
    refine ‚ü®hs.segment_subset hx‚ÇÄs hxs, fun y hy ‚Ü¶ (h y hy).trans ?_‚ü©
    gcongr
    exact norm_sub_le_of_mem_segment hy
  filter_upwards [this] with x ‚ü®h_segment, h‚ü©
  convert (convex_segment x‚ÇÄ x).norm_image_sub_le_of_norm_hasFDerivWithin_le
    (f := fun x ‚Ü¶ f x - f x‚ÇÄ) (y := x) (x := x‚ÇÄ) (s := segment ‚Ñù x‚ÇÄ x) ?_ h
    (left_mem_segment ‚Ñù x‚ÇÄ x) (right_mem_segment ‚Ñù x‚ÇÄ x) using 1
  ¬∑ simp
  ¬∑ simp only [hasFDerivWithinAt_sub_const_iff]
    exact fun x hx ‚Ü¶ (hff' x (h_segment hx)).mono h_segment

theorem isLittleO_pow_succ_real {f f' : ‚Ñù ‚Üí E} {x‚ÇÄ : ‚Ñù} {n : ‚Ñï} {s : Set ‚Ñù}
    (hs : Convex ‚Ñù s) (hx‚ÇÄs : x‚ÇÄ ‚àà s)
    (hff' : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) (hf' : f' =o[ùìù[s] x‚ÇÄ] fun x ‚Ü¶ (x - x‚ÇÄ) ^ n) :
    (fun x ‚Ü¶ f x - f x‚ÇÄ) =o[ùìù[s] x‚ÇÄ] fun x ‚Ü¶ (x - x‚ÇÄ) ^ (n + 1) := by
  have h := hs.isLittleO_pow_succ hx‚ÇÄs hff' ?_ (n := n)
  ¬∑ rw [Asymptotics.isLittleO_iff] at h ‚ä¢
    simpa using h
  ¬∑ rw [Asymptotics.isLittleO_iff] at hf' ‚ä¢
    convert hf' using 4 with c hc x
    simp

end Convex

namespace Convex

variable {ùïú G : Type*} [RCLike ùïú] [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f f' : ùïú ‚Üí G} {s : Set ùïú} {x y : ùïú}

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `HasDerivWithinAt`. -/
theorem norm_image_sub_le_of_norm_hasDerivWithin_le {C : ‚Ñù}
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s)
    (xs : x ‚àà s) (ys : y ‚àà s) : ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `HasDerivWithinAt` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_hasDerivWithin_le {C : ‚Ñù‚â•0} (hs : Convex ‚Ñù s)
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) (bound : ‚àÄ x ‚àà s, ‚Äñf' x‚Äñ‚Çä ‚â§ C) :
    LipschitzOnWith C f s :=
  Convex.lipschitzOnWith_of_nnnorm_hasFDerivWithin_le (fun x hx => (hf x hx).hasFDerivWithinAt)
    (fun x hx => le_trans (by simp) (bound x hx)) hs

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function within
this set is bounded by `C`, then the function is `C`-Lipschitz. Version with `derivWithin` -/
theorem norm_image_sub_le_of_norm_derivWithin_le {C : ‚Ñù} (hf : DifferentiableOn ùïú f s)
    (bound : ‚àÄ x ‚àà s, ‚ÄñderivWithin f s x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound xs
    ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `derivWithin` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_derivWithin_le {C : ‚Ñù‚â•0} (hs : Convex ‚Ñù s)
    (hf : DifferentiableOn ùïú f s) (bound : ‚àÄ x ‚àà s, ‚ÄñderivWithin f s x‚Äñ‚Çä ‚â§ C) :
    LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le (fun x hx => (hf x hx).hasDerivWithinAt) bound

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C`, then the function is `C`-Lipschitz. Version with `deriv`. -/
theorem norm_image_sub_le_of_norm_deriv_le {C : ‚Ñù} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñderiv f x‚Äñ ‚â§ C) (hs : Convex ‚Ñù s) (xs : x ‚àà s) (ys : y ‚àà s) :
    ‚Äñf y - f x‚Äñ ‚â§ C * ‚Äñy - x‚Äñ :=
  hs.norm_image_sub_le_of_norm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound xs ys

/-- The mean value theorem on a convex set in dimension 1: if the derivative of a function is
bounded by `C` on `s`, then the function is `C`-Lipschitz on `s`.
Version with `deriv` and `LipschitzOnWith`. -/
theorem lipschitzOnWith_of_nnnorm_deriv_le {C : ‚Ñù‚â•0} (hf : ‚àÄ x ‚àà s, DifferentiableAt ùïú f x)
    (bound : ‚àÄ x ‚àà s, ‚Äñderiv f x‚Äñ‚Çä ‚â§ C) (hs : Convex ‚Ñù s) : LipschitzOnWith C f s :=
  hs.lipschitzOnWith_of_nnnorm_hasDerivWithin_le
    (fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt) bound

/-- The mean value theorem set in dimension 1: if the derivative of a function is bounded by `C`,
then the function is `C`-Lipschitz. Version with `deriv` and `LipschitzWith`. -/
theorem _root_.lipschitzWith_of_nnnorm_deriv_le {C : ‚Ñù‚â•0} (hf : Differentiable ùïú f)
    (bound : ‚àÄ x, ‚Äñderiv f x‚Äñ‚Çä ‚â§ C) : LipschitzWith C f :=
  lipschitzOnWith_univ.1 <|
    convex_univ.lipschitzOnWith_of_nnnorm_deriv_le (fun x _ => hf x) fun x _ => bound x

/-- If `f : ùïú ‚Üí G`, `ùïú = R` or `ùïú = ‚ÑÇ`, is differentiable everywhere and its derivative equal zero,
then it is a constant function. -/
theorem _root_.is_const_of_deriv_eq_zero (hf : Differentiable ùïú f) (hf' : ‚àÄ x, deriv f x = 0)
    (x y : ùïú) : f x = f y :=
  is_const_of_fderiv_eq_zero hf (fun z => by ext; simp [‚Üê deriv_fderiv, hf']) _ _

theorem _root_.IsOpen.isOpen_inter_preimage_of_deriv_eq_zero
    (hs : IsOpen s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (deriv f) 0) (t : Set G) : IsOpen (s ‚à© f ‚Åª¬π' t) :=
  hs.isOpen_inter_preimage_of_fderiv_eq_zero hf
    (fun x hx ‚Ü¶ by ext; simp [‚Üê deriv_fderiv, hf' hx]) t

theorem _root_.IsOpen.exists_is_const_of_deriv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (deriv f) 0) : ‚àÉ a, ‚àÄ x ‚àà s, f x = a :=
  hs.exists_is_const_of_fderiv_eq_zero hs' hf (fun {x} hx ‚Ü¶ by ext; simp [‚Üê deriv_fderiv, hf' hx])

theorem _root_.IsOpen.is_const_of_deriv_eq_zero
    (hs : IsOpen s) (hs' : IsPreconnected s) (hf : DifferentiableOn ùïú f s)
    (hf' : s.EqOn (deriv f) 0) {x y : ùïú} (hx : x ‚àà s) (hy : y ‚àà s) : f x = f y :=
  hs.is_const_of_fderiv_eq_zero hs' hf (fun a ha ‚Ü¶ by ext; simp [‚Üê deriv_fderiv, hf' ha]) hx hy

theorem _root_.IsOpen.exists_eq_add_of_deriv_eq {f g : ùïú ‚Üí G} (hs : IsOpen s)
    (hs' : IsPreconnected s)
    (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s)
    (hf' : s.EqOn (deriv f) (deriv g)) : ‚àÉ a, s.EqOn f (g ¬∑ + a) :=
  hs.exists_eq_add_of_fderiv_eq hs' hf hg (fun x hx ‚Ü¶ by ext; simp [‚Üê deriv_fderiv, hf' hx])

theorem _root_.IsOpen.eqOn_of_deriv_eq {f g : ùïú ‚Üí G} (hs : IsOpen s)
    (hs' : IsPreconnected s) (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s)
    (hf' : s.EqOn (deriv f) (deriv g)) (hx : x ‚àà s) (hfgx : f x = g x) :
    s.EqOn f g :=
  hs.eqOn_of_fderiv_eq hs' hf hg (fun _ hx ‚Ü¶ ContinuousLinearMap.ext_ring (hf' hx)) hx hfgx

end Convex

end

section RCLike

/-!
### Vector-valued functions `f : E ‚Üí F`. Strict differentiability.

A `C^1` function is strictly differentiable, when the field is `‚Ñù` or `‚ÑÇ`. This follows from the
mean value inequality on balls, which is a particular case of the above results after restricting
the scalars to `‚Ñù`. Note that it does not make sense to talk of a convex set over `‚ÑÇ`, but balls
make sense and are enough. Many formulations of the mean value inequality could be generalized to
balls over `‚Ñù` or `‚ÑÇ`. For now, we only include the ones that we need.
-/

variable {ùïú : Type*} [RCLike ùïú] {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G] {H : Type*}
  [NormedAddCommGroup H] [NormedSpace ùïú H] {f : G ‚Üí H} {f' : G ‚Üí G ‚ÜíL[ùïú] H} {x : G}

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt
    (hder : ‚àÄ·∂† y in ùìù x, HasFDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictFDerivAt f (f' x) x := by
  -- turn little-o definition of strict_fderiv into an epsilon-delta statement
  rw [hasStrictFDerivAt_iff_isLittleO, isLittleO_iff]
  refine fun c hc => Metric.eventually_nhds_iff_ball.mpr ?_
  -- the correct Œµ is the modulus of continuity of f'
  rcases Metric.mem_nhds_iff.mp (inter_mem hder (hcont <| ball_mem_nhds _ hc)) with ‚ü®Œµ, Œµ0, hŒµ‚ü©
  refine ‚ü®Œµ, Œµ0, ?_‚ü©
  -- simplify formulas involving the product E √ó E
  rintro ‚ü®a, b‚ü© h
  rw [‚Üê ball_prod_same, prodMk_mem_set_prod_eq] at h
  -- exploit the choice of Œµ as the modulus of continuity of f'
  have hf' : ‚àÄ x' ‚àà ball x Œµ, ‚Äñf' x' - f' x‚Äñ ‚â§ c := fun x' H' => by
    rw [‚Üê dist_eq_norm]
    exact le_of_lt (hŒµ H').2
  -- apply mean value theorem
  letI : NormedSpace ‚Ñù G := RestrictScalars.normedSpace ‚Ñù ùïú G
  refine (convex_ball _ _).norm_image_sub_le_of_norm_hasFDerivWithin_le' ?_ hf' h.2 h.1
  exact fun y hy => (hŒµ hy).1.hasFDerivWithinAt

/-- Over the reals or the complexes, a continuously differentiable function is strictly
differentiable. -/
theorem hasStrictDerivAt_of_hasDerivAt_of_continuousAt {f f' : ùïú ‚Üí G} {x : ùïú}
    (hder : ‚àÄ·∂† y in ùìù x, HasDerivAt f (f' y) y) (hcont : ContinuousAt f' x) :
    HasStrictDerivAt f (f' x) x :=
  hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt (hder.mono fun _ hy => hy.hasFDerivAt) <|
    (smulRightL ùïú ùïú G 1).continuous.continuousAt.comp hcont

end RCLike
