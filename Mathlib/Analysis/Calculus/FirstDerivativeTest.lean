/-
Copyright (c) 2024 BjÃ¸rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BjÃ¸rn Kjos-Hanssen
-/
import Mathlib.Analysis.Calculus.MeanValue
import Mathlib.Order.Interval.Set.Basic

/-!
# The First-Derivative Test

We prove the first-derivative test in the strong form given on Wikipedia.

The test is proved over the real numbers â„
using `monotoneOn_of_deriv_nonneg` from [Mathlib.Analysis.Calculus.MeanValue].

## Main results

* `first_derivative_test_max`: Suppose `f` is a real-valued function of a real variable
  defined on some interval containing the point `a`.
  Further suppose that `f` is continuous at `a` and differentiable on some open interval
  containing `a`, except possibly at `a` itself.

  If there exists a positive number `r > 0` such that for every `x` in `(a âˆ’ r, a)`
  we have `fâ€²(x) â‰¥ 0`, and for every `x` in `(a, a + r)` we have `fâ€²(x) â‰¤ 0`,
  then `f` has a local maximum at `a`.

* `first_derivative_test_min`: The dual of `first_derivative_max`, for minima.

## Tags

derivative test, calculus
-/

open Set

/-!
### Some facts about differentiability and continuity

We prove a couple of auxiliary lemmas elaborating on facts such as
"differentiable implies continuous",
"an open interval is an open set", and "`fun x => -x` is antitone". -/


/-- If `f` is differentiable on `(a,b)`, and `x âˆˆ (a,b)`, then `f` is differentiable at `x`.-/
theorem differentiableOn_differentiableAt_Ioo.{u_1, u_2, u_3} {ğ•œ : Type u_1}
  [NontriviallyNormedField ğ•œ] {E : Type u_2} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [LinearOrder E] [OrderClosedTopology E]
    {a x b : E} (hab : x âˆˆ Set.Ioo a b)
    {F : Type u_3} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    {f : E â†’ F}
    (hdâ‚€ : DifferentiableOn ğ•œ f (Set.Ioo a b)) :
    DifferentiableAt ğ•œ f x := by
  apply DifferentiableOn.differentiableAt
  exact hdâ‚€
  refine IsOpen.mem_nhds ?hs.hs hab
  apply isOpen_Ioo

/-- If `f` is continuous at `b` and differentiable on `(a,b)` then `f` is
  continuous on the half-open interval `(a,b]`. -/
theorem continuous_Ioc.{u_1, u_2, u_3} {ğ•œ : Type u_1} [NontriviallyNormedField ğ•œ]
  {E : Type u_2} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [LinearOrder E] [OrderClosedTopology E]
    {F : Type u_3} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    {f : E â†’ F}
    {a b : E}
    (gâ‚€ : a < b) (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn ğ•œ f (Set.Ioo a b)) : ContinuousOn f (Set.Ioc a b) :=
  fun _ hx â†¦ (Ioo_union_right gâ‚€ â–¸ hx).elim
  (fun hx â†¦ (hdâ‚€.differentiableAt <| Ioo_mem_nhds hx.1 hx.2).continuousAt.continuousWithinAt)
  (fun hx â†¦ mem_singleton_iff.1 hx â–¸ h.continuousWithinAt)

/-- If `f` is continuous at `b` and differentiable on `(b,c)` then `f` is
  continuous on the half-open interval `[b,c)`. -/
theorem continuous_Ico.{u_1, u_2, u_3} {ğ•œ : Type u_1} [NontriviallyNormedField ğ•œ]
  {E : Type u_2} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [LinearOrder E] [OrderClosedTopology E]
    {F : Type u_3} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    {f : E â†’ F}
    {b c : E} (gâ‚ : b < c)
    (h : ContinuousAt f b) (hdâ‚ : DifferentiableOn ğ•œ f (Set.Ioo b c)) :
    ContinuousOn f (Set.Ico b c) :=
  fun _ hx â†¦ (Ioo_union_left gâ‚ â–¸ hx).elim
  (fun hx â†¦ (hdâ‚.differentiableAt <| Ioo_mem_nhds hx.1 hx.2).continuousAt.continuousWithinAt)
  (fun hx â†¦ mem_singleton_iff.1 hx â–¸ h.continuousWithinAt)

/-- If `f` is differentiable on a set `s` then so is `-f`. -/
theorem differentiableOn_neg_Ioo
  {f : â„ â†’ â„} {s : Set â„} (hdâ‚€ : DifferentiableOn â„ f s) :
    DifferentiableOn â„ (-f) s :=
    (show -f = ((fun x => -x) âˆ˜ (fun x => f x)) by rfl)
      â–¸ (DifferentiableOn.comp (differentiableOn_neg Set.univ) hdâ‚€)
        (fun _ _ â†¦ trivial)

/-- If `f'` is the derivative of `f` then  `f' x â‰¤ 0 â†’ 0 â‰¤ (-f)' x`. -/
theorem deriv_neg_nonneg {f : â„ â†’ â„} {a b : â„}
  (hdâ‚€ : DifferentiableOn â„ f (Set.Ioo a b))
    (hâ‚€ : âˆ€ x âˆˆ Set.Ioo a b, deriv f x â‰¤ 0) (x : â„)
    (hx : x âˆˆ Set.Ioo a b) : 0 â‰¤ deriv (-f) x :=
  (@deriv.comp â„ _ x â„ _ _ f (fun x => -x)
    (Differentiable.differentiableAt differentiable_neg)
    (DifferentiableOn.differentiableAt hdâ‚€ (Ioo_mem_nhds hx.1 hx.2))) â–¸ (by
    rw [deriv_neg'', neg_mul, one_mul, Left.nonneg_neg_iff];
    exact hâ‚€ _ hx
  )

/-- If `f'` is the derivative of `f` then  `0 â‰¤ f' x â†’ (-f)' x â‰¤ 0`. -/
theorem deriv_neg_nonpos {f : â„ â†’ â„} {b c : â„}
  (hdâ‚ : DifferentiableOn â„ f (Set.Ioo b c))
  (hâ‚ : âˆ€ x âˆˆ Set.Ioo b c, 0 â‰¤ deriv f x) (x : â„) :
  x âˆˆ Set.Ioo b c â†’ deriv (-f) x â‰¤ 0 :=
    fun hx => (@deriv.comp â„ _ x â„ _ _ f (fun x => -x)
    (Differentiable.differentiableAt differentiable_neg)
    (DifferentiableOn.differentiableAt hdâ‚ (Ioo_mem_nhds hx.1 hx.2))) â–¸ (by
    rw [deriv_neg'', neg_mul, one_mul, Left.neg_nonpos_iff]
    exact hâ‚ _ hx
  )

/-!
### The First-Derivative Test

Using the connection beetween monotonicity and derivatives we obtain the familiar
First-Derivative Test from calculus.
-/

/-- If `f` is monotone on `(a,b]` and antitone on `[b,c)` then `f` has
a local maximum at `b`. -/
lemma isLocalMax_of_mono_anti.{u, v}
  {Î± : Type u} [TopologicalSpace Î±] [LinearOrder Î±] [OrderClosedTopology Î±]
    {Î² : Type v} [Preorder Î²]
    {a b c : Î±} (gâ‚€ : a < b) (gâ‚ : b < c)
    {f : Î± â†’ Î²}
    (hâ‚€ : MonotoneOn f (Set.Ioc a b))
    (hâ‚ : AntitoneOn f (Set.Ico b c)) : IsLocalMax f b := by
  unfold IsLocalMax IsMaxFilter Filter.Eventually
  rw [nhds_def, Filter.mem_iInf]
  use {Set.Ioo a c}, (Set.toFinite _), (fun _ â†¦ Set.Ioo a c âˆª {x | f x â‰¤ f b})
  simp only [Set.mem_setOf_eq, Subtype.forall, Set.mem_singleton_iff, forall_eq, Set.mem_Ioo,
    Set.iInter_coe_set, Set.iInter_iInter_eq_left]
  constructor
  Â· exact Filter.mem_iInf_of_mem
      (by simp_all only [and_self, true_and]; apply isOpen_Ioo)
      (by simp_all)
  Â· ext u
    simp only [Set.mem_setOf_eq, Set.mem_union, Set.mem_Ioo, iff_or_self, and_imp]
    intros
    exact (em (u < b)).elim
      (fun H => hâ‚€ (by simp_all only [mem_Ioc, true_and]; exact le_of_lt H)
        (by simp_all) (le_of_lt H))
      (fun H => hâ‚ (by simp_all) (by simp_all) (le_of_not_lt H))

 /-- The First-Derivative Test from calculus, maxima version.
  Suppose `a < b < c`,
    `f : â„ â†’ â„` is continuous at `b`,
    the derivative `f'` is nonnegative on `(a,b)`, and
    the derivative `f'` is nonpositive on `(b,c)`.
  Then `f` has a local maximum at `a`. -/
lemma first_derivative_test_max {f : â„ â†’ â„} {a b c : â„}
  (gâ‚€ : a < b) (gâ‚ : b < c)
    (h : ContinuousAt f b)
    (hdâ‚€ : DifferentiableOn â„ f (Set.Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Set.Ioo b c))
    (hâ‚€ :  âˆ€ x âˆˆ Set.Ioo a b, 0 â‰¤ deriv f x)
    (hâ‚ :  âˆ€ x âˆˆ Set.Ioo b c, deriv f x â‰¤ 0)
    : IsLocalMax f b :=
  isLocalMax_of_mono_anti gâ‚€ gâ‚
    (monotoneOn_of_deriv_nonneg (convex_Ioc a b)
    (continuous_Ioc gâ‚€ h hdâ‚€) (by simp_all) (by simp_all))
    (antitoneOn_of_deriv_nonpos (convex_Ico b c)
    (continuous_Ico gâ‚ h hdâ‚) (by simp_all) (by simp_all))

/-- The First-Derivative Test from calculus, minima version. -/
lemma first_derivative_test_min {f : â„ â†’ â„} {a b c : â„}
  (h : ContinuousAt f b)
    {gâ‚€ : a < b} {gâ‚ : b < c}
    (hdâ‚€ : DifferentiableOn â„ f (Set.Ioo a b))
    (hdâ‚ : DifferentiableOn â„ f (Set.Ioo b c))
    (hâ‚€ : âˆ€ x âˆˆ Set.Ioo a b, deriv f x â‰¤ 0)
    (hâ‚ : âˆ€ x âˆˆ Set.Ioo b c, 0 â‰¤ deriv f x)
    : IsLocalMin f b := by
    have Q := @first_derivative_test_max (-f) a b c gâ‚€ gâ‚
      (by simp_all)
      (by simp_all[differentiableOn_neg_Ioo])
      (by simp_all[differentiableOn_neg_Ioo])
      (by intro x;apply deriv_neg_nonneg;repeat tauto)
      (by intro x;apply deriv_neg_nonpos;repeat tauto)
    unfold IsLocalMin IsMinFilter
    unfold IsLocalMax IsMaxFilter at Q
    simp only [Pi.neg_apply, neg_le_neg_iff] at Q; exact Q
