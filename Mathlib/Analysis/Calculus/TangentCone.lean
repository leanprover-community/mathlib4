/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Convex.Topology
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Seminorm
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Tangent cone

In this file, we define two predicates `UniqueDiffWithinAt ğ•œ s x` and `UniqueDiffOn ğ•œ s`
ensuring that, if a function has two derivatives, then they have to coincide. As a direct
definition of this fact (quantifying on all target types and all functions) would depend on
universes, we use a more intrinsic definition: if all the possible tangent directions to the set
`s` at the point `x` span a dense subset of the whole subset, it is easy to check that the
derivative has to be unique.

Therefore, we introduce the set of all tangent directions, named `tangentConeAt`,
and express `UniqueDiffWithinAt` and `UniqueDiffOn` in terms of it.
One should however think of this definition as an implementation detail: the only reason to
introduce the predicates `UniqueDiffWithinAt` and `UniqueDiffOn` is to ensure the uniqueness
of the derivative. This is why their names reflect their uses, and not how they are defined.

## Implementation details

Note that this file is imported by `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`. Hence, derivatives
are not defined yet. The property of uniqueness of the derivative is therefore proved in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`, but based on the properties of the tangent cone we
prove here.
-/


variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]

open Filter Set Metric
open scoped Topology Pointwise

section TangentCone

variable {E : Type*} [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E]

/-- The set of all tangent directions to the set `s` at the point `x`. -/
def tangentConeAt (s : Set E) (x : E) : Set E :=
  { y : E | âˆƒ (c : â„• â†’ ğ•œ) (d : â„• â†’ E),
    (âˆ€á¶  n in atTop, x + d n âˆˆ s) âˆ§
    Tendsto (fun n => â€–c nâ€–) atTop atTop âˆ§
    Tendsto (fun n => c n â€¢ d n) atTop (ğ“ y) }

/-- A property ensuring that the tangent cone to `s` at `x` spans a dense subset of the whole space.
The main role of this property is to ensure that the differential within `s` at `x` is unique,
hence this name. The uniqueness it asserts is proved in `UniqueDiffWithinAt.eq` in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.
To avoid pathologies in dimension 0, we also require that `x` belongs to the closure of `s` (which
is automatic when `E` is not `0`-dimensional). -/
@[mk_iff]
structure UniqueDiffWithinAt (s : Set E) (x : E) : Prop where
  dense_tangentConeAt : Dense (Submodule.span ğ•œ (tangentConeAt ğ•œ s x) : Set E)
  mem_closure : x âˆˆ closure s

@[deprecated (since := "2025-04-27")]
alias UniqueDiffWithinAt.dense_tangentCone := UniqueDiffWithinAt.dense_tangentConeAt

/-- A property ensuring that the tangent cone to `s` at any of its points spans a dense subset of
the whole space. The main role of this property is to ensure that the differential along `s` is
unique, hence this name. The uniqueness it asserts is proved in `UniqueDiffOn.eq` in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`. -/
def UniqueDiffOn (s : Set E) : Prop :=
  âˆ€ x âˆˆ s, UniqueDiffWithinAt ğ•œ s x

end TangentCone

variable {ğ•œ}
variable {E F G : Type*}

section TangentCone

-- This section is devoted to the properties of the tangent cone.

open NormedField
section TVS
variable [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {x y : E} {s t : Set E}

theorem mem_tangentConeAt_of_pow_smul {r : ğ•œ} (hrâ‚€ : r â‰  0) (hr : â€–râ€– < 1)
    (hs : âˆ€á¶  n : â„• in atTop, x + r ^ n â€¢ y âˆˆ s) : y âˆˆ tangentConeAt ğ•œ s x := by
  refine âŸ¨fun n â†¦ (r ^ n)â»Â¹, fun n â†¦ r ^ n â€¢ y, hs, ?_, ?_âŸ©
  Â· simp only [norm_inv, norm_pow, â† inv_pow]
    exact tendsto_pow_atTop_atTop_of_one_lt <| (one_lt_invâ‚€ (norm_pos_iff.2 hrâ‚€)).2 hr
  Â· simp only [inv_smul_smulâ‚€ (pow_ne_zero _ hrâ‚€), tendsto_const_nhds]

@[simp]
theorem tangentConeAt_univ : tangentConeAt ğ•œ univ x = univ :=
  let âŸ¨_r, hrâ‚€, hrâŸ© := exists_norm_lt_one ğ•œ
  eq_univ_of_forall fun _ â†¦ mem_tangentConeAt_of_pow_smul (norm_pos_iff.1 hrâ‚€) hr <|
    Eventually.of_forall fun _ â†¦ mem_univ _

@[deprecated (since := "2025-04-27")] alias tangentCone_univ := tangentConeAt_univ

@[gcongr]
theorem tangentConeAt_mono (h : s âŠ† t) : tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := by
  rintro y âŸ¨c, d, ds, ctop, climâŸ©
  exact âŸ¨c, d, mem_of_superset ds fun n hn => h hn, ctop, climâŸ©

@[deprecated (since := "2025-04-27")] alias tangentCone_mono := tangentConeAt_mono

end TVS

section Normed
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable [NormedAddCommGroup G] [NormedSpace â„ G]
variable {x y : E} {s t : Set E}

@[simp]
theorem tangentConeAt_closure : tangentConeAt ğ•œ (closure s) x = tangentConeAt ğ•œ s x := by
  refine Subset.antisymm ?_ (tangentConeAt_mono subset_closure)
  rintro y âŸ¨c, d, ds, ctop, climâŸ©
  obtain âŸ¨u, -, u_pos, u_limâŸ© :
      âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ Tendsto u atTop (ğ“ (0 : â„)) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have : âˆ€á¶  n in atTop, âˆƒ d', x + d' âˆˆ s âˆ§ dist (c n â€¢ d n) (c n â€¢ d') < u n := by
    filter_upwards [ctop.eventually_gt_atTop 0, ds] with n hn hns
    rcases Metric.mem_closure_iff.mp hns (u n / â€–c nâ€–) (div_pos (u_pos n) hn) with âŸ¨y, hys, hyâŸ©
    refine âŸ¨y - x, by simpa, ?_âŸ©
    rwa [dist_smulâ‚€, â† dist_add_left x, add_sub_cancel, â† lt_div_iffâ‚€' hn]
  simp only [Filter.skolem, eventually_and] at this
  rcases this with âŸ¨d', hd's, hd'âŸ©
  exact âŸ¨c, d', hd's, ctop, clim.congr_dist
    (squeeze_zero' (.of_forall fun _ â†¦ dist_nonneg) (hd'.mono fun _ â†¦ le_of_lt) u_lim)âŸ©

/-- Auxiliary lemma ensuring that, under the assumptions defining the tangent cone,
the sequence `d` tends to 0 at infinity. -/
theorem tangentConeAt.lim_zero {Î± : Type*} (l : Filter Î±) {c : Î± â†’ ğ•œ} {d : Î± â†’ E}
    (hc : Tendsto (fun n => â€–c nâ€–) l atTop) (hd : Tendsto (fun n => c n â€¢ d n) l (ğ“ y)) :
    Tendsto d l (ğ“ 0) := by
  have A : Tendsto (fun n => â€–c nâ€–â»Â¹) l (ğ“ 0) := tendsto_inv_atTop_zero.comp hc
  have B : Tendsto (fun n => â€–c n â€¢ d nâ€–) l (ğ“ â€–yâ€–) := (continuous_norm.tendsto _).comp hd
  have C : Tendsto (fun n => â€–c nâ€–â»Â¹ * â€–c n â€¢ d nâ€–) l (ğ“ (0 * â€–yâ€–)) := A.mul B
  rw [zero_mul] at C
  have : âˆ€á¶  n in l, â€–c nâ€–â»Â¹ * â€–c n â€¢ d nâ€– = â€–d nâ€– := by
    refine (eventually_ne_of_tendsto_norm_atTop hc 0).mono fun n hn => ?_
    rw [norm_smul, â† mul_assoc, inv_mul_cancelâ‚€, one_mul]
    rwa [Ne, norm_eq_zero]
  have D : Tendsto (fun n => â€–d nâ€–) l (ğ“ 0) := Tendsto.congr' this C
  rw [tendsto_zero_iff_norm_tendsto_zero]
  exact D

theorem tangentConeAt_mono_nhds (h : ğ“[s] x â‰¤ ğ“[t] x) :
    tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := by
  rintro y âŸ¨c, d, ds, ctop, climâŸ©
  refine âŸ¨c, d, ?_, ctop, climâŸ©
  suffices Tendsto (fun n => x + d n) atTop (ğ“[t] x) from
    tendsto_principal.1 (tendsto_inf.1 this).2
  refine (tendsto_inf.2 âŸ¨?_, tendsto_principal.2 dsâŸ©).mono_right h
  simpa only [add_zero] using tendsto_const_nhds.add (tangentConeAt.lim_zero atTop ctop clim)

@[deprecated (since := "2025-04-27")] alias tangentCone_mono_nhds := tangentConeAt_mono_nhds

/-- Tangent cone of `s` at `x` depends only on `ğ“[s] x`. -/
theorem tangentConeAt_congr (h : ğ“[s] x = ğ“[t] x) : tangentConeAt ğ•œ s x = tangentConeAt ğ•œ t x :=
  Subset.antisymm (tangentConeAt_mono_nhds h.le) (tangentConeAt_mono_nhds h.ge)

@[deprecated (since := "2025-04-27")] alias tangentCone_congr := tangentConeAt_congr

/-- Intersecting with a neighborhood of the point does not change the tangent cone. -/
theorem tangentConeAt_inter_nhds (ht : t âˆˆ ğ“ x) : tangentConeAt ğ•œ (s âˆ© t) x = tangentConeAt ğ•œ s x :=
  tangentConeAt_congr (nhdsWithin_restrict' _ ht).symm

@[deprecated (since := "2025-04-27")] alias tangentCone_inter_nhds := tangentConeAt_inter_nhds

/-- The tangent cone of a product contains the tangent cone of its left factor. -/
theorem subset_tangentConeAt_prod_left {t : Set F} {y : F} (ht : y âˆˆ closure t) :
    LinearMap.inl ğ•œ E F '' tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ (s Ã—Ë¢ t) (x, y) := by
  rintro _ âŸ¨v, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©
  have : âˆ€ n, âˆƒ d', y + d' âˆˆ t âˆ§ â€–c n â€¢ d'â€– < ((1 : â„) / 2) ^ n := by
    intro n
    rcases mem_closure_iff_nhds.1 ht _
        (eventually_nhds_norm_smul_sub_lt (c n) y (pow_pos one_half_pos n)) with
      âŸ¨z, hz, hztâŸ©
    exact âŸ¨z - y, by simpa using hzt, by simpa using hzâŸ©
  choose d' hd' using this
  refine âŸ¨c, fun n => (d n, d' n), ?_, hc, ?_âŸ©
  Â· change âˆ€á¶  n in atTop, (x, y) + (d n, d' n) âˆˆ s Ã—Ë¢ t
    filter_upwards [hd] with n hn
    simp [hn, (hd' n).1]
  Â· apply Tendsto.prodMk_nhds hy _
    refine squeeze_zero_norm (fun n => (hd' n).2.le) ?_
    exact tendsto_pow_atTop_nhds_zero_of_lt_one one_half_pos.le one_half_lt_one

@[deprecated (since := "2025-04-27")]
alias subset_tangentCone_prod_left := subset_tangentConeAt_prod_left

/-- The tangent cone of a product contains the tangent cone of its right factor. -/
theorem subset_tangentConeAt_prod_right {t : Set F} {y : F} (hs : x âˆˆ closure s) :
    LinearMap.inr ğ•œ E F '' tangentConeAt ğ•œ t y âŠ† tangentConeAt ğ•œ (s Ã—Ë¢ t) (x, y) := by
  rintro _ âŸ¨w, âŸ¨c, d, hd, hc, hyâŸ©, rflâŸ©
  have : âˆ€ n, âˆƒ d', x + d' âˆˆ s âˆ§ â€–c n â€¢ d'â€– < ((1 : â„) / 2) ^ n := by
    intro n
    rcases mem_closure_iff_nhds.1 hs _
        (eventually_nhds_norm_smul_sub_lt (c n) x (pow_pos one_half_pos n)) with
      âŸ¨z, hz, hzsâŸ©
    exact âŸ¨z - x, by simpa using hzs, by simpa using hzâŸ©
  choose d' hd' using this
  refine âŸ¨c, fun n => (d' n, d n), ?_, hc, ?_âŸ©
  Â· change âˆ€á¶  n in atTop, (x, y) + (d' n, d n) âˆˆ s Ã—Ë¢ t
    filter_upwards [hd] with n hn
    simp [hn, (hd' n).1]
  Â· apply Tendsto.prodMk_nhds _ hy
    refine squeeze_zero_norm (fun n => (hd' n).2.le) ?_
    exact tendsto_pow_atTop_nhds_zero_of_lt_one one_half_pos.le one_half_lt_one

@[deprecated (since := "2025-04-27")]
alias subset_tangentCone_prod_right := subset_tangentConeAt_prod_right

/-- The tangent cone of a product contains the tangent cone of each factor. -/
theorem mapsTo_tangentConeAt_pi {Î¹ : Type*} [DecidableEq Î¹] {E : Î¹ â†’ Type*}
    [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] {s : âˆ€ i, Set (E i)} {x : âˆ€ i, E i}
    {i : Î¹} (hi : âˆ€ j â‰  i, x j âˆˆ closure (s j)) :
    MapsTo (LinearMap.single ğ•œ E i) (tangentConeAt ğ•œ (s i) (x i))
      (tangentConeAt ğ•œ (Set.pi univ s) x) := by
  rintro w âŸ¨c, d, hd, hc, hyâŸ©
  have : âˆ€ n, âˆ€ j â‰  i, âˆƒ d', x j + d' âˆˆ s j âˆ§ â€–c n â€¢ d'â€– < (1 / 2 : â„) ^ n := fun n j hj â†¦ by
    rcases mem_closure_iff_nhds.1 (hi j hj) _
        (eventually_nhds_norm_smul_sub_lt (c n) (x j) (pow_pos one_half_pos n)) with
      âŸ¨z, hz, hzsâŸ©
    exact âŸ¨z - x j, by simpa using hzs, by simpa using hzâŸ©
  choose! d' hd's hcd' using this
  refine âŸ¨c, fun n => Function.update (d' n) i (d n), hd.mono fun n hn j _ => ?_, hc,
      tendsto_pi_nhds.2 fun j => ?_âŸ©
  Â· rcases em (j = i) with (rfl | hj) <;> simp [*]
  Â· rcases em (j = i) with (rfl | hj)
    Â· simp [hy]
    Â· suffices Tendsto (fun n => c n â€¢ d' n j) atTop (ğ“ 0) by simpa [hj]
      refine squeeze_zero_norm (fun n => (hcd' n j hj).le) ?_
      exact tendsto_pow_atTop_nhds_zero_of_lt_one one_half_pos.le one_half_lt_one

@[deprecated (since := "2025-04-27")] alias mapsTo_tangentCone_pi := mapsTo_tangentConeAt_pi

/-- If a subset of a real vector space contains an open segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
theorem mem_tangentConeAt_of_openSegment_subset {s : Set G} {x y : G} (h : openSegment â„ x y âŠ† s) :
    y - x âˆˆ tangentConeAt â„ s x := by
  refine mem_tangentConeAt_of_pow_smul one_half_pos.ne' (by norm_num) ?_
  refine (eventually_ne_atTop 0).mono fun n hn â†¦ (h ?_)
  rw [openSegment_eq_image]
  refine âŸ¨(1 / 2) ^ n, âŸ¨?_, ?_âŸ©, ?_âŸ©
  Â· exact pow_pos one_half_pos _
  Â· exact pow_lt_oneâ‚€ one_half_pos.le one_half_lt_one hn
  Â· simp only [sub_smul, one_smul, smul_sub]; abel

@[deprecated (since := "2025-04-27")]
alias mem_tangentCone_of_openSegment_subset := mem_tangentConeAt_of_openSegment_subset

/-- If a subset of a real vector space contains a segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
theorem mem_tangentConeAt_of_segment_subset {s : Set G} {x y : G} (h : segment â„ x y âŠ† s) :
    y - x âˆˆ tangentConeAt â„ s x :=
  mem_tangentConeAt_of_openSegment_subset ((openSegment_subset_segment â„ x y).trans h)

@[deprecated (since := "2025-04-27")]
alias mem_tangentCone_of_segment_subset := mem_tangentConeAt_of_segment_subset

/-- The tangent cone at a non-isolated point contains `0`. -/
theorem zero_mem_tangentCone {s : Set E} {x : E} (hx : x âˆˆ closure s) :
    0 âˆˆ tangentConeAt ğ•œ s x := by
  /- Take a sequence `d n` tending to `0` such that `x + d n âˆˆ s`. Taking `c n` of the order
  of `1 / (d n) ^ (1/2)`, then `c n` tends to infinity, but `c n â€¢ d n` tends to `0`. By definition,
  this shows that `0` belongs to the tangent cone. -/
  obtain âŸ¨u, -, hu, u_limâŸ© :
      âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n âˆ§ u n < 1) âˆ§ Tendsto u atTop (ğ“ (0 : â„)) :=
    exists_seq_strictAnti_tendsto' one_pos
  choose u_pos u_lt_one using hu
  choose v hvs hvu using fun n â†¦ Metric.mem_closure_iff.mp hx _ (mul_pos (u_pos n) (u_pos n))
  let d n := v n - x
  let âŸ¨r, hrâŸ© := exists_one_lt_norm ğ•œ
  have A n := exists_nat_pow_near (one_le_inv_iffâ‚€.mpr âŸ¨u_pos n, (u_lt_one n).leâŸ©) hr
  choose m hm_le hlt_m using A
  set c := fun n â†¦ r ^ (m n + 1)
  have c_lim : Tendsto (fun n â†¦ â€–c nâ€–) atTop atTop := by
    simp only [c, norm_pow]
    refine tendsto_atTop_mono (fun n â†¦ (hlt_m n).le) <| .inv_tendsto_nhdsGT_zero ?_
    exact tendsto_nhdsWithin_iff.mpr âŸ¨u_lim, .of_forall u_posâŸ©
  refine âŸ¨c, d, .of_forall <| by simpa [d], c_lim, ?_âŸ©
  have Hle n : â€–c n â€¢ d nâ€– â‰¤ â€–râ€– * u n := by
    specialize u_pos n
    calc
      â€–c n â€¢ d nâ€– â‰¤ (u n)â»Â¹ * â€–râ€– * (u n * u n) := by
        simp only [c, norm_smul, norm_pow, pow_succ, norm_mul, d, â† dist_eq_norm']
        gcongr
        exacts [hm_le n, (hvu n).le]
      _ = â€–râ€– * u n := by field_simp
  refine squeeze_zero_norm Hle ?_
  simpa using tendsto_const_nhds.mul u_lim

/-- If `x` is not an accumulation point of `s, then the tangent cone of `s` at `x`
is a subset of `{0}`. -/
theorem tangentConeAt_subset_zero (hx : Â¬AccPt x (ğ“Ÿ s)) : tangentConeAt ğ•œ s x âŠ† 0 := by
  rintro y âŸ¨c, d, hds, hc, hcdâŸ©
  suffices âˆ€á¶  n in .atTop, d n = 0 from
    tendsto_nhds_unique hcd <| tendsto_const_nhds.congr' <| this.mono fun n hn â†¦ by simp [hn]
  simp only [accPt_iff_frequently, not_frequently, not_and', ne_eq, not_not] at hx
  have : Tendsto (x + d Â·) atTop (ğ“ x) := by
    simpa using tendsto_const_nhds.add (tangentConeAt.lim_zero _ hc hcd)
  filter_upwards [this.eventually hx, hds] with n hâ‚ hâ‚‚
  simpa using hâ‚ hâ‚‚

theorem UniqueDiffWithinAt.accPt [Nontrivial E] (h : UniqueDiffWithinAt ğ•œ s x) : AccPt x (ğ“Ÿ s) := by
  by_contra! h'
  have : Dense (Submodule.span ğ•œ (0 : Set E) : Set E) :=
    h.1.mono <| by gcongr; exact tangentConeAt_subset_zero h'
  simp [dense_iff_closure_eq] at this

/-- In a proper space, the tangent cone at a non-isolated point is nontrivial. -/
theorem tangentConeAt_nonempty_of_properSpace [ProperSpace E]
    {s : Set E} {x : E} (hx : AccPt x (ğ“Ÿ s)) :
    (tangentConeAt ğ•œ s x âˆ© {0}á¶œ).Nonempty := by
  /- Take a sequence `d n` tending to `0` such that `x + d n âˆˆ s`. Taking `c n` of the order
  of `1 / d n`. Then `c n â€¢ d n` belongs to a fixed annulus. By compactness, one can extract
  a subsequence converging to a limit `l`. Then `l` is nonzero, and by definition it belongs to
  the tangent cone. -/
  obtain âŸ¨u, -, u_pos, u_limâŸ© :
      âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ Tendsto u atTop (ğ“ (0 : â„)) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have A n : âˆƒ y âˆˆ closedBall x (u n) âˆ© s, y â‰  x :=
    (accPt_iff_nhds).mp hx _ (closedBall_mem_nhds _ (u_pos n))
  choose v hv hvx using A
  choose hvu hvs using hv
  let d := fun n â†¦ v n - x
  have M n : x + d n âˆˆ s \ {x} := by simp [d, hvs, hvx]
  let âŸ¨r, hrâŸ© := exists_one_lt_norm ğ•œ
  have W n := rescale_to_shell hr zero_lt_one (x := d n) (by simpa using (M n).2)
  choose c c_ne c_le le_c hc using W
  have c_lim : Tendsto (fun n â†¦ â€–c nâ€–) atTop atTop := by
    suffices Tendsto (fun n â†¦ â€–c nâ€–â»Â¹ â»Â¹ ) atTop atTop by simpa
    apply tendsto_inv_nhdsGT_zero.comp
    simp only [nhdsWithin, tendsto_inf, tendsto_principal, mem_Ioi, eventually_atTop, ge_iff_le]
    have B (n : â„•) : â€–c nâ€–â»Â¹ â‰¤ 1â»Â¹ * â€–râ€– * u n := by
      apply (hc n).trans
      gcongr
      simpa [d, dist_eq_norm] using hvu n
    refine âŸ¨?_, 0, fun n hn â†¦ by simpa using c_ne nâŸ©
    apply squeeze_zero (fun n â†¦ by positivity) B
    simpa using u_lim.const_mul _
  obtain âŸ¨l, l_mem, Ï†, Ï†_strict, hÏ†âŸ© :
      âˆƒ l âˆˆ Metric.closedBall (0 : E) 1 \ Metric.ball (0 : E) (1 / â€–râ€–),
      âˆƒ (Ï† : â„• â†’ â„•), StrictMono Ï† âˆ§ Tendsto ((fun n â†¦ c n â€¢ d n) âˆ˜ Ï†) atTop (ğ“ l) := by
    apply IsCompact.tendsto_subseq _ (fun n â†¦ ?_)
    Â· exact (isCompact_closedBall 0 1).diff Metric.isOpen_ball
    simp only [mem_diff, Metric.mem_closedBall, dist_zero_right, (c_le n).le,
      Metric.mem_ball, not_lt, true_and, le_c n]
  refine âŸ¨l, ?_, ?_âŸ©; swap
  Â· simp only [mem_compl_iff, mem_singleton_iff]
    contrapose! l_mem
    simp only [one_div, l_mem, mem_diff, Metric.mem_closedBall, dist_self, zero_le_one,
      Metric.mem_ball, inv_pos, norm_pos_iff, ne_eq, not_not, true_and]
    contrapose! hr
    simp [hr]
  refine âŸ¨c âˆ˜ Ï†, d âˆ˜ Ï†, .of_forall fun n â†¦ ?_, ?_, hÏ†âŸ©
  Â· simpa [d] using hvs (Ï† n)
  Â· exact c_lim.comp Ï†_strict.tendsto_atTop

@[deprecated (since := "2025-04-27")]
alias tangentCone_nonempty_of_properSpace := tangentConeAt_nonempty_of_properSpace

/-- The tangent cone at a non-isolated point in dimension 1 is the whole space. -/
theorem tangentConeAt_eq_univ {s : Set ğ•œ} {x : ğ•œ} (hx : AccPt x (ğ“Ÿ s)) :
    tangentConeAt ğ•œ s x = univ := by
  apply eq_univ_iff_forall.2 (fun y â†¦ ?_)
  -- first deal with the case of `0`, which has to be handled separately.
  rcases eq_or_ne y 0 with rfl | hy
  Â· exact zero_mem_tangentCone (mem_closure_iff_clusterPt.mpr hx.clusterPt)
  /- Assume now `y` is a fixed nonzero scalar. Take a sequence `d n` tending to `0` such
  that `x + d n âˆˆ s`. Let `c n = y / d n`. Then `â€–c nâ€–` tends to infinity, and `c n â€¢ d n`
  converges to `y` (as it is equal to `y`). By definition, this shows that `y` belongs to the
  tangent cone. -/
  obtain âŸ¨u, -, u_pos, u_limâŸ© :
      âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n) âˆ§ Tendsto u atTop (ğ“ (0 : â„)) :=
    exists_seq_strictAnti_tendsto (0 : â„)
  have A n : âˆƒ y âˆˆ closedBall x (u n) âˆ© s, y â‰  x :=
    accPt_iff_nhds.mp hx _ (closedBall_mem_nhds _ (u_pos n))
  choose v hv hvx using A
  choose hvu hvs using hv
  let d := fun n â†¦ v n - x
  have d_ne n : d n â‰  0 := by simpa [d, sub_eq_zero] using hvx n
  refine âŸ¨fun n â†¦ y * (d n)â»Â¹, d, .of_forall ?_, ?_, ?_âŸ©
  Â· simpa [d] using hvs
  Â· simp only [norm_mul, norm_inv]
    apply (tendsto_const_mul_atTop_of_pos (by simpa using hy)).2
    apply tendsto_inv_nhdsGT_zero.comp
    simp only [nhdsWithin, tendsto_inf, tendsto_principal, mem_Ioi, norm_pos_iff, ne_eq,
      eventually_atTop, ge_iff_le]
    have B (n : â„•) : â€–d nâ€– â‰¤ u n := by simpa [dist_eq_norm] using hvu n
    refine âŸ¨?_, 0, fun n hn â†¦ by simpa using d_ne nâŸ©
    exact squeeze_zero (fun n â†¦ by positivity) B u_lim
  Â· convert tendsto_const_nhds (Î± := â„•) (x := y) with n
    simp [mul_assoc, inv_mul_cancelâ‚€ (d_ne n)]

@[deprecated (since := "2025-04-27")] alias tangentCone_eq_univ := tangentConeAt_eq_univ

end Normed

end TangentCone

section UniqueDiff

/-!
### Properties of `UniqueDiffWithinAt` and `UniqueDiffOn`

This section is devoted to properties of the predicates `UniqueDiffWithinAt` and `UniqueDiffOn`. -/

section TVS
variable [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {x y : E} {s t : Set E}

theorem UniqueDiffOn.uniqueDiffWithinAt {s : Set E} {x} (hs : UniqueDiffOn ğ•œ s) (h : x âˆˆ s) :
    UniqueDiffWithinAt ğ•œ s x :=
  hs x h

theorem uniqueDiffWithinAt_univ : UniqueDiffWithinAt ğ•œ univ x := by
  rw [uniqueDiffWithinAt_iff, tangentConeAt_univ]
  simp

theorem uniqueDiffOn_univ : UniqueDiffOn ğ•œ (univ : Set E) :=
  fun _ _ => uniqueDiffWithinAt_univ

theorem uniqueDiffOn_empty : UniqueDiffOn ğ•œ (âˆ… : Set E) :=
  fun _ hx => hx.elim

theorem UniqueDiffWithinAt.congr_pt (h : UniqueDiffWithinAt ğ•œ s x) (hy : x = y) :
    UniqueDiffWithinAt ğ•œ s y := hy â–¸ h

end TVS

section Normed
variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
variable [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {x y : E} {s t : Set E}

@[simp]
theorem uniqueDiffWithinAt_closure :
    UniqueDiffWithinAt ğ•œ (closure s) x â†” UniqueDiffWithinAt ğ•œ s x := by
  simp [uniqueDiffWithinAt_iff]

protected alias âŸ¨UniqueDiffWithinAt.of_closure, UniqueDiffWithinAt.closureâŸ© :=
  uniqueDiffWithinAt_closure

theorem UniqueDiffWithinAt.mono_nhds (h : UniqueDiffWithinAt ğ•œ s x) (st : ğ“[s] x â‰¤ ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ t x := by
  simp only [uniqueDiffWithinAt_iff] at *
  rw [mem_closure_iff_nhdsWithin_neBot] at h âŠ¢
  exact âŸ¨h.1.mono <| Submodule.span_mono <| tangentConeAt_mono_nhds st, h.2.mono stâŸ©

theorem UniqueDiffWithinAt.mono (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† t) :
    UniqueDiffWithinAt ğ•œ t x :=
  h.mono_nhds <| nhdsWithin_mono _ st

theorem UniqueDiffWithinAt.mono_closure (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† closure t) :
    UniqueDiffWithinAt ğ•œ t x :=
  (h.mono st).of_closure

theorem uniqueDiffWithinAt_congr (st : ğ“[s] x = ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ s x â†” UniqueDiffWithinAt ğ•œ t x :=
  âŸ¨fun h => h.mono_nhds <| le_of_eq st, fun h => h.mono_nhds <| le_of_eq st.symmâŸ©

theorem uniqueDiffWithinAt_inter (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict' _ ht).symm

theorem UniqueDiffWithinAt.inter (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter ht).2 hs

theorem uniqueDiffWithinAt_inter' (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict'' _ ht).symm

theorem UniqueDiffWithinAt.inter' (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter' ht).2 hs

theorem uniqueDiffWithinAt_of_mem_nhds (h : s âˆˆ ğ“ x) : UniqueDiffWithinAt ğ•œ s x := by
  simpa only [univ_inter] using uniqueDiffWithinAt_univ.inter h

theorem IsOpen.uniqueDiffWithinAt (hs : IsOpen s) (xs : x âˆˆ s) : UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_of_mem_nhds (IsOpen.mem_nhds hs xs)

theorem UniqueDiffOn.inter (hs : UniqueDiffOn ğ•œ s) (ht : IsOpen t) : UniqueDiffOn ğ•œ (s âˆ© t) :=
  fun x hx => (hs x hx.1).inter (IsOpen.mem_nhds ht hx.2)

theorem IsOpen.uniqueDiffOn (hs : IsOpen s) : UniqueDiffOn ğ•œ s :=
  fun _ hx => IsOpen.uniqueDiffWithinAt hs hx

/-- The product of two sets of unique differentiability at points `x` and `y` has unique
differentiability at `(x, y)`. -/
theorem UniqueDiffWithinAt.prod {t : Set F} {y : F} (hs : UniqueDiffWithinAt ğ•œ s x)
    (ht : UniqueDiffWithinAt ğ•œ t y) : UniqueDiffWithinAt ğ•œ (s Ã—Ë¢ t) (x, y) := by
  rw [uniqueDiffWithinAt_iff] at hs ht âŠ¢
  rw [closure_prod_eq]
  refine âŸ¨?_, hs.2, ht.2âŸ©
  have : _ â‰¤ Submodule.span ğ•œ (tangentConeAt ğ•œ (s Ã—Ë¢ t) (x, y)) := Submodule.span_mono
    (union_subset (subset_tangentConeAt_prod_left ht.2) (subset_tangentConeAt_prod_right hs.2))
  rw [LinearMap.span_inl_union_inr, SetLike.le_def] at this
  exact (hs.1.prod ht.1).mono this

theorem UniqueDiffWithinAt.univ_pi (Î¹ : Type*) [Finite Î¹] (E : Î¹ â†’ Type*)
    [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] (s : âˆ€ i, Set (E i)) (x : âˆ€ i, E i)
    (h : âˆ€ i, UniqueDiffWithinAt ğ•œ (s i) (x i)) : UniqueDiffWithinAt ğ•œ (Set.pi univ s) x := by
  classical
  simp only [uniqueDiffWithinAt_iff, closure_pi_set] at h âŠ¢
  refine âŸ¨(dense_pi univ fun i _ => (h i).1).mono ?_, fun i _ => (h i).2âŸ©
  norm_cast
  simp only [â† Submodule.iSup_map_single, iSup_le_iff, LinearMap.map_span, Submodule.span_le,
    â† mapsTo']
  exact fun i => (mapsTo_tangentConeAt_pi fun j _ => (h j).2).mono Subset.rfl Submodule.subset_span

theorem UniqueDiffWithinAt.pi (Î¹ : Type*) [Finite Î¹] (E : Î¹ â†’ Type*)
    [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] (s : âˆ€ i, Set (E i)) (x : âˆ€ i, E i)
    (I : Set Î¹) (h : âˆ€ i âˆˆ I, UniqueDiffWithinAt ğ•œ (s i) (x i)) :
    UniqueDiffWithinAt ğ•œ (Set.pi I s) x := by
  classical
  rw [â† Set.univ_pi_piecewise_univ]
  refine UniqueDiffWithinAt.univ_pi Î¹ E _ _ fun i => ?_
  by_cases hi : i âˆˆ I <;> simp [*, uniqueDiffWithinAt_univ]

/-- The product of two sets of unique differentiability is a set of unique differentiability. -/
theorem UniqueDiffOn.prod {t : Set F} (hs : UniqueDiffOn ğ•œ s) (ht : UniqueDiffOn ğ•œ t) :
    UniqueDiffOn ğ•œ (s Ã—Ë¢ t) :=
  fun âŸ¨x, yâŸ© h => UniqueDiffWithinAt.prod (hs x h.1) (ht y h.2)

/-- The finite product of a family of sets of unique differentiability is a set of unique
differentiability. -/
theorem UniqueDiffOn.pi (Î¹ : Type*) [Finite Î¹] (E : Î¹ â†’ Type*) [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace ğ•œ (E i)] (s : âˆ€ i, Set (E i)) (I : Set Î¹)
    (h : âˆ€ i âˆˆ I, UniqueDiffOn ğ•œ (s i)) : UniqueDiffOn ğ•œ (Set.pi I s) :=
  fun x hx => UniqueDiffWithinAt.pi _ _ _ _ _ fun i hi => h i hi (x i) (hx i hi)

/-- The finite product of a family of sets of unique differentiability is a set of unique
differentiability. -/
theorem UniqueDiffOn.univ_pi (Î¹ : Type*) [Finite Î¹] (E : Î¹ â†’ Type*)
    [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] (s : âˆ€ i, Set (E i))
    (h : âˆ€ i, UniqueDiffOn ğ•œ (s i)) : UniqueDiffOn ğ•œ (Set.pi univ s) :=
  UniqueDiffOn.pi _ _ _ _ fun i _ => h i

/--
Given `x âˆˆ s` and a field extension `ğ•œ âŠ† ğ•œ'`, the tangent cone of `s` at `x` with
respect to `ğ•œ` is contained in the tangent cone of `s` at `x` with respect to `ğ•œ'`.
-/
theorem tangentConeAt_mono_field : tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ' s x := by
  intro Î± hÎ±
  simp only [tangentConeAt, eventually_atTop, ge_iff_le, tendsto_norm_atTop_iff_cobounded,
    mem_setOf_eq] at hÎ± âŠ¢
  obtain âŸ¨c, d, âŸ¨a, hâ‚aâŸ©, hâ‚, hâ‚‚âŸ© := hÎ±
  use ((algebraMap ğ•œ ğ•œ') âˆ˜ c), d
  constructor
  Â· use a
  Â· constructor
    Â· intro Î² hÎ²
      rw [mem_map, mem_atTop_sets]
      obtain âŸ¨n, hnâŸ© := mem_atTop_sets.1
        (mem_map.1 (hâ‚ (algebraMap_cobounded_le_cobounded (ğ•œ := ğ•œ) (ğ•œ' := ğ•œ') hÎ²)))
      use n, fun _ _ â†¦ by simp_all
    Â· simpa

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and that `x âˆˆ s` is a point
of unique differentiability with respect to the set `s` and the smaller field `ğ•œ`, then `x` is also
a point of unique differentiability with respect to the set `s` and the larger field `ğ•œ'`.
-/
theorem UniqueDiffWithinAt.mono_field (hâ‚‚s : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ' s x := by
  simp_all only [uniqueDiffWithinAt_iff, and_true]
  apply Dense.mono _ hâ‚‚s.1
  trans â†‘(Submodule.span ğ•œ (tangentConeAt ğ•œ' s x))
  <;> simp [Submodule.span_mono tangentConeAt_mono_field]

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and all points of `s` are
points of unique differentiability with respect to the smaller field `ğ•œ`, then they are also points
of unique differentiability with respect to the larger field `ğ•œ`.
-/
theorem UniqueDiffOn.mono_field (hâ‚‚s : UniqueDiffOn ğ•œ s) :
    UniqueDiffOn ğ•œ' s := fun x hx â†¦ (hâ‚‚s x hx).mono_field

end Normed

section RealNormed
variable [NormedAddCommGroup G] [NormedSpace â„ G]

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability at every point of its closure. -/
theorem uniqueDiffWithinAt_convex {s : Set G} (conv : Convex â„ s) (hs : (interior s).Nonempty)
    {x : G} (hx : x âˆˆ closure s) : UniqueDiffWithinAt â„ s x := by
  rcases hs with âŸ¨y, hyâŸ©
  suffices y - x âˆˆ interior (tangentConeAt â„ s x) by
    refine âŸ¨Dense.of_closure ?_, hxâŸ©
    simp [(Submodule.span â„ (tangentConeAt â„ s x)).eq_top_of_nonempty_interior'
        âŸ¨y - x, interior_mono Submodule.subset_span thisâŸ©]
  rw [mem_interior_iff_mem_nhds]
  replace hy : interior s âˆˆ ğ“ y := IsOpen.mem_nhds isOpen_interior hy
  apply mem_of_superset ((isOpenMap_sub_right x).image_mem_nhds hy)
  rintro _ âŸ¨z, zs, rflâŸ©
  refine mem_tangentConeAt_of_openSegment_subset (Subset.trans ?_ interior_subset)
  exact conv.openSegment_closure_interior_subset_interior hx zs

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability. -/
theorem uniqueDiffOn_convex {s : Set G} (conv : Convex â„ s) (hs : (interior s).Nonempty) :
    UniqueDiffOn â„ s :=
  fun _ xs => uniqueDiffWithinAt_convex conv hs (subset_closure xs)

end RealNormed

section Real

theorem uniqueDiffOn_Ici (a : â„) : UniqueDiffOn â„ (Ici a) :=
  uniqueDiffOn_convex (convex_Ici a) <| by simp only [interior_Ici, nonempty_Ioi]

theorem uniqueDiffOn_Iic (a : â„) : UniqueDiffOn â„ (Iic a) :=
  uniqueDiffOn_convex (convex_Iic a) <| by simp only [interior_Iic, nonempty_Iio]

theorem uniqueDiffOn_Ioi (a : â„) : UniqueDiffOn â„ (Ioi a) :=
  isOpen_Ioi.uniqueDiffOn

theorem uniqueDiffOn_Iio (a : â„) : UniqueDiffOn â„ (Iio a) :=
  isOpen_Iio.uniqueDiffOn

theorem uniqueDiffOn_Icc {a b : â„} (hab : a < b) : UniqueDiffOn â„ (Icc a b) :=
  uniqueDiffOn_convex (convex_Icc a b) <| by simp only [interior_Icc, nonempty_Ioo, hab]

theorem uniqueDiffOn_Ico (a b : â„) : UniqueDiffOn â„ (Ico a b) :=
  if hab : a < b then
    uniqueDiffOn_convex (convex_Ico a b) <| by simp only [interior_Ico, nonempty_Ioo, hab]
  else by simp only [Ico_eq_empty hab, uniqueDiffOn_empty]

theorem uniqueDiffOn_Ioc (a b : â„) : UniqueDiffOn â„ (Ioc a b) :=
  if hab : a < b then
    uniqueDiffOn_convex (convex_Ioc a b) <| by simp only [interior_Ioc, nonempty_Ioo, hab]
  else by simp only [Ioc_eq_empty hab, uniqueDiffOn_empty]

theorem uniqueDiffOn_Ioo (a b : â„) : UniqueDiffOn â„ (Ioo a b) :=
  isOpen_Ioo.uniqueDiffOn

/-- The real interval `[0, 1]` is a set of unique differentiability. -/
theorem uniqueDiffOn_Icc_zero_one : UniqueDiffOn â„ (Icc (0 : â„) 1) :=
  uniqueDiffOn_Icc zero_lt_one

theorem uniqueDiffWithinAt_Ioo {a b t : â„} (ht : t âˆˆ Set.Ioo a b) :
    UniqueDiffWithinAt â„ (Set.Ioo a b) t :=
  IsOpen.uniqueDiffWithinAt isOpen_Ioo ht

theorem uniqueDiffWithinAt_Ioi (a : â„) : UniqueDiffWithinAt â„ (Ioi a) a :=
  uniqueDiffWithinAt_convex (convex_Ioi a) (by simp) (by simp)

theorem uniqueDiffWithinAt_Iio (a : â„) : UniqueDiffWithinAt â„ (Iio a) a :=
  uniqueDiffWithinAt_convex (convex_Iio a) (by simp) (by simp)

theorem uniqueDiffWithinAt_Ici (x : â„) : UniqueDiffWithinAt â„ (Ici x) x :=
  (uniqueDiffWithinAt_Ioi x).mono Set.Ioi_subset_Ici_self

theorem uniqueDiffWithinAt_Iic (x : â„) : UniqueDiffWithinAt â„ (Iic x) x :=
  (uniqueDiffWithinAt_Iio x).mono Set.Iio_subset_Iic_self

/-- In one dimension, a point is a point of unique differentiability of a set
iff it is an accumulation point of the set. -/
theorem uniqueDiffWithinAt_iff_accPt {s : Set ğ•œ} {x : ğ•œ} :
    UniqueDiffWithinAt ğ•œ s x â†” AccPt x (ğ“Ÿ s) :=
  âŸ¨UniqueDiffWithinAt.accPt, fun h â†¦
    âŸ¨by simp [tangentConeAt_eq_univ h], mem_closure_iff_clusterPt.mpr h.clusterPtâŸ©âŸ©

alias âŸ¨_, AccPt.uniqueDiffWithinAtâŸ© := uniqueDiffWithinAt_iff_accPt

/-- In one dimension, every point is either a point of unique differentiability, or isolated. -/
@[deprecated uniqueDiffWithinAt_iff_accPt (since := "2025-04-20")]
theorem uniqueDiffWithinAt_or_nhdsWithin_eq_bot (s : Set ğ•œ) (x : ğ•œ) :
    UniqueDiffWithinAt ğ•œ s x âˆ¨ ğ“[s \ {x}] x = âŠ¥ :=
  (em (AccPt x (ğ“Ÿ s))).imp AccPt.uniqueDiffWithinAt fun h â†¦ by
    rwa [accPt_principal_iff_nhdsWithin, not_neBot] at h

end Real

end UniqueDiff
