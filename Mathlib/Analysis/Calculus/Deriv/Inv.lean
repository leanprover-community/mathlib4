/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp

#align_import analysis.calculus.deriv.inv from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Derivatives of `x â†¦ xâ»Â¹` and `f x / g x`

In this file we prove `(xâ»Â¹)' = -1 / x ^ 2`, `((f x)â»Â¹)' = -f' x / (f x) ^ 2`, and
`(f x / g x)' = (f' x * g x - f x * g' x) / (g x) ^ 2` for different notions of derivative.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative
-/


universe u v w

open scoped Classical
open Topology Filter ENNReal

open Filter Asymptotics Set

open ContinuousLinearMap (smulRight smulRight_one_eq_iff)

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {E : Type w} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {f fâ‚€ fâ‚ g : ğ•œ â†’ F}
variable {f' fâ‚€' fâ‚' g' : F}
variable {x : ğ•œ}
variable {s t : Set ğ•œ}
variable {L : Filter ğ•œ}

section Inverse

/-! ### Derivative of `x â†¦ xâ»Â¹` -/

theorem hasStrictDerivAt_inv (hx : x â‰  0) : HasStrictDerivAt Inv.inv (-(x ^ 2)â»Â¹) x := by
  suffices
    (fun p : ğ•œ Ã— ğ•œ => (p.1 - p.2) * ((x * x)â»Â¹ - (p.1 * p.2)â»Â¹)) =o[ğ“ (x, x)] fun p =>
      (p.1 - p.2) * 1 by
    refine this.congr' ?_ (eventually_of_forall fun _ => mul_one _)
    refine Eventually.mono ((isOpen_ne.prod isOpen_ne).mem_nhds âŸ¨hx, hxâŸ©) ?_
    rintro âŸ¨y, zâŸ© âŸ¨hy, hzâŸ©
    simp only [mem_setOf_eq] at hy hz
    -- hy : y â‰  0, hz : z â‰  0
    field_simp [hx, hy, hz]
    ring
  refine (isBigO_refl (fun p : ğ•œ Ã— ğ•œ => p.1 - p.2) _).mul_isLittleO ((isLittleO_one_iff ğ•œ).2 ?_)
  rw [â† sub_self (x * x)â»Â¹]
  exact tendsto_const_nhds.sub ((continuous_mul.tendsto (x, x)).invâ‚€ <| mul_ne_zero hx hx)
#align has_strict_deriv_at_inv hasStrictDerivAt_inv

theorem hasDerivAt_inv (x_ne_zero : x â‰  0) : HasDerivAt (fun y => yâ»Â¹) (-(x ^ 2)â»Â¹) x :=
  (hasStrictDerivAt_inv x_ne_zero).hasDerivAt
#align has_deriv_at_inv hasDerivAt_inv

theorem hasDerivWithinAt_inv (x_ne_zero : x â‰  0) (s : Set ğ•œ) :
    HasDerivWithinAt (fun x => xâ»Â¹) (-(x ^ 2)â»Â¹) s x :=
  (hasDerivAt_inv x_ne_zero).hasDerivWithinAt
#align has_deriv_within_at_inv hasDerivWithinAt_inv

theorem differentiableAt_inv : DifferentiableAt ğ•œ (fun x => xâ»Â¹) x â†” x â‰  0 :=
  âŸ¨fun H => NormedField.continuousAt_inv.1 H.continuousAt, fun H =>
    (hasDerivAt_inv H).differentiableAtâŸ©
#align differentiable_at_inv differentiableAt_inv

theorem differentiableWithinAt_inv (x_ne_zero : x â‰  0) :
    DifferentiableWithinAt ğ•œ (fun x => xâ»Â¹) s x :=
  (differentiableAt_inv.2 x_ne_zero).differentiableWithinAt
#align differentiable_within_at_inv differentiableWithinAt_inv

theorem differentiableOn_inv : DifferentiableOn ğ•œ (fun x : ğ•œ => xâ»Â¹) { x | x â‰  0 } := fun _x hx =>
  differentiableWithinAt_inv hx
#align differentiable_on_inv differentiableOn_inv

theorem deriv_inv : deriv (fun x => xâ»Â¹) x = -(x ^ 2)â»Â¹ := by
  rcases eq_or_ne x 0 with (rfl | hne)
  Â· simp [deriv_zero_of_not_differentiableAt (mt differentiableAt_inv.1 (not_not.2 rfl))]
  Â· exact (hasDerivAt_inv hne).deriv
#align deriv_inv deriv_inv

@[simp]
theorem deriv_inv' : (deriv fun x : ğ•œ => xâ»Â¹) = fun x => -(x ^ 2)â»Â¹ :=
  funext fun _ => deriv_inv
#align deriv_inv' deriv_inv'

theorem derivWithin_inv (x_ne_zero : x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin (fun x => xâ»Â¹) s x = -(x ^ 2)â»Â¹ := by
  rw [DifferentiableAt.derivWithin (differentiableAt_inv.2 x_ne_zero) hxs]
  exact deriv_inv
#align deriv_within_inv derivWithin_inv

theorem hasFDerivAt_inv (x_ne_zero : x â‰  0) :
    HasFDerivAt (fun x => xâ»Â¹) (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
  hasDerivAt_inv x_ne_zero
#align has_fderiv_at_inv hasFDerivAt_inv

theorem hasFDerivWithinAt_inv (x_ne_zero : x â‰  0) :
    HasFDerivWithinAt (fun x => xâ»Â¹) (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) s x :=
  (hasFDerivAt_inv x_ne_zero).hasFDerivWithinAt
#align has_fderiv_within_at_inv hasFDerivWithinAt_inv

theorem fderiv_inv : fderiv ğ•œ (fun x => xâ»Â¹) x = smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) := by
  rw [â† deriv_fderiv, deriv_inv]
#align fderiv_inv fderiv_inv

theorem fderivWithin_inv (x_ne_zero : x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x => xâ»Â¹) s x = smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) := by
  rw [DifferentiableAt.fderivWithin (differentiableAt_inv.2 x_ne_zero) hxs]
  exact fderiv_inv
#align fderiv_within_inv fderivWithin_inv

variable {c : ğ•œ â†’ ğ•œ} {h : E â†’ ğ•œ} {c' : ğ•œ} {z : E} {S : Set E}

theorem HasDerivWithinAt.inv (hc : HasDerivWithinAt c c' s x) (hx : c x â‰  0) :
    HasDerivWithinAt (fun y => (c y)â»Â¹) (-c' / c x ^ 2) s x := by
  convert (hasDerivAt_inv hx).comp_hasDerivWithinAt x hc using 1
  field_simp
#align has_deriv_within_at.inv HasDerivWithinAt.inv

theorem HasDerivAt.inv (hc : HasDerivAt c c' x) (hx : c x â‰  0) :
    HasDerivAt (fun y => (c y)â»Â¹) (-c' / c x ^ 2) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.inv hx
#align has_deriv_at.inv HasDerivAt.inv

theorem DifferentiableWithinAt.inv (hf : DifferentiableWithinAt ğ•œ h S z) (hz : h z â‰  0) :
    DifferentiableWithinAt ğ•œ (fun x => (h x)â»Â¹) S z :=
  (differentiableAt_inv.mpr hz).comp_differentiableWithinAt z hf
#align differentiable_within_at.inv DifferentiableWithinAt.inv

@[simp]
theorem DifferentiableAt.inv (hf : DifferentiableAt ğ•œ h z) (hz : h z â‰  0) :
    DifferentiableAt ğ•œ (fun x => (h x)â»Â¹) z :=
  (differentiableAt_inv.mpr hz).comp z hf
#align differentiable_at.inv DifferentiableAt.inv

theorem DifferentiableOn.inv (hf : DifferentiableOn ğ•œ h S) (hz : âˆ€ x âˆˆ S, h x â‰  0) :
    DifferentiableOn ğ•œ (fun x => (h x)â»Â¹) S := fun x h => (hf x h).inv (hz x h)
#align differentiable_on.inv DifferentiableOn.inv

@[simp]
theorem Differentiable.inv (hf : Differentiable ğ•œ h) (hz : âˆ€ x, h x â‰  0) :
    Differentiable ğ•œ fun x => (h x)â»Â¹ := fun x => (hf x).inv (hz x)
#align differentiable.inv Differentiable.inv

theorem derivWithin_inv' (hc : DifferentiableWithinAt ğ•œ c s x) (hx : c x â‰  0)
    (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin (fun x => (c x)â»Â¹) s x = -derivWithin c s x / c x ^ 2 :=
  (hc.hasDerivWithinAt.inv hx).derivWithin hxs
#align deriv_within_inv' derivWithin_inv'

@[simp]
theorem deriv_inv'' (hc : DifferentiableAt ğ•œ c x) (hx : c x â‰  0) :
    deriv (fun x => (c x)â»Â¹) x = -deriv c x / c x ^ 2 :=
  (hc.hasDerivAt.inv hx).deriv
#align deriv_inv'' deriv_inv''

end Inverse

section Division

/-! ### Derivative of `x â†¦ c x / d x` -/

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {c d : ğ•œ â†’ ğ•œ'} {c' d' : ğ•œ'}

theorem HasDerivWithinAt.div (hc : HasDerivWithinAt c c' s x) (hd : HasDerivWithinAt d d' s x)
    (hx : d x â‰  0) :
    HasDerivWithinAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) s x := by
  convert hc.mul ((hasDerivAt_inv hx).comp_hasDerivWithinAt x hd) using 1
  Â· simp only [div_eq_mul_inv, (Â· âˆ˜ Â·)]
  Â· field_simp
    ring
#align has_deriv_within_at.div HasDerivWithinAt.div

theorem HasStrictDerivAt.div (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x)
    (hx : d x â‰  0) : HasStrictDerivAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) x := by
  convert hc.mul ((hasStrictDerivAt_inv hx).comp x hd) using 1
  Â· simp only [div_eq_mul_inv, (Â· âˆ˜ Â·)]
  Â· field_simp
    ring
#align has_strict_deriv_at.div HasStrictDerivAt.div

theorem HasDerivAt.div (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) (hx : d x â‰  0) :
    HasDerivAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.div hd hx
#align has_deriv_at.div HasDerivAt.div

theorem DifferentiableWithinAt.div (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) (hx : d x â‰  0) :
    DifferentiableWithinAt ğ•œ (fun x => c x / d x) s x :=
  (hc.hasDerivWithinAt.div hd.hasDerivWithinAt hx).differentiableWithinAt
#align differentiable_within_at.div DifferentiableWithinAt.div

@[simp]
theorem DifferentiableAt.div (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x)
    (hx : d x â‰  0) : DifferentiableAt ğ•œ (fun x => c x / d x) x :=
  (hc.hasDerivAt.div hd.hasDerivAt hx).differentiableAt
#align differentiable_at.div DifferentiableAt.div

theorem DifferentiableOn.div (hc : DifferentiableOn ğ•œ c s) (hd : DifferentiableOn ğ•œ d s)
    (hx : âˆ€ x âˆˆ s, d x â‰  0) : DifferentiableOn ğ•œ (fun x => c x / d x) s := fun x h =>
  (hc x h).div (hd x h) (hx x h)
#align differentiable_on.div DifferentiableOn.div

@[simp]
theorem Differentiable.div (hc : Differentiable ğ•œ c) (hd : Differentiable ğ•œ d) (hx : âˆ€ x, d x â‰  0) :
    Differentiable ğ•œ fun x => c x / d x := fun x => (hc x).div (hd x) (hx x)
#align differentiable.div Differentiable.div

theorem derivWithin_div (hc : DifferentiableWithinAt ğ•œ c s x) (hd : DifferentiableWithinAt ğ•œ d s x)
    (hx : d x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin (fun x => c x / d x) s x =
      (derivWithin c s x * d x - c x * derivWithin d s x) / d x ^ 2 :=
  (hc.hasDerivWithinAt.div hd.hasDerivWithinAt hx).derivWithin hxs
#align deriv_within_div derivWithin_div

@[simp]
theorem deriv_div (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) (hx : d x â‰  0) :
    deriv (fun x => c x / d x) x = (deriv c x * d x - c x * deriv d x) / d x ^ 2 :=
  (hc.hasDerivAt.div hd.hasDerivAt hx).deriv
#align deriv_div deriv_div

end Division
