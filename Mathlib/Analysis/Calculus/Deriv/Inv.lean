/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp

/-!
# Derivatives of `x â†¦ xâ»Â¹` and `f x / g x`

In this file we prove `(xâ»Â¹)' = -1 / x ^ 2`, `((f x)â»Â¹)' = -f' x / (f x) ^ 2`, and
`(f x / g x)' = (f' x * g x - f x * g' x) / (g x) ^ 2` for different notions of derivative.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative
-/


universe u

open scoped Topology
open Filter Asymptotics Set

open ContinuousLinearMap (smulRight)

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {x : ğ•œ} {s : Set ğ•œ}

section Inverse

/-! ### Derivative of `x â†¦ xâ»Â¹` -/

theorem hasStrictDerivAt_inv (hx : x â‰  0) : HasStrictDerivAt Inv.inv (-(x ^ 2)â»Â¹) x := by
  suffices
    (fun p : ğ•œ Ã— ğ•œ => (p.1 - p.2) * ((x * x)â»Â¹ - (p.1 * p.2)â»Â¹)) =o[ğ“ (x, x)] fun p =>
      (p.1 - p.2) * 1 by
    refine .of_isLittleO <| this.congr' ?_ (Eventually.of_forall fun _ => mul_one _)
    refine Eventually.mono ((isOpen_ne.prod isOpen_ne).mem_nhds âŸ¨hx, hxâŸ©) ?_
    rintro âŸ¨y, zâŸ© âŸ¨hy, hzâŸ©
    simp only [mem_setOf_eq] at hy hz
    -- hy : y â‰  0, hz : z â‰  0
    field_simp [hx, hy, hz]
    ring
  refine (isBigO_refl (fun p : ğ•œ Ã— ğ•œ => p.1 - p.2) _).mul_isLittleO ((isLittleO_one_iff ğ•œ).2 ?_)
  rw [â† sub_self (x * x)â»Â¹]
  exact tendsto_const_nhds.sub ((continuous_mul.tendsto (x, x)).invâ‚€ <| mul_ne_zero hx hx)

theorem hasDerivAt_inv (x_ne_zero : x â‰  0) : HasDerivAt (fun y => yâ»Â¹) (-(x ^ 2)â»Â¹) x :=
  (hasStrictDerivAt_inv x_ne_zero).hasDerivAt

theorem hasDerivWithinAt_inv (x_ne_zero : x â‰  0) (s : Set ğ•œ) :
    HasDerivWithinAt (fun x => xâ»Â¹) (-(x ^ 2)â»Â¹) s x :=
  (hasDerivAt_inv x_ne_zero).hasDerivWithinAt

theorem differentiableAt_inv_iff : DifferentiableAt ğ•œ (fun x => xâ»Â¹) x â†” x â‰  0 :=
  âŸ¨fun H => NormedField.continuousAt_inv.1 H.continuousAt, fun H =>
    (hasDerivAt_inv H).differentiableAtâŸ©

theorem deriv_inv : deriv (fun x => xâ»Â¹) x = -(x ^ 2)â»Â¹ := by
  rcases eq_or_ne x 0 with (rfl | hne)
  Â· simp [deriv_zero_of_not_differentiableAt (mt differentiableAt_inv_iff.1 (not_not.2 rfl))]
  Â· exact (hasDerivAt_inv hne).deriv

@[simp]
theorem deriv_inv' : (deriv fun x : ğ•œ => xâ»Â¹) = fun x => -(x ^ 2)â»Â¹ :=
  funext fun _ => deriv_inv

theorem derivWithin_inv (x_ne_zero : x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin (fun x => xâ»Â¹) s x = -(x ^ 2)â»Â¹ := by
  rw [DifferentiableAt.derivWithin (differentiableAt_inv x_ne_zero) hxs]
  exact deriv_inv

theorem hasFDerivAt_inv (x_ne_zero : x â‰  0) :
    HasFDerivAt (fun x => xâ»Â¹) (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
  hasDerivAt_inv x_ne_zero

theorem hasStrictFDerivAt_inv (x_ne_zero : x â‰  0) :
    HasStrictFDerivAt (fun x => xâ»Â¹) (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
  hasStrictDerivAt_inv x_ne_zero

theorem hasFDerivWithinAt_inv (x_ne_zero : x â‰  0) :
    HasFDerivWithinAt (fun x => xâ»Â¹) (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) : ğ•œ â†’L[ğ•œ] ğ•œ) s x :=
  (hasFDerivAt_inv x_ne_zero).hasFDerivWithinAt

theorem fderiv_inv : fderiv ğ•œ (fun x => xâ»Â¹) x = smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) := by
  rw [â† deriv_fderiv, deriv_inv]

theorem fderivWithin_inv (x_ne_zero : x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x => xâ»Â¹) s x = smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (-(x ^ 2)â»Â¹) := by
  rw [DifferentiableAt.fderivWithin (differentiableAt_inv x_ne_zero) hxs]
  exact fderiv_inv

variable {c : ğ•œ â†’ ğ•œ} {c' : ğ•œ}

theorem HasDerivWithinAt.inv (hc : HasDerivWithinAt c c' s x) (hx : c x â‰  0) :
    HasDerivWithinAt (fun y => (c y)â»Â¹) (-c' / c x ^ 2) s x := by
  convert (hasDerivAt_inv hx).comp_hasDerivWithinAt x hc using 1
  field_simp

theorem HasDerivAt.inv (hc : HasDerivAt c c' x) (hx : c x â‰  0) :
    HasDerivAt (fun y => (c y)â»Â¹) (-c' / c x ^ 2) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.inv hx

theorem derivWithin_inv' (hc : DifferentiableWithinAt ğ•œ c s x) (hx : c x â‰  0) :
    derivWithin (fun x => (c x)â»Â¹) s x = -derivWithin c s x / c x ^ 2 := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.inv hx).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp]
theorem deriv_inv'' (hc : DifferentiableAt ğ•œ c x) (hx : c x â‰  0) :
    deriv (fun x => (c x)â»Â¹) x = -deriv c x / c x ^ 2 :=
  (hc.hasDerivAt.inv hx).deriv

end Inverse

section Division

/-! ### Derivative of `x â†¦ c x / d x` -/

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {c d : ğ•œ â†’ ğ•œ'} {c' d' : ğ•œ'}

theorem HasDerivWithinAt.div (hc : HasDerivWithinAt c c' s x) (hd : HasDerivWithinAt d d' s x)
    (hx : d x â‰  0) :
    HasDerivWithinAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) s x := by
  convert hc.mul ((hasDerivAt_inv hx).comp_hasDerivWithinAt x hd) using 1
  Â· simp only [div_eq_mul_inv, (Â· âˆ˜ Â·)]
  Â· field_simp
    ring

theorem HasStrictDerivAt.div (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x)
    (hx : d x â‰  0) : HasStrictDerivAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) x := by
  convert hc.mul ((hasStrictDerivAt_inv hx).comp x hd) using 1
  Â· simp only [div_eq_mul_inv, (Â· âˆ˜ Â·)]
  Â· field_simp
    ring

theorem HasDerivAt.div (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) (hx : d x â‰  0) :
    HasDerivAt (fun y => c y / d y) ((c' * d x - c x * d') / d x ^ 2) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.div hd hx

theorem DifferentiableWithinAt.div (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) (hx : d x â‰  0) :
    DifferentiableWithinAt ğ•œ (fun x => c x / d x) s x :=
  (hc.hasDerivWithinAt.div hd.hasDerivWithinAt hx).differentiableWithinAt

@[simp]
theorem DifferentiableAt.div (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x)
    (hx : d x â‰  0) : DifferentiableAt ğ•œ (fun x => c x / d x) x :=
  (hc.hasDerivAt.div hd.hasDerivAt hx).differentiableAt

theorem DifferentiableOn.div (hc : DifferentiableOn ğ•œ c s) (hd : DifferentiableOn ğ•œ d s)
    (hx : âˆ€ x âˆˆ s, d x â‰  0) : DifferentiableOn ğ•œ (fun x => c x / d x) s := fun x h =>
  (hc x h).div (hd x h) (hx x h)

@[simp]
theorem Differentiable.div (hc : Differentiable ğ•œ c) (hd : Differentiable ğ•œ d) (hx : âˆ€ x, d x â‰  0) :
    Differentiable ğ•œ fun x => c x / d x := fun x => (hc x).div (hd x) (hx x)

theorem derivWithin_div (hc : DifferentiableWithinAt ğ•œ c s x) (hd : DifferentiableWithinAt ğ•œ d s x)
    (hx : d x â‰  0) :
    derivWithin (fun x => c x / d x) s x =
      (derivWithin c s x * d x - c x * derivWithin d s x) / d x ^ 2 := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.div hd.hasDerivWithinAt hx).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp]
theorem deriv_div (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) (hx : d x â‰  0) :
    deriv (fun x => c x / d x) x = (deriv c x * d x - c x * deriv d x) / d x ^ 2 :=
  (hc.hasDerivAt.div hd.hasDerivAt hx).deriv

end Division
