/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, SÃ©bastien GouÃ«zel, Yury Kudryashov, Yuyang Zhao
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars

/-!
# One-dimensional derivatives of compositions of functions

In this file we prove the chain rule for the following cases:

* `HasDerivAt.comp` etc: `f : ğ•œ' â†’ ğ•œ'` composed with `g : ğ•œ â†’ ğ•œ'`;
* `HasDerivAt.scomp` etc: `f : ğ•œ' â†’ E` composed with `g : ğ•œ â†’ ğ•œ'`;
* `HasFDerivAt.comp_hasDerivAt` etc: `f : E â†’ F` composed with `g : ğ•œ â†’ E`;

Here `ğ•œ` is the base normed field, `E` and `F` are normed spaces over `ğ•œ` and `ğ•œ'` is an algebra
over `ğ•œ` (e.g., `ğ•œ'=ğ•œ` or `ğ•œ=â„`, `ğ•œ'=â„‚`).

We also give versions with the `of_eq` suffix, which require an equality proof instead
of definitional equality of the different points used in the composition. These versions are
often more flexible to use.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`analysis/calculus/deriv/basic`.

## Keywords

derivative, chain rule
-/


universe u v w

open scoped Topology Filter ENNReal

open Filter Asymptotics Set

open ContinuousLinearMap (smulRight smulRight_one_eq_iff)

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {E : Type w} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {f : ğ•œ â†’ F}
variable {f' : F}
variable {x : ğ•œ}
variable {s : Set ğ•œ}
variable {L : Filter ğ•œ}

section Composition

/-!
### Derivative of the composition of a vector function and a scalar function

We use `scomp` in lemmas on composition of vector valued and scalar valued functions, and `comp`
in lemmas on composition of scalar valued functions, in analogy for `smul` and `mul` (and also
because the `comp` version with the shorter name will show up much more often in applications).
The formula for the derivative involves `smul` in `scomp` lemmas, which can be reduced to
usual multiplication in `comp` lemmas.
-/


/- For composition lemmas, we put x explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition -/
variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' F]
  [IsScalarTower ğ•œ ğ•œ' F] {s' t' : Set ğ•œ'} {h : ğ•œ â†’ ğ•œ'} {hâ‚‚ : ğ•œ' â†’ ğ•œ'} {h' hâ‚‚' : ğ•œ'}
  {gâ‚ : ğ•œ' â†’ F} {gâ‚' : F} {L' : Filter ğ•œ'} {y : ğ•œ'} (x)

theorem HasDerivAtFilter.scomp (hg : HasDerivAtFilter gâ‚ gâ‚' (h x) L')
    (hh : HasDerivAtFilter h h' x L) (hL : Tendsto h L L') :
    HasDerivAtFilter (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x L := by
  simpa using ((hg.restrictScalars ğ•œ).comp x hh hL).hasDerivAtFilter

theorem HasDerivAtFilter.scomp_of_eq (hg : HasDerivAtFilter gâ‚ gâ‚' y L')
    (hh : HasDerivAtFilter h h' x L) (hy : y = h x) (hL : Tendsto h L L') :
    HasDerivAtFilter (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x L := by
  rw [hy] at hg; exact hg.scomp x hh hL

theorem HasDerivWithinAt.scomp_hasDerivAt (hg : HasDerivWithinAt gâ‚ gâ‚' s' (h x))
    (hh : HasDerivAt h h' x) (hs : âˆ€ x, h x âˆˆ s') : HasDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x :=
  hg.scomp x hh <| tendsto_inf.2 âŸ¨hh.continuousAt, tendsto_principal.2 <| Eventually.of_forall hsâŸ©

theorem HasDerivWithinAt.scomp_hasDerivAt_of_eq (hg : HasDerivWithinAt gâ‚ gâ‚' s' y)
    (hh : HasDerivAt h h' x) (hs : âˆ€ x, h x âˆˆ s') (hy : y = h x) :
    HasDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x := by
  rw [hy] at hg; exact hg.scomp_hasDerivAt x hh hs

nonrec theorem HasDerivWithinAt.scomp (hg : HasDerivWithinAt gâ‚ gâ‚' t' (h x))
    (hh : HasDerivWithinAt h h' s x) (hst : MapsTo h s t') :
    HasDerivWithinAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x :=
  hg.scomp x hh <| hh.continuousWithinAt.tendsto_nhdsWithin hst

theorem HasDerivWithinAt.scomp_of_eq (hg : HasDerivWithinAt gâ‚ gâ‚' t' y)
    (hh : HasDerivWithinAt h h' s x) (hst : MapsTo h s t') (hy : y = h x) :
    HasDerivWithinAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x := by
  rw [hy] at hg; exact hg.scomp x hh hst

/-- The chain rule. -/
nonrec theorem HasDerivAt.scomp (hg : HasDerivAt gâ‚ gâ‚' (h x)) (hh : HasDerivAt h h' x) :
    HasDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x :=
  hg.scomp x hh hh.continuousAt

/-- The chain rule. -/
theorem HasDerivAt.scomp_of_eq
    (hg : HasDerivAt gâ‚ gâ‚' y) (hh : HasDerivAt h h' x) (hy : y = h x) :
    HasDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x := by
  rw [hy] at hg; exact hg.scomp x hh

theorem HasStrictDerivAt.scomp (hg : HasStrictDerivAt gâ‚ gâ‚' (h x)) (hh : HasStrictDerivAt h h' x) :
    HasStrictDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x := by
  simpa using ((hg.restrictScalars ğ•œ).comp x hh).hasStrictDerivAt

theorem HasStrictDerivAt.scomp_of_eq
    (hg : HasStrictDerivAt gâ‚ gâ‚' y) (hh : HasStrictDerivAt h h' x) (hy : y = h x) :
    HasStrictDerivAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') x := by
  rw [hy] at hg; exact hg.scomp x hh

theorem HasDerivAt.scomp_hasDerivWithinAt (hg : HasDerivAt gâ‚ gâ‚' (h x))
    (hh : HasDerivWithinAt h h' s x) : HasDerivWithinAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x :=
  HasDerivWithinAt.scomp x hg.hasDerivWithinAt hh (mapsTo_univ _ _)

theorem HasDerivAt.scomp_hasDerivWithinAt_of_eq (hg : HasDerivAt gâ‚ gâ‚' y)
    (hh : HasDerivWithinAt h h' s x) (hy : y = h x) :
    HasDerivWithinAt (gâ‚ âˆ˜ h) (h' â€¢ gâ‚') s x := by
  rw [hy] at hg; exact hg.scomp_hasDerivWithinAt x hh

theorem derivWithin.scomp (hg : DifferentiableWithinAt ğ•œ' gâ‚ t' (h x))
    (hh : DifferentiableWithinAt ğ•œ h s x) (hs : MapsTo h s t') :
    derivWithin (gâ‚ âˆ˜ h) s x = derivWithin h s x â€¢ derivWithin gâ‚ t' (h x) := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (HasDerivWithinAt.scomp x hg.hasDerivWithinAt hh.hasDerivWithinAt hs).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem derivWithin.scomp_of_eq (hg : DifferentiableWithinAt ğ•œ' gâ‚ t' y)
    (hh : DifferentiableWithinAt ğ•œ h s x) (hs : MapsTo h s t')
    (hy : y = h x) :
    derivWithin (gâ‚ âˆ˜ h) s x = derivWithin h s x â€¢ derivWithin gâ‚ t' (h x) := by
  rw [hy] at hg; exact derivWithin.scomp x hg hh hs

theorem deriv.scomp (hg : DifferentiableAt ğ•œ' gâ‚ (h x)) (hh : DifferentiableAt ğ•œ h x) :
    deriv (gâ‚ âˆ˜ h) x = deriv h x â€¢ deriv gâ‚ (h x) :=
  (HasDerivAt.scomp x hg.hasDerivAt hh.hasDerivAt).deriv

theorem deriv.scomp_of_eq
    (hg : DifferentiableAt ğ•œ' gâ‚ y) (hh : DifferentiableAt ğ•œ h x) (hy : y = h x) :
    deriv (gâ‚ âˆ˜ h) x = deriv h x â€¢ deriv gâ‚ (h x) := by
  rw [hy] at hg; exact deriv.scomp x hg hh

/-! ### Derivative of the composition of a scalar and vector functions -/

theorem HasDerivAtFilter.comp_hasFDerivAtFilter {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x) {L'' : Filter E}
    (hhâ‚‚ : HasDerivAtFilter hâ‚‚ hâ‚‚' (f x) L') (hf : HasFDerivAtFilter f f' x L'')
    (hL : Tendsto f L'' L') : HasFDerivAtFilter (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x L'' := by
  convert (hhâ‚‚.restrictScalars ğ•œ).comp x hf hL
  ext x
  simp [mul_comm]

theorem HasDerivAtFilter.comp_hasFDerivAtFilter_of_eq
    {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x) {L'' : Filter E}
    (hhâ‚‚ : HasDerivAtFilter hâ‚‚ hâ‚‚' y L') (hf : HasFDerivAtFilter f f' x L'')
    (hL : Tendsto f L'' L') (hy : y = f x) : HasFDerivAtFilter (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x L'' := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp_hasFDerivAtFilter x hf hL

theorem HasStrictDerivAt.comp_hasStrictFDerivAt {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
    (hh : HasStrictDerivAt hâ‚‚ hâ‚‚' (f x)) (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x := by
  rw [HasStrictDerivAt] at hh
  convert (hh.restrictScalars ğ•œ).comp x hf
  ext x
  simp [mul_comm]

theorem HasStrictDerivAt.comp_hasStrictFDerivAt_of_eq {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
    (hh : HasStrictDerivAt hâ‚‚ hâ‚‚' y) (hf : HasStrictFDerivAt f f' x) (hy : y = f x) :
    HasStrictFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x := by
  rw [hy] at hh; exact hh.comp_hasStrictFDerivAt x hf

theorem HasDerivAt.comp_hasFDerivAt {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
    (hh : HasDerivAt hâ‚‚ hâ‚‚' (f x)) (hf : HasFDerivAt f f' x) : HasFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x :=
  hh.comp_hasFDerivAtFilter x hf hf.continuousAt

theorem HasDerivAt.comp_hasFDerivAt_of_eq {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} (x)
    (hh : HasDerivAt hâ‚‚ hâ‚‚' y) (hf : HasFDerivAt f f' x) (hy : y = f x) :
    HasFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x := by
  rw [hy] at hh; exact hh.comp_hasFDerivAt x hf

theorem HasDerivAt.comp_hasFDerivWithinAt {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s} (x)
    (hh : HasDerivAt hâ‚‚ hâ‚‚' (f x)) (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x :=
  hh.comp_hasFDerivAtFilter x hf hf.continuousWithinAt

theorem HasDerivAt.comp_hasFDerivWithinAt_of_eq {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s} (x)
    (hh : HasDerivAt hâ‚‚ hâ‚‚' y) (hf : HasFDerivWithinAt f f' s x) (hy : y = f x) :
    HasFDerivWithinAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x := by
  rw [hy] at hh; exact hh.comp_hasFDerivWithinAt x hf

theorem HasDerivWithinAt.comp_hasFDerivWithinAt {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s t} (x)
    (hh : HasDerivWithinAt hâ‚‚ hâ‚‚' t (f x)) (hf : HasFDerivWithinAt f f' s x) (hst : MapsTo f s t) :
    HasFDerivWithinAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x :=
  hh.comp_hasFDerivAtFilter x hf <| hf.continuousWithinAt.tendsto_nhdsWithin hst

theorem HasDerivWithinAt.comp_hasFDerivWithinAt_of_eq {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {s t} (x)
    (hh : HasDerivWithinAt hâ‚‚ hâ‚‚' t y) (hf : HasFDerivWithinAt f f' s x) (hst : MapsTo f s t)
    (hy : y = f x) :
    HasFDerivWithinAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') s x := by
  rw [hy] at hh; exact hh.comp_hasFDerivWithinAt x hf hst

theorem HasDerivWithinAt.comp_hasFDerivAt {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {t} (x)
    (hh : HasDerivWithinAt hâ‚‚ hâ‚‚' t (f x)) (hf : HasFDerivAt f f' x) (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t) :
    HasFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x :=
  hh.comp_hasFDerivAtFilter x hf <| tendsto_nhdsWithin_iff.mpr âŸ¨hf.continuousAt, htâŸ©

theorem HasDerivWithinAt.comp_hasFDerivAt_of_eq {f : E â†’ ğ•œ'} {f' : E â†’L[ğ•œ] ğ•œ'} {t} (x)
    (hh : HasDerivWithinAt hâ‚‚ hâ‚‚' t y) (hf : HasFDerivAt f f' x) (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t)
    (hy : y = f x) : HasFDerivAt (hâ‚‚ âˆ˜ f) (hâ‚‚' â€¢ f') x := by
  subst y; exact hh.comp_hasFDerivAt x hf ht

/-! ### Derivative of the composition of two scalar functions -/

theorem HasDerivAtFilter.comp (hhâ‚‚ : HasDerivAtFilter hâ‚‚ hâ‚‚' (h x) L')
    (hh : HasDerivAtFilter h h' x L) (hL : Tendsto h L L') :
    HasDerivAtFilter (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x L := by
  rw [mul_comm]
  exact hhâ‚‚.scomp x hh hL

theorem HasDerivAtFilter.comp_of_eq (hhâ‚‚ : HasDerivAtFilter hâ‚‚ hâ‚‚' y L')
    (hh : HasDerivAtFilter h h' x L) (hL : Tendsto h L L') (hy : y = h x) :
    HasDerivAtFilter (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x L := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp x hh hL

theorem HasDerivWithinAt.comp (hhâ‚‚ : HasDerivWithinAt hâ‚‚ hâ‚‚' s' (h x))
    (hh : HasDerivWithinAt h h' s x) (hst : MapsTo h s s') :
    HasDerivWithinAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x := by
  rw [mul_comm]
  exact hhâ‚‚.scomp x hh hst

theorem HasDerivWithinAt.comp_of_eq (hhâ‚‚ : HasDerivWithinAt hâ‚‚ hâ‚‚' s' y)
    (hh : HasDerivWithinAt h h' s x) (hst : MapsTo h s s') (hy : y = h x) :
    HasDerivWithinAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp x hh hst

/-- The chain rule.

Note that the function `hâ‚‚` is a function on an algebra. If you are looking for the chain rule
with `hâ‚‚` taking values in a vector space, use `HasDerivAt.scomp`. -/
nonrec theorem HasDerivAt.comp (hhâ‚‚ : HasDerivAt hâ‚‚ hâ‚‚' (h x)) (hh : HasDerivAt h h' x) :
    HasDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x :=
  hhâ‚‚.comp x hh hh.continuousAt

/-- The chain rule.

Note that the function `hâ‚‚` is a function on an algebra. If you are looking for the chain rule
with `hâ‚‚` taking values in a vector space, use `HasDerivAt.scomp_of_eq`. -/
theorem HasDerivAt.comp_of_eq
    (hhâ‚‚ : HasDerivAt hâ‚‚ hâ‚‚' y) (hh : HasDerivAt h h' x) (hy : y = h x) :
    HasDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp x hh

theorem HasStrictDerivAt.comp (hhâ‚‚ : HasStrictDerivAt hâ‚‚ hâ‚‚' (h x)) (hh : HasStrictDerivAt h h' x) :
    HasStrictDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x := by
  rw [mul_comm]
  exact hhâ‚‚.scomp x hh

theorem HasStrictDerivAt.comp_of_eq
    (hhâ‚‚ : HasStrictDerivAt hâ‚‚ hâ‚‚' y) (hh : HasStrictDerivAt h h' x) (hy : y = h x) :
    HasStrictDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp x hh

theorem HasDerivAt.comp_hasDerivWithinAt (hhâ‚‚ : HasDerivAt hâ‚‚ hâ‚‚' (h x))
    (hh : HasDerivWithinAt h h' s x) : HasDerivWithinAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x :=
  hhâ‚‚.hasDerivWithinAt.comp x hh (mapsTo_univ _ _)

theorem HasDerivAt.comp_hasDerivWithinAt_of_eq (hhâ‚‚ : HasDerivAt hâ‚‚ hâ‚‚' y)
    (hh : HasDerivWithinAt h h' s x) (hy : y = h x) :
    HasDerivWithinAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') s x := by
  rw [hy] at hhâ‚‚; exact hhâ‚‚.comp_hasDerivWithinAt x hh

theorem HasDerivWithinAt.comp_hasDerivAt {t} (hhâ‚‚ : HasDerivWithinAt hâ‚‚ hâ‚‚' t (h x))
    (hh : HasDerivAt h h' x) (ht : âˆ€á¶  x' in ğ“ x, h x' âˆˆ t) : HasDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x :=
  HasDerivAtFilter.comp x hhâ‚‚ hh <| tendsto_nhdsWithin_iff.mpr âŸ¨hh.continuousAt, htâŸ©

theorem HasDerivWithinAt.comp_hasDerivAt_of_eq {t} (hhâ‚‚ : HasDerivWithinAt hâ‚‚ hâ‚‚' t y)
    (hh : HasDerivAt h h' x) (ht : âˆ€á¶  x' in ğ“ x, h x' âˆˆ t) (hy : y = h x) :
    HasDerivAt (hâ‚‚ âˆ˜ h) (hâ‚‚' * h') x := by
  subst y; exact hhâ‚‚.comp_hasDerivAt x hh ht

theorem derivWithin_comp (hhâ‚‚ : DifferentiableWithinAt ğ•œ' hâ‚‚ s' (h x))
    (hh : DifferentiableWithinAt ğ•œ h s x) (hs : MapsTo h s s') :
    derivWithin (hâ‚‚ âˆ˜ h) s x = derivWithin hâ‚‚ s' (h x) * derivWithin h s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hhâ‚‚.hasDerivWithinAt.comp x hh.hasDerivWithinAt hs).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem derivWithin_comp_of_eq (hhâ‚‚ : DifferentiableWithinAt ğ•œ' hâ‚‚ s' y)
    (hh : DifferentiableWithinAt ğ•œ h s x) (hs : MapsTo h s s')
    (hy : h x = y) :
    derivWithin (hâ‚‚ âˆ˜ h) s x = derivWithin hâ‚‚ s' (h x) * derivWithin h s x := by
  subst hy; exact derivWithin_comp x hhâ‚‚ hh hs

theorem deriv_comp (hhâ‚‚ : DifferentiableAt ğ•œ' hâ‚‚ (h x)) (hh : DifferentiableAt ğ•œ h x) :
    deriv (hâ‚‚ âˆ˜ h) x = deriv hâ‚‚ (h x) * deriv h x :=
  (hhâ‚‚.hasDerivAt.comp x hh.hasDerivAt).deriv

theorem deriv_comp_of_eq (hhâ‚‚ : DifferentiableAt ğ•œ' hâ‚‚ y) (hh : DifferentiableAt ğ•œ h x)
    (hy : h x = y) :
    deriv (hâ‚‚ âˆ˜ h) x = deriv hâ‚‚ (h x) * deriv h x := by
  subst hy; exact deriv_comp x hhâ‚‚ hh

protected nonrec theorem HasDerivAtFilter.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
    (hf : HasDerivAtFilter f f' x L) (hL : Tendsto f L L) (hx : f x = x) (n : â„•) :
    HasDerivAtFilter f^[n] (f' ^ n) x L := by
  have := hf.iterate hL hx n
  rwa [ContinuousLinearMap.smulRight_one_pow] at this

protected nonrec theorem HasDerivAt.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ} (hf : HasDerivAt f f' x)
    (hx : f x = x) (n : â„•) : HasDerivAt f^[n] (f' ^ n) x :=
  hf.iterate _ (have := hf.tendsto_nhds le_rfl; by rwa [hx] at this) hx n

protected theorem HasDerivWithinAt.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ} (hf : HasDerivWithinAt f f' s x)
    (hx : f x = x) (hs : MapsTo f s s) (n : â„•) : HasDerivWithinAt f^[n] (f' ^ n) s x := by
  have := HasFDerivWithinAt.iterate hf hx hs n
  rwa [ContinuousLinearMap.smulRight_one_pow] at this

protected nonrec theorem HasStrictDerivAt.iterate {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}
    (hf : HasStrictDerivAt f f' x) (hx : f x = x) (n : â„•) :
    HasStrictDerivAt f^[n] (f' ^ n) x := by
  have := hf.iterate hx n
  rwa [ContinuousLinearMap.smulRight_one_pow] at this

end Composition

section CompositionVector

/-! ### Derivative of the composition of a function between vector spaces and a function on `ğ•œ` -/

open ContinuousLinearMap

variable {l : F â†’ E} {l' : F â†’L[ğ•œ] E} {y : F}
variable (x)

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative within a set
equal to the FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem HasFDerivWithinAt.comp_hasDerivWithinAt {t : Set F} (hl : HasFDerivWithinAt l l' t (f x))
    (hf : HasDerivWithinAt f f' s x) (hst : MapsTo f s t) :
    HasDerivWithinAt (l âˆ˜ f) (l' f') s x := by
  simpa only [one_apply, one_smul, smulRight_apply, coe_comp', (Â· âˆ˜ Â·)] using
    (hl.comp x hf.hasFDerivWithinAt hst).hasDerivWithinAt

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative within a set
equal to the FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem HasFDerivWithinAt.comp_hasDerivWithinAt_of_eq {t : Set F}
    (hl : HasFDerivWithinAt l l' t y)
    (hf : HasDerivWithinAt f f' s x) (hst : MapsTo f s t) (hy : y = f x) :
    HasDerivWithinAt (l âˆ˜ f) (l' f') s x := by
  rw [hy] at hl; exact hl.comp_hasDerivWithinAt x hf hst

theorem HasFDerivWithinAt.comp_hasDerivAt {t : Set F} (hl : HasFDerivWithinAt l l' t (f x))
    (hf : HasDerivAt f f' x) (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t) : HasDerivAt (l âˆ˜ f) (l' f') x := by
  simpa only [one_apply, one_smul, smulRight_apply, coe_comp', (Â· âˆ˜ Â·)] using
    (hl.comp_hasFDerivAt x hf.hasFDerivAt ht).hasDerivAt

theorem HasFDerivWithinAt.comp_hasDerivAt_of_eq {t : Set F} (hl : HasFDerivWithinAt l l' t y)
    (hf : HasDerivAt f f' x) (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t) (hy : y = f x) :
    HasDerivAt (l âˆ˜ f) (l' f') x := by
  subst y; exact hl.comp_hasDerivAt x hf ht

theorem HasFDerivAt.comp_hasDerivWithinAt (hl : HasFDerivAt l l' (f x))
    (hf : HasDerivWithinAt f f' s x) : HasDerivWithinAt (l âˆ˜ f) (l' f') s x :=
  hl.hasFDerivWithinAt.comp_hasDerivWithinAt x hf (mapsTo_univ _ _)

theorem HasFDerivAt.comp_hasDerivWithinAt_of_eq (hl : HasFDerivAt l l' y)
    (hf : HasDerivWithinAt f f' s x) (hy : y = f x) :
    HasDerivWithinAt (l âˆ˜ f) (l' f') s x := by
  rw [hy] at hl; exact hl.comp_hasDerivWithinAt x hf

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative equal to the
FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem HasFDerivAt.comp_hasDerivAt (hl : HasFDerivAt l l' (f x)) (hf : HasDerivAt f f' x) :
    HasDerivAt (l âˆ˜ f) (l' f') x :=
  hasDerivWithinAt_univ.mp <| hl.comp_hasDerivWithinAt x hf.hasDerivWithinAt

/-- The composition `l âˆ˜ f` where `l : F â†’ E` and `f : ğ•œ â†’ F`, has a derivative equal to the
FrÃ©chet derivative of `l` applied to the derivative of `f`. -/
theorem HasFDerivAt.comp_hasDerivAt_of_eq
    (hl : HasFDerivAt l l' y) (hf : HasDerivAt f f' x) (hy : y = f x) :
    HasDerivAt (l âˆ˜ f) (l' f') x := by
  rw [hy] at hl; exact hl.comp_hasDerivAt x hf

theorem HasStrictFDerivAt.comp_hasStrictDerivAt (hl : HasStrictFDerivAt l l' (f x))
    (hf : HasStrictDerivAt f f' x) : HasStrictDerivAt (l âˆ˜ f) (l' f') x := by
  simpa only [one_apply, one_smul, smulRight_apply, coe_comp', (Â· âˆ˜ Â·)] using
    (hl.comp x hf.hasStrictFDerivAt).hasStrictDerivAt

theorem HasStrictFDerivAt.comp_hasStrictDerivAt_of_eq (hl : HasStrictFDerivAt l l' y)
    (hf : HasStrictDerivAt f f' x) (hy : y = f x) :
    HasStrictDerivAt (l âˆ˜ f) (l' f') x := by
  rw [hy] at hl; exact hl.comp_hasStrictDerivAt x hf

theorem fderivWithin_comp_derivWithin {t : Set F} (hl : DifferentiableWithinAt ğ•œ l t (f x))
    (hf : DifferentiableWithinAt ğ•œ f s x) (hs : MapsTo f s t) :
    derivWithin (l âˆ˜ f) s x = (fderivWithin ğ•œ l t (f x) : F â†’ E) (derivWithin f s x) := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hl.hasFDerivWithinAt.comp_hasDerivWithinAt x hf.hasDerivWithinAt hs).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem fderivWithin_comp_derivWithin_of_eq {t : Set F} (hl : DifferentiableWithinAt ğ•œ l t y)
    (hf : DifferentiableWithinAt ğ•œ f s x) (hs : MapsTo f s t) (hy : y = f x) :
    derivWithin (l âˆ˜ f) s x = (fderivWithin ğ•œ l t (f x) : F â†’ E) (derivWithin f s x) := by
  rw [hy] at hl; exact fderivWithin_comp_derivWithin x hl hf hs

theorem fderiv_comp_deriv (hl : DifferentiableAt ğ•œ l (f x)) (hf : DifferentiableAt ğ•œ f x) :
    deriv (l âˆ˜ f) x = (fderiv ğ•œ l (f x) : F â†’ E) (deriv f x) :=
  (hl.hasFDerivAt.comp_hasDerivAt x hf.hasDerivAt).deriv

theorem fderiv_comp_deriv_of_eq (hl : DifferentiableAt ğ•œ l y) (hf : DifferentiableAt ğ•œ f x)
    (hy : y = f x) :
    deriv (l âˆ˜ f) x = (fderiv ğ•œ l (f x) : F â†’ E) (deriv f x) := by
  rw [hy] at hl; exact fderiv_comp_deriv x hl hf

end CompositionVector
