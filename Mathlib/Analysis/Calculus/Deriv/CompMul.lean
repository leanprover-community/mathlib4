/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.Deriv.Mul

/-!
# Derivative of `x â†¦ f (cx)`

In this file we prove that the derivative of `fun x â†¦ f (c * x)`
equals `c` times the derivative of `f` evaluated at `c * x`.

Since Mathlib uses `0` as the fallback value for the derivatives whenever they are undefined,
the theorems in this file require neither differentiability of `f`,
nor assumptions like `UniqueDiffWithinAt ğ•œ s x`.
-/

open Set
open scoped Pointwise

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {c : ğ•œ} {f : ğ•œ â†’ E} {f' : E} {s : Set ğ•œ} {x : ğ•œ}

theorem hasDerivWithinAt_comp_mul_left_smul_iff :
    HasDerivWithinAt (f <| c * Â·) (c â€¢ f') s x â†” HasDerivWithinAt f f' (c â€¢ s) (c * x) := by
  simp only [hasDerivWithinAt_iff_hasFDerivWithinAt, â† smul_eq_mul,
    â† hasFDerivWithinAt_comp_smul_smul_iff]
  simp only [ContinuousLinearMap.one_smulRight_eq_toSpanSingleton,
    ContinuousLinearMap.toSpanSingleton_smul]

variable (c f s x) in
theorem derivWithin_comp_mul_left :
    derivWithin (f <| c * Â·) s x = c â€¢ derivWithin f (c â€¢ s) (c * x) := by
  simp only [â† smul_eq_mul]
  rw [â† derivWithin_const_smul', derivWithin, derivWithin,
    fderivWithin_comp_smul_eq_fderivWithin_smul, Pi.smul_def]

variable (c f x) in
theorem deriv_comp_mul_left : deriv (f <| c * Â·) x = c â€¢ deriv f (c * x) := by
  simp only [â† smul_eq_mul, deriv, fderiv_comp_smul, ContinuousLinearMap.smul_apply]
