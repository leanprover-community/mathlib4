/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Comp

/-!
# Derivative of `(f x) ^ n`, `n : â„•`

In this file we prove that `(x ^ n)' = n * x ^ (n - 1)`, where `n` is a natural number.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative, power
-/

universe u

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {x : ğ•œ} {s : Set ğ•œ}

/-! ### Derivative of `x â†¦ x^n` for `n : â„•` -/

variable {c : ğ•œ â†’ ğ•œ} {c' : ğ•œ}
variable (n : â„•)

theorem hasStrictDerivAt_pow :
    âˆ€ (n : â„•) (x : ğ•œ), HasStrictDerivAt (fun x : ğ•œ â†¦ x ^ n) ((n : ğ•œ) * x ^ (n - 1)) x
  | 0, x => by simp [hasStrictDerivAt_const]
  | 1, x => by simpa using hasStrictDerivAt_id x
  | n + 1 + 1, x => by
    simpa [pow_succ, add_mul, mul_assoc] using
      (hasStrictDerivAt_pow (n + 1) x).mul (hasStrictDerivAt_id x)

theorem hasDerivAt_pow (n : â„•) (x : ğ•œ) :
    HasDerivAt (fun x : ğ•œ => x ^ n) ((n : ğ•œ) * x ^ (n - 1)) x :=
  (hasStrictDerivAt_pow n x).hasDerivAt

theorem hasDerivWithinAt_pow (n : â„•) (x : ğ•œ) (s : Set ğ•œ) :
    HasDerivWithinAt (fun x : ğ•œ => x ^ n) ((n : ğ•œ) * x ^ (n - 1)) s x :=
  (hasDerivAt_pow n x).hasDerivWithinAt

theorem differentiableAt_pow : DifferentiableAt ğ•œ (fun x : ğ•œ => x ^ n) x :=
  (hasDerivAt_pow n x).differentiableAt

theorem differentiableWithinAt_pow :
    DifferentiableWithinAt ğ•œ (fun x : ğ•œ => x ^ n) s x :=
  (differentiableAt_pow n).differentiableWithinAt

theorem differentiable_pow : Differentiable ğ•œ fun x : ğ•œ => x ^ n := fun _ => differentiableAt_pow n

theorem differentiableOn_pow : DifferentiableOn ğ•œ (fun x : ğ•œ => x ^ n) s :=
  (differentiable_pow n).differentiableOn

theorem deriv_pow : deriv (fun x : ğ•œ => x ^ n) x = (n : ğ•œ) * x ^ (n - 1) :=
  (hasDerivAt_pow n x).deriv

@[simp]
theorem deriv_pow' : (deriv fun x : ğ•œ => x ^ n) = fun x => (n : ğ•œ) * x ^ (n - 1) :=
  funext fun _ => deriv_pow n

theorem derivWithin_pow (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin (fun x : ğ•œ => x ^ n) s x = (n : ğ•œ) * x ^ (n - 1) :=
  (hasDerivWithinAt_pow n x s).derivWithin hxs

theorem HasDerivWithinAt.pow (hc : HasDerivWithinAt c c' s x) :
    HasDerivWithinAt (fun y => c y ^ n) ((n : ğ•œ) * c x ^ (n - 1) * c') s x :=
  (hasDerivAt_pow n (c x)).comp_hasDerivWithinAt x hc

theorem HasDerivAt.pow (hc : HasDerivAt c c' x) :
    HasDerivAt (fun y => c y ^ n) ((n : ğ•œ) * c x ^ (n - 1) * c') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.pow n

theorem derivWithin_pow' (hc : DifferentiableWithinAt ğ•œ c s x) :
    derivWithin (fun x => c x ^ n) s x = (n : ğ•œ) * c x ^ (n - 1) * derivWithin c s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.pow n).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp]
theorem deriv_pow'' (hc : DifferentiableAt ğ•œ c x) :
    deriv (fun x => c x ^ n) x = (n : ğ•œ) * c x ^ (n - 1) * deriv c x :=
  (hc.hasDerivAt.pow n).deriv
