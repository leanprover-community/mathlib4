/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Pow

/-!
# Derivative of `(f x) ^ n`, `n : â„•`

In this file we prove that the FrÃ©chet derivative of `fun x => f x ^ n`,
where `n` is a natural number, is `n * f x ^ (n - 1) * f'`.
Additionally, we prove the case for non-commutative rings (with primed names like `deriv_pow'`),
where the result is instead `âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i`.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Mathlib/Analysis/Calculus/Deriv/Basic.lean`.

## Keywords

derivative, power
-/

variable {ğ•œ ğ”¸ : Type*}

section NormedRing
variable [NontriviallyNormedField ğ•œ] [NormedRing ğ”¸]
variable [NormedAlgebra ğ•œ ğ”¸] {f : ğ•œ â†’ ğ”¸} {f' : ğ”¸} {x : ğ•œ} {s : Set ğ•œ}

nonrec theorem HasStrictDerivAt.fun_pow' (h : HasStrictDerivAt f f' x) (n : â„•) :
    HasStrictDerivAt (fun x â†¦ f x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) x := by
  unfold HasStrictDerivAt
  convert h.pow' n
  ext
  simp

nonrec theorem HasStrictDerivAt.pow' (h : HasStrictDerivAt f f' x) (n : â„•) :
    HasStrictDerivAt (f ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) x := h.fun_pow' n

nonrec theorem HasDerivWithinAt.fun_pow' (h : HasDerivWithinAt f f' s x) (n : â„•) :
    HasDerivWithinAt (fun x â†¦ f x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) s x := by
  simpa using h.hasFDerivWithinAt.pow' n |>.hasDerivWithinAt

nonrec theorem HasDerivWithinAt.pow' (h : HasDerivWithinAt f f' s x) (n : â„•) :
    HasDerivWithinAt (f ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) s x := h.fun_pow' n

theorem HasDerivAt.fun_pow' (h : HasDerivAt f f' x) (n : â„•) :
    HasDerivAt (fun x â†¦ f x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) x := by
  simpa using h.hasFDerivAt.pow' n |>.hasDerivAt

theorem HasDerivAt.pow' (h : HasDerivAt f f' x) (n : â„•) :
    HasDerivAt (f ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * f' * f x ^ i) x := h.fun_pow' n

@[simp low]
theorem derivWithin_fun_pow' (h : DifferentiableWithinAt ğ•œ f s x) (n : â„•) :
    derivWithin (fun x => f x ^ n) s x =
      âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * derivWithin f s x * f x ^ i := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (h.hasDerivWithinAt.pow' n).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp low]
theorem derivWithin_pow' (h : DifferentiableWithinAt ğ•œ f s x) (n : â„•) :
    derivWithin (f ^ n) s x =
      âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * derivWithin f s x * f x ^ i :=
  derivWithin_fun_pow' h n

@[simp low]
theorem deriv_fun_pow' (h : DifferentiableAt ğ•œ f x) (n : â„•) :
    deriv (fun x => f x ^ n) x = âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * deriv f x * f x ^ i :=
  (h.hasDerivAt.pow' n).deriv

@[simp low]
theorem deriv_pow' (h : DifferentiableAt ğ•œ f x) (n : â„•) :
    deriv (f ^ n) x = âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) * deriv f x * f x ^ i :=
  deriv_fun_pow' h n

end NormedRing

section NormedCommRing
variable [NontriviallyNormedField ğ•œ] [NormedCommRing ğ”¸]
variable [NormedAlgebra ğ•œ ğ”¸] {f : ğ•œ â†’ ğ”¸} {f' : ğ”¸} {x : ğ•œ} {s : Set ğ•œ}

open scoped RightActions

nonrec theorem HasStrictDerivAt.fun_pow (h : HasStrictDerivAt f f' x) (n : â„•) :
    HasStrictDerivAt (fun x â†¦ f x ^ n) (n * f x ^ (n - 1) * f') x := by
  unfold HasStrictDerivAt
  convert h.pow n
  ext
  simp [mul_assoc]

nonrec theorem HasStrictDerivAt.pow (h : HasStrictDerivAt f f' x) (n : â„•) :
    HasStrictDerivAt (f ^ n) (n * f x ^ (n - 1) * f') x := h.fun_pow n

nonrec theorem HasDerivWithinAt.fun_pow (h : HasDerivWithinAt f f' s x) (n : â„•) :
    HasDerivWithinAt (fun x â†¦ f x ^ n) (n * f x ^ (n - 1) * f') s x := by
  simpa using h.hasFDerivWithinAt.pow n |>.hasDerivWithinAt

nonrec theorem HasDerivWithinAt.pow (h : HasDerivWithinAt f f' s x) (n : â„•) :
    HasDerivWithinAt (f ^ n) (n * f x ^ (n - 1) * f') s x := h.fun_pow n

theorem HasDerivAt.fun_pow (h : HasDerivAt f f' x) (n : â„•) :
    HasDerivAt (fun x â†¦ f x ^ n) (n * f x ^ (n - 1) * f') x := by
  simpa using h.hasFDerivAt.pow n |>.hasDerivAt

theorem HasDerivAt.pow (h : HasDerivAt f f' x) (n : â„•) :
    HasDerivAt (f ^ n) (n * f x ^ (n - 1) * f') x := h.fun_pow n

@[simp]
theorem derivWithin_fun_pow (h : DifferentiableWithinAt ğ•œ f s x) (n : â„•) :
    derivWithin (fun x => f x ^ n) s x = n * f x ^ (n - 1) * derivWithin f s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (h.hasDerivWithinAt.pow n).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp]
theorem derivWithin_pow (h : DifferentiableWithinAt ğ•œ f s x) (n : â„•) :
    derivWithin (f ^ n) s x = n * f x ^ (n - 1) * derivWithin f s x :=
  derivWithin_fun_pow h n

@[simp]
theorem deriv_fun_pow (h : DifferentiableAt ğ•œ f x) (n : â„•) :
    deriv (fun x => f x ^ n) x = n * f x ^ (n - 1) * deriv f x :=
  (h.hasDerivAt.pow n).deriv

@[simp]
theorem deriv_pow (h : DifferentiableAt ğ•œ f x) (n : â„•) :
    deriv (f ^ n) x = n * f x ^ (n - 1) * deriv f x := deriv_fun_pow h n

end NormedCommRing

section NontriviallyNormedField
variable [NontriviallyNormedField ğ•œ] {x : ğ•œ} {s : Set ğ•œ} {c : ğ•œ â†’ ğ•œ}

@[deprecated deriv_fun_pow (since := "2025-07-16")]
theorem deriv_fun_pow'' {c : ğ•œ â†’ ğ•œ} (n : â„•) (hc : DifferentiableAt ğ•œ c x) :
    deriv (fun x => c x ^ n) x = (n : ğ•œ) * c x ^ (n - 1) * deriv c x :=
  deriv_fun_pow hc n

@[deprecated deriv_pow (since := "2025-07-16")]
theorem deriv_pow'' {c : ğ•œ â†’ ğ•œ} (n : â„•) (hc : DifferentiableAt ğ•œ c x) :
    deriv (c ^ n) x = (n : ğ•œ) * c x ^ (n - 1) * deriv c x :=
  deriv_pow hc n

theorem hasStrictDerivAt_pow (n : â„•) (x : ğ•œ) :
    HasStrictDerivAt (fun x : ğ•œ â†¦ x ^ n) (n * x ^ (n - 1)) x := by
  simpa using (hasStrictDerivAt_id x).pow n

theorem hasDerivWithinAt_pow (n : â„•) (x : ğ•œ) :
    HasDerivWithinAt (fun x : ğ•œ â†¦ x ^ n) (n * x ^ (n - 1)) s x := by
  simpa using (hasDerivWithinAt_id x s).pow n

theorem hasDerivAt_pow (n : â„•) (x : ğ•œ) :
    HasDerivAt (fun x : ğ•œ => x ^ n) ((n : ğ•œ) * x ^ (n - 1)) x := by
  simpa using (hasStrictDerivAt_pow n x).hasDerivAt

theorem derivWithin_pow_field (h : UniqueDiffWithinAt ğ•œ s x) (n : â„•) :
    derivWithin (fun x => x ^ n) s x = (n : ğ•œ) * x ^ (n - 1) := by
  rw [derivWithin_fun_pow (differentiableWithinAt_id' (s := s)) n, derivWithin_id' _ _ h, mul_one]

theorem deriv_pow_field (n : â„•) : deriv (fun x => x ^ n) x = (n : ğ•œ) * x ^ (n - 1) := by
  simp

end NontriviallyNormedField
