/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Const
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace

/-!

# One-dimensional derivatives

This file defines the derivative of a function `f : ğ•œ â†’ F` where `ğ•œ` is a
normed field and `F` is a normed space over this field. The derivative of
such a function `f` at a point `x` is given by an element `f' : F`.

The theory is developed analogously to the [FrÃ©chet
derivatives](./fderiv.html). We first introduce predicates defined in terms
of the corresponding predicates for FrÃ©chet derivatives:

- `HasDerivAtFilter f f' x L` states that the function `f` has the
  derivative `f'` at the point `x` as `x` goes along the filter `L`.

- `HasDerivWithinAt f f' s x` states that the function `f` has the
  derivative `f'` at the point `x` within the subset `s`.

- `HasDerivAt f f' x` states that the function `f` has the derivative `f'`
  at the point `x`.

- `HasStrictDerivAt f f' x` states that the function `f` has the derivative `f'`
  at the point `x` in the sense of strict differentiability, i.e.,
  `f y - f z = (y - z) â€¢ f' + o (y - z)` as `y, z â†’ x`.

For the last two notions we also define a functional version:

- `derivWithin f s x` is a derivative of `f` at `x` within `s`. If the
  derivative does not exist, then `derivWithin f s x` equals zero.

- `deriv f x` is a derivative of `f` at `x`. If the derivative does not
  exist, then `deriv f x` equals zero.

The theorems `fderivWithin_derivWithin` and `fderiv_deriv` show that the
one-dimensional derivatives coincide with the general FrÃ©chet derivatives.

We also show the existence and compute the derivatives of:
  - constants
  - the identity function
  - linear maps (in `Linear.lean`)
  - addition (in `Add.lean`)
  - sum of finitely many functions (in `Add.lean`)
  - negation (in `Add.lean`)
  - subtraction (in `Add.lean`)
  - star (in `Star.lean`)
  - multiplication of two functions in `ğ•œ â†’ ğ•œ` (in `Mul.lean`)
  - multiplication of a function in `ğ•œ â†’ ğ•œ` and of a function in `ğ•œ â†’ E` (in `Mul.lean`)
  - powers of a function (in `Pow.lean` and `ZPow.lean`)
  - inverse `x â†’ xâ»Â¹` (in `Inv.lean`)
  - division (in `Inv.lean`)
  - composition of a function in `ğ•œ â†’ F` with a function in `ğ•œ â†’ ğ•œ` (in `Comp.lean`)
  - composition of a function in `F â†’ E` with a function in `ğ•œ â†’ F` (in `Comp.lean`)
  - inverse function (assuming that it exists; the inverse function theorem is in `Inverse.lean`)
  - polynomials (in `Polynomial.lean`)

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `HasDerivAt`'s easier,
and they more frequently lead to the desired result.

We set up the simplifier so that it can compute the derivative of simple functions. For instance,
```lean
example (x : â„) :
    deriv (fun x â†¦ cos (sin x) * exp x) x = (cos (sin x) - sin (sin x) * cos x) * exp x := by
  simp; ring
```

The relationship between the derivative of a function and its definition from a standard
undergraduate course as the limit of the slope `(f y - f x) / (y - x)` as `y` tends to `ğ“[â‰ ] x`
is developed in the file `Slope.lean`.

## Implementation notes

Most of the theorems are direct restatements of the corresponding theorems
for FrÃ©chet derivatives.

The strategy to construct simp lemmas that give the simplifier the possibility to compute
derivatives is the same as the one for differentiability statements, as explained in
`FDeriv/Basic.lean`. See the explanations there.
-/

universe u v w

noncomputable section

open scoped Topology ENNReal NNReal
open Filter Asymptotics Set

open ContinuousLinearMap (smulRight smulRight_one_eq_iff)

section TVS

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]

section
variable [ContinuousSMul ğ•œ F]
/-- `f` has the derivative `f'` at the point `x` as `x` goes along the filter `L`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges along the filter `L`.
-/
def HasDerivAtFilter (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) (L : Filter ğ•œ) :=
  HasFDerivAtFilter f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x L

/-- `f` has the derivative `f'` at the point `x` within the subset `s`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def HasDerivWithinAt (f : ğ•œ â†’ F) (f' : F) (s : Set ğ•œ) (x : ğ•œ) :=
  HasDerivAtFilter f f' x (ğ“[s] x)

/-- `f` has the derivative `f'` at the point `x`.

That is, `f x' = f x + (x' - x) â€¢ f' + o(x' - x)` where `x'` converges to `x`.
-/
def HasDerivAt (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
  HasDerivAtFilter f f' x (ğ“ x)

/-- `f` has the derivative `f'` at the point `x` in the sense of strict differentiability.

That is, `f y - f z = (y - z) â€¢ f' + o(y - z)` as `y, z â†’ x`. -/
def HasStrictDerivAt (f : ğ•œ â†’ F) (f' : F) (x : ğ•œ) :=
  HasStrictFDerivAt f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x

end
/-- Derivative of `f` at the point `x` within the set `s`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', HasDerivWithinAt f f' s x`), then
`f x' = f x + (x' - x) â€¢ derivWithin f s x + o(x' - x)` where `x'` converges to `x` inside `s`.
-/
def derivWithin (f : ğ•œ â†’ F) (s : Set ğ•œ) (x : ğ•œ) :=
  fderivWithin ğ•œ f s x 1

/-- Derivative of `f` at the point `x`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', HasDerivAt f f' x`), then
`f x' = f x + (x' - x) â€¢ deriv f x + o(x' - x)` where `x'` converges to `x`.
-/
def deriv (f : ğ•œ â†’ F) (x : ğ•œ) :=
  fderiv ğ•œ f x 1

variable {f fâ‚€ fâ‚ : ğ•œ â†’ F}
variable {f' fâ‚€' fâ‚' g' : F}
variable {x : ğ•œ}
variable {s t : Set ğ•œ}
variable {L Lâ‚ Lâ‚‚ : Filter ğ•œ}

section
variable [ContinuousSMul ğ•œ F]
/-- Expressing `HasFDerivAtFilter f f' x L` in terms of `HasDerivAtFilter` -/
theorem hasFDerivAtFilter_iff_hasDerivAtFilter {f' : ğ•œ â†’L[ğ•œ] F} :
    HasFDerivAtFilter f f' x L â†” HasDerivAtFilter f (f' 1) x L := by simp [HasDerivAtFilter]

theorem HasFDerivAtFilter.hasDerivAtFilter {f' : ğ•œ â†’L[ğ•œ] F} :
    HasFDerivAtFilter f f' x L â†’ HasDerivAtFilter f (f' 1) x L :=
  hasFDerivAtFilter_iff_hasDerivAtFilter.mp

/-- Expressing `HasFDerivWithinAt f f' s x` in terms of `HasDerivWithinAt` -/
theorem hasFDerivWithinAt_iff_hasDerivWithinAt {f' : ğ•œ â†’L[ğ•œ] F} :
    HasFDerivWithinAt f f' s x â†” HasDerivWithinAt f (f' 1) s x :=
  hasFDerivAtFilter_iff_hasDerivAtFilter

/-- Expressing `HasDerivWithinAt f f' s x` in terms of `HasFDerivWithinAt` -/
theorem hasDerivWithinAt_iff_hasFDerivWithinAt {f' : F} :
    HasDerivWithinAt f f' s x â†” HasFDerivWithinAt f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
  Iff.rfl

theorem HasFDerivWithinAt.hasDerivWithinAt {f' : ğ•œ â†’L[ğ•œ] F} :
    HasFDerivWithinAt f f' s x â†’ HasDerivWithinAt f (f' 1) s x :=
  hasFDerivWithinAt_iff_hasDerivWithinAt.mp

theorem HasDerivWithinAt.hasFDerivWithinAt {f' : F} :
    HasDerivWithinAt f f' s x â†’ HasFDerivWithinAt f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') s x :=
  hasDerivWithinAt_iff_hasFDerivWithinAt.mp

/-- Expressing `HasFDerivAt f f' x` in terms of `HasDerivAt` -/
theorem hasFDerivAt_iff_hasDerivAt {f' : ğ•œ â†’L[ğ•œ] F} : HasFDerivAt f f' x â†” HasDerivAt f (f' 1) x :=
  hasFDerivAtFilter_iff_hasDerivAtFilter

theorem HasFDerivAt.hasDerivAt {f' : ğ•œ â†’L[ğ•œ] F} : HasFDerivAt f f' x â†’ HasDerivAt f (f' 1) x :=
  hasFDerivAt_iff_hasDerivAt.mp

theorem hasStrictFDerivAt_iff_hasStrictDerivAt {f' : ğ•œ â†’L[ğ•œ] F} :
    HasStrictFDerivAt f f' x â†” HasStrictDerivAt f (f' 1) x := by
  simp [HasStrictDerivAt]

protected theorem HasStrictFDerivAt.hasStrictDerivAt {f' : ğ•œ â†’L[ğ•œ] F} :
    HasStrictFDerivAt f f' x â†’ HasStrictDerivAt f (f' 1) x :=
  hasStrictFDerivAt_iff_hasStrictDerivAt.mp

theorem hasStrictDerivAt_iff_hasStrictFDerivAt :
    HasStrictDerivAt f f' x â†” HasStrictFDerivAt f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
  Iff.rfl

alias âŸ¨HasStrictDerivAt.hasStrictFDerivAt, _âŸ© := hasStrictDerivAt_iff_hasStrictFDerivAt

/-- Expressing `HasDerivAt f f' x` in terms of `HasFDerivAt` -/
theorem hasDerivAt_iff_hasFDerivAt {f' : F} :
    HasDerivAt f f' x â†” HasFDerivAt f (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') x :=
  Iff.rfl

alias âŸ¨HasDerivAt.hasFDerivAt, _âŸ© := hasDerivAt_iff_hasFDerivAt

end
theorem derivWithin_zero_of_not_differentiableWithinAt (h : Â¬DifferentiableWithinAt ğ•œ f s x) :
    derivWithin f s x = 0 := by
  unfold derivWithin
  rw [fderivWithin_zero_of_not_differentiableWithinAt h]
  simp

theorem differentiableWithinAt_of_derivWithin_ne_zero (h : derivWithin f s x â‰  0) :
    DifferentiableWithinAt ğ•œ f s x :=
  not_imp_comm.1 derivWithin_zero_of_not_differentiableWithinAt h

end TVS

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable {f fâ‚€ fâ‚ : ğ•œ â†’ F}
variable {f' fâ‚€' fâ‚' g' : F}
variable {x : ğ•œ}
variable {s t : Set ğ•œ}
variable {L Lâ‚ Lâ‚‚ : Filter ğ•œ}

theorem derivWithin_zero_of_not_accPt (h : Â¬AccPt x (ğ“Ÿ s)) : derivWithin f s x = 0 := by
  rw [derivWithin, fderivWithin_zero_of_not_accPt h, ContinuousLinearMap.zero_apply]

theorem derivWithin_zero_of_not_uniqueDiffWithinAt (h : Â¬UniqueDiffWithinAt ğ•œ s x) :
    derivWithin f s x = 0 :=
  derivWithin_zero_of_not_accPt <| mt AccPt.uniqueDiffWithinAt h

set_option linter.deprecated false in
@[deprecated derivWithin_zero_of_not_accPt (since := "2025-04-20")]
theorem derivWithin_zero_of_isolated (h : ğ“[s \ {x}] x = âŠ¥) : derivWithin f s x = 0 := by
  rw [derivWithin, fderivWithin_zero_of_isolated h, ContinuousLinearMap.zero_apply]

theorem derivWithin_zero_of_notMem_closure (h : x âˆ‰ closure s) : derivWithin f s x = 0 := by
  rw [derivWithin, fderivWithin_zero_of_notMem_closure h, ContinuousLinearMap.zero_apply]

@[deprecated (since := "2025-05-24")]
alias derivWithin_zero_of_nmem_closure := derivWithin_zero_of_notMem_closure

theorem deriv_zero_of_not_differentiableAt (h : Â¬DifferentiableAt ğ•œ f x) : deriv f x = 0 := by
  unfold deriv
  rw [fderiv_zero_of_not_differentiableAt h]
  simp

theorem differentiableAt_of_deriv_ne_zero (h : deriv f x â‰  0) : DifferentiableAt ğ•œ f x :=
  not_imp_comm.1 deriv_zero_of_not_differentiableAt h

theorem UniqueDiffWithinAt.eq_deriv (s : Set ğ•œ) (H : UniqueDiffWithinAt ğ•œ s x)
    (h : HasDerivWithinAt f f' s x) (hâ‚ : HasDerivWithinAt f fâ‚' s x) : f' = fâ‚' :=
  smulRight_one_eq_iff.mp <| UniqueDiffWithinAt.eq H h hâ‚

theorem hasDerivAtFilter_iff_isLittleO :
    HasDerivAtFilter f f' x L â†” (fun x' : ğ•œ => f x' - f x - (x' - x) â€¢ f') =o[L] fun x' => x' - x :=
  hasFDerivAtFilter_iff_isLittleO ..

theorem hasDerivAtFilter_iff_tendsto :
    HasDerivAtFilter f f' x L â†”
      Tendsto (fun x' : ğ•œ => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) L (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem hasDerivWithinAt_iff_isLittleO :
    HasDerivWithinAt f f' s x â†”
      (fun x' : ğ•œ => f x' - f x - (x' - x) â€¢ f') =o[ğ“[s] x] fun x' => x' - x :=
  hasFDerivAtFilter_iff_isLittleO ..

theorem hasDerivWithinAt_iff_tendsto :
    HasDerivWithinAt f f' s x â†”
      Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) (ğ“[s] x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem hasDerivAt_iff_isLittleO :
    HasDerivAt f f' x â†” (fun x' : ğ•œ => f x' - f x - (x' - x) â€¢ f') =o[ğ“ x] fun x' => x' - x :=
  hasFDerivAtFilter_iff_isLittleO ..

theorem hasDerivAt_iff_tendsto :
    HasDerivAt f f' x â†” Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - (x' - x) â€¢ f'â€–) (ğ“ x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem HasDerivAtFilter.isBigO_sub (h : HasDerivAtFilter f f' x L) :
    (fun x' => f x' - f x) =O[L] fun x' => x' - x :=
  HasFDerivAtFilter.isBigO_sub h

nonrec theorem HasDerivAtFilter.isBigO_sub_rev (hf : HasDerivAtFilter f f' x L) (hf' : f' â‰  0) :
    (fun x' => x' - x) =O[L] fun x' => f x' - f x :=
  suffices AntilipschitzWith â€–f'â€–â‚Šâ»Â¹ (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') from hf.isBigO_sub_rev this
  AddMonoidHomClass.antilipschitz_of_bound (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) f') fun x => by
    simp [norm_smul, â† div_eq_inv_mul, mul_div_cancel_rightâ‚€ _ (mt norm_eq_zero.1 hf')]

theorem HasStrictDerivAt.hasDerivAt (h : HasStrictDerivAt f f' x) : HasDerivAt f f' x :=
  h.hasFDerivAt

theorem hasDerivWithinAt_congr_set' {s t : Set ğ•œ} (y : ğ•œ) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    HasDerivWithinAt f f' s x â†” HasDerivWithinAt f f' t x :=
  hasFDerivWithinAt_congr_set' y h

theorem hasDerivWithinAt_congr_set {s t : Set ğ•œ} (h : s =á¶ [ğ“ x] t) :
    HasDerivWithinAt f f' s x â†” HasDerivWithinAt f f' t x :=
  hasFDerivWithinAt_congr_set h

alias âŸ¨HasDerivWithinAt.congr_set, _âŸ© := hasDerivWithinAt_congr_set

@[simp]
theorem hasDerivWithinAt_diff_singleton :
    HasDerivWithinAt f f' (s \ {x}) x â†” HasDerivWithinAt f f' s x :=
  hasFDerivWithinAt_diff_singleton _

@[simp]
theorem hasDerivWithinAt_Ioi_iff_Ici [PartialOrder ğ•œ] :
    HasDerivWithinAt f f' (Ioi x) x â†” HasDerivWithinAt f f' (Ici x) x := by
  rw [â† Ici_diff_left, hasDerivWithinAt_diff_singleton]

alias âŸ¨HasDerivWithinAt.Ici_of_Ioi, HasDerivWithinAt.Ioi_of_IciâŸ© := hasDerivWithinAt_Ioi_iff_Ici

@[simp]
theorem hasDerivWithinAt_Iio_iff_Iic [PartialOrder ğ•œ] :
    HasDerivWithinAt f f' (Iio x) x â†” HasDerivWithinAt f f' (Iic x) x := by
  rw [â† Iic_diff_right, hasDerivWithinAt_diff_singleton]

alias âŸ¨HasDerivWithinAt.Iic_of_Iio, HasDerivWithinAt.Iio_of_IicâŸ© := hasDerivWithinAt_Iio_iff_Iic

theorem HasDerivWithinAt.Ioi_iff_Ioo [LinearOrder ğ•œ] [OrderClosedTopology ğ•œ] {x y : ğ•œ} (h : x < y) :
    HasDerivWithinAt f f' (Ioo x y) x â†” HasDerivWithinAt f f' (Ioi x) x :=
  hasFDerivWithinAt_inter <| Iio_mem_nhds h

alias âŸ¨HasDerivWithinAt.Ioi_of_Ioo, HasDerivWithinAt.Ioo_of_IoiâŸ© := HasDerivWithinAt.Ioi_iff_Ioo

theorem hasDerivAt_iff_isLittleO_nhds_zero :
    HasDerivAt f f' x â†” (fun h => f (x + h) - f x - h â€¢ f') =o[ğ“ 0] fun h => h :=
  hasFDerivAt_iff_isLittleO_nhds_zero

theorem HasDerivAtFilter.mono (h : HasDerivAtFilter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
    HasDerivAtFilter f f' x Lâ‚ :=
  HasFDerivAtFilter.mono h hst

theorem HasDerivWithinAt.mono (h : HasDerivWithinAt f f' t x) (hst : s âŠ† t) :
    HasDerivWithinAt f f' s x :=
  HasFDerivWithinAt.mono h hst

theorem HasDerivWithinAt.mono_of_mem_nhdsWithin (h : HasDerivWithinAt f f' t x) (hst : t âˆˆ ğ“[s] x) :
    HasDerivWithinAt f f' s x :=
  HasFDerivWithinAt.mono_of_mem_nhdsWithin h hst

theorem HasDerivAt.hasDerivAtFilter (h : HasDerivAt f f' x) (hL : L â‰¤ ğ“ x) :
    HasDerivAtFilter f f' x L :=
  HasFDerivAt.hasFDerivAtFilter h hL

theorem HasDerivAt.hasDerivWithinAt (h : HasDerivAt f f' x) : HasDerivWithinAt f f' s x :=
  HasFDerivAt.hasFDerivWithinAt h

theorem HasDerivWithinAt.differentiableWithinAt (h : HasDerivWithinAt f f' s x) :
    DifferentiableWithinAt ğ•œ f s x :=
  HasFDerivWithinAt.differentiableWithinAt h

theorem HasDerivAt.differentiableAt (h : HasDerivAt f f' x) : DifferentiableAt ğ•œ f x :=
  HasFDerivAt.differentiableAt h

@[simp]
theorem hasDerivWithinAt_univ : HasDerivWithinAt f f' univ x â†” HasDerivAt f f' x :=
  hasFDerivWithinAt_univ

theorem HasDerivAt.unique (hâ‚€ : HasDerivAt f fâ‚€' x) (hâ‚ : HasDerivAt f fâ‚' x) : fâ‚€' = fâ‚' :=
  smulRight_one_eq_iff.mp <| hâ‚€.hasFDerivAt.unique hâ‚

theorem hasDerivWithinAt_inter' (h : t âˆˆ ğ“[s] x) :
    HasDerivWithinAt f f' (s âˆ© t) x â†” HasDerivWithinAt f f' s x :=
  hasFDerivWithinAt_inter' h

theorem hasDerivWithinAt_inter (h : t âˆˆ ğ“ x) :
    HasDerivWithinAt f f' (s âˆ© t) x â†” HasDerivWithinAt f f' s x :=
  hasFDerivWithinAt_inter h

theorem HasDerivWithinAt.union (hs : HasDerivWithinAt f f' s x) (ht : HasDerivWithinAt f f' t x) :
    HasDerivWithinAt f f' (s âˆª t) x :=
  hs.hasFDerivWithinAt.union ht.hasFDerivWithinAt

theorem HasDerivWithinAt.hasDerivAt (h : HasDerivWithinAt f f' s x) (hs : s âˆˆ ğ“ x) :
    HasDerivAt f f' x :=
  HasFDerivWithinAt.hasFDerivAt h hs

theorem DifferentiableWithinAt.hasDerivWithinAt (h : DifferentiableWithinAt ğ•œ f s x) :
    HasDerivWithinAt f (derivWithin f s x) s x :=
  h.hasFDerivWithinAt.hasDerivWithinAt

theorem DifferentiableAt.hasDerivAt (h : DifferentiableAt ğ•œ f x) : HasDerivAt f (deriv f x) x :=
  h.hasFDerivAt.hasDerivAt

@[simp]
theorem hasDerivAt_deriv_iff : HasDerivAt f (deriv f x) x â†” DifferentiableAt ğ•œ f x :=
  âŸ¨fun h => h.differentiableAt, fun h => h.hasDerivAtâŸ©

@[simp]
theorem hasDerivWithinAt_derivWithin_iff :
    HasDerivWithinAt f (derivWithin f s x) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨fun h => h.differentiableWithinAt, fun h => h.hasDerivWithinAtâŸ©

theorem DifferentiableOn.hasDerivAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    HasDerivAt f (deriv f x) x :=
  (h.hasFDerivAt hs).hasDerivAt

theorem HasDerivAt.deriv (h : HasDerivAt f f' x) : deriv f x = f' :=
  h.differentiableAt.hasDerivAt.unique h

theorem deriv_eq {f' : ğ•œ â†’ F} (h : âˆ€ x, HasDerivAt f (f' x) x) : deriv f = f' :=
  funext fun x => (h x).deriv

theorem HasDerivWithinAt.derivWithin (h : HasDerivWithinAt f f' s x)
    (hxs : UniqueDiffWithinAt ğ•œ s x) : derivWithin f s x = f' :=
  hxs.eq_deriv _ h.differentiableWithinAt.hasDerivWithinAt h

theorem fderivWithin_derivWithin : (fderivWithin ğ•œ f s x : ğ•œ â†’ F) 1 = derivWithin f s x :=
  rfl

theorem derivWithin_fderivWithin :
    smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (derivWithin f s x) = fderivWithin ğ•œ f s x := by simp [derivWithin]

theorem norm_derivWithin_eq_norm_fderivWithin : â€–derivWithin f s xâ€– = â€–fderivWithin ğ•œ f s xâ€– := by
  simp [â† derivWithin_fderivWithin]

theorem fderiv_deriv : (fderiv ğ•œ f x : ğ•œ â†’ F) 1 = deriv f x :=
  rfl

@[simp]
theorem fderiv_eq_smul_deriv (y : ğ•œ) : (fderiv ğ•œ f x : ğ•œ â†’ F) y = y â€¢ deriv f x := by
  rw [â† fderiv_deriv, â† ContinuousLinearMap.map_smul]
  simp only [smul_eq_mul, mul_one]

theorem deriv_fderiv : smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (deriv f x) = fderiv ğ•œ f x := by
  simp only [deriv, ContinuousLinearMap.smulRight_one_one]

lemma fderiv_eq_deriv_mul {f : ğ•œ â†’ ğ•œ} {x y : ğ•œ} : (fderiv ğ•œ f x : ğ•œ â†’ ğ•œ) y = (deriv f x) * y := by
  simp [mul_comm]

theorem norm_deriv_eq_norm_fderiv : â€–deriv f xâ€– = â€–fderiv ğ•œ f xâ€– := by
  simp [â† deriv_fderiv]

theorem DifferentiableAt.derivWithin (h : DifferentiableAt ğ•œ f x) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    derivWithin f s x = deriv f x := by
  unfold _root_.derivWithin deriv
  rw [h.fderivWithin hxs]

theorem HasDerivWithinAt.deriv_eq_zero (hd : HasDerivWithinAt f 0 s x)
    (H : UniqueDiffWithinAt ğ•œ s x) : deriv f x = 0 :=
  (em' (DifferentiableAt ğ•œ f x)).elim deriv_zero_of_not_differentiableAt fun h =>
    H.eq_deriv _ h.hasDerivAt.hasDerivWithinAt hd

theorem derivWithin_of_mem_nhdsWithin (st : t âˆˆ ğ“[s] x) (ht : UniqueDiffWithinAt ğ•œ s x)
    (h : DifferentiableWithinAt ğ•œ f t x) : derivWithin f s x = derivWithin f t x :=
  ((DifferentiableWithinAt.hasDerivWithinAt h).mono_of_mem_nhdsWithin st).derivWithin ht

theorem derivWithin_subset (st : s âŠ† t) (ht : UniqueDiffWithinAt ğ•œ s x)
    (h : DifferentiableWithinAt ğ•œ f t x) : derivWithin f s x = derivWithin f t x :=
  ((DifferentiableWithinAt.hasDerivWithinAt h).mono st).derivWithin ht

theorem derivWithin_congr_set' (y : ğ•œ) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    derivWithin f s x = derivWithin f t x := by simp only [derivWithin, fderivWithin_congr_set' y h]

theorem derivWithin_congr_set (h : s =á¶ [ğ“ x] t) : derivWithin f s x = derivWithin f t x := by
  simp only [derivWithin, fderivWithin_congr_set h]

@[simp]
theorem derivWithin_univ : derivWithin f univ = deriv f := by
  ext
  unfold derivWithin deriv
  rw [fderivWithin_univ]

theorem derivWithin_inter (ht : t âˆˆ ğ“ x) : derivWithin f (s âˆ© t) x = derivWithin f s x := by
  unfold derivWithin
  rw [fderivWithin_inter ht]

theorem derivWithin_of_mem_nhds (h : s âˆˆ ğ“ x) : derivWithin f s x = deriv f x := by
  simp only [derivWithin, deriv, fderivWithin_of_mem_nhds h]

theorem derivWithin_of_isOpen (hs : IsOpen s) (hx : x âˆˆ s) : derivWithin f s x = deriv f x :=
  derivWithin_of_mem_nhds (hs.mem_nhds hx)

lemma deriv_eqOn {f' : ğ•œ â†’ F} (hs : IsOpen s) (hf' : âˆ€ x âˆˆ s, HasDerivWithinAt f (f' x) s x) :
    s.EqOn (deriv f) f' := fun x hx â†¦ by
  rw [â† derivWithin_of_isOpen hs hx, (hf' _ hx).derivWithin <| hs.uniqueDiffWithinAt hx]

theorem deriv_mem_iff {f : ğ•œ â†’ F} {s : Set F} {x : ğ•œ} :
    deriv f x âˆˆ s â†”
      DifferentiableAt ğ•œ f x âˆ§ deriv f x âˆˆ s âˆ¨ Â¬DifferentiableAt ğ•œ f x âˆ§ (0 : F) âˆˆ s := by
  by_cases hx : DifferentiableAt ğ•œ f x <;> simp [deriv_zero_of_not_differentiableAt, *]

theorem derivWithin_mem_iff {f : ğ•œ â†’ F} {t : Set ğ•œ} {s : Set F} {x : ğ•œ} :
    derivWithin f t x âˆˆ s â†”
      DifferentiableWithinAt ğ•œ f t x âˆ§ derivWithin f t x âˆˆ s âˆ¨
        Â¬DifferentiableWithinAt ğ•œ f t x âˆ§ (0 : F) âˆˆ s := by
  by_cases hx : DifferentiableWithinAt ğ•œ f t x <;>
    simp [derivWithin_zero_of_not_differentiableWithinAt, *]

theorem differentiableWithinAt_Ioi_iff_Ici [PartialOrder ğ•œ] :
    DifferentiableWithinAt ğ•œ f (Ioi x) x â†” DifferentiableWithinAt ğ•œ f (Ici x) x :=
  âŸ¨fun h => h.hasDerivWithinAt.Ici_of_Ioi.differentiableWithinAt, fun h =>
    h.hasDerivWithinAt.Ioi_of_Ici.differentiableWithinAtâŸ©

-- Golfed while splitting the file
theorem derivWithin_Ioi_eq_Ici {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (f : â„ â†’ E)
    (x : â„) : derivWithin f (Ioi x) x = derivWithin f (Ici x) x := by
  by_cases H : DifferentiableWithinAt â„ f (Ioi x) x
  Â· have A := H.hasDerivWithinAt.Ici_of_Ioi
    have B := (differentiableWithinAt_Ioi_iff_Ici.1 H).hasDerivWithinAt
    simpa using (uniqueDiffOn_Ici x).eq left_mem_Ici A B
  Â· rw [derivWithin_zero_of_not_differentiableWithinAt H,
      derivWithin_zero_of_not_differentiableWithinAt]
    rwa [differentiableWithinAt_Ioi_iff_Ici] at H

section congr

/-! ### Congruence properties of derivatives -/

theorem Filter.EventuallyEq.hasDerivAtFilter_iff (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x)
    (hâ‚ : fâ‚€' = fâ‚') : HasDerivAtFilter fâ‚€ fâ‚€' x L â†” HasDerivAtFilter fâ‚ fâ‚' x L :=
  hâ‚€.hasFDerivAtFilter_iff hx (by simp [hâ‚])

theorem HasDerivAtFilter.congr_of_eventuallyEq (h : HasDerivAtFilter f f' x L) (hL : fâ‚ =á¶ [L] f)
    (hx : fâ‚ x = f x) : HasDerivAtFilter fâ‚ f' x L := by rwa [hL.hasDerivAtFilter_iff hx rfl]

theorem HasDerivWithinAt.congr_mono (h : HasDerivWithinAt f f' s x) (ht : âˆ€ x âˆˆ t, fâ‚ x = f x)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : HasDerivWithinAt fâ‚ f' t x :=
  HasFDerivWithinAt.congr_mono h ht hx hâ‚

theorem HasDerivWithinAt.congr (h : HasDerivWithinAt f f' s x) (hs : âˆ€ x âˆˆ s, fâ‚ x = f x)
    (hx : fâ‚ x = f x) : HasDerivWithinAt fâ‚ f' s x :=
  h.congr_mono hs hx (Subset.refl _)

theorem HasDerivWithinAt.congr_of_mem (h : HasDerivWithinAt f f' s x) (hs : âˆ€ x âˆˆ s, fâ‚ x = f x)
    (hx : x âˆˆ s) : HasDerivWithinAt fâ‚ f' s x :=
  h.congr hs (hs _ hx)

theorem HasDerivWithinAt.congr_of_eventuallyEq (h : HasDerivWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : HasDerivWithinAt fâ‚ f' s x :=
  HasDerivAtFilter.congr_of_eventuallyEq h hâ‚ hx

theorem Filter.EventuallyEq.hasDerivWithinAt_iff (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    HasDerivWithinAt fâ‚ f' s x â†” HasDerivWithinAt f f' s x :=
  âŸ¨fun h' â†¦ h'.congr_of_eventuallyEq hâ‚.symm hx.symm, fun h' â†¦ h'.congr_of_eventuallyEq hâ‚ hxâŸ©

theorem HasDerivWithinAt.congr_of_eventuallyEq_of_mem (h : HasDerivWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : HasDerivWithinAt fâ‚ f' s x :=
  h.congr_of_eventuallyEq hâ‚ (hâ‚.eq_of_nhdsWithin hx)

theorem Filter.EventuallyEq.hasDerivWithinAt_iff_of_mem (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) :
    HasDerivWithinAt fâ‚ f' s x â†” HasDerivWithinAt f f' s x :=
  âŸ¨fun h' â†¦ h'.congr_of_eventuallyEq_of_mem hâ‚.symm hx,
  fun h' â†¦ h'.congr_of_eventuallyEq_of_mem hâ‚ hxâŸ©

theorem HasStrictDerivAt.congr_deriv (h : HasStrictDerivAt f f' x) (h' : f' = g') :
    HasStrictDerivAt f g' x :=
  h.congr_fderiv <| congr_arg _ h'

theorem HasDerivAt.congr_deriv (h : HasDerivAt f f' x) (h' : f' = g') : HasDerivAt f g' x :=
  HasFDerivAt.congr_fderiv h <| congr_arg _ h'

theorem HasDerivWithinAt.congr_deriv (h : HasDerivWithinAt f f' s x) (h' : f' = g') :
    HasDerivWithinAt f g' s x :=
  HasFDerivWithinAt.congr_fderiv h <| congr_arg _ h'

theorem HasDerivAt.congr_of_eventuallyEq (h : HasDerivAt f f' x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    HasDerivAt fâ‚ f' x :=
  HasDerivAtFilter.congr_of_eventuallyEq h hâ‚ (mem_of_mem_nhds hâ‚ :)

theorem Filter.EventuallyEq.hasDerivAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) :
    HasDerivAt fâ‚€ f' x â†” HasDerivAt fâ‚ f' x :=
  âŸ¨fun h' â†¦ h'.congr_of_eventuallyEq h.symm, fun h' â†¦ h'.congr_of_eventuallyEq hâŸ©

theorem Filter.EventuallyEq.derivWithin_eq (hs : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    derivWithin fâ‚ s x = derivWithin f s x := by
  unfold derivWithin
  rw [hs.fderivWithin_eq hx]

theorem Filter.EventuallyEq.derivWithin_eq_of_mem (hs : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) :
    derivWithin fâ‚ s x = derivWithin f s x :=
  hs.derivWithin_eq <| hs.self_of_nhdsWithin hx

theorem Filter.EventuallyEq.derivWithin_eq_of_nhds (hs : fâ‚ =á¶ [ğ“ x] f) :
    derivWithin fâ‚ s x = derivWithin f s x :=
  (hs.filter_mono nhdsWithin_le_nhds).derivWithin_eq hs.self_of_nhds

theorem derivWithin_congr (hs : EqOn fâ‚ f s) (hx : fâ‚ x = f x) :
    derivWithin fâ‚ s x = derivWithin f s x := by
  unfold derivWithin
  rw [fderivWithin_congr hs hx]

theorem Filter.EventuallyEq.deriv_eq (hL : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ x = deriv f x := by
  unfold deriv
  rwa [Filter.EventuallyEq.fderiv_eq]

protected theorem Filter.EventuallyEq.deriv (h : fâ‚ =á¶ [ğ“ x] f) : deriv fâ‚ =á¶ [ğ“ x] deriv f :=
  h.eventuallyEq_nhds.mono fun _ h => h.deriv_eq

end congr

section id

/-! ### Derivative of the identity -/

variable (s x L)

theorem hasDerivAtFilter_id : HasDerivAtFilter id 1 x L :=
  (hasFDerivAtFilter_id x L).hasDerivAtFilter

theorem hasDerivWithinAt_id : HasDerivWithinAt id 1 s x :=
  hasDerivAtFilter_id _ _

theorem hasDerivAt_id : HasDerivAt id 1 x :=
  hasDerivAtFilter_id _ _

theorem hasDerivAt_id' : HasDerivAt (fun x : ğ•œ => x) 1 x :=
  hasDerivAtFilter_id _ _

theorem hasStrictDerivAt_id : HasStrictDerivAt id 1 x :=
  (hasStrictFDerivAt_id x).hasStrictDerivAt

theorem deriv_id : deriv id x = 1 :=
  HasDerivAt.deriv (hasDerivAt_id x)

@[simp]
theorem deriv_id' : deriv (@id ğ•œ) = fun _ => 1 :=
  funext deriv_id

/-- Variant with `fun x => x` rather than `id` -/
@[simp]
theorem deriv_id'' : (deriv fun x : ğ•œ => x) = fun _ => 1 :=
  deriv_id'

theorem derivWithin_id (hxs : UniqueDiffWithinAt ğ•œ s x) : derivWithin id s x = 1 :=
  (hasDerivWithinAt_id x s).derivWithin hxs

/-- Variant with `fun x => x` rather than `id` -/
theorem derivWithin_id' (hxs : UniqueDiffWithinAt ğ•œ s x) : derivWithin (fun x => x) s x = 1 :=
  derivWithin_id x s hxs

end id

section Const

/-! ### Derivative of constant functions

This include the constant functions `0`, `1`, `Nat.cast n`, `Int.cast z`, and other numerals.
-/

variable (c : F) (s x L)

theorem hasDerivAtFilter_const : HasDerivAtFilter (fun _ => c) 0 x L :=
  (hasFDerivAtFilter_const c x L).hasDerivAtFilter

theorem hasDerivAtFilter_zero : HasDerivAtFilter (0 : ğ•œ â†’ F) 0 x L :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivAtFilter_one [One F] : HasDerivAtFilter (1 : ğ•œ â†’ F) 0 x L :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivAtFilter_natCast [NatCast F] (n : â„•) : HasDerivAtFilter (n : ğ•œ â†’ F) 0 x L :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivAtFilter_intCast [IntCast F] (z : â„¤) : HasDerivAtFilter (z : ğ•œ â†’ F) 0 x L :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivAtFilter_ofNat (n : â„•) [OfNat F n] : HasDerivAtFilter (ofNat(n) : ğ•œ â†’ F) 0 x L :=
  hasDerivAtFilter_const _ _ _

theorem hasStrictDerivAt_const : HasStrictDerivAt (fun _ => c) 0 x :=
  (hasStrictFDerivAt_const c x).hasStrictDerivAt

theorem hasStrictDerivAt_zero : HasStrictDerivAt (0 : ğ•œ â†’ F) 0 x :=
  hasStrictDerivAt_const _ _

theorem hasStrictDerivAt_one [One F] : HasStrictDerivAt (1 : ğ•œ â†’ F) 0 x :=
  hasStrictDerivAt_const _ _

theorem hasStrictDerivAt_natCast [NatCast F] (n : â„•) : HasStrictDerivAt (n : ğ•œ â†’ F) 0 x :=
  hasStrictDerivAt_const _ _

theorem hasStrictDerivAt_intCast [IntCast F] (z : â„¤) : HasStrictDerivAt (z : ğ•œ â†’ F) 0 x :=
  hasStrictDerivAt_const _ _

theorem HasStrictDerivAt_ofNat (n : â„•) [OfNat F n] : HasStrictDerivAt (ofNat(n) : ğ•œ â†’ F) 0 x :=
  hasStrictDerivAt_const _ _

theorem hasDerivWithinAt_const : HasDerivWithinAt (fun _ => c) 0 s x :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivWithinAt_zero : HasDerivWithinAt (0 : ğ•œ â†’ F) 0 s x :=
  hasDerivAtFilter_zero _ _

theorem hasDerivWithinAt_one [One F] : HasDerivWithinAt (1 : ğ•œ â†’ F) 0 s x :=
  hasDerivWithinAt_const _ _ _

theorem hasDerivWithinAt_natCast [NatCast F] (n : â„•) : HasDerivWithinAt (n : ğ•œ â†’ F) 0 s x :=
  hasDerivWithinAt_const _ _ _

theorem hasDerivWithinAt_intCast [IntCast F] (z : â„¤) : HasDerivWithinAt (z : ğ•œ â†’ F) 0 s x :=
  hasDerivWithinAt_const _ _ _

theorem hasDerivWithinAt_ofNat (n : â„•) [OfNat F n] : HasDerivWithinAt (ofNat(n) : ğ•œ â†’ F) 0 s x :=
  hasDerivWithinAt_const _ _ _

theorem hasDerivAt_const : HasDerivAt (fun _ => c) 0 x :=
  hasDerivAtFilter_const _ _ _

theorem hasDerivAt_zero : HasDerivAt (0 : ğ•œ â†’ F) 0 x :=
  hasDerivAtFilter_zero _ _

theorem hasDerivAt_one [One F] : HasDerivAt (1 : ğ•œ â†’ F) 0 x :=
  hasDerivAt_const _ _

theorem hasDerivAt_natCast [NatCast F] (n : â„•) : HasDerivAt (n : ğ•œ â†’ F) 0 x :=
  hasDerivAt_const _ _

theorem hasDerivAt_intCast [IntCast F] (z : â„¤) : HasDerivAt (z : ğ•œ â†’ F) 0 x :=
  hasDerivAt_const _ _

theorem hasDerivAt_ofNat (n : â„•) [OfNat F n] : HasDerivAt (ofNat(n) : ğ•œ â†’ F) 0 x :=
  hasDerivAt_const _ _

theorem deriv_const : deriv (fun _ => c) x = 0 :=
  HasDerivAt.deriv (hasDerivAt_const x c)

@[simp]
theorem deriv_const' : (deriv fun _ : ğ•œ => c) = fun _ => 0 :=
  funext fun x => deriv_const x c

@[simp]
theorem deriv_zero : deriv (0 : ğ•œ â†’ F) = 0 := funext fun _ => deriv_const _ _

@[simp]
theorem deriv_one [One F] : deriv (1 : ğ•œ â†’ F) = 0 := funext fun _ => deriv_const _ _

@[simp]
theorem deriv_natCast [NatCast F] (n : â„•) : deriv (n : ğ•œ â†’ F) = 0 := funext fun _ => deriv_const _ _

@[simp]
theorem deriv_intCast [IntCast F] (z : â„¤) : deriv (z : ğ•œ â†’ F) = 0 := funext fun _ => deriv_const _ _

@[simp low]
theorem deriv_ofNat (n : â„•) [OfNat F n] : deriv (ofNat(n) : ğ•œ â†’ F) = 0 :=
  funext fun _ => deriv_const _ _

@[simp]
theorem derivWithin_fun_const : derivWithin (fun _ => c) s = 0 := by
  ext; simp [derivWithin]

@[simp]
theorem derivWithin_const : derivWithin (Function.const ğ•œ c) s = 0 :=
  derivWithin_fun_const _ _

@[simp]
theorem derivWithin_zero : derivWithin (0 : ğ•œ â†’ F) s = 0 := derivWithin_const _ _

@[simp]
theorem derivWithin_one [One F] : derivWithin (1 : ğ•œ â†’ F) s = 0 := derivWithin_const _ _

@[simp]
theorem derivWithin_natCast [NatCast F] (n : â„•) : derivWithin (n : ğ•œ â†’ F) s = 0 :=
  derivWithin_const _ _

@[simp]
theorem derivWithin_intCast [IntCast F] (z : â„¤) : derivWithin (z : ğ•œ â†’ F) s = 0 :=
  derivWithin_const _ _

@[simp low]
theorem derivWithin_ofNat (n : â„•) [OfNat F n] : derivWithin (ofNat(n) : ğ•œ â†’ F) s = 0 :=
  derivWithin_const _ _

end Const

section Continuous

/-! ### Continuity of a function admitting a derivative -/

nonrec theorem HasDerivAtFilter.tendsto_nhds (hL : L â‰¤ ğ“ x) (h : HasDerivAtFilter f f' x L) :
    Tendsto f L (ğ“ (f x)) :=
  h.tendsto_nhds hL

theorem HasDerivWithinAt.continuousWithinAt (h : HasDerivWithinAt f f' s x) :
    ContinuousWithinAt f s x :=
  HasDerivAtFilter.tendsto_nhds inf_le_left h

theorem HasDerivAt.continuousAt (h : HasDerivAt f f' x) : ContinuousAt f x :=
  HasDerivAtFilter.tendsto_nhds le_rfl h

protected theorem HasDerivAt.continuousOn {f f' : ğ•œ â†’ F} (hderiv : âˆ€ x âˆˆ s, HasDerivAt f (f' x) x) :
    ContinuousOn f s := fun x hx => (hderiv x hx).continuousAt.continuousWithinAt

end Continuous

section MeanValue

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. This version
only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a neighborhood of `x`. -/
theorem HasDerivAt.le_of_lip' {f : ğ•œ â†’ F} {f' : F} {xâ‚€ : ğ•œ} (hf : HasDerivAt f f' xâ‚€)
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) :
    â€–f'â€– â‰¤ C := by
  simpa using HasFDerivAt.le_of_lip' hf.hasFDerivAt hCâ‚€ hlip

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. -/
theorem HasDerivAt.le_of_lipschitzOn {f : ğ•œ â†’ F} {f' : F} {xâ‚€ : ğ•œ} (hf : HasDerivAt f f' xâ‚€)
    {s : Set ğ•œ} (hs : s âˆˆ ğ“ xâ‚€) {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) : â€–f'â€– â‰¤ C := by
  simpa using HasFDerivAt.le_of_lipschitzOn hf.hasFDerivAt hs hlip

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
then its derivative at `xâ‚€` has norm bounded by `C`. -/
theorem HasDerivAt.le_of_lipschitz {f : ğ•œ â†’ F} {f' : F} {xâ‚€ : ğ•œ} (hf : HasDerivAt f f' xâ‚€)
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–f'â€– â‰¤ C := by
  simpa using HasFDerivAt.le_of_lipschitz hf.hasFDerivAt hlip

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. This version
only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a neighborhood of `x`. -/
theorem norm_deriv_le_of_lip' {f : ğ•œ â†’ F} {xâ‚€ : ğ•œ}
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) :
    â€–deriv f xâ‚€â€– â‰¤ C := by
  simpa [norm_deriv_eq_norm_fderiv] using norm_fderiv_le_of_lip' ğ•œ hCâ‚€ hlip

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`.
Version using `deriv`. -/
theorem norm_deriv_le_of_lipschitzOn {f : ğ•œ â†’ F} {xâ‚€ : ğ•œ} {s : Set ğ•œ} (hs : s âˆˆ ğ“ xâ‚€)
    {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) : â€–deriv f xâ‚€â€– â‰¤ C := by
  simpa [norm_deriv_eq_norm_fderiv] using norm_fderiv_le_of_lipschitzOn ğ•œ hs hlip

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz then
its derivative at `xâ‚€` has norm bounded by `C`.
Version using `deriv`. -/
theorem norm_deriv_le_of_lipschitz {f : ğ•œ â†’ F} {xâ‚€ : ğ•œ}
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–deriv f xâ‚€â€– â‰¤ C := by
  simpa [norm_deriv_eq_norm_fderiv] using norm_fderiv_le_of_lipschitz ğ•œ hlip

end MeanValue

section Semilinear

variable {Ïƒ Ïƒ' : RingHom ğ•œ ğ•œ} [RingHomIsometric Ïƒ] [RingHomInvPair Ïƒ Ïƒ']
  {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ğ•œ F'] (L : F â†’SL[Ïƒ] F')

variable (Ïƒ')

/-- If `L` is a `Ïƒ`-semilinear map, and `f` has FrÃ©chet derivative `f'` at `x`, then `L âˆ˜ f âˆ˜ Ïƒâ»Â¹`
has FrÃ©chet derivative `L âˆ˜ f'` at `Ïƒ x`. -/
lemma HasDerivAt.comp_semilinear (hf : HasDerivAt f f' x) :
    HasDerivAt (L âˆ˜ f âˆ˜ Ïƒ') (L f') (Ïƒ x) := by
  have : RingHomIsometric Ïƒ' := .inv Ïƒ
  let R : ğ•œ â†’SL[Ïƒ'] ğ•œ := âŸ¨Ïƒ'.toSemilinearMap, Ïƒ'.isometry.continuousâŸ©
  have hR (k : ğ•œ) : R k = Ïƒ' k := rfl
  rw [hasDerivAt_iff_hasFDerivAt]
  convert HasFDerivAt.comp_semilinear L R (f' := (1 : ğ•œ â†’L[ğ•œ] ğ•œ).smulRight f') ?_
  Â· ext
    simp [R]
  Â· rwa [â† hasDerivAt_iff_hasFDerivAt, hR, RingHomInvPair.comp_apply_eq]

/-- If `f` is differentiable at `x`, and `L` is `Ïƒ`-semilinear, then `L âˆ˜ f âˆ˜ Ïƒâ»Â¹` is
differentiable at `Ïƒ x`. -/
lemma DifferentiableAt.comp_semilinearâ‚ (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (L âˆ˜ f âˆ˜ Ïƒ') (Ïƒ x) :=
  (hf.hasDerivAt.comp_semilinear Ïƒ' L).differentiableAt

variable (Ïƒ) {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ}

/-- If `f` has derivative `f'` at `x`, and `Ïƒ, Ïƒ'` are mutually inverse normed-ring automorphisms,
then `Ïƒ âˆ˜ f âˆ˜ Ïƒ'` has derivative `Ïƒ f'` at `Ïƒ x`. -/
lemma HasDerivAt.comp_ringHom (hf : HasDerivAt f f' x) : HasDerivAt (Ïƒ âˆ˜ f âˆ˜ Ïƒ') (Ïƒ  f') (Ïƒ x) :=
  hf.comp_semilinear Ïƒ' âŸ¨Ïƒ.toSemilinearMap, Ïƒ.isometry.continuousâŸ©

/-- If `f` is differentiable at `x`, and `L` is `Ïƒ`-semilinear, then `L âˆ˜ f âˆ˜ Ïƒâ»Â¹` is
differentiable at `Ïƒ x`. -/
lemma DifferentiableAt.comp_ringHom (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (Ïƒ âˆ˜ f âˆ˜ Ïƒ') (Ïƒ x) :=
  (hf.hasDerivAt.comp_ringHom Ïƒ Ïƒ').differentiableAt

end Semilinear
