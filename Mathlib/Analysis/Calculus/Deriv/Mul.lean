/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, Anatole Dedecker, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Add

/-!
# Derivative of `f x * g x`

In this file we prove formulas for `(f x * g x)'` and `(f x â€¢ g x)'`.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative, multiplication
-/

universe u v w

noncomputable section

open scoped Topology Filter ENNReal

open Filter Asymptotics Set

open ContinuousLinearMap (smulRight smulRight_one_eq_iff)

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {E : Type w} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {f : ğ•œ â†’ F}
variable {f' : F}
variable {x : ğ•œ}
variable {s : Set ğ•œ}
variable {L : Filter ğ•œ}

/-! ### Derivative of bilinear maps -/

namespace ContinuousLinearMap

variable {B : E â†’L[ğ•œ] F â†’L[ğ•œ] G} {u : ğ•œ â†’ E} {v : ğ•œ â†’ F} {u' : E} {v' : F}

theorem hasDerivWithinAt_of_bilinear
    (hu : HasDerivWithinAt u u' s x) (hv : HasDerivWithinAt v v' s x) :
    HasDerivWithinAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) s x := by
  simpa using (B.hasFDerivWithinAt_of_bilinear
    hu.hasFDerivWithinAt hv.hasFDerivWithinAt).hasDerivWithinAt

theorem hasDerivAt_of_bilinear (hu : HasDerivAt u u' x) (hv : HasDerivAt v v' x) :
    HasDerivAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) x := by
  simpa using (B.hasFDerivAt_of_bilinear hu.hasFDerivAt hv.hasFDerivAt).hasDerivAt

theorem hasStrictDerivAt_of_bilinear (hu : HasStrictDerivAt u u' x) (hv : HasStrictDerivAt v v' x) :
    HasStrictDerivAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) x := by
  simpa using
    (B.hasStrictFDerivAt_of_bilinear hu.hasStrictFDerivAt hv.hasStrictFDerivAt).hasStrictDerivAt

theorem derivWithin_of_bilinear
    (hu : DifferentiableWithinAt ğ•œ u s x) (hv : DifferentiableWithinAt ğ•œ v s x) :
    derivWithin (fun y => B (u y) (v y)) s x =
      B (u x) (derivWithin v s x) + B (derivWithin u s x) (v x) := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (B.hasDerivWithinAt_of_bilinear hu.hasDerivWithinAt hv.hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_of_bilinear (hu : DifferentiableAt ğ•œ u x) (hv : DifferentiableAt ğ•œ v x) :
    deriv (fun y => B (u y) (v y)) x = B (u x) (deriv v x) + B (deriv u x) (v x) :=
  (B.hasDerivAt_of_bilinear hu.hasDerivAt hv.hasDerivAt).deriv

end ContinuousLinearMap

section SMul

/-! ### Derivative of the multiplication of a scalar function and a vector function -/


variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' F]
  [IsScalarTower ğ•œ ğ•œ' F] {c : ğ•œ â†’ ğ•œ'} {c' : ğ•œ'}

theorem HasDerivWithinAt.smul (hc : HasDerivWithinAt c c' s x) (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) s x := by
  simpa using (HasFDerivWithinAt.smul hc hf).hasDerivWithinAt

theorem HasDerivAt.smul (hc : HasDerivAt c c' x) (hf : HasDerivAt f f' x) :
    HasDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.smul hf

nonrec theorem HasStrictDerivAt.smul (hc : HasStrictDerivAt c c' x) (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x := by
  simpa using (hc.smul hf).hasStrictDerivAt

theorem derivWithin_smul (hc : DifferentiableWithinAt ğ•œ c s x)
    (hf : DifferentiableWithinAt ğ•œ f s x) :
    derivWithin (fun y => c y â€¢ f y) s x = c x â€¢ derivWithin f s x + derivWithin c s x â€¢ f x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.smul hf.hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_smul (hc : DifferentiableAt ğ•œ c x) (hf : DifferentiableAt ğ•œ f x) :
    deriv (fun y => c y â€¢ f y) x = c x â€¢ deriv f x + deriv c x â€¢ f x :=
  (hc.hasDerivAt.smul hf.hasDerivAt).deriv

theorem HasStrictDerivAt.smul_const (hc : HasStrictDerivAt c c' x) (f : F) :
    HasStrictDerivAt (fun y => c y â€¢ f) (c' â€¢ f) x := by
  have := hc.smul (hasStrictDerivAt_const x f)
  rwa [smul_zero, zero_add] at this

theorem HasDerivWithinAt.smul_const (hc : HasDerivWithinAt c c' s x) (f : F) :
    HasDerivWithinAt (fun y => c y â€¢ f) (c' â€¢ f) s x := by
  have := hc.smul (hasDerivWithinAt_const x s f)
  rwa [smul_zero, zero_add] at this

theorem HasDerivAt.smul_const (hc : HasDerivAt c c' x) (f : F) :
    HasDerivAt (fun y => c y â€¢ f) (c' â€¢ f) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.smul_const f

theorem derivWithin_smul_const (hc : DifferentiableWithinAt ğ•œ c s x) (f : F) :
    derivWithin (fun y => c y â€¢ f) s x = derivWithin c s x â€¢ f := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.smul_const f).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_smul_const (hc : DifferentiableAt ğ•œ c x) (f : F) :
    deriv (fun y => c y â€¢ f) x = deriv c x â€¢ f :=
  (hc.hasDerivAt.smul_const f).deriv

end SMul

section ConstSMul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F]

nonrec theorem HasStrictDerivAt.const_smul (c : R) (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun y => c â€¢ f y) (c â€¢ f') x := by
  simpa using (hf.const_smul c).hasStrictDerivAt

nonrec theorem HasDerivAtFilter.const_smul (c : R) (hf : HasDerivAtFilter f f' x L) :
    HasDerivAtFilter (fun y => c â€¢ f y) (c â€¢ f') x L := by
  simpa using (hf.const_smul c).hasDerivAtFilter

nonrec theorem HasDerivWithinAt.const_smul (c : R) (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun y => c â€¢ f y) (c â€¢ f') s x :=
  hf.const_smul c

nonrec theorem HasDerivAt.const_smul (c : R) (hf : HasDerivAt f f' x) :
    HasDerivAt (fun y => c â€¢ f y) (c â€¢ f') x :=
  hf.const_smul c

theorem derivWithin_const_smul (c : R) (hf : DifferentiableWithinAt ğ•œ f s x) :
    derivWithin (fun y => c â€¢ f y) s x = c â€¢ derivWithin f s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hf.hasDerivWithinAt.const_smul c).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

/-- A variant of `derivWithin_const_smul` without differentiability assumption when the scalar
multiplication is by field elements. -/
lemma derivWithin_const_smul' {f : ğ•œ â†’ F} {x : ğ•œ} {R : Type*} [Field R] [Module R F]
    [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F] (c : R) :
    derivWithin (fun y â†¦ c â€¢ f y) s x = c â€¢ derivWithin f s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· simp [â† fderivWithin_derivWithin, â† Pi.smul_def, fderivWithin_const_smul_field c hsx]
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_const_smul (c : R) (hf : DifferentiableAt ğ•œ f x) :
    deriv (fun y => c â€¢ f y) x = c â€¢ deriv f x :=
  (hf.hasDerivAt.const_smul c).deriv

/-- A variant of `deriv_const_smul` without differentiability assumption when the scalar
multiplication is by field elements. -/
lemma deriv_const_smul' {f : ğ•œ â†’ F} {x : ğ•œ} {R : Type*} [Field R] [Module R F] [SMulCommClass ğ•œ R F]
    [ContinuousConstSMul R F] (c : R) :
    deriv (fun y â†¦ c â€¢ f y) x = c â€¢ deriv f x := by
  simp only [â† derivWithin_univ, derivWithin_const_smul']

end ConstSMul

section Mul

/-! ### Derivative of the multiplication of two functions -/


variable {ğ•œ' ğ”¸ : Type*} [NormedField ğ•œ'] [NormedRing ğ”¸] [NormedAlgebra ğ•œ ğ•œ'] [NormedAlgebra ğ•œ ğ”¸]
  {c d : ğ•œ â†’ ğ”¸} {c' d' : ğ”¸} {u v : ğ•œ â†’ ğ•œ'}

theorem HasDerivWithinAt.mul (hc : HasDerivWithinAt c c' s x) (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => c y * d y) (c' * d x + c x * d') s x := by
  have := (HasFDerivWithinAt.mul' hc hd).hasDerivWithinAt
  rwa [ContinuousLinearMap.add_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, one_smul, one_smul,
    add_comm] at this

theorem HasDerivAt.mul (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => c y * d y) (c' * d x + c x * d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.mul hd

theorem HasStrictDerivAt.mul (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => c y * d y) (c' * d x + c x * d') x := by
  have := (HasStrictFDerivAt.mul' hc hd).hasStrictDerivAt
  rwa [ContinuousLinearMap.add_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, one_smul, one_smul,
    add_comm] at this

theorem derivWithin_mul (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) :
    derivWithin (fun y => c y * d y) s x = derivWithin c s x * d x + c x * derivWithin d s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.mul hd.hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

@[simp]
theorem deriv_mul (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    deriv (fun y => c y * d y) x = deriv c x * d x + c x * deriv d x :=
  (hc.hasDerivAt.mul hd.hasDerivAt).deriv

theorem HasDerivWithinAt.mul_const (hc : HasDerivWithinAt c c' s x) (d : ğ”¸) :
    HasDerivWithinAt (fun y => c y * d) (c' * d) s x := by
  convert hc.mul (hasDerivWithinAt_const x s d) using 1
  rw [mul_zero, add_zero]

theorem HasDerivAt.mul_const (hc : HasDerivAt c c' x) (d : ğ”¸) :
    HasDerivAt (fun y => c y * d) (c' * d) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.mul_const d

theorem hasDerivAt_mul_const (c : ğ•œ) : HasDerivAt (fun x => x * c) c x := by
  simpa only [one_mul] using (hasDerivAt_id' x).mul_const c

theorem HasStrictDerivAt.mul_const (hc : HasStrictDerivAt c c' x) (d : ğ”¸) :
    HasStrictDerivAt (fun y => c y * d) (c' * d) x := by
  convert hc.mul (hasStrictDerivAt_const x d) using 1
  rw [mul_zero, add_zero]

theorem derivWithin_mul_const (hc : DifferentiableWithinAt ğ•œ c s x) (d : ğ”¸) :
    derivWithin (fun y => c y * d) s x = derivWithin c s x * d := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.mul_const d).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

lemma derivWithin_mul_const_field (u : ğ•œ') :
    derivWithin (fun y => v y * u) s x = derivWithin v s x * u := by
  by_cases hv : DifferentiableWithinAt ğ•œ v s x
  Â· rw [derivWithin_mul_const hv u]
  by_cases hu : u = 0
  Â· simp [hu]
  rw [derivWithin_zero_of_not_differentiableWithinAt hv, zero_mul,
      derivWithin_zero_of_not_differentiableWithinAt]
  have : v = fun x â†¦ (v x * u) * uâ»Â¹ := by ext; simp [hu]
  exact fun h_diff â†¦ hv <| this â–¸ h_diff.mul_const _

theorem deriv_mul_const (hc : DifferentiableAt ğ•œ c x) (d : ğ”¸) :
    deriv (fun y => c y * d) x = deriv c x * d :=
  (hc.hasDerivAt.mul_const d).deriv

theorem deriv_mul_const_field (v : ğ•œ') : deriv (fun y => u y * v) x = deriv u x * v := by
  by_cases hu : DifferentiableAt ğ•œ u x
  Â· exact deriv_mul_const hu v
  Â· rw [deriv_zero_of_not_differentiableAt hu, zero_mul]
    rcases eq_or_ne v 0 with (rfl | hd)
    Â· simp only [mul_zero, deriv_const]
    Â· refine deriv_zero_of_not_differentiableAt (mt (fun H => ?_) hu)
      simpa only [mul_inv_cancel_rightâ‚€ hd] using H.mul_const vâ»Â¹

@[simp]
theorem deriv_mul_const_field' (v : ğ•œ') : (deriv fun x => u x * v) = fun x => deriv u x * v :=
  funext fun _ => deriv_mul_const_field v

theorem HasDerivWithinAt.const_mul (c : ğ”¸) (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => c * d y) (c * d') s x := by
  convert (hasDerivWithinAt_const x s c).mul hd using 1
  rw [zero_mul, zero_add]

theorem HasDerivAt.const_mul (c : ğ”¸) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => c * d y) (c * d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hd.const_mul c

theorem HasStrictDerivAt.const_mul (c : ğ”¸) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => c * d y) (c * d') x := by
  convert (hasStrictDerivAt_const _ _).mul hd using 1
  rw [zero_mul, zero_add]

theorem derivWithin_const_mul (c : ğ”¸) (hd : DifferentiableWithinAt ğ•œ d s x) :
    derivWithin (fun y => c * d y) s x = c * derivWithin d s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hd.hasDerivWithinAt.const_mul c).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

lemma derivWithin_const_mul_field (u : ğ•œ') :
    derivWithin (fun y => u * v y) s x = u * derivWithin v s x := by
  simp_rw [mul_comm u]
  exact derivWithin_mul_const_field u

theorem deriv_const_mul (c : ğ”¸) (hd : DifferentiableAt ğ•œ d x) :
    deriv (fun y => c * d y) x = c * deriv d x :=
  (hd.hasDerivAt.const_mul c).deriv

theorem deriv_const_mul_field (u : ğ•œ') : deriv (fun y => u * v y) x = u * deriv v x := by
  simp only [mul_comm u, deriv_mul_const_field]

@[simp]
theorem deriv_const_mul_field' (u : ğ•œ') : (deriv fun x => u * v x) = fun x => u * deriv v x :=
  funext fun _ => deriv_const_mul_field u

end Mul

section Prod

section HasDeriv

variable {Î¹ : Type*} [DecidableEq Î¹] {ğ”¸' : Type*} [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸']
  {u : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ”¸'} {f' : Î¹ â†’ ğ”¸'}

theorem HasDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivAt (f i) (f' i) x) :
    HasDerivAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivAt)).hasDerivAt

theorem HasDerivWithinAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivWithinAt (f i) (f' i) s x) :
    HasDerivWithinAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) s x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivWithinAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivWithinAt)).hasDerivWithinAt

theorem HasStrictDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasStrictDerivAt (f i) (f' i) x) :
    HasStrictDerivAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasStrictFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasStrictFDerivAt)).hasStrictDerivAt

theorem deriv_finset_prod (hf : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (f i) x) :
    deriv (âˆ i âˆˆ u, f i Â·) x = âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ deriv (f i) x :=
  (HasDerivAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivAt).deriv

theorem derivWithin_finset_prod
    (hf : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (f i) s x) :
    derivWithin (âˆ i âˆˆ u, f i Â·) s x =
      âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ derivWithin (f i) s x := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (HasDerivWithinAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

end HasDeriv

variable {Î¹ : Type*} {ğ”¸' : Type*} [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸']
  {u : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ”¸'}

@[fun_prop]
theorem DifferentiableAt.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (f i) x) :
    DifferentiableAt ğ•œ (âˆ i âˆˆ u, f i Â·) x := by
  classical
  exact
    (HasDerivAt.finset_prod (fun i hi â†¦ DifferentiableAt.hasDerivAt (hd i hi))).differentiableAt

@[fun_prop]
theorem DifferentiableWithinAt.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (f i) s x) :
    DifferentiableWithinAt ğ•œ (âˆ i âˆˆ u, f i Â·) s x := by
  classical
  exact (HasDerivWithinAt.finset_prod (fun i hi â†¦
    DifferentiableWithinAt.hasDerivWithinAt (hd i hi))).differentiableWithinAt

@[fun_prop]
theorem DifferentiableOn.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableOn ğ•œ (f i) s) :
    DifferentiableOn ğ•œ (âˆ i âˆˆ u, f i Â·) s :=
  fun x hx â†¦ .finset_prod (fun i hi â†¦ hd i hi x hx)

@[fun_prop]
theorem Differentiable.finset_prod (hd : âˆ€ i âˆˆ u, Differentiable ğ•œ (f i)) :
    Differentiable ğ•œ (âˆ i âˆˆ u, f i Â·) :=
  fun x â†¦ .finset_prod (fun i hi â†¦ hd i hi x)

end Prod

section Div

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {c : ğ•œ â†’ ğ•œ'} {c' : ğ•œ'}

theorem HasDerivAt.div_const (hc : HasDerivAt c c' x) (d : ğ•œ') :
    HasDerivAt (fun x => c x / d) (c' / d) x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

theorem HasDerivWithinAt.div_const (hc : HasDerivWithinAt c c' s x) (d : ğ•œ') :
    HasDerivWithinAt (fun x => c x / d) (c' / d) s x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

theorem HasStrictDerivAt.div_const (hc : HasStrictDerivAt c c' x) (d : ğ•œ') :
    HasStrictDerivAt (fun x => c x / d) (c' / d) x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹

@[fun_prop]
theorem DifferentiableWithinAt.div_const (hc : DifferentiableWithinAt ğ•œ c s x) (d : ğ•œ') :
    DifferentiableWithinAt ğ•œ (fun x => c x / d) s x :=
  (hc.hasDerivWithinAt.div_const _).differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.div_const (hc : DifferentiableAt ğ•œ c x) (d : ğ•œ') :
    DifferentiableAt ğ•œ (fun x => c x / d) x :=
  (hc.hasDerivAt.div_const _).differentiableAt

@[fun_prop]
theorem DifferentiableOn.div_const (hc : DifferentiableOn ğ•œ c s) (d : ğ•œ') :
    DifferentiableOn ğ•œ (fun x => c x / d) s := fun x hx => (hc x hx).div_const d

@[simp, fun_prop]
theorem Differentiable.div_const (hc : Differentiable ğ•œ c) (d : ğ•œ') :
    Differentiable ğ•œ fun x => c x / d := fun x => (hc x).div_const d

theorem derivWithin_div_const (hc : DifferentiableWithinAt ğ•œ c s x) (d : ğ•œ') :
    derivWithin (fun x => c x / d) s x = derivWithin c s x / d := by
  simp [div_eq_inv_mul, derivWithin_const_mul, hc]

@[simp]
theorem deriv_div_const (d : ğ•œ') : deriv (fun x => c x / d) x = deriv c x / d := by
  simp only [div_eq_mul_inv, deriv_mul_const_field]

end Div

section CLMCompApply

/-! ### Derivative of the pointwise composition/application of continuous linear maps -/


open ContinuousLinearMap

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] {c : ğ•œ â†’ F â†’L[ğ•œ] G} {c' : F â†’L[ğ•œ] G}
  {d : ğ•œ â†’ E â†’L[ğ•œ] F} {d' : E â†’L[ğ•œ] F} {u : ğ•œ â†’ F} {u' : F}

theorem HasStrictDerivAt.clm_comp (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') x := by
  have := (hc.hasStrictFDerivAt.clm_comp hd.hasStrictFDerivAt).hasStrictDerivAt
  rwa [add_apply, comp_apply, comp_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this

theorem HasDerivWithinAt.clm_comp (hc : HasDerivWithinAt c c' s x)
    (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') s x := by
  have := (hc.hasFDerivWithinAt.clm_comp hd.hasFDerivWithinAt).hasDerivWithinAt
  rwa [add_apply, comp_apply, comp_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this

theorem HasDerivAt.clm_comp (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.clm_comp hd

theorem derivWithin_clm_comp (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) :
    derivWithin (fun y => (c y).comp (d y)) s x =
      (derivWithin c s x).comp (d x) + (c x).comp (derivWithin d s x) := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.clm_comp hd.hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_clm_comp (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    deriv (fun y => (c y).comp (d y)) x = (deriv c x).comp (d x) + (c x).comp (deriv d x) :=
  (hc.hasDerivAt.clm_comp hd.hasDerivAt).deriv

theorem HasStrictDerivAt.clm_apply (hc : HasStrictDerivAt c c' x) (hu : HasStrictDerivAt u u' x) :
    HasStrictDerivAt (fun y => (c y) (u y)) (c' (u x) + c x u') x := by
  have := (hc.hasStrictFDerivAt.clm_apply hu.hasStrictFDerivAt).hasStrictDerivAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this

theorem HasDerivWithinAt.clm_apply (hc : HasDerivWithinAt c c' s x)
    (hu : HasDerivWithinAt u u' s x) :
    HasDerivWithinAt (fun y => (c y) (u y)) (c' (u x) + c x u') s x := by
  have := (hc.hasFDerivWithinAt.clm_apply hu.hasFDerivWithinAt).hasDerivWithinAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this

theorem HasDerivAt.clm_apply (hc : HasDerivAt c c' x) (hu : HasDerivAt u u' x) :
    HasDerivAt (fun y => (c y) (u y)) (c' (u x) + c x u') x := by
  have := (hc.hasFDerivAt.clm_apply hu.hasFDerivAt).hasDerivAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this

theorem derivWithin_clm_apply (hc : DifferentiableWithinAt ğ•œ c s x)
    (hu : DifferentiableWithinAt ğ•œ u s x) :
    derivWithin (fun y => (c y) (u y)) s x = derivWithin c s x (u x) + c x (derivWithin u s x) := by
  by_cases hsx : UniqueDiffWithinAt ğ•œ s x
  Â· exact (hc.hasDerivWithinAt.clm_apply hu.hasDerivWithinAt).derivWithin hsx
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hsx]

theorem deriv_clm_apply (hc : DifferentiableAt ğ•œ c x) (hu : DifferentiableAt ğ•œ u x) :
    deriv (fun y => (c y) (u y)) x = deriv c x (u x) + c x (deriv u x) :=
  (hc.hasDerivAt.clm_apply hu.hasDerivAt).deriv

end CLMCompApply
