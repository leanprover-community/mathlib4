/-
Copyright (c) 2019 Gabriel Ebner. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Gabriel Ebner, Anatole Dedecker, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Add

#align_import analysis.calculus.deriv.mul from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Derivative of `f x * g x`

In this file we prove formulas for `(f x * g x)'` and `(f x â€¢ g x)'`.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative, multiplication
-/


universe u v w

noncomputable section

open scoped Classical Topology Filter ENNReal

open Filter Asymptotics Set

open ContinuousLinearMap (smulRight smulRight_one_eq_iff)

variable {ð•œ : Type u} [NontriviallyNormedField ð•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ð•œ F]
variable {E : Type w} [NormedAddCommGroup E] [NormedSpace ð•œ E]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ð•œ G]
variable {f fâ‚€ fâ‚ g : ð•œ â†’ F}
variable {f' fâ‚€' fâ‚' g' : F}
variable {x : ð•œ}
variable {s t : Set ð•œ}
variable {L Lâ‚ Lâ‚‚ : Filter ð•œ}

/-! ### Derivative of bilinear maps -/

namespace ContinuousLinearMap

variable {B : E â†’L[ð•œ] F â†’L[ð•œ] G} {u : ð•œ â†’ E} {v : ð•œ â†’ F} {u' : E} {v' : F}

theorem hasDerivWithinAt_of_bilinear
    (hu : HasDerivWithinAt u u' s x) (hv : HasDerivWithinAt v v' s x) :
    HasDerivWithinAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) s x := by
  simpa using (B.hasFDerivWithinAt_of_bilinear
    hu.hasFDerivWithinAt hv.hasFDerivWithinAt).hasDerivWithinAt

theorem hasDerivAt_of_bilinear (hu : HasDerivAt u u' x) (hv : HasDerivAt v v' x) :
    HasDerivAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) x := by
  simpa using (B.hasFDerivAt_of_bilinear hu.hasFDerivAt hv.hasFDerivAt).hasDerivAt

theorem hasStrictDerivAt_of_bilinear (hu : HasStrictDerivAt u u' x) (hv : HasStrictDerivAt v v' x) :
    HasStrictDerivAt (fun x â†¦ B (u x) (v x)) (B (u x) v' + B u' (v x)) x := by
  simpa using
    (B.hasStrictFDerivAt_of_bilinear hu.hasStrictFDerivAt hv.hasStrictFDerivAt).hasStrictDerivAt

theorem derivWithin_of_bilinear (hxs : UniqueDiffWithinAt ð•œ s x)
    (hu : DifferentiableWithinAt ð•œ u s x) (hv : DifferentiableWithinAt ð•œ v s x) :
    derivWithin (fun y => B (u y) (v y)) s x =
      B (u x) (derivWithin v s x) + B (derivWithin u s x) (v x) :=
  (B.hasDerivWithinAt_of_bilinear hu.hasDerivWithinAt hv.hasDerivWithinAt).derivWithin hxs

theorem deriv_of_bilinear (hu : DifferentiableAt ð•œ u x) (hv : DifferentiableAt ð•œ v x) :
    deriv (fun y => B (u y) (v y)) x = B (u x) (deriv v x) + B (deriv u x) (v x) :=
  (B.hasDerivAt_of_bilinear hu.hasDerivAt hv.hasDerivAt).deriv

end ContinuousLinearMap

section SMul

/-! ### Derivative of the multiplication of a scalar function and a vector function -/


variable {ð•œ' : Type*} [NontriviallyNormedField ð•œ'] [NormedAlgebra ð•œ ð•œ'] [NormedSpace ð•œ' F]
  [IsScalarTower ð•œ ð•œ' F] {c : ð•œ â†’ ð•œ'} {c' : ð•œ'}

theorem HasDerivWithinAt.smul (hc : HasDerivWithinAt c c' s x) (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) s x := by
  simpa using (HasFDerivWithinAt.smul hc hf).hasDerivWithinAt
#align has_deriv_within_at.smul HasDerivWithinAt.smul

theorem HasDerivAt.smul (hc : HasDerivAt c c' x) (hf : HasDerivAt f f' x) :
    HasDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.smul hf
#align has_deriv_at.smul HasDerivAt.smul

nonrec theorem HasStrictDerivAt.smul (hc : HasStrictDerivAt c c' x) (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c' â€¢ f x) x := by
  simpa using (hc.smul hf).hasStrictDerivAt
#align has_strict_deriv_at.smul HasStrictDerivAt.smul

theorem derivWithin_smul (hxs : UniqueDiffWithinAt ð•œ s x) (hc : DifferentiableWithinAt ð•œ c s x)
    (hf : DifferentiableWithinAt ð•œ f s x) :
    derivWithin (fun y => c y â€¢ f y) s x = c x â€¢ derivWithin f s x + derivWithin c s x â€¢ f x :=
  (hc.hasDerivWithinAt.smul hf.hasDerivWithinAt).derivWithin hxs
#align deriv_within_smul derivWithin_smul

theorem deriv_smul (hc : DifferentiableAt ð•œ c x) (hf : DifferentiableAt ð•œ f x) :
    deriv (fun y => c y â€¢ f y) x = c x â€¢ deriv f x + deriv c x â€¢ f x :=
  (hc.hasDerivAt.smul hf.hasDerivAt).deriv
#align deriv_smul deriv_smul

theorem HasStrictDerivAt.smul_const (hc : HasStrictDerivAt c c' x) (f : F) :
    HasStrictDerivAt (fun y => c y â€¢ f) (c' â€¢ f) x := by
  have := hc.smul (hasStrictDerivAt_const x f)
  rwa [smul_zero, zero_add] at this
#align has_strict_deriv_at.smul_const HasStrictDerivAt.smul_const

theorem HasDerivWithinAt.smul_const (hc : HasDerivWithinAt c c' s x) (f : F) :
    HasDerivWithinAt (fun y => c y â€¢ f) (c' â€¢ f) s x := by
  have := hc.smul (hasDerivWithinAt_const x s f)
  rwa [smul_zero, zero_add] at this
#align has_deriv_within_at.smul_const HasDerivWithinAt.smul_const

theorem HasDerivAt.smul_const (hc : HasDerivAt c c' x) (f : F) :
    HasDerivAt (fun y => c y â€¢ f) (c' â€¢ f) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.smul_const f
#align has_deriv_at.smul_const HasDerivAt.smul_const

theorem derivWithin_smul_const (hxs : UniqueDiffWithinAt ð•œ s x)
    (hc : DifferentiableWithinAt ð•œ c s x) (f : F) :
    derivWithin (fun y => c y â€¢ f) s x = derivWithin c s x â€¢ f :=
  (hc.hasDerivWithinAt.smul_const f).derivWithin hxs
#align deriv_within_smul_const derivWithin_smul_const

theorem deriv_smul_const (hc : DifferentiableAt ð•œ c x) (f : F) :
    deriv (fun y => c y â€¢ f) x = deriv c x â€¢ f :=
  (hc.hasDerivAt.smul_const f).deriv
#align deriv_smul_const deriv_smul_const

end SMul

section ConstSMul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ð•œ R F] [ContinuousConstSMul R F]

nonrec theorem HasStrictDerivAt.const_smul (c : R) (hf : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun y => c â€¢ f y) (c â€¢ f') x := by
  simpa using (hf.const_smul c).hasStrictDerivAt
#align has_strict_deriv_at.const_smul HasStrictDerivAt.const_smul

nonrec theorem HasDerivAtFilter.const_smul (c : R) (hf : HasDerivAtFilter f f' x L) :
    HasDerivAtFilter (fun y => c â€¢ f y) (c â€¢ f') x L := by
  simpa using (hf.const_smul c).hasDerivAtFilter
#align has_deriv_at_filter.const_smul HasDerivAtFilter.const_smul

nonrec theorem HasDerivWithinAt.const_smul (c : R) (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun y => c â€¢ f y) (c â€¢ f') s x :=
  hf.const_smul c
#align has_deriv_within_at.const_smul HasDerivWithinAt.const_smul

nonrec theorem HasDerivAt.const_smul (c : R) (hf : HasDerivAt f f' x) :
    HasDerivAt (fun y => c â€¢ f y) (c â€¢ f') x :=
  hf.const_smul c
#align has_deriv_at.const_smul HasDerivAt.const_smul

theorem derivWithin_const_smul (hxs : UniqueDiffWithinAt ð•œ s x) (c : R)
    (hf : DifferentiableWithinAt ð•œ f s x) :
    derivWithin (fun y => c â€¢ f y) s x = c â€¢ derivWithin f s x :=
  (hf.hasDerivWithinAt.const_smul c).derivWithin hxs
#align deriv_within_const_smul derivWithin_const_smul

theorem deriv_const_smul (c : R) (hf : DifferentiableAt ð•œ f x) :
    deriv (fun y => c â€¢ f y) x = c â€¢ deriv f x :=
  (hf.hasDerivAt.const_smul c).deriv
#align deriv_const_smul deriv_const_smul

/-- A variant of `deriv_const_smul` without differentiability assumption when the scalar
multiplication is by field elements. -/
lemma deriv_const_smul' {f : ð•œ â†’ F} {x : ð•œ} {R : Type*} [Field R] [Module R F] [SMulCommClass ð•œ R F]
    [ContinuousConstSMul R F] (c : R) :
    deriv (fun y â†¦ c â€¢ f y) x = c â€¢ deriv f x := by
  by_cases hf : DifferentiableAt ð•œ f x
  Â· exact deriv_const_smul c hf
  Â· rcases eq_or_ne c 0 with rfl | hc
    Â· simp only [zero_smul, deriv_const']
    Â· have H : Â¬DifferentiableAt ð•œ (fun y â†¦ c â€¢ f y) x := by
        contrapose! hf
        change DifferentiableAt ð•œ (fun y â†¦ f y) x
        conv => enter [2, y]; rw [â† inv_smul_smulâ‚€ hc (f y)]
        exact DifferentiableAt.const_smul hf câ»Â¹
      rw [deriv_zero_of_not_differentiableAt hf, deriv_zero_of_not_differentiableAt H, smul_zero]

end ConstSMul

section Mul

/-! ### Derivative of the multiplication of two functions -/


variable {ð•œ' ð”¸ : Type*} [NormedField ð•œ'] [NormedRing ð”¸] [NormedAlgebra ð•œ ð•œ'] [NormedAlgebra ð•œ ð”¸]
  {c d : ð•œ â†’ ð”¸} {c' d' : ð”¸} {u v : ð•œ â†’ ð•œ'}

theorem HasDerivWithinAt.mul (hc : HasDerivWithinAt c c' s x) (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => c y * d y) (c' * d x + c x * d') s x := by
  have := (HasFDerivWithinAt.mul' hc hd).hasDerivWithinAt
  rwa [ContinuousLinearMap.add_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, one_smul, one_smul,
    add_comm] at this
#align has_deriv_within_at.mul HasDerivWithinAt.mul

theorem HasDerivAt.mul (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => c y * d y) (c' * d x + c x * d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.mul hd
#align has_deriv_at.mul HasDerivAt.mul

theorem HasStrictDerivAt.mul (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => c y * d y) (c' * d x + c x * d') x := by
  have := (HasStrictFDerivAt.mul' hc hd).hasStrictDerivAt
  rwa [ContinuousLinearMap.add_apply, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, one_smul, one_smul,
    add_comm] at this
#align has_strict_deriv_at.mul HasStrictDerivAt.mul

theorem derivWithin_mul (hxs : UniqueDiffWithinAt ð•œ s x) (hc : DifferentiableWithinAt ð•œ c s x)
    (hd : DifferentiableWithinAt ð•œ d s x) :
    derivWithin (fun y => c y * d y) s x = derivWithin c s x * d x + c x * derivWithin d s x :=
  (hc.hasDerivWithinAt.mul hd.hasDerivWithinAt).derivWithin hxs
#align deriv_within_mul derivWithin_mul

@[simp]
theorem deriv_mul (hc : DifferentiableAt ð•œ c x) (hd : DifferentiableAt ð•œ d x) :
    deriv (fun y => c y * d y) x = deriv c x * d x + c x * deriv d x :=
  (hc.hasDerivAt.mul hd.hasDerivAt).deriv
#align deriv_mul deriv_mul

theorem HasDerivWithinAt.mul_const (hc : HasDerivWithinAt c c' s x) (d : ð”¸) :
    HasDerivWithinAt (fun y => c y * d) (c' * d) s x := by
  convert hc.mul (hasDerivWithinAt_const x s d) using 1
  rw [mul_zero, add_zero]
#align has_deriv_within_at.mul_const HasDerivWithinAt.mul_const

theorem HasDerivAt.mul_const (hc : HasDerivAt c c' x) (d : ð”¸) :
    HasDerivAt (fun y => c y * d) (c' * d) x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.mul_const d
#align has_deriv_at.mul_const HasDerivAt.mul_const

theorem hasDerivAt_mul_const (c : ð•œ) : HasDerivAt (fun x => x * c) c x := by
  simpa only [one_mul] using (hasDerivAt_id' x).mul_const c
#align has_deriv_at_mul_const hasDerivAt_mul_const

theorem HasStrictDerivAt.mul_const (hc : HasStrictDerivAt c c' x) (d : ð”¸) :
    HasStrictDerivAt (fun y => c y * d) (c' * d) x := by
  convert hc.mul (hasStrictDerivAt_const x d) using 1
  rw [mul_zero, add_zero]
#align has_strict_deriv_at.mul_const HasStrictDerivAt.mul_const

theorem derivWithin_mul_const (hxs : UniqueDiffWithinAt ð•œ s x) (hc : DifferentiableWithinAt ð•œ c s x)
    (d : ð”¸) : derivWithin (fun y => c y * d) s x = derivWithin c s x * d :=
  (hc.hasDerivWithinAt.mul_const d).derivWithin hxs
#align deriv_within_mul_const derivWithin_mul_const

theorem deriv_mul_const (hc : DifferentiableAt ð•œ c x) (d : ð”¸) :
    deriv (fun y => c y * d) x = deriv c x * d :=
  (hc.hasDerivAt.mul_const d).deriv
#align deriv_mul_const deriv_mul_const

theorem deriv_mul_const_field (v : ð•œ') : deriv (fun y => u y * v) x = deriv u x * v := by
  by_cases hu : DifferentiableAt ð•œ u x
  Â· exact deriv_mul_const hu v
  Â· rw [deriv_zero_of_not_differentiableAt hu, zero_mul]
    rcases eq_or_ne v 0 with (rfl | hd)
    Â· simp only [mul_zero, deriv_const]
    Â· refine deriv_zero_of_not_differentiableAt (mt (fun H => ?_) hu)
      simpa only [mul_inv_cancel_rightâ‚€ hd] using H.mul_const vâ»Â¹
#align deriv_mul_const_field deriv_mul_const_field

@[simp]
theorem deriv_mul_const_field' (v : ð•œ') : (deriv fun x => u x * v) = fun x => deriv u x * v :=
  funext fun _ => deriv_mul_const_field v
#align deriv_mul_const_field' deriv_mul_const_field'

theorem HasDerivWithinAt.const_mul (c : ð”¸) (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => c * d y) (c * d') s x := by
  convert (hasDerivWithinAt_const x s c).mul hd using 1
  rw [zero_mul, zero_add]
#align has_deriv_within_at.const_mul HasDerivWithinAt.const_mul

theorem HasDerivAt.const_mul (c : ð”¸) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => c * d y) (c * d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hd.const_mul c
#align has_deriv_at.const_mul HasDerivAt.const_mul

theorem HasStrictDerivAt.const_mul (c : ð”¸) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => c * d y) (c * d') x := by
  convert (hasStrictDerivAt_const _ _).mul hd using 1
  rw [zero_mul, zero_add]
#align has_strict_deriv_at.const_mul HasStrictDerivAt.const_mul

theorem derivWithin_const_mul (hxs : UniqueDiffWithinAt ð•œ s x) (c : ð”¸)
    (hd : DifferentiableWithinAt ð•œ d s x) :
    derivWithin (fun y => c * d y) s x = c * derivWithin d s x :=
  (hd.hasDerivWithinAt.const_mul c).derivWithin hxs
#align deriv_within_const_mul derivWithin_const_mul

theorem deriv_const_mul (c : ð”¸) (hd : DifferentiableAt ð•œ d x) :
    deriv (fun y => c * d y) x = c * deriv d x :=
  (hd.hasDerivAt.const_mul c).deriv
#align deriv_const_mul deriv_const_mul

theorem deriv_const_mul_field (u : ð•œ') : deriv (fun y => u * v y) x = u * deriv v x := by
  simp only [mul_comm u, deriv_mul_const_field]
#align deriv_const_mul_field deriv_const_mul_field

@[simp]
theorem deriv_const_mul_field' (u : ð•œ') : (deriv fun x => u * v x) = fun x => u * deriv v x :=
  funext fun _ => deriv_const_mul_field u
#align deriv_const_mul_field' deriv_const_mul_field'

end Mul

section Prod

section HasDeriv

variable {Î¹ : Type*} [DecidableEq Î¹] {ð”¸' : Type*} [NormedCommRing ð”¸'] [NormedAlgebra ð•œ ð”¸']
  {u : Finset Î¹} {f : Î¹ â†’ ð•œ â†’ ð”¸'} {f' : Î¹ â†’ ð”¸'}

theorem HasDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivAt (f i) (f' i) x) :
    HasDerivAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivAt)).hasDerivAt

theorem HasDerivWithinAt.finset_prod (hf : âˆ€ i âˆˆ u, HasDerivWithinAt (f i) (f' i) s x) :
    HasDerivWithinAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) s x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasFDerivWithinAt.finset_prod (fun i hi â†¦ (hf i hi).hasFDerivWithinAt)).hasDerivWithinAt

theorem HasStrictDerivAt.finset_prod (hf : âˆ€ i âˆˆ u, HasStrictDerivAt (f i) (f' i) x) :
    HasStrictDerivAt (âˆ i âˆˆ u, f i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ f' i) x := by
  simpa [ContinuousLinearMap.sum_apply, ContinuousLinearMap.smul_apply] using
    (HasStrictFDerivAt.finset_prod (fun i hi â†¦ (hf i hi).hasStrictFDerivAt)).hasStrictDerivAt

theorem deriv_finset_prod (hf : âˆ€ i âˆˆ u, DifferentiableAt ð•œ (f i) x) :
    deriv (âˆ i âˆˆ u, f i Â·) x = âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ deriv (f i) x :=
  (HasDerivAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivAt).deriv

theorem derivWithin_finset_prod (hxs : UniqueDiffWithinAt ð•œ s x)
    (hf : âˆ€ i âˆˆ u, DifferentiableWithinAt ð•œ (f i) s x) :
    derivWithin (âˆ i âˆˆ u, f i Â·) s x =
      âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, f j x) â€¢ derivWithin (f i) s x :=
  (HasDerivWithinAt.finset_prod fun i hi â†¦ (hf i hi).hasDerivWithinAt).derivWithin hxs

end HasDeriv

variable {Î¹ : Type*} {ð”¸' : Type*} [NormedCommRing ð”¸'] [NormedAlgebra ð•œ ð”¸']
  {u : Finset Î¹} {f : Î¹ â†’ ð•œ â†’ ð”¸'} {f' : Î¹ â†’ ð”¸'}

theorem DifferentiableAt.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableAt ð•œ (f i) x) :
    DifferentiableAt ð•œ (âˆ i âˆˆ u, f i Â·) x :=
  (HasDerivAt.finset_prod (fun i hi â†¦ DifferentiableAt.hasDerivAt (hd i hi))).differentiableAt

theorem DifferentiableWithinAt.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableWithinAt ð•œ (f i) s x) :
    DifferentiableWithinAt ð•œ (âˆ i âˆˆ u, f i Â·) s x :=
  (HasDerivWithinAt.finset_prod (fun i hi â†¦
    DifferentiableWithinAt.hasDerivWithinAt (hd i hi))).differentiableWithinAt

theorem DifferentiableOn.finset_prod (hd : âˆ€ i âˆˆ u, DifferentiableOn ð•œ (f i) s) :
    DifferentiableOn ð•œ (âˆ i âˆˆ u, f i Â·) s :=
  fun x hx â†¦ .finset_prod (fun i hi â†¦ hd i hi x hx)

theorem Differentiable.finset_prod (hd : âˆ€ i âˆˆ u, Differentiable ð•œ (f i)) :
    Differentiable ð•œ (âˆ i âˆˆ u, f i Â·) :=
  fun x â†¦ .finset_prod (fun i hi â†¦ hd i hi x)

end Prod

section Div

variable {ð•œ' : Type*} [NontriviallyNormedField ð•œ'] [NormedAlgebra ð•œ ð•œ'] {c d : ð•œ â†’ ð•œ'} {c' d' : ð•œ'}

theorem HasDerivAt.div_const (hc : HasDerivAt c c' x) (d : ð•œ') :
    HasDerivAt (fun x => c x / d) (c' / d) x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹
#align has_deriv_at.div_const HasDerivAt.div_const

theorem HasDerivWithinAt.div_const (hc : HasDerivWithinAt c c' s x) (d : ð•œ') :
    HasDerivWithinAt (fun x => c x / d) (c' / d) s x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹
#align has_deriv_within_at.div_const HasDerivWithinAt.div_const

theorem HasStrictDerivAt.div_const (hc : HasStrictDerivAt c c' x) (d : ð•œ') :
    HasStrictDerivAt (fun x => c x / d) (c' / d) x := by
  simpa only [div_eq_mul_inv] using hc.mul_const dâ»Â¹
#align has_strict_deriv_at.div_const HasStrictDerivAt.div_const

theorem DifferentiableWithinAt.div_const (hc : DifferentiableWithinAt ð•œ c s x) (d : ð•œ') :
    DifferentiableWithinAt ð•œ (fun x => c x / d) s x :=
  (hc.hasDerivWithinAt.div_const _).differentiableWithinAt
#align differentiable_within_at.div_const DifferentiableWithinAt.div_const

@[simp]
theorem DifferentiableAt.div_const (hc : DifferentiableAt ð•œ c x) (d : ð•œ') :
    DifferentiableAt ð•œ (fun x => c x / d) x :=
  (hc.hasDerivAt.div_const _).differentiableAt
#align differentiable_at.div_const DifferentiableAt.div_const

theorem DifferentiableOn.div_const (hc : DifferentiableOn ð•œ c s) (d : ð•œ') :
    DifferentiableOn ð•œ (fun x => c x / d) s := fun x hx => (hc x hx).div_const d
#align differentiable_on.div_const DifferentiableOn.div_const

@[simp]
theorem Differentiable.div_const (hc : Differentiable ð•œ c) (d : ð•œ') :
    Differentiable ð•œ fun x => c x / d := fun x => (hc x).div_const d
#align differentiable.div_const Differentiable.div_const

theorem derivWithin_div_const (hc : DifferentiableWithinAt ð•œ c s x)
    (d : ð•œ') (hxs : UniqueDiffWithinAt ð•œ s x) :
    derivWithin (fun x => c x / d) s x = derivWithin c s x / d := by
  simp [div_eq_inv_mul, derivWithin_const_mul, hc, hxs]
#align deriv_within_div_const derivWithin_div_const

@[simp]
theorem deriv_div_const (d : ð•œ') : deriv (fun x => c x / d) x = deriv c x / d := by
  simp only [div_eq_mul_inv, deriv_mul_const_field]
#align deriv_div_const deriv_div_const

end Div

section CLMCompApply

/-! ### Derivative of the pointwise composition/application of continuous linear maps -/


open ContinuousLinearMap

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ð•œ G] {c : ð•œ â†’ F â†’L[ð•œ] G} {c' : F â†’L[ð•œ] G}
  {d : ð•œ â†’ E â†’L[ð•œ] F} {d' : E â†’L[ð•œ] F} {u : ð•œ â†’ F} {u' : F}

theorem HasStrictDerivAt.clm_comp (hc : HasStrictDerivAt c c' x) (hd : HasStrictDerivAt d d' x) :
    HasStrictDerivAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') x := by
  have := (hc.hasStrictFDerivAt.clm_comp hd.hasStrictFDerivAt).hasStrictDerivAt
  rwa [add_apply, comp_apply, comp_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this
#align has_strict_deriv_at.clm_comp HasStrictDerivAt.clm_comp

theorem HasDerivWithinAt.clm_comp (hc : HasDerivWithinAt c c' s x)
    (hd : HasDerivWithinAt d d' s x) :
    HasDerivWithinAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') s x := by
  have := (hc.hasFDerivWithinAt.clm_comp hd.hasFDerivWithinAt).hasDerivWithinAt
  rwa [add_apply, comp_apply, comp_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this
#align has_deriv_within_at.clm_comp HasDerivWithinAt.clm_comp

theorem HasDerivAt.clm_comp (hc : HasDerivAt c c' x) (hd : HasDerivAt d d' x) :
    HasDerivAt (fun y => (c y).comp (d y)) (c'.comp (d x) + (c x).comp d') x := by
  rw [â† hasDerivWithinAt_univ] at *
  exact hc.clm_comp hd
#align has_deriv_at.clm_comp HasDerivAt.clm_comp

theorem derivWithin_clm_comp (hc : DifferentiableWithinAt ð•œ c s x)
    (hd : DifferentiableWithinAt ð•œ d s x) (hxs : UniqueDiffWithinAt ð•œ s x) :
    derivWithin (fun y => (c y).comp (d y)) s x =
      (derivWithin c s x).comp (d x) + (c x).comp (derivWithin d s x) :=
  (hc.hasDerivWithinAt.clm_comp hd.hasDerivWithinAt).derivWithin hxs
#align deriv_within_clm_comp derivWithin_clm_comp

theorem deriv_clm_comp (hc : DifferentiableAt ð•œ c x) (hd : DifferentiableAt ð•œ d x) :
    deriv (fun y => (c y).comp (d y)) x = (deriv c x).comp (d x) + (c x).comp (deriv d x) :=
  (hc.hasDerivAt.clm_comp hd.hasDerivAt).deriv
#align deriv_clm_comp deriv_clm_comp

theorem HasStrictDerivAt.clm_apply (hc : HasStrictDerivAt c c' x) (hu : HasStrictDerivAt u u' x) :
    HasStrictDerivAt (fun y => (c y) (u y)) (c' (u x) + c x u') x := by
  have := (hc.hasStrictFDerivAt.clm_apply hu.hasStrictFDerivAt).hasStrictDerivAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this
#align has_strict_deriv_at.clm_apply HasStrictDerivAt.clm_apply

theorem HasDerivWithinAt.clm_apply (hc : HasDerivWithinAt c c' s x)
    (hu : HasDerivWithinAt u u' s x) :
    HasDerivWithinAt (fun y => (c y) (u y)) (c' (u x) + c x u') s x := by
  have := (hc.hasFDerivWithinAt.clm_apply hu.hasFDerivWithinAt).hasDerivWithinAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this
#align has_deriv_within_at.clm_apply HasDerivWithinAt.clm_apply

theorem HasDerivAt.clm_apply (hc : HasDerivAt c c' x) (hu : HasDerivAt u u' x) :
    HasDerivAt (fun y => (c y) (u y)) (c' (u x) + c x u') x := by
  have := (hc.hasFDerivAt.clm_apply hu.hasFDerivAt).hasDerivAt
  rwa [add_apply, comp_apply, flip_apply, smulRight_apply, smulRight_apply, one_apply, one_smul,
    one_smul, add_comm] at this
#align has_deriv_at.clm_apply HasDerivAt.clm_apply

theorem derivWithin_clm_apply (hxs : UniqueDiffWithinAt ð•œ s x) (hc : DifferentiableWithinAt ð•œ c s x)
    (hu : DifferentiableWithinAt ð•œ u s x) :
    derivWithin (fun y => (c y) (u y)) s x = derivWithin c s x (u x) + c x (derivWithin u s x) :=
  (hc.hasDerivWithinAt.clm_apply hu.hasDerivWithinAt).derivWithin hxs
#align deriv_within_clm_apply derivWithin_clm_apply

theorem deriv_clm_apply (hc : DifferentiableAt ð•œ c x) (hu : DifferentiableAt ð•œ u x) :
    deriv (fun y => (c y) (u y)) x = deriv c x (u x) + c x (deriv u x) :=
  (hc.hasDerivAt.clm_apply hu.hasDerivAt).deriv
#align deriv_clm_apply deriv_clm_apply

end CLMCompApply
