/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Star

/-!
# Star operations on derivatives

This file contains the usual formulas (and existence assertions) for the derivative of the star
operation.

Most of the results in this file only apply when the field that the derivative is respect to has a
trivial star operation; which as should be expected rules out `ğ•œ = ğ•œ`. The exceptions are
`HasDerivAt.conj_conj` and `DifferentiableAt.conj_conj`, showing that `conj âˆ˜ f âˆ˜ conj` is
differentiable when `f` is (and giving a formula for its derivative).
-/

universe u v w

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] [StarRing ğ•œ]
  {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [StarAddMonoid F] [StarModule ğ•œ F]
  [ContinuousStar F] {f : ğ•œ â†’ F} {f' : F} {x : ğ•œ}

/-! ### Derivative of `x â†¦ star x` -/

section TrivialStar

variable [TrivialStar ğ•œ] {s : Set ğ•œ} {L : Filter ğ•œ}

protected nonrec theorem HasDerivAtFilter.star (h : HasDerivAtFilter f f' x L) :
    HasDerivAtFilter (fun x => star (f x)) (star f') x L := by
  simpa using h.star.hasDerivAtFilter

protected nonrec theorem HasDerivWithinAt.star (h : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (fun x => star (f x)) (star f') s x :=
  h.star

protected nonrec theorem HasDerivAt.star (h : HasDerivAt f f' x) :
    HasDerivAt (fun x => star (f x)) (star f') x :=
  h.star

protected nonrec theorem HasStrictDerivAt.star (h : HasStrictDerivAt f f' x) :
    HasStrictDerivAt (fun x => star (f x)) (star f') x := by simpa using h.star.hasStrictDerivAt

protected theorem derivWithin.star :
    derivWithin (fun y => star (f y)) s x = star (derivWithin f s x) := by
  by_cases hxs : UniqueDiffWithinAt ğ•œ s x
  Â· exact DFunLike.congr_fun (fderivWithin_star hxs) _
  Â· simp [derivWithin_zero_of_not_uniqueDiffWithinAt hxs]

protected theorem deriv.star : deriv (fun y => star (f y)) x = star (deriv f x) :=
  DFunLike.congr_fun fderiv_star _

@[simp]
protected theorem deriv.star' : (deriv fun y => star (f y)) = fun x => star (deriv f x) :=
  funext fun _ => deriv.star

end TrivialStar

section NontrivialStar

variable [NormedStarGroup ğ•œ]

open scoped ComplexConjugate

/-- If `f` has derivative `f'` at `z`, then `star âˆ˜ f âˆ˜ conj` has derivative `star f'` at
`conj z`. -/
lemma HasDerivAt.star_conj {f : ğ•œ â†’ F} {f' : F} (hf : HasDerivAt f f' x) :
    HasDerivAt (star âˆ˜ f âˆ˜ conj) (star f') (conj x) := by
  rw [hasDerivAt_iff_hasFDerivAt]
  convert hf.hasFDerivAt.star_star
  ext
  simp

/-- A function `f` has derivative `f'` at `z` iff `star âˆ˜ f âˆ˜ conj` has derivative `star f'` at
`conj z`. -/
lemma hasDerivAt_star_conj_iff {f : ğ•œ â†’ F} {x : ğ•œ} {f' : F} :
    HasDerivAt (star âˆ˜ f âˆ˜ conj) (star f') (conj x) â†” HasDerivAt f f' x :=
  âŸ¨fun hf â†¦ by convert hf.star_conj <;> simp [Function.comp_def],
  fun hf â†¦ hf.star_conjâŸ©

/-- If `f` has derivative `f'` at `z`, then `conj âˆ˜ f âˆ˜ conj` has derivative `conj f'` at
`conj z`. -/
lemma HasDerivAt.conj_conj {f : ğ•œ â†’ ğ•œ} {f' : ğ•œ} (hf : HasDerivAt f f' x) :
    HasDerivAt (conj âˆ˜ f âˆ˜ conj) (conj f') (conj x) :=
  hf.star_conj

/-- A function `f` has derivative `f'` at `z` iff `conj âˆ˜ f âˆ˜ conj` has derivative `conj f'` at
`conj z`. -/
lemma hasDerivAt_conj_conj_iff {f : ğ•œ â†’ ğ•œ} {x f' : ğ•œ} :
    HasDerivAt (conj âˆ˜ f âˆ˜ conj) (conj f') (conj x) â†” HasDerivAt f f' x :=
  hasDerivAt_star_conj_iff

/-- If `f` is differentiable at `conj z`, then `star âˆ˜ f âˆ˜ conj` is differentiable at `z`. -/
lemma DifferentiableAt.star_conj {f : ğ•œ â†’ F} (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (star âˆ˜ f âˆ˜ conj) (conj x) :=
  hf.star_star

/-- A function `f` is differentiable at `conj z` iff `star âˆ˜ f âˆ˜ conj` is differentiable at `z`. -/
lemma differentiableAt_star_conj_iff {f : ğ•œ â†’ F} :
    DifferentiableAt ğ•œ (star âˆ˜ f âˆ˜ conj) (conj x) â†” DifferentiableAt ğ•œ f x :=
  âŸ¨fun hf â†¦ by convert hf.star_conj <;> simp [Function.comp_def], fun hf â†¦ hf.star_starâŸ©

/-- If `f` is differentiable at `conj z`, then `conj âˆ˜ f âˆ˜ conj` is differentiable at `z`. -/
lemma DifferentiableAt.conj_conj {f : ğ•œ â†’ ğ•œ} (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (star âˆ˜ f âˆ˜ conj) (conj x) :=
  hf.star_star

/-- A function `f` is differentiable at `conj z` iff `conj âˆ˜ f âˆ˜ conj` is differentiable at `z`. -/
lemma differentiableAt_conj_conj_iff {f : ğ•œ â†’ ğ•œ} :
    DifferentiableAt ğ•œ (conj âˆ˜ f âˆ˜ conj) (conj x) â†” DifferentiableAt ğ•œ f x :=
  differentiableAt_star_conj_iff

/--
The derivative of `star âˆ˜ f âˆ˜ conj` is `star âˆ˜ deriv f âˆ˜ conj`, allowing for the possibility that
both sides have the junk value `0`.
-/
@[simp]
lemma deriv_star_conj {f : ğ•œ â†’ F} :
    deriv (star âˆ˜ f âˆ˜ conj) = star âˆ˜ deriv f âˆ˜ conj := by
  ext z
  by_cases hf : DifferentiableAt ğ•œ f (conj z)
  Â· have hd := hf.hasDerivAt.star_conj.deriv
    simp only [Function.comp_def, RingHomCompTriple.comp_apply, RingHom.id_apply,
      Function.comp_apply] at hd âŠ¢
    exact hd
  Â· simp only [Function.comp_apply, hf, not_false_eq_true, deriv_zero_of_not_differentiableAt,
      star_zero]
    apply deriv_zero_of_not_differentiableAt
    contrapose! hf
    rw[â† RCLike.conj_conj z] at hf
    rwa[â† differentiableAt_star_conj_iff]

/--
The derivative of `conj âˆ˜ f âˆ˜ conj` is `conj âˆ˜ deriv f âˆ˜ conj`, allowing for the possibility that
both sides have the junk value `0`.
-/
@[simp]
lemma deriv_conj_conj {f : ğ•œ â†’ ğ•œ} :
    deriv (conj âˆ˜ f âˆ˜ conj) = conj âˆ˜ deriv f âˆ˜ conj := deriv_star_conj

end NontrivialStar
