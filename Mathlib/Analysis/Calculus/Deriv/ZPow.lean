/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Pow
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.Calculus.Deriv.Shift

/-!
# Derivatives of `x ^ m`, `m : â„¤`

In this file we prove theorems about (iterated) derivatives of `x ^ m`, `m : â„¤`.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`analysis/calculus/deriv/basic`.

## Keywords

derivative, power
-/

universe u v w

open Topology Filter Asymptotics Set
open scoped Nat

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {E : Type v} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {x : ğ•œ}
variable {s : Set ğ•œ}
variable {m : â„¤}

/-! ### Derivative of `x â†¦ x^m` for `m : â„¤` -/

theorem hasStrictDerivAt_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
    HasStrictDerivAt (fun x => x ^ m) ((m : ğ•œ) * x ^ (m - 1)) x := by
  have : âˆ€ m : â„¤, 0 < m â†’ HasStrictDerivAt (Â· ^ m) ((m : ğ•œ) * x ^ (m - 1)) x := fun m hm â†¦ by
    lift m to â„• using hm.le
    simp only [zpow_natCast, Int.cast_natCast]
    convert hasStrictDerivAt_pow m x using 2
    rw [â† Int.ofNat_one, â† Int.ofNat_sub, zpow_natCast]
    norm_cast at hm
  rcases lt_trichotomy m 0 with (hm | hm | hm)
  Â· have hx : x â‰  0 := h.resolve_right hm.not_ge
    have := (hasStrictDerivAt_inv ?_).scomp _ (this (-m) (neg_pos.2 hm)) <;>
      [skip; exact zpow_ne_zero _ hx]
    simp only [Function.comp_def, zpow_neg, inv_inv, smul_eq_mul] at this
    convert this using 1
    rw [sq, mul_inv, inv_inv, Int.cast_neg, neg_mul, neg_mul_neg, â† zpow_addâ‚€ hx, mul_assoc, â†
      zpow_addâ‚€ hx]
    congr
    abel
  Â· simp only [hm, zpow_zero, Int.cast_zero, zero_mul, hasStrictDerivAt_const]
  Â· exact this m hm

theorem hasDerivAt_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
    HasDerivAt (fun x => x ^ m) ((m : ğ•œ) * x ^ (m - 1)) x :=
  (hasStrictDerivAt_zpow m x h).hasDerivAt

theorem hasDerivWithinAt_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) (s : Set ğ•œ) :
    HasDerivWithinAt (fun x => x ^ m) ((m : ğ•œ) * x ^ (m - 1)) s x :=
  (hasDerivAt_zpow m x h).hasDerivWithinAt

theorem differentiableAt_zpow : DifferentiableAt ğ•œ (fun x => x ^ m) x â†” x â‰  0 âˆ¨ 0 â‰¤ m :=
  âŸ¨fun H => NormedField.continuousAt_zpow.1 H.continuousAt, fun H =>
    (hasDerivAt_zpow m x H).differentiableAtâŸ©

theorem differentiableWithinAt_zpow (m : â„¤) (x : ğ•œ) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
    DifferentiableWithinAt ğ•œ (fun x => x ^ m) s x :=
  (differentiableAt_zpow.mpr h).differentiableWithinAt

theorem differentiableOn_zpow (m : â„¤) (s : Set ğ•œ) (h : (0 : ğ•œ) âˆ‰ s âˆ¨ 0 â‰¤ m) :
    DifferentiableOn ğ•œ (fun x => x ^ m) s := fun x hxs =>
  differentiableWithinAt_zpow m x <| h.imp_left <| ne_of_mem_of_not_mem hxs

theorem deriv_zpow (m : â„¤) (x : ğ•œ) : deriv (fun x => x ^ m) x = m * x ^ (m - 1) := by
  by_cases H : x â‰  0 âˆ¨ 0 â‰¤ m
  Â· exact (hasDerivAt_zpow m x H).deriv
  Â· rw [deriv_zero_of_not_differentiableAt (mt differentiableAt_zpow.1 H)]
    push_neg at H
    rcases H with âŸ¨rfl, hmâŸ©
    rw [zero_zpow _ ((sub_one_lt _).trans hm).ne, mul_zero]

@[simp]
theorem deriv_zpow' (m : â„¤) : (deriv fun x : ğ•œ => x ^ m) = fun x => (m : ğ•œ) * x ^ (m - 1) :=
  funext <| deriv_zpow m

theorem derivWithin_zpow (hxs : UniqueDiffWithinAt ğ•œ s x) (h : x â‰  0 âˆ¨ 0 â‰¤ m) :
    derivWithin (fun x => x ^ m) s x = (m : ğ•œ) * x ^ (m - 1) :=
  (hasDerivWithinAt_zpow m x h s).derivWithin hxs

@[simp]
theorem iter_deriv_zpow' (m : â„¤) (k : â„•) :
    (deriv^[k] fun x : ğ•œ => x ^ m) =
      fun x => (âˆ i âˆˆ Finset.range k, ((m : ğ•œ) - i)) * x ^ (m - k) := by
  induction k with
  | zero =>
    simp only [one_mul, Int.ofNat_zero, id, sub_zero, Finset.prod_range_zero, Function.iterate_zero]
  | succ k ihk =>
    simp only [Function.iterate_succ_apply', ihk, deriv_const_mul_field', deriv_zpow',
      Finset.prod_range_succ, Int.natCast_succ, â† sub_sub, Int.cast_sub, Int.cast_natCast,
      mul_assoc]

theorem iter_deriv_zpow (m : â„¤) (x : ğ•œ) (k : â„•) :
    deriv^[k] (fun y => y ^ m) x = (âˆ i âˆˆ Finset.range k, ((m : ğ•œ) - i)) * x ^ (m - k) :=
  congr_fun (iter_deriv_zpow' m k) x

theorem iter_deriv_pow (n : â„•) (x : ğ•œ) (k : â„•) :
    deriv^[k] (fun x : ğ•œ => x ^ n) x = (âˆ i âˆˆ Finset.range k, ((n : ğ•œ) - i)) * x ^ (n - k) := by
  simp only [â† zpow_natCast, iter_deriv_zpow, Int.cast_natCast]
  rcases le_or_gt k n with hkn | hnk
  Â· rw [Int.ofNat_sub hkn]
  Â· have : (âˆ i âˆˆ Finset.range k, (n - i : ğ•œ)) = 0 :=
      Finset.prod_eq_zero (Finset.mem_range.2 hnk) (sub_self _)
    simp only [this, zero_mul]

@[simp]
theorem iter_deriv_pow' (n k : â„•) :
    (deriv^[k] fun x : ğ•œ => x ^ n) =
      fun x => (âˆ i âˆˆ Finset.range k, ((n : ğ•œ) - i)) * x ^ (n - k) :=
  funext fun x => iter_deriv_pow n x k

theorem iter_deriv_inv (k : â„•) (x : ğ•œ) :
    deriv^[k] Inv.inv x = (-1) ^ k * k ! * x ^ (-1 - k : â„¤) := calc
  deriv^[k] Inv.inv x = deriv^[k] (Â· ^ (-1 : â„¤)) x := by simp
  _ = (âˆ i âˆˆ Finset.range k, (-1 - i : ğ•œ)) * x ^ (-1 - k : â„¤) := mod_cast iter_deriv_zpow (-1) x k
  _ = (-1) ^ k * k ! * x ^ (-1 - k : â„¤) := by
    simp only [â† neg_add', Finset.prod_neg, â† Finset.prod_Ico_id_eq_factorial,
      Finset.prod_Ico_eq_prod_range]
    simp

@[simp]
theorem iter_deriv_inv' (k : â„•) :
    deriv^[k] Inv.inv = fun x : ğ•œ => (-1) ^ k * k ! * x ^ (-1 - k : â„¤) :=
  funext (iter_deriv_inv k)

open Nat Function in
theorem iter_deriv_inv_linear (k : â„•) (c d : ğ•œ) :
    deriv^[k] (fun x â†¦ (c * x + d)â»Â¹) =
    (fun x : ğ•œ â†¦ (-1) ^ k * k ! * c ^ k * (c * x + d) ^ (-1 - k : â„¤)) := by
  induction k with
  | zero => simp
  | succ k ihk =>
    rw [factorial_succ, add_comm k 1, iterate_add_apply, ihk]
    ext z
    simp only [Int.reduceNeg, iterate_one, deriv_const_mul_field', cast_add, cast_one]
    by_cases hd : c = 0
    Â· simp [hd]
    Â· have := deriv_comp_add_const (fun x â†¦ (c * x) ^ (-1 - k : â„¤)) (d / c) z
      have h0 : (fun x â†¦ (c * (x + d / c)) ^ (-1 - (k : â„¤))) =
        (fun x â†¦ (c * x + d) ^ (-1 - (k : â„¤))) := by
        ext y
        field_simp
      rw [h0, deriv_comp_mul_left c (fun x â†¦ (x) ^ (-1 - k : â„¤)) (z + d / c)] at this
      simp [this]
      field_simp
      ring_nf

theorem iter_deriv_inv_linear_sub (k : â„•) (c d : ğ•œ) :
    deriv^[k] (fun x â†¦ (c * x - d)â»Â¹) =
    (fun x : ğ•œ â†¦ (-1) ^ k * k ! * c ^ k * (c * x - d) ^ (-1 - k : â„¤)) := by
  simpa [sub_eq_add_neg] using iter_deriv_inv_linear k c (-d)

variable {f : E â†’ ğ•œ} {t : Set E} {a : E}

theorem DifferentiableWithinAt.zpow (hf : DifferentiableWithinAt ğ•œ f t a) (h : f a â‰  0 âˆ¨ 0 â‰¤ m) :
    DifferentiableWithinAt ğ•œ (fun x => f x ^ m) t a :=
  (differentiableAt_zpow.2 h).comp_differentiableWithinAt a hf

theorem DifferentiableAt.zpow (hf : DifferentiableAt ğ•œ f a) (h : f a â‰  0 âˆ¨ 0 â‰¤ m) :
    DifferentiableAt ğ•œ (fun x => f x ^ m) a :=
  (differentiableAt_zpow.2 h).comp a hf

theorem DifferentiableOn.zpow (hf : DifferentiableOn ğ•œ f t) (h : (âˆ€ x âˆˆ t, f x â‰  0) âˆ¨ 0 â‰¤ m) :
    DifferentiableOn ğ•œ (fun x => f x ^ m) t := fun x hx =>
  (hf x hx).zpow <| h.imp_left fun h => h x hx

theorem Differentiable.zpow (hf : Differentiable ğ•œ f) (h : (âˆ€ x, f x â‰  0) âˆ¨ 0 â‰¤ m) :
    Differentiable ğ•œ fun x => f x ^ m := fun x => (hf x).zpow <| h.imp_left fun h => h x
