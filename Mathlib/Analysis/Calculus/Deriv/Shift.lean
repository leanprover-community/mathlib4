/-
Copyright (c) 2023 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll, YaÃ«l Dillies
-/
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.CompMul

/-!
### Invariance of the derivative under translation

We show that if a function `f` has derivative `f'` at a point `a + x`, then `f (a + Â·)`
has derivative `f'` at `x`. Similarly for `x + a`.
-/

open scoped Pointwise

variable {ğ•œ F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {f : ğ•œ â†’ F} {f' : F}

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_const_add (a x : ğ•œ) (hf : HasDerivAt f f' (a + x)) :
    HasDerivAt (fun x â†¦ f (a + x)) f' x := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hf <| hasDerivAt_id' x |>.const_add a

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_add_const (x a : ğ•œ) (hf : HasDerivAt f f' (x + a)) :
    HasDerivAt (fun x â†¦ f (x + a)) f' x := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hf <| hasDerivAt_id' x |>.add_const a

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_const_sub (a x : ğ•œ) (hf : HasDerivAt f f' (a - x)) :
    HasDerivAt (fun x â†¦ f (a - x)) (-f') x := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hf <| hasDerivAt_id' x |>.const_sub a

/-- Translation in the domain does not change the derivative. -/
lemma HasDerivAt.comp_sub_const (x a : ğ•œ) (hf : HasDerivAt f f' (x - a)) :
    HasDerivAt (fun x â†¦ f (x - a)) f' x := by
  simpa [Function.comp_def] using HasDerivAt.scomp (ğ•œ := ğ•œ) x hf <| hasDerivAt_id' x |>.sub_const a

variable (f)
variable (a : ğ•œ) (s : Set ğ•œ) (x : ğ•œ)

lemma derivWithin_comp_neg : derivWithin (f <| -Â·) s x = -derivWithin f (-s) (-x) := by
  simpa using derivWithin_comp_mul_left (-1) f s x

/-- The derivative of `x â†¦ f (-x)` at `a` is the negative of the derivative of `f` at `-a`. -/
lemma deriv_comp_neg : deriv (fun x â†¦ f (-x)) x = -deriv f (-x) := by
  simpa using deriv_comp_mul_left (-1) f x

/-- Translation in the domain does not change the derivative. -/
lemma derivWithin_comp_const_add :
    derivWithin (f <| a + Â·) s x = derivWithin f (a +áµ¥ s) (a + x) := by
  simp only [derivWithin, fderivWithin_comp_add_left]

/-- Translation in the domain does not change the derivative. -/
lemma deriv_comp_const_add : deriv (fun x â†¦ f (a + x)) x = deriv f (a + x) := by
  simp only [deriv, fderiv_comp_add_left]

/-- Translation in the domain does not change the derivative. -/
lemma derivWithin_comp_add_const :
    derivWithin (f <| Â· + a) s x = derivWithin f (a +áµ¥ s) (x + a) := by
  simp only [derivWithin, fderivWithin_comp_add_right]

/-- Translation in the domain does not change the derivative. -/
lemma deriv_comp_add_const : deriv (fun x â†¦ f (x + a)) x = deriv f (x + a) := by
  simpa [add_comm] using deriv_comp_const_add f a x

lemma derivWithin_comp_const_sub :
    derivWithin (f <| a - Â·) s x = -derivWithin f (a +áµ¥ -s) (a - x) := by
  simp only [sub_eq_add_neg]
  rw [derivWithin_comp_neg (f <| a + Â·), derivWithin_comp_const_add]

lemma deriv_comp_const_sub : deriv (fun x â†¦ f (a - x)) x = -deriv f (a - x) := by
  simp_rw [sub_eq_add_neg, deriv_comp_neg (f <| a + Â·), deriv_comp_const_add]

lemma derivWithin_comp_sub_const :
    derivWithin (fun x â†¦ f (x - a)) s x = derivWithin f (-a +áµ¥ s) (x - a) := by
  simp_rw [sub_eq_add_neg, derivWithin_comp_add_const]

lemma deriv_comp_sub_const : deriv (fun x â†¦ f (x - a)) x = deriv f (x - a) := by
  simp_rw [sub_eq_add_neg, deriv_comp_add_const]
