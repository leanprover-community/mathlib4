/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.Equiv

/-!
# Inverse function theorem - the easy half

In this file we prove that `g' (f x) = (f' x)â»Â¹` provided that `f` is strictly differentiable at
`x`, `f' x â‰  0`, and `g` is a local left inverse of `f` that is continuous at `f x`. This is the
easy half of the inverse function theorem: the harder half states that `g` exists.

For a more detailed overview of one-dimensional derivatives in mathlib, see the module docstring of
`Analysis/Calculus/Deriv/Basic`.

## Keywords

derivative, inverse function
-/


universe u v

open scoped Topology
open Filter Set

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {f : ğ•œ â†’ F}
variable {f' : F}
variable {s : Set ğ•œ} {x : ğ•œ} {c : F}

theorem HasStrictDerivAt.hasStrictFDerivAt_equiv {f : ğ•œ â†’ ğ•œ} {f' x : ğ•œ}
    (hf : HasStrictDerivAt f f' x) (hf' : f' â‰  0) :
    HasStrictFDerivAt f (ContinuousLinearEquiv.unitsEquivAut ğ•œ (Units.mk0 f' hf') : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
  hf

theorem HasDerivAt.hasFDerivAt_equiv {f : ğ•œ â†’ ğ•œ} {f' x : ğ•œ} (hf : HasDerivAt f f' x)
    (hf' : f' â‰  0) :
    HasFDerivAt f (ContinuousLinearEquiv.unitsEquivAut ğ•œ (Units.mk0 f' hf') : ğ•œ â†’L[ğ•œ] ğ•œ) x :=
  hf

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasStrictDerivAt.of_local_left_inverse {f g : ğ•œ â†’ ğ•œ} {f' a : ğ•œ} (hg : ContinuousAt g a)
    (hf : HasStrictDerivAt f f' (g a)) (hf' : f' â‰  0) (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
    HasStrictDerivAt g f'â»Â¹ a :=
  (hf.hasStrictFDerivAt_equiv hf').of_local_left_inverse hg hfg

/-- If `f` is an open partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has a
nonzero derivative `f'` at `f.symm a` in the strict sense, then `f.symm` has the derivative `f'â»Â¹`
at `a` in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem OpenPartialHomeomorph.hasStrictDerivAt_symm (f : OpenPartialHomeomorph ğ•œ ğ•œ) {a f' : ğ•œ}
    (ha : a âˆˆ f.target) (hf' : f' â‰  0) (htff' : HasStrictDerivAt f f' (f.symm a)) :
    HasStrictDerivAt f.symm f'â»Â¹ a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) hf' (f.eventually_right_inverse ha)

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem HasDerivAt.of_local_left_inverse {f g : ğ•œ â†’ ğ•œ} {f' a : ğ•œ} (hg : ContinuousAt g a)
    (hf : HasDerivAt f f' (g a)) (hf' : f' â‰  0) (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) :
    HasDerivAt g f'â»Â¹ a :=
  (hf.hasFDerivAt_equiv hf').of_local_left_inverse hg hfg

/-- If `f` is an open partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has a
nonzero derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem OpenPartialHomeomorph.hasDerivAt_symm (f : OpenPartialHomeomorph ğ•œ ğ•œ) {a f' : ğ•œ}
    (ha : a âˆˆ f.target) (hf' : f' â‰  0) (htff' : HasDerivAt f f' (f.symm a)) :
    HasDerivAt f.symm f'â»Â¹ a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) hf' (f.eventually_right_inverse ha)

theorem HasDerivWithinAt.eventually_ne (h : HasDerivWithinAt f f' s x) (hf' : f' â‰  0) :
    âˆ€á¶  z in ğ“[s \ {x}] x, f z â‰  c :=
  h.hasFDerivWithinAt.eventually_ne
    âŸ¨â€–f'â€–â»Â¹, fun z => by simp [norm_smul]; field_simp; rflâŸ©

theorem HasDerivAt.eventually_ne (h : HasDerivAt f f' x) (hf' : f' â‰  0) :
    âˆ€á¶  z in ğ“[â‰ ] x, f z â‰  c := by
  simpa only [compl_eq_univ_diff] using (hasDerivWithinAt_univ.2 h).eventually_ne hf'

theorem HasDerivAt.tendsto_nhdsNE (h : HasDerivAt f f' x) (hf' : f' â‰  0) :
    Tendsto f (ğ“[â‰ ] x) (ğ“[â‰ ] f x) :=
  tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ h.continuousAt.continuousWithinAt
    (h.eventually_ne hf')

@[deprecated (since := "2025-03-02")]
alias HasDerivAt.tendsto_punctured_nhds := HasDerivAt.tendsto_nhdsNE

/-- If a function is equal to a constant at a set of points that accumulates to `x` in `s`,
then its derivative within `s` at `x` equals zero,
either because it has derivative zero or because it isn't differentiable at this point. -/
theorem derivWithin_zero_of_frequently_const {c} (h : âˆƒá¶  y in ğ“[s \ {x}] x, f y = c) :
    derivWithin f s x = 0 := by
  by_cases hf : DifferentiableWithinAt ğ•œ f s x
  Â· contrapose h
    rw [not_frequently]
    exact hf.hasDerivWithinAt.eventually_ne h
  Â· exact derivWithin_zero_of_not_differentiableWithinAt hf

/-- If a function is equal to a constant at a set of points that accumulates to `x`,
then its derivative at `x` equals zero,
either because it has derivative zero or because it isn't differentiable at this point. -/
theorem deriv_zero_of_frequently_const {c} (h : âˆƒá¶  y in ğ“[â‰ ] x, f y = c) : deriv f x = 0 := by
  rw [â† derivWithin_univ, derivWithin_zero_of_frequently_const]
  rwa [â† compl_eq_univ_diff]

theorem not_differentiableWithinAt_of_local_left_inverse_hasDerivWithinAt_zero {f g : ğ•œ â†’ ğ•œ} {a : ğ•œ}
    {s t : Set ğ•œ} (ha : a âˆˆ s) (hsu : UniqueDiffWithinAt ğ•œ s a) (hf : HasDerivWithinAt f 0 t (g a))
    (hst : MapsTo g s t) (hfg : f âˆ˜ g =á¶ [ğ“[s] a] id) : Â¬DifferentiableWithinAt ğ•œ g s a := by
  intro hg
  have := (hf.comp a hg.hasDerivWithinAt hst).congr_of_eventuallyEq_of_mem hfg.symm ha
  simpa using hsu.eq_deriv _ this (hasDerivWithinAt_id _ _)

theorem not_differentiableAt_of_local_left_inverse_hasDerivAt_zero {f g : ğ•œ â†’ ğ•œ} {a : ğ•œ}
    (hf : HasDerivAt f 0 (g a)) (hfg : f âˆ˜ g =á¶ [ğ“ a] id) : Â¬DifferentiableAt ğ•œ g a := by
  intro hg
  have := (hf.comp a hg.hasDerivAt).congr_of_eventuallyEq hfg.symm
  simpa using this.unique (hasDerivAt_id a)
