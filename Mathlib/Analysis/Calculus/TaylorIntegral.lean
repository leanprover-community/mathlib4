/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.MeasureTheory.Integral.IntervalIntegral.IntegrationByParts
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.Pow
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs

/-!
# Taylor's formula with an integral remainder

In this file we prove Taylor's formula with the remainder term in integral form.

* `add_eq_sum_add_integral_iteratedFDeriv`: version for higher dimensions with `iteratedFDeriv`
* `add_eq_sum_add_integral_iteratedDeriv`: one-dimensional version with `iteratedDeriv`
-/

open Nat

variable {ùïú E F : Type*}
variable [NormedAddCommGroup E] [NormedAddCommGroup F]

section NontriviallyNormedField

variable [NontriviallyNormedField ùïú] [NormedSpace ùïú E] [NormedSpace ùïú F]

variable {f : E ‚Üí F} {x y : E} {t : ùïú} {n : ‚Ñï}

/-- The iterated derivative is given by the derivative of the `n-1` iterated derivative. -/
theorem DifferentiableAt.iteratedFDeriv_succ_apply_left' {m : Fin (n + 1) ‚Üí E}
    (hf : DifferentiableAt ùïú (iteratedFDeriv ùïú n f) x) :
    iteratedFDeriv ùïú (n + 1) f x m =
    fderiv ùïú (fun y ‚Ü¶ iteratedFDeriv ùïú n f y (Fin.tail m)) x (m 0) := by
  convert iteratedFDeriv_succ_apply_left m
  simp [fderiv_continuousMultilinear_apply_const hf]

theorem DifferentiableAt.deriv_comp_add_smul (hf : DifferentiableAt ùïú f (x + t ‚Ä¢ y)) :
    deriv (fun (s : ùïú) ‚Ü¶ f (x + s ‚Ä¢ y)) t = fderiv ùïú f (x + t ‚Ä¢ y) y := by
  have hg : Differentiable ùïú (fun (s : ùïú) ‚Ü¶ (x + s ‚Ä¢ y)) := by fun_prop
  convert fderiv_comp_deriv t hf hg.differentiableAt
  simpa using (deriv_smul_const (x := t) differentiableAt_id y).symm

theorem ContDiffAt.deriv_fderiv_add_smul (hf : ContDiffAt ùïú (n + 1) f (x + t ‚Ä¢ y)) :
    deriv (fun (s : ùïú) ‚Ü¶ iteratedFDeriv ùïú n f (x + s ‚Ä¢ y) (fun _ ‚Ü¶ y)) t =
    iteratedFDeriv ùïú (n + 1) f (x + t ‚Ä¢ y) (fun _ ‚Ü¶ y) := by
  have hf' : DifferentiableAt ùïú (iteratedFDeriv ùïú n f) (x + t ‚Ä¢ y) := by
    apply hf.differentiableAt_iteratedFDeriv
    norm_cast
    exact lt_add_one n
  convert (hf'.continuousMultilinear_apply_const _).deriv_comp_add_smul
  exact hf'.iteratedFDeriv_succ_apply_left'

end NontriviallyNormedField

variable [NormedSpace ‚Ñù E] [NormedSpace ‚Ñù F]

variable {f : E ‚Üí F} {x y : E} {n : ‚Ñï}

variable [CompleteSpace F]

/-- *Taylor's theorem with remainder in integral form*.

Version for higher dimensions. -/
theorem add_eq_sum_add_integral_iteratedFDeriv (hf : ‚àÄ (t : ‚Ñù) (_ht : t ‚àà Set.Icc 0 1),
    ContDiffAt ‚Ñù (n + 1) f (x + t ‚Ä¢ y)) :
    f (x + y) = ‚àë k ‚àà Finset.range (n + 1), (k ! : ‚Ñù)‚Åª¬π ‚Ä¢ (iteratedFDeriv ‚Ñù k f x (fun _ ‚Ü¶ y)) +
    (n ! : ‚Ñù)‚Åª¬π ‚Ä¢ ‚à´ t in 0..1, (1 - t)^n ‚Ä¢ iteratedFDeriv ‚Ñù (n + 1) f (x + t ‚Ä¢ y) (fun _ ‚Ü¶ y) := by
  simp_rw [‚Üê Set.uIcc_of_le zero_le_one] at hf
  induction n with
  | zero =>
    -- The base case follows from the fundamental theorem of calculus
    have h_eq : Set.EqOn (fun t ‚Ü¶ (fderiv ‚Ñù f (x + t ‚Ä¢ y)) y) (deriv fun (s : ‚Ñù) ‚Ü¶ f (x + s ‚Ä¢ y))
        (Set.uIcc 0 1) := by
      intro t ht
      rw [DifferentiableAt.deriv_comp_add_smul]
      exact (hf t ht).differentiableAt (le_refl _)
    simp only [zero_add, Finset.range_one, Finset.sum_singleton, factorial_zero, cast_one, inv_one,
      iteratedFDeriv_zero_apply, one_smul, pow_zero, reduceAdd, iteratedFDeriv_one_apply]
    rw [‚Üê sub_eq_iff_eq_add', Eq.comm, intervalIntegral.integral_congr h_eq]
    have hf' : ‚àÄ (t : ‚Ñù) (ht : t ‚àà Set.uIcc 0 1), DifferentiableAt ‚Ñù (fun s ‚Ü¶ f (x + s ‚Ä¢ y)) t :=
      fun t ht ‚Ü¶ ((hf t ht).differentiableAt (by simp)).comp t (by fun_prop)
    have hint : IntervalIntegrable (deriv (fun s ‚Ü¶ f (x + s ‚Ä¢ y))) MeasureTheory.volume 0 1 := by
      have h‚ÇÅ : ContinuousOn (fderiv ‚Ñù f) ((fun t ‚Ü¶ x + t ‚Ä¢ y) '' Set.uIcc (0 : ‚Ñù) 1) := by
        intro z ‚ü®t, ht, hz‚ü©
        rw [‚Üê hz]
        exact (((hf t ht).fderiv_right (le_refl _)).continuousAt (n := 0)).continuousWithinAt
      have h‚ÇÇ : ContinuousOn (fun x_1 ‚Ü¶ fderiv ‚Ñù f (x + x_1 ‚Ä¢ y)) (Set.uIcc (0 : ‚Ñù) 1) := by
        apply h‚ÇÅ.comp (t := (fun t ‚Ü¶ x + t ‚Ä¢ y) '' (Set.uIcc (0 : ‚Ñù) 1)) (by fun_prop)
        intro t ht
        use t
      apply (ContinuousOn.congr _ h_eq.symm).intervalIntegrable
      fun_prop
    simpa using intervalIntegral.integral_deriv_eq_sub hf' hint
  | succ n ih =>
    -- We use the inductive hypothesis to cancel all lower order terms
    specialize ih (fun t ht ‚Ü¶ (hf t ht).of_le (by simp))
    rw [Finset.sum_range_succ, add_assoc]
    convert ih using 2
    -- We define the functions u and v that we will integrate by parts
    set u := fun (k : ‚Ñï) (t : ‚Ñù) ‚Ü¶ (k ! : ‚Ñù)‚Åª¬π * (1 - t) ^ k
    have hu : ‚àÄ (t : ‚Ñù), HasDerivAt (u (n + 1)) (-u n t) t := by
      intro t
      unfold u
      have : (-((n ! : ‚Ñù)‚Åª¬π * (1 - t) ^ n)) =
          ((n + 1) ! : ‚Ñù)‚Åª¬π * ((n + 1) * (1 - t) ^ n * (-1)) := by
        field_simp
        congr 1
        rw [Nat.factorial_succ]
        grind
      rw [this]
      convert (((hasDerivAt_id t).const_sub 1).pow _).const_mul _
      norm_cast
    have hu' : Continuous (u n) := by fun_prop
    set v := fun (k : ‚Ñï) (t : ‚Ñù) ‚Ü¶ iteratedFDeriv ‚Ñù k f (x + t ‚Ä¢ y) (fun _ ‚Ü¶ y)
    have hv : ‚àÄ (t : ‚Ñù) (ht : t ‚àà Set.uIcc 0 1), HasDerivAt (v (n + 1)) (v (n + 1 + 1) t) t := by
      intro t ht
      unfold v
      rw [‚Üê (hf t ht).deriv_fderiv_add_smul]
      have h_diff : DifferentiableAt ‚Ñù (iteratedFDeriv ‚Ñù (n + 1) f) (x + t ‚Ä¢ y) := by
        apply (hf t ht).differentiableAt_iteratedFDeriv
        norm_cast
        grind
      refine DifferentiableAt.hasDerivAt ?_
      fun_prop
    have hv' : ContinuousOn (v (n + 1 + 1)) (Set.uIcc 0 1) := by
      intro t ht
      have h_cont : ContinuousAt (iteratedFDeriv ‚Ñù (n + 1 + 1) f) (x + t ‚Ä¢ y) :=
        ((hf t ht).iteratedFDeriv_right (i := n + 1 + 1) (m := 0) (by simp)).continuousAt
      exact (h_cont.comp (x := t) (by fun_prop)).continuousWithinAt.eval_const _
    -- Now we apply integration by parts and simplify
    simpa [‚Üê eq_neg_add_iff_add_eq, ‚Üê intervalIntegral.integral_smul, smul_smul, u, v] using
      intervalIntegral.integral_smul_deriv_eq_deriv_smul (fun t _ ‚Ü¶ hu t) hv
      (hu'.neg.intervalIntegrable _ _) hv'.intervalIntegrable

/-- *Taylor's theorem with remainder in integral form*.

Version for the 1-dimensional derivative. -/
theorem add_eq_sum_add_integral_iteratedDeriv {f : ‚Ñù ‚Üí F} {x y : ‚Ñù}
    (hf : ‚àÄ (t : ‚Ñù) (_ht : t ‚àà Set.uIcc x (x + y)), ContDiffAt ‚Ñù (n + 1) f t) :
    f (x + y) = ‚àë k ‚àà Finset.range (n + 1), (k ! : ‚Ñù)‚Åª¬π ‚Ä¢ y ^ k ‚Ä¢ iteratedDeriv k f x +
    (n ! : ‚Ñù)‚Åª¬π ‚Ä¢ ‚à´ t in 0..1, (1 - t)^n ‚Ä¢ y ^ (n + 1) ‚Ä¢ iteratedDeriv (n + 1) f (x + t * y) := by
  have hf' : ‚àÄ (t : ‚Ñù) (_ht : t ‚àà Set.Icc 0 1), ContDiffAt ‚Ñù (n + 1) f (x + t * y) := by
    intro t ‚ü®ht‚ÇÅ, ht‚ÇÇ‚ü©
    apply hf (x + t * y)
    by_cases h : 0 ‚â§ y
    ¬∑ refine Set.mem_uIcc_of_le ?_ ?_
      ¬∑ simp only [le_add_iff_nonneg_right]
        positivity
      simp only [add_le_add_iff_left]
      exact mul_le_of_le_one_left h ht‚ÇÇ
    ¬∑ simp only [not_le] at h
      refine Set.mem_uIcc_of_ge ?_ ?_
      ¬∑ simp only [add_le_add_iff_left]
        exact le_mul_of_le_one_left h.le ht‚ÇÇ
      simp only [add_le_iff_nonpos_right]
      exact mul_nonpos_of_nonneg_of_nonpos ht‚ÇÅ h.le
  simpa [iteratedFDeriv_apply_eq_iteratedDeriv_mul_prod] using
    add_eq_sum_add_integral_iteratedFDeriv hf'
