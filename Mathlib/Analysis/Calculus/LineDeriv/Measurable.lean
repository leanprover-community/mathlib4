/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.Analysis.Calculus.FDeriv.Measurable

/-! # Measurability of the line derivative

We prove in `measurable_lineDeriv` that the line derivative of a function (with respect to a
locally compact scalar field) is measurable, provided the function is continuous.

In `measurable_lineDeriv_uncurry`, assuming additionally that the source space is second countable,
we show that `(x, v) â†¦ lineDeriv ğ•œ f x v` is also measurable.

An assumption such as continuity is necessary, as otherwise one could alternate in a non-measurable
way between differentiable and non-differentiable functions along the various lines
directed by `v`.
-/

open MeasureTheory

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [LocallyCompactSpace ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [MeasurableSpace E] [OpensMeasurableSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {f : E â†’ F} {v : E}

/-!
Measurability of the line derivative `lineDeriv ğ•œ f x v` with respect to a fixed direction `v`.
-/

theorem measurableSet_lineDifferentiableAt (hf : Continuous f) :
    MeasurableSet {x : E | LineDifferentiableAt ğ•œ f x v} := by
  borelize ğ•œ
  let g : E â†’ ğ•œ â†’ F := fun x t â†¦ f (x + t â€¢ v)
  have hg : Continuous g.uncurry := by fun_prop
  exact measurable_prodMk_right (measurableSet_of_differentiableAt_with_param ğ•œ hg)

theorem measurable_lineDeriv [MeasurableSpace F] [BorelSpace F]
    (hf : Continuous f) : Measurable (fun x â†¦ lineDeriv ğ•œ f x v) := by
  borelize ğ•œ
  let g : E â†’ ğ•œ â†’ F := fun x t â†¦ f (x + t â€¢ v)
  have hg : Continuous g.uncurry := by fun_prop
  exact (measurable_deriv_with_param hg).comp measurable_prodMk_right

theorem stronglyMeasurable_lineDeriv [SecondCountableTopologyEither E F] (hf : Continuous f) :
    StronglyMeasurable (fun x â†¦ lineDeriv ğ•œ f x v) := by
  borelize ğ•œ
  let g : E â†’ ğ•œ â†’ F := fun x t â†¦ f (x + t â€¢ v)
  have hg : Continuous g.uncurry := by fun_prop
  exact (stronglyMeasurable_deriv_with_param hg).comp_measurable measurable_prodMk_right

theorem aemeasurable_lineDeriv [MeasurableSpace F] [BorelSpace F]
    (hf : Continuous f) (Î¼ : Measure E) :
    AEMeasurable (fun x â†¦ lineDeriv ğ•œ f x v) Î¼ :=
  (measurable_lineDeriv hf).aemeasurable

theorem aestronglyMeasurable_lineDeriv [SecondCountableTopologyEither E F]
    (hf : Continuous f) (Î¼ : Measure E) :
    AEStronglyMeasurable (fun x â†¦ lineDeriv ğ•œ f x v) Î¼ :=
  (stronglyMeasurable_lineDeriv hf).aestronglyMeasurable

/-!
Measurability of the line derivative `lineDeriv ğ•œ f x v` when varying both `x` and `v`. For this,
we need an additional second countability assumption on `E` to make sure that open sets are
measurable in `E Ã— E`.
-/

variable [SecondCountableTopology E]

theorem measurableSet_lineDifferentiableAt_uncurry (hf : Continuous f) :
    MeasurableSet {p : E Ã— E | LineDifferentiableAt ğ•œ f p.1 p.2} := by
  borelize ğ•œ
  let g : (E Ã— E) â†’ ğ•œ â†’ F := fun p t â†¦ f (p.1 + t â€¢ p.2)
  have : Continuous g.uncurry :=
    hf.comp <| (continuous_fst.comp continuous_fst).add
    <| continuous_snd.smul (continuous_snd.comp continuous_fst)
  have M_meas : MeasurableSet {q : (E Ã— E) Ã— ğ•œ | DifferentiableAt ğ•œ (g q.1) q.2} :=
    measurableSet_of_differentiableAt_with_param ğ•œ this
  exact measurable_prodMk_right M_meas

theorem measurable_lineDeriv_uncurry [MeasurableSpace F] [BorelSpace F]
    (hf : Continuous f) : Measurable (fun (p : E Ã— E) â†¦ lineDeriv ğ•œ f p.1 p.2) := by
  borelize ğ•œ
  let g : (E Ã— E) â†’ ğ•œ â†’ F := fun p t â†¦ f (p.1 + t â€¢ p.2)
  have : Continuous g.uncurry :=
    hf.comp <| (continuous_fst.comp continuous_fst).add
    <| continuous_snd.smul (continuous_snd.comp continuous_fst)
  exact (measurable_deriv_with_param this).comp measurable_prodMk_right

theorem stronglyMeasurable_lineDeriv_uncurry (hf : Continuous f) :
    StronglyMeasurable (fun (p : E Ã— E) â†¦ lineDeriv ğ•œ f p.1 p.2) := by
  borelize ğ•œ
  let g : (E Ã— E) â†’ ğ•œ â†’ F := fun p t â†¦ f (p.1 + t â€¢ p.2)
  have : Continuous g.uncurry :=
    hf.comp <| (continuous_fst.comp continuous_fst).add
    <| continuous_snd.smul (continuous_snd.comp continuous_fst)
  exact (stronglyMeasurable_deriv_with_param this).comp_measurable measurable_prodMk_right

theorem aemeasurable_lineDeriv_uncurry [MeasurableSpace F] [BorelSpace F]
    (hf : Continuous f) (Î¼ : Measure (E Ã— E)) :
    AEMeasurable (fun (p : E Ã— E) â†¦ lineDeriv ğ•œ f p.1 p.2) Î¼ :=
  (measurable_lineDeriv_uncurry hf).aemeasurable

theorem aestronglyMeasurable_lineDeriv_uncurry (hf : Continuous f) (Î¼ : Measure (E Ã— E)) :
    AEStronglyMeasurable (fun (p : E Ã— E) â†¦ lineDeriv ğ•œ f p.1 p.2) Î¼ :=
  (stronglyMeasurable_lineDeriv_uncurry hf).aestronglyMeasurable
