/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Slope

/-!
# Line derivatives

We define the line derivative of a function `f : E â†’ F`, at a point `x : E` along a vector `v : E`,
as the element `f' : F` such that `f (x + t â€¢ v) = f x + t â€¢ f' + o (t)` as `t` tends to `0` in
the scalar field `ğ•œ`, if it exists. It is denoted by `lineDeriv ğ•œ f x v`.

This notion is generally less well behaved than the full FrÃ©chet derivative (for instance, the
composition of functions which are line-differentiable is not line-differentiable in general).
The FrÃ©chet derivative should therefore be favored over this one in general, although the line
derivative may sometimes prove handy.

The line derivative in direction `v` is also called the Gateaux derivative in direction `v`,
although the term "Gateaux derivative" is sometimes reserved for the situation where there is
such a derivative in all directions, for the map `v â†¦ lineDeriv ğ•œ f x v` (which doesn't have to be
linear in general).

## Main definition and results

We mimic the definitions and statements for the FrÃ©chet derivative and the one-dimensional
derivative. We define in particular the following objects:

* `LineDifferentiableWithinAt ğ•œ f s x v`
* `LineDifferentiableAt ğ•œ f x v`
* `HasLineDerivWithinAt ğ•œ f f' s x v`
* `HasLineDerivAt ğ•œ f s x v`
* `lineDerivWithin ğ•œ f s x v`
* `lineDeriv ğ•œ f x v`

and develop about them a basic API inspired by the one for the FrÃ©chet derivative.

We depart from the FrÃ©chet derivative in two places, as the dependence of the following predicates
on the direction would make them barely usable:
* We do not define an analogue of the predicate `UniqueDiffOn`;
* We do not define `LineDifferentiableOn` nor `LineDifferentiable`.
-/

noncomputable section

open scoped Topology Filter ENNReal NNReal

open Filter Asymptotics Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section Module
/-!
Results that do not rely on a topological structure on `E`
-/

variable (ğ•œ)
variable {E : Type*} [AddCommGroup E] [Module ğ•œ E]

/-- `f` has the derivative `f'` at the point `x` along the direction `v` in the set `s`.
That is, `f (x + t v) = f x + t â€¢ f' + o (t)` when `t` tends to `0` and `x + t v âˆˆ s`.
Note that this definition is less well behaved than the total FrÃ©chet derivative, which
should generally be favored over this one. -/
def HasLineDerivWithinAt (f : E â†’ F) (f' : F) (s : Set E) (x : E) (v : E) :=
  HasDerivWithinAt (fun t â†¦ f (x + t â€¢ v)) f' ((fun t â†¦ x + t â€¢ v) â»Â¹' s) (0 : ğ•œ)

/-- `f` has the derivative `f'` at the point `x` along the direction `v`.
That is, `f (x + t v) = f x + t â€¢ f' + o (t)` when `t` tends to `0`.
Note that this definition is less well behaved than the total FrÃ©chet derivative, which
should generally be favored over this one. -/
def HasLineDerivAt (f : E â†’ F) (f' : F) (x : E) (v : E) :=
  HasDerivAt (fun t â†¦ f (x + t â€¢ v)) f' (0 : ğ•œ)

/-- `f` is line-differentiable at the point `x` in the direction `v` in the set `s` if there
exists `f'` such that `f (x + t v) = f x + t â€¢ f' + o (t)` when `t` tends to `0` and `x + t v âˆˆ s`.
-/
def LineDifferentiableWithinAt (f : E â†’ F) (s : Set E) (x : E) (v : E) : Prop :=
  DifferentiableWithinAt ğ•œ (fun t â†¦ f (x + t â€¢ v)) ((fun t â†¦ x + t â€¢ v) â»Â¹' s) (0 : ğ•œ)

/-- `f` is line-differentiable at the point `x` in the direction `v` if there
exists `f'` such that `f (x + t v) = f x + t â€¢ f' + o (t)` when `t` tends to `0`. -/
def LineDifferentiableAt (f : E â†’ F) (x : E) (v : E) : Prop :=
  DifferentiableAt ğ•œ (fun t â†¦ f (x + t â€¢ v)) (0 : ğ•œ)

/-- Line derivative of `f` at the point `x` in the direction `v` within the set `s`, if it exists.
Zero otherwise.

If the line derivative exists (i.e., `âˆƒ f', HasLineDerivWithinAt ğ•œ f f' s x v`), then
`f (x + t v) = f x + t lineDerivWithin ğ•œ f s x v + o (t)` when `t` tends to `0` and `x + t v âˆˆ s`.
-/
def lineDerivWithin (f : E â†’ F) (s : Set E) (x : E) (v : E) : F :=
  derivWithin (fun t â†¦ f (x + t â€¢ v)) ((fun t â†¦ x + t â€¢ v) â»Â¹' s) (0 : ğ•œ)

/-- Line derivative of `f` at the point `x` in the direction `v`, if it exists.  Zero otherwise.

If the line derivative exists (i.e., `âˆƒ f', HasLineDerivAt ğ•œ f f' x v`), then
`f (x + t v) = f x + t lineDeriv ğ•œ f x v + o (t)` when `t` tends to `0`.
-/
def lineDeriv (f : E â†’ F) (x : E) (v : E) : F :=
  deriv (fun t â†¦ f (x + t â€¢ v)) (0 : ğ•œ)

variable {ğ•œ}
variable {f fâ‚ : E â†’ F} {f' fâ‚€' fâ‚' : F} {s t : Set E} {x v : E}

lemma HasLineDerivWithinAt.mono (hf : HasLineDerivWithinAt ğ•œ f f' s x v) (hst : t âŠ† s) :
    HasLineDerivWithinAt ğ•œ f f' t x v :=
  HasDerivWithinAt.mono hf (preimage_mono hst)

lemma HasLineDerivAt.hasLineDerivWithinAt (hf : HasLineDerivAt ğ•œ f f' x v) (s : Set E) :
    HasLineDerivWithinAt ğ•œ f f' s x v :=
  HasDerivAt.hasDerivWithinAt hf

lemma HasLineDerivWithinAt.lineDifferentiableWithinAt (hf : HasLineDerivWithinAt ğ•œ f f' s x v) :
    LineDifferentiableWithinAt ğ•œ f s x v :=
  HasDerivWithinAt.differentiableWithinAt hf

theorem HasLineDerivAt.lineDifferentiableAt (hf : HasLineDerivAt ğ•œ f f' x v) :
    LineDifferentiableAt ğ•œ f x v :=
  HasDerivAt.differentiableAt hf

theorem LineDifferentiableWithinAt.hasLineDerivWithinAt (h : LineDifferentiableWithinAt ğ•œ f s x v) :
    HasLineDerivWithinAt ğ•œ f (lineDerivWithin ğ•œ f s x v) s x v :=
  DifferentiableWithinAt.hasDerivWithinAt h

theorem LineDifferentiableAt.hasLineDerivAt (h : LineDifferentiableAt ğ•œ f x v) :
    HasLineDerivAt ğ•œ f (lineDeriv ğ•œ f x v) x v :=
  DifferentiableAt.hasDerivAt h

@[simp] lemma hasLineDerivWithinAt_univ :
    HasLineDerivWithinAt ğ•œ f f' univ x v â†” HasLineDerivAt ğ•œ f f' x v := by
  simp only [HasLineDerivWithinAt, HasLineDerivAt, preimage_univ, hasDerivWithinAt_univ]

theorem lineDerivWithin_zero_of_not_lineDifferentiableWithinAt
    (h : Â¬LineDifferentiableWithinAt ğ•œ f s x v) :
    lineDerivWithin ğ•œ f s x v = 0 :=
  derivWithin_zero_of_not_differentiableWithinAt h

theorem lineDeriv_zero_of_not_lineDifferentiableAt (h : Â¬LineDifferentiableAt ğ•œ f x v) :
    lineDeriv ğ•œ f x v = 0 :=
  deriv_zero_of_not_differentiableAt h

theorem hasLineDerivAt_iff_isLittleO_nhds_zero :
    HasLineDerivAt ğ•œ f f' x v â†”
      (fun t : ğ•œ => f (x + t â€¢ v) - f x - t â€¢ f') =o[ğ“ 0] fun t => t := by
  simp only [HasLineDerivAt, hasDerivAt_iff_isLittleO_nhds_zero, zero_add, zero_smul, add_zero]

theorem HasLineDerivAt.unique (hâ‚€ : HasLineDerivAt ğ•œ f fâ‚€' x v) (hâ‚ : HasLineDerivAt ğ•œ f fâ‚' x v) :
    fâ‚€' = fâ‚' :=
  HasDerivAt.unique hâ‚€ hâ‚

protected theorem HasLineDerivAt.lineDeriv (h : HasLineDerivAt ğ•œ f f' x v) :
    lineDeriv ğ•œ f x v = f' := by
  rw [h.unique h.lineDifferentiableAt.hasLineDerivAt]

theorem lineDifferentiableWithinAt_univ :
    LineDifferentiableWithinAt ğ•œ f univ x v â†” LineDifferentiableAt ğ•œ f x v := by
  simp only [LineDifferentiableWithinAt, LineDifferentiableAt, preimage_univ,
    differentiableWithinAt_univ]

theorem LineDifferentiableAt.lineDifferentiableWithinAt (h : LineDifferentiableAt ğ•œ f x v) :
    LineDifferentiableWithinAt ğ•œ f s x v :=
  (differentiableWithinAt_univ.2 h).mono (subset_univ _)

@[simp]
theorem lineDerivWithin_univ : lineDerivWithin ğ•œ f univ x v = lineDeriv ğ•œ f x v := by
  simp [lineDerivWithin, lineDeriv]

theorem LineDifferentiableWithinAt.mono (h : LineDifferentiableWithinAt ğ•œ f t x v) (st : s âŠ† t) :
    LineDifferentiableWithinAt ğ•œ f s x v :=
  (h.hasLineDerivWithinAt.mono st).lineDifferentiableWithinAt

theorem HasLineDerivWithinAt.congr_mono (h : HasLineDerivWithinAt ğ•œ f f' s x v) (ht : EqOn fâ‚ f t)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : HasLineDerivWithinAt ğ•œ fâ‚ f' t x v :=
  HasDerivWithinAt.congr_mono h (fun y hy â†¦ ht hy) (by simpa using hx) (preimage_mono hâ‚)

theorem HasLineDerivWithinAt.congr (h : HasLineDerivWithinAt ğ•œ f f' s x v) (hs : EqOn fâ‚ f s)
    (hx : fâ‚ x = f x) : HasLineDerivWithinAt ğ•œ fâ‚ f' s x v :=
  h.congr_mono hs hx (Subset.refl _)

theorem HasLineDerivWithinAt.congr' (h : HasLineDerivWithinAt ğ•œ f f' s x v)
    (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) :
    HasLineDerivWithinAt ğ•œ fâ‚ f' s x v :=
  h.congr hs (hs hx)

theorem LineDifferentiableWithinAt.congr_mono (h : LineDifferentiableWithinAt ğ•œ f s x v)
    (ht : EqOn fâ‚ f t) (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) :
    LineDifferentiableWithinAt ğ•œ fâ‚ t x v :=
  (HasLineDerivWithinAt.congr_mono h.hasLineDerivWithinAt ht hx hâ‚).differentiableWithinAt

theorem LineDifferentiableWithinAt.congr (h : LineDifferentiableWithinAt ğ•œ f s x v)
    (ht : âˆ€ x âˆˆ s, fâ‚ x = f x) (hx : fâ‚ x = f x) :
    LineDifferentiableWithinAt ğ•œ fâ‚ s x v :=
  LineDifferentiableWithinAt.congr_mono h ht hx (Subset.refl _)

theorem lineDerivWithin_congr (hs : EqOn fâ‚ f s) (hx : fâ‚ x = f x) :
    lineDerivWithin ğ•œ fâ‚ s x v = lineDerivWithin ğ•œ f s x v :=
  derivWithin_congr (fun y hy â†¦ hs hy) (by simpa using hx)

theorem lineDerivWithin_congr' (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) :
    lineDerivWithin ğ•œ fâ‚ s x v = lineDerivWithin ğ•œ f s x v :=
  lineDerivWithin_congr hs (hs hx)

theorem hasLineDerivAt_iff_tendsto_slope_zero :
    HasLineDerivAt ğ•œ f f' x v â†”
      Tendsto (fun (t : ğ•œ) â†¦ tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) (ğ“[â‰ ] 0) (ğ“ f') := by
  simp only [HasLineDerivAt, hasDerivAt_iff_tendsto_slope_zero, zero_add,
    zero_smul, add_zero]

alias âŸ¨HasLineDerivAt.tendsto_slope_zero, _âŸ© := hasLineDerivAt_iff_tendsto_slope_zero

theorem HasLineDerivAt.tendsto_slope_zero_right [PartialOrder ğ•œ] (h : HasLineDerivAt ğ•œ f f' x v) :
    Tendsto (fun (t : ğ•œ) â†¦ tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) (ğ“[>] 0) (ğ“ f') :=
  h.tendsto_slope_zero.mono_left (nhds_right'_le_nhds_ne 0)

theorem HasLineDerivAt.tendsto_slope_zero_left [PartialOrder ğ•œ] (h : HasLineDerivAt ğ•œ f f' x v) :
    Tendsto (fun (t : ğ•œ) â†¦ tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) (ğ“[<] 0) (ğ“ f') :=
  h.tendsto_slope_zero.mono_left (nhds_left'_le_nhds_ne 0)

end Module

section NormedSpace

/-!
Results that need a normed space structure on `E`
-/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f fâ‚€ fâ‚ : E â†’ F} {f' : F} {s t : Set E} {x v : E} {L : E â†’L[ğ•œ] F}

theorem HasLineDerivWithinAt.mono_of_mem
    (h : HasLineDerivWithinAt ğ•œ f f' t x v) (hst : t âˆˆ ğ“[s] x) :
    HasLineDerivWithinAt ğ•œ f f' s x v := by
  apply HasDerivWithinAt.mono_of_mem h
  apply ContinuousWithinAt.preimage_mem_nhdsWithin'' _ hst (by simp)
  apply Continuous.continuousWithinAt; continuity

theorem HasLineDerivWithinAt.hasLineDerivAt
    (h : HasLineDerivWithinAt ğ•œ f f' s x v) (hs : s âˆˆ ğ“ x) :
    HasLineDerivAt ğ•œ f f' x v := by
  rw [â† hasLineDerivWithinAt_univ]
  rw [â† nhdsWithin_univ] at hs
  exact h.mono_of_mem hs

theorem LineDifferentiableWithinAt.lineDifferentiableAt (h : LineDifferentiableWithinAt ğ•œ f s x v)
    (hs : s âˆˆ ğ“ x) : LineDifferentiableAt ğ•œ f x v :=
  (h.hasLineDerivWithinAt.hasLineDerivAt hs).lineDifferentiableAt

lemma HasFDerivWithinAt.hasLineDerivWithinAt (hf : HasFDerivWithinAt f L s x) (v : E) :
    HasLineDerivWithinAt ğ•œ f (L v) s x v := by
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  rw [show x = F (0 : ğ•œ) by simp [F]] at hf
  have A : HasDerivWithinAt F (0 + (1 : ğ•œ) â€¢ v) (F â»Â¹' s) 0 :=
    ((hasDerivAt_const (0 : ğ•œ) x).add ((hasDerivAt_id' (0 : ğ•œ)).smul_const v)).hasDerivWithinAt
  simp only [one_smul, zero_add] at A
  exact hf.comp_hasDerivWithinAt (x := (0 : ğ•œ)) A (mapsTo_preimage F s)

lemma HasFDerivAt.hasLineDerivAt (hf : HasFDerivAt f L x) (v : E) :
    HasLineDerivAt ğ•œ f (L v) x v := by
  rw [â† hasLineDerivWithinAt_univ]
  exact hf.hasFDerivWithinAt.hasLineDerivWithinAt v

lemma DifferentiableAt.lineDeriv_eq_fderiv (hf : DifferentiableAt ğ•œ f x) :
    lineDeriv ğ•œ f x v = fderiv ğ•œ f x v :=
  (hf.hasFDerivAt.hasLineDerivAt v).lineDeriv

theorem LineDifferentiableWithinAt.mono_of_mem (h : LineDifferentiableWithinAt ğ•œ f s x v)
    (hst : s âˆˆ ğ“[t] x) : LineDifferentiableWithinAt ğ•œ f t x v :=
  (h.hasLineDerivWithinAt.mono_of_mem hst).lineDifferentiableWithinAt

theorem lineDerivWithin_of_mem_nhds (h : s âˆˆ ğ“ x) :
    lineDerivWithin ğ•œ f s x v = lineDeriv ğ•œ f x v := by
  apply derivWithin_of_mem_nhds
  apply (Continuous.continuousAt _).preimage_mem_nhds (by simpa using h)
  continuity

theorem lineDerivWithin_of_isOpen (hs : IsOpen s) (hx : x âˆˆ s) :
    lineDerivWithin ğ•œ f s x v = lineDeriv ğ•œ f x v :=
  lineDerivWithin_of_mem_nhds (hs.mem_nhds hx)

theorem hasLineDerivWithinAt_congr_set (h : s =á¶ [ğ“ x] t) :
    HasLineDerivWithinAt ğ•œ f f' s x v â†” HasLineDerivWithinAt ğ•œ f f' t x v := by
  apply hasDerivWithinAt_congr_set
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  have B : ContinuousAt F 0 := by apply Continuous.continuousAt; continuity
  have : s =á¶ [ğ“ (F 0)] t := by convert h; simp [F]
  exact B.preimage_mem_nhds this

theorem lineDifferentiableWithinAt_congr_set (h : s =á¶ [ğ“ x] t) :
    LineDifferentiableWithinAt ğ•œ f s x v â†” LineDifferentiableWithinAt ğ•œ f t x v :=
  âŸ¨fun h' â†¦ ((hasLineDerivWithinAt_congr_set h).1
    h'.hasLineDerivWithinAt).lineDifferentiableWithinAt,
  fun h' â†¦ ((hasLineDerivWithinAt_congr_set h.symm).1
    h'.hasLineDerivWithinAt).lineDifferentiableWithinAtâŸ©

theorem lineDerivWithin_congr_set (h : s =á¶ [ğ“ x] t) :
    lineDerivWithin ğ•œ f s x v = lineDerivWithin ğ•œ f t x v := by
  apply derivWithin_congr_set
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  have B : ContinuousAt F 0 := by apply Continuous.continuousAt; continuity
  have : s =á¶ [ğ“ (F 0)] t := by convert h; simp [F]
  exact B.preimage_mem_nhds this

theorem Filter.EventuallyEq.hasLineDerivAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) :
    HasLineDerivAt ğ•œ fâ‚€ f' x v â†” HasLineDerivAt ğ•œ fâ‚ f' x v := by
  apply hasDerivAt_iff
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  have B : ContinuousAt F 0 := by apply Continuous.continuousAt; continuity
  have : fâ‚€ =á¶ [ğ“ (F 0)] fâ‚ := by convert h; simp [F]
  exact B.preimage_mem_nhds this

theorem Filter.EventuallyEq.lineDifferentiableAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) :
    LineDifferentiableAt ğ•œ fâ‚€ x v â†” LineDifferentiableAt ğ•œ fâ‚ x v :=
  âŸ¨fun h' â†¦ (h.hasLineDerivAt_iff.1 h'.hasLineDerivAt).lineDifferentiableAt,
  fun h' â†¦ (h.hasLineDerivAt_iff.2 h'.hasLineDerivAt).lineDifferentiableAtâŸ©

theorem Filter.EventuallyEq.hasLineDerivWithinAt_iff (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : fâ‚€ x = fâ‚ x) :
    HasLineDerivWithinAt ğ•œ fâ‚€ f' s x v â†” HasLineDerivWithinAt ğ•œ fâ‚ f' s x v := by
  apply hasDerivWithinAt_iff
  Â· have A : Continuous (fun (t : ğ•œ) â†¦ x + t â€¢ v) := by continuity
    exact A.continuousWithinAt.preimage_mem_nhdsWithin'' h (by simp)
  Â· simpa using hx

theorem Filter.EventuallyEq.hasLineDerivWithinAt_iff_of_mem (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : x âˆˆ s) :
    HasLineDerivWithinAt ğ•œ fâ‚€ f' s x v â†” HasLineDerivWithinAt ğ•œ fâ‚ f' s x v :=
  h.hasLineDerivWithinAt_iff (h.eq_of_nhdsWithin hx)

theorem Filter.EventuallyEq.lineDifferentiableWithinAt_iff
    (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : fâ‚€ x = fâ‚ x) :
    LineDifferentiableWithinAt ğ•œ fâ‚€ s x v â†” LineDifferentiableWithinAt ğ•œ fâ‚ s x v :=
  âŸ¨fun h' â†¦ ((h.hasLineDerivWithinAt_iff hx).1 h'.hasLineDerivWithinAt).lineDifferentiableWithinAt,
  fun h' â†¦ ((h.hasLineDerivWithinAt_iff hx).2 h'.hasLineDerivWithinAt).lineDifferentiableWithinAtâŸ©

theorem Filter.EventuallyEq.lineDifferentiableWithinAt_iff_of_mem
    (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : x âˆˆ s) :
    LineDifferentiableWithinAt ğ•œ fâ‚€ s x v â†” LineDifferentiableWithinAt ğ•œ fâ‚ s x v :=
  h.lineDifferentiableWithinAt_iff (h.eq_of_nhdsWithin hx)

lemma HasLineDerivWithinAt.congr_of_eventuallyEq (hf : HasLineDerivWithinAt ğ•œ f f' s x v)
    (h'f : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : HasLineDerivWithinAt ğ•œ fâ‚ f' s x v := by
  apply HasDerivWithinAt.congr_of_eventuallyEq hf _ (by simp [hx])
  have A : Continuous (fun (t : ğ•œ) â†¦ x + t â€¢ v) := by continuity
  exact A.continuousWithinAt.preimage_mem_nhdsWithin'' h'f (by simp)

theorem HasLineDerivAt.congr_of_eventuallyEq (h : HasLineDerivAt ğ•œ f f' x v) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    HasLineDerivAt ğ•œ fâ‚ f' x v := by
  apply HasDerivAt.congr_of_eventuallyEq h
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  rw [show x = F 0 by simp [F]] at hâ‚
  exact (Continuous.continuousAt (by continuity)).preimage_mem_nhds hâ‚

theorem LineDifferentiableWithinAt.congr_of_eventuallyEq (h : LineDifferentiableWithinAt ğ•œ f s x v)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : LineDifferentiableWithinAt ğ•œ fâ‚ s x v :=
  (h.hasLineDerivWithinAt.congr_of_eventuallyEq hâ‚ hx).differentiableWithinAt

theorem LineDifferentiableAt.congr_of_eventuallyEq
    (h : LineDifferentiableAt ğ•œ f x v) (hL : fâ‚ =á¶ [ğ“ x] f) :
    LineDifferentiableAt ğ•œ fâ‚ x v := by
  apply DifferentiableAt.congr_of_eventuallyEq h
  let F := fun (t : ğ•œ) â†¦ x + t â€¢ v
  rw [show x = F 0 by simp [F]] at hL
  exact (Continuous.continuousAt (by continuity)).preimage_mem_nhds hL

theorem Filter.EventuallyEq.lineDerivWithin_eq (hs : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    lineDerivWithin ğ•œ fâ‚ s x v = lineDerivWithin ğ•œ f s x v := by
  apply derivWithin_eq ?_ (by simpa using hx)
  have A : Continuous (fun (t : ğ•œ) â†¦ x + t â€¢ v) := by continuity
  exact A.continuousWithinAt.preimage_mem_nhdsWithin'' hs (by simp)

theorem Filter.EventuallyEq.lineDerivWithin_eq_nhds (h : fâ‚ =á¶ [ğ“ x] f) :
    lineDerivWithin ğ•œ fâ‚ s x v = lineDerivWithin ğ•œ f s x v :=
  (h.filter_mono nhdsWithin_le_nhds).lineDerivWithin_eq h.self_of_nhds

theorem Filter.EventuallyEq.lineDeriv_eq (h : fâ‚ =á¶ [ğ“ x] f) :
    lineDeriv ğ•œ fâ‚ x v = lineDeriv ğ•œ f x v := by
  rw [â† lineDerivWithin_univ, â† lineDerivWithin_univ, h.lineDerivWithin_eq_nhds]

/-- Converse to the mean value inequality: if `f` is line differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its line derivative at `xâ‚€` in the direction `v` has norm
bounded by `C * â€–vâ€–`. This version only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a
neighborhood of `x`. -/
theorem HasLineDerivAt.le_of_lip' {f : E â†’ F} {f' : F} {xâ‚€ : E} (hf : HasLineDerivAt ğ•œ f f' xâ‚€ v)
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) :
    â€–f'â€– â‰¤ C * â€–vâ€– := by
  apply HasDerivAt.le_of_lip' hf (by positivity)
  have A : Continuous (fun (t : ğ•œ) â†¦ xâ‚€ + t â€¢ v) := by continuity
  have : âˆ€á¶  x in ğ“ (xâ‚€ + (0 : ğ•œ) â€¢ v), â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€– := by simpa using hlip
  filter_upwards [(A.continuousAt (x := 0)).preimage_mem_nhds this] with t ht
  simp only [preimage_setOf_eq, add_sub_cancel_left, norm_smul, mem_setOf_eq, mul_comm (â€–tâ€–)] at ht
  simpa [mul_assoc] using ht

/-- Converse to the mean value inequality: if `f` is line differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its line derivative at `xâ‚€` in the direction `v` has norm
bounded by `C * â€–vâ€–`. This version only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a
neighborhood of `x`. -/
theorem HasLineDerivAt.le_of_lipschitzOn
    {f : E â†’ F} {f' : F} {xâ‚€ : E} (hf : HasLineDerivAt ğ•œ f f' xâ‚€ v)
    {s : Set E} (hs : s âˆˆ ğ“ xâ‚€) {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) :
    â€–f'â€– â‰¤ C * â€–vâ€– := by
  refine hf.le_of_lip' C.coe_nonneg ?_
  filter_upwards [hs] with x hx using hlip.norm_sub_le hx (mem_of_mem_nhds hs)

/-- Converse to the mean value inequality: if `f` is line differentiable at `xâ‚€` and `C`-lipschitz
then its line derivative at `xâ‚€` in the direction `v` has norm bounded by `C * â€–vâ€–`. -/
theorem HasLineDerivAt.le_of_lipschitz
    {f : E â†’ F} {f' : F} {xâ‚€ : E} (hf : HasLineDerivAt ğ•œ f f' xâ‚€ v)
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–f'â€– â‰¤ C * â€–vâ€– :=
  hf.le_of_lipschitzOn univ_mem (lipschitzOn_univ.2 hlip)

variable (ğ•œ)

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz
on a neighborhood of `xâ‚€` then its line derivative at `xâ‚€` in the direction `v` has norm
bounded by `C * â€–vâ€–`. This version only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a
neighborhood of `x`.
Version using `lineDeriv`. -/
theorem norm_lineDeriv_le_of_lip' {f : E â†’ F} {xâ‚€ : E}
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) :
    â€–lineDeriv ğ•œ f xâ‚€ vâ€– â‰¤ C * â€–vâ€– := by
  apply norm_deriv_le_of_lip' (by positivity)
  have A : Continuous (fun (t : ğ•œ) â†¦ xâ‚€ + t â€¢ v) := by continuity
  have : âˆ€á¶  x in ğ“ (xâ‚€ + (0 : ğ•œ) â€¢ v), â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€– := by simpa using hlip
  filter_upwards [(A.continuousAt (x := 0)).preimage_mem_nhds this] with t ht
  simp only [preimage_setOf_eq, add_sub_cancel_left, norm_smul, mem_setOf_eq, mul_comm (â€–tâ€–)] at ht
  simpa [mul_assoc] using ht

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz on a neighborhood of `xâ‚€`
then its line derivative at `xâ‚€` in the direction `v` has norm bounded by `C * â€–vâ€–`.
Version using `lineDeriv`. -/
theorem norm_lineDeriv_le_of_lipschitzOn {f : E â†’ F} {xâ‚€ : E} {s : Set E} (hs : s âˆˆ ğ“ xâ‚€)
    {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) : â€–lineDeriv ğ•œ f xâ‚€ vâ€– â‰¤ C * â€–vâ€– := by
  refine norm_lineDeriv_le_of_lip' ğ•œ C.coe_nonneg ?_
  filter_upwards [hs] with x hx using hlip.norm_sub_le hx (mem_of_mem_nhds hs)

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz then
its line derivative at `xâ‚€` in the direction `v` has norm bounded by `C * â€–vâ€–`.
Version using `lineDeriv`. -/
theorem norm_lineDeriv_le_of_lipschitz {f : E â†’ F} {xâ‚€ : E}
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–lineDeriv ğ•œ f xâ‚€ vâ€– â‰¤ C * â€–vâ€– :=
  norm_lineDeriv_le_of_lipschitzOn ğ•œ univ_mem (lipschitzOn_univ.2 hlip)

variable {ğ•œ}

end NormedSpace

section Zero

variable {E : Type*} [AddCommGroup E] [Module ğ•œ E] {f : E â†’ F} {s : Set E} {x : E}

theorem hasLineDerivWithinAt_zero : HasLineDerivWithinAt ğ•œ f 0 s x 0 := by
  simp [HasLineDerivWithinAt, hasDerivWithinAt_const]

theorem hasLineDerivAt_zero : HasLineDerivAt ğ•œ f 0 x 0 := by
  simp [HasLineDerivAt, hasDerivAt_const]

theorem lineDifferentiableWithinAt_zero : LineDifferentiableWithinAt ğ•œ f s x 0 :=
  hasLineDerivWithinAt_zero.lineDifferentiableWithinAt

theorem lineDifferentiableAt_zero : LineDifferentiableAt ğ•œ f x 0 :=
  hasLineDerivAt_zero.lineDifferentiableAt

theorem lineDeriv_zero : lineDeriv ğ•œ f x 0 = 0 :=
  hasLineDerivAt_zero.lineDeriv

end Zero

section CompRight

variable {E : Type*} [AddCommGroup E] [Module ğ•œ E]
  {E' : Type*} [AddCommGroup E'] [Module ğ•œ E']
  {f : E â†’ F} {f' : F} {x v : E'} {L : E' â†’â‚—[ğ•œ] E}

theorem HasLineDerivAt.of_comp {v : E'} (hf : HasLineDerivAt ğ•œ (f âˆ˜ L) f' x v) :
    HasLineDerivAt ğ•œ f f' (L x) (L v) := by
  simpa [HasLineDerivAt] using hf

theorem LineDifferentiableAt.of_comp {v : E'} (hf : LineDifferentiableAt ğ•œ (f âˆ˜ L) x v) :
    LineDifferentiableAt ğ•œ f (L x) (L v) :=
  hf.hasLineDerivAt.of_comp.lineDifferentiableAt

end CompRight

section SMul

variable {E : Type*} [AddCommGroup E] [Module ğ•œ E] {f : E â†’ F} {s : Set E} {x v : E} {f' : F}

theorem HasLineDerivWithinAt.smul (h : HasLineDerivWithinAt ğ•œ f f' s x v) (c : ğ•œ) :
    HasLineDerivWithinAt ğ•œ f (c â€¢ f') s x (c â€¢ v) := by
  simp only [HasLineDerivWithinAt] at h âŠ¢
  let g := fun (t : ğ•œ) â†¦ c â€¢ t
  let s' := (fun (t : ğ•œ) â†¦ x + t â€¢ v) â»Â¹' s
  have A : HasDerivAt g c 0 := by simpa using (hasDerivAt_id (0 : ğ•œ)).const_smul c
  have B : HasDerivWithinAt (fun t â†¦ f (x + t â€¢ v)) f' s' (g 0) := by simpa [g] using h
  have Z := B.scomp (0 : ğ•œ) A.hasDerivWithinAt (mapsTo_preimage g s')
  simp only [g, s', Function.comp, smul_eq_mul, mul_comm c, â† smul_smul] at Z
  convert Z
  ext t
  simp [â† smul_smul]

theorem hasLineDerivWithinAt_smul_iff {c : ğ•œ} (hc : c â‰  0) :
    HasLineDerivWithinAt ğ•œ f (c â€¢ f') s x (c â€¢ v) â†” HasLineDerivWithinAt ğ•œ f f' s x v :=
  âŸ¨fun h â†¦ by simpa [smul_smul, inv_mul_cancel hc] using h.smul (c â»Â¹), fun h â†¦ h.smul câŸ©

theorem HasLineDerivAt.smul (h : HasLineDerivAt ğ•œ f f' x v) (c : ğ•œ) :
    HasLineDerivAt ğ•œ f (c â€¢ f') x (c â€¢ v) := by
  simp only [â† hasLineDerivWithinAt_univ] at h âŠ¢
  exact HasLineDerivWithinAt.smul h c

theorem hasLineDerivAt_smul_iff {c : ğ•œ} (hc : c â‰  0) :
    HasLineDerivAt ğ•œ f (c â€¢ f') x (c â€¢ v) â†” HasLineDerivAt ğ•œ f f' x v :=
  âŸ¨fun h â†¦ by simpa [smul_smul, inv_mul_cancel hc] using h.smul (c â»Â¹), fun h â†¦ h.smul câŸ©

theorem LineDifferentiableWithinAt.smul (h : LineDifferentiableWithinAt ğ•œ f s x v) (c : ğ•œ) :
    LineDifferentiableWithinAt ğ•œ f s x (c â€¢ v) :=
  (h.hasLineDerivWithinAt.smul c).lineDifferentiableWithinAt

theorem lineDifferentiableWithinAt_smul_iff {c : ğ•œ} (hc : c â‰  0) :
    LineDifferentiableWithinAt ğ•œ f s x (c â€¢ v) â†” LineDifferentiableWithinAt ğ•œ f s x v :=
  âŸ¨fun h â†¦ by simpa [smul_smul, inv_mul_cancel hc] using h.smul (c â»Â¹), fun h â†¦ h.smul câŸ©

theorem LineDifferentiableAt.smul (h : LineDifferentiableAt ğ•œ f x v) (c : ğ•œ) :
    LineDifferentiableAt ğ•œ f x (c â€¢ v) :=
  (h.hasLineDerivAt.smul c).lineDifferentiableAt

theorem lineDifferentiableAt_smul_iff {c : ğ•œ} (hc : c â‰  0) :
    LineDifferentiableAt ğ•œ f x (c â€¢ v) â†” LineDifferentiableAt ğ•œ f x v :=
  âŸ¨fun h â†¦ by simpa [smul_smul, inv_mul_cancel hc] using h.smul (c â»Â¹), fun h â†¦ h.smul câŸ©

theorem lineDeriv_smul {c : ğ•œ} : lineDeriv ğ•œ f x (c â€¢ v) = c â€¢ lineDeriv ğ•œ f x v := by
  rcases eq_or_ne c 0 with rfl|hc
  Â· simp [lineDeriv_zero]
  by_cases H : LineDifferentiableAt ğ•œ f x v
  Â· exact (H.hasLineDerivAt.smul c).lineDeriv
  Â· have H' : Â¬ (LineDifferentiableAt ğ•œ f x (c â€¢ v)) := by
      simpa [lineDifferentiableAt_smul_iff hc] using H
    simp [lineDeriv_zero_of_not_lineDifferentiableAt, H, H']

theorem lineDeriv_neg : lineDeriv ğ•œ f x (-v) = - lineDeriv ğ•œ f x v := by
  rw [â† neg_one_smul (R := ğ•œ) v, lineDeriv_smul, neg_one_smul]

end SMul
