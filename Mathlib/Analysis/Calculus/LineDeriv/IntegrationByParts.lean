/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.MeasureTheory.Integral.IntegralEqImproper

/-!
# Integration by parts for line derivatives

Let `f, g : E â†’ â„` be two differentiable functions on a real vector space endowed with a Haar
measure. Then `âˆ« f * g' = - âˆ« f' * g`, where `f'` and `g'` denote the derivatives of `f` and `g`
in a given direction `v`, provided that `f * g`, `f' * g` and `f * g'` are all integrable.

In this file, we prove this theorem as well as more general versions where the multiplication is
replaced by a general continuous bilinear form, giving versions both for the line derivative and
the FrÃ©chet derivative. These results are derived from the one-dimensional version and a Fubini
argument.

## Main statements

* `integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable`: integration by parts
  in terms of line derivatives, with `HasLineDerivAt` assumptions and general bilinear form.
* `integral_bilinear_hasFDerivAt_right_eq_neg_left_of_integrable`: integration by parts
  in terms of FrÃ©chet derivatives, with `HasFDerivAt` assumptions and general bilinear form.
* `integral_bilinear_fderiv_right_eq_neg_left_of_integrable`: integration by parts
  in terms of FrÃ©chet derivatives, written with `fderiv` assumptions and general bilinear form.
* `integral_smul_fderiv_eq_neg_fderiv_smul_of_integrable`: integration by parts for scalar
  action, in terms of FrÃ©chet derivatives, written with `fderiv` assumptions.
* `integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable`: integration by parts for scalar
  multiplication, in terms of FrÃ©chet derivatives, written with `fderiv` assumptions.

## Implementation notes

A standard set of assumptions for integration by parts in a finite-dimensional real vector
space (without boundary term) is that the functions tend to zero at infinity and have integrable
derivatives. In this file, we instead assume that the functions are integrable and have integrable
derivatives. These sets of assumptions are not directly comparable (an integrable function with
integrable derivative does *not* have to tend to zero at infinity). The one we use is geared
towards applications to Fourier transforms.

TODO: prove similar theorems assuming that the functions tend to zero at infinity and have
integrable derivatives.
-/

open MeasureTheory Measure Module Topology

variable {E F G W : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup F]
  [NormedSpace â„ F] [NormedAddCommGroup G] [NormedSpace â„ G] [NormedAddCommGroup W]
  [NormedSpace â„ W] [MeasurableSpace E] {Î¼ : Measure E}

lemma integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable_aux1 [SigmaFinite Î¼]
    {f f' : E Ã— â„ â†’ F} {g g' : E Ã— â„ â†’ G} {B : F â†’L[â„] G â†’L[â„] W}
    (hf'g : Integrable (fun x â†¦ B (f' x) (g x)) (Î¼.prod volume))
    (hfg' : Integrable (fun x â†¦ B (f x) (g' x)) (Î¼.prod volume))
    (hfg : Integrable (fun x â†¦ B (f x) (g x)) (Î¼.prod volume))
    (hf : âˆ€ x, HasLineDerivAt â„ f (f' x) x (0, 1)) (hg : âˆ€ x, HasLineDerivAt â„ g (g' x) x (0, 1)) :
    âˆ« x, B (f x) (g' x) âˆ‚(Î¼.prod volume) = - âˆ« x, B (f' x) (g x) âˆ‚(Î¼.prod volume) := calc
  âˆ« x, B (f x) (g' x) âˆ‚(Î¼.prod volume)
    = âˆ« x, (âˆ« t, B (f (x, t)) (g' (x, t))) âˆ‚Î¼ := integral_prod _ hfg'
  _ = âˆ« x, (- âˆ« t, B (f' (x, t)) (g (x, t))) âˆ‚Î¼ := by
    apply integral_congr_ae
    filter_upwards [hf'g.prod_right_ae, hfg'.prod_right_ae, hfg.prod_right_ae]
      with x hf'gx hfg'x hfgx
    apply integral_bilinear_hasDerivAt_right_eq_neg_left_of_integrable ?_ ?_ hfg'x hf'gx hfgx
    Â· intro t
      convert (hf (x, t)).scomp_of_eq t ((hasDerivAt_id t).add (hasDerivAt_const t (-t))) (by simp)
        <;> simp
    Â· intro t
      convert (hg (x, t)).scomp_of_eq t ((hasDerivAt_id t).add (hasDerivAt_const t (-t))) (by simp)
        <;> simp
  _ = - âˆ« x, B (f' x) (g x) âˆ‚(Î¼.prod volume) := by rw [integral_neg, integral_prod _ hf'g]

variable [BorelSpace E]

lemma integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable_aux2
    [FiniteDimensional â„ E] {Î¼ : Measure (E Ã— â„)} [IsAddHaarMeasure Î¼]
    {f f' : E Ã— â„ â†’ F} {g g' : E Ã— â„ â†’ G} {B : F â†’L[â„] G â†’L[â„] W}
    (hf'g : Integrable (fun x â†¦ B (f' x) (g x)) Î¼)
    (hfg' : Integrable (fun x â†¦ B (f x) (g' x)) Î¼)
    (hfg : Integrable (fun x â†¦ B (f x) (g x)) Î¼)
    (hf : âˆ€ x, HasLineDerivAt â„ f (f' x) x (0, 1)) (hg : âˆ€ x, HasLineDerivAt â„ g (g' x) x (0, 1)) :
    âˆ« x, B (f x) (g' x) âˆ‚Î¼ = - âˆ« x, B (f' x) (g x) âˆ‚Î¼ := by
  let Î½ : Measure E := addHaar
  have A : Î½.prod volume = (addHaarScalarFactor (Î½.prod volume) Î¼) â€¢ Î¼ :=
    isAddLeftInvariant_eq_smul _ _
  have Hf'g : Integrable (fun x â†¦ B (f' x) (g x)) (Î½.prod volume) := by
    rw [A]; exact hf'g.smul_measure_nnreal
  have Hfg' : Integrable (fun x â†¦ B (f x) (g' x)) (Î½.prod volume) := by
    rw [A]; exact hfg'.smul_measure_nnreal
  have Hfg : Integrable (fun x â†¦ B (f x) (g x)) (Î½.prod volume) := by
    rw [A]; exact hfg.smul_measure_nnreal
  rw [isAddLeftInvariant_eq_smul Î¼ (Î½.prod volume)]
  simp [integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable_aux1 Hf'g Hfg' Hfg hf hg]

variable [FiniteDimensional â„ E] [IsAddHaarMeasure Î¼]

/-- **Integration by parts for line derivatives**
Version with a general bilinear form `B`.
If `B f g` is integrable, as well as `B f' g` and `B f g'` where `f'` and `g'` are derivatives
of `f` and `g` in a given direction `v`, then `âˆ« B f g' = - âˆ« B f' g`. -/
theorem integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable
    {f f' : E â†’ F} {g g' : E â†’ G} {v : E} {B : F â†’L[â„] G â†’L[â„] W}
    (hf'g : Integrable (fun x â†¦ B (f' x) (g x)) Î¼) (hfg' : Integrable (fun x â†¦ B (f x) (g' x)) Î¼)
    (hfg : Integrable (fun x â†¦ B (f x) (g x)) Î¼)
    (hf : âˆ€ x, HasLineDerivAt â„ f (f' x) x v) (hg : âˆ€ x, HasLineDerivAt â„ g (g' x) x v) :
    âˆ« x, B (f x) (g' x) âˆ‚Î¼ = - âˆ« x, B (f' x) (g x) âˆ‚Î¼ := by
  by_cases hW : CompleteSpace W; swap
  Â· simp [integral, hW]
  rcases eq_or_ne v 0 with rfl | hv
  Â· have Hf' x : f' x = 0 := by
      simpa [(hasLineDerivAt_zero (f := f) (x := x)).lineDeriv] using (hf x).lineDeriv.symm
    have Hg' x : g' x = 0 := by
      simpa [(hasLineDerivAt_zero (f := g) (x := x)).lineDeriv] using (hg x).lineDeriv.symm
    simp [Hf', Hg']
  have : Nontrivial E := nontrivial_iff.2 âŸ¨v, 0, hvâŸ©
  let n := finrank â„ E
  let E' := Fin (n - 1) â†’ â„
  obtain âŸ¨L, hLâŸ© : âˆƒ L : E â‰ƒL[â„] (E' Ã— â„), L v = (0, 1) := by
    have : finrank â„ (E' Ã— â„) = n := by simpa [this, E'] using Nat.sub_add_cancel finrank_pos
    have Lâ‚€ : E â‰ƒL[â„] (E' Ã— â„) := (ContinuousLinearEquiv.ofFinrankEq this).symm
    obtain âŸ¨M, hMâŸ© : âˆƒ M : (E' Ã— â„) â‰ƒL[â„] (E' Ã— â„), M (Lâ‚€ v) = (0, 1) := by
      apply SeparatingDual.exists_continuousLinearEquiv_apply_eq
      Â· simpa using hv
      Â· simp
    exact âŸ¨Lâ‚€.trans M, by simp [hM]âŸ©
  let Î½ := Measure.map L Î¼
  suffices H : âˆ« (x : E' Ã— â„), (B (f (L.symm x))) (g' (L.symm x)) âˆ‚Î½ =
      -âˆ« (x : E' Ã— â„), (B (f' (L.symm x))) (g (L.symm x)) âˆ‚Î½ by
    have : Î¼ = Measure.map L.symm Î½ := by
      simp [Î½, Measure.map_map L.symm.continuous.measurable L.continuous.measurable]
    have hL : IsClosedEmbedding L.symm := L.symm.toHomeomorph.isClosedEmbedding
    simpa [this, hL.integral_map] using H
  have L_emb : MeasurableEmbedding L := L.toHomeomorph.measurableEmbedding
  apply integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable_aux2
  Â· simpa [Î½, L_emb.integrable_map_iff, Function.comp_def] using hf'g
  Â· simpa [Î½, L_emb.integrable_map_iff, Function.comp_def] using hfg'
  Â· simpa [Î½, L_emb.integrable_map_iff, Function.comp_def] using hfg
  Â· intro x
    have : f = (f âˆ˜ L.symm) âˆ˜ (L : E â†’â‚—[â„] (E' Ã— â„)) := by ext y; simp
    specialize hf (L.symm x)
    rw [this] at hf
    convert hf.of_comp using 1
    Â· simp
    Â· simp [â† hL]
  Â· intro x
    have : g = (g âˆ˜ L.symm) âˆ˜ (L : E â†’â‚—[â„] (E' Ã— â„)) := by ext y; simp
    specialize hg (L.symm x)
    rw [this] at hg
    convert hg.of_comp using 1
    Â· simp
    Â· simp [â† hL]

/-- **Integration by parts for FrÃ©chet derivatives**
Version with a general bilinear form `B`.
If `B f g` is integrable, as well as `B f' g` and `B f g'` where `f'` and `g'` are derivatives
of `f` and `g` in a given direction `v`, then `âˆ« B f g' = - âˆ« B f' g`. -/
theorem integral_bilinear_hasFDerivAt_right_eq_neg_left_of_integrable
    {f : E â†’ F} {f' : E â†’ (E â†’L[â„] F)}
    {g : E â†’ G} {g' : E â†’ (E â†’L[â„] G)} {v : E} {B : F â†’L[â„] G â†’L[â„] W}
    (hf'g : Integrable (fun x â†¦ B (f' x v) (g x)) Î¼)
    (hfg' : Integrable (fun x â†¦ B (f x) (g' x v)) Î¼)
    (hfg : Integrable (fun x â†¦ B (f x) (g x)) Î¼)
    (hf : âˆ€ x, HasFDerivAt f (f' x) x) (hg : âˆ€ x, HasFDerivAt g (g' x) x) :
    âˆ« x, B (f x) (g' x v) âˆ‚Î¼ = - âˆ« x, B (f' x v) (g x) âˆ‚Î¼ :=
  integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable hf'g hfg' hfg
    (fun x â†¦ (hf x).hasLineDerivAt v) (fun x â†¦ (hg x).hasLineDerivAt v)

/-- **Integration by parts for FrÃ©chet derivatives**
Version with a general bilinear form `B`.
If `B f g` is integrable, as well as `B f' g` and `B f g'` where `f'` and `g'` are the derivatives
of `f` and `g` in a given direction `v`, then `âˆ« B f g' = - âˆ« B f' g`. -/
theorem integral_bilinear_fderiv_right_eq_neg_left_of_integrable
    {f : E â†’ F} {g : E â†’ G} {v : E} {B : F â†’L[â„] G â†’L[â„] W}
    (hf'g : Integrable (fun x â†¦ B (fderiv â„ f x v) (g x)) Î¼)
    (hfg' : Integrable (fun x â†¦ B (f x) (fderiv â„ g x v)) Î¼)
    (hfg : Integrable (fun x â†¦ B (f x) (g x)) Î¼)
    (hf : Differentiable â„ f) (hg : Differentiable â„ g) :
    âˆ« x, B (f x) (fderiv â„ g x v) âˆ‚Î¼ = - âˆ« x, B (fderiv â„ f x v) (g x) âˆ‚Î¼ :=
  integral_bilinear_hasFDerivAt_right_eq_neg_left_of_integrable hf'g hfg' hfg
    (fun x â†¦ (hf x).hasFDerivAt) (fun x â†¦ (hg x).hasFDerivAt)

variable {ğ•œ : Type*} [NormedField ğ•œ] [NormedAlgebra â„ ğ•œ]
    [NormedSpace ğ•œ G] [IsScalarTower â„ ğ•œ G]

/-- **Integration by parts for FrÃ©chet derivatives**
Version with a scalar function: `âˆ« f â€¢ g' = - âˆ« f' â€¢ g` when `f â€¢ g'` and `f' â€¢ g` and `f â€¢ g`
are integrable, where `f'` and `g'` are the derivatives of `f` and `g` in a given direction `v`. -/
theorem integral_smul_fderiv_eq_neg_fderiv_smul_of_integrable
    {f : E â†’ ğ•œ} {g : E â†’ G} {v : E}
    (hf'g : Integrable (fun x â†¦ fderiv â„ f x v â€¢ g x) Î¼)
    (hfg' : Integrable (fun x â†¦ f x â€¢ fderiv â„ g x v) Î¼)
    (hfg : Integrable (fun x â†¦ f x â€¢ g x) Î¼)
    (hf : Differentiable â„ f) (hg : Differentiable â„ g) :
    âˆ« x, f x â€¢ fderiv â„ g x v âˆ‚Î¼ = - âˆ« x, fderiv â„ f x v â€¢ g x âˆ‚Î¼ :=
  integral_bilinear_fderiv_right_eq_neg_left_of_integrable
    (B := ContinuousLinearMap.lsmul â„ ğ•œ) hf'g hfg' hfg hf hg

/-- **Integration by parts for FrÃ©chet derivatives**
Version with two scalar functions: `âˆ« f * g' = - âˆ« f' * g` when `f * g'` and `f' * g` and `f * g`
are integrable, where `f'` and `g'` are the derivatives of `f` and `g` in a given direction `v`. -/
theorem integral_mul_fderiv_eq_neg_fderiv_mul_of_integrable
    {f : E â†’ ğ•œ} {g : E â†’ ğ•œ} {v : E}
    (hf'g : Integrable (fun x â†¦ fderiv â„ f x v * g x) Î¼)
    (hfg' : Integrable (fun x â†¦ f x * fderiv â„ g x v) Î¼)
    (hfg : Integrable (fun x â†¦ f x * g x) Î¼)
    (hf : Differentiable â„ f) (hg : Differentiable â„ g) :
    âˆ« x, f x * fderiv â„ g x v âˆ‚Î¼ = - âˆ« x, fderiv â„ f x v * g x âˆ‚Î¼ :=
  integral_bilinear_fderiv_right_eq_neg_left_of_integrable
    (B := ContinuousLinearMap.mul â„ ğ•œ) hf'g hfg' hfg hf hg
