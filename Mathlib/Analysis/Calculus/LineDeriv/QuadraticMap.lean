/-
Copyright (c) 2024 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.LinearAlgebra.QuadraticForm.Basic

/-!
# Quadratic forms are line (Gateaux) differentiable

In this file we prove that a quadratic form is line differentiable,
with the line derivative given by the polar bilinear form.
Note that this statement does not need topology on the domain.
In particular, it applies to discontinuous quadratic forms on infinite-dimensional spaces.
-/

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace QuadraticMap

theorem hasLineDerivAt (f : QuadraticMap ğ•œ E F) (a b : E) :
    HasLineDerivAt ğ•œ f (polar f a b) a b := by
  simpa [HasLineDerivAt, QuadraticMap.map_add, f.map_smul] using
    ((hasDerivAt_const (0 : ğ•œ) (f a)).add <|
      ((hasDerivAt_id 0).mul (hasDerivAt_id 0)).smul (hasDerivAt_const 0 (f b))).add
      ((hasDerivAt_id 0).smul (hasDerivAt_const 0 (polar f a b)))

theorem lineDifferentiableAt (f : QuadraticMap ğ•œ E F) (a b : E) : LineDifferentiableAt ğ•œ f a b :=
  (f.hasLineDerivAt a b).lineDifferentiableAt

@[simp]
protected theorem lineDeriv (f : QuadraticMap ğ•œ E F) : lineDeriv ğ•œ f = polar f := by
  ext a b
  exact (f.hasLineDerivAt a b).lineDeriv

end QuadraticMap
