/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Mul

/-!
# Slope of a differentiable function

Given a function `f : ğ•œ â†’ E` from a nontrivially normed field to a normed space over this field,
`dslope f a b` is defined as `slope f a b = (b - a)â»Â¹ â€¢ (f b - f a)` for `a â‰  b` and as `deriv f a`
for `a = b`.

In this file we define `dslope` and prove some basic lemmas about its continuity and
differentiability.
-/

open scoped Topology Filter

open Function Set Filter

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

open Classical in
/-- `dslope f a b` is defined as `slope f a b = (b - a)â»Â¹ â€¢ (f b - f a)` for `a â‰  b` and
`deriv f a` for `a = b`. -/
noncomputable def dslope (f : ğ•œ â†’ E) (a : ğ•œ) : ğ•œ â†’ E :=
  update (slope f a) a (deriv f a)

@[simp]
theorem dslope_same (f : ğ•œ â†’ E) (a : ğ•œ) : dslope f a a = deriv f a := by
  classical
  exact update_self ..

variable {f : ğ•œ â†’ E} {a b : ğ•œ} {s : Set ğ•œ}

theorem dslope_of_ne (f : ğ•œ â†’ E) (h : b â‰  a) : dslope f a b = slope f a b := by
  classical
  exact update_of_ne h ..

theorem ContinuousLinearMap.dslope_comp {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    (f : E â†’L[ğ•œ] F) (g : ğ•œ â†’ E) (a b : ğ•œ) (H : a = b â†’ DifferentiableAt ğ•œ g a) :
    dslope (f âˆ˜ g) a b = f (dslope g a b) := by
  rcases eq_or_ne b a with (rfl | hne)
  Â· simp only [dslope_same]
    exact (f.hasFDerivAt.comp_hasDerivAt b (H rfl).hasDerivAt).deriv
  Â· simpa only [dslope_of_ne _ hne] using f.toLinearMap.slope_comp g a b

theorem eqOn_dslope_slope (f : ğ•œ â†’ E) (a : ğ•œ) : EqOn (dslope f a) (slope f a) {a}á¶œ := fun _ =>
  dslope_of_ne f

theorem dslope_eventuallyEq_slope_of_ne (f : ğ•œ â†’ E) (h : b â‰  a) : dslope f a =á¶ [ğ“ b] slope f a :=
  (eqOn_dslope_slope f a).eventuallyEq_of_mem (isOpen_ne.mem_nhds h)

theorem dslope_eventuallyEq_slope_nhdsNE (f : ğ•œ â†’ E) : dslope f a =á¶ [ğ“[â‰ ] a] slope f a :=
  (eqOn_dslope_slope f a).eventuallyEq_of_mem self_mem_nhdsWithin

@[deprecated (since := "2025-03-02")]
alias dslope_eventuallyEq_slope_punctured_nhds := dslope_eventuallyEq_slope_nhdsNE

@[simp]
theorem sub_smul_dslope (f : ğ•œ â†’ E) (a b : ğ•œ) : (b - a) â€¢ dslope f a b = f b - f a := by
  rcases eq_or_ne b a with (rfl | hne) <;> simp [dslope_of_ne, *]

theorem dslope_sub_smul_of_ne (f : ğ•œ â†’ E) (h : b â‰  a) :
    dslope (fun x => (x - a) â€¢ f x) a b = f b := by
  rw [dslope_of_ne _ h, slope_sub_smul _ h.symm]

theorem eqOn_dslope_sub_smul (f : ğ•œ â†’ E) (a : ğ•œ) :
    EqOn (dslope (fun x => (x - a) â€¢ f x) a) f {a}á¶œ := fun _ => dslope_sub_smul_of_ne f

theorem dslope_sub_smul [DecidableEq ğ•œ] (f : ğ•œ â†’ E) (a : ğ•œ) :
    dslope (fun x => (x - a) â€¢ f x) a = update f a (deriv (fun x => (x - a) â€¢ f x) a) :=
  eq_update_iff.2 âŸ¨dslope_same _ _, eqOn_dslope_sub_smul f aâŸ©

@[simp]
theorem continuousAt_dslope_same : ContinuousAt (dslope f a) a â†” DifferentiableAt ğ•œ f a := by
  simp only [dslope, continuousAt_update_same, â† hasDerivAt_deriv_iff, hasDerivAt_iff_tendsto_slope]

theorem ContinuousWithinAt.of_dslope (h : ContinuousWithinAt (dslope f a) s b) :
    ContinuousWithinAt f s b := by
  have : ContinuousWithinAt (fun x => (x - a) â€¢ dslope f a x + f a) s b :=
    ((continuousWithinAt_id.sub continuousWithinAt_const).smul h).add continuousWithinAt_const
  simpa only [sub_smul_dslope, sub_add_cancel] using this

theorem ContinuousAt.of_dslope (h : ContinuousAt (dslope f a) b) : ContinuousAt f b :=
  (continuousWithinAt_univ _ _).1 h.continuousWithinAt.of_dslope

theorem ContinuousOn.of_dslope (h : ContinuousOn (dslope f a) s) : ContinuousOn f s := fun x hx =>
  (h x hx).of_dslope

theorem continuousWithinAt_dslope_of_ne (h : b â‰  a) :
    ContinuousWithinAt (dslope f a) s b â†” ContinuousWithinAt f s b := by
  refine âŸ¨ContinuousWithinAt.of_dslope, fun hc => ?_âŸ©
  classical
  simp only [dslope, continuousWithinAt_update_of_ne h]
  exact ((continuousWithinAt_id.sub continuousWithinAt_const).invâ‚€ (sub_ne_zero.2 h)).smul
    (hc.sub continuousWithinAt_const)

theorem continuousAt_dslope_of_ne (h : b â‰  a) : ContinuousAt (dslope f a) b â†” ContinuousAt f b := by
  simp only [â† continuousWithinAt_univ, continuousWithinAt_dslope_of_ne h]

theorem continuousOn_dslope (h : s âˆˆ ğ“ a) :
    ContinuousOn (dslope f a) s â†” ContinuousOn f s âˆ§ DifferentiableAt ğ•œ f a := by
  refine âŸ¨fun hc => âŸ¨hc.of_dslope, continuousAt_dslope_same.1 <| hc.continuousAt hâŸ©, ?_âŸ©
  rintro âŸ¨hc, hdâŸ© x hx
  rcases eq_or_ne x a with (rfl | hne)
  exacts [(continuousAt_dslope_same.2 hd).continuousWithinAt,
    (continuousWithinAt_dslope_of_ne hne).2 (hc x hx)]

theorem DifferentiableWithinAt.of_dslope (h : DifferentiableWithinAt ğ•œ (dslope f a) s b) :
    DifferentiableWithinAt ğ•œ f s b := by
  simpa only [id, sub_smul_dslope f a, sub_add_cancel] using
    ((differentiableWithinAt_id.sub_const a).smul h).add_const (f a)

theorem DifferentiableAt.of_dslope (h : DifferentiableAt ğ•œ (dslope f a) b) :
    DifferentiableAt ğ•œ f b :=
  differentiableWithinAt_univ.1 h.differentiableWithinAt.of_dslope

theorem DifferentiableOn.of_dslope (h : DifferentiableOn ğ•œ (dslope f a) s) :
    DifferentiableOn ğ•œ f s := fun x hx => (h x hx).of_dslope

theorem differentiableWithinAt_dslope_of_ne (h : b â‰  a) :
    DifferentiableWithinAt ğ•œ (dslope f a) s b â†” DifferentiableWithinAt ğ•œ f s b := by
  refine âŸ¨DifferentiableWithinAt.of_dslope, fun hd => ?_âŸ©
  refine (((differentiableWithinAt_id.sub_const a).inv (sub_ne_zero.2 h)).smul
    (hd.sub_const (f a))).congr_of_eventuallyEq ?_ (dslope_of_ne _ h)
  refine (eqOn_dslope_slope _ _).eventuallyEq_of_mem ?_
  exact mem_nhdsWithin_of_mem_nhds (isOpen_ne.mem_nhds h)

theorem differentiableOn_dslope_of_notMem (h : a âˆ‰ s) :
    DifferentiableOn ğ•œ (dslope f a) s â†” DifferentiableOn ğ•œ f s :=
  forall_congr' fun _ =>
    forall_congr' fun hx => differentiableWithinAt_dslope_of_ne <| ne_of_mem_of_not_mem hx h

@[deprecated (since := "2025-05-24")]
alias differentiableOn_dslope_of_nmem := differentiableOn_dslope_of_notMem

theorem differentiableAt_dslope_of_ne (h : b â‰  a) :
    DifferentiableAt ğ•œ (dslope f a) b â†” DifferentiableAt ğ•œ f b := by
  simp only [â† differentiableWithinAt_univ, differentiableWithinAt_dslope_of_ne h]
