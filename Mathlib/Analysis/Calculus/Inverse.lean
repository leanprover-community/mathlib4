/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth, S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.ContDiff.FiniteDimension
import Mathlib.Analysis.Calculus.ContDiff.IsROrC
import Mathlib.Analysis.NormedSpace.Banach

#align_import analysis.calculus.inverse from "leanprover-community/mathlib"@"2c1d8ca2812b64f88992a5294ea3dba144755cd1"

/-!
# Inverse function theorem

In this file we prove the inverse function theorem. It says that if a map `f : E ‚Üí F`
has an invertible strict derivative `f'` at `a`, then it is locally invertible,
and the inverse function has derivative `f' ‚Åª¬π`.

We define `HasStrictDerivAt.toLocalHomeomorph` that repacks a function `f`
with a `hf : HasStrictFDerivAt f f' a`, `f' : E ‚âÉL[ùïú] F`, into a `LocalHomeomorph`.
The `toFun` of this `LocalHomeomorph` is defeq to `f`, so one can apply theorems
about `LocalHomeomorph` to `hf.toLocalHomeomorph f`, and get statements about `f`.

Then we define `HasStrictFDerivAt.localInverse` to be the `invFun` of this `LocalHomeomorph`,
and prove two versions of the inverse function theorem:

* `HasStrictFDerivAt.to_localInverse`: if `f` has an invertible derivative `f'` at `a` in the
  strict sense (`hf`), then `hf.localInverse f f' a` has derivative `f'.symm` at `f a` in the
  strict sense;

* `HasStrictFDerivAt.to_local_left_inverse`: if `f` has an invertible derivative `f'` at `a` in
  the strict sense and `g` is locally left inverse to `f` near `a`, then `g` has derivative
  `f'.symm` at `f a` in the strict sense.

In the one-dimensional case we reformulate these theorems in terms of `HasStrictDerivAt` and
`f'‚Åª¬π`.

We also reformulate the theorems in terms of `ContDiff`, to give that `C^k` (respectively,
smooth) inputs give `C^k` (smooth) inverses.  These versions require that continuous
differentiability implies strict differentiability; this is false over a general field, true over
`‚Ñù` or `‚ÑÇ` and implemented here assuming `IsROrC ùïÇ`.

Some related theorems, providing the derivative and higher regularity assuming that we already know
the inverse function, are formulated in the `Analysis/Calculus/FDeriv` and `Analysis/Calculus/Deriv`
folders, and in `ContDiff.lean`.

## Notations

In the section about `ApproximatesLinearOn` we introduce some `local notation` to make formulas
shorter:

* by `N` we denote `‚Äñf'‚Åª¬π‚Äñ`;
* by `g` we denote the auxiliary contracting map `x ‚Ü¶ x + f'.symm (y - f x)` used to prove that
  `{x | f x = y}` is nonempty.

## Tags

derivative, strictly differentiable, continuously differentiable, smooth, inverse function
-/


open Function Set Filter Metric

open scoped Topology Classical NNReal

noncomputable section

variable {ùïú : Type*} [NontriviallyNormedField ùïú]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]

variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ùïú G']

variable {Œµ : ‚Ñù}

open Asymptotics Filter Metric Set

open ContinuousLinearMap (id)

/-!
### Non-linear maps close to affine maps

In this section we study a map `f` such that `‚Äñf x - f y - f' (x - y)‚Äñ ‚â§ c * ‚Äñx - y‚Äñ` on an open set
`s`, where `f' : E ‚ÜíL[ùïú] F` is a continuous linear map and `c` is suitably small. Maps of this type
behave like `f a + f' (x - a)` near each `a ‚àà s`.

When `f'` is onto, we show that `f` is locally onto.

When `f'` is a continuous linear equiv, we show that `f` is a homeomorphism
between `s` and `f '' s`. More precisely, we define `ApproximatesLinearOn.toLocalHomeomorph` to
be a `LocalHomeomorph` with `toFun = f`, `source = s`, and `target = f '' s`.

Maps of this type naturally appear in the proof of the inverse function theorem (see next section),
and `ApproximatesLinearOn.toLocalHomeomorph` will imply that the locally inverse function
exists.

We define this auxiliary notion to split the proof of the inverse function theorem into small
lemmas. This approach makes it possible

- to prove a lower estimate on the size of the domain of the inverse function;

- to reuse parts of the proofs in the case if a function is not strictly differentiable. E.g., for a
  function `f : E √ó F ‚Üí G` with estimates on `f x y‚ÇÅ - f x y‚ÇÇ` but not on `f x‚ÇÅ y - f x‚ÇÇ y`.
-/


/-- We say that `f` approximates a continuous linear map `f'` on `s` with constant `c`,
if `‚Äñf x - f y - f' (x - y)‚Äñ ‚â§ c * ‚Äñx - y‚Äñ` whenever `x, y ‚àà s`.

This predicate is defined to facilitate the splitting of the inverse function theorem into small
lemmas. Some of these lemmas can be useful, e.g., to prove that the inverse function is defined
on a specific set. -/
def ApproximatesLinearOn (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (s : Set E) (c : ‚Ñù‚â•0) : Prop :=
  ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, ‚Äñf x - f y - f' (x - y)‚Äñ ‚â§ c * ‚Äñx - y‚Äñ
#align approximates_linear_on ApproximatesLinearOn

@[simp]
theorem approximatesLinearOn_empty (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (c : ‚Ñù‚â•0) :
    ApproximatesLinearOn f f' ‚àÖ c := by simp [ApproximatesLinearOn]
#align approximates_linear_on_empty approximatesLinearOn_empty

namespace ApproximatesLinearOn

variable [CompleteSpace E] {f : E ‚Üí F}

/-! First we prove some properties of a function that `ApproximatesLinearOn` a (not necessarily
invertible) continuous linear map. -/


section

variable {f' : E ‚ÜíL[ùïú] F} {s t : Set E} {c c' : ‚Ñù‚â•0}

theorem mono_num (hc : c ‚â§ c') (hf : ApproximatesLinearOn f f' s c) :
    ApproximatesLinearOn f f' s c' := fun x hx y hy =>
  le_trans (hf x hx y hy) (mul_le_mul_of_nonneg_right hc <| norm_nonneg _)
#align approximates_linear_on.mono_num ApproximatesLinearOn.mono_num

theorem mono_set (hst : s ‚äÜ t) (hf : ApproximatesLinearOn f f' t c) :
    ApproximatesLinearOn f f' s c := fun x hx y hy => hf x (hst hx) y (hst hy)
#align approximates_linear_on.mono_set ApproximatesLinearOn.mono_set

theorem approximatesLinearOn_iff_lipschitzOnWith {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {s : Set E}
    {c : ‚Ñù‚â•0} : ApproximatesLinearOn f f' s c ‚Üî LipschitzOnWith c (f - ‚áëf') s := by
  have : ‚àÄ x y, f x - f y - f' (x - y) = (f - f') x - (f - f') y := fun x y ‚Ü¶ by
    simp only [map_sub, Pi.sub_apply]; abel
  simp only [this, lipschitzOnWith_iff_norm_sub_le, ApproximatesLinearOn]
#align approximates_linear_on.approximates_linear_on_iff_lipschitz_on_with ApproximatesLinearOn.approximatesLinearOn_iff_lipschitzOnWith

alias ‚ü®lipschitzOnWith, _root_.LipschitzOnWith.approximatesLinearOn‚ü© :=
  approximatesLinearOn_iff_lipschitzOnWith
#align approximates_linear_on.lipschitz_on_with ApproximatesLinearOn.lipschitzOnWith
#align lipschitz_on_with.approximates_linear_on LipschitzOnWith.approximatesLinearOn

theorem lipschitz_sub (hf : ApproximatesLinearOn f f' s c) :
    LipschitzWith c fun x : s => f x - f' x :=
  hf.lipschitzOnWith.to_restrict
#align approximates_linear_on.lipschitz_sub ApproximatesLinearOn.lipschitz_sub

protected theorem lipschitz (hf : ApproximatesLinearOn f f' s c) :
    LipschitzWith (‚Äñf'‚Äñ‚Çä + c) (s.restrict f) := by
  simpa only [restrict_apply, add_sub_cancel'_right] using
    (f'.lipschitz.restrict s).add hf.lipschitz_sub
#align approximates_linear_on.lipschitz ApproximatesLinearOn.lipschitz

protected theorem continuous (hf : ApproximatesLinearOn f f' s c) : Continuous (s.restrict f) :=
  hf.lipschitz.continuous
#align approximates_linear_on.continuous ApproximatesLinearOn.continuous

protected theorem continuousOn (hf : ApproximatesLinearOn f f' s c) : ContinuousOn f s :=
  continuousOn_iff_continuous_restrict.2 hf.continuous
#align approximates_linear_on.continuous_on ApproximatesLinearOn.continuousOn

end

section LocallyOnto

/-!
We prove that a function which is linearly approximated by a continuous linear map with a nonlinear
right inverse is locally onto. This will apply to the case where the approximating map is a linear
equivalence, for the local inverse theorem, but also whenever the approximating map is onto,
by Banach's open mapping theorem. -/


variable {s : Set E} {c : ‚Ñù‚â•0} {f' : E ‚ÜíL[ùïú] F}

/-- If a function is linearly approximated by a continuous linear map with a (possibly nonlinear)
right inverse, then it is locally onto: a ball of an explicit radius is included in the image
of the map. -/
theorem surjOn_closedBall_of_nonlinearRightInverse (hf : ApproximatesLinearOn f f' s c)
    (f'symm : f'.NonlinearRightInverse) {Œµ : ‚Ñù} {b : E} (Œµ0 : 0 ‚â§ Œµ) (hŒµ : closedBall b Œµ ‚äÜ s) :
    SurjOn f (closedBall b Œµ) (closedBall (f b) (((f'symm.nnnorm : ‚Ñù)‚Åª¬π - c) * Œµ)) := by
  intro y hy
  cases' le_or_lt (f'symm.nnnorm : ‚Ñù)‚Åª¬π c with hc hc
  ¬∑ refine' ‚ü®b, by simp [Œµ0], _‚ü©
    have : dist y (f b) ‚â§ 0 :=
      (mem_closedBall.1 hy).trans (mul_nonpos_of_nonpos_of_nonneg (by linarith) Œµ0)
    simp only [dist_le_zero] at this
    rw [this]
  have If' : (0 : ‚Ñù) < f'symm.nnnorm := by rw [‚Üê inv_pos]; exact (NNReal.coe_nonneg _).trans_lt hc
  have Icf' : (c : ‚Ñù) * f'symm.nnnorm < 1 := by rwa [inv_eq_one_div, lt_div_iff If'] at hc
  have Jf' : (f'symm.nnnorm : ‚Ñù) ‚â† 0 := ne_of_gt If'
  have Jcf' : (1 : ‚Ñù) - c * f'symm.nnnorm ‚â† 0 := by apply ne_of_gt; linarith
  /- We have to show that `y` can be written as `f x` for some `x ‚àà closedBall b Œµ`.
    The idea of the proof is to apply the Banach contraction principle to the map
    `g : x ‚Ü¶ x + f'symm (y - f x)`, as a fixed point of this map satisfies `f x = y`.
    When `f'symm` is a genuine linear inverse, `g` is a contracting map. In our case, since `f'symm`
    is nonlinear, this map is not contracting (it is not even continuous), but still the proof of
    the contraction theorem holds: `u‚Çô = g‚Åø b` is a Cauchy sequence, converging exponentially fast
    to the desired point `x`. Instead of appealing to general results, we check this by hand.

    The main point is that `f (u n)` becomes exponentially close to `y`, and therefore
    `dist (u (n+1)) (u n)` becomes exponentally small, making it possible to get an inductive
    bound on `dist (u n) b`, from which one checks that `u n` stays in the ball on which one has a
    control. Therefore, the bound can be checked at the next step, and so on inductively.
    -/
  set g := fun x => x + f'symm (y - f x) with hg
  set u := fun n : ‚Ñï => g^[n] b with hu
  have usucc : ‚àÄ n, u (n + 1) = g (u n) := by simp [hu, ‚Üê iterate_succ_apply' g _ b]
  -- First bound: if `f z` is close to `y`, then `g z` is close to `z` (i.e., almost a fixed point).
  have A : ‚àÄ z, dist (g z) z ‚â§ f'symm.nnnorm * dist (f z) y := by
    intro z
    rw [dist_eq_norm, hg, add_sub_cancel', dist_eq_norm']
    exact f'symm.bound _
  -- Second bound: if `z` and `g z` are in the set with good control, then `f (g z)` becomes closer
  -- to `y` than `f z` was (this uses the linear approximation property, and is the reason for the
  -- choice of the formula for `g`).
  have B :
    ‚àÄ z ‚àà closedBall b Œµ,
      g z ‚àà closedBall b Œµ ‚Üí dist (f (g z)) y ‚â§ c * f'symm.nnnorm * dist (f z) y := by
    intro z hz hgz
    set v := f'symm (y - f z)
    calc
      dist (f (g z)) y = ‚Äñf (z + v) - y‚Äñ := by rw [dist_eq_norm]
      _ = ‚Äñf (z + v) - f z - f' v + f' v - (y - f z)‚Äñ := by congr 1; abel
      _ = ‚Äñf (z + v) - f z - f' (z + v - z)‚Äñ := by
        simp only [ContinuousLinearMap.NonlinearRightInverse.right_inv, add_sub_cancel',
          sub_add_cancel]
      _ ‚â§ c * ‚Äñz + v - z‚Äñ := (hf _ (hŒµ hgz) _ (hŒµ hz))
      _ ‚â§ c * (f'symm.nnnorm * dist (f z) y) := by
        gcongr
        simpa [dist_eq_norm'] using f'symm.bound (y - f z)
      _ = c * f'symm.nnnorm * dist (f z) y := by ring
  -- Third bound: a complicated bound on `dist w b` (that will show up in the induction) is enough
  -- to check that `w` is in the ball on which one has controls. Will be used to check that `u n`
  -- belongs to this ball for all `n`.
  have C : ‚àÄ (n : ‚Ñï) (w : E), dist w b ‚â§ f'symm.nnnorm * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n) /
      (1 - c * f'symm.nnnorm) * dist (f b) y ‚Üí w ‚àà closedBall b Œµ := fun n w hw ‚Ü¶ by
    apply hw.trans
    rw [div_mul_eq_mul_div, div_le_iff]; swap; ¬∑ linarith
    calc
      (f'symm.nnnorm : ‚Ñù) * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n) * dist (f b) y =
          f'symm.nnnorm * dist (f b) y * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n) :=
        by ring
      _ ‚â§ f'symm.nnnorm * dist (f b) y * 1 := by
        gcongr
        rw [sub_le_self_iff]
        positivity
      _ ‚â§ f'symm.nnnorm * (((f'symm.nnnorm : ‚Ñù)‚Åª¬π - c) * Œµ) := by
        rw [mul_one]
        gcongr
        exact mem_closedBall'.1 hy
      _ = Œµ * (1 - c * f'symm.nnnorm) := by field_simp; ring

  /- Main inductive control: `f (u n)` becomes exponentially close to `y`, and therefore
    `dist (u (n+1)) (u n)` becomes exponentally small, making it possible to get an inductive
    bound on `dist (u n) b`, from which one checks that `u n` remains in the ball on which we
    have estimates. -/
  have D : ‚àÄ n : ‚Ñï, dist (f (u n)) y ‚â§ ((c : ‚Ñù) * f'symm.nnnorm) ^ n * dist (f b) y ‚àß
      dist (u n) b ‚â§ f'symm.nnnorm * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n) /
        (1 - (c : ‚Ñù) * f'symm.nnnorm) * dist (f b) y := fun n ‚Ü¶ by
    induction' n with n IH; ¬∑ simp [hu, le_refl]
    rw [usucc]
    have Ign : dist (g (u n)) b ‚â§ f'symm.nnnorm * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n.succ) /
        (1 - c * f'symm.nnnorm) * dist (f b) y :=
      calc
        dist (g (u n)) b ‚â§ dist (g (u n)) (u n) + dist (u n) b := dist_triangle _ _ _
        _ ‚â§ f'symm.nnnorm * dist (f (u n)) y + dist (u n) b := (add_le_add (A _) le_rfl)
        _ ‚â§ f'symm.nnnorm * (((c : ‚Ñù) * f'symm.nnnorm) ^ n * dist (f b) y) +
              f'symm.nnnorm * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n) / (1 - c * f'symm.nnnorm) *
                dist (f b) y := by gcongr; exact IH.1; exact IH.2
        _ = f'symm.nnnorm * (1 - ((c : ‚Ñù) * f'symm.nnnorm) ^ n.succ) /
              (1 - (c : ‚Ñù) * f'symm.nnnorm) * dist (f b) y := by
          field_simp [Jcf', pow_succ]; ring
    refine' ‚ü®_, Ign‚ü©
    calc
      dist (f (g (u n))) y ‚â§ c * f'symm.nnnorm * dist (f (u n)) y :=
        B _ (C n _ IH.2) (C n.succ _ Ign)
      _ ‚â§ (c : ‚Ñù) * f'symm.nnnorm * (((c : ‚Ñù) * f'symm.nnnorm) ^ n * dist (f b) y) := by
        gcongr
        apply IH.1
      _ = ((c : ‚Ñù) * f'symm.nnnorm) ^ n.succ * dist (f b) y := by simp only [pow_succ']; ring
  -- Deduce from the inductive bound that `u‚Çô` is a Cauchy sequence, therefore converging.
  have : CauchySeq u
  ¬∑ refine cauchySeq_of_le_geometric _ (‚Üëf'symm.nnnorm * dist (f b) y) Icf' fun n ‚Ü¶ ?_
    calc
      dist (u n) (u (n + 1)) = dist (g (u n)) (u n) := by rw [usucc, dist_comm]
      _ ‚â§ f'symm.nnnorm * dist (f (u n)) y := (A _)
      _ ‚â§ f'symm.nnnorm * (((c : ‚Ñù) * f'symm.nnnorm) ^ n * dist (f b) y) := by
        gcongr
        exact (D n).1
      _ = f'symm.nnnorm * dist (f b) y * ((c : ‚Ñù) * f'symm.nnnorm) ^ n := by ring
  obtain ‚ü®x, hx‚ü© : ‚àÉ x, Tendsto u atTop (ùìù x) := cauchySeq_tendsto_of_complete this
  -- As all the `u‚Çô` belong to the ball `closedBall b Œµ`, so does their limit `x`.
  have xmem : x ‚àà closedBall b Œµ :=
    isClosed_ball.mem_of_tendsto hx (eventually_of_forall fun n => C n _ (D n).2)
  refine' ‚ü®x, xmem, _‚ü©
  -- It remains to check that `f x = y`. This follows from continuity of `f` on `closedBall b Œµ`
  -- and from the fact that `f u‚Çô` is converging to `y` by construction.
  have hx' : Tendsto u atTop (ùìù[closedBall b Œµ] x) := by
    simp only [nhdsWithin, tendsto_inf, hx, true_and_iff, ge_iff_le, tendsto_principal]
    exact eventually_of_forall fun n => C n _ (D n).2
  have T1 : Tendsto (f ‚àò u) atTop (ùìù (f x)) :=
    (hf.continuousOn.mono hŒµ x xmem).tendsto.comp hx'
  have T2 : Tendsto (f ‚àò u) atTop (ùìù y) := by
    rw [tendsto_iff_dist_tendsto_zero]
    refine' squeeze_zero (fun _ => dist_nonneg) (fun n => (D n).1) _
    simpa using (tendsto_pow_atTop_nhds_0_of_lt_1 (by positivity) Icf').mul tendsto_const_nhds
  exact tendsto_nhds_unique T1 T2
#align approximates_linear_on.surj_on_closed_ball_of_nonlinear_right_inverse ApproximatesLinearOn.surjOn_closedBall_of_nonlinearRightInverse

theorem open_image (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse)
    (hs : IsOpen s) (hc : Subsingleton F ‚à® c < f'symm.nnnorm‚Åª¬π) : IsOpen (f '' s) := by
  cases' hc with hE hc
  ¬∑ exact isOpen_discrete _
  simp only [isOpen_iff_mem_nhds, nhds_basis_closedBall.mem_iff, ball_image_iff] at hs ‚ä¢
  intro x hx
  rcases hs x hx with ‚ü®Œµ, Œµ0, hŒµ‚ü©
  refine' ‚ü®(f'symm.nnnorm‚Åª¬π - c) * Œµ, mul_pos (sub_pos.2 hc) Œµ0, _‚ü©
  exact (hf.surjOn_closedBall_of_nonlinearRightInverse f'symm (le_of_lt Œµ0) hŒµ).mono hŒµ Subset.rfl
#align approximates_linear_on.open_image ApproximatesLinearOn.open_image

theorem image_mem_nhds (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse)
    {x : E} (hs : s ‚àà ùìù x) (hc : Subsingleton F ‚à® c < f'symm.nnnorm‚Åª¬π) : f '' s ‚àà ùìù (f x) := by
  obtain ‚ü®t, hts, ht, xt‚ü© : ‚àÉ t, t ‚äÜ s ‚àß IsOpen t ‚àß x ‚àà t := _root_.mem_nhds_iff.1 hs
  have := IsOpen.mem_nhds ((hf.mono_set hts).open_image f'symm ht hc) (mem_image_of_mem _ xt)
  exact mem_of_superset this (image_subset _ hts)
#align approximates_linear_on.image_mem_nhds ApproximatesLinearOn.image_mem_nhds

theorem map_nhds_eq (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse) {x : E}
    (hs : s ‚àà ùìù x) (hc : Subsingleton F ‚à® c < f'symm.nnnorm‚Åª¬π) : map f (ùìù x) = ùìù (f x) := by
  refine'
    le_antisymm ((hf.continuousOn x (mem_of_mem_nhds hs)).continuousAt hs) (le_map fun t ht => _)
  have : f '' (s ‚à© t) ‚àà ùìù (f x) :=
    (hf.mono_set (inter_subset_left s t)).image_mem_nhds f'symm (inter_mem hs ht) hc
  exact mem_of_superset this (image_subset _ (inter_subset_right _ _))
#align approximates_linear_on.map_nhds_eq ApproximatesLinearOn.map_nhds_eq

end LocallyOnto

/-!
From now on we assume that `f` approximates an invertible continuous linear map `f : E ‚âÉL[ùïú] F`.

We also assume that either `E = {0}`, or `c < ‚Äñf'‚Åª¬π‚Äñ‚Åª¬π`. We use `N` as an abbreviation for `‚Äñf'‚Åª¬π‚Äñ`.
-/


variable {f' : E ‚âÉL[ùïú] F} {s : Set E} {c : ‚Ñù‚â•0}

local notation "N" => ‚Äñ(f'.symm : F ‚ÜíL[ùïú] E)‚Äñ‚Çä

protected theorem antilipschitz (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : AntilipschitzWith (N‚Åª¬π - c)‚Åª¬π (s.restrict f) := by
  cases' hc with hE hc
  ¬∑ exact AntilipschitzWith.of_subsingleton
  convert (f'.antilipschitz.restrict s).add_lipschitzWith hf.lipschitz_sub hc
  simp [restrict]
#align approximates_linear_on.antilipschitz ApproximatesLinearOn.antilipschitz

protected theorem injective (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : Injective (s.restrict f) :=
  (hf.antilipschitz hc).injective
#align approximates_linear_on.injective ApproximatesLinearOn.injective

protected theorem injOn (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : InjOn f s :=
  injOn_iff_injective.2 <| hf.injective hc
#align approximates_linear_on.inj_on ApproximatesLinearOn.injOn

protected theorem surjective [CompleteSpace E] (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) univ c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : Surjective f := by
  cases' hc with hE hc
  ¬∑ haveI : Subsingleton F := (Equiv.subsingleton_congr f'.toEquiv).1 hE
    exact surjective_to_subsingleton _
  ¬∑ apply forall_of_forall_mem_closedBall (fun y : F => ‚àÉ a, f a = y) (f 0) _
    have hc' : (0 : ‚Ñù) < N‚Åª¬π - c := by rw [sub_pos]; exact hc
    let p : ‚Ñù ‚Üí Prop := fun R => closedBall (f 0) R ‚äÜ Set.range f
    have hp : ‚àÄ·∂† r : ‚Ñù in atTop, p ((N‚Åª¬π - c) * r) := by
      have hr : ‚àÄ·∂† r : ‚Ñù in atTop, 0 ‚â§ r := eventually_ge_atTop 0
      refine' hr.mono fun r hr => Subset.trans _ (image_subset_range f (closedBall 0 r))
      refine' hf.surjOn_closedBall_of_nonlinearRightInverse f'.toNonlinearRightInverse hr _
      exact subset_univ _
    refine' ((tendsto_id.const_mul_atTop hc').frequently hp.frequently).mono _
    exact fun R h y hy => h hy
#align approximates_linear_on.surjective ApproximatesLinearOn.surjective

/-- A map approximating a linear equivalence on a set defines a local equivalence on this set.
Should not be used outside of this file, because it is superseded by `toLocalHomeomorph` below.

This is a first step towards the inverse function. -/
def toLocalEquiv (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : LocalEquiv E F :=
  (hf.injOn hc).toLocalEquiv _ _
#align approximates_linear_on.to_local_equiv ApproximatesLinearOn.toLocalEquiv

/-- The inverse function is continuous on `f '' s`. Use properties of `LocalHomeomorph` instead. -/
theorem inverse_continuousOn (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : ContinuousOn (hf.toLocalEquiv hc).symm (f '' s) := by
  apply continuousOn_iff_continuous_restrict.2
  refine' ((hf.antilipschitz hc).to_rightInvOn' _ (hf.toLocalEquiv hc).right_inv').continuous
  exact fun x hx => (hf.toLocalEquiv hc).map_target hx
#align approximates_linear_on.inverse_continuous_on ApproximatesLinearOn.inverse_continuousOn

/-- The inverse function is approximated linearly on `f '' s` by `f'.symm`. -/
theorem to_inv (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c) (hc : Subsingleton E ‚à® c < N‚Åª¬π) :
    ApproximatesLinearOn (hf.toLocalEquiv hc).symm (f'.symm : F ‚ÜíL[ùïú] E) (f '' s)
      (N * (N‚Åª¬π - c)‚Åª¬π * c) := fun x hx y hy ‚Ü¶ by
  set A := hf.toLocalEquiv hc
  have Af : ‚àÄ z, A z = f z := fun z => rfl
  rcases (mem_image _ _ _).1 hx with ‚ü®x', x's, rfl‚ü©
  rcases (mem_image _ _ _).1 hy with ‚ü®y', y's, rfl‚ü©
  rw [‚Üê Af x', ‚Üê Af y', A.left_inv x's, A.left_inv y's]
  calc
    ‚Äñx' - y' - f'.symm (A x' - A y')‚Äñ ‚â§ N * ‚Äñf' (x' - y' - f'.symm (A x' - A y'))‚Äñ :=
      (f' : E ‚ÜíL[ùïú] F).bound_of_antilipschitz f'.antilipschitz _
    _ = N * ‚ÄñA y' - A x' - f' (y' - x')‚Äñ := by
      congr 2
      simp only [ContinuousLinearEquiv.apply_symm_apply, ContinuousLinearEquiv.map_sub]
      abel
    _ ‚â§ N * (c * ‚Äñy' - x'‚Äñ) := (mul_le_mul_of_nonneg_left (hf _ y's _ x's) (NNReal.coe_nonneg _))
    _ ‚â§ N * (c * (((N‚Åª¬π - c)‚Åª¬π : ‚Ñù‚â•0) * ‚ÄñA y' - A x'‚Äñ)) := by
      gcongr
      rw [‚Üê dist_eq_norm, ‚Üê dist_eq_norm]
      exact (hf.antilipschitz hc).le_mul_dist ‚ü®y', y's‚ü© ‚ü®x', x's‚ü©
    _ = (N * (N‚Åª¬π - c)‚Åª¬π * c : ‚Ñù‚â•0) * ‚ÄñA x' - A y'‚Äñ := by
      simp only [norm_sub_rev, NNReal.coe_mul]; ring
#align approximates_linear_on.to_inv ApproximatesLinearOn.to_inv

section

variable (f s)

/-- Given a function `f` that approximates a linear equivalence on an open set `s`,
returns a local homeomorph with `toFun = f` and `source = s`. -/
def toLocalHomeomorph (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) (hs : IsOpen s) : LocalHomeomorph E F where
  toLocalEquiv := hf.toLocalEquiv hc
  open_source := hs
  open_target := hf.open_image f'.toNonlinearRightInverse hs <| by
    rwa [f'.toEquiv.subsingleton_congr] at hc
  continuous_toFun := hf.continuousOn
  continuous_invFun := hf.inverse_continuousOn hc
#align approximates_linear_on.to_local_homeomorph ApproximatesLinearOn.toLocalHomeomorph

@[simp]
theorem toLocalHomeomorph_coe (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) (hs : IsOpen s) :
    (hf.toLocalHomeomorph f s hc hs : E ‚Üí F) = f :=
  rfl
#align approximates_linear_on.to_local_homeomorph_coe ApproximatesLinearOn.toLocalHomeomorph_coe

@[simp]
theorem toLocalHomeomorph_source (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) (hs : IsOpen s) : (hf.toLocalHomeomorph f s hc hs).source = s :=
  rfl
#align approximates_linear_on.to_local_homeomorph_source ApproximatesLinearOn.toLocalHomeomorph_source

@[simp]
theorem toLocalHomeomorph_target (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) (hs : IsOpen s) :
    (hf.toLocalHomeomorph f s hc hs).target = f '' s :=
  rfl
#align approximates_linear_on.to_local_homeomorph_target ApproximatesLinearOn.toLocalHomeomorph_target

/-- A function `f` that approximates a linear equivalence on the whole space is a homeomorphism. -/
def toHomeomorph (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) univ c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) : E ‚âÉ‚Çú F := by
  refine' (hf.toLocalHomeomorph _ _ hc isOpen_univ).toHomeomorphOfSourceEqUnivTargetEqUniv rfl _
  rw [toLocalHomeomorph_target, image_univ, range_iff_surjective]
  exact hf.surjective hc
#align approximates_linear_on.to_homeomorph ApproximatesLinearOn.toHomeomorph

/-- In a real vector space, a function `f` that approximates a linear equivalence on a subset `s`
can be extended to a homeomorphism of the whole space. -/
theorem exists_homeomorph_extension {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [FiniteDimensional ‚Ñù F] {s : Set E}
    {f : E ‚Üí F} {f' : E ‚âÉL[‚Ñù] F} {c : ‚Ñù‚â•0} (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[‚Ñù] F) s c)
    (hc : Subsingleton E ‚à® lipschitzExtensionConstant F * c < ‚Äñ(f'.symm : F ‚ÜíL[‚Ñù] E)‚Äñ‚Çä‚Åª¬π) :
    ‚àÉ g : E ‚âÉ‚Çú F, EqOn f g s := by
  -- the difference `f - f'` is Lipschitz on `s`. It can be extended to a Lipschitz function `u`
  -- on the whole space, with a slightly worse Lipschitz constant. Then `f' + u` will be the
  -- desired homeomorphism.
  obtain ‚ü®u, hu, uf‚ü© :
    ‚àÉ u : E ‚Üí F, LipschitzWith (lipschitzExtensionConstant F * c) u ‚àß EqOn (f - ‚áëf') u s :=
    hf.lipschitzOnWith.extend_finite_dimension
  let g : E ‚Üí F := fun x => f' x + u x
  have fg : EqOn f g s := fun x hx => by simp_rw [‚Üê uf hx, Pi.sub_apply, add_sub_cancel'_right]
  have hg : ApproximatesLinearOn g (f' : E ‚ÜíL[‚Ñù] F) univ (lipschitzExtensionConstant F * c) := by
    apply LipschitzOnWith.approximatesLinearOn
    rw [lipschitzOn_univ]
    convert hu
    ext x
    simp only [add_sub_cancel', ContinuousLinearEquiv.coe_coe, Pi.sub_apply]
  haveI : FiniteDimensional ‚Ñù E := f'.symm.finiteDimensional
  exact ‚ü®hg.toHomeomorph g hc, fg‚ü©
#align approximates_linear_on.exists_homeomorph_extension ApproximatesLinearOn.exists_homeomorph_extension

end

theorem closedBall_subset_target (hf : ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s c)
    (hc : Subsingleton E ‚à® c < N‚Åª¬π) (hs : IsOpen s) {b : E} (Œµ0 : 0 ‚â§ Œµ) (hŒµ : closedBall b Œµ ‚äÜ s) :
    closedBall (f b) ((N‚Åª¬π - c) * Œµ) ‚äÜ (hf.toLocalHomeomorph f s hc hs).target :=
  (hf.surjOn_closedBall_of_nonlinearRightInverse f'.toNonlinearRightInverse Œµ0 hŒµ).mono hŒµ
    Subset.rfl
#align approximates_linear_on.closed_ball_subset_target ApproximatesLinearOn.closedBall_subset_target

end ApproximatesLinearOn

/-!
### Inverse function theorem

Now we prove the inverse function theorem. Let `f : E ‚Üí F` be a map defined on a complete vector
space `E`. Assume that `f` has an invertible derivative `f' : E ‚âÉL[ùïú] F` at `a : E` in the strict
sense. Then `f` approximates `f'` in the sense of `ApproximatesLinearOn` on an open neighborhood
of `a`, and we can apply `ApproximatesLinearOn.toLocalHomeomorph` to construct the inverse
function. -/

namespace HasStrictFDerivAt

/-- If `f` has derivative `f'` at `a` in the strict sense and `c > 0`, then `f` approximates `f'`
with constant `c` on some neighborhood of `a`. -/
theorem approximates_deriv_on_nhds {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {a : E}
    (hf : HasStrictFDerivAt f f' a) {c : ‚Ñù‚â•0} (hc : Subsingleton E ‚à® 0 < c) :
    ‚àÉ s ‚àà ùìù a, ApproximatesLinearOn f f' s c := by
  cases' hc with hE hc
  ¬∑ refine' ‚ü®univ, IsOpen.mem_nhds isOpen_univ trivial, fun x _ y _ => _‚ü©
    simp [@Subsingleton.elim E hE x y]
  have := hf.def hc
  rw [nhds_prod_eq, Filter.Eventually, mem_prod_same_iff] at this
  rcases this with ‚ü®s, has, hs‚ü©
  exact ‚ü®s, has, fun x hx y hy => hs (mk_mem_prod hx hy)‚ü©
#align has_strict_fderiv_at.approximates_deriv_on_nhds HasStrictFDerivAt.approximates_deriv_on_nhds

theorem map_nhds_eq_of_surj [CompleteSpace E] [CompleteSpace F] {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {a : E}
    (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) (h : LinearMap.range f' = ‚ä§) :
    map f (ùìù a) = ùìù (f a) := by
  let f'symm := f'.nonlinearRightInverseOfSurjective h
  set c : ‚Ñù‚â•0 := f'symm.nnnorm‚Åª¬π / 2 with hc
  have f'symm_pos : 0 < f'symm.nnnorm := f'.nonlinearRightInverseOfSurjective_nnnorm_pos h
  have cpos : 0 < c := by simp [hc, half_pos, inv_pos, f'symm_pos]
  obtain ‚ü®s, s_nhds, hs‚ü© : ‚àÉ s ‚àà ùìù a, ApproximatesLinearOn f f' s c :=
    hf.approximates_deriv_on_nhds (Or.inr cpos)
  apply hs.map_nhds_eq f'symm s_nhds (Or.inr (NNReal.half_lt_self _))
  simp [ne_of_gt f'symm_pos]
#align has_strict_fderiv_at.map_nhds_eq_of_surj HasStrictFDerivAt.map_nhds_eq_of_surj

variable [CompleteSpace E] {f : E ‚Üí F} {f' : E ‚âÉL[ùïú] F} {a : E}

theorem approximates_deriv_on_open_nhds (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    ‚àÉ s : Set E, a ‚àà s ‚àß IsOpen s ‚àß
      ApproximatesLinearOn f (f' : E ‚ÜíL[ùïú] F) s (‚Äñ(f'.symm : F ‚ÜíL[ùïú] E)‚Äñ‚Çä‚Åª¬π / 2) := by
  simp only [‚Üê and_assoc]
  refine ((nhds_basis_opens a).exists_iff fun s t => ApproximatesLinearOn.mono_set).1 ?_
  exact
    hf.approximates_deriv_on_nhds <|
      f'.subsingleton_or_nnnorm_symm_pos.imp id fun hf' => half_pos <| inv_pos.2 hf'
#align has_strict_fderiv_at.approximates_deriv_on_open_nhds HasStrictFDerivAt.approximates_deriv_on_open_nhds

variable (f)

/-- Given a function with an invertible strict derivative at `a`, returns a `LocalHomeomorph`
with `to_fun = f` and `a ‚àà source`. This is a part of the inverse function theorem.
The other part `HasStrictFDerivAt.to_localInverse` states that the inverse function
of this `LocalHomeomorph` has derivative `f'.symm`. -/
def toLocalHomeomorph (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) : LocalHomeomorph E F :=
  ApproximatesLinearOn.toLocalHomeomorph f (Classical.choose hf.approximates_deriv_on_open_nhds)
    (Classical.choose_spec hf.approximates_deriv_on_open_nhds).2.2
    (f'.subsingleton_or_nnnorm_symm_pos.imp id fun hf' =>
      NNReal.half_lt_self <| ne_of_gt <| inv_pos.2 hf')
    (Classical.choose_spec hf.approximates_deriv_on_open_nhds).2.1
#align has_strict_fderiv_at.to_local_homeomorph HasStrictFDerivAt.toLocalHomeomorph

variable {f}

@[simp]
theorem toLocalHomeomorph_coe (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    (hf.toLocalHomeomorph f : E ‚Üí F) = f :=
  rfl
#align has_strict_fderiv_at.to_local_homeomorph_coe HasStrictFDerivAt.toLocalHomeomorph_coe

theorem mem_toLocalHomeomorph_source (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    a ‚àà (hf.toLocalHomeomorph f).source :=
  (Classical.choose_spec hf.approximates_deriv_on_open_nhds).1
#align has_strict_fderiv_at.mem_to_local_homeomorph_source HasStrictFDerivAt.mem_toLocalHomeomorph_source

theorem image_mem_toLocalHomeomorph_target (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    f a ‚àà (hf.toLocalHomeomorph f).target :=
  (hf.toLocalHomeomorph f).map_source hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.image_mem_to_local_homeomorph_target HasStrictFDerivAt.image_mem_toLocalHomeomorph_target

theorem map_nhds_eq_of_equiv (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    map f (ùìù a) = ùìù (f a) :=
  (hf.toLocalHomeomorph f).map_nhds_eq hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.map_nhds_eq_of_equiv HasStrictFDerivAt.map_nhds_eq_of_equiv

variable (f f' a)

/-- Given a function `f` with an invertible derivative, returns a function that is locally inverse
to `f`. -/
def localInverse (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) : F ‚Üí E :=
  (hf.toLocalHomeomorph f).symm
#align has_strict_fderiv_at.local_inverse HasStrictFDerivAt.localInverse

variable {f f' a}

theorem localInverse_def (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    hf.localInverse f _ _ = (hf.toLocalHomeomorph f).symm :=
  rfl
#align has_strict_fderiv_at.local_inverse_def HasStrictFDerivAt.localInverse_def

theorem eventually_left_inverse (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    ‚àÄ·∂† x in ùìù a, hf.localInverse f f' a (f x) = x :=
  (hf.toLocalHomeomorph f).eventually_left_inverse hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.eventually_left_inverse HasStrictFDerivAt.eventually_left_inverse

@[simp]
theorem localInverse_apply_image (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    hf.localInverse f f' a (f a) = a :=
  hf.eventually_left_inverse.self_of_nhds
#align has_strict_fderiv_at.local_inverse_apply_image HasStrictFDerivAt.localInverse_apply_image

theorem eventually_right_inverse (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    ‚àÄ·∂† y in ùìù (f a), f (hf.localInverse f f' a y) = y :=
  (hf.toLocalHomeomorph f).eventually_right_inverse' hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.eventually_right_inverse HasStrictFDerivAt.eventually_right_inverse

theorem localInverse_continuousAt (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    ContinuousAt (hf.localInverse f f' a) (f a) :=
  (hf.toLocalHomeomorph f).continuousAt_symm hf.image_mem_toLocalHomeomorph_target
#align has_strict_fderiv_at.local_inverse_continuous_at HasStrictFDerivAt.localInverse_continuousAt

theorem localInverse_tendsto (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    Tendsto (hf.localInverse f f' a) (ùìù <| f a) (ùìù a) :=
  (hf.toLocalHomeomorph f).tendsto_symm hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.local_inverse_tendsto HasStrictFDerivAt.localInverse_tendsto

theorem localInverse_unique (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) {g : F ‚Üí E}
    (hg : ‚àÄ·∂† x in ùìù a, g (f x) = x) : ‚àÄ·∂† y in ùìù (f a), g y = localInverse f f' a hf y :=
  eventuallyEq_of_left_inv_of_right_inv hg hf.eventually_right_inverse <|
    (hf.toLocalHomeomorph f).tendsto_symm hf.mem_toLocalHomeomorph_source
#align has_strict_fderiv_at.local_inverse_unique HasStrictFDerivAt.localInverse_unique

/-- If `f` has an invertible derivative `f'` at `a` in the sense of strict differentiability `(hf)`,
then the inverse function `hf.localInverse f` has derivative `f'.symm` at `f a`. -/
theorem to_localInverse (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) :
    HasStrictFDerivAt (hf.localInverse f f' a) (f'.symm : F ‚ÜíL[ùïú] E) (f a) :=
  (hf.toLocalHomeomorph f).hasStrictFDerivAt_symm hf.image_mem_toLocalHomeomorph_target <| by
    simpa [‚Üê localInverse_def] using hf
#align has_strict_fderiv_at.to_local_inverse HasStrictFDerivAt.to_localInverse

/-- If `f : E ‚Üí F` has an invertible derivative `f'` at `a` in the sense of strict differentiability
and `g (f x) = x` in a neighborhood of `a`, then `g` has derivative `f'.symm` at `f a`.

For a version assuming `f (g y) = y` and continuity of `g` at `f a` but not `[CompleteSpace E]`
see `of_local_left_inverse`.  -/
theorem to_local_left_inverse (hf : HasStrictFDerivAt f (f' : E ‚ÜíL[ùïú] F) a) {g : F ‚Üí E}
    (hg : ‚àÄ·∂† x in ùìù a, g (f x) = x) : HasStrictFDerivAt g (f'.symm : F ‚ÜíL[ùïú] E) (f a) :=
  hf.to_localInverse.congr_of_eventuallyEq <| (hf.localInverse_unique hg).mono fun _ => Eq.symm
#align has_strict_fderiv_at.to_local_left_inverse HasStrictFDerivAt.to_local_left_inverse

end HasStrictFDerivAt

/-- If a function has an invertible strict derivative at all points, then it is an open map. -/
theorem open_map_of_strict_fderiv_equiv [CompleteSpace E] {f : E ‚Üí F} {f' : E ‚Üí E ‚âÉL[ùïú] F}
    (hf : ‚àÄ x, HasStrictFDerivAt f (f' x : E ‚ÜíL[ùïú] F) x) : IsOpenMap f :=
  isOpenMap_iff_nhds_le.2 fun x => (hf x).map_nhds_eq_of_equiv.ge
#align open_map_of_strict_fderiv_equiv open_map_of_strict_fderiv_equiv

/-!
### Inverse function theorem, 1D case

In this case we prove a version of the inverse function theorem for maps `f : ùïú ‚Üí ùïú`.
We use `ContinuousLinearEquiv.unitsEquivAut` to translate `HasStrictDerivAt f f' a` and
`f' ‚â† 0` into `HasStrictFDerivAt f (_ : ùïú ‚âÉL[ùïú] ùïú) a`.
-/


namespace HasStrictDerivAt

variable [CompleteSpace ùïú] {f : ùïú ‚Üí ùïú} {f' a : ùïú} (hf : HasStrictDerivAt f f' a) (hf' : f' ‚â† 0)

variable (f f' a)

/-- A function that is inverse to `f` near `a`. -/
@[reducible]
def localInverse : ùïú ‚Üí ùïú :=
  (hf.hasStrictFDerivAt_equiv hf').localInverse _ _ _
#align has_strict_deriv_at.local_inverse HasStrictDerivAt.localInverse

variable {f f' a}

theorem map_nhds_eq : map f (ùìù a) = ùìù (f a) :=
  (hf.hasStrictFDerivAt_equiv hf').map_nhds_eq_of_equiv
#align has_strict_deriv_at.map_nhds_eq HasStrictDerivAt.map_nhds_eq

theorem to_localInverse : HasStrictDerivAt (hf.localInverse f f' a hf') f'‚Åª¬π (f a) :=
  (hf.hasStrictFDerivAt_equiv hf').to_localInverse
#align has_strict_deriv_at.to_local_inverse HasStrictDerivAt.to_localInverse

theorem to_local_left_inverse {g : ùïú ‚Üí ùïú} (hg : ‚àÄ·∂† x in ùìù a, g (f x) = x) :
    HasStrictDerivAt g f'‚Åª¬π (f a) :=
  (hf.hasStrictFDerivAt_equiv hf').to_local_left_inverse hg
#align has_strict_deriv_at.to_local_left_inverse HasStrictDerivAt.to_local_left_inverse

end HasStrictDerivAt

/-- If a function has a non-zero strict derivative at all points, then it is an open map. -/
theorem open_map_of_strict_deriv [CompleteSpace ùïú] {f f' : ùïú ‚Üí ùïú}
    (hf : ‚àÄ x, HasStrictDerivAt f (f' x) x) (h0 : ‚àÄ x, f' x ‚â† 0) : IsOpenMap f :=
  isOpenMap_iff_nhds_le.2 fun x => ((hf x).map_nhds_eq (h0 x)).ge
#align open_map_of_strict_deriv open_map_of_strict_deriv

/-!
### Inverse function theorem, smooth case

-/


namespace ContDiffAt

variable {ùïÇ : Type*} [IsROrC ùïÇ]

variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ùïÇ E']

variable {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ùïÇ F']

variable [CompleteSpace E'] (f : E' ‚Üí F') {f' : E' ‚âÉL[ùïÇ] F'} {a : E'}

/-- Given a `ContDiff` function over `ùïÇ` (which is `‚Ñù` or `‚ÑÇ`) with an invertible
derivative at `a`, returns a `LocalHomeomorph` with `to_fun = f` and `a ‚àà source`. -/
def toLocalHomeomorph {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a) (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a)
    (hn : 1 ‚â§ n) : LocalHomeomorph E' F' :=
  (hf.hasStrictFDerivAt' hf' hn).toLocalHomeomorph f
#align cont_diff_at.to_local_homeomorph ContDiffAt.toLocalHomeomorph

variable {f}

@[simp]
theorem toLocalHomeomorph_coe {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a)
    (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a) (hn : 1 ‚â§ n) :
    (hf.toLocalHomeomorph f hf' hn : E' ‚Üí F') = f :=
  rfl
#align cont_diff_at.to_local_homeomorph_coe ContDiffAt.toLocalHomeomorph_coe

theorem mem_toLocalHomeomorph_source {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a)
    (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a) (hn : 1 ‚â§ n) :
    a ‚àà (hf.toLocalHomeomorph f hf' hn).source :=
  (hf.hasStrictFDerivAt' hf' hn).mem_toLocalHomeomorph_source
#align cont_diff_at.mem_to_local_homeomorph_source ContDiffAt.mem_toLocalHomeomorph_source

theorem image_mem_toLocalHomeomorph_target {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a)
    (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a) (hn : 1 ‚â§ n) :
    f a ‚àà (hf.toLocalHomeomorph f hf' hn).target :=
  (hf.hasStrictFDerivAt' hf' hn).image_mem_toLocalHomeomorph_target
#align cont_diff_at.image_mem_to_local_homeomorph_target ContDiffAt.image_mem_toLocalHomeomorph_target

/-- Given a `ContDiff` function over `ùïÇ` (which is `‚Ñù` or `‚ÑÇ`) with an invertible derivative
at `a`, returns a function that is locally inverse to `f`. -/
def localInverse {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a) (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a)
    (hn : 1 ‚â§ n) : F' ‚Üí E' :=
  (hf.hasStrictFDerivAt' hf' hn).localInverse f f' a
#align cont_diff_at.local_inverse ContDiffAt.localInverse

theorem localInverse_apply_image {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a)
    (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a) (hn : 1 ‚â§ n) : hf.localInverse hf' hn (f a) = a :=
  (hf.hasStrictFDerivAt' hf' hn).localInverse_apply_image
#align cont_diff_at.local_inverse_apply_image ContDiffAt.localInverse_apply_image

/-- Given a `ContDiff` function over `ùïÇ` (which is `‚Ñù` or `‚ÑÇ`) with an invertible derivative
at `a`, the inverse function (produced by `ContDiff.toLocalHomeomorph`) is
also `ContDiff`. -/
theorem to_localInverse {n : ‚Ñï‚àû} (hf : ContDiffAt ùïÇ n f a)
    (hf' : HasFDerivAt f (f' : E' ‚ÜíL[ùïÇ] F') a) (hn : 1 ‚â§ n) :
    ContDiffAt ùïÇ n (hf.localInverse hf' hn) (f a) := by
  have := hf.localInverse_apply_image hf' hn
  apply (hf.toLocalHomeomorph f hf' hn).contDiffAt_symm
    (image_mem_toLocalHomeomorph_target hf hf' hn)
  ¬∑ convert hf'
  ¬∑ convert hf
#align cont_diff_at.to_local_inverse ContDiffAt.to_localInverse

end ContDiffAt
