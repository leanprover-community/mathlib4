/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Symmetric

/-!
# Vector fields in vector spaces

We study functions of the form `V : E â†’ E` on a vector space, thinking of these as vector fields.
We define several notions in this context, with the aim to generalize them to vector fields on
manifolds.

Notably, we define the pullback of a vector field under a map, as
`VectorField.pullback ğ•œ f V x := (fderiv ğ•œ f x).inverse (V (f x))` (together with the same notion
within a set).

We also define the Lie bracket of two vector fields as
`VectorField.lieBracket ğ•œ V W x := fderiv ğ•œ W x (V x) - fderiv ğ•œ V x (W x)`
(together with the same notion within a set).

In addition to comprehensive API on these two notions, the main results are the following:
* `VectorField.pullback_lieBracket` states that the pullback of the Lie bracket
  is the Lie bracket of the pullbacks, when the second derivative is symmetric.
* `VectorField.leibniz_identity_lieBracket` is the Leibniz
  identity `[U, [V, W]] = [[U, V], W] + [V, [U, W]]`.

-/

open Set
open scoped Topology

noncomputable section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {n : WithTop â„•âˆ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {V W Vâ‚ Wâ‚ : E â†’ E} {s t : Set E} {x : E}

/-!
### The Lie bracket of vector fields in a vector space

We define the Lie bracket of two vector fields, and call it `lieBracket ğ•œ V W x`. We also define
a version localized to sets, `lieBracketWithin ğ•œ V W s x`. We copy the relevant API
of `fderivWithin` and `fderiv` for these notions to get a comprehensive API.
-/

namespace VectorField

variable (ğ•œ) in
/-- The Lie bracket `[V, W] (x)` of two vector fields at a point, defined as
`DW(x) (V x) - DV(x) (W x)`. -/
def lieBracket (V W : E â†’ E) (x : E) : E :=
  fderiv ğ•œ W x (V x) - fderiv ğ•œ V x (W x)

variable (ğ•œ) in
/-- The Lie bracket `[V, W] (x)` of two vector fields within a set at a point, defined as
`DW(x) (V x) - DV(x) (W x)` where the derivatives are taken inside `s`. -/
def lieBracketWithin (V W : E â†’ E) (s : Set E) (x : E) : E :=
  fderivWithin ğ•œ W s x (V x) - fderivWithin ğ•œ V s x (W x)

lemma lieBracket_eq :
    lieBracket ğ•œ V W = fun x â†¦ fderiv ğ•œ W x (V x) - fderiv ğ•œ V x (W x) := rfl

lemma lieBracketWithin_eq :
    lieBracketWithin ğ•œ V W s =
      fun x â†¦ fderivWithin ğ•œ W s x (V x) - fderivWithin ğ•œ V s x (W x) := rfl

@[simp]
theorem lieBracketWithin_univ : lieBracketWithin ğ•œ V W univ = lieBracket ğ•œ V W := by
  ext1 x
  simp [lieBracketWithin, lieBracket]

lemma lieBracketWithin_eq_zero_of_eq_zero (hV : V x = 0) (hW : W x = 0) :
    lieBracketWithin ğ•œ V W s x = 0 := by
  simp [lieBracketWithin, hV, hW]

lemma lieBracket_eq_zero_of_eq_zero (hV : V x = 0) (hW : W x = 0) :
    lieBracket ğ•œ V W x = 0 := by
  simp [lieBracket, hV, hW]

lemma lieBracketWithin_swap : lieBracketWithin ğ•œ V W s = - lieBracketWithin ğ•œ W V s := by
  ext x; simp [lieBracketWithin]

lemma lieBracket_swap : lieBracket ğ•œ V W x = - lieBracket ğ•œ W V x := by
  simp [lieBracket]

@[simp] lemma lieBracketWithin_self : lieBracketWithin ğ•œ V V s = 0 := by
  ext x; simp [lieBracketWithin]

@[simp] lemma lieBracket_self : lieBracket ğ•œ V V = 0 := by
  ext x; simp [lieBracket]

lemma lieBracketWithin_const_smul_left {c : ğ•œ} (hV : DifferentiableWithinAt ğ•œ V s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ (c â€¢ V) W s x =
      c â€¢ lieBracketWithin ğ•œ V W s x := by
  simp [lieBracketWithin, smul_sub, fderivWithin_const_smul hs hV]

lemma lieBracket_const_smul_left {c : ğ•œ} (hV : DifferentiableAt ğ•œ V x) :
    lieBracket ğ•œ (c â€¢ V) W x = c â€¢ lieBracket ğ•œ V W x := by
  simp only [â† differentiableWithinAt_univ, â† lieBracketWithin_univ] at hV âŠ¢
  exact lieBracketWithin_const_smul_left hV uniqueDiffWithinAt_univ

lemma lieBracketWithin_const_smul_right {c : ğ•œ} (hW : DifferentiableWithinAt ğ•œ W s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ V (c â€¢ W) s x =
      c â€¢ lieBracketWithin ğ•œ V W s x := by
  simp [lieBracketWithin, smul_sub, fderivWithin_const_smul hs hW]

lemma lieBracket_const_smul_right {c : ğ•œ} (hW : DifferentiableAt ğ•œ W x) :
    lieBracket ğ•œ V (c â€¢ W) x = c â€¢ lieBracket ğ•œ V W x := by
  simp only [â† differentiableWithinAt_univ, â† lieBracketWithin_univ] at hW âŠ¢
  exact lieBracketWithin_const_smul_right hW uniqueDiffWithinAt_univ

/--
Product rule for Lie Brackets: given two vector fields `V W : E â†’ E` and a function `f : E â†’ ğ•œ`,
we have `[V, f â€¢ W] = (df V) â€¢ W + f â€¢ [V, W]`
-/
lemma lieBracketWithin_smul_right {f : E â†’ ğ•œ} (hf : DifferentiableWithinAt ğ•œ f s x)
    (hW : DifferentiableWithinAt ğ•œ W s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ V (fun y â†¦ f y â€¢ W y) s x =
      (fderivWithin ğ•œ f s x) (V x) â€¢ (W x) + (f x) â€¢ lieBracketWithin ğ•œ V W s x := by
  simp [lieBracketWithin, fderivWithin_fun_smul hs hf hW, map_smul, add_comm, smul_sub,
    add_sub_assoc]

/--
Product rule for Lie Brackets: given two vector fields `V W : E â†’ E` and a function `f : E â†’ ğ•œ`,
we have `[V, f â€¢ W] = (df V) â€¢ W + f â€¢ [V, W]`
-/
lemma lieBracket_smul_right {f : E â†’ ğ•œ} (hf : DifferentiableAt ğ•œ f x)
    (hW : DifferentiableAt ğ•œ W x) :
    lieBracket ğ•œ V (fun y â†¦ f y â€¢ W y) x =
      (fderiv ğ•œ f x) (V x) â€¢ (W x) + (f x) â€¢ lieBracket ğ•œ V W x := by
  simp_rw [â† differentiableWithinAt_univ, â† lieBracketWithin_univ, fderiv] at hW hf âŠ¢
  exact lieBracketWithin_smul_right hf hW uniqueDiffWithinAt_univ

/--
Product rule for Lie Brackets: given two vector fields `V W : E â†’ E` and a function `f : E â†’ ğ•œ`,
we have `[f â€¢ V, W] = - (df W) â€¢ V + f â€¢ [V, W]`
-/
lemma lieBracketWithin_smul_left {f : E â†’ ğ•œ} (hf : DifferentiableWithinAt ğ•œ f s x)
    (hV : DifferentiableWithinAt ğ•œ V s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ (fun y â†¦ f y â€¢ V y) W s x =
      - (fderivWithin ğ•œ f s x) (W x) â€¢ (V x)  + (f x) â€¢ lieBracketWithin ğ•œ V W s x := by
  rw [lieBracketWithin_swap, Pi.neg_apply, lieBracketWithin_smul_right hf hV hs,
    lieBracketWithin_swap, add_comm]
  simp

/--
Product rule for Lie Brackets: given two vector fields `V W : E â†’ E` and a function `f : E â†’ ğ•œ`,
we have `[f â€¢ V, W] = - (df W) â€¢ V + f â€¢ [V, W]`
-/
lemma lieBracket_fmul_left {f : E â†’ ğ•œ} (hf : DifferentiableAt ğ•œ f x)
    (hV : DifferentiableAt ğ•œ V x) :
    lieBracket ğ•œ (fun y â†¦ f y â€¢ V y) W x =
      - (fderiv ğ•œ f x) (W x) â€¢ (V x)  + (f x) â€¢ lieBracket ğ•œ V W x := by
  rw [lieBracket_swap, lieBracket_smul_right hf hV, lieBracket_swap, add_comm]
  simp

lemma lieBracketWithin_add_left (hV : DifferentiableWithinAt ğ•œ V s x)
    (hVâ‚ : DifferentiableWithinAt ğ•œ Vâ‚ s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ (V + Vâ‚) W s x =
      lieBracketWithin ğ•œ V W s x + lieBracketWithin ğ•œ Vâ‚ W s x := by
  simp only [lieBracketWithin, Pi.add_apply, map_add]
  rw [fderivWithin_add hs hV hVâ‚, ContinuousLinearMap.add_apply]
  abel

lemma lieBracket_add_left (hV : DifferentiableAt ğ•œ V x) (hVâ‚ : DifferentiableAt ğ•œ Vâ‚ x) :
    lieBracket ğ•œ (V + Vâ‚) W  x =
      lieBracket ğ•œ V W x + lieBracket ğ•œ Vâ‚ W x := by
  simp only [lieBracket, Pi.add_apply, map_add]
  rw [fderiv_add hV hVâ‚, ContinuousLinearMap.add_apply]
  abel

/-- We have `[0, W] = 0` for all vector fields `W`: this depends on the junk value 0
if `W` is not differentiable. Version within a set. -/
@[simp]
lemma lieBracketWithin_zero_left : lieBracketWithin ğ•œ 0 W s = 0 := by ext; simp [lieBracketWithin]

/-- We have `[W, 0] = 0` for all vector fields `W`: this depends on the junk value 0
if `W` is not differentiable. Version within a set. -/
@[simp]
lemma lieBracketWithin_zero_right : lieBracketWithin ğ•œ W 0 s = 0 := by ext; simp [lieBracketWithin]

/-- We have `[0, W] = 0` for all vector fields `W`: this depends on the junk value 0
if `W` is not differentiable. -/
@[simp]
lemma lieBracket_zero_left : lieBracket ğ•œ 0 W = 0 := by simp [â† lieBracketWithin_univ]

/-- We have `[W, 0] = 0` for all vector fields `W`: this depends on the junk value 0
if `W` is not differentiable. -/
@[simp]
lemma lieBracket_zero_right : lieBracket ğ•œ W 0 = 0 := by simp [â† lieBracketWithin_univ]

lemma lieBracketWithin_add_right (hW : DifferentiableWithinAt ğ•œ W s x)
    (hWâ‚ : DifferentiableWithinAt ğ•œ Wâ‚ s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    lieBracketWithin ğ•œ V (W + Wâ‚) s x =
      lieBracketWithin ğ•œ V W s x + lieBracketWithin ğ•œ V Wâ‚ s x := by
  simp only [lieBracketWithin, Pi.add_apply, map_add]
  rw [fderivWithin_add hs hW hWâ‚, ContinuousLinearMap.add_apply]
  abel

lemma lieBracket_add_right (hW : DifferentiableAt ğ•œ W x) (hWâ‚ : DifferentiableAt ğ•œ Wâ‚ x) :
    lieBracket ğ•œ V (W + Wâ‚) x =
      lieBracket ğ•œ V W x + lieBracket ğ•œ V Wâ‚ x := by
  simp only [lieBracket, Pi.add_apply, map_add]
  rw [fderiv_add hW hWâ‚, ContinuousLinearMap.add_apply]
  abel

lemma _root_.ContDiffWithinAt.lieBracketWithin_vectorField
    {m n : WithTop â„•âˆ} (hV : ContDiffWithinAt ğ•œ n V s x)
    (hW : ContDiffWithinAt ğ•œ n W s x) (hs : UniqueDiffOn ğ•œ s) (hmn : m + 1 â‰¤ n) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ m (lieBracketWithin ğ•œ V W s) s x := by
  apply ContDiffWithinAt.sub
  Â· exact ContDiffWithinAt.clm_apply (hW.fderivWithin_right hs hmn hx)
      (hV.of_le (le_trans le_self_add hmn))
  Â· exact ContDiffWithinAt.clm_apply (hV.fderivWithin_right hs hmn hx)
      (hW.of_le (le_trans le_self_add hmn))

lemma _root_.ContDiffAt.lieBracket_vectorField {m n : WithTop â„•âˆ} (hV : ContDiffAt ğ•œ n V x)
    (hW : ContDiffAt ğ•œ n W x) (hmn : m + 1 â‰¤ n) :
    ContDiffAt ğ•œ m (lieBracket ğ•œ V W) x := by
  rw [â† contDiffWithinAt_univ] at hV hW âŠ¢
  simp_rw [â† lieBracketWithin_univ]
  exact hV.lieBracketWithin_vectorField hW uniqueDiffOn_univ hmn (mem_univ _)

lemma _root_.ContDiffOn.lieBracketWithin_vectorField {m n : WithTop â„•âˆ} (hV : ContDiffOn ğ•œ n V s)
    (hW : ContDiffOn ğ•œ n W s) (hs : UniqueDiffOn ğ•œ s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (lieBracketWithin ğ•œ V W s) s :=
  fun x hx â†¦ (hV x hx).lieBracketWithin_vectorField (hW x hx) hs hmn hx

lemma _root_.ContDiff.lieBracket_vectorField {m n : WithTop â„•âˆ} (hV : ContDiff ğ•œ n V)
    (hW : ContDiff ğ•œ n W) (hmn : m + 1 â‰¤ n) :
    ContDiff ğ•œ m (lieBracket ğ•œ V W) :=
  contDiff_iff_contDiffAt.2 (fun _ â†¦ hV.contDiffAt.lieBracket_vectorField hW.contDiffAt hmn)

theorem lieBracketWithin_of_mem_nhdsWithin (st : t âˆˆ ğ“[s] x) (hs : UniqueDiffWithinAt ğ•œ s x)
    (hV : DifferentiableWithinAt ğ•œ V t x) (hW : DifferentiableWithinAt ğ•œ W t x) :
    lieBracketWithin ğ•œ V W s x = lieBracketWithin ğ•œ V W t x := by
  simp [lieBracketWithin, fderivWithin_of_mem_nhdsWithin st hs hV,
    fderivWithin_of_mem_nhdsWithin st hs hW]

theorem lieBracketWithin_subset (st : s âŠ† t) (ht : UniqueDiffWithinAt ğ•œ s x)
    (hV : DifferentiableWithinAt ğ•œ V t x) (hW : DifferentiableWithinAt ğ•œ W t x) :
    lieBracketWithin ğ•œ V W s x = lieBracketWithin ğ•œ V W t x :=
  lieBracketWithin_of_mem_nhdsWithin (nhdsWithin_mono _ st self_mem_nhdsWithin) ht hV hW

theorem lieBracketWithin_inter (ht : t âˆˆ ğ“ x) :
    lieBracketWithin ğ•œ V W (s âˆ© t) x = lieBracketWithin ğ•œ V W s x := by
  simp [lieBracketWithin, fderivWithin_inter, ht]

theorem lieBracketWithin_of_mem_nhds (h : s âˆˆ ğ“ x) :
    lieBracketWithin ğ•œ V W s x = lieBracket ğ•œ V W x := by
  rw [â† lieBracketWithin_univ, â† univ_inter s, lieBracketWithin_inter h]

theorem lieBracketWithin_of_isOpen (hs : IsOpen s) (hx : x âˆˆ s) :
    lieBracketWithin ğ•œ V W s x = lieBracket ğ•œ V W x :=
  lieBracketWithin_of_mem_nhds (hs.mem_nhds hx)

theorem lieBracketWithin_eq_lieBracket (hs : UniqueDiffWithinAt ğ•œ s x)
    (hV : DifferentiableAt ğ•œ V x) (hW : DifferentiableAt ğ•œ W x) :
    lieBracketWithin ğ•œ V W s x = lieBracket ğ•œ V W x := by
  simp [lieBracketWithin, lieBracket, fderivWithin_eq_fderiv, hs, hV, hW]

/-- Variant of `lieBracketWithin_congr_set` where one requires the sets to coincide only in
the complement of a point. -/
theorem lieBracketWithin_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    lieBracketWithin ğ•œ V W s x = lieBracketWithin ğ•œ V W t x := by
  simp [lieBracketWithin, fderivWithin_congr_set' _ h]

theorem lieBracketWithin_congr_set (h : s =á¶ [ğ“ x] t) :
    lieBracketWithin ğ•œ V W s x = lieBracketWithin ğ•œ V W t x :=
  lieBracketWithin_congr_set' x <| h.filter_mono inf_le_left

/-- Variant of `lieBracketWithin_eventually_congr_set` where one requires the sets to coincide only
in the complement of a point. -/
theorem lieBracketWithin_eventually_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    lieBracketWithin ğ•œ V W s =á¶ [ğ“ x] lieBracketWithin ğ•œ V W t :=
  (eventually_nhds_nhdsWithin.2 h).mono fun _ => lieBracketWithin_congr_set' y

theorem lieBracketWithin_eventually_congr_set (h : s =á¶ [ğ“ x] t) :
    lieBracketWithin ğ•œ V W s =á¶ [ğ“ x] lieBracketWithin ğ•œ V W t :=
  lieBracketWithin_eventually_congr_set' x <| h.filter_mono inf_le_left

theorem _root_.DifferentiableWithinAt.lieBracketWithin_congr_mono
    (hV : DifferentiableWithinAt ğ•œ V s x) (hVs : EqOn Vâ‚ V t) (hVx : Vâ‚ x = V x)
    (hW : DifferentiableWithinAt ğ•œ W s x) (hWs : EqOn Wâ‚ W t) (hWx : Wâ‚ x = W x)
    (hxt : UniqueDiffWithinAt ğ•œ t x) (hâ‚ : t âŠ† s) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ t x = lieBracketWithin ğ•œ V W s x := by
  simp [lieBracketWithin, hV.fderivWithin_congr_mono, hW.fderivWithin_congr_mono, hVs, hVx,
    hWs, hWx, hxt, hâ‚]

theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField_eq
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hxV : Vâ‚ x = V x) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (hxW : Wâ‚ x = W x) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x := by
  simp only [lieBracketWithin, hV.fderivWithin_eq hxV, hW.fderivWithin_eq hxW, hxV, hxW]

theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField_eq_of_mem
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (hx : x âˆˆ s) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x :=
  hV.lieBracketWithin_vectorField_eq (mem_of_mem_nhdsWithin hx hV :)
    hW (mem_of_mem_nhdsWithin hx hW :)

/-- If vector fields coincide on a neighborhood of a point within a set, then the Lie brackets
also coincide on a neighborhood of this point within this set. Version where one considers the Lie
bracket within a subset. -/
theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField'
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) (ht : t âŠ† s) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ t =á¶ [ğ“[s] x] lieBracketWithin ğ•œ V W t := by
  filter_upwards [hV.fderivWithin' ht (ğ•œ := ğ•œ), hW.fderivWithin' ht (ğ•œ := ğ•œ), hV, hW]
    with x hV' hW' hV hW
  simp [lieBracketWithin, hV', hW', hV, hW]

protected theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField
    (hV : Vâ‚ =á¶ [ğ“[s] x] V) (hW : Wâ‚ =á¶ [ğ“[s] x] W) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s =á¶ [ğ“[s] x] lieBracketWithin ğ•œ V W s :=
  hV.lieBracketWithin_vectorField' hW Subset.rfl

protected theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField_eq_of_insert
    (hV : Vâ‚ =á¶ [ğ“[insert x s] x] V) (hW : Wâ‚ =á¶ [ğ“[insert x s] x] W) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x := by
  apply mem_of_mem_nhdsWithin (mem_insert x s) (hV.lieBracketWithin_vectorField' hW
    (subset_insert x s))

theorem _root_.Filter.EventuallyEq.lieBracketWithin_vectorField_eq_nhds
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x :=
  (hV.filter_mono nhdsWithin_le_nhds).lieBracketWithin_vectorField_eq hV.self_of_nhds
    (hW.filter_mono nhdsWithin_le_nhds) hW.self_of_nhds

theorem lieBracketWithin_congr
    (hV : EqOn Vâ‚ V s) (hVx : Vâ‚ x = V x) (hW : EqOn Wâ‚ W s) (hWx : Wâ‚ x = W x) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x :=
  (hV.eventuallyEq.filter_mono inf_le_right).lieBracketWithin_vectorField_eq hVx
    (hW.eventuallyEq.filter_mono inf_le_right) hWx

/-- Version of `lieBracketWithin_congr` in which one assumes that the point belongs to the
given set. -/
theorem lieBracketWithin_congr' (hV : EqOn Vâ‚ V s) (hW : EqOn Wâ‚ W s) (hx : x âˆˆ s) :
    lieBracketWithin ğ•œ Vâ‚ Wâ‚ s x = lieBracketWithin ğ•œ V W s x :=
  lieBracketWithin_congr hV (hV hx) hW (hW hx)

theorem _root_.Filter.EventuallyEq.lieBracket_vectorField_eq
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) :
    lieBracket ğ•œ Vâ‚ Wâ‚ x = lieBracket ğ•œ V W x := by
  rw [â† lieBracketWithin_univ, â† lieBracketWithin_univ, hV.lieBracketWithin_vectorField_eq_nhds hW]

protected theorem _root_.Filter.EventuallyEq.lieBracket_vectorField
    (hV : Vâ‚ =á¶ [ğ“ x] V) (hW : Wâ‚ =á¶ [ğ“ x] W) : lieBracket ğ•œ Vâ‚ Wâ‚ =á¶ [ğ“ x] lieBracket ğ•œ V W := by
  filter_upwards [hV.eventuallyEq_nhds, hW.eventuallyEq_nhds] with y hVy hWy
  exact hVy.lieBracket_vectorField_eq hWy

/-- The Lie bracket of vector fields in vector spaces satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
lemma leibniz_identity_lieBracketWithin_of_isSymmSndFDerivWithinAt
    {U V W : E â†’ E} {s : Set E} {x : E} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s)
    (hU : ContDiffWithinAt ğ•œ 2 U s x) (hV : ContDiffWithinAt ğ•œ 2 V s x)
    (hW : ContDiffWithinAt ğ•œ 2 W s x)
    (h'U : IsSymmSndFDerivWithinAt ğ•œ U s x) (h'V : IsSymmSndFDerivWithinAt ğ•œ V s x)
    (h'W : IsSymmSndFDerivWithinAt ğ•œ W s x) :
    lieBracketWithin ğ•œ U (lieBracketWithin ğ•œ V W s) s x =
      lieBracketWithin ğ•œ (lieBracketWithin ğ•œ U V s) W s x
      + lieBracketWithin ğ•œ V (lieBracketWithin ğ•œ U W s) s x := by
  simp only [lieBracketWithin_eq, map_sub]
  have auxâ‚ {U V : E â†’ E} (hU : ContDiffWithinAt ğ•œ 2 U s x) (hV : ContDiffWithinAt ğ•œ 2 V s x) :
      DifferentiableWithinAt ğ•œ (fun x â†¦ (fderivWithin ğ•œ V s x) (U x)) s x :=
    have := hV.fderivWithin_right_apply (hU.of_le one_le_two) hs le_rfl hx
    this.differentiableWithinAt le_rfl
  have auxâ‚‚ {U V : E â†’ E} (hU : ContDiffWithinAt ğ•œ 2 U s x) (hV : ContDiffWithinAt ğ•œ 2 V s x) :
      fderivWithin ğ•œ (fun y â†¦ (fderivWithin ğ•œ U s y) (V y)) s x =
        (fderivWithin ğ•œ U s x).comp (fderivWithin ğ•œ V s x) +
        (fderivWithin ğ•œ (fderivWithin ğ•œ U s) s x).flip (V x) := by
    refine fderivWithin_clm_apply (hs x hx) ?_ (hV.differentiableWithinAt one_le_two)
    exact (hU.fderivWithin_right hs le_rfl hx).differentiableWithinAt le_rfl
  rw [fderivWithin_fun_sub (hs x hx) (auxâ‚ hV hW) (auxâ‚ hW hV)]
  rw [fderivWithin_fun_sub (hs x hx) (auxâ‚ hU hV) (auxâ‚ hV hU)]
  rw [fderivWithin_fun_sub (hs x hx) (auxâ‚ hU hW) (auxâ‚ hW hU)]
  rw [auxâ‚‚ hW hV, auxâ‚‚ hV hW, auxâ‚‚ hV hU, auxâ‚‚ hU hV, auxâ‚‚ hW hU, auxâ‚‚ hU hW]
  simp only [ContinuousLinearMap.coe_sub', Pi.sub_apply, ContinuousLinearMap.add_apply,
    ContinuousLinearMap.coe_comp', Function.comp_apply, ContinuousLinearMap.flip_apply, h'V.eq,
    h'U.eq, h'W.eq]
  abel

/-- The Lie bracket of vector fields in vector spaces satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
lemma leibniz_identity_lieBracketWithin (hn : minSmoothness ğ•œ 2 â‰¤ n)
    {U V W : E â†’ E} {s : Set E} {x : E}
    (hs : UniqueDiffOn ğ•œ s) (h'x : x âˆˆ closure (interior s)) (hx : x âˆˆ s)
    (hU : ContDiffWithinAt ğ•œ n U s x) (hV : ContDiffWithinAt ğ•œ n V s x)
    (hW : ContDiffWithinAt ğ•œ n W s x) :
    lieBracketWithin ğ•œ U (lieBracketWithin ğ•œ V W s) s x =
      lieBracketWithin ğ•œ (lieBracketWithin ğ•œ U V s) W s x
      + lieBracketWithin ğ•œ V (lieBracketWithin ğ•œ U W s) s x := by
  apply leibniz_identity_lieBracketWithin_of_isSymmSndFDerivWithinAt hs hx
    (hU.of_le (le_minSmoothness.trans hn)) (hV.of_le (le_minSmoothness.trans hn))
    (hW.of_le (le_minSmoothness.trans hn))
  Â· exact hU.isSymmSndFDerivWithinAt hn hs h'x hx
  Â· exact hV.isSymmSndFDerivWithinAt hn hs h'x hx
  Â· exact hW.isSymmSndFDerivWithinAt hn hs h'x hx

/-- The Lie bracket of vector fields in vector spaces satisfies the Leibniz identity
`[U, [V, W]] = [[U, V], W] + [V, [U, W]]`. -/
lemma leibniz_identity_lieBracket (hn : minSmoothness ğ•œ 2 â‰¤ n) {U V W : E â†’ E} {x : E}
    (hU : ContDiffAt ğ•œ n U x) (hV : ContDiffAt ğ•œ n V x) (hW : ContDiffAt ğ•œ n W x) :
    lieBracket ğ•œ U (lieBracket ğ•œ V W) x =
      lieBracket ğ•œ (lieBracket ğ•œ U V) W x + lieBracket ğ•œ V (lieBracket ğ•œ U W) x := by
  simp only [â† lieBracketWithin_univ, â† contDiffWithinAt_univ] at hU hV hW âŠ¢
  exact leibniz_identity_lieBracketWithin hn uniqueDiffOn_univ (by simp) (mem_univ _) hU hV hW


/-!
### The pullback of vector fields in a vector space
-/

variable (ğ•œ) in
/-- The pullback of a vector field under a function, defined
as `(f^* V) (x) = Df(x)^{-1} (V (f x))`. If `Df(x)` is not invertible, we use the junk value `0`.
-/
def pullback (f : E â†’ F) (V : F â†’ F) (x : E) : E := (fderiv ğ•œ f x).inverse (V (f x))

variable (ğ•œ) in
/-- The pullback within a set of a vector field under a function, defined
as `(f^* V) (x) = Df(x)^{-1} (V (f x))` where `Df(x)` is the derivative of `f` within `s`.
If `Df(x)` is not invertible, we use the junk value `0`.
-/
def pullbackWithin (f : E â†’ F) (V : F â†’ F) (s : Set E) (x : E) : E :=
  (fderivWithin ğ•œ f s x).inverse (V (f x))

lemma pullbackWithin_eq {f : E â†’ F} {V : F â†’ F} {s : Set E} :
    pullbackWithin ğ•œ f V s = fun x â†¦ (fderivWithin ğ•œ f s x).inverse (V (f x)) := rfl

lemma pullback_eq_of_fderiv_eq
    {f : E â†’ F} {M : E â‰ƒL[ğ•œ] F} {x : E} (hf : M = fderiv ğ•œ f x) (V : F â†’ F) :
    pullback ğ•œ f V x = M.symm (V (f x)) := by
  simp [pullback, â† hf]

lemma pullback_eq_of_not_isInvertible {f : E â†’ F} {x : E}
    (h : Â¬(fderiv ğ•œ f x).IsInvertible) (V : F â†’ F) :
    pullback ğ•œ f V x = 0 := by
  simp [pullback, h]

lemma pullbackWithin_eq_of_not_isInvertible {f : E â†’ F} {x : E}
    (h : Â¬(fderivWithin ğ•œ f s x).IsInvertible) (V : F â†’ F) :
    pullbackWithin ğ•œ f V s x = 0 := by
  simp [pullbackWithin, h]

lemma pullbackWithin_eq_of_fderivWithin_eq
    {f : E â†’ F} {M : E â‰ƒL[ğ•œ] F} {x : E} (hf : M = fderivWithin ğ•œ f s x) (V : F â†’ F) :
    pullbackWithin ğ•œ f V s x = M.symm (V (f x)) := by
  simp [pullbackWithin, â† hf]

@[simp] lemma pullbackWithin_univ {f : E â†’ F} {V : F â†’ F} :
    pullbackWithin ğ•œ f V univ = pullback ğ•œ f V := by
  ext x
  simp [pullbackWithin, pullback]

open scoped Topology Filter

lemma fderiv_pullback (f : E â†’ F) (V : F â†’ F) (x : E) (h'f : (fderiv ğ•œ f x).IsInvertible) :
    fderiv ğ•œ f x (pullback ğ•œ f V x) = V (f x) := by
  rcases h'f with âŸ¨M, hMâŸ©
  simp [pullback_eq_of_fderiv_eq hM, â† hM]

lemma fderivWithin_pullbackWithin {f : E â†’ F} {V : F â†’ F} {x : E}
    (h'f : (fderivWithin ğ•œ f s x).IsInvertible) :
    fderivWithin ğ•œ f s x (pullbackWithin ğ•œ f V s x) = V (f x) := by
  rcases h'f with âŸ¨M, hMâŸ©
  simp [pullbackWithin_eq_of_fderivWithin_eq hM, â† hM]

open Set

variable [CompleteSpace E]

/-- If a `C^2` map has an invertible derivative within a set at a point, then nearby derivatives
can be written as continuous linear equivs, which depend in a `C^1` way on the point, as well as
their inverse, and moreover one can compute the derivative of the inverse. -/
lemma _root_.exists_continuousLinearEquiv_fderivWithin_symm_eq
    {f : E â†’ F} {s : Set E} {x : E} (h'f : ContDiffWithinAt ğ•œ 2 f s x)
    (hf : (fderivWithin ğ•œ f s x).IsInvertible) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    âˆƒ N : E â†’ (E â‰ƒL[ğ•œ] F), ContDiffWithinAt ğ•œ 1 (fun y â†¦ (N y : E â†’L[ğ•œ] F)) s x
    âˆ§ ContDiffWithinAt ğ•œ 1 (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) s x
    âˆ§ (âˆ€á¶  y in ğ“[s] x, N y = fderivWithin ğ•œ f s y)
    âˆ§ âˆ€ v, fderivWithin ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) s x v
      = - (N x).symm  âˆ˜L ((fderivWithin ğ•œ (fderivWithin ğ•œ f s) s x v)) âˆ˜L (N x).symm := by
  classical
  rcases hf with âŸ¨M, hMâŸ©
  let U := {y | âˆƒ (N : E â‰ƒL[ğ•œ] F), N = fderivWithin ğ•œ f s y}
  have hU : U âˆˆ ğ“[s] x := by
    have I : range ((â†‘) : (E â‰ƒL[ğ•œ] F) â†’ E â†’L[ğ•œ] F) âˆˆ ğ“ (fderivWithin ğ•œ f s x) := by
      rw [â† hM]
      exact M.nhds
    have : ContinuousWithinAt (fderivWithin ğ•œ f s) s x :=
      (h'f.fderivWithin_right (m := 1) hs le_rfl hx).continuousWithinAt
    exact this I
  let N : E â†’ (E â‰ƒL[ğ•œ] F) := fun x â†¦ if h : x âˆˆ U then h.choose else M
  have eN : (fun y â†¦ (N y : E â†’L[ğ•œ] F)) =á¶ [ğ“[s] x] fun y â†¦ fderivWithin ğ•œ f s y := by
    filter_upwards [hU] with y hy
    simpa only [hy, â†“reduceDIte, N] using Exists.choose_spec hy
  have e'N : N x = fderivWithin ğ•œ f s x := by apply mem_of_mem_nhdsWithin hx eN
  have hN : ContDiffWithinAt ğ•œ 1 (fun y â†¦ (N y : E â†’L[ğ•œ] F)) s x := by
    have : ContDiffWithinAt ğ•œ 1 (fun y â†¦ fderivWithin ğ•œ f s y) s x :=
      h'f.fderivWithin_right (m := 1) hs le_rfl hx
    apply this.congr_of_eventuallyEq eN e'N
  have hN' : ContDiffWithinAt ğ•œ 1 (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) s x := by
    have : ContDiffWithinAt ğ•œ 1 (ContinuousLinearMap.inverse âˆ˜ (fun y â†¦ (N y : E â†’L[ğ•œ] F))) s x :=
      (contDiffAt_map_inverse (N x)).comp_contDiffWithinAt x hN
    convert this with y
    simp only [Function.comp_apply, ContinuousLinearMap.inverse_equiv]
  refine âŸ¨N, hN, hN', eN, fun v â†¦ ?_âŸ©
  have A' y : ContinuousLinearMap.compL ğ•œ F E F (N y : E â†’L[ğ•œ] F) ((N y).symm : F â†’L[ğ•œ] E)
      = ContinuousLinearMap.id ğ•œ F := by ext; simp
  have : fderivWithin ğ•œ (fun y â†¦ ContinuousLinearMap.compL ğ•œ F E F (N y : E â†’L[ğ•œ] F)
      ((N y).symm : F â†’L[ğ•œ] E)) s x v = 0 := by
    simp [A', fderivWithin_const_apply]
  have I : (N x : E â†’L[ğ•œ] F) âˆ˜L (fderivWithin ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) s x v) =
      - (fderivWithin ğ•œ (fun y â†¦ (N y : E â†’L[ğ•œ] F)) s x v) âˆ˜L ((N x).symm : F â†’L[ğ•œ] E) := by
    rw [ContinuousLinearMap.fderivWithin_of_bilinear _ (hN.differentiableWithinAt le_rfl)
      (hN'.differentiableWithinAt le_rfl) (hs x hx)] at this
    simpa [eq_neg_iff_add_eq_zero] using this
  have B (M : F â†’L[ğ•œ] E) : M = ((N x).symm : F â†’L[ğ•œ] E) âˆ˜L ((N x) âˆ˜L M) := by
    ext; simp
  rw [B (fderivWithin ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) s x v), I]
  simp only [ContinuousLinearMap.comp_neg, eN.fderivWithin_eq e'N]

lemma DifferentiableWithinAt.pullbackWithin {f : E â†’ F} {V : F â†’ F} {s : Set E} {t : Set F} {x : E}
    (hV : DifferentiableWithinAt ğ•œ V t (f x))
    (hf : ContDiffWithinAt ğ•œ 2 f s x) (hf' : (fderivWithin ğ•œ f s x).IsInvertible)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    DifferentiableWithinAt ğ•œ (pullbackWithin ğ•œ f V s) s x := by
  rcases exists_continuousLinearEquiv_fderivWithin_symm_eq hf hf' hs hx
    with âŸ¨M, -, M_symm_smooth, hM, -âŸ©
  simp only [pullbackWithin_eq]
  have : DifferentiableWithinAt ğ•œ (fun y â†¦ ((M y).symm : F â†’L[ğ•œ] E) (V (f y))) s x := by
    apply DifferentiableWithinAt.clm_apply
    Â· exact M_symm_smooth.differentiableWithinAt le_rfl
    Â· exact hV.comp _ (hf.differentiableWithinAt one_le_two) hst
  apply this.congr_of_eventuallyEq
  Â· filter_upwards [hM] with y hy using by simp [â† hy]
  Â· have hMx : M x = fderivWithin ğ•œ f s x := by apply mem_of_mem_nhdsWithin hx hM
    simp [â† hMx]

/-- If a `C^2` map has an invertible derivative at a point, then nearby derivatives can be written
as continuous linear equivs, which depend in a `C^1` way on the point, as well as their inverse, and
moreover one can compute the derivative of the inverse. -/
lemma _root_.exists_continuousLinearEquiv_fderiv_symm_eq
    {f : E â†’ F} {x : E} (h'f : ContDiffAt ğ•œ 2 f x) (hf : (fderiv ğ•œ f x).IsInvertible) :
    âˆƒ N : E â†’ (E â‰ƒL[ğ•œ] F), ContDiffAt ğ•œ 1 (fun y â†¦ (N y : E â†’L[ğ•œ] F)) x
    âˆ§ ContDiffAt ğ•œ 1 (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x
    âˆ§ (âˆ€á¶  y in ğ“ x, N y = fderiv ğ•œ f y)
    âˆ§ âˆ€ v, fderiv ğ•œ (fun y â†¦ ((N y).symm : F â†’L[ğ•œ] E)) x v
      = - (N x).symm  âˆ˜L ((fderiv ğ•œ (fderiv ğ•œ f) x v)) âˆ˜L (N x).symm := by
  simp only [â† fderivWithin_univ, â† contDiffWithinAt_univ, â† nhdsWithin_univ] at hf h'f âŠ¢
  exact exists_continuousLinearEquiv_fderivWithin_symm_eq h'f hf uniqueDiffOn_univ (mem_univ _)

/-- The Lie bracket commutes with taking pullbacks. This requires the function to have symmetric
second derivative. Version in a complete space. One could also give a version avoiding
completeness but requiring that `f` is a local diffeo. -/
lemma pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt
    {f : E â†’ F} {V W : F â†’ F} {x : E} {t : Set F}
    (hf : IsSymmSndFDerivWithinAt ğ•œ f s x) (h'f : ContDiffWithinAt ğ•œ 2 f s x)
    (hV : DifferentiableWithinAt ğ•œ V t (f x)) (hW : DifferentiableWithinAt ğ•œ W t (f x))
    (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hst : MapsTo f s t) :
    pullbackWithin ğ•œ f (lieBracketWithin ğ•œ V W t) s x
      = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V s) (pullbackWithin ğ•œ f W s) s x := by
  by_cases h : (fderivWithin ğ•œ f s x).IsInvertible; swap
  Â· simp [pullbackWithin_eq_of_not_isInvertible h, lieBracketWithin_eq]
  rcases exists_continuousLinearEquiv_fderivWithin_symm_eq h'f h hu hx
    with âŸ¨M, -, M_symm_smooth, hM, M_diffâŸ©
  have hMx : M x = fderivWithin ğ•œ f s x := (mem_of_mem_nhdsWithin hx hM :)
  have AV : fderivWithin ğ•œ (pullbackWithin ğ•œ f V s) s x =
      fderivWithin ğ•œ (fun y â†¦ ((M y).symm : F â†’L[ğ•œ] E) (V (f y))) s x := by
    apply Filter.EventuallyEq.fderivWithin_eq_of_mem _ hx
    filter_upwards [hM] with y hy using pullbackWithin_eq_of_fderivWithin_eq hy _
  have AW : fderivWithin ğ•œ (pullbackWithin ğ•œ f W s) s x =
      fderivWithin ğ•œ (fun y â†¦ ((M y).symm : F â†’L[ğ•œ] E) (W (f y))) s x := by
    apply Filter.EventuallyEq.fderivWithin_eq_of_mem _ hx
    filter_upwards [hM] with y hy using pullbackWithin_eq_of_fderivWithin_eq hy _
  have Af : DifferentiableWithinAt ğ•œ f s x := h'f.differentiableWithinAt one_le_two
  simp only [lieBracketWithin_eq, pullbackWithin_eq_of_fderivWithin_eq hMx, map_sub, AV, AW]
  rw [fderivWithin_clm_apply, fderivWithin_clm_apply]
  Â· simp [fderivWithin_comp' x hW Af hst (hu x hx), â† hMx,
      fderivWithin_comp' x hV Af hst (hu x hx), M_diff, hf.eq]
  Â· exact hu x hx
  Â· exact M_symm_smooth.differentiableWithinAt le_rfl
  Â· exact hV.comp x Af hst
  Â· exact hu x hx
  Â· exact M_symm_smooth.differentiableWithinAt le_rfl
  Â· exact hW.comp x Af hst

/-- The Lie bracket commutes with taking pullbacks. This requires the function to have symmetric
second derivative. Version in a complete space. One could also give a version avoiding
completeness but requiring that `f` is a local diffeo. Variant where unique differentiability and
the invariance property are only required in a smaller set `u`. -/
lemma pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt_of_eventuallyEq
    {f : E â†’ F} {V W : F â†’ F} {x : E} {t : Set F} {u : Set E}
    (hf : IsSymmSndFDerivWithinAt ğ•œ f s x) (h'f : ContDiffWithinAt ğ•œ 2 f s x)
    (hV : DifferentiableWithinAt ğ•œ V t (f x)) (hW : DifferentiableWithinAt ğ•œ W t (f x))
    (hu : UniqueDiffOn ğ•œ u) (hx : x âˆˆ u) (hst : MapsTo f u t) (hus : u =á¶ [ğ“ x] s) :
    pullbackWithin ğ•œ f (lieBracketWithin ğ•œ V W t) s x
      = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V s) (pullbackWithin ğ•œ f W s) s x := calc
  pullbackWithin ğ•œ f (lieBracketWithin ğ•œ V W t) s x
  _ = pullbackWithin ğ•œ f (lieBracketWithin ğ•œ V W t) u x := by
    simp only [pullbackWithin]
    congr 2
    exact fderivWithin_congr_set hus.symm
  _ = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V u) (pullbackWithin ğ•œ f W u) u x :=
    pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt
      (hf.congr_set hus.symm) (h'f.congr_set hus.symm) hV hW hu hx hst
  _ = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V s) (pullbackWithin ğ•œ f W s) u x := by
    apply Filter.EventuallyEq.lieBracketWithin_vectorField_eq_of_mem _ _ hx
    Â· apply nhdsWithin_le_nhds
      filter_upwards [fderivWithin_eventually_congr_set (ğ•œ := ğ•œ) (f := f) hus] with y hy
      simp [pullbackWithin, hy]
    Â· apply nhdsWithin_le_nhds
      filter_upwards [fderivWithin_eventually_congr_set (ğ•œ := ğ•œ) (f := f) hus] with y hy
      simp [pullbackWithin, hy]
  _ = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V s) (pullbackWithin ğ•œ f W s) s x :=
    lieBracketWithin_congr_set hus

/-- The Lie bracket commutes with taking pullbacks. This requires the function to have symmetric
second derivative. Version in a complete space. One could also give a version avoiding
completeness but requiring that `f` is a local diffeo. -/
lemma pullback_lieBracket_of_isSymmSndFDerivAt {f : E â†’ F} {V W : F â†’ F} {x : E}
    (hf : IsSymmSndFDerivAt ğ•œ f x) (h'f : ContDiffAt ğ•œ 2 f x)
    (hV : DifferentiableAt ğ•œ V (f x)) (hW : DifferentiableAt ğ•œ W (f x)) :
    pullback ğ•œ f (lieBracket ğ•œ V W) x = lieBracket ğ•œ (pullback ğ•œ f V) (pullback ğ•œ f W) x := by
  simp only [â† lieBracketWithin_univ, â† pullbackWithin_univ, â† isSymmSndFDerivWithinAt_univ,
    â† differentiableWithinAt_univ] at hf h'f hV hW âŠ¢
  exact pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt hf h'f hV hW uniqueDiffOn_univ
    (mem_univ _) (mapsTo_univ _ _)

/-- The Lie bracket commutes with taking pullbacks. This requires the function to have symmetric
second derivative. Version in a complete space. One could also give a version avoiding
completeness but requiring that `f` is a local diffeo. -/
lemma pullbackWithin_lieBracketWithin
    {f : E â†’ F} {V W : F â†’ F} {x : E} {t : Set F} (hn : minSmoothness ğ•œ 2 â‰¤ n)
    (h'f : ContDiffWithinAt ğ•œ n f s x)
    (hV : DifferentiableWithinAt ğ•œ V t (f x)) (hW : DifferentiableWithinAt ğ•œ W t (f x))
    (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (h'x : x âˆˆ closure (interior s)) (hst : MapsTo f s t) :
    pullbackWithin ğ•œ f (lieBracketWithin ğ•œ V W t) s x
      = lieBracketWithin ğ•œ (pullbackWithin ğ•œ f V s) (pullbackWithin ğ•œ f W s) s x :=
  pullbackWithin_lieBracketWithin_of_isSymmSndFDerivWithinAt
  (h'f.isSymmSndFDerivWithinAt hn hu h'x hx) (h'f.of_le (le_minSmoothness.trans hn)) hV hW hu hx hst

/-- The Lie bracket commutes with taking pullbacks. One could also give a version avoiding
completeness but requiring that `f` is a local diffeo. -/
lemma pullback_lieBracket (hn : minSmoothness ğ•œ 2 â‰¤ n)
    {f : E â†’ F} {V W : F â†’ F} {x : E} (h'f : ContDiffAt ğ•œ n f x)
    (hV : DifferentiableAt ğ•œ V (f x)) (hW : DifferentiableAt ğ•œ W (f x)) :
    pullback ğ•œ f (lieBracket ğ•œ V W) x = lieBracket ğ•œ (pullback ğ•œ f V) (pullback ğ•œ f W) x :=
  pullback_lieBracket_of_isSymmSndFDerivAt (h'f.isSymmSndFDerivAt hn)
    (h'f.of_le (le_minSmoothness.trans hn)) hV hW

end VectorField
