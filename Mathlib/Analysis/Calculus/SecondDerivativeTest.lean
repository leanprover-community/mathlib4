/-
Copyright (c) 2024 BjÃ¸rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BjÃ¸rn Kjos-Hanssen, Jireh Loreaux, Floris van Doorn, Eric Wieser
-/
import Mathlib.Analysis.Calculus.FirstDerivativeTest
import Mathlib.Order.Interval.Set.Basic

/-!
# The Second-Derivative Test

We prove the Second-Derivative test from calculus using the First-Derivative test.
Source: [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#Proof_of_the_second-derivative_test).

## Main results

* `isLocalMin_of_deriv_deriv_pos`: The second-derivative test, minimum version.

## Tags

derivative test, calculus
-/

open Set Filter Topology

section SecondDeriv

variable {f : â„ â†’ â„} {xâ‚€ : â„}


lemma slope_pos_iff {ğ•œ} [LinearOrderedField ğ•œ] {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : xâ‚€ < b) :
    0 < slope f xâ‚€ b â†” f xâ‚€ < f b := by
  simp [slope, hb]

lemma slope_pos_iff_gt {ğ•œ} [LinearOrderedField ğ•œ] {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : b < xâ‚€) :
    0 < slope f xâ‚€ b â†” f b < f xâ‚€ := by
  rw [slope_comm, slope_pos_iff hb]

lemma pos_of_slope_pos {b : â„} (hb : xâ‚€ < b) (hbf : 0 < slope f xâ‚€ b)
    (hf : f xâ‚€ = 0) : 0 < f b := by
  simp_all [slope, hf]

lemma neg_of_slope_pos {b : â„} (hb : b < xâ‚€) (hbf : 0 < slope f xâ‚€ b)
    (hf : f xâ‚€ = 0) : f b < 0 := by
  simp_all [slope, hf]
  exact neg_of_mul_pos_right hbf <| le_of_lt <| inv_lt_zero.mpr <| by linarith

lemma neg_of_slope_neg {b : â„} (hb : b < xâ‚€) (hbf : 0 < slope f xâ‚€ b)
    (hf : f xâ‚€ = 0) : f b < 0 := by
  simp_all [slope]
  exact neg_of_mul_pos_right hbf <| le_of_lt <| inv_lt_zero.mpr <| by linarith

open SignType in
lemma eventually_nhdsWithin_sign_eq_of_deriv_pos (hf : deriv f xâ‚€ > 0) (hx : f xâ‚€ = 0) :
    âˆ€á¶  x in ğ“ xâ‚€, sign (f x) = sign (x - xâ‚€) := by
  rw [â† nhdsWithin_compl_singleton_sup_pure xâ‚€, eventually_sup]
  refine âŸ¨?_, by simpaâŸ©
  have h_tendsto := hasDerivAt_iff_tendsto_slope.mp
    (differentiableAt_of_deriv_ne_zero <| ne_of_gt hf).hasDerivAt
  filter_upwards [(h_tendsto.eventually <| eventually_gt_nhds hf),
    self_mem_nhdsWithin] with x hxâ‚€ hxâ‚
  rw [mem_compl_iff, mem_singleton_iff, â† Ne.eq_def] at hxâ‚
  obtain (hx' | hx') := hxâ‚.lt_or_lt
  Â· rw [sign_neg (neg_of_slope_pos hx' hxâ‚€ hx), sign_neg (sub_neg.mpr hx')]
  Â· rw [sign_pos (pos_of_slope_pos hx' hxâ‚€ hx), sign_pos (sub_pos.mpr hx')]

/-- The Second-Derivative Test from calculus, minimum version.
This version applies to functions like `x^2 + 1[x â‰¥ 0]` as well as twice differentiable
functions.
 -/
theorem isLocalMin_of_deriv_deriv_pos (hf : deriv (deriv f) xâ‚€ > 0) (hd : deriv f xâ‚€ = 0)
    (hc : ContinuousAt f xâ‚€) : IsLocalMin f xâ‚€ := by
  have hâ‚€ : âˆ€á¶  (b : â„) in ğ“[<] xâ‚€, deriv f b < 0 := by
    have := eventually_nhdsWithin_sign_eq_of_deriv_pos hf hd
    exact (eventually_nhdsWithin_of_eventually_nhds this).mp <|
      eventually_nhdsWithin_of_forall <| fun x hxâ‚€ hxâ‚ => by
        rw [sign_neg <| sub_neg.mpr hxâ‚€] at hxâ‚
        simp only [SignType.sign, OrderHom.coe_mk] at hxâ‚
        split at hxâ‚
        Â· simp at hxâ‚
        Â· split at hxâ‚ <;> tauto
  have hâ‚ : âˆ€á¶  (b : â„) in ğ“[>] xâ‚€, deriv f b > 0 := by
    have := eventually_nhdsWithin_sign_eq_of_deriv_pos hf hd
    exact (eventually_nhdsWithin_of_eventually_nhds this).mp <|
      eventually_nhdsWithin_of_forall <| fun x hxâ‚€ hxâ‚ => by
        rw [sign_pos <| sub_pos.mpr hxâ‚€] at hxâ‚
        simp [SignType.sign] at hxâ‚
        split_ifs at hxâ‚ with gâ‚€ <;>
        (simp only [imp_false, not_le] at hxâ‚; exact hxâ‚)
  have hfâ‚€ : âˆ€á¶  (x : â„) in (ğ“[<] xâ‚€ âŠ” ğ“[>] xâ‚€), deriv f x â‰  0 :=
    eventually_sup.mpr âŸ¨hâ‚€.mono fun x hx => (ne_of_gt hx).symm,
                        hâ‚.mono fun x hx => (ne_of_lt hx).symmâŸ©
  have hf : âˆ€á¶  (x : â„) in ğ“[â‰ ] xâ‚€, deriv f x â‰  0 := (nhdsLT_sup_nhdsGT xâ‚€) â–¸ hfâ‚€
  exact isLocalMin_of_deriv hc (hf.mono fun x a â†¦ differentiableAt_of_deriv_ne_zero a)
    (hâ‚€.mono fun _ => le_of_lt) (hâ‚.mono fun _ => le_of_lt)

end SecondDeriv
