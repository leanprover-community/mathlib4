/-
Copyright (c) 2024 BjÃ¸rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BjÃ¸rn Kjos-Hanssen, Jireh Loreaux, Floris van Doorn, Eric Wieser
-/
import Mathlib.Analysis.Calculus.FirstDerivativeTest
import Mathlib.Order.Interval.Set.Basic
import Mathlib.LinearAlgebra.AffineSpace.Slope
import Mathlib.LinearAlgebra.AffineSpace.Ordered

/-!
# The Second-Derivative Test

We prove the Second-Derivative test from calculus using the First-Derivative test.
Source: [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#Proof_of_the_second-derivative_test).

## Main results

* `isLocalMin_of_deriv_deriv_pos`: The second-derivative test, minimum version.

## Tags

derivative test, calculus
-/

open Set Filter Topology SignType

section SecondDeriv

variable {f : â„ â†’ â„} {xâ‚€ : â„}


lemma slope_pos_iff {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : xâ‚€ < b) :
    0 < slope f xâ‚€ b â†” f xâ‚€ < f b := by
  simp [slope, hb]

lemma slope_pos_iff_gt {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ} (hb : b < xâ‚€) :
    0 < slope f xâ‚€ b â†” f b < f xâ‚€ := by
  rw [slope_comm, slope_pos_iff hb]

lemma pos_of_slope_pos {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ}
    (hb : xâ‚€ < b) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : 0 < f b := by
  simp_all [slope, hf]

lemma neg_of_slope_pos {ğ•œ} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ]
    {f : ğ•œ â†’ ğ•œ} {xâ‚€ b : ğ•œ}
    (hb : b < xâ‚€) (hbf : 0 < slope f xâ‚€ b) (hf : f xâ‚€ = 0) : f b < 0 := by
  rwa [slope_pos_iff_gt, hf] at hbf
  exact hb



/-- If the derivative of `f` is positive at a root `xâ‚€` of `f`, then locally the sign of `f x`
matches `x - xâ‚€`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_pos (hf : deriv f xâ‚€ > 0) (hx : f xâ‚€ = 0) :
    âˆ€á¶  x in ğ“ xâ‚€, sign (f x) = sign (x - xâ‚€) := by
  rw [â† nhdsNE_sup_pure xâ‚€, eventually_sup]
  refine âŸ¨?_, by simpaâŸ©
  have h_tendsto := hasDerivAt_iff_tendsto_slope.mp
    (differentiableAt_of_deriv_ne_zero <| ne_of_gt hf).hasDerivAt
  filter_upwards [(h_tendsto.eventually <| eventually_gt_nhds hf),
    self_mem_nhdsWithin] with x hxâ‚€ hxâ‚
  rw [mem_compl_iff, mem_singleton_iff, â† Ne.eq_def] at hxâ‚
  obtain (hx' | hx') := hxâ‚.lt_or_lt
  Â· rw [sign_neg (neg_of_slope_pos hx' hxâ‚€ hx), sign_neg (sub_neg.mpr hx')]
  Â· rw [sign_pos (pos_of_slope_pos hx' hxâ‚€ hx), sign_pos (sub_pos.mpr hx')]

/-- If the derivative of `f` is negative at a root `xâ‚€` of `f`, then locally the sign of `f x`
matches `xâ‚€ - x`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_neg (hf : deriv f xâ‚€ < 0) (hx : f xâ‚€ = 0) :
    âˆ€á¶  x in ğ“ xâ‚€, sign (f x) = sign (xâ‚€ - x) := by
  simpa [Left.sign_neg, -neg_sub, â† neg_sub xâ‚€] using
    eventually_nhdsWithin_sign_eq_of_deriv_pos
        (f := (-f Â·)) (xâ‚€ := xâ‚€) (by simpa [deriv.neg]) (by simpa)

lemma deriv_neg_left_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“ xâ‚€, sign (deriv f x) = sign (x - xâ‚€)) :
    âˆ€á¶  (b : â„) in ğ“[<] xâ‚€, deriv f b < 0 := by
  filter_upwards [inter_mem_nhdsWithin _ hâ‚€] with x âŸ¨(hx : x < xâ‚€), (hx' : sign _ = _)âŸ©
  rwa [â† sub_neg, â† sign_eq_neg_one_iff, â† hx', sign_eq_neg_one_iff] at hx

lemma deriv_neg_right_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“ xâ‚€, sign (deriv f x) = sign (xâ‚€ - x)) :
     âˆ€á¶  (b : â„) in ğ“[>] xâ‚€, deriv f b < 0 := by
  filter_upwards [inter_mem_nhdsWithin _ hâ‚€] with x âŸ¨(hx : xâ‚€ < x), (hx' : sign _ = _)âŸ©
  rwa [â† sub_neg, â† sign_eq_neg_one_iff, â† hx', sign_eq_neg_one_iff] at hx

lemma deriv_pos_right_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“ xâ‚€, sign (deriv f x) = sign (x - xâ‚€)) :
     âˆ€á¶  (b : â„) in ğ“[>] xâ‚€, deriv f b > 0 := by
  filter_upwards [inter_mem_nhdsWithin _ hâ‚€] with x âŸ¨(hx : xâ‚€ < x), (hx' : sign _ = _)âŸ©
  rwa [â† sub_pos, â† sign_eq_one_iff, â† hx', sign_eq_one_iff] at hx

lemma deriv_pos_left_of_sign_deriv {f : â„ â†’ â„} {xâ‚€ : â„}
    (hâ‚€ : âˆ€á¶  (x : â„) in ğ“ xâ‚€, sign (deriv f x) = sign (xâ‚€ - x)) :
    âˆ€á¶  (b : â„) in ğ“[<] xâ‚€, deriv f b > 0 := by
  filter_upwards [inter_mem_nhdsWithin _ hâ‚€] with x âŸ¨(hx : x < xâ‚€), (hx' : sign _ = _)âŸ©
  rwa [â† sub_pos, â† sign_eq_one_iff, â† hx', sign_eq_one_iff] at hx

/-- The Second-Derivative Test from calculus, minimum version.
Applies to functions like `x^2 + 1[x â‰¥ 0]` as well as twice differentiable
functions. -/
theorem isLocalMin_of_deriv_deriv_pos (hf : deriv (deriv f) xâ‚€ > 0) (hd : deriv f xâ‚€ = 0)
    (hc : ContinuousAt f xâ‚€) : IsLocalMin f xâ‚€ := by
  have hâ‚€ := eventually_nhdsWithin_sign_eq_of_deriv_pos hf hd
  have hl := deriv_neg_left_of_sign_deriv hâ‚€
  have hg := deriv_pos_right_of_sign_deriv hâ‚€
  have hfâ‚€ := eventually_sup.mpr âŸ¨hl.mono fun x hx => (ne_of_gt hx).symm,
                                  hg.mono fun x hx => (ne_of_lt hx).symmâŸ©
  have hf := (nhdsLT_sup_nhdsGT xâ‚€) â–¸ hfâ‚€
  exact isLocalMin_of_deriv hc (hf.mono fun x a â†¦ differentiableAt_of_deriv_ne_zero a)
    (hl.mono fun _ => le_of_lt) (hg.mono fun _ => le_of_lt)

/-- The Second-Derivative Test from calculus, maximum version. -/
theorem isLocalMax_of_deriv_deriv_neg (hf : deriv (deriv f) xâ‚€ < 0) (hd : deriv f xâ‚€ = 0)
    (hc : ContinuousAt f xâ‚€) : IsLocalMax f xâ‚€ := by
  simpa using isLocalMin_of_deriv_deriv_pos (by simpa) (by simpa) hc.neg |>.neg

end SecondDeriv
