/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Calculus.Deriv.ZPow

/-!
# Logarithmic Derivatives

We define the logarithmic derivative of a function f as `deriv f / f`. We then prove some basic
facts about this, including how it changes under multiplication and composition.

We conclude by showing that the logarithmic derivative of a sequence of functions converging
locally uniformly to a function is the logarithmic derivative of the limit function. This is useful
for example for the Mittag-Leffler expansion of the cotangent function.
-/

noncomputable section

open Filter Function

open scoped Topology BigOperators Classical

variable {ð•œ ð•œ': Type*} [NontriviallyNormedField ð•œ] [NontriviallyNormedField ð•œ']
  [NormedAlgebra ð•œ ð•œ']

/-- The logarithmic derivative of a function defined as `deriv f /f`. Note that it will be zero
at `x` if `f` is not DifferentiableAt `x`. -/
def logDeriv (f : ð•œ â†’ ð•œ') :=
  deriv f / f

lemma logDeriv_eq_zero_of_not_differentiableAt (f : ð•œ â†’ ð•œ') (x : ð•œ) (h : Â¬DifferentiableAt ð•œ f x) :
    logDeriv f x = 0 := by
  simp only [logDeriv, Pi.div_apply, deriv_zero_of_not_differentiableAt h, zero_div]

@[simp]
theorem logDeriv_id (x : ð•œ) : logDeriv id x = 1 / x := by
  rw [logDeriv]
  simp only [deriv_id', Pi.div_apply, id_eq, one_div]

@[simp]
theorem logDeriv_const (a : ð•œ') : logDeriv (fun _ : ð•œ â†¦ a) = 0 := by
  rw [logDeriv]
  ext1 x
  simp only [deriv_const', Pi.div_apply, zero_div, Pi.zero_apply]

theorem logDeriv_mul {f g : ð•œ â†’ ð•œ'} (x : ð•œ) (hf : f x â‰  0) (hg : g x â‰  0)
    (hdf : DifferentiableAt ð•œ f x) (hdg : DifferentiableAt ð•œ g x) :
      logDeriv (fun z => f z * g z) x = logDeriv f x + logDeriv g x := by
  simp only [logDeriv, Pi.div_apply, deriv_mul hdf hdg]
  field_simp [hf, hg , mul_comm]

theorem logDeriv_mul_const {f : ð•œ â†’ ð•œ'} (x : ð•œ) (a : ð•œ') (ha : a â‰  0):
    logDeriv (fun z => f z * a) x = logDeriv f x := by
  simp only [logDeriv, deriv_mul_const_field', Pi.div_apply]
  rw [mul_div_mul_right (deriv (fun x â†¦ f x) x) (f x) ha]

theorem logDeriv_const_mul {f : ð•œ â†’ ð•œ'} (x : ð•œ) (a : ð•œ') (ha : a â‰  0):
    logDeriv (fun z => a * f z) x = logDeriv f x := by
  simp only [logDeriv, deriv_const_mul_field', Pi.div_apply]
  rw [mul_div_mul_left (deriv (fun x â†¦ f x) x) (f x) ha]

/-- The logarithmic derivative of a finite product is the sum of the logarithmic derivatives. -/
theorem logDeriv_prod {Î± : Type*} (s : Finset Î±) (f : Î± â†’ ð•œ â†’ ð•œ') (t : ð•œ) (hf : âˆ€ x âˆˆ s, f x t â‰  0)
    (hd : âˆ€ x âˆˆ s, DifferentiableAt ð•œ (f x) t) :
    logDeriv (âˆ i in s, f i) t = âˆ‘ i in s, logDeriv (f i) t := by
  induction' s using Finset.cons_induction_on with a s ha ih
  Â· simp only [Finset.prod_empty, Finset.sum_empty]
    exact congrFun (logDeriv_const (1 : ð•œ')) t
  Â· rw [Finset.forall_mem_cons] at hf
    rw [Finset.cons_eq_insert _ _ ha, Finset.prod_insert ha, Finset.sum_insert ha]
    have := logDeriv_mul (f := f a) (g := âˆ i in s, f i) t ?_ ?_ ?_ ?_
    Â· simp only [ne_eq, Finset.cons_eq_insert, Finset.mem_insert, forall_eq_or_imp,
        Finset.prod_apply] at *
      rw [ih hf.2 (fun _ hx â†¦ hd.2 _ hx)] at this
      rw [â† this]
      congr
      exact Finset.prod_fn s fun c â†¦ f c
    Â· exact hf.1
    Â· simp only [Finset.prod_apply, Finset.prod_ne_zero_iff]
      exact hf.2
    Â· apply hd
      simp only [Finset.cons_eq_insert, Finset.mem_insert, eq_self_iff_true, true_or_iff]
    Â· rw [Finset.prod_fn]
      apply DifferentiableAt.finset_prod
      intro r hr
      apply hd
      simp only [Finset.cons_eq_insert, Finset.mem_insert, hr, or_true]

lemma logDeriv_pow {x : ð•œ} {n : â„•} (hn : n â‰  0) (hx : x â‰  0) :
    logDeriv (fun z => z ^ n) x = n * logDeriv id x := by
  simp only [logDeriv, deriv_pow', Pi.div_apply, deriv_id', id_eq, one_div]
  field_simp [hx, mul_assoc, pow_sub_one_mul hn x]

lemma logDeriv_zpow {x : ð•œ} {n : â„¤} (hx : x â‰  0) :
    logDeriv (fun z => z ^ n) x = n * logDeriv id x := by
  simp only [logDeriv, deriv_zpow', Pi.div_apply, zpow_sub_oneâ‚€ hx, deriv_id', id_eq, one_div]
  field_simp [hx, mul_assoc, mul_comm (x^n) x]
  simpa only [one_mul, div_one] using
    (mul_div_mul_right (n : ð•œ) 1 (mul_ne_zero hx (zpow_ne_zero n hx)))

theorem logDeriv_comp {f g : ð•œ â†’ ð•œ} {x : ð•œ} (hf : DifferentiableAt ð•œ f (g x))
    (hg : DifferentiableAt ð•œ g x) : logDeriv (f âˆ˜ g) x = (logDeriv f) (g x) * deriv g x := by
  simp only [logDeriv, Pi.div_apply, deriv.comp _ hf hg, comp_apply]
  ring

lemma logDeriv_fun_pow {f : ð•œ â†’ ð•œ} {x : ð•œ} {n : â„•} (hn : n â‰  0) (hf : f x â‰  0)
    (hdf : DifferentiableAt ð•œ f x) :  logDeriv (fun z => (f z) ^ n) x = n * logDeriv f x := by
  rw [â† comp_def (fun z => z^n) f,
    logDeriv_comp (f := fun z => z^n) (g := f) (differentiableAt_pow n) hdf, logDeriv_pow hn hf]
  simp only [logDeriv, deriv_id', Pi.div_apply, id_eq, one_div]
  ring

lemma logDeriv_fun_zpow {f : ð•œ â†’ ð•œ} {x : ð•œ} {n : â„¤} (hf : f x â‰  0) (hdf : DifferentiableAt ð•œ f x) :
    logDeriv (fun z => (f z) ^ n) x = n * logDeriv f x := by
  rw [show (fun z => (f z)^n) = (fun z => z^n) âˆ˜ f by rfl,
    logDeriv_comp (f := fun z => z^n) (g := f) ?_ hdf, logDeriv_zpow hf]
  Â· simp only [logDeriv, deriv_id', Pi.div_apply, id_eq, one_div]
    ring
  Â· simp only [differentiableAt_zpow, ne_eq, hf, not_false_eq_true, true_or]
