/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.SmoothSeries
import Mathlib.Analysis.Calculus.BumpFunction.InnerProduct
import Mathlib.Analysis.Convolution
import Mathlib.Analysis.InnerProductSpace.EuclideanDist
import Mathlib.Data.Set.Pointwise.Support
import Mathlib.MeasureTheory.Measure.Haar.NormedSpace
import Mathlib.MeasureTheory.Measure.Haar.Unique

#align_import analysis.calculus.bump_function_findim from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Bump functions in finite-dimensional vector spaces

Let `E` be a finite-dimensional real normed vector space. We show that any open set `s` in `E` is
exactly the support of a smooth function taking values in `[0, 1]`,
in `IsOpen.exists_smooth_support_eq`.

Then we use this construction to construct bump functions with nice behavior, by convolving
the indicator function of `closedBall 0 1` with a function as above with `s = ball 0 D`.
-/


noncomputable section

open Set Metric TopologicalSpace Function Asymptotics MeasureTheory FiniteDimensional
  ContinuousLinearMap Filter MeasureTheory.Measure Bornology

open scoped Pointwise Topology NNReal BigOperators Convolution

variable {E : Type*} [NormedAddCommGroup E]

section

variable [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]

/-- If a set `s` is a neighborhood of `x`, then there exists a smooth function `f` taking
values in `[0, 1]`, supported in `s` and with `f x = 1`. -/
theorem exists_smooth_tsupport_subset {s : Set E} {x : E} (hs : s ‚àà ùìù x) :
    ‚àÉ f : E ‚Üí ‚Ñù,
      tsupport f ‚äÜ s ‚àß HasCompactSupport f ‚àß ContDiff ‚Ñù ‚ä§ f ‚àß range f ‚äÜ Icc 0 1 ‚àß f x = 1 := by
  obtain ‚ü®d : ‚Ñù, d_pos : 0 < d, hd : Euclidean.closedBall x d ‚äÜ s‚ü© :=
    Euclidean.nhds_basis_closedBall.mem_iff.1 hs
  let c : ContDiffBump (toEuclidean x) :=
    { rIn := d / 2
      rOut := d
      rIn_pos := half_pos d_pos
      rIn_lt_rOut := half_lt_self d_pos }
  let f : E ‚Üí ‚Ñù := c ‚àò toEuclidean
  have f_supp : f.support ‚äÜ Euclidean.ball x d := by
    intro y hy
    have : toEuclidean y ‚àà Function.support c := by
      simpa only [Function.mem_support, Function.comp_apply, Ne] using hy
    rwa [c.support_eq] at this
  have f_tsupp : tsupport f ‚äÜ Euclidean.closedBall x d := by
    rw [tsupport, ‚Üê Euclidean.closure_ball _ d_pos.ne']
    exact closure_mono f_supp
  refine' ‚ü®f, f_tsupp.trans hd, _, _, _, _‚ü©
  ¬∑ refine' isCompact_of_isClosed_isBounded isClosed_closure _
    have : IsBounded (Euclidean.closedBall x d) := Euclidean.isCompact_closedBall.isBounded
    refine this.subset (Euclidean.isClosed_closedBall.closure_subset_iff.2 ?_)
    exact f_supp.trans Euclidean.ball_subset_closedBall
  ¬∑ apply c.contDiff.comp
    exact ContinuousLinearEquiv.contDiff _
  ¬∑ rintro t ‚ü®y, rfl‚ü©
    exact ‚ü®c.nonneg, c.le_one‚ü©
  ¬∑ apply c.one_of_mem_closedBall
    apply mem_closedBall_self
    exact (half_pos d_pos).le
#align exists_smooth_tsupport_subset exists_smooth_tsupport_subset

/-- Given an open set `s` in a finite-dimensional real normed vector space, there exists a smooth
function with values in `[0, 1]` whose support is exactly `s`. -/
theorem IsOpen.exists_smooth_support_eq {s : Set E} (hs : IsOpen s) :
    ‚àÉ f : E ‚Üí ‚Ñù, f.support = s ‚àß ContDiff ‚Ñù ‚ä§ f ‚àß Set.range f ‚äÜ Set.Icc 0 1 := by
  /- For any given point `x` in `s`, one can construct a smooth function with support in `s` and
    nonzero at `x`. By second-countability, it follows that we may cover `s` with the supports of
    countably many such functions, say `g i`.
    Then `‚àë i, r i ‚Ä¢ g i` will be the desired function if `r i` is a sequence of positive numbers
    tending quickly enough to zero. Indeed, this ensures that, for any `k ‚â§ i`, the `k`-th
    derivative of `r i ‚Ä¢ g i` is bounded by a prescribed (summable) sequence `u i`. From this, the
    summability of the series and of its successive derivatives follows. -/
  rcases eq_empty_or_nonempty s with (rfl | h's)
  ¬∑ exact
      ‚ü®fun _ => 0, Function.support_zero, contDiff_const, by
        simp only [range_const, singleton_subset_iff, left_mem_Icc, zero_le_one]‚ü©
  let Œπ := { f : E ‚Üí ‚Ñù // f.support ‚äÜ s ‚àß HasCompactSupport f ‚àß ContDiff ‚Ñù ‚ä§ f ‚àß range f ‚äÜ Icc 0 1 }
  obtain ‚ü®T, T_count, hT‚ü© : ‚àÉ T : Set Œπ, T.Countable ‚àß ‚ãÉ f ‚àà T, support (f : E ‚Üí ‚Ñù) = s := by
    have : ‚ãÉ f : Œπ, (f : E ‚Üí ‚Ñù).support = s := by
      refine' Subset.antisymm (iUnion_subset fun f => f.2.1) _
      intro x hx
      rcases exists_smooth_tsupport_subset (hs.mem_nhds hx) with ‚ü®f, hf‚ü©
      let g : Œπ := ‚ü®f, (subset_tsupport f).trans hf.1, hf.2.1, hf.2.2.1, hf.2.2.2.1‚ü©
      have : x ‚àà support (g : E ‚Üí ‚Ñù) := by
        simp only [hf.2.2.2.2, Subtype.coe_mk, mem_support, Ne, one_ne_zero, not_false_iff]
      exact mem_iUnion_of_mem _ this
    simp_rw [‚Üê this]
    apply isOpen_iUnion_countable
    rintro ‚ü®f, hf‚ü©
    exact hf.2.2.1.continuous.isOpen_support
  obtain ‚ü®g0, hg‚ü© : ‚àÉ g0 : ‚Ñï ‚Üí Œπ, T = range g0 := by
    apply Countable.exists_eq_range T_count
    rcases eq_empty_or_nonempty T with (rfl | hT)
    ¬∑ simp only [Œπ, iUnion_false, iUnion_empty] at hT
      simp only [‚Üê hT, mem_empty_iff_false, iUnion_of_empty, iUnion_empty, Set.not_nonempty_empty]
          at h's
    ¬∑ exact hT
  let g : ‚Ñï ‚Üí E ‚Üí ‚Ñù := fun n => (g0 n).1
  have g_s : ‚àÄ n, support (g n) ‚äÜ s := fun n => (g0 n).2.1
  have s_g : ‚àÄ x ‚àà s, ‚àÉ n, x ‚àà support (g n) := fun x hx ‚Ü¶ by
    rw [‚Üê hT] at hx
    obtain ‚ü®i, iT, hi‚ü© : ‚àÉ i ‚àà T, x ‚àà support (i : E ‚Üí ‚Ñù) := by
      simpa only [mem_iUnion, exists_prop] using hx
    rw [hg, mem_range] at iT
    rcases iT with ‚ü®n, hn‚ü©
    rw [‚Üê hn] at hi
    exact ‚ü®n, hi‚ü©
  have g_smooth : ‚àÄ n, ContDiff ‚Ñù ‚ä§ (g n) := fun n => (g0 n).2.2.2.1
  have g_comp_supp : ‚àÄ n, HasCompactSupport (g n) := fun n => (g0 n).2.2.1
  have g_nonneg : ‚àÄ n x, 0 ‚â§ g n x := fun n x => ((g0 n).2.2.2.2 (mem_range_self x)).1
  obtain ‚ü®Œ¥, Œ¥pos, c, Œ¥c, c_lt‚ü© :
      ‚àÉ Œ¥ : ‚Ñï ‚Üí ‚Ñù‚â•0, (‚àÄ i : ‚Ñï, 0 < Œ¥ i) ‚àß ‚àÉ c : NNReal, HasSum Œ¥ c ‚àß c < 1 :=
    NNReal.exists_pos_sum_of_countable one_ne_zero ‚Ñï
  have : ‚àÄ n : ‚Ñï, ‚àÉ r : ‚Ñù, 0 < r ‚àß ‚àÄ i ‚â§ n, ‚àÄ x, ‚ÄñiteratedFDeriv ‚Ñù i (r ‚Ä¢ g n) x‚Äñ ‚â§ Œ¥ n := by
    intro n
    have : ‚àÄ i, ‚àÉ R, ‚àÄ x, ‚ÄñiteratedFDeriv ‚Ñù i (fun x => g n x) x‚Äñ ‚â§ R := by
      intro i
      have : BddAbove (range fun x => ‚ÄñiteratedFDeriv ‚Ñù i (fun x : E => g n x) x‚Äñ) := by
        apply
          ((g_smooth n).continuous_iteratedFDeriv le_top).norm.bddAbove_range_of_hasCompactSupport
        apply HasCompactSupport.comp_left _ norm_zero
        apply (g_comp_supp n).iteratedFDeriv
      rcases this with ‚ü®R, hR‚ü©
      exact ‚ü®R, fun x => hR (mem_range_self _)‚ü©
    choose R hR using this
    let M := max (((Finset.range (n + 1)).image R).max' (by simp)) 1
    have Œ¥npos : 0 < Œ¥ n := Œ¥pos n
    have IR : ‚àÄ i ‚â§ n, R i ‚â§ M := by
      intro i hi
      refine' le_trans _ (le_max_left _ _)
      apply Finset.le_max'
      apply Finset.mem_image_of_mem
      -- Porting note: was
      -- simp only [Finset.mem_range]
      -- linarith
      simpa only [Finset.mem_range, Nat.lt_add_one_iff]
    refine' ‚ü®M‚Åª¬π * Œ¥ n, by positivity, fun i hi x => _‚ü©
    calc
      ‚ÄñiteratedFDeriv ‚Ñù i ((M‚Åª¬π * Œ¥ n) ‚Ä¢ g n) x‚Äñ = ‚Äñ(M‚Åª¬π * Œ¥ n) ‚Ä¢ iteratedFDeriv ‚Ñù i (g n) x‚Äñ := by
        rw [iteratedFDeriv_const_smul_apply]; exact (g_smooth n).of_le le_top
      _ = M‚Åª¬π * Œ¥ n * ‚ÄñiteratedFDeriv ‚Ñù i (g n) x‚Äñ := by
        rw [norm_smul _ (iteratedFDeriv ‚Ñù i (g n) x), Real.norm_of_nonneg]; positivity
      _ ‚â§ M‚Åª¬π * Œ¥ n * M := (mul_le_mul_of_nonneg_left ((hR i x).trans (IR i hi)) (by positivity))
      _ = Œ¥ n := by field_simp
  choose r rpos hr using this
  have S : ‚àÄ x, Summable fun n => (r n ‚Ä¢ g n) x := fun x ‚Ü¶ by
    refine' .of_nnnorm_bounded _ Œ¥c.summable fun n => _
    rw [‚Üê NNReal.coe_le_coe, coe_nnnorm]
    simpa only [norm_iteratedFDeriv_zero] using hr n 0 (zero_le n) x
  refine' ‚ü®fun x => ‚àë' n, (r n ‚Ä¢ g n) x, _, _, _‚ü©
  ¬∑ apply Subset.antisymm
    ¬∑ intro x hx
      simp only [Pi.smul_apply, Algebra.id.smul_eq_mul, mem_support, Ne] at hx
      contrapose! hx
      have : ‚àÄ n, g n x = 0 := by
        intro n
        contrapose! hx
        exact g_s n hx
      simp only [this, mul_zero, tsum_zero]
    ¬∑ intro x hx
      obtain ‚ü®n, hn‚ü© : ‚àÉ n, x ‚àà support (g n) := s_g x hx
      have I : 0 < r n * g n x := mul_pos (rpos n) (lt_of_le_of_ne (g_nonneg n x) (Ne.symm hn))
      exact ne_of_gt (tsum_pos (S x) (fun i => mul_nonneg (rpos i).le (g_nonneg i x)) n I)
  ¬∑ refine'
      contDiff_tsum_of_eventually (fun n => (g_smooth n).const_smul (r n))
        (fun k _ => (NNReal.hasSum_coe.2 Œ¥c).summable) _
    intro i _
    simp only [Nat.cofinite_eq_atTop, Pi.smul_apply, Algebra.id.smul_eq_mul,
      Filter.eventually_atTop, ge_iff_le]
    exact ‚ü®i, fun n hn x => hr _ _ hn _‚ü©
  ¬∑ rintro - ‚ü®y, rfl‚ü©
    refine' ‚ü®tsum_nonneg fun n => mul_nonneg (rpos n).le (g_nonneg n y), le_trans _ c_lt.le‚ü©
    have A : HasSum (fun n => (Œ¥ n : ‚Ñù)) c := NNReal.hasSum_coe.2 Œ¥c
    simp only [Pi.smul_apply, smul_eq_mul, NNReal.val_eq_coe, ‚Üê A.tsum_eq, ge_iff_le]
    apply tsum_le_tsum _ (S y) A.summable
    intro n
    apply (le_abs_self _).trans
    simpa only [norm_iteratedFDeriv_zero] using hr n 0 (zero_le n) y
#align is_open.exists_smooth_support_eq IsOpen.exists_smooth_support_eq

end

section

namespace ExistsContDiffBumpBase

/-- An auxiliary function to construct partitions of unity on finite-dimensional real vector spaces.
It is the characteristic function of the closed unit ball. -/
def œÜ : E ‚Üí ‚Ñù :=
  (closedBall (0 : E) 1).indicator fun _ => (1 : ‚Ñù)
#align exists_cont_diff_bump_base.œÜ ExistsContDiffBumpBase.œÜ

variable [NormedSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]

section HelperDefinitions

variable (E)

theorem u_exists :
    ‚àÉ u : E ‚Üí ‚Ñù,
      ContDiff ‚Ñù ‚ä§ u ‚àß (‚àÄ x, u x ‚àà Icc (0 : ‚Ñù) 1) ‚àß support u = ball 0 1 ‚àß ‚àÄ x, u (-x) = u x := by
  have A : IsOpen (ball (0 : E) 1) := isOpen_ball
  obtain ‚ü®f, f_support, f_smooth, f_range‚ü© :
      ‚àÉ f : E ‚Üí ‚Ñù, f.support = ball (0 : E) 1 ‚àß ContDiff ‚Ñù ‚ä§ f ‚àß Set.range f ‚äÜ Set.Icc 0 1 :=
    A.exists_smooth_support_eq
  have B : ‚àÄ x, f x ‚àà Icc (0 : ‚Ñù) 1 := fun x => f_range (mem_range_self x)
  refine' ‚ü®fun x => (f x + f (-x)) / 2, _, _, _, _‚ü©
  ¬∑ exact (f_smooth.add (f_smooth.comp contDiff_neg)).div_const _
  ¬∑ intro x
    simp only [mem_Icc]
    constructor
    ¬∑ linarith [(B x).1, (B (-x)).1]
    ¬∑ linarith [(B x).2, (B (-x)).2]
  ¬∑ refine' support_eq_iff.2 ‚ü®fun x hx => _, fun x hx => _‚ü©
    ¬∑ apply ne_of_gt
      have : 0 < f x := by
        apply lt_of_le_of_ne (B x).1 (Ne.symm _)
        rwa [‚Üê f_support] at hx
      linarith [(B (-x)).1]
    ¬∑ have I1 : x ‚àâ support f := by rwa [f_support]
      have I2 : -x ‚àâ support f := by
        rw [f_support]
        simpa using hx
      simp only [mem_support, Classical.not_not] at I1 I2
      simp only [I1, I2, add_zero, zero_div]
  ¬∑ intro x; simp only [add_comm, neg_neg]
#align exists_cont_diff_bump_base.u_exists ExistsContDiffBumpBase.u_exists

variable {E}

/-- An auxiliary function to construct partitions of unity on finite-dimensional real vector spaces,
which is smooth, symmetric, and with support equal to the unit ball. -/
def u (x : E) : ‚Ñù :=
  Classical.choose (u_exists E) x
#align exists_cont_diff_bump_base.u ExistsContDiffBumpBase.u

variable (E)

theorem u_smooth : ContDiff ‚Ñù ‚ä§ (u : E ‚Üí ‚Ñù) :=
  (Classical.choose_spec (u_exists E)).1
#align exists_cont_diff_bump_base.u_smooth ExistsContDiffBumpBase.u_smooth

theorem u_continuous : Continuous (u : E ‚Üí ‚Ñù) :=
  (u_smooth E).continuous
#align exists_cont_diff_bump_base.u_continuous ExistsContDiffBumpBase.u_continuous

theorem u_support : support (u : E ‚Üí ‚Ñù) = ball 0 1 :=
  (Classical.choose_spec (u_exists E)).2.2.1
#align exists_cont_diff_bump_base.u_support ExistsContDiffBumpBase.u_support

theorem u_compact_support : HasCompactSupport (u : E ‚Üí ‚Ñù) := by
  rw [hasCompactSupport_def, u_support, closure_ball (0 : E) one_ne_zero]
  exact isCompact_closedBall _ _
#align exists_cont_diff_bump_base.u_compact_support ExistsContDiffBumpBase.u_compact_support

variable {E}

theorem u_nonneg (x : E) : 0 ‚â§ u x :=
  ((Classical.choose_spec (u_exists E)).2.1 x).1
#align exists_cont_diff_bump_base.u_nonneg ExistsContDiffBumpBase.u_nonneg

theorem u_le_one (x : E) : u x ‚â§ 1 :=
  ((Classical.choose_spec (u_exists E)).2.1 x).2
#align exists_cont_diff_bump_base.u_le_one ExistsContDiffBumpBase.u_le_one

theorem u_neg (x : E) : u (-x) = u x :=
  (Classical.choose_spec (u_exists E)).2.2.2 x
#align exists_cont_diff_bump_base.u_neg ExistsContDiffBumpBase.u_neg

variable [MeasurableSpace E] [BorelSpace E]

local notation "Œº" => MeasureTheory.Measure.addHaar

variable (E)

theorem u_int_pos : 0 < ‚à´ x : E, u x ‚àÇŒº := by
  refine' (integral_pos_iff_support_of_nonneg u_nonneg _).mpr _
  ¬∑ exact (u_continuous E).integrable_of_hasCompactSupport (u_compact_support E)
  ¬∑ rw [u_support]; exact measure_ball_pos _ _ zero_lt_one
#align exists_cont_diff_bump_base.u_int_pos ExistsContDiffBumpBase.u_int_pos

variable {E}
-- Porting note: `W` upper case
set_option linter.uppercaseLean3 false

/-- An auxiliary function to construct partitions of unity on finite-dimensional real vector spaces,
which is smooth, symmetric, with support equal to the ball of radius `D` and integral `1`. -/
def w (D : ‚Ñù) (x : E) : ‚Ñù :=
  ((‚à´ x : E, u x ‚àÇŒº) * |D| ^ finrank ‚Ñù E)‚Åª¬π ‚Ä¢ u (D‚Åª¬π ‚Ä¢ x)
#align exists_cont_diff_bump_base.W ExistsContDiffBumpBase.w

theorem w_def (D : ‚Ñù) :
    (w D : E ‚Üí ‚Ñù) = fun x => ((‚à´ x : E, u x ‚àÇŒº) * |D| ^ finrank ‚Ñù E)‚Åª¬π ‚Ä¢ u (D‚Åª¬π ‚Ä¢ x) := by
  ext1 x; rfl
#align exists_cont_diff_bump_base.W_def ExistsContDiffBumpBase.w_def

theorem w_nonneg (D : ‚Ñù) (x : E) : 0 ‚â§ w D x := by
  apply mul_nonneg _ (u_nonneg _)
  apply inv_nonneg.2
  apply mul_nonneg (u_int_pos E).le
  norm_cast
  apply pow_nonneg (abs_nonneg D)
#align exists_cont_diff_bump_base.W_nonneg ExistsContDiffBumpBase.w_nonneg

theorem w_mul_œÜ_nonneg (D : ‚Ñù) (x y : E) : 0 ‚â§ w D y * œÜ (x - y) :=
  mul_nonneg (w_nonneg D y) (indicator_nonneg (by simp only [zero_le_one, imp_true_iff]) _)
#align exists_cont_diff_bump_base.W_mul_œÜ_nonneg ExistsContDiffBumpBase.w_mul_œÜ_nonneg

variable (E)

theorem w_integral {D : ‚Ñù} (Dpos : 0 < D) : ‚à´ x : E, w D x ‚àÇŒº = 1 := by
  simp_rw [w, integral_smul]
  rw [integral_comp_inv_smul_of_nonneg Œº (u : E ‚Üí ‚Ñù) Dpos.le, abs_of_nonneg Dpos.le, mul_comm]
  field_simp [(u_int_pos E).ne']
#align exists_cont_diff_bump_base.W_integral ExistsContDiffBumpBase.w_integral

theorem w_support {D : ‚Ñù} (Dpos : 0 < D) : support (w D : E ‚Üí ‚Ñù) = ball 0 D := by
  have B : D ‚Ä¢ ball (0 : E) 1 = ball 0 D := by
    rw [smul_unitBall Dpos.ne', Real.norm_of_nonneg Dpos.le]
  have C : D ^ finrank ‚Ñù E ‚â† 0 := by
    norm_cast
    exact pow_ne_zero _ Dpos.ne'
  simp only [w_def, Algebra.id.smul_eq_mul, support_mul, support_inv, univ_inter,
    support_comp_inv_smul‚ÇÄ Dpos.ne', u_support, B, support_const (u_int_pos E).ne', support_const C,
    abs_of_nonneg Dpos.le]
#align exists_cont_diff_bump_base.W_support ExistsContDiffBumpBase.w_support

theorem w_compact_support {D : ‚Ñù} (Dpos : 0 < D) : HasCompactSupport (w D : E ‚Üí ‚Ñù) := by
  rw [hasCompactSupport_def, w_support E Dpos, closure_ball (0 : E) Dpos.ne']
  exact isCompact_closedBall _ _
#align exists_cont_diff_bump_base.W_compact_support ExistsContDiffBumpBase.w_compact_support

variable {E}

/-- An auxiliary function to construct partitions of unity on finite-dimensional real vector spaces.
It is the convolution between a smooth function of integral `1` supported in the ball of radius `D`,
with the indicator function of the closed unit ball. Therefore, it is smooth, equal to `1` on the
ball of radius `1 - D`, with support equal to the ball of radius `1 + D`. -/
def y (D : ‚Ñù) : E ‚Üí ‚Ñù :=
  w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] œÜ
#align exists_cont_diff_bump_base.Y ExistsContDiffBumpBase.y

theorem y_neg (D : ‚Ñù) (x : E) : y D (-x) = y D x := by
  apply convolution_neg_of_neg_eq
  ¬∑ filter_upwards with x
    simp only [w_def, Real.rpow_natCast, mul_inv_rev, smul_neg, u_neg, smul_eq_mul, forall_const]
  ¬∑ filter_upwards with x
    simp only [œÜ, indicator, mem_closedBall, dist_zero_right, norm_neg, forall_const]
#align exists_cont_diff_bump_base.Y_neg ExistsContDiffBumpBase.y_neg

theorem y_eq_one_of_mem_closedBall {D : ‚Ñù} {x : E} (Dpos : 0 < D)
    (hx : x ‚àà closedBall (0 : E) (1 - D)) : y D x = 1 := by
  change (w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] œÜ) x = 1
  have B : ‚àÄ y : E, y ‚àà ball x D ‚Üí œÜ y = 1 := by
    have C : ball x D ‚äÜ ball 0 1 := by
      apply ball_subset_ball'
      simp only [mem_closedBall] at hx
      linarith only [hx]
    intro y hy
    simp only [œÜ, indicator, mem_closedBall, ite_eq_left_iff, not_le, zero_ne_one]
    intro h'y
    linarith only [mem_ball.1 (C hy), h'y]
  have Bx : œÜ x = 1 := B _ (mem_ball_self Dpos)
  have B' : ‚àÄ y, y ‚àà ball x D ‚Üí œÜ y = œÜ x := by rw [Bx]; exact B
  rw [convolution_eq_right' _ (le_of_eq (w_support E Dpos)) B']
  simp only [lsmul_apply, Algebra.id.smul_eq_mul, integral_mul_right, w_integral E Dpos, Bx,
    one_mul]
#align exists_cont_diff_bump_base.Y_eq_one_of_mem_closed_ball ExistsContDiffBumpBase.y_eq_one_of_mem_closedBall

theorem y_eq_zero_of_not_mem_ball {D : ‚Ñù} {x : E} (Dpos : 0 < D) (hx : x ‚àâ ball (0 : E) (1 + D)) :
    y D x = 0 := by
  change (w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] œÜ) x = 0
  have B : ‚àÄ y, y ‚àà ball x D ‚Üí œÜ y = 0 := by
    intro y hy
    simp only [œÜ, indicator, mem_closedBall_zero_iff, ite_eq_right_iff, one_ne_zero]
    intro h'y
    have C : ball y D ‚äÜ ball 0 (1 + D) := by
      apply ball_subset_ball'
      rw [‚Üê dist_zero_right] at h'y
      linarith only [h'y]
    exact hx (C (mem_ball_comm.1 hy))
  have Bx : œÜ x = 0 := B _ (mem_ball_self Dpos)
  have B' : ‚àÄ y, y ‚àà ball x D ‚Üí œÜ y = œÜ x := by rw [Bx]; exact B
  rw [convolution_eq_right' _ (le_of_eq (w_support E Dpos)) B']
  simp only [lsmul_apply, Algebra.id.smul_eq_mul, Bx, mul_zero, integral_const]
#align exists_cont_diff_bump_base.Y_eq_zero_of_not_mem_ball ExistsContDiffBumpBase.y_eq_zero_of_not_mem_ball

theorem y_nonneg (D : ‚Ñù) (x : E) : 0 ‚â§ y D x :=
  integral_nonneg (w_mul_œÜ_nonneg D x)
#align exists_cont_diff_bump_base.Y_nonneg ExistsContDiffBumpBase.y_nonneg

theorem y_le_one {D : ‚Ñù} (x : E) (Dpos : 0 < D) : y D x ‚â§ 1 := by
  have A : (w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] œÜ) x ‚â§ (w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] 1) x := by
    apply
      convolution_mono_right_of_nonneg _ (w_nonneg D) (indicator_le_self' fun x _ => zero_le_one)
        fun _ => zero_le_one
    refine'
      (HasCompactSupport.convolutionExistsLeft _ (w_compact_support E Dpos) _
          (locallyIntegrable_const (1 : ‚Ñù)) x).integrable
    exact continuous_const.mul ((u_continuous E).comp (continuous_id.const_smul _))
  have B : (w D ‚ãÜ[lsmul ‚Ñù ‚Ñù, Œº] fun _ => (1 : ‚Ñù)) x = 1 := by
    simp only [convolution, ContinuousLinearMap.map_smul, mul_inv_rev, coe_smul', mul_one,
      lsmul_apply, Algebra.id.smul_eq_mul, integral_mul_left, w_integral E Dpos, Pi.smul_apply]
  exact A.trans (le_of_eq B)
#align exists_cont_diff_bump_base.Y_le_one ExistsContDiffBumpBase.y_le_one

theorem y_pos_of_mem_ball {D : ‚Ñù} {x : E} (Dpos : 0 < D) (D_lt_one : D < 1)
    (hx : x ‚àà ball (0 : E) (1 + D)) : 0 < y D x := by
  simp only [mem_ball_zero_iff] at hx
  refine' (integral_pos_iff_support_of_nonneg (w_mul_œÜ_nonneg D x) _).2 _
  ¬∑ have F_comp : HasCompactSupport (w D) := w_compact_support E Dpos
    have B : LocallyIntegrable (œÜ : E ‚Üí ‚Ñù) Œº :=
      (locallyIntegrable_const _).indicator measurableSet_closedBall
    have C : Continuous (w D : E ‚Üí ‚Ñù) :=
      continuous_const.mul ((u_continuous E).comp (continuous_id.const_smul _))
    exact
      (HasCompactSupport.convolutionExistsLeft (lsmul ‚Ñù ‚Ñù : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù) F_comp C B
          x).integrable
  ¬∑ set z := (D / (1 + D)) ‚Ä¢ x with hz
    have B : 0 < 1 + D := by linarith
    have C : ball z (D * (1 + D - ‚Äñx‚Äñ) / (1 + D)) ‚äÜ support fun y : E => w D y * œÜ (x - y) := by
      intro y hy
      simp only [support_mul, w_support E Dpos]
      simp only [œÜ, mem_inter_iff, mem_support, Ne, indicator_apply_eq_zero,
        mem_closedBall_zero_iff, one_ne_zero, not_forall, not_false_iff, exists_prop, and_true_iff]
      constructor
      ¬∑ apply ball_subset_ball' _ hy
        simp only [hz, norm_smul, abs_of_nonneg Dpos.le, abs_of_nonneg B.le, dist_zero_right,
          Real.norm_eq_abs, abs_div]
        simp only [div_le_iff B, field_simps]
        ring_nf
        rfl
      ¬∑ have ID : ‚ÄñD / (1 + D) - 1‚Äñ = 1 / (1 + D) := by
          rw [Real.norm_of_nonpos]
          ¬∑ simp only [B.ne', Ne, not_false_iff, mul_one, neg_sub, add_tsub_cancel_right,
              field_simps]
          ¬∑ simp only [B.ne', Ne, not_false_iff, mul_one, field_simps]
            apply div_nonpos_of_nonpos_of_nonneg _ B.le
            linarith only
        rw [‚Üê mem_closedBall_iff_norm']
        apply closedBall_subset_closedBall' _ (ball_subset_closedBall hy)
        rw [‚Üê one_smul ‚Ñù x, dist_eq_norm, hz, ‚Üê sub_smul, one_smul, norm_smul, ID]
        simp only [B.ne', div_le_iff B, field_simps]
        nlinarith only [hx, D_lt_one]
    apply lt_of_lt_of_le _ (measure_mono C)
    apply measure_ball_pos
    exact div_pos (mul_pos Dpos (by linarith only [hx])) B
#align exists_cont_diff_bump_base.Y_pos_of_mem_ball ExistsContDiffBumpBase.y_pos_of_mem_ball

variable (E)

theorem y_smooth : ContDiffOn ‚Ñù ‚ä§ (uncurry y) (Ioo (0 : ‚Ñù) 1 √óÀ¢ (univ : Set E)) := by
  have hs : IsOpen (Ioo (0 : ‚Ñù) (1 : ‚Ñù)) := isOpen_Ioo
  have hk : IsCompact (closedBall (0 : E) 1) := ProperSpace.isCompact_closedBall _ _
  refine' contDiffOn_convolution_left_with_param (lsmul ‚Ñù ‚Ñù) hs hk _ _ _
  ¬∑ rintro p x hp hx
    simp only [w, mul_inv_rev, Algebra.id.smul_eq_mul, mul_eq_zero, inv_eq_zero]
    right
    contrapose! hx
    have : p‚Åª¬π ‚Ä¢ x ‚àà support u := mem_support.2 hx
    simp only [u_support, norm_smul, mem_ball_zero_iff, Real.norm_eq_abs, abs_inv,
      abs_of_nonneg hp.1.le, ‚Üê div_eq_inv_mul, div_lt_one hp.1] at this
    rw [mem_closedBall_zero_iff]
    exact this.le.trans hp.2.le
  ¬∑ exact (locallyIntegrable_const _).indicator measurableSet_closedBall
  ¬∑ apply ContDiffOn.mul
    ¬∑ norm_cast
      refine'
        (contDiffOn_const.mul _).inv fun x hx =>
          ne_of_gt (mul_pos (u_int_pos E) (pow_pos (abs_pos_of_pos hx.1.1) (finrank ‚Ñù E)))
      apply ContDiffOn.pow
      simp_rw [‚Üê Real.norm_eq_abs]
      apply ContDiffOn.norm ‚Ñù
      ¬∑ exact contDiffOn_fst
      ¬∑ intro x hx; exact ne_of_gt hx.1.1
    ¬∑ apply (u_smooth E).comp_contDiffOn
      exact ContDiffOn.smul (contDiffOn_fst.inv fun x hx => ne_of_gt hx.1.1) contDiffOn_snd
#align exists_cont_diff_bump_base.Y_smooth ExistsContDiffBumpBase.y_smooth

theorem y_support {D : ‚Ñù} (Dpos : 0 < D) (D_lt_one : D < 1) :
    support (y D : E ‚Üí ‚Ñù) = ball (0 : E) (1 + D) :=
  support_eq_iff.2
    ‚ü®fun _ hx => (y_pos_of_mem_ball Dpos D_lt_one hx).ne', fun _ hx =>
      y_eq_zero_of_not_mem_ball Dpos hx‚ü©
#align exists_cont_diff_bump_base.Y_support ExistsContDiffBumpBase.y_support

variable {E}

end HelperDefinitions

instance (priority := 100) {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [FiniteDimensional ‚Ñù E] : HasContDiffBump E := by
  refine' ‚ü®‚ü®_‚ü©‚ü©
  borelize E
  have IR : ‚àÄ R : ‚Ñù, 1 < R ‚Üí 0 < (R - 1) / (R + 1) := by intro R hR; apply div_pos <;> linarith
  exact
    { toFun := fun R x => if 1 < R then y ((R - 1) / (R + 1)) (((R + 1) / 2)‚Åª¬π ‚Ä¢ x) else 0
      mem_Icc := fun R x => by
        simp only [mem_Icc]
        split_ifs with h
        ¬∑ refine' ‚ü®y_nonneg _ _, y_le_one _ (IR R h)‚ü©
        ¬∑ simp only [le_refl, zero_le_one, and_self]
      symmetric := fun R x => by
        simp only
        split_ifs
        ¬∑ simp only [y_neg, smul_neg]
        ¬∑ rfl
      smooth := by
        suffices
          ContDiffOn ‚Ñù ‚ä§
            (uncurry y ‚àò fun p : ‚Ñù √ó E => ((p.1 - 1) / (p.1 + 1), ((p.1 + 1) / 2)‚Åª¬π ‚Ä¢ p.2))
            (Ioi 1 √óÀ¢ univ) by
          apply this.congr
          rintro ‚ü®R, x‚ü© ‚ü®hR : 1 < R, _‚ü©
          simp only [hR, uncurry_apply_pair, if_true, Function.comp_apply]
        apply (y_smooth E).comp
        ¬∑ apply ContDiffOn.prod
          ¬∑ refine'
              (contDiffOn_fst.sub contDiffOn_const).div (contDiffOn_fst.add contDiffOn_const) _
            rintro ‚ü®R, x‚ü© ‚ü®hR : 1 < R, _‚ü©
            apply ne_of_gt
            dsimp only
            linarith
          ¬∑ apply ContDiffOn.smul _ contDiffOn_snd
            refine' ((contDiffOn_fst.add contDiffOn_const).div_const _).inv _
            rintro ‚ü®R, x‚ü© ‚ü®hR : 1 < R, _‚ü©
            apply ne_of_gt
            dsimp only
            linarith
        ¬∑ rintro ‚ü®R, x‚ü© ‚ü®hR : 1 < R, _‚ü©
          have A : 0 < (R - 1) / (R + 1) := by apply div_pos <;> linarith
          have B : (R - 1) / (R + 1) < 1 := by apply (div_lt_one _).2 <;> linarith
          simp only [mem_preimage, prod_mk_mem_set_prod_eq, mem_Ioo, mem_univ, and_true_iff, A, B]
      eq_one := fun R hR x hx => by
        have A : 0 < R + 1 := by linarith
        simp only [hR, if_true]
        apply y_eq_one_of_mem_closedBall (IR R hR)
        simp only [norm_smul, inv_div, mem_closedBall_zero_iff, Real.norm_eq_abs, abs_div, abs_two,
          abs_of_nonneg A.le]
        calc
          2 / (R + 1) * ‚Äñx‚Äñ ‚â§ 2 / (R + 1) := mul_le_of_le_one_right (by positivity) hx
          _ = 1 - (R - 1) / (R + 1) := by field_simp; ring
      support := fun R hR => by
        have A : 0 < (R + 1) / 2 := by linarith
        have C : (R - 1) / (R + 1) < 1 := by apply (div_lt_one _).2 <;> linarith
        simp only [hR, if_true, support_comp_inv_smul‚ÇÄ A.ne', y_support _ (IR R hR) C,
          _root_.smul_ball A.ne', Real.norm_of_nonneg A.le, smul_zero]
        refine' congr (congr_arg ball (Eq.refl 0)) _
        field_simp; ring }

end ExistsContDiffBumpBase

end
