/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Analysis.Calculus.BumpFunction.Basic
import Mathlib.MeasureTheory.Integral.SetIntegral
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar

#align_import analysis.calculus.bump_function_inner from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Normed bump function

In this file we define `ContDiffBump.normed f Î¼` to be the bump function `f` normalized so that
`âˆ« x, f.normed Î¼ x âˆ‚Î¼ = 1` and prove some properties of this function.
-/

noncomputable section

open Function Filter Set Metric MeasureTheory FiniteDimensional Measure
open scoped Topology

namespace ContDiffBump

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [HasContDiffBump E]
  [MeasurableSpace E] {c : E} (f : ContDiffBump c) {x : E} {n : â„•âˆ} {Î¼ : Measure E}

/-- A bump function normed so that `âˆ« x, f.normed Î¼ x âˆ‚Î¼ = 1`. -/
protected def normed (Î¼ : Measure E) : E â†’ â„ := fun x => f x / âˆ« x, f x âˆ‚Î¼
#align cont_diff_bump.normed ContDiffBump.normed

theorem normed_def {Î¼ : Measure E} (x : E) : f.normed Î¼ x = f x / âˆ« x, f x âˆ‚Î¼ :=
  rfl
#align cont_diff_bump.normed_def ContDiffBump.normed_def

theorem nonneg_normed (x : E) : 0 â‰¤ f.normed Î¼ x :=
  div_nonneg f.nonneg <| integral_nonneg f.nonneg'
#align cont_diff_bump.nonneg_normed ContDiffBump.nonneg_normed

theorem contDiff_normed {n : â„•âˆ} : ContDiff â„ n (f.normed Î¼) :=
  f.contDiff.div_const _
#align cont_diff_bump.cont_diff_normed ContDiffBump.contDiff_normed

theorem continuous_normed : Continuous (f.normed Î¼) :=
  f.continuous.div_const _
#align cont_diff_bump.continuous_normed ContDiffBump.continuous_normed

theorem normed_sub (x : E) : f.normed Î¼ (c - x) = f.normed Î¼ (c + x) := by
  simp_rw [f.normed_def, f.sub]
#align cont_diff_bump.normed_sub ContDiffBump.normed_sub

theorem normed_neg (f : ContDiffBump (0 : E)) (x : E) : f.normed Î¼ (-x) = f.normed Î¼ x := by
  simp_rw [f.normed_def, f.neg]
#align cont_diff_bump.normed_neg ContDiffBump.normed_neg

variable [BorelSpace E] [FiniteDimensional â„ E] [IsLocallyFiniteMeasure Î¼]

protected theorem integrable : Integrable f Î¼ :=
  f.continuous.integrable_of_hasCompactSupport f.hasCompactSupport
#align cont_diff_bump.integrable ContDiffBump.integrable

protected theorem integrable_normed : Integrable (f.normed Î¼) Î¼ :=
  f.integrable.div_const _
#align cont_diff_bump.integrable_normed ContDiffBump.integrable_normed

variable [Î¼.IsOpenPosMeasure]

theorem integral_pos : 0 < âˆ« x, f x âˆ‚Î¼ := by
  refine (integral_pos_iff_support_of_nonneg f.nonneg' f.integrable).mpr ?_
  rw [f.support_eq]
  exact measure_ball_pos Î¼ c f.rOut_pos
#align cont_diff_bump.integral_pos ContDiffBump.integral_pos

theorem integral_normed : âˆ« x, f.normed Î¼ x âˆ‚Î¼ = 1 := by
  simp_rw [ContDiffBump.normed, div_eq_mul_inv, mul_comm (f _), â† smul_eq_mul, integral_smul]
  exact inv_mul_cancel f.integral_pos.ne'
#align cont_diff_bump.integral_normed ContDiffBump.integral_normed

theorem support_normed_eq : Function.support (f.normed Î¼) = Metric.ball c f.rOut := by
  unfold ContDiffBump.normed
  rw [support_div, f.support_eq, support_const f.integral_pos.ne', inter_univ]
#align cont_diff_bump.support_normed_eq ContDiffBump.support_normed_eq

theorem tsupport_normed_eq : tsupport (f.normed Î¼) = Metric.closedBall c f.rOut := by
  rw [tsupport, f.support_normed_eq, closure_ball _ f.rOut_pos.ne']
#align cont_diff_bump.tsupport_normed_eq ContDiffBump.tsupport_normed_eq

theorem hasCompactSupport_normed : HasCompactSupport (f.normed Î¼) := by
  simp only [HasCompactSupport, f.tsupport_normed_eq (Î¼ := Î¼), isCompact_closedBall]
#align cont_diff_bump.has_compact_support_normed ContDiffBump.hasCompactSupport_normed

theorem tendsto_support_normed_smallSets {Î¹} {Ï† : Î¹ â†’ ContDiffBump c} {l : Filter Î¹}
    (hÏ† : Tendsto (fun i => (Ï† i).rOut) l (ğ“ 0)) :
    Tendsto (fun i => Function.support fun x => (Ï† i).normed Î¼ x) l (ğ“ c).smallSets := by
  simp_rw [NormedAddCommGroup.tendsto_nhds_zero, Real.norm_eq_abs,
    abs_eq_self.mpr (Ï† _).rOut_pos.le] at hÏ†
  rw [nhds_basis_ball.smallSets.tendsto_right_iff]
  refine fun Îµ hÎµ â†¦ (hÏ† Îµ hÎµ).mono fun i hi â†¦ ?_
  rw [(Ï† i).support_normed_eq]
  exact ball_subset_ball hi.le
#align cont_diff_bump.tendsto_support_normed_small_sets ContDiffBump.tendsto_support_normed_smallSets

variable (Î¼)

theorem integral_normed_smul {X} [NormedAddCommGroup X] [NormedSpace â„ X]
    [CompleteSpace X] (z : X) : âˆ« x, f.normed Î¼ x â€¢ z âˆ‚Î¼ = z := by
  simp_rw [integral_smul_const, f.integral_normed (Î¼ := Î¼), one_smul]
#align cont_diff_bump.integral_normed_smul ContDiffBump.integral_normed_smul

theorem measure_closedBall_le_integral : (Î¼ (closedBall c f.rIn)).toReal â‰¤ âˆ« x, f x âˆ‚Î¼ := by calc
  (Î¼ (closedBall c f.rIn)).toReal = âˆ« x in closedBall c f.rIn, 1 âˆ‚Î¼ := by simp
  _ = âˆ« x in closedBall c f.rIn, f x âˆ‚Î¼ := setIntegral_congr measurableSet_closedBall
        (fun x hx â†¦ (one_of_mem_closedBall f hx).symm)
  _ â‰¤ âˆ« x, f x âˆ‚Î¼ := setIntegral_le_integral f.integrable (eventually_of_forall (fun x â†¦ f.nonneg))

theorem normed_le_div_measure_closedBall_rIn (x : E) :
    f.normed Î¼ x â‰¤ 1 / (Î¼ (closedBall c f.rIn)).toReal := by
  rw [normed_def]
  gcongr
  Â· exact ENNReal.toReal_pos (measure_closedBall_pos _ _ f.rIn_pos).ne' measure_closedBall_lt_top.ne
  Â· exact f.le_one
  Â· exact f.measure_closedBall_le_integral Î¼

theorem integral_le_measure_closedBall : âˆ« x, f x âˆ‚Î¼ â‰¤ (Î¼ (closedBall c f.rOut)).toReal := by calc
  âˆ« x, f x âˆ‚Î¼ = âˆ« x in closedBall c f.rOut, f x âˆ‚Î¼ := by
    apply (setIntegral_eq_integral_of_forall_compl_eq_zero (fun x hx â†¦ ?_)).symm
    apply f.zero_of_le_dist (le_of_lt _)
    simpa using hx
  _ â‰¤ âˆ« x in closedBall c f.rOut, 1 âˆ‚Î¼ := by
    apply setIntegral_mono f.integrable.integrableOn _ (fun x â†¦ f.le_one)
    simp [measure_closedBall_lt_top]
  _ = (Î¼ (closedBall c f.rOut)).toReal := by simp

theorem measure_closedBall_div_le_integral [IsAddHaarMeasure Î¼] (K : â„) (h : f.rOut â‰¤ K * f.rIn) :
    (Î¼ (closedBall c f.rOut)).toReal / K ^ finrank â„ E â‰¤ âˆ« x, f x âˆ‚Î¼ := by
  have K_pos : 0 < K := by
    simpa [f.rIn_pos, not_lt.2 f.rIn_pos.le] using mul_pos_iff.1 (f.rOut_pos.trans_le h)
  apply le_trans _ (f.measure_closedBall_le_integral Î¼)
  rw [div_le_iff (pow_pos K_pos _), addHaar_closedBall' _ _ f.rIn_pos.le,
    addHaar_closedBall' _ _ f.rOut_pos.le, ENNReal.toReal_mul, ENNReal.toReal_mul,
    ENNReal.toReal_ofReal (pow_nonneg f.rOut_pos.le _),
    ENNReal.toReal_ofReal (pow_nonneg f.rIn_pos.le _), mul_assoc, mul_comm _ (K ^ _), â† mul_assoc,
    â† mul_pow, mul_comm _ K]
  gcongr
  exact f.rOut_pos.le

theorem normed_le_div_measure_closedBall_rOut [IsAddHaarMeasure Î¼] (K : â„) (h : f.rOut â‰¤ K * f.rIn)
    (x : E) :
    f.normed Î¼ x â‰¤ K ^ finrank â„ E / (Î¼ (closedBall c f.rOut)).toReal := by
  have K_pos : 0 < K := by
    simpa [f.rIn_pos, not_lt.2 f.rIn_pos.le] using mul_pos_iff.1 (f.rOut_pos.trans_le h)
  have : f x / âˆ« y, f y âˆ‚Î¼ â‰¤ 1 / âˆ« y, f y âˆ‚Î¼ := by
    gcongr
    Â· exact f.integral_pos.le
    Â· exact f.le_one
  apply this.trans
  rw [div_le_div_iff f.integral_pos, one_mul, â† div_le_iff' (pow_pos K_pos _)]
  Â· exact f.measure_closedBall_div_le_integral Î¼ K h
  Â· exact ENNReal.toReal_pos (measure_closedBall_pos _ _ f.rOut_pos).ne'
      measure_closedBall_lt_top.ne

end ContDiffBump
