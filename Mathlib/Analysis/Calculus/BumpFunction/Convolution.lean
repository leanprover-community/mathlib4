/-
Copyright (c) 2022 Floris van Doorn. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Floris van Doorn
-/
import Mathlib.Analysis.Convolution
import Mathlib.Analysis.Calculus.BumpFunction.Normed
import Mathlib.MeasureTheory.Integral.Average
import Mathlib.MeasureTheory.Covering.Differentiation
import Mathlib.MeasureTheory.Covering.BesicovitchVectorSpace
import Mathlib.MeasureTheory.Measure.Haar.Unique

#align_import analysis.convolution from "leanprover-community/mathlib"@"8905e5ed90859939681a725b00f6063e65096d95"

/-!
# Convolution with a bump function

In this file we prove lemmas about convolutions `(Ï†.normed Î¼ â‹†[lsmul â„ â„, Î¼] g) xâ‚€`,
where `Ï† : ContDiffBump 0` is a smooth bump function.

We prove that this convolution is equal to `g xâ‚€`
if `g` is a constant on `Metric.ball xâ‚€ Ï†.rOut`.
We also provide estimates in the case if `g x` is close to `g xâ‚€` on this ball.

## Main results

- `ContDiffBump.convolution_tendsto_right_of_continuous`:
  Let `g` be a continuous function; let `Ï† i` be a family of `ContDiffBump 0` functions with.
  If `(Ï† i).rOut` tends to zero along a filter `l`,
  then `((Ï† i).normed Î¼ â‹†[lsmul â„ â„, Î¼] g) xâ‚€` tends to `g xâ‚€` along the same filter.
- `ContDiffBump.convolution_tendsto_right`: generalization of the above lemma.
- `ContDiffBump.ae_convolution_tendsto_right_of_locallyIntegrable`: let `g` be a locally
  integrable function. Then the convolution of `g` with a family of bump functions with
  support tending to `0` converges almost everywhere to `g`.

## Keywords

convolution, smooth function, bump function
-/

universe uG uE'

open ContinuousLinearMap Metric MeasureTheory Filter Function Measure Set
open scoped Convolution Topology

namespace ContDiffBump

variable {G : Type uG} {E' : Type uE'} [NormedAddCommGroup E'] {g : G â†’ E'} [MeasurableSpace G]
  {Î¼ : MeasureTheory.Measure G} [NormedSpace â„ E'] [NormedAddCommGroup G] [NormedSpace â„ G]
  [HasContDiffBump G] [CompleteSpace E'] {Ï† : ContDiffBump (0 : G)} {xâ‚€ : G}

/-- If `Ï†` is a bump function, compute `(Ï† â‹† g) xâ‚€`
if `g` is constant on `Metric.ball xâ‚€ Ï†.rOut`. -/
theorem convolution_eq_right {xâ‚€ : G} (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.rOut, g x = g xâ‚€) :
    (Ï† â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€ = integral Î¼ Ï† â€¢ g xâ‚€ := by
  simp_rw [convolution_eq_right' _ Ï†.support_eq.subset hg, lsmul_apply, integral_smul_const]
#align cont_diff_bump.convolution_eq_right ContDiffBump.convolution_eq_right

variable [BorelSpace G]
variable [IsLocallyFiniteMeasure Î¼] [Î¼.IsOpenPosMeasure]
variable [FiniteDimensional â„ G]

/-- If `Ï†` is a normed bump function, compute `Ï† â‹† g`
if `g` is constant on `Metric.ball xâ‚€ Ï†.rOut`. -/
theorem normed_convolution_eq_right {xâ‚€ : G} (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.rOut, g x = g xâ‚€) :
    (Ï†.normed Î¼ â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€ = g xâ‚€ := by
  rw [convolution_eq_right' _ Ï†.support_normed_eq.subset hg]
  exact integral_normed_smul Ï† Î¼ (g xâ‚€)
#align cont_diff_bump.normed_convolution_eq_right ContDiffBump.normed_convolution_eq_right

variable [Î¼.IsAddLeftInvariant]

/-- If `Ï†` is a normed bump function, approximate `(Ï† â‹† g) xâ‚€`
if `g` is near `g xâ‚€` on a ball with radius `Ï†.rOut` around `xâ‚€`. -/
theorem dist_normed_convolution_le {xâ‚€ : G} {Îµ : â„} (hmg : AEStronglyMeasurable g Î¼)
    (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.rOut, dist (g x) (g xâ‚€) â‰¤ Îµ) :
    dist ((Ï†.normed Î¼ â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) (g xâ‚€) â‰¤ Îµ :=
  dist_convolution_le (by simp_rw [â† dist_self (g xâ‚€), hg xâ‚€ (mem_ball_self Ï†.rOut_pos)])
    Ï†.support_normed_eq.subset Ï†.nonneg_normed Ï†.integral_normed hmg hg
#align cont_diff_bump.dist_normed_convolution_le ContDiffBump.dist_normed_convolution_le

/-- `(Ï† i â‹† g i) (k i)` tends to `zâ‚€` as `i` tends to some filter `l` if
* `Ï†` is a sequence of normed bump functions
  such that `(Ï† i).rOut` tends to `0` as `i` tends to `l`;
* `g i` is `Î¼`-a.e. strongly measurable as `i` tends to `l`;
* `g i x` tends to `zâ‚€` as `(i, x)` tends to `l Ã—Ë¢ ğ“ xâ‚€`;
* `k i` tends to `xâ‚€`. -/
nonrec theorem convolution_tendsto_right {Î¹} {Ï† : Î¹ â†’ ContDiffBump (0 : G)} {g : Î¹ â†’ G â†’ E'}
    {k : Î¹ â†’ G} {xâ‚€ : G} {zâ‚€ : E'} {l : Filter Î¹} (hÏ† : Tendsto (fun i => (Ï† i).rOut) l (ğ“ 0))
    (hig : âˆ€á¶  i in l, AEStronglyMeasurable (g i) Î¼) (hcg : Tendsto (uncurry g) (l Ã—Ë¢ ğ“ xâ‚€) (ğ“ zâ‚€))
    (hk : Tendsto k l (ğ“ xâ‚€)) :
    Tendsto (fun i => ((Ï† i).normed Î¼ â‹†[lsmul â„ â„, Î¼] g i) (k i)) l (ğ“ zâ‚€) :=
  convolution_tendsto_right (eventually_of_forall fun i => (Ï† i).nonneg_normed)
    (eventually_of_forall fun i => (Ï† i).integral_normed) (tendsto_support_normed_smallSets hÏ†) hig
    hcg hk
#align cont_diff_bump.convolution_tendsto_right ContDiffBump.convolution_tendsto_right

/-- Special case of `ContDiffBump.convolution_tendsto_right` where `g` is continuous,
  and the limit is taken only in the first function. -/
theorem convolution_tendsto_right_of_continuous {Î¹} {Ï† : Î¹ â†’ ContDiffBump (0 : G)} {l : Filter Î¹}
    (hÏ† : Tendsto (fun i => (Ï† i).rOut) l (ğ“ 0)) (hg : Continuous g) (xâ‚€ : G) :
    Tendsto (fun i => ((Ï† i).normed Î¼ â‹†[lsmul â„ â„, Î¼] g) xâ‚€) l (ğ“ (g xâ‚€)) :=
  convolution_tendsto_right hÏ† (eventually_of_forall fun _ => hg.aestronglyMeasurable)
    ((hg.tendsto xâ‚€).comp tendsto_snd) tendsto_const_nhds
#align cont_diff_bump.convolution_tendsto_right_of_continuous ContDiffBump.convolution_tendsto_right_of_continuous

/-- If a function `g` is locally integrable, then the convolution `Ï† i * g` converges almost
everywhere to `g` if `Ï† i` is a sequence of bump functions with support tending to `0`, provided
that the ratio between the inner and outer radii of `Ï† i` remains bounded. -/
theorem ae_convolution_tendsto_right_of_locallyIntegrable
    {Î¹} {Ï† : Î¹ â†’ ContDiffBump (0 : G)} {l : Filter Î¹} {K : â„}
    (hÏ† : Tendsto (fun i â†¦ (Ï† i).rOut) l (ğ“ 0))
    (h'Ï† : âˆ€á¶  i in l, (Ï† i).rOut â‰¤ K * (Ï† i).rIn) (hg : LocallyIntegrable g Î¼) : âˆ€áµ xâ‚€ âˆ‚Î¼,
    Tendsto (fun i â†¦ ((Ï† i).normed Î¼ â‹†[lsmul â„ â„, Î¼] g) xâ‚€) l (ğ“ (g xâ‚€)) := by
  have : IsAddHaarMeasure Î¼ := âŸ¨âŸ©
  -- By Lebesgue differentiation theorem, the average of `g` on a small ball converges
  -- almost everywhere to the value of `g` as the radius shrinks to zero.
  -- We will see that this set of points satisfies the desired conclusion.
  filter_upwards [(Besicovitch.vitaliFamily Î¼).ae_tendsto_average_norm_sub hg] with xâ‚€ hâ‚€
  simp only [convolution_eq_swap, lsmul_apply]
  have hÏ†' : Tendsto (fun i â†¦ (Ï† i).rOut) l (ğ“[>] 0) :=
    tendsto_nhdsWithin_iff.2 âŸ¨hÏ†, eventually_of_forall (fun i â†¦ (Ï† i).rOut_pos)âŸ©
  have := (hâ‚€.comp (Besicovitch.tendsto_filterAt Î¼ xâ‚€)).comp hÏ†'
  simp only [Function.comp] at this
  apply tendsto_integral_smul_of_tendsto_average_norm_sub (K ^ (FiniteDimensional.finrank â„ G)) this
  Â· filter_upwards with i using
      hg.integrableOn_isCompact (isCompact_closedBall _ _)
  Â· apply tendsto_const_nhds.congr (fun i â†¦ ?_)
    rw [â† integral_neg_eq_self]
    simp only [sub_neg_eq_add, integral_add_left_eq_self, integral_normed]
  Â· filter_upwards with i
    change support ((ContDiffBump.normed (Ï† i) Î¼) âˆ˜ (fun y â†¦ xâ‚€ - y)) âŠ† closedBall xâ‚€ (Ï† i).rOut
    simp only [support_comp_eq_preimage, support_normed_eq]
    intro x hx
    simp only [mem_preimage, mem_ball, dist_zero_right] at hx
    simpa [dist_eq_norm_sub'] using hx.le
  Â· filter_upwards [h'Ï†] with i hi x
    rw [abs_of_nonneg (nonneg_normed _ _), addHaar_closedBall_center]
    exact (Ï† i).normed_le_div_measure_closedBall_rOut _ _ hi _

end ContDiffBump
