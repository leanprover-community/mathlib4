/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.FDeriv.Analytic

/-!
# Analytic functions are `C^âˆ`.
-/

open Set ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {f : E â†’ F} {s : Set E} {x : E} {n : WithTop â„•âˆ}

/-- An analytic function is infinitely differentiable. -/
protected theorem AnalyticOnNhd.contDiffOn [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) :
    ContDiffOn ğ•œ n f s := by
  let t := { x | AnalyticAt ğ•œ f x }
  suffices ContDiffOn ğ•œ Ï‰ f t from (this.of_le le_top).mono h
  rw [â† contDiffOn_infty_iff_contDiffOn_omega]
  have H : AnalyticOnNhd ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_analyticAt ğ•œ f
  exact contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)

/-- An analytic function on the whole space is infinitely differentiable there. -/
theorem AnalyticOnNhd.contDiff [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f univ) :
    ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ]
  exact h.contDiffOn

theorem AnalyticAt.contDiffAt [CompleteSpace F] (h : AnalyticAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x := by
  obtain âŸ¨s, hs, hfâŸ© := h.exists_mem_nhds_analyticOnNhd
  exact hf.contDiffOn.contDiffAt hs

protected lemma AnalyticWithinAt.contDiffWithinAt [CompleteSpace F] {f : E â†’ F} {s : Set E} {x : E}
    (h : AnalyticWithinAt ğ•œ f s x) {n : â„•âˆ} : ContDiffWithinAt ğ•œ n f s x := by
  rcases h.exists_analyticAt with âŸ¨g, fx, fg, hgâŸ©
  exact hg.contDiffAt.contDiffWithinAt.congr (fg.mono (subset_insert _ _)) fx

protected lemma AnalyticOn.contDiffOn [CompleteSpace F] {f : E â†’ F} {s : Set E}
    (h : AnalyticOn ğ•œ f s) {n : â„•âˆ} : ContDiffOn ğ•œ n f s :=
  fun x m â†¦ (h x m).contDiffWithinAt

@[deprecated (since := "2024-09-26")]
alias AnalyticWithinOn.contDiffOn := AnalyticOn.contDiffOn
