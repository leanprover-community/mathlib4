/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.FormalMultilinearSeries
import Mathlib.Data.ENat.Lattice

/-!
# Iterated derivatives of a function

In this file, we define iteratively the `n+1`-th derivative of a function as the
derivative of the `n`-th derivative. It is called `iteratedFDeriv ğ•œ n f x` where `ğ•œ` is the
field, `n` is the number of iterations, `f` is the function and `x` is the point, and it is given
as an `n`-multilinear map. We also define a version `iteratedFDerivWithin` relative to a domain.
Note that, in domains, there may be several choices of possible derivative, so we make some
arbitrary choice in the definition.

We also define a predicate `HasFTaylorSeriesUpTo` (and its localized version
`HasFTaylorSeriesUpToOn`), saying that a sequence of multilinear maps is *a* sequence of
derivatives of `f`. Contrary to `iteratedFDerivWithin`, it accommodates well the
non-uniqueness of derivatives.

## Main definitions and results

Let `f : E â†’ F` be a map between normed vector spaces over a nontrivially normed field `ğ•œ`.

* `HasFTaylorSeriesUpTo n f p`: expresses that the formal multilinear series `p` is a sequence
  of iterated derivatives of `f`, up to the `n`-th term (where `n` is a natural number or `âˆ`).
* `HasFTaylorSeriesUpToOn n f p s`: same thing, but inside a set `s`. The notion of derivative
  is now taken inside `s`. In particular, derivatives don't have to be unique.

* `iteratedFDerivWithin ğ•œ n f s x` is an `n`-th derivative of `f` over the field `ğ•œ` on the
  set `s` at the point `x`. It is a continuous multilinear map from `E^n` to `F`, defined as a
  derivative within `s` of `iteratedFDerivWithin ğ•œ (n-1) f s` if one exists, and `0` otherwise.
* `iteratedFDeriv ğ•œ n f x` is the `n`-th derivative of `f` over the field `ğ•œ` at the point `x`.
  It is a continuous multilinear map from `E^n` to `F`, defined as a derivative of
  `iteratedFDeriv ğ•œ (n-1) f` if one exists, and `0` otherwise.


### Side of the composition, and universe issues

With a naÃ¯ve direct definition, the `n`-th derivative of a function belongs to the space
`E â†’L[ğ•œ] (E â†’L[ğ•œ] (E ... F)...)))` where there are n iterations of `E â†’L[ğ•œ]`. This space
may also be seen as the space of continuous multilinear functions on `n` copies of `E` with
values in `F`, by uncurrying. This is the point of view that is usually adopted in textbooks,
and that we also use. This means that the definition and the first proofs are slightly involved,
as one has to keep track of the uncurrying operation. The uncurrying can be done from the
left or from the right, amounting to defining the `n+1`-th derivative either as the derivative of
the `n`-th derivative, or as the `n`-th derivative of the derivative.
For proofs, it would be more convenient to use the latter approach (from the right),
as it means to prove things at the `n+1`-th step we only need to understand well enough the
derivative in `E â†’L[ğ•œ] F` (contrary to the approach from the left, where one would need to know
enough on the `n`-th derivative to deduce things on the `n+1`-th derivative).

However, the definition from the right leads to a universe polymorphism problem: if we define
`iteratedFDeriv ğ•œ (n + 1) f x = iteratedFDeriv ğ•œ n (fderiv ğ•œ f) x` by induction, we need to
generalize over all spaces (as `f` and `fderiv ğ•œ f` don't take values in the same space). It is
only possible to generalize over all spaces in some fixed universe in an inductive definition.
For `f : E â†’ F`, then `fderiv ğ•œ f` is a map `E â†’ (E â†’L[ğ•œ] F)`. Therefore, the definition will only
work if `F` and `E â†’L[ğ•œ] F` are in the same universe.

This issue does not appear with the definition from the left, where one does not need to generalize
over all spaces. Therefore, we use the definition from the left. This means some proofs later on
become a little bit more complicated: to prove that a function is `C^n`, the most efficient approach
is to exhibit a formula for its `n`-th derivative and prove it is continuous (contrary to the
inductive approach where one would prove smoothness statements without giving a formula for the
derivative). In the end, this approach is still satisfactory as it is good to have formulas for the
iterated derivatives in various constructions.

One point where this explicit approach is particularly delicate is in the proof of smoothness of a
composition: there is a formula for the `n`-th derivative of a composition (FaÃ  di Bruno's formula),
but it is very complicated, while the inductive proof is very simple. The inductive proof would
be good enough for `C^n` functions with `n âˆˆ â„• âˆª {âˆ}` (modulo polymorphism issues, i.e., one would
need to first prove inductively the result when all spaces belong to the same universe, and then
prove the general result by lifting all the spaces to a common universe). However, it would not
work for `C^Ï‰` functions. Therefore, we give the proof based on FaÃ  di Bruno's formula, which is
more complicated but more general.

### Variables management

The textbook definitions and proofs use various identifications and abuse of notations, for instance
when saying that the natural space in which the derivative lives, i.e.,
`E â†’L[ğ•œ] (E â†’L[ğ•œ] ( ... â†’L[ğ•œ] F))`, is the same as a space of multilinear maps. When doing things
formally, we need to provide explicit maps for these identifications, and chase some diagrams to see
everything is compatible with the identifications. In particular, one needs to check that taking the
derivative and then doing the identification, or first doing the identification and then taking the
derivative, gives the same result. The key point for this is that taking the derivative commutes
with continuous linear equivalences. Therefore, we need to implement all our identifications with
continuous linear equivs.

## Notation

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `âŠ¤ : â„•âˆ` with `âˆ`.
-/


noncomputable section

open ENat NNReal Topology Filter Set Fin Filter Function

/-- Smoothness exponent for analytic functions. -/
scoped[ContDiff] notation3 "Ï‰" => (âŠ¤ : WithTop â„•âˆ)
/-- Smoothness exponent for infinitely differentiable functions. -/
scoped[ContDiff] notation3 "âˆ" => ((âŠ¤ : â„•âˆ) : WithTop â„•âˆ)

open scoped ContDiff Pointwise

universe u uE uF

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type uE} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type uF} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {s t u : Set E} {f fâ‚ : E â†’ F} {x : E} {m n N : WithTop â„•âˆ}
  {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Functions with a Taylor series on a domain -/

/-- `HasFTaylorSeriesUpToOn n f p s` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`HasFDerivWithinAt` but for higher-order derivatives.

Notice that `p` does not sum up to `f` on the diagonal (`FormalMultilinearSeries.sum`), even if
`f` is analytic and `n = âˆ`: an additional `1/m!` factor on the `m`th term is necessary for that. -/
structure HasFTaylorSeriesUpToOn
  (n : WithTop â„•âˆ) (f : E â†’ F) (p : E â†’ FormalMultilinearSeries ğ•œ E F) (s : Set E) : Prop where
  zero_eq : âˆ€ x âˆˆ s, (p x 0).curry0 = f x
  protected fderivWithin : âˆ€ m : â„•, m < n â†’ âˆ€ x âˆˆ s,
    HasFDerivWithinAt (p Â· m) (p x m.succ).curryLeft s x
  cont : âˆ€ m : â„•, m â‰¤ n â†’ ContinuousOn (p Â· m) s

theorem HasFTaylorSeriesUpToOn.zero_eq' (h : HasFTaylorSeriesUpToOn n f p s) {x : E} (hx : x âˆˆ s) :
    p x 0 = (continuousMultilinearCurryFin0 ğ•œ E F).symm (f x) := by
  rw [â† h.zero_eq x hx]
  exact (p x 0).uncurry0_curry0.symm

/-- If two functions coincide on a set `s`, then a Taylor series for the first one is as well a
Taylor series for the second one. -/
theorem HasFTaylorSeriesUpToOn.congr (h : HasFTaylorSeriesUpToOn n f p s)
    (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) : HasFTaylorSeriesUpToOn n fâ‚ p s := by
  refine âŸ¨fun x hx => ?_, h.fderivWithin, h.contâŸ©
  rw [hâ‚ x hx]
  exact h.zero_eq x hx

theorem HasFTaylorSeriesUpToOn.congr_series {q} (hp : HasFTaylorSeriesUpToOn n f p s)
    (hpq : âˆ€ m : â„•, m â‰¤ n â†’ EqOn (p Â· m) (q Â· m) s) :
    HasFTaylorSeriesUpToOn n f q s where
  zero_eq x hx := by simp only [â† (hpq 0 (zero_le n) hx), hp.zero_eq x hx]
  fderivWithin m hm x hx := by
    refine ((hp.fderivWithin m hm x hx).congr' (hpq m hm.le).symm hx).congr_fderiv ?_
    refine congrArg _ (hpq (m + 1) ?_ hx)
    exact ENat.add_one_natCast_le_withTop_of_lt hm
  cont m hm := (hp.cont m hm).congr (hpq m hm).symm

theorem HasFTaylorSeriesUpToOn.mono (h : HasFTaylorSeriesUpToOn n f p s) {t : Set E} (hst : t âŠ† s) :
    HasFTaylorSeriesUpToOn n f p t :=
  âŸ¨fun x hx => h.zero_eq x (hst hx), fun m hm x hx => (h.fderivWithin m hm x (hst hx)).mono hst,
    fun m hm => (h.cont m hm).mono hstâŸ©

theorem HasFTaylorSeriesUpToOn.of_le (h : HasFTaylorSeriesUpToOn n f p s) (hmn : m â‰¤ n) :
    HasFTaylorSeriesUpToOn m f p s :=
  âŸ¨h.zero_eq, fun k hk x hx => h.fderivWithin k (lt_of_lt_of_le hk hmn) x hx, fun k hk =>
    h.cont k (le_trans hk hmn)âŸ©

theorem HasFTaylorSeriesUpToOn.continuousOn (h : HasFTaylorSeriesUpToOn n f p s) :
    ContinuousOn f s := by
  have := (h.cont 0 bot_le).congr fun x hx => (h.zero_eq' hx).symm
  rwa [â† (continuousMultilinearCurryFin0 ğ•œ E F).symm.comp_continuousOn_iff]

theorem hasFTaylorSeriesUpToOn_zero_iff :
    HasFTaylorSeriesUpToOn 0 f p s â†” ContinuousOn f s âˆ§ âˆ€ x âˆˆ s, (p x 0).curry0 = f x := by
  refine âŸ¨fun H => âŸ¨H.continuousOn, H.zero_eqâŸ©, fun H =>
      âŸ¨H.2, fun m hm => False.elim (not_le.2 hm bot_le), fun m hm â†¦ ?_âŸ©âŸ©
  obtain rfl : m = 0 := mod_cast hm.antisymm (zero_le _)
  have : EqOn (p Â· 0) ((continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f) s := fun x hx â†¦
    (continuousMultilinearCurryFin0 ğ•œ E F).eq_symm_apply.2 (H.2 x hx)
  rw [continuousOn_congr this, LinearIsometryEquiv.comp_continuousOn_iff]
  exact H.1

theorem hasFTaylorSeriesUpToOn_top_iff_add (hN : âˆ â‰¤ N) (k : â„•) :
    HasFTaylorSeriesUpToOn N f p s â†” âˆ€ n : â„•, HasFTaylorSeriesUpToOn (n + k : â„•) f p s := by
  constructor
  Â· intro H n
    apply H.of_le (natCast_le_of_coe_top_le_withTop hN _)
  Â· intro H
    constructor
    Â· exact (H 0).zero_eq
    Â· intro m _
      apply (H m.succ).fderivWithin m (by norm_cast; cutsat)
    Â· intro m _
      apply (H m).cont m (by simp)

theorem hasFTaylorSeriesUpToOn_top_iff (hN : âˆ â‰¤ N) :
    HasFTaylorSeriesUpToOn N f p s â†” âˆ€ n : â„•, HasFTaylorSeriesUpToOn n f p s := by
  simpa using hasFTaylorSeriesUpToOn_top_iff_add hN 0

/-- In the case that `n = âˆ` we don't need the continuity assumption in
`HasFTaylorSeriesUpToOn`. -/
theorem hasFTaylorSeriesUpToOn_top_iff' (hN : âˆ â‰¤ N) :
    HasFTaylorSeriesUpToOn N f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).curry0 = f x) âˆ§
        âˆ€ m : â„•, âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y m) (p x m.succ).curryLeft s x := by
  -- Everything except for the continuity is trivial:
  refine âŸ¨fun h => âŸ¨h.1, fun m => h.2 m (natCast_lt_of_coe_top_le_withTop hN _)âŸ©, fun h =>
    âŸ¨h.1, fun m _ => h.2 m, fun m _ x hx =>
      -- The continuity follows from the existence of a derivative:
      (h.2 m x hx).continuousWithinAtâŸ©âŸ©

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpToOn.hasFDerivWithinAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : x âˆˆ s) : HasFDerivWithinAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) s x := by
  have A : âˆ€ y âˆˆ s, f y = (continuousMultilinearCurryFin0 ğ•œ E F) (p y 0) := fun y hy â†¦
    (h.zero_eq y hy).symm
  suffices H : HasFDerivWithinAt (continuousMultilinearCurryFin0 ğ•œ E F âˆ˜ (p Â· 0))
    (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) s x from H.congr A (A x hx)
  rw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff']
  have : ((0 : â„•) : â„•âˆ) < n := zero_lt_one.trans_le hn
  convert h.fderivWithin _ this x hx
  ext y v
  change (p x 1) (snoc 0 y) = (p x 1) (cons y v)
  congr with i
  rw [Unique.eq_default (Î± := Fin 1) i]
  rfl

theorem HasFTaylorSeriesUpToOn.differentiableOn (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n) :
    DifferentiableOn ğ•œ f s := fun _x hx => (h.hasFDerivWithinAt hn hx).differentiableWithinAt

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then the term
of order `1` of this series is a derivative of `f` at `x`. -/
theorem HasFTaylorSeriesUpToOn.hasFDerivAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : s âˆˆ ğ“ x) : HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) x :=
  (h.hasFDerivWithinAt hn (mem_of_mem_nhds hx)).hasFDerivAt hx

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
in a neighborhood of `x`, the term of order `1` of this series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpToOn.eventually_hasFDerivAt (h : HasFTaylorSeriesUpToOn n f p s)
    (hn : 1 â‰¤ n) (hx : s âˆˆ ğ“ x) :
    âˆ€á¶  y in ğ“ x, HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p y 1)) y :=
  (eventually_eventually_nhds.2 hx).mono fun _y hy => h.hasFDerivAt hn hy

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
it is differentiable at `x`. -/
theorem HasFTaylorSeriesUpToOn.differentiableAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : s âˆˆ ğ“ x) : DifferentiableAt ğ•œ f x :=
  (h.hasFDerivAt hn hx).differentiableAt

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p` is a Taylor series up to `n`, and
`p (n + 1)` is a derivative of `p n`. -/
theorem hasFTaylorSeriesUpToOn_succ_iff_left {n : â„•} :
    HasFTaylorSeriesUpToOn (n + 1) f p s â†”
      HasFTaylorSeriesUpToOn n f p s âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y n) (p x n.succ).curryLeft s x) âˆ§
          ContinuousOn (fun x => p x (n + 1)) s := by
  constructor
  Â· exact fun h â†¦ âŸ¨h.of_le (mod_cast Nat.le_succ n),
      h.fderivWithin _ (mod_cast lt_add_one n), h.cont (n + 1) le_rflâŸ©
  Â· intro h
    constructor
    Â· exact h.1.zero_eq
    Â· intro m hm
      by_cases h' : m < n
      Â· exact h.1.fderivWithin m (mod_cast h')
      Â· have : m = n := Nat.eq_of_lt_succ_of_not_lt (mod_cast hm) h'
        rw [this]
        exact h.2.1
    Â· intro m hm
      by_cases h' : m â‰¤ n
      Â· apply h.1.cont m (mod_cast h')
      Â· have : m = n + 1 := le_antisymm (mod_cast hm) (not_le.1 h')
        rw [this]
        exact h.2.2

theorem HasFTaylorSeriesUpToOn.shift_of_succ
    {n : â„•} (H : HasFTaylorSeriesUpToOn (n + 1 : â„•) f p s) :
    (HasFTaylorSeriesUpToOn n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
      (fun x => (p x).shift)) s := by
  constructor
  Â· intro x _
    rfl
  Â· intro m (hm : (m : WithTop â„•âˆ) < n) x (hx : x âˆˆ s)
    have A : (m.succ : WithTop â„•âˆ) < n.succ := by
      rw [Nat.cast_lt] at hm âŠ¢
      exact Nat.succ_lt_succ hm
    change HasFDerivWithinAt (continuousMultilinearCurryRightEquiv' ğ•œ m E F âˆ˜ (p Â· m.succ))
      (p x m.succ.succ).curryRight.curryLeft s x
    rw [(continuousMultilinearCurryRightEquiv' ğ•œ m E F).comp_hasFDerivWithinAt_iff']
    convert H.fderivWithin _ A x hx
    ext y v
    change p x (m + 2) (snoc (cons y (init v)) (v (last _))) = p x (m + 2) (cons y v)
    rw [â† cons_snoc_eq_snoc_cons, snoc_init_self]
  Â· intro m (hm : (m : WithTop â„•âˆ) â‰¤ n)
    suffices A : ContinuousOn (p Â· (m + 1)) s from
      (continuousMultilinearCurryRightEquiv' ğ•œ m E F).continuous.comp_continuousOn A
    refine H.cont _ ?_
    rw [Nat.cast_le] at hm âŠ¢
    exact Nat.succ_le_succ hm

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. Version for `n : â„•`. -/
theorem hasFTaylorSeriesUpToOn_succ_nat_iff_right {n : â„•} :
    HasFTaylorSeriesUpToOn (n + 1 : â„•) f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).curry0 = f x) âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y 0) (p x 1).curryLeft s x) âˆ§
          HasFTaylorSeriesUpToOn n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
            (fun x => (p x).shift) s := by
  constructor
  Â· intro H
    refine âŸ¨H.zero_eq, H.fderivWithin 0 (Nat.cast_lt.2 (Nat.succ_pos n)), ?_âŸ©
    exact H.shift_of_succ
  Â· rintro âŸ¨Hzero_eq, Hfderiv_zero, HtaylorâŸ©
    constructor
    Â· exact Hzero_eq
    Â· intro m (hm : (m : WithTop â„•âˆ) < n.succ) x (hx : x âˆˆ s)
      rcases m with - | m
      Â· exact Hfderiv_zero x hx
      Â· have A : (m : WithTop â„•âˆ) < n := by
          rw [Nat.cast_lt] at hm âŠ¢
          exact Nat.lt_of_succ_lt_succ hm
        have :
          HasFDerivWithinAt (ğ•œ := ğ•œ) (continuousMultilinearCurryRightEquiv' ğ•œ m E F âˆ˜ (p Â· m.succ))
            ((p x).shift m.succ).curryLeft s x := Htaylor.fderivWithin _ A x hx
        rw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff'
            (f' := ((p x).shift m.succ).curryLeft)] at this
        convert this
        ext y v
        change
          (p x (Nat.succ (Nat.succ m))) (cons y v) =
            (p x m.succ.succ) (snoc (cons y (init v)) (v (last _)))
        rw [â† cons_snoc_eq_snoc_cons, snoc_init_self]
    Â· intro m (hm : (m : WithTop â„•âˆ) â‰¤ n.succ)
      rcases m with - | m
      Â· have : DifferentiableOn ğ•œ (fun x => p x 0) s := fun x hx =>
          (Hfderiv_zero x hx).differentiableWithinAt
        exact this.continuousOn
      Â· refine (continuousMultilinearCurryRightEquiv' ğ•œ m E F).comp_continuousOn_iff.mp ?_
        refine Htaylor.cont _ ?_
        rw [Nat.cast_le] at hm âŠ¢
        exact Nat.lt_succ_iff.mp hm

/-- `p` is a Taylor series of `f` up to `âŠ¤` if and only if `p.shift` is a Taylor series up to `âŠ¤`
for `p 1`, which is a derivative of `f`. -/
theorem hasFTaylorSeriesUpToOn_top_iff_right (hN : âˆ â‰¤ N) :
    HasFTaylorSeriesUpToOn N f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).curry0 = f x) âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y 0) (p x 1).curryLeft s x) âˆ§
          HasFTaylorSeriesUpToOn N (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
            (fun x => (p x).shift) s := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [hasFTaylorSeriesUpToOn_top_iff_add hN 1] at h
    rw [hasFTaylorSeriesUpToOn_top_iff hN]
    exact âŸ¨(hasFTaylorSeriesUpToOn_succ_nat_iff_right.1 (h 1)).1,
      (hasFTaylorSeriesUpToOn_succ_nat_iff_right.1 (h 1)).2.1,
      fun n â†¦ (hasFTaylorSeriesUpToOn_succ_nat_iff_right.1 (h n)).2.2âŸ©
  Â· apply (hasFTaylorSeriesUpToOn_top_iff_add hN 1).2 (fun n â†¦ ?_)
    rw [hasFTaylorSeriesUpToOn_succ_nat_iff_right]
    exact âŸ¨h.1, h.2.1, (h.2.2).of_le (m := n) (natCast_le_of_coe_top_le_withTop hN n)âŸ©

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. Version for `n : WithTop â„•âˆ`. -/
theorem hasFTaylorSeriesUpToOn_succ_iff_right :
    HasFTaylorSeriesUpToOn (n + 1) f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).curry0 = f x) âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y 0) (p x 1).curryLeft s x) âˆ§
          HasFTaylorSeriesUpToOn n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
            (fun x => (p x).shift) s := by
  match n with
  | âŠ¤ => exact hasFTaylorSeriesUpToOn_top_iff_right (by simp)
  | (âŠ¤ : â„•âˆ) => exact hasFTaylorSeriesUpToOn_top_iff_right (by simp)
  | (n : â„•) => exact hasFTaylorSeriesUpToOn_succ_nat_iff_right

/-! ### Iterated derivative within a set -/


variable (ğ•œ)

/-- The `n`-th derivative of a function along a set, defined inductively by saying that the `n+1`-th
derivative of `f` is the derivative of the `n`-th derivative of `f` along this set, together with
an uncurrying step to see it as a multilinear map in `n+1` variables..
-/
noncomputable def iteratedFDerivWithin (n : â„•) (f : E â†’ F) (s : Set E) : E â†’ E[Ã—n]â†’L[ğ•œ] F :=
  Nat.recOn n (fun x => ContinuousMultilinearMap.uncurry0 ğ•œ E (f x)) fun _ rec x =>
    ContinuousLinearMap.uncurryLeft (fderivWithin ğ•œ rec s x)

/-- Formal Taylor series associated to a function within a set. -/
def ftaylorSeriesWithin (f : E â†’ F) (s : Set E) (x : E) : FormalMultilinearSeries ğ•œ E F := fun n =>
  iteratedFDerivWithin ğ•œ n f s x

variable {ğ•œ}

@[simp]
theorem iteratedFDerivWithin_zero_apply (m : Fin 0 â†’ E) :
    (iteratedFDerivWithin ğ•œ 0 f s x : (Fin 0 â†’ E) â†’ F) m = f x :=
  rfl

theorem iteratedFDerivWithin_zero_eq_comp :
    iteratedFDerivWithin ğ•œ 0 f s = (continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f :=
  rfl

@[simp]
theorem dist_iteratedFDerivWithin_zero (f : E â†’ F) (s : Set E) (x : E)
    (g : E â†’ F) (t : Set E) (y : E) :
    dist (iteratedFDerivWithin ğ•œ 0 f s x) (iteratedFDerivWithin ğ•œ 0 g t y) = dist (f x) (g y) := by
  simp only [iteratedFDerivWithin_zero_eq_comp, comp_apply, LinearIsometryEquiv.dist_map]

@[simp]
theorem norm_iteratedFDerivWithin_zero : â€–iteratedFDerivWithin ğ•œ 0 f s xâ€– = â€–f xâ€– := by
  rw [iteratedFDerivWithin_zero_eq_comp, comp_apply, LinearIsometryEquiv.norm_map]

theorem iteratedFDerivWithin_succ_apply_left {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDerivWithin ğ•œ (n + 1) f s x : (Fin (n + 1) â†’ E) â†’ F) m =
      (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s x : E â†’ E[Ã—n]â†’L[ğ•œ] F) (m 0) (tail m) :=
  rfl

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
theorem iteratedFDerivWithin_succ_eq_comp_left {n : â„•} :
    iteratedFDerivWithin ğ•œ (n + 1) f s =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F).symm âˆ˜
        fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s :=
  rfl

theorem fderivWithin_iteratedFDerivWithin {s : Set E} {n : â„•} :
    fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F) âˆ˜
        iteratedFDerivWithin ğ•œ (n + 1) f s :=
  rfl

theorem norm_fderivWithin_iteratedFDerivWithin {n : â„•} :
    â€–fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s xâ€– =
      â€–iteratedFDerivWithin ğ•œ (n + 1) f s xâ€– := by
  rw [iteratedFDerivWithin_succ_eq_comp_left, comp_apply, LinearIsometryEquiv.norm_map]

@[simp]
theorem dist_iteratedFDerivWithin_one (f g : E â†’ F) {y}
    (hsx : UniqueDiffWithinAt ğ•œ s x) (hyt : UniqueDiffWithinAt ğ•œ t y) :
    dist (iteratedFDerivWithin ğ•œ 1 f s x) (iteratedFDerivWithin ğ•œ 1 g t y)
      = dist (fderivWithin ğ•œ f s x) (fderivWithin ğ•œ g t y) := by
  simp only [iteratedFDerivWithin_succ_eq_comp_left, comp_apply,
    LinearIsometryEquiv.dist_map, iteratedFDerivWithin_zero_eq_comp,
    LinearIsometryEquiv.comp_fderivWithin, hsx, hyt]
  apply (continuousMultilinearCurryFin0 ğ•œ E F).symm.toLinearIsometry.postcomp.dist_map

@[simp]
theorem norm_iteratedFDerivWithin_one (f : E â†’ F) (h : UniqueDiffWithinAt ğ•œ s x) :
    â€–iteratedFDerivWithin ğ•œ 1 f s xâ€– = â€–fderivWithin ğ•œ f s xâ€– := by
  simp only [â† norm_fderivWithin_iteratedFDerivWithin,
    iteratedFDerivWithin_zero_eq_comp, LinearIsometryEquiv.comp_fderivWithin _ h]
  apply (continuousMultilinearCurryFin0 ğ•œ E F).symm.toLinearIsometry.norm_toContinuousLinearMap_comp

theorem iteratedFDerivWithin_succ_apply_right {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s)
    (m : Fin (n + 1) â†’ E) :
    (iteratedFDerivWithin ğ•œ (n + 1) f s x : (Fin (n + 1) â†’ E) â†’ F) m =
      iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s x (init m) (m (last n)) := by
  induction n generalizing x with
  | zero =>
    rw [iteratedFDerivWithin_succ_eq_comp_left, iteratedFDerivWithin_zero_eq_comp,
      iteratedFDerivWithin_zero_apply, Function.comp_apply,
      LinearIsometryEquiv.comp_fderivWithin _ (hs x hx)]
    simp
  | succ n IH =>
    let I := (continuousMultilinearCurryRightEquiv' ğ•œ n E F).symm
    have A : âˆ€ y âˆˆ s, iteratedFDerivWithin ğ•œ n.succ f s y =
        (I âˆ˜ iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) y := fun y hy â†¦ by
      ext m
      simp [IH hy m, I]
    calc
      (iteratedFDerivWithin ğ•œ (n + 2) f s x : (Fin (n + 2) â†’ E) â†’ F) m =
          (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n.succ f s) s x : E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0)
            (tail m) := by
        simp [iteratedFDerivWithin_succ_eq_comp_left]
      _ = (fderivWithin ğ•œ (I âˆ˜ iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s) s x :
              E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0) (tail m) := by
        rw [fderivWithin_congr A (A x hx)]
      _ = (I âˆ˜ fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s) s x :
              E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0) (tail m) := by
        simp [LinearIsometryEquiv.comp_fderivWithin _ (hs x hx)]
      _ = (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) s x :
              E â†’ E[Ã—n]â†’L[ğ•œ] E â†’L[ğ•œ] F) (m 0) (init (tail m)) ((tail m) (last n)) := by
        simp [I]
      _ = iteratedFDerivWithin ğ•œ (Nat.succ n) (fun y => fderivWithin ğ•œ f s y) s x (init m)
            (m (last (n + 1))) := by
        rw [iteratedFDerivWithin_succ_apply_left, tail_init_eq_init_tail]
        simp [init, tail]

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
theorem iteratedFDerivWithin_succ_eq_comp_right {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ (n + 1) f s x =
      ((continuousMultilinearCurryRightEquiv' ğ•œ n E F).symm âˆ˜
          iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s)
        x := by
  ext m; simp [iteratedFDerivWithin_succ_apply_right hs hx]

theorem norm_iteratedFDerivWithin_fderivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    â€–iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s xâ€– =
      â€–iteratedFDerivWithin ğ•œ (n + 1) f s xâ€– := by
  rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply, LinearIsometryEquiv.norm_map]

@[simp]
theorem iteratedFDerivWithin_one_apply (h : UniqueDiffWithinAt ğ•œ s x) (m : Fin 1 â†’ E) :
    iteratedFDerivWithin ğ•œ 1 f s x m = fderivWithin ğ•œ f s x (m 0) := by
  simp [iteratedFDerivWithin_succ_apply_left, iteratedFDerivWithin_zero_eq_comp,
    (continuousMultilinearCurryFin0 ğ•œ E F).symm.comp_fderivWithin h]

/-- On a set of unique differentiability, the second derivative is obtained by taking the
derivative of the derivative. -/
lemma iteratedFDerivWithin_two_apply (f : E â†’ F) {z : E} (hs : UniqueDiffOn ğ•œ s) (hz : z âˆˆ s)
    (m : Fin 2 â†’ E) :
    iteratedFDerivWithin ğ•œ 2 f s z m = fderivWithin ğ•œ (fderivWithin ğ•œ f s) s z (m 0) (m 1) := by
  simp only [iteratedFDerivWithin_succ_apply_right hs hz]
  rfl

/-- On a set of unique differentiability, the second derivative is obtained by taking the
derivative of the derivative. -/
lemma iteratedFDerivWithin_two_apply' (f : E â†’ F) {z : E} (hs : UniqueDiffOn ğ•œ s) (hz : z âˆˆ s)
    (v w : E) :
    iteratedFDerivWithin ğ•œ 2 f s z ![v, w] = fderivWithin ğ•œ (fderivWithin ğ•œ f s) s z v w :=
  iteratedFDerivWithin_two_apply f hs hz _

theorem Filter.EventuallyEq.iteratedFDerivWithin' (h : fâ‚ =á¶ [ğ“[s] x] f) (ht : t âŠ† s) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ t =á¶ [ğ“[s] x] iteratedFDerivWithin ğ•œ n f t := by
  induction n with
  | zero => exact h.mono fun y hy => DFunLike.ext _ _ fun _ => hy
  | succ n ihn =>
    have : fderivWithin ğ•œ _ t =á¶ [ğ“[s] x] fderivWithin ğ•œ _ t := ihn.fderivWithin' ht
    refine this.mono fun y hy => ?_
    simp only [iteratedFDerivWithin_succ_eq_comp_left, hy, (Â· âˆ˜ Â·)]

protected theorem Filter.EventuallyEq.iteratedFDerivWithin (h : fâ‚ =á¶ [ğ“[s] x] f) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ s =á¶ [ğ“[s] x] iteratedFDerivWithin ğ•œ n f s :=
  h.iteratedFDerivWithin' Subset.rfl n

/-- If two functions coincide in a neighborhood of `x` within a set `s` and at `x`, then their
iterated differentials within this set at `x` coincide. -/
theorem Filter.EventuallyEq.iteratedFDerivWithin_eq (h : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x)
    (n : â„•) : iteratedFDerivWithin ğ•œ n fâ‚ s x = iteratedFDerivWithin ğ•œ n f s x :=
  have : fâ‚ =á¶ [ğ“[insert x s] x] f := by simpa [EventuallyEq, hx]
  (this.iteratedFDerivWithin' (subset_insert _ _) n).self_of_nhdsWithin (mem_insert _ _)

/-- If two functions coincide on a set `s`, then their iterated differentials within this set
coincide. See also `Filter.EventuallyEq.iteratedFDerivWithin_eq` and
`Filter.EventuallyEq.iteratedFDerivWithin`. -/
theorem iteratedFDerivWithin_congr (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ s x = iteratedFDerivWithin ğ•œ n f s x :=
  (hs.eventuallyEq.filter_mono inf_le_right).iteratedFDerivWithin_eq (hs hx) _

/-- If two functions coincide on a set `s`, then their iterated differentials within this set
coincide. See also `Filter.EventuallyEq.iteratedFDerivWithin_eq` and
`Filter.EventuallyEq.iteratedFDerivWithin`. -/
protected theorem Set.EqOn.iteratedFDerivWithin (hs : EqOn fâ‚ f s) (n : â„•) :
    EqOn (iteratedFDerivWithin ğ•œ n fâ‚ s) (iteratedFDerivWithin ğ•œ n f s) s := fun _x hx =>
  iteratedFDerivWithin_congr hs hx n

theorem iteratedFDerivWithin_eventually_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ n f t := by
  induction n generalizing x with
  | zero => rfl
  | succ n ihn =>
    refine (eventually_nhds_nhdsWithin.2 h).mono fun y hy => ?_
    simp only [iteratedFDerivWithin_succ_eq_comp_left, (Â· âˆ˜ Â·)]
    rw [(ihn hy).fderivWithin_eq_of_nhds, fderivWithin_congr_set' _ hy]

theorem iteratedFDerivWithin_eventually_congr_set (h : s =á¶ [ğ“ x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ n f t :=
  iteratedFDerivWithin_eventually_congr_set' x (h.filter_mono inf_le_left) n

/-- If two sets coincide in a punctured neighborhood of `x`,
then the corresponding iterated derivatives are equal.

Note that we also allow to puncture the neighborhood of `x` at `y`.
If `y â‰  x`, then this is a no-op. -/
theorem iteratedFDerivWithin_congr_set' {y} (h : s =á¶ [ğ“[{y}á¶œ] x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s x = iteratedFDerivWithin ğ•œ n f t x :=
  (iteratedFDerivWithin_eventually_congr_set' y h n).self_of_nhds

@[simp]
theorem iteratedFDerivWithin_insert {n y} :
    iteratedFDerivWithin ğ•œ n f (insert x s) y = iteratedFDerivWithin ğ•œ n f s y :=
  iteratedFDerivWithin_congr_set' (y := x)
    (eventually_mem_nhdsWithin.mono <| by intros; simp_all).set_eq _

theorem iteratedFDerivWithin_congr_set (h : s =á¶ [ğ“ x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s x = iteratedFDerivWithin ğ•œ n f t x :=
  (iteratedFDerivWithin_eventually_congr_set h n).self_of_nhds

@[simp]
theorem ftaylorSeriesWithin_insert :
    ftaylorSeriesWithin ğ•œ f (insert x s) = ftaylorSeriesWithin ğ•œ f s := by
  ext y n : 2
  apply iteratedFDerivWithin_insert

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x` within `s`. -/
theorem iteratedFDerivWithin_inter' {n : â„•} (hu : u âˆˆ ğ“[s] x) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_congr_set (nhdsWithin_eq_iff_eventuallyEq.1 <| nhdsWithin_inter_of_mem' hu) _

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x`. -/
theorem iteratedFDerivWithin_inter {n : â„•} (hu : u âˆˆ ğ“ x) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_inter' (mem_nhdsWithin_of_mem_nhds hu)

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with an open set containing `x`. -/
theorem iteratedFDerivWithin_inter_open {n : â„•} (hu : IsOpen u) (hx : x âˆˆ u) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_inter (hu.mem_nhds hx)

/-- On a set with unique differentiability, any choice of iterated differential has to coincide
with the one we have chosen in `iteratedFDerivWithin ğ•œ m f s`. -/
theorem HasFTaylorSeriesUpToOn.eq_iteratedFDerivWithin_of_uniqueDiffOn
    (h : HasFTaylorSeriesUpToOn n f p s) {m : â„•} (hmn : m â‰¤ n) (hs : UniqueDiffOn ğ•œ s)
    (hx : x âˆˆ s) : p x m = iteratedFDerivWithin ğ•œ m f s x := by
  induction m generalizing x with
  | zero => rw [h.zero_eq' hx, iteratedFDerivWithin_zero_eq_comp, comp_apply]
  | succ m IH =>
    have A : m < n := lt_of_lt_of_le (mod_cast lt_add_one m) hmn
    have :
      HasFDerivWithinAt (fun y : E => iteratedFDerivWithin ğ•œ m f s y)
        (ContinuousMultilinearMap.curryLeft (p x (Nat.succ m))) s x :=
      (h.fderivWithin m A x hx).congr (fun y hy => (IH (le_of_lt A) hy).symm)
        (IH (le_of_lt A) hx).symm
    rw [iteratedFDerivWithin_succ_eq_comp_left, Function.comp_apply, this.fderivWithin (hs x hx)]
    exact (ContinuousMultilinearMap.uncurry_curryLeft _).symm

/-- The iterated derivative commutes with shifting the function by a constant on the left. -/
lemma iteratedFDerivWithin_comp_add_left' (n : â„•) (a : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (a + z)) s =
      fun x â†¦ iteratedFDerivWithin ğ•œ n f (a +áµ¥ s) (a + x) := by
  induction n with
  | zero => simp [iteratedFDerivWithin]
  | succ n IH =>
    ext v
    rw [iteratedFDerivWithin_succ_eq_comp_left, iteratedFDerivWithin_succ_eq_comp_left]
    simp only [Nat.succ_eq_add_one, IH, comp_apply, continuousMultilinearCurryLeftEquiv_symm_apply]
    congr 2
    rw [fderivWithin_comp_add_left]

/-- The iterated derivative commutes with shifting the function by a constant on the left. -/
lemma iteratedFDerivWithin_comp_add_left (n : â„•) (a : E) (x : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (a + z)) s x =
      iteratedFDerivWithin ğ•œ n f (a +áµ¥ s) (a + x) := by
  simp [iteratedFDerivWithin_comp_add_left']

/-- The iterated derivative commutes with shifting the function by a constant on the right. -/
lemma iteratedFDerivWithin_comp_add_right' (n : â„•) (a : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (z + a)) s =
      fun x â†¦ iteratedFDerivWithin ğ•œ n f (a +áµ¥ s) (x + a) := by
  simpa [add_comm a] using iteratedFDerivWithin_comp_add_left' n a

/-- The iterated derivative commutes with shifting the function by a constant on the right. -/
lemma iteratedFDerivWithin_comp_add_right (n : â„•) (a : E) (x : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (z + a)) s x =
      iteratedFDerivWithin ğ•œ n f (a +áµ¥ s) (x + a) := by
  simp [iteratedFDerivWithin_comp_add_right']

/-- The iterated derivative commutes with subtracting a constant. -/
lemma iteratedFDerivWithin_comp_sub' (n : â„•) (a : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (z - a)) s =
      fun x â†¦ iteratedFDerivWithin ğ•œ n f (-a +áµ¥ s) (x - a) := by
  simpa [sub_eq_add_neg] using iteratedFDerivWithin_comp_add_right' n (-a)

/-- The iterated derivative commutes with subtracting a constant. -/
lemma iteratedFDerivWithin_comp_sub (n : â„•) (a : E) :
    iteratedFDerivWithin ğ•œ n (fun z â†¦ f (z - a)) s x =
      iteratedFDerivWithin ğ•œ n f (-a +áµ¥ s) (x - a) := by
  simp [iteratedFDerivWithin_comp_sub']

/-! ### Functions with a Taylor series on the whole space -/

/-- `HasFTaylorSeriesUpTo n f p` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`HasFDerivAt` but for higher-order derivatives.

Notice that `p` does not sum up to `f` on the diagonal (`FormalMultilinearSeries.sum`), even if
`f` is analytic and `n = âˆ`: an addition `1/m!` factor on the `m`th term is necessary for that. -/
structure HasFTaylorSeriesUpTo
  (n : WithTop â„•âˆ) (f : E â†’ F) (p : E â†’ FormalMultilinearSeries ğ•œ E F) : Prop where
  zero_eq : âˆ€ x, (p x 0).curry0 = f x
  fderiv : âˆ€ m : â„•, m < n â†’ âˆ€ x, HasFDerivAt (fun y => p y m) (p x m.succ).curryLeft x
  cont : âˆ€ m : â„•, m â‰¤ n â†’ Continuous fun x => p x m

theorem HasFTaylorSeriesUpTo.zero_eq' (h : HasFTaylorSeriesUpTo n f p) (x : E) :
    p x 0 = (continuousMultilinearCurryFin0 ğ•œ E F).symm (f x) := by
  rw [â† h.zero_eq x]
  exact (p x 0).uncurry0_curry0.symm

theorem hasFTaylorSeriesUpToOn_univ_iff :
    HasFTaylorSeriesUpToOn n f p univ â†” HasFTaylorSeriesUpTo n f p := by
  constructor
  Â· intro H
    constructor
    Â· exact fun x => H.zero_eq x (mem_univ x)
    Â· intro m hm x
      rw [â† hasFDerivWithinAt_univ]
      exact H.fderivWithin m hm x (mem_univ x)
    Â· intro m hm
      rw [â† continuousOn_univ]
      exact H.cont m hm
  Â· intro H
    constructor
    Â· exact fun x _ => H.zero_eq x
    Â· intro m hm x _
      rw [hasFDerivWithinAt_univ]
      exact H.fderiv m hm x
    Â· intro m hm
      rw [continuousOn_univ]
      exact H.cont m hm

theorem HasFTaylorSeriesUpTo.hasFTaylorSeriesUpToOn (h : HasFTaylorSeriesUpTo n f p) (s : Set E) :
    HasFTaylorSeriesUpToOn n f p s :=
  (hasFTaylorSeriesUpToOn_univ_iff.2 h).mono (subset_univ _)

theorem HasFTaylorSeriesUpTo.of_le (h : HasFTaylorSeriesUpTo n f p) (hmn : m â‰¤ n) :
    HasFTaylorSeriesUpTo m f p := by
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h âŠ¢; exact h.of_le hmn

theorem HasFTaylorSeriesUpTo.continuous (h : HasFTaylorSeriesUpTo n f p) : Continuous f := by
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h
  rw [â† continuousOn_univ]
  exact h.continuousOn

theorem hasFTaylorSeriesUpTo_zero_iff :
    HasFTaylorSeriesUpTo 0 f p â†” Continuous f âˆ§ âˆ€ x, (p x 0).curry0 = f x := by
  simp [hasFTaylorSeriesUpToOn_univ_iff.symm, continuousOn_univ,
    hasFTaylorSeriesUpToOn_zero_iff]

theorem hasFTaylorSeriesUpTo_top_iff (hN : âˆ â‰¤ N) :
    HasFTaylorSeriesUpTo N f p â†” âˆ€ n : â„•, HasFTaylorSeriesUpTo n f p := by
  simp only [â† hasFTaylorSeriesUpToOn_univ_iff, hasFTaylorSeriesUpToOn_top_iff hN]

/-- In the case that `n = âˆ` we don't need the continuity assumption in
`HasFTaylorSeriesUpTo`. -/
theorem hasFTaylorSeriesUpTo_top_iff' (hN : âˆ â‰¤ N) :
    HasFTaylorSeriesUpTo N f p â†”
      (âˆ€ x, (p x 0).curry0 = f x) âˆ§
        âˆ€ (m : â„•) (x), HasFDerivAt (fun y => p y m) (p x m.succ).curryLeft x := by
  simp only [â† hasFTaylorSeriesUpToOn_univ_iff, hasFTaylorSeriesUpToOn_top_iff' hN, mem_univ,
    forall_true_left, hasFDerivWithinAt_univ]

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpTo.hasFDerivAt (h : HasFTaylorSeriesUpTo n f p) (hn : 1 â‰¤ n) (x : E) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) x := by
  rw [â† hasFDerivWithinAt_univ]
  exact (hasFTaylorSeriesUpToOn_univ_iff.2 h).hasFDerivWithinAt hn (mem_univ _)

theorem HasFTaylorSeriesUpTo.differentiable (h : HasFTaylorSeriesUpTo n f p) (hn : 1 â‰¤ n) :
    Differentiable ğ•œ f := fun x => (h.hasFDerivAt hn x).differentiableAt

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. -/
theorem hasFTaylorSeriesUpTo_succ_nat_iff_right {n : â„•} :
    HasFTaylorSeriesUpTo (n + 1 : â„•) f p â†”
      (âˆ€ x, (p x 0).curry0 = f x) âˆ§
        (âˆ€ x, HasFDerivAt (fun y => p y 0) (p x 1).curryLeft x) âˆ§
          HasFTaylorSeriesUpTo n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) fun x =>
            (p x).shift := by
  simp only [hasFTaylorSeriesUpToOn_succ_nat_iff_right, â† hasFTaylorSeriesUpToOn_univ_iff, mem_univ,
    forall_true_left, hasFDerivWithinAt_univ]

/-! ### Iterated derivative -/


variable (ğ•œ)

/-- The `n`-th derivative of a function, as a multilinear map, defined inductively. -/
noncomputable def iteratedFDeriv (n : â„•) (f : E â†’ F) : E â†’ E[Ã—n]â†’L[ğ•œ] F :=
  Nat.recOn n (fun x => ContinuousMultilinearMap.uncurry0 ğ•œ E (f x)) fun _ rec x =>
    ContinuousLinearMap.uncurryLeft (fderiv ğ•œ rec x)

/-- Formal Taylor series associated to a function. -/
def ftaylorSeries (f : E â†’ F) (x : E) : FormalMultilinearSeries ğ•œ E F := fun n =>
  iteratedFDeriv ğ•œ n f x

variable {ğ•œ}

@[simp]
theorem iteratedFDeriv_zero_apply (m : Fin 0 â†’ E) :
    (iteratedFDeriv ğ•œ 0 f x : (Fin 0 â†’ E) â†’ F) m = f x :=
  rfl

theorem iteratedFDeriv_zero_eq_comp :
    iteratedFDeriv ğ•œ 0 f = (continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f :=
  rfl

@[simp]
theorem norm_iteratedFDeriv_zero : â€–iteratedFDeriv ğ•œ 0 f xâ€– = â€–f xâ€– := by
  rw [iteratedFDeriv_zero_eq_comp, comp_apply, LinearIsometryEquiv.norm_map]

theorem iteratedFDerivWithin_zero_eq : iteratedFDerivWithin ğ•œ 0 f s = iteratedFDeriv ğ•œ 0 f := rfl

theorem iteratedFDeriv_succ_apply_left {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDeriv ğ•œ (n + 1) f x : (Fin (n + 1) â†’ E) â†’ F) m =
      (fderiv ğ•œ (iteratedFDeriv ğ•œ n f) x : E â†’ E[Ã—n]â†’L[ğ•œ] F) (m 0) (tail m) :=
  rfl

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
theorem iteratedFDeriv_succ_eq_comp_left {n : â„•} :
    iteratedFDeriv ğ•œ (n + 1) f =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F).symm âˆ˜
        fderiv ğ•œ (iteratedFDeriv ğ•œ n f) :=
  rfl

/-- Writing explicitly the derivative of the `n`-th derivative as the composition of a currying
linear equiv, and the `n + 1`-th derivative. -/
theorem fderiv_iteratedFDeriv {n : â„•} :
    fderiv ğ•œ (iteratedFDeriv ğ•œ n f) =
      continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F âˆ˜
        iteratedFDeriv ğ•œ (n + 1) f :=
  rfl

theorem tsupport_iteratedFDeriv_subset (n : â„•) : tsupport (iteratedFDeriv ğ•œ n f) âŠ† tsupport f := by
  induction n with
  | zero =>
    rw [iteratedFDeriv_zero_eq_comp]
    exact closure_minimal ((support_comp_subset (LinearIsometryEquiv.map_zero _) _).trans
      subset_closure) isClosed_closure
  | succ n IH =>
    rw [iteratedFDeriv_succ_eq_comp_left]
    exact closure_minimal ((support_comp_subset (LinearIsometryEquiv.map_zero _) _).trans
      ((support_fderiv_subset ğ•œ).trans IH)) isClosed_closure

theorem support_iteratedFDeriv_subset (n : â„•) : support (iteratedFDeriv ğ•œ n f) âŠ† tsupport f :=
  subset_closure.trans (tsupport_iteratedFDeriv_subset n)

theorem HasCompactSupport.iteratedFDeriv (hf : HasCompactSupport f) (n : â„•) :
    HasCompactSupport (iteratedFDeriv ğ•œ n f) :=
  hf.of_isClosed_subset isClosed_closure (tsupport_iteratedFDeriv_subset n)

theorem norm_fderiv_iteratedFDeriv {n : â„•} :
    â€–fderiv ğ•œ (iteratedFDeriv ğ•œ n f) xâ€– = â€–iteratedFDeriv ğ•œ (n + 1) f xâ€– := by
  rw [iteratedFDeriv_succ_eq_comp_left, comp_apply, LinearIsometryEquiv.norm_map]

theorem iteratedFDerivWithin_univ {n : â„•} :
    iteratedFDerivWithin ğ•œ n f univ = iteratedFDeriv ğ•œ n f := by
  induction n with
  | zero => ext x; simp
  | succ n IH =>
    ext x m
    rw [iteratedFDeriv_succ_apply_left, iteratedFDerivWithin_succ_apply_left, IH, fderivWithin_univ]

variable (ğ•œ) in
/-- If two functions agree in a neighborhood, then so do their iterated derivatives. -/
theorem Filter.EventuallyEq.iteratedFDeriv
    {fâ‚ fâ‚‚ : E â†’ F} {x : E} (h : fâ‚ =á¶ [ğ“ x] fâ‚‚) (n : â„•) :
    iteratedFDeriv ğ•œ n fâ‚ =á¶ [ğ“ x] iteratedFDeriv ğ•œ n fâ‚‚ := by
  simp_all [â† nhdsWithin_univ, â† iteratedFDerivWithin_univ,
    Filter.EventuallyEq.iteratedFDerivWithin]

theorem HasFTaylorSeriesUpTo.eq_iteratedFDeriv
    (h : HasFTaylorSeriesUpTo n f p) {m : â„•} (hmn : m â‰¤ n) (x : E) :
    p x m = iteratedFDeriv ğ•œ m f x := by
  rw [â† iteratedFDerivWithin_univ]
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h
  exact h.eq_iteratedFDerivWithin_of_uniqueDiffOn hmn uniqueDiffOn_univ (mem_univ _)

/-- In an open set, the iterated derivative within this set coincides with the global iterated
derivative. -/
theorem iteratedFDerivWithin_of_isOpen (n : â„•) (hs : IsOpen s) :
    EqOn (iteratedFDerivWithin ğ•œ n f s) (iteratedFDeriv ğ•œ n f) s := by
  induction n with
  | zero =>
    intro x _
    ext1
    simp only [iteratedFDerivWithin_zero_apply, iteratedFDeriv_zero_apply]
  | succ n IH =>
    intro x hx
    rw [iteratedFDeriv_succ_eq_comp_left, iteratedFDerivWithin_succ_eq_comp_left]
    dsimp
    congr 1
    rw [fderivWithin_of_isOpen hs hx]
    apply Filter.EventuallyEq.fderiv_eq
    filter_upwards [hs.mem_nhds hx]
    exact IH

theorem ftaylorSeriesWithin_univ : ftaylorSeriesWithin ğ•œ f univ = ftaylorSeries ğ•œ f := by
  ext1 x; ext1 n
  change iteratedFDerivWithin ğ•œ n f univ x = iteratedFDeriv ğ•œ n f x
  rw [iteratedFDerivWithin_univ]

theorem iteratedFDeriv_succ_apply_right {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDeriv ğ•œ (n + 1) f x : (Fin (n + 1) â†’ E) â†’ F) m =
      iteratedFDeriv ğ•œ n (fun y => fderiv ğ•œ f y) x (init m) (m (last n)) := by
  rw [â† iteratedFDerivWithin_univ, â† iteratedFDerivWithin_univ, â† fderivWithin_univ]
  exact iteratedFDerivWithin_succ_apply_right uniqueDiffOn_univ (mem_univ _) _

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
theorem iteratedFDeriv_succ_eq_comp_right {n : â„•} :
    iteratedFDeriv ğ•œ (n + 1) f x =
      ((continuousMultilinearCurryRightEquiv' ğ•œ n E F).symm âˆ˜
          iteratedFDeriv ğ•œ n fun y => fderiv ğ•œ f y) x := by
  ext m
  rw [iteratedFDeriv_succ_apply_right, comp_apply, continuousMultilinearCurryRightEquiv_symm_apply']

theorem norm_iteratedFDeriv_fderiv {n : â„•} :
    â€–iteratedFDeriv ğ•œ n (fderiv ğ•œ f) xâ€– = â€–iteratedFDeriv ğ•œ (n + 1) f xâ€– := by
  rw [iteratedFDeriv_succ_eq_comp_right, comp_apply, LinearIsometryEquiv.norm_map]

@[simp]
theorem iteratedFDeriv_one_apply (m : Fin 1 â†’ E) :
    iteratedFDeriv ğ•œ 1 f x m = fderiv ğ•œ f x (m 0) := by
  rw [iteratedFDeriv_succ_apply_right, iteratedFDeriv_zero_apply, last_zero]

lemma iteratedFDeriv_two_apply (f : E â†’ F) (z : E) (m : Fin 2 â†’ E) :
    iteratedFDeriv ğ•œ 2 f z m = fderiv ğ•œ (fderiv ğ•œ f) z (m 0) (m 1) := by
  simp [iteratedFDeriv_succ_apply_right, init]

/-- The iterated derivative commutes with shifting the function by a constant on the left. -/
lemma iteratedFDeriv_comp_add_left' (n : â„•) (a : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (a + z)) = fun x â†¦ iteratedFDeriv ğ•œ n f (a + x) := by
  simpa [â† iteratedFDerivWithin_univ] using iteratedFDerivWithin_comp_add_left' n a (s := univ)

/-- The iterated derivative commutes with shifting the function by a constant on the left. -/
lemma iteratedFDeriv_comp_add_left (n : â„•) (a : E) (x : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (a + z)) x = iteratedFDeriv ğ•œ n f (a + x) := by
  simp [iteratedFDeriv_comp_add_left']

/-- The iterated derivative commutes with shifting the function by a constant on the right. -/
lemma iteratedFDeriv_comp_add_right' (n : â„•) (a : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (z + a)) = fun x â†¦ iteratedFDeriv ğ•œ n f (x + a) := by
  simpa [add_comm a] using iteratedFDeriv_comp_add_left' n a

/-- The iterated derivative commutes with shifting the function by a constant on the right. -/
lemma iteratedFDeriv_comp_add_right (n : â„•) (a : E) (x : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (z + a)) x = iteratedFDeriv ğ•œ n f (x + a) := by
  simp [iteratedFDeriv_comp_add_right']

/-- The iterated derivative commutes with subtracting a constant. -/
lemma iteratedFDeriv_comp_sub' (n : â„•) (a : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (z - a)) = fun x â†¦ iteratedFDeriv ğ•œ n f (x - a) := by
  simpa [sub_eq_add_neg] using iteratedFDeriv_comp_add_right' n (-a)

/-- The iterated derivative commutes with subtracting a constant. -/
lemma iteratedFDeriv_comp_sub (n : â„•) (a : E) (x : E) :
    iteratedFDeriv ğ•œ n (fun z â†¦ f (z - a)) x = iteratedFDeriv ğ•œ n f (x - a) := by
  simp [iteratedFDeriv_comp_sub']
