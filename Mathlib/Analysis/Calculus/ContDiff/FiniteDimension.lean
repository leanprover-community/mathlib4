/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Normed.Module.FiniteDimension

/-!
# Higher differentiability in finite dimensions.

-/


noncomputable section

universe uD uE uF

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {D : Type uD} [NormedAddCommGroup D] [NormedSpace ğ•œ D]
  {E : Type uE} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type uF} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {n : WithTop â„•âˆ} {f : D â†’ E} {s : Set D}

/-! ### Finite dimensional results -/

section FiniteDimensional

open Function Module

open scoped ContDiff

variable [CompleteSpace ğ•œ]


/-- A family of continuous linear maps is `C^n` on `s` if all its applications are. -/
theorem contDiffOn_clm_apply {f : D â†’ E â†’L[ğ•œ] F} {s : Set D} [FiniteDimensional ğ•œ E] :
    ContDiffOn ğ•œ n f s â†” âˆ€ y, ContDiffOn ğ•œ n (fun x => f x y) s := by
  refine âŸ¨fun h y => h.clm_apply contDiffOn_const, fun h => ?_âŸ©
  let d := finrank ğ•œ E
  have hd : d = finrank ğ•œ (Fin d â†’ ğ•œ) := (finrank_fin_fun ğ•œ).symm
  let eâ‚ := ContinuousLinearEquiv.ofFinrankEq hd
  let eâ‚‚ := (eâ‚.arrowCongr (1 : F â‰ƒL[ğ•œ] F)).trans (ContinuousLinearEquiv.piRing (Fin d))
  rw [â† id_comp f, â† eâ‚‚.symm_comp_self]
  exact eâ‚‚.symm.contDiff.comp_contDiffOn (contDiffOn_pi.mpr fun i => h _)

theorem contDiff_clm_apply_iff {f : D â†’ E â†’L[ğ•œ] F} [FiniteDimensional ğ•œ E] :
    ContDiff ğ•œ n f â†” âˆ€ y, ContDiff ğ•œ n fun x => f x y := by
  simp_rw [â† contDiffOn_univ, contDiffOn_clm_apply]

/-- This is a useful lemma to prove that a certain operation preserves functions being `C^n`.
When you do induction on `n`, this gives a useful characterization of a function being `C^(n+1)`,
assuming you have already computed the derivative. The advantage of this version over
`contDiff_succ_iff_fderiv` is that both occurrences of `ContDiff` are for functions with the same
domain and codomain (`D` and `E`). This is not the case for `contDiff_succ_iff_fderiv`, which
often requires an inconvenient need to generalize `F`, which results in universe issues
(see the discussion in the section of `ContDiff.comp`).

This lemma avoids these universe issues, but only applies for finite dimensional `D`. -/
theorem contDiff_succ_iff_fderiv_apply [FiniteDimensional ğ•œ D] :
    ContDiff ğ•œ (n + 1) f â†” Differentiable ğ•œ f âˆ§
      (n = Ï‰ â†’ AnalyticOnNhd ğ•œ f Set.univ) âˆ§ âˆ€ y, ContDiff ğ•œ n fun x => fderiv ğ•œ f x y := by
  rw [contDiff_succ_iff_fderiv, contDiff_clm_apply_iff]

theorem contDiffOn_succ_of_fderiv_apply [FiniteDimensional ğ•œ D]
    (hf : DifferentiableOn ğ•œ f s) (h'f : n = Ï‰ â†’ AnalyticOn ğ•œ f s)
    (h : âˆ€ y, ContDiffOn ğ•œ n (fun x => fderivWithin ğ•œ f s x y) s) :
    ContDiffOn ğ•œ (n + 1) f s :=
  contDiffOn_succ_of_fderivWithin hf h'f <| contDiffOn_clm_apply.mpr h

theorem contDiffOn_succ_iff_fderiv_apply [FiniteDimensional ğ•œ D] (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ f s) âˆ§
      âˆ€ y, ContDiffOn ğ•œ n (fun x => fderivWithin ğ•œ f s x y) s := by
  rw [contDiffOn_succ_iff_fderivWithin hs, contDiffOn_clm_apply]

end FiniteDimensional
