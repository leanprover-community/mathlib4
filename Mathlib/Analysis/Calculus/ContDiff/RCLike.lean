/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Higher differentiability over `â„` or `â„‚`
-/

noncomputable section

open Set Fin Filter Function

open scoped NNReal Topology

section Real

/-!
### Results over `â„` or `â„‚`
  The results in this section rely on the Mean Value Theorem, and therefore hold only over `â„` (and
  its extension fields such as `â„‚`).
-/

variable {n : WithTop â„•âˆ} {ğ•‚ : Type*} [RCLike ğ•‚] {E' : Type*} [NormedAddCommGroup E']
  [NormedSpace ğ•‚ E'] {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ğ•‚ F']

/-- If a function has a Taylor series at order at least 1, then at points in the interior of the
domain of definition, the term of order 1 of this series is a strict derivative of `f`. -/
theorem HasFTaylorSeriesUpToOn.hasStrictFDerivAt {n : WithTop â„•âˆ}
    {s : Set E'} {f : E' â†’ F'} {x : E'}
    {p : E' â†’ FormalMultilinearSeries ğ•‚ E' F'} (hf : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hs : s âˆˆ ğ“ x) : HasStrictFDerivAt f ((continuousMultilinearCurryFin1 ğ•‚ E' F') (p x 1)) x :=
  hasStrictFDerivAt_of_hasFDerivAt_of_continuousAt (hf.eventually_hasFDerivAt hn hs) <|
    (continuousMultilinearCurryFin1 ğ•‚ E' F').continuousAt.comp <| (hf.cont 1 hn).continuousAt hs

/-- If a function is `C^n` with `1 â‰¤ n` around a point, and its derivative at that point is given to
us as `f'`, then `f'` is also a strict derivative. -/
theorem ContDiffAt.hasStrictFDerivAt' {f : E' â†’ F'} {f' : E' â†’L[ğ•‚] F'} {x : E'}
    (hf : ContDiffAt ğ•‚ n f x) (hf' : HasFDerivAt f f' x) (hn : 1 â‰¤ n) :
    HasStrictFDerivAt f f' x := by
  rcases hf.of_le hn 1 le_rfl with âŸ¨u, H, p, hpâŸ©
  simp only [nhdsWithin_univ, mem_univ, insert_eq_of_mem] at H
  have := hp.hasStrictFDerivAt le_rfl H
  rwa [hf'.unique this.hasFDerivAt]

/-- If a function is `C^n` with `1 â‰¤ n` around a point, and its derivative at that point is given to
us as `f'`, then `f'` is also a strict derivative. -/
theorem ContDiffAt.hasStrictDerivAt' {f : ğ•‚ â†’ F'} {f' : F'} {x : ğ•‚} (hf : ContDiffAt ğ•‚ n f x)
    (hf' : HasDerivAt f f' x) (hn : 1 â‰¤ n) : HasStrictDerivAt f f' x :=
  hf.hasStrictFDerivAt' hf' hn

/-- If a function is `C^n` with `1 â‰¤ n` around a point, then the derivative of `f` at this point
is also a strict derivative. -/
theorem ContDiffAt.hasStrictFDerivAt {f : E' â†’ F'} {x : E'} (hf : ContDiffAt ğ•‚ n f x) (hn : 1 â‰¤ n) :
    HasStrictFDerivAt f (fderiv ğ•‚ f x) x :=
  hf.hasStrictFDerivAt' (hf.differentiableAt hn).hasFDerivAt hn

/-- If a function is `C^n` with `1 â‰¤ n` around a point, then the derivative of `f` at this point
is also a strict derivative. -/
theorem ContDiffAt.hasStrictDerivAt {f : ğ•‚ â†’ F'} {x : ğ•‚} (hf : ContDiffAt ğ•‚ n f x) (hn : 1 â‰¤ n) :
    HasStrictDerivAt f (deriv f x) x :=
  (hf.hasStrictFDerivAt hn).hasStrictDerivAt

/-- If a function is `C^n` with `1 â‰¤ n`, then the derivative of `f` is also a strict derivative. -/
theorem ContDiff.hasStrictFDerivAt {f : E' â†’ F'} {x : E'} (hf : ContDiff ğ•‚ n f) (hn : 1 â‰¤ n) :
    HasStrictFDerivAt f (fderiv ğ•‚ f x) x :=
  hf.contDiffAt.hasStrictFDerivAt hn

/-- If a function is `C^n` with `1 â‰¤ n`, then the derivative of `f` is also a strict derivative. -/
theorem ContDiff.hasStrictDerivAt {f : ğ•‚ â†’ F'} {x : ğ•‚} (hf : ContDiff ğ•‚ n f) (hn : 1 â‰¤ n) :
    HasStrictDerivAt f (deriv f x) x :=
  hf.contDiffAt.hasStrictDerivAt hn

variable {E F : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E] [NormedAddCommGroup F] [NormedSpace â„ F] {f : E â†’ F}
    {p : E â†’ FormalMultilinearSeries â„ E F} {s : Set E} {x : E}

/-- If `f` has a formal Taylor series `p` up to order `1` on `{x} âˆª s`, where `s` is a convex set,
and `â€–p x 1â€–â‚Š < K`, then `f` is `K`-Lipschitz in a neighborhood of `x` within `s`. -/
theorem HasFTaylorSeriesUpToOn.exists_lipschitzOnWith_of_nnnorm_lt
    (hf : HasFTaylorSeriesUpToOn 1 f p (insert x s)) (hs : Convex â„ s) (K : â„â‰¥0)
    (hK : â€–p x 1â€–â‚Š < K) : âˆƒ t âˆˆ ğ“[s] x, LipschitzOnWith K f t := by
  set f' := fun y => continuousMultilinearCurryFin1 â„ E F (p y 1)
  have hder : âˆ€ y âˆˆ s, HasFDerivWithinAt f (f' y) s y := fun y hy =>
    (hf.hasFDerivWithinAt le_rfl (subset_insert x s hy)).mono (subset_insert x s)
  have hcont : ContinuousWithinAt f' s x :=
    (continuousMultilinearCurryFin1 â„ E F).continuousAt.comp_continuousWithinAt
      ((hf.cont _ le_rfl _ (mem_insert _ _)).mono (subset_insert x s))
  replace hK : â€–f' xâ€–â‚Š < K := by simpa only [f', LinearIsometryEquiv.nnnorm_map]
  exact
    hs.exists_nhdsWithin_lipschitzOnWith_of_hasFDerivWithinAt_of_nnnorm_lt
      (eventually_nhdsWithin_iff.2 <| Eventually.of_forall hder) hcont K hK

/-- If `f` has a formal Taylor series `p` up to order `1` on `{x} âˆª s`, where `s` is a convex set,
then `f` is Lipschitz in a neighborhood of `x` within `s`. -/
theorem HasFTaylorSeriesUpToOn.exists_lipschitzOnWith
    (hf : HasFTaylorSeriesUpToOn 1 f p (insert x s)) (hs : Convex â„ s) :
    âˆƒ K, âˆƒ t âˆˆ ğ“[s] x, LipschitzOnWith K f t :=
  (exists_gt _).imp <| hf.exists_lipschitzOnWith_of_nnnorm_lt hs

/-- If `f` is `C^1` within a convex set `s` at `x`, then it is Lipschitz on a neighborhood of `x`
within `s`. -/
theorem ContDiffWithinAt.exists_lipschitzOnWith
    (hf : ContDiffWithinAt â„ 1 f s x) (hs : Convex â„ s) :
    âˆƒ K : â„â‰¥0, âˆƒ t âˆˆ ğ“[s] x, LipschitzOnWith K f t := by
  rcases hf 1 le_rfl with âŸ¨t, hst, p, hpâŸ©
  rcases Metric.mem_nhdsWithin_iff.mp hst with âŸ¨Îµ, Îµ0, hÎµâŸ©
  replace hp : HasFTaylorSeriesUpToOn 1 f p (Metric.ball x Îµ âˆ© insert x s) := hp.mono hÎµ
  clear hst hÎµ t
  rw [â† insert_eq_of_mem (Metric.mem_ball_self Îµ0), â† insert_inter_distrib] at hp
  rcases hp.exists_lipschitzOnWith ((convex_ball _ _).inter hs) with âŸ¨K, t, hst, hftâŸ©
  rw [inter_comm, â† nhdsWithin_restrict' _ (Metric.ball_mem_nhds _ Îµ0)] at hst
  exact âŸ¨K, t, hst, hftâŸ©

/-- If `f` is `C^1` at `x` and `K > â€–fderiv ğ•‚ f xâ€–`, then `f` is `K`-Lipschitz in a neighborhood of
`x`. -/
theorem ContDiffAt.exists_lipschitzOnWith_of_nnnorm_lt {f : E' â†’ F'} {x : E'}
    (hf : ContDiffAt ğ•‚ 1 f x) (K : â„â‰¥0) (hK : â€–fderiv ğ•‚ f xâ€–â‚Š < K) :
    âˆƒ t âˆˆ ğ“ x, LipschitzOnWith K f t :=
  (hf.hasStrictFDerivAt le_rfl).exists_lipschitzOnWith_of_nnnorm_lt K hK

/-- If `f` is `C^1` at `x`, then `f` is Lipschitz in a neighborhood of `x`. -/
theorem ContDiffAt.exists_lipschitzOnWith {f : E' â†’ F'} {x : E'} (hf : ContDiffAt ğ•‚ 1 f x) :
    âˆƒ K, âˆƒ t âˆˆ ğ“ x, LipschitzOnWith K f t :=
  (hf.hasStrictFDerivAt le_rfl).exists_lipschitzOnWith

/-- If `f` is `CÂ¹` on a convex set `s`, it is locally Lipschitz on `s`. -/
lemma ContDiffOn.locallyLipschitzOn {f : E â†’ F} {s : Set E} (hs : Convex â„ s)
    (hf : ContDiffOn â„ 1 f s) : LocallyLipschitzOn s f := by
  intro x hx
  obtain âŸ¨K, t, ht, hfâŸ© := ContDiffWithinAt.exists_lipschitzOnWith (hf x hx) hs
  use K, t

/-- If `f` is `CÂ¹`, it is locally Lipschitz. -/
lemma ContDiff.locallyLipschitz {f : E' â†’ F'} (hf : ContDiff ğ•‚ 1 f) : LocallyLipschitz f := by
  intro x
  rcases hf.contDiffAt.exists_lipschitzOnWith with âŸ¨K, t, ht, hfâŸ©
  use K, t

-- TODO: find home
protected lemma LipschitzOnWith.closure {Î± Î² : Type*} [PseudoEMetricSpace Î±]
    [PseudoEMetricSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} {K : â„â‰¥0}
    (hcont : ContinuousOn f (closure s)) (hf : LipschitzOnWith K f s) :
    LipschitzOnWith K f (closure s) := by
  have := ENNReal.continuous_const_mul (ENNReal.coe_ne_top (r := K))
  refine fun x hx â†¦ le_on_closure (fun y hy â†¦ le_on_closure (fun x hx â†¦ hf hx hy) ?_ ?_ hx) ?_ ?_
  all_goals fun_prop

-- TODO: find home
open Metric in
/-- If `f` is locally Lipschitz on a compact set `s`, it is Lipschitz on `s`. -/
lemma LocallyLipschitzOn.exists_lipschitzOnWith_of_compact {Î± Î² : Type*} [PseudoMetricSpace Î±]
    [PseudoMetricSpace Î²] {f : Î± â†’ Î²} {s : Set Î±} (hs : IsCompact s)
    (hf : LocallyLipschitzOn s f) : âˆƒ K, LipschitzOnWith K f s := by
  have hf' := hf.continuousOn
  replace hf : âˆ€ x âˆˆ s, âˆƒ Îµ > 0, âˆƒ K, LipschitzOnWith K f (ball x Îµ âˆ© s) := fun x hx â†¦ by
    let âŸ¨K, t, ht, hfâŸ© := hf hx
    let âŸ¨Îµ, hÎµ, hÎµ'âŸ© := Metric.mem_nhdsWithin_iff.1 ht
    exact âŸ¨Îµ, hÎµ, K, hf.mono hÎµ'âŸ©
  choose Îµ hÎµ K hf using hf
  have : âˆ€ x (hx : x âˆˆ s), âˆƒ K' : â„â‰¥0, âˆ€ y âˆˆ s.diff (ball x (Îµ x hx)),
      edist (f x) (f y) â‰¤ K' * edist x y := fun x hx â†¦ by
    let âŸ¨K', hK'âŸ© := (hs.diff isOpen_ball).bddAbove_image
      (f := fun y â†¦ dist (f x) (f y) / dist x y) <| .div (.mono (by fun_prop) s.diff_subset)
        (by fun_prop) fun y hy â†¦ ((hÎµ x hx).trans_le <| not_lt.1 <| dist_comm x y â–¸ hy.2).ne.symm
    refine âŸ¨âŸ¨K' âŠ” 0, le_sup_rightâŸ©, fun y hy â†¦ ?_âŸ©
    rw [edist_nndist, edist_nndist, â† ENNReal.coe_mul, ENNReal.coe_le_coe]
    refine (div_le_iffâ‚€ ?_).1 ?_
    Â· exact NNReal.coe_pos.1 <| coe_nndist x y â–¸
        ((hÎµ x hx).trans_le <| not_lt.1 <| dist_comm x y â–¸ hy.2)
    Â· simp [â† NNReal.coe_le_coe, (mem_upperBounds.1 hK') _ <| Set.mem_image_of_mem _ hy]
  choose K' hK' using this
  obtain âŸ¨t, htâŸ© := hs.elim_nhdsWithin_subcover' (fun x hx â†¦ s âˆ© ball x (Îµ x hx / 2))
    (fun x hx â†¦ inter_mem_nhdsWithin s <| ball_mem_nhds x <| half_pos <| hÎµ x hx)
  use t.sup fun i â†¦ K _ i.2 + 2 * K' _ i.2
  intro x hx y hy
  let âŸ¨z, hz, hx'âŸ© := mem_iUnionâ‚‚.1 <| ht hx
  by_cases hy' : y âˆˆ ball z.1 (Îµ _ z.2)
  Â· refine (hf _ z.2 âŸ¨hx'.2.trans <| half_lt_self <| hÎµ _ z.2, hxâŸ© âŸ¨hy', hyâŸ©).trans <|
      mul_le_mul_of_nonneg_right ?_ <| zero_le _
    exact ENNReal.coe_le_coe.2 <| t.le_sup_of_le hz <| le_self_add
  Â· refine (edist_triangle_left _ _ (f z)).trans ?_
    refine .trans ?_ <| mul_le_mul_of_nonneg_right (ENNReal.coe_le_coe.2 <| t.le_sup hz) (zero_le _)
    refine (add_le_add (hf _ z.2 âŸ¨mem_ball_self <| hÎµ _ z.2, z.2âŸ© âŸ¨hx'.2.trans <| half_lt_self <|
      hÎµ _ z.2, hxâŸ©) <| hK' _ z.2 _ âŸ¨hy, hy'âŸ©).trans ?_
    refine (add_le_add_left (mul_le_mul_of_nonneg_left (edist_triangle_left z.1 y x)
      (zero_le _)) _).trans ?_
    simp_rw [edist_dist, ENNReal.coe_nnreal_eq]
    rw [â† ENNReal.ofReal_mul, â† ENNReal.ofReal_mul, â† ENNReal.ofReal_add, â† ENNReal.ofReal_mul,
      â† ENNReal.ofReal_add, NNReal.coe_add, NNReal.coe_mul, NNReal.coe_ofNat,
      mul_add, â† add_assoc, dist_comm, â† add_mul]
    Â· have h : dist x z â‰¤ dist x y := by
        linarith [mem_ball.1 hx'.2, (not_lt (Î± := â„)).1 hy', dist_triangle_left y z x]
      apply ENNReal.ofReal_le_ofReal
      refine (add_le_add_right (mul_le_mul_of_nonneg_left h (by positivity)) _).trans ?_
      linarith
    all_goals positivity

/-- If `f` is `CÂ¹` on a convex compact set `s`, it is Lipschitz on `s`. -/
theorem ContDiffOn.exists_lipschitzOnWith {s : Set E} {f : E â†’ F} {n} (hf : ContDiffOn â„ n f s)
    (hn : 1 â‰¤ n) (hs : Convex â„ s) (hs' : IsCompact s) :
    âˆƒ K, LipschitzOnWith K f s := by
  apply LocallyLipschitzOn.exists_lipschitzOnWith_of_compact hs'
  apply (hf.of_le hn).locallyLipschitzOn hs

/-- A `C^n` function with compact support is Lipschitz. -/
theorem ContDiff.lipschitzWith_of_hasCompactSupport {f : E' â†’ F'}
    (hf : HasCompactSupport f) (h'f : ContDiff ğ•‚ n f) (hn : 1 â‰¤ n) :
    âˆƒ C, LipschitzWith C f := by
  obtain âŸ¨C, hCâŸ© := (hf.fderiv ğ•‚).exists_bound_of_continuous (h'f.continuous_fderiv hn)
  refine âŸ¨âŸ¨max C 0, le_max_right _ _âŸ©, ?_âŸ©
  apply lipschitzWith_of_nnnorm_fderiv_le (h'f.differentiable hn) (fun x â†¦ ?_)
  simp [â† NNReal.coe_le_coe, hC x]

end Real
