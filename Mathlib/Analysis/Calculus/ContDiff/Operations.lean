/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Analysis.Calculus.Deriv.Inverse

/-!
# Higher differentiability of usual operations

We prove that the usual operations (addition, multiplication, difference, composition, and
so on) preserve `C^n` functions.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `(âŠ¤ : â„•âˆ) : WithTop â„•âˆ` with `âˆ` and `âŠ¤ : WithTop â„•âˆ` with `Ï‰`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

open scoped NNReal Nat ContDiff

universe u uE uF uG

attribute [local instance 1001]
  NormedAddCommGroup.toAddCommGroup AddCommGroup.toAddCommMonoid

open Set Fin Filter Function

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type uE} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {F : Type uF}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {X : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X] {s t : Set E} {f : E â†’ F}
  {g : F â†’ G} {x xâ‚€ : E} {b : E Ã— F â†’ G} {m n : WithTop â„•âˆ} {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-!
### Smoothness of functions `f : E â†’ Î  i, F' i`
-/

section Pi

variable {Î¹ Î¹' : Type*} [Fintype Î¹] [Fintype Î¹'] {F' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (F' i)]
  [âˆ€ i, NormedSpace ğ•œ (F' i)] {Ï† : âˆ€ i, E â†’ F' i} {p' : âˆ€ i, E â†’ FormalMultilinearSeries ğ•œ E (F' i)}
  {Î¦ : E â†’ âˆ€ i, F' i} {P' : E â†’ FormalMultilinearSeries ğ•œ E (âˆ€ i, F' i)}

theorem hasFTaylorSeriesUpToOn_pi {n : WithTop â„•âˆ} :
    HasFTaylorSeriesUpToOn n (fun x i => Ï† i x)
        (fun x m => ContinuousMultilinearMap.pi fun i => p' i x m) s â†”
      âˆ€ i, HasFTaylorSeriesUpToOn n (Ï† i) (p' i) s := by
  set pr := @ContinuousLinearMap.proj ğ•œ _ Î¹ F' _ _ _
  set L : âˆ€ m : â„•, (âˆ€ i, E[Ã—m]â†’L[ğ•œ] F' i) â‰ƒâ‚—áµ¢[ğ•œ] E[Ã—m]â†’L[ğ•œ] âˆ€ i, F' i := fun m =>
    ContinuousMultilinearMap.piâ‚—áµ¢ _ _
  refine âŸ¨fun h i => ?_, fun h => âŸ¨fun x hx => ?_, ?_, ?_âŸ©âŸ©
  Â· exact h.continuousLinearMap_comp (pr i)
  Â· ext1 i
    exact (h i).zero_eq x hx
  Â· intro m hm x hx
    exact (L m).hasFDerivAt.comp_hasFDerivWithinAt x <|
      hasFDerivWithinAt_pi.2 fun i => (h i).fderivWithin m hm x hx
  Â· intro m hm
    exact (L m).continuous.comp_continuousOn <| continuousOn_pi.2 fun i => (h i).cont m hm

@[simp]
theorem hasFTaylorSeriesUpToOn_pi' {n : WithTop â„•âˆ} :
    HasFTaylorSeriesUpToOn n Î¦ P' s â†”
      âˆ€ i, HasFTaylorSeriesUpToOn n (fun x => Î¦ x i)
        (fun x m => (@ContinuousLinearMap.proj ğ•œ _ Î¹ F' _ _ _ i).compContinuousMultilinearMap
          (P' x m)) s := by
  convert hasFTaylorSeriesUpToOn_pi (ğ•œ := ğ•œ) (Ï† := fun i x â†¦ Î¦ x i); ext; rfl

theorem contDiffWithinAt_pi :
    ContDiffWithinAt ğ•œ n Î¦ s x â†” âˆ€ i, ContDiffWithinAt ğ•œ n (fun x => Î¦ x i) s x := by
  set pr := @ContinuousLinearMap.proj ğ•œ _ Î¹ F' _ _ _
  refine âŸ¨fun h i => h.continuousLinearMap_comp (pr i), fun h â†¦ ?_âŸ©
  match n with
  | Ï‰ =>
    choose u hux p hp h'p using h
    refine âŸ¨â‹‚ i, u i, Filter.iInter_mem.2 hux, _,
      hasFTaylorSeriesUpToOn_pi.2 fun i => (hp i).mono <| iInter_subset _ _, fun m â†¦ ?_âŸ©
    set L : (âˆ€ i, E[Ã—m]â†’L[ğ•œ] F' i) â‰ƒâ‚—áµ¢[ğ•œ] E[Ã—m]â†’L[ğ•œ] âˆ€ i, F' i :=
      ContinuousMultilinearMap.piâ‚—áµ¢ _ _
    change AnalyticOn ğ•œ (fun x â†¦ L (fun i â†¦ p i x m)) (â‹‚ i, u i)
    apply (L.analyticOnNhd univ).comp_analyticOn ?_ (mapsTo_univ _ _)
    exact AnalyticOn.pi (fun i â†¦ (h'p i m).mono (iInter_subset _ _))
  | (n : â„•âˆ) =>
    intro m hm
    choose u hux p hp using fun i => h i m hm
    exact âŸ¨â‹‚ i, u i, Filter.iInter_mem.2 hux, _,
      hasFTaylorSeriesUpToOn_pi.2 fun i => (hp i).mono <| iInter_subset _ _âŸ©

theorem contDiffOn_pi : ContDiffOn ğ•œ n Î¦ s â†” âˆ€ i, ContDiffOn ğ•œ n (fun x => Î¦ x i) s :=
  âŸ¨fun h _ x hx => contDiffWithinAt_pi.1 (h x hx) _, fun h x hx =>
    contDiffWithinAt_pi.2 fun i => h i x hxâŸ©

theorem contDiffAt_pi : ContDiffAt ğ•œ n Î¦ x â†” âˆ€ i, ContDiffAt ğ•œ n (fun x => Î¦ x i) x :=
  contDiffWithinAt_pi

theorem contDiff_pi : ContDiff ğ•œ n Î¦ â†” âˆ€ i, ContDiff ğ•œ n fun x => Î¦ x i := by
  simp only [â† contDiffOn_univ, contDiffOn_pi]

@[fun_prop]
theorem contDiff_pi' (hÎ¦ : âˆ€ i, ContDiff ğ•œ n fun x => Î¦ x i) : ContDiff ğ•œ n Î¦ :=
  contDiff_pi.2 hÎ¦

@[fun_prop]
theorem contDiffOn_pi' (hÎ¦ : âˆ€ i, ContDiffOn ğ•œ n (fun x => Î¦ x i) s) : ContDiffOn ğ•œ n Î¦ s :=
  contDiffOn_pi.2 hÎ¦

@[fun_prop]
theorem contDiffAt_pi' (hÎ¦ : âˆ€ i, ContDiffAt ğ•œ n (fun x => Î¦ x i) x) : ContDiffAt ğ•œ n Î¦ x :=
  contDiffAt_pi.2 hÎ¦

theorem contDiff_update [DecidableEq Î¹] (k : WithTop â„•âˆ) (x : âˆ€ i, F' i) (i : Î¹) :
    ContDiff ğ•œ k (update x i) := by
  rw [contDiff_pi]
  intro j
  dsimp [Function.update]
  split_ifs with h
  Â· subst h
    exact contDiff_id
  Â· exact contDiff_const

variable (F') in
theorem contDiff_single [DecidableEq Î¹] (k : WithTop â„•âˆ) (i : Î¹) :
    ContDiff ğ•œ k (Pi.single i : F' i â†’ âˆ€ i, F' i) :=
  contDiff_update k 0 i

variable (ğ•œ E)

@[fun_prop]
theorem contDiff_apply (i : Î¹) : ContDiff ğ•œ n fun f : Î¹ â†’ E => f i :=
  contDiff_pi.mp contDiff_id i

@[fun_prop]
theorem contDiffAt_apply (i : Î¹) (f : Î¹ â†’ E) : ContDiffAt ğ•œ n (fun f : Î¹ â†’ E => f i) f :=
  (contDiff_apply ğ•œ E i).contDiffAt

@[fun_prop]
theorem contDiffOn_apply (i : Î¹) (s : Set (Î¹ â†’ E)) : ContDiffOn ğ•œ n (fun f : Î¹ â†’ E => f i) s :=
  (contDiff_apply ğ•œ E i).contDiffOn

theorem contDiff_apply_apply (i : Î¹) (j : Î¹') : ContDiff ğ•œ n fun f : Î¹ â†’ Î¹' â†’ E => f i j :=
  contDiff_pi.mp (contDiff_apply ğ•œ (Î¹' â†’ E) i) j

end Pi

/-! ### Sum of two functions -/

section Add

theorem HasFTaylorSeriesUpToOn.add {n : WithTop â„•âˆ} {q g} (hf : HasFTaylorSeriesUpToOn n f p s)
    (hg : HasFTaylorSeriesUpToOn n g q s) : HasFTaylorSeriesUpToOn n (f + g) (p + q) s := by
  exact HasFTaylorSeriesUpToOn.continuousLinearMap_comp
    (ContinuousLinearMap.fst ğ•œ F F + .snd ğ•œ F F) (hf.prodMk hg)

-- The sum is smooth.
@[fun_prop]
theorem contDiff_add : ContDiff ğ•œ n fun p : F Ã— F => p.1 + p.2 :=
  (IsBoundedLinearMap.fst.add IsBoundedLinearMap.snd).contDiff

/-- The sum of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
@[fun_prop]
theorem ContDiffWithinAt.add {s : Set E} {f g : E â†’ F} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) : ContDiffWithinAt ğ•œ n (fun x => f x + g x) s x :=
  contDiff_add.contDiffWithinAt.comp x (hf.prodMk hg) subset_preimage_univ

/-- The sum of two `C^n` functions at a point is `C^n` at this point. -/
@[fun_prop]
theorem ContDiffAt.add {f g : E â†’ F} (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    ContDiffAt ğ•œ n (fun x => f x + g x) x := by
  rw [â† contDiffWithinAt_univ] at *; exact hf.add hg

/-- The sum of two `C^n`functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.add {f g : E â†’ F} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x => f x + g x :=
  contDiff_add.comp (hf.prodMk hg)

/-- The sum of two `C^n` functions on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.add {s : Set E} {f g : E â†’ F} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x => f x + g x) s := fun x hx =>
  (hf x hx).add (hg x hx)

variable {i : â„•}

/-- The iterated derivative of the sum of two functions is the sum of the iterated derivatives.
See also `iteratedFDerivWithin_add_apply'`, which uses the spelling `(fun x â†¦ f x + g x)`
instead of `f + g`. -/
theorem iteratedFDerivWithin_add_apply {f g : E â†’ F} (hf : ContDiffWithinAt ğ•œ i f s x)
    (hg : ContDiffWithinAt ğ•œ i g s x) (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ i (f + g) s x =
      iteratedFDerivWithin ğ•œ i f s x + iteratedFDerivWithin ğ•œ i g s x := by
  have := (hf.eventually (by simp)).and (hg.eventually (by simp))
  obtain âŸ¨t, ht, hxt, hâŸ© := mem_nhdsWithin.mp this
  have hft : ContDiffOn ğ•œ i f (s âˆ© t) := fun a ha â†¦ (h (by simp_all)).1.mono inter_subset_left
  have hgt : ContDiffOn ğ•œ i g (s âˆ© t) := fun a ha â†¦ (h (by simp_all)).2.mono inter_subset_left
  have hut : UniqueDiffOn ğ•œ (s âˆ© t) := hu.inter ht
  have H : â†‘(s âˆ© t) =á¶ [ğ“ x] s :=
    inter_eventuallyEq_left.mpr (eventually_of_mem (ht.mem_nhds hxt) (fun _ h _ â†¦ h))
  rw [â† iteratedFDerivWithin_congr_set H, â† iteratedFDerivWithin_congr_set H,
    â† iteratedFDerivWithin_congr_set H]
  exact .symm (((hft.ftaylorSeriesWithin hut).add
      (hgt.ftaylorSeriesWithin hut)).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl hut âŸ¨hx, hxtâŸ©)

/-- The iterated derivative of the sum of two functions is the sum of the iterated derivatives.
This is the same as `iteratedFDerivWithin_add_apply`, but using the spelling `(fun x â†¦ f x + g x)`
instead of `f + g`, which can be handy for some rewrites.
TODO: use one form consistently. -/
theorem iteratedFDerivWithin_add_apply' {f g : E â†’ F} (hf : ContDiffWithinAt ğ•œ i f s x)
    (hg : ContDiffWithinAt ğ•œ i g s x) (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ i (fun x => f x + g x) s x =
      iteratedFDerivWithin ğ•œ i f s x + iteratedFDerivWithin ğ•œ i g s x :=
  iteratedFDerivWithin_add_apply hf hg hu hx

theorem iteratedFDeriv_add_apply {i : â„•} {f g : E â†’ F}
    (hf : ContDiffAt ğ•œ i f x) (hg : ContDiffAt ğ•œ i g x) :
    iteratedFDeriv ğ•œ i (f + g) x = iteratedFDeriv ğ•œ i f x + iteratedFDeriv ğ•œ i g x := by
  simp_rw [â† iteratedFDerivWithin_univ]
  exact iteratedFDerivWithin_add_apply hf hg uniqueDiffOn_univ (Set.mem_univ _)

theorem iteratedFDeriv_add_apply' {i : â„•} {f g : E â†’ F} (hf : ContDiffAt ğ•œ i f x)
    (hg : ContDiffAt ğ•œ i g x) :
    iteratedFDeriv ğ•œ i (fun x => f x + g x) x = iteratedFDeriv ğ•œ i f x + iteratedFDeriv ğ•œ i g x :=
  iteratedFDeriv_add_apply hf hg

end Add

/-! ### Negative -/

section Neg

-- The negative is smooth.
@[fun_prop]
theorem contDiff_neg : ContDiff ğ•œ n fun p : F => -p :=
  IsBoundedLinearMap.id.neg.contDiff

/-- The negative of a `C^n` function within a domain at a point is `C^n` within this domain at
this point. -/
@[fun_prop]
theorem ContDiffWithinAt.neg {s : Set E} {f : E â†’ F} (hf : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n (fun x => -f x) s x :=
  contDiff_neg.contDiffWithinAt.comp x hf subset_preimage_univ

/-- The negative of a `C^n` function at a point is `C^n` at this point. -/
@[fun_prop]
theorem ContDiffAt.neg {f : E â†’ F} (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x => -f x) x := by rw [â† contDiffWithinAt_univ] at *; exact hf.neg

/-- The negative of a `C^n`function is `C^n`. -/
@[fun_prop]
theorem ContDiff.neg {f : E â†’ F} (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x => -f x :=
  contDiff_neg.comp hf

/-- The negative of a `C^n` function on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.neg {s : Set E} {f : E â†’ F} (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (fun x => -f x) s := fun x hx => (hf x hx).neg

variable {i : â„•}

-- TODO: define `Neg` instance on `ContinuousLinearEquiv`,
-- prove it from `ContinuousLinearEquiv.iteratedFDerivWithin_comp_left`
theorem iteratedFDerivWithin_neg_apply {f : E â†’ F} (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ i (-f) s x = -iteratedFDerivWithin ğ•œ i f s x := by
  induction' i with i hi generalizing x
  Â· ext; simp
  Â· ext h
    calc
      iteratedFDerivWithin ğ•œ (i + 1) (-f) s x h =
          fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i (-f) s) s x (h 0) (Fin.tail h) :=
        rfl
      _ = fderivWithin ğ•œ (-iteratedFDerivWithin ğ•œ i f s) s x (h 0) (Fin.tail h) := by
        rw [fderivWithin_congr' (@hi) hx]; rfl
      _ = -(fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i f s) s) x (h 0) (Fin.tail h) := by
        rw [fderivWithin_neg (hu x hx)]; rfl
      _ = -(iteratedFDerivWithin ğ•œ (i + 1) f s) x h := rfl

theorem iteratedFDeriv_neg_apply {i : â„•} {f : E â†’ F} :
    iteratedFDeriv ğ•œ i (-f) x = -iteratedFDeriv ğ•œ i f x := by
  simp_rw [â† iteratedFDerivWithin_univ]
  exact iteratedFDerivWithin_neg_apply uniqueDiffOn_univ (Set.mem_univ _)

end Neg

/-! ### Subtraction -/

/-- The difference of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
@[fun_prop]
theorem ContDiffWithinAt.sub {s : Set E} {f g : E â†’ F} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) : ContDiffWithinAt ğ•œ n (fun x => f x - g x) s x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions at a point is `C^n` at this point. -/
@[fun_prop]
theorem ContDiffAt.sub {f g : E â†’ F} (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    ContDiffAt ğ•œ n (fun x => f x - g x) x := by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.sub {s : Set E} {f g : E â†’ F} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x => f x - g x) s := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

/-- The difference of two `C^n` functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.sub {f g : E â†’ F} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x => f x - g x := by simpa only [sub_eq_add_neg] using hf.add hg.neg

/-! ### Sum of finitely many functions -/

@[fun_prop]
theorem ContDiffWithinAt.sum {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : Finset Î¹} {t : Set E} {x : E}
    (h : âˆ€ i âˆˆ s, ContDiffWithinAt ğ•œ n (fun x => f i x) t x) :
    ContDiffWithinAt ğ•œ n (fun x => âˆ‘ i âˆˆ s, f i x) t x := by
  classical
    induction' s using Finset.induction_on with i s is IH
    Â· simp [contDiffWithinAt_const]
    Â· simp only [is, Finset.sum_insert, not_false_iff]
      exact (h _ (Finset.mem_insert_self i s)).add
        (IH fun j hj => h _ (Finset.mem_insert_of_mem hj))

@[fun_prop]
theorem ContDiffAt.sum {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : Finset Î¹} {x : E}
    (h : âˆ€ i âˆˆ s, ContDiffAt ğ•œ n (fun x => f i x) x) :
    ContDiffAt ğ•œ n (fun x => âˆ‘ i âˆˆ s, f i x) x := by
  rw [â† contDiffWithinAt_univ] at *; exact ContDiffWithinAt.sum h

@[fun_prop]
theorem ContDiffOn.sum {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : Finset Î¹} {t : Set E}
    (h : âˆ€ i âˆˆ s, ContDiffOn ğ•œ n (fun x => f i x) t) :
    ContDiffOn ğ•œ n (fun x => âˆ‘ i âˆˆ s, f i x) t := fun x hx =>
  ContDiffWithinAt.sum fun i hi => h i hi x hx

@[fun_prop]
theorem ContDiff.sum {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {s : Finset Î¹}
    (h : âˆ€ i âˆˆ s, ContDiff ğ•œ n fun x => f i x) : ContDiff ğ•œ n fun x => âˆ‘ i âˆˆ s, f i x := by
  simp only [â† contDiffOn_univ] at *; exact ContDiffOn.sum h

theorem iteratedFDerivWithin_sum_apply {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {u : Finset Î¹} {i : â„•} {x : E}
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (h : âˆ€ j âˆˆ u, ContDiffWithinAt ğ•œ i (f j) s x) :
    iteratedFDerivWithin ğ•œ i (âˆ‘ j âˆˆ u, f j Â·) s x =
      âˆ‘ j âˆˆ u, iteratedFDerivWithin ğ•œ i (f j) s x := by
  induction u using Finset.cons_induction with
  | empty => ext; simp [hs, hx]
  | cons a u ha IH =>
    simp only [Finset.mem_cons, forall_eq_or_imp] at h
    simp only [Finset.sum_cons]
    rw [iteratedFDerivWithin_add_apply' h.1 (ContDiffWithinAt.sum h.2) hs hx, IH h.2]

theorem iteratedFDeriv_sum {Î¹ : Type*} {f : Î¹ â†’ E â†’ F} {u : Finset Î¹} {i : â„•}
    (h : âˆ€ j âˆˆ u, ContDiff ğ•œ i (f j)) :
    iteratedFDeriv ğ•œ i (âˆ‘ j âˆˆ u, f j Â·) = âˆ‘ j âˆˆ u, iteratedFDeriv ğ•œ i (f j) :=
  funext fun x â†¦ by simpa [iteratedFDerivWithin_univ] using
    iteratedFDerivWithin_sum_apply uniqueDiffOn_univ (mem_univ x) (h Â· Â· |>.contDiffWithinAt)

/-! ### Product of two functions -/

section MulProd

variable {ğ”¸ ğ”¸' Î¹ ğ•œ' : Type*} [NormedRing ğ”¸] [NormedAlgebra ğ•œ ğ”¸] [NormedCommRing ğ”¸']
  [NormedAlgebra ğ•œ ğ”¸'] [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']

-- The product is smooth.
@[fun_prop]
theorem contDiff_mul : ContDiff ğ•œ n fun p : ğ”¸ Ã— ğ”¸ => p.1 * p.2 :=
  (ContinuousLinearMap.mul ğ•œ ğ”¸).isBoundedBilinearMap.contDiff

/-- The product of two `C^n` functions within a set at a point is `C^n` within this set
at this point. -/
@[fun_prop]
theorem ContDiffWithinAt.mul {s : Set E} {f g : E â†’ ğ”¸} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) : ContDiffWithinAt ğ•œ n (fun x => f x * g x) s x :=
  contDiff_mul.comp_contDiffWithinAt (hf.prodMk hg)

/-- The product of two `C^n` functions at a point is `C^n` at this point. -/
@[fun_prop]
nonrec theorem ContDiffAt.mul {f g : E â†’ ğ”¸} (hf : ContDiffAt ğ•œ n f x) (hg : ContDiffAt ğ•œ n g x) :
    ContDiffAt ğ•œ n (fun x => f x * g x) x :=
  hf.mul hg

/-- The product of two `C^n` functions on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.mul {f g : E â†’ ğ”¸} (hf : ContDiffOn ğ•œ n f s) (hg : ContDiffOn ğ•œ n g s) :
    ContDiffOn ğ•œ n (fun x => f x * g x) s := fun x hx => (hf x hx).mul (hg x hx)

/-- The product of two `C^n`functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.mul {f g : E â†’ ğ”¸} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x => f x * g x :=
  contDiff_mul.comp (hf.prodMk hg)

@[fun_prop]
theorem contDiffWithinAt_prod' {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'}
    (h : âˆ€ i âˆˆ t, ContDiffWithinAt ğ•œ n (f i) s x) : ContDiffWithinAt ğ•œ n (âˆ i âˆˆ t, f i) s x :=
  Finset.prod_induction f (fun f => ContDiffWithinAt ğ•œ n f s x) (fun _ _ => ContDiffWithinAt.mul)
    (contDiffWithinAt_const (c := 1)) h

@[fun_prop]
theorem contDiffWithinAt_prod {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'}
    (h : âˆ€ i âˆˆ t, ContDiffWithinAt ğ•œ n (f i) s x) :
    ContDiffWithinAt ğ•œ n (fun y => âˆ i âˆˆ t, f i y) s x := by
  simpa only [â† Finset.prod_apply] using contDiffWithinAt_prod' h

@[fun_prop]
theorem contDiffAt_prod' {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiffAt ğ•œ n (f i) x) :
    ContDiffAt ğ•œ n (âˆ i âˆˆ t, f i) x :=
  contDiffWithinAt_prod' h

@[fun_prop]
theorem contDiffAt_prod {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiffAt ğ•œ n (f i) x) :
    ContDiffAt ğ•œ n (fun y => âˆ i âˆˆ t, f i y) x :=
  contDiffWithinAt_prod h

@[fun_prop]
theorem contDiffOn_prod' {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiffOn ğ•œ n (f i) s) :
    ContDiffOn ğ•œ n (âˆ i âˆˆ t, f i) s := fun x hx => contDiffWithinAt_prod' fun i hi => h i hi x hx

@[fun_prop]
theorem contDiffOn_prod {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiffOn ğ•œ n (f i) s) :
    ContDiffOn ğ•œ n (fun y => âˆ i âˆˆ t, f i y) s := fun x hx =>
  contDiffWithinAt_prod fun i hi => h i hi x hx

@[fun_prop]
theorem contDiff_prod' {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiff ğ•œ n (f i)) :
    ContDiff ğ•œ n (âˆ i âˆˆ t, f i) :=
  contDiff_iff_contDiffAt.mpr fun _ => contDiffAt_prod' fun i hi => (h i hi).contDiffAt

@[fun_prop]
theorem contDiff_prod {t : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸'} (h : âˆ€ i âˆˆ t, ContDiff ğ•œ n (f i)) :
    ContDiff ğ•œ n fun y => âˆ i âˆˆ t, f i y :=
  contDiff_iff_contDiffAt.mpr fun _ => contDiffAt_prod fun i hi => (h i hi).contDiffAt

@[fun_prop]
theorem ContDiff.pow {f : E â†’ ğ”¸} (hf : ContDiff ğ•œ n f) : âˆ€ m : â„•, ContDiff ğ•œ n fun x => f x ^ m
  | 0 => by simpa using contDiff_const
  | m + 1 => by simpa [pow_succ] using (hf.pow m).mul hf

@[fun_prop]
theorem ContDiffWithinAt.pow {f : E â†’ ğ”¸} (hf : ContDiffWithinAt ğ•œ n f s x) (m : â„•) :
    ContDiffWithinAt ğ•œ n (fun y => f y ^ m) s x :=
  (contDiff_id.pow m).comp_contDiffWithinAt hf

@[fun_prop]
nonrec theorem ContDiffAt.pow {f : E â†’ ğ”¸} (hf : ContDiffAt ğ•œ n f x) (m : â„•) :
    ContDiffAt ğ•œ n (fun y => f y ^ m) x :=
  hf.pow m

@[fun_prop]
theorem ContDiffOn.pow {f : E â†’ ğ”¸} (hf : ContDiffOn ğ•œ n f s) (m : â„•) :
    ContDiffOn ğ•œ n (fun y => f y ^ m) s := fun y hy => (hf y hy).pow m

@[fun_prop]
theorem ContDiffWithinAt.div_const {f : E â†’ ğ•œ'} {n} (hf : ContDiffWithinAt ğ•œ n f s x) (c : ğ•œ') :
    ContDiffWithinAt ğ•œ n (fun x => f x / c) s x := by
  simpa only [div_eq_mul_inv] using hf.mul contDiffWithinAt_const

@[fun_prop]
nonrec theorem ContDiffAt.div_const {f : E â†’ ğ•œ'} {n} (hf : ContDiffAt ğ•œ n f x) (c : ğ•œ') :
    ContDiffAt ğ•œ n (fun x => f x / c) x :=
  hf.div_const c

@[fun_prop]
theorem ContDiffOn.div_const {f : E â†’ ğ•œ'} {n} (hf : ContDiffOn ğ•œ n f s) (c : ğ•œ') :
    ContDiffOn ğ•œ n (fun x => f x / c) s := fun x hx => (hf x hx).div_const c

@[fun_prop]
theorem ContDiff.div_const {f : E â†’ ğ•œ'} {n} (hf : ContDiff ğ•œ n f) (c : ğ•œ') :
    ContDiff ğ•œ n fun x => f x / c := by simpa only [div_eq_mul_inv] using hf.mul contDiff_const


end MulProd

/-! ### Scalar multiplication -/

section SMul

-- The scalar multiplication is smooth.
@[fun_prop]
theorem contDiff_smul : ContDiff ğ•œ n fun p : ğ•œ Ã— F => p.1 â€¢ p.2 :=
  isBoundedBilinearMap_smul.contDiff

/-- The scalar multiplication of two `C^n` functions within a set at a point is `C^n` within this
set at this point. -/
@[fun_prop]
theorem ContDiffWithinAt.smul {s : Set E} {f : E â†’ ğ•œ} {g : E â†’ F} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) : ContDiffWithinAt ğ•œ n (fun x => f x â€¢ g x) s x :=
  contDiff_smul.contDiffWithinAt.comp x (hf.prodMk hg) subset_preimage_univ

/-- The scalar multiplication of two `C^n` functions at a point is `C^n` at this point. -/
@[fun_prop]
theorem ContDiffAt.smul {f : E â†’ ğ•œ} {g : E â†’ F} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) : ContDiffAt ğ•œ n (fun x => f x â€¢ g x) x := by
  rw [â† contDiffWithinAt_univ] at *; exact hf.smul hg

/-- The scalar multiplication of two `C^n` functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.smul {f : E â†’ ğ•œ} {g : E â†’ F} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x => f x â€¢ g x :=
  contDiff_smul.comp (hf.prodMk hg)

/-- The scalar multiplication of two `C^n` functions on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.smul {s : Set E} {f : E â†’ ğ•œ} {g : E â†’ F} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x => f x â€¢ g x) s := fun x hx =>
  (hf x hx).smul (hg x hx)

end SMul

/-! ### Constant scalar multiplication

TODO: generalize results in this section.

1. It should be possible to assume `[Monoid R] [DistribMulAction R F] [SMulCommClass ğ•œ R F]`.
2. If `c` is a unit (or `R` is a group), then one can drop `ContDiff*` assumptions in some
  lemmas.
-/

section ConstSMul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ğ•œ R F]
variable [ContinuousConstSMul R F]

-- The scalar multiplication with a constant is smooth.
@[fun_prop]
theorem contDiff_const_smul (c : R) : ContDiff ğ•œ n fun p : F => c â€¢ p :=
  (c â€¢ ContinuousLinearMap.id ğ•œ F).contDiff

/-- The scalar multiplication of a constant and a `C^n` function within a set at a point is `C^n`
within this set at this point. -/
@[fun_prop]
theorem ContDiffWithinAt.const_smul {s : Set E} {f : E â†’ F} {x : E} (c : R)
    (hf : ContDiffWithinAt ğ•œ n f s x) : ContDiffWithinAt ğ•œ n (fun y => c â€¢ f y) s x :=
  (contDiff_const_smul c).contDiffAt.comp_contDiffWithinAt x hf

/-- The scalar multiplication of a constant and a `C^n` function at a point is `C^n` at this
point. -/
@[fun_prop]
theorem ContDiffAt.const_smul {f : E â†’ F} {x : E} (c : R) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun y => c â€¢ f y) x := by
  rw [â† contDiffWithinAt_univ] at *; exact hf.const_smul c

/-- The scalar multiplication of a constant and a `C^n` function is `C^n`. -/
@[fun_prop]
theorem ContDiff.const_smul {f : E â†’ F} (c : R) (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n fun y => c â€¢ f y :=
  (contDiff_const_smul c).comp hf

/-- The scalar multiplication of a constant and a `C^n` on a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.const_smul {s : Set E} {f : E â†’ F} (c : R) (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (fun y => c â€¢ f y) s := fun x hx => (hf x hx).const_smul c

variable {i : â„•} {a : R}

theorem iteratedFDerivWithin_const_smul_apply (hf : ContDiffWithinAt ğ•œ i f s x)
    (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ i (a â€¢ f) s x = a â€¢ iteratedFDerivWithin ğ•œ i f s x :=
  (a â€¢ (1 : F â†’L[ğ•œ] F)).iteratedFDerivWithin_comp_left hf hu hx le_rfl

theorem iteratedFDeriv_const_smul_apply (hf : ContDiffAt ğ•œ i f x) :
    iteratedFDeriv ğ•œ i (a â€¢ f) x = a â€¢ iteratedFDeriv ğ•œ i f x :=
  (a â€¢ (1 : F â†’L[ğ•œ] F)).iteratedFDeriv_comp_left hf le_rfl

theorem iteratedFDeriv_const_smul_apply' (hf : ContDiffAt ğ•œ i f x) :
    iteratedFDeriv ğ•œ i (fun x â†¦ a â€¢ f x) x = a â€¢ iteratedFDeriv ğ•œ i f x :=
  iteratedFDeriv_const_smul_apply hf

end ConstSMul

/-! ### Cartesian product of two functions -/

section prodMap

variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
variable {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ğ•œ F']

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
@[fun_prop]
theorem ContDiffWithinAt.prodMap' {s : Set E} {t : Set E'} {f : E â†’ F} {g : E' â†’ F'} {p : E Ã— E'}
    (hf : ContDiffWithinAt ğ•œ n f s p.1) (hg : ContDiffWithinAt ğ•œ n g t p.2) :
    ContDiffWithinAt ğ•œ n (Prod.map f g) (s Ã—Ë¢ t) p :=
  (hf.comp p contDiffWithinAt_fst (prod_subset_preimage_fst _ _)).prodMk
    (hg.comp p contDiffWithinAt_snd (prod_subset_preimage_snd _ _))

@[deprecated (since := "2025-03-09")]
alias ContDiffWithinAt.prod_map' := ContDiffWithinAt.prodMap'

@[fun_prop]
theorem ContDiffWithinAt.prodMap {s : Set E} {t : Set E'} {f : E â†’ F} {g : E' â†’ F'} {x : E} {y : E'}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g t y) :
    ContDiffWithinAt ğ•œ n (Prod.map f g) (s Ã—Ë¢ t) (x, y) :=
  ContDiffWithinAt.prodMap' hf hg

@[deprecated (since := "2025-03-09")]
alias ContDiffWithinAt.prod_map := ContDiffWithinAt.prodMap

/-- The product map of two `C^n` functions on a set is `C^n` on the product set. -/
@[fun_prop]
theorem ContDiffOn.prodMap {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {F' : Type*}
    [NormedAddCommGroup F'] [NormedSpace ğ•œ F'] {s : Set E} {t : Set E'} {f : E â†’ F} {g : E' â†’ F'}
    (hf : ContDiffOn ğ•œ n f s) (hg : ContDiffOn ğ•œ n g t) : ContDiffOn ğ•œ n (Prod.map f g) (s Ã—Ë¢ t) :=
  (hf.comp contDiffOn_fst (prod_subset_preimage_fst _ _)).prodMk
    (hg.comp contDiffOn_snd (prod_subset_preimage_snd _ _))

@[deprecated (since := "2025-03-09")]
alias ContDiffOn.prod_map := ContDiffOn.prodMap

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
@[fun_prop]
theorem ContDiffAt.prodMap {f : E â†’ F} {g : E' â†’ F'} {x : E} {y : E'} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g y) : ContDiffAt ğ•œ n (Prod.map f g) (x, y) := by
  rw [ContDiffAt] at *
  simpa only [univ_prod_univ] using hf.prodMap hg

@[deprecated (since := "2025-03-09")]
alias ContDiffAt.prod_map := ContDiffAt.prodMap

/-- The product map of two `C^n` functions within a set at a point is `C^n`
within the product set at the product point. -/
@[fun_prop]
theorem ContDiffAt.prodMap' {f : E â†’ F} {g : E' â†’ F'} {p : E Ã— E'} (hf : ContDiffAt ğ•œ n f p.1)
    (hg : ContDiffAt ğ•œ n g p.2) : ContDiffAt ğ•œ n (Prod.map f g) p :=
  hf.prodMap hg

@[deprecated (since := "2025-03-09")]
alias ContDiffAt.prod_map' := ContDiffAt.prodMap'

/-- The product map of two `C^n` functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.prodMap {f : E â†’ F} {g : E' â†’ F'} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n (Prod.map f g) := by
  rw [contDiff_iff_contDiffAt] at *
  exact fun âŸ¨x, yâŸ© => (hf x).prodMap (hg y)

@[deprecated (since := "2025-03-09")]
alias ContDiff.prod_map := ContDiff.prodMap

@[fun_prop]
theorem contDiff_prodMk_left (fâ‚€ : F) : ContDiff ğ•œ n fun e : E => (e, fâ‚€) :=
  contDiff_id.prodMk contDiff_const

@[deprecated (since := "2025-03-09")]
alias contDiff_prod_mk_left := contDiff_prodMk_left

@[fun_prop]
theorem contDiff_prodMk_right (eâ‚€ : E) : ContDiff ğ•œ n fun f : F => (eâ‚€, f) :=
  contDiff_const.prodMk contDiff_id

@[deprecated (since := "2025-03-09")]
alias contDiff_prod_mk_right := contDiff_prodMk_right

end prodMap

/-!
### Inversion in a complete normed algebra (or more generally with summable geometric series)
-/

section AlgebraInverse

variable (ğ•œ)
variable {R : Type*} [NormedRing R] [NormedAlgebra ğ•œ R]

open NormedRing ContinuousLinearMap Ring

/-- In a complete normed algebra, the operation of inversion is `C^n`, for all `n`, at each
invertible element, as it is analytic. -/
@[fun_prop]
theorem contDiffAt_ringInverse [HasSummableGeomSeries R] (x : RË£) :
    ContDiffAt ğ•œ n Ring.inverse (x : R) := by
  have := AnalyticOnNhd.contDiffOn (analyticOnNhd_inverse (ğ•œ := ğ•œ) (A := R)) (n := n)
    Units.isOpen.uniqueDiffOn x x.isUnit
  exact this.contDiffAt (Units.isOpen.mem_nhds x.isUnit)

@[deprecated (since := "2025-04-22")] alias contDiffAt_ring_inverse := contDiffAt_ringInverse

variable {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']

@[fun_prop]
theorem contDiffAt_inv {x : ğ•œ'} (hx : x â‰  0) {n} : ContDiffAt ğ•œ n Inv.inv x := by
  simpa only [Ring.inverse_eq_inv'] using contDiffAt_ringInverse ğ•œ (Units.mk0 x hx)

@[fun_prop]
theorem contDiffOn_inv {n} : ContDiffOn ğ•œ n (Inv.inv : ğ•œ' â†’ ğ•œ') {0}á¶œ := fun _ hx =>
  (contDiffAt_inv ğ•œ hx).contDiffWithinAt

variable {ğ•œ}

@[fun_prop]
theorem ContDiffWithinAt.inv {f : E â†’ ğ•œ'} {n} (hf : ContDiffWithinAt ğ•œ n f s x) (hx : f x â‰  0) :
    ContDiffWithinAt ğ•œ n (fun x => (f x)â»Â¹) s x :=
  (contDiffAt_inv ğ•œ hx).comp_contDiffWithinAt x hf

@[fun_prop]
theorem ContDiffOn.inv {f : E â†’ ğ•œ'} (hf : ContDiffOn ğ•œ n f s) (h : âˆ€ x âˆˆ s, f x â‰  0) :
    ContDiffOn ğ•œ n (fun x => (f x)â»Â¹) s := fun x hx => (hf.contDiffWithinAt hx).inv (h x hx)

@[fun_prop]
nonrec theorem ContDiffAt.inv {f : E â†’ ğ•œ'} (hf : ContDiffAt ğ•œ n f x) (hx : f x â‰  0) :
    ContDiffAt ğ•œ n (fun x => (f x)â»Â¹) x :=
  hf.inv hx

@[fun_prop]
theorem ContDiff.inv {f : E â†’ ğ•œ'} (hf : ContDiff ğ•œ n f) (h : âˆ€ x, f x â‰  0) :
    ContDiff ğ•œ n fun x => (f x)â»Â¹ := by
  rw [contDiff_iff_contDiffAt]; exact fun x => hf.contDiffAt.inv (h x)

-- TODO: generalize to `f g : E â†’ ğ•œ'`
@[fun_prop]
theorem ContDiffWithinAt.div {f g : E â†’ ğ•œ} {n} (hf : ContDiffWithinAt ğ•œ n f s x)
    (hg : ContDiffWithinAt ğ•œ n g s x) (hx : g x â‰  0) :
    ContDiffWithinAt ğ•œ n (fun x => f x / g x) s x := by
  simpa only [div_eq_mul_inv] using hf.mul (hg.inv hx)

@[fun_prop]
theorem ContDiffOn.div {f g : E â†’ ğ•œ} {n} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) (hâ‚€ : âˆ€ x âˆˆ s, g x â‰  0) : ContDiffOn ğ•œ n (f / g) s := fun x hx =>
  (hf x hx).div (hg x hx) (hâ‚€ x hx)

@[fun_prop]
theorem ContDiffOn.fun_div {f g : E â†’ ğ•œ} {n} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) (hâ‚€ : âˆ€ x âˆˆ s, g x â‰  0) : ContDiffOn ğ•œ n (fun x => f x / g x) s :=
  ContDiffOn.div hf hg hâ‚€

@[fun_prop]
nonrec theorem ContDiffAt.div {f g : E â†’ ğ•œ} {n} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) (hx : g x â‰  0) : ContDiffAt ğ•œ n (fun x => f x / g x) x :=
  hf.div hg hx

@[fun_prop]
theorem ContDiff.div {f g : E â†’ ğ•œ} {n} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g)
    (h0 : âˆ€ x, g x â‰  0) : ContDiff ğ•œ n fun x => f x / g x := by
  simp only [contDiff_iff_contDiffAt] at *
  exact fun x => (hf x).div (hg x) (h0 x)

end AlgebraInverse

/-! ### Inversion of continuous linear maps between Banach spaces -/

section MapInverse

open ContinuousLinearMap

/-- At a continuous linear equivalence `e : E â‰ƒL[ğ•œ] F` between Banach spaces, the operation of
inversion is `C^n`, for all `n`. -/
@[fun_prop]
theorem contDiffAt_map_inverse [CompleteSpace E] (e : E â‰ƒL[ğ•œ] F) :
    ContDiffAt ğ•œ n inverse (e : E â†’L[ğ•œ] F) := by
  nontriviality E
  -- first, we use the lemma `inverse_eq_ringInverse` to rewrite in terms of `Ring.inverse` in the
  -- ring `E â†’L[ğ•œ] E`
  let Oâ‚ : (E â†’L[ğ•œ] E) â†’ F â†’L[ğ•œ] E := fun f => f.comp (e.symm : F â†’L[ğ•œ] E)
  let Oâ‚‚ : (E â†’L[ğ•œ] F) â†’ E â†’L[ğ•œ] E := fun f => (e.symm : F â†’L[ğ•œ] E).comp f
  have : ContinuousLinearMap.inverse = Oâ‚ âˆ˜ Ring.inverse âˆ˜ Oâ‚‚ := funext (inverse_eq_ringInverse e)
  rw [this]
  -- `Oâ‚` and `Oâ‚‚` are `ContDiff`,
  -- so we reduce to proving that `Ring.inverse` is `ContDiff`
  have hâ‚ : ContDiff ğ•œ n Oâ‚ := contDiff_id.clm_comp contDiff_const
  have hâ‚‚ : ContDiff ğ•œ n Oâ‚‚ := contDiff_const.clm_comp contDiff_id
  refine hâ‚.contDiffAt.comp _ (ContDiffAt.comp _ ?_ hâ‚‚.contDiffAt)
  convert contDiffAt_ringInverse ğ•œ (1 : (E â†’L[ğ•œ] E)Ë£)
  simp [Oâ‚‚, one_def]

/-- At an invertible map `e : M â†’L[R] Mâ‚‚` between Banach spaces, the operation of
inversion is `C^n`, for all `n`. -/
theorem ContinuousLinearMap.IsInvertible.contDiffAt_map_inverse [CompleteSpace E] {e : E â†’L[ğ•œ] F}
    (he : e.IsInvertible) : ContDiffAt ğ•œ n inverse e := by
  rcases he with âŸ¨M, rflâŸ©
  exact _root_.contDiffAt_map_inverse M

end MapInverse

section FunctionInverse

open ContinuousLinearMap

/-- If `f` is a local homeomorphism and the point `a` is in its target,
and if `f` is `n` times continuously differentiable at `f.symm a`,
and if the derivative at `f.symm a` is a continuous linear equivalence,
then `f.symm` is `n` times continuously differentiable at the point `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.contDiffAt_symm [CompleteSpace E] (f : PartialHomeomorph E F)
    {fâ‚€' : E â‰ƒL[ğ•œ] F} {a : F} (ha : a âˆˆ f.target)
    (hfâ‚€' : HasFDerivAt f (fâ‚€' : E â†’L[ğ•œ] F) (f.symm a)) (hf : ContDiffAt ğ•œ n f (f.symm a)) :
    ContDiffAt ğ•œ n f.symm a := by
  match n with
  | Ï‰ =>
    apply AnalyticAt.contDiffAt
    exact f.analyticAt_symm ha hf.analyticAt hfâ‚€'.fderiv
  | (n : â„•âˆ) =>
    -- We prove this by induction on `n`
    induction' n using ENat.nat_induction with n IH Itop
    Â· apply contDiffAt_zero.2
      exact âŸ¨f.target, IsOpen.mem_nhds f.open_target ha, f.continuousOn_invFunâŸ©
    Â· obtain âŸ¨f', âŸ¨u, hu, hff'âŸ©, hf'âŸ© := contDiffAt_succ_iff_hasFDerivAt.mp hf
      apply contDiffAt_succ_iff_hasFDerivAt.2
      -- For showing `n.succ` times continuous differentiability (the main inductive step), it
      -- suffices to produce the derivative and show that it is `n` times continuously
      -- differentiable
      have eq_fâ‚€' : f' (f.symm a) = fâ‚€' := (hff' (f.symm a) (mem_of_mem_nhds hu)).unique hfâ‚€'
      -- This follows by a bootstrapping formula expressing the derivative as a
      -- function of `f` itself
      refine âŸ¨inverse âˆ˜ f' âˆ˜ f.symm, ?_, ?_âŸ©
      Â· -- We first check that the derivative of `f` is that formula
        have h_nhds : { y : E | âˆƒ e : E â‰ƒL[ğ•œ] F, â†‘e = f' y } âˆˆ ğ“ (f.symm a) := by
          have hfâ‚€' := fâ‚€'.nhds
          rw [â† eq_fâ‚€'] at hfâ‚€'
          exact hf'.continuousAt.preimage_mem_nhds hfâ‚€'
        obtain âŸ¨t, htu, ht, htfâŸ© := mem_nhds_iff.mp (Filter.inter_mem hu h_nhds)
        use f.target âˆ© f.symm â»Â¹' t
        refine âŸ¨IsOpen.mem_nhds ?_ ?_, ?_âŸ©
        Â· exact f.isOpen_inter_preimage_symm ht
        Â· exact mem_inter ha (mem_preimage.mpr htf)
        intro x hx
        obtain âŸ¨hxu, e, heâŸ© := htu hx.2
        have h_deriv : HasFDerivAt f (e : E â†’L[ğ•œ] F) (f.symm x) := by
          rw [he]
          exact hff' (f.symm x) hxu
        convert f.hasFDerivAt_symm hx.1 h_deriv
        simp [â† he]
      Â· -- Then we check that the formula, being a composition of `ContDiff` pieces, is
        -- itself `ContDiff`
        have h_derivâ‚ : ContDiffAt ğ•œ n inverse (f' (f.symm a)) := by
          rw [eq_fâ‚€']
          exact contDiffAt_map_inverse _
        have h_derivâ‚‚ : ContDiffAt ğ•œ n f.symm a := by
          refine IH (hf.of_le ?_)
          norm_cast
          exact Nat.le_succ n
        exact (h_derivâ‚.comp _ hf').comp _ h_derivâ‚‚
    Â· refine contDiffAt_infty.mpr ?_
      intro n
      exact Itop n (contDiffAt_infty.mp hf n)

/-- If `f` is an `n` times continuously differentiable homeomorphism,
and if the derivative of `f` at each point is a continuous linear equivalence,
then `f.symm` is `n` times continuously differentiable.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem Homeomorph.contDiff_symm [CompleteSpace E] (f : E â‰ƒâ‚œ F) {fâ‚€' : E â†’ E â‰ƒL[ğ•œ] F}
    (hfâ‚€' : âˆ€ a, HasFDerivAt f (fâ‚€' a : E â†’L[ğ•œ] F) a) (hf : ContDiff ğ•œ n (f : E â†’ F)) :
    ContDiff ğ•œ n (f.symm : F â†’ E) :=
  contDiff_iff_contDiffAt.2 fun x =>
    f.toPartialHomeomorph.contDiffAt_symm (mem_univ x) (hfâ‚€' _) hf.contDiffAt

/-- Let `f` be a local homeomorphism of a nontrivially normed field, let `a` be a point in its
target. if `f` is `n` times continuously differentiable at `f.symm a`, and if the derivative at
`f.symm a` is nonzero, then `f.symm` is `n` times continuously differentiable at the point `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.contDiffAt_symm_deriv [CompleteSpace ğ•œ] (f : PartialHomeomorph ğ•œ ğ•œ)
    {fâ‚€' a : ğ•œ} (hâ‚€ : fâ‚€' â‰  0) (ha : a âˆˆ f.target) (hfâ‚€' : HasDerivAt f fâ‚€' (f.symm a))
    (hf : ContDiffAt ğ•œ n f (f.symm a)) : ContDiffAt ğ•œ n f.symm a :=
  f.contDiffAt_symm ha (hfâ‚€'.hasFDerivAt_equiv hâ‚€) hf

/-- Let `f` be an `n` times continuously differentiable homeomorphism of a nontrivially normed
field.  Suppose that the derivative of `f` is never equal to zero. Then `f.symm` is `n` times
continuously differentiable.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem Homeomorph.contDiff_symm_deriv [CompleteSpace ğ•œ] (f : ğ•œ â‰ƒâ‚œ ğ•œ) {f' : ğ•œ â†’ ğ•œ}
    (hâ‚€ : âˆ€ x, f' x â‰  0) (hf' : âˆ€ x, HasDerivAt f (f' x) x) (hf : ContDiff ğ•œ n (f : ğ•œ â†’ ğ•œ)) :
    ContDiff ğ•œ n (f.symm : ğ•œ â†’ ğ•œ) :=
  contDiff_iff_contDiffAt.2 fun x =>
    f.toPartialHomeomorph.contDiffAt_symm_deriv (hâ‚€ _) (mem_univ x) (hf' _) hf.contDiffAt

namespace PartialHomeomorph

variable (ğ•œ)

/-- Restrict a partial homeomorphism to the subsets of the source and target
that consist of points `x âˆˆ f.source`, `y = f x âˆˆ f.target`
such that `f` is `C^n` at `x` and `f.symm` is `C^n` at `y`.

Note that `n` is a natural number or `Ï‰`, but not `âˆ`,
because the set of points of `C^âˆ`-smoothness of `f` is not guaranteed to be open. -/
@[simps! apply symm_apply source target]
def restrContDiff (f : PartialHomeomorph E F) (n : WithTop â„•âˆ) (hn : n â‰  âˆ) :
    PartialHomeomorph E F :=
  haveI H : f.IsImage {x | ContDiffAt ğ•œ n f x âˆ§ ContDiffAt ğ•œ n f.symm (f x)}
      {y | ContDiffAt ğ•œ n f.symm y âˆ§ ContDiffAt ğ•œ n f (f.symm y)} := fun x hx â†¦ by
    simp [hx, and_comm]
  H.restr <| isOpen_iff_mem_nhds.2 fun _ âŸ¨hxs, hxf, hxf'âŸ© â†¦
    inter_mem (f.open_source.mem_nhds hxs) <| (hxf.eventually hn).and <|
    f.continuousAt hxs (hxf'.eventually hn)

lemma contDiffOn_restrContDiff_source (f : PartialHomeomorph E F) {n : WithTop â„•âˆ} (hn : n â‰  âˆ) :
    ContDiffOn ğ•œ n f (f.restrContDiff ğ•œ n hn).source := fun _x hx â†¦ hx.2.1.contDiffWithinAt

lemma contDiffOn_restrContDiff_target (f : PartialHomeomorph E F) {n : WithTop â„•âˆ} (hn : n â‰  âˆ) :
    ContDiffOn ğ•œ n f.symm (f.restrContDiff ğ•œ n hn).target := fun _x hx â†¦ hx.2.1.contDiffWithinAt

end PartialHomeomorph

end FunctionInverse

section RestrictScalars

/-!
### Restricting from `â„‚` to `â„`, or generally from `ğ•œ'` to `ğ•œ`

If a function is `n` times continuously differentiable over `â„‚`, then it is `n` times continuously
differentiable over `â„`. In this paragraph, we give variants of this statement, in the general
situation where `â„‚` and `â„` are replaced respectively by `ğ•œ'` and `ğ•œ` where `ğ•œ'` is a normed algebra
over `ğ•œ`.
-/


variable (ğ•œ)
variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
variable [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
variable [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
variable {p' : E â†’ FormalMultilinearSeries ğ•œ' E F}

theorem HasFTaylorSeriesUpToOn.restrictScalars {n : WithTop â„•âˆ}
    (h : HasFTaylorSeriesUpToOn n f p' s) :
    HasFTaylorSeriesUpToOn n f (fun x => (p' x).restrictScalars ğ•œ) s where
  zero_eq x hx := h.zero_eq x hx
  fderivWithin m hm x hx :=
    ((ContinuousMultilinearMap.restrictScalarsLinear ğ•œ).hasFDerivAt.comp_hasFDerivWithinAt x <|
        (h.fderivWithin m hm x hx).restrictScalars ğ•œ :)
  cont m hm := ContinuousMultilinearMap.continuous_restrictScalars.comp_continuousOn (h.cont m hm)

theorem ContDiffWithinAt.restrict_scalars (h : ContDiffWithinAt ğ•œ' n f s x) :
    ContDiffWithinAt ğ•œ n f s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, u_mem, p', hp', Hp'âŸ© := h
    refine âŸ¨u, u_mem, _, hp'.restrictScalars _, fun i â†¦ ?_âŸ©
    change AnalyticOn ğ•œ (fun x â†¦ ContinuousMultilinearMap.restrictScalarsLinear ğ•œ (p' x i)) u
    apply AnalyticOnNhd.comp_analyticOn _ (Hp' i).restrictScalars (Set.mapsTo_univ _ _)
    exact ContinuousLinearMap.analyticOnNhd _ _
  | (n : â„•âˆ) =>
    intro m hm
    rcases h m hm with âŸ¨u, u_mem, p', hp'âŸ©
    exact âŸ¨u, u_mem, _, hp'.restrictScalars _âŸ©

theorem ContDiffOn.restrict_scalars (h : ContDiffOn ğ•œ' n f s) : ContDiffOn ğ•œ n f s := fun x hx =>
  (h x hx).restrict_scalars _

theorem ContDiffAt.restrict_scalars (h : ContDiffAt ğ•œ' n f x) : ContDiffAt ğ•œ n f x :=
  contDiffWithinAt_univ.1 <| h.contDiffWithinAt.restrict_scalars _

theorem ContDiff.restrict_scalars (h : ContDiff ğ•œ' n f) : ContDiff ğ•œ n f :=
  contDiff_iff_contDiffAt.2 fun _ => h.contDiffAt.restrict_scalars _

end RestrictScalars
