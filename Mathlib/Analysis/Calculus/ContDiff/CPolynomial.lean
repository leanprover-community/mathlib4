/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Calculus.ContDiff.Defs

/-!
# Higher smoothness of continuously polynomial functions

We prove that continuously polynomial functions are `C^âˆ`. In particular, this is the case
of continuous multilinear maps.
-/

open Filter Asymptotics

open scoped ENNReal

universe u v

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ} {n : â„•}
variable {f : E â†’ F} {x : E} {s : Set E}

/-- A polynomial function is infinitely differentiable. -/
theorem CPolynomialOn.contDiffOn (h : CPolynomialOn ğ•œ f s) {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s :=
  let t := { x | CPolynomialAt ğ•œ f x }
  suffices ContDiffOn ğ•œ n f t from this.mono h
  have H : CPolynomialOn ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_cPolynomialAt ğ•œ f
  contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).analyticOnNhd.differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)

theorem CPolynomialAt.contDiffAt (h : CPolynomialAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x :=
  let âŸ¨_, hs, hfâŸ© := h.exists_mem_nhds_cPolynomialOn
  hf.contDiffOn.contDiffAt hs

end fderiv

namespace ContinuousMultilinearMap

variable {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [Fintype Î¹] (f : ContinuousMultilinearMap ğ•œ E F) {n : â„•âˆ} {x : Î  i, E i}

open FormalMultilinearSeries

lemma contDiffAt : ContDiffAt ğ•œ n f x := f.cpolynomialAt.contDiffAt

lemma contDiff : ContDiff ğ•œ n f := contDiff_iff_contDiffAt.mpr (fun _ â†¦ f.contDiffAt)

end ContinuousMultilinearMap
