/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Basic
import Mathlib.Data.Finset.Sym
import Mathlib.Data.Nat.Choose.Cast
import Mathlib.Data.Nat.Choose.Multinomial

#align_import analysis.calculus.cont_diff from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Bounds on higher derivatives

`norm_iteratedFDeriv_comp_le` gives the bound `n! * C * D ^ n` for the `n`-th derivative
  of `g âˆ˜ f` assuming that the derivatives of `g` are bounded by `C` and the `i`-th
  derivative of `f` is bounded by `D ^ i`.
-/

noncomputable section

open scoped Classical NNReal Nat

universe u uD uE uF uG

open Set Fin Filter Function

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {D : Type uD} [NormedAddCommGroup D]
  [NormedSpace ğ•œ D] {E : Type uE} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {F : Type uF}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {s sâ‚ t u : Set E}

/-!## Quantitative bounds -/

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear. This lemma is an auxiliary version
assuming all spaces live in the same universe, to enable an induction. Use instead
`ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear` that removes this assumption. -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_aux {Du Eu Fu Gu : Type u}
    [NormedAddCommGroup Du] [NormedSpace ğ•œ Du] [NormedAddCommGroup Eu] [NormedSpace ğ•œ Eu]
    [NormedAddCommGroup Fu] [NormedSpace ğ•œ Fu] [NormedAddCommGroup Gu] [NormedSpace ğ•œ Gu]
    (B : Eu â†’L[ğ•œ] Fu â†’L[ğ•œ] Gu) {f : Du â†’ Eu} {g : Du â†’ Fu} {n : â„•} {s : Set Du} {x : Du}
    (hf : ContDiffOn ğ•œ n f s) (hg : ContDiffOn ğ•œ n g s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    â€–iteratedFDerivWithin ğ•œ n (fun y => B (f y) (g y)) s xâ€– â‰¤
      â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– := by
  /- We argue by induction on `n`. The bound is trivial for `n = 0`. For `n + 1`, we write
    the `(n+1)`-th derivative as the `n`-th derivative of the derivative `B f g' + B f' g`,
    and apply the inductive assumption to each of those two terms. For this induction to make sense,
    the spaces of linear maps that appear in the induction should be in the same universe as the
    original spaces, which explains why we assume in the lemma that all spaces live in the same
    universe. -/
  induction' n with n IH generalizing Eu Fu Gu
  Â· simp only [Nat.zero_eq, norm_iteratedFDerivWithin_zero, zero_add, Finset.range_one,
      Finset.sum_singleton, Nat.choose_self, Nat.cast_one, one_mul, Nat.sub_zero, â† mul_assoc]
    apply B.le_opNormâ‚‚
  Â· have In : (n : â„•âˆ) + 1 â‰¤ n.succ := by simp only [Nat.cast_succ, le_refl]
    -- Porting note: the next line is a hack allowing Lean to find the operator norm instance.
    let norm := @ContinuousLinearMap.hasOpNorm _ _ Eu ((Du â†’L[ğ•œ] Fu) â†’L[ğ•œ] Du â†’L[ğ•œ] Gu) _ _ _ _ _ _
      (RingHom.id ğ•œ)
    have I1 :
        â€–iteratedFDerivWithin ğ•œ n (fun y : Du => B.precompR Du (f y) (fderivWithin ğ•œ g s y)) s xâ€– â‰¤
          â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), n.choose i * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
            â€–iteratedFDerivWithin ğ•œ (n + 1 - i) g s xâ€– := by
      calc
        â€–iteratedFDerivWithin ğ•œ n (fun y : Du => B.precompR Du (f y) (fderivWithin ğ•œ g s y)) s xâ€– â‰¤
            â€–B.precompR Duâ€– * âˆ‘ i âˆˆ Finset.range (n + 1),
              n.choose i * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
                â€–iteratedFDerivWithin ğ•œ (n - i) (fderivWithin ğ•œ g s) s xâ€– :=
          IH _ (hf.of_le (Nat.cast_le.2 (Nat.le_succ n))) (hg.fderivWithin hs In)
        _ â‰¤ â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), n.choose i * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
              â€–iteratedFDerivWithin ğ•œ (n - i) (fderivWithin ğ•œ g s) s xâ€– :=
            mul_le_mul_of_nonneg_right (B.norm_precompR_le Du) (by positivity)
        _ = _ := by
          congr 1
          apply Finset.sum_congr rfl fun i hi => ?_
          rw [Nat.succ_sub (Nat.lt_succ_iff.1 (Finset.mem_range.1 hi)),
            â† norm_iteratedFDerivWithin_fderivWithin hs hx]
    -- Porting note: the next line is a hack allowing Lean to find the operator norm instance.
    let norm := @ContinuousLinearMap.hasOpNorm _ _ (Du â†’L[ğ•œ] Eu) (Fu â†’L[ğ•œ] Du â†’L[ğ•œ] Gu) _ _ _ _ _ _
      (RingHom.id ğ•œ)
    have I2 :
        â€–iteratedFDerivWithin ğ•œ n (fun y : Du => B.precompL Du (fderivWithin ğ•œ f s y) (g y)) s xâ€– â‰¤
        â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), n.choose i * â€–iteratedFDerivWithin ğ•œ (i + 1) f s xâ€– *
          â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– :=
      calc
        â€–iteratedFDerivWithin ğ•œ n (fun y : Du => B.precompL Du (fderivWithin ğ•œ f s y) (g y)) s xâ€– â‰¤
            â€–B.precompL Duâ€– * âˆ‘ i âˆˆ Finset.range (n + 1),
              n.choose i * â€–iteratedFDerivWithin ğ•œ i (fderivWithin ğ•œ f s) s xâ€– *
                â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– :=
          IH _ (hf.fderivWithin hs In) (hg.of_le (Nat.cast_le.2 (Nat.le_succ n)))
        _ â‰¤ â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1),
            n.choose i * â€–iteratedFDerivWithin ğ•œ i (fderivWithin ğ•œ f s) s xâ€– *
              â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– :=
            mul_le_mul_of_nonneg_right (B.norm_precompL_le Du) (by positivity)
        _ = _ := by
          congr 1
          apply Finset.sum_congr rfl fun i _ => ?_
          rw [â† norm_iteratedFDerivWithin_fderivWithin hs hx]
    have J : iteratedFDerivWithin ğ•œ n
        (fun y : Du => fderivWithin ğ•œ (fun y : Du => B (f y) (g y)) s y) s x =
          iteratedFDerivWithin ğ•œ n (fun y => B.precompR Du (f y)
            (fderivWithin ğ•œ g s y) + B.precompL Du (fderivWithin ğ•œ f s y) (g y)) s x := by
      apply iteratedFDerivWithin_congr (fun y hy => ?_) hx
      have L : (1 : â„•âˆ) â‰¤ n.succ := by
        simpa only [ENat.coe_one, Nat.one_le_cast] using Nat.succ_pos n
      exact B.fderivWithin_of_bilinear (hf.differentiableOn L y hy) (hg.differentiableOn L y hy)
        (hs y hy)
    rw [â† norm_iteratedFDerivWithin_fderivWithin hs hx, J]
    have A : ContDiffOn ğ•œ n (fun y => B.precompR Du (f y) (fderivWithin ğ•œ g s y)) s :=
      (B.precompR Du).isBoundedBilinearMap.contDiff.comp_contDiff_onâ‚‚
        (hf.of_le (Nat.cast_le.2 (Nat.le_succ n))) (hg.fderivWithin hs In)
    have A' : ContDiffOn ğ•œ n (fun y => B.precompL Du (fderivWithin ğ•œ f s y) (g y)) s :=
      (B.precompL Du).isBoundedBilinearMap.contDiff.comp_contDiff_onâ‚‚ (hf.fderivWithin hs In)
        (hg.of_le (Nat.cast_le.2 (Nat.le_succ n)))
    rw [iteratedFDerivWithin_add_apply' A A' hs hx]
    apply (norm_add_le _ _).trans ((add_le_add I1 I2).trans (le_of_eq ?_))
    simp_rw [â† mul_add, mul_assoc]
    congr 1
    exact (Finset.sum_choose_succ_mul
      (fun i j => â€–iteratedFDerivWithin ğ•œ i f s xâ€– * â€–iteratedFDerivWithin ğ•œ j g s xâ€–) n).symm
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear_aux ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_aux

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear:
`â€–D^n (x â†¦ B (f x) (g x))â€– â‰¤ â€–Bâ€– âˆ‘_{k â‰¤ n} n.choose k â€–D^k fâ€– â€–D^{n-k} gâ€–` -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G)
    {f : D â†’ E} {g : D â†’ F} {N : â„•âˆ} {s : Set D} {x : D} (hf : ContDiffOn ğ•œ N f s)
    (hg : ContDiffOn ğ•œ N g s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDerivWithin ğ•œ n (fun y => B (f y) (g y)) s xâ€– â‰¤
      â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– := by
  /- We reduce the bound to the case where all spaces live in the same universe (in which we
    already have proved the result), by using linear isometries between the spaces and their `ULift`
    to a common universe. These linear isometries preserve the norm of the iterated derivative. -/
  let Du : Type max uD uE uF uG := ULift.{max uE uF uG, uD} D
  let Eu : Type max uD uE uF uG := ULift.{max uD uF uG, uE} E
  let Fu : Type max uD uE uF uG := ULift.{max uD uE uG, uF} F
  let Gu : Type max uD uE uF uG := ULift.{max uD uE uF, uG} G
  have isoD : Du â‰ƒâ‚—áµ¢[ğ•œ] D := LinearIsometryEquiv.ulift ğ•œ D
  have isoE : Eu â‰ƒâ‚—áµ¢[ğ•œ] E := LinearIsometryEquiv.ulift ğ•œ E
  have isoF : Fu â‰ƒâ‚—áµ¢[ğ•œ] F := LinearIsometryEquiv.ulift ğ•œ F
  have isoG : Gu â‰ƒâ‚—áµ¢[ğ•œ] G := LinearIsometryEquiv.ulift ğ•œ G
  -- lift `f` and `g` to versions `fu` and `gu` on the lifted spaces.
  set fu : Du â†’ Eu := isoE.symm âˆ˜ f âˆ˜ isoD with hfu
  set gu : Du â†’ Fu := isoF.symm âˆ˜ g âˆ˜ isoD with hgu
  -- lift the bilinear map `B` to a bilinear map `Bu` on the lifted spaces.
  set Buâ‚€ : Eu â†’L[ğ•œ] Fu â†’L[ğ•œ] G := ((B.comp (isoE : Eu â†’L[ğ•œ] E)).flip.comp (isoF : Fu â†’L[ğ•œ] F)).flip
    with hBuâ‚€
  let Bu : Eu â†’L[ğ•œ] Fu â†’L[ğ•œ] Gu :=
   ContinuousLinearMap.compL ğ•œ Eu (Fu â†’L[ğ•œ] G) (Fu â†’L[ğ•œ] Gu)
    (ContinuousLinearMap.compL ğ•œ Fu G Gu (isoG.symm : G â†’L[ğ•œ] Gu)) Buâ‚€
  have hBu : Bu = ContinuousLinearMap.compL ğ•œ Eu (Fu â†’L[ğ•œ] G) (Fu â†’L[ğ•œ] Gu)
      (ContinuousLinearMap.compL ğ•œ Fu G Gu (isoG.symm : G â†’L[ğ•œ] Gu)) Buâ‚€ := rfl
  have Bu_eq : (fun y => Bu (fu y) (gu y)) = isoG.symm âˆ˜ (fun y => B (f y) (g y)) âˆ˜ isoD := by
    ext1 y
    simp [hBu, hBuâ‚€, hfu, hgu]
  -- All norms are preserved by the lifting process.
  have Bu_le : â€–Buâ€– â‰¤ â€–Bâ€– := by
    refine' ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg B) fun y => _
    refine' ContinuousLinearMap.opNorm_le_bound _ (by positivity) fun x => _
    simp only [hBu, hBuâ‚€, compL_apply, coe_comp', Function.comp_apply,
      ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_coe, flip_apply,
      LinearIsometryEquiv.norm_map]
    calc
      â€–B (isoE y) (isoF x)â€– â‰¤ â€–B (isoE y)â€– * â€–isoF xâ€– := ContinuousLinearMap.le_opNorm _ _
      _ â‰¤ â€–Bâ€– * â€–isoE yâ€– * â€–isoF xâ€– := by gcongr; apply ContinuousLinearMap.le_opNorm
      _ = â€–Bâ€– * â€–yâ€– * â€–xâ€– := by simp only [LinearIsometryEquiv.norm_map]
  let su := isoD â»Â¹' s
  have hsu : UniqueDiffOn ğ•œ su := isoD.toContinuousLinearEquiv.uniqueDiffOn_preimage_iff.2 hs
  let xu := isoD.symm x
  have hxu : xu âˆˆ su := by
    simpa only [xu, su, Set.mem_preimage, LinearIsometryEquiv.apply_symm_apply] using hx
  have xu_x : isoD xu = x := by simp only [xu, LinearIsometryEquiv.apply_symm_apply]
  have hfu : ContDiffOn ğ•œ n fu su :=
    isoE.symm.contDiff.comp_contDiffOn
      ((hf.of_le hn).comp_continuousLinearMap (isoD : Du â†’L[ğ•œ] D))
  have hgu : ContDiffOn ğ•œ n gu su :=
    isoF.symm.contDiff.comp_contDiffOn
      ((hg.of_le hn).comp_continuousLinearMap (isoD : Du â†’L[ğ•œ] D))
  have Nfu : âˆ€ i, â€–iteratedFDerivWithin ğ•œ i fu su xuâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := by
    intro i
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [â† xu_x] at hx
  have Ngu : âˆ€ i, â€–iteratedFDerivWithin ğ•œ i gu su xuâ€– = â€–iteratedFDerivWithin ğ•œ i g s xâ€– := by
    intro i
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [â† xu_x] at hx
  have NBu :
    â€–iteratedFDerivWithin ğ•œ n (fun y => Bu (fu y) (gu y)) su xuâ€– =
      â€–iteratedFDerivWithin ğ•œ n (fun y => B (f y) (g y)) s xâ€– := by
    rw [Bu_eq]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hsu hxu]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ hs, xu_x]
    rwa [â† xu_x] at hx
  -- state the bound for the lifted objects, and deduce the original bound from it.
  have : â€–iteratedFDerivWithin ğ•œ n (fun y => Bu (fu y) (gu y)) su xuâ€– â‰¤
      â€–Buâ€– * âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i fu su xuâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) gu su xuâ€– :=
    Bu.norm_iteratedFDerivWithin_le_of_bilinear_aux hfu hgu hsu hxu
  simp only [Nfu, Ngu, NBu] at this
  exact this.trans (mul_le_mul_of_nonneg_right Bu_le (by positivity))
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear:
`â€–D^n (x â†¦ B (f x) (g x))â€– â‰¤ â€–Bâ€– âˆ‘_{k â‰¤ n} n.choose k â€–D^k fâ€– â€–D^{n-k} gâ€–` -/
theorem ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G) {f : D â†’ E}
    {g : D â†’ F} {N : â„•âˆ} (hf : ContDiff ğ•œ N f) (hg : ContDiff ğ•œ N g) (x : D) {n : â„•}
    (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (fun y => B (f y) (g y)) xâ€– â‰¤ â€–Bâ€– * âˆ‘ i âˆˆ Finset.range (n + 1),
      (n.choose i : â„) * â€–iteratedFDeriv ğ•œ i f xâ€– * â€–iteratedFDeriv ğ•œ (n - i) g xâ€– := by
  simp_rw [â† iteratedFDerivWithin_univ]
  exact B.norm_iteratedFDerivWithin_le_of_bilinear hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ
    (mem_univ x) hn
#align continuous_linear_map.norm_iterated_fderiv_le_of_bilinear ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` within a set in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear of norm at most `1`:
`â€–D^n (x â†¦ B (f x) (g x))â€– â‰¤ âˆ‘_{k â‰¤ n} n.choose k â€–D^k fâ€– â€–D^{n-k} gâ€–` -/
theorem ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
    (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G) {f : D â†’ E} {g : D â†’ F} {N : â„•âˆ} {s : Set D} {x : D}
    (hf : ContDiffOn ğ•œ N f s) (hg : ContDiffOn ğ•œ N g s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•}
    (hn : (n : â„•âˆ) â‰¤ N) (hB : â€–Bâ€– â‰¤ 1) : â€–iteratedFDerivWithin ğ•œ n (fun y => B (f y) (g y)) s xâ€– â‰¤
      âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– := by
  apply (B.norm_iteratedFDerivWithin_le_of_bilinear hf hg hs hx hn).trans
  exact mul_le_of_le_one_left (by positivity) hB
#align continuous_linear_map.norm_iterated_fderiv_within_le_of_bilinear_of_le_one ContinuousLinearMap.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one

/-- Bounding the norm of the iterated derivative of `B (f x) (g x)` in terms of the
iterated derivatives of `f` and `g` when `B` is bilinear of norm at most `1`:
`â€–D^n (x â†¦ B (f x) (g x))â€– â‰¤ âˆ‘_{k â‰¤ n} n.choose k â€–D^k fâ€– â€–D^{n-k} gâ€–` -/
theorem ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G)
    {f : D â†’ E} {g : D â†’ F} {N : â„•âˆ} (hf : ContDiff ğ•œ N f) (hg : ContDiff ğ•œ N g) (x : D) {n : â„•}
    (hn : (n : â„•âˆ) â‰¤ N) (hB : â€–Bâ€– â‰¤ 1) : â€–iteratedFDeriv ğ•œ n (fun y => B (f y) (g y)) xâ€– â‰¤
      âˆ‘ i âˆˆ Finset.range (n + 1),
        (n.choose i : â„) * â€–iteratedFDeriv ğ•œ i f xâ€– * â€–iteratedFDeriv ğ•œ (n - i) g xâ€– := by
  simp_rw [â† iteratedFDerivWithin_univ]
  exact B.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one hf.contDiffOn hg.contDiffOn
    uniqueDiffOn_univ (mem_univ x) hn hB
#align continuous_linear_map.norm_iterated_fderiv_le_of_bilinear_of_le_one ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one

section

variable {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' F]
  [IsScalarTower ğ•œ ğ•œ' F]

theorem norm_iteratedFDerivWithin_smul_le {f : E â†’ ğ•œ'} {g : E â†’ F} {N : â„•âˆ}
    (hf : ContDiffOn ğ•œ N f s) (hg : ContDiffOn ğ•œ N g s) (hs : UniqueDiffOn ğ•œ s) {x : E} (hx : x âˆˆ s)
    {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) : â€–iteratedFDerivWithin ğ•œ n (fun y => f y â€¢ g y) s xâ€– â‰¤
      âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– :=
  (ContinuousLinearMap.lsmul ğ•œ ğ•œ' :
    ğ•œ' â†’L[ğ•œ] F â†’L[ğ•œ] F).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
      hf hg hs hx hn ContinuousLinearMap.opNorm_lsmul_le
#align norm_iterated_fderiv_within_smul_le norm_iteratedFDerivWithin_smul_le

theorem norm_iteratedFDeriv_smul_le {f : E â†’ ğ•œ'} {g : E â†’ F} {N : â„•âˆ} (hf : ContDiff ğ•œ N f)
    (hg : ContDiff ğ•œ N g) (x : E) {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (fun y => f y â€¢ g y) xâ€– â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1),
      (n.choose i : â„) * â€–iteratedFDeriv ğ•œ i f xâ€– * â€–iteratedFDeriv ğ•œ (n - i) g xâ€– :=
  (ContinuousLinearMap.lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] F â†’L[ğ•œ] F).norm_iteratedFDeriv_le_of_bilinear_of_le_one
    hf hg x hn ContinuousLinearMap.opNorm_lsmul_le
#align norm_iterated_fderiv_smul_le norm_iteratedFDeriv_smul_le

end

section

variable {Î¹ : Type*} {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A] {A' : Type*} [NormedCommRing A']
  [NormedAlgebra ğ•œ A']

theorem norm_iteratedFDerivWithin_mul_le {f : E â†’ A} {g : E â†’ A} {N : â„•âˆ} (hf : ContDiffOn ğ•œ N f s)
    (hg : ContDiffOn ğ•œ N g s) (hs : UniqueDiffOn ğ•œ s) {x : E} (hx : x âˆˆ s) {n : â„•}
    (hn : (n : â„•âˆ) â‰¤ N) : â€–iteratedFDerivWithin ğ•œ n (fun y => f y * g y) s xâ€– â‰¤
      âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– :=
  (ContinuousLinearMap.mul ğ•œ A :
    A â†’L[ğ•œ] A â†’L[ğ•œ] A).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
      hf hg hs hx hn (ContinuousLinearMap.opNorm_mul_le _ _)
#align norm_iterated_fderiv_within_mul_le norm_iteratedFDerivWithin_mul_le

theorem norm_iteratedFDeriv_mul_le {f : E â†’ A} {g : E â†’ A} {N : â„•âˆ} (hf : ContDiff ğ•œ N f)
    (hg : ContDiff ğ•œ N g) (x : E) {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (fun y => f y * g y) xâ€– â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1),
      (n.choose i : â„) * â€–iteratedFDeriv ğ•œ i f xâ€– * â€–iteratedFDeriv ğ•œ (n - i) g xâ€– := by
  simp_rw [â† iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_mul_le
    hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ (mem_univ x) hn
#align norm_iterated_fderiv_mul_le norm_iteratedFDeriv_mul_le

-- TODO: Add `norm_iteratedFDeriv[Within]_list_prod_le` for non-commutative `NormedRing A`.

theorem norm_iteratedFDerivWithin_prod_le [DecidableEq Î¹] [NormOneClass A'] {u : Finset Î¹}
    {f : Î¹ â†’ E â†’ A'} {N : â„•âˆ} (hf : âˆ€ i âˆˆ u, ContDiffOn ğ•œ N (f i) s) (hs : UniqueDiffOn ğ•œ s) {x : E}
    (hx : x âˆˆ s) {n : â„•} (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDerivWithin ğ•œ n (âˆ j âˆˆ u, f j Â·) s xâ€– â‰¤
      âˆ‘ p âˆˆ u.sym n, (p : Multiset Î¹).multinomial *
        âˆ j âˆˆ u, â€–iteratedFDerivWithin ğ•œ (Multiset.count j p) (f j) s xâ€– := by
  induction u using Finset.induction generalizing n with
  | empty =>
    cases n with
    | zero => simp [Sym.eq_nil_of_card_zero]
    | succ n => simp [iteratedFDerivWithin_succ_const _ _ hs hx]
  | @insert i u hi IH =>
    conv => lhs; simp only [Finset.prod_insert hi]
    simp only [Finset.mem_insert, forall_eq_or_imp] at hf
    refine le_trans (norm_iteratedFDerivWithin_mul_le hf.1 (contDiffOn_prod hf.2) hs hx hn) ?_
    rw [â† Finset.sum_coe_sort (Finset.sym _ _)]
    rw [Finset.sum_equiv (Finset.symInsertEquiv hi) (t := Finset.univ)
      (g := (fun v â†¦ v.multinomial *
          âˆ j âˆˆ insert i u, â€–iteratedFDerivWithin ğ•œ (v.count j) (f j) s xâ€–) âˆ˜
        Sym.toMultiset âˆ˜ Subtype.val âˆ˜ (Finset.symInsertEquiv hi).symm)
      (by simp) (by simp only [â† comp_apply (g := Finset.symInsertEquiv hi), comp.assoc]; simp)]
    rw [â† Finset.univ_sigma_univ, Finset.sum_sigma, Finset.sum_range]
    simp only [comp_apply, Finset.symInsertEquiv_symm_apply_coe]
    refine Finset.sum_le_sum ?_
    intro m _
    specialize IH hf.2 (n := n - m) (le_trans (WithTop.coe_le_coe.mpr (n.sub_le m)) hn)
    refine le_trans (mul_le_mul_of_nonneg_left IH (by simp [mul_nonneg])) ?_
    rw [Finset.mul_sum, â† Finset.sum_coe_sort]
    refine Finset.sum_le_sum ?_
    simp only [Finset.mem_univ, forall_true_left, Subtype.forall, Finset.mem_sym_iff]
    intro p hp
    refine le_of_eq ?_
    rw [Finset.prod_insert hi]
    have hip : i âˆ‰ p := mt (hp i) hi
    rw [Sym.count_coe_fill_self_of_not_mem hip, Sym.multinomial_coe_fill_of_not_mem hip]
    suffices âˆ j âˆˆ u, â€–iteratedFDerivWithin ğ•œ (Multiset.count j p) (f j) s xâ€– =
        âˆ j âˆˆ u, â€–iteratedFDerivWithin ğ•œ (Multiset.count j (Sym.fill i m p)) (f j) s xâ€– by
      rw [this, Nat.cast_mul]
      ring
    refine Finset.prod_congr rfl ?_
    intro j hj
    have hji : j â‰  i := mt (Â· â–¸ hj) hi
    rw [Sym.count_coe_fill_of_ne hji]

theorem norm_iteratedFDeriv_prod_le [DecidableEq Î¹] [NormOneClass A'] {u : Finset Î¹}
    {f : Î¹ â†’ E â†’ A'} {N : â„•âˆ} (hf : âˆ€ i âˆˆ u, ContDiff ğ•œ N (f i)) {x : E} {n : â„•}
    (hn : (n : â„•âˆ) â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (âˆ j âˆˆ u, f j Â·) xâ€– â‰¤
      âˆ‘ p âˆˆ u.sym n, (p : Multiset Î¹).multinomial *
        âˆ j âˆˆ u, â€–iteratedFDeriv ğ•œ ((p : Multiset Î¹).count j) (f j) xâ€– := by
  simpa [iteratedFDerivWithin_univ] using
    norm_iteratedFDerivWithin_prod_le (fun i hi â†¦ (hf i hi).contDiffOn) uniqueDiffOn_univ
      (mem_univ x) hn

end

/-- If the derivatives within a set of `g` at `f x` are bounded by `C`, and the `i`-th derivative
within a set of `f` at `x` is bounded by `D^i` for all `1 â‰¤ i â‰¤ n`, then the `n`-th derivative
of `g âˆ˜ f` is bounded by `n! * C * D^n`.
This lemma proves this estimate assuming additionally that two of the spaces live in the same
universe, to make an induction possible. Use instead `norm_iteratedFDerivWithin_comp_le` that
removes this assumption. -/
theorem norm_iteratedFDerivWithin_comp_le_aux {Fu Gu : Type u} [NormedAddCommGroup Fu]
    [NormedSpace ğ•œ Fu] [NormedAddCommGroup Gu] [NormedSpace ğ•œ Gu] {g : Fu â†’ Gu} {f : E â†’ Fu} {n : â„•}
    {s : Set E} {t : Set Fu} {x : E} (hg : ContDiffOn ğ•œ n g t) (hf : ContDiffOn ğ•œ n f s)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hst : MapsTo f s t) (hx : x âˆˆ s) {C : â„}
    {D : â„} (hC : âˆ€ i, i â‰¤ n â†’ â€–iteratedFDerivWithin ğ•œ i g t (f x)â€– â‰¤ C)
    (hD : âˆ€ i, 1 â‰¤ i â†’ i â‰¤ n â†’ â€–iteratedFDerivWithin ğ•œ i f s xâ€– â‰¤ D ^ i) :
    â€–iteratedFDerivWithin ğ•œ n (g âˆ˜ f) s xâ€– â‰¤ n ! * C * D ^ n := by
  /- We argue by induction on `n`, using that `D^(n+1) (g âˆ˜ f) = D^n (g ' âˆ˜ f â¬ f')`. The successive
    derivatives of `g' âˆ˜ f` are controlled thanks to the inductive assumption, and those of `f'` are
    controlled by assumption.
    As composition of linear maps is a bilinear map, one may use
    `ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one` to get from these a bound
    on `D^n (g ' âˆ˜ f â¬ f')`. -/
  induction' n using Nat.case_strong_induction_on with n IH generalizing Gu
  Â· simpa [norm_iteratedFDerivWithin_zero, Nat.factorial_zero, algebraMap.coe_one, one_mul,
      pow_zero, mul_one, comp_apply] using hC 0 le_rfl
  have M : (n : â„•âˆ) < n.succ := Nat.cast_lt.2 n.lt_succ_self
  have Cnonneg : 0 â‰¤ C := (norm_nonneg _).trans (hC 0 bot_le)
  have Dnonneg : 0 â‰¤ D := by
    have : 1 â‰¤ n + 1 := by simp only [le_add_iff_nonneg_left, zero_le']
    simpa only [pow_one] using (norm_nonneg _).trans (hD 1 le_rfl this)
  -- use the inductive assumption to bound the derivatives of `g' âˆ˜ f`.
  have I : âˆ€ i âˆˆ Finset.range (n + 1),
      â€–iteratedFDerivWithin ğ•œ i (fderivWithin ğ•œ g t âˆ˜ f) s xâ€– â‰¤ i ! * C * D ^ i := by
    intro i hi
    simp only [Finset.mem_range_succ_iff] at hi
    apply IH i hi
    Â· apply hg.fderivWithin ht
      simp only [Nat.cast_succ]
      exact add_le_add_right (Nat.cast_le.2 hi) _
    Â· apply hf.of_le (Nat.cast_le.2 (hi.trans n.le_succ))
    Â· intro j hj
      have : â€–iteratedFDerivWithin ğ•œ j (fderivWithin ğ•œ g t) t (f x)â€– =
          â€–iteratedFDerivWithin ğ•œ (j + 1) g t (f x)â€– := by
        rw [iteratedFDerivWithin_succ_eq_comp_right ht (hst hx), comp_apply,
          LinearIsometryEquiv.norm_map]
      rw [this]
      exact hC (j + 1) (add_le_add (hj.trans hi) le_rfl)
    Â· intro j hj h'j
      exact hD j hj (h'j.trans (hi.trans n.le_succ))
  -- reformulate `hD` as a bound for the derivatives of `f'`.
  have J : âˆ€ i, â€–iteratedFDerivWithin ğ•œ (n - i) (fderivWithin ğ•œ f s) s xâ€– â‰¤ D ^ (n - i + 1) := by
    intro i
    have : â€–iteratedFDerivWithin ğ•œ (n - i) (fderivWithin ğ•œ f s) s xâ€– =
        â€–iteratedFDerivWithin ğ•œ (n - i + 1) f s xâ€– := by
      rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply, LinearIsometryEquiv.norm_map]
    rw [this]
    apply hD
    Â· simp only [le_add_iff_nonneg_left, zero_le']
    Â· apply Nat.succ_le_succ tsub_le_self
  -- Now put these together: first, notice that we have to bound `D^n (g' âˆ˜ f â¬ f')`.
  calc
    â€–iteratedFDerivWithin ğ•œ (n + 1) (g âˆ˜ f) s xâ€– =
        â€–iteratedFDerivWithin ğ•œ n (fun y : E => fderivWithin ğ•œ (g âˆ˜ f) s y) s xâ€– := by
      rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply,
        LinearIsometryEquiv.norm_map]
    _ = â€–iteratedFDerivWithin ğ•œ n (fun y : E => ContinuousLinearMap.compL ğ•œ E Fu Gu
        (fderivWithin ğ•œ g t (f y)) (fderivWithin ğ•œ f s y)) s xâ€– := by
      have L : (1 : â„•âˆ) â‰¤ n.succ := by simpa only [ENat.coe_one, Nat.one_le_cast] using n.succ_pos
      congr 1
      refine iteratedFDerivWithin_congr (fun y hy => ?_) hx _
      apply fderivWithin.comp _ _ _ hst (hs y hy)
      Â· exact hg.differentiableOn L _ (hst hy)
      Â· exact hf.differentiableOn L _ hy
    -- bound it using the fact that the composition of linear maps is a bilinear operation,
    -- for which we have bounds for the`n`-th derivative.
    _ â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1),
        (n.choose i : â„) * â€–iteratedFDerivWithin ğ•œ i (fderivWithin ğ•œ g t âˆ˜ f) s xâ€– *
          â€–iteratedFDerivWithin ğ•œ (n - i) (fderivWithin ğ•œ f s) s xâ€– := by
      have A : ContDiffOn ğ•œ n (fderivWithin ğ•œ g t âˆ˜ f) s := by
        apply ContDiffOn.comp _ (hf.of_le M.le) hst
        apply hg.fderivWithin ht
        simp only [Nat.cast_succ, le_refl]
      have B : ContDiffOn ğ•œ n (fderivWithin ğ•œ f s) s := by
        apply hf.fderivWithin hs
        simp only [Nat.cast_succ, le_refl]
      exact (ContinuousLinearMap.compL ğ•œ E Fu Gu).norm_iteratedFDerivWithin_le_of_bilinear_of_le_one
        A B hs hx le_rfl (ContinuousLinearMap.norm_compL_le ğ•œ E Fu Gu)
    -- bound each of the terms using the estimates on previous derivatives (that use the inductive
    -- assumption for `g' âˆ˜ f`).
    _ â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i : â„) * (i ! * C * D ^ i) * D ^ (n - i + 1) := by
      gcongr with i hi
      Â· simp only [mul_assoc (n.choose i : â„)]
        exact I i hi
      Â· exact J i
    -- We are left with trivial algebraic manipulations to see that this is smaller than
    -- the claimed bound.
    _ = âˆ‘ i âˆˆ Finset.range (n + 1),
      -- Porting note: had to insert a few more explicit type ascriptions in this and similar
      -- expressions.
        (n ! : â„) * ((i ! : â„)â»Â¹ * i !) * C * (D ^ i * D ^ (n - i + 1)) * ((n - i)! : â„)â»Â¹ := by
      congr! 1 with i hi
      simp only [Nat.cast_choose â„ (Finset.mem_range_succ_iff.1 hi), div_eq_inv_mul, mul_inv]
      ring
    _ = âˆ‘ i âˆˆ Finset.range (n + 1), (n ! : â„) * 1 * C * D ^ (n + 1) * ((n - i)! : â„)â»Â¹ := by
      congr! with i hi
      Â· apply inv_mul_cancel
        simpa only [Ne, Nat.cast_eq_zero] using i.factorial_ne_zero
      Â· rw [â† pow_add]
        congr 1
        rw [Nat.add_succ, Nat.succ_inj']
        exact Nat.add_sub_of_le (Finset.mem_range_succ_iff.1 hi)
    _ â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1), (n ! : â„) * 1 * C * D ^ (n + 1) * 1 := by
      gcongr with i
      apply inv_le_one
      simpa only [Nat.one_le_cast] using (n - i).factorial_pos
    _ = (n + 1)! * C * D ^ (n + 1) := by
      simp only [mul_assoc, mul_one, Finset.sum_const, Finset.card_range, nsmul_eq_mul,
        Nat.factorial_succ, Nat.cast_mul]
#align norm_iterated_fderiv_within_comp_le_aux norm_iteratedFDerivWithin_comp_le_aux

/-- If the derivatives within a set of `g` at `f x` are bounded by `C`, and the `i`-th derivative
within a set of `f` at `x` is bounded by `D^i` for all `1 â‰¤ i â‰¤ n`, then the `n`-th derivative
of `g âˆ˜ f` is bounded by `n! * C * D^n`. -/
theorem norm_iteratedFDerivWithin_comp_le {g : F â†’ G} {f : E â†’ F} {n : â„•} {s : Set E} {t : Set F}
    {x : E} {N : â„•âˆ} (hg : ContDiffOn ğ•œ N g t) (hf : ContDiffOn ğ•œ N f s) (hn : (n : â„•âˆ) â‰¤ N)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hst : MapsTo f s t) (hx : x âˆˆ s) {C : â„}
    {D : â„} (hC : âˆ€ i, i â‰¤ n â†’ â€–iteratedFDerivWithin ğ•œ i g t (f x)â€– â‰¤ C)
    (hD : âˆ€ i, 1 â‰¤ i â†’ i â‰¤ n â†’ â€–iteratedFDerivWithin ğ•œ i f s xâ€– â‰¤ D ^ i) :
    â€–iteratedFDerivWithin ğ•œ n (g âˆ˜ f) s xâ€– â‰¤ n ! * C * D ^ n := by
  /- We reduce the bound to the case where all spaces live in the same universe (in which we
    already have proved the result), by using linear isometries between the spaces and their `ULift`
    to a common universe. These linear isometries preserve the norm of the iterated derivative. -/
  let Fu : Type max uF uG := ULift.{uG, uF} F
  let Gu : Type max uF uG := ULift.{uF, uG} G
  have isoF : Fu â‰ƒâ‚—áµ¢[ğ•œ] F := LinearIsometryEquiv.ulift ğ•œ F
  have isoG : Gu â‰ƒâ‚—áµ¢[ğ•œ] G := LinearIsometryEquiv.ulift ğ•œ G
  -- lift `f` and `g` to versions `fu` and `gu` on the lifted spaces.
  let fu : E â†’ Fu := isoF.symm âˆ˜ f
  let gu : Fu â†’ Gu := isoG.symm âˆ˜ g âˆ˜ isoF
  let tu := isoF â»Â¹' t
  have htu : UniqueDiffOn ğ•œ tu := isoF.toContinuousLinearEquiv.uniqueDiffOn_preimage_iff.2 ht
  have hstu : MapsTo fu s tu := fun y hy â†¦ by
    simpa only [fu, tu, mem_preimage, comp_apply, LinearIsometryEquiv.apply_symm_apply] using hst hy
  have Ffu : isoF (fu x) = f x := by
    simp only [fu, comp_apply, LinearIsometryEquiv.apply_symm_apply]
  -- All norms are preserved by the lifting process.
  have hfu : ContDiffOn ğ•œ n fu s := isoF.symm.contDiff.comp_contDiffOn (hf.of_le hn)
  have hgu : ContDiffOn ğ•œ n gu tu :=
    isoG.symm.contDiff.comp_contDiffOn
      ((hg.of_le hn).comp_continuousLinearMap (isoF : Fu â†’L[ğ•œ] F))
  have Nfu : âˆ€ i, â€–iteratedFDerivWithin ğ•œ i fu s xâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := fun i â†¦ by
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hs hx]
  simp_rw [â† Nfu] at hD
  have Ngu : âˆ€ i,
      â€–iteratedFDerivWithin ğ•œ i gu tu (fu x)â€– = â€–iteratedFDerivWithin ğ•œ i g t (f x)â€– := fun i â†¦ by
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ htu (hstu hx)]
    rw [LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right _ _ ht, Ffu]
    rw [Ffu]
    exact hst hx
  simp_rw [â† Ngu] at hC
  have Nfgu :
      â€–iteratedFDerivWithin ğ•œ n (g âˆ˜ f) s xâ€– = â€–iteratedFDerivWithin ğ•œ n (gu âˆ˜ fu) s xâ€– := by
    have : gu âˆ˜ fu = isoG.symm âˆ˜ g âˆ˜ f := by
      ext x
      simp only [fu, gu, comp_apply, LinearIsometryEquiv.map_eq_iff,
        LinearIsometryEquiv.apply_symm_apply]
    rw [this, LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left _ _ hs hx]
  -- deduce the required bound from the one for `gu âˆ˜ fu`.
  rw [Nfgu]
  exact norm_iteratedFDerivWithin_comp_le_aux hgu hfu htu hs hstu hx hC hD
#align norm_iterated_fderiv_within_comp_le norm_iteratedFDerivWithin_comp_le

/-- If the derivatives of `g` at `f x` are bounded by `C`, and the `i`-th derivative
of `f` at `x` is bounded by `D^i` for all `1 â‰¤ i â‰¤ n`, then the `n`-th derivative
of `g âˆ˜ f` is bounded by `n! * C * D^n`. -/
theorem norm_iteratedFDeriv_comp_le {g : F â†’ G} {f : E â†’ F} {n : â„•} {N : â„•âˆ} (hg : ContDiff ğ•œ N g)
    (hf : ContDiff ğ•œ N f) (hn : (n : â„•âˆ) â‰¤ N) (x : E) {C : â„} {D : â„}
    (hC : âˆ€ i, i â‰¤ n â†’ â€–iteratedFDeriv ğ•œ i g (f x)â€– â‰¤ C)
    (hD : âˆ€ i, 1 â‰¤ i â†’ i â‰¤ n â†’ â€–iteratedFDeriv ğ•œ i f xâ€– â‰¤ D ^ i) :
    â€–iteratedFDeriv ğ•œ n (g âˆ˜ f) xâ€– â‰¤ n ! * C * D ^ n := by
  simp_rw [â† iteratedFDerivWithin_univ] at hC hD âŠ¢
  exact norm_iteratedFDerivWithin_comp_le hg.contDiffOn hf.contDiffOn hn uniqueDiffOn_univ
    uniqueDiffOn_univ (mapsTo_univ _ _) (mem_univ x) hC hD
#align norm_iterated_fderiv_comp_le norm_iteratedFDeriv_comp_le

section Apply

theorem norm_iteratedFDerivWithin_clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F} {s : Set E} {x : E}
    {N : â„•âˆ} {n : â„•} (hf : ContDiffOn ğ•œ N f s) (hg : ContDiffOn ğ•œ N g s) (hs : UniqueDiffOn ğ•œ s)
    (hx : x âˆˆ s) (hn : â†‘n â‰¤ N) : â€–iteratedFDerivWithin ğ•œ n (fun y => (f y) (g y)) s xâ€– â‰¤
      âˆ‘ i âˆˆ Finset.range (n + 1), â†‘(n.choose i) * â€–iteratedFDerivWithin ğ•œ i f s xâ€– *
        â€–iteratedFDerivWithin ğ•œ (n - i) g s xâ€– := by
  let B : (F â†’L[ğ•œ] G) â†’L[ğ•œ] F â†’L[ğ•œ] G := ContinuousLinearMap.flip (ContinuousLinearMap.apply ğ•œ G)
  have hB : â€–Bâ€– â‰¤ 1 := by
    simp only [B, ContinuousLinearMap.opNorm_flip, ContinuousLinearMap.apply]
    refine ContinuousLinearMap.opNorm_le_bound _ zero_le_one fun f => ?_
    simp only [ContinuousLinearMap.coe_id', id, one_mul]
    rfl
  exact B.norm_iteratedFDerivWithin_le_of_bilinear_of_le_one hf hg hs hx hn hB
#align norm_iterated_fderiv_within_clm_apply norm_iteratedFDerivWithin_clm_apply

theorem norm_iteratedFDeriv_clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F} {N : â„•âˆ} {n : â„•}
    (hf : ContDiff ğ•œ N f) (hg : ContDiff ğ•œ N g) (x : E) (hn : â†‘n â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (fun y : E => (f y) (g y)) xâ€– â‰¤ âˆ‘ i âˆˆ Finset.range (n + 1),
      â†‘(n.choose i) * â€–iteratedFDeriv ğ•œ i f xâ€– * â€–iteratedFDeriv ğ•œ (n - i) g xâ€– := by
  simp only [â† iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_clm_apply hf.contDiffOn hg.contDiffOn uniqueDiffOn_univ
    (Set.mem_univ x) hn
#align norm_iterated_fderiv_clm_apply norm_iteratedFDeriv_clm_apply

theorem norm_iteratedFDerivWithin_clm_apply_const {f : E â†’ F â†’L[ğ•œ] G} {c : F} {s : Set E} {x : E}
    {N : â„•âˆ} {n : â„•} (hf : ContDiffOn ğ•œ N f s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (hn : â†‘n â‰¤ N) :
    â€–iteratedFDerivWithin ğ•œ n (fun y : E => (f y) c) s xâ€– â‰¤
      â€–câ€– * â€–iteratedFDerivWithin ğ•œ n f s xâ€– := by
  let g : (F â†’L[ğ•œ] G) â†’L[ğ•œ] G := ContinuousLinearMap.apply ğ•œ G c
  have h := g.norm_compContinuousMultilinearMap_le (iteratedFDerivWithin ğ•œ n f s x)
  rw [â† g.iteratedFDerivWithin_comp_left hf hs hx hn] at h
  refine h.trans (mul_le_mul_of_nonneg_right ?_ (norm_nonneg _))
  refine g.opNorm_le_bound (norm_nonneg _) fun f => ?_
  rw [ContinuousLinearMap.apply_apply, mul_comm]
  exact f.le_opNorm c
#align norm_iterated_fderiv_within_clm_apply_const norm_iteratedFDerivWithin_clm_apply_const

theorem norm_iteratedFDeriv_clm_apply_const {f : E â†’ F â†’L[ğ•œ] G} {c : F} {x : E} {N : â„•âˆ} {n : â„•}
    (hf : ContDiff ğ•œ N f) (hn : â†‘n â‰¤ N) :
    â€–iteratedFDeriv ğ•œ n (fun y : E => (f y) c) xâ€– â‰¤ â€–câ€– * â€–iteratedFDeriv ğ•œ n f xâ€– := by
  simp only [â† iteratedFDerivWithin_univ]
  exact norm_iteratedFDerivWithin_clm_apply_const hf.contDiffOn uniqueDiffOn_univ
    (Set.mem_univ x) hn
#align norm_iterated_fderiv_clm_apply_const norm_iteratedFDeriv_clm_apply_const

end Apply
