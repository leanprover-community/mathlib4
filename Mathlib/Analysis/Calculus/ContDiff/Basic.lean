/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Floris van Doorn
-/
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.ContDiff.FaaDiBruno
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Mul

/-!
# Higher differentiability of composition

We prove that the composition of `C^n` functions is `C^n`.
We also expand the API around `C^n` functions.

## Main results

* `ContDiff.comp` states that the composition of two `C^n` functions is `C^n`.

Similar results are given for `C^n` functions on domains.

## Notation

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `(âŠ¤ : â„•âˆ) : WithTop â„•âˆ` with `âˆ` and `âŠ¤ : WithTop â„•âˆ` with `Ï‰`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

noncomputable section

open scoped NNReal Nat ContDiff

universe u uE uF uG

attribute [local instance 1001]
  NormedAddCommGroup.toAddCommGroup AddCommGroup.toAddCommMonoid

open Set Fin Filter Function

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type uE} [NormedAddCommGroup E] [NormedSpace ğ•œ E] {F : Type uF}
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {X : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X] {s t : Set E} {f : E â†’ F}
  {g : F â†’ G} {x xâ‚€ : E} {b : E Ã— F â†’ G} {m n : WithTop â„•âˆ} {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Constants -/
section constants

theorem iteratedFDerivWithin_succ_const (n : â„•) (c : F) :
    iteratedFDerivWithin ğ•œ (n + 1) (fun _ : E â†¦ c) s = 0 := by
  induction n  with
  | zero =>
    ext1
    simp [iteratedFDerivWithin_succ_eq_comp_left, iteratedFDerivWithin_zero_eq_comp, comp_def]
  | succ n IH =>
    rw [iteratedFDerivWithin_succ_eq_comp_left, IH]
    simp only [Pi.zero_def, comp_def, fderivWithin_fun_const, map_zero]

@[simp]
theorem iteratedFDerivWithin_zero_fun {i : â„•} :
    iteratedFDerivWithin ğ•œ i (fun _ : E â†¦ (0 : F)) s = 0 := by
  cases i with
  | zero => ext; simp
  | succ i => apply iteratedFDerivWithin_succ_const

@[simp]
theorem iteratedFDeriv_zero_fun {n : â„•} : (iteratedFDeriv ğ•œ n fun _ : E â†¦ (0 : F)) = 0 :=
  funext fun x â†¦ by simp only [â† iteratedFDerivWithin_univ, iteratedFDerivWithin_zero_fun]

theorem contDiff_zero_fun : ContDiff ğ•œ n fun _ : E => (0 : F) :=
  analyticOnNhd_const.contDiff

/-- Constants are `C^âˆ`. -/
@[fun_prop]
theorem contDiff_const {c : F} : ContDiff ğ•œ n fun _ : E => c :=
  analyticOnNhd_const.contDiff

@[fun_prop]
theorem contDiffOn_const {c : F} {s : Set E} : ContDiffOn ğ•œ n (fun _ : E => c) s :=
  contDiff_const.contDiffOn

@[fun_prop]
theorem contDiffAt_const {c : F} : ContDiffAt ğ•œ n (fun _ : E => c) x :=
  contDiff_const.contDiffAt

@[fun_prop]
theorem contDiffWithinAt_const {c : F} : ContDiffWithinAt ğ•œ n (fun _ : E => c) s x :=
  contDiffAt_const.contDiffWithinAt

@[nontriviality]
theorem contDiff_of_subsingleton [Subsingleton F] : ContDiff ğ•œ n f := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiff_const

@[nontriviality]
theorem contDiffAt_of_subsingleton [Subsingleton F] : ContDiffAt ğ•œ n f x := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffAt_const

@[nontriviality]
theorem contDiffWithinAt_of_subsingleton [Subsingleton F] : ContDiffWithinAt ğ•œ n f s x := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffWithinAt_const

@[nontriviality]
theorem contDiffOn_of_subsingleton [Subsingleton F] : ContDiffOn ğ•œ n f s := by
  rw [Subsingleton.elim f fun _ => 0]; exact contDiffOn_const

theorem iteratedFDerivWithin_const_of_ne {n : â„•} (hn : n â‰  0) (c : F) (s : Set E) :
    iteratedFDerivWithin ğ•œ n (fun _ : E â†¦ c) s = 0 := by
  cases n with
  | zero => contradiction
  | succ n => exact iteratedFDerivWithin_succ_const n c

theorem iteratedFDeriv_const_of_ne {n : â„•} (hn : n â‰  0) (c : F) :
    (iteratedFDeriv ğ•œ n fun _ : E â†¦ c) = 0 := by
  simp only [â† iteratedFDerivWithin_univ, iteratedFDerivWithin_const_of_ne hn]

theorem iteratedFDeriv_succ_const (n : â„•) (c : F) :
    (iteratedFDeriv ğ•œ (n + 1) fun _ : E â†¦ c) = 0 :=
  iteratedFDeriv_const_of_ne (by simp) _

theorem contDiffWithinAt_singleton : ContDiffWithinAt ğ•œ n f {x} x :=
  (contDiffWithinAt_const (c := f x)).congr (by simp) rfl

end constants

/-! ### Smoothness of linear functions -/
section linear

/-- Unbundled bounded linear functions are `C^n`. -/
theorem IsBoundedLinearMap.contDiff (hf : IsBoundedLinearMap ğ•œ f) : ContDiff ğ•œ n f :=
  (ContinuousLinearMap.analyticOnNhd hf.toContinuousLinearMap univ).contDiff

@[fun_prop]
theorem ContinuousLinearMap.contDiff (f : E â†’L[ğ•œ] F) : ContDiff ğ•œ n f :=
  f.isBoundedLinearMap.contDiff

@[fun_prop]
theorem ContinuousLinearEquiv.contDiff (f : E â‰ƒL[ğ•œ] F) : ContDiff ğ•œ n f :=
  (f : E â†’L[ğ•œ] F).contDiff

@[fun_prop]
theorem LinearIsometry.contDiff (f : E â†’â‚—áµ¢[ğ•œ] F) : ContDiff ğ•œ n f :=
  f.toContinuousLinearMap.contDiff

@[fun_prop]
theorem LinearIsometryEquiv.contDiff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) : ContDiff ğ•œ n f :=
  (f : E â†’L[ğ•œ] F).contDiff

/-- The identity is `C^n`. -/
theorem contDiff_id : ContDiff ğ•œ n (id : E â†’ E) :=
  IsBoundedLinearMap.id.contDiff

@[fun_prop]
theorem contDiff_fun_id : ContDiff ğ•œ n (fun x : E => x) :=
  IsBoundedLinearMap.id.contDiff

theorem contDiffWithinAt_id {s x} : ContDiffWithinAt ğ•œ n (id : E â†’ E) s x :=
  contDiff_id.contDiffWithinAt

@[fun_prop]
theorem contDiffWithinAt_fun_id {s x} : ContDiffWithinAt ğ•œ n (fun x : E => x) s x :=
  contDiff_id.contDiffWithinAt

theorem contDiffAt_id {x} : ContDiffAt ğ•œ n (id : E â†’ E) x :=
  contDiff_id.contDiffAt

@[fun_prop]
theorem contDiffAt_fun_id {x} : ContDiffAt ğ•œ n (fun x : E => x) x :=
  contDiff_id.contDiffAt

theorem contDiffOn_id {s} : ContDiffOn ğ•œ n (id : E â†’ E) s :=
  contDiff_id.contDiffOn

@[fun_prop]
theorem contDiffOn_fun_id {s} : ContDiffOn ğ•œ n (fun x : E => x) s :=
  contDiff_id.contDiffOn

/-- Bilinear functions are `C^n`. -/
theorem IsBoundedBilinearMap.contDiff (hb : IsBoundedBilinearMap ğ•œ b) : ContDiff ğ•œ n b :=
  (hb.toContinuousLinearMap.analyticOnNhd_bilinear _).contDiff

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `g âˆ˜ f` admits a Taylor
series whose `k`-th term is given by `g âˆ˜ (p k)`. -/
theorem HasFTaylorSeriesUpToOn.continuousLinearMap_comp {n : WithTop â„•âˆ} (g : F â†’L[ğ•œ] G)
    (hf : HasFTaylorSeriesUpToOn n f p s) :
    HasFTaylorSeriesUpToOn n (g âˆ˜ f) (fun x k => g.compContinuousMultilinearMap (p x k)) s where
  zero_eq x hx := congr_arg g (hf.zero_eq x hx)
  fderivWithin m hm x hx := (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
    (fun _ : Fin m => E) F G g).hasFDerivAt.comp_hasFDerivWithinAt x (hf.fderivWithin m hm x hx)
  cont m hm := (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
    (fun _ : Fin m => E) F G g).continuous.comp_continuousOn (hf.cont m hm)

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
theorem ContDiffWithinAt.continuousLinearMap_comp (g : F â†’L[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    refine âŸ¨u, hu, _, hp.continuousLinearMap_comp g, fun i â†¦ ?_âŸ©
    change AnalyticOn ğ•œ
      (fun x â†¦ (ContinuousLinearMap.compContinuousMultilinearMapL ğ•œ
      (fun _ : Fin i â†¦ E) F G g) (p x i)) u
    apply AnalyticOnNhd.comp_analyticOn _ (h'p i) (Set.mapsTo_univ _ _)
    exact ContinuousLinearMap.analyticOnNhd _ _
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    exact âŸ¨u, hu, _, hp.continuousLinearMap_comp gâŸ©

/-- Composition by continuous linear maps on the left preserves `C^n` functions in a domain
at a point. -/
theorem ContDiffAt.continuousLinearMap_comp (g : F â†’L[ğ•œ] G) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (g âˆ˜ f) x :=
  ContDiffWithinAt.continuousLinearMap_comp g hf

/-- Composition by continuous linear maps on the left preserves `C^n` functions on domains. -/
theorem ContDiffOn.continuousLinearMap_comp (g : F â†’L[ğ•œ] G) (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (g âˆ˜ f) s := fun x hx => (hf x hx).continuousLinearMap_comp g

/-- Composition by continuous linear maps on the left preserves `C^n` functions. -/
theorem ContDiff.continuousLinearMap_comp {f : E â†’ F} (g : F â†’L[ğ•œ] G) (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n fun x => g (f x) :=
  contDiffOn_univ.1 <| ContDiffOn.continuousLinearMap_comp _ (contDiffOn_univ.2 hf)

/-- The iterated derivative within a set of the composition with a linear map on the left is
obtained by applying the linear map to the iterated derivative. -/
theorem ContinuousLinearMap.iteratedFDerivWithin_comp_left {f : E â†’ F} (g : F â†’L[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      g.compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) := by
  rcases hf.contDiffOn' hi (by simp) with âŸ¨U, hU, hxU, hfUâŸ©
  rw [â† iteratedFDerivWithin_inter_open hU hxU, â† iteratedFDerivWithin_inter_open (f := f) hU hxU]
  rw [insert_eq_of_mem hx] at hfU
  exact .symm <| (hfU.ftaylorSeriesWithin (hs.inter hU)).continuousLinearMap_comp g
    |>.eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter hU) âŸ¨hx, hxUâŸ©

/-- The iterated derivative of the composition with a linear map on the left is
obtained by applying the linear map to the iterated derivative. -/
theorem ContinuousLinearMap.iteratedFDeriv_comp_left {f : E â†’ F} (g : F â†’L[ğ•œ] G)
    (hf : ContDiffAt ğ•œ n f x) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) x = g.compContinuousMultilinearMap (iteratedFDeriv ğ•œ i f x) := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.iteratedFDerivWithin_comp_left hf.contDiffWithinAt uniqueDiffOn_univ (mem_univ x) hi

/-- The iterated derivative within a set of the composition with a linear equiv on the left is
obtained by applying the linear equiv to the iterated derivative. This is true without
differentiability assumptions. -/
theorem ContinuousLinearEquiv.iteratedFDerivWithin_comp_left (g : F â‰ƒL[ğ•œ] G) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (i : â„•) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (g : F â†’L[ğ•œ] G).compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) := by
  induction i generalizing x with ext1 m
  | zero =>
    simp only [iteratedFDerivWithin_zero_apply, comp_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, coe_coe]
  | succ i IH =>
    rw [iteratedFDerivWithin_succ_apply_left]
    have Z : fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s) s x =
        fderivWithin ğ•œ (g.continuousMultilinearMapCongrRight (fun _ : Fin i => E) âˆ˜
          iteratedFDerivWithin ğ•œ i f s) s x :=
      fderivWithin_congr' (@IH) hx
    simp_rw [Z]
    rw [(g.continuousMultilinearMapCongrRight fun _ : Fin i => E).comp_fderivWithin (hs x hx)]
    simp only [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, comp_apply,
      ContinuousLinearEquiv.continuousMultilinearMapCongrRight_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, EmbeddingLike.apply_eq_iff_eq]
    rw [iteratedFDerivWithin_succ_apply_left]

/-- Iterated derivatives commute with left composition by continuous linear equivalences. -/
theorem ContinuousLinearEquiv.iteratedFDeriv_comp_left {f : E â†’ F} {x : E} (g : F â‰ƒL[ğ•œ] G) {i : â„•} :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) x =
      g.toContinuousLinearMap.compContinuousMultilinearMap (iteratedFDeriv ğ•œ i f x) := by
  simp only [â† iteratedFDerivWithin_univ]
  apply g.iteratedFDerivWithin_comp_left f uniqueDiffOn_univ trivial

/-- Composition with a linear isometry on the left preserves the norm of the iterated
derivative within a set. -/
theorem LinearIsometry.norm_iteratedFDerivWithin_comp_left {f : E â†’ F} (g : F â†’â‚—áµ¢[ğ•œ] G)
    (hf : ContDiffWithinAt ğ•œ n f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    â€–iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s xâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := by
  have :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      g.toContinuousLinearMap.compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) :=
    g.toContinuousLinearMap.iteratedFDerivWithin_comp_left hf hs hx hi
  rw [this]
  apply LinearIsometry.norm_compContinuousMultilinearMap

/-- Composition with a linear isometry on the left preserves the norm of the iterated
derivative. -/
theorem LinearIsometry.norm_iteratedFDeriv_comp_left {f : E â†’ F} (g : F â†’â‚—áµ¢[ğ•œ] G)
    (hf : ContDiffAt ğ•œ n f x) {i : â„•} (hi : i â‰¤ n) :
    â€–iteratedFDeriv ğ•œ i (g âˆ˜ f) xâ€– = â€–iteratedFDeriv ğ•œ i f xâ€– := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.norm_iteratedFDerivWithin_comp_left hf.contDiffWithinAt uniqueDiffOn_univ (mem_univ x) hi

/-- Composition with a linear isometry equiv on the left preserves the norm of the iterated
derivative within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_left (g : F â‰ƒâ‚—áµ¢[ğ•œ] G) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (i : â„•) :
    â€–iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s xâ€– = â€–iteratedFDerivWithin ğ•œ i f s xâ€– := by
  have :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (g : F â†’L[ğ•œ] G).compContinuousMultilinearMap (iteratedFDerivWithin ğ•œ i f s x) :=
    g.toContinuousLinearEquiv.iteratedFDerivWithin_comp_left f hs hx i
  rw [this]
  apply LinearIsometry.norm_compContinuousMultilinearMap g.toLinearIsometry

/-- Composition with a linear isometry equiv on the left preserves the norm of the iterated
derivative. -/
theorem LinearIsometryEquiv.norm_iteratedFDeriv_comp_left (g : F â‰ƒâ‚—áµ¢[ğ•œ] G) (f : E â†’ F) (x : E)
    (i : â„•) : â€–iteratedFDeriv ğ•œ i (g âˆ˜ f) xâ€– = â€–iteratedFDeriv ğ•œ i f xâ€– := by
  rw [â† iteratedFDerivWithin_univ, â† iteratedFDerivWithin_univ]
  apply g.norm_iteratedFDerivWithin_comp_left f uniqueDiffOn_univ (mem_univ x) i

/-- Composition by continuous linear equivs on the left respects higher differentiability at a
point in a domain. -/
theorem ContinuousLinearEquiv.comp_contDiffWithinAt_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffWithinAt ğ•œ n (e âˆ˜ f) s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H => by
    simpa only [Function.comp_def, e.symm.coe_coe, e.symm_apply_apply] using
      H.continuousLinearMap_comp (e.symm : G â†’L[ğ•œ] F),
    fun H => H.continuousLinearMap_comp (e : F â†’L[ğ•œ] G)âŸ©

/-- Composition by continuous linear equivs on the left respects higher differentiability at a
point. -/
theorem ContinuousLinearEquiv.comp_contDiffAt_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffAt ğ•œ n (e âˆ˜ f) x â†” ContDiffAt ğ•œ n f x := by
  simp only [â† contDiffWithinAt_univ, e.comp_contDiffWithinAt_iff]

/-- Composition by continuous linear equivs on the left respects higher differentiability on
domains. -/
theorem ContinuousLinearEquiv.comp_contDiffOn_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiffOn ğ•œ n (e âˆ˜ f) s â†” ContDiffOn ğ•œ n f s := by
  simp [ContDiffOn, e.comp_contDiffWithinAt_iff]

/-- Composition by continuous linear equivs on the left respects higher differentiability. -/
theorem ContinuousLinearEquiv.comp_contDiff_iff (e : F â‰ƒL[ğ•œ] G) :
    ContDiff ğ•œ n (e âˆ˜ f) â†” ContDiff ğ•œ n f := by
  simp only [â† contDiffOn_univ, e.comp_contDiffOn_iff]

/-- If `f` admits a Taylor series `p` in a set `s`, and `g` is linear, then `f âˆ˜ g` admits a Taylor
series in `g â»Â¹' s`, whose `k`-th term is given by `p k (g vâ‚, ..., g vâ‚–)` . -/
theorem HasFTaylorSeriesUpToOn.compContinuousLinearMap
    (hf : HasFTaylorSeriesUpToOn n f p s) (g : G â†’L[ğ•œ] E) :
    HasFTaylorSeriesUpToOn n (f âˆ˜ g) (fun x k => (p (g x) k).compContinuousLinearMap fun _ => g)
      (g â»Â¹' s) := by
  let A : âˆ€ m : â„•, (E[Ã—m]â†’L[ğ•œ] F) â†’ G[Ã—m]â†’L[ğ•œ] F := fun m h => h.compContinuousLinearMap fun _ => g
  have hA : âˆ€ m, IsBoundedLinearMap ğ•œ (A m) := fun m =>
    isBoundedLinearMap_continuousMultilinearMap_comp_linear g
  constructor
  Â· intro x hx
    simp only [(hf.zero_eq (g x) hx).symm, Function.comp_apply]
    change (p (g x) 0 fun _ : Fin 0 => g 0) = p (g x) 0 0
    rw [ContinuousLinearMap.map_zero]
    rfl
  Â· intro m hm x hx
    convert (hA m).hasFDerivAt.comp_hasFDerivWithinAt x
        ((hf.fderivWithin m hm (g x) hx).comp x g.hasFDerivWithinAt (Subset.refl _))
    ext y v
    change p (g x) (Nat.succ m) (g âˆ˜ cons y v) = p (g x) m.succ (cons (g y) (g âˆ˜ v))
    rw [comp_cons]
  Â· intro m hm
    exact (hA m).continuous.comp_continuousOn <| (hf.cont m hm).comp g.continuous.continuousOn <|
      Subset.refl _

/-- Composition by continuous linear maps on the right preserves `C^n` functions at a point on
a domain. -/
theorem ContDiffWithinAt.comp_continuousLinearMap {x : G} (g : G â†’L[ğ•œ] E)
    (hf : ContDiffWithinAt ğ•œ n f s (g x)) : ContDiffWithinAt ğ•œ n (f âˆ˜ g) (g â»Â¹' s) x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    refine âŸ¨g â»Â¹' u, ?_, _, hp.compContinuousLinearMap g, ?_âŸ©
    Â· refine g.continuous.continuousWithinAt.tendsto_nhdsWithin ?_ hu
      exact (mapsTo_singleton.2 <| mem_singleton _).union_union (mapsTo_preimage _ _)
    Â· intro i
      change AnalyticOn ğ•œ (fun x â†¦
        ContinuousMultilinearMap.compContinuousLinearMapL (fun _ â†¦ g) (p (g x) i)) (â‡‘g â»Â¹' u)
      apply AnalyticOn.comp _ _ (Set.mapsTo_univ _ _)
      Â· exact ContinuousLinearMap.analyticOn _ _
      Â· exact (h'p i).comp (g.analyticOn _) (mapsTo_preimage _ _)
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    refine âŸ¨g â»Â¹' u, ?_, _, hp.compContinuousLinearMap gâŸ©
    refine g.continuous.continuousWithinAt.tendsto_nhdsWithin ?_ hu
    exact (mapsTo_singleton.2 <| mem_singleton _).union_union (mapsTo_preimage _ _)

/-- Composition by continuous linear maps on the right preserves `C^n` functions on domains. -/
theorem ContDiffOn.comp_continuousLinearMap (hf : ContDiffOn ğ•œ n f s) (g : G â†’L[ğ•œ] E) :
    ContDiffOn ğ•œ n (f âˆ˜ g) (g â»Â¹' s) := fun x hx => (hf (g x) hx).comp_continuousLinearMap g

/-- Composition by continuous linear maps on the right preserves `C^n` functions. -/
theorem ContDiff.comp_continuousLinearMap {f : E â†’ F} {g : G â†’L[ğ•œ] E} (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n (f âˆ˜ g) :=
  contDiffOn_univ.1 <| ContDiffOn.comp_continuousLinearMap (contDiffOn_univ.2 hf) _

/-- The iterated derivative within a set of the composition with a linear map on the right is
obtained by composing the iterated derivative with the linear map. -/
theorem ContinuousLinearMap.iteratedFDerivWithin_comp_right {f : E â†’ F} (g : G â†’L[ğ•œ] E)
    (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) (h's : UniqueDiffOn ğ•œ (g â»Â¹' s)) {x : G}
    (hx : g x âˆˆ s) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g :=
  ((((hf.of_le hi).ftaylorSeriesWithin hs).compContinuousLinearMap
    g).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl h's hx).symm

/-- The iterated derivative within a set of the composition with a linear equiv on the right is
obtained by composing the iterated derivative with the linear equiv. -/
theorem ContinuousLinearEquiv.iteratedFDerivWithin_comp_right (g : G â‰ƒL[ğ•œ] E) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) {x : G} (hx : g x âˆˆ s) (i : â„•) :
    iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g := by
  induction i generalizing x with ext1 m
  | zero =>
    simp only [iteratedFDerivWithin_zero_apply, comp_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply]
  | succ i IH =>
    simp only [ContinuousMultilinearMap.compContinuousLinearMap_apply,
      ContinuousLinearEquiv.coe_coe, iteratedFDerivWithin_succ_apply_left]
    have : fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s)) (g â»Â¹' s) x =
        fderivWithin ğ•œ
          (ContinuousLinearEquiv.continuousMultilinearMapCongrLeft _ (fun _x : Fin i => g) âˆ˜
            (iteratedFDerivWithin ğ•œ i f s âˆ˜ g)) (g â»Â¹' s) x :=
      fderivWithin_congr' (@IH) hx
    rw [this, ContinuousLinearEquiv.comp_fderivWithin _ (g.uniqueDiffOn_preimage_iff.2 hs x hx)]
    simp only [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, comp_apply,
      ContinuousLinearEquiv.continuousMultilinearMapCongrLeft_apply,
      ContinuousMultilinearMap.compContinuousLinearMap_apply]
    rw [ContinuousLinearEquiv.comp_right_fderivWithin _ (g.uniqueDiffOn_preimage_iff.2 hs x hx),
      ContinuousLinearMap.coe_comp', coe_coe, comp_apply, tail_def, tail_def]

/-- The iterated derivative of the composition with a linear map on the right is
obtained by composing the iterated derivative with the linear map. -/
theorem ContinuousLinearMap.iteratedFDeriv_comp_right (g : G â†’L[ğ•œ] E) {f : E â†’ F}
    (hf : ContDiff ğ•œ n f) (x : G) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (f âˆ˜ g) x =
      (iteratedFDeriv ğ•œ i f (g x)).compContinuousLinearMap fun _ => g := by
  simp only [â† iteratedFDerivWithin_univ]
  exact g.iteratedFDerivWithin_comp_right hf.contDiffOn uniqueDiffOn_univ uniqueDiffOn_univ
      (mem_univ _) hi

/-- Composition with a linear isometry on the right preserves the norm of the iterated derivative
within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDerivWithin_comp_right (g : G â‰ƒâ‚—áµ¢[ğ•œ] E) (f : E â†’ F)
    (hs : UniqueDiffOn ğ•œ s) {x : G} (hx : g x âˆˆ s) (i : â„•) :
    â€–iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) xâ€– = â€–iteratedFDerivWithin ğ•œ i f s (g x)â€– := by
  have : iteratedFDerivWithin ğ•œ i (f âˆ˜ g) (g â»Â¹' s) x =
      (iteratedFDerivWithin ğ•œ i f s (g x)).compContinuousLinearMap fun _ => g :=
    g.toContinuousLinearEquiv.iteratedFDerivWithin_comp_right f hs hx i
  rw [this, ContinuousMultilinearMap.norm_compContinuous_linearIsometryEquiv]

/-- Composition with a linear isometry on the right preserves the norm of the iterated derivative
within a set. -/
theorem LinearIsometryEquiv.norm_iteratedFDeriv_comp_right (g : G â‰ƒâ‚—áµ¢[ğ•œ] E) (f : E â†’ F) (x : G)
    (i : â„•) : â€–iteratedFDeriv ğ•œ i (f âˆ˜ g) xâ€– = â€–iteratedFDeriv ğ•œ i f (g x)â€– := by
  simp only [â† iteratedFDerivWithin_univ]
  apply g.norm_iteratedFDerivWithin_comp_right f uniqueDiffOn_univ (mem_univ (g x)) i

/-- Composition by continuous linear equivs on the right respects higher differentiability at a
point in a domain. -/
theorem ContinuousLinearEquiv.contDiffWithinAt_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffWithinAt ğ•œ n (f âˆ˜ e) (e â»Â¹' s) (e.symm x) â†” ContDiffWithinAt ğ•œ n f s x := by
  constructor
  Â· intro H
    simpa [â† preimage_comp, Function.comp_def] using H.comp_continuousLinearMap (e.symm : E â†’L[ğ•œ] G)
  Â· intro H
    rw [â† e.apply_symm_apply x, â† e.coe_coe] at H
    exact H.comp_continuousLinearMap _

/-- Composition by continuous linear equivs on the right respects higher differentiability at a
point. -/
theorem ContinuousLinearEquiv.contDiffAt_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffAt ğ•œ n (f âˆ˜ e) (e.symm x) â†” ContDiffAt ğ•œ n f x := by
  rw [â† contDiffWithinAt_univ, â† contDiffWithinAt_univ, â† preimage_univ]
  exact e.contDiffWithinAt_comp_iff

/-- Composition by continuous linear equivs on the right respects higher differentiability on
domains. -/
theorem ContinuousLinearEquiv.contDiffOn_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiffOn ğ•œ n (f âˆ˜ e) (e â»Â¹' s) â†” ContDiffOn ğ•œ n f s :=
  âŸ¨fun H => by simpa [Function.comp_def] using H.comp_continuousLinearMap (e.symm : E â†’L[ğ•œ] G),
    fun H => H.comp_continuousLinearMap (e : G â†’L[ğ•œ] E)âŸ©

/-- Composition by continuous linear equivs on the right respects higher differentiability. -/
theorem ContinuousLinearEquiv.contDiff_comp_iff (e : G â‰ƒL[ğ•œ] E) :
    ContDiff ğ•œ n (f âˆ˜ e) â†” ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† contDiffOn_univ, â† preimage_univ]
  exact e.contDiffOn_comp_iff

end linear

/-! ### The Cartesian product of two C^n functions is C^n. -/
section prod

/-- If two functions `f` and `g` admit Taylor series `p` and `q` in a set `s`, then the Cartesian
product of `f` and `g` admits the Cartesian product of `p` and `q` as a Taylor series. -/
theorem HasFTaylorSeriesUpToOn.prodMk {n : WithTop â„•âˆ}
    (hf : HasFTaylorSeriesUpToOn n f p s) {g : E â†’ G}
    {q : E â†’ FormalMultilinearSeries ğ•œ E G} (hg : HasFTaylorSeriesUpToOn n g q s) :
    HasFTaylorSeriesUpToOn n (fun y => (f y, g y)) (fun y k => (p y k).prod (q y k)) s := by
  set L := fun m => ContinuousMultilinearMap.prodL ğ•œ (fun _ : Fin m => E) F G
  constructor
  Â· intro x hx; rw [â† hf.zero_eq x hx, â† hg.zero_eq x hx]; rfl
  Â· intro m hm x hx
    convert (L m).hasFDerivAt.comp_hasFDerivWithinAt x
        ((hf.fderivWithin m hm x hx).prodMk (hg.fderivWithin m hm x hx))
  Â· intro m hm
    exact (L m).continuous.comp_continuousOn ((hf.cont m hm).prodMk (hg.cont m hm))

@[deprecated (since := "2025-03-09")]
alias HasFTaylorSeriesUpToOn.prod := HasFTaylorSeriesUpToOn.prodMk

/-- The Cartesian product of `C^n` functions at a point in a domain is `C^n`. -/
@[fun_prop]
theorem ContDiffWithinAt.prodMk {s : Set E} {f : E â†’ F} {g : E â†’ G}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    ContDiffWithinAt ğ•œ n (fun x : E => (f x, g x)) s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := hf
    obtain âŸ¨v, hv, q, hq, h'qâŸ© := hg
    refine âŸ¨u âˆ© v, Filter.inter_mem hu hv, _,
      (hp.mono inter_subset_left).prodMk (hq.mono inter_subset_right), fun i â†¦ ?_âŸ©
    change AnalyticOn ğ•œ (fun x â†¦ ContinuousMultilinearMap.prodL _ _ _ _ (p x i, q x i)) (u âˆ© v)
    apply (LinearIsometryEquiv.analyticOnNhd _ _).comp_analyticOn _ (Set.mapsTo_univ _ _)
    exact ((h'p i).mono inter_subset_left).prod ((h'q i).mono inter_subset_right)
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    rcases hg m hm with âŸ¨v, hv, q, hqâŸ©
    exact âŸ¨u âˆ© v, Filter.inter_mem hu hv, _,
      (hp.mono inter_subset_left).prodMk (hq.mono inter_subset_right)âŸ©

@[deprecated (since := "2025-03-09")]
alias ContDiffWithinAt.prod := ContDiffWithinAt.prodMk

/-- The Cartesian product of `C^n` functions on domains is `C^n`. -/
@[fun_prop]
theorem ContDiffOn.prodMk {s : Set E} {f : E â†’ F} {g : E â†’ G} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x : E => (f x, g x)) s := fun x hx =>
  (hf x hx).prodMk (hg x hx)

@[deprecated (since := "2025-03-09")]
alias ContDiffOn.prod := ContDiffOn.prodMk

/-- The Cartesian product of `C^n` functions at a point is `C^n`. -/
@[fun_prop]
theorem ContDiffAt.prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) : ContDiffAt ğ•œ n (fun x : E => (f x, g x)) x :=
  contDiffWithinAt_univ.1 <| hf.contDiffWithinAt.prodMk hg.contDiffWithinAt

@[deprecated (since := "2025-03-09")]
alias ContDiffAt.prod := ContDiffAt.prodMk

/-- The Cartesian product of `C^n` functions is `C^n`. -/
@[fun_prop]
theorem ContDiff.prodMk {f : E â†’ F} {g : E â†’ G} (hf : ContDiff ğ•œ n f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x : E => (f x, g x) :=
  contDiffOn_univ.1 <| hf.contDiffOn.prodMk hg.contDiffOn

@[deprecated (since := "2025-03-09")]
alias ContDiff.prod := ContDiff.prodMk

end prod

/-! ### Being `C^k` on a union of open sets can be tested on each set -/
section contDiffOn_union

/-- If a function is `C^k` on two open sets, it is also `C^n` on their union. -/
lemma ContDiffOn.union_of_isOpen (hf : ContDiffOn ğ•œ n f s) (hf' : ContDiffOn ğ•œ n f t)
    (hs : IsOpen s) (ht : IsOpen t) :
    ContDiffOn ğ•œ n f (s âˆª t) := by
  rintro x (hx | hx)
  Â· exact (hf x hx).contDiffAt (hs.mem_nhds hx) |>.contDiffWithinAt
  Â· exact (hf' x hx).contDiffAt (ht.mem_nhds hx) |>.contDiffWithinAt

/-- A function is `C^k` on two open sets iff it is `C^k` on their union. -/
lemma contDiffOn_union_iff_of_isOpen (hs : IsOpen s) (ht : IsOpen t) :
    ContDiffOn ğ•œ n f (s âˆª t) â†” ContDiffOn ğ•œ n f s âˆ§ ContDiffOn ğ•œ n f t :=
  âŸ¨fun h â†¦ âŸ¨h.mono subset_union_left, h.mono subset_union_rightâŸ©,
   fun âŸ¨hfs, hftâŸ© â†¦ ContDiffOn.union_of_isOpen hfs hft hs htâŸ©

lemma contDiff_of_contDiffOn_union_of_isOpen (hf : ContDiffOn ğ•œ n f s)
    (hf' : ContDiffOn ğ•œ n f t) (hst : s âˆª t = univ) (hs : IsOpen s) (ht : IsOpen t) :
    ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† hst]
  exact hf.union_of_isOpen hf' hs ht

/-- If a function is `C^k` on open sets `s i`, it is `C^k` on their union -/
lemma ContDiffOn.iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i)) (hs : âˆ€ i, IsOpen (s i)) :
    ContDiffOn ğ•œ n f (â‹ƒ i, s i) := by
  rintro x âŸ¨si, âŸ¨i, rflâŸ©, hxsiâŸ©
  exact (hf i).contDiffAt ((hs i).mem_nhds hxsi) |>.contDiffWithinAt

/-- A function is `C^k` on a union of open sets `s i` iff it is `C^k` on each `s i`. -/
lemma contDiffOn_iUnion_iff_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hs : âˆ€ i, IsOpen (s i)) :
    ContDiffOn ğ•œ n f (â‹ƒ i, s i) â†” âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i) :=
  âŸ¨fun h i â†¦ h.mono <| subset_iUnion_of_subset i fun _ a â†¦ a,
   fun h â†¦ ContDiffOn.iUnion_of_isOpen h hsâŸ©

lemma contDiff_of_contDiffOn_iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, ContDiffOn ğ•œ n f (s i)) (hs : âˆ€ i, IsOpen (s i)) (hs' : â‹ƒ i, s i = univ) :
    ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ, â† hs']
  exact ContDiffOn.iUnion_of_isOpen hf hs

end contDiffOn_union

section comp

/-!
### Composition of `C^n` functions

We show that the composition of `C^n` functions is `C^n`. One way to do this would be to
use the following simple inductive proof. Assume it is done for `n`.
Then, to check it for `n+1`, one needs to check that the derivative of `g âˆ˜ f` is `C^n`, i.e.,
that `Dg(f x) â¬ Df(x)` is `C^n`. The term `Dg (f x)` is the composition of two `C^n` functions, so
it is `C^n` by the inductive assumption. The term `Df(x)` is also `C^n`. Then, the matrix
multiplication is the application of a bilinear map (which is `C^âˆ`, and therefore `C^n`) to
`x â†¦ (Dg(f x), Df x)`. As the composition of two `C^n` maps, it is again `C^n`, and we are done.

There are two difficulties in this proof.

The first one is that it is an induction over all Banach
spaces. In Lean, this is only possible if they belong to a fixed universe. One could formalize this
by first proving the statement in this case, and then extending the result to general universes
by embedding all the spaces we consider in a common universe through `ULift`.

The second one is that it does not work cleanly for analytic maps: for this case, we need to
exhibit a whole sequence of derivatives which are all analytic, not just finitely many of them, so
an induction is never enough at a finite step.

Both these difficulties can be overcome with some cost. However, we choose a different path: we
write down an explicit formula for the `n`-th derivative of `g âˆ˜ f` in terms of derivatives of
`g` and `f` (this is the formula of Faa-Di Bruno) and use this formula to get a suitable Taylor
expansion for `g âˆ˜ f`. Writing down the formula of Faa-Di Bruno is not easy as the formula is quite
intricate, but it is also useful for other purposes and once available it makes the proof here
essentially trivial.
-/

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
theorem ContDiffWithinAt.comp {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x) (st : MapsTo f s t) :
    ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  match n with
  | Ï‰ =>
    have h'f : ContDiffWithinAt ğ•œ Ï‰ f s x := hf
    obtain âŸ¨u, hu, p, hp, h'pâŸ© := h'f
    obtain âŸ¨v, hv, q, hq, h'qâŸ© := hg
    let w := insert x s âˆ© (u âˆ© f â»Â¹' v)
    have wv : w âŠ† f â»Â¹' v := fun y hy => hy.2.2
    have wu : w âŠ† u := fun y hy => hy.2.1
    refine âŸ¨w, ?_, fun y â†¦ (q (f y)).taylorComp (p y), hq.comp (hp.mono wu) wv, ?_âŸ©
    Â· apply inter_mem self_mem_nhdsWithin (inter_mem hu ?_)
      apply (continuousWithinAt_insert_self.2 hf.continuousWithinAt).preimage_mem_nhdsWithin'
      apply nhdsWithin_mono _ _ hv
      simp only [image_insert_eq]
      apply insert_subset_insert
      exact image_subset_iff.mpr st
    Â· have : AnalyticOn ğ•œ f w := by
        have : AnalyticOn ğ•œ (fun y â†¦ (continuousMultilinearCurryFin0 ğ•œ E F).symm (f y)) w :=
          ((h'p 0).mono wu).congr  fun y hy â†¦ (hp.zero_eq' (wu hy)).symm
        have : AnalyticOn ğ•œ (fun y â†¦ (continuousMultilinearCurryFin0 ğ•œ E F)
            ((continuousMultilinearCurryFin0 ğ•œ E F).symm (f y))) w :=
          AnalyticOnNhd.comp_analyticOn (LinearIsometryEquiv.analyticOnNhd _ _ ) this
          (mapsTo_univ _ _)
        simpa using this
      exact analyticOn_taylorComp h'q (fun n â†¦ (h'p n).mono wu) this wv
  | (n : â„•âˆ) =>
    intro m hm
    rcases hf m hm with âŸ¨u, hu, p, hpâŸ©
    rcases hg m hm with âŸ¨v, hv, q, hqâŸ©
    let w := insert x s âˆ© (u âˆ© f â»Â¹' v)
    have wv : w âŠ† f â»Â¹' v := fun y hy => hy.2.2
    have wu : w âŠ† u := fun y hy => hy.2.1
    refine âŸ¨w, ?_, fun y â†¦ (q (f y)).taylorComp (p y), hq.comp (hp.mono wu) wvâŸ©
    apply inter_mem self_mem_nhdsWithin (inter_mem hu ?_)
    apply (continuousWithinAt_insert_self.2 hf.continuousWithinAt).preimage_mem_nhdsWithin'
    apply nhdsWithin_mono _ _ hv
    simp only [image_insert_eq]
    apply insert_subset_insert
    exact image_subset_iff.mpr st

/-- The composition of `C^n` functions on domains is `C^n`. -/
theorem ContDiffOn.comp {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (hg : ContDiffOn ğ•œ n g t)
    (hf : ContDiffOn ğ•œ n f s) (st : MapsTo f s t) : ContDiffOn ğ•œ n (g âˆ˜ f) s :=
  fun x hx â†¦ ContDiffWithinAt.comp x (hg (f x) (st hx)) (hf x hx) st

/-- The composition of `C^n` functions on domains is `C^n`. -/
theorem ContDiffOn.comp_inter
    {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (hg : ContDiffOn ğ•œ n g t)
    (hf : ContDiffOn ğ•œ n f s) : ContDiffOn ğ•œ n (g âˆ˜ f) (s âˆ© f â»Â¹' t) :=
  hg.comp (hf.mono inter_subset_left) inter_subset_right

/-- The composition of a `C^n` function on a domain with a `C^n` function is `C^n`. -/
theorem ContDiff.comp_contDiffOn {s : Set E} {g : F â†’ G} {f : E â†’ F} (hg : ContDiff ğ•œ n g)
    (hf : ContDiffOn ğ•œ n f s) : ContDiffOn ğ•œ n (g âˆ˜ f) s :=
  (contDiffOn_univ.2 hg).comp hf (mapsTo_univ _ _)

@[fun_prop]
theorem ContDiff.fun_comp_contDiffOn {s : Set E} {g : F â†’ G} {f : E â†’ F} (hg : ContDiff ğ•œ n g)
    (hf : ContDiffOn ğ•œ n f s) : ContDiffOn ğ•œ n (fun x => g (f x)) s :=
  (contDiffOn_univ.2 hg).comp hf (mapsTo_univ _ _)

theorem ContDiffOn.comp_contDiff {s : Set F} {g : F â†’ G} {f : E â†’ F} (hg : ContDiffOn ğ•œ n g s)
    (hf : ContDiff ğ•œ n f) (hs : âˆ€ x, f x âˆˆ s) : ContDiff ğ•œ n (g âˆ˜ f) := by
  rw [â† contDiffOn_univ] at *
  exact hg.comp hf fun x _ => hs x

theorem ContDiffOn.image_comp_contDiff {s : Set E} {g : F â†’ G} {f : E â†’ F}
    (hg : ContDiffOn ğ•œ n g (f '' s)) (hf : ContDiff ğ•œ n f) : ContDiffOn ğ•œ n (g âˆ˜ f) s :=
  hg.comp hf.contDiffOn (s.mapsTo_image f)

/-- The composition of `C^n` functions is `C^n`. -/
theorem ContDiff.comp {g : F â†’ G} {f : E â†’ F} (hg : ContDiff ğ•œ n g) (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n (g âˆ˜ f) :=
  contDiffOn_univ.1 <| ContDiffOn.comp (contDiffOn_univ.2 hg) (contDiffOn_univ.2 hf) (subset_univ _)

@[fun_prop]
theorem ContDiff.fun_comp {g : F â†’ G} {f : E â†’ F} (hg : ContDiff ğ•œ n g) (hf : ContDiff ğ•œ n f) :
    ContDiff ğ•œ n (fun x => g (f x)) := hg.comp hf

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
theorem ContDiffWithinAt.comp_of_eq {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} {y : F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t y) (hf : ContDiffWithinAt ğ•œ n f s x) (st : MapsTo f s t)
    (hy : f x = y) :
    ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  subst hy; exact hg.comp x hf st

/-- The composition of `C^n` functions at points in domains is `C^n`,
  with a weaker condition on `s` and `t`. -/
theorem ContDiffWithinAt.comp_of_mem_nhdsWithin_image
    {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (hs : t âˆˆ ğ“[f '' s] f x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x :=
  (hg.mono_of_mem_nhdsWithin hs).comp x hf (subset_preimage_image f s)

/-- The composition of `C^n` functions at points in domains is `C^n`,
  with a weaker condition on `s` and `t`. -/
theorem ContDiffWithinAt.comp_of_mem_nhdsWithin_image_of_eq
    {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} {y : F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t y) (hf : ContDiffWithinAt ğ•œ n f s x)
    (hs : t âˆˆ ğ“[f '' s] f x) (hy : f x = y) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  subst hy; exact hg.comp_of_mem_nhdsWithin_image x hf hs

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
theorem ContDiffWithinAt.comp_inter {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n (g âˆ˜ f) (s âˆ© f â»Â¹' t) x :=
  hg.comp x (hf.mono inter_subset_left) inter_subset_right

/-- The composition of `C^n` functions at points in domains is `C^n`. -/
theorem ContDiffWithinAt.comp_inter_of_eq {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} {y : F}
    (x : E) (hg : ContDiffWithinAt ğ•œ n g t y) (hf : ContDiffWithinAt ğ•œ n f s x) (hy : f x = y) :
    ContDiffWithinAt ğ•œ n (g âˆ˜ f) (s âˆ© f â»Â¹' t) x := by
  subst hy; exact hg.comp_inter x hf

/-- The composition of `C^n` functions at points in domains is `C^n`,
  with a weaker condition on `s` and `t`. -/
theorem ContDiffWithinAt.comp_of_preimage_mem_nhdsWithin
    {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (hs : f â»Â¹' t âˆˆ ğ“[s] x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x :=
  (hg.comp_inter x hf).mono_of_mem_nhdsWithin (inter_mem self_mem_nhdsWithin hs)

/-- The composition of `C^n` functions at points in domains is `C^n`,
  with a weaker condition on `s` and `t`. -/
theorem ContDiffWithinAt.comp_of_preimage_mem_nhdsWithin_of_eq
    {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F} {y : F} (x : E)
    (hg : ContDiffWithinAt ğ•œ n g t y) (hf : ContDiffWithinAt ğ•œ n f s x)
    (hs : f â»Â¹' t âˆˆ ğ“[s] x) (hy : f x = y) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  subst hy; exact hg.comp_of_preimage_mem_nhdsWithin x hf hs

theorem ContDiffAt.comp_contDiffWithinAt (x : E) (hg : ContDiffAt ğ•œ n g (f x))
    (hf : ContDiffWithinAt ğ•œ n f s x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x :=
  hg.comp x hf (mapsTo_univ _ _)

theorem ContDiffAt.comp_contDiffWithinAt_of_eq {y : F} (x : E) (hg : ContDiffAt ğ•œ n g y)
    (hf : ContDiffWithinAt ğ•œ n f s x) (hy : f x = y) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) s x := by
  subst hy; exact hg.comp_contDiffWithinAt x hf

/-- The composition of `C^n` functions at points is `C^n`. -/
nonrec theorem ContDiffAt.comp (x : E) (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (g âˆ˜ f) x :=
  hg.comp x hf (mapsTo_univ _ _)

@[fun_prop]
theorem ContDiffAt.fun_comp (x : E) (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x => g (f x)) x := hg.comp x hf

theorem ContDiff.comp_contDiffWithinAt {g : F â†’ G} {f : E â†’ F} (h : ContDiff ğ•œ n g)
    (hf : ContDiffWithinAt ğ•œ n f t x) : ContDiffWithinAt ğ•œ n (g âˆ˜ f) t x :=
  haveI : ContDiffWithinAt ğ•œ n g univ (f x) := h.contDiffAt.contDiffWithinAt
  this.comp x hf (subset_univ _)

theorem ContDiff.comp_contDiffAt {g : F â†’ G} {f : E â†’ F} (x : E) (hg : ContDiff ğ•œ n g)
    (hf : ContDiffAt ğ•œ n f x) : ContDiffAt ğ•œ n (g âˆ˜ f) x :=
  hg.comp_contDiffWithinAt hf

theorem iteratedFDerivWithin_comp_of_eventually_mem {t : Set F}
    (hg : ContDiffWithinAt ğ•œ n g t (f x)) (hf : ContDiffWithinAt ğ•œ n f s x)
    (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s) (hxs : x âˆˆ s) (hst : âˆ€á¶  y in ğ“[s] x, f y âˆˆ t)
    {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (ftaylorSeriesWithin ğ•œ g t (f x)).taylorComp (ftaylorSeriesWithin ğ•œ f s x) i := by
  obtain âŸ¨u, hxu, huo, hfu, hguâŸ© : âˆƒ u, x âˆˆ u âˆ§ IsOpen u âˆ§
      HasFTaylorSeriesUpToOn i f (ftaylorSeriesWithin ğ•œ f s) (s âˆ© u) âˆ§
      HasFTaylorSeriesUpToOn i g (ftaylorSeriesWithin ğ•œ g t) (f '' (s âˆ© u)) := by
    have hxt : f x âˆˆ t := hst.self_of_nhdsWithin hxs
    have hf_tendsto : Tendsto f (ğ“[s] x) (ğ“[t] (f x)) :=
      tendsto_nhdsWithin_iff.mpr âŸ¨hf.continuousWithinAt, hstâŸ©
    have Hâ‚ : âˆ€á¶  u in (ğ“[s] x).smallSets,
        HasFTaylorSeriesUpToOn i f (ftaylorSeriesWithin ğ•œ f s) u :=
      hf.eventually_hasFTaylorSeriesUpToOn hs hxs hi
    have Hâ‚‚ : âˆ€á¶  u in (ğ“[s] x).smallSets,
        HasFTaylorSeriesUpToOn i g (ftaylorSeriesWithin ğ•œ g t) (f '' u) :=
      hf_tendsto.image_smallSets.eventually (hg.eventually_hasFTaylorSeriesUpToOn ht hxt hi)
    rcases (nhdsWithin_basis_open _ _).smallSets.eventually_iff.mp (Hâ‚.and Hâ‚‚)
      with âŸ¨u, âŸ¨hxu, huoâŸ©, huâŸ©
    exact âŸ¨u, hxu, huo, hu (by simp [inter_comm])âŸ©
  exact .symm <| (hgu.comp hfu (mapsTo_image _ _)).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl
    (hs.inter huo) âŸ¨hxs, hxuâŸ© |>.trans <| iteratedFDerivWithin_inter_open huo hxu

theorem iteratedFDerivWithin_comp {t : Set F} (hg : ContDiffWithinAt ğ•œ n g t (f x))
    (hf : ContDiffWithinAt ğ•œ n f s x) (ht : UniqueDiffOn ğ•œ t) (hs : UniqueDiffOn ğ•œ s)
    (hx : x âˆˆ s) (hst : MapsTo f s t) {i : â„•} (hi : i â‰¤ n) :
    iteratedFDerivWithin ğ•œ i (g âˆ˜ f) s x =
      (ftaylorSeriesWithin ğ•œ g t (f x)).taylorComp (ftaylorSeriesWithin ğ•œ f s x) i :=
  iteratedFDerivWithin_comp_of_eventually_mem hg hf ht hs hx (eventually_mem_nhdsWithin.mono hst) hi

theorem iteratedFDeriv_comp (hg : ContDiffAt ğ•œ n g (f x)) (hf : ContDiffAt ğ•œ n f x)
    {i : â„•} (hi : i â‰¤ n) :
    iteratedFDeriv ğ•œ i (g âˆ˜ f) x =
      (ftaylorSeries ğ•œ g (f x)).taylorComp (ftaylorSeries ğ•œ f x) i := by
  simp only [â† iteratedFDerivWithin_univ, â† ftaylorSeriesWithin_univ]
  exact iteratedFDerivWithin_comp hg.contDiffWithinAt hf.contDiffWithinAt
    uniqueDiffOn_univ uniqueDiffOn_univ (mem_univ _) (mapsTo_univ _ _) hi

end comp

/-!
### Smoothness of projections
-/

/-- The first projection in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiff_fst : ContDiff ğ•œ n (Prod.fst : E Ã— F â†’ E) :=
  IsBoundedLinearMap.contDiff IsBoundedLinearMap.fst

/-- Postcomposing `f` with `Prod.fst` is `C^n` -/
@[fun_prop]
theorem ContDiff.fst {f : E â†’ F Ã— G} (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x => (f x).1 :=
  contDiff_fst.comp hf

/-- Precomposing `f` with `Prod.fst` is `C^n` -/
theorem ContDiff.fst' {f : E â†’ G} (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x : E Ã— F => f x.1 :=
  hf.comp contDiff_fst

/-- The first projection on a domain in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffOn_fst {s : Set (E Ã— F)} : ContDiffOn ğ•œ n (Prod.fst : E Ã— F â†’ E) s :=
  ContDiff.contDiffOn contDiff_fst

@[fun_prop]
theorem ContDiffOn.fst {f : E â†’ F Ã— G} {s : Set E} (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (fun x => (f x).1) s :=
  contDiff_fst.comp_contDiffOn hf

/-- The first projection at a point in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffAt_fst {p : E Ã— F} : ContDiffAt ğ•œ n (Prod.fst : E Ã— F â†’ E) p :=
  contDiff_fst.contDiffAt

/-- Postcomposing `f` with `Prod.fst` is `C^n` at `(x, y)` -/
@[fun_prop]
theorem ContDiffAt.fst {f : E â†’ F Ã— G} {x : E} (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x => (f x).1) x :=
  contDiffAt_fst.comp x hf

/-- Precomposing `f` with `Prod.fst` is `C^n` at `(x, y)` -/
theorem ContDiffAt.fst' {f : E â†’ G} {x : E} {y : F} (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x : E Ã— F => f x.1) (x, y) :=
  ContDiffAt.comp (x, y) hf contDiffAt_fst

/-- Precomposing `f` with `Prod.fst` is `C^n` at `x : E Ã— F` -/
theorem ContDiffAt.fst'' {f : E â†’ G} {x : E Ã— F} (hf : ContDiffAt ğ•œ n f x.1) :
    ContDiffAt ğ•œ n (fun x : E Ã— F => f x.1) x :=
  hf.comp x contDiffAt_fst

/-- The first projection within a domain at a point in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffWithinAt_fst {s : Set (E Ã— F)} {p : E Ã— F} :
    ContDiffWithinAt ğ•œ n (Prod.fst : E Ã— F â†’ E) s p :=
  contDiff_fst.contDiffWithinAt

/-- The second projection in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiff_snd : ContDiff ğ•œ n (Prod.snd : E Ã— F â†’ F) :=
  IsBoundedLinearMap.contDiff IsBoundedLinearMap.snd

/-- Postcomposing `f` with `Prod.snd` is `C^n` -/
@[fun_prop]
theorem ContDiff.snd {f : E â†’ F Ã— G} (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x => (f x).2 :=
  contDiff_snd.comp hf

/-- Precomposing `f` with `Prod.snd` is `C^n` -/
theorem ContDiff.snd' {f : F â†’ G} (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x : E Ã— F => f x.2 :=
  hf.comp contDiff_snd

/-- The second projection on a domain in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffOn_snd {s : Set (E Ã— F)} : ContDiffOn ğ•œ n (Prod.snd : E Ã— F â†’ F) s :=
  ContDiff.contDiffOn contDiff_snd

@[fun_prop]
theorem ContDiffOn.snd {f : E â†’ F Ã— G} {s : Set E} (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (fun x => (f x).2) s :=
  contDiff_snd.comp_contDiffOn hf

/-- The second projection at a point in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffAt_snd {p : E Ã— F} : ContDiffAt ğ•œ n (Prod.snd : E Ã— F â†’ F) p :=
  contDiff_snd.contDiffAt

/-- Postcomposing `f` with `Prod.snd` is `C^n` at `x` -/
@[fun_prop]
theorem ContDiffAt.snd {f : E â†’ F Ã— G} {x : E} (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x => (f x).2) x :=
  contDiffAt_snd.comp x hf

/-- Precomposing `f` with `Prod.snd` is `C^n` at `(x, y)` -/
theorem ContDiffAt.snd' {f : F â†’ G} {x : E} {y : F} (hf : ContDiffAt ğ•œ n f y) :
    ContDiffAt ğ•œ n (fun x : E Ã— F => f x.2) (x, y) :=
  ContDiffAt.comp (x, y) hf contDiffAt_snd

/-- Precomposing `f` with `Prod.snd` is `C^n` at `x : E Ã— F` -/
theorem ContDiffAt.snd'' {f : F â†’ G} {x : E Ã— F} (hf : ContDiffAt ğ•œ n f x.2) :
    ContDiffAt ğ•œ n (fun x : E Ã— F => f x.2) x :=
  hf.comp x contDiffAt_snd

/-- The second projection within a domain at a point in a product is `C^âˆ`. -/
@[fun_prop]
theorem contDiffWithinAt_snd {s : Set (E Ã— F)} {p : E Ã— F} :
    ContDiffWithinAt ğ•œ n (Prod.snd : E Ã— F â†’ F) s p :=
  contDiff_snd.contDiffWithinAt

section NAry

variable {Eâ‚ Eâ‚‚ Eâ‚ƒ : Type*}
variable [NormedAddCommGroup Eâ‚] [NormedAddCommGroup Eâ‚‚] [NormedAddCommGroup Eâ‚ƒ]
  [NormedSpace ğ•œ Eâ‚] [NormedSpace ğ•œ Eâ‚‚] [NormedSpace ğ•œ Eâ‚ƒ]

theorem ContDiff.compâ‚‚ {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} (hg : ContDiff ğ•œ n g)
    (hfâ‚ : ContDiff ğ•œ n fâ‚) (hfâ‚‚ : ContDiff ğ•œ n fâ‚‚) : ContDiff ğ•œ n fun x => g (fâ‚ x, fâ‚‚ x) :=
  hg.comp <| hfâ‚.prodMk hfâ‚‚

theorem ContDiffAt.compâ‚‚ {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} {x : F}
    (hg : ContDiffAt ğ•œ n g (fâ‚ x, fâ‚‚ x))
    (hfâ‚ : ContDiffAt ğ•œ n fâ‚ x) (hfâ‚‚ : ContDiffAt ğ•œ n fâ‚‚ x) :
    ContDiffAt ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x)) x :=
  hg.comp x (hfâ‚.prodMk hfâ‚‚)

theorem ContDiffAt.compâ‚‚_contDiffWithinAt {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚}
    {s : Set F} {x : F} (hg : ContDiffAt ğ•œ n g (fâ‚ x, fâ‚‚ x))
    (hfâ‚ : ContDiffWithinAt ğ•œ n fâ‚ s x) (hfâ‚‚ : ContDiffWithinAt ğ•œ n fâ‚‚ s x) :
    ContDiffWithinAt ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x)) s x :=
  hg.comp_contDiffWithinAt x (hfâ‚.prodMk hfâ‚‚)

theorem ContDiff.compâ‚‚_contDiffAt {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} {x : F}
    (hg : ContDiff ğ•œ n g) (hfâ‚ : ContDiffAt ğ•œ n fâ‚ x) (hfâ‚‚ : ContDiffAt ğ•œ n fâ‚‚ x) :
    ContDiffAt ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x)) x :=
  hg.contDiffAt.compâ‚‚ hfâ‚ hfâ‚‚

theorem ContDiff.compâ‚‚_contDiffWithinAt {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚}
    {s : Set F} {x : F} (hg : ContDiff ğ•œ n g)
    (hfâ‚ : ContDiffWithinAt ğ•œ n fâ‚ s x) (hfâ‚‚ : ContDiffWithinAt ğ•œ n fâ‚‚ s x) :
    ContDiffWithinAt ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x)) s x :=
  hg.contDiffAt.comp_contDiffWithinAt x (hfâ‚.prodMk hfâ‚‚)

theorem ContDiff.compâ‚‚_contDiffOn {g : Eâ‚ Ã— Eâ‚‚ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} {s : Set F}
    (hg : ContDiff ğ•œ n g) (hfâ‚ : ContDiffOn ğ•œ n fâ‚ s) (hfâ‚‚ : ContDiffOn ğ•œ n fâ‚‚ s) :
    ContDiffOn ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x)) s :=
  hg.comp_contDiffOn <| hfâ‚.prodMk hfâ‚‚

theorem ContDiff.compâ‚ƒ {g : Eâ‚ Ã— Eâ‚‚ Ã— Eâ‚ƒ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} {fâ‚ƒ : F â†’ Eâ‚ƒ}
    (hg : ContDiff ğ•œ n g) (hfâ‚ : ContDiff ğ•œ n fâ‚) (hfâ‚‚ : ContDiff ğ•œ n fâ‚‚) (hfâ‚ƒ : ContDiff ğ•œ n fâ‚ƒ) :
    ContDiff ğ•œ n fun x => g (fâ‚ x, fâ‚‚ x, fâ‚ƒ x) :=
  hg.compâ‚‚ hfâ‚ <| hfâ‚‚.prodMk hfâ‚ƒ

theorem ContDiff.compâ‚ƒ_contDiffOn {g : Eâ‚ Ã— Eâ‚‚ Ã— Eâ‚ƒ â†’ G} {fâ‚ : F â†’ Eâ‚} {fâ‚‚ : F â†’ Eâ‚‚} {fâ‚ƒ : F â†’ Eâ‚ƒ}
    {s : Set F} (hg : ContDiff ğ•œ n g) (hfâ‚ : ContDiffOn ğ•œ n fâ‚ s) (hfâ‚‚ : ContDiffOn ğ•œ n fâ‚‚ s)
    (hfâ‚ƒ : ContDiffOn ğ•œ n fâ‚ƒ s) : ContDiffOn ğ•œ n (fun x => g (fâ‚ x, fâ‚‚ x, fâ‚ƒ x)) s :=
  hg.compâ‚‚_contDiffOn hfâ‚ <| hfâ‚‚.prodMk hfâ‚ƒ

end NAry

section SpecificBilinearMaps

@[fun_prop]
theorem ContDiff.clm_comp {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F} (hg : ContDiff ğ•œ n g)
    (hf : ContDiff ğ•œ n f) : ContDiff ğ•œ n fun x => (g x).comp (f x) :=
  isBoundedBilinearMap_comp.contDiff.compâ‚‚ (g := fun p => p.1.comp p.2) hg hf

@[fun_prop]
theorem ContDiffOn.clm_comp {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F} {s : Set X}
    (hg : ContDiffOn ğ•œ n g s) (hf : ContDiffOn ğ•œ n f s) :
    ContDiffOn ğ•œ n (fun x => (g x).comp (f x)) s :=
  (isBoundedBilinearMap_comp (E := E) (F := F) (G := G)).contDiff.compâ‚‚_contDiffOn hg hf

@[fun_prop]
theorem ContDiffAt.clm_comp {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F} {x : X}
    (hg : ContDiffAt ğ•œ n g x) (hf : ContDiffAt ğ•œ n f x) :
    ContDiffAt ğ•œ n (fun x => (g x).comp (f x)) x :=
  (isBoundedBilinearMap_comp (E := E) (G := G)).contDiff.compâ‚‚_contDiffAt hg hf

@[fun_prop]
theorem ContDiffWithinAt.clm_comp {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F} {s : Set X} {x : X}
    (hg : ContDiffWithinAt ğ•œ n g s x) (hf : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n (fun x => (g x).comp (f x)) s x :=
  (isBoundedBilinearMap_comp (E := E) (G := G)).contDiff.compâ‚‚_contDiffWithinAt hg hf

@[fun_prop]
theorem ContDiff.clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F} (hf : ContDiff ğ•œ n f)
    (hg : ContDiff ğ•œ n g) : ContDiff ğ•œ n fun x => (f x) (g x) :=
  isBoundedBilinearMap_apply.contDiff.compâ‚‚ hf hg

@[fun_prop]
theorem ContDiffOn.clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x => (f x) (g x)) s :=
  isBoundedBilinearMap_apply.contDiff.compâ‚‚_contDiffOn hf hg

@[fun_prop]
theorem ContDiffAt.clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) : ContDiffAt ğ•œ n (fun x => (f x) (g x)) x :=
  isBoundedBilinearMap_apply.contDiff.compâ‚‚_contDiffAt hf hg

@[fun_prop]
theorem ContDiffWithinAt.clm_apply {f : E â†’ F â†’L[ğ•œ] G} {g : E â†’ F}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    ContDiffWithinAt ğ•œ n (fun x => (f x) (g x)) s x :=
  isBoundedBilinearMap_apply.contDiff.compâ‚‚_contDiffWithinAt hf hg

@[fun_prop]
theorem ContDiff.smulRight {f : E â†’ StrongDual ğ•œ F} {g : E â†’ G} (hf : ContDiff ğ•œ n f)
    (hg : ContDiff ğ•œ n g) : ContDiff ğ•œ n fun x => (f x).smulRight (g x) :=
  isBoundedBilinearMap_smulRight.contDiff.compâ‚‚ (g := fun p => p.1.smulRight p.2) hf hg

@[fun_prop]
theorem ContDiffOn.smulRight {f : E â†’ StrongDual ğ•œ F} {g : E â†’ G} (hf : ContDiffOn ğ•œ n f s)
    (hg : ContDiffOn ğ•œ n g s) : ContDiffOn ğ•œ n (fun x => (f x).smulRight (g x)) s :=
  (isBoundedBilinearMap_smulRight (E := F)).contDiff.compâ‚‚_contDiffOn hf hg

@[fun_prop]
theorem ContDiffAt.smulRight {f : E â†’ StrongDual ğ•œ F} {g : E â†’ G} (hf : ContDiffAt ğ•œ n f x)
    (hg : ContDiffAt ğ•œ n g x) : ContDiffAt ğ•œ n (fun x => (f x).smulRight (g x)) x :=
  (isBoundedBilinearMap_smulRight (E := F)).contDiff.compâ‚‚_contDiffAt hf hg

@[fun_prop]
theorem ContDiffWithinAt.smulRight {f : E â†’ StrongDual ğ•œ F} {g : E â†’ G}
    (hf : ContDiffWithinAt ğ•œ n f s x) (hg : ContDiffWithinAt ğ•œ n g s x) :
    ContDiffWithinAt ğ•œ n (fun x => (f x).smulRight (g x)) s x :=
  (isBoundedBilinearMap_smulRight (E := F)).contDiff.compâ‚‚_contDiffWithinAt hf hg

end SpecificBilinearMaps

section ClmApplyConst

/-- Application of a `ContinuousLinearMap` to a constant commutes with `iteratedFDerivWithin`. -/
theorem iteratedFDerivWithin_clm_apply_const_apply
    {s : Set E} (hs : UniqueDiffOn ğ•œ s) {c : E â†’ F â†’L[ğ•œ] G}
    (hc : ContDiffOn ğ•œ n c s) {i : â„•} (hi : i â‰¤ n) {x : E} (hx : x âˆˆ s) {u : F} {m : Fin i â†’ E} :
    (iteratedFDerivWithin ğ•œ i (fun y â†¦ (c y) u) s x) m = (iteratedFDerivWithin ğ•œ i c s x) m u := by
  induction i generalizing x with
  | zero => simp
  | succ i ih =>
    replace hi : (i : WithTop â„•âˆ) < n := lt_of_lt_of_le (by norm_cast; simp) hi
    have h_deriv_apply : DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ i (fun y â†¦ (c y) u) s) s :=
      (hc.clm_apply contDiffOn_const).differentiableOn_iteratedFDerivWithin hi hs
    have h_deriv : DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ i c s) s :=
      hc.differentiableOn_iteratedFDerivWithin hi hs
    simp only [iteratedFDerivWithin_succ_apply_left]
    rw [â† fderivWithin_continuousMultilinear_apply_const_apply (hs x hx) (h_deriv_apply x hx)]
    rw [fderivWithin_congr' (fun x hx â†¦ ih hi.le hx) hx]
    rw [fderivWithin_clm_apply (hs x hx) (h_deriv.continuousMultilinear_apply_const _ x hx)
      (differentiableWithinAt_const u)]
    rw [fderivWithin_const_apply]
    simp only [ContinuousLinearMap.flip_apply, ContinuousLinearMap.comp_zero, zero_add]
    rw [fderivWithin_continuousMultilinear_apply_const_apply (hs x hx) (h_deriv x hx)]

/-- Application of a `ContinuousLinearMap` to a constant commutes with `iteratedFDeriv`. -/
theorem iteratedFDeriv_clm_apply_const_apply
    {c : E â†’ F â†’L[ğ•œ] G} (hc : ContDiff ğ•œ n c)
    {i : â„•} (hi : i â‰¤ n) {x : E} {u : F} {m : Fin i â†’ E} :
    (iteratedFDeriv ğ•œ i (fun y â†¦ (c y) u) x) m = (iteratedFDeriv ğ•œ i c x) m u := by
  simp only [â† iteratedFDerivWithin_univ]
  exact iteratedFDerivWithin_clm_apply_const_apply uniqueDiffOn_univ hc.contDiffOn hi (mem_univ _)

end ClmApplyConst

/-- The natural equivalence `(E Ã— F) Ã— G â‰ƒ E Ã— (F Ã— G)` is smooth.

Warning: if you think you need this lemma, it is likely that you can simplify your proof by
reformulating the lemma that you're applying next using the tips in
Note [continuity lemma statement]
-/
theorem contDiff_prodAssoc {n : WithTop â„•âˆ} : ContDiff ğ•œ n <| Equiv.prodAssoc E F G :=
  (LinearIsometryEquiv.prodAssoc ğ•œ E F G).contDiff

/-- The natural equivalence `E Ã— (F Ã— G) â‰ƒ (E Ã— F) Ã— G` is smooth.

Warning: see remarks attached to `contDiff_prodAssoc`
-/
theorem contDiff_prodAssoc_symm {n : WithTop â„•âˆ} : ContDiff ğ•œ n <| (Equiv.prodAssoc E F G).symm :=
  (LinearIsometryEquiv.prodAssoc ğ•œ E F G).symm.contDiff

/-! ### Bundled derivatives are smooth -/
section bundled

/-- One direction of `contDiffWithinAt_succ_iff_hasFDerivWithinAt`, but where all derivatives are
taken within the same set. Version for partial derivatives / functions with parameters. If `f x` is
a `C^n+1` family of functions and `g x` is a `C^n` family of points, then the derivative of `f x` at
`g x` depends in a `C^n` way on `x`. We give a general version of this fact relative to sets which
may not have unique derivatives, in the following form.  If `f : E Ã— F â†’ G` is `C^n+1` at
`(xâ‚€, g(xâ‚€))` in `(s âˆª {xâ‚€}) Ã— t âŠ† E Ã— F` and `g : E â†’ F` is `C^n` at `xâ‚€` within some set `s âŠ† E`,
then there is a function `f' : E â†’ F â†’L[ğ•œ] G` that is `C^n` at `xâ‚€` within `s` such that for all `x`
sufficiently close to `xâ‚€` within `s âˆª {xâ‚€}` the function `y â†¦ f x y` has derivative `f' x` at `g x`
within `t âŠ† F`.  For convenience, we return an explicit set of `x`'s where this holds that is a
subset of `s âˆª {xâ‚€}`.  We need one additional condition, namely that `t` is a neighborhood of
`g(xâ‚€)` within `g '' s`. -/
theorem ContDiffWithinAt.hasFDerivWithinAt_nhds {f : E â†’ F â†’ G} {g : E â†’ F} {t : Set F} (hn : n â‰  âˆ)
    {xâ‚€ : E} (hf : ContDiffWithinAt ğ•œ (n + 1) (uncurry f) (insert xâ‚€ s Ã—Ë¢ t) (xâ‚€, g xâ‚€))
    (hg : ContDiffWithinAt ğ•œ n g s xâ‚€) (hgt : t âˆˆ ğ“[g '' s] g xâ‚€) :
    âˆƒ v âˆˆ ğ“[insert xâ‚€ s] xâ‚€, v âŠ† insert xâ‚€ s âˆ§ âˆƒ f' : E â†’ F â†’L[ğ•œ] G,
      (âˆ€ x âˆˆ v, HasFDerivWithinAt (f x) (f' x) t (g x)) âˆ§
        ContDiffWithinAt ğ•œ n (fun x => f' x) s xâ‚€ := by
  have hst : insert xâ‚€ s Ã—Ë¢ t âˆˆ ğ“[(fun x => (x, g x)) '' s] (xâ‚€, g xâ‚€) := by
    refine nhdsWithin_mono _ ?_ (nhdsWithin_prod self_mem_nhdsWithin hgt)
    simp_rw [image_subset_iff, mk_preimage_prod, preimage_id', subset_inter_iff, subset_insert,
      true_and, subset_preimage_image]
  obtain âŸ¨v, hv, hvs, f_an, f', hvf', hf'âŸ© :=
    (contDiffWithinAt_succ_iff_hasFDerivWithinAt' hn).mp hf
  refine
    âŸ¨(fun z => (z, g z)) â»Â¹' v âˆ© insert xâ‚€ s, ?_, inter_subset_right, fun z =>
      (f' (z, g z)).comp (ContinuousLinearMap.inr ğ•œ E F), ?_, ?_âŸ©
  Â· refine inter_mem ?_ self_mem_nhdsWithin
    have := mem_of_mem_nhdsWithin (mem_insert _ _) hv
    refine mem_nhdsWithin_insert.mpr âŸ¨this, ?_âŸ©
    refine (continuousWithinAt_id.prodMk hg.continuousWithinAt).preimage_mem_nhdsWithin' ?_
    rw [â† nhdsWithin_le_iff] at hst hv âŠ¢
    exact (hst.trans <| nhdsWithin_mono _ <| subset_insert _ _).trans hv
  Â· intro z hz
    have := hvf' (z, g z) hz.1
    refine this.comp _ (hasFDerivAt_prodMk_right _ _).hasFDerivWithinAt ?_
    exact mapsTo_iff_image_subset.mpr (image_prodMk_subset_prod_right hz.2)
  Â· exact (hf'.continuousLinearMap_comp <| (ContinuousLinearMap.compL ğ•œ F (E Ã— F) G).flip
      (ContinuousLinearMap.inr ğ•œ E F)).comp_of_mem_nhdsWithin_image xâ‚€
      (contDiffWithinAt_id.prodMk hg) hst

/-- The most general lemma stating that `x â†¦ fderivWithin ğ•œ (f x) t (g x)` is `C^n`
at a point within a set.
To show that `x â†¦ D_yf(x,y)g(x)` (taken within `t`) is `C^m` at `xâ‚€` within `s`, we require that
* `f` is `C^n` at `(xâ‚€, g(xâ‚€))` within `(s âˆª {xâ‚€}) Ã— t` for `n â‰¥ m+1`.
* `g` is `C^m` at `xâ‚€` within `s`;
* Derivatives are unique at `g(x)` within `t` for `x` sufficiently close to `xâ‚€` within `s âˆª {xâ‚€}`;
* `t` is a neighborhood of `g(xâ‚€)` within `g '' s`; -/
theorem ContDiffWithinAt.fderivWithin'' {f : E â†’ F â†’ G} {g : E â†’ F} {t : Set F}
    (hf : ContDiffWithinAt ğ•œ n (Function.uncurry f) (insert xâ‚€ s Ã—Ë¢ t) (xâ‚€, g xâ‚€))
    (hg : ContDiffWithinAt ğ•œ m g s xâ‚€)
    (ht : âˆ€á¶  x in ğ“[insert xâ‚€ s] xâ‚€, UniqueDiffWithinAt ğ•œ t (g x)) (hmn : m + 1 â‰¤ n)
    (hgt : t âˆˆ ğ“[g '' s] g xâ‚€) :
    ContDiffWithinAt ğ•œ m (fun x => fderivWithin ğ•œ (f x) t (g x)) s xâ‚€ := by
  have : âˆ€ k : â„•, k â‰¤ m â†’ ContDiffWithinAt ğ•œ k (fun x => fderivWithin ğ•œ (f x) t (g x)) s xâ‚€ := by
    intro k hkm
    obtain âŸ¨v, hv, -, f', hvf', hf'âŸ© :=
      (hf.of_le <| (add_le_add_right hkm 1).trans hmn).hasFDerivWithinAt_nhds (by simp)
        (hg.of_le hkm) hgt
    refine hf'.congr_of_eventuallyEq_insert ?_
    filter_upwards [hv, ht]
    exact fun y hy h2y => (hvf' y hy).fderivWithin h2y
  match m with
  | Ï‰ =>
    obtain rfl : n = Ï‰ := by simpa using hmn
    obtain âŸ¨v, hv, -, f', hvf', hf'âŸ© := hf.hasFDerivWithinAt_nhds (by simp) hg hgt
    refine hf'.congr_of_eventuallyEq_insert ?_
    filter_upwards [hv, ht]
    exact fun y hy h2y => (hvf' y hy).fderivWithin h2y
  | âˆ =>
    rw [contDiffWithinAt_infty]
    exact fun k â†¦ this k (by exact_mod_cast le_top)
  | (m : â„•) => exact this _ le_rfl

/-- A special case of `ContDiffWithinAt.fderivWithin''` where we require that `s âŠ† gâ»Â¹(t)`. -/
theorem ContDiffWithinAt.fderivWithin' {f : E â†’ F â†’ G} {g : E â†’ F} {t : Set F}
    (hf : ContDiffWithinAt ğ•œ n (Function.uncurry f) (insert xâ‚€ s Ã—Ë¢ t) (xâ‚€, g xâ‚€))
    (hg : ContDiffWithinAt ğ•œ m g s xâ‚€)
    (ht : âˆ€á¶  x in ğ“[insert xâ‚€ s] xâ‚€, UniqueDiffWithinAt ğ•œ t (g x)) (hmn : m + 1 â‰¤ n)
    (hst : s âŠ† g â»Â¹' t) : ContDiffWithinAt ğ•œ m (fun x => fderivWithin ğ•œ (f x) t (g x)) s xâ‚€ :=
  hf.fderivWithin'' hg ht hmn <| mem_of_superset self_mem_nhdsWithin <| image_subset_iff.mpr hst

/-- A special case of `ContDiffWithinAt.fderivWithin'` where we require that `xâ‚€ âˆˆ s` and there
are unique derivatives everywhere within `t`. -/
protected theorem ContDiffWithinAt.fderivWithin {f : E â†’ F â†’ G} {g : E â†’ F} {t : Set F}
    (hf : ContDiffWithinAt ğ•œ n (Function.uncurry f) (s Ã—Ë¢ t) (xâ‚€, g xâ‚€))
    (hg : ContDiffWithinAt ğ•œ m g s xâ‚€) (ht : UniqueDiffOn ğ•œ t) (hmn : m + 1 â‰¤ n) (hxâ‚€ : xâ‚€ âˆˆ s)
    (hst : s âŠ† g â»Â¹' t) : ContDiffWithinAt ğ•œ m (fun x => fderivWithin ğ•œ (f x) t (g x)) s xâ‚€ := by
  rw [â† insert_eq_self.mpr hxâ‚€] at hf
  refine hf.fderivWithin' hg ?_ hmn hst
  rw [insert_eq_self.mpr hxâ‚€]
  exact eventually_of_mem self_mem_nhdsWithin fun x hx => ht _ (hst hx)

/-- `x â†¦ fderivWithin ğ•œ (f x) t (g x) (k x)` is smooth at a point within a set. -/
theorem ContDiffWithinAt.fderivWithin_apply {f : E â†’ F â†’ G} {g k : E â†’ F} {t : Set F}
    (hf : ContDiffWithinAt ğ•œ n (Function.uncurry f) (s Ã—Ë¢ t) (xâ‚€, g xâ‚€))
    (hg : ContDiffWithinAt ğ•œ m g s xâ‚€) (hk : ContDiffWithinAt ğ•œ m k s xâ‚€) (ht : UniqueDiffOn ğ•œ t)
    (hmn : m + 1 â‰¤ n) (hxâ‚€ : xâ‚€ âˆˆ s) (hst : s âŠ† g â»Â¹' t) :
    ContDiffWithinAt ğ•œ m (fun x => fderivWithin ğ•œ (f x) t (g x) (k x)) s xâ‚€ :=
  (contDiff_fst.clm_apply contDiff_snd).contDiffAt.comp_contDiffWithinAt xâ‚€
    ((hf.fderivWithin hg ht hmn hxâ‚€ hst).prodMk hk)

/-- `fderivWithin ğ•œ f s` is smooth at `xâ‚€` within `s`. -/
theorem ContDiffWithinAt.fderivWithin_right (hf : ContDiffWithinAt ğ•œ n f s xâ‚€)
    (hs : UniqueDiffOn ğ•œ s) (hmn : m + 1 â‰¤ n) (hxâ‚€s : xâ‚€ âˆˆ s) :
    ContDiffWithinAt ğ•œ m (fderivWithin ğ•œ f s) s xâ‚€ :=
  ContDiffWithinAt.fderivWithin
    (ContDiffWithinAt.comp (xâ‚€, xâ‚€) hf contDiffWithinAt_snd <| prod_subset_preimage_snd s s)
    contDiffWithinAt_id hs hmn hxâ‚€s (by rw [preimage_id'])

/-- `x â†¦ fderivWithin ğ•œ f s x (k x)` is smooth at `xâ‚€` within `s`. -/
theorem ContDiffWithinAt.fderivWithin_right_apply
    {f : F â†’ G} {k : F â†’ F} {s : Set F} {xâ‚€ : F}
    (hf : ContDiffWithinAt ğ•œ n f s xâ‚€) (hk : ContDiffWithinAt ğ•œ m k s xâ‚€)
    (hs : UniqueDiffOn ğ•œ s) (hmn : m + 1 â‰¤ n) (hxâ‚€s : xâ‚€ âˆˆ s) :
    ContDiffWithinAt ğ•œ m (fun x => fderivWithin ğ•œ f s x (k x)) s xâ‚€ :=
  ContDiffWithinAt.fderivWithin_apply
    (ContDiffWithinAt.comp (xâ‚€, xâ‚€) hf contDiffWithinAt_snd <| prod_subset_preimage_snd s s)
    contDiffWithinAt_id hk hs hmn hxâ‚€s (by rw [preimage_id'])

-- TODO: can we make a version of `ContDiffWithinAt.fderivWithin` for iterated derivatives?
theorem ContDiffWithinAt.iteratedFDerivWithin_right {i : â„•} (hf : ContDiffWithinAt ğ•œ n f s xâ‚€)
    (hs : UniqueDiffOn ğ•œ s) (hmn : m + i â‰¤ n) (hxâ‚€s : xâ‚€ âˆˆ s) :
    ContDiffWithinAt ğ•œ m (iteratedFDerivWithin ğ•œ i f s) s xâ‚€ := by
  induction i generalizing m with
  | zero =>
    simp only [CharP.cast_eq_zero, add_zero] at hmn
    exact (hf.of_le hmn).continuousLinearMap_comp
      ((continuousMultilinearCurryFin0 ğ•œ E F).symm : _ â†’L[ğ•œ] E [Ã—0]â†’L[ğ•œ] F)
  | succ i hi =>
    rw [Nat.cast_succ, add_comm _ 1, â† add_assoc] at hmn
    exact ((hi hmn).fderivWithin_right hs le_rfl hxâ‚€s).continuousLinearMap_comp
      ((continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (i+1) â†¦ E) F).symm :
        _ â†’L[ğ•œ] E [Ã—(i+1)]â†’L[ğ•œ] F)

/-- `x â†¦ fderiv ğ•œ (f x) (g x)` is smooth at `xâ‚€`. -/
protected theorem ContDiffAt.fderiv {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiffAt ğ•œ n (Function.uncurry f) (xâ‚€, g xâ‚€)) (hg : ContDiffAt ğ•œ m g xâ‚€)
    (hmn : m + 1 â‰¤ n) : ContDiffAt ğ•œ m (fun x => fderiv ğ•œ (f x) (g x)) xâ‚€ := by
  simp_rw [â† fderivWithin_univ]
  refine (ContDiffWithinAt.fderivWithin hf.contDiffWithinAt hg.contDiffWithinAt uniqueDiffOn_univ
    hmn (mem_univ xâ‚€) ?_).contDiffAt univ_mem
  rw [preimage_univ]

@[fun_prop]
protected theorem ContDiffAt.fderiv_succ {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiffAt ğ•œ (m + 1) (Function.uncurry f) (xâ‚€, g xâ‚€)) (hg : ContDiffAt ğ•œ m g xâ‚€) :
    ContDiffAt ğ•œ m (fun x => fderiv ğ•œ (f x) (g x)) xâ‚€ :=
  ContDiffAt.fderiv hf hg (le_refl _)

/-- `fderiv ğ•œ f` is smooth at `xâ‚€`. -/
theorem ContDiffAt.fderiv_right (hf : ContDiffAt ğ•œ n f xâ‚€) (hmn : m + 1 â‰¤ n) :
    ContDiffAt ğ•œ m (fderiv ğ•œ f) xâ‚€ :=
  ContDiffAt.fderiv (ContDiffAt.comp (xâ‚€, xâ‚€) hf contDiffAt_snd) contDiffAt_id hmn

theorem ContDiffAt.fderiv_right_succ (hf : ContDiffAt ğ•œ (n + 1) f xâ‚€) :
    ContDiffAt ğ•œ n (fderiv ğ•œ f) xâ‚€ :=
  ContDiffAt.fderiv (ContDiffAt.comp (xâ‚€, xâ‚€) hf contDiffAt_snd) contDiffAt_id (le_refl (n + 1))

theorem ContDiffAt.iteratedFDeriv_right {i : â„•} (hf : ContDiffAt ğ•œ n f xâ‚€)
    (hmn : m + i â‰¤ n) : ContDiffAt ğ•œ m (iteratedFDeriv ğ•œ i f) xâ‚€ := by
  rw [â† iteratedFDerivWithin_univ, â† contDiffWithinAt_univ] at *
  exact hf.iteratedFDerivWithin_right uniqueDiffOn_univ hmn trivial

/-- `x â†¦ fderiv ğ•œ (f x) (g x)` is smooth. -/
protected theorem ContDiff.fderiv {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiff ğ•œ m <| Function.uncurry f) (hg : ContDiff ğ•œ n g) (hnm : n + 1 â‰¤ m) :
    ContDiff ğ•œ n fun x => fderiv ğ•œ (f x) (g x) :=
  contDiff_iff_contDiffAt.mpr fun _ => hf.contDiffAt.fderiv hg.contDiffAt hnm

@[fun_prop]
protected theorem ContDiff.fderiv_succ {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiff ğ•œ (n + 1) <| Function.uncurry f) (hg : ContDiff ğ•œ n g) :
    ContDiff ğ•œ n fun x => fderiv ğ•œ (f x) (g x) :=
  contDiff_iff_contDiffAt.mpr fun _ => hf.contDiffAt.fderiv hg.contDiffAt (le_refl (n + 1))

/-- `fderiv ğ•œ f` is smooth. -/
theorem ContDiff.fderiv_right (hf : ContDiff ğ•œ n f) (hmn : m + 1 â‰¤ n) :
    ContDiff ğ•œ m (fderiv ğ•œ f) :=
  contDiff_iff_contDiffAt.mpr fun _x => hf.contDiffAt.fderiv_right hmn

theorem ContDiff.iteratedFDeriv_right {i : â„•} (hf : ContDiff ğ•œ n f)
    (hmn : m + i â‰¤ n) : ContDiff ğ•œ m (iteratedFDeriv ğ•œ i f) :=
  contDiff_iff_contDiffAt.mpr fun _x => hf.contDiffAt.iteratedFDeriv_right hmn

@[fun_prop]
theorem ContDiff.iteratedFDeriv_right' {i : â„•} (hf : ContDiff ğ•œ (m + i) f) :
    ContDiff ğ•œ m (iteratedFDeriv ğ•œ i f) :=
  contDiff_iff_contDiffAt.mpr fun _x => hf.contDiffAt.iteratedFDeriv_right (le_refl _)

/-- `x â†¦ fderiv ğ•œ (f x) (g x)` is continuous. -/
theorem Continuous.fderiv {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiff ğ•œ n <| Function.uncurry f) (hg : Continuous g) (hn : 1 â‰¤ n) :
    Continuous fun x => fderiv ğ•œ (f x) (g x) :=
  (hf.fderiv (contDiff_zero.mpr hg) hn).continuous

@[fun_prop]
theorem Continuous.fderiv_one {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiff ğ•œ 1 <| Function.uncurry f) (hg : Continuous g) :
    Continuous fun x => _root_.fderiv ğ•œ (f x) (g x) :=
  (hf.fderiv (contDiff_zero.mpr hg) (le_refl 1)).continuous

@[fun_prop]
protected theorem Differentiable.fderiv_two {f : E â†’ F â†’ G} {g : E â†’ F}
    (hf : ContDiff ğ•œ 2 <| Function.uncurry f) (hg : ContDiff ğ•œ 1 g) :
    Differentiable ğ•œ fun x => fderiv ğ•œ (f x) (g x) :=
  ContDiff.differentiable
    (contDiff_iff_contDiffAt.mpr fun _ => hf.contDiffAt.fderiv hg.contDiffAt (le_refl 2))
    (le_refl 1)

/-- `x â†¦ fderiv ğ•œ (f x) (g x) (k x)` is smooth. -/
theorem ContDiff.fderiv_apply {f : E â†’ F â†’ G} {g k : E â†’ F}
    (hf : ContDiff ğ•œ m <| Function.uncurry f) (hg : ContDiff ğ•œ n g) (hk : ContDiff ğ•œ n k)
    (hnm : n + 1 â‰¤ m) : ContDiff ğ•œ n fun x => fderiv ğ•œ (f x) (g x) (k x) :=
  (hf.fderiv hg hnm).clm_apply hk

/-- The bundled derivative of a `C^{n+1}` function is `C^n`. -/
theorem contDiffOn_fderivWithin_apply {s : Set E} {f : E â†’ F} (hf : ContDiffOn ğ•œ n f s)
    (hs : UniqueDiffOn ğ•œ s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (fun p : E Ã— E => (fderivWithin ğ•œ f s p.1 : E â†’L[ğ•œ] F) p.2) (s Ã—Ë¢ univ) :=
  ((hf.fderivWithin hs hmn).comp contDiffOn_fst (prod_subset_preimage_fst _ _)).clm_apply
    contDiffOn_snd

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
theorem ContDiffOn.continuousOn_fderivWithin_apply (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hn : 1 â‰¤ n) :
    ContinuousOn (fun p : E Ã— E => (fderivWithin ğ•œ f s p.1 : E â†’ F) p.2) (s Ã—Ë¢ univ) :=
  (contDiffOn_fderivWithin_apply (m := 0) hf hs hn).continuousOn

/-- The bundled derivative of a `C^{n+1}` function is `C^n`. -/
theorem ContDiff.contDiff_fderiv_apply {f : E â†’ F} (hf : ContDiff ğ•œ n f) (hmn : m + 1 â‰¤ n) :
    ContDiff ğ•œ m fun p : E Ã— E => (fderiv ğ•œ f p.1 : E â†’L[ğ•œ] F) p.2 := by
  rw [â† contDiffOn_univ] at hf âŠ¢
  rw [â† fderivWithin_univ, â† univ_prod_univ]
  exact contDiffOn_fderivWithin_apply hf uniqueDiffOn_univ hmn

end bundled

section deriv

/-!
### One dimension

All results up to now have been expressed in terms of the general FrÃ©chet derivative `fderiv`. For
maps defined on the field, the one-dimensional derivative `deriv` is often easier to use. In this
paragraph, we reformulate some higher smoothness results in terms of `deriv`.
-/


variable {fâ‚‚ : ğ•œ â†’ F} {sâ‚‚ : Set ğ•œ}

open ContinuousLinearMap (smulRight)

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (formulated with `derivWithin`) is `C^n`. -/
theorem contDiffOn_succ_iff_derivWithin (hs : UniqueDiffOn ğ•œ sâ‚‚) :
    ContDiffOn ğ•œ (n + 1) fâ‚‚ sâ‚‚ â†”
      DifferentiableOn ğ•œ fâ‚‚ sâ‚‚ âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ fâ‚‚ sâ‚‚) âˆ§
        ContDiffOn ğ•œ n (derivWithin fâ‚‚ sâ‚‚) sâ‚‚ := by
  rw [contDiffOn_succ_iff_fderivWithin hs, and_congr_right_iff]
  intro _
  constructor
  Â· rintro âŸ¨h', hâŸ©
    refine âŸ¨h', ?_âŸ©
    have : derivWithin fâ‚‚ sâ‚‚ = (fun u : ğ•œ â†’L[ğ•œ] F => u 1) âˆ˜ fderivWithin ğ•œ fâ‚‚ sâ‚‚ := by
      ext x; rfl
    simp_rw [this]
    apply ContDiff.comp_contDiffOn _ h
    exact (isBoundedBilinearMap_apply.isBoundedLinearMap_left _).contDiff
  Â· rintro âŸ¨h', hâŸ©
    refine âŸ¨h', ?_âŸ©
    have : fderivWithin ğ•œ fâ‚‚ sâ‚‚ = smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) âˆ˜ derivWithin fâ‚‚ sâ‚‚ := by
      ext x; simp [derivWithin]
    simp only [this]
    apply ContDiff.comp_contDiffOn _ h
    have : IsBoundedBilinearMap ğ•œ fun _ : (ğ•œ â†’L[ğ•œ] ğ•œ) Ã— F => _ := isBoundedBilinearMap_smulRight
    exact (this.isBoundedLinearMap_right _).contDiff

theorem contDiffOn_infty_iff_derivWithin (hs : UniqueDiffOn ğ•œ sâ‚‚) :
    ContDiffOn ğ•œ âˆ fâ‚‚ sâ‚‚ â†” DifferentiableOn ğ•œ fâ‚‚ sâ‚‚ âˆ§ ContDiffOn ğ•œ âˆ (derivWithin fâ‚‚ sâ‚‚) sâ‚‚ := by
  rw [show âˆ = âˆ + 1 by rfl, contDiffOn_succ_iff_derivWithin hs]
  simp

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (formulated with `deriv`) is `C^n`. -/
theorem contDiffOn_succ_iff_deriv_of_isOpen (hs : IsOpen sâ‚‚) :
    ContDiffOn ğ•œ (n + 1) fâ‚‚ sâ‚‚ â†”
      DifferentiableOn ğ•œ fâ‚‚ sâ‚‚ âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ fâ‚‚ sâ‚‚) âˆ§
        ContDiffOn ğ•œ n (deriv fâ‚‚) sâ‚‚ := by
  rw [contDiffOn_succ_iff_derivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and (Iff.rfl.and (contDiffOn_congr fun _ => derivWithin_of_isOpen hs))

theorem contDiffOn_infty_iff_deriv_of_isOpen (hs : IsOpen sâ‚‚) :
    ContDiffOn ğ•œ âˆ fâ‚‚ sâ‚‚ â†” DifferentiableOn ğ•œ fâ‚‚ sâ‚‚ âˆ§ ContDiffOn ğ•œ âˆ (deriv fâ‚‚) sâ‚‚ := by
  rw [show âˆ = âˆ + 1 by rfl, contDiffOn_succ_iff_deriv_of_isOpen hs]
  simp

protected theorem ContDiffOn.derivWithin (hf : ContDiffOn ğ•œ n fâ‚‚ sâ‚‚) (hs : UniqueDiffOn ğ•œ sâ‚‚)
    (hmn : m + 1 â‰¤ n) : ContDiffOn ğ•œ m (derivWithin fâ‚‚ sâ‚‚) sâ‚‚ :=
  ((contDiffOn_succ_iff_derivWithin hs).1 (hf.of_le hmn)).2.2

theorem ContDiffOn.deriv_of_isOpen (hf : ContDiffOn ğ•œ n fâ‚‚ sâ‚‚) (hs : IsOpen sâ‚‚) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (deriv fâ‚‚) sâ‚‚ :=
  (hf.derivWithin hs.uniqueDiffOn hmn).congr fun _ hx => (derivWithin_of_isOpen hs hx).symm

theorem ContDiffOn.continuousOn_derivWithin (h : ContDiffOn ğ•œ n fâ‚‚ sâ‚‚) (hs : UniqueDiffOn ğ•œ sâ‚‚)
    (hn : 1 â‰¤ n) : ContinuousOn (derivWithin fâ‚‚ sâ‚‚) sâ‚‚ := by
  rw [show (1 : WithTop â„•âˆ) = 0 + 1 from rfl] at hn
  exact ((contDiffOn_succ_iff_derivWithin hs).1 (h.of_le hn)).2.2.continuousOn

theorem ContDiffOn.continuousOn_deriv_of_isOpen (h : ContDiffOn ğ•œ n fâ‚‚ sâ‚‚) (hs : IsOpen sâ‚‚)
    (hn : 1 â‰¤ n) : ContinuousOn (deriv fâ‚‚) sâ‚‚ := by
  rw [show (1 : WithTop â„•âˆ) = 0 + 1 from rfl] at hn
  exact ((contDiffOn_succ_iff_deriv_of_isOpen hs).1 (h.of_le hn)).2.2.continuousOn

/-- A function is `C^(n + 1)` if and only if it is differentiable,
  and its derivative (formulated in terms of `deriv`) is `C^n`. -/
theorem contDiff_succ_iff_deriv :
    ContDiff ğ•œ (n + 1) fâ‚‚ â†” Differentiable ğ•œ fâ‚‚ âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ fâ‚‚ univ) âˆ§
      ContDiff ğ•œ n (deriv fâ‚‚) := by
  simp only [â† contDiffOn_univ, contDiffOn_succ_iff_deriv_of_isOpen, isOpen_univ,
    differentiableOn_univ]

theorem contDiff_one_iff_deriv :
    ContDiff ğ•œ 1 fâ‚‚ â†” Differentiable ğ•œ fâ‚‚ âˆ§ Continuous (deriv fâ‚‚) := by
  rw [show (1 : WithTop â„•âˆ) = 0 + 1 from rfl, contDiff_succ_iff_deriv]
  simp

theorem contDiff_infty_iff_deriv :
    ContDiff ğ•œ âˆ fâ‚‚ â†” Differentiable ğ•œ fâ‚‚ âˆ§ ContDiff ğ•œ âˆ (deriv fâ‚‚) := by
  rw [show (âˆ : WithTop â„•âˆ) = âˆ + 1 from rfl, contDiff_succ_iff_deriv]
  simp

theorem ContDiff.continuous_deriv (h : ContDiff ğ•œ n fâ‚‚) (hn : 1 â‰¤ n) : Continuous (deriv fâ‚‚) := by
  rw [show (1 : WithTop â„•âˆ) = 0 + 1 from rfl] at hn
  exact (contDiff_succ_iff_deriv.mp (h.of_le hn)).2.2.continuous

@[fun_prop]
theorem ContDiff.continuous_deriv_one (h : ContDiff ğ•œ 1 fâ‚‚) : Continuous (deriv fâ‚‚) :=
  ContDiff.continuous_deriv h (le_refl 1)

@[fun_prop]
theorem ContDiff.differentiable_deriv_two (h : ContDiff ğ•œ 2 fâ‚‚) : Differentiable ğ•œ (deriv fâ‚‚) := by
  unfold deriv; fun_prop

@[fun_prop]
theorem ContDiff.deriv' (h : ContDiff ğ•œ (n + 1) fâ‚‚) : ContDiff ğ•œ n (deriv fâ‚‚) := by
  unfold deriv; fun_prop

@[fun_prop]
theorem ContDiff.iterate_deriv :
    âˆ€ (n : â„•) {fâ‚‚ : ğ•œ â†’ F}, ContDiff ğ•œ âˆ fâ‚‚ â†’ ContDiff ğ•œ âˆ (deriv^[n] fâ‚‚)
  | 0,     _, hf => hf
  | n + 1, _, hf => ContDiff.iterate_deriv n (contDiff_infty_iff_deriv.mp hf).2

@[fun_prop]
theorem ContDiff.iterate_deriv' (n : â„•) :
    âˆ€ (k : â„•) {fâ‚‚ : ğ•œ â†’ F}, ContDiff ğ•œ (n + k : â„•) fâ‚‚ â†’ ContDiff ğ•œ n (deriv^[k] fâ‚‚)
  | 0,     _, hf => hf
  | k + 1, _, hf => ContDiff.iterate_deriv' _ k (contDiff_succ_iff_deriv.mp hf).2.2

end deriv
