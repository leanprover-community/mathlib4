/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.FormalMultilinearSeries

#align_import analysis.calculus.cont_diff_def from "leanprover-community/mathlib"@"3a69562db5a458db8322b190ec8d9a8bbd8a5b14"

/-!
# Higher differentiability

A function is `C^1` on a domain if it is differentiable there, and its derivative is continuous.
By induction, it is `C^n` if it is `C^{n-1}` and its (n-1)-th derivative is `C^1` there or,
equivalently, if it is `C^1` and its derivative is `C^{n-1}`.
Finally, it is `C^âˆ` if it is `C^n` for all n.

We formalize these notions by defining iteratively the `n+1`-th derivative of a function as the
derivative of the `n`-th derivative. It is called `iteratedFDeriv ğ•œ n f x` where `ğ•œ` is the
field, `n` is the number of iterations, `f` is the function and `x` is the point, and it is given
as an `n`-multilinear map. We also define a version `iteratedFDerivWithin` relative to a domain,
as well as predicates `ContDiffWithinAt`, `ContDiffAt`, `ContDiffOn` and
`ContDiff` saying that the function is `C^n` within a set at a point, at a point, on a set
and on the whole space respectively.

To avoid the issue of choice when choosing a derivative in sets where the derivative is not
necessarily unique, `ContDiffOn` is not defined directly in terms of the
regularity of the specific choice `iteratedFDerivWithin ğ•œ n f s` inside `s`, but in terms of the
existence of a nice sequence of derivatives, expressed with a predicate
`HasFTaylorSeriesUpToOn`.

We prove basic properties of these notions.

## Main definitions and results
Let `f : E â†’ F` be a map between normed vector spaces over a nontrivially normed field `ğ•œ`.

* `HasFTaylorSeriesUpTo n f p`: expresses that the formal multilinear series `p` is a sequence
  of iterated derivatives of `f`, up to the `n`-th term (where `n` is a natural number or `âˆ`).
* `HasFTaylorSeriesUpToOn n f p s`: same thing, but inside a set `s`. The notion of derivative
  is now taken inside `s`. In particular, derivatives don't have to be unique.
* `ContDiff ğ•œ n f`: expresses that `f` is `C^n`, i.e., it admits a Taylor series up to
  rank `n`.
* `ContDiffOn ğ•œ n f s`: expresses that `f` is `C^n` in `s`.
* `ContDiffAt ğ•œ n f x`: expresses that `f` is `C^n` around `x`.
* `ContDiffWithinAt ğ•œ n f s x`: expresses that `f` is `C^n` around `x` within the set `s`.
* `iteratedFDerivWithin ğ•œ n f s x` is an `n`-th derivative of `f` over the field `ğ•œ` on the
  set `s` at the point `x`. It is a continuous multilinear map from `E^n` to `F`, defined as a
  derivative within `s` of `iteratedFDerivWithin ğ•œ (n-1) f s` if one exists, and `0` otherwise.
* `iteratedFDeriv ğ•œ n f x` is the `n`-th derivative of `f` over the field `ğ•œ` at the point `x`.
  It is a continuous multilinear map from `E^n` to `F`, defined as a derivative of
  `iteratedFDeriv ğ•œ (n-1) f` if one exists, and `0` otherwise.

In sets of unique differentiability, `ContDiffOn ğ•œ n f s` can be expressed in terms of the
properties of `iteratedFDerivWithin ğ•œ m f s` for `m â‰¤ n`. In the whole space,
`ContDiff ğ•œ n f` can be expressed in terms of the properties of `iteratedFDeriv ğ•œ m f`
for `m â‰¤ n`.

## Implementation notes

The definitions in this file are designed to work on any field `ğ•œ`. They are sometimes slightly more
complicated than the naive definitions one would guess from the intuition over the real or complex
numbers, but they are designed to circumvent the lack of gluing properties and partitions of unity
in general. In the usual situations, they coincide with the usual definitions.

### Definition of `C^n` functions in domains

One could define `C^n` functions in a domain `s` by fixing an arbitrary choice of derivatives (this
is what we do with `iteratedFDerivWithin`) and requiring that all these derivatives up to `n` are
continuous. If the derivative is not unique, this could lead to strange behavior like two `C^n`
functions `f` and `g` on `s` whose sum is not `C^n`. A better definition is thus to say that a
function is `C^n` inside `s` if it admits a sequence of derivatives up to `n` inside `s`.

This definition still has the problem that a function which is locally `C^n` would not need to
be `C^n`, as different choices of sequences of derivatives around different points might possibly
not be glued together to give a globally defined sequence of derivatives. (Note that this issue
can not happen over reals, thanks to partition of unity, but the behavior over a general field is
not so clear, and we want a definition for general fields). Also, there are locality
problems for the order parameter: one could image a function which, for each `n`, has a nice
sequence of derivatives up to order `n`, but they do not coincide for varying `n` and can therefore
not be glued to give rise to an infinite sequence of derivatives. This would give a function
which is `C^n` for all `n`, but not `C^âˆ`. We solve this issue by putting locality conditions
in space and order in our definition of `ContDiffWithinAt` and `ContDiffOn`.
The resulting definition is slightly more complicated to work with (in fact not so much), but it
gives rise to completely satisfactory theorems.

For instance, with this definition, a real function which is `C^m` (but not better) on `(-1/m, 1/m)`
for each natural `m` is by definition `C^âˆ` at `0`.

There is another issue with the definition of `ContDiffWithinAt ğ•œ n f s x`. We can
require the existence and good behavior of derivatives up to order `n` on a neighborhood of `x`
within `s`. However, this does not imply continuity or differentiability within `s` of the function
at `x` when `x` does not belong to `s`. Therefore, we require such existence and good behavior on
a neighborhood of `x` within `s âˆª {x}` (which appears as `insert x s` in this file).

### Side of the composition, and universe issues

With a naÃ¯ve direct definition, the `n`-th derivative of a function belongs to the space
`E â†’L[ğ•œ] (E â†’L[ğ•œ] (E ... F)...)))` where there are n iterations of `E â†’L[ğ•œ]`. This space
may also be seen as the space of continuous multilinear functions on `n` copies of `E` with
values in `F`, by uncurrying. This is the point of view that is usually adopted in textbooks,
and that we also use. This means that the definition and the first proofs are slightly involved,
as one has to keep track of the uncurrying operation. The uncurrying can be done from the
left or from the right, amounting to defining the `n+1`-th derivative either as the derivative of
the `n`-th derivative, or as the `n`-th derivative of the derivative.
For proofs, it would be more convenient to use the latter approach (from the right),
as it means to prove things at the `n+1`-th step we only need to understand well enough the
derivative in `E â†’L[ğ•œ] F` (contrary to the approach from the left, where one would need to know
enough on the `n`-th derivative to deduce things on the `n+1`-th derivative).

However, the definition from the right leads to a universe polymorphism problem: if we define
`iteratedFDeriv ğ•œ (n + 1) f x = iteratedFDeriv ğ•œ n (fderiv ğ•œ f) x` by induction, we need to
generalize over all spaces (as `f` and `fderiv ğ•œ f` don't take values in the same space). It is
only possible to generalize over all spaces in some fixed universe in an inductive definition.
For `f : E â†’ F`, then `fderiv ğ•œ f` is a map `E â†’ (E â†’L[ğ•œ] F)`. Therefore, the definition will only
work if `F` and `E â†’L[ğ•œ] F` are in the same universe.

This issue does not appear with the definition from the left, where one does not need to generalize
over all spaces. Therefore, we use the definition from the left. This means some proofs later on
become a little bit more complicated: to prove that a function is `C^n`, the most efficient approach
is to exhibit a formula for its `n`-th derivative and prove it is continuous (contrary to the
inductive approach where one would prove smoothness statements without giving a formula for the
derivative). In the end, this approach is still satisfactory as it is good to have formulas for the
iterated derivatives in various constructions.

One point where we depart from this explicit approach is in the proof of smoothness of a
composition: there is a formula for the `n`-th derivative of a composition (FaÃ  di Bruno's formula),
but it is very complicated and barely usable, while the inductive proof is very simple. Thus, we
give the inductive proof. As explained above, it works by generalizing over the target space, hence
it only works well if all spaces belong to the same universe. To get the general version, we lift
things to a common universe using a trick.

### Variables management

The textbook definitions and proofs use various identifications and abuse of notations, for instance
when saying that the natural space in which the derivative lives, i.e.,
`E â†’L[ğ•œ] (E â†’L[ğ•œ] ( ... â†’L[ğ•œ] F))`, is the same as a space of multilinear maps. When doing things
formally, we need to provide explicit maps for these identifications, and chase some diagrams to see
everything is compatible with the identifications. In particular, one needs to check that taking the
derivative and then doing the identification, or first doing the identification and then taking the
derivative, gives the same result. The key point for this is that taking the derivative commutes
with continuous linear equivalences. Therefore, we need to implement all our identifications with
continuous linear equivs.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `âŠ¤ : â„•âˆ` with `âˆ`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

noncomputable section

open scoped Classical
open NNReal Topology Filter

local notation "âˆ" => (âŠ¤ : â„•âˆ)

/-
Porting note: These lines are not required in Mathlib4.
attribute [local instance 1001]
  NormedAddCommGroup.toAddCommGroup NormedSpace.toModule' AddCommGroup.toAddCommMonoid
-/

open Set Fin Filter Function

universe u uE uF uG uX

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type uE} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type uF} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] {X : Type uX} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
  {s sâ‚ t u : Set E} {f fâ‚ : E â†’ F} {g : F â†’ G} {x xâ‚€ : E} {c : F} {m n : â„•âˆ}
  {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Functions with a Taylor series on a domain -/

/-- `HasFTaylorSeriesUpToOn n f p s` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`HasFDerivWithinAt` but for higher order derivatives.

Notice that `p` does not sum up to `f` on the diagonal (`FormalMultilinearSeries.sum`), even if
`f` is analytic and `n = âˆ`: an additional `1/m!` factor on the `m`th term is necessary for that. -/
structure HasFTaylorSeriesUpToOn (n : â„•âˆ) (f : E â†’ F) (p : E â†’ FormalMultilinearSeries ğ•œ E F)
  (s : Set E) : Prop where
  zero_eq : âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x
  protected fderivWithin : âˆ€ m : â„•, (m : â„•âˆ) < n â†’ âˆ€ x âˆˆ s,
    HasFDerivWithinAt (p Â· m) (p x m.succ).curryLeft s x
  cont : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (p Â· m) s
#align has_ftaylor_series_up_to_on HasFTaylorSeriesUpToOn

theorem HasFTaylorSeriesUpToOn.zero_eq' (h : HasFTaylorSeriesUpToOn n f p s) {x : E} (hx : x âˆˆ s) :
    p x 0 = (continuousMultilinearCurryFin0 ğ•œ E F).symm (f x) := by
  rw [â† h.zero_eq x hx]
  exact (p x 0).uncurry0_curry0.symm
#align has_ftaylor_series_up_to_on.zero_eq' HasFTaylorSeriesUpToOn.zero_eq'

/-- If two functions coincide on a set `s`, then a Taylor series for the first one is as well a
Taylor series for the second one. -/
theorem HasFTaylorSeriesUpToOn.congr (h : HasFTaylorSeriesUpToOn n f p s)
    (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) : HasFTaylorSeriesUpToOn n fâ‚ p s := by
  refine âŸ¨fun x hx => ?_, h.fderivWithin, h.contâŸ©
  rw [hâ‚ x hx]
  exact h.zero_eq x hx
#align has_ftaylor_series_up_to_on.congr HasFTaylorSeriesUpToOn.congr

theorem HasFTaylorSeriesUpToOn.mono (h : HasFTaylorSeriesUpToOn n f p s) {t : Set E} (hst : t âŠ† s) :
    HasFTaylorSeriesUpToOn n f p t :=
  âŸ¨fun x hx => h.zero_eq x (hst hx), fun m hm x hx => (h.fderivWithin m hm x (hst hx)).mono hst,
    fun m hm => (h.cont m hm).mono hstâŸ©
#align has_ftaylor_series_up_to_on.mono HasFTaylorSeriesUpToOn.mono

theorem HasFTaylorSeriesUpToOn.of_le (h : HasFTaylorSeriesUpToOn n f p s) (hmn : m â‰¤ n) :
    HasFTaylorSeriesUpToOn m f p s :=
  âŸ¨h.zero_eq, fun k hk x hx => h.fderivWithin k (lt_of_lt_of_le hk hmn) x hx, fun k hk =>
    h.cont k (le_trans hk hmn)âŸ©
#align has_ftaylor_series_up_to_on.of_le HasFTaylorSeriesUpToOn.of_le

theorem HasFTaylorSeriesUpToOn.continuousOn (h : HasFTaylorSeriesUpToOn n f p s) :
    ContinuousOn f s := by
  have := (h.cont 0 bot_le).congr fun x hx => (h.zero_eq' hx).symm
  rwa [â† (continuousMultilinearCurryFin0 ğ•œ E F).symm.comp_continuousOn_iff]
#align has_ftaylor_series_up_to_on.continuous_on HasFTaylorSeriesUpToOn.continuousOn

theorem hasFTaylorSeriesUpToOn_zero_iff :
    HasFTaylorSeriesUpToOn 0 f p s â†” ContinuousOn f s âˆ§ âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x := by
  refine âŸ¨fun H => âŸ¨H.continuousOn, H.zero_eqâŸ©, fun H =>
      âŸ¨H.2, fun m hm => False.elim (not_le.2 hm bot_le), fun m hm â†¦ ?_âŸ©âŸ©
  obtain rfl : m = 0 := mod_cast hm.antisymm (zero_le _)
  have : EqOn (p Â· 0) ((continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f) s := fun x hx â†¦
    (continuousMultilinearCurryFin0 ğ•œ E F).eq_symm_apply.2 (H.2 x hx)
  rw [continuousOn_congr this, LinearIsometryEquiv.comp_continuousOn_iff]
  exact H.1
#align has_ftaylor_series_up_to_on_zero_iff hasFTaylorSeriesUpToOn_zero_iff

theorem hasFTaylorSeriesUpToOn_top_iff :
    HasFTaylorSeriesUpToOn âˆ f p s â†” âˆ€ n : â„•, HasFTaylorSeriesUpToOn n f p s := by
  constructor
  Â· intro H n; exact H.of_le le_top
  Â· intro H
    constructor
    Â· exact (H 0).zero_eq
    Â· intro m _
      apply (H m.succ).fderivWithin m (WithTop.coe_lt_coe.2 (lt_add_one m))
    Â· intro m _
      apply (H m).cont m le_rfl
#align has_ftaylor_series_up_to_on_top_iff hasFTaylorSeriesUpToOn_top_iff

/-- In the case that `n = âˆ` we don't need the continuity assumption in
`HasFTaylorSeriesUpToOn`. -/
theorem hasFTaylorSeriesUpToOn_top_iff' :
    HasFTaylorSeriesUpToOn âˆ f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x) âˆ§
        âˆ€ m : â„•, âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y m) (p x m.succ).curryLeft s x :=
  -- Everything except for the continuity is trivial:
  âŸ¨fun h => âŸ¨h.1, fun m => h.2 m (WithTop.coe_lt_top m)âŸ©, fun h =>
    âŸ¨h.1, fun m _ => h.2 m, fun m _ x hx =>
      -- The continuity follows from the existence of a derivative:
      (h.2 m x hx).continuousWithinAtâŸ©âŸ©
#align has_ftaylor_series_up_to_on_top_iff' hasFTaylorSeriesUpToOn_top_iff'

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpToOn.hasFDerivWithinAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : x âˆˆ s) : HasFDerivWithinAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) s x := by
  have A : âˆ€ y âˆˆ s, f y = (continuousMultilinearCurryFin0 ğ•œ E F) (p y 0) := fun y hy â†¦
    (h.zero_eq y hy).symm
  suffices H : HasFDerivWithinAt (continuousMultilinearCurryFin0 ğ•œ E F âˆ˜ (p Â· 0))
    (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) s x from H.congr A (A x hx)
  rw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff']
  have : ((0 : â„•) : â„•âˆ) < n := zero_lt_one.trans_le hn
  convert h.fderivWithin _ this x hx
  ext y v
  change (p x 1) (snoc 0 y) = (p x 1) (cons y v)
  congr with i
  rw [Unique.eq_default (Î± := Fin 1) i]
  rfl
#align has_ftaylor_series_up_to_on.has_fderiv_within_at HasFTaylorSeriesUpToOn.hasFDerivWithinAt

theorem HasFTaylorSeriesUpToOn.differentiableOn (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n) :
    DifferentiableOn ğ•œ f s := fun _x hx => (h.hasFDerivWithinAt hn hx).differentiableWithinAt
#align has_ftaylor_series_up_to_on.differentiable_on HasFTaylorSeriesUpToOn.differentiableOn

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then the term
of order `1` of this series is a derivative of `f` at `x`. -/
theorem HasFTaylorSeriesUpToOn.hasFDerivAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : s âˆˆ ğ“ x) : HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) x :=
  (h.hasFDerivWithinAt hn (mem_of_mem_nhds hx)).hasFDerivAt hx
#align has_ftaylor_series_up_to_on.has_fderiv_at HasFTaylorSeriesUpToOn.hasFDerivAt

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
in a neighborhood of `x`, the term of order `1` of this series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpToOn.eventually_hasFDerivAt (h : HasFTaylorSeriesUpToOn n f p s)
    (hn : 1 â‰¤ n) (hx : s âˆˆ ğ“ x) :
    âˆ€á¶  y in ğ“ x, HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p y 1)) y :=
  (eventually_eventually_nhds.2 hx).mono fun _y hy => h.hasFDerivAt hn hy
#align has_ftaylor_series_up_to_on.eventually_has_fderiv_at HasFTaylorSeriesUpToOn.eventually_hasFDerivAt

/-- If a function has a Taylor series at order at least `1` on a neighborhood of `x`, then
it is differentiable at `x`. -/
theorem HasFTaylorSeriesUpToOn.differentiableAt (h : HasFTaylorSeriesUpToOn n f p s) (hn : 1 â‰¤ n)
    (hx : s âˆˆ ğ“ x) : DifferentiableAt ğ•œ f x :=
  (h.hasFDerivAt hn hx).differentiableAt
#align has_ftaylor_series_up_to_on.differentiable_at HasFTaylorSeriesUpToOn.differentiableAt

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p` is a Taylor series up to `n`, and
`p (n + 1)` is a derivative of `p n`. -/
theorem hasFTaylorSeriesUpToOn_succ_iff_left {n : â„•} :
    HasFTaylorSeriesUpToOn (n + 1) f p s â†”
      HasFTaylorSeriesUpToOn n f p s âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y n) (p x n.succ).curryLeft s x) âˆ§
          ContinuousOn (fun x => p x (n + 1)) s := by
  constructor
  Â· exact fun h â†¦ âŸ¨h.of_le (WithTop.coe_le_coe.2 (Nat.le_succ n)),
      h.fderivWithin _ (WithTop.coe_lt_coe.2 (lt_add_one n)), h.cont (n + 1) le_rflâŸ©
  Â· intro h
    constructor
    Â· exact h.1.zero_eq
    Â· intro m hm
      by_cases h' : m < n
      Â· exact h.1.fderivWithin m (WithTop.coe_lt_coe.2 h')
      Â· have : m = n := Nat.eq_of_lt_succ_of_not_lt (WithTop.coe_lt_coe.1 hm) h'
        rw [this]
        exact h.2.1
    Â· intro m hm
      by_cases h' : m â‰¤ n
      Â· apply h.1.cont m (WithTop.coe_le_coe.2 h')
      Â· have : m = n + 1 := le_antisymm (WithTop.coe_le_coe.1 hm) (not_le.1 h')
        rw [this]
        exact h.2.2
#align has_ftaylor_series_up_to_on_succ_iff_left hasFTaylorSeriesUpToOn_succ_iff_left

#adaptation_note
/--
After https://github.com/leanprover/lean4/pull/4119,
without `set_option maxSynthPendingDepth 2` this proof needs substantial repair.
-/
set_option maxSynthPendingDepth 2 in
-- Porting note: this was split out from `hasFTaylorSeriesUpToOn_succ_iff_right` to avoid a timeout.
theorem HasFTaylorSeriesUpToOn.shift_of_succ
    {n : â„•} (H : HasFTaylorSeriesUpToOn (n + 1 : â„•) f p s) :
    (HasFTaylorSeriesUpToOn n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
      (fun x => (p x).shift)) s := by
  constructor
  Â· intro x _
    rfl
  Â· intro m (hm : (m : â„•âˆ) < n) x (hx : x âˆˆ s)
    have A : (m.succ : â„•âˆ) < n.succ := by
      rw [Nat.cast_lt] at hm âŠ¢
      exact Nat.succ_lt_succ hm
    change HasFDerivWithinAt ((continuousMultilinearCurryRightEquiv' ğ•œ m E F).symm âˆ˜ (p Â· m.succ))
      (p x m.succ.succ).curryRight.curryLeft s x
    rw [((continuousMultilinearCurryRightEquiv' ğ•œ m E F).symm).comp_hasFDerivWithinAt_iff']
    convert H.fderivWithin _ A x hx
    ext y v
    change p x (m + 2) (snoc (cons y (init v)) (v (last _))) = p x (m + 2) (cons y v)
    rw [â† cons_snoc_eq_snoc_cons, snoc_init_self]
  Â· intro m (hm : (m : â„•âˆ) â‰¤ n)
    suffices A : ContinuousOn (p Â· (m + 1)) s from
      ((continuousMultilinearCurryRightEquiv' ğ•œ m E F).symm).continuous.comp_continuousOn A
    refine H.cont _ ?_
    rw [Nat.cast_le] at hm âŠ¢
    exact Nat.succ_le_succ hm

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. -/
theorem hasFTaylorSeriesUpToOn_succ_iff_right {n : â„•} :
    HasFTaylorSeriesUpToOn (n + 1 : â„•) f p s â†”
      (âˆ€ x âˆˆ s, (p x 0).uncurry0 = f x) âˆ§
        (âˆ€ x âˆˆ s, HasFDerivWithinAt (fun y => p y 0) (p x 1).curryLeft s x) âˆ§
          HasFTaylorSeriesUpToOn n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1))
            (fun x => (p x).shift) s := by
  constructor
  Â· intro H
    refine âŸ¨H.zero_eq, H.fderivWithin 0 (Nat.cast_lt.2 (Nat.succ_pos n)), ?_âŸ©
    exact H.shift_of_succ
  Â· rintro âŸ¨Hzero_eq, Hfderiv_zero, HtaylorâŸ©
    constructor
    Â· exact Hzero_eq
    Â· intro m (hm : (m : â„•âˆ) < n.succ) x (hx : x âˆˆ s)
      cases' m with m
      Â· exact Hfderiv_zero x hx
      Â· have A : (m : â„•âˆ) < n := by
          rw [Nat.cast_lt] at hm âŠ¢
          exact Nat.lt_of_succ_lt_succ hm
        have :
          HasFDerivWithinAt ((continuousMultilinearCurryRightEquiv' ğ•œ m E F).symm âˆ˜ (p Â· m.succ))
            ((p x).shift m.succ).curryLeft s x := Htaylor.fderivWithin _ A x hx
        rw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff'] at this
        convert this
        ext y v
        change
          (p x (Nat.succ (Nat.succ m))) (cons y v) =
            (p x m.succ.succ) (snoc (cons y (init v)) (v (last _)))
        rw [â† cons_snoc_eq_snoc_cons, snoc_init_self]
    Â· intro m (hm : (m : â„•âˆ) â‰¤ n.succ)
      cases' m with m
      Â· have : DifferentiableOn ğ•œ (fun x => p x 0) s := fun x hx =>
          (Hfderiv_zero x hx).differentiableWithinAt
        exact this.continuousOn
      Â· refine (continuousMultilinearCurryRightEquiv' ğ•œ m E F).symm.comp_continuousOn_iff.mp ?_
        refine Htaylor.cont _ ?_
        rw [Nat.cast_le] at hm âŠ¢
        exact Nat.lt_succ_iff.mp hm
#align has_ftaylor_series_up_to_on_succ_iff_right hasFTaylorSeriesUpToOn_succ_iff_right

/-! ### Smooth functions within a set around a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to order `n` within a set `s` at a point `x` if
it admits continuous derivatives up to order `n` in a neighborhood of `x` in `s âˆª {x}`.
For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).

For instance, a real function which is `C^m` on `(-1/m, 1/m)` for each natural `m`, but not
better, is `C^âˆ` at `0` within `univ`.
-/
def ContDiffWithinAt (n : â„•âˆ) (f : E â†’ F) (s : Set E) (x : E) : Prop :=
  âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ âˆƒ u âˆˆ ğ“[insert x s] x,
    âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn m f p u
#align cont_diff_within_at ContDiffWithinAt

variable {ğ•œ}

theorem contDiffWithinAt_nat {n : â„•} :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x,
      âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn n f p u :=
  âŸ¨fun H => H n le_rfl, fun âŸ¨u, hu, p, hpâŸ© _m hm => âŸ¨u, hu, p, hp.of_le hmâŸ©âŸ©
#align cont_diff_within_at_nat contDiffWithinAt_nat

theorem ContDiffWithinAt.of_le (h : ContDiffWithinAt ğ•œ n f s x) (hmn : m â‰¤ n) :
    ContDiffWithinAt ğ•œ m f s x := fun k hk => h k (le_trans hk hmn)
#align cont_diff_within_at.of_le ContDiffWithinAt.of_le

theorem contDiffWithinAt_iff_forall_nat_le :
    ContDiffWithinAt ğ•œ n f s x â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffWithinAt ğ•œ m f s x :=
  âŸ¨fun H _m hm => H.of_le hm, fun H m hm => H m hm _ le_rflâŸ©
#align cont_diff_within_at_iff_forall_nat_le contDiffWithinAt_iff_forall_nat_le

theorem contDiffWithinAt_top : ContDiffWithinAt ğ•œ âˆ f s x â†” âˆ€ n : â„•, ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_iff_forall_nat_le.trans <| by simp only [forall_prop_of_true, le_top]
#align cont_diff_within_at_top contDiffWithinAt_top

theorem ContDiffWithinAt.continuousWithinAt (h : ContDiffWithinAt ğ•œ n f s x) :
    ContinuousWithinAt f s x := by
  rcases h 0 bot_le with âŸ¨u, hu, p, HâŸ©
  rw [mem_nhdsWithin_insert] at hu
  exact (H.continuousOn.continuousWithinAt hu.1).mono_of_mem hu.2
#align cont_diff_within_at.continuous_within_at ContDiffWithinAt.continuousWithinAt

theorem ContDiffWithinAt.congr_of_eventuallyEq (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x := fun m hm =>
  let âŸ¨u, hu, p, HâŸ© := h m hm
  âŸ¨{ x âˆˆ u | fâ‚ x = f x }, Filter.inter_mem hu (mem_nhdsWithin_insert.2 âŸ¨hx, hâ‚âŸ©), p,
    (H.mono (sep_subset _ _)).congr fun _ => And.rightâŸ©
#align cont_diff_within_at.congr_of_eventually_eq ContDiffWithinAt.congr_of_eventuallyEq

theorem ContDiffWithinAt.congr_of_eventuallyEq_insert (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (nhdsWithin_mono x (subset_insert x s) hâ‚)
    (mem_of_mem_nhdsWithin (mem_insert x s) hâ‚ : _)
#align cont_diff_within_at.congr_of_eventually_eq_insert ContDiffWithinAt.congr_of_eventuallyEq_insert

theorem ContDiffWithinAt.congr_of_eventually_eq' (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq hâ‚ <| hâ‚.self_of_nhdsWithin hx
#align cont_diff_within_at.congr_of_eventually_eq' ContDiffWithinAt.congr_of_eventually_eq'

theorem Filter.EventuallyEq.contDiffWithinAt_iff (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H => ContDiffWithinAt.congr_of_eventuallyEq H hâ‚.symm hx.symm, fun H =>
    H.congr_of_eventuallyEq hâ‚ hxâŸ©
#align filter.eventually_eq.cont_diff_within_at_iff Filter.EventuallyEq.contDiffWithinAt_iff

theorem ContDiffWithinAt.congr (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (Filter.eventuallyEq_of_mem self_mem_nhdsWithin hâ‚) hx
#align cont_diff_within_at.congr ContDiffWithinAt.congr

theorem ContDiffWithinAt.congr' (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr hâ‚ (hâ‚ _ hx)
#align cont_diff_within_at.congr' ContDiffWithinAt.congr'

theorem ContDiffWithinAt.mono_of_mem (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : s âˆˆ ğ“[t] x) : ContDiffWithinAt ğ•œ n f t x := by
  intro m hm
  rcases h m hm with âŸ¨u, hu, p, HâŸ©
  exact âŸ¨u, nhdsWithin_le_of_mem (insert_mem_nhdsWithin_insert hst) hu, p, HâŸ©
#align cont_diff_within_at.mono_of_mem ContDiffWithinAt.mono_of_mem

theorem ContDiffWithinAt.mono (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E} (hst : t âŠ† s) :
    ContDiffWithinAt ğ•œ n f t x :=
  h.mono_of_mem <| Filter.mem_of_superset self_mem_nhdsWithin hst
#align cont_diff_within_at.mono ContDiffWithinAt.mono

theorem ContDiffWithinAt.congr_nhds (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : ğ“[s] x = ğ“[t] x) : ContDiffWithinAt ğ•œ n f t x :=
  h.mono_of_mem <| hst â–¸ self_mem_nhdsWithin
#align cont_diff_within_at.congr_nhds ContDiffWithinAt.congr_nhds

theorem contDiffWithinAt_congr_nhds {t : Set E} (hst : ğ“[s] x = ğ“[t] x) :
    ContDiffWithinAt ğ•œ n f s x â†” ContDiffWithinAt ğ•œ n f t x :=
  âŸ¨fun h => h.congr_nhds hst, fun h => h.congr_nhds hst.symmâŸ©
#align cont_diff_within_at_congr_nhds contDiffWithinAt_congr_nhds

theorem contDiffWithinAt_inter' (h : t âˆˆ ğ“[s] x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr_nhds <| Eq.symm <| nhdsWithin_restrict'' _ h
#align cont_diff_within_at_inter' contDiffWithinAt_inter'

theorem contDiffWithinAt_inter (h : t âˆˆ ğ“ x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds h)
#align cont_diff_within_at_inter contDiffWithinAt_inter

theorem contDiffWithinAt_insert_self :
    ContDiffWithinAt ğ•œ n f (insert x s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  simp_rw [ContDiffWithinAt, insert_idem]

theorem contDiffWithinAt_insert {y : E} :
    ContDiffWithinAt ğ•œ n f (insert y s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  rcases eq_or_ne x y with (rfl | h)
  Â· exact contDiffWithinAt_insert_self
  simp_rw [ContDiffWithinAt, insert_comm x y, nhdsWithin_insert_of_ne h]
#align cont_diff_within_at_insert contDiffWithinAt_insert

alias âŸ¨ContDiffWithinAt.of_insert, ContDiffWithinAt.insert'âŸ© := contDiffWithinAt_insert
#align cont_diff_within_at.of_insert ContDiffWithinAt.of_insert
#align cont_diff_within_at.insert' ContDiffWithinAt.insert'

protected theorem ContDiffWithinAt.insert (h : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n f (insert x s) x :=
  h.insert'
#align cont_diff_within_at.insert ContDiffWithinAt.insert

/-- If a function is `C^n` within a set at a point, with `n â‰¥ 1`, then it is differentiable
within this set at this point. -/
theorem ContDiffWithinAt.differentiable_within_at' (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f (insert x s) x := by
  rcases h 1 hn with âŸ¨u, hu, p, HâŸ©
  rcases mem_nhdsWithin.1 hu with âŸ¨t, t_open, xt, tuâŸ©
  rw [inter_comm] at tu
  have := ((H.mono tu).differentiableOn le_rfl) x âŸ¨mem_insert x s, xtâŸ©
  exact (differentiableWithinAt_inter (IsOpen.mem_nhds t_open xt)).1 this
#align cont_diff_within_at.differentiable_within_at' ContDiffWithinAt.differentiable_within_at'

theorem ContDiffWithinAt.differentiableWithinAt (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f s x :=
  (h.differentiable_within_at' hn).mono (subset_insert x s)
#align cont_diff_within_at.differentiable_within_at ContDiffWithinAt.differentiableWithinAt

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt {n : â„•} :
    ContDiffWithinAt ğ•œ (n + 1 : â„•) f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
      (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffWithinAt ğ•œ n f' u x := by
  constructor
  Â· intro h
    rcases h n.succ le_rfl with âŸ¨u, hu, p, HpâŸ©
    refine
      âŸ¨u, hu, fun y => (continuousMultilinearCurryFin1 ğ•œ E F) (p y 1), fun y hy =>
        Hp.hasFDerivWithinAt (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)) hy, ?_âŸ©
    intro m hm
    refine âŸ¨u, ?_, fun y : E => (p y).shift, ?_âŸ©
    Â· -- Porting note: without the explicit argument Lean is not sure of the type.
      convert @self_mem_nhdsWithin _ _ x u
      have : x âˆˆ insert x s := by simp
      exact insert_eq_of_mem (mem_of_mem_nhdsWithin this hu)
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right] at Hp
      exact Hp.2.2.of_le hm
  Â· rintro âŸ¨u, hu, f', f'_eq_deriv, Hf'âŸ©
    rw [contDiffWithinAt_nat]
    rcases Hf' n le_rfl with âŸ¨v, hv, p', Hp'âŸ©
    refine âŸ¨v âˆ© u, ?_, fun x => (p' x).unshift (f x), ?_âŸ©
    Â· apply Filter.inter_mem _ hu
      apply nhdsWithin_le_of_mem hu
      exact nhdsWithin_mono _ (subset_insert x u) hv
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right]
      refine âŸ¨fun y _ => rfl, fun y hy => ?_, ?_âŸ©
      Â· change
          HasFDerivWithinAt (fun z => (continuousMultilinearCurryFin0 ğ•œ E F).symm (f z))
            (FormalMultilinearSeries.unshift (p' y) (f y) 1).curryLeft (v âˆ© u) y
        -- Porting note: needed `erw` here.
        -- https://github.com/leanprover-community/mathlib4/issues/5164
        erw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff']
        convert (f'_eq_deriv y hy.2).mono inter_subset_right
        rw [â† Hp'.zero_eq y hy.1]
        ext z
        change ((p' y 0) (init (@cons 0 (fun _ => E) z 0))) (@cons 0 (fun _ => E) z 0 (last 0)) =
          ((p' y 0) 0) z
        congr
        norm_num [eq_iff_true_of_subsingleton]
      Â· convert (Hp'.mono inter_subset_left).congr fun x hx => Hp'.zero_eq x hx.1 using 1
        Â· ext x y
          change p' x 0 (init (@snoc 0 (fun _ : Fin 1 => E) 0 y)) y = p' x 0 0 y
          rw [init_snoc]
        Â· ext x k v y
          change p' x k (init (@snoc k (fun _ : Fin k.succ => E) v y))
            (@snoc k (fun _ : Fin k.succ => E) v y (last k)) = p' x k v y
          rw [snoc_last, init_snoc]
#align cont_diff_within_at_succ_iff_has_fderiv_within_at contDiffWithinAt_succ_iff_hasFDerivWithinAt

/-- A version of `contDiffWithinAt_succ_iff_hasFDerivWithinAt` where all derivatives
  are taken within the same set. -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt' {n : â„•} :
    ContDiffWithinAt ğ•œ (n + 1 : â„•) f s x â†”
      âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) s x) âˆ§ ContDiffWithinAt ğ•œ n f' s x := by
  refine âŸ¨fun hf => ?_, ?_âŸ©
  Â· obtain âŸ¨u, hu, f', huf', hf'âŸ© := contDiffWithinAt_succ_iff_hasFDerivWithinAt.mp hf
    obtain âŸ¨w, hw, hxw, hwuâŸ© := mem_nhdsWithin.mp hu
    rw [inter_comm] at hwu
    refine âŸ¨insert x s âˆ© w, inter_mem_nhdsWithin _ (hw.mem_nhds hxw), inter_subset_left, f',
      fun y hy => ?_, ?_âŸ©
    Â· refine ((huf' y <| hwu hy).mono hwu).mono_of_mem ?_
      refine mem_of_superset ?_ (inter_subset_inter_left _ (subset_insert _ _))
      exact inter_mem_nhdsWithin _ (hw.mem_nhds hy.2)
    Â· exact hf'.mono_of_mem (nhdsWithin_mono _ (subset_insert _ _) hu)
  Â· rw [â† contDiffWithinAt_insert, contDiffWithinAt_succ_iff_hasFDerivWithinAt,
      insert_eq_of_mem (mem_insert _ _)]
    rintro âŸ¨u, hu, hus, f', huf', hf'âŸ©
    exact âŸ¨u, hu, f', fun y hy => (huf' y hy).insert'.mono hus, hf'.insert.mono husâŸ©
#align cont_diff_within_at_succ_iff_has_fderiv_within_at' contDiffWithinAt_succ_iff_hasFDerivWithinAt'

/-! ### Smooth functions within a set -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` on `s` if, for any point `x` in `s`, it
admits continuous derivatives up to order `n` on a neighborhood of `x` in `s`.

For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).
-/
def ContDiffOn (n : â„•âˆ) (f : E â†’ F) (s : Set E) : Prop :=
  âˆ€ x âˆˆ s, ContDiffWithinAt ğ•œ n f s x
#align cont_diff_on ContDiffOn

variable {ğ•œ}

theorem HasFTaylorSeriesUpToOn.contDiffOn {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpToOn n f f' s) : ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  use s
  simp only [Set.insert_eq_of_mem hx, self_mem_nhdsWithin, true_and_iff]
  exact âŸ¨f', hf.of_le hmâŸ©
#align has_ftaylor_series_up_to_on.cont_diff_on HasFTaylorSeriesUpToOn.contDiffOn

theorem ContDiffOn.contDiffWithinAt (h : ContDiffOn ğ•œ n f s) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n f s x :=
  h x hx
#align cont_diff_on.cont_diff_within_at ContDiffOn.contDiffWithinAt

theorem ContDiffWithinAt.contDiffOn' {m : â„•} (hm : (m : â„•âˆ) â‰¤ n)
    (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ m f (insert x s âˆ© u) := by
  rcases h m hm with âŸ¨t, ht, p, hpâŸ©
  rcases mem_nhdsWithin.1 ht with âŸ¨u, huo, hxu, hutâŸ©
  rw [inter_comm] at hut
  exact âŸ¨u, huo, hxu, (hp.mono hut).contDiffOnâŸ©
#align cont_diff_within_at.cont_diff_on' ContDiffWithinAt.contDiffOn'

theorem ContDiffWithinAt.contDiffOn {m : â„•} (hm : (m : â„•âˆ) â‰¤ n) (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ ContDiffOn ğ•œ m f u :=
  let âŸ¨_u, uo, xu, hâŸ© := h.contDiffOn' hm
  âŸ¨_, inter_mem_nhdsWithin _ (uo.mem_nhds xu), inter_subset_left, hâŸ©
#align cont_diff_within_at.cont_diff_on ContDiffWithinAt.contDiffOn

protected theorem ContDiffWithinAt.eventually {n : â„•} (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆ€á¶  y in ğ“[insert x s] x, ContDiffWithinAt ğ•œ n f s y := by
  rcases h.contDiffOn le_rfl with âŸ¨u, hu, _, hdâŸ©
  have : âˆ€á¶  y : E in ğ“[insert x s] x, u âˆˆ ğ“[insert x s] y âˆ§ y âˆˆ u :=
    (eventually_nhdsWithin_nhdsWithin.2 hu).and hu
  refine this.mono fun y hy => (hd y hy.2).mono_of_mem ?_
  exact nhdsWithin_mono y (subset_insert _ _) hy.1
#align cont_diff_within_at.eventually ContDiffWithinAt.eventually

theorem ContDiffOn.of_le (h : ContDiffOn ğ•œ n f s) (hmn : m â‰¤ n) : ContDiffOn ğ•œ m f s := fun x hx =>
  (h x hx).of_le hmn
#align cont_diff_on.of_le ContDiffOn.of_le

theorem ContDiffOn.of_succ {n : â„•} (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ n f s :=
  h.of_le <| WithTop.coe_le_coe.mpr le_self_add
#align cont_diff_on.of_succ ContDiffOn.of_succ

theorem ContDiffOn.one_of_succ {n : â„•} (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ 1 f s :=
  h.of_le <| WithTop.coe_le_coe.mpr le_add_self
#align cont_diff_on.one_of_succ ContDiffOn.one_of_succ

theorem contDiffOn_iff_forall_nat_le : ContDiffOn ğ•œ n f s â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffOn ğ•œ m f s :=
  âŸ¨fun H _ hm => H.of_le hm, fun H x hx m hm => H m hm x hx m le_rflâŸ©
#align cont_diff_on_iff_forall_nat_le contDiffOn_iff_forall_nat_le

theorem contDiffOn_top : ContDiffOn ğ•œ âˆ f s â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s :=
  contDiffOn_iff_forall_nat_le.trans <| by simp only [le_top, forall_prop_of_true]
#align cont_diff_on_top contDiffOn_top

theorem contDiffOn_all_iff_nat : (âˆ€ n, ContDiffOn ğ•œ n f s) â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s := by
  refine âŸ¨fun H n => H n, ?_âŸ©
  rintro H (_ | n)
  exacts [contDiffOn_top.2 H, H n]
#align cont_diff_on_all_iff_nat contDiffOn_all_iff_nat

theorem ContDiffOn.continuousOn (h : ContDiffOn ğ•œ n f s) : ContinuousOn f s := fun x hx =>
  (h x hx).continuousWithinAt
#align cont_diff_on.continuous_on ContDiffOn.continuousOn

theorem ContDiffOn.congr (h : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
    ContDiffOn ğ•œ n fâ‚ s := fun x hx => (h x hx).congr hâ‚ (hâ‚ x hx)
#align cont_diff_on.congr ContDiffOn.congr

theorem contDiffOn_congr (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) : ContDiffOn ğ•œ n fâ‚ s â†” ContDiffOn ğ•œ n f s :=
  âŸ¨fun H => H.congr fun x hx => (hâ‚ x hx).symm, fun H => H.congr hâ‚âŸ©
#align cont_diff_on_congr contDiffOn_congr

theorem ContDiffOn.mono (h : ContDiffOn ğ•œ n f s) {t : Set E} (hst : t âŠ† s) : ContDiffOn ğ•œ n f t :=
  fun x hx => (h x (hst hx)).mono hst
#align cont_diff_on.mono ContDiffOn.mono

theorem ContDiffOn.congr_mono (hf : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ sâ‚, fâ‚ x = f x) (hs : sâ‚ âŠ† s) :
    ContDiffOn ğ•œ n fâ‚ sâ‚ :=
  (hf.mono hs).congr hâ‚
#align cont_diff_on.congr_mono ContDiffOn.congr_mono

/-- If a function is `C^n` on a set with `n â‰¥ 1`, then it is differentiable there. -/
theorem ContDiffOn.differentiableOn (h : ContDiffOn ğ•œ n f s) (hn : 1 â‰¤ n) :
    DifferentiableOn ğ•œ f s := fun x hx => (h x hx).differentiableWithinAt hn
#align cont_diff_on.differentiable_on ContDiffOn.differentiableOn

/-- If a function is `C^n` around each point in a set, then it is `C^n` on the set. -/
theorem contDiffOn_of_locally_contDiffOn
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ n f (s âˆ© u)) : ContDiffOn ğ•œ n f s := by
  intro x xs
  rcases h x xs with âŸ¨u, u_open, xu, huâŸ©
  apply (contDiffWithinAt_inter _).1 (hu x âŸ¨xs, xuâŸ©)
  exact IsOpen.mem_nhds u_open xu
#align cont_diff_on_of_locally_cont_diff_on contDiffOn_of_locally_contDiffOn

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem contDiffOn_succ_iff_hasFDerivWithinAt {n : â„•} :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffOn ğ•œ n f' u := by
  constructor
  Â· intro h x hx
    rcases (h x hx) n.succ le_rfl with âŸ¨u, hu, p, HpâŸ©
    refine
      âŸ¨u, hu, fun y => (continuousMultilinearCurryFin1 ğ•œ E F) (p y 1), fun y hy =>
        Hp.hasFDerivWithinAt (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)) hy, ?_âŸ©
    rw [hasFTaylorSeriesUpToOn_succ_iff_right] at Hp
    intro z hz m hm
    refine âŸ¨u, ?_, fun x : E => (p x).shift, Hp.2.2.of_le hmâŸ©
    -- Porting note: without the explicit arguments `convert` can not determine the type.
    convert @self_mem_nhdsWithin _ _ z u
    exact insert_eq_of_mem hz
  Â· intro h x hx
    rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt]
    rcases h x hx with âŸ¨u, u_nhbd, f', hu, hf'âŸ©
    have : x âˆˆ u := mem_of_mem_nhdsWithin (mem_insert _ _) u_nhbd
    exact âŸ¨u, u_nhbd, f', hu, hf' x thisâŸ©
#align cont_diff_on_succ_iff_has_fderiv_within_at contDiffOn_succ_iff_hasFDerivWithinAt

/-! ### Iterated derivative within a set -/


variable (ğ•œ)

/-- The `n`-th derivative of a function along a set, defined inductively by saying that the `n+1`-th
derivative of `f` is the derivative of the `n`-th derivative of `f` along this set, together with
an uncurrying step to see it as a multilinear map in `n+1` variables..
-/
noncomputable def iteratedFDerivWithin (n : â„•) (f : E â†’ F) (s : Set E) : E â†’ E[Ã—n]â†’L[ğ•œ] F :=
  Nat.recOn n (fun x => ContinuousMultilinearMap.curry0 ğ•œ E (f x)) fun _ rec x =>
    ContinuousLinearMap.uncurryLeft (fderivWithin ğ•œ rec s x)
#align iterated_fderiv_within iteratedFDerivWithin

/-- Formal Taylor series associated to a function within a set. -/
def ftaylorSeriesWithin (f : E â†’ F) (s : Set E) (x : E) : FormalMultilinearSeries ğ•œ E F := fun n =>
  iteratedFDerivWithin ğ•œ n f s x
#align ftaylor_series_within ftaylorSeriesWithin

variable {ğ•œ}

@[simp]
theorem iteratedFDerivWithin_zero_apply (m : Fin 0 â†’ E) :
    (iteratedFDerivWithin ğ•œ 0 f s x : (Fin 0 â†’ E) â†’ F) m = f x :=
  rfl
#align iterated_fderiv_within_zero_apply iteratedFDerivWithin_zero_apply

theorem iteratedFDerivWithin_zero_eq_comp :
    iteratedFDerivWithin ğ•œ 0 f s = (continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f :=
  rfl
#align iterated_fderiv_within_zero_eq_comp iteratedFDerivWithin_zero_eq_comp

@[simp]
theorem norm_iteratedFDerivWithin_zero : â€–iteratedFDerivWithin ğ•œ 0 f s xâ€– = â€–f xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDerivWithin_zero_eq_comp, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_iterated_fderiv_within_zero norm_iteratedFDerivWithin_zero

theorem iteratedFDerivWithin_succ_apply_left {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDerivWithin ğ•œ (n + 1) f s x : (Fin (n + 1) â†’ E) â†’ F) m =
      (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s x : E â†’ E[Ã—n]â†’L[ğ•œ] F) (m 0) (tail m) :=
  rfl
#align iterated_fderiv_within_succ_apply_left iteratedFDerivWithin_succ_apply_left

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
theorem iteratedFDerivWithin_succ_eq_comp_left {n : â„•} :
    iteratedFDerivWithin ğ•œ (n + 1) f s =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F :
          (E â†’L[ğ•œ] (E [Ã—n]â†’L[ğ•œ] F)) â†’ (E [Ã—n.succ]â†’L[ğ•œ] F)) âˆ˜
        fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s :=
  rfl
#align iterated_fderiv_within_succ_eq_comp_left iteratedFDerivWithin_succ_eq_comp_left

theorem fderivWithin_iteratedFDerivWithin {s : Set E} {n : â„•} :
    fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F).symm âˆ˜
        iteratedFDerivWithin ğ•œ (n + 1) f s := by
  rw [iteratedFDerivWithin_succ_eq_comp_left]
  ext1 x
  simp only [Function.comp_apply, LinearIsometryEquiv.symm_apply_apply]

theorem norm_fderivWithin_iteratedFDerivWithin {n : â„•} :
    â€–fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n f s) s xâ€– =
      â€–iteratedFDerivWithin ğ•œ (n + 1) f s xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDerivWithin_succ_eq_comp_left, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_fderiv_within_iterated_fderiv_within norm_fderivWithin_iteratedFDerivWithin

theorem iteratedFDerivWithin_succ_apply_right {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s)
    (m : Fin (n + 1) â†’ E) :
    (iteratedFDerivWithin ğ•œ (n + 1) f s x : (Fin (n + 1) â†’ E) â†’ F) m =
      iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s x (init m) (m (last n)) := by
  induction' n with n IH generalizing x
  Â· rw [iteratedFDerivWithin_succ_eq_comp_left, iteratedFDerivWithin_zero_eq_comp,
      iteratedFDerivWithin_zero_apply, Function.comp_apply,
      LinearIsometryEquiv.comp_fderivWithin _ (hs x hx)]
    rfl
  Â· let I := continuousMultilinearCurryRightEquiv' ğ•œ n E F
    have A : âˆ€ y âˆˆ s, iteratedFDerivWithin ğ•œ n.succ f s y =
        (I âˆ˜ iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) y := fun y hy â†¦ by
      ext m
      rw [@IH y hy m]
      rfl
    calc
      (iteratedFDerivWithin ğ•œ (n + 2) f s x : (Fin (n + 2) â†’ E) â†’ F) m =
          (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n.succ f s) s x : E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0)
            (tail m) :=
        rfl
      _ = (fderivWithin ğ•œ (I âˆ˜ iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s) s x :
              E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0) (tail m) := by
        rw [fderivWithin_congr A (A x hx)]
      _ = (I âˆ˜ fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s) s x :
              E â†’ E[Ã—n + 1]â†’L[ğ•œ] F) (m 0) (tail m) := by
        #adaptation_note
        /--
        After https://github.com/leanprover/lean4/pull/4119 we need to either use
        `set_option maxSynthPendingDepth 2 in`
        or fill in an explicit argument as
        ```
        simp only [LinearIsometryEquiv.comp_fderivWithin _
          (f := iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s) (hs x hx)]
        ```
        -/
        set_option maxSynthPendingDepth 2 in
          simp only [LinearIsometryEquiv.comp_fderivWithin _ (hs x hx)]
        rfl
      _ = (fderivWithin ğ•œ (iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) s x :
              E â†’ E[Ã—n]â†’L[ğ•œ] E â†’L[ğ•œ] F) (m 0) (init (tail m)) ((tail m) (last n)) := rfl
      _ = iteratedFDerivWithin ğ•œ (Nat.succ n) (fun y => fderivWithin ğ•œ f s y) s x (init m)
            (m (last (n + 1))) := by
        rw [iteratedFDerivWithin_succ_apply_left, tail_init_eq_init_tail]
        rfl
#align iterated_fderiv_within_succ_apply_right iteratedFDerivWithin_succ_apply_right

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
theorem iteratedFDerivWithin_succ_eq_comp_right {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ (n + 1) f s x =
      (continuousMultilinearCurryRightEquiv' ğ•œ n E F âˆ˜
          iteratedFDerivWithin ğ•œ n (fun y => fderivWithin ğ•œ f s y) s)
        x := by
  ext m; rw [iteratedFDerivWithin_succ_apply_right hs hx]; rfl
#align iterated_fderiv_within_succ_eq_comp_right iteratedFDerivWithin_succ_eq_comp_right

theorem norm_iteratedFDerivWithin_fderivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    â€–iteratedFDerivWithin ğ•œ n (fderivWithin ğ•œ f s) s xâ€– =
      â€–iteratedFDerivWithin ğ•œ (n + 1) f s xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDerivWithin_succ_eq_comp_right hs hx, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_iterated_fderiv_within_fderiv_within norm_iteratedFDerivWithin_fderivWithin

@[simp]
theorem iteratedFDerivWithin_one_apply (h : UniqueDiffWithinAt ğ•œ s x) (m : Fin 1 â†’ E) :
    iteratedFDerivWithin ğ•œ 1 f s x m = fderivWithin ğ•œ f s x (m 0) := by
  simp only [iteratedFDerivWithin_succ_apply_left, iteratedFDerivWithin_zero_eq_comp,
    (continuousMultilinearCurryFin0 ğ•œ E F).symm.comp_fderivWithin h]
  rfl
#align iterated_fderiv_within_one_apply iteratedFDerivWithin_one_apply

/-- On a set of unique differentiability, the second derivative is obtained by taking the
derivative of the derivative. -/
lemma iteratedFDerivWithin_two_apply (f : E â†’ F) {z : E} (hs : UniqueDiffOn ğ•œ s) (hz : z âˆˆ s)
    (m : Fin 2 â†’ E) :
    iteratedFDerivWithin ğ•œ 2 f s z m = fderivWithin ğ•œ (fderivWithin ğ•œ f s) s z (m 0) (m 1) := by
  simp only [iteratedFDerivWithin_succ_apply_right hs hz]
  rfl

theorem Filter.EventuallyEq.iteratedFDerivWithin' (h : fâ‚ =á¶ [ğ“[s] x] f) (ht : t âŠ† s) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ t =á¶ [ğ“[s] x] iteratedFDerivWithin ğ•œ n f t := by
  induction' n with n ihn
  Â· exact h.mono fun y hy => DFunLike.ext _ _ fun _ => hy
  Â· have : fderivWithin ğ•œ _ t =á¶ [ğ“[s] x] fderivWithin ğ•œ _ t := ihn.fderivWithin' ht
    apply this.mono
    intro y hy
    simp only [iteratedFDerivWithin_succ_eq_comp_left, hy, (Â· âˆ˜ Â·)]
#align filter.eventually_eq.iterated_fderiv_within' Filter.EventuallyEq.iteratedFDerivWithin'

protected theorem Filter.EventuallyEq.iteratedFDerivWithin (h : fâ‚ =á¶ [ğ“[s] x] f) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ s =á¶ [ğ“[s] x] iteratedFDerivWithin ğ•œ n f s :=
  h.iteratedFDerivWithin' Subset.rfl n
#align filter.eventually_eq.iterated_fderiv_within Filter.EventuallyEq.iteratedFDerivWithin

/-- If two functions coincide in a neighborhood of `x` within a set `s` and at `x`, then their
iterated differentials within this set at `x` coincide. -/
theorem Filter.EventuallyEq.iteratedFDerivWithin_eq (h : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x)
    (n : â„•) : iteratedFDerivWithin ğ•œ n fâ‚ s x = iteratedFDerivWithin ğ•œ n f s x :=
  have : fâ‚ =á¶ [ğ“[insert x s] x] f := by simpa [EventuallyEq, hx]
  (this.iteratedFDerivWithin' (subset_insert _ _) n).self_of_nhdsWithin (mem_insert _ _)
#align filter.eventually_eq.iterated_fderiv_within_eq Filter.EventuallyEq.iteratedFDerivWithin_eq

/-- If two functions coincide on a set `s`, then their iterated differentials within this set
coincide. See also `Filter.EventuallyEq.iteratedFDerivWithin_eq` and
`Filter.EventuallyEq.iteratedFDerivWithin`. -/
theorem iteratedFDerivWithin_congr (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) (n : â„•) :
    iteratedFDerivWithin ğ•œ n fâ‚ s x = iteratedFDerivWithin ğ•œ n f s x :=
  (hs.eventuallyEq.filter_mono inf_le_right).iteratedFDerivWithin_eq (hs hx) _
#align iterated_fderiv_within_congr iteratedFDerivWithin_congr

/-- If two functions coincide on a set `s`, then their iterated differentials within this set
coincide. See also `Filter.EventuallyEq.iteratedFDerivWithin_eq` and
`Filter.EventuallyEq.iteratedFDerivWithin`. -/
protected theorem Set.EqOn.iteratedFDerivWithin (hs : EqOn fâ‚ f s) (n : â„•) :
    EqOn (iteratedFDerivWithin ğ•œ n fâ‚ s) (iteratedFDerivWithin ğ•œ n f s) s := fun _x hx =>
  iteratedFDerivWithin_congr hs hx n
#align set.eq_on.iterated_fderiv_within Set.EqOn.iteratedFDerivWithin

theorem iteratedFDerivWithin_eventually_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ n f t := by
  induction' n with n ihn generalizing x
  Â· rfl
  Â· refine (eventually_nhds_nhdsWithin.2 h).mono fun y hy => ?_
    simp only [iteratedFDerivWithin_succ_eq_comp_left, (Â· âˆ˜ Â·)]
    rw [(ihn hy).fderivWithin_eq_nhds, fderivWithin_congr_set' _ hy]
#align iterated_fderiv_within_eventually_congr_set' iteratedFDerivWithin_eventually_congr_set'

theorem iteratedFDerivWithin_eventually_congr_set (h : s =á¶ [ğ“ x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ n f t :=
  iteratedFDerivWithin_eventually_congr_set' x (h.filter_mono inf_le_left) n
#align iterated_fderiv_within_eventually_congr_set iteratedFDerivWithin_eventually_congr_set

theorem iteratedFDerivWithin_congr_set (h : s =á¶ [ğ“ x] t) (n : â„•) :
    iteratedFDerivWithin ğ•œ n f s x = iteratedFDerivWithin ğ•œ n f t x :=
  (iteratedFDerivWithin_eventually_congr_set h n).self_of_nhds
#align iterated_fderiv_within_congr_set iteratedFDerivWithin_congr_set

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x` within `s`. -/
theorem iteratedFDerivWithin_inter' {n : â„•} (hu : u âˆˆ ğ“[s] x) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_congr_set (nhdsWithin_eq_iff_eventuallyEq.1 <| nhdsWithin_inter_of_mem' hu) _
#align iterated_fderiv_within_inter' iteratedFDerivWithin_inter'

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with a neighborhood of `x`. -/
theorem iteratedFDerivWithin_inter {n : â„•} (hu : u âˆˆ ğ“ x) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_inter' (mem_nhdsWithin_of_mem_nhds hu)
#align iterated_fderiv_within_inter iteratedFDerivWithin_inter

/-- The iterated differential within a set `s` at a point `x` is not modified if one intersects
`s` with an open set containing `x`. -/
theorem iteratedFDerivWithin_inter_open {n : â„•} (hu : IsOpen u) (hx : x âˆˆ u) :
    iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
  iteratedFDerivWithin_inter (hu.mem_nhds hx)
#align iterated_fderiv_within_inter_open iteratedFDerivWithin_inter_open

@[simp]
theorem contDiffOn_zero : ContDiffOn ğ•œ 0 f s â†” ContinuousOn f s := by
  refine âŸ¨fun H => H.continuousOn, fun H => ?_âŸ©
  intro x hx m hm
  have : (m : â„•âˆ) = 0 := le_antisymm hm bot_le
  rw [this]
  refine âŸ¨insert x s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  rw [hasFTaylorSeriesUpToOn_zero_iff]
  exact âŸ¨by rwa [insert_eq_of_mem hx], fun x _ => by simp [ftaylorSeriesWithin]âŸ©
#align cont_diff_on_zero contDiffOn_zero

theorem contDiffWithinAt_zero (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ 0 f s x â†” âˆƒ u âˆˆ ğ“[s] x, ContinuousOn f (s âˆ© u) := by
  constructor
  Â· intro h
    obtain âŸ¨u, H, p, hpâŸ© := h 0 le_rfl
    refine âŸ¨u, ?_, ?_âŸ©
    Â· simpa [hx] using H
    Â· simp only [Nat.cast_zero, hasFTaylorSeriesUpToOn_zero_iff] at hp
      exact hp.1.mono inter_subset_right
  Â· rintro âŸ¨u, H, huâŸ©
    rw [â† contDiffWithinAt_inter' H]
    have h' : x âˆˆ s âˆ© u := âŸ¨hx, mem_of_mem_nhdsWithin hx HâŸ©
    exact (contDiffOn_zero.mpr hu).contDiffWithinAt h'
#align cont_diff_within_at_zero contDiffWithinAt_zero

/-- On a set with unique differentiability, any choice of iterated differential has to coincide
with the one we have chosen in `iteratedFDerivWithin ğ•œ m f s`. -/
theorem HasFTaylorSeriesUpToOn.eq_iteratedFDerivWithin_of_uniqueDiffOn
    (h : HasFTaylorSeriesUpToOn n f p s) {m : â„•} (hmn : (m : â„•âˆ) â‰¤ n) (hs : UniqueDiffOn ğ•œ s)
    (hx : x âˆˆ s) : p x m = iteratedFDerivWithin ğ•œ m f s x := by
  induction' m with m IH generalizing x
  Â· rw [h.zero_eq' hx, iteratedFDerivWithin_zero_eq_comp]; rfl
  Â· have A : (m : â„•âˆ) < n := lt_of_lt_of_le (WithTop.coe_lt_coe.2 (lt_add_one m)) hmn
    have :
      HasFDerivWithinAt (fun y : E => iteratedFDerivWithin ğ•œ m f s y)
        (ContinuousMultilinearMap.curryLeft (p x (Nat.succ m))) s x :=
      (h.fderivWithin m A x hx).congr (fun y hy => (IH (le_of_lt A) hy).symm)
        (IH (le_of_lt A) hx).symm
    rw [iteratedFDerivWithin_succ_eq_comp_left, Function.comp_apply, this.fderivWithin (hs x hx)]
    exact (ContinuousMultilinearMap.uncurry_curryLeft _).symm
#align has_ftaylor_series_up_to_on.eq_ftaylor_series_of_unique_diff_on HasFTaylorSeriesUpToOn.eq_iteratedFDerivWithin_of_uniqueDiffOn

@[deprecated (since := "2024-03-28")]
alias HasFTaylorSeriesUpToOn.eq_ftaylor_series_of_uniqueDiffOn :=
  HasFTaylorSeriesUpToOn.eq_iteratedFDerivWithin_of_uniqueDiffOn

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylorSeriesWithin ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
protected theorem ContDiffOn.ftaylorSeriesWithin (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) :
    HasFTaylorSeriesUpToOn n f (ftaylorSeriesWithin ğ•œ f s) s := by
  constructor
  Â· intro x _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.uncurry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro m hm x hx
    rcases (h x hx) m.succ (ENat.add_one_le_of_lt hm) with âŸ¨u, hu, p, HpâŸ©
    rw [insert_eq_of_mem hx] at hu
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [inter_comm] at ho
    have : p x m.succ = ftaylorSeriesWithin ğ•œ f s x m.succ := by
      change p x m.succ = iteratedFDerivWithin ğ•œ m.succ f s x
      rw [â† iteratedFDerivWithin_inter_open o_open xo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hx, xoâŸ©
    rw [â† this, â† hasFDerivWithinAt_inter (IsOpen.mem_nhds o_open xo)]
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact
        (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn (WithTop.coe_le_coe.2 (Nat.le_succ m))
          (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact
      ((Hp.mono ho).fderivWithin m (WithTop.coe_lt_coe.2 (lt_add_one m)) x âŸ¨hx, xoâŸ©).congr
        (fun y hy => (A y hy).symm) (A x âŸ¨hx, xoâŸ©).symm
  Â· intro m hm
    apply continuousOn_of_locally_continuousOn
    intro x hx
    rcases h x hx m hm with âŸ¨u, hu, p, HpâŸ©
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [insert_eq_of_mem hx] at ho
    rw [inter_comm] at ho
    refine âŸ¨o, o_open, xo, ?_âŸ©
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact ((Hp.mono ho).cont m le_rfl).congr fun y hy => (A y hy).symm
#align cont_diff_on.ftaylor_series_within ContDiffOn.ftaylorSeriesWithin

theorem contDiffOn_of_continuousOn_differentiableOn
    (Hcont : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s)
    (Hdiff : âˆ€ m : â„•, (m : â„•âˆ) < n â†’
      DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s) :
    ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  rw [insert_eq_of_mem hx]
  refine âŸ¨s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  constructor
  Â· intro y _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.uncurry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro k hk y hy
    convert (Hdiff k (lt_of_lt_of_le hk hm) y hy).hasFDerivWithinAt
  Â· intro k hk
    exact Hcont k (le_trans hk hm)
#align cont_diff_on_of_continuous_on_differentiable_on contDiffOn_of_continuousOn_differentiableOn

theorem contDiffOn_of_differentiableOn
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s) :
    ContDiffOn ğ•œ n f s :=
  contDiffOn_of_continuousOn_differentiableOn (fun m hm => (h m hm).continuousOn) fun m hm =>
    h m (le_of_lt hm)
#align cont_diff_on_of_differentiable_on contDiffOn_of_differentiableOn

theorem ContDiffOn.continuousOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : (m : â„•âˆ) â‰¤ n) (hs : UniqueDiffOn ğ•œ s) : ContinuousOn (iteratedFDerivWithin ğ•œ m f s) s :=
  (h.ftaylorSeriesWithin hs).cont m hmn
#align cont_diff_on.continuous_on_iterated_fderiv_within ContDiffOn.continuousOn_iteratedFDerivWithin

theorem ContDiffOn.differentiableOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : (m : â„•âˆ) < n) (hs : UniqueDiffOn ğ•œ s) :
    DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s := fun x hx =>
  ((h.ftaylorSeriesWithin hs).fderivWithin m hmn x hx).differentiableWithinAt
#align cont_diff_on.differentiable_on_iterated_fderiv_within ContDiffOn.differentiableOn_iteratedFDerivWithin

theorem ContDiffWithinAt.differentiableWithinAt_iteratedFDerivWithin {m : â„•}
    (h : ContDiffWithinAt ğ•œ n f s x) (hmn : (m : â„•âˆ) < n) (hs : UniqueDiffOn ğ•œ (insert x s)) :
    DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f s) s x := by
  rcases h.contDiffOn' (ENat.add_one_le_of_lt hmn) with âŸ¨u, uo, xu, huâŸ©
  set t := insert x s âˆ© u
  have A : t =á¶ [ğ“[â‰ ] x] s := by
    simp only [set_eventuallyEq_iff_inf_principal, â† nhdsWithin_inter']
    rw [â† inter_assoc, nhdsWithin_inter_of_mem', â† diff_eq_compl_inter, insert_diff_of_mem,
      diff_eq_compl_inter]
    exacts [rfl, mem_nhdsWithin_of_mem_nhds (uo.mem_nhds xu)]
  have B : iteratedFDerivWithin ğ•œ m f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ m f t :=
    iteratedFDerivWithin_eventually_congr_set' _ A.symm _
  have C : DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f t) t x :=
    hu.differentiableOn_iteratedFDerivWithin (Nat.cast_lt.2 m.lt_succ_self) (hs.inter uo) x
      âŸ¨mem_insert _ _, xuâŸ©
  rw [differentiableWithinAt_congr_set' _ A] at C
  exact C.congr_of_eventuallyEq (B.filter_mono inf_le_left) B.self_of_nhds
#align cont_diff_within_at.differentiable_within_at_iterated_fderiv_within ContDiffWithinAt.differentiableWithinAt_iteratedFDerivWithin

theorem contDiffOn_iff_continuousOn_differentiableOn (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†”
      (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s) âˆ§
        âˆ€ m : â„•, (m : â„•âˆ) < n â†’ DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s :=
  âŸ¨fun h => âŸ¨fun _m hm => h.continuousOn_iteratedFDerivWithin hm hs, fun _m hm =>
      h.differentiableOn_iteratedFDerivWithin hm hsâŸ©,
    fun h => contDiffOn_of_continuousOn_differentiableOn h.1 h.2âŸ©
#align cont_diff_on_iff_continuous_on_differentiable_on contDiffOn_iff_continuousOn_differentiableOn

theorem contDiffOn_succ_of_fderivWithin {n : â„•} (hf : DifferentiableOn ğ•œ f s)
    (h : ContDiffOn ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) : ContDiffOn ğ•œ (n + 1 : â„•) f s := by
  intro x hx
  rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt, insert_eq_of_mem hx]
  exact
    âŸ¨s, self_mem_nhdsWithin, fderivWithin ğ•œ f s, fun y hy => (hf y hy).hasFDerivWithinAt, h x hxâŸ©
#align cont_diff_on_succ_of_fderiv_within contDiffOn_succ_of_fderivWithin

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (expressed with `fderivWithin`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ n (fun y => fderivWithin ğ•œ f s y) s := by
  refine âŸ¨fun H => ?_, fun h => contDiffOn_succ_of_fderivWithin h.1 h.2âŸ©
  refine âŸ¨H.differentiableOn (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)), fun x hx => ?_âŸ©
  rcases contDiffWithinAt_succ_iff_hasFDerivWithinAt.1 (H x hx) with âŸ¨u, hu, f', hff', hf'âŸ©
  rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
  rw [inter_comm, insert_eq_of_mem hx] at ho
  have := hf'.mono ho
  rw [contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds (IsOpen.mem_nhds o_open xo))] at this
  apply this.congr_of_eventually_eq' _ hx
  have : o âˆ© s âˆˆ ğ“[s] x := mem_nhdsWithin.2 âŸ¨o, o_open, xo, Subset.refl _âŸ©
  rw [inter_comm] at this
  refine Filter.eventuallyEq_of_mem this fun y hy => ?_
  have A : fderivWithin ğ•œ f (s âˆ© o) y = f' y :=
    ((hff' y (ho hy)).mono ho).fderivWithin (hs.inter o_open y hy)
  rwa [fderivWithin_inter (o_open.mem_nhds hy.2)] at A
#align cont_diff_on_succ_iff_fderiv_within contDiffOn_succ_iff_fderivWithin

theorem contDiffOn_succ_iff_hasFDerivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiffOn ğ•œ n f' s âˆ§ âˆ€ x, x âˆˆ s â†’ HasFDerivWithinAt f (f' x) s x := by
  rw [contDiffOn_succ_iff_fderivWithin hs]
  refine âŸ¨fun h => âŸ¨fderivWithin ğ•œ f s, h.2, fun x hx => (h.1 x hx).hasFDerivWithinAtâŸ©, fun h => ?_âŸ©
  rcases h with âŸ¨f', h1, h2âŸ©
  refine âŸ¨fun x hx => (h2 x hx).differentiableWithinAt, fun x hx => ?_âŸ©
  exact (h1 x hx).congr' (fun y hy => (h2 y hy).fderivWithin (hs y hy)) hx
#align cont_diff_on_succ_iff_has_fderiv_within contDiffOn_succ_iff_hasFDerivWithin

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (expressed with `fderiv`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderiv_of_isOpen {n : â„•} (hs : IsOpen s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ n (fun y => fderiv ğ•œ f y) s := by
  rw [contDiffOn_succ_iff_fderivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and (contDiffOn_congr fun x hx â†¦ fderivWithin_of_isOpen hs hx)
#align cont_diff_on_succ_iff_fderiv_of_open contDiffOn_succ_iff_fderiv_of_isOpen

/-- A function is `C^âˆ` on a domain with unique derivatives if and only if it is differentiable
there, and its derivative (expressed with `fderivWithin`) is `C^âˆ`. -/
theorem contDiffOn_top_iff_fderivWithin (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ âˆ f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fun y => fderivWithin ğ•œ f s y) s := by
  constructor
  Â· intro h
    refine âŸ¨h.differentiableOn le_top, ?_âŸ©
    refine contDiffOn_top.2 fun n => ((contDiffOn_succ_iff_fderivWithin hs).1 ?_).2
    exact h.of_le le_top
  Â· intro h
    refine contDiffOn_top.2 fun n => ?_
    have A : (n : â„•âˆ) â‰¤ âˆ := le_top
    apply ((contDiffOn_succ_iff_fderivWithin hs).2 âŸ¨h.1, h.2.of_le AâŸ©).of_le
    exact WithTop.coe_le_coe.2 (Nat.le_succ n)
#align cont_diff_on_top_iff_fderiv_within contDiffOn_top_iff_fderivWithin

/-- A function is `C^âˆ` on an open domain if and only if it is differentiable there, and its
derivative (expressed with `fderiv`) is `C^âˆ`. -/
theorem contDiffOn_top_iff_fderiv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ğ•œ âˆ f s â†” DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fun y => fderiv ğ•œ f y) s := by
  rw [contDiffOn_top_iff_fderivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and <| contDiffOn_congr fun x hx â†¦ fderivWithin_of_isOpen hs hx
#align cont_diff_on_top_iff_fderiv_of_open contDiffOn_top_iff_fderiv_of_isOpen

protected theorem ContDiffOn.fderivWithin (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hmn : m + 1 â‰¤ n) : ContDiffOn ğ•œ m (fun y => fderivWithin ğ•œ f s y) s := by
  cases' m with m
  Â· change âˆ + 1 â‰¤ n at hmn
    have : n = âˆ := by simpa using hmn
    rw [this] at hf
    exact ((contDiffOn_top_iff_fderivWithin hs).1 hf).2
  Â· change (m.succ : â„•âˆ) â‰¤ n at hmn
    exact ((contDiffOn_succ_iff_fderivWithin hs).1 (hf.of_le hmn)).2
#align cont_diff_on.fderiv_within ContDiffOn.fderivWithin

theorem ContDiffOn.fderiv_of_isOpen (hf : ContDiffOn ğ•œ n f s) (hs : IsOpen s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (fun y => fderiv ğ•œ f y) s :=
  (hf.fderivWithin hs.uniqueDiffOn hmn).congr fun _ hx => (fderivWithin_of_isOpen hs hx).symm
#align cont_diff_on.fderiv_of_open ContDiffOn.fderiv_of_isOpen

theorem ContDiffOn.continuousOn_fderivWithin (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hn : 1 â‰¤ n) : ContinuousOn (fun x => fderivWithin ğ•œ f s x) s :=
  ((contDiffOn_succ_iff_fderivWithin hs).1 (h.of_le hn)).2.continuousOn
#align cont_diff_on.continuous_on_fderiv_within ContDiffOn.continuousOn_fderivWithin

theorem ContDiffOn.continuousOn_fderiv_of_isOpen (h : ContDiffOn ğ•œ n f s) (hs : IsOpen s)
    (hn : 1 â‰¤ n) : ContinuousOn (fun x => fderiv ğ•œ f x) s :=
  ((contDiffOn_succ_iff_fderiv_of_isOpen hs).1 (h.of_le hn)).2.continuousOn
#align cont_diff_on.continuous_on_fderiv_of_open ContDiffOn.continuousOn_fderiv_of_isOpen

/-! ### Functions with a Taylor series on the whole space -/

/-- `HasFTaylorSeriesUpTo n f p` registers the fact that `p 0 = f` and `p (m+1)` is a
derivative of `p m` for `m < n`, and is continuous for `m â‰¤ n`. This is a predicate analogous to
`HasFDerivAt` but for higher order derivatives.

Notice that `p` does not sum up to `f` on the diagonal (`FormalMultilinearSeries.sum`), even if
`f` is analytic and `n = âˆ`: an addition `1/m!` factor on the `m`th term is necessary for that. -/
structure HasFTaylorSeriesUpTo (n : â„•âˆ) (f : E â†’ F) (p : E â†’ FormalMultilinearSeries ğ•œ E F) :
  Prop where
  zero_eq : âˆ€ x, (p x 0).uncurry0 = f x
  fderiv : âˆ€ m : â„•, (m : â„•âˆ) < n â†’ âˆ€ x, HasFDerivAt (fun y => p y m) (p x m.succ).curryLeft x
  cont : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Continuous fun x => p x m
#align has_ftaylor_series_up_to HasFTaylorSeriesUpTo

theorem HasFTaylorSeriesUpTo.zero_eq' (h : HasFTaylorSeriesUpTo n f p) (x : E) :
    p x 0 = (continuousMultilinearCurryFin0 ğ•œ E F).symm (f x) := by
  rw [â† h.zero_eq x]
  exact (p x 0).uncurry0_curry0.symm
#align has_ftaylor_series_up_to.zero_eq' HasFTaylorSeriesUpTo.zero_eq'

theorem hasFTaylorSeriesUpToOn_univ_iff :
    HasFTaylorSeriesUpToOn n f p univ â†” HasFTaylorSeriesUpTo n f p := by
  constructor
  Â· intro H
    constructor
    Â· exact fun x => H.zero_eq x (mem_univ x)
    Â· intro m hm x
      rw [â† hasFDerivWithinAt_univ]
      exact H.fderivWithin m hm x (mem_univ x)
    Â· intro m hm
      rw [continuous_iff_continuousOn_univ]
      exact H.cont m hm
  Â· intro H
    constructor
    Â· exact fun x _ => H.zero_eq x
    Â· intro m hm x _
      rw [hasFDerivWithinAt_univ]
      exact H.fderiv m hm x
    Â· intro m hm
      rw [â† continuous_iff_continuousOn_univ]
      exact H.cont m hm
#align has_ftaylor_series_up_to_on_univ_iff hasFTaylorSeriesUpToOn_univ_iff

theorem HasFTaylorSeriesUpTo.hasFTaylorSeriesUpToOn (h : HasFTaylorSeriesUpTo n f p) (s : Set E) :
    HasFTaylorSeriesUpToOn n f p s :=
  (hasFTaylorSeriesUpToOn_univ_iff.2 h).mono (subset_univ _)
#align has_ftaylor_series_up_to.has_ftaylor_series_up_to_on HasFTaylorSeriesUpTo.hasFTaylorSeriesUpToOn

theorem HasFTaylorSeriesUpTo.ofLe (h : HasFTaylorSeriesUpTo n f p) (hmn : m â‰¤ n) :
    HasFTaylorSeriesUpTo m f p := by
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h âŠ¢; exact h.of_le hmn
#align has_ftaylor_series_up_to.of_le HasFTaylorSeriesUpTo.ofLe

theorem HasFTaylorSeriesUpTo.continuous (h : HasFTaylorSeriesUpTo n f p) : Continuous f := by
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h
  rw [continuous_iff_continuousOn_univ]
  exact h.continuousOn
#align has_ftaylor_series_up_to.continuous HasFTaylorSeriesUpTo.continuous

theorem hasFTaylorSeriesUpTo_zero_iff :
    HasFTaylorSeriesUpTo 0 f p â†” Continuous f âˆ§ âˆ€ x, (p x 0).uncurry0 = f x := by
  simp [hasFTaylorSeriesUpToOn_univ_iff.symm, continuous_iff_continuousOn_univ,
    hasFTaylorSeriesUpToOn_zero_iff]
#align has_ftaylor_series_up_to_zero_iff hasFTaylorSeriesUpTo_zero_iff

theorem hasFTaylorSeriesUpTo_top_iff :
    HasFTaylorSeriesUpTo âˆ f p â†” âˆ€ n : â„•, HasFTaylorSeriesUpTo n f p := by
  simp only [â† hasFTaylorSeriesUpToOn_univ_iff, hasFTaylorSeriesUpToOn_top_iff]
#align has_ftaylor_series_up_to_top_iff hasFTaylorSeriesUpTo_top_iff

/-- In the case that `n = âˆ` we don't need the continuity assumption in
`HasFTaylorSeriesUpTo`. -/
theorem hasFTaylorSeriesUpTo_top_iff' :
    HasFTaylorSeriesUpTo âˆ f p â†”
      (âˆ€ x, (p x 0).uncurry0 = f x) âˆ§
        âˆ€ (m : â„•) (x), HasFDerivAt (fun y => p y m) (p x m.succ).curryLeft x := by
  simp only [â† hasFTaylorSeriesUpToOn_univ_iff, hasFTaylorSeriesUpToOn_top_iff', mem_univ,
    forall_true_left, hasFDerivWithinAt_univ]
#align has_ftaylor_series_up_to_top_iff' hasFTaylorSeriesUpTo_top_iff'

/-- If a function has a Taylor series at order at least `1`, then the term of order `1` of this
series is a derivative of `f`. -/
theorem HasFTaylorSeriesUpTo.hasFDerivAt (h : HasFTaylorSeriesUpTo n f p) (hn : 1 â‰¤ n) (x : E) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) x := by
  rw [â† hasFDerivWithinAt_univ]
  exact (hasFTaylorSeriesUpToOn_univ_iff.2 h).hasFDerivWithinAt hn (mem_univ _)
#align has_ftaylor_series_up_to.has_fderiv_at HasFTaylorSeriesUpTo.hasFDerivAt

theorem HasFTaylorSeriesUpTo.differentiable (h : HasFTaylorSeriesUpTo n f p) (hn : 1 â‰¤ n) :
    Differentiable ğ•œ f := fun x => (h.hasFDerivAt hn x).differentiableAt
#align has_ftaylor_series_up_to.differentiable HasFTaylorSeriesUpTo.differentiable

/-- `p` is a Taylor series of `f` up to `n+1` if and only if `p.shift` is a Taylor series up to `n`
for `p 1`, which is a derivative of `f`. -/
theorem hasFTaylorSeriesUpTo_succ_iff_right {n : â„•} :
    HasFTaylorSeriesUpTo (n + 1 : â„•) f p â†”
      (âˆ€ x, (p x 0).uncurry0 = f x) âˆ§
        (âˆ€ x, HasFDerivAt (fun y => p y 0) (p x 1).curryLeft x) âˆ§
          HasFTaylorSeriesUpTo n (fun x => continuousMultilinearCurryFin1 ğ•œ E F (p x 1)) fun x =>
            (p x).shift := by
  simp only [hasFTaylorSeriesUpToOn_succ_iff_right, â† hasFTaylorSeriesUpToOn_univ_iff, mem_univ,
    forall_true_left, hasFDerivWithinAt_univ]
#align has_ftaylor_series_up_to_succ_iff_right hasFTaylorSeriesUpTo_succ_iff_right

/-! ### Smooth functions at a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` at a point `x` if, for any integer `k â‰¤ n`,
there is a neighborhood of `x` where `f` admits derivatives up to order `n`, which are continuous.
-/
def ContDiffAt (n : â„•âˆ) (f : E â†’ F) (x : E) : Prop :=
  ContDiffWithinAt ğ•œ n f univ x
#align cont_diff_at ContDiffAt

variable {ğ•œ}

theorem contDiffWithinAt_univ : ContDiffWithinAt ğ•œ n f univ x â†” ContDiffAt ğ•œ n f x :=
  Iff.rfl
#align cont_diff_within_at_univ contDiffWithinAt_univ

theorem contDiffAt_top : ContDiffAt ğ•œ âˆ f x â†” âˆ€ n : â„•, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffWithinAt_univ, contDiffWithinAt_top]
#align cont_diff_at_top contDiffAt_top

theorem ContDiffAt.contDiffWithinAt (h : ContDiffAt ğ•œ n f x) : ContDiffWithinAt ğ•œ n f s x :=
  h.mono (subset_univ _)
#align cont_diff_at.cont_diff_within_at ContDiffAt.contDiffWithinAt

theorem ContDiffWithinAt.contDiffAt (h : ContDiffWithinAt ğ•œ n f s x) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x := by rwa [ContDiffAt, â† contDiffWithinAt_inter hx, univ_inter]
#align cont_diff_within_at.cont_diff_at ContDiffWithinAt.contDiffAt

theorem ContDiffOn.contDiffAt (h : ContDiffOn ğ•œ n f s) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x :=
  (h _ (mem_of_mem_nhds hx)).contDiffAt hx

theorem ContDiffAt.congr_of_eventuallyEq (h : ContDiffAt ğ•œ n f x) (hg : fâ‚ =á¶ [ğ“ x] f) :
    ContDiffAt ğ•œ n fâ‚ x :=
  h.congr_of_eventually_eq' (by rwa [nhdsWithin_univ]) (mem_univ x)
#align cont_diff_at.congr_of_eventually_eq ContDiffAt.congr_of_eventuallyEq

theorem ContDiffAt.of_le (h : ContDiffAt ğ•œ n f x) (hmn : m â‰¤ n) : ContDiffAt ğ•œ m f x :=
  ContDiffWithinAt.of_le h hmn
#align cont_diff_at.of_le ContDiffAt.of_le

theorem ContDiffAt.continuousAt (h : ContDiffAt ğ•œ n f x) : ContinuousAt f x := by
  simpa [continuousWithinAt_univ] using h.continuousWithinAt
#align cont_diff_at.continuous_at ContDiffAt.continuousAt

/-- If a function is `C^n` with `n â‰¥ 1` at a point, then it is differentiable there. -/
theorem ContDiffAt.differentiableAt (h : ContDiffAt ğ•œ n f x) (hn : 1 â‰¤ n) :
    DifferentiableAt ğ•œ f x := by
  simpa [hn, differentiableWithinAt_univ] using h.differentiableWithinAt
#align cont_diff_at.differentiable_at ContDiffAt.differentiableAt

nonrec lemma ContDiffAt.contDiffOn {m : â„•} (h : ContDiffAt ğ•œ n f x) (hm : m â‰¤ n) :
    âˆƒ u âˆˆ ğ“ x, ContDiffOn ğ•œ m f u := by
  simpa [nhdsWithin_univ] using h.contDiffOn hm

/-- A function is `C^(n + 1)` at a point iff locally, it has a derivative which is `C^n`. -/
theorem contDiffAt_succ_iff_hasFDerivAt {n : â„•} :
    ContDiffAt ğ•œ (n + 1 : â„•) f x â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, (âˆƒ u âˆˆ ğ“ x, âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x) âˆ§ ContDiffAt ğ•œ n f' x := by
  rw [â† contDiffWithinAt_univ, contDiffWithinAt_succ_iff_hasFDerivWithinAt]
  simp only [nhdsWithin_univ, exists_prop, mem_univ, insert_eq_of_mem]
  constructor
  Â· rintro âŸ¨u, H, f', h_fderiv, h_cont_diffâŸ©
    rcases mem_nhds_iff.mp H with âŸ¨t, htu, ht, hxtâŸ©
    refine âŸ¨f', âŸ¨t, ?_âŸ©, h_cont_diff.contDiffAt HâŸ©
    refine âŸ¨mem_nhds_iff.mpr âŸ¨t, Subset.rfl, ht, hxtâŸ©, ?_âŸ©
    intro y hyt
    refine (h_fderiv y (htu hyt)).hasFDerivAt ?_
    exact mem_nhds_iff.mpr âŸ¨t, htu, ht, hytâŸ©
  Â· rintro âŸ¨f', âŸ¨u, H, h_fderivâŸ©, h_cont_diffâŸ©
    refine âŸ¨u, H, f', ?_, h_cont_diff.contDiffWithinAtâŸ©
    intro x hxu
    exact (h_fderiv x hxu).hasFDerivWithinAt
#align cont_diff_at_succ_iff_has_fderiv_at contDiffAt_succ_iff_hasFDerivAt

protected theorem ContDiffAt.eventually {n : â„•} (h : ContDiffAt ğ•œ n f x) :
    âˆ€á¶  y in ğ“ x, ContDiffAt ğ•œ n f y := by
  simpa [nhdsWithin_univ] using ContDiffWithinAt.eventually h
#align cont_diff_at.eventually ContDiffAt.eventually

/-! ### Smooth functions -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` if it admits derivatives up to
order `n`, which are continuous. Contrary to the case of definitions in domains (where derivatives
might not be unique) we do not need to localize the definition in space or time.
-/
def ContDiff (n : â„•âˆ) (f : E â†’ F) : Prop :=
  âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpTo n f p
#align cont_diff ContDiff

variable {ğ•œ}

/-- If `f` has a Taylor series up to `n`, then it is `C^n`. -/
theorem HasFTaylorSeriesUpTo.contDiff {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpTo n f f') : ContDiff ğ•œ n f :=
  âŸ¨f', hfâŸ©
#align has_ftaylor_series_up_to.cont_diff HasFTaylorSeriesUpTo.contDiff

theorem contDiffOn_univ : ContDiffOn ğ•œ n f univ â†” ContDiff ğ•œ n f := by
  constructor
  Â· intro H
    use ftaylorSeriesWithin ğ•œ f univ
    rw [â† hasFTaylorSeriesUpToOn_univ_iff]
    exact H.ftaylorSeriesWithin uniqueDiffOn_univ
  Â· rintro âŸ¨p, hpâŸ© x _ m hm
    exact âŸ¨univ, Filter.univ_sets _, p, (hp.hasFTaylorSeriesUpToOn univ).of_le hmâŸ©
#align cont_diff_on_univ contDiffOn_univ

theorem contDiff_iff_contDiffAt : ContDiff ğ•œ n f â†” âˆ€ x, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffOn_univ, ContDiffOn, ContDiffAt]
#align cont_diff_iff_cont_diff_at contDiff_iff_contDiffAt

theorem ContDiff.contDiffAt (h : ContDiff ğ•œ n f) : ContDiffAt ğ•œ n f x :=
  contDiff_iff_contDiffAt.1 h x
#align cont_diff.cont_diff_at ContDiff.contDiffAt

theorem ContDiff.contDiffWithinAt (h : ContDiff ğ•œ n f) : ContDiffWithinAt ğ•œ n f s x :=
  h.contDiffAt.contDiffWithinAt
#align cont_diff.cont_diff_within_at ContDiff.contDiffWithinAt

theorem contDiff_top : ContDiff ğ•œ âˆ f â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp [contDiffOn_univ.symm, contDiffOn_top]
#align cont_diff_top contDiff_top

theorem contDiff_all_iff_nat : (âˆ€ n, ContDiff ğ•œ n f) â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp only [â† contDiffOn_univ, contDiffOn_all_iff_nat]
#align cont_diff_all_iff_nat contDiff_all_iff_nat

theorem ContDiff.contDiffOn (h : ContDiff ğ•œ n f) : ContDiffOn ğ•œ n f s :=
  (contDiffOn_univ.2 h).mono (subset_univ _)
#align cont_diff.cont_diff_on ContDiff.contDiffOn

@[simp]
theorem contDiff_zero : ContDiff ğ•œ 0 f â†” Continuous f := by
  rw [â† contDiffOn_univ, continuous_iff_continuousOn_univ]
  exact contDiffOn_zero
#align cont_diff_zero contDiff_zero

theorem contDiffAt_zero : ContDiffAt ğ•œ 0 f x â†” âˆƒ u âˆˆ ğ“ x, ContinuousOn f u := by
  rw [â† contDiffWithinAt_univ]; simp [contDiffWithinAt_zero, nhdsWithin_univ]
#align cont_diff_at_zero contDiffAt_zero

theorem contDiffAt_one_iff :
    ContDiffAt ğ•œ 1 f x â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, âˆƒ u âˆˆ ğ“ x, ContinuousOn f' u âˆ§ âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x := by
  simp_rw [show (1 : â„•âˆ) = (0 + 1 : â„•) from (zero_add 1).symm, contDiffAt_succ_iff_hasFDerivAt,
    show ((0 : â„•) : â„•âˆ) = 0 from rfl, contDiffAt_zero,
    exists_mem_and_iff antitone_bforall antitone_continuousOn, and_comm]
#align cont_diff_at_one_iff contDiffAt_one_iff

theorem ContDiff.of_le (h : ContDiff ğ•œ n f) (hmn : m â‰¤ n) : ContDiff ğ•œ m f :=
  contDiffOn_univ.1 <| (contDiffOn_univ.2 h).of_le hmn
#align cont_diff.of_le ContDiff.of_le

theorem ContDiff.of_succ {n : â„•} (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ n f :=
  h.of_le <| WithTop.coe_le_coe.mpr le_self_add
#align cont_diff.of_succ ContDiff.of_succ

theorem ContDiff.one_of_succ {n : â„•} (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ 1 f :=
  h.of_le <| WithTop.coe_le_coe.mpr le_add_self
#align cont_diff.one_of_succ ContDiff.one_of_succ

theorem ContDiff.continuous (h : ContDiff ğ•œ n f) : Continuous f :=
  contDiff_zero.1 (h.of_le bot_le)
#align cont_diff.continuous ContDiff.continuous

/-- If a function is `C^n` with `n â‰¥ 1`, then it is differentiable. -/
theorem ContDiff.differentiable (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) : Differentiable ğ•œ f :=
  differentiableOn_univ.1 <| (contDiffOn_univ.2 h).differentiableOn hn
#align cont_diff.differentiable ContDiff.differentiable

theorem contDiff_iff_forall_nat_le : ContDiff ğ•œ n f â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiff ğ•œ m f := by
  simp_rw [â† contDiffOn_univ]; exact contDiffOn_iff_forall_nat_le
#align cont_diff_iff_forall_nat_le contDiff_iff_forall_nat_le

/-- A function is `C^(n+1)` iff it has a `C^n` derivative. -/
theorem contDiff_succ_iff_hasFDerivAt {n : â„•} :
    ContDiff ğ•œ (n + 1 : â„•) f â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiff ğ•œ n f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  simp only [â† contDiffOn_univ, â† hasFDerivWithinAt_univ,
    contDiffOn_succ_iff_hasFDerivWithin uniqueDiffOn_univ, Set.mem_univ, forall_true_left]
#align cont_diff_succ_iff_has_fderiv contDiff_succ_iff_hasFDerivAt

/-! ### Iterated derivative -/


variable (ğ•œ)

/-- The `n`-th derivative of a function, as a multilinear map, defined inductively. -/
noncomputable def iteratedFDeriv (n : â„•) (f : E â†’ F) : E â†’ E[Ã—n]â†’L[ğ•œ] F :=
  Nat.recOn n (fun x => ContinuousMultilinearMap.curry0 ğ•œ E (f x)) fun _ rec x =>
    ContinuousLinearMap.uncurryLeft (fderiv ğ•œ rec x)
#align iterated_fderiv iteratedFDeriv

/-- Formal Taylor series associated to a function. -/
def ftaylorSeries (f : E â†’ F) (x : E) : FormalMultilinearSeries ğ•œ E F := fun n =>
  iteratedFDeriv ğ•œ n f x
#align ftaylor_series ftaylorSeries

variable {ğ•œ}

@[simp]
theorem iteratedFDeriv_zero_apply (m : Fin 0 â†’ E) :
    (iteratedFDeriv ğ•œ 0 f x : (Fin 0 â†’ E) â†’ F) m = f x :=
  rfl
#align iterated_fderiv_zero_apply iteratedFDeriv_zero_apply

theorem iteratedFDeriv_zero_eq_comp :
    iteratedFDeriv ğ•œ 0 f = (continuousMultilinearCurryFin0 ğ•œ E F).symm âˆ˜ f :=
  rfl
#align iterated_fderiv_zero_eq_comp iteratedFDeriv_zero_eq_comp

@[simp]
theorem norm_iteratedFDeriv_zero : â€–iteratedFDeriv ğ•œ 0 f xâ€– = â€–f xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDeriv_zero_eq_comp, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_iterated_fderiv_zero norm_iteratedFDeriv_zero

theorem iteratedFDerivWithin_zero_eq : iteratedFDerivWithin ğ•œ 0 f s = iteratedFDeriv ğ•œ 0 f := rfl
#align iterated_fderiv_with_zero_eq iteratedFDerivWithin_zero_eq

theorem iteratedFDeriv_succ_apply_left {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDeriv ğ•œ (n + 1) f x : (Fin (n + 1) â†’ E) â†’ F) m =
      (fderiv ğ•œ (iteratedFDeriv ğ•œ n f) x : E â†’ E[Ã—n]â†’L[ğ•œ] F) (m 0) (tail m) :=
  rfl
#align iterated_fderiv_succ_apply_left iteratedFDeriv_succ_apply_left

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the derivative of the `n`-th derivative. -/
theorem iteratedFDeriv_succ_eq_comp_left {n : â„•} :
    iteratedFDeriv ğ•œ (n + 1) f =
      continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F âˆ˜
        fderiv ğ•œ (iteratedFDeriv ğ•œ n f) :=
  rfl
#align iterated_fderiv_succ_eq_comp_left iteratedFDeriv_succ_eq_comp_left

/-- Writing explicitly the derivative of the `n`-th derivative as the composition of a currying
linear equiv, and the `n + 1`-th derivative. -/
theorem fderiv_iteratedFDeriv {n : â„•} :
    fderiv ğ•œ (iteratedFDeriv ğ•œ n f) =
      (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) => E) F).symm âˆ˜
        iteratedFDeriv ğ•œ (n + 1) f := by
  rw [iteratedFDeriv_succ_eq_comp_left]
  ext1 x
  simp only [Function.comp_apply, LinearIsometryEquiv.symm_apply_apply]
#align fderiv_iterated_fderiv fderiv_iteratedFDeriv

theorem tsupport_iteratedFDeriv_subset (n : â„•) : tsupport (iteratedFDeriv ğ•œ n f) âŠ† tsupport f := by
  induction' n with n IH
  Â· rw [iteratedFDeriv_zero_eq_comp]
    exact closure_minimal ((support_comp_subset (LinearIsometryEquiv.map_zero _) _).trans
      subset_closure) isClosed_closure
  Â· rw [iteratedFDeriv_succ_eq_comp_left]
    exact closure_minimal ((support_comp_subset (LinearIsometryEquiv.map_zero _) _).trans
      ((support_fderiv_subset ğ•œ).trans IH)) isClosed_closure

theorem support_iteratedFDeriv_subset (n : â„•) : support (iteratedFDeriv ğ•œ n f) âŠ† tsupport f :=
  subset_closure.trans (tsupport_iteratedFDeriv_subset n)

theorem HasCompactSupport.iteratedFDeriv (hf : HasCompactSupport f) (n : â„•) :
    HasCompactSupport (iteratedFDeriv ğ•œ n f) :=
  hf.of_isClosed_subset isClosed_closure (tsupport_iteratedFDeriv_subset n)
#align has_compact_support.iterated_fderiv HasCompactSupport.iteratedFDeriv

theorem norm_fderiv_iteratedFDeriv {n : â„•} :
    â€–fderiv ğ•œ (iteratedFDeriv ğ•œ n f) xâ€– = â€–iteratedFDeriv ğ•œ (n + 1) f xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDeriv_succ_eq_comp_left, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_fderiv_iterated_fderiv norm_fderiv_iteratedFDeriv

theorem iteratedFDerivWithin_univ {n : â„•} :
    iteratedFDerivWithin ğ•œ n f univ = iteratedFDeriv ğ•œ n f := by
  induction' n with n IH
  Â· ext x; simp
  Â· ext x m
    rw [iteratedFDeriv_succ_apply_left, iteratedFDerivWithin_succ_apply_left, IH, fderivWithin_univ]
#align iterated_fderiv_within_univ iteratedFDerivWithin_univ

theorem HasFTaylorSeriesUpTo.eq_iteratedFDeriv
    (h : HasFTaylorSeriesUpTo n f p) {m : â„•} (hmn : (m : â„•âˆ) â‰¤ n) (x : E) :
    p x m = iteratedFDeriv ğ•œ m f x := by
  rw [â† iteratedFDerivWithin_univ]
  rw [â† hasFTaylorSeriesUpToOn_univ_iff] at h
  exact h.eq_iteratedFDerivWithin_of_uniqueDiffOn hmn uniqueDiffOn_univ (mem_univ _)

/-- In an open set, the iterated derivative within this set coincides with the global iterated
derivative. -/
theorem iteratedFDerivWithin_of_isOpen (n : â„•) (hs : IsOpen s) :
    EqOn (iteratedFDerivWithin ğ•œ n f s) (iteratedFDeriv ğ•œ n f) s := by
  induction' n with n IH
  Â· intro x _
    ext1
    simp only [Nat.zero_eq, iteratedFDerivWithin_zero_apply, iteratedFDeriv_zero_apply]
  Â· intro x hx
    rw [iteratedFDeriv_succ_eq_comp_left, iteratedFDerivWithin_succ_eq_comp_left]
    dsimp
    congr 1
    rw [fderivWithin_of_isOpen hs hx]
    apply Filter.EventuallyEq.fderiv_eq
    filter_upwards [hs.mem_nhds hx]
    exact IH
#align iterated_fderiv_within_of_is_open iteratedFDerivWithin_of_isOpen

theorem ftaylorSeriesWithin_univ : ftaylorSeriesWithin ğ•œ f univ = ftaylorSeries ğ•œ f := by
  ext1 x; ext1 n
  change iteratedFDerivWithin ğ•œ n f univ x = iteratedFDeriv ğ•œ n f x
  rw [iteratedFDerivWithin_univ]
#align ftaylor_series_within_univ ftaylorSeriesWithin_univ

theorem iteratedFDeriv_succ_apply_right {n : â„•} (m : Fin (n + 1) â†’ E) :
    (iteratedFDeriv ğ•œ (n + 1) f x : (Fin (n + 1) â†’ E) â†’ F) m =
      iteratedFDeriv ğ•œ n (fun y => fderiv ğ•œ f y) x (init m) (m (last n)) := by
  rw [â† iteratedFDerivWithin_univ, â† iteratedFDerivWithin_univ, â† fderivWithin_univ]
  exact iteratedFDerivWithin_succ_apply_right uniqueDiffOn_univ (mem_univ _) _
#align iterated_fderiv_succ_apply_right iteratedFDeriv_succ_apply_right

/-- Writing explicitly the `n+1`-th derivative as the composition of a currying linear equiv,
and the `n`-th derivative of the derivative. -/
theorem iteratedFDeriv_succ_eq_comp_right {n : â„•} :
    iteratedFDeriv ğ•œ (n + 1) f x =
      (continuousMultilinearCurryRightEquiv' ğ•œ n E F âˆ˜ iteratedFDeriv ğ•œ n fun y => fderiv ğ•œ f y)
        x := by
  ext m; rw [iteratedFDeriv_succ_apply_right]; rfl
#align iterated_fderiv_succ_eq_comp_right iteratedFDeriv_succ_eq_comp_right

theorem norm_iteratedFDeriv_fderiv {n : â„•} :
    â€–iteratedFDeriv ğ•œ n (fderiv ğ•œ f) xâ€– = â€–iteratedFDeriv ğ•œ (n + 1) f xâ€– := by
  -- Porting note: added `comp_apply`.
  rw [iteratedFDeriv_succ_eq_comp_right, comp_apply, LinearIsometryEquiv.norm_map]
#align norm_iterated_fderiv_fderiv norm_iteratedFDeriv_fderiv

@[simp]
theorem iteratedFDeriv_one_apply (m : Fin 1 â†’ E) :
    iteratedFDeriv ğ•œ 1 f x m = fderiv ğ•œ f x (m 0) := by
  rw [iteratedFDeriv_succ_apply_right, iteratedFDeriv_zero_apply]; rfl
#align iterated_fderiv_one_apply iteratedFDeriv_one_apply

lemma iteratedFDeriv_two_apply (f : E â†’ F) (z : E) (m : Fin 2 â†’ E) :
    iteratedFDeriv ğ•œ 2 f z m = fderiv ğ•œ (fderiv ğ•œ f) z (m 0) (m 1) := by
  simp only [iteratedFDeriv_succ_apply_right]
  rfl

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylorSeriesWithin ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
theorem contDiff_iff_ftaylorSeries :
    ContDiff ğ•œ n f â†” HasFTaylorSeriesUpTo n f (ftaylorSeries ğ•œ f) := by
  constructor
  Â· rw [â† contDiffOn_univ, â† hasFTaylorSeriesUpToOn_univ_iff, â† ftaylorSeriesWithin_univ]
    exact fun h => ContDiffOn.ftaylorSeriesWithin h uniqueDiffOn_univ
  Â· intro h; exact âŸ¨ftaylorSeries ğ•œ f, hâŸ©
#align cont_diff_iff_ftaylor_series contDiff_iff_ftaylorSeries

theorem contDiff_iff_continuous_differentiable :
    ContDiff ğ•œ n f â†”
      (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Continuous fun x => iteratedFDeriv ğ•œ m f x) âˆ§
        âˆ€ m : â„•, (m : â„•âˆ) < n â†’ Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x := by
  simp [contDiffOn_univ.symm, continuous_iff_continuousOn_univ, differentiableOn_univ.symm,
    iteratedFDerivWithin_univ, contDiffOn_iff_continuousOn_differentiableOn uniqueDiffOn_univ]
#align cont_diff_iff_continuous_differentiable contDiff_iff_continuous_differentiable

/-- If `f` is `C^n` then its `m`-times iterated derivative is continuous for `m â‰¤ n`. -/
theorem ContDiff.continuous_iteratedFDeriv {m : â„•} (hm : (m : â„•âˆ) â‰¤ n) (hf : ContDiff ğ•œ n f) :
    Continuous fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp hf).1 m hm
#align cont_diff.continuous_iterated_fderiv ContDiff.continuous_iteratedFDeriv

/-- If `f` is `C^n` then its `m`-times iterated derivative is differentiable for `m < n`. -/
theorem ContDiff.differentiable_iteratedFDeriv {m : â„•} (hm : (m : â„•âˆ) < n) (hf : ContDiff ğ•œ n f) :
    Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp hf).2 m hm
#align cont_diff.differentiable_iterated_fderiv ContDiff.differentiable_iteratedFDeriv

theorem contDiff_of_differentiable_iteratedFDeriv
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Differentiable ğ•œ (iteratedFDeriv ğ•œ m f)) : ContDiff ğ•œ n f :=
  contDiff_iff_continuous_differentiable.2
    âŸ¨fun m hm => (h m hm).continuous, fun m hm => h m (le_of_lt hm)âŸ©
#align cont_diff_of_differentiable_iterated_fderiv contDiff_of_differentiable_iteratedFDeriv

/-- A function is `C^(n + 1)` if and only if it is differentiable,
and its derivative (formulated in terms of `fderiv`) is `C^n`. -/
theorem contDiff_succ_iff_fderiv {n : â„•} :
    ContDiff ğ•œ (n + 1 : â„•) f â†” Differentiable ğ•œ f âˆ§ ContDiff ğ•œ n fun y => fderiv ğ•œ f y := by
  simp only [â† contDiffOn_univ, â† differentiableOn_univ, â† fderivWithin_univ,
    contDiffOn_succ_iff_fderivWithin uniqueDiffOn_univ]
#align cont_diff_succ_iff_fderiv contDiff_succ_iff_fderiv

theorem contDiff_one_iff_fderiv : ContDiff ğ•œ 1 f â†” Differentiable ğ•œ f âˆ§ Continuous (fderiv ğ•œ f) :=
  contDiff_succ_iff_fderiv.trans <| Iff.rfl.and contDiff_zero
#align cont_diff_one_iff_fderiv contDiff_one_iff_fderiv

/-- A function is `C^âˆ` if and only if it is differentiable,
and its derivative (formulated in terms of `fderiv`) is `C^âˆ`. -/
theorem contDiff_top_iff_fderiv :
    ContDiff ğ•œ âˆ f â†” Differentiable ğ•œ f âˆ§ ContDiff ğ•œ âˆ fun y => fderiv ğ•œ f y := by
  simp only [â† contDiffOn_univ, â† differentiableOn_univ, â† fderivWithin_univ]
  rw [contDiffOn_top_iff_fderivWithin uniqueDiffOn_univ]
#align cont_diff_top_iff_fderiv contDiff_top_iff_fderiv

theorem ContDiff.continuous_fderiv (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous fun x => fderiv ğ•œ f x :=
  (contDiff_succ_iff_fderiv.1 (h.of_le hn)).2.continuous
#align cont_diff.continuous_fderiv ContDiff.continuous_fderiv

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
theorem ContDiff.continuous_fderiv_apply (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1 : E â†’ F) p.2 :=
  have A : Continuous fun q : (E â†’L[ğ•œ] F) Ã— E => q.1 q.2 := isBoundedBilinearMap_apply.continuous
  have B : Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1, p.2) :=
    ((h.continuous_fderiv hn).comp continuous_fst).prod_mk continuous_snd
  A.comp B
#align cont_diff.continuous_fderiv_apply ContDiff.continuous_fderiv_apply
