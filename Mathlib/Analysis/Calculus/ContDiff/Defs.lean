/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.ContDiff.FTaylorSeries

/-!
# Higher differentiability

A function is `C^1` on a domain if it is differentiable there, and its derivative is continuous.
By induction, it is `C^n` if it is `C^{n-1}` and its (n-1)-th derivative is `C^1` there or,
equivalently, if it is `C^1` and its derivative is `C^{n-1}`.
Finally, it is `C^âˆ` if it is `C^n` for all n.

We formalize these notions with predicates `ContDiffWithinAt`, `ContDiffAt`, `ContDiffOn` and
`ContDiff` saying that the function is `C^n` within a set at a point, at a point, on a set
and on the whole space respectively.

To avoid the issue of choice when choosing a derivative in sets where the derivative is not
necessarily unique, `ContDiffOn` is not defined directly in terms of the
regularity of the specific choice `iteratedFDerivWithin ğ•œ n f s` inside `s`, but in terms of the
existence of a nice sequence of derivatives, expressed with a predicate
`HasFTaylorSeriesUpToOn` defined in the file `FTaylorSeries`.

We prove basic properties of these notions.

## Main definitions and results
Let `f : E â†’ F` be a map between normed vector spaces over a nontrivially normed field `ğ•œ`.

* `ContDiff ğ•œ n f`: expresses that `f` is `C^n`, i.e., it admits a Taylor series up to
  rank `n`.
* `ContDiffOn ğ•œ n f s`: expresses that `f` is `C^n` in `s`.
* `ContDiffAt ğ•œ n f x`: expresses that `f` is `C^n` around `x`.
* `ContDiffWithinAt ğ•œ n f s x`: expresses that `f` is `C^n` around `x` within the set `s`.

In sets of unique differentiability, `ContDiffOn ğ•œ n f s` can be expressed in terms of the
properties of `iteratedFDerivWithin ğ•œ m f s` for `m â‰¤ n`. In the whole space,
`ContDiff ğ•œ n f` can be expressed in terms of the properties of `iteratedFDeriv ğ•œ m f`
for `m â‰¤ n`.

## Implementation notes

The definitions in this file are designed to work on any field `ğ•œ`. They are sometimes slightly more
complicated than the naive definitions one would guess from the intuition over the real or complex
numbers, but they are designed to circumvent the lack of gluing properties and partitions of unity
in general. In the usual situations, they coincide with the usual definitions.

### Definition of `C^n` functions in domains

One could define `C^n` functions in a domain `s` by fixing an arbitrary choice of derivatives (this
is what we do with `iteratedFDerivWithin`) and requiring that all these derivatives up to `n` are
continuous. If the derivative is not unique, this could lead to strange behavior like two `C^n`
functions `f` and `g` on `s` whose sum is not `C^n`. A better definition is thus to say that a
function is `C^n` inside `s` if it admits a sequence of derivatives up to `n` inside `s`.

This definition still has the problem that a function which is locally `C^n` would not need to
be `C^n`, as different choices of sequences of derivatives around different points might possibly
not be glued together to give a globally defined sequence of derivatives. (Note that this issue
can not happen over reals, thanks to partition of unity, but the behavior over a general field is
not so clear, and we want a definition for general fields). Also, there are locality
problems for the order parameter: one could image a function which, for each `n`, has a nice
sequence of derivatives up to order `n`, but they do not coincide for varying `n` and can therefore
not be glued to give rise to an infinite sequence of derivatives. This would give a function
which is `C^n` for all `n`, but not `C^âˆ`. We solve this issue by putting locality conditions
in space and order in our definition of `ContDiffWithinAt` and `ContDiffOn`.
The resulting definition is slightly more complicated to work with (in fact not so much), but it
gives rise to completely satisfactory theorems.

For instance, with this definition, a real function which is `C^m` (but not better) on `(-1/m, 1/m)`
for each natural `m` is by definition `C^âˆ` at `0`.

There is another issue with the definition of `ContDiffWithinAt ğ•œ n f s x`. We can
require the existence and good behavior of derivatives up to order `n` on a neighborhood of `x`
within `s`. However, this does not imply continuity or differentiability within `s` of the function
at `x` when `x` does not belong to `s`. Therefore, we require such existence and good behavior on
a neighborhood of `x` within `s âˆª {x}` (which appears as `insert x s` in this file).

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `âŠ¤ : â„•âˆ` with `âˆ`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

noncomputable section

open scoped Classical
open NNReal Topology Filter

local notation "âˆ" => (âŠ¤ : â„•âˆ)

/-
Porting note: These lines are not required in Mathlib4.
attribute [local instance 1001]
  NormedAddCommGroup.toAddCommGroup NormedSpace.toModule' AddCommGroup.toAddCommMonoid
-/

open Set Fin Filter Function

universe u uE uF uG uX

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type uE} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type uF} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] {X : Type uX} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
  {s sâ‚ t u : Set E} {f fâ‚ : E â†’ F} {g : F â†’ G} {x xâ‚€ : E} {c : F} {m n : â„•âˆ}
  {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Smooth functions within a set around a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to order `n` within a set `s` at a point `x` if
it admits continuous derivatives up to order `n` in a neighborhood of `x` in `s âˆª {x}`.
For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).

For instance, a real function which is `C^m` on `(-1/m, 1/m)` for each natural `m`, but not
better, is `C^âˆ` at `0` within `univ`.
-/
def ContDiffWithinAt (n : â„•âˆ) (f : E â†’ F) (s : Set E) (x : E) : Prop :=
  âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ âˆƒ u âˆˆ ğ“[insert x s] x,
    âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn m f p u

variable {ğ•œ}

theorem contDiffWithinAt_nat {n : â„•} :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x,
      âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn n f p u :=
  âŸ¨fun H => H n le_rfl, fun âŸ¨u, hu, p, hpâŸ© _m hm => âŸ¨u, hu, p, hp.of_le hmâŸ©âŸ©

theorem ContDiffWithinAt.of_le (h : ContDiffWithinAt ğ•œ n f s x) (hmn : m â‰¤ n) :
    ContDiffWithinAt ğ•œ m f s x := fun k hk => h k (le_trans hk hmn)

theorem contDiffWithinAt_iff_forall_nat_le :
    ContDiffWithinAt ğ•œ n f s x â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffWithinAt ğ•œ m f s x :=
  âŸ¨fun H _m hm => H.of_le hm, fun H m hm => H m hm _ le_rflâŸ©

theorem contDiffWithinAt_top : ContDiffWithinAt ğ•œ âˆ f s x â†” âˆ€ n : â„•, ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_iff_forall_nat_le.trans <| by simp only [forall_prop_of_true, le_top]

theorem ContDiffWithinAt.continuousWithinAt (h : ContDiffWithinAt ğ•œ n f s x) :
    ContinuousWithinAt f s x := by
  rcases h 0 bot_le with âŸ¨u, hu, p, HâŸ©
  rw [mem_nhdsWithin_insert] at hu
  exact (H.continuousOn.continuousWithinAt hu.1).mono_of_mem_nhdsWithin hu.2

theorem ContDiffWithinAt.congr_of_eventuallyEq (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x := fun m hm =>
  let âŸ¨u, hu, p, HâŸ© := h m hm
  âŸ¨{ x âˆˆ u | fâ‚ x = f x }, Filter.inter_mem hu (mem_nhdsWithin_insert.2 âŸ¨hx, hâ‚âŸ©), p,
    (H.mono (sep_subset _ _)).congr fun _ => And.rightâŸ©

theorem Filter.EventuallyEq.congr_contDiffWithinAt (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq hâ‚.symm hx.symm, fun H â†¦ H.congr_of_eventuallyEq hâ‚ hxâŸ©

@[deprecated (since := "2024-10-18")]
alias Filter.EventuallyEq.contDiffWithinAt_iff := Filter.EventuallyEq.congr_contDiffWithinAt

theorem ContDiffWithinAt.congr_of_eventuallyEq_insert (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (nhdsWithin_mono x (subset_insert x s) hâ‚)
    (mem_of_mem_nhdsWithin (mem_insert x s) hâ‚ : _)

theorem Filter.EventuallyEq.congr_contDiffWithinAt_of_insert (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq_insert hâ‚.symm, fun H â†¦ H.congr_of_eventuallyEq_insert hâ‚âŸ©

theorem ContDiffWithinAt.congr_of_eventuallyEq_of_mem (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq hâ‚ <| hâ‚.self_of_nhdsWithin hx

@[deprecated (since := "2024-10-18")]
alias ContDiffWithinAt.congr_of_eventually_eq' := ContDiffWithinAt.congr_of_eventuallyEq_of_mem

theorem Filter.EventuallyEq.congr_contDiffWithinAt_of_mem (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s):
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq_of_mem hâ‚.symm hx, fun H â†¦ H.congr_of_eventuallyEq_of_mem hâ‚ hxâŸ©

theorem ContDiffWithinAt.congr (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (Filter.eventuallyEq_of_mem self_mem_nhdsWithin hâ‚) hx

theorem contDiffWithinAt_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun h' â†¦ h'.congr (fun x hx â†¦ (hâ‚ x hx).symm) hx.symm, fun h' â†¦  h'.congr hâ‚ hxâŸ©

theorem ContDiffWithinAt.congr_of_mem (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr hâ‚ (hâ‚ _ hx)

@[deprecated (since := "2024-10-18")]
alias ContDiffWithinAt.congr' := ContDiffWithinAt.congr_of_mem

theorem contDiffWithinAt_congr_of_mem (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr hâ‚ (hâ‚ x hx)

theorem ContDiffWithinAt.congr_of_insert (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : âˆ€ y âˆˆ insert x s, fâ‚ y = f y) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr (fun y hy â†¦ hâ‚ y (mem_insert_of_mem _ hy)) (hâ‚ x (mem_insert _ _))

theorem contDiffWithinAt_congr_of_insert (hâ‚ : âˆ€ y âˆˆ insert x s, fâ‚ y = f y) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr (fun y hy â†¦ hâ‚ y (mem_insert_of_mem _ hy)) (hâ‚ x (mem_insert _ _))

theorem ContDiffWithinAt.mono_of_mem_nhdsWithin (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : s âˆˆ ğ“[t] x) : ContDiffWithinAt ğ•œ n f t x := by
  intro m hm
  rcases h m hm with âŸ¨u, hu, p, HâŸ©
  exact âŸ¨u, nhdsWithin_le_of_mem (insert_mem_nhdsWithin_insert hst) hu, p, HâŸ©

@[deprecated (since := "2024-10-30")]
alias ContDiffWithinAt.mono_of_mem := ContDiffWithinAt.mono_of_mem_nhdsWithin

theorem ContDiffWithinAt.mono (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E} (hst : t âŠ† s) :
    ContDiffWithinAt ğ•œ n f t x :=
  h.mono_of_mem_nhdsWithin <| Filter.mem_of_superset self_mem_nhdsWithin hst

theorem ContDiffWithinAt.congr_mono
    (h : ContDiffWithinAt ğ•œ n f s x) (h' : EqOn fâ‚ f sâ‚) (hâ‚ : sâ‚ âŠ† s) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ sâ‚ x :=
  (h.mono hâ‚).congr h' hx

theorem ContDiffWithinAt.congr_set (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : s =á¶ [ğ“ x] t) : ContDiffWithinAt ğ•œ n f t x := by
  rw [â† nhdsWithin_eq_iff_eventuallyEq] at hst
  apply h.mono_of_mem_nhdsWithin <| hst â–¸ self_mem_nhdsWithin

@[deprecated (since := "2024-10-23")]
alias ContDiffWithinAt.congr_nhds := ContDiffWithinAt.congr_set

theorem contDiffWithinAt_congr_set {t : Set E} (hst : s =á¶ [ğ“ x] t) :
    ContDiffWithinAt ğ•œ n f s x â†” ContDiffWithinAt ğ•œ n f t x :=
  âŸ¨fun h => h.congr_set hst, fun h => h.congr_set hst.symmâŸ©

@[deprecated (since := "2024-10-23")]
alias contDiffWithinAt_congr_nhds := contDiffWithinAt_congr_set

theorem contDiffWithinAt_inter' (h : t âˆˆ ğ“[s] x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr_set (mem_nhdsWithin_iff_eventuallyEq.1 h).symm

theorem contDiffWithinAt_inter (h : t âˆˆ ğ“ x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds h)

theorem contDiffWithinAt_insert_self :
    ContDiffWithinAt ğ•œ n f (insert x s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  simp_rw [ContDiffWithinAt, insert_idem]

theorem contDiffWithinAt_insert {y : E} :
    ContDiffWithinAt ğ•œ n f (insert y s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  rcases eq_or_ne x y with (rfl | h)
  Â· exact contDiffWithinAt_insert_self
  simp_rw [ContDiffWithinAt, insert_comm x y, nhdsWithin_insert_of_ne h]

alias âŸ¨ContDiffWithinAt.of_insert, ContDiffWithinAt.insert'âŸ© := contDiffWithinAt_insert

protected theorem ContDiffWithinAt.insert (h : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n f (insert x s) x :=
  h.insert'

theorem contDiffWithinAt_diff_singleton {y : E} :
    ContDiffWithinAt ğ•œ n f (s \ {y}) x â†” ContDiffWithinAt ğ•œ n f s x := by
  rw [â† contDiffWithinAt_insert, insert_diff_singleton, contDiffWithinAt_insert]

/-- If a function is `C^n` within a set at a point, with `n â‰¥ 1`, then it is differentiable
within this set at this point. -/
theorem ContDiffWithinAt.differentiableWithinAt' (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f (insert x s) x := by
  rcases h 1 hn with âŸ¨u, hu, p, HâŸ©
  rcases mem_nhdsWithin.1 hu with âŸ¨t, t_open, xt, tuâŸ©
  rw [inter_comm] at tu
  exact (differentiableWithinAt_inter (IsOpen.mem_nhds t_open xt)).1 <|
    ((H.mono tu).differentiableOn le_rfl) x âŸ¨mem_insert x s, xtâŸ©

@[deprecated (since := "2024-10-10")]
alias ContDiffWithinAt.differentiable_within_at' := ContDiffWithinAt.differentiableWithinAt'

theorem ContDiffWithinAt.differentiableWithinAt (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f s x :=
  (h.differentiableWithinAt' hn).mono (subset_insert x s)

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt {n : â„•} :
    ContDiffWithinAt ğ•œ (n + 1 : â„•) f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
      (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffWithinAt ğ•œ n f' u x := by
  constructor
  Â· intro h
    rcases h n.succ le_rfl with âŸ¨u, hu, p, HpâŸ©
    refine
      âŸ¨u, hu, fun y => (continuousMultilinearCurryFin1 ğ•œ E F) (p y 1), fun y hy =>
        Hp.hasFDerivWithinAt (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)) hy, ?_âŸ©
    intro m hm
    refine âŸ¨u, ?_, fun y : E => (p y).shift, ?_âŸ©
    Â· -- Porting note: without the explicit argument Lean is not sure of the type.
      convert @self_mem_nhdsWithin _ _ x u
      have : x âˆˆ insert x s := by simp
      exact insert_eq_of_mem (mem_of_mem_nhdsWithin this hu)
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right] at Hp
      exact Hp.2.2.of_le hm
  Â· rintro âŸ¨u, hu, f', f'_eq_deriv, Hf'âŸ©
    rw [contDiffWithinAt_nat]
    rcases Hf' n le_rfl with âŸ¨v, hv, p', Hp'âŸ©
    refine âŸ¨v âˆ© u, ?_, fun x => (p' x).unshift (f x), ?_âŸ©
    Â· apply Filter.inter_mem _ hu
      apply nhdsWithin_le_of_mem hu
      exact nhdsWithin_mono _ (subset_insert x u) hv
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right]
      refine âŸ¨fun y _ => rfl, fun y hy => ?_, ?_âŸ©
      Â· change
          HasFDerivWithinAt (fun z => (continuousMultilinearCurryFin0 ğ•œ E F).symm (f z))
            (FormalMultilinearSeries.unshift (p' y) (f y) 1).curryLeft (v âˆ© u) y
        -- Porting note: needed `erw` here.
        -- https://github.com/leanprover-community/mathlib4/issues/5164
        erw [LinearIsometryEquiv.comp_hasFDerivWithinAt_iff']
        convert (f'_eq_deriv y hy.2).mono inter_subset_right
        rw [â† Hp'.zero_eq y hy.1]
        ext z
        change ((p' y 0) (init (@cons 0 (fun _ => E) z 0))) (@cons 0 (fun _ => E) z 0 (last 0)) =
          ((p' y 0) 0) z
        congr
        norm_num [eq_iff_true_of_subsingleton]
      Â· convert (Hp'.mono inter_subset_left).congr fun x hx => Hp'.zero_eq x hx.1 using 1
        Â· ext x y
          change p' x 0 (init (@snoc 0 (fun _ : Fin 1 => E) 0 y)) y = p' x 0 0 y
          rw [init_snoc]
        Â· ext x k v y
          change p' x k (init (@snoc k (fun _ : Fin k.succ => E) v y))
            (@snoc k (fun _ : Fin k.succ => E) v y (last k)) = p' x k v y
          rw [snoc_last, init_snoc]

/-- A version of `contDiffWithinAt_succ_iff_hasFDerivWithinAt` where all derivatives
  are taken within the same set. -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt' {n : â„•} :
    ContDiffWithinAt ğ•œ (n + 1 : â„•) f s x â†”
      âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) s x) âˆ§ ContDiffWithinAt ğ•œ n f' s x := by
  refine âŸ¨fun hf => ?_, ?_âŸ©
  Â· obtain âŸ¨u, hu, f', huf', hf'âŸ© := contDiffWithinAt_succ_iff_hasFDerivWithinAt.mp hf
    obtain âŸ¨w, hw, hxw, hwuâŸ© := mem_nhdsWithin.mp hu
    rw [inter_comm] at hwu
    refine âŸ¨insert x s âˆ© w, inter_mem_nhdsWithin _ (hw.mem_nhds hxw), inter_subset_left, f',
      fun y hy => ?_, ?_âŸ©
    Â· refine ((huf' y <| hwu hy).mono hwu).mono_of_mem ?_
      refine mem_of_superset ?_ (inter_subset_inter_left _ (subset_insert _ _))
      exact inter_mem_nhdsWithin _ (hw.mem_nhds hy.2)
    Â· exact hf'.mono_of_mem_nhdsWithin (nhdsWithin_mono _ (subset_insert _ _) hu)
  Â· rw [â† contDiffWithinAt_insert, contDiffWithinAt_succ_iff_hasFDerivWithinAt,
      insert_eq_of_mem (mem_insert _ _)]
    rintro âŸ¨u, hu, hus, f', huf', hf'âŸ©
    exact âŸ¨u, hu, f', fun y hy => (huf' y hy).insert'.mono hus, hf'.insert.mono husâŸ©

/-! ### Smooth functions within a set -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` on `s` if, for any point `x` in `s`, it
admits continuous derivatives up to order `n` on a neighborhood of `x` in `s`.

For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).
-/
def ContDiffOn (n : â„•âˆ) (f : E â†’ F) (s : Set E) : Prop :=
  âˆ€ x âˆˆ s, ContDiffWithinAt ğ•œ n f s x

variable {ğ•œ}

theorem HasFTaylorSeriesUpToOn.contDiffOn {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpToOn n f f' s) : ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  use s
  simp only [Set.insert_eq_of_mem hx, self_mem_nhdsWithin, true_and]
  exact âŸ¨f', hf.of_le hmâŸ©

theorem ContDiffOn.contDiffWithinAt (h : ContDiffOn ğ•œ n f s) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n f s x :=
  h x hx

theorem ContDiffWithinAt.contDiffOn' {m : â„•} (hm : (m : â„•âˆ) â‰¤ n)
    (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ m f (insert x s âˆ© u) := by
  rcases h m hm with âŸ¨t, ht, p, hpâŸ©
  rcases mem_nhdsWithin.1 ht with âŸ¨u, huo, hxu, hutâŸ©
  rw [inter_comm] at hut
  exact âŸ¨u, huo, hxu, (hp.mono hut).contDiffOnâŸ©

theorem ContDiffWithinAt.contDiffOn {m : â„•} (hm : (m : â„•âˆ) â‰¤ n) (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ ContDiffOn ğ•œ m f u :=
  let âŸ¨_u, uo, xu, hâŸ© := h.contDiffOn' hm
  âŸ¨_, inter_mem_nhdsWithin _ (uo.mem_nhds xu), inter_subset_left, hâŸ©

/-- A function is `C^n` within a set at a point, for `n : â„•`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contDiffWithinAt_iff_contDiffOn_nhds {n : â„•} :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, ContDiffOn ğ•œ n f u := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rcases h.contDiffOn le_rfl with âŸ¨u, hu, h'uâŸ©
    exact âŸ¨u, hu, h'u.2âŸ©
  Â· rcases h with âŸ¨u, u_mem, huâŸ©
    have : x âˆˆ u := mem_of_mem_nhdsWithin (mem_insert x s) u_mem
    exact (hu x this).mono_of_mem_nhdsWithin (nhdsWithin_mono _ (subset_insert x s) u_mem)

protected theorem ContDiffWithinAt.eventually {n : â„•} (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆ€á¶  y in ğ“[insert x s] x, ContDiffWithinAt ğ•œ n f s y := by
  rcases h.contDiffOn le_rfl with âŸ¨u, hu, _, hdâŸ©
  have : âˆ€á¶  y : E in ğ“[insert x s] x, u âˆˆ ğ“[insert x s] y âˆ§ y âˆˆ u :=
    (eventually_eventually_nhdsWithin.2 hu).and hu
  refine this.mono fun y hy => (hd y hy.2).mono_of_mem_nhdsWithin ?_
  exact nhdsWithin_mono y (subset_insert _ _) hy.1

theorem ContDiffOn.of_le (h : ContDiffOn ğ•œ n f s) (hmn : m â‰¤ n) : ContDiffOn ğ•œ m f s := fun x hx =>
  (h x hx).of_le hmn

theorem ContDiffOn.of_succ {n : â„•} (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ n f s :=
  h.of_le <| WithTop.coe_le_coe.mpr le_self_add

theorem ContDiffOn.one_of_succ {n : â„•} (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ 1 f s :=
  h.of_le <| WithTop.coe_le_coe.mpr le_add_self

theorem contDiffOn_iff_forall_nat_le : ContDiffOn ğ•œ n f s â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffOn ğ•œ m f s :=
  âŸ¨fun H _ hm => H.of_le hm, fun H x hx m hm => H m hm x hx m le_rflâŸ©

theorem contDiffOn_top : ContDiffOn ğ•œ âˆ f s â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s :=
  contDiffOn_iff_forall_nat_le.trans <| by simp only [le_top, forall_prop_of_true]

theorem contDiffOn_all_iff_nat : (âˆ€ n, ContDiffOn ğ•œ n f s) â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s := by
  refine âŸ¨fun H n => H n, ?_âŸ©
  rintro H (_ | n)
  exacts [contDiffOn_top.2 H, H n]

theorem ContDiffOn.continuousOn (h : ContDiffOn ğ•œ n f s) : ContinuousOn f s := fun x hx =>
  (h x hx).continuousWithinAt

theorem ContDiffOn.congr (h : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
    ContDiffOn ğ•œ n fâ‚ s := fun x hx => (h x hx).congr hâ‚ (hâ‚ x hx)

theorem contDiffOn_congr (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) : ContDiffOn ğ•œ n fâ‚ s â†” ContDiffOn ğ•œ n f s :=
  âŸ¨fun H => H.congr fun x hx => (hâ‚ x hx).symm, fun H => H.congr hâ‚âŸ©

theorem ContDiffOn.mono (h : ContDiffOn ğ•œ n f s) {t : Set E} (hst : t âŠ† s) : ContDiffOn ğ•œ n f t :=
  fun x hx => (h x (hst hx)).mono hst

theorem ContDiffOn.congr_mono (hf : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ sâ‚, fâ‚ x = f x) (hs : sâ‚ âŠ† s) :
    ContDiffOn ğ•œ n fâ‚ sâ‚ :=
  (hf.mono hs).congr hâ‚

/-- If a function is `C^n` on a set with `n â‰¥ 1`, then it is differentiable there. -/
theorem ContDiffOn.differentiableOn (h : ContDiffOn ğ•œ n f s) (hn : 1 â‰¤ n) :
    DifferentiableOn ğ•œ f s := fun x hx => (h x hx).differentiableWithinAt hn

/-- If a function is `C^n` around each point in a set, then it is `C^n` on the set. -/
theorem contDiffOn_of_locally_contDiffOn
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ n f (s âˆ© u)) : ContDiffOn ğ•œ n f s := by
  intro x xs
  rcases h x xs with âŸ¨u, u_open, xu, huâŸ©
  apply (contDiffWithinAt_inter _).1 (hu x âŸ¨xs, xuâŸ©)
  exact IsOpen.mem_nhds u_open xu

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem contDiffOn_succ_iff_hasFDerivWithinAt {n : â„•} :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffOn ğ•œ n f' u := by
  constructor
  Â· intro h x hx
    rcases (h x hx) n.succ le_rfl with âŸ¨u, hu, p, HpâŸ©
    refine
      âŸ¨u, hu, fun y => (continuousMultilinearCurryFin1 ğ•œ E F) (p y 1), fun y hy =>
        Hp.hasFDerivWithinAt (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)) hy, ?_âŸ©
    rw [hasFTaylorSeriesUpToOn_succ_iff_right] at Hp
    intro z hz m hm
    refine âŸ¨u, ?_, fun x : E => (p x).shift, Hp.2.2.of_le hmâŸ©
    -- Porting note: without the explicit arguments `convert` can not determine the type.
    convert @self_mem_nhdsWithin _ _ z u
    exact insert_eq_of_mem hz
  Â· intro h x hx
    rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt]
    rcases h x hx with âŸ¨u, u_nhbd, f', hu, hf'âŸ©
    have : x âˆˆ u := mem_of_mem_nhdsWithin (mem_insert _ _) u_nhbd
    exact âŸ¨u, u_nhbd, f', hu, hf' x thisâŸ©

@[simp]
theorem contDiffOn_zero : ContDiffOn ğ•œ 0 f s â†” ContinuousOn f s := by
  refine âŸ¨fun H => H.continuousOn, fun H => fun x hx m hm â†¦ ?_âŸ©
  have : (m : â„•âˆ) = 0 := le_antisymm hm bot_le
  rw [this]
  refine âŸ¨insert x s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  rw [hasFTaylorSeriesUpToOn_zero_iff]
  exact âŸ¨by rwa [insert_eq_of_mem hx], fun x _ => by simp [ftaylorSeriesWithin]âŸ©

theorem contDiffWithinAt_zero (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ 0 f s x â†” âˆƒ u âˆˆ ğ“[s] x, ContinuousOn f (s âˆ© u) := by
  constructor
  Â· intro h
    obtain âŸ¨u, H, p, hpâŸ© := h 0 le_rfl
    refine âŸ¨u, ?_, ?_âŸ©
    Â· simpa [hx] using H
    Â· simp only [Nat.cast_zero, hasFTaylorSeriesUpToOn_zero_iff] at hp
      exact hp.1.mono inter_subset_right
  Â· rintro âŸ¨u, H, huâŸ©
    rw [â† contDiffWithinAt_inter' H]
    have h' : x âˆˆ s âˆ© u := âŸ¨hx, mem_of_mem_nhdsWithin hx HâŸ©
    exact (contDiffOn_zero.mpr hu).contDiffWithinAt h'

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylorSeriesWithin ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
protected theorem ContDiffOn.ftaylorSeriesWithin (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) :
    HasFTaylorSeriesUpToOn n f (ftaylorSeriesWithin ğ•œ f s) s := by
  constructor
  Â· intro x _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.curry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro m hm x hx
    rcases (h x hx) m.succ (Order.add_one_le_of_lt hm) with âŸ¨u, hu, p, HpâŸ©
    rw [insert_eq_of_mem hx] at hu
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [inter_comm] at ho
    have : p x m.succ = ftaylorSeriesWithin ğ•œ f s x m.succ := by
      change p x m.succ = iteratedFDerivWithin ğ•œ m.succ f s x
      rw [â† iteratedFDerivWithin_inter_open o_open xo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hx, xoâŸ©
    rw [â† this, â† hasFDerivWithinAt_inter (IsOpen.mem_nhds o_open xo)]
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact
        (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn (WithTop.coe_le_coe.2 (Nat.le_succ m))
          (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact
      ((Hp.mono ho).fderivWithin m (WithTop.coe_lt_coe.2 (lt_add_one m)) x âŸ¨hx, xoâŸ©).congr
        (fun y hy => (A y hy).symm) (A x âŸ¨hx, xoâŸ©).symm
  Â· intro m hm
    apply continuousOn_of_locally_continuousOn
    intro x hx
    rcases h x hx m hm with âŸ¨u, hu, p, HpâŸ©
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [insert_eq_of_mem hx] at ho
    rw [inter_comm] at ho
    refine âŸ¨o, o_open, xo, ?_âŸ©
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact ((Hp.mono ho).cont m le_rfl).congr fun y hy => (A y hy).symm

theorem contDiffOn_of_continuousOn_differentiableOn
    (Hcont : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s)
    (Hdiff : âˆ€ m : â„•, (m : â„•âˆ) < n â†’
      DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s) :
    ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  rw [insert_eq_of_mem hx]
  refine âŸ¨s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  constructor
  Â· intro y _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.curry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro k hk y hy
    convert (Hdiff k (lt_of_lt_of_le hk hm) y hy).hasFDerivWithinAt
  Â· intro k hk
    exact Hcont k (le_trans hk hm)

theorem contDiffOn_of_differentiableOn
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s) :
    ContDiffOn ğ•œ n f s :=
  contDiffOn_of_continuousOn_differentiableOn (fun m hm => (h m hm).continuousOn) fun m hm =>
    h m (le_of_lt hm)

theorem ContDiffOn.continuousOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : (m : â„•âˆ) â‰¤ n) (hs : UniqueDiffOn ğ•œ s) : ContinuousOn (iteratedFDerivWithin ğ•œ m f s) s :=
  (h.ftaylorSeriesWithin hs).cont m hmn

theorem ContDiffOn.differentiableOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : (m : â„•âˆ) < n) (hs : UniqueDiffOn ğ•œ s) :
    DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s := fun x hx =>
  ((h.ftaylorSeriesWithin hs).fderivWithin m hmn x hx).differentiableWithinAt

theorem ContDiffWithinAt.differentiableWithinAt_iteratedFDerivWithin {m : â„•}
    (h : ContDiffWithinAt ğ•œ n f s x) (hmn : (m : â„•âˆ) < n) (hs : UniqueDiffOn ğ•œ (insert x s)) :
    DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f s) s x := by
  rcases h.contDiffOn' (Order.add_one_le_of_lt hmn) with âŸ¨u, uo, xu, huâŸ©
  set t := insert x s âˆ© u
  have A : t =á¶ [ğ“[â‰ ] x] s := by
    simp only [set_eventuallyEq_iff_inf_principal, â† nhdsWithin_inter']
    rw [â† inter_assoc, nhdsWithin_inter_of_mem', â† diff_eq_compl_inter, insert_diff_of_mem,
      diff_eq_compl_inter]
    exacts [rfl, mem_nhdsWithin_of_mem_nhds (uo.mem_nhds xu)]
  have B : iteratedFDerivWithin ğ•œ m f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ m f t :=
    iteratedFDerivWithin_eventually_congr_set' _ A.symm _
  have C : DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f t) t x :=
    hu.differentiableOn_iteratedFDerivWithin (Nat.cast_lt.2 m.lt_succ_self) (hs.inter uo) x
      âŸ¨mem_insert _ _, xuâŸ©
  rw [differentiableWithinAt_congr_set' _ A] at C
  exact C.congr_of_eventuallyEq (B.filter_mono inf_le_left) B.self_of_nhds

theorem contDiffOn_iff_continuousOn_differentiableOn (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†”
      (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s) âˆ§
        âˆ€ m : â„•, (m : â„•âˆ) < n â†’ DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s :=
  âŸ¨fun h => âŸ¨fun _m hm => h.continuousOn_iteratedFDerivWithin hm hs, fun _m hm =>
      h.differentiableOn_iteratedFDerivWithin hm hsâŸ©,
    fun h => contDiffOn_of_continuousOn_differentiableOn h.1 h.2âŸ©

theorem contDiffOn_succ_of_fderivWithin {n : â„•} (hf : DifferentiableOn ğ•œ f s)
    (h : ContDiffOn ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) : ContDiffOn ğ•œ (n + 1 : â„•) f s := by
  intro x hx
  rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt, insert_eq_of_mem hx]
  exact
    âŸ¨s, self_mem_nhdsWithin, fderivWithin ğ•œ f s, fun y hy => (hf y hy).hasFDerivWithinAt, h x hxâŸ©

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (expressed with `fderivWithin`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ n (fun y => fderivWithin ğ•œ f s y) s := by
  refine âŸ¨fun H => ?_, fun h => contDiffOn_succ_of_fderivWithin h.1 h.2âŸ©
  refine âŸ¨H.differentiableOn (WithTop.coe_le_coe.2 (Nat.le_add_left 1 n)), fun x hx => ?_âŸ©
  rcases contDiffWithinAt_succ_iff_hasFDerivWithinAt.1 (H x hx) with âŸ¨u, hu, f', hff', hf'âŸ©
  rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
  rw [inter_comm, insert_eq_of_mem hx] at ho
  have := hf'.mono ho
  rw [contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds (IsOpen.mem_nhds o_open xo))] at this
  apply this.congr_of_eventuallyEq_of_mem _ hx
  have : o âˆ© s âˆˆ ğ“[s] x := mem_nhdsWithin.2 âŸ¨o, o_open, xo, Subset.refl _âŸ©
  rw [inter_comm] at this
  refine Filter.eventuallyEq_of_mem this fun y hy => ?_
  have A : fderivWithin ğ•œ f (s âˆ© o) y = f' y :=
    ((hff' y (ho hy)).mono ho).fderivWithin (hs.inter o_open y hy)
  rwa [fderivWithin_inter (o_open.mem_nhds hy.2)] at A

theorem contDiffOn_succ_iff_hasFDerivWithin {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiffOn ğ•œ n f' s âˆ§ âˆ€ x, x âˆˆ s â†’ HasFDerivWithinAt f (f' x) s x := by
  rw [contDiffOn_succ_iff_fderivWithin hs]
  refine âŸ¨fun h => âŸ¨fderivWithin ğ•œ f s, h.2, fun x hx => (h.1 x hx).hasFDerivWithinAtâŸ©, fun h => ?_âŸ©
  rcases h with âŸ¨f', h1, h2âŸ©
  refine âŸ¨fun x hx => (h2 x hx).differentiableWithinAt, fun x hx => ?_âŸ©
  exact (h1 x hx).congr_of_mem (fun y hy => (h2 y hy).fderivWithin (hs y hy)) hx

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (expressed with `fderiv`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderiv_of_isOpen {n : â„•} (hs : IsOpen s) :
    ContDiffOn ğ•œ (n + 1 : â„•) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ n (fun y => fderiv ğ•œ f y) s := by
  rw [contDiffOn_succ_iff_fderivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and (contDiffOn_congr fun x hx â†¦ fderivWithin_of_isOpen hs hx)

/-- A function is `C^âˆ` on a domain with unique derivatives if and only if it is differentiable
there, and its derivative (expressed with `fderivWithin`) is `C^âˆ`. -/
theorem contDiffOn_top_iff_fderivWithin (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ âˆ f s â†”
      DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fun y => fderivWithin ğ•œ f s y) s := by
  constructor
  Â· intro h
    refine âŸ¨h.differentiableOn le_top, ?_âŸ©
    refine contDiffOn_top.2 fun n => ((contDiffOn_succ_iff_fderivWithin hs).1 ?_).2
    exact h.of_le le_top
  Â· intro h
    refine contDiffOn_top.2 fun n => ?_
    have A : (n : â„•âˆ) â‰¤ âˆ := le_top
    apply ((contDiffOn_succ_iff_fderivWithin hs).2 âŸ¨h.1, h.2.of_le AâŸ©).of_le
    exact WithTop.coe_le_coe.2 (Nat.le_succ n)

/-- A function is `C^âˆ` on an open domain if and only if it is differentiable there, and its
derivative (expressed with `fderiv`) is `C^âˆ`. -/
theorem contDiffOn_top_iff_fderiv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ğ•œ âˆ f s â†” DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fun y => fderiv ğ•œ f y) s := by
  rw [contDiffOn_top_iff_fderivWithin hs.uniqueDiffOn]
  exact Iff.rfl.and <| contDiffOn_congr fun x hx â†¦ fderivWithin_of_isOpen hs hx

protected theorem ContDiffOn.fderivWithin (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hmn : m + 1 â‰¤ n) : ContDiffOn ğ•œ m (fun y => fderivWithin ğ•œ f s y) s := by
  cases' m with m
  Â· change âˆ + 1 â‰¤ n at hmn
    have : n = âˆ := by simpa using hmn
    rw [this] at hf
    exact ((contDiffOn_top_iff_fderivWithin hs).1 hf).2
  Â· change (m.succ : â„•âˆ) â‰¤ n at hmn
    exact ((contDiffOn_succ_iff_fderivWithin hs).1 (hf.of_le hmn)).2

theorem ContDiffOn.fderiv_of_isOpen (hf : ContDiffOn ğ•œ n f s) (hs : IsOpen s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (fun y => fderiv ğ•œ f y) s :=
  (hf.fderivWithin hs.uniqueDiffOn hmn).congr fun _ hx => (fderivWithin_of_isOpen hs hx).symm

theorem ContDiffOn.continuousOn_fderivWithin (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hn : 1 â‰¤ n) : ContinuousOn (fun x => fderivWithin ğ•œ f s x) s :=
  ((contDiffOn_succ_iff_fderivWithin hs).1 (h.of_le hn)).2.continuousOn

theorem ContDiffOn.continuousOn_fderiv_of_isOpen (h : ContDiffOn ğ•œ n f s) (hs : IsOpen s)
    (hn : 1 â‰¤ n) : ContinuousOn (fun x => fderiv ğ•œ f x) s :=
  ((contDiffOn_succ_iff_fderiv_of_isOpen hs).1 (h.of_le hn)).2.continuousOn

/-! ### Smooth functions at a point -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` at a point `x` if, for any integer `k â‰¤ n`,
there is a neighborhood of `x` where `f` admits derivatives up to order `n`, which are continuous.
-/
def ContDiffAt (n : â„•âˆ) (f : E â†’ F) (x : E) : Prop :=
  ContDiffWithinAt ğ•œ n f univ x

variable {ğ•œ}

theorem contDiffWithinAt_univ : ContDiffWithinAt ğ•œ n f univ x â†” ContDiffAt ğ•œ n f x :=
  Iff.rfl

theorem contDiffAt_top : ContDiffAt ğ•œ âˆ f x â†” âˆ€ n : â„•, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffWithinAt_univ, contDiffWithinAt_top]

theorem ContDiffAt.contDiffWithinAt (h : ContDiffAt ğ•œ n f x) : ContDiffWithinAt ğ•œ n f s x :=
  h.mono (subset_univ _)

theorem ContDiffWithinAt.contDiffAt (h : ContDiffWithinAt ğ•œ n f s x) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x := by rwa [ContDiffAt, â† contDiffWithinAt_inter hx, univ_inter]

theorem contDiffWithinAt_iff_contDiffAt (h : s âˆˆ ğ“ x) :
    ContDiffWithinAt ğ•œ n f s x â†” ContDiffAt ğ•œ n f x := by
  rw [â† univ_inter s, contDiffWithinAt_inter h, contDiffWithinAt_univ]

theorem IsOpen.contDiffOn_iff (hs : IsOpen s) :
    ContDiffOn ğ•œ n f s â†” âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ ContDiffAt ğ•œ n f a :=
  forallâ‚‚_congr fun _ => contDiffWithinAt_iff_contDiffAt âˆ˜ hs.mem_nhds

theorem ContDiffOn.contDiffAt (h : ContDiffOn ğ•œ n f s) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x :=
  (h _ (mem_of_mem_nhds hx)).contDiffAt hx

theorem ContDiffAt.congr_of_eventuallyEq (h : ContDiffAt ğ•œ n f x) (hg : fâ‚ =á¶ [ğ“ x] f) :
    ContDiffAt ğ•œ n fâ‚ x :=
  h.congr_of_eventuallyEq_of_mem (by rwa [nhdsWithin_univ]) (mem_univ x)

theorem ContDiffAt.of_le (h : ContDiffAt ğ•œ n f x) (hmn : m â‰¤ n) : ContDiffAt ğ•œ m f x :=
  ContDiffWithinAt.of_le h hmn

theorem ContDiffAt.continuousAt (h : ContDiffAt ğ•œ n f x) : ContinuousAt f x := by
  simpa [continuousWithinAt_univ] using h.continuousWithinAt

@[simp]
theorem contDiffWithinAt_compl_self :
    ContDiffWithinAt ğ•œ n f {x}á¶œ x â†” ContDiffAt ğ•œ n f x := by
  rw [compl_eq_univ_diff, contDiffWithinAt_diff_singleton, contDiffWithinAt_univ]

/-- If a function is `C^n` with `n â‰¥ 1` at a point, then it is differentiable there. -/
theorem ContDiffAt.differentiableAt (h : ContDiffAt ğ•œ n f x) (hn : 1 â‰¤ n) :
    DifferentiableAt ğ•œ f x := by
  simpa [hn, differentiableWithinAt_univ] using h.differentiableWithinAt

nonrec lemma ContDiffAt.contDiffOn {m : â„•} (h : ContDiffAt ğ•œ n f x) (hm : m â‰¤ n) :
    âˆƒ u âˆˆ ğ“ x, ContDiffOn ğ•œ m f u := by
  simpa [nhdsWithin_univ] using h.contDiffOn hm

/-- A function is `C^(n + 1)` at a point iff locally, it has a derivative which is `C^n`. -/
theorem contDiffAt_succ_iff_hasFDerivAt {n : â„•} :
    ContDiffAt ğ•œ (n + 1 : â„•) f x â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, (âˆƒ u âˆˆ ğ“ x, âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x) âˆ§ ContDiffAt ğ•œ n f' x := by
  rw [â† contDiffWithinAt_univ, contDiffWithinAt_succ_iff_hasFDerivWithinAt]
  simp only [nhdsWithin_univ, exists_prop, mem_univ, insert_eq_of_mem]
  constructor
  Â· rintro âŸ¨u, H, f', h_fderiv, h_cont_diffâŸ©
    rcases mem_nhds_iff.mp H with âŸ¨t, htu, ht, hxtâŸ©
    refine âŸ¨f', âŸ¨t, ?_âŸ©, h_cont_diff.contDiffAt HâŸ©
    refine âŸ¨mem_nhds_iff.mpr âŸ¨t, Subset.rfl, ht, hxtâŸ©, ?_âŸ©
    intro y hyt
    refine (h_fderiv y (htu hyt)).hasFDerivAt ?_
    exact mem_nhds_iff.mpr âŸ¨t, htu, ht, hytâŸ©
  Â· rintro âŸ¨f', âŸ¨u, H, h_fderivâŸ©, h_cont_diffâŸ©
    refine âŸ¨u, H, f', ?_, h_cont_diff.contDiffWithinAtâŸ©
    intro x hxu
    exact (h_fderiv x hxu).hasFDerivWithinAt

protected theorem ContDiffAt.eventually {n : â„•} (h : ContDiffAt ğ•œ n f x) :
    âˆ€á¶  y in ğ“ x, ContDiffAt ğ•œ n f y := by
  simpa [nhdsWithin_univ] using ContDiffWithinAt.eventually h

/-! ### Smooth functions -/

variable (ğ•œ)

/-- A function is continuously differentiable up to `n` if it admits derivatives up to
order `n`, which are continuous. Contrary to the case of definitions in domains (where derivatives
might not be unique) we do not need to localize the definition in space or time.
-/
def ContDiff (n : â„•âˆ) (f : E â†’ F) : Prop :=
  âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpTo n f p

variable {ğ•œ}

/-- If `f` has a Taylor series up to `n`, then it is `C^n`. -/
theorem HasFTaylorSeriesUpTo.contDiff {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpTo n f f') : ContDiff ğ•œ n f :=
  âŸ¨f', hfâŸ©

theorem contDiffOn_univ : ContDiffOn ğ•œ n f univ â†” ContDiff ğ•œ n f := by
  constructor
  Â· intro H
    use ftaylorSeriesWithin ğ•œ f univ
    rw [â† hasFTaylorSeriesUpToOn_univ_iff]
    exact H.ftaylorSeriesWithin uniqueDiffOn_univ
  Â· rintro âŸ¨p, hpâŸ© x _ m hm
    exact âŸ¨univ, Filter.univ_sets _, p, (hp.hasFTaylorSeriesUpToOn univ).of_le hmâŸ©

theorem contDiff_iff_contDiffAt : ContDiff ğ•œ n f â†” âˆ€ x, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffOn_univ, ContDiffOn, ContDiffAt]

theorem ContDiff.contDiffAt (h : ContDiff ğ•œ n f) : ContDiffAt ğ•œ n f x :=
  contDiff_iff_contDiffAt.1 h x

theorem ContDiff.contDiffWithinAt (h : ContDiff ğ•œ n f) : ContDiffWithinAt ğ•œ n f s x :=
  h.contDiffAt.contDiffWithinAt

theorem contDiff_top : ContDiff ğ•œ âˆ f â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp [contDiffOn_univ.symm, contDiffOn_top]

theorem contDiff_all_iff_nat : (âˆ€ n, ContDiff ğ•œ n f) â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp only [â† contDiffOn_univ, contDiffOn_all_iff_nat]

theorem ContDiff.contDiffOn (h : ContDiff ğ•œ n f) : ContDiffOn ğ•œ n f s :=
  (contDiffOn_univ.2 h).mono (subset_univ _)

@[simp]
theorem contDiff_zero : ContDiff ğ•œ 0 f â†” Continuous f := by
  rw [â† contDiffOn_univ, continuous_iff_continuousOn_univ]
  exact contDiffOn_zero

theorem contDiffAt_zero : ContDiffAt ğ•œ 0 f x â†” âˆƒ u âˆˆ ğ“ x, ContinuousOn f u := by
  rw [â† contDiffWithinAt_univ]; simp [contDiffWithinAt_zero, nhdsWithin_univ]

theorem contDiffAt_one_iff :
    ContDiffAt ğ•œ 1 f x â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, âˆƒ u âˆˆ ğ“ x, ContinuousOn f' u âˆ§ âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x := by
  simp_rw [show (1 : â„•âˆ) = (0 + 1 : â„•) from (zero_add 1).symm, contDiffAt_succ_iff_hasFDerivAt,
    show ((0 : â„•) : â„•âˆ) = 0 from rfl, contDiffAt_zero,
    exists_mem_and_iff antitone_bforall antitone_continuousOn, and_comm]

theorem ContDiff.of_le (h : ContDiff ğ•œ n f) (hmn : m â‰¤ n) : ContDiff ğ•œ m f :=
  contDiffOn_univ.1 <| (contDiffOn_univ.2 h).of_le hmn

theorem ContDiff.of_succ {n : â„•} (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ n f :=
  h.of_le <| WithTop.coe_le_coe.mpr le_self_add

theorem ContDiff.one_of_succ {n : â„•} (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ 1 f :=
  h.of_le <| WithTop.coe_le_coe.mpr le_add_self

theorem ContDiff.continuous (h : ContDiff ğ•œ n f) : Continuous f :=
  contDiff_zero.1 (h.of_le bot_le)

/-- If a function is `C^n` with `n â‰¥ 1`, then it is differentiable. -/
theorem ContDiff.differentiable (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) : Differentiable ğ•œ f :=
  differentiableOn_univ.1 <| (contDiffOn_univ.2 h).differentiableOn hn

theorem contDiff_iff_forall_nat_le : ContDiff ğ•œ n f â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiff ğ•œ m f := by
  simp_rw [â† contDiffOn_univ]; exact contDiffOn_iff_forall_nat_le

/-- A function is `C^(n+1)` iff it has a `C^n` derivative. -/
theorem contDiff_succ_iff_hasFDerivAt {n : â„•} :
    ContDiff ğ•œ (n + 1 : â„•) f â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiff ğ•œ n f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  simp only [â† contDiffOn_univ, â† hasFDerivWithinAt_univ,
    contDiffOn_succ_iff_hasFDerivWithin uniqueDiffOn_univ, Set.mem_univ, forall_true_left]

theorem contDiff_one_iff_hasFDerivAt : ContDiff ğ•œ 1 f â†”
    âˆƒ f' : E â†’ E â†’L[ğ•œ] F, Continuous f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  convert contDiff_succ_iff_hasFDerivAt using 4; simp

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylorSeriesWithin ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
theorem contDiff_iff_ftaylorSeries :
    ContDiff ğ•œ n f â†” HasFTaylorSeriesUpTo n f (ftaylorSeries ğ•œ f) := by
  constructor
  Â· rw [â† contDiffOn_univ, â† hasFTaylorSeriesUpToOn_univ_iff, â† ftaylorSeriesWithin_univ]
    exact fun h => ContDiffOn.ftaylorSeriesWithin h uniqueDiffOn_univ
  Â· intro h; exact âŸ¨ftaylorSeries ğ•œ f, hâŸ©

theorem contDiff_iff_continuous_differentiable :
    ContDiff ğ•œ n f â†”
      (âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Continuous fun x => iteratedFDeriv ğ•œ m f x) âˆ§
        âˆ€ m : â„•, (m : â„•âˆ) < n â†’ Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x := by
  simp [contDiffOn_univ.symm, continuous_iff_continuousOn_univ, differentiableOn_univ.symm,
    iteratedFDerivWithin_univ, contDiffOn_iff_continuousOn_differentiableOn uniqueDiffOn_univ]

/-- If `f` is `C^n` then its `m`-times iterated derivative is continuous for `m â‰¤ n`. -/
theorem ContDiff.continuous_iteratedFDeriv {m : â„•} (hm : (m : â„•âˆ) â‰¤ n) (hf : ContDiff ğ•œ n f) :
    Continuous fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp hf).1 m hm

/-- If `f` is `C^n` then its `m`-times iterated derivative is differentiable for `m < n`. -/
theorem ContDiff.differentiable_iteratedFDeriv {m : â„•} (hm : (m : â„•âˆ) < n) (hf : ContDiff ğ•œ n f) :
    Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp hf).2 m hm

theorem contDiff_of_differentiable_iteratedFDeriv
    (h : âˆ€ m : â„•, (m : â„•âˆ) â‰¤ n â†’ Differentiable ğ•œ (iteratedFDeriv ğ•œ m f)) : ContDiff ğ•œ n f :=
  contDiff_iff_continuous_differentiable.2
    âŸ¨fun m hm => (h m hm).continuous, fun m hm => h m (le_of_lt hm)âŸ©

/-- A function is `C^(n + 1)` if and only if it is differentiable,
and its derivative (formulated in terms of `fderiv`) is `C^n`. -/
theorem contDiff_succ_iff_fderiv {n : â„•} :
    ContDiff ğ•œ (n + 1 : â„•) f â†” Differentiable ğ•œ f âˆ§ ContDiff ğ•œ n fun y => fderiv ğ•œ f y := by
  simp only [â† contDiffOn_univ, â† differentiableOn_univ, â† fderivWithin_univ,
    contDiffOn_succ_iff_fderivWithin uniqueDiffOn_univ]

theorem contDiff_one_iff_fderiv : ContDiff ğ•œ 1 f â†” Differentiable ğ•œ f âˆ§ Continuous (fderiv ğ•œ f) :=
  contDiff_succ_iff_fderiv.trans <| Iff.rfl.and contDiff_zero

/-- A function is `C^âˆ` if and only if it is differentiable,
and its derivative (formulated in terms of `fderiv`) is `C^âˆ`. -/
theorem contDiff_top_iff_fderiv :
    ContDiff ğ•œ âˆ f â†” Differentiable ğ•œ f âˆ§ ContDiff ğ•œ âˆ fun y => fderiv ğ•œ f y := by
  simp only [â† contDiffOn_univ, â† differentiableOn_univ, â† fderivWithin_univ]
  rw [contDiffOn_top_iff_fderivWithin uniqueDiffOn_univ]

theorem ContDiff.continuous_fderiv (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous fun x => fderiv ğ•œ f x :=
  (contDiff_succ_iff_fderiv.1 (h.of_le hn)).2.continuous

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
theorem ContDiff.continuous_fderiv_apply (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1 : E â†’ F) p.2 :=
  have A : Continuous fun q : (E â†’L[ğ•œ] F) Ã— E => q.1 q.2 := isBoundedBilinearMap_apply.continuous
  have B : Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1, p.2) :=
    ((h.continuous_fderiv hn).comp continuous_fst).prod_mk continuous_snd
  A.comp B
