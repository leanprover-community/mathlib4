/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Analytic.Within
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Calculus.ContDiff.FTaylorSeries

/-!
# Higher differentiability

A function is `C^1` on a domain if it is differentiable there, and its derivative is continuous.
By induction, it is `C^n` if it is `C^{n-1}` and its (n-1)-th derivative is `C^1` there or,
equivalently, if it is `C^1` and its derivative is `C^{n-1}`.
It is `C^âˆ` if it is `C^n` for all n.
Finally, it is `C^Ï‰` if it is analytic (as well as all its derivative, which is automatic if the
space is complete).

We formalize these notions with predicates `ContDiffWithinAt`, `ContDiffAt`, `ContDiffOn` and
`ContDiff` saying that the function is `C^n` within a set at a point, at a point, on a set
and on the whole space respectively.

To avoid the issue of choice when choosing a derivative in sets where the derivative is not
necessarily unique, `ContDiffOn` is not defined directly in terms of the
regularity of the specific choice `iteratedFDerivWithin ğ•œ n f s` inside `s`, but in terms of the
existence of a nice sequence of derivatives, expressed with a predicate
`HasFTaylorSeriesUpToOn` defined in the file `FTaylorSeries`.

We prove basic properties of these notions.

## Main definitions and results
Let `f : E â†’ F` be a map between normed vector spaces over a nontrivially normed field `ğ•œ`.

* `ContDiff ğ•œ n f`: expresses that `f` is `C^n`, i.e., it admits a Taylor series up to
  rank `n`.
* `ContDiffOn ğ•œ n f s`: expresses that `f` is `C^n` in `s`.
* `ContDiffAt ğ•œ n f x`: expresses that `f` is `C^n` around `x`.
* `ContDiffWithinAt ğ•œ n f s x`: expresses that `f` is `C^n` around `x` within the set `s`.

In sets of unique differentiability, `ContDiffOn ğ•œ n f s` can be expressed in terms of the
properties of `iteratedFDerivWithin ğ•œ m f s` for `m â‰¤ n`. In the whole space,
`ContDiff ğ•œ n f` can be expressed in terms of the properties of `iteratedFDeriv ğ•œ m f`
for `m â‰¤ n`.

## Implementation notes

The definitions in this file are designed to work on any field `ğ•œ`. They are sometimes slightly more
complicated than the naive definitions one would guess from the intuition over the real or complex
numbers, but they are designed to circumvent the lack of gluing properties and partitions of unity
in general. In the usual situations, they coincide with the usual definitions.

### Definition of `C^n` functions in domains

One could define `C^n` functions in a domain `s` by fixing an arbitrary choice of derivatives (this
is what we do with `iteratedFDerivWithin`) and requiring that all these derivatives up to `n` are
continuous. If the derivative is not unique, this could lead to strange behavior like two `C^n`
functions `f` and `g` on `s` whose sum is not `C^n`. A better definition is thus to say that a
function is `C^n` inside `s` if it admits a sequence of derivatives up to `n` inside `s`.

This definition still has the problem that a function which is locally `C^n` would not need to
be `C^n`, as different choices of sequences of derivatives around different points might possibly
not be glued together to give a globally defined sequence of derivatives. (Note that this issue
can not happen over reals, thanks to partition of unity, but the behavior over a general field is
not so clear, and we want a definition for general fields). Also, there are locality
problems for the order parameter: one could image a function which, for each `n`, has a nice
sequence of derivatives up to order `n`, but they do not coincide for varying `n` and can therefore
not be glued to give rise to an infinite sequence of derivatives. This would give a function
which is `C^n` for all `n`, but not `C^âˆ`. We solve this issue by putting locality conditions
in space and order in our definition of `ContDiffWithinAt` and `ContDiffOn`.
The resulting definition is slightly more complicated to work with (in fact not so much), but it
gives rise to completely satisfactory theorems.

For instance, with this definition, a real function which is `C^m` (but not better) on `(-1/m, 1/m)`
for each natural `m` is by definition `C^âˆ` at `0`.

There is another issue with the definition of `ContDiffWithinAt ğ•œ n f s x`. We can
require the existence and good behavior of derivatives up to order `n` on a neighborhood of `x`
within `s`. However, this does not imply continuity or differentiability within `s` of the function
at `x` when `x` does not belong to `s`. Therefore, we require such existence and good behavior on
a neighborhood of `x` within `s âˆª {x}` (which appears as `insert x s` in this file).

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

In this file, we denote `(âŠ¤ : â„•âˆ) : WithTop â„•âˆ` with `âˆ`, and `âŠ¤ : WithTop â„•âˆ` with `Ï‰`. To
avoid ambiguities with the two tops, the theorems name use either `infty` or `omega`.
These notations are scoped in `ContDiff`.

## Tags

derivative, differentiability, higher derivative, `C^n`, multilinear, Taylor series, formal series
-/

noncomputable section

open Set Fin Filter Function
open scoped NNReal Topology ContDiff

universe u uE uF uG uX

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type uE} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type uF} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type uG}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] {X : Type uX} [NormedAddCommGroup X] [NormedSpace ğ•œ X]
  {s sâ‚ t u : Set E} {f fâ‚ : E â†’ F} {g : F â†’ G} {x xâ‚€ : E} {c : F} {m n : WithTop â„•âˆ}
  {p : E â†’ FormalMultilinearSeries ğ•œ E F}

/-! ### Smooth functions within a set around a point -/

variable (ğ•œ) in
/-- A function is continuously differentiable up to order `n` within a set `s` at a point `x` if
it admits continuous derivatives up to order `n` in a neighborhood of `x` in `s âˆª {x}`.
For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).
For `n = Ï‰`, we require the function to be analytic within `s` at `x`. The precise definition we
give (all the derivatives should be analytic) is more involved to work around issues when the space
is not complete, but it is equivalent when the space is complete.

For instance, a real function which is `C^m` on `(-1/m, 1/m)` for each natural `m`, but not
better, is `C^âˆ` at `0` within `univ`.
-/
@[fun_prop]
def ContDiffWithinAt (n : WithTop â„•âˆ) (f : E â†’ F) (s : Set E) (x : E) : Prop :=
  match n with
  | Ï‰ => âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F,
      HasFTaylorSeriesUpToOn Ï‰ f p u âˆ§ âˆ€ i, AnalyticOn ğ•œ (fun x â†¦ p x i) u
  | (n : â„•âˆ) => âˆ€ m : â„•, m â‰¤ n â†’ âˆƒ u âˆˆ ğ“[insert x s] x,
      âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn m f p u

lemma HasFTaylorSeriesUpToOn.analyticOn
    (hf : HasFTaylorSeriesUpToOn Ï‰ f p s) (h : AnalyticOn ğ•œ (fun x â†¦ p x 0) s) :
    AnalyticOn ğ•œ f s := by
  have : AnalyticOn ğ•œ (fun x â†¦ (continuousMultilinearCurryFin0 ğ•œ E F) (p x 0)) s :=
    (LinearIsometryEquiv.analyticOnNhd _ _ ).comp_analyticOn
      h (Set.mapsTo_univ _ _)
  exact this.congr (fun y hy â†¦ (hf.zero_eq _ hy).symm)

lemma ContDiffWithinAt.analyticOn (h : ContDiffWithinAt ğ•œ Ï‰ f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, AnalyticOn ğ•œ f u := by
  obtain âŸ¨u, hu, p, hp, h'pâŸ© := h
  exact âŸ¨u, hu, hp.analyticOn (h'p 0)âŸ©

lemma ContDiffWithinAt.analyticWithinAt (h : ContDiffWithinAt ğ•œ Ï‰ f s x) :
    AnalyticWithinAt ğ•œ f s x := by
  obtain âŸ¨u, hu, hfâŸ© := h.analyticOn
  have xu : x âˆˆ u := mem_of_mem_nhdsWithin (by simp) hu
  exact (hf x xu).mono_of_mem_nhdsWithin (nhdsWithin_mono _ (subset_insert _ _) hu)

theorem contDiffWithinAt_omega_iff_analyticWithinAt [CompleteSpace F] :
    ContDiffWithinAt ğ•œ Ï‰ f s x â†” AnalyticWithinAt ğ•œ f s x := by
  refine âŸ¨fun h â†¦ h.analyticWithinAt, fun h â†¦ ?_âŸ©
  obtain âŸ¨u, hu, p, hp, h'pâŸ© := h.exists_hasFTaylorSeriesUpToOn Ï‰
  exact âŸ¨u, hu, p, hp.of_le le_top, fun i â†¦ h'p iâŸ©

theorem contDiffWithinAt_nat {n : â„•} :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x,
      âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn n f p u :=
  âŸ¨fun H => H n le_rfl, fun âŸ¨u, hu, p, hpâŸ© _m hm => âŸ¨u, hu, p, hp.of_le (mod_cast hm)âŸ©âŸ©

/-- When `n` is either a natural number or `Ï‰`, one can characterize the property of being `C^n`
as the existence of a neighborhood on which there is a Taylor series up to order `n`,
requiring in addition that its terms are analytic in the `Ï‰` case. -/
lemma contDiffWithinAt_iff_of_ne_infty (hn : n â‰  âˆ) :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x,
      âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpToOn n f p u âˆ§
        (n = Ï‰ â†’ âˆ€ i, AnalyticOn ğ•œ (fun x â†¦ p x i) u) := by
  match n with
  | Ï‰ => simp [ContDiffWithinAt]
  | âˆ => simp at hn
  | (n : â„•) => simp [contDiffWithinAt_nat]

@[fun_prop]
theorem ContDiffWithinAt.of_le (h : ContDiffWithinAt ğ•œ n f s x) (hmn : m â‰¤ n) :
    ContDiffWithinAt ğ•œ m f s x := by
  match n with
  | Ï‰ => match m with
    | Ï‰ => exact h
    | (m : â„•âˆ) =>
      intro k _
      obtain âŸ¨u, hu, p, hp, -âŸ© := h
      exact âŸ¨u, hu, p, hp.of_le le_topâŸ©
  | (n : â„•âˆ) => match m with
    | Ï‰ => simp at hmn
    | (m : â„•âˆ) => exact fun k hk â†¦ h k (le_trans hk (mod_cast hmn))

/-- In a complete space, a function which is analytic within a set at a point is also `C^Ï‰` there.
Note that the same statement for `AnalyticOn` does not require completeness, see
`AnalyticOn.contDiffOn`. -/
theorem AnalyticWithinAt.contDiffWithinAt [CompleteSpace F] (h : AnalyticWithinAt ğ•œ f s x) :
    ContDiffWithinAt ğ•œ n f s x :=
  (contDiffWithinAt_omega_iff_analyticWithinAt.2 h).of_le le_top

theorem contDiffWithinAt_iff_forall_nat_le {n : â„•âˆ} :
    ContDiffWithinAt ğ•œ n f s x â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffWithinAt ğ•œ m f s x :=
  âŸ¨fun H _ hm => H.of_le (mod_cast hm), fun H m hm => H m hm _ le_rflâŸ©

theorem contDiffWithinAt_infty :
    ContDiffWithinAt ğ•œ âˆ f s x â†” âˆ€ n : â„•, ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_iff_forall_nat_le.trans <| by simp only [forall_prop_of_true, le_top]

theorem ContDiffWithinAt.continuousWithinAt (h : ContDiffWithinAt ğ•œ n f s x) :
    ContinuousWithinAt f s x := by
  have := h.of_le (zero_le _)
  simp only [ContDiffWithinAt, nonpos_iff_eq_zero, Nat.cast_eq_zero, forall_eq, CharP.cast_eq_zero]
    at this
  rcases this with âŸ¨u, hu, p, HâŸ©
  rw [mem_nhdsWithin_insert] at hu
  exact (H.continuousOn.continuousWithinAt hu.1).mono_of_mem_nhdsWithin hu.2

theorem ContDiffWithinAt.congr_of_eventuallyEq (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, H, H'âŸ© := h
    exact âŸ¨{x âˆˆ u | fâ‚ x = f x}, Filter.inter_mem hu (mem_nhdsWithin_insert.2 âŸ¨hx, hâ‚âŸ©), p,
      (H.mono (sep_subset _ _)).congr fun _ â†¦ And.right,
      fun i â†¦ (H' i).mono (sep_subset _ _)âŸ©
  | (n : â„•âˆ) =>
    intro m hm
    let âŸ¨u, hu, p, HâŸ© := h m hm
    exact âŸ¨{ x âˆˆ u | fâ‚ x = f x }, Filter.inter_mem hu (mem_nhdsWithin_insert.2 âŸ¨hx, hâ‚âŸ©), p,
      (H.mono (sep_subset _ _)).congr fun _ â†¦ And.rightâŸ©

theorem Filter.EventuallyEq.congr_contDiffWithinAt (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq hâ‚.symm hx.symm, fun H â†¦ H.congr_of_eventuallyEq hâ‚ hxâŸ©

theorem ContDiffWithinAt.congr_of_eventuallyEq_insert (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (nhdsWithin_mono x (subset_insert x s) hâ‚)
    (mem_of_mem_nhdsWithin (mem_insert x s) hâ‚ :)

theorem Filter.EventuallyEq.congr_contDiffWithinAt_of_insert (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq_insert hâ‚.symm, fun H â†¦ H.congr_of_eventuallyEq_insert hâ‚âŸ©

theorem ContDiffWithinAt.congr_of_eventuallyEq_of_mem (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq hâ‚ <| hâ‚.self_of_nhdsWithin hx

theorem Filter.EventuallyEq.congr_contDiffWithinAt_of_mem (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun H â†¦ H.congr_of_eventuallyEq_of_mem hâ‚.symm hx, fun H â†¦ H.congr_of_eventuallyEq_of_mem hâ‚ hxâŸ©

theorem ContDiffWithinAt.congr (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : fâ‚ x = f x) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr_of_eventuallyEq (Filter.eventuallyEq_of_mem self_mem_nhdsWithin hâ‚) hx

theorem contDiffWithinAt_congr (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  âŸ¨fun h' â†¦ h'.congr (fun x hx â†¦ (hâ‚ x hx).symm) hx.symm, fun h' â†¦  h'.congr hâ‚ hxâŸ©

theorem ContDiffWithinAt.congr_of_mem (h : ContDiffWithinAt ğ•œ n f s x) (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y)
    (hx : x âˆˆ s) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr hâ‚ (hâ‚ _ hx)

theorem contDiffWithinAt_congr_of_mem (hâ‚ : âˆ€ y âˆˆ s, fâ‚ y = f y) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr hâ‚ (hâ‚ x hx)

theorem ContDiffWithinAt.congr_of_insert (h : ContDiffWithinAt ğ•œ n f s x)
    (hâ‚ : âˆ€ y âˆˆ insert x s, fâ‚ y = f y) : ContDiffWithinAt ğ•œ n fâ‚ s x :=
  h.congr (fun y hy â†¦ hâ‚ y (mem_insert_of_mem _ hy)) (hâ‚ x (mem_insert _ _))

theorem contDiffWithinAt_congr_of_insert (hâ‚ : âˆ€ y âˆˆ insert x s, fâ‚ y = f y) :
    ContDiffWithinAt ğ•œ n fâ‚ s x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr (fun y hy â†¦ hâ‚ y (mem_insert_of_mem _ hy)) (hâ‚ x (mem_insert _ _))

theorem ContDiffWithinAt.mono_of_mem_nhdsWithin (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : s âˆˆ ğ“[t] x) : ContDiffWithinAt ğ•œ n f t x := by
  match n with
  | Ï‰ =>
    obtain âŸ¨u, hu, p, H, H'âŸ© := h
    exact âŸ¨u, nhdsWithin_le_of_mem (insert_mem_nhdsWithin_insert hst) hu, p, H, H'âŸ©
  | (n : â„•âˆ) =>
    intro m hm
    rcases h m hm with âŸ¨u, hu, p, HâŸ©
    exact âŸ¨u, nhdsWithin_le_of_mem (insert_mem_nhdsWithin_insert hst) hu, p, HâŸ©

theorem ContDiffWithinAt.mono (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E} (hst : t âŠ† s) :
    ContDiffWithinAt ğ•œ n f t x :=
  h.mono_of_mem_nhdsWithin <| Filter.mem_of_superset self_mem_nhdsWithin hst

theorem ContDiffWithinAt.congr_mono
    (h : ContDiffWithinAt ğ•œ n f s x) (h' : EqOn fâ‚ f sâ‚) (hâ‚ : sâ‚ âŠ† s) (hx : fâ‚ x = f x) :
    ContDiffWithinAt ğ•œ n fâ‚ sâ‚ x :=
  (h.mono hâ‚).congr h' hx

theorem ContDiffWithinAt.congr_set (h : ContDiffWithinAt ğ•œ n f s x) {t : Set E}
    (hst : s =á¶ [ğ“ x] t) : ContDiffWithinAt ğ•œ n f t x := by
  rw [â† nhdsWithin_eq_iff_eventuallyEq] at hst
  apply h.mono_of_mem_nhdsWithin <| hst â–¸ self_mem_nhdsWithin

theorem contDiffWithinAt_congr_set {t : Set E} (hst : s =á¶ [ğ“ x] t) :
    ContDiffWithinAt ğ•œ n f s x â†” ContDiffWithinAt ğ•œ n f t x :=
  âŸ¨fun h => h.congr_set hst, fun h => h.congr_set hst.symmâŸ©

theorem contDiffWithinAt_inter' (h : t âˆˆ ğ“[s] x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_congr_set (mem_nhdsWithin_iff_eventuallyEq.1 h).symm

theorem contDiffWithinAt_inter (h : t âˆˆ ğ“ x) :
    ContDiffWithinAt ğ•œ n f (s âˆ© t) x â†” ContDiffWithinAt ğ•œ n f s x :=
  contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds h)

theorem contDiffWithinAt_insert_self :
    ContDiffWithinAt ğ•œ n f (insert x s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  match n with
  | Ï‰ => simp [ContDiffWithinAt]
  | (n : â„•âˆ) => simp_rw [ContDiffWithinAt, insert_idem]

theorem contDiffWithinAt_insert {y : E} :
    ContDiffWithinAt ğ•œ n f (insert y s) x â†” ContDiffWithinAt ğ•œ n f s x := by
  rcases eq_or_ne x y with (rfl | hx)
  Â· exact contDiffWithinAt_insert_self
  refine âŸ¨fun h â†¦ h.mono (subset_insert _ _), fun h â†¦ ?_âŸ©
  apply h.mono_of_mem_nhdsWithin
  simp [nhdsWithin_insert_of_ne hx, self_mem_nhdsWithin]

alias âŸ¨ContDiffWithinAt.of_insert, ContDiffWithinAt.insert'âŸ© := contDiffWithinAt_insert

protected theorem ContDiffWithinAt.insert (h : ContDiffWithinAt ğ•œ n f s x) :
    ContDiffWithinAt ğ•œ n f (insert x s) x :=
  h.insert'

theorem contDiffWithinAt_diff_singleton {y : E} :
    ContDiffWithinAt ğ•œ n f (s \ {y}) x â†” ContDiffWithinAt ğ•œ n f s x := by
  rw [â† contDiffWithinAt_insert, insert_diff_singleton, contDiffWithinAt_insert]

/-- If a function is `C^n` within a set at a point, with `n â‰¥ 1`, then it is differentiable
within this set at this point. -/
theorem ContDiffWithinAt.differentiableWithinAt' (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f (insert x s) x := by
  rcases contDiffWithinAt_nat.1 (h.of_le hn) with âŸ¨u, hu, p, HâŸ©
  rcases mem_nhdsWithin.1 hu with âŸ¨t, t_open, xt, tuâŸ©
  rw [inter_comm] at tu
  exact (differentiableWithinAt_inter (IsOpen.mem_nhds t_open xt)).1 <|
    ((H.mono tu).differentiableOn le_rfl) x âŸ¨mem_insert x s, xtâŸ©

theorem ContDiffWithinAt.differentiableWithinAt (h : ContDiffWithinAt ğ•œ n f s x) (hn : 1 â‰¤ n) :
    DifferentiableWithinAt ğ•œ f s x :=
  (h.differentiableWithinAt' hn).mono (subset_insert x s)

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`
(and moreover the function is analytic when `n = Ï‰`). -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt (hn : n â‰  âˆ) :
    ContDiffWithinAt ğ•œ (n + 1) f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, (n = Ï‰ â†’ AnalyticOn ğ•œ f u) âˆ§
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
      (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffWithinAt ğ•œ n f' u x := by
  have h'n : n + 1 â‰  âˆ := by simpa using hn
  constructor
  Â· intro h
    rcases (contDiffWithinAt_iff_of_ne_infty h'n).1 h with âŸ¨u, hu, p, Hp, H'pâŸ©
    refine âŸ¨u, hu, ?_, fun y => (continuousMultilinearCurryFin1 ğ•œ E F) (p y 1),
        fun y hy => Hp.hasFDerivWithinAt le_add_self hy, ?_âŸ©
    Â· rintro rfl
      exact Hp.analyticOn (H'p rfl 0)
    apply (contDiffWithinAt_iff_of_ne_infty hn).2
    refine âŸ¨u, ?_, fun y : E => (p y).shift, ?_âŸ©
    Â· convert @self_mem_nhdsWithin _ _ x u
      have : x âˆˆ insert x s := by simp
      exact insert_eq_of_mem (mem_of_mem_nhdsWithin this hu)
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right] at Hp
      refine âŸ¨Hp.2.2, ?_âŸ©
      rintro rfl i
      change AnalyticOn ğ•œ
        (fun x â†¦ (continuousMultilinearCurryRightEquiv' ğ•œ i E F) (p x (i + 1))) u
      apply (LinearIsometryEquiv.analyticOnNhd _ _).comp_analyticOn
        ?_ (Set.mapsTo_univ _ _)
      exact H'p rfl _
  Â· rintro âŸ¨u, hu, hf, f', f'_eq_deriv, Hf'âŸ©
    rw [contDiffWithinAt_iff_of_ne_infty h'n]
    rcases (contDiffWithinAt_iff_of_ne_infty hn).1 Hf' with âŸ¨v, hv, p', Hp', p'_anâŸ©
    refine âŸ¨v âˆ© u, ?_, fun x => (p' x).unshift (f x), ?_, ?_âŸ©
    Â· apply Filter.inter_mem _ hu
      apply nhdsWithin_le_of_mem hu
      exact nhdsWithin_mono _ (subset_insert x u) hv
    Â· rw [hasFTaylorSeriesUpToOn_succ_iff_right]
      refine âŸ¨fun y _ => rfl, fun y hy => ?_, ?_âŸ©
      Â· change
          HasFDerivWithinAt (fun z => (continuousMultilinearCurryFin0 ğ•œ E F).symm (f z))
            (FormalMultilinearSeries.unshift (p' y) (f y) 1).curryLeft (v âˆ© u) y
        rw [â† Function.comp_def _ f, LinearIsometryEquiv.comp_hasFDerivWithinAt_iff']
        convert (f'_eq_deriv y hy.2).mono inter_subset_right
        rw [â† Hp'.zero_eq y hy.1]
        ext z
        change ((p' y 0) (init (@cons 0 (fun _ => E) z 0))) (@cons 0 (fun _ => E) z 0 (last 0)) =
          ((p' y 0) 0) z
        congr
        norm_num [eq_iff_true_of_subsingleton]
      Â· convert (Hp'.mono inter_subset_left).congr fun x hx => Hp'.zero_eq x hx.1 using 1
        Â· ext x y
          change p' x 0 (init (@snoc 0 (fun _ : Fin 1 => E) 0 y)) y = p' x 0 0 y
          rw [init_snoc]
        Â· ext x k v y
          change p' x k (init (@snoc k (fun _ : Fin k.succ => E) v y))
            (@snoc k (fun _ : Fin k.succ => E) v y (last k)) = p' x k v y
          rw [snoc_last, init_snoc]
    Â· intro h i
      simp only [WithTop.add_eq_top, WithTop.one_ne_top, or_false] at h
      match i with
      | 0 =>
        simp only [FormalMultilinearSeries.unshift]
        apply AnalyticOnNhd.comp_analyticOn _ ((hf h).mono inter_subset_right)
          (Set.mapsTo_univ _ _)
        exact LinearIsometryEquiv.analyticOnNhd _ _
      | i + 1 =>
        simp only [FormalMultilinearSeries.unshift, Nat.succ_eq_add_one]
        apply AnalyticOnNhd.comp_analyticOn _ ((p'_an h i).mono inter_subset_left)
          (Set.mapsTo_univ _ _)
        exact LinearIsometryEquiv.analyticOnNhd _ _

/-- A version of `contDiffWithinAt_succ_iff_hasFDerivWithinAt` where all derivatives
  are taken within the same set. -/
theorem contDiffWithinAt_succ_iff_hasFDerivWithinAt' (hn : n â‰  âˆ) :
    ContDiffWithinAt ğ•œ (n + 1) f s x â†”
      âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ f u) âˆ§
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) s x) âˆ§ ContDiffWithinAt ğ•œ n f' s x := by
  refine âŸ¨fun hf => ?_, ?_âŸ©
  Â· obtain âŸ¨u, hu, f_an, f', huf', hf'âŸ© := (contDiffWithinAt_succ_iff_hasFDerivWithinAt hn).mp hf
    obtain âŸ¨w, hw, hxw, hwuâŸ© := mem_nhdsWithin.mp hu
    rw [inter_comm] at hwu
    refine âŸ¨insert x s âˆ© w, inter_mem_nhdsWithin _ (hw.mem_nhds hxw), inter_subset_left, ?_, f',
      fun y hy => ?_, ?_âŸ©
    Â· intro h
      apply (f_an h).mono hwu
    Â· refine ((huf' y <| hwu hy).mono hwu).mono_of_mem_nhdsWithin ?_
      refine mem_of_superset ?_ (inter_subset_inter_left _ (subset_insert _ _))
      exact inter_mem_nhdsWithin _ (hw.mem_nhds hy.2)
    Â· exact hf'.mono_of_mem_nhdsWithin (nhdsWithin_mono _ (subset_insert _ _) hu)
  Â· rw [â† contDiffWithinAt_insert, contDiffWithinAt_succ_iff_hasFDerivWithinAt hn,
      insert_eq_of_mem (mem_insert _ _)]
    rintro âŸ¨u, hu, hus, f_an, f', huf', hf'âŸ©
    exact âŸ¨u, hu, f_an, f', fun y hy => (huf' y hy).insert'.mono hus, hf'.insert.mono husâŸ©


/-! ### Smooth functions within a set -/

variable (ğ•œ) in
/-- A function is continuously differentiable up to `n` on `s` if, for any point `x` in `s`, it
admits continuous derivatives up to order `n` on a neighborhood of `x` in `s`.

For `n = âˆ`, we only require that this holds up to any finite order (where the neighborhood may
depend on the finite order we consider).
-/
@[fun_prop]
def ContDiffOn (n : WithTop â„•âˆ) (f : E â†’ F) (s : Set E) : Prop :=
  âˆ€ x âˆˆ s, ContDiffWithinAt ğ•œ n f s x

theorem HasFTaylorSeriesUpToOn.contDiffOn {n : â„•âˆ} {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpToOn n f f' s) : ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  use s
  simp only [Set.insert_eq_of_mem hx, self_mem_nhdsWithin, true_and]
  exact âŸ¨f', hf.of_le (mod_cast hm)âŸ©

theorem ContDiffOn.contDiffWithinAt (h : ContDiffOn ğ•œ n f s) (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ n f s x :=
  h x hx

@[fun_prop]
theorem ContDiffOn.of_le (h : ContDiffOn ğ•œ n f s) (hmn : m â‰¤ n) : ContDiffOn ğ•œ m f s := fun x hx =>
  (h x hx).of_le hmn

theorem ContDiffWithinAt.contDiffOn' (hm : m â‰¤ n) (h' : m = âˆ â†’ n = Ï‰)
    (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ m f (insert x s âˆ© u) := by
  rcases eq_or_ne n Ï‰ with rfl | hn
  Â· obtain âŸ¨t, ht, p, hp, h'pâŸ© := h
    rcases mem_nhdsWithin.1 ht with âŸ¨u, huo, hxu, hutâŸ©
    rw [inter_comm] at hut
    refine âŸ¨u, huo, hxu, ?_âŸ©
    suffices ContDiffOn ğ•œ Ï‰ f (insert x s âˆ© u) from this.of_le le_top
    intro y hy
    refine âŸ¨insert x s âˆ© u, ?_, p, hp.mono hut, fun i â†¦ (h'p i).mono hutâŸ©
    simp only [insert_eq_of_mem, hy, self_mem_nhdsWithin]
  Â· match m with
    | Ï‰ => simp [hn] at hm
    | âˆ => exact (hn (h' rfl)).elim
    | (m : â„•) =>
      rcases contDiffWithinAt_nat.1 (h.of_le hm) with âŸ¨t, ht, p, hpâŸ©
      rcases mem_nhdsWithin.1 ht with âŸ¨u, huo, hxu, hutâŸ©
      rw [inter_comm] at hut
      exact âŸ¨u, huo, hxu, (hp.mono hut).contDiffOnâŸ©

theorem ContDiffWithinAt.contDiffOn (hm : m â‰¤ n) (h' : m = âˆ â†’ n = Ï‰)
    (h : ContDiffWithinAt ğ•œ n f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ ContDiffOn ğ•œ m f u := by
  obtain âŸ¨_u, uo, xu, hâŸ© := h.contDiffOn' hm h'
  exact âŸ¨_, inter_mem_nhdsWithin _ (uo.mem_nhds xu), inter_subset_left, hâŸ©

theorem ContDiffOn.analyticOn (h : ContDiffOn ğ•œ Ï‰ f s) : AnalyticOn ğ•œ f s :=
  fun x hx â†¦ (h x hx).analyticWithinAt

/-- A function is `C^n` within a set at a point, for `n : â„•`, if and only if it is `C^n` on
a neighborhood of this point. -/
theorem contDiffWithinAt_iff_contDiffOn_nhds (hn : n â‰  âˆ) :
    ContDiffWithinAt ğ•œ n f s x â†” âˆƒ u âˆˆ ğ“[insert x s] x, ContDiffOn ğ•œ n f u := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rcases h.contDiffOn le_rfl (by simp [hn]) with âŸ¨u, hu, h'uâŸ©
    exact âŸ¨u, hu, h'u.2âŸ©
  Â· rcases h with âŸ¨u, u_mem, huâŸ©
    have : x âˆˆ u := mem_of_mem_nhdsWithin (mem_insert x s) u_mem
    exact (hu x this).mono_of_mem_nhdsWithin (nhdsWithin_mono _ (subset_insert x s) u_mem)

protected theorem ContDiffWithinAt.eventually (h : ContDiffWithinAt ğ•œ n f s x) (hn : n â‰  âˆ) :
    âˆ€á¶  y in ğ“[insert x s] x, ContDiffWithinAt ğ•œ n f s y := by
  rcases h.contDiffOn le_rfl (by simp [hn]) with âŸ¨u, hu, _, hdâŸ©
  have : âˆ€á¶  y : E in ğ“[insert x s] x, u âˆˆ ğ“[insert x s] y âˆ§ y âˆˆ u :=
    (eventually_eventually_nhdsWithin.2 hu).and hu
  refine this.mono fun y hy => (hd y hy.2).mono_of_mem_nhdsWithin ?_
  exact nhdsWithin_mono y (subset_insert _ _) hy.1

theorem ContDiffOn.of_succ (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ n f s :=
  h.of_le le_self_add

theorem ContDiffOn.one_of_succ (h : ContDiffOn ğ•œ (n + 1) f s) : ContDiffOn ğ•œ 1 f s :=
  h.of_le le_add_self

theorem contDiffOn_iff_forall_nat_le {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiffOn ğ•œ m f s :=
  âŸ¨fun H _ hm => H.of_le (mod_cast hm), fun H x hx m hm => H m hm x hx m le_rflâŸ©

theorem contDiffOn_infty : ContDiffOn ğ•œ âˆ f s â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s :=
  contDiffOn_iff_forall_nat_le.trans <| by simp only [le_top, forall_prop_of_true]

theorem contDiffOn_all_iff_nat :
    (âˆ€ (n : â„•âˆ), ContDiffOn ğ•œ n f s) â†” âˆ€ n : â„•, ContDiffOn ğ•œ n f s := by
  refine âŸ¨fun H n => H n, ?_âŸ©
  rintro H (_ | n)
  exacts [contDiffOn_infty.2 H, H n]

theorem ContDiffOn.continuousOn (h : ContDiffOn ğ•œ n f s) : ContinuousOn f s := fun x hx =>
  (h x hx).continuousWithinAt

@[fun_prop]
theorem ContDiffOn.continuousOn_zero (h : ContDiffOn ğ•œ 0 f s) : ContinuousOn f s := fun x hx =>
  (h x hx).continuousWithinAt

theorem ContDiffOn.congr (h : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) :
    ContDiffOn ğ•œ n fâ‚ s := fun x hx => (h x hx).congr hâ‚ (hâ‚ x hx)

theorem contDiffOn_congr (hâ‚ : âˆ€ x âˆˆ s, fâ‚ x = f x) : ContDiffOn ğ•œ n fâ‚ s â†” ContDiffOn ğ•œ n f s :=
  âŸ¨fun H => H.congr fun x hx => (hâ‚ x hx).symm, fun H => H.congr hâ‚âŸ©

theorem ContDiffOn.mono (h : ContDiffOn ğ•œ n f s) {t : Set E} (hst : t âŠ† s) : ContDiffOn ğ•œ n f t :=
  fun x hx => (h x (hst hx)).mono hst

theorem ContDiffOn.congr_mono (hf : ContDiffOn ğ•œ n f s) (hâ‚ : âˆ€ x âˆˆ sâ‚, fâ‚ x = f x) (hs : sâ‚ âŠ† s) :
    ContDiffOn ğ•œ n fâ‚ sâ‚ :=
  (hf.mono hs).congr hâ‚

/-- If a function is `C^n` on a set with `n â‰¥ 1`, then it is differentiable there. -/
theorem ContDiffOn.differentiableOn (h : ContDiffOn ğ•œ n f s) (hn : 1 â‰¤ n) :
    DifferentiableOn ğ•œ f s := fun x hx => (h x hx).differentiableWithinAt hn

@[fun_prop]
theorem ContDiffOn.differentiableOn_one (h : ContDiffOn ğ•œ 1 f s) :
    DifferentiableOn ğ•œ f s := fun x hx => (h x hx).differentiableWithinAt (le_refl 1)

/-- If a function is `C^n` around each point in a set, then it is `C^n` on the set. -/
theorem contDiffOn_of_locally_contDiffOn
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ ContDiffOn ğ•œ n f (s âˆ© u)) : ContDiffOn ğ•œ n f s := by
  intro x xs
  rcases h x xs with âŸ¨u, u_open, xu, huâŸ©
  apply (contDiffWithinAt_inter _).1 (hu x âŸ¨xs, xuâŸ©)
  exact IsOpen.mem_nhds u_open xu

/-- A function is `C^(n + 1)` on a domain iff locally, it has a derivative which is `C^n`. -/
theorem contDiffOn_succ_iff_hasFDerivWithinAt (hn : n â‰  âˆ) :
    ContDiffOn ğ•œ (n + 1) f s â†”
      âˆ€ x âˆˆ s, âˆƒ u âˆˆ ğ“[insert x s] x, (n = Ï‰ â†’ AnalyticOn ğ•œ f u) âˆ§ âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
        (âˆ€ x âˆˆ u, HasFDerivWithinAt f (f' x) u x) âˆ§ ContDiffOn ğ•œ n f' u := by
  constructor
  Â· intro h x hx
    rcases (contDiffWithinAt_succ_iff_hasFDerivWithinAt hn).1 (h x hx) with
      âŸ¨u, hu, f_an, f', hf', Hf'âŸ©
    rcases Hf'.contDiffOn le_rfl (by simp [hn]) with âŸ¨v, vu, v'u, hvâŸ©
    rw [insert_eq_of_mem hx] at hu âŠ¢
    have xu : x âˆˆ u := mem_of_mem_nhdsWithin hx hu
    rw [insert_eq_of_mem xu] at vu v'u
    exact âŸ¨v, nhdsWithin_le_of_mem hu vu, fun h â†¦ (f_an h).mono v'u, f',
      fun y hy â†¦ (hf' y (v'u hy)).mono v'u, hvâŸ©
  Â· intro h x hx
    rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt hn]
    rcases h x hx with âŸ¨u, u_nhbd, f_an, f', hu, hf'âŸ©
    have : x âˆˆ u := mem_of_mem_nhdsWithin (mem_insert _ _) u_nhbd
    exact âŸ¨u, u_nhbd, f_an, f', hu, hf' x thisâŸ©


/-! ### Iterated derivative within a set -/

@[simp]
theorem contDiffOn_zero : ContDiffOn ğ•œ 0 f s â†” ContinuousOn f s := by
  refine âŸ¨fun H => H.continuousOn, fun H => fun x hx m hm â†¦ ?_âŸ©
  have : (m : WithTop â„•âˆ) = 0 := le_antisymm (mod_cast hm) bot_le
  rw [this]
  refine âŸ¨insert x s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  rw [hasFTaylorSeriesUpToOn_zero_iff]
  exact âŸ¨by rwa [insert_eq_of_mem hx], fun x _ => by simp [ftaylorSeriesWithin]âŸ©

theorem contDiffWithinAt_zero (hx : x âˆˆ s) :
    ContDiffWithinAt ğ•œ 0 f s x â†” âˆƒ u âˆˆ ğ“[s] x, ContinuousOn f (s âˆ© u) := by
  constructor
  Â· intro h
    obtain âŸ¨u, H, p, hpâŸ© := h 0 le_rfl
    refine âŸ¨u, ?_, ?_âŸ©
    Â· simpa [hx] using H
    Â· simp only [Nat.cast_zero, hasFTaylorSeriesUpToOn_zero_iff] at hp
      exact hp.1.mono inter_subset_right
  Â· rintro âŸ¨u, H, huâŸ©
    rw [â† contDiffWithinAt_inter' H]
    have h' : x âˆˆ s âˆ© u := âŸ¨hx, mem_of_mem_nhdsWithin hx HâŸ©
    exact (contDiffOn_zero.mpr hu).contDiffWithinAt h'

/-- When a function is `C^n` in a set `s` of unique differentiability, it admits
`ftaylorSeriesWithin ğ•œ f s` as a Taylor series up to order `n` in `s`. -/
protected theorem ContDiffOn.ftaylorSeriesWithin
    (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s) :
    HasFTaylorSeriesUpToOn n f (ftaylorSeriesWithin ğ•œ f s) s := by
  constructor
  Â· intro x _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.curry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro m hm x hx
    have : (m + 1 : â„•) â‰¤ n := ENat.add_one_natCast_le_withTop_of_lt hm
    rcases (h x hx).of_le this _ le_rfl with âŸ¨u, hu, p, HpâŸ©
    rw [insert_eq_of_mem hx] at hu
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [inter_comm] at ho
    have : p x m.succ = ftaylorSeriesWithin ğ•œ f s x m.succ := by
      change p x m.succ = iteratedFDerivWithin ğ•œ m.succ f s x
      rw [â† iteratedFDerivWithin_inter_open o_open xo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hx, xoâŸ©
    rw [â† this, â† hasFDerivWithinAt_inter (IsOpen.mem_nhds o_open xo)]
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact
        (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn (mod_cast Nat.le_succ m)
          (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact
      ((Hp.mono ho).fderivWithin m (mod_cast lt_add_one m) x âŸ¨hx, xoâŸ©).congr
        (fun y hy => (A y hy).symm) (A x âŸ¨hx, xoâŸ©).symm
  Â· intro m hm
    apply continuousOn_of_locally_continuousOn
    intro x hx
    rcases (h x hx).of_le hm _ le_rfl with âŸ¨u, hu, p, HpâŸ©
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [insert_eq_of_mem hx] at ho
    rw [inter_comm] at ho
    refine âŸ¨o, o_open, xo, ?_âŸ©
    have A : âˆ€ y âˆˆ s âˆ© o, p y m = ftaylorSeriesWithin ğ•œ f s y m := by
      rintro y âŸ¨hy, yoâŸ©
      change p y m = iteratedFDerivWithin ğ•œ m f s y
      rw [â† iteratedFDerivWithin_inter_open o_open yo]
      exact (Hp.mono ho).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl (hs.inter o_open) âŸ¨hy, yoâŸ©
    exact ((Hp.mono ho).cont m le_rfl).congr fun y hy => (A y hy).symm

theorem iteratedFDerivWithin_subset {n : â„•} (st : s âŠ† t) (hs : UniqueDiffOn ğ•œ s)
    (ht : UniqueDiffOn ğ•œ t) (h : ContDiffOn ğ•œ n f t) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ n f s x = iteratedFDerivWithin ğ•œ n f t x :=
  (((h.ftaylorSeriesWithin ht).mono st).eq_iteratedFDerivWithin_of_uniqueDiffOn le_rfl hs hx).symm

theorem ContDiffWithinAt.eventually_hasFTaylorSeriesUpToOn {f : E â†’ F} {s : Set E} {a : E}
    (h : ContDiffWithinAt ğ•œ n f s a) (hs : UniqueDiffOn ğ•œ s) (ha : a âˆˆ s) {m : â„•} (hm : m â‰¤ n) :
    âˆ€á¶  t in (ğ“[s] a).smallSets, HasFTaylorSeriesUpToOn m f (ftaylorSeriesWithin ğ•œ f s) t := by
  rcases h.contDiffOn' hm (by simp) with âŸ¨U, hUo, haU, hfUâŸ©
  have : âˆ€á¶  t in (ğ“[s] a).smallSets, t âŠ† s âˆ© U := by
    rw [eventually_smallSets_subset]
    exact inter_mem_nhdsWithin _ <| hUo.mem_nhds haU
  refine this.mono fun t ht â†¦ .mono ?_ ht
  rw [insert_eq_of_mem ha] at hfU
  refine (hfU.ftaylorSeriesWithin (hs.inter hUo)).congr_series fun k hk x hx â†¦ ?_
  exact iteratedFDerivWithin_inter_open hUo hx.2

/-- On a set with unique differentiability, an analytic function is automatically `C^Ï‰`, as its
successive derivatives are also analytic. This does not require completeness of the space. See
also `AnalyticOn.contDiffOn_of_completeSpace`. -/
theorem AnalyticOn.contDiffOn (h : AnalyticOn ğ•œ f s) (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s := by
  suffices ContDiffOn ğ•œ Ï‰ f s from this.of_le le_top
  rcases h.exists_hasFTaylorSeriesUpToOn hs with âŸ¨p, hpâŸ©
  intro x hx
  refine âŸ¨s, ?_, p, hpâŸ©
  rw [insert_eq_of_mem hx]
  exact self_mem_nhdsWithin

/-- On a set with unique differentiability, an analytic function is automatically `C^Ï‰`, as its
successive derivatives are also analytic. This does not require completeness of the space. See
also `AnalyticOnNhd.contDiffOn_of_completeSpace`. -/
theorem AnalyticOnNhd.contDiffOn (h : AnalyticOnNhd ğ•œ f s) (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s := h.analyticOn.contDiffOn hs

/-- An analytic function is automatically `C^Ï‰` in a complete space -/
theorem AnalyticOn.contDiffOn_of_completeSpace [CompleteSpace F] (h : AnalyticOn ğ•œ f s) :
    ContDiffOn ğ•œ n f s :=
  fun x hx â†¦ (h x hx).contDiffWithinAt

/-- An analytic function is automatically `C^Ï‰` in a complete space -/
theorem AnalyticOnNhd.contDiffOn_of_completeSpace [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) :
    ContDiffOn ğ•œ n f s :=
  h.analyticOn.contDiffOn_of_completeSpace

theorem contDiffOn_of_continuousOn_differentiableOn {n : â„•âˆ}
    (Hcont : âˆ€ m : â„•, m â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s)
    (Hdiff : âˆ€ m : â„•, m < n â†’
      DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s) :
    ContDiffOn ğ•œ n f s := by
  intro x hx m hm
  rw [insert_eq_of_mem hx]
  refine âŸ¨s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_âŸ©
  constructor
  Â· intro y _
    simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.curry0_apply,
      iteratedFDerivWithin_zero_apply]
  Â· intro k hk y hy
    convert (Hdiff k (lt_of_lt_of_le (mod_cast hk) (mod_cast hm)) y hy).hasFDerivWithinAt
  Â· intro k hk
    exact Hcont k (le_trans (mod_cast hk) (mod_cast hm))

theorem contDiffOn_of_differentiableOn {n : â„•âˆ}
    (h : âˆ€ m : â„•, m â‰¤ n â†’ DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s) :
    ContDiffOn ğ•œ n f s :=
  contDiffOn_of_continuousOn_differentiableOn (fun m hm => (h m hm).continuousOn) fun m hm =>
    h m (le_of_lt hm)

theorem contDiffOn_of_analyticOn_iteratedFDerivWithin
    (h : âˆ€ m, AnalyticOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s) :
    ContDiffOn ğ•œ n f s := by
  suffices ContDiffOn ğ•œ Ï‰ f s from this.of_le le_top
  intro x hx
  refine âŸ¨insert x s, self_mem_nhdsWithin, ftaylorSeriesWithin ğ•œ f s, ?_, ?_âŸ©
  Â· rw [insert_eq_of_mem hx]
    constructor
    Â· intro y _
      simp only [ftaylorSeriesWithin, ContinuousMultilinearMap.curry0_apply,
        iteratedFDerivWithin_zero_apply]
    Â· intro k _ y hy
      exact ((h k).differentiableOn y hy).hasFDerivWithinAt
    Â· intro k _
      exact (h k).continuousOn
  Â· intro i
    rw [insert_eq_of_mem hx]
    exact h i

theorem contDiffOn_omega_iff_analyticOn (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ Ï‰ f s â†” AnalyticOn ğ•œ f s :=
  âŸ¨fun h m â†¦ h.analyticOn m, fun h â†¦ h.contDiffOn hsâŸ©

theorem ContDiffOn.continuousOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : m â‰¤ n) (hs : UniqueDiffOn ğ•œ s) : ContinuousOn (iteratedFDerivWithin ğ•œ m f s) s :=
  ((h.of_le hmn).ftaylorSeriesWithin hs).cont m le_rfl

theorem ContDiffOn.differentiableOn_iteratedFDerivWithin {m : â„•} (h : ContDiffOn ğ•œ n f s)
    (hmn : m < n) (hs : UniqueDiffOn ğ•œ s) :
    DifferentiableOn ğ•œ (iteratedFDerivWithin ğ•œ m f s) s := by
  intro x hx
  have : (m + 1 : â„•) â‰¤ n := ENat.add_one_natCast_le_withTop_of_lt hmn
  apply (((h.of_le this).ftaylorSeriesWithin hs).fderivWithin m ?_ x hx).differentiableWithinAt
  exact_mod_cast lt_add_one m

theorem ContDiffWithinAt.differentiableWithinAt_iteratedFDerivWithin {m : â„•}
    (h : ContDiffWithinAt ğ•œ n f s x) (hmn : m < n) (hs : UniqueDiffOn ğ•œ (insert x s)) :
    DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f s) s x := by
  have : (m + 1 : WithTop â„•âˆ) â‰  âˆ := Ne.symm (ne_of_beq_false rfl)
  rcases h.contDiffOn' (ENat.add_one_natCast_le_withTop_of_lt hmn) (by simp [this])
    with âŸ¨u, uo, xu, huâŸ©
  set t := insert x s âˆ© u
  have A : t =á¶ [ğ“[â‰ ] x] s := by
    simp only [set_eventuallyEq_iff_inf_principal, â† nhdsWithin_inter']
    rw [â† inter_assoc, nhdsWithin_inter_of_mem', â† diff_eq_compl_inter, insert_diff_of_mem,
      diff_eq_compl_inter]
    exacts [rfl, mem_nhdsWithin_of_mem_nhds (uo.mem_nhds xu)]
  have B : iteratedFDerivWithin ğ•œ m f s =á¶ [ğ“ x] iteratedFDerivWithin ğ•œ m f t :=
    iteratedFDerivWithin_eventually_congr_set' _ A.symm _
  have C : DifferentiableWithinAt ğ•œ (iteratedFDerivWithin ğ•œ m f t) t x :=
    hu.differentiableOn_iteratedFDerivWithin (Nat.cast_lt.2 m.lt_succ_self) (hs.inter uo) x
      âŸ¨mem_insert _ _, xuâŸ©
  rw [differentiableWithinAt_congr_set' _ A] at C
  exact C.congr_of_eventuallyEq (B.filter_mono inf_le_left) B.self_of_nhds

theorem contDiffOn_iff_continuousOn_differentiableOn {n : â„•âˆ} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†”
      (âˆ€ m : â„•, m â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s) âˆ§
        âˆ€ m : â„•, m < n â†’ DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s :=
  âŸ¨fun h => âŸ¨fun _m hm => h.continuousOn_iteratedFDerivWithin (mod_cast hm) hs,
      fun _m hm => h.differentiableOn_iteratedFDerivWithin (mod_cast hm) hsâŸ©,
    fun h => contDiffOn_of_continuousOn_differentiableOn h.1 h.2âŸ©

theorem contDiffOn_nat_iff_continuousOn_differentiableOn {n : â„•} (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ n f s â†”
      (âˆ€ m : â„•, m â‰¤ n â†’ ContinuousOn (fun x => iteratedFDerivWithin ğ•œ m f s x) s) âˆ§
        âˆ€ m : â„•, m < n â†’ DifferentiableOn ğ•œ (fun x => iteratedFDerivWithin ğ•œ m f s x) s := by
  rw [â† WithTop.coe_natCast, contDiffOn_iff_continuousOn_differentiableOn hs]
  simp

theorem contDiffOn_succ_of_fderivWithin (hf : DifferentiableOn ğ•œ f s)
    (h' : n = Ï‰ â†’ AnalyticOn ğ•œ f s)
    (h : ContDiffOn ğ•œ n (fun y => fderivWithin ğ•œ f s y) s) : ContDiffOn ğ•œ (n + 1) f s := by
  rcases eq_or_ne n âˆ with rfl | hn
  Â· rw [ENat.coe_top_add_one, contDiffOn_infty]
    intro m x hx
    apply ContDiffWithinAt.of_le _ (show (m : WithTop â„•âˆ) â‰¤ m + 1 from le_self_add)
    rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt (by simp),
      insert_eq_of_mem hx]
    exact âŸ¨s, self_mem_nhdsWithin, (by simp), fderivWithin ğ•œ f s,
      fun y hy => (hf y hy).hasFDerivWithinAt, (h x hx).of_le (mod_cast le_top)âŸ©
  Â· intro x hx
    rw [contDiffWithinAt_succ_iff_hasFDerivWithinAt hn,
      insert_eq_of_mem hx]
    exact âŸ¨s, self_mem_nhdsWithin, h', fderivWithin ğ•œ f s,
      fun y hy => (hf y hy).hasFDerivWithinAt, h x hxâŸ©

theorem contDiffOn_of_analyticOn_of_fderivWithin (hf : AnalyticOn ğ•œ f s)
    (h : ContDiffOn ğ•œ Ï‰ (fun y â†¦ fderivWithin ğ•œ f s y) s) : ContDiffOn ğ•œ n f s := by
  suffices ContDiffOn ğ•œ (Ï‰ + 1) f s from this.of_le le_top
  exact contDiffOn_succ_of_fderivWithin hf.differentiableOn (fun _ â†¦ hf) h

/-- A function is `C^(n + 1)` on a domain with unique derivatives if and only if it is
differentiable there, and its derivative (expressed with `fderivWithin`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderivWithin (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ f s) âˆ§
      ContDiffOn ğ•œ n (fderivWithin ğ•œ f s) s := by
  refine âŸ¨fun H => ?_, fun h => contDiffOn_succ_of_fderivWithin h.1 h.2.1 h.2.2âŸ©
  refine âŸ¨H.differentiableOn le_add_self, ?_, fun x hx => ?_âŸ©
  Â· rintro rfl
    exact H.analyticOn
  have A (m : â„•) (hm : m â‰¤ n) : ContDiffWithinAt ğ•œ m (fun y => fderivWithin ğ•œ f s y) s x := by
    rcases (contDiffWithinAt_succ_iff_hasFDerivWithinAt (n := m) (ne_of_beq_false rfl)).1
      (H.of_le (add_le_add_right hm 1) x hx) with âŸ¨u, hu, -, f', hff', hf'âŸ©
    rcases mem_nhdsWithin.1 hu with âŸ¨o, o_open, xo, hoâŸ©
    rw [inter_comm, insert_eq_of_mem hx] at ho
    have := hf'.mono ho
    rw [contDiffWithinAt_inter' (mem_nhdsWithin_of_mem_nhds (IsOpen.mem_nhds o_open xo))] at this
    apply this.congr_of_eventuallyEq_of_mem _ hx
    have : o âˆ© s âˆˆ ğ“[s] x := mem_nhdsWithin.2 âŸ¨o, o_open, xo, Subset.refl _âŸ©
    rw [inter_comm] at this
    refine Filter.eventuallyEq_of_mem this fun y hy => ?_
    have A : fderivWithin ğ•œ f (s âˆ© o) y = f' y :=
      ((hff' y (ho hy)).mono ho).fderivWithin (hs.inter o_open y hy)
    rwa [fderivWithin_inter (o_open.mem_nhds hy.2)] at A
  match n with
  | Ï‰ => exact (H.analyticOn.fderivWithin hs).contDiffOn hs (n := Ï‰) x hx
  | âˆ => exact contDiffWithinAt_infty.2 (fun m â†¦ A m (mod_cast le_top))
  | (n : â„•) => exact A n le_rfl

theorem contDiffOn_succ_iff_hasFDerivWithinAt_of_uniqueDiffOn (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ (n + 1) f s â†” (n = Ï‰ â†’ AnalyticOn ğ•œ f s) âˆ§
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiffOn ğ•œ n f' s âˆ§ âˆ€ x, x âˆˆ s â†’ HasFDerivWithinAt f (f' x) s x := by
  rw [contDiffOn_succ_iff_fderivWithin hs]
  refine âŸ¨fun h => âŸ¨h.2.1, fderivWithin ğ•œ f s, h.2.2,
    fun x hx => (h.1 x hx).hasFDerivWithinAtâŸ©, fun âŸ¨f_an, hâŸ© => ?_âŸ©
  rcases h with âŸ¨f', h1, h2âŸ©
  refine âŸ¨fun x hx => (h2 x hx).differentiableWithinAt, f_an, fun x hx => ?_âŸ©
  exact (h1 x hx).congr_of_mem (fun y hy => (h2 y hy).fderivWithin (hs y hy)) hx

theorem contDiffOn_infty_iff_fderivWithin (hs : UniqueDiffOn ğ•œ s) :
    ContDiffOn ğ•œ âˆ f s â†” DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fderivWithin ğ•œ f s) s := by
  rw [â† ENat.coe_top_add_one, contDiffOn_succ_iff_fderivWithin hs]
  simp

/-- A function is `C^(n + 1)` on an open domain if and only if it is
differentiable there, and its derivative (expressed with `fderiv`) is `C^n`. -/
theorem contDiffOn_succ_iff_fderiv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ğ•œ (n + 1) f s â†”
      DifferentiableOn ğ•œ f s âˆ§ (n = Ï‰ â†’ AnalyticOn ğ•œ f s) âˆ§
      ContDiffOn ğ•œ n (fderiv ğ•œ f) s := by
  rw [contDiffOn_succ_iff_fderivWithin hs.uniqueDiffOn,
    contDiffOn_congr fun x hx â†¦ fderivWithin_of_isOpen hs hx]

theorem contDiffOn_infty_iff_fderiv_of_isOpen (hs : IsOpen s) :
    ContDiffOn ğ•œ âˆ f s â†” DifferentiableOn ğ•œ f s âˆ§ ContDiffOn ğ•œ âˆ (fderiv ğ•œ f) s := by
  rw [â† ENat.coe_top_add_one, contDiffOn_succ_iff_fderiv_of_isOpen hs]
  simp

protected theorem ContDiffOn.fderivWithin (hf : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hmn : m + 1 â‰¤ n) : ContDiffOn ğ•œ m (fderivWithin ğ•œ f s) s :=
  ((contDiffOn_succ_iff_fderivWithin hs).1 (hf.of_le hmn)).2.2

theorem ContDiffOn.fderiv_of_isOpen (hf : ContDiffOn ğ•œ n f s) (hs : IsOpen s) (hmn : m + 1 â‰¤ n) :
    ContDiffOn ğ•œ m (fderiv ğ•œ f) s :=
  (hf.fderivWithin hs.uniqueDiffOn hmn).congr fun _ hx => (fderivWithin_of_isOpen hs hx).symm

theorem ContDiffOn.continuousOn_fderivWithin (h : ContDiffOn ğ•œ n f s) (hs : UniqueDiffOn ğ•œ s)
    (hn : 1 â‰¤ n) : ContinuousOn (fderivWithin ğ•œ f s) s :=
  ((contDiffOn_succ_iff_fderivWithin hs).1
    (h.of_le (show 0 + (1 : WithTop â„•âˆ) â‰¤ n from hn))).2.2.continuousOn

theorem ContDiffOn.continuousOn_fderiv_of_isOpen (h : ContDiffOn ğ•œ n f s) (hs : IsOpen s)
    (hn : 1 â‰¤ n) : ContinuousOn (fderiv ğ•œ f) s :=
  ((contDiffOn_succ_iff_fderiv_of_isOpen hs).1
    (h.of_le (show 0 + (1 : WithTop â„•âˆ) â‰¤ n from hn))).2.2.continuousOn

/-! ### Smooth functions at a point -/

variable (ğ•œ) in
/-- A function is continuously differentiable up to `n` at a point `x` if, for any integer `k â‰¤ n`,
there is a neighborhood of `x` where `f` admits derivatives up to order `n`, which are continuous.
-/
@[fun_prop]
def ContDiffAt (n : WithTop â„•âˆ) (f : E â†’ F) (x : E) : Prop :=
  ContDiffWithinAt ğ•œ n f univ x

theorem contDiffWithinAt_univ : ContDiffWithinAt ğ•œ n f univ x â†” ContDiffAt ğ•œ n f x :=
  Iff.rfl

theorem contDiffAt_infty : ContDiffAt ğ•œ âˆ f x â†” âˆ€ n : â„•, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffWithinAt_univ, contDiffWithinAt_infty]

@[fun_prop]
theorem ContDiffAt.contDiffWithinAt (h : ContDiffAt ğ•œ n f x) : ContDiffWithinAt ğ•œ n f s x :=
  h.mono (subset_univ _)

@[fun_prop]
theorem ContDiffWithinAt.contDiffAt (h : ContDiffWithinAt ğ•œ n f s x) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x := by rwa [ContDiffAt, â† contDiffWithinAt_inter hx, univ_inter]

theorem contDiffWithinAt_iff_contDiffAt (h : s âˆˆ ğ“ x) :
    ContDiffWithinAt ğ•œ n f s x â†” ContDiffAt ğ•œ n f x := by
  rw [â† univ_inter s, contDiffWithinAt_inter h, contDiffWithinAt_univ]

theorem IsOpen.contDiffOn_iff (hs : IsOpen s) :
    ContDiffOn ğ•œ n f s â†” âˆ€ â¦ƒaâ¦„, a âˆˆ s â†’ ContDiffAt ğ•œ n f a :=
  forallâ‚‚_congr fun _ => contDiffWithinAt_iff_contDiffAt âˆ˜ hs.mem_nhds

@[fun_prop]
theorem ContDiffOn.contDiffAt (h : ContDiffOn ğ•œ n f s) (hx : s âˆˆ ğ“ x) :
    ContDiffAt ğ•œ n f x :=
  (h _ (mem_of_mem_nhds hx)).contDiffAt hx

theorem ContDiffAt.congr_of_eventuallyEq (h : ContDiffAt ğ•œ n f x) (hg : fâ‚ =á¶ [ğ“ x] f) :
    ContDiffAt ğ•œ n fâ‚ x :=
  h.congr_of_eventuallyEq_of_mem (by rwa [nhdsWithin_univ]) (mem_univ x)

theorem ContDiffAt.of_le (h : ContDiffAt ğ•œ n f x) (hmn : m â‰¤ n) : ContDiffAt ğ•œ m f x :=
  ContDiffWithinAt.of_le h hmn

@[fun_prop]
theorem ContDiffAt.continuousAt (h : ContDiffAt ğ•œ n f x) : ContinuousAt f x := by
  simpa [continuousWithinAt_univ] using h.continuousWithinAt

theorem ContDiffAt.analyticAt (h : ContDiffAt ğ•œ Ï‰ f x) : AnalyticAt ğ•œ f x := by
  rw [â† contDiffWithinAt_univ] at h
  rw [â† analyticWithinAt_univ]
  exact h.analyticWithinAt

/-- In a complete space, a function which is analytic at a point is also `C^Ï‰` there.
Note that the same statement for `AnalyticOn` does not require completeness, see
`AnalyticOn.contDiffOn`. -/
theorem AnalyticAt.contDiffAt [CompleteSpace F] (h : AnalyticAt ğ•œ f x) :
    ContDiffAt ğ•œ n f x := by
  rw [â† contDiffWithinAt_univ]
  rw [â† analyticWithinAt_univ] at h
  exact h.contDiffWithinAt

@[simp]
theorem contDiffWithinAt_compl_self :
    ContDiffWithinAt ğ•œ n f {x}á¶œ x â†” ContDiffAt ğ•œ n f x := by
  rw [compl_eq_univ_diff, contDiffWithinAt_diff_singleton, contDiffWithinAt_univ]

/-- If a function is `C^n` with `n â‰¥ 1` at a point, then it is differentiable there. -/
theorem ContDiffAt.differentiableAt (h : ContDiffAt ğ•œ n f x) (hn : 1 â‰¤ n) :
    DifferentiableAt ğ•œ f x := by
  simpa [hn, differentiableWithinAt_univ] using h.differentiableWithinAt

theorem ContDiffAt.differentiableAt_iteratedFDeriv
    {f : E â†’ F} {n : WithTop â„•âˆ} {m : â„•} {x : E} (h : ContDiffAt ğ•œ n f x) (hmn : â†‘m < n) :
    DifferentiableAt ğ•œ (iteratedFDeriv ğ•œ m f) x := by
  rw [â† differentiableWithinAt_univ]
  convert (h.differentiableWithinAt_iteratedFDerivWithin hmn (by simp [uniqueDiffOn_univ]))
  exact iteratedFDerivWithin_univ.symm

@[fun_prop]
theorem ContDiffAt.differentiableAt_one (h : ContDiffAt ğ•œ 1 f x) :
    DifferentiableAt ğ•œ f x := by
  simpa [(le_refl 1), differentiableWithinAt_univ] using h.differentiableWithinAt

nonrec lemma ContDiffAt.contDiffOn (h : ContDiffAt ğ•œ n f x) (hm : m â‰¤ n) (h' : m = âˆ â†’ n = Ï‰) :
    âˆƒ u âˆˆ ğ“ x, ContDiffOn ğ•œ m f u := by
  simpa [nhdsWithin_univ] using h.contDiffOn hm h'

/-- A function is `C^(n + 1)` at a point iff locally, it has a derivative which is `C^n`. -/
theorem contDiffAt_succ_iff_hasFDerivAt {n : â„•} :
    ContDiffAt ğ•œ (n + 1) f x â†” âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
      (âˆƒ u âˆˆ ğ“ x, âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x) âˆ§ ContDiffAt ğ•œ n f' x := by
  rw [â† contDiffWithinAt_univ, contDiffWithinAt_succ_iff_hasFDerivWithinAt (by simp)]
  simp only [nhdsWithin_univ, mem_univ, insert_eq_of_mem]
  constructor
  Â· rintro âŸ¨u, H, -, f', h_fderiv, h_cont_diffâŸ©
    rcases mem_nhds_iff.mp H with âŸ¨t, htu, ht, hxtâŸ©
    refine âŸ¨f', âŸ¨t, ?_âŸ©, h_cont_diff.contDiffAt HâŸ©
    refine âŸ¨mem_nhds_iff.mpr âŸ¨t, Subset.rfl, ht, hxtâŸ©, ?_âŸ©
    intro y hyt
    refine (h_fderiv y (htu hyt)).hasFDerivAt ?_
    exact mem_nhds_iff.mpr âŸ¨t, htu, ht, hytâŸ©
  Â· rintro âŸ¨f', âŸ¨u, H, h_fderivâŸ©, h_cont_diffâŸ©
    refine âŸ¨u, H, by simp, f', fun x hxu â†¦ ?_, h_cont_diff.contDiffWithinAtâŸ©
    exact (h_fderiv x hxu).hasFDerivWithinAt

protected theorem ContDiffAt.eventually (h : ContDiffAt ğ•œ n f x) (h' : n â‰  âˆ) :
    âˆ€á¶  y in ğ“ x, ContDiffAt ğ•œ n f y := by
  simpa [nhdsWithin_univ] using ContDiffWithinAt.eventually h h'

theorem iteratedFDerivWithin_eq_iteratedFDeriv {n : â„•}
    (hs : UniqueDiffOn ğ•œ s) (h : ContDiffAt ğ•œ n f x) (hx : x âˆˆ s) :
    iteratedFDerivWithin ğ•œ n f s x = iteratedFDeriv ğ•œ n f x := by
  rw [â† iteratedFDerivWithin_univ]
  rcases h.contDiffOn' le_rfl (by simp) with âŸ¨u, u_open, xu, huâŸ©
  rw [â† iteratedFDerivWithin_inter_open u_open xu,
    â† iteratedFDerivWithin_inter_open u_open xu (s := univ)]
  apply iteratedFDerivWithin_subset
  Â· exact inter_subset_inter_left _ (subset_univ _)
  Â· exact hs.inter u_open
  Â· apply uniqueDiffOn_univ.inter u_open
  Â· simpa using hu
  Â· exact âŸ¨hx, xuâŸ©

/-! ### Smooth functions -/

variable (ğ•œ) in
/-- A function is continuously differentiable up to `n` if it admits derivatives up to
order `n`, which are continuous. Contrary to the case of definitions in domains (where derivatives
might not be unique) we do not need to localize the definition in space or time.
-/
@[fun_prop]
def ContDiff (n : WithTop â„•âˆ) (f : E â†’ F) : Prop :=
  match n with
  | Ï‰ => âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpTo âŠ¤ f p
      âˆ§ âˆ€ i, AnalyticOnNhd ğ•œ (fun x â†¦ p x i) univ
  | (n : â„•âˆ) => âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F, HasFTaylorSeriesUpTo n f p

/-- If `f` has a Taylor series up to `n`, then it is `C^n`. -/
theorem HasFTaylorSeriesUpTo.contDiff {n : â„•âˆ} {f' : E â†’ FormalMultilinearSeries ğ•œ E F}
    (hf : HasFTaylorSeriesUpTo n f f') : ContDiff ğ•œ n f :=
  âŸ¨f', hfâŸ©

theorem contDiffOn_univ : ContDiffOn ğ•œ n f univ â†” ContDiff ğ•œ n f := by
  match n with
  | Ï‰ =>
    constructor
    Â· intro H
      use ftaylorSeriesWithin ğ•œ f univ
      rw [â† hasFTaylorSeriesUpToOn_univ_iff]
      refine âŸ¨H.ftaylorSeriesWithin uniqueDiffOn_univ, fun i â†¦ ?_âŸ©
      rw [â† analyticOn_univ]
      exact H.analyticOn.iteratedFDerivWithin uniqueDiffOn_univ _
    Â· rintro âŸ¨p, hp, h'pâŸ© x _
      exact âŸ¨univ, Filter.univ_sets _, p, (hp.hasFTaylorSeriesUpToOn univ).of_le le_top,
        fun i â†¦ (h'p i).analyticOnâŸ©
  | (n : â„•âˆ) =>
    constructor
    Â· intro H
      use ftaylorSeriesWithin ğ•œ f univ
      rw [â† hasFTaylorSeriesUpToOn_univ_iff]
      exact H.ftaylorSeriesWithin uniqueDiffOn_univ
    Â· rintro âŸ¨p, hpâŸ© x _ m hm
      exact âŸ¨univ, Filter.univ_sets _, p,
        (hp.hasFTaylorSeriesUpToOn univ).of_le (mod_cast hm)âŸ©

theorem contDiff_iff_contDiffAt : ContDiff ğ•œ n f â†” âˆ€ x, ContDiffAt ğ•œ n f x := by
  simp [â† contDiffOn_univ, ContDiffOn, ContDiffAt]

@[fun_prop]
theorem ContDiff.contDiffAt (h : ContDiff ğ•œ n f) : ContDiffAt ğ•œ n f x :=
  contDiff_iff_contDiffAt.1 h x

@[fun_prop]
theorem ContDiff.contDiffWithinAt (h : ContDiff ğ•œ n f) : ContDiffWithinAt ğ•œ n f s x :=
  h.contDiffAt.contDiffWithinAt

theorem contDiff_infty : ContDiff ğ•œ âˆ f â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp [contDiffOn_univ.symm, contDiffOn_infty]

theorem contDiff_all_iff_nat : (âˆ€ n : â„•âˆ, ContDiff ğ•œ n f) â†” âˆ€ n : â„•, ContDiff ğ•œ n f := by
  simp only [â† contDiffOn_univ, contDiffOn_all_iff_nat]

@[fun_prop]
theorem ContDiff.contDiffOn (h : ContDiff ğ•œ n f) : ContDiffOn ğ•œ n f s :=
  (contDiffOn_univ.2 h).mono (subset_univ _)

@[simp]
theorem contDiff_zero : ContDiff ğ•œ 0 f â†” Continuous f := by
  rw [â† contDiffOn_univ, â† continuousOn_univ]
  exact contDiffOn_zero

theorem contDiffAt_zero : ContDiffAt ğ•œ 0 f x â†” âˆƒ u âˆˆ ğ“ x, ContinuousOn f u := by
  rw [â† contDiffWithinAt_univ]; simp [contDiffWithinAt_zero, nhdsWithin_univ]

theorem contDiffAt_one_iff :
    ContDiffAt ğ•œ 1 f x â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, âˆƒ u âˆˆ ğ“ x, ContinuousOn f' u âˆ§ âˆ€ x âˆˆ u, HasFDerivAt f (f' x) x := by
  rw [show (1 : WithTop â„•âˆ) = (0 : â„•) + 1 from rfl]
  simp_rw [contDiffAt_succ_iff_hasFDerivAt, show ((0 : â„•) : WithTop â„•âˆ) = 0 from rfl,
    contDiffAt_zero, exists_mem_and_iff antitone_bforall antitone_continuousOn, and_comm]

@[fun_prop]
theorem ContDiff.of_le (h : ContDiff ğ•œ n f) (hmn : m â‰¤ n) : ContDiff ğ•œ m f :=
  contDiffOn_univ.1 <| (contDiffOn_univ.2 h).of_le hmn

theorem ContDiff.of_succ (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ n f :=
  h.of_le le_self_add

theorem ContDiff.one_of_succ (h : ContDiff ğ•œ (n + 1) f) : ContDiff ğ•œ 1 f := by
  apply h.of_le le_add_self

theorem ContDiff.continuous (h : ContDiff ğ•œ n f) : Continuous f :=
  contDiff_zero.1 (h.of_le bot_le)

@[fun_prop]
theorem ContDiff.continuous_zero (h : ContDiff ğ•œ 0 f) : Continuous f :=
  contDiff_zero.1 (h.of_le bot_le)

/-- If a function is `C^n` with `n â‰¥ 1`, then it is differentiable. -/
theorem ContDiff.differentiable (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) : Differentiable ğ•œ f :=
  differentiableOn_univ.1 <| (contDiffOn_univ.2 h).differentiableOn hn

@[fun_prop]
theorem ContDiff.differentiable_one (h : ContDiff ğ•œ 1 f) : Differentiable ğ•œ f :=
  differentiableOn_univ.1 <| (contDiffOn_univ.2 h).differentiableOn (le_refl 1)

theorem contDiff_iff_forall_nat_le {n : â„•âˆ} :
    ContDiff ğ•œ n f â†” âˆ€ m : â„•, â†‘m â‰¤ n â†’ ContDiff ğ•œ m f := by
  simp_rw [â† contDiffOn_univ]; exact contDiffOn_iff_forall_nat_le

/-- A function is `C^(n+1)` iff it has a `C^n` derivative. -/
theorem contDiff_succ_iff_hasFDerivAt {n : â„•} :
    ContDiff ğ•œ (n + 1) f â†”
      âˆƒ f' : E â†’ E â†’L[ğ•œ] F, ContDiff ğ•œ n f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  simp only [â† contDiffOn_univ, â† hasFDerivWithinAt_univ, Set.mem_univ, forall_true_left,
    contDiffOn_succ_iff_hasFDerivWithinAt_of_uniqueDiffOn uniqueDiffOn_univ,
    WithTop.natCast_ne_top, analyticOn_univ, false_implies, true_and]

theorem contDiff_one_iff_hasFDerivAt : ContDiff ğ•œ 1 f â†”
    âˆƒ f' : E â†’ E â†’L[ğ•œ] F, Continuous f' âˆ§ âˆ€ x, HasFDerivAt f (f' x) x := by
  convert contDiff_succ_iff_hasFDerivAt using 4; simp

theorem AnalyticOn.contDiff (hf : AnalyticOn ğ•œ f univ) : ContDiff ğ•œ n f := by
  rw [â† contDiffOn_univ]
  exact hf.contDiffOn (n := n) uniqueDiffOn_univ

theorem AnalyticOnNhd.contDiff (hf : AnalyticOnNhd ğ•œ f univ) : ContDiff ğ•œ n f :=
  hf.analyticOn.contDiff

theorem ContDiff.analyticOnNhd (h : ContDiff ğ•œ Ï‰ f) : AnalyticOnNhd ğ•œ f s := by
  rw [â† contDiffOn_univ] at h
  have := h.analyticOn
  rw [analyticOn_univ] at this
  exact this.mono (subset_univ _)

theorem contDiff_omega_iff_analyticOnNhd :
    ContDiff ğ•œ Ï‰ f â†” AnalyticOnNhd ğ•œ f univ :=
  âŸ¨fun h â†¦ h.analyticOnNhd, fun h â†¦ h.contDiffâŸ©

/-! ### Iterated derivative -/

/-- When a function is `C^n`, it admits `ftaylorSeries ğ•œ f` as a Taylor series up
to order `n` in `s`. -/
theorem ContDiff.ftaylorSeries (hf : ContDiff ğ•œ n f) :
    HasFTaylorSeriesUpTo n f (ftaylorSeries ğ•œ f) := by
  simp only [â† contDiffOn_univ, â† hasFTaylorSeriesUpToOn_univ_iff, â† ftaylorSeriesWithin_univ]
    at hf âŠ¢
  exact ContDiffOn.ftaylorSeriesWithin hf uniqueDiffOn_univ

/-- For `n : â„•âˆ`, a function is `C^n` iff it admits `ftaylorSeries ğ•œ f`
as a Taylor series up to order `n`. -/
theorem contDiff_iff_ftaylorSeries {n : â„•âˆ} :
    ContDiff ğ•œ n f â†” HasFTaylorSeriesUpTo n f (ftaylorSeries ğ•œ f) := by
  constructor
  Â· rw [â† contDiffOn_univ, â† hasFTaylorSeriesUpToOn_univ_iff, â† ftaylorSeriesWithin_univ]
    exact fun h â†¦ ContDiffOn.ftaylorSeriesWithin h uniqueDiffOn_univ
  Â· exact fun h â†¦ âŸ¨ftaylorSeries ğ•œ f, hâŸ©

theorem contDiff_iff_continuous_differentiable {n : â„•âˆ} :
    ContDiff ğ•œ n f â†”
      (âˆ€ m : â„•, m â‰¤ n â†’ Continuous fun x => iteratedFDeriv ğ•œ m f x) âˆ§
        âˆ€ m : â„•, m < n â†’ Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x := by
  simp [contDiffOn_univ.symm, continuousOn_univ, differentiableOn_univ.symm,
    iteratedFDerivWithin_univ, contDiffOn_iff_continuousOn_differentiableOn uniqueDiffOn_univ]

theorem contDiff_nat_iff_continuous_differentiable {n : â„•} :
    ContDiff ğ•œ n f â†”
      (âˆ€ m : â„•, m â‰¤ n â†’ Continuous fun x => iteratedFDeriv ğ•œ m f x) âˆ§
        âˆ€ m : â„•, m < n â†’ Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x := by
  rw [â† WithTop.coe_natCast, contDiff_iff_continuous_differentiable]
  simp

/-- If `f` is `C^n` then its `m`-times iterated derivative is continuous for `m â‰¤ n`. -/
theorem ContDiff.continuous_iteratedFDeriv {m : â„•} (hm : m â‰¤ n) (hf : ContDiff ğ•œ n f) :
    Continuous fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp (hf.of_le hm)).1 m le_rfl

@[fun_prop]
theorem ContDiff.continuous_iteratedFDeriv' {m : â„•} (hf : ContDiff ğ•œ m f) :
    Continuous fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp hf).1 m le_rfl

/-- If `f` is `C^n` then its `m`-times iterated derivative is differentiable for `m < n`. -/
theorem ContDiff.differentiable_iteratedFDeriv {m : â„•} (hm : m < n) (hf : ContDiff ğ•œ n f) :
    Differentiable ğ•œ fun x => iteratedFDeriv ğ•œ m f x :=
  (contDiff_iff_continuous_differentiable.mp
    (hf.of_le (ENat.add_one_natCast_le_withTop_of_lt hm))).2 m (mod_cast lt_add_one m)

theorem contDiff_of_differentiable_iteratedFDeriv {n : â„•âˆ}
    (h : âˆ€ m : â„•, m â‰¤ n â†’ Differentiable ğ•œ (iteratedFDeriv ğ•œ m f)) : ContDiff ğ•œ n f :=
  contDiff_iff_continuous_differentiable.2
    âŸ¨fun m hm => (h m hm).continuous, fun m hm => h m (le_of_lt hm)âŸ©

/-- A function is `C^(n + 1)` if and only if it is differentiable,
and its derivative (formulated in terms of `fderiv`) is `C^n`. -/
theorem contDiff_succ_iff_fderiv :
    ContDiff ğ•œ (n + 1) f â†” Differentiable ğ•œ f âˆ§ (n = Ï‰ â†’ AnalyticOnNhd ğ•œ f univ) âˆ§
      ContDiff ğ•œ n (fderiv ğ•œ f) := by
  simp only [â† contDiffOn_univ, â† differentiableOn_univ, â† fderivWithin_univ,
    contDiffOn_succ_iff_fderivWithin uniqueDiffOn_univ, analyticOn_univ]

theorem contDiff_one_iff_fderiv :
    ContDiff ğ•œ 1 f â†” Differentiable ğ•œ f âˆ§ Continuous (fderiv ğ•œ f) := by
  rw [â† zero_add 1, contDiff_succ_iff_fderiv]
  simp

theorem contDiff_infty_iff_fderiv :
    ContDiff ğ•œ âˆ f â†” Differentiable ğ•œ f âˆ§ ContDiff ğ•œ âˆ (fderiv ğ•œ f) := by
  rw [â† ENat.coe_top_add_one, contDiff_succ_iff_fderiv]
  simp

theorem ContDiff.continuous_fderiv (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous (fderiv ğ•œ f) :=
  (contDiff_one_iff_fderiv.1 (h.of_le hn)).2

/-- If a function is at least `C^1`, its bundled derivative (mapping `(x, v)` to `Df(x) v`) is
continuous. -/
theorem ContDiff.continuous_fderiv_apply (h : ContDiff ğ•œ n f) (hn : 1 â‰¤ n) :
    Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1 : E â†’ F) p.2 :=
  have A : Continuous fun q : (E â†’L[ğ•œ] F) Ã— E => q.1 q.2 := isBoundedBilinearMap_apply.continuous
  have B : Continuous fun p : E Ã— E => (fderiv ğ•œ f p.1, p.2) :=
    ((h.continuous_fderiv hn).comp continuous_fst).prodMk continuous_snd
  A.comp B
