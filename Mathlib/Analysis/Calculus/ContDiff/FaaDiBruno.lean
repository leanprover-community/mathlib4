/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Data.Finset.Sort
import Mathlib.Data.Fin.VecNotation
import Mathlib.Data.Fintype.Powerset
import Mathlib.Data.Fintype.Sigma
import Mathlib.Data.Set.Function
import Mathlib.Logic.Equiv.Fintype
-- import Mathlib.Analysis.Analytic.Within
-- import Mathlib.Analysis.Calculus.FDeriv.Analytic
-- import Mathlib.Analysis.Calculus.ContDiff.FTaylorSeries

/-!
# Faa di Bruno formula

The Faa di Bruno formula gives the iterated derivative of `g âˆ˜ f` in terms of those of
`g` and `f`. It is expressed in terms of partitions `I` of `{0, ..., n-1}`. For such a
partition, denote by `k` its number of parts, write the parts as `Iâ‚€, ..., Iâ‚–â‚‹â‚` ordered so
that `max Iâ‚€ < ... < max Iâ‚–â‚‹â‚`, and let `iâ‚˜` be the number of elements of `Iâ‚˜`. Then
`D^n (g âˆ˜ f) (x) (vâ‚€, ..., vâ‚™â‚‹â‚) =
  âˆ‘_{I partition of {0, ..., n-1}}
    D^k g (f x) (D^{iâ‚€} f (x) (v_{Iâ‚€}), ..., D^{iâ‚–â‚‹â‚} f (x) (v_{Iâ‚–â‚‹â‚}))`
where by `v_{Iâ‚˜}` we mean the vectors `váµ¢` with indices in `Iâ‚˜`, i.e., the composition of `v`
with the increasing embedding of `Fin iâ‚˜` into `Fin n` with range `Iâ‚˜`.

For instance, for `n = 2`, there are 2 partitions of `{0, 1}`, given by `{0}, {1}` and `{0, 1}`,
and therefore
`D^2(g âˆ˜ f) (x) (vâ‚€, vâ‚) = D^2 g (f x) (Df (x) vâ‚€, Df (x) vâ‚) + Dg (f x) (D^2f (x) (vâ‚€, vâ‚))`.

The formula is straightforward to prove by induction, as differentiating
`D^k g (f x) (D^{iâ‚€} f (x) (v_{Iâ‚€}), ..., D^{iâ‚–â‚‹â‚} f (x) (v_{Iâ‚–â‚‹â‚}))` gives a sum
with `k + 1` terms where one differentiates either `D^k g (f x)`, or one of the `D^{iâ‚˜} f (x)`,
amounting to adding to the partition `I` either a new atom `{-1}` to its left, or extending `Iâ‚˜`
by adding `-1` to it. In this way, one obtains bijectively all partitions of `{-1, ..., n}`,
and the proof can go on (up to relabelling).

The main difficulty is to write things down in a precise language, namely to write
`D^k g (f x) (D^{iâ‚€} f (x) (v_{Iâ‚€}), ..., D^{iâ‚–â‚‹â‚} f (x) (v_{Iâ‚–â‚‹â‚}))` as a continuous multilinear
map of the `váµ¢`. For this, instead of working with partitions of `{0, ..., n-1}` and ordering their
parts, we work with partitions in which the ordering is part of the data -- this is equivalent,
but much more convenient to implement. We call these `OrderedFinpartition n`.

Note that the implementation of `OrderedFinpartition` is very specific to the Faa di Bruno formula:
as testified by the formula above, what matters is really the embedding of the parts in `Fin n`,
and moreover the parts have to be ordered by `max Iâ‚€ < ... < max Iâ‚–â‚‹â‚` for the formula to hold
in the general case where the iterated differential might not be symmetric. The defeqs with respect
to `Fin.cons` are also important when doing the induction. For this reason, we do not expect this
class to be useful beyond the Faa di Bruno formula, which is why it is in this file instead
of a dedicated file in the `Combinatorics` folder.

## Main results

Given `c : OrderedFinpartition n` and two formal multilinear series `q` and `p`, we
define `c.compAlongOrderedFinpartition q p` as an `n`-multilinear map given by the formula above,
i.e., `(vâ‚, ..., vâ‚™) â†¦ qâ‚– (p_{iâ‚} (v_{Iâ‚}), ..., p_{iâ‚–} (v_{Iâ‚–}))`.

Then, we define `q.taylorComp p` as a formal multilinear series whose `n`-th term is
the sum of `c.compAlongOrderedFinpartition q p` over all ordered finpartitions of size `n`.

Finally, we prove in `HasFTaylorSeriesUptoOn.comp` that, if two functions `g` and `f` have Taylor
series up to `n` given by `q` and `p`, then `g âˆ˜ f` also has a Taylor series,
given by `q.taylorComp p`.

## Implementation

A first technical difficulty is to implement the extension process of `OrderedFinpartition`
corresponding to adding a new atom, or appending an atom to an existing part, and defining the
associated increasing parameterizations that show up in the definition
of `compAlongOrderedFinpartition`.

Then, one has to show that the ordered finpartitions thus
obtained give exactly all ordered finpartitions of order `n+1`. For this, we define the inverse
process (shrinking a finpartition of `n+1` by erasing `0`, either as an atom or from the part
that contains it), and we show that these processes are inverse to each other, yielding an
equivalence between `(c : OrderedFinpartition n) Ã— Option (Fin c.length)`
and `OrderedFinpartition (n + 1)`. This equivalence shows up prominently in the inductive proof
of Faa di Bruno formula to identify the sums that show up.
-/

noncomputable section

open Set Fin Function -- Filter

namespace OrderedFinpartition

/-- A part of an ordered finpartition. It is a nonempty finite set in `Fin n`,
but we use sorted tuples instead, so that we can get nice definitional equalities
for the size and the embedding.  -/
@[ext]
structure Part (n : â„•) where
  size : â„•
  size_ne_zero : size â‰  0
  toFun : Fin size â†’ Fin n
  strictMono : StrictMono toFun
  deriving DecidableEq, Repr

namespace Part

variable {m n : â„•}

initialize_simps_projections Part (toFun â†’ apply)

instance size_neZero (p : Part n) : NeZero p.size := âŸ¨p.size_ne_zeroâŸ©

attribute [simp] size_ne_zero

@[simp]
theorem size_pos (p : Part n) : 0 < p.size := Nat.pos_iff_ne_zero.mpr p.size_ne_zero

@[simp]
theorem one_le_size (p : Part n) : 1 â‰¤ p.size := p.size_pos

attribute [coe] toFun
instance : CoeFun (Part n) fun p â†¦ Fin p.size â†’ Fin n where coe := toFun

@[simp]
theorem lt_iff_lt (p : Part n) {i j : Fin p.size} : p i < p j â†” i < j :=
  p.strictMono.lt_iff_lt

@[simp]
theorem le_iff_le (p : Part n) {i j : Fin p.size} : p i â‰¤ p j â†” i â‰¤ j :=
  p.strictMono.le_iff_le

theorem injective (p : Part n) : Injective p := p.strictMono.injective

@[simp]
theorem apply_inj (p : Part n) {i j : Fin p.size} : p i = p j â†” i = j :=
  p.injective.eq_iff

/-- The last (and the greatest) element of a part.
We introduce a definition instead of using `p âŠ¤`
to avoid dependent types. -/
def last (p : Part n) : Fin n := p âŠ¤

@[simp] lemma apply_top (p : Part n) : p âŠ¤ = p.last := rfl

/-!
### Equivalence to nonempty `Finset`s
-/

/-- Range of a `OrderedFinpartition.Part` as a `Finset`. -/
protected def range (p : Part n) : Finset (Fin n) :=
  Finset.univ.map âŸ¨p, p.strictMono.injectiveâŸ©

@[simp]
theorem coe_range (p : Part n) : (p.range : Set (Fin n)) = Set.range p := by
  simp [Part.range]

theorem mem_range (p : Part n) {i : Fin n} : i âˆˆ p.range â†” âˆƒ j, p j = i := by
  simp [Part.range]

@[simp]
theorem card_range (p : Part n) : p.range.card = p.size := by simp [Part.range]

theorem range_nonempty (p : Part n) : p.range.Nonempty := by simp [â† Finset.card_pos]

theorem range_injective : Injective (@Part.range n) := by
  intro pâ‚ pâ‚‚ h
  have hâ‚ : pâ‚.size = pâ‚‚.size := by simpa using congr(Finset.card $h)
  cases pâ‚; cases pâ‚‚
  subst hâ‚
  congr
  rw [â† StrictMono.range_inj â€¹_â€º â€¹_â€º]
  simpa [Part.range, â† Finset.coe_inj] using h

@[simp]
lemma range_inj {pâ‚ pâ‚‚ : Part n} : pâ‚.range = pâ‚‚.range â†” pâ‚ = pâ‚‚ := range_injective.eq_iff

/-- Define a `Part n` from a nonempty `Finset`. -/
@[simps]
def ofFinset (s : Finset (Fin n)) (hs : s.Nonempty) : Part n where
  size := s.card
  size_ne_zero := by simp [hs.ne_empty]
  toFun := s.orderEmbOfFin rfl
  strictMono := OrderEmbedding.strictMono _

@[simp]
theorem range_ofFinset (s : Finset (Fin n)) (hs : s.Nonempty) : (ofFinset s hs).range = s := by
  simp [Part.range, â† Finset.coe_inj]

@[simp]
theorem ofFinset_range (p : Part n) : ofFinset p.range p.range_nonempty = p := by
  simp [â† range_inj]

/-- Equivalence between `Part n` and the set of nonempty finite sets in `Fin n`. -/
@[simps]
def equivFinset : Part n â‰ƒ {s : Finset (Fin n) // s.Nonempty} where
  toFun p := âŸ¨p.range, p.range_nonemptyâŸ©
  invFun s := ofFinset s.1 s.2
  left_inv := ofFinset_range
  right_inv _ := Subtype.eq <| range_ofFinset _ _

/-- Each `Fin n` has finitely many parts. -/
instance : Fintype (Part n) := .ofEquiv _ equivFinset.symm

@[simp]
theorem card_part : Fintype.card (Part n) = 2 ^ n - 1 := by
  simp [Fintype.card_congr equivFinset, Finset.nonempty_iff_ne_empty]

@[simp]
theorem size_le (p : Part n) : p.size â‰¤ n := by simpa using p.range.card_le_univ

theorem pos (p : Part n) : 0 < n := p.size_pos.trans_le p.size_le

theorem neZero (p : Part n) : NeZero n := .of_pos p.pos

/-- There are nonempty parts of `Fin 0`. -/
instance instIsEmpty : IsEmpty (Part 0) where
  false p := p.pos.ne rfl

@[simp]
theorem zero_mem_range (p : Part n) :
    haveI := p.neZero; 0 âˆˆ p.range â†” p 0 = 0 := by
  haveI := p.neZero
  rw [p.mem_range]
  refine âŸ¨fun âŸ¨j, hjâŸ© â†¦ le_antisymm ?_ (Fin.zero_le' _), fun h â†¦ âŸ¨0, hâŸ©âŸ©
  exact hj â–¸ p.strictMono.monotone (Fin.zero_le' _)

theorem apply_ne_zero {p : Part n} :
    haveI := p.neZero; (âˆ€ i, p i â‰  0) â†” p 0 â‰  0 := by
  simp only [ne_eq, â† not_exists, â† mem_range, zero_mem_range]

/-- A part that contains a single element. -/
@[simps]
def atom (i : Fin n) : Part n where
  size := 1
  size_ne_zero := one_ne_zero
  toFun _ := i
  strictMono := Subsingleton.strictMono _

@[simp]
lemma atom_last (i : Fin n) : (atom i).last = i := rfl

@[simp]
theorem atom_range (i : Fin n) : (atom i).range = {i} := by simp [Part.range]

theorem atom_injective : (@atom n).Injective := LeftInverse.injective atom_last

@[simp]
lemma atom_inj {i j : Fin n} : atom i = atom j â†” i = j := atom_injective.eq_iff

@[simp]
theorem range_eq_singleton {p : Part n} {i : Fin n} : p.range = {i} â†” p = atom i :=
  range_injective.eq_iff' <| atom_range i

theorem size_eq_one {p : Part n} : p.size = 1 â†” âˆƒ i, p = atom i := by
  rw [â† card_range, Finset.card_eq_one]
  simp_rw [range_eq_singleton]

theorem one_lt_size_of_eq_of_ne_atom {p : Part n} {i j} (hâ‚ : p i = j) (hâ‚‚ : p â‰  atom j) :
    1 < p.size := by
  rw [p.one_le_size.gt_iff_ne, ne_eq, size_eq_one]
  rintro âŸ¨k, rflâŸ©
  simp_all

@[simp]
lemma last_eq_zero {p : Part n} : haveI := p.neZero; p.last = 0 â†” p = atom 0 := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ h â–¸ rflâŸ©
  suffices âˆ€ i, p i = p.last by
    rw [â† range_eq_singleton, â† p.range_nonempty.subset_singleton_iff]
    simpa [Finset.subset_iff, Part.mem_range, h] using this
  exact fun i â†¦ le_antisymm (p.strictMono.monotone le_top) <| h.symm â–¸ Nat.zero_le _

@[simp]
lemma last_pos {p : Part n} : haveI := p.neZero; 0 < p.last â†” p â‰  atom 0 := by
  haveI := p.neZero
  simp [pos_iff_ne_zero']

/-- If `n â‰  0`, then `atom 0` is the default `Part n`. -/
instance instInhabited [NeZero n] : Inhabited (Part n) := âŸ¨atom 0âŸ©

/-- There is a unique part in `Fin 1`. -/
instance instUnique : Unique (Part 1) where
  uniq p := range_injective <| by simp only [(range_nonempty _).eq_univ]

/-- The part that contains the whole type. -/
@[simps]
def univ (n : â„•) (h : n â‰  0) : Part n where
  size := n
  size_ne_zero := h
  toFun := id
  strictMono := strictMono_id

/-- The embedding as a bundled `OrderEmbedding`. -/
@[simps! (config := .asFn)]
def emb (p : Part n) : Fin p.size â†ªo Fin n :=
  .ofStrictMono p p.strictMono

/-- Map a `Part m` along an order embedding from `Fin m` to `Fin n`.

The two intended applications are:
- `f = Fin.succOrderEmb`;
- `f = q.emb` for `q : Part n` and `p : Part q.size`. -/
@[simps (config := .asFn)]
def map (p : Part m) (f : Fin m â†ªo Fin n) : Part n where
  __ := p
  toFun := f âˆ˜ p
  strictMono := f.strictMono.comp p.strictMono

@[simp]
theorem range_map (p : Part m) (f : Fin m â†ªo Fin n) :
    (p.map f).range = p.range.map f.toEmbedding := by
  ext; simp [Part.mem_range]

@[simp]
theorem map_inj {pâ‚ pâ‚‚ : Part m} {f : Fin m â†ªo Fin n} : pâ‚.map f = pâ‚‚.map f â†” pâ‚ = pâ‚‚ := by
  simp [â† range_inj]

@[simp]
theorem last_map (p : Part m) (f : Fin m â†ªo Fin n) : (p.map f).last = f p.last :=
  rfl

@[simp]
theorem map_atom (f : Fin m â†ªo Fin n) (i : Fin m) : (atom i).map f = atom (f i) := rfl

@[simp]
theorem map_eq_atom {p : Part m} {f : Fin m â†ªo Fin n} {i : Fin n} :
    p.map f = atom i â†” âˆƒ j, f j = i âˆ§ p = atom j := by
  refine âŸ¨fun h â†¦ ?_, fun âŸ¨j, hji, hpjâŸ© â†¦ by simp [*]âŸ©
  obtain âŸ¨j, rflâŸ© : âˆƒ j, p = atom j := by
    rw [â† size_eq_one, â† map_size, h, atom_size]
  use j, by simpa using h

/-- Map all elements of a part to `Fin (n + 1)` using `Fin.succ`,
then prepend zero. -/
@[simps size]
def extendZero (p : Part n) : Part (n + 1) where
  size := p.size + 1
  size_ne_zero := Nat.succ_ne_zero _
  toFun := Fin.cons 0 (.succ âˆ˜ p)
  strictMono := by
    intro i j hlt
    rcases Fin.exists_succ_eq.mpr hlt.ne_bot with âŸ¨j, rflâŸ©
    cases i using Fin.cases with
    | zero => simp
    | succ i => simpa using hlt

@[simp]
theorem extendZero_apply_zero (p : Part n) : p.extendZero 0 = 0 := rfl

@[simp]
theorem extendZero_apply_succ (p : Part n) (i : Fin p.size) : p.extendZero i.succ = (p i).succ := by
  simp [extendZero]

@[simp]
theorem extendZero_last (p : Part n) : p.extendZero.last = p.last.succ := by
  rw [last, last, â† extendZero_apply_succ, Fin.succ_top]

theorem range_extendZero_eq_cons (p : Part n) :
    p.extendZero.range = .cons 0 (p.range.map (Fin.succEmb n)) (by simp [Fin.succ_ne_zero]) := by
  ext
  simp [extendZero, Fin.exists_fin_succ, eq_comm (a := (0 : Fin _)), mem_range]

@[simp]
theorem extendZero_ne_atom (p : Part n) (i : Fin (n + 1)) : p.extendZero â‰  atom i :=
  ne_of_apply_ne size <| by simp

@[simps size, simps (config := .lemmasOnly) apply]
def mapPred (p : Part (n + 1)) (h : p 0 â‰  0) : Part n where
  size := p.size
  size_ne_zero := p.size_ne_zero
  toFun i := (p i).pred <| apply_ne_zero.2 h i
  strictMono := Fin.strictMono_pred_comp _ p.strictMono

@[simp]
lemma mapPred_inj {pâ‚ pâ‚‚ : Part (n + 1)} {hâ‚ hâ‚‚} :
    pâ‚.mapPred hâ‚ = pâ‚‚.mapPred hâ‚‚ â†” pâ‚ = pâ‚‚ := by
  simp +contextual [Part.ext_iff, Fin.heq_fun_iff, mapPred_apply]

@[simp]
theorem mapPred_last (p : Part (n + 1)) (h : p 0 â‰  0) :
    (p.mapPred h).last = p.last.pred (apply_ne_zero.2 h âŠ¤) :=
  rfl

theorem mapPred_range_eq_preimage (p : Part (n + 1)) (h : p 0 â‰  0) :
    (p.mapPred h).range = p.range.preimage Fin.succ (succ_injective _).injOn := by
  ext
  simp [Part.mem_range, pred_eq_iff_eq_succ, mapPred_apply]

@[simp]
theorem mapPred_map_succ (p : Part n) :
    (p.map (succOrderEmb n)).mapPred (Fin.succ_ne_zero _) = p := by
  cases p
  simp [map, mapPred]

@[simp]
theorem map_succ_mapPred (p : Part (n + 1)) (h : p 0 â‰  0) :
    (p.mapPred h).map (succOrderEmb n) = p := by
  rw [â† mapPred_inj, mapPred_map_succ]

@[simps size, simps (config := .lemmasOnly) apply]
def eraseZero [NeZero n] (p : Part n) (hâ‚ : p 0 = 0) (hâ‚‚ : p â‰  atom 0) : Part n where
  size := p.size - 1
  size_ne_zero := (Nat.sub_pos_of_lt <| one_lt_size_of_eq_of_ne_atom hâ‚ hâ‚‚).ne'
  toFun i := p <| i.succ.cast <| Nat.sub_add_cancel p.one_le_size
  strictMono i j hlt := by simpa [Fin.cast_lt_cast]

@[simp]
lemma eraseZero_last [NeZero n] (p : Part n) (hâ‚ : p 0 = 0) (hâ‚‚ : p â‰  atom 0) :
    (p.eraseZero hâ‚ hâ‚‚).last = p.last := by
  rw [Part.last, eraseZero_apply]
  simp

lemma eraseZero_ne_zero [NeZero n] (p : Part n) (hâ‚ : p 0 = 0) (hâ‚‚ : p â‰  atom 0)
    (i : Fin (p.size - 1)) : p.eraseZero hâ‚ hâ‚‚ i â‰  0 :=
  hâ‚ â–¸ (p.strictMono <| Nat.succ_pos i).ne'

@[simp]
lemma eraseZero_range [NeZero n] (p : Part n) (hâ‚ : p 0 = 0) (hâ‚‚ : p â‰  atom 0) :
    (p.eraseZero hâ‚ hâ‚‚).range = p.range.erase 0 := by
  ext i
  by_cases hi : i = 0 <;> simp [Part.mem_range, eraseZero_apply, hi, p.injective.eq_iff' hâ‚,
    (finCongr <| Nat.sub_add_cancel p.one_le_size).surjective.exists, exists_fin_succ, hâ‚, Ne.symm]

def preimageSucc (p : Part (n + 1)) (h : p â‰  atom 0) : Part n :=
  if hâ‚€ : p 0 = 0 then (p.eraseZero hâ‚€ h).mapPred (p.eraseZero_ne_zero _ _ _) else p.mapPred hâ‚€

@[simp]
theorem preimageSucc_last (p : Part (n + 1)) (h : p â‰  atom 0) :
    (p.preimageSucc h).last = p.last.pred (by simpa) := by
  unfold preimageSucc
  split_ifs <;> simp

@[simp]
theorem preimageSucc_range (p : Part (n + 1)) (h : p â‰  atom 0) :
    (p.preimageSucc h).range = p.range.preimage succ (succ_injective _).injOn := by
  simp [â† Finset.coe_inj, preimageSucc, apply_dite Part.range, mapPred_range_eq_preimage,
    Set.disjoint_left, succ_ne_zero]

@[simp]
lemma preimageSucc_extendZero (p : Part n) :
    p.extendZero.preimageSucc (extendZero_ne_atom _ _) = p := by
  simp [Part.ext_iff, extendZero, preimageSucc, mapPred, eraseZero]

@[simp]
lemma preimageSucc_map_succ (p : Part n) :
    (p.map (succOrderEmb n)).preimageSucc (by simp [succ_ne_zero]) = p := by
  simp [Part.ext_iff, preimageSucc, succ_ne_zero]

lemma extendZero_preimageSucc (p : Part (n + 1)) (hâ‚ : p â‰  atom 0) (hâ‚‚ : p 0 = 0) :
    (p.preimageSucc hâ‚).extendZero = p := by
  simp [preimageSucc, hâ‚‚, Part.ext_iff, Fin.heq_fun_iff, forall_fin_succ, mapPred_apply,
    eraseZero_apply, Fin.cast]

lemma map_succ_preimageSucc (p : Part (n + 1)) (h : p 0 â‰  0) :
    (p.preimageSucc <| ne_of_apply_ne (toFun Â· 0) h).map (succOrderEmb n) = p := by
  simp [Part.ext_iff, preimageSucc, h]

end Part

end OrderedFinpartition

/-- A partition of `Fin n` into finitely many nonempty subsets, given by the increasing
parameterization of these subsets. We order the subsets by increasing greatest element.
This definition is tailored-made for the Faa di Bruno formula, and probably not useful elsewhere,
because of the specific parameterization by `Fin n` and the peculiar ordering. -/
@[ext]
structure OrderedFinpartition (n : â„•) where
  /-- The number of parts in the partition -/
  length : â„•
  /-- The size of each part -/
  part : Fin length â†’ OrderedFinpartition.Part n
  /-- The parts are ordered by increasing greatest element. -/
  part_last_strictMono : StrictMono fun m â†¦ (part m).last
  /-- The parts are disjoint -/
  disjoint : Pairwise (Disjoint on fun m â†¦ (part m).range)
  /-- The parts cover everything -/
  cover x : âˆƒ m, x âˆˆ (part m).range
  deriving DecidableEq

namespace OrderedFinpartition

/-! ### Basic API for ordered finpartitions -/

/-- The ordered finpartition of `Fin n` into singletons. -/
@[simps] def atomic (n : â„•) : OrderedFinpartition n where
  length := n
  part i :=  .atom i
  part_last_strictMono := strictMono_id
  disjoint _ _ h := by simp [h.symm]
  cover m := by simp

variable {n : â„•} (c : OrderedFinpartition n)

instance : Inhabited (OrderedFinpartition n) := âŸ¨atomic nâŸ©

@[simp]
lemma part_last_inj {i j : Fin c.length} : (c.part i).last = (c.part j).last â†” i = j :=
  c.part_last_strictMono.injective.eq_iff

@[simp]
lemma part_last_lt_part_last {i j : Fin c.length} : (c.part i).last < (c.part j).last â†” i < j :=
  c.part_last_strictMono.lt_iff_lt

@[simp]
lemma part_last_le_part_last {i j : Fin c.length} : (c.part i).last â‰¤ (c.part j).last â†” i â‰¤ j :=
  c.part_last_strictMono.le_iff_le

lemma length_le : c.length â‰¤ n := by
  simpa only [Fintype.card_fin]
    using Fintype.card_le_of_injective _ c.part_last_strictMono.injective

@[deprecated Part.size_le (since := "2025-01-20")]
lemma partSize_le (m : Fin c.length) : (c.part m).size â‰¤ n := (c.part m).size_le

lemma part_injective : Injective c.part :=
  c.part_last_strictMono.injective.of_comp (f := Part.last)

@[simp]
lemma part_inj {i j} : c.part i = c.part j â†” i = j := c.part_injective.eq_iff

lemma part_injectiveâ‚‚ :
    Injective fun x : (i : Fin c.length) Ã— Fin (c.part i).size â†¦ c.part x.1 x.2 := by
  rintro âŸ¨i, xâŸ© âŸ¨j, yâŸ© h
  obtain rfl : i = j := by
    apply c.disjoint.eq
    have h : âˆƒ x y, c.part j y = c.part i x := âŸ¨x, y, h.symmâŸ©
    simpa [onFun, Finset.disjoint_left, Part.mem_range] using h
  simpa using (c.part i).injective h

@[deprecated (since := "2025-01-20")]
alias emb_injective := part_injectiveâ‚‚

theorem part_bijectiveâ‚‚ :
    Bijective fun x : (i : Fin c.length) Ã— Fin (c.part i).size â†¦ c.part x.1 x.2 :=
  âŸ¨c.part_injectiveâ‚‚, fun i â†¦ by simpa [Part.mem_range] using c.cover iâŸ©

@[simp]
lemma part_injâ‚‚ {i j i' j'} : c.part i j = c.part i' j' â†” i = i' âˆ§ (j : â„•) = j' := by
  simpa +contextual only [Sigma.mk.inj_iff, â† exists_prop, Fin.heq_ext_iff]
    using c.part_injectiveâ‚‚.eq_iff (a := âŸ¨i, jâŸ©) (b := âŸ¨i', j'âŸ©)

@[simp]
lemma part_mem_range {i j k} : c.part i j âˆˆ (c.part k).range â†” i = k := by
  suffices i = k â†’ âˆƒ (x : Fin (c.part k).size), (j : â„•) = x by
    simpa [Part.mem_range, eq_comm] using this
  rintro rfl
  use j

/-- The finite set of all parts of an ordered finpartition. -/
def parts : Finset (Part n) :=
  Finset.univ.map âŸ¨c.part, c.part_injectiveâŸ©

@[simp]
lemma card_parts : c.parts.card = c.length := by simp [parts]

@[simp]
lemma coe_parts : c.parts.toSet = Set.range c.part := by simp [parts]

@[simp]
lemma mem_parts {p} : p âˆˆ c.parts â†” âˆƒ i, c.part i = p := by simp [parts]

/-- An ordered finpartition is completely determined by the finite set of its parts. -/
theorem parts_injective : Injective (@parts n) := by
  intro câ‚ câ‚‚ h
  have hâ‚ : câ‚.length = câ‚‚.length := by simpa using congr($h |>.card)
  replace h : Set.range câ‚.part = Set.range câ‚‚.part := by
    simp only [â† coe_parts, h]
  cases' câ‚ with lengthâ‚ partâ‚ monoâ‚ disjâ‚ _
  cases' câ‚‚ with lengthâ‚‚ partâ‚‚ monoâ‚‚ disjâ‚‚ _
  subst hâ‚
  suffices partâ‚ = partâ‚‚ by congr
  have hâ‚‚ : (partâ‚ Â· |>.last) = (partâ‚‚ Â· |>.last) := by
    rw [â† monoâ‚.range_inj monoâ‚‚]
    simpa only [â† Set.range_comp] using congr((fun p â†¦ p âŠ¤) '' $h)
  ext1 i
  obtain âŸ¨j, hjâŸ© : partâ‚‚ i âˆˆ Set.range partâ‚ := by simp [h]
  have hâ‚ƒ : partâ‚ i âŠ¤ = partâ‚ j âŠ¤ := .trans congr($hâ‚‚ i) <| .symm congr($hj âŠ¤)
  rw [â† hj, monoâ‚.injective hâ‚ƒ]

theorem disjoint_setRange {i j} (h : i â‰  j) : Disjoint (range (c.part i)) (range (c.part j)) := by
  simpa only [â† Part.coe_range, Finset.disjoint_coe] using c.disjoint h

instance : Unique (OrderedFinpartition 0) :=
  have : Subsingleton (OrderedFinpartition 0) := parts_injective.subsingleton
  .mk' _

/-- An ordered finpartition gives an equivalence between `Fin n`
and the disjoint union of the parts, each of them parameterized by `Fin (c.part i).size`. -/
@[simps symm_apply]
def equivSigma : Fin n â‰ƒ ((i : Fin c.length) Ã— Fin (c.part i).size) where
  toFun := Fintype.bijInv c.part_bijectiveâ‚‚
  invFun x := c.part x.1 x.2
  left_inv := Fintype.rightInverse_bijInv _
  right_inv := Fintype.leftInverse_bijInv _

/-- Given `j : Fin n`, the index of the part to which it belongs. -/
def index (j : Fin n) : Fin c.length :=
  (c.equivSigma j).1

/-- The inverse of `c.emb` for `c : OrderedFinpartition`. It maps `j : Fin n` to the point in
`Fin (c.partSize (c.index j))` which is mapped back to `j` by `c.emb (c.index j)`. -/
def invEmbedding (j : Fin n) : Fin (c.part (c.index j)).size :=
  (c.equivSigma j).2

@[simp] lemma part_invEmbedding (j : Fin n) :
    c.part (c.index j) (c.invEmbedding j) = j :=
  c.equivSigma.symm_apply_apply j

@[simp]
lemma equivSigma_part (i j) : c.equivSigma (c.part i j) = âŸ¨i, jâŸ© :=
  c.equivSigma.apply_symm_apply âŸ¨i, jâŸ©

@[simp]
lemma index_part (i j) : c.index (c.part i j) = i := by simp [index]

lemma index_eq_iff_mem_range {i j} : c.index i = j â†” i âˆˆ (c.part j).range := by
  rcases c.equivSigma.symm.surjective i with âŸ¨âŸ¨k, lâŸ©, rflâŸ©
  simp

@[simp]
lemma mem_part_index_range (j : Fin n) : j âˆˆ (c.part (c.index j)).range :=
  (Part.mem_range _).mpr âŸ¨_, c.part_invEmbedding jâŸ©

@[to_additive] lemma prod_sigma_eq_prod {M : Type*} [CommMonoid M] (v : Fin n â†’ M) :
    âˆ (m : Fin c.length), âˆ (r : Fin (c.part m).size), v (c.part m r) = âˆ i, v i := by
  rw [Finset.prod_sigma', Finset.univ_sigma_univ, â† c.equivSigma.symm.prod_comp]
  simp only [equivSigma_symm_apply]

@[simp]
theorem sum_part_size : âˆ‘ i, (c.part i).size = n := by
  simpa using c.sum_sigma_eq_sum (1 : Fin n â†’ â„•)

@[simp]
lemma length_eq_zero : c.length = 0 â†” n = 0 where
  mp h := by
    have : IsEmpty (Fin c.length) := by rw [h]; infer_instance
    rw [â† c.sum_part_size, Finset.sum_of_isEmpty]
  mpr := by
    rintro rfl
    rw [Unique.eq_default c]
    rfl

@[simp]
lemma length_pos : 0 < c.length â†” 0 < n := by
  simp only [Nat.pos_iff_ne_zero, ne_eq, length_eq_zero]

@[simp]
lemma one_le_length : 1 â‰¤ c.length â†” 1 â‰¤ n := c.length_pos

instance neZero_length [NeZero n] (c : OrderedFinpartition n) : NeZero c.length :=
  .of_pos <| c.length_pos.2 pos'

@[deprecated Part.size_neZero (since := "2025-01-20")]
lemma neZero_partSize (c : OrderedFinpartition n) (i : Fin c.length) : NeZero (c.part i).size :=
  inferInstance

@[simp]
lemma part_index_zero_zero [NeZero n] : c.part (c.index 0) 0 = 0 :=
  (Part.zero_mem_range _).mp <| c.mem_part_index_range 0

/-!
### Extending and shrinking ordered finpartitions

We show how an ordered finpartition can be extended to the left, either by adding a new atomic
part (in `extendLeft`) or adding the new element to an existing part (in `extendMiddle`).
Conversely, one can shrink a finpartition by deleting the element to the left, with a different
behavior if it was an atomic part (in `eraseLeft`, in which case the number of parts decreases by
one) or if it belonged to a non-atomic part (in `eraseMiddle`, in which case the number of parts
stays the same).

These operations are inverse to each other, giving rise to an equivalence between
`((c : OrderedFinpartition n) Ã— Option (Fin c.length))` and `OrderedFinpartition (n + 1)`
called `OrderedFinPartition.extendEquiv`.
-/

/-- Extend an ordered partition of `n` entries, by adding a new singleton part to the left. -/
@[simps length]
def extendLeft (c : OrderedFinpartition n) : OrderedFinpartition (n + 1) where
  length := c.length + 1
  part := Fin.cons (.atom 0) fun i â†¦ (c.part i).map (Fin.succOrderEmb n)
  part_last_strictMono i j hij := by
    rcases Fin.eq_succ_of_ne_zero hij.ne_bot with âŸ¨j, rflâŸ©
    cases i using Fin.cases with
    | zero => simp
    | succ => simpa using c.part_last_strictMono (Fin.succ_lt_succ_iff.mp hij)
  disjoint := by
    rw [pairwise_disjoint_on]
    intro i j hij
    rcases Fin.eq_succ_of_ne_zero hij.ne_bot with âŸ¨j, rflâŸ©
    cases i using Fin.cases with
    | zero => simp [Fin.succ_ne_zero]
    | succ => simpa using c.disjoint (Fin.succ_lt_succ_iff.mp hij).ne
  cover i := by
    cases i using Fin.cases with
    | zero =>
      use 0
      simp
    | succ i =>
      use (c.index i).succ
      simp

@[simp]
theorem extendLeft_part_zero (c : OrderedFinpartition n) : c.extendLeft.part 0 = .atom 0 := rfl

@[simp]
theorem extendLeft_part_succ (c : OrderedFinpartition n) (i : Fin c.length) :
    c.extendLeft.part i.succ = (c.part i).map (Fin.succOrderEmb n) :=
  rfl

/-- Extend an ordered partition of `n` entries, by adding to the `i`-th part a new point to the
left. -/
@[simps length, simps (config := .lemmasOnly) part]
def extendMiddle (c : OrderedFinpartition n) (k : Fin c.length) : OrderedFinpartition (n + 1) where
  length := c.length
  part := update (fun i â†¦ (c.part i).map (Fin.succOrderEmb n)) k (c.part k).extendZero
  part_last_strictMono := by
    simpa [apply_update fun _ â†¦ Part.last] using Fin.strictMono_succ.comp c.part_last_strictMono
  disjoint i j hne := by
    wlog hik : i â‰  k generalizing i j
    Â· obtain rfl : i = k := by push_neg at hik; exact hik
      exact this j i hne.symm hne.symm |>.symm
    rcases eq_or_ne j k with rfl | hjk <;>
      simpa [onFun, *, Part.range_extendZero_eq_cons, Fin.succ_ne_zero] using c.disjoint hne
  cover i := by
    cases i using Fin.cases with
    | zero =>
      use k
      simp
    | succ i =>
      use c.index i
      rcases eq_or_ne (c.index i) k with rfl | hne <;> simp [*, Part.range_extendZero_eq_cons]

@[simp]
theorem extendMiddle_part_self (c : OrderedFinpartition n) (k : Fin c.length) :
    (c.extendMiddle k).part k = (c.part k).extendZero := by
  simp [extendMiddle_part]

@[simp]
theorem extendMiddle_part_of_ne (c : OrderedFinpartition n) {i j : Fin c.length} (h : j â‰  i) :
    (c.extendMiddle i).part j = (c.part j).map (Fin.succOrderEmb n) := by
  simp [extendMiddle_part, h]

/-- If the first part of a partition is not `Part.atom 0`,
then none of the the parts is `Part.atom 0`. -/
theorem part_ne_atom_zero [NeZero n] (h : c.part 0 â‰  .atom 0) (i) : c.part i â‰  .atom 0 := by
  contrapose! h
  rw [â† Part.last_eq_zero, â† (Fin.zero_le' _).le_iff_eq] at h âŠ¢
  exact (c.part_last_strictMono.monotone (Fin.zero_le' _)).trans h

@[simp]
theorem extendMiddle_part_ne_atom_zero (k : Fin c.length) :
    âˆ€ i, (c.extendMiddle k).part i â‰  .atom 0 := by
  intro i
  rcases eq_or_ne k i with rfl | hne <;>
    simp [extendMiddle_part, *, Ne.symm, succ_ne_zero]

/-- Extend an ordered partition of `n` entries, by adding singleton to the left
or appending it to one of the existing part. -/
def extend (c : OrderedFinpartition n) : Fin (c.length + 1) â†’ OrderedFinpartition (n + 1) :=
  Fin.cons c.extendLeft c.extendMiddle

@[simp]
lemma extend_zero (c : OrderedFinpartition n) : c.extend 0 = c.extendLeft := rfl

@[simp]
lemma extend_succ (c : OrderedFinpartition n) (i : Fin c.length) :
    c.extend i.succ = c.extendMiddle i :=
  rfl

/-- Given an ordered finpartition of `n + 1`, with a leftmost part equal to `Part.atom 0`,
remove this atom to form an ordered finpartition of `n`. -/
@[simps length, simps (config := .lemmasOnly) part]
def eraseLeft (c : OrderedFinpartition (n + 1)) (hc : c.part 0 = .atom 0) :
    OrderedFinpartition n :=
  have eq : c.length - 1 + 1 = c.length := Nat.sub_add_cancel <| by simp
  { length := c.length - 1
    part i := (c.part <| i.succ.cast eq).mapPred <| by
      rw [ne_eq, â† Part.zero_mem_range]
      exact Finset.disjoint_left.mp (c.disjoint (i := 0) (by simp [Fin.ext_iff])) (by simp [hc])
    part_last_strictMono i j hlt := by simpa
    disjoint i j hne := by
      simp_rw [onFun, Part.mapPred_range_eq_preimage]
      simpa [â† Finset.disjoint_coe] using (c.disjoint_setRange (by simpa)).preimage Fin.succ
    cover i := by
      simpa [Part.mapPred_range_eq_preimage, (finCongr eq).surjective.exists, exists_fin_succ, hc,
        succ_ne_zero] using c.cover i.succ }

@[simp]
theorem eraseLeft_extendLeft : c.extendLeft.eraseLeft rfl = c := by
  simp [eraseLeft, extendLeft, funext_iff]

theorem extendLeft_injective : Injective (@extendLeft n) := by
  intro câ‚ câ‚‚ h
  rw [â† câ‚.eraseLeft_extendLeft, â† câ‚‚.eraseLeft_extendLeft]
  simp only [h]

@[simp]
lemma extendLeft_inj {câ‚ câ‚‚ : OrderedFinpartition n} :
    câ‚.extendLeft = câ‚‚.extendLeft â†” câ‚ = câ‚‚ :=
  extendLeft_injective.eq_iff

@[simp]
theorem extendLeft_eraseLeft (c : OrderedFinpartition (n + 1)) (hc : c.part 0 = .atom 0) :
    (c.eraseLeft hc).extendLeft = c := by
  simp [OrderedFinpartition.ext_iff, eraseLeft, extendLeft, Fin.heq_fun_iff, forall_fin_succ, hc,
    â† Fin.val_inj]

/-- Given an ordered finpartition of `n+1`, with a leftmost atom different from `{0}`, remove `{0}`
from the atom that contains it, to form an ordered finpartition of `n`. -/
@[simps]
def eraseMiddle (c : OrderedFinpartition (n + 1)) (hc : c.part 0 â‰  .atom 0) :
    OrderedFinpartition n where
  length := c.length
  part i := (c.part i).preimageSucc (c.part_ne_atom_zero hc i)
  part_last_strictMono i j hlt := by simpa
  disjoint i j hne := by
    simpa [onFun, â† Finset.disjoint_coe] using (c.disjoint_setRange hne).preimage succ
  cover i := by simpa using c.cover i.succ

@[simp]
theorem eraseMiddle_extendMiddle (i : Fin c.length) :
    (c.extendMiddle i).eraseMiddle (extendMiddle_part_ne_atom_zero c i 0) = c := by
  suffices âˆ€ j, ((c.extendMiddle i).part j).preimageSucc _ = c.part j by
    simpa [OrderedFinpartition.ext_iff, funext_iff]
  intro j
  rcases eq_or_ne i j with rfl | hne <;> simp [*, Ne.symm]

@[simp]
theorem extendMiddle_eraseMiddle (c : OrderedFinpartition (n + 1)) (hc : c.part 0 â‰  .atom 0) :
    (c.eraseMiddle hc).extendMiddle (c.index 0) = c := by
  suffices âˆ€ j, ((c.eraseMiddle hc).extendMiddle (c.index 0)).part j = c.part j by
    simpa [OrderedFinpartition.ext_iff, funext_iff]
  intro j
  rcases eq_or_ne j (c.index 0) with rfl | hne
  Â· simp [Part.extendZero_preimageSucc]
  Â· have : c.part j 0 â‰  0 := by
      simpa [index_eq_iff_mem_range] using hne.symm
    simp [*, Part.map_succ_preimageSucc]

/-- Extending the ordered partitions of `Fin n` bijects with the ordered partitions
of `Fin (n+1)`. -/
def extendEquiv (n : â„•) :
    ((c : OrderedFinpartition n) Ã— Fin (c.length + 1)) â‰ƒ OrderedFinpartition (n + 1) where
  toFun c := c.1.extend c.2
  invFun c := if h : c.part 0 = .atom 0 then âŸ¨c.eraseLeft h, 0âŸ© else
    âŸ¨c.eraseMiddle h, .succ (c.index 0)âŸ©
  left_inv := by
    rintro âŸ¨c, oâŸ©
    cases o using Fin.cases with
    | zero =>
      simp
    | succ o =>
      simp [index_eq_iff_mem_range]
  right_inv c := by
    simp only
    rw [apply_dite (fun c : (c : OrderedFinpartition n) Ã— Fin (c.length + 1) â†¦ c.1.extend c.2)]
    split_ifs with h
    Â· simp
    Â· simp

/-! ### Applying ordered finpartitions to multilinear maps -/

/-- Given a formal multilinear series `p`, an ordered partition `c` of `n` and the index `i` of a
block of `c`, we may define a function on `Fin n â†’ E` by picking the variables in the `i`-th block
of `n`, and applying the corresponding coefficient of `p` to these variables. This function is
called `p.applyOrderedFinpartition c v i` for `v : Fin n â†’ E` and `i : Fin c.k`. -/
def applyOrderedFinpartition (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F) :
    (Fin n â†’ E) â†’ Fin c.length â†’ F :=
  fun v m â†¦ p m (v âˆ˜ c.emb m)

lemma applyOrderedFinpartition_apply (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F)
    (v : Fin n â†’ E) :
  c.applyOrderedFinpartition p v = (fun m â†¦ p m (v âˆ˜ c.emb m)) := rfl

theorem norm_applyOrderedFinpartition_le (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F)
    (v : Fin n â†’ E) (m : Fin c.length) :
    â€–c.applyOrderedFinpartition p v mâ€– â‰¤ â€–p mâ€– * âˆ i : Fin (c.partSize m), â€–v (c.emb m i)â€– :=
  (p m).le_opNorm _

/-- Technical lemma stating how `c.applyOrderedFinpartition` commutes with updating variables. This
will be the key point to show that functions constructed from `applyOrderedFinpartition` retain
multilinearity. -/
theorem applyOrderedFinpartition_update_right
    (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F)
    (j : Fin n) (v : Fin n â†’ E) (z : E) :
    c.applyOrderedFinpartition p (update v j z) =
      update (c.applyOrderedFinpartition p v) (c.index j)
        (p (c.index j)
          (Function.update (v âˆ˜ c.emb (c.index j)) (c.invEmbedding j) z)) := by
  ext m
  by_cases h : m = c.index j
  Â· rw [h]
    simp only [applyOrderedFinpartition, update_self]
    congr
    rw [â† Function.update_comp_eq_of_injective]
    Â· simp
    Â· exact (c.emb_strictMono (c.index j)).injective
  Â· simp only [applyOrderedFinpartition, ne_eq, h, not_false_eq_true,
      update_of_ne]
    congr
    apply Function.update_comp_eq_of_not_mem_range
    have A : Disjoint (range (c.emb m)) (range (c.emb (c.index j))) :=
      c.disjoint (mem_univ m) (mem_univ (c.index j)) h
    have : j âˆˆ range (c.emb (c.index j)) := mem_range.2 âŸ¨c.invEmbedding j, by simpâŸ©
    exact Set.disjoint_right.1 A this

theorem applyOrderedFinpartition_update_left (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F)
    (m : Fin c.length) (v : Fin n â†’ E) (q : E[Ã—c.partSize m]â†’L[ğ•œ] F) :
    c.applyOrderedFinpartition (update p m q) v
      = update (c.applyOrderedFinpartition p v) m (q (v âˆ˜ c.emb m)) := by
  ext d
  by_cases h : d = m
  Â· rw [h]
    simp [applyOrderedFinpartition]
  Â· simp [h, applyOrderedFinpartition]

/-- Given a an ordered finite partition `c` of `n`, a continuous multilinear map `f` in `c.length`
variables, and for each `m` a continuous multilinear map `p m` in `c.partSize m` variables,
one can form a continuous multilinear map in `n`
variables by applying `p m` to each part of the partition, and then
applying `f` to the resulting vector. It is called `c.compAlongOrderedFinpartition f p`. -/
def compAlongOrderedFinpartition (f : F [Ã—c.length]â†’L[ğ•œ] G) (p : âˆ€ i, E [Ã—c.partSize i]â†’L[ğ•œ] F) :
    E[Ã—n]â†’L[ğ•œ] G where
  toFun v := f (c.applyOrderedFinpartition p v)
  map_update_add' v i x y := by
    cases Subsingleton.elim â€¹_â€º (instDecidableEqFin _)
    simp only [applyOrderedFinpartition_update_right, ContinuousMultilinearMap.map_update_add]
  map_update_smul' v i c x := by
    cases Subsingleton.elim â€¹_â€º (instDecidableEqFin _)
    simp only [applyOrderedFinpartition_update_right, ContinuousMultilinearMap.map_update_smul]
  cont := by
    apply f.cont.comp
    change Continuous (fun v m â†¦ p m (v âˆ˜ c.emb m))
    fun_prop

@[simp] lemma compAlongOrderFinpartition_apply (f : F [Ã—c.length]â†’L[ğ•œ] G)
    (p : âˆ€ i, E[Ã—c.partSize i]â†’L[ğ•œ] F) (v : Fin n â†’ E) :
    c.compAlongOrderedFinpartition f p v = f (c.applyOrderedFinpartition p v) := rfl

theorem norm_compAlongOrderedFinpartition_le (f : F [Ã—c.length]â†’L[ğ•œ] G)
    (p : âˆ€ i, E [Ã—c.partSize i]â†’L[ğ•œ] F) :
    â€–c.compAlongOrderedFinpartition f pâ€– â‰¤ â€–fâ€– * âˆ i, â€–p iâ€– := by
  refine ContinuousMultilinearMap.opNorm_le_bound (by positivity) fun v â†¦ ?_
  rw [compAlongOrderFinpartition_apply, mul_assoc, â† c.prod_sigma_eq_prod,
    â† Finset.prod_mul_distrib]
  exact f.le_opNorm_mul_prod_of_le <| c.norm_applyOrderedFinpartition_le _ _

/-- Bundled version of `compAlongOrderedFinpartition`, depending linearly on `f`
and multilinearly on `p`. -/
@[simps apply_apply]
def compAlongOrderedFinpartitionâ‚— :
    (F [Ã—c.length]â†’L[ğ•œ] G) â†’â‚—[ğ•œ]
      MultilinearMap ğ•œ (fun i : Fin c.length â†¦ E[Ã—c.partSize i]â†’L[ğ•œ] F) (E[Ã—n]â†’L[ğ•œ] G) where
  toFun f :=
    { toFun := fun p â†¦ c.compAlongOrderedFinpartition f p
      map_update_add' := by
        intro inst p m q q'
        cases Subsingleton.elim â€¹_â€º (instDecidableEqFin _)
        ext v
        simp [applyOrderedFinpartition_update_left]
      map_update_smul' := by
        intro inst p m a q
        cases Subsingleton.elim â€¹_â€º (instDecidableEqFin _)
        ext v
        simp [applyOrderedFinpartition_update_left] }
  map_add' _ _ := rfl
  map_smul' _ _ :=  rfl

variable (ğ•œ E F G) in
/-- Bundled version of `compAlongOrderedFinpartition`, depending continuously linearly on `f`
and continuously multilinearly on `p`. -/
noncomputable def compAlongOrderedFinpartitionL :
    (F [Ã—c.length]â†’L[ğ•œ] G) â†’L[ğ•œ]
      ContinuousMultilinearMap ğ•œ (fun i â†¦ E[Ã—c.partSize i]â†’L[ğ•œ] F) (E[Ã—n]â†’L[ğ•œ] G) := by
  refine MultilinearMap.mkContinuousLinear c.compAlongOrderedFinpartitionâ‚— 1 fun f p â†¦ ?_
  simp only [one_mul, compAlongOrderedFinpartitionâ‚—_apply_apply]
  apply norm_compAlongOrderedFinpartition_le

@[simp] lemma compAlongOrderedFinpartitionL_apply (f : F [Ã—c.length]â†’L[ğ•œ] G)
    (p : âˆ€ (i : Fin c.length), E[Ã—c.partSize i]â†’L[ğ•œ] F) :
    c.compAlongOrderedFinpartitionL ğ•œ E F G f p = c.compAlongOrderedFinpartition f p := rfl

theorem norm_compAlongOrderedFinpartitionL_le :
    â€–c.compAlongOrderedFinpartitionL ğ•œ E F Gâ€– â‰¤ 1 :=
  MultilinearMap.mkContinuousLinear_norm_le _ zero_le_one _

end OrderedFinpartition

/-! ### The Faa di Bruno formula -/

namespace FormalMultilinearSeries

/-- Given two formal multilinear series `q` and `p` and a composition `c` of `n`, one may
form a continuous multilinear map in `n` variables by applying the right coefficient of `p` to each
block of the composition, and then applying `q c.length` to the resulting vector. It is
called `q.compAlongComposition p c`. -/
def compAlongOrderedFinpartition {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : OrderedFinpartition n) :
    E [Ã—n]â†’L[ğ•œ] G :=
  c.compAlongOrderedFinpartition (q c.length) (fun m â†¦ p (c.partSize m))

@[simp]
theorem compAlongOrderedFinpartition_apply {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : OrderedFinpartition n) (v : Fin n â†’ E) :
    (q.compAlongOrderedFinpartition p c) v =
      q c.length (c.applyOrderedFinpartition (fun m â†¦ (p (c.partSize m))) v) :=
  rfl

/-- Taylor formal composition of two formal multilinear series. The `n`-th coefficient in the
composition is defined to be the sum of `q.compAlongOrderedFinpartition p c` over all
ordered partitions of `n`.
In other words, this term (as a multilinear function applied to `vâ‚€, ..., vâ‚™â‚‹â‚`) is
`âˆ‘'_{k} âˆ‘'_{Iâ‚€ âŠ” ... âŠ” Iâ‚–â‚‹â‚ = {0, ..., n-1}} qâ‚– (p_{iâ‚€} (...), ..., p_{iâ‚–â‚‹â‚} (...))`, where
`iâ‚˜` is the size of `Iâ‚˜` and one puts all variables of `Iâ‚˜` as arguments to `p_{iâ‚˜}`, in
increasing order. The sets `Iâ‚€, ..., Iâ‚–â‚‹â‚` are ordered so that `max Iâ‚€ < max Iâ‚ < ... < max Iâ‚–â‚‹â‚`.

This definition is chosen so that the `n`-th derivative of `g âˆ˜ f` is the Taylor composition of
the iterated derivatives of `g` and of `f`.

Not to be confused with another notion of composition for formal multilinear series, called just
`FormalMultilinearSeries.comp`, appearing in the composition of analytic functions.
-/
protected noncomputable def taylorComp
    (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F) :
    FormalMultilinearSeries ğ•œ E G :=
  fun n â†¦ âˆ‘ c : OrderedFinpartition n, q.compAlongOrderedFinpartition p c

end FormalMultilinearSeries

theorem analyticOn_taylorComp
    (hq : âˆ€ (n : â„•), AnalyticOn ğ•œ (fun x â†¦ q x n) t)
    (hp : âˆ€ n, AnalyticOn ğ•œ (fun x â†¦ p x n) s) {f : E â†’ F}
    (hf : AnalyticOn ğ•œ f s) (h : MapsTo f s t) (n : â„•) :
    AnalyticOn ğ•œ (fun x â†¦ (q (f x)).taylorComp (p x) n) s := by
  apply Finset.analyticOn_sum _ (fun c _ â†¦ ?_)
  let B := c.compAlongOrderedFinpartitionL ğ•œ E F G
  change AnalyticOn ğ•œ
    ((fun p â†¦ B p.1 p.2) âˆ˜ (fun x â†¦ (q (f x) c.length, fun m â†¦ p x (c.partSize m)))) s
  apply B.analyticOnNhd_uncurry_of_multilinear.comp_analyticOn ?_ (mapsTo_univ _ _)
  apply AnalyticOn.prod
  Â· exact (hq c.length).comp hf h
  Â· exact AnalyticOn.pi (fun i â†¦ hp _)

open OrderedFinpartition

/-- Composing two formal multilinear series `q` and `p` along an ordered partition extended by a
new atom to the left corresponds to applying `p 1` on the first coordinates, and the initial
ordered partition on the other coordinates.
This is one of the terms that appears when differentiating in the Faa di Bruno
formula, going from step `m` to step `m + 1`. -/
private lemma faaDiBruno_aux1 {m : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : OrderedFinpartition m) :
    (q.compAlongOrderedFinpartition p (c.extend none)).curryLeft =
    ((c.compAlongOrderedFinpartitionL ğ•œ E F G).flipMultilinear fun i â†¦ p (c.partSize i)).comp
      ((q (c.length + 1)).curryLeft.comp ((continuousMultilinearCurryFin1 ğ•œ E F) (p 1))) := by
  ext e v
  simp only [Nat.succ_eq_add_one, OrderedFinpartition.extend, extendLeft,
    ContinuousMultilinearMap.curryLeft_apply,
    FormalMultilinearSeries.compAlongOrderedFinpartition_apply, applyOrderedFinpartition_apply,
    ContinuousLinearMap.coe_comp', comp_apply, continuousMultilinearCurryFin1_apply,
    Matrix.zero_empty, ContinuousLinearMap.flipMultilinear_apply_apply,
    compAlongOrderedFinpartitionL_apply, compAlongOrderFinpartition_apply]
  congr
  ext j
  exact Fin.cases rfl (fun i â†¦ rfl) j

/-- Composing a formal multilinear series with an ordered partition extended by adding a left point
to an already existing atom of index `i` corresponds to updating the `i`th block,
using `p (c.partSize i + 1)` instead of `p (c.partSize i)` there.
This is one of the terms that appears when differentiating in the Faa di Bruno
formula, going from step `m` to step `m + 1`. -/
private lemma faaDiBruno_aux2 {m : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : OrderedFinpartition m) (i : Fin c.length) :
    (q.compAlongOrderedFinpartition p (c.extend (some i))).curryLeft =
    ((c.compAlongOrderedFinpartitionL ğ•œ E F G (q c.length)).toContinuousLinearMap
      (fun i â†¦ p (c.partSize i)) i).comp (p (c.partSize i + 1)).curryLeft := by
  ext e v
  simp? [OrderedFinpartition.extend, extendMiddle, applyOrderedFinpartition_apply] says
    simp only [Nat.succ_eq_add_one, OrderedFinpartition.extend, extendMiddle,
      ContinuousMultilinearMap.curryLeft_apply,
      FormalMultilinearSeries.compAlongOrderedFinpartition_apply, applyOrderedFinpartition_apply,
      ContinuousLinearMap.coe_comp', comp_apply,
      ContinuousMultilinearMap.toContinuousLinearMap_apply, compAlongOrderedFinpartitionL_apply,
      compAlongOrderFinpartition_apply]
  congr
  ext j
  rcases eq_or_ne j i with rfl | hij
  Â· simp only [â†“reduceDIte, update_self, ContinuousMultilinearMap.curryLeft_apply,
      Nat.succ_eq_add_one]
    apply FormalMultilinearSeries.congr _ (by simp)
    intro a ha h'a
    match a with
    | 0 => simp
    | a + 1 => simp [cons]
  Â· simp only [hij, â†“reduceDIte, ne_eq, not_false_eq_true, update_of_ne]
    apply FormalMultilinearSeries.congr _ (by simp [hij])
    simp

/-- *Faa di Bruno* formula: If two functions `g` and `f` have Taylor series up to `n` given by
`q` and `p`, then `g âˆ˜ f` also has a Taylor series, given by `q.taylorComp p`. -/
theorem HasFTaylorSeriesUpToOn.comp {n : WithTop â„•âˆ} {g : F â†’ G} {f : E â†’ F}
    (hg : HasFTaylorSeriesUpToOn n g q t) (hf : HasFTaylorSeriesUpToOn n f p s) (h : MapsTo f s t) :
    HasFTaylorSeriesUpToOn n (g âˆ˜ f) (fun x â†¦ (q (f x)).taylorComp (p x)) s := by
  /- One has to check that the `m+1`-th term is the derivative of the `m`-th term. The `m`-th term
  is a sum, that one can differentiate term by term. Each term is a linear map into continuous
  multilinear maps, applied to parts of `p` and `q`. One knows how to differentiate such a map,
  thanks to `HasFDerivWithinAt.linear_multilinear_comp`. The terms that show up are matched, using
  `faaDiBruno_aux1` and `faaDiBruno_aux2`, with terms of the same form at order `m+1`. Then, one
  needs to check that one gets each term once and exactly once, which is given by the bijection
  `OrderedFinpartition.extendEquiv m`. -/
  classical
  constructor
  Â· intro x hx
    simp [FormalMultilinearSeries.taylorComp, default, HasFTaylorSeriesUpToOn.zero_eq' hg (h hx)]
  Â· intro m hm x hx
    have A (c : OrderedFinpartition m) :
      HasFDerivWithinAt (fun x â†¦ (q (f x)).compAlongOrderedFinpartition (p x) c)
        (âˆ‘ i : Option (Fin c.length),
          ((q (f x)).compAlongOrderedFinpartition (p x) (c.extend i)).curryLeft) s x := by
      let B := c.compAlongOrderedFinpartitionL ğ•œ E F G
      change HasFDerivWithinAt (fun y â†¦ B (q (f y) c.length) (fun i â†¦ p y (c.partSize i)))
        (âˆ‘ i : Option (Fin c.length),
          ((q (f x)).compAlongOrderedFinpartition (p x) (c.extend i)).curryLeft) s x
      have cm : (c.length : WithTop â„•âˆ) â‰¤ m := mod_cast OrderedFinpartition.length_le c
      have cp i : (c.partSize i : WithTop â„•âˆ) â‰¤ m := by
        exact_mod_cast OrderedFinpartition.partSize_le c i
      have I i : HasFDerivWithinAt (fun x â†¦ p x (c.partSize i))
          (p x (c.partSize i).succ).curryLeft s x :=
        hf.fderivWithin (c.partSize i) ((cp i).trans_lt hm) x hx
      have J : HasFDerivWithinAt (fun x â†¦ q x c.length) (q (f x) c.length.succ).curryLeft
        t (f x) := hg.fderivWithin c.length (cm.trans_lt hm) (f x) (h hx)
      have K : HasFDerivWithinAt f ((continuousMultilinearCurryFin1 ğ•œ E F) (p x 1)) s x :=
        hf.hasFDerivWithinAt (le_trans (mod_cast Nat.le_add_left 1 m)
          (ENat.add_one_natCast_le_withTop_of_lt hm)) hx
      convert HasFDerivWithinAt.linear_multilinear_comp (J.comp x K h) I B
      simp only [B, Nat.succ_eq_add_one, Fintype.sum_option, comp_apply, faaDiBruno_aux1,
        faaDiBruno_aux2]
    have B : HasFDerivWithinAt (fun x â†¦ (q (f x)).taylorComp (p x) m)
        (âˆ‘ c : OrderedFinpartition m, âˆ‘ i : Option (Fin c.length),
          ((q (f x)).compAlongOrderedFinpartition (p x) (c.extend i)).curryLeft) s x :=
      HasFDerivWithinAt.sum (fun c _ â†¦ A c)
    suffices âˆ‘ c : OrderedFinpartition m, âˆ‘ i : Option (Fin c.length),
          ((q (f x)).compAlongOrderedFinpartition (p x) (c.extend i)) =
        (q (f x)).taylorComp (p x) (m + 1) by
      rw [â† this]
      convert B
      ext v
      simp only [Nat.succ_eq_add_one, Fintype.sum_option, ContinuousMultilinearMap.curryLeft_apply,
        ContinuousMultilinearMap.sum_apply, ContinuousMultilinearMap.add_apply,
        FormalMultilinearSeries.compAlongOrderedFinpartition_apply, ContinuousLinearMap.coe_sum',
        Finset.sum_apply, ContinuousLinearMap.add_apply]
    rw [Finset.sum_sigma']
    exact Fintype.sum_equiv (OrderedFinpartition.extendEquiv m) _ _ (fun p â†¦ rfl)
  Â· intro m hm
    apply continuousOn_finset_sum _ (fun c _ â†¦ ?_)
    let B := c.compAlongOrderedFinpartitionL ğ•œ E F G
    change ContinuousOn
      ((fun p â†¦ B p.1 p.2) âˆ˜ (fun x â†¦ (q (f x) c.length, fun i â†¦ p x (c.partSize i)))) s
    apply B.continuous_uncurry_of_multilinear.comp_continuousOn (ContinuousOn.prod ?_ ?_)
    Â· have : (c.length : WithTop â„•âˆ) â‰¤ m := mod_cast OrderedFinpartition.length_le c
      exact (hg.cont c.length (this.trans hm)).comp hf.continuousOn h
    Â· apply continuousOn_pi.2 (fun i â†¦ ?_)
      have : (c.partSize i : WithTop â„•âˆ) â‰¤ m := by
        exact_mod_cast OrderedFinpartition.partSize_le c i
      exact hf.cont _ (this.trans hm)
