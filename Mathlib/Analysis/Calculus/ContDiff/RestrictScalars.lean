/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars

/-!
### Restricting Scalars in Iterated Derivatives

This file establishes standard theorems on restriction of scalars for iterated FrÃ©chet derivatives,
comparing iterated derivatives with respect to a field `ğ•œ'` to iterated derivatives with respect to
a subfield `ğ•œ âŠ† ğ•œ'`. The result are analogous to thouse found in
`Mathlib.Analysis.Calculus.FDeriv.RestrictScalars`.
-/

variable
  {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
  {x : E} {f : E â†’ F} {n : â„•}

open ContinuousMultilinearMap Topology

private lemma fderiv_restrictScalarsLinear_comp
    {Ï† : E â†’ (ContinuousMultilinearMap ğ•œ' (fun _ : Fin n â†¦ E) F)} (h : DifferentiableAt ğ•œ' Ï† x) :
    fderiv ğ•œ ((restrictScalarsLinear ğ•œ) âˆ˜ Ï†) x
      = (restrictScalars ğ•œ) âˆ˜ ((fderiv ğ•œ' Ï† x).restrictScalars ğ•œ) := by
  rw [fderiv_comp _ (by fun_prop) (h.restrictScalars ğ•œ), ContinuousLinearMap.fderiv]
  ext a b
  simp [h.fderiv_restrictScalars ğ•œ]

/--
If `f` is `n` times continuously differentiable at `x`, then the `n`th iterated FrÃ©chet derivative
with respect to `ğ•œ` equals scalar restriction of the `n`th iterated FrÃ©chet derivative with respect
to `ğ•œ'`.
-/
theorem ContDiffAt.iteratedFDeriv_restrictScalars_eventuallyEq (h : ContDiffAt ğ•œ' n f x) :
    (restrictScalarsLinear ğ•œ) âˆ˜ (iteratedFDeriv ğ•œ' n f) =á¶ [ğ“ x] (iteratedFDeriv ğ•œ n f) := by
  induction n with
  | zero =>
    filter_upwards with a
    ext m
    simp [iteratedFDeriv_zero_apply m]
  | succ n hn =>
    have : ContDiffAt ğ•œ' n f x := h.of_le (Nat.cast_le.mpr (n.le_add_right 1))
    have tâ‚€ := hn this
    have tâ‚ := this.eventually
    simp only [ne_eq, ENat.natCast_ne_coe_top, not_false_eq_true, forall_const] at tâ‚
    filter_upwards [tâ‚€.eventually_nhds, tâ‚.eventually_nhds,
      h.eventually (by simp)] with a hâ‚a hâ‚‚a hâ‚ƒa
    rw [â† Filter.EventuallyEq] at hâ‚a
    ext m
    simp only [restrictScalarsLinear_apply, Function.comp_apply, coe_restrictScalars,
      iteratedFDeriv_succ_apply_left]
    rw [â† hâ‚a.fderiv_eq, fderiv_restrictScalarsLinear_comp]
    Â· simp
    Â· apply hâ‚ƒa.differentiableAt_iteratedFDeriv
      rw [Nat.cast_lt]
      simp

/--
If `f` is `n` times continuously differentiable at `x`, then the `n`th iterated FrÃ©chet derivative
with respect to `ğ•œ` equals scalar restriction of the `n`th iterated FrÃ©chet derivative with respect
to `ğ•œ'`.
-/
@[simp]
theorem ContDiffAt.iteratedFDeriv_restrictScalars (h : ContDiffAt ğ•œ' n f x) :
    ((restrictScalarsLinear ğ•œ) âˆ˜ iteratedFDeriv ğ•œ' n f) x = iteratedFDeriv ğ•œ n f x :=
  h.iteratedFDeriv_restrictScalars_eventuallyEq.eq_of_nhds
