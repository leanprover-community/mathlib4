/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars

/-!
### Restricting Scalars in Iterated FrÃ©chet Derivatives

This file establishes standard theorems on restriction of scalars for iterated FrÃ©chet derivatives,
comparing iterated derivatives with respect to a field `ğ•œ'` to iterated derivatives with respect to
a subfield `ğ•œ âŠ† ğ•œ'`. The results are analogous to those found in
`Mathlib.Analysis.Calculus.FDeriv.RestrictScalars`.
-/

variable
  {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
  {x : E} {f : E â†’ F} {n : â„•} {s : Set E}

open ContinuousMultilinearMap Topology

/-- Derivation rule for compositions of scalar restriction with continuous multilinear maps. -/
lemma fderivWithin_restrictScalars_comp
    {Ï† : E â†’ (ContinuousMultilinearMap ğ•œ' (fun _ : Fin n â†¦ E) F)}
    (h : DifferentiableWithinAt ğ•œ' Ï† s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ ((restrictScalars ğ•œ) âˆ˜ Ï†) s x
      = (restrictScalars ğ•œ) âˆ˜ ((fderivWithin ğ•œ' Ï† s x).restrictScalars ğ•œ) := by
  simp only [â† restrictScalarsLinear_apply]
  rw [fderiv_comp_fderivWithin _ (by fun_prop) (h.restrictScalars ğ•œ) hs, ContinuousLinearMap.fderiv]
  ext a b
  simp [h.restrictScalars_fderivWithin ğ•œ hs]

/--
If `f` is `n` times continuously differentiable at `x` within `s`, then the `n`th iterated FrÃ©chet
derivative within `s` with respect to `ğ•œ` equals scalar restriction of the `n`th iterated FrÃ©chet
derivative within `s` with respect to `ğ•œ'`.
-/
theorem ContDiffWithinAt.restrictScalars_iteratedFDerivWithin_eventuallyEq
    (h : ContDiffWithinAt ğ•œ' n f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    (restrictScalars ğ•œ) âˆ˜ (iteratedFDerivWithin ğ•œ' n f s)
      =á¶ [ğ“[s] x] iteratedFDerivWithin ğ•œ n f s := by
  induction n with
  | zero =>
    filter_upwards with a
    ext m
    simp
  | succ n hn =>
    have tâ‚€ := h.of_le (Nat.cast_le.mpr (n.le_add_right 1))
    have tâ‚ : âˆ€á¶  (y : E) in ğ“[s] x, ContDiffWithinAt ğ•œ' (â†‘(n + 1)) f s y := by
      nth_rw 2 [â† s.insert_eq_of_mem hx]
      apply h.eventually (by simp)
    filter_upwards [eventually_eventually_nhdsWithin.2 (hn tâ‚€), tâ‚,
      eventually_mem_nhdsWithin (a := x) (s := s)] with a hâ‚a hâ‚ƒa hâ‚„a
    rw [â† Filter.EventuallyEq] at hâ‚a
    ext m
    simp only [Function.comp_apply, coe_restrictScalars, iteratedFDerivWithin_succ_apply_left]
    rw [â† (hâ‚a.fderivWithin' (by tauto)).eq_of_nhdsWithin hâ‚„a,
      fderivWithin_restrictScalars_comp]
    Â· simp
    Â· apply hâ‚ƒa.differentiableWithinAt_iteratedFDerivWithin
      Â· rw [Nat.cast_lt]
        simp
      Â· have : UniqueDiffOn ğ•œ' s := hs.mono_field
        simpa [s.insert_eq_of_mem hâ‚„a]
    apply hs a hâ‚„a

/--
If `f` is `n` times continuously differentiable at `x`, then the `n`th iterated FrÃ©chet derivative
with respect to `ğ•œ` equals scalar restriction of the `n`th iterated FrÃ©chet derivative with respect
to `ğ•œ'`.
-/
theorem ContDiffAt.restrictScalars_iteratedFDeriv_eventuallyEq (h : ContDiffAt ğ•œ' n f x) :
    (restrictScalars ğ•œ) âˆ˜ (iteratedFDeriv ğ•œ' n f) =á¶ [ğ“ x] iteratedFDeriv ğ•œ n f := by
  have h' : ContDiffWithinAt ğ•œ' n f Set.univ x := h
  convert (h'.restrictScalars_iteratedFDerivWithin_eventuallyEq _ trivial)
  <;> simp [iteratedFDerivWithin_univ.symm, uniqueDiffOn_univ]

/--
If `f` is `n` times continuously differentiable at `x`, then the `n`th iterated FrÃ©chet derivative
with respect to `ğ•œ` equals scalar restriction of the `n`th iterated FrÃ©chet derivative with respect
to `ğ•œ'`.
-/
theorem ContDiffAt.restrictScalars_iteratedFDeriv (h : ContDiffAt ğ•œ' n f x) :
    ((restrictScalars ğ•œ) âˆ˜ iteratedFDeriv ğ•œ' n f) x = iteratedFDeriv ğ•œ n f x :=
  h.restrictScalars_iteratedFDeriv_eventuallyEq.eq_of_nhds
