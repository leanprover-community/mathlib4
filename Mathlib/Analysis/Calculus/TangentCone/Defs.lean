/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Analysis.Normed.Field.Basic
public import Mathlib.Order.Filter.Pointwise

/-!
# Tangent cone

In this file, we define two predicates `UniqueDiffWithinAt ğ•œ s x` and `UniqueDiffOn ğ•œ s`
ensuring that, if a function has two derivatives, then they have to coincide. As a direct
definition of this fact (quantifying on all target types and all functions) would depend on
universes, we use a more intrinsic definition: if all the possible tangent directions to the set
`s` at the point `x` span a dense subset of the whole subset, it is easy to check that the
derivative has to be unique.

Therefore, we introduce the set of all tangent directions, named `tangentConeAt`,
and express `UniqueDiffWithinAt` and `UniqueDiffOn` in terms of it.
One should however think of this definition as an implementation detail: the only reason to
introduce the predicates `UniqueDiffWithinAt` and `UniqueDiffOn` is to ensure the uniqueness
of the derivative. This is why their names reflect their uses, and not how they are defined.

## Implementation details

Note that this file is imported by `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`. Hence, derivatives
are not defined yet. The property of uniqueness of the derivative is therefore proved in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`, but based on the properties of the tangent cone we
prove here.
-/

@[expose] public section

open Filter Set Metric
open scoped Topology Pointwise

universe u v
variable (R : Type u) {E : Type v}

section TangentConeAt

variable [AddCommGroup E] [SMul R E] [TopologicalSpace E] {s : Set E} {x y : E}

/-- The set of all tangent directions to the set `s` at the point `x`.

A point `y` belongs to the tangent cone of `s` at `x` iff
there exist a family of scalars `c n`, a family of vectors `d n`,
and a nontrivial filter in the index type such that

- `d n â†’ 0` along the filter;
- `x + d n âˆˆ s` eventually along the filter;
- `c n â€¢ d n â†’ y` along the filter,

The actual definition is given in terms of cluster points of a filter,
see `mem_tangentConeAt_of_seq` and `exists_fun_of_mem_tangentConeAt`
for the two implications unfolding this definition in more convenient way.

In a space with first countable topology,
one can assume that the index type is `â„•` and the filter is `atTop`,
but the definition we use is more useful without that assumption.
-/
irreducible_def tangentConeAt (s : Set E) (x : E) : Set E :=
  {y : E | ClusterPt y ((âŠ¤ : Filter R) â€¢ ğ“[(x + Â·) â»Â¹' s] 0)}

variable {R}

/-- Let `c n` be a family of scalars, `d n` be a family of vectors, and `l` be a filter such that

- `d n â†’ 0` along `l`;
- `x + d n âˆˆ s` frequently along `l`;
- `c n â€¢ d n â†’ y` along `l`.

Then `y` belongs to the tangent cone of `s` at `x`.
See also

- `mem_tangentConeAt_of_seq` for a version assuming that `x + d n âˆˆ s` eventually along `l`.
- `exists_fun_of_mem_tangentConeAt` for the other implication.
-/
theorem mem_tangentConeAt_of_frequently {Î± : Type*} (l : Filter Î±) (c : Î± â†’ R) (d : Î± â†’ E)
    (hdâ‚€ : Tendsto d l (ğ“ 0)) (hds : âˆƒá¶  n in l, x + d n âˆˆ s)
    (hcd : Tendsto (fun n â†¦ c n â€¢ d n) l (ğ“ y)) : y âˆˆ tangentConeAt R s x := by
  suffices Tendsto (fun n â†¦ c n â€¢ d n) (l âŠ“ ğ“Ÿ {y | x + d y âˆˆ s}) (âŠ¤ â€¢ ğ“[(x + Â·) â»Â¹' s] 0) by
    rw [frequently_iff_neBot] at hds
    rw [tangentConeAt_def]
    exact ClusterPt.mono (hcd.mono_left inf_le_left).mapClusterPt this
  rw [â† mapâ‚‚_smul, â† map_prod_eq_mapâ‚‚]
  refine tendsto_map.comp (tendsto_top.prodMk (tendsto_nhdsWithin_iff.mpr âŸ¨?_, ?_âŸ©))
  Â· exact hdâ‚€.mono_left inf_le_left
  Â· simp [eventually_inf_principal]

/-- A special case of `mem_tangentConeAt_of_frequently`, which avoids `Filter.Frequently`. -/
theorem mem_tangentConeAt_of_seq {Î± : Type*} (l : Filter Î±) [l.NeBot] (c : Î± â†’ R) (d : Î± â†’ E)
    (hdâ‚€ : Tendsto d l (ğ“ 0)) (hds : âˆ€á¶  n in l, x + d n âˆˆ s)
    (hcd : Tendsto (fun n â†¦ c n â€¢ d n) l (ğ“ y)) : y âˆˆ tangentConeAt R s x :=
  mem_tangentConeAt_of_frequently l c d hdâ‚€ hds.frequently hcd

/-- If `y` belongs to the tangent cone of `s` at `x`, then there exist

- an index type `Î±` and a nontrivial filter `l` on `Î±`;
- a family of scalars `c n`, `n : Î±`, and a family of vectors `d n`, `n : Î±` such that
- `d n â†’ 0` along `l`;
- `x + d n âˆˆ s` eventually along `l`;
- `c n â€¢ d n â†’ y` along `l`.

In fact, one can take `Î± = R Ã— E`, `c = Prod.fst`, and `d = Prod.snd`, but this is not important,
so the lemma statement hides these details.

This lemma provides a convenient way to unfold the definition of `tangentConeAt`. -/
theorem exists_fun_of_mem_tangentConeAt (h : y âˆˆ tangentConeAt R s x) :
    âˆƒ (Î± : Type (max u v)) (l : Filter Î±) (_hl : l.NeBot) (c : Î± â†’ R) (d : Î± â†’ E),
      Tendsto d l (ğ“ 0) âˆ§ (âˆ€á¶  n in l, x + d n âˆˆ s) âˆ§ Tendsto (fun n â†¦ c n â€¢ d n) l (ğ“ y) := by
  rw [tangentConeAt, mem_setOf, â† mapâ‚‚_smul, â† map_prod_eq_mapâ‚‚, ClusterPt,
    â† neBot_inf_comap_iff_map'] at h
  refine âŸ¨R Ã— E, _, h, Prod.fst, Prod.snd, ?_, ?_, ?_âŸ©
  Â· refine (tendsto_snd (f := âŠ¤)).mono_left <| inf_le_right.trans <| ?_
    gcongr
    apply nhdsWithin_le_nhds
  Â· refine .filter_mono inf_le_right ?_
    rw [top_prod, eventually_comap]
    filter_upwards [eventually_mem_nhdsWithin]
    simp +contextual
  Â· exact tendsto_comap.mono_left inf_le_left

end TangentConeAt

/-- "Positive" tangent cone to `s` at `x`. -/
abbrev posTangentConeAt [AddCommGroup E] [Module â„ E] [TopologicalSpace E] (s : Set E) (x : E) :
    Set E :=
  tangentConeAt NNReal s x

variable [Semiring R] [AddCommGroup E] [Module R E] [TopologicalSpace E]

/-- A property ensuring that the tangent cone to `s` at `x` spans a dense subset of the whole space.
The main role of this property is to ensure that the differential within `s` at `x` is unique,
hence this name. The uniqueness it asserts is proved in `UniqueDiffWithinAt.eq` in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.
To avoid pathologies in dimension 0, we also require that `x` belongs to the closure of `s` (which
is automatic when `E` is not `0`-dimensional). -/
@[mk_iff]
structure UniqueDiffWithinAt (s : Set E) (x : E) : Prop where
  dense_tangentConeAt : Dense (Submodule.span R (tangentConeAt R s x) : Set E)
  mem_closure : x âˆˆ closure s

/-- A property ensuring that the tangent cone to `s` at any of its points spans a dense subset of
the whole space. The main role of this property is to ensure that the differential along `s` is
unique, hence this name. The uniqueness it asserts is proved in `UniqueDiffOn.eq` in
`Mathlib/Analysis/Calculus/FDeriv/Basic.lean`. -/
def UniqueDiffOn (s : Set E) : Prop :=
  âˆ€ x âˆˆ s, UniqueDiffWithinAt R s x

variable {R} in
theorem UniqueDiffOn.uniqueDiffWithinAt {s : Set E} {x} (hs : UniqueDiffOn R s) (h : x âˆˆ s) :
    UniqueDiffWithinAt R s x :=
  hs x h
