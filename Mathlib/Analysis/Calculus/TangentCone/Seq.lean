/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.TangentCone.Basic
public import Mathlib.Topology.Algebra.MulAction
public import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.SpecificLimits.Normed

/-!
# Tangent cone points as limits of sequences

This file contains a few ways to describe `tangentConeAt`
as the set of limits of certain sequences.

In many cases, one can generalize results about the tangent cone
by using `mem_tangentConeAt_of_seq` and `exists_fun_of_mem_tangentConeAt`
instead of these lemmas.
-/

public section

open Filter
open scoped Topology

/-- In a vector space with first countable topology, a vector `y` belongs to `tangentConeAt ğ•œ s x`
if and only if there exist sequences `c n` and `d n` such that

- `d n` tends to zero as `n â†’ âˆ`;
- `x + d n âˆˆ s` for sufficiently large `n`;
- `c n â€¢ d n` tends to `y` as `n â†’ âˆ`.

See `mem_tangentConeAt_of_seq` and `exists_fun_of_mem_tangentConeAt`
for versions of two implications of this theorem that don't assume first countable topology. -/
theorem mem_tangentConeAt_iff_exists_seq {R E : Type*} [AddCommGroup E] [SMul R E]
    [TopologicalSpace E] [FirstCountableTopology E] {s : Set E} {x y : E} :
    y âˆˆ tangentConeAt R s x â†” âˆƒ (c : â„• â†’ R) (d : â„• â†’ E), Tendsto d atTop (ğ“ 0) âˆ§
      (âˆ€á¶  n in atTop, x + d n âˆˆ s) âˆ§ Tendsto (fun n â†¦ c n â€¢ d n) atTop (ğ“ y) := by
  constructor
  Â· intro h
    simp only [tangentConeAt_def, Set.mem_setOf, â† mapâ‚‚_smul, â† map_prod_eq_mapâ‚‚, ClusterPt,
      â† neBot_inf_comap_iff_map'] at h
    rcases @exists_seq_tendsto _ _ _ h with âŸ¨cd, hcdâŸ©
    simp only [tendsto_inf, tendsto_comap_iff, tendsto_prod_iff', tendsto_nhdsWithin_iff] at hcd
    exact âŸ¨Prod.fst âˆ˜ cd, Prod.snd âˆ˜ cd, hcd.2.2.1, hcd.2.2.2, hcd.1âŸ©
  Â· rintro âŸ¨c, d, hdâ‚€, hds, hcdâŸ©
    exact mem_tangentConeAt_of_seq atTop c d hdâ‚€ hds hcd

section
variable {ğ•œ E : Type*} [NormedDivisionRing ğ•œ] [AddCommGroup E] [Module ğ•œ E]
  [TopologicalSpace E] [ContinuousSMul ğ•œ E] {s : Set E} {x y : E} {r : ğ•œ}

/-- Auxiliary lemma ensuring that, under the assumptions from an old definition of the tangent cone,
the sequence `d` tends to 0 at infinity. -/
theorem tangentConeAt.lim_zero {Î± : Type*} (l : Filter Î±) {c : Î± â†’ ğ•œ} {d : Î± â†’ E} {y : E}
    (hc : Tendsto (fun n => â€–c nâ€–) l atTop) (hd : Tendsto (fun n => c n â€¢ d n) l (ğ“ y)) :
    Tendsto d l (ğ“ 0) := by
  have : âˆ€á¶  n in l, (c n)â»Â¹ â€¢ c n â€¢ d n = d n :=
    (eventually_ne_of_tendsto_norm_atTop hc 0).mono fun n hn â†¦ inv_smul_smulâ‚€ hn (d n)
  rw [tendsto_norm_atTop_iff_cobounded] at hc
  simpa using Tendsto.congr' this <| (tendsto_invâ‚€_cobounded.comp hc).smul hd

theorem mem_tangentConeAt_of_pow_smul (hrâ‚€ : r â‰  0) (hr : â€–râ€– < 1)
    (hs : âˆ€á¶  n : â„• in atTop, x + r ^ n â€¢ y âˆˆ s) :
    y âˆˆ tangentConeAt ğ•œ s x := by
  refine mem_tangentConeAt_of_add_smul_mem
    (tendsto_nhdsWithin_iff.mpr âŸ¨tendsto_pow_atTop_nhds_zero_of_norm_lt_one hr, ?_âŸ©) hs
  simp [hrâ‚€]

end

/-- In a normed space over a nontrivially normed field,
a point `y` belongs to the tangent cone of a set `s` at `x`
iff there exiss a sequence of scalars `c n` and a sequence of points `d n` such that

- `â€–c nâ€– â†’ âˆ` as `n â†’ âˆ`;
- `x + d n âˆˆ s` for sufficiently large `n`;
- `c n â€¢ d n` tendst to `y`.

Before https://github.com/leanprover-community/mathlib4/pull/34127,
the right-hand side of this equivalence was the definition of the tangent cone.

In most cases, `exists_fun_of_mem_tangentConeAt` and/or `mem_tangentConeAt_of_seq`
can be used to generalize a proof using this lemma to topological vector spaces.
-/
theorem mem_tangentConeAt_iff_exists_seq_norm_tendsto_atTop {ğ•œ E : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {s : Set E} {x y : E} :
    y âˆˆ tangentConeAt ğ•œ s x â†”
      âˆƒ (c : â„• â†’ ğ•œ) (d : â„• â†’ E), Tendsto (â€–c Â·â€–) atTop atTop âˆ§ (âˆ€á¶  n in atTop, x + d n âˆˆ s) âˆ§
        Tendsto (fun n â†¦ c n â€¢ d n) atTop (ğ“ y) := by
  constructor
  Â· rcases eq_or_ne y 0 with rfl | hyâ‚€
    Â· rw [zero_mem_tangentConeAt_iff]
      intro hx
      obtain âŸ¨c, hcâŸ© := NormedField.exists_lt_norm ğ•œ 1
      have (n : â„•) : âˆƒ d : E, x + d âˆˆ s âˆ§ â€–dâ€– < (1 / (2 * â€–câ€–)) ^ n := by
        rw [Metric.mem_closure_iff] at hx
        rcases hx ((1 / (2 * â€–câ€–)) ^ n) (by positivity) with âŸ¨v, hvs, hvâŸ©
        use v - x
        simp_all [dist_eq_norm_sub']
      choose d hds hd using this
      refine âŸ¨(c ^ Â·), d, ?tendsto_c, .of_forall hds, ?tendsto_cdâŸ©
      case tendsto_c =>
        simp only [norm_pow]
        exact tendsto_pow_atTop_atTop_of_one_lt hc
      case tendsto_cd =>
        rw [atTop_basis.tendsto_iff (Metric.nhds_basis_ball_pow one_half_pos one_half_lt_one)]
        refine fun N _ â†¦ âŸ¨N, trivial, fun n hn â†¦ ?_âŸ©
        rw [Set.mem_Ici] at hn
        suffices â€–câ€– ^ n * â€–d nâ€– < 1 / (2 ^ N) by simpa [norm_smul]
        rw [â† lt_div_iffâ‚€' (by positivity)]
        refine (hd n).trans_le ?_
        grw [hn]
        Â· simp [mul_pow, div_eq_inv_mul]
        Â· norm_num1
    Â· rw [mem_tangentConeAt_iff_exists_seq]
      rintro âŸ¨c, d, hdâ‚€, hds, hcdâŸ©
      refine âŸ¨c, d, ?_, hds, hcdâŸ©
      replace hdâ‚€ := hdâ‚€.norm
      have hdâ‚€' : âˆ€á¶  n in .atTop, d n â‰  0 :=
        hcd.eventually_ne hyâ‚€ |>.mono fun _ â†¦ right_ne_zero_of_smul
      replace hcd := hcd.norm
      simp only [norm_smul, norm_zero, â† div_inv_eq_mul] at hdâ‚€ hcd
      refine .num ?_ (by simpa) hcd
      rw [â† inv_nhdsGT_zero (ğ•œ := â„), â† Filter.comap_inv, Filter.tendsto_comap_iff]
      simpa [Function.comp_def, tendsto_nhdsWithin_iff, hdâ‚€] using hdâ‚€'
  Â· rintro âŸ¨c, d, hc, hds, hcdâŸ©
    exact mem_tangentConeAt_of_seq atTop c d (tangentConeAt.lim_zero atTop hc hcd) hds hcd
