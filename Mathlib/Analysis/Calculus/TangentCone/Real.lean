/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.TangentCone.Basic
import Mathlib.Analysis.Convex.Topology

/-!
# Unique differentiability property in real normed spaces

In this file we prove that

- `uniqueDiffOn_convex`: a convex set with nonempty interior in a real normed space
  has the unique differentiability property;
- `uniqueDiffOn_Ioc` etc: intervals on the real line have the unique differentiability property.
-/

open Filter Set
open scoped Topology

theorem mem_tangentConeAt_of_pow_smul {r : ùïú} (hr‚ÇÄ : r ‚â† 0) (hr : ‚Äñr‚Äñ < 1)
    (hs : ‚àÄ·∂† n : ‚Ñï in atTop, x + r ^ n ‚Ä¢ y ‚àà s) : y ‚àà tangentConeAt ùïú s x := by
  apply mem_tangentConeAt_of_seq (c := fun n ‚Ü¶ (r ^ n)‚Åª¬π) _ hs
  ¬∑ simp [hr‚ÇÄ, tendsto_const_nhds]
  ¬∑ simpa using (tendsto_pow_atTop_nhds_zero_of_norm_lt_one hr).smul_const y

section RealNormed

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- If a subset of a real vector space contains an open segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
theorem mem_tangentConeAt_of_openSegment_subset {s : Set E} {x y : E} (h : openSegment ‚Ñù x y ‚äÜ s) :
    y - x ‚àà tangentConeAt ‚Ñù s x := by
  refine mem_tangentConeAt_of_pow_smul one_half_pos.ne' (by norm_num) ?_
  refine (eventually_ne_atTop 0).mono fun n hn ‚Ü¶ (h ?_)
  rw [openSegment_eq_image]
  refine ‚ü®(1 / 2) ^ n, ‚ü®?_, ?_‚ü©, ?_‚ü©
  ¬∑ exact pow_pos one_half_pos _
  ¬∑ exact pow_lt_one‚ÇÄ one_half_pos.le one_half_lt_one hn
  ¬∑ simp only [sub_smul, one_smul, smul_sub]; abel

@[deprecated (since := "2025-04-27")]
alias mem_tangentCone_of_openSegment_subset := mem_tangentConeAt_of_openSegment_subset

/-- If a subset of a real vector space contains a segment, then the direction of this
segment belongs to the tangent cone at its endpoints. -/
theorem mem_tangentConeAt_of_segment_subset {s : Set E} {x y : E} (h : segment ‚Ñù x y ‚äÜ s) :
    y - x ‚àà tangentConeAt ‚Ñù s x :=
  mem_tangentConeAt_of_openSegment_subset ((openSegment_subset_segment ‚Ñù x y).trans h)

@[deprecated (since := "2025-04-27")]
alias mem_tangentCone_of_segment_subset := mem_tangentConeAt_of_segment_subset

theorem Convex.span_tangentConeAt {s : Set E} (conv : Convex ‚Ñù s) (hs : (interior s).Nonempty)
    {x : E} (hx : x ‚àà closure s) : Submodule.span ‚Ñù (tangentConeAt ‚Ñù s x) = ‚ä§ := by
  rcases hs with ‚ü®y, hy‚ü©
  suffices y - x ‚àà interior (tangentConeAt ‚Ñù s x) by
    apply (Submodule.span ‚Ñù (tangentConeAt ‚Ñù s x)).eq_top_of_nonempty_interior'
    exact ‚ü®y - x, interior_mono Submodule.subset_span this‚ü©
  rw [mem_interior_iff_mem_nhds]
  replace hy : interior s ‚àà ùìù y := IsOpen.mem_nhds isOpen_interior hy
  apply mem_of_superset ((isOpenMap_sub_right x).image_mem_nhds hy)
  rintro _ ‚ü®z, zs, rfl‚ü©
  refine mem_tangentConeAt_of_openSegment_subset (Subset.trans ?_ interior_subset)
  exact conv.openSegment_closure_interior_subset_interior hx zs

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability at every point of its closure. -/
theorem uniqueDiffWithinAt_convex {s : Set E} (conv : Convex ‚Ñù s) (hs : (interior s).Nonempty)
    {x : E} (hx : x ‚àà closure s) : UniqueDiffWithinAt ‚Ñù s x := by
  simp [uniqueDiffWithinAt_iff, conv.span_tangentConeAt hs hx, hx]

/-- In a real vector space, a convex set with nonempty interior is a set of unique
differentiability. -/
theorem uniqueDiffOn_convex {s : Set E} (conv : Convex ‚Ñù s) (hs : (interior s).Nonempty) :
    UniqueDiffOn ‚Ñù s :=
  fun _ xs => uniqueDiffWithinAt_convex conv hs (subset_closure xs)

end RealNormed

section Real

theorem uniqueDiffOn_Ici (a : ‚Ñù) : UniqueDiffOn ‚Ñù (Ici a) :=
  uniqueDiffOn_convex (convex_Ici a) <| by simp only [interior_Ici, nonempty_Ioi]

theorem uniqueDiffOn_Iic (a : ‚Ñù) : UniqueDiffOn ‚Ñù (Iic a) :=
  uniqueDiffOn_convex (convex_Iic a) <| by simp only [interior_Iic, nonempty_Iio]

theorem uniqueDiffOn_Ioi (a : ‚Ñù) : UniqueDiffOn ‚Ñù (Ioi a) :=
  isOpen_Ioi.uniqueDiffOn

theorem uniqueDiffOn_Iio (a : ‚Ñù) : UniqueDiffOn ‚Ñù (Iio a) :=
  isOpen_Iio.uniqueDiffOn

theorem uniqueDiffOn_Icc {a b : ‚Ñù} (hab : a < b) : UniqueDiffOn ‚Ñù (Icc a b) :=
  uniqueDiffOn_convex (convex_Icc a b) <| by simp only [interior_Icc, nonempty_Ioo, hab]

theorem uniqueDiffOn_Ico (a b : ‚Ñù) : UniqueDiffOn ‚Ñù (Ico a b) :=
  if hab : a < b then
    uniqueDiffOn_convex (convex_Ico a b) <| by simp only [interior_Ico, nonempty_Ioo, hab]
  else by simp only [Ico_eq_empty hab, uniqueDiffOn_empty]

theorem uniqueDiffOn_Ioc (a b : ‚Ñù) : UniqueDiffOn ‚Ñù (Ioc a b) :=
  if hab : a < b then
    uniqueDiffOn_convex (convex_Ioc a b) <| by simp only [interior_Ioc, nonempty_Ioo, hab]
  else by simp only [Ioc_eq_empty hab, uniqueDiffOn_empty]

theorem uniqueDiffOn_Ioo (a b : ‚Ñù) : UniqueDiffOn ‚Ñù (Ioo a b) :=
  isOpen_Ioo.uniqueDiffOn

/-- The real interval `[0, 1]` is a set of unique differentiability. -/
theorem uniqueDiffOn_Icc_zero_one : UniqueDiffOn ‚Ñù (Icc (0 : ‚Ñù) 1) :=
  uniqueDiffOn_Icc zero_lt_one

theorem uniqueDiffWithinAt_Ioo {a b t : ‚Ñù} (ht : t ‚àà Set.Ioo a b) :
    UniqueDiffWithinAt ‚Ñù (Set.Ioo a b) t :=
  IsOpen.uniqueDiffWithinAt isOpen_Ioo ht

theorem uniqueDiffWithinAt_Ioi (a : ‚Ñù) : UniqueDiffWithinAt ‚Ñù (Ioi a) a :=
  uniqueDiffWithinAt_convex (convex_Ioi a) (by simp) (by simp)

theorem uniqueDiffWithinAt_Iio (a : ‚Ñù) : UniqueDiffWithinAt ‚Ñù (Iio a) a :=
  uniqueDiffWithinAt_convex (convex_Iio a) (by simp) (by simp)

theorem uniqueDiffWithinAt_Ici (x : ‚Ñù) : UniqueDiffWithinAt ‚Ñù (Ici x) x :=
  (uniqueDiffWithinAt_Ioi x).mono Set.Ioi_subset_Ici_self

theorem uniqueDiffWithinAt_Iic (x : ‚Ñù) : UniqueDiffWithinAt ‚Ñù (Iic x) x :=
  (uniqueDiffWithinAt_Iio x).mono Set.Iio_subset_Iic_self

end Real
