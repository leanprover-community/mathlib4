/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.TangentCone.Defs
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Analysis.Normed.Module.Basic

/-!
# Basic properties of tangent cones and sets with unique differentiability property

In this file we prove basic lemmas about `tangentConeAt`, `UniqueDiffWithinAt`,
and `UniqueDiffOn`.
-/

open Filter Set Metric NormedField
open scoped Topology Pointwise

namespace Filter

theorem HasBasis.mapâ‚‚ {Î¹Î± Î¹Î² : Type*} {Î± Î² Î³ : Type*} {la : Filter Î±} {lb : Filter Î²}
    {pa : Î¹Î± â†’ Prop} {sa : Î¹Î± â†’ Set Î±} {pb : Î¹Î² â†’ Prop} {sb : Î¹Î² â†’ Set Î²}
    (f : Î± â†’ Î² â†’ Î³) (ha : la.HasBasis pa sa) (hb : lb.HasBasis pb sb) :
    (la.mapâ‚‚ f lb).HasBasis (fun i : Î¹Î± Ã— Î¹Î² â†¦ pa i.1 âˆ§ pb i.2)
      fun i â†¦ ((sa i.1).image2 f (sb i.2)) := by
  simpa [map_prod_eq_mapâ‚‚] using (ha.prod hb).map f.uncurry

@[to_additive]
theorem HasBasis.smul {Î¹Î± Î¹Î² : Type*} {Î± Î² : Type*} [SMul Î± Î²]
    {la : Filter Î±} {lb : Filter Î²} {pa : Î¹Î± â†’ Prop} {sa : Î¹Î± â†’ Set Î±}
    {pb : Î¹Î² â†’ Prop} {sb : Î¹Î² â†’ Set Î²}
    (ha : la.HasBasis pa sa) (hb : lb.HasBasis pb sb) :
    (la â€¢ lb).HasBasis (fun i : Î¹Î± Ã— Î¹Î² â†¦ pa i.1 âˆ§ pb i.2) fun i â†¦ (sa i.1 â€¢ sb i.2) :=
  ha.mapâ‚‚ (Â· â€¢ Â·) hb

theorem HasBasis.eq_top_iff {Î¹ : Sort*} {Î± : Type*} {l : Filter Î±} {p : Î¹ â†’ Prop}
    {s : Î¹ â†’ Set Î±} (h : l.HasBasis p s) : l = âŠ¤ â†” âˆ€ i, p i â†’ s i = univ := by
  simp [â† top_le_iff, h.ge_iff]

theorem univ_smul_nhds_zero {Gâ‚€ X : Type*} [GroupWithZero Gâ‚€] [Zero X] [MulActionWithZero Gâ‚€ X]
    [TopologicalSpace Gâ‚€] [(ğ“[â‰ ] (0 : Gâ‚€)).NeBot] [TopologicalSpace X] [ContinuousSMul Gâ‚€ X]
    {s : Set X} (hs : s âˆˆ ğ“ 0) :
    (univ : Set Gâ‚€) â€¢ s = univ := by
  refine eq_univ_of_forall fun x â†¦ ?_
  have : Tendsto (Â· â€¢ x) (ğ“ (0 : Gâ‚€)) (ğ“ 0) := by
    rw [â† zero_smul Gâ‚€ x]
    exact tendsto_id.smul tendsto_const_nhds
  rcases nonempty_of_mem (inter_mem_nhdsWithin {0}á¶œ <| mem_map.1 <| this hs) with âŸ¨c, hcâ‚€, hcâŸ©
  refine âŸ¨câ»Â¹, trivial, c â€¢ x, hc, ?_âŸ©
  simp_all

@[simp]
theorem top_smul_nhds_zero {Gâ‚€ X : Type*} [GroupWithZero Gâ‚€] [Zero X] [MulActionWithZero Gâ‚€ X]
    [TopologicalSpace Gâ‚€] [(ğ“[â‰ ] (0 : Gâ‚€)).NeBot] [TopologicalSpace X] [ContinuousSMul Gâ‚€ X] :
    (âŠ¤ : Filter Gâ‚€) â€¢ ğ“ (0 : X) = âŠ¤ := by
  rw [(hasBasis_top.smul (basis_sets _)).eq_top_iff]
  rintro âŸ¨_, sâŸ© âŸ¨-, hsâŸ©
  exact univ_smul_nhds_zero hs

end Filter

variable {ğ•œ E : Type*}

section SMulMonoid

variable [AddCommMonoid E] [SMul ğ•œ E] [TopologicalSpace E] {s t : Set E} {x : E}

@[gcongr]
theorem tangentConeAt_mono (h : s âŠ† t) : tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := fun y hy â†¦
  hy.mono <| by gcongr

@[deprecated (since := "2025-04-27")] alias tangentCone_mono := tangentConeAt_mono

/--
Given `x âˆˆ s` and a field extension `ğ•œ âŠ† ğ•œ'`, the tangent cone of `s` at `x` with
respect to `ğ•œ` is contained in the tangent cone of `s` at `x` with respect to `ğ•œ'`.
-/
theorem tangentConeAt_mono_field
    {ğ•œ' : Type*} [Monoid ğ•œ'] [SMul ğ•œ ğ•œ'] [MulAction ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E] :
    tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ' s x := by
  refine fun y hy â†¦ hy.mono ?_
  rw [â† smul_one_smul (Filter ğ•œ')]
  grw [le_top (a := âŠ¤ â€¢ 1)]

theorem Filter.HasBasis.tangentConeAt_eq_biInter_closure {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set E}
    (h : (ğ“ 0).HasBasis p U) :
    tangentConeAt ğ•œ s x = â‹‚ (i) (_ : p i), closure ((univ : Set ğ•œ) â€¢ (U i âˆ© (x + Â·) â»Â¹' s)) := by
  ext y
  simp only [tangentConeAt, mem_setOf_eq, mem_iInterâ‚‚, â† mapâ‚‚_smul, â† map_prod_eq_mapâ‚‚,
    ((nhdsWithin_hasBasis h _).top_prod.map _).clusterPt_iff_forall_mem_closure, image_prod,
    image2_smul]

theorem tangentConeAt_eq_biInter_closure :
    tangentConeAt ğ•œ s x = â‹‚ U âˆˆ ğ“ 0, closure ((univ : Set ğ•œ) â€¢ (U âˆ© (x + Â·) â»Â¹' s)) :=
  (basis_sets _).tangentConeAt_eq_biInter_closure

variable [ContinuousAdd E]

theorem tangentConeAt_mono_nhds (h : ğ“[s] x â‰¤ ğ“[t] x) :
    tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := by
  refine fun y hy â†¦ hy.mono ?_
  gcongr _ â€¢ ?_
  rw [nhdsWithin_le_iff]
  suffices Tendsto (x + Â·) (ğ“[(x + Â·) â»Â¹' s] 0) (ğ“[s] x) from
    this.mono_right h |> tendsto_nhdsWithin_iff.mp |>.2
  refine .inf ?_ (mapsTo_preimage _ _).tendsto
  exact (continuous_add_left x).tendsto' 0 x (add_zero _)

@[deprecated (since := "2025-04-27")] alias tangentCone_mono_nhds := tangentConeAt_mono_nhds

/-- Tangent cone of `s` at `x` depends only on `ğ“[s] x`. -/
theorem tangentConeAt_congr (h : ğ“[s] x = ğ“[t] x) : tangentConeAt ğ•œ s x = tangentConeAt ğ•œ t x :=
  Subset.antisymm (tangentConeAt_mono_nhds h.le) (tangentConeAt_mono_nhds h.ge)

@[deprecated (since := "2025-04-27")] alias tangentCone_congr := tangentConeAt_congr

/-- Intersecting with a neighborhood of the point does not change the tangent cone. -/
theorem tangentConeAt_inter_nhds (ht : t âˆˆ ğ“ x) : tangentConeAt ğ•œ (s âˆ© t) x = tangentConeAt ğ•œ s x :=
  tangentConeAt_congr (nhdsWithin_restrict' _ ht).symm

@[deprecated (since := "2025-04-27")] alias tangentCone_inter_nhds := tangentConeAt_inter_nhds

end SMulMonoid

section SMulGroup

variable [AddCommGroup E] [SMul ğ•œ E]
  [TopologicalSpace E] [ContinuousAdd E] [ContinuousConstSMul ğ•œ E]
  {s t : Set E} {x : E}

@[simp]
theorem tangentConeAt_closure : tangentConeAt ğ•œ (closure s) x = tangentConeAt ğ•œ s x := by
  refine Subset.antisymm ?_ (tangentConeAt_mono subset_closure)
  simp only [(nhds_basis_opens _).tangentConeAt_eq_biInter_closure]
  refine iInterâ‚‚_mono fun U hU â†¦ closure_minimal ?_ isClosed_closure
  grw [(isOpenMap_add_left x).preimage_closure_subset_closure_preimage, hU.2.inter_closure,
    set_smul_closure_subset]

end SMulGroup

section TVS

@[simp]
theorem tangentConeAt_univ [DivisionSemiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E]
    [TopologicalSpace ğ•œ] [(ğ“[â‰ ] (0 : ğ•œ)).NeBot] [TopologicalSpace E] [ContinuousSMul ğ•œ E] {x : E} :
    tangentConeAt ğ•œ univ x = univ := by
  simp [tangentConeAt]

@[deprecated (since := "2025-04-27")] alias tangentCone_univ := tangentConeAt_univ

/-
TODO: restore, deprecate
/-- Auxiliary lemma ensuring that, under the assumptions defining the tangent cone,
the sequence `d` tends to 0 at infinity. -/
theorem tangentConeAt.lim_zero {Î± : Type*} (l : Filter Î±) {c : Î± â†’ ğ•œ} {d : Î± â†’ E}
    (hc : Tendsto (fun n => â€–c nâ€–) l atTop) (hd : Tendsto (fun n => c n â€¢ d n) l (ğ“ y)) :
    Tendsto d l (ğ“ 0) := by
  have : âˆ€á¶  n in l, (c n)â»Â¹ â€¢ c n â€¢ d n = d n :=
    (eventually_ne_of_tendsto_norm_atTop hc 0).mono fun n hn â†¦ inv_smul_smulâ‚€ hn (d n)
  rw [tendsto_norm_atTop_iff_cobounded] at hc
  simpa using Tendsto.congr' this <| (tendsto_invâ‚€_cobounded.comp hc).smul hd
-/

end TVS

section Normed
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {x y : E} {s t : Set E}

/-- The tangent cone at a non-isolated point contains `0`. -/
theorem zero_mem_tangentCone {s : Set E} {x : E} (hx : x âˆˆ closure s) :
    0 âˆˆ tangentConeAt ğ•œ s x := by
  /- Take a sequence `d n` tending to `0` such that `x + d n âˆˆ s`. Taking `c n` of the order
  of `1 / (d n) ^ (1/2)`, then `c n` tends to infinity, but `c n â€¢ d n` tends to `0`. By definition,
  this shows that `0` belongs to the tangent cone. -/
  obtain âŸ¨u, -, hu, u_limâŸ© :
      âˆƒ u, StrictAnti u âˆ§ (âˆ€ (n : â„•), 0 < u n âˆ§ u n < 1) âˆ§ Tendsto u atTop (ğ“ (0 : â„)) :=
    exists_seq_strictAnti_tendsto' one_pos
  choose u_pos u_lt_one using hu
  choose v hvs hvu using fun n â†¦ Metric.mem_closure_iff.mp hx _ (mul_pos (u_pos n) (u_pos n))
  let d n := v n - x
  let âŸ¨r, hrâŸ© := exists_one_lt_norm ğ•œ
  have A n := exists_nat_pow_near (one_le_inv_iffâ‚€.mpr âŸ¨u_pos n, (u_lt_one n).leâŸ©) hr
  choose m hm_le hlt_m using A
  set c := fun n â†¦ r ^ (m n + 1)
  have c_lim : Tendsto (fun n â†¦ â€–c nâ€–) atTop atTop := by
    simp only [c, norm_pow]
    refine tendsto_atTop_mono (fun n â†¦ (hlt_m n).le) <| .inv_tendsto_nhdsGT_zero ?_
    exact tendsto_nhdsWithin_iff.mpr âŸ¨u_lim, .of_forall u_posâŸ©
  refine âŸ¨c, d, .of_forall <| by simpa [d], c_lim, ?_âŸ©
  have Hle n : â€–c n â€¢ d nâ€– â‰¤ â€–râ€– * u n := by
    specialize u_pos n
    calc
      â€–c n â€¢ d nâ€– â‰¤ (u n)â»Â¹ * â€–râ€– * (u n * u n) := by
        simp only [c, norm_smul, norm_pow, pow_succ, norm_mul, d, â† dist_eq_norm']
        gcongr
        exacts [hm_le n, (hvu n).le]
      _ = â€–râ€– * u n := by field_simp
  refine squeeze_zero_norm Hle ?_
  simpa using tendsto_const_nhds.mul u_lim

/-- If `x` is not an accumulation point of `s, then the tangent cone of `s` at `x`
is a subset of `{0}`. -/
theorem tangentConeAt_subset_zero (hx : Â¬AccPt x (ğ“Ÿ s)) : tangentConeAt ğ•œ s x âŠ† 0 := by
  rintro y âŸ¨c, d, hds, hc, hcdâŸ©
  suffices âˆ€á¶  n in .atTop, d n = 0 from
    tendsto_nhds_unique hcd <| tendsto_const_nhds.congr' <| this.mono fun n hn â†¦ by simp [hn]
  simp only [accPt_iff_frequently, not_frequently, not_and', ne_eq, not_not] at hx
  have : Tendsto (x + d Â·) atTop (ğ“ x) := by
    simpa using tendsto_const_nhds.add (tangentConeAt.lim_zero _ hc hcd)
  filter_upwards [this.eventually hx, hds] with n hâ‚ hâ‚‚
  simpa using hâ‚ hâ‚‚

theorem UniqueDiffWithinAt.accPt [Nontrivial E] (h : UniqueDiffWithinAt ğ•œ s x) : AccPt x (ğ“Ÿ s) := by
  by_contra! h'
  have : Dense (Submodule.span ğ•œ (0 : Set E) : Set E) :=
    h.1.mono <| by gcongr; exact tangentConeAt_subset_zero h'
  simp [dense_iff_closure_eq] at this

end Normed

section UniqueDiff

/-!
### Properties of `UniqueDiffWithinAt` and `UniqueDiffOn`

This section is devoted to properties of the predicates `UniqueDiffWithinAt` and `UniqueDiffOn`. -/

section Module
variable [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {x y : E} {s t : Set E}

theorem UniqueDiffOn.uniqueDiffWithinAt {s : Set E} {x} (hs : UniqueDiffOn ğ•œ s) (h : x âˆˆ s) :
    UniqueDiffWithinAt ğ•œ s x :=
  hs x h

@[simp]
theorem uniqueDiffWithinAt_univ : UniqueDiffWithinAt ğ•œ univ x := by
  rw [uniqueDiffWithinAt_iff, tangentConeAt_univ]
  simp

@[simp]
theorem uniqueDiffOn_univ : UniqueDiffOn ğ•œ (univ : Set E) :=
  fun _ _ => uniqueDiffWithinAt_univ

theorem uniqueDiffOn_empty : UniqueDiffOn ğ•œ (âˆ… : Set E) :=
  fun _ hx => hx.elim

theorem UniqueDiffWithinAt.congr_pt (h : UniqueDiffWithinAt ğ•œ s x) (hy : x = y) :
    UniqueDiffWithinAt ğ•œ s y := hy â–¸ h

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  [Module ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and that `x âˆˆ s` is a point
of unique differentiability with respect to the set `s` and the smaller field `ğ•œ`, then `x` is also
a point of unique differentiability with respect to the set `s` and the larger field `ğ•œ'`.
-/
theorem UniqueDiffWithinAt.mono_field (hâ‚‚s : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ' s x := by
  simp_all only [uniqueDiffWithinAt_iff, and_true]
  apply Dense.mono _ hâ‚‚s.1
  trans â†‘(Submodule.span ğ•œ (tangentConeAt ğ•œ' s x))
  <;> simp [Submodule.span_mono tangentConeAt_mono_field]

/--
Assume that `E` is a normed vector space over normed fields `ğ•œ âŠ† ğ•œ'` and all points of `s` are
points of unique differentiability with respect to the smaller field `ğ•œ`, then they are also points
of unique differentiability with respect to the larger field `ğ•œ`.
-/
theorem UniqueDiffOn.mono_field (hâ‚‚s : UniqueDiffOn ğ•œ s) :
    UniqueDiffOn ğ•œ' s := fun x hx â†¦ (hâ‚‚s x hx).mono_field

end Module

section TVS
variable [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {x y : E} {s t : Set E}
variable [ContinuousAdd E] [ContinuousSMul ğ•œ E]

theorem UniqueDiffWithinAt.mono_nhds (h : UniqueDiffWithinAt ğ•œ s x) (st : ğ“[s] x â‰¤ ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ t x := by
  simp only [uniqueDiffWithinAt_iff] at *
  rw [mem_closure_iff_nhdsWithin_neBot] at h âŠ¢
  exact âŸ¨h.1.mono <| Submodule.span_mono <| tangentConeAt_mono_nhds st, h.2.mono stâŸ©

theorem UniqueDiffWithinAt.mono (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† t) :
    UniqueDiffWithinAt ğ•œ t x :=
  h.mono_nhds <| nhdsWithin_mono _ st

theorem uniqueDiffWithinAt_congr (st : ğ“[s] x = ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ s x â†” UniqueDiffWithinAt ğ•œ t x :=
  âŸ¨fun h => h.mono_nhds <| le_of_eq st, fun h => h.mono_nhds <| le_of_eq st.symmâŸ©

theorem uniqueDiffWithinAt_inter (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict' _ ht).symm

theorem UniqueDiffWithinAt.inter (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter ht).2 hs

theorem uniqueDiffWithinAt_inter' (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict'' _ ht).symm

theorem UniqueDiffWithinAt.inter' (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter' ht).2 hs

theorem uniqueDiffWithinAt_of_mem_nhds (h : s âˆˆ ğ“ x) : UniqueDiffWithinAt ğ•œ s x := by
  simpa only [univ_inter] using uniqueDiffWithinAt_univ.inter h

theorem IsOpen.uniqueDiffWithinAt (hs : IsOpen s) (xs : x âˆˆ s) : UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_of_mem_nhds (IsOpen.mem_nhds hs xs)

theorem UniqueDiffOn.inter (hs : UniqueDiffOn ğ•œ s) (ht : IsOpen t) : UniqueDiffOn ğ•œ (s âˆ© t) :=
  fun x hx => (hs x hx.1).inter (IsOpen.mem_nhds ht hx.2)

theorem IsOpen.uniqueDiffOn (hs : IsOpen s) : UniqueDiffOn ğ•œ s :=
  fun _ hx => IsOpen.uniqueDiffWithinAt hs hx

end TVS

section Normed
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {x y : E} {s t : Set E}

@[simp]
theorem uniqueDiffWithinAt_closure :
    UniqueDiffWithinAt ğ•œ (closure s) x â†” UniqueDiffWithinAt ğ•œ s x := by
  simp [uniqueDiffWithinAt_iff]

protected alias âŸ¨UniqueDiffWithinAt.of_closure, UniqueDiffWithinAt.closureâŸ© :=
  uniqueDiffWithinAt_closure

theorem UniqueDiffWithinAt.mono_closure (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† closure t) :
    UniqueDiffWithinAt ğ•œ t x :=
  (h.mono st).of_closure

end Normed

end UniqueDiff
