/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Analysis.Calculus.TangentCone.Defs
public import Mathlib.Topology.Algebra.Group.Basic

/-!
# Basic properties of tangent cones and sets with unique differentiability property

In this file we prove basic lemmas about `tangentConeAt`, `UniqueDiffWithinAt`,
and `UniqueDiffOn`.
-/

public section

open Filter Set Metric
open scoped Topology Pointwise

variable {ğ•œ E : Type*}

section SMul

variable [AddCommGroup E] [SMul ğ•œ E] [TopologicalSpace E] {s t : Set E} {x : E}

@[gcongr]
theorem tangentConeAt_mono (h : s âŠ† t) : tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := by
  simp only [tangentConeAt_def, setOf_subset_setOf]
  refine fun y hy â†¦ hy.mono ?_
  gcongr

/--
Given `x âˆˆ s` and a semiring extension `ğ•œ âŠ† ğ•œ'`, the tangent cone of `s` at `x` with
respect to `ğ•œ` is contained in the tangent cone of `s` at `x` with respect to `ğ•œ'`.
-/
theorem tangentConeAt_mono_field
    {ğ•œ' : Type*} [Monoid ğ•œ'] [SMul ğ•œ ğ•œ'] [MulAction ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E] :
    tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ' s x := by
  simp only [tangentConeAt_def, setOf_subset_setOf]
  refine fun y hy â†¦ hy.mono ?_
  rw [â† smul_one_smul (Filter ğ•œ')]
  grw [le_top (a := âŠ¤ â€¢ 1)]

theorem Filter.HasBasis.tangentConeAt_eq_biInter_closure {Î¹} {p : Î¹ â†’ Prop} {U : Î¹ â†’ Set E}
    (h : (ğ“ 0).HasBasis p U) :
    tangentConeAt ğ•œ s x = â‹‚ (i) (_ : p i), closure ((univ : Set ğ•œ) â€¢ (U i âˆ© (x + Â·) â»Â¹' s)) := by
  ext y
  simp only [tangentConeAt_def, mem_setOf_eq, mem_iInterâ‚‚, â† mapâ‚‚_smul, â† map_prod_eq_mapâ‚‚,
    ((nhdsWithin_hasBasis h _).top_prod.map _).clusterPt_iff_forall_mem_closure, image_prod,
    image2_smul]

theorem tangentConeAt_eq_biInter_closure :
    tangentConeAt ğ•œ s x = â‹‚ U âˆˆ ğ“ 0, closure ((univ : Set ğ•œ) â€¢ (U âˆ© (x + Â·) â»Â¹' s)) :=
  (basis_sets _).tangentConeAt_eq_biInter_closure

variable [ContinuousAdd E]

theorem tangentConeAt_mono_nhds (h : ğ“[s] x â‰¤ ğ“[t] x) :
    tangentConeAt ğ•œ s x âŠ† tangentConeAt ğ•œ t x := by
  simp only [tangentConeAt_def, setOf_subset_setOf]
  refine fun y hy â†¦ hy.mono ?_
  gcongr _ â€¢ ?_
  rw [nhdsWithin_le_iff]
  suffices Tendsto (x + Â·) (ğ“[(x + Â·) â»Â¹' s] 0) (ğ“[s] x) from
    this.mono_right h |> tendsto_nhdsWithin_iff.mp |>.2
  refine .inf ?_ (mapsTo_preimage _ _).tendsto
  exact (continuous_add_left x).tendsto' 0 x (add_zero _)

/-- Tangent cone of `s` at `x` depends only on `ğ“[s] x`. -/
theorem tangentConeAt_congr (h : ğ“[s] x = ğ“[t] x) : tangentConeAt ğ•œ s x = tangentConeAt ğ•œ t x :=
  Subset.antisymm (tangentConeAt_mono_nhds h.le) (tangentConeAt_mono_nhds h.ge)

/-- Intersecting with a neighborhood of the point does not change the tangent cone. -/
theorem tangentConeAt_inter_nhds (ht : t âˆˆ ğ“ x) : tangentConeAt ğ•œ (s âˆ© t) x = tangentConeAt ğ•œ s x :=
  tangentConeAt_congr (nhdsWithin_restrict' _ ht).symm

theorem mem_closure_of_nonempty_tangentConeAt (h : (tangentConeAt ğ•œ s x).Nonempty) :
    x âˆˆ closure s := by
  rcases h with âŸ¨y, hyâŸ©
  rcases exists_fun_of_mem_tangentConeAt hy with âŸ¨Î¹, l, hl, -, d, hd, hds, -âŸ©
  refine mem_closure_of_tendsto ?_ hds
  simpa using tendsto_const_nhds.add hd

variable [ContinuousConstSMul ğ•œ E]

@[simp]
theorem tangentConeAt_closure : tangentConeAt ğ•œ (closure s) x = tangentConeAt ğ•œ s x := by
  refine Subset.antisymm ?_ (tangentConeAt_mono subset_closure)
  simp only [(nhds_basis_opens _).tangentConeAt_eq_biInter_closure]
  refine iInterâ‚‚_mono fun U hU â†¦ closure_minimal ?_ isClosed_closure
  grw [(isOpenMap_add_left x).preimage_closure_subset_closure_preimage, hU.2.inter_closure,
    set_smul_closure_subset]

end SMul

section Module

variable [AddCommGroup E] [Semiring ğ•œ] [Module ğ•œ E] [TopologicalSpace E] [ContinuousAdd E]
  {s t : Set E} {x : E}

omit [ContinuousAdd E] in
theorem UniqueDiffWithinAt.mono (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† t) :
    UniqueDiffWithinAt ğ•œ t x := by
  rw [uniqueDiffWithinAt_iff] at *
  grw [â† st]
  exact h

omit [ContinuousAdd E] in
protected theorem UniqueDiffWithinAt.closure (h : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ (closure s) x :=
  h.mono subset_closure

theorem UniqueDiffWithinAt.mono_nhds (h : UniqueDiffWithinAt ğ•œ s x) (st : ğ“[s] x â‰¤ ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ t x := by
  simp only [uniqueDiffWithinAt_iff] at *
  rw [mem_closure_iff_nhdsWithin_neBot] at h âŠ¢
  exact âŸ¨h.1.mono <| Submodule.span_mono <| tangentConeAt_mono_nhds st, h.2.mono stâŸ©

theorem uniqueDiffWithinAt_congr (st : ğ“[s] x = ğ“[t] x) :
    UniqueDiffWithinAt ğ•œ s x â†” UniqueDiffWithinAt ğ•œ t x :=
  âŸ¨fun h => h.mono_nhds <| le_of_eq st, fun h => h.mono_nhds <| le_of_eq st.symmâŸ©

theorem uniqueDiffWithinAt_inter (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict' _ ht).symm

theorem UniqueDiffWithinAt.inter (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“ x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter ht).2 hs

theorem UniqueDiffOn.inter (hs : UniqueDiffOn ğ•œ s) (ht : IsOpen t) : UniqueDiffOn ğ•œ (s âˆ© t) :=
  fun x hx => (hs x hx.1).inter (IsOpen.mem_nhds ht hx.2)

theorem uniqueDiffWithinAt_inter' (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x â†” UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_congr <| (nhdsWithin_restrict'' _ ht).symm

theorem UniqueDiffWithinAt.inter' (hs : UniqueDiffWithinAt ğ•œ s x) (ht : t âˆˆ ğ“[s] x) :
    UniqueDiffWithinAt ğ•œ (s âˆ© t) x :=
  (uniqueDiffWithinAt_inter' ht).2 hs

/-- The tangent cone at a non-isolated point contains `0`. -/
theorem zero_mem_tangentConeAt (hx : x âˆˆ closure s) :
    0 âˆˆ tangentConeAt ğ•œ s x := by
  rw [mem_closure_iff_frequently] at hx
  apply mem_tangentConeAt_of_frequently (ğ“ x) 1 (Â· + (-x))
  Â· exact Continuous.tendsto' (by fun_prop) _ _ (by simp)
  Â· simpa
  Â· simp only [Pi.one_apply, one_smul]
    exact Continuous.tendsto' (by fun_prop) _ _ (by simp)

@[deprecated (since := "2026-01-21")]
alias zero_mem_tangentCone := zero_mem_tangentConeAt

@[simp]
theorem zero_mem_tangentConeAt_iff : 0 âˆˆ tangentConeAt ğ•œ s x â†” x âˆˆ closure s :=
  âŸ¨fun h â†¦ mem_closure_of_nonempty_tangentConeAt âŸ¨_, hâŸ©, zero_mem_tangentConeAtâŸ©

/-- If `x` is not an accumulation point of `s`, then the tangent cone of `s` at `x`
is a subset of `{0}`. -/
theorem tangentConeAt_subset_zero [T2Space E] (hx : Â¬AccPt x (ğ“Ÿ s)) : tangentConeAt ğ•œ s x âŠ† 0 := by
  intro y hy
  rcases exists_fun_of_mem_tangentConeAt hy with âŸ¨Î¹, l, hl, c, d, hdâ‚€, hds, hcdâŸ©
  have Hâ‚ : Tendsto (x + d Â·) l (ğ“ x) := by
    simpa using tendsto_const_nhds.add hdâ‚€
  have Hâ‚‚ : âˆ€á¶  n in l, d n = 0 := by
    simp only [accPt_iff_frequently, not_frequently, not_and', ne_eq, not_not] at hx
    simpa using hds.mp (Hâ‚.eventually hx)
  have Hâ‚ƒ : âˆ€á¶  n in l, c n â€¢ d n = 0 := Hâ‚‚.mono fun n hn â†¦ by simp [hn]
  simpa using tendsto_nhds_unique_of_eventuallyEq hcd tendsto_const_nhds Hâ‚ƒ

theorem AccPt.of_mem_tangentConeAt_ne_zero [T2Space E] {y : E} (hy : y âˆˆ tangentConeAt ğ•œ s x)
    (hyâ‚€ : y â‰  0) : AccPt x (ğ“Ÿ s) := by
  contrapose! hyâ‚€
  exact tangentConeAt_subset_zero hyâ‚€ hy

theorem UniqueDiffWithinAt.accPt [T2Space E] [Nontrivial E] (h : UniqueDiffWithinAt ğ•œ s x) :
    AccPt x (ğ“Ÿ s) := by
  by_contra! h'
  have : Dense (Submodule.span ğ•œ (0 : Set E) : Set E) :=
    h.1.mono <| by gcongr; exact tangentConeAt_subset_zero h'
  simp [dense_iff_closure_eq] at this

end Module

section TVS

variable [DivisionSemiring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace ğ•œ]
  [TopologicalSpace E] [ContinuousSMul ğ•œ E] {s : Set E} {x y : E}

theorem mem_tangentConeAt_of_add_smul_mem {Î± : Type*} {l : Filter Î±} [l.NeBot] {c : Î± â†’ ğ•œ}
    (hcâ‚€ : Tendsto c l (ğ“[â‰ ] 0)) (hmem : âˆ€á¶  n in l, x + c n â€¢ y âˆˆ s) :
    y âˆˆ tangentConeAt ğ•œ s x := by
  rw [tendsto_nhdsWithin_iff] at hcâ‚€
  refine mem_tangentConeAt_of_seq l câ»Â¹ (c Â· â€¢ y) ?_ hmem ?_
  Â· simpa using hcâ‚€.1.smul (tendsto_const_nhds (x := y))
  Â· refine tendsto_nhds_of_eventually_eq <| hcâ‚€.2.mono fun n hn â†¦ ?_
    simp_all

variable [(ğ“[â‰ ] (0 : ğ•œ)).NeBot]

@[simp]
theorem tangentConeAt_univ : tangentConeAt ğ•œ univ x = univ := by
  simp [tangentConeAt]

theorem tangentConeAt_of_mem_nhds [ContinuousAdd E] (h : s âˆˆ ğ“ x) : tangentConeAt ğ•œ s x = univ := by
  rw [â† s.univ_inter, tangentConeAt_inter_nhds h, tangentConeAt_univ]

end TVS

section UniqueDiff

/-!
### Properties of `UniqueDiffWithinAt` and `UniqueDiffOn`

This section is devoted to properties of the predicates `UniqueDiffWithinAt` and `UniqueDiffOn`. -/

section Semiring
variable [Semiring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {x y : E} {s t : Set E}

theorem uniqueDiffOn_empty : UniqueDiffOn ğ•œ (âˆ… : Set E) :=
  fun _ hx => hx.elim

theorem UniqueDiffWithinAt.congr_pt (h : UniqueDiffWithinAt ğ•œ s x) (hy : x = y) :
    UniqueDiffWithinAt ğ•œ s y := hy â–¸ h

variable {ğ•œ' : Type*} [Semiring ğ•œ'] [SMul ğ•œ ğ•œ'] [Module ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]

/--
Assume that `E` is a normed vector space over semirings `ğ•œ âŠ† ğ•œ'` and that `x âˆˆ s` is a point
of unique differentiability with respect to the set `s` and the smaller semiring `ğ•œ`,
then `x` is also a point of unique differentiability with respect to the set `s`
and the larger semiring `ğ•œ'`.
-/
theorem UniqueDiffWithinAt.mono_field (hs : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ' s x := by
  simp_all only [uniqueDiffWithinAt_iff, and_true]
  apply Dense.mono _ hs.1
  trans â†‘(Submodule.span ğ•œ (tangentConeAt ğ•œ' s x)) <;>
    simp [Submodule.span_mono tangentConeAt_mono_field]

/--
Assume that `E` is a normed vector space over semirings `ğ•œ âŠ† ğ•œ'`
and all points of `s` are points of unique differentiability
with respect to the smaller semiring `ğ•œ`,
then they are also points of unique differentiability with respect to the larger semiring `ğ•œ`.
-/
theorem UniqueDiffOn.mono_field (hs : UniqueDiffOn ğ•œ s) : UniqueDiffOn ğ•œ' s :=
  fun x hx â†¦ (hs x hx).mono_field

variable [ContinuousAdd E] [ContinuousConstSMul ğ•œ E]

@[simp]
theorem uniqueDiffWithinAt_closure :
    UniqueDiffWithinAt ğ•œ (closure s) x â†” UniqueDiffWithinAt ğ•œ s x := by
  simp [uniqueDiffWithinAt_iff]

protected alias âŸ¨UniqueDiffWithinAt.of_closure, _âŸ© := uniqueDiffWithinAt_closure

theorem UniqueDiffWithinAt.mono_closure (h : UniqueDiffWithinAt ğ•œ s x) (st : s âŠ† closure t) :
    UniqueDiffWithinAt ğ•œ t x :=
  (h.mono st).of_closure

end Semiring

section DivisionSemiring

variable [DivisionSemiring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [TopologicalSpace ğ•œ] [(ğ“[â‰ ] (0 : ğ•œ)).NeBot] [ContinuousSMul ğ•œ E]
  {x y : E} {s t : Set E}

@[simp]
theorem uniqueDiffWithinAt_univ : UniqueDiffWithinAt ğ•œ univ x := by
  rw [uniqueDiffWithinAt_iff, tangentConeAt_univ]
  simp

@[simp]
theorem uniqueDiffOn_univ : UniqueDiffOn ğ•œ (univ : Set E) :=
  fun _ _ => uniqueDiffWithinAt_univ

variable [ContinuousAdd E]

theorem uniqueDiffWithinAt_of_mem_nhds (h : s âˆˆ ğ“ x) : UniqueDiffWithinAt ğ•œ s x := by
  simpa only [univ_inter] using uniqueDiffWithinAt_univ.inter h

theorem IsOpen.uniqueDiffWithinAt (hs : IsOpen s) (xs : x âˆˆ s) : UniqueDiffWithinAt ğ•œ s x :=
  uniqueDiffWithinAt_of_mem_nhds (IsOpen.mem_nhds hs xs)

theorem IsOpen.uniqueDiffOn (hs : IsOpen s) : UniqueDiffOn ğ•œ s :=
  fun _ hx => IsOpen.uniqueDiffWithinAt hs hx

end DivisionSemiring

end UniqueDiff
