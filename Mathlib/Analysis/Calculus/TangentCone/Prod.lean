/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.TangentCone.Defs
public import Mathlib.LinearAlgebra.Prod
public import Mathlib.Topology.Algebra.Monoid
import Mathlib.Analysis.Calculus.TangentCone.Basic

/-!
# Product of sets with unique differentiability property

In this file we prove that the product of two sets with unique differentiability property
has the same property, see `UniqueDiffOn.prod`.
-/

public section

open Filter Set
open scoped Topology

variable {ð•œ E F : Type*} [Semiring ð•œ]
  [AddCommGroup E] [Module ð•œ E] [TopologicalSpace E] [ContinuousAdd E] [ContinuousConstSMul ð•œ E]
  [AddCommGroup F] [Module ð•œ F] [TopologicalSpace F] [ContinuousAdd F] [ContinuousConstSMul ð•œ F]
  {x : E} {s : Set E} {y : F} {t : Set F}

/-- The tangent cone of a product contains the tangent cone of its left factor. -/
theorem subset_tangentConeAt_prod_left (ht : y âˆˆ closure t) :
    LinearMap.inl ð•œ E F '' tangentConeAt ð•œ s x âŠ† tangentConeAt ð•œ (s Ã—Ë¢ t) (x, y) := by
  rw [â† tangentConeAt_closure (s := s Ã—Ë¢ t), closure_prod_eq]
  rintro _ âŸ¨z, hz, rflâŸ©
  rcases exists_fun_of_mem_tangentConeAt hz with âŸ¨Î¹, l, hl, c, d, hdâ‚€, hds, hcdâŸ©
  refine mem_tangentConeAt_of_seq l c (fun n â†¦ (d n, 0)) (hdâ‚€.prodMk_nhds tendsto_const_nhds)
    (hds.mono fun n hn â†¦ by simp [ht, subset_closure hn]) ?_
  simpa using hcd.prodMk_nhds tendsto_const_nhds

/-- The tangent cone of a product contains the tangent cone of its right factor. -/
theorem subset_tangentConeAt_prod_right (hs : x âˆˆ closure s) :
    LinearMap.inr ð•œ E F '' tangentConeAt ð•œ t y âŠ† tangentConeAt ð•œ (s Ã—Ë¢ t) (x, y) := by
  rw [â† tangentConeAt_closure (s := s Ã—Ë¢ t), closure_prod_eq]
  rintro _ âŸ¨z, hz, rflâŸ©
  rcases exists_fun_of_mem_tangentConeAt hz with âŸ¨Î¹, l, hl, c, d, hdâ‚€, hds, hcdâŸ©
  refine mem_tangentConeAt_of_seq l c (fun n â†¦ (0, d n)) (tendsto_const_nhds.prodMk_nhds hdâ‚€)
    (hds.mono fun n hn â†¦ by simp [hs, subset_closure hn]) ?_
  simpa using tendsto_const_nhds.prodMk_nhds hcd

/-- The product of two sets of unique differentiability at points `x` and `y` has unique
differentiability at `(x, y)`. -/
theorem UniqueDiffWithinAt.prod (hs : UniqueDiffWithinAt ð•œ s x)
    (ht : UniqueDiffWithinAt ð•œ t y) : UniqueDiffWithinAt ð•œ (s Ã—Ë¢ t) (x, y) := by
  rw [uniqueDiffWithinAt_iff] at hs ht âŠ¢
  rw [closure_prod_eq]
  refine âŸ¨?_, hs.2, ht.2âŸ©
  have : _ â‰¤ Submodule.span ð•œ (tangentConeAt ð•œ (s Ã—Ë¢ t) (x, y)) := Submodule.span_mono
    (union_subset (subset_tangentConeAt_prod_left ht.2) (subset_tangentConeAt_prod_right hs.2))
  rw [LinearMap.span_inl_union_inr, SetLike.le_def] at this
  exact (hs.1.prod ht.1).mono this

/-- The product of two sets of unique differentiability is a set of unique differentiability. -/
theorem UniqueDiffOn.prod (hs : UniqueDiffOn ð•œ s) (ht : UniqueDiffOn ð•œ t) :
    UniqueDiffOn ð•œ (s Ã—Ë¢ t) :=
  fun âŸ¨x, yâŸ© h => UniqueDiffWithinAt.prod (hs x h.1) (ht y h.2)
