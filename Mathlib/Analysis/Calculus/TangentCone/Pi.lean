/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Analysis.Calculus.TangentCone.Basic
import Mathlib.Topology.Algebra.Module.Basic

/-!
# Indexed product of sets with unique differentiability property

In this file we prove that the indexed product
of a family sets with unique differentiability property
has the same property, see `UniqueDiffOn.pi` and  `UniqueDiffOn.univ_pi`.
-/

public section

open Filter Set
open scoped Topology

section Semiring

variable {ğ•œ : Type*} [Semiring ğ•œ]
  {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousAdd (E i)] [âˆ€ i, ContinuousConstSMul ğ•œ (E i)]
  {s : âˆ€ i, Set (E i)} {x : âˆ€ i, E i}

/-- The tangent cone of a product contains the tangent cone of each factor. -/
theorem mapsTo_tangentConeAt_pi [DecidableEq Î¹] {i : Î¹} (hi : âˆ€ j â‰  i, x j âˆˆ closure (s j)) :
    MapsTo (Pi.single i) (tangentConeAt ğ•œ (s i) (x i)) (tangentConeAt ğ•œ (Set.pi univ s) x) := by
  rw [â† tangentConeAt_closure (s := .pi _ _)]
  intro y hy
  rcases exists_fun_of_mem_tangentConeAt hy with âŸ¨Î¹, l, hl, c, d, hdâ‚€, hds, hcdâŸ©
  apply mem_tangentConeAt_of_seq l c (fun n â†¦ Pi.single i (d n))
  Â· rw [tendsto_pi_nhds]
    intro j
    rcases eq_or_ne j i with rfl | hj <;> simp [*, tendsto_const_nhds]
  Â· refine hds.mono fun n hn â†¦ ?_
    rw [closure_pi_set, mem_univ_pi]
    intro j
    rcases eq_or_ne j i with rfl | hj <;> simp [*, subset_closure hn]
  Â· rw [tendsto_pi_nhds]
    intro j
    rcases eq_or_ne j i with rfl | hj <;> simp [*, tendsto_const_nhds]

theorem UniqueDiffWithinAt.univ_pi {s : âˆ€ i, Set (E i)} {x : âˆ€ i, E i}
    (h : âˆ€ i, UniqueDiffWithinAt ğ•œ (s i) (x i)) : UniqueDiffWithinAt ğ•œ (Set.pi univ s) x := by
  classical
  simp only [uniqueDiffWithinAt_iff, closure_pi_set] at h âŠ¢
  refine âŸ¨.of_closure <| (dense_pi univ fun i _ â†¦ (h i).1).closure.mono ?_, fun i _ => (h i).2âŸ©
  simp only [closure_pi_set, â† Submodule.closure_coe_iSup_map_single, Submodule.map_span]
  gcongr
  refine iSup_le fun i â†¦ ?_
  gcongr
  exact mapsTo_tangentConeAt_pi (fun j _ â†¦ (h j).2) |>.image_subset

/-- The product of a family of sets of unique differentiability is a set of unique
differentiability. -/
theorem UniqueDiffOn.univ_pi {s : âˆ€ i, Set (E i)} (h : âˆ€ i, UniqueDiffOn ğ•œ (s i)) :
    UniqueDiffOn ğ•œ (Set.pi univ s) :=
  fun _x hx â†¦ .univ_pi fun i â†¦ h i _ <| hx i (mem_univ i)

end Semiring

variable {ğ•œ : Type*} [DivisionSemiring ğ•œ]
  {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [TopologicalSpace ğ•œ] [(ğ“[â‰ ] (0 : ğ•œ)).NeBot]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, ContinuousAdd (E i)] [âˆ€ i, ContinuousSMul ğ•œ (E i)]
  {s : âˆ€ i, Set (E i)} {x : âˆ€ i, E i} {I : Set Î¹}

theorem UniqueDiffWithinAt.pi (h : âˆ€ i âˆˆ I, UniqueDiffWithinAt ğ•œ (s i) (x i)) :
    UniqueDiffWithinAt ğ•œ (Set.pi I s) x := by
  classical
  rw [â† Set.univ_pi_piecewise_univ]
  refine UniqueDiffWithinAt.univ_pi fun i => ?_
  by_cases hi : i âˆˆ I <;> simp [*, uniqueDiffWithinAt_univ]

/-- The product of a family of sets of unique differentiability is a set of unique
differentiability. -/
theorem UniqueDiffOn.pi (h : âˆ€ i âˆˆ I, UniqueDiffOn ğ•œ (s i)) : UniqueDiffOn ğ•œ (Set.pi I s) :=
  fun x hx => UniqueDiffWithinAt.pi fun i hi => h i hi (x i) (hx i hi)
