/-
Copyright (c) 2026 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Basic
public import Mathlib.Topology.MetricSpace.Holder

/-!
# `C^{k+(Œ±)}` functions in the sense of Moreira

In [Moreira2001], Moreira proves a version of the Morse-Sard theorem
for a carefully chosen class of maps he calls $C^{k+(Œ±)}$ maps.
Namely, a map `f` is said to be $C^{k+(Œ±)}$ at `a`,
where `k` is a natural number and `0 ‚â§ Œ± ‚â§ 1`,
if it is $C^k$ at this point and $D^kf(x)-D^kf(a) = O(‚Äñx - a‚Äñ ^ Œ±)$ as `x ‚Üí a`.

The main theorem of [Moreira2001] only assumes
that a function is $C^{k+(Œ±)}$ at all points of a set `s`
and the rank of the Fr√©chet derivative at these points is at most a given number `p`,
which is strictly less than the dimension of the domain.
Then it provides an upper estimate on the Hausdorff dimension of the image of `s` under `f`.

In this file, we define `ContDiffMoreiraHolderAt` to be the predicate
saying that a function is $C^{k+(Œ±)}$ in the sense described above
and prove basic properties of this predicate.

## Implementation notes

In the original paper, `k` is assumed to be a strictly positive number.
We define the predicate for any `k : ‚Ñï`, then assume `k ‚â† 0` whenever it is necessary.
-/

@[expose] public section

open scoped unitInterval Topology NNReal
open Asymptotics Filter Set

variable {E F G : Type*}
  [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  {k l m : ‚Ñï} {Œ± Œ≤ : I} {f : E ‚Üí F} {a : E}

/-- A map `f` is said to be $C^{k+(Œ±)}$ at `a`, where `k` is a natural number and `0 ‚â§ Œ± ‚â§ 1`,
if it is $C^k$ at this point and $D^kf(x)-D^kf(a) = O(‚Äñx - a‚Äñ ^ k)$ as `x ‚Üí a`. -/
@[mk_iff]
structure ContDiffMoreiraHolderAt (k : ‚Ñï) (Œ± : I) (f : E ‚Üí F) (a : E) : Prop where
  /-- A $C^{k+(Œ±)}$ map is a $C^k$ map. -/
  contDiffAt : ContDiffAt ‚Ñù k f a
  /-- A $C^{k+(Œ±)}$ map satisfies $D^kf(x)-D^kf(a) = O(‚Äñx - a‚Äñ ^ k)$ as `x ‚Üí a`. -/
  isBigO : (iteratedFDeriv ‚Ñù k f ¬∑ - iteratedFDeriv ‚Ñù k f a) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù))

/-- A $C^n$ map is a $C^{k+(Œ±)}$ map with any `k < n`. -/
theorem ContDiffAt.contDiffMoreiraHolderAt {n : WithTop ‚Ñï‚àû} (h : ContDiffAt ‚Ñù n f a) (hk : k < n)
    (Œ± : I) : ContDiffMoreiraHolderAt k Œ± f a where
  contDiffAt := h.of_le hk.le
  isBigO := calc
    (iteratedFDeriv ‚Ñù k f ¬∑ - iteratedFDeriv ‚Ñù k f a) =O[ùìù a] (¬∑ - a) :=
      (h.differentiableAt_iteratedFDeriv hk).isBigO_sub
    _ =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) :=
      .of_norm_left <| .comp_tendsto (.id_rpow_of_le_one Œ±.2.2) <| tendsto_norm_sub_self_nhdsGE a

namespace ContDiffMoreiraHolderAt

theorem continuousAt (h : ContDiffMoreiraHolderAt k Œ± f a) : ContinuousAt f a :=
  h.contDiffAt.continuousAt

theorem differentiableAt (h : ContDiffMoreiraHolderAt k Œ± f a) (hk : k ‚â† 0) :
    DifferentiableAt ‚Ñù f a :=
  h.contDiffAt.differentiableAt <| mod_cast hk

/-- A function is $C^{k+(0)}$ at a point if and only if it is $C^k$ at the point. -/
@[simp]
theorem zero_exponent_iff : ContDiffMoreiraHolderAt k 0 f a ‚Üî ContDiffAt ‚Ñù k f a := by
  refine ‚ü®contDiffAt, fun h ‚Ü¶ ‚ü®h, ?_‚ü©‚ü©
  simpa using ((h.continuousAt_iteratedFDeriv le_rfl).sub_const _).norm.isBoundedUnder_le

/-- A function is $C^{0+(Œ±)}$ at a point if and only if
it is $C^0$ at the point (i.e., it is continuous on a neighborhood of the point)
and $f(x) - f(a) = O(‚Äñx - a‚Äñ ^ Œ±)$. -/
theorem zero_left_iff :
    ContDiffMoreiraHolderAt 0 Œ± f a ‚Üî
      ContDiffAt ‚Ñù 0 f a ‚àß (f ¬∑ - f a) =O[ùìù a] (‚Äñ¬∑ - a‚Äñ ^ (Œ± : ‚Ñù)) := by
  simp only [contDiffMoreiraHolderAt_iff, Nat.cast_zero, and_congr_right_iff]
  intro hfc
  simp only [iteratedFDeriv_zero_eq_comp, Function.comp_def, ‚Üê map_sub]
  rw [‚Üê isBigO_norm_left]
  simp_rw [LinearIsometryEquiv.norm_map, isBigO_norm_left]

theorem of_exponent_le (hf : ContDiffMoreiraHolderAt k Œ± f a) (hle : Œ≤ ‚â§ Œ±) :
    ContDiffMoreiraHolderAt k Œ≤ f a where
  contDiffAt := hf.contDiffAt
  isBigO := hf.isBigO.trans <| by
    refine .comp_tendsto (.rpow_rpow_nhdsGE_zero_of_le_of_imp hle fun hŒ± ‚Ü¶ ?_) ?_
    ¬∑ exact le_antisymm (le_trans (mod_cast hle) hŒ±.le) Œ≤.2.1
    ¬∑ exact tendsto_norm_sub_self_nhdsGE a

theorem of_lt (hf : ContDiffMoreiraHolderAt k Œ± f a) (hlt : l < k) :
    ContDiffMoreiraHolderAt l Œ≤ f a :=
  hf.contDiffAt.contDiffMoreiraHolderAt (mod_cast hlt) _

theorem of_toLex_le (hf : ContDiffMoreiraHolderAt k Œ± f a) (hle : toLex (l, Œ≤) ‚â§ toLex (k, Œ±)) :
    ContDiffMoreiraHolderAt l Œ≤ f a :=
  (Prod.Lex.le_iff.mp hle).elim hf.of_lt <| by rintro ‚ü®rfl, hle‚ü©; exact hf.of_exponent_le hle

theorem of_le (hf : ContDiffMoreiraHolderAt k Œ± f a) (hl : l ‚â§ k) :
    ContDiffMoreiraHolderAt l Œ± f a :=
  hf.of_toLex_le <| Prod.Lex.toLex_mono ‚ü®hl, le_rfl‚ü©

/-- If a function is $C^{k+Œ±}$ on a neighborhood of a point `a`,
i.e., it is $C^k$ on this neighborhood and $D^k f$ is H√∂lder continuous on it,
then the function is $C^{k+(Œ±)}$ at `a`. -/
theorem of_contDiffOn_holderWith {s : Set E} {C : ‚Ñù‚â•0} (hf : ContDiffOn ‚Ñù k f s) (hs : s ‚àà ùìù a)
    (hd : HolderOnWith C ‚ü®Œ±, Œ±.2.1‚ü© (iteratedFDeriv ‚Ñù k f) s) :
    ContDiffMoreiraHolderAt k Œ± f a where
  contDiffAt := hf.contDiffAt hs
  isBigO := .of_bound C <| mem_of_superset hs fun x hx ‚Ü¶ by
    simpa [Real.abs_rpow_of_nonneg, ‚Üê dist_eq_norm, dist_nonneg]
      using hd.dist_le hx (mem_of_mem_nhds hs)

theorem fst {a : E √ó F} : ContDiffMoreiraHolderAt k Œ± Prod.fst a :=
  contDiffAt_fst.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

theorem snd {a : E √ó F} : ContDiffMoreiraHolderAt k Œ± Prod.snd a :=
  contDiffAt_snd.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

theorem prodMk {g : E ‚Üí G} (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hg : ContDiffMoreiraHolderAt k Œ± g a) :
    ContDiffMoreiraHolderAt k Œ± (fun x ‚Ü¶ (f x, g x)) a where
  contDiffAt := hf.contDiffAt.prodMk hg.contDiffAt
  isBigO := calc
    _ =·∂†[ùìù a] (fun x ‚Ü¶ (iteratedFDeriv ‚Ñù k f x - iteratedFDeriv ‚Ñù k f a).prod
                (iteratedFDeriv ‚Ñù k g x - iteratedFDeriv ‚Ñù k g a)) := by
      filter_upwards [hf.contDiffAt.eventually (by simp),
        hg.contDiffAt.eventually (by simp)] with x hfx hgx
      apply DFunLike.ext
      rw [iteratedFDeriv_prodMk _ _ le_rfl, iteratedFDeriv_prodMk _ _ le_rfl] <;>
        simp [hfx, hgx, hf.contDiffAt, hg.contDiffAt]
    _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := by
      refine .of_norm_left ?_
      simp only [ContinuousMultilinearMap.opNorm_prod, ‚Üê Prod.norm_mk]
      exact (hf.isBigO.prod_left hg.isBigO).norm_left

/-- Composition of two $C^{k+(Œ±)}$ functions is a $C^{k+(Œ±)}$ function,
provided that one of them is differentiable.

The latter condition follows automatically from the functions being $C^{k+(Œ±)}$,
if `k ‚â† 0`, see `comp` below. -/
theorem comp_of_differentiableAt {g : F ‚Üí G} (hg : ContDiffMoreiraHolderAt k Œ± g (f a))
    (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hd : DifferentiableAt ‚Ñù g (f a) ‚à® DifferentiableAt ‚Ñù f a) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a where
  contDiffAt := hg.contDiffAt.comp a hf.contDiffAt
  isBigO := calc
    (iteratedFDeriv ‚Ñù k (g ‚àò f) ¬∑ - iteratedFDeriv ‚Ñù k (g ‚àò f) a)
      =·∂†[ùìù a] fun x ‚Ü¶ (ftaylorSeries ‚Ñù g (f x)).taylorComp (ftaylorSeries ‚Ñù f x) k -
        (ftaylorSeries ‚Ñù g (f a)).taylorComp (ftaylorSeries ‚Ñù f a) k := by
      filter_upwards [hf.contDiffAt.eventually (by simp),
        hf.continuousAt.eventually (hg.contDiffAt.eventually (by simp))] with x hfx hgx
      rw [iteratedFDeriv_comp hgx hfx le_rfl,
        iteratedFDeriv_comp hg.contDiffAt hf.contDiffAt le_rfl]
    _ =O[ùìù a] fun x ‚Ü¶ ‚Äñx - a‚Äñ ^ (Œ± : ‚Ñù) := by
      apply FormalMultilinearSeries.taylorComp_sub_taylorComp_isBigO
      ¬∑ intro i hi
        exact ((hg.contDiffAt.continuousAt_iteratedFDeriv (mod_cast hi)).comp hf.continuousAt)
          |>.norm.isBoundedUnder_le
      ¬∑ intro i hi
        by_cases hfd : DifferentiableAt ‚Ñù f a
        ¬∑ refine ((hg.of_le hi).isBigO.comp_tendsto hf.continuousAt).trans ?_
          refine .rpow Œ±.2.1 (.of_forall fun _ ‚Ü¶ norm_nonneg _) <| .norm_norm ?_
          exact hfd.isBigO_sub
        ¬∑ obtain rfl : k = 0 := by
            contrapose! hfd
            exact hf.differentiableAt hfd
          obtain rfl : i = 0 := by rwa [nonpos_iff_eq_zero] at hi
          refine .of_norm_left ?_
          simp only [ftaylorSeries, iteratedFDeriv_zero_eq_comp, Function.comp_apply, ‚Üê map_sub,
            LinearIsometryEquiv.norm_map, isBigO_norm_left]
          refine ((hd.resolve_right hfd).isBigO_sub.comp_tendsto hf.continuousAt).trans ?_
          refine .trans (.of_norm_right ?_) hf.isBigO
          simp [iteratedFDeriv_zero_eq_comp, ‚Üê map_sub, Function.comp_def, isBigO_refl]
      ¬∑ intro i hi
        exact (hf.contDiffAt.continuousAt_iteratedFDeriv (mod_cast hi)).norm.isBoundedUnder_le
      ¬∑ exact fun _ _ ‚Ü¶ isBoundedUnder_const
      ¬∑ exact fun i hi ‚Ü¶ (hf.of_le hi).isBigO

/-- Composition of two $C^{k+(Œ±)}$ functions, `k ‚â† 0`, is a $C^{k+(Œ±)}$ function. -/
theorem comp {g : F ‚Üí G} (hg : ContDiffMoreiraHolderAt k Œ± g (f a))
    (hf : ContDiffMoreiraHolderAt k Œ± f a) (hk : k ‚â† 0) : ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a :=
  hg.comp_of_differentiableAt hf (.inl <| hg.differentiableAt hk)

theorem _root_.ContinuousLinearMap.contDiffMoreiraHolderAt (f : E ‚ÜíL[‚Ñù] F) :
    ContDiffMoreiraHolderAt k Œ± f a :=
  f.contDiff.contDiffAt.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) _

theorem _root_.ContinuousLinearEquiv.contDiffMoreiraHolderAt (f : E ‚âÉL[‚Ñù] F) :
    ContDiffMoreiraHolderAt k Œ± f a :=
  f.toContinuousLinearMap.contDiffMoreiraHolderAt

theorem continuousLinearMap_comp (hf : ContDiffMoreiraHolderAt k Œ± f a) (g : F ‚ÜíL[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a :=
  g.contDiffMoreiraHolderAt.comp_of_differentiableAt hf <| .inl g.differentiableAt

@[simp]
theorem _root_.ContinuousLinearEquiv.contDiffMoreiraHolderAt_left_comp (g : F ‚âÉL[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a ‚Üî ContDiffMoreiraHolderAt k Œ± f a :=
  ‚ü®fun h ‚Ü¶ by simpa [Function.comp_def] using h.continuousLinearMap_comp (g.symm : G ‚ÜíL[‚Ñù] F),
    fun h ‚Ü¶ h.continuousLinearMap_comp (g : F ‚ÜíL[‚Ñù] G)‚ü©

@[simp]
theorem _root_.LinearIsometryEquiv.contDiffMoreiraHolderAt_left_comp (g : F ‚âÉ‚Çó·µ¢[‚Ñù] G) :
    ContDiffMoreiraHolderAt k Œ± (g ‚àò f) a ‚Üî ContDiffMoreiraHolderAt k Œ± f a :=
  g.toContinuousLinearEquiv.contDiffMoreiraHolderAt_left_comp

protected theorem id : ContDiffMoreiraHolderAt k Œ± id a :=
  ContinuousLinearMap.id ‚Ñù E |>.contDiffMoreiraHolderAt

protected theorem const {b : F} : ContDiffMoreiraHolderAt k Œ± (Function.const E b) a :=
  contDiffAt_const.contDiffMoreiraHolderAt (WithTop.coe_lt_top _) Œ±

/-- The derivative of a $C^{k + (Œ±)}$ function is a $C^{l + (Œ±)}$ function, if `l < k`. -/
protected theorem fderiv (hf : ContDiffMoreiraHolderAt k Œ± f a) (hl : l < k) :
    ContDiffMoreiraHolderAt l Œ± (fderiv ‚Ñù f) a where
  contDiffAt := hf.contDiffAt.fderiv_right (mod_cast hl)
  isBigO := .of_norm_left <| by
    simpa [iteratedFDeriv_succ_eq_comp_right, Function.comp_def, ‚Üê dist_eq_norm_sub]
      using hf.of_le (Nat.add_one_le_iff.mpr hl) |>.isBigO |>.norm_left

/-- If `f` is a $C^{k+(Œ±)}$ function and `l + m ‚â§ k`, then $D^mf$ is a $C^{l + (Œ±)}$ function. -/
protected theorem iteratedFDeriv (hf : ContDiffMoreiraHolderAt k Œ± f a) (hl : l + m ‚â§ k) :
    ContDiffMoreiraHolderAt l Œ± (iteratedFDeriv ‚Ñù m f) a := by
  induction m generalizing l with
  | zero =>
    simpa +unfoldPartialApp [iteratedFDeriv_zero_eq_comp] using hf.of_le hl
  | succ m ihm =>
    rw [‚Üê add_assoc, add_right_comm] at hl
    simpa +unfoldPartialApp [iteratedFDeriv_succ_eq_comp_left] using (ihm hl).fderiv l.lt_add_one

theorem congr_eventuallyEq {g : E ‚Üí F} (hf : ContDiffMoreiraHolderAt k Œ± f a) (hfg : f =·∂†[ùìù a] g) :
    ContDiffMoreiraHolderAt k Œ± g a where
  contDiffAt := hf.contDiffAt.congr_of_eventuallyEq hfg.symm
  isBigO := by
    refine EventuallyEq.trans_isBigO (.sub ?_ ?_) hf.isBigO
    ¬∑ exact hfg.symm.iteratedFDeriv ‚Ñù _
    ¬∑ rw [hfg.symm.iteratedFDeriv ‚Ñù _ |>.self_of_nhds]

theorem clm_apply {f : E ‚Üí F ‚ÜíL[‚Ñù] G} {g : E ‚Üí F} (hf : ContDiffMoreiraHolderAt k Œ± f a)
    (hg : ContDiffMoreiraHolderAt k Œ± g a) : ContDiffMoreiraHolderAt k Œ± (fun x ‚Ü¶ f x (g x)) a :=
  (contDiffAt_fst.clm_apply contDiffAt_snd).contDiffMoreiraHolderAt (WithTop.coe_lt_top _) _
    |>.comp_of_differentiableAt (hf.prodMk hg) <| .inl (by fun_prop)

end ContDiffMoreiraHolderAt
