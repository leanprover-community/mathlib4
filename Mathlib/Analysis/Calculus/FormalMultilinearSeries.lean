/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.NormedSpace.Multilinear.Curry

#align_import analysis.calculus.formal_multilinear_series from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Formal multilinear series

In this file we define `FormalMultilinearSeries ğ•œ E F` to be a family of `n`-multilinear maps for
all `n`, designed to model the sequence of derivatives of a function. In other files we use this
notion to define `C^n` functions (called `contDiff` in `mathlib`) and analytic functions.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

## Tags

multilinear, formal series
-/


noncomputable section

open Set Fin Topology

-- Porting note: added explicit universes to fix compile
universe u u' v w x
variable {ğ•œ : Type u} {ğ•œ' : Type u'} {E : Type v} {F : Type w} {G : Type x}

section

variable [Ring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] [TopologicalAddGroup E]
  [ContinuousConstSMul ğ•œ E] [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
  [TopologicalAddGroup F] [ContinuousConstSMul ğ•œ F] [AddCommGroup G] [Module ğ•œ G]
  [TopologicalSpace G] [TopologicalAddGroup G] [ContinuousConstSMul ğ•œ G]

/-- A formal multilinear series over a field `ğ•œ`, from `E` to `F`, is given by a family of
multilinear maps from `E^n` to `F` for all `n`. -/
@[nolint unusedArguments]
def FormalMultilinearSeries (ğ•œ : Type*) (E : Type*) (F : Type*) [Ring ğ•œ] [AddCommGroup E]
    [Module ğ•œ E] [TopologicalSpace E] [TopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]
    [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F] [TopologicalAddGroup F]
    [ContinuousConstSMul ğ•œ F] :=
  âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F
#align formal_multilinear_series FormalMultilinearSeries

-- Porting note: was `deriving`
instance : AddCommGroup (FormalMultilinearSeries ğ•œ E F) :=
  inferInstanceAs <| AddCommGroup <| âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F

instance : Inhabited (FormalMultilinearSeries ğ•œ E F) :=
  âŸ¨0âŸ©

section Module

instance (ğ•œ') [Semiring ğ•œ'] [Module ğ•œ' F] [ContinuousConstSMul ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F] :
    Module ğ•œ' (FormalMultilinearSeries ğ•œ E F) :=
  inferInstanceAs <| Module ğ•œ' <| âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F

end Module

namespace FormalMultilinearSeries

@[simp] -- Porting note (#10756): new theorem; was not needed in Lean 3
theorem zero_apply (n : â„•) : (0 : FormalMultilinearSeries ğ•œ E F) n = 0 := rfl

@[simp] -- Porting note (#10756): new theorem; was not needed in Lean 3
theorem neg_apply (f : FormalMultilinearSeries ğ•œ E F) (n : â„•) : (-f) n = - f n := rfl

@[ext] -- Porting note (#10756): new theorem
protected theorem ext {p q : FormalMultilinearSeries ğ•œ E F} (h : âˆ€ n, p n = q n) : p = q :=
  funext h

protected theorem ext_iff {p q : FormalMultilinearSeries ğ•œ E F} : p = q â†” âˆ€ n, p n = q n :=
  Function.funext_iff
#align formal_multilinear_series.ext_iff FormalMultilinearSeries.ext_iff

protected theorem ne_iff {p q : FormalMultilinearSeries ğ•œ E F} : p â‰  q â†” âˆƒ n, p n â‰  q n :=
  Function.ne_iff
#align formal_multilinear_series.ne_iff FormalMultilinearSeries.ne_iff

/-- Cartesian product of two formal multilinear series (with the same field `ğ•œ` and the same source
space, but possibly different target spaces). -/
def prod (p : FormalMultilinearSeries ğ•œ E F) (q : FormalMultilinearSeries ğ•œ E G) :
    FormalMultilinearSeries ğ•œ E (F Ã— G)
  | n => (p n).prod (q n)

/-- Killing the zeroth coefficient in a formal multilinear series -/
def removeZero (p : FormalMultilinearSeries ğ•œ E F) : FormalMultilinearSeries ğ•œ E F
  | 0 => 0
  | n + 1 => p (n + 1)
#align formal_multilinear_series.remove_zero FormalMultilinearSeries.removeZero

@[simp]
theorem removeZero_coeff_zero (p : FormalMultilinearSeries ğ•œ E F) : p.removeZero 0 = 0 :=
  rfl
#align formal_multilinear_series.remove_zero_coeff_zero FormalMultilinearSeries.removeZero_coeff_zero

@[simp]
theorem removeZero_coeff_succ (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) :
    p.removeZero (n + 1) = p (n + 1) :=
  rfl
#align formal_multilinear_series.remove_zero_coeff_succ FormalMultilinearSeries.removeZero_coeff_succ

theorem removeZero_of_pos (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (h : 0 < n) :
    p.removeZero n = p n := by
  rw [â† Nat.succ_pred_eq_of_pos h]
  rfl
#align formal_multilinear_series.remove_zero_of_pos FormalMultilinearSeries.removeZero_of_pos

/-- Convenience congruence lemma stating in a dependent setting that, if the arguments to a formal
multilinear series are equal, then the values are also equal. -/
theorem congr (p : FormalMultilinearSeries ğ•œ E F) {m n : â„•} {v : Fin m â†’ E} {w : Fin n â†’ E}
    (h1 : m = n) (h2 : âˆ€ (i : â„•) (him : i < m) (hin : i < n), v âŸ¨i, himâŸ© = w âŸ¨i, hinâŸ©) :
    p m v = p n w := by
  subst n
  congr with âŸ¨i, hiâŸ©
  exact h2 i hi hi
#align formal_multilinear_series.congr FormalMultilinearSeries.congr

/-- Composing each term `pâ‚™` in a formal multilinear series with `(u, ..., u)` where `u` is a fixed
continuous linear map, gives a new formal multilinear series `p.compContinuousLinearMap u`. -/
def compContinuousLinearMap (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) :
    FormalMultilinearSeries ğ•œ E G := fun n => (p n).compContinuousLinearMap fun _ : Fin n => u
#align formal_multilinear_series.comp_continuous_linear_map FormalMultilinearSeries.compContinuousLinearMap

@[simp]
theorem compContinuousLinearMap_apply (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) (n : â„•)
    (v : Fin n â†’ E) : (p.compContinuousLinearMap u) n v = p n (u âˆ˜ v) :=
  rfl
#align formal_multilinear_series.comp_continuous_linear_map_apply FormalMultilinearSeries.compContinuousLinearMap_apply

variable (ğ•œ) [Ring ğ•œ'] [SMul ğ•œ ğ•œ']
variable [Module ğ•œ' E] [ContinuousConstSMul ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
variable [Module ğ•œ' F] [ContinuousConstSMul ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]

/-- Reinterpret a formal `ğ•œ'`-multilinear series as a formal `ğ•œ`-multilinear series. -/
@[simp]
protected def restrictScalars (p : FormalMultilinearSeries ğ•œ' E F) :
    FormalMultilinearSeries ğ•œ E F := fun n => (p n).restrictScalars ğ•œ
#align formal_multilinear_series.restrict_scalars FormalMultilinearSeries.restrictScalars

end FormalMultilinearSeries

end

namespace FormalMultilinearSeries

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

variable (p : FormalMultilinearSeries ğ•œ E F)

/-- Forgetting the zeroth term in a formal multilinear series, and interpreting the following terms
as multilinear maps into `E â†’L[ğ•œ] F`. If `p` is the Taylor series (`HasFTaylorSeriesUpTo`) of a
function, then `p.shift` is the Taylor series of the derivative of the function. Note that the
`p.sum` of a Taylor series `p` does not give the original function; for a formal multilinear
series that sums to the derivative of `p.sum`, see `HasFPowerSeriesOnBall.fderiv`. -/
def shift : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F) := fun n => (p n.succ).curryRight
#align formal_multilinear_series.shift FormalMultilinearSeries.shift

/-- Adding a zeroth term to a formal multilinear series taking values in `E â†’L[ğ•œ] F`. This
corresponds to starting from a Taylor series (`HasFTaylorSeriesUpTo`) for the derivative of a
function, and building a Taylor series for the function itself. -/
def unshift (q : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F)) (z : F) : FormalMultilinearSeries ğ•œ E F
  | 0 => (continuousMultilinearCurryFin0 ğ•œ E F).symm z
  | n + 1 => -- Porting note: added type hint here and explicit universes to fix compile
    (continuousMultilinearCurryRightEquiv' ğ•œ n E F :
      (E [Ã—n]â†’L[ğ•œ] E â†’L[ğ•œ] F) â†’ (E [Ã—n.succ]â†’L[ğ•œ] F)) (q n)
#align formal_multilinear_series.unshift FormalMultilinearSeries.unshift

end FormalMultilinearSeries

section

variable [Ring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] [TopologicalAddGroup E]
  [ContinuousConstSMul ğ•œ E] [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
  [TopologicalAddGroup F] [ContinuousConstSMul ğ•œ F] [AddCommGroup G] [Module ğ•œ G]
  [TopologicalSpace G] [TopologicalAddGroup G] [ContinuousConstSMul ğ•œ G]

namespace ContinuousLinearMap

/-- Composing each term `pâ‚™` in a formal multilinear series with a continuous linear map `f` on the
left gives a new formal multilinear series `f.compFormalMultilinearSeries p` whose general term
is `f âˆ˜ pâ‚™`. -/
def compFormalMultilinearSeries (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F) :
    FormalMultilinearSeries ğ•œ E G := fun n => f.compContinuousMultilinearMap (p n)
#align continuous_linear_map.comp_formal_multilinear_series ContinuousLinearMap.compFormalMultilinearSeries

@[simp]
theorem compFormalMultilinearSeries_apply (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F)
    (n : â„•) : (f.compFormalMultilinearSeries p) n = f.compContinuousMultilinearMap (p n) :=
  rfl
#align continuous_linear_map.comp_formal_multilinear_series_apply ContinuousLinearMap.compFormalMultilinearSeries_apply

theorem compFormalMultilinearSeries_apply' (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F)
    (n : â„•) (v : Fin n â†’ E) : (f.compFormalMultilinearSeries p) n v = f (p n v) :=
  rfl
#align continuous_linear_map.comp_formal_multilinear_series_apply' ContinuousLinearMap.compFormalMultilinearSeries_apply'

end ContinuousLinearMap

namespace ContinuousMultilinearMap

variable {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, TopologicalAddGroup (E i)]
  [âˆ€ i, ContinuousConstSMul ğ•œ (E i)] [Fintype Î¹] (f : ContinuousMultilinearMap ğ•œ E F)

/-- Realize a ContinuousMultilinearMap on `âˆ€ i : Î¹, E i` as the evaluation of a
FormalMultilinearSeries by choosing an arbitrary identification `Î¹ â‰ƒ Fin (Fintype.card Î¹)`. -/
noncomputable def toFormalMultilinearSeries : FormalMultilinearSeries ğ•œ (âˆ€ i, E i) F :=
  fun n â†¦ if h : Fintype.card Î¹ = n then
    (f.compContinuousLinearMap .proj).domDomCongr (Fintype.equivFinOfCardEq h)
  else 0

end ContinuousMultilinearMap

end

namespace FormalMultilinearSeries

section Order

variable [Ring ğ•œ] {n : â„•} [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
  [TopologicalAddGroup E] [ContinuousConstSMul ğ•œ E] [AddCommGroup F] [Module ğ•œ F]
  [TopologicalSpace F] [TopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
  {p : FormalMultilinearSeries ğ•œ E F}

/-- The index of the first non-zero coefficient in `p` (or `0` if all coefficients are zero). This
  is the order of the isolated zero of an analytic function `f` at a point if `p` is the Taylor
  series of `f` at that point. -/
noncomputable def order (p : FormalMultilinearSeries ğ•œ E F) : â„• :=
  sInf { n | p n â‰  0 }
#align formal_multilinear_series.order FormalMultilinearSeries.order

@[simp]
theorem order_zero : (0 : FormalMultilinearSeries ğ•œ E F).order = 0 := by simp [order]
#align formal_multilinear_series.order_zero FormalMultilinearSeries.order_zero

theorem ne_zero_of_order_ne_zero (hp : p.order â‰  0) : p â‰  0 := fun h => by simp [h] at hp
#align formal_multilinear_series.ne_zero_of_order_ne_zero FormalMultilinearSeries.ne_zero_of_order_ne_zero

theorem order_eq_find [DecidablePred fun n => p n â‰  0] (hp : âˆƒ n, p n â‰  0) :
    p.order = Nat.find hp := by convert Nat.sInf_def hp
#align formal_multilinear_series.order_eq_find FormalMultilinearSeries.order_eq_find

theorem order_eq_find' [DecidablePred fun n => p n â‰  0] (hp : p â‰  0) :
    p.order = Nat.find (FormalMultilinearSeries.ne_iff.mp hp) :=
  order_eq_find _
#align formal_multilinear_series.order_eq_find' FormalMultilinearSeries.order_eq_find'

theorem order_eq_zero_iff' : p.order = 0 â†” p = 0 âˆ¨ p 0 â‰  0 := by
  simpa [order, Nat.sInf_eq_zero, FormalMultilinearSeries.ext_iff, eq_empty_iff_forall_not_mem]
    using or_comm
#align formal_multilinear_series.order_eq_zero_iff' FormalMultilinearSeries.order_eq_zero_iff'

theorem order_eq_zero_iff (hp : p â‰  0) : p.order = 0 â†” p 0 â‰  0 := by
  simp [order_eq_zero_iff', hp]
#align formal_multilinear_series.order_eq_zero_iff FormalMultilinearSeries.order_eq_zero_iff

theorem apply_order_ne_zero (hp : p â‰  0) : p p.order â‰  0 :=
  Nat.sInf_mem (FormalMultilinearSeries.ne_iff.1 hp)
#align formal_multilinear_series.apply_order_ne_zero FormalMultilinearSeries.apply_order_ne_zero

theorem apply_order_ne_zero' (hp : p.order â‰  0) : p p.order â‰  0 :=
  apply_order_ne_zero (ne_zero_of_order_ne_zero hp)
#align formal_multilinear_series.apply_order_ne_zero' FormalMultilinearSeries.apply_order_ne_zero'

theorem apply_eq_zero_of_lt_order (hp : n < p.order) : p n = 0 :=
  by_contra <| Nat.not_mem_of_lt_sInf hp
#align formal_multilinear_series.apply_eq_zero_of_lt_order FormalMultilinearSeries.apply_eq_zero_of_lt_order

end Order

section Coef

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {s : E}
  {p : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E} {n : â„•} {z zâ‚€ : ğ•œ} {y : Fin n â†’ ğ•œ}

/-- The `n`th coefficient of `p` when seen as a power series. -/
def coeff (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•) : E :=
  p n 1
#align formal_multilinear_series.coeff FormalMultilinearSeries.coeff

theorem mkPiRing_coeff_eq (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•) :
    ContinuousMultilinearMap.mkPiRing ğ•œ (Fin n) (p.coeff n) = p n :=
  (p n).mkPiRing_apply_one_eq_self
#align formal_multilinear_series.mk_pi_field_coeff_eq FormalMultilinearSeries.mkPiRing_coeff_eq

@[simp]
theorem apply_eq_prod_smul_coeff : p n y = (âˆ i, y i) â€¢ p.coeff n := by
  convert (p n).toMultilinearMap.map_smul_univ y 1
  simp only [Pi.one_apply, Algebra.id.smul_eq_mul, mul_one]
#align formal_multilinear_series.apply_eq_prod_smul_coeff FormalMultilinearSeries.apply_eq_prod_smul_coeff

theorem coeff_eq_zero : p.coeff n = 0 â†” p n = 0 := by
  rw [â† mkPiRing_coeff_eq p, ContinuousMultilinearMap.mkPiRing_eq_zero_iff]
#align formal_multilinear_series.coeff_eq_zero FormalMultilinearSeries.coeff_eq_zero

theorem apply_eq_pow_smul_coeff : (p n fun _ => z) = z ^ n â€¢ p.coeff n := by simp
#align formal_multilinear_series.apply_eq_pow_smul_coeff FormalMultilinearSeries.apply_eq_pow_smul_coeff

@[simp]
theorem norm_apply_eq_norm_coef : â€–p nâ€– = â€–coeff p nâ€– := by
  rw [â† mkPiRing_coeff_eq p, ContinuousMultilinearMap.norm_mkPiRing]
#align formal_multilinear_series.norm_apply_eq_norm_coef FormalMultilinearSeries.norm_apply_eq_norm_coef

end Coef

section Fslope

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {p : FormalMultilinearSeries ğ•œ ğ•œ E} {n : â„•}

/-- The formal counterpart of `dslope`, corresponding to the expansion of `(f z - f 0) / z`. If `f`
has `p` as a power series, then `dslope f` has `fslope p` as a power series. -/
noncomputable def fslope (p : FormalMultilinearSeries ğ•œ ğ•œ E) : FormalMultilinearSeries ğ•œ ğ•œ E :=
  fun n => (p (n + 1)).curryLeft 1
#align formal_multilinear_series.fslope FormalMultilinearSeries.fslope

@[simp]
theorem coeff_fslope : p.fslope.coeff n = p.coeff (n + 1) := by
  simp only [fslope, coeff, ContinuousMultilinearMap.curryLeft_apply]
  congr 1
  exact Fin.cons_self_tail 1
#align formal_multilinear_series.coeff_fslope FormalMultilinearSeries.coeff_fslope

@[simp]
theorem coeff_iterate_fslope (k n : â„•) : (fslope^[k] p).coeff n = p.coeff (n + k) := by
  induction k generalizing p with
  | zero => rfl
  | succ k ih => simp [ih, add_assoc]
#align formal_multilinear_series.coeff_iterate_fslope FormalMultilinearSeries.coeff_iterate_fslope

end Fslope

end FormalMultilinearSeries

section Const

/-- The formal multilinear series where all terms of positive degree are equal to zero, and the term
of degree zero is `c`. It is the power series expansion of the constant function equal to `c`
everywhere. -/
def constFormalMultilinearSeries (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [ContinuousConstSMul ğ•œ E] [TopologicalAddGroup E]
    {F : Type*} [NormedAddCommGroup F] [TopologicalAddGroup F] [NormedSpace ğ•œ F]
    [ContinuousConstSMul ğ•œ F] (c : F) : FormalMultilinearSeries ğ•œ E F
  | 0 => ContinuousMultilinearMap.curry0 _ _ c
  | _ => 0
#align const_formal_multilinear_series constFormalMultilinearSeries

@[simp]
theorem constFormalMultilinearSeries_apply [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] {c : F} {n : â„•} (hn : n â‰  0) :
    constFormalMultilinearSeries ğ•œ E c n = 0 :=
  Nat.casesOn n (fun hn => (hn rfl).elim) (fun _ _ => rfl) hn
#align const_formal_multilinear_series_apply constFormalMultilinearSeries_apply

end Const

section Linear

variable [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace ContinuousLinearMap

/-- Formal power series of a continuous linear map `f : E â†’L[ğ•œ] F` at `x : E`:
`f y = f x + f (y - x)`. -/
def fpowerSeries (f : E â†’L[ğ•œ] F) (x : E) : FormalMultilinearSeries ğ•œ E F
  | 0 => ContinuousMultilinearMap.curry0 ğ•œ _ (f x)
  | 1 => (continuousMultilinearCurryFin1 ğ•œ E F).symm f
  | _ => 0
#align continuous_linear_map.fpower_series ContinuousLinearMap.fpowerSeries

theorem fpowerSeries_apply_zero (f : E â†’L[ğ•œ] F) (x : E) :
    f.fpowerSeries x 0 = ContinuousMultilinearMap.curry0 ğ•œ _ (f x) :=
  rfl

theorem fpowerSeries_apply_one (f : E â†’L[ğ•œ] F) (x : E) :
    f.fpowerSeries x 1 = (continuousMultilinearCurryFin1 ğ•œ E F).symm f :=
  rfl

theorem fpowerSeries_apply_add_two (f : E â†’L[ğ•œ] F) (x : E) (n : â„•) : f.fpowerSeries x (n + 2) = 0 :=
  rfl
#align continuous_linear_map.fpower_series_apply_add_two ContinuousLinearMap.fpowerSeries_apply_add_two

attribute
  [eqns fpowerSeries_apply_zero fpowerSeries_apply_one fpowerSeries_apply_add_two] fpowerSeries
attribute [simp] fpowerSeries

end ContinuousLinearMap

end Linear
