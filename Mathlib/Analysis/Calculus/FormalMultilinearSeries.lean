/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.NormedSpace.Multilinear.Curry

/-!
# Formal multilinear series

In this file we define `FormalMultilinearSeries ğ•œ E F` to be a family of `n`-multilinear maps for
all `n`, designed to model the sequence of derivatives of a function. In other files we use this
notion to define `C^n` functions (called `contDiff` in `mathlib`) and analytic functions.

## Notations

We use the notation `E [Ã—n]â†’L[ğ•œ] F` for the space of continuous multilinear maps on `E^n` with
values in `F`. This is the space in which the `n`-th derivative of a function from `E` to `F` lives.

## Tags

multilinear, formal series
-/


noncomputable section

open Set Fin Topology

universe u u' v w x y
variable {ğ•œ : Type u} {ğ•œ' : Type u'} {E : Type v} {F : Type w} {G : Type x} {H : Type y}

section

variable [Semiring ğ•œ]
  [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E] [ContinuousAdd E] [ContinuousConstSMul ğ•œ E]
  [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F] [ContinuousAdd F] [ContinuousConstSMul ğ•œ F]
  [AddCommMonoid G] [Module ğ•œ G] [TopologicalSpace G] [ContinuousAdd G] [ContinuousConstSMul ğ•œ G]
  [AddCommMonoid H] [Module ğ•œ H] [TopologicalSpace H] [ContinuousAdd H] [ContinuousConstSMul ğ•œ H]

/-- A formal multilinear series over a field `ğ•œ`, from `E` to `F`, is given by a family of
multilinear maps from `E^n` to `F` for all `n`. -/
@[nolint unusedArguments]
def FormalMultilinearSeries (ğ•œ : Type*) (E : Type*) (F : Type*) [Semiring ğ•œ] [AddCommMonoid E]
    [Module ğ•œ E] [TopologicalSpace E] [ContinuousAdd E] [ContinuousConstSMul ğ•œ E]
    [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F] [ContinuousAdd F]
    [ContinuousConstSMul ğ•œ F] :=
  âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F
deriving AddCommMonoid, Inhabited

section Module

instance (ğ•œ') [Semiring ğ•œ'] [Module ğ•œ' F] [ContinuousConstSMul ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F] :
    Module ğ•œ' (FormalMultilinearSeries ğ•œ E F) :=
  inferInstanceAs <| Module ğ•œ' <| âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F

end Module

namespace FormalMultilinearSeries

@[simp]
theorem zero_apply (n : â„•) : (0 : FormalMultilinearSeries ğ•œ E F) n = 0 := rfl

@[simp]
theorem add_apply (p q : FormalMultilinearSeries ğ•œ E F) (n : â„•) : (p + q) n = p n + q n := rfl

@[simp]
theorem smul_apply [Semiring ğ•œ'] [Module ğ•œ' F] [ContinuousConstSMul ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F]
    (f : FormalMultilinearSeries ğ•œ E F) (n : â„•) (a : ğ•œ') : (a â€¢ f) n = a â€¢ f n := rfl

@[ext]
protected theorem ext {p q : FormalMultilinearSeries ğ•œ E F} (h : âˆ€ n, p n = q n) : p = q :=
  funext h

protected theorem ne_iff {p q : FormalMultilinearSeries ğ•œ E F} : p â‰  q â†” âˆƒ n, p n â‰  q n :=
  Function.ne_iff

/-- Cartesian product of two formal multilinear series (with the same field `ğ•œ` and the same source
space, but possibly different target spaces). -/
def prod (p : FormalMultilinearSeries ğ•œ E F) (q : FormalMultilinearSeries ğ•œ E G) :
    FormalMultilinearSeries ğ•œ E (F Ã— G)
  | n => (p n).prod (q n)

/-- Product of formal multilinear series (with the same field `ğ•œ` and the same source
space, but possibly different target spaces). -/
@[simp] def pi {Î¹ : Type*} {F : Î¹ â†’ Type*}
    [âˆ€ i, AddCommGroup (F i)] [âˆ€ i, Module ğ•œ (F i)] [âˆ€ i, TopologicalSpace (F i)]
    [âˆ€ i, IsTopologicalAddGroup (F i)] [âˆ€ i, ContinuousConstSMul ğ•œ (F i)]
    (p : Î  i, FormalMultilinearSeries ğ•œ E (F i)) :
    FormalMultilinearSeries ğ•œ E (Î  i, F i)
  | n => ContinuousMultilinearMap.pi (fun i â†¦ p i n)

/-- Killing the zeroth coefficient in a formal multilinear series -/
def removeZero (p : FormalMultilinearSeries ğ•œ E F) : FormalMultilinearSeries ğ•œ E F
  | 0 => 0
  | n + 1 => p (n + 1)

@[simp]
theorem removeZero_coeff_zero (p : FormalMultilinearSeries ğ•œ E F) : p.removeZero 0 = 0 :=
  rfl

@[simp]
theorem removeZero_coeff_succ (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) :
    p.removeZero (n + 1) = p (n + 1) :=
  rfl

theorem removeZero_of_pos (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (h : 0 < n) :
    p.removeZero n = p n := by
  rw [â† Nat.succ_pred_eq_of_pos h]
  rfl

/-- Convenience congruence lemma stating in a dependent setting that, if the arguments to a formal
multilinear series are equal, then the values are also equal. -/
theorem congr (p : FormalMultilinearSeries ğ•œ E F) {m n : â„•} {v : Fin m â†’ E} {w : Fin n â†’ E}
    (h1 : m = n) (h2 : âˆ€ (i : â„•) (him : i < m) (hin : i < n), v âŸ¨i, himâŸ© = w âŸ¨i, hinâŸ©) :
    p m v = p n w := by
  subst n
  congr with âŸ¨i, hiâŸ©
  exact h2 i hi hi

lemma congr_zero (p : FormalMultilinearSeries ğ•œ E F) {k l : â„•} (h : k = l) (h' : p k = 0) :
    p l = 0 := by
  subst h; exact h'

/-- Composing each term `pâ‚™` in a formal multilinear series with `(u, ..., u)` where `u` is a fixed
continuous linear map, gives a new formal multilinear series `p.compContinuousLinearMap u`. -/
def compContinuousLinearMap (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) :
    FormalMultilinearSeries ğ•œ E G := fun n => (p n).compContinuousLinearMap fun _ : Fin n => u

@[simp]
theorem compContinuousLinearMap_apply (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) (n : â„•)
    (v : Fin n â†’ E) : (p.compContinuousLinearMap u) n v = p n (u âˆ˜ v) :=
  rfl

@[simp]
theorem compContinuousLinearMap_id (p : FormalMultilinearSeries ğ•œ E F) :
    p.compContinuousLinearMap (.id _ _) = p :=
  rfl

theorem compContinuousLinearMap_comp (p : FormalMultilinearSeries ğ•œ G H) (uâ‚ : F â†’L[ğ•œ] G)
    (uâ‚‚ : E â†’L[ğ•œ] F) :
    (p.compContinuousLinearMap uâ‚).compContinuousLinearMap uâ‚‚ =
    p.compContinuousLinearMap (uâ‚.comp uâ‚‚) :=
  rfl

variable (ğ•œ) [Semiring ğ•œ'] [SMul ğ•œ ğ•œ']
variable [Module ğ•œ' E] [ContinuousConstSMul ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
variable [Module ğ•œ' F] [ContinuousConstSMul ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]

/-- Reinterpret a formal `ğ•œ'`-multilinear series as a formal `ğ•œ`-multilinear series. -/
@[simp]
protected def restrictScalars (p : FormalMultilinearSeries ğ•œ' E F) :
    FormalMultilinearSeries ğ•œ E F := fun n => (p n).restrictScalars ğ•œ

end FormalMultilinearSeries

end

namespace FormalMultilinearSeries
variable [Ring ğ•œ] [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E] [IsTopologicalAddGroup E]
  [ContinuousConstSMul ğ•œ E] [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]
  [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]

instance : AddCommGroup (FormalMultilinearSeries ğ•œ E F) :=
  inferInstanceAs <| AddCommGroup <| âˆ€ n : â„•, E[Ã—n]â†’L[ğ•œ] F

@[simp]
theorem neg_apply (f : FormalMultilinearSeries ğ•œ E F) (n : â„•) : (-f) n = - f n := rfl

@[simp]
theorem sub_apply (f g : FormalMultilinearSeries ğ•œ E F) (n : â„•) : (f - g) n = f n - g n := rfl

end FormalMultilinearSeries

namespace FormalMultilinearSeries

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F]

variable (p : FormalMultilinearSeries ğ•œ E F)

/-- Forgetting the zeroth term in a formal multilinear series, and interpreting the following terms
as multilinear maps into `E â†’L[ğ•œ] F`. If `p` is the Taylor series (`HasFTaylorSeriesUpTo`) of a
function, then `p.shift` is the Taylor series of the derivative of the function. Note that the
`p.sum` of a Taylor series `p` does not give the original function; for a formal multilinear
series that sums to the derivative of `p.sum`, see `HasFPowerSeriesOnBall.fderiv`. -/
def shift : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F) := fun n => (p n.succ).curryRight

/-- Adding a zeroth term to a formal multilinear series taking values in `E â†’L[ğ•œ] F`. This
corresponds to starting from a Taylor series (`HasFTaylorSeriesUpTo`) for the derivative of a
function, and building a Taylor series for the function itself. -/
def unshift (q : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F)) (z : F) : FormalMultilinearSeries ğ•œ E F
  | 0 => (continuousMultilinearCurryFin0 ğ•œ E F).symm z
  | n + 1 => (continuousMultilinearCurryRightEquiv' ğ•œ n E F).symm (q n)

theorem unshift_shift {p : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F)} {z : F} :
    (p.unshift z).shift = p := by
  ext1 n
  simp [shift, unshift]
  exact LinearIsometryEquiv.apply_symm_apply (continuousMultilinearCurryRightEquiv' ğ•œ n E F) (p n)

end FormalMultilinearSeries

section

variable [Semiring ğ•œ] [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E] [ContinuousAdd E]
  [ContinuousConstSMul ğ•œ E] [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F]
  [ContinuousAdd F] [ContinuousConstSMul ğ•œ F] [AddCommMonoid G] [Module ğ•œ G]
  [TopologicalSpace G] [ContinuousAdd G] [ContinuousConstSMul ğ•œ G]

namespace ContinuousLinearMap

/-- Composing each term `pâ‚™` in a formal multilinear series with a continuous linear map `f` on the
left gives a new formal multilinear series `f.compFormalMultilinearSeries p` whose general term
is `f âˆ˜ pâ‚™`. -/
def compFormalMultilinearSeries (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F) :
    FormalMultilinearSeries ğ•œ E G := fun n => f.compContinuousMultilinearMap (p n)

@[simp]
theorem compFormalMultilinearSeries_apply (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F)
    (n : â„•) : (f.compFormalMultilinearSeries p) n = f.compContinuousMultilinearMap (p n) :=
  rfl

theorem compFormalMultilinearSeries_apply' (f : F â†’L[ğ•œ] G) (p : FormalMultilinearSeries ğ•œ E F)
    (n : â„•) (v : Fin n â†’ E) : (f.compFormalMultilinearSeries p) n v = f (p n v) :=
  rfl

end ContinuousLinearMap

namespace ContinuousMultilinearMap

variable {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, AddCommGroup (E i)] [âˆ€ i, Module ğ•œ (E i)]
  [âˆ€ i, TopologicalSpace (E i)] [âˆ€ i, IsTopologicalAddGroup (E i)]
  [âˆ€ i, ContinuousConstSMul ğ•œ (E i)] [Fintype Î¹] (f : ContinuousMultilinearMap ğ•œ E F)

/-- Realize a ContinuousMultilinearMap on `âˆ€ i : Î¹, E i` as the evaluation of a
FormalMultilinearSeries by choosing an arbitrary identification `Î¹ â‰ƒ Fin (Fintype.card Î¹)`. -/
noncomputable def toFormalMultilinearSeries : FormalMultilinearSeries ğ•œ (âˆ€ i, E i) F :=
  fun n â†¦ if h : Fintype.card Î¹ = n then
    (f.compContinuousLinearMap .proj).domDomCongr (Fintype.equivFinOfCardEq h)
  else 0

end ContinuousMultilinearMap

end

namespace FormalMultilinearSeries

section Order

variable [Semiring ğ•œ] {n : â„•} [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E]
  [ContinuousAdd E] [ContinuousConstSMul ğ•œ E] [AddCommMonoid F] [Module ğ•œ F]
  [TopologicalSpace F] [ContinuousAdd F] [ContinuousConstSMul ğ•œ F]
  {p : FormalMultilinearSeries ğ•œ E F}

/-- The index of the first non-zero coefficient in `p` (or `0` if all coefficients are zero). This
  is the order of the isolated zero of an analytic function `f` at a point if `p` is the Taylor
  series of `f` at that point. -/
noncomputable def order (p : FormalMultilinearSeries ğ•œ E F) : â„• :=
  sInf { n | p n â‰  0 }

@[simp]
theorem order_zero : (0 : FormalMultilinearSeries ğ•œ E F).order = 0 := by simp [order]

theorem ne_zero_of_order_ne_zero (hp : p.order â‰  0) : p â‰  0 := fun h => by simp [h] at hp

theorem order_eq_find [DecidablePred fun n => p n â‰  0] (hp : âˆƒ n, p n â‰  0) :
    p.order = Nat.find hp := by convert Nat.sInf_def hp

theorem order_eq_find' [DecidablePred fun n => p n â‰  0] (hp : p â‰  0) :
    p.order = Nat.find (FormalMultilinearSeries.ne_iff.mp hp) :=
  order_eq_find _

theorem order_eq_zero_iff' : p.order = 0 â†” p = 0 âˆ¨ p 0 â‰  0 := by
  simpa [order, Nat.sInf_eq_zero, FormalMultilinearSeries.ext_iff, eq_empty_iff_forall_notMem]
    using or_comm

theorem order_eq_zero_iff (hp : p â‰  0) : p.order = 0 â†” p 0 â‰  0 := by
  simp [order_eq_zero_iff', hp]

theorem apply_order_ne_zero (hp : p â‰  0) : p p.order â‰  0 :=
  Nat.sInf_mem (FormalMultilinearSeries.ne_iff.1 hp)

theorem apply_order_ne_zero' (hp : p.order â‰  0) : p p.order â‰  0 :=
  apply_order_ne_zero (ne_zero_of_order_ne_zero hp)

theorem apply_eq_zero_of_lt_order (hp : n < p.order) : p n = 0 :=
  by_contra <| Nat.notMem_of_lt_sInf hp

end Order

section Coef

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {p : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E} {n : â„•} {z : ğ•œ} {y : Fin n â†’ ğ•œ}

/-- The `n`th coefficient of `p` when seen as a power series. -/
def coeff (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•) : E :=
  p n 1

theorem mkPiRing_coeff_eq (p : FormalMultilinearSeries ğ•œ ğ•œ E) (n : â„•) :
    ContinuousMultilinearMap.mkPiRing ğ•œ (Fin n) (p.coeff n) = p n :=
  (p n).mkPiRing_apply_one_eq_self

@[simp]
theorem apply_eq_prod_smul_coeff : p n y = (âˆ i, y i) â€¢ p.coeff n := by
  convert (p n).toMultilinearMap.map_smul_univ y 1
  simp only [Pi.one_apply, Algebra.id.smul_eq_mul, mul_one]

theorem coeff_eq_zero : p.coeff n = 0 â†” p n = 0 := by
  rw [â† mkPiRing_coeff_eq p, ContinuousMultilinearMap.mkPiRing_eq_zero_iff]

theorem apply_eq_pow_smul_coeff : (p n fun _ => z) = z ^ n â€¢ p.coeff n := by simp

@[simp]
theorem norm_apply_eq_norm_coef : â€–p nâ€– = â€–coeff p nâ€– := by
  rw [â† mkPiRing_coeff_eq p, ContinuousMultilinearMap.norm_mkPiRing]

end Coef

section Fslope

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {p : FormalMultilinearSeries ğ•œ ğ•œ E} {n : â„•}

/-- The formal counterpart of `dslope`, corresponding to the expansion of `(f z - f 0) / z`. If `f`
has `p` as a power series, then `dslope f` has `fslope p` as a power series. -/
noncomputable def fslope (p : FormalMultilinearSeries ğ•œ ğ•œ E) : FormalMultilinearSeries ğ•œ ğ•œ E :=
  fun n => (p (n + 1)).curryLeft 1

@[simp]
theorem coeff_fslope : p.fslope.coeff n = p.coeff (n + 1) := by
  simp only [fslope, coeff, ContinuousMultilinearMap.curryLeft_apply]
  congr 1
  exact Fin.cons_self_tail (fun _ => (1 : ğ•œ))

@[simp]
theorem coeff_iterate_fslope (k n : â„•) : (fslope^[k] p).coeff n = p.coeff (n + k) := by
  induction k generalizing p with
  | zero => rfl
  | succ k ih => simp [ih, add_assoc]

end Fslope

end FormalMultilinearSeries

section Const

/-- The formal multilinear series where all terms of positive degree are equal to zero, and the term
of degree zero is `c`. It is the power series expansion of the constant function equal to `c`
everywhere. -/
def constFormalMultilinearSeries (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [ContinuousConstSMul ğ•œ E] [IsTopologicalAddGroup E]
    {F : Type*} [NormedAddCommGroup F] [IsTopologicalAddGroup F] [NormedSpace ğ•œ F]
    [ContinuousConstSMul ğ•œ F] (c : F) : FormalMultilinearSeries ğ•œ E F
  | 0 => ContinuousMultilinearMap.uncurry0 _ _ c
  | _ => 0

@[simp]
theorem constFormalMultilinearSeries_apply_zero [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] {c : F} :
    constFormalMultilinearSeries ğ•œ E c 0 = ContinuousMultilinearMap.uncurry0 _ _ c :=
  rfl

@[simp]
theorem constFormalMultilinearSeries_apply_succ [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] {c : F} {n : â„•} :
    constFormalMultilinearSeries ğ•œ E c (n + 1) = 0 :=
  rfl

theorem constFormalMultilinearSeries_apply_of_nonzero [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] {c : F}
    {n : â„•} (hn : n â‰  0) : constFormalMultilinearSeries ğ•œ E c n = 0 :=
  Nat.casesOn n (fun hn => (hn rfl).elim) (fun _ _ => rfl) hn

@[deprecated (since := "2025-06-23")]
alias constFormalMultilinearSeries_apply := constFormalMultilinearSeries_apply_of_nonzero

@[simp]
lemma constFormalMultilinearSeries_zero [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] :
    constFormalMultilinearSeries ğ•œ E (0 : F) = 0 := by
  ext n x
  simp only [FormalMultilinearSeries.zero_apply, ContinuousMultilinearMap.zero_apply,
    constFormalMultilinearSeries]
  induction n
  Â· simp only [ContinuousMultilinearMap.uncurry0_apply]
  Â· simp only [constFormalMultilinearSeries.match_1.eq_2, ContinuousMultilinearMap.zero_apply]

@[simp]
lemma compContinuousLinearMap_zero [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    [NormedAddCommGroup G] [NormedSpace ğ•œ G]
    (p : FormalMultilinearSeries ğ•œ F G) :
    p.compContinuousLinearMap (0 : E â†’L[ğ•œ] F) = constFormalMultilinearSeries ğ•œ E (p 0 0) := by
  ext n v
  cases n with
  | zero =>
    simp only [FormalMultilinearSeries.compContinuousLinearMap_apply, Matrix.zero_empty,
      constFormalMultilinearSeries_apply_zero, ContinuousMultilinearMap.uncurry0_apply]
    congr
    apply Subsingleton.allEq
  | succ =>
    simp [ContinuousLinearMap.coe_zero']

end Const

section Linear

variable [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace ContinuousLinearMap

/-- Formal power series of a continuous linear map `f : E â†’L[ğ•œ] F` at `x : E`:
`f y = f x + f (y - x)`. -/
def fpowerSeries (f : E â†’L[ğ•œ] F) (x : E) : FormalMultilinearSeries ğ•œ E F
  | 0 => ContinuousMultilinearMap.uncurry0 ğ•œ _ (f x)
  | 1 => (continuousMultilinearCurryFin1 ğ•œ E F).symm f
  | _ => 0

@[simp]
theorem fpowerSeries_apply_zero (f : E â†’L[ğ•œ] F) (x : E) :
    f.fpowerSeries x 0 = ContinuousMultilinearMap.uncurry0 ğ•œ _ (f x) :=
  rfl

@[simp]
theorem fpowerSeries_apply_one (f : E â†’L[ğ•œ] F) (x : E) :
    f.fpowerSeries x 1 = (continuousMultilinearCurryFin1 ğ•œ E F).symm f :=
  rfl

@[simp]
theorem fpowerSeries_apply_add_two (f : E â†’L[ğ•œ] F) (x : E) (n : â„•) : f.fpowerSeries x (n + 2) = 0 :=
  rfl

end ContinuousLinearMap

end Linear
