/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Inv
import Mathlib.Analysis.NormedSpace.Real

#align_import analysis.calculus.diff_cont_on_cl from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Functions differentiable on a domain and continuous on its closure

Many theorems in complex analysis assume that a function is complex differentiable on a domain and
is continuous on its closure. In this file we define a predicate `DiffContOnCl` that expresses
this property and prove basic facts about this predicate.
-/


open Set Filter Metric

open scoped Topology

variable (ğ•œ : Type*) {E F G : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] [NormedAddCommGroup G]
  [NormedSpace ğ•œ G] {f g : E â†’ F} {s t : Set E} {x : E}

/-- A predicate saying that a function is differentiable on a set and is continuous on its
closure. This is a common assumption in complex analysis. -/
structure DiffContOnCl (f : E â†’ F) (s : Set E) : Prop where
  protected differentiableOn : DifferentiableOn ğ•œ f s
  protected continuousOn : ContinuousOn f (closure s)
#align diff_cont_on_cl DiffContOnCl

variable {ğ•œ}

theorem DifferentiableOn.diffContOnCl (h : DifferentiableOn ğ•œ f (closure s)) : DiffContOnCl ğ•œ f s :=
  âŸ¨h.mono subset_closure, h.continuousOnâŸ©
#align differentiable_on.diff_cont_on_cl DifferentiableOn.diffContOnCl

theorem Differentiable.diffContOnCl (h : Differentiable ğ•œ f) : DiffContOnCl ğ•œ f s :=
  âŸ¨h.differentiableOn, h.continuous.continuousOnâŸ©
#align differentiable.diff_cont_on_cl Differentiable.diffContOnCl

theorem IsClosed.diffContOnCl_iff (hs : IsClosed s) : DiffContOnCl ğ•œ f s â†” DifferentiableOn ğ•œ f s :=
  âŸ¨fun h => h.differentiableOn, fun h => âŸ¨h, hs.closure_eq.symm â–¸ h.continuousOnâŸ©âŸ©
#align is_closed.diff_cont_on_cl_iff IsClosed.diffContOnCl_iff

theorem diffContOnCl_univ : DiffContOnCl ğ•œ f univ â†” Differentiable ğ•œ f :=
  isClosed_univ.diffContOnCl_iff.trans differentiableOn_univ
#align diff_cont_on_cl_univ diffContOnCl_univ

theorem diffContOnCl_const {c : F} : DiffContOnCl ğ•œ (fun _ : E => c) s :=
  âŸ¨differentiableOn_const c, continuousOn_constâŸ©
#align diff_cont_on_cl_const diffContOnCl_const

namespace DiffContOnCl

theorem comp {g : G â†’ E} {t : Set G} (hf : DiffContOnCl ğ•œ f s) (hg : DiffContOnCl ğ•œ g t)
    (h : MapsTo g t s) : DiffContOnCl ğ•œ (f âˆ˜ g) t :=
  âŸ¨hf.1.comp hg.1 h, hf.2.comp hg.2 <| h.closure_of_continuousOn hg.2âŸ©
#align diff_cont_on_cl.comp DiffContOnCl.comp

theorem continuousOn_ball [NormedSpace â„ E] {x : E} {r : â„} (h : DiffContOnCl ğ•œ f (ball x r)) :
    ContinuousOn f (closedBall x r) := by
  rcases eq_or_ne r 0 with (rfl | hr)
  Â· rw [closedBall_zero]
    exact continuousOn_singleton f x
  Â· rw [â† closure_ball x hr]
    exact h.continuousOn
#align diff_cont_on_cl.continuous_on_ball DiffContOnCl.continuousOn_ball

theorem mk_ball {x : E} {r : â„} (hd : DifferentiableOn ğ•œ f (ball x r))
    (hc : ContinuousOn f (closedBall x r)) : DiffContOnCl ğ•œ f (ball x r) :=
  âŸ¨hd, hc.mono <| closure_ball_subset_closedBallâŸ©
#align diff_cont_on_cl.mk_ball DiffContOnCl.mk_ball

protected theorem differentiableAt (h : DiffContOnCl ğ•œ f s) (hs : IsOpen s) (hx : x âˆˆ s) :
    DifferentiableAt ğ•œ f x :=
  h.differentiableOn.differentiableAt <| hs.mem_nhds hx
#align diff_cont_on_cl.differentiable_at DiffContOnCl.differentiableAt

theorem differentiableAt' (h : DiffContOnCl ğ•œ f s) (hx : s âˆˆ ğ“ x) : DifferentiableAt ğ•œ f x :=
  h.differentiableOn.differentiableAt hx
#align diff_cont_on_cl.differentiable_at' DiffContOnCl.differentiableAt'

protected theorem mono (h : DiffContOnCl ğ•œ f s) (ht : t âŠ† s) : DiffContOnCl ğ•œ f t :=
  âŸ¨h.differentiableOn.mono ht, h.continuousOn.mono (closure_mono ht)âŸ©
#align diff_cont_on_cl.mono DiffContOnCl.mono

theorem add (hf : DiffContOnCl ğ•œ f s) (hg : DiffContOnCl ğ•œ g s) : DiffContOnCl ğ•œ (f + g) s :=
  âŸ¨hf.1.add hg.1, hf.2.add hg.2âŸ©
#align diff_cont_on_cl.add DiffContOnCl.add

theorem add_const (hf : DiffContOnCl ğ•œ f s) (c : F) : DiffContOnCl ğ•œ (fun x => f x + c) s :=
  hf.add diffContOnCl_const
#align diff_cont_on_cl.add_const DiffContOnCl.add_const

theorem const_add (hf : DiffContOnCl ğ•œ f s) (c : F) : DiffContOnCl ğ•œ (fun x => c + f x) s :=
  diffContOnCl_const.add hf
#align diff_cont_on_cl.const_add DiffContOnCl.const_add

theorem neg (hf : DiffContOnCl ğ•œ f s) : DiffContOnCl ğ•œ (-f) s :=
  âŸ¨hf.1.neg, hf.2.negâŸ©
#align diff_cont_on_cl.neg DiffContOnCl.neg

theorem sub (hf : DiffContOnCl ğ•œ f s) (hg : DiffContOnCl ğ•œ g s) : DiffContOnCl ğ•œ (f - g) s :=
  âŸ¨hf.1.sub hg.1, hf.2.sub hg.2âŸ©
#align diff_cont_on_cl.sub DiffContOnCl.sub

theorem sub_const (hf : DiffContOnCl ğ•œ f s) (c : F) : DiffContOnCl ğ•œ (fun x => f x - c) s :=
  hf.sub diffContOnCl_const
#align diff_cont_on_cl.sub_const DiffContOnCl.sub_const

theorem const_sub (hf : DiffContOnCl ğ•œ f s) (c : F) : DiffContOnCl ğ•œ (fun x => c - f x) s :=
  diffContOnCl_const.sub hf
#align diff_cont_on_cl.const_sub DiffContOnCl.const_sub

theorem const_smul {R : Type*} [Semiring R] [Module R F] [SMulCommClass ğ•œ R F]
    [ContinuousConstSMul R F] (hf : DiffContOnCl ğ•œ f s) (c : R) : DiffContOnCl ğ•œ (c â€¢ f) s :=
  âŸ¨hf.1.const_smul c, hf.2.const_smul câŸ©
#align diff_cont_on_cl.const_smul DiffContOnCl.const_smul

theorem smul {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' F]
    [IsScalarTower ğ•œ ğ•œ' F] {c : E â†’ ğ•œ'} {f : E â†’ F} {s : Set E} (hc : DiffContOnCl ğ•œ c s)
    (hf : DiffContOnCl ğ•œ f s) : DiffContOnCl ğ•œ (fun x => c x â€¢ f x) s :=
  âŸ¨hc.1.smul hf.1, hc.2.smul hf.2âŸ©
#align diff_cont_on_cl.smul DiffContOnCl.smul

theorem smul_const {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
    [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F] {c : E â†’ ğ•œ'} {s : Set E} (hc : DiffContOnCl ğ•œ c s)
    (y : F) : DiffContOnCl ğ•œ (fun x => c x â€¢ y) s :=
  hc.smul diffContOnCl_const
#align diff_cont_on_cl.smul_const DiffContOnCl.smul_const

theorem inv {f : E â†’ ğ•œ} (hf : DiffContOnCl ğ•œ f s) (hâ‚€ : âˆ€ x âˆˆ closure s, f x â‰  0) :
    DiffContOnCl ğ•œ fâ»Â¹ s :=
  âŸ¨differentiableOn_inv.comp hf.1 fun _ hx => hâ‚€ _ (subset_closure hx), hf.2.invâ‚€ hâ‚€âŸ©
#align diff_cont_on_cl.inv DiffContOnCl.inv

end DiffContOnCl

theorem Differentiable.comp_diffContOnCl {g : G â†’ E} {t : Set G} (hf : Differentiable ğ•œ f)
    (hg : DiffContOnCl ğ•œ g t) : DiffContOnCl ğ•œ (f âˆ˜ g) t :=
  hf.diffContOnCl.comp hg (mapsTo_image _ _)
#align differentiable.comp_diff_cont_on_cl Differentiable.comp_diffContOnCl

theorem DifferentiableOn.diffContOnCl_ball {U : Set E} {c : E} {R : â„} (hf : DifferentiableOn ğ•œ f U)
    (hc : closedBall c R âŠ† U) : DiffContOnCl ğ•œ f (ball c R) :=
  DiffContOnCl.mk_ball (hf.mono (ball_subset_closedBall.trans hc)) (hf.continuousOn.mono hc)
#align differentiable_on.diff_cont_on_cl_ball DifferentiableOn.diffContOnCl_ball
