/-
Copyright (c) 2021 Yourong Zang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yourong Zang
-/
import Mathlib.Analysis.NormedSpace.ConformalLinearMap
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.FDeriv.RestrictScalars

#align_import analysis.calculus.conformal.normed_space from "leanprover-community/mathlib"@"e3fb84046afd187b710170887195d50bada934ee"

/-!
# Conformal Maps

A continuous linear map between real normed spaces `X` and `Y` is `ConformalAt` some point `x`
if it is real differentiable at that point and its differential is a conformal linear map.

## Main definitions

* `ConformalAt`: the main definition of conformal maps
* `Conformal`: maps that are conformal at every point

## Main results
* The conformality of the composition of two conformal maps, the identity map
  and multiplications by nonzero constants
* `conformalAt_iff_isConformalMap_fderiv`: an equivalent definition of the conformality of a map

In `Analysis.Calculus.Conformal.InnerProduct`:
* `conformalAt_iff`: an equivalent definition of the conformality of a map

In `Geometry.Euclidean.Angle.Unoriented.Conformal`:
* `ConformalAt.preserves_angle`: if a map is conformal at `x`, then its differential preserves
  all angles at `x`

## Tags

conformal

## Warning

The definition of conformality in this file does NOT require the maps to be orientation-preserving.
Maps such as the complex conjugate are considered to be conformal.
-/


noncomputable section

variable {X Y Z : Type*} [NormedAddCommGroup X] [NormedAddCommGroup Y] [NormedAddCommGroup Z]
  [NormedSpace ‚Ñù X] [NormedSpace ‚Ñù Y] [NormedSpace ‚Ñù Z]

section LocConformality

open LinearIsometry ContinuousLinearMap

/-- A map `f` is said to be conformal if it has a conformal differential `f'`. -/
def ConformalAt (f : X ‚Üí Y) (x : X) :=
  ‚àÉ f' : X ‚ÜíL[‚Ñù] Y, HasFDerivAt f f' x ‚àß IsConformalMap f'
#align conformal_at ConformalAt

theorem conformalAt_id (x : X) : ConformalAt _root_.id x :=
  ‚ü®id ‚Ñù X, hasFDerivAt_id _, isConformalMap_id‚ü©
#align conformal_at_id conformalAt_id

theorem conformalAt_const_smul {c : ‚Ñù} (h : c ‚â† 0) (x : X) : ConformalAt (fun x' : X => c ‚Ä¢ x') x :=
  ‚ü®c ‚Ä¢ ContinuousLinearMap.id ‚Ñù X, (hasFDerivAt_id x).const_smul c, isConformalMap_const_smul h‚ü©
#align conformal_at_const_smul conformalAt_const_smul

@[nontriviality]
theorem Subsingleton.conformalAt [Subsingleton X] (f : X ‚Üí Y) (x : X) : ConformalAt f x :=
  ‚ü®0, hasFDerivAt_of_subsingleton _ _, isConformalMap_of_subsingleton _‚ü©
#align subsingleton.conformal_at Subsingleton.conformalAt

/-- A function is a conformal map if and only if its differential is a conformal linear map-/
theorem conformalAt_iff_isConformalMap_fderiv {f : X ‚Üí Y} {x : X} :
    ConformalAt f x ‚Üî IsConformalMap (fderiv ‚Ñù f x) := by
  constructor
  -- ‚ä¢ ConformalAt f x ‚Üí IsConformalMap (fderiv ‚Ñù f x)
  ¬∑ rintro ‚ü®f', hf, hf'‚ü©
    -- ‚ä¢ IsConformalMap (fderiv ‚Ñù f x)
    rwa [hf.fderiv]
    -- üéâ no goals
  ¬∑ intro H
    -- ‚ä¢ ConformalAt f x
    by_cases h : DifferentiableAt ‚Ñù f x
    -- ‚ä¢ ConformalAt f x
    ¬∑ exact ‚ü®fderiv ‚Ñù f x, h.hasFDerivAt, H‚ü©
      -- üéâ no goals
    ¬∑ nontriviality X
      -- ‚ä¢ ConformalAt f x
      exact absurd (fderiv_zero_of_not_differentiableAt h) H.ne_zero
      -- üéâ no goals
#align conformal_at_iff_is_conformal_map_fderiv conformalAt_iff_isConformalMap_fderiv

namespace ConformalAt

theorem differentiableAt {f : X ‚Üí Y} {x : X} (h : ConformalAt f x) : DifferentiableAt ‚Ñù f x :=
  let ‚ü®_, h‚ÇÅ, _‚ü© := h
  h‚ÇÅ.differentiableAt
#align conformal_at.differentiable_at ConformalAt.differentiableAt

theorem congr {f g : X ‚Üí Y} {x : X} {u : Set X} (hx : x ‚àà u) (hu : IsOpen u) (hf : ConformalAt f x)
    (h : ‚àÄ x : X, x ‚àà u ‚Üí g x = f x) : ConformalAt g x :=
  let ‚ü®f', hfderiv, hf'‚ü© := hf
  ‚ü®f', hfderiv.congr_of_eventuallyEq ((hu.eventually_mem hx).mono h), hf'‚ü©
#align conformal_at.congr ConformalAt.congr

theorem comp {f : X ‚Üí Y} {g : Y ‚Üí Z} (x : X) (hg : ConformalAt g (f x)) (hf : ConformalAt f x) :
    ConformalAt (g ‚àò f) x := by
  rcases hf with ‚ü®f', hf‚ÇÅ, cf‚ü©
  -- ‚ä¢ ConformalAt (g ‚àò f) x
  rcases hg with ‚ü®g', hg‚ÇÅ, cg‚ü©
  -- ‚ä¢ ConformalAt (g ‚àò f) x
  exact ‚ü®g'.comp f', hg‚ÇÅ.comp x hf‚ÇÅ, cg.comp cf‚ü©
  -- üéâ no goals
#align conformal_at.comp ConformalAt.comp

theorem const_smul {f : X ‚Üí Y} {x : X} {c : ‚Ñù} (hc : c ‚â† 0) (hf : ConformalAt f x) :
    ConformalAt (c ‚Ä¢ f) x :=
  (conformalAt_const_smul hc <| f x).comp x hf
#align conformal_at.const_smul ConformalAt.const_smul

end ConformalAt

end LocConformality

section GlobalConformality

/-- A map `f` is conformal if it's conformal at every point. -/
def Conformal (f : X ‚Üí Y) :=
  ‚àÄ x : X, ConformalAt f x
#align conformal Conformal

theorem conformal_id : Conformal (id : X ‚Üí X) := fun x => conformalAt_id x
#align conformal_id conformal_id

theorem conformal_const_smul {c : ‚Ñù} (h : c ‚â† 0) : Conformal fun x : X => c ‚Ä¢ x := fun x =>
  conformalAt_const_smul h x
#align conformal_const_smul conformal_const_smul

namespace Conformal

theorem conformalAt {f : X ‚Üí Y} (h : Conformal f) (x : X) : ConformalAt f x :=
  h x
#align conformal.conformal_at Conformal.conformalAt

theorem differentiable {f : X ‚Üí Y} (h : Conformal f) : Differentiable ‚Ñù f := fun x =>
  (h x).differentiableAt
#align conformal.differentiable Conformal.differentiable

theorem comp {f : X ‚Üí Y} {g : Y ‚Üí Z} (hf : Conformal f) (hg : Conformal g) : Conformal (g ‚àò f) :=
  fun x => (hg <| f x).comp x (hf x)
#align conformal.comp Conformal.comp

theorem const_smul {f : X ‚Üí Y} (hf : Conformal f) {c : ‚Ñù} (hc : c ‚â† 0) : Conformal (c ‚Ä¢ f) :=
  fun x => (hf x).const_smul hc
#align conformal.const_smul Conformal.const_smul

end Conformal

end GlobalConformality
