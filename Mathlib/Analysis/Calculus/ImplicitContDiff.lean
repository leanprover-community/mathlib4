/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.Calculus.ImplicitFunction.OfProdDomain
public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

/-!
# Implicit function theorem

In this file, we apply the generalised implicit function theorem to the more familiar case and show
that the implicit function preserves the smoothness class of the implicit equation.

Let `Eâ‚`, `Eâ‚‚`, and `F` be real or complex Banach spaces. Let `f : Eâ‚ Ã— Eâ‚‚ â†’ F` be a function that
is $C^n$ at a point `(uâ‚, uâ‚‚) : Eâ‚ Ã— Eâ‚‚`, where `n â‰¥ 1`. Let `f'` be the derivative of `f` at
`(uâ‚, uâ‚‚)`. If the map `y â†¦ f' (0, y)` is a Banach space isomorphism, then there exists a function
`Ïˆ : Eâ‚ â†’ Eâ‚‚` such that `Ïˆ uâ‚ = uâ‚‚`, and `f (x, Ïˆ x) = f (uâ‚, uâ‚‚)` holds for all `x` in a
neighbourhood of `uâ‚`. Furthermore, `Ïˆ` is $C^n$ at `uâ‚`.

## TODO
* FaÃ  di Bruno.

## Tags

implicit function, inverse function
-/

@[expose] public section

variable {ğ•œ : Type*} [RCLike ğ•œ]
  {Eâ‚ : Type*} [NormedAddCommGroup Eâ‚] [NormedSpace ğ•œ Eâ‚] [CompleteSpace Eâ‚]
  {Eâ‚‚ : Type*} [NormedAddCommGroup Eâ‚‚] [NormedSpace ğ•œ Eâ‚‚] [CompleteSpace Eâ‚‚]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]

namespace ImplicitFunctionData

/-- The implicit function defined by a $C^n$ implicit equation is $C^n$. This applies to the general
form of the implicit function theorem. -/
theorem contDiff_implicitFunction {Ï† : ImplicitFunctionData ğ•œ Eâ‚ Eâ‚‚ F} {n : WithTop â„•âˆ}
    (hl : ContDiffAt ğ•œ n Ï†.leftFun Ï†.pt) (hr : ContDiffAt ğ•œ n Ï†.rightFun Ï†.pt) (hn : n â‰  0) :
    ContDiffAt ğ•œ n Ï†.implicitFunction.uncurry (Ï†.prodFun Ï†.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toOpenPartialHomeomorph,
    â† HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (Ï†.hasStrictFDerivAt |>.hasFDerivAt) hn

end ImplicitFunctionData

open scoped Topology

namespace ContDiffAt

variable {u : Eâ‚ Ã— Eâ‚‚} {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {n : WithTop â„•âˆ}

/-- Implicit function `Ïˆ` defined by `f (x, Ïˆ x) = f u`. -/
noncomputable def implicitFunction
    (cdf : ContDiffAt ğ•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    Eâ‚ â†’ Eâ‚‚ :=
  (cdf.hasStrictFDerivAt pn).implicitFunctionOfProdDomain ifâ‚‚

/-- `implicitFunction` is indeed the (local) implicit function defined by `f`. -/
theorem image_implicitFunction
    (cdf : ContDiffAt ğ•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  x in ğ“ u.1, f (x, cdf.implicitFunction pn ifâ‚‚ x) = f u :=
  (cdf.hasStrictFDerivAt pn).image_implicitFunctionOfProdDomain ifâ‚‚

theorem eventually_implicitFunction_apply_eq
    (cdf : ContDiffAt ğ•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  v in ğ“ u, f v = f u â†” cdf.implicitFunction pn ifâ‚‚ v.1 = v.2 :=
  (cdf.hasStrictFDerivAt pn).image_eq_iff_implicitFunctionOfProdDomain ifâ‚‚

/-- If the implicit equation `f` is $C^n$ at `(uâ‚, uâ‚‚)`, then its implicit function `Ïˆ` around `uâ‚`
is also $C^n$ at `uâ‚`. -/
theorem contDiffAt_implicitFunction
    (cdf : ContDiffAt ğ•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    ContDiffAt ğ•œ n (cdf.implicitFunction pn ifâ‚‚) u.1 := by
  have := (cdf.hasStrictFDerivAt pn).implicitFunctionDataOfProdDomain ifâ‚‚
    |>.contDiff_implicitFunction cdf contDiffAt_fst pn
  unfold implicitFunction HasStrictFDerivAt.implicitFunctionOfProdDomain
  fun_prop

theorem hasStrictFDerivAt_implicitFunction
    (cdf : ContDiffAt ğ•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    HasStrictFDerivAt (cdf.implicitFunction pn ifâ‚‚)
      (-(fderiv ğ•œ f u âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).inverse âˆ˜L (fderiv ğ•œ f u âˆ˜L .inl ğ•œ Eâ‚ Eâ‚‚)) u.1 :=
  (cdf.hasStrictFDerivAt pn).hasStrictFDerivAt_implicitFunctionOfProdDomain ifâ‚‚

end ContDiffAt

/-- A predicate stating the sufficient conditions on an implicit equation `f : Eâ‚ Ã— Eâ‚‚ â†’ F` that
will lead to a $C^n$ implicit function `Ïˆ : Eâ‚ â†’ Eâ‚‚`. -/
@[deprecated "ContDiffAt.implicitFunction does not require this" (since := "2026-01-27")]
structure IsContDiffImplicitAt (n : WithTop â„•âˆ) (f : Eâ‚ Ã— Eâ‚‚ â†’ F) (f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F)
    (u : Eâ‚ Ã— Eâ‚‚) : Prop where
  hasFDerivAt : HasFDerivAt f f' u
  contDiffAt : ContDiffAt ğ•œ n f u
  bijective : Function.Bijective (f'.comp (ContinuousLinearMap.inr ğ•œ Eâ‚ Eâ‚‚))
  ne_zero : n â‰  0

namespace IsContDiffImplicitAt

@[deprecated (since := "2026-01-27")]
alias implicitFunction := ContDiffAt.implicitFunction

@[deprecated (since := "2026-01-27")]
alias apply_implicitFunction := ContDiffAt.image_implicitFunction

@[deprecated (since := "2026-01-27")]
alias eventually_implicitFunction_apply_eq := ContDiffAt.eventually_implicitFunction_apply_eq

@[deprecated (since := "2026-01-27")]
alias contDiffAt_implicitFunction := ContDiffAt.contDiffAt_implicitFunction

end IsContDiffImplicitAt

end
