/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.Calculus.ImplicitFunction.OfProdDomain
public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

/-!
# Implicit function theorem

In this file, we apply the generalised implicit function theorem to the more familiar case and show
that the implicit function preserves the smoothness class of the implicit equation.

Let `E‚ÇÅ`, `E‚ÇÇ`, and `F` be real or complex Banach spaces. Let `f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F` be a function that
is $C^n$ at a point `(u‚ÇÅ, u‚ÇÇ) : E‚ÇÅ √ó E‚ÇÇ`, where `n ‚â• 1`. Let `f'` be the derivative of `f` at
`(u‚ÇÅ, u‚ÇÇ)`. If the map `y ‚Ü¶ f' (0, y)` is a Banach space isomorphism, then there exists a function
`œà : E‚ÇÅ ‚Üí E‚ÇÇ` such that `œà u‚ÇÅ = u‚ÇÇ`, and `f (x, œà x) = f (u‚ÇÅ, u‚ÇÇ)` holds for all `x` in a
neighbourhood of `u‚ÇÅ`. Furthermore, `œà` is $C^n$ at `u‚ÇÅ`.

## TODO
* Local uniqueness of the implicit function
* Derivative of the implicit function

## Tags

implicit function, inverse function
-/

@[expose] public section

variable {ùïú : Type*} [RCLike ùïú]
  {E‚ÇÅ : Type*} [NormedAddCommGroup E‚ÇÅ] [NormedSpace ùïú E‚ÇÅ] [CompleteSpace E‚ÇÅ]
  {E‚ÇÇ : Type*} [NormedAddCommGroup E‚ÇÇ] [NormedSpace ùïú E‚ÇÇ] [CompleteSpace E‚ÇÇ]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F] [CompleteSpace F]

namespace ImplicitFunctionData

/-- The implicit function defined by a $C^n$ implicit equation is $C^n$. This applies to the general
form of the implicit function theorem. -/
theorem contDiff_implicitFunction {œÜ : ImplicitFunctionData ùïú E‚ÇÅ E‚ÇÇ F} {n : WithTop ‚Ñï‚àû}
    (hl : ContDiffAt ùïú n œÜ.leftFun œÜ.pt) (hr : ContDiffAt ùïú n œÜ.rightFun œÜ.pt) (hn : n ‚â† 0) :
    ContDiffAt ùïú n œÜ.implicitFunction.uncurry (œÜ.prodFun œÜ.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toOpenPartialHomeomorph,
    ‚Üê HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (œÜ.hasStrictFDerivAt |>.hasFDerivAt) hn

end ImplicitFunctionData

open scoped Topology

namespace ContDiffAt

/-- Implicit function `œà` defined by `f (x, œà x) = f u`. -/
noncomputable def implicitFunction {f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F} {u : E‚ÇÅ √ó E‚ÇÇ} {n : WithTop ‚Ñï‚àû}
    (cdf : ContDiffAt ùïú n f u) (pn : n ‚â† 0) (if‚ÇÇ : (fderiv ùïú f u ‚àòL .inr ùïú E‚ÇÅ E‚ÇÇ).IsInvertible) :
    E‚ÇÅ ‚Üí E‚ÇÇ :=
  (cdf.hasStrictFDerivAt pn).implicitFunctionOfProdDomain if‚ÇÇ

/-- `implicitFunction` is indeed the (local) implicit function defined by `f`. -/
theorem image_implicitFunction {f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F} {u : E‚ÇÅ √ó E‚ÇÇ} {n : WithTop ‚Ñï‚àû}
    (cdf : ContDiffAt ùïú n f u) (pn : n ‚â† 0) (if‚ÇÇ : (fderiv ùïú f u ‚àòL .inr ùïú E‚ÇÅ E‚ÇÇ).IsInvertible) :
    ‚àÄ·∂† x in ùìù u.1, f (x, cdf.implicitFunction pn if‚ÇÇ x) = f u :=
  (cdf.hasStrictFDerivAt pn).image_implicitFunctionOfProdDomain if‚ÇÇ

theorem eventually_implicitFunction_apply_eq {f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F} {u : E‚ÇÅ √ó E‚ÇÇ} {n : WithTop ‚Ñï‚àû}
    (cdf : ContDiffAt ùïú n f u) (pn : n ‚â† 0) (if‚ÇÇ : (fderiv ùïú f u ‚àòL .inr ùïú E‚ÇÅ E‚ÇÇ).IsInvertible) :
    ‚àÄ·∂† v in ùìù u, f v = f u ‚Üî cdf.implicitFunction pn if‚ÇÇ v.1 = v.2 :=
  (cdf.hasStrictFDerivAt pn).image_eq_iff_implicitFunctionOfProdDomain if‚ÇÇ

/-- If the implicit equation `f` is $C^n$ at `(u‚ÇÅ, u‚ÇÇ)`, then its implicit function `œà` around `u‚ÇÅ`
is also $C^n$ at `u‚ÇÅ`. -/
theorem contDiffAt_implicitFunction {f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F} {u : E‚ÇÅ √ó E‚ÇÇ} {n : WithTop ‚Ñï‚àû}
    (cdf : ContDiffAt ùïú n f u) (pn : n ‚â† 0) (if‚ÇÇ : (fderiv ùïú f u ‚àòL .inr ùïú E‚ÇÅ E‚ÇÇ).IsInvertible) :
    ContDiffAt ùïú n (cdf.implicitFunction pn if‚ÇÇ) u.1 := by
  have := (cdf.hasStrictFDerivAt pn).implicitFunctionDataOfProdDomain if‚ÇÇ
            |>.contDiff_implicitFunction cdf contDiffAt_fst pn
  unfold implicitFunction HasStrictFDerivAt.implicitFunctionOfProdDomain
  fun_prop

end ContDiffAt

/-- A predicate stating the sufficient conditions on an implicit equation `f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F` that
will lead to a $C^n$ implicit function `œà : E‚ÇÅ ‚Üí E‚ÇÇ`. -/
@[deprecated "ContDiffAt.implicitFunction does not require this" (since := "2026-01-19")]
structure IsContDiffImplicitAt (n : WithTop ‚Ñï‚àû) (f : E‚ÇÅ √ó E‚ÇÇ ‚Üí F) (f' : E‚ÇÅ √ó E‚ÇÇ ‚ÜíL[ùïú] F)
    (u : E‚ÇÅ √ó E‚ÇÇ) : Prop where
  hasFDerivAt : HasFDerivAt f f' u
  contDiffAt : ContDiffAt ùïú n f u
  bijective : Function.Bijective (f'.comp (ContinuousLinearMap.inr ùïú E‚ÇÅ E‚ÇÇ))
  ne_zero : n ‚â† 0

namespace IsContDiffImplicitAt

@[deprecated (since := "2026-01-19")]
alias implicitFunction := ContDiffAt.implicitFunction

@[deprecated (since := "2026-01-19")]
alias apply_implicitFunction := ContDiffAt.image_implicitFunction

@[deprecated (since := "2026-01-19")]
alias eventually_implicitFunction_apply_eq := ContDiffAt.eventually_implicitFunction_apply_eq

@[deprecated (since := "2026-01-19")]
alias contDiffAt_implicitFunction := ContDiffAt.contDiffAt_implicitFunction

end IsContDiffImplicitAt

end
