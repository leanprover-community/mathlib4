/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.Calculus.Implicit
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

noncomputable section

open scoped Topology

open Filter

open ContinuousLinearMap (fst snd smulRight ker_prod)

open ContinuousLinearEquiv (ofBijective)

open LinearMap (ker range)

namespace ImplicitFunctionData

variable {ğ•œ : Type*} [RCLike ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [CompleteSpace E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [CompleteSpace F] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [CompleteSpace G]
  (Ï† : ImplicitFunctionData ğ•œ E F G) {n : WithTop â„•âˆ}

theorem contDiff_implicitFunction (hl : ContDiffAt ğ•œ n Ï†.leftFun Ï†.pt)
    (hr : ContDiffAt ğ•œ n Ï†.rightFun Ï†.pt) (hn : 1 â‰¤ n) :
    ContDiffAt ğ•œ n Ï†.implicitFunction.uncurry (Ï†.prodFun Ï†.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toPartialHomeomorph,
    â† HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (Ï†.hasStrictFDerivAt |>.hasFDerivAt) hn

variable {f : E Ã— F â†’ F} {f' : E Ã— F â†’L[ğ•œ] F} {a : E Ã— F}

/-- Function `y : E â†’ F` defined implicitly by `f (x, y) = z`.

Note: For the smoothness theorem for integral curves, `f = 0` is the integral equation. The
derivative of `f` wrt the curve is invertible (`hf'`) and independent of `x` (`hf''`). -/
def implicitFunctionDataOfProd (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) :
    ImplicitFunctionData ğ•œ (E Ã— F) E F where
  leftFun := Prod.fst
  leftDeriv := ContinuousLinearMap.fst ğ•œ E F
  rightFun := f
  rightDeriv := f'
  pt := a
  left_has_deriv := by fun_prop
  right_has_deriv := hf
  left_range := LinearMap.range_eq_top_of_surjective _ fun x â†¦ âŸ¨(x, 0), rflâŸ©
  right_range := hf'
  isCompl_ker := by
    have : LinearMap.ker (ContinuousLinearMap.fst ğ•œ E F) = LinearMap.ker (LinearMap.fst ğ•œ E F) :=
      rfl
    rw [isCompl_comm, this, LinearMap.ker_fst, hf'']
    exact LinearMap.isCompl_range_inl_inr

@[simp]
lemma implicitFunctionDataOfProd_prodFun (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) :
    (implicitFunctionDataOfProd hf hf' hf'').prodFun (implicitFunctionDataOfProd hf hf' hf'').pt =
      (a.1, f a) := rfl

-- /-- A partial homeomorphism between `E` and `F Ã— f'.ker` sending level surfaces of `f`
-- to vertical subspaces. -/
-- def implicitToPartialHomeomorphOfProd (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
--     (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) : PartialHomeomorph (E Ã— F) (E Ã— F) :=
--   (implicitFunctionDataOfProd hf hf' hf'').toPartialHomeomorph

def implicitFunctionOfProd (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) : E â†’ F â†’ E Ã— F :=
  (implicitFunctionDataOfProd hf hf' hf'').implicitFunction

/-- Implicit function `y` defined by `f (x, y x) = f a`. -/
def implicitFunctionOfProd' (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) : E â†’ F :=
  fun x â†¦ (implicitFunctionOfProd hf hf' hf'' x (f a)).2

lemma implicitFunctionOfProd_fst (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) :
    âˆ€á¶  p in ğ“ (a.1, f a), (implicitFunctionOfProd hf hf' hf'' p.1 p.2).1 = p.1 := by
  exact (implicitFunctionDataOfProd hf hf' hf'').prod_map_implicitFunction.mono
    fun _ â†¦ congr_arg Prod.fst

-- lemma for `implicitFunctionOfProd_snd` but only at `(x, f a)`

lemma rightFun_implicitFunctionOfProd (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) :
    âˆ€á¶  p in ğ“ (a.1, f a), f (implicitFunctionOfProd hf hf' hf'' p.1 p.2) = p.2 :=
  (implicitFunctionDataOfProd hf hf' hf'').prod_map_implicitFunction.mono
    fun _ â†¦ congr_arg Prod.snd

section

variable {Î± Î² Î³ Î´ : Type*} {Î¹ : Sort*}
variable {s : Set Î±} {t : Set Î²} {f : Filter Î±} {g : Filter Î²}

theorem Filter.eventually_prod_iff_exists_mem {p : Î± Ã— Î² â†’ Prop} :
    (âˆ€á¶  x in f Ã—Ë¢ g, p x) â†” âˆƒ s âˆˆ f, âˆƒ t âˆˆ g, âˆ€ x âˆˆ s, âˆ€ y âˆˆ t, p âŸ¨x, yâŸ© := by
  rw [Filter.eventually_iff_exists_mem]
  refine âŸ¨fun âŸ¨st, hst, hâŸ© â†¦ ?_, fun âŸ¨s, hs, t, ht, hâŸ© â†¦ ?_âŸ©
  Â· have âŸ¨s, hs, t, ht, hpâŸ© := Filter.mem_prod_iff.mp hst
    exact âŸ¨s, hs, t, ht, fun x hx y hy â†¦ h _ <| hp âŸ¨hx, hyâŸ©âŸ©
  Â· exact âŸ¨s Ã—Ë¢ t, Filter.prod_mem_prod hs ht, fun âŸ¨x, yâŸ© âŸ¨hx, hyâŸ© â†¦ h x hx y hyâŸ©

end

/-- `implicitFunctionOfProd' .. : E â†’ F` is indeed the (local) implicit function to `f`. -/
lemma rightFun_implicitFunctionOfProd' (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) :
    âˆ€á¶  x in ğ“ a.1, f (x, implicitFunctionOfProd' hf hf' hf'' x) = f a := by
  have := rightFun_implicitFunctionOfProd hf hf' hf''
  have hfst := implicitFunctionOfProd_fst hf hf' hf''
  rw [nhds_prod_eq, eventually_swap_iff] at this hfst
  replace := this.curry.self_of_nhds
  replace hfst := hfst.curry.self_of_nhds
  apply this.mp
  apply hfst.mono
  intro x
  simp_rw [Prod.swap_prod_mk]
  intro h h'
  rw [â† h', implicitFunctionOfProd']
  congr 1
  ext
  Â· rw [h]
  Â· rfl

/-- If `f` is $C^n$ at `(x, y)`, then its implicit function around `x` is also $C^n$ at `x`. -/
theorem contDiff_implicitFunctionOfProd' (h : ContDiffAt ğ•œ n f a) (hf : HasFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hf'' : ker f' = range (LinearMap.inl ğ•œ E F)) (hn : 1 â‰¤ n) :
    ContDiffAt ğ•œ n (implicitFunctionOfProd' (h.hasStrictFDerivAt' hf hn) hf' hf'') a.1 := by
  have := implicitFunctionDataOfProd (h.hasStrictFDerivAt' hf hn) hf' hf''
    |>.contDiff_implicitFunction contDiffAt_fst h hn
  rw [â† implicitFunctionOfProd, implicitFunctionDataOfProd_prodFun] at this
  -- make a lemma
  have heq : implicitFunctionOfProd' (h.hasStrictFDerivAt' hf hn) hf' hf'' = fun x â†¦
      ((implicitFunctionOfProd (h.hasStrictFDerivAt' hf hn) hf' hf'').uncurry (x, (f a))).2 := rfl
  rw [heq]
  fun_prop

end ImplicitFunctionData

end
