/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
import Mathlib.Analysis.Calculus.Implicit
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

/-!
# Implicit function theorem

In this file, we apply the generalised implicit function theorem to the more familiar case and show
that the implicit function preserves the smoothness class of the implicit equation.

Let `E` and `F` be real or complex Banach spaces. Let `f : E Ã— F â†’ F` be a function that is $C^n$ at
a point `(a, b) : E Ã— F`, where `n â‰¥ 1`. Let `f'` be the derivative of `f` at `(a, b)`. If the range
of `f'` is all of `F`, and the kernel of `f'` is the subspace `E Ã— {0}` in `E Ã— F`, then there
exists a function `Ï† : E â†’ F` such that `Ï† a = b`, and `f x (Ï† x) = f a b` holds for all `x` in a
neighbourhood of `a`. Furthoremore, `Ï†` is $C^n$ at `a`.

## TODO
* Local uniqueness of the implicit function
* Derivative of the implicit function

## Tags

implicit function, inverse function
-/

section
-- move somewhere
variable {Î± Î² Î³ Î´ : Type*} {Î¹ : Sort*}
variable {s : Set Î±} {t : Set Î²} {f : Filter Î±} {g : Filter Î²}

theorem Filter.eventually_prod_iff_exists_mem {p : Î± Ã— Î² â†’ Prop} :
    (âˆ€á¶  x in f Ã—Ë¢ g, p x) â†” âˆƒ s âˆˆ f, âˆƒ t âˆˆ g, âˆ€ x âˆˆ s, âˆ€ y âˆˆ t, p âŸ¨x, yâŸ© := by
  rw [Filter.eventually_iff_exists_mem]
  refine âŸ¨fun âŸ¨st, hst, hâŸ© â†¦ ?_, fun âŸ¨s, hs, t, ht, hâŸ© â†¦ ?_âŸ©
  Â· have âŸ¨s, hs, t, ht, hpâŸ© := Filter.mem_prod_iff.mp hst
    exact âŸ¨s, hs, t, ht, fun x hx y hy â†¦ h _ <| hp âŸ¨hx, hyâŸ©âŸ©
  Â· exact âŸ¨s Ã—Ë¢ t, Filter.prod_mem_prod hs ht, fun âŸ¨x, yâŸ© âŸ¨hx, hyâŸ© â†¦ h x hx y hyâŸ©

end

namespace ImplicitFunctionData
-- goes in the general theory

variable {ğ•œ : Type*} [RCLike ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [CompleteSpace E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [CompleteSpace F] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [CompleteSpace G]
  (Ï† : ImplicitFunctionData ğ•œ E F G) {n : WithTop â„•âˆ}

theorem contDiff_implicitFunction (hl : ContDiffAt ğ•œ n Ï†.leftFun Ï†.pt)
    (hr : ContDiffAt ğ•œ n Ï†.rightFun Ï†.pt) (hn : 1 â‰¤ n) :
    ContDiffAt ğ•œ n Ï†.implicitFunction.uncurry (Ï†.prodFun Ï†.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toPartialHomeomorph,
    â† HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (Ï†.hasStrictFDerivAt |>.hasFDerivAt) hn

end ImplicitFunctionData

open Filter

open LinearMap (ker range)

open scoped Topology

/-- A predicate stating the sufficient conditions on an implicit equation `f : E Ã— F â†’ F` that will
lead to a $C^n$ implicit function `Ï† : E â†’ F`. -/
structure IsContDiffImplicitAt {ğ•œ : Type*} [RCLike ğ•œ]
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
    (n : WithTop â„•âˆ) (f : E Ã— F â†’ F) (f' : E Ã— F â†’L[ğ•œ] F) (a : E Ã— F) : Prop where
  hasFDerivAt : HasFDerivAt f f' a
  contDiffAt : ContDiffAt ğ•œ n f a
  range_eq_top : range f' = âŠ¤
  ker_eq_left : ker f' = range (LinearMap.inl ğ•œ E F)
  one_le : 1 â‰¤ n

namespace IsContDiffImplicitAt

variable
  {ğ•œ : Type*} [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {n : WithTop â„•âˆ} {f : E Ã— F â†’ F} {f' : E Ã— F â†’L[ğ•œ] F} {a : E Ã— F}

def implicitFunctionData (h : IsContDiffImplicitAt n f f' a) :
    ImplicitFunctionData ğ•œ (E Ã— F) E F where
  leftFun := Prod.fst
  leftDeriv := ContinuousLinearMap.fst ğ•œ E F
  rightFun := f
  rightDeriv := f'
  pt := a
  left_has_deriv := by fun_prop
  right_has_deriv := h.contDiffAt.hasStrictFDerivAt' h.hasFDerivAt h.one_le
  left_range := LinearMap.range_eq_top_of_surjective _ fun x â†¦ âŸ¨(x, 0), rflâŸ©
  right_range := h.range_eq_top
  isCompl_ker := by
    have : LinearMap.ker (ContinuousLinearMap.fst ğ•œ E F) = LinearMap.ker (LinearMap.fst ğ•œ E F) :=
      rfl
    rw [isCompl_comm, this, LinearMap.ker_fst, h.ker_eq_left]
    exact LinearMap.isCompl_range_inl_inr

@[simp]
lemma implicitFunctionData_prodFun (h : IsContDiffImplicitAt n f f' a) :
    h.implicitFunctionData.prodFun h.implicitFunctionData.pt = (a.1, f a) := rfl

noncomputable def implicitFunctionAux (h : IsContDiffImplicitAt n f f' a) : E â†’ F â†’ E Ã— F :=
  h.implicitFunctionData.implicitFunction

lemma implicitFunctionAux_fst (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  p in ğ“ (a.1, f a), (h.implicitFunctionAux p.1 p.2).1 = p.1 :=
  h.implicitFunctionData.prod_map_implicitFunction.mono fun _ â†¦ congr_arg Prod.fst

-- lemma for `implicitFunctionAux_snd` but only at `(x, f a)`

lemma rightFun_implicitFunctionAux (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  p in ğ“ (a.1, f a), f (h.implicitFunctionAux p.1 p.2) = p.2 :=
  h.implicitFunctionData.prod_map_implicitFunction.mono fun _ â†¦ congr_arg Prod.snd

/-- Implicit function `y` defined by `f (x, y x) = f a`. -/
noncomputable def implicitFunction (h : IsContDiffImplicitAt n f f' a) : E â†’ F :=
  fun x â†¦ (h.implicitFunctionAux x (f a)).2

lemma implicitFunction_def (h : IsContDiffImplicitAt n f f' a) :
    h.implicitFunction = fun x â†¦ (h.implicitFunctionData.implicitFunction.uncurry (x, (f a))).2 :=
  rfl

/-- `implicitFunction` is indeed the (local) implicit function defined by `f`. -/
lemma apply_implicitFunction (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  x in ğ“ a.1, f (x, h.implicitFunction x) = f a := by
  have := h.rightFun_implicitFunctionAux
  have hfst := h.implicitFunctionAux_fst
  rw [nhds_prod_eq, eventually_swap_iff] at this hfst
  replace := this.curry.self_of_nhds
  replace hfst := hfst.curry.self_of_nhds
  apply this.mp
  apply hfst.mono
  intro x
  simp_rw [Prod.swap_prod_mk]
  intro h1 h2
  rw [â† h2]
  congr 1
  ext
  Â· rw [h1]
  Â· rfl

/-- If `f` is $C^n$ at `(x, y)`, then its implicit function around `x` is also $C^n$ at `x`. -/
theorem contDiff_implicitFunction (h : IsContDiffImplicitAt n f f' a) :
    ContDiffAt ğ•œ n h.implicitFunction a.1 := by
  have := h.implicitFunctionData.contDiff_implicitFunction contDiffAt_fst h.contDiffAt h.one_le
  rw [implicitFunction_def]
  fun_prop

end IsContDiffImplicitAt
