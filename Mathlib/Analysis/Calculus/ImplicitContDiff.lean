/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.Calculus.Implicit
public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

/-!
# Implicit function theorem

In this file, we apply the generalised implicit function theorem to the more familiar case and show
that the implicit function preserves the smoothness class of the implicit equation.

Let `E`, `F`, and `G` be real or complex Banach spaces. Let `f : E Ã— F â†’ G` be a function that is
$C^n$ at a point `(a, b) : E Ã— F`, where `n â‰¥ 1`. Let `f'` be the derivative of `f` at `(a, b)`. If
the map `y â†¦ f' (0, y)` is a Banach space isomorphism, then there exists a function `Ï† : E â†’ F` such
that `Ï† a = b`, and `f x (Ï† x) = f a b` holds for all `x` in a neighbourhood of `a`. Furthermore,
`Ï†` is $C^n$ at `a`.

## TODO
* Local uniqueness of the implicit function
* Derivative of the implicit function

## Tags

implicit function, inverse function
-/

variable
  {ğ•œ : Type*} [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [CompleteSpace G]

namespace ImplicitFunctionData

/-- The implicit function defined by a $C^n$ implicit equation is $C^n$. This applies to the general
form of the implicit function theorem. -/
theorem contDiff_implicitFunction {Ï† : ImplicitFunctionData ğ•œ E F G} {n : WithTop â„•âˆ}
    (hl : ContDiffAt ğ•œ n Ï†.leftFun Ï†.pt) (hr : ContDiffAt ğ•œ n Ï†.rightFun Ï†.pt) (hn : 1 â‰¤ n) :
    ContDiffAt ğ•œ n Ï†.implicitFunction.uncurry (Ï†.prodFun Ï†.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toOpenPartialHomeomorph,
    â† HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (Ï†.hasStrictFDerivAt |>.hasFDerivAt) hn

end ImplicitFunctionData

open Filter

open LinearMap (ker range)

open scoped Topology

/-- A predicate stating the sufficient conditions on an implicit equation `f : E Ã— F â†’ G` that will
lead to a $C^n$ implicit function `Ï† : E â†’ F`. -/
structure IsContDiffImplicitAt (n : WithTop â„•âˆ) (f : E Ã— F â†’ G) (f' : E Ã— F â†’L[ğ•œ] G) (a : E Ã— F) :
    Prop where
  hasFDerivAt : HasFDerivAt f f' a
  contDiffAt : ContDiffAt ğ•œ n f a
  bijective : Function.Bijective (f'.comp (ContinuousLinearMap.inr ğ•œ E F))
  one_le : 1 â‰¤ n

namespace IsContDiffImplicitAt

variable
  {n : WithTop â„•âˆ} {f : E Ã— F â†’ G} {f' : E Ã— F â†’L[ğ•œ] G} {a : E Ã— F}

/-- We record the parameters of our specific case in order to apply the general implicit function
theorem. -/
def implicitFunctionData (h : IsContDiffImplicitAt n f f' a) :
    ImplicitFunctionData ğ•œ (E Ã— F) E G where
  leftFun := Prod.fst
  leftDeriv := ContinuousLinearMap.fst ğ•œ E F
  rightFun := f
  rightDeriv := f'
  pt := a
  hasStrictFDerivAt_leftFun := by fun_prop
  hasStrictFDerivAt_rightFun := h.contDiffAt.hasStrictFDerivAt' h.hasFDerivAt h.one_le
  range_leftDeriv := LinearMap.range_eq_top_of_surjective _ fun x â†¦ âŸ¨(x, 0), rflâŸ©
  range_rightDeriv := by
    apply top_unique
    rw [â† LinearMap.range_eq_top_of_surjective _ h.bijective.surjective]
    exact LinearMap.range_comp_le_range _ _
  isCompl_ker := by
    apply IsCompl.of_eq
    Â· ext x
      rw [Submodule.mem_inf, Submodule.mem_bot, LinearMap.mem_ker, ContinuousLinearMap.coe_fst',
        LinearMap.mem_ker]
      constructor
      Â· intro âŸ¨h1, h2âŸ©
        rw [â† Prod.mk.eta (p := x), h1] at h2
        rw [Prod.ext_iff]
        refine âŸ¨h1, h.bijective.injective ?_âŸ©
        simp [h2, map_zero]
      Â· rintro rfl
        exact âŸ¨rfl, map_zero _âŸ©
    Â· ext x
      simp only [Submodule.mem_sup, Submodule.mem_top, iff_true]
      obtain âŸ¨y, hyâŸ© := h.bijective.surjective (f' x)
      exact âŸ¨(0, y), by simp, x - (0, y), by simp [map_sub, â† hy], by abelâŸ©

@[simp]
lemma implicitFunctionData_leftFun_pt (h : IsContDiffImplicitAt n f f' a) :
    h.implicitFunctionData.leftFun h.implicitFunctionData.pt = a.1 := rfl

@[simp]
lemma implicitFunctionData_rightFun_pt (h : IsContDiffImplicitAt n f f' a) :
    h.implicitFunctionData.rightFun h.implicitFunctionData.pt = f a := rfl

/-- The implicit function provided by the general theorem, from which we construct the more useful
form `IsContDiffImplicitAt.implicitFunction`. -/
noncomputable def implicitFunctionAux (h : IsContDiffImplicitAt n f f' a) : E â†’ G â†’ E Ã— F :=
  h.implicitFunctionData.implicitFunction

lemma implicitFunctionAux_fst (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  p in ğ“ (a.1, f a), (h.implicitFunctionAux p.1 p.2).1 = p.1 :=
  h.implicitFunctionData.prod_map_implicitFunction.mono fun _ â†¦ congr_arg Prod.fst

lemma comp_implicitFunctionAux_eq_snd (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  p in ğ“ (a.1, f a), f (h.implicitFunctionAux p.1 p.2) = p.2 :=
  h.implicitFunctionData.prod_map_implicitFunction.mono fun _ â†¦ congr_arg Prod.snd

/-- Implicit function `Ï†` defined by `f (x, Ï† x) = f a`. -/
noncomputable def implicitFunction (h : IsContDiffImplicitAt n f f' a) : E â†’ F :=
  fun x â†¦ (h.implicitFunctionAux x (f a)).2

lemma implicitFunction_def (h : IsContDiffImplicitAt n f f' a) :
    h.implicitFunction = fun x â†¦ (h.implicitFunctionData.implicitFunction.uncurry (x, f a)).2 :=
  rfl

/-- `implicitFunction` is indeed the (local) implicit function defined by `f`. -/
lemma apply_implicitFunction (h : IsContDiffImplicitAt n f f' a) :
    âˆ€á¶  x in ğ“ a.1, f (x, h.implicitFunction x) = f a := by
  have := h.comp_implicitFunctionAux_eq_snd
  have hfst := h.implicitFunctionAux_fst
  rw [nhds_prod_eq, eventually_swap_iff] at this hfst
  apply this.curry.self_of_nhds.mp
  apply hfst.curry.self_of_nhds.mono
  simp_rw [Prod.swap_prod_mk]
  intro x h1 h2
  rw [â† h2]
  congr 1
  ext
  Â· rw [h1]
  Â· rfl

/-- If the implicit equation `f` is $C^n$ at `(x, y)`, then its implicit function `Ï†` around `x` is
also $C^n$ at `x`. -/
theorem contDiffAt_implicitFunction (h : IsContDiffImplicitAt n f f' a) :
    ContDiffAt ğ•œ n h.implicitFunction a.1 := by
  have := h.implicitFunctionData.contDiff_implicitFunction contDiffAt_fst h.contDiffAt h.one_le
  rw [implicitFunction_def]
  fun_prop

end IsContDiffImplicitAt
