/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.Calculus.Implicit
public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ContDiff

/-!
# Implicit function theorem

In this file, we apply the generalised implicit function theorem to the more familiar case and show
that the implicit function preserves the smoothness class of the implicit equation.

Let `Eâ‚`, `Eâ‚‚`, and `F` be real or complex Banach spaces. Let `f : Eâ‚ Ã— Eâ‚‚ â†’ F` be a function that
is $C^n$ at a point `(uâ‚, uâ‚‚) : Eâ‚ Ã— Eâ‚‚`, where `n â‰¥ 1`. Let `f'` be the derivative of `f` at
`(uâ‚, uâ‚‚)`. If the map `y â†¦ f' (0, y)` is a Banach space isomorphism, then there exists a function
`Ïˆ : Eâ‚ â†’ Eâ‚‚` such that `Ïˆ uâ‚ = uâ‚‚`, and `f (x, Ïˆ x) = f (uâ‚, uâ‚‚)` holds for all `x` in a
neighbourhood of `uâ‚`. Furthermore, `Ïˆ` is $C^n$ at `uâ‚`.

## TODO
* Local uniqueness of the implicit function
* Derivative of the implicit function

## Tags

implicit function, inverse function
-/

@[expose] public section

variable {ð•œ Eâ‚ Eâ‚‚ F : Type*} [RCLike ð•œ]
  [NormedAddCommGroup Eâ‚] [NormedSpace ð•œ Eâ‚] [CompleteSpace Eâ‚]
  [NormedAddCommGroup Eâ‚‚] [NormedSpace ð•œ Eâ‚‚] [CompleteSpace Eâ‚‚]
  [NormedAddCommGroup F] [NormedSpace ð•œ F] [CompleteSpace F]

namespace ImplicitFunctionData

/-- The implicit function defined by a $C^n$ implicit equation is $C^n$. This applies to the general
form of the implicit function theorem. -/
theorem contDiff_implicitFunction {Ï† : ImplicitFunctionData ð•œ Eâ‚ Eâ‚‚ F} {n : WithTop â„•âˆž}
    (hl : ContDiffAt ð•œ n Ï†.leftFun Ï†.pt) (hr : ContDiffAt ð•œ n Ï†.rightFun Ï†.pt) (hn : n â‰  0) :
    ContDiffAt ð•œ n Ï†.implicitFunction.uncurry (Ï†.prodFun Ï†.pt) := by
  rw [implicitFunction, Function.uncurry_curry, toOpenPartialHomeomorph,
    â† HasStrictFDerivAt.localInverse_def]
  exact (hl.prodMk hr).to_localInverse (Ï†.hasStrictFDerivAt |>.hasFDerivAt) hn

end ImplicitFunctionData

open scoped Topology

namespace ContDiffAt

/-- Implicit function `Ïˆ` defined by `f (x, Ïˆ x) = f u`. -/
noncomputable def implicitFunction {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {u : Eâ‚ Ã— Eâ‚‚} {n : WithTop â„•âˆž}
    (cdf : ContDiffAt ð•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ð•œ f u âˆ˜L .inr ð•œ Eâ‚ Eâ‚‚).IsInvertible) :
    Eâ‚ â†’ Eâ‚‚ :=
  (cdf.hasStrictFDerivAt pn).implicitFunctionOfProdDomain ifâ‚‚

/-- `implicitFunction` is indeed the (local) implicit function defined by `f`. -/
theorem image_implicitFunction {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {u : Eâ‚ Ã— Eâ‚‚} {n : WithTop â„•âˆž}
    (cdf : ContDiffAt ð•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ð•œ f u âˆ˜L .inr ð•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  x in ð“ u.1, f (x, cdf.implicitFunction pn ifâ‚‚ x) = f u :=
  (cdf.hasStrictFDerivAt pn).image_implicitFunctionOfProdDomain ifâ‚‚

theorem eventually_implicitFunction_apply_eq {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {u : Eâ‚ Ã— Eâ‚‚} {n : WithTop â„•âˆž}
    (cdf : ContDiffAt ð•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ð•œ f u âˆ˜L .inr ð•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  v in ð“ u, f v = f u â†” cdf.implicitFunction pn ifâ‚‚ v.1 = v.2 :=
  (cdf.hasStrictFDerivAt pn).image_eq_iff_implicitFunctionOfProdDomain ifâ‚‚

/-- If the implicit equation `f` is $C^n$ at `(uâ‚, uâ‚‚)`, then its implicit function `Ïˆ` around `uâ‚`
is also $C^n$ at `uâ‚`. -/
theorem contDiffAt_implicitFunction {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {u : Eâ‚ Ã— Eâ‚‚} {n : WithTop â„•âˆž}
    (cdf : ContDiffAt ð•œ n f u) (pn : n â‰  0) (ifâ‚‚ : (fderiv ð•œ f u âˆ˜L .inr ð•œ Eâ‚ Eâ‚‚).IsInvertible) :
    ContDiffAt ð•œ n (cdf.implicitFunction pn ifâ‚‚) u.1 := by
  have := (cdf.hasStrictFDerivAt pn).implicitFunctionDataOfProdDomain ifâ‚‚
            |>.contDiff_implicitFunction cdf contDiffAt_fst pn
  unfold implicitFunction HasStrictFDerivAt.implicitFunctionOfProdDomain
  fun_prop

end ContDiffAt
