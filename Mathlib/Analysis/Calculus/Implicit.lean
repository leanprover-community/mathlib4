/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.FDeriv
import Mathlib.Analysis.Calculus.FDeriv.Partial
import Mathlib.Analysis.Normed.Module.Complemented

/-!
# Implicit function theorem

We prove four versions of the implicit function theorem. First we define a structure
`ImplicitFunctionData` that holds arguments for the most general version of the implicit function
theorem, see `ImplicitFunctionData.implicitFunction` and
`ImplicitFunctionData.implicitFunction_hasStrictFDerivAt`. This version allows a user to choose a
specific implicit function but provides only a little convenience over the inverse function theorem.

Then we define `HasStrictFDerivAt.implicitFunctionDataOfComplemented`: implicit function defined by
`f (g z y) = z`, where `f : E â†’ F` is a function strictly differentiable at `a` such that its
derivative `f'` is surjective and has a `complemented` kernel.

Third, if the codomain of `f` is a finite-dimensional space, then we can automatically prove
that the kernel of `f'` is complemented, hence the only assumptions are `HasStrictFDerivAt`
and `f'.range = âŠ¤`. This version is named `HasStrictFDerivAt.implicitFunction`.

Lastly we consider the common case of a bivariate `f`, the second of whose partial derivatives is
invertible. Then we may apply the general theorem to obtain `Ïˆ` such that for `(yâ‚, yâ‚‚)` in a
neighbourhood of `(xâ‚, xâ‚‚)` we have `f yâ‚ yâ‚‚ = f xâ‚ xâ‚‚ â†” Ïˆ yâ‚ = yâ‚‚`.

## TODO

* Add a version for `f : ğ•œ Ã— ğ•œ â†’ ğ•œ` proving `HasStrictDerivAt` and `deriv Ï† = ...`.
* Prove that in a real vector space the implicit function has the same smoothness as the original
  one.
* If the original function is differentiable in a neighborhood, then the implicit function is
  differentiable in a neighborhood as well. Current setup only proves differentiability at one
  point for the implicit function constructed in this file (as opposed to an unspecified implicit
  function). One of the ways to overcome this difficulty is to use uniqueness of the implicit
  function in the general version of the theorem. Another way is to prove that *any* implicit
  function satisfying some predicate is strictly differentiable.

## Tags

implicit function, inverse function
-/


noncomputable section

open scoped Topology

open Filter

open ContinuousLinearMap (fst snd smulRight ker_prod)

open ContinuousLinearEquiv (ofBijective)

open LinearMap (ker range)

/-!
### General version

Consider two functions `f : E â†’ F` and `g : E â†’ G` and a point `a` such that

* both functions are strictly differentiable at `a`;
* the derivatives are surjective;
* the kernels of the derivatives are complementary subspaces of `E`.

Note that the map `x â†¦ (f x, g x)` has a bijective derivative, hence it is a partial homeomorphism
between `E` and `F Ã— G`. We use this fact to define a function `Ï† : F â†’ G â†’ E`
(see `ImplicitFunctionData.implicitFunction`) such that for `(y, z)` close enough to `(f a, g a)`
we have `f (Ï† y z) = y` and `g (Ï† y z) = z`.

We also prove a formula for $$\frac{\partial\varphi}{\partial z}.$$

Though this statement is almost symmetric with respect to `F`, `G`, we interpret it in the following
way. Consider a family of surfaces `{x | f x = y}`, `y âˆˆ ğ“ (f a)`. Each of these surfaces is
parametrized by `Ï† y`.

There are many ways to choose a (differentiable) function `Ï†` such that `f (Ï† y z) = y` but the
extra condition `g (Ï† y z) = z` allows a user to select one of these functions. If we imagine
that the level surfaces `f = const` form a local horizontal foliation, then the choice of
`g` fixes a transverse foliation `g = const`, and `Ï†` is the inverse function of the projection
of `{x | f x = y}` along this transverse foliation.

This version of the theorem is used to prove the other versions and can be used if a user
needs to have a complete control over the choice of the implicit function.
-/


/-- Data for the general version of the implicit function theorem. It holds two functions
`f : E â†’ F` and `g : E â†’ G` (named `leftFun` and `rightFun`) and a point `a` (named `pt`) such that

* both functions are strictly differentiable at `a`;
* the derivatives are surjective;
* the kernels of the derivatives are complementary subspaces of `E`. -/
structure ImplicitFunctionData (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] (E : Type*)
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E] (F : Type*) [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] (G : Type*) [NormedAddCommGroup G] [NormedSpace ğ•œ G]
    [CompleteSpace G] where
  /-- Left function -/
  leftFun : E â†’ F
  /-- Derivative of the left function -/
  leftDeriv : E â†’L[ğ•œ] F
  /-- Right function -/
  rightFun : E â†’ G
  /-- Derivative of the right function -/
  rightDeriv : E â†’L[ğ•œ] G
  /-- The point at which `leftFun` and `rightFun` are strictly differentiable -/
  pt : E
  left_has_deriv : HasStrictFDerivAt leftFun leftDeriv pt
  right_has_deriv : HasStrictFDerivAt rightFun rightDeriv pt
  left_range : range leftDeriv = âŠ¤
  right_range : range rightDeriv = âŠ¤
  isCompl_ker : IsCompl (ker leftDeriv) (ker rightDeriv)

namespace ImplicitFunctionData

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [CompleteSpace E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [CompleteSpace F] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G] [CompleteSpace G]
  (Ï† : ImplicitFunctionData ğ•œ E F G)

/-- The function given by `x â†¦ (leftFun x, rightFun x)`. -/
def prodFun (x : E) : F Ã— G :=
  (Ï†.leftFun x, Ï†.rightFun x)

@[simp]
theorem prodFun_apply (x : E) : Ï†.prodFun x = (Ï†.leftFun x, Ï†.rightFun x) :=
  rfl

protected theorem hasStrictFDerivAt :
    HasStrictFDerivAt Ï†.prodFun
      (Ï†.leftDeriv.equivProdOfSurjectiveOfIsCompl Ï†.rightDeriv Ï†.left_range Ï†.right_range
          Ï†.isCompl_ker :
        E â†’L[ğ•œ] F Ã— G)
      Ï†.pt :=
  Ï†.left_has_deriv.prodMk Ï†.right_has_deriv

/-- Implicit function theorem. If `f : E â†’ F` and `g : E â†’ G` are two maps strictly differentiable
at `a`, their derivatives `f'`, `g'` are surjective, and the kernels of these derivatives are
complementary subspaces of `E`, then `x â†¦ (f x, g x)` defines a partial homeomorphism between
`E` and `F Ã— G`. In particular, `{x | f x = f a}` is locally homeomorphic to `G`. -/
def toPartialHomeomorph : PartialHomeomorph E (F Ã— G) :=
  Ï†.hasStrictFDerivAt.toPartialHomeomorph _

/-- Implicit function theorem. If `f : E â†’ F` and `g : E â†’ G` are two maps strictly differentiable
at `a`, their derivatives `f'`, `g'` are surjective, and the kernels of these derivatives are
complementary subspaces of `E`, then `implicitFunction` is the unique (germ of a) map
`Ï† : F â†’ G â†’ E` such that `f (Ï† y z) = y` and `g (Ï† y z) = z`. -/
def implicitFunction : F â†’ G â†’ E :=
  Function.curry <| Ï†.toPartialHomeomorph.symm

@[simp]
theorem toPartialHomeomorph_coe : â‡‘Ï†.toPartialHomeomorph = Ï†.prodFun :=
  rfl

theorem toPartialHomeomorph_apply (x : E) : Ï†.toPartialHomeomorph x = (Ï†.leftFun x, Ï†.rightFun x) :=
  rfl

theorem pt_mem_toPartialHomeomorph_source : Ï†.pt âˆˆ Ï†.toPartialHomeomorph.source :=
  Ï†.hasStrictFDerivAt.mem_toPartialHomeomorph_source

theorem map_pt_mem_toPartialHomeomorph_target :
    (Ï†.leftFun Ï†.pt, Ï†.rightFun Ï†.pt) âˆˆ Ï†.toPartialHomeomorph.target :=
  Ï†.toPartialHomeomorph.map_source <| Ï†.pt_mem_toPartialHomeomorph_source

theorem prod_map_implicitFunction :
    âˆ€á¶  p : F Ã— G in ğ“ (Ï†.prodFun Ï†.pt), Ï†.prodFun (Ï†.implicitFunction p.1 p.2) = p :=
  Ï†.hasStrictFDerivAt.eventually_right_inverse.mono fun âŸ¨_, _âŸ© h => h

theorem left_map_implicitFunction :
    âˆ€á¶  p : F Ã— G in ğ“ (Ï†.prodFun Ï†.pt), Ï†.leftFun (Ï†.implicitFunction p.1 p.2) = p.1 :=
  Ï†.prod_map_implicitFunction.mono fun _ => congr_arg Prod.fst

theorem right_map_implicitFunction :
    âˆ€á¶  p : F Ã— G in ğ“ (Ï†.prodFun Ï†.pt), Ï†.rightFun (Ï†.implicitFunction p.1 p.2) = p.2 :=
  Ï†.prod_map_implicitFunction.mono fun _ => congr_arg Prod.snd

theorem implicitFunction_apply_image :
    âˆ€á¶  x in ğ“ Ï†.pt, Ï†.implicitFunction (Ï†.leftFun x) (Ï†.rightFun x) = x :=
  Ï†.hasStrictFDerivAt.eventually_left_inverse

theorem leftFun_implicitFunction : âˆ€á¶  x in ğ“ Ï†.pt,
    Ï†.leftFun (Ï†.implicitFunction (Ï†.leftFun Ï†.pt) (Ï†.rightFun x)) = Ï†.leftFun Ï†.pt := by
  have := Ï†.left_map_implicitFunction.curry_nhds.self_of_nhds.prod_inr_nhds (Ï†.leftFun Ï†.pt)
  rwa [â† prodFun_apply, â† Ï†.hasStrictFDerivAt.map_nhds_eq_of_equiv, eventually_map] at this

theorem rightFun_implicitFunction : âˆ€á¶  x in ğ“ Ï†.pt,
    Ï†.rightFun (Ï†.implicitFunction (Ï†.leftFun Ï†.pt) (Ï†.rightFun x)) = Ï†.rightFun x := by
  have := Ï†.right_map_implicitFunction.curry_nhds.self_of_nhds.prod_inr_nhds (Ï†.leftFun Ï†.pt)
  rwa [â† prodFun_apply, â† Ï†.hasStrictFDerivAt.map_nhds_eq_of_equiv, eventually_map] at this

theorem leftFun_eq_iff_implicitFunction : âˆ€á¶  x in ğ“ Ï†.pt,
    Ï†.leftFun x = Ï†.leftFun Ï†.pt â†” Ï†.implicitFunction (Ï†.leftFun Ï†.pt) (Ï†.rightFun x) = x := by
  filter_upwards [Ï†.implicitFunction_apply_image, Ï†.leftFun_implicitFunction] with x hxâ‚ hxâ‚‚
  constructor <;> exact fun h => by rwa [â† h]

theorem map_nhds_eq : map Ï†.leftFun (ğ“ Ï†.pt) = ğ“ (Ï†.leftFun Ï†.pt) :=
  show map (Prod.fst âˆ˜ Ï†.prodFun) (ğ“ Ï†.pt) = ğ“ (Ï†.prodFun Ï†.pt).1 by
    rw [â† map_map, Ï†.hasStrictFDerivAt.map_nhds_eq_of_equiv, map_fst_nhds]

theorem implicitFunction_hasStrictFDerivAt (g'inv : G â†’L[ğ•œ] E)
    (hg'inv : Ï†.rightDeriv.comp g'inv = ContinuousLinearMap.id ğ•œ G)
    (hg'invf : Ï†.leftDeriv.comp g'inv = 0) :
    HasStrictFDerivAt (Ï†.implicitFunction (Ï†.leftFun Ï†.pt)) g'inv (Ï†.rightFun Ï†.pt) := by
  have := Ï†.hasStrictFDerivAt.to_localInverse
  simp only [prodFun] at this
  convert this.comp (Ï†.rightFun Ï†.pt)
    ((hasStrictFDerivAt_const _ _).prodMk (hasStrictFDerivAt_id _))
  simp only [ContinuousLinearMap.ext_iff, ContinuousLinearMap.comp_apply] at hg'inv hg'invf âŠ¢
  simp [ContinuousLinearEquiv.eq_symm_apply, *]

end ImplicitFunctionData

namespace HasStrictFDerivAt

section Complemented

/-!
### Case of a complemented kernel

In this section we prove the following version of the implicit function theorem. Consider a map
`f : E â†’ F` and a point `a : E` such that `f` is strictly differentiable at `a`, its derivative `f'`
is surjective and the kernel of `f'` is a complemented subspace of `E` (i.e., it has a closed
complementary subspace). Then there exists a function `Ï† : F â†’ ker f' â†’ E` such that for `(y, z)`
close to `(f a, 0)` we have `f (Ï† y z) = y` and the derivative of `Ï† (f a)` at zero is the
embedding `ker f' â†’ E`.

Note that a map with these properties is not unique. E.g., different choices of a subspace
complementary to `ker f'` lead to different maps `Ï†`.
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [CompleteSpace E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [CompleteSpace F] {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {a : E}

section Defs

variable (f f')

/-- Data used to apply the generic implicit function theorem to the case of a strictly
differentiable map such that its derivative is surjective and has a complemented kernel. -/
@[simp]
def implicitFunctionDataOfComplemented (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) : ImplicitFunctionData ğ•œ E F (ker f') where
  leftFun := f
  leftDeriv := f'
  rightFun x := Classical.choose hker (x - a)
  rightDeriv := Classical.choose hker
  pt := a
  left_has_deriv := hf
  right_has_deriv :=
    (Classical.choose hker).hasStrictFDerivAt.comp a ((hasStrictFDerivAt_id a).sub_const a)
  left_range := hf'
  right_range := LinearMap.range_eq_of_proj (Classical.choose_spec hker)
  isCompl_ker := LinearMap.isCompl_of_proj (Classical.choose_spec hker)

/-- A partial homeomorphism between `E` and `F Ã— f'.ker` sending level surfaces of `f`
to vertical subspaces. -/
def implicitToPartialHomeomorphOfComplemented (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) : PartialHomeomorph E (F Ã— ker f') :=
  (implicitFunctionDataOfComplemented f f' hf hf' hker).toPartialHomeomorph

/-- Implicit function `g` defined by `f (g z y) = z`. -/
def implicitFunctionOfComplemented (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) : F â†’ ker f' â†’ E :=
  (implicitFunctionDataOfComplemented f f' hf hf' hker).implicitFunction

end Defs

@[simp]
theorem implicitToPartialHomeomorphOfComplemented_fst (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) (x : E) :
    (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker x).fst = f x :=
  rfl

theorem implicitToPartialHomeomorphOfComplemented_apply (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) (y : E) :
    hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker y =
      (f y, Classical.choose hker (y - a)) :=
  rfl

@[simp]
theorem implicitToPartialHomeomorphOfComplemented_apply_ker (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) (y : ker f') :
    hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker (y + a) = (f (y + a), y) := by
  simp only [implicitToPartialHomeomorphOfComplemented_apply, add_sub_cancel_right,
    Classical.choose_spec hker]

@[simp]
theorem implicitToPartialHomeomorphOfComplemented_self (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) :
    hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker a = (f a, 0) := by
  simp [hf.implicitToPartialHomeomorphOfComplemented_apply]

theorem mem_implicitToPartialHomeomorphOfComplemented_source (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) :
    a âˆˆ (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker).source :=
  ImplicitFunctionData.pt_mem_toPartialHomeomorph_source _

theorem mem_implicitToPartialHomeomorphOfComplemented_target (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) :
    (f a, (0 : ker f')) âˆˆ (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker).target := by
  simpa only [implicitToPartialHomeomorphOfComplemented_self] using
    (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker).map_source <|
      hf.mem_implicitToPartialHomeomorphOfComplemented_source hf' hker

/-- `HasStrictFDerivAt.implicitFunctionOfComplemented` sends `(z, y)` to a point in `f â»Â¹' z`. -/
theorem map_implicitFunctionOfComplemented_eq (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) :
    âˆ€á¶  p : F Ã— ker f' in ğ“ (f a, 0),
      f (hf.implicitFunctionOfComplemented f f' hf' hker p.1 p.2) = p.1 :=
  ((hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker).eventually_right_inverse <|
        hf.mem_implicitToPartialHomeomorphOfComplemented_target hf' hker).mono
    fun âŸ¨_, _âŸ© h => congr_arg Prod.fst h

/-- Any point in some neighborhood of `a` can be represented as
`HasStrictFDerivAt.implicitFunctionOfComplemented` of some point. -/
theorem eq_implicitFunctionOfComplemented (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) :
    âˆ€á¶  x in ğ“ a, hf.implicitFunctionOfComplemented f f' hf' hker (f x)
      (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker x).snd = x :=
  (implicitFunctionDataOfComplemented f f' hf hf' hker).implicitFunction_apply_image

@[simp]
theorem implicitFunctionOfComplemented_apply_image (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) (hker : (ker f').ClosedComplemented) :
    hf.implicitFunctionOfComplemented f f' hf' hker (f a) 0 = a := by
  simpa only [implicitToPartialHomeomorphOfComplemented_self] using
      (hf.implicitToPartialHomeomorphOfComplemented f f' hf' hker).left_inv
      (hf.mem_implicitToPartialHomeomorphOfComplemented_source hf' hker)

theorem to_implicitFunctionOfComplemented (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (hker : (ker f').ClosedComplemented) :
    HasStrictFDerivAt (hf.implicitFunctionOfComplemented f f' hf' hker (f a))
      (ker f').subtypeL 0 := by
  convert (implicitFunctionDataOfComplemented f f' hf hf' hker).implicitFunction_hasStrictFDerivAt
    (ker f').subtypeL _ _
  swap
  Â· ext
    simp only [Classical.choose_spec hker, implicitFunctionDataOfComplemented,
      ContinuousLinearMap.comp_apply, Submodule.coe_subtypeL', Submodule.coe_subtype,
      ContinuousLinearMap.id_apply]
  swap
  Â· ext
    simp only [ContinuousLinearMap.comp_apply, Submodule.coe_subtypeL', Submodule.coe_subtype,
      LinearMap.map_coe_ker, ContinuousLinearMap.zero_apply]
  simp only [implicitFunctionDataOfComplemented, map_sub, sub_self]

end Complemented

/-!
### Finite-dimensional case

In this section we prove the following version of the implicit function theorem. Consider a map
`f : E â†’ F` from a Banach normed space to a finite-dimensional space.
Take a point `a : E` such that `f` is strictly differentiable at `a` and its derivative `f'`
is surjective. Then there exists a function `Ï† : F â†’ ker f' â†’ E` such that for `(y, z)`
close to `(f a, 0)` we have `f (Ï† y z) = y` and the derivative of `Ï† (f a)` at zero is the
embedding `ker f' â†’ E`.

This version deduces that `ker f'` is a complemented subspace from the fact that `F` is a finite
dimensional space, then applies the previous version.

Note that a map with these properties is not unique. E.g., different choices of a subspace
complementary to `ker f'` lead to different maps `Ï†`.
-/

section FiniteDimensional

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ] {E : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [FiniteDimensional ğ•œ F] (f : E â†’ F) (f' : E â†’L[ğ•œ] F) {a : E}

/-- Given a map `f : E â†’ F` to a finite-dimensional space with a surjective derivative `f'`,
returns a partial homeomorphism between `E` and `F Ã— ker f'`. -/
def implicitToPartialHomeomorph (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    PartialHomeomorph E (F Ã— ker f') :=
  haveI := FiniteDimensional.complete ğ•œ F
  hf.implicitToPartialHomeomorphOfComplemented f f' hf'
    f'.ker_closedComplemented_of_finiteDimensional_range

/-- Implicit function `g` defined by `f (g z y) = z`. -/
def implicitFunction (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) : F â†’ ker f' â†’ E :=
  Function.curry <| (hf.implicitToPartialHomeomorph f f' hf').symm

variable {f f'}

@[simp]
theorem implicitToPartialHomeomorph_fst (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (x : E) : (hf.implicitToPartialHomeomorph f f' hf' x).fst = f x :=
  rfl

@[simp]
theorem implicitToPartialHomeomorph_apply_ker (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤)
    (y : ker f') : hf.implicitToPartialHomeomorph f f' hf' (y + a) = (f (y + a), y) :=
  haveI := FiniteDimensional.complete ğ•œ F
  implicitToPartialHomeomorphOfComplemented_apply_ker ..

@[simp]
theorem implicitToPartialHomeomorph_self (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    hf.implicitToPartialHomeomorph f f' hf' a = (f a, 0) :=
  haveI := FiniteDimensional.complete ğ•œ F
  implicitToPartialHomeomorphOfComplemented_self ..

theorem mem_implicitToPartialHomeomorph_source (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) : a âˆˆ (hf.implicitToPartialHomeomorph f f' hf').source :=
  haveI := FiniteDimensional.complete ğ•œ F
  ImplicitFunctionData.pt_mem_toPartialHomeomorph_source _

theorem mem_implicitToPartialHomeomorph_target (hf : HasStrictFDerivAt f f' a)
    (hf' : range f' = âŠ¤) : (f a, (0 : ker f')) âˆˆ (hf.implicitToPartialHomeomorph f f' hf').target :=
  haveI := FiniteDimensional.complete ğ•œ F
  mem_implicitToPartialHomeomorphOfComplemented_target ..

theorem tendsto_implicitFunction (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) {Î± : Type*}
    {l : Filter Î±} {gâ‚ : Î± â†’ F} {gâ‚‚ : Î± â†’ ker f'} (hâ‚ : Tendsto gâ‚ l (ğ“ <| f a))
    (hâ‚‚ : Tendsto gâ‚‚ l (ğ“ 0)) :
    Tendsto (fun t => hf.implicitFunction f f' hf' (gâ‚ t) (gâ‚‚ t)) l (ğ“ a) := by
  refine ((hf.implicitToPartialHomeomorph f f' hf').tendsto_symm
    (hf.mem_implicitToPartialHomeomorph_source hf')).comp ?_
  rw [implicitToPartialHomeomorph_self]
  exact hâ‚.prodMk_nhds hâ‚‚

alias _root_.Filter.Tendsto.implicitFunction := tendsto_implicitFunction

/-- `HasStrictFDerivAt.implicitFunction` sends `(z, y)` to a point in `f â»Â¹' z`. -/
theorem map_implicitFunction_eq (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    âˆ€á¶  p : F Ã— ker f' in ğ“ (f a, 0), f (hf.implicitFunction f f' hf' p.1 p.2) = p.1 :=
  haveI := FiniteDimensional.complete ğ•œ F
  map_implicitFunctionOfComplemented_eq ..

@[simp]
theorem implicitFunction_apply_image (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    hf.implicitFunction f f' hf' (f a) 0 = a := by
  haveI := FiniteDimensional.complete ğ•œ F
  apply implicitFunctionOfComplemented_apply_image

/-- Any point in some neighborhood of `a` can be represented as `HasStrictFDerivAt.implicitFunction`
of some point. -/
theorem eq_implicitFunction (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    âˆ€á¶  x in ğ“ a,
      hf.implicitFunction f f' hf' (f x) (hf.implicitToPartialHomeomorph f f' hf' x).snd = x :=
  haveI := FiniteDimensional.complete ğ•œ F
  eq_implicitFunctionOfComplemented ..

theorem to_implicitFunction (hf : HasStrictFDerivAt f f' a) (hf' : range f' = âŠ¤) :
    HasStrictFDerivAt (hf.implicitFunction f f' hf' (f a)) (ker f').subtypeL 0 :=
  haveI := FiniteDimensional.complete ğ•œ F
  to_implicitFunctionOfComplemented ..

end FiniteDimensional

section ProdDomain

/-!
### Bivariate case

Here we identify `E` with `Eâ‚ Ã— Eâ‚‚`, `G` with `Eâ‚` and `g : E â†’ G` with the first projection. Now
given `f : Eâ‚ â†’ Eâ‚‚ â†’ F` and its two partial derivatives, the second invertible, we may construct an
instance of the `ImplicitFunctionData` data structure and extract `Ïˆ : Eâ‚ â†’ Eâ‚‚` with the desired
properties. This functionality is wrapped by `HasStrictFDerivAt.implicitFunOfProdDomain` for
uncurried `f` in this section and `implicitFunOfBivariate` for curried `f` in the next. A formula
for the first derivative of `Ïˆ` is immediately derived.

## TODO

* Derive a formula for the second derivative of `Ïˆ`.
-/

variable {ğ•œ Eâ‚ Eâ‚‚ F : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup Eâ‚] [NormedSpace ğ•œ Eâ‚] [CompleteSpace Eâ‚] [NormedAddCommGroup Eâ‚‚]
  [NormedSpace ğ•œ Eâ‚‚] [CompleteSpace Eâ‚‚] [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]

/-- Given linear maps `fâ‚ : Eâ‚ â†’L[ğ•œ] F` and `fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F` (the second invertible) and that
`HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) x`, we prove that the kernels of `f : E â†’ F` and `g : E â†’ G` in
the original formulation are complementary and construct an object of type `ImplicitFunctionData`
thereby permitting use of the general machinery provided above. -/
def implicitFunDataOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {x : Eâ‚ Ã— Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) x) :
    ImplicitFunctionData ğ•œ (Eâ‚ Ã— Eâ‚‚) F Eâ‚ where
  leftFun := f
  rightFun := Prod.fst
  pt := x
  leftDeriv := fâ‚.coprod fâ‚‚
  left_has_deriv := dfx
  rightDeriv := ContinuousLinearMap.fst ğ•œ Eâ‚ Eâ‚‚
  right_has_deriv := hasStrictFDerivAt_fst
  left_range := by
    rw [ContinuousLinearMap.range_coprod]
    convert sup_top_eq _
    exact LinearEquivClass.range fâ‚‚
  right_range := Submodule.range_fst
  isCompl_ker := by
    constructor
    Â· rw [Submodule.disjoint_def]
      aesop
    Â· rw [Submodule.codisjoint_iff_exists_add_eq]
      intro (hâ‚, hâ‚‚)
      use (hâ‚, fâ‚‚.symm (fâ‚ (-hâ‚))), (0, hâ‚‚ - fâ‚‚.symm (fâ‚ (-hâ‚)))
      simp

/-- Implicit function `Ïˆ : Eâ‚ â†’ Eâ‚‚` associated with the (uncurried) bivariate function
`f : Eâ‚ Ã— Eâ‚‚ â†’ F` at `x`. -/
def implicitFunOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {x : Eâ‚ Ã— Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) x) :
    Eâ‚ â†’ Eâ‚‚ :=
  fun u => (dfx.implicitFunDataOfProdDomain.implicitFunction (f x) u).2

theorem hasStrictFDerivAt_implicitFunOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) (xâ‚, xâ‚‚)) :
    HasStrictFDerivAt dfx.implicitFunOfProdDomain (-fâ‚‚.symm âˆ˜L fâ‚) xâ‚ := by
  set Ïˆ' : Eâ‚ â†’L[ğ•œ] Eâ‚‚ := -fâ‚‚.symm âˆ˜L fâ‚
  apply HasStrictFDerivAt.snd (fâ‚‚' := (ContinuousLinearMap.id ğ•œ Eâ‚).prod Ïˆ')
  apply dfx.implicitFunDataOfProdDomain.implicitFunction_hasStrictFDerivAt
  Â· apply ContinuousLinearMap.fst_comp_prod
  Â· change fâ‚ + fâ‚‚ âˆ˜L Ïˆ' = 0
    simp [Ïˆ', â† ContinuousLinearMap.comp_assoc]

theorem image_eq_iff_implicitFunOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {x : Eâ‚ Ã— Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) x) :
    âˆ€á¶  y in ğ“ x, f y = f x â†” dfx.implicitFunOfProdDomain y.1 = y.2 := by
  let Ï† := dfx.implicitFunDataOfProdDomain
  filter_upwards [Ï†.leftFun_eq_iff_implicitFunction, Ï†.rightFun_implicitFunction] with y h h'
  exact Iff.trans h âŸ¨congrArg _, by aesopâŸ©

theorem tendsto_implicitFunOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) (xâ‚, xâ‚‚)) :
    Tendsto dfx.implicitFunOfProdDomain (ğ“ xâ‚) (ğ“ xâ‚‚) := by
  have := dfx.hasStrictFDerivAt_implicitFunOfProdDomain.continuousAt.tendsto
  rwa [dfx.image_eq_iff_implicitFunOfProdDomain.self_of_nhds.mp rfl] at this

theorem image_implicitFunOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
    {fâ‚ : Eâ‚ â†’L[ğ•œ] F} {fâ‚‚ : Eâ‚‚ â‰ƒL[ğ•œ] F} (dfx : HasStrictFDerivAt f (fâ‚.coprod fâ‚‚) (xâ‚, xâ‚‚)) :
    âˆ€á¶  u in ğ“ xâ‚, f (u, dfx.implicitFunOfProdDomain u) = f (xâ‚, xâ‚‚) := by
  have hÏˆ := dfx.tendsto_implicitFunOfProdDomain
  set Ïˆ := dfx.implicitFunOfProdDomain
  suffices âˆ€á¶  u in ğ“ xâ‚, f (u, Ïˆ u) = f (xâ‚, xâ‚‚) â†” Ïˆ u = Ïˆ u by simpa
  apply Eventually.image_of_prod (r := fun u v => f (u, v) = f (xâ‚, xâ‚‚) â†” Ïˆ u = v) hÏˆ
  rw [â† nhds_prod_eq]
  exact dfx.image_eq_iff_implicitFunOfProdDomain

end ProdDomain

end HasStrictFDerivAt

section Bivariate

variable {ğ•œ Eâ‚ Eâ‚‚ F : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
  [NormedAddCommGroup Eâ‚] [NormedSpace ğ•œ Eâ‚] [CompleteSpace Eâ‚] [NormedAddCommGroup Eâ‚‚]
  [NormedSpace ğ•œ Eâ‚‚] [CompleteSpace Eâ‚‚] [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]

variable {f : Eâ‚ â†’ Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
  {fâ‚ : Eâ‚ â†’ Eâ‚‚ â†’ Eâ‚ â†’L[ğ•œ] F} (cfâ‚ : ContinuousAt â†¿fâ‚ (xâ‚, xâ‚‚))
  (dfâ‚ : âˆ€á¶  y in ğ“ (xâ‚, xâ‚‚), HasFDerivAt (f Â· y.2) (fâ‚ y.1 y.2) y.1)
  {fâ‚‚ : Eâ‚ â†’ Eâ‚‚ â†’ Eâ‚‚ â†’L[ğ•œ] F} (cfâ‚‚ : ContinuousAt â†¿fâ‚‚ (xâ‚, xâ‚‚))
  (dfâ‚‚ : âˆ€á¶  y in ğ“ (xâ‚, xâ‚‚), HasFDerivAt (f y.1 Â·) (fâ‚‚ y.1 y.2) y.2)
  {fâ‚‚x : Eâ‚‚ â‰ƒL[ğ•œ] F} (hfâ‚‚x : fâ‚‚ xâ‚ xâ‚‚ = fâ‚‚x)

/-- Implicit function `Ïˆ : Eâ‚ â†’ Eâ‚‚` associated with the (curried) bivariate function
`f : Eâ‚ â†’ Eâ‚‚ â†’ F` at `(xâ‚, xâ‚‚)`. -/
def implicitFunOfBivariate : Eâ‚ â†’ Eâ‚‚ :=
  hfâ‚‚x â–¸ hasStrictFDerivAt_uncurry_coprod cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ |>.implicitFunOfProdDomain

theorem hasStrictFDerivAt_implicitFunOfBivariate :
    HasStrictFDerivAt (implicitFunOfBivariate cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ hfâ‚‚x) (-fâ‚‚x.symm âˆ˜L fâ‚ xâ‚ xâ‚‚) xâ‚ :=
  hfâ‚‚x â–¸ hasStrictFDerivAt_uncurry_coprod cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚
    |>.hasStrictFDerivAt_implicitFunOfProdDomain

theorem image_eq_iff_implicitFunOfBivariate :
    âˆ€á¶  y in ğ“ (xâ‚, xâ‚‚), â†¿f y = f xâ‚ xâ‚‚ â†” implicitFunOfBivariate cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ hfâ‚‚x y.1 = y.2 :=
  hfâ‚‚x â–¸ hasStrictFDerivAt_uncurry_coprod cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ |>.image_eq_iff_implicitFunOfProdDomain

theorem tendsto_implicitFunOfBivariate :
    Tendsto (implicitFunOfBivariate cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ hfâ‚‚x) (ğ“ xâ‚) (ğ“ xâ‚‚) :=
  hfâ‚‚x â–¸ hasStrictFDerivAt_uncurry_coprod cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ |>.tendsto_implicitFunOfProdDomain

theorem image_implicitFunOfBivariate :
    âˆ€á¶  u in ğ“ xâ‚, f u (implicitFunOfBivariate cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ hfâ‚‚x u) = f xâ‚ xâ‚‚ :=
  hfâ‚‚x â–¸ hasStrictFDerivAt_uncurry_coprod cfâ‚ dfâ‚ cfâ‚‚ dfâ‚‚ |>.image_implicitFunOfProdDomain

end Bivariate
