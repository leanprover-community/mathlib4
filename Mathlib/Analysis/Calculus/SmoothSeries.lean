/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.UniformLimitsDeriv
import Mathlib.Topology.Algebra.InfiniteSum.Module
import Mathlib.Analysis.Normed.Group.FunctionSeries

/-!
# Smoothness of series

We show that series of functions are differentiable, or smooth, when each individual
function in the series is and additionally suitable uniform summable bounds are satisfied.

More specifically,
* `differentiable_tsum` ensures that a series of differentiable functions is differentiable.
* `contDiff_tsum` ensures that a series of `C^n` functions is `C^n`.

We also give versions of these statements which are localized to a set.
-/


open Set Metric TopologicalSpace Function Asymptotics Filter

open scoped Topology NNReal

variable {Î± Î² ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [CompleteSpace F] {u : Î± â†’ â„}

/-! ### Differentiability -/

variable [NormedSpace ğ•œ F]
variable {f : Î± â†’ E â†’ F} {f' : Î± â†’ E â†’ E â†’L[ğ•œ] F} {g : Î± â†’ ğ•œ â†’ F} {g' : Î± â†’ ğ•œ â†’ F} {v : â„• â†’ Î± â†’ â„}
  {s : Set E} {t : Set ğ•œ} {xâ‚€ x : E} {yâ‚€ y : ğ•œ} {N : â„•âˆ}

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series converges everywhere on the set. -/
theorem summable_of_summable_hasFDerivAt_of_isPreconnected (hu : Summable u) (hs : IsOpen s)
    (h's : IsPreconnected s) (hf : âˆ€ n x, x âˆˆ s â†’ HasFDerivAt (f n) (f' n x) x)
    (hf' : âˆ€ n x, x âˆˆ s â†’ â€–f' n xâ€– â‰¤ u n) (hxâ‚€ : xâ‚€ âˆˆ s) (hf0 : Summable (f Â· xâ‚€))
    (hx : x âˆˆ s) : Summable fun n => f n x := by
  haveI := Classical.decEq Î±
  rw [summable_iff_cauchySeq_finset] at hf0 âŠ¢
  have A : UniformCauchySeqOn (fun t : Finset Î± => fun x => âˆ‘ i âˆˆ t, f' i x) atTop s :=
    (tendstoUniformlyOn_tsum hu hf').uniformCauchySeqOn
  refine cauchy_map_of_uniformCauchySeqOn_fderiv (f := fun t x â†¦ âˆ‘ i âˆˆ t, f i x)
    hs h's A (fun t y hy => ?_) hxâ‚€ hx hf0
  exact HasFDerivAt.fun_sum fun i _ => hf i y hy

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series converges everywhere on the set. -/
theorem summable_of_summable_hasDerivAt_of_isPreconnected (hu : Summable u) (ht : IsOpen t)
    (h't : IsPreconnected t) (hg : âˆ€ n y, y âˆˆ t â†’ HasDerivAt (g n) (g' n y) y)
    (hg' : âˆ€ n y, y âˆˆ t â†’ â€–g' n yâ€– â‰¤ u n) (hyâ‚€ : yâ‚€ âˆˆ t) (hg0 : Summable (g Â· yâ‚€))
    (hy : y âˆˆ t) : Summable fun n => g n y := by
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hg
  refine summable_of_summable_hasFDerivAt_of_isPreconnected hu ht h't hg ?_ hyâ‚€ hg0 hy
  simpa

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series is differentiable on the set and its derivative is the sum of the
derivatives. -/
theorem hasFDerivAt_tsum_of_isPreconnected (hu : Summable u) (hs : IsOpen s)
    (h's : IsPreconnected s) (hf : âˆ€ n x, x âˆˆ s â†’ HasFDerivAt (f n) (f' n x) x)
    (hf' : âˆ€ n x, x âˆˆ s â†’ â€–f' n xâ€– â‰¤ u n) (hxâ‚€ : xâ‚€ âˆˆ s) (hf0 : Summable fun n => f n xâ‚€)
    (hx : x âˆˆ s) : HasFDerivAt (fun y => âˆ‘' n, f n y) (âˆ‘' n, f' n x) x := by
  classical
    have A :
      âˆ€ x : E, x âˆˆ s â†’ Tendsto (fun t : Finset Î± => âˆ‘ n âˆˆ t, f n x) atTop (ğ“ (âˆ‘' n, f n x)) := by
      intro y hy
      apply Summable.hasSum
      exact summable_of_summable_hasFDerivAt_of_isPreconnected hu hs h's hf hf' hxâ‚€ hf0 hy
    refine hasFDerivAt_of_tendstoUniformlyOn hs (tendstoUniformlyOn_tsum hu hf')
      (fun t y hy => ?_) A hx
    exact HasFDerivAt.fun_sum fun n _ => hf n y hy

/-- Consider a series of functions `âˆ‘' n, f n x` on a preconnected open set. If the series converges
at a point, and all functions in the series are differentiable with a summable bound on the
derivatives, then the series is differentiable on the set and its derivative is the sum of the
derivatives. -/
theorem hasDerivAt_tsum_of_isPreconnected (hu : Summable u) (ht : IsOpen t)
    (h't : IsPreconnected t) (hg : âˆ€ n y, y âˆˆ t â†’ HasDerivAt (g n) (g' n y) y)
    (hg' : âˆ€ n y, y âˆˆ t â†’ â€–g' n yâ€– â‰¤ u n) (hyâ‚€ : yâ‚€ âˆˆ t) (hg0 : Summable fun n => g n yâ‚€)
    (hy : y âˆˆ t) : HasDerivAt (fun z => âˆ‘' n, g n z) (âˆ‘' n, g' n y) y := by
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hg âŠ¢
  convert hasFDerivAt_tsum_of_isPreconnected hu ht h't hg ?_ hyâ‚€ hg0 hy
  Â· exact (ContinuousLinearMap.smulRightL ğ•œ ğ•œ F 1).map_tsum <|
      .of_norm_bounded hu fun n â†¦ hg' n y hy
  Â· simpa

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series converges everywhere. -/
theorem summable_of_summable_hasFDerivAt (hu : Summable u)
    (hf : âˆ€ n x, HasFDerivAt (f n) (f' n x) x) (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n)
    (hf0 : Summable fun n => f n xâ‚€) (x : E) : Summable fun n => f n x := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  let _ : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ _
  exact summable_of_summable_hasFDerivAt_of_isPreconnected hu isOpen_univ isPreconnected_univ
    (fun n x _ => hf n x) (fun n x _ => hf' n x) (mem_univ _) hf0 (mem_univ _)

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series converges everywhere. -/
theorem summable_of_summable_hasDerivAt (hu : Summable u)
    (hg : âˆ€ n y, HasDerivAt (g n) (g' n y) y) (hg' : âˆ€ n y, â€–g' n yâ€– â‰¤ u n)
    (hg0 : Summable fun n => g n yâ‚€) (y : ğ•œ) : Summable fun n => g n y := by
  exact summable_of_summable_hasDerivAt_of_isPreconnected hu isOpen_univ isPreconnected_univ
    (fun n x _ => hg n x) (fun n x _ => hg' n x) (mem_univ _) hg0 (mem_univ _)

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series is differentiable and its derivative is the sum of the derivatives. -/
theorem hasFDerivAt_tsum (hu : Summable u) (hf : âˆ€ n x, HasFDerivAt (f n) (f' n x) x)
    (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n) (hf0 : Summable fun n => f n xâ‚€) (x : E) :
    HasFDerivAt (fun y => âˆ‘' n, f n y) (âˆ‘' n, f' n x) x := by
  letI : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
  let A : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ _
  exact hasFDerivAt_tsum_of_isPreconnected hu isOpen_univ isPreconnected_univ
    (fun n x _ => hf n x) (fun n x _ => hf' n x) (mem_univ _) hf0 (mem_univ _)

/-- Consider a series of functions `âˆ‘' n, f n x`. If the series converges at a
point, and all functions in the series are differentiable with a summable bound on the derivatives,
then the series is differentiable and its derivative is the sum of the derivatives. -/
theorem hasDerivAt_tsum (hu : Summable u) (hg : âˆ€ n y, HasDerivAt (g n) (g' n y) y)
    (hg' : âˆ€ n y, â€–g' n yâ€– â‰¤ u n) (hg0 : Summable fun n => g n yâ‚€) (y : ğ•œ) :
    HasDerivAt (fun z => âˆ‘' n, g n z) (âˆ‘' n, g' n y) y := by
  exact hasDerivAt_tsum_of_isPreconnected hu isOpen_univ isPreconnected_univ
    (fun n y _ => hg n y) (fun n y _ => hg' n y) (mem_univ _) hg0 (mem_univ _)

/-- Consider a series of functions `âˆ‘' n, f n x`. If all functions in the series are differentiable
with a summable bound on the derivatives, then the series is differentiable.
Note that our assumptions do not ensure the pointwise convergence, but if there is no pointwise
convergence then the series is zero everywhere so the result still holds. -/
theorem differentiable_tsum (hu : Summable u) (hf : âˆ€ n x, HasFDerivAt (f n) (f' n x) x)
    (hf' : âˆ€ n x, â€–f' n xâ€– â‰¤ u n) : Differentiable ğ•œ fun y => âˆ‘' n, f n y := by
  by_cases h : âˆƒ xâ‚€, Summable fun n => f n xâ‚€
  Â· rcases h with âŸ¨xâ‚€, hf0âŸ©
    intro x
    exact (hasFDerivAt_tsum hu hf hf' hf0 x).differentiableAt
  Â· push_neg at h
    have : (fun x => âˆ‘' n, f n x) = 0 := by ext1 x; exact tsum_eq_zero_of_not_summable (h x)
    rw [this]
    exact differentiable_const 0

/-- Consider a series of functions `âˆ‘' n, f n x`. If all functions in the series are differentiable
with a summable bound on the derivatives, then the series is differentiable.
Note that our assumptions do not ensure the pointwise convergence, but if there is no pointwise
convergence then the series is zero everywhere so the result still holds. -/
theorem differentiable_tsum' (hu : Summable u) (hg : âˆ€ n y, HasDerivAt (g n) (g' n y) y)
    (hg' : âˆ€ n y, â€–g' n yâ€– â‰¤ u n) : Differentiable ğ•œ fun z => âˆ‘' n, g n z := by
  simp_rw [hasDerivAt_iff_hasFDerivAt] at hg
  refine differentiable_tsum hu hg ?_
  simpa

theorem fderiv_tsum_apply (hu : Summable u) (hf : âˆ€ n, Differentiable ğ•œ (f n))
    (hf' : âˆ€ n x, â€–fderiv ğ•œ (f n) xâ€– â‰¤ u n) (hf0 : Summable fun n => f n xâ‚€) (x : E) :
    fderiv ğ•œ (fun y => âˆ‘' n, f n y) x = âˆ‘' n, fderiv ğ•œ (f n) x :=
  (hasFDerivAt_tsum hu (fun n x => (hf n x).hasFDerivAt) hf' hf0 _).fderiv

theorem deriv_tsum_apply (hu : Summable u) (hg : âˆ€ n, Differentiable ğ•œ (g n))
    (hg' : âˆ€ n y, â€–deriv (g n) yâ€– â‰¤ u n) (hg0 : Summable fun n => g n yâ‚€) (y : ğ•œ) :
    deriv (fun z => âˆ‘' n, g n z) y = âˆ‘' n, deriv (g n) y :=
  (hasDerivAt_tsum hu (fun n y => (hg n y).hasDerivAt) hg' hg0 _).deriv

theorem fderiv_tsum (hu : Summable u) (hf : âˆ€ n, Differentiable ğ•œ (f n))
    (hf' : âˆ€ n x, â€–fderiv ğ•œ (f n) xâ€– â‰¤ u n) (hf0 : Summable fun n => f n xâ‚€) :
    (fderiv ğ•œ fun y => âˆ‘' n, f n y) = fun x => âˆ‘' n, fderiv ğ•œ (f n) x := by
  ext1 x
  exact fderiv_tsum_apply hu hf hf' hf0 x

theorem deriv_tsum (hu : Summable u) (hg : âˆ€ n, Differentiable ğ•œ (g n))
    (hg' : âˆ€ n y, â€–deriv (g n) yâ€– â‰¤ u n) (hg0 : Summable fun n => g n yâ‚€) :
    (deriv fun y => âˆ‘' n, g n y) = fun y => âˆ‘' n, deriv (g n) y := by
  ext1 x
  exact deriv_tsum_apply hu hg hg' hg0 x

/-! ### Higher smoothness -/

/-- Consider a series of `C^n` functions, with summable uniform bounds on the successive
derivatives. Then the iterated derivative of the sum is the sum of the iterated derivative. -/
theorem iteratedFDeriv_tsum (hf : âˆ€ i, ContDiff ğ•œ N (f i))
    (hv : âˆ€ k : â„•, (k : â„•âˆ) â‰¤ N â†’ Summable (v k))
    (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), (k : â„•âˆ) â‰¤ N â†’ â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i) {k : â„•}
    (hk : (k : â„•âˆ) â‰¤ N) :
    (iteratedFDeriv ğ•œ k fun y => âˆ‘' n, f n y) = fun x => âˆ‘' n, iteratedFDeriv ğ•œ k (f n) x := by
  induction k with
  | zero =>
    ext1 x
    simp_rw [iteratedFDeriv_zero_eq_comp]
    exact (continuousMultilinearCurryFin0 ğ•œ E F).symm.toContinuousLinearEquiv.map_tsum
  | succ k IH =>
    have h'k : (k : â„•âˆ) < N := lt_of_lt_of_le (WithTop.coe_lt_coe.2 (Nat.lt_succ_self _)) hk
    have A : Summable fun n => iteratedFDeriv ğ•œ k (f n) 0 :=
      .of_norm_bounded (hv k h'k.le) fun n â†¦ h'f k n 0 h'k.le
    simp_rw [iteratedFDeriv_succ_eq_comp_left, IH h'k.le]
    rw [fderiv_tsum (hv _ hk) (fun n => (hf n).differentiable_iteratedFDeriv
        (mod_cast h'k)) _ A]
    Â· ext1 x
      exact (continuousMultilinearCurryLeftEquiv ğ•œ
        (fun _ : Fin (k + 1) => E) F).symm.toContinuousLinearEquiv.map_tsum
    Â· intro n x
      simpa only [iteratedFDeriv_succ_eq_comp_left, LinearIsometryEquiv.norm_map, comp_apply]
        using h'f k.succ n x hk

/-- Consider a series of smooth functions, with summable uniform bounds on the successive
derivatives. Then the iterated derivative of the sum is the sum of the iterated derivative. -/
theorem iteratedFDeriv_tsum_apply (hf : âˆ€ i, ContDiff ğ•œ N (f i))
    (hv : âˆ€ k : â„•, (k : â„•âˆ) â‰¤ N â†’ Summable (v k))
    (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), (k : â„•âˆ) â‰¤ N â†’ â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i) {k : â„•}
    (hk : (k : â„•âˆ) â‰¤ N) (x : E) :
    iteratedFDeriv ğ•œ k (fun y => âˆ‘' n, f n y) x = âˆ‘' n, iteratedFDeriv ğ•œ k (f n) x := by
  rw [iteratedFDeriv_tsum hf hv h'f hk]

/-- Consider a series of functions `âˆ‘' i, f i x`. Assume that each individual function `f i` is of
class `C^N`, and moreover there is a uniform summable upper bound on the `k`-th derivative
for each `k â‰¤ N`. Then the series is also `C^N`. -/
theorem contDiff_tsum (hf : âˆ€ i, ContDiff ğ•œ N (f i)) (hv : âˆ€ k : â„•, (k : â„•âˆ) â‰¤ N â†’ Summable (v k))
    (h'f : âˆ€ (k : â„•) (i : Î±) (x : E), k â‰¤ N â†’ â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i) :
    ContDiff ğ•œ N fun x => âˆ‘' i, f i x := by
  rw [contDiff_iff_continuous_differentiable]
  constructor
  Â· intro m hm
    rw [iteratedFDeriv_tsum hf hv h'f hm]
    refine continuous_tsum ?_ (hv m hm) ?_
    Â· intro i
      exact ContDiff.continuous_iteratedFDeriv (mod_cast hm) (hf i)
    Â· intro n x
      exact h'f _ _ _ hm
  Â· intro m hm
    have h'm : ((m + 1 : â„•) : â„•âˆ) â‰¤ N := by
      simpa only [ENat.coe_add, ENat.coe_one] using Order.add_one_le_of_lt hm
    rw [iteratedFDeriv_tsum hf hv h'f hm.le]
    have A n x : HasFDerivAt (iteratedFDeriv ğ•œ m (f n)) (fderiv ğ•œ (iteratedFDeriv ğ•œ m (f n)) x) x :=
      (ContDiff.differentiable_iteratedFDeriv (mod_cast hm)
        (hf n)).differentiableAt.hasFDerivAt
    refine differentiable_tsum (hv _ h'm) A fun n x => ?_
    rw [fderiv_iteratedFDeriv, comp_apply, LinearIsometryEquiv.norm_map]
    exact h'f _ _ _ h'm

/-- Consider a series of functions `âˆ‘' i, f i x`. Assume that each individual function `f i` is of
class `C^N`, and moreover there is a uniform summable upper bound on the `k`-th derivative
for each `k â‰¤ N` (except maybe for finitely many `i`s). Then the series is also `C^N`. -/
theorem contDiff_tsum_of_eventually (hf : âˆ€ i, ContDiff ğ•œ N (f i))
    (hv : âˆ€ k : â„•, k â‰¤ N â†’ Summable (v k))
    (h'f : âˆ€ k : â„•, k â‰¤ N â†’
      âˆ€á¶  i in (Filter.cofinite : Filter Î±), âˆ€ x : E, â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i) :
    ContDiff ğ•œ N fun x => âˆ‘' i, f i x := by
  classical
    refine contDiff_iff_forall_nat_le.2 fun m hm => ?_
    let t : Set Î± :=
      { i : Î± | Â¬âˆ€ k : â„•, k âˆˆ Finset.range (m + 1) â†’ âˆ€ x, â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i }
    have ht : Set.Finite t :=
      haveI A :
        âˆ€á¶  i in (Filter.cofinite : Filter Î±),
          âˆ€ k : â„•, k âˆˆ Finset.range (m + 1) â†’ âˆ€ x : E, â€–iteratedFDeriv ğ•œ k (f i) xâ€– â‰¤ v k i := by
        rw [eventually_all_finset]
        intro i hi
        apply h'f
        simp only [Finset.mem_range_succ_iff] at hi
        exact (WithTop.coe_le_coe.2 hi).trans hm
      eventually_cofinite.2 A
    let T : Finset Î± := ht.toFinset
    have : (fun x => âˆ‘' i, f i x) = (fun x => âˆ‘ i âˆˆ T, f i x) +
        fun x => âˆ‘' i : { i // i âˆ‰ T }, f i x := by
      ext1 x
      refine (Summable.sum_add_tsum_subtype_compl ?_ T).symm
      refine .of_norm_bounded_eventually (hv 0 (zero_le _)) ?_
      filter_upwards [h'f 0 (zero_le _)] with i hi
      simpa only [norm_iteratedFDeriv_zero] using hi x
    rw [this]
    apply (ContDiff.sum fun i _ => (hf i).of_le (mod_cast hm)).add
    have h'u : âˆ€ k : â„•, (k : â„•âˆ) â‰¤ m â†’ Summable (v k âˆ˜ ((â†‘) : { i // i âˆ‰ T } â†’ Î±)) := fun k hk =>
      (hv k (hk.trans hm)).subtype _
    refine contDiff_tsum (fun i => (hf i).of_le (mod_cast hm)) h'u ?_
    rintro k âŸ¨i, hiâŸ© x hk
    simp only [t, T, Finite.mem_toFinset, mem_setOf_eq, Finset.mem_range, not_forall, not_le,
      exists_prop, not_exists, not_and, not_lt] at hi
    exact hi k (Nat.lt_succ_iff.2 (WithTop.coe_le_coe.1 hk)) x
