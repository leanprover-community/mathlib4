/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Calculus.Deriv.ZPow

/-!
# Logarithmic Derivatives

We define the logarithmic derivative of a function f as `deriv f / f`. We then prove some basic
facts about this, including how it changes under multiplication and composition.

-/

noncomputable section

open Filter Function

open scoped Topology

variable {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ']
  [NormedAlgebra ğ•œ ğ•œ']

/-- The logarithmic derivative of a function defined as `deriv f /f`. Note that it will be zero
at `x` if `f` is not DifferentiableAt `x`. -/
def logDeriv (f : ğ•œ â†’ ğ•œ') :=
  deriv f / f

theorem logDeriv_apply (f : ğ•œ â†’ ğ•œ') (x : ğ•œ) : logDeriv f x = deriv f x / f x := rfl

lemma logDeriv_eq_zero_of_not_differentiableAt (f : ğ•œ â†’ ğ•œ') (x : ğ•œ) (h : Â¬DifferentiableAt ğ•œ f x) :
    logDeriv f x = 0 := by
  simp only [logDeriv_apply, deriv_zero_of_not_differentiableAt h, zero_div]

@[simp]
theorem logDeriv_id (x : ğ•œ) : logDeriv id x = 1 / x := by
  simp [logDeriv_apply]

@[simp] theorem logDeriv_id' (x : ğ•œ) : logDeriv (Â·) x = 1 / x := logDeriv_id x

@[simp]
theorem logDeriv_const (a : ğ•œ') : logDeriv (fun _ : ğ•œ â†¦ a) = 0 := by
  ext
  simp [logDeriv_apply]

theorem logDeriv_mul {f g : ğ•œ â†’ ğ•œ'} (x : ğ•œ) (hf : f x â‰  0) (hg : g x â‰  0)
    (hdf : DifferentiableAt ğ•œ f x) (hdg : DifferentiableAt ğ•œ g x) :
      logDeriv (fun z => f z * g z) x = logDeriv f x + logDeriv g x := by
  simp only [logDeriv_apply, deriv_mul hdf hdg]
  field_simp [mul_comm]

theorem logDeriv_div {f g : ğ•œ â†’ ğ•œ'} (x : ğ•œ) (hf : f x â‰  0) (hg : g x â‰  0)
    (hdf : DifferentiableAt ğ•œ f x) (hdg : DifferentiableAt ğ•œ g x) :
    logDeriv (fun z => f z / g z) x = logDeriv f x - logDeriv g x := by
  simp only [logDeriv_apply, deriv_div hdf hdg]
  field_simp [mul_comm]
  ring

theorem logDeriv_mul_const {f : ğ•œ â†’ ğ•œ'} (x : ğ•œ) (a : ğ•œ') (ha : a â‰  0):
    logDeriv (fun z => f z * a) x = logDeriv f x := by
  simp only [logDeriv_apply, deriv_mul_const_field, mul_div_mul_right _ _ ha]

theorem logDeriv_const_mul {f : ğ•œ â†’ ğ•œ'} (x : ğ•œ) (a : ğ•œ') (ha : a â‰  0):
    logDeriv (fun z => a * f z) x = logDeriv f x := by
  simp only [logDeriv_apply, deriv_const_mul_field, mul_div_mul_left _ _ ha]

/-- The logarithmic derivative of a finite product is the sum of the logarithmic derivatives. -/
theorem logDeriv_prod {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ ğ•œ â†’ ğ•œ') (x : ğ•œ) (hf : âˆ€ i âˆˆ s, f i x â‰  0)
    (hd : âˆ€ i âˆˆ s, DifferentiableAt ğ•œ (f i) x) :
    logDeriv (âˆ i âˆˆ s, f i Â·) x = âˆ‘ i âˆˆ s, logDeriv (f i) x := by
  induction s using Finset.cons_induction with
  | empty => simp
  | cons a s ha ih =>
    rw [Finset.forall_mem_cons] at hf hd
    simp_rw [Finset.prod_cons, Finset.sum_cons]
    rw [logDeriv_mul, ih hf.2 hd.2]
    Â· exact hf.1
    Â· simpa [Finset.prod_eq_zero_iff] using hf.2
    Â· exact hd.1
    Â· exact .finset_prod hd.2

lemma logDeriv_fun_zpow {f : ğ•œ â†’ ğ•œ'} {x : ğ•œ} (hdf : DifferentiableAt ğ•œ f x) (n : â„¤) :
    logDeriv (f Â· ^ n) x = n * logDeriv f x := by
  rcases eq_or_ne n 0 with rfl | hn; Â· simp
  rcases eq_or_ne (f x) 0 with hf | hf
  Â· simp [logDeriv_apply, zero_zpow, *]
  Â· rw [logDeriv_apply, â† comp_def (Â·^n), deriv_comp _ (differentiableAt_zpow.2 <| .inl hf) hdf,
      deriv_zpow, logDeriv_apply]
    field_simp [zpow_ne_zero, zpow_sub_oneâ‚€ hf]
    ring

lemma logDeriv_fun_pow {f : ğ•œ â†’ ğ•œ'} {x : ğ•œ} (hdf : DifferentiableAt ğ•œ f x) (n : â„•) :
    logDeriv (f Â· ^ n) x = n * logDeriv f x :=
  mod_cast logDeriv_fun_zpow hdf n

@[simp]
lemma logDeriv_zpow (x : ğ•œ) (n : â„¤) : logDeriv (Â· ^ n) x = n / x := by
  rw [logDeriv_fun_zpow (by fun_prop), logDeriv_id', mul_one_div]

@[simp]
lemma logDeriv_pow (x : ğ•œ) (n : â„•) : logDeriv (Â· ^ n) x = n / x :=
  mod_cast logDeriv_zpow x n

@[simp] lemma logDeriv_inv (x : ğ•œ) : logDeriv (Â·â»Â¹) x = -1 / x := by
  simpa using logDeriv_zpow x (-1)

theorem logDeriv_comp {f : ğ•œ' â†’ ğ•œ'} {g : ğ•œ â†’ ğ•œ'} {x : ğ•œ} (hf : DifferentiableAt ğ•œ' f (g x))
    (hg : DifferentiableAt ğ•œ g x) : logDeriv (f âˆ˜ g) x = logDeriv f (g x) * deriv g x := by
  simp only [logDeriv, Pi.div_apply, deriv_comp _ hf hg, comp_apply]
  ring
