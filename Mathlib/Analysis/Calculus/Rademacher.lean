/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Measurable
import Mathlib.Analysis.NormedSpace.FiniteDimension
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar
import Mathlib.Analysis.BoundedVariation
import Mathlib.Analysis.NormedSpace.HahnBanach.SeparatingDual
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff

/-!
# Rademacher theorem: a Lipschitz function is differentiable almost everywhere

-/

open Filter MeasureTheory Measure FiniteDimensional Metric Set

open scoped BigOperators NNReal ENNReal Topology

namespace LipschitzWith

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  [MeasurableSpace E] [BorelSpace E] {C D : â„â‰¥0} {f g : E â†’ â„}

/-!
### Step 1: A Lipschitz function is ae differentiable in any given direction

This follows from the one-dimensional result that a Lipschitz function on `â„` has bounded
variation, and is therefore ae differentiable, together with a Fubini argument.
-/

theorem ae_lineDifferentiableAt_of_prod
    {C : â„â‰¥0} {f : E Ã— â„ â†’ â„} (hf : LipschitzWith C f) {Î¼ : Measure E} :
    âˆ€áµ p âˆ‚(Î¼.prod volume), LineDifferentiableAt â„ f p (0, 1) := by
  apply (ae_prod_mem_iff_ae_ae_mem (measurableSet_lineDifferentiableAt hf.continuous)).2
  apply eventually_of_forall (fun x â†¦ ?_)
  have : âˆ€áµ (y : â„), DifferentiableAt â„ (fun z â†¦ f (x, z)) y := by
    apply LipschitzWith.ae_differentiableAt (C := C)
    intro z z'
    convert hf (x, z) (x, z')
    simp [Prod.edist_eq]
  filter_upwards [this] with y hy
  have h'y : DifferentiableAt â„ (fun z â†¦ f (x, z)) (y + 0) := by simpa using hy
  have : DifferentiableAt â„ (fun t â†¦ y + t) 0 :=
    (differentiable_id.const_add _).differentiableAt
  simpa only [LineDifferentiableAt, Prod.smul_mk, smul_zero, smul_eq_mul, mul_one, Prod.mk_add_mk,
    add_zero] using h'y.comp 0 this

variable {Î¼ : Measure E} [IsAddHaarMeasure Î¼]

theorem ae_lineDifferentiableAt (hf : LipschitzWith C f) (v : E) :
    âˆ€áµ p âˆ‚Î¼, LineDifferentiableAt â„ f p v := by
  rcases eq_or_ne v 0 with rfl|hv
  Â· simp [lineDifferentiableAt_zero]
  let n := finrank â„ E
  let F := Fin (n-1) â†’ â„
  obtain âŸ¨L, hLâŸ© : âˆƒ L : (F Ã— â„) â‰ƒL[â„] E, L (0, 1) = v := by
    have : Nontrivial E := nontrivial_of_ne v 0 hv
    have M : (F Ã— â„) â‰ƒL[â„] E := by
      apply ContinuousLinearEquiv.ofFinrankEq
      simpa using Nat.sub_add_cancel finrank_pos
    obtain âŸ¨N, hNâŸ© : âˆƒ N : E â‰ƒL[â„] E, N (M (0, 1)) = v :=
      SeparatingDual.exists_continuousLinearEquiv_apply_eq (by simp) hv
    exact âŸ¨M.trans N, hNâŸ©
  let Ï : Measure (F Ã— â„) := addHaar.prod volume
  have : IsAddHaarMeasure (Measure.map L Ï) := L.isAddHaarMeasure_map Ï
  suffices H : âˆ€áµ p âˆ‚(Measure.map L Ï), LineDifferentiableAt â„ f p v from
    absolutelyContinuous_isAddHaarMeasure _ _ H
  apply (ae_map_iff L.continuous.aemeasurable (measurableSet_lineDifferentiableAt hf.continuous)).2
  have : âˆ€áµ p âˆ‚Ï, LineDifferentiableAt â„ (f âˆ˜ L) p (0, 1) :=
    (hf.comp L.lipschitz).ae_lineDifferentiableAt_of_prod
  filter_upwards [this] with p hp
  have h'p : LineDifferentiableAt â„ (f âˆ˜ (L : (F Ã— â„) â†’â‚—[â„] E)) p (0, 1) := hp
  rw [â† hL]
  exact LineDifferentiableAt.of_comp h'p

theorem memâ„’p_lineDeriv (hf : LipschitzWith C f) (v : E) :
    Memâ„’p (fun x â†¦ lineDeriv â„ f x v) âˆ Î¼ :=
  memâ„’p_top_of_bound (aestronglyMeasurable_lineDeriv hf.continuous Î¼)
    (C * â€–vâ€–) (eventually_of_forall (fun _x â†¦ norm_lineDeriv_le_of_lipschitz â„ hf))

theorem locallyIntegrable_lineDeriv (hf : LipschitzWith C f) (v : E) :
    LocallyIntegrable (fun x â†¦ lineDeriv â„ f x v) Î¼ :=
  (hf.memâ„’p_lineDeriv v).locallyIntegrable le_top

/-!
### Step 2: the ae line derivative is linear

Surprisingly, this is the hardest step. We prove it using an elegant but slightly sophisticated
argument by Morrey, with a distributional flavor: we integrate against a smooth function, and push
the derivative to the smooth function by integration by parts. As the derivative of a smooth
function is linear, this gives the result.
-/

theorem integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul
    (hf : LipschitzWith C f) (hg : Integrable g Î¼) (v : E) :
    Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
      (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) := by
  apply tendsto_integral_filter_of_dominated_convergence (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–)
  Â· apply eventually_of_forall (fun t â†¦ ?_)
    apply AEStronglyMeasurable.mul ?_ hg.aestronglyMeasurable
    apply aestronglyMeasurable_const.smul
    apply AEStronglyMeasurable.sub _ hf.continuous.measurable.aestronglyMeasurable
    apply AEMeasurable.aestronglyMeasurable
    exact hf.continuous.measurable.comp_aemeasurable' (aemeasurable_id'.add_const _)
  Â· filter_upwards [self_mem_nhdsWithin] with t (ht : 0 < t)
    apply eventually_of_forall (fun x â†¦ ?_)
    calc â€–tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x) * g xâ€–
      = (tâ»Â¹ * â€–f (x + t â€¢ v) - f xâ€–) * â€–g xâ€– := by simp [norm_mul, ht.le]
    _ â‰¤ (tâ»Â¹ * (C * â€–(x + t â€¢ v) - xâ€–)) * â€–g xâ€– := by
      gcongr; exact LipschitzWith.norm_sub_le hf (x + t â€¢ v) x
    _ = (C * â€–vâ€–) *â€–g xâ€– := by field_simp [norm_smul, abs_of_nonneg ht.le]; ring
  Â· exact hg.norm.const_mul _
  Â· filter_upwards [hf.ae_lineDifferentiableAt v] with x hx
    exact hx.hasLineDerivAt.tendsto_nhdsWithin_right.mul tendsto_const_nhds

theorem integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul'
    (hf : LipschitzWith C f) (h'f : HasCompactSupport f) (hg : Continuous g) (v : E) :
    Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
      (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) := by
  let K := cthickening (â€–vâ€–) (tsupport f)
  have K_compact : IsCompact K := IsCompact.cthickening h'f
  apply tendsto_integral_filter_of_dominated_convergence
      (K.indicator (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–))
  Â· apply eventually_of_forall (fun t â†¦ ?_)
    apply AEStronglyMeasurable.mul ?_ hg.aestronglyMeasurable
    apply aestronglyMeasurable_const.smul
    apply AEStronglyMeasurable.sub _ hf.continuous.measurable.aestronglyMeasurable
    apply AEMeasurable.aestronglyMeasurable
    exact hf.continuous.measurable.comp_aemeasurable' (aemeasurable_id'.add_const _)
  Â· filter_upwards [Ioc_mem_nhdsWithin_Ioi' zero_lt_one] with t ht
    have t_pos : 0 < t := ht.1
    apply eventually_of_forall (fun x â†¦ ?_)
    by_cases hx : x âˆˆ K
    Â· calc â€–tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x) * g xâ€–
        = (tâ»Â¹ * â€–f (x + t â€¢ v) - f xâ€–) * â€–g xâ€– := by simp [norm_mul, t_pos.le]
      _ â‰¤ (tâ»Â¹ * (C * â€–(x + t â€¢ v) - xâ€–)) * â€–g xâ€– := by
        gcongr; exact LipschitzWith.norm_sub_le hf (x + t â€¢ v) x
      _ = (C * â€–vâ€–) *â€–g xâ€– := by field_simp [norm_smul, abs_of_nonneg t_pos.le]; ring
      _ = K.indicator (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–) x := by rw [indicator_of_mem hx]
    Â· have A : f x = 0 := by
        rw [â† Function.nmem_support]
        contrapose! hx
        exact self_subset_cthickening _ (subset_tsupport _ hx)
      have B : f (x + t â€¢ v) = 0 := by
        rw [â† Function.nmem_support]
        contrapose! hx
        apply mem_cthickening_of_dist_le _ _  (â€–vâ€–) (tsupport f) (subset_tsupport _ hx)
        simp only [dist_eq_norm, sub_add_cancel', norm_neg, norm_smul, Real.norm_eq_abs,
          abs_of_nonneg t_pos.le, norm_pos_iff]
        exact mul_le_of_le_one_left (norm_nonneg v) ht.2
      simp only [B, A, _root_.sub_self, smul_eq_mul, mul_zero, zero_mul, norm_zero]
      exact indicator_nonneg (fun y _hy â†¦ by positivity) _
  Â· rw [integrable_indicator_iff K_compact.measurableSet]
    apply ContinuousOn.integrableOn_compact K_compact
    exact (Continuous.mul continuous_const hg.norm).continuousOn
  Â· filter_upwards [hf.ae_lineDifferentiableAt v] with x hx
    exact hx.hasLineDerivAt.tendsto_nhdsWithin_right.mul tendsto_const_nhds

/-- Integration by parts formula for the line derivative of Lipschitz functions, assuming one of
them is compactly supported. -/
theorem integral_lineDeriv_mul_eq
    (hf : LipschitzWith C f) (hg : LipschitzWith D g) (h'g : HasCompactSupport g) (v : E) :
    âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼ = âˆ« x, lineDeriv â„ g x (-v) * f x âˆ‚Î¼ := by
  /- Write down the line derivative as the limit of `(f (x + t v) - f x) / t` and
  `(g (x - t v) - g x) / t`, and therefore the integrals as limits of the corresponding integrals
  thanks to the dominated convergence theorem. At fixed positive `t`, the integrals coincide
  (with the change of variables `y = x + t v`), so the limits also coincide. -/
  have A : Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
              (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) :=
    integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul
      hf (hg.continuous.integrable_of_hasCompactSupport h'g) v
  have B : Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (g (x + t â€¢ (-v)) - g x)) * f x âˆ‚Î¼) (ğ“[>] 0)
              (ğ“ (âˆ« x, lineDeriv â„ g x (-v) * f x âˆ‚Î¼)) :=
    integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul' hg h'g hf.continuous (-v)
  suffices S1 : âˆ€ (t : â„), âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼ =
                            âˆ« x, (tâ»Â¹ â€¢ (g (x + t â€¢ (-v)) - g x)) * f x âˆ‚Î¼ by
    simp only [S1] at A; exact tendsto_nhds_unique A B
  intro t
  suffices S2 : âˆ« x, (f (x + t â€¢ v) - f x) * g x âˆ‚Î¼ = âˆ« x, f x * (g (x + t â€¢ (-v)) - g x) âˆ‚Î¼ by
    simp only [smul_eq_mul, mul_assoc, integral_mul_left, S2, mul_neg, mul_comm (f _)]
  have S3 : âˆ« x, f (x + t â€¢ v) * g x âˆ‚Î¼ = âˆ« x, f x * g (x + t â€¢ (-v)) âˆ‚Î¼ := by
    rw [â† integral_add_right_eq_self _ (t â€¢ (-v))]; simp
  simp_rw [_root_.sub_mul, _root_.mul_sub]
  rw [integral_sub, integral_sub, S3]
  Â· apply Continuous.integrable_of_hasCompactSupport
    Â· exact hf.continuous.mul (hg.continuous.comp (continuous_add_right _))
    Â· exact (h'g.comp_homeomorph (Homeomorph.addRight (t â€¢ (-v)))).mul_left
  Â· exact (hf.continuous.mul hg.continuous).integrable_of_hasCompactSupport h'g.mul_left
  Â· apply Continuous.integrable_of_hasCompactSupport
    Â· exact (hf.continuous.comp (continuous_add_right _)).mul hg.continuous
    Â· exact h'g.mul_left
  Â· exact (hf.continuous.mul hg.continuous).integrable_of_hasCompactSupport h'g.mul_left

/-- The line derivative of a Lipschitz function is almost everywhere linear with respect to fixed
coefficients. -/
theorem ae_lineDeriv_sum_eq
    {Î¹ : Type*} {s : Finset Î¹} {a : Î¹ â†’ â„} {v : Î¹ â†’ E} (hf : LipschitzWith C f) :
    âˆ€áµ x âˆ‚Î¼, lineDeriv â„ f x (âˆ‘ i in s, a i â€¢ v i) = âˆ‘ i in s, a i â€¢ lineDeriv â„ f x (v i) := by
  /- Clever argument by Morrey: integrate against a smooth compactly supported function `g`, switch
  the derivative to `g` by integration by parts, and use the linearity of the derivative of `g` to
  conclude that the initial integrals coincide. -/
  apply ae_eq_of_integral_contDiff_smul_eq (hf.locallyIntegrable_lineDeriv _)
    (locallyIntegrable_finset_sum _ (fun i hi â†¦  (hf.locallyIntegrable_lineDeriv (v i)).smul (a i)))
    (fun g g_smooth g_comp â†¦ ?_)
  simp_rw [Finset.smul_sum]
  have A : âˆ€ i âˆˆ s, Integrable (fun x â†¦ g x â€¢ (a i â€¢ fun x â†¦ lineDeriv â„ f x (v i)) x) Î¼ :=
    fun i hi â†¦ (g_smooth.continuous.integrable_of_hasCompactSupport g_comp).smul_of_top_left
      ((hf.memâ„’p_lineDeriv (v i)).const_smul (a i))
  rw [integral_finset_sum _ A]
  suffices S1 : âˆ« x, lineDeriv â„ f x (âˆ‘ i in s, a i â€¢ v i) * g x âˆ‚Î¼
      = âˆ‘ i in s, a i * âˆ« x, lineDeriv â„ f x (v i) * g x âˆ‚Î¼ by
    dsimp only [smul_eq_mul, Pi.smul_apply]
    simp_rw [â† mul_assoc, mul_comm _ (a _), mul_assoc, integral_mul_left, mul_comm (g _), S1]
  suffices S2 : âˆ« x, (âˆ‘ i in s, a i * fderiv â„ g x (v i)) * f x âˆ‚Î¼ =
                  âˆ‘ i in s, a i * âˆ« x, fderiv â„ g x (v i) * f x âˆ‚Î¼ by
    obtain âŸ¨D, g_lipâŸ© : âˆƒ D, LipschitzWith D g :=
      ContDiff.lipschitzWith_of_hasCompactSupport g_comp g_smooth le_top
    simp_rw [integral_lineDeriv_mul_eq hf g_lip g_comp]
    simp_rw [(g_smooth.differentiable le_top).differentiableAt.lineDeriv_eq_fderiv]
    simp only [map_neg, ContinuousLinearMap.map_sum, SMulHomClass.map_smul, smul_eq_mul, neg_mul]
    simp only [integral_neg, mul_neg, Finset.sum_neg_distrib, neg_inj]
    exact S2
  suffices B : âˆ€ i âˆˆ s, Integrable (fun x â†¦ a i * (fderiv â„ g x (v i) * f x)) Î¼ by
    simp_rw [Finset.sum_mul, mul_assoc, integral_finset_sum s B, integral_mul_left]
  intro i _hi
  let L : (E â†’L[â„] â„) â†’ â„ := fun f â†¦ f (v i)
  have L_cont : Continuous L := (ContinuousLinearMap.apply â„ (Fâ‚— := â„) (v i)).continuous
  change Integrable (fun x â†¦ a i * ((L âˆ˜ (fderiv â„ g)) x * f x)) Î¼
  refine (Continuous.integrable_of_hasCompactSupport ?_ ?_).const_mul _
  Â· exact (L_cont.comp (g_smooth.continuous_fderiv le_top)).mul hf.continuous
  Â· exact ((g_comp.fderiv â„).comp_left rfl).mul_right

/-!
### Step 3: construct the derivative using the line derivatives along a basis
-/



end LipschitzWith
