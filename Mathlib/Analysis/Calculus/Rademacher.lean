/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.LineDeriv.Measurable
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.MeasureTheory.Measure.Lebesgue.EqHaar
import Mathlib.Analysis.BoundedVariation
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff
import Mathlib.MeasureTheory.Measure.Haar.Disintegration

/-!
# Rademacher's theorem: a Lipschitz function is differentiable almost everywhere

This file proves Rademacher's theorem: a Lipschitz function between finite-dimensional real vector
spaces is differentiable almost everywhere with respect to the Lebesgue measure. This is the content
of `LipschitzWith.ae_differentiableAt`. Versions for functions which are Lipschitz on sets are also
given (see `LipschitzOnWith.ae_differentiableWithinAt`).

## Implementation

There are many proofs of Rademacher's theorem. We follow the one by Morrey, which is not the most
elementary but maybe the most elegant once necessary prerequisites are set up.
* Step 0: without loss of generality, one may assume that `f` is real-valued.
* Step 1: Since a one-dimensional Lipschitz function has bounded variation, it is differentiable
  almost everywhere. With a Fubini argument, it follows that given any vector `v` then `f` is ae
  differentiable in the direction of `v`. See `LipschitzWith.ae_lineDifferentiableAt`.
* Step 2: the line derivative `LineDeriv â„ f x v` is ae linear in `v`. Morrey proves this by a
  duality argument, integrating against a smooth compactly supported function `g`, passing the
  derivative to `g` by integration by parts, and using the linearity of the derivative of `g`.
  See `LipschitzWith.ae_lineDeriv_sum_eq`.
* Step 3: consider a countable dense set `s` of directions. Almost everywhere, the function `f`
  is line-differentiable in all these directions and the line derivative is linear. Approximating
  any direction by a direction in `s` and using the fact that `f` is Lipschitz to control the error,
  it follows that `f` is FrÃ©chet-differentiable at these points.
  See `LipschitzWith.hasFDerivAt_of_hasLineDerivAt_of_closure`.

## References

* [Pertti Mattila, Geometry of sets and measures in Euclidean spaces, Theorem 7.3][Federer1996]
-/

open Filter MeasureTheory Measure Module Metric Set Asymptotics

open scoped NNReal ENNReal Topology

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  [MeasurableSpace E] [BorelSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] {C D : â„â‰¥0} {f g : E â†’ â„} {s : Set E}
  {Î¼ : Measure E}

namespace LipschitzWith

/-!
### Step 1: A Lipschitz function is ae differentiable in any given direction

This follows from the one-dimensional result that a Lipschitz function on `â„` has bounded
variation, and is therefore ae differentiable, together with a Fubini argument.
-/


theorem memLp_lineDeriv (hf : LipschitzWith C f) (v : E) :
    MemLp (fun x â†¦ lineDeriv â„ f x v) âˆ Î¼ :=
  memLp_top_of_bound (aestronglyMeasurable_lineDeriv hf.continuous Î¼)
    (C * â€–vâ€–) (.of_forall fun _x â†¦ norm_lineDeriv_le_of_lipschitz â„ hf)

variable [FiniteDimensional â„ E] [IsAddHaarMeasure Î¼]

theorem ae_lineDifferentiableAt
    (hf : LipschitzWith C f) (v : E) :
    âˆ€áµ p âˆ‚Î¼, LineDifferentiableAt â„ f p v := by
  let L : â„ â†’L[â„] E := ContinuousLinearMap.smulRight (1 : â„ â†’L[â„] â„) v
  suffices A : âˆ€ p, âˆ€áµ (t : â„) âˆ‚volume, LineDifferentiableAt â„ f (p + t â€¢ v) v from
    ae_mem_of_ae_add_linearMap_mem L.toLinearMap volume Î¼
      (measurableSet_lineDifferentiableAt hf.continuous) A
  intro p
  have : âˆ€áµ (s : â„), DifferentiableAt â„ (fun t â†¦ f (p + t â€¢ v)) s :=
    (hf.comp ((LipschitzWith.const p).add L.lipschitz)).ae_differentiableAt_real
  filter_upwards [this] with s hs
  have h's : DifferentiableAt â„ (fun t â†¦ f (p + t â€¢ v)) (s + 0) := by simpa using hs
  have : DifferentiableAt â„ (fun t â†¦ s + t) 0 := differentiableAt_id.const_add _
  simp only [LineDifferentiableAt]
  convert h's.comp 0 this with _ t
  simp only [add_assoc, Function.comp_apply, add_smul]

theorem locallyIntegrable_lineDeriv (hf : LipschitzWith C f) (v : E) :
    LocallyIntegrable (fun x â†¦ lineDeriv â„ f x v) Î¼ :=
  (hf.memLp_lineDeriv v).locallyIntegrable le_top

/-!
### Step 2: the ae line derivative is linear

Surprisingly, this is the hardest step. We prove it using an elegant but slightly sophisticated
argument by Morrey, with a distributional flavor: we integrate against a smooth function, and push
the derivative to the smooth function by integration by parts. As the derivative of a smooth
function is linear, this gives the result.
-/

theorem integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul
    (hf : LipschitzWith C f) (hg : Integrable g Î¼) (v : E) :
    Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
      (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) := by
  apply tendsto_integral_filter_of_dominated_convergence (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–)
  Â· filter_upwards with t
    apply AEStronglyMeasurable.mul ?_ hg.aestronglyMeasurable
    apply aestronglyMeasurable_const.smul
    apply AEStronglyMeasurable.sub _ hf.continuous.measurable.aestronglyMeasurable
    apply AEMeasurable.aestronglyMeasurable
    exact hf.continuous.measurable.comp_aemeasurable' (aemeasurable_id'.add_const _)
  Â· filter_upwards [self_mem_nhdsWithin] with t (ht : 0 < t)
    filter_upwards with x
    calc â€–tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x) * g xâ€–
      = (tâ»Â¹ * â€–f (x + t â€¢ v) - f xâ€–) * â€–g xâ€– := by simp [norm_mul, ht.le]
    _ â‰¤ (tâ»Â¹ * (C * â€–(x + t â€¢ v) - xâ€–)) * â€–g xâ€– := by
      gcongr; exact LipschitzWith.norm_sub_le hf (x + t â€¢ v) x
    _ = (C * â€–vâ€–) *â€–g xâ€– := by simp [field, norm_smul, abs_of_nonneg ht.le]
  Â· exact hg.norm.const_mul _
  Â· filter_upwards [hf.ae_lineDifferentiableAt v] with x hx
    exact hx.hasLineDerivAt.tendsto_slope_zero_right.mul tendsto_const_nhds

theorem integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul'
    (hf : LipschitzWith C f) (h'f : HasCompactSupport f) (hg : Continuous g) (v : E) :
    Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
      (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) := by
  let K := cthickening (â€–vâ€–) (tsupport f)
  have K_compact : IsCompact K := IsCompact.cthickening h'f
  apply tendsto_integral_filter_of_dominated_convergence
      (K.indicator (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–))
  Â· filter_upwards with t
    apply AEStronglyMeasurable.mul ?_ hg.aestronglyMeasurable
    apply aestronglyMeasurable_const.smul
    apply AEStronglyMeasurable.sub _ hf.continuous.measurable.aestronglyMeasurable
    apply AEMeasurable.aestronglyMeasurable
    exact hf.continuous.measurable.comp_aemeasurable' (aemeasurable_id'.add_const _)
  Â· filter_upwards [Ioc_mem_nhdsGT zero_lt_one] with t ht
    have t_pos : 0 < t := ht.1
    filter_upwards with x
    by_cases hx : x âˆˆ K
    Â· calc â€–tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x) * g xâ€–
        = (tâ»Â¹ * â€–f (x + t â€¢ v) - f xâ€–) * â€–g xâ€– := by simp [norm_mul, t_pos.le]
      _ â‰¤ (tâ»Â¹ * (C * â€–(x + t â€¢ v) - xâ€–)) * â€–g xâ€– := by
        gcongr; exact LipschitzWith.norm_sub_le hf (x + t â€¢ v) x
      _ = (C * â€–vâ€–) *â€–g xâ€– := by simp [field, norm_smul, abs_of_nonneg t_pos.le]
      _ = K.indicator (fun x â†¦ (C * â€–vâ€–) * â€–g xâ€–) x := by rw [indicator_of_mem hx]
    Â· have A : f x = 0 := by
        rw [â† Function.notMem_support]
        contrapose! hx
        exact self_subset_cthickening _ (subset_tsupport _ hx)
      have B : f (x + t â€¢ v) = 0 := by
        rw [â† Function.notMem_support]
        contrapose! hx
        apply mem_cthickening_of_dist_le _ _ (â€–vâ€–) (tsupport f) (subset_tsupport _ hx)
        simp only [dist_eq_norm, sub_add_cancel_left, norm_neg, norm_smul, Real.norm_eq_abs,
          abs_of_nonneg t_pos.le]
        exact mul_le_of_le_one_left (norm_nonneg v) ht.2
      simp only [B, A, _root_.sub_self, smul_eq_mul, mul_zero, zero_mul, norm_zero]
      exact indicator_nonneg (fun y _hy â†¦ by positivity) _
  Â· rw [integrable_indicator_iff K_compact.measurableSet]
    apply ContinuousOn.integrableOn_compact K_compact
    exact (Continuous.mul continuous_const hg.norm).continuousOn
  Â· filter_upwards [hf.ae_lineDifferentiableAt v] with x hx
    exact hx.hasLineDerivAt.tendsto_slope_zero_right.mul tendsto_const_nhds

/-- Integration by parts formula for the line derivative of Lipschitz functions, assuming one of
them is compactly supported. -/
theorem integral_lineDeriv_mul_eq
    (hf : LipschitzWith C f) (hg : LipschitzWith D g) (h'g : HasCompactSupport g) (v : E) :
    âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼ = âˆ« x, lineDeriv â„ g x (-v) * f x âˆ‚Î¼ := by
  /- Write down the line derivative as the limit of `(f (x + t v) - f x) / t` and
  `(g (x - t v) - g x) / t`, and therefore the integrals as limits of the corresponding integrals
  thanks to the dominated convergence theorem. At fixed positive `t`, the integrals coincide
  (with the change of variables `y = x + t v`), so the limits also coincide. -/
  have A : Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼) (ğ“[>] 0)
              (ğ“ (âˆ« x, lineDeriv â„ f x v * g x âˆ‚Î¼)) :=
    integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul
      hf (hg.continuous.integrable_of_hasCompactSupport h'g) v
  have B : Tendsto (fun (t : â„) â†¦ âˆ« x, (tâ»Â¹ â€¢ (g (x + t â€¢ (-v)) - g x)) * f x âˆ‚Î¼) (ğ“[>] 0)
              (ğ“ (âˆ« x, lineDeriv â„ g x (-v) * f x âˆ‚Î¼)) :=
    integral_inv_smul_sub_mul_tendsto_integral_lineDeriv_mul' hg h'g hf.continuous (-v)
  suffices S1 : âˆ€ (t : â„), âˆ« x, (tâ»Â¹ â€¢ (f (x + t â€¢ v) - f x)) * g x âˆ‚Î¼ =
                            âˆ« x, (tâ»Â¹ â€¢ (g (x + t â€¢ (-v)) - g x)) * f x âˆ‚Î¼ by
    simp only [S1] at A; exact tendsto_nhds_unique A B
  intro t
  suffices S2 : âˆ« x, (f (x + t â€¢ v) - f x) * g x âˆ‚Î¼ = âˆ« x, f x * (g (x + t â€¢ (-v)) - g x) âˆ‚Î¼ by
    simp only [smul_eq_mul, mul_assoc, integral_const_mul, S2, mul_comm (f _)]
  have S3 : âˆ« x, f (x + t â€¢ v) * g x âˆ‚Î¼ = âˆ« x, f x * g (x + t â€¢ (-v)) âˆ‚Î¼ := by
    rw [â† integral_add_right_eq_self _ (t â€¢ (-v))]; simp
  simp_rw [_root_.sub_mul, _root_.mul_sub]
  rw [integral_sub, integral_sub, S3]
  Â· apply Continuous.integrable_of_hasCompactSupport
    Â· exact hf.continuous.mul (hg.continuous.comp (continuous_add_right _))
    Â· exact (h'g.comp_homeomorph (Homeomorph.addRight (t â€¢ (-v)))).mul_left
  Â· exact (hf.continuous.mul hg.continuous).integrable_of_hasCompactSupport h'g.mul_left
  Â· apply Continuous.integrable_of_hasCompactSupport
    Â· exact (hf.continuous.comp (continuous_add_right _)).mul hg.continuous
    Â· exact h'g.mul_left
  Â· exact (hf.continuous.mul hg.continuous).integrable_of_hasCompactSupport h'g.mul_left

/-- The line derivative of a Lipschitz function is almost everywhere linear with respect to fixed
coefficients. -/
theorem ae_lineDeriv_sum_eq
    (hf : LipschitzWith C f) {Î¹ : Type*} (s : Finset Î¹) (a : Î¹ â†’ â„) (v : Î¹ â†’ E) :
    âˆ€áµ x âˆ‚Î¼, lineDeriv â„ f x (âˆ‘ i âˆˆ s, a i â€¢ v i) = âˆ‘ i âˆˆ s, a i â€¢ lineDeriv â„ f x (v i) := by
  /- Clever argument by Morrey: integrate against a smooth compactly supported function `g`, switch
  the derivative to `g` by integration by parts, and use the linearity of the derivative of `g` to
  conclude that the initial integrals coincide. -/
  apply ae_eq_of_integral_contDiff_smul_eq (hf.locallyIntegrable_lineDeriv _)
    (locallyIntegrable_finset_sum _ (fun i hi â†¦ (hf.locallyIntegrable_lineDeriv (v i)).smul (a i)))
    (fun g g_smooth g_comp â†¦ ?_)
  simp_rw [Finset.smul_sum]
  have A : âˆ€ i âˆˆ s, Integrable (fun x â†¦ g x â€¢ (a i â€¢ fun x â†¦ lineDeriv â„ f x (v i)) x) Î¼ :=
    fun i hi â†¦ (g_smooth.continuous.integrable_of_hasCompactSupport g_comp).smul_of_top_left
      ((hf.memLp_lineDeriv (v i)).const_smul (a i))
  rw [integral_finset_sum _ A]
  suffices S1 : âˆ« x, lineDeriv â„ f x (âˆ‘ i âˆˆ s, a i â€¢ v i) * g x âˆ‚Î¼
      = âˆ‘ i âˆˆ s, a i * âˆ« x, lineDeriv â„ f x (v i) * g x âˆ‚Î¼ by
    dsimp only [smul_eq_mul, Pi.smul_apply]
    simp_rw [â† mul_assoc, mul_comm _ (a _), mul_assoc, integral_const_mul, mul_comm (g _), S1]
  suffices S2 : âˆ« x, (âˆ‘ i âˆˆ s, a i * fderiv â„ g x (v i)) * f x âˆ‚Î¼ =
                  âˆ‘ i âˆˆ s, a i * âˆ« x, fderiv â„ g x (v i) * f x âˆ‚Î¼ by
    obtain âŸ¨D, g_lipâŸ© : âˆƒ D, LipschitzWith D g :=
      ContDiff.lipschitzWith_of_hasCompactSupport g_comp g_smooth (mod_cast le_top)
    simp_rw [integral_lineDeriv_mul_eq hf g_lip g_comp]
    simp_rw [(g_smooth.differentiable (mod_cast le_top)).differentiableAt.lineDeriv_eq_fderiv]
    simp only [map_neg, _root_.map_sum, map_smul, smul_eq_mul, neg_mul]
    simp only [integral_neg, mul_neg, Finset.sum_neg_distrib, neg_inj]
    exact S2
  suffices B : âˆ€ i âˆˆ s, Integrable (fun x â†¦ a i * (fderiv â„ g x (v i) * f x)) Î¼ by
    simp_rw [Finset.sum_mul, mul_assoc, integral_finset_sum s B, integral_const_mul]
  intro i _hi
  let L : StrongDual â„ E â†’ â„ := fun f â†¦ f (v i)
  change Integrable (fun x â†¦ a i * ((L âˆ˜ (fderiv â„ g)) x * f x)) Î¼
  refine (Continuous.integrable_of_hasCompactSupport ?_ ?_).const_mul _
  Â· exact ((g_smooth.continuous_fderiv (mod_cast le_top)).clm_apply continuous_const).mul
      hf.continuous
  Â· exact ((g_comp.fderiv â„).comp_left rfl).mul_right

/-!
### Step 3: construct the derivative using the line derivatives along a basis
-/

theorem ae_exists_fderiv_of_countable
    (hf : LipschitzWith C f) {s : Set E} (hs : s.Countable) :
    âˆ€áµ x âˆ‚Î¼, âˆƒ (L : StrongDual â„ E), âˆ€ v âˆˆ s, HasLineDerivAt â„ f (L v) x v := by
  have B := Basis.ofVectorSpace â„ E
  have I1 : âˆ€áµ (x : E) âˆ‚Î¼, âˆ€ v âˆˆ s, lineDeriv â„ f x (âˆ‘ i, (B.repr v i) â€¢ B i) =
                                  âˆ‘ i, B.repr v i â€¢ lineDeriv â„ f x (B i) :=
    (ae_ball_iff hs).2 (fun v _ â†¦ hf.ae_lineDeriv_sum_eq _ _ _)
  have I2 : âˆ€áµ (x : E) âˆ‚Î¼, âˆ€ v âˆˆ s, LineDifferentiableAt â„ f x v :=
    (ae_ball_iff hs).2 (fun v _ â†¦ hf.ae_lineDifferentiableAt v)
  filter_upwards [I1, I2] with x hx h'x
  let L : StrongDual â„ E :=
    LinearMap.toContinuousLinearMap (B.constr â„ (fun i â†¦ lineDeriv â„ f x (B i)))
  refine âŸ¨L, fun v hv â†¦ ?_âŸ©
  have J : L v = lineDeriv â„ f x v := by convert (hx v hv).symm <;> simp [L, B.sum_repr v]
  simpa [J] using (h'x v hv).hasLineDerivAt

omit [MeasurableSpace E] in
/-- If a Lipschitz functions has line derivatives in a dense set of directions, all of them given by
a single continuous linear map `L`, then it admits `L` as FrÃ©chet derivative. -/
theorem hasFDerivAt_of_hasLineDerivAt_of_closure
    {f : E â†’ F} (hf : LipschitzWith C f) {s : Set E} (hs : sphere 0 1 âŠ† closure s)
    {L : E â†’L[â„] F} {x : E} (hL : âˆ€ v âˆˆ s, HasLineDerivAt â„ f (L v) x v) :
    HasFDerivAt f L x := by
  rw [hasFDerivAt_iff_isLittleO_nhds_zero, isLittleO_iff]
  intro Îµ Îµpos
  obtain âŸ¨Î´, Î´pos, hÎ´âŸ© : âˆƒ Î´, 0 < Î´ âˆ§ (C + â€–Lâ€– + 1) * Î´ = Îµ :=
    âŸ¨Îµ / (C + â€–Lâ€– + 1), by positivity, mul_div_cancelâ‚€ Îµ (by positivity)âŸ©
  obtain âŸ¨q, hqs, q_fin, hqâŸ© : âˆƒ q, q âŠ† s âˆ§ q.Finite âˆ§ sphere 0 1 âŠ† â‹ƒ y âˆˆ q, ball y Î´ := by
    have : sphere 0 1 âŠ† â‹ƒ y âˆˆ s, ball y Î´ := by
      apply hs.trans (fun z hz â†¦ ?_)
      obtain âŸ¨y, ys, hyâŸ© : âˆƒ y âˆˆ s, dist z y < Î´ := Metric.mem_closure_iff.1 hz Î´ Î´pos
      exact mem_biUnion ys hy
    exact (isCompact_sphere 0 1).elim_finite_subcover_image (fun y _hy â†¦ isOpen_ball) this
  have I : âˆ€á¶  t in ğ“ (0 : â„), âˆ€ v âˆˆ q, â€–f (x + t â€¢ v) - f x - t â€¢ L vâ€– â‰¤ Î´ * â€–tâ€– := by
    apply (Finite.eventually_all q_fin).2 (fun v hv â†¦ ?_)
    apply Asymptotics.IsLittleO.def ?_ Î´pos
    exact hasLineDerivAt_iff_isLittleO_nhds_zero.1 (hL v (hqs hv))
  obtain âŸ¨r, r_pos, hrâŸ© : âˆƒ (r : â„), 0 < r âˆ§ âˆ€ (t : â„), â€–tâ€– < r â†’
      âˆ€ v âˆˆ q, â€–f (x + t â€¢ v) - f x - t â€¢ L vâ€– â‰¤ Î´ * â€–tâ€– := by
    rcases Metric.mem_nhds_iff.1 I with âŸ¨r, r_pos, hrâŸ©
    exact âŸ¨r, r_pos, fun t ht v hv â†¦ hr (mem_ball_zero_iff.2 ht) v hvâŸ©
  apply Metric.mem_nhds_iff.2 âŸ¨r, r_pos, fun v hv â†¦ ?_âŸ©
  rcases eq_or_ne v 0 with rfl | v_ne
  Â· simp
  obtain âŸ¨w, Ï, w_mem, hvw, hÏâŸ© : âˆƒ w Ï, w âˆˆ sphere 0 1 âˆ§ v = Ï â€¢ w âˆ§ Ï = â€–vâ€– := by
    refine âŸ¨â€–vâ€–â»Â¹ â€¢ v, â€–vâ€–, by simp [norm_smul, inv_mul_cancelâ‚€ (norm_ne_zero_iff.2 v_ne)], ?_, rflâŸ©
    simp [smul_smul, mul_inv_cancelâ‚€ (norm_ne_zero_iff.2 v_ne)]
  have norm_rho : â€–Ïâ€– = Ï := by rw [hÏ, norm_norm]
  have rho_pos : 0 â‰¤ Ï := by simp [hÏ]
  obtain âŸ¨y, yq, hyâŸ© : âˆƒ y âˆˆ q, â€–w - yâ€– < Î´ := by simpa [â† dist_eq_norm] using hq w_mem
  have : â€–y - wâ€– < Î´ := by rwa [norm_sub_rev]
  calc  â€–f (x + v) - f x - L vâ€–
      = â€–f (x + Ï â€¢ w) - f x - Ï â€¢ L wâ€– := by simp [hvw]
    _ = â€–(f (x + Ï â€¢ w) - f (x + Ï â€¢ y)) + (Ï â€¢ L y - Ï â€¢ L w)
          + (f (x + Ï â€¢ y) - f x - Ï â€¢ L y)â€– := by congr; abel
    _ â‰¤ â€–f (x + Ï â€¢ w) - f (x + Ï â€¢ y)â€– + â€–Ï â€¢ L y - Ï â€¢ L wâ€–
          + â€–f (x + Ï â€¢ y) - f x - Ï â€¢ L yâ€– := norm_addâ‚ƒ_le
    _ â‰¤ C * â€–(x + Ï â€¢ w) - (x + Ï â€¢ y)â€– + Ï * (â€–Lâ€– * â€–y - wâ€–) + Î´ * Ï := by
      gcongr
      Â· exact hf.norm_sub_le _ _
      Â· rw [â† smul_sub, norm_smul, norm_rho]
        gcongr
        exact L.lipschitz.norm_sub_le _ _
      Â· conv_rhs => rw [â† norm_rho]
        apply hr _ _ _ yq
        simpa [norm_rho, hÏ] using hv
    _ â‰¤ C * (Ï * Î´) + Ï * (â€–Lâ€– * Î´) + Î´ * Ï := by
      simp only [add_sub_add_left_eq_sub, â† smul_sub, norm_smul, norm_rho]; gcongr
    _ = ((C + â€–Lâ€– + 1) * Î´) * Ï := by ring
    _ = Îµ * â€–vâ€– := by rw [hÎ´, hÏ]

/-- A real-valued function on a finite-dimensional space which is Lipschitz is
differentiable almost everywere. Superseded by
`LipschitzWith.ae_differentiableAt` which works for functions taking value in any
finite-dimensional space. -/
theorem ae_differentiableAt_of_real (hf : LipschitzWith C f) :
    âˆ€áµ x âˆ‚Î¼, DifferentiableAt â„ f x := by
  obtain âŸ¨s, s_count, s_denseâŸ© : âˆƒ (s : Set E), s.Countable âˆ§ Dense s :=
    TopologicalSpace.exists_countable_dense E
  have hs : sphere 0 1 âŠ† closure s := by rw [s_dense.closure_eq]; exact subset_univ _
  filter_upwards [hf.ae_exists_fderiv_of_countable s_count]
  rintro x âŸ¨L, hLâŸ©
  exact (hf.hasFDerivAt_of_hasLineDerivAt_of_closure hs hL).differentiableAt

end LipschitzWith

variable [FiniteDimensional â„ E] [FiniteDimensional â„ F] [IsAddHaarMeasure Î¼]

namespace LipschitzOnWith

/-- A real-valued function on a finite-dimensional space which is Lipschitz on a set is
differentiable almost everywere in this set. Superseded by
`LipschitzOnWith.ae_differentiableWithinAt_of_mem` which works for functions taking value in any
finite-dimensional space. -/
theorem ae_differentiableWithinAt_of_mem_of_real (hf : LipschitzOnWith C f s) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ DifferentiableWithinAt â„ f s x := by
  obtain âŸ¨g, g_lip, hgâŸ© : âˆƒ (g : E â†’ â„), LipschitzWith C g âˆ§ EqOn f g s := hf.extend_real
  filter_upwards [g_lip.ae_differentiableAt_of_real] with x hx xs
  exact hx.differentiableWithinAt.congr hg (hg xs)

/-- A function on a finite-dimensional space which is Lipschitz on a set and taking values in a
product space is differentiable almost everywere in this set. Superseded by
`LipschitzOnWith.ae_differentiableWithinAt_of_mem` which works for functions taking value in any
finite-dimensional space. -/
theorem ae_differentiableWithinAt_of_mem_pi
    {Î¹ : Type*} [Fintype Î¹] {f : E â†’ Î¹ â†’ â„} {s : Set E}
    (hf : LipschitzOnWith C f s) : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ DifferentiableWithinAt â„ f s x := by
  have A : âˆ€ i : Î¹, LipschitzWith 1 (fun x : Î¹ â†’ â„ â†¦ x i) := fun i => LipschitzWith.eval i
  have : âˆ€ i : Î¹, âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ DifferentiableWithinAt â„ (fun x : E â†¦ f x i) s x := fun i â†¦ by
    apply ae_differentiableWithinAt_of_mem_of_real
    exact LipschitzWith.comp_lipschitzOnWith (A i) hf
  filter_upwards [ae_all_iff.2 this] with x hx xs
  exact differentiableWithinAt_pi.2 (fun i â†¦ hx i xs)

/-- *Rademacher's theorem*: a function between finite-dimensional real vector spaces which is
Lipschitz on a set is differentiable almost everywere in this set. -/
theorem ae_differentiableWithinAt_of_mem {f : E â†’ F} (hf : LipschitzOnWith C f s) :
    âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ DifferentiableWithinAt â„ f s x := by
  have A := (Basis.ofVectorSpace â„ F).equivFun.toContinuousLinearEquiv
  suffices H : âˆ€áµ x âˆ‚Î¼, x âˆˆ s â†’ DifferentiableWithinAt â„ (A âˆ˜ f) s x by
    filter_upwards [H] with x hx xs
    have : f = (A.symm âˆ˜ A) âˆ˜ f := by
      simp only [ContinuousLinearEquiv.symm_comp_self, Function.id_comp]
    rw [this]
    exact A.symm.differentiableAt.comp_differentiableWithinAt x (hx xs)
  apply ae_differentiableWithinAt_of_mem_pi
  exact A.lipschitz.comp_lipschitzOnWith hf

/-- *Rademacher's theorem*: a function between finite-dimensional real vector spaces which is
Lipschitz on a set is differentiable almost everywere in this set. -/
theorem ae_differentiableWithinAt {f : E â†’ F} (hf : LipschitzOnWith C f s)
    (hs : MeasurableSet s) :
    âˆ€áµ x âˆ‚(Î¼.restrict s), DifferentiableWithinAt â„ f s x := by
  rw [ae_restrict_iff' hs]
  exact hf.ae_differentiableWithinAt_of_mem

end LipschitzOnWith

/-- *Rademacher's theorem*: a Lipschitz function between finite-dimensional real vector spaces is
differentiable almost everywhere. -/
theorem LipschitzWith.ae_differentiableAt {f : E â†’ F} (h : LipschitzWith C f) :
    âˆ€áµ x âˆ‚Î¼, DifferentiableAt â„ f x := by
  rw [â† lipschitzOnWith_univ] at h
  simpa [differentiableWithinAt_univ] using h.ae_differentiableWithinAt_of_mem

/-- In a real finite-dimensional normed vector space,
  the norm is almost everywhere differentiable. -/
theorem ae_differentiableAt_norm :
    âˆ€áµ x âˆ‚Î¼, DifferentiableAt â„ (â€–Â·â€–) x := lipschitzWith_one_norm.ae_differentiableAt

omit [MeasurableSpace E] in
/-- In a real finite-dimensional normed vector space,
  the set of points where the norm is differentiable at is dense. -/
theorem dense_differentiableAt_norm :
    Dense {x : E | DifferentiableAt â„ (â€–Â·â€–) x} :=
  let _ : MeasurableSpace E := borel E
  have _ : BorelSpace E := âŸ¨rflâŸ©
  let w := Basis.ofVectorSpace â„ E
  MeasureTheory.Measure.dense_of_ae (ae_differentiableAt_norm (Î¼ := w.addHaar))
