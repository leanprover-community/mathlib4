/-
Copyright (c) 2023 Chenyi Li. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chenyi Li, Ziyu Wang, Zaiwen Wen
-/
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.Deriv.Comp
import Mathlib.Analysis.Calculus.Gradient.Basic

/-!
# Gradient

## Main results

This file contains the following parts of gradient.
* the chain rule for the `g : ğ•œ â†’ ğ•œ` composed with `f : F â†’ ğ•œ`.
* the gradient for the sum of two functions.
* the gradient for finite sum of functions.
* the gradient for the sum of a constant and a function.
* the gradient for the difference of two functions.
* the gradient for the difference of a constant and a function.
* the gradient for the product of two functions.
* the gradient for the product of a constant and a function.
-/

noncomputable section

open Topology InnerProductSpace Set

variable {ğ•œ F : Type*} [IsROrC ğ•œ]

variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] [CompleteSpace F]

variable {f : F â†’ ğ•œ} {f' x : F} {L : Filter F} {s : Set F}

local notation "âˆ‡" => gradient

section Composition

open Set Filter

variable {g : ğ•œ â†’ ğ•œ} {g' : ğ•œ}

variable {L' : Filter ğ•œ} {t : Set ğ•œ}

theorem HasGradientAtFilter.comp
    (hg : HasGradientAtFilter g g' (f x) L') (hf : HasGradientAtFilter f f' x L)
    (hL : Tendsto f L L') : HasGradientAtFilter (g âˆ˜ f) (g' â€¢ f') x L := by
  rw [HasGradientAtFilter, map_smulâ‚›â‚—]
  exact hg.hasDerivAtFilter.comp_hasFDerivAtFilter x hf hL

theorem HasGradientWithinAt.comp
    (hg : HasGradientWithinAt g g' t (f x)) (hf : HasGradientWithinAt f f' s x)
    (hst : MapsTo f s t) : HasGradientWithinAt (g âˆ˜ f) (g' â€¢ f') s x :=
  HasGradientAtFilter.comp hg hf <| hf.continuousWithinAt.tendsto_nhdsWithin hst

theorem HasGradientAt.comp_hasGradientWithinAt
    (hg : HasGradientAt g g' (f x)) (hf : HasGradientWithinAt f f' s x) :
    HasGradientWithinAt (g âˆ˜ f) (g' â€¢ f') s x :=
  hg.comp hf hf.continuousWithinAt

theorem HasGradientWithinAt.comp_of_mem
    (hg : HasGradientWithinAt g g' t (f x)) (hf : HasGradientWithinAt f f' s x)
    (hst : Tendsto f (ğ“[s] x) (ğ“[t] f x)) : HasGradientWithinAt (g âˆ˜ f) (g' â€¢ f') s x :=
  HasGradientAtFilter.comp hg hf hst

/-- The chain rule. -/
theorem HasGradientAt.comp (hg : HasGradientAt g g' (f x))
    (hf : HasGradientAt f f' x) : HasGradientAt (g âˆ˜ f) (g' â€¢ f') x :=
  HasGradientAtFilter.comp hg hf hf.continuousAt

theorem gradient.comp (hg : DifferentiableAt ğ•œ g (f x)) (hf : DifferentiableAt ğ•œ f x) :
    âˆ‡ (g âˆ˜ f) x = (âˆ‡ g (f x)) â€¢ (âˆ‡ f x) :=
  (hg.hasGradientAt.comp hf.hasGradientAt).gradient

end Composition

section ConstSmul

open Set Filter

theorem HasGradientAtFilter.const_smul (h : HasGradientAtFilter f f' x L) (c : ğ•œ) :
    HasGradientAtFilter (fun x => c â€¢ f x) ((starRingEnd ğ•œ) c â€¢ f') x L := by
  have : c â€¢ (toDual ğ•œ F) f' = (toDual ğ•œ F) ((starRingEnd ğ•œ) c â€¢ f') := by
    rw [map_smulâ‚›â‚—, RingHomCompTriple.comp_apply, RingHom.id_apply]
  rw [HasGradientAtFilter, â† this]; rw [HasGradientAtFilter] at h
  exact h.const_smul c

theorem HasGradientWithinAt.const_smul (h : HasGradientWithinAt f f' s x) (c : ğ•œ) :
    HasGradientWithinAt (fun x => c â€¢ f x) ((starRingEnd ğ•œ) c â€¢ f') s x := by
  exact HasGradientAtFilter.const_smul h c

nonrec theorem HasGradientAt.const_smul (h : HasGradientAt f f' x) (c : ğ•œ) :
    HasGradientAt (fun x => c â€¢ f x) ((starRingEnd ğ•œ) c â€¢ f') x := by
  exact  h.const_smul c

theorem Gradient_const_smul (h : DifferentiableAt ğ•œ f x) (c : ğ•œ) :
    âˆ‡ (fun y => c â€¢ f y) x = (starRingEnd ğ•œ) c â€¢ âˆ‡ f x :=
  (h.hasGradientAt.const_smul c).gradient

variable [InnerProductSpace â„ F] {f : F â†’ â„} (c : â„)

theorem HasGradientAtFilter.const_smul' (h : HasGradientAtFilter f f' x L) :
    HasGradientAtFilter (fun x => c â€¢ f x) (c â€¢ f') x L := h.const_smul c

nonrec theorem HasGradientWithinAt.const_smul' (h : HasGradientWithinAt f f' s x) :
    HasGradientWithinAt (fun x => c â€¢ f x) (c â€¢ f') s x := h.const_smul c

nonrec theorem HasGradientAt.const_smul' (h : HasGradientAt f f' x) :
    HasGradientAt (fun x => c â€¢ f x) (c â€¢ f') x := h.const_smul c

end ConstSmul

section Add

variable {f' : F} {g : F â†’ ğ•œ} {x : F} {g' : F}
variable {L : Filter F} {f : F â†’ ğ•œ} {L' : Filter ğ•œ} {t : Set ğ•œ}

theorem HasGradientAtFilter.add (hf : HasGradientAtFilter f f' x L)
    (hg : HasGradientAtFilter g g' x L) :
    HasGradientAtFilter (fun y => f y + g y) (f' + g') x L := by
  rw [HasGradientAtFilter]; rw [HasGradientAtFilter] at hf hg
  have : (toDual ğ•œ F) (f' + g') = (toDual ğ•œ F) f' + (toDual ğ•œ F) g' := by simp
  rw [this]; exact hf.add hg

nonrec theorem HasGradientWithinAt.add (hf : HasGradientWithinAt f f' s x)
    (hg : HasGradientWithinAt g g' s x) : HasGradientWithinAt (fun y => f y + g y) (f' + g') s x :=
  hf.add hg

nonrec theorem HasGradientAt.add (hf : HasGradientAt f f' x) (hg : HasGradientAt g g' x) :
    HasGradientAt (fun x => f x + g x) (f' + g') x :=
  hf.add hg

theorem Gradient_add (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    âˆ‡ (fun y => f y + g y) x = âˆ‡ f x + âˆ‡ g x :=
  (hf.hasGradientAt.add hg.hasGradientAt).gradient

theorem HasGradientAtFilter.add_const (hf : HasGradientAtFilter f f' x L) (c : ğ•œ) :
    HasGradientAtFilter (fun y => f y + c) f' x L :=
  add_zero f' â–¸ hf.add (hasGradientAtFilter_const _ _ _)

nonrec theorem HasGradientWithinAt.add_const (hf : HasGradientWithinAt f f' s x) (c : ğ•œ) :
    HasGradientWithinAt (fun y => f y + c) f' s x :=
  hf.add_const c

nonrec theorem HasGradientAt.add_const (hf : HasGradientAt f f' x) (c : ğ•œ) :
    HasGradientAt (fun x => f x + c) f' x :=
  hf.add_const c

theorem Gradient_add_const (c : ğ•œ) : âˆ‡ (fun y => f y + c) x = âˆ‡ f x := by
  unfold gradient
  simp only [EmbeddingLike.apply_eq_iff_eq]
  exact fderiv_add_const c

theorem HasGradientAtFilter.const_add (hf : HasGradientAtFilter f f' x L) (c : ğ•œ) :
    HasGradientAtFilter (fun y => c + f y) f' x L :=
  zero_add f' â–¸ (hasGradientAtFilter_const _ _ _).add hf

nonrec theorem HasGradientWithinAt.const_add (hf : HasGradientWithinAt f f' s x) (c : ğ•œ) :
    HasGradientWithinAt (fun y => c + f y) f' s x :=
  hf.const_add c

nonrec theorem HasGradientAt.const_add (hf : HasGradientAt f f' x) (c : ğ•œ) :
    HasGradientAt (fun x => c + f x) f' x :=
  hf.const_add c

theorem Gradient_const_add (c : ğ•œ) : âˆ‡ (fun y => c + f y) x = âˆ‡ f x := by
  simp only [add_comm c, Gradient_add_const]

end Add

section Sum

/-! ### Gradient of a finite sum of functions -/


open BigOperators Asymptotics

variable {Î¹ : Type*} {u : Finset Î¹} {A : Î¹ â†’ F â†’ ğ•œ} {A' : Î¹ â†’ F}

theorem HasGradientAtFilter.sum (h : âˆ€ i âˆˆ u, HasGradientAtFilter (A i) (A' i) x L) :
    HasGradientAtFilter (fun y => âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x L := by
  have : âˆ‘ i in u, (toDual ğ•œ F) (A' i) = (toDual ğ•œ F) (âˆ‘ i in u, A' i) := by
    rw [map_sum]
  rw [HasGradientAtFilter, â† this]; unfold HasGradientAtFilter at h
  exact HasFDerivAtFilter.sum h

theorem HasGradientWithinAt.sum (h : âˆ€ i âˆˆ u, HasGradientWithinAt (A i) (A' i) s x) :
    HasGradientWithinAt (fun y => âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) s x := by
  exact HasGradientAtFilter.sum h

theorem HasGradientAt.sum (h : âˆ€ i âˆˆ u, HasGradientAt (A i) (A' i) x) :
    HasGradientAt (fun y => âˆ‘ i in u, A i y) (âˆ‘ i in u, A' i) x := by
  exact HasGradientAtFilter.sum h

theorem Gradient_sum (h : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (A i) x) :
    âˆ‡ (fun y => âˆ‘ i in u, A i y) x = âˆ‘ i in u, âˆ‡ (A i) x :=
  (HasGradientAt.sum fun i hi => (h i hi).hasGradientAt).gradient

end Sum

section Neg

/-! ### Gradient of the negative of a function -/

theorem HasGradientAtFilter.neg (h : HasGradientAtFilter f f' x L) :
    HasGradientAtFilter (fun x => -f x) (-f') x L := by
  have : -(toDual ğ•œ F) f' = (toDual ğ•œ F) (-f') := by simp
  rw [HasGradientAtFilter, â† this]; rw [HasGradientAtFilter] at h
  exact h.neg

nonrec theorem HasGradientWithinAt.neg (h : HasGradientWithinAt f f' s x) :
  HasGradientWithinAt (fun x => -f x) (-f') s x := by exact h.neg

nonrec theorem HasGradientAt.neg (h : HasGradientAt f f' x) :
  HasGradientAt (fun x => -f x) (-f') x := by exact h.neg

theorem Gradient_neg : âˆ‡ (fun y => - f y) x = - âˆ‡ f x := by
  unfold gradient
  simp only [fderiv_neg, map_neg]

end Neg

section Sub

/-! ### Gradient of the difference of two functions -/

variable {f' : F} {g : F â†’ ğ•œ} {x : F} {g' : F}
variable {L : Filter F} {f : F â†’ ğ•œ} {L' : Filter ğ•œ} {t : Set ğ•œ}

theorem HasGradientAtFilter.sub (hf : HasGradientAtFilter f f' x L)
    (hg : HasGradientAtFilter g g' x L) :
    HasGradientAtFilter (fun x => f x - g x) (f' - g') x L := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

nonrec theorem HasGradientWithinAt.sub (hf : HasGradientWithinAt f f' s x)
    (hg : HasGradientWithinAt g g' s x) : HasGradientWithinAt (fun x => f x - g x) (f' - g') s x :=
  hf.sub hg

nonrec theorem HasGradientAt.sub (hf : HasGradientAt f f' x) (hg : HasGradientAt g g' x) :
    HasGradientAt (fun x => f x - g x) (f' - g') x :=
  hf.sub hg

theorem Gradient_sub (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    âˆ‡ (fun y => f y - g y) x = âˆ‡ f x - âˆ‡ g x :=
  (hf.hasGradientAt.sub hg.hasGradientAt).gradient

theorem HasGradientAtFilter.sub_const (hf : HasGradientAtFilter f f' x L) (c : ğ•œ) :
    HasGradientAtFilter (fun x => f x - c) f' x L := by
  simpa only [sub_eq_add_neg] using hf.add_const (-c)

nonrec theorem HasGradientWithinAt.sub_const (hf : HasGradientWithinAt f f' s x) (c : ğ•œ) :
    HasGradientWithinAt (fun x => f x - c) f' s x :=
  hf.sub_const c

nonrec theorem HasGradientAt.sub_const (hf : HasGradientAt f f' x) (c : ğ•œ) :
    HasGradientAt (fun x => f x - c) f' x := by
  exact hf.sub_const c

theorem Gradient_sub_const (c : ğ•œ) : âˆ‡ (fun y => f y - c) x = âˆ‡ f x := by
  simp only [sub_eq_add_neg, Gradient_add_const]

theorem HasGradientAtFilter.const_sub (hf : HasGradientAtFilter f f' x L) (c : ğ•œ) :
    HasGradientAtFilter (fun x => c - f x) (-f') x L := by
  simpa only [sub_eq_add_neg] using hf.neg.const_add c

nonrec theorem HasGradientWithinAt.const_sub (hf : HasGradientWithinAt f f' s x) (c : ğ•œ) :
    HasGradientWithinAt (fun x => c - f x) (-f') s x :=
  hf.const_sub c

nonrec theorem HasGradientAt.const_sub (hf : HasGradientAt f f' x) (c : ğ•œ) :
    HasGradientAt (fun x => c - f x) (-f') x :=
  hf.const_sub c

theorem Gradient_const_sub (c : ğ•œ) : âˆ‡ (fun y => c - f y) x = - âˆ‡ f x := by
  calc
    âˆ‡ (fun y => c - f y) x = âˆ‡ (fun y => - f y + c) x := by  congr; ext x; rw [sub_eq_neg_add]
    _ = - âˆ‡ f x := by rw [Gradient_add_const c, Gradient_neg]

end Sub

section Mul

variable {a' b' c' d' : F} {a b c d : F â†’ ğ•œ} {x : F}

open ContinuousLinearMap

lemma equiv_lemma_mul : c x â€¢ (toDual ğ•œ F) d' + d x â€¢ (toDual ğ•œ F) c'
    = (toDual ğ•œ F) ((starRingEnd ğ•œ) (c x) â€¢ d' + (starRingEnd ğ•œ) (d x) â€¢ c'):= by
  simp only [map_add, map_smulâ‚›â‚—, RingHomCompTriple.comp_apply, RingHom.id_apply]

theorem HasGradientAt.mul (hc : HasGradientAt c c' x) (hd : HasGradientAt d d' x) :
    HasGradientAt (fun y => c y * d y)
    ((starRingEnd ğ•œ) (c x) â€¢ d' + (starRingEnd ğ•œ) (d x) â€¢ c') x := by
  rw [hasGradientAt_iff_hasFDerivAt, â† equiv_lemma_mul]
  rw [hasGradientAt_iff_hasFDerivAt] at hc hd
  exact hc.mul hd

theorem HasGradientWithinAt.mul (hc : HasGradientWithinAt c c' s x)
    (hd : HasGradientWithinAt d d' s x) :
    HasGradientWithinAt (fun y => c y * d y) ((starRingEnd ğ•œ) (c x) â€¢ d'
      + (starRingEnd ğ•œ) (d x) â€¢ c') s x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, â† equiv_lemma_mul]
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt] at hc hd
  exact hc.mul hd

theorem Gradient_mul (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    âˆ‡ (fun y => c y * d y) x = (starRingEnd ğ•œ) (c x) â€¢ âˆ‡ d x
      + (starRingEnd ğ•œ) (d x) â€¢ âˆ‡ c x :=
  (hc.hasGradientAt.mul hd.hasGradientAt).gradient

variable [InnerProductSpace â„ F] {a' b' c' d' : F} {a b c d : F â†’ â„} {x : F}

theorem HasGradientAt.mul' (hc : HasGradientAt c c' x) (hd : HasGradientAt d d' x) :
    HasGradientAt (fun y => c y * d y) ((c x) â€¢ d' + (d x) â€¢ c') x :=
  HasGradientAt.mul hc hd

theorem HasGradientWithinAt.mul' (hc : HasGradientWithinAt c c' s x)
    (hd : HasGradientWithinAt d d' s x) :
    HasGradientWithinAt (fun y => c y * d y) ((c x) â€¢ d' + (d x) â€¢ c') s x :=
  hc.mul hd

theorem Gradient_mul' (hc : DifferentiableAt â„ c x) (hd : DifferentiableAt â„ d x) :
    âˆ‡ (fun y => c y * d y) x = (c x) â€¢ âˆ‡ d x + (d x) â€¢ âˆ‡ c x :=
  Gradient_mul hc hd

end Mul

section Mul_const

variable {c' a': F} {c a: F â†’ ğ•œ} {x : F} (d b: ğ•œ)

lemma equiv_lemma_mul_const : d â€¢ (toDual ğ•œ F) c' = (toDual ğ•œ F) ((starRingEnd ğ•œ) d â€¢ c') := by
  rw [map_smulâ‚›â‚—, RingHomCompTriple.comp_apply, RingHom.id_apply]

theorem HasGradientWithinAt.mul_const (hc : HasGradientWithinAt c c' s x) :
    HasGradientWithinAt (fun y => c y * d) ((starRingEnd ğ•œ) d â€¢ c') s x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, â† equiv_lemma_mul_const]
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt] at hc
  exact hc.mul_const d

theorem HasGradientAt.mul_const (hc : HasGradientAt c c' x) :
    HasGradientAt (fun y => c y * d) ((starRingEnd ğ•œ) d â€¢ c') x := by
  rw [hasGradientAt_iff_hasFDerivAt, â† equiv_lemma_mul_const]
  rw [hasGradientAt_iff_hasFDerivAt] at hc
  exact hc.mul_const d

theorem Gradient_mul_const (hc : DifferentiableAt ğ•œ c x) :
    âˆ‡ (fun y => c y * d) x = (starRingEnd ğ•œ) d â€¢ âˆ‡ c x :=
  (hc.hasGradientAt.mul_const d).gradient

lemma equiv_lemma_const_mul : b â€¢ (toDual ğ•œ F) a' = (toDual ğ•œ F) ((starRingEnd ğ•œ) b â€¢ a') := by
  rw [map_smulâ‚›â‚—, RingHomCompTriple.comp_apply, RingHom.id_apply]

theorem HasGradientWithinAt.const_mul (ha : HasGradientWithinAt a a' s x) :
    HasGradientWithinAt (fun y => b * a y) ((starRingEnd ğ•œ) b â€¢ a') s x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, â† equiv_lemma_const_mul]
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt] at ha
  exact ha.const_mul b

theorem HasGradientAt.const_mul (ha : HasGradientAt a a' x) :
    HasGradientAt (fun y => b * a y) ((starRingEnd ğ•œ) b â€¢ a') x := by
  rw [hasGradientAt_iff_hasFDerivAt, â† equiv_lemma_const_mul]
  rw [hasGradientAt_iff_hasFDerivAt] at ha
  exact ha.const_mul b

theorem Gradient_const_mul (ha : DifferentiableAt ğ•œ a x) :
    âˆ‡ (fun y => b * a y) x = (starRingEnd ğ•œ) b â€¢ âˆ‡ a x :=
  (ha.hasGradientAt.const_mul b).gradient

variable [InnerProductSpace â„ F] {c' a': F} {c a: F â†’ â„} {x : F} (d b: â„)

theorem HasGradientWithinAt.mul_const' (hc : HasGradientWithinAt c c' s x) :
    HasGradientWithinAt (fun y => c y * d) (d â€¢ c') s x :=
  HasGradientWithinAt.mul_const d hc

theorem HasGradientAt.mul_const' (hc : HasGradientAt c c' x) :
    HasGradientAt (fun y => c y * d) (d â€¢ c') x :=
  HasGradientAt.mul_const d hc

theorem Gradient_mul_const' (hc : DifferentiableAt â„ c x) :
    âˆ‡ (fun y => c y * d) x = d â€¢ âˆ‡ c x :=
  Gradient_mul_const d hc

theorem HasGradientWithinAt.const_mul' (ha : HasGradientWithinAt a a' s x) :
    HasGradientWithinAt (fun y => b * a y) (b â€¢ a') s x :=
  HasGradientWithinAt.const_mul b ha

theorem HasGradientAt.const_mul' (ha : HasGradientAt a a' x) :
    HasGradientAt (fun y => b * a y) (b â€¢ a') x :=
  HasGradientAt.const_mul b ha

theorem Gradient_const_mul' (ha : DifferentiableAt â„ a x) :
    âˆ‡ (fun y => b * a y) x = b â€¢ âˆ‡ a x :=
  Gradient_const_mul b ha

end Mul_const
