/-
Copyright (c) 2023 Ziyu Wang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ziyu Wang, Chenyi Li, Yu Penghao, Cao Zhipeng
-/
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.Calculus.FDeriv.Basic
import Mathlib.Analysis.Calculus.Deriv.Basic

/-!
# Gradient

## Main Definitions

Let `f` be a function from a Hilbert Space `F` to `ğ•œ` (`ğ•œ` is `â„` or `â„‚`) , `x` be a point in `F`
and `f'` be a vector in F. Then

  `HasGradientWithinAt f f' s x`

says that `f` has a gradient `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `HasGradientAt f f' x := HasGradientWithinAt f f' x univ`

## Main results

This file contains the following parts of gradient.
* the definition of gradient.
* the theorems translating between `HasGradientAtFilter` and `HasFDerivAtFilter`,
  `HasGradientWithinAt` and `HasFDerivWithinAt`, `HasGradientAt` and `HasFDerivAt`,
  `Gradient` and `fderiv`.
* theorems the Uniqueness of Gradient.
* the theorems translating between  `HasGradientAtFilter` and `HasDerivAtFilter`,
  `HasGradientAt` and `HasDerivAt`, `Gradient` and `deriv` when `F = ğ•œ`.
* the theorems about the congruence of the gradient.
* the theorems about the gradient of constant function.
* the theorems about the continuity of a function admitting a gradient.
-/

open Topology InnerProductSpace Set

noncomputable section

variable {ğ•œ F : Type*} [IsROrC ğ•œ]

variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] [CompleteSpace F]

variable {f : F â†’ ğ•œ} {f' x : F}

/-- A function `f` has the gradient `f'` as derivative along the filter `L` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` when `x'` converges along the filter `L`.-/
def HasGradientAtFilter (f : F â†’ ğ•œ) (f' x : F) (L : Filter F) :=
  HasFDerivAtFilter f (toDual ğ•œ F f') x L

/-- `f` has the gradient `f'` at the point `x` within the subset `s` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x` inside `s`.-/
def HasGradientWithinAt (f : F â†’ ğ•œ) (f' : F) (s : Set F) (x : F):=
  HasGradientAtFilter f f' x (ğ“[s] x)

/-- `f` has the gradient `f'` at the point `x` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x`. -/
def HasGradientAt (f : F â†’ ğ•œ) (f' x : F) :=
  HasGradientAtFilter f f' x (ğ“ x)

/-- Gradient of `f` at the point `x`, if it exists.  Zero otherwise.
If the derivative exists (i.e., `âˆƒ f', HasGradientAt f f' x`), then
`f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x`. -/
def gradient (f : F â†’ ğ•œ) (x : F) : F := (toDual ğ•œ F).symm (fderiv ğ•œ f x)

local notation "âˆ‡" => gradient

variable {s : Set F} {L : Filter F}

theorem HasGradientWithinAt_iff_HasFDerivWithinAt {s : Set F} :
    HasGradientWithinAt f f' s x â†” HasFDerivWithinAt f (toDual ğ•œ F f') s x := Iff.rfl

theorem HasGradientAt_iff_HasFDerivAt :
    HasGradientAt f f' x â†” HasFDerivAt f (toDual ğ•œ F f') x := Iff.rfl

theorem HasGradientAt.hasFDerivAt {frechet : F â†’L[ğ•œ] ğ•œ}
    (h : HasGradientAt f ((toDual ğ•œ F).symm frechet) x) :
    HasFDerivAt f frechet x := by
  rw [â† (toDual ğ•œ F).apply_symm_apply frechet]
  exact HasGradientAt_iff_HasFDerivAt.mp h

theorem HasFDerivAt.hasGradientAt {frechet : F â†’L[ğ•œ] ğ•œ} (h : HasFDerivAt f frechet x) :
    HasGradientAt f ((toDual ğ•œ F).symm frechet) x := by
  rw [â† (toDual ğ•œ F).apply_symm_apply frechet] at h
  exact HasGradientAt_iff_HasFDerivAt.mpr h
theorem Gradient_zero_of_not_differentiableAt (h : Â¬DifferentiableAt ğ•œ f x) : âˆ‡ f x = 0 := by
  have : (toDual ğ•œ F).symm 0 = 0 := by simp only [map_zero]
  rw [â† fderiv_zero_of_not_differentiableAt h] at this
  exact this

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

section GradientUniqueness

variable {gradf gradg : F}

theorem HasGradientAt.unique (hf : HasGradientAt f gradf x) (hg : HasGradientAt f gradg x) :
    gradf = gradg := (toDual ğ•œ F).injective
  ((HasGradientAt_iff_HasFDerivAt.mp hf).unique (HasGradientAt_iff_HasFDerivAt.mp hg))

end GradientUniqueness

theorem HasGradientWithinAt_iff_HasFDerivWithinAt' {s : Set F} {frechet : F â†’L[ğ•œ] ğ•œ} :
    HasGradientWithinAt f ((toDual ğ•œ F).symm frechet) s x â†” HasFDerivWithinAt f frechet s x := by
  conv_rhs => rw [â† (toDual ğ•œ F).apply_symm_apply frechet]

theorem DifferentiableAt.hasGradientAt (h : DifferentiableAt ğ•œ f x) :
    HasGradientAt f (âˆ‡ f x) x := by
  rw [HasGradientAt_iff_HasFDerivAt, gradient, (toDual ğ•œ F).apply_symm_apply (fderiv ğ•œ f x)]
  exact h.hasFDerivAt

theorem HasGradientAt.differentiableAt (h : HasGradientAt f f' x) :
    DifferentiableAt ğ•œ f x := by use ((toDual ğ•œ F) f'); apply HasGradientAt_iff_HasFDerivAt.mp h

theorem HasGradientWithinAt.differentiableWithinAt (h : HasGradientWithinAt f f' s x) :
    DifferentiableWithinAt ğ•œ f s x := HasFDerivWithinAt.differentiableWithinAt h

@[simp]
theorem hasGradientWithinAt_univ : HasGradientWithinAt f f' univ x â†” HasGradientAt f f' x := by
  rw [HasGradientWithinAt_iff_HasFDerivWithinAt, HasGradientAt_iff_HasFDerivAt]
  rw [hasFDerivWithinAt_univ]

theorem DifferentiableOn.hasGradientAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    HasGradientAt f (âˆ‡ f x) x := (h.hasFDerivAt hs).hasGradientAt

theorem HasGradientAt.gradient (h : HasGradientAt f f' x) : âˆ‡ f x = f' :=
  h.differentiableAt.hasGradientAt.unique h

theorem gradient_eq {f' : F â†’ F} (h : âˆ€ x, HasGradientAt f (f' x) x) : âˆ‡ f = f' :=
  funext fun x => (h x).gradient

variable {g : ğ•œ â†’ ğ•œ} {g' u : ğ•œ} {L' : Filter ğ•œ}

theorem toDual_eq_StarRingEnd : ((toDual ğ•œ ğ•œ) g') 1 = starRingEnd ğ•œ g' := by simp

theorem StarRingEnd_eq_toDual : ((toDual ğ•œ ğ•œ) (starRingEnd ğ•œ g')) 1 = g' := by simp

theorem Mul_one_eq_SterRingEnd (g' : ğ•œ) : ContinuousLinearMap.smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ)
    (starRingEnd ğ•œ g') = (toDual ğ•œ ğ•œ) g' := by
  refine Iff.mpr ContinuousLinearMap.ext_iff ?_
  simp; intro v; rw [toDual_apply, IsROrC.inner_apply, mul_comm]

theorem SterRingEnd_eq_Mul_one (g' : ğ•œ) : ContinuousLinearMap.smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ)
    g' = (toDual ğ•œ ğ•œ) (starRingEnd ğ•œ g') := by
  refine Iff.mpr ContinuousLinearMap.ext_iff ?_
  simp; intro; rw [toDual_apply, IsROrC.inner_apply, mul_comm]
  rw [RingHomCompTriple.comp_apply, RingHom.id_apply]

theorem HasGradientAtFilter.hasDerivAtFilter (h : HasGradientAtFilter g g' u L') :
    HasDerivAtFilter g (starRingEnd ğ•œ g') u L' := by
  rw [HasDerivAtFilter, Mul_one_eq_SterRingEnd]; exact h

theorem HasDerivAtFilter.hasGradientAtFilter (h : HasDerivAtFilter g g' u L') :
    HasGradientAtFilter g (starRingEnd ğ•œ g') u L' := by
  rw [HasGradientAtFilter, â† SterRingEnd_eq_Mul_one]; exact h

theorem HasGradientAt.hasDerivAt (h : HasGradientAt g g' u) :
    HasDerivAt g (starRingEnd ğ•œ g') u := by
  rw [HasGradientAt_iff_HasFDerivAt, hasFDerivAt_iff_hasDerivAt, toDual_eq_StarRingEnd] at h
  exact h

theorem HasGradientAt.hasDerivAt' {g : â„ â†’ â„} {g' u: â„} (h :HasGradientAt g g' u) :
    HasDerivAt g g' u := h.hasDerivAt

theorem HasDerivAt.hasGradientAt (h : HasDerivAt g g' u) :
    HasGradientAt g (starRingEnd ğ•œ g') u := by
  rw [HasGradientAt_iff_HasFDerivAt, hasFDerivAt_iff_hasDerivAt, StarRingEnd_eq_toDual]
  exact h

theorem HasDerivAt.hasGradientAt' {g : â„ â†’ â„} {g' u: â„} (h :HasDerivAt g g' u) :
    HasGradientAt g g' u := h.hasGradientAt

theorem gradient_deriv : âˆ‡ g u = starRingEnd ğ•œ (deriv g u) := by
  by_cases h: DifferentiableAt ğ•œ g u
  Â· rw [h.hasGradientAt.hasDerivAt.deriv]
    exact Eq.symm (IsROrC.conj_conj (âˆ‡ g u))
  Â· rw [Gradient_zero_of_not_differentiableAt h, deriv_zero_of_not_differentiableAt h]
    exact Eq.symm (RingHom.map_zero (starRingEnd ğ•œ))

theorem gradient_deriv' {g : â„ â†’ â„} {u: â„} : âˆ‡ g u = deriv g u := gradient_deriv

open Filter

/-- If a function f has a derivative f' at x, a rescaled version of f around x converges to f',
i.e., `n (f (x + (1/n) v) - f x)` converges to `f' v`. More generally, if `c n` tends to infinity
and `c n * d n` tends to `v`, then `c n * (f (x + d n) - f x)` tends to `< f', v >`. This lemma
expresses this fact, for functions having a derivative within a set. Its specific formulation is
useful for tangent cone related discussions. -/
theorem HasGradientWithinAt.lim (h : HasGradientWithinAt f f' s x) {Î± : Type*} (l : Filter Î±)
    {c : Î± â†’ ğ•œ} {d : Î± â†’ F} {v : F} (dtop : âˆ€á¶  n in l, x + d n âˆˆ s)
    (clim : Tendsto (fun n => â€–c nâ€–) l atTop) (cdlim : Tendsto (fun n => c n â€¢ d n) l (ğ“ v)) :
    Tendsto (fun n => c n â€¢ (f (x + d n) - f x)) l (ğ“ âŸªf', vâŸ«) := by
  have : _ := (HasGradientWithinAt_iff_HasFDerivWithinAt.mp h).lim l dtop clim cdlim
  rw [toDual_apply] at this
  exact this

section GradientProperties

theorem hasGradientAtFilter_iff_isLittleO :
    HasGradientAtFilter f f' x L â†”
    (fun x' : F => f x' - f x - âŸªf' , x' - xâŸ«) =o[L] fun x' => x' - x := Iff.rfl

theorem hasGradientWithinAt_iff_isLittleO :
    HasGradientWithinAt f f' s x â†”
    (fun x' : F => f x' - f x - âŸªf' , x' - xâŸ«) =o[ğ“[s] x] fun x' => x' - x := Iff.rfl

theorem hasGradientWithinAt_iff_tendsto :
    HasGradientWithinAt f f' s x â†”
    Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - âŸªf' , x' - xâŸ«â€–) (ğ“[s] x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem hasGradientAt_iff_isLittleO : HasGradientAt f f' x â†”
    (fun x' : F => f x' - f x - âŸªf' , x' - xâŸ«) =o[ğ“ x] fun x' => x' - x := Iff.rfl

theorem hasGradientAt_iff_tendsto :
    HasGradientAt f f' x â†”
    Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - âŸªf' , x' - xâŸ«â€–) (ğ“ x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem HasGradientAtFilter.isBigO_sub (h : HasGradientAtFilter f f' x L) :
    (fun x' => f x' - f x) =O[L] fun x' => x' - x := HasFDerivAtFilter.isBigO_sub h

theorem hasGradientWithinAt_congr_set' {s t : Set F} (y : F) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    HasGradientWithinAt f f' s x â†” HasGradientWithinAt f f' t x := hasFDerivWithinAt_congr_set' y h

theorem hasGradientWithinAt_congr_set {s t : Set F} (h : s =á¶ [ğ“ x] t) :
    HasGradientWithinAt f f' s x â†” HasGradientWithinAt f f' t x := hasFDerivWithinAt_congr_set h

theorem hasGradientAt_iff_isLittleO_nhds_zero : HasGradientAt f f' x â†”
    (fun h => f (x + h) - f x - âŸªf' , hâŸ«)
    =o[ğ“ 0] fun h => h := hasFDerivAt_iff_isLittleO_nhds_zero

end GradientProperties

/-! ### Congruence properties of the Gradient -/
section congr

variable {fâ‚€ fâ‚ : F â†’ ğ•œ} {fâ‚€' fâ‚' : F} {xâ‚€ xâ‚ : F} {sâ‚€ sâ‚ t : Set F} {Lâ‚€ Lâ‚ : Filter F}

theorem Filter.EventuallyEq.hasGradientAtFilter_iff (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x)
    (hâ‚ : fâ‚€' = fâ‚') : HasGradientAtFilter fâ‚€ fâ‚€' x L â†” HasGradientAtFilter fâ‚ fâ‚' x L :=
  hâ‚€.hasFDerivAtFilter_iff hx (by simp [hâ‚])

theorem HasGradientAtFilter.congr_of_eventuallyEq (h : HasGradientAtFilter f f' x L)
    (hL : fâ‚ =á¶ [L] f) (hx : fâ‚ x = f x) : HasGradientAtFilter fâ‚ f' x L := by
  rwa [hL.hasGradientAtFilter_iff hx rfl]

theorem HasGradientWithinAt.congr_mono (h : HasGradientWithinAt f f' s x) (ht : âˆ€ x âˆˆ t, fâ‚ x = f x)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : HasGradientWithinAt fâ‚ f' t x :=
  HasFDerivWithinAt.congr_mono h ht hx hâ‚

theorem HasGradientWithinAt.congr (h : HasGradientWithinAt f f' s x) (hs : âˆ€ x âˆˆ s, fâ‚ x = f x)
    (hx : fâ‚ x = f x) : HasGradientWithinAt fâ‚ f' s x := h.congr_mono hs hx (by tauto)

theorem HasGradientWithinAt.congr_of_mem (h : HasGradientWithinAt f f' s x)
    (hs : âˆ€ x âˆˆ s, fâ‚ x = f x) (hx : x âˆˆ s) : HasGradientWithinAt fâ‚ f' s x := h.congr hs (hs _ hx)

theorem HasGradientWithinAt.congr_of_eventuallyEq (h : HasGradientWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : HasGradientWithinAt fâ‚ f' s x :=
  HasGradientAtFilter.congr_of_eventuallyEq h hâ‚ hx

theorem HasGradientWithinAt.congr_of_eventuallyEq_of_mem (h : HasGradientWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : HasGradientWithinAt fâ‚ f' s x := by
  apply h.congr_of_eventuallyEq hâ‚ (hâ‚.eq_of_nhdsWithin hx)

theorem HasGradientAt.congr_of_eventuallyEq (h : HasGradientAt f f' x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    HasGradientAt fâ‚ f' x := HasGradientAtFilter.congr_of_eventuallyEq h hâ‚ (mem_of_mem_nhds hâ‚ : _)

theorem Filter.EventuallyEq.gradient_eq (hL : fâ‚ =á¶ [ğ“ x] f) : âˆ‡ fâ‚ x = âˆ‡ f x := by
  unfold gradient
  rwa [Filter.EventuallyEq.fderiv_eq]

protected theorem Filter.EventuallyEq.gradient (h : fâ‚ =á¶ [ğ“ x] f) : âˆ‡ fâ‚ =á¶ [ğ“ x] âˆ‡ f :=
  h.eventuallyEq_nhds.mono fun _ h => h.gradient_eq

end congr

/-! ### The Gradient of constant functions -/

section Const

variable (c : ğ•œ) (s x L)

theorem hasGradientAtFilter_const : HasGradientAtFilter (fun _ => c) 0 x L := by
  rw [HasGradientAtFilter, map_zero]; apply hasFDerivAtFilter_const c x L

theorem hasGradientWithinAt_const : HasGradientWithinAt (fun _ => c) 0 s x :=
  hasGradientAtFilter_const _ _ _

theorem hasGradientAt_const : HasGradientAt (fun _ => c) 0 x := hasGradientAtFilter_const _ _ _

theorem gradient_const : âˆ‡ (fun _ => c) x = 0 := by
  rw [gradient, fderiv_const, Pi.zero_apply, map_zero]

@[simp]
theorem gradient_const' : (âˆ‡ fun _ : ğ•œ => c) = fun _ => 0 := funext fun x => gradient_const x c

end Const

section Continuous

/-! ### Continuity of a function admitting a gradient -/

nonrec theorem HasGradientAtFilter.tendsto_nhds (hL : L â‰¤ ğ“ x) (h : HasGradientAtFilter f f' x L) :
    Tendsto f L (ğ“ (f x)) := h.tendsto_nhds hL

theorem HasGradientWithinAt.continuousWithinAt (h : HasGradientWithinAt f f' s x) :
    ContinuousWithinAt f s x := by apply HasGradientAtFilter.tendsto_nhds inf_le_left h

theorem HasGradientAt.continuousAt (h : HasGradientAt f f' x) : ContinuousAt f x :=
  HasGradientAtFilter.tendsto_nhds le_rfl h

protected theorem HasGradientAt.continuousOn {f' : F â†’ F}
    (hderiv : âˆ€ x âˆˆ s, HasGradientAt f (f' x) x) :
  ContinuousOn f s := fun x hx => (hderiv x hx).continuousAt.continuousWithinAt

end Continuous