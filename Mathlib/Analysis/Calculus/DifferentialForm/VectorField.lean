/-
Copyright (c) 2025 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Calculus.DifferentialForm.Basic
import Mathlib.Analysis.Calculus.VectorField

/-!
# Theorems about evaluation of differential forms on vector fields

In this file we prove the following formula and its corollaries.
If `Ï‰` is a differentiable `k`-form and `V i` are `k + 1` differentiable vector fields, then

$$
  dÏ‰(V_0(x), \dots, V_n(x)) =
    \left(\sum_{i=0}^k (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_k(x))) +
      \sum_{0 \le i < j\le k} (-1)^{i + j}
        Ï‰(x; [V_i, V_j](x), V_0(x), â€¦, \widehat{V_i(x)}, â€¦, \widehat{V_j(x)}, â€¦, V_k(x)),
$$
where $$[V_i, V_j]$$ is the commutator of the vector fields $$V_i$$ and $$V_j$$.
As usual, $$\widehat{V_i(x)}$$ means that this item is removed from the sequence.

There is no convenient way to write the second term in Lean for `k = 0`,
so we only state this theorem for `k = n + 1`,
see `extDerivWithin_apply_vectorField` and `extDeriv_apply_vectorField`.

In this case, we write the second term as a sum over `i j : Fin (n + 1)`, `i â‰¤ j`,
where the indexes `(i, j)` in our sum currespond to `(i, j + 1)`
(formally, `(Fin.castSucc i, Fin.succ j)`) in the formula above.
For this reason, we have `-` before the sum in our formal statement.
-/

open Filter ContinuousAlternatingMap Finset VectorField
open scoped Topology

variable {ğ•œ E F : Type*}
  [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {n m k : â„•} {r : WithTop â„•âˆ}
  {s t : Set E} {x : E}

/--
If `Ï‰` is a differentiable `(n + 1)`-form and `V i` are `n + 2` differentiable vector fields, then

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))) -
      \sum_{0 \le i \le j\le n} (-1)^{i + j}
        Ï‰(x; [V_i, V_{j + 1}](x),
          V_0(x), â€¦, \widehat{V_i(x)}, â€¦, \widehat{V_{j + 1}(x)}, â€¦, V_k(x)),
$$

where $$[V_i, V_{j + 1}]$$ is the commutator of the vector fields $$V_i$$ and $$V_{j + 1}$$.
As usual, $$\widehat{V_i(x)}$$ means that this item is removed from the sequence.

In informal texts, this formula is usually written as

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))) +
      \sum_{0 \le i < j\le n + 1} (-1)^{i + j}
        Ï‰(x; [V_i, V_j](x),
          V_0(x), â€¦, \widehat{V_i(x)}, â€¦, \widehat{V_j(x)}, â€¦, V_k(x)).
$$

In the sum from our formalization,
each index `(i, j)` corresponds to the index `(Fin.castSucc i, Fin.succ j)`
in the sum used in informal texts.

For this reason, `i + j` in our some has the opposite parity compared to informal texts,
which changes the sign before the sum from `+` to `-`.
-/
theorem extDerivWithin_apply_vectorField
    {Ï‰ : E â†’ E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F} {V : Fin (n + 2) â†’ E â†’ E}
    (hÏ‰ : DifferentiableWithinAt ğ•œ Ï‰ s x) (hV : âˆ€ i, DifferentiableWithinAt ğ•œ (V i) s x)
    (hsx : UniqueDiffWithinAt ğ•œ s x) :
    extDerivWithin Ï‰ s x (V Â· x) =
      (âˆ‘ i, (-1) ^ i.val â€¢ fderivWithin ğ•œ (fun x â†¦ Ï‰ x (i.removeNth (V Â· x))) s x (V i x)) -
        âˆ‘ i : Fin (n + 1), âˆ‘ j â‰¥ i,
          (-1) ^ (i + j : â„•) â€¢
            Ï‰ x (Matrix.vecCons (lieBracketWithin ğ•œ (V i.castSucc) (V j.succ) s x)
              (j.removeNth <| i.castSucc.removeNth (V Â· x))) := by
  have Hâ‚€ (i : Fin (n + 2)) (j : Fin (n + 1)) :
      DifferentiableWithinAt ğ•œ (fun y â†¦ i.removeNth (V Â· y) j) s x := hV ..
  symm
  simp only [extDerivWithin_apply,
    fderivWithin_continuousAlternatingMap_apply_const_apply,
    fderivWithin_continuousAlternatingMap_apply_apply hÏ‰ (Hâ‚€ _) hsx, *,
    smul_add, sum_add_distrib, add_sub_assoc, add_eq_left, sub_eq_zero, smul_sum]
  rw [Fin.sum_sum_eq_sum_triangle_add]
  refine Fintype.sum_congr _ _ fun i â†¦ sum_congr rfl fun j hj â†¦ ?_
  rw [mem_Ici] at hj
  simp only [â† Fin.insertNth_removeNth, map_insertNth]
  rw [Fin.removeNth_removeNth_eq_swap]
  simp? [pow_add, lieBracketWithin, mul_smul, smul_comm ((-1) ^ (j : â„•)), smul_sub,
      â† sub_eq_add_neg] says
    simp only [Int.reduceNeg, Fin.coe_castSucc, Fin.val_succ, pow_add, pow_one, mul_neg, mul_one,
      neg_smul, smul_comm ((-1) ^ (j : â„•)), â† sub_eq_add_neg, lieBracketWithin,
      map_vecCons_sub, smul_sub, mul_smul]
  have Hâ‚ : i.castSucc.succAbove j = j.succ := by simp [Fin.succAbove_of_le_castSucc, hj]
  have Hâ‚‚ : j.predAbove i.castSucc = i := by simp [Fin.predAbove_of_le_castSucc, hj]
  have Hâ‚ƒ : j.succ.succAbove i = i.castSucc := by simp [Fin.succAbove_of_castSucc_lt, hj]
  simp +unfoldPartialApp [Hâ‚, Hâ‚‚, Hâ‚ƒ, Fin.removeNth]

/--
If `Ï‰` is a differentiable `(n + 1)`-form and `V i` are `n + 2` differentiable vector fields, then

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))) -
      \sum_{0 \le i \le j\le n} (-1)^{i + j}
        Ï‰(x; [V_i, V_{j + 1}](x),
          V_0(x), â€¦, \widehat{V_i(x)}, â€¦, \widehat{V_{j + 1}(x)}, â€¦, V_k(x)),
$$

where $$[V_i, V_{j + 1}]$$ is the commutator of the vector fields $$V_i$$ and $$V_{j + 1}$$.
As usual, $$\widehat{V_i(x)}$$ means that this item is removed from the sequence.

In informal texts, this formula is usually written as

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))) +
      \sum_{0 \le i < j\le n + 1} (-1)^{i + j}
        Ï‰(x; [V_i, V_j](x),
          V_0(x), â€¦, \widehat{V_i(x)}, â€¦, \widehat{V_j(x)}, â€¦, V_k(x)).
$$

In the sum from our formalization,
each index `(i, j)` corresponds to the index `(Fin.castSucc i, Fin.succ j)`
in the sum used in informal texts.

For this reason, `i + j` in our some has the opposite parity compared to informal texts,
which changes the sign before the sum from `+` to `-`.
-/
theorem extDeriv_apply_vectorField {Ï‰ : E â†’ E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F} {V : Fin (n + 2) â†’ E â†’ E}
    (hÏ‰ : DifferentiableAt ğ•œ Ï‰ x) (hV : âˆ€ i, DifferentiableAt ğ•œ (V i) x) :
    extDeriv Ï‰ x (V Â· x) =
      (âˆ‘ i, (-1) ^ i.val â€¢ fderiv ğ•œ (fun x â†¦ Ï‰ x (i.removeNth (V Â· x))) x (V i x)) -
        âˆ‘ i : Fin (n + 1), âˆ‘ j â‰¥ i,
          (-1) ^ (i + j : â„•) â€¢
            Ï‰ x (Matrix.vecCons (lieBracket ğ•œ (V i.castSucc) (V j.succ) x)
              (j.removeNth <| i.castSucc.removeNth (V Â· x))) := by
  simp only [â† differentiableWithinAt_univ, â† extDerivWithin_univ, â† fderivWithin_univ,
    â† lieBracketWithin_univ] at *
  exact extDerivWithin_apply_vectorField hÏ‰ hV (by simp)

/-- Let `Ï‰` be a differentiable `n`-form and `V i` be `n + 1` differentiable vector fields.
If `V i` pairwise commute at `x`, i.e., $$[V_i, V_j](x) = 0$$ for all `i â‰  j`, then

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))).
$$
-/
theorem extDerivWithin_apply_vectorField_of_pairwise_commute
    {Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F} {V : Fin (n + 1) â†’ E â†’ E}
    (hÏ‰ : DifferentiableWithinAt ğ•œ Ï‰ s x) (hV : âˆ€ i, DifferentiableWithinAt ğ•œ (V i) s x)
    (hsx : UniqueDiffWithinAt ğ•œ s x)
    (hcomm : Pairwise fun i j â†¦ lieBracketWithin ğ•œ (V i) (V j) s x = 0) :
    extDerivWithin Ï‰ s x (V Â· x) =
      (âˆ‘ i, (-1) ^ i.val â€¢ fderivWithin ğ•œ (fun x â†¦ Ï‰ x (i.removeNth (V Â· x))) s x (V i x)) := by
  cases n with
  | zero =>
    simp [extDerivWithin_apply, fderivWithin_continuousAlternatingMap_apply_const_apply,
      fderivWithin_continuousAlternatingMap_apply_apply, *]
  | succ n =>
    rw [extDerivWithin_apply_vectorField hÏ‰ hV hsx, sub_eq_self]
    refine Fintype.sum_eq_zero _ fun i â†¦ sum_eq_zero fun j hj â†¦ ?_
    rw [hcomm (ne_of_lt <| by simpa using hj), (Ï‰ x).map_coord_zero 0] <;>
      simp

/-- Let `Ï‰` be a differentiable `n`-form and `V i` be `n + 1` differentiable vector fields.
If `V i` pairwise commute at `x`, i.e., $$[V_i, V_j](x) = 0$$ for all `i â‰  j`, then

$$
  dÏ‰(V_0(x), \dots, V_{n + 1}(x)) =
    \left(\sum_{i=0}^{n + 1} (-1)^i â€¢ D_x(Ï‰(x; V_0(x), \dots, V_{n + 1}(x))).
$$
-/
theorem extDeriv_apply_vectorField_of_pairwise_commute
    {Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F} {V : Fin (n + 1) â†’ E â†’ E}
    (hÏ‰ : DifferentiableAt ğ•œ Ï‰ x) (hV : âˆ€ i, DifferentiableAt ğ•œ (V i) x)
    (hcomm : Pairwise fun i j â†¦ lieBracket ğ•œ (V i) (V j) x = 0) :
    extDeriv Ï‰ x (V Â· x) =
      (âˆ‘ i, (-1) ^ i.val â€¢ fderiv ğ•œ (fun x â†¦ Ï‰ x (i.removeNth (V Â· x))) x (V i x)) := by
  simp only [â† differentiableWithinAt_univ, â† lieBracketWithin_univ, â† extDerivWithin_univ,
    â† fderivWithin_univ] at *
  exact extDerivWithin_apply_vectorField_of_pairwise_commute hÏ‰ hV (by simp) hcomm
