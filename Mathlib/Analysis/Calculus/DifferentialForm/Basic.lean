/-
Copyright (c) 2025 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Sam Lindauer
-/
import Mathlib.Analysis.NormedSpace.Alternating.Uncurry.Fin
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Calculus.FDeriv.CompCLM

/-!
# Exterior derivative of a differential form on a normed space

In this file we define the exterior derivative of a differential form on a normed space.
Under certain smoothness assumptions, we prove that this operation is linear in the form
and the second exterior derivative of a form is zero.

We represent a differential `n`-form on `E` taking values in `F` as `E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F`.

## Implementation notes

There are a few competing definitions of the exterior derivative of a differential form
that differ from each other by a normalization factor.
We use the following one:

$$
dÏ‰(x; v_0, \dots, v_n) = \sum_{i=0}^n (-1)^i D_x Ï‰(x; v_0, \dots, \widehat{v_i}, \dots, v_n) Â· v_i
$$

where $$\widehat{v_i}$$ means that we omit this element of the tuple, see `extDeriv_apply`.

## TODO

- Introduce notation for:
  - an unbundled `n`-form on a normed space;
  - a bundled `C^r`-smooth `n`-form on a normed space;
  - same for manifolds (not defined yet).
- Introduce bundled `C^r`-smooth `n`-forms on normed spaces and manifolds.
  - Discuss the future API and the use cases that need to be covered on Zulip.
  - Introduce new types & notation, copy the API.
- Add shorter and more readable definitions (or abbreviations?)
  for `0`-forms (`constOfIsEmpty`) and `1`-forms (`ofSubsingleton`),
  sync with the API for `ContinuousMultilinearMap`.
-/

open Filter ContinuousAlternatingMap Set
open scoped Topology

variable {ğ•œ E F : Type*}
  [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {n m k : â„•} {r : WithTop â„•âˆ}
  {Ï‰ Ï‰â‚ Ï‰â‚‚ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F} {s t : Set E} {x : E}

/-- Exterior derivative of a differential form.

There are a few competing definitions of the exterior derivative of a differential form
that differ from each other by a normalization factor.
We use the following one:

$$
dÏ‰(x; v_0, \dots, v_n) = \sum_{i=0}^n (-1)^i D_x Ï‰(x; v_0, \dots, \widehat{v_i}, \dots, v_n) Â· v_i
$$

where $$\widehat{v_i}$$ means that we omit this element of the tuple, see `extDeriv_apply`.
-/
noncomputable def extDeriv (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (x : E) : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  .alternatizeUncurryFin (fderiv ğ•œ Ï‰ x)

/-- Exterior derivative of a differential form within a set.

There are a few competing definitions of the exterior derivative of a differential form
that differ from each other by a normalization factor.
We use the following one:

$$
dÏ‰(x; v_0, \dots, v_n) = \sum_{i=0}^n (-1)^i D_x Ï‰(x; v_0, \dots, \widehat{v_i}, \dots, v_n) Â· v_i
$$

where $$\widehat{v_i}$$ means that we omit this element of the tuple, see `extDerivWithin_apply`.
-/
noncomputable def extDerivWithin (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (s : Set E) (x : E) :
    E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  .alternatizeUncurryFin (fderivWithin ğ•œ Ï‰ s x)

@[simp]
theorem extDerivWithin_univ (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) :
    extDerivWithin Ï‰ univ = extDeriv Ï‰ := by
  ext1 x
  rw [extDerivWithin, extDeriv, fderivWithin_univ]

theorem extDerivWithin_add (hsx : UniqueDiffWithinAt ğ•œ s x)
    (hÏ‰â‚ : DifferentiableWithinAt ğ•œ Ï‰â‚ s x) (hÏ‰â‚‚ : DifferentiableWithinAt ğ•œ Ï‰â‚‚ s x) :
    extDerivWithin (Ï‰â‚ + Ï‰â‚‚) s x = extDerivWithin Ï‰â‚ s x + extDerivWithin Ï‰â‚‚ s x := by
  simp [extDerivWithin, fderivWithin_add hsx hÏ‰â‚ hÏ‰â‚‚, alternatizeUncurryFin_add]

theorem extDerivWithin_fun_add (hsx : UniqueDiffWithinAt ğ•œ s x)
    (hÏ‰â‚ : DifferentiableWithinAt ğ•œ Ï‰â‚ s x) (hÏ‰â‚‚ : DifferentiableWithinAt ğ•œ Ï‰â‚‚ s x) :
    extDerivWithin (fun x â†¦ Ï‰â‚ x + Ï‰â‚‚ x) s x = extDerivWithin Ï‰â‚ s x + extDerivWithin Ï‰â‚‚ s x :=
  extDerivWithin_add hsx hÏ‰â‚ hÏ‰â‚‚

theorem extDeriv_add (hÏ‰â‚ : DifferentiableAt ğ•œ Ï‰â‚ x) (hÏ‰â‚‚ : DifferentiableAt ğ•œ Ï‰â‚‚ x) :
    extDeriv (Ï‰â‚ + Ï‰â‚‚) x = extDeriv Ï‰â‚ x + extDeriv Ï‰â‚‚ x := by
  simp [â† extDerivWithin_univ, extDerivWithin_add, *, DifferentiableAt.differentiableWithinAt]

theorem extDeriv_fun_add (hÏ‰â‚ : DifferentiableAt ğ•œ Ï‰â‚ x) (hÏ‰â‚‚ : DifferentiableAt ğ•œ Ï‰â‚‚ x) :
    extDeriv (fun x â†¦ Ï‰â‚ x + Ï‰â‚‚ x) x = extDeriv Ï‰â‚ x + extDeriv Ï‰â‚‚ x :=
  extDeriv_add hÏ‰â‚ hÏ‰â‚‚

theorem extDerivWithin_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (hsx : UniqueDiffWithinAt ğ•œ s x) :
    extDerivWithin (c â€¢ Ï‰) s x = c â€¢ extDerivWithin Ï‰ s x := by
  simp [extDerivWithin, fderivWithin_const_smul_of_field, hsx, alternatizeUncurryFin_smul]

theorem extDerivWithin_fun_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F)
    (hsx : UniqueDiffWithinAt ğ•œ s x) :
    extDerivWithin (fun x â†¦ c â€¢ Ï‰ x) s x = c â€¢ extDerivWithin Ï‰ s x :=
  extDerivWithin_smul c Ï‰ hsx

theorem extDeriv_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) :
    extDeriv (c â€¢ Ï‰) x = c â€¢ extDeriv Ï‰ x := by
  simp [â† extDerivWithin_univ, extDerivWithin_smul]

theorem extDeriv_fun_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) :
    extDeriv (c â€¢ Ï‰) x = c â€¢ extDeriv Ï‰ x :=
  extDeriv_smul c Ï‰

/-- The exterior derivative of a `0`-form given by a function `f` within a set
is the 1-form given by the derivative of `f` within the set. -/
theorem extDerivWithin_constOfIsEmpty (f : E â†’ F) (hs : UniqueDiffWithinAt ğ•œ s x) :
    extDerivWithin (fun x â†¦ constOfIsEmpty ğ•œ E (Fin 0) (f x)) s x =
      .ofSubsingleton _ _ _ (0 : Fin 1) (fderivWithin ğ•œ f s x) := by
  simp only [extDerivWithin, â† constOfIsEmptyLIE_apply, â† Function.comp_def _ f,
    (constOfIsEmptyLIE ğ•œ E F (Fin 0)).comp_fderivWithin hs,
    alternatizeUncurryFin_constOfIsEmptyLIE_comp]

/-- The exterior derivative of a `0`-form given by a function `f`
is the 1-form given by the derivative of `f`. -/
theorem extDeriv_constOfIsEmpty (f : E â†’ F) (x : E) :
    extDeriv (fun x â†¦ constOfIsEmpty ğ•œ E (Fin 0) (f x)) x =
      .ofSubsingleton _ _ _ (0 : Fin 1) (fderiv ğ•œ f x) := by
  simp [â† extDerivWithin_univ, extDerivWithin_constOfIsEmpty, fderivWithin_univ]

theorem Filter.EventuallyEq.extDerivWithin_eq (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (hx : Ï‰â‚ x = Ï‰â‚‚ x) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x := by
  simp only [extDerivWithin, alternatizeUncurryFin, hs.fderivWithin_eq hx]

theorem Filter.EventuallyEq.extDerivWithin_eq_of_mem (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (hx : x âˆˆ s) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x :=
  hs.extDerivWithin_eq (mem_of_mem_nhdsWithin hx hs :)

theorem Filter.EventuallyEq.extDerivWithin_eq_of_insert (hs : Ï‰â‚ =á¶ [ğ“[insert x s] x] Ï‰â‚‚) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x := by
  apply Filter.EventuallyEq.extDerivWithin_eq (nhdsWithin_mono _ (subset_insert x s) hs)
  exact (mem_of_mem_nhdsWithin (mem_insert x s) hs :)

theorem Filter.EventuallyEq.extDerivWithin' (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (ht : t âŠ† s) :
    extDerivWithin Ï‰â‚ t =á¶ [ğ“[s] x] extDerivWithin Ï‰â‚‚ t :=
  (eventually_eventually_nhdsWithin.2 hs).mp <| eventually_mem_nhdsWithin.mono fun _y hys hs =>
    EventuallyEq.extDerivWithin_eq (hs.filter_mono <| nhdsWithin_mono _ ht)
        (hs.self_of_nhdsWithin hys)

protected theorem Filter.EventuallyEq.extDerivWithin (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) :
    extDerivWithin Ï‰â‚ s =á¶ [ğ“[s] x] extDerivWithin Ï‰â‚‚ s :=
  hs.extDerivWithin' .rfl

theorem Filter.EventuallyEq.extDerivWithin_eq_nhds (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x :=
  (h.filter_mono nhdsWithin_le_nhds).extDerivWithin_eq h.self_of_nhds

theorem extDerivWithin_congr (hs : EqOn Ï‰â‚ Ï‰â‚‚ s) (hx : Ï‰â‚ x = Ï‰â‚‚ x) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x :=
  (hs.eventuallyEq.filter_mono inf_le_right).extDerivWithin_eq hx

theorem extDerivWithin_congr' (hs : EqOn Ï‰â‚ Ï‰â‚‚ s) (hx : x âˆˆ s) :
    extDerivWithin Ï‰â‚ s x = extDerivWithin Ï‰â‚‚ s x :=
  extDerivWithin_congr hs (hs hx)

protected theorem Filter.EventuallyEq.extDeriv (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) :
    extDeriv Ï‰â‚ =á¶ [ğ“ x] extDeriv Ï‰â‚‚ := by
  simp only [â† nhdsWithin_univ, â† extDerivWithin_univ] at *
  exact h.extDerivWithin

theorem Filter.EventuallyEq.extDeriv_eq (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) : extDeriv Ï‰â‚ x = extDeriv Ï‰â‚‚ x :=
  h.extDeriv.self_of_nhds

theorem extDerivWithin_apply (h : DifferentiableWithinAt ğ•œ Ï‰ s x) (hs : UniqueDiffWithinAt ğ•œ s x)
    (v : Fin (n + 1) â†’ E) :
    extDerivWithin Ï‰ s x v =
      âˆ‘ i, (-1) ^ i.val â€¢ fderivWithin ğ•œ (Ï‰ Â· (i.removeNth v)) s x (v i) := by
  simp [extDerivWithin, ContinuousAlternatingMap.alternatizeUncurryFin_apply,
    fderivWithin_continuousAlternatingMap_apply_const_apply, *]

theorem extDeriv_apply (h : DifferentiableAt ğ•œ Ï‰ x) (v : Fin (n + 1) â†’ E) :
    extDeriv Ï‰ x v = âˆ‘ i, (-1) ^ i.val â€¢ fderiv ğ•œ (Ï‰ Â· (i.removeNth v)) x (v i) := by
  simp [â† extDerivWithin_univ, extDerivWithin_apply h.differentiableWithinAt]

/-- The second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem extDerivWithin_extDerivWithin_apply (hÏ‰ : ContDiffWithinAt ğ•œ r Ï‰ s x)
    (hr : minSmoothness ğ•œ 2 â‰¤ r) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ closure (interior s))
    (h'x : x âˆˆ s) : extDerivWithin (extDerivWithin Ï‰ s) s x = 0 := calc
  extDerivWithin (extDerivWithin Ï‰ s) s x
    = alternatizeUncurryFin (fderivWithin ğ•œ (fun y â†¦
        alternatizeUncurryFin (fderivWithin ğ•œ Ï‰ s y)) s x) := rfl
  _ = alternatizeUncurryFin (alternatizeUncurryFinCLM _ _ _ âˆ˜L
        fderivWithin ğ•œ (fderivWithin ğ•œ Ï‰ s) s x) := by
    congr 1
    have : DifferentiableWithinAt ğ•œ (fderivWithin ğ•œ Ï‰ s) s x := by
      refine (hÏ‰.fderivWithin_right hs ?_ h'x).differentiableWithinAt le_rfl
      exact le_minSmoothness.trans hr
    exact alternatizeUncurryFinCLM _ _ _ |>.hasFDerivAt.comp_hasFDerivWithinAt x
      this.hasFDerivWithinAt |>.fderivWithin (hs.uniqueDiffWithinAt h'x)
  _ = 0 := alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_of_symmetric <|
    hÏ‰.isSymmSndFDerivWithinAt hr hs hx h'x

/-- The second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem extDerivWithin_extDerivWithin_eqOn (hÏ‰ : ContDiffOn ğ•œ r Ï‰ s) (hr : minSmoothness ğ•œ 2 â‰¤ r)
    (hs : UniqueDiffOn ğ•œ s) :
    EqOn (extDerivWithin (extDerivWithin Ï‰ s) s) 0 (s âˆ© closure (interior s)) := by
  rintro x âŸ¨h'x, hxâŸ©
  exact extDerivWithin_extDerivWithin_apply (hÏ‰.contDiffWithinAt h'x) hr hs hx h'x

/-- The second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem extDeriv_extDeriv_apply (hÏ‰ : ContDiffAt ğ•œ r Ï‰ x) (hr : minSmoothness ğ•œ 2 â‰¤ r) :
    extDeriv (extDeriv Ï‰) x = 0 := by
  simp only [â† extDerivWithin_univ]
  apply extDerivWithin_extDerivWithin_apply (s := univ) hÏ‰.contDiffWithinAt hr <;> simp

/-- The second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem extDeriv_extDeriv (h : ContDiff ğ•œ r Ï‰) (hr : minSmoothness ğ•œ 2 â‰¤ r) :
    extDeriv (extDeriv Ï‰) = 0 :=
  funext fun _ â†¦ extDeriv_extDeriv_apply h.contDiffAt hr
