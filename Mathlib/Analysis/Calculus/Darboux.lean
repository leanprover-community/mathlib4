/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.LocalExtr.Basic

#align_import analysis.calculus.darboux from "leanprover-community/mathlib"@"61b5e2755ccb464b68d05a9acf891ae04992d09d"

/-!
# Darboux's theorem

In this file we prove that the derivative of a differentiable function on an interval takes all
intermediate values. The proof is based on the
[Wikipedia](https://en.wikipedia.org/wiki/Darboux%27s_theorem_(analysis)) page about this theorem.
-/

open Filter Set

open scoped Topology Classical

variable {a b : ‚Ñù} {f f' : ‚Ñù ‚Üí ‚Ñù}

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' a < m < f' b`, then `f' c = m` for some
`c ‚àà (a, b)`. -/
theorem exists_hasDerivWithinAt_eq_of_gt_of_lt (hab : a ‚â§ b)
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x) {m : ‚Ñù} (hma : f' a < m)
    (hmb : m < f' b) : m ‚àà f' '' Ioo a b := by
  rcases hab.eq_or_lt with (rfl | hab')
  ¬∑ exact (lt_asymm hma hmb).elim
  set g : ‚Ñù ‚Üí ‚Ñù := fun x => f x - m * x
  have hg : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt g (f' x - m) (Icc a b) x := by
    intro x hx
    simpa using (hf x hx).sub ((hasDerivWithinAt_id x _).const_mul m)
  obtain ‚ü®c, cmem, hc‚ü© : ‚àÉ c ‚àà Icc a b, IsMinOn g (Icc a b) c :=
    isCompact_Icc.exists_isMinOn (nonempty_Icc.2 <| hab) fun x hx => (hg x hx).continuousWithinAt
  have cmem' : c ‚àà Ioo a b := by
    rcases cmem.1.eq_or_lt with (rfl | hac)
    -- Show that `c` can't be equal to `a`
    ¬∑ refine absurd (sub_nonneg.1 <| nonneg_of_mul_nonneg_right ?_ (sub_pos.2 hab'))
        (not_le_of_lt hma)
      have : b - a ‚àà posTangentConeAt (Icc a b) a :=
        mem_posTangentConeAt_of_segment_subset (segment_eq_Icc hab ‚ñ∏ Subset.refl _)
      simpa only [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply]
        using hc.localize.hasFDerivWithinAt_nonneg (hg a (left_mem_Icc.2 hab)) this
    rcases cmem.2.eq_or_gt with (rfl | hcb)
    -- Show that `c` can't be equal to `b`
    ¬∑ refine absurd (sub_nonpos.1 <| nonpos_of_mul_nonneg_right ?_ (sub_lt_zero.2 hab'))
        (not_le_of_lt hmb)
      have : a - b ‚àà posTangentConeAt (Icc a b) b :=
        mem_posTangentConeAt_of_segment_subset (by rw [segment_symm, segment_eq_Icc hab])
      simpa only [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply]
        using hc.localize.hasFDerivWithinAt_nonneg (hg b (right_mem_Icc.2 hab)) this
    exact ‚ü®hac, hcb‚ü©
  use c, cmem'
  rw [‚Üê sub_eq_zero]
  have : Icc a b ‚àà ùìù c := by rwa [‚Üê mem_interior_iff_mem_nhds, interior_Icc]
  exact (hc.isLocalMin this).hasDerivAt_eq_zero ((hg c cmem).hasDerivAt this)
#align exists_has_deriv_within_at_eq_of_gt_of_lt exists_hasDerivWithinAt_eq_of_gt_of_lt

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' b < m < f' a`, then `f' c = m` for some `c ‚àà (a, b)`.
-/
theorem exists_hasDerivWithinAt_eq_of_lt_of_gt (hab : a ‚â§ b)
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x) {m : ‚Ñù} (hma : m < f' a)
    (hmb : f' b < m) : m ‚àà f' '' Ioo a b :=
  let ‚ü®c, cmem, hc‚ü© :=
    exists_hasDerivWithinAt_eq_of_gt_of_lt hab (fun x hx => (hf x hx).neg) (neg_lt_neg hma)
      (neg_lt_neg hmb)
  ‚ü®c, cmem, neg_injective hc‚ü©
#align exists_has_deriv_within_at_eq_of_lt_of_gt exists_hasDerivWithinAt_eq_of_lt_of_gt

/-- **Darboux's theorem**: the image of a `Set.OrdConnected` set under `f'` is a `Set.OrdConnected`
set, `HasDerivWithinAt` version. -/
theorem Set.OrdConnected.image_hasDerivWithinAt {s : Set ‚Ñù} (hs : OrdConnected s)
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) : OrdConnected (f' '' s) := by
  apply ordConnected_of_Ioo
  rintro _ ‚ü®a, ha, rfl‚ü© _ ‚ü®b, hb, rfl‚ü© - m ‚ü®hma, hmb‚ü©
  rcases le_total a b with hab | hab
  ¬∑ have : Icc a b ‚äÜ s := hs.out ha hb
    rcases exists_hasDerivWithinAt_eq_of_gt_of_lt hab (fun x hx => (hf x <| this hx).mono this) hma
        hmb with
      ‚ü®c, cmem, hc‚ü©
    exact ‚ü®c, this <| Ioo_subset_Icc_self cmem, hc‚ü©
  ¬∑ have : Icc b a ‚äÜ s := hs.out hb ha
    rcases exists_hasDerivWithinAt_eq_of_lt_of_gt hab (fun x hx => (hf x <| this hx).mono this) hmb
        hma with
      ‚ü®c, cmem, hc‚ü©
    exact ‚ü®c, this <| Ioo_subset_Icc_self cmem, hc‚ü©
#align set.ord_connected.image_has_deriv_within_at Set.OrdConnected.image_hasDerivWithinAt

/-- **Darboux's theorem**: the image of a `Set.OrdConnected` set under `f'` is a `Set.OrdConnected`
set, `derivWithin` version. -/
theorem Set.OrdConnected.image_derivWithin {s : Set ‚Ñù} (hs : OrdConnected s)
    (hf : DifferentiableOn ‚Ñù f s) : OrdConnected (derivWithin f s '' s) :=
  hs.image_hasDerivWithinAt fun x hx => (hf x hx).hasDerivWithinAt
#align set.ord_connected.image_deriv_within Set.OrdConnected.image_derivWithin

/-- **Darboux's theorem**: the image of a `Set.OrdConnected` set under `f'` is a `Set.OrdConnected`
set, `deriv` version. -/
theorem Set.OrdConnected.image_deriv {s : Set ‚Ñù} (hs : OrdConnected s)
    (hf : ‚àÄ x ‚àà s, DifferentiableAt ‚Ñù f x) : OrdConnected (deriv f '' s) :=
  hs.image_hasDerivWithinAt fun x hx => (hf x hx).hasDerivAt.hasDerivWithinAt
#align set.ord_connected.image_deriv Set.OrdConnected.image_deriv

/-- **Darboux's theorem**: the image of a convex set under `f'` is a convex set,
`HasDerivWithinAt` version. -/
theorem Convex.image_hasDerivWithinAt {s : Set ‚Ñù} (hs : Convex ‚Ñù s)
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) : Convex ‚Ñù (f' '' s) :=
  (hs.ordConnected.image_hasDerivWithinAt hf).convex
#align convex.image_has_deriv_within_at Convex.image_hasDerivWithinAt

/-- **Darboux's theorem**: the image of a convex set under `f'` is a convex set,
`derivWithin` version. -/
theorem Convex.image_derivWithin {s : Set ‚Ñù} (hs : Convex ‚Ñù s) (hf : DifferentiableOn ‚Ñù f s) :
    Convex ‚Ñù (derivWithin f s '' s) :=
  (hs.ordConnected.image_derivWithin hf).convex
#align convex.image_deriv_within Convex.image_derivWithin

/-- **Darboux's theorem**: the image of a convex set under `f'` is a convex set,
`deriv` version. -/
theorem Convex.image_deriv {s : Set ‚Ñù} (hs : Convex ‚Ñù s) (hf : ‚àÄ x ‚àà s, DifferentiableAt ‚Ñù f x) :
    Convex ‚Ñù (deriv f '' s) :=
  (hs.ordConnected.image_deriv hf).convex
#align convex.image_deriv Convex.image_deriv

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' a ‚â§ m ‚â§ f' b`, then `f' c = m` for some
`c ‚àà [a, b]`. -/
theorem exists_hasDerivWithinAt_eq_of_ge_of_le (hab : a ‚â§ b)
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x) {m : ‚Ñù} (hma : f' a ‚â§ m)
    (hmb : m ‚â§ f' b) : m ‚àà f' '' Icc a b :=
  (ordConnected_Icc.image_hasDerivWithinAt hf).out (mem_image_of_mem _ (left_mem_Icc.2 hab))
    (mem_image_of_mem _ (right_mem_Icc.2 hab)) ‚ü®hma, hmb‚ü©
#align exists_has_deriv_within_at_eq_of_ge_of_le exists_hasDerivWithinAt_eq_of_ge_of_le

/-- **Darboux's theorem**: if `a ‚â§ b` and `f' b ‚â§ m ‚â§ f' a`, then `f' c = m` for some
`c ‚àà [a, b]`. -/
theorem exists_hasDerivWithinAt_eq_of_le_of_ge (hab : a ‚â§ b)
    (hf : ‚àÄ x ‚àà Icc a b, HasDerivWithinAt f (f' x) (Icc a b) x) {m : ‚Ñù} (hma : f' a ‚â§ m)
    (hmb : m ‚â§ f' b) : m ‚àà f' '' Icc a b :=
  (ordConnected_Icc.image_hasDerivWithinAt hf).out (mem_image_of_mem _ (left_mem_Icc.2 hab))
    (mem_image_of_mem _ (right_mem_Icc.2 hab)) ‚ü®hma, hmb‚ü©
#align exists_has_deriv_within_at_eq_of_le_of_ge exists_hasDerivWithinAt_eq_of_le_of_ge

/-- If the derivative of a function is never equal to `m`, then either
it is always greater than `m`, or it is always less than `m`. -/
theorem hasDerivWithinAt_forall_lt_or_forall_gt_of_forall_ne {s : Set ‚Ñù} (hs : Convex ‚Ñù s)
    (hf : ‚àÄ x ‚àà s, HasDerivWithinAt f (f' x) s x) {m : ‚Ñù} (hf' : ‚àÄ x ‚àà s, f' x ‚â† m) :
    (‚àÄ x ‚àà s, f' x < m) ‚à® ‚àÄ x ‚àà s, m < f' x := by
  contrapose! hf'
  rcases hf' with ‚ü®‚ü®b, hb, hmb‚ü©, ‚ü®a, ha, hma‚ü©‚ü©
  exact (hs.ordConnected.image_hasDerivWithinAt hf).out (mem_image_of_mem f' ha)
    (mem_image_of_mem f' hb) ‚ü®hma, hmb‚ü©
#align has_deriv_within_at_forall_lt_or_forall_gt_of_forall_ne hasDerivWithinAt_forall_lt_or_forall_gt_of_forall_ne
