/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.ApproximatesLinearOn

/-!
# Inverse function theorem

In this file we prove the inverse function theorem. It says that if a map `f : E â†’ F`
has an invertible strict derivative `f'` at `a`, then it is locally invertible,
and the inverse function has derivative `f' â»Â¹`.

We define `HasStrictFDerivAt.toPartialHomeomorph` that repacks a function `f`
with a `hf : HasStrictFDerivAt f f' a`, `f' : E â‰ƒL[ğ•œ] F`, into a `PartialHomeomorph`.
The `toFun` of this `PartialHomeomorph` is defeq to `f`, so one can apply theorems
about `PartialHomeomorph` to `hf.toPartialHomeomorph f`, and get statements about `f`.

Then we define `HasStrictFDerivAt.localInverse` to be the `invFun` of this `PartialHomeomorph`,
and prove two versions of the inverse function theorem:

* `HasStrictFDerivAt.to_localInverse`: if `f` has an invertible derivative `f'` at `a` in the
  strict sense (`hf`), then `hf.localInverse f f' a` has derivative `f'.symm` at `f a` in the
  strict sense;

* `HasStrictFDerivAt.to_local_left_inverse`: if `f` has an invertible derivative `f'` at `a` in
  the strict sense and `g` is locally left inverse to `f` near `a`, then `g` has derivative
  `f'.symm` at `f a` in the strict sense.

Some related theorems, providing the derivative and higher regularity assuming that we already know
the inverse function, are formulated in the `Analysis/Calculus/FDeriv` and `Analysis/Calculus/Deriv`
folders, and in `ContDiff.lean`.

## Tags

derivative, strictly differentiable, continuously differentiable, smooth, inverse function
-/

open Function Set Filter Metric

open scoped Topology NNReal

noncomputable section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

open Asymptotics Filter Metric Set

open ContinuousLinearMap (id)


/-!
### Inverse function theorem

Let `f : E â†’ F` be a map defined on a complete vector
space `E`. Assume that `f` has an invertible derivative `f' : E â‰ƒL[ğ•œ] F` at `a : E` in the strict
sense. Then `f` approximates `f'` in the sense of `ApproximatesLinearOn` on an open neighborhood
of `a`, and we can apply `ApproximatesLinearOn.toPartialHomeomorph` to construct the inverse
function. -/

namespace HasStrictFDerivAt

/-- If `f` has derivative `f'` at `a` in the strict sense and `c > 0`, then `f` approximates `f'`
with constant `c` on some neighborhood of `a`. -/
theorem approximates_deriv_on_nhds {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {a : E}
    (hf : HasStrictFDerivAt f f' a) {c : â„â‰¥0} (hc : Subsingleton E âˆ¨ 0 < c) :
    âˆƒ s âˆˆ ğ“ a, ApproximatesLinearOn f f' s c := by
  rcases hc with hE | hc
  Â· refine âŸ¨univ, IsOpen.mem_nhds isOpen_univ trivial, fun x _ y _ => ?_âŸ©
    simp [@Subsingleton.elim E hE x y]
  have := hf.isLittleO.def hc
  rw [nhds_prod_eq, Filter.Eventually, mem_prod_same_iff] at this
  rcases this with âŸ¨s, has, hsâŸ©
  exact âŸ¨s, has, fun x hx y hy => hs (mk_mem_prod hx hy)âŸ©

theorem map_nhds_eq_of_surj [CompleteSpace E] [CompleteSpace F] {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {a : E}
    (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) (h : LinearMap.range f' = âŠ¤) :
    map f (ğ“ a) = ğ“ (f a) := by
  let f'symm := f'.nonlinearRightInverseOfSurjective h
  set c : â„â‰¥0 := f'symm.nnnormâ»Â¹ / 2 with hc
  have f'symm_pos : 0 < f'symm.nnnorm := f'.nonlinearRightInverseOfSurjective_nnnorm_pos h
  have cpos : 0 < c := by simp [hc, half_pos, inv_pos, f'symm_pos]
  obtain âŸ¨s, s_nhds, hsâŸ© : âˆƒ s âˆˆ ğ“ a, ApproximatesLinearOn f f' s c :=
    hf.approximates_deriv_on_nhds (Or.inr cpos)
  apply hs.map_nhds_eq f'symm s_nhds (Or.inr (NNReal.half_lt_self _))
  simp [ne_of_gt f'symm_pos]

variable {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {a : E}

theorem approximates_deriv_on_open_nhds (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    âˆƒ s : Set E, a âˆˆ s âˆ§ IsOpen s âˆ§
      ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s (â€–(f'.symm : F â†’L[ğ•œ] E)â€–â‚Šâ»Â¹ / 2) := by
  simp only [â† and_assoc]
  refine ((nhds_basis_opens a).exists_iff fun s t => ApproximatesLinearOn.mono_set).1 ?_
  exact
    hf.approximates_deriv_on_nhds <|
      f'.subsingleton_or_nnnorm_symm_pos.imp id fun hf' => half_pos <| inv_pos.2 hf'

variable (f)
variable [CompleteSpace E]

/-- Given a function with an invertible strict derivative at `a`, returns a `PartialHomeomorph`
with `to_fun = f` and `a âˆˆ source`. This is a part of the inverse function theorem.
The other part `HasStrictFDerivAt.to_localInverse` states that the inverse function
of this `PartialHomeomorph` has derivative `f'.symm`. -/
def toPartialHomeomorph (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) : PartialHomeomorph E F :=
  ApproximatesLinearOn.toPartialHomeomorph f (Classical.choose hf.approximates_deriv_on_open_nhds)
    (Classical.choose_spec hf.approximates_deriv_on_open_nhds).2.2
    (f'.subsingleton_or_nnnorm_symm_pos.imp id fun hf' =>
      NNReal.half_lt_self <| ne_of_gt <| inv_pos.2 hf')
    (Classical.choose_spec hf.approximates_deriv_on_open_nhds).2.1

variable {f}

@[simp]
theorem toPartialHomeomorph_coe (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    (hf.toPartialHomeomorph f : E â†’ F) = f :=
  rfl

theorem mem_toPartialHomeomorph_source (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    a âˆˆ (hf.toPartialHomeomorph f).source :=
  (Classical.choose_spec hf.approximates_deriv_on_open_nhds).1

theorem image_mem_toPartialHomeomorph_target (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    f a âˆˆ (hf.toPartialHomeomorph f).target :=
  (hf.toPartialHomeomorph f).map_source hf.mem_toPartialHomeomorph_source

theorem map_nhds_eq_of_equiv (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    map f (ğ“ a) = ğ“ (f a) :=
  (hf.toPartialHomeomorph f).map_nhds_eq hf.mem_toPartialHomeomorph_source

variable (f f' a)

/-- Given a function `f` with an invertible derivative, returns a function that is locally inverse
to `f`. -/
def localInverse (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) : F â†’ E :=
  (hf.toPartialHomeomorph f).symm

variable {f f' a}

theorem localInverse_def (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    hf.localInverse f _ _ = (hf.toPartialHomeomorph f).symm :=
  rfl

theorem eventually_left_inverse (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    âˆ€á¶  x in ğ“ a, hf.localInverse f f' a (f x) = x :=
  (hf.toPartialHomeomorph f).eventually_left_inverse hf.mem_toPartialHomeomorph_source

@[simp]
theorem localInverse_apply_image (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    hf.localInverse f f' a (f a) = a :=
  hf.eventually_left_inverse.self_of_nhds

theorem eventually_right_inverse (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    âˆ€á¶  y in ğ“ (f a), f (hf.localInverse f f' a y) = y :=
  (hf.toPartialHomeomorph f).eventually_right_inverse' hf.mem_toPartialHomeomorph_source

theorem localInverse_continuousAt (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    ContinuousAt (hf.localInverse f f' a) (f a) :=
  (hf.toPartialHomeomorph f).continuousAt_symm hf.image_mem_toPartialHomeomorph_target

theorem localInverse_tendsto (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    Tendsto (hf.localInverse f f' a) (ğ“ <| f a) (ğ“ a) :=
  (hf.toPartialHomeomorph f).tendsto_symm hf.mem_toPartialHomeomorph_source

theorem localInverse_unique (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) {g : F â†’ E}
    (hg : âˆ€á¶  x in ğ“ a, g (f x) = x) : âˆ€á¶  y in ğ“ (f a), g y = localInverse f f' a hf y :=
  eventuallyEq_of_left_inv_of_right_inv hg hf.eventually_right_inverse <|
    (hf.toPartialHomeomorph f).tendsto_symm hf.mem_toPartialHomeomorph_source

/-- If `f` has an invertible derivative `f'` at `a` in the sense of strict differentiability `(hf)`,
then the inverse function `hf.localInverse f` has derivative `f'.symm` at `f a`. -/
theorem to_localInverse (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) :
    HasStrictFDerivAt (hf.localInverse f f' a) (f'.symm : F â†’L[ğ•œ] E) (f a) :=
  (hf.toPartialHomeomorph f).hasStrictFDerivAt_symm hf.image_mem_toPartialHomeomorph_target <| by
    simpa [â† localInverse_def] using hf

/-- If `f : E â†’ F` has an invertible derivative `f'` at `a` in the sense of strict differentiability
and `g (f x) = x` in a neighborhood of `a`, then `g` has derivative `f'.symm` at `f a`.

For a version assuming `f (g y) = y` and continuity of `g` at `f a` but not `[CompleteSpace E]`
see `of_local_left_inverse`. -/
theorem to_local_left_inverse (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) a) {g : F â†’ E}
    (hg : âˆ€á¶  x in ğ“ a, g (f x) = x) : HasStrictFDerivAt g (f'.symm : F â†’L[ğ•œ] E) (f a) :=
  hf.to_localInverse.congr_of_eventuallyEq <| (hf.localInverse_unique hg).mono fun _ => Eq.symm

end HasStrictFDerivAt

/-- If a function has an invertible strict derivative at all points, then it is an open map. -/
theorem isOpenMap_of_hasStrictFDerivAt_equiv [CompleteSpace E] {f : E â†’ F} {f' : E â†’ E â‰ƒL[ğ•œ] F}
    (hf : âˆ€ x, HasStrictFDerivAt f (f' x : E â†’L[ğ•œ] F) x) : IsOpenMap f :=
  isOpenMap_iff_nhds_le.2 fun x => (hf x).map_nhds_eq_of_equiv.ge
