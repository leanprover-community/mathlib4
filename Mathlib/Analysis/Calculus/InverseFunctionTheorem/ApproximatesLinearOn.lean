/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Normed.Operator.Banach
import Mathlib.Analysis.Normed.Operator.NormedSpace
import Mathlib.Topology.OpenPartialHomeomorph

/-!
# Non-linear maps close to affine maps

In this file we study a map `f` such that `â€–f x - f y - f' (x - y)â€– â‰¤ c * â€–x - yâ€–` on an open set
`s`, where `f' : E â†’L[ğ•œ] F` is a continuous linear map and `c` is suitably small. Maps of this type
behave like `f a + f' (x - a)` near each `a âˆˆ s`.

When `f'` is onto, we show that `f` is locally onto.

When `f'` is a continuous linear equiv, we show that `f` is a homeomorphism
between `s` and `f '' s`. More precisely, we define `ApproximatesLinearOn.toOpenPartialHomeomorph`
to be an `OpenPartialHomeomorph` with `toFun = f`, `source = s`, and `target = f '' s`.
between `s` and `f '' s`. More precisely, we define `ApproximatesLinearOn.toOpenPartialHomeomorph`
to be an `OpenPartialHomeomorph` with `toFun = f`, `source = s`, and `target = f '' s`.

Maps of this type naturally appear in the proof of the inverse function theorem (see next section),
and `ApproximatesLinearOn.toOpenPartialHomeomorph` will imply that the locally inverse function
and `ApproximatesLinearOn.toOpenPartialHomeomorph` will imply that the locally inverse function
exists.

We define this auxiliary notion to split the proof of the inverse function theorem into small
lemmas. This approach makes it possible

- to prove a lower estimate on the size of the domain of the inverse function;

- to reuse parts of the proofs in the case if a function is not strictly differentiable. E.g., for a
  function `f : E Ã— F â†’ G` with estimates on `f x yâ‚ - f x yâ‚‚` but not on `f xâ‚ y - f xâ‚‚ y`.

## Notation

We introduce some `local notation` to make formulas shorter:

* by `N` we denote `â€–f'â»Â¹â€–`;
* by `g` we denote the auxiliary contracting map `x â†¦ x + f'.symm (y - f x)` used to prove that
  `{x | f x = y}` is nonempty.
-/

open Function Set Filter Metric

open scoped Topology NNReal

noncomputable section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {Îµ : â„}

open Filter Metric Set

open ContinuousLinearMap (id)

/-- We say that `f` approximates a continuous linear map `f'` on `s` with constant `c`,
if `â€–f x - f y - f' (x - y)â€– â‰¤ c * â€–x - yâ€–` whenever `x, y âˆˆ s`.

This predicate is defined to facilitate the splitting of the inverse function theorem into small
lemmas. Some of these lemmas can be useful, e.g., to prove that the inverse function is defined
on a specific set. -/
def ApproximatesLinearOn (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (s : Set E) (c : â„â‰¥0) : Prop :=
  âˆ€ x âˆˆ s, âˆ€ y âˆˆ s, â€–f x - f y - f' (x - y)â€– â‰¤ c * â€–x - yâ€–

@[simp]
theorem approximatesLinearOn_empty (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (c : â„â‰¥0) :
    ApproximatesLinearOn f f' âˆ… c := by simp [ApproximatesLinearOn]

namespace ApproximatesLinearOn

variable {f : E â†’ F}

/-! First we prove some properties of a function that `ApproximatesLinearOn` a (not necessarily
invertible) continuous linear map. -/


section

variable {f' : E â†’L[ğ•œ] F} {s t : Set E} {c c' : â„â‰¥0}

theorem mono_num (hc : c â‰¤ c') (hf : ApproximatesLinearOn f f' s c) :
    ApproximatesLinearOn f f' s c' := fun x hx y hy =>
  le_trans (hf x hx y hy) (mul_le_mul_of_nonneg_right hc <| norm_nonneg _)

theorem mono_set (hst : s âŠ† t) (hf : ApproximatesLinearOn f f' t c) :
    ApproximatesLinearOn f f' s c := fun x hx y hy => hf x (hst hx) y (hst hy)

theorem approximatesLinearOn_iff_lipschitzOnWith {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {s : Set E}
    {c : â„â‰¥0} : ApproximatesLinearOn f f' s c â†” LipschitzOnWith c (f - â‡‘f') s := by
  have : âˆ€ x y, f x - f y - f' (x - y) = (f - f') x - (f - f') y := fun x y â†¦ by
    simp only [map_sub, Pi.sub_apply]; abel
  simp only [this, lipschitzOnWith_iff_norm_sub_le, ApproximatesLinearOn]

alias âŸ¨lipschitzOnWith, _root_.LipschitzOnWith.approximatesLinearOnâŸ© :=
  approximatesLinearOn_iff_lipschitzOnWith

theorem lipschitz_sub (hf : ApproximatesLinearOn f f' s c) :
    LipschitzWith c fun x : s => f x - f' x :=
  hf.lipschitzOnWith.to_restrict

protected theorem lipschitz (hf : ApproximatesLinearOn f f' s c) :
    LipschitzWith (â€–f'â€–â‚Š + c) (s.restrict f) := by
  simpa only [restrict_apply, add_sub_cancel] using
    (f'.lipschitz.restrict s).add hf.lipschitz_sub

protected theorem continuous (hf : ApproximatesLinearOn f f' s c) : Continuous (s.restrict f) :=
  hf.lipschitz.continuous

protected theorem continuousOn (hf : ApproximatesLinearOn f f' s c) : ContinuousOn f s :=
  continuousOn_iff_continuous_restrict.2 hf.continuous

end

section LocallyOnto

/-!
We prove that a function which is linearly approximated by a continuous linear map with a nonlinear
right inverse is locally onto. This will apply to the case where the approximating map is a linear
equivalence, for the local inverse theorem, but also whenever the approximating map is onto,
by Banach's open mapping theorem. -/


variable [CompleteSpace E] {s : Set E} {c : â„â‰¥0} {f' : E â†’L[ğ•œ] F}

/-- If a function is linearly approximated by a continuous linear map with a (possibly nonlinear)
right inverse, then it is locally onto: a ball of an explicit radius is included in the image
of the map. -/
theorem surjOn_closedBall_of_nonlinearRightInverse
    (hf : ApproximatesLinearOn f f' s c)
    (f'symm : f'.NonlinearRightInverse) {Îµ : â„} {b : E} (Îµ0 : 0 â‰¤ Îµ) (hÎµ : closedBall b Îµ âŠ† s) :
    SurjOn f (closedBall b Îµ) (closedBall (f b) (((f'symm.nnnorm : â„)â»Â¹ - c) * Îµ)) := by
  intro y hy
  rcases le_or_gt (f'symm.nnnorm : â„)â»Â¹ c with hc | hc
  Â· refine âŸ¨b, by simp [Îµ0], ?_âŸ©
    have : dist y (f b) â‰¤ 0 :=
      (mem_closedBall.1 hy).trans (mul_nonpos_of_nonpos_of_nonneg (by linarith) Îµ0)
    simp only [dist_le_zero] at this
    rw [this]
  have If' : (0 : â„) < f'symm.nnnorm := by rw [â† inv_pos]; exact (NNReal.coe_nonneg _).trans_lt hc
  have Icf' : (c : â„) * f'symm.nnnorm < 1 := by rwa [inv_eq_one_div, lt_div_iffâ‚€ If'] at hc
  have Jcf' : (1 : â„) - c * f'symm.nnnorm â‰  0 := by apply ne_of_gt; linarith
  /- We have to show that `y` can be written as `f x` for some `x âˆˆ closedBall b Îµ`.
    The idea of the proof is to apply the Banach contraction principle to the map
    `g : x â†¦ x + f'symm (y - f x)`, as a fixed point of this map satisfies `f x = y`.
    When `f'symm` is a genuine linear inverse, `g` is a contracting map. In our case, since `f'symm`
    is nonlinear, this map is not contracting (it is not even continuous), but still the proof of
    the contraction theorem holds: `uâ‚™ = gâ¿ b` is a Cauchy sequence, converging exponentially fast
    to the desired point `x`. Instead of appealing to general results, we check this by hand.

    The main point is that `f (u n)` becomes exponentially close to `y`, and therefore
    `dist (u (n+1)) (u n)` becomes exponentally small, making it possible to get an inductive
    bound on `dist (u n) b`, from which one checks that `u n` stays in the ball on which one has a
    control. Therefore, the bound can be checked at the next step, and so on inductively.
    -/
  set g := fun x => x + f'symm (y - f x) with hg
  set u := fun n : â„• => g^[n] b with hu
  have usucc : âˆ€ n, u (n + 1) = g (u n) := by simp [hu, â† iterate_succ_apply' g _ b]
  -- First bound: if `f z` is close to `y`, then `g z` is close to `z` (i.e., almost a fixed point).
  have A : âˆ€ z, dist (g z) z â‰¤ f'symm.nnnorm * dist (f z) y := by
    intro z
    rw [dist_eq_norm, hg, add_sub_cancel_left, dist_eq_norm']
    exact f'symm.bound _
  -- Second bound: if `z` and `g z` are in the set with good control, then `f (g z)` becomes closer
  -- to `y` than `f z` was (this uses the linear approximation property, and is the reason for the
  -- choice of the formula for `g`).
  have B :
    âˆ€ z âˆˆ closedBall b Îµ,
      g z âˆˆ closedBall b Îµ â†’ dist (f (g z)) y â‰¤ c * f'symm.nnnorm * dist (f z) y := by
    intro z hz hgz
    set v := f'symm (y - f z)
    calc
      dist (f (g z)) y = â€–f (z + v) - yâ€– := by rw [dist_eq_norm]
      _ = â€–f (z + v) - f z - f' v + f' v - (y - f z)â€– := by congr 1; abel
      _ = â€–f (z + v) - f z - f' (z + v - z)â€– := by
        simp only [v, ContinuousLinearMap.NonlinearRightInverse.right_inv, add_sub_cancel_left,
          sub_add_cancel]
      _ â‰¤ c * â€–z + v - zâ€– := hf _ (hÎµ hgz) _ (hÎµ hz)
      _ â‰¤ c * (f'symm.nnnorm * dist (f z) y) := by
        gcongr
        simpa [dist_eq_norm'] using f'symm.bound (y - f z)
      _ = c * f'symm.nnnorm * dist (f z) y := by ring
  -- Third bound: a complicated bound on `dist w b` (that will show up in the induction) is enough
  -- to check that `w` is in the ball on which one has controls. Will be used to check that `u n`
  -- belongs to this ball for all `n`.
  have C : âˆ€ (n : â„•) (w : E), dist w b â‰¤ f'symm.nnnorm * (1 - ((c : â„) * f'symm.nnnorm) ^ n) /
      (1 - c * f'symm.nnnorm) * dist (f b) y â†’ w âˆˆ closedBall b Îµ := fun n w hw â†¦ by
    apply hw.trans
    rw [div_mul_eq_mul_div, div_le_iffâ‚€]; swap; Â· linarith
    calc
      (f'symm.nnnorm : â„) * (1 - ((c : â„) * f'symm.nnnorm) ^ n) * dist (f b) y =
          f'symm.nnnorm * dist (f b) y * (1 - ((c : â„) * f'symm.nnnorm) ^ n) := by
        ring
      _ â‰¤ f'symm.nnnorm * dist (f b) y * 1 := by
        gcongr
        rw [sub_le_self_iff]
        positivity
      _ â‰¤ f'symm.nnnorm * (((f'symm.nnnorm : â„)â»Â¹ - c) * Îµ) := by
        rw [mul_one]
        gcongr
        exact mem_closedBall'.1 hy
      _ = Îµ * (1 - c * f'symm.nnnorm) := by field_simp
  /- Main inductive control: `f (u n)` becomes exponentially close to `y`, and therefore
    `dist (u (n+1)) (u n)` becomes exponentally small, making it possible to get an inductive
    bound on `dist (u n) b`, from which one checks that `u n` remains in the ball on which we
    have estimates. -/
  have D : âˆ€ n : â„•, dist (f (u n)) y â‰¤ ((c : â„) * f'symm.nnnorm) ^ n * dist (f b) y âˆ§
      dist (u n) b â‰¤ f'symm.nnnorm * (1 - ((c : â„) * f'symm.nnnorm) ^ n) /
        (1 - (c : â„) * f'symm.nnnorm) * dist (f b) y := fun n â†¦ by
    induction n with
    | zero => simp [hu, le_refl]
    | succ n IH => ?_
    rw [usucc]
    have Ign : dist (g (u n)) b â‰¤ f'symm.nnnorm * (1 - ((c : â„) * f'symm.nnnorm) ^ n.succ) /
        (1 - c * f'symm.nnnorm) * dist (f b) y :=
      calc
        dist (g (u n)) b â‰¤ dist (g (u n)) (u n) + dist (u n) b := dist_triangle _ _ _
        _ â‰¤ f'symm.nnnorm * dist (f (u n)) y + dist (u n) b := add_le_add (A _) le_rfl
        _ â‰¤ f'symm.nnnorm * (((c : â„) * f'symm.nnnorm) ^ n * dist (f b) y) +
              f'symm.nnnorm * (1 - ((c : â„) * f'symm.nnnorm) ^ n) / (1 - c * f'symm.nnnorm) *
                dist (f b) y := by
                  gcongr
                  Â· exact IH.1
                  Â· exact IH.2
        _ = f'symm.nnnorm * (1 - ((c : â„) * f'symm.nnnorm) ^ n.succ) /
              (1 - (c : â„) * f'symm.nnnorm) * dist (f b) y := by
          replace Jcf' : (1:â„) - f'symm.nnnorm * c â‰  0 := by convert Jcf' using 1; ring
          simp [field, pow_succ, -mul_eq_mul_left_iff]
          ring
    refine âŸ¨?_, IgnâŸ©
    calc
      dist (f (g (u n))) y â‰¤ c * f'symm.nnnorm * dist (f (u n)) y :=
        B _ (C n _ IH.2) (C n.succ _ Ign)
      _ â‰¤ (c : â„) * f'symm.nnnorm * (((c : â„) * f'symm.nnnorm) ^ n * dist (f b) y) := by
        gcongr
        apply IH.1
      _ = ((c : â„) * f'symm.nnnorm) ^ n.succ * dist (f b) y := by simp only [pow_succ']; ring
  -- Deduce from the inductive bound that `uâ‚™` is a Cauchy sequence, therefore converging.
  have : CauchySeq u := by
    refine cauchySeq_of_le_geometric _ (â†‘f'symm.nnnorm * dist (f b) y) Icf' fun n â†¦ ?_
    calc
      dist (u n) (u (n + 1)) = dist (g (u n)) (u n) := by rw [usucc, dist_comm]
      _ â‰¤ f'symm.nnnorm * dist (f (u n)) y := A _
      _ â‰¤ f'symm.nnnorm * (((c : â„) * f'symm.nnnorm) ^ n * dist (f b) y) := by
        gcongr
        exact (D n).1
      _ = f'symm.nnnorm * dist (f b) y * ((c : â„) * f'symm.nnnorm) ^ n := by ring
  obtain âŸ¨x, hxâŸ© : âˆƒ x, Tendsto u atTop (ğ“ x) := cauchySeq_tendsto_of_complete this
  -- As all the `uâ‚™` belong to the ball `closedBall b Îµ`, so does their limit `x`.
  have xmem : x âˆˆ closedBall b Îµ :=
    isClosed_closedBall.mem_of_tendsto hx (Eventually.of_forall fun n => C n _ (D n).2)
  refine âŸ¨x, xmem, ?_âŸ©
  -- It remains to check that `f x = y`. This follows from continuity of `f` on `closedBall b Îµ`
  -- and from the fact that `f uâ‚™` is converging to `y` by construction.
  have hx' : Tendsto u atTop (ğ“[closedBall b Îµ] x) := by
    simp only [nhdsWithin, tendsto_inf, hx, true_and, tendsto_principal]
    exact Eventually.of_forall fun n => C n _ (D n).2
  have T1 : Tendsto (f âˆ˜ u) atTop (ğ“ (f x)) :=
    (hf.continuousOn.mono hÎµ x xmem).tendsto.comp hx'
  have T2 : Tendsto (f âˆ˜ u) atTop (ğ“ y) := by
    rw [tendsto_iff_dist_tendsto_zero]
    refine squeeze_zero (fun _ => dist_nonneg) (fun n => (D n).1) ?_
    simpa using (tendsto_pow_atTop_nhds_zero_of_lt_one (by positivity) Icf').mul tendsto_const_nhds
  exact tendsto_nhds_unique T1 T2

theorem open_image (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse)
    (hs : IsOpen s) (hc : Subsingleton F âˆ¨ c < f'symm.nnnormâ»Â¹) : IsOpen (f '' s) := by
  rcases hc with hE | hc
  Â· exact isOpen_discrete _
  simp only [isOpen_iff_mem_nhds, nhds_basis_closedBall.mem_iff, forall_mem_image] at hs âŠ¢
  intro x hx
  rcases hs x hx with âŸ¨Îµ, Îµ0, hÎµâŸ©
  refine âŸ¨(f'symm.nnnormâ»Â¹ - c) * Îµ, mul_pos (sub_pos.2 hc) Îµ0, ?_âŸ©
  exact (hf.surjOn_closedBall_of_nonlinearRightInverse f'symm (le_of_lt Îµ0) hÎµ).mono hÎµ Subset.rfl

theorem image_mem_nhds (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse)
    {x : E} (hs : s âˆˆ ğ“ x) (hc : Subsingleton F âˆ¨ c < f'symm.nnnormâ»Â¹) : f '' s âˆˆ ğ“ (f x) := by
  obtain âŸ¨t, hts, ht, xtâŸ© : âˆƒ t, t âŠ† s âˆ§ IsOpen t âˆ§ x âˆˆ t := _root_.mem_nhds_iff.1 hs
  have := IsOpen.mem_nhds ((hf.mono_set hts).open_image f'symm ht hc) (mem_image_of_mem _ xt)
  exact mem_of_superset this (image_mono hts)

theorem map_nhds_eq (hf : ApproximatesLinearOn f f' s c) (f'symm : f'.NonlinearRightInverse) {x : E}
    (hs : s âˆˆ ğ“ x) (hc : Subsingleton F âˆ¨ c < f'symm.nnnormâ»Â¹) : map f (ğ“ x) = ğ“ (f x) := by
  refine
    le_antisymm ((hf.continuousOn x (mem_of_mem_nhds hs)).continuousAt hs) (le_map fun t ht => ?_)
  have : f '' (s âˆ© t) âˆˆ ğ“ (f x) :=
    (hf.mono_set inter_subset_left).image_mem_nhds f'symm (inter_mem hs ht) hc
  exact mem_of_superset this (image_mono inter_subset_right)

end LocallyOnto

/-!
From now on we assume that `f` approximates an invertible continuous linear map `f : E â‰ƒL[ğ•œ] F`.

We also assume that either `E = {0}`, or `c < â€–f'â»Â¹â€–â»Â¹`. We use `N` as an abbreviation for `â€–f'â»Â¹â€–`.
-/


variable {f' : E â‰ƒL[ğ•œ] F} {s : Set E} {c : â„â‰¥0}

local notation "N" => â€–(f'.symm : F â†’L[ğ•œ] E)â€–â‚Š

protected theorem antilipschitz (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : AntilipschitzWith (Nâ»Â¹ - c)â»Â¹ (s.restrict f) := by
  rcases hc with hE | hc
  Â· exact AntilipschitzWith.of_subsingleton
  convert (f'.antilipschitz.restrict s).add_lipschitzWith hf.lipschitz_sub hc
  simp [restrict]

protected theorem injective (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : Injective (s.restrict f) :=
  (hf.antilipschitz hc).injective

protected theorem injOn (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : InjOn f s :=
  injOn_iff_injective.2 <| hf.injective hc

protected theorem surjective [CompleteSpace E] (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) univ c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : Surjective f := by
  rcases hc with hE | hc
  Â· haveI : Subsingleton F := (Equiv.subsingleton_congr f'.toEquiv).1 hE
    exact surjective_to_subsingleton _
  Â· apply forall_of_forall_mem_closedBall (fun y : F => âˆƒ a, f a = y) (f 0) _
    have hc' : (0 : â„) < Nâ»Â¹ - c := by rw [sub_pos]; exact hc
    let p : â„ â†’ Prop := fun R => closedBall (f 0) R âŠ† Set.range f
    have hp : âˆ€á¶  r : â„ in atTop, p ((Nâ»Â¹ - c) * r) := by
      have hr : âˆ€á¶  r : â„ in atTop, 0 â‰¤ r := eventually_ge_atTop 0
      refine hr.mono fun r hr => Subset.trans ?_ (image_subset_range f (closedBall 0 r))
      refine hf.surjOn_closedBall_of_nonlinearRightInverse f'.toNonlinearRightInverse hr ?_
      exact subset_univ _
    refine ((tendsto_id.const_mul_atTop hc').frequently hp.frequently).mono ?_
    exact fun R h y hy => h hy

/-- A map approximating a linear equivalence on a set defines a partial equivalence on this set.
Should not be used outside of this file, because it is superseded by `toOpenPartialHomeomorph`
below.

This is a first step towards the inverse function. -/
def toPartialEquiv (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : PartialEquiv E F :=
  (hf.injOn hc).toPartialEquiv _ _

/-- The inverse function is continuous on `f '' s`.
Use properties of `OpenPartialHomeomorph` instead. -/
theorem inverse_continuousOn (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : ContinuousOn (hf.toPartialEquiv hc).symm (f '' s) := by
  apply continuousOn_iff_continuous_restrict.2
  refine ((hf.antilipschitz hc).to_rightInvOn' ?_ (hf.toPartialEquiv hc).right_inv').continuous
  exact fun x hx => (hf.toPartialEquiv hc).map_target hx

/-- The inverse function is approximated linearly on `f '' s` by `f'.symm`. -/
theorem to_inv (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c) (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) :
    ApproximatesLinearOn (hf.toPartialEquiv hc).symm (f'.symm : F â†’L[ğ•œ] E) (f '' s)
      (N * (Nâ»Â¹ - c)â»Â¹ * c) := fun x hx y hy â†¦ by
  set A := hf.toPartialEquiv hc
  have Af : âˆ€ z, A z = f z := fun z => rfl
  rcases (mem_image _ _ _).1 hx with âŸ¨x', x's, rflâŸ©
  rcases (mem_image _ _ _).1 hy with âŸ¨y', y's, rflâŸ©
  rw [â† Af x', â† Af y', A.left_inv x's, A.left_inv y's]
  calc
    â€–x' - y' - f'.symm (A x' - A y')â€– â‰¤ N * â€–f' (x' - y' - f'.symm (A x' - A y'))â€– :=
      (f' : E â†’L[ğ•œ] F).bound_of_antilipschitz f'.antilipschitz _
    _ = N * â€–A y' - A x' - f' (y' - x')â€– := by
      congr 2
      simp only [ContinuousLinearEquiv.apply_symm_apply, ContinuousLinearEquiv.map_sub]
      abel
    _ â‰¤ N * (c * â€–y' - x'â€–) := mul_le_mul_of_nonneg_left (hf _ y's _ x's) (NNReal.coe_nonneg _)
    _ â‰¤ N * (c * (((Nâ»Â¹ - c)â»Â¹ : â„â‰¥0) * â€–A y' - A x'â€–)) := by
      gcongr
      rw [â† dist_eq_norm, â† dist_eq_norm]
      exact (hf.antilipschitz hc).le_mul_dist âŸ¨y', y'sâŸ© âŸ¨x', x'sâŸ©
    _ = (N * (Nâ»Â¹ - c)â»Â¹ * c : â„â‰¥0) * â€–A x' - A y'â€– := by
      simp only [norm_sub_rev, NNReal.coe_mul]; ring

variable [CompleteSpace E]

section

variable (f s)

/-- Given a function `f` that approximates a linear equivalence on an open set `s`,
returns a open partial homeomorphism with `toFun = f` and `source = s`. -/
def toOpenPartialHomeomorph (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) (hs : IsOpen s) : OpenPartialHomeomorph E F where
  toPartialEquiv := hf.toPartialEquiv hc
  open_source := hs
  open_target := hf.open_image f'.toNonlinearRightInverse hs <| by
    rwa [f'.toEquiv.subsingleton_congr] at hc
  continuousOn_toFun := hf.continuousOn
  continuousOn_invFun := hf.inverse_continuousOn hc

@[deprecated (since := "2025-08-29")] noncomputable alias
  toPartialHomeomorph := toOpenPartialHomeomorph

@[simp]
theorem toOpenPartialHomeomorph_coe (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) (hs : IsOpen s) :
    (hf.toOpenPartialHomeomorph f s hc hs : E â†’ F) = f :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_coe := toOpenPartialHomeomorph_coe

@[simp]
theorem toOpenPartialHomeomorph_source (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) (hs : IsOpen s) :
    (hf.toOpenPartialHomeomorph f s hc hs).source = s :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_source := toOpenPartialHomeomorph_source

@[simp]
theorem toOpenPartialHomeomorph_target (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) (hs : IsOpen s) :
    (hf.toOpenPartialHomeomorph f s hc hs).target = f '' s :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_target := toOpenPartialHomeomorph_target

/-- A function `f` that approximates a linear equivalence on the whole space is a homeomorphism. -/
def toHomeomorph (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) univ c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) : E â‰ƒâ‚œ F := by
  refine
    (hf.toOpenPartialHomeomorph _ _ hc isOpen_univ).toHomeomorphOfSourceEqUnivTargetEqUniv rfl ?_
  rw [toOpenPartialHomeomorph_target, image_univ, range_eq_univ]
  exact hf.surjective hc

end

theorem closedBall_subset_target (hf : ApproximatesLinearOn f (f' : E â†’L[ğ•œ] F) s c)
    (hc : Subsingleton E âˆ¨ c < Nâ»Â¹) (hs : IsOpen s) {b : E} (Îµ0 : 0 â‰¤ Îµ) (hÎµ : closedBall b Îµ âŠ† s) :
    closedBall (f b) ((Nâ»Â¹ - c) * Îµ) âŠ† (hf.toOpenPartialHomeomorph f s hc hs).target :=
  (hf.surjOn_closedBall_of_nonlinearRightInverse f'.toNonlinearRightInverse Îµ0 hÎµ).mono hÎµ
    Subset.rfl

end ApproximatesLinearOn
