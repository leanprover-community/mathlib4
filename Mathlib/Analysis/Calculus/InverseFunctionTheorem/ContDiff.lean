/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.ContDiff.RCLike
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.FDeriv

/-!
# Inverse function theorem, `C^r` case

In this file we specialize the inverse function theorem to `C^r`-smooth functions.
-/

noncomputable section

namespace ContDiffAt

variable {ğ•‚ : Type*} [RCLike ğ•‚]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•‚ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•‚ F]
variable [CompleteSpace E] (f : E â†’ F) {f' : E â‰ƒL[ğ•‚] F} {a : E} {n : WithTop â„•âˆ}

/-- Given a `ContDiff` function over `ğ•‚` (which is `â„` or `â„‚`) with an invertible
derivative at `a`, returns an `OpenPartialHomeomorph` with `to_fun = f` and `a âˆˆ source`. -/
def toOpenPartialHomeomorph (hf : ContDiffAt ğ•‚ n f a) (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a)
    (hn : 1 â‰¤ n) : OpenPartialHomeomorph E F :=
  (hf.hasStrictFDerivAt' hf' hn).toOpenPartialHomeomorph f

@[deprecated (since := "2025-08-29")] noncomputable alias
  toPartialHomeomorph := toOpenPartialHomeomorph

variable {f}

@[simp]
theorem toOpenPartialHomeomorph_coe (hf : ContDiffAt ğ•‚ n f a)
    (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a) (hn : 1 â‰¤ n) :
    (hf.toOpenPartialHomeomorph f hf' hn : E â†’ F) = f :=
  rfl

@[deprecated (since := "2025-08-29")] alias
  toPartialHomeomorph_coe := toOpenPartialHomeomorph_coe

theorem mem_toOpenPartialHomeomorph_source (hf : ContDiffAt ğ•‚ n f a)
    (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a) (hn : 1 â‰¤ n) :
    a âˆˆ (hf.toOpenPartialHomeomorph f hf' hn).source :=
  (hf.hasStrictFDerivAt' hf' hn).mem_toOpenPartialHomeomorph_source

@[deprecated (since := "2025-08-29")] alias
  mem_toPartialHomeomorph_source := mem_toOpenPartialHomeomorph_source

theorem image_mem_toOpenPartialHomeomorph_target (hf : ContDiffAt ğ•‚ n f a)
    (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a) (hn : 1 â‰¤ n) :
    f a âˆˆ (hf.toOpenPartialHomeomorph f hf' hn).target :=
  (hf.hasStrictFDerivAt' hf' hn).image_mem_toOpenPartialHomeomorph_target

@[deprecated (since := "2025-08-29")] alias
  image_mem_toPartialHomeomorph_target := image_mem_toOpenPartialHomeomorph_target

/-- Given a `ContDiff` function over `ğ•‚` (which is `â„` or `â„‚`) with an invertible derivative
at `a`, returns a function that is locally inverse to `f`. -/
def localInverse (hf : ContDiffAt ğ•‚ n f a) (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a)
    (hn : 1 â‰¤ n) : F â†’ E :=
  (hf.hasStrictFDerivAt' hf' hn).localInverse f f' a

theorem localInverse_apply_image (hf : ContDiffAt ğ•‚ n f a)
    (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a) (hn : 1 â‰¤ n) : hf.localInverse hf' hn (f a) = a :=
  (hf.hasStrictFDerivAt' hf' hn).localInverse_apply_image

/-- Given a `ContDiff` function over `ğ•‚` (which is `â„` or `â„‚`) with an invertible derivative
at `a`, the inverse function (produced by `ContDiff.toOpenPartialHomeomorph`) is
also `ContDiff`. -/
theorem to_localInverse (hf : ContDiffAt ğ•‚ n f a)
    (hf' : HasFDerivAt f (f' : E â†’L[ğ•‚] F) a) (hn : 1 â‰¤ n) :
    ContDiffAt ğ•‚ n (hf.localInverse hf' hn) (f a) := by
  have := hf.localInverse_apply_image hf' hn
  apply (hf.toOpenPartialHomeomorph f hf' hn).contDiffAt_symm
    (image_mem_toOpenPartialHomeomorph_target hf hf' hn)
  Â· convert hf'
  Â· convert hf

end ContDiffAt
