/-
Copyright (c) 2024 Bj√∏rn Kjos-Hanssen. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bj√∏rn Kjos-Hanssen, Patrick Massot, Floris van Doorn, Jireh Loreaux, Eric Wieser
-/
import Mathlib.Topology.Order.OrderClosedExtr
import Mathlib.Analysis.Calculus.Deriv.MeanValue
import Mathlib.Order.Interval.Set.Basic
import Mathlib.LinearAlgebra.AffineSpace.Ordered

/-!
# The First-Derivative Test

We prove the first-derivative test from calculus, in the strong form given on [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#First-derivative_test).

The test is proved over the real numbers ‚Ñù
using `monotoneOn_of_deriv_nonneg` from `Mathlib/Analysis/Calculus/Deriv/MeanValue.lean`.

# The Second-Derivative Test

We prove the Second-Derivative Test using the First-Derivative Test.
Source: [Wikipedia](https://en.wikipedia.org/wiki/Derivative_test#Proof_of_the_second-derivative_test).

## Main results

* `isLocalMax_of_deriv_Ioo`: Suppose `f` is a real-valued function of a real variable
  defined on some interval containing the point `a`.
  Further suppose that `f` is continuous at `a` and differentiable on some open interval
  containing `a`, except possibly at `a` itself.

  If there exists a positive number `r > 0` such that for every `x` in `(a ‚àí r, a)`
  we have `f‚Ä≤(x) ‚â• 0`, and for every `x` in `(a, a + r)` we have `f‚Ä≤(x) ‚â§ 0`,
  then `f` has a local maximum at `a`.

* `isLocalMin_of_deriv_Ioo`: The dual of `first_derivative_max`, for minima.

* `isLocalMax_of_deriv`: 1st derivative test for maxima using filters.

* `isLocalMin_of_deriv`: 1st derivative test for minima using filters.

* `isLocalMin_of_deriv_deriv_pos`: The second-derivative test, minimum version.


## Tags

derivative test, calculus
-/


open Set Topology


/-- The First-Derivative Test from calculus, maxima version.
Suppose `a < b < c`, `f : ‚Ñù ‚Üí ‚Ñù` is continuous at `b`,
the derivative `f'` is nonnegative on `(a,b)`, and
the derivative `f'` is nonpositive on `(b,c)`. Then `f` has a local maximum at `a`. -/
lemma isLocalMax_of_deriv_Ioo {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù} (g‚ÇÄ : a < b) (g‚ÇÅ : b < c)
    (h : ContinuousAt f b)
    (hd‚ÇÄ : DifferentiableOn ‚Ñù f (Ioo a b))
    (hd‚ÇÅ : DifferentiableOn ‚Ñù f (Ioo b c))
    (h‚ÇÄ : ‚àÄ x ‚àà Ioo a b, 0 ‚â§ deriv f x)
    (h‚ÇÅ : ‚àÄ x ‚àà Ioo b c, deriv f x ‚â§ 0) : IsLocalMax f b :=
  have hIoc : ContinuousOn f (Ioc a b) :=
    Ioo_union_right g‚ÇÄ ‚ñ∏ hd‚ÇÄ.continuousOn.union_continuousAt (isOpen_Ioo (a := a) (b := b))
      (by simp_all)
  have hIco : ContinuousOn f (Ico b c) :=
    Ioo_union_left g‚ÇÅ ‚ñ∏ hd‚ÇÅ.continuousOn.union_continuousAt (isOpen_Ioo (a := b) (b := c))
      (by simp_all)
  isLocalMax_of_mono_anti g‚ÇÄ g‚ÇÅ
    (monotoneOn_of_deriv_nonneg (convex_Ioc a b) hIoc (by simp_all) (by simp_all))
    (antitoneOn_of_deriv_nonpos (convex_Ico b c) hIco (by simp_all) (by simp_all))


/-- The First-Derivative Test from calculus, minima version. -/
lemma isLocalMin_of_deriv_Ioo {f : ‚Ñù ‚Üí ‚Ñù} {a b c : ‚Ñù}
    (g‚ÇÄ : a < b) (g‚ÇÅ : b < c) (h : ContinuousAt f b)
    (hd‚ÇÄ : DifferentiableOn ‚Ñù f (Ioo a b)) (hd‚ÇÅ : DifferentiableOn ‚Ñù f (Ioo b c))
    (h‚ÇÄ : ‚àÄ x ‚àà Ioo a b, deriv f x ‚â§ 0)
    (h‚ÇÅ : ‚àÄ x ‚àà Ioo b c, 0 ‚â§ deriv f x) : IsLocalMin f b := by
  have := isLocalMax_of_deriv_Ioo (f := -f) g‚ÇÄ g‚ÇÅ
    (by simp_all) hd‚ÇÄ.neg hd‚ÇÅ.neg
    (fun x hx ‚Ü¶ deriv.neg (f := f) ‚ñ∏ Left.nonneg_neg_iff.mpr <|h‚ÇÄ x hx)
    (fun x hx ‚Ü¶ deriv.neg (f := f) ‚ñ∏ Left.neg_nonpos_iff.mpr <|h‚ÇÅ x hx)
  exact (neg_neg f) ‚ñ∏ IsLocalMax.neg this

/-- The First-Derivative Test from calculus, maxima version,
expressed in terms of left and right filters. -/
lemma isLocalMax_of_deriv' {f : ‚Ñù ‚Üí ‚Ñù} {b : ‚Ñù} (h : ContinuousAt f b)
    (hd‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, DifferentiableAt ‚Ñù f x) (hd‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, DifferentiableAt ‚Ñù f x)
    (h‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, 0 ‚â§ deriv f x) (h‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, deriv f x ‚â§ 0) :
    IsLocalMax f b := by
  obtain ‚ü®a, ha‚ü© := (nhdsLT_basis b).eventually_iff.mp <| hd‚ÇÄ.and h‚ÇÄ
  obtain ‚ü®c, hc‚ü© := (nhdsGT_basis b).eventually_iff.mp <| hd‚ÇÅ.and h‚ÇÅ
  exact isLocalMax_of_deriv_Ioo ha.1 hc.1 h
    (fun _ hx ‚Ü¶ (ha.2 hx).1.differentiableWithinAt)
    (fun _ hx ‚Ü¶ (hc.2 hx).1.differentiableWithinAt)
    (fun _ hx ‚Ü¶ (ha.2 hx).2) (fun x hx ‚Ü¶ (hc.2 hx).2)

/-- The First-Derivative Test from calculus, minima version,
expressed in terms of left and right filters. -/
lemma isLocalMin_of_deriv' {f : ‚Ñù ‚Üí ‚Ñù} {b : ‚Ñù} (h : ContinuousAt f b)
    (hd‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, DifferentiableAt ‚Ñù f x) (hd‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, DifferentiableAt ‚Ñù f x)
    (h‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, deriv f x ‚â§ 0) (h‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, deriv f x ‚â• 0) :
    IsLocalMin f b := by
  obtain ‚ü®a, ha‚ü© := (nhdsLT_basis b).eventually_iff.mp <| hd‚ÇÄ.and h‚ÇÄ
  obtain ‚ü®c, hc‚ü© := (nhdsGT_basis b).eventually_iff.mp <| hd‚ÇÅ.and h‚ÇÅ
  exact isLocalMin_of_deriv_Ioo ha.1 hc.1 h
    (fun _ hx ‚Ü¶ (ha.2 hx).1.differentiableWithinAt)
    (fun _ hx ‚Ü¶ (hc.2 hx).1.differentiableWithinAt)
    (fun _ hx ‚Ü¶ (ha.2 hx).2) (fun x hx ‚Ü¶ (hc.2 hx).2)

/-- The First Derivative test, maximum version. -/
theorem isLocalMax_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} {b : ‚Ñù} (h : ContinuousAt f b)
    (hd : ‚àÄ·∂† x in ùìù[‚â†] b, DifferentiableAt ‚Ñù f x)
    (h‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, 0 ‚â§ deriv f x) (h‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, deriv f x ‚â§ 0) :
    IsLocalMax f b :=
  isLocalMax_of_deriv' h (nhdsLT_le_nhdsNE _ (by tauto)) (nhdsGT_le_nhdsNE _ (by tauto)) h‚ÇÄ h‚ÇÅ

/-- The First Derivative test, minimum version. -/
theorem isLocalMin_of_deriv {f : ‚Ñù ‚Üí ‚Ñù} {b : ‚Ñù} (h : ContinuousAt f b)
    (hd : ‚àÄ·∂† x in ùìù[‚â†] b, DifferentiableAt ‚Ñù f x)
    (h‚ÇÄ : ‚àÄ·∂† x in ùìù[<] b, deriv f x ‚â§ 0) (h‚ÇÅ : ‚àÄ·∂† x in ùìù[>] b, 0 ‚â§ deriv f x) :
    IsLocalMin f b :=
  isLocalMin_of_deriv' h (nhdsLT_le_nhdsNE _ (by tauto)) (nhdsGT_le_nhdsNE _ (by tauto)) h‚ÇÄ h‚ÇÅ

open Filter SignType

section SecondDeriv

variable {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù}

/-- If the derivative of `f` is positive at a root `x‚ÇÄ` of `f`, then locally the sign of `f x`
matches `x - x‚ÇÄ`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_pos (hf : deriv f x‚ÇÄ > 0) (hx : f x‚ÇÄ = 0) :
    ‚àÄ·∂† x in ùìù x‚ÇÄ, sign (f x) = sign (x - x‚ÇÄ) := by
  rw [‚Üê nhdsNE_sup_pure x‚ÇÄ, eventually_sup]
  refine ‚ü®?_, by simpa‚ü©
  have h_tendsto := hasDerivAt_iff_tendsto_slope.mp
    (differentiableAt_of_deriv_ne_zero <| ne_of_gt hf).hasDerivAt
  filter_upwards [(h_tendsto.eventually <| eventually_gt_nhds hf),
    self_mem_nhdsWithin] with x hx‚ÇÄ hx‚ÇÅ
  rw [mem_compl_iff, mem_singleton_iff, ‚Üê Ne.eq_def] at hx‚ÇÅ
  obtain (hx' | hx') := hx‚ÇÅ.lt_or_gt
  ¬∑ rw [sign_neg (neg_of_slope_pos hx' hx‚ÇÄ hx), sign_neg (sub_neg.mpr hx')]
  ¬∑ rw [sign_pos (pos_of_slope_pos hx' hx‚ÇÄ hx), sign_pos (sub_pos.mpr hx')]

/-- If the derivative of `f` is negative at a root `x‚ÇÄ` of `f`, then locally the sign of `f x`
matches `x‚ÇÄ - x`. -/
lemma eventually_nhdsWithin_sign_eq_of_deriv_neg (hf : deriv f x‚ÇÄ < 0) (hx : f x‚ÇÄ = 0) :
    ‚àÄ·∂† x in ùìù x‚ÇÄ, sign (f x) = sign (x‚ÇÄ - x) := by
  simpa [Left.sign_neg, -neg_sub, ‚Üê neg_sub x‚ÇÄ] using
    eventually_nhdsWithin_sign_eq_of_deriv_pos
      (f := (-f ¬∑)) (x‚ÇÄ := x‚ÇÄ) (by simpa [deriv.neg]) (by simpa)

lemma deriv_neg_left_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù}
    (h‚ÇÄ : ‚àÄ·∂† (x : ‚Ñù) in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x - x‚ÇÄ)) :
    ‚àÄ·∂† (b : ‚Ñù) in ùìù[<] x‚ÇÄ, deriv f b < 0 := by
  filter_upwards [nhdsLT_le_nhdsNE _ h‚ÇÄ, self_mem_nhdsWithin] with x hx' (hx : x < x‚ÇÄ)
  rwa [‚Üê sub_neg, ‚Üê sign_eq_neg_one_iff, ‚Üê hx', sign_eq_neg_one_iff] at hx

lemma deriv_neg_right_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù}
    (h‚ÇÄ : ‚àÄ·∂† (x : ‚Ñù) in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x‚ÇÄ - x)) :
     ‚àÄ·∂† (b : ‚Ñù) in ùìù[>] x‚ÇÄ, deriv f b < 0 := by
  filter_upwards [nhdsGT_le_nhdsNE _ h‚ÇÄ, self_mem_nhdsWithin] with x hx' (hx : x‚ÇÄ < x)
  rwa [‚Üê sub_neg, ‚Üê sign_eq_neg_one_iff, ‚Üê hx', sign_eq_neg_one_iff] at hx

lemma deriv_pos_right_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù}
    (h‚ÇÄ : ‚àÄ·∂† (x : ‚Ñù) in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x - x‚ÇÄ)) :
     ‚àÄ·∂† (b : ‚Ñù) in ùìù[>] x‚ÇÄ, deriv f b > 0 := by
  filter_upwards [nhdsGT_le_nhdsNE _ h‚ÇÄ, self_mem_nhdsWithin] with x hx' (hx : x‚ÇÄ < x)
  rwa [‚Üê sub_pos, ‚Üê sign_eq_one_iff, ‚Üê hx', sign_eq_one_iff] at hx

lemma deriv_pos_left_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù}
    (h‚ÇÄ : ‚àÄ·∂† (x : ‚Ñù) in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x‚ÇÄ - x)) :
    ‚àÄ·∂† (b : ‚Ñù) in ùìù[<] x‚ÇÄ, deriv f b > 0 := by
  filter_upwards [nhdsLT_le_nhdsNE _ h‚ÇÄ, self_mem_nhdsWithin] with x hx' (hx : x < x‚ÇÄ)
  rwa [‚Üê sub_pos, ‚Üê sign_eq_one_iff, ‚Üê hx', sign_eq_one_iff] at hx

/-- The First Derivative test with a hypothesis on the sign of the derivative, maximum version. -/
theorem isLocalMax_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù} (h : ContinuousAt f x‚ÇÄ)
    (hf : ‚àÄ·∂† x in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x‚ÇÄ - x)) :
    IsLocalMax f x‚ÇÄ := by
  have hl := deriv_pos_left_of_sign_deriv hf
  have hg := deriv_neg_right_of_sign_deriv hf
  replace hf := (nhdsLT_sup_nhdsGT x‚ÇÄ) ‚ñ∏
    eventually_sup.mpr ‚ü®hl.mono fun x hx ‚Ü¶ hx.ne', hg.mono fun x hx ‚Ü¶ hx.ne‚ü©
  exact isLocalMax_of_deriv h (hf.mono fun x hx ‚Ü¶ differentiableAt_of_deriv_ne_zero hx)
    (hl.mono fun _ ‚Ü¶ le_of_lt) (hg.mono fun _ ‚Ü¶ le_of_lt)

/-- The First Derivative test with a hypothesis on the sign of the derivative, minimum version. -/
theorem isLocalMin_of_sign_deriv {f : ‚Ñù ‚Üí ‚Ñù} {x‚ÇÄ : ‚Ñù} (h : ContinuousAt f x‚ÇÄ)
    (hf : ‚àÄ·∂† x in ùìù[‚â†] x‚ÇÄ, sign (deriv f x) = sign (x - x‚ÇÄ)) :
    IsLocalMin f x‚ÇÄ := by
  refine neg_neg f ‚ñ∏ (isLocalMax_of_sign_deriv (f := (-f ¬∑)) h.neg ?foo |>.neg)
  simpa [Left.sign_neg, -neg_sub, ‚Üê neg_sub _ x‚ÇÄ, deriv.neg]

/-- The Second-Derivative Test from calculus, minimum version.
Applies to functions like `x^2 + 1[x ‚â• 0]` as well as twice differentiable
functions. -/
theorem isLocalMin_of_deriv_deriv_pos (hf : deriv (deriv f) x‚ÇÄ > 0) (hd : deriv f x‚ÇÄ = 0)
    (hc : ContinuousAt f x‚ÇÄ) : IsLocalMin f x‚ÇÄ :=
  isLocalMin_of_sign_deriv hc <| nhdsWithin_le_nhds <|
    eventually_nhdsWithin_sign_eq_of_deriv_pos hf hd

/-- The Second-Derivative Test from calculus, maximum version. -/
theorem isLocalMax_of_deriv_deriv_neg (hf : deriv (deriv f) x‚ÇÄ < 0) (hd : deriv f x‚ÇÄ = 0)
    (hc : ContinuousAt f x‚ÇÄ) : IsLocalMax f x‚ÇÄ := by
  simpa using isLocalMin_of_deriv_deriv_pos (by simpa) (by simpa) hc.neg |>.neg

end SecondDeriv
