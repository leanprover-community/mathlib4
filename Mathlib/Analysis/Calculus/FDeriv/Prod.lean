/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

#align_import analysis.calculus.fderiv.prod from "leanprover-community/mathlib"@"e354e865255654389cc46e6032160238df2e0f40"

/-!
# Derivative of the cartesian product of functions

For detailed documentation of the Fr√©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
cartesian products of functions, and functions into Pi-types.
-/


open Filter Asymptotics ContinuousLinearMap Set Metric

open scoped Classical
open Topology NNReal Filter Asymptotics ENNReal

noncomputable section

section

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ùïú G']
variable {f f‚ÇÄ f‚ÇÅ g : E ‚Üí F}
variable {f' f‚ÇÄ' f‚ÇÅ' g' : E ‚ÜíL[ùïú] F}
variable (e : E ‚ÜíL[ùïú] F)
variable {x : E}
variable {s t : Set E}
variable {L L‚ÇÅ L‚ÇÇ : Filter E}

section CartesianProduct

/-! ### Derivative of the cartesian product of two functions -/


section Prod

variable {f‚ÇÇ : E ‚Üí G} {f‚ÇÇ' : E ‚ÜíL[ùïú] G}

protected theorem HasStrictFDerivAt.prod (hf‚ÇÅ : HasStrictFDerivAt f‚ÇÅ f‚ÇÅ' x)
    (hf‚ÇÇ : HasStrictFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasStrictFDerivAt (fun x => (f‚ÇÅ x, f‚ÇÇ x)) (f‚ÇÅ'.prod f‚ÇÇ') x :=
  hf‚ÇÅ.prod_left hf‚ÇÇ
#align has_strict_fderiv_at.prod HasStrictFDerivAt.prod

theorem HasFDerivAtFilter.prod (hf‚ÇÅ : HasFDerivAtFilter f‚ÇÅ f‚ÇÅ' x L)
    (hf‚ÇÇ : HasFDerivAtFilter f‚ÇÇ f‚ÇÇ' x L) :
    HasFDerivAtFilter (fun x => (f‚ÇÅ x, f‚ÇÇ x)) (f‚ÇÅ'.prod f‚ÇÇ') x L :=
  .of_isLittleO <| hf‚ÇÅ.isLittleO.prod_left hf‚ÇÇ.isLittleO
#align has_fderiv_at_filter.prod HasFDerivAtFilter.prod

@[fun_prop]
nonrec theorem HasFDerivWithinAt.prod (hf‚ÇÅ : HasFDerivWithinAt f‚ÇÅ f‚ÇÅ' s x)
    (hf‚ÇÇ : HasFDerivWithinAt f‚ÇÇ f‚ÇÇ' s x) :
    HasFDerivWithinAt (fun x => (f‚ÇÅ x, f‚ÇÇ x)) (f‚ÇÅ'.prod f‚ÇÇ') s x :=
  hf‚ÇÅ.prod hf‚ÇÇ
#align has_fderiv_within_at.prod HasFDerivWithinAt.prod

@[fun_prop]
nonrec theorem HasFDerivAt.prod (hf‚ÇÅ : HasFDerivAt f‚ÇÅ f‚ÇÅ' x) (hf‚ÇÇ : HasFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasFDerivAt (fun x => (f‚ÇÅ x, f‚ÇÇ x)) (f‚ÇÅ'.prod f‚ÇÇ') x :=
  hf‚ÇÅ.prod hf‚ÇÇ
#align has_fderiv_at.prod HasFDerivAt.prod

@[fun_prop]
theorem hasFDerivAt_prod_mk_left (e‚ÇÄ : E) (f‚ÇÄ : F) :
    HasFDerivAt (fun e : E => (e, f‚ÇÄ)) (inl ùïú E F) e‚ÇÄ :=
  (hasFDerivAt_id e‚ÇÄ).prod (hasFDerivAt_const f‚ÇÄ e‚ÇÄ)
#align has_fderiv_at_prod_mk_left hasFDerivAt_prod_mk_left

@[fun_prop]
theorem hasFDerivAt_prod_mk_right (e‚ÇÄ : E) (f‚ÇÄ : F) :
    HasFDerivAt (fun f : F => (e‚ÇÄ, f)) (inr ùïú E F) f‚ÇÄ :=
  (hasFDerivAt_const e‚ÇÄ f‚ÇÄ).prod (hasFDerivAt_id f‚ÇÄ)
#align has_fderiv_at_prod_mk_right hasFDerivAt_prod_mk_right

@[fun_prop]
theorem DifferentiableWithinAt.prod (hf‚ÇÅ : DifferentiableWithinAt ùïú f‚ÇÅ s x)
    (hf‚ÇÇ : DifferentiableWithinAt ùïú f‚ÇÇ s x) :
    DifferentiableWithinAt ùïú (fun x : E => (f‚ÇÅ x, f‚ÇÇ x)) s x :=
  (hf‚ÇÅ.hasFDerivWithinAt.prod hf‚ÇÇ.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.prod DifferentiableWithinAt.prod

@[simp, fun_prop]
theorem DifferentiableAt.prod (hf‚ÇÅ : DifferentiableAt ùïú f‚ÇÅ x) (hf‚ÇÇ : DifferentiableAt ùïú f‚ÇÇ x) :
    DifferentiableAt ùïú (fun x : E => (f‚ÇÅ x, f‚ÇÇ x)) x :=
  (hf‚ÇÅ.hasFDerivAt.prod hf‚ÇÇ.hasFDerivAt).differentiableAt
#align differentiable_at.prod DifferentiableAt.prod

@[fun_prop]
theorem DifferentiableOn.prod (hf‚ÇÅ : DifferentiableOn ùïú f‚ÇÅ s) (hf‚ÇÇ : DifferentiableOn ùïú f‚ÇÇ s) :
    DifferentiableOn ùïú (fun x : E => (f‚ÇÅ x, f‚ÇÇ x)) s := fun x hx =>
  DifferentiableWithinAt.prod (hf‚ÇÅ x hx) (hf‚ÇÇ x hx)
#align differentiable_on.prod DifferentiableOn.prod

@[simp, fun_prop]
theorem Differentiable.prod (hf‚ÇÅ : Differentiable ùïú f‚ÇÅ) (hf‚ÇÇ : Differentiable ùïú f‚ÇÇ) :
    Differentiable ùïú fun x : E => (f‚ÇÅ x, f‚ÇÇ x) := fun x => DifferentiableAt.prod (hf‚ÇÅ x) (hf‚ÇÇ x)
#align differentiable.prod Differentiable.prod

theorem DifferentiableAt.fderiv_prod (hf‚ÇÅ : DifferentiableAt ùïú f‚ÇÅ x)
    (hf‚ÇÇ : DifferentiableAt ùïú f‚ÇÇ x) :
    fderiv ùïú (fun x : E => (f‚ÇÅ x, f‚ÇÇ x)) x = (fderiv ùïú f‚ÇÅ x).prod (fderiv ùïú f‚ÇÇ x) :=
  (hf‚ÇÅ.hasFDerivAt.prod hf‚ÇÇ.hasFDerivAt).fderiv
#align differentiable_at.fderiv_prod DifferentiableAt.fderiv_prod

theorem DifferentiableWithinAt.fderivWithin_prod (hf‚ÇÅ : DifferentiableWithinAt ùïú f‚ÇÅ s x)
    (hf‚ÇÇ : DifferentiableWithinAt ùïú f‚ÇÇ s x) (hxs : UniqueDiffWithinAt ùïú s x) :
    fderivWithin ùïú (fun x : E => (f‚ÇÅ x, f‚ÇÇ x)) s x =
      (fderivWithin ùïú f‚ÇÅ s x).prod (fderivWithin ùïú f‚ÇÇ s x) :=
  (hf‚ÇÅ.hasFDerivWithinAt.prod hf‚ÇÇ.hasFDerivWithinAt).fderivWithin hxs
#align differentiable_within_at.fderiv_within_prod DifferentiableWithinAt.fderivWithin_prod

end Prod

section Fst

variable {f‚ÇÇ : E ‚Üí F √ó G} {f‚ÇÇ' : E ‚ÜíL[ùïú] F √ó G} {p : E √ó F}

@[fun_prop]
theorem hasStrictFDerivAt_fst : HasStrictFDerivAt (@Prod.fst E F) (fst ùïú E F) p :=
  (fst ùïú E F).hasStrictFDerivAt
#align has_strict_fderiv_at_fst hasStrictFDerivAt_fst

@[fun_prop]
protected theorem HasStrictFDerivAt.fst (h : HasStrictFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasStrictFDerivAt (fun x => (f‚ÇÇ x).1) ((fst ùïú F G).comp f‚ÇÇ') x :=
  hasStrictFDerivAt_fst.comp x h
#align has_strict_fderiv_at.fst HasStrictFDerivAt.fst

theorem hasFDerivAtFilter_fst {L : Filter (E √ó F)} :
    HasFDerivAtFilter (@Prod.fst E F) (fst ùïú E F) p L :=
  (fst ùïú E F).hasFDerivAtFilter
#align has_fderiv_at_filter_fst hasFDerivAtFilter_fst

protected theorem HasFDerivAtFilter.fst (h : HasFDerivAtFilter f‚ÇÇ f‚ÇÇ' x L) :
    HasFDerivAtFilter (fun x => (f‚ÇÇ x).1) ((fst ùïú F G).comp f‚ÇÇ') x L :=
  hasFDerivAtFilter_fst.comp x h tendsto_map
#align has_fderiv_at_filter.fst HasFDerivAtFilter.fst

@[fun_prop]
theorem hasFDerivAt_fst : HasFDerivAt (@Prod.fst E F) (fst ùïú E F) p :=
  hasFDerivAtFilter_fst
#align has_fderiv_at_fst hasFDerivAt_fst

@[fun_prop]
protected nonrec theorem HasFDerivAt.fst (h : HasFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasFDerivAt (fun x => (f‚ÇÇ x).1) ((fst ùïú F G).comp f‚ÇÇ') x :=
  h.fst
#align has_fderiv_at.fst HasFDerivAt.fst

@[fun_prop]
theorem hasFDerivWithinAt_fst {s : Set (E √ó F)} :
    HasFDerivWithinAt (@Prod.fst E F) (fst ùïú E F) s p :=
  hasFDerivAtFilter_fst
#align has_fderiv_within_at_fst hasFDerivWithinAt_fst

@[fun_prop]
protected nonrec theorem HasFDerivWithinAt.fst (h : HasFDerivWithinAt f‚ÇÇ f‚ÇÇ' s x) :
    HasFDerivWithinAt (fun x => (f‚ÇÇ x).1) ((fst ùïú F G).comp f‚ÇÇ') s x :=
  h.fst
#align has_fderiv_within_at.fst HasFDerivWithinAt.fst

@[fun_prop]
theorem differentiableAt_fst : DifferentiableAt ùïú Prod.fst p :=
  hasFDerivAt_fst.differentiableAt
#align differentiable_at_fst differentiableAt_fst

@[simp, fun_prop]
protected theorem DifferentiableAt.fst (h : DifferentiableAt ùïú f‚ÇÇ x) :
    DifferentiableAt ùïú (fun x => (f‚ÇÇ x).1) x :=
  differentiableAt_fst.comp x h
#align differentiable_at.fst DifferentiableAt.fst

@[fun_prop]
theorem differentiable_fst : Differentiable ùïú (Prod.fst : E √ó F ‚Üí E) := fun _ =>
  differentiableAt_fst
#align differentiable_fst differentiable_fst

@[simp, fun_prop]
protected theorem Differentiable.fst (h : Differentiable ùïú f‚ÇÇ) :
    Differentiable ùïú fun x => (f‚ÇÇ x).1 :=
  differentiable_fst.comp h
#align differentiable.fst Differentiable.fst

@[fun_prop]
theorem differentiableWithinAt_fst {s : Set (E √ó F)} : DifferentiableWithinAt ùïú Prod.fst s p :=
  differentiableAt_fst.differentiableWithinAt
#align differentiable_within_at_fst differentiableWithinAt_fst

@[fun_prop]
protected theorem DifferentiableWithinAt.fst (h : DifferentiableWithinAt ùïú f‚ÇÇ s x) :
    DifferentiableWithinAt ùïú (fun x => (f‚ÇÇ x).1) s x :=
  differentiableAt_fst.comp_differentiableWithinAt x h
#align differentiable_within_at.fst DifferentiableWithinAt.fst

@[fun_prop]
theorem differentiableOn_fst {s : Set (E √ó F)} : DifferentiableOn ùïú Prod.fst s :=
  differentiable_fst.differentiableOn
#align differentiable_on_fst differentiableOn_fst

@[fun_prop]
protected theorem DifferentiableOn.fst (h : DifferentiableOn ùïú f‚ÇÇ s) :
    DifferentiableOn ùïú (fun x => (f‚ÇÇ x).1) s :=
  differentiable_fst.comp_differentiableOn h
#align differentiable_on.fst DifferentiableOn.fst

theorem fderiv_fst : fderiv ùïú Prod.fst p = fst ùïú E F :=
  hasFDerivAt_fst.fderiv
#align fderiv_fst fderiv_fst

theorem fderiv.fst (h : DifferentiableAt ùïú f‚ÇÇ x) :
    fderiv ùïú (fun x => (f‚ÇÇ x).1) x = (fst ùïú F G).comp (fderiv ùïú f‚ÇÇ x) :=
  h.hasFDerivAt.fst.fderiv
#align fderiv.fst fderiv.fst

theorem fderivWithin_fst {s : Set (E √ó F)} (hs : UniqueDiffWithinAt ùïú s p) :
    fderivWithin ùïú Prod.fst s p = fst ùïú E F :=
  hasFDerivWithinAt_fst.fderivWithin hs
#align fderiv_within_fst fderivWithin_fst

theorem fderivWithin.fst (hs : UniqueDiffWithinAt ùïú s x) (h : DifferentiableWithinAt ùïú f‚ÇÇ s x) :
    fderivWithin ùïú (fun x => (f‚ÇÇ x).1) s x = (fst ùïú F G).comp (fderivWithin ùïú f‚ÇÇ s x) :=
  h.hasFDerivWithinAt.fst.fderivWithin hs
#align fderiv_within.fst fderivWithin.fst

end Fst

section Snd

variable {f‚ÇÇ : E ‚Üí F √ó G} {f‚ÇÇ' : E ‚ÜíL[ùïú] F √ó G} {p : E √ó F}

@[fun_prop]
theorem hasStrictFDerivAt_snd : HasStrictFDerivAt (@Prod.snd E F) (snd ùïú E F) p :=
  (snd ùïú E F).hasStrictFDerivAt
#align has_strict_fderiv_at_snd hasStrictFDerivAt_snd

@[fun_prop]
protected theorem HasStrictFDerivAt.snd (h : HasStrictFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasStrictFDerivAt (fun x => (f‚ÇÇ x).2) ((snd ùïú F G).comp f‚ÇÇ') x :=
  hasStrictFDerivAt_snd.comp x h
#align has_strict_fderiv_at.snd HasStrictFDerivAt.snd

theorem hasFDerivAtFilter_snd {L : Filter (E √ó F)} :
    HasFDerivAtFilter (@Prod.snd E F) (snd ùïú E F) p L :=
  (snd ùïú E F).hasFDerivAtFilter
#align has_fderiv_at_filter_snd hasFDerivAtFilter_snd

protected theorem HasFDerivAtFilter.snd (h : HasFDerivAtFilter f‚ÇÇ f‚ÇÇ' x L) :
    HasFDerivAtFilter (fun x => (f‚ÇÇ x).2) ((snd ùïú F G).comp f‚ÇÇ') x L :=
  hasFDerivAtFilter_snd.comp x h tendsto_map
#align has_fderiv_at_filter.snd HasFDerivAtFilter.snd

@[fun_prop]
theorem hasFDerivAt_snd : HasFDerivAt (@Prod.snd E F) (snd ùïú E F) p :=
  hasFDerivAtFilter_snd
#align has_fderiv_at_snd hasFDerivAt_snd

@[fun_prop]
protected nonrec theorem HasFDerivAt.snd (h : HasFDerivAt f‚ÇÇ f‚ÇÇ' x) :
    HasFDerivAt (fun x => (f‚ÇÇ x).2) ((snd ùïú F G).comp f‚ÇÇ') x :=
  h.snd
#align has_fderiv_at.snd HasFDerivAt.snd

@[fun_prop]
theorem hasFDerivWithinAt_snd {s : Set (E √ó F)} :
    HasFDerivWithinAt (@Prod.snd E F) (snd ùïú E F) s p :=
  hasFDerivAtFilter_snd
#align has_fderiv_within_at_snd hasFDerivWithinAt_snd

@[fun_prop]
protected nonrec theorem HasFDerivWithinAt.snd (h : HasFDerivWithinAt f‚ÇÇ f‚ÇÇ' s x) :
    HasFDerivWithinAt (fun x => (f‚ÇÇ x).2) ((snd ùïú F G).comp f‚ÇÇ') s x :=
  h.snd
#align has_fderiv_within_at.snd HasFDerivWithinAt.snd

@[fun_prop]
theorem differentiableAt_snd : DifferentiableAt ùïú Prod.snd p :=
  hasFDerivAt_snd.differentiableAt
#align differentiable_at_snd differentiableAt_snd

@[simp, fun_prop]
protected theorem DifferentiableAt.snd (h : DifferentiableAt ùïú f‚ÇÇ x) :
    DifferentiableAt ùïú (fun x => (f‚ÇÇ x).2) x :=
  differentiableAt_snd.comp x h
#align differentiable_at.snd DifferentiableAt.snd

@[fun_prop]
theorem differentiable_snd : Differentiable ùïú (Prod.snd : E √ó F ‚Üí F) := fun _ =>
  differentiableAt_snd
#align differentiable_snd differentiable_snd

@[simp, fun_prop]
protected theorem Differentiable.snd (h : Differentiable ùïú f‚ÇÇ) :
    Differentiable ùïú fun x => (f‚ÇÇ x).2 :=
  differentiable_snd.comp h
#align differentiable.snd Differentiable.snd

@[fun_prop]
theorem differentiableWithinAt_snd {s : Set (E √ó F)} : DifferentiableWithinAt ùïú Prod.snd s p :=
  differentiableAt_snd.differentiableWithinAt
#align differentiable_within_at_snd differentiableWithinAt_snd

@[fun_prop]
protected theorem DifferentiableWithinAt.snd (h : DifferentiableWithinAt ùïú f‚ÇÇ s x) :
    DifferentiableWithinAt ùïú (fun x => (f‚ÇÇ x).2) s x :=
  differentiableAt_snd.comp_differentiableWithinAt x h
#align differentiable_within_at.snd DifferentiableWithinAt.snd

@[fun_prop]
theorem differentiableOn_snd {s : Set (E √ó F)} : DifferentiableOn ùïú Prod.snd s :=
  differentiable_snd.differentiableOn
#align differentiable_on_snd differentiableOn_snd

@[fun_prop]
protected theorem DifferentiableOn.snd (h : DifferentiableOn ùïú f‚ÇÇ s) :
    DifferentiableOn ùïú (fun x => (f‚ÇÇ x).2) s :=
  differentiable_snd.comp_differentiableOn h
#align differentiable_on.snd DifferentiableOn.snd

theorem fderiv_snd : fderiv ùïú Prod.snd p = snd ùïú E F :=
  hasFDerivAt_snd.fderiv
#align fderiv_snd fderiv_snd

theorem fderiv.snd (h : DifferentiableAt ùïú f‚ÇÇ x) :
    fderiv ùïú (fun x => (f‚ÇÇ x).2) x = (snd ùïú F G).comp (fderiv ùïú f‚ÇÇ x) :=
  h.hasFDerivAt.snd.fderiv
#align fderiv.snd fderiv.snd

theorem fderivWithin_snd {s : Set (E √ó F)} (hs : UniqueDiffWithinAt ùïú s p) :
    fderivWithin ùïú Prod.snd s p = snd ùïú E F :=
  hasFDerivWithinAt_snd.fderivWithin hs
#align fderiv_within_snd fderivWithin_snd

theorem fderivWithin.snd (hs : UniqueDiffWithinAt ùïú s x) (h : DifferentiableWithinAt ùïú f‚ÇÇ s x) :
    fderivWithin ùïú (fun x => (f‚ÇÇ x).2) s x = (snd ùïú F G).comp (fderivWithin ùïú f‚ÇÇ s x) :=
  h.hasFDerivWithinAt.snd.fderivWithin hs
#align fderiv_within.snd fderivWithin.snd

end Snd

section prodMap

variable {f‚ÇÇ : G ‚Üí G'} {f‚ÇÇ' : G ‚ÜíL[ùïú] G'} {y : G} (p : E √ó G)

@[fun_prop]
protected theorem HasStrictFDerivAt.prodMap (hf : HasStrictFDerivAt f f' p.1)
    (hf‚ÇÇ : HasStrictFDerivAt f‚ÇÇ f‚ÇÇ' p.2) : HasStrictFDerivAt (Prod.map f f‚ÇÇ) (f'.prodMap f‚ÇÇ') p :=
  (hf.comp p hasStrictFDerivAt_fst).prod (hf‚ÇÇ.comp p hasStrictFDerivAt_snd)
#align has_strict_fderiv_at.prod_map HasStrictFDerivAt.prodMap

@[fun_prop]
protected theorem HasFDerivAt.prodMap (hf : HasFDerivAt f f' p.1) (hf‚ÇÇ : HasFDerivAt f‚ÇÇ f‚ÇÇ' p.2) :
    HasFDerivAt (Prod.map f f‚ÇÇ) (f'.prodMap f‚ÇÇ') p :=
  (hf.comp p hasFDerivAt_fst).prod (hf‚ÇÇ.comp p hasFDerivAt_snd)
#align has_fderiv_at.prod_map HasFDerivAt.prodMap

@[simp, fun_prop]
protected theorem DifferentiableAt.prod_map (hf : DifferentiableAt ùïú f p.1)
    (hf‚ÇÇ : DifferentiableAt ùïú f‚ÇÇ p.2) : DifferentiableAt ùïú (fun p : E √ó G => (f p.1, f‚ÇÇ p.2)) p :=
  (hf.comp p differentiableAt_fst).prod (hf‚ÇÇ.comp p differentiableAt_snd)
#align differentiable_at.prod_map DifferentiableAt.prod_map

end prodMap

section Pi

/-!
### Derivatives of functions `f : E ‚Üí Œ† i, F' i`

In this section we formulate `has*FDeriv*_pi` theorems as `iff`s, and provide two versions of each
theorem:

* the version without `'` deals with `œÜ : Œ† i, E ‚Üí F' i` and `œÜ' : Œ† i, E ‚ÜíL[ùïú] F' i`
  and is designed to deduce differentiability of `fun x i ‚Ü¶ œÜ i x` from differentiability
  of each `œÜ i`;
* the version with `'` deals with `Œ¶ : E ‚Üí Œ† i, F' i` and `Œ¶' : E ‚ÜíL[ùïú] Œ† i, F' i`
  and is designed to deduce differentiability of the components `fun x ‚Ü¶ Œ¶ x i` from
  differentiability of `Œ¶`.
-/


variable {Œπ : Type*} [Fintype Œπ] {F' : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (F' i)]
  [‚àÄ i, NormedSpace ùïú (F' i)] {œÜ : ‚àÄ i, E ‚Üí F' i} {œÜ' : ‚àÄ i, E ‚ÜíL[ùïú] F' i} {Œ¶ : E ‚Üí ‚àÄ i, F' i}
  {Œ¶' : E ‚ÜíL[ùïú] ‚àÄ i, F' i}

@[simp]
theorem hasStrictFDerivAt_pi' :
    HasStrictFDerivAt Œ¶ Œ¶' x ‚Üî ‚àÄ i, HasStrictFDerivAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') x := by
  simp only [HasStrictFDerivAt, ContinuousLinearMap.coe_pi]
  exact isLittleO_pi
#align has_strict_fderiv_at_pi' hasStrictFDerivAt_pi'

@[fun_prop]
theorem hasStrictFDerivAt_pi'' (hœÜ : ‚àÄ i, HasStrictFDerivAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') x) :
    HasStrictFDerivAt Œ¶ Œ¶' x := hasStrictFDerivAt_pi'.2 hœÜ

@[fun_prop]
theorem hasStrictFDerivAt_apply (i : Œπ) (f : ‚àÄ i, F' i) :
    HasStrictFDerivAt (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) (proj i) f := by
  let id' := ContinuousLinearMap.id ùïú (‚àÄ i, F' i)
  have h := ((hasStrictFDerivAt_pi'
             (Œ¶ := fun (f : ‚àÄ i, F' i) (i' : Œπ) => f i') (Œ¶':=id') (x:=f))).1
  have h' : comp (proj i) id' = proj i := by rfl
  rw [‚Üê h']; apply h; apply hasStrictFDerivAt_id

@[simp 1100] -- Porting note: increased priority to make lint happy
theorem hasStrictFDerivAt_pi :
    HasStrictFDerivAt (fun x i => œÜ i x) (ContinuousLinearMap.pi œÜ') x ‚Üî
      ‚àÄ i, HasStrictFDerivAt (œÜ i) (œÜ' i) x :=
  hasStrictFDerivAt_pi'
#align has_strict_fderiv_at_pi hasStrictFDerivAt_pi

@[simp]
theorem hasFDerivAtFilter_pi' :
    HasFDerivAtFilter Œ¶ Œ¶' x L ‚Üî
      ‚àÄ i, HasFDerivAtFilter (fun x => Œ¶ x i) ((proj i).comp Œ¶') x L := by
  simp only [hasFDerivAtFilter_iff_isLittleO, ContinuousLinearMap.coe_pi]
  exact isLittleO_pi
#align has_fderiv_at_filter_pi' hasFDerivAtFilter_pi'

theorem hasFDerivAtFilter_pi :
    HasFDerivAtFilter (fun x i => œÜ i x) (ContinuousLinearMap.pi œÜ') x L ‚Üî
      ‚àÄ i, HasFDerivAtFilter (œÜ i) (œÜ' i) x L :=
  hasFDerivAtFilter_pi'
#align has_fderiv_at_filter_pi hasFDerivAtFilter_pi

@[simp]
theorem hasFDerivAt_pi' :
    HasFDerivAt Œ¶ Œ¶' x ‚Üî ‚àÄ i, HasFDerivAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') x :=
  hasFDerivAtFilter_pi'
#align has_fderiv_at_pi' hasFDerivAt_pi'

@[fun_prop]
theorem hasFDerivAt_pi'' (hœÜ : ‚àÄ i, HasFDerivAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') x) :
    HasFDerivAt Œ¶ Œ¶' x := hasFDerivAt_pi'.2 hœÜ

@[fun_prop]
theorem hasFDerivAt_apply (i : Œπ) (f : ‚àÄ i, F' i) :
    HasFDerivAt (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) (proj i) f := by
  apply HasStrictFDerivAt.hasFDerivAt
  apply hasStrictFDerivAt_apply

theorem hasFDerivAt_pi :
    HasFDerivAt (fun x i => œÜ i x) (ContinuousLinearMap.pi œÜ') x ‚Üî
      ‚àÄ i, HasFDerivAt (œÜ i) (œÜ' i) x :=
  hasFDerivAtFilter_pi
#align has_fderiv_at_pi hasFDerivAt_pi

@[simp]
theorem hasFDerivWithinAt_pi' :
    HasFDerivWithinAt Œ¶ Œ¶' s x ‚Üî ‚àÄ i, HasFDerivWithinAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') s x :=
  hasFDerivAtFilter_pi'
#align has_fderiv_within_at_pi' hasFDerivWithinAt_pi'

@[fun_prop]
theorem hasFDerivWithinAt_pi''
    (hœÜ : ‚àÄ i, HasFDerivWithinAt (fun x => Œ¶ x i) ((proj i).comp Œ¶') s x) :
    HasFDerivWithinAt Œ¶ Œ¶' s x := hasFDerivWithinAt_pi'.2 hœÜ

@[fun_prop]
theorem hasFDerivWithinAt_apply (i : Œπ) (f : ‚àÄ i, F' i) (s' : Set (‚àÄ i, F' i)) :
    HasFDerivWithinAt (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) (proj i) s' f := by
  let id' := ContinuousLinearMap.id ùïú (‚àÄ i, F' i)
  have h := ((hasFDerivWithinAt_pi'
             (Œ¶ := fun (f : ‚àÄ i, F' i) (i' : Œπ) => f i') (Œ¶':=id') (x:=f) (s:=s'))).1
  have h' : comp (proj i) id' = proj i := by rfl
  rw [‚Üê h']; apply h; apply hasFDerivWithinAt_id

theorem hasFDerivWithinAt_pi :
    HasFDerivWithinAt (fun x i => œÜ i x) (ContinuousLinearMap.pi œÜ') s x ‚Üî
      ‚àÄ i, HasFDerivWithinAt (œÜ i) (œÜ' i) s x :=
  hasFDerivAtFilter_pi
#align has_fderiv_within_at_pi hasFDerivWithinAt_pi

@[simp]
theorem differentiableWithinAt_pi :
    DifferentiableWithinAt ùïú Œ¶ s x ‚Üî ‚àÄ i, DifferentiableWithinAt ùïú (fun x => Œ¶ x i) s x :=
  ‚ü®fun h i => (hasFDerivWithinAt_pi'.1 h.hasFDerivWithinAt i).differentiableWithinAt, fun h =>
    (hasFDerivWithinAt_pi.2 fun i => (h i).hasFDerivWithinAt).differentiableWithinAt‚ü©
#align differentiable_within_at_pi differentiableWithinAt_pi

@[fun_prop]
theorem differentiableWithinAt_pi'' (hœÜ : ‚àÄ i, DifferentiableWithinAt ùïú (fun x => Œ¶ x i) s x) :
    DifferentiableWithinAt ùïú Œ¶ s x := differentiableWithinAt_pi.2 hœÜ

@[fun_prop]
theorem differentiableWithinAt_apply (i : Œπ) (f : ‚àÄ i, F' i) (s' : Set (‚àÄ i, F' i)) :
    DifferentiableWithinAt (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) s' f := by
  apply HasFDerivWithinAt.differentiableWithinAt
  fun_prop

@[simp]
theorem differentiableAt_pi : DifferentiableAt ùïú Œ¶ x ‚Üî ‚àÄ i, DifferentiableAt ùïú (fun x => Œ¶ x i) x :=
  ‚ü®fun h i => (hasFDerivAt_pi'.1 h.hasFDerivAt i).differentiableAt, fun h =>
    (hasFDerivAt_pi.2 fun i => (h i).hasFDerivAt).differentiableAt‚ü©
#align differentiable_at_pi differentiableAt_pi

@[fun_prop]
theorem differentiableAt_pi'' (hœÜ : ‚àÄ i, DifferentiableAt ùïú (fun x => Œ¶ x i) x) :
    DifferentiableAt ùïú Œ¶ x := differentiableAt_pi.2 hœÜ

@[fun_prop]
theorem differentiableAt_apply (i : Œπ) (f : ‚àÄ i, F' i) :
    DifferentiableAt (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) f := by
  have h := ((differentiableAt_pi (ùïú:=ùïú)
             (Œ¶ := fun (f : ‚àÄ i, F' i) (i' : Œπ) => f i') (x:=f))).1
  apply h; apply differentiableAt_id

theorem differentiableOn_pi : DifferentiableOn ùïú Œ¶ s ‚Üî ‚àÄ i, DifferentiableOn ùïú (fun x => Œ¶ x i) s :=
  ‚ü®fun h i x hx => differentiableWithinAt_pi.1 (h x hx) i, fun h x hx =>
    differentiableWithinAt_pi.2 fun i => h i x hx‚ü©
#align differentiable_on_pi differentiableOn_pi

@[fun_prop]
theorem differentiableOn_pi'' (hœÜ : ‚àÄ i, DifferentiableOn ùïú (fun x => Œ¶ x i) s) :
    DifferentiableOn ùïú Œ¶ s := differentiableOn_pi.2 hœÜ

@[fun_prop]
theorem differentiableOn_apply (i : Œπ) (s' : Set (‚àÄ i, F' i)) :
    DifferentiableOn (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) s' := by
  have h := ((differentiableOn_pi (ùïú:=ùïú)
             (Œ¶ := fun (f : ‚àÄ i, F' i) (i' : Œπ) => f i') (s:=s'))).1
  apply h; apply differentiableOn_id

theorem differentiable_pi : Differentiable ùïú Œ¶ ‚Üî ‚àÄ i, Differentiable ùïú fun x => Œ¶ x i :=
  ‚ü®fun h i x => differentiableAt_pi.1 (h x) i, fun h x => differentiableAt_pi.2 fun i => h i x‚ü©
#align differentiable_pi differentiable_pi

@[fun_prop]
theorem differentiable_pi'' (hœÜ : ‚àÄ i, Differentiable ùïú fun x => Œ¶ x i) :
    Differentiable ùïú Œ¶ := differentiable_pi.2 hœÜ

@[fun_prop]
theorem differentiable_apply (i : Œπ) :
    Differentiable (ùïú:=ùïú) (fun f : ‚àÄ i, F' i => f i) := by intro x; apply differentiableAt_apply

-- TODO: find out which version (`œÜ` or `Œ¶`) works better with `rw`/`simp`
theorem fderivWithin_pi (h : ‚àÄ i, DifferentiableWithinAt ùïú (œÜ i) s x)
    (hs : UniqueDiffWithinAt ùïú s x) :
    fderivWithin ùïú (fun x i => œÜ i x) s x = pi fun i => fderivWithin ùïú (œÜ i) s x :=
  (hasFDerivWithinAt_pi.2 fun i => (h i).hasFDerivWithinAt).fderivWithin hs
#align fderiv_within_pi fderivWithin_pi

theorem fderiv_pi (h : ‚àÄ i, DifferentiableAt ùïú (œÜ i) x) :
    fderiv ùïú (fun x i => œÜ i x) x = pi fun i => fderiv ùïú (œÜ i) x :=
  (hasFDerivAt_pi.2 fun i => (h i).hasFDerivAt).fderiv
#align fderiv_pi fderiv_pi

end Pi

end CartesianProduct

end
