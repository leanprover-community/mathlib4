/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

/-!
# Derivative of the cartesian product of functions

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
cartesian products of functions, and functions into Pi-types.
-/


open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable (e : E â†’L[ğ•œ] F)
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

section CartesianProduct

/-! ### Derivative of the cartesian product of two functions -/


section Prod

variable {fâ‚‚ : E â†’ G} {fâ‚‚' : E â†’L[ğ•œ] G}

protected theorem HasStrictFDerivAt.prod (hfâ‚ : HasStrictFDerivAt fâ‚ fâ‚' x)
    (hfâ‚‚ : HasStrictFDerivAt fâ‚‚ fâ‚‚' x) :
    HasStrictFDerivAt (fun x => (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') x :=
  .of_isLittleO <| hfâ‚.isLittleO.prod_left hfâ‚‚.isLittleO

theorem HasFDerivAtFilter.prod (hfâ‚ : HasFDerivAtFilter fâ‚ fâ‚' x L)
    (hfâ‚‚ : HasFDerivAtFilter fâ‚‚ fâ‚‚' x L) :
    HasFDerivAtFilter (fun x => (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') x L :=
  .of_isLittleO <| hfâ‚.isLittleO.prod_left hfâ‚‚.isLittleO

@[fun_prop]
nonrec theorem HasFDerivWithinAt.prod (hfâ‚ : HasFDerivWithinAt fâ‚ fâ‚' s x)
    (hfâ‚‚ : HasFDerivWithinAt fâ‚‚ fâ‚‚' s x) :
    HasFDerivWithinAt (fun x => (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') s x :=
  hfâ‚.prod hfâ‚‚

@[fun_prop]
nonrec theorem HasFDerivAt.prod (hfâ‚ : HasFDerivAt fâ‚ fâ‚' x) (hfâ‚‚ : HasFDerivAt fâ‚‚ fâ‚‚' x) :
    HasFDerivAt (fun x => (fâ‚ x, fâ‚‚ x)) (fâ‚'.prod fâ‚‚') x :=
  hfâ‚.prod hfâ‚‚

@[fun_prop]
theorem hasFDerivAt_prod_mk_left (eâ‚€ : E) (fâ‚€ : F) :
    HasFDerivAt (fun e : E => (e, fâ‚€)) (inl ğ•œ E F) eâ‚€ :=
  (hasFDerivAt_id eâ‚€).prod (hasFDerivAt_const fâ‚€ eâ‚€)

@[fun_prop]
theorem hasFDerivAt_prod_mk_right (eâ‚€ : E) (fâ‚€ : F) :
    HasFDerivAt (fun f : F => (eâ‚€, f)) (inr ğ•œ E F) fâ‚€ :=
  (hasFDerivAt_const eâ‚€ fâ‚€).prod (hasFDerivAt_id fâ‚€)

@[fun_prop]
theorem DifferentiableWithinAt.prod (hfâ‚ : DifferentiableWithinAt ğ•œ fâ‚ s x)
    (hfâ‚‚ : DifferentiableWithinAt ğ•œ fâ‚‚ s x) :
    DifferentiableWithinAt ğ•œ (fun x : E => (fâ‚ x, fâ‚‚ x)) s x :=
  (hfâ‚.hasFDerivWithinAt.prod hfâ‚‚.hasFDerivWithinAt).differentiableWithinAt

@[simp, fun_prop]
theorem DifferentiableAt.prod (hfâ‚ : DifferentiableAt ğ•œ fâ‚ x) (hfâ‚‚ : DifferentiableAt ğ•œ fâ‚‚ x) :
    DifferentiableAt ğ•œ (fun x : E => (fâ‚ x, fâ‚‚ x)) x :=
  (hfâ‚.hasFDerivAt.prod hfâ‚‚.hasFDerivAt).differentiableAt

@[fun_prop]
theorem DifferentiableOn.prod (hfâ‚ : DifferentiableOn ğ•œ fâ‚ s) (hfâ‚‚ : DifferentiableOn ğ•œ fâ‚‚ s) :
    DifferentiableOn ğ•œ (fun x : E => (fâ‚ x, fâ‚‚ x)) s := fun x hx =>
  DifferentiableWithinAt.prod (hfâ‚ x hx) (hfâ‚‚ x hx)

@[simp, fun_prop]
theorem Differentiable.prod (hfâ‚ : Differentiable ğ•œ fâ‚) (hfâ‚‚ : Differentiable ğ•œ fâ‚‚) :
    Differentiable ğ•œ fun x : E => (fâ‚ x, fâ‚‚ x) := fun x => DifferentiableAt.prod (hfâ‚ x) (hfâ‚‚ x)

theorem DifferentiableAt.fderiv_prod (hfâ‚ : DifferentiableAt ğ•œ fâ‚ x)
    (hfâ‚‚ : DifferentiableAt ğ•œ fâ‚‚ x) :
    fderiv ğ•œ (fun x : E => (fâ‚ x, fâ‚‚ x)) x = (fderiv ğ•œ fâ‚ x).prod (fderiv ğ•œ fâ‚‚ x) :=
  (hfâ‚.hasFDerivAt.prod hfâ‚‚.hasFDerivAt).fderiv

theorem DifferentiableWithinAt.fderivWithin_prod (hfâ‚ : DifferentiableWithinAt ğ•œ fâ‚ s x)
    (hfâ‚‚ : DifferentiableWithinAt ğ•œ fâ‚‚ s x) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x : E => (fâ‚ x, fâ‚‚ x)) s x =
      (fderivWithin ğ•œ fâ‚ s x).prod (fderivWithin ğ•œ fâ‚‚ s x) :=
  (hfâ‚.hasFDerivWithinAt.prod hfâ‚‚.hasFDerivWithinAt).fderivWithin hxs

end Prod

section Fst

variable {fâ‚‚ : E â†’ F Ã— G} {fâ‚‚' : E â†’L[ğ•œ] F Ã— G} {p : E Ã— F}

@[fun_prop]
theorem hasStrictFDerivAt_fst : HasStrictFDerivAt (@Prod.fst E F) (fst ğ•œ E F) p :=
  (fst ğ•œ E F).hasStrictFDerivAt

@[fun_prop]
protected theorem HasStrictFDerivAt.fst (h : HasStrictFDerivAt fâ‚‚ fâ‚‚' x) :
    HasStrictFDerivAt (fun x => (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x :=
  hasStrictFDerivAt_fst.comp x h

theorem hasFDerivAtFilter_fst {L : Filter (E Ã— F)} :
    HasFDerivAtFilter (@Prod.fst E F) (fst ğ•œ E F) p L :=
  (fst ğ•œ E F).hasFDerivAtFilter

protected theorem HasFDerivAtFilter.fst (h : HasFDerivAtFilter fâ‚‚ fâ‚‚' x L) :
    HasFDerivAtFilter (fun x => (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x L :=
  hasFDerivAtFilter_fst.comp x h tendsto_map

@[fun_prop]
theorem hasFDerivAt_fst : HasFDerivAt (@Prod.fst E F) (fst ğ•œ E F) p :=
  hasFDerivAtFilter_fst

@[fun_prop]
protected nonrec theorem HasFDerivAt.fst (h : HasFDerivAt fâ‚‚ fâ‚‚' x) :
    HasFDerivAt (fun x => (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') x :=
  h.fst

@[fun_prop]
theorem hasFDerivWithinAt_fst {s : Set (E Ã— F)} :
    HasFDerivWithinAt (@Prod.fst E F) (fst ğ•œ E F) s p :=
  hasFDerivAtFilter_fst

@[fun_prop]
protected nonrec theorem HasFDerivWithinAt.fst (h : HasFDerivWithinAt fâ‚‚ fâ‚‚' s x) :
    HasFDerivWithinAt (fun x => (fâ‚‚ x).1) ((fst ğ•œ F G).comp fâ‚‚') s x :=
  h.fst

@[fun_prop]
theorem differentiableAt_fst : DifferentiableAt ğ•œ Prod.fst p :=
  hasFDerivAt_fst.differentiableAt

@[simp, fun_prop]
protected theorem DifferentiableAt.fst (h : DifferentiableAt ğ•œ fâ‚‚ x) :
    DifferentiableAt ğ•œ (fun x => (fâ‚‚ x).1) x :=
  differentiableAt_fst.comp x h

@[fun_prop]
theorem differentiable_fst : Differentiable ğ•œ (Prod.fst : E Ã— F â†’ E) := fun _ =>
  differentiableAt_fst

@[simp, fun_prop]
protected theorem Differentiable.fst (h : Differentiable ğ•œ fâ‚‚) :
    Differentiable ğ•œ fun x => (fâ‚‚ x).1 :=
  differentiable_fst.comp h

@[fun_prop]
theorem differentiableWithinAt_fst {s : Set (E Ã— F)} : DifferentiableWithinAt ğ•œ Prod.fst s p :=
  differentiableAt_fst.differentiableWithinAt

@[fun_prop]
protected theorem DifferentiableWithinAt.fst (h : DifferentiableWithinAt ğ•œ fâ‚‚ s x) :
    DifferentiableWithinAt ğ•œ (fun x => (fâ‚‚ x).1) s x :=
  differentiableAt_fst.comp_differentiableWithinAt x h

@[fun_prop]
theorem differentiableOn_fst {s : Set (E Ã— F)} : DifferentiableOn ğ•œ Prod.fst s :=
  differentiable_fst.differentiableOn

@[fun_prop]
protected theorem DifferentiableOn.fst (h : DifferentiableOn ğ•œ fâ‚‚ s) :
    DifferentiableOn ğ•œ (fun x => (fâ‚‚ x).1) s :=
  differentiable_fst.comp_differentiableOn h

theorem fderiv_fst : fderiv ğ•œ Prod.fst p = fst ğ•œ E F :=
  hasFDerivAt_fst.fderiv

theorem fderiv.fst (h : DifferentiableAt ğ•œ fâ‚‚ x) :
    fderiv ğ•œ (fun x => (fâ‚‚ x).1) x = (fst ğ•œ F G).comp (fderiv ğ•œ fâ‚‚ x) :=
  h.hasFDerivAt.fst.fderiv

theorem fderivWithin_fst {s : Set (E Ã— F)} (hs : UniqueDiffWithinAt ğ•œ s p) :
    fderivWithin ğ•œ Prod.fst s p = fst ğ•œ E F :=
  hasFDerivWithinAt_fst.fderivWithin hs

theorem fderivWithin.fst (hs : UniqueDiffWithinAt ğ•œ s x) (h : DifferentiableWithinAt ğ•œ fâ‚‚ s x) :
    fderivWithin ğ•œ (fun x => (fâ‚‚ x).1) s x = (fst ğ•œ F G).comp (fderivWithin ğ•œ fâ‚‚ s x) :=
  h.hasFDerivWithinAt.fst.fderivWithin hs

end Fst

section Snd

variable {fâ‚‚ : E â†’ F Ã— G} {fâ‚‚' : E â†’L[ğ•œ] F Ã— G} {p : E Ã— F}

@[fun_prop]
theorem hasStrictFDerivAt_snd : HasStrictFDerivAt (@Prod.snd E F) (snd ğ•œ E F) p :=
  (snd ğ•œ E F).hasStrictFDerivAt

@[fun_prop]
protected theorem HasStrictFDerivAt.snd (h : HasStrictFDerivAt fâ‚‚ fâ‚‚' x) :
    HasStrictFDerivAt (fun x => (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x :=
  hasStrictFDerivAt_snd.comp x h

theorem hasFDerivAtFilter_snd {L : Filter (E Ã— F)} :
    HasFDerivAtFilter (@Prod.snd E F) (snd ğ•œ E F) p L :=
  (snd ğ•œ E F).hasFDerivAtFilter

protected theorem HasFDerivAtFilter.snd (h : HasFDerivAtFilter fâ‚‚ fâ‚‚' x L) :
    HasFDerivAtFilter (fun x => (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x L :=
  hasFDerivAtFilter_snd.comp x h tendsto_map

@[fun_prop]
theorem hasFDerivAt_snd : HasFDerivAt (@Prod.snd E F) (snd ğ•œ E F) p :=
  hasFDerivAtFilter_snd

@[fun_prop]
protected nonrec theorem HasFDerivAt.snd (h : HasFDerivAt fâ‚‚ fâ‚‚' x) :
    HasFDerivAt (fun x => (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') x :=
  h.snd

@[fun_prop]
theorem hasFDerivWithinAt_snd {s : Set (E Ã— F)} :
    HasFDerivWithinAt (@Prod.snd E F) (snd ğ•œ E F) s p :=
  hasFDerivAtFilter_snd

@[fun_prop]
protected nonrec theorem HasFDerivWithinAt.snd (h : HasFDerivWithinAt fâ‚‚ fâ‚‚' s x) :
    HasFDerivWithinAt (fun x => (fâ‚‚ x).2) ((snd ğ•œ F G).comp fâ‚‚') s x :=
  h.snd

@[fun_prop]
theorem differentiableAt_snd : DifferentiableAt ğ•œ Prod.snd p :=
  hasFDerivAt_snd.differentiableAt

@[simp, fun_prop]
protected theorem DifferentiableAt.snd (h : DifferentiableAt ğ•œ fâ‚‚ x) :
    DifferentiableAt ğ•œ (fun x => (fâ‚‚ x).2) x :=
  differentiableAt_snd.comp x h

@[fun_prop]
theorem differentiable_snd : Differentiable ğ•œ (Prod.snd : E Ã— F â†’ F) := fun _ =>
  differentiableAt_snd

@[simp, fun_prop]
protected theorem Differentiable.snd (h : Differentiable ğ•œ fâ‚‚) :
    Differentiable ğ•œ fun x => (fâ‚‚ x).2 :=
  differentiable_snd.comp h

@[fun_prop]
theorem differentiableWithinAt_snd {s : Set (E Ã— F)} : DifferentiableWithinAt ğ•œ Prod.snd s p :=
  differentiableAt_snd.differentiableWithinAt

@[fun_prop]
protected theorem DifferentiableWithinAt.snd (h : DifferentiableWithinAt ğ•œ fâ‚‚ s x) :
    DifferentiableWithinAt ğ•œ (fun x => (fâ‚‚ x).2) s x :=
  differentiableAt_snd.comp_differentiableWithinAt x h

@[fun_prop]
theorem differentiableOn_snd {s : Set (E Ã— F)} : DifferentiableOn ğ•œ Prod.snd s :=
  differentiable_snd.differentiableOn

@[fun_prop]
protected theorem DifferentiableOn.snd (h : DifferentiableOn ğ•œ fâ‚‚ s) :
    DifferentiableOn ğ•œ (fun x => (fâ‚‚ x).2) s :=
  differentiable_snd.comp_differentiableOn h

theorem fderiv_snd : fderiv ğ•œ Prod.snd p = snd ğ•œ E F :=
  hasFDerivAt_snd.fderiv

theorem fderiv.snd (h : DifferentiableAt ğ•œ fâ‚‚ x) :
    fderiv ğ•œ (fun x => (fâ‚‚ x).2) x = (snd ğ•œ F G).comp (fderiv ğ•œ fâ‚‚ x) :=
  h.hasFDerivAt.snd.fderiv

theorem fderivWithin_snd {s : Set (E Ã— F)} (hs : UniqueDiffWithinAt ğ•œ s p) :
    fderivWithin ğ•œ Prod.snd s p = snd ğ•œ E F :=
  hasFDerivWithinAt_snd.fderivWithin hs

theorem fderivWithin.snd (hs : UniqueDiffWithinAt ğ•œ s x) (h : DifferentiableWithinAt ğ•œ fâ‚‚ s x) :
    fderivWithin ğ•œ (fun x => (fâ‚‚ x).2) s x = (snd ğ•œ F G).comp (fderivWithin ğ•œ fâ‚‚ s x) :=
  h.hasFDerivWithinAt.snd.fderivWithin hs

end Snd

section prodMap

variable {fâ‚‚ : G â†’ G'} {fâ‚‚' : G â†’L[ğ•œ] G'} {y : G} (p : E Ã— G)

@[fun_prop]
protected theorem HasStrictFDerivAt.prodMap (hf : HasStrictFDerivAt f f' p.1)
    (hfâ‚‚ : HasStrictFDerivAt fâ‚‚ fâ‚‚' p.2) : HasStrictFDerivAt (Prod.map f fâ‚‚) (f'.prodMap fâ‚‚') p :=
  (hf.comp p hasStrictFDerivAt_fst).prod (hfâ‚‚.comp p hasStrictFDerivAt_snd)

@[fun_prop]
protected theorem HasFDerivAt.prodMap (hf : HasFDerivAt f f' p.1) (hfâ‚‚ : HasFDerivAt fâ‚‚ fâ‚‚' p.2) :
    HasFDerivAt (Prod.map f fâ‚‚) (f'.prodMap fâ‚‚') p :=
  (hf.comp p hasFDerivAt_fst).prod (hfâ‚‚.comp p hasFDerivAt_snd)

@[simp, fun_prop]
protected theorem DifferentiableAt.prod_map (hf : DifferentiableAt ğ•œ f p.1)
    (hfâ‚‚ : DifferentiableAt ğ•œ fâ‚‚ p.2) : DifferentiableAt ğ•œ (fun p : E Ã— G => (f p.1, fâ‚‚ p.2)) p :=
  (hf.comp p differentiableAt_fst).prod (hfâ‚‚.comp p differentiableAt_snd)

end prodMap

section Pi

/-!
### Derivatives of functions `f : E â†’ Î  i, F' i`

In this section we formulate `has*FDeriv*_pi` theorems as `iff`s, and provide two versions of each
theorem:

* the version without `'` deals with `Ï† : Î  i, E â†’ F' i` and `Ï†' : Î  i, E â†’L[ğ•œ] F' i`
  and is designed to deduce differentiability of `fun x i â†¦ Ï† i x` from differentiability
  of each `Ï† i`;
* the version with `'` deals with `Î¦ : E â†’ Î  i, F' i` and `Î¦' : E â†’L[ğ•œ] Î  i, F' i`
  and is designed to deduce differentiability of the components `fun x â†¦ Î¦ x i` from
  differentiability of `Î¦`.
-/


variable {Î¹ : Type*} [Fintype Î¹] {F' : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (F' i)]
  [âˆ€ i, NormedSpace ğ•œ (F' i)] {Ï† : âˆ€ i, E â†’ F' i} {Ï†' : âˆ€ i, E â†’L[ğ•œ] F' i} {Î¦ : E â†’ âˆ€ i, F' i}
  {Î¦' : E â†’L[ğ•œ] âˆ€ i, F' i}

@[simp]
theorem hasStrictFDerivAt_pi' :
    HasStrictFDerivAt Î¦ Î¦' x â†” âˆ€ i, HasStrictFDerivAt (fun x => Î¦ x i) ((proj i).comp Î¦') x := by
  simp only [hasStrictFDerivAt_iff_isLittleO, ContinuousLinearMap.coe_pi]
  exact isLittleO_pi

@[fun_prop]
theorem hasStrictFDerivAt_pi'' (hÏ† : âˆ€ i, HasStrictFDerivAt (fun x => Î¦ x i) ((proj i).comp Î¦') x) :
    HasStrictFDerivAt Î¦ Î¦' x := hasStrictFDerivAt_pi'.2 hÏ†

@[fun_prop]
theorem hasStrictFDerivAt_apply (i : Î¹) (f : âˆ€ i, F' i) :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) (proj i) f := by
  let id' := ContinuousLinearMap.id ğ•œ (âˆ€ i, F' i)
  have h := ((hasStrictFDerivAt_pi'
             (Î¦ := fun (f : âˆ€ i, F' i) (i' : Î¹) => f i') (Î¦' := id') (x := f))).1
  have h' : comp (proj i) id' = proj i := by rfl
  rw [â† h']; apply h; apply hasStrictFDerivAt_id

@[simp 1100] -- Porting note: increased priority to make lint happy
theorem hasStrictFDerivAt_pi :
    HasStrictFDerivAt (fun x i => Ï† i x) (ContinuousLinearMap.pi Ï†') x â†”
      âˆ€ i, HasStrictFDerivAt (Ï† i) (Ï†' i) x :=
  hasStrictFDerivAt_pi'

@[simp]
theorem hasFDerivAtFilter_pi' :
    HasFDerivAtFilter Î¦ Î¦' x L â†”
      âˆ€ i, HasFDerivAtFilter (fun x => Î¦ x i) ((proj i).comp Î¦') x L := by
  simp only [hasFDerivAtFilter_iff_isLittleO, ContinuousLinearMap.coe_pi]
  exact isLittleO_pi

theorem hasFDerivAtFilter_pi :
    HasFDerivAtFilter (fun x i => Ï† i x) (ContinuousLinearMap.pi Ï†') x L â†”
      âˆ€ i, HasFDerivAtFilter (Ï† i) (Ï†' i) x L :=
  hasFDerivAtFilter_pi'

@[simp]
theorem hasFDerivAt_pi' :
    HasFDerivAt Î¦ Î¦' x â†” âˆ€ i, HasFDerivAt (fun x => Î¦ x i) ((proj i).comp Î¦') x :=
  hasFDerivAtFilter_pi'

@[fun_prop]
theorem hasFDerivAt_pi'' (hÏ† : âˆ€ i, HasFDerivAt (fun x => Î¦ x i) ((proj i).comp Î¦') x) :
    HasFDerivAt Î¦ Î¦' x := hasFDerivAt_pi'.2 hÏ†

@[fun_prop]
theorem hasFDerivAt_apply (i : Î¹) (f : âˆ€ i, F' i) :
    HasFDerivAt (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) (proj i) f := by
  apply HasStrictFDerivAt.hasFDerivAt
  apply hasStrictFDerivAt_apply

theorem hasFDerivAt_pi :
    HasFDerivAt (fun x i => Ï† i x) (ContinuousLinearMap.pi Ï†') x â†”
      âˆ€ i, HasFDerivAt (Ï† i) (Ï†' i) x :=
  hasFDerivAtFilter_pi

@[simp]
theorem hasFDerivWithinAt_pi' :
    HasFDerivWithinAt Î¦ Î¦' s x â†” âˆ€ i, HasFDerivWithinAt (fun x => Î¦ x i) ((proj i).comp Î¦') s x :=
  hasFDerivAtFilter_pi'

@[fun_prop]
theorem hasFDerivWithinAt_pi''
    (hÏ† : âˆ€ i, HasFDerivWithinAt (fun x => Î¦ x i) ((proj i).comp Î¦') s x) :
    HasFDerivWithinAt Î¦ Î¦' s x := hasFDerivWithinAt_pi'.2 hÏ†

@[fun_prop]
theorem hasFDerivWithinAt_apply (i : Î¹) (f : âˆ€ i, F' i) (s' : Set (âˆ€ i, F' i)) :
    HasFDerivWithinAt (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) (proj i) s' f := by
  let id' := ContinuousLinearMap.id ğ•œ (âˆ€ i, F' i)
  have h := ((hasFDerivWithinAt_pi'
             (Î¦ := fun (f : âˆ€ i, F' i) (i' : Î¹) => f i') (Î¦' := id') (x := f) (s := s'))).1
  have h' : comp (proj i) id' = proj i := by rfl
  rw [â† h']; apply h; apply hasFDerivWithinAt_id

theorem hasFDerivWithinAt_pi :
    HasFDerivWithinAt (fun x i => Ï† i x) (ContinuousLinearMap.pi Ï†') s x â†”
      âˆ€ i, HasFDerivWithinAt (Ï† i) (Ï†' i) s x :=
  hasFDerivAtFilter_pi

@[simp]
theorem differentiableWithinAt_pi :
    DifferentiableWithinAt ğ•œ Î¦ s x â†” âˆ€ i, DifferentiableWithinAt ğ•œ (fun x => Î¦ x i) s x :=
  âŸ¨fun h i => (hasFDerivWithinAt_pi'.1 h.hasFDerivWithinAt i).differentiableWithinAt, fun h =>
    (hasFDerivWithinAt_pi.2 fun i => (h i).hasFDerivWithinAt).differentiableWithinAtâŸ©

@[fun_prop]
theorem differentiableWithinAt_pi'' (hÏ† : âˆ€ i, DifferentiableWithinAt ğ•œ (fun x => Î¦ x i) s x) :
    DifferentiableWithinAt ğ•œ Î¦ s x := differentiableWithinAt_pi.2 hÏ†

@[fun_prop]
theorem differentiableWithinAt_apply (i : Î¹) (f : âˆ€ i, F' i) (s' : Set (âˆ€ i, F' i)) :
    DifferentiableWithinAt (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) s' f := by
  apply HasFDerivWithinAt.differentiableWithinAt
  fun_prop

@[simp]
theorem differentiableAt_pi : DifferentiableAt ğ•œ Î¦ x â†” âˆ€ i, DifferentiableAt ğ•œ (fun x => Î¦ x i) x :=
  âŸ¨fun h i => (hasFDerivAt_pi'.1 h.hasFDerivAt i).differentiableAt, fun h =>
    (hasFDerivAt_pi.2 fun i => (h i).hasFDerivAt).differentiableAtâŸ©

@[fun_prop]
theorem differentiableAt_pi'' (hÏ† : âˆ€ i, DifferentiableAt ğ•œ (fun x => Î¦ x i) x) :
    DifferentiableAt ğ•œ Î¦ x := differentiableAt_pi.2 hÏ†

@[fun_prop]
theorem differentiableAt_apply (i : Î¹) (f : âˆ€ i, F' i) :
    DifferentiableAt (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) f := by
  have h := ((differentiableAt_pi (ğ•œ := ğ•œ)
             (Î¦ := fun (f : âˆ€ i, F' i) (i' : Î¹) => f i') (x := f))).1
  apply h; apply differentiableAt_id

theorem differentiableOn_pi : DifferentiableOn ğ•œ Î¦ s â†” âˆ€ i, DifferentiableOn ğ•œ (fun x => Î¦ x i) s :=
  âŸ¨fun h i x hx => differentiableWithinAt_pi.1 (h x hx) i, fun h x hx =>
    differentiableWithinAt_pi.2 fun i => h i x hxâŸ©

@[fun_prop]
theorem differentiableOn_pi'' (hÏ† : âˆ€ i, DifferentiableOn ğ•œ (fun x => Î¦ x i) s) :
    DifferentiableOn ğ•œ Î¦ s := differentiableOn_pi.2 hÏ†

@[fun_prop]
theorem differentiableOn_apply (i : Î¹) (s' : Set (âˆ€ i, F' i)) :
    DifferentiableOn (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) s' := by
  have h := ((differentiableOn_pi (ğ•œ := ğ•œ)
             (Î¦ := fun (f : âˆ€ i, F' i) (i' : Î¹) => f i') (s := s'))).1
  apply h; apply differentiableOn_id

theorem differentiable_pi : Differentiable ğ•œ Î¦ â†” âˆ€ i, Differentiable ğ•œ fun x => Î¦ x i :=
  âŸ¨fun h i x => differentiableAt_pi.1 (h x) i, fun h x => differentiableAt_pi.2 fun i => h i xâŸ©

@[fun_prop]
theorem differentiable_pi'' (hÏ† : âˆ€ i, Differentiable ğ•œ fun x => Î¦ x i) :
    Differentiable ğ•œ Î¦ := differentiable_pi.2 hÏ†

@[fun_prop]
theorem differentiable_apply (i : Î¹) :
    Differentiable (ğ•œ := ğ•œ) (fun f : âˆ€ i, F' i => f i) := by intro x; apply differentiableAt_apply

-- TODO: find out which version (`Ï†` or `Î¦`) works better with `rw`/`simp`
theorem fderivWithin_pi (h : âˆ€ i, DifferentiableWithinAt ğ•œ (Ï† i) s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x i => Ï† i x) s x = pi fun i => fderivWithin ğ•œ (Ï† i) s x :=
  (hasFDerivWithinAt_pi.2 fun i => (h i).hasFDerivWithinAt).fderivWithin hs

theorem fderiv_pi (h : âˆ€ i, DifferentiableAt ğ•œ (Ï† i) x) :
    fderiv ğ•œ (fun x i => Ï† i x) x = pi fun i => fderiv ğ•œ (Ï† i) x :=
  (hasFDerivAt_pi.2 fun i => (h i).hasFDerivAt).fderiv

end Pi

/-!
### Derivatives of tuples `f : E â†’ Î  i : Fin n.succ, F' i`

These can be used to prove results about functions of the form `fun x â†¦ ![f x, g x, h x]`,
as `Matrix.vecCons` is defeq to `Fin.cons`.
-/
section PiFin

section TVS

variable {n : â„•} {ğ•œ : Type*} {Î± : Fin n.succ â†’ Type*}
variable [Semiring ğ•œ]
variable [âˆ€ i, AddCommMonoid (Î± i)] [âˆ€ i, Module ğ•œ (Î± i)] [âˆ€ i, TopologicalSpace (Î± i)]

-- TODO: find a better home for this definition
variable (ğ•œ Î±) in
/-- `Fin.consEquiv` as a continuous linear equivalence.  -/
@[simps]
def Fin.consEquivL : (Î± 0 Ã— Î  i, Î± (Fin.succ i)) â‰ƒL[ğ•œ] (Î  i, Î± i) where
  __ := Fin.consEquiv Î±
  map_add' x y := funext <| Fin.cases rfl (by simp)
  map_smul' c x := funext <| Fin.cases rfl (by simp)
  continuous_toFun := continuous_id.fst.finCons continuous_id.snd
  continuous_invFun := .prod_mk (continuous_apply 0) (by continuity)

/-- `Fin.cons` in the codomain of continuous linear maps. -/
abbrev ContinuousLinearMap.finCons
    {E : Type*} [AddCommMonoid E] [Module ğ•œ E] [TopologicalSpace E]
    (Ï†' : E â†’L[ğ•œ] Î± 0) (Ï†s' : E â†’L[ğ•œ] Î  i, Î± (Fin.succ i)) :
    E â†’L[ğ•œ] Î  i, Î± i :=
  Fin.consEquivL ğ•œ Î± âˆ˜L Ï†'.prod Ï†s'

end TVS

variable {n : Nat} {F' : Fin n.succ â†’ Type*}
variable [âˆ€ i, NormedAddCommGroup (F' i)] [âˆ€ i, NormedSpace ğ•œ (F' i)]
variable {Ï† : E â†’ F' 0} {Ï†s : E â†’ âˆ€ i, F' (Fin.succ i)}

theorem hasStrictFDerivAt_finCons {Ï†' : E â†’L[ğ•œ] Î  i, F' i} :
    HasStrictFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) Ï†' x â†”
      HasStrictFDerivAt Ï† (.proj 0 âˆ˜L Ï†') x âˆ§
      HasStrictFDerivAt Ï†s (Pi.compRightL ğ•œ F' Fin.succ âˆ˜L Ï†') x := by
  rw [hasStrictFDerivAt_pi', Fin.forall_fin_succ, hasStrictFDerivAt_pi']
  dsimp [ContinuousLinearMap.comp, LinearMap.comp, Function.comp_def]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `hasStrictFDerivAt_finCons` where the derivative variables are free on the RHS
instead. -/
theorem hasStrictFDerivAt_finCons'
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)} :
    HasStrictFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x â†”
      HasStrictFDerivAt Ï† Ï†' x âˆ§ HasStrictFDerivAt Ï†s Ï†s' x :=
  hasStrictFDerivAt_finCons

@[fun_prop]
theorem HasStrictFDerivAt.finCons
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)}
    (h : HasStrictFDerivAt Ï† Ï†' x) (hs : HasStrictFDerivAt Ï†s Ï†s' x) :
    HasStrictFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x :=
  hasStrictFDerivAt_finCons'.mpr âŸ¨h, hsâŸ©

theorem hasFDerivAtFilter_finCons
    {Ï†' : E â†’L[ğ•œ] Î  i, F' i} {l : Filter E} :
    HasFDerivAtFilter (fun x => Fin.cons (Ï† x) (Ï†s x)) Ï†' x l â†”
      HasFDerivAtFilter Ï† (.proj 0 âˆ˜L Ï†') x l âˆ§
      HasFDerivAtFilter Ï†s (Pi.compRightL ğ•œ F' Fin.succ âˆ˜L Ï†') x l := by
  rw [hasFDerivAtFilter_pi', Fin.forall_fin_succ, hasFDerivAtFilter_pi']
  dsimp [ContinuousLinearMap.comp, LinearMap.comp, Function.comp_def]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `hasFDerivAtFilter_finCons` where the derivative variables are free on the RHS
instead. -/
theorem hasFDerivAtFilter_finCons'
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)} {l : Filter E} :
    HasFDerivAtFilter (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x l â†”
      HasFDerivAtFilter Ï† Ï†' x l âˆ§ HasFDerivAtFilter Ï†s Ï†s' x l :=
  hasFDerivAtFilter_finCons

theorem HasFDerivAtFilter.finCons
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)} {l : Filter E}
    (h : HasFDerivAtFilter Ï† Ï†' x l) (hs : HasFDerivAtFilter Ï†s Ï†s' x l) :
    HasFDerivAtFilter (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x l :=
  hasFDerivAtFilter_finCons'.mpr âŸ¨h, hsâŸ©

theorem hasFDerivAt_finCons
    {Ï†' : E â†’L[ğ•œ] Î  i, F' i} :
    HasFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) Ï†' x â†”
      HasFDerivAt Ï† (.proj 0 âˆ˜L Ï†') x âˆ§ HasFDerivAt Ï†s (Pi.compRightL ğ•œ F' Fin.succ âˆ˜L Ï†') x :=
  hasFDerivAtFilter_finCons

/-- A variant of `hasFDerivAt_finCons` where the derivative variables are free on the RHS
instead. -/
theorem hasFDerivAt_finCons'
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)} :
    HasFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x â†”
      HasFDerivAt Ï† Ï†' x âˆ§ HasFDerivAt Ï†s Ï†s' x :=
  hasFDerivAt_finCons

@[fun_prop]
theorem HasFDerivAt.finCons
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)}
    (h : HasFDerivAt Ï† Ï†' x) (hs : HasFDerivAt Ï†s Ï†s' x) :
    HasFDerivAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') x :=
  hasFDerivAt_finCons'.mpr âŸ¨h, hsâŸ©

theorem hasFDerivWithinAt_finCons
    {Ï†' : E â†’L[ğ•œ] Î  i, F' i} :
    HasFDerivWithinAt (fun x => Fin.cons (Ï† x) (Ï†s x)) Ï†' s x â†”
      HasFDerivWithinAt Ï† (.proj 0 âˆ˜L Ï†') s x âˆ§
      HasFDerivWithinAt Ï†s (Pi.compRightL ğ•œ F' Fin.succ âˆ˜L Ï†') s x :=
  hasFDerivAtFilter_finCons

/-- A variant of `hasFDerivWithinAt_finCons` where the derivative variables are free on the RHS
instead. -/
theorem hasFDerivWithinAt_finCons'
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)} :
    HasFDerivWithinAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') s x â†”
      HasFDerivWithinAt Ï† Ï†' s x âˆ§ HasFDerivWithinAt Ï†s Ï†s' s x :=
  hasFDerivAtFilter_finCons

@[fun_prop]
theorem HasFDerivWithinAt.finCons
    {Ï†' : E â†’L[ğ•œ] F' 0} {Ï†s' : E â†’L[ğ•œ] Î  i, F' (Fin.succ i)}
    (h : HasFDerivWithinAt Ï† Ï†' s x) (hs : HasFDerivWithinAt Ï†s Ï†s' s x) :
    HasFDerivWithinAt (fun x => Fin.cons (Ï† x) (Ï†s x)) (Ï†'.finCons Ï†s') s x :=
  hasFDerivWithinAt_finCons'.mpr âŸ¨h, hsâŸ©

theorem differentiableWithinAt_finCons :
    DifferentiableWithinAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s x â†”
      DifferentiableWithinAt ğ•œ Ï† s x âˆ§ DifferentiableWithinAt ğ•œ Ï†s s x := by
  rw [differentiableWithinAt_pi, Fin.forall_fin_succ, differentiableWithinAt_pi]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `differentiableWithinAt_finCons` where the derivative variables are free on the RHS
instead. -/
theorem differentiableWithinAt_finCons' :
    DifferentiableWithinAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s x â†”
      DifferentiableWithinAt ğ•œ Ï† s x âˆ§ DifferentiableWithinAt ğ•œ Ï†s s x :=
  differentiableWithinAt_finCons

@[fun_prop]
theorem DifferentiableWithinAt.finCons
    (h : DifferentiableWithinAt ğ•œ Ï† s x) (hs : DifferentiableWithinAt ğ•œ Ï†s s x) :
    DifferentiableWithinAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s x :=
  differentiableWithinAt_finCons'.mpr âŸ¨h, hsâŸ©

theorem differentiableAt_finCons :
    DifferentiableAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) x â†”
      DifferentiableAt ğ•œ Ï† x âˆ§ DifferentiableAt ğ•œ Ï†s x := by
  rw [differentiableAt_pi, Fin.forall_fin_succ, differentiableAt_pi]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `differentiableAt_finCons` where the derivative variables are free on the RHS
instead. -/
theorem differentiableAt_finCons' :
    DifferentiableAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) x â†”
      DifferentiableAt ğ•œ Ï† x âˆ§ DifferentiableAt ğ•œ Ï†s x :=
  differentiableAt_finCons

@[fun_prop]
theorem DifferentiableAt.finCons
    (h : DifferentiableAt ğ•œ Ï† x) (hs : DifferentiableAt ğ•œ Ï†s x) :
    DifferentiableAt ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) x :=
  differentiableAt_finCons'.mpr âŸ¨h, hsâŸ©

theorem differentiableOn_finCons :
    DifferentiableOn ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s â†”
      DifferentiableOn ğ•œ Ï† s âˆ§ DifferentiableOn ğ•œ Ï†s s := by
  rw [differentiableOn_pi, Fin.forall_fin_succ, differentiableOn_pi]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `differentiableOn_finCons` where the derivative variables are free on the RHS
instead. -/
theorem differentiableOn_finCons' :
    DifferentiableOn ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s â†”
      DifferentiableOn ğ•œ Ï† s âˆ§ DifferentiableOn ğ•œ Ï†s s :=
  differentiableOn_finCons

@[fun_prop]
theorem DifferentiableOn.finCons
    (h : DifferentiableOn ğ•œ Ï† s) (hs : DifferentiableOn ğ•œ Ï†s s) :
    DifferentiableOn ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) s :=
  differentiableOn_finCons'.mpr âŸ¨h, hsâŸ©

theorem differentiable_finCons :
    Differentiable ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) â†”
      Differentiable ğ•œ Ï† âˆ§ Differentiable ğ•œ Ï†s := by
  rw [differentiable_pi, Fin.forall_fin_succ, differentiable_pi]
  simp only [Fin.cons_zero, Fin.cons_succ]

/-- A variant of `differentiable_finCons` where the derivative variables are free on the RHS
instead. -/
theorem differentiable_finCons' :
    Differentiable ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) â†”
      Differentiable ğ•œ Ï† âˆ§ Differentiable ğ•œ Ï†s :=
  differentiable_finCons

@[fun_prop]
theorem Differentiable.finCons
    (h : Differentiable ğ•œ Ï†) (hs : Differentiable ğ•œ Ï†s) :
    Differentiable ğ•œ (fun x => Fin.cons (Ï† x) (Ï†s x)) :=
  differentiable_finCons'.mpr âŸ¨h, hsâŸ©

-- TODO: write the `Fin.cons` versions of `fderivWithin_pi` and `fderiv_pi`

end PiFin

end CartesianProduct

end
