/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.CPolynomial
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.ContDiff.Defs

#align_import analysis.calculus.fderiv_analytic from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Frechet derivatives of analytic functions.

A function expressible as a power series at a point has a Frechet derivative there.
Also the special case in terms of `deriv` when the domain is 1-dimensional.
-/


open Filter Asymptotics

open scoped ENNReal

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ}

variable {f : E â†’ F} {x : E} {s : Set E}

theorem HasFPowerSeriesAt.hasStrictFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x := by
  refine' h.isBigO_image_sub_norm_mul_norm_sub.trans_isLittleO (IsLittleO.of_norm_right _)
  refine' isLittleO_iff_exists_eq_mul.2 âŸ¨fun y => â€–y - (x, x)â€–, _, EventuallyEq.rflâŸ©
  refine' (continuous_id.sub continuous_const).norm.tendsto' _ _ _
  rw [_root_.id, sub_self, norm_zero]
#align has_fpower_series_at.has_strict_fderiv_at HasFPowerSeriesAt.hasStrictFDerivAt

theorem HasFPowerSeriesAt.hasFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x :=
  h.hasStrictFDerivAt.hasFDerivAt
#align has_fpower_series_at.has_fderiv_at HasFPowerSeriesAt.hasFDerivAt

theorem HasFPowerSeriesAt.differentiableAt (h : HasFPowerSeriesAt f p x) : DifferentiableAt ğ•œ f x :=
  h.hasFDerivAt.differentiableAt
#align has_fpower_series_at.differentiable_at HasFPowerSeriesAt.differentiableAt

theorem AnalyticAt.differentiableAt : AnalyticAt ğ•œ f x â†’ DifferentiableAt ğ•œ f x
  | âŸ¨_, hpâŸ© => hp.differentiableAt
#align analytic_at.differentiable_at AnalyticAt.differentiableAt

theorem AnalyticAt.differentiableWithinAt (h : AnalyticAt ğ•œ f x) : DifferentiableWithinAt ğ•œ f s x :=
  h.differentiableAt.differentiableWithinAt
#align analytic_at.differentiable_within_at AnalyticAt.differentiableWithinAt

theorem HasFPowerSeriesAt.fderiv_eq (h : HasFPowerSeriesAt f p x) :
    fderiv ğ•œ f x = continuousMultilinearCurryFin1 ğ•œ E F (p 1) :=
  h.hasFDerivAt.fderiv
#align has_fpower_series_at.fderiv_eq HasFPowerSeriesAt.fderiv_eq

theorem HasFPowerSeriesOnBall.differentiableOn [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) : DifferentiableOn ğ•œ f (EMetric.ball x r) := fun _ hy =>
  (h.analyticAt_of_mem hy).differentiableWithinAt
#align has_fpower_series_on_ball.differentiable_on HasFPowerSeriesOnBall.differentiableOn

theorem AnalyticOn.differentiableOn (h : AnalyticOn ğ•œ f s) : DifferentiableOn ğ•œ f s := fun y hy =>
  (h y hy).differentiableWithinAt
#align analytic_on.differentiable_on AnalyticOn.differentiableOn

theorem HasFPowerSeriesOnBall.hasFDerivAt [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).hasFPowerSeriesAt.hasFDerivAt
#align has_fpower_series_on_ball.has_fderiv_at HasFPowerSeriesOnBall.hasFDerivAt

theorem HasFPowerSeriesOnBall.fderiv_eq [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv
#align has_fpower_series_on_ball.fderiv_eq HasFPowerSeriesOnBall.fderiv_eq

/-- If a function has a power series on a ball, then so does its derivative. -/
theorem HasFPowerSeriesOnBall.fderiv [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesOnBall (fderiv ğ•œ f)
      ((continuousMultilinearCurryFin1 ğ•œ E F : (E[Ã—1]â†’L[ğ•œ] F) â†’L[ğ•œ] E â†’L[ğ•œ] F)
        |>.compFormalMultilinearSeries (p.changeOriginSeries 1)) x r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesOnBall ?_
    simpa using ((p.hasFPowerSeriesOnBall_changeOrigin 1
      (h.r_pos.trans_le h.r_le)).mono h.r_pos h.r_le).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel'_right]
  simpa only [edist_eq_coe_nnnorm_sub, EMetric.mem_ball] using hz
#align has_fpower_series_on_ball.fderiv HasFPowerSeriesOnBall.fderiv

/-- If a function is analytic on a set `s`, so is its FrÃ©chet derivative. -/
theorem AnalyticOn.fderiv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) :
    AnalyticOn ğ•œ (fderiv ğ•œ f) s := by
  intro y hy
  rcases h y hy with âŸ¨p, r, hpâŸ©
  exact hp.fderiv.analyticAt
#align analytic_on.fderiv AnalyticOn.fderiv

/-- If a function is analytic on a set `s`, so are its successive FrÃ©chet derivative. -/
theorem AnalyticOn.iteratedFDeriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) (n : â„•) :
    AnalyticOn ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction' n with n IH
  Â· rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_analyticOn h
  Â· rw [iteratedFDeriv_succ_eq_comp_left]
    -- Porting note: for reasons that I do not understand at all, `?g` cannot be inlined.
    convert ContinuousLinearMap.comp_analyticOn ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F)
    simp
#align analytic_on.iterated_fderiv AnalyticOn.iteratedFDeriv

/-- An analytic function is infinitely differentiable. -/
theorem AnalyticOn.contDiffOn [CompleteSpace F] (h : AnalyticOn ğ•œ f s) {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s :=
  let t := { x | AnalyticAt ğ•œ f x }
  suffices ContDiffOn ğ•œ n f t from this.mono h
  have H : AnalyticOn ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_analyticAt ğ•œ f
  contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
#align analytic_on.cont_diff_on AnalyticOn.contDiffOn

theorem AnalyticAt.contDiffAt [CompleteSpace F] (h : AnalyticAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x := by
  obtain âŸ¨s, hs, hfâŸ© := h.exists_mem_nhds_analyticOn
  exact hf.contDiffOn.contDiffAt hs

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}

variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

protected theorem HasFPowerSeriesAt.hasStrictDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictFDerivAt.hasStrictDerivAt
#align has_fpower_series_at.has_strict_deriv_at HasFPowerSeriesAt.hasStrictDerivAt

protected theorem HasFPowerSeriesAt.hasDerivAt (h : HasFPowerSeriesAt f p x) :
    HasDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictDerivAt.hasDerivAt
#align has_fpower_series_at.has_deriv_at HasFPowerSeriesAt.hasDerivAt

protected theorem HasFPowerSeriesAt.deriv (h : HasFPowerSeriesAt f p x) :
    deriv f x = p 1 fun _ => 1 :=
  h.hasDerivAt.deriv
#align has_fpower_series_at.deriv HasFPowerSeriesAt.deriv

/-- If a function is analytic on a set `s`, so is its derivative. -/
theorem AnalyticOn.deriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) : AnalyticOn ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_analyticOn h.fderiv
#align analytic_on.deriv AnalyticOn.deriv

/-- If a function is analytic on a set `s`, so are its successive derivatives. -/
theorem AnalyticOn.iterated_deriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) (n : â„•) :
    AnalyticOn ğ•œ (_root_.deriv^[n] f) s := by
  induction' n with n IH
  Â· exact h
  Â· simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv
#align analytic_on.iterated_deriv AnalyticOn.iterated_deriv

end deriv
section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ} {n : â„•}

variable {f : E â†’ F} {x : E} {s : Set E}

/-! The case of continuously polynomial functions. We get the same differentiability
results as for analytic functions, but without the assumptions that `F` is complete.-/

theorem HasFiniteFPowerSeriesOnBall.differentiableOn
    (h : HasFiniteFPowerSeriesOnBall f p x n r) : DifferentiableOn ğ•œ f (EMetric.ball x r) :=
  fun _ hy â†¦ (h.cPolynomialAt_of_mem hy).analyticAt.differentiableWithinAt

theorem HasFiniteFPowerSeriesOnBall.hasFDerivAt (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).toHasFPowerSeriesOnBall.hasFPowerSeriesAt.hasFDerivAt

theorem HasFiniteFPowerSeriesOnBall.fderiv_eq (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv

/-- If a function has a finite power series on a ball, then so does its derivative. -/
protected theorem HasFiniteFPowerSeriesOnBall.fderiv
    (h : HasFiniteFPowerSeriesOnBall f p x (n + 1) r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f)
      ((continuousMultilinearCurryFin1 ğ•œ E F : (E[Ã—1]â†’L[ğ•œ] F) â†’L[ğ•œ] E â†’L[ğ•œ] F)
        |>.compFormalMultilinearSeries (p.changeOriginSeries 1)) x n r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFiniteFPowerSeriesOnBall ?_
    simpa using
      ((p.hasFiniteFPowerSeriesOnBall_changeOrigin 1 h.finite).mono h.r_pos le_top).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel'_right]
  simpa only [edist_eq_coe_nnnorm_sub, EMetric.mem_ball] using hz

/-- If a function has a finite power series on a ball, then so does its derivative.
This is a variant of `HasFiniteFPowerSeriesOnBall.fderiv` where the degree of `f` is `< n`
and not `< n + 1`. -/
theorem HasFiniteFPowerSeriesOnBall.fderiv' (h : HasFiniteFPowerSeriesOnBall f p x n r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f)
      ((continuousMultilinearCurryFin1 ğ•œ E F : (E[Ã—1]â†’L[ğ•œ] F) â†’L[ğ•œ] E â†’L[ğ•œ] F)
        |>.compFormalMultilinearSeries (p.changeOriginSeries 1)) x (n - 1) r := by
  obtain rfl | hn := eq_or_ne n 0
  Â· rw [zero_tsub]
    refine HasFiniteFPowerSeriesOnBall.bound_zero_of_eq_zero (fun y hy â†¦ ?_) h.r_pos fun n â†¦ ?_
    Â· rw [Filter.EventuallyEq.fderiv_eq (f := fun _ â†¦ 0)]
      Â· rw [fderiv_const, Pi.zero_apply]
      Â· exact Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨EMetric.ball x r,
          EMetric.isOpen_ball.mem_nhds hy, fun z hz â†¦ by rw [h.eq_zero_of_bound_zero z hz]âŸ©
    Â· apply ContinuousMultilinearMap.ext; intro a
      change (continuousMultilinearCurryFin1 ğ•œ E F) (p.changeOriginSeries 1 n a) = 0
      rw [p.changeOriginSeries_finite_of_finite h.finite 1 (Nat.zero_le _)]
      exact map_zero _
  Â· rw [â† Nat.succ_pred hn] at h
    exact h.fderiv

/-- If a function is polynomial on a set `s`, so is its FrÃ©chet derivative. -/
theorem CPolynomialOn.fderiv (h : CPolynomialOn ğ•œ f s) :
    CPolynomialOn ğ•œ (fderiv ğ•œ f) s := by
  intro y hy
  rcases h y hy with âŸ¨p, r, n, hpâŸ©
  exact hp.fderiv'.cPolynomialAt

/-- If a function is polynomial on a set `s`, so are its successive FrÃ©chet derivative. -/
theorem CPolynomialOn.iteratedFDeriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction' n with n IH
  Â· rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_cPolynomialOn h
  Â· rw [iteratedFDeriv_succ_eq_comp_left]
    convert ContinuousLinearMap.comp_cPolynomialOn ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F)
    simp

/-- A polynomial function is infinitely differentiable. -/
theorem CPolynomialOn.contDiffOn (h : CPolynomialOn ğ•œ f s) {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s :=
  let t := { x | CPolynomialAt ğ•œ f x }
  suffices ContDiffOn ğ•œ n f t from this.mono h
  have H : CPolynomialOn ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_cPolynomialAt ğ•œ f
  contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).analyticOn.differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)

theorem CPolynomialAt.contDiffAt (h : CPolynomialAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x :=
  let âŸ¨_, hs, hfâŸ© := h.exists_mem_nhds_cPolynomialOn
  hf.contDiffOn.contDiffAt hs

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}

variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

/-- If a function is polynomial on a set `s`, so is its derivative. -/
protected theorem CPolynomialOn.deriv (h : CPolynomialOn ğ•œ f s) : CPolynomialOn ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_cPolynomialOn h.fderiv

/-- If a function is polynomial on a set `s`, so are its successive derivatives. -/
theorem CPolynomialOn.iterated_deriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (deriv^[n] f) s := by
  induction' n with n IH
  Â· exact h
  Â· simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

end deriv

namespace ContinuousMultilinearMap

--TODO: move to appropriate place

variable {R : Type*} {Î¹ : Type*} {Mâ‚ : Î¹ â†’ Type*} {Mâ‚‚ : Type*} [Ring R]
  [(i : Î¹) â†’ AddCommGroup (Mâ‚ i)] [AddCommGroup Mâ‚‚] [(i : Î¹) â†’ Module R (Mâ‚ i)] [Module R Mâ‚‚]
  [(i : Î¹) â†’ TopologicalSpace (Mâ‚ i)] [(i : Î¹) â†’ TopologicalAddGroup (Mâ‚ i)]
  [(i : Î¹) â†’ ContinuousConstSMul R (Mâ‚ i)] [TopologicalSpace Mâ‚‚] [TopologicalAddGroup Mâ‚‚]
  [ContinuousConstSMul R Mâ‚‚] [Fintype Î¹] (f : ContinuousMultilinearMap R Mâ‚ Mâ‚‚)

/-- Realize a ContinuousMultilinearMap on `âˆ€ i : Î¹, Mâ‚ i` as the evaluation of a
FormalMultilinearSeries by choosing an arbitrary identification `Î¹ â‰ƒ Fin (Fintype.card Î¹)`. -/
noncomputable def toFormalMultilinearSeries : FormalMultilinearSeries R (âˆ€ i, Mâ‚ i) Mâ‚‚ :=
  fun n â†¦ if h : Fintype.card Î¹ = n then
    (f.compContinuousLinearMap ContinuousLinearMap.proj).domDomCongr (Fintype.equivFinOfCardEq h)
  else 0

open scoped BigOperators

/-- The derivative of a continuous multilinear map, as a continuous linear map
from `âˆ€ i, Mâ‚ i` to `Mâ‚‚`; see `ContinuousMultilinearMap.hasFDerivAt`. -/
def linearDeriv [DecidableEq Î¹] (x : (i : Î¹) â†’ Mâ‚ i) : ((i : Î¹) â†’ Mâ‚ i) â†’L[R] Mâ‚‚ :=
  âˆ‘ i : Î¹, (f.toContinuousLinearMap x i).comp (.proj i)

@[simp]
lemma linearDeriv_apply [DecidableEq Î¹] (f : ContinuousMultilinearMap R Mâ‚ Mâ‚‚)
    (x y : (i : Î¹) â†’ Mâ‚ i) :
    f.linearDeriv x y = âˆ‘ i, f (Function.update x i (y i)) := by
  unfold linearDeriv toContinuousLinearMap
  simp only [ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_comp',
    ContinuousLinearMap.coe_mk', LinearMap.coe_mk, LinearMap.coe_toAddHom, Finset.sum_apply,
    Function.comp_apply, ContinuousLinearMap.proj_apply, MultilinearMap.toLinearMap_apply, coe_coe]

variable {R : Type*} {Î¹ : Type*} {Mâ‚ : Î¹ â†’ Type*} {Mâ‚‚ : Type*} [NontriviallyNormedField R]
  [(i : Î¹) â†’ NormedAddCommGroup (Mâ‚ i)] [NormedAddCommGroup Mâ‚‚] [(i : Î¹) â†’ NormedSpace R (Mâ‚ i)]
  [NormedSpace R Mâ‚‚] [Fintype Î¹] (f : ContinuousMultilinearMap R Mâ‚ Mâ‚‚)

open FormalMultilinearSeries

protected theorem hasFiniteFPowerSeriesOnBall :
    HasFiniteFPowerSeriesOnBall f f.toFormalMultilinearSeries 0 (Fintype.card Î¹ + 1) âŠ¤ :=
  .mk' (fun m hm â†¦ dif_neg (Nat.succ_le_iff.mp hm).ne) ENNReal.zero_lt_top fun y _ â†¦ by
    rw [Finset.sum_eq_single_of_mem _ (Finset.self_mem_range_succ _), zero_add]
    Â· rw [toFormalMultilinearSeries, dif_pos rfl]; rfl
    Â· intro m _ ne; rw [toFormalMultilinearSeries, dif_neg ne.symm]; rfl

theorem changeOriginSeries_support (f : ContinuousMultilinearMap R Mâ‚ Mâ‚‚) {k l : â„•}
    (h : k + l â‰  Fintype.card Î¹) :
    f.toFormalMultilinearSeries.changeOriginSeries k l = 0 := by
  unfold FormalMultilinearSeries.changeOriginSeries
  exact Finset.sum_eq_zero (fun _ _ â†¦ by
    rw [FormalMultilinearSeries.changeOriginSeriesTerm, AddEquivClass.map_eq_zero_iff]
    simp only [toFormalMultilinearSeries, Ne.symm h, dite_false])

open Finset in
theorem changeOrigin_toFormalMultilinearSeries [DecidableEq Î¹] (x : âˆ€ i, Mâ‚ i) :
    continuousMultilinearCurryFin1 R (âˆ€ i, Mâ‚ i) Mâ‚‚ (f.toFormalMultilinearSeries.changeOrigin x 1) =
    f.linearDeriv x := by
  ext y
  simp only [continuousMultilinearCurryFin1_apply, linearDeriv_apply]
  rw [FormalMultilinearSeries.changeOrigin, FormalMultilinearSeries.sum,
    tsum_eq_single (Fintype.card Î¹ - 1)]
  Â· by_cases he : IsEmpty Î¹
    Â· simp only [univ_eq_empty, sum_empty]
      letI := he
      rw [Fintype.card_eq_zero, Nat.zero_sub, changeOriginSeries_support, zero_apply, zero_apply]
      rw [Fintype.card_eq_zero, add_zero]
      exact Nat.one_ne_zero
    Â· have heq : Fin.snoc 0 y = (fun _ : Fin (0 + 1) â†¦ y) := by
        ext _ _
        unfold Fin.snoc
        simp only [Fin.coe_fin_one, lt_self_iff_false, Fin.castSucc_castLT, Pi.zero_apply,
          cast_eq, dite_eq_ite, ite_false]
      rw [FormalMultilinearSeries.changeOriginSeries, ContinuousMultilinearMap.sum_apply,
        ContinuousMultilinearMap.sum_apply, heq]
      have hcard : Fintype.card Î¹ = 1 + (Fintype.card Î¹ - 1) := by
        letI := not_isEmpty_iff.mp he
        rw [â† Nat.succ_eq_one_add, â† Nat.pred_eq_sub_one, Nat.succ_pred Fintype.card_ne_zero]
      set I : (i : Î¹) â†’ i âˆˆ Finset.univ â†’ {s : Finset (Fin (1 + (Fintype.card Î¹ - 1))) //
          s.card = Fintype.card Î¹ - 1} := by
        intro i _
        refine âŸ¨Finset.univ.erase (Fintype.equivFinOfCardEq hcard i), ?_âŸ©
        simp only [mem_univ, card_erase_of_mem, card_fin, add_tsub_cancel_left]
      rw [sum_bij I (fun _ _ â†¦ mem_univ _) (fun _ _ _ _ â†¦ by
          simp only [mem_univ, not_true_eq_false, Subtype.mk.injEq,
          erase_inj _ (mem_univ _), Equiv.apply_eq_iff_eq, imp_self])]
      Â· intro âŸ¨s, hsâŸ© _
        have h : sá¶œ.card = 1 := by
          rw [Finset.card_compl, hs]
          simp only [ge_iff_le, Fintype.card_fin, add_le_iff_nonpos_left, nonpos_iff_eq_zero,
            add_tsub_cancel_right]
        obtain âŸ¨a, haâŸ© := Finset.card_eq_one.mp h
        existsi ((Fintype.equivFinOfCardEq hcard).symm a), Finset.mem_univ _
        simp only [mem_univ, not_true_eq_false, Equiv.apply_symm_apply, Subtype.mk.injEq]
        rw [Finset.erase_eq, â† ha]
        simp only [sdiff_compl, ge_iff_le, le_eq_subset, subset_univ, inf_of_le_right]
      Â· intro i _
        rw [FormalMultilinearSeries.changeOriginSeriesTerm_apply, toFormalMultilinearSeries]
        simp_rw [eq_true hcard]
        rw [dite_true]
        simp only [piecewise_erase_univ, domDomCongr_apply, compContinuousLinearMap_apply,
          ContinuousLinearMap.proj_apply]
        congr
        ext j
        by_cases hj : j = i
        Â· rw [hj, Function.update_same, Function.update_same]
        Â· rw [Function.update_noteq hj, Function.update_noteq]
          rw [ne_eq, Equiv.apply_eq_iff_eq]
          exact hj
  Â· intro m hm
    have h' : Fintype.card Î¹ â‰  1 + m := fun h â†¦ by
      apply_fun Nat.pred at h
      rw [â† Nat.succ_eq_one_add, Nat.pred_succ, Nat.pred_eq_sub_one] at h
      exact hm (Eq.symm h)
    rw [FormalMultilinearSeries.changeOriginSeries, sum_apply]
    apply Finset.sum_eq_zero
    intro âŸ¨s, hsâŸ© _
    rw [FormalMultilinearSeries.changeOriginSeriesTerm, toFormalMultilinearSeries]
    simp only [h', dite_false]
    erw [LinearMap.map_zero]
    rw [ContinuousMultilinearMap.zero_apply]

protected theorem hasFDerivAt [DecidableEq Î¹] (x : âˆ€ i, Mâ‚ i) :
    HasFDerivAt f (f.linearDeriv x) x := by
  rw [â† changeOrigin_toFormalMultilinearSeries]
  convert f.hasFiniteFPowerSeriesOnBall.hasFDerivAt (y := x) ENNReal.coe_lt_top
  rw [zero_add]

end ContinuousMultilinearMap
