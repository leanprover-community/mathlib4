/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.CPolynomial
import Mathlib.Analysis.Analytic.Inverse
import Mathlib.Analysis.Analytic.Within
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.ContDiff.FTaylorSeries
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Prod
import Mathlib.Analysis.Normed.Module.Completion

/-!
# FrÃ©chet derivatives of analytic functions.

A function expressible as a power series at a point has a FrÃ©chet derivative there.
Also the special case in terms of `deriv` when the domain is 1-dimensional.

As an application, we show that continuous multilinear maps are smooth. We also compute their
iterated derivatives, in `ContinuousMultilinearMap.iteratedFDeriv_eq`.

## Main definitions and results

* `AnalyticAt.differentiableAt` : an analytic function at a point is differentiable there.
* `AnalyticOnNhd.fderiv` : in a complete space, if a function is analytic on a
  neighborhood of a set `s`, so is its derivative.
* `AnalyticOnNhd.fderiv_of_isOpen` : if a function is analytic on a neighborhood of an
  open set `s`, so is its derivative.
* `AnalyticOn.fderivWithin` : if a function is analytic on a set of unique differentiability,
  so is its derivative within this set.
* `PartialHomeomorph.analyticAt_symm` : if a partial homeomorphism `f` is analytic at a
  point `f.symm a`, with invertible derivative, then its inverse is analytic at `a`.

## Comments on completeness

Some theorems need a complete space, some don't, for the following reason.

(1) If a function is analytic at a point `x`, then it is differentiable there (with derivative given
by the first term in the power series). There is no issue of convergence here.

(2) If a function has a power series on a ball `B (x, r)`, there is no guarantee that the power
series for the derivative will converge at `y â‰  x`, if the space is not complete. So, to deduce
that `f` is differentiable at `y`, one needs completeness in general.

(3) However, if a function `f` has a power series on a ball `B (x, r)`, and is a priori known to be
differentiable at some point `y â‰  x`, then the power series for the derivative of `f` will
automatically converge at `y`, towards the given derivative: this follows from the facts that this
is true in the completion (thanks to the previous point) and that the map to the completion is
an embedding.

(4) Therefore, if one assumes `AnalyticOn ğ•œ f s` where `s` is an open set, then `f` is analytic
therefore differentiable at every point of `s`, by (1), so by (3) the power series for its
derivative converges on whole balls. Therefore, the derivative of `f` is also analytic on `s`. The
same holds if `s` is merely a set with unique differentials.

(5) However, this does not work for `AnalyticOnNhd ğ•œ f s`, as we don't get for free
differentiability at points in a neighborhood of `s`. Therefore, the theorem that deduces
`AnalyticOnNhd ğ•œ (fderiv ğ•œ f) s` from `AnalyticOnNhd ğ•œ f s` requires completeness of the space.

-/

open Filter Asymptotics Set

open scoped ENNReal Topology

universe u v

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ}
variable {f : E â†’ F} {x : E} {s : Set E}

/-- A function which is analytic within a set is strictly differentiable there. Since we
don't have a predicate `HasStrictFDerivWithinAt`, we spell out what it would mean. -/
theorem HasFPowerSeriesWithinAt.hasStrictFDerivWithinAt (h : HasFPowerSeriesWithinAt f p s x) :
    (fun y â†¦ f y.1 - f y.2 - (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) (y.1 - y.2))
      =o[ğ“[insert x s Ã—Ë¢ insert x s] (x, x)] fun y â†¦ y.1 - y.2 := by
  refine h.isBigO_image_sub_norm_mul_norm_sub.trans_isLittleO (IsLittleO.of_norm_right ?_)
  refine isLittleO_iff_exists_eq_mul.2 âŸ¨fun y => â€–y - (x, x)â€–, ?_, EventuallyEq.rflâŸ©
  apply Tendsto.mono_left _ nhdsWithin_le_nhds
  refine (continuous_id.sub continuous_const).norm.tendsto' _ _ ?_
  rw [_root_.id, sub_self, norm_zero]

theorem HasFPowerSeriesAt.hasStrictFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x := by
  simpa only [hasStrictFDerivAt_iff_isLittleO, Set.insert_eq_of_mem, Set.mem_univ,
      Set.univ_prod_univ, nhdsWithin_univ]
    using (h.hasFPowerSeriesWithinAt (s := Set.univ)).hasStrictFDerivWithinAt

theorem HasFPowerSeriesWithinAt.hasFDerivWithinAt (h : HasFPowerSeriesWithinAt f p s x) :
    HasFDerivWithinAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) (insert x s) x := by
  rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO, isLittleO_iff]
  intro c hc
  have : Tendsto (fun y â†¦ (y, x)) (ğ“[insert x s] x) (ğ“[insert x s Ã—Ë¢ insert x s] (x, x)) := by
    rw [nhdsWithin_prod_eq]
    exact Tendsto.prodMk tendsto_id (tendsto_const_nhdsWithin (by simp))
  exact this (isLittleO_iff.1 h.hasStrictFDerivWithinAt hc)

theorem HasFPowerSeriesAt.hasFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x :=
  h.hasStrictFDerivAt.hasFDerivAt

theorem HasFPowerSeriesWithinAt.differentiableWithinAt (h : HasFPowerSeriesWithinAt f p s x) :
    DifferentiableWithinAt ğ•œ f (insert x s) x :=
  h.hasFDerivWithinAt.differentiableWithinAt

theorem HasFPowerSeriesAt.differentiableAt (h : HasFPowerSeriesAt f p x) : DifferentiableAt ğ•œ f x :=
  h.hasFDerivAt.differentiableAt

theorem AnalyticWithinAt.differentiableWithinAt (h : AnalyticWithinAt ğ•œ f s x) :
    DifferentiableWithinAt ğ•œ f (insert x s) x := by
  obtain âŸ¨p, hpâŸ© := h
  exact hp.differentiableWithinAt

@[fun_prop]
theorem AnalyticAt.differentiableAt : AnalyticAt ğ•œ f x â†’ DifferentiableAt ğ•œ f x
  | âŸ¨_, hpâŸ© => hp.differentiableAt

theorem AnalyticAt.differentiableWithinAt (h : AnalyticAt ğ•œ f x) : DifferentiableWithinAt ğ•œ f s x :=
  h.differentiableAt.differentiableWithinAt

theorem HasFPowerSeriesWithinAt.fderivWithin_eq
    (h : HasFPowerSeriesWithinAt f p s x) (hu : UniqueDiffWithinAt ğ•œ (insert x s) x) :
    fderivWithin ğ•œ f (insert x s) x = continuousMultilinearCurryFin1 ğ•œ E F (p 1) :=
  h.hasFDerivWithinAt.fderivWithin hu

theorem HasFPowerSeriesAt.fderiv_eq (h : HasFPowerSeriesAt f p x) :
    fderiv ğ•œ f x = continuousMultilinearCurryFin1 ğ•œ E F (p 1) :=
  h.hasFDerivAt.fderiv

theorem AnalyticAt.hasStrictFDerivAt (h : AnalyticAt ğ•œ f x) :
    HasStrictFDerivAt f (fderiv ğ•œ f x) x := by
  rcases h with âŸ¨p, hpâŸ©
  rw [hp.fderiv_eq]
  exact hp.hasStrictFDerivAt

theorem HasFPowerSeriesWithinOnBall.differentiableOn [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r) :
    DifferentiableOn ğ•œ f (insert x s âˆ© EMetric.ball x r) := by
  intro y hy
  have Z := (h.analyticWithinAt_of_mem hy).differentiableWithinAt
  rcases eq_or_ne y x with rfl | hy
  Â· exact Z.mono inter_subset_left
  Â· apply (Z.mono (subset_insert _ _)).mono_of_mem_nhdsWithin
    suffices s âˆˆ ğ“[insert x s] y from nhdsWithin_mono _ inter_subset_left this
    rw [nhdsWithin_insert_of_ne hy]
    exact self_mem_nhdsWithin

theorem HasFPowerSeriesOnBall.differentiableOn [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) : DifferentiableOn ğ•œ f (EMetric.ball x r) := fun _ hy =>
  (h.analyticAt_of_mem hy).differentiableWithinAt

theorem HasFPowerSeriesAt.eventually_differentiableAt
    [CompleteSpace F] (hp : HasFPowerSeriesAt f p x) :
    âˆ€á¶  z in ğ“ x, DifferentiableAt ğ•œ f z := by
  obtain âŸ¨r, hpâŸ© := hp
  exact hp.differentiableOn.eventually_differentiableAt (EMetric.ball_mem_nhds _ hp.r_pos)

theorem AnalyticOn.differentiableOn (h : AnalyticOn ğ•œ f s) : DifferentiableOn ğ•œ f s :=
  fun y hy â†¦ (h y hy).differentiableWithinAt.mono (by simp)

theorem AnalyticOnNhd.differentiableOn (h : AnalyticOnNhd ğ•œ f s) : DifferentiableOn ğ•œ f s :=
  fun y hy â†¦ (h y hy).differentiableWithinAt

theorem HasFPowerSeriesWithinOnBall.hasFDerivWithinAt [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) (h'y : x + y âˆˆ insert x s) :
    HasFDerivWithinAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1))
      (insert x s) (x + y) := by
  rcases eq_or_ne y 0 with rfl | h''y
  Â· convert (h.changeOrigin hy h'y).hasFPowerSeriesWithinAt.hasFDerivWithinAt
    simp
  Â· have Z := (h.changeOrigin hy h'y).hasFPowerSeriesWithinAt.hasFDerivWithinAt
    apply (Z.mono (subset_insert _ _)).mono_of_mem_nhdsWithin
    rw [nhdsWithin_insert_of_ne]
    Â· exact self_mem_nhdsWithin
    Â· simpa using h''y

theorem HasFPowerSeriesOnBall.hasFDerivAt [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).hasFPowerSeriesAt.hasFDerivAt

theorem HasFPowerSeriesWithinOnBall.fderivWithin_eq [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) (h'y : x + y âˆˆ insert x s) (hu : UniqueDiffOn ğ•œ (insert x s)) :
    fderivWithin ğ•œ f (insert x s) (x + y) =
      continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivWithinAt hy h'y).fderivWithin (hu _ h'y)

theorem HasFPowerSeriesOnBall.fderiv_eq [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv

/-- If a function has a power series on a ball, then so does its derivative. -/
protected theorem HasFPowerSeriesOnBall.fderiv [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesOnBall ?_
    simpa using ((p.hasFPowerSeriesOnBall_changeOrigin 1
      (h.r_pos.trans_le h.r_le)).mono h.r_pos h.r_le).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel]
  simpa only [edist_eq_enorm_sub, EMetric.mem_ball] using hz

/-- If a function has a power series within a set on a ball, then so does its derivative. -/
protected theorem HasFPowerSeriesWithinOnBall.fderivWithin [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r) (hu : UniqueDiffOn ğ•œ (insert x s)) :
    HasFPowerSeriesWithinOnBall (fderivWithin ğ•œ f (insert x s)) p.derivSeries s x r := by
  refine .congr' (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    (fun z hz â†¦ ?_)
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesWithinOnBall ?_
    apply HasFPowerSeriesOnBall.hasFPowerSeriesWithinOnBall
    simpa using ((p.hasFPowerSeriesOnBall_changeOrigin 1
      (h.r_pos.trans_le h.r_le)).mono h.r_pos h.r_le).comp_sub x
  Â· dsimp only
    rw [â† h.fderivWithin_eq _ _ hu, add_sub_cancel]
    Â· simpa only [edist_eq_enorm_sub, EMetric.mem_ball] using hz.2
    Â· simpa using hz.1

/-- If a function has a power series within a set on a ball, then so does its derivative. For a
version without completeness, but assuming that the function is analytic on the set `s`, see
`HasFPowerSeriesWithinOnBall.fderivWithin_of_mem_of_analyticOn`. -/
protected theorem HasFPowerSeriesWithinOnBall.fderivWithin_of_mem [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r) (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    HasFPowerSeriesWithinOnBall (fderivWithin ğ•œ f s) p.derivSeries s x r := by
  have : insert x s = s := insert_eq_of_mem hx
  rw [â† this] at hu
  convert h.fderivWithin hu
  exact this.symm

/-- If a function is analytic on a set `s`, so is its FrÃ©chet derivative. -/
@[fun_prop]
protected theorem AnalyticAt.fderiv [CompleteSpace F] (h : AnalyticAt ğ•œ f x) :
    AnalyticAt ğ•œ (fderiv ğ•œ f) x := by
  rcases h with âŸ¨p, r, hpâŸ©
  exact hp.fderiv.analyticAt

/-- If a function is analytic on a set `s`, so is its FrÃ©chet derivative. See also
`AnalyticOnNhd.fderiv_of_isOpen`, removing the completeness assumption but requiring the set
to be open. -/
protected theorem AnalyticOnNhd.fderiv [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) :
    AnalyticOnNhd ğ•œ (fderiv ğ•œ f) s :=
  fun y hy â†¦ AnalyticAt.fderiv (h y hy)

/-- If a function is analytic on a set `s`, so are its successive FrÃ©chet derivative. See also
`AnalyticOnNhd.iteratedFDeriv_of_isOpen`, removing the completeness assumption but requiring the set
to be open. -/
protected theorem AnalyticOnNhd.iteratedFDeriv [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) (n : â„•) :
    AnalyticOnNhd ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction n with
  | zero =>
    rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_analyticOnNhd h
  | succ n IH =>
    rw [iteratedFDeriv_succ_eq_comp_left]
    -- Porting note: for reasons that I do not understand at all, `?g` cannot be inlined.
    convert ContinuousLinearMap.comp_analyticOnNhd ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F).symm
    simp

/-- If a function is analytic on a neighborhood of a set `s`, then it has a Taylor series given
by the sequence of its derivatives. Note that, if the function were just analytic on `s`, then
one would have to use instead the sequence of derivatives inside the set, as in
`AnalyticOn.hasFTaylorSeriesUpToOn`. -/
lemma AnalyticOnNhd.hasFTaylorSeriesUpToOn [CompleteSpace F]
    (n : WithTop â„•âˆ) (h : AnalyticOnNhd ğ•œ f s) :
    HasFTaylorSeriesUpToOn n f (ftaylorSeries ğ•œ f) s := by
  refine âŸ¨fun x _hx â†¦ rfl, fun m _hm x hx â†¦ ?_, fun m _hm x hx â†¦ ?_âŸ©
  Â· apply HasFDerivAt.hasFDerivWithinAt
    exact ((h.iteratedFDeriv m x hx).differentiableAt).hasFDerivAt
  Â· apply (DifferentiableAt.continuousAt (ğ•œ := ğ•œ) ?_).continuousWithinAt
    exact (h.iteratedFDeriv m x hx).differentiableAt

lemma AnalyticWithinAt.exists_hasFTaylorSeriesUpToOn [CompleteSpace F]
    (n : WithTop â„•âˆ) (h : AnalyticWithinAt ğ•œ f s x) :
    âˆƒ u âˆˆ ğ“[insert x s] x, âˆƒ (p : E â†’ FormalMultilinearSeries ğ•œ E F),
    HasFTaylorSeriesUpToOn n f p u âˆ§ âˆ€ i, AnalyticOn ğ•œ (fun x â†¦ p x i) u := by
  rcases h.exists_analyticAt with âŸ¨g, -, fg, hgâŸ©
  rcases hg.exists_mem_nhds_analyticOnNhd with âŸ¨v, vx, hvâŸ©
  refine âŸ¨insert x s âˆ© v, inter_mem_nhdsWithin _ vx, ftaylorSeries ğ•œ g, ?_, fun i â†¦ ?_âŸ©
  Â· suffices HasFTaylorSeriesUpToOn n g (ftaylorSeries ğ•œ g) (insert x s âˆ© v) from
      this.congr (fun y hy â†¦ fg hy.1)
    exact AnalyticOnNhd.hasFTaylorSeriesUpToOn _ (hv.mono Set.inter_subset_right)
  Â· exact (hv.iteratedFDeriv i).analyticOn.mono Set.inter_subset_right

/-- If a function has a power series `p` within a set of unique differentiability, inside a ball,
and is differentiable at a point, then the derivative series of `p` is summable at a point, with
sum the given differential. Note that this theorem does not require completeness of the space. -/
theorem HasFPowerSeriesWithinOnBall.hasSum_derivSeries_of_hasFDerivWithinAt
    (h : HasFPowerSeriesWithinOnBall f p s x r)
    {f' : E â†’L[ğ•œ] F}
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) (h'y : x + y âˆˆ insert x s)
    (hf' : HasFDerivWithinAt f f' (insert x s) (x + y))
    (hu : UniqueDiffOn ğ•œ (insert x s)) :
    HasSum (fun n â†¦ p.derivSeries n (fun _ â†¦ y)) f' := by
  /- In the completion of the space, the derivative series is summable, and its sum is a derivative
  of the function. Therefore, by uniqueness of derivatives, its sum is the image of `f'` under
  the canonical embedding. As this is an embedding, it means that there was also convergence in
  the original space, to `f'`. -/
  let F' := UniformSpace.Completion F
  let a : F â†’L[ğ•œ] F' := UniformSpace.Completion.toComplL
  let b : (E â†’L[ğ•œ] F) â†’â‚—áµ¢[ğ•œ] (E â†’L[ğ•œ] F') := UniformSpace.Completion.toComplâ‚—áµ¢.postcomp
  rw [â† b.isEmbedding.hasSum_iff]
  have : HasFPowerSeriesWithinOnBall (a âˆ˜ f) (a.compFormalMultilinearSeries p) s x r :=
    a.comp_hasFPowerSeriesWithinOnBall h
  have Z := (this.fderivWithin hu).hasSum h'y (by simpa [edist_zero_eq_enorm] using hy)
  have : fderivWithin ğ•œ (a âˆ˜ f) (insert x s) (x + y) = a âˆ˜L f' := by
    apply HasFDerivWithinAt.fderivWithin _ (hu _ h'y)
    exact a.hasFDerivAt.comp_hasFDerivWithinAt (x + y) hf'
  rw [this] at Z
  convert Z with n
  ext v
  simp only [FormalMultilinearSeries.derivSeries, ContinuousLinearMap.coe_sum', Finset.sum_apply,
    ContinuousLinearMap.compFormalMultilinearSeries_apply,
    FormalMultilinearSeries.changeOriginSeries,
    ContinuousLinearMap.compContinuousMultilinearMap_coe, ContinuousLinearEquiv.coe_coe,
    LinearIsometryEquiv.coe_coe, Function.comp_apply, ContinuousMultilinearMap.sum_apply, map_sum]
  rfl

/-- If a function has a power series within a set on a ball, then so does its derivative. Version
assuming that the function is analytic on `s`. For a version without this assumption but requiring
that `F` is complete, see `HasFPowerSeriesWithinOnBall.fderivWithin_of_mem`. -/
protected theorem HasFPowerSeriesWithinOnBall.fderivWithin_of_mem_of_analyticOn
    (hr : HasFPowerSeriesWithinOnBall f p s x r)
    (h : AnalyticOn ğ•œ f s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    HasFPowerSeriesWithinOnBall (fderivWithin ğ•œ f s) p.derivSeries s x r := by
  refine âŸ¨hr.r_le.trans p.radius_le_radius_derivSeries, hr.r_pos, fun {y} hy h'y â†¦ ?_âŸ©
  apply hr.hasSum_derivSeries_of_hasFDerivWithinAt (by simpa [edist_zero_eq_enorm] using h'y) hy
  Â· rw [insert_eq_of_mem hx] at hy âŠ¢
    apply DifferentiableWithinAt.hasFDerivWithinAt
    exact h.differentiableOn _ hy
  Â· rwa [insert_eq_of_mem hx]

/-- If a function is analytic within a set with unique differentials, then so is its derivative.
Note that this theorem does not require completeness of the space. -/
protected theorem AnalyticOn.fderivWithin (h : AnalyticOn ğ•œ f s) (hu : UniqueDiffOn ğ•œ s) :
    AnalyticOn ğ•œ (fderivWithin ğ•œ f s) s := by
  intro x hx
  rcases h x hx with âŸ¨p, r, hrâŸ©
  refine âŸ¨p.derivSeries, r, hr.fderivWithin_of_mem_of_analyticOn h hu hxâŸ©

/-- If a function is analytic on a set `s`, so are its successive FrÃ©chet derivative within this
set. Note that this theorem does not require completeness of the space. -/
protected theorem AnalyticOn.iteratedFDerivWithin (h : AnalyticOn ğ•œ f s)
    (hu : UniqueDiffOn ğ•œ s) (n : â„•) :
    AnalyticOn ğ•œ (iteratedFDerivWithin ğ•œ n f s) s := by
  induction n with
  | zero =>
    rw [iteratedFDerivWithin_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F)
      |>.comp_analyticOn h
  | succ n IH =>
    rw [iteratedFDerivWithin_succ_eq_comp_left]
    apply AnalyticOnNhd.comp_analyticOn _ (IH.fderivWithin hu) (mapsTo_univ _ _)
    apply LinearIsometryEquiv.analyticOnNhd

protected lemma AnalyticOn.hasFTaylorSeriesUpToOn {n : WithTop â„•âˆ}
    (h : AnalyticOn ğ•œ f s) (hu : UniqueDiffOn ğ•œ s) :
    HasFTaylorSeriesUpToOn n f (ftaylorSeriesWithin ğ•œ f s) s := by
  refine âŸ¨fun x _hx â†¦ rfl, fun m _hm x hx â†¦ ?_, fun m _hm x hx â†¦ ?_âŸ©
  Â· have := (h.iteratedFDerivWithin hu m x hx).differentiableWithinAt.hasFDerivWithinAt
    rwa [insert_eq_of_mem hx] at this
  Â· exact (h.iteratedFDerivWithin hu m x hx).continuousWithinAt

lemma AnalyticOn.exists_hasFTaylorSeriesUpToOn
    (h : AnalyticOn ğ•œ f s) (hu : UniqueDiffOn ğ•œ s) :
    âˆƒ p : E â†’ FormalMultilinearSeries ğ•œ E F,
      HasFTaylorSeriesUpToOn âŠ¤ f p s âˆ§ âˆ€ i, AnalyticOn ğ•œ (fun x â†¦ p x i) s :=
  âŸ¨ftaylorSeriesWithin ğ•œ f s, h.hasFTaylorSeriesUpToOn hu, h.iteratedFDerivWithin huâŸ©

theorem AnalyticOnNhd.fderiv_of_isOpen (h : AnalyticOnNhd ğ•œ f s) (hs : IsOpen s) :
    AnalyticOnNhd ğ•œ (fderiv ğ•œ f) s := by
  rw [â† hs.analyticOn_iff_analyticOnNhd] at h âŠ¢
  exact (h.fderivWithin hs.uniqueDiffOn).congr (fun x hx â†¦ (fderivWithin_of_isOpen hs hx).symm)

theorem AnalyticOnNhd.iteratedFDeriv_of_isOpen (h : AnalyticOnNhd ğ•œ f s) (hs : IsOpen s) (n : â„•) :
    AnalyticOnNhd ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  rw [â† hs.analyticOn_iff_analyticOnNhd] at h âŠ¢
  exact (h.iteratedFDerivWithin hs.uniqueDiffOn n).congr
    (fun x hx â†¦ (iteratedFDerivWithin_of_isOpen n hs hx).symm)

/-- If a partial homeomorphism `f` is analytic at a point `a`, with invertible derivative, then
its inverse is analytic at `f a`. -/
theorem PartialHomeomorph.analyticAt_symm' (f : PartialHomeomorph E F) {a : E}
    {i : E â‰ƒL[ğ•œ] F} (h0 : a âˆˆ f.source) (h : AnalyticAt ğ•œ f a) (h' : fderiv ğ•œ f a = i) :
    AnalyticAt ğ•œ f.symm (f a) := by
  rcases h with âŸ¨p, hpâŸ©
  have : p 1 = (continuousMultilinearCurryFin1 ğ•œ E F).symm i := by simp [â† h', hp.fderiv_eq]
  exact (f.hasFPowerSeriesAt_symm h0 hp this).analyticAt

/-- If a partial homeomorphism `f` is analytic at a point `f.symm a`, with invertible derivative,
then its inverse is analytic at `a`. -/
theorem PartialHomeomorph.analyticAt_symm (f : PartialHomeomorph E F) {a : F}
    {i : E â‰ƒL[ğ•œ] F} (h0 : a âˆˆ f.target) (h : AnalyticAt ğ•œ f (f.symm a))
    (h' : fderiv ğ•œ f (f.symm a) = i) :
    AnalyticAt ğ•œ f.symm a := by
  have : a = f (f.symm a) := by simp [h0]
  rw [this]
  exact f.analyticAt_symm' (by simp [h0]) h h'

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}
variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

protected theorem HasFPowerSeriesAt.hasStrictDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictFDerivAt.hasStrictDerivAt

protected theorem HasFPowerSeriesAt.hasDerivAt (h : HasFPowerSeriesAt f p x) :
    HasDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictDerivAt.hasDerivAt

protected theorem HasFPowerSeriesAt.deriv (h : HasFPowerSeriesAt f p x) :
    deriv f x = p 1 fun _ => 1 :=
  h.hasDerivAt.deriv

/-- If a function is analytic on a set `s` in a complete space, so is its derivative. -/
protected theorem AnalyticOnNhd.deriv [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) :
    AnalyticOnNhd ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_analyticOnNhd h.fderiv

/-- If a function is analytic on an open set `s`, so is its derivative. -/
theorem AnalyticOnNhd.deriv_of_isOpen (h : AnalyticOnNhd ğ•œ f s) (hs : IsOpen s) :
    AnalyticOnNhd ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_analyticOnNhd (h.fderiv_of_isOpen hs)

/-- If a function is analytic on a set `s`, so are its successive derivatives. -/
theorem AnalyticOnNhd.iterated_deriv [CompleteSpace F] (h : AnalyticOnNhd ğ•œ f s) (n : â„•) :
    AnalyticOnNhd ğ•œ (deriv^[n] f) s := by
  induction n with
  | zero => exact h
  | succ n IH => simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

protected theorem AnalyticAt.deriv [CompleteSpace F] (h : AnalyticAt ğ•œ f x) :
    AnalyticAt ğ•œ (deriv f) x := by
  obtain âŸ¨r, hr, hâŸ© := h.exists_ball_analyticOnNhd
  exact h.deriv x (by simp [hr])

theorem AnalyticAt.iterated_deriv [CompleteSpace F] (h : AnalyticAt ğ•œ f x) (n : â„•) :
    AnalyticAt ğ•œ (deriv^[n] f) x := by
  induction n with
  | zero => exact h
  | succ n IH => simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

end deriv
section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ} {n : â„•}
variable {f : E â†’ F} {x : E} {s : Set E}

/-! The case of continuously polynomial functions. We get the same differentiability
results as for analytic functions, but without the assumptions that `F` is complete. -/

theorem HasFiniteFPowerSeriesOnBall.differentiableOn
    (h : HasFiniteFPowerSeriesOnBall f p x n r) : DifferentiableOn ğ•œ f (EMetric.ball x r) :=
  fun _ hy â†¦ (h.cpolynomialAt_of_mem hy).analyticAt.differentiableWithinAt

theorem HasFiniteFPowerSeriesOnBall.hasFDerivAt (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).toHasFPowerSeriesOnBall.hasFPowerSeriesAt.hasFDerivAt

theorem HasFiniteFPowerSeriesOnBall.fderiv_eq (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv

/-- If a function has a finite power series on a ball, then so does its derivative. -/
protected theorem HasFiniteFPowerSeriesOnBall.fderiv
    (h : HasFiniteFPowerSeriesOnBall f p x (n + 1) r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x n r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFiniteFPowerSeriesOnBall ?_
    simpa using
      ((p.hasFiniteFPowerSeriesOnBall_changeOrigin 1 h.finite).mono h.r_pos le_top).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel]
  simpa only [edist_eq_enorm_sub, EMetric.mem_ball] using hz

/-- If a function has a finite power series on a ball, then so does its derivative.
This is a variant of `HasFiniteFPowerSeriesOnBall.fderiv` where the degree of `f` is `< n`
and not `< n + 1`. -/
theorem HasFiniteFPowerSeriesOnBall.fderiv' (h : HasFiniteFPowerSeriesOnBall f p x n r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x (n - 1) r := by
  obtain rfl | hn := eq_or_ne n 0
  Â· rw [zero_tsub]
    refine HasFiniteFPowerSeriesOnBall.bound_zero_of_eq_zero (fun y hy â†¦ ?_) h.r_pos fun n â†¦ ?_
    Â· rw [Filter.EventuallyEq.fderiv_eq (f := fun _ â†¦ 0)]
      Â· simp
      Â· exact Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨EMetric.ball x r,
          EMetric.isOpen_ball.mem_nhds hy, fun z hz â†¦ by rw [h.eq_zero_of_bound_zero z hz]âŸ©
    Â· apply ContinuousMultilinearMap.ext; intro a
      change (continuousMultilinearCurryFin1 ğ•œ E F) (p.changeOriginSeries 1 n a) = 0
      rw [p.changeOriginSeries_finite_of_finite h.finite 1 (Nat.zero_le _)]
      exact map_zero _
  Â· rw [â† Nat.succ_pred hn] at h
    exact h.fderiv

/-- If a function is polynomial on a set `s`, so is its FrÃ©chet derivative. -/
theorem CPolynomialOn.fderiv (h : CPolynomialOn ğ•œ f s) :
    CPolynomialOn ğ•œ (fderiv ğ•œ f) s := by
  intro y hy
  rcases h y hy with âŸ¨p, r, n, hpâŸ©
  exact hp.fderiv'.cpolynomialAt

/-- If a function is polynomial on a set `s`, so are its successive FrÃ©chet derivative. -/
theorem CPolynomialOn.iteratedFDeriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction n with
  | zero =>
    rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_cpolynomialOn h
  | succ n IH =>
    rw [iteratedFDeriv_succ_eq_comp_left]
    convert ContinuousLinearMap.comp_cpolynomialOn ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F).symm
    simp

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}
variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

/-- If a function is polynomial on a set `s`, so is its derivative. -/
protected theorem CPolynomialOn.deriv (h : CPolynomialOn ğ•œ f s) : CPolynomialOn ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_cpolynomialOn h.fderiv

/-- If a function is polynomial on a set `s`, so are its successive derivatives. -/
theorem CPolynomialOn.iterated_deriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (deriv^[n] f) s := by
  induction n with
  | zero => exact h
  | succ n IH => simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

end deriv

namespace ContinuousMultilinearMap

variable {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [Fintype Î¹] (f : ContinuousMultilinearMap ğ•œ E F)

open FormalMultilinearSeries

theorem changeOriginSeries_support {k l : â„•} (h : k + l â‰  Fintype.card Î¹) :
    f.toFormalMultilinearSeries.changeOriginSeries k l = 0 :=
  Finset.sum_eq_zero fun _ _ â†¦ by
    simp_rw [FormalMultilinearSeries.changeOriginSeriesTerm,
      toFormalMultilinearSeries, dif_neg h.symm, LinearIsometryEquiv.map_zero]

variable {n : WithTop â„•âˆ} (x : âˆ€ i, E i)

open Finset in
theorem changeOrigin_toFormalMultilinearSeries [DecidableEq Î¹] :
    continuousMultilinearCurryFin1 ğ•œ (âˆ€ i, E i) F (f.toFormalMultilinearSeries.changeOrigin x 1) =
    f.linearDeriv x := by
  ext y
  rw [continuousMultilinearCurryFin1_apply, linearDeriv_apply,
      changeOrigin, FormalMultilinearSeries.sum]
  cases isEmpty_or_nonempty Î¹
  Â· have (l : _) : 1 + l â‰  Fintype.card Î¹ := by
      rw [add_comm, Fintype.card_eq_zero]; exact Nat.succ_ne_zero _
    simp_rw [Fintype.sum_empty, changeOriginSeries_support _ (this _), zero_apply _, tsum_zero]; rfl
  rw [tsum_eq_single (Fintype.card Î¹ - 1), changeOriginSeries]; swap
  Â· intro m hm
    rw [Ne, eq_tsub_iff_add_eq_of_le (by exact Fintype.card_pos), add_comm] at hm
    rw [f.changeOriginSeries_support hm, zero_apply]
  rw [sum_apply, ContinuousMultilinearMap.sum_apply, Fin.snoc_zero]
  simp_rw [changeOriginSeriesTerm_apply]
  refine (Fintype.sum_bijective (?_ âˆ˜ Fintype.equivFinOfCardEq (Nat.add_sub_of_le
    Fintype.card_pos).symm) (.comp ?_ <| Equiv.bijective _) _ _ fun i â†¦ ?_).symm
  Â· exact (âŸ¨{Â·}á¶œ, by
      rw [card_compl, Fintype.card_fin, Finset.card_singleton, Nat.add_sub_cancel_left]âŸ©)
  Â· use fun _ _ â†¦ (singleton_injective <| compl_injective <| Subtype.ext_iff.mp Â·)
    intro âŸ¨s, hsâŸ©
    have h : #sá¶œ = 1 := by rw [card_compl, hs, Fintype.card_fin, Nat.add_sub_cancel]
    obtain âŸ¨a, haâŸ© := card_eq_one.mp h
    exact âŸ¨a, Subtype.ext (compl_eq_comm.mp ha)âŸ©
  rw [Function.comp_apply, Subtype.coe_mk, compl_singleton, piecewise_erase_univ,
    toFormalMultilinearSeries, dif_pos (Nat.add_sub_of_le Fintype.card_pos).symm]
  simp_rw [domDomCongr_apply, compContinuousLinearMap_apply, ContinuousLinearMap.proj_apply,
    Function.update_apply, (Equiv.injective _).eq_iff, ite_apply]
  congr
  grind [Function.update_self, Function.update_of_ne]

protected theorem hasFDerivAt [DecidableEq Î¹] : HasFDerivAt f (f.linearDeriv x) x := by
  rw [â† changeOrigin_toFormalMultilinearSeries]
  convert f.hasFiniteFPowerSeriesOnBall.hasFDerivAt (y := x) ENNReal.coe_lt_top
  rw [zero_add]

/-- Given `f` a multilinear map, then the derivative of `x â†¦ f (gâ‚ x, ..., gâ‚™ x)` at `x` applied
to a vector `v` is given by `âˆ‘ i, f (gâ‚ x, ..., g'áµ¢ v, ..., gâ‚™ x)`. Version inside a set. -/
theorem _root_.HasFDerivWithinAt.multilinear_comp
    [DecidableEq Î¹] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
    {g : âˆ€ i, G â†’ E i} {g' : âˆ€ i, G â†’L[ğ•œ] E i} {s : Set G} {x : G}
    (hg : âˆ€ i, HasFDerivWithinAt (g i) (g' i) s x) :
    HasFDerivWithinAt (fun x â†¦ f (fun i â†¦ g i x))
      ((âˆ‘ i : Î¹, (f.toContinuousLinearMap (fun j â†¦ g j x) i) âˆ˜L (g' i))) s x := by
  convert (f.hasFDerivAt (fun j â†¦ g j x)).comp_hasFDerivWithinAt x (hasFDerivWithinAt_pi.2 hg)
  ext v
  simp [linearDeriv]

/-- Given `f` a multilinear map, then the derivative of `x â†¦ f (gâ‚ x, ..., gâ‚™ x)` at `x` applied
to a vector `v` is given by `âˆ‘ i, f (gâ‚ x, ..., g'áµ¢ v, ..., gâ‚™ x)`. -/
theorem _root_.HasFDerivAt.multilinear_comp
    [DecidableEq Î¹] {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
    {g : âˆ€ i, G â†’ E i} {g' : âˆ€ i, G â†’L[ğ•œ] E i} {x : G}
    (hg : âˆ€ i, HasFDerivAt (g i) (g' i) x) :
    HasFDerivAt (fun x â†¦ f (fun i â†¦ g i x))
      ((âˆ‘ i : Î¹, (f.toContinuousLinearMap (fun j â†¦ g j x) i) âˆ˜L (g' i))) x := by
  convert (f.hasFDerivAt (fun j â†¦ g j x)).comp x (hasFDerivAt_pi.2 hg)
  ext v
  simp [linearDeriv]

/-- Technical lemma used in the proof of `hasFTaylorSeriesUpTo_iteratedFDeriv`, to compare sums
over embedding of `Fin k` and `Fin (k + 1)`. -/
private lemma _root_.Equiv.succ_embeddingFinSucc_fst_symm_apply {Î¹ : Type*} [DecidableEq Î¹]
    {n : â„•} (e : Fin (n + 1) â†ª Î¹) {k : Î¹}
    (h'k : k âˆˆ Set.range (Equiv.embeddingFinSucc n Î¹ e).1) (hk : k âˆˆ Set.range e) :
    Fin.succ ((Equiv.embeddingFinSucc n Î¹ e).1.toEquivRange.symm âŸ¨k, h'kâŸ©)
      = e.toEquivRange.symm âŸ¨k, hkâŸ© := by
  rcases hk with âŸ¨j, rflâŸ©
  have hj : j â‰  0 := by
    rintro rfl
    simp at h'k
  simp only [Function.Embedding.toEquivRange_symm_apply_self]
  have : e j = (Equiv.embeddingFinSucc n Î¹ e).1 (Fin.pred j hj) := by simp
  simp_rw [this]
  simp [-Equiv.embeddingFinSucc_fst]

/-- A continuous multilinear function `f` admits a Taylor series, whose successive terms are given
by `f.iteratedFDeriv n`. This is the point of the definition of `f.iteratedFDeriv`. -/
theorem hasFTaylorSeriesUpTo_iteratedFDeriv :
    HasFTaylorSeriesUpTo âŠ¤ f (fun v n â†¦ f.iteratedFDeriv n v) := by
  classical
  constructor
  Â· simp [ContinuousMultilinearMap.iteratedFDeriv]
  Â· rintro n - x
    suffices H : curryLeft (f.iteratedFDeriv (Nat.succ n) x) = (âˆ‘ e : Fin n â†ª Î¹,
          ((iteratedFDerivComponent f e.toEquivRange).linearDeriv
            (Pi.compRightL ğ•œ _ Subtype.val x)) âˆ˜L (Pi.compRightL ğ•œ _ Subtype.val)) by
      have A : HasFDerivAt (f.iteratedFDeriv n) (âˆ‘ e : Fin n â†ª Î¹,
          ((iteratedFDerivComponent f e.toEquivRange).linearDeriv (Pi.compRightL ğ•œ _ Subtype.val x))
            âˆ˜L (Pi.compRightL ğ•œ _ Subtype.val)) x := by
        apply HasFDerivAt.fun_sum (fun s _hs â†¦ ?_)
        exact (ContinuousMultilinearMap.hasFDerivAt _ _).comp x (ContinuousLinearMap.hasFDerivAt _)
      rwa [â† H] at A
    ext v m
    simp only [ContinuousMultilinearMap.iteratedFDeriv, curryLeft_apply, sum_apply,
      iteratedFDerivComponent_apply, Finset.univ_sigma_univ,
      Pi.compRightL_apply, ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_comp',
      Finset.sum_apply, Function.comp_apply, linearDeriv_apply, Finset.sum_sigma']
    rw [â† (Equiv.embeddingFinSucc n Î¹).sum_comp]
    congr with e
    congr with k
    by_cases hke : k âˆˆ Set.range e
    Â· simp only [hke, â†“reduceDIte]
      split_ifs with hkf
      Â· simp only [â† Equiv.succ_embeddingFinSucc_fst_symm_apply e hkf hke, Fin.cons_succ]
      Â· obtain rfl : k = e 0 := by
          rcases hke with âŸ¨j, rflâŸ©
          simpa using hkf
        simp only [Function.Embedding.toEquivRange_symm_apply_self, Fin.cons_zero, Function.update,
          Pi.compRightL_apply]
        split_ifs with h
        Â· congr!
        Â· exfalso
          apply h
          simp_rw [â† Equiv.embeddingFinSucc_snd e]
    Â· have hkf : k âˆ‰ Set.range (Equiv.embeddingFinSucc n Î¹ e).1 := by
        contrapose! hke
        rw [Equiv.embeddingFinSucc_fst] at hke
        exact Set.range_comp_subset_range _ _ hke
      simp only [hke, hkf, â†“reduceDIte, Pi.compRightL,
        ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk]
      rw [Function.update_of_ne]
      contrapose! hke
      rw [show k = _ from Subtype.ext_iff.1 hke, Equiv.embeddingFinSucc_snd e]
      exact Set.mem_range_self _
  Â· rintro n -
    apply continuous_finset_sum _ (fun e _ â†¦ ?_)
    exact (ContinuousMultilinearMap.coe_continuous _).comp (ContinuousLinearMap.continuous _)

theorem iteratedFDeriv_eq (n : â„•) :
    iteratedFDeriv ğ•œ n f = f.iteratedFDeriv n :=
  funext fun x â†¦ (f.hasFTaylorSeriesUpTo_iteratedFDeriv.eq_iteratedFDeriv (m := n) le_top x).symm

theorem norm_iteratedFDeriv_le (n : â„•) (x : (i : Î¹) â†’ E i) :
    â€–iteratedFDeriv ğ•œ n f xâ€–
      â‰¤ Nat.descFactorial (Fintype.card Î¹) n * â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - n) := by
  rw [f.iteratedFDeriv_eq]
  exact f.norm_iteratedFDeriv_le' n x

end ContinuousMultilinearMap

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ğ•œ E F)

open Fintype ContinuousLinearMap in
theorem derivSeries_apply_diag (n : â„•) (x : E) :
    derivSeries p n (fun _ â†¦ x) x = (n + 1) â€¢ p (n + 1) fun _ â†¦ x := by
  simp only [derivSeries, compFormalMultilinearSeries_apply, changeOriginSeries,
    compContinuousMultilinearMap_coe, ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_coe,
    Function.comp_apply, ContinuousMultilinearMap.sum_apply, map_sum, coe_sum', Finset.sum_apply,
    continuousMultilinearCurryFin1_apply, Matrix.zero_empty]
  convert Finset.sum_const _
  Â· rw [Fin.snoc_zero, changeOriginSeriesTerm_apply, Finset.piecewise_same, add_comm]
  Â· rw [â† card, card_subtype, â† Finset.powerset_univ, â† Finset.powersetCard_eq_filter,
      Finset.card_powersetCard, â† card, card_fin, eq_comm, add_comm, Nat.choose_succ_self_right]

@[simp]
lemma derivSeries_coeff_one (p : FormalMultilinearSeries ğ•œ ğ•œ F) (n : â„•) :
    p.derivSeries.coeff n 1 = (n + 1) â€¢ p.coeff (n + 1) :=
  p.derivSeries_apply_diag _ _

end FormalMultilinearSeries

namespace HasFPowerSeriesOnBall

open FormalMultilinearSeries ENNReal Nat

variable {p : FormalMultilinearSeries ğ•œ E F} {f : E â†’ F} {x : E} {r : â„â‰¥0âˆ}
  (h : HasFPowerSeriesOnBall f p x r) (y : E)

include h in
theorem iteratedFDeriv_zero_apply_diag : iteratedFDeriv ğ•œ 0 f x = p 0 := by
  ext
  convert (h.hasSum <| EMetric.mem_ball_self h.r_pos).tsum_eq.symm
  Â· rw [iteratedFDeriv_zero_apply, add_zero]
  Â· rw [tsum_eq_single 0 fun n hn â†¦ by haveI := NeZero.mk hn; exact (p n).map_zero]
    exact congr(p 0 $(Subsingleton.elim _ _))

open ContinuousLinearMap

private theorem factorial_smul' {n : â„•} : âˆ€ {F : Type max u v} [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] {p : FormalMultilinearSeries ğ•œ E F}
    {f : E â†’ F}, HasFPowerSeriesOnBall f p x r â†’
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  induction n with | zero => _ | succ n ih => _ <;> intro F _ _ _ p f h
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· rw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag,
      â† ContinuousLinearMap.smul_apply, ih h.fderiv, iteratedFDeriv_succ_apply_right]
    rfl

variable [CompleteSpace F]
include h

/-- The iterated derivative of an analytic function, on vectors `(y, ..., y)`, is given by `n!`
times the `n`-th term in the power series. For a more general result giving the full iterated
derivative as a sum over the permutations of `Fin n`, see
`HasFPowerSeriesOnBall.iteratedFDeriv_eq_sum`. -/
theorem factorial_smul (n : â„•) :
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  cases n
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· rw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag,
      â† ContinuousLinearMap.smul_apply, factorial_smul' _ h.fderiv, iteratedFDeriv_succ_apply_right]
    rfl

theorem hasSum_iteratedFDeriv [CharZero ğ•œ] {y : E} (hy : y âˆˆ EMetric.ball 0 r) :
    HasSum (fun n â†¦ (n ! : ğ•œ)â»Â¹ â€¢ iteratedFDeriv ğ•œ n f x fun _ â†¦ y) (f (x + y)) := by
  convert h.hasSum hy with n
  rw [â† h.factorial_smul y n, smul_comm, â† smul_assoc, nsmul_eq_mul,
    mul_inv_cancelâ‚€ <| cast_ne_zero.mpr n.factorial_ne_zero, one_smul]

end HasFPowerSeriesOnBall

/-!
### Derivative of a linear map into multilinear maps
-/

namespace ContinuousLinearMap

variable {Î¹ : Type*} {G : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, NormedSpace ğ•œ (G i)]
  [Fintype Î¹] {H : Type*} [NormedAddCommGroup H]
  [NormedSpace ğ•œ H]

theorem hasFDerivAt_uncurry_of_multilinear [DecidableEq Î¹]
    (f : E â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ G F) (v : E Ã— Î  i, G i) :
    HasFDerivAt (fun (p : E Ã— Î  i, G i) â†¦ f p.1 p.2)
      ((f.flipMultilinear v.2) âˆ˜L (.fst _ _ _) +
        âˆ‘ i : Î¹, ((f v.1).toContinuousLinearMap v.2 i) âˆ˜L (.proj _) âˆ˜L (.snd _ _ _)) v := by
  convert HasFDerivAt.multilinear_comp (f.continuousMultilinearMapOption)
    (g := fun (_ : Option Î¹) p â†¦ p) (g' := fun _ â†¦ ContinuousLinearMap.id _ _) (x := v)
    (fun _ â†¦ hasFDerivAt_id _)
  have I : f.continuousMultilinearMapOption.toContinuousLinearMap (fun _ â†¦ v) none =
      (f.flipMultilinear v.2) âˆ˜L (.fst _ _ _) := by
    simp [ContinuousMultilinearMap.toContinuousLinearMap, continuousMultilinearMapOption]
    apply ContinuousLinearMap.ext (fun w â†¦ ?_)
    simp
  have J : âˆ€ (i : Î¹), f.continuousMultilinearMapOption.toContinuousLinearMap (fun _ â†¦ v) (some i)
      = ((f v.1).toContinuousLinearMap v.2 i) âˆ˜L (.proj _) âˆ˜L (.snd _ _ _) := by
    intro i
    apply ContinuousLinearMap.ext (fun w â†¦ ?_)
    simp only [ContinuousMultilinearMap.toContinuousLinearMap, continuousMultilinearMapOption,
      coe_mk', MultilinearMap.toLinearMap_apply, ContinuousMultilinearMap.coe_coe,
      MultilinearMap.coe_mkContinuous, MultilinearMap.coe_mk, ne_eq, reduceCtorEq,
      not_false_eq_true, Function.update_of_ne, coe_comp', coe_snd', Function.comp_apply,
      proj_apply]
    congr
    ext j
    rcases eq_or_ne j i with rfl | hij
    Â· simp
    Â· simp [hij]
  simp [I, J]

/-- Given `f` a linear map into multilinear maps, then the derivative
of `x â†¦ f (a x) (bâ‚ x, ..., bâ‚™ x)` at `x` applied to a vector `v` is given by
`f (a' v) (bâ‚ x, ...., bâ‚™ x) + âˆ‘ i, f a (bâ‚ x, ..., b'áµ¢ v, ..., bâ‚™ x)`. Version inside a set. -/
theorem _root_.HasFDerivWithinAt.linear_multilinear_comp
    [DecidableEq Î¹] {a : H â†’ E} {a' : H â†’L[ğ•œ] E}
    {b : âˆ€ i, H â†’ G i} {b' : âˆ€ i, H â†’L[ğ•œ] G i} {s : Set H} {x : H}
    (ha : HasFDerivWithinAt a a' s x) (hb : âˆ€ i, HasFDerivWithinAt (b i) (b' i) s x)
    (f : E â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ G F) :
    HasFDerivWithinAt (fun y â†¦ f (a y) (fun i â†¦ b i y))
      ((f.flipMultilinear (fun i â†¦ b i x)) âˆ˜L a' +
        âˆ‘ i, ((f (a x)).toContinuousLinearMap (fun j â†¦ b j x) i) âˆ˜L (b' i)) s x := by
  convert (hasFDerivAt_uncurry_of_multilinear f (a x, fun i â†¦ b i x)).comp_hasFDerivWithinAt x
    (ha.prodMk (hasFDerivWithinAt_pi.mpr hb))
  ext v
  simp

/-- Given `f` a linear map into multilinear maps, then the derivative
of `x â†¦ f (a x) (bâ‚ x, ..., bâ‚™ x)` at `x` applied to a vector `v` is given by
`f (a' v) (bâ‚ x, ...., bâ‚™ x) + âˆ‘ i, f a (bâ‚ x, ..., b'áµ¢ v, ..., bâ‚™ x)`. -/
theorem _root_.HasFDerivAt.linear_multilinear_comp [DecidableEq Î¹] {a : H â†’ E} {a' : H â†’L[ğ•œ] E}
    {b : âˆ€ i, H â†’ G i} {b' : âˆ€ i, H â†’L[ğ•œ] G i} {x : H}
    (ha : HasFDerivAt a a' x) (hb : âˆ€ i, HasFDerivAt (b i) (b' i) x)
    (f : E â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ G F) :
    HasFDerivAt (fun y â†¦ f (a y) (fun i â†¦ b i y))
      ((f.flipMultilinear (fun i â†¦ b i x)) âˆ˜L a' +
        âˆ‘ i, ((f (a x)).toContinuousLinearMap (fun j â†¦ b j x) i) âˆ˜L (b' i)) x := by
  simp_rw [â† hasFDerivWithinAt_univ] at ha hb âŠ¢
  exact HasFDerivWithinAt.linear_multilinear_comp ha hb f

end ContinuousLinearMap
