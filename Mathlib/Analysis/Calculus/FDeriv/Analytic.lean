/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.CPolynomial
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Analysis.Calculus.FDeriv.Add

#align_import analysis.calculus.fderiv_analytic from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Frechet derivatives of analytic functions.

A function expressible as a power series at a point has a Frechet derivative there.
Also the special case in terms of `deriv` when the domain is 1-dimensional.

As an application, we show that continuous multilinear maps are smooth. We also compute their
iterated derivatives, in `ContinuousMultilinearMap.iteratedFDeriv_eq`.
-/

open Filter Asymptotics

open scoped ENNReal

universe u v

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type v} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ}
variable {f : E â†’ F} {x : E} {s : Set E}

theorem HasFPowerSeriesAt.hasStrictFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x := by
  refine h.isBigO_image_sub_norm_mul_norm_sub.trans_isLittleO (IsLittleO.of_norm_right ?_)
  refine isLittleO_iff_exists_eq_mul.2 âŸ¨fun y => â€–y - (x, x)â€–, ?_, EventuallyEq.rflâŸ©
  refine (continuous_id.sub continuous_const).norm.tendsto' _ _ ?_
  rw [_root_.id, sub_self, norm_zero]
#align has_fpower_series_at.has_strict_fderiv_at HasFPowerSeriesAt.hasStrictFDerivAt

theorem HasFPowerSeriesAt.hasFDerivAt (h : HasFPowerSeriesAt f p x) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p 1)) x :=
  h.hasStrictFDerivAt.hasFDerivAt
#align has_fpower_series_at.has_fderiv_at HasFPowerSeriesAt.hasFDerivAt

theorem HasFPowerSeriesAt.differentiableAt (h : HasFPowerSeriesAt f p x) : DifferentiableAt ğ•œ f x :=
  h.hasFDerivAt.differentiableAt
#align has_fpower_series_at.differentiable_at HasFPowerSeriesAt.differentiableAt

theorem AnalyticAt.differentiableAt : AnalyticAt ğ•œ f x â†’ DifferentiableAt ğ•œ f x
  | âŸ¨_, hpâŸ© => hp.differentiableAt
#align analytic_at.differentiable_at AnalyticAt.differentiableAt

theorem AnalyticAt.differentiableWithinAt (h : AnalyticAt ğ•œ f x) : DifferentiableWithinAt ğ•œ f s x :=
  h.differentiableAt.differentiableWithinAt
#align analytic_at.differentiable_within_at AnalyticAt.differentiableWithinAt

theorem HasFPowerSeriesAt.fderiv_eq (h : HasFPowerSeriesAt f p x) :
    fderiv ğ•œ f x = continuousMultilinearCurryFin1 ğ•œ E F (p 1) :=
  h.hasFDerivAt.fderiv
#align has_fpower_series_at.fderiv_eq HasFPowerSeriesAt.fderiv_eq

theorem HasFPowerSeriesOnBall.differentiableOn [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) : DifferentiableOn ğ•œ f (EMetric.ball x r) := fun _ hy =>
  (h.analyticAt_of_mem hy).differentiableWithinAt
#align has_fpower_series_on_ball.differentiable_on HasFPowerSeriesOnBall.differentiableOn

theorem AnalyticOn.differentiableOn (h : AnalyticOn ğ•œ f s) : DifferentiableOn ğ•œ f s := fun y hy =>
  (h y hy).differentiableWithinAt
#align analytic_on.differentiable_on AnalyticOn.differentiableOn

theorem HasFPowerSeriesOnBall.hasFDerivAt [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).hasFPowerSeriesAt.hasFDerivAt
#align has_fpower_series_on_ball.has_fderiv_at HasFPowerSeriesOnBall.hasFDerivAt

theorem HasFPowerSeriesOnBall.fderiv_eq [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv
#align has_fpower_series_on_ball.fderiv_eq HasFPowerSeriesOnBall.fderiv_eq

/-- If a function has a power series on a ball, then so does its derivative. -/
theorem HasFPowerSeriesOnBall.fderiv [CompleteSpace F] (h : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesOnBall ?_
    simpa using ((p.hasFPowerSeriesOnBall_changeOrigin 1
      (h.r_pos.trans_le h.r_le)).mono h.r_pos h.r_le).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel]
  simpa only [edist_eq_coe_nnnorm_sub, EMetric.mem_ball] using hz
#align has_fpower_series_on_ball.fderiv HasFPowerSeriesOnBall.fderiv

/-- If a function is analytic on a set `s`, so is its FrÃ©chet derivative. -/
theorem AnalyticOn.fderiv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) :
    AnalyticOn ğ•œ (fderiv ğ•œ f) s := by
  intro y hy
  rcases h y hy with âŸ¨p, r, hpâŸ©
  exact hp.fderiv.analyticAt
#align analytic_on.fderiv AnalyticOn.fderiv

/-- If a function is analytic on a set `s`, so are its successive FrÃ©chet derivative. -/
theorem AnalyticOn.iteratedFDeriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) (n : â„•) :
    AnalyticOn ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction' n with n IH
  Â· rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_analyticOn h
  Â· rw [iteratedFDeriv_succ_eq_comp_left]
    -- Porting note: for reasons that I do not understand at all, `?g` cannot be inlined.
    convert ContinuousLinearMap.comp_analyticOn ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F)
    simp
#align analytic_on.iterated_fderiv AnalyticOn.iteratedFDeriv

/-- An analytic function is infinitely differentiable. -/
theorem AnalyticOn.contDiffOn [CompleteSpace F] (h : AnalyticOn ğ•œ f s) {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s :=
  let t := { x | AnalyticAt ğ•œ f x }
  suffices ContDiffOn ğ•œ n f t from this.mono h
  have H : AnalyticOn ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_analyticAt ğ•œ f
  contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
#align analytic_on.cont_diff_on AnalyticOn.contDiffOn

theorem AnalyticAt.contDiffAt [CompleteSpace F] (h : AnalyticAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x := by
  obtain âŸ¨s, hs, hfâŸ© := h.exists_mem_nhds_analyticOn
  exact hf.contDiffOn.contDiffAt hs

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}
variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

protected theorem HasFPowerSeriesAt.hasStrictDerivAt (h : HasFPowerSeriesAt f p x) :
    HasStrictDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictFDerivAt.hasStrictDerivAt
#align has_fpower_series_at.has_strict_deriv_at HasFPowerSeriesAt.hasStrictDerivAt

protected theorem HasFPowerSeriesAt.hasDerivAt (h : HasFPowerSeriesAt f p x) :
    HasDerivAt f (p 1 fun _ => 1) x :=
  h.hasStrictDerivAt.hasDerivAt
#align has_fpower_series_at.has_deriv_at HasFPowerSeriesAt.hasDerivAt

protected theorem HasFPowerSeriesAt.deriv (h : HasFPowerSeriesAt f p x) :
    deriv f x = p 1 fun _ => 1 :=
  h.hasDerivAt.deriv
#align has_fpower_series_at.deriv HasFPowerSeriesAt.deriv

/-- If a function is analytic on a set `s`, so is its derivative. -/
theorem AnalyticOn.deriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) : AnalyticOn ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_analyticOn h.fderiv
#align analytic_on.deriv AnalyticOn.deriv

/-- If a function is analytic on a set `s`, so are its successive derivatives. -/
theorem AnalyticOn.iterated_deriv [CompleteSpace F] (h : AnalyticOn ğ•œ f s) (n : â„•) :
    AnalyticOn ğ•œ (_root_.deriv^[n] f) s := by
  induction' n with n IH
  Â· exact h
  Â· simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv
#align analytic_on.iterated_deriv AnalyticOn.iterated_deriv

end deriv
section fderiv

variable {p : FormalMultilinearSeries ğ•œ E F} {r : â„â‰¥0âˆ} {n : â„•}
variable {f : E â†’ F} {x : E} {s : Set E}

/-! The case of continuously polynomial functions. We get the same differentiability
results as for analytic functions, but without the assumptions that `F` is complete. -/

theorem HasFiniteFPowerSeriesOnBall.differentiableOn
    (h : HasFiniteFPowerSeriesOnBall f p x n r) : DifferentiableOn ğ•œ f (EMetric.ball x r) :=
  fun _ hy â†¦ (h.cPolynomialAt_of_mem hy).analyticAt.differentiableWithinAt

theorem HasFiniteFPowerSeriesOnBall.hasFDerivAt (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFDerivAt f (continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1)) (x + y) :=
  (h.changeOrigin hy).toHasFPowerSeriesOnBall.hasFPowerSeriesAt.hasFDerivAt

theorem HasFiniteFPowerSeriesOnBall.fderiv_eq (h : HasFiniteFPowerSeriesOnBall f p x n r)
    {y : E} (hy : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    fderiv ğ•œ f (x + y) = continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin y 1) :=
  (h.hasFDerivAt hy).fderiv

/-- If a function has a finite power series on a ball, then so does its derivative. -/
protected theorem HasFiniteFPowerSeriesOnBall.fderiv
    (h : HasFiniteFPowerSeriesOnBall f p x (n + 1) r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x n r := by
  refine .congr (f := fun z â†¦ continuousMultilinearCurryFin1 ğ•œ E F (p.changeOrigin (z - x) 1)) ?_
    fun z hz â†¦ ?_
  Â· refine continuousMultilinearCurryFin1 ğ•œ E F
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFiniteFPowerSeriesOnBall ?_
    simpa using
      ((p.hasFiniteFPowerSeriesOnBall_changeOrigin 1 h.finite).mono h.r_pos le_top).comp_sub x
  dsimp only
  rw [â† h.fderiv_eq, add_sub_cancel]
  simpa only [edist_eq_coe_nnnorm_sub, EMetric.mem_ball] using hz

/-- If a function has a finite power series on a ball, then so does its derivative.
This is a variant of `HasFiniteFPowerSeriesOnBall.fderiv` where the degree of `f` is `< n`
and not `< n + 1`. -/
theorem HasFiniteFPowerSeriesOnBall.fderiv' (h : HasFiniteFPowerSeriesOnBall f p x n r) :
    HasFiniteFPowerSeriesOnBall (fderiv ğ•œ f) p.derivSeries x (n - 1) r := by
  obtain rfl | hn := eq_or_ne n 0
  Â· rw [zero_tsub]
    refine HasFiniteFPowerSeriesOnBall.bound_zero_of_eq_zero (fun y hy â†¦ ?_) h.r_pos fun n â†¦ ?_
    Â· rw [Filter.EventuallyEq.fderiv_eq (f := fun _ â†¦ 0)]
      Â· rw [fderiv_const, Pi.zero_apply]
      Â· exact Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨EMetric.ball x r,
          EMetric.isOpen_ball.mem_nhds hy, fun z hz â†¦ by rw [h.eq_zero_of_bound_zero z hz]âŸ©
    Â· apply ContinuousMultilinearMap.ext; intro a
      change (continuousMultilinearCurryFin1 ğ•œ E F) (p.changeOriginSeries 1 n a) = 0
      rw [p.changeOriginSeries_finite_of_finite h.finite 1 (Nat.zero_le _)]
      exact map_zero _
  Â· rw [â† Nat.succ_pred hn] at h
    exact h.fderiv

/-- If a function is polynomial on a set `s`, so is its FrÃ©chet derivative. -/
theorem CPolynomialOn.fderiv (h : CPolynomialOn ğ•œ f s) :
    CPolynomialOn ğ•œ (fderiv ğ•œ f) s := by
  intro y hy
  rcases h y hy with âŸ¨p, r, n, hpâŸ©
  exact hp.fderiv'.cPolynomialAt

/-- If a function is polynomial on a set `s`, so are its successive FrÃ©chet derivative. -/
theorem CPolynomialOn.iteratedFDeriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (iteratedFDeriv ğ•œ n f) s := by
  induction' n with n IH
  Â· rw [iteratedFDeriv_zero_eq_comp]
    exact ((continuousMultilinearCurryFin0 ğ•œ E F).symm : F â†’L[ğ•œ] E[Ã—0]â†’L[ğ•œ] F).comp_cPolynomialOn h
  Â· rw [iteratedFDeriv_succ_eq_comp_left]
    convert ContinuousLinearMap.comp_cPolynomialOn ?g IH.fderiv
    case g => exact â†‘(continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (n + 1) â†¦ E) F)
    simp

/-- A polynomial function is infinitely differentiable. -/
theorem CPolynomialOn.contDiffOn (h : CPolynomialOn ğ•œ f s) {n : â„•âˆ} :
    ContDiffOn ğ•œ n f s :=
  let t := { x | CPolynomialAt ğ•œ f x }
  suffices ContDiffOn ğ•œ n f t from this.mono h
  have H : CPolynomialOn ğ•œ f t := fun _x hx â†¦ hx
  have t_open : IsOpen t := isOpen_cPolynomialAt ğ•œ f
  contDiffOn_of_continuousOn_differentiableOn
    (fun m _ â†¦ (H.iteratedFDeriv m).continuousOn.congr
      fun  _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)
    (fun m _ â†¦ (H.iteratedFDeriv m).analyticOn.differentiableOn.congr
      fun _ hx â†¦ iteratedFDerivWithin_of_isOpen _ t_open hx)

theorem CPolynomialAt.contDiffAt (h : CPolynomialAt ğ•œ f x) {n : â„•âˆ} :
    ContDiffAt ğ•œ n f x :=
  let âŸ¨_, hs, hfâŸ© := h.exists_mem_nhds_cPolynomialOn
  hf.contDiffOn.contDiffAt hs

end fderiv

section deriv

variable {p : FormalMultilinearSeries ğ•œ ğ•œ F} {r : â„â‰¥0âˆ}
variable {f : ğ•œ â†’ F} {x : ğ•œ} {s : Set ğ•œ}

/-- If a function is polynomial on a set `s`, so is its derivative. -/
protected theorem CPolynomialOn.deriv (h : CPolynomialOn ğ•œ f s) : CPolynomialOn ğ•œ (deriv f) s :=
  (ContinuousLinearMap.apply ğ•œ F (1 : ğ•œ)).comp_cPolynomialOn h.fderiv

/-- If a function is polynomial on a set `s`, so are its successive derivatives. -/
theorem CPolynomialOn.iterated_deriv (h : CPolynomialOn ğ•œ f s) (n : â„•) :
    CPolynomialOn ğ•œ (deriv^[n] f) s := by
  induction' n with n IH
  Â· exact h
  Â· simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

end deriv

namespace ContinuousMultilinearMap

variable {Î¹ : Type*} {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [Fintype Î¹] (f : ContinuousMultilinearMap ğ•œ E F)

open FormalMultilinearSeries

protected theorem hasFiniteFPowerSeriesOnBall :
    HasFiniteFPowerSeriesOnBall f f.toFormalMultilinearSeries 0 (Fintype.card Î¹ + 1) âŠ¤ :=
  .mk' (fun m hm â†¦ dif_neg (Nat.succ_le_iff.mp hm).ne) ENNReal.zero_lt_top fun y _ â†¦ by
    rw [Finset.sum_eq_single_of_mem _ (Finset.self_mem_range_succ _), zero_add]
    Â· rw [toFormalMultilinearSeries, dif_pos rfl]; rfl
    Â· intro m _ ne; rw [toFormalMultilinearSeries, dif_neg ne.symm]; rfl

theorem changeOriginSeries_support {k l : â„•} (h : k + l â‰  Fintype.card Î¹) :
    f.toFormalMultilinearSeries.changeOriginSeries k l = 0 :=
  Finset.sum_eq_zero fun _ _ â†¦ by
    simp_rw [FormalMultilinearSeries.changeOriginSeriesTerm,
      toFormalMultilinearSeries, dif_neg h.symm, LinearIsometryEquiv.map_zero]

variable {n : â„•âˆ} (x : âˆ€ i, E i)

open Finset in
theorem changeOrigin_toFormalMultilinearSeries [DecidableEq Î¹] :
    continuousMultilinearCurryFin1 ğ•œ (âˆ€ i, E i) F (f.toFormalMultilinearSeries.changeOrigin x 1) =
    f.linearDeriv x := by
  ext y
  rw [continuousMultilinearCurryFin1_apply, linearDeriv_apply,
      changeOrigin, FormalMultilinearSeries.sum]
  cases isEmpty_or_nonempty Î¹
  Â· have (l) : 1 + l â‰  Fintype.card Î¹ := by
      rw [add_comm, Fintype.card_eq_zero]; exact Nat.succ_ne_zero _
    simp_rw [Fintype.sum_empty, changeOriginSeries_support _ (this _), zero_apply _, tsum_zero]; rfl
  rw [tsum_eq_single (Fintype.card Î¹ - 1), changeOriginSeries]; swap
  Â· intro m hm
    rw [Ne, eq_tsub_iff_add_eq_of_le (by exact Fintype.card_pos), add_comm] at hm
    rw [f.changeOriginSeries_support hm, zero_apply]
  rw [sum_apply, ContinuousMultilinearMap.sum_apply, Fin.snoc_zero]
  simp_rw [changeOriginSeriesTerm_apply]
  refine (Fintype.sum_bijective (?_ âˆ˜ Fintype.equivFinOfCardEq (Nat.add_sub_of_le
    Fintype.card_pos).symm) (.comp ?_ <| Equiv.bijective _) _ _ fun i â†¦ ?_).symm
  Â· exact (âŸ¨{Â·}á¶œ, by
      rw [card_compl, Fintype.card_fin, card_singleton, Nat.add_sub_cancel_left]âŸ©)
  Â· use fun _ _ â†¦ (singleton_injective <| compl_injective <| Subtype.ext_iff.mp Â·)
    intro âŸ¨s, hsâŸ©
    have h : sá¶œ.card = 1 := by rw [card_compl, hs, Fintype.card_fin, Nat.add_sub_cancel]
    obtain âŸ¨a, haâŸ© := card_eq_one.mp h
    exact âŸ¨a, Subtype.ext (compl_eq_comm.mp ha)âŸ©
  rw [Function.comp_apply, Subtype.coe_mk, compl_singleton, piecewise_erase_univ,
    toFormalMultilinearSeries, dif_pos (Nat.add_sub_of_le Fintype.card_pos).symm]
  simp_rw [domDomCongr_apply, compContinuousLinearMap_apply, ContinuousLinearMap.proj_apply,
    Function.update_apply, (Equiv.injective _).eq_iff, ite_apply]
  congr; ext j
  obtain rfl | hj := eq_or_ne j i
  Â· rw [Function.update_same, if_pos rfl]
  Â· rw [Function.update_noteq hj, if_neg hj]

protected theorem hasFDerivAt [DecidableEq Î¹] : HasFDerivAt f (f.linearDeriv x) x := by
  rw [â† changeOrigin_toFormalMultilinearSeries]
  convert f.hasFiniteFPowerSeriesOnBall.hasFDerivAt (y := x) ENNReal.coe_lt_top
  rw [zero_add]

/-- Technical lemma used in the proof of `hasFTaylorSeriesUpTo_iteratedFDeriv`, to compare sums
over embedding of `Fin k` and `Fin (k + 1)`. -/
private lemma _root_.Equiv.succ_embeddingFinSucc_fst_symm_apply {Î¹ : Type*} [DecidableEq Î¹]
    {n : â„•} (e : Fin (n+1) â†ª Î¹) {k : Î¹}
    (h'k : k âˆˆ Set.range (Equiv.embeddingFinSucc n Î¹ e).1) (hk : k âˆˆ Set.range e) :
    Fin.succ ((Equiv.embeddingFinSucc n Î¹ e).1.toEquivRange.symm âŸ¨k, h'kâŸ©)
      = e.toEquivRange.symm âŸ¨k, hkâŸ© := by
  rcases hk with âŸ¨j, rflâŸ©
  have hj : j â‰  0 := by
    rintro rfl
    simp at h'k
  simp only [Function.Embedding.toEquivRange_symm_apply_self]
  have : e j = (Equiv.embeddingFinSucc n Î¹ e).1 (Fin.pred j hj) := by simp
  simp_rw [this]
  simp [-Equiv.embeddingFinSucc_fst]

/-- A continuous multilinear function `f` admits a Taylor series, whose successive terms are given
by `f.iteratedFDeriv n`. This is the point of the definition of `f.iteratedFDeriv`. -/
theorem hasFTaylorSeriesUpTo_iteratedFDeriv :
    HasFTaylorSeriesUpTo âŠ¤ f (fun v n â†¦ f.iteratedFDeriv n v) := by
  classical
  constructor
  Â· simp [ContinuousMultilinearMap.iteratedFDeriv]
  Â· rintro n - x
    suffices H : curryLeft (f.iteratedFDeriv (Nat.succ n) x) = (âˆ‘ e : Fin n â†ª Î¹,
          ((iteratedFDerivComponent f e.toEquivRange).linearDeriv
            (Pi.compRightL ğ•œ _ Subtype.val x)) âˆ˜L (Pi.compRightL ğ•œ _ Subtype.val)) by
      have A : HasFDerivAt (f.iteratedFDeriv n) (âˆ‘ e : Fin n â†ª Î¹,
          ((iteratedFDerivComponent f e.toEquivRange).linearDeriv (Pi.compRightL ğ•œ _ Subtype.val x))
            âˆ˜L (Pi.compRightL ğ•œ _ Subtype.val)) x := by
        apply HasFDerivAt.sum (fun s _hs â†¦ ?_)
        exact (ContinuousMultilinearMap.hasFDerivAt _ _).comp x (ContinuousLinearMap.hasFDerivAt _)
      rwa [â† H] at A
    ext v m
    simp only [ContinuousMultilinearMap.iteratedFDeriv, curryLeft_apply, sum_apply,
      iteratedFDerivComponent_apply, Finset.univ_sigma_univ,
      Pi.compRightL_apply, ContinuousLinearMap.coe_sum', ContinuousLinearMap.coe_comp',
      Finset.sum_apply, Function.comp_apply, linearDeriv_apply, Finset.sum_sigma']
    rw [â† (Equiv.embeddingFinSucc n Î¹).sum_comp]
    congr with e
    congr with k
    by_cases hke : k âˆˆ Set.range e
    Â· simp only [hke, â†“reduceDite]
      split_ifs with hkf
      Â· simp only [â† Equiv.succ_embeddingFinSucc_fst_symm_apply e hkf hke, Fin.cons_succ]
      Â· obtain rfl : k = e 0 := by
          rcases hke with âŸ¨j, rflâŸ©
          simpa using hkf
        simp only [Function.Embedding.toEquivRange_symm_apply_self, Fin.cons_zero, Function.update,
          Pi.compRightL_apply]
        split_ifs with h
        Â· congr!
        Â· exfalso
          apply h
          simp_rw [â† Equiv.embeddingFinSucc_snd e]
    Â· have hkf : k âˆ‰ Set.range (Equiv.embeddingFinSucc n Î¹ e).1 := by
        contrapose! hke
        rw [Equiv.embeddingFinSucc_fst] at hke
        exact Set.range_comp_subset_range _ _ hke
      simp only [hke, hkf, â†“reduceDite, Pi.compRightL,
        ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk]
      rw [Function.update_noteq]
      contrapose! hke
      rw [show k = _ from Subtype.ext_iff_val.1 hke, Equiv.embeddingFinSucc_snd e]
      exact Set.mem_range_self _
  Â· rintro n -
    apply continuous_finset_sum _ (fun e _ â†¦ ?_)
    exact (ContinuousMultilinearMap.coe_continuous _).comp (ContinuousLinearMap.continuous _)

theorem iteratedFDeriv_eq (n : â„•) :
    iteratedFDeriv ğ•œ n f = f.iteratedFDeriv n :=
  funext fun x â†¦ (f.hasFTaylorSeriesUpTo_iteratedFDeriv.eq_iteratedFDeriv (m := n) le_top x).symm

theorem norm_iteratedFDeriv_le (n : â„•) (x : (i : Î¹) â†’ E i) :
    â€–iteratedFDeriv ğ•œ n f xâ€–
      â‰¤ Nat.descFactorial (Fintype.card Î¹) n * â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - n) := by
  rw [f.iteratedFDeriv_eq]
  exact f.norm_iteratedFDeriv_le' n x

lemma cPolynomialAt : CPolynomialAt ğ•œ f x :=
  f.hasFiniteFPowerSeriesOnBall.cPolynomialAt_of_mem
    (by simp only [Metric.emetric_ball_top, Set.mem_univ])

lemma cPolyomialOn : CPolynomialOn ğ•œ f âŠ¤ := fun x _ â†¦ f.cPolynomialAt x

lemma contDiffAt : ContDiffAt ğ•œ n f x := (f.cPolynomialAt x).contDiffAt

lemma contDiff : ContDiff ğ•œ n f := contDiff_iff_contDiffAt.mpr f.contDiffAt

end ContinuousMultilinearMap

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ğ•œ E F)

open Fintype ContinuousLinearMap in
theorem derivSeries_apply_diag (n : â„•) (x : E) :
    derivSeries p n (fun _ â†¦ x) x = (n + 1) â€¢ p (n + 1) fun _ â†¦ x := by
  simp only [derivSeries, compFormalMultilinearSeries_apply, changeOriginSeries,
    compContinuousMultilinearMap_coe, ContinuousLinearEquiv.coe_coe, LinearIsometryEquiv.coe_coe,
    Function.comp_apply, ContinuousMultilinearMap.sum_apply, map_sum, coe_sum', Finset.sum_apply,
    continuousMultilinearCurryFin1_apply, Matrix.zero_empty]
  convert Finset.sum_const _
  Â· rw [Fin.snoc_zero, changeOriginSeriesTerm_apply, Finset.piecewise_same, add_comm]
  Â· rw [â† card, card_subtype, â† Finset.powerset_univ, â† Finset.powersetCard_eq_filter,
      Finset.card_powersetCard, â† card, card_fin, eq_comm, add_comm, Nat.choose_succ_self_right]

end FormalMultilinearSeries

namespace HasFPowerSeriesOnBall

open FormalMultilinearSeries ENNReal Nat

variable {p : FormalMultilinearSeries ğ•œ E F} {f : E â†’ F} {x : E} {r : â„â‰¥0âˆ}
  (h : HasFPowerSeriesOnBall f p x r) (y : E)

theorem iteratedFDeriv_zero_apply_diag : iteratedFDeriv ğ•œ 0 f x = p 0 := by
  ext
  convert (h.hasSum <| EMetric.mem_ball_self h.r_pos).tsum_eq.symm
  Â· rw [iteratedFDeriv_zero_apply, add_zero]
  Â· rw [tsum_eq_single 0 fun n hn â†¦ by haveI := NeZero.mk hn; exact (p n).map_zero]
    exact congr(p 0 $(Subsingleton.elim _ _))

open ContinuousLinearMap

private theorem factorial_smul' {n : â„•} : âˆ€ {F : Type max u v} [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] {p : FormalMultilinearSeries ğ•œ E F}
    {f : E â†’ F}, HasFPowerSeriesOnBall f p x r â†’
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  induction' n with n ih <;> intro F _ _ _ p f h
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· rw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag, â† smul_apply,
      ih h.fderiv, iteratedFDeriv_succ_apply_right]
    rfl

variable [CompleteSpace F]

theorem factorial_smul (n : â„•) :
    n ! â€¢ p n (fun _ â†¦ y) = iteratedFDeriv ğ•œ n f x (fun _ â†¦ y) := by
  cases n
  Â· rw [factorial_zero, one_smul, h.iteratedFDeriv_zero_apply_diag]
  Â· rw [factorial_succ, mul_comm, mul_smul, â† derivSeries_apply_diag, â† smul_apply,
      factorial_smul'.{_,u,v} _ h.fderiv, iteratedFDeriv_succ_apply_right]
    rfl

theorem hasSum_iteratedFDeriv [CharZero ğ•œ] {y : E} (hy : y âˆˆ EMetric.ball 0 r) :
    HasSum (fun n â†¦ (n ! : ğ•œ)â»Â¹ â€¢ iteratedFDeriv ğ•œ n f x fun _ â†¦ y) (f (x + y)) := by
  convert h.hasSum hy with n
  rw [â† h.factorial_smul y n, smul_comm, â† smul_assoc, nsmul_eq_mul,
    mul_inv_cancel <| cast_ne_zero.mpr n.factorial_ne_zero, one_smul]

end HasFPowerSeriesOnBall
