/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Basic

/-!
# The derivative of the scalar restriction of a linear map

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
the scalar restriction of a linear map.
-/


open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section RestrictScalars

/-!
### Restricting from `â„‚` to `â„`, or generally from `ğ•œ'` to `ğ•œ`

If a function is differentiable over `â„‚`, then it is differentiable over `â„`. In this paragraph,
we give variants of this statement, in the general situation where `â„‚` and `â„` are replaced
respectively by `ğ•œ'` and `ğ•œ` where `ğ•œ'` is a normed algebra over `ğ•œ`.
-/


variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E]
variable [IsScalarTower ğ•œ ğ•œ' E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F]
variable [IsScalarTower ğ•œ ğ•œ' F]
variable {f : E â†’ F} {f' : E â†’L[ğ•œ'] F} {s : Set E} {x : E}

@[fun_prop]
theorem HasStrictFDerivAt.restrictScalars (h : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt f (f'.restrictScalars ğ•œ) x :=
  .of_isLittleO h.isLittleO

theorem HasFDerivAtFilter.restrictScalars {L} (h : HasFDerivAtFilter f f' x L) :
    HasFDerivAtFilter f (f'.restrictScalars ğ•œ) x L :=
  .of_isLittleO h.isLittleO

@[fun_prop]
theorem HasFDerivAt.restrictScalars (h : HasFDerivAt f f' x) :
    HasFDerivAt f (f'.restrictScalars ğ•œ) x :=
  .of_isLittleO h.isLittleO

@[fun_prop]
theorem HasFDerivWithinAt.restrictScalars (h : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt f (f'.restrictScalars ğ•œ) s x :=
  .of_isLittleO h.isLittleO

@[fun_prop]
theorem DifferentiableAt.restrictScalars (h : DifferentiableAt ğ•œ' f x) : DifferentiableAt ğ•œ f x :=
  (h.hasFDerivAt.restrictScalars ğ•œ).differentiableAt

@[fun_prop]
theorem DifferentiableWithinAt.restrictScalars (h : DifferentiableWithinAt ğ•œ' f s x) :
    DifferentiableWithinAt ğ•œ f s x :=
  (h.hasFDerivWithinAt.restrictScalars ğ•œ).differentiableWithinAt

@[fun_prop]
theorem DifferentiableOn.restrictScalars (h : DifferentiableOn ğ•œ' f s) : DifferentiableOn ğ•œ f s :=
  fun x hx => (h x hx).restrictScalars ğ•œ

@[fun_prop]
theorem Differentiable.restrictScalars (h : Differentiable ğ•œ' f) : Differentiable ğ•œ f := fun x =>
  (h x).restrictScalars ğ•œ

@[fun_prop]
theorem HasFDerivWithinAt.of_restrictScalars {g' : E â†’L[ğ•œ] F} (h : HasFDerivWithinAt f g' s x)
    (H : f'.restrictScalars ğ•œ = g') : HasFDerivWithinAt f f' s x := by
  rw [â† H] at h
  exact .of_isLittleO h.isLittleO

@[fun_prop]
theorem hasFDerivAt_of_restrictScalars {g' : E â†’L[ğ•œ] F} (h : HasFDerivAt f g' x)
    (H : f'.restrictScalars ğ•œ = g') : HasFDerivAt f f' x := by
  rw [â† H] at h
  exact .of_isLittleO h.isLittleO

theorem DifferentiableAt.fderiv_restrictScalars (h : DifferentiableAt ğ•œ' f x) :
    fderiv ğ•œ f x = (fderiv ğ•œ' f x).restrictScalars ğ•œ :=
  (h.hasFDerivAt.restrictScalars ğ•œ).fderiv

theorem DifferentiableWithinAt.restrictScalars_fderivWithin (hf : DifferentiableWithinAt ğ•œ' f s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    (fderivWithin ğ•œ' f s x).restrictScalars ğ•œ = fderivWithin ğ•œ f s x :=
  ((hf.hasFDerivWithinAt.restrictScalars ğ•œ).fderivWithin hs).symm

theorem differentiableWithinAt_iff_restrictScalars (hf : DifferentiableWithinAt ğ•œ f s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) : DifferentiableWithinAt ğ•œ' f s x â†”
      âˆƒ g' : E â†’L[ğ•œ'] F, g'.restrictScalars ğ•œ = fderivWithin ğ•œ f s x := by
  constructor
  Â· rintro âŸ¨g', hg'âŸ©
    exact âŸ¨g', hs.eq (hg'.restrictScalars ğ•œ) hf.hasFDerivWithinAtâŸ©
  Â· rintro âŸ¨f', hf'âŸ©
    exact âŸ¨f', hf.hasFDerivWithinAt.of_restrictScalars ğ•œ hf'âŸ©

theorem differentiableAt_iff_restrictScalars (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ' f x â†” âˆƒ g' : E â†’L[ğ•œ'] F, g'.restrictScalars ğ•œ = fderiv ğ•œ f x := by
  rw [â† differentiableWithinAt_univ, â† fderivWithin_univ]
  exact
    differentiableWithinAt_iff_restrictScalars ğ•œ hf.differentiableWithinAt uniqueDiffWithinAt_univ

end RestrictScalars
