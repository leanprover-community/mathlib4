/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.Lemmas
import Mathlib.Analysis.Calculus.FDeriv.Defs
import Mathlib.Analysis.Calculus.TangentCone
import Mathlib.Analysis.NormedSpace.OperatorNorm.Asymptotics

/-!
# The FrÃ©chet derivative: basic properties

Let `E` and `F` be normed spaces, `f : E â†’ F`, and `f' : E â†’L[ğ•œ] F` a
continuous ğ•œ-linear map, where `ğ•œ` is a non-discrete normed field. Then

  `HasFDerivWithinAt f f' s x`

says that `f` has derivative `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `HasFDerivAt f f' x := HasFDerivWithinAt f f' x univ`

Finally,

  `HasStrictFDerivAt f f' x`

means that `f : E â†’ F` has derivative `f' : E â†’L[ğ•œ] F` in the sense of strict differentiability,
i.e., `f y - f z - f'(y - z) = o(y - z)` as `y, z â†’ x`. This notion is used in the inverse
function theorem, and is defined here only to avoid proving theorems like
`IsBoundedBilinearMap.hasFDerivAt` twice: first for `HasFDerivAt`, then for
`HasStrictFDerivAt`.

## Main results

This file builds on the bare-bones definition given in `Defs.lean` by establishing a variety of
relatively straightforward properties of the derivative.

Deeper properties are defined in other files in the folder `Analysis/Calculus/FDeriv/`, which
contain the usual formulas (and existence assertions) for the derivative of
* constants (`Const.lean`)
* bounded linear maps (`Linear.lean`)
* bounded bilinear maps (`Bilinear.lean`)
* sum of two functions (`Add.lean`)
* sum of finitely many functions (`Add.lean`)
* multiplication of a function by a scalar constant (`Add.lean`)
* negative of a function (`Add.lean`)
* subtraction of two functions (`Add.lean`)
* multiplication of a function by a scalar function (`Mul.lean`)
* multiplication of two scalar functions (`Mul.lean`)
* composition of functions (the chain rule) (`Comp.lean`)
* inverse function (`Mul.lean`)
  (assuming that it exists; the inverse function theorem is in `../Inverse.lean`)

For most binary operations we also define `const_op` and `op_const` theorems for the cases when
the first or second argument is a constant. This makes writing chains of `HasDerivAt`'s easier,
and they more frequently lead to the desired result.

One can also interpret the derivative of a function `f : ğ•œ â†’ E` as an element of `E` (by identifying
a linear function from `ğ•œ` to `E` with its value at `1`). Results on the FrÃ©chet derivative are
translated to this more elementary point of view on the derivative in the file `Deriv.lean`. The
derivative of polynomials is handled there, as it is naturally one-dimensional.

The simplifier is set up to prove automatically that some functions are differentiable, or
differentiable at a point (but not differentiable on a set or within a set at a point, as checking
automatically that the good domains are mapped one to the other when using composition is not
something the simplifier can easily do). This means that one can write
`example (x : â„) : Differentiable â„ (fun x â†¦ sin (exp (3 + x^2)) - 5 * cos x) := by simp`.
If there are divisions, one needs to supply to the simplifier proofs that the denominators do
not vanish, as in
```lean
example (x : â„) (h : 1 + sin x â‰  0) : DifferentiableAt â„ (fun x â†¦ exp x / (1 + sin x)) x := by
  simp [h]
```
Of course, these examples only work once `exp`, `cos` and `sin` have been shown to be
differentiable, in `Mathlib/Analysis/SpecialFunctions/Trigonometric/Deriv.lean`.

The simplifier is not set up to compute the FrÃ©chet derivative of maps (as these are in general
complicated multidimensional linear maps), but it will compute one-dimensional derivatives,
see `Deriv.lean`.

## Implementation details

For a discussion of the definitions and their rationale, see the file docstring of
`Mathlib.Analysis.Calculus.FDeriv.Defs`.

To make sure that the simplifier can prove automatically that functions are differentiable, we tag
many lemmas with the `simp` attribute, for instance those saying that the sum of differentiable
functions is differentiable, as well as their product, their cartesian product, and so on. A notable
exception is the chain rule: we do not mark as a simp lemma the fact that, if `f` and `g` are
differentiable, then their composition also is: `simp` would always be able to match this lemma,
by taking `f` or `g` to be the identity. Instead, for every reasonable function (say, `exp`),
we add a lemma that if `f` is differentiable then so is `(fun x â†¦ exp (f x))`. This means adding
some boilerplate lemmas, but these can also be useful in their own right.

Tests for this ability of the simplifier (with more examples) are provided in
`Tests/Differentiable.lean`.

## TODO

Generalize more results to topological vector spaces.

## Tags

derivative, differentiable, FrÃ©chet, calculus

-/

open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

section DerivativeUniqueness

/- In this section, we discuss the uniqueness of the derivative.
We prove that the definitions `UniqueDiffWithinAt` and `UniqueDiffOn` indeed imply the
uniqueness of the derivative. -/
/-- If a function f has a derivative f' at x, a rescaled version of f around x converges to f',
i.e., `n (f (x + (1/n) v) - f x)` converges to `f' v`. More generally, if `c n` tends to infinity
and `c n * d n` tends to `v`, then `c n * (f (x + d n) - f x)` tends to `f' v`. This lemma expresses
this fact, for functions having a derivative within a set. Its specific formulation is useful for
tangent cone related discussions. -/
theorem HasFDerivWithinAt.lim (h : HasFDerivWithinAt f f' s x) {Î± : Type*} (l : Filter Î±)
    {c : Î± â†’ ğ•œ} {d : Î± â†’ E} {v : E} (dtop : âˆ€á¶  n in l, x + d n âˆˆ s)
    (clim : Tendsto (fun n => â€–c nâ€–) l atTop) (cdlim : Tendsto (fun n => c n â€¢ d n) l (ğ“ v)) :
    Tendsto (fun n => c n â€¢ (f (x + d n) - f x)) l (ğ“ (f' v)) := by
  have tendsto_arg : Tendsto (fun n => x + d n) l (ğ“[s] x) := by
    conv in ğ“[s] x => rw [â† add_zero x]
    rw [nhdsWithin, tendsto_inf]
    constructor
    Â· apply tendsto_const_nhds.add (tangentConeAt.lim_zero l clim cdlim)
    Â· rwa [tendsto_principal]
  have : (fun y => f y - f x - f' (y - x)) =o[ğ“[s] x] fun y => y - x := h.isLittleO
  have : (fun n => f (x + d n) - f x - f' (x + d n - x)) =o[l] fun n => x + d n - x :=
    this.comp_tendsto tendsto_arg
  have : (fun n => f (x + d n) - f x - f' (d n)) =o[l] d := by simpa only [add_sub_cancel_left]
  have : (fun n => c n â€¢ (f (x + d n) - f x - f' (d n))) =o[l] fun n => c n â€¢ d n :=
    (isBigO_refl c l).smul_isLittleO this
  have : (fun n => c n â€¢ (f (x + d n) - f x - f' (d n))) =o[l] fun _ => (1 : â„) :=
    this.trans_isBigO (cdlim.isBigO_one â„)
  have L1 : Tendsto (fun n => c n â€¢ (f (x + d n) - f x - f' (d n))) l (ğ“ 0) :=
    (isLittleO_one_iff â„).1 this
  have L2 : Tendsto (fun n => f' (c n â€¢ d n)) l (ğ“ (f' v)) :=
    Tendsto.comp f'.cont.continuousAt cdlim
  have L3 :
    Tendsto (fun n => c n â€¢ (f (x + d n) - f x - f' (d n)) + f' (c n â€¢ d n)) l (ğ“ (0 + f' v)) :=
    L1.add L2
  have :
    (fun n => c n â€¢ (f (x + d n) - f x - f' (d n)) + f' (c n â€¢ d n)) = fun n =>
      c n â€¢ (f (x + d n) - f x) := by
    ext n
    simp [smul_sub]
  rwa [this, zero_add] at L3

/-- If `f'` and `fâ‚'` are two derivatives of `f` within `s` at `x`, then they are equal on the
tangent cone to `s` at `x` -/
theorem HasFDerivWithinAt.unique_on (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt f fâ‚' s x) : EqOn f' fâ‚' (tangentConeAt ğ•œ s x) :=
  fun _ âŸ¨_, _, dtop, clim, cdlimâŸ© =>
  tendsto_nhds_unique (hf.lim atTop dtop clim cdlim) (hg.lim atTop dtop clim cdlim)

/-- `UniqueDiffWithinAt` achieves its goal: it implies the uniqueness of the derivative. -/
theorem UniqueDiffWithinAt.eq (H : UniqueDiffWithinAt ğ•œ s x) (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt f fâ‚' s x) : f' = fâ‚' :=
  ContinuousLinearMap.ext_on H.1 (hf.unique_on hg)

theorem UniqueDiffOn.eq (H : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) (h : HasFDerivWithinAt f f' s x)
    (hâ‚ : HasFDerivWithinAt f fâ‚' s x) : f' = fâ‚' :=
  (H x hx).eq h hâ‚

end DerivativeUniqueness

section FDerivProperties

/-! ### Basic properties of the derivative -/


theorem hasFDerivAtFilter_iff_tendsto :
    HasFDerivAtFilter f f' x L â†”
      Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) L (ğ“ 0) := by
  have h : âˆ€ x', â€–x' - xâ€– = 0 â†’ â€–f x' - f x - f' (x' - x)â€– = 0 := fun x' hx' => by
    rw [sub_eq_zero.1 (norm_eq_zero.1 hx')]
    simp
  rw [hasFDerivAtFilter_iff_isLittleO, â† isLittleO_norm_left, â† isLittleO_norm_right,
    isLittleO_iff_tendsto h]
  exact tendsto_congr fun _ => div_eq_inv_mul _ _

theorem hasFDerivWithinAt_iff_tendsto :
    HasFDerivWithinAt f f' s x â†”
      Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) (ğ“[s] x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem hasFDerivAt_iff_tendsto :
    HasFDerivAt f f' x â†” Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) (ğ“ x) (ğ“ 0) :=
  hasFDerivAtFilter_iff_tendsto

theorem hasFDerivAt_iff_isLittleO_nhds_zero :
    HasFDerivAt f f' x â†” (fun h : E => f (x + h) - f x - f' h) =o[ğ“ 0] fun h => h := by
  rw [HasFDerivAt, hasFDerivAtFilter_iff_isLittleO, â† map_add_left_nhds_zero x, isLittleO_map]
  simp [Function.comp_def]

nonrec theorem HasFDerivAtFilter.mono (h : HasFDerivAtFilter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
    HasFDerivAtFilter f f' x Lâ‚ :=
  .of_isLittleOTVS <| h.isLittleOTVS.mono hst

theorem HasFDerivWithinAt.mono_of_mem_nhdsWithin
    (h : HasFDerivWithinAt f f' t x) (hst : t âˆˆ ğ“[s] x) :
    HasFDerivWithinAt f f' s x :=
  h.mono <| nhdsWithin_le_iff.mpr hst

nonrec theorem HasFDerivWithinAt.mono (h : HasFDerivWithinAt f f' t x) (hst : s âŠ† t) :
    HasFDerivWithinAt f f' s x :=
  h.mono <| nhdsWithin_mono _ hst

theorem HasFDerivAt.hasFDerivAtFilter (h : HasFDerivAt f f' x) (hL : L â‰¤ ğ“ x) :
    HasFDerivAtFilter f f' x L :=
  h.mono hL

@[fun_prop]
theorem HasFDerivAt.hasFDerivWithinAt (h : HasFDerivAt f f' x) : HasFDerivWithinAt f f' s x :=
  h.hasFDerivAtFilter inf_le_left

@[fun_prop]
theorem HasFDerivWithinAt.differentiableWithinAt (h : HasFDerivWithinAt f f' s x) :
    DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨f', hâŸ©

@[fun_prop]
theorem HasFDerivAt.differentiableAt (h : HasFDerivAt f f' x) : DifferentiableAt ğ•œ f x :=
  âŸ¨f', hâŸ©

@[simp]
theorem hasFDerivWithinAt_univ : HasFDerivWithinAt f f' univ x â†” HasFDerivAt f f' x := by
  simp only [HasFDerivWithinAt, nhdsWithin_univ, HasFDerivAt]

alias âŸ¨HasFDerivWithinAt.hasFDerivAt_of_univ, _âŸ© := hasFDerivWithinAt_univ

theorem differentiableWithinAt_univ :
    DifferentiableWithinAt ğ•œ f univ x â†” DifferentiableAt ğ•œ f x := by
  simp only [DifferentiableWithinAt, hasFDerivWithinAt_univ, DifferentiableAt]

theorem fderiv_zero_of_not_differentiableAt (h : Â¬DifferentiableAt ğ•œ f x) : fderiv ğ•œ f x = 0 := by
  rw [fderiv, fderivWithin_zero_of_not_differentiableWithinAt]
  rwa [differentiableWithinAt_univ]

theorem hasFDerivWithinAt_of_mem_nhds (h : s âˆˆ ğ“ x) :
    HasFDerivWithinAt f f' s x â†” HasFDerivAt f f' x := by
  rw [HasFDerivAt, HasFDerivWithinAt, nhdsWithin_eq_nhds.mpr h]

lemma hasFDerivWithinAt_of_isOpen (h : IsOpen s) (hx : x âˆˆ s) :
    HasFDerivWithinAt f f' s x â†” HasFDerivAt f f' x :=
  hasFDerivWithinAt_of_mem_nhds (h.mem_nhds hx)

@[simp]
theorem hasFDerivWithinAt_insert {y : E} :
    HasFDerivWithinAt f f' (insert y s) x â†” HasFDerivWithinAt f f' s x := by
  rcases eq_or_ne x y with (rfl | h)
  Â· simp_rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleOTVS]
    apply isLittleOTVS_insert
    simp only [sub_self, map_zero]
  refine âŸ¨fun h => h.mono <| subset_insert y s, fun hf => hf.mono_of_mem_nhdsWithin ?_âŸ©
  simp_rw [nhdsWithin_insert_of_ne h, self_mem_nhdsWithin]

alias âŸ¨HasFDerivWithinAt.of_insert, HasFDerivWithinAt.insert'âŸ© := hasFDerivWithinAt_insert

protected theorem HasFDerivWithinAt.insert (h : HasFDerivWithinAt g g' s x) :
    HasFDerivWithinAt g g' (insert x s) x :=
  h.insert'

@[simp]
theorem hasFDerivWithinAt_diff_singleton (y : E) :
    HasFDerivWithinAt f f' (s \ {y}) x â†” HasFDerivWithinAt f f' s x := by
  rw [â† hasFDerivWithinAt_insert, insert_diff_singleton, hasFDerivWithinAt_insert]

@[simp]
protected theorem HasFDerivWithinAt.empty : HasFDerivWithinAt f f' âˆ… x := by
  simp [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleOTVS]

@[simp]
protected theorem DifferentiableWithinAt.empty : DifferentiableWithinAt ğ•œ f âˆ… x :=
  âŸ¨0, .emptyâŸ©

theorem HasFDerivWithinAt.of_finite (h : s.Finite) : HasFDerivWithinAt f f' s x := by
  induction s, h using Set.Finite.induction_on with
  | empty => exact .empty
  | insert _ _ ih => exact ih.insert'

theorem DifferentiableWithinAt.of_finite (h : s.Finite) : DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨0, .of_finite hâŸ©

@[simp]
protected theorem HasFDerivWithinAt.singleton {y} : HasFDerivWithinAt f f' {x} y :=
  .of_finite <| finite_singleton _

@[simp]
protected theorem DifferentiableWithinAt.singleton {y} : DifferentiableWithinAt ğ•œ f {x} y :=
  âŸ¨0, .singletonâŸ©

theorem HasFDerivWithinAt.of_subsingleton (h : s.Subsingleton) : HasFDerivWithinAt f f' s x :=
  .of_finite h.finite

theorem DifferentiableWithinAt.of_subsingleton (h : s.Subsingleton) :
    DifferentiableWithinAt ğ•œ f s x :=
  .of_finite h.finite

theorem HasStrictFDerivAt.isBigO_sub (hf : HasStrictFDerivAt f f' x) :
    (fun p : E Ã— E => f p.1 - f p.2) =O[ğ“ (x, x)] fun p : E Ã— E => p.1 - p.2 :=
  hf.isLittleO.isBigO.congr_of_sub.2 (f'.isBigO_comp _ _)

theorem HasFDerivAtFilter.isBigO_sub (h : HasFDerivAtFilter f f' x L) :
    (fun x' => f x' - f x) =O[L] fun x' => x' - x :=
  h.isLittleO.isBigO.congr_of_sub.2 (f'.isBigO_sub _ _)

@[fun_prop]
protected theorem HasStrictFDerivAt.hasFDerivAt (hf : HasStrictFDerivAt f f' x) :
    HasFDerivAt f f' x :=
  .of_isLittleOTVS <| by
    simpa only using hf.isLittleOTVS.comp_tendsto (tendsto_id.prodMk_nhds tendsto_const_nhds)

protected theorem HasStrictFDerivAt.differentiableAt (hf : HasStrictFDerivAt f f' x) :
    DifferentiableAt ğ•œ f x :=
  hf.hasFDerivAt.differentiableAt

/-- If `f` is strictly differentiable at `x` with derivative `f'` and `K > â€–f'â€–â‚Š`, then `f` is
`K`-Lipschitz in a neighborhood of `x`. -/
theorem HasStrictFDerivAt.exists_lipschitzOnWith_of_nnnorm_lt (hf : HasStrictFDerivAt f f' x)
    (K : â„â‰¥0) (hK : â€–f'â€–â‚Š < K) : âˆƒ s âˆˆ ğ“ x, LipschitzOnWith K f s := by
  have := hf.isLittleO.add_isBigOWith (f'.isBigOWith_comp _ _) hK
  simp only [sub_add_cancel, IsBigOWith] at this
  rcases exists_nhds_square this with âŸ¨U, Uo, xU, hUâŸ©
  exact
    âŸ¨U, Uo.mem_nhds xU, lipschitzOnWith_iff_norm_sub_le.2 fun x hx y hy => hU (mk_mem_prod hx hy)âŸ©

/-- If `f` is strictly differentiable at `x` with derivative `f'`, then `f` is Lipschitz in a
neighborhood of `x`. See also `HasStrictFDerivAt.exists_lipschitzOnWith_of_nnnorm_lt` for a
more precise statement. -/
theorem HasStrictFDerivAt.exists_lipschitzOnWith (hf : HasStrictFDerivAt f f' x) :
    âˆƒ K, âˆƒ s âˆˆ ğ“ x, LipschitzOnWith K f s :=
  (exists_gt _).imp hf.exists_lipschitzOnWith_of_nnnorm_lt

/-- Directional derivative agrees with `HasFDeriv`. -/
theorem HasFDerivAt.lim (hf : HasFDerivAt f f' x) (v : E) {Î± : Type*} {c : Î± â†’ ğ•œ} {l : Filter Î±}
    (hc : Tendsto (fun n => â€–c nâ€–) l atTop) :
    Tendsto (fun n => c n â€¢ (f (x + (c n)â»Â¹ â€¢ v) - f x)) l (ğ“ (f' v)) := by
  refine (hasFDerivWithinAt_univ.2 hf).lim _ univ_mem hc ?_
  intro U hU
  refine (eventually_ne_of_tendsto_norm_atTop hc (0 : ğ•œ)).mono fun y hy => ?_
  convert mem_of_mem_nhds hU
  dsimp only
  rw [â† mul_smul, mul_inv_cancelâ‚€ hy, one_smul]

theorem HasFDerivAt.unique (hâ‚€ : HasFDerivAt f fâ‚€' x) (hâ‚ : HasFDerivAt f fâ‚' x) : fâ‚€' = fâ‚' := by
  rw [â† hasFDerivWithinAt_univ] at hâ‚€ hâ‚
  exact uniqueDiffWithinAt_univ.eq hâ‚€ hâ‚

theorem hasFDerivWithinAt_inter' (h : t âˆˆ ğ“[s] x) :
    HasFDerivWithinAt f f' (s âˆ© t) x â†” HasFDerivWithinAt f f' s x := by
  simp [HasFDerivWithinAt, nhdsWithin_restrict'' s h]

theorem hasFDerivWithinAt_inter (h : t âˆˆ ğ“ x) :
    HasFDerivWithinAt f f' (s âˆ© t) x â†” HasFDerivWithinAt f f' s x := by
  simp [HasFDerivWithinAt, nhdsWithin_restrict' s h]

theorem HasFDerivWithinAt.union (hs : HasFDerivWithinAt f f' s x)
    (ht : HasFDerivWithinAt f f' t x) : HasFDerivWithinAt f f' (s âˆª t) x := by
  simp only [HasFDerivWithinAt, nhdsWithin_union]
  exact .of_isLittleOTVS <| hs.isLittleOTVS.sup ht.isLittleOTVS

theorem HasFDerivWithinAt.hasFDerivAt (h : HasFDerivWithinAt f f' s x) (hs : s âˆˆ ğ“ x) :
    HasFDerivAt f f' x := by
  rwa [â† univ_inter s, hasFDerivWithinAt_inter hs, hasFDerivWithinAt_univ] at h

theorem DifferentiableWithinAt.differentiableAt (h : DifferentiableWithinAt ğ•œ f s x)
    (hs : s âˆˆ ğ“ x) : DifferentiableAt ğ•œ f x :=
  h.imp fun _ hf' => hf'.hasFDerivAt hs

/-- If `x` is isolated in `s`, then `f` has any derivative at `x` within `s`,
as this statement is empty. -/
theorem HasFDerivWithinAt.of_not_accPt (h : Â¬AccPt x (ğ“Ÿ s)) : HasFDerivWithinAt f f' s x := by
  rw [accPt_principal_iff_nhdsWithin, not_neBot] at h
  rw [â† hasFDerivWithinAt_diff_singleton x, HasFDerivWithinAt, h,
    hasFDerivAtFilter_iff_isLittleOTVS]
  exact .bot

/-- If `x` is isolated in `s`, then `f` has any derivative at `x` within `s`,
as this statement is empty. -/
@[deprecated HasFDerivWithinAt.of_not_accPt (since := "2025-04-20")]
theorem HasFDerivWithinAt.of_nhdsWithin_eq_bot (h : ğ“[s \ {x}] x = âŠ¥) :
    HasFDerivWithinAt f f' s x :=
  .of_not_accPt <| by rwa [accPt_principal_iff_nhdsWithin, not_neBot]

/-- If `x` is not in the closure of `s`, then `f` has any derivative at `x` within `s`,
as this statement is empty. -/
theorem HasFDerivWithinAt.of_notMem_closure (h : x âˆ‰ closure s) : HasFDerivWithinAt f f' s x :=
  .of_not_accPt (h Â·.clusterPt.mem_closure)

@[deprecated (since := "2025-05-23")]
alias HasFDerivWithinAt.of_not_mem_closure := HasFDerivWithinAt.of_notMem_closure

@[deprecated (since := "2025-04-20")]
alias hasFDerivWithinAt_of_nmem_closure := HasFDerivWithinAt.of_not_mem_closure

theorem fderivWithin_zero_of_not_accPt (h : Â¬AccPt x (ğ“Ÿ s)) : fderivWithin ğ•œ f s x = 0 := by
  rw [fderivWithin, if_pos (.of_not_accPt h)]

set_option linter.deprecated false in
@[deprecated fderivWithin_zero_of_not_accPt (since := "2025-04-20")]
theorem fderivWithin_zero_of_isolated (h : ğ“[s \ {x}] x = âŠ¥) : fderivWithin ğ•œ f s x = 0 := by
  rw [fderivWithin, if_pos (.of_nhdsWithin_eq_bot h)]

theorem fderivWithin_zero_of_notMem_closure (h : x âˆ‰ closure s) : fderivWithin ğ•œ f s x = 0 :=
  fderivWithin_zero_of_not_accPt (h Â·.clusterPt.mem_closure)

@[deprecated (since := "2025-05-24")]
alias fderivWithin_zero_of_nmem_closure := fderivWithin_zero_of_notMem_closure

theorem DifferentiableWithinAt.hasFDerivWithinAt (h : DifferentiableWithinAt ğ•œ f s x) :
    HasFDerivWithinAt f (fderivWithin ğ•œ f s x) s x := by
  simp only [fderivWithin, dif_pos h]
  split_ifs with hâ‚€
  exacts [hâ‚€, Classical.choose_spec h]

theorem DifferentiableAt.hasFDerivAt (h : DifferentiableAt ğ•œ f x) :
    HasFDerivAt f (fderiv ğ•œ f x) x := by
  rw [fderiv, â† hasFDerivWithinAt_univ]
  rw [â† differentiableWithinAt_univ] at h
  exact h.hasFDerivWithinAt

theorem DifferentiableOn.hasFDerivAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    HasFDerivAt f (fderiv ğ•œ f x) x :=
  ((h x (mem_of_mem_nhds hs)).differentiableAt hs).hasFDerivAt

theorem DifferentiableOn.differentiableAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    DifferentiableAt ğ•œ f x :=
  (h.hasFDerivAt hs).differentiableAt

theorem DifferentiableOn.eventually_differentiableAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    âˆ€á¶  y in ğ“ x, DifferentiableAt ğ•œ f y :=
  (eventually_eventually_nhds.2 hs).mono fun _ => h.differentiableAt

protected theorem HasFDerivAt.fderiv (h : HasFDerivAt f f' x) : fderiv ğ•œ f x = f' := by
  ext
  rw [h.unique h.differentiableAt.hasFDerivAt]

theorem fderiv_eq {f' : E â†’ E â†’L[ğ•œ] F} (h : âˆ€ x, HasFDerivAt f (f' x) x) : fderiv ğ•œ f = f' :=
  funext fun x => (h x).fderiv

protected theorem HasFDerivWithinAt.fderivWithin (h : HasFDerivWithinAt f f' s x)
    (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ f s x = f' :=
  (hxs.eq h h.differentiableWithinAt.hasFDerivWithinAt).symm

theorem DifferentiableWithinAt.mono (h : DifferentiableWithinAt ğ•œ f t x) (st : s âŠ† t) :
    DifferentiableWithinAt ğ•œ f s x := by
  rcases h with âŸ¨f', hf'âŸ©
  exact âŸ¨f', hf'.mono stâŸ©

theorem DifferentiableWithinAt.mono_of_mem_nhdsWithin
    (h : DifferentiableWithinAt ğ•œ f s x) {t : Set E} (hst : s âˆˆ ğ“[t] x) :
    DifferentiableWithinAt ğ•œ f t x :=
  (h.hasFDerivWithinAt.mono_of_mem_nhdsWithin hst).differentiableWithinAt

theorem DifferentiableWithinAt.congr_nhds (h : DifferentiableWithinAt ğ•œ f s x) {t : Set E}
    (hst : ğ“[s] x = ğ“[t] x) : DifferentiableWithinAt ğ•œ f t x :=
  h.mono_of_mem_nhdsWithin <| hst â–¸ self_mem_nhdsWithin

theorem differentiableWithinAt_congr_nhds {t : Set E} (hst : ğ“[s] x = ğ“[t] x) :
    DifferentiableWithinAt ğ•œ f s x â†” DifferentiableWithinAt ğ•œ f t x :=
  âŸ¨fun h => h.congr_nhds hst, fun h => h.congr_nhds hst.symmâŸ©

theorem differentiableWithinAt_inter (ht : t âˆˆ ğ“ x) :
    DifferentiableWithinAt ğ•œ f (s âˆ© t) x â†” DifferentiableWithinAt ğ•œ f s x := by
  simp only [DifferentiableWithinAt, hasFDerivWithinAt_inter ht]

theorem differentiableWithinAt_inter' (ht : t âˆˆ ğ“[s] x) :
    DifferentiableWithinAt ğ•œ f (s âˆ© t) x â†” DifferentiableWithinAt ğ•œ f s x := by
  simp only [DifferentiableWithinAt, hasFDerivWithinAt_inter' ht]

theorem differentiableWithinAt_insert_self :
    DifferentiableWithinAt ğ•œ f (insert x s) x â†” DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨fun h â†¦ h.mono (subset_insert x s), fun h â†¦ h.hasFDerivWithinAt.insert.differentiableWithinAtâŸ©

theorem differentiableWithinAt_insert {y : E} :
    DifferentiableWithinAt ğ•œ f (insert y s) x â†” DifferentiableWithinAt ğ•œ f s x := by
  rcases eq_or_ne x y with (rfl | h)
  Â· exact differentiableWithinAt_insert_self
  apply differentiableWithinAt_congr_nhds
  exact nhdsWithin_insert_of_ne h

alias âŸ¨DifferentiableWithinAt.of_insert, DifferentiableWithinAt.insert'âŸ© :=
differentiableWithinAt_insert

protected theorem DifferentiableWithinAt.insert (h : DifferentiableWithinAt ğ•œ f s x) :
    DifferentiableWithinAt ğ•œ f (insert x s) x :=
  h.insert'

theorem DifferentiableAt.differentiableWithinAt (h : DifferentiableAt ğ•œ f x) :
    DifferentiableWithinAt ğ•œ f s x :=
  (differentiableWithinAt_univ.2 h).mono (subset_univ _)

@[fun_prop]
theorem Differentiable.differentiableAt (h : Differentiable ğ•œ f) : DifferentiableAt ğ•œ f x :=
  h x

protected theorem DifferentiableAt.fderivWithin (h : DifferentiableAt ğ•œ f x)
    (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ f s x = fderiv ğ•œ f x :=
  h.hasFDerivAt.hasFDerivWithinAt.fderivWithin hxs

theorem DifferentiableOn.mono (h : DifferentiableOn ğ•œ f t) (st : s âŠ† t) : DifferentiableOn ğ•œ f s :=
  fun x hx => (h x (st hx)).mono st

theorem differentiableOn_univ : DifferentiableOn ğ•œ f univ â†” Differentiable ğ•œ f := by
  simp only [DifferentiableOn, Differentiable, differentiableWithinAt_univ, mem_univ,
    forall_true_left]

@[fun_prop]
theorem Differentiable.differentiableOn (h : Differentiable ğ•œ f) : DifferentiableOn ğ•œ f s :=
  (differentiableOn_univ.2 h).mono (subset_univ _)

theorem differentiableOn_of_locally_differentiableOn
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ DifferentiableOn ğ•œ f (s âˆ© u)) :
    DifferentiableOn ğ•œ f s := by
  intro x xs
  rcases h x xs with âŸ¨t, t_open, xt, htâŸ©
  exact (differentiableWithinAt_inter (IsOpen.mem_nhds t_open xt)).1 (ht x âŸ¨xs, xtâŸ©)

theorem fderivWithin_of_mem_nhdsWithin (st : t âˆˆ ğ“[s] x) (ht : UniqueDiffWithinAt ğ•œ s x)
    (h : DifferentiableWithinAt ğ•œ f t x) : fderivWithin ğ•œ f s x = fderivWithin ğ•œ f t x :=
  ((DifferentiableWithinAt.hasFDerivWithinAt h).mono_of_mem_nhdsWithin st).fderivWithin ht

theorem fderivWithin_subset (st : s âŠ† t) (ht : UniqueDiffWithinAt ğ•œ s x)
    (h : DifferentiableWithinAt ğ•œ f t x) : fderivWithin ğ•œ f s x = fderivWithin ğ•œ f t x :=
  fderivWithin_of_mem_nhdsWithin (nhdsWithin_mono _ st self_mem_nhdsWithin) ht h

theorem fderivWithin_inter (ht : t âˆˆ ğ“ x) : fderivWithin ğ•œ f (s âˆ© t) x = fderivWithin ğ•œ f s x := by
  classical
  simp [fderivWithin, hasFDerivWithinAt_inter ht, DifferentiableWithinAt]

theorem fderivWithin_of_mem_nhds (h : s âˆˆ ğ“ x) : fderivWithin ğ•œ f s x = fderiv ğ•œ f x := by
  rw [â† fderivWithin_univ, â† univ_inter s, fderivWithin_inter h]

theorem fderivWithin_of_isOpen (hs : IsOpen s) (hx : x âˆˆ s) : fderivWithin ğ•œ f s x = fderiv ğ•œ f x :=
  fderivWithin_of_mem_nhds (hs.mem_nhds hx)

theorem fderivWithin_eq_fderiv (hs : UniqueDiffWithinAt ğ•œ s x) (h : DifferentiableAt ğ•œ f x) :
    fderivWithin ğ•œ f s x = fderiv ğ•œ f x := by
  rw [â† fderivWithin_univ]
  exact fderivWithin_subset (subset_univ _) hs h.differentiableWithinAt

theorem fderiv_mem_iff {f : E â†’ F} {s : Set (E â†’L[ğ•œ] F)} {x : E} : fderiv ğ•œ f x âˆˆ s â†”
    DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ s âˆ¨ Â¬DifferentiableAt ğ•œ f x âˆ§ (0 : E â†’L[ğ•œ] F) âˆˆ s := by
  by_cases hx : DifferentiableAt ğ•œ f x <;> simp [fderiv_zero_of_not_differentiableAt, *]

theorem fderivWithin_mem_iff {f : E â†’ F} {t : Set E} {s : Set (E â†’L[ğ•œ] F)} {x : E} :
    fderivWithin ğ•œ f t x âˆˆ s â†”
      DifferentiableWithinAt ğ•œ f t x âˆ§ fderivWithin ğ•œ f t x âˆˆ s âˆ¨
        Â¬DifferentiableWithinAt ğ•œ f t x âˆ§ (0 : E â†’L[ğ•œ] F) âˆˆ s := by
  by_cases hx : DifferentiableWithinAt ğ•œ f t x <;>
    simp [fderivWithin_zero_of_not_differentiableWithinAt, *]

theorem Asymptotics.IsBigO.hasFDerivWithinAt {s : Set E} {xâ‚€ : E} {n : â„•}
    (h : f =O[ğ“[s] xâ‚€] fun x => â€–x - xâ‚€â€– ^ n) (hxâ‚€ : xâ‚€ âˆˆ s) (hn : 1 < n) :
    HasFDerivWithinAt f (0 : E â†’L[ğ•œ] F) s xâ‚€ := by
  simp_rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO,
    h.eq_zero_of_norm_pow_within hxâ‚€ hn.ne_bot, zero_apply, sub_zero,
    h.trans_isLittleO ((isLittleO_pow_sub_sub xâ‚€ hn).mono nhdsWithin_le_nhds)]

theorem Asymptotics.IsBigO.hasFDerivAt {xâ‚€ : E} {n : â„•} (h : f =O[ğ“ xâ‚€] fun x => â€–x - xâ‚€â€– ^ n)
    (hn : 1 < n) : HasFDerivAt f (0 : E â†’L[ğ•œ] F) xâ‚€ := by
  rw [â† nhdsWithin_univ] at h
  exact (h.hasFDerivWithinAt (mem_univ _) hn).hasFDerivAt_of_univ

nonrec theorem HasFDerivWithinAt.isBigO_sub {f : E â†’ F} {s : Set E} {xâ‚€ : E} {f' : E â†’L[ğ•œ] F}
    (h : HasFDerivWithinAt f f' s xâ‚€) : (f Â· - f xâ‚€) =O[ğ“[s] xâ‚€] (Â· - xâ‚€) :=
  h.isBigO_sub

lemma DifferentiableWithinAt.isBigO_sub {f : E â†’ F} {s : Set E} {xâ‚€ : E}
    (h : DifferentiableWithinAt ğ•œ f s xâ‚€) : (f Â· - f xâ‚€) =O[ğ“[s] xâ‚€] (Â· - xâ‚€) :=
  h.hasFDerivWithinAt.isBigO_sub

nonrec theorem HasFDerivAt.isBigO_sub {f : E â†’ F} {xâ‚€ : E} {f' : E â†’L[ğ•œ] F}
    (h : HasFDerivAt f f' xâ‚€) : (f Â· - f xâ‚€) =O[ğ“ xâ‚€] (Â· - xâ‚€) :=
  h.isBigO_sub

nonrec theorem DifferentiableAt.isBigO_sub {f : E â†’ F} {xâ‚€ : E} (h : DifferentiableAt ğ•œ f xâ‚€) :
    (f Â· - f xâ‚€) =O[ğ“ xâ‚€] (Â· - xâ‚€) :=
  h.hasFDerivAt.isBigO_sub

end FDerivProperties

/-! ### Being differentiable on a union of open sets can be tested on each set -/
section differentiableOn_union

/-- If a function is differentiable on two open sets, it is also differentiable on their union. -/
lemma DifferentiableOn.union_of_isOpen
    (hf : DifferentiableOn ğ•œ f s) (hf' : DifferentiableOn ğ•œ f t)
    (hs : IsOpen s) (ht : IsOpen t) :
    DifferentiableOn ğ•œ f (s âˆª t) := by
  intro x hx
  obtain (hx | hx) := hx
  Â· exact (hf x hx).differentiableAt (hs.mem_nhds hx) |>.differentiableWithinAt
  Â· exact (hf' x hx).differentiableAt (ht.mem_nhds hx) |>.differentiableWithinAt

/-- A function is differentiable on two open sets iff it is differentiable on their union. -/
lemma differentiableOn_union_iff_of_isOpen (hs : IsOpen s) (ht : IsOpen t) :
    DifferentiableOn ğ•œ f (s âˆª t) â†” DifferentiableOn ğ•œ f s âˆ§ DifferentiableOn ğ•œ f t :=
  âŸ¨fun h â†¦ âŸ¨h.mono subset_union_left, h.mono subset_union_rightâŸ©,
    fun âŸ¨hfs, hftâŸ© â†¦ DifferentiableOn.union_of_isOpen hfs hft hs htâŸ©

lemma differentiable_of_differentiableOn_union_of_isOpen (hf : DifferentiableOn ğ•œ f s)
    (hf' : DifferentiableOn ğ•œ f t) (hst : s âˆª t = univ) (hs : IsOpen s) (ht : IsOpen t) :
    Differentiable ğ•œ f := by
  rw [â† differentiableOn_univ, â† hst]
  exact hf.union_of_isOpen hf' hs ht

/-- If a function is differentiable on open sets `s i`, it is differentiable on their union. -/
lemma DifferentiableOn.iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, DifferentiableOn ğ•œ f (s i)) (hs : âˆ€ i, IsOpen (s i)) :
    DifferentiableOn ğ•œ f (â‹ƒ i, s i) := by
  rintro x âŸ¨si, âŸ¨i, rflâŸ©, hxsiâŸ©
  exact (hf i).differentiableAt ((hs i).mem_nhds hxsi) |>.differentiableWithinAt

/-- A function is differentiable on a union of open sets `s i`
iff it is differentiable on each `s i`. -/
lemma differentiableOn_iUnion_iff_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hs : âˆ€ i, IsOpen (s i)) :
    DifferentiableOn ğ•œ f (â‹ƒ i, s i) â†” âˆ€ i : Î¹, DifferentiableOn ğ•œ f (s i) :=
  âŸ¨fun h i â†¦ h.mono <| subset_iUnion_of_subset i fun _ a â†¦ a,
   fun h â†¦ DifferentiableOn.iUnion_of_isOpen h hsâŸ©

lemma differentiable_of_differentiableOn_iUnion_of_isOpen {Î¹ : Type*} {s : Î¹ â†’ Set E}
    (hf : âˆ€ i : Î¹, DifferentiableOn ğ•œ f (s i))
    (hs : âˆ€ i, IsOpen (s i)) (hs' : â‹ƒ i, s i = univ) :
    Differentiable ğ•œ f := by
  rw [â† differentiableOn_univ, â† hs']
  exact DifferentiableOn.iUnion_of_isOpen hf hs

end differentiableOn_union

section Continuous

/-! ### Deducing continuity from differentiability -/


theorem HasFDerivAtFilter.tendsto_nhds (hL : L â‰¤ ğ“ x) (h : HasFDerivAtFilter f f' x L) :
    Tendsto f L (ğ“ (f x)) := by
  have : Tendsto (fun x' => f x' - f x) L (ğ“ 0) := by
    refine h.isBigO_sub.trans_tendsto (Tendsto.mono_left ?_ hL)
    rw [â† sub_self x]
    exact tendsto_id.sub tendsto_const_nhds
  have := this.add (tendsto_const_nhds (x := f x))
  rw [zero_add (f x)] at this
  exact this.congr (by simp only [sub_add_cancel, forall_const])

theorem HasFDerivWithinAt.continuousWithinAt (h : HasFDerivWithinAt f f' s x) :
    ContinuousWithinAt f s x :=
  HasFDerivAtFilter.tendsto_nhds inf_le_left h

theorem HasFDerivAt.continuousAt (h : HasFDerivAt f f' x) : ContinuousAt f x :=
  HasFDerivAtFilter.tendsto_nhds le_rfl h

@[fun_prop]
theorem DifferentiableWithinAt.continuousWithinAt (h : DifferentiableWithinAt ğ•œ f s x) :
    ContinuousWithinAt f s x :=
  let âŸ¨_, hf'âŸ© := h
  hf'.continuousWithinAt

@[fun_prop]
theorem DifferentiableAt.continuousAt (h : DifferentiableAt ğ•œ f x) : ContinuousAt f x :=
  let âŸ¨_, hf'âŸ© := h
  hf'.continuousAt

@[fun_prop]
theorem DifferentiableOn.continuousOn (h : DifferentiableOn ğ•œ f s) : ContinuousOn f s := fun x hx =>
  (h x hx).continuousWithinAt

@[fun_prop]
theorem Differentiable.continuous (h : Differentiable ğ•œ f) : Continuous f :=
  continuous_iff_continuousAt.2 fun x => (h x).continuousAt

protected theorem HasStrictFDerivAt.continuousAt (hf : HasStrictFDerivAt f f' x) :
    ContinuousAt f x :=
  hf.hasFDerivAt.continuousAt

theorem HasStrictFDerivAt.isBigO_sub_rev {f' : E â‰ƒL[ğ•œ] F}
    (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) x) :
    (fun p : E Ã— E => p.1 - p.2) =O[ğ“ (x, x)] fun p : E Ã— E => f p.1 - f p.2 :=
  ((f'.isBigO_comp_rev _ _).trans
      (hf.isLittleO.trans_isBigO (f'.isBigO_comp_rev _ _)).right_isBigO_add).congr
    (fun _ => rfl) fun _ => sub_add_cancel _ _

theorem HasFDerivAtFilter.isBigO_sub_rev (hf : HasFDerivAtFilter f f' x L) {C}
    (hf' : AntilipschitzWith C f') : (fun x' => x' - x) =O[L] fun x' => f x' - f x :=
  have : (fun x' => x' - x) =O[L] fun x' => f' (x' - x) :=
    isBigO_iff.2 âŸ¨C, Eventually.of_forall fun _ => ZeroHomClass.bound_of_antilipschitz f' hf' _âŸ©
  (this.trans (hf.isLittleO.trans_isBigO this).right_isBigO_add).congr (fun _ => rfl) fun _ =>
    sub_add_cancel _ _

end Continuous

section id

/-! ### Derivative of the identity -/

@[fun_prop]
theorem hasStrictFDerivAt_id (x : E) : HasStrictFDerivAt id (id ğ•œ E) x :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left <| by simp

theorem hasFDerivAtFilter_id (x : E) (L : Filter E) : HasFDerivAtFilter id (id ğ•œ E) x L :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left <| by simp

@[fun_prop]
theorem hasFDerivWithinAt_id (x : E) (s : Set E) : HasFDerivWithinAt id (id ğ•œ E) s x :=
  hasFDerivAtFilter_id _ _

@[fun_prop]
theorem hasFDerivAt_id (x : E) : HasFDerivAt id (id ğ•œ E) x :=
  hasFDerivAtFilter_id _ _

@[simp, fun_prop]
theorem differentiableAt_id : DifferentiableAt ğ•œ id x :=
  (hasFDerivAt_id x).differentiableAt

/-- Variant with `fun x => x` rather than `id` -/
@[simp, fun_prop]
theorem differentiableAt_fun_id : DifferentiableAt ğ•œ (fun x => x) x :=
  (hasFDerivAt_id x).differentiableAt

@[deprecated (since := "2025-06-25")] alias differentiableAt_id' := differentiableAt_fun_id

@[fun_prop]
theorem differentiableWithinAt_id : DifferentiableWithinAt ğ•œ id s x :=
  differentiableAt_id.differentiableWithinAt

/-- Variant with `fun x => x` rather than `id` -/
@[fun_prop]
theorem differentiableWithinAt_id' : DifferentiableWithinAt ğ•œ (fun x => x) s x :=
  differentiableWithinAt_id

@[simp, fun_prop]
theorem differentiable_id : Differentiable ğ•œ (id : E â†’ E) := fun _ => differentiableAt_id

/-- Variant with `fun x => x` rather than `id` -/
@[simp, fun_prop]
theorem differentiable_fun_id : Differentiable ğ•œ fun x : E => x := fun _ => differentiableAt_id

@[deprecated (since := "2025-06-25")] alias differentiable_id' := differentiable_fun_id

@[fun_prop]
theorem differentiableOn_id : DifferentiableOn ğ•œ id s :=
  differentiable_id.differentiableOn

@[simp]
theorem fderiv_id : fderiv ğ•œ id x = id ğ•œ E :=
  HasFDerivAt.fderiv (hasFDerivAt_id x)

@[simp]
theorem fderiv_id' : fderiv ğ•œ (fun x : E => x) x = ContinuousLinearMap.id ğ•œ E :=
  fderiv_id

theorem fderivWithin_id (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ id s x = id ğ•œ E := by
  rw [DifferentiableAt.fderivWithin differentiableAt_id hxs]
  exact fderiv_id

theorem fderivWithin_id' (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x : E => x) s x = ContinuousLinearMap.id ğ•œ E :=
  fderivWithin_id hxs

end id

section MeanValue

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. This version
only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a neighborhood of `x`. -/
theorem HasFDerivAt.le_of_lip' {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {xâ‚€ : E} (hf : HasFDerivAt f f' xâ‚€)
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) : â€–f'â€– â‰¤ C := by
  refine le_of_forall_pos_le_add fun Îµ Îµ0 => opNorm_le_of_nhds_zero ?_ ?_
  Â· exact add_nonneg hCâ‚€ Îµ0.le
  rw [â† map_add_left_nhds_zero xâ‚€, eventually_map] at hlip
  filter_upwards [isLittleO_iff.1 (hasFDerivAt_iff_isLittleO_nhds_zero.1 hf) Îµ0, hlip] with y hy hyC
  rw [add_sub_cancel_left] at hyC
  calc
    â€–f' yâ€– â‰¤ â€–f (xâ‚€ + y) - f xâ‚€â€– + â€–f (xâ‚€ + y) - f xâ‚€ - f' yâ€– := norm_le_insert _ _
    _ â‰¤ C * â€–yâ€– + Îµ * â€–yâ€– := add_le_add hyC hy
    _ = (C + Îµ) * â€–yâ€– := (add_mul _ _ _).symm

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. -/
theorem HasFDerivAt.le_of_lipschitzOn
    {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {xâ‚€ : E} (hf : HasFDerivAt f f' xâ‚€)
    {s : Set E} (hs : s âˆˆ ğ“ xâ‚€) {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) : â€–f'â€– â‰¤ C := by
  refine hf.le_of_lip' C.coe_nonneg ?_
  filter_upwards [hs] with x hx using hlip.norm_sub_le hx (mem_of_mem_nhds hs)

/-- Converse to the mean value inequality: if `f` is differentiable at `xâ‚€` and `C`-lipschitz
then its derivative at `xâ‚€` has norm bounded by `C`. -/
theorem HasFDerivAt.le_of_lipschitz {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {xâ‚€ : E} (hf : HasFDerivAt f f' xâ‚€)
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–f'â€– â‰¤ C :=
  hf.le_of_lipschitzOn univ_mem (lipschitzOnWith_univ.2 hlip)

variable (ğ•œ)

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`. This version
only assumes that `â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–` in a neighborhood of `x`. -/
theorem norm_fderiv_le_of_lip' {f : E â†’ F} {xâ‚€ : E}
    {C : â„} (hCâ‚€ : 0 â‰¤ C) (hlip : âˆ€á¶  x in ğ“ xâ‚€, â€–f x - f xâ‚€â€– â‰¤ C * â€–x - xâ‚€â€–) :
    â€–fderiv ğ•œ f xâ‚€â€– â‰¤ C := by
  by_cases hf : DifferentiableAt ğ•œ f xâ‚€
  Â· exact hf.hasFDerivAt.le_of_lip' hCâ‚€ hlip
  Â· rw [fderiv_zero_of_not_differentiableAt hf]
    simp [hCâ‚€]

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz
on a neighborhood of `xâ‚€` then its derivative at `xâ‚€` has norm bounded by `C`.
Version using `fderiv`. -/
theorem norm_fderiv_le_of_lipschitzOn {f : E â†’ F} {xâ‚€ : E} {s : Set E} (hs : s âˆˆ ğ“ xâ‚€)
    {C : â„â‰¥0} (hlip : LipschitzOnWith C f s) : â€–fderiv ğ•œ f xâ‚€â€– â‰¤ C := by
  refine norm_fderiv_le_of_lip' ğ•œ C.coe_nonneg ?_
  filter_upwards [hs] with x hx using hlip.norm_sub_le hx (mem_of_mem_nhds hs)

/-- Converse to the mean value inequality: if `f` is `C`-lipschitz then
its derivative at `xâ‚€` has norm bounded by `C`.
Version using `fderiv`. -/
theorem norm_fderiv_le_of_lipschitz {f : E â†’ F} {xâ‚€ : E}
    {C : â„â‰¥0} (hlip : LipschitzWith C f) : â€–fderiv ğ•œ f xâ‚€â€– â‰¤ C :=
  norm_fderiv_le_of_lipschitzOn ğ•œ univ_mem (lipschitzOnWith_univ.2 hlip)

end MeanValue

end

section Semilinear
/-!
## Results involving semilinear maps
-/
variable {ğ•œ V V' W W' : Type*} [NontriviallyNormedField ğ•œ] {Ïƒ Ïƒ' : RingHom ğ•œ ğ•œ}
  [NormedAddCommGroup V] [NormedSpace ğ•œ V] [NormedAddCommGroup V'] [NormedSpace ğ•œ V']
  [NormedAddCommGroup W] [NormedSpace ğ•œ W] [NormedAddCommGroup W'] [NormedSpace ğ•œ W']
  [RingHomIsometric Ïƒ] [RingHomInvPair Ïƒ Ïƒ'] (L : W â†’SL[Ïƒ] W') (R : V' â†’SL[Ïƒ'] V)

/-- If `L` and `R` are semilinear maps whose composite is linear, and `f` has FrÃ©chet derivative
`f'` at `R z`, then `L âˆ˜ f âˆ˜ R` has FrÃ©chet derivative `L âˆ˜ f' âˆ˜ R` at `z`. -/
lemma HasFDerivAt.comp_semilinear {f : V â†’ W} {z : V'} {f' : V â†’L[ğ•œ] W}
    (hf : HasFDerivAt f f' (R z)) : HasFDerivAt (L âˆ˜ f âˆ˜ R) (L.comp (f'.comp R)) z := by
  have : RingHomIsometric Ïƒ' := .inv Ïƒ
  rw [hasFDerivAt_iff_isLittleO_nhds_zero] at âŠ¢ hf
  have := hf.comp_tendsto (R.map_zero â–¸ R.continuous.continuousAt.tendsto)
  simpa using ((L.isBigO_comp _ _).trans_isLittleO this).trans_isBigO (R.isBigO_id _)

/-- If `L` and `R` are semilinear maps whose composite is linear, and `f` is differentiable at
`R z`, then `L âˆ˜ f âˆ˜ R` is differentiable at `z`. -/
lemma DifferentiableAt.comp_semilinearâ‚‚ {f : V â†’ W} {z : V'} (hf : DifferentiableAt ğ•œ f (R z)) :
    DifferentiableAt ğ•œ (L âˆ˜ f âˆ˜ R) z := by
  simpa using (hf.hasFDerivAt.comp_semilinear L R).differentiableAt

end Semilinear
