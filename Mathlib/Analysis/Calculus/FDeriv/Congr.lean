/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Basic

/-!
# The FrÃ©chet derivative: congruence properties

Lemmas about congruence properties of the FrÃ©chet derivative under change of function, set, etc.

## Tags

derivative, differentiable, FrÃ©chet, calculus

-/

open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

section congr

/-! ### congr properties of the derivative -/

theorem hasFDerivWithinAt_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    HasFDerivWithinAt f f' s x â†” HasFDerivWithinAt f f' t x :=
  calc
    HasFDerivWithinAt f f' s x â†” HasFDerivWithinAt f f' (s \ {y}) x :=
      (hasFDerivWithinAt_diff_singleton _).symm
    _ â†” HasFDerivWithinAt f f' (t \ {y}) x := by
      suffices ğ“[s \ {y}] x = ğ“[t \ {y}] x by simp only [HasFDerivWithinAt, this]
      simpa only [set_eventuallyEq_iff_inf_principal, â† nhdsWithin_inter', diff_eq,
        inter_comm] using h
    _ â†” HasFDerivWithinAt f f' t x := hasFDerivWithinAt_diff_singleton _

theorem hasFDerivWithinAt_congr_set (h : s =á¶ [ğ“ x] t) :
    HasFDerivWithinAt f f' s x â†” HasFDerivWithinAt f f' t x :=
  hasFDerivWithinAt_congr_set' x <| h.filter_mono inf_le_left

theorem differentiableWithinAt_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    DifferentiableWithinAt ğ•œ f s x â†” DifferentiableWithinAt ğ•œ f t x :=
  exists_congr fun _ => hasFDerivWithinAt_congr_set' _ h

theorem differentiableWithinAt_congr_set (h : s =á¶ [ğ“ x] t) :
    DifferentiableWithinAt ğ•œ f s x â†” DifferentiableWithinAt ğ•œ f t x :=
  exists_congr fun _ => hasFDerivWithinAt_congr_set h

theorem fderivWithin_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    fderivWithin ğ•œ f s x = fderivWithin ğ•œ f t x := by
  classical
  simp only [fderivWithin, differentiableWithinAt_congr_set' _ h, hasFDerivWithinAt_congr_set' _ h]

theorem fderivWithin_congr_set (h : s =á¶ [ğ“ x] t) : fderivWithin ğ•œ f s x = fderivWithin ğ•œ f t x :=
  fderivWithin_congr_set' x <| h.filter_mono inf_le_left

theorem fderivWithin_eventually_congr_set' (y : E) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    fderivWithin ğ•œ f s =á¶ [ğ“ x] fderivWithin ğ•œ f t :=
  (eventually_nhds_nhdsWithin.2 h).mono fun _ => fderivWithin_congr_set' y

theorem fderivWithin_eventually_congr_set (h : s =á¶ [ğ“ x] t) :
    fderivWithin ğ•œ f s =á¶ [ğ“ x] fderivWithin ğ•œ f t :=
  fderivWithin_eventually_congr_set' x <| h.filter_mono inf_le_left

theorem Filter.EventuallyEq.hasStrictFDerivAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) (h' : âˆ€ y, fâ‚€' y = fâ‚' y) :
    HasStrictFDerivAt fâ‚€ fâ‚€' x â†” HasStrictFDerivAt fâ‚ fâ‚' x := by
  rw [hasStrictFDerivAt_iff_isLittleOTVS, hasStrictFDerivAt_iff_isLittleOTVS]
  refine isLittleOTVS_congr ((h.prodMk_nhds h).mono ?_) .rfl
  rintro p âŸ¨hpâ‚, hpâ‚‚âŸ©
  simp only [*]

theorem HasStrictFDerivAt.congr_fderiv (h : HasStrictFDerivAt f f' x) (h' : f' = g') :
    HasStrictFDerivAt f g' x :=
  h' â–¸ h

theorem HasFDerivAt.congr_fderiv (h : HasFDerivAt f f' x) (h' : f' = g') : HasFDerivAt f g' x :=
  h' â–¸ h

theorem HasFDerivWithinAt.congr_fderiv (h : HasFDerivWithinAt f f' s x) (h' : f' = g') :
    HasFDerivWithinAt f g' s x :=
  h' â–¸ h

theorem HasStrictFDerivAt.congr_of_eventuallyEq (h : HasStrictFDerivAt f f' x)
    (hâ‚ : f =á¶ [ğ“ x] fâ‚) : HasStrictFDerivAt fâ‚ f' x :=
  (hâ‚.hasStrictFDerivAt_iff fun _ => rfl).1 h

theorem Filter.EventuallyEq.hasFDerivAtFilter_iff (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x)
    (hâ‚ : âˆ€ x, fâ‚€' x = fâ‚' x) : HasFDerivAtFilter fâ‚€ fâ‚€' x L â†” HasFDerivAtFilter fâ‚ fâ‚' x L := by
  simp only [hasFDerivAtFilter_iff_isLittleOTVS]
  exact isLittleOTVS_congr (hâ‚€.mono fun y hy => by simp only [hy, hâ‚, hx]) .rfl

theorem HasFDerivAtFilter.congr_of_eventuallyEq (h : HasFDerivAtFilter f f' x L) (hL : fâ‚ =á¶ [L] f)
    (hx : fâ‚ x = f x) : HasFDerivAtFilter fâ‚ f' x L :=
  (hL.hasFDerivAtFilter_iff hx fun _ => rfl).2 h

theorem Filter.EventuallyEq.hasFDerivAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) :
    HasFDerivAt fâ‚€ f' x â†” HasFDerivAt fâ‚ f' x :=
  h.hasFDerivAtFilter_iff h.eq_of_nhds fun _ => _root_.rfl

theorem Filter.EventuallyEq.differentiableAt_iff (h : fâ‚€ =á¶ [ğ“ x] fâ‚) :
    DifferentiableAt ğ•œ fâ‚€ x â†” DifferentiableAt ğ•œ fâ‚ x :=
  exists_congr fun _ => h.hasFDerivAt_iff

theorem Filter.EventuallyEq.hasFDerivWithinAt_iff (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : fâ‚€ x = fâ‚ x) :
    HasFDerivWithinAt fâ‚€ f' s x â†” HasFDerivWithinAt fâ‚ f' s x :=
  h.hasFDerivAtFilter_iff hx fun _ => _root_.rfl

theorem Filter.EventuallyEq.hasFDerivWithinAt_iff_of_mem (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : x âˆˆ s) :
    HasFDerivWithinAt fâ‚€ f' s x â†” HasFDerivWithinAt fâ‚ f' s x :=
  h.hasFDerivWithinAt_iff (h.eq_of_nhdsWithin hx)

theorem Filter.EventuallyEq.differentiableWithinAt_iff (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : fâ‚€ x = fâ‚ x) :
    DifferentiableWithinAt ğ•œ fâ‚€ s x â†” DifferentiableWithinAt ğ•œ fâ‚ s x :=
  exists_congr fun _ => h.hasFDerivWithinAt_iff hx

theorem Filter.EventuallyEq.differentiableWithinAt_iff_of_mem (h : fâ‚€ =á¶ [ğ“[s] x] fâ‚) (hx : x âˆˆ s) :
    DifferentiableWithinAt ğ•œ fâ‚€ s x â†” DifferentiableWithinAt ğ•œ fâ‚ s x :=
  h.differentiableWithinAt_iff (h.eq_of_nhdsWithin hx)

theorem HasFDerivWithinAt.congr_mono (h : HasFDerivWithinAt f f' s x) (ht : EqOn fâ‚ f t)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : HasFDerivWithinAt fâ‚ f' t x :=
  HasFDerivAtFilter.congr_of_eventuallyEq (h.mono hâ‚) (Filter.mem_inf_of_right ht) hx

theorem HasFDerivWithinAt.congr (h : HasFDerivWithinAt f f' s x) (hs : EqOn fâ‚ f s)
    (hx : fâ‚ x = f x) : HasFDerivWithinAt fâ‚ f' s x :=
  h.congr_mono hs hx (Subset.refl _)

theorem HasFDerivWithinAt.congr' (h : HasFDerivWithinAt f f' s x) (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) :
    HasFDerivWithinAt fâ‚ f' s x :=
  h.congr hs (hs hx)

theorem HasFDerivWithinAt.congr_of_eventuallyEq (h : HasFDerivWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : HasFDerivWithinAt fâ‚ f' s x :=
  HasFDerivAtFilter.congr_of_eventuallyEq h hâ‚ hx

theorem HasFDerivAt.congr_of_eventuallyEq (h : HasFDerivAt f f' x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    HasFDerivAt fâ‚ f' x :=
  HasFDerivAtFilter.congr_of_eventuallyEq h hâ‚ (mem_of_mem_nhds hâ‚ :)

theorem DifferentiableWithinAt.congr_mono (h : DifferentiableWithinAt ğ•œ f s x) (ht : EqOn fâ‚ f t)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : DifferentiableWithinAt ğ•œ fâ‚ t x :=
  (HasFDerivWithinAt.congr_mono h.hasFDerivWithinAt ht hx hâ‚).differentiableWithinAt

theorem DifferentiableWithinAt.congr (h : DifferentiableWithinAt ğ•œ f s x) (ht : âˆ€ x âˆˆ s, fâ‚ x = f x)
    (hx : fâ‚ x = f x) : DifferentiableWithinAt ğ•œ fâ‚ s x :=
  DifferentiableWithinAt.congr_mono h ht hx (Subset.refl _)

theorem DifferentiableWithinAt.congr_of_eventuallyEq (h : DifferentiableWithinAt ğ•œ f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : DifferentiableWithinAt ğ•œ fâ‚ s x :=
  (h.hasFDerivWithinAt.congr_of_eventuallyEq hâ‚ hx).differentiableWithinAt

theorem DifferentiableWithinAt.congr_of_eventuallyEq_of_mem (h : DifferentiableWithinAt ğ•œ f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : DifferentiableWithinAt ğ•œ fâ‚ s x :=
  h.congr_of_eventuallyEq hâ‚ (mem_of_mem_nhdsWithin hx hâ‚ :)

theorem DifferentiableWithinAt.congr_of_eventuallyEq_insert (h : DifferentiableWithinAt ğ•œ f s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[insert x s] x] f) : DifferentiableWithinAt ğ•œ fâ‚ s x :=
  (h.insert.congr_of_eventuallyEq_of_mem hâ‚ (mem_insert _ _)).of_insert

theorem DifferentiableOn.congr_mono (h : DifferentiableOn ğ•œ f s) (h' : âˆ€ x âˆˆ t, fâ‚ x = f x)
    (hâ‚ : t âŠ† s) : DifferentiableOn ğ•œ fâ‚ t := fun x hx => (h x (hâ‚ hx)).congr_mono h' (h' x hx) hâ‚

theorem DifferentiableOn.congr (h : DifferentiableOn ğ•œ f s) (h' : âˆ€ x âˆˆ s, fâ‚ x = f x) :
    DifferentiableOn ğ•œ fâ‚ s := fun x hx => (h x hx).congr h' (h' x hx)

theorem differentiableOn_congr (h' : âˆ€ x âˆˆ s, fâ‚ x = f x) :
    DifferentiableOn ğ•œ fâ‚ s â†” DifferentiableOn ğ•œ f s :=
  âŸ¨fun h => DifferentiableOn.congr h fun y hy => (h' y hy).symm, fun h =>
    DifferentiableOn.congr h h'âŸ©

theorem DifferentiableAt.congr_of_eventuallyEq (h : DifferentiableAt ğ•œ f x) (hL : fâ‚ =á¶ [ğ“ x] f) :
    DifferentiableAt ğ•œ fâ‚ x :=
  hL.differentiableAt_iff.2 h

theorem DifferentiableWithinAt.fderivWithin_congr_mono (h : DifferentiableWithinAt ğ•œ f s x)
    (hs : EqOn fâ‚ f t) (hx : fâ‚ x = f x) (hxt : UniqueDiffWithinAt ğ•œ t x) (hâ‚ : t âŠ† s) :
    fderivWithin ğ•œ fâ‚ t x = fderivWithin ğ•œ f s x :=
  (HasFDerivWithinAt.congr_mono h.hasFDerivWithinAt hs hx hâ‚).fderivWithin hxt

theorem Filter.EventuallyEq.fderivWithin_eq (hs : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x := by
  classical
  simp only [fderivWithin, DifferentiableWithinAt, hs.hasFDerivWithinAt_iff hx]

theorem Filter.EventuallyEq.fderivWithin_eq_of_mem (hs : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x :=
  hs.fderivWithin_eq (mem_of_mem_nhdsWithin hx hs :)

theorem Filter.EventuallyEq.fderivWithin_eq_of_insert (hs : fâ‚ =á¶ [ğ“[insert x s] x] f) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x := by
  apply Filter.EventuallyEq.fderivWithin_eq (nhdsWithin_mono _ (subset_insert x s) hs)
  exact (mem_of_mem_nhdsWithin (mem_insert x s) hs :)

theorem Filter.EventuallyEq.fderivWithin' (hs : fâ‚ =á¶ [ğ“[s] x] f) (ht : t âŠ† s) :
    fderivWithin ğ•œ fâ‚ t =á¶ [ğ“[s] x] fderivWithin ğ•œ f t :=
  (eventually_eventually_nhdsWithin.2 hs).mp <|
    eventually_mem_nhdsWithin.mono fun _y hys hs =>
      EventuallyEq.fderivWithin_eq (hs.filter_mono <| nhdsWithin_mono _ ht)
        (hs.self_of_nhdsWithin hys)

protected theorem Filter.EventuallyEq.fderivWithin (hs : fâ‚ =á¶ [ğ“[s] x] f) :
    fderivWithin ğ•œ fâ‚ s =á¶ [ğ“[s] x] fderivWithin ğ•œ f s :=
  hs.fderivWithin' Subset.rfl

theorem Filter.EventuallyEq.fderivWithin_eq_of_nhds (h : fâ‚ =á¶ [ğ“ x] f) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x :=
  (h.filter_mono nhdsWithin_le_nhds).fderivWithin_eq h.self_of_nhds

@[deprecated (since := "2025-05-20")]
alias Filter.EventuallyEq.fderivWithin_eq_nhds := Filter.EventuallyEq.fderivWithin_eq_of_nhds

theorem fderivWithin_congr (hs : EqOn fâ‚ f s) (hx : fâ‚ x = f x) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x :=
  (hs.eventuallyEq.filter_mono inf_le_right).fderivWithin_eq hx

theorem fderivWithin_congr' (hs : EqOn fâ‚ f s) (hx : x âˆˆ s) :
    fderivWithin ğ•œ fâ‚ s x = fderivWithin ğ•œ f s x :=
  fderivWithin_congr hs (hs hx)

theorem Filter.EventuallyEq.fderiv_eq (h : fâ‚ =á¶ [ğ“ x] f) : fderiv ğ•œ fâ‚ x = fderiv ğ•œ f x := by
  rw [â† fderivWithin_univ, â† fderivWithin_univ, h.fderivWithin_eq_of_nhds]

protected theorem Filter.EventuallyEq.fderiv (h : fâ‚ =á¶ [ğ“ x] f) : fderiv ğ•œ fâ‚ =á¶ [ğ“ x] fderiv ğ•œ f :=
  h.eventuallyEq_nhds.mono fun _ h => h.fderiv_eq

end congr

end
