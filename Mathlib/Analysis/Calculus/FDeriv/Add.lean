/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

#align_import analysis.calculus.fderiv.add from "leanprover-community/mathlib"@"e3fb84046afd187b710170887195d50bada934ee"

/-!
# Additive operations on derivatives

For detailed documentation of the Fr√©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of

* sum of finitely many functions
* multiplication of a function by a scalar constant
* negative of a function
* subtraction of two functions
-/


open Filter Asymptotics ContinuousLinearMap Set Metric

open Topology Classical NNReal Filter Asymptotics ENNReal

noncomputable section

section

variable {ùïú : Type*} [NontriviallyNormedField ùïú]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]

variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ùïú G']

variable {f f‚ÇÄ f‚ÇÅ g : E ‚Üí F}

variable {f' f‚ÇÄ' f‚ÇÅ' g' : E ‚ÜíL[ùïú] F}

variable (e : E ‚ÜíL[ùïú] F)

variable {x : E}

variable {s t : Set E}

variable {L L‚ÇÅ L‚ÇÇ : Filter E}

section ConstSmul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ùïú R F] [ContinuousConstSMul R F]

/-! ### Derivative of a function multiplied by a constant -/


theorem HasStrictFDerivAt.const_smul (h : HasStrictFDerivAt f f' x) (c : R) :
    HasStrictFDerivAt (fun x => c ‚Ä¢ f x) (c ‚Ä¢ f') x :=
  (c ‚Ä¢ (1 : F ‚ÜíL[ùïú] F)).hasStrictFDerivAt.comp x h
#align has_strict_fderiv_at.const_smul HasStrictFDerivAt.const_smul

theorem HasFDerivAtFilter.const_smul (h : HasFDerivAtFilter f f' x L) (c : R) :
    HasFDerivAtFilter (fun x => c ‚Ä¢ f x) (c ‚Ä¢ f') x L :=
  (c ‚Ä¢ (1 : F ‚ÜíL[ùïú] F)).hasFDerivAtFilter.comp x h tendsto_map
#align has_fderiv_at_filter.const_smul HasFDerivAtFilter.const_smul

nonrec theorem HasFDerivWithinAt.const_smul (h : HasFDerivWithinAt f f' s x) (c : R) :
    HasFDerivWithinAt (fun x => c ‚Ä¢ f x) (c ‚Ä¢ f') s x :=
  h.const_smul c
#align has_fderiv_within_at.const_smul HasFDerivWithinAt.const_smul

nonrec theorem HasFDerivAt.const_smul (h : HasFDerivAt f f' x) (c : R) :
    HasFDerivAt (fun x => c ‚Ä¢ f x) (c ‚Ä¢ f') x :=
  h.const_smul c
#align has_fderiv_at.const_smul HasFDerivAt.const_smul

theorem DifferentiableWithinAt.const_smul (h : DifferentiableWithinAt ùïú f s x) (c : R) :
    DifferentiableWithinAt ùïú (fun y => c ‚Ä¢ f y) s x :=
  (h.hasFDerivWithinAt.const_smul c).differentiableWithinAt
#align differentiable_within_at.const_smul DifferentiableWithinAt.const_smul

theorem DifferentiableAt.const_smul (h : DifferentiableAt ùïú f x) (c : R) :
    DifferentiableAt ùïú (fun y => c ‚Ä¢ f y) x :=
  (h.hasFDerivAt.const_smul c).differentiableAt
#align differentiable_at.const_smul DifferentiableAt.const_smul

theorem DifferentiableOn.const_smul (h : DifferentiableOn ùïú f s) (c : R) :
    DifferentiableOn ùïú (fun y => c ‚Ä¢ f y) s := fun x hx => (h x hx).const_smul c
#align differentiable_on.const_smul DifferentiableOn.const_smul

theorem Differentiable.const_smul (h : Differentiable ùïú f) (c : R) :
    Differentiable ùïú fun y => c ‚Ä¢ f y := fun x => (h x).const_smul c
#align differentiable.const_smul Differentiable.const_smul

theorem fderivWithin_const_smul (hxs : UniqueDiffWithinAt ùïú s x)
    (h : DifferentiableWithinAt ùïú f s x) (c : R) :
    fderivWithin ùïú (fun y => c ‚Ä¢ f y) s x = c ‚Ä¢ fderivWithin ùïú f s x :=
  (h.hasFDerivWithinAt.const_smul c).fderivWithin hxs
#align fderiv_within_const_smul fderivWithin_const_smul

theorem fderiv_const_smul (h : DifferentiableAt ùïú f x) (c : R) :
    fderiv ùïú (fun y => c ‚Ä¢ f y) x = c ‚Ä¢ fderiv ùïú f x :=
  (h.hasFDerivAt.const_smul c).fderiv
#align fderiv_const_smul fderiv_const_smul

end ConstSmul

section Add

/-! ### Derivative of the sum of two functions -/


nonrec theorem HasStrictFDerivAt.add (hf : HasStrictFDerivAt f f' x)
    (hg : HasStrictFDerivAt g g' x) : HasStrictFDerivAt (fun y => f y + g y) (f' + g') x :=
  (hf.add hg).congr_left fun y => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel
#align has_strict_fderiv_at.add HasStrictFDerivAt.add

nonrec theorem HasFDerivAtFilter.add (hf : HasFDerivAtFilter f f' x L)
    (hg : HasFDerivAtFilter g g' x L) : HasFDerivAtFilter (fun y => f y + g y) (f' + g') x L :=
  (hf.add hg).congr_left fun _ => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel
#align has_fderiv_at_filter.add HasFDerivAtFilter.add

nonrec theorem HasFDerivWithinAt.add (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) : HasFDerivWithinAt (fun y => f y + g y) (f' + g') s x :=
  hf.add hg
#align has_fderiv_within_at.add HasFDerivWithinAt.add

nonrec theorem HasFDerivAt.add (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x => f x + g x) (f' + g') x :=
  hf.add hg
#align has_fderiv_at.add HasFDerivAt.add

theorem DifferentiableWithinAt.add (hf : DifferentiableWithinAt ùïú f s x)
    (hg : DifferentiableWithinAt ùïú g s x) : DifferentiableWithinAt ùïú (fun y => f y + g y) s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.add DifferentiableWithinAt.add

@[simp]
theorem DifferentiableAt.add (hf : DifferentiableAt ùïú f x) (hg : DifferentiableAt ùïú g x) :
    DifferentiableAt ùïú (fun y => f y + g y) x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).differentiableAt
#align differentiable_at.add DifferentiableAt.add

theorem DifferentiableOn.add (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s) :
    DifferentiableOn ùïú (fun y => f y + g y) s := fun x hx => (hf x hx).add (hg x hx)
#align differentiable_on.add DifferentiableOn.add

@[simp]
theorem Differentiable.add (hf : Differentiable ùïú f) (hg : Differentiable ùïú g) :
    Differentiable ùïú fun y => f y + g y := fun x => (hf x).add (hg x)
#align differentiable.add Differentiable.add

theorem fderivWithin_add (hxs : UniqueDiffWithinAt ùïú s x) (hf : DifferentiableWithinAt ùïú f s x)
    (hg : DifferentiableWithinAt ùïú g s x) :
    fderivWithin ùïú (fun y => f y + g y) s x = fderivWithin ùïú f s x + fderivWithin ùïú g s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_add fderivWithin_add

theorem fderiv_add (hf : DifferentiableAt ùïú f x) (hg : DifferentiableAt ùïú g x) :
    fderiv ùïú (fun y => f y + g y) x = fderiv ùïú f x + fderiv ùïú g x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).fderiv
#align fderiv_add fderiv_add

theorem HasStrictFDerivAt.add_const (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun y => f y + c) f' x :=
  add_zero f' ‚ñ∏ hf.add (hasStrictFDerivAt_const _ _)
#align has_strict_fderiv_at.add_const HasStrictFDerivAt.add_const

theorem HasFDerivAtFilter.add_const (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun y => f y + c) f' x L :=
  add_zero f' ‚ñ∏ hf.add (hasFDerivAtFilter_const _ _ _)
#align has_fderiv_at_filter.add_const HasFDerivAtFilter.add_const

nonrec theorem HasFDerivWithinAt.add_const (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun y => f y + c) f' s x :=
  hf.add_const c
#align has_fderiv_within_at.add_const HasFDerivWithinAt.add_const

nonrec theorem HasFDerivAt.add_const (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => f x + c) f' x :=
  hf.add_const c
#align has_fderiv_at.add_const HasFDerivAt.add_const

theorem DifferentiableWithinAt.add_const (hf : DifferentiableWithinAt ùïú f s x) (c : F) :
    DifferentiableWithinAt ùïú (fun y => f y + c) s x :=
  (hf.hasFDerivWithinAt.add_const c).differentiableWithinAt
#align differentiable_within_at.add_const DifferentiableWithinAt.add_const

@[simp]
theorem differentiableWithinAt_add_const_iff (c : F) :
    DifferentiableWithinAt ùïú (fun y => f y + c) s x ‚Üî DifferentiableWithinAt ùïú f s x :=
  ‚ü®fun h => by simpa using h.add_const (-c), fun h => h.add_const c‚ü©
#align differentiable_within_at_add_const_iff differentiableWithinAt_add_const_iff

theorem DifferentiableAt.add_const (hf : DifferentiableAt ùïú f x) (c : F) :
    DifferentiableAt ùïú (fun y => f y + c) x :=
  (hf.hasFDerivAt.add_const c).differentiableAt
#align differentiable_at.add_const DifferentiableAt.add_const

@[simp]
theorem differentiableAt_add_const_iff (c : F) :
    DifferentiableAt ùïú (fun y => f y + c) x ‚Üî DifferentiableAt ùïú f x :=
  ‚ü®fun h => by simpa using h.add_const (-c), fun h => h.add_const c‚ü©
#align differentiable_at_add_const_iff differentiableAt_add_const_iff

theorem DifferentiableOn.add_const (hf : DifferentiableOn ùïú f s) (c : F) :
    DifferentiableOn ùïú (fun y => f y + c) s := fun x hx => (hf x hx).add_const c
#align differentiable_on.add_const DifferentiableOn.add_const

@[simp]
theorem differentiableOn_add_const_iff (c : F) :
    DifferentiableOn ùïú (fun y => f y + c) s ‚Üî DifferentiableOn ùïú f s :=
  ‚ü®fun h => by simpa using h.add_const (-c), fun h => h.add_const c‚ü©
#align differentiable_on_add_const_iff differentiableOn_add_const_iff

theorem Differentiable.add_const (hf : Differentiable ùïú f) (c : F) :
    Differentiable ùïú fun y => f y + c := fun x => (hf x).add_const c
#align differentiable.add_const Differentiable.add_const

@[simp]
theorem differentiable_add_const_iff (c : F) :
    (Differentiable ùïú fun y => f y + c) ‚Üî Differentiable ùïú f :=
  ‚ü®fun h => by simpa using h.add_const (-c), fun h => h.add_const c‚ü©
#align differentiable_add_const_iff differentiable_add_const_iff

theorem fderivWithin_add_const (hxs : UniqueDiffWithinAt ùïú s x) (c : F) :
    fderivWithin ùïú (fun y => f y + c) s x = fderivWithin ùïú f s x :=
  if hf : DifferentiableWithinAt ùïú f s x then (hf.hasFDerivWithinAt.add_const c).fderivWithin hxs
  else by
    rw [fderivWithin_zero_of_not_differentiableWithinAt hf,
      fderivWithin_zero_of_not_differentiableWithinAt]
    simpa
#align fderiv_within_add_const fderivWithin_add_const

theorem fderiv_add_const (c : F) : fderiv ùïú (fun y => f y + c) x = fderiv ùïú f x := by
  simp only [‚Üê fderivWithin_univ, fderivWithin_add_const uniqueDiffWithinAt_univ]
#align fderiv_add_const fderiv_add_const

theorem HasStrictFDerivAt.const_add (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun y => c + f y) f' x :=
  zero_add f' ‚ñ∏ (hasStrictFDerivAt_const _ _).add hf
#align has_strict_fderiv_at.const_add HasStrictFDerivAt.const_add

theorem HasFDerivAtFilter.const_add (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun y => c + f y) f' x L :=
  zero_add f' ‚ñ∏ (hasFDerivAtFilter_const _ _ _).add hf
#align has_fderiv_at_filter.const_add HasFDerivAtFilter.const_add

nonrec theorem HasFDerivWithinAt.const_add (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun y => c + f y) f' s x :=
  hf.const_add c
#align has_fderiv_within_at.const_add HasFDerivWithinAt.const_add

nonrec theorem HasFDerivAt.const_add (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => c + f x) f' x :=
  hf.const_add c
#align has_fderiv_at.const_add HasFDerivAt.const_add

theorem DifferentiableWithinAt.const_add (hf : DifferentiableWithinAt ùïú f s x) (c : F) :
    DifferentiableWithinAt ùïú (fun y => c + f y) s x :=
  (hf.hasFDerivWithinAt.const_add c).differentiableWithinAt
#align differentiable_within_at.const_add DifferentiableWithinAt.const_add

@[simp]
theorem differentiableWithinAt_const_add_iff (c : F) :
    DifferentiableWithinAt ùïú (fun y => c + f y) s x ‚Üî DifferentiableWithinAt ùïú f s x :=
  ‚ü®fun h => by simpa using h.const_add (-c), fun h => h.const_add c‚ü©
#align differentiable_within_at_const_add_iff differentiableWithinAt_const_add_iff

theorem DifferentiableAt.const_add (hf : DifferentiableAt ùïú f x) (c : F) :
    DifferentiableAt ùïú (fun y => c + f y) x :=
  (hf.hasFDerivAt.const_add c).differentiableAt
#align differentiable_at.const_add DifferentiableAt.const_add

@[simp]
theorem differentiableAt_const_add_iff (c : F) :
    DifferentiableAt ùïú (fun y => c + f y) x ‚Üî DifferentiableAt ùïú f x :=
  ‚ü®fun h => by simpa using h.const_add (-c), fun h => h.const_add c‚ü©
#align differentiable_at_const_add_iff differentiableAt_const_add_iff

theorem DifferentiableOn.const_add (hf : DifferentiableOn ùïú f s) (c : F) :
    DifferentiableOn ùïú (fun y => c + f y) s := fun x hx => (hf x hx).const_add c
#align differentiable_on.const_add DifferentiableOn.const_add

@[simp]
theorem differentiableOn_const_add_iff (c : F) :
    DifferentiableOn ùïú (fun y => c + f y) s ‚Üî DifferentiableOn ùïú f s :=
  ‚ü®fun h => by simpa using h.const_add (-c), fun h => h.const_add c‚ü©
#align differentiable_on_const_add_iff differentiableOn_const_add_iff

theorem Differentiable.const_add (hf : Differentiable ùïú f) (c : F) :
    Differentiable ùïú fun y => c + f y := fun x => (hf x).const_add c
#align differentiable.const_add Differentiable.const_add

@[simp]
theorem differentiable_const_add_iff (c : F) :
    (Differentiable ùïú fun y => c + f y) ‚Üî Differentiable ùïú f :=
  ‚ü®fun h => by simpa using h.const_add (-c), fun h => h.const_add c‚ü©
#align differentiable_const_add_iff differentiable_const_add_iff

theorem fderivWithin_const_add (hxs : UniqueDiffWithinAt ùïú s x) (c : F) :
    fderivWithin ùïú (fun y => c + f y) s x = fderivWithin ùïú f s x := by
  simpa only [add_comm] using fderivWithin_add_const hxs c
#align fderiv_within_const_add fderivWithin_const_add

theorem fderiv_const_add (c : F) : fderiv ùïú (fun y => c + f y) x = fderiv ùïú f x := by
  simp only [add_comm c, fderiv_add_const]
#align fderiv_const_add fderiv_const_add

end Add

section Sum

/-! ### Derivative of a finite sum of functions -/


open BigOperators

variable {Œπ : Type*} {u : Finset Œπ} {A : Œπ ‚Üí E ‚Üí F} {A' : Œπ ‚Üí E ‚ÜíL[ùïú] F}

theorem HasStrictFDerivAt.sum (h : ‚àÄ i ‚àà u, HasStrictFDerivAt (A i) (A' i) x) :
    HasStrictFDerivAt (fun y => ‚àë i in u, A i y) (‚àë i in u, A' i) x := by
  dsimp [HasStrictFDerivAt] at *
  convert IsLittleO.sum h
  simp [Finset.sum_sub_distrib, ContinuousLinearMap.sum_apply]
#align has_strict_fderiv_at.sum HasStrictFDerivAt.sum

theorem HasFDerivAtFilter.sum (h : ‚àÄ i ‚àà u, HasFDerivAtFilter (A i) (A' i) x L) :
    HasFDerivAtFilter (fun y => ‚àë i in u, A i y) (‚àë i in u, A' i) x L := by
  dsimp [HasFDerivAtFilter] at *
  convert IsLittleO.sum h
  simp [ContinuousLinearMap.sum_apply]
#align has_fderiv_at_filter.sum HasFDerivAtFilter.sum

theorem HasFDerivWithinAt.sum (h : ‚àÄ i ‚àà u, HasFDerivWithinAt (A i) (A' i) s x) :
    HasFDerivWithinAt (fun y => ‚àë i in u, A i y) (‚àë i in u, A' i) s x :=
  HasFDerivAtFilter.sum h
#align has_fderiv_within_at.sum HasFDerivWithinAt.sum

theorem HasFDerivAt.sum (h : ‚àÄ i ‚àà u, HasFDerivAt (A i) (A' i) x) :
    HasFDerivAt (fun y => ‚àë i in u, A i y) (‚àë i in u, A' i) x :=
  HasFDerivAtFilter.sum h
#align has_fderiv_at.sum HasFDerivAt.sum

theorem DifferentiableWithinAt.sum (h : ‚àÄ i ‚àà u, DifferentiableWithinAt ùïú (A i) s x) :
    DifferentiableWithinAt ùïú (fun y => ‚àë i in u, A i y) s x :=
  HasFDerivWithinAt.differentiableWithinAt <|
    HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt
#align differentiable_within_at.sum DifferentiableWithinAt.sum

@[simp]
theorem DifferentiableAt.sum (h : ‚àÄ i ‚àà u, DifferentiableAt ùïú (A i) x) :
    DifferentiableAt ùïú (fun y => ‚àë i in u, A i y) x :=
  HasFDerivAt.differentiableAt <| HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt
#align differentiable_at.sum DifferentiableAt.sum

theorem DifferentiableOn.sum (h : ‚àÄ i ‚àà u, DifferentiableOn ùïú (A i) s) :
    DifferentiableOn ùïú (fun y => ‚àë i in u, A i y) s := fun x hx =>
  DifferentiableWithinAt.sum fun i hi => h i hi x hx
#align differentiable_on.sum DifferentiableOn.sum

@[simp]
theorem Differentiable.sum (h : ‚àÄ i ‚àà u, Differentiable ùïú (A i)) :
    Differentiable ùïú fun y => ‚àë i in u, A i y := fun x => DifferentiableAt.sum fun i hi => h i hi x
#align differentiable.sum Differentiable.sum

theorem fderivWithin_sum (hxs : UniqueDiffWithinAt ùïú s x)
    (h : ‚àÄ i ‚àà u, DifferentiableWithinAt ùïú (A i) s x) :
    fderivWithin ùïú (fun y => ‚àë i in u, A i y) s x = ‚àë i in u, fderivWithin ùïú (A i) s x :=
  (HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_sum fderivWithin_sum

theorem fderiv_sum (h : ‚àÄ i ‚àà u, DifferentiableAt ùïú (A i) x) :
    fderiv ùïú (fun y => ‚àë i in u, A i y) x = ‚àë i in u, fderiv ùïú (A i) x :=
  (HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt).fderiv
#align fderiv_sum fderiv_sum

end Sum

section Neg

/-! ### Derivative of the negative of a function -/


theorem HasStrictFDerivAt.neg (h : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => -f x) (-f') x :=
  (-1 : F ‚ÜíL[ùïú] F).hasStrictFDerivAt.comp x h
#align has_strict_fderiv_at.neg HasStrictFDerivAt.neg

theorem HasFDerivAtFilter.neg (h : HasFDerivAtFilter f f' x L) :
    HasFDerivAtFilter (fun x => -f x) (-f') x L :=
  (-1 : F ‚ÜíL[ùïú] F).hasFDerivAtFilter.comp x h tendsto_map
#align has_fderiv_at_filter.neg HasFDerivAtFilter.neg

nonrec theorem HasFDerivWithinAt.neg (h : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => -f x) (-f') s x :=
  h.neg
#align has_fderiv_within_at.neg HasFDerivWithinAt.neg

nonrec theorem HasFDerivAt.neg (h : HasFDerivAt f f' x) : HasFDerivAt (fun x => -f x) (-f') x :=
  h.neg
#align has_fderiv_at.neg HasFDerivAt.neg

theorem DifferentiableWithinAt.neg (h : DifferentiableWithinAt ùïú f s x) :
    DifferentiableWithinAt ùïú (fun y => -f y) s x :=
  h.hasFDerivWithinAt.neg.differentiableWithinAt
#align differentiable_within_at.neg DifferentiableWithinAt.neg

@[simp]
theorem differentiableWithinAt_neg_iff :
    DifferentiableWithinAt ùïú (fun y => -f y) s x ‚Üî DifferentiableWithinAt ùïú f s x :=
  ‚ü®fun h => by simpa only [neg_neg] using h.neg, fun h => h.neg‚ü©
#align differentiable_within_at_neg_iff differentiableWithinAt_neg_iff

theorem DifferentiableAt.neg (h : DifferentiableAt ùïú f x) : DifferentiableAt ùïú (fun y => -f y) x :=
  h.hasFDerivAt.neg.differentiableAt
#align differentiable_at.neg DifferentiableAt.neg

@[simp]
theorem differentiableAt_neg_iff : DifferentiableAt ùïú (fun y => -f y) x ‚Üî DifferentiableAt ùïú f x :=
  ‚ü®fun h => by simpa only [neg_neg] using h.neg, fun h => h.neg‚ü©
#align differentiable_at_neg_iff differentiableAt_neg_iff

theorem DifferentiableOn.neg (h : DifferentiableOn ùïú f s) : DifferentiableOn ùïú (fun y => -f y) s :=
  fun x hx => (h x hx).neg
#align differentiable_on.neg DifferentiableOn.neg

@[simp]
theorem differentiableOn_neg_iff : DifferentiableOn ùïú (fun y => -f y) s ‚Üî DifferentiableOn ùïú f s :=
  ‚ü®fun h => by simpa only [neg_neg] using h.neg, fun h => h.neg‚ü©
#align differentiable_on_neg_iff differentiableOn_neg_iff

theorem Differentiable.neg (h : Differentiable ùïú f) : Differentiable ùïú fun y => -f y := fun x =>
  (h x).neg
#align differentiable.neg Differentiable.neg

@[simp]
theorem differentiable_neg_iff : (Differentiable ùïú fun y => -f y) ‚Üî Differentiable ùïú f :=
  ‚ü®fun h => by simpa only [neg_neg] using h.neg, fun h => h.neg‚ü©
#align differentiable_neg_iff differentiable_neg_iff

theorem fderivWithin_neg (hxs : UniqueDiffWithinAt ùïú s x) :
    fderivWithin ùïú (fun y => -f y) s x = -fderivWithin ùïú f s x :=
  if h : DifferentiableWithinAt ùïú f s x then h.hasFDerivWithinAt.neg.fderivWithin hxs
  else by
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt, neg_zero]
    simpa
#align fderiv_within_neg fderivWithin_neg

@[simp]
theorem fderiv_neg : fderiv ùïú (fun y => -f y) x = -fderiv ùïú f x := by
  simp only [‚Üê fderivWithin_univ, fderivWithin_neg uniqueDiffWithinAt_univ]
#align fderiv_neg fderiv_neg

end Neg

section Sub

/-! ### Derivative of the difference of two functions -/


theorem HasStrictFDerivAt.sub (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun x => f x - g x) (f' - g') x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_strict_fderiv_at.sub HasStrictFDerivAt.sub

theorem HasFDerivAtFilter.sub (hf : HasFDerivAtFilter f f' x L) (hg : HasFDerivAtFilter g g' x L) :
    HasFDerivAtFilter (fun x => f x - g x) (f' - g') x L := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_fderiv_at_filter.sub HasFDerivAtFilter.sub

nonrec theorem HasFDerivWithinAt.sub (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) : HasFDerivWithinAt (fun x => f x - g x) (f' - g') s x :=
  hf.sub hg
#align has_fderiv_within_at.sub HasFDerivWithinAt.sub

nonrec theorem HasFDerivAt.sub (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x => f x - g x) (f' - g') x :=
  hf.sub hg
#align has_fderiv_at.sub HasFDerivAt.sub

theorem DifferentiableWithinAt.sub (hf : DifferentiableWithinAt ùïú f s x)
    (hg : DifferentiableWithinAt ùïú g s x) : DifferentiableWithinAt ùïú (fun y => f y - g y) s x :=
  (hf.hasFDerivWithinAt.sub hg.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.sub DifferentiableWithinAt.sub

@[simp]
theorem DifferentiableAt.sub (hf : DifferentiableAt ùïú f x) (hg : DifferentiableAt ùïú g x) :
    DifferentiableAt ùïú (fun y => f y - g y) x :=
  (hf.hasFDerivAt.sub hg.hasFDerivAt).differentiableAt
#align differentiable_at.sub DifferentiableAt.sub

theorem DifferentiableOn.sub (hf : DifferentiableOn ùïú f s) (hg : DifferentiableOn ùïú g s) :
    DifferentiableOn ùïú (fun y => f y - g y) s := fun x hx => (hf x hx).sub (hg x hx)
#align differentiable_on.sub DifferentiableOn.sub

@[simp]
theorem Differentiable.sub (hf : Differentiable ùïú f) (hg : Differentiable ùïú g) :
    Differentiable ùïú fun y => f y - g y := fun x => (hf x).sub (hg x)
#align differentiable.sub Differentiable.sub

theorem fderivWithin_sub (hxs : UniqueDiffWithinAt ùïú s x) (hf : DifferentiableWithinAt ùïú f s x)
    (hg : DifferentiableWithinAt ùïú g s x) :
    fderivWithin ùïú (fun y => f y - g y) s x = fderivWithin ùïú f s x - fderivWithin ùïú g s x :=
  (hf.hasFDerivWithinAt.sub hg.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_sub fderivWithin_sub

theorem fderiv_sub (hf : DifferentiableAt ùïú f x) (hg : DifferentiableAt ùïú g x) :
    fderiv ùïú (fun y => f y - g y) x = fderiv ùïú f x - fderiv ùïú g x :=
  (hf.hasFDerivAt.sub hg.hasFDerivAt).fderiv
#align fderiv_sub fderiv_sub

theorem HasStrictFDerivAt.sub_const (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun x => f x - c) f' x := by
  simpa only [sub_eq_add_neg] using hf.add_const (-c)
#align has_strict_fderiv_at.sub_const HasStrictFDerivAt.sub_const

theorem HasFDerivAtFilter.sub_const (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun x => f x - c) f' x L := by
  simpa only [sub_eq_add_neg] using hf.add_const (-c)
#align has_fderiv_at_filter.sub_const HasFDerivAtFilter.sub_const

nonrec theorem HasFDerivWithinAt.sub_const (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun x => f x - c) f' s x :=
  hf.sub_const c
#align has_fderiv_within_at.sub_const HasFDerivWithinAt.sub_const

nonrec theorem HasFDerivAt.sub_const (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => f x - c) f' x :=
  hf.sub_const c
#align has_fderiv_at.sub_const HasFDerivAt.sub_const

theorem hasStrictFDerivAt_sub_const {x : F} (c : F) : HasStrictFDerivAt (¬∑ - c) (id ùïú F) x :=
  (hasStrictFDerivAt_id x).sub_const c

theorem hasFDerivAt_sub_const {x : F} (c : F) : HasFDerivAt (¬∑ - c) (id ùïú F) x :=
  (hasFDerivAt_id x).sub_const c

theorem DifferentiableWithinAt.sub_const (hf : DifferentiableWithinAt ùïú f s x) (c : F) :
    DifferentiableWithinAt ùïú (fun y => f y - c) s x :=
  (hf.hasFDerivWithinAt.sub_const c).differentiableWithinAt
#align differentiable_within_at.sub_const DifferentiableWithinAt.sub_const

@[simp]
theorem differentiableWithinAt_sub_const_iff (c : F) :
    DifferentiableWithinAt ùïú (fun y => f y - c) s x ‚Üî DifferentiableWithinAt ùïú f s x := by
  simp only [sub_eq_add_neg, differentiableWithinAt_add_const_iff]
#align differentiable_within_at_sub_const_iff differentiableWithinAt_sub_const_iff

theorem DifferentiableAt.sub_const (hf : DifferentiableAt ùïú f x) (c : F) :
    DifferentiableAt ùïú (fun y => f y - c) x :=
  (hf.hasFDerivAt.sub_const c).differentiableAt
#align differentiable_at.sub_const DifferentiableAt.sub_const

@[simp]
theorem differentiableAt_sub_const_iff (c : F) :
    DifferentiableAt ùïú (fun y => f y - c) x ‚Üî DifferentiableAt ùïú f x := by
  simp only [sub_eq_add_neg, differentiableAt_add_const_iff]
#align differentiable_at_sub_const_iff differentiableAt_sub_const_iff

theorem DifferentiableOn.sub_const (hf : DifferentiableOn ùïú f s) (c : F) :
    DifferentiableOn ùïú (fun y => f y - c) s := fun x hx => (hf x hx).sub_const c
#align differentiable_on.sub_const DifferentiableOn.sub_const

@[simp]
theorem differentiableOn_sub_const_iff (c : F) :
    DifferentiableOn ùïú (fun y => f y - c) s ‚Üî DifferentiableOn ùïú f s := by
  simp only [sub_eq_add_neg, differentiableOn_add_const_iff]
#align differentiable_on_sub_const_iff differentiableOn_sub_const_iff

theorem Differentiable.sub_const (hf : Differentiable ùïú f) (c : F) :
    Differentiable ùïú fun y => f y - c := fun x => (hf x).sub_const c
#align differentiable.sub_const Differentiable.sub_const

@[simp]
theorem differentiable_sub_const_iff (c : F) :
    (Differentiable ùïú fun y => f y - c) ‚Üî Differentiable ùïú f := by
  simp only [sub_eq_add_neg, differentiable_add_const_iff]
#align differentiable_sub_const_iff differentiable_sub_const_iff

theorem fderivWithin_sub_const (hxs : UniqueDiffWithinAt ùïú s x) (c : F) :
    fderivWithin ùïú (fun y => f y - c) s x = fderivWithin ùïú f s x := by
  simp only [sub_eq_add_neg, fderivWithin_add_const hxs]
#align fderiv_within_sub_const fderivWithin_sub_const

theorem fderiv_sub_const (c : F) : fderiv ùïú (fun y => f y - c) x = fderiv ùïú f x := by
  simp only [sub_eq_add_neg, fderiv_add_const]
#align fderiv_sub_const fderiv_sub_const

theorem HasStrictFDerivAt.const_sub (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun x => c - f x) (-f') x := by
  simpa only [sub_eq_add_neg] using hf.neg.const_add c
#align has_strict_fderiv_at.const_sub HasStrictFDerivAt.const_sub

theorem HasFDerivAtFilter.const_sub (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun x => c - f x) (-f') x L := by
  simpa only [sub_eq_add_neg] using hf.neg.const_add c
#align has_fderiv_at_filter.const_sub HasFDerivAtFilter.const_sub

nonrec theorem HasFDerivWithinAt.const_sub (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun x => c - f x) (-f') s x :=
  hf.const_sub c
#align has_fderiv_within_at.const_sub HasFDerivWithinAt.const_sub

nonrec theorem HasFDerivAt.const_sub (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => c - f x) (-f') x :=
  hf.const_sub c
#align has_fderiv_at.const_sub HasFDerivAt.const_sub

theorem DifferentiableWithinAt.const_sub (hf : DifferentiableWithinAt ùïú f s x) (c : F) :
    DifferentiableWithinAt ùïú (fun y => c - f y) s x :=
  (hf.hasFDerivWithinAt.const_sub c).differentiableWithinAt
#align differentiable_within_at.const_sub DifferentiableWithinAt.const_sub

@[simp]
theorem differentiableWithinAt_const_sub_iff (c : F) :
    DifferentiableWithinAt ùïú (fun y => c - f y) s x ‚Üî DifferentiableWithinAt ùïú f s x := by
  simp [sub_eq_add_neg]
#align differentiable_within_at_const_sub_iff differentiableWithinAt_const_sub_iff

theorem DifferentiableAt.const_sub (hf : DifferentiableAt ùïú f x) (c : F) :
    DifferentiableAt ùïú (fun y => c - f y) x :=
  (hf.hasFDerivAt.const_sub c).differentiableAt
#align differentiable_at.const_sub DifferentiableAt.const_sub

@[simp]
theorem differentiableAt_const_sub_iff (c : F) :
    DifferentiableAt ùïú (fun y => c - f y) x ‚Üî DifferentiableAt ùïú f x := by simp [sub_eq_add_neg]
#align differentiable_at_const_sub_iff differentiableAt_const_sub_iff

theorem DifferentiableOn.const_sub (hf : DifferentiableOn ùïú f s) (c : F) :
    DifferentiableOn ùïú (fun y => c - f y) s := fun x hx => (hf x hx).const_sub c
#align differentiable_on.const_sub DifferentiableOn.const_sub

@[simp]
theorem differentiableOn_const_sub_iff (c : F) :
    DifferentiableOn ùïú (fun y => c - f y) s ‚Üî DifferentiableOn ùïú f s := by simp [sub_eq_add_neg]
#align differentiable_on_const_sub_iff differentiableOn_const_sub_iff

theorem Differentiable.const_sub (hf : Differentiable ùïú f) (c : F) :
    Differentiable ùïú fun y => c - f y := fun x => (hf x).const_sub c
#align differentiable.const_sub Differentiable.const_sub

@[simp]
theorem differentiable_const_sub_iff (c : F) :
    (Differentiable ùïú fun y => c - f y) ‚Üî Differentiable ùïú f := by simp [sub_eq_add_neg]
#align differentiable_const_sub_iff differentiable_const_sub_iff

theorem fderivWithin_const_sub (hxs : UniqueDiffWithinAt ùïú s x) (c : F) :
    fderivWithin ùïú (fun y => c - f y) s x = -fderivWithin ùïú f s x := by
  simp only [sub_eq_add_neg, fderivWithin_const_add, fderivWithin_neg, hxs]
#align fderiv_within_const_sub fderivWithin_const_sub

theorem fderiv_const_sub (c : F) : fderiv ùïú (fun y => c - f y) x = -fderiv ùïú f x := by
  simp only [‚Üê fderivWithin_univ, fderivWithin_const_sub uniqueDiffWithinAt_univ]
#align fderiv_const_sub fderiv_const_sub

end Sub

end
