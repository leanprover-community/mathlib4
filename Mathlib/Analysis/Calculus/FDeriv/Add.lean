/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

#align_import analysis.calculus.fderiv.add from "leanprover-community/mathlib"@"e3fb84046afd187b710170887195d50bada934ee"

/-!
# Additive operations on derivatives

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of

* sum of finitely many functions
* multiplication of a function by a scalar constant
* negative of a function
* subtraction of two functions
-/


open Filter Asymptotics ContinuousLinearMap Set Metric

open scoped Classical
open Topology NNReal Filter Asymptotics ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable (e : E â†’L[ğ•œ] F)
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

section ConstSMul

variable {R : Type*} [Semiring R] [Module R F] [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F]

/-! ### Derivative of a function multiplied by a constant -/

@[fun_prop]
theorem HasStrictFDerivAt.const_smul (h : HasStrictFDerivAt f f' x) (c : R) :
    HasStrictFDerivAt (fun x => c â€¢ f x) (c â€¢ f') x :=
  (c â€¢ (1 : F â†’L[ğ•œ] F)).hasStrictFDerivAt.comp x h
#align has_strict_fderiv_at.const_smul HasStrictFDerivAt.const_smul

theorem HasFDerivAtFilter.const_smul (h : HasFDerivAtFilter f f' x L) (c : R) :
    HasFDerivAtFilter (fun x => c â€¢ f x) (c â€¢ f') x L :=
  (c â€¢ (1 : F â†’L[ğ•œ] F)).hasFDerivAtFilter.comp x h tendsto_map
#align has_fderiv_at_filter.const_smul HasFDerivAtFilter.const_smul

@[fun_prop]
nonrec theorem HasFDerivWithinAt.const_smul (h : HasFDerivWithinAt f f' s x) (c : R) :
    HasFDerivWithinAt (fun x => c â€¢ f x) (c â€¢ f') s x :=
  h.const_smul c
#align has_fderiv_within_at.const_smul HasFDerivWithinAt.const_smul

@[fun_prop]
nonrec theorem HasFDerivAt.const_smul (h : HasFDerivAt f f' x) (c : R) :
    HasFDerivAt (fun x => c â€¢ f x) (c â€¢ f') x :=
  h.const_smul c
#align has_fderiv_at.const_smul HasFDerivAt.const_smul

@[fun_prop]
theorem DifferentiableWithinAt.const_smul (h : DifferentiableWithinAt ğ•œ f s x) (c : R) :
    DifferentiableWithinAt ğ•œ (fun y => c â€¢ f y) s x :=
  (h.hasFDerivWithinAt.const_smul c).differentiableWithinAt
#align differentiable_within_at.const_smul DifferentiableWithinAt.const_smul

@[fun_prop]
theorem DifferentiableAt.const_smul (h : DifferentiableAt ğ•œ f x) (c : R) :
    DifferentiableAt ğ•œ (fun y => c â€¢ f y) x :=
  (h.hasFDerivAt.const_smul c).differentiableAt
#align differentiable_at.const_smul DifferentiableAt.const_smul

@[fun_prop]
theorem DifferentiableOn.const_smul (h : DifferentiableOn ğ•œ f s) (c : R) :
    DifferentiableOn ğ•œ (fun y => c â€¢ f y) s := fun x hx => (h x hx).const_smul c
#align differentiable_on.const_smul DifferentiableOn.const_smul

@[fun_prop]
theorem Differentiable.const_smul (h : Differentiable ğ•œ f) (c : R) :
    Differentiable ğ•œ fun y => c â€¢ f y := fun x => (h x).const_smul c
#align differentiable.const_smul Differentiable.const_smul

theorem fderivWithin_const_smul (hxs : UniqueDiffWithinAt ğ•œ s x)
    (h : DifferentiableWithinAt ğ•œ f s x) (c : R) :
    fderivWithin ğ•œ (fun y => c â€¢ f y) s x = c â€¢ fderivWithin ğ•œ f s x :=
  (h.hasFDerivWithinAt.const_smul c).fderivWithin hxs
#align fderiv_within_const_smul fderivWithin_const_smul

theorem fderiv_const_smul (h : DifferentiableAt ğ•œ f x) (c : R) :
    fderiv ğ•œ (fun y => c â€¢ f y) x = c â€¢ fderiv ğ•œ f x :=
  (h.hasFDerivAt.const_smul c).fderiv
#align fderiv_const_smul fderiv_const_smul

end ConstSMul

section Add

/-! ### Derivative of the sum of two functions -/


@[fun_prop]
nonrec theorem HasStrictFDerivAt.add (hf : HasStrictFDerivAt f f' x)
    (hg : HasStrictFDerivAt g g' x) : HasStrictFDerivAt (fun y => f y + g y) (f' + g') x :=
  (hf.add hg).congr_left fun y => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel
#align has_strict_fderiv_at.add HasStrictFDerivAt.add

theorem HasFDerivAtFilter.add (hf : HasFDerivAtFilter f f' x L)
    (hg : HasFDerivAtFilter g g' x L) : HasFDerivAtFilter (fun y => f y + g y) (f' + g') x L :=
  .of_isLittleO <| (hf.isLittleO.add hg.isLittleO).congr_left fun _ => by
    simp only [LinearMap.sub_apply, LinearMap.add_apply, map_sub, map_add, add_apply]
    abel
#align has_fderiv_at_filter.add HasFDerivAtFilter.add

@[fun_prop]
nonrec theorem HasFDerivWithinAt.add (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) : HasFDerivWithinAt (fun y => f y + g y) (f' + g') s x :=
  hf.add hg
#align has_fderiv_within_at.add HasFDerivWithinAt.add

@[fun_prop]
nonrec theorem HasFDerivAt.add (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x => f x + g x) (f' + g') x :=
  hf.add hg
#align has_fderiv_at.add HasFDerivAt.add

@[fun_prop]
theorem DifferentiableWithinAt.add (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : DifferentiableWithinAt ğ•œ g s x) : DifferentiableWithinAt ğ•œ (fun y => f y + g y) s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.add DifferentiableWithinAt.add

@[simp, fun_prop]
theorem DifferentiableAt.add (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    DifferentiableAt ğ•œ (fun y => f y + g y) x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).differentiableAt
#align differentiable_at.add DifferentiableAt.add

@[fun_prop]
theorem DifferentiableOn.add (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s) :
    DifferentiableOn ğ•œ (fun y => f y + g y) s := fun x hx => (hf x hx).add (hg x hx)
#align differentiable_on.add DifferentiableOn.add

@[simp, fun_prop]
theorem Differentiable.add (hf : Differentiable ğ•œ f) (hg : Differentiable ğ•œ g) :
    Differentiable ğ•œ fun y => f y + g y := fun x => (hf x).add (hg x)
#align differentiable.add Differentiable.add

theorem fderivWithin_add (hxs : UniqueDiffWithinAt ğ•œ s x) (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : DifferentiableWithinAt ğ•œ g s x) :
    fderivWithin ğ•œ (fun y => f y + g y) s x = fderivWithin ğ•œ f s x + fderivWithin ğ•œ g s x :=
  (hf.hasFDerivWithinAt.add hg.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_add fderivWithin_add

theorem fderiv_add (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    fderiv ğ•œ (fun y => f y + g y) x = fderiv ğ•œ f x + fderiv ğ•œ g x :=
  (hf.hasFDerivAt.add hg.hasFDerivAt).fderiv
#align fderiv_add fderiv_add

@[fun_prop]
theorem HasStrictFDerivAt.add_const (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun y => f y + c) f' x :=
  add_zero f' â–¸ hf.add (hasStrictFDerivAt_const _ _)
#align has_strict_fderiv_at.add_const HasStrictFDerivAt.add_const

theorem HasFDerivAtFilter.add_const (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun y => f y + c) f' x L :=
  add_zero f' â–¸ hf.add (hasFDerivAtFilter_const _ _ _)
#align has_fderiv_at_filter.add_const HasFDerivAtFilter.add_const

@[fun_prop]
nonrec theorem HasFDerivWithinAt.add_const (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun y => f y + c) f' s x :=
  hf.add_const c
#align has_fderiv_within_at.add_const HasFDerivWithinAt.add_const

@[fun_prop]
nonrec theorem HasFDerivAt.add_const (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => f x + c) f' x :=
  hf.add_const c
#align has_fderiv_at.add_const HasFDerivAt.add_const

@[fun_prop]
theorem DifferentiableWithinAt.add_const (hf : DifferentiableWithinAt ğ•œ f s x) (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => f y + c) s x :=
  (hf.hasFDerivWithinAt.add_const c).differentiableWithinAt
#align differentiable_within_at.add_const DifferentiableWithinAt.add_const

@[simp]
theorem differentiableWithinAt_add_const_iff (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => f y + c) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨fun h => by simpa using h.add_const (-c), fun h => h.add_const câŸ©
#align differentiable_within_at_add_const_iff differentiableWithinAt_add_const_iff

@[fun_prop]
theorem DifferentiableAt.add_const (hf : DifferentiableAt ğ•œ f x) (c : F) :
    DifferentiableAt ğ•œ (fun y => f y + c) x :=
  (hf.hasFDerivAt.add_const c).differentiableAt
#align differentiable_at.add_const DifferentiableAt.add_const

@[simp]
theorem differentiableAt_add_const_iff (c : F) :
    DifferentiableAt ğ•œ (fun y => f y + c) x â†” DifferentiableAt ğ•œ f x :=
  âŸ¨fun h => by simpa using h.add_const (-c), fun h => h.add_const câŸ©
#align differentiable_at_add_const_iff differentiableAt_add_const_iff

@[fun_prop]
theorem DifferentiableOn.add_const (hf : DifferentiableOn ğ•œ f s) (c : F) :
    DifferentiableOn ğ•œ (fun y => f y + c) s := fun x hx => (hf x hx).add_const c
#align differentiable_on.add_const DifferentiableOn.add_const

@[simp]
theorem differentiableOn_add_const_iff (c : F) :
    DifferentiableOn ğ•œ (fun y => f y + c) s â†” DifferentiableOn ğ•œ f s :=
  âŸ¨fun h => by simpa using h.add_const (-c), fun h => h.add_const câŸ©
#align differentiable_on_add_const_iff differentiableOn_add_const_iff

@[fun_prop]
theorem Differentiable.add_const (hf : Differentiable ğ•œ f) (c : F) :
    Differentiable ğ•œ fun y => f y + c := fun x => (hf x).add_const c
#align differentiable.add_const Differentiable.add_const

@[simp]
theorem differentiable_add_const_iff (c : F) :
    (Differentiable ğ•œ fun y => f y + c) â†” Differentiable ğ•œ f :=
  âŸ¨fun h => by simpa using h.add_const (-c), fun h => h.add_const câŸ©
#align differentiable_add_const_iff differentiable_add_const_iff

theorem fderivWithin_add_const (hxs : UniqueDiffWithinAt ğ•œ s x) (c : F) :
    fderivWithin ğ•œ (fun y => f y + c) s x = fderivWithin ğ•œ f s x :=
  if hf : DifferentiableWithinAt ğ•œ f s x then (hf.hasFDerivWithinAt.add_const c).fderivWithin hxs
  else by
    rw [fderivWithin_zero_of_not_differentiableWithinAt hf,
      fderivWithin_zero_of_not_differentiableWithinAt]
    simpa
#align fderiv_within_add_const fderivWithin_add_const

theorem fderiv_add_const (c : F) : fderiv ğ•œ (fun y => f y + c) x = fderiv ğ•œ f x := by
  simp only [â† fderivWithin_univ, fderivWithin_add_const uniqueDiffWithinAt_univ]
#align fderiv_add_const fderiv_add_const

@[fun_prop]
theorem HasStrictFDerivAt.const_add (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun y => c + f y) f' x :=
  zero_add f' â–¸ (hasStrictFDerivAt_const _ _).add hf
#align has_strict_fderiv_at.const_add HasStrictFDerivAt.const_add

theorem HasFDerivAtFilter.const_add (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun y => c + f y) f' x L :=
  zero_add f' â–¸ (hasFDerivAtFilter_const _ _ _).add hf
#align has_fderiv_at_filter.const_add HasFDerivAtFilter.const_add

@[fun_prop]
nonrec theorem HasFDerivWithinAt.const_add (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun y => c + f y) f' s x :=
  hf.const_add c
#align has_fderiv_within_at.const_add HasFDerivWithinAt.const_add

@[fun_prop]
nonrec theorem HasFDerivAt.const_add (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => c + f x) f' x :=
  hf.const_add c
#align has_fderiv_at.const_add HasFDerivAt.const_add

@[fun_prop]
theorem DifferentiableWithinAt.const_add (hf : DifferentiableWithinAt ğ•œ f s x) (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => c + f y) s x :=
  (hf.hasFDerivWithinAt.const_add c).differentiableWithinAt
#align differentiable_within_at.const_add DifferentiableWithinAt.const_add

@[simp]
theorem differentiableWithinAt_const_add_iff (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => c + f y) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨fun h => by simpa using h.const_add (-c), fun h => h.const_add câŸ©
#align differentiable_within_at_const_add_iff differentiableWithinAt_const_add_iff

@[fun_prop]
theorem DifferentiableAt.const_add (hf : DifferentiableAt ğ•œ f x) (c : F) :
    DifferentiableAt ğ•œ (fun y => c + f y) x :=
  (hf.hasFDerivAt.const_add c).differentiableAt
#align differentiable_at.const_add DifferentiableAt.const_add

@[simp]
theorem differentiableAt_const_add_iff (c : F) :
    DifferentiableAt ğ•œ (fun y => c + f y) x â†” DifferentiableAt ğ•œ f x :=
  âŸ¨fun h => by simpa using h.const_add (-c), fun h => h.const_add câŸ©
#align differentiable_at_const_add_iff differentiableAt_const_add_iff

@[fun_prop]
theorem DifferentiableOn.const_add (hf : DifferentiableOn ğ•œ f s) (c : F) :
    DifferentiableOn ğ•œ (fun y => c + f y) s := fun x hx => (hf x hx).const_add c
#align differentiable_on.const_add DifferentiableOn.const_add

@[simp]
theorem differentiableOn_const_add_iff (c : F) :
    DifferentiableOn ğ•œ (fun y => c + f y) s â†” DifferentiableOn ğ•œ f s :=
  âŸ¨fun h => by simpa using h.const_add (-c), fun h => h.const_add câŸ©
#align differentiable_on_const_add_iff differentiableOn_const_add_iff

@[fun_prop]
theorem Differentiable.const_add (hf : Differentiable ğ•œ f) (c : F) :
    Differentiable ğ•œ fun y => c + f y := fun x => (hf x).const_add c
#align differentiable.const_add Differentiable.const_add

@[simp]
theorem differentiable_const_add_iff (c : F) :
    (Differentiable ğ•œ fun y => c + f y) â†” Differentiable ğ•œ f :=
  âŸ¨fun h => by simpa using h.const_add (-c), fun h => h.const_add câŸ©
#align differentiable_const_add_iff differentiable_const_add_iff

theorem fderivWithin_const_add (hxs : UniqueDiffWithinAt ğ•œ s x) (c : F) :
    fderivWithin ğ•œ (fun y => c + f y) s x = fderivWithin ğ•œ f s x := by
  simpa only [add_comm] using fderivWithin_add_const hxs c
#align fderiv_within_const_add fderivWithin_const_add

theorem fderiv_const_add (c : F) : fderiv ğ•œ (fun y => c + f y) x = fderiv ğ•œ f x := by
  simp only [add_comm c, fderiv_add_const]
#align fderiv_const_add fderiv_const_add

end Add

section Sum

/-! ### Derivative of a finite sum of functions -/


variable {Î¹ : Type*} {u : Finset Î¹} {A : Î¹ â†’ E â†’ F} {A' : Î¹ â†’ E â†’L[ğ•œ] F}

@[fun_prop]
theorem HasStrictFDerivAt.sum (h : âˆ€ i âˆˆ u, HasStrictFDerivAt (A i) (A' i) x) :
    HasStrictFDerivAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x := by
  dsimp [HasStrictFDerivAt] at *
  convert IsLittleO.sum h
  simp [Finset.sum_sub_distrib, ContinuousLinearMap.sum_apply]
#align has_strict_fderiv_at.sum HasStrictFDerivAt.sum

theorem HasFDerivAtFilter.sum (h : âˆ€ i âˆˆ u, HasFDerivAtFilter (A i) (A' i) x L) :
    HasFDerivAtFilter (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x L := by
  simp only [hasFDerivAtFilter_iff_isLittleO] at *
  convert IsLittleO.sum h
  simp [ContinuousLinearMap.sum_apply]
#align has_fderiv_at_filter.sum HasFDerivAtFilter.sum

@[fun_prop]
theorem HasFDerivWithinAt.sum (h : âˆ€ i âˆˆ u, HasFDerivWithinAt (A i) (A' i) s x) :
    HasFDerivWithinAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) s x :=
  HasFDerivAtFilter.sum h
#align has_fderiv_within_at.sum HasFDerivWithinAt.sum

@[fun_prop]
theorem HasFDerivAt.sum (h : âˆ€ i âˆˆ u, HasFDerivAt (A i) (A' i) x) :
    HasFDerivAt (fun y => âˆ‘ i âˆˆ u, A i y) (âˆ‘ i âˆˆ u, A' i) x :=
  HasFDerivAtFilter.sum h
#align has_fderiv_at.sum HasFDerivAt.sum

@[fun_prop]
theorem DifferentiableWithinAt.sum (h : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (A i) s x) :
    DifferentiableWithinAt ğ•œ (fun y => âˆ‘ i âˆˆ u, A i y) s x :=
  HasFDerivWithinAt.differentiableWithinAt <|
    HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt
#align differentiable_within_at.sum DifferentiableWithinAt.sum

@[simp, fun_prop]
theorem DifferentiableAt.sum (h : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (A i) x) :
    DifferentiableAt ğ•œ (fun y => âˆ‘ i âˆˆ u, A i y) x :=
  HasFDerivAt.differentiableAt <| HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt
#align differentiable_at.sum DifferentiableAt.sum

@[fun_prop]
theorem DifferentiableOn.sum (h : âˆ€ i âˆˆ u, DifferentiableOn ğ•œ (A i) s) :
    DifferentiableOn ğ•œ (fun y => âˆ‘ i âˆˆ u, A i y) s := fun x hx =>
  DifferentiableWithinAt.sum fun i hi => h i hi x hx
#align differentiable_on.sum DifferentiableOn.sum

@[simp, fun_prop]
theorem Differentiable.sum (h : âˆ€ i âˆˆ u, Differentiable ğ•œ (A i)) :
    Differentiable ğ•œ fun y => âˆ‘ i âˆˆ u, A i y := fun x => DifferentiableAt.sum fun i hi => h i hi x
#align differentiable.sum Differentiable.sum

theorem fderivWithin_sum (hxs : UniqueDiffWithinAt ğ•œ s x)
    (h : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (A i) s x) :
    fderivWithin ğ•œ (fun y => âˆ‘ i âˆˆ u, A i y) s x = âˆ‘ i âˆˆ u, fderivWithin ğ•œ (A i) s x :=
  (HasFDerivWithinAt.sum fun i hi => (h i hi).hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_sum fderivWithin_sum

theorem fderiv_sum (h : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (A i) x) :
    fderiv ğ•œ (fun y => âˆ‘ i âˆˆ u, A i y) x = âˆ‘ i âˆˆ u, fderiv ğ•œ (A i) x :=
  (HasFDerivAt.sum fun i hi => (h i hi).hasFDerivAt).fderiv
#align fderiv_sum fderiv_sum

end Sum

section Neg

/-! ### Derivative of the negative of a function -/


@[fun_prop]
theorem HasStrictFDerivAt.neg (h : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => -f x) (-f') x :=
  (-1 : F â†’L[ğ•œ] F).hasStrictFDerivAt.comp x h
#align has_strict_fderiv_at.neg HasStrictFDerivAt.neg

theorem HasFDerivAtFilter.neg (h : HasFDerivAtFilter f f' x L) :
    HasFDerivAtFilter (fun x => -f x) (-f') x L :=
  (-1 : F â†’L[ğ•œ] F).hasFDerivAtFilter.comp x h tendsto_map
#align has_fderiv_at_filter.neg HasFDerivAtFilter.neg

@[fun_prop]
nonrec theorem HasFDerivWithinAt.neg (h : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => -f x) (-f') s x :=
  h.neg
#align has_fderiv_within_at.neg HasFDerivWithinAt.neg

@[fun_prop]
nonrec theorem HasFDerivAt.neg (h : HasFDerivAt f f' x) : HasFDerivAt (fun x => -f x) (-f') x :=
  h.neg
#align has_fderiv_at.neg HasFDerivAt.neg

@[fun_prop]
theorem DifferentiableWithinAt.neg (h : DifferentiableWithinAt ğ•œ f s x) :
    DifferentiableWithinAt ğ•œ (fun y => -f y) s x :=
  h.hasFDerivWithinAt.neg.differentiableWithinAt
#align differentiable_within_at.neg DifferentiableWithinAt.neg

@[simp]
theorem differentiableWithinAt_neg_iff :
    DifferentiableWithinAt ğ•œ (fun y => -f y) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©
#align differentiable_within_at_neg_iff differentiableWithinAt_neg_iff

@[fun_prop]
theorem DifferentiableAt.neg (h : DifferentiableAt ğ•œ f x) : DifferentiableAt ğ•œ (fun y => -f y) x :=
  h.hasFDerivAt.neg.differentiableAt
#align differentiable_at.neg DifferentiableAt.neg

@[simp]
theorem differentiableAt_neg_iff : DifferentiableAt ğ•œ (fun y => -f y) x â†” DifferentiableAt ğ•œ f x :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©
#align differentiable_at_neg_iff differentiableAt_neg_iff

@[fun_prop]
theorem DifferentiableOn.neg (h : DifferentiableOn ğ•œ f s) : DifferentiableOn ğ•œ (fun y => -f y) s :=
  fun x hx => (h x hx).neg
#align differentiable_on.neg DifferentiableOn.neg

@[simp]
theorem differentiableOn_neg_iff : DifferentiableOn ğ•œ (fun y => -f y) s â†” DifferentiableOn ğ•œ f s :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©
#align differentiable_on_neg_iff differentiableOn_neg_iff

@[fun_prop]
theorem Differentiable.neg (h : Differentiable ğ•œ f) : Differentiable ğ•œ fun y => -f y := fun x =>
  (h x).neg
#align differentiable.neg Differentiable.neg

@[simp]
theorem differentiable_neg_iff : (Differentiable ğ•œ fun y => -f y) â†” Differentiable ğ•œ f :=
  âŸ¨fun h => by simpa only [neg_neg] using h.neg, fun h => h.negâŸ©
#align differentiable_neg_iff differentiable_neg_iff

theorem fderivWithin_neg (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun y => -f y) s x = -fderivWithin ğ•œ f s x :=
  if h : DifferentiableWithinAt ğ•œ f s x then h.hasFDerivWithinAt.neg.fderivWithin hxs
  else by
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt, neg_zero]
    simpa
#align fderiv_within_neg fderivWithin_neg

@[simp]
theorem fderiv_neg : fderiv ğ•œ (fun y => -f y) x = -fderiv ğ•œ f x := by
  simp only [â† fderivWithin_univ, fderivWithin_neg uniqueDiffWithinAt_univ]
#align fderiv_neg fderiv_neg

end Neg

section Sub

/-! ### Derivative of the difference of two functions -/


@[fun_prop]
theorem HasStrictFDerivAt.sub (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun x => f x - g x) (f' - g') x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_strict_fderiv_at.sub HasStrictFDerivAt.sub

theorem HasFDerivAtFilter.sub (hf : HasFDerivAtFilter f f' x L) (hg : HasFDerivAtFilter g g' x L) :
    HasFDerivAtFilter (fun x => f x - g x) (f' - g') x L := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_fderiv_at_filter.sub HasFDerivAtFilter.sub

@[fun_prop]
nonrec theorem HasFDerivWithinAt.sub (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) : HasFDerivWithinAt (fun x => f x - g x) (f' - g') s x :=
  hf.sub hg
#align has_fderiv_within_at.sub HasFDerivWithinAt.sub

@[fun_prop]
nonrec theorem HasFDerivAt.sub (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x => f x - g x) (f' - g') x :=
  hf.sub hg
#align has_fderiv_at.sub HasFDerivAt.sub

@[fun_prop]
theorem DifferentiableWithinAt.sub (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : DifferentiableWithinAt ğ•œ g s x) : DifferentiableWithinAt ğ•œ (fun y => f y - g y) s x :=
  (hf.hasFDerivWithinAt.sub hg.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.sub DifferentiableWithinAt.sub

@[simp, fun_prop]
theorem DifferentiableAt.sub (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    DifferentiableAt ğ•œ (fun y => f y - g y) x :=
  (hf.hasFDerivAt.sub hg.hasFDerivAt).differentiableAt
#align differentiable_at.sub DifferentiableAt.sub

@[fun_prop]
theorem DifferentiableOn.sub (hf : DifferentiableOn ğ•œ f s) (hg : DifferentiableOn ğ•œ g s) :
    DifferentiableOn ğ•œ (fun y => f y - g y) s := fun x hx => (hf x hx).sub (hg x hx)
#align differentiable_on.sub DifferentiableOn.sub

@[simp, fun_prop]
theorem Differentiable.sub (hf : Differentiable ğ•œ f) (hg : Differentiable ğ•œ g) :
    Differentiable ğ•œ fun y => f y - g y := fun x => (hf x).sub (hg x)
#align differentiable.sub Differentiable.sub

theorem fderivWithin_sub (hxs : UniqueDiffWithinAt ğ•œ s x) (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : DifferentiableWithinAt ğ•œ g s x) :
    fderivWithin ğ•œ (fun y => f y - g y) s x = fderivWithin ğ•œ f s x - fderivWithin ğ•œ g s x :=
  (hf.hasFDerivWithinAt.sub hg.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_sub fderivWithin_sub

theorem fderiv_sub (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    fderiv ğ•œ (fun y => f y - g y) x = fderiv ğ•œ f x - fderiv ğ•œ g x :=
  (hf.hasFDerivAt.sub hg.hasFDerivAt).fderiv
#align fderiv_sub fderiv_sub

@[fun_prop]
theorem HasStrictFDerivAt.sub_const (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun x => f x - c) f' x := by
  simpa only [sub_eq_add_neg] using hf.add_const (-c)
#align has_strict_fderiv_at.sub_const HasStrictFDerivAt.sub_const

theorem HasFDerivAtFilter.sub_const (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun x => f x - c) f' x L := by
  simpa only [sub_eq_add_neg] using hf.add_const (-c)
#align has_fderiv_at_filter.sub_const HasFDerivAtFilter.sub_const

@[fun_prop]
nonrec theorem HasFDerivWithinAt.sub_const (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun x => f x - c) f' s x :=
  hf.sub_const c
#align has_fderiv_within_at.sub_const HasFDerivWithinAt.sub_const

@[fun_prop]
nonrec theorem HasFDerivAt.sub_const (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => f x - c) f' x :=
  hf.sub_const c
#align has_fderiv_at.sub_const HasFDerivAt.sub_const

@[fun_prop]
theorem hasStrictFDerivAt_sub_const {x : F} (c : F) : HasStrictFDerivAt (Â· - c) (id ğ•œ F) x :=
  (hasStrictFDerivAt_id x).sub_const c

@[fun_prop]
theorem hasFDerivAt_sub_const {x : F} (c : F) : HasFDerivAt (Â· - c) (id ğ•œ F) x :=
  (hasFDerivAt_id x).sub_const c

@[fun_prop]
theorem DifferentiableWithinAt.sub_const (hf : DifferentiableWithinAt ğ•œ f s x) (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => f y - c) s x :=
  (hf.hasFDerivWithinAt.sub_const c).differentiableWithinAt
#align differentiable_within_at.sub_const DifferentiableWithinAt.sub_const

@[simp]
theorem differentiableWithinAt_sub_const_iff (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => f y - c) s x â†” DifferentiableWithinAt ğ•œ f s x := by
  simp only [sub_eq_add_neg, differentiableWithinAt_add_const_iff]
#align differentiable_within_at_sub_const_iff differentiableWithinAt_sub_const_iff

@[fun_prop]
theorem DifferentiableAt.sub_const (hf : DifferentiableAt ğ•œ f x) (c : F) :
    DifferentiableAt ğ•œ (fun y => f y - c) x :=
  (hf.hasFDerivAt.sub_const c).differentiableAt
#align differentiable_at.sub_const DifferentiableAt.sub_const

@[simp]
theorem differentiableAt_sub_const_iff (c : F) :
    DifferentiableAt ğ•œ (fun y => f y - c) x â†” DifferentiableAt ğ•œ f x := by
  simp only [sub_eq_add_neg, differentiableAt_add_const_iff]
#align differentiable_at_sub_const_iff differentiableAt_sub_const_iff

@[fun_prop]
theorem DifferentiableOn.sub_const (hf : DifferentiableOn ğ•œ f s) (c : F) :
    DifferentiableOn ğ•œ (fun y => f y - c) s := fun x hx => (hf x hx).sub_const c
#align differentiable_on.sub_const DifferentiableOn.sub_const

@[simp]
theorem differentiableOn_sub_const_iff (c : F) :
    DifferentiableOn ğ•œ (fun y => f y - c) s â†” DifferentiableOn ğ•œ f s := by
  simp only [sub_eq_add_neg, differentiableOn_add_const_iff]
#align differentiable_on_sub_const_iff differentiableOn_sub_const_iff

@[fun_prop]
theorem Differentiable.sub_const (hf : Differentiable ğ•œ f) (c : F) :
    Differentiable ğ•œ fun y => f y - c := fun x => (hf x).sub_const c
#align differentiable.sub_const Differentiable.sub_const

@[simp]
theorem differentiable_sub_const_iff (c : F) :
    (Differentiable ğ•œ fun y => f y - c) â†” Differentiable ğ•œ f := by
  simp only [sub_eq_add_neg, differentiable_add_const_iff]
#align differentiable_sub_const_iff differentiable_sub_const_iff

theorem fderivWithin_sub_const (hxs : UniqueDiffWithinAt ğ•œ s x) (c : F) :
    fderivWithin ğ•œ (fun y => f y - c) s x = fderivWithin ğ•œ f s x := by
  simp only [sub_eq_add_neg, fderivWithin_add_const hxs]
#align fderiv_within_sub_const fderivWithin_sub_const

theorem fderiv_sub_const (c : F) : fderiv ğ•œ (fun y => f y - c) x = fderiv ğ•œ f x := by
  simp only [sub_eq_add_neg, fderiv_add_const]
#align fderiv_sub_const fderiv_sub_const

@[fun_prop]
theorem HasStrictFDerivAt.const_sub (hf : HasStrictFDerivAt f f' x) (c : F) :
    HasStrictFDerivAt (fun x => c - f x) (-f') x := by
  simpa only [sub_eq_add_neg] using hf.neg.const_add c
#align has_strict_fderiv_at.const_sub HasStrictFDerivAt.const_sub

theorem HasFDerivAtFilter.const_sub (hf : HasFDerivAtFilter f f' x L) (c : F) :
    HasFDerivAtFilter (fun x => c - f x) (-f') x L := by
  simpa only [sub_eq_add_neg] using hf.neg.const_add c
#align has_fderiv_at_filter.const_sub HasFDerivAtFilter.const_sub

@[fun_prop]
nonrec theorem HasFDerivWithinAt.const_sub (hf : HasFDerivWithinAt f f' s x) (c : F) :
    HasFDerivWithinAt (fun x => c - f x) (-f') s x :=
  hf.const_sub c
#align has_fderiv_within_at.const_sub HasFDerivWithinAt.const_sub

@[fun_prop]
nonrec theorem HasFDerivAt.const_sub (hf : HasFDerivAt f f' x) (c : F) :
    HasFDerivAt (fun x => c - f x) (-f') x :=
  hf.const_sub c
#align has_fderiv_at.const_sub HasFDerivAt.const_sub

@[fun_prop]
theorem DifferentiableWithinAt.const_sub (hf : DifferentiableWithinAt ğ•œ f s x) (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => c - f y) s x :=
  (hf.hasFDerivWithinAt.const_sub c).differentiableWithinAt
#align differentiable_within_at.const_sub DifferentiableWithinAt.const_sub

@[simp]
theorem differentiableWithinAt_const_sub_iff (c : F) :
    DifferentiableWithinAt ğ•œ (fun y => c - f y) s x â†” DifferentiableWithinAt ğ•œ f s x := by
  simp [sub_eq_add_neg]
#align differentiable_within_at_const_sub_iff differentiableWithinAt_const_sub_iff

@[fun_prop]
theorem DifferentiableAt.const_sub (hf : DifferentiableAt ğ•œ f x) (c : F) :
    DifferentiableAt ğ•œ (fun y => c - f y) x :=
  (hf.hasFDerivAt.const_sub c).differentiableAt
#align differentiable_at.const_sub DifferentiableAt.const_sub

@[simp]
theorem differentiableAt_const_sub_iff (c : F) :
    DifferentiableAt ğ•œ (fun y => c - f y) x â†” DifferentiableAt ğ•œ f x := by simp [sub_eq_add_neg]
#align differentiable_at_const_sub_iff differentiableAt_const_sub_iff

@[fun_prop]
theorem DifferentiableOn.const_sub (hf : DifferentiableOn ğ•œ f s) (c : F) :
    DifferentiableOn ğ•œ (fun y => c - f y) s := fun x hx => (hf x hx).const_sub c
#align differentiable_on.const_sub DifferentiableOn.const_sub

@[simp]
theorem differentiableOn_const_sub_iff (c : F) :
    DifferentiableOn ğ•œ (fun y => c - f y) s â†” DifferentiableOn ğ•œ f s := by simp [sub_eq_add_neg]
#align differentiable_on_const_sub_iff differentiableOn_const_sub_iff

@[fun_prop]
theorem Differentiable.const_sub (hf : Differentiable ğ•œ f) (c : F) :
    Differentiable ğ•œ fun y => c - f y := fun x => (hf x).const_sub c
#align differentiable.const_sub Differentiable.const_sub

@[simp]
theorem differentiable_const_sub_iff (c : F) :
    (Differentiable ğ•œ fun y => c - f y) â†” Differentiable ğ•œ f := by simp [sub_eq_add_neg]
#align differentiable_const_sub_iff differentiable_const_sub_iff

theorem fderivWithin_const_sub (hxs : UniqueDiffWithinAt ğ•œ s x) (c : F) :
    fderivWithin ğ•œ (fun y => c - f y) s x = -fderivWithin ğ•œ f s x := by
  simp only [sub_eq_add_neg, fderivWithin_const_add, fderivWithin_neg, hxs]
#align fderiv_within_const_sub fderivWithin_const_sub

theorem fderiv_const_sub (c : F) : fderiv ğ•œ (fun y => c - f y) x = -fderiv ğ•œ f x := by
  simp only [â† fderivWithin_univ, fderivWithin_const_sub uniqueDiffWithinAt_univ]
#align fderiv_const_sub fderiv_const_sub

end Sub

end
