/-
Copyright (c) 2019 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Extending differentiability to the boundary

We investigate how differentiable functions inside a set extend to differentiable functions
on the boundary. For this, it suffices that the function and its derivative admit limits there.
A general version of this statement is given in `hasFDerivWithinAt_closure_of_tendsto_fderiv`.

One-dimensional versions, in which one wants to obtain differentiability at the left endpoint or
the right endpoint of an interval, are given in `hasDerivWithinAt_Ici_of_tendsto_deriv` and
`hasDerivWithinAt_Iic_of_tendsto_deriv`.  These versions are formulated in terms of the
one-dimensional derivative `deriv ‚Ñù f`.
-/


variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace ‚Ñù F]

open Filter Set Metric ContinuousLinearMap

open scoped Topology

/-- If a function `f` is differentiable in a convex open set and continuous on its closure, and its
derivative converges to a limit `f'` at a point on the boundary, then `f` is differentiable there
with derivative `f'`. -/
theorem hasFDerivWithinAt_closure_of_tendsto_fderiv {f : E ‚Üí F} {s : Set E} {x : E} {f' : E ‚ÜíL[‚Ñù] F}
    (f_diff : DifferentiableOn ‚Ñù f s) (s_conv : Convex ‚Ñù s) (s_open : IsOpen s)
    (f_cont : ‚àÄ y ‚àà closure s, ContinuousWithinAt f s y)
    (h : Tendsto (fun y => fderiv ‚Ñù f y) (ùìù[s] x) (ùìù f')) :
    HasFDerivWithinAt f f' (closure s) x := by
  classical
    -- one can assume without loss of generality that `x` belongs to the closure of `s`, as the
    -- statement is empty otherwise
    by_cases hx : x ‚àâ closure s
    ¬∑ rw [‚Üê closure_closure] at hx; exact HasFDerivWithinAt.of_notMem_closure hx
    push_neg at hx
    rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO, Asymptotics.isLittleO_iff]
    /- One needs to show that `‚Äñf y - f x - f' (y - x)‚Äñ ‚â§ Œµ ‚Äñy - x‚Äñ` for `y` close to `x` in
      `closure s`, where `Œµ` is an arbitrary positive constant. By continuity of the functions, it
      suffices to prove this for nearby points inside `s`. In a neighborhood of `x`, the derivative
      of `f` is arbitrarily close to `f'` by assumption. The mean value inequality completes the
      proof. -/
    intro Œµ Œµ_pos
    obtain ‚ü®Œ¥, Œ¥_pos, hŒ¥‚ü© : ‚àÉ Œ¥ > 0, ‚àÄ y ‚àà s, dist y x < Œ¥ ‚Üí ‚Äñfderiv ‚Ñù f y - f'‚Äñ < Œµ := by
      simpa [dist_zero_right] using tendsto_nhdsWithin_nhds.1 h Œµ Œµ_pos
    set B := ball x Œ¥
    suffices ‚àÄ y ‚àà B ‚à© closure s, ‚Äñf y - f x - (f' y - f' x)‚Äñ ‚â§ Œµ * ‚Äñy - x‚Äñ from
      mem_nhdsWithin_iff.2 ‚ü®Œ¥, Œ¥_pos, fun y hy => by simpa using this y hy‚ü©
    suffices
      ‚àÄ p : E √ó E,
        p ‚àà closure ((B ‚à© s) √óÀ¢ (B ‚à© s)) ‚Üí ‚Äñf p.2 - f p.1 - (f' p.2 - f' p.1)‚Äñ ‚â§ Œµ * ‚Äñp.2 - p.1‚Äñ by
      rw [closure_prod_eq] at this
      intro y y_in
      apply this ‚ü®x, y‚ü©
      have : B ‚à© closure s ‚äÜ closure (B ‚à© s) := isOpen_ball.inter_closure
      exact ‚ü®this ‚ü®mem_ball_self Œ¥_pos, hx‚ü©, this y_in‚ü©
    have key : ‚àÄ p : E √ó E, p ‚àà (B ‚à© s) √óÀ¢ (B ‚à© s) ‚Üí
          ‚Äñf p.2 - f p.1 - (f' p.2 - f' p.1)‚Äñ ‚â§ Œµ * ‚Äñp.2 - p.1‚Äñ := by
      rintro ‚ü®u, v‚ü© ‚ü®u_in, v_in‚ü©
      have conv : Convex ‚Ñù (B ‚à© s) := (convex_ball _ _).inter s_conv
      have diff : DifferentiableOn ‚Ñù f (B ‚à© s) := f_diff.mono inter_subset_right
      have bound : ‚àÄ z ‚àà B ‚à© s, ‚ÄñfderivWithin ‚Ñù f (B ‚à© s) z - f'‚Äñ ‚â§ Œµ := by
        intro z z_in
        have h := hŒ¥ z
        have : fderivWithin ‚Ñù f (B ‚à© s) z = fderiv ‚Ñù f z := by
          have op : IsOpen (B ‚à© s) := isOpen_ball.inter s_open
          rw [DifferentiableAt.fderivWithin _ (op.uniqueDiffOn z z_in)]
          exact (diff z z_in).differentiableAt (IsOpen.mem_nhds op z_in)
        rw [‚Üê this] at h
        exact le_of_lt (h z_in.2 z_in.1)
      simpa using conv.norm_image_sub_le_of_norm_fderivWithin_le' diff bound u_in v_in
    rintro ‚ü®u, v‚ü© uv_in
    have f_cont' : ‚àÄ y ‚àà closure s, ContinuousWithinAt (f -  ‚áëf') s y := by
      intro y y_in
      exact Tendsto.sub (f_cont y y_in) f'.cont.continuousWithinAt
    refine ContinuousWithinAt.closure_le uv_in ?_ ?_ key
    all_goals
      -- common start for both continuity proofs
      have : (B ‚à© s) √óÀ¢ (B ‚à© s) ‚äÜ s √óÀ¢ s := by gcongr <;> exact inter_subset_right
      obtain ‚ü®u_in, v_in‚ü© : u ‚àà closure s ‚àß v ‚àà closure s := by
        simpa [closure_prod_eq] using closure_mono this uv_in
      apply ContinuousWithinAt.mono _ this
      simp only [ContinuousWithinAt]
    ¬∑ rw [nhdsWithin_prod_eq]
      have : ‚àÄ u v, f v - f u - (f' v - f' u) = f v - f' v - (f u - f' u) := by intros; abel
      simp only [this]
      exact
        Tendsto.comp continuous_norm.continuousAt
          ((Tendsto.comp (f_cont' v v_in) tendsto_snd).sub <|
            Tendsto.comp (f_cont' u u_in) tendsto_fst)
    ¬∑ apply tendsto_nhdsWithin_of_tendsto_nhds
      rw [nhds_prod_eq]
      exact
        tendsto_const_nhds.mul
          (Tendsto.comp continuous_norm.continuousAt <| tendsto_snd.sub tendsto_fst)

/-- If a function is differentiable on the right of a point `a : ‚Ñù`, continuous at `a`, and
its derivative also converges at `a`, then `f` is differentiable on the right at `a`. -/
theorem hasDerivWithinAt_Ici_of_tendsto_deriv {s : Set ‚Ñù} {e : E} {a : ‚Ñù} {f : ‚Ñù ‚Üí E}
    (f_diff : DifferentiableOn ‚Ñù f s) (f_lim : ContinuousWithinAt f s a) (hs : s ‚àà ùìù[>] a)
    (f_lim' : Tendsto (fun x => deriv f x) (ùìù[>] a) (ùìù e)) : HasDerivWithinAt f e (Ici a) a := by
  /- This is a specialization of `hasFDerivWithinAt_closure_of_tendsto_fderiv`. To be in the
    setting of this theorem, we need to work on an open interval with closure contained in
    `s ‚à™ {a}`, that we call `t = (a, b)`. Then, we check all the assumptions of this theorem and
    we apply it. -/
  obtain ‚ü®b, ab : a < b, sab : Ioc a b ‚äÜ s‚ü© := mem_nhdsGT_iff_exists_Ioc_subset.1 hs
  let t := Ioo a b
  have ts : t ‚äÜ s := Subset.trans Ioo_subset_Ioc_self sab
  have t_diff : DifferentiableOn ‚Ñù f t := f_diff.mono ts
  have t_conv : Convex ‚Ñù t := convex_Ioo a b
  have t_open : IsOpen t := isOpen_Ioo
  have t_closure : closure t = Icc a b := closure_Ioo ab.ne
  have t_cont : ‚àÄ y ‚àà closure t, ContinuousWithinAt f t y := by
    rw [t_closure]
    intro y hy
    by_cases h : y = a
    ¬∑ rw [h]; exact f_lim.mono ts
    ¬∑ have : y ‚àà s := sab ‚ü®lt_of_le_of_ne hy.1 (Ne.symm h), hy.2‚ü©
      exact (f_diff.continuousOn y this).mono ts
  have t_diff' : Tendsto (fun x => fderiv ‚Ñù f x) (ùìù[t] a) (ùìù (smulRight (1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù) e)) := by
    simp only [deriv_fderiv.symm]
    exact Tendsto.comp
      (isBoundedBilinearMap_smulRight : IsBoundedBilinearMap ‚Ñù _).continuous_right.continuousAt
      (tendsto_nhdsWithin_mono_left Ioo_subset_Ioi_self f_lim')
  -- now we can apply `hasFDerivWithinAt_closure_of_tendsto_fderiv`
  have : HasDerivWithinAt f e (Icc a b) a := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt, ‚Üê t_closure]
    exact hasFDerivWithinAt_closure_of_tendsto_fderiv t_diff t_conv t_open t_cont t_diff'
  exact this.mono_of_mem_nhdsWithin (Icc_mem_nhdsGE ab)

/-- If a function is differentiable on the left of a point `a : ‚Ñù`, continuous at `a`, and
its derivative also converges at `a`, then `f` is differentiable on the left at `a`. -/
theorem hasDerivWithinAt_Iic_of_tendsto_deriv {s : Set ‚Ñù} {e : E} {a : ‚Ñù}
    {f : ‚Ñù ‚Üí E} (f_diff : DifferentiableOn ‚Ñù f s) (f_lim : ContinuousWithinAt f s a)
    (hs : s ‚àà ùìù[<] a) (f_lim' : Tendsto (fun x => deriv f x) (ùìù[<] a) (ùìù e)) :
    HasDerivWithinAt f e (Iic a) a := by
  /- This is a specialization of `hasFDerivWithinAt_closure_of_tendsto_fderiv`. To be in the
    setting of this theorem, we need to work on an open interval with closure contained in
    `s ‚à™ {a}`, that we call `t = (b, a)`. Then, we check all the assumptions of this theorem and we
    apply it. -/
  obtain ‚ü®b, ba, sab‚ü© : ‚àÉ b ‚àà Iio a, Ico b a ‚äÜ s := mem_nhdsLT_iff_exists_Ico_subset.1 hs
  let t := Ioo b a
  have ts : t ‚äÜ s := Subset.trans Ioo_subset_Ico_self sab
  have t_diff : DifferentiableOn ‚Ñù f t := f_diff.mono ts
  have t_conv : Convex ‚Ñù t := convex_Ioo b a
  have t_open : IsOpen t := isOpen_Ioo
  have t_closure : closure t = Icc b a := closure_Ioo (ne_of_lt ba)
  have t_cont : ‚àÄ y ‚àà closure t, ContinuousWithinAt f t y := by
    rw [t_closure]
    intro y hy
    by_cases h : y = a
    ¬∑ rw [h]; exact f_lim.mono ts
    ¬∑ have : y ‚àà s := sab ‚ü®hy.1, lt_of_le_of_ne hy.2 h‚ü©
      exact (f_diff.continuousOn y this).mono ts
  have t_diff' : Tendsto (fun x => fderiv ‚Ñù f x) (ùìù[t] a) (ùìù (smulRight (1 : ‚Ñù ‚ÜíL[‚Ñù] ‚Ñù) e)) := by
    simp only [deriv_fderiv.symm]
    exact Tendsto.comp
      (isBoundedBilinearMap_smulRight : IsBoundedBilinearMap ‚Ñù _).continuous_right.continuousAt
      (tendsto_nhdsWithin_mono_left Ioo_subset_Iio_self f_lim')
  -- now we can apply `hasFDerivWithinAt_closure_of_tendsto_fderiv`
  have : HasDerivWithinAt f e (Icc b a) a := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt, ‚Üê t_closure]
    exact hasFDerivWithinAt_closure_of_tendsto_fderiv t_diff t_conv t_open t_cont t_diff'
  exact this.mono_of_mem_nhdsWithin (Icc_mem_nhdsLE ba)

/-- If a real function `f` has a derivative `g` everywhere but at a point, and `f` and `g` are
continuous at this point, then `g` is also the derivative of `f` at this point. -/
theorem hasDerivAt_of_hasDerivAt_of_ne {f g : ‚Ñù ‚Üí E} {x : ‚Ñù}
    (f_diff : ‚àÄ y ‚â† x, HasDerivAt f (g y) y) (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) : HasDerivAt f (g x) x := by
  have A : HasDerivWithinAt f (g x) (Ici x) x := by
    have diff : DifferentiableOn ‚Ñù f (Ioi x) := fun y hy =>
      (f_diff y (ne_of_gt hy)).differentiableAt.differentiableWithinAt
    -- next line is the nontrivial bit of this proof, appealing to differentiability
    -- extension results.
    apply
      hasDerivWithinAt_Ici_of_tendsto_deriv diff hf.continuousWithinAt
        self_mem_nhdsWithin
    have : Tendsto g (ùìù[>] x) (ùìù (g x)) := tendsto_inf_left hg
    apply this.congr' _
    apply mem_of_superset self_mem_nhdsWithin fun y hy => _
    intros y hy
    exact (f_diff y (ne_of_gt hy)).deriv.symm
  have B : HasDerivWithinAt f (g x) (Iic x) x := by
    have diff : DifferentiableOn ‚Ñù f (Iio x) := fun y hy =>
      (f_diff y (ne_of_lt hy)).differentiableAt.differentiableWithinAt
    -- next line is the nontrivial bit of this proof, appealing to differentiability
    -- extension results.
    apply
      hasDerivWithinAt_Iic_of_tendsto_deriv diff hf.continuousWithinAt
        self_mem_nhdsWithin
    have : Tendsto g (ùìù[<] x) (ùìù (g x)) := tendsto_inf_left hg
    apply this.congr' _
    apply mem_of_superset self_mem_nhdsWithin fun y hy => _
    intros y hy
    exact (f_diff y (ne_of_lt hy)).deriv.symm
  simpa using B.union A

/-- If a real function `f` has a derivative `g` everywhere but at a point, and `f` and `g` are
continuous at this point, then `g` is the derivative of `f` everywhere. -/
theorem hasDerivAt_of_hasDerivAt_of_ne' {f g : ‚Ñù ‚Üí E} {x : ‚Ñù}
    (f_diff : ‚àÄ y ‚â† x, HasDerivAt f (g y) y) (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) (y : ‚Ñù) : HasDerivAt f (g y) y := by
  rcases eq_or_ne y x with (rfl | hne)
  ¬∑ exact hasDerivAt_of_hasDerivAt_of_ne f_diff hf hg
  ¬∑ exact f_diff y hne
