/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.MeanValue

#align_import analysis.calculus.extend_deriv from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Extending differentiability to the boundary

We investigate how differentiable functions inside a set extend to differentiable functions
on the boundary. For this, it suffices that the function and its derivative admit limits there.
A general version of this statement is given in `has_fderiv_at_boundary_of_tendsto_fderiv`.

One-dimensional versions, in which one wants to obtain differentiability at the left endpoint or
the right endpoint of an interval, are given in
`has_deriv_at_interval_left_endpoint_of_tendsto_deriv` and
`has_deriv_at_interval_right_endpoint_of_tendsto_deriv`. These versions are formulated in terms
of the one-dimensional derivative `deriv â„ f`.
-/


variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

open Filter Set Metric ContinuousLinearMap

open scoped Topology

attribute [local mono] Set.prod_mono

/-- If a function `f` is differentiable in a convex open set and continuous on its closure, and its
derivative converges to a limit `f'` at a point on the boundary, then `f` is differentiable there
with derivative `f'`. -/
theorem has_fderiv_at_boundary_of_tendsto_fderiv {f : E â†’ F} {s : Set E} {x : E} {f' : E â†’L[â„] F}
    (f_diff : DifferentiableOn â„ f s) (s_conv : Convex â„ s) (s_open : IsOpen s)
    (f_cont : âˆ€ y âˆˆ closure s, ContinuousWithinAt f s y)
    (h : Tendsto (fun y => fderiv â„ f y) (ğ“[s] x) (ğ“ f')) :
    HasFDerivWithinAt f f' (closure s) x := by
  classical
    -- one can assume without loss of generality that `x` belongs to the closure of `s`, as the
    -- statement is empty otherwise
    by_cases hx : x âˆ‰ closure s
    Â· rw [â† closure_closure] at hx; exact hasFDerivWithinAt_of_nmem_closure hx
    push_neg at hx
    rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO, Asymptotics.isLittleO_iff]
    /- One needs to show that `â€–f y - f x - f' (y - x)â€– â‰¤ Îµ â€–y - xâ€–` for `y` close to `x` in
      `closure s`, where `Îµ` is an arbitrary positive constant. By continuity of the functions, it
      suffices to prove this for nearby points inside `s`. In a neighborhood of `x`, the derivative
      of `f` is arbitrarily close to `f'` by assumption. The mean value inequality completes the
      proof. -/
    intro Îµ Îµ_pos
    obtain âŸ¨Î´, Î´_pos, hÎ´âŸ© : âˆƒ Î´ > 0, âˆ€ y âˆˆ s, dist y x < Î´ â†’ â€–fderiv â„ f y - f'â€– < Îµ := by
      simpa [dist_zero_right] using tendsto_nhdsWithin_nhds.1 h Îµ Îµ_pos
    set B := ball x Î´
    suffices âˆ€ y âˆˆ B âˆ© closure s, â€–f y - f x - (f' y - f' x)â€– â‰¤ Îµ * â€–y - xâ€– from
      mem_nhdsWithin_iff.2 âŸ¨Î´, Î´_pos, fun y hy => by simpa using this y hyâŸ©
    suffices
      âˆ€ p : E Ã— E,
        p âˆˆ closure ((B âˆ© s) Ã—Ë¢ (B âˆ© s)) â†’ â€–f p.2 - f p.1 - (f' p.2 - f' p.1)â€– â‰¤ Îµ * â€–p.2 - p.1â€– by
      rw [closure_prod_eq] at this
      intro y y_in
      apply this âŸ¨x, yâŸ©
      have : B âˆ© closure s âŠ† closure (B âˆ© s) := isOpen_ball.inter_closure
      exact âŸ¨this âŸ¨mem_ball_self Î´_pos, hxâŸ©, this y_inâŸ©
    have key : âˆ€ p : E Ã— E, p âˆˆ (B âˆ© s) Ã—Ë¢ (B âˆ© s) â†’
          â€–f p.2 - f p.1 - (f' p.2 - f' p.1)â€– â‰¤ Îµ * â€–p.2 - p.1â€– := by
      rintro âŸ¨u, vâŸ© âŸ¨u_in, v_inâŸ©
      have conv : Convex â„ (B âˆ© s) := (convex_ball _ _).inter s_conv
      have diff : DifferentiableOn â„ f (B âˆ© s) := f_diff.mono inter_subset_right
      have bound : âˆ€ z âˆˆ B âˆ© s, â€–fderivWithin â„ f (B âˆ© s) z - f'â€– â‰¤ Îµ := by
        intro z z_in
        have h := hÎ´ z
        have : fderivWithin â„ f (B âˆ© s) z = fderiv â„ f z := by
          have op : IsOpen (B âˆ© s) := isOpen_ball.inter s_open
          rw [DifferentiableAt.fderivWithin _ (op.uniqueDiffOn z z_in)]
          exact (diff z z_in).differentiableAt (IsOpen.mem_nhds op z_in)
        rw [â† this] at h
        exact le_of_lt (h z_in.2 z_in.1)
      simpa using conv.norm_image_sub_le_of_norm_fderivWithin_le' diff bound u_in v_in
    rintro âŸ¨u, vâŸ© uv_in
    have f_cont' : âˆ€ y âˆˆ closure s, ContinuousWithinAt (f -  â‡‘f') s y := by
      intro y y_in
      exact Tendsto.sub (f_cont y y_in) f'.cont.continuousWithinAt
    refine ContinuousWithinAt.closure_le uv_in ?_ ?_ key
    all_goals
      -- common start for both continuity proofs
      have : (B âˆ© s) Ã—Ë¢ (B âˆ© s) âŠ† s Ã—Ë¢ s := by mono <;> exact inter_subset_right
      obtain âŸ¨u_in, v_inâŸ© : u âˆˆ closure s âˆ§ v âˆˆ closure s := by
        simpa [closure_prod_eq] using closure_mono this uv_in
      apply ContinuousWithinAt.mono _ this
      simp only [ContinuousWithinAt]
    Â· rw [nhdsWithin_prod_eq]
      have : âˆ€ u v, f v - f u - (f' v - f' u) = f v - f' v - (f u - f' u) := by intros; abel
      simp only [this]
      exact
        Tendsto.comp continuous_norm.continuousAt
          ((Tendsto.comp (f_cont' v v_in) tendsto_snd).sub <|
            Tendsto.comp (f_cont' u u_in) tendsto_fst)
    Â· apply tendsto_nhdsWithin_of_tendsto_nhds
      rw [nhds_prod_eq]
      exact
        tendsto_const_nhds.mul
          (Tendsto.comp continuous_norm.continuousAt <| tendsto_snd.sub tendsto_fst)
#align has_fderiv_at_boundary_of_tendsto_fderiv has_fderiv_at_boundary_of_tendsto_fderiv

/-- If a function is differentiable on the right of a point `a : â„`, continuous at `a`, and
its derivative also converges at `a`, then `f` is differentiable on the right at `a`. -/
theorem has_deriv_at_interval_left_endpoint_of_tendsto_deriv {s : Set â„} {e : E} {a : â„} {f : â„ â†’ E}
    (f_diff : DifferentiableOn â„ f s) (f_lim : ContinuousWithinAt f s a) (hs : s âˆˆ ğ“[>] a)
    (f_lim' : Tendsto (fun x => deriv f x) (ğ“[>] a) (ğ“ e)) : HasDerivWithinAt f e (Ici a) a := by
  /- This is a specialization of `has_fderiv_at_boundary_of_tendsto_fderiv`. To be in the setting of
    this theorem, we need to work on an open interval with closure contained in `s âˆª {a}`, that we
    call `t = (a, b)`. Then, we check all the assumptions of this theorem and we apply it. -/
  obtain âŸ¨b, ab : a < b, sab : Ioc a b âŠ† sâŸ© := mem_nhdsWithin_Ioi_iff_exists_Ioc_subset.1 hs
  let t := Ioo a b
  have ts : t âŠ† s := Subset.trans Ioo_subset_Ioc_self sab
  have t_diff : DifferentiableOn â„ f t := f_diff.mono ts
  have t_conv : Convex â„ t := convex_Ioo a b
  have t_open : IsOpen t := isOpen_Ioo
  have t_closure : closure t = Icc a b := closure_Ioo ab.ne
  have t_cont : âˆ€ y âˆˆ closure t, ContinuousWithinAt f t y := by
    rw [t_closure]
    intro y hy
    by_cases h : y = a
    Â· rw [h]; exact f_lim.mono ts
    Â· have : y âˆˆ s := sab âŸ¨lt_of_le_of_ne hy.1 (Ne.symm h), hy.2âŸ©
      exact (f_diff.continuousOn y this).mono ts
  have t_diff' : Tendsto (fun x => fderiv â„ f x) (ğ“[t] a) (ğ“ (smulRight (1 : â„ â†’L[â„] â„) e)) := by
    simp only [deriv_fderiv.symm]
    exact Tendsto.comp
      (isBoundedBilinearMap_smulRight : IsBoundedBilinearMap â„ _).continuous_right.continuousAt
      (tendsto_nhdsWithin_mono_left Ioo_subset_Ioi_self f_lim')
  -- now we can apply `has_fderiv_at_boundary_of_differentiable`
  have : HasDerivWithinAt f e (Icc a b) a := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt, â† t_closure]
    exact has_fderiv_at_boundary_of_tendsto_fderiv t_diff t_conv t_open t_cont t_diff'
  exact this.mono_of_mem (Icc_mem_nhdsWithin_Ici <| left_mem_Ico.2 ab)
#align has_deriv_at_interval_left_endpoint_of_tendsto_deriv has_deriv_at_interval_left_endpoint_of_tendsto_deriv

/-- If a function is differentiable on the left of a point `a : â„`, continuous at `a`, and
its derivative also converges at `a`, then `f` is differentiable on the left at `a`. -/
theorem has_deriv_at_interval_right_endpoint_of_tendsto_deriv {s : Set â„} {e : E} {a : â„}
    {f : â„ â†’ E} (f_diff : DifferentiableOn â„ f s) (f_lim : ContinuousWithinAt f s a)
    (hs : s âˆˆ ğ“[<] a) (f_lim' : Tendsto (fun x => deriv f x) (ğ“[<] a) (ğ“ e)) :
    HasDerivWithinAt f e (Iic a) a := by
  /- This is a specialization of `has_fderiv_at_boundary_of_differentiable`. To be in the setting of
    this theorem, we need to work on an open interval with closure contained in `s âˆª {a}`, that we
    call `t = (b, a)`. Then, we check all the assumptions of this theorem and we apply it. -/
  obtain âŸ¨b, ba, sabâŸ© : âˆƒ b âˆˆ Iio a, Ico b a âŠ† s := mem_nhdsWithin_Iio_iff_exists_Ico_subset.1 hs
  let t := Ioo b a
  have ts : t âŠ† s := Subset.trans Ioo_subset_Ico_self sab
  have t_diff : DifferentiableOn â„ f t := f_diff.mono ts
  have t_conv : Convex â„ t := convex_Ioo b a
  have t_open : IsOpen t := isOpen_Ioo
  have t_closure : closure t = Icc b a := closure_Ioo (ne_of_lt ba)
  have t_cont : âˆ€ y âˆˆ closure t, ContinuousWithinAt f t y := by
    rw [t_closure]
    intro y hy
    by_cases h : y = a
    Â· rw [h]; exact f_lim.mono ts
    Â· have : y âˆˆ s := sab âŸ¨hy.1, lt_of_le_of_ne hy.2 hâŸ©
      exact (f_diff.continuousOn y this).mono ts
  have t_diff' : Tendsto (fun x => fderiv â„ f x) (ğ“[t] a) (ğ“ (smulRight (1 : â„ â†’L[â„] â„) e)) := by
    simp only [deriv_fderiv.symm]
    exact Tendsto.comp
      (isBoundedBilinearMap_smulRight : IsBoundedBilinearMap â„ _).continuous_right.continuousAt
      (tendsto_nhdsWithin_mono_left Ioo_subset_Iio_self f_lim')
  -- now we can apply `has_fderiv_at_boundary_of_differentiable`
  have : HasDerivWithinAt f e (Icc b a) a := by
    rw [hasDerivWithinAt_iff_hasFDerivWithinAt, â† t_closure]
    exact has_fderiv_at_boundary_of_tendsto_fderiv t_diff t_conv t_open t_cont t_diff'
  exact this.mono_of_mem (Icc_mem_nhdsWithin_Iic <| right_mem_Ioc.2 ba)
#align has_deriv_at_interval_right_endpoint_of_tendsto_deriv has_deriv_at_interval_right_endpoint_of_tendsto_deriv

/-- If a real function `f` has a derivative `g` everywhere but at a point, and `f` and `g` are
continuous at this point, then `g` is also the derivative of `f` at this point. -/
theorem hasDerivAt_of_hasDerivAt_of_ne {f g : â„ â†’ E} {x : â„}
    (f_diff : âˆ€ y â‰  x, HasDerivAt f (g y) y) (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) : HasDerivAt f (g x) x := by
  have A : HasDerivWithinAt f (g x) (Ici x) x := by
    have diff : DifferentiableOn â„ f (Ioi x) := fun y hy =>
      (f_diff y (ne_of_gt hy)).differentiableAt.differentiableWithinAt
    -- next line is the nontrivial bit of this proof, appealing to differentiability
    -- extension results.
    apply
      has_deriv_at_interval_left_endpoint_of_tendsto_deriv diff hf.continuousWithinAt
        self_mem_nhdsWithin
    have : Tendsto g (ğ“[>] x) (ğ“ (g x)) := tendsto_inf_left hg
    apply this.congr' _
    apply mem_of_superset self_mem_nhdsWithin fun y hy => _
    intros y hy
    exact (f_diff y (ne_of_gt hy)).deriv.symm
  have B : HasDerivWithinAt f (g x) (Iic x) x := by
    have diff : DifferentiableOn â„ f (Iio x) := fun y hy =>
      (f_diff y (ne_of_lt hy)).differentiableAt.differentiableWithinAt
    -- next line is the nontrivial bit of this proof, appealing to differentiability
    -- extension results.
    apply
      has_deriv_at_interval_right_endpoint_of_tendsto_deriv diff hf.continuousWithinAt
        self_mem_nhdsWithin
    have : Tendsto g (ğ“[<] x) (ğ“ (g x)) := tendsto_inf_left hg
    apply this.congr' _
    apply mem_of_superset self_mem_nhdsWithin fun y hy => _
    intros y hy
    exact (f_diff y (ne_of_lt hy)).deriv.symm
  simpa using B.union A
#align has_deriv_at_of_has_deriv_at_of_ne hasDerivAt_of_hasDerivAt_of_ne

/-- If a real function `f` has a derivative `g` everywhere but at a point, and `f` and `g` are
continuous at this point, then `g` is the derivative of `f` everywhere. -/
theorem hasDerivAt_of_hasDerivAt_of_ne' {f g : â„ â†’ E} {x : â„}
    (f_diff : âˆ€ y â‰  x, HasDerivAt f (g y) y) (hf : ContinuousAt f x)
    (hg : ContinuousAt g x) (y : â„) : HasDerivAt f (g y) y := by
  rcases eq_or_ne y x with (rfl | hne)
  Â· exact hasDerivAt_of_hasDerivAt_of_ne f_diff hf hg
  Â· exact f_diff y hne
#align has_deriv_at_of_has_deriv_at_of_ne' hasDerivAt_of_hasDerivAt_of_ne'
