/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Prod
import Mathlib.Analysis.Calculus.FDeriv.Star
import Mathlib.Analysis.Matrix
import Mathlib.Data.Matrix.Bilinear
import Mathlib.Data.Matrix.Kronecker
import Mathlib.Data.Matrix.Hadamard
import Mathlib.Topology.UniformSpace.Matrix

/-!
# FrÃ©chet derivatives of Matrices

This file proves results about `fderiv` and matrix operations.
-/

variable {Î¹ : Type*} {ğ•œ : Type*} {E F : Type*} {A : Type*} {m n p q : Type*}

variable [Fintype m] [Fintype n] [Fintype p] [Fintype q]

open scoped Kronecker
open Matrix

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

section elementwise

theorem hasFDerivAtFilter_matrix
    {M : E â†’ Matrix m n F} {x : E} {M' : E â†’L[ğ•œ] Matrix m n F} {L : Filter E} :
    HasFDerivAtFilter M M' x L â†” âˆ€ i j,
      HasFDerivAtFilter (fun x => M x i j)
        (âŸ¨Matrix.entryLinearMap ğ•œ F i j, continuous_id.matrix_elem i jâŸ© âˆ˜L M') x L := by
  erw [hasFDerivAtFilter_pi']
  simp_rw [hasFDerivAtFilter_pi', â† ContinuousLinearMap.comp_assoc]
  rfl

theorem hasFDerivWithinAt_matrix
    {M : E â†’ Matrix m n F} {s : Set E} {x : E} {M' : E â†’L[ğ•œ] Matrix m n F} :
    HasFDerivWithinAt M M' s x â†” âˆ€ i j,
      HasFDerivWithinAt (fun x => M x i j)
        (âŸ¨Matrix.entryLinearMap ğ•œ F i j, continuous_id.matrix_elem i jâŸ© âˆ˜L M') s x :=
  hasFDerivAtFilter_matrix

theorem hasFDerivAt_matrix {M : E â†’ Matrix m n F} {x : E} {M' : E â†’L[ğ•œ] Matrix m n F} :
    HasFDerivAt M M' x â†” âˆ€ i j,
      HasFDerivAt (fun x => M x i j)
        (âŸ¨Matrix.entryLinearMap ğ•œ F i j, continuous_id.matrix_elem i jâŸ© âˆ˜L M') x :=
  hasFDerivAtFilter_matrix

theorem differentiableWithinAt_matrix {M : E â†’ Matrix m n F} {s : Set E} {x : E} :
    DifferentiableWithinAt ğ•œ M s x â†” âˆ€ i j, DifferentiableWithinAt ğ•œ (fun x => M x i j) s x := by
  erw [differentiableWithinAt_pi]
  simp_rw [differentiableWithinAt_pi]

theorem differentiableAt_matrix {M : E â†’ Matrix m n F} {x : E} :
    DifferentiableAt ğ•œ M x â†” âˆ€ i j, DifferentiableAt ğ•œ (fun x => M x i j) x := by
  erw [differentiableAt_pi]
  simp_rw [differentiableAt_pi]

theorem differentiableOn_matrix {M : E â†’ Matrix m n F} {s : Set E} :
    DifferentiableOn ğ•œ M s â†” âˆ€ i j, DifferentiableOn ğ•œ (fun x => M x i j) s := by
  erw [differentiableOn_pi]
  simp_rw [differentiableOn_pi]

theorem differentiable_matrix {M : E â†’ Matrix m n F} :
    Differentiable ğ•œ M â†” âˆ€ i j, Differentiable ğ•œ (fun x => M x i j) := by
  erw [differentiable_pi]
  simp_rw [differentiable_pi]

end elementwise

/-- `HasFDerivAt.mul` for rectangular matrices. -/
theorem HasFDerivAt.matrix_mul [NormedRing A] [NormedAlgebra ğ•œ A]
    {M : E â†’ Matrix m n A} {N : E â†’ Matrix n p A}
    {M' : E â†’L[ğ•œ] Matrix m n A} {N' : E â†’L[ğ•œ] Matrix n p A} {x : E}
    (hM : HasFDerivAt M M' x) (hN : HasFDerivAt N N' x) :
    HasFDerivAt (fun a => M a * N a)
      (âŸ¨mulLeftLinearMap p ğ•œ (M x), continuous_const.matrix_mul continuous_idâŸ© âˆ˜L N' +
      âŸ¨mulRightLinearMap m ğ•œ (N x), continuous_id.matrix_mul continuous_constâŸ© âˆ˜L M') x := by
  rw [hasFDerivAt_matrix] at hM hN âŠ¢
  intro i j
  simp only [mul_apply, Matrix.add_apply, â† Finset.sum_add_distrib]
  refine (HasFDerivAt.sum fun k _hk => (hM _ _).mul' (hN _ _)).congr_fderiv ?_
  ext x
  simp [Finset.sum_add_distrib, Matrix.mul_apply]

section trace

theorem HasFDerivAtFilter.matrix_trace
    {M : E â†’ Matrix m m F} {x : E} {M' : E â†’L[ğ•œ] Matrix m m F} {L : Filter E}
    (hM : HasFDerivAtFilter M M' x L) :
    HasFDerivAtFilter (fun a => (M a).trace)
      (âŸ¨Matrix.traceLinearMap _ _ _, continuous_id.matrix_traceâŸ© âˆ˜L M') x L := by
  have := (HasFDerivAtFilter.sum fun i (hi : i âˆˆ Finset.univ) =>
    (hasFDerivAtFilter_matrix.mp hM : _) i i)
  convert this using 1
  ext
  simp [Matrix.trace]

theorem HasFDerivWithinAt.matrix_trace
    {M : E â†’ Matrix m m F} {s : Set E} {x : E} {M' : E â†’L[ğ•œ] Matrix m m F}
    (hM : HasFDerivWithinAt M M' s x) :
    HasFDerivWithinAt (fun a => (M a).trace)
      (âŸ¨Matrix.traceLinearMap _ _ _, continuous_id.matrix_traceâŸ© âˆ˜L M') s x :=
  HasFDerivAtFilter.matrix_trace hM

theorem HasFDerivAt.matrix_trace {M : E â†’ Matrix m m F} {x : E} {M' : E â†’L[ğ•œ] Matrix m m F}
    (hM : HasFDerivAt M M' x) :
    HasFDerivAt (fun a => (M a).trace)
      (âŸ¨Matrix.traceLinearMap _ _ _, continuous_id.matrix_traceâŸ© âˆ˜L M') x :=
  HasFDerivAtFilter.matrix_trace hM

theorem DifferentiableWithinAt.matrix_trace {M : E â†’ Matrix m m F} {s : Set E} {x : E}
    (hM : DifferentiableWithinAt ğ•œ M s x) :
    DifferentiableWithinAt ğ•œ (fun a => (M a).trace) s x :=
  let âŸ¨_, hMâŸ© := hM; hM.matrix_trace.differentiableWithinAt

theorem DifferentiableOn.matrix_trace {M : E â†’ Matrix m m F} {s : Set E}
    (hM : DifferentiableOn ğ•œ M s) :
    DifferentiableOn ğ•œ (fun a => (M a).trace) s :=
  fun x hx => (hM x hx).matrix_trace

theorem DifferentiableAt.matrix_trace {M : E â†’ Matrix m m F} {x : E}
    (hM : DifferentiableAt ğ•œ M x) :
    DifferentiableAt ğ•œ (fun a => (M a).trace) x :=
  let âŸ¨_, hMâŸ© := hM; hM.matrix_trace.differentiableAt

theorem Differentiable.matrix_trace {M : E â†’ Matrix m m F} (hM : Differentiable ğ•œ M) :
    Differentiable ğ•œ (fun a => (M a).trace) :=
  fun x => (hM x).matrix_trace

end trace

section transpose

theorem HasFDerivAtFilter.matrix_transpose
    {M : E â†’ Matrix m n F} {M' : E â†’L[ğ•œ] Matrix m n F} {x : E} {L : Filter E}
    (hM : HasFDerivAtFilter M M' x L) :
    HasFDerivAtFilter (fun a => (M a)áµ€)
      (âŸ¨Matrix.transposeLinearEquiv m n ğ•œ F, continuous_id.matrix_transposeâŸ© âˆ˜L M') x L :=
  hasFDerivAtFilter_matrix.mpr fun i j => (hasFDerivAtFilter_matrix.mp hM : _) j i

theorem HasFDerivWithinAt.matrix_transpose
    {M : E â†’ Matrix m n F} {M' : E â†’L[ğ•œ] Matrix m n F} {s : Set E} {x : E}
    (hM : HasFDerivWithinAt M M' s x) :
    HasFDerivWithinAt (fun a => (M a)áµ€)
      (âŸ¨Matrix.transposeLinearEquiv m n ğ•œ F, continuous_id.matrix_transposeâŸ© âˆ˜L M') s x :=
  HasFDerivAtFilter.matrix_transpose hM

theorem HasFDerivAt.matrix_transpose
    {M : E â†’ Matrix m n F} {M' : E â†’L[ğ•œ] Matrix m n F} {x : E} (hM : HasFDerivAt M M' x) :
    HasFDerivAt (fun a => (M a)áµ€)
      (âŸ¨Matrix.transposeLinearEquiv m n ğ•œ F, continuous_id.matrix_transposeâŸ© âˆ˜L M') x :=
  HasFDerivAtFilter.matrix_transpose hM

theorem DifferentiableWithinAt.matrix_transpose {M : E â†’ Matrix m n F} {s : Set E} {x : E}
    (hM : DifferentiableWithinAt ğ•œ M s x) :
    DifferentiableWithinAt ğ•œ (fun a => (M a)áµ€) s x :=
  let âŸ¨_, hMâŸ© := hM; let aux := hM.matrix_transpose
  by convert aux.differentiableWithinAt

theorem DifferentiableOn.matrix_transpose {M : E â†’ Matrix m n F} {s : Set E}
    (hM : DifferentiableOn ğ•œ M s) :
    DifferentiableOn ğ•œ (fun a => (M a)áµ€) s :=
  fun x hx => (hM x hx).matrix_transpose

theorem DifferentiableAt.matrix_transpose {M : E â†’ Matrix m n F} {x : E}
    (hM : DifferentiableAt ğ•œ M x) :
    DifferentiableAt ğ•œ (fun a => (M a)áµ€) x :=
  let âŸ¨_, hMâŸ© := hM; hM.matrix_transpose.differentiableAt

theorem Differentiable.matrix_transpose {M : E â†’ Matrix m n F} (hM : Differentiable ğ•œ M) :
    Differentiable ğ•œ (fun a => (M a)áµ€) :=
  fun x => (hM x).matrix_transpose

end transpose

section conjTranspose
variable [StarRing ğ•œ] [TrivialStar ğ•œ] [StarAddMonoid F] [StarModule ğ•œ F] [ContinuousStar F]

theorem HasFDerivAtFilter.matrix_conjTranspose
    {M : E â†’ Matrix m n F} {M' : E â†’L[ğ•œ] Matrix m n F} {x : E} {L : Filter E}
    (hM : HasFDerivAtFilter M M' x L) :
    HasFDerivAtFilter (fun a => (M a)á´´)
      (âŸ¨{ Matrix.conjTransposeLinearEquiv m n ğ•œ F with map_smul' := by simp},
          continuous_id.matrix_conjTransposeâŸ© âˆ˜L M') x L :=
  (hasFDerivAtFilter_matrix.mpr fun i j => ((hasFDerivAtFilter_matrix.mp hM : _) j i).star)

theorem HasFDerivAt.matrix_conjTranspose
    {M : E â†’ Matrix m n F} {M' : E â†’L[ğ•œ] Matrix m n F} {x : E} (hM : HasFDerivAt M M' x) :
    HasFDerivAt (fun a => (M a)á´´)
      (âŸ¨{ Matrix.conjTransposeLinearEquiv m n ğ•œ F with map_smul' := by simp},
          continuous_id.matrix_conjTransposeâŸ© âˆ˜L M') x :=
  HasFDerivAtFilter.matrix_conjTranspose hM

end conjTranspose
