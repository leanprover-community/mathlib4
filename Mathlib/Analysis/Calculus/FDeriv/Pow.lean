/-
Copyright (c) 2025 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.FDeriv.Comp

/-!
# FrÃ©chet Derivative of `f x ^ n`, `n : â„•`

In this file we prove that the FrÃ©chet derivative of `fun x => f x ^ n`,
where `n` is a natural number, is `n â€¢ f x ^ (n - 1)) â€¢ f'`.
Additionally, we prove the case for non-commutative rings (with primed names like `fderiv_pow'`),
where the result is instead `âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i`.

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.

## Keywords

derivative, power
-/

variable {ğ•œ ğ”¸ E : Type*}

section NormedRing
variable [NontriviallyNormedField ğ•œ] [NormedRing ğ”¸] [NormedAddCommGroup E]
variable [NormedAlgebra ğ•œ ğ”¸] [NormedSpace ğ•œ E] {f : E â†’ ğ”¸} {f' : E â†’L[ğ•œ] ğ”¸} {x : E} {s : Set E}

open scoped RightActions

private theorem aux (f : E â†’ ğ”¸) (f' : E â†’L[ğ•œ] ğ”¸) (x : E) (n : â„•) :
    f x â€¢> âˆ‘ i âˆˆ Finset.range (n + 1), f x ^ ((n + 1).pred - i) â€¢> f' <â€¢ f x ^ i
      + f' <â€¢ (f x ^ (n + 1)) =
    âˆ‘ i âˆˆ Finset.range (n + 1 + 1), f x ^ ((n + 1 + 1).pred - i) â€¢> f' <â€¢ f x ^ i := by
  rw [Finset.sum_range_succ _ (n + 1), Finset.smul_sum]
  simp only [Nat.pred_eq_sub_one, add_tsub_cancel_right, tsub_self, pow_zero, one_smul]
  simp_rw [smul_comm (_ : ğ”¸) (_ : ğ”¸áµáµ’áµ–), smul_smul, â† pow_succ']
  congr! 5 with x hx
  simp only [Finset.mem_range, Nat.lt_succ_iff] at hx
  rw [tsub_add_eq_add_tsub hx]

theorem HasStrictFDerivAt.fun_pow' (h : HasStrictFDerivAt f f' x) (n : â„•) :
    HasStrictFDerivAt (fun x â†¦ f x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) x :=
  match n with
  | 0 => by simpa using hasStrictFDerivAt_const 1 x
  | 1 => by simpa using h
  | n + 1 + 1 => by
    have := h.mul' (h.fun_pow' (n + 1))
    simp_rw [pow_succ' _ (n + 1)]
    refine this.congr_fderiv <| aux _ _ _ _

theorem HasStrictFDerivAt.pow' (h : HasStrictFDerivAt f f' x) (n : â„•) :
    HasStrictFDerivAt (f ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) x := h.fun_pow' n

theorem hasStrictFDerivAt_pow' (n : â„•) {x : ğ”¸} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, x ^ (n.pred - i) â€¢> ContinuousLinearMap.id ğ•œ _ <â€¢ x ^ i) x :=
  hasStrictFDerivAt_id _ |>.pow' n

theorem HasFDerivWithinAt.fun_pow' (h : HasFDerivWithinAt f f' s x) (n : â„•) :
    HasFDerivWithinAt (fun x â†¦ f x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) s x :=
  match n with
  | 0 => by simpa using hasFDerivWithinAt_const 1 x s
  | 1 => by simpa using h
  | n + 1 + 1 => by
    have := h.mul' (h.fun_pow' (n + 1))
    simp_rw [pow_succ' _ (n + 1)]
    exact this.congr_fderiv <| aux _ _ _ _

theorem HasFDerivWithinAt.pow' (h : HasFDerivWithinAt f f' s x) (n : â„•) :
    HasFDerivWithinAt (f ^ n)
      (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) s x := h.fun_pow' n

theorem hasFDerivWithinAt_pow' (n : â„•) {x : ğ”¸} {s : Set ğ”¸} :
    HasFDerivWithinAt (ğ•œ := ğ•œ) (fun x â†¦ x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, x ^ (n.pred - i) â€¢> ContinuousLinearMap.id ğ•œ _ <â€¢ x ^ i) s x :=
  hasFDerivWithinAt_id _ _ |>.pow' n

theorem HasFDerivAt.fun_pow' (h : HasFDerivAt f f' x) (n : â„•) :
    HasFDerivAt (fun x â†¦ f x ^ n) (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) x :=
  match n with
  | 0 => by simpa using hasFDerivAt_const 1 x
  | 1 => by simpa using h
  | n + 1 + 1 => by
    have := h.mul' (h.fun_pow' (n + 1))
    simp_rw [pow_succ' _ (n + 1)]
    exact this.congr_fderiv <| aux _ _ _ _

theorem HasFDerivAt.pow' (h : HasFDerivAt f f' x) (n : â„•) :
    HasFDerivAt (f ^ n) (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> f' <â€¢ f x ^ i) x :=
  h.fun_pow' n

theorem hasFDerivAt_pow' (n : â„•) {x : ğ”¸} :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ x ^ n)
      (âˆ‘ i âˆˆ Finset.range n, x ^ (n.pred - i) â€¢> ContinuousLinearMap.id ğ•œ _ <â€¢ x ^ i) x :=
  hasFDerivAt_id _ |>.pow' n

@[fun_prop]
theorem DifferentiableWithinAt.fun_pow (hf : DifferentiableWithinAt ğ•œ f s x) (n : â„•) :
    DifferentiableWithinAt ğ•œ (fun x => f x ^ n) s x :=
  let âŸ¨_, hf'âŸ© := hf; âŸ¨_, hf'.pow' nâŸ©

@[fun_prop]
theorem DifferentiableWithinAt.pow (hf : DifferentiableWithinAt ğ•œ f s x) :
    âˆ€ n : â„•, DifferentiableWithinAt ğ•œ (f ^ n) s x :=
  hf.fun_pow

theorem differentiableWithinAt_pow (n : â„•) {x : ğ”¸} {s : Set ğ”¸} :
    DifferentiableWithinAt ğ•œ (fun x : ğ”¸ => x ^ n) s x :=
  differentiableWithinAt_id.pow _

@[simp, fun_prop]
theorem DifferentiableAt.fun_pow (hf : DifferentiableAt ğ•œ f x) (n : â„•) :
    DifferentiableAt ğ•œ (fun x => f x ^ n) x :=
  differentiableWithinAt_univ.mp <| hf.differentiableWithinAt.pow n

@[simp, fun_prop]
theorem DifferentiableAt.pow (hf : DifferentiableAt ğ•œ f x) (n : â„•) :
    DifferentiableAt ğ•œ (f ^ n) x := hf.fun_pow n

theorem differentiableAt_pow (n : â„•) {x : ğ”¸} : DifferentiableAt ğ•œ (fun x : ğ”¸ => x ^ n) x :=
  differentiableAt_id.pow _

@[fun_prop]
theorem DifferentiableOn.fun_pow (hf : DifferentiableOn ğ•œ f s) (n : â„•) :
    DifferentiableOn ğ•œ (fun x => f x ^ n) s := fun x h => (hf x h).pow n

@[fun_prop]
theorem DifferentiableOn.pow (hf : DifferentiableOn ğ•œ f s) (n : â„•) :
    DifferentiableOn ğ•œ (f ^ n) s := hf.fun_pow n

theorem differentiableOn_pow (n : â„•) {s : Set ğ”¸} : DifferentiableOn ğ•œ (fun x : ğ”¸ => x ^ n) s :=
  differentiableOn_id.pow n

@[simp, fun_prop]
theorem Differentiable.fun_pow (hf : Differentiable ğ•œ f) (n : â„•) :
    Differentiable ğ•œ fun x => f x ^ n :=
  fun x => (hf x).pow n

@[simp, fun_prop]
theorem Differentiable.pow (hf : Differentiable ğ•œ f) (n : â„•) : Differentiable ğ•œ (f ^ n) :=
  hf.fun_pow n

theorem differentiable_pow (n : â„•) : Differentiable ğ•œ fun x : ğ”¸ => x ^ n :=
  differentiable_id.pow _

theorem fderiv_fun_pow' (n : â„•) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun x â†¦ f x ^ n) x
      = (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> fderiv ğ•œ f x <â€¢ f x ^ i) :=
  hf.hasFDerivAt.pow' n |>.fderiv

theorem fderiv_pow' (n : â„•) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (f ^ n) x
      = (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> fderiv ğ•œ f x <â€¢ f x ^ i) :=
  fderiv_fun_pow' n hf

theorem fderiv_pow_ring' {x : ğ”¸} (n : â„•) :
    fderiv ğ•œ (fun x : ğ”¸ â†¦ x ^ n) x
      = (âˆ‘ i âˆˆ Finset.range n, x ^ (n.pred - i) â€¢> .id _ _ <â€¢ x ^ i) := by
  rw [fderiv_fun_pow' n differentiableAt_fun_id, fderiv_id']

theorem fderivWithin_fun_pow' (hxs : UniqueDiffWithinAt ğ•œ s x)
    (n : â„•) (hf : DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ (fun x â†¦ f x ^ n) s x
      = (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> fderivWithin ğ•œ f s x <â€¢ f x ^ i) :=
  hf.hasFDerivWithinAt.pow' n |>.fderivWithin hxs

theorem fderivWithin_pow' (hxs : UniqueDiffWithinAt ğ•œ s x)
    (n : â„•) (hf : DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ (f ^ n) s x
      = (âˆ‘ i âˆˆ Finset.range n, f x ^ (n.pred - i) â€¢> fderivWithin ğ•œ f s x <â€¢ f x ^ i) :=
  fderivWithin_fun_pow' hxs n hf

theorem fderivWithin_pow_ring' {s : Set ğ”¸} {x : ğ”¸} (n : â„•) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x : ğ”¸ â†¦ x ^ n) s x
      = (âˆ‘ i âˆˆ Finset.range n, x ^ (n.pred - i) â€¢> .id _ _ <â€¢ x ^ i) := by
  rw [fderivWithin_fun_pow' hxs n differentiableAt_fun_id.differentiableWithinAt,
    fderivWithin_id' hxs]

end NormedRing

section NormedCommRing
variable [NontriviallyNormedField ğ•œ] [NormedCommRing ğ”¸] [NormedAddCommGroup E]
variable [NormedAlgebra ğ•œ ğ”¸] [NormedSpace ğ•œ E] {f : E â†’ ğ”¸} {f' : E â†’L[ğ•œ] ğ”¸} {x : E} {s : Set E}

private theorem aux_sum_eq_pow (n : â„•) :
    âˆ‘ i âˆˆ Finset.range n, MulOpposite.op (f x ^ i) â€¢ f x ^ (n.pred - i) â€¢ f' =
      (n â€¢ f x ^ (n - 1)) â€¢ f' := by
  simp_rw [op_smul_eq_smul, smul_smul, â† pow_add, â† Finset.sum_smul]
  rw [Finset.sum_eq_card_nsmul, Finset.card_range, smul_assoc]
  intro a ha
  congr
  exact add_tsub_cancel_of_le (Nat.le_pred_of_lt <| Finset.mem_range.1 ha)

theorem HasStrictFDerivAt.pow (h : HasStrictFDerivAt f f' x) (n : â„•) :
    HasStrictFDerivAt (fun x â†¦ f x ^ n) ((n â€¢ f x ^ (n - 1)) â€¢ f') x :=
  h.pow' n |>.congr_fderiv <| aux_sum_eq_pow _

theorem hasStrictFDerivAt_pow (n : â„•) {x : ğ”¸} :
    HasStrictFDerivAt (ğ•œ := ğ•œ)
      (fun x : ğ”¸ â†¦ x ^ n) ((n â€¢ x ^ (n - 1)) â€¢ ContinuousLinearMap.id ğ•œ ğ”¸) x :=
  hasStrictFDerivAt_id _ |>.pow n

theorem HasFDerivWithinAt.pow (h : HasFDerivWithinAt f f' s x) (n : â„•) :
    HasFDerivWithinAt (fun x â†¦ f x ^ n) ((n â€¢ f x ^ (n - 1)) â€¢ f') s x :=
  h.pow' n |>.congr_fderiv <| aux_sum_eq_pow _

theorem hasFDerivWithinAt_pow (n : â„•) {x : ğ”¸} {s : Set ğ”¸} :
    HasFDerivWithinAt (ğ•œ := ğ•œ)
      (fun x : ğ”¸ â†¦ x ^ n) ((n â€¢ x ^ (n - 1)) â€¢ ContinuousLinearMap.id ğ•œ ğ”¸) s x :=
  hasFDerivWithinAt_id _ _ |>.pow n

theorem HasFDerivAt.pow (h : HasFDerivAt f f' x) (n : â„•) :
    HasFDerivAt (fun x â†¦ f x ^ n) ((n â€¢ f x ^ (n - 1)) â€¢ f') x :=
  h.pow' n |>.congr_fderiv <| aux_sum_eq_pow _

theorem hasFDerivAt_pow (n : â„•) {x : ğ”¸} :
    HasFDerivAt (ğ•œ := ğ•œ)
      (fun x : ğ”¸ â†¦ x ^ n) ((n â€¢ x ^ (n - 1)) â€¢ ContinuousLinearMap.id ğ•œ ğ”¸) x :=
  hasFDerivAt_id _ |>.pow n

theorem fderiv_fun_pow (n : â„•) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun x â†¦ f x ^ n) x = (n â€¢ f x ^ (n - 1)) â€¢ fderiv ğ•œ f x :=
  hf.hasFDerivAt.pow n |>.fderiv

theorem fderiv_pow (n : â„•) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun x â†¦ f x ^ n) x = (n â€¢ f x ^ (n - 1)) â€¢ fderiv ğ•œ f x :=
  fderiv_fun_pow n hf

theorem fderiv_pow_ring {x : ğ”¸} (n : â„•) :
    fderiv ğ•œ (fun x : ğ”¸ â†¦ x ^ n) x = (n â€¢ x ^ (n - 1)) â€¢ .id _ _ := by
  rw [fderiv_fun_pow n differentiableAt_fun_id, fderiv_id']

theorem fderivWithin_fun_pow (hxs : UniqueDiffWithinAt ğ•œ s x)
    (n : â„•) (hf : DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ (fun x â†¦ f x ^ n) s x = (n â€¢ f x ^ (n - 1)) â€¢ fderivWithin ğ•œ f s x :=
  hf.hasFDerivWithinAt.pow n |>.fderivWithin hxs

theorem fderivWithin_pow (hxs : UniqueDiffWithinAt ğ•œ s x)
    (n : â„•) (hf : DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ (f ^ n) s x = (n â€¢ f x ^ (n - 1)) â€¢ fderivWithin ğ•œ f s x :=
  fderivWithin_fun_pow hxs n hf

theorem fderivWithin_pow_ring {s : Set ğ”¸} {x : ğ”¸} (n : â„•) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x : ğ”¸ â†¦ x ^ n) s x = (n â€¢ x ^ (n - 1)) â€¢ .id _ _ := by
  rw [fderivWithin_fun_pow hxs n differentiableAt_fun_id.differentiableWithinAt,
    fderivWithin_id' hxs]

end NormedCommRing
