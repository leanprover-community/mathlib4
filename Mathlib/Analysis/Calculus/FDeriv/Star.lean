/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.CStarAlgebra.Basic
import Mathlib.Topology.Algebra.Module.Star

/-!
# Star operations on derivatives

This file contains the usual formulas (and existence assertions) for the FrÃ©chet derivative of the
star operation. For detailed documentation of the FrÃ©chet derivative, see the module docstring of
`Analysis/Calculus/FDeriv/Basic.lean`.

Most of the results in this file only apply when the field that the derivative is respect to has a
trivial star operation; which as should be expected rules out `ğ•œ = â„‚`. The exceptions are
`HasFDerivAt.star_star` and `DifferentiableAt.star_star`, showing that `star âˆ˜ f âˆ˜ star` is
differentiable when `f` is (and giving a formula for its derivative).
-/


variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [StarRing ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [StarAddMonoid F] [NormedSpace ğ•œ F] [StarModule ğ•œ F]
  [ContinuousStar F]

variable {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {x : E} {s : Set E} {L : Filter E}

section TrivialStar

variable [TrivialStar ğ•œ]

@[fun_prop]
protected theorem HasStrictFDerivAt.star (h : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).toContinuousLinearMap.hasStrictFDerivAt.comp x h

protected theorem HasFDerivAtFilter.star (h : HasFDerivAtFilter f f' x L) :
    HasFDerivAtFilter (fun x => star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x L :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).toContinuousLinearMap.hasFDerivAtFilter.comp x h Filter.tendsto_map

@[fun_prop]
protected nonrec theorem HasFDerivWithinAt.star (h : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun x => star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') s x :=
  h.star

@[fun_prop]
protected nonrec theorem HasFDerivAt.star (h : HasFDerivAt f f' x) :
    HasFDerivAt (fun x => star (f x)) (((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L f') x :=
  h.star

@[fun_prop]
protected theorem DifferentiableWithinAt.star (h : DifferentiableWithinAt ğ•œ f s x) :
    DifferentiableWithinAt ğ•œ (fun y => star (f y)) s x :=
  h.hasFDerivWithinAt.star.differentiableWithinAt

@[simp]
theorem differentiableWithinAt_star_iff :
    DifferentiableWithinAt ğ•œ (fun y => star (f y)) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiableWithinAt_iff

@[fun_prop]
protected theorem DifferentiableAt.star (h : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (fun y => star (f y)) x :=
  h.hasFDerivAt.star.differentiableAt

@[simp]
theorem differentiableAt_star_iff :
    DifferentiableAt ğ•œ (fun y => star (f y)) x â†” DifferentiableAt ğ•œ f x :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiableAt_iff

@[fun_prop]
protected theorem DifferentiableOn.star (h : DifferentiableOn ğ•œ f s) :
    DifferentiableOn ğ•œ (fun y => star (f y)) s := fun x hx => (h x hx).star

@[simp]
theorem differentiableOn_star_iff :
    DifferentiableOn ğ•œ (fun y => star (f y)) s â†” DifferentiableOn ğ•œ f s :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiableOn_iff

@[fun_prop]
protected theorem Differentiable.star (h : Differentiable ğ•œ f) :
    Differentiable ğ•œ fun y => star (f y) :=
  fun x => (h x).star

@[simp]
theorem differentiable_star_iff : (Differentiable ğ•œ fun y => star (f y)) â†” Differentiable ğ•œ f :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_differentiable_iff

theorem fderivWithin_star (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun y => star (f y)) s x =
      ((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L fderivWithin ğ•œ f s x :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_fderivWithin hxs

@[simp]
theorem fderiv_star :
    fderiv ğ•œ (fun y => star (f y)) x = ((starL' ğ•œ : F â‰ƒL[ğ•œ] F) : F â†’L[ğ•œ] F) âˆ˜L fderiv ğ•œ f x :=
  (starL' ğ•œ : F â‰ƒL[ğ•œ] F).comp_fderiv

end TrivialStar

section NontrivialStar

/-!
## Composing on the left and right with `star`
-/

variable [StarAddMonoid E] [StarModule ğ•œ E] [ContinuousStar E] [NormedStarGroup ğ•œ]

/-- If `f` has derivative `f'` at `z`, then `star âˆ˜ f âˆ˜ star` has derivative `starL âˆ˜ f' âˆ˜ starL`
at `star z`. -/
@[fun_prop]
lemma HasFDerivAt.star_star {f : E â†’ F} {z : E} {f' : E â†’L[ğ•œ] F} (hf : HasFDerivAt f f' z) :
    HasFDerivAt (star âˆ˜ f âˆ˜ star)
      ((starL ğ•œ).toContinuousLinearMap.comp <| f'.comp (starL ğ•œ).toContinuousLinearMap) (star z) :=
  .comp_semilinear (starL ğ•œ).toContinuousLinearMap (starL ğ•œ).toContinuousLinearMap
    (by simpa using hf)

/-- If `f` is differentiable at `z`, then `star âˆ˜ f âˆ˜ star` is differentiable at `star z`. -/
@[fun_prop]
lemma DifferentiableAt.star_star {f : E â†’ F} {z : E} (hf : DifferentiableAt ğ•œ f z) :
    DifferentiableAt ğ•œ (star âˆ˜ f âˆ˜ star) (star z) :=
  hf.hasFDerivAt.star_star.differentiableAt

end NontrivialStar
