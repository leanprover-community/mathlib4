/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Bilinear

#align_import analysis.calculus.fderiv.mul from "leanprover-community/mathlib"@"d608fc5d4e69d4cc21885913fb573a88b0deb521"

/-!
# Multiplicative operations on derivatives

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of

* multiplication of a function by a scalar function
* product of finitely many scalar functions
* taking the pointwise multiplicative inverse (i.e. `Inv.inv` or `Ring.inverse`) of a function
-/


open scoped Classical
open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable (e : E â†’L[ğ•œ] F)
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

section CLMCompApply

/-! ### Derivative of the pointwise composition/application of continuous linear maps -/


variable {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H] {c : E â†’ G â†’L[ğ•œ] H}
  {c' : E â†’L[ğ•œ] G â†’L[ğ•œ] H} {d : E â†’ F â†’L[ğ•œ] G} {d' : E â†’L[ğ•œ] F â†’L[ğ•œ] G} {u : E â†’ G} {u' : E â†’L[ğ•œ] G}

@[fun_prop]
theorem HasStrictFDerivAt.clm_comp (hc : HasStrictFDerivAt c c' x) (hd : HasStrictFDerivAt d d' x) :
    HasStrictFDerivAt (fun y => (c y).comp (d y))
      ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') x :=
  (isBoundedBilinearMap_comp.hasStrictFDerivAt (c x, d x)).comp x <| hc.prod hd
#align has_strict_fderiv_at.clm_comp HasStrictFDerivAt.clm_comp

@[fun_prop]
theorem HasFDerivWithinAt.clm_comp (hc : HasFDerivWithinAt c c' s x)
    (hd : HasFDerivWithinAt d d' s x) :
    HasFDerivWithinAt (fun y => (c y).comp (d y))
      ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') s x :=
  (isBoundedBilinearMap_comp.hasFDerivAt (c x, d x)).comp_hasFDerivWithinAt x <| hc.prod hd
#align has_fderiv_within_at.clm_comp HasFDerivWithinAt.clm_comp

@[fun_prop]
theorem HasFDerivAt.clm_comp (hc : HasFDerivAt c c' x) (hd : HasFDerivAt d d' x) :
    HasFDerivAt (fun y => (c y).comp (d y))
      ((compL ğ•œ F G H (c x)).comp d' + ((compL ğ•œ F G H).flip (d x)).comp c') x :=
  (isBoundedBilinearMap_comp.hasFDerivAt (c x, d x)).comp x <| hc.prod hd
#align has_fderiv_at.clm_comp HasFDerivAt.clm_comp

@[fun_prop]
theorem DifferentiableWithinAt.clm_comp (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) :
    DifferentiableWithinAt ğ•œ (fun y => (c y).comp (d y)) s x :=
  (hc.hasFDerivWithinAt.clm_comp hd.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.clm_comp DifferentiableWithinAt.clm_comp

@[fun_prop]
theorem DifferentiableAt.clm_comp (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    DifferentiableAt ğ•œ (fun y => (c y).comp (d y)) x :=
  (hc.hasFDerivAt.clm_comp hd.hasFDerivAt).differentiableAt
#align differentiable_at.clm_comp DifferentiableAt.clm_comp

@[fun_prop]
theorem DifferentiableOn.clm_comp (hc : DifferentiableOn ğ•œ c s) (hd : DifferentiableOn ğ•œ d s) :
    DifferentiableOn ğ•œ (fun y => (c y).comp (d y)) s := fun x hx => (hc x hx).clm_comp (hd x hx)
#align differentiable_on.clm_comp DifferentiableOn.clm_comp

@[fun_prop]
theorem Differentiable.clm_comp (hc : Differentiable ğ•œ c) (hd : Differentiable ğ•œ d) :
    Differentiable ğ•œ fun y => (c y).comp (d y) := fun x => (hc x).clm_comp (hd x)
#align differentiable.clm_comp Differentiable.clm_comp

theorem fderivWithin_clm_comp (hxs : UniqueDiffWithinAt ğ•œ s x) (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) :
    fderivWithin ğ•œ (fun y => (c y).comp (d y)) s x =
      (compL ğ•œ F G H (c x)).comp (fderivWithin ğ•œ d s x) +
        ((compL ğ•œ F G H).flip (d x)).comp (fderivWithin ğ•œ c s x) :=
  (hc.hasFDerivWithinAt.clm_comp hd.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_clm_comp fderivWithin_clm_comp

theorem fderiv_clm_comp (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    fderiv ğ•œ (fun y => (c y).comp (d y)) x =
      (compL ğ•œ F G H (c x)).comp (fderiv ğ•œ d x) +
        ((compL ğ•œ F G H).flip (d x)).comp (fderiv ğ•œ c x) :=
  (hc.hasFDerivAt.clm_comp hd.hasFDerivAt).fderiv
#align fderiv_clm_comp fderiv_clm_comp

@[fun_prop]
theorem HasStrictFDerivAt.clm_apply (hc : HasStrictFDerivAt c c' x)
    (hu : HasStrictFDerivAt u u' x) :
    HasStrictFDerivAt (fun y => (c y) (u y)) ((c x).comp u' + c'.flip (u x)) x :=
  (isBoundedBilinearMap_apply.hasStrictFDerivAt (c x, u x)).comp x (hc.prod hu)
#align has_strict_fderiv_at.clm_apply HasStrictFDerivAt.clm_apply

@[fun_prop]
theorem HasFDerivWithinAt.clm_apply (hc : HasFDerivWithinAt c c' s x)
    (hu : HasFDerivWithinAt u u' s x) :
    HasFDerivWithinAt (fun y => (c y) (u y)) ((c x).comp u' + c'.flip (u x)) s x :=
  (isBoundedBilinearMap_apply.hasFDerivAt (c x, u x)).comp_hasFDerivWithinAt x (hc.prod hu)
#align has_fderiv_within_at.clm_apply HasFDerivWithinAt.clm_apply

@[fun_prop]
theorem HasFDerivAt.clm_apply (hc : HasFDerivAt c c' x) (hu : HasFDerivAt u u' x) :
    HasFDerivAt (fun y => (c y) (u y)) ((c x).comp u' + c'.flip (u x)) x :=
  (isBoundedBilinearMap_apply.hasFDerivAt (c x, u x)).comp x (hc.prod hu)
#align has_fderiv_at.clm_apply HasFDerivAt.clm_apply

@[fun_prop]
theorem DifferentiableWithinAt.clm_apply (hc : DifferentiableWithinAt ğ•œ c s x)
    (hu : DifferentiableWithinAt ğ•œ u s x) : DifferentiableWithinAt ğ•œ (fun y => (c y) (u y)) s x :=
  (hc.hasFDerivWithinAt.clm_apply hu.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.clm_apply DifferentiableWithinAt.clm_apply

@[fun_prop]
theorem DifferentiableAt.clm_apply (hc : DifferentiableAt ğ•œ c x) (hu : DifferentiableAt ğ•œ u x) :
    DifferentiableAt ğ•œ (fun y => (c y) (u y)) x :=
  (hc.hasFDerivAt.clm_apply hu.hasFDerivAt).differentiableAt
#align differentiable_at.clm_apply DifferentiableAt.clm_apply

@[fun_prop]
theorem DifferentiableOn.clm_apply (hc : DifferentiableOn ğ•œ c s) (hu : DifferentiableOn ğ•œ u s) :
    DifferentiableOn ğ•œ (fun y => (c y) (u y)) s := fun x hx => (hc x hx).clm_apply (hu x hx)
#align differentiable_on.clm_apply DifferentiableOn.clm_apply

@[fun_prop]
theorem Differentiable.clm_apply (hc : Differentiable ğ•œ c) (hu : Differentiable ğ•œ u) :
    Differentiable ğ•œ fun y => (c y) (u y) := fun x => (hc x).clm_apply (hu x)
#align differentiable.clm_apply Differentiable.clm_apply

theorem fderivWithin_clm_apply (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hc : DifferentiableWithinAt ğ•œ c s x) (hu : DifferentiableWithinAt ğ•œ u s x) :
    fderivWithin ğ•œ (fun y => (c y) (u y)) s x =
      (c x).comp (fderivWithin ğ•œ u s x) + (fderivWithin ğ•œ c s x).flip (u x) :=
  (hc.hasFDerivWithinAt.clm_apply hu.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_clm_apply fderivWithin_clm_apply

theorem fderiv_clm_apply (hc : DifferentiableAt ğ•œ c x) (hu : DifferentiableAt ğ•œ u x) :
    fderiv ğ•œ (fun y => (c y) (u y)) x = (c x).comp (fderiv ğ•œ u x) + (fderiv ğ•œ c x).flip (u x) :=
  (hc.hasFDerivAt.clm_apply hu.hasFDerivAt).fderiv
#align fderiv_clm_apply fderiv_clm_apply

end CLMCompApply

section ContinuousMultilinearApplyConst

/-! ### Derivative of the application of continuous multilinear maps to a constant -/

variable {Î¹ : Type*} [Fintype Î¹]
  {M : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace ğ•œ (M i)]
  {H : Type*} [NormedAddCommGroup H] [NormedSpace ğ•œ H]
  {c : E â†’ ContinuousMultilinearMap ğ•œ M H}
  {c' : E â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ M H}

@[fun_prop]
theorem HasStrictFDerivAt.continuousMultilinear_apply_const (hc : HasStrictFDerivAt c c' x)
    (u : âˆ€ i, M i) : HasStrictFDerivAt (fun y â†¦ (c y) u) (c'.flipMultilinear u) x :=
  (ContinuousMultilinearMap.apply ğ•œ M H u).hasStrictFDerivAt.comp x hc

@[fun_prop]
theorem HasFDerivWithinAt.continuousMultilinear_apply_const (hc : HasFDerivWithinAt c c' s x)
    (u : âˆ€ i, M i) :
    HasFDerivWithinAt (fun y â†¦ (c y) u) (c'.flipMultilinear u) s x :=
  (ContinuousMultilinearMap.apply ğ•œ M H u).hasFDerivAt.comp_hasFDerivWithinAt x hc

@[fun_prop]
theorem HasFDerivAt.continuousMultilinear_apply_const (hc : HasFDerivAt c c' x) (u : âˆ€ i, M i) :
    HasFDerivAt (fun y â†¦ (c y) u) (c'.flipMultilinear u) x :=
  (ContinuousMultilinearMap.apply ğ•œ M H u).hasFDerivAt.comp x hc

@[fun_prop]
theorem DifferentiableWithinAt.continuousMultilinear_apply_const
    (hc : DifferentiableWithinAt ğ•œ c s x) (u : âˆ€ i, M i) :
    DifferentiableWithinAt ğ•œ (fun y â†¦ (c y) u) s x :=
  (hc.hasFDerivWithinAt.continuousMultilinear_apply_const u).differentiableWithinAt

@[fun_prop]
theorem DifferentiableAt.continuousMultilinear_apply_const (hc : DifferentiableAt ğ•œ c x)
    (u : âˆ€ i, M i) :
    DifferentiableAt ğ•œ (fun y â†¦ (c y) u) x :=
  (hc.hasFDerivAt.continuousMultilinear_apply_const u).differentiableAt

@[fun_prop]
theorem DifferentiableOn.continuousMultilinear_apply_const (hc : DifferentiableOn ğ•œ c s)
    (u : âˆ€ i, M i) : DifferentiableOn ğ•œ (fun y â†¦ (c y) u) s :=
  fun x hx â†¦ (hc x hx).continuousMultilinear_apply_const u

@[fun_prop]
theorem Differentiable.continuousMultilinear_apply_const (hc : Differentiable ğ•œ c) (u : âˆ€ i, M i) :
    Differentiable ğ•œ fun y â†¦ (c y) u := fun x â†¦ (hc x).continuousMultilinear_apply_const u

theorem fderivWithin_continuousMultilinear_apply_const (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hc : DifferentiableWithinAt ğ•œ c s x) (u : âˆ€ i, M i) :
    fderivWithin ğ•œ (fun y â†¦ (c y) u) s x = ((fderivWithin ğ•œ c s x).flipMultilinear u) :=
  (hc.hasFDerivWithinAt.continuousMultilinear_apply_const u).fderivWithin hxs

theorem fderiv_continuousMultilinear_apply_const (hc : DifferentiableAt ğ•œ c x) (u : âˆ€ i, M i) :
    (fderiv ğ•œ (fun y â†¦ (c y) u) x) = (fderiv ğ•œ c x).flipMultilinear u :=
  (hc.hasFDerivAt.continuousMultilinear_apply_const u).fderiv

/-- Application of a `ContinuousMultilinearMap` to a constant commutes with `fderivWithin`. -/
theorem fderivWithin_continuousMultilinear_apply_const_apply (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hc : DifferentiableWithinAt ğ•œ c s x) (u : âˆ€ i, M i) (m : E) :
    (fderivWithin ğ•œ (fun y â†¦ (c y) u) s x) m = (fderivWithin ğ•œ c s x) m u := by
  simp [fderivWithin_continuousMultilinear_apply_const hxs hc]

/-- Application of a `ContinuousMultilinearMap` to a constant commutes with `fderiv`. -/
theorem fderiv_continuousMultilinear_apply_const_apply (hc : DifferentiableAt ğ•œ c x)
    (u : âˆ€ i, M i) (m : E) :
    (fderiv ğ•œ (fun y â†¦ (c y) u) x) m = (fderiv ğ•œ c x) m u := by
  simp [fderiv_continuousMultilinear_apply_const hc]

end ContinuousMultilinearApplyConst

section SMul

/-! ### Derivative of the product of a scalar-valued function and a vector-valued function

If `c` is a differentiable scalar-valued function and `f` is a differentiable vector-valued
function, then `fun x â†¦ c x â€¢ f x` is differentiable as well. Lemmas in this section works for
function `c` taking values in the base field, as well as in a normed algebra over the base
field: e.g., they work for `c : E â†’ â„‚` and `f : E â†’ F` provided that `F` is a complex
normed vector space.
-/


variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' F]
  [IsScalarTower ğ•œ ğ•œ' F]

variable {c : E â†’ ğ•œ'} {c' : E â†’L[ğ•œ] ğ•œ'}

@[fun_prop]
theorem HasStrictFDerivAt.smul (hc : HasStrictFDerivAt c c' x) (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c'.smulRight (f x)) x :=
  (isBoundedBilinearMap_smul.hasStrictFDerivAt (c x, f x)).comp x <| hc.prod hf
#align has_strict_fderiv_at.smul HasStrictFDerivAt.smul

@[fun_prop]
theorem HasFDerivWithinAt.smul (hc : HasFDerivWithinAt c c' s x) (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (fun y => c y â€¢ f y) (c x â€¢ f' + c'.smulRight (f x)) s x :=
  (isBoundedBilinearMap_smul.hasFDerivAt (c x, f x)).comp_hasFDerivWithinAt x <| hc.prod hf
#align has_fderiv_within_at.smul HasFDerivWithinAt.smul

@[fun_prop]
theorem HasFDerivAt.smul (hc : HasFDerivAt c c' x) (hf : HasFDerivAt f f' x) :
    HasFDerivAt (fun y => c y â€¢ f y) (c x â€¢ f' + c'.smulRight (f x)) x :=
  (isBoundedBilinearMap_smul.hasFDerivAt (c x, f x)).comp x <| hc.prod hf
#align has_fderiv_at.smul HasFDerivAt.smul

@[fun_prop]
theorem DifferentiableWithinAt.smul (hc : DifferentiableWithinAt ğ•œ c s x)
    (hf : DifferentiableWithinAt ğ•œ f s x) : DifferentiableWithinAt ğ•œ (fun y => c y â€¢ f y) s x :=
  (hc.hasFDerivWithinAt.smul hf.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.smul DifferentiableWithinAt.smul

@[simp, fun_prop]
theorem DifferentiableAt.smul (hc : DifferentiableAt ğ•œ c x) (hf : DifferentiableAt ğ•œ f x) :
    DifferentiableAt ğ•œ (fun y => c y â€¢ f y) x :=
  (hc.hasFDerivAt.smul hf.hasFDerivAt).differentiableAt
#align differentiable_at.smul DifferentiableAt.smul

@[fun_prop]
theorem DifferentiableOn.smul (hc : DifferentiableOn ğ•œ c s) (hf : DifferentiableOn ğ•œ f s) :
    DifferentiableOn ğ•œ (fun y => c y â€¢ f y) s := fun x hx => (hc x hx).smul (hf x hx)
#align differentiable_on.smul DifferentiableOn.smul

@[simp, fun_prop]
theorem Differentiable.smul (hc : Differentiable ğ•œ c) (hf : Differentiable ğ•œ f) :
    Differentiable ğ•œ fun y => c y â€¢ f y := fun x => (hc x).smul (hf x)
#align differentiable.smul Differentiable.smul

theorem fderivWithin_smul (hxs : UniqueDiffWithinAt ğ•œ s x) (hc : DifferentiableWithinAt ğ•œ c s x)
    (hf : DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ (fun y => c y â€¢ f y) s x =
      c x â€¢ fderivWithin ğ•œ f s x + (fderivWithin ğ•œ c s x).smulRight (f x) :=
  (hc.hasFDerivWithinAt.smul hf.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_smul fderivWithin_smul

theorem fderiv_smul (hc : DifferentiableAt ğ•œ c x) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun y => c y â€¢ f y) x = c x â€¢ fderiv ğ•œ f x + (fderiv ğ•œ c x).smulRight (f x) :=
  (hc.hasFDerivAt.smul hf.hasFDerivAt).fderiv
#align fderiv_smul fderiv_smul

@[fun_prop]
theorem HasStrictFDerivAt.smul_const (hc : HasStrictFDerivAt c c' x) (f : F) :
    HasStrictFDerivAt (fun y => c y â€¢ f) (c'.smulRight f) x := by
  simpa only [smul_zero, zero_add] using hc.smul (hasStrictFDerivAt_const f x)
#align has_strict_fderiv_at.smul_const HasStrictFDerivAt.smul_const

@[fun_prop]
theorem HasFDerivWithinAt.smul_const (hc : HasFDerivWithinAt c c' s x) (f : F) :
    HasFDerivWithinAt (fun y => c y â€¢ f) (c'.smulRight f) s x := by
  simpa only [smul_zero, zero_add] using hc.smul (hasFDerivWithinAt_const f x s)
#align has_fderiv_within_at.smul_const HasFDerivWithinAt.smul_const

@[fun_prop]
theorem HasFDerivAt.smul_const (hc : HasFDerivAt c c' x) (f : F) :
    HasFDerivAt (fun y => c y â€¢ f) (c'.smulRight f) x := by
  simpa only [smul_zero, zero_add] using hc.smul (hasFDerivAt_const f x)
#align has_fderiv_at.smul_const HasFDerivAt.smul_const

@[fun_prop]
theorem DifferentiableWithinAt.smul_const (hc : DifferentiableWithinAt ğ•œ c s x) (f : F) :
    DifferentiableWithinAt ğ•œ (fun y => c y â€¢ f) s x :=
  (hc.hasFDerivWithinAt.smul_const f).differentiableWithinAt
#align differentiable_within_at.smul_const DifferentiableWithinAt.smul_const

@[fun_prop]
theorem DifferentiableAt.smul_const (hc : DifferentiableAt ğ•œ c x) (f : F) :
    DifferentiableAt ğ•œ (fun y => c y â€¢ f) x :=
  (hc.hasFDerivAt.smul_const f).differentiableAt
#align differentiable_at.smul_const DifferentiableAt.smul_const

@[fun_prop]
theorem DifferentiableOn.smul_const (hc : DifferentiableOn ğ•œ c s) (f : F) :
    DifferentiableOn ğ•œ (fun y => c y â€¢ f) s := fun x hx => (hc x hx).smul_const f
#align differentiable_on.smul_const DifferentiableOn.smul_const

@[fun_prop]
theorem Differentiable.smul_const (hc : Differentiable ğ•œ c) (f : F) :
    Differentiable ğ•œ fun y => c y â€¢ f := fun x => (hc x).smul_const f
#align differentiable.smul_const Differentiable.smul_const

theorem fderivWithin_smul_const (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hc : DifferentiableWithinAt ğ•œ c s x) (f : F) :
    fderivWithin ğ•œ (fun y => c y â€¢ f) s x = (fderivWithin ğ•œ c s x).smulRight f :=
  (hc.hasFDerivWithinAt.smul_const f).fderivWithin hxs
#align fderiv_within_smul_const fderivWithin_smul_const

theorem fderiv_smul_const (hc : DifferentiableAt ğ•œ c x) (f : F) :
    fderiv ğ•œ (fun y => c y â€¢ f) x = (fderiv ğ•œ c x).smulRight f :=
  (hc.hasFDerivAt.smul_const f).fderiv
#align fderiv_smul_const fderiv_smul_const

end SMul

section Mul

/-! ### Derivative of the product of two functions -/


variable {ğ”¸ ğ”¸' : Type*} [NormedRing ğ”¸] [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸] [NormedAlgebra ğ•œ ğ”¸']
  {a b : E â†’ ğ”¸} {a' b' : E â†’L[ğ•œ] ğ”¸} {c d : E â†’ ğ”¸'} {c' d' : E â†’L[ğ•œ] ğ”¸'}

@[fun_prop]
theorem HasStrictFDerivAt.mul' {x : E} (ha : HasStrictFDerivAt a a' x)
    (hb : HasStrictFDerivAt b b' x) :
    HasStrictFDerivAt (fun y => a y * b y) (a x â€¢ b' + a'.smulRight (b x)) x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).isBoundedBilinearMap.hasStrictFDerivAt (a x, b x)).comp x
    (ha.prod hb)
#align has_strict_fderiv_at.mul' HasStrictFDerivAt.mul'

@[fun_prop]
theorem HasStrictFDerivAt.mul (hc : HasStrictFDerivAt c c' x) (hd : HasStrictFDerivAt d d' x) :
    HasStrictFDerivAt (fun y => c y * d y) (c x â€¢ d' + d x â€¢ c') x := by
  convert hc.mul' hd
  ext z
  apply mul_comm
#align has_strict_fderiv_at.mul HasStrictFDerivAt.mul

@[fun_prop]
theorem HasFDerivWithinAt.mul' (ha : HasFDerivWithinAt a a' s x) (hb : HasFDerivWithinAt b b' s x) :
    HasFDerivWithinAt (fun y => a y * b y) (a x â€¢ b' + a'.smulRight (b x)) s x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).isBoundedBilinearMap.hasFDerivAt (a x, b x)).comp_hasFDerivWithinAt
    x (ha.prod hb)
#align has_fderiv_within_at.mul' HasFDerivWithinAt.mul'

@[fun_prop]
theorem HasFDerivWithinAt.mul (hc : HasFDerivWithinAt c c' s x) (hd : HasFDerivWithinAt d d' s x) :
    HasFDerivWithinAt (fun y => c y * d y) (c x â€¢ d' + d x â€¢ c') s x := by
  convert hc.mul' hd
  ext z
  apply mul_comm
#align has_fderiv_within_at.mul HasFDerivWithinAt.mul

@[fun_prop]
theorem HasFDerivAt.mul' (ha : HasFDerivAt a a' x) (hb : HasFDerivAt b b' x) :
    HasFDerivAt (fun y => a y * b y) (a x â€¢ b' + a'.smulRight (b x)) x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).isBoundedBilinearMap.hasFDerivAt (a x, b x)).comp x (ha.prod hb)
#align has_fderiv_at.mul' HasFDerivAt.mul'

@[fun_prop]
theorem HasFDerivAt.mul (hc : HasFDerivAt c c' x) (hd : HasFDerivAt d d' x) :
    HasFDerivAt (fun y => c y * d y) (c x â€¢ d' + d x â€¢ c') x := by
  convert hc.mul' hd
  ext z
  apply mul_comm
#align has_fderiv_at.mul HasFDerivAt.mul

@[fun_prop]
theorem DifferentiableWithinAt.mul (ha : DifferentiableWithinAt ğ•œ a s x)
    (hb : DifferentiableWithinAt ğ•œ b s x) : DifferentiableWithinAt ğ•œ (fun y => a y * b y) s x :=
  (ha.hasFDerivWithinAt.mul' hb.hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.mul DifferentiableWithinAt.mul

@[simp, fun_prop]
theorem DifferentiableAt.mul (ha : DifferentiableAt ğ•œ a x) (hb : DifferentiableAt ğ•œ b x) :
    DifferentiableAt ğ•œ (fun y => a y * b y) x :=
  (ha.hasFDerivAt.mul' hb.hasFDerivAt).differentiableAt
#align differentiable_at.mul DifferentiableAt.mul

@[fun_prop]
theorem DifferentiableOn.mul (ha : DifferentiableOn ğ•œ a s) (hb : DifferentiableOn ğ•œ b s) :
    DifferentiableOn ğ•œ (fun y => a y * b y) s := fun x hx => (ha x hx).mul (hb x hx)
#align differentiable_on.mul DifferentiableOn.mul

@[simp, fun_prop]
theorem Differentiable.mul (ha : Differentiable ğ•œ a) (hb : Differentiable ğ•œ b) :
    Differentiable ğ•œ fun y => a y * b y := fun x => (ha x).mul (hb x)
#align differentiable.mul Differentiable.mul

@[fun_prop]
theorem DifferentiableWithinAt.pow (ha : DifferentiableWithinAt ğ•œ a s x) :
    âˆ€ n : â„•, DifferentiableWithinAt ğ•œ (fun x => a x ^ n) s x
  | 0 => by simp only [pow_zero, differentiableWithinAt_const]
  | n + 1 => by simp only [pow_succ', DifferentiableWithinAt.pow ha n, ha.mul]
#align differentiable_within_at.pow DifferentiableWithinAt.pow

@[simp, fun_prop]
theorem DifferentiableAt.pow (ha : DifferentiableAt ğ•œ a x) (n : â„•) :
    DifferentiableAt ğ•œ (fun x => a x ^ n) x :=
  differentiableWithinAt_univ.mp <| ha.differentiableWithinAt.pow n
#align differentiable_at.pow DifferentiableAt.pow

@[fun_prop]
theorem DifferentiableOn.pow (ha : DifferentiableOn ğ•œ a s) (n : â„•) :
    DifferentiableOn ğ•œ (fun x => a x ^ n) s := fun x h => (ha x h).pow n
#align differentiable_on.pow DifferentiableOn.pow

@[simp, fun_prop]
theorem Differentiable.pow (ha : Differentiable ğ•œ a) (n : â„•) : Differentiable ğ•œ fun x => a x ^ n :=
  fun x => (ha x).pow n
#align differentiable.pow Differentiable.pow

theorem fderivWithin_mul' (hxs : UniqueDiffWithinAt ğ•œ s x) (ha : DifferentiableWithinAt ğ•œ a s x)
    (hb : DifferentiableWithinAt ğ•œ b s x) :
    fderivWithin ğ•œ (fun y => a y * b y) s x =
      a x â€¢ fderivWithin ğ•œ b s x + (fderivWithin ğ•œ a s x).smulRight (b x) :=
  (ha.hasFDerivWithinAt.mul' hb.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_mul' fderivWithin_mul'

theorem fderivWithin_mul (hxs : UniqueDiffWithinAt ğ•œ s x) (hc : DifferentiableWithinAt ğ•œ c s x)
    (hd : DifferentiableWithinAt ğ•œ d s x) :
    fderivWithin ğ•œ (fun y => c y * d y) s x =
      c x â€¢ fderivWithin ğ•œ d s x + d x â€¢ fderivWithin ğ•œ c s x :=
  (hc.hasFDerivWithinAt.mul hd.hasFDerivWithinAt).fderivWithin hxs
#align fderiv_within_mul fderivWithin_mul

theorem fderiv_mul' (ha : DifferentiableAt ğ•œ a x) (hb : DifferentiableAt ğ•œ b x) :
    fderiv ğ•œ (fun y => a y * b y) x = a x â€¢ fderiv ğ•œ b x + (fderiv ğ•œ a x).smulRight (b x) :=
  (ha.hasFDerivAt.mul' hb.hasFDerivAt).fderiv
#align fderiv_mul' fderiv_mul'

theorem fderiv_mul (hc : DifferentiableAt ğ•œ c x) (hd : DifferentiableAt ğ•œ d x) :
    fderiv ğ•œ (fun y => c y * d y) x = c x â€¢ fderiv ğ•œ d x + d x â€¢ fderiv ğ•œ c x :=
  (hc.hasFDerivAt.mul hd.hasFDerivAt).fderiv
#align fderiv_mul fderiv_mul

@[fun_prop]
theorem HasStrictFDerivAt.mul_const' (ha : HasStrictFDerivAt a a' x) (b : ğ”¸) :
    HasStrictFDerivAt (fun y => a y * b) (a'.smulRight b) x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).flip b).hasStrictFDerivAt.comp x ha
#align has_strict_fderiv_at.mul_const' HasStrictFDerivAt.mul_const'

@[fun_prop]
theorem HasStrictFDerivAt.mul_const (hc : HasStrictFDerivAt c c' x) (d : ğ”¸') :
    HasStrictFDerivAt (fun y => c y * d) (d â€¢ c') x := by
  convert hc.mul_const' d
  ext z
  apply mul_comm
#align has_strict_fderiv_at.mul_const HasStrictFDerivAt.mul_const

@[fun_prop]
theorem HasFDerivWithinAt.mul_const' (ha : HasFDerivWithinAt a a' s x) (b : ğ”¸) :
    HasFDerivWithinAt (fun y => a y * b) (a'.smulRight b) s x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).flip b).hasFDerivAt.comp_hasFDerivWithinAt x ha
#align has_fderiv_within_at.mul_const' HasFDerivWithinAt.mul_const'

@[fun_prop]
theorem HasFDerivWithinAt.mul_const (hc : HasFDerivWithinAt c c' s x) (d : ğ”¸') :
    HasFDerivWithinAt (fun y => c y * d) (d â€¢ c') s x := by
  convert hc.mul_const' d
  ext z
  apply mul_comm
#align has_fderiv_within_at.mul_const HasFDerivWithinAt.mul_const

@[fun_prop]
theorem HasFDerivAt.mul_const' (ha : HasFDerivAt a a' x) (b : ğ”¸) :
    HasFDerivAt (fun y => a y * b) (a'.smulRight b) x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸).flip b).hasFDerivAt.comp x ha
#align has_fderiv_at.mul_const' HasFDerivAt.mul_const'

@[fun_prop]
theorem HasFDerivAt.mul_const (hc : HasFDerivAt c c' x) (d : ğ”¸') :
    HasFDerivAt (fun y => c y * d) (d â€¢ c') x := by
  convert hc.mul_const' d
  ext z
  apply mul_comm
#align has_fderiv_at.mul_const HasFDerivAt.mul_const

@[fun_prop]
theorem DifferentiableWithinAt.mul_const (ha : DifferentiableWithinAt ğ•œ a s x) (b : ğ”¸) :
    DifferentiableWithinAt ğ•œ (fun y => a y * b) s x :=
  (ha.hasFDerivWithinAt.mul_const' b).differentiableWithinAt
#align differentiable_within_at.mul_const DifferentiableWithinAt.mul_const

@[fun_prop]
theorem DifferentiableAt.mul_const (ha : DifferentiableAt ğ•œ a x) (b : ğ”¸) :
    DifferentiableAt ğ•œ (fun y => a y * b) x :=
  (ha.hasFDerivAt.mul_const' b).differentiableAt
#align differentiable_at.mul_const DifferentiableAt.mul_const

@[fun_prop]
theorem DifferentiableOn.mul_const (ha : DifferentiableOn ğ•œ a s) (b : ğ”¸) :
    DifferentiableOn ğ•œ (fun y => a y * b) s := fun x hx => (ha x hx).mul_const b
#align differentiable_on.mul_const DifferentiableOn.mul_const

@[fun_prop]
theorem Differentiable.mul_const (ha : Differentiable ğ•œ a) (b : ğ”¸) :
    Differentiable ğ•œ fun y => a y * b := fun x => (ha x).mul_const b
#align differentiable.mul_const Differentiable.mul_const

theorem fderivWithin_mul_const' (hxs : UniqueDiffWithinAt ğ•œ s x)
    (ha : DifferentiableWithinAt ğ•œ a s x) (b : ğ”¸) :
    fderivWithin ğ•œ (fun y => a y * b) s x = (fderivWithin ğ•œ a s x).smulRight b :=
  (ha.hasFDerivWithinAt.mul_const' b).fderivWithin hxs
#align fderiv_within_mul_const' fderivWithin_mul_const'

theorem fderivWithin_mul_const (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hc : DifferentiableWithinAt ğ•œ c s x) (d : ğ”¸') :
    fderivWithin ğ•œ (fun y => c y * d) s x = d â€¢ fderivWithin ğ•œ c s x :=
  (hc.hasFDerivWithinAt.mul_const d).fderivWithin hxs
#align fderiv_within_mul_const fderivWithin_mul_const

theorem fderiv_mul_const' (ha : DifferentiableAt ğ•œ a x) (b : ğ”¸) :
    fderiv ğ•œ (fun y => a y * b) x = (fderiv ğ•œ a x).smulRight b :=
  (ha.hasFDerivAt.mul_const' b).fderiv
#align fderiv_mul_const' fderiv_mul_const'

theorem fderiv_mul_const (hc : DifferentiableAt ğ•œ c x) (d : ğ”¸') :
    fderiv ğ•œ (fun y => c y * d) x = d â€¢ fderiv ğ•œ c x :=
  (hc.hasFDerivAt.mul_const d).fderiv
#align fderiv_mul_const fderiv_mul_const

@[fun_prop]
theorem HasStrictFDerivAt.const_mul (ha : HasStrictFDerivAt a a' x) (b : ğ”¸) :
    HasStrictFDerivAt (fun y => b * a y) (b â€¢ a') x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸) b).hasStrictFDerivAt.comp x ha
#align has_strict_fderiv_at.const_mul HasStrictFDerivAt.const_mul

@[fun_prop]
theorem HasFDerivWithinAt.const_mul (ha : HasFDerivWithinAt a a' s x) (b : ğ”¸) :
    HasFDerivWithinAt (fun y => b * a y) (b â€¢ a') s x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸) b).hasFDerivAt.comp_hasFDerivWithinAt x ha
#align has_fderiv_within_at.const_mul HasFDerivWithinAt.const_mul

@[fun_prop]
theorem HasFDerivAt.const_mul (ha : HasFDerivAt a a' x) (b : ğ”¸) :
    HasFDerivAt (fun y => b * a y) (b â€¢ a') x :=
  ((ContinuousLinearMap.mul ğ•œ ğ”¸) b).hasFDerivAt.comp x ha
#align has_fderiv_at.const_mul HasFDerivAt.const_mul

@[fun_prop]
theorem DifferentiableWithinAt.const_mul (ha : DifferentiableWithinAt ğ•œ a s x) (b : ğ”¸) :
    DifferentiableWithinAt ğ•œ (fun y => b * a y) s x :=
  (ha.hasFDerivWithinAt.const_mul b).differentiableWithinAt
#align differentiable_within_at.const_mul DifferentiableWithinAt.const_mul

@[fun_prop]
theorem DifferentiableAt.const_mul (ha : DifferentiableAt ğ•œ a x) (b : ğ”¸) :
    DifferentiableAt ğ•œ (fun y => b * a y) x :=
  (ha.hasFDerivAt.const_mul b).differentiableAt
#align differentiable_at.const_mul DifferentiableAt.const_mul

@[fun_prop]
theorem DifferentiableOn.const_mul (ha : DifferentiableOn ğ•œ a s) (b : ğ”¸) :
    DifferentiableOn ğ•œ (fun y => b * a y) s := fun x hx => (ha x hx).const_mul b
#align differentiable_on.const_mul DifferentiableOn.const_mul

@[fun_prop]
theorem Differentiable.const_mul (ha : Differentiable ğ•œ a) (b : ğ”¸) :
    Differentiable ğ•œ fun y => b * a y := fun x => (ha x).const_mul b
#align differentiable.const_mul Differentiable.const_mul

theorem fderivWithin_const_mul (hxs : UniqueDiffWithinAt ğ•œ s x)
    (ha : DifferentiableWithinAt ğ•œ a s x) (b : ğ”¸) :
    fderivWithin ğ•œ (fun y => b * a y) s x = b â€¢ fderivWithin ğ•œ a s x :=
  (ha.hasFDerivWithinAt.const_mul b).fderivWithin hxs
#align fderiv_within_const_mul fderivWithin_const_mul

theorem fderiv_const_mul (ha : DifferentiableAt ğ•œ a x) (b : ğ”¸) :
    fderiv ğ•œ (fun y => b * a y) x = b â€¢ fderiv ğ•œ a x :=
  (ha.hasFDerivAt.const_mul b).fderiv
#align fderiv_const_mul fderiv_const_mul

end Mul

section Prod

/-! ### Derivative of a finite product of functions -/

variable {Î¹ : Type*} {ğ”¸ ğ”¸' : Type*} [NormedRing ğ”¸] [NormedCommRing ğ”¸'] [NormedAlgebra ğ•œ ğ”¸]
  [NormedAlgebra ğ•œ ğ”¸'] {u : Finset Î¹} {f : Î¹ â†’ E â†’ ğ”¸} {f' : Î¹ â†’ E â†’L[ğ•œ] ğ”¸} {g : Î¹ â†’ E â†’ ğ”¸'}
  {g' : Î¹ â†’ E â†’L[ğ•œ] ğ”¸'}

@[fun_prop]
theorem hasStrictFDerivAt_list_prod' [Fintype Î¹] {l : List Î¹} {x : Î¹ â†’ ğ”¸} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (l.map x).prod)
      (âˆ‘ i : Fin l.length, ((l.take i).map x).prod â€¢
        smulRight (proj (l.get i)) ((l.drop (.succ i)).map x).prod) x := by
  induction l with
  | nil => simp [hasStrictFDerivAt_const]
  | cons a l IH =>
    simp only [List.map_cons, List.prod_cons, â† proj_apply (R := ğ•œ) (Ï† := fun _ : Î¹ â†¦ ğ”¸) a]
    exact .congr_fderiv (.mul' (ContinuousLinearMap.hasStrictFDerivAt _) IH)
      (by ext; simp [Fin.sum_univ_succ, Finset.mul_sum, mul_assoc, add_comm])

@[fun_prop]
theorem hasStrictFDerivAt_list_prod_finRange' {n : â„•} {x : Fin n â†’ ğ”¸} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ ((List.finRange n).map x).prod)
      (âˆ‘ i : Fin n, (((List.finRange n).take i).map x).prod â€¢
        smulRight (proj i) (((List.finRange n).drop (.succ i)).map x).prod) x :=
  hasStrictFDerivAt_list_prod'.congr_fderiv <|
    Finset.sum_equiv (finCongr (List.length_finRange n)) (by simp) (by simp [Fin.forall_iff])

@[fun_prop]
theorem hasStrictFDerivAt_list_prod_attach' [DecidableEq Î¹] {l : List Î¹} {x : {i // i âˆˆ l} â†’ ğ”¸} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (l.attach.map x).prod)
      (âˆ‘ i : Fin l.length, ((l.attach.take i).map x).prod â€¢
        smulRight (proj (l.attach.get (i.cast l.length_attach.symm)))
          ((l.attach.drop (.succ i)).map x).prod) x :=
  hasStrictFDerivAt_list_prod'.congr_fderiv <| Eq.symm <|
    Finset.sum_equiv (finCongr l.length_attach.symm) (by simp) (by simp)

@[fun_prop]
theorem hasFDerivAt_list_prod' [Fintype Î¹] {l : List Î¹} {x : Î¹ â†’ ğ”¸'} :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (l.map x).prod)
      (âˆ‘ i : Fin l.length, ((l.take i).map x).prod â€¢
        smulRight (proj (l.get i)) ((l.drop (.succ i)).map x).prod) x :=
  hasStrictFDerivAt_list_prod'.hasFDerivAt

@[fun_prop]
theorem hasFDerivAt_list_prod_finRange' {n : â„•} {x : Fin n â†’ ğ”¸} :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ ((List.finRange n).map x).prod)
      (âˆ‘ i : Fin n, (((List.finRange n).take i).map x).prod â€¢
        smulRight (proj i) (((List.finRange n).drop (.succ i)).map x).prod) x :=
  (hasStrictFDerivAt_list_prod_finRange').hasFDerivAt

@[fun_prop]
theorem hasFDerivAt_list_prod_attach' [DecidableEq Î¹] {l : List Î¹} {x : {i // i âˆˆ l} â†’ ğ”¸} :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (l.attach.map x).prod)
      (âˆ‘ i : Fin l.length, ((l.attach.take i).map x).prod â€¢
        smulRight (proj (l.attach.get (i.cast l.length_attach.symm)))
          ((l.attach.drop (.succ i)).map x).prod) x :=
  hasStrictFDerivAt_list_prod_attach'.hasFDerivAt

/--
Auxiliary lemma for `hasStrictFDerivAt_multiset_prod`.

For `NormedCommRing ğ”¸'`, can rewrite as `Multiset` using `Multiset.prod_coe`.
-/
@[fun_prop]
theorem hasStrictFDerivAt_list_prod [DecidableEq Î¹] [Fintype Î¹] {l : List Î¹} {x : Î¹ â†’ ğ”¸'} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (l.map x).prod)
      (l.map fun i â†¦ ((l.erase i).map x).prod â€¢ proj i).sum x := by
  refine .congr_fderiv hasStrictFDerivAt_list_prod' ?_
  conv_rhs => arg 1; arg 2; rw [â† List.finRange_map_get l]
  simp only [List.map_map, â† List.sum_toFinset _ (List.nodup_finRange _), List.toFinset_finRange,
    Function.comp_def, ((List.erase_get _).map _).prod_eq, List.eraseIdx_eq_take_drop_succ,
    List.map_append, List.prod_append]
  exact Finset.sum_congr rfl fun i _ â†¦ by
    ext; simp only [smul_apply, smulRight_apply, smul_eq_mul]; ring

@[fun_prop]
theorem hasStrictFDerivAt_multiset_prod [DecidableEq Î¹] [Fintype Î¹] {u : Multiset Î¹} {x : Î¹ â†’ ğ”¸'} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (u.map x).prod)
      (u.map (fun i â†¦ ((u.erase i).map x).prod â€¢ proj i)).sum x :=
  u.inductionOn fun l â†¦ by simpa using hasStrictFDerivAt_list_prod

@[fun_prop]
theorem hasFDerivAt_multiset_prod [DecidableEq Î¹] [Fintype Î¹] {u : Multiset Î¹} {x : Î¹ â†’ ğ”¸'} :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x â†¦ (u.map x).prod)
      (Multiset.sum (u.map (fun i â†¦ ((u.erase i).map x).prod â€¢ proj i))) x :=
  hasStrictFDerivAt_multiset_prod.hasFDerivAt

theorem hasStrictFDerivAt_finset_prod [DecidableEq Î¹] [Fintype Î¹] {x : Î¹ â†’ ğ”¸'} :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (âˆ i âˆˆ u, Â· i) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, x j) â€¢ proj i) x := by
  simp only [Finset.sum_eq_multiset_sum, Finset.prod_eq_multiset_prod]
  exact hasStrictFDerivAt_multiset_prod

theorem hasFDerivAt_finset_prod [DecidableEq Î¹] [Fintype Î¹] {x : Î¹ â†’ ğ”¸'} :
    HasFDerivAt (ğ•œ := ğ•œ) (âˆ i âˆˆ u, Â· i) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, x j) â€¢ proj i) x :=
  hasStrictFDerivAt_finset_prod.hasFDerivAt

section Comp

@[fun_prop]
theorem HasStrictFDerivAt.list_prod' {l : List Î¹} {x : E}
    (h : âˆ€ i âˆˆ l, HasStrictFDerivAt (f i Â·) (f' i) x) :
    HasStrictFDerivAt (fun x â†¦ (l.map (f Â· x)).prod)
      (âˆ‘ i : Fin l.length, ((l.take i).map (f Â· x)).prod â€¢
        smulRight (f' (l.get i)) ((l.drop (.succ i)).map (f Â· x)).prod) x := by
  simp only [â† List.finRange_map_get l, List.map_map]
  refine .congr_fderiv (hasStrictFDerivAt_list_prod_finRange'.comp x
    (hasStrictFDerivAt_pi.mpr fun i â†¦ h (l.get i) (l.get_mem i i.isLt))) ?_
  ext m
  simp [â† Function.comp_def (f Â· x) (l.get Â·), â† List.map_map, List.map_take, List.map_drop]

/--
Unlike `HasFDerivAt.finset_prod`, supports non-commutative multiply and duplicate elements.
-/
@[fun_prop]
theorem HasFDerivAt.list_prod' {l : List Î¹} {x : E}
    (h : âˆ€ i âˆˆ l, HasFDerivAt (f i Â·) (f' i) x) :
    HasFDerivAt (fun x â†¦ (l.map (f Â· x)).prod)
      (âˆ‘ i : Fin l.length, ((l.take i).map (f Â· x)).prod â€¢
        smulRight (f' (l.get i)) ((l.drop (.succ i)).map (f Â· x)).prod) x := by
  simp only [â† List.finRange_map_get l, List.map_map]
  refine .congr_fderiv (hasFDerivAt_list_prod_finRange'.comp x
    (hasFDerivAt_pi.mpr fun i â†¦ h (l.get i) (l.get_mem i i.isLt))) ?_
  ext m
  simp [â† Function.comp_def (f Â· x) (l.get Â·), â† List.map_map, List.map_take, List.map_drop]

@[fun_prop]
theorem HasFDerivWithinAt.list_prod' {l : List Î¹} {x : E}
    (h : âˆ€ i âˆˆ l, HasFDerivWithinAt (f i Â·) (f' i) s x) :
    HasFDerivWithinAt (fun x â†¦ (l.map (f Â· x)).prod)
      (âˆ‘ i : Fin l.length, ((l.take i).map (f Â· x)).prod â€¢
        smulRight (f' (l.get i)) ((l.drop (.succ i)).map (f Â· x)).prod) s x := by
  simp only [â† List.finRange_map_get l, List.map_map]
  refine .congr_fderiv (hasFDerivAt_list_prod_finRange'.comp_hasFDerivWithinAt x
    (hasFDerivWithinAt_pi.mpr fun i â†¦ h (l.get i) (l.get_mem i i.isLt))) ?_
  ext m
  simp [â† Function.comp_def (f Â· x) (l.get Â·), â† List.map_map, List.map_take, List.map_drop]

theorem fderiv_list_prod' {l : List Î¹} {x : E}
    (h : âˆ€ i âˆˆ l, DifferentiableAt ğ•œ (f i Â·) x) :
    fderiv ğ•œ (fun x â†¦ (l.map (f Â· x)).prod) x =
      âˆ‘ i : Fin l.length, ((l.take i).map (f Â· x)).prod â€¢
        smulRight (fderiv ğ•œ (fun x â†¦ f (l.get i) x) x)
          ((l.drop (.succ i)).map (f Â· x)).prod :=
  (HasFDerivAt.list_prod' fun i hi â†¦ (h i hi).hasFDerivAt).fderiv

theorem fderivWithin_list_prod' {l : List Î¹} {x : E}
    (hxs : UniqueDiffWithinAt ğ•œ s x) (h : âˆ€ i âˆˆ l, DifferentiableWithinAt ğ•œ (f i Â·) s x) :
    fderivWithin ğ•œ (fun x â†¦ (l.map (f Â· x)).prod) s x =
      âˆ‘ i : Fin l.length, ((l.take i).map (f Â· x)).prod â€¢
        smulRight (fderivWithin ğ•œ (fun x â†¦ f (l.get i) x) s x)
          ((l.drop (.succ i)).map (f Â· x)).prod :=
  (HasFDerivWithinAt.list_prod' fun i hi â†¦ (h i hi).hasFDerivWithinAt).fderivWithin hxs

@[fun_prop]
theorem HasStrictFDerivAt.multiset_prod [DecidableEq Î¹] {u : Multiset Î¹} {x : E}
    (h : âˆ€ i âˆˆ u, HasStrictFDerivAt (g i Â·) (g' i) x) :
    HasStrictFDerivAt (fun x â†¦ (u.map (g Â· x)).prod)
      (u.map fun i â†¦ ((u.erase i).map (g Â· x)).prod â€¢ g' i).sum x := by
  simp only [â† Multiset.attach_map_val u, Multiset.map_map]
  exact .congr_fderiv
    (hasStrictFDerivAt_multiset_prod.comp x <| hasStrictFDerivAt_pi.mpr fun i â†¦ h i i.prop)
    (by ext; simp [Finset.sum_multiset_map_count, u.erase_attach_map (g Â· x)])

/--
Unlike `HasFDerivAt.finset_prod`, supports duplicate elements.
-/
@[fun_prop]
theorem HasFDerivAt.multiset_prod [DecidableEq Î¹] {u : Multiset Î¹} {x : E}
    (h : âˆ€ i âˆˆ u, HasFDerivAt (g i Â·) (g' i) x) :
    HasFDerivAt (fun x â†¦ (u.map (g Â· x)).prod)
      (u.map fun i â†¦ ((u.erase i).map (g Â· x)).prod â€¢ g' i).sum x := by
  simp only [â† Multiset.attach_map_val u, Multiset.map_map]
  exact .congr_fderiv
    (hasFDerivAt_multiset_prod.comp x <| hasFDerivAt_pi.mpr fun i â†¦ h i i.prop)
    (by ext; simp [Finset.sum_multiset_map_count, u.erase_attach_map (g Â· x)])

@[fun_prop]
theorem HasFDerivWithinAt.multiset_prod [DecidableEq Î¹] {u : Multiset Î¹} {x : E}
    (h : âˆ€ i âˆˆ u, HasFDerivWithinAt (g i Â·) (g' i) s x) :
    HasFDerivWithinAt (fun x â†¦ (u.map (g Â· x)).prod)
      (u.map fun i â†¦ ((u.erase i).map (g Â· x)).prod â€¢ g' i).sum s x := by
  simp only [â† Multiset.attach_map_val u, Multiset.map_map]
  exact .congr_fderiv
    (hasFDerivAt_multiset_prod.comp_hasFDerivWithinAt x <|
      hasFDerivWithinAt_pi.mpr fun i â†¦ h i i.prop)
    (by ext; simp [Finset.sum_multiset_map_count, u.erase_attach_map (g Â· x)])

theorem fderiv_multiset_prod [DecidableEq Î¹] {u : Multiset Î¹} {x : E}
    (h : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (g i Â·) x) :
    fderiv ğ•œ (fun x â†¦ (u.map (g Â· x)).prod) x =
      (u.map fun i â†¦ ((u.erase i).map (g Â· x)).prod â€¢ fderiv ğ•œ (g i) x).sum :=
  (HasFDerivAt.multiset_prod fun i hi â†¦ (h i hi).hasFDerivAt).fderiv

theorem fderivWithin_multiset_prod [DecidableEq Î¹] {u : Multiset Î¹} {x : E}
    (hxs : UniqueDiffWithinAt ğ•œ s x) (h : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (g i Â·) s x) :
    fderivWithin ğ•œ (fun x â†¦ (u.map (g Â· x)).prod) s x =
      (u.map fun i â†¦ ((u.erase i).map (g Â· x)).prod â€¢ fderivWithin ğ•œ (g i) s x).sum :=
  (HasFDerivWithinAt.multiset_prod fun i hi â†¦ (h i hi).hasFDerivWithinAt).fderivWithin hxs

theorem HasStrictFDerivAt.finset_prod [DecidableEq Î¹] {x : E}
    (hg : âˆ€ i âˆˆ u, HasStrictFDerivAt (g i) (g' i) x) :
    HasStrictFDerivAt (âˆ i âˆˆ u, g i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, g j x) â€¢ g' i) x := by
  simpa [â† Finset.prod_attach u] using .congr_fderiv
    (hasStrictFDerivAt_finset_prod.comp x <| hasStrictFDerivAt_pi.mpr fun i â†¦ hg i i.prop)
    (by ext; simp [Finset.prod_erase_attach (g Â· x), â† u.sum_attach])

theorem HasFDerivAt.finset_prod [DecidableEq Î¹] {x : E}
    (hg : âˆ€ i âˆˆ u, HasFDerivAt (g i) (g' i) x) :
    HasFDerivAt (âˆ i âˆˆ u, g i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, g j x) â€¢ g' i) x := by
  simpa [â† Finset.prod_attach u] using .congr_fderiv
    (hasFDerivAt_finset_prod.comp x <| hasFDerivAt_pi.mpr fun i â†¦ hg i i.prop)
    (by ext; simp [Finset.prod_erase_attach (g Â· x), â† u.sum_attach])

theorem HasFDerivWithinAt.finset_prod [DecidableEq Î¹] {x : E}
    (hg : âˆ€ i âˆˆ u, HasFDerivWithinAt (g i) (g' i) s x) :
    HasFDerivWithinAt (âˆ i âˆˆ u, g i Â·) (âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, g j x) â€¢ g' i) s x := by
  simpa [â† Finset.prod_attach u] using .congr_fderiv
    (hasFDerivAt_finset_prod.comp_hasFDerivWithinAt x <|
      hasFDerivWithinAt_pi.mpr fun i â†¦ hg i i.prop)
    (by ext; simp [Finset.prod_erase_attach (g Â· x), â† u.sum_attach])

theorem fderiv_finset_prod [DecidableEq Î¹] {x : E} (hg : âˆ€ i âˆˆ u, DifferentiableAt ğ•œ (g i) x) :
    fderiv ğ•œ (âˆ i âˆˆ u, g i Â·) x = âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, (g j x)) â€¢ fderiv ğ•œ (g i) x :=
  (HasFDerivAt.finset_prod fun i hi â†¦ (hg i hi).hasFDerivAt).fderiv

theorem fderivWithin_finset_prod [DecidableEq Î¹] {x : E} (hxs : UniqueDiffWithinAt ğ•œ s x)
    (hg : âˆ€ i âˆˆ u, DifferentiableWithinAt ğ•œ (g i) s x) :
    fderivWithin ğ•œ (âˆ i âˆˆ u, g i Â·) s x =
      âˆ‘ i âˆˆ u, (âˆ j âˆˆ u.erase i, (g j x)) â€¢ fderivWithin ğ•œ (g i) s x :=
  (HasFDerivWithinAt.finset_prod fun i hi â†¦ (hg i hi).hasFDerivWithinAt).fderivWithin hxs

end Comp

end Prod

section AlgebraInverse

variable {R : Type*} [NormedRing R] [NormedAlgebra ğ•œ R] [CompleteSpace R]

open NormedRing ContinuousLinearMap Ring

/-- At an invertible element `x` of a normed algebra `R`, the FrÃ©chet derivative of the inversion
operation is the linear map `fun t â†¦ - xâ»Â¹ * t * xâ»Â¹`.

TODO: prove that `Ring.inverse` is analytic and use it to prove a `HasStrictFDerivAt` lemma.
TODO (low prio): prove a version without assumption `[CompleteSpace R]` but within the set of
units. -/
@[fun_prop]
theorem hasFDerivAt_ring_inverse (x : RË£) :
    HasFDerivAt Ring.inverse (-mulLeftRight ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹) x :=
  have : (fun t : R => Ring.inverse (â†‘x + t) - â†‘xâ»Â¹ + â†‘xâ»Â¹ * t * â†‘xâ»Â¹) =o[ğ“ 0] id :=
    (inverse_add_norm_diff_second_order x).trans_isLittleO (isLittleO_norm_pow_id one_lt_two)
  by simpa [hasFDerivAt_iff_isLittleO_nhds_zero] using this
#align has_fderiv_at_ring_inverse hasFDerivAt_ring_inverse

@[fun_prop]
theorem differentiableAt_inverse {x : R} (hx : IsUnit x) :
    DifferentiableAt ğ•œ (@Ring.inverse R _) x :=
  let âŸ¨u, huâŸ© := hx; hu â–¸ (hasFDerivAt_ring_inverse u).differentiableAt

@[fun_prop]
theorem differentiableWithinAt_inverse {x : R} (hx : IsUnit x) (s : Set R) :
    DifferentiableWithinAt ğ•œ (@Ring.inverse R _) s x :=
  (differentiableAt_inverse hx).differentiableWithinAt
#align differentiable_within_at_inverse differentiableWithinAt_inverse

@[fun_prop]
theorem differentiableOn_inverse : DifferentiableOn ğ•œ (@Ring.inverse R _) {x | IsUnit x} :=
  fun _x hx => differentiableWithinAt_inverse hx _
#align differentiable_on_inverse differentiableOn_inverse

theorem fderiv_inverse (x : RË£) : fderiv ğ•œ (@Ring.inverse R _) x = -mulLeftRight ğ•œ R â†‘xâ»Â¹ â†‘xâ»Â¹ :=
  (hasFDerivAt_ring_inverse x).fderiv
#align fderiv_inverse fderiv_inverse

variable {h : E â†’ R} {z : E} {S : Set E}

@[fun_prop]
theorem DifferentiableWithinAt.inverse (hf : DifferentiableWithinAt ğ•œ h S z) (hz : IsUnit (h z)) :
    DifferentiableWithinAt ğ•œ (fun x => Ring.inverse (h x)) S z :=
  (differentiableAt_inverse hz).comp_differentiableWithinAt z hf
#align differentiable_within_at.inverse DifferentiableWithinAt.inverse

@[simp, fun_prop]
theorem DifferentiableAt.inverse (hf : DifferentiableAt ğ•œ h z) (hz : IsUnit (h z)) :
    DifferentiableAt ğ•œ (fun x => Ring.inverse (h x)) z :=
  (differentiableAt_inverse hz).comp z hf
#align differentiable_at.inverse DifferentiableAt.inverse

@[fun_prop]
theorem DifferentiableOn.inverse (hf : DifferentiableOn ğ•œ h S) (hz : âˆ€ x âˆˆ S, IsUnit (h x)) :
    DifferentiableOn ğ•œ (fun x => Ring.inverse (h x)) S := fun x h => (hf x h).inverse (hz x h)
#align differentiable_on.inverse DifferentiableOn.inverse

@[simp, fun_prop]
theorem Differentiable.inverse (hf : Differentiable ğ•œ h) (hz : âˆ€ x, IsUnit (h x)) :
    Differentiable ğ•œ fun x => Ring.inverse (h x) := fun x => (hf x).inverse (hz x)
#align differentiable.inverse Differentiable.inverse

end AlgebraInverse

/-! ### Derivative of the inverse in a division ring

Note these lemmas are primed as they need `CompleteSpace R`, whereas the other lemmas in
`Mathlib/Analysis/Calculus/Deriv/Inv.lean` do not, but instead need `NontriviallyNormedField R`.
-/

section DivisionRingInverse

variable {R : Type*} [NormedDivisionRing R] [NormedAlgebra ğ•œ R] [CompleteSpace R]

open NormedRing ContinuousLinearMap Ring

/-- At an invertible element `x` of a normed division algebra `R`, the FrÃ©chet derivative of the
inversion operation is the linear map `fun t â†¦ - xâ»Â¹ * t * xâ»Â¹`. -/
@[fun_prop]
theorem hasFDerivAt_inv' {x : R} (hx : x â‰  0) :
    HasFDerivAt Inv.inv (-mulLeftRight ğ•œ R xâ»Â¹ xâ»Â¹) x := by
  simpa using hasFDerivAt_ring_inverse (Units.mk0 _ hx)
#align has_fderiv_at_inv' hasFDerivAt_inv'

@[fun_prop]
theorem differentiableAt_inv' {x : R} (hx : x â‰  0) : DifferentiableAt ğ•œ Inv.inv x :=
  (hasFDerivAt_inv' hx).differentiableAt
#align differentiable_at_inv' differentiableAt_inv'

@[fun_prop]
theorem differentiableWithinAt_inv' {x : R} (hx : x â‰  0) (s : Set R) :
    DifferentiableWithinAt ğ•œ (fun x => xâ»Â¹) s x :=
  (differentiableAt_inv' hx).differentiableWithinAt
#align differentiable_within_at_inv' differentiableWithinAt_inv'

@[fun_prop]
theorem differentiableOn_inv' : DifferentiableOn ğ•œ (fun x : R => xâ»Â¹) {x | x â‰  0} := fun _x hx =>
  differentiableWithinAt_inv' hx _
#align differentiable_on_inv' differentiableOn_inv'

/-- Non-commutative version of `fderiv_inv` -/
theorem fderiv_inv' {x : R} (hx : x â‰  0) : fderiv ğ•œ Inv.inv x = -mulLeftRight ğ•œ R xâ»Â¹ xâ»Â¹ :=
  (hasFDerivAt_inv' hx).fderiv
#align fderiv_inv' fderiv_inv'

/-- Non-commutative version of `fderivWithin_inv` -/
theorem fderivWithin_inv' {s : Set R} {x : R} (hx : x â‰  0) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x => xâ»Â¹) s x = -mulLeftRight ğ•œ R xâ»Â¹ xâ»Â¹ := by
  rw [DifferentiableAt.fderivWithin (differentiableAt_inv' hx) hxs]
  exact fderiv_inv' hx
#align fderiv_within_inv' fderivWithin_inv'

variable {h : E â†’ R} {z : E} {S : Set E}

@[fun_prop]
theorem DifferentiableWithinAt.inv' (hf : DifferentiableWithinAt ğ•œ h S z) (hz : h z â‰  0) :
    DifferentiableWithinAt ğ•œ (fun x => (h x)â»Â¹) S z :=
  (differentiableAt_inv' hz).comp_differentiableWithinAt z hf
#align differentiable_within_at.inv' DifferentiableWithinAt.inv'

@[simp, fun_prop]
theorem DifferentiableAt.inv' (hf : DifferentiableAt ğ•œ h z) (hz : h z â‰  0) :
    DifferentiableAt ğ•œ (fun x => (h x)â»Â¹) z :=
  (differentiableAt_inv' hz).comp z hf
#align differentiable_at.inv' DifferentiableAt.inv'

@[fun_prop]
theorem DifferentiableOn.inv' (hf : DifferentiableOn ğ•œ h S) (hz : âˆ€ x âˆˆ S, h x â‰  0) :
    DifferentiableOn ğ•œ (fun x => (h x)â»Â¹) S := fun x h => (hf x h).inv' (hz x h)
#align differentiable_on.inv' DifferentiableOn.inv'

@[simp, fun_prop]
theorem Differentiable.inv' (hf : Differentiable ğ•œ h) (hz : âˆ€ x, h x â‰  0) :
    Differentiable ğ•œ fun x => (h x)â»Â¹ := fun x => (hf x).inv' (hz x)
#align differentiable.inv' Differentiable.inv'

end DivisionRingInverse

end
