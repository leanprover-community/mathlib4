/-
Copyright (c) 2025 BenoÃ®t Guillemet. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: BenoÃ®t Guillemet
-/
import Mathlib.Algebra.MvPolynomial.PDeriv
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Mul
import Mathlib.Analysis.Calculus.Deriv.Pow
import Mathlib.Algebra.BigOperators.Group.Finset

/-!
# Derivatives of multivariate polynomials

In this file we prove that derivatives of multivariate polynomials in the analysis sense agree with
their derivatives in the algebraic sense.
-/

open scoped MvPolynomial

open ContinuousLinearMap (smulRight proj)

variable {Î¹ : Type} [Fintype Î¹] [DecidableEq Î¹]
variable {ğ•œ : Type} [NontriviallyNormedField ğ•œ]
variable {x : Î¹ â†’ ğ•œ} {s : Set (Î¹ â†’ ğ•œ)}

namespace MvPolynomial

/-! ### Derivative of a multivariate polynomial -/

variable {R : Type} [CommSemiring R] [Algebra R ğ•œ]
variable (p : MvPolynomial Î¹ ğ•œ) (q : MvPolynomial Î¹ R)

theorem hasStrictFDerivAt_monomial {u : Î¹ â†’â‚€ â„•} (x : Î¹ â†’ ğ•œ) :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x => âˆ i : Î¹, x i ^ u i)
    (âˆ‘ i âˆˆ u.support, (âˆ j âˆˆ u.support.erase i, x j ^ u j) â€¢ u i â€¢ x i ^ (u i - 1) â€¢ proj i) x := by
  simp_rw [â† u.prod_fintype _ (fun _ => pow_zero _)]
  refine HasStrictFDerivAt.finset_prod (fun i _ => ?_)
  have : (u i â€¢ x i ^ (u i - 1) â€¢ proj (R := ğ•œ) (Ï† := fun _ => ğ•œ) i) =
      (smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (u i * x i ^ (u i - 1))).comp (proj i) := by
    ext x
    simp [mul_comm, mul_assoc]
  rw [this]
  exact HasStrictFDerivAt.comp x (hasStrictDerivAt_pow (u i) (x i)).hasStrictFDerivAt
    (hasStrictFDerivAt_apply i x)

lemma prod_pow_sub_single_eq_prod_erase_mul {u : Î¹ â†’â‚€ â„•} {i : Î¹} (hi : i âˆˆ u.support) (x : Î¹ â†’ ğ•œ) :
    âˆ j : Î¹, x j ^ (u j - Finsupp.single i 1 j)
    = (âˆ j âˆˆ u.support.erase i, x j ^ u j) * x i ^ (u i - 1) := by
  rw [â† Finset.prod_subset u.support.subset_univ (fun j _ hj => ?_),
    â† Finset.prod_erase_mul _ _ hi, Finsupp.single_apply, if_pos rfl,
    Finset.prod_congr rfl (fun j hj => ?_)]
  Â· rw [Finsupp.single_apply, if_neg (Finset.ne_of_mem_erase hj).symm, tsub_zero]
  Â· rw [Finsupp.single_apply, if_neg (fun h => hj (by rwa [â† h])), tsub_zero,
      Finsupp.not_mem_support_iff.1 hj, pow_zero]

theorem hasStrictFDerivAt_monomial' {u : Î¹ â†’â‚€ â„•} (x : Î¹ â†’ ğ•œ) :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x => âˆ i : Î¹, x i ^ u i)
    (âˆ‘ i : Î¹, u i â€¢ (âˆ j : Î¹, x j ^ (u j - (Finsupp.single i 1) j)) â€¢ (proj i)) x := by
  rw [â† u.sum_fintype (fun _ k => k â€¢ _) (fun _ => zero_smul _ _)]
  show HasStrictFDerivAt _ (âˆ‘ i âˆˆ u.support, _ â€¢ _) _
  rw [u.support.sum_congr rfl (fun i hi =>
    by rw [prod_pow_sub_single_eq_prod_erase_mul hi, smul_comm, mul_smul, â† smul_comm (u i)])]
  exact hasStrictFDerivAt_monomial x

/-- The derivative (in the analysis sense) of a multivariate polynomial `p` is given by `pderiv`. -/
protected theorem hasStrictFDerivAt (x : Î¹ â†’ ğ•œ) :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x => eval x p)
    (âˆ‘ i : Î¹, (eval x (pderiv i p)) â€¢ (proj i)) x := by
  induction p using MvPolynomial.induction_on' with
  | h1 u a => simp only [eval_monomial, Finsupp.prod_pow, pderiv_monomial, Finsupp.coe_tsub,
                Pi.sub_apply, mul_smul, â† Finset.smul_sum]
              apply HasStrictFDerivAt.const_mul
              rw [Finset.sum_congr (Î² := (Î¹ â†’ ğ•œ) â†’L[ğ•œ] ğ•œ) rfl
                (fun _ _ => Nat.cast_smul_eq_nsmul _ _ _)]
              exact hasStrictFDerivAt_monomial' x
  | h2 p q hp hq => simp only [map_add]
                    rw [Finset.sum_congr (Î² := (Î¹ â†’ ğ•œ) â†’L[ğ•œ] ğ•œ) rfl (fun _ _ => add_smul _ _ _),
                      Finset.sum_add_distrib]
                    exact hp.add hq

protected theorem hasStrictFDerivAt_aeval (x : Î¹ â†’ ğ•œ) :
    HasStrictFDerivAt (ğ•œ := ğ•œ) (fun x => aeval x q)
    (âˆ‘ i : Î¹, (aeval x (pderiv i q)) â€¢ (proj i)) x := by
  simpa only [aeval_def, evalâ‚‚_eq_eval_map, pderiv_map] using
    (q.map (algebraMap R ğ•œ)).hasStrictFDerivAt x

/-- The derivative (in the analysis sense) of a polynomial `p` is given by `pderiv`. -/
protected theorem hasFDerivAt (x : Î¹ â†’ ğ•œ) :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x => eval x p)
    (âˆ‘ i : Î¹, (eval x (pderiv i p)) â€¢ (proj i)) x :=
  (p.hasStrictFDerivAt x).hasFDerivAt

protected theorem hasFDerivAt_aeval (x : Î¹ â†’ ğ•œ) :
    HasFDerivAt (ğ•œ := ğ•œ) (fun x => aeval x q)
    (âˆ‘ i : Î¹, (aeval x (pderiv i q)) â€¢ (proj i)) x :=
  (q.hasStrictFDerivAt_aeval x).hasFDerivAt

protected theorem hasFDerivWithinAt (x : Î¹ â†’ ğ•œ) (s : Set (Î¹ â†’ ğ•œ)) :
    HasFDerivWithinAt (ğ•œ := ğ•œ) (fun x => eval x p)
    (âˆ‘ i : Î¹, (eval x (pderiv i p)) â€¢ (proj i)) s x :=
  (p.hasFDerivAt x).hasFDerivWithinAt

protected theorem hasFDerivWithinAt_aeval (x : Î¹ â†’ ğ•œ) (s : Set (Î¹ â†’ ğ•œ)) :
    HasFDerivWithinAt (ğ•œ := ğ•œ) (fun x => aeval x q)
    (âˆ‘ i : Î¹, (aeval x (pderiv i q)) â€¢ (proj i)) s x :=
  (q.hasFDerivAt_aeval x).hasFDerivWithinAt

protected theorem differentiableAt :
    DifferentiableAt ğ•œ (fun x => eval x p) x :=
  (p.hasStrictFDerivAt x).differentiableAt

protected theorem differentiableAt_aeval :
    DifferentiableAt ğ•œ (fun x => aeval x q) x :=
  (q.hasStrictFDerivAt_aeval x).differentiableAt

protected theorem differentiableWithinAt (s : Set (Î¹ â†’ ğ•œ)) :
    DifferentiableWithinAt ğ•œ (fun x => eval x p) s x :=
  p.differentiableAt.differentiableWithinAt

protected theorem differentiableWithinAt_aeval (s : Set (Î¹ â†’ ğ•œ)) :
    DifferentiableWithinAt ğ•œ (fun x => aeval x q) s x :=
  q.differentiableAt_aeval.differentiableWithinAt

protected theorem differentiable :
    Differentiable ğ•œ (fun x => eval x p) :=
  fun _ => p.differentiableAt

protected theorem differentiable_aeval :
    Differentiable ğ•œ (fun x : Î¹ â†’ ğ•œ => aeval x q) :=
  fun _ => q.differentiableAt_aeval

protected theorem differentiableOn (s : Set (Î¹ â†’ ğ•œ)) :
    DifferentiableOn ğ•œ (fun x => eval x p) s :=
  p.differentiable.differentiableOn

protected theorem differentiableOn_aeval (s : Set (Î¹ â†’ ğ•œ)) :
    DifferentiableOn ğ•œ (fun x : Î¹ â†’ ğ•œ => aeval x q) s :=
  q.differentiable_aeval.differentiableOn

@[simp]
protected theorem fderiv :
    fderiv ğ•œ (fun x => eval x p) x = âˆ‘ i : Î¹, (eval x (pderiv i p)) â€¢ (proj i) :=
  (p.hasFDerivAt x).fderiv

@[simp]
protected theorem fderiv_aeval :
    fderiv ğ•œ (fun x => aeval x q) x = âˆ‘ i : Î¹, (aeval x (pderiv i q)) â€¢ (proj i) :=
  (q.hasFDerivAt_aeval x).fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x => eval x p) s x = âˆ‘ i : Î¹, (eval x (pderiv i p)) â€¢ (proj i) := by
  rw [DifferentiableAt.fderivWithin p.differentiableAt hxs]
  exact p.fderiv

protected theorem derivWithin_aeval (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x => aeval x q) s x = âˆ‘ i : Î¹, (aeval x (pderiv i q)) â€¢ (proj i) := by
  simpa only [aeval_def, evalâ‚‚_eq_eval_map, pderiv_map] using
    (q.map (algebraMap R ğ•œ)).fderivWithin hxs

end MvPolynomial
