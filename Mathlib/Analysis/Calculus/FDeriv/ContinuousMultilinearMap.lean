/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Calculus.FDeriv.CompCLM

/-!
# Derivatives of operations on continuous multilinear maps

In this file,

- `Î¹` is an index type (`Fin n` in many applications);
- `E`, `F i`, `G i`, `H`, are normed spaces for each `i : Î¹`;
- `f x` is a continuous multilinear map from `Î  i, G i` to `H`, depending on a parameter `x : E`;
- for each `i : Î¹`, `g i x` is a continuous linear map `F i â†’ G i`, depending on a parameter `x : E`;

Given this data, for each `x` we can define a continuous multilinear map from `Î  i, F i` to `H`
given by `(f x).compContinuousLinearMap (fun i â†¦ g i x) v = f x (fun i â†¦ g i x (v i))`.

As a map between functional spaces,
`ContinuousMultilinearMap.compContinuousLinearMap` is multilinear in `(f; g i)`.
Thus its derivative with respect to each map (`f` or `g i`)
is given by substituting `f'` or `g' i` instead of `f` or `g i`
in `(f x).compContinuousLinearMap (fun i â†¦ g i x)`,
and the full differential is given by the sum of these terms.

In terms of bundled maps, the derivative with respect to `f`
is given by `ContinuousMultilinearMap.compContinuousLinearMapL`
and the sum of terms that represent the derivatives with respect to `g i`
is given by `ContinuousMultilinearMap.fderivCompContinuousLinearMap`.

All statements in the first section are claiming this, for various notions of differentiation. 
The second section deduces the corresponding differentiability results when `Î¹` is finite.
-/

variable {ğ•œ Î¹ E : Type*} {F G : Î¹ â†’ Type*} {H : Type*}
  [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [âˆ€ i, NormedAddCommGroup (F i)] [âˆ€ i, NormedSpace ğ•œ (F i)]
  [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, NormedSpace ğ•œ (G i)]
  [NormedAddCommGroup H] [NormedSpace ğ•œ H]
  {f : E â†’ ContinuousMultilinearMap ğ•œ G H} {f' : E â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ G H}
  {g : âˆ€ i, E â†’ F i â†’L[ğ•œ] G i} {g' : âˆ€ i, E â†’L[ğ•œ] F i â†’L[ğ•œ] G i}
  {s : Set E} {x : E}

open ContinuousMultilinearMap

section HasFDerivAt

variable [Fintype Î¹] [DecidableEq Î¹]

theorem ContinuousMultilinearMap.hasStrictFDerivAt_compContinuousLinearMap
    (fg : ContinuousMultilinearMap ğ•œ G H Ã— âˆ€ i, F i â†’L[ğ•œ] G i) :
    HasStrictFDerivAt
      (fun fg : ContinuousMultilinearMap ğ•œ G H Ã— âˆ€ i, F i â†’L[ğ•œ] G i â†¦
        fg.1.compContinuousLinearMap fg.2)
      (compContinuousLinearMapL fg.2 âˆ˜L .fst _ _ _ +
        fg.1.fderivCompContinuousLinearMap fg.2 âˆ˜L .snd _ _ _)
      fg := by
  have := (compContinuousLinearMapContinuousMultilinear ğ•œ F G H).hasStrictFDerivAt fg.2
  convert this.comp fg hasStrictFDerivAt_snd |>.clm_apply hasStrictFDerivAt_fst
  ext <;> simp [fderivCompContinuousLinearMap]

theorem HasStrictFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasStrictFDerivAt f f' x) (hg : âˆ€ i, HasStrictFDerivAt (g i) (g' i) x) :
    HasStrictFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g Â· x) âˆ˜L .pi g') x :=
  hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x))
    |>.comp x (hf.prodMk (hasStrictFDerivAt_pi.2 hg))

theorem HasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasFDerivAt f f' x) (hg : âˆ€ i, HasFDerivAt (g i) (g' i) x) :
    HasFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g Â· x) âˆ˜L .pi g') x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x)) |>.hasFDerivAt
    |>.comp x (hf.prodMk (hasFDerivAt_pi.2 hg))

theorem HasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasFDerivWithinAt f f' s x) (hg : âˆ€ i, HasFDerivWithinAt (g i) (g' i) s x) :
    HasFDerivWithinAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g Â· x) âˆ˜L .pi g') s x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x)) |>.hasFDerivAt
    |>.comp_hasFDerivWithinAt x (hf.prodMk (hasFDerivWithinAt_pi.2 hg))

theorem fderivWithin_continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ğ•œ f s x) (hg : âˆ€ i, DifferentiableWithinAt ğ•œ (g i) s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s x =
      compContinuousLinearMapL (g Â· x) âˆ˜L fderivWithin ğ•œ f s x +
        (f x).fderivCompContinuousLinearMap (g Â· x) âˆ˜L .pi fun i â†¦ fderivWithin ğ•œ (g i) s x :=
  hf.hasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivWithinAt) |>.fderivWithin hs

theorem fderiv_continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableAt ğ•œ f x) (hg : âˆ€ i, DifferentiableAt ğ•œ (g i) x) :
    fderiv ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) x =
      compContinuousLinearMapL (g Â· x) âˆ˜L fderiv ğ•œ f x +
        (f x).fderivCompContinuousLinearMap (g Â· x) âˆ˜L .pi fun i â†¦ fderiv ğ•œ (g i) x :=
  hf.hasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivAt) |>.fderiv

end HasFDerivAt

variable [Finite Î¹]

theorem DifferentiableWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ğ•œ f s x) (hg : âˆ€ i, DifferentiableWithinAt ğ•œ (g i) s x) :
    DifferentiableWithinAt ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivWithinAt) |>.differentiableWithinAt

theorem DifferentiableAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableAt ğ•œ f x) (hg : âˆ€ i, DifferentiableAt ğ•œ (g i) x) :
    DifferentiableAt ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivAt) |>.differentiableAt

theorem DifferentiableOn.continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableOn ğ•œ f s) (hg : âˆ€ i, DifferentiableOn ğ•œ (g i) s) :
    DifferentiableOn ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s := fun x hx â†¦
  (hf x hx).continuousMultilinearMapCompContinuousLinearMap (hg Â· x hx)

theorem Differentiable.continuousMultilinearMapCompContinuousLinearMap
    (hf : Differentiable ğ•œ f) (hg : âˆ€ i, Differentiable ğ•œ (g i)) :
    Differentiable ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) := fun x â†¦
  (hf x).continuousMultilinearMapCompContinuousLinearMap (hg Â· x)
