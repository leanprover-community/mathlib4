/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Calculus.FDeriv.CompCLM

/-!
# Derivatives of operations on continuous multilinear maps

In this file we prove a formula
for the derivative of `fun x â†¦ (f x).compContinuousLinearMap (g Â· x)`,
where `f x` is a continuous multilinear map
and `g i x`, `i : Î¹`, is a family of continuous linear maps.
-/

variable {ð•œ Î¹ E : Type*} {F G : Î¹ â†’ Type*} {H : Type*}
  [NontriviallyNormedField ð•œ]
  [NormedAddCommGroup E] [NormedSpace ð•œ E]
  [âˆ€ i, NormedAddCommGroup (F i)] [âˆ€ i, NormedSpace ð•œ (F i)]
  [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, NormedSpace ð•œ (G i)]
  [NormedAddCommGroup H] [NormedSpace ð•œ H]
  {f : E â†’ ContinuousMultilinearMap ð•œ G H} {f' : E â†’L[ð•œ] ContinuousMultilinearMap ð•œ G H}
  {g : âˆ€ i, E â†’ F i â†’L[ð•œ] G i} {g' : âˆ€ i, E â†’L[ð•œ] F i â†’L[ð•œ] G i}
  {s : Set E} {x : E}

open ContinuousMultilinearMap

section HasFDerivAt

variable [Fintype Î¹] [DecidableEq Î¹]

theorem ContinuousMultilinearMap.hasStrictFDerivAt_compContinuousLinearMap
    (fg : ContinuousMultilinearMap ð•œ G H Ã— âˆ€ i, F i â†’L[ð•œ] G i) :
    HasStrictFDerivAt
      (fun fg : ContinuousMultilinearMap ð•œ G H Ã— âˆ€ i, F i â†’L[ð•œ] G i â†¦
        fg.1.compContinuousLinearMap fg.2)
      (compContinuousLinearMapL fg.2 âˆ˜L .fst _ _ _ +
        ((ContinuousLinearMap.apply _ _ fg.1).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv fg.2 âˆ˜L .snd _ _ _)
      fg := by
  have := (compContinuousLinearMapContinuousMultilinear ð•œ F G H).hasStrictFDerivAt fg.2
  convert this.comp fg hasStrictFDerivAt_snd |>.clm_apply hasStrictFDerivAt_fst
  ext <;> simp

theorem HasStrictFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasStrictFDerivAt f f' x) (hg : âˆ€ i, HasStrictFDerivAt (g i) (g' i) x) :
    HasStrictFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        ((ContinuousLinearMap.apply _ _ (f x)).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv (g Â· x) âˆ˜L
        .pi g') x :=
  hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x)) |>.comp x
    (hf.prodMk (hasStrictFDerivAt_pi.2 hg))

theorem HasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasFDerivAt f f' x) (hg : âˆ€ i, HasFDerivAt (g i) (g' i) x) :
    HasFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        ((ContinuousLinearMap.apply _ _ (f x)).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv (g Â· x) âˆ˜L
        .pi g') x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x)) |>.hasFDerivAt |>.comp x
    (hf.prodMk (hasFDerivAt_pi.2 hg))

theorem HasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : HasFDerivWithinAt f f' s x) (hg : âˆ€ i, HasFDerivWithinAt (g i) (g' i) s x) :
    HasFDerivWithinAt (fun x â†¦ (f x).compContinuousLinearMap (g Â· x))
      (compContinuousLinearMapL (g Â· x) âˆ˜L f' +
        ((ContinuousLinearMap.apply _ _ (f x)).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv (g Â· x) âˆ˜L
        .pi g') s x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g Â· x)) |>.hasFDerivAt
    |>.comp_hasFDerivWithinAt x (hf.prodMk (hasFDerivWithinAt_pi.2 hg))

theorem fderivWithin_continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ð•œ f s x) (hg : âˆ€ i, DifferentiableWithinAt ð•œ (g i) s x)
    (hs : UniqueDiffWithinAt ð•œ s x) :
    fderivWithin ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s x =
      compContinuousLinearMapL (g Â· x) âˆ˜L fderivWithin ð•œ f s x +
        ((ContinuousLinearMap.apply _ _ (f x)).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv (g Â· x) âˆ˜L
        .pi fun i â†¦ fderivWithin ð•œ (g i) s x :=
  hf.hasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivWithinAt) |>.fderivWithin hs

theorem fderiv_continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableAt ð•œ f x) (hg : âˆ€ i, DifferentiableAt ð•œ (g i) x) :
    fderiv ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) x =
      compContinuousLinearMapL (g Â· x) âˆ˜L fderiv ð•œ f x +
        ((ContinuousLinearMap.apply _ _ (f x)).compContinuousMultilinearMap
          (compContinuousLinearMapContinuousMultilinear ð•œ F G H)).linearDeriv (g Â· x) âˆ˜L
        .pi fun i â†¦ fderiv ð•œ (g i) x :=
  hf.hasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivAt) |>.fderiv

theorem fderivWithin_continuousMultilinearMapCompContinuousLinearMap_apply
    (hf : DifferentiableWithinAt ð•œ f s x) (hg : âˆ€ i, DifferentiableWithinAt ð•œ (g i) s x)
    (hs : UniqueDiffWithinAt ð•œ s x) (dx : E) (v : âˆ€ i, F i) :
    fderivWithin ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s x dx v =
      fderivWithin ð•œ f s x dx (fun i â†¦ g i x (v i)) +
        âˆ‘ i, (f x)
          (Function.update (fun j â†¦ g j x (v j)) i (fderivWithin ð•œ (g i) s x dx (v i))) := by
  rw [fderivWithin_continuousMultilinearMapCompContinuousLinearMap hf hg hs]
  simp [Function.apply_update fun (i : Î¹) (g : F i â†’L[ð•œ] G i) â†¦ g (v i)]

theorem fderiv_continuousMultilinearMapCompContinuousLinearMap_apply
    (hf : DifferentiableAt ð•œ f x) (hg : âˆ€ i, DifferentiableAt ð•œ (g i) x) (dx : E) (v : âˆ€ i, F i) :
    fderiv ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) x dx v =
      fderiv ð•œ f x dx (fun i â†¦ g i x (v i)) +
        âˆ‘ i, (f x) (Function.update (fun j â†¦ g j x (v j)) i (fderiv ð•œ (g i) x dx (v i))) := by
  simp only [â† fderivWithin_univ, â† differentiableWithinAt_univ] at *
  exact fderivWithin_continuousMultilinearMapCompContinuousLinearMap_apply hf hg
    uniqueDiffWithinAt_univ dx v

end HasFDerivAt

variable [Finite Î¹]

theorem DifferentiableWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ð•œ f s x) (hg : âˆ€ i, DifferentiableWithinAt ð•œ (g i) s x) :
    DifferentiableWithinAt ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) s x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivWithinAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivWithinAt) |>.differentiableWithinAt

theorem DifferentiableAt.continuousMultilinearMapCompContinuousLinearMap
    (hf : DifferentiableAt ð•œ f x) (hg : âˆ€ i, DifferentiableAt ð•œ (g i) x) :
    DifferentiableAt ð•œ (fun x â†¦ (f x).compContinuousLinearMap (g Â· x)) x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivAt.continuousMultilinearMapCompContinuousLinearMap
    (fun i â†¦ (hg i).hasFDerivAt) |>.differentiableAt
