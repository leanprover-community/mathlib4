/-
Copyright (c) 2025 Igor Khavkine, A Tucker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine, A Tucker
-/
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Partial derivatives

Results in this file relate the partial derivatives of a bivariate function to its differentiability
in the product space.

## Main statements

* `hasStrictFDerivAt_uncurry_coprod`: establishing strict differentiability at a point `x` in the
  product space, this requires that both partial derivatives exist in a neighbourhood of `x` and be
  continuous at `x`.

* `hasFDerivWithinAt_uncurry_coprod_of_continuousWithinAt_snd`: this weaker result requires that
  both partial derivatives exist, but only the second need exist in a neighbourhood of `x` (and be
  continuous at `x`).

* `HasFDerivWithinAt.partial_fst` , `HasFDerivWithinAt.partial_snd`: if `f` is differentiable
   with derivative `f' z` at `z`, then the partial derivatives of `(f ‚àò (z.1, ¬∑))`
   and `(f ‚àò (¬∑, z.2))` are respectively `(f' z) ‚àòL (.inl ùïú E F)` and
   `(f' z) ‚àòL (.inr ùïú E F)`. If `f'` is continuous, then continuity can be obtained by
   by combining `Continuous(|At|On|WithinAt).clm_comp` and `Continuous(|At|On|WithinAt)_const`.

* `hasFDerivWithinAt_of_partial_snd_continuousWithinAt_prod_open` : a weak sufficient condition
  for differeniability of `f` at `z = (x,y)` is that, say, the first derivative (within set `s`)
  `f'xz` exists at `z`, while the second partial derivative `f'y z` exists and is jointly
  continuous at `z` in the product set `s √óÀ¢ t` where `t` is open, with the derivative given by
  `f'z = f'xz.coprod (f'y z)`. `hasFDerivWithinAt_of_partial_fst_continuousWithinAt_prod_open` has
  the roles of the partial derivatives reversed.

  The proofs follow ¬ß9.8.1 from Dieudonn√©'s *Foundations of Modern Analysis* (1969).

* `hasFDerivWithinAt_continuous(On|WithinAt)_of_partial_continuous(On|WithinAt)_open`: when
  both partial derivatives exist and are continuous on (or at `z` in) an open set `u`, this more
  convenient theorem directly deduces continous differentiability on (or at `z` in) `u`.
-/

open Asymptotics Filter
open scoped Convex Topology

section aux

variable {E F : Type*} [TopologicalSpace E] [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
variable {s : Set E} {t : Set F} {Œæ : E} {x : F} {y z : E ‚Üí F}

theorem eventually_segment {r : E ‚Üí F ‚Üí Prop}
    (hy : Tendsto y (ùìù[s] Œæ) (ùìù x)) (hz : Tendsto z (ùìù[s] Œæ) (ùìù x))
    (seg : ‚àÄ·∂† œá in ùìù[s] Œæ, [z œá -[‚Ñù] y œá] ‚äÜ t) (hr : ‚àÄ·∂† p in ùìù[s √óÀ¢ t] (Œæ, x), r p.1 p.2) :
    ‚àÄ·∂† œá in ùìù[s] Œæ, ‚àÄ v ‚àà [z œá -[‚Ñù] y œá], r œá v := by
  rw [nhdsWithin_prod_eq, eventually_prod_iff] at hr
  obtain ‚ü®p, hp, q, hq, hr‚ü© := hr
  rw [eventually_iff, Metric.mem_nhdsWithin_iff] at hq
  obtain ‚ü®Œ¥, hŒ¥, hq‚ü© := hq
  rw [Metric.tendsto_nhds] at hy hz
  filter_upwards [hp, hy Œ¥ hŒ¥, hz Œ¥ hŒ¥, seg] with œá hp hy hz seg
  have := convex_iff_segment_subset.mp (convex_ball x Œ¥) hz hy
  exact fun v hv => hr hp <| hq ‚ü®this hv, seg hv‚ü©

variable {ùïú G : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
variable [NormedSpace ùïú F] [NormedAddCommGroup G] [NormedSpace ùïú G]

theorem isLittleO_sub_sub_fderiv {f : E ‚Üí F ‚Üí G} {f' : E ‚Üí F ‚Üí F ‚ÜíL[ùïú] G}
    (hy : Tendsto y (ùìù[s] Œæ) (ùìù x)) (hz : Tendsto z (ùìù[s] Œæ) (ùìù x))
    (seg : ‚àÄ·∂† œá in ùìù[s] Œæ, [z œá -[‚Ñù] y œá] ‚äÜ t) (cf' : ContinuousWithinAt ‚Üøf' (s √óÀ¢ t) (Œæ, x))
    (df' : ‚àÄ·∂† p in ùìù[s √óÀ¢ t] (Œæ, x), HasFDerivWithinAt (f p.1) (f' p.1 p.2) t p.2) :
    (fun œá => f œá (y œá) - f œá (z œá) - f' Œæ x (y œá - z œá)) =o[ùìù[s] Œæ] (fun œá => y œá - z œá) := by
  rw [isLittleO_iff]
  intro Œµ hŒµ
  replace cf' : ‚àÄ·∂† œá in ùìù[s] Œæ, ‚àÄ v ‚àà [z œá -[‚Ñù] y œá], dist (f' œá v) (f' Œæ x) < Œµ := by
    rw [Metric.continuousWithinAt_iff'] at cf'
    exact eventually_segment hy hz seg (cf' Œµ hŒµ)
  replace df' : ‚àÄ·∂† œá in ùìù[s] Œæ, ‚àÄ v ‚àà [z œá -[‚Ñù] y œá], HasFDerivWithinAt (f œá) (f' œá v) t v :=
    eventually_segment hy hz seg df'
  filter_upwards [seg, cf', df'] with œá seg cf' df'
  exact Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (fun v hv => (df' v hv).mono seg) (fun v hv => (cf' v hv).le)
    (convex_segment ..) (left_mem_segment ..) (right_mem_segment ..)

end aux

variable {ùïú E‚ÇÅ E‚ÇÇ F : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
variable [NormedAddCommGroup E‚ÇÅ] [NormedAddCommGroup E‚ÇÇ] [NormedAddCommGroup F]
variable [NormedSpace ùïú E‚ÇÅ] [NormedSpace ùïú E‚ÇÇ] [NormedSpace ùïú F]

/-- If a bivariate function `f` has partial derivatives `f‚ÇÅ` and `f‚ÇÇ` in a neighbourhood of a point
`(x‚ÇÅ, x‚ÇÇ)` and if they are continuous at that point then the uncurried function `‚Üøf` is strictly
differentiable there with its derivative mapping `(h‚ÇÅ, h‚ÇÇ)` to `f‚ÇÅ x‚ÇÅ x‚ÇÇ h‚ÇÅ + f‚ÇÇ x‚ÇÅ x‚ÇÇ h‚ÇÇ`. -/
theorem hasStrictFDerivAt_uncurry_coprod {f : E‚ÇÅ ‚Üí E‚ÇÇ ‚Üí F} {x‚ÇÅ : E‚ÇÅ} {x‚ÇÇ : E‚ÇÇ}
    {f‚ÇÅ : E‚ÇÅ ‚Üí E‚ÇÇ ‚Üí E‚ÇÅ ‚ÜíL[ùïú] F} (cf‚ÇÅ : ContinuousAt ‚Üøf‚ÇÅ (x‚ÇÅ, x‚ÇÇ))
    (df‚ÇÅ : ‚àÄ·∂† y in ùìù (x‚ÇÅ, x‚ÇÇ), HasFDerivAt (f ¬∑ y.2) (f‚ÇÅ y.1 y.2) y.1)
    {f‚ÇÇ : E‚ÇÅ ‚Üí E‚ÇÇ ‚Üí E‚ÇÇ ‚ÜíL[ùïú] F} (cf‚ÇÇ : ContinuousAt ‚Üøf‚ÇÇ (x‚ÇÅ, x‚ÇÇ))
    (df‚ÇÇ : ‚àÄ·∂† y in ùìù (x‚ÇÅ, x‚ÇÇ), HasFDerivAt (f y.1 ¬∑) (f‚ÇÇ y.1 y.2) y.2) :
    HasStrictFDerivAt ‚Üøf ((f‚ÇÅ x‚ÇÅ x‚ÇÇ).coprod (f‚ÇÇ x‚ÇÅ x‚ÇÇ)) (x‚ÇÅ, x‚ÇÇ) := by
  unfold ContinuousAt at cf‚ÇÅ cf‚ÇÇ
  rw [nhds_prod_eq] at cf‚ÇÅ cf‚ÇÇ df‚ÇÅ df‚ÇÇ
  rw [hasStrictFDerivAt_iff_isLittleO]
  calc
    fun (y, z) => f y.1 y.2 - f z.1 z.2 - ((f‚ÇÅ x‚ÇÅ x‚ÇÇ).coprod (f‚ÇÇ x‚ÇÅ x‚ÇÇ)) (y - z)
    _ = fun (y, z) => (f y.1 z.2 - f z.1 z.2 - f‚ÇÅ x‚ÇÅ x‚ÇÇ (y.1 - z.1))
          + (f y.1 y.2 - f y.1 z.2 - f‚ÇÇ x‚ÇÅ x‚ÇÇ (y.2 - z.2)) := by
      ext
      dsimp only [ContinuousLinearMap.coprod_apply]
      abel
    _ =o[ùìù ((x‚ÇÅ, x‚ÇÇ), (x‚ÇÅ, x‚ÇÇ))] fun (y, z) => y - z := by
      let : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
      apply IsLittleO.add
      ¬∑ calc
          fun (y, z) => f y.1 z.2 - f z.1 z.2 - f‚ÇÅ x‚ÇÅ x‚ÇÇ (y.1 - z.1)
          _ =o[ùìù ((x‚ÇÅ, x‚ÇÇ), (x‚ÇÅ, x‚ÇÇ))] (fun (y, z) => y.1 - z.1 : _ ‚Üí E‚ÇÅ) := by
            rw [‚Üê nhdsWithin_univ]
            have h := tendsto_snd.prodMk <| tendsto_snd.comp <| tendsto_snd.comp <|
              tendsto_fst (f := (ùìù x‚ÇÅ √óÀ¢ ùìù x‚ÇÇ) √óÀ¢ (ùìù x‚ÇÅ √óÀ¢ ùìù x‚ÇÇ)) (g := ùìù x‚ÇÅ)
            let : NormedSpace ‚Ñù E‚ÇÅ := RestrictScalars.normedSpace ‚Ñù ùïú E‚ÇÅ
            apply isLittleO_sub_sub_fderiv (E := (E‚ÇÅ √ó E‚ÇÇ) √ó (E‚ÇÅ √ó E‚ÇÇ))
              (t := Set.univ) (f := fun (y, z) u => f u z.2) (f' := fun (y, z) u => f‚ÇÅ u z.2)
            ¬∑ simpa [nhds_prod_eq] using tendsto_fst.comp tendsto_fst
            ¬∑ simpa [nhds_prod_eq] using tendsto_fst.comp tendsto_snd
            ¬∑ simp
            ¬∑ simpa [continuousWithinAt_univ, ContinuousAt, nhds_prod_eq] using cf‚ÇÅ.comp h
            ¬∑ simpa [nhds_prod_eq] using h.eventually df‚ÇÅ
          _ =O[ùìù ((x‚ÇÅ, x‚ÇÇ), (x‚ÇÅ, x‚ÇÇ))] (fun (y, z) => y - z : _ ‚Üí E‚ÇÅ √ó E‚ÇÇ) := by
            simp [isBigO_of_le]
      ¬∑ calc
          fun (y, z) => f y.1 y.2 - f y.1 z.2 - f‚ÇÇ x‚ÇÅ x‚ÇÇ (y.2 - z.2)
          _ =o[ùìù ((x‚ÇÅ, x‚ÇÇ), (x‚ÇÅ, x‚ÇÇ))] (fun (y, z) => y.2 - z.2 : _ ‚Üí E‚ÇÇ) := by
            rw [‚Üê nhdsWithin_univ]
            have h := (tendsto_fst.comp <| tendsto_fst.comp tendsto_fst).prodMk <|
              tendsto_snd (f := (ùìù x‚ÇÅ √óÀ¢ ùìù x‚ÇÇ) √óÀ¢ (ùìù x‚ÇÅ √óÀ¢ ùìù x‚ÇÇ)) (g := ùìù x‚ÇÇ)
            let : NormedSpace ‚Ñù E‚ÇÇ := RestrictScalars.normedSpace ‚Ñù ùïú E‚ÇÇ
            apply isLittleO_sub_sub_fderiv (E := (E‚ÇÅ √ó E‚ÇÇ) √ó (E‚ÇÅ √ó E‚ÇÇ))
              (t := Set.univ) (f := fun (y, z) v => f y.1 v) (f' := fun (y, z) v => f‚ÇÇ y.1 v)
            ¬∑ simpa [nhds_prod_eq] using tendsto_snd.comp tendsto_fst
            ¬∑ simpa [nhds_prod_eq] using tendsto_snd.comp tendsto_snd
            ¬∑ simp
            ¬∑ simpa [continuousWithinAt_univ, ContinuousAt, nhds_prod_eq] using cf‚ÇÇ.comp h
            ¬∑ simpa [nhds_prod_eq] using h.eventually df‚ÇÇ
          _ =O[ùìù ((x‚ÇÅ, x‚ÇÇ), (x‚ÇÅ, x‚ÇÇ))] (fun (y, z) => y - z : _ ‚Üí E‚ÇÅ √ó E‚ÇÇ) := by
            simp [isBigO_of_le]

/-- If a bivariate function `f` has partial derivatives `f‚ÇÅx` at `(x‚ÇÅ, x‚ÇÇ)` and `f‚ÇÇ` in a
neighbourhood of `(x‚ÇÅ, x‚ÇÇ)`, continuous there, then the uncurried function `‚Üøf` is differentiable at
`(x‚ÇÅ, x‚ÇÇ)` with its derivative mapping `(h‚ÇÅ, h‚ÇÇ)` to `f‚ÇÅx h‚ÇÅ + f‚ÇÇ x‚ÇÅ x‚ÇÇ h‚ÇÇ`. -/
theorem hasFDerivWithinAt_uncurry_coprod_of_continuousWithinAt_snd
    [NormedSpace ‚Ñù E‚ÇÇ] {f : E‚ÇÅ ‚Üí E‚ÇÇ ‚Üí F} {x‚ÇÅ : E‚ÇÅ} {x‚ÇÇ : E‚ÇÇ}
    {s‚ÇÅ : Set E‚ÇÅ} {s‚ÇÇ : Set E‚ÇÇ} (seg : ‚àÄ·∂† v in ùìù[s‚ÇÇ] x‚ÇÇ, [x‚ÇÇ -[‚Ñù] v] ‚äÜ s‚ÇÇ)
    {f‚ÇÅx : E‚ÇÅ ‚ÜíL[ùïú] F} (df‚ÇÅx : HasFDerivWithinAt (f ¬∑ x‚ÇÇ) f‚ÇÅx s‚ÇÅ x‚ÇÅ)
    {f‚ÇÇ : E‚ÇÅ ‚Üí E‚ÇÇ ‚Üí E‚ÇÇ ‚ÜíL[ùïú] F} (cf‚ÇÇ : ContinuousWithinAt ‚Üøf‚ÇÇ (s‚ÇÅ √óÀ¢ s‚ÇÇ) (x‚ÇÅ, x‚ÇÇ))
    (df‚ÇÇ : ‚àÄ·∂† y in ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ), HasFDerivWithinAt (f y.1 ¬∑) (f‚ÇÇ y.1 y.2) s‚ÇÇ y.2) :
    HasFDerivWithinAt ‚Üøf (f‚ÇÅx.coprod (f‚ÇÇ x‚ÇÅ x‚ÇÇ)) (s‚ÇÅ √óÀ¢ s‚ÇÇ) (x‚ÇÅ, x‚ÇÇ) := by
  unfold ContinuousWithinAt at cf‚ÇÇ
  rw [nhdsWithin_prod_eq] at cf‚ÇÇ df‚ÇÇ
  rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO]
  calc
    fun y => ‚Üøf y - f x‚ÇÅ x‚ÇÇ - (f‚ÇÅx.coprod (f‚ÇÇ x‚ÇÅ x‚ÇÇ)) (y.1 - x‚ÇÅ, y.2 - x‚ÇÇ)
    _ = fun y => f y.1 x‚ÇÇ - f x‚ÇÅ x‚ÇÇ - f‚ÇÅx (y.1 - x‚ÇÅ) + (‚Üøf y - f y.1 x‚ÇÇ - f‚ÇÇ x‚ÇÅ x‚ÇÇ (y.2 - x‚ÇÇ)) := by
      ext
      rw [ContinuousLinearMap.coprod_apply]
      abel
    _ =o[ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ)] fun y => (y.1 - x‚ÇÅ, y.2 - x‚ÇÇ) := by
      apply IsLittleO.add
      ¬∑ calc
          _ = (fun y‚ÇÅ => f y‚ÇÅ x‚ÇÇ - f x‚ÇÅ x‚ÇÇ - f‚ÇÅx (y‚ÇÅ - x‚ÇÅ)) ‚àò Prod.fst := by
            rw [Function.comp_def]
          _ =o[ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ)] ((fun y‚ÇÅ => y‚ÇÅ - x‚ÇÅ) ‚àò Prod.fst) := by
            rw [nhdsWithin_prod_eq]
            apply IsLittleO.comp_fst
            rwa [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO] at df‚ÇÅx
          _ =O[ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ)] fun y => (y.1 - x‚ÇÅ, y.2 - x‚ÇÇ) := by
            simp [isBigO_of_le]
      ¬∑ calc
          fun y => f y.1 y.2 - f y.1 x‚ÇÇ - f‚ÇÇ x‚ÇÅ x‚ÇÇ (y.2 - x‚ÇÇ)
          _ =o[ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ)] fun y => y.2 - x‚ÇÇ := by
            have h := (tendsto_fst.comp tendsto_fst).prodMk <|
              tendsto_snd (f := ùìù[s‚ÇÅ] x‚ÇÅ √óÀ¢ ùìù[s‚ÇÇ] x‚ÇÇ) (g := ùìù[s‚ÇÇ] x‚ÇÇ)
            apply isLittleO_sub_sub_fderiv (E := E‚ÇÅ √ó E‚ÇÇ) (f' := fun y v => f‚ÇÇ y.1 v)
            ¬∑ simpa [nhdsWithin_prod_eq] using tendsto_nhds_of_tendsto_nhdsWithin tendsto_snd
            ¬∑ exact tendsto_const_nhds
            ¬∑ simpa [nhdsWithin_prod_eq] using seg.prod_inr _
            ¬∑ simpa [ContinuousWithinAt, nhdsWithin_prod_eq] using cf‚ÇÇ.comp h
            ¬∑ simpa [nhdsWithin_prod_eq] using h.eventually df‚ÇÇ
          _ =O[ùìù[s‚ÇÅ √óÀ¢ s‚ÇÇ] (x‚ÇÅ, x‚ÇÇ)] fun y => (y.1 - x‚ÇÅ, y.2 - x‚ÇÇ) := by
            simp [isBigO_of_le]

open Set Function Metric

section PartialFDeriv

/-- Differentiable implies also that the first partial derivative exists. -/
theorem HasFDerivWithinAt.partial_fst
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {f' : E √ó F ‚Üí E √ó F ‚ÜíL[ùïú] G}
  {s : Set E} {t : Set F}
  {z : E √ó F} (hz : z ‚àà s √óÀ¢ t)
  (hf : HasFDerivWithinAt f (f' z) (s √óÀ¢ t) z) :
      HasFDerivWithinAt (f ‚àò (¬∑, z.2)) (f' z ‚àòL .inl ..) s z.1 := by
    have hleft (x:E) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_id (ùïú := ùïú) x s)
      (hasFDerivWithinAt_const z.2 x s)
    convert HasFDerivWithinAt.comp z.1 (hf) (hleft z.1)
      (fun x hx => mem_prod.mpr ‚ü®hx, (mem_prod.mp hz).right‚ü©)

/-- Differentiable implies also that the second partial derivative exists. -/
theorem HasFDerivWithinAt.partial_snd
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {f' : E √ó F ‚Üí E √ó F ‚ÜíL[ùïú] G}
  {s : Set E} {t : Set F}
  {z : E √ó F} (hz : z ‚àà s √óÀ¢ t)
  (hf : HasFDerivWithinAt f (f' z) (s √óÀ¢ t) z) :
      HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) (f' z ‚àòL .inr ..) t z.2 := by
    have hright (y:F) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_const z.1 y t)
      (hasFDerivWithinAt_id (ùïú := ùïú) y t)
    convert HasFDerivWithinAt.comp z.2 (hf) (hright z.2)
      (fun y hy => mem_prod.mpr ‚ü®(mem_prod.mp hz).left, hy‚ü©)

/-- If a function `f : E √ó F ‚Üí G` has a first partial derivative (within set `s`) `f'xz` at `z`
and has a second partial derivative (within open set `t`) `f'y` continuous on `s √óÀ¢ t`,
then `f` has a derivative at `z`, with the derivative given by `f'z = f'xz.coprod (f'y z)`.

See `hasFDerivWithinAt_of_partial_fst_continuousWithinAt_prod_open` for the order of derivatives
swapped.
-/
theorem hasFDerivWithinAt_of_partial_snd_continuousWithinAt_prod_open
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {s : Set E} {t : Set F} {z : E √ó F}
  (hz : z ‚àà s √óÀ¢ t) (ht : IsOpen t)
  {f'xz : E ‚ÜíL[ùïú] G} {f'y : E √ó F ‚Üí F ‚ÜíL[ùïú] G}
  (hf'y_cont : ContinuousWithinAt f'y (s √óÀ¢ t) z)
  (hf'xz : HasFDerivWithinAt (f ‚àò (¬∑, z.2)) f'xz s z.1)
  (hf'y : ‚àÄ z' ‚àà s √óÀ¢ t, HasFDerivAt (f ‚àò (z'.1, ¬∑)) (f'y z') z'.2) :
    HasFDerivWithinAt f (f'xz.coprod (f'y z)) (s √óÀ¢ t) z := by
  replace hz : _ ‚àß _ := ‚ü®mem_prod.mp hz, hz‚ü©
  simp only at hz
  -- rewrite derivatives as limits using norms
  simp only [hasFDerivWithinAt_iff_tendsto, tendsto_nhdsWithin_nhds, dist_eq_norm] at ‚ä¢ hf'xz
  simp only [ContinuousLinearMap.coprod_apply, sub_zero, norm_mul, norm_inv,
    norm_norm] at ‚ä¢ hf'xz
  simp only [Metric.continuousWithinAt_iff, dist_eq_norm] at hf'y_cont
  -- get a target Œµ' and immediately shrink it to Œµ for convenice
  intro Œµ' hŒµ'
  rw [show Œµ' = (Œµ'/2/2/2)*2 + (Œµ'/2/2/2)*2 + (Œµ'/2/2/2)*2 + (Œµ'/2/2/2)*2 by ring]
  have hŒµ := half_pos (half_pos (half_pos hŒµ'))
  set Œµ := Œµ' / 2 / 2 / 2
  -- get Œ¥x from x-differentiability
  -- get Œ¥y from continuity of y-derivative
  -- get Œ¥t is constrained by the possibly small size of t
  replace ‚ü®Œ¥x, hŒ¥x, hf'xz‚ü© := hf'xz Œµ hŒµ
  replace ‚ü®Œ¥y, hŒ¥y, hf'y_cont‚ü© := hf'y_cont Œµ hŒµ
  obtain ‚ü®Œ¥t, hŒ¥t‚ü© := isOpen_iff.mp ht z.2 hz.1.2
  use (min Œ¥x (min Œ¥y Œ¥t)) -- derive desired Œ¥
  refine ‚ü®?pos, ?_‚ü©
  case pos => exact lt_min hŒ¥x (lt_min hŒ¥y hŒ¥t.1) -- positivity of Œ¥
  -- get working point (x,y) ‚àà E √ó F within Œ¥ distance of z
  intro (x,y) hst hŒ¥
  replace hst : _ ‚àß _ := ‚ü®mem_prod.mp hst, hst‚ü©
  simp only at hst
  simp only [Prod.fst_sub, Prod.snd_sub]
  rw [mul_comm]
  -- simplify norm conditions into bounds on ‚Äñx-z.1‚Äñ and ‚Äñy-z.2‚Äñ
  simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub] at hŒ¥
  simp only [lt_inf_iff, sup_lt_iff] at hŒ¥
  obtain ‚ü®‚ü®hxx, hyx‚ü©, ‚ü®‚ü®hxy, hyy‚ü©, ‚ü®hxt, hyt‚ü©‚ü©‚ü© := hŒ¥
  -- rewrite desired variation in f for easier estimation
  have hf := calc
    f (x,y) - f z - (f'xz (x - z.1) + (f'y z) (y - z.2))
      = f (x,y) - f (x,z.2)
      + f (x,z.2) - f (z.1,z.2) - (f'xz (x - z.1) + (f'y z) (y - z.2)) := by
        abel
    _ = f (x,y) - f (x,z.2) - (f'y z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1) := by
        abel
    _ = f (x,y) - f (x,z.2) - (f'y (x,z.2)) (y - z.2)
      + (f'y (x,z.2)) (y - z.2) - (f'y z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1) := by
        abel
    _ = f (x,y) - f (x,z.2) - (f'y (x,z.2)) (y - z.2)
      + (f'y (x,z.2) - f'y z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1) := by
        rw [ContinuousLinearMap.sub_apply]
        abel
    _ = f (x,y) - f (x,z.2) - (f'y (x,z.2)) (y - z.2)
      + (f'y (x,z.2) - f'y z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1)) := by
        abel
  -- set up the hypotheses and use the inequality version of the Mean Value Theorem
  have mvt_diff : ‚àÄ y ‚àà ball z.2 (min Œ¥y Œ¥t),
      HasFDerivWithinAt (f ‚àò (x,¬∑)) (f'y (x,y)) (ball z.2 (min Œ¥y Œ¥t)) y := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    apply (hf'y (x,y') (mem_prod.mpr ‚ü®hst.1.1, _‚ü©)).hasFDerivWithinAt.mono
    ¬∑ calc
        ball z.2 (min Œ¥y Œ¥t)
          ‚äÜ ball z.2 Œ¥t := ball_subset_ball (min_le_right _ _)
        _ ‚äÜ t := hŒ¥t.2
    ¬∑ exact mem_of_subset_of_mem hŒ¥t.2 (mem_ball_iff_norm.mpr hy'.2)
  have mvt_bound : ‚àÄ y' ‚àà ball z.2 (min Œ¥y Œ¥t), ‚Äñf'y (x,y') - f'y (x,z.2)‚Äñ ‚â§ Œµ + Œµ := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    rw [‚Üê dist_eq_norm]
    apply (dist_triangle _ (f'y z) _).trans
    rw [dist_eq_norm, dist_eq_norm, norm_sub_rev (f'y z) _]
    have hxy' : ‚Äñ(x,y') - z‚Äñ < Œ¥y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sup_lt_iff]
      exact ‚ü®hxy, hy'.1‚ü©
    have hxz2 : ‚Äñ(x,z.2) - z‚Äñ < Œ¥y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
        sup_of_le_left]
      exact hxy
    apply add_le_add (hf'y_cont _ hxy').le (hf'y_cont _ hxz2).le
    ¬∑ apply mem_prod.mpr ‚ü®hst.1.1, _‚ü©
      exact mem_of_subset_of_mem hŒ¥t.2 (mem_ball_iff_norm.mpr hy'.2)
    ¬∑ exact mem_prod.mpr ‚ü®hst.1.1, hz.1.2‚ü©
  have mvt {a b} (ha : a ‚àà _) (hb : b ‚àà _) :=
    -- inequality version of Mean Value Theorem
    Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'
      mvt_diff
      mvt_bound
      (convex_ball z.2 (min Œ¥y Œ¥t)) ha hb
  simp only [comp_apply] at mvt
  -- use the calculation above and start applying norms and estimates on the goal, term by term
  rw [hf]
  replace hf := calc
    ‚Äñf (x,y) - f (x,z.2) - (f'y (x,z.2)) (y - z.2)
      + (f'y (x,z.2) - f'y z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1))‚Äñ
      ‚â§ ‚Äñf (x,y) - f (x,z.2) - (f'y (x,z.2)) (y - z.2)‚Äñ
      + ‚Äñ(f'y (x,z.2) - f'y z) (y - z.2)‚Äñ
      + ‚Äñ(f (x,z.2) - f (z.1,z.2) - f'xz (x - z.1))‚Äñ := norm_add‚ÇÉ_le
    _ ‚â§ (Œµ + Œµ) * ‚Äñy - z.2‚Äñ
      + ‚Äñ(f'y (x,z.2) - f'y z)‚Äñ * ‚Äñy - z.2‚Äñ
      + Œµ * ‚Äñx - z.1‚Äñ := by
        apply add_le_add (add_le_add _ _) _ -- compare term by term
        ¬∑ exact mvt -- Mean Value estimate
            (mem_ball_self (lt_min hŒ¥y hŒ¥t.1))
            (mem_ball_iff_norm.mpr (lt_min hyy hyt))
        ¬∑ exact ContinuousLinearMap.le_opNorm _ _ -- operator norm estimate
        ¬∑ rw [mul_comm]
          by_cases hxnz : 0 < ‚Äñx - z.1‚Äñ
          case neg => -- handle trivial x = z.1 case
            replace hxnz := (not_lt.mp hxnz).antisymm (norm_nonneg _)
            have hxnz' := eq_of_sub_eq_zero (norm_eq_zero.mp hxnz)
            repeat rw [hxnz, hxnz']
            simp only [Prod.mk.eta, sub_self, map_zero, norm_zero, zero_mul, le_refl]
          case pos =>
            apply (inv_mul_le_iff‚ÇÄ hxnz).mp
            exact (hf'xz hst.1.1 hxx).le -- apply differentiability estimate
    _ ‚â§ Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñx - z.1‚Äñ := by
        rw [add_mul]
        apply add_le_add (add_le_add le_rfl _) le_rfl
        apply mul_le_mul (hf'y_cont _ _).le le_rfl (norm_nonneg (y - z.2)) hŒµ.le
        ¬∑ exact (mem_prod.mpr ‚ü®hst.1.1, hz.1.2‚ü©)
        ¬∑ simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
          sup_of_le_left, hxy]
  -- now apply the estimate hf to the goal
  apply (mul_le_mul_of_nonneg_right hf (by simp only [inv_nonneg, norm_nonneg])).trans_lt _
  -- it remains only to simplify the inequality term by term and compare coefficients
  simp only [add_mul, mul_assoc]
  apply add_lt_add (add_lt_add (add_lt_add _ _) _)
  all_goals
    apply (mul_lt_mul_left hŒµ).mpr
    refine LE.le.trans_lt ?_ (one_lt_two)
    rw [mul_comm]
    apply inv_mul_le_of_le_mul‚ÇÄ (norm_nonneg _) zero_le_one
    simp only [mul_one, Prod.norm_def, Prod.fst_sub, Prod.snd_sub]
    first | exact le_max_right _ _ | exact le_max_left _ _

/-- If a function `f : E √ó F ‚Üí G` has a second partial derivative (within set `t`) `f'yz` at `z`
and has a first partial derivative (within open set `s`) `f'x` continuous on `s √óÀ¢ t`,
then `f` has a derivative at `z`, with the derivative given by `f'z = (f'x z).coprod f'yz`.

See `hasFDerivWithinAt_of_partial_snd_continuousWithinAt_prod_open` for the order of derivatives
swapped.
-/
theorem hasFDerivWithinAt_of_partial_fst_continuousWithinAt_prod_open
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {s : Set E} {t : Set F} {z : E √ó F}
  (hz : z ‚àà s √óÀ¢ t) (hs : IsOpen s)
  {f'x : E √ó F ‚Üí E ‚ÜíL[ùïú] G} {f'yz : F ‚ÜíL[ùïú] G}
  (hf'x_cont : ContinuousWithinAt f'x (s √óÀ¢ t) z)
  (hf'x : ‚àÄ z' ‚àà s √óÀ¢ t, HasFDerivAt (f ‚àò (¬∑, z'.2)) (f'x z') z'.1)
  (hf'yz : HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) f'yz t z.2) :
    HasFDerivWithinAt f ((f'x z).coprod f'yz) (s √óÀ¢ t) z := by
  have hmt_st := mapsTo_swap_prod s t
  have hmt_ts := mapsTo_swap_prod t s
  have hf'x_swap_cont := (z.swap_swap ‚ñ∏ hf'x_cont).comp
    continuous_swap.continuousWithinAt
    hmt_ts
  -- exchange `E` and `F` to use a previous result
  have hswap := hasFDerivWithinAt_of_partial_snd_continuousWithinAt_prod_open
    (f := f ‚àò Prod.swap)
    (z := z.swap)
    hz.symm hs
    hf'x_swap_cont
    hf'yz
    (fun z' hz' => (hf'x z'.swap (hmt_ts hz')))
  -- exchange `E` and `F` back in the result to satisfy the goal
  let cle_swap := ContinuousLinearEquiv.prodComm ùïú E F
  convert hswap.comp z (cle_swap.hasFDerivWithinAt) hmt_st
  unfold cle_swap
  simp only [Prod.swap_swap, comp_apply, ContinuousLinearMap.coprod_comp_prodComm]

/-- If a function `f : E √ó F ‚Üí G` has partial derivative `f'x` or `f'y` on an open set `u`,
and they are continuous at `z ‚àà u`, then `f` is continously differentiable at `z`, with
the derivative given by `f' z = (f'x z).coprod (f'y z)`.
-/
theorem hasFDerivWithinAt_continuousWithinAt_of_partial_continuousWithinAt_open
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  --NB: [NormedSpace ‚Ñù E] is not needed because the proof eventually applies
  --    the Mean Value Theorem only in the F direction. But it could have been
  --    the other way around and it is odd to not have symmetry in the hypotheses
  {E : Type*} [NormedAddCommGroup E] /-[NormedSpace ‚Ñù E]-/ [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {u : Set (E √ó F)} (hu : IsOpen u) {z : E √ó F} (hz : z ‚àà u)
  {f'x : E √ó F ‚Üí E ‚ÜíL[ùïú] G} {f'y : E √ó F ‚Üí F ‚ÜíL[ùïú] G}
  (hf'x_cont : ContinuousWithinAt f'x u z) (hf'y_cont : ContinuousWithinAt f'y u z)
  (hf'x : ‚àÄ z ‚àà u, HasFDerivAt (f ‚àò (¬∑, z.2)) (f'x z) z.1)
  (hf'y : ‚àÄ z ‚àà u, HasFDerivAt (f ‚àò (z.1, ¬∑)) (f'y z) z.2) :
    ContinuousWithinAt (fun z => (f'x z).coprod (f'y z)) u z
    ‚àß HasFDerivAt f ((f'x z).coprod (f'y z)) z := by
  refine ‚ü®?cont, ?diff‚ü©
  case cont =>
    -- combine continuity of partial to get continuity of total derivative
    exact hf'x_cont.continuousLinearMapCoprod hf'y_cont
  case diff =>
    -- first restrict all properties to a product neighborhood of z
    obtain ‚ü®s,t,hs,ht,hz1,hz2,hst‚ü© := isOpen_prod_iff.mp hu z.1 z.2 hz
    have hstn : s √óÀ¢ t ‚àà nhds z := IsOpen.mem_nhds (hs.prod ht) (mem_prod.mpr ‚ü®hz1, hz2‚ü©)
    have hsu (z : E √ó F) (hz : z ‚àà s √óÀ¢ t) : s ‚äÜ ((¬∑,z.2) ‚Åª¬π' u) := by
      apply HasSubset.Subset.trans _ (preimage_mono hst)
      rw [mk_preimage_prod_left (mem_prod.mpr hz).2]
    have htu (z : E √ó F) (hz : z ‚àà s √óÀ¢ t) : t ‚äÜ ((z.1,¬∑) ‚Åª¬π' u) := by
      apply HasSubset.Subset.trans _ (preimage_mono hst)
      rw [mk_preimage_prod_right (mem_prod.mpr hz).1]
    replace hf'y_cont := hf'y_cont.mono hst
    -- now apply the weaker criteria to get differentiability
    apply HasFDerivWithinAt.hasFDerivAt _ hstn
    apply hasFDerivWithinAt_of_partial_snd_continuousWithinAt_prod_open
      ‚ü®hz1,hz2‚ü© ht
      hf'y_cont
      _ _
    ¬∑ exact (hf'x z hz).hasFDerivWithinAt.mono (hsu z ‚ü®hz1,hz2‚ü©)
    ¬∑ exact (fun z hz => (hf'y z (mem_of_subset_of_mem hst hz)))

/-- If a function `f : E √ó F ‚Üí G` has partial derivative `f'x` or `f'y` continuous
on an open set `u`, then `f` is continously differentiable on this set, with
the derivative given by `f' = f'x.coprod f'y`.
-/
theorem hasFDerivWithinAt_continuousOn_of_partial_continuousOn_open
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  --NB: [NormedSpace ‚Ñù E] is not needed because the proof eventually applies
  --    the Mean Value Theorem only in the F direction. But it could have been
  --    the other way around and it is odd to not have symmetry in the hypotheses
  {E : Type*} [NormedAddCommGroup E] /-[NormedSpace ‚Ñù E]-/ [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {u : Set (E √ó F)} (hu : IsOpen u)
  {f'x : E √ó F ‚Üí E ‚ÜíL[ùïú] G} {f'y : E √ó F ‚Üí F ‚ÜíL[ùïú] G}
  (hf'x_cont : ContinuousOn f'x u) (hf'y_cont : ContinuousOn f'y u)
  (hf'x : ‚àÄ z ‚àà u, HasFDerivAt (f ‚àò (¬∑, z.2)) (f'x z) z.1)
  (hf'y : ‚àÄ z ‚àà u, HasFDerivAt (f ‚àò (z.1, ¬∑)) (f'y z) z.2) :
    ContinuousOn (fun z => (f'x z).coprod (f'y z)) u
    ‚àß ‚àÄ z ‚àà u, HasFDerivAt f ((f'x z).coprod (f'y z)) z := by
  simp only [ContinuousOn, ‚Üê forall‚ÇÇ_and]
  intro z hz
  apply hasFDerivWithinAt_continuousWithinAt_of_partial_continuousWithinAt_open
    hu hz
    (hf'x_cont.continuousWithinAt hz) (hf'y_cont.continuousWithinAt hz)
    hf'x hf'y

end PartialFDeriv
