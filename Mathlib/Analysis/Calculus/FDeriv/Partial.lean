/-
Copyright (c) 2025 Igor Khavkine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine
-/
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Partial derivatives

In this file we prove some basic facts about partial derivatives of functions
defined on a product space, like `f : E Ã— F â†’ G`:

* `HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn` ,
  `HasFDerivWithinAt.continuousOn_open_of_partial_continuousOn` : if `f` is differentiable
  on an open set `u`, such that the partial derivaties along `E` and `F` are continuous on `u`,
  then `f` is continuously differentiable on `E Ã— F`. The first version has simplified
  hypotheses for wen `u = s Ã—Ë¢ t` is a product of open sets.

* `HasFDerivWithinAt.partial_continuousOn_of_continuousOn` ,
  `HasFDerivWithinAt.partial_continuous_of_continuousOn_prod` : if `f` is continuously
  differentiable within a set `u`, then it is partially differentiable `E` and `F` within `u`
  and its partial derivatives, given by restricting the derivative of `f`, are also
  continuous within `u`. This is the easier converse direction of the preceding result.

The proofs follow Â§9.8.1 from DieudonnÃ©'s *Foundations of Modern Analysis* (1969).
-/

open Set Function Metric Real

section PartialFDeriv

/-- If a function `f : E Ã— F â†’ G` has partial derivatives `fx` and `fy` continuous
  on an open set `s Ã—Ë¢ t`, then `f` is continously differentiable on this set, with
  the deriative given by combining `fx` and `fy`.
-/
theorem HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E Ã— F â†’ G} {s : Set E} {t : Set F} (hs : IsOpen s) (ht : IsOpen t)
  {fx : E Ã— F â†’ E â†’L[ğ•œ] G} {fy : E Ã— F â†’ F â†’L[ğ•œ] G}
  (fxy_cont : ContinuousOn fx (s Ã—Ë¢ t) âˆ§ ContinuousOn fy (s Ã—Ë¢ t)) --TODO: should be disjunction
  (hfx : âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt (f âˆ˜ (Â·, z.2)) (fx z) s z.1)
  (hfy : âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt (f âˆ˜ (z.1, Â·)) (fy z) t z.2) :
    âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt f ((fx z).coprod (fy z)) (s Ã—Ë¢ t) z := by
  -- save hypothesis hfy before rewriting it
  intro z hz
  replace hz : _ âˆ§ _ := âŸ¨mem_prod.mp hz, hzâŸ©
  simp only at hz
  have hfy_within := hfy
  -- rewrite derivatives as limits using norms
  simp only [hasFDerivWithinAt_iff_tendsto, tendsto_nhdsWithin_nhds, dist_eq_norm] at âŠ¢ hfx hfy
  simp only [ContinuousLinearMap.coprod_apply, sub_zero, norm_mul, norm_inv, norm_norm] at âŠ¢ hfx hfy
  obtain âŸ¨_, fy_contâŸ© := fxy_cont
  simp only [Metric.continuousOn_iff, dist_eq_norm, norm_eq_abs] at fy_cont
  -- get a target Îµ' and immediately shrink it to Îµ for convenice
  intro Îµ' hÎµ'
  rw [show Îµ' = 2*(Îµ'/2/2/2) + 2*(Îµ'/2/2/2) + 2*(Îµ'/2/2/2) + 2*(Îµ'/2/2/2) by ring]
  have hÎµ := half_pos (half_pos (half_pos hÎµ'))
  set Îµ := Îµ' / 2 / 2 / 2
  -- get Î´x from x-differentiability and Î´y from continuity of y-derivative
  -- also Î´s and Î´t are constrained by the possibly small sizes of s and t
  obtain âŸ¨Î´x, hÎ´x, hfx_zâŸ© := hfx z hz.2 Îµ hÎµ
  obtain âŸ¨Î´y, hÎ´y, hfy_zâŸ© := fy_cont z hz.2 Îµ hÎµ
  obtain âŸ¨Î´s, hÎ´sâŸ© := isOpen_iff.mp hs z.1 hz.1.1
  obtain âŸ¨Î´t, hÎ´tâŸ© := isOpen_iff.mp ht z.2 hz.1.2
  use (min (min Î´x Î´s) (min Î´y Î´t)) -- derive desired Î´
  constructor; Â· exact lt_min (lt_min hÎ´x hÎ´s.1) (lt_min hÎ´y hÎ´t.1) -- positivity of Î´
  -- get working point (x,y) âˆˆ E Ã— F within Î´ distance of z
  intro (x,y) hst hÎ´
  replace hst : _ âˆ§ _ := âŸ¨mem_prod.mp hst, hstâŸ©
  simp only at hst
  simp only [Prod.fst_sub, Prod.snd_sub]
  rw [mul_comm]
  -- simplify norm conditions into bounds on â€–x-z.1â€– and â€–y-z.2â€–
  have hxx := hÎ´
  simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub] at hxx
  simp only [lt_inf_iff, sup_lt_iff] at hxx
  replace âŸ¨âŸ¨âŸ¨hxx, hyxâŸ©, âŸ¨hxs, hysâŸ©âŸ©, âŸ¨âŸ¨hxy, hyyâŸ©, âŸ¨hxt, hytâŸ©âŸ©âŸ© := hxx
  -- rewrite desired variation in f for easier estimation
  have hf := calc
    f (x,y) - f z - ((fx z) (x - z.1) + (fy z) (y - z.2))
      = f (x,y) - f (x,z.2)
      + f (x,z.2) - f (z.1,z.2) - ((fx z) (x - z.1) + (fy z) (y - z.2)) := by
        simp only [map_sub, sub_add_cancel, Prod.mk.eta]
    _ = f (x,y) - f (x,z.2) - (fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        rw [add_comm _ (fy _ _), â† sub_sub]
        rw [sub_right_comm _ _ (fy _ _), add_sub_right_comm _ _ (fy _ _)]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2)) (y - z.2) - (fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        simp only [map_sub, Prod.mk.eta, sub_add_cancel]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        rw [ContinuousLinearMap.sub_apply]
        simp only [map_sub, sub_add_cancel, Prod.mk.eta, sub_add_sub_cancel]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1)) := by
        rw [add_sub_assoc _ (f _) _, add_sub_assoc _ ((f _) - _) _]
  -- set up the hypotheses and use the inequality version of the Mean Value Theorem
  have mvt_diff : âˆ€ y âˆˆ ball z.2 (min Î´y Î´t),
      HasFDerivWithinAt (f âˆ˜ (x,Â·)) (fy (x,y)) (ball z.2 (min Î´y Î´t)) y := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    apply (hfy_within (x,y') (mem_prod.mpr âŸ¨hst.1.1, _âŸ©)).mono
    Â· calc
        ball z.2 (min Î´y Î´t) âŠ† ball z.2 Î´t := ball_subset_ball (min_le_right _ _)
        _ âŠ† t := hÎ´t.2
    Â· exact mem_of_subset_of_mem hÎ´t.2 (mem_ball_iff_norm.mpr hy'.2)
  have mvt_bound : âˆ€ y' âˆˆ ball z.2 (min Î´y Î´t), â€–fy (x,y') - fy (x,z.2)â€– â‰¤ Îµ + Îµ := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    rw [â† dist_eq_norm]
    apply (dist_triangle _ (fy z) _).trans
    rw [dist_eq_norm, dist_eq_norm, norm_sub_rev (fy z) _]
    have hxy' : â€–(x,y') - zâ€– < Î´y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sup_lt_iff]
      exact âŸ¨hxy, hy'.1âŸ©
    have hxz2 : â€–(x,z.2) - zâ€– < Î´y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
        sup_of_le_left]
      exact hxy
    apply add_le_add (hfy_z _ _ hxy').le (hfy_z _ _ hxz2).le
    Â· apply mem_prod.mpr âŸ¨hst.1.1, _âŸ©
      exact mem_of_subset_of_mem hÎ´t.2 (mem_ball_iff_norm.mpr hy'.2)
    Â· exact mem_prod.mpr âŸ¨hst.1.1, hz.1.2âŸ©
  have mvt {a b} (ha : a âˆˆ _) (hb : b âˆˆ _) :=
    -- inequality version of Mean Value Theorem
    Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'
      mvt_diff
      mvt_bound
      (convex_ball z.2 (min Î´y Î´t)) ha hb
  simp only [comp_apply] at mvt
  -- use the calculation above and start applying norms and estimates, term by term
  rw [hf]
  replace hf := calc
    â€–f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1))â€–
      â‰¤ â€–f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)â€–
      + â€–(fy (x,z.2) - fy z) (y - z.2)â€–
      + â€–(f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1))â€– := norm_addâ‚ƒ_le
    _ â‰¤ (Îµ + Îµ) * â€–y - z.2â€–
      + â€–(fy (x,z.2) - fy z)â€– * â€–y - z.2â€–
      + Îµ * â€–x - z.1â€– := by
        apply add_le_add (add_le_add _ _) _ -- compare term by term
        Â· exact mvt -- Mean Value estimate
            (mem_ball_self (lt_min hÎ´y hÎ´t.1))
            (mem_ball_iff_norm.mpr (lt_min hyy hyt))
        Â· exact ContinuousLinearMap.le_opNorm _ _ -- operator norm estimate
        Â· rw [mul_comm]
          by_cases hxnz : 0 < â€–x - z.1â€–
          case neg => -- handle trivial x = z.1 case
            replace hxnz := (not_lt.mp hxnz).antisymm (norm_nonneg _)
            have hxnz' := eq_of_sub_eq_zero (norm_eq_zero.mp hxnz)
            repeat rw [hxnz, hxnz']
            simp only [Prod.mk.eta, sub_self, map_zero, norm_zero, zero_mul, le_refl]
          case pos =>
            apply (inv_mul_le_iffâ‚€ hxnz).mp
            exact (hfx_z hst.1.1 hxx).le -- apply differentiability estimate
    _ â‰¤ Îµ * â€–y - z.2â€– + Îµ * â€–y - z.2â€– + Îµ * â€–y - z.2â€– + Îµ * â€–x - z.1â€– := by
        rw [add_mul]
        apply add_le_add (add_le_add le_rfl _) le_rfl
        apply mul_le_mul (hfy_z _ _ _).le le_rfl (norm_nonneg (y - z.2)) hÎµ.le
        Â· exact (mem_prod.mpr âŸ¨hst.1.1, hz.1.2âŸ©)
        Â· simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
          sup_of_le_left, hxy]
  -- now apply the estimate hf to the goal
  apply (mul_le_mul_of_nonneg_right hf (by simp only [inv_nonneg, norm_nonneg])).trans_lt _
  -- it remains only to simplify the inequality term by term and compare coefficients
  simp only [add_mul, mul_assoc]
  rw [mul_comm 2]
  apply add_lt_add (add_lt_add (add_lt_add _ _) _)
  all_goals
    apply (mul_lt_mul_left hÎµ).mpr
    refine LE.le.trans_lt ?_ (one_lt_two)
    rw [mul_comm]
    apply inv_mul_le_of_le_mulâ‚€ (norm_nonneg _) zero_le_one
    simp only [mul_one, Prod.norm_def, Prod.fst_sub, Prod.snd_sub]
    first | exact le_max_right _ _ | exact le_max_left _ _

/-- If a function `f : E Ã— F â†’ G` has partial derivatives `fx` and `fy` continuous
  on an open set `u`, then `f` is continously differentiable on this set, with
  the deriative given by combining `fx` and `fy`.

  See `HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn` for simplified
  version with `u = s Ã—Ë¢ t` being a product of two opens.
-/
theorem HasFDerivWithinAt.continuousOn_open_of_partial_continuousOn
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E Ã— F â†’ G} {u : Set (E Ã— F)} (hu : IsOpen u)
  {fx : E Ã— F â†’ E â†’L[ğ•œ] G} {fy : E Ã— F â†’ F â†’L[ğ•œ] G}
  (fx_cont : ContinuousOn fx u) (fy_cont : ContinuousOn fy u)
  (hfx : âˆ€ z âˆˆ u, HasFDerivWithinAt (f âˆ˜ (Â·, z.2)) (fx z) ((Â·,z.2) â»Â¹' u) z.1)
  (hfy : âˆ€ z âˆˆ u, HasFDerivWithinAt (f âˆ˜ (z.1, Â·)) (fy z) ((z.1,Â·) â»Â¹' u) z.2) :
    âˆ€ z âˆˆ u, HasFDerivWithinAt f ((fx z).coprod (fy z)) u z := by
  intro z hz
  obtain âŸ¨s,t,hs,ht,hz1,hz2,hstâŸ© := isOpen_prod_iff.mp hu z.1 z.2 hz
  have hstn : s Ã—Ë¢ t âˆˆ nhds z := IsOpen.mem_nhds (hs.prod ht) (mem_prod.mpr âŸ¨hz1, hz2âŸ©)
  apply (hasFDerivWithinAt_inter hstn).mp
  rw [â† right_eq_inter.mpr hst]
  have hsu (z : E Ã— F) (hz : z âˆˆ s Ã—Ë¢ t) : s âŠ† ((Â·,z.2) â»Â¹' u) := by
    apply HasSubset.Subset.trans _ (preimage_mono hst)
    rw [mk_preimage_prod_left (mem_prod.mpr hz).2]
  have htu (z : E Ã— F) (hz : z âˆˆ s Ã—Ë¢ t) : t âŠ† ((z.1,Â·) â»Â¹' u) := by
    apply HasSubset.Subset.trans _ (preimage_mono hst)
    rw [mk_preimage_prod_right (mem_prod.mpr hz).1]
  apply HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn
    hs ht
    âŸ¨fx_cont.mono hst, fy_cont.mono hstâŸ©
    _ _
    z (mem_prod.mpr âŸ¨hz1, hz2âŸ©)
  Â· exact (fun z hz => (hfx z (mem_of_subset_of_mem hst hz)).mono (hsu z hz))
  Â· exact (fun z hz => (hfy z (mem_of_subset_of_mem hst hz)).mono (htu z hz))

/-- If `f : E Ã— F â†’ G` is continuously differentiable within a set `u`, then
  it is partially differentiable within `u` and its partial derivatives,
  obtained by restricting the total derivative of `f`, are also continuous on `u`.

  See `HasFDerivWithinAt.partial_continuousOn_of_continuousOn_prod` for a simplified
  version where `u = s Ã—Ë¢ t` is a product set.
-/
theorem HasFDerivWithinAt.partial_continuousOn_of_continuousOn
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E Ã— F â†’ G} {f' : E Ã— F â†’ E Ã— F â†’L[ğ•œ] G} {u : Set (E Ã— F)}
  (hfc : ContinuousOn f' u) (hf : âˆ€ z âˆˆ u, HasFDerivWithinAt f (f' z) u z) :
    let fx' := fun z => (f' z).comp (.inl _ _ _);
    let fy' := fun z => (f' z).comp (.inr _ _ _);
    (ContinuousOn fx' u âˆ§ ContinuousOn fy' u) âˆ§
    ( (âˆ€ z âˆˆ u, HasFDerivWithinAt (f âˆ˜ (Â· ,z.2)) (fx' z) ((Â·,z.2) â»Â¹' u) z.1)
    âˆ§ (âˆ€ z âˆˆ u, HasFDerivWithinAt (f âˆ˜ (z.1, Â·)) (fy' z) ((z.1,Â·) â»Â¹' u) z.2) )
    := by
  set fx' := fun z => (f' z).comp (.inl _ _ _)
  set fy' := fun z => (f' z).comp (.inr _ _ _)
  refine âŸ¨?cont, ?diffâŸ©
  case cont =>
    rw [continuousOn_iff_continuous_restrict] at hfc âŠ¢
    rw [continuousOn_iff_continuous_restrict] at âŠ¢
    exact âŸ¨hfc.clm_comp_const _, hfc.clm_comp_const _âŸ©
  case diff =>
    rw [â† forallâ‚‚_and]
    intro z hz
    have hz12 := (Prod.mk.eta (p := z)).symm â–¸ hz
    set su := ((Â· ,z.2)) â»Â¹' u
    set tu := ((z.1, Â·)) â»Â¹' u
    set fx := (f âˆ˜ (Â· ,z.2))
    set fy := (f âˆ˜ (z.1, Â·))
    have hfx (x:E) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_id (ğ•œ := ğ•œ) x su)
      (hasFDerivWithinAt_const z.2 x su)
    have hfy (y:F) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_const z.1 y tu)
      (hasFDerivWithinAt_id (ğ•œ := ğ•œ) y tu)
    refine âŸ¨HasFDerivWithinAt.comp z.1 (hf (z.1,z.2) hz12) (hfx z.1) ?_,
      HasFDerivWithinAt.comp z.2 (hf (z.1,z.2) hz12) (hfy z.2) ?_âŸ©
    Â· exact fun â¦ƒ_â¦„ a â†¦ a
    Â· exact fun â¦ƒ_â¦„ a â†¦ a

/-- If `f : E Ã— F â†’ G` is continuously differentiable within a set `s Ã—Ë¢ t`, then
  it is partially differentiable within `s Ã—Ë¢ t` and its partial derivatives,
  obtained by restricting the total derivative of `f`, are also continuous on `s Ã—Ë¢ t`.
-/
theorem HasFDerivWithinAt.partial_continuousOn_of_continuousOn_prod
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E Ã— F â†’ G} {f' : E Ã— F â†’ E Ã— F â†’L[ğ•œ] G} {s : Set E} {t : Set F}
  (hf : âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt f (f' z) (s Ã—Ë¢ t) z) (hfc : ContinuousOn f' (s Ã—Ë¢ t)) :
    let fx' := fun z => (f' z).comp (.inl _ _ _);
    let fy' := fun z => (f' z).comp (.inr _ _ _);
    (ContinuousOn fx' (s Ã—Ë¢ t) âˆ§ ContinuousOn fy' (s Ã—Ë¢ t)) âˆ§
    ( (âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt (f âˆ˜ (Â· ,z.2)) (fx' z) s z.1)
    âˆ§ (âˆ€ z âˆˆ s Ã—Ë¢ t, HasFDerivWithinAt (f âˆ˜ (z.1, Â·)) (fy' z) t z.2) )
     := by
  refine âŸ¨?cont, ?diffâŸ©
  case cont =>
    rw [continuousOn_iff_continuous_restrict] at hfc âŠ¢
    rw [continuousOn_iff_continuous_restrict] at âŠ¢
    exact âŸ¨hfc.clm_comp_const _, hfc.clm_comp_const _âŸ©
  case diff =>
    rw [â† forallâ‚‚_and]
    intro z hz
    have hsu : ((fun x â†¦ (x, z.2)) â»Â¹' s Ã—Ë¢ t) = s := by
      exact mk_preimage_prod_left (mem_prod.mpr hz).2
    have htu : ((fun y â†¦ (z.1, y)) â»Â¹' s Ã—Ë¢ t) = t := by
      exact mk_preimage_prod_right (mem_prod.mpr hz).1
    have := forallâ‚‚_and.mpr (HasFDerivWithinAt.partial_continuousOn_of_continuousOn hfc hf).2 z hz
    rw [hsu, htu] at this
    exact this

end PartialFDeriv
