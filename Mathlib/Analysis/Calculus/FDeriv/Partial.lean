/-
Copyright (c) 2025 A Tucker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: A Tucker
-/
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Partial derivatives

Results in this file relate the partial derivatives of a bivariate function to its differentiability
in the product space.

## Main statements

- `hasStrictFDerivAt_uncurry_coprod`: establishing strict differentiability at a point `x` in the
  product space, this requires that both partial derivatives exist in a neighbourhood of `x` and be
  continuous at `x`.
- `hasFDerivWithinAt_uncurry_coprod_of_continuousWithinAt_snd`: this weaker result requires that
  both partial derivatives exist, but only the second need exist in a neighbourhood of `x` (and be
  continuous at `x`).
-/

open Asymptotics Filter
open scoped Convex Topology

section aux

variable {E F : Type*} [TopologicalSpace E] [NormedAddCommGroup F] [NormedSpace â„ F]
variable {s : Set E} {t : Set F} {Î¾ : E} {x : F} {y z : E â†’ F}

theorem eventually_segment {r : E â†’ F â†’ Prop}
    (hy : Tendsto y (ğ“[s] Î¾) (ğ“ x)) (hz : Tendsto z (ğ“[s] Î¾) (ğ“ x))
    (seg : âˆ€á¶  Ï‡ in ğ“[s] Î¾, [z Ï‡ -[â„] y Ï‡] âŠ† t) (hr : âˆ€á¶  p in ğ“[s Ã—Ë¢ t] (Î¾, x), r p.1 p.2) :
    âˆ€á¶  Ï‡ in ğ“[s] Î¾, âˆ€ v âˆˆ [z Ï‡ -[â„] y Ï‡], r Ï‡ v := by
  rw [nhdsWithin_prod_eq, eventually_prod_iff] at hr
  obtain âŸ¨p, hp, q, hq, hrâŸ© := hr
  rw [eventually_iff, Metric.mem_nhdsWithin_iff] at hq
  obtain âŸ¨Î´, hÎ´, hqâŸ© := hq
  rw [Metric.tendsto_nhds] at hy hz
  filter_upwards [hp, hy Î´ hÎ´, hz Î´ hÎ´, seg] with Ï‡ hp hy hz seg
  have := convex_iff_segment_subset.mp (convex_ball x Î´) hz hy
  exact fun v hv => hr hp <| hq âŸ¨this hv, seg hvâŸ©

variable {ğ•œ G : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
variable [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

theorem isLittleO_sub_sub_fderiv {f : E â†’ F â†’ G} {f' : E â†’ F â†’ F â†’L[ğ•œ] G}
    (hy : Tendsto y (ğ“[s] Î¾) (ğ“ x)) (hz : Tendsto z (ğ“[s] Î¾) (ğ“ x))
    (seg : âˆ€á¶  Ï‡ in ğ“[s] Î¾, [z Ï‡ -[â„] y Ï‡] âŠ† t) (cf' : ContinuousWithinAt â†¿f' (s Ã—Ë¢ t) (Î¾, x))
    (df' : âˆ€á¶  p in ğ“[s Ã—Ë¢ t] (Î¾, x), HasFDerivWithinAt (f p.1) (f' p.1 p.2) t p.2) :
    (fun Ï‡ => f Ï‡ (y Ï‡) - f Ï‡ (z Ï‡) - f' Î¾ x (y Ï‡ - z Ï‡)) =o[ğ“[s] Î¾] (fun Ï‡ => y Ï‡ - z Ï‡) := by
  rw [isLittleO_iff]
  intro Îµ hÎµ
  replace cf' : âˆ€á¶  Ï‡ in ğ“[s] Î¾, âˆ€ v âˆˆ [z Ï‡ -[â„] y Ï‡], dist (f' Ï‡ v) (f' Î¾ x) < Îµ := by
    rw [Metric.continuousWithinAt_iff'] at cf'
    exact eventually_segment hy hz seg (cf' Îµ hÎµ)
  replace df' : âˆ€á¶  Ï‡ in ğ“[s] Î¾, âˆ€ v âˆˆ [z Ï‡ -[â„] y Ï‡], HasFDerivWithinAt (f Ï‡) (f' Ï‡ v) t v :=
    eventually_segment hy hz seg df'
  filter_upwards [seg, cf', df'] with Ï‡ seg cf' df'
  exact Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'
    (fun v hv => (df' v hv).mono seg) (fun v hv => (cf' v hv).le)
    (convex_segment ..) (left_mem_segment ..) (right_mem_segment ..)

end aux

variable {ğ•œ Eâ‚ Eâ‚‚ F : Type*} [NontriviallyNormedField ğ•œ] [IsRCLikeNormedField ğ•œ]
variable [NormedAddCommGroup Eâ‚] [NormedAddCommGroup Eâ‚‚] [NormedAddCommGroup F]
variable [NormedSpace ğ•œ Eâ‚] [NormedSpace ğ•œ Eâ‚‚] [NormedSpace ğ•œ F]

/-- If a bivariate function `f` has partial derivatives `fâ‚` and `fâ‚‚` in a neighbourhood of a point
`(xâ‚, xâ‚‚)` and if they are continuous at that point then the uncurried function `â†¿f` is strictly
differentiable there with its derivative mapping `(hâ‚, hâ‚‚)` to `fâ‚ xâ‚ xâ‚‚ hâ‚ + fâ‚‚ xâ‚ xâ‚‚ hâ‚‚`. -/
theorem hasStrictFDerivAt_uncurry_coprod {f : Eâ‚ â†’ Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
    {fâ‚ : Eâ‚ â†’ Eâ‚‚ â†’ Eâ‚ â†’L[ğ•œ] F} (cfâ‚ : ContinuousAt â†¿fâ‚ (xâ‚, xâ‚‚))
    (dfâ‚ : âˆ€á¶  y in ğ“ (xâ‚, xâ‚‚), HasFDerivAt (f Â· y.2) (fâ‚ y.1 y.2) y.1)
    {fâ‚‚ : Eâ‚ â†’ Eâ‚‚ â†’ Eâ‚‚ â†’L[ğ•œ] F} (cfâ‚‚ : ContinuousAt â†¿fâ‚‚ (xâ‚, xâ‚‚))
    (dfâ‚‚ : âˆ€á¶  y in ğ“ (xâ‚, xâ‚‚), HasFDerivAt (f y.1 Â·) (fâ‚‚ y.1 y.2) y.2) :
    HasStrictFDerivAt â†¿f ((fâ‚ xâ‚ xâ‚‚).coprod (fâ‚‚ xâ‚ xâ‚‚)) (xâ‚, xâ‚‚) := by
  unfold ContinuousAt at cfâ‚ cfâ‚‚
  rw [nhds_prod_eq] at cfâ‚ cfâ‚‚ dfâ‚ dfâ‚‚
  rw [hasStrictFDerivAt_iff_isLittleO]
  calc
    fun (y, z) => f y.1 y.2 - f z.1 z.2 - ((fâ‚ xâ‚ xâ‚‚).coprod (fâ‚‚ xâ‚ xâ‚‚)) (y - z)
    _ = fun (y, z) => (f y.1 z.2 - f z.1 z.2 - fâ‚ xâ‚ xâ‚‚ (y.1 - z.1))
          + (f y.1 y.2 - f y.1 z.2 - fâ‚‚ xâ‚ xâ‚‚ (y.2 - z.2)) := by
      ext
      dsimp only [ContinuousLinearMap.coprod_apply]
      abel
    _ =o[ğ“ ((xâ‚, xâ‚‚), (xâ‚, xâ‚‚))] fun (y, z) => y - z := by
      let : RCLike ğ•œ := IsRCLikeNormedField.rclike ğ•œ
      apply IsLittleO.add
      Â· calc
          fun (y, z) => f y.1 z.2 - f z.1 z.2 - fâ‚ xâ‚ xâ‚‚ (y.1 - z.1)
          _ =o[ğ“ ((xâ‚, xâ‚‚), (xâ‚, xâ‚‚))] (fun (y, z) => y.1 - z.1 : _ â†’ Eâ‚) := by
            rw [â† nhdsWithin_univ]
            have h := tendsto_snd.prodMk <| tendsto_snd.comp <| tendsto_snd.comp <|
              tendsto_fst (f := (ğ“ xâ‚ Ã—Ë¢ ğ“ xâ‚‚) Ã—Ë¢ (ğ“ xâ‚ Ã—Ë¢ ğ“ xâ‚‚)) (g := ğ“ xâ‚)
            let : NormedSpace â„ Eâ‚ := RestrictScalars.normedSpace â„ ğ•œ Eâ‚
            apply isLittleO_sub_sub_fderiv (E := (Eâ‚ Ã— Eâ‚‚) Ã— (Eâ‚ Ã— Eâ‚‚))
              (t := Set.univ) (f := fun (y, z) u => f u z.2) (f' := fun (y, z) u => fâ‚ u z.2)
            Â· simpa [nhds_prod_eq] using tendsto_fst.comp tendsto_fst
            Â· simpa [nhds_prod_eq] using tendsto_fst.comp tendsto_snd
            Â· simp
            Â· simpa [continuousWithinAt_univ, ContinuousAt, nhds_prod_eq] using cfâ‚.comp h
            Â· simpa [nhds_prod_eq] using h.eventually dfâ‚
          _ =O[ğ“ ((xâ‚, xâ‚‚), (xâ‚, xâ‚‚))] (fun (y, z) => y - z : _ â†’ Eâ‚ Ã— Eâ‚‚) := by
            simp [isBigO_of_le]
      Â· calc
          fun (y, z) => f y.1 y.2 - f y.1 z.2 - fâ‚‚ xâ‚ xâ‚‚ (y.2 - z.2)
          _ =o[ğ“ ((xâ‚, xâ‚‚), (xâ‚, xâ‚‚))] (fun (y, z) => y.2 - z.2 : _ â†’ Eâ‚‚) := by
            rw [â† nhdsWithin_univ]
            have h := (tendsto_fst.comp <| tendsto_fst.comp tendsto_fst).prodMk <|
              tendsto_snd (f := (ğ“ xâ‚ Ã—Ë¢ ğ“ xâ‚‚) Ã—Ë¢ (ğ“ xâ‚ Ã—Ë¢ ğ“ xâ‚‚)) (g := ğ“ xâ‚‚)
            let : NormedSpace â„ Eâ‚‚ := RestrictScalars.normedSpace â„ ğ•œ Eâ‚‚
            apply isLittleO_sub_sub_fderiv (E := (Eâ‚ Ã— Eâ‚‚) Ã— (Eâ‚ Ã— Eâ‚‚))
              (t := Set.univ) (f := fun (y, z) v => f y.1 v) (f' := fun (y, z) v => fâ‚‚ y.1 v)
            Â· simpa [nhds_prod_eq] using tendsto_snd.comp tendsto_fst
            Â· simpa [nhds_prod_eq] using tendsto_snd.comp tendsto_snd
            Â· simp
            Â· simpa [continuousWithinAt_univ, ContinuousAt, nhds_prod_eq] using cfâ‚‚.comp h
            Â· simpa [nhds_prod_eq] using h.eventually dfâ‚‚
          _ =O[ğ“ ((xâ‚, xâ‚‚), (xâ‚, xâ‚‚))] (fun (y, z) => y - z : _ â†’ Eâ‚ Ã— Eâ‚‚) := by
            simp [isBigO_of_le]

/-- If a bivariate function `f` has partial derivatives `fâ‚` at `(xâ‚, xâ‚‚)` and `fâ‚‚` in a
neighbourhood of `(xâ‚, xâ‚‚)`, continuous there, then the uncurried function `â†¿f` is differentiable at
`(xâ‚, xâ‚‚)` with its derivative mapping `(hâ‚, hâ‚‚)` to `fâ‚ xâ‚ xâ‚‚ hâ‚ + fâ‚‚ xâ‚ xâ‚‚ hâ‚‚`. -/
theorem hasFDerivWithinAt_uncurry_coprod_of_continuousWithinAt_snd
    [NormedSpace â„ Eâ‚‚] {f : Eâ‚ â†’ Eâ‚‚ â†’ F} {xâ‚ : Eâ‚} {xâ‚‚ : Eâ‚‚}
    {sâ‚ : Set Eâ‚} {sâ‚‚ : Set Eâ‚‚} (seg : âˆ€á¶  v in ğ“[sâ‚‚] xâ‚‚, [xâ‚‚ -[â„] v] âŠ† sâ‚‚)
    {fâ‚x : Eâ‚ â†’L[ğ•œ] F} (dfâ‚x : HasFDerivWithinAt (f Â· xâ‚‚) fâ‚x sâ‚ xâ‚)
    {fâ‚‚ : Eâ‚ â†’ Eâ‚‚ â†’ Eâ‚‚ â†’L[ğ•œ] F} (cfâ‚‚ : ContinuousWithinAt â†¿fâ‚‚ (sâ‚ Ã—Ë¢ sâ‚‚) (xâ‚, xâ‚‚))
    (dfâ‚‚ : âˆ€á¶  y in ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚), HasFDerivWithinAt (f y.1 Â·) (fâ‚‚ y.1 y.2) sâ‚‚ y.2) :
    HasFDerivWithinAt â†¿f (fâ‚x.coprod (fâ‚‚ xâ‚ xâ‚‚)) (sâ‚ Ã—Ë¢ sâ‚‚) (xâ‚, xâ‚‚) := by
  unfold ContinuousWithinAt at cfâ‚‚
  rw [nhdsWithin_prod_eq] at cfâ‚‚ dfâ‚‚
  rw [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO]
  calc
    fun y => â†¿f y - f xâ‚ xâ‚‚ - (fâ‚x.coprod (fâ‚‚ xâ‚ xâ‚‚)) (y.1 - xâ‚, y.2 - xâ‚‚)
    _ = fun y => f y.1 xâ‚‚ - f xâ‚ xâ‚‚ - fâ‚x (y.1 - xâ‚) + (â†¿f y - f y.1 xâ‚‚ - fâ‚‚ xâ‚ xâ‚‚ (y.2 - xâ‚‚)) := by
      ext
      rw [ContinuousLinearMap.coprod_apply]
      abel
    _ =o[ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚)] fun y => (y.1 - xâ‚, y.2 - xâ‚‚) := by
      apply IsLittleO.add
      Â· calc
          _ = (fun yâ‚ => f yâ‚ xâ‚‚ - f xâ‚ xâ‚‚ - fâ‚x (yâ‚ - xâ‚)) âˆ˜ Prod.fst := by
            rw [Function.comp_def]
          _ =o[ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚)] ((fun yâ‚ => yâ‚ - xâ‚) âˆ˜ Prod.fst) := by
            rw [nhdsWithin_prod_eq]
            apply IsLittleO.comp_fst
            rwa [HasFDerivWithinAt, hasFDerivAtFilter_iff_isLittleO] at dfâ‚x
          _ =O[ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚)] fun y => (y.1 - xâ‚, y.2 - xâ‚‚) := by
            simp [isBigO_of_le]
      Â· calc
          fun y => f y.1 y.2 - f y.1 xâ‚‚ - fâ‚‚ xâ‚ xâ‚‚ (y.2 - xâ‚‚)
          _ =o[ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚)] fun y => y.2 - xâ‚‚ := by
            have h := (tendsto_fst.comp tendsto_fst).prodMk <|
              tendsto_snd (f := ğ“[sâ‚] xâ‚ Ã—Ë¢ ğ“[sâ‚‚] xâ‚‚) (g := ğ“[sâ‚‚] xâ‚‚)
            apply isLittleO_sub_sub_fderiv (E := Eâ‚ Ã— Eâ‚‚) (f' := fun y v => fâ‚‚ y.1 v)
            Â· simpa [nhdsWithin_prod_eq] using tendsto_nhds_of_tendsto_nhdsWithin tendsto_snd
            Â· exact tendsto_const_nhds
            Â· simpa [nhdsWithin_prod_eq] using seg.prod_inr _
            Â· simpa [ContinuousWithinAt, nhdsWithin_prod_eq] using cfâ‚‚.comp h
            Â· simpa [nhdsWithin_prod_eq] using h.eventually dfâ‚‚
          _ =O[ğ“[sâ‚ Ã—Ë¢ sâ‚‚] (xâ‚, xâ‚‚)] fun y => (y.1 - xâ‚, y.2 - xâ‚‚) := by
            simp [isBigO_of_le]
