/-
Copyright (c) 2025 Igor Khavkine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Igor Khavkine
-/
import Mathlib.Analysis.Calculus.MeanValue

/-!
# Partial derivatives

In this file we prove some basic facts about partial derivatives of functions
defined on a product space, like `f : E √ó F ‚Üí G`:

* `HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn` ,
  `HasFDerivWithinAt.continuousOn_open_of_partial_continuousOn` : if `f` is differentiable
  on an open set `u`, such that the partial derivaties along `E` and `F` are continuous on `u`,
  then `f` is continuously differentiable on `E √ó F`. The first version has simplified
  hypotheses for wen `u = s √óÀ¢ t` is a product of open sets.

* `HasFDerivWithinAt.partial_continuousOn_of_continuousOn` ,
  `HasFDerivWithinAt.partial_continuous_of_continuousOn_prod` : if `f` is continuously
  differentiable within a set `u`, then it is partially differentiable `E` and `F` within `u`
  and its partial derivatives, given by restricting the derivative of `f`, are also
  continuous within `u`. This is the easier converse direction of the preceding result.

The proofs follow ¬ß9.8.1 from Dieudonn√©'s *Foundations of Modern Analysis* (1969).
-/

open Set Function Metric Real

section PartialFDeriv

universe u_EF

theorem continuousOn_swap {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    (s : Set (X √ó Y)) : ContinuousOn Prod.swap s := by
  unfold ContinuousOn; intro z hz
  apply continuous_swap.continuousWithinAt

open ContinuousLinearMap in
theorem ContinousLinearMap.coprod_comp_prod
  (R : Type*) [Semiring R]
  (M : Type*) [TopologicalSpace M] [AddCommMonoid M] [Module R M]
  (M‚ÇÇ : Type*) [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ]
  (M‚ÇÉ : Type*) [TopologicalSpace M‚ÇÉ] [AddCommMonoid M‚ÇÉ] [Module R M‚ÇÉ]
  (M‚ÇÑ : Type*) [TopologicalSpace M‚ÇÑ] [AddCommMonoid M‚ÇÑ] [Module R M‚ÇÑ] [ContinuousAdd M‚ÇÑ]
  (f : M‚ÇÇ ‚ÜíL[R] M‚ÇÑ) (g : M‚ÇÉ ‚ÜíL[R] M‚ÇÑ) (f' : M ‚ÜíL[R] M‚ÇÇ) (g' : M ‚ÜíL[R] M‚ÇÉ) :
    (f.coprod g).comp  (f'.prod g') = f.comp f' + g.comp g' := by
  ext
  simp only [coe_comp', Function.comp_apply, prod_apply, coprod_apply, add_apply]

abbrev ContinuousLinearMap.swap
  (ùïú : Type*) [Semiring ùïú]
  (E : Type*) [TopologicalSpace E] [AddCommMonoid E] [Module ùïú E]
  (F : Type*) [TopologicalSpace F] [AddCommMonoid F] [Module ùïú F]
  := (ContinuousLinearMap.snd ùïú E F).prod (ContinuousLinearMap.fst ùïú E F)

open ContinuousLinearMap in
theorem ContinousLinearMap.coprod_comp_swap
  (R : Type*) [Semiring R]
  (M‚ÇÅ : Type*) [TopologicalSpace M‚ÇÅ] [AddCommMonoid M‚ÇÅ] [Module R M‚ÇÅ]
  (M‚ÇÇ : Type*) [TopologicalSpace M‚ÇÇ] [AddCommMonoid M‚ÇÇ] [Module R M‚ÇÇ]
  (M : Type*) [TopologicalSpace M] [AddCommMonoid M] [Module R M] [ContinuousAdd M]
  (f : M‚ÇÅ ‚ÜíL[R] M) (g : M‚ÇÇ ‚ÜíL[R] M) :
    (f.coprod g).comp (ContinuousLinearMap.swap R _ _) = (g.coprod f) := by
  ext; all_goals
  simp only [coe_comp', Function.comp_apply, inl_apply, inr_apply, prod_apply, coe_snd', coe_fst',
    coprod_apply, map_zero, zero_add, add_zero, coprod_comp_inl, coprod_comp_inr]

theorem hasFDerivWithinAt_swap
  (ùïú : Type*) [NontriviallyNormedField ùïú]
  (E : Type*) [NormedAddCommGroup E] [NormedSpace ùïú E]
  (F : Type*) [NormedAddCommGroup F] [NormedSpace ùïú F]
  (s : Set (E √ó F)) (z : E √ó F) :
    HasFDerivWithinAt
      (Prod.swap : E √ó F ‚Üí F √ó E)
      (ContinuousLinearMap.swap ùïú E F)
      s z
  := hasFDerivWithinAt_snd.prodMk hasFDerivWithinAt_fst

/-- If a function `f : E √ó F ‚Üí G` has partial derivatives `fx` and `fy` continuous
  on an open set `s √óÀ¢ t`, then `f` is continously differentiable on this set, with
  the deriative given by combining `fx` and `fy`.
-/
theorem HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  {E : Type u_EF} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedSpace ùïú E]
  {F : Type u_EF} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {s : Set E} {t : Set F} (hs : IsOpen s) (ht : IsOpen t)
  {fx : E √ó F ‚Üí E ‚ÜíL[ùïú] G} {fy : E √ó F ‚Üí F ‚ÜíL[ùïú] G}
  (fxy_cont : ContinuousOn fx (s √óÀ¢ t) ‚à® ContinuousOn fy (s √óÀ¢ t))
  (hfx : ‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt (f ‚àò (¬∑, z.2)) (fx z) s z.1)
  (hfy : ‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) (fy z) t z.2) :
    ‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt f ((fx z).coprod (fy z)) (s √óÀ¢ t) z := by
  -- if fx is continuous, WLOG swap the E and F arguments
  wlog fy_cont : ContinuousOn fy (s √óÀ¢ t)
  case inr =>
    have fx_cont := Or.resolve_right fxy_cont fy_cont
    clear fy_cont fxy_cont
    intro z hz
    simp only [mem_prod, Prod.fst_swap, Prod.snd_swap] at hz
    have hmt_st := mapsTo_swap_prod s t
    have hmt_ts := mapsTo_swap_prod t s
    have fx_swap_cont := fx_cont.comp (continuousOn_swap (t √óÀ¢ s)) hmt_ts
    have hswap := this
      (f := f ‚àò Prod.swap)
      ht hs
      (Or.inr fx_swap_cont)
      (fun z' hz' => (hfy z'.swap (hmt_ts hz')))
      (fun z' hz' => (hfx z'.swap (hmt_ts hz')))
      fx_swap_cont
      z.swap hz.symm
    convert hswap.comp z (hasFDerivWithinAt_swap ùïú E F (s √óÀ¢ t) z) hmt_st
    simp only [ContinousLinearMap.coprod_comp_swap, comp_apply, Prod.swap_swap]
  -- now continue the proof with the default hypothesis that fy is continuous
  clear fxy_cont
  intro z hz
  replace hz : _ ‚àß _ := ‚ü®mem_prod.mp hz, hz‚ü©
  simp only at hz
  have hfy_within := hfy -- save hypothesis hfy before rewriting it
  -- rewrite derivatives as limits using norms
  simp only [hasFDerivWithinAt_iff_tendsto, tendsto_nhdsWithin_nhds, dist_eq_norm] at ‚ä¢ hfx hfy
  simp only [ContinuousLinearMap.coprod_apply, sub_zero, norm_mul, norm_inv, norm_norm] at ‚ä¢ hfx hfy
  simp only [Metric.continuousOn_iff, dist_eq_norm, norm_eq_abs] at fy_cont
  -- get a target Œµ' and immediately shrink it to Œµ for convenice
  intro Œµ' hŒµ'
  rw [show Œµ' = 2*(Œµ'/2/2/2) + 2*(Œµ'/2/2/2) + 2*(Œµ'/2/2/2) + 2*(Œµ'/2/2/2) by ring]
  have hŒµ := half_pos (half_pos (half_pos hŒµ'))
  set Œµ := Œµ' / 2 / 2 / 2
  -- get Œ¥x from x-differentiability and Œ¥y from continuity of y-derivative
  -- also Œ¥s and Œ¥t are constrained by the possibly small sizes of s and t
  obtain ‚ü®Œ¥x, hŒ¥x, hfx_z‚ü© := hfx z hz.2 Œµ hŒµ
  obtain ‚ü®Œ¥y, hŒ¥y, hfy_z‚ü© := fy_cont z hz.2 Œµ hŒµ
  obtain ‚ü®Œ¥s, hŒ¥s‚ü© := isOpen_iff.mp hs z.1 hz.1.1
  obtain ‚ü®Œ¥t, hŒ¥t‚ü© := isOpen_iff.mp ht z.2 hz.1.2
  use (min (min Œ¥x Œ¥s) (min Œ¥y Œ¥t)) -- derive desired Œ¥
  constructor; ¬∑ exact lt_min (lt_min hŒ¥x hŒ¥s.1) (lt_min hŒ¥y hŒ¥t.1) -- positivity of Œ¥
  -- get working point (x,y) ‚àà E √ó F within Œ¥ distance of z
  intro (x,y) hst hŒ¥
  replace hst : _ ‚àß _ := ‚ü®mem_prod.mp hst, hst‚ü©
  simp only at hst
  simp only [Prod.fst_sub, Prod.snd_sub]
  rw [mul_comm]
  -- simplify norm conditions into bounds on ‚Äñx-z.1‚Äñ and ‚Äñy-z.2‚Äñ
  have hxx := hŒ¥
  simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub] at hxx
  simp only [lt_inf_iff, sup_lt_iff] at hxx
  replace ‚ü®‚ü®‚ü®hxx, hyx‚ü©, ‚ü®hxs, hys‚ü©‚ü©, ‚ü®‚ü®hxy, hyy‚ü©, ‚ü®hxt, hyt‚ü©‚ü©‚ü© := hxx
  -- rewrite desired variation in f for easier estimation
  have hf := calc
    f (x,y) - f z - ((fx z) (x - z.1) + (fy z) (y - z.2))
      = f (x,y) - f (x,z.2)
      + f (x,z.2) - f (z.1,z.2) - ((fx z) (x - z.1) + (fy z) (y - z.2)) := by
        simp only [map_sub, sub_add_cancel, Prod.mk.eta]
    _ = f (x,y) - f (x,z.2) - (fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        rw [add_comm _ (fy _ _), ‚Üê sub_sub]
        rw [sub_right_comm _ _ (fy _ _), add_sub_right_comm _ _ (fy _ _)]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2)) (y - z.2) - (fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        simp only [map_sub, Prod.mk.eta, sub_add_cancel]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1) := by
        rw [ContinuousLinearMap.sub_apply]
        simp only [map_sub, sub_add_cancel, Prod.mk.eta, sub_add_sub_cancel]
    _ = f (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1)) := by
        rw [add_sub_assoc _ (f _) _, add_sub_assoc _ ((f _) - _) _]
  -- set up the hypotheses and use the inequality version of the Mean Value Theorem
  have mvt_diff : ‚àÄ y ‚àà ball z.2 (min Œ¥y Œ¥t),
      HasFDerivWithinAt (f ‚àò (x,¬∑)) (fy (x,y)) (ball z.2 (min Œ¥y Œ¥t)) y := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    apply (hfy_within (x,y') (mem_prod.mpr ‚ü®hst.1.1, _‚ü©)).mono
    ¬∑ calc
        ball z.2 (min Œ¥y Œ¥t) ‚äÜ ball z.2 Œ¥t := ball_subset_ball (min_le_right _ _)
        _ ‚äÜ t := hŒ¥t.2
    ¬∑ exact mem_of_subset_of_mem hŒ¥t.2 (mem_ball_iff_norm.mpr hy'.2)
  have mvt_bound : ‚àÄ y' ‚àà ball z.2 (min Œ¥y Œ¥t), ‚Äñfy (x,y') - fy (x,z.2)‚Äñ ‚â§ Œµ + Œµ := by
    intro y' hy'
    rw [mem_ball_iff_norm, lt_min_iff] at hy'
    rw [‚Üê dist_eq_norm]
    apply (dist_triangle _ (fy z) _).trans
    rw [dist_eq_norm, dist_eq_norm, norm_sub_rev (fy z) _]
    have hxy' : ‚Äñ(x,y') - z‚Äñ < Œ¥y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sup_lt_iff]
      exact ‚ü®hxy, hy'.1‚ü©
    have hxz2 : ‚Äñ(x,z.2) - z‚Äñ < Œ¥y := by
      simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
        sup_of_le_left]
      exact hxy
    apply add_le_add (hfy_z _ _ hxy').le (hfy_z _ _ hxz2).le
    ¬∑ apply mem_prod.mpr ‚ü®hst.1.1, _‚ü©
      exact mem_of_subset_of_mem hŒ¥t.2 (mem_ball_iff_norm.mpr hy'.2)
    ¬∑ exact mem_prod.mpr ‚ü®hst.1.1, hz.1.2‚ü©
  have mvt {a b} (ha : a ‚àà _) (hb : b ‚àà _) :=
    -- inequality version of Mean Value Theorem
    Convex.norm_image_sub_le_of_norm_hasFDerivWithin_le'
      mvt_diff
      mvt_bound
      (convex_ball z.2 (min Œ¥y Œ¥t)) ha hb
  simp only [comp_apply] at mvt
  -- use the calculation above and start applying norms and estimates, term by term
  rw [hf]
  replace hf := calc
    ‚Äñf (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)
      + (fy (x,z.2) - fy z) (y - z.2)
      + (f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1))‚Äñ
      ‚â§ ‚Äñf (x,y) - f (x,z.2) - (fy (x,z.2)) (y - z.2)‚Äñ
      + ‚Äñ(fy (x,z.2) - fy z) (y - z.2)‚Äñ
      + ‚Äñ(f (x,z.2) - f (z.1,z.2) - (fx z) (x - z.1))‚Äñ := norm_add‚ÇÉ_le
    _ ‚â§ (Œµ + Œµ) * ‚Äñy - z.2‚Äñ
      + ‚Äñ(fy (x,z.2) - fy z)‚Äñ * ‚Äñy - z.2‚Äñ
      + Œµ * ‚Äñx - z.1‚Äñ := by
        apply add_le_add (add_le_add _ _) _ -- compare term by term
        ¬∑ exact mvt -- Mean Value estimate
            (mem_ball_self (lt_min hŒ¥y hŒ¥t.1))
            (mem_ball_iff_norm.mpr (lt_min hyy hyt))
        ¬∑ exact ContinuousLinearMap.le_opNorm _ _ -- operator norm estimate
        ¬∑ rw [mul_comm]
          by_cases hxnz : 0 < ‚Äñx - z.1‚Äñ
          case neg => -- handle trivial x = z.1 case
            replace hxnz := (not_lt.mp hxnz).antisymm (norm_nonneg _)
            have hxnz' := eq_of_sub_eq_zero (norm_eq_zero.mp hxnz)
            repeat rw [hxnz, hxnz']
            simp only [Prod.mk.eta, sub_self, map_zero, norm_zero, zero_mul, le_refl]
          case pos =>
            apply (inv_mul_le_iff‚ÇÄ hxnz).mp
            exact (hfx_z hst.1.1 hxx).le -- apply differentiability estimate
    _ ‚â§ Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñy - z.2‚Äñ + Œµ * ‚Äñx - z.1‚Äñ := by
        rw [add_mul]
        apply add_le_add (add_le_add le_rfl _) le_rfl
        apply mul_le_mul (hfy_z _ _ _).le le_rfl (norm_nonneg (y - z.2)) hŒµ.le
        ¬∑ exact (mem_prod.mpr ‚ü®hst.1.1, hz.1.2‚ü©)
        ¬∑ simp only [Prod.norm_def, Prod.fst_sub, Prod.snd_sub, sub_self, norm_zero, norm_nonneg,
          sup_of_le_left, hxy]
  -- now apply the estimate hf to the goal
  apply (mul_le_mul_of_nonneg_right hf (by simp only [inv_nonneg, norm_nonneg])).trans_lt _
  -- it remains only to simplify the inequality term by term and compare coefficients
  simp only [add_mul, mul_assoc]
  rw [mul_comm 2]
  apply add_lt_add (add_lt_add (add_lt_add _ _) _)
  all_goals
    apply (mul_lt_mul_left hŒµ).mpr
    refine LE.le.trans_lt ?_ (one_lt_two)
    rw [mul_comm]
    apply inv_mul_le_of_le_mul‚ÇÄ (norm_nonneg _) zero_le_one
    simp only [mul_one, Prod.norm_def, Prod.fst_sub, Prod.snd_sub]
    first | exact le_max_right _ _ | exact le_max_left _ _

/-- If a function `f : E √ó F ‚Üí G` has partial derivatives `fx` and `fy` continuous
  on an open set `u`, then `f` is continously differentiable on this set, with
  the deriative given by combining `fx` and `fy`.

  See `HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn` for simplified
  version with `u = s √óÀ¢ t` being a product of two opens.
-/
theorem HasFDerivWithinAt.continuousOn_open_of_partial_continuousOn
  {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú]
  {E : Type u_EF} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [NormedSpace ùïú E]
  {F : Type u_EF} [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {u : Set (E √ó F)} (hu : IsOpen u)
  {fx : E √ó F ‚Üí E ‚ÜíL[ùïú] G} {fy : E √ó F ‚Üí F ‚ÜíL[ùïú] G}
  (fxy_cont : ContinuousOn fx u ‚à® ContinuousOn fy u)
  (hfx : ‚àÄ z ‚àà u, HasFDerivWithinAt (f ‚àò (¬∑, z.2)) (fx z) ((¬∑,z.2) ‚Åª¬π' u) z.1)
  (hfy : ‚àÄ z ‚àà u, HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) (fy z) ((z.1,¬∑) ‚Åª¬π' u) z.2) :
    ‚àÄ z ‚àà u, HasFDerivWithinAt f ((fx z).coprod (fy z)) u z := by
  intro z hz
  obtain ‚ü®s,t,hs,ht,hz1,hz2,hst‚ü© := isOpen_prod_iff.mp hu z.1 z.2 hz
  have hstn : s √óÀ¢ t ‚àà nhds z := IsOpen.mem_nhds (hs.prod ht) (mem_prod.mpr ‚ü®hz1, hz2‚ü©)
  apply (hasFDerivWithinAt_inter hstn).mp
  rw [‚Üê right_eq_inter.mpr hst]
  have hsu (z : E √ó F) (hz : z ‚àà s √óÀ¢ t) : s ‚äÜ ((¬∑,z.2) ‚Åª¬π' u) := by
    apply HasSubset.Subset.trans _ (preimage_mono hst)
    rw [mk_preimage_prod_left (mem_prod.mpr hz).2]
  have htu (z : E √ó F) (hz : z ‚àà s √óÀ¢ t) : t ‚äÜ ((z.1,¬∑) ‚Åª¬π' u) := by
    apply HasSubset.Subset.trans _ (preimage_mono hst)
    rw [mk_preimage_prod_right (mem_prod.mpr hz).1]
  have fxy_cont_st : ContinuousOn fx (s √óÀ¢ t) ‚à® ContinuousOn fy (s √óÀ¢ t) := by
    rcases fxy_cont with fx_cont | fy_cont
    ¬∑ exact Or.inl (fx_cont.mono hst)
    ¬∑ exact Or.inr (fy_cont.mono hst)
  apply HasFDerivWithinAt.continuousOn_open_prod_of_partial_continuousOn
    hs ht
    fxy_cont_st
    _ _
    z (mem_prod.mpr ‚ü®hz1, hz2‚ü©)
  ¬∑ exact (fun z hz => (hfx z (mem_of_subset_of_mem hst hz)).mono (hsu z hz))
  ¬∑ exact (fun z hz => (hfy z (mem_of_subset_of_mem hst hz)).mono (htu z hz))

/-- If `f : E √ó F ‚Üí G` is continuously differentiable within a set `u`, then
  it is partially differentiable within `u` and its partial derivatives,
  obtained by restricting the total derivative of `f`, are also continuous on `u`.

  See `HasFDerivWithinAt.partial_continuousOn_of_continuousOn_prod` for a simplified
  version where `u = s √óÀ¢ t` is a product set.
-/
theorem HasFDerivWithinAt.partial_continuousOn_of_continuousOn
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {f' : E √ó F ‚Üí E √ó F ‚ÜíL[ùïú] G} {u : Set (E √ó F)}
  (hfc : ContinuousOn f' u) (hf : ‚àÄ z ‚àà u, HasFDerivWithinAt f (f' z) u z) :
    let fx' := fun z => (f' z).comp (.inl _ _ _);
    let fy' := fun z => (f' z).comp (.inr _ _ _);
    (ContinuousOn fx' u ‚àß ContinuousOn fy' u) ‚àß
    ( (‚àÄ z ‚àà u, HasFDerivWithinAt (f ‚àò (¬∑ ,z.2)) (fx' z) ((¬∑,z.2) ‚Åª¬π' u) z.1)
    ‚àß (‚àÄ z ‚àà u, HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) (fy' z) ((z.1,¬∑) ‚Åª¬π' u) z.2) )
    := by
  set fx' := fun z => (f' z).comp (.inl _ _ _)
  set fy' := fun z => (f' z).comp (.inr _ _ _)
  refine ‚ü®?cont, ?diff‚ü©
  case cont =>
    rw [continuousOn_iff_continuous_restrict] at hfc ‚ä¢
    rw [continuousOn_iff_continuous_restrict] at ‚ä¢
    exact ‚ü®hfc.clm_comp_const _, hfc.clm_comp_const _‚ü©
  case diff =>
    rw [‚Üê forall‚ÇÇ_and]
    intro z hz
    have hz12 := (Prod.mk.eta (p := z)).symm ‚ñ∏ hz
    set su := ((¬∑ ,z.2)) ‚Åª¬π' u
    set tu := ((z.1, ¬∑)) ‚Åª¬π' u
    set fx := (f ‚àò (¬∑ ,z.2))
    set fy := (f ‚àò (z.1, ¬∑))
    have hfx (x:E) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_id (ùïú := ùïú) x su)
      (hasFDerivWithinAt_const z.2 x su)
    have hfy (y:F) := HasFDerivWithinAt.prodMk
      (hasFDerivWithinAt_const z.1 y tu)
      (hasFDerivWithinAt_id (ùïú := ùïú) y tu)
    refine ‚ü®HasFDerivWithinAt.comp z.1 (hf (z.1,z.2) hz12) (hfx z.1) ?_,
      HasFDerivWithinAt.comp z.2 (hf (z.1,z.2) hz12) (hfy z.2) ?_‚ü©
    ¬∑ exact fun ‚¶É_‚¶Ñ a ‚Ü¶ a
    ¬∑ exact fun ‚¶É_‚¶Ñ a ‚Ü¶ a

/-- If `f : E √ó F ‚Üí G` is continuously differentiable within a set `s √óÀ¢ t`, then
  it is partially differentiable within `s √óÀ¢ t` and its partial derivatives,
  obtained by restricting the total derivative of `f`, are also continuous on `s √óÀ¢ t`.
-/
theorem HasFDerivWithinAt.partial_continuousOn_of_continuousOn_prod
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ùïú G]
  {f : E √ó F ‚Üí G} {f' : E √ó F ‚Üí E √ó F ‚ÜíL[ùïú] G} {s : Set E} {t : Set F}
  (hf : ‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt f (f' z) (s √óÀ¢ t) z) (hfc : ContinuousOn f' (s √óÀ¢ t)) :
    let fx' := fun z => (f' z).comp (.inl _ _ _);
    let fy' := fun z => (f' z).comp (.inr _ _ _);
    (ContinuousOn fx' (s √óÀ¢ t) ‚àß ContinuousOn fy' (s √óÀ¢ t)) ‚àß
    ( (‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt (f ‚àò (¬∑ ,z.2)) (fx' z) s z.1)
    ‚àß (‚àÄ z ‚àà s √óÀ¢ t, HasFDerivWithinAt (f ‚àò (z.1, ¬∑)) (fy' z) t z.2) )
     := by
  refine ‚ü®?cont, ?diff‚ü©
  case cont =>
    rw [continuousOn_iff_continuous_restrict] at hfc ‚ä¢
    rw [continuousOn_iff_continuous_restrict] at ‚ä¢
    exact ‚ü®hfc.clm_comp_const _, hfc.clm_comp_const _‚ü©
  case diff =>
    rw [‚Üê forall‚ÇÇ_and]
    intro z hz
    have hsu : ((fun x ‚Ü¶ (x, z.2)) ‚Åª¬π' s √óÀ¢ t) = s := by
      exact mk_preimage_prod_left (mem_prod.mpr hz).2
    have htu : ((fun y ‚Ü¶ (z.1, y)) ‚Åª¬π' s √óÀ¢ t) = t := by
      exact mk_preimage_prod_right (mem_prod.mpr hz).1
    have := forall‚ÇÇ_and.mpr (HasFDerivWithinAt.partial_continuousOn_of_continuousOn hfc hf).2 z hz
    rw [hsu, htu] at this
    exact this

end PartialFDeriv
