/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Congr

/-!
# FrÃ©chet derivative of constant functions

This file contains the usual formulas (and existence assertions) for the derivative of constant
functions, including various special cases such as the functions `0`, `1`, `Nat.cast n`,
`Int.cast z`, and other numerals.

## Tags

derivative, differentiable, FrÃ©chet, calculus

-/

open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable {f : E â†’ F} {x : E} {s : Set E}

section Const

@[fun_prop]
theorem hasStrictFDerivAt_const (c : F) (x : E) :
    HasStrictFDerivAt (fun _ => c) (0 : E â†’L[ğ•œ] F) x :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left fun _ => by
    simp only [zero_apply, sub_self, Pi.zero_apply]

@[fun_prop]
theorem hasStrictFDerivAt_zero (x : E) :
    HasStrictFDerivAt (0 : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasStrictFDerivAt_const _ _

@[fun_prop]
theorem hasStrictFDerivAt_one [One F] (x : E) :
    HasStrictFDerivAt (1 : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasStrictFDerivAt_const _ _

@[fun_prop]
theorem hasStrictFDerivAt_natCast [NatCast F] (n : â„•) (x : E) :
    HasStrictFDerivAt (n : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasStrictFDerivAt_const _ _

@[fun_prop]
theorem hasStrictFDerivAt_intCast [IntCast F] (z : â„¤) (x : E) :
    HasStrictFDerivAt (z : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasStrictFDerivAt_const _ _

@[fun_prop]
theorem hasStrictFDerivAt_ofNat (n : â„•) [OfNat F n] (x : E) :
    HasStrictFDerivAt (ofNat(n) : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasStrictFDerivAt_const _ _

theorem hasFDerivAtFilter_const (c : F) (x : E) (L : Filter E) :
    HasFDerivAtFilter (fun _ => c) (0 : E â†’L[ğ•œ] F) x L :=
  .of_isLittleOTVS <| (IsLittleOTVS.zero _ _).congr_left fun _ => by
    simp only [zero_apply, sub_self, Pi.zero_apply]

theorem hasFDerivAtFilter_zero (x : E) (L : Filter E) :
    HasFDerivAtFilter (0 : E â†’ F) (0 : E â†’L[ğ•œ] F) x L := hasFDerivAtFilter_const _ _ _

theorem hasFDerivAtFilter_one [One F] (x : E) (L : Filter E) :
    HasFDerivAtFilter (1 : E â†’ F) (0 : E â†’L[ğ•œ] F) x L := hasFDerivAtFilter_const _ _ _

theorem hasFDerivAtFilter_natCast [NatCast F] (n : â„•) (x : E) (L : Filter E) :
    HasFDerivAtFilter (n : E â†’ F) (0 : E â†’L[ğ•œ] F) x L :=
  hasFDerivAtFilter_const _ _ _

theorem hasFDerivAtFilter_intCast [IntCast F] (z : â„¤) (x : E) (L : Filter E) :
    HasFDerivAtFilter (z : E â†’ F) (0 : E â†’L[ğ•œ] F) x L :=
  hasFDerivAtFilter_const _ _ _

theorem hasFDerivAtFilter_ofNat (n : â„•) [OfNat F n] (x : E) (L : Filter E) :
    HasFDerivAtFilter (ofNat(n) : E â†’ F) (0 : E â†’L[ğ•œ] F) x L :=
  hasFDerivAtFilter_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_const (c : F) (x : E) (s : Set E) :
    HasFDerivWithinAt (fun _ => c) (0 : E â†’L[ğ•œ] F) s x :=
  hasFDerivAtFilter_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_zero (x : E) (s : Set E) :
    HasFDerivWithinAt (0 : E â†’ F) (0 : E â†’L[ğ•œ] F) s x := hasFDerivWithinAt_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_one [One F] (x : E) (s : Set E) :
    HasFDerivWithinAt (1 : E â†’ F) (0 : E â†’L[ğ•œ] F) s x := hasFDerivWithinAt_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_natCast [NatCast F] (n : â„•) (x : E) (s : Set E) :
    HasFDerivWithinAt (n : E â†’ F) (0 : E â†’L[ğ•œ] F) s x :=
  hasFDerivWithinAt_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_intCast [IntCast F] (z : â„¤) (x : E) (s : Set E) :
    HasFDerivWithinAt (z : E â†’ F) (0 : E â†’L[ğ•œ] F) s x :=
  hasFDerivWithinAt_const _ _ _

@[fun_prop]
theorem hasFDerivWithinAt_ofNat (n : â„•) [OfNat F n] (x : E) (s : Set E) :
    HasFDerivWithinAt (ofNat(n) : E â†’ F) (0 : E â†’L[ğ•œ] F) s x :=
  hasFDerivWithinAt_const _ _ _

@[fun_prop]
theorem hasFDerivAt_const (c : F) (x : E) : HasFDerivAt (fun _ => c) (0 : E â†’L[ğ•œ] F) x :=
  hasFDerivAtFilter_const _ _ _

@[fun_prop]
theorem hasFDerivAt_zero (x : E) :
    HasFDerivAt (0 : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasFDerivAt_const _ _

@[fun_prop]
theorem hasFDerivAt_one [One F] (x : E) :
    HasFDerivAt (1 : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasFDerivAt_const _ _

@[fun_prop]
theorem hasFDerivAt_natCast [NatCast F] (n : â„•) (x : E) :
    HasFDerivAt (n : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasFDerivAt_const _ _

@[fun_prop]
theorem hasFDerivAt_intCast [IntCast F] (z : â„¤) (x : E) :
    HasFDerivAt (z : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasFDerivAt_const _ _

@[fun_prop]
theorem hasFDerivAt_ofNat (n : â„•) [OfNat F n] (x : E) :
    HasFDerivAt (ofNat(n) : E â†’ F) (0 : E â†’L[ğ•œ] F) x := hasFDerivAt_const _ _

@[simp, fun_prop]
theorem differentiableAt_const (c : F) : DifferentiableAt ğ•œ (fun _ => c) x :=
  âŸ¨0, hasFDerivAt_const c xâŸ©

@[simp, fun_prop]
theorem differentiableAt_zero (x : E) :
    DifferentiableAt ğ•œ (0 : E â†’ F) x := differentiableAt_const _

@[simp, fun_prop]
theorem differentiableAt_one [One F] (x : E) :
    DifferentiableAt ğ•œ (1 : E â†’ F) x := differentiableAt_const _

@[simp, fun_prop]
theorem differentiableAt_natCast [NatCast F] (n : â„•) (x : E) :
    DifferentiableAt ğ•œ (n : E â†’ F) x := differentiableAt_const _

@[simp, fun_prop]
theorem differentiableAt_intCast [IntCast F] (z : â„¤) (x : E) :
    DifferentiableAt ğ•œ (z : E â†’ F) x := differentiableAt_const _

@[simp low, fun_prop]
theorem differentiableAt_ofNat (n : â„•) [OfNat F n] (x : E) :
    DifferentiableAt ğ•œ (ofNat(n) : E â†’ F) x := differentiableAt_const _

@[fun_prop]
theorem differentiableWithinAt_const (c : F) : DifferentiableWithinAt ğ•œ (fun _ => c) s x :=
  DifferentiableAt.differentiableWithinAt (differentiableAt_const _)

@[fun_prop]
theorem differentiableWithinAt_zero :
    DifferentiableWithinAt ğ•œ (0 : E â†’ F) s x := differentiableWithinAt_const _

@[fun_prop]
theorem differentiableWithinAt_one [One F] :
    DifferentiableWithinAt ğ•œ (1 : E â†’ F) s x := differentiableWithinAt_const _

@[fun_prop]
theorem differentiableWithinAt_natCast [NatCast F] (n : â„•) :
    DifferentiableWithinAt ğ•œ (n : E â†’ F) s x := differentiableWithinAt_const _

@[fun_prop]
theorem differentiableWithinAt_intCast [IntCast F] (z : â„¤) :
    DifferentiableWithinAt ğ•œ (z : E â†’ F) s x := differentiableWithinAt_const _

@[fun_prop]
theorem differentiableWithinAt_ofNat (n : â„•) [OfNat F n] :
    DifferentiableWithinAt ğ•œ (ofNat(n) : E â†’ F) s x := differentiableWithinAt_const _

theorem fderivWithin_const_apply (c : F) : fderivWithin ğ•œ (fun _ => c) s x = 0 := by
  rw [fderivWithin, if_pos]
  apply hasFDerivWithinAt_const

@[simp]
theorem fderivWithin_fun_const (c : F) : fderivWithin ğ•œ (fun _ â†¦ c) s = 0 := by
  ext
  rw [fderivWithin_const_apply, Pi.zero_apply]

@[simp]
theorem fderivWithin_const (c : F) : fderivWithin ğ•œ (Function.const E c) s = 0 :=
  fderivWithin_fun_const c

@[simp]
theorem fderivWithin_zero : fderivWithin ğ•œ (0 : E â†’ F) s = 0 := fderivWithin_const _

@[simp]
theorem fderivWithin_one [One F] : fderivWithin ğ•œ (1 : E â†’ F) s = 0 := fderivWithin_const _

@[simp]
theorem fderivWithin_natCast [NatCast F] (n : â„•) : fderivWithin ğ•œ (n : E â†’ F) s = 0 :=
  fderivWithin_const _

@[simp]
theorem fderivWithin_intCast [IntCast F] (z : â„¤) : fderivWithin ğ•œ (z : E â†’ F) s = 0 :=
  fderivWithin_const _

@[simp low]
theorem fderivWithin_ofNat (n : â„•) [OfNat F n] : fderivWithin ğ•œ (ofNat(n) : E â†’ F) s = 0 :=
  fderivWithin_const _

theorem fderiv_const_apply (c : F) : fderiv ğ•œ (fun _ => c) x = 0 :=
  (hasFDerivAt_const c x).fderiv

@[simp]
theorem fderiv_fun_const (c : F) : fderiv ğ•œ (fun _ : E => c) = 0 := by
  rw [â† fderivWithin_univ, fderivWithin_fun_const]

@[simp]
theorem fderiv_const (c : F) : fderiv ğ•œ (Function.const E c) = 0 :=
  fderiv_fun_const c

@[simp]
theorem fderiv_zero : fderiv ğ•œ (0 : E â†’ F) = 0 := fderiv_const _

@[simp]
theorem fderiv_one [One F] : fderiv ğ•œ (1 : E â†’ F) = 0 := fderiv_const _

@[simp]
theorem fderiv_natCast [NatCast F] (n : â„•) : fderiv ğ•œ (n : E â†’ F) = 0 := fderiv_const _

@[simp]
theorem fderiv_intCast [IntCast F] (z : â„¤) : fderiv ğ•œ (z : E â†’ F) = 0 := fderiv_const _

@[simp low]
theorem fderiv_ofNat (n : â„•) [OfNat F n] : fderiv ğ•œ (ofNat(n) : E â†’ F) = 0 := fderiv_const _

@[simp, fun_prop]
theorem differentiable_const (c : F) : Differentiable ğ•œ fun _ : E => c := fun _ =>
  differentiableAt_const _

@[simp, fun_prop]
theorem differentiable_zero :
    Differentiable ğ•œ (0 : E â†’ F) := differentiable_const _

@[simp, fun_prop]
theorem differentiable_one [One F] :
    Differentiable ğ•œ (1 : E â†’ F) := differentiable_const _

@[simp, fun_prop]
theorem differentiable_natCast [NatCast F] (n : â„•) :
    Differentiable ğ•œ (n : E â†’ F) := differentiable_const _

@[simp, fun_prop]
theorem differentiable_intCast [IntCast F] (z : â„¤) :
    Differentiable ğ•œ (z : E â†’ F) := differentiable_const _

@[simp low, fun_prop]
theorem differentiable_ofNat (n : â„•) [OfNat F n] :
    Differentiable ğ•œ (ofNat(n) : E â†’ F) := differentiable_const _

@[simp, fun_prop]
theorem differentiableOn_const (c : F) : DifferentiableOn ğ•œ (fun _ => c) s :=
  (differentiable_const _).differentiableOn

@[simp, fun_prop]
theorem differentiableOn_zero :
    DifferentiableOn ğ•œ (0 : E â†’ F) s := differentiableOn_const _

@[simp, fun_prop]
theorem differentiableOn_one [One F] :
    DifferentiableOn ğ•œ (1 : E â†’ F) s := differentiableOn_const _

@[simp, fun_prop]
theorem differentiableOn_natCast [NatCast F] (n : â„•) :
    DifferentiableOn ğ•œ (n : E â†’ F) s := differentiableOn_const _

@[simp, fun_prop]
theorem differentiableOn_intCast [IntCast F] (z : â„¤) :
    DifferentiableOn ğ•œ (z : E â†’ F) s := differentiableOn_const _

@[simp low, fun_prop]
theorem differentiableOn_ofNat (n : â„•) [OfNat F n] :
    DifferentiableOn ğ•œ (ofNat(n) : E â†’ F) s := differentiableOn_const _

@[fun_prop]
theorem hasFDerivWithinAt_singleton (f : E â†’ F) (x : E) :
    HasFDerivWithinAt f (0 : E â†’L[ğ•œ] F) {x} x := by
  simp only [HasFDerivWithinAt, nhdsWithin_singleton, hasFDerivAtFilter_iff_isLittleO,
    isLittleO_pure, ContinuousLinearMap.zero_apply, sub_self]

@[fun_prop]
theorem hasFDerivAt_of_subsingleton [h : Subsingleton E] (f : E â†’ F) (x : E) :
    HasFDerivAt f (0 : E â†’L[ğ•œ] F) x := by
  rw [â† hasFDerivWithinAt_univ, subsingleton_univ.eq_singleton_of_mem (mem_univ x)]
  exact hasFDerivWithinAt_singleton f x

@[fun_prop]
theorem differentiableOn_empty : DifferentiableOn ğ•œ f âˆ… := fun _ => False.elim

@[fun_prop]
theorem differentiableOn_singleton : DifferentiableOn ğ•œ f {x} :=
  forall_eq.2 (hasFDerivWithinAt_singleton f x).differentiableWithinAt

@[fun_prop]
theorem Set.Subsingleton.differentiableOn (hs : s.Subsingleton) : DifferentiableOn ğ•œ f s :=
  hs.induction_on differentiableOn_empty fun _ => differentiableOn_singleton

theorem hasFDerivAt_zero_of_eventually_const (c : F) (hf : f =á¶ [ğ“ x] fun _ => c) :
    HasFDerivAt f (0 : E â†’L[ğ•œ] F) x :=
  (hasFDerivAt_const _ _).congr_of_eventuallyEq hf

end Const

theorem differentiableWithinAt_of_isInvertible_fderivWithin
    (hf : (fderivWithin ğ•œ f s x).IsInvertible) : DifferentiableWithinAt ğ•œ f s x := by
  contrapose hf
  rw [fderivWithin_zero_of_not_differentiableWithinAt hf]
  contrapose! hf
  rcases isInvertible_zero_iff.1 hf with âŸ¨hE, hFâŸ©
  exact (hasFDerivAt_of_subsingleton _ _).differentiableAt.differentiableWithinAt

theorem differentiableAt_of_isInvertible_fderiv
    (hf : (fderiv ğ•œ f x).IsInvertible) : DifferentiableAt ğ•œ f x := by
  simp only [â† differentiableWithinAt_univ, â† fderivWithin_univ] at hf âŠ¢
  exact differentiableWithinAt_of_isInvertible_fderivWithin hf


/-! ### Support of derivatives -/

section Support

open Function

variable (ğ•œ : Type*) {E F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F} {x : E}

theorem HasStrictFDerivAt.of_notMem_tsupport (h : x âˆ‰ tsupport f) :
    HasStrictFDerivAt f (0 : E â†’L[ğ•œ] F) x := by
  rw [notMem_tsupport_iff_eventuallyEq] at h
  exact (hasStrictFDerivAt_const (0 : F) x).congr_of_eventuallyEq h.symm

@[deprecated (since := "2025-05-24")]
alias HasStrictFDerivAt.of_nmem_tsupport := HasStrictFDerivAt.of_notMem_tsupport

theorem HasFDerivAt.of_notMem_tsupport (h : x âˆ‰ tsupport f) :
    HasFDerivAt f (0 : E â†’L[ğ•œ] F) x :=
  (HasStrictFDerivAt.of_notMem_tsupport ğ•œ h).hasFDerivAt

@[deprecated (since := "2025-05-24")]
alias HasFDerivAt.of_nmem_tsupport := HasFDerivAt.of_notMem_tsupport

theorem HasFDerivWithinAt.of_notMem_tsupport {s : Set E} {x : E} (h : x âˆ‰ tsupport f) :
    HasFDerivWithinAt f (0 : E â†’L[ğ•œ] F) s x :=
  (HasFDerivAt.of_notMem_tsupport ğ•œ h).hasFDerivWithinAt

@[deprecated (since := "2025-05-23")]
alias HasFDerivWithinAt.of_not_mem_tsupport := HasFDerivWithinAt.of_notMem_tsupport

theorem fderiv_of_notMem_tsupport (h : x âˆ‰ tsupport f) : fderiv ğ•œ f x = 0 :=
  (HasFDerivAt.of_notMem_tsupport ğ•œ h).fderiv

@[deprecated (since := "2025-05-23")] alias fderiv_of_not_mem_tsupport := fderiv_of_notMem_tsupport

theorem support_fderiv_subset : support (fderiv ğ•œ f) âŠ† tsupport f := fun x â†¦ by
  rw [â† not_imp_not, notMem_support]
  exact fderiv_of_notMem_tsupport _

theorem tsupport_fderiv_subset : tsupport (fderiv ğ•œ f) âŠ† tsupport f :=
  closure_minimal (support_fderiv_subset ğ•œ) isClosed_closure

protected theorem HasCompactSupport.fderiv (hf : HasCompactSupport f) :
    HasCompactSupport (fderiv ğ•œ f) :=
  hf.mono' <| support_fderiv_subset ğ•œ

protected theorem HasCompactSupport.fderiv_apply (hf : HasCompactSupport f) (v : E) :
    HasCompactSupport (fderiv ğ•œ f Â· v) :=
  hf.fderiv ğ•œ |>.comp_left (g := fun L : E â†’L[ğ•œ] F â†¦ L v) rfl

end Support


end
