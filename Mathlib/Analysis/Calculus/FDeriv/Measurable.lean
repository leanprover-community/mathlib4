/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.Deriv.Basic
import Mathlib.Analysis.Calculus.Deriv.Slope
import Mathlib.Analysis.NormedSpace.BoundedLinearMaps
import Mathlib.Analysis.NormedSpace.FiniteDimension
import Mathlib.MeasureTheory.Constructions.BorelSpace.ContinuousLinearMap
import Mathlib.MeasureTheory.Function.StronglyMeasurable.Basic

#align_import analysis.calculus.fderiv_measurable from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Derivative is measurable

In this file we prove that the derivative of any function with complete codomain is a measurable
function. Namely, we prove:

* `measurableSet_of_differentiableAt`: the set `{x | DifferentiableAt ğ•œ f x}` is measurable;
* `measurable_fderiv`: the function `fderiv ğ•œ f` is measurable;
* `measurable_fderiv_apply_const`: for a fixed vector `y`, the function `fun x â†¦ fderiv ğ•œ f x y`
  is measurable;
* `measurable_deriv`: the function `deriv f` is measurable (for `f : ğ•œ â†’ F`).

We also show the same results for the right derivative on the real line
(see `measurable_derivWithin_Ici` and `measurable_derivWithin_Ioi`), following the same
proof strategy.

We also prove measurability statements for functions depending on a parameter: for `f : Î± â†’ E â†’ F`,
we show the measurability of `(p : Î± Ã— E) â†¦ fderiv ğ•œ (f p.1) p.2`. This requires additional
assumptions. We give versions of the above statements (appending `with_param` to their names) when
`f` is continuous and `E` is locally compact.

## Implementation

We give a proof that avoids second-countability issues, by expressing the differentiability set
as a function of open sets in the following way. Define `A (L, r, Îµ)` to be the set of points
where, on a ball of radius roughly `r` around `x`, the function is uniformly approximated by the
linear map `L`, up to `Îµ r`. It is an open set.
Let also `B (L, r, s, Îµ) = A (L, r, Îµ) âˆ© A (L, s, Îµ)`: we require that at two possibly different
scales `r` and `s`, the function is well approximated by the linear map `L`. It is also open.

We claim that the differentiability set of `f` is exactly
`D = â‹‚ Îµ > 0, â‹ƒ Î´ > 0, â‹‚ r, s < Î´, â‹ƒ L, B (L, r, s, Îµ)`.
In other words, for any `Îµ > 0`, we require that there is a size `Î´` such that, for any two scales
below this size, the function is well approximated by a linear map, common to the two scales.

The set `â‹ƒ L, B (L, r, s, Îµ)` is open, as a union of open sets. Converting the intersections and
unions to countable ones (using real numbers of the form `2 ^ (-n)`), it follows that the
differentiability set is measurable.

To prove the claim, there are two inclusions. One is trivial: if the function is differentiable
at `x`, then `x` belongs to `D` (just take `L` to be the derivative, and use that the
differentiability exactly says that the map is well approximated by `L`). This is proved in
`mem_A_of_differentiable` and `differentiable_set_subset_D`.

For the other direction, the difficulty is that `L` in the union may depend on `Îµ, r, s`. The key
point is that, in fact, it doesn't depend too much on them. First, if `x` belongs both to
`A (L, r, Îµ)` and `A (L', r, Îµ)`, then `L` and `L'` have to be close on a shell, and thus
`â€–L - L'â€–` is bounded by `Îµ` (see `norm_sub_le_of_mem_A`). Assume now `x âˆˆ D`. If one has two maps
`L` and `L'` such that `x` belongs to `A (L, r, Îµ)` and to `A (L', r', Îµ')`, one deduces that `L` is
close to `L'` by arguing as follows. Consider another scale `s` smaller than `r` and `r'`. Take a
linear map `Lâ‚` that approximates `f` around `x` both at scales `r` and `s` w.r.t. `Îµ` (it exists as
`x` belongs to `D`). Take also `Lâ‚‚` that approximates `f` around `x` both at scales `r'` and `s`
w.r.t. `Îµ'`. Then `Lâ‚` is close to `L` (as they are close on a shell of radius `r`), and `Lâ‚‚` is
close to `Lâ‚` (as they are close on a shell of radius `s`), and `L'` is close to `Lâ‚‚` (as they are
close on a shell of radius `r'`). It follows that `L` is close to `L'`, as we claimed.

It follows that the different approximating linear maps that show up form a Cauchy sequence when
`Îµ` tends to `0`. When the target space is complete, this sequence converges, to a limit `f'`.
With the same kind of arguments, one checks that `f` is differentiable with derivative `f'`.

To show that the derivative itself is measurable, add in the definition of `B` and `D` a set
`K` of continuous linear maps to which `L` should belong. Then, when `K` is complete, the set `D K`
is exactly the set of points where `f` is differentiable with a derivative in `K`.

## Tags

derivative, measurable function, Borel Ïƒ-algebra
-/

set_option linter.uppercaseLean3 false -- A B D

noncomputable section

open Set Metric Asymptotics Filter ContinuousLinearMap MeasureTheory TopologicalSpace
open scoped Topology

namespace ContinuousLinearMap

variable {ğ•œ E F : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

theorem measurable_applyâ‚‚ [MeasurableSpace E] [OpensMeasurableSpace E]
    [SecondCountableTopologyEither (E â†’L[ğ•œ] F) E]
    [MeasurableSpace F] [BorelSpace F] : Measurable fun p : (E â†’L[ğ•œ] F) Ã— E => p.1 p.2 :=
  isBoundedBilinearMap_apply.continuous.measurable
#align continuous_linear_map.measurable_applyâ‚‚ ContinuousLinearMap.measurable_applyâ‚‚

end ContinuousLinearMap

section fderiv

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {f : E â†’ F} (K : Set (E â†’L[ğ•œ] F))

namespace FDerivMeasurableAux

/-- The set `A f L r Îµ` is the set of points `x` around which the function `f` is well approximated
at scale `r` by the linear map `L`, up to an error `Îµ`. We tweak the definition to make sure that
this is an open set. -/
def A (f : E â†’ F) (L : E â†’L[ğ•œ] F) (r Îµ : â„) : Set E :=
  { x | âˆƒ r' âˆˆ Ioc (r / 2) r, âˆ€ y âˆˆ ball x r', âˆ€ z âˆˆ ball x r', â€–f z - f y - L (z - y)â€– < Îµ * r }
#align fderiv_measurable_aux.A FDerivMeasurableAux.A

/-- The set `B f K r s Îµ` is the set of points `x` around which there exists a continuous linear map
`L` belonging to `K` (a given set of continuous linear maps) that approximates well the
function `f` (up to an error `Îµ`), simultaneously at scales `r` and `s`. -/
def B (f : E â†’ F) (K : Set (E â†’L[ğ•œ] F)) (r s Îµ : â„) : Set E :=
  â‹ƒ L âˆˆ K, A f L r Îµ âˆ© A f L s Îµ
#align fderiv_measurable_aux.B FDerivMeasurableAux.B

/-- The set `D f K` is a complicated set constructed using countable intersections and unions. Its
main use is that, when `K` is complete, it is exactly the set of points where `f` is differentiable,
with a derivative in `K`. -/
def D (f : E â†’ F) (K : Set (E â†’L[ğ•œ] F)) : Set E :=
  â‹‚ e : â„•, â‹ƒ n : â„•, â‹‚ (p â‰¥ n) (q â‰¥ n), B f K ((1 / 2) ^ p) ((1 / 2) ^ q) ((1 / 2) ^ e)
#align fderiv_measurable_aux.D FDerivMeasurableAux.D

theorem isOpen_A (L : E â†’L[ğ•œ] F) (r Îµ : â„) : IsOpen (A f L r Îµ) := by
  rw [Metric.isOpen_iff]
  rintro x âŸ¨r', r'_mem, hr'âŸ©
  obtain âŸ¨s, s_gt, s_ltâŸ© : âˆƒ s : â„, r / 2 < s âˆ§ s < r' := exists_between r'_mem.1
  have : s âˆˆ Ioc (r / 2) r := âŸ¨s_gt, le_of_lt (s_lt.trans_le r'_mem.2)âŸ©
  refine âŸ¨r' - s, by linarith, fun x' hx' => âŸ¨s, this, ?_âŸ©âŸ©
  have B : ball x' s âŠ† ball x r' := ball_subset (le_of_lt hx')
  intro y hy z hz
  exact hr' y (B hy) z (B hz)
#align fderiv_measurable_aux.is_open_A FDerivMeasurableAux.isOpen_A

theorem isOpen_B {K : Set (E â†’L[ğ•œ] F)} {r s Îµ : â„} : IsOpen (B f K r s Îµ) := by
  simp [B, isOpen_biUnion, IsOpen.inter, isOpen_A]
#align fderiv_measurable_aux.is_open_B FDerivMeasurableAux.isOpen_B

theorem A_mono (L : E â†’L[ğ•œ] F) (r : â„) {Îµ Î´ : â„} (h : Îµ â‰¤ Î´) : A f L r Îµ âŠ† A f L r Î´ := by
  rintro x âŸ¨r', r'r, hr'âŸ©
  refine âŸ¨r', r'r, fun y hy z hz => (hr' y hy z hz).trans_le (mul_le_mul_of_nonneg_right h ?_)âŸ©
  linarith [mem_ball.1 hy, r'r.2, @dist_nonneg _ _ y x]
#align fderiv_measurable_aux.A_mono FDerivMeasurableAux.A_mono

theorem le_of_mem_A {r Îµ : â„} {L : E â†’L[ğ•œ] F} {x : E} (hx : x âˆˆ A f L r Îµ) {y z : E}
    (hy : y âˆˆ closedBall x (r / 2)) (hz : z âˆˆ closedBall x (r / 2)) :
    â€–f z - f y - L (z - y)â€– â‰¤ Îµ * r := by
  rcases hx with âŸ¨r', r'mem, hr'âŸ©
  apply le_of_lt
  exact hr' _ ((mem_closedBall.1 hy).trans_lt r'mem.1) _ ((mem_closedBall.1 hz).trans_lt r'mem.1)
#align fderiv_measurable_aux.le_of_mem_A FDerivMeasurableAux.le_of_mem_A

theorem mem_A_of_differentiable {Îµ : â„} (hÎµ : 0 < Îµ) {x : E} (hx : DifferentiableAt ğ•œ f x) :
    âˆƒ R > 0, âˆ€ r âˆˆ Ioo (0 : â„) R, x âˆˆ A f (fderiv ğ•œ f x) r Îµ := by
  let Î´ := (Îµ / 2) / 2
  obtain âŸ¨R, R_pos, hRâŸ© :
      âˆƒ R > 0, âˆ€ y âˆˆ ball x R, â€–f y - f x - fderiv ğ•œ f x (y - x)â€– â‰¤ Î´ * â€–y - xâ€– :=
    eventually_nhds_iff_ball.1 <| hx.hasFDerivAt.isLittleO.bound <| by positivity
  refine âŸ¨R, R_pos, fun r hr => ?_âŸ©
  have : r âˆˆ Ioc (r / 2) r := right_mem_Ioc.2 <| half_lt_self hr.1
  refine âŸ¨r, this, fun y hy z hz => ?_âŸ©
  calc
    â€–f z - f y - (fderiv ğ•œ f x) (z - y)â€– =
        â€–f z - f x - (fderiv ğ•œ f x) (z - x) - (f y - f x - (fderiv ğ•œ f x) (y - x))â€– := by
      simp only [map_sub]; abel_nf
    _ â‰¤ â€–f z - f x - (fderiv ğ•œ f x) (z - x)â€– + â€–f y - f x - (fderiv ğ•œ f x) (y - x)â€– :=
      norm_sub_le _ _
    _ â‰¤ Î´ * â€–z - xâ€– + Î´ * â€–y - xâ€– :=
      add_le_add (hR _ (ball_subset_ball hr.2.le hz)) (hR _ (ball_subset_ball hr.2.le hy))
    _ â‰¤ Î´ * r + Î´ * r := by rw [mem_ball_iff_norm] at hz hy; gcongr
    _ = (Îµ / 2) * r := by ring
    _ < Îµ * r := by gcongr; exacts [hr.1, half_lt_self hÎµ]
#align fderiv_measurable_aux.mem_A_of_differentiable FDerivMeasurableAux.mem_A_of_differentiable

theorem norm_sub_le_of_mem_A {c : ğ•œ} (hc : 1 < â€–câ€–) {r Îµ : â„} (hÎµ : 0 < Îµ) (hr : 0 < r) {x : E}
    {Lâ‚ Lâ‚‚ : E â†’L[ğ•œ] F} (hâ‚ : x âˆˆ A f Lâ‚ r Îµ) (hâ‚‚ : x âˆˆ A f Lâ‚‚ r Îµ) : â€–Lâ‚ - Lâ‚‚â€– â‰¤ 4 * â€–câ€– * Îµ := by
  refine opNorm_le_of_shell (half_pos hr) (by positivity) hc ?_
  intro y ley ylt
  rw [div_div, div_le_iff' (mul_pos (by norm_num : (0 : â„) < 2) (zero_lt_one.trans hc))] at ley
  calc
    â€–(Lâ‚ - Lâ‚‚) yâ€– = â€–f (x + y) - f x - Lâ‚‚ (x + y - x) - (f (x + y) - f x - Lâ‚ (x + y - x))â€– := by
      simp
    _ â‰¤ â€–f (x + y) - f x - Lâ‚‚ (x + y - x)â€– + â€–f (x + y) - f x - Lâ‚ (x + y - x)â€– := norm_sub_le _ _
    _ â‰¤ Îµ * r + Îµ * r := by
      apply add_le_add
      Â· apply le_of_mem_A hâ‚‚
        Â· simp only [le_of_lt (half_pos hr), mem_closedBall, dist_self]
        Â· simp only [dist_eq_norm, add_sub_cancel_left, mem_closedBall, ylt.le]
      Â· apply le_of_mem_A hâ‚
        Â· simp only [le_of_lt (half_pos hr), mem_closedBall, dist_self]
        Â· simp only [dist_eq_norm, add_sub_cancel_left, mem_closedBall, ylt.le]
    _ = 2 * Îµ * r := by ring
    _ â‰¤ 2 * Îµ * (2 * â€–câ€– * â€–yâ€–) := by gcongr
    _ = 4 * â€–câ€– * Îµ * â€–yâ€– := by ring
#align fderiv_measurable_aux.norm_sub_le_of_mem_A FDerivMeasurableAux.norm_sub_le_of_mem_A

/-- Easy inclusion: a differentiability point with derivative in `K` belongs to `D f K`. -/
theorem differentiable_set_subset_D :
    { x | DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K } âŠ† D f K := by
  intro x hx
  rw [D, mem_iInter]
  intro e
  have : (0 : â„) < (1 / 2) ^ e := by positivity
  rcases mem_A_of_differentiable this hx.1 with âŸ¨R, R_pos, hRâŸ©
  obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, (1 / 2) ^ n < R :=
    exists_pow_lt_of_lt_one R_pos (by norm_num : (1 : â„) / 2 < 1)
  simp only [mem_iUnion, mem_iInter, B, mem_inter_iff]
  refine âŸ¨n, fun p hp q hq => âŸ¨fderiv ğ•œ f x, hx.2, âŸ¨?_, ?_âŸ©âŸ©âŸ© <;>
    Â· refine hR _ âŸ¨pow_pos (by norm_num) _, lt_of_le_of_lt ?_ hnâŸ©
      exact pow_le_pow_of_le_one (by norm_num) (by norm_num) (by assumption)
#align fderiv_measurable_aux.differentiable_set_subset_D FDerivMeasurableAux.differentiable_set_subset_D

/-- Harder inclusion: at a point in `D f K`, the function `f` has a derivative, in `K`. -/
theorem D_subset_differentiable_set {K : Set (E â†’L[ğ•œ] F)} (hK : IsComplete K) :
    D f K âŠ† { x | DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K } := by
  have P : âˆ€ {n : â„•}, (0 : â„) < (1 / 2) ^ n := fun {n} => pow_pos (by norm_num) n
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  intro x hx
  have :
    âˆ€ e : â„•, âˆƒ n : â„•, âˆ€ p q, n â‰¤ p â†’ n â‰¤ q â†’
      âˆƒ L âˆˆ K, x âˆˆ A f L ((1 / 2) ^ p) ((1 / 2) ^ e) âˆ© A f L ((1 / 2) ^ q) ((1 / 2) ^ e) := by
    intro e
    have := mem_iInter.1 hx e
    rcases mem_iUnion.1 this with âŸ¨n, hnâŸ©
    refine âŸ¨n, fun p q hp hq => ?_âŸ©
    simp only [mem_iInter, ge_iff_le] at hn
    rcases mem_iUnion.1 (hn p hp q hq) with âŸ¨L, hLâŸ©
    exact âŸ¨L, exists_prop.mp <| mem_iUnion.1 hLâŸ©
  /- Recast the assumptions: for each `e`, there exist `n e` and linear maps `L e p q` in `K`
    such that, for `p, q â‰¥ n e`, then `f` is well approximated by `L e p q` at scale `2 ^ (-p)` and
    `2 ^ (-q)`, with an error `2 ^ (-e)`. -/
  choose! n L hn using this
  /- All the operators `L e p q` that show up are close to each other. To prove this, we argue
      that `L e p q` is close to `L e p r` (where `r` is large enough), as both approximate `f` at
      scale `2 ^(- p)`. And `L e p r` is close to `L e' p' r` as both approximate `f` at scale
      `2 ^ (- r)`. And `L e' p' r` is close to `L e' p' q'` as both approximate `f` at scale
      `2 ^ (- p')`. -/
  have M :
    âˆ€ e p q e' p' q',
      n e â‰¤ p â†’
        n e â‰¤ q â†’
          n e' â‰¤ p' â†’ n e' â‰¤ q' â†’ e â‰¤ e' â†’ â€–L e p q - L e' p' q'â€– â‰¤ 12 * â€–câ€– * (1 / 2) ^ e := by
    intro e p q e' p' q' hp hq hp' hq' he'
    let r := max (n e) (n e')
    have I : ((1 : â„) / 2) ^ e' â‰¤ (1 / 2) ^ e :=
      pow_le_pow_of_le_one (by norm_num) (by norm_num) he'
    have J1 : â€–L e p q - L e p râ€– â‰¤ 4 * â€–câ€– * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e p q) ((1 / 2) ^ p) ((1 / 2) ^ e) := (hn e p q hp hq).2.1
      have I2 : x âˆˆ A f (L e p r) ((1 / 2) ^ p) ((1 / 2) ^ e) := (hn e p r hp (le_max_left _ _)).2.1
      exact norm_sub_le_of_mem_A hc P P I1 I2
    have J2 : â€–L e p r - L e' p' râ€– â‰¤ 4 * â€–câ€– * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e p r) ((1 / 2) ^ r) ((1 / 2) ^ e) := (hn e p r hp (le_max_left _ _)).2.2
      have I2 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ r) ((1 / 2) ^ e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.2
      exact norm_sub_le_of_mem_A hc P P I1 (A_mono _ _ I I2)
    have J3 : â€–L e' p' r - L e' p' q'â€– â‰¤ 4 * â€–câ€– * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ p') ((1 / 2) ^ e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.1
      have I2 : x âˆˆ A f (L e' p' q') ((1 / 2) ^ p') ((1 / 2) ^ e') := (hn e' p' q' hp' hq').2.1
      exact norm_sub_le_of_mem_A hc P P (A_mono _ _ I I1) (A_mono _ _ I I2)
    calc
      â€–L e p q - L e' p' q'â€– =
          â€–L e p q - L e p r + (L e p r - L e' p' r) + (L e' p' r - L e' p' q')â€– := by
        congr 1; abel
      _ â‰¤ â€–L e p q - L e p râ€– + â€–L e p r - L e' p' râ€– + â€–L e' p' r - L e' p' q'â€– :=
        norm_addâ‚ƒ_le _ _ _
      _ â‰¤ 4 * â€–câ€– * (1 / 2) ^ e + 4 * â€–câ€– * (1 / 2) ^ e + 4 * â€–câ€– * (1 / 2) ^ e := by gcongr
      _ = 12 * â€–câ€– * (1 / 2) ^ e := by ring
  /- For definiteness, use `L0 e = L e (n e) (n e)`, to have a single sequence. We claim that this
    is a Cauchy sequence. -/
  let L0 : â„• â†’ E â†’L[ğ•œ] F := fun e => L e (n e) (n e)
  have : CauchySeq L0 := by
    rw [Metric.cauchySeq_iff']
    intro Îµ Îµpos
    obtain âŸ¨e, heâŸ© : âˆƒ e : â„•, (1 / 2) ^ e < Îµ / (12 * â€–câ€–) :=
      exists_pow_lt_of_lt_one (by positivity) (by norm_num)
    refine âŸ¨e, fun e' he' => ?_âŸ©
    rw [dist_comm, dist_eq_norm]
    calc
      â€–L0 e - L0 e'â€– â‰¤ 12 * â€–câ€– * (1 / 2) ^ e := M _ _ _ _ _ _ le_rfl le_rfl le_rfl le_rfl he'
      _ < 12 * â€–câ€– * (Îµ / (12 * â€–câ€–)) := by gcongr
      _ = Îµ := by field_simp
  -- As it is Cauchy, the sequence `L0` converges, to a limit `f'` in `K`.
  obtain âŸ¨f', f'K, hf'âŸ© : âˆƒ f' âˆˆ K, Tendsto L0 atTop (ğ“ f') :=
    cauchySeq_tendsto_of_isComplete hK (fun e => (hn e (n e) (n e) le_rfl le_rfl).1) this
  have Lf' : âˆ€ e p, n e â‰¤ p â†’ â€–L e (n e) p - f'â€– â‰¤ 12 * â€–câ€– * (1 / 2) ^ e := by
    intro e p hp
    apply le_of_tendsto (tendsto_const_nhds.sub hf').norm
    rw [eventually_atTop]
    exact âŸ¨e, fun e' he' => M _ _ _ _ _ _ le_rfl hp le_rfl le_rfl he'âŸ©
  -- Let us show that `f` has derivative `f'` at `x`.
  have : HasFDerivAt f f' x := by
    simp only [hasFDerivAt_iff_isLittleO_nhds_zero, isLittleO_iff]
    /- to get an approximation with a precision `Îµ`, we will replace `f` with `L e (n e) m` for
      some large enough `e` (yielding a small error by uniform approximation). As one can vary `m`,
      this makes it possible to cover all scales, and thus to obtain a good linear approximation in
      the whole ball of radius `(1/2)^(n e)`. -/
    intro Îµ Îµpos
    have pos : 0 < 4 + 12 * â€–câ€– := by positivity
    obtain âŸ¨e, heâŸ© : âˆƒ e : â„•, (1 / 2) ^ e < Îµ / (4 + 12 * â€–câ€–) :=
      exists_pow_lt_of_lt_one (div_pos Îµpos pos) (by norm_num)
    rw [eventually_nhds_iff_ball]
    refine âŸ¨(1 / 2) ^ (n e + 1), P, fun y hy => ?_âŸ©
    -- We need to show that `f (x + y) - f x - f' y` is small. For this, we will work at scale
    -- `k` where `k` is chosen with `â€–yâ€– âˆ¼ 2 ^ (-k)`.
    by_cases y_pos : y = 0;
    Â· simp [y_pos]
    have yzero : 0 < â€–yâ€– := norm_pos_iff.mpr y_pos
    have y_lt : â€–yâ€– < (1 / 2) ^ (n e + 1) := by simpa using mem_ball_iff_norm.1 hy
    have yone : â€–yâ€– â‰¤ 1 := le_trans y_lt.le (pow_le_one _ (by norm_num) (by norm_num))
    -- define the scale `k`.
    obtain âŸ¨k, hk, h'kâŸ© : âˆƒ k : â„•, (1 / 2) ^ (k + 1) < â€–yâ€– âˆ§ â€–yâ€– â‰¤ (1 / 2) ^ k :=
      exists_nat_pow_near_of_lt_one yzero yone (by norm_num : (0 : â„) < 1 / 2)
        (by norm_num : (1 : â„) / 2 < 1)
    -- the scale is large enough (as `y` is small enough)
    have k_gt : n e < k := by
      have : ((1 : â„) / 2) ^ (k + 1) < (1 / 2) ^ (n e + 1) := lt_trans hk y_lt
      rw [pow_lt_pow_iff_right_of_lt_one (by norm_num : (0 : â„) < 1 / 2) (by norm_num)] at this
      omega
    set m := k - 1
    have m_ge : n e â‰¤ m := Nat.le_sub_one_of_lt k_gt
    have km : k = m + 1 := (Nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) k_gt)).symm
    rw [km] at hk h'k
    -- `f` is well approximated by `L e (n e) k` at the relevant scale
    -- (in fact, we use `m = k - 1` instead of `k` because of the precise definition of `A`).
    have J1 : â€–f (x + y) - f x - L e (n e) m (x + y - x)â€– â‰¤ (1 / 2) ^ e * (1 / 2) ^ m := by
      apply le_of_mem_A (hn e (n e) m le_rfl m_ge).2.2
      Â· simp only [mem_closedBall, dist_self]
        positivity
      Â· simpa only [dist_eq_norm, add_sub_cancel_left, mem_closedBall, pow_succ, mul_one_div] using
          h'k
    have J2 : â€–f (x + y) - f x - L e (n e) m yâ€– â‰¤ 4 * (1 / 2) ^ e * â€–yâ€– :=
      calc
        â€–f (x + y) - f x - L e (n e) m yâ€– â‰¤ (1 / 2) ^ e * (1 / 2) ^ m := by
          simpa only [add_sub_cancel_left] using J1
        _ = 4 * (1 / 2) ^ e * (1 / 2) ^ (m + 2) := by field_simp; ring
        _ â‰¤ 4 * (1 / 2) ^ e * â€–yâ€– := by gcongr
    -- use the previous estimates to see that `f (x + y) - f x - f' y` is small.
    calc
      â€–f (x + y) - f x - f' yâ€– = â€–f (x + y) - f x - L e (n e) m y + (L e (n e) m - f') yâ€– :=
        congr_arg _ (by simp)
      _ â‰¤ 4 * (1 / 2) ^ e * â€–yâ€– + 12 * â€–câ€– * (1 / 2) ^ e * â€–yâ€– :=
        norm_add_le_of_le J2 <| (le_opNorm _ _).trans <| by gcongr; exact Lf' _ _ m_ge
      _ = (4 + 12 * â€–câ€–) * â€–yâ€– * (1 / 2) ^ e := by ring
      _ â‰¤ (4 + 12 * â€–câ€–) * â€–yâ€– * (Îµ / (4 + 12 * â€–câ€–)) := by gcongr
      _ = Îµ * â€–yâ€– := by field_simp [ne_of_gt pos]; ring
  rw [â† this.fderiv] at f'K
  exact âŸ¨this.differentiableAt, f'KâŸ©
#align fderiv_measurable_aux.D_subset_differentiable_set FDerivMeasurableAux.D_subset_differentiable_set

theorem differentiable_set_eq_D (hK : IsComplete K) :
    { x | DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K } = D f K :=
  Subset.antisymm (differentiable_set_subset_D _) (D_subset_differentiable_set hK)
#align fderiv_measurable_aux.differentiable_set_eq_D FDerivMeasurableAux.differentiable_set_eq_D

end FDerivMeasurableAux

open FDerivMeasurableAux

variable [MeasurableSpace E] [OpensMeasurableSpace E]
variable (ğ•œ f)

/-- The set of differentiability points of a function, with derivative in a given complete set,
is Borel-measurable. -/
theorem measurableSet_of_differentiableAt_of_isComplete {K : Set (E â†’L[ğ•œ] F)} (hK : IsComplete K) :
    MeasurableSet { x | DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ K } := by
  -- Porting note: was
  -- simp [differentiable_set_eq_D K hK, D, isOpen_B.measurableSet, MeasurableSet.iInter,
  --   MeasurableSet.iUnion]
  simp only [D, differentiable_set_eq_D K hK]
  repeat apply_rules [MeasurableSet.iUnion, MeasurableSet.iInter] <;> intro
  exact isOpen_B.measurableSet
#align measurable_set_of_differentiable_at_of_is_complete measurableSet_of_differentiableAt_of_isComplete

variable [CompleteSpace F]

/-- The set of differentiability points of a function taking values in a complete space is
Borel-measurable. -/
theorem measurableSet_of_differentiableAt : MeasurableSet { x | DifferentiableAt ğ•œ f x } := by
  have : IsComplete (univ : Set (E â†’L[ğ•œ] F)) := complete_univ
  convert measurableSet_of_differentiableAt_of_isComplete ğ•œ f this
  simp
#align measurable_set_of_differentiable_at measurableSet_of_differentiableAt

@[measurability]
theorem measurable_fderiv : Measurable (fderiv ğ•œ f) := by
  refine measurable_of_isClosed fun s hs => ?_
  have :
    fderiv ğ•œ f â»Â¹' s =
      { x | DifferentiableAt ğ•œ f x âˆ§ fderiv ğ•œ f x âˆˆ s } âˆª
        { x | Â¬DifferentiableAt ğ•œ f x } âˆ© { _x | (0 : E â†’L[ğ•œ] F) âˆˆ s } :=
    Set.ext fun x => mem_preimage.trans fderiv_mem_iff
  rw [this]
  exact
    (measurableSet_of_differentiableAt_of_isComplete _ _ hs.isComplete).union
      ((measurableSet_of_differentiableAt _ _).compl.inter (MeasurableSet.const _))
#align measurable_fderiv measurable_fderiv

@[measurability]
theorem measurable_fderiv_apply_const [MeasurableSpace F] [BorelSpace F] (y : E) :
    Measurable fun x => fderiv ğ•œ f x y :=
  (ContinuousLinearMap.measurable_apply y).comp (measurable_fderiv ğ•œ f)
#align measurable_fderiv_apply_const measurable_fderiv_apply_const

variable {ğ•œ}

@[measurability]
theorem measurable_deriv [MeasurableSpace ğ•œ] [OpensMeasurableSpace ğ•œ] [MeasurableSpace F]
    [BorelSpace F] (f : ğ•œ â†’ F) : Measurable (deriv f) := by
  simpa only [fderiv_deriv] using measurable_fderiv_apply_const ğ•œ f 1
#align measurable_deriv measurable_deriv

theorem stronglyMeasurable_deriv [MeasurableSpace ğ•œ] [OpensMeasurableSpace ğ•œ]
    [h : SecondCountableTopologyEither ğ•œ F] (f : ğ•œ â†’ F) : StronglyMeasurable (deriv f) := by
  borelize F
  rcases h.out with hğ•œ|hF
  Â· exact stronglyMeasurable_iff_measurable_separable.2
      âŸ¨measurable_deriv f, isSeparable_range_deriv _âŸ©
  Â· exact (measurable_deriv f).stronglyMeasurable
#align strongly_measurable_deriv stronglyMeasurable_deriv

theorem aemeasurable_deriv [MeasurableSpace ğ•œ] [OpensMeasurableSpace ğ•œ] [MeasurableSpace F]
    [BorelSpace F] (f : ğ•œ â†’ F) (Î¼ : Measure ğ•œ) : AEMeasurable (deriv f) Î¼ :=
  (measurable_deriv f).aemeasurable
#align ae_measurable_deriv aemeasurable_deriv

theorem aestronglyMeasurable_deriv [MeasurableSpace ğ•œ] [OpensMeasurableSpace ğ•œ]
    [SecondCountableTopologyEither ğ•œ F] (f : ğ•œ â†’ F) (Î¼ : Measure ğ•œ) :
    AEStronglyMeasurable (deriv f) Î¼ :=
  (stronglyMeasurable_deriv f).aestronglyMeasurable
#align ae_strongly_measurable_deriv aestronglyMeasurable_deriv

end fderiv

section RightDeriv

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
variable {f : â„ â†’ F} (K : Set F)

namespace RightDerivMeasurableAux

/-- The set `A f L r Îµ` is the set of points `x` around which the function `f` is well approximated
at scale `r` by the linear map `h â†¦ h â€¢ L`, up to an error `Îµ`. We tweak the definition to
make sure that this is open on the right. -/
def A (f : â„ â†’ F) (L : F) (r Îµ : â„) : Set â„ :=
  { x | âˆƒ r' âˆˆ Ioc (r / 2) r, âˆ€áµ‰ (y âˆˆ Icc x (x + r')) (z âˆˆ Icc x (x + r')),
    â€–f z - f y - (z - y) â€¢ Lâ€– â‰¤ Îµ * r }
#align right_deriv_measurable_aux.A RightDerivMeasurableAux.A

/-- The set `B f K r s Îµ` is the set of points `x` around which there exists a vector
`L` belonging to `K` (a given set of vectors) such that `h â€¢ L` approximates well `f (x + h)`
(up to an error `Îµ`), simultaneously at scales `r` and `s`. -/
def B (f : â„ â†’ F) (K : Set F) (r s Îµ : â„) : Set â„ :=
  â‹ƒ L âˆˆ K, A f L r Îµ âˆ© A f L s Îµ
#align right_deriv_measurable_aux.B RightDerivMeasurableAux.B

/-- The set `D f K` is a complicated set constructed using countable intersections and unions. Its
main use is that, when `K` is complete, it is exactly the set of points where `f` is differentiable,
with a derivative in `K`. -/
def D (f : â„ â†’ F) (K : Set F) : Set â„ :=
  â‹‚ e : â„•, â‹ƒ n : â„•, â‹‚ (p â‰¥ n) (q â‰¥ n), B f K ((1 / 2) ^ p) ((1 / 2) ^ q) ((1 / 2) ^ e)
#align right_deriv_measurable_aux.D RightDerivMeasurableAux.D

theorem A_mem_nhdsWithin_Ioi {L : F} {r Îµ x : â„} (hx : x âˆˆ A f L r Îµ) : A f L r Îµ âˆˆ ğ“[>] x := by
  rcases hx with âŸ¨r', rr', hr'âŸ©
  rw [mem_nhdsWithin_Ioi_iff_exists_Ioo_subset]
  obtain âŸ¨s, s_gt, s_ltâŸ© : âˆƒ s : â„, r / 2 < s âˆ§ s < r' := exists_between rr'.1
  have : s âˆˆ Ioc (r / 2) r := âŸ¨s_gt, le_of_lt (s_lt.trans_le rr'.2)âŸ©
  refine âŸ¨x + r' - s, by simp only [mem_Ioi]; linarith, fun x' hx' => âŸ¨s, this, ?_âŸ©âŸ©
  have A : Icc x' (x' + s) âŠ† Icc x (x + r') := by
    apply Icc_subset_Icc hx'.1.le
    linarith [hx'.2]
  intro y hy z hz
  exact hr' y (A hy) z (A hz)
#align right_deriv_measurable_aux.A_mem_nhds_within_Ioi RightDerivMeasurableAux.A_mem_nhdsWithin_Ioi

theorem B_mem_nhdsWithin_Ioi {K : Set F} {r s Îµ x : â„} (hx : x âˆˆ B f K r s Îµ) :
    B f K r s Îµ âˆˆ ğ“[>] x := by
  obtain âŸ¨L, LK, hLâ‚, hLâ‚‚âŸ© : âˆƒ L : F, L âˆˆ K âˆ§ x âˆˆ A f L r Îµ âˆ§ x âˆˆ A f L s Îµ := by
    simpa only [B, mem_iUnion, mem_inter_iff, exists_prop] using hx
  filter_upwards [A_mem_nhdsWithin_Ioi hLâ‚, A_mem_nhdsWithin_Ioi hLâ‚‚] with y hyâ‚ hyâ‚‚
  simp only [B, mem_iUnion, mem_inter_iff, exists_prop]
  exact âŸ¨L, LK, hyâ‚, hyâ‚‚âŸ©
#align right_deriv_measurable_aux.B_mem_nhds_within_Ioi RightDerivMeasurableAux.B_mem_nhdsWithin_Ioi

theorem measurableSet_B {K : Set F} {r s Îµ : â„} : MeasurableSet (B f K r s Îµ) :=
  measurableSet_of_mem_nhdsWithin_Ioi fun _ hx => B_mem_nhdsWithin_Ioi hx
#align right_deriv_measurable_aux.measurable_set_B RightDerivMeasurableAux.measurableSet_B

theorem A_mono (L : F) (r : â„) {Îµ Î´ : â„} (h : Îµ â‰¤ Î´) : A f L r Îµ âŠ† A f L r Î´ := by
  rintro x âŸ¨r', r'r, hr'âŸ©
  refine âŸ¨r', r'r, fun y hy z hz => (hr' y hy z hz).trans (mul_le_mul_of_nonneg_right h ?_)âŸ©
  linarith [hy.1, hy.2, r'r.2]
#align right_deriv_measurable_aux.A_mono RightDerivMeasurableAux.A_mono

theorem le_of_mem_A {r Îµ : â„} {L : F} {x : â„} (hx : x âˆˆ A f L r Îµ) {y z : â„}
    (hy : y âˆˆ Icc x (x + r / 2)) (hz : z âˆˆ Icc x (x + r / 2)) :
  â€–f z - f y - (z - y) â€¢ Lâ€– â‰¤ Îµ * r := by
  rcases hx with âŸ¨r', r'mem, hr'âŸ©
  have A : x + r / 2 â‰¤ x + r' := by linarith [r'mem.1]
  exact hr' _ ((Icc_subset_Icc le_rfl A) hy) _ ((Icc_subset_Icc le_rfl A) hz)
#align right_deriv_measurable_aux.le_of_mem_A RightDerivMeasurableAux.le_of_mem_A

theorem mem_A_of_differentiable {Îµ : â„} (hÎµ : 0 < Îµ) {x : â„}
    (hx : DifferentiableWithinAt â„ f (Ici x) x) :
    âˆƒ R > 0, âˆ€ r âˆˆ Ioo (0 : â„) R, x âˆˆ A f (derivWithin f (Ici x) x) r Îµ := by
  have := hx.hasDerivWithinAt
  simp_rw [hasDerivWithinAt_iff_isLittleO, isLittleO_iff] at this
  rcases mem_nhdsWithin_Ici_iff_exists_Ico_subset.1 (this (half_pos hÎµ)) with âŸ¨m, xm, hmâŸ©
  refine âŸ¨m - x, by linarith [show x < m from xm], fun r hr => ?_âŸ©
  have : r âˆˆ Ioc (r / 2) r := âŸ¨half_lt_self hr.1, le_rflâŸ©
  refine âŸ¨r, this, fun y hy z hz => ?_âŸ©
  calc
    â€–f z - f y - (z - y) â€¢ derivWithin f (Ici x) xâ€– =
        â€–f z - f x - (z - x) â€¢ derivWithin f (Ici x) x -
            (f y - f x - (y - x) â€¢ derivWithin f (Ici x) x)â€– := by
      congr 1; simp only [sub_smul]; abel
    _ â‰¤
        â€–f z - f x - (z - x) â€¢ derivWithin f (Ici x) xâ€– +
          â€–f y - f x - (y - x) â€¢ derivWithin f (Ici x) xâ€– :=
      (norm_sub_le _ _)
    _ â‰¤ Îµ / 2 * â€–z - xâ€– + Îµ / 2 * â€–y - xâ€– :=
      (add_le_add (hm âŸ¨hz.1, hz.2.trans_lt (by linarith [hr.2])âŸ©)
        (hm âŸ¨hy.1, hy.2.trans_lt (by linarith [hr.2])âŸ©))
    _ â‰¤ Îµ / 2 * r + Îµ / 2 * r := by
      gcongr
      Â· rw [Real.norm_of_nonneg] <;> linarith [hz.1, hz.2]
      Â· rw [Real.norm_of_nonneg] <;> linarith [hy.1, hy.2]
    _ = Îµ * r := by ring
#align right_deriv_measurable_aux.mem_A_of_differentiable RightDerivMeasurableAux.mem_A_of_differentiable

theorem norm_sub_le_of_mem_A {r x : â„} (hr : 0 < r) (Îµ : â„) {Lâ‚ Lâ‚‚ : F} (hâ‚ : x âˆˆ A f Lâ‚ r Îµ)
    (hâ‚‚ : x âˆˆ A f Lâ‚‚ r Îµ) : â€–Lâ‚ - Lâ‚‚â€– â‰¤ 4 * Îµ := by
  suffices H : â€–(r / 2) â€¢ (Lâ‚ - Lâ‚‚)â€– â‰¤ r / 2 * (4 * Îµ) by
    rwa [norm_smul, Real.norm_of_nonneg (half_pos hr).le, mul_le_mul_left (half_pos hr)] at H
  calc
    â€–(r / 2) â€¢ (Lâ‚ - Lâ‚‚)â€– =
        â€–f (x + r / 2) - f x - (x + r / 2 - x) â€¢ Lâ‚‚ -
            (f (x + r / 2) - f x - (x + r / 2 - x) â€¢ Lâ‚)â€– := by
      simp [smul_sub]
    _ â‰¤ â€–f (x + r / 2) - f x - (x + r / 2 - x) â€¢ Lâ‚‚â€– +
          â€–f (x + r / 2) - f x - (x + r / 2 - x) â€¢ Lâ‚â€– :=
      norm_sub_le _ _
    _ â‰¤ Îµ * r + Îµ * r := by
      apply add_le_add
      Â· apply le_of_mem_A hâ‚‚ <;> simp [(half_pos hr).le]
      Â· apply le_of_mem_A hâ‚ <;> simp [(half_pos hr).le]
    _ = r / 2 * (4 * Îµ) := by ring
#align right_deriv_measurable_aux.norm_sub_le_of_mem_A RightDerivMeasurableAux.norm_sub_le_of_mem_A

/-- Easy inclusion: a differentiability point with derivative in `K` belongs to `D f K`. -/
theorem differentiable_set_subset_D :
    { x | DifferentiableWithinAt â„ f (Ici x) x âˆ§ derivWithin f (Ici x) x âˆˆ K } âŠ† D f K := by
  intro x hx
  rw [D, mem_iInter]
  intro e
  have : (0 : â„) < (1 / 2) ^ e := pow_pos (by norm_num) _
  rcases mem_A_of_differentiable this hx.1 with âŸ¨R, R_pos, hRâŸ©
  obtain âŸ¨n, hnâŸ© : âˆƒ n : â„•, (1 / 2) ^ n < R :=
    exists_pow_lt_of_lt_one R_pos (by norm_num : (1 : â„) / 2 < 1)
  simp only [mem_iUnion, mem_iInter, B, mem_inter_iff]
  refine âŸ¨n, fun p hp q hq => âŸ¨derivWithin f (Ici x) x, hx.2, âŸ¨?_, ?_âŸ©âŸ©âŸ© <;>
    Â· refine hR _ âŸ¨pow_pos (by norm_num) _, lt_of_le_of_lt ?_ hnâŸ©
      exact pow_le_pow_of_le_one (by norm_num) (by norm_num) (by assumption)
#align right_deriv_measurable_aux.differentiable_set_subset_D RightDerivMeasurableAux.differentiable_set_subset_D

/-- Harder inclusion: at a point in `D f K`, the function `f` has a derivative, in `K`. -/
theorem D_subset_differentiable_set {K : Set F} (hK : IsComplete K) :
    D f K âŠ† { x | DifferentiableWithinAt â„ f (Ici x) x âˆ§ derivWithin f (Ici x) x âˆˆ K } := by
  have P : âˆ€ {n : â„•}, (0 : â„) < (1 / 2) ^ n := fun {n} => pow_pos (by norm_num) n
  intro x hx
  have :
    âˆ€ e : â„•, âˆƒ n : â„•, âˆ€ p q, n â‰¤ p â†’ n â‰¤ q â†’
      âˆƒ L âˆˆ K, x âˆˆ A f L ((1 / 2) ^ p) ((1 / 2) ^ e) âˆ© A f L ((1 / 2) ^ q) ((1 / 2) ^ e) := by
    intro e
    have := mem_iInter.1 hx e
    rcases mem_iUnion.1 this with âŸ¨n, hnâŸ©
    refine âŸ¨n, fun p q hp hq => ?_âŸ©
    simp only [mem_iInter, ge_iff_le] at hn
    rcases mem_iUnion.1 (hn p hp q hq) with âŸ¨L, hLâŸ©
    exact âŸ¨L, exists_prop.mp <| mem_iUnion.1 hLâŸ©
  /- Recast the assumptions: for each `e`, there exist `n e` and linear maps `L e p q` in `K`
    such that, for `p, q â‰¥ n e`, then `f` is well approximated by `L e p q` at scale `2 ^ (-p)` and
    `2 ^ (-q)`, with an error `2 ^ (-e)`. -/
  choose! n L hn using this
  /- All the operators `L e p q` that show up are close to each other. To prove this, we argue
      that `L e p q` is close to `L e p r` (where `r` is large enough), as both approximate `f` at
      scale `2 ^(- p)`. And `L e p r` is close to `L e' p' r` as both approximate `f` at scale
      `2 ^ (- r)`. And `L e' p' r` is close to `L e' p' q'` as both approximate `f` at scale
      `2 ^ (- p')`. -/
  have M :
    âˆ€ e p q e' p' q',
      n e â‰¤ p â†’
        n e â‰¤ q â†’ n e' â‰¤ p' â†’ n e' â‰¤ q' â†’ e â‰¤ e' â†’ â€–L e p q - L e' p' q'â€– â‰¤ 12 * (1 / 2) ^ e := by
    intro e p q e' p' q' hp hq hp' hq' he'
    let r := max (n e) (n e')
    have I : ((1 : â„) / 2) ^ e' â‰¤ (1 / 2) ^ e :=
      pow_le_pow_of_le_one (by norm_num) (by norm_num) he'
    have J1 : â€–L e p q - L e p râ€– â‰¤ 4 * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e p q) ((1 / 2) ^ p) ((1 / 2) ^ e) := (hn e p q hp hq).2.1
      have I2 : x âˆˆ A f (L e p r) ((1 / 2) ^ p) ((1 / 2) ^ e) := (hn e p r hp (le_max_left _ _)).2.1
      exact norm_sub_le_of_mem_A P _ I1 I2
    have J2 : â€–L e p r - L e' p' râ€– â‰¤ 4 * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e p r) ((1 / 2) ^ r) ((1 / 2) ^ e) := (hn e p r hp (le_max_left _ _)).2.2
      have I2 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ r) ((1 / 2) ^ e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.2
      exact norm_sub_le_of_mem_A P _ I1 (A_mono _ _ I I2)
    have J3 : â€–L e' p' r - L e' p' q'â€– â‰¤ 4 * (1 / 2) ^ e := by
      have I1 : x âˆˆ A f (L e' p' r) ((1 / 2) ^ p') ((1 / 2) ^ e') :=
        (hn e' p' r hp' (le_max_right _ _)).2.1
      have I2 : x âˆˆ A f (L e' p' q') ((1 / 2) ^ p') ((1 / 2) ^ e') := (hn e' p' q' hp' hq').2.1
      exact norm_sub_le_of_mem_A P _ (A_mono _ _ I I1) (A_mono _ _ I I2)
    calc
      â€–L e p q - L e' p' q'â€– =
          â€–L e p q - L e p r + (L e p r - L e' p' r) + (L e' p' r - L e' p' q')â€– := by
        congr 1; abel
      _ â‰¤ â€–L e p q - L e p râ€– + â€–L e p r - L e' p' râ€– + â€–L e' p' r - L e' p' q'â€– :=
        (le_trans (norm_add_le _ _) (add_le_add_right (norm_add_le _ _) _))
      _ â‰¤ 4 * (1 / 2) ^ e + 4 * (1 / 2) ^ e + 4 * (1 / 2) ^ e := by gcongr
        -- Porting note: proof was `by apply_rules [add_le_add]`
      _ = 12 * (1 / 2) ^ e := by ring

  /- For definiteness, use `L0 e = L e (n e) (n e)`, to have a single sequence. We claim that this
    is a Cauchy sequence. -/
  let L0 : â„• â†’ F := fun e => L e (n e) (n e)
  have : CauchySeq L0 := by
    rw [Metric.cauchySeq_iff']
    intro Îµ Îµpos
    obtain âŸ¨e, heâŸ© : âˆƒ e : â„•, (1 / 2) ^ e < Îµ / 12 :=
      exists_pow_lt_of_lt_one (div_pos Îµpos (by norm_num)) (by norm_num)
    refine âŸ¨e, fun e' he' => ?_âŸ©
    rw [dist_comm, dist_eq_norm]
    calc
      â€–L0 e - L0 e'â€– â‰¤ 12 * (1 / 2) ^ e := M _ _ _ _ _ _ le_rfl le_rfl le_rfl le_rfl he'
      _ < 12 * (Îµ / 12) := mul_lt_mul' le_rfl he (le_of_lt P) (by norm_num)
      _ = Îµ := by field_simp [(by norm_num : (12 : â„) â‰  0)]

  -- As it is Cauchy, the sequence `L0` converges, to a limit `f'` in `K`.
  obtain âŸ¨f', f'K, hf'âŸ© : âˆƒ f' âˆˆ K, Tendsto L0 atTop (ğ“ f') :=
    cauchySeq_tendsto_of_isComplete hK (fun e => (hn e (n e) (n e) le_rfl le_rfl).1) this
  have Lf' : âˆ€ e p, n e â‰¤ p â†’ â€–L e (n e) p - f'â€– â‰¤ 12 * (1 / 2) ^ e := by
    intro e p hp
    apply le_of_tendsto (tendsto_const_nhds.sub hf').norm
    rw [eventually_atTop]
    exact âŸ¨e, fun e' he' => M _ _ _ _ _ _ le_rfl hp le_rfl le_rfl he'âŸ©
  -- Let us show that `f` has right derivative `f'` at `x`.
  have : HasDerivWithinAt f f' (Ici x) x := by
    simp only [hasDerivWithinAt_iff_isLittleO, isLittleO_iff]
    /- to get an approximation with a precision `Îµ`, we will replace `f` with `L e (n e) m` for
      some large enough `e` (yielding a small error by uniform approximation). As one can vary `m`,
      this makes it possible to cover all scales, and thus to obtain a good linear approximation in
      the whole interval of length `(1/2)^(n e)`. -/
    intro Îµ Îµpos
    obtain âŸ¨e, heâŸ© : âˆƒ e : â„•, (1 / 2) ^ e < Îµ / 16 :=
      exists_pow_lt_of_lt_one (div_pos Îµpos (by norm_num)) (by norm_num)
    have xmem : x âˆˆ Ico x (x + (1 / 2) ^ (n e + 1)) := by
      simp only [one_div, left_mem_Ico, lt_add_iff_pos_right, inv_pos, pow_pos, zero_lt_two,
        zero_lt_one]
    filter_upwards [Icc_mem_nhdsWithin_Ici xmem] with y hy
    -- We need to show that `f y - f x - f' (y - x)` is small. For this, we will work at scale
    -- `k` where `k` is chosen with `â€–y - xâ€– âˆ¼ 2 ^ (-k)`.
    rcases eq_or_lt_of_le hy.1 with (rfl | xy)
    Â· simp only [sub_self, zero_smul, norm_zero, mul_zero, le_rfl]
    have yzero : 0 < y - x := sub_pos.2 xy
    have y_le : y - x â‰¤ (1 / 2) ^ (n e + 1) := by linarith [hy.2]
    have yone : y - x â‰¤ 1 := le_trans y_le (pow_le_one _ (by norm_num) (by norm_num))
    -- define the scale `k`.
    obtain âŸ¨k, hk, h'kâŸ© : âˆƒ k : â„•, (1 / 2) ^ (k + 1) < y - x âˆ§ y - x â‰¤ (1 / 2) ^ k :=
      exists_nat_pow_near_of_lt_one yzero yone (by norm_num : (0 : â„) < 1 / 2)
        (by norm_num : (1 : â„) / 2 < 1)
    -- the scale is large enough (as `y - x` is small enough)
    have k_gt : n e < k := by
      have : ((1 : â„) / 2) ^ (k + 1) < (1 / 2) ^ (n e + 1) := lt_of_lt_of_le hk y_le
      rw [pow_lt_pow_iff_right_of_lt_one (by norm_num : (0 : â„) < 1 / 2) (by norm_num)] at this
      omega
    set m := k - 1
    have m_ge : n e â‰¤ m := Nat.le_sub_one_of_lt k_gt
    have km : k = m + 1 := (Nat.succ_pred_eq_of_pos (lt_of_le_of_lt (zero_le _) k_gt)).symm
    rw [km] at hk h'k
    -- `f` is well approximated by `L e (n e) k` at the relevant scale
    -- (in fact, we use `m = k - 1` instead of `k` because of the precise definition of `A`).
    have J : â€–f y - f x - (y - x) â€¢ L e (n e) mâ€– â‰¤ 4 * (1 / 2) ^ e * â€–y - xâ€– :=
      calc
        â€–f y - f x - (y - x) â€¢ L e (n e) mâ€– â‰¤ (1 / 2) ^ e * (1 / 2) ^ m := by
          apply le_of_mem_A (hn e (n e) m le_rfl m_ge).2.2
          Â· simp only [one_div, inv_pow, left_mem_Icc, le_add_iff_nonneg_right]
            positivity
          Â· simp only [pow_add, tsub_le_iff_left] at h'k
            simpa only [hy.1, mem_Icc, true_and_iff, one_div, pow_one] using h'k
        _ = 4 * (1 / 2) ^ e * (1 / 2) ^ (m + 2) := by field_simp; ring
        _ â‰¤ 4 * (1 / 2) ^ e * (y - x) := by gcongr
        _ = 4 * (1 / 2) ^ e * â€–y - xâ€– := by rw [Real.norm_of_nonneg yzero.le]
    calc
      â€–f y - f x - (y - x) â€¢ f'â€– =
          â€–f y - f x - (y - x) â€¢ L e (n e) m + (y - x) â€¢ (L e (n e) m - f')â€– := by
        simp only [smul_sub, sub_add_sub_cancel]
      _ â‰¤ 4 * (1 / 2) ^ e * â€–y - xâ€– + â€–y - xâ€– * (12 * (1 / 2) ^ e) :=
        norm_add_le_of_le J <| by rw [norm_smul]; gcongr; exact Lf' _ _ m_ge
      _ = 16 * â€–y - xâ€– * (1 / 2) ^ e := by ring
      _ â‰¤ 16 * â€–y - xâ€– * (Îµ / 16) := by gcongr
      _ = Îµ * â€–y - xâ€– := by ring

  rw [â† this.derivWithin (uniqueDiffOn_Ici x x Set.left_mem_Ici)] at f'K
  exact âŸ¨this.differentiableWithinAt, f'KâŸ©
#align right_deriv_measurable_aux.D_subset_differentiable_set RightDerivMeasurableAux.D_subset_differentiable_set

theorem differentiable_set_eq_D (hK : IsComplete K) :
    { x | DifferentiableWithinAt â„ f (Ici x) x âˆ§ derivWithin f (Ici x) x âˆˆ K } = D f K :=
  Subset.antisymm (differentiable_set_subset_D _) (D_subset_differentiable_set hK)
#align right_deriv_measurable_aux.differentiable_set_eq_D RightDerivMeasurableAux.differentiable_set_eq_D

end RightDerivMeasurableAux

open RightDerivMeasurableAux

variable (f)

/-- The set of right differentiability points of a function, with derivative in a given complete
set, is Borel-measurable. -/
theorem measurableSet_of_differentiableWithinAt_Ici_of_isComplete {K : Set F} (hK : IsComplete K) :
    MeasurableSet { x | DifferentiableWithinAt â„ f (Ici x) x âˆ§ derivWithin f (Ici x) x âˆˆ K } := by
  -- simp [differentiable_set_eq_d K hK, D, measurableSet_b, MeasurableSet.iInter,
  --   MeasurableSet.iUnion]
  simp only [differentiable_set_eq_D K hK, D]
  repeat apply_rules [MeasurableSet.iUnion, MeasurableSet.iInter] <;> intro
  exact measurableSet_B
#align measurable_set_of_differentiable_within_at_Ici_of_is_complete measurableSet_of_differentiableWithinAt_Ici_of_isComplete

variable [CompleteSpace F]

/-- The set of right differentiability points of a function taking values in a complete space is
Borel-measurable. -/
theorem measurableSet_of_differentiableWithinAt_Ici :
    MeasurableSet { x | DifferentiableWithinAt â„ f (Ici x) x } := by
  have : IsComplete (univ : Set F) := complete_univ
  convert measurableSet_of_differentiableWithinAt_Ici_of_isComplete f this
  simp
#align measurable_set_of_differentiable_within_at_Ici measurableSet_of_differentiableWithinAt_Ici

@[measurability]
theorem measurable_derivWithin_Ici [MeasurableSpace F] [BorelSpace F] :
    Measurable fun x => derivWithin f (Ici x) x := by
  refine measurable_of_isClosed fun s hs => ?_
  have :
    (fun x => derivWithin f (Ici x) x) â»Â¹' s =
      { x | DifferentiableWithinAt â„ f (Ici x) x âˆ§ derivWithin f (Ici x) x âˆˆ s } âˆª
        { x | Â¬DifferentiableWithinAt â„ f (Ici x) x } âˆ© { _x | (0 : F) âˆˆ s } :=
    Set.ext fun x => mem_preimage.trans derivWithin_mem_iff
  rw [this]
  exact
    (measurableSet_of_differentiableWithinAt_Ici_of_isComplete _ hs.isComplete).union
      ((measurableSet_of_differentiableWithinAt_Ici _).compl.inter (MeasurableSet.const _))
#align measurable_deriv_within_Ici measurable_derivWithin_Ici

theorem stronglyMeasurable_derivWithin_Ici :
    StronglyMeasurable (fun x â†¦ derivWithin f (Ici x) x) := by
  borelize F
  apply stronglyMeasurable_iff_measurable_separable.2 âŸ¨measurable_derivWithin_Ici f, ?_âŸ©
  obtain âŸ¨t, t_count, htâŸ© : âˆƒ t : Set â„, t.Countable âˆ§ Dense t := exists_countable_dense â„
  suffices H : range (fun x â†¦ derivWithin f (Ici x) x) âŠ† closure (Submodule.span â„ (f '' t)) from
    IsSeparable.mono (t_count.image f).isSeparable.span.closure H
  rintro - âŸ¨x, rflâŸ©
  suffices H' : range (fun y â†¦ derivWithin f (Ici x) y) âŠ† closure (Submodule.span â„ (f '' t)) from
    H' (mem_range_self _)
  apply range_derivWithin_subset_closure_span_image
  calc Ici x
    = closure (Ioi x âˆ© closure t) := by simp [dense_iff_closure_eq.1 ht]
  _ âŠ† closure (closure (Ioi x âˆ© t)) := by
      apply closure_mono
      simpa [inter_comm] using (isOpen_Ioi (a := x)).closure_inter (s := t)
  _ âŠ† closure (Ici x âˆ© t) := by
      rw [closure_closure]
      exact closure_mono (inter_subset_inter_left _ Ioi_subset_Ici_self)
#align strongly_measurable_deriv_within_Ici stronglyMeasurable_derivWithin_Ici

theorem aemeasurable_derivWithin_Ici [MeasurableSpace F] [BorelSpace F] (Î¼ : Measure â„) :
    AEMeasurable (fun x => derivWithin f (Ici x) x) Î¼ :=
  (measurable_derivWithin_Ici f).aemeasurable
#align ae_measurable_deriv_within_Ici aemeasurable_derivWithin_Ici

theorem aestronglyMeasurable_derivWithin_Ici (Î¼ : Measure â„) :
    AEStronglyMeasurable (fun x => derivWithin f (Ici x) x) Î¼ :=
  (stronglyMeasurable_derivWithin_Ici f).aestronglyMeasurable
#align ae_strongly_measurable_deriv_within_Ici aestronglyMeasurable_derivWithin_Ici

/-- The set of right differentiability points of a function taking values in a complete space is
Borel-measurable. -/
theorem measurableSet_of_differentiableWithinAt_Ioi :
    MeasurableSet { x | DifferentiableWithinAt â„ f (Ioi x) x } := by
  simpa [differentiableWithinAt_Ioi_iff_Ici] using measurableSet_of_differentiableWithinAt_Ici f
#align measurable_set_of_differentiable_within_at_Ioi measurableSet_of_differentiableWithinAt_Ioi

@[measurability]
theorem measurable_derivWithin_Ioi [MeasurableSpace F] [BorelSpace F] :
    Measurable fun x => derivWithin f (Ioi x) x := by
  simpa [derivWithin_Ioi_eq_Ici] using measurable_derivWithin_Ici f
#align measurable_deriv_within_Ioi measurable_derivWithin_Ioi

theorem stronglyMeasurable_derivWithin_Ioi :
    StronglyMeasurable (fun x â†¦ derivWithin f (Ioi x) x) := by
  simpa [derivWithin_Ioi_eq_Ici] using stronglyMeasurable_derivWithin_Ici f
#align strongly_measurable_deriv_within_Ioi stronglyMeasurable_derivWithin_Ioi

theorem aemeasurable_derivWithin_Ioi [MeasurableSpace F] [BorelSpace F] (Î¼ : Measure â„) :
    AEMeasurable (fun x => derivWithin f (Ioi x) x) Î¼ :=
  (measurable_derivWithin_Ioi f).aemeasurable
#align ae_measurable_deriv_within_Ioi aemeasurable_derivWithin_Ioi

theorem aestronglyMeasurable_derivWithin_Ioi (Î¼ : Measure â„) :
    AEStronglyMeasurable (fun x => derivWithin f (Ioi x) x) Î¼ :=
  (stronglyMeasurable_derivWithin_Ioi f).aestronglyMeasurable
#align ae_strongly_measurable_deriv_within_Ioi aestronglyMeasurable_derivWithin_Ioi

end RightDeriv

section WithParam

/- In this section, we prove the measurability of the derivative in a context with parameters:
given `f : Î± â†’ E â†’ F`, we want to show that `p â†¦ fderiv ğ•œ (f p.1) p.2` is measurable. Contrary
to the previous sections, some assumptions are needed for this: if `f p.1` depends arbitrarily on
`p.1`, this is obviously false. We require that `f` is continuous and `E` is locally compact --
then the proofs in the previous sections adapt readily, as the set `A` defined above is open, so
that the differentiability set `D` is measurable. -/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [LocallyCompactSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {Î± : Type*} [TopologicalSpace Î±] [MeasurableSpace Î±] [MeasurableSpace E]
  [OpensMeasurableSpace Î±] [OpensMeasurableSpace E]
  {f : Î± â†’ E â†’ F} (K : Set (E â†’L[ğ•œ] F))

namespace FDerivMeasurableAux

open Uniformity

lemma isOpen_A_with_param {r s : â„} (hf : Continuous f.uncurry) (L : E â†’L[ğ•œ] F) :
    IsOpen {p : Î± Ã— E | p.2 âˆˆ A (f p.1) L r s} := by
  have : ProperSpace E := .of_locallyCompactSpace ğ•œ
  simp only [A, half_lt_self_iff, not_lt, mem_Ioc, mem_ball, map_sub, mem_setOf_eq]
  apply isOpen_iff_mem_nhds.2
  rintro âŸ¨a, xâŸ© âŸ¨r', âŸ¨Irr', Ir'râŸ©, hrâŸ©
  have ha : Continuous (f a) := hf.uncurry_left a
  rcases exists_between Irr' with âŸ¨t, hrt, htr'âŸ©
  rcases exists_between hrt with âŸ¨t', hrt', ht'tâŸ©
  obtain âŸ¨b, b_lt, hbâŸ© : âˆƒ b, b < s * r âˆ§ âˆ€ y âˆˆ closedBall x t, âˆ€ z âˆˆ closedBall x t,
      â€–f a z - f a y - (L z - L y)â€– â‰¤ b := by
    have B : Continuous (fun (p : E Ã— E) â†¦ â€–f a p.2 - f a p.1 - (L p.2 - L p.1)â€–) := by
      -- `continuity` took several seconds to solve this.
      refine continuous_norm.comp' <| Continuous.sub ?_ ?_
      Â· exact ha.comp' continuous_snd |>.sub <| ha.comp' continuous_fst
      Â· exact L.continuous.comp' continuous_snd |>.sub <| L.continuous.comp' continuous_fst
    have C : (closedBall x t Ã—Ë¢ closedBall x t).Nonempty := by simp; linarith
    rcases ((isCompact_closedBall x t).prod (isCompact_closedBall x t)).exists_isMaxOn
      C B.continuousOn with âŸ¨p, pt, hpâŸ©
    simp only [mem_prod, mem_closedBall] at pt
    refine âŸ¨â€–f a p.2 - f a p.1 - (L p.2 - L p.1)â€–,
      hr p.1 (pt.1.trans_lt htr') p.2 (pt.2.trans_lt htr'), fun y hy z hz â†¦ ?_âŸ©
    have D : (y, z) âˆˆ closedBall x t Ã—Ë¢ closedBall x t := mem_prod.2 âŸ¨hy, hzâŸ©
    exact hp D
  obtain âŸ¨Îµ, Îµpos, hÎµâŸ© : âˆƒ Îµ, 0 < Îµ âˆ§ b + 2 * Îµ < s * r :=
    âŸ¨(s * r - b) / 3, by linarith, by linarithâŸ©
  obtain âŸ¨u, u_open, au, huâŸ© : âˆƒ u, IsOpen u âˆ§ a âˆˆ u âˆ§ âˆ€ (p : Î± Ã— E),
      p.1 âˆˆ u â†’ p.2 âˆˆ closedBall x t â†’ dist (f.uncurry p) (f.uncurry (a, p.2)) < Îµ := by
    have C : Continuous (fun (p : Î± Ã— E) â†¦ f a p.2) :=
      -- `continuity` took several seconds to solve this.
      ha.comp' continuous_snd
    have D : ({a} Ã—Ë¢ closedBall x t).EqOn f.uncurry (fun p â†¦ f a p.2) := by
      rintro âŸ¨b, yâŸ© âŸ¨hb, -âŸ©
      simp only [mem_singleton_iff] at hb
      simp [hb]
    obtain âŸ¨v, v_open, sub_v, hvâŸ© : âˆƒ v, IsOpen v âˆ§ {a} Ã—Ë¢ closedBall x t âŠ† v âˆ§
        âˆ€ p âˆˆ v, dist (Function.uncurry f p) (f a p.2) < Îµ :=
      Uniform.exists_is_open_mem_uniformity_of_forall_mem_eq (s := {a} Ã—Ë¢ closedBall x t)
        (fun p _ â†¦ hf.continuousAt) (fun p _ â†¦ C.continuousAt) D (dist_mem_uniformity Îµpos)
    obtain âŸ¨w, w', w_open, -, sub_w, sub_w', hww'âŸ© : âˆƒ (w : Set Î±) (w' : Set E),
        IsOpen w âˆ§ IsOpen w' âˆ§ {a} âŠ† w âˆ§ closedBall x t âŠ† w' âˆ§ w Ã—Ë¢ w' âŠ† v :=
      generalized_tube_lemma isCompact_singleton (isCompact_closedBall x t) v_open sub_v
    refine âŸ¨w, w_open, sub_w rfl, ?_âŸ©
    rintro âŸ¨b, yâŸ© h hby
    exact hv _ (hww' âŸ¨h, sub_w' hbyâŸ©)
  have : u Ã—Ë¢ ball x (t - t') âˆˆ ğ“ (a, x) :=
    prod_mem_nhds (u_open.mem_nhds au) (ball_mem_nhds _ (sub_pos.2 ht't))
  filter_upwards [this]
  rintro âŸ¨a', x'âŸ© ha'x'
  simp only [mem_prod, mem_ball] at ha'x'
  refine âŸ¨t', âŸ¨hrt', ht't.le.trans (htr'.le.trans Ir'r)âŸ©, fun y hy z hz â†¦ ?_âŸ©
  have dyx : dist y x â‰¤ t := by linarith [dist_triangle y x' x]
  have dzx : dist z x â‰¤ t := by linarith [dist_triangle z x' x]
  calc
  â€–f a' z - f a' y - (L z - L y)â€– =
    â€–(f a' z - f a z) + (f a y - f a' y) + (f a z - f a y - (L z - L y))â€– := by congr; abel
  _ â‰¤ â€–f a' z - f a zâ€– + â€–f a y - f a' yâ€– + â€–f a z - f a y - (L z - L y)â€– := norm_addâ‚ƒ_le _ _ _
  _ â‰¤ Îµ + Îµ + b := by
      gcongr
      Â· rw [â† dist_eq_norm]
        change dist (f.uncurry (a', z)) (f.uncurry (a, z)) â‰¤ Îµ
        apply (hu _ _ _).le
        Â· exact ha'x'.1
        Â· simp [dzx]
      Â· rw [â† dist_eq_norm']
        change dist (f.uncurry (a', y)) (f.uncurry (a, y)) â‰¤ Îµ
        apply (hu _ _ _).le
        Â· exact ha'x'.1
        Â· simp [dyx]
      Â· simp [hb, dyx, dzx]
  _ < s * r := by linarith

lemma isOpen_B_with_param {r s t : â„} (hf : Continuous f.uncurry) (K : Set (E â†’L[ğ•œ] F)) :
    IsOpen {p : Î± Ã— E | p.2 âˆˆ B (f p.1) K r s t} := by
  suffices H : IsOpen (â‹ƒ L âˆˆ K,
      {p : Î± Ã— E | p.2 âˆˆ A (f p.1) L r t âˆ§ p.2 âˆˆ A (f p.1) L s t}) by
    convert H; ext p; simp [B]
  refine isOpen_biUnion (fun L _ â†¦ ?_)
  exact (isOpen_A_with_param hf L).inter (isOpen_A_with_param hf L)

end FDerivMeasurableAux

open FDerivMeasurableAux

theorem measurableSet_of_differentiableAt_of_isComplete_with_param
    (hf : Continuous f.uncurry) {K : Set (E â†’L[ğ•œ] F)} (hK : IsComplete K) :
    MeasurableSet {p : Î± Ã— E | DifferentiableAt ğ•œ (f p.1) p.2 âˆ§ fderiv ğ•œ (f p.1) p.2 âˆˆ K} := by
  have : {p : Î± Ã— E | DifferentiableAt ğ•œ (f p.1) p.2 âˆ§ fderiv ğ•œ (f p.1) p.2 âˆˆ K}
          = {p : Î± Ã— E | p.2 âˆˆ D (f p.1) K} := by simp [â† differentiable_set_eq_D K hK]
  rw [this]
  simp only [D, mem_iInter, mem_iUnion]
  simp only [setOf_forall, setOf_exists]
  refine MeasurableSet.iInter (fun _ â†¦ ?_)
  refine MeasurableSet.iUnion (fun _ â†¦ ?_)
  refine MeasurableSet.iInter (fun _ â†¦ ?_)
  refine MeasurableSet.iInter (fun _ â†¦ ?_)
  refine MeasurableSet.iInter (fun _ â†¦ ?_)
  refine MeasurableSet.iInter (fun _ â†¦ ?_)
  have : ProperSpace E := .of_locallyCompactSpace ğ•œ
  exact (isOpen_B_with_param hf K).measurableSet

variable (ğ•œ)
variable [CompleteSpace F]

/-- The set of differentiability points of a continuous function depending on a parameter taking
values in a complete space is Borel-measurable. -/
theorem measurableSet_of_differentiableAt_with_param (hf : Continuous f.uncurry) :
    MeasurableSet {p : Î± Ã— E | DifferentiableAt ğ•œ (f p.1) p.2} := by
  have : IsComplete (univ : Set (E â†’L[ğ•œ] F)) := complete_univ
  convert measurableSet_of_differentiableAt_of_isComplete_with_param hf this
  simp

theorem measurable_fderiv_with_param (hf : Continuous f.uncurry) :
    Measurable (fun (p : Î± Ã— E) â†¦ fderiv ğ•œ (f p.1) p.2) := by
  refine measurable_of_isClosed (fun s hs â†¦ ?_)
  have :
    (fun (p : Î± Ã— E) â†¦ fderiv ğ•œ (f p.1) p.2) â»Â¹' s =
      {p | DifferentiableAt ğ•œ (f p.1) p.2 âˆ§ fderiv ğ•œ (f p.1) p.2 âˆˆ s } âˆª
        { p | Â¬DifferentiableAt ğ•œ (f p.1) p.2} âˆ© { _p | (0 : E â†’L[ğ•œ] F) âˆˆ s} :=
    Set.ext (fun x â†¦ mem_preimage.trans fderiv_mem_iff)
  rw [this]
  exact
    (measurableSet_of_differentiableAt_of_isComplete_with_param hf hs.isComplete).union
      ((measurableSet_of_differentiableAt_with_param _ hf).compl.inter (MeasurableSet.const _))

theorem measurable_fderiv_apply_const_with_param [MeasurableSpace F] [BorelSpace F]
    (hf : Continuous f.uncurry) (y : E) :
    Measurable (fun (p : Î± Ã— E) â†¦ fderiv ğ•œ (f p.1) p.2 y) :=
  (ContinuousLinearMap.measurable_apply y).comp (measurable_fderiv_with_param ğ•œ hf)

variable {ğ•œ}

theorem measurable_deriv_with_param [LocallyCompactSpace ğ•œ] [MeasurableSpace ğ•œ]
    [OpensMeasurableSpace ğ•œ] [MeasurableSpace F]
    [BorelSpace F] {f : Î± â†’ ğ•œ â†’ F} (hf : Continuous f.uncurry) :
    Measurable (fun (p : Î± Ã— ğ•œ) â†¦ deriv (f p.1) p.2) := by
  simpa only [fderiv_deriv] using measurable_fderiv_apply_const_with_param ğ•œ hf 1

theorem stronglyMeasurable_deriv_with_param [LocallyCompactSpace ğ•œ] [MeasurableSpace ğ•œ]
    [OpensMeasurableSpace ğ•œ] [h : SecondCountableTopologyEither Î± F]
    {f : Î± â†’ ğ•œ â†’ F} (hf : Continuous f.uncurry) :
    StronglyMeasurable (fun (p : Î± Ã— ğ•œ) â†¦ deriv (f p.1) p.2) := by
  borelize F
  rcases h.out with hÎ±|hF
  Â· have : ProperSpace ğ•œ := .of_locallyCompactSpace ğ•œ
    apply stronglyMeasurable_iff_measurable_separable.2 âŸ¨measurable_deriv_with_param hf, ?_âŸ©
    have : range (fun (p : Î± Ã— ğ•œ) â†¦ deriv (f p.1) p.2)
        âŠ† closure (Submodule.span ğ•œ (range f.uncurry)) := by
      rintro - âŸ¨p, rflâŸ©
      have A : deriv (f p.1) p.2 âˆˆ closure (Submodule.span ğ•œ (range (f p.1))) := by
        rw [â† image_univ]
        apply range_deriv_subset_closure_span_image _ dense_univ (mem_range_self _)
      have B : range (f p.1) âŠ† range (f.uncurry) := by
        rintro - âŸ¨x, rflâŸ©
        exact mem_range_self (p.1, x)
      exact closure_mono (Submodule.span_mono B) A
    exact (isSeparable_range hf).span.closure.mono this
  Â· exact (measurable_deriv_with_param hf).stronglyMeasurable

theorem aemeasurable_deriv_with_param [LocallyCompactSpace ğ•œ] [MeasurableSpace ğ•œ]
    [OpensMeasurableSpace ğ•œ] [MeasurableSpace F]
    [BorelSpace F] {f : Î± â†’ ğ•œ â†’ F} (hf : Continuous f.uncurry) (Î¼ : Measure (Î± Ã— ğ•œ)) :
    AEMeasurable (fun (p : Î± Ã— ğ•œ) â†¦ deriv (f p.1) p.2) Î¼ :=
  (measurable_deriv_with_param hf).aemeasurable

theorem aestronglyMeasurable_deriv_with_param [LocallyCompactSpace ğ•œ] [MeasurableSpace ğ•œ]
    [OpensMeasurableSpace ğ•œ] [SecondCountableTopologyEither Î± F]
    {f : Î± â†’ ğ•œ â†’ F} (hf : Continuous f.uncurry) (Î¼ : Measure (Î± Ã— ğ•œ)) :
    AEStronglyMeasurable (fun (p : Î± Ã— ğ•œ) â†¦ deriv (f p.1) p.2) Î¼ :=
  (stronglyMeasurable_deriv_with_param hf).aestronglyMeasurable

end WithParam
