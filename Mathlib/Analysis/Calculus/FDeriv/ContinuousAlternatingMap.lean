/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.ContinuousMultilinearMap
import Mathlib.Analysis.NormedSpace.Alternating.Basic

/-!
# Derivatives of operations on continuous alternating maps

In this file we prove formulas for the derivatives of

- `ContinuousAlternatingMap.compContinuousLinearMap`;
- application of a `ContinuousAlternatingMap`.
-/

variable {ğ•œ Î¹ E F G H : Type*}
  [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] [NormedAddCommGroup H] [NormedSpace ğ•œ H]

open ContinuousAlternatingMap
open scoped Topology BigOperators

section CompContinuousLinearMap

variable
  {f : E â†’ G [â‹€^Î¹]â†’L[ğ•œ] H} {f' : E â†’L[ğ•œ] G [â‹€^Î¹]â†’L[ğ•œ] H}
  {g : E â†’ F â†’L[ğ•œ] G} {g' : E â†’L[ğ•œ] F â†’L[ğ•œ] G}
  {s : Set E} {x : E}

theorem ContinuousAlternatingMap.hasStrictFDerivAt_toContinuousMultilinearMap_comp_iff
    [Finite Î¹] :
    HasStrictFDerivAt (toContinuousMultilinearMap âˆ˜ f) (toContinuousMultilinearMapCLM ğ•œ âˆ˜L f') x â†”
      HasStrictFDerivAt f f' x := by
  cases nonempty_fintype Î¹
  constructor <;> intro h
  Â· rw [hasStrictFDerivAt_iff_isLittleOTVS] at h âŠ¢
    refine Asymptotics.IsBigOTVS.trans_isLittleOTVS ?_ h
    simp only [Function.comp_apply, â† toContinuousMultilinearMapCLM_apply ğ•œ,
      ContinuousLinearMap.comp_apply, â† map_sub]
    apply LinearMap.isBigOTVS_rev_comp
    simp [isEmbedding_toContinuousMultilinearMap.nhds_eq_comap]
  Â· exact (toContinuousMultilinearMapCLM ğ•œ).hasStrictFDerivAt.comp x h

section HasFDerivAt

variable [Fintype Î¹] [DecidableEq Î¹]

theorem ContinuousAlternatingMap.hasStrictFDerivAt_compContinuousLinearMap
    (fg : (G [â‹€^Î¹]â†’L[ğ•œ] H) Ã— (F â†’L[ğ•œ] G)) :
    HasStrictFDerivAt
      (fun fg : (G [â‹€^Î¹]â†’L[ğ•œ] H) Ã— (F â†’L[ğ•œ] G) â†¦ fg.1.compContinuousLinearMap fg.2)
      (compContinuousLinearMapCLM fg.2 âˆ˜L .fst _ _ _ +
        fg.1.fderivCompContinuousLinearMap fg.2 âˆ˜L .snd _ _ _)
      fg := by
  rw [â† hasStrictFDerivAt_toContinuousMultilinearMap_comp_iff]
  have Hâ‚ := ContinuousMultilinearMap.hasStrictFDerivAt_compContinuousLinearMap
    (fg.1.1, fun _ : Î¹ â†¦ fg.2)
  have Hâ‚‚ := ((toContinuousMultilinearMapCLM ğ•œ).hasStrictFDerivAt (x := fg.1))
  have Hâ‚ƒ := hasStrictFDerivAt_pi.mpr fun i : Î¹ â†¦ hasStrictFDerivAt_id (ğ•œ := ğ•œ) fg.2
  exact Hâ‚.comp fg (Hâ‚‚.prodMap fg Hâ‚ƒ)

theorem HasStrictFDerivAt.continuousAlternatingMapCompContinuousLinearMap
    (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g x))
      (compContinuousLinearMapCLM (g x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g x) âˆ˜L g') x :=
  hasStrictFDerivAt_compContinuousLinearMap (f x, g x) |>.comp x (hf.prodMk hg)

theorem HasFDerivAt.continuousAlternatingMapCompContinuousLinearMap
    (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun x â†¦ (f x).compContinuousLinearMap (g x))
      (compContinuousLinearMapCLM (g x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g x) âˆ˜L g') x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g x)) |>.hasFDerivAt
    |>.comp x (hf.prodMk hg)

theorem HasFDerivWithinAt.continuousAlternatingMapCompContinuousLinearMap
    (hf : HasFDerivWithinAt f f' s x) (hg : HasFDerivWithinAt g g' s x) :
    HasFDerivWithinAt (fun x â†¦ (f x).compContinuousLinearMap (g x))
      (compContinuousLinearMapCLM (g x) âˆ˜L f' +
        (f x).fderivCompContinuousLinearMap (g x) âˆ˜L g') s x := by
  convert hasStrictFDerivAt_compContinuousLinearMap (f x, (g x)) |>.hasFDerivAt
    |>.comp_hasFDerivWithinAt x (hf.prodMk hg)

theorem fderivWithin_continuousAlternatingMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ğ•œ f s x) (hg : DifferentiableWithinAt ğ•œ g s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g x)) s x =
      compContinuousLinearMapCLM (g x) âˆ˜L fderivWithin ğ•œ f s x +
        (f x).fderivCompContinuousLinearMap (g x) âˆ˜L fderivWithin ğ•œ g s x :=
  hf.hasFDerivWithinAt.continuousAlternatingMapCompContinuousLinearMap (hg.hasFDerivWithinAt)
    |>.fderivWithin hs

theorem fderiv_continuousAlternatingMapCompContinuousLinearMap
    (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    fderiv ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g x)) x =
      compContinuousLinearMapCLM (g x) âˆ˜L fderiv ğ•œ f x +
        (f x).fderivCompContinuousLinearMap (g x) âˆ˜L fderiv ğ•œ g x :=
  hf.hasFDerivAt.continuousAlternatingMapCompContinuousLinearMap (hg.hasFDerivAt) |>.fderiv

end HasFDerivAt

variable [Finite Î¹]

theorem DifferentiableWithinAt.continuousAlternatingMapCompContinuousLinearMap
    (hf : DifferentiableWithinAt ğ•œ f s x) (hg : DifferentiableWithinAt ğ•œ g s x) :
    DifferentiableWithinAt ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g x)) s x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivWithinAt.continuousAlternatingMapCompContinuousLinearMap hg.hasFDerivWithinAt
    |>.differentiableWithinAt

theorem DifferentiableAt.continuousAlternatingMapCompContinuousLinearMap
    (hf : DifferentiableAt ğ•œ f x) (hg : DifferentiableAt ğ•œ g x) :
    DifferentiableAt ğ•œ (fun x â†¦ (f x).compContinuousLinearMap (g x)) x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivAt.continuousAlternatingMapCompContinuousLinearMap hg.hasFDerivAt
    |>.differentiableAt

end CompContinuousLinearMap

section Apply

variable {f : E â†’ F [â‹€^Î¹]â†’L[ğ•œ] G} {f' : E â†’L[ğ•œ] F [â‹€^Î¹]â†’L[ğ•œ] G}
  {g : Î¹ â†’ E â†’ F} {g' : Î¹ â†’ E â†’L[ğ•œ] F}
  {s : Set E} {x : E}

section HasFDerivAt

variable [Fintype Î¹] [DecidableEq Î¹]

namespace ContinuousAlternatingMap

theorem hasStrictFDerivAt (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (x : Î¹ â†’ E) :
    HasStrictFDerivAt f (f.1.linearDeriv x) x :=
  f.1.hasStrictFDerivAt x

theorem hasFDerivAt (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (x : Î¹ â†’ E) : HasFDerivAt f (f.1.linearDeriv x) x :=
  f.1.hasFDerivAt x

theorem hasFDerivWithinAt (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (s : Set (Î¹ â†’ E)) (x : Î¹ â†’ E) :
    HasFDerivWithinAt f (f.1.linearDeriv x) s x :=
  (f.hasFDerivAt x).hasFDerivWithinAt

end ContinuousAlternatingMap

theorem HasStrictFDerivAt.continuousAlternatingMap_apply (hf : HasStrictFDerivAt f f' x)
    (hg : âˆ€ i, HasStrictFDerivAt (g i) (g' i) x) :
    HasStrictFDerivAt
      (fun x â†¦ f x (g Â· x))
      (apply ğ•œ F G (g Â· x) âˆ˜L f' + âˆ‘ i, (f x).toContinuousLinearMap (g Â· x) i âˆ˜L g' i)
      x :=
  (toContinuousMultilinearMapCLM ğ•œ).hasStrictFDerivAt.comp x hf
    |>.continuousMultilinearMap_apply hg

theorem HasFDerivAt.continuousAlternatingMap_apply (hf : HasFDerivAt f f' x)
    (hg : âˆ€ i, HasFDerivAt (g i) (g' i) x) :
    HasFDerivAt
      (fun x â†¦ f x (g Â· x))
      (apply ğ•œ F G (g Â· x) âˆ˜L f' + âˆ‘ i, (f x).toContinuousLinearMap (g Â· x) i âˆ˜L g' i)
      x :=
  (toContinuousMultilinearMapCLM ğ•œ).hasFDerivAt.comp x hf
    |>.continuousMultilinearMap_apply hg

theorem HasFDerivWithinAt.continuousAlternatingMap_apply (hf : HasFDerivWithinAt f f' s x)
    (hg : âˆ€ i, HasFDerivWithinAt (g i) (g' i) s x) :
    HasFDerivWithinAt
      (fun x â†¦ f x (g Â· x))
      (apply ğ•œ F G (g Â· x) âˆ˜L f' + âˆ‘ i, (f x).toContinuousLinearMap (g Â· x) i âˆ˜L g' i)
      s x :=
  (toContinuousMultilinearMapCLM ğ•œ).hasFDerivAt.comp_hasFDerivWithinAt x hf
    |>.continuousMultilinearMap_apply hg

theorem fderivWithin_continuousAlternatingMap_apply (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : âˆ€ i, DifferentiableWithinAt ğ•œ (g i) s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x â†¦ f x (g Â· x)) s x =
      apply ğ•œ F G (g Â· x) âˆ˜L fderivWithin ğ•œ f s x +
        âˆ‘ i, (f x).toContinuousLinearMap (g Â· x) i âˆ˜L fderivWithin ğ•œ (g i) s x :=
  hf.hasFDerivWithinAt.continuousAlternatingMap_apply (fun i â†¦ (hg i).hasFDerivWithinAt)
    |>.fderivWithin hs

theorem fderivWithin_continuousAlternatingMap_apply_apply (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : âˆ€ i, DifferentiableWithinAt ğ•œ (g i) s x) (hs : UniqueDiffWithinAt ğ•œ s x) (dx : E) :
    fderivWithin ğ•œ (fun x â†¦ f x (g Â· x)) s x dx =
      fderivWithin ğ•œ f s x dx (g Â· x) +
        âˆ‘ i, f x (Function.update (g Â· x) i (fderivWithin ğ•œ (g i) s x dx)) := by
  simp [fderivWithin_continuousAlternatingMap_apply, *]

theorem fderiv_continuousAlternatingMap_apply (hf : DifferentiableAt ğ•œ f x)
    (hg : âˆ€ i, DifferentiableAt ğ•œ (g i) x) :
    fderiv ğ•œ (fun x â†¦ f x (g Â· x)) x =
      apply ğ•œ F G (g Â· x) âˆ˜L fderiv ğ•œ f x +
        âˆ‘ i, (f x).toContinuousLinearMap (g Â· x) i âˆ˜L fderiv ğ•œ (g i) x :=
  hf.hasFDerivAt.continuousAlternatingMap_apply (fun i â†¦ (hg i).hasFDerivAt) |>.fderiv

theorem fderiv_continuousAlternatingMap_apply_apply (hf : DifferentiableAt ğ•œ f x)
    (hg : âˆ€ i, DifferentiableAt ğ•œ (g i) x) (dx : E) :
    fderiv ğ•œ (fun x â†¦ f x (g Â· x)) x dx =
      fderiv ğ•œ f x dx (g Â· x) +
        âˆ‘ i, f x (Function.update (g Â· x) i (fderiv ğ•œ (g i) x dx)) := by
  simp [fderiv_continuousAlternatingMap_apply, *]

end HasFDerivAt

variable [Finite Î¹]

theorem DifferentiableWithinAt.continuousAlternatingMap_apply (hf : DifferentiableWithinAt ğ•œ f s x)
    (hg : âˆ€ i, DifferentiableWithinAt ğ•œ (g i) s x) :
    DifferentiableWithinAt ğ•œ (fun x â†¦ f x (g Â· x)) s x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivWithinAt.continuousAlternatingMap_apply (fun i â†¦ (hg i).hasFDerivWithinAt)
    |>.differentiableWithinAt

theorem DifferentiableAt.continuousAlternatingMap_apply (hf : DifferentiableAt ğ•œ f x)
    (hg : âˆ€ i, DifferentiableAt ğ•œ (g i) x) : DifferentiableAt ğ•œ (fun x â†¦ f x (g Â· x)) x := by
  cases nonempty_fintype Î¹
  classical
  exact hf.hasFDerivAt.continuousAlternatingMap_apply (fun i â†¦ (hg i).hasFDerivAt)
    |>.differentiableAt

theorem DifferentiableOn.continuousAlternatingMap_apply (hf : DifferentiableOn ğ•œ f s)
    (hg : âˆ€ i, DifferentiableOn ğ•œ (g i) s) : DifferentiableOn ğ•œ (fun x â†¦ f x (g Â· x)) s :=
  fun x hx â†¦ (hf x hx).continuousAlternatingMap_apply (hg Â· x hx)

theorem Differentiable.continuousAlternatingMap_apply (hf : Differentiable ğ•œ f)
    (hg : âˆ€ i, Differentiable ğ•œ (g i)) : Differentiable ğ•œ (fun x â†¦ f x (g Â· x)) :=
  fun x â†¦ (hf x).continuousAlternatingMap_apply (hg Â· x)

theorem ContinuousAlternatingMap.differentiable (f : E [â‹€^Î¹]â†’L[ğ•œ] F) : Differentiable ğ•œ f := by
  cases nonempty_fintype Î¹
  -- TODO: marking `Differentiable.continuousAlternatingMap_apply` as `fun_prop` doesn't work.
  -- Fix it
  apply Differentiable.continuousAlternatingMap_apply <;> fun_prop

end Apply
