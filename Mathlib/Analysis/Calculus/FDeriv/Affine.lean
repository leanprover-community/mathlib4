/-
Copyright (c) 2025 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
module

public import Mathlib.Analysis.Calculus.FDeriv.Add
public import Mathlib.Analysis.Normed.Group.AddTorsor
public import Mathlib.Topology.Algebra.ContinuousAffineMap


/-!
# The derivative of continuous affine maps

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
continuous affine maps.
-/

public section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  (f : E â†’á´¬[ğ•œ] F) {x : E} {s : Set E} {L : Filter E}

namespace ContinuousAffineMap

/-!
### Continuous affine maps
-/

@[fun_prop]
protected theorem hasStrictFDerivAt {x : E} : HasStrictFDerivAt f f.contLinear x := by
  rw [f.decomp]
  simpa using f.contLinear.hasStrictFDerivAt.add (hasStrictFDerivAt_const (f 0) x)

protected theorem hasFDerivAtFilter : HasFDerivAtFilter f f.contLinear x L := by
  rw [f.decomp]
  simpa using f.contLinear.hasFDerivAtFilter.add (hasFDerivAtFilter_const (f 0) x L)

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt f f.contLinear s x :=
  f.hasFDerivAtFilter

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt f f.contLinear x :=
  f.hasFDerivAtFilter

@[simp, fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ f x :=
  f.hasFDerivAt.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ f s x :=
  f.differentiableAt.differentiableWithinAt

@[simp]
protected theorem fderiv : fderiv ğ•œ f x = f.contLinear :=
  f.hasFDerivAt.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ f s x = f.contLinear := by
  rw [DifferentiableAt.fderivWithin f.differentiableAt hxs]
  exact f.fderiv

@[simp, fun_prop]
protected theorem differentiable : Differentiable ğ•œ f := fun _ =>
  f.differentiableAt

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ f s :=
  f.differentiable.differentiableOn

end ContinuousAffineMap
