module

public import Mathlib.Analysis.Calculus.FDeriv.Basic
import Mathlib.Analysis.Normed.Operator.NNNorm

public section

open Filter
open scoped Topology

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {s : Set E}
  {g : G â†’ E} {h : G â†’ F} {h' : G â†’L[ğ•œ] F} {a : G} {t : Set G}

theorem HasFDerivWithinAt.of_comp_left_of_leftInverse {f'symm : F â†’L[ğ•œ] E}
    (hst : Tendsto g (ğ“[t] a) (ğ“[s] (g a))) (hf : HasFDerivWithinAt f f' s (g a))
    (hh : HasFDerivWithinAt h h' t a) (hcomp : f âˆ˜ g =á¶ [ğ“[t] a] h)
    (hf'symm : Function.LeftInverse f'symm f') (ha : a âˆˆ t) :
    HasFDerivWithinAt g (f'symm âˆ˜L h') t a := by
  refine .of_isLittleO ?_
  calc (fun x' â†¦ g x' - g a - (f'symm âˆ˜L h') (x' - a))
    _ =O[ğ“[t] a] fun x' â†¦ f' (g x' - g a) - h' (x' - a) :=
      f'symm.isBigO_comp _ _ |>.congr_left <| by simp [hf'symm _]
    _ =o[ğ“[t] a] (Â· - a) := ?_
  refine hf.isLittleO.comp_tendsto hst |>.symm |>.trans_isBigO ?_ |>.triangle ?_
  Â· have hlip : AntilipschitzWith â€–f'symmâ€–â‚Š f' := fun x y â†¦ by
      simpa [hf'symm _] using f'symm.lipschitz (f' x) (f' y)
    refine hf.isBigO_sub_rev hlip |>.comp_tendsto hst |>.trans ?_
    refine hh.isBigO_sub.congr' (hcomp.mono fun x hx â†¦ ?_) .rfl
    simp [â† hx, â† hcomp.self_of_nhdsWithin ha]
  Â· refine hh.isLittleO.congr' (hcomp.mono fun x hx â†¦ ?_) .rfl
    simp [â† hx, â† hcomp.self_of_nhdsWithin ha]

theorem HasFDerivWithinAt.of_comp_left (hst : Tendsto g (ğ“[t] a) (ğ“[s] (g a)))
    (hf : HasFDerivWithinAt f f' s (g a)) (hh : HasFDerivWithinAt h h' t a)
    (hcomp : f âˆ˜ g =á¶ [ğ“[t] a] h) (hf' : f'.IsInvertible) (ha : a âˆˆ t) :
    HasFDerivWithinAt g (f'.inverse âˆ˜L h') t a :=
  .of_comp_left_of_leftInverse hst hf hh hcomp hf'.inverse_apply_self ha

/-- If `f (g y) = y` for `y` in a neighborhood of `a` within `t`,
`g` maps a neighborhood of `a` within `t` to a neighborhood of `g a` within `s`,
and `f` has an invertible derivative `f'` at `g a` within `s`,
then `g` has the derivative `f'â»Â¹` at `a` within `t`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasFDerivWithinAt.of_local_left_inverse {g : F â†’ E} {f' : E â‰ƒL[ğ•œ] F} {a : F} {t : Set F}
    (hg : Tendsto g (ğ“[t] a) (ğ“[s] (g a))) (hf : HasFDerivWithinAt f (f' : E â†’L[ğ•œ] F) s (g a))
    (ha : a âˆˆ t) (hfg : âˆ€á¶  y in ğ“[t] a, f (g y) = y) :
    HasFDerivWithinAt g (f'.symm : F â†’L[ğ•œ] E) t a := by
  simpa using hf.of_comp_left hg (hasFDerivWithinAt_id ..) hfg (by simp) ha

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasStrictFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasStrictFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  replace hg := hg.prodMap' hg
  replace hfg := hfg.prodMk_nhds hfg
  have :
    (fun p : F Ã— F => g p.1 - g p.2 - f'.symm (p.1 - p.2)) =O[ğ“ (a, a)] fun p : F Ã— F =>
      f' (g p.1 - g p.2) - (p.1 - p.2) := by
    refine ((f'.symm : F â†’L[ğ•œ] E).isBigO_comp _ _).congr (fun x => ?_) fun _ => rfl
    simp
  refine .of_isLittleO <| this.trans_isLittleO ?_
  clear this
  refine ((hf.isLittleO.comp_tendsto hg).symm.congr'
    (hfg.mono ?_) (Eventually.of_forall fun _ => rfl)).trans_isBigO ?_
  Â· rintro p âŸ¨hp1, hp2âŸ©
    simp [hp1, hp2]
  Â· refine (hf.isBigO_sub_rev.comp_tendsto hg).congr' (Eventually.of_forall fun _ => rfl)
      (hfg.mono ?_)
    rintro p âŸ¨hp1, hp2âŸ©
    simp only [(Â· âˆ˜ Â·), hp1, hp2, Prod.map]

theorem HasFDerivAt.of_comp_left {g : G â†’ E} {h : G â†’ F} {h' : G â†’L[ğ•œ] F} {a : G}
    (hst : ContinuousAt g a) (hf : HasFDerivAt f f' (g a)) (hh : HasFDerivAt h h' a)
    (hf' : f'.IsInvertible) (hcomp : f âˆ˜ g =á¶ [ğ“ a] h) :
    HasFDerivAt g (f'.inverse.comp h') a := by
  simp only [â† hasFDerivWithinAt_univ, â† nhdsWithin_univ] at *
  refine hf.of_comp_left ?_ hh hcomp hf' trivial
  simpa

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem HasFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  simp only [â† hasFDerivWithinAt_univ, â† nhdsWithin_univ] at hf hfg âŠ¢
  exact hf.of_local_left_inverse (.inf hg (by simp)) (Set.mem_univ _) hfg

/-- If `f` is an open partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` in the sense of strict differentiability at `f.symm a`, then `f.symm` has
the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem OpenPartialHomeomorph.hasStrictFDerivAt_symm (f : OpenPartialHomeomorph E F)
    {f' : E â‰ƒL[ğ•œ] F} {a : F} (ha : a âˆˆ f.target)
    (htff' : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasStrictFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)

/-- If `f` is an open partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem OpenPartialHomeomorph.hasFDerivAt_symm (f : OpenPartialHomeomorph E F) {f' : E â‰ƒL[ğ•œ] F}
    {a : F} (ha : a âˆˆ f.target) (htff' : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)
