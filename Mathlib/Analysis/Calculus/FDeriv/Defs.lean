/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.TVS

/-!
# The Fr√©chet derivative: definition

Let `E` and `F` be normed spaces, `f : E ‚Üí F`, and `f' : E ‚ÜíL[ùïú] F` a
continuous ùïú-linear map, where `ùïú` is a non-discrete normed field. Then

  `HasFDerivWithinAt f f' s x`

says that `f` has derivative `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `HasFDerivAt f f' x := HasFDerivWithinAt f f' x univ`

Finally,

  `HasStrictFDerivAt f f' x`

means that `f : E ‚Üí F` has derivative `f' : E ‚ÜíL[ùïú] F` in the sense of strict differentiability,
i.e., `f y - f z - f'(y - z) = o(y - z)` as `y, z ‚Üí x`. This notion is used in the inverse
function theorem, and is defined here only to avoid proving theorems like
`IsBoundedBilinearMap.hasFDerivAt` twice: first for `HasFDerivAt`, then for
`HasStrictFDerivAt`.

This file `Defs.lean` is intended to just contain the definitions and the bare minimum of
supporting lemmas; a much wider range of elementary properties are proved in the file  `Basic.lean`.

Other files in the folder `Analysis/Calculus/FDeriv/` contain the usual formulas
(and existence assertions) for the derivative of
* constants (`Const.lean`)
* the identity
* bounded linear maps (`Linear.lean`)
* bounded bilinear maps (`Bilinear.lean`)
* sum of two functions (`Add.lean`)
* sum of finitely many functions (`Add.lean`)
* multiplication of a function by a scalar constant (`Add.lean`)
* negative of a function (`Add.lean`)
* subtraction of two functions (`Add.lean`)
* multiplication of a function by a scalar function (`Mul.lean`)
* multiplication of two scalar functions (`Mul.lean`)
* composition of functions (the chain rule) (`Comp.lean`)
* inverse function (`Mul.lean`)
  (assuming that it exists; the inverse function theorem is in `../Inverse.lean`)

## Implementation details

The derivative is defined in terms of the `IsLittleOTVS` relation to ensure the definition does not
ingrain a choice of norm, and is then quickly translated to the more convenient `IsLittleO` in the
subsequent theorems. It is also characterized in terms of the `Tendsto` relation.

We also introduce predicates `DifferentiableWithinAt ùïú f s x` (where `ùïú` is the base field,
`f` the function to be differentiated, `x` the point at which the derivative is asserted to exist,
and `s` the set along which the derivative is defined), as well as `DifferentiableAt ùïú f x`,
`DifferentiableOn ùïú f s` and `Differentiable ùïú f` to express the existence of a derivative.

To be able to compute with derivatives, we write `fderivWithin ùïú f s x` and `fderiv ùïú f x`
for some choice of a derivative if it exists, and the zero function otherwise. This choice only
behaves well along sets for which the derivative is unique, i.e., those for which the tangent
directions span a dense subset of the whole space. The predicates `UniqueDiffWithinAt s x` and
`UniqueDiffOn s`, defined in `TangentCone.lean` express this property. We prove that indeed
they imply the uniqueness of the derivative. This is satisfied for open subsets, and in particular
for `univ`. This uniqueness only holds when the field is non-discrete, which we request at the very
beginning: otherwise, a derivative can be defined, but it has no interesting properties whatsoever.

## TODO

Generalize more results to topological vector spaces.

## Tags

derivative, differentiable, Fr√©chet, calculus

-/

open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

variable {ùïú : Type*} [NontriviallyNormedField ùïú]

noncomputable section TVS
/-!
## Definitions valid in an arbitrary topological vector space
-/

variable {E : Type*} [AddCommGroup E] [Module ùïú E] [TopologicalSpace E]
variable {F : Type*} [AddCommGroup F] [Module ùïú F] [TopologicalSpace F]

/-- A function `f` has the continuous linear map `f'` as derivative along the filter `L` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` converges along the filter `L`. This definition
is designed to be specialized for `L = ùìù x` (in `HasFDerivAt`), giving rise to the usual notion
of Fr√©chet derivative, and for `L = ùìù[s] x` (in `HasFDerivWithinAt`), giving rise to
the notion of Fr√©chet derivative along the set `s`. -/
@[mk_iff hasFDerivAtFilter_iff_isLittleOTVS]
structure HasFDerivAtFilter (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) (L : Filter E) : Prop where
  of_isLittleOTVS ::
    isLittleOTVS : (fun x' => f x' - f x - f' (x' - x)) =o[ùïú; L] (fun x' => x' - x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` within a set `s` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x` inside `s`. -/
@[fun_prop]
def HasFDerivWithinAt (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (s : Set E) (x : E) :=
  HasFDerivAtFilter f f' x (ùìù[s] x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x`. -/
@[fun_prop]
def HasFDerivAt (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) :=
  HasFDerivAtFilter f f' x (ùìù x)

/-- A function `f` has derivative `f'` at `a` in the sense of *strict differentiability*
if `f x - f y - f' (x - y) = o(x - y)` as `x, y ‚Üí a`. This form of differentiability is required,
e.g., by the inverse function theorem. Any `C^1` function on a vector space over `‚Ñù` is strictly
differentiable but this definition works, e.g., for vector spaces over `p`-adic numbers. -/
@[fun_prop, mk_iff hasStrictFDerivAt_iff_isLittleOTVS]
structure HasStrictFDerivAt (f : E ‚Üí F) (f' : E ‚ÜíL[ùïú] F) (x : E) where
  of_isLittleOTVS ::
    isLittleOTVS :
      (fun p : E √ó E => f p.1 - f p.2 - f' (p.1 - p.2))
        =o[ùïú; ùìù (x, x)] (fun p : E √ó E => p.1 - p.2)

variable (ùïú)

/-- A function `f` is differentiable at a point `x` within a set `s` if it admits a derivative
there (possibly non-unique). -/
@[fun_prop]
def DifferentiableWithinAt (f : E ‚Üí F) (s : Set E) (x : E) :=
  ‚àÉ f' : E ‚ÜíL[ùïú] F, HasFDerivWithinAt f f' s x

/-- A function `f` is differentiable at a point `x` if it admits a derivative there (possibly
non-unique). -/
@[fun_prop]
def DifferentiableAt (f : E ‚Üí F) (x : E) :=
  ‚àÉ f' : E ‚ÜíL[ùïú] F, HasFDerivAt f f' x

open scoped Classical in
/-- If `f` has a derivative at `x` within `s`, then `fderivWithin ùïú f s x` is such a derivative.
Otherwise, it is set to `0`. We also set it to be zero, if zero is one of possible derivatives. -/
irreducible_def fderivWithin (f : E ‚Üí F) (s : Set E) (x : E) : E ‚ÜíL[ùïú] F :=
  if HasFDerivWithinAt f (0 : E ‚ÜíL[ùïú] F) s x
    then 0
  else if h : DifferentiableWithinAt ùïú f s x
    then Classical.choose h
  else 0

/-- If `f` has a derivative at `x`, then `fderiv ùïú f x` is such a derivative. Otherwise, it is
set to `0`. -/
irreducible_def fderiv (f : E ‚Üí F) (x : E) : E ‚ÜíL[ùïú] F :=
  fderivWithin ùïú f univ x

/-- `DifferentiableOn ùïú f s` means that `f` is differentiable within `s` at any point of `s`. -/
@[fun_prop]
def DifferentiableOn (f : E ‚Üí F) (s : Set E) :=
  ‚àÄ x ‚àà s, DifferentiableWithinAt ùïú f s x

/-- `Differentiable ùïú f` means that `f` is differentiable at any point. -/
@[fun_prop]
def Differentiable (f : E ‚Üí F) :=
  ‚àÄ x, DifferentiableAt ùïú f x

variable {ùïú}
variable {f f‚ÇÄ f‚ÇÅ g : E ‚Üí F}
variable {f' f‚ÇÄ' f‚ÇÅ' g' : E ‚ÜíL[ùïú] F}
variable {x : E}
variable {s t : Set E}
variable {L L‚ÇÅ L‚ÇÇ : Filter E}

theorem fderivWithin_zero_of_not_differentiableWithinAt (h : ¬¨DifferentiableWithinAt ùïú f s x) :
    fderivWithin ùïú f s x = 0 := by
  simp [fderivWithin, h]

@[simp]
theorem fderivWithin_univ : fderivWithin ùïú f univ = fderiv ùïú f := by
  ext
  rw [fderiv]

end TVS

section Normed
/-!
## Reformulations for normed spaces
-/

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]
variable {f : E ‚Üí F} {f' : E ‚ÜíL[ùïú] F} {x : E}

theorem hasFDerivAtFilter_iff_isLittleO {L : Filter E} :
    HasFDerivAtFilter f f' x L ‚Üî (fun x' => f x' - f x - f' (x' - x)) =o[L] fun x' => x' - x :=
  (hasFDerivAtFilter_iff_isLittleOTVS ..).trans isLittleOTVS_iff_isLittleO

alias ‚ü®HasFDerivAtFilter.isLittleO, HasFDerivAtFilter.of_isLittleO‚ü© :=
  hasFDerivAtFilter_iff_isLittleO

theorem hasStrictFDerivAt_iff_isLittleO :
    HasStrictFDerivAt f f' x ‚Üî
      (fun p : E √ó E => f p.1 - f p.2 - f' (p.1 - p.2)) =o[ùìù (x, x)] fun p : E √ó E => p.1 - p.2 :=
  (hasStrictFDerivAt_iff_isLittleOTVS ..).trans isLittleOTVS_iff_isLittleO

alias ‚ü®HasStrictFDerivAt.isLittleO, HasStrictFDerivAt.of_isLittleO‚ü© :=
  hasStrictFDerivAt_iff_isLittleO

end Normed
