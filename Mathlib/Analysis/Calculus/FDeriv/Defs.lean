/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.TVS

open Filter Asymptotics Set Metric Topology

noncomputable section

section TVS
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [AddCommGroup E] [Module ğ•œ E] [TopologicalSpace E]
variable {F : Type*} [AddCommGroup F] [Module ğ•œ F] [TopologicalSpace F]

/-- A function `f` has the continuous linear map `f'` as derivative along the filter `L` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` converges along the filter `L`. This definition
is designed to be specialized for `L = ğ“ x` (in `HasFDerivAt`), giving rise to the usual notion
of FrÃ©chet derivative, and for `L = ğ“[s] x` (in `HasFDerivWithinAt`), giving rise to
the notion of FrÃ©chet derivative along the set `s`. -/
@[mk_iff hasFDerivAtFilter_iff_isLittleOTVS]
structure HasFDerivAtFilter (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) (L : Filter E) : Prop where
  of_isLittleOTVS ::
    isLittleOTVS : (fun x' => f x' - f x - f' (x' - x)) =o[ğ•œ;L] (fun x' => x' - x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` within a set `s` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x` inside `s`. -/
@[fun_prop]
def HasFDerivWithinAt (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (s : Set E) (x : E) :=
  HasFDerivAtFilter f f' x (ğ“[s] x)

/-- A function `f` has the continuous linear map `f'` as derivative at `x` if
`f x' = f x + f' (x' - x) + o (x' - x)` when `x'` tends to `x`. -/
@[fun_prop]
def HasFDerivAt (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) :=
  HasFDerivAtFilter f f' x (ğ“ x)

/-- A function `f` has derivative `f'` at `a` in the sense of *strict differentiability*
if `f x - f y - f' (x - y) = o(x - y)` as `x, y â†’ a`. This form of differentiability is required,
e.g., by the inverse function theorem. Any `C^1` function on a vector space over `â„` is strictly
differentiable but this definition works, e.g., for vector spaces over `p`-adic numbers. -/
@[fun_prop, mk_iff hasStrictFDerivAt_iff_isLittleOTVS]
structure HasStrictFDerivAt (f : E â†’ F) (f' : E â†’L[ğ•œ] F) (x : E) where
  of_isLittleOTVS ::
    isLittleOTVS :
      (fun p : E Ã— E => f p.1 - f p.2 - f' (p.1 - p.2))
        =o[ğ•œ;ğ“ (x, x)] (fun p : E Ã— E => p.1 - p.2)

variable (ğ•œ)

/-- A function `f` is differentiable at a point `x` within a set `s` if it admits a derivative
there (possibly non-unique). -/
@[fun_prop]
def DifferentiableWithinAt (f : E â†’ F) (s : Set E) (x : E) :=
  âˆƒ f' : E â†’L[ğ•œ] F, HasFDerivWithinAt f f' s x

/-- A function `f` is differentiable at a point `x` if it admits a derivative there (possibly
non-unique). -/
@[fun_prop]
def DifferentiableAt (f : E â†’ F) (x : E) :=
  âˆƒ f' : E â†’L[ğ•œ] F, HasFDerivAt f f' x

open scoped Classical in
/-- If `f` has a derivative at `x` within `s`, then `fderivWithin ğ•œ f s x` is such a derivative.
Otherwise, it is set to `0`. We also set it to be zero, if zero is one of possible derivatives. -/
irreducible_def fderivWithin (f : E â†’ F) (s : Set E) (x : E) : E â†’L[ğ•œ] F :=
  if HasFDerivWithinAt f (0 : E â†’L[ğ•œ] F) s x
    then 0
  else if h : DifferentiableWithinAt ğ•œ f s x
    then Classical.choose h
  else 0

/-- If `f` has a derivative at `x`, then `fderiv ğ•œ f x` is such a derivative. Otherwise, it is
set to `0`. -/
irreducible_def fderiv (f : E â†’ F) (x : E) : E â†’L[ğ•œ] F :=
  fderivWithin ğ•œ f univ x

/-- `DifferentiableOn ğ•œ f s` means that `f` is differentiable within `s` at any point of `s`. -/
@[fun_prop]
def DifferentiableOn (f : E â†’ F) (s : Set E) :=
  âˆ€ x âˆˆ s, DifferentiableWithinAt ğ•œ f s x

/-- `Differentiable ğ•œ f` means that `f` is differentiable at any point. -/
@[fun_prop]
def Differentiable (f : E â†’ F) :=
  âˆ€ x, DifferentiableAt ğ•œ f x

variable {ğ•œ}
variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

nonrec theorem HasFDerivAtFilter.mono (h : HasFDerivAtFilter f f' x Lâ‚‚) (hst : Lâ‚ â‰¤ Lâ‚‚) :
    HasFDerivAtFilter f f' x Lâ‚ :=
  .of_isLittleOTVS <| h.isLittleOTVS.mono hst

theorem HasFDerivWithinAt.mono_of_mem_nhdsWithin
    (h : HasFDerivWithinAt f f' t x) (hst : t âˆˆ ğ“[s] x) :
    HasFDerivWithinAt f f' s x :=
  h.mono <| nhdsWithin_le_iff.mpr hst

@[deprecated (since := "2024-10-31")]
alias HasFDerivWithinAt.mono_of_mem := HasFDerivWithinAt.mono_of_mem_nhdsWithin

nonrec theorem HasFDerivWithinAt.mono (h : HasFDerivWithinAt f f' t x) (hst : s âŠ† t) :
    HasFDerivWithinAt f f' s x :=
  h.mono <| nhdsWithin_mono _ hst

theorem HasFDerivAt.hasFDerivAtFilter (h : HasFDerivAt f f' x) (hL : L â‰¤ ğ“ x) :
    HasFDerivAtFilter f f' x L :=
  h.mono hL

@[fun_prop]
theorem HasFDerivAt.hasFDerivWithinAt (h : HasFDerivAt f f' x) : HasFDerivWithinAt f f' s x :=
  h.hasFDerivAtFilter inf_le_left

@[fun_prop]
theorem HasFDerivWithinAt.differentiableWithinAt (h : HasFDerivWithinAt f f' s x) :
    DifferentiableWithinAt ğ•œ f s x :=
  âŸ¨f', hâŸ©

@[fun_prop]
theorem HasFDerivAt.differentiableAt (h : HasFDerivAt f f' x) : DifferentiableAt ğ•œ f x :=
  âŸ¨f', hâŸ©

@[simp]
theorem hasFDerivWithinAt_univ : HasFDerivWithinAt f f' univ x â†” HasFDerivAt f f' x := by
  simp only [HasFDerivWithinAt, nhdsWithin_univ, HasFDerivAt]

alias âŸ¨HasFDerivWithinAt.hasFDerivAt_of_univ, _âŸ© := hasFDerivWithinAt_univ

theorem differentiableWithinAt_univ :
    DifferentiableWithinAt ğ•œ f univ x â†” DifferentiableAt ğ•œ f x := by
  simp only [DifferentiableWithinAt, hasFDerivWithinAt_univ, DifferentiableAt]

theorem fderivWithin_zero_of_not_differentiableWithinAt (h : Â¬DifferentiableWithinAt ğ•œ f s x) :
    fderivWithin ğ•œ f s x = 0 := by
  simp [fderivWithin, h]

theorem fderiv_zero_of_not_differentiableAt (h : Â¬DifferentiableAt ğ•œ f x) : fderiv ğ•œ f x = 0 := by
  rw [fderiv, fderivWithin_zero_of_not_differentiableWithinAt]
  rwa [differentiableWithinAt_univ]

@[simp]
theorem fderivWithin_univ : fderivWithin ğ•œ f univ = fderiv ğ•œ f := by
  ext
  rw [fderiv]

end TVS
