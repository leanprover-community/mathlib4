/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Analysis.Calculus.FDeriv.Comp
import Mathlib.Analysis.Calculus.FDeriv.Const
import Mathlib.Analysis.Calculus.FDeriv.Linear

/-!
# The derivative of a linear equivalence

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
continuous linear equivalences.

We also prove the usual formula for the derivative of the inverse function, assuming it exists.
The inverse function theorem is in `Mathlib/Analysis/Calculus/InverseFunctionTheorem/FDeriv.lean`.
-/

open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f : E â†’ F} {f' : E â†’L[ğ•œ] F} {x : E} {s : Set E} {c : F}

namespace ContinuousLinearEquiv

/-! ### Differentiability of linear equivs, and invariance of differentiability -/


variable (iso : E â‰ƒL[ğ•œ] F)

@[fun_prop]
protected theorem hasStrictFDerivAt : HasStrictFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  iso.toContinuousLinearMap.hasStrictFDerivAt

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt iso (iso : E â†’L[ğ•œ] F) s x :=
  iso.toContinuousLinearMap.hasFDerivWithinAt

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  iso.toContinuousLinearMap.hasFDerivAtFilter

@[fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ iso x :=
  iso.hasFDerivAt.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ iso s x :=
  iso.differentiableAt.differentiableWithinAt

protected theorem fderiv : fderiv ğ•œ iso x = iso :=
  iso.hasFDerivAt.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ iso s x = iso :=
  iso.toContinuousLinearMap.fderivWithin hxs

@[fun_prop]
protected theorem differentiable : Differentiable ğ•œ iso := fun _ => iso.differentiableAt

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ iso s :=
  iso.differentiable.differentiableOn

theorem comp_differentiableWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} :
    DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x â†” DifferentiableWithinAt ğ•œ f s x := by
  refine âŸ¨fun H => ?_, fun H => iso.differentiable.differentiableAt.comp_differentiableWithinAt x HâŸ©
  have : DifferentiableWithinAt ğ•œ (iso.symm âˆ˜ iso âˆ˜ f) s x :=
    iso.symm.differentiable.differentiableAt.comp_differentiableWithinAt x H
  rwa [â† Function.comp_assoc iso.symm iso f, iso.symm_comp_self] at this

theorem comp_differentiableAt_iff {f : G â†’ E} {x : G} :
    DifferentiableAt ğ•œ (iso âˆ˜ f) x â†” DifferentiableAt ğ•œ f x := by
  rw [â† differentiableWithinAt_univ, â† differentiableWithinAt_univ,
    iso.comp_differentiableWithinAt_iff]

theorem comp_differentiableOn_iff {f : G â†’ E} {s : Set G} :
    DifferentiableOn ğ•œ (iso âˆ˜ f) s â†” DifferentiableOn ğ•œ f s := by
  rw [DifferentiableOn, DifferentiableOn]
  simp only [iso.comp_differentiableWithinAt_iff]

theorem comp_differentiable_iff {f : G â†’ E} : Differentiable ğ•œ (iso âˆ˜ f) â†” Differentiable ğ•œ f := by
  rw [â† differentiableOn_univ, â† differentiableOn_univ]
  exact iso.comp_differentiableOn_iff

theorem comp_hasFDerivWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivWithinAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” HasFDerivWithinAt f f' s x := by
  refine âŸ¨fun H => ?_, fun H => iso.hasFDerivAt.comp_hasFDerivWithinAt x HâŸ©
  simpa [Function.comp_def, â† ContinuousLinearMap.comp_assoc]
    using iso.symm.hasFDerivAt.comp_hasFDerivWithinAt x H

theorem comp_hasStrictFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasStrictFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasStrictFDerivAt f f' x := by
  refine âŸ¨fun H => ?_, fun H => iso.hasStrictFDerivAt.comp x HâŸ©
  convert iso.symm.hasStrictFDerivAt.comp x H using 1 <;>
    ext z <;> apply (iso.symm_apply_apply _).symm

theorem comp_hasFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasFDerivAt f f' x := by
  simp_rw [â† hasFDerivWithinAt_univ, iso.comp_hasFDerivWithinAt_iff]

theorem comp_hasFDerivWithinAt_iff' {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivWithinAt (iso âˆ˜ f) f' s x â†”
      HasFDerivWithinAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x := by
  rw [â† iso.comp_hasFDerivWithinAt_iff, â† ContinuousLinearMap.comp_assoc, iso.coe_comp_coe_symm,
    ContinuousLinearMap.id_comp]

theorem comp_hasFDerivAt_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivAt (iso âˆ˜ f) f' x â†” HasFDerivAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') x := by
  simp_rw [â† hasFDerivWithinAt_univ, iso.comp_hasFDerivWithinAt_iff']

theorem comp_fderivWithin {f : G â†’ E} {s : Set G} {x : G} (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderivWithin ğ•œ f s x) := by
  by_cases h : DifferentiableWithinAt ğ•œ f s x
  Â· rw [fderiv_comp_fderivWithin x iso.differentiableAt h hxs, iso.fderiv]
  Â· have : Â¬DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x := mt iso.comp_differentiableWithinAt_iff.1 h
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt this, ContinuousLinearMap.comp_zero]

theorem comp_fderiv {f : G â†’ E} {x : G} :
    fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) := by
  rw [â† fderivWithin_univ, â† fderivWithin_univ]
  exact iso.comp_fderivWithin uniqueDiffWithinAt_univ

lemma _root_.fderivWithin_continuousLinearEquiv_comp (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G))
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) s x =
      (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderivWithin ğ•œ f s x) := by
  change fderivWithin ğ•œ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L) âˆ˜ f) s x = _
  rw [ContinuousLinearEquiv.comp_fderivWithin _ hs]

lemma _root_.fderiv_continuousLinearEquiv_comp (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G)) (x : E) :
    fderiv ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) x =
      (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderiv ğ•œ f x) := by
  change fderiv ğ•œ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L) âˆ˜ f) x = _
  rw [ContinuousLinearEquiv.comp_fderiv]

lemma _root_.fderiv_continuousLinearEquiv_comp' (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G)) :
    fderiv ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) =
      fun x â†¦ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderiv ğ•œ f x) := by
  ext x : 1
  exact fderiv_continuousLinearEquiv_comp L f x

theorem comp_right_differentiableWithinAt_iff {f : F â†’ G} {s : Set F} {x : E} :
    DifferentiableWithinAt ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x â†” DifferentiableWithinAt ğ•œ f s (iso x) := by
  refine âŸ¨fun H => ?_, fun H => H.comp x iso.differentiableWithinAt (mapsTo_preimage _ s)âŸ©
  have : DifferentiableWithinAt ğ•œ ((f âˆ˜ iso) âˆ˜ iso.symm) s (iso x) := by
    rw [â† iso.symm_apply_apply x] at H
    apply H.comp (iso x) iso.symm.differentiableWithinAt
    intro y hy
    simpa only [mem_preimage, apply_symm_apply] using hy
  rwa [Function.comp_assoc, iso.self_comp_symm] at this

theorem comp_right_differentiableAt_iff {f : F â†’ G} {x : E} :
    DifferentiableAt ğ•œ (f âˆ˜ iso) x â†” DifferentiableAt ğ•œ f (iso x) := by
  simp only [â† differentiableWithinAt_univ, â† iso.comp_right_differentiableWithinAt_iff,
    preimage_univ]

theorem comp_right_differentiableOn_iff {f : F â†’ G} {s : Set F} :
    DifferentiableOn ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) â†” DifferentiableOn ğ•œ f s := by
  refine âŸ¨fun H y hy => ?_, fun H y hy => iso.comp_right_differentiableWithinAt_iff.2 (H _ hy)âŸ©
  rw [â† iso.apply_symm_apply y, â† comp_right_differentiableWithinAt_iff]
  apply H
  simpa only [mem_preimage, apply_symm_apply] using hy

theorem comp_right_differentiable_iff {f : F â†’ G} :
    Differentiable ğ•œ (f âˆ˜ iso) â†” Differentiable ğ•œ f := by
  simp only [â† differentiableOn_univ, â† iso.comp_right_differentiableOn_iff, preimage_univ]

theorem comp_right_hasFDerivWithinAt_iff {f : F â†’ G} {s : Set F} {x : E} {f' : F â†’L[ğ•œ] G} :
    HasFDerivWithinAt (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) (iso â»Â¹' s) x â†”
      HasFDerivWithinAt f f' s (iso x) := by
  refine âŸ¨fun H => ?_, fun H => H.comp x iso.hasFDerivWithinAt (mapsTo_preimage _ s)âŸ©
  rw [â† iso.symm_apply_apply x] at H
  have A : f = (f âˆ˜ iso) âˆ˜ iso.symm := by
    rw [Function.comp_assoc, iso.self_comp_symm]
    rfl
  have B : f' = (f'.comp (iso : E â†’L[ğ•œ] F)).comp (iso.symm : F â†’L[ğ•œ] E) := by
    rw [ContinuousLinearMap.comp_assoc, iso.coe_comp_coe_symm, ContinuousLinearMap.comp_id]
  rw [A, B]
  apply H.comp (iso x) iso.symm.hasFDerivWithinAt
  intro y hy
  simpa only [mem_preimage, apply_symm_apply] using hy

theorem comp_right_hasFDerivAt_iff {f : F â†’ G} {x : E} {f' : F â†’L[ğ•œ] G} :
    HasFDerivAt (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) x â†” HasFDerivAt f f' (iso x) := by
  simp only [â† hasFDerivWithinAt_univ, â† comp_right_hasFDerivWithinAt_iff, preimage_univ]

theorem comp_right_hasFDerivWithinAt_iff' {f : F â†’ G} {s : Set F} {x : E} {f' : E â†’L[ğ•œ] G} :
    HasFDerivWithinAt (f âˆ˜ iso) f' (iso â»Â¹' s) x â†”
      HasFDerivWithinAt f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) s (iso x) := by
  rw [â† iso.comp_right_hasFDerivWithinAt_iff, ContinuousLinearMap.comp_assoc,
    iso.coe_symm_comp_coe, ContinuousLinearMap.comp_id]

theorem comp_right_hasFDerivAt_iff' {f : F â†’ G} {x : E} {f' : E â†’L[ğ•œ] G} :
    HasFDerivAt (f âˆ˜ iso) f' x â†” HasFDerivAt f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) (iso x) := by
  simp only [â† hasFDerivWithinAt_univ, â† iso.comp_right_hasFDerivWithinAt_iff', preimage_univ]

theorem comp_right_fderivWithin {f : F â†’ G} {s : Set F} {x : E}
    (hxs : UniqueDiffWithinAt ğ•œ (iso â»Â¹' s) x) :
    fderivWithin ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x =
      (fderivWithin ğ•œ f s (iso x)).comp (iso : E â†’L[ğ•œ] F) := by
  by_cases h : DifferentiableWithinAt ğ•œ f s (iso x)
  Â· exact (iso.comp_right_hasFDerivWithinAt_iff.2 h.hasFDerivWithinAt).fderivWithin hxs
  Â· have : Â¬DifferentiableWithinAt ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x := by
      intro h'
      exact h (iso.comp_right_differentiableWithinAt_iff.1 h')
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt this, ContinuousLinearMap.zero_comp]

theorem comp_right_fderiv {f : F â†’ G} {x : E} :
    fderiv ğ•œ (f âˆ˜ iso) x = (fderiv ğ•œ f (iso x)).comp (iso : E â†’L[ğ•œ] F) := by
  rw [â† fderivWithin_univ, â† fderivWithin_univ, â† iso.comp_right_fderivWithin, preimage_univ]
  exact uniqueDiffWithinAt_univ

end ContinuousLinearEquiv

namespace LinearIsometryEquiv

/-! ### Differentiability of linear isometry equivs, and invariance of differentiability -/


variable (iso : E â‰ƒâ‚—áµ¢[ğ•œ] F)

@[fun_prop]
protected theorem hasStrictFDerivAt : HasStrictFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  (iso : E â‰ƒL[ğ•œ] F).hasStrictFDerivAt

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt iso (iso : E â†’L[ğ•œ] F) s x :=
  (iso : E â‰ƒL[ğ•œ] F).hasFDerivWithinAt

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  (iso : E â‰ƒL[ğ•œ] F).hasFDerivAt

@[fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ iso x :=
  iso.hasFDerivAt.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ iso s x :=
  iso.differentiableAt.differentiableWithinAt

protected theorem fderiv : fderiv ğ•œ iso x = iso :=
  iso.hasFDerivAt.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ iso s x = iso :=
  (iso : E â‰ƒL[ğ•œ] F).fderivWithin hxs

@[fun_prop]
protected theorem differentiable : Differentiable ğ•œ iso := fun _ => iso.differentiableAt

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ iso s :=
  iso.differentiable.differentiableOn

theorem comp_differentiableWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} :
    DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableWithinAt_iff

theorem comp_differentiableAt_iff {f : G â†’ E} {x : G} :
    DifferentiableAt ğ•œ (iso âˆ˜ f) x â†” DifferentiableAt ğ•œ f x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableAt_iff

theorem comp_differentiableOn_iff {f : G â†’ E} {s : Set G} :
    DifferentiableOn ğ•œ (iso âˆ˜ f) s â†” DifferentiableOn ğ•œ f s :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableOn_iff

theorem comp_differentiable_iff {f : G â†’ E} : Differentiable ğ•œ (iso âˆ˜ f) â†” Differentiable ğ•œ f :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiable_iff

theorem comp_hasFDerivWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivWithinAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” HasFDerivWithinAt f f' s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivWithinAt_iff

theorem comp_hasStrictFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasStrictFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasStrictFDerivAt f f' x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasStrictFDerivAt_iff

theorem comp_hasFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasFDerivAt f f' x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivAt_iff

theorem comp_hasFDerivWithinAt_iff' {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivWithinAt (iso âˆ˜ f) f' s x â†” HasFDerivWithinAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivWithinAt_iff'

theorem comp_hasFDerivAt_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivAt (iso âˆ˜ f) f' x â†” HasFDerivAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivAt_iff'

theorem comp_fderivWithin {f : G â†’ E} {s : Set G} {x : G} (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderivWithin ğ•œ f s x) :=
  (iso : E â‰ƒL[ğ•œ] F).comp_fderivWithin hxs

theorem comp_fderiv {f : G â†’ E} {x : G} :
    fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
  (iso : E â‰ƒL[ğ•œ] F).comp_fderiv

theorem comp_fderiv' {f : G â†’ E} :
    fderiv ğ•œ (iso âˆ˜ f) = fun x â†¦ (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) := by
  ext x : 1
  exact LinearIsometryEquiv.comp_fderiv iso

end LinearIsometryEquiv

/-- If `f (g y) = y` for `y` in a neighborhood of `a` within `t`,
`g` maps a neighborhood of `a` within `t` to a neighborhood of `g a` within `s`,
and `f` has an invertible derivative `f'` at `g a` within `s`,
then `g` has the derivative `f'â»Â¹` at `a` within `t`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasFDerivWithinAt.of_local_left_inverse {g : F â†’ E} {f' : E â‰ƒL[ğ•œ] F} {a : F} {t : Set F}
    (hg : Tendsto g (ğ“[t] a) (ğ“[s] (g a))) (hf : HasFDerivWithinAt f (f' : E â†’L[ğ•œ] F) s (g a))
    (ha : a âˆˆ t) (hfg : âˆ€á¶  y in ğ“[t] a, f (g y) = y) :
    HasFDerivWithinAt g (f'.symm : F â†’L[ğ•œ] E) t a := by
  have : (fun x : F => g x - g a - f'.symm (x - a)) =O[ğ“[t] a]
      fun x : F => f' (g x - g a) - (x - a) :=
    ((f'.symm : F â†’L[ğ•œ] E).isBigO_comp _ _).congr (fun x â†¦ by simp) fun _ â†¦ rfl
  refine .of_isLittleO <| this.trans_isLittleO ?_
  clear this
  refine ((hf.isLittleO.comp_tendsto hg).symm.congr' (hfg.mono ?_) .rfl).trans_isBigO ?_
  Â· intro p hp
    simp [hp, hfg.self_of_nhdsWithin ha]
  Â· refine ((hf.isBigO_sub_rev f'.antilipschitz).comp_tendsto hg).congr'
      (Eventually.of_forall fun _ => rfl) (hfg.mono ?_)
    rintro p hp
    simp only [(Â· âˆ˜ Â·), hp, hfg.self_of_nhdsWithin ha]

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasStrictFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasStrictFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  replace hg := hg.prodMap' hg
  replace hfg := hfg.prodMk_nhds hfg
  have :
    (fun p : F Ã— F => g p.1 - g p.2 - f'.symm (p.1 - p.2)) =O[ğ“ (a, a)] fun p : F Ã— F =>
      f' (g p.1 - g p.2) - (p.1 - p.2) := by
    refine ((f'.symm : F â†’L[ğ•œ] E).isBigO_comp _ _).congr (fun x => ?_) fun _ => rfl
    simp
  refine .of_isLittleO <| this.trans_isLittleO ?_
  clear this
  refine ((hf.isLittleO.comp_tendsto hg).symm.congr'
    (hfg.mono ?_) (Eventually.of_forall fun _ => rfl)).trans_isBigO ?_
  Â· rintro p âŸ¨hp1, hp2âŸ©
    simp [hp1, hp2]
  Â· refine (hf.isBigO_sub_rev.comp_tendsto hg).congr' (Eventually.of_forall fun _ => rfl)
      (hfg.mono ?_)
    rintro p âŸ¨hp1, hp2âŸ©
    simp only [(Â· âˆ˜ Â·), hp1, hp2, Prod.map]

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem HasFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  simp only [â† hasFDerivWithinAt_univ, â† nhdsWithin_univ] at hf hfg âŠ¢
  exact hf.of_local_left_inverse (.inf hg (by simp)) (mem_univ _) hfg

/-- If `f` is a partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` in the sense of strict differentiability at `f.symm a`, then `f.symm` has
the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.hasStrictFDerivAt_symm (f : PartialHomeomorph E F) {f' : E â‰ƒL[ğ•œ] F}
    {a : F} (ha : a âˆˆ f.target) (htff' : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasStrictFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)

/-- If `f` is a partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.hasFDerivAt_symm (f : PartialHomeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
    (ha : a âˆˆ f.target) (htff' : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)

theorem HasFDerivWithinAt.eventually_ne (h : HasFDerivWithinAt f f' s x)
    (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) : âˆ€á¶  z in ğ“[s \ {x}] x, f z â‰  c := by
  rcases eq_or_ne (f x) c with rfl | hc
  Â· rw [nhdsWithin, diff_eq, â† inf_principal, â† inf_assoc, eventually_inf_principal]
    have A : (fun z => z - x) =O[ğ“[s] x] fun z => f' (z - x) :=
      isBigO_iff.2 <| hf'.imp fun C hC => Eventually.of_forall fun z => hC _
    have : (fun z => f z - f x) ~[ğ“[s] x] fun z => f' (z - x) := h.isLittleO.trans_isBigO A
    simpa [not_imp_not, sub_eq_zero] using (A.trans this.isBigO_symm).eq_zero_imp
  Â· exact (h.continuousWithinAt.eventually_ne hc).filter_mono <| by gcongr; apply diff_subset

theorem HasFDerivAt.eventually_ne (h : HasFDerivAt f f' x) (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) :
    âˆ€á¶  z in ğ“[â‰ ] x, f z â‰  c := by
  simpa only [compl_eq_univ_diff] using (hasFDerivWithinAt_univ.2 h).eventually_ne hf'

end

section

/-
  In the special case of a normed space over the reals,
  we can use scalar multiplication in the `tendsto` characterization
  of the FrÃ©chet derivative.
-/
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
variable {f : E â†’ F} {f' : E â†’L[â„] F} {x : E}

theorem has_fderiv_at_filter_real_equiv {L : Filter E} :
    Tendsto (fun x' : E => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) L (ğ“ 0) â†”
      Tendsto (fun x' : E => â€–x' - xâ€–â»Â¹ â€¢ (f x' - f x - f' (x' - x))) L (ğ“ 0) := by
  symm
  rw [tendsto_iff_norm_sub_tendsto_zero]
  refine tendsto_congr fun x' => ?_
  simp [norm_smul]

theorem HasFDerivAt.lim_real (hf : HasFDerivAt f f' x) (v : E) :
    Tendsto (fun c : â„ => c â€¢ (f (x + câ»Â¹ â€¢ v) - f x)) atTop (ğ“ (f' v)) := by
  apply hf.lim v
  rw [tendsto_atTop_atTop]
  exact fun b => âŸ¨b, fun a ha => le_trans ha (le_abs_self _)âŸ©

end

open scoped Pointwise

section TangentCone

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F} {s : Set E}
  {f' : E â†’L[ğ•œ] F} {x : E}

/-- The image of a tangent cone under the differential of a map is included in the tangent cone to
the image. -/
theorem HasFDerivWithinAt.mapsTo_tangent_cone (h : HasFDerivWithinAt f f' s x) :
    MapsTo f' (tangentConeAt ğ•œ s x) (tangentConeAt ğ•œ (f '' s) (f x)) := by
  rintro v âŸ¨c, d, dtop, clim, cdlimâŸ©
  refine
    âŸ¨c, fun n => f (x + d n) - f x, mem_of_superset dtop ?_, clim, h.lim atTop dtop clim cdlimâŸ©
  simp +contextual [-mem_image, mem_image_of_mem]

/-- If a set has the unique differentiability property at a point x, then the image of this set
under a map with onto derivative has also the unique differentiability property at the image point.
-/
theorem HasFDerivWithinAt.uniqueDiffWithinAt (h : HasFDerivWithinAt f f' s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) (h' : DenseRange f') : UniqueDiffWithinAt ğ•œ (f '' s) (f x) := by
  refine âŸ¨h'.dense_of_mapsTo f'.continuous hs.1 ?_, h.continuousWithinAt.mem_closure_image hs.2âŸ©
  show
    Submodule.span ğ•œ (tangentConeAt ğ•œ s x) â‰¤
      (Submodule.span ğ•œ (tangentConeAt ğ•œ (f '' s) (f x))).comap f'
  rw [Submodule.span_le]
  exact h.mapsTo_tangent_cone.mono Subset.rfl Submodule.subset_span

theorem UniqueDiffOn.image {f' : E â†’ E â†’L[ğ•œ] F} (hs : UniqueDiffOn ğ•œ s)
    (hf' : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (hd : âˆ€ x âˆˆ s, DenseRange (f' x)) :
    UniqueDiffOn ğ•œ (f '' s) :=
  forall_mem_image.2 fun x hx => (hf' x hx).uniqueDiffWithinAt (hs x hx) (hd x hx)

theorem HasFDerivWithinAt.uniqueDiffWithinAt_of_continuousLinearEquiv (e' : E â‰ƒL[ğ•œ] F)
    (h : HasFDerivWithinAt f (e' : E â†’L[ğ•œ] F) s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ (f '' s) (f x) :=
  h.uniqueDiffWithinAt hs e'.surjective.denseRange

theorem ContinuousLinearEquiv.uniqueDiffOn_image (e : E â‰ƒL[ğ•œ] F) (h : UniqueDiffOn ğ•œ s) :
    UniqueDiffOn ğ•œ (e '' s) :=
  h.image (fun _ _ => e.hasFDerivWithinAt) fun _ _ => e.surjective.denseRange

@[simp]
theorem ContinuousLinearEquiv.uniqueDiffOn_image_iff (e : E â‰ƒL[ğ•œ] F) :
    UniqueDiffOn ğ•œ (e '' s) â†” UniqueDiffOn ğ•œ s :=
  âŸ¨fun h => e.symm_image_image s â–¸ e.symm.uniqueDiffOn_image h, e.uniqueDiffOn_imageâŸ©

@[simp]
theorem ContinuousLinearEquiv.uniqueDiffOn_preimage_iff (e : F â‰ƒL[ğ•œ] E) :
    UniqueDiffOn ğ•œ (e â»Â¹' s) â†” UniqueDiffOn ğ•œ s := by
  rw [â† e.image_symm_eq_preimage, e.symm.uniqueDiffOn_image_iff]

protected theorem UniqueDiffWithinAt.smul (h : UniqueDiffWithinAt ğ•œ s x) {c : ğ•œ} (hc : c â‰  0) :
    UniqueDiffWithinAt ğ•œ (c â€¢ s) (c â€¢ x) :=
  (ContinuousLinearEquiv.smulLeft <| Units.mk0 c hc).hasFDerivWithinAt
    |>.uniqueDiffWithinAt_of_continuousLinearEquiv _ h

protected theorem UniqueDiffWithinAt.smul_iff {c : ğ•œ} (hc : c â‰  0) :
    UniqueDiffWithinAt ğ•œ (c â€¢ s) (c â€¢ x) â†” UniqueDiffWithinAt ğ•œ s x :=
  âŸ¨fun h â†¦ by simpa [hc] using h.smul (inv_ne_zero hc), (.smul Â· hc)âŸ©

end TangentCone

section SMulLeft

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F} {s : Set E}
  {f' : E â†’L[ğ•œ] F} {x : E}

theorem fderivWithin_const_smul_field {R : Type*} [DivisionRing R] [Module R F]
    [SMulCommClass ğ•œ R F] [ContinuousConstSMul R F] (c : R) (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (c â€¢ f) s x = c â€¢ fderivWithin ğ•œ f s x := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· lift c to RË£ using IsUnit.mk0 _ hc
    have : SMulCommClass RË£ ğ•œ F := .symm _ _ _
    exact (ContinuousLinearEquiv.smulLeft c).comp_fderivWithin hs

theorem hasFDerivWithinAt_comp_smul_smul_iff {c : ğ•œ} :
    HasFDerivWithinAt (f <| c â€¢ Â·) (c â€¢ f') s x â†” HasFDerivWithinAt f f' (c â€¢ s) (c â€¢ x) := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp [hasFDerivWithinAt_const, HasFDerivWithinAt.of_subsingleton (subsingleton_zero_smul_set _)]
  Â· lift c to ğ•œË£ using IsUnit.mk0 c hc
    have A : f'.comp ((ContinuousLinearEquiv.smulLeft c : E â‰ƒL[ğ•œ] E) : E â†’L[ğ•œ] E) = c â€¢ f' := by
      ext; simp
    rw [â† Units.smul_def c x, â† ContinuousLinearEquiv.smulLeft_apply_apply (Râ‚ := ğ•œ),
      â† ContinuousLinearEquiv.comp_right_hasFDerivWithinAt_iff, A]
    simp [Function.comp_def, â† Units.smul_def, â† preimage_smul_inv, preimage_preimage]

theorem hasFDerivWithinAt_comp_smul_iff_smul {c : ğ•œ} (hc : c â‰  0) :
    HasFDerivWithinAt (f <| c â€¢ Â·) f' s x â†” HasFDerivWithinAt (c â€¢ f) f' (c â€¢ s) (c â€¢ x) := by
  simp only [â† hasFDerivWithinAt_comp_smul_smul_iff, Pi.smul_apply]
  lift c to ğ•œË£ using IsUnit.mk0 c hc
  exact (ContinuousLinearEquiv.smulLeft c).comp_hasFDerivWithinAt_iff.symm

theorem fderivWithin_comp_smul_eq_fderivWithin_smul (c : ğ•œ) :
    fderivWithin ğ•œ (f <| c â€¢ Â·) s x = fderivWithin ğ•œ (c â€¢ f) (c â€¢ s) (c â€¢ x) := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· classical
    simp only [fderivWithin, DifferentiableWithinAt, hasFDerivWithinAt_comp_smul_iff_smul hc]

theorem fderivWithin_comp_smul (c : ğ•œ) (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (f <| c â€¢ Â·) s x = c â€¢ fderivWithin ğ•œ f (c â€¢ s) (c â€¢ x) := by
  rcases eq_or_ne c 0 with rfl | hc
  Â· simp
  Â· rw [fderivWithin_comp_smul_eq_fderivWithin_smul, fderivWithin_const_smul_field]
    exact hs.smul hc

theorem fderiv_comp_smul (c : ğ•œ) : fderiv ğ•œ (f <| c â€¢ Â·) x = c â€¢ fderiv ğ•œ f (c â€¢ x) := by
  rw [â† fderivWithin_univ, fderivWithin_comp_smul _ uniqueDiffWithinAt_univ]
  rcases eq_or_ne c 0 with rfl | hc <;> simp [smul_set_univâ‚€, *]

end SMulLeft
