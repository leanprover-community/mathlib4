/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Analysis.Calculus.FDeriv.Linear
import Mathlib.Analysis.Calculus.FDeriv.Comp

#align_import analysis.calculus.fderiv.equiv from "leanprover-community/mathlib"@"e3fb84046afd187b710170887195d50bada934ee"

/-!
# The derivative of a linear equivalence

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
continuous linear equivalences.

We also prove the usual formula for the derivative of the inverse function, assuming it exists.
The inverse function theorem is in `Mathlib/Analysis/Calculus/InverseFunctionTheorem/FDeriv.lean`.
-/

open Filter Asymptotics ContinuousLinearMap Set Metric

open scoped Classical
open Topology NNReal Filter Asymptotics ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f fâ‚€ fâ‚ g : E â†’ F}
variable {f' fâ‚€' fâ‚' g' : E â†’L[ğ•œ] F}
variable (e : E â†’L[ğ•œ] F)
variable {x : E}
variable {s t : Set E}
variable {L Lâ‚ Lâ‚‚ : Filter E}

namespace ContinuousLinearEquiv

/-! ### Differentiability of linear equivs, and invariance of differentiability -/


variable (iso : E â‰ƒL[ğ•œ] F)

@[fun_prop]
protected theorem hasStrictFDerivAt : HasStrictFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  iso.toContinuousLinearMap.hasStrictFDerivAt
#align continuous_linear_equiv.has_strict_fderiv_at ContinuousLinearEquiv.hasStrictFDerivAt

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt iso (iso : E â†’L[ğ•œ] F) s x :=
  iso.toContinuousLinearMap.hasFDerivWithinAt
#align continuous_linear_equiv.has_fderiv_within_at ContinuousLinearEquiv.hasFDerivWithinAt

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  iso.toContinuousLinearMap.hasFDerivAtFilter
#align continuous_linear_equiv.has_fderiv_at ContinuousLinearEquiv.hasFDerivAt

@[fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ iso x :=
  iso.hasFDerivAt.differentiableAt
#align continuous_linear_equiv.differentiable_at ContinuousLinearEquiv.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ iso s x :=
  iso.differentiableAt.differentiableWithinAt
#align continuous_linear_equiv.differentiable_within_at ContinuousLinearEquiv.differentiableWithinAt

protected theorem fderiv : fderiv ğ•œ iso x = iso :=
  iso.hasFDerivAt.fderiv
#align continuous_linear_equiv.fderiv ContinuousLinearEquiv.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ iso s x = iso :=
  iso.toContinuousLinearMap.fderivWithin hxs
#align continuous_linear_equiv.fderiv_within ContinuousLinearEquiv.fderivWithin

@[fun_prop]
protected theorem differentiable : Differentiable ğ•œ iso := fun _ => iso.differentiableAt
#align continuous_linear_equiv.differentiable ContinuousLinearEquiv.differentiable

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ iso s :=
  iso.differentiable.differentiableOn
#align continuous_linear_equiv.differentiable_on ContinuousLinearEquiv.differentiableOn

theorem comp_differentiableWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} :
    DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x â†” DifferentiableWithinAt ğ•œ f s x := by
  refine
    âŸ¨fun H => ?_, fun H => iso.differentiable.differentiableAt.comp_differentiableWithinAt x HâŸ©
  have : DifferentiableWithinAt ğ•œ (iso.symm âˆ˜ iso âˆ˜ f) s x :=
    iso.symm.differentiable.differentiableAt.comp_differentiableWithinAt x H
  rwa [â† Function.comp.assoc iso.symm iso f, iso.symm_comp_self] at this
#align continuous_linear_equiv.comp_differentiable_within_at_iff ContinuousLinearEquiv.comp_differentiableWithinAt_iff

theorem comp_differentiableAt_iff {f : G â†’ E} {x : G} :
    DifferentiableAt ğ•œ (iso âˆ˜ f) x â†” DifferentiableAt ğ•œ f x := by
  rw [â† differentiableWithinAt_univ, â† differentiableWithinAt_univ,
    iso.comp_differentiableWithinAt_iff]
#align continuous_linear_equiv.comp_differentiable_at_iff ContinuousLinearEquiv.comp_differentiableAt_iff

theorem comp_differentiableOn_iff {f : G â†’ E} {s : Set G} :
    DifferentiableOn ğ•œ (iso âˆ˜ f) s â†” DifferentiableOn ğ•œ f s := by
  rw [DifferentiableOn, DifferentiableOn]
  simp only [iso.comp_differentiableWithinAt_iff]
#align continuous_linear_equiv.comp_differentiable_on_iff ContinuousLinearEquiv.comp_differentiableOn_iff

theorem comp_differentiable_iff {f : G â†’ E} : Differentiable ğ•œ (iso âˆ˜ f) â†” Differentiable ğ•œ f := by
  rw [â† differentiableOn_univ, â† differentiableOn_univ]
  exact iso.comp_differentiableOn_iff
#align continuous_linear_equiv.comp_differentiable_iff ContinuousLinearEquiv.comp_differentiable_iff

theorem comp_hasFDerivWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivWithinAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” HasFDerivWithinAt f f' s x := by
  refine âŸ¨fun H => ?_, fun H => iso.hasFDerivAt.comp_hasFDerivWithinAt x HâŸ©
  have A : f = iso.symm âˆ˜ iso âˆ˜ f := by
    rw [â† Function.comp.assoc, iso.symm_comp_self]
    rfl
  have B : f' = (iso.symm : F â†’L[ğ•œ] E).comp ((iso : E â†’L[ğ•œ] F).comp f') := by
    rw [â† ContinuousLinearMap.comp_assoc, iso.coe_symm_comp_coe, ContinuousLinearMap.id_comp]
  rw [A, B]
  exact iso.symm.hasFDerivAt.comp_hasFDerivWithinAt x H
#align continuous_linear_equiv.comp_has_fderiv_within_at_iff ContinuousLinearEquiv.comp_hasFDerivWithinAt_iff

theorem comp_hasStrictFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasStrictFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasStrictFDerivAt f f' x := by
  refine âŸ¨fun H => ?_, fun H => iso.hasStrictFDerivAt.comp x HâŸ©
  convert iso.symm.hasStrictFDerivAt.comp x H using 1 <;>
    ext z <;> apply (iso.symm_apply_apply _).symm
#align continuous_linear_equiv.comp_has_strict_fderiv_at_iff ContinuousLinearEquiv.comp_hasStrictFDerivAt_iff

theorem comp_hasFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasFDerivAt f f' x := by
  simp_rw [â† hasFDerivWithinAt_univ, iso.comp_hasFDerivWithinAt_iff]
#align continuous_linear_equiv.comp_has_fderiv_at_iff ContinuousLinearEquiv.comp_hasFDerivAt_iff

theorem comp_hasFDerivWithinAt_iff' {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivWithinAt (iso âˆ˜ f) f' s x â†”
      HasFDerivWithinAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x := by
  rw [â† iso.comp_hasFDerivWithinAt_iff, â† ContinuousLinearMap.comp_assoc, iso.coe_comp_coe_symm,
    ContinuousLinearMap.id_comp]
#align continuous_linear_equiv.comp_has_fderiv_within_at_iff' ContinuousLinearEquiv.comp_hasFDerivWithinAt_iff'

theorem comp_hasFDerivAt_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivAt (iso âˆ˜ f) f' x â†” HasFDerivAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') x := by
  simp_rw [â† hasFDerivWithinAt_univ, iso.comp_hasFDerivWithinAt_iff']
#align continuous_linear_equiv.comp_has_fderiv_at_iff' ContinuousLinearEquiv.comp_hasFDerivAt_iff'

theorem comp_fderivWithin {f : G â†’ E} {s : Set G} {x : G} (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderivWithin ğ•œ f s x) := by
  by_cases h : DifferentiableWithinAt ğ•œ f s x
  Â· rw [fderiv.comp_fderivWithin x iso.differentiableAt h hxs, iso.fderiv]
  Â· have : Â¬DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x := mt iso.comp_differentiableWithinAt_iff.1 h
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt this, ContinuousLinearMap.comp_zero]
#align continuous_linear_equiv.comp_fderiv_within ContinuousLinearEquiv.comp_fderivWithin

theorem comp_fderiv {f : G â†’ E} {x : G} :
    fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) := by
  rw [â† fderivWithin_univ, â† fderivWithin_univ]
  exact iso.comp_fderivWithin uniqueDiffWithinAt_univ
#align continuous_linear_equiv.comp_fderiv ContinuousLinearEquiv.comp_fderiv

lemma _root_.fderivWithin_continuousLinearEquiv_comp (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G))
    (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) s x =
      (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderivWithin ğ•œ f s x) := by
  change fderivWithin ğ•œ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L) âˆ˜ f) s x = _
  rw [ContinuousLinearEquiv.comp_fderivWithin _ hs]

lemma _root_.fderiv_continuousLinearEquiv_comp (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G)) (x : E) :
    fderiv ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) x =
      (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderiv ğ•œ f x) := by
  change fderiv ğ•œ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L) âˆ˜ f) x = _
  rw [ContinuousLinearEquiv.comp_fderiv]

lemma _root_.fderiv_continuousLinearEquiv_comp' (L : G â‰ƒL[ğ•œ] G') (f : E â†’ (F â†’L[ğ•œ] G)) :
    fderiv ğ•œ (fun x â†¦ (L : G â†’L[ğ•œ] G').comp (f x)) =
      fun x â†¦ (((ContinuousLinearEquiv.refl ğ•œ F).arrowCongr L)) âˆ˜L (fderiv ğ•œ f x) := by
  ext x : 1
  exact fderiv_continuousLinearEquiv_comp L f x

theorem comp_right_differentiableWithinAt_iff {f : F â†’ G} {s : Set F} {x : E} :
    DifferentiableWithinAt ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x â†” DifferentiableWithinAt ğ•œ f s (iso x) := by
  refine âŸ¨fun H => ?_, fun H => H.comp x iso.differentiableWithinAt (mapsTo_preimage _ s)âŸ©
  have : DifferentiableWithinAt ğ•œ ((f âˆ˜ iso) âˆ˜ iso.symm) s (iso x) := by
    rw [â† iso.symm_apply_apply x] at H
    apply H.comp (iso x) iso.symm.differentiableWithinAt
    intro y hy
    simpa only [mem_preimage, apply_symm_apply] using hy
  rwa [Function.comp.assoc, iso.self_comp_symm] at this
#align continuous_linear_equiv.comp_right_differentiable_within_at_iff ContinuousLinearEquiv.comp_right_differentiableWithinAt_iff

theorem comp_right_differentiableAt_iff {f : F â†’ G} {x : E} :
    DifferentiableAt ğ•œ (f âˆ˜ iso) x â†” DifferentiableAt ğ•œ f (iso x) := by
  simp only [â† differentiableWithinAt_univ, â† iso.comp_right_differentiableWithinAt_iff,
    preimage_univ]
#align continuous_linear_equiv.comp_right_differentiable_at_iff ContinuousLinearEquiv.comp_right_differentiableAt_iff

theorem comp_right_differentiableOn_iff {f : F â†’ G} {s : Set F} :
    DifferentiableOn ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) â†” DifferentiableOn ğ•œ f s := by
  refine âŸ¨fun H y hy => ?_, fun H y hy => iso.comp_right_differentiableWithinAt_iff.2 (H _ hy)âŸ©
  rw [â† iso.apply_symm_apply y, â† comp_right_differentiableWithinAt_iff]
  apply H
  simpa only [mem_preimage, apply_symm_apply] using hy
#align continuous_linear_equiv.comp_right_differentiable_on_iff ContinuousLinearEquiv.comp_right_differentiableOn_iff

theorem comp_right_differentiable_iff {f : F â†’ G} :
    Differentiable ğ•œ (f âˆ˜ iso) â†” Differentiable ğ•œ f := by
  simp only [â† differentiableOn_univ, â† iso.comp_right_differentiableOn_iff, preimage_univ]
#align continuous_linear_equiv.comp_right_differentiable_iff ContinuousLinearEquiv.comp_right_differentiable_iff

theorem comp_right_hasFDerivWithinAt_iff {f : F â†’ G} {s : Set F} {x : E} {f' : F â†’L[ğ•œ] G} :
    HasFDerivWithinAt (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) (iso â»Â¹' s) x â†”
      HasFDerivWithinAt f f' s (iso x) := by
  refine âŸ¨fun H => ?_, fun H => H.comp x iso.hasFDerivWithinAt (mapsTo_preimage _ s)âŸ©
  rw [â† iso.symm_apply_apply x] at H
  have A : f = (f âˆ˜ iso) âˆ˜ iso.symm := by
    rw [Function.comp.assoc, iso.self_comp_symm]
    rfl
  have B : f' = (f'.comp (iso : E â†’L[ğ•œ] F)).comp (iso.symm : F â†’L[ğ•œ] E) := by
    rw [ContinuousLinearMap.comp_assoc, iso.coe_comp_coe_symm, ContinuousLinearMap.comp_id]
  rw [A, B]
  apply H.comp (iso x) iso.symm.hasFDerivWithinAt
  intro y hy
  simpa only [mem_preimage, apply_symm_apply] using hy
#align continuous_linear_equiv.comp_right_has_fderiv_within_at_iff ContinuousLinearEquiv.comp_right_hasFDerivWithinAt_iff

theorem comp_right_hasFDerivAt_iff {f : F â†’ G} {x : E} {f' : F â†’L[ğ•œ] G} :
    HasFDerivAt (f âˆ˜ iso) (f'.comp (iso : E â†’L[ğ•œ] F)) x â†” HasFDerivAt f f' (iso x) := by
  simp only [â† hasFDerivWithinAt_univ, â† comp_right_hasFDerivWithinAt_iff, preimage_univ]
#align continuous_linear_equiv.comp_right_has_fderiv_at_iff ContinuousLinearEquiv.comp_right_hasFDerivAt_iff

theorem comp_right_hasFDerivWithinAt_iff' {f : F â†’ G} {s : Set F} {x : E} {f' : E â†’L[ğ•œ] G} :
    HasFDerivWithinAt (f âˆ˜ iso) f' (iso â»Â¹' s) x â†”
      HasFDerivWithinAt f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) s (iso x) := by
  rw [â† iso.comp_right_hasFDerivWithinAt_iff, ContinuousLinearMap.comp_assoc,
    iso.coe_symm_comp_coe, ContinuousLinearMap.comp_id]
#align continuous_linear_equiv.comp_right_has_fderiv_within_at_iff' ContinuousLinearEquiv.comp_right_hasFDerivWithinAt_iff'

theorem comp_right_hasFDerivAt_iff' {f : F â†’ G} {x : E} {f' : E â†’L[ğ•œ] G} :
    HasFDerivAt (f âˆ˜ iso) f' x â†” HasFDerivAt f (f'.comp (iso.symm : F â†’L[ğ•œ] E)) (iso x) := by
  simp only [â† hasFDerivWithinAt_univ, â† iso.comp_right_hasFDerivWithinAt_iff', preimage_univ]
#align continuous_linear_equiv.comp_right_has_fderiv_at_iff' ContinuousLinearEquiv.comp_right_hasFDerivAt_iff'

theorem comp_right_fderivWithin {f : F â†’ G} {s : Set F} {x : E}
    (hxs : UniqueDiffWithinAt ğ•œ (iso â»Â¹' s) x) :
    fderivWithin ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x =
      (fderivWithin ğ•œ f s (iso x)).comp (iso : E â†’L[ğ•œ] F) := by
  by_cases h : DifferentiableWithinAt ğ•œ f s (iso x)
  Â· exact (iso.comp_right_hasFDerivWithinAt_iff.2 h.hasFDerivWithinAt).fderivWithin hxs
  Â· have : Â¬DifferentiableWithinAt ğ•œ (f âˆ˜ iso) (iso â»Â¹' s) x := by
      intro h'
      exact h (iso.comp_right_differentiableWithinAt_iff.1 h')
    rw [fderivWithin_zero_of_not_differentiableWithinAt h,
      fderivWithin_zero_of_not_differentiableWithinAt this, ContinuousLinearMap.zero_comp]
#align continuous_linear_equiv.comp_right_fderiv_within ContinuousLinearEquiv.comp_right_fderivWithin

theorem comp_right_fderiv {f : F â†’ G} {x : E} :
    fderiv ğ•œ (f âˆ˜ iso) x = (fderiv ğ•œ f (iso x)).comp (iso : E â†’L[ğ•œ] F) := by
  rw [â† fderivWithin_univ, â† fderivWithin_univ, â† iso.comp_right_fderivWithin, preimage_univ]
  exact uniqueDiffWithinAt_univ
#align continuous_linear_equiv.comp_right_fderiv ContinuousLinearEquiv.comp_right_fderiv

end ContinuousLinearEquiv

namespace LinearIsometryEquiv

/-! ### Differentiability of linear isometry equivs, and invariance of differentiability -/


variable (iso : E â‰ƒâ‚—áµ¢[ğ•œ] F)

@[fun_prop]
protected theorem hasStrictFDerivAt : HasStrictFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  (iso : E â‰ƒL[ğ•œ] F).hasStrictFDerivAt
#align linear_isometry_equiv.has_strict_fderiv_at LinearIsometryEquiv.hasStrictFDerivAt

@[fun_prop]
protected theorem hasFDerivWithinAt : HasFDerivWithinAt iso (iso : E â†’L[ğ•œ] F) s x :=
  (iso : E â‰ƒL[ğ•œ] F).hasFDerivWithinAt
#align linear_isometry_equiv.has_fderiv_within_at LinearIsometryEquiv.hasFDerivWithinAt

@[fun_prop]
protected theorem hasFDerivAt : HasFDerivAt iso (iso : E â†’L[ğ•œ] F) x :=
  (iso : E â‰ƒL[ğ•œ] F).hasFDerivAt
#align linear_isometry_equiv.has_fderiv_at LinearIsometryEquiv.hasFDerivAt

@[fun_prop]
protected theorem differentiableAt : DifferentiableAt ğ•œ iso x :=
  iso.hasFDerivAt.differentiableAt
#align linear_isometry_equiv.differentiable_at LinearIsometryEquiv.differentiableAt

@[fun_prop]
protected theorem differentiableWithinAt : DifferentiableWithinAt ğ•œ iso s x :=
  iso.differentiableAt.differentiableWithinAt
#align linear_isometry_equiv.differentiable_within_at LinearIsometryEquiv.differentiableWithinAt

protected theorem fderiv : fderiv ğ•œ iso x = iso :=
  iso.hasFDerivAt.fderiv
#align linear_isometry_equiv.fderiv LinearIsometryEquiv.fderiv

protected theorem fderivWithin (hxs : UniqueDiffWithinAt ğ•œ s x) : fderivWithin ğ•œ iso s x = iso :=
  (iso : E â‰ƒL[ğ•œ] F).fderivWithin hxs
#align linear_isometry_equiv.fderiv_within LinearIsometryEquiv.fderivWithin

@[fun_prop]
protected theorem differentiable : Differentiable ğ•œ iso := fun _ => iso.differentiableAt
#align linear_isometry_equiv.differentiable LinearIsometryEquiv.differentiable

@[fun_prop]
protected theorem differentiableOn : DifferentiableOn ğ•œ iso s :=
  iso.differentiable.differentiableOn
#align linear_isometry_equiv.differentiable_on LinearIsometryEquiv.differentiableOn

theorem comp_differentiableWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} :
    DifferentiableWithinAt ğ•œ (iso âˆ˜ f) s x â†” DifferentiableWithinAt ğ•œ f s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableWithinAt_iff
#align linear_isometry_equiv.comp_differentiable_within_at_iff LinearIsometryEquiv.comp_differentiableWithinAt_iff

theorem comp_differentiableAt_iff {f : G â†’ E} {x : G} :
    DifferentiableAt ğ•œ (iso âˆ˜ f) x â†” DifferentiableAt ğ•œ f x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableAt_iff
#align linear_isometry_equiv.comp_differentiable_at_iff LinearIsometryEquiv.comp_differentiableAt_iff

theorem comp_differentiableOn_iff {f : G â†’ E} {s : Set G} :
    DifferentiableOn ğ•œ (iso âˆ˜ f) s â†” DifferentiableOn ğ•œ f s :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiableOn_iff
#align linear_isometry_equiv.comp_differentiable_on_iff LinearIsometryEquiv.comp_differentiableOn_iff

theorem comp_differentiable_iff {f : G â†’ E} : Differentiable ğ•œ (iso âˆ˜ f) â†” Differentiable ğ•œ f :=
  (iso : E â‰ƒL[ğ•œ] F).comp_differentiable_iff
#align linear_isometry_equiv.comp_differentiable_iff LinearIsometryEquiv.comp_differentiable_iff

theorem comp_hasFDerivWithinAt_iff {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivWithinAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') s x â†” HasFDerivWithinAt f f' s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivWithinAt_iff
#align linear_isometry_equiv.comp_has_fderiv_within_at_iff LinearIsometryEquiv.comp_hasFDerivWithinAt_iff

theorem comp_hasStrictFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasStrictFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasStrictFDerivAt f f' x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasStrictFDerivAt_iff
#align linear_isometry_equiv.comp_has_strict_fderiv_at_iff LinearIsometryEquiv.comp_hasStrictFDerivAt_iff

theorem comp_hasFDerivAt_iff {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] E} :
    HasFDerivAt (iso âˆ˜ f) ((iso : E â†’L[ğ•œ] F).comp f') x â†” HasFDerivAt f f' x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivAt_iff
#align linear_isometry_equiv.comp_has_fderiv_at_iff LinearIsometryEquiv.comp_hasFDerivAt_iff

theorem comp_hasFDerivWithinAt_iff' {f : G â†’ E} {s : Set G} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivWithinAt (iso âˆ˜ f) f' s x â†” HasFDerivWithinAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') s x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivWithinAt_iff'
#align linear_isometry_equiv.comp_has_fderiv_within_at_iff' LinearIsometryEquiv.comp_hasFDerivWithinAt_iff'

theorem comp_hasFDerivAt_iff' {f : G â†’ E} {x : G} {f' : G â†’L[ğ•œ] F} :
    HasFDerivAt (iso âˆ˜ f) f' x â†” HasFDerivAt f ((iso.symm : F â†’L[ğ•œ] E).comp f') x :=
  (iso : E â‰ƒL[ğ•œ] F).comp_hasFDerivAt_iff'
#align linear_isometry_equiv.comp_has_fderiv_at_iff' LinearIsometryEquiv.comp_hasFDerivAt_iff'

theorem comp_fderivWithin {f : G â†’ E} {s : Set G} {x : G} (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (iso âˆ˜ f) s x = (iso : E â†’L[ğ•œ] F).comp (fderivWithin ğ•œ f s x) :=
  (iso : E â‰ƒL[ğ•œ] F).comp_fderivWithin hxs
#align linear_isometry_equiv.comp_fderiv_within LinearIsometryEquiv.comp_fderivWithin

theorem comp_fderiv {f : G â†’ E} {x : G} :
    fderiv ğ•œ (iso âˆ˜ f) x = (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) :=
  (iso : E â‰ƒL[ğ•œ] F).comp_fderiv
#align linear_isometry_equiv.comp_fderiv LinearIsometryEquiv.comp_fderiv

theorem comp_fderiv' {f : G â†’ E} :
    fderiv ğ•œ (iso âˆ˜ f) = fun x â†¦ (iso : E â†’L[ğ•œ] F).comp (fderiv ğ•œ f x) := by
  ext x : 1
  exact LinearIsometryEquiv.comp_fderiv iso

end LinearIsometryEquiv

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a` in the strict sense, then `g` has the derivative `f'â»Â¹` at `a`
in the strict sense.

This is one of the easy parts of the inverse function theorem: it assumes that we already have an
inverse function. -/
theorem HasStrictFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasStrictFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  replace hg := hg.prod_map' hg
  replace hfg := hfg.prod_mk_nhds hfg
  have :
    (fun p : F Ã— F => g p.1 - g p.2 - f'.symm (p.1 - p.2)) =O[ğ“ (a, a)] fun p : F Ã— F =>
      f' (g p.1 - g p.2) - (p.1 - p.2) := by
    refine ((f'.symm : F â†’L[ğ•œ] E).isBigO_comp _ _).congr (fun x => ?_) fun _ => rfl
    simp
  refine this.trans_isLittleO ?_
  clear this
  refine ((hf.comp_tendsto hg).symm.congr'
    (hfg.mono ?_) (eventually_of_forall fun _ => rfl)).trans_isBigO ?_
  Â· rintro p âŸ¨hp1, hp2âŸ©
    simp [hp1, hp2]
  Â· refine (hf.isBigO_sub_rev.comp_tendsto hg).congr' (eventually_of_forall fun _ => rfl)
      (hfg.mono ?_)
    rintro p âŸ¨hp1, hp2âŸ©
    simp only [(Â· âˆ˜ Â·), hp1, hp2]
#align has_strict_fderiv_at.of_local_left_inverse HasStrictFDerivAt.of_local_left_inverse

/-- If `f (g y) = y` for `y` in some neighborhood of `a`, `g` is continuous at `a`, and `f` has an
invertible derivative `f'` at `g a`, then `g` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem HasFDerivAt.of_local_left_inverse {f : E â†’ F} {f' : E â‰ƒL[ğ•œ] F} {g : F â†’ E} {a : F}
    (hg : ContinuousAt g a) (hf : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (g a))
    (hfg : âˆ€á¶  y in ğ“ a, f (g y) = y) : HasFDerivAt g (f'.symm : F â†’L[ğ•œ] E) a := by
  have : (fun x : F => g x - g a - f'.symm (x - a)) =O[ğ“ a]
      fun x : F => f' (g x - g a) - (x - a) := by
    refine ((f'.symm : F â†’L[ğ•œ] E).isBigO_comp _ _).congr (fun x => ?_) fun _ => rfl
    simp
  refine HasFDerivAtFilter.of_isLittleO <| this.trans_isLittleO ?_
  clear this
  refine ((hf.isLittleO.comp_tendsto hg).symm.congr' (hfg.mono ?_) .rfl).trans_isBigO ?_
  Â· intro p hp
    simp [hp, hfg.self_of_nhds]
  Â· refine ((hf.isBigO_sub_rev f'.antilipschitz).comp_tendsto hg).congr'
      (eventually_of_forall fun _ => rfl) (hfg.mono ?_)
    rintro p hp
    simp only [(Â· âˆ˜ Â·), hp, hfg.self_of_nhds]
#align has_fderiv_at.of_local_left_inverse HasFDerivAt.of_local_left_inverse

/-- If `f` is a partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` in the sense of strict differentiability at `f.symm a`, then `f.symm` has
the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.hasStrictFDerivAt_symm (f : PartialHomeomorph E F) {f' : E â‰ƒL[ğ•œ] F}
    {a : F} (ha : a âˆˆ f.target) (htff' : HasStrictFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasStrictFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)
#align local_homeomorph.has_strict_fderiv_at_symm PartialHomeomorph.hasStrictFDerivAt_symm

/-- If `f` is a partial homeomorphism defined on a neighbourhood of `f.symm a`, and `f` has an
invertible derivative `f'` at `f.symm a`, then `f.symm` has the derivative `f'â»Â¹` at `a`.

This is one of the easy parts of the inverse function theorem: it assumes that we already have
an inverse function. -/
theorem PartialHomeomorph.hasFDerivAt_symm (f : PartialHomeomorph E F) {f' : E â‰ƒL[ğ•œ] F} {a : F}
    (ha : a âˆˆ f.target) (htff' : HasFDerivAt f (f' : E â†’L[ğ•œ] F) (f.symm a)) :
    HasFDerivAt f.symm (f'.symm : F â†’L[ğ•œ] E) a :=
  htff'.of_local_left_inverse (f.symm.continuousAt ha) (f.eventually_right_inverse ha)
#align local_homeomorph.has_fderiv_at_symm PartialHomeomorph.hasFDerivAt_symm

theorem HasFDerivWithinAt.eventually_ne (h : HasFDerivWithinAt f f' s x)
    (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) : âˆ€á¶  z in ğ“[s \ {x}] x, f z â‰  f x := by
  rw [nhdsWithin, diff_eq, â† inf_principal, â† inf_assoc, eventually_inf_principal]
  have A : (fun z => z - x) =O[ğ“[s] x] fun z => f' (z - x) :=
    isBigO_iff.2 <| hf'.imp fun C hC => eventually_of_forall fun z => hC _
  have : (fun z => f z - f x) ~[ğ“[s] x] fun z => f' (z - x) := h.isLittleO.trans_isBigO A
  simpa [not_imp_not, sub_eq_zero] using (A.trans this.isBigO_symm).eq_zero_imp
#align has_fderiv_within_at.eventually_ne HasFDerivWithinAt.eventually_ne

theorem HasFDerivAt.eventually_ne (h : HasFDerivAt f f' x) (hf' : âˆƒ C, âˆ€ z, â€–zâ€– â‰¤ C * â€–f' zâ€–) :
    âˆ€á¶  z in ğ“[â‰ ] x, f z â‰  f x := by
  simpa only [compl_eq_univ_diff] using (hasFDerivWithinAt_univ.2 h).eventually_ne hf'
#align has_fderiv_at.eventually_ne HasFDerivAt.eventually_ne

end

section

/-
  In the special case of a normed space over the reals,
  we can use scalar multiplication in the `tendsto` characterization
  of the FrÃ©chet derivative.
-/
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
variable {f : E â†’ F} {f' : E â†’L[â„] F} {x : E}

theorem has_fderiv_at_filter_real_equiv {L : Filter E} :
    Tendsto (fun x' : E => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - f' (x' - x)â€–) L (ğ“ 0) â†”
      Tendsto (fun x' : E => â€–x' - xâ€–â»Â¹ â€¢ (f x' - f x - f' (x' - x))) L (ğ“ 0) := by
  symm
  rw [tendsto_iff_norm_sub_tendsto_zero]
  refine tendsto_congr fun x' => ?_
  simp [norm_smul]
#align has_fderiv_at_filter_real_equiv has_fderiv_at_filter_real_equiv

theorem HasFDerivAt.lim_real (hf : HasFDerivAt f f' x) (v : E) :
    Tendsto (fun c : â„ => c â€¢ (f (x + câ»Â¹ â€¢ v) - f x)) atTop (ğ“ (f' v)) := by
  apply hf.lim v
  rw [tendsto_atTop_atTop]
  exact fun b => âŸ¨b, fun a ha => le_trans ha (le_abs_self _)âŸ©
#align has_fderiv_at.lim_real HasFDerivAt.lim_real

end

section TangentCone

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {f : E â†’ F} {s : Set E}
  {f' : E â†’L[ğ•œ] F}

/-- The image of a tangent cone under the differential of a map is included in the tangent cone to
the image. -/
theorem HasFDerivWithinAt.mapsTo_tangent_cone {x : E} (h : HasFDerivWithinAt f f' s x) :
    MapsTo f' (tangentConeAt ğ•œ s x) (tangentConeAt ğ•œ (f '' s) (f x)) := by
  rintro v âŸ¨c, d, dtop, clim, cdlimâŸ©
  refine
    âŸ¨c, fun n => f (x + d n) - f x, mem_of_superset dtop ?_, clim, h.lim atTop dtop clim cdlimâŸ©
  simp (config := { contextual := true }) [-mem_image, mem_image_of_mem]
#align has_fderiv_within_at.maps_to_tangent_cone HasFDerivWithinAt.mapsTo_tangent_cone

/-- If a set has the unique differentiability property at a point x, then the image of this set
under a map with onto derivative has also the unique differentiability property at the image point.
-/
theorem HasFDerivWithinAt.uniqueDiffWithinAt {x : E} (h : HasFDerivWithinAt f f' s x)
    (hs : UniqueDiffWithinAt ğ•œ s x) (h' : DenseRange f') : UniqueDiffWithinAt ğ•œ (f '' s) (f x) := by
  refine âŸ¨h'.dense_of_mapsTo f'.continuous hs.1 ?_, h.continuousWithinAt.mem_closure_image hs.2âŸ©
  show
    Submodule.span ğ•œ (tangentConeAt ğ•œ s x) â‰¤
      (Submodule.span ğ•œ (tangentConeAt ğ•œ (f '' s) (f x))).comap f'
  rw [Submodule.span_le]
  exact h.mapsTo_tangent_cone.mono Subset.rfl Submodule.subset_span
#align has_fderiv_within_at.unique_diff_within_at HasFDerivWithinAt.uniqueDiffWithinAt

theorem UniqueDiffOn.image {f' : E â†’ E â†’L[ğ•œ] F} (hs : UniqueDiffOn ğ•œ s)
    (hf' : âˆ€ x âˆˆ s, HasFDerivWithinAt f (f' x) s x) (hd : âˆ€ x âˆˆ s, DenseRange (f' x)) :
    UniqueDiffOn ğ•œ (f '' s) :=
  forall_mem_image.2 fun x hx => (hf' x hx).uniqueDiffWithinAt (hs x hx) (hd x hx)
#align unique_diff_on.image UniqueDiffOn.image

theorem HasFDerivWithinAt.uniqueDiffWithinAt_of_continuousLinearEquiv {x : E} (e' : E â‰ƒL[ğ•œ] F)
    (h : HasFDerivWithinAt f (e' : E â†’L[ğ•œ] F) s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    UniqueDiffWithinAt ğ•œ (f '' s) (f x) :=
  h.uniqueDiffWithinAt hs e'.surjective.denseRange
#align has_fderiv_within_at.unique_diff_within_at_of_continuous_linear_equiv HasFDerivWithinAt.uniqueDiffWithinAt_of_continuousLinearEquiv

theorem ContinuousLinearEquiv.uniqueDiffOn_image (e : E â‰ƒL[ğ•œ] F) (h : UniqueDiffOn ğ•œ s) :
    UniqueDiffOn ğ•œ (e '' s) :=
  h.image (fun _ _ => e.hasFDerivWithinAt) fun _ _ => e.surjective.denseRange
#align continuous_linear_equiv.unique_diff_on_image ContinuousLinearEquiv.uniqueDiffOn_image

@[simp]
theorem ContinuousLinearEquiv.uniqueDiffOn_image_iff (e : E â‰ƒL[ğ•œ] F) :
    UniqueDiffOn ğ•œ (e '' s) â†” UniqueDiffOn ğ•œ s :=
  âŸ¨fun h => e.symm_image_image s â–¸ e.symm.uniqueDiffOn_image h, e.uniqueDiffOn_imageâŸ©
#align continuous_linear_equiv.unique_diff_on_image_iff ContinuousLinearEquiv.uniqueDiffOn_image_iff

@[simp]
theorem ContinuousLinearEquiv.uniqueDiffOn_preimage_iff (e : F â‰ƒL[ğ•œ] E) :
    UniqueDiffOn ğ•œ (e â»Â¹' s) â†” UniqueDiffOn ğ•œ s := by
  rw [â† e.image_symm_eq_preimage, e.symm.uniqueDiffOn_image_iff]
#align continuous_linear_equiv.unique_diff_on_preimage_iff ContinuousLinearEquiv.uniqueDiffOn_preimage_iff

end TangentCone
