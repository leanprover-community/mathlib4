/-
Copyright (c) 2026 A Tucker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: A Tucker
-/
module

public import Mathlib.Analysis.Calculus.Implicit

/-!
# Implicit function theorem

We consider the common case of bivariate `f`, the second of whose partial derivatives is invertible.
Then we may apply the general theorem to obtain `Ïˆ` such that for `(vâ‚, vâ‚‚)` in a neighbourhood of
`(uâ‚, uâ‚‚)` we have `f (vâ‚, vâ‚‚) = f (uâ‚, uâ‚‚) â†” Ïˆ vâ‚ = vâ‚‚`.

## Tags

implicit function, inverse function
-/

@[expose] public section ProdDomain

/-!
### Case of a product space domain

Given strictly differentiable `f : Eâ‚ Ã— Eâ‚‚ â†’ F`, the second of whose partial derivatives is known to
be invertible, we may construct an `ImplicitFunctionData ğ•œ (Eâ‚ Ã— Eâ‚‚) F Eâ‚` with `f` as its `leftFun`
and `Prod.fst : Eâ‚ Ã— Eâ‚‚ â†’ Eâ‚` as its `rightFun`. We may then extract `Ïˆ : Eâ‚ â†’ Eâ‚‚` with the desired
properties. This functionality is wrapped by `HasStrictFDerivAt.implicitFunctionOfProdDomain`. A
formula for the first derivative of `Ïˆ` is immediately derived.
-/

open Filter
open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {Eâ‚ : Type*} [NormedAddCommGroup Eâ‚] [NormedSpace ğ•œ Eâ‚] [CompleteSpace Eâ‚]
  {Eâ‚‚ : Type*} [NormedAddCommGroup Eâ‚‚] [NormedSpace ğ•œ Eâ‚‚] [CompleteSpace Eâ‚‚]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F]

namespace HasStrictFDerivAt

/-- Given `HasStrictFDerivAt f f' u` with `f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F` decomposing into `fâ‚ : Eâ‚ â†’L[ğ•œ] F`
and `fâ‚‚ : Eâ‚‚ â†’L[ğ•œ] F`, the latter invertible, we prove that the kernels of `f'` and
`ContinuousLinearMap.fst ğ•œ Eâ‚ Eâ‚‚` are complementary and construct an object of type
`ImplicitFunctionData` thereby permitting use of the general machinery provided above. -/
def implicitFunctionDataOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    ImplicitFunctionData ğ•œ (Eâ‚ Ã— Eâ‚‚) F Eâ‚ where
  leftFun := f
  rightFun := Prod.fst
  pt := u
  leftDeriv := f'
  hasStrictFDerivAt_leftFun := dfu
  rightDeriv := .fst ğ•œ Eâ‚ Eâ‚‚
  hasStrictFDerivAt_rightFun := hasStrictFDerivAt_fst
  range_leftDeriv := by
    have : (f' âˆ˜L .inr _ _ _).range â‰¤ f'.range := LinearMap.range_comp_le_range _ _
    rwa [LinearMap.range_eq_top.mpr ifâ‚‚.surjective, top_le_iff] at this
  range_rightDeriv := Submodule.range_fst
  isCompl_ker := by
    constructor
    Â· rw [LinearMap.disjoint_ker]
      intro (_, y) h rfl
      simpa using (injective_iff_map_eq_zero _).mp ifâ‚‚.injective y h
    Â· rw [Submodule.codisjoint_iff_exists_add_eq]
      intro v
      obtain âŸ¨y, hyâŸ© := ifâ‚‚.surjective (f' v)
      use v - (0, y), (0, y)
      aesop

/-- Implicit function `Ïˆ : Eâ‚ â†’ Eâ‚‚` associated with the (uncurried) bivariate function
`f : Eâ‚ Ã— Eâ‚‚ â†’ F` at `u`. -/
noncomputable def implicitFunctionOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    Eâ‚ â†’ Eâ‚‚ :=
  fun x => ((dfu.implicitFunctionDataOfProdDomain ifâ‚‚).implicitFunction (f u) x).2

theorem hasStrictFDerivAt_implicitFunctionOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    HasStrictFDerivAt (dfu.implicitFunctionOfProdDomain ifâ‚‚)
      (-(f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).inverse âˆ˜L (f' âˆ˜L .inl ğ•œ Eâ‚ Eâ‚‚)) u.1 := by
  have : f' âˆ˜L (.prod (.id _ _) (-(f' âˆ˜L .inr _ _ _).inverse âˆ˜L (f' âˆ˜L .inl _ _ _))) = 0 := by
    ext x
    rw [f'.comp_apply, â† f'.comp_inl_add_comp_inr]
    simp [map_neg, ifâ‚‚]
  exact ((dfu.implicitFunctionDataOfProdDomain ifâ‚‚).hasStrictFDerivAt_implicitFunction _
    (ContinuousLinearMap.fst_comp_prod _ _) this).snd

theorem image_eq_iff_implicitFunctionOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  v in ğ“ u, f v = f u â†” dfu.implicitFunctionOfProdDomain ifâ‚‚ v.1 = v.2 := by
  let Ï† := dfu.implicitFunctionDataOfProdDomain ifâ‚‚
  filter_upwards [Ï†.leftFun_eq_iff_implicitFunction, Ï†.rightFun_implicitFunction] with y h h'
  exact Iff.trans h âŸ¨congrArg _, by aesopâŸ©

theorem tendsto_implicitFunctionOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    Tendsto (dfu.implicitFunctionOfProdDomain ifâ‚‚) (ğ“ u.1) (ğ“ u.2) := by
  have := (dfu.hasStrictFDerivAt_implicitFunctionOfProdDomain ifâ‚‚).continuousAt.tendsto
  rwa [(dfu.image_eq_iff_implicitFunctionOfProdDomain ifâ‚‚).self_of_nhds.mp rfl] at this

theorem image_implicitFunctionOfProdDomain {f : Eâ‚ Ã— Eâ‚‚ â†’ F} {f' : Eâ‚ Ã— Eâ‚‚ â†’L[ğ•œ] F}
    {u : Eâ‚ Ã— Eâ‚‚} (dfu : HasStrictFDerivAt f f' u) (ifâ‚‚ : (f' âˆ˜L .inr ğ•œ Eâ‚ Eâ‚‚).IsInvertible) :
    âˆ€á¶  x in ğ“ u.1, f (x, dfu.implicitFunctionOfProdDomain ifâ‚‚ x) = f u := by
  have hÏˆ := dfu.tendsto_implicitFunctionOfProdDomain ifâ‚‚
  set Ïˆ := dfu.implicitFunctionOfProdDomain ifâ‚‚
  suffices âˆ€á¶  x in ğ“ u.1, f (x, Ïˆ x) = f u â†” Ïˆ x = Ïˆ x by simpa
  apply Eventually.image_of_prod (r := fun x y => f (x, y) = f u â†” Ïˆ x = y) hÏˆ
  rw [â† nhds_prod_eq]
  exact dfu.image_eq_iff_implicitFunctionOfProdDomain ifâ‚‚

end HasStrictFDerivAt

end ProdDomain
