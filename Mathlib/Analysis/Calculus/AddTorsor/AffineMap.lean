/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Topology.Algebra.ContinuousAffineMap
import Mathlib.Analysis.Normed.Group.AddTorsor

/-!
# Smooth affine maps

This file contains results about smoothness of affine maps.

## Main definitions:

* `ContinuousAffineMap.contDiff`: a continuous affine map is smooth

-/
namespace ContinuousAffineMap

variable {ð•œ V W : Type*} [NontriviallyNormedField ð•œ]
variable [NormedAddCommGroup V] [NormedSpace ð•œ V]
variable [NormedAddCommGroup W] [NormedSpace ð•œ W]

/-- A continuous affine map between normed vector spaces is smooth. -/
theorem contDiff {n : WithTop â„•âˆž} (f : V â†’á´¬[ð•œ] W) : ContDiff ð•œ n f := by
  rw [f.decomp]
  apply f.contLinear.contDiff.add
  exact contDiff_const

theorem differentiable (f : V â†’á´¬[ð•œ] W) : Differentiable ð•œ f :=
  f.contDiff.differentiable le_rfl

theorem differentiableAt (f : V â†’á´¬[ð•œ] W) {x : V} : DifferentiableAt ð•œ f x :=
  f.differentiable x

theorem differentiableOn (f : V â†’á´¬[ð•œ] W) {s : Set V} : DifferentiableOn ð•œ f s :=
  f.differentiable.differentiableOn

theorem differentiableWithinAt (f : V â†’á´¬[ð•œ] W) {s : Set V} {x : V} :
    DifferentiableWithinAt ð•œ f s x :=
  f.differentiableAt.differentiableWithinAt

@[simp] theorem fderiv (f : V â†’á´¬[ð•œ] W) {x : V} : fderiv ð•œ f x = f.contLinear := by
  conv_lhs => rw [f.decomp]
  rw [fderiv_add f.contLinear.differentiableAt]; swap
  Â· exact differentiableAt_const _
  simp only [fderiv_const, Pi.zero_apply, add_zero, ContinuousLinearMap.fderiv]

end ContinuousAffineMap
