/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FDeriv.Prod
import Mathlib.Analysis.Calculus.InverseFunctionTheorem.FDeriv
import Mathlib.LinearAlgebra.Dual.Defs

/-!
# Lagrange multipliers

In this file we formalize the
[Lagrange multipliers](https://en.wikipedia.org/wiki/Lagrange_multiplier) method of solving
conditional extremum problems: if a function `Ï†` has a local extremum at `xâ‚€` on the set
`f â»Â¹' {f xâ‚€}`, `f x = (fâ‚€ x, ..., fâ‚™â‚‹â‚ x)`, then the differentials of `fâ‚–` and `Ï†` are linearly
dependent. First we formulate a geometric version of this theorem which does not rely on the
target space being `â„â¿`, then restate it in terms of coordinates.

## TODO

Formalize Karush-Kuhn-Tucker theorem

## Tags

lagrange multiplier, local extremum

-/


open Filter Set

open scoped Topology Filter

variable {E F : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [CompleteSpace E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [CompleteSpace F] {f : E â†’ F} {Ï† : E â†’ â„} {xâ‚€ : E}
  {f' : E â†’L[â„] F} {Ï†' : E â†’L[â„] â„}

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, both `f : E â†’ F` and `Ï†` are strictly differentiable at `xâ‚€`, and the codomain of `f` is
a complete space, then the linear map `x â†¦ (f' x, Ï†' x)` is not surjective. -/
theorem IsLocalExtrOn.range_ne_top_of_hasStrictFDerivAt
    (hextr : IsLocalExtrOn Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : HasStrictFDerivAt f f' xâ‚€)
    (hÏ†' : HasStrictFDerivAt Ï† Ï†' xâ‚€) : LinearMap.range (f'.prod Ï†') â‰  âŠ¤ := by
  intro htop
  set fÏ† := fun x => (f x, Ï† x)
  have A : map Ï† (ğ“[f â»Â¹' {f xâ‚€}] xâ‚€) = ğ“ (Ï† xâ‚€) := by
    change map (Prod.snd âˆ˜ fÏ†) (ğ“[fÏ† â»Â¹' {p | p.1 = f xâ‚€}] xâ‚€) = ğ“ (Ï† xâ‚€)
    rw [â† map_map, nhdsWithin, map_inf_principal_preimage,
      (hf'.prodMk hÏ†').map_nhds_eq_of_surj htop]
    exact map_snd_nhdsWithin _
  exact hextr.not_nhds_le_map A.ge

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, both `f : E â†’ F` and `Ï†` are strictly differentiable at `xâ‚€`, and the codomain of `f` is
a complete space, then there exist `Î› : dual â„ F` and `Î›â‚€ : â„` such that `(Î›, Î›â‚€) â‰  0` and
`Î› (f' x) + Î›â‚€ â€¢ Ï†' x = 0` for all `x`. -/
theorem IsLocalExtrOn.exists_linear_map_of_hasStrictFDerivAt
    (hextr : IsLocalExtrOn Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : HasStrictFDerivAt f f' xâ‚€)
    (hÏ†' : HasStrictFDerivAt Ï† Ï†' xâ‚€) :
    âˆƒ (Î› : Module.Dual â„ F) (Î›â‚€ : â„), (Î›, Î›â‚€) â‰  0 âˆ§ âˆ€ x, Î› (f' x) + Î›â‚€ â€¢ Ï†' x = 0 := by
  rcases Submodule.exists_le_ker_of_lt_top _
      (lt_top_iff_ne_top.2 <| hextr.range_ne_top_of_hasStrictFDerivAt hf' hÏ†') with
    âŸ¨Î›', h0, hÎ›'âŸ©
  set e : ((F â†’â‚—[â„] â„) Ã— â„) â‰ƒâ‚—[â„] F Ã— â„ â†’â‚—[â„] â„ :=
    ((LinearEquiv.refl â„ (F â†’â‚—[â„] â„)).prodCongr (LinearMap.ringLmapEquivSelf â„ â„ â„).symm).trans
      (LinearMap.coprodEquiv â„)
  rcases e.surjective Î›' with âŸ¨âŸ¨Î›, Î›â‚€âŸ©, rflâŸ©
  refine âŸ¨Î›, Î›â‚€, e.map_ne_zero_iff.1 h0, fun x => ?_âŸ©
  convert LinearMap.congr_fun (LinearMap.range_le_ker_iff.1 hÎ›') x using 1
  -- squeezed `simp [mul_comm]` to speed up elaboration
  simp only [e, smul_eq_mul, LinearEquiv.trans_apply, LinearEquiv.prodCongr_apply,
    LinearEquiv.refl_apply, LinearMap.ringLmapEquivSelf_symm_apply, LinearMap.coprodEquiv_apply,
    ContinuousLinearMap.coe_prod, LinearMap.coprod_comp_prod, LinearMap.add_apply,
    LinearMap.coe_comp, ContinuousLinearMap.coe_coe, Function.comp_apply, LinearMap.coe_smulRight,
    Module.End.one_apply, mul_comm]

/-- Lagrange multipliers theorem: if `Ï† : E â†’ â„` has a local extremum on the set `{x | f x = f xâ‚€}`
at `xâ‚€`, and both `f : E â†’ â„` and `Ï†` are strictly differentiable at `xâ‚€`, then there exist
`a b : â„` such that `(a, b) â‰  0` and `a â€¢ f' + b â€¢ Ï†' = 0`. -/
theorem IsLocalExtrOn.exists_multipliers_of_hasStrictFDerivAt_1d {f : E â†’ â„} {f' : E â†’L[â„] â„}
    (hextr : IsLocalExtrOn Ï† {x | f x = f xâ‚€} xâ‚€) (hf' : HasStrictFDerivAt f f' xâ‚€)
    (hÏ†' : HasStrictFDerivAt Ï† Ï†' xâ‚€) : âˆƒ a b : â„, (a, b) â‰  0 âˆ§ a â€¢ f' + b â€¢ Ï†' = 0 := by
  obtain âŸ¨Î›, Î›â‚€, hÎ›, hfÎ›âŸ© := hextr.exists_linear_map_of_hasStrictFDerivAt hf' hÏ†'
  refine âŸ¨Î› 1, Î›â‚€, ?_, ?_âŸ©
  Â· contrapose! hÎ›
    simp only [Prod.mk_eq_zero] at hÎ› âŠ¢
    refine âŸ¨LinearMap.ext fun x => ?_, hÎ›.2âŸ©
    simpa [hÎ›.1] using Î›.map_smul x 1
  Â· ext x
    have Hâ‚ : Î› (f' x) = f' x * Î› 1 := by
      simpa only [mul_one, Algebra.id.smul_eq_mul] using Î›.map_smul (f' x) 1
    have Hâ‚‚ : f' x * Î› 1 + Î›â‚€ * Ï†' x = 0 := by simpa only [Algebra.id.smul_eq_mul, Hâ‚] using hfÎ› x
    simpa [mul_comm] using Hâ‚‚

/-- Lagrange multipliers theorem, 1d version. Let `f : Î¹ â†’ E â†’ â„` be a finite family of functions.
Suppose that `Ï† : E â†’ â„` has a local extremum on the set `{x | âˆ€ i, f i x = f i xâ‚€}` at `xâ‚€`.
Suppose that all functions `f i` as well as `Ï†` are strictly differentiable at `xâ‚€`.
Then the derivatives `f' i : E â†’ L[â„] â„` and `Ï†' : E â†’L[â„] â„` are linearly dependent:
there exist `Î› : Î¹ â†’ â„` and `Î›â‚€ : â„`, `(Î›, Î›â‚€) â‰  0`, such that `âˆ‘ i, Î› i â€¢ f' i + Î›â‚€ â€¢ Ï†' = 0`.

See also `IsLocalExtrOn.linear_dependent_of_hasStrictFDerivAt` for a version that
states `Â¬LinearIndependent â„ _` instead of existence of `Î›` and `Î›â‚€`. -/
theorem IsLocalExtrOn.exists_multipliers_of_hasStrictFDerivAt {Î¹ : Type*} [Fintype Î¹]
    {f : Î¹ â†’ E â†’ â„} {f' : Î¹ â†’ E â†’L[â„] â„} (hextr : IsLocalExtrOn Ï† {x | âˆ€ i, f i x = f i xâ‚€} xâ‚€)
    (hf' : âˆ€ i, HasStrictFDerivAt (f i) (f' i) xâ‚€) (hÏ†' : HasStrictFDerivAt Ï† Ï†' xâ‚€) :
    âˆƒ (Î› : Î¹ â†’ â„) (Î›â‚€ : â„), (Î›, Î›â‚€) â‰  0 âˆ§ (âˆ‘ i, Î› i â€¢ f' i) + Î›â‚€ â€¢ Ï†' = 0 := by
  letI := Classical.decEq Î¹
  replace hextr : IsLocalExtrOn Ï† {x | (fun i => f i x) = fun i => f i xâ‚€} xâ‚€ := by
    simpa only [funext_iff] using hextr
  rcases hextr.exists_linear_map_of_hasStrictFDerivAt (hasStrictFDerivAt_pi.2 fun i => hf' i)
      hÏ†' with
    âŸ¨Î›, Î›â‚€, h0, hsumâŸ©
  rcases (LinearEquiv.piRing â„ â„ Î¹ â„).symm.surjective Î› with âŸ¨Î›, rflâŸ©
  refine âŸ¨Î›, Î›â‚€, ?_, ?_âŸ©
  Â· simpa only [Ne, Prod.ext_iff, LinearEquiv.map_eq_zero_iff, Prod.fst_zero] using h0
  Â· ext x; simpa [mul_comm] using hsum x

/-- Lagrange multipliers theorem. Let `f : Î¹ â†’ E â†’ â„` be a finite family of functions.
Suppose that `Ï† : E â†’ â„` has a local extremum on the set `{x | âˆ€ i, f i x = f i xâ‚€}` at `xâ‚€`.
Suppose that all functions `f i` as well as `Ï†` are strictly differentiable at `xâ‚€`.
Then the derivatives `f' i : E â†’ L[â„] â„` and `Ï†' : E â†’L[â„] â„` are linearly dependent.

See also `IsLocalExtrOn.exists_multipliers_of_hasStrictFDerivAt` for a version that
that states existence of Lagrange multipliers `Î›` and `Î›â‚€` instead of using
`Â¬LinearIndependent â„ _` -/
theorem IsLocalExtrOn.linear_dependent_of_hasStrictFDerivAt {Î¹ : Type*} [Finite Î¹] {f : Î¹ â†’ E â†’ â„}
    {f' : Î¹ â†’ E â†’L[â„] â„} (hextr : IsLocalExtrOn Ï† {x | âˆ€ i, f i x = f i xâ‚€} xâ‚€)
    (hf' : âˆ€ i, HasStrictFDerivAt (f i) (f' i) xâ‚€) (hÏ†' : HasStrictFDerivAt Ï† Ï†' xâ‚€) :
    Â¬LinearIndependent â„ (Option.elim' Ï†' f' : Option Î¹ â†’ E â†’L[â„] â„) := by
  cases nonempty_fintype Î¹
  rw [Fintype.linearIndependent_iff]; push_neg
  rcases hextr.exists_multipliers_of_hasStrictFDerivAt hf' hÏ†' with âŸ¨Î›, Î›â‚€, hÎ›, hÎ›fâŸ©
  refine âŸ¨Option.elim' Î›â‚€ Î›, ?_, ?_âŸ©
  Â· simpa [add_comm] using hÎ›f
  Â· simpa only [funext_iff, not_and_or, or_comm, Option.exists, Prod.mk_eq_zero, Ne,
      not_forall] using hÎ›
