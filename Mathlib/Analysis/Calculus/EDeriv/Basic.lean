/-
Copyright (c) 2025 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov, Sam Lindauer
-/
import Mathlib.Analysis.NormedSpace.Alternating.Uncurry.Fin
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Calculus.FDeriv.CompCLM

/-!
# Exterior derivative of a differential form

In this file we define the exterior derivative of a differential form.
Under certain smoothness assumptions, we prove that this operation is linear in the form
and the second exterior derivative of a form is zero.
-/

open Filter ContinuousAlternatingMap Set
open scoped Topology

variable {ğ•œ E F : Type*}
  [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {n m k : â„•} {r : WithTop â„•âˆ}
  {Ï‰ Ï‰â‚ Ï‰â‚‚ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F} {s t : Set E} {x : E}

/-- Exterior derivative of a differential form. -/
noncomputable def ederiv (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (x : E) : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  .uncurryFin (fderiv ğ•œ Ï‰ x)

/- Exterior derivative of a differential form within a set. -/
noncomputable def ederivWithin (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (s : Set E) (x : E) :
    E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  .uncurryFin (fderivWithin ğ•œ Ï‰ s x)

@[simp]
theorem ederivWithin_univ (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) :
    ederivWithin Ï‰ univ = ederiv Ï‰ := by
  ext1 x
  rw[ederivWithin, ederiv, fderivWithin_univ]

theorem ederivWithin_add (hsx : UniqueDiffWithinAt ğ•œ s x)
    (hÏ‰â‚ : DifferentiableWithinAt ğ•œ Ï‰â‚ s x) (hÏ‰â‚‚ : DifferentiableWithinAt ğ•œ Ï‰â‚‚ s x) :
    ederivWithin (Ï‰â‚ + Ï‰â‚‚) s x = ederivWithin Ï‰â‚ s x + ederivWithin Ï‰â‚‚ s x := by
  simp [ederivWithin, fderivWithin_add hsx hÏ‰â‚ hÏ‰â‚‚, uncurryFin_add]

theorem ederivWithin_fun_add (hsx : UniqueDiffWithinAt ğ•œ s x)
    (hÏ‰â‚ : DifferentiableWithinAt ğ•œ Ï‰â‚ s x) (hÏ‰â‚‚ : DifferentiableWithinAt ğ•œ Ï‰â‚‚ s x) :
    ederivWithin (fun x â†¦ Ï‰â‚ x + Ï‰â‚‚ x) s x = ederivWithin Ï‰â‚ s x + ederivWithin Ï‰â‚‚ s x :=
  ederivWithin_add hsx hÏ‰â‚ hÏ‰â‚‚

theorem ederiv_add (hÏ‰â‚ : DifferentiableAt ğ•œ Ï‰â‚ x) (hÏ‰â‚‚ : DifferentiableAt ğ•œ Ï‰â‚‚ x) :
    ederiv (Ï‰â‚ + Ï‰â‚‚) x = ederiv Ï‰â‚ x + ederiv Ï‰â‚‚ x := by
  simp [â† ederivWithin_univ, ederivWithin_add, *, DifferentiableAt.differentiableWithinAt]

theorem ederiv_fun_add (hÏ‰â‚ : DifferentiableAt ğ•œ Ï‰â‚ x) (hÏ‰â‚‚ : DifferentiableAt ğ•œ Ï‰â‚‚ x) :
    ederiv (fun x â†¦ Ï‰â‚ x + Ï‰â‚‚ x) x = ederiv Ï‰â‚ x + ederiv Ï‰â‚‚ x :=
  ederiv_add hÏ‰â‚ hÏ‰â‚‚

theorem ederivWithin_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) (hsx : UniqueDiffWithinAt ğ•œ s x) :
    ederivWithin (c â€¢ Ï‰) s x = c â€¢ ederivWithin Ï‰ s x := by
  simp [ederivWithin, fderivWithin_const_smul_of_field, hsx, uncurryFin_smul]

theorem ederivWithin_fun_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F)
    (hsx : UniqueDiffWithinAt ğ•œ s x) :
    ederivWithin (fun x â†¦ c â€¢ Ï‰ x) s x = c â€¢ ederivWithin Ï‰ s x :=
  ederivWithin_smul c Ï‰ hsx

theorem ederiv_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) : ederiv (c â€¢ Ï‰) x = c â€¢ ederiv Ï‰ x := by
  simp [â† ederivWithin_univ, ederivWithin_smul]

theorem ederiv_fun_smul (c : ğ•œ) (Ï‰ : E â†’ E [â‹€^Fin n]â†’L[ğ•œ] F) :
    ederiv (c â€¢ Ï‰) x = c â€¢ ederiv Ï‰ x :=
  ederiv_smul c Ï‰

/-- Exterior derivative of a 0-form given by a function `f` within a set
is the 1-form given by the derivative of `f` within the set. -/
theorem ederivWithin_constOfIsEmpty (f : E â†’ F) (hs : UniqueDiffWithinAt ğ•œ s x) :
    ederivWithin (fun x â†¦ constOfIsEmpty ğ•œ E (Fin 0) (f x)) s x =
      .ofSubsingleton _ _ _ (0 : Fin 1) (fderivWithin ğ•œ f s x) := by
  simp only [ederivWithin, â† constOfIsEmptyLIE_apply, â† Function.comp_def _ f,
    (constOfIsEmptyLIE ğ•œ E F (Fin 0)).comp_fderivWithin hs,
    uncurryFin_constOfIsEmptyLIE_comp]

/-- Exterior derivative of a 0-form given by a function `f`
is the 1-form given by the derivative of `f`. -/
theorem ederiv_constOfIsEmpty (f : E â†’ F) (x : E) :
    ederiv (fun x â†¦ constOfIsEmpty ğ•œ E (Fin 0) (f x)) x =
      .ofSubsingleton _ _ _ (0 : Fin 1) (fderiv ğ•œ f x) := by
  simp [â† ederivWithin_univ, ederivWithin_constOfIsEmpty, fderivWithin_univ]

theorem Filter.EventuallyEq.ederivWithin_eq (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (hx : Ï‰â‚ x = Ï‰â‚‚ x) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x := by
  simp only [ederivWithin, uncurryFin, hs.fderivWithin_eq hx]

theorem Filter.EventuallyEq.ederivWithin_eq_of_mem (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (hx : x âˆˆ s) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x :=
  hs.ederivWithin_eq (mem_of_mem_nhdsWithin hx hs :)

theorem Filter.EventuallyEq.ederivWithin_eq_of_insert (hs : Ï‰â‚ =á¶ [ğ“[insert x s] x] Ï‰â‚‚) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x := by
  apply Filter.EventuallyEq.ederivWithin_eq (nhdsWithin_mono _ (subset_insert x s) hs)
  exact (mem_of_mem_nhdsWithin (mem_insert x s) hs :)

theorem Filter.EventuallyEq.ederivWithin' (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) (ht : t âŠ† s) :
    ederivWithin Ï‰â‚ t =á¶ [ğ“[s] x] ederivWithin Ï‰â‚‚ t :=
  (eventually_eventually_nhdsWithin.2 hs).mp <| eventually_mem_nhdsWithin.mono fun _y hys hs =>
    EventuallyEq.ederivWithin_eq (hs.filter_mono <| nhdsWithin_mono _ ht)
        (hs.self_of_nhdsWithin hys)

protected theorem Filter.EventuallyEq.ederivWithin (hs : Ï‰â‚ =á¶ [ğ“[s] x] Ï‰â‚‚) :
    ederivWithin Ï‰â‚ s =á¶ [ğ“[s] x] ederivWithin Ï‰â‚‚ s :=
  hs.ederivWithin' .rfl

theorem Filter.EventuallyEq.ederivWithin_eq_nhds (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x :=
  (h.filter_mono nhdsWithin_le_nhds).ederivWithin_eq h.self_of_nhds

theorem ederivWithin_congr (hs : EqOn Ï‰â‚ Ï‰â‚‚ s) (hx : Ï‰â‚ x = Ï‰â‚‚ x) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x :=
  (hs.eventuallyEq.filter_mono inf_le_right).ederivWithin_eq hx

theorem ederivWithin_congr' (hs : EqOn Ï‰â‚ Ï‰â‚‚ s) (hx : x âˆˆ s) :
    ederivWithin Ï‰â‚ s x = ederivWithin Ï‰â‚‚ s x :=
  ederivWithin_congr hs (hs hx)

protected theorem Filter.EventuallyEq.ederiv (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) :
    ederiv Ï‰â‚ =á¶ [ğ“ x] ederiv Ï‰â‚‚ := by
  simp only [â† nhdsWithin_univ, â† ederivWithin_univ] at *
  exact h.ederivWithin

theorem Filter.EventuallyEq.ederiv_eq (h : Ï‰â‚ =á¶ [ğ“ x] Ï‰â‚‚) : ederiv Ï‰â‚ x = ederiv Ï‰â‚‚ x :=
  h.ederiv.self_of_nhds

theorem ederivWithin_apply (h : DifferentiableWithinAt ğ•œ Ï‰ s x) (hs : UniqueDiffWithinAt ğ•œ s x)
    (v : Fin (n + 1) â†’ E) :
    ederivWithin Ï‰ s x v = âˆ‘ i, (-1) ^ i.val â€¢ fderivWithin ğ•œ (Ï‰ Â· (i.removeNth v)) s x (v i) := by
  simp [ederivWithin, ContinuousAlternatingMap.uncurryFin_apply,
    fderivWithin_continuousAlternatingMap_apply_const_apply, *]

theorem ederiv_apply (h : DifferentiableAt ğ•œ Ï‰ x) (v : Fin (n + 1) â†’ E) :
    ederiv Ï‰ x v = âˆ‘ i, (-1) ^ i.val â€¢ fderiv ğ•œ (Ï‰ Â· (i.removeNth v)) x (v i) := by
  simp [â† ederivWithin_univ, ederivWithin_apply h.differentiableWithinAt]

/-- Second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem ederivWithin_ederivWithin_apply (hÏ‰ : ContDiffWithinAt ğ•œ r Ï‰ s x)
    (hr : minSmoothness ğ•œ 2 â‰¤ r) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ closure (interior s))
    (h'x : x âˆˆ s) : ederivWithin (ederivWithin Ï‰ s) s x = 0 := calc
  ederivWithin (ederivWithin Ï‰ s) s x =
    uncurryFin (fderivWithin ğ•œ (fun y â†¦ uncurryFin (fderivWithin ğ•œ Ï‰ s y)) s x) := rfl
  _ = uncurryFin (uncurryFinCLM _ _ _ âˆ˜L fderivWithin ğ•œ (fderivWithin ğ•œ Ï‰ s) s x) := by
    congr 1
    have : DifferentiableWithinAt ğ•œ (fderivWithin ğ•œ Ï‰ s) s x := by
      refine (hÏ‰.fderivWithin_right hs ?_ h'x).differentiableWithinAt le_rfl
      exact le_minSmoothness.trans hr
    exact (uncurryFinCLM _ _ _).hasFDerivAt.comp_hasFDerivWithinAt x this.hasFDerivWithinAt
      |>.fderivWithin (hs.uniqueDiffWithinAt h'x)
  _ = 0 := uncurryFin_uncurryFinCLM_comp_of_symmetric <| hÏ‰.isSymmSndFDerivWithinAt hr hs hx h'x

/-- Second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem ederivWithin_ederivWithin_eqOn (hÏ‰ : ContDiffOn ğ•œ r Ï‰ s) (hr : minSmoothness ğ•œ 2 â‰¤ r)
    (hs : UniqueDiffOn ğ•œ s) :
    EqOn (ederivWithin (ederivWithin Ï‰ s) s) 0 (s âˆ© closure (interior s)) := by
  rintro x âŸ¨h'x, hxâŸ©
  exact ederivWithin_ederivWithin_apply (hÏ‰.contDiffWithinAt h'x) hr hs hx h'x

/-- Second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem ederiv_ederiv_apply (hÏ‰ : ContDiffAt ğ•œ r Ï‰ x) (hr : minSmoothness ğ•œ 2 â‰¤ r) :
    ederiv (ederiv Ï‰) x = 0 := by
  simp only [â† ederivWithin_univ]
  apply ederivWithin_ederivWithin_apply (s := univ) hÏ‰.contDiffWithinAt hr <;> simp

/-- Second exterior derivative of a sufficiently smooth differential form is zero. -/
theorem ederiv_ederiv (h : ContDiff ğ•œ r Ï‰) (hr : minSmoothness ğ•œ 2 â‰¤ r) : ederiv (ederiv Ï‰) = 0 :=
  funext fun _ â†¦ ederiv_ederiv_apply h.contDiffAt hr
