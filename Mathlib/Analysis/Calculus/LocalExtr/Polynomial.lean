/-
Copyright (c) 2021 Benjamin Davidson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Benjamin Davidson, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.LocalExtr.Rolle
import Mathlib.Analysis.Calculus.Deriv.Polynomial
import Mathlib.Topology.Algebra.Polynomial

#align_import analysis.calculus.local_extr from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Rolle's Theorem for polynomials

In this file we use Rolle's Theorem
to relate the number of real roots of a real polynomial and its derivative.
Namely, we prove the following facts.

* `Polynomial.card_roots_toFinset_le_card_roots_derivative_diff_roots_succ`:
  the number of roots of a real polynomial `p` is at most the number of roots of its derivative
  that are not roots of `p` plus one.
* `Polynomial.card_roots_toFinset_le_derivative`, `Polynomial.card_rootSet_le_derivative`:
  the number of roots of a real polynomial
  is at most the number of roots of its derivative plus one.
* `Polynomial.card_roots_le_derivative`: same, but the roots are counted with multiplicities.

## Keywords

polynomial, Rolle's Theorem, root
-/

namespace Polynomial

open scoped BigOperators

/-- The number of roots of a real polynomial `p` is at most the number of roots of its derivative
that are not roots of `p` plus one. -/
theorem card_roots_toFinset_le_card_roots_derivative_diff_roots_succ (p : â„[X]) :
    p.roots.toFinset.card â‰¤ (p.derivative.roots.toFinset \ p.roots.toFinset).card + 1 := by
  cases' eq_or_ne (derivative p) 0 with hp' hp'
  -- âŠ¢ Finset.card (Multiset.toFinset (roots p)) â‰¤ Finset.card (Multiset.toFinset ( â€¦
  Â· rw [eq_C_of_derivative_eq_zero hp', roots_C, Multiset.toFinset_zero, Finset.card_empty]
    -- âŠ¢ 0 â‰¤ Finset.card (Multiset.toFinset (roots (â†‘derivative (â†‘C (coeff p 0)))) \  â€¦
    exact zero_le _
    -- ðŸŽ‰ no goals
  have hp : p â‰  0 := ne_of_apply_ne derivative (by rwa [derivative_zero])
  -- âŠ¢ Finset.card (Multiset.toFinset (roots p)) â‰¤ Finset.card (Multiset.toFinset ( â€¦
  refine' Finset.card_le_diff_of_interleaved fun x hx y hy hxy hxy' => _
  -- âŠ¢ âˆƒ z, z âˆˆ Multiset.toFinset (roots (â†‘derivative p)) âˆ§ x < z âˆ§ z < y
  rw [Multiset.mem_toFinset, mem_roots hp] at hx hy
  -- âŠ¢ âˆƒ z, z âˆˆ Multiset.toFinset (roots (â†‘derivative p)) âˆ§ x < z âˆ§ z < y
  obtain âŸ¨z, hz1, hz2âŸ© := exists_deriv_eq_zero hxy p.continuousOn (hx.trans hy.symm)
  -- âŠ¢ âˆƒ z, z âˆˆ Multiset.toFinset (roots (â†‘derivative p)) âˆ§ x < z âˆ§ z < y
  refine' âŸ¨z, _, hz1âŸ©
  -- âŠ¢ z âˆˆ Multiset.toFinset (roots (â†‘derivative p))
  rwa [Multiset.mem_toFinset, mem_roots hp', IsRoot, â† p.deriv]
  -- ðŸŽ‰ no goals
#align polynomial.card_roots_to_finset_le_card_roots_derivative_diff_roots_succ Polynomial.card_roots_toFinset_le_card_roots_derivative_diff_roots_succ

/-- The number of roots of a real polynomial is at most the number of roots of its derivative plus
one. -/
theorem card_roots_toFinset_le_derivative (p : â„[X]) :
    p.roots.toFinset.card â‰¤ p.derivative.roots.toFinset.card + 1 :=
  p.card_roots_toFinset_le_card_roots_derivative_diff_roots_succ.trans <|
    add_le_add_right (Finset.card_mono <| Finset.sdiff_subset _ _) _
#align polynomial.card_roots_to_finset_le_derivative Polynomial.card_roots_toFinset_le_derivative

/-- The number of roots of a real polynomial (counted with multiplicities) is at most the number of
roots of its derivative (counted with multiplicities) plus one. -/
theorem card_roots_le_derivative (p : â„[X]) :
    Multiset.card p.roots â‰¤ Multiset.card (derivative p).roots + 1 :=
  calc
    Multiset.card p.roots = âˆ‘ x in p.roots.toFinset, p.roots.count x :=
      (Multiset.toFinset_sum_count_eq _).symm
    _ = âˆ‘ x in p.roots.toFinset, (p.roots.count x - 1 + 1) :=
      (Eq.symm <| Finset.sum_congr rfl fun x hx => tsub_add_cancel_of_le <|
        Nat.succ_le_iff.2 <| Multiset.count_pos.2 <| Multiset.mem_toFinset.1 hx)
    _ = (âˆ‘ x in p.roots.toFinset, (p.rootMultiplicity x - 1)) + p.roots.toFinset.card := by
      simp only [Finset.sum_add_distrib, Finset.card_eq_sum_ones, count_roots]
      -- ðŸŽ‰ no goals
    _ â‰¤ (âˆ‘ x in p.roots.toFinset, p.derivative.rootMultiplicity x) +
          ((p.derivative.roots.toFinset \ p.roots.toFinset).card + 1) :=
      (add_le_add
        (Finset.sum_le_sum fun x _ => rootMultiplicity_sub_one_le_derivative_rootMultiplicity _ _)
        p.card_roots_toFinset_le_card_roots_derivative_diff_roots_succ)
    _ â‰¤ (âˆ‘ x in p.roots.toFinset, p.derivative.roots.count x) +
          ((âˆ‘ x in p.derivative.roots.toFinset \ p.roots.toFinset,
            p.derivative.roots.count x) + 1) := by
      simp only [â† count_roots]
      -- âŠ¢ âˆ‘ x in Multiset.toFinset (roots p), Multiset.count x (roots (â†‘derivative p)) â€¦
      refine' add_le_add_left (add_le_add_right ((Finset.card_eq_sum_ones _).trans_le _) _) _
      -- âŠ¢ âˆ‘ x in Multiset.toFinset (roots (â†‘derivative p)) \ Multiset.toFinset (roots  â€¦
      refine' Finset.sum_le_sum fun x hx => Nat.succ_le_iff.2 <| _
      -- âŠ¢ 0 < Multiset.count x (roots (â†‘derivative p))
      rw [Multiset.count_pos, â† Multiset.mem_toFinset]
      -- âŠ¢ x âˆˆ Multiset.toFinset (roots (â†‘derivative p))
      exact (Finset.mem_sdiff.1 hx).1
      -- ðŸŽ‰ no goals
    _ = Multiset.card (derivative p).roots + 1 := by
      rw [â† add_assoc, â† Finset.sum_union Finset.disjoint_sdiff, Finset.union_sdiff_self_eq_union, â†
        Multiset.toFinset_sum_count_eq, â† Finset.sum_subset (Finset.subset_union_right _ _)]
      intro x _ hxâ‚‚
      -- âŠ¢ Multiset.count x (roots (â†‘derivative p)) = 0
      simpa only [Multiset.mem_toFinset, Multiset.count_eq_zero] using hxâ‚‚
      -- ðŸŽ‰ no goals
#align polynomial.card_roots_le_derivative Polynomial.card_roots_le_derivative

/-- The number of real roots of a polynomial is at most the number of roots of its derivative plus
one. -/
theorem card_rootSet_le_derivative {F : Type*} [CommRing F] [Algebra F â„] (p : F[X]) :
    Fintype.card (p.rootSet â„) â‰¤ Fintype.card (p.derivative.rootSet â„) + 1 := by
  simpa only [rootSet_def, Finset.coe_sort_coe, Fintype.card_coe, derivative_map] using
    card_roots_toFinset_le_derivative (p.map (algebraMap F â„))
#align polynomial.card_root_set_le_derivative Polynomial.card_rootSet_le_derivative

end Polynomial
