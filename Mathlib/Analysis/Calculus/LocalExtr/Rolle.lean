/-
Copyright (c) 2019 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Anatole Dedecker
-/
import Mathlib.Analysis.Calculus.LocalExtr.Basic
import Mathlib.Topology.Algebra.Order.Rolle

#align_import analysis.calculus.local_extr from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Rolle's Theorem

In this file we prove Rolle's Theorem. The theorem says that for a function `f : â„ â†’ â„` such that

* $f$ is differentiable on an open interval $(a, b)$, $a < b$;
* $f$ is continuous on the corresponding closed interval $[a, b]$;
* $f(a) = f(b)$,

there exists a point $câˆˆ(a, b)$ such that $f'(c)=0$.

We prove four versions of this theorem.

* `exists_hasDerivAt_eq_zero` is closest to the statement given above. It assumes that at every
  point $x âˆˆ (a, b)$ function $f$ has derivative $f'(x)$, then concludes that $f'(c)=0$ for some
  $câˆˆ(a, b)$.
* `exists_deriv_eq_zero` deals with `deriv f` instead of an arbitrary function `f'` and a predicate
  `HasDerivAt`; since we use zero as the "junk" value for `deriv f c`, this version does not
  assume that `f` is differentiable on the open interval.
* `exists_hasDerivAt_eq_zero'` is similar to `exists_hasDerivAt_eq_zero` but instead of assuming
  continuity on the closed interval $[a, b]$ it assumes that $f$ tends to the same limit as $x$
  tends to $a$ from the right and as $x$ tends to $b$ from the left.
* `exists_deriv_eq_zero'` relates to `exists_deriv_eq_zero` as `exists_hasDerivAt_eq_zero'`
  relates to ``exists_hasDerivAt_eq_zero`.

## References

* [Rolle's Theorem](https://en.wikipedia.org/wiki/Rolle's_theorem);

## Tags

local extremum, Rolle's Theorem
-/

open Set Filter Topology

variable {f f' : â„ â†’ â„} {a b l : â„}

/-- **Rolle's Theorem** `HasDerivAt` version -/
theorem exists_hasDerivAt_eq_zero (hab : a < b) (hfc : ContinuousOn f (Icc a b)) (hfI : f a = f b)
    (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) : âˆƒ c âˆˆ Ioo a b, f' c = 0 :=
  let âŸ¨c, cmem, hcâŸ© := exists_isLocalExtr_Ioo hab hfc hfI
  âŸ¨c, cmem, hc.hasDerivAt_eq_zero <| hff' c cmemâŸ©
#align exists_has_deriv_at_eq_zero exists_hasDerivAt_eq_zero

/-- **Rolle's Theorem** `deriv` version -/
theorem exists_deriv_eq_zero (hab : a < b) (hfc : ContinuousOn f (Icc a b)) (hfI : f a = f b) :
    âˆƒ c âˆˆ Ioo a b, deriv f c = 0 :=
  let âŸ¨c, cmem, hcâŸ© := exists_isLocalExtr_Ioo hab hfc hfI
  âŸ¨c, cmem, hc.deriv_eq_zeroâŸ©
#align exists_deriv_eq_zero exists_deriv_eq_zero

/-- **Rolle's Theorem**, a version for a function on an open interval: if `f` has derivative `f'`
on `(a, b)` and has the same limit `l` at `ğ“[>] a` and `ğ“[<] b`, then `f' c = 0`
for some `c âˆˆ (a, b)`.  -/
theorem exists_hasDerivAt_eq_zero' (hab : a < b) (hfa : Tendsto f (ğ“[>] a) (ğ“ l))
    (hfb : Tendsto f (ğ“[<] b) (ğ“ l)) (hff' : âˆ€ x âˆˆ Ioo a b, HasDerivAt f (f' x) x) :
    âˆƒ c âˆˆ Ioo a b, f' c = 0 :=
  let âŸ¨c, cmem, hcâŸ© := exists_isLocalExtr_Ioo_of_tendsto hab
    (fun x hx â†¦ (hff' x hx).continuousAt.continuousWithinAt) hfa hfb
  âŸ¨c, cmem, hc.hasDerivAt_eq_zero <| hff' c cmemâŸ©
#align exists_has_deriv_at_eq_zero' exists_hasDerivAt_eq_zero'

/-- **Rolle's Theorem**, a version for a function on an open interval: if `f` has the same limit
`l` at `ğ“[>] a` and `ğ“[<] b`, then `deriv f c = 0` for some `c âˆˆ (a, b)`. This version
does not require differentiability of `f` because we define `deriv f c = 0` whenever `f` is not
differentiable at `c`. -/
theorem exists_deriv_eq_zero' (hab : a < b) (hfa : Tendsto f (ğ“[>] a) (ğ“ l))
    (hfb : Tendsto f (ğ“[<] b) (ğ“ l)) : âˆƒ c âˆˆ Ioo a b, deriv f c = 0 := by
  by_cases h : âˆ€ x âˆˆ Ioo a b, DifferentiableAt â„ f x
  Â· exact exists_hasDerivAt_eq_zero' hab hfa hfb fun x hx => (h x hx).hasDerivAt
  Â· obtain âŸ¨c, hc, hcdiffâŸ© : âˆƒ x âˆˆ Ioo a b, Â¬DifferentiableAt â„ f x := by
      push_neg at h; exact h
    exact âŸ¨c, hc, deriv_zero_of_not_differentiableAt hcdiffâŸ©
#align exists_deriv_eq_zero' exists_deriv_eq_zero'
