/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.CstarAlgebra.ContinuousFunctionalCalculus.Order

/-!
# Hilbert Câ‹†-modules

FIXME

## Main declarations

## Implementation note

FIXME
-/

open scoped ComplexOrder RightActions

/-- A *Hilbert Câ‹†-module* is a complex linear space `E` endowed with a right `A`-module structure
(where `A` is typically a Câ‹†-algebra) and an inner product `âŸªx, yâŸ«_A` which satisfies the
following properties. -/
class HilbertCstarModule (A : outParam <| Type*) (E : Type*) [NonUnitalSemiring A] [StarRing A]
    [Module â„‚ A] [AddCommGroup E] [Module â„‚ E] [PartialOrder A] [SMul Aáµáµ’áµ– E] [Norm A] [Norm E]
    extends Inner A E where
  inner_add_right {x} {y} {z} : inner x (y + z) = inner x y + inner x z
  inner_self_nonneg {x} : 0 â‰¤ inner x x
  inner_self {x} : inner x x = 0 â†” x = 0
  inner_op_smul_right {a : A} {x y : E} : inner x (y <â€¢ a) = inner x y * a
  inner_smul_right_complex {z : â„‚} {x} {y} : inner x (z â€¢ y) = z â€¢ inner x y
  star_inner x y : star (inner x y) = inner y x
  norm_eq_sqrt_norm_inner_self x : â€–xâ€– = âˆšâ€–inner x xâ€–

attribute [simp] HilbertCstarModule.inner_add_right HilbertCstarModule.star_inner
  HilbertCstarModule.inner_op_smul_right HilbertCstarModule.inner_smul_right_complex

namespace HilbertCstarModule

section general

variable {A E : Type*} [NonUnitalRing A] [StarRing A] [AddCommGroup E] [Module â„‚ A]
  [Module â„‚ E] [PartialOrder A] [SMul Aáµáµ’áµ– E] [StarModule â„‚ A] [Norm A] [Norm E]
  [HilbertCstarModule A E]

local notation "âŸª" x ", " y "âŸ«" => inner (ğ•œ := A) x y

@[simp]
lemma inner_add_left {x y z : E} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« := by
  rw [â† star_star (r := âŸªx + y, zâŸ«)]
  simp only [inner_add_right, star_add, star_inner]

@[simp]
lemma inner_op_smul_left {a : A} {x y : E} : âŸªx <â€¢ a, yâŸ« = star a * âŸªx, yâŸ« := by
  rw [â† star_inner]; simp

@[simp]
lemma inner_smul_left_complex {z : â„‚} {x y : E} : âŸªz â€¢ x, yâŸ« = star z â€¢ âŸªx, yâŸ« := by
  rw [â† star_inner]
  simp

@[simp]
lemma inner_smul_left_real {z : â„} {x y : E} : âŸªz â€¢ x, yâŸ« = z â€¢ âŸªx, yâŸ« := by
  have hâ‚ : z â€¢ x = (z : â„‚) â€¢ x := by simp
  rw [hâ‚, â† star_inner, inner_smul_right_complex]
  simp

@[simp]
lemma inner_smul_right_real {z : â„} {x y : E} : âŸªx, z â€¢ yâŸ« = z â€¢ âŸªx, yâŸ« := by
  have hâ‚ : z â€¢ y = (z : â„‚) â€¢ y := by simp
  rw [hâ‚, â† star_inner, inner_smul_left_complex]
  simp

/-- The function `y â†¦ âŸªx, yâŸ«` bundled as a linear map. -/
def innerRightL (x : E) : E â†’â‚—[â„‚] A where
  toFun y := âŸªx, yâŸ«
  map_add' z y := by simp
  map_smul' z y := by simp

/-- The function `y â†¦ âŸªy, xâŸ«` bundled as a conjugate-linear map. -/
def innerLeftL (x : E) : E â†’â‚—â‹†[â„‚] A where
  toFun y := âŸªy, xâŸ«
  map_add' z y := by simp
  map_smul' z y := by simp

lemma inner_eq_innerRightL (x y : E) : âŸªx, yâŸ« = innerRightL x y := rfl

lemma inner_eq_innerLeftL (x y : E) : âŸªx, yâŸ« = innerLeftL y x := rfl

@[simp] lemma inner_zero_right {x : E} : âŸªx, 0âŸ« = 0 := by simp [inner_eq_innerRightL]
@[simp] lemma inner_zero_left {x : E} : âŸª0, xâŸ« = 0 := by simp [inner_eq_innerLeftL]
@[simp] lemma inner_neg_right {x y : E} : âŸªx, -yâŸ« = -âŸªx, yâŸ« := by simp [inner_eq_innerRightL]
@[simp] lemma inner_neg_left {x y : E} : âŸª-x, yâŸ« = -âŸªx, yâŸ« := by simp [inner_eq_innerLeftL]
@[simp] lemma inner_sub_right {x y z : E} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« := by
  simp [inner_eq_innerRightL]
@[simp] lemma inner_sub_left {x y z : E} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« := by
  simp [inner_eq_innerLeftL]

@[simp]
lemma inner_sum_right {Î¹ : Type*} [DecidableEq Î¹] {s : Finset Î¹} {x : E} {y : Î¹ â†’ E} :
    âŸªx, âˆ‘ i âˆˆ s, y iâŸ« = âˆ‘ i âˆˆ s, âŸªx, y iâŸ« := by
  induction s using Finset.induction_on
  case empty => simp
  case insert a t a_notmem_t hbase =>
    simp_rw [Finset.sum_insert a_notmem_t]
    simp only [inner_add_right, hbase]

@[simp]
lemma inner_sum_left {Î¹ : Type*} [DecidableEq Î¹] {s : Finset Î¹} {x : Î¹ â†’ E} {y : E} :
    âŸªâˆ‘ i âˆˆ s, x i, yâŸ« = âˆ‘ i âˆˆ s, âŸªx i, yâŸ« := by rw [â† star_inner y]; simp

@[simp]
lemma isSelfAdjoint_inner_self {x : E} : IsSelfAdjoint âŸªx, xâŸ«_A := star_inner _ _

end general

section normed

variable {A E : Type*} [NonUnitalNormedRing A] [StarRing A] [CstarRing A] [PartialOrder A]
  [CompleteSpace A] [StarOrderedRing A] [AddCommGroup E] [NormedSpace â„‚ A]
  [Module â„‚ E] [SMul Aáµáµ’áµ– E] [Norm E]
  [StarModule â„‚ A] [hsm : HilbertCstarModule A E] [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A]

local notation "âŸª" x ", " y "âŸ«" => inner (ğ•œ := A) x y

variable (A) in
/-- The norm associated with a Hilbert Câ‹†-module. It is not registered as a norm, since a type
might already have a norm defined on it. -/
noncomputable def norm : Norm E where
  norm x := Real.sqrt â€–âŸªx, xâŸ«â€–

--attribute [local instance] norm

--lemma norm_eq_sqrt_norm_inner_self {x : E} : â€–xâ€– = Real.sqrt â€–âŸªx, xâŸ«â€– := rfl

lemma inner_self_eq_norm_sq {x : E} : â€–âŸªx, xâŸ«â€– = â€–xâ€– ^ 2 := by simp [norm_eq_sqrt_norm_inner_self]

@[simp] lemma norm_zero : â€–(0 : E)â€– = 0 := by simp [norm_eq_sqrt_norm_inner_self]

@[simp]
lemma norm_zero_iff (x : E) : â€–xâ€– = 0 â†” x = 0 :=
  âŸ¨fun h => by simpa [norm_eq_sqrt_norm_inner_self, inner_self] using h, fun h => by simp [norm, h]âŸ©

lemma norm_nonneg {x : E} : 0 â‰¤ â€–xâ€– := by simp [norm_eq_sqrt_norm_inner_self]; positivity

lemma norm_pos {x : E} (hx : x â‰  0) : 0 < â€–xâ€– := by
  simp only [norm_eq_sqrt_norm_inner_self, Real.sqrt_pos, norm_pos_iff]
  intro H
  rw [inner_self] at H
  exact hx H

@[simp]
lemma norm_neg {x : E} : â€–-xâ€– = â€–xâ€– := by simp [norm_eq_sqrt_norm_inner_self]

lemma norm_sq_eq {x : E} : â€–xâ€– ^ 2 = â€–âŸªx, xâŸ«â€– := by simp [norm_eq_sqrt_norm_inner_self]

lemma smul_nonneg_iff {a : A} {r : â„} (hr : 0 < r) : 0 â‰¤ a â†” 0 â‰¤ r â€¢ a := by
  refine âŸ¨smul_nonneg (le_of_lt hr), fun hra => ?_âŸ©
  have : a = râ»Â¹ â€¢ (r â€¢ a) := by
    rw [smul_smul, inv_mul_cancel]
    exact (MulAction.one_smul a).symm
    exact Ne.symm (ne_of_lt hr)
  rw [this]
  refine smul_nonneg ?_ hra
  positivity

@[simp]
protected lemma norm_smul {r : â„} {x : E} : â€–r â€¢ xâ€– = â€–râ€– * â€–xâ€– := by
  rw [norm_eq_sqrt_norm_inner_self, norm_eq_sqrt_norm_inner_self x]
  simp only [inner_smul_left_real, inner_smul_right_real, _root_.norm_smul, â† mul_assoc]
  rw [Real.sqrt_mul (by positivity)]
  congr
  exact Real.sqrt_mul_self (by positivity)

lemma cauchy_schwarzâ‚ (x y : E) : âŸªy, xâŸ« * âŸªx, yâŸ« â‰¤ â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ« := by
  rcases eq_or_ne x 0 with h|h
  Â· simp [h]
  Â· have hâ‚ : âˆ€ (a : A),
        (0 : A) â‰¤ â€–xâ€– ^ 2 â€¢ (star a * a) - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := fun a => by
      calc (0 : A) â‰¤ âŸªx <â€¢ a - â€–xâ€– ^ 2 â€¢ y, x <â€¢ a - â€–xâ€– ^ 2 â€¢ yâŸ«_A := by
                      exact inner_self_nonneg
            _ = star a * âŸªx, xâŸ« * a - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := by
                      simp only [inner_sub_right, inner_op_smul_right, inner_sub_left,
                        inner_op_smul_left, inner_smul_left_real, sub_mul, smul_mul_assoc,
                        inner_smul_right_real, smul_sub]
                      abel
            _ â‰¤ â€–xâ€– ^ 2 â€¢ (star a * a) - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := by
                      gcongr
                      calc _ â‰¤ â€–âŸªx, xâŸ«_Aâ€– â€¢ (star a * a) := CstarRing.conjugate_le_norm_smul
                        _ = (Real.sqrt â€–âŸªx, xâŸ«_Aâ€–) ^ 2 â€¢ (star a * a) := by
                                  congr
                                  have : 0 â‰¤ â€–âŸªx, xâŸ«_Aâ€– := by positivity
                                  rw [Real.sq_sqrt this]
                        _ = â€–xâ€– ^ 2 â€¢ (star a * a) := by rw [â† norm_eq_sqrt_norm_inner_self]
    specialize hâ‚ âŸªx, yâŸ«
    simp only [star_inner, sub_self, zero_sub, le_neg_add_iff_add_le, add_zero] at hâ‚
    rwa [smul_le_smul_iff_of_pos_left (pow_pos (norm_pos h) _)] at hâ‚

lemma cauchy_schwarzâ‚‚ (x y : E) : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := by
  have := calc â€–âŸªx, yâŸ«â€– ^ 2 = â€–âŸªy, xâŸ« * âŸªx, yâŸ«â€– := by
                rw [â† star_inner x, CstarRing.norm_star_mul_self, pow_two]
    _ â‰¤ â€–â€–xâ€–^ 2 â€¢ âŸªy, yâŸ«â€– := by
                refine CstarRing.norm_le_norm_of_nonneg_of_le ?_ (cauchy_schwarzâ‚ x y)
                rw [â† star_inner x]
                exact star_mul_self_nonneg âŸªx, yâŸ«_A
    _ = â€–xâ€– ^ 2 * â€–âŸªy, yâŸ«â€– := by simp [_root_.norm_smul]
    _ = â€–xâ€– ^ 2 * â€–yâ€– ^ 2 := by
                simp only [norm_eq_sqrt_norm_inner_self, _root_.norm_nonneg, Real.sq_sqrt]
    _ = (â€–xâ€– * â€–yâ€–) ^ 2 := by simp only [mul_pow]
  refine (pow_le_pow_iff_left (R := â„) (_root_.norm_nonneg âŸªx, yâŸ«_A) ?_ (by norm_num)).mp this
  exact mul_nonneg norm_nonneg norm_nonneg

lemma norm_triangle (x y : E) : â€–x + yâ€– â‰¤ â€–xâ€– + â€–yâ€– := by
  have h : â€–x + yâ€– ^ 2 â‰¤ (â€–xâ€– + â€–yâ€–) ^ 2 := by
    calc _ â‰¤ â€–âŸªx, xâŸ« + âŸªy, xâŸ«â€– + â€–âŸªx, yâŸ«â€– + â€–âŸªy, yâŸ«â€– := by
          simp only [norm_eq_sqrt_norm_inner_self, inner_add_right, inner_add_left, â† add_assoc,
            _root_.norm_nonneg, Real.sq_sqrt]
          exact norm_addâ‚ƒ_le _ _ _
      _ â‰¤ â€–âŸªx, xâŸ«â€– + â€–âŸªy, xâŸ«â€– + â€–âŸªx, yâŸ«â€– + â€–âŸªy, yâŸ«â€– := by gcongr; exact norm_add_le _ _
      _ â‰¤ â€–âŸªx, xâŸ«â€– + â€–yâ€– * â€–xâ€– + â€–xâ€– * â€–yâ€– + â€–âŸªy, yâŸ«â€– := by
          gcongr <;> exact cauchy_schwarzâ‚‚ _ _
      _ = â€–xâ€– ^ 2 + â€–yâ€– * â€–xâ€– + â€–xâ€– * â€–yâ€– + â€–yâ€– ^ 2 := by
          simp [norm_eq_sqrt_norm_inner_self]
      _ = (â€–xâ€– + â€–yâ€–) ^ 2 := by simp only [add_pow_two, add_left_inj]; ring
  refine (pow_le_pow_iff_left norm_nonneg ?_ (by norm_num)).mp h
  exact add_nonneg norm_nonneg norm_nonneg

/-- This allows us to get `NormedAddCommGroup` and `NormedSpace` instances on `E` via
`NormedAddCommGroup.ofCore` and `NormedSpace.ofCore`. -/
lemma normedSpaceCore : NormedSpace.Core â„‚ E where
  norm_nonneg x := norm_nonneg
  norm_eq_zero_iff x := norm_zero_iff x
  norm_smul c x := by simp [norm_eq_sqrt_norm_inner_self, _root_.norm_smul, â† mul_assoc]
  norm_triangle x y := norm_triangle x y

end normed

end HilbertCstarModule
