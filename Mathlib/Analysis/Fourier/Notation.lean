/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Algebra.Module.Equiv.Defs
public import Mathlib.Algebra.BigOperators.Group.Finset.Defs
public import Mathlib.Topology.Algebra.Module.Equiv

/-! # Type classes for the Fourier transform

In this file we define type classes for the Fourier transform and the inverse Fourier transform.
We introduce the notation `ğ“•` and `ğ“•â»` in these classes to denote the Fourier transform and
the inverse Fourier transform, respectively.

Moreover, we provide type-classes that encode the linear structure and the Fourier inversion
theorem.
-/

@[expose] public section

universe u v w

variable {R E F : Type*}

/--
The notation typeclass for the Fourier transform.

While the Fourier transform is a linear operator, the notation is for the function `E â†’ F` without
any additional properties. This makes it possible to use the notation for functions where
integrability is an issue.
Moreover, including a scalar multiplication causes problems for inferring the notation type class.
-/
class FourierTransform (E : Type u) (F : outParam (Type v)) where
  /-- `ğ“• f` is the Fourier transform of `f`. The meaning of this notation is type-dependent. -/
  fourier : E â†’ F

/--
The notation typeclass for the inverse Fourier transform.

While the inverse Fourier transform is a linear operator, the notation is for the function `E â†’ F`
without any additional properties. This makes it possible to use the notation for functions where
integrability is an issue.
Moreover, including a scalar multiplication causes problems for inferring the notation type class.
-/
class FourierTransformInv (E : Type u) (F : outParam (Type v)) where
  /-- `ğ“•â» f` is the inverse Fourier transform of `f`. The meaning of this notation is
  type-dependent. -/
  fourierInv : E â†’ F

namespace FourierTransform

export FourierTransformInv (fourierInv)

@[inherit_doc] scoped notation "ğ“•" => fourier
@[inherit_doc] scoped notation "ğ“•â»" => fourierInv

end FourierTransform

section Module

open scoped FourierTransform

/-- A `FourierAdd` is a function space on which the Fourier transform is additive. -/
class FourierAdd (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [FourierTransform E F] where
  fourier_add : âˆ€ (f g : E), ğ“• (f + g) = ğ“• f + ğ“• g

/-- A `FourierSMul` is a function space on which the Fourier transform is homogeneous. -/
class FourierSMul (R : Type*) (E : Type*) (F : outParam (Type*)) [SMul R E] [SMul R F]
    [FourierTransform E F] where
  fourier_smul : âˆ€ (r : R) (f : E), ğ“• (r â€¢ f) = r â€¢ ğ“• f

/-- The Fourier transform is continuous. -/
class ContinuousFourier (E : Type*) (F : outParam (Type*))
    [TopologicalSpace E] [TopologicalSpace F] [FourierTransform E F] where
  continuous_fourier : Continuous (ğ“• : E â†’ F)

/-- A `FourierInvAdd` is a function space on which the inverse Fourier transform is additive. -/
class FourierInvAdd (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [FourierTransformInv E F]
    where
  fourierInv_add : âˆ€ (f g : E), ğ“•â» (f + g) = ğ“•â» f + ğ“•â» g

/-- A `FourierInvSMul` is a function space on which the inverse Fourier transform is homogeneous. -/
class FourierInvSMul (R : Type*) (E : Type*) (F : outParam (Type*)) [SMul R E] [SMul R F]
    [FourierTransformInv E F] where
  fourierInv_smul : âˆ€ (r : R) (f : E), ğ“•â» (r â€¢ f) = r â€¢ ğ“•â» f

/-- The inverse Fourier transform is continuous. -/
class ContinuousFourierInv (E : Type*) (F : outParam (Type*))
    [TopologicalSpace E] [TopologicalSpace F] [FourierTransformInv E F] where
  continuous_fourierInv : Continuous (ğ“•â» : E â†’ F)

/-- A `FourierModule` is a function space on which the Fourier transform is a linear map. -/
@[deprecated "use `FourierAdd` and `FourierSMul` instead" (since := "2026-01-06")]
structure FourierModule (R : Type*) (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [SMul R E]
    [SMul R F] extends FourierTransform E F where
  fourier_add : âˆ€ (f g : E), ğ“• (f + g) = ğ“• f + ğ“• g
  fourier_smul : âˆ€ (r : R) (f : E), ğ“• (r â€¢ f) = r â€¢ ğ“• f

/-- A `FourierInvModule` is a function space on which the Fourier transform is a linear map. -/
@[deprecated "use `FourierInvAdd` and `FourierInvSMul` instead" (since := "2026-01-06")]
structure FourierInvModule (R : Type*) (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [SMul R E]
    [SMul R F] extends FourierTransformInv E F where
  fourierInv_add : âˆ€ (f g : E), ğ“•â» (f + g) = ğ“•â» f + ğ“•â» g
  fourierInv_smul : âˆ€ (r : R) (f : E), ğ“•â» (r â€¢ f) = r â€¢ ğ“•â» f

namespace FourierTransform

export FourierAdd (fourier_add)
export FourierSMul (fourier_smul)
export ContinuousFourier (continuous_fourier)
export FourierInvAdd (fourierInv_add)
export FourierInvSMul (fourierInv_smul)
export ContinuousFourierInv (continuous_fourierInv)

attribute [simp] fourier_add
attribute [simp] fourier_smul
attribute [simp] fourierInv_add
attribute [simp] fourierInv_smul
attribute [fun_prop] continuous_fourier
attribute [fun_prop] continuous_fourierInv

variable [Semiring R] [AddCommMonoid E] [AddCommMonoid F] [Module R E] [Module R F]

section fourierCLM

variable [FourierTransform E F] [FourierAdd E F] [FourierSMul R E F]

variable (R E) in
/-- The Fourier transform as a linear map. -/
def fourierâ‚— : E â†’â‚—[R] F where
  toFun := ğ“•
  map_add' := fourier_add
  map_smul' := fourier_smul

@[simp]
lemma fourierâ‚—_apply (f : E) : fourierâ‚— R E f = ğ“• f := rfl

include R in
variable (R) in
lemma fourier_zero : ğ“• (0 : E) = 0 :=
  (fourierâ‚— R E).map_zero

variable [TopologicalSpace E] [TopologicalSpace F] [ContinuousFourier E F]

variable (R E) in
/-- The Fourier transform as a continuous linear map. -/
def fourierCLM : E â†’L[R] F where
  __ := fourierâ‚— R E
  cont := continuous_fourier

@[simp]
lemma fourierCLM_apply (f : E) : fourierCLM R E f = ğ“• f := rfl

end fourierCLM

section fourierInvCLM

variable [FourierTransformInv E F] [FourierInvAdd E F] [FourierInvSMul R E F]

variable (R E) in
/-- The inverse Fourier transform as a linear map. -/
def fourierInvâ‚— : E â†’â‚—[R] F where
  toFun := ğ“•â»
  map_add' := fourierInv_add
  map_smul' := fourierInv_smul

@[simp]
lemma fourierInvâ‚—_apply (f : E) : fourierInvâ‚— R E f = ğ“•â» f := rfl

include R in
variable (R) in
lemma fourierInv_zero : ğ“•â» (0 : E) = 0 :=
  (fourierInvâ‚— R E).map_zero

variable [TopologicalSpace E] [TopologicalSpace F] [ContinuousFourierInv E F]

variable (R E) in
/-- The inverse Fourier transform as a continuous linear map. -/
def fourierInvCLM : E â†’L[R] F where
  toFun := ğ“•â»
  map_add' := fourierInv_add
  map_smul' := fourierInv_smul
  cont := continuous_fourierInv

@[simp]
lemma fourierInvCLM_apply (f : E) : fourierInvCLM R E f = ğ“•â» f := rfl

end fourierInvCLM

end FourierTransform

end Module

section Pair

open FourierTransform

/-- A `FourierPair` is a pair of spaces `E` and `F` such that `ğ“•â» âˆ˜ ğ“• = id` on `E`. -/
class FourierPair (E F : Type*) [FourierTransform E F] [FourierTransformInv F E] where
  fourierInv_fourier_eq : âˆ€ (f : E), ğ“•â» (ğ“• f) = f

/-- A `FourierInvPair` is a pair of spaces `E` and `F` such that `ğ“• âˆ˜ ğ“•â» = id` on `E`. -/
class FourierInvPair (E F : Type*) [FourierTransform F E] [FourierTransformInv E F] where
  fourier_fourierInv_eq : âˆ€ (f : E), ğ“• (ğ“•â» f) = f

namespace FourierTransform

export FourierPair (fourierInv_fourier_eq)
export FourierInvPair (fourier_fourierInv_eq)

attribute [simp] fourierInv_fourier_eq
attribute [simp] fourier_fourierInv_eq

variable {R E F : Type*} [Semiring R] [AddCommMonoid E] [AddCommMonoid F] [Module R E] [Module R F]
  [FourierTransform E F] [FourierAdd E F] [FourierSMul R E F]
  [FourierTransformInv F E]
  [FourierPair E F] [FourierInvPair F E]

variable (R E) in
/-- The Fourier transform as a linear equivalence. -/
def fourierEquiv : E â‰ƒâ‚—[R] F where
  __ := fourierâ‚— R E
  invFun := ğ“•â»
  left_inv := fourierInv_fourier_eq
  right_inv := fourier_fourierInv_eq

@[simp]
lemma fourierEquiv_apply (f : E) : fourierEquiv R E f = ğ“• f := rfl

@[simp]
lemma fourierEquiv_symm_apply (f : F) : (fourierEquiv R E).symm f = ğ“•â» f := rfl

variable [TopologicalSpace E] [TopologicalSpace F]
  [ContinuousFourier E F] [ContinuousFourierInv F E]

variable (R E) in
/-- The Fourier transform as a continuous linear equivalence. -/
def fourierCLE : E â‰ƒL[R] F where
  __ := fourierEquiv R E
  continuous_toFun := continuous_fourier
  continuous_invFun := continuous_fourierInv

@[simp]
lemma fourierCLE_apply (f : E) : fourierCLE R E f = ğ“• f := rfl

@[simp]
lemma fourierCLE_symm_apply (f : F) : (fourierCLE R E).symm f = ğ“•â» f := rfl

end FourierTransform

end Pair
