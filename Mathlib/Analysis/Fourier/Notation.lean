/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Algebra.Module.LinearMap.Defs

/-! # Type classes for the Fourier transform

In this file we define type classes for the Fourier transform and the inverse Fourier transform.
We introduce the notation `ğ“•` and `ğ“•â»` in these classes to denote the Fourier transform and
the inverse Fourier transform, respectively.

Moreover, we provide type-classes that encode the linear structure and the Fourier inversion
theorem.
-/

universe u v w

/--
The notation typeclass for the Fourier transform.
-/
class FourierTransform (E : Type u) (F : outParam (Type v)) where
  /-- `ğ“• f` is the Fourier transform of `f`. The meaning of this notation is type-dependent. -/
  fourierTransform : E â†’ F

/--
The notation typeclass for the inverse Fourier transform.
-/
class FourierTransformInv (E : Type u) (F : outParam (Type v)) where
  /-- `ğ“•â» f` is the inverse Fourier transform of `f`. The meaning of this notation is
  type-dependent. -/
  fourierTransformInv : E â†’ F

namespace FourierTransform

@[inherit_doc] scoped notation "ğ“•" => FourierTransform.fourierTransform
@[inherit_doc] scoped notation "ğ“•â»" => FourierTransformInv.fourierTransformInv

end FourierTransform

section add

open FourierTransform

/-- A `FourierModule` is a function space on which the Fourier transform is a linear map. -/
class FourierModule (R : Type*) (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [SMul R E]
    [SMul R F] extends FourierTransform E F where
  fourier_add : âˆ€ (f g : E), ğ“• (f + g) = ğ“• f + ğ“• g
  fourier_smul : âˆ€ (r : R) (f : E), ğ“• (r â€¢ f) = r â€¢ ğ“• f

attribute [simp] FourierModule.fourier_add
attribute [simp] FourierModule.fourier_smul

/-- A `FourierInvModule` is a function space on which the Fourier transform is a linear map. -/
class FourierInvModule (R : Type*) (E : Type*) (F : outParam (Type*)) [Add E] [Add F] [SMul R E]
    [SMul R F] extends FourierTransformInv E F where
  fourierInv_add : âˆ€ (f g : E), ğ“•â» (f + g) = ğ“•â» f + ğ“•â» g
  fourierInv_smul : âˆ€ (r : R) (f : E), ğ“•â» (r â€¢ f) = r â€¢ ğ“•â» f

attribute [simp] FourierInvModule.fourierInv_add
attribute [simp] FourierInvModule.fourierInv_smul

variable {R E F : Type*} [Semiring R] [AddCommMonoid E] [AddCommMonoid F] [Module R E] [Module R F]

variable (R E F) [FourierModule R E F] in
/-- The Fourier transform as a linear map. -/
def fourierâ‚— : E â†’â‚—[R] F where
  toFun := ğ“•
  map_add' := FourierModule.fourier_add
  map_smul' := FourierModule.fourier_smul

@[simp]
lemma FourierTransform.fourier_zero [FourierModule R E F] : ğ“• (0 : E) = 0 :=
  (fourierâ‚— R E F).map_zero

variable (R E F) [FourierInvModule R E F] in
/-- The inverse Fourier transform as a linear map. -/
def fourierInvâ‚— : E â†’â‚—[R] F where
  toFun := ğ“•â»
  map_add' := FourierInvModule.fourierInv_add
  map_smul' := FourierInvModule.fourierInv_smul

@[simp]
lemma FourierTransform.fourierInv_zero [FourierInvModule R E F] : ğ“•â» (0 : E) = 0 :=
  (fourierInvâ‚— R E F).map_zero

end add

section pair

open FourierTransform

/-- A `FourierPair` is a pair of spaces `E` and `F` such that `ğ“•â» âˆ˜ ğ“• = id` on `E`. -/
class FourierPair (E F : Type*) extends FourierTransform E F, FourierTransformInv F E where
  inv_fourier : âˆ€ (f : E), ğ“•â» (ğ“• f) = f

/-- A `FourierPairInv` is a pair of spaces `E` and `F` such that `ğ“• âˆ˜ ğ“•â» = id` on `F`. -/
class FourierPairInv (E F : Type*) extends FourierTransform E F, FourierTransformInv F E where
  fourier_inv : âˆ€ (f : F), ğ“• (ğ“•â» f) = f

attribute [simp] FourierPair.inv_fourier
attribute [simp] FourierPairInv.fourier_inv

end pair
