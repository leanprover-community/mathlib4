/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.InnerProductSpace.Extend
import Mathlib.Analysis.Distribution.FourierSchwartz
import Mathlib.Analysis.Distribution.DenseLp
import Mathlib.Analysis.Distribution.TemperedDistribution

/-!

# The Fourier transform on L^2

-/

section FourierTransform

variable
  {V E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„‚ E] [CompleteSpace E]
  [NormedAddCommGroup V] [MeasurableSpace V] [BorelSpace V]

open SchwartzMap MeasureTheory FourierTransform

variable [InnerProductSpace â„ V] [FiniteDimensional â„ V]

variable (V E) in
noncomputable
def Lp.fourierTransformLI : (Lp (Î± := V) E 2) â‰ƒâ‚—áµ¢[â„‚] (Lp (Î± := V) E 2) :=
  (SchwartzMap.fourierTransformCLE â„‚ (V := V) (E := E)).toLinearEquiv.extendOfIsometry
    (SchwartzMap.toLpCLM â„‚ (E := V) E 2) (SchwartzMap.toLpCLM â„‚ (E := V) E 2)
    (LinearMap.ker_eq_bot_of_injective (injective_toLp _ _)) (by
        apply SchwartzMap.denseRange_toLpCLM
        exact Ne.symm ENNReal.top_ne_ofNat)
    (by apply norm_fourierTransform_toL2_eq)
    (LinearMap.ker_eq_bot_of_injective (injective_toLp _ _)) (by
        apply SchwartzMap.denseRange_toLpCLM
        exact Ne.symm ENNReal.top_ne_ofNat)
    (by
      intro f
      convert (norm_fourierTransform_toL2_eq (ğ“•â» f)).symm
      simp)

noncomputable
instance Lp.instFourierTransform : FourierTransform (Lp (Î± := V) E 2) (Lp (Î± := V) E 2) where
  fourierTransform := fourierTransformLI V E

noncomputable
instance Lp.instFourierTransformInv : FourierTransformInv (Lp (Î± := V) E 2) (Lp (Î± := V) E 2) where
  fourierTransformInv := (fourierTransformLI V E).symm

noncomputable
instance instFourierPair : FourierPair (Lp (Î± := V) E 2) (Lp (Î± := V) E 2) where
  inv_fourier := (Lp.fourierTransformLI V E).symm_apply_apply

noncomputable
instance instFourierPairInv : FourierPairInv (Lp (Î± := V) E 2) (Lp (Î± := V) E 2) where
  fourier_inv := (Lp.fourierTransformLI V E).apply_symm_apply

@[simp]
theorem toLp_fourierTransform_eq (f : ğ“¢(V, E)) : ğ“• (f.toLp 2) = (ğ“• f).toLp 2 := by
  apply LinearMap.extendOfNorm_eq (LinearMap.ker_eq_bot_of_injective (injective_toLp _ _))
  Â· apply SchwartzMap.denseRange_toLpCLM
    exact Ne.symm ENNReal.top_ne_ofNat
  use 1
  intro f
  rw [one_mul]
  exact (norm_fourierTransform_toL2_eq f).le

@[simp]
theorem toLp_fourierTransformInv_eq (f : ğ“¢(V, E)) : ğ“•â» (f.toLp 2) = (ğ“•â» f).toLp 2 := by
  apply LinearMap.extendOfNorm_eq (LinearMap.ker_eq_bot_of_injective (injective_toLp _ _))
  Â· apply SchwartzMap.denseRange_toLpCLM
    exact Ne.symm ENNReal.top_ne_ofNat
  use 1
  intro f
  rw [one_mul]
  convert (norm_fourierTransform_toL2_eq (ğ“•â» f)).symm.le
  simp


end FourierTransform


variable {E F V : Type*}
  [NormedAddCommGroup E] [MeasurableSpace E] [BorelSpace E]
  [InnerProductSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup F] [InnerProductSpace â„‚ F] [CompleteSpace F]
  [NormedAddCommGroup V] [NormedSpace â„‚ V] [CompleteSpace V]

open SchwartzMap MeasureTheory FourierTransform

variable (V) in
/-- The Fourier transform on `L^2` coincides with the Fourier transform on `ğ“¢'`. -/
theorem toTemperedDistribution_fourierTransform_eq (f : Lp (Î± := E) F 2) :
    ğ“• (f : ğ“¢'(â„‚, E, F â†’L[â„‚] V, V)) = (ğ“• f : Lp (Î± := E) F 2) := by
  set p := fun f : Lp (Î± := E) F 2 â†¦
    ğ“• (Lp.toTemperedDistribution â„‚ V f) =
      (Lp.toTemperedDistribution â„‚ V (ğ“• f))
  apply DenseRange.induction_on (p := p)
    ( SchwartzMap.denseRange_toLpCLM (F := F) (E := E) (Î¼ := volume) (p := 2)
      (Ne.symm ENNReal.top_ne_ofNat)) f
  Â· apply isClosed_eq
    Â· exact ((TemperedDistribution.fourierTransformCLM â„‚ E _ V) âˆ˜L
        (Lp.toTemperedDistributionCLM â„‚ F V volume 2)).cont
    Â· exact (Lp.toTemperedDistributionCLM â„‚ F V volume 2).cont.comp
        (Lp.fourierTransformLI E F).continuous
  intro f
  unfold p
  simp [TemperedDistribution.fourierTransformCLM_toTemperedDistributionCLM_eq]

variable (V) in
theorem toTemperedDistribution_fourierTransformInv_eq (f : Lp (Î± := E) F 2) :
    ğ“•â» (f : ğ“¢'(â„‚, E, F â†’L[â„‚] V, V)) = (ğ“•â» f : Lp (Î± := E) F 2) := by
  have := toTemperedDistribution_fourierTransform_eq V (ğ“•â» f)
  apply_fun ğ“•â» at this
  simp only [FourierPair.inv_fourier, FourierPairInv.fourier_inv] at this
  exact this.symm
