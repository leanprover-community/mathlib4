/-
Copyright (c) 2023 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.BigOperators.Expect
import Mathlib.Algebra.Group.AddChar
import Mathlib.Analysis.RCLike.Inner

/-!
# Orthogonality of characters of a finite abelian group

This file proves that characters of a finite abelian group are orthogonal, and in particular that
there are at most as many characters as there are elements of the group.
-/

open Finset hiding card
open Fintype (card)
open Function RCLike
open scoped BigOperators ComplexConjugate DirectSum

variable {G H R : Type*}

namespace AddChar
section AddGroup
variable [AddGroup G]

section Semifield
variable [Fintype G] [Semifield R] [IsDomain R] [CharZero R] {Ïˆ : AddChar G R}

lemma expect_eq_ite (Ïˆ : AddChar G R) : ğ”¼ a, Ïˆ a = if Ïˆ = 0 then 1 else 0 := by
  simp [Fintype.expect_eq_sum_div_card, sum_eq_ite, ite_div]

lemma expect_eq_zero_iff_ne_zero : ğ”¼ x, Ïˆ x = 0 â†” Ïˆ â‰  0 := by
  rw [expect_eq_ite, one_ne_zero.ite_eq_right_iff]

lemma expect_ne_zero_iff_eq_zero : ğ”¼ x, Ïˆ x â‰  0 â†” Ïˆ = 0 := expect_eq_zero_iff_ne_zero.not_left

end Semifield

section RCLike
variable [RCLike R] [Fintype G]

lemma wInner_cWeight_self (Ïˆ : AddChar G R) : âŸª(Ïˆ : G â†’ R), ÏˆâŸ«â‚™_[R] = 1 := by
  simp [wInner_cWeight_eq_expect, Ïˆ.norm_apply, RCLike.mul_conj]

end RCLike
end AddGroup

section AddCommGroup
variable [AddCommGroup G]

section RCLike
variable [RCLike R] {Ïˆâ‚ Ïˆâ‚‚ : AddChar G R}

lemma wInner_cWeight_eq_boole [Fintype G] (Ïˆâ‚ Ïˆâ‚‚ : AddChar G R) :
    âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«â‚™_[R] = if Ïˆâ‚ = Ïˆâ‚‚ then 1 else 0 := by
  split_ifs with h
  Â· rw [h, wInner_cWeight_self]
  have : Ïˆâ‚‚ * Ïˆâ‚â»Â¹  â‰  1 := by rwa [Ne, mul_inv_eq_one, eq_comm]
  simp_rw [wInner_cWeight_eq_expect, RCLike.inner_apply, â† inv_apply_eq_conj]
  simpa [map_neg_eq_inv] using expect_eq_zero_iff_ne_zero.2 this

lemma wInner_cWeight_eq_zero_iff_ne [Fintype G] : âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«â‚™_[R] = 0 â†” Ïˆâ‚ â‰  Ïˆâ‚‚ := by
  rw [wInner_cWeight_eq_boole, one_ne_zero.ite_eq_right_iff]

lemma wInner_cWeight_eq_one_iff_eq [Fintype G] : âŸª(Ïˆâ‚ : G â†’ R), Ïˆâ‚‚âŸ«â‚™_[R] = 1 â†” Ïˆâ‚ = Ïˆâ‚‚ := by
  rw [wInner_cWeight_eq_boole, one_ne_zero.ite_eq_left_iff]

variable (G R)

protected lemma linearIndependent [Finite G] : LinearIndependent R ((â‡‘) : AddChar G R â†’ G â†’ R) := by
  cases nonempty_fintype G
  exact linearIndependent_of_ne_zero_of_wInner_cWeight_eq_zero coe_ne_zero
    fun Ïˆâ‚ Ïˆâ‚‚ â†¦ wInner_cWeight_eq_zero_iff_ne.2

noncomputable instance instFintype [Finite G] : Fintype (AddChar G R) :=
  @Fintype.ofFinite _ (AddChar.linearIndependent G R).finite

@[simp] lemma card_addChar_le [Fintype G] : card (AddChar G R) â‰¤ card G := by
  simpa only [Module.finrank_fintype_fun_eq_card] using
    (AddChar.linearIndependent G R).fintype_card_le_finrank

end RCLike
end AddCommGroup
end AddChar
