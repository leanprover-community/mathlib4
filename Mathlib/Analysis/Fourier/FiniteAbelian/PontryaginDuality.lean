/-
Copyright (c) 2023 YaÃ«l Dillies, Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: YaÃ«l Dillies, Bhavik Mehta
-/
import Mathlib.Algebra.DirectSum.AddChar
import Mathlib.Analysis.Fourier.FiniteAbelian.Orthogonality
import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.GroupTheory.FiniteAbelian.Basic
import Mathlib.Topology.Instances.AddCircle.Real

/-!
# Pontryagin duality for finite abelian groups

This file proves the Pontryagin duality in case of finite abelian groups. This states that any
finite abelian group is canonically isomorphic to its double dual (the space of complex-valued
characters of its space of complex-valued characters).

We first prove it for `ZMod n` and then extend to all finite abelian groups using the
Structure Theorem.

## TODO

Reuse the work done in `Mathlib/GroupTheory/FiniteAbelian/Duality.lean`. This requires to write some
more glue.
-/

noncomputable section

open Circle Finset Function Module Multiplicative
open Fintype (card)
open Real hiding exp
open scoped BigOperators DirectSum

variable {Î± : Type*} [AddCommGroup Î±] {n : â„•} {a b : Î±}

namespace AddChar
variable (n : â„•) [NeZero n]

/-- Indexing of the complex characters of `ZMod n`. `AddChar.zmod n x` is the character sending `y`
to `e ^ (2 * Ï€ * i * x * y / n)`. -/
def zmod (x : ZMod n) : AddChar (ZMod n) Circle :=
  AddChar.compAddMonoidHom âŸ¨AddCircle.toCircle, AddCircle.toCircle_zero, AddCircle.toCircle_addâŸ© <|
    ZMod.toAddCircle.comp <| .mulLeft x

@[simp] lemma zmod_intCast (x y : â„¤) : zmod n x y = exp (2 * Ï€ * (x * y / n)) := by
  simp [zmod, â† Int.cast_mul x y, -Int.cast_mul, ZMod.toAddCircle_intCast,
    AddCircle.toCircle_apply_mk]

@[simp] lemma zmod_zero : zmod n 0 = 1 :=
  DFunLike.ext _ _ <| by simp [zmod]

variable {n}

@[simp] lemma zmod_add : âˆ€ x y : ZMod n, zmod n (x + y) = zmod n x * zmod n y := by
  simp [DFunLike.ext_iff, zmod, add_mul, map_add_eq_mul]

lemma zmod_injective : Injective (zmod n) := by
  simp_rw [Injective, ZMod.intCast_surjective.forall]
  rintro x y h
  have hn : (n : â„) â‰  0 := NeZero.ne _
  simpa [pi_ne_zero, exp_inj, hn, CharP.intCast_eq_intCast (ZMod n) n] using
    (zmod_intCast ..).symm.trans <| (DFunLike.congr_fun h ((1 : â„¤) : ZMod n)).trans <|
      zmod_intCast ..

@[simp] lemma zmod_inj {x y : ZMod n} : zmod n x = zmod n y â†” x = y := zmod_injective.eq_iff

/-- `AddChar.zmod` bundled as an `AddChar`. -/
def zmodHom : AddChar (ZMod n) (AddChar (ZMod n) Circle) where
  toFun := zmod n
  map_zero_eq_one' := by simp
  map_add_eq_mul' := by simp

/-- Character on a product of `ZMod`s given by `x â†¦ âˆ i, e ^ (2 * Ï€ * I * x i * y / n)`. -/
private def mkZModAux {Î¹ : Type} [DecidableEq Î¹] (n : Î¹ â†’ â„•) [âˆ€ i, NeZero (n i)]
    (u : âˆ€ i, ZMod (n i)) : AddChar (â¨ i, ZMod (n i)) Circle :=
  AddChar.directSum fun i â†¦ zmod (n i) (u i)

private lemma mkZModAux_injective {Î¹ : Type} [DecidableEq Î¹] {n : Î¹ â†’ â„•} [âˆ€ i, NeZero (n i)] :
    Injective (mkZModAux n) :=
  AddChar.directSum_injective.comp fun f g h â†¦ by simpa [funext_iff] using h

/-- The circle-valued characters of a finite abelian group are the same as its complex-valued
characters. -/
def circleEquivComplex [Finite Î±] : AddChar Î± Circle â‰ƒ+ AddChar Î± â„‚ where
  toFun Ïˆ := toMonoidHomEquiv.symm <| coeHom.comp Ïˆ.toMonoidHom
  invFun Ïˆ :=
    { toFun := fun a â†¦ (âŸ¨Ïˆ a, mem_sphere_zero_iff_norm.2 <| Ïˆ.norm_apply _âŸ© : Circle)
      map_zero_eq_one' := by simp [Circle]
      map_add_eq_mul' := fun a b â†¦ by ext : 1; simp [map_add_eq_mul] }
  left_inv Ïˆ := by ext : 1; simp
  right_inv Ïˆ := by ext : 1; simp
  map_add' Ïˆ Ï‡ := rfl

@[simp] lemma card_eq [Fintype Î±] : card (AddChar Î± â„‚) = card Î± := by
  obtain âŸ¨Î¹, _, n, hn, âŸ¨eâŸ©âŸ© := AddCommGroup.equiv_directSum_zmod_of_finite' Î±
  classical
  have hn' i : NeZero (n i) := by have := hn i; exact âŸ¨by positivityâŸ©
  let f : Î± â†’ AddChar Î± â„‚ := fun a â†¦ coeHom.compAddChar ((mkZModAux n <| e a).compAddMonoidHom e)
  have hf : Injective f := circleEquivComplex.injective.comp
    ((compAddMonoidHom_injective_left _ e.surjective).comp <| mkZModAux_injective.comp <|
      DFunLike.coe_injective.comp <| e.injective.comp Additive.ofMul.injective)
  exact (card_addChar_le _ _).antisymm (Fintype.card_le_of_injective _ hf)

/-- `ZMod n` is (noncanonically) isomorphic to its group of characters. -/
def zmodAddEquiv : ZMod n â‰ƒ+ AddChar (ZMod n) â„‚ := by
  refine AddEquiv.ofBijective
    (circleEquivComplex.toAddMonoidHom.comp <| AddChar.toAddMonoidHom zmodHom) ?_
  rw [Fintype.bijective_iff_injective_and_card, card_eq]
  exact âŸ¨circleEquivComplex.injective.comp zmod_injective, rflâŸ©

@[simp] lemma zmodAddEquiv_apply (x : ZMod n) :
    zmodAddEquiv x = circleEquivComplex (zmod n x) := rfl

section Finite
variable (Î±) [Finite Î±]

/-- Complex-valued characters of a finite abelian group `Î±` form a basis of `Î± â†’ â„‚`. -/
def complexBasis : Basis (AddChar Î± â„‚) â„‚ (Î± â†’ â„‚) :=
  basisOfLinearIndependentOfCardEqFinrank (AddChar.linearIndependent _ _) <| by
    cases nonempty_fintype Î±; rw [card_eq, Module.finrank_fintype_fun_eq_card]

@[simp, norm_cast]
lemma coe_complexBasis : â‡‘(complexBasis Î±) = ((â‡‘) : AddChar Î± â„‚ â†’ Î± â†’ â„‚) := by
  rw [complexBasis, coe_basisOfLinearIndependentOfCardEqFinrank]

variable {Î±}

@[simp]
lemma complexBasis_apply (Ïˆ : AddChar Î± â„‚) : complexBasis Î± Ïˆ = Ïˆ := by rw [coe_complexBasis]

lemma exists_apply_ne_zero : (âˆƒ Ïˆ : AddChar Î± â„‚, Ïˆ a â‰  1) â†” a â‰  0 := by
  refine âŸ¨?_, fun ha â†¦ ?_âŸ©
  Â· rintro âŸ¨Ïˆ, hÏˆâŸ© rfl
    exact hÏˆ Ïˆ.map_zero_eq_one
  classical
  by_contra! h
  let f : Î± â†’ â„‚ := fun b â†¦ if a = b then 1 else 0
  have hâ‚€ := congr_fun ((complexBasis Î±).sum_repr f) 0
  have hâ‚ := congr_fun ((complexBasis Î±).sum_repr f) a
  simp only [complexBasis_apply, Fintype.sum_apply, Pi.smul_apply, h, smul_eq_mul, mul_one,
    map_zero_eq_one, if_pos rfl, if_neg ha, f] at hâ‚€ hâ‚
  exact one_ne_zero (hâ‚.symm.trans hâ‚€)

lemma forall_apply_eq_zero : (âˆ€ Ïˆ : AddChar Î± â„‚, Ïˆ a = 1) â†” a = 0 := by
  simpa using exists_apply_ne_zero.not

lemma doubleDualEmb_injective : Injective (doubleDualEmb : Î± â†’ AddChar (AddChar Î± â„‚) â„‚) :=
  doubleDualEmb.ker_eq_bot_iff.1 <| eq_bot_iff.2 fun a ha â†¦
    forall_apply_eq_zero.1 fun Ïˆ â†¦ by simpa using DFunLike.congr_fun ha (Additive.ofMul Ïˆ)

lemma doubleDualEmb_bijective : Bijective (doubleDualEmb : Î± â†’ AddChar (AddChar Î± â„‚) â„‚) := by
  cases nonempty_fintype Î±
  exact (Fintype.bijective_iff_injective_and_card _).2
    âŸ¨doubleDualEmb_injective, card_eq.symm.trans card_eq.symmâŸ©

@[simp]
lemma doubleDualEmb_inj : (doubleDualEmb a : AddChar (AddChar Î± â„‚) â„‚) = doubleDualEmb b â†” a = b :=
  doubleDualEmb_injective.eq_iff

@[simp] lemma doubleDualEmb_eq_zero : (doubleDualEmb a : AddChar (AddChar Î± â„‚) â„‚) = 0 â†” a = 0 := by
  rw [â† map_zero doubleDualEmb, doubleDualEmb_inj]

lemma doubleDualEmb_ne_zero : (doubleDualEmb a : AddChar (AddChar Î± â„‚) â„‚) â‰  0 â†” a â‰  0 :=
  doubleDualEmb_eq_zero.not

/-- The double dual isomorphism of a finite abelian group. -/
def doubleDualEquiv : Î± â‰ƒ+ AddChar (AddChar Î± â„‚) â„‚ := .ofBijective _ doubleDualEmb_bijective

@[simp]
lemma coe_doubleDualEquiv : â‡‘(doubleDualEquiv : Î± â‰ƒ+ AddChar (AddChar Î± â„‚) â„‚) = doubleDualEmb := rfl

@[simp] lemma doubleDualEmb_doubleDualEquiv_symm_apply (a : AddChar (AddChar Î± â„‚) â„‚) :
    doubleDualEmb (doubleDualEquiv.symm a) = a :=
  doubleDualEquiv.apply_symm_apply _

@[simp] lemma doubleDualEquiv_symm_doubleDualEmb_apply (a : AddChar (AddChar Î± â„‚) â„‚) :
    doubleDualEquiv.symm (doubleDualEmb a) = a := doubleDualEquiv.symm_apply_apply _

end Finite

lemma sum_apply_eq_ite [Fintype Î±] [DecidableEq Î±] (a : Î±) :
    âˆ‘ Ïˆ : AddChar Î± â„‚, Ïˆ a = if a = 0 then (Fintype.card Î± : â„‚) else 0 := by
  simpa using sum_eq_ite (doubleDualEmb a : AddChar (AddChar Î± â„‚) â„‚)

lemma expect_apply_eq_ite [Finite Î±] [DecidableEq Î±] (a : Î±) :
    ð”¼ Ïˆ : AddChar Î± â„‚, Ïˆ a = if a = 0 then 1 else 0 := by
  simpa using expect_eq_ite (doubleDualEmb a : AddChar (AddChar Î± â„‚) â„‚)

lemma sum_apply_eq_zero_iff_ne_zero [Finite Î±] : âˆ‘ Ïˆ : AddChar Î± â„‚, Ïˆ a = 0 â†” a â‰  0 := by
  classical
  cases nonempty_fintype Î±
  rw [sum_apply_eq_ite, Ne.ite_eq_right_iff]
  exact Nat.cast_ne_zero.2 Fintype.card_ne_zero

lemma sum_apply_ne_zero_iff_eq_zero [Finite Î±] : âˆ‘ Ïˆ : AddChar Î± â„‚, Ïˆ a â‰  0 â†” a = 0 :=
  sum_apply_eq_zero_iff_ne_zero.not_left

lemma expect_apply_eq_zero_iff_ne_zero [Finite Î±] : ð”¼ Ïˆ : AddChar Î± â„‚, Ïˆ a = 0 â†” a â‰  0 := by
  classical
  cases nonempty_fintype Î±
  rw [expect_apply_eq_ite, one_ne_zero.ite_eq_right_iff]

lemma expect_apply_ne_zero_iff_eq_zero [Finite Î±] : ð”¼ Ïˆ : AddChar Î± â„‚, Ïˆ a â‰  0 â†” a = 0 :=
  expect_apply_eq_zero_iff_ne_zero.not_left

end AddChar
