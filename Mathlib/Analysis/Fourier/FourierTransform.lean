/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Algebra.Group.AddChar
import Mathlib.Analysis.Complex.Circle
import Mathlib.Analysis.Fourier.Notation
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.MeasureTheory.Integral.Prod
import Mathlib.MeasureTheory.Integral.Bochner.Set
import Mathlib.MeasureTheory.Measure.Haar.InnerProductSpace
import Mathlib.MeasureTheory.Measure.Haar.OfBasis

/-!
# The Fourier transform

We set up the Fourier transform for complex-valued functions on finite-dimensional spaces.

## Design choices

In namespace `VectorFourier`, we define the Fourier integral in the following context:
* `ğ•œ` is a commutative ring.
* `V` and `W` are `ğ•œ`-modules.
* `e` is a unitary additive character of `ğ•œ`, i.e. an `AddChar ğ•œ Circle`.
* `Î¼` is a measure on `V`.
* `L` is a `ğ•œ`-bilinear form `V Ã— W â†’ ğ•œ`.
* `E` is a complete normed `â„‚`-vector space.

With these definitions, we define `fourierIntegral` to be the map from functions `V â†’ E` to
functions `W â†’ E` that sends `f` to

`fun w â†¦ âˆ« v in V, e (-L v w) â€¢ f v âˆ‚Î¼`,

This includes the cases `W` is the dual of `V` and `L` is the canonical pairing, or `W = V` and `L`
is a bilinear form (e.g. an inner product).

In namespace `Fourier`, we consider the more familiar special case when `V = W = ğ•œ` and `L` is the
multiplication map (but still allowing `ğ•œ` to be an arbitrary ring equipped with a measure).

The most familiar case of all is when `V = W = ğ•œ = â„`, `L` is multiplication, `Î¼` is volume, and
`e` is `Real.fourierChar`, i.e. the character `fun x â†¦ exp ((2 * Ï€ * x) * I)` (for which we
introduced the notation `ğ` in the scope `FourierTransform`).

Another familiar case (which generalizes the previous one) is when `V = W` is an inner product space
over `â„` and `L` is the scalar product. We introduce two notations `ğ“•` for the Fourier transform in
this case and `ğ“•â» f (v) = ğ“• f (-v)` for the inverse Fourier transform. These notations make
in particular sense for `V = W = â„`.

## Main results

At present the only nontrivial lemma we prove is `fourierIntegral_continuous`, stating that the
Fourier transform of an integrable function is continuous (under mild assumptions).
-/


noncomputable section

local notation "ğ•Š" => Circle

open MeasureTheory Filter

open scoped Topology

/-! ## Fourier theory for functions on general vector spaces -/

namespace VectorFourier

variable {ğ•œ : Type*} [CommRing ğ•œ] {V : Type*} [AddCommGroup V] [Module ğ•œ V] [MeasurableSpace V]
  {W : Type*} [AddCommGroup W] [Module ğ•œ W]
  {E F G : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedAddCommGroup F] [NormedSpace â„‚ F]
  [NormedAddCommGroup G] [NormedSpace â„‚ G]

section Defs

/-- The Fourier transform integral for `f : V â†’ E`, with respect to a bilinear form `L : V Ã— W â†’ ğ•œ`
and an additive character `e`. -/
def fourierIntegral (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure V) (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E)
    (w : W) : E :=
  âˆ« v, e (-L v w) â€¢ f v âˆ‚Î¼

theorem fourierIntegral_const_smul (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure V)
    (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (r : â„‚) :
    fourierIntegral e Î¼ L (r â€¢ f) = r â€¢ fourierIntegral e Î¼ L f := by
  ext1 w
  simp only [Pi.smul_apply, fourierIntegral, smul_comm _ r, integral_smul]

/-- The uniform norm of the Fourier integral of `f` is bounded by the `LÂ¹` norm of `f`. -/
theorem norm_fourierIntegral_le_integral_norm (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure V)
    (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (w : W) :
    â€–fourierIntegral e Î¼ L f wâ€– â‰¤ âˆ« v : V, â€–f vâ€– âˆ‚Î¼ := by
  refine (norm_integral_le_integral_norm _).trans (le_of_eq ?_)
  simp_rw [Circle.norm_smul]

/-- The Fourier integral converts right-translation into scalar multiplication by a phase factor. -/
theorem fourierIntegral_comp_add_right [MeasurableAdd V] (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure V)
    [Î¼.IsAddRightInvariant] (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (vâ‚€ : V) :
    fourierIntegral e Î¼ L (f âˆ˜ fun v â†¦ v + vâ‚€) =
      fun w â†¦ e (L vâ‚€ w) â€¢ fourierIntegral e Î¼ L f w := by
  ext1 w
  dsimp only [fourierIntegral, Function.comp_apply, Circle.smul_def]
  conv in L _ => rw [â† add_sub_cancel_right v vâ‚€]
  rw [integral_add_right_eq_self fun v : V â†¦ (e (-L (v - vâ‚€) w) : â„‚) â€¢ f v, â† integral_smul]
  congr 1 with v
  rw [â† smul_assoc, smul_eq_mul, â† Circle.coe_mul, â† e.map_add_eq_mul, â† LinearMap.neg_apply,
    â† sub_eq_add_neg, â† LinearMap.sub_apply, LinearMap.map_sub, neg_sub]

end Defs

section Continuous

/-!
In this section we assume ğ•œ, `V`, `W` have topologies,
and `L`, `e` are continuous (but `f` needn't be).

This is used to ensure that `e (-L v w)` is (a.e. strongly) measurable. We could get away with
imposing only a measurable-space structure on ğ•œ (it doesn't have to be the Borel sigma-algebra of
a topology); but it seems hard to imagine cases where this extra generality would be useful, and
allowing it would complicate matters in the most important use cases.
-/
variable [TopologicalSpace ğ•œ] [IsTopologicalRing ğ•œ] [TopologicalSpace V] [BorelSpace V]
  [TopologicalSpace W] {e : AddChar ğ•œ ğ•Š} {Î¼ : Measure V} {L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ}

/-- For any `w`, the Fourier integral is convergent iff `f` is integrable. -/
theorem fourierIntegral_convergent_iff (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) {f : V â†’ E} (w : W) :
    Integrable (fun v : V â†¦ e (-L v w) â€¢ f v) Î¼ â†” Integrable f Î¼ := by
  -- first prove one-way implication
  have aux {g : V â†’ E} (hg : Integrable g Î¼) (x : W) :
      Integrable (fun v : V â†¦ e (-L v x) â€¢ g v) Î¼ := by
    have c : Continuous fun v â†¦ e (-L v x) := he.comp (hL.comp (.prodMk_left _)).neg
    simp_rw [â† integrable_norm_iff (c.aestronglyMeasurable.smul hg.1), Circle.norm_smul]
    exact hg.norm
  -- then use it for both directions
  refine âŸ¨fun hf â†¦ ?_, fun hf â†¦ aux hf wâŸ©
  have := aux hf (-w)
  simp_rw [â† mul_smul (e _) (e _) (f _), â† e.map_add_eq_mul, LinearMap.map_neg, neg_add_cancel,
    e.map_zero_eq_one, one_smul] at this -- the `(e _)` speeds up elaboration considerably
  exact this

theorem fourierIntegral_add (he : Continuous e) (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2)
    {f g : V â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    fourierIntegral e Î¼ L (f + g) = fourierIntegral e Î¼ L f + fourierIntegral e Î¼ L g := by
  ext1 w
  dsimp only [Pi.add_apply, fourierIntegral]
  simp_rw [smul_add]
  rw [integral_add]
  Â· exact (fourierIntegral_convergent_iff he hL w).2 hf
  Â· exact (fourierIntegral_convergent_iff he hL w).2 hg

/-- The Fourier integral of an `L^1` function is a continuous function. -/
theorem fourierIntegral_continuous [FirstCountableTopology W] (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) {f : V â†’ E} (hf : Integrable f Î¼) :
    Continuous (fourierIntegral e Î¼ L f) := by
  apply continuous_of_dominated
  Â· exact fun w â†¦ ((fourierIntegral_convergent_iff he hL w).2 hf).1
  Â· exact fun w â†¦ ae_of_all _ fun v â†¦ le_of_eq (Circle.norm_smul _ _)
  Â· exact hf.norm
  Â· refine ae_of_all _ fun v â†¦ (he.comp ?_).smul continuous_const
    exact (hL.comp (.prodMk_right _)).neg

end Continuous

section Fubini

variable [TopologicalSpace ğ•œ] [IsTopologicalRing ğ•œ] [TopologicalSpace V] [BorelSpace V]
  [TopologicalSpace W] [MeasurableSpace W] [BorelSpace W]
  {e : AddChar ğ•œ ğ•Š} {Î¼ : Measure V} {L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ}
  {Î½ : Measure W} [SigmaFinite Î¼] [SigmaFinite Î½] [SecondCountableTopology V]

variable {Ïƒ : â„‚ â†’+* â„‚} [RingHomIsometric Ïƒ]

/-- Fubini's theorem for the Fourier integral.

This is the main technical step in proving both Parseval's identity and self-adjointness of the
Fourier transform. -/
theorem integral_fourierIntegral_swap
    {f : V â†’ E} {g : W â†’ F} (M : F â†’L[â„‚] E â†’SL[Ïƒ] G) (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    âˆ« Î¾, (âˆ« x, M (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î¼) âˆ‚Î½ =
    âˆ« x, (âˆ« Î¾, M (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î½) âˆ‚Î¼ := by
  rw [integral_integral_swap]
  have : Integrable (fun (p : W Ã— V) â†¦ â€–Mâ€– * (â€–g p.1â€– * â€–f p.2â€–)) (Î½.prod Î¼) :=
    (hg.norm.mul_prod hf.norm).const_mul _
  apply this.mono
  Â· change AEStronglyMeasurable (fun p : W Ã— V â†¦ (M (g p.1) (e (-(L p.2) p.1) â€¢ f p.2) )) _
    have A : AEStronglyMeasurable (fun (p : W Ã— V) â†¦ e (-L p.2 p.1) â€¢ f p.2) (Î½.prod Î¼) := by
      refine (Continuous.aestronglyMeasurable ?_).smul hf.1.comp_snd
      exact he.comp (hL.comp continuous_swap).neg
    have A' : AEStronglyMeasurable (fun p â†¦ (g p.1, e (-(L p.2) p.1) â€¢ f p.2) : W Ã— V â†’ F Ã— E)
      (Measure.prod Î½ Î¼) := hg.1.comp_fst.prodMk A
    have hM : Continuous (fun q â†¦ M q.1 q.2 : F Ã— E â†’ G) :=
      -- There is no `Continuous.clm_apply` for semilinear continuous maps
      (M.flip.cont.comp continuous_snd).clm_apply continuous_fst
    apply hM.comp_aestronglyMeasurable A' -- `exact` works, but `apply` is 10x faster!
  Â· filter_upwards with âŸ¨Î¾, xâŸ©
    simp only [Function.uncurry_apply_pair, norm_mul, norm_norm, ge_iff_le, â† mul_assoc]
    convert M.le_opNormâ‚‚ (g Î¾) (e (-L x Î¾) â€¢ f x) using 2
    simp

variable [CompleteSpace E] [CompleteSpace F]
/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourierIntegral_eq_flip
    {f : V â†’ E} {g : W â†’ F} (M : E â†’L[â„‚] F â†’L[â„‚] G) (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    âˆ« Î¾, M (fourierIntegral e Î¼ L f Î¾) (g Î¾) âˆ‚Î½ =
      âˆ« x, M (f x) (fourierIntegral e Î½ L.flip g x) âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G; swap; Â· simp [integral, hG]
  calc
  âˆ« Î¾, M.flip (g Î¾) (âˆ« x, e (-L x Î¾) â€¢ f x âˆ‚Î¼) âˆ‚Î½
    = âˆ« Î¾, (âˆ« x, M.flip (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î¼) âˆ‚Î½ := by
    congr with Î¾
    apply (ContinuousLinearMap.integral_comp_comm _ _).symm
    exact (fourierIntegral_convergent_iff he hL _).2 hf
  _ = âˆ« x, (âˆ« Î¾, M.flip (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î½) âˆ‚Î¼ :=
    integral_fourierIntegral_swap M.flip he hL hf hg
  _ = âˆ« x, (âˆ« Î¾, M (f x) (e (-L.flip Î¾ x) â€¢ g Î¾) âˆ‚Î½) âˆ‚Î¼ := by
    simp only [ContinuousLinearMap.flip_apply, ContinuousLinearMap.map_smul_of_tower,
      ContinuousLinearMap.coe_smul', Pi.smul_apply, LinearMap.flip_apply]
  _ = âˆ« x, M (f x) (âˆ« Î¾, e (-L.flip Î¾ x) â€¢ g Î¾ âˆ‚Î½) âˆ‚Î¼ := by
    congr with x
    apply ContinuousLinearMap.integral_comp_comm
    apply (fourierIntegral_convergent_iff he _ _).2 hg
    exact hL.comp continuous_swap

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourierIntegral_smul_eq_flip
    {f : V â†’ â„‚} {g : W â†’ F} (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    âˆ« Î¾, (fourierIntegral e Î¼ L f Î¾) â€¢ (g Î¾) âˆ‚Î½ =
      âˆ« x, (f x) â€¢ (fourierIntegral e Î½ L.flip g x) âˆ‚Î¼ :=
  integral_bilin_fourierIntegral_eq_flip (ContinuousLinearMap.lsmul â„‚ â„‚) he hL hf hg

/-- The Fourier transform satisfies `âˆ« ğ“• f * conj g = âˆ« f * conj (ğ“•â»Â¹ g)`, which together
with the Fourier inversion theorem yields Plancherel's theorem. The stated version is more
convenient since it does only require integrability of `f` and `g`.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_sesq_fourierIntegral_eq_neg_flip
    {f : V â†’ E} {g : W â†’ F} (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) (he : Continuous e)
    (hL : Continuous fun p : V Ã— W â†¦ L p.1 p.2) (hf : Integrable f Î¼) (hg : Integrable g Î½) :
    âˆ« Î¾, M (fourierIntegral e Î¼ L f Î¾) (g Î¾) âˆ‚Î½ =
      âˆ« x, M (f x) (fourierIntegral e Î½ (-L.flip) g x) âˆ‚Î¼ := by
  by_cases hG : CompleteSpace G; swap; Â· simp [integral, hG]
  calc
  âˆ« Î¾, M.flip (g Î¾) (âˆ« x, e (-L x Î¾) â€¢ f x âˆ‚Î¼) âˆ‚Î½
    = âˆ« Î¾, (âˆ« x, M.flip (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î¼) âˆ‚Î½ := by
    congr with Î¾
    apply (ContinuousLinearMap.integral_comp_commSL RCLike.conj_smul _ _).symm
    exact (fourierIntegral_convergent_iff he hL _).2 hf
  _ = âˆ« x, (âˆ« Î¾, M.flip (g Î¾) (e (-L x Î¾) â€¢ f x) âˆ‚Î½) âˆ‚Î¼ :=
    integral_fourierIntegral_swap M.flip he hL hf hg
  _ = âˆ« x, (âˆ« Î¾, M (f x) (e (L.flip Î¾ x) â€¢ g Î¾) âˆ‚Î½) âˆ‚Î¼ := by
    congr with x
    congr with Î¾
    rw [â† smul_one_smul â„‚ _ (f x), â† smul_one_smul â„‚ _ (g Î¾)]
    simp only [map_smulâ‚›â‚—, ContinuousLinearMap.flip_apply, LinearMap.flip_apply, RingHom.id_apply,
      Circle.smul_def, smul_eq_mul, mul_one, â† Circle.coe_inv_eq_conj, AddChar.map_neg_eq_inv,
      inv_inv]
  _ = âˆ« x, (âˆ« Î¾, M (f x) (e (-(-L.flip Î¾) x) â€¢ g Î¾) âˆ‚Î½) âˆ‚Î¼ := by
    simp only [LinearMap.flip_apply, ContinuousLinearMap.map_smul_of_tower, LinearMap.neg_apply,
      neg_neg]
  _ = âˆ« x, M (f x) (âˆ« Î¾, e (-(-L.flip Î¾) x) â€¢ g Î¾ âˆ‚Î½) âˆ‚Î¼ := by
    congr with x
    apply ContinuousLinearMap.integral_comp_comm
    have hLflip : Continuous fun (p : W Ã— V) => (-L.flip p.1) p.2 :=
      (continuous_neg.comp hL).comp continuous_swap
    exact (fourierIntegral_convergent_iff (L := -L.flip) he hLflip x).2 hg

end Fubini

lemma fourierIntegral_probChar {V W : Type*} {_ : MeasurableSpace V}
    [AddCommGroup V] [Module â„ V] [AddCommGroup W] [Module â„ W]
    (L : V â†’â‚—[â„] W â†’â‚—[â„] â„) (Î¼ : Measure V) (f : V â†’ E) (w : W) :
    fourierIntegral Real.probChar Î¼ L f w =
      âˆ« v : V, Complex.exp (- L v w * Complex.I) â€¢ f v âˆ‚Î¼ := by
  simp_rw [fourierIntegral, Circle.smul_def, Real.probChar_apply, Complex.ofReal_neg]

end VectorFourier

namespace VectorFourier

variable {ğ•œ Î¹ E F V W : Type*} [Fintype Î¹] [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup V] [NormedSpace ğ•œ V] [MeasurableSpace V] [BorelSpace V]
  [NormedAddCommGroup W] [NormedSpace ğ•œ W]
  {e : AddChar ğ•œ ğ•Š} {Î¼ : Measure V} {L : V â†’L[ğ•œ] W â†’L[ğ•œ] ğ•œ}
  [NormedAddCommGroup F] [NormedSpace â„ F]
  [NormedAddCommGroup E] [NormedSpace â„‚ E]
  {M : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace â„ (M i)]

theorem fourierIntegral_continuousLinearMap_apply
    {f : V â†’ (F â†’L[â„] E)} {a : F} {w : W} (he : Continuous e) (hf : Integrable f Î¼) :
    fourierIntegral e Î¼ L.toLinearMapâ‚â‚‚ f w a =
      fourierIntegral e Î¼ L.toLinearMapâ‚â‚‚ (fun x â†¦ f x a) w := by
  rw [fourierIntegral, ContinuousLinearMap.integral_apply]
  Â· rfl
  Â· apply (fourierIntegral_convergent_iff he _ _).2 hf
    exact L.continuousâ‚‚

theorem fourierIntegral_continuousMultilinearMap_apply
    {f : V â†’ (ContinuousMultilinearMap â„ M E)} {m : (i : Î¹) â†’ M i} {w : W} (he : Continuous e)
    (hf : Integrable f Î¼) :
    fourierIntegral e Î¼ L.toLinearMapâ‚â‚‚ f w m =
      fourierIntegral e Î¼ L.toLinearMapâ‚â‚‚ (fun x â†¦ f x m) w := by
  rw [fourierIntegral, ContinuousMultilinearMap.integral_apply]
  Â· rfl
  Â· apply (fourierIntegral_convergent_iff he _ _).2 hf
    exact L.continuousâ‚‚

end VectorFourier


/-! ## Fourier theory for functions on `ğ•œ` -/


namespace Fourier

variable {ğ•œ : Type*} [CommRing ğ•œ] [MeasurableSpace ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace â„‚ E]

section Defs

/-- The Fourier transform integral for `f : ğ•œ â†’ E`, with respect to the measure `Î¼` and additive
character `e`. -/
def fourierIntegral (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (w : ğ•œ) : E :=
  VectorFourier.fourierIntegral e Î¼ (LinearMap.mul ğ•œ ğ•œ) f w

theorem fourierIntegral_def (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (w : ğ•œ) :
    fourierIntegral e Î¼ f w = âˆ« v : ğ•œ, e (-(v * w)) â€¢ f v âˆ‚Î¼ :=
  rfl

theorem fourierIntegral_const_smul (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (r : â„‚) :
    fourierIntegral e Î¼ (r â€¢ f) = r â€¢ fourierIntegral e Î¼ f :=
  VectorFourier.fourierIntegral_const_smul _ _ _ _ _

/-- The uniform norm of the Fourier transform of `f` is bounded by the `LÂ¹` norm of `f`. -/
theorem norm_fourierIntegral_le_integral_norm (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure ğ•œ)
    (f : ğ•œ â†’ E) (w : ğ•œ) : â€–fourierIntegral e Î¼ f wâ€– â‰¤ âˆ« x : ğ•œ, â€–f xâ€– âˆ‚Î¼ :=
  VectorFourier.norm_fourierIntegral_le_integral_norm _ _ _ _ _

/-- The Fourier transform converts right-translation into scalar multiplication by a phase
factor. -/
theorem fourierIntegral_comp_add_right [MeasurableAdd ğ•œ] (e : AddChar ğ•œ ğ•Š) (Î¼ : Measure ğ•œ)
    [Î¼.IsAddRightInvariant] (f : ğ•œ â†’ E) (vâ‚€ : ğ•œ) :
    fourierIntegral e Î¼ (f âˆ˜ fun v â†¦ v + vâ‚€) = fun w â†¦ e (vâ‚€ * w) â€¢ fourierIntegral e Î¼ f w :=
  VectorFourier.fourierIntegral_comp_add_right _ _ _ _ _

end Defs

end Fourier

open scoped Real

namespace Real

open FourierTransform

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

theorem vector_fourierIntegral_eq_integral_exp_smul {V : Type*} [AddCommGroup V] [Module â„ V]
    [MeasurableSpace V] {W : Type*} [AddCommGroup W] [Module â„ W] (L : V â†’â‚—[â„] W â†’â‚—[â„] â„)
    (Î¼ : Measure V) (f : V â†’ E) (w : W) :
    VectorFourier.fourierIntegral fourierChar Î¼ L f w =
      âˆ« v : V, Complex.exp (â†‘(-2 * Ï€ * L v w) * Complex.I) â€¢ f v âˆ‚Î¼ := by
  simp_rw [VectorFourier.fourierIntegral, Circle.smul_def, Real.fourierChar_apply, mul_neg,
    neg_mul]

/-- The Fourier integral is well defined iff the function is integrable. Version with a general
continuous bilinear function `L`. For the specialization to the inner product in an inner product
space, see `Real.fourierIntegral_convergent_iff`. -/
@[simp]
theorem fourierIntegral_convergent_iff' {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
    [NormedAddCommGroup W] [NormedSpace â„ W] [MeasurableSpace V] [BorelSpace V] {Î¼ : Measure V}
    {f : V â†’ E} (L : V â†’L[â„] W â†’L[â„] â„) (w : W) :
    Integrable (fun v : V â†¦ ğ (- L v w) â€¢ f v) Î¼ â†” Integrable f Î¼ :=
  VectorFourier.fourierIntegral_convergent_iff (E := E) (L := L.toLinearMapâ‚â‚‚)
    continuous_fourierChar L.continuousâ‚‚ _

section Apply

variable {Î¹ F V W : Type*} [Fintype Î¹]
  [NormedAddCommGroup V] [NormedSpace â„ V] [MeasurableSpace V] [BorelSpace V]
  [NormedAddCommGroup W] [NormedSpace â„ W]
  {Î¼ : Measure V} {L : V â†’L[â„] W â†’L[â„] â„}
  [NormedAddCommGroup F] [NormedSpace â„ F]
  {M : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace â„ (M i)]

theorem fourierIntegral_continuousLinearMap_apply'
    {f : V â†’ (F â†’L[â„] E)} {a : F} {w : W} (hf : Integrable f Î¼) :
    VectorFourier.fourierIntegral ğ Î¼ L.toLinearMapâ‚â‚‚ f w a =
      VectorFourier.fourierIntegral ğ Î¼ L.toLinearMapâ‚â‚‚ (fun x â†¦ f x a) w :=
  VectorFourier.fourierIntegral_continuousLinearMap_apply continuous_fourierChar hf

theorem fourierIntegral_continuousMultilinearMap_apply'
    {f : V â†’ ContinuousMultilinearMap â„ M E} {m : (i : Î¹) â†’ M i} {w : W} (hf : Integrable f Î¼) :
    VectorFourier.fourierIntegral ğ Î¼ L.toLinearMapâ‚â‚‚ f w m =
      VectorFourier.fourierIntegral ğ Î¼ L.toLinearMapâ‚â‚‚ (fun x â†¦ f x m) w :=
  VectorFourier.fourierIntegral_continuousMultilinearMap_apply continuous_fourierChar hf

end Apply

variable {V : Type*} [NormedAddCommGroup V]
  [InnerProductSpace â„ V] [MeasurableSpace V] [BorelSpace V]
  {W : Type*} [NormedAddCommGroup W]
  [InnerProductSpace â„ W] [MeasurableSpace W] [BorelSpace W] [FiniteDimensional â„ W]

open scoped RealInnerProductSpace

@[simp] theorem fourierIntegral_convergent_iff {Î¼ : Measure V} {f : V â†’ E} (w : V) :
    Integrable (fun v : V â†¦ ğ (- âŸªv, wâŸ«) â€¢ f v) Î¼ â†” Integrable f Î¼ :=
  fourierIntegral_convergent_iff' (innerSL â„) w

variable [FiniteDimensional â„ V]

instance FourierTransform : FourierTransform (V â†’ E) (V â†’ E) where
  fourierTransform f := VectorFourier.fourierIntegral ğ volume (innerâ‚— V) f

instance FourierTransformInv : FourierTransformInv (V â†’ E) (V â†’ E) where
  fourierTransformInv f w := VectorFourier.fourierIntegral ğ volume (-innerâ‚— V) f w

lemma fourierIntegral_eq (f : V â†’ E) (w : V) :
    ğ“• f w = âˆ« v, ğ (-âŸªv, wâŸ«) â€¢ f v := rfl

lemma fourierIntegral_eq' (f : V â†’ E) (w : V) :
    ğ“• f w = âˆ« v, Complex.exp ((â†‘(-2 * Ï€ * âŸªv, wâŸ«) * Complex.I)) â€¢ f v := by
  simp_rw [fourierIntegral_eq, Circle.smul_def, Real.fourierChar_apply, mul_neg, neg_mul]

lemma fourierIntegralInv_eq (f : V â†’ E) (w : V) :
    ğ“•â» f w = âˆ« v, ğ âŸªv, wâŸ« â€¢ f v := by
  simp [FourierTransformInv.fourierTransformInv, VectorFourier.fourierIntegral]

lemma fourierIntegralInv_eq' (f : V â†’ E) (w : V) :
    ğ“•â» f w = âˆ« v, Complex.exp ((â†‘(2 * Ï€ * âŸªv, wâŸ«) * Complex.I)) â€¢ f v := by
  simp_rw [fourierIntegralInv_eq, Circle.smul_def, Real.fourierChar_apply]

lemma fourierIntegral_comp_linearIsometry (A : W â‰ƒâ‚—áµ¢[â„] V) (f : V â†’ E) (w : W) :
    ğ“• (f âˆ˜ A) w = (ğ“• f) (A w) := by
  simp only [fourierIntegral_eq, â† A.inner_map_map, Function.comp_apply,
    â† MeasurePreserving.integral_comp A.measurePreserving A.toHomeomorph.measurableEmbedding]

lemma fourierIntegralInv_eq_fourierIntegral_neg (f : V â†’ E) (w : V) :
    ğ“•â» f w = ğ“• f (-w) := by
  simp [fourierIntegral_eq, fourierIntegralInv_eq]

lemma fourierIntegralInv_eq_fourierIntegral_comp_neg (f : V â†’ E) :
    ğ“•â» f = ğ“• (fun x â†¦ f (-x)) := by
  ext y
  rw [fourierIntegralInv_eq_fourierIntegral_neg]
  change ğ“• f (LinearIsometryEquiv.neg â„ y) = ğ“• (f âˆ˜ LinearIsometryEquiv.neg â„) y
  exact (fourierIntegral_comp_linearIsometry _ _ _).symm

lemma fourierIntegralInv_comm (f : V â†’ E) :
    ğ“• (ğ“•â» f) = ğ“•â» (ğ“• f) := by
  conv_rhs => rw [fourierIntegralInv_eq_fourierIntegral_comp_neg]
  simp_rw [â† fourierIntegralInv_eq_fourierIntegral_neg]

lemma fourierIntegralInv_comp_linearIsometry (A : W â‰ƒâ‚—áµ¢[â„] V) (f : V â†’ E) (w : W) :
    ğ“•â» (f âˆ˜ A) w = (ğ“•â» f) (A w) := by
  simp [fourierIntegralInv_eq_fourierIntegral_neg, fourierIntegral_comp_linearIsometry]

theorem fourierIntegral_real_eq (f : â„ â†’ E) (w : â„) :
    ğ“• f w = âˆ« v : â„, ğ (-(v * w)) â€¢ f v := by
  simp_rw [mul_comm _ w]
  rfl

theorem fourierIntegral_real_eq_integral_exp_smul (f : â„ â†’ E) (w : â„) :
    ğ“• f w = âˆ« v : â„, Complex.exp (â†‘(-2 * Ï€ * v * w) * Complex.I) â€¢ f v := by
  simp_rw [fourierIntegral_real_eq, Circle.smul_def, Real.fourierChar_apply, mul_neg, neg_mul,
    mul_assoc]

theorem fourierIntegral_continuousLinearMap_apply
    {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
    {f : V â†’ (F â†’L[â„] E)} {a : F} {v : V} (hf : Integrable f) :
    ğ“• f v a = ğ“• (fun x â†¦ f x a) v :=
  fourierIntegral_continuousLinearMap_apply' (L := innerSL â„) hf

theorem fourierIntegral_continuousMultilinearMap_apply {Î¹ : Type*} [Fintype Î¹]
    {M : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (M i)] [âˆ€ i, NormedSpace â„ (M i)]
    {f : V â†’ ContinuousMultilinearMap â„ M E} {m : (i : Î¹) â†’ M i} {v : V} (hf : Integrable f) :
    ğ“• f v m = ğ“• (fun x â†¦ f x m) v :=
  fourierIntegral_continuousMultilinearMap_apply' (L := innerSL â„) hf

end Real
