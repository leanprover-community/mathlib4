/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Complex.Circle
import Mathlib.MeasureTheory.Group.Integration
import Mathlib.MeasureTheory.Measure.Haar.OfBasis

#align_import analysis.fourier.fourier_transform from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# The Fourier transform

We set up the Fourier transform for complex-valued functions on finite-dimensional spaces.

## Design choices

In namespace `VectorFourier`, we define the Fourier integral in the following context:
* `ğ•œ` is a commutative ring.
* `V` and `W` are `ğ•œ`-modules.
* `e` is a unitary additive character of `ğ•œ`, i.e. a homomorphism `(Multiplicative ğ•œ) â†’* circle`.
* `Î¼` is a measure on `V`.
* `L` is a `ğ•œ`-bilinear form `V Ã— W â†’ ğ•œ`.
* `E` is a complete normed `â„‚`-vector space.

With these definitions, we define `fourierIntegral` to be the map from functions `V â†’ E` to
functions `W â†’ E` that sends `f` to

`fun w â†¦ âˆ« v in V, e [-L v w] â€¢ f v âˆ‚Î¼`,

where `e [x]` is notational sugar for `(e (Multiplicative.ofAdd x) : â„‚)` (available in locale
`fourier_transform`). This includes the cases `W` is the dual of `V` and `L` is the canonical
pairing, or `W = V` and `L` is a bilinear form (e.g. an inner product).

In namespace `fourier`, we consider the more familiar special case when `V = W = ğ•œ` and `L` is the
multiplication map (but still allowing `ğ•œ` to be an arbitrary ring equipped with a measure).

The most familiar case of all is when `V = W = ğ•œ = â„`, `L` is multiplication, `Î¼` is volume, and
`e` is `Real.fourierChar`, i.e. the character `fun x â†¦ exp ((2 * Ï€ * x) * I)`. The Fourier integral
in this case is defined as `Real.fourierIntegral`.

## Main results

At present the only nontrivial lemma we prove is `fourierIntegral_continuous`, stating that the
Fourier transform of an integrable function is continuous (under mild assumptions).
-/


noncomputable section

local notation "ğ•Š" => circle

open MeasureTheory Filter

open scoped Topology

-- To avoid messing around with multiplicative vs. additive characters, we make a notation.
scoped[FourierTransform] notation e "[" x "]" => (e (Multiplicative.ofAdd x) : â„‚)

open FourierTransform

/-! ## Fourier theory for functions on general vector spaces -/


namespace VectorFourier

variable {ğ•œ : Type*} [CommRing ğ•œ] {V : Type*} [AddCommGroup V] [Module ğ•œ V] [MeasurableSpace V]
  {W : Type*} [AddCommGroup W] [Module ğ•œ W] {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

section Defs

/-- The Fourier transform integral for `f : V â†’ E`, with respect to a bilinear form `L : V Ã— W â†’ ğ•œ`
and an additive character `e`. -/
def fourierIntegral (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure V) (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E)
    (w : W) : E :=
  âˆ« v, e[-L v w] â€¢ f v âˆ‚Î¼
#align vector_fourier.fourier_integral VectorFourier.fourierIntegral

theorem fourierIntegral_smul_const (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure V)
    (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (r : â„‚) :
    fourierIntegral e Î¼ L (r â€¢ f) = r â€¢ fourierIntegral e Î¼ L f := by
  ext1 w
  -- Porting note: was
  -- simp only [Pi.smul_apply, fourierIntegral, smul_comm _ r, integral_smul]
  simp only [Pi.smul_apply, fourierIntegral, â† integral_smul]
  congr; funext
  rw [smul_comm]
#align vector_fourier.fourier_integral_smul_const VectorFourier.fourierIntegral_smul_const

/-- The uniform norm of the Fourier integral of `f` is bounded by the `LÂ¹` norm of `f`. -/
theorem norm_fourierIntegral_le_integral_norm (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure V)
    (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (w : W) :
    â€–fourierIntegral e Î¼ L f wâ€– â‰¤ âˆ« v : V, â€–f vâ€– âˆ‚Î¼ := by
  refine' (norm_integral_le_integral_norm _).trans (le_of_eq _)
  simp_rw [norm_smul, Complex.norm_eq_abs, abs_coe_circle, one_mul]
#align vector_fourier.norm_fourier_integral_le_integral_norm VectorFourier.norm_fourierIntegral_le_integral_norm

/-- The Fourier integral converts right-translation into scalar multiplication by a phase factor.-/
theorem fourierIntegral_comp_add_right [MeasurableAdd V] (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure V)
    [Î¼.IsAddRightInvariant] (L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ) (f : V â†’ E) (vâ‚€ : V) :
    fourierIntegral e Î¼ L (f âˆ˜ fun v => v + vâ‚€) =
      fun w => e[L vâ‚€ w] â€¢ fourierIntegral e Î¼ L f w := by
  ext1 w
  dsimp only [fourierIntegral, Function.comp_apply]
  conv in L _ => rw [â† add_sub_cancel v vâ‚€]
  rw [integral_add_right_eq_self fun v : V => e[-L (v - vâ‚€) w] â€¢ f v]
  dsimp only
  rw [â† integral_smul]
  congr 1 with v
  rw [â† smul_assoc, smul_eq_mul, â† Submonoid.coe_mul, â† e.map_mul, â† ofAdd_add, â†
    LinearMap.neg_apply, â† sub_eq_add_neg, â† LinearMap.sub_apply, LinearMap.map_sub, neg_sub]
#align vector_fourier.fourier_integral_comp_add_right VectorFourier.fourierIntegral_comp_add_right

end Defs

section Continuous

/- In this section we assume ğ•œ, V, W have topologies, and L, e are continuous (but f needn't be).
   This is used to ensure that `e [-L v w]` is (ae strongly) measurable. We could get away with
   imposing only a measurable-space structure on ğ•œ (it doesn't have to be the Borel sigma-algebra of
   a topology); but it seems hard to imagine cases where this extra generality would be useful, and
   allowing it would complicate matters in the most important use cases.
-/
variable [TopologicalSpace ğ•œ] [TopologicalRing ğ•œ] [TopologicalSpace V] [BorelSpace V]
  [TopologicalSpace W] {e : Multiplicative ğ•œ â†’* ğ•Š} {Î¼ : Measure V} {L : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] ğ•œ}

/-- For any `w`, the Fourier integral is convergent iff `f` is integrable. -/
theorem fourier_integral_convergent_iff (he : Continuous e)
    (hL : Continuous fun p : V Ã— W => L p.1 p.2) {f : V â†’ E} (w : W) :
    Integrable f Î¼ â†” Integrable (fun v : V => e[-L v w] â€¢ f v) Î¼ := by
  -- first prove one-way implication
  have aux :
    âˆ€ {g : V â†’ E} (_ : Integrable g Î¼) (x : W), Integrable (fun v : V => e[-L v x] â€¢ g v) Î¼ := by
    intro g hg x
    have c : Continuous fun v => e[-L v x] := by
      refine' (continuous_induced_rng.mp he).comp (continuous_ofAdd.comp (Continuous.neg _))
      exact hL.comp (continuous_prod_mk.mpr âŸ¨continuous_id, continuous_constâŸ©)
    rw [â† integrable_norm_iff (c.aestronglyMeasurable.smul hg.1)]
    convert hg.norm using 2
    rw [norm_smul, Complex.norm_eq_abs, abs_coe_circle, one_mul]
  -- then use it for both directions
  refine' âŸ¨fun hf => aux hf w, fun hf => _âŸ©
  convert aux hf (-w)
  rw [â† smul_assoc, smul_eq_mul, â† Submonoid.coe_mul, â† MonoidHom.map_mul, â† ofAdd_add,
    LinearMap.map_neg, neg_neg, â† sub_eq_add_neg, sub_self, ofAdd_zero, MonoidHom.map_one,
    Submonoid.coe_one, one_smul]
#align vector_fourier.fourier_integral_convergent_iff VectorFourier.fourier_integral_convergent_iff

variable [CompleteSpace E]

theorem fourierIntegral_add (he : Continuous e) (hL : Continuous fun p : V Ã— W => L p.1 p.2)
    {f g : V â†’ E} (hf : Integrable f Î¼) (hg : Integrable g Î¼) :
    fourierIntegral e Î¼ L f + fourierIntegral e Î¼ L g = fourierIntegral e Î¼ L (f + g) := by
  ext1 w
  dsimp only [Pi.add_apply, fourierIntegral]
  simp_rw [smul_add]
  rw [integral_add]
  Â· exact (fourier_integral_convergent_iff he hL w).mp hf
  Â· exact (fourier_integral_convergent_iff he hL w).mp hg
#align vector_fourier.fourier_integral_add VectorFourier.fourierIntegral_add

/-- The Fourier integral of an `L^1` function is a continuous function. -/
theorem fourierIntegral_continuous [TopologicalSpace.FirstCountableTopology W] (he : Continuous e)
    (hL : Continuous fun p : V Ã— W => L p.1 p.2) {f : V â†’ E} (hf : Integrable f Î¼) :
    Continuous (fourierIntegral e Î¼ L f) := by
  apply continuous_of_dominated
  Â· exact fun w => ((fourier_integral_convergent_iff he hL w).mp hf).1
  Â· refine' fun w => ae_of_all _ fun v => _
    Â· exact fun v => â€–f vâ€–
    Â· rw [norm_smul, Complex.norm_eq_abs, abs_coe_circle, one_mul]
  Â· exact hf.norm
  Â· rw [continuous_induced_rng] at he
    refine' ae_of_all _ fun v => (he.comp (continuous_ofAdd.comp _)).smul continuous_const
    refine' (hL.comp (continuous_prod_mk.mpr âŸ¨continuous_const, continuous_idâŸ©)).neg
#align vector_fourier.fourier_integral_continuous VectorFourier.fourierIntegral_continuous

end Continuous

end VectorFourier

/-! ## Fourier theory for functions on `ğ•œ` -/


namespace Fourier

variable {ğ•œ : Type*} [CommRing ğ•œ] [MeasurableSpace ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace â„‚ E]

section Defs

variable [CompleteSpace E]

/-- The Fourier transform integral for `f : ğ•œ â†’ E`, with respect to the measure `Î¼` and additive
character `e`. -/
def fourierIntegral (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (w : ğ•œ) : E :=
  VectorFourier.fourierIntegral e Î¼ (LinearMap.mul ğ•œ ğ•œ) f w
#align fourier.fourier_integral Fourier.fourierIntegral

theorem fourierIntegral_def (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (w : ğ•œ) :
    fourierIntegral e Î¼ f w = âˆ« v : ğ•œ, e[-(v * w)] â€¢ f v âˆ‚Î¼ :=
  rfl
#align fourier.fourier_integral_def Fourier.fourierIntegral_def

theorem fourierIntegral_smul_const (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure ğ•œ) (f : ğ•œ â†’ E) (r : â„‚) :
    fourierIntegral e Î¼ (r â€¢ f) = r â€¢ fourierIntegral e Î¼ f :=
  VectorFourier.fourierIntegral_smul_const _ _ _ _ _
#align fourier.fourier_integral_smul_const Fourier.fourierIntegral_smul_const

/-- The uniform norm of the Fourier transform of `f` is bounded by the `LÂ¹` norm of `f`. -/
theorem norm_fourierIntegral_le_integral_norm (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure ğ•œ)
    (f : ğ•œ â†’ E) (w : ğ•œ) : â€–fourierIntegral e Î¼ f wâ€– â‰¤ âˆ« x : ğ•œ, â€–f xâ€– âˆ‚Î¼ :=
  VectorFourier.norm_fourierIntegral_le_integral_norm _ _ _ _ _
#align fourier.norm_fourier_integral_le_integral_norm Fourier.norm_fourierIntegral_le_integral_norm

/-- The Fourier transform converts right-translation into scalar multiplication by a phase factor.-/
theorem fourierIntegral_comp_add_right [MeasurableAdd ğ•œ] (e : Multiplicative ğ•œ â†’* ğ•Š) (Î¼ : Measure ğ•œ)
    [Î¼.IsAddRightInvariant] (f : ğ•œ â†’ E) (vâ‚€ : ğ•œ) :
    fourierIntegral e Î¼ (f âˆ˜ fun v => v + vâ‚€) = fun w => e[vâ‚€ * w] â€¢ fourierIntegral e Î¼ f w :=
  VectorFourier.fourierIntegral_comp_add_right _ _ _ _ _
#align fourier.fourier_integral_comp_add_right Fourier.fourierIntegral_comp_add_right

end Defs

end Fourier

open scoped Real

namespace Real

/-- The standard additive character of `â„`, given by `fun x â†¦ exp (2 * Ï€ * x * I)`. -/
def fourierChar : Multiplicative â„ â†’* ğ•Š where
  toFun z := expMapCircle (2 * Ï€ * Multiplicative.toAdd z)
  map_one' := by simp only; rw [toAdd_one, mul_zero, expMapCircle_zero]
  map_mul' x y := by simp only; rw [toAdd_mul, mul_add, expMapCircle_add]
#align real.fourier_char Real.fourierChar

theorem fourierChar_apply (x : â„) : Real.fourierChar[x] = Complex.exp (â†‘(2 * Ï€ * x) * Complex.I) :=
  by rfl
#align real.fourier_char_apply Real.fourierChar_apply

@[continuity]
theorem continuous_fourierChar : Continuous Real.fourierChar :=
  (map_continuous expMapCircle).comp (continuous_const.mul continuous_toAdd)
#align real.continuous_fourier_char Real.continuous_fourierChar

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

theorem vector_fourierIntegral_eq_integral_exp_smul {V : Type*} [AddCommGroup V] [Module â„ V]
    [MeasurableSpace V] {W : Type*} [AddCommGroup W] [Module â„ W] (L : V â†’â‚—[â„] W â†’â‚—[â„] â„)
    (Î¼ : Measure V) (f : V â†’ E) (w : W) :
    VectorFourier.fourierIntegral fourierChar Î¼ L f w =
      âˆ« v : V, Complex.exp (â†‘(-2 * Ï€ * L v w) * Complex.I) â€¢ f v âˆ‚Î¼ :=
  by simp_rw [VectorFourier.fourierIntegral, Real.fourierChar_apply, mul_neg, neg_mul]
#align real.vector_fourier_integral_eq_integral_exp_smul Real.vector_fourierIntegral_eq_integral_exp_smul

/-- The Fourier integral for `f : â„ â†’ E`, with respect to the standard additive character and
measure on `â„`. -/
def fourierIntegral (f : â„ â†’ E) (w : â„) :=
  Fourier.fourierIntegral fourierChar volume f w
#align real.fourier_integral Real.fourierIntegral

theorem fourierIntegral_def (f : â„ â†’ E) (w : â„) :
    fourierIntegral f w = âˆ« v : â„, fourierChar[-(v * w)] â€¢ f v :=
  rfl
#align real.fourier_integral_def Real.fourierIntegral_def

scoped[FourierTransform] notation "ğ“•" => Real.fourierIntegral

theorem fourierIntegral_eq_integral_exp_smul {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]
    (f : â„ â†’ E) (w : â„) :
    ğ“• f w = âˆ« v : â„, Complex.exp (â†‘(-2 * Ï€ * v * w) * Complex.I) â€¢ f v := by
  simp_rw [fourierIntegral_def, Real.fourierChar_apply, mul_neg, neg_mul, mul_assoc]
#align real.fourier_integral_eq_integral_exp_smul Real.fourierIntegral_eq_integral_exp_smul

end Real
