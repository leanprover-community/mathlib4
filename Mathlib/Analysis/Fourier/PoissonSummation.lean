/-
Copyright (c) 2023 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.PSeries
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.MeasureTheory.Measure.Lebesgue.Integral

#align_import analysis.fourier.poisson_summation from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# Poisson's summation formula

We prove Poisson's summation formula `âˆ‘ (n : â„¤), f n = âˆ‘ (n : â„¤), ğ“• f n`, where `ğ“• f` is the
Fourier transform of `f`, under the following hypotheses:
* `f` is a continuous function `â„ â†’ â„‚`.
* The sum `âˆ‘ (n : â„¤), ğ“• f n` is convergent.
* For all compacts `K âŠ‚ â„`, the sum `âˆ‘ (n : â„¤), sup { â€–f(x + n)â€– | x âˆˆ K }` is convergent.
See `Real.tsum_eq_tsum_fourierIntegral` for this formulation.

These hypotheses are potentially a little awkward to apply, so we also provide the less general but
easier-to-use result `Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay`, in which we assume `f` and
`ğ“• f` both decay as `|x| ^ (-b)` for some `b > 1`, and the even more specific result
`SchwartzMap.tsum_eq_tsum_fourierIntegral`, where we assume that both `f` and `ğ“• f` are Schwartz
functions.

## TODO

At the moment `SchwartzMap.tsum_eq_tsum_fourierIntegral` requires separate proofs that both `f`
and `ğ“• f` are Schwartz functions. In fact, `ğ“• f` is automatically Schwartz if `f` is; and once
we have this lemma in the library, we should adjust the hypotheses here accordingly.
-/


noncomputable section

open Function hiding comp_apply

open Set hiding restrict_apply

open Complex hiding abs_of_nonneg

open Real

open TopologicalSpace Filter MeasureTheory Asymptotics

open scoped Real BigOperators Filter FourierTransform

attribute [local instance] Real.fact_zero_lt_one

open ContinuousMap

/-- The key lemma for Poisson summation: the `m`-th Fourier coefficient of the periodic function
`âˆ‘' n : â„¤, f (x + n)` is the value at `m` of the Fourier transform of `f`. -/
theorem Real.fourierCoeff_tsum_comp_add {f : C(â„, â„‚)}
    (hf : âˆ€ K : Compacts â„, Summable fun n : â„¤ => â€–(f.comp (ContinuousMap.addRight n)).restrict Kâ€–)
    (m : â„¤) : fourierCoeff (Periodic.lift <| f.periodic_tsum_comp_add_zsmul 1) m = ğ“• f m := by
  -- NB: This proof can be shortened somewhat by telescoping together some of the steps in the calc
  -- block, but I think it's more legible this way. We start with preliminaries about the integrand.
  let e : C(â„, â„‚) := (fourier (-m)).comp âŸ¨((â†‘) : â„ â†’ UnitAddCircle), continuous_quotient_mk'âŸ©
  -- âŠ¢ fourierCoeff (Periodic.lift (_ : Periodic (â†‘(âˆ‘' (n : â„¤), ContinuousMap.comp  â€¦
  have neK : âˆ€ (K : Compacts â„) (g : C(â„, â„‚)), â€–(e * g).restrict Kâ€– = â€–g.restrict Kâ€– := by
    have : âˆ€ x : â„, â€–e xâ€– = 1 := fun x => abs_coe_circle (AddCircle.toCircle (-m â€¢ x))
    intro K g
    simp_rw [norm_eq_iSup_norm, restrict_apply, mul_apply, norm_mul, this, one_mul]
  have eadd : âˆ€ (n : â„¤), e.comp (ContinuousMap.addRight n) = e := by
    intro n; ext1 x
    have : Periodic e 1 := Periodic.comp (fun x => AddCircle.coe_add_period 1 x) (fourier (-m))
    simpa only [mul_one] using this.int_mul n x
  -- Now the main argument. First unwind some definitions.
  calc
    fourierCoeff (Periodic.lift <| f.periodic_tsum_comp_add_zsmul 1) m =
        âˆ« x in (0 : â„)..1, e x * (âˆ‘' n : â„¤, f.comp (ContinuousMap.addRight n)) x := by
      simp_rw [fourierCoeff_eq_intervalIntegral _ m 0, div_one, one_smul, zero_add, comp_apply,
        coe_mk, Periodic.lift_coe, zsmul_one, smul_eq_mul]
    -- Transform sum in C(â„, â„‚) evaluated at x into pointwise sum of values.
    _ = âˆ« x in (0:â„)..1, âˆ‘' n : â„¤, (e * f.comp (ContinuousMap.addRight n)) x := by
      simp_rw [coe_mul, Pi.mul_apply,
        â† ContinuousMap.tsum_apply (summable_of_locally_summable_norm hf), tsum_mul_left]
    -- Swap sum and integral.
    _ = âˆ‘' n : â„¤, âˆ« x in (0:â„)..1, (e * f.comp (ContinuousMap.addRight n)) x := by
      refine' (intervalIntegral.tsum_intervalIntegral_eq_of_summable_norm _).symm
      convert hf âŸ¨uIcc 0 1, isCompact_uIccâŸ© using 1
      exact funext fun n => neK _ _
    _ = âˆ‘' n : â„¤, âˆ« x in (0:â„)..1, (e * f).comp (ContinuousMap.addRight n) x := by
      simp only [ContinuousMap.comp_apply, mul_comp] at eadd âŠ¢
      simp_rw [eadd]
    -- Rearrange sum of interval integrals into an integral over `â„`.
    _ = âˆ« x, e x * f x := by
      suffices Integrable (e * f) from this.hasSum_intervalIntegral_comp_add_int.tsum_eq
      apply integrable_of_summable_norm_Icc
      convert hf âŸ¨Icc 0 1, isCompact_IccâŸ© using 1
      simp_rw [mul_comp] at eadd âŠ¢
      simp_rw [eadd]
      exact funext fun n => neK âŸ¨Icc 0 1, isCompact_IccâŸ© _
    -- Minor tidying to finish
    _ = ğ“• f m := by
      rw [fourierIntegral_eq_integral_exp_smul]
      congr 1 with x : 1
      rw [smul_eq_mul, comp_apply, coe_mk, coe_mk, ContinuousMap.toFun_eq_coe, fourier_coe_apply]
      congr 2
      push_cast
      ring
#align real.fourier_coeff_tsum_comp_add Real.fourierCoeff_tsum_comp_add

/-- **Poisson's summation formula**, most general form. -/
theorem Real.tsum_eq_tsum_fourierIntegral {f : C(â„, â„‚)}
    (h_norm :
      âˆ€ K : Compacts â„, Summable fun n : â„¤ => â€–(f.comp <| ContinuousMap.addRight n).restrict Kâ€–)
    (h_sum : Summable fun n : â„¤ => ğ“• f n) : âˆ‘' n : â„¤, f n = (âˆ‘' n : â„¤, ğ“• f n) := by
  let F : C(UnitAddCircle, â„‚) :=
    âŸ¨(f.periodic_tsum_comp_add_zsmul 1).lift, continuous_coinduced_dom.mpr (map_continuous _)âŸ©
  have : Summable (fourierCoeff F) := by
    convert h_sum
    exact Real.fourierCoeff_tsum_comp_add h_norm _
  convert (has_pointwise_sum_fourier_series_of_summable this 0).tsum_eq.symm using 1
  -- âŠ¢ âˆ‘' (n : â„¤), â†‘f â†‘n = â†‘F 0
  Â· have := (hasSum_apply (summable_of_locally_summable_norm h_norm).hasSum 0).tsum_eq
    -- âŠ¢ âˆ‘' (n : â„¤), â†‘f â†‘n = â†‘F 0
    simpa only [coe_mk, â† QuotientAddGroup.mk_zero, Periodic.lift_coe, zsmul_one, comp_apply,
      coe_addRight, zero_add] using this
  Â· congr 1 with n : 1
    -- âŠ¢ ğ“• â†‘f â†‘n = fourierCoeff (â†‘F) n â€¢ â†‘(fourier n) 0
    rw [â† Real.fourierCoeff_tsum_comp_add h_norm n, fourier_eval_zero, smul_eq_mul, mul_one]
    -- âŠ¢ fourierCoeff (Periodic.lift (_ : Periodic (â†‘(âˆ‘' (n : â„¤), ContinuousMap.comp  â€¦
    rfl
    -- ğŸ‰ no goals
#align real.tsum_eq_tsum_fourier_integral Real.tsum_eq_tsum_fourierIntegral

section RpowDecay

variable {E : Type*} [NormedAddCommGroup E]

/-- If `f` is `O(x ^ (-b))` at infinity, then so is the function
`Î» x, â€–f.restrict (Icc (x + R) (x + S))â€–` for any fixed `R` and `S`. -/
theorem isBigO_norm_Icc_restrict_atTop {f : C(â„, E)} {b : â„} (hb : 0 < b)
    (hf : IsBigO atTop f fun x : â„ => |x| ^ (-b)) (R S : â„) :
    IsBigO atTop (fun x : â„ => â€–f.restrict (Icc (x + R) (x + S))â€–) fun x : â„ => |x| ^ (-b) := by
  -- First establish an explicit estimate on decay of inverse powers.
  -- This is logically independent of the rest of the proof, but of no mathematical interest in
  -- itself, so it is proved using `async` rather than being formulated as a separate lemma.
  have claim :
    âˆ€ x : â„, max 0 (-2 * R) < x â†’ âˆ€ y : â„, x + R â‰¤ y â†’ y ^ (-b) â‰¤ (1 / 2) ^ (-b) * x ^ (-b) := by
    intro x hx y hy
    rw [max_lt_iff] at hx
    have hxR : 0 < x + R := by
      rcases le_or_lt 0 R with (h | h)
      Â· exact add_pos_of_pos_of_nonneg hx.1 h
      Â· rw [â† sub_lt_iff_lt_add, zero_sub]
        refine' lt_trans _ hx.2
        rwa [neg_mul, neg_lt_neg_iff, two_mul, add_lt_iff_neg_left]
    have hy' : 0 < y := hxR.trans_le hy
    have : y ^ (-b) â‰¤ (x + R) ^ (-b) := by
      rw [rpow_neg hy'.le, rpow_neg hxR.le,
        inv_le_inv (rpow_pos_of_pos hy' _) (rpow_pos_of_pos hxR _)]
      exact rpow_le_rpow hxR.le hy hb.le
    refine' this.trans _
    rw [â† mul_rpow one_half_pos.le hx.1.le, rpow_neg (mul_pos one_half_pos hx.1).le,
      rpow_neg hxR.le]
    refine' inv_le_inv_of_le (rpow_pos_of_pos (mul_pos one_half_pos hx.1) _) _
    exact rpow_le_rpow (mul_pos one_half_pos hx.1).le (by linarith) hb.le
  -- Now the main proof.
  obtain âŸ¨c, hc, hc'âŸ© := hf.exists_pos
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–) =O[atTop] fun x  â€¦
  simp only [IsBigO, IsBigOWith, eventually_atTop] at hc' âŠ¢
  -- âŠ¢ âˆƒ c a, âˆ€ (b_1 : â„), b_1 â‰¥ a â†’ â€–â€–ContinuousMap.restrict (Icc (b_1 + R) (b_1 + â€¦
  obtain âŸ¨d, hdâŸ© := hc'
  -- âŠ¢ âˆƒ c a, âˆ€ (b_1 : â„), b_1 â‰¥ a â†’ â€–â€–ContinuousMap.restrict (Icc (b_1 + R) (b_1 + â€¦
  refine' âŸ¨c * (1 / 2) ^ (-b), âŸ¨max (1 + max 0 (-2 * R)) (d - R), fun x hx => _âŸ©âŸ©
  -- âŠ¢ â€–â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–â€– â‰¤ c * (1 / 2) ^ (-b) * â€–| â€¦
  rw [ge_iff_le, max_le_iff] at hx
  -- âŠ¢ â€–â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–â€– â‰¤ c * (1 / 2) ^ (-b) * â€–| â€¦
  have hx' : max 0 (-2 * R) < x := by linarith
  -- âŠ¢ â€–â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–â€– â‰¤ c * (1 / 2) ^ (-b) * â€–| â€¦
  rw [max_lt_iff] at hx'
  -- âŠ¢ â€–â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–â€– â‰¤ c * (1 / 2) ^ (-b) * â€–| â€¦
  rw [norm_norm,
    ContinuousMap.norm_le _
      (mul_nonneg (mul_nonneg hc.le <| rpow_nonneg_of_nonneg one_half_pos.le _) (norm_nonneg _))]
  refine' fun y => (hd y.1 (by linarith [hx.1, y.2.1])).trans _
  -- âŠ¢ c * â€–|â†‘y| ^ (-b)â€– â‰¤ c * (1 / 2) ^ (-b) * â€–|x| ^ (-b)â€–
  have A : âˆ€ x : â„, 0 â‰¤ |x| ^ (-b) := fun x => by positivity
  -- âŠ¢ c * â€–|â†‘y| ^ (-b)â€– â‰¤ c * (1 / 2) ^ (-b) * â€–|x| ^ (-b)â€–
  rw [mul_assoc, mul_le_mul_left hc, norm_of_nonneg (A _), norm_of_nonneg (A _)]
  -- âŠ¢ |â†‘y| ^ (-b) â‰¤ (1 / 2) ^ (-b) * |x| ^ (-b)
  convert claim x (by linarith only [hx.1]) y.1 y.2.1
  -- âŠ¢ |â†‘y| = â†‘y
  Â· apply abs_of_nonneg; linarith [y.2.1]
    -- âŠ¢ 0 â‰¤ â†‘y
                         -- ğŸ‰ no goals
  Â· exact abs_of_pos hx'.1
    -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align is_O_norm_Icc_restrict_at_top isBigO_norm_Icc_restrict_atTop

theorem isBigO_norm_Icc_restrict_atBot {f : C(â„, E)} {b : â„} (hb : 0 < b)
    (hf : IsBigO atBot f fun x : â„ => |x| ^ (-b)) (R S : â„) :
    IsBigO atBot (fun x : â„ => â€–f.restrict (Icc (x + R) (x + S))â€–) fun x : â„ => |x| ^ (-b) := by
  have h1 : IsBigO atTop (f.comp (ContinuousMap.mk _ continuous_neg)) fun x : â„ => |x| ^ (-b) := by
    convert hf.comp_tendsto tendsto_neg_atTop_atBot using 1
    ext1 x; simp only [Function.comp_apply, abs_neg]
  have h2 := (isBigO_norm_Icc_restrict_atTop hb h1 (-S) (-R)).comp_tendsto tendsto_neg_atBot_atTop
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–) =O[atBot] fun x  â€¦
  have : (fun x : â„ => |x| ^ (-b)) âˆ˜ Neg.neg = fun x : â„ => |x| ^ (-b) := by
    ext1 x; simp only [Function.comp_apply, abs_neg]
  rw [this] at h2
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–) =O[atBot] fun x  â€¦
  refine' (isBigO_of_le _ fun x => _).trans h2
  -- âŠ¢ â€–â€–ContinuousMap.restrict (Icc (x + R) (x + S)) fâ€–â€– â‰¤ â€–((fun x => â€–Continuous â€¦
  -- equality holds, but less work to prove `â‰¤` alone
  rw [norm_norm, Function.comp_apply, norm_norm, ContinuousMap.norm_le _ (norm_nonneg _)]
  -- âŠ¢ âˆ€ (x_1 : â†‘(Icc (x + R) (x + S))), â€–â†‘(ContinuousMap.restrict (Icc (x + R) (x  â€¦
  rintro âŸ¨x, hxâŸ©
  -- âŠ¢ â€–â†‘(ContinuousMap.restrict (Icc (xâœ + R) (xâœ + S)) f) { val := x, property := â€¦
  rw [ContinuousMap.restrict_apply_mk]
  -- âŠ¢ â€–â†‘f xâ€– â‰¤ â€–ContinuousMap.restrict (Icc (-xâœ + -S) (-xâœ + -R)) (ContinuousMap. â€¦
  refine' (le_of_eq _).trans (ContinuousMap.norm_coe_le_norm _ âŸ¨-x, _âŸ©)
  -- âŠ¢ â€–â†‘f xâ€– = â€–â†‘(ContinuousMap.restrict (Icc (-xâœ + -S) (-xâœ + -R)) (ContinuousMa â€¦
  rw [ContinuousMap.restrict_apply_mk, ContinuousMap.comp_apply, ContinuousMap.coe_mk,
    ContinuousMap.coe_mk, neg_neg]
  exact âŸ¨by linarith [hx.2], by linarith [hx.1]âŸ©
  -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align is_O_norm_Icc_restrict_at_bot isBigO_norm_Icc_restrict_atBot

theorem isBigO_norm_restrict_cocompact (f : C(â„, E)) {b : â„} (hb : 0 < b)
    (hf : IsBigO (cocompact â„) f fun x : â„ => |x| ^ (-b)) (K : Compacts â„) :
    IsBigO (cocompact â„) (fun x => â€–(f.comp (ContinuousMap.addRight x)).restrict Kâ€–) fun x =>
      |x| ^ (-b) := by
  obtain âŸ¨r, hrâŸ© := K.isCompact.bounded.subset_ball 0
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap. â€¦
  rw [closedBall_eq_Icc, zero_add, zero_sub] at hr
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap. â€¦
  have :
    âˆ€ x : â„,
      â€–(f.comp (ContinuousMap.addRight x)).restrict Kâ€– â‰¤ â€–f.restrict (Icc (x - r) (x + r))â€– := by
    intro x
    rw [ContinuousMap.norm_le _ (norm_nonneg _)]
    rintro âŸ¨y, hyâŸ©
    refine' (le_of_eq _).trans (ContinuousMap.norm_coe_le_norm _ âŸ¨y + x, _âŸ©)
    Â· simp_rw [ContinuousMap.restrict_apply, ContinuousMap.comp_apply, ContinuousMap.coe_addRight]
    Â· exact âŸ¨by linarith [(hr hy).1], by linarith [(hr hy).2]âŸ©
  simp_rw [cocompact_eq, isBigO_sup] at hf âŠ¢
  -- âŠ¢ ((fun x => â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap â€¦
  constructor
  -- âŠ¢ (fun x => â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap. â€¦
  Â· refine' (isBigO_of_le atBot _).trans (isBigO_norm_Icc_restrict_atBot hb hf.1 (-r) r)
    -- âŠ¢ âˆ€ (x : â„), â€–â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMa â€¦
    simp_rw [norm_norm]; exact this
    -- âŠ¢ âˆ€ (x : â„), â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap â€¦
                         -- ğŸ‰ no goals
  Â· refine' (isBigO_of_le atTop _).trans (isBigO_norm_Icc_restrict_atTop hb hf.2 (-r) r)
    -- âŠ¢ âˆ€ (x : â„), â€–â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMa â€¦
    simp_rw [norm_norm]; exact this
    -- âŠ¢ âˆ€ (x : â„), â€–ContinuousMap.restrict (â†‘K) (ContinuousMap.comp f (ContinuousMap â€¦
                         -- ğŸ‰ no goals
set_option linter.uppercaseLean3 false in
#align is_O_norm_restrict_cocompact isBigO_norm_restrict_cocompact

/-- **Poisson's summation formula**, assuming that `f` decays as
`|x| ^ (-b)` for some `1 < b` and its Fourier transform is summable. -/
theorem Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay_of_summable {f : â„ â†’ â„‚} (hc : Continuous f)
    {b : â„} (hb : 1 < b) (hf : IsBigO (cocompact â„) f fun x : â„ => |x| ^ (-b))
    (hFf : Summable fun n : â„¤ => ğ“• f n) : âˆ‘' n : â„¤, f n = (âˆ‘' n : â„¤, ğ“• f n) :=
  Real.tsum_eq_tsum_fourierIntegral
    (fun K =>
      summable_of_isBigO (Real.summable_abs_int_rpow hb)
        ((isBigO_norm_restrict_cocompact (ContinuousMap.mk _ hc) (zero_lt_one.trans hb) hf
              K).comp_tendsto
          Int.tendsto_coe_cofinite))
    hFf
#align real.tsum_eq_tsum_fourier_integral_of_rpow_decay_of_summable Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay_of_summable

/-- **Poisson's summation formula**, assuming that both `f` and its Fourier transform decay as
`|x| ^ (-b)` for some `1 < b`. (This is the one-dimensional case of Corollary VII.2.6 of Stein and
Weiss, *Introduction to Fourier analysis on Euclidean spaces*.) -/
theorem Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay {f : â„ â†’ â„‚} (hc : Continuous f) {b : â„}
    (hb : 1 < b) (hf : IsBigO (cocompact â„) f fun x : â„ => |x| ^ (-b))
    (hFf : IsBigO (cocompact â„) (ğ“• f) fun x : â„ => |x| ^ (-b)) :
    âˆ‘' n : â„¤, f n = âˆ‘' n : â„¤, ğ“• f n :=
  Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay_of_summable hc hb hf
    (summable_of_isBigO (Real.summable_abs_int_rpow hb) (hFf.comp_tendsto Int.tendsto_coe_cofinite))
#align real.tsum_eq_tsum_fourier_integral_of_rpow_decay Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay

end RpowDecay

section Schwartz

/-- **Poisson's summation formula** for Schwartz functions. -/
theorem SchwartzMap.tsum_eq_tsum_fourierIntegral (f g : SchwartzMap â„ â„‚) (hfg : ğ“• f = g) :
    âˆ‘' n : â„¤, f n = (âˆ‘' n : â„¤, g n) := by
  -- We know that Schwartz functions are `O(â€–x ^ (-b)â€–)` for *every* `b`; for this argument we take
  -- `b = 2` and work with that.
  simp_rw [â† hfg]
  -- âŠ¢ âˆ‘' (n : â„¤), â†‘f â†‘n = âˆ‘' (n : â„¤), ğ“• â†‘f â†‘n
  rw [Real.tsum_eq_tsum_fourierIntegral_of_rpow_decay f.continuous one_lt_two
    (f.isBigO_cocompact_rpow (-2))]
  rw [hfg]
  -- âŠ¢ â†‘g =O[cocompact â„] fun x => |x| ^ (-2)
  exact g.isBigO_cocompact_rpow (-2)
  -- ğŸ‰ no goals
#align schwartz_map.tsum_eq_tsum_fourier_integral SchwartzMap.tsum_eq_tsum_fourierIntegral

end Schwartz
