/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.NumberTheory.DirichletCharacter.GaussSum

/-!
# Fourier theory on `ZMod N`

Basic definitions and properties of the discrete Fourier transform for functions on `ZMod N`.

### Main definitions and results

* `ZMod.stdAddChar`: the additive character of `ZMod N` sending `1` to `exp (2 * Ï€ * I / N)`.
* `ZMod.discreteFourierTransform`: the Fourier transform with respect to this add char. The local
  notation `ğ“•`, scoped in namespace `ZMod`, is available for this.
* `DirichletCharacter.fourierTransform_eq_inv_mul_gaussSum`: the discrete Fourier transform of a
  primitive Dirichlet character `Ï‡` is a Gauss sum times `Ï‡â»Â¹`.
-/

open scoped Real

namespace ZMod

variable {N : â„•} [NeZero N]

/-- The `AddMonoidHom` from `ZMod N` to `â„ / â„¤` sending `j mod N` to `j / N mod 1`. -/
noncomputable def toAddCircle : ZMod N â†’+ UnitAddCircle :=
  lift N âŸ¨AddMonoidHom.mk' (fun j â†¦ â†‘(j / N : â„)) (by simp [add_div]), by simpâŸ©

lemma toAddCircle_coe (j : â„¤) :
    toAddCircle (j : ZMod N) = â†‘(j / N : â„) := by
  simp [toAddCircle]

lemma toAddCircle_apply (j : ZMod N) :
    toAddCircle j = â†‘(j.val / N : â„) := by
  conv_lhs => rw [show j = (val j : â„¤) by simp, toAddCircle_coe]
  simp only [natCast_val, intCast_cast]

variable (N) in
lemma toAddCircle_injective : Function.Injective (toAddCircle : ZMod N â†’ _) := by
  intro x y hxy
  have : (0 : â„) < N := Nat.cast_pos.mpr (NeZero.pos _)
  rwa [toAddCircle_apply, toAddCircle_apply, AddCircle.coe_eq_coe_iff_of_mem_Ico
    (hp := Real.fact_zero_lt_one) (a := 0), div_left_inj' this.ne', Nat.cast_inj,
    (val_injective N).eq_iff] at hxy <;>
  exact âŸ¨by positivity, by simpa only [zero_add, div_lt_one this, Nat.cast_lt] using val_lt _âŸ©

/-- The additive character from `ZMod N` to the unit circle in `â„‚`, sending `j mod N` to
`exp (2 * Ï€ * I * j / N)`. -/
noncomputable def toCircle : AddChar (ZMod N) circle where
  toFun := fun j â†¦ (toAddCircle j).toCircle
  map_add_mul' a b := by simp_rw [map_add, AddCircle.toCircle_add]
  map_zero_one' := by simp_rw [map_zero, AddCircle.toCircle, â† QuotientAddGroup.mk_zero,
    Function.Periodic.lift_coe, mul_zero, expMapCircle_zero]

lemma toCircle_coe (j : â„¤) :
    toCircle (j : ZMod N) = Complex.exp (2 * Ï€ * Complex.I * j / N) := by
  rw [toCircle, AddChar.coe_mk, AddCircle.toCircle, toAddCircle_coe,
    Function.Periodic.lift_coe, expMapCircle_apply]
  push_cast
  ring_nf

lemma toCircle_apply (j : ZMod N) :
    toCircle j = Complex.exp (2 * Ï€ * Complex.I * j.val / N) := by
  rw [â† Int.cast_natCast, â† toCircle_coe, natCast_val, intCast_zmod_cast]

/-- The additive character from `ZMod N` to `â„‚`, sending
`j mod N` to `exp (2 * Ï€ * I * j / N)`. -/
noncomputable def stdAddChar : AddChar (ZMod N) â„‚ := circle.subtype.compAddChar toCircle

lemma stdAddChar_coe (j : â„¤) :
    stdAddChar (j : ZMod N) = Complex.exp (2 * Ï€ * Complex.I * j / N) := by
  simp only [stdAddChar, MonoidHom.coe_compAddChar, Function.comp_apply,
    Submonoid.coe_subtype, toCircle_coe]

lemma stdAddChar_apply (j : ZMod N) : stdAddChar j = â†‘(toCircle j) := rfl

section fourier

open MeasureTheory

/-- The discrete measurable space structure (every set is measurable). -/
instance instMeasurableSpaceZMod : MeasurableSpace (ZMod N) := âŠ¤

/-- The discrete Fourier transform on `â„¤ / Nâ„¤` (with the counting measure) -/
noncomputable def dft (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) : â„‚ :=
  Fourier.fourierIntegral toCircle Measure.count Î¦ k

@[inherit_doc] scoped notation "ğ“•" => dft

lemma dft_def (Î¦ : ZMod N â†’ â„‚) (k : ZMod N) :
    ğ“• Î¦ k = âˆ‘ j : ZMod N, toCircle (-(j * k)) â€¢ Î¦ j := by
  simp only [dft, Fourier.fourierIntegral_def,
    integral_countable' (integrable_count_iff.mpr <| Finite.summable _), Measure.count_singleton,
    ENNReal.one_toReal, one_smul, tsum_fintype]

end fourier

end ZMod

open ZMod

namespace DirichletCharacter

variable {N : â„•} [NeZero N] (Ï‡ : DirichletCharacter â„‚ N)

lemma fourierTransform_eq_gaussSum_mulShift (k : ZMod N) :
    ğ“• Ï‡ k = gaussSum Ï‡ (stdAddChar.mulShift (-k)) := by
  simp only [dft_def]
  refine congr_arg Finset.univ.sum (funext fun j â†¦ ?_)
  rw [AddChar.mulShift_apply, mul_comm j, Submonoid.smul_def, smul_eq_mul, neg_mul,
    stdAddChar_apply, mul_comm (Ï‡ _)]

/-- For a primitive Dirichlet character `Ï‡`, the Fourier transform of `Ï‡` is a constant multiple
of `Ï‡â»Â¹` (and the constant is essentially the Gauss sum). -/
lemma fourierTransform_eq_inv_mul_gaussSum (k : ZMod N) (hÏ‡ : isPrimitive Ï‡) :
    ğ“• Ï‡ k = Ï‡â»Â¹ (-k) * gaussSum Ï‡ stdAddChar := by
  rw [fourierTransform_eq_gaussSum_mulShift, gaussSum_mulShift_of_isPrimitive _ hÏ‡]

end DirichletCharacter
