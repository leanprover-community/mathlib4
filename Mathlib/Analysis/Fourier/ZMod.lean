/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.NumberTheory.DirichletCharacter.GaussSum

/-!
# Fourier theory on `ZMod N`

Basic definitions and properties of the discrete Fourier transform for functions on `ZMod N`.

### Main definitions and results

* `ZMod.stdAddChar`: the additive character of `ZMod N` sending `1` to `exp (2 * œÄ * I / N)`.
* `ZMod.discreteFourierTransform`: the Fourier transform with respect to this add char. The local
  notation `ùìï`, scoped in namespace `ZMod`, is available for this.
* `DirichletCharacter.fourierTransform_eq_inv_mul_gaussSum`: the discrete Fourier transform of a
  primitive Dirichlet character `œá` is a Gauss sum times `œá‚Åª¬π`.
-/

open scoped Real

namespace ZMod

/-- The `AddMonoidHom` from `ZMod N` to `‚Ñù / ‚Ñ§` sending `j mod N` to `j / N mod 1`. -/
noncomputable def toAddCircle {N : ‚Ñï+} : ZMod N ‚Üí+ UnitAddCircle :=
  lift N ‚ü®AddMonoidHom.mk' (fun j ‚Ü¶ ‚Üë(j / N : ‚Ñù)) (by simp [add_div]), by simp‚ü©

lemma toAddCircle_coe {N : ‚Ñï+} (j : ‚Ñ§) :
    toAddCircle (j : ZMod N) = ‚Üë(j / N : ‚Ñù) := by
  simp [toAddCircle]

lemma toAddCircle_apply {N : ‚Ñï+} (j : ZMod N) :
    toAddCircle j = ‚Üë(j.val / N : ‚Ñù) := by
  conv_lhs => rw [show j = (val j : ‚Ñ§) by simp, toAddCircle_coe]
  simp only [natCast_val, intCast_cast]

lemma toAddCircle_injective (N : ‚Ñï+) : Function.Injective (toAddCircle : ZMod N ‚Üí _) := by
  intro x y hxy
  have : (0 : ‚Ñù) < N := Nat.cast_pos.mpr N.pos
  rwa [toAddCircle_apply, toAddCircle_apply, AddCircle.coe_eq_coe_iff_of_mem_Ico
    (hp := Real.fact_zero_lt_one) (a := 0), div_left_inj' this.ne', Nat.cast_inj,
    (val_injective N).eq_iff] at hxy <;>
  exact ‚ü®by positivity, by simpa only [zero_add, div_lt_one this, Nat.cast_lt] using val_lt _‚ü©

/-- The additive character from `ZMod N` to the unit circle in `‚ÑÇ`, sending `j mod N` to
`exp (2 * œÄ * I * j / N)`. -/
noncomputable def toCircle {N : ‚Ñï+} : AddChar (ZMod N) circle where
  toFun := fun j ‚Ü¶ (toAddCircle j).toCircle
  map_add_mul' a b := by simp_rw [map_add, AddCircle.toCircle_add]
  map_zero_one' := by simp_rw [map_zero, AddCircle.toCircle, ‚Üê QuotientAddGroup.mk_zero,
    Function.Periodic.lift_coe, mul_zero, expMapCircle_zero]

lemma toCircle_coe {N : ‚Ñï+} (j : ‚Ñ§) :
    toCircle (j : ZMod N) = Complex.exp (2 * œÄ * Complex.I * j / N) := by
  rw [toCircle, AddChar.coe_mk, AddCircle.toCircle, toAddCircle_coe,
    Function.Periodic.lift_coe, expMapCircle_apply]
  push_cast
  ring_nf

lemma toCircle_apply {N : ‚Ñï+} (j : ZMod N) :
    toCircle j = Complex.exp (2 * œÄ * Complex.I * j.val / N) := by
  rw [‚Üê Int.cast_natCast, ‚Üê toCircle_coe, natCast_val, intCast_zmod_cast]

/-- The additive character from `ZMod N` to `‚ÑÇ`, sending
`j mod N` to `exp (2 * œÄ * I * j / N)`. -/
noncomputable def stdAddChar {N : ‚Ñï+} : AddChar (ZMod N) ‚ÑÇ :=
  (Submonoid.subtype circle).compAddChar toCircle

lemma stdAddChar_coe {N : ‚Ñï+} (j : ‚Ñ§) :
    stdAddChar (j : ZMod N) = Complex.exp (2 * œÄ * Complex.I * j / N) := by
  simp only [stdAddChar, MonoidHom.coe_compAddChar, Function.comp_apply,
    Submonoid.coe_subtype, toCircle_coe]

lemma stdAddChar_apply {N : ‚Ñï+} (j : ZMod N) :
    stdAddChar j = ‚Üë(toCircle j) :=
  rfl

section fourier

open MeasureTheory

/-- The discrete measurable space structure (every set is measurable). -/
local instance instMeasurableSpaceZMod (N : ‚Ñï+) : MeasurableSpace (ZMod N) := ‚ä§

/-- The discrete Fourier transform on `‚Ñ§ / N‚Ñ§`. -/
noncomputable def discreteFourierTransform {N : ‚Ñï+} (Œ¶ : ZMod N ‚Üí ‚ÑÇ) (k : ZMod N) : ‚ÑÇ :=
  Fourier.fourierIntegral toCircle Measure.count Œ¶ k

@[inherit_doc]
scoped notation "ùìï" => discreteFourierTransform

lemma discreteFourierTransform_def {N : ‚Ñï+} (Œ¶ : ZMod N ‚Üí ‚ÑÇ) (k : ZMod N) :
    ùìï Œ¶ k = ‚àë j : ZMod N, toCircle (-(j * k)) ‚Ä¢ Œ¶ j := by
  simp only [discreteFourierTransform, Fourier.fourierIntegral_def,
    integral_countable' (integrable_count_iff.mpr <| Finite.summable _), Measure.count_singleton,
    ENNReal.one_toReal, one_smul, tsum_fintype]

end fourier

end ZMod

open ZMod

namespace DirichletCharacter

variable {N : ‚Ñï+} (œá : DirichletCharacter ‚ÑÇ N)

lemma fourierTransform_eq_gaussSum_mulShift (k : ZMod N) :
    ùìï œá k = gaussSum œá (stdAddChar.mulShift (-k)) := by
  simp only [discreteFourierTransform_def]
  refine congr_arg Finset.univ.sum (funext fun j ‚Ü¶ ?_)
  rw [AddChar.mulShift_apply, mul_comm j, Submonoid.smul_def, smul_eq_mul, neg_mul,
    stdAddChar_apply, mul_comm (œá _)]

/-- For a primitive Dirichlet character `œá`, the Fourier transform of `œá` is a constant multiple
of `œá‚Åª¬π` (and the constant is essentially the Gauss sum). -/
lemma fourierTransform_eq_inv_mul_gaussSum (k : ZMod N) (hœá : isPrimitive œá) :
    ùìï œá k = œá‚Åª¬π (-k) * gaussSum œá stdAddChar := by
  rw [fourierTransform_eq_gaussSum_mulShift, gaussSum_mulShift_of_isPrimitive _ hœá]

end DirichletCharacter
