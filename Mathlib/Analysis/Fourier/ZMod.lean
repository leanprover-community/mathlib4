/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Algebra.Group.EvenFunction
import Mathlib.Analysis.SpecialFunctions.Complex.CircleAddChar
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.NumberTheory.DirichletCharacter.GaussSum

/-!
# Fourier theory on `ZMod N`

Basic definitions and properties of the discrete Fourier transform for functions on `ZMod N`
(taking values in an arbitrary `â„‚`-vector space).

### Main definitions and results

* `ZMod.dft`: the Fourier transform, with respect to the standard additive character
  `ZMod.stdAddChar` (mapping `j mod N` to `exp (2 * Ï€ * I * j / N)`). The notation `ğ“•`, scoped in
  namespace `ZMod`, is available for this.
* `ZMod.dft_dft`: the Fourier inversion formula.
* `DirichletCharacter.fourierTransform_eq_inv_mul_gaussSum`: the discrete Fourier transform of a
  primitive Dirichlet character `Ï‡` is a Gauss sum times `Ï‡â»Â¹`.
-/

open MeasureTheory Finset AddChar ZMod

namespace ZMod

variable {N : â„•} [NeZero N] {E : Type*} [AddCommGroup E] [Module â„‚ E]

section private_defs
/-
It doesn't _quite_ work to define the Fourier transform as a `LinearEquiv` in one go, because that
leads to annoying repetition between the proof fields. So we set up a private definition first,
prove a minimal set of lemmas about it, and then define the `LinearEquiv` using that.

**Do not add more lemmas about `auxDFT`**: it should be invisible to end-users.
-/

/--
The discrete Fourier transform on `â„¤ / N â„¤` (with the counting measure). This definition is
private because it is superseded by the bundled `LinearEquiv` version.
-/
private noncomputable def auxDFT (Î¦ : ZMod N â†’ E) (k : ZMod N) : E :=
  âˆ‘ j : ZMod N, stdAddChar (-(j * k)) â€¢ Î¦ j

private lemma auxDFT_neg (Î¦ : ZMod N â†’ E) : auxDFT (fun j â†¦ Î¦ (-j)) = fun k â†¦ auxDFT Î¦ (-k) := by
  ext1 k; simpa only [auxDFT] using
    Fintype.sum_equiv (Equiv.neg _) _ _ (fun j â†¦ by rw [Equiv.neg_apply, neg_mul_neg])

/-- Fourier inversion formula, discrete case. -/
private lemma auxDFT_auxDFT (Î¦ : ZMod N â†’ E) : auxDFT (auxDFT Î¦) = fun j â†¦ (N : â„‚) â€¢ Î¦ (-j) := by
  ext1 j
  simp only [auxDFT, mul_comm _ j, smul_sum, â† smul_assoc, smul_eq_mul, â† map_add_eq_mul, â†
    neg_add, â† add_mul]
  rw [sum_comm]
  simp only [â† sum_smul, â† neg_mul]
  have h1 (t : ZMod N) : âˆ‘ i, stdAddChar (t * i) = if t = 0 then â†‘N else 0 := by
    split_ifs with h
    Â· simp only [h, zero_mul, map_zero_eq_one, sum_const, card_univ, card,
        nsmul_eq_mul, mul_one]
    Â· exact sum_eq_zero_of_ne_one (isPrimitive_stdAddChar N h)
  have h2 (x j : ZMod N) : -(j + x) = 0 â†” x = -j := by
    rw [neg_add, add_comm, add_eq_zero_iff_neg_eq, neg_neg]
  simp only [h1, h2, ite_smul, zero_smul, sum_ite_eq', mem_univ, ite_true]

private lemma auxDFT_smul (c : â„‚) (Î¦ : ZMod N â†’ E) :
    auxDFT (c â€¢ Î¦) = c â€¢ auxDFT Î¦ := by
  ext; simp only [Pi.smul_def, auxDFT, smul_sum, smul_comm c]

end private_defs

section defs

/--
The discrete Fourier transform on `â„¤ / N â„¤` (with the counting measure), bundled as a linear
equivalence. Denoted as `ğ“•` within the `ZMod` namespace.
-/
noncomputable def dft : (ZMod N â†’ E) â‰ƒâ‚—[â„‚] (ZMod N â†’ E) where
  toFun := auxDFT
  map_add' Î¦â‚ Î¦â‚‚ := by
    ext; simp only [auxDFT, Pi.add_def, smul_add, sum_add_distrib]
  map_smul' c Î¦ := by
    ext; simp only [auxDFT, Pi.smul_apply, RingHom.id_apply, smul_sum, smul_comm c]
  invFun Î¦ k := (N : â„‚)â»Â¹ â€¢ auxDFT Î¦ (-k)
  left_inv Î¦ := by
    simp only [auxDFT_auxDFT, neg_neg, â† mul_smul, inv_mul_cancelâ‚€ (NeZero.ne _), one_smul]
  right_inv Î¦ := by
    ext1 j
    simp only [â† Pi.smul_def, auxDFT_smul, auxDFT_neg, auxDFT_auxDFT, neg_neg, â† mul_smul,
      inv_mul_cancelâ‚€ (NeZero.ne _), one_smul]

@[inherit_doc] scoped notation "ğ“•" => dft

/-- The inverse Fourier transform on `ZMod N`. -/
scoped notation "ğ“•â»" => LinearEquiv.symm dft

lemma dft_apply (Î¦ : ZMod N â†’ E) (k : ZMod N) :
    ğ“• Î¦ k = âˆ‘ j : ZMod N, stdAddChar (-(j * k)) â€¢ Î¦ j :=
  rfl

lemma dft_def (Î¦ : ZMod N â†’ E) :
    ğ“• Î¦ = fun k â†¦ âˆ‘ j : ZMod N, stdAddChar (-(j * k)) â€¢ Î¦ j :=
  rfl

lemma invDFT_apply (Î¨ : ZMod N â†’ E) (k : ZMod N) :
    ğ“•â» Î¨ k = (N : â„‚)â»Â¹ â€¢ âˆ‘ j : ZMod N, stdAddChar (j * k) â€¢ Î¨ j := by
  simp only [dft, LinearEquiv.coe_symm_mk, auxDFT, mul_neg, neg_neg]

lemma invDFT_def (Î¨ : ZMod N â†’ E) :
    ğ“•â» Î¨ = fun k â†¦ (N : â„‚)â»Â¹ â€¢ âˆ‘ j : ZMod N, stdAddChar (j * k) â€¢ Î¨ j :=
  funext <| invDFT_apply Î¨

lemma invDFT_apply' (Î¨ : ZMod N â†’ E) (k : ZMod N) : ğ“•â» Î¨ k = (N : â„‚)â»Â¹ â€¢ ğ“• Î¨ (-k) :=
  rfl

lemma invDFT_def' (Î¨ : ZMod N â†’ E) : ğ“•â» Î¨ = fun k â†¦ (N : â„‚)â»Â¹ â€¢ ğ“• Î¨ (-k) :=
  rfl

lemma dft_apply_zero (Î¦ : ZMod N â†’ E) : ğ“• Î¦ 0 = âˆ‘ j, Î¦ j := by
  simp only [dft_apply, mul_zero, neg_zero, map_zero_eq_one, one_smul]

/--
The discrete Fourier transform agrees with the general one (assuming the target space is a complete
normed space).
-/
lemma dft_eq_fourier {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [CompleteSpace E]
    (Î¦ : ZMod N â†’ E) (k : ZMod N) :
    ğ“• Î¦ k = Fourier.fourierIntegral toCircle Measure.count Î¦ k := by
  simp only [dft_apply, stdAddChar_apply, Fourier.fourierIntegral_def, Circle.smul_def,
    integral_countable' <| .of_finite .., count_real_singleton, one_smul, tsum_fintype]

end defs

section arith
/-!
## Compatibility with scalar multiplication

These lemmas are more general than `LinearEquiv.map_mul` etc, since they allow any scalars that
commute with the `â„‚`-action, rather than just `â„‚` itself.
-/

lemma dft_const_smul {R : Type*} [DistribSMul R E] [SMulCommClass R â„‚ E] (r : R) (Î¦ : ZMod N â†’ E) :
    ğ“• (r â€¢ Î¦) = r â€¢ ğ“• Î¦ := by
  simp only [Pi.smul_def, dft_def, smul_sum, smul_comm]

lemma dft_smul_const {R : Type*} [Ring R] [Module â„‚ R] [Module R E] [IsScalarTower â„‚ R E]
    (Î¦ : ZMod N â†’ R) (e : E) :
    ğ“• (fun j â†¦ Î¦ j â€¢ e) = fun k â†¦ ğ“• Î¦ k â€¢ e := by
  simp only [dft_def, sum_smul, smul_assoc]

lemma dft_const_mul {R : Type*} [Ring R] [Algebra â„‚ R] (r : R) (Î¦ : ZMod N â†’ R) :
    ğ“• (fun j â†¦ r * Î¦ j) = fun k â†¦ r * ğ“• Î¦ k :=
  dft_const_smul r Î¦

lemma dft_mul_const {R : Type*} [Ring R] [Algebra â„‚ R] (Î¦ : ZMod N â†’ R) (r : R) :
    ğ“• (fun j â†¦ Î¦ j * r) = fun k â†¦ ğ“• Î¦ k * r :=
  dft_smul_const Î¦ r

end arith

section inversion

lemma dft_comp_neg (Î¦ : ZMod N â†’ E) : ğ“• (fun j â†¦ Î¦ (-j)) = fun k â†¦ ğ“• Î¦ (-k) :=
  auxDFT_neg ..

/-- Fourier inversion formula, discrete case. -/
lemma dft_dft (Î¦ : ZMod N â†’ E) : ğ“• (ğ“• Î¦) = fun j â†¦ (N : â„‚) â€¢ Î¦ (-j) :=
  auxDFT_auxDFT ..

end inversion

lemma dft_comp_unitMul (Î¦ : ZMod N â†’ E) (u : (ZMod N)Ë£) (k : ZMod N) :
    ğ“• (fun j â†¦ Î¦ (u.val * j)) k = ğ“• Î¦ (uâ»Â¹.val * k) := by
  refine Fintype.sum_equiv u.mulLeft _ _ fun x â†¦ ?_
  simp only [mul_comm u.val, u.mulLeft_apply, â† mul_assoc, u.mul_inv_cancel_right]

section signs

/-- The discrete Fourier transform of `Î¦` is even if and only if `Î¦` itself is. -/
lemma dft_even_iff {Î¦ : ZMod N â†’ â„‚} : (ğ“• Î¦).Even â†” Î¦.Even := by
  have h {f : ZMod N â†’ â„‚} (hf : f.Even) : (ğ“• f).Even := by
    simp only [Function.Even, â† congr_fun (dft_comp_neg f), funext hf, implies_true]
  refine âŸ¨fun hÎ¦ x â†¦ ?_, hâŸ©
  simpa only [neg_neg, smul_right_inj (NeZero.ne (N : â„‚)), dft_dft] using h hÎ¦ (-x)

/-- The discrete Fourier transform of `Î¦` is odd if and only if `Î¦` itself is. -/
lemma dft_odd_iff {Î¦ : ZMod N â†’ â„‚} : (ğ“• Î¦).Odd â†” Î¦.Odd := by
  have h {f : ZMod N â†’ â„‚} (hf : f.Odd) : (ğ“• f).Odd := by
    simp only [Function.Odd, â† congr_fun (dft_comp_neg f), funext hf, â† Pi.neg_apply, map_neg,
      implies_true]
  refine âŸ¨fun hÎ¦ x â†¦ ?_, hâŸ©
  simpa only [neg_neg, dft_dft, â† smul_neg, smul_right_inj (NeZero.ne (N : â„‚))] using h hÎ¦ (-x)

end signs

end ZMod

namespace DirichletCharacter

variable {N : â„•} [NeZero N]

lemma fourierTransform_eq_gaussSum_mulShift (Ï‡ : DirichletCharacter â„‚ N) (k : ZMod N) :
    ğ“• Ï‡ k = gaussSum Ï‡ (stdAddChar.mulShift (-k)) := by
  simp only [dft_apply, smul_eq_mul]
  congr 1 with j
  rw [mulShift_apply, mul_comm j, neg_mul, stdAddChar_apply, mul_comm (Ï‡ _)]

/-- For a primitive Dirichlet character `Ï‡`, the Fourier transform of `Ï‡` is a constant multiple
of `Ï‡â»Â¹` (and the constant is essentially the Gauss sum). -/
lemma IsPrimitive.fourierTransform_eq_inv_mul_gaussSum {Ï‡ : DirichletCharacter â„‚ N}
    (hÏ‡ : IsPrimitive Ï‡) (k : ZMod N) :
    ğ“• Ï‡ k = Ï‡â»Â¹ (-k) * gaussSum Ï‡ stdAddChar := by
  rw [fourierTransform_eq_gaussSum_mulShift, gaussSum_mulShift_of_isPrimitive _ hÏ‡]

end DirichletCharacter
