/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, David Loeffler, Heather Macbeth, SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Calculus.LineDeriv.IntegrationByParts
import Mathlib.Analysis.Calculus.ContDiff.Bounds

/-!
# Derivatives of the Fourier transform

In this file we compute the FrÃ©chet derivative of the Fourier transform of `f`, where `f` is a
function such that both `f` and `v â†¦ â€–vâ€– * â€–f vâ€–` are integrable. Here the Fourier transform is
understood as an operator `(V â†’ E) â†’ (W â†’ E)`, where `V` and `W` are normed `â„`-vector spaces
and the Fourier transform is taken with respect to a continuous `â„`-bilinear
pairing `L : V Ã— W â†’ â„` and a given reference measure `Î¼`.

We also investigate higher derivatives: Assuming that `â€–vâ€–^n * â€–f vâ€–` is integrable, we show
that the Fourier transform of `f` is `C^n`.

We also study in a parallel way the Fourier transform of the derivative, which is obtained by
tensoring the Fourier transform of the original function with the bilinear form. We also get
results for iterated derivatives.

A consequence of these results is that, if a function is smooth and all its derivatives are
integrable when multiplied by `â€–vâ€–^k`, then the same goes for its Fourier transform, with
explicit bounds.

We give specialized versions of these results on inner product spaces (where `L` is the scalar
product) and on the real line, where we express the one-dimensional derivative in more concrete
terms, as the Fourier transform of `-2Ï€I x * f x` (or `(-2Ï€I x)^n * f x` for higher derivatives).

## Main definitions and results

We introduce two convenience definitions:

* `VectorFourier.fourierSMulRight L f`: given `f : V â†’ E` and `L` a bilinear pairing
  between `V` and `W`, then this is the function `fun v â†¦ -(2 * Ï€ * I) (L v â¬) â€¢ f v`,
  from `V` to `Hom (W, E)`.
  This is essentially `ContinousLinearMap.smulRight`, up to the factor `- 2Ï€I` designed to make sure
  that the Fourier integral of `fourierSMulRight L f` is the derivative of the Fourier
  integral of `f`.
* `VectorFourier.fourierPowSMulRight` is the higher order analogue for higher derivatives:
  `fourierPowSMulRight L f v n` is informally `(-(2 * Ï€ * I))^n (L v â¬)^n â€¢ f v`, in
  the space of continuous multilinear maps `W [Ã—n]â†’L[â„] E`.

With these definitions, the statements read as follows, first in a general context
(arbitrary `L` and `Î¼`):

* `VectorFourier.hasFDerivAt_fourierIntegral`: the Fourier integral of `f` is differentiable, with
    derivative the Fourier integral of `fourierSMulRight L f`.
* `VectorFourier.differentiable_fourierIntegral`: the Fourier integral of `f` is differentiable.
* `VectorFourier.fderiv_fourierIntegral`: formula for the derivative of the Fourier integral of `f`.
* `VectorFourier.fourierIntegral_fderiv`: formula for the Fourier integral of the derivative of `f`.
* `VectorFourier.hasFTaylorSeriesUpTo_fourierIntegral`: under suitable integrability conditions,
  the Fourier integral of `f` has an explicit Taylor series up to order `N`, given by the Fourier
  integrals of `fun v â†¦ fourierPowSMulRight L f v n`.
* `VectorFourier.contDiff_fourierIntegral`: under suitable integrability conditions,
  the Fourier integral of `f` is `C^n`.
* `VectorFourier.iteratedFDeriv_fourierIntegral`: under suitable integrability conditions,
  explicit formula for the `n`-th derivative of the Fourier integral of `f`, as the Fourier
  integral of `fun v â†¦ fourierPowSMulRight L f v n`.
* `VectorFourier.pow_mul_norm_iteratedFDeriv_fourierIntegral_le`: explicit bounds for the `n`-th
  derivative of the Fourier integral, multiplied by a power function, in terms of corresponding
  integrals for the original function.

These statements are then specialized to the case of the usual Fourier transform on
finite-dimensional inner product spaces with their canonical Lebesgue measure (covering in
particular the case of the real line), replacing the namespace `VectorFourier` by
the namespace `Real` in the above statements.

We also give specialized versions of the one-dimensional real derivative (and iterated derivative)
in `Real.deriv_fourierIntegral` and `Real.iteratedDeriv_fourierIntegral`.
-/

noncomputable section

open Real Complex MeasureTheory Filter TopologicalSpace

open scoped FourierTransform Topology

-- without this local instance, Lean tries first the instance
-- `secondCountableTopologyEither_of_right` (whose priority is 100) and takes a very long time to
-- fail. Since we only use the left instance in this file, we make sure it is tried first.
attribute [local instance 101] secondCountableTopologyEither_of_left

namespace Real

lemma hasDerivAt_fourierChar (x : â„) : HasDerivAt (ğ Â· : â„ â†’ â„‚) (2 * Ï€ * I * ğ x) x := by
  have h1 (y : â„) : ğ y = fourier 1 (y : UnitAddCircle) := by
    rw [fourierChar_apply, fourier_coe_apply]
    push_cast
    ring_nf
  simpa only [h1, Int.cast_one, ofReal_one, div_one, mul_one] using hasDerivAt_fourier 1 1 x

lemma differentiable_fourierChar : Differentiable â„ (ğ Â· : â„ â†’ â„‚) :=
  fun x â†¦ (Real.hasDerivAt_fourierChar x).differentiableAt

lemma deriv_fourierChar (x : â„) : deriv (ğ Â· : â„ â†’ â„‚) x = 2 * Ï€ * I * ğ x :=
  (Real.hasDerivAt_fourierChar x).deriv

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup W] [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„)

lemma hasFDerivAt_fourierChar_neg_bilinear_right (v : V) (w : W) :
    HasFDerivAt (fun w â†¦ (ğ (-L v w) : â„‚))
      ((-2 * Ï€ * I * ğ (-L v w)) â€¢ (ofRealCLM âˆ˜L (L v))) w := by
  have ha : HasFDerivAt (fun w' : W â†¦ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert (hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg using 1
  ext y
  simp only [neg_mul, ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_comp', Pi.smul_apply,
    Function.comp_apply, ofRealCLM_apply, smul_eq_mul, ContinuousLinearMap.comp_neg,
    ContinuousLinearMap.neg_apply, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.one_apply, real_smul, neg_inj]
  ring

lemma fderiv_fourierChar_neg_bilinear_right_apply (v : V) (w y : W) :
    fderiv â„ (fun w â†¦ (ğ (-L v w) : â„‚)) w y = -2 * Ï€ * I * L v y * ğ (-L v w) := by
  simp only [(hasFDerivAt_fourierChar_neg_bilinear_right L v w).fderiv, neg_mul,
    ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_comp', Pi.smul_apply,
    Function.comp_apply, ofRealCLM_apply, smul_eq_mul, neg_inj]
  ring

lemma differentiable_fourierChar_neg_bilinear_right (v : V) :
    Differentiable â„ (fun w â†¦ (ğ (-L v w) : â„‚)) :=
  fun w â†¦ (hasFDerivAt_fourierChar_neg_bilinear_right L v w).differentiableAt

lemma hasFDerivAt_fourierChar_neg_bilinear_left (v : V) (w : W) :
    HasFDerivAt (fun v â†¦ (ğ (-L v w) : â„‚))
      ((-2 * Ï€ * I * ğ (-L v w)) â€¢ (ofRealCLM âˆ˜L (L.flip w))) v :=
  hasFDerivAt_fourierChar_neg_bilinear_right L.flip w v

lemma fderiv_fourierChar_neg_bilinear_left_apply (v y : V) (w : W) :
    fderiv â„ (fun v â†¦ (ğ (-L v w) : â„‚)) v y = -2 * Ï€ * I * L y w * ğ (-L v w) := by
  simp only [(hasFDerivAt_fourierChar_neg_bilinear_left L v w).fderiv, neg_mul,
    ContinuousLinearMap.coe_smul', ContinuousLinearMap.coe_comp', Pi.smul_apply,
    Function.comp_apply, ContinuousLinearMap.flip_apply, ofRealCLM_apply, smul_eq_mul, neg_inj]
  ring

lemma differentiable_fourierChar_neg_bilinear_left (w : W) :
    Differentiable â„ (fun v â†¦ (ğ (-L v w) : â„‚)) :=
  fun v â†¦ (hasFDerivAt_fourierChar_neg_bilinear_left L v w).differentiableAt

end Real

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

namespace VectorFourier

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup W] [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E)

/-- Send a function `f : V â†’ E` to the function `f : V â†’ Hom (W, E)` given by
`v â†¦ (w â†¦ -2 * Ï€ * I * L (v, w) â€¢ f v)`. This is designed so that the Fourier transform of
`fourierSMulRight L f` is the derivative of the Fourier transform of `f`. -/
def fourierSMulRight (v : V) : (W â†’L[â„] E) := -(2 * Ï€ * I) â€¢ (L v).smulRight (f v)

@[simp] lemma fourierSMulRight_apply (v : V) (w : W) :
    fourierSMulRight L f v w = -(2 * Ï€ * I) â€¢ L v w â€¢ f v := rfl

/-- The `w`-derivative of the Fourier transform integrand. -/
lemma hasFDerivAt_fourierChar_smul (v : V) (w : W) :
    HasFDerivAt (fun w' â†¦ ğ (-L v w') â€¢ f v) (ğ (-L v w) â€¢ fourierSMulRight L f v) w := by
  have ha : HasFDerivAt (fun w' : W â†¦ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert ((hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg).smul_const (f v)
  ext w' : 1
  simp_rw [fourierSMulRight, ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply]
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, â† smul_assoc, smul_comm,
    â† smul_assoc, real_smul, real_smul, Submonoid.smul_def, smul_eq_mul]
  push_cast
  ring_nf

lemma norm_fourierSMulRight (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) :
    â€–fourierSMulRight L f vâ€– = (2 * Ï€) * â€–L vâ€– * â€–f vâ€– := by
  rw [fourierSMulRight, norm_smul _ (ContinuousLinearMap.smulRight (L v) (f v)),
    norm_neg, norm_mul, norm_mul, norm_eq_abs I, abs_I,
    mul_one, norm_eq_abs ((_ : â„) : â„‚), Complex.abs_of_nonneg pi_pos.le, norm_eq_abs (2 : â„‚),
    Complex.abs_two, ContinuousLinearMap.norm_smulRight_apply, â† mul_assoc]

lemma norm_fourierSMulRight_le (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) :
    â€–fourierSMulRight L f vâ€– â‰¤ 2 * Ï€ * â€–Lâ€– * â€–vâ€– * â€–f vâ€– := calc
  â€–fourierSMulRight L f vâ€– = (2 * Ï€) * â€–L vâ€– * â€–f vâ€– := norm_fourierSMulRight _ _ _
  _ â‰¤ (2 * Ï€) * (â€–Lâ€– * â€–vâ€–) * â€–f vâ€– := by gcongr; exact L.le_opNorm _
  _ = 2 * Ï€ * â€–Lâ€– * â€–vâ€– * â€–f vâ€– := by ring

lemma _root_.MeasureTheory.AEStronglyMeasurable.fourierSMulRight
    [SecondCountableTopologyEither V (W â†’L[â„] â„)] [MeasurableSpace V] [BorelSpace V]
    {L : V â†’L[â„] W â†’L[â„] â„} {f : V â†’ E} {Î¼ : Measure V}
    (hf : AEStronglyMeasurable f Î¼) :
    AEStronglyMeasurable (fun v â†¦ fourierSMulRight L f v) Î¼ := by
  apply AEStronglyMeasurable.const_smul'
  have aux0 : Continuous fun p : (W â†’L[â„] â„) Ã— E â†¦ p.1.smulRight p.2 :=
    (ContinuousLinearMap.smulRightL â„ W E).continuousâ‚‚
  have aux1 : AEStronglyMeasurable (fun v â†¦ (L v, f v)) Î¼ :=
    L.continuous.aestronglyMeasurable.prod_mk hf
  -- Elaboration without the expected type is faster here:
  exact (aux0.comp_aestronglyMeasurable aux1 : _)

variable {f}

/-- Main theorem of this section: if both `f` and `x â†¦ â€–xâ€– * â€–f xâ€–` are integrable, then the
Fourier transform of `f` has a FrÃ©chet derivative (everywhere in its domain) and its derivative is
the Fourier transform of `smulRight L f`. -/
theorem hasFDerivAt_fourierIntegral
    [MeasurableSpace V] [BorelSpace V] [SecondCountableTopology V] {Î¼ : Measure V}
    (hf : Integrable f Î¼) (hf' : Integrable (fun v : V â†¦ â€–vâ€– * â€–f vâ€–) Î¼) (w : W) :
    HasFDerivAt (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)
      (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fourierSMulRight L f) w) w := by
  let F : W â†’ V â†’ E := fun w' v â†¦ ğ (-L v w') â€¢ f v
  let F' : W â†’ V â†’ W â†’L[â„] E := fun w' v â†¦ ğ (-L v w') â€¢ fourierSMulRight L f v
  let B : V â†’ â„ := fun v â†¦ 2 * Ï€ * â€–Lâ€– * â€–vâ€– * â€–f vâ€–
  have h0 (w' : W) : Integrable (F w') Î¼ :=
    (fourierIntegral_convergent_iff continuous_fourierChar
      (by apply L.continuousâ‚‚ : Continuous (fun p : V Ã— W â†¦ L.toLinearMapâ‚‚ p.1 p.2)) w').2 hf
  have h1 : âˆ€á¶  w' in ğ“ w, AEStronglyMeasurable (F w') Î¼ :=
    eventually_of_forall (fun w' â†¦ (h0 w').aestronglyMeasurable)
  have h3 : AEStronglyMeasurable (F' w) Î¼ := by
    refine .smul ?_ hf.1.fourierSMulRight
    refine (continuous_fourierChar.comp ?_).aestronglyMeasurable
    exact (L.continuousâ‚‚.comp (Continuous.Prod.mk_left w)).neg
  have h4 : (âˆ€áµ v âˆ‚Î¼, âˆ€ (w' : W), w' âˆˆ Metric.ball w 1 â†’ â€–F' w' vâ€– â‰¤ B v) := by
    filter_upwards with v w' _
    rw [norm_circle_smul _ (fourierSMulRight L f v)]
    exact norm_fourierSMulRight_le L f v
  have h5 : Integrable B Î¼ := by simpa only [â† mul_assoc] using hf'.const_mul (2 * Ï€ * â€–Lâ€–)
  have h6 : âˆ€áµ v âˆ‚Î¼, âˆ€ w', w' âˆˆ Metric.ball w 1 â†’ HasFDerivAt (fun x â†¦ F x v) (F' w' v) w' :=
    ae_of_all _ (fun v w' _ â†¦ hasFDerivAt_fourierChar_smul L f v w')
  exact hasFDerivAt_integral_of_dominated_of_fderiv_le one_pos h1 (h0 w) h3 h4 h5 h6

lemma fderiv_fourierIntegral
    [MeasurableSpace V] [BorelSpace V] [SecondCountableTopology V] {Î¼ : Measure V}
    (hf : Integrable f Î¼) (hf' : Integrable (fun v : V â†¦ â€–vâ€– * â€–f vâ€–) Î¼) :
    fderiv â„ (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) =
      fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fourierSMulRight L f) := by
  ext w : 1
  exact (hasFDerivAt_fourierIntegral L hf hf' w).fderiv

lemma differentiable_fourierIntegral
    [MeasurableSpace V] [BorelSpace V] [SecondCountableTopology V] {Î¼ : Measure V}
    (hf : Integrable f Î¼) (hf' : Integrable (fun v : V â†¦ â€–vâ€– * â€–f vâ€–) Î¼) :
    Differentiable â„ (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) :=
  fun w â†¦ (hasFDerivAt_fourierIntegral L hf hf' w).differentiableAt

/-- The Fourier integral of the derivative of a function is obtained by multiplying the Fourier
integral of the original function by `-L w v`. -/
theorem fourierIntegral_fderiv [MeasurableSpace V] [BorelSpace V] [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼]
    (hf : Integrable f Î¼) (h'f : Differentiable â„ f) (hf' : Integrable (fderiv â„ f) Î¼) :
    fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fderiv â„ f)
      = fourierSMulRight (-L.flip) (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) := by
  ext w y
  let g (v : V) : â„‚ := ğ (-L v w)
  /- First rewrite things in a simplified form, without any real change. -/
  suffices âˆ« x, g x â€¢ fderiv â„ f x y âˆ‚Î¼ = âˆ« x, (2 * â†‘Ï€ * I * L y w * g x) â€¢ f x âˆ‚Î¼ by
    rw [fourierIntegral_continuousLinearMap_apply' hf']
    simpa only [fourierIntegral, ContinuousLinearMap.toLinearMapâ‚‚_apply, fourierSMulRight_apply,
      ContinuousLinearMap.neg_apply, ContinuousLinearMap.flip_apply, â† integral_smul, neg_smul,
      smul_neg, â† smul_smul, coe_smul, neg_neg]
  -- Key step: integrate by parts with respect to `y` to switch the derivative from `f` to `g`.
  have A x : fderiv â„ g x y = - 2 * â†‘Ï€ * I * L y w * g x :=
    fderiv_fourierChar_neg_bilinear_left_apply _ _ _ _
  rw [integral_smul_fderiv_eq_neg_fderiv_smul_of_integrable, â† integral_neg]
  Â· congr with x
    simp only [A, neg_mul, neg_smul, neg_neg]
  Â· have : Integrable (fun x â†¦ (-(2 * â†‘Ï€ * I * â†‘((L y) w)) â€¢ ((g x : â„‚) â€¢ f x))) Î¼ :=
      ((fourierIntegral_convergent_iff' _ _).2 hf).smul _
    convert this using 2 with x
    simp only [A, neg_mul, neg_smul, smul_smul]
  Â· exact (fourierIntegral_convergent_iff' _ _).2 (hf'.apply_continuousLinearMap _)
  Â· exact (fourierIntegral_convergent_iff' _ _).2 hf
  Â· exact differentiable_fourierChar_neg_bilinear_left _ _
  Â· exact h'f

/-- The formal multilinear series whose `n`-th term is
`(wâ‚, ..., wâ‚™) â†¦ (-2Ï€I)^n * L v wâ‚ * ... * L v wâ‚™ â€¢ f v`, as a continuous multilinear map in
the space `W [Ã—n]â†’L[â„] E`.

This is designed so that the Fourier transform of `v â†¦ fourierPowSMulRight L f v n` is the
`n`-th derivative of the Fourier transform of `f`.
-/
def fourierPowSMulRight (f : V â†’ E) (v : V) : FormalMultilinearSeries â„ W E := fun n â†¦
  (- (2 * Ï€ * I))^n â€¢ ((ContinuousMultilinearMap.mkPiRing â„ (Fin n) (f v)).compContinuousLinearMap
  (fun _ â†¦ L v))

/- Increase the priority to make sure that this lemma is used instead of
`FormalMultilinearSeries.apply_eq_prod_smul_coeff` even in dimension 1. -/
@[simp 1100] lemma fourierPowSMulRight_apply {f : V â†’ E} {v : V} {n : â„•} {m : Fin n â†’ W} :
    fourierPowSMulRight L f v n m = (- (2 * Ï€ * I))^n â€¢ (âˆ i, L v (m i)) â€¢ f v := by
  simp [fourierPowSMulRight]

open ContinuousMultilinearMap

/-- Decomposing `fourierPowSMulRight L f v n` as a composition of continuous bilinear and
multilinear maps, to deduce easily its continuity and differentiability properties. -/
lemma fourierPowSMulRight_eq_comp {f : V â†’ E} {v : V} {n : â„•} :
    fourierPowSMulRight L f v n = (- (2 * Ï€ * I))^n â€¢ smulRightL â„ (fun (_ : Fin n) â†¦ W) E
      (compContinuousLinearMapLRight
        (ContinuousMultilinearMap.mkPiAlgebra â„ (Fin n) â„) (fun _ â†¦ L v)) (f v) := rfl

@[continuity, fun_prop]
lemma _root_.Continuous.fourierPowSMulRight {f : V â†’ E} (hf : Continuous f) (n : â„•) :
    Continuous (fun v â†¦ fourierPowSMulRight L f v n) := by
  simp_rw [fourierPowSMulRight_eq_comp]
  apply Continuous.const_smul
  apply (smulRightL â„ (fun (_ : Fin n) â†¦ W) E).continuousâ‚‚.compâ‚‚ _ hf
  exact Continuous.comp (map_continuous _) (continuous_pi (fun _ â†¦ L.continuous))

lemma _root_.ContDiff.fourierPowSMulRight {f : V â†’ E} {k : â„•âˆ} (hf : ContDiff â„ k f) (n : â„•) :
    ContDiff â„ k (fun v â†¦ fourierPowSMulRight L f v n) := by
  simp_rw [fourierPowSMulRight_eq_comp]
  apply ContDiff.const_smul
  apply (smulRightL â„ (fun (_ : Fin n) â†¦ W) E).isBoundedBilinearMap.contDiff.compâ‚‚ _ hf
  apply (ContinuousMultilinearMap.contDiff _).comp
  exact contDiff_pi.2 (fun _ â†¦ L.contDiff)

lemma norm_fourierPowSMulRight_le (f : V â†’ E) (v : V) (n : â„•) :
    â€–fourierPowSMulRight L f v nâ€– â‰¤ (2 * Ï€ * â€–Lâ€–) ^ n * â€–vâ€– ^ n * â€–f vâ€– := by
  apply ContinuousMultilinearMap.opNorm_le_bound _ (by positivity) (fun m â†¦ ?_)
  calc
  â€–fourierPowSMulRight L f v n mâ€–
    = (2 * Ï€) ^ n * ((âˆ x : Fin n, |(L v) (m x)|) * â€–f vâ€–) := by
      simp [_root_.abs_of_nonneg pi_nonneg, norm_smul]
  _ â‰¤ (2 * Ï€) ^ n * ((âˆ x : Fin n, â€–Lâ€– * â€–vâ€– * â€–m xâ€–) * â€–f vâ€–) := by
      gcongr with i _hi
      Â· exact fun i _hi â†¦ abs_nonneg _
      Â· exact L.le_opNormâ‚‚ v (m i)
  _ = (2 * Ï€ * â€–Lâ€–) ^ n * â€–vâ€– ^ n * â€–f vâ€– * âˆ i : Fin n, â€–m iâ€– := by
      simp [Finset.prod_mul_distrib, mul_pow]; ring

set_option maxSynthPendingDepth 2 in
/-- The iterated derivative of a function multiplied by `(L v â¬) ^ n` can be controlled in terms
of the iterated derivatives of the initial function. -/
lemma norm_iteratedFDeriv_fourierPowSMulRight
    {f : V â†’ E} {K : â„•âˆ} {C : â„} (hf : ContDiff â„ K f) {n : â„•} {k : â„•} (hk : k â‰¤ K)
    {v : V} (hv : âˆ€ i â‰¤ k, âˆ€ j â‰¤ n, â€–vâ€– ^ j * â€–iteratedFDeriv â„ i f vâ€– â‰¤ C) :
    â€–iteratedFDeriv â„ k (fun v â†¦ fourierPowSMulRight L f v n) vâ€– â‰¤
      (2 * Ï€) ^ n * (2 * n + 2) ^ k * â€–Lâ€– ^ n * C := by
  /- We write `fourierPowSMulRight L f v n` as a composition of bilinear and multilinear maps,
  thanks to `fourierPowSMulRight_eq_comp`, and then we control the iterated derivatives of these
  thanks to general bounds on derivatives of bilinear and multilinear maps. More precisely,
  `fourierPowSMulRight L f v n m = (- (2 * Ï€ * I))^n â€¢ (âˆ i, L v (m i)) â€¢ f v`. Here,
  `(- (2 * Ï€ * I))^n` contributes `(2Ï€)^n` to the bound. The second product is bilinear, so the
  iterated derivative is controlled as a weighted sum of those of `v â†¦ âˆ i, L v (m i)` and of `f`.

  The harder part is to control the iterated derivatives of `v â†¦ âˆ i, L v (m i)`. For this, one
  argues that this is multilinear in `v`, to apply general bounds for iterated derivatives of
  multilinear maps. More precisely, we write it as the composition of a multilinear map `T` (making
  the product operation) and the tuple of linear maps `v â†¦ (L v â¬, ..., L v â¬)` -/
  simp_rw [fourierPowSMulRight_eq_comp]
  -- first step: controlling the iterated derivatives of `v â†¦ âˆ i, L v (m i)`, written below
  -- as `v â†¦ T (fun _ â†¦ L v)`, or `T âˆ˜ (ContinuousLinearMap.pi (fun (_ : Fin n) â†¦ L))`.
  let T : (W â†’L[â„] â„) [Ã—n]â†’L[â„] (W [Ã—n]â†’L[â„] â„) :=
    compContinuousLinearMapLRight (ContinuousMultilinearMap.mkPiAlgebra â„ (Fin n) â„)
  have Iâ‚ m : â€–iteratedFDeriv â„ m T (fun _ â†¦ L v)â€– â‰¤
      n.descFactorial m * 1 * (â€–Lâ€– * â€–vâ€–) ^ (n - m) := by
    have : â€–Tâ€– â‰¤ 1 := by
      apply (norm_compContinuousLinearMapLRight_le _ _).trans
      simp only [norm_mkPiAlgebra, le_refl]
    apply (ContinuousMultilinearMap.norm_iteratedFDeriv_le _ _ _).trans
    simp only [Fintype.card_fin]
    gcongr
    refine (pi_norm_le_iff_of_nonneg (by positivity)).mpr (fun _ â†¦ ?_)
    exact ContinuousLinearMap.le_opNorm _ _
  have Iâ‚‚ m : â€–iteratedFDeriv â„ m (T âˆ˜ (ContinuousLinearMap.pi (fun (_ : Fin n) â†¦ L))) vâ€– â‰¤
      (n.descFactorial m * 1 * (â€–Lâ€– * â€–vâ€–) ^ (n - m)) * â€–Lâ€– ^ m := by
    rw [ContinuousLinearMap.iteratedFDeriv_comp_right _ (ContinuousMultilinearMap.contDiff _)
      _ le_top]
    apply (norm_compContinuousLinearMap_le _ _).trans
    simp only [Finset.prod_const, Finset.card_fin]
    gcongr
    Â· exact Iâ‚ m
    Â· exact ContinuousLinearMap.norm_pi_le_of_le (fun _ â†¦ le_rfl) (norm_nonneg _)
  have Iâ‚ƒ m : â€–iteratedFDeriv â„ m (T âˆ˜ (ContinuousLinearMap.pi (fun (_ : Fin n) â†¦ L))) vâ€– â‰¤
      n.descFactorial m * â€–Lâ€– ^ n * â€–vâ€– ^ (n - m) := by
    apply (Iâ‚‚ m).trans (le_of_eq _)
    rcases le_or_lt m n with hm | hm
    Â· rw [show â€–Lâ€– ^ n = â€–Lâ€– ^ (m + (n - m)) by rw [Nat.add_sub_cancel' hm], pow_add]
      ring
    Â· simp only [Nat.descFactorial_eq_zero_iff_lt.mpr hm, CharP.cast_eq_zero, mul_one, zero_mul]
  -- second step: factor out the `(2 * Ï€) ^ n` factor, and cancel it on both sides.
  have A : ContDiff â„ K (fun y â†¦ T (fun _ â†¦ L y)) :=
    (ContinuousMultilinearMap.contDiff _).comp (contDiff_pi.2 fun _ â†¦ L.contDiff)
  rw [iteratedFDeriv_const_smul_apply' (hf := (smulRightL â„ (fun _ â†¦ W)
    E).isBoundedBilinearMap.contDiff.compâ‚‚ (A.of_le hk) (hf.of_le hk)),
    norm_smul (Î² := V [Ã—k]â†’L[â„] (W [Ã—n]â†’L[â„] E))]
  simp only [norm_pow, norm_neg, norm_mul, RCLike.norm_ofNat, Complex.norm_eq_abs, abs_ofReal,
    _root_.abs_of_nonneg pi_nonneg, abs_I, mul_one, mul_assoc]
  gcongr
  -- third step: argue that the scalar multiplication is bilinear to bound the iterated derivatives
  -- of `v â†¦ (âˆ i, L v (m i)) â€¢ f v` in terms of those of `v â†¦ (âˆ i, L v (m i))` and of `f`.
  -- The former are controlled by the first step, the latter by the assumptions.
  apply (ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear_of_le_one _ A hf _
    hk ContinuousMultilinearMap.norm_smulRightL_le).trans
  calc
  âˆ‘ i in Finset.range (k + 1),
    k.choose i * â€–iteratedFDeriv â„ i (fun (y : V) â†¦ T (fun _ â†¦ L y)) vâ€– *
      â€–iteratedFDeriv â„ (k - i) f vâ€–
    â‰¤ âˆ‘ i in Finset.range (k + 1),
      k.choose i * (n.descFactorial i * â€–Lâ€– ^ n * â€–vâ€– ^ (n - i)) *
        â€–iteratedFDeriv â„ (k - i) f vâ€– := by
    gcongr with i _hi
    exact Iâ‚ƒ i
  _ = âˆ‘ i in Finset.range (k + 1), (k.choose i * n.descFactorial i * â€–Lâ€– ^ n) *
        (â€–vâ€– ^ (n - i) * â€–iteratedFDeriv â„ (k - i) f vâ€–) := by
    congr with i
    ring
  _ â‰¤ âˆ‘ i in Finset.range (k + 1), (k.choose i * (n + 1 : â„•) ^ k * â€–Lâ€– ^ n) * C := by
    gcongr with i hi
    Â· rw [â† Nat.cast_pow, Nat.cast_le]
      calc n.descFactorial i â‰¤ n ^ i := Nat.descFactorial_le_pow _ _
      _ â‰¤ (n + 1) ^ i := pow_le_pow_left (by omega) (by omega) i
      _ â‰¤ (n + 1) ^ k := pow_le_pow_right (by omega) (Finset.mem_range_succ_iff.mp hi)
    Â· exact hv _ (by omega) _ (by omega)
  _ = (2 * n + 2) ^ k * (â€–Lâ€–^n * C) := by
    simp only [â† Finset.sum_mul, â† Nat.cast_sum, Nat.sum_range_choose, mul_one, â† mul_assoc,
      Nat.cast_pow, Nat.cast_ofNat, Nat.cast_add, Nat.cast_one, â† mul_pow, mul_add]

variable [SecondCountableTopology V] [MeasurableSpace V] [BorelSpace V] {Î¼ : Measure V}

lemma _root_.MeasureTheory.AEStronglyMeasurable.fourierPowSMulRight
    (hf : AEStronglyMeasurable f Î¼) (n : â„•) :
    AEStronglyMeasurable (fun v â†¦ fourierPowSMulRight L f v n) Î¼ := by
  simp_rw [fourierPowSMulRight_eq_comp]
  apply AEStronglyMeasurable.const_smul'
  apply (smulRightL â„ (fun (_ : Fin n) â†¦ W) E).continuousâ‚‚.comp_aestronglyMeasurableâ‚‚ _ hf
  apply Continuous.aestronglyMeasurable
  exact Continuous.comp (map_continuous _) (continuous_pi (fun _ â†¦ L.continuous))

lemma integrable_fourierPowSMulRight {n : â„•} (hf : Integrable (fun v â†¦ â€–vâ€– ^ n * â€–f vâ€–) Î¼)
    (h'f : AEStronglyMeasurable f Î¼) : Integrable (fun v â†¦ fourierPowSMulRight L f v n) Î¼ := by
  refine (hf.const_mul ((2 * Ï€ * â€–Lâ€–) ^ n)).mono' (h'f.fourierPowSMulRight L n) ?_
  filter_upwards with v
  exact (norm_fourierPowSMulRight_le L f v n).trans (le_of_eq (by ring))

lemma hasFTaylorSeriesUpTo_fourierIntegral {N : â„•âˆ}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–) Î¼)
    (h'f : AEStronglyMeasurable f Î¼) :
    HasFTaylorSeriesUpTo N (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)
      (fun w n â†¦ fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fun v â†¦ fourierPowSMulRight L f v n) w) := by
  constructor
  Â· intro w
    rw [uncurry0_apply, Matrix.zero_empty, fourierIntegral_continuousMultilinearMap_apply'
      (integrable_fourierPowSMulRight L (hf 0 bot_le) h'f)]
    simp only [fourierPowSMulRight_apply, pow_zero, Finset.univ_eq_empty, Finset.prod_empty,
      one_smul]
  Â· intro n hn w
    have Iâ‚ : Integrable (fun v â†¦ fourierPowSMulRight L f v n) Î¼ :=
      integrable_fourierPowSMulRight L (hf n hn.le) h'f
    have Iâ‚‚ : Integrable (fun v â†¦ â€–vâ€– * â€–fourierPowSMulRight L f v nâ€–) Î¼ := by
      apply ((hf (n+1) (ENat.add_one_le_of_lt hn)).const_mul ((2 * Ï€ * â€–Lâ€–) ^ n)).mono'
        (continuous_norm.aestronglyMeasurable.mul (h'f.fourierPowSMulRight L n).norm)
      filter_upwards with v
      simp only [Pi.mul_apply, norm_mul, norm_norm]
      calc
      â€–vâ€– * â€–fourierPowSMulRight L f v nâ€–
        â‰¤ â€–vâ€– * ((2 * Ï€ * â€–Lâ€–) ^ n * â€–vâ€– ^ n * â€–f vâ€–) := by
          gcongr; apply norm_fourierPowSMulRight_le
      _ = (2 * Ï€ * â€–Lâ€–) ^ n * (â€–vâ€– ^ (n + 1) * â€–f vâ€–) := by rw [pow_succ]; ring
    have Iâ‚ƒ : Integrable (fun v â†¦ fourierPowSMulRight L f v (n + 1)) Î¼ :=
      integrable_fourierPowSMulRight L (hf (n + 1) (ENat.add_one_le_of_lt hn)) h'f
    have Iâ‚„ : Integrable
        (fun v â†¦ fourierSMulRight L (fun v â†¦ fourierPowSMulRight L f v n) v) Î¼ := by
      apply (Iâ‚‚.const_mul ((2 * Ï€ * â€–Lâ€–))).mono' (h'f.fourierPowSMulRight L n).fourierSMulRight
      filter_upwards with v
      exact (norm_fourierSMulRight_le _ _ _).trans (le_of_eq (by ring))
    have E : curryLeft
          (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fun v â†¦ fourierPowSMulRight L f v (n + 1)) w) =
        fourierIntegral ğ Î¼ L.toLinearMapâ‚‚
          (fourierSMulRight L fun v â†¦ fourierPowSMulRight L f v n) w := by
      ext w' m
      rw [curryLeft_apply, fourierIntegral_continuousMultilinearMap_apply' Iâ‚ƒ,
        fourierIntegral_continuousLinearMap_apply' Iâ‚„,
        fourierIntegral_continuousMultilinearMap_apply' (Iâ‚„.apply_continuousLinearMap _)]
      congr with v
      simp only [fourierPowSMulRight_apply, mul_comm, pow_succ, neg_mul, Fin.prod_univ_succ,
        Fin.cons_zero, Fin.cons_succ, neg_smul, fourierSMulRight_apply, neg_apply, smul_apply,
        smul_comm (M := â„) (N := â„‚) (Î± := E), smul_smul]
    exact E â–¸ hasFDerivAt_fourierIntegral L Iâ‚ Iâ‚‚ w
  Â· intro n hn
    apply fourierIntegral_continuous Real.continuous_fourierChar (by apply L.continuousâ‚‚)
    exact integrable_fourierPowSMulRight L (hf n hn) h'f

/-- If `â€–vâ€–^n * â€–f vâ€–` is integrable for all `n â‰¤ N`, then the Fourier transform of `f` is `C^N`. -/
theorem contDiff_fourierIntegral {N : â„•âˆ}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–) Î¼) :
    ContDiff â„ N (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) := by
  by_cases h'f : Integrable f Î¼
  Â· exact (hasFTaylorSeriesUpTo_fourierIntegral L hf h'f.1).contDiff
  Â· have : fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f = 0 := by
      ext w; simp [fourierIntegral, integral, h'f]
    simpa [this] using contDiff_const

/-- If `â€–vâ€–^n * â€–f vâ€–` is integrable for all `n â‰¤ N`, then the `n`-th derivative of the Fourier
transform of `f` is the Fourier transform of `fourierPowSMulRight L f v n`,
i.e., `(L v â¬) ^ n â€¢ f v`. -/
lemma iteratedFDeriv_fourierIntegral {N : â„•âˆ}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–) Î¼)
    (h'f : AEStronglyMeasurable f Î¼) {n : â„•} (hn : n â‰¤ N) :
    iteratedFDeriv â„ n (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) =
      fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fun v â†¦ fourierPowSMulRight L f v n) := by
  ext w : 1
  exact ((hasFTaylorSeriesUpTo_fourierIntegral L hf h'f).eq_iteratedFDeriv hn w).symm

/-- The Fourier integral of the `n`-th derivative of a function is obtained by multiplying the
Fourier integral of the original function by `(2Ï€I L w â¬ )^n`. -/
theorem fourierIntegral_iteratedFDeriv [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼] {N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f) Î¼) {n : â„•} (hn : n â‰¤ N) :
    fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (iteratedFDeriv â„ n f)
      = (fun w â†¦ fourierPowSMulRight (-L.flip) (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) w n) := by
  induction n with
  | zero =>
    ext w m
    simp only [iteratedFDeriv_zero_apply, Nat.zero_eq, fourierPowSMulRight_apply, pow_zero,
      Finset.univ_eq_empty, ContinuousLinearMap.neg_apply, ContinuousLinearMap.flip_apply,
      Finset.prod_empty, one_smul, fourierIntegral_continuousMultilinearMap_apply' ((h'f 0 bot_le))]
  | succ n ih =>
    ext w m
    have J : Integrable (fderiv â„ (iteratedFDeriv â„ n f)) Î¼ := by
      specialize h'f (n + 1) hn
      rw [iteratedFDeriv_succ_eq_comp_left] at h'f
      exact (LinearIsometryEquiv.integrable_comp_iff _).1 h'f
    suffices H : (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ (fderiv â„ (iteratedFDeriv â„ n f)) w)
          (m 0) (Fin.tail m) =
        (-(2 * Ï€ * I)) ^ (n + 1) â€¢ (âˆ x : Fin (n + 1), -L (m x) w) â€¢ âˆ« v, ğ (-L v w) â€¢ f v âˆ‚Î¼ by
      rw [fourierIntegral_continuousMultilinearMap_apply' (h'f _ hn)]
      simp only [iteratedFDeriv_succ_apply_left, fourierPowSMulRight_apply,
        ContinuousLinearMap.neg_apply, ContinuousLinearMap.flip_apply]
      rw [â† fourierIntegral_continuousMultilinearMap_apply' ((J.apply_continuousLinearMap _)),
          â† fourierIntegral_continuousLinearMap_apply' J]
      exact H
    have h'n : n < N := (Nat.cast_lt.mpr n.lt_succ_self).trans_le hn
    rw [fourierIntegral_fderiv _ (h'f n h'n.le) (hf.differentiable_iteratedFDeriv h'n) J]
    simp only [ih h'n.le, fourierSMulRight_apply, ContinuousLinearMap.neg_apply,
      ContinuousLinearMap.flip_apply, neg_smul, smul_neg, neg_neg, smul_apply,
      fourierPowSMulRight_apply, â† coe_smul (E := E), smul_smul]
    congr 1
    simp only [ofReal_prod, ofReal_neg, pow_succ, mul_neg, Fin.prod_univ_succ, neg_mul,
      ofReal_mul, neg_neg, Fin.tail_def]
    ring

/-- The `k`-th derivative of the Fourier integral of `f`, multiplied by `(L v w) ^ n`, is the
Fourier integral of the `n`-th derivative of `(L v w) ^ k * f`. -/
theorem fourierPowSMulRight_iteratedFDeriv_fourierIntegral [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼] {K N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (k n : â„•), k â‰¤ K â†’ n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^k * â€–iteratedFDeriv â„ n f vâ€–) Î¼)
    {k n : â„•} (hk : k â‰¤ K) (hn : n â‰¤ N) {w : W} :
    fourierPowSMulRight (-L.flip)
      (iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) w n =
    fourierIntegral ğ Î¼ L.toLinearMapâ‚‚
      (iteratedFDeriv â„ n (fun v â†¦ fourierPowSMulRight L f v k)) w := by
  rw [fourierIntegral_iteratedFDeriv (N := N) _ (hf.fourierPowSMulRight _ _) _ hn]
  Â· congr
    rw [iteratedFDeriv_fourierIntegral (N := K) _ _ hf.continuous.aestronglyMeasurable hk]
    intro k hk
    simpa only [norm_iteratedFDeriv_zero] using h'f k 0 hk bot_le
  Â· intro m hm
    have I : Integrable (fun v â†¦ âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (m + 1),
        â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€–) Î¼ := by
      apply integrable_finset_sum _ (fun p hp â†¦ ?_)
      simp only [Finset.mem_product, Finset.mem_range_succ_iff] at hp
      exact h'f _ _ ((Nat.cast_le.2 hp.1).trans hk) ((Nat.cast_le.2 hp.2).trans hm)
    apply (I.const_mul ((2 * Ï€) ^ k * (2 * k + 2) ^ m * â€–Lâ€– ^ k)).mono'
      ((hf.fourierPowSMulRight L k).continuous_iteratedFDeriv hm).aestronglyMeasurable
    filter_upwards with v
    refine norm_iteratedFDeriv_fourierPowSMulRight _ hf hm (fun i hi j hj â†¦ ?_)
    apply Finset.single_le_sum (f := fun p â†¦ â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€–) (a := (j, i))
    Â· intro i _hi
      positivity
    Â· simpa only [Finset.mem_product, Finset.mem_range_succ_iff] using âŸ¨hj, hiâŸ©

/-- One can bound the `k`-th derivative of the Fourier integral of `f`, multiplied by `(L v w) ^ n`,
in terms of integrals of iterated derivatives of `f` (of order up to `n`) multiplied by `â€–vâ€– ^ i`
(for `i â‰¤ k`).
Auxiliary version in terms of the operator norm of `fourierPowSMulRight (-L.flip) â¬`. For a version
in terms of `|L v w| ^ n * â¬`, see `pow_mul_norm_iteratedFDeriv_fourierIntegral_le`.
-/
theorem norm_fourierPowSMulRight_iteratedFDeriv_fourierIntegral_le [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼] {K N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (k n : â„•), k â‰¤ K â†’ n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^k * â€–iteratedFDeriv â„ n f vâ€–) Î¼)
    {k n : â„•} (hk : k â‰¤ K) (hn : n â‰¤ N) {w : W} :
    â€–fourierPowSMulRight (-L.flip)
      (iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) w nâ€– â‰¤
    (2 * Ï€) ^ k * (2 * k + 2) ^ n * â€–Lâ€– ^ k * âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
      âˆ« v, â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– âˆ‚Î¼ := by
  rw [fourierPowSMulRight_iteratedFDeriv_fourierIntegral L hf h'f hk hn]
  apply (norm_fourierIntegral_le_integral_norm _ _ _ _ _).trans
  have I p (hp : p âˆˆ Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1)) :
      Integrable (fun v â†¦ â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€–) Î¼ := by
    simp only [Finset.mem_product, Finset.mem_range_succ_iff] at hp
    exact h'f _ _ (le_trans (by simpa using hp.1) hk) (le_trans (by simpa using hp.2) hn)
  rw [â† integral_finset_sum _ I, â† integral_mul_left]
  apply integral_mono_of_nonneg
  Â· filter_upwards with v using norm_nonneg _
  Â· exact (integrable_finset_sum _ I).const_mul _
  Â· filter_upwards with v
    apply norm_iteratedFDeriv_fourierPowSMulRight _ hf hn _
    intro i hi j hj
    apply Finset.single_le_sum (f := fun p â†¦ â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€–) (a := (j, i))
    Â· intro i _hi
      positivity
    Â· simp only [Finset.mem_product, Finset.mem_range_succ_iff]
      exact âŸ¨hj, hiâŸ©

/-- One can bound the `k`-th derivative of the Fourier integral of `f`, multiplied by `(L v w) ^ n`,
in terms of integrals of iterated derivatives of `f` (of order up to `n`) multiplied by `â€–vâ€– ^ i`
(for `i â‰¤ k`). -/
lemma pow_mul_norm_iteratedFDeriv_fourierIntegral_le [FiniteDimensional â„ V]
    {Î¼ : Measure V} [Measure.IsAddHaarMeasure Î¼] {K N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (k n : â„•), k â‰¤ K â†’ n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^k * â€–iteratedFDeriv â„ n f vâ€–) Î¼)
    {k n : â„•} (hk : k â‰¤ K) (hn : n â‰¤ N) (v : V) (w : W) :
    |L v w| ^ n * â€–(iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) wâ€– â‰¤
      â€–vâ€– ^ n * (2 * Ï€ * â€–Lâ€–) ^ k * (2 * k + 2) ^ n *
        âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
          âˆ« v, â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– âˆ‚Î¼ := calc
  |L v w| ^ n * â€–(iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) wâ€–
  _ â‰¤ (2 * Ï€) ^ n
      * (|L v w| ^ n * â€–iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f) wâ€–) := by
    apply le_mul_of_one_le_left (by positivity)
    apply one_le_pow_of_one_le
    linarith [one_le_pi_div_two]
  _ = â€–fourierPowSMulRight (-L.flip)
        (iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) w n (fun _ â†¦ v)â€– := by
    simp [norm_smul, _root_.abs_of_nonneg pi_nonneg]
  _ â‰¤ â€–fourierPowSMulRight (-L.flip)
        (iteratedFDeriv â„ k (fourierIntegral ğ Î¼ L.toLinearMapâ‚‚ f)) w nâ€– * âˆ _ : Fin n, â€–vâ€– :=
    le_opNorm _ _
  _ â‰¤ ((2 * Ï€) ^ k * (2 * k + 2) ^ n * â€–Lâ€– ^ k *
      âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
        âˆ« v, â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– âˆ‚Î¼) * â€–vâ€– ^ n := by
    gcongr
    Â· apply norm_fourierPowSMulRight_iteratedFDeriv_fourierIntegral_le _ hf h'f hk hn
    Â· simp
  _ = â€–vâ€– ^ n * (2 * Ï€ * â€–Lâ€–) ^ k * (2 * k + 2) ^ n *
        âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
          âˆ« v, â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– âˆ‚Î¼ := by
    simp [mul_pow]
    ring

end VectorFourier

namespace Real
open VectorFourier

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V] {f : V â†’ E}

/-- The FrÃ©chet derivative of the Fourier transform of `f` is the Fourier transform of
    `fun v â†¦ -2 * Ï€ * I âŸªv, â¬âŸ« f v`. -/
theorem hasFDerivAt_fourierIntegral
    (hf_int : Integrable f) (hvf_int : Integrable (fun v â†¦ â€–vâ€– * â€–f vâ€–)) (x : V) :
    HasFDerivAt (ğ“• f) (ğ“• (fourierSMulRight (innerSL â„) f) x) x :=
  VectorFourier.hasFDerivAt_fourierIntegral (innerSL â„) hf_int hvf_int x

/-- The FrÃ©chet derivative of the Fourier transform of `f` is the Fourier transform of
    `fun v â†¦ -2 * Ï€ * I âŸªv, â¬âŸ« f v`. -/
theorem fderiv_fourierIntegral
    (hf_int : Integrable f) (hvf_int : Integrable (fun v â†¦ â€–vâ€– * â€–f vâ€–)) :
    fderiv â„ (ğ“• f) = ğ“• (fourierSMulRight (innerSL â„) f) :=
  VectorFourier.fderiv_fourierIntegral (innerSL â„) hf_int hvf_int

theorem differentiable_fourierIntegral
    (hf_int : Integrable f) (hvf_int : Integrable (fun v â†¦ â€–vâ€– * â€–f vâ€–)) :
    Differentiable â„ (ğ“• f) :=
  VectorFourier.differentiable_fourierIntegral (innerSL â„) hf_int hvf_int

/-- The Fourier integral of the FrÃ©chet derivative of a function is obtained by multiplying the
Fourier integral of the original function by `2Ï€I âŸªv, wâŸ«`. -/
theorem fourierIntegral_fderiv
    (hf : Integrable f) (h'f : Differentiable â„ f) (hf' : Integrable (fderiv â„ f)) :
    ğ“• (fderiv â„ f) = fourierSMulRight (-innerSL â„) (ğ“• f) := by
  rw [â† innerSL_real_flip V]
  exact VectorFourier.fourierIntegral_fderiv (innerSL â„) hf h'f hf'

/-- If `â€–vâ€–^n * â€–f vâ€–` is integrable, then the Fourier transform of `f` is `C^n`. -/
theorem contDiff_fourierIntegral {N : â„•âˆ}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–)) :
    ContDiff â„ N (ğ“• f) :=
  VectorFourier.contDiff_fourierIntegral (innerSL â„) hf

/-- If `â€–vâ€–^n * â€–f vâ€–` is integrable, then the `n`-th derivative of the Fourier transform of `f` is
  the Fourier transform of `fun v â†¦ (-2 * Ï€ * I) ^ n âŸªv, â¬âŸ«^n f v`. -/
theorem iteratedFDeriv_fourierIntegral {N : â„•âˆ}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–))
    (h'f : AEStronglyMeasurable f) {n : â„•} (hn : n â‰¤ N) :
    iteratedFDeriv â„ n (ğ“• f) = ğ“• (fun v â†¦ fourierPowSMulRight (innerSL â„) f v n) :=
  VectorFourier.iteratedFDeriv_fourierIntegral (innerSL â„) hf h'f hn

/-- The Fourier integral of the `n`-th derivative of a function is obtained by multiplying the
Fourier integral of the original function by `(2Ï€I L w â¬ )^n`. -/
theorem fourierIntegral_iteratedFDeriv {N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f)) {n : â„•} (hn : n â‰¤ N) :
    ğ“• (iteratedFDeriv â„ n f)
      = (fun w â†¦ fourierPowSMulRight (-innerSL â„) (ğ“• f) w n) := by
  rw [â† innerSL_real_flip V]
  exact VectorFourier.fourierIntegral_iteratedFDeriv (innerSL â„) hf h'f hn

/-- One can bound `â€–wâ€–^n * â€–D^k (ğ“• f) wâ€–` in terms of integrals of the derivatives of `f` (or order
at most `n`) multiplied by powers of `v` (of order at most `k`). -/
lemma pow_mul_norm_iteratedFDeriv_fourierIntegral_le [FiniteDimensional â„ V]
    {K N : â„•âˆ} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (k n : â„•), k â‰¤ K â†’ n â‰¤ N â†’ Integrable (fun v â†¦ â€–vâ€–^k * â€–iteratedFDeriv â„ n f vâ€–))
    {k n : â„•} (hk : k â‰¤ K) (hn : n â‰¤ N) (w : V) :
    â€–wâ€– ^ n * â€–iteratedFDeriv â„ k (ğ“• f) wâ€– â‰¤ (2 * Ï€) ^ k * (2 * k + 2) ^ n *
      âˆ‘ p in Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
        âˆ« v, â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– := by
  have Z : â€–wâ€– ^ n * (â€–wâ€– ^ n * â€–iteratedFDeriv â„ k (ğ“• f) wâ€–) â‰¤
      â€–wâ€– ^ n * ((2 * (Ï€ * â€–innerSL (E := V) â„â€–)) ^ k * ((2 * k + 2) ^ n *
          âˆ‘ p âˆˆ Finset.range (k + 1) Ã—Ë¢ Finset.range (n + 1),
            âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 f vâ€– âˆ‚volume)) := by
    have := VectorFourier.pow_mul_norm_iteratedFDeriv_fourierIntegral_le (innerSL â„) hf h'f hk hn
      w w
    simp only [innerSL_apply _ w w, real_inner_self_eq_norm_sq w, _root_.abs_pow, abs_norm,
      mul_assoc] at this
    rwa [pow_two, mul_pow, mul_assoc] at this
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp only [pow_zero, one_mul, mul_one, zero_add, Finset.range_one, Finset.product_singleton,
      Finset.sum_map, Function.Embedding.coeFn_mk, norm_iteratedFDeriv_zero, ge_iff_le] at Z âŠ¢
    apply Z.trans
    conv_rhs => rw [â† mul_one Ï€]
    gcongr
    exact norm_innerSL_le _
  rcases eq_or_ne w 0 with rfl | hw
  Â· simp [hn]
    positivity
  rw [mul_le_mul_left (pow_pos (by simp [hw]) n)] at Z
  apply Z.trans
  conv_rhs => rw [â† mul_one Ï€]
  simp only [mul_assoc]
  gcongr
  exact norm_innerSL_le _

lemma hasDerivAt_fourierIntegral
    {f : â„ â†’ E} (hf : Integrable f) (hf' : Integrable (fun x : â„ â†¦ x â€¢ f x)) (w : â„) :
    HasDerivAt (ğ“• f) (ğ“• (fun x : â„ â†¦ (-2 * Ï€ * I * x) â€¢ f x) w) w := by
  have hf'' : Integrable (fun v : â„ â†¦ â€–vâ€– * â€–f vâ€–) := by simpa only [norm_smul] using hf'.norm
  let L := ContinuousLinearMap.mul â„ â„
  have h_int : Integrable fun v â†¦ fourierSMulRight L f v := by
    suffices Integrable fun v â†¦ ContinuousLinearMap.smulRight (L v) (f v) by
      simpa only [fourierSMulRight, neg_smul, neg_mul, Pi.smul_apply] using this.smul (-2 * Ï€ * I)
    convert ((ContinuousLinearMap.ring_lmap_equiv_self â„
      E).symm.toContinuousLinearEquiv.toContinuousLinearMap).integrable_comp hf' using 2 with v
    apply ContinuousLinearMap.ext_ring
    rw [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
      ContinuousLinearMap.map_smul]
    exact congr_arg (fun x â†¦ v â€¢ x) (one_smul â„ (f v)).symm
  rw [â† VectorFourier.fourierIntegral_convergent_iff continuous_fourierChar L.continuousâ‚‚ w]
    at h_int
  convert (VectorFourier.hasFDerivAt_fourierIntegral L hf hf'' w).hasDerivAt using 1
  erw [ContinuousLinearMap.integral_apply h_int]
  simp_rw [ContinuousLinearMap.smul_apply, fourierSMulRight, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, L, ContinuousLinearMap.mul_apply', mul_one,
    â† neg_mul, mul_smul]
  rfl

theorem deriv_fourierIntegral
    {f : â„ â†’ E} (hf : Integrable f) (hf' : Integrable (fun x : â„ â†¦ x â€¢ f x)) :
    deriv (ğ“• f) = ğ“• (fun x : â„ â†¦ (-2 * Ï€ * I * x) â€¢ f x) := by
  ext x
  exact (hasDerivAt_fourierIntegral hf hf' x).deriv

/-- The Fourier integral of the FrÃ©chet derivative of a function is obtained by multiplying the
Fourier integral of the original function by `2Ï€I x`. -/
theorem fourierIntegral_deriv
    {f : â„ â†’ E} (hf : Integrable f) (h'f : Differentiable â„ f) (hf' : Integrable (deriv f)) :
    ğ“• (deriv f) = fun (x : â„) â†¦ (2 * Ï€ * I * x) â€¢ (ğ“• f x) := by
  ext x
  have I : Integrable (fun x â†¦ fderiv â„ f x) := by
    simpa only [â† deriv_fderiv] using (ContinuousLinearMap.smulRightL â„ â„ E 1).integrable_comp hf'
  have : ğ“• (deriv f) x = ğ“• (fderiv â„ f) x 1 := by
    simp only [fourierIntegral_continuousLinearMap_apply I, fderiv_deriv]
  rw [this, fourierIntegral_fderiv hf h'f I]
  simp only [fourierSMulRight_apply, ContinuousLinearMap.neg_apply, innerSL_apply, smul_smul,
    RCLike.inner_apply, conj_trivial, mul_one, neg_smul, smul_neg, neg_neg, neg_mul, â† coe_smul]

theorem iteratedDeriv_fourierIntegral {f : â„ â†’ E} {N : â„•âˆ} {n : â„•}
    (hf : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (fun x â†¦ x^n â€¢ f x)) (hn : n â‰¤ N) :
    iteratedDeriv n (ğ“• f) = ğ“• (fun x : â„ â†¦ (-2 * Ï€ * I * x) ^ n â€¢ f x) := by
  ext x : 1
  have A (n : â„•) (hn : n â‰¤ N) : Integrable (fun v â†¦ â€–vâ€–^n * â€–f vâ€–) := by
    convert (hf n hn).norm with x
    simp [norm_smul]
  have B : AEStronglyMeasurable f := by
    convert (hf 0 (zero_le _)).1 with x
    simp
  rw [iteratedDeriv, iteratedFDeriv_fourierIntegral A B hn,
    fourierIntegral_continuousMultilinearMap_apply (integrable_fourierPowSMulRight _ (A n hn) B),
    fourierIntegral_eq, fourierIntegral_eq]
  congr with y
  suffices (-(2 * Ï€ * I)) ^ n â€¢ y ^ n â€¢ f y = (-(2 * Ï€ * I * y)) ^ n â€¢ f y by
    simpa [innerSL_apply _]
  simp only [â† neg_mul, â† coe_smul, smul_smul, mul_pow, ofReal_pow, mul_assoc]

theorem fourierIntegral_iteratedDeriv {f : â„ â†’ E} {N : â„•âˆ} {n : â„•} (hf : ContDiff â„ N f)
    (h'f : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedDeriv n f)) (hn : n â‰¤ N) :
    ğ“• (iteratedDeriv n f) = fun (x : â„) â†¦ (2 * Ï€ * I * x) ^ n â€¢ (ğ“• f x) := by
  ext x : 1
  have A : âˆ€ (n : â„•), n â‰¤ N â†’ Integrable (iteratedFDeriv â„ n f) := by
    intro n hn
    rw [iteratedFDeriv_eq_equiv_comp]
    exact (LinearIsometryEquiv.integrable_comp_iff _).2 (h'f n hn)
  change ğ“• (fun x â†¦ iteratedDeriv n f x) x = _
  simp_rw [iteratedDeriv, â† fourierIntegral_continuousMultilinearMap_apply (A n hn),
    fourierIntegral_iteratedFDeriv hf A hn]
  simp [â† coe_smul, smul_smul, â† mul_pow]

end Real
