/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
/-!
# Derivative of the Fourier transform

In this file we compute the FrÃ©chet derivative of `ğ“• f`, where `f` is a function such that both
`f` and `v â†¦ â€–vâ€– * â€–f vâ€–` are integrable. Here `ğ“•` is understood as an operator `(V â†’ E) â†’ (W â†’ E)`,
where `V` and `W` are normed `â„`-vector spaces and the Fourier transform is taken with respect to a
continuous `â„`-bilinear pairing `L : V Ã— W â†’ â„`.

We also give a separate lemma for the most common case when `V = W = â„` and `L` is the obvious
multiplication map.
-/

noncomputable section

open Real Complex MeasureTheory Filter TopologicalSpace

open scoped FourierTransform Topology

section SmulRight

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]

lemma aestronglyMeasurable_smulRight {X : Type*} [MeasurableSpace X] {Î¼ : Measure X}
    {A B : Type*} [NormedAddCommGroup A] [NormedSpace ğ•œ A] [NormedAddCommGroup B] [NormedSpace ğ•œ B]
    {f : X â†’ A â†’L[ğ•œ] ğ•œ} {g : X â†’ B}
    (hf : AEStronglyMeasurable f Î¼) (hg : AEStronglyMeasurable g Î¼) :
    AEStronglyMeasurable (fun x : X â†¦ (f x).smulRight (g x)) Î¼ :=
  (ContinuousLinearMap.smulRightL ğ•œ A B).continuousâ‚‚.comp_aestronglyMeasurable (hf.prod_mk hg)

end SmulRight

section bilinear_maps

variable {ğ•œ V W E : Type*} [NormedField ğ•œ] [AddCommMonoid V] [TopologicalSpace V] [Module ğ•œ V]
  [AddCommGroup W] [TopologicalSpace W] [Module ğ•œ W] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Send a continuous bilinear map to an abstract bilinear map (forgetting continuity). -/
def ContinuousLinearMap.toLinearMapâ‚‚ (L : V â†’L[ğ•œ] W â†’L[ğ•œ] E) : V â†’â‚—[ğ•œ] W â†’â‚—[ğ•œ] E :=
  (ContinuousLinearMap.coeLM ğ•œ).comp L.toLinearMap

end bilinear_maps

lemma hasDerivAt_fourierChar (x : â„) :
    HasDerivAt (fun y â†¦ fourierChar [y] : â„ â†’ â„‚)
    (2 * Ï€ * I * fourierChar [x]) x := by
  convert hasDerivAt_fourier 1 1 x using 1
  Â· ext1 y
    rw [fourierChar_apply, fourier_coe_apply]
    congr 1
    push_cast
    ring
  Â· rw [Int.cast_one, ofReal_one, div_one, mul_one, fourierChar_apply, fourier_coe_apply]
    congr 2
    push_cast
    ring

variable {E V W : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

namespace VectorFourier -- special case of VectorFourier theory when e is the standard add char

variable [NormedAddCommGroup V] [NormedSpace â„ V] [NormedAddCommGroup W] [NormedSpace â„ W]

/-- Send a function `f : V â†’ E` to the function `f : V â†’ Hom (W, E)` given by
`v â†’ (w â†’ L(v, w) â€¢ f v)`. -/
def mul_L (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) : (W â†’L[â„] E) := (L v).smulRight (f v)

lemma norm_mul_L (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) : â€–mul_L L f vâ€– = â€–L vâ€– * â€–f vâ€– :=
  ContinuousLinearMap.norm_smulRight_apply _ _

/-- The `w`-derivative of the Fourier transform integrand. -/
lemma HasFDerivAt_fourier_transform_integrand_right
    (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) (w : W) :
    HasFDerivAt (fun w' : W â†¦ fourierChar [-L v w'] â€¢ f v)
    ((-2 * â†‘Ï€ * I) â€¢ (fourierChar [-L v w] â€¢ mul_L L f v)) w := by
  have ha : HasFDerivAt (fun w' : W â†¦ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert ((hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg).smul_const (f v)
  ext1 w'
  clear ha
  simp_rw [mul_L, ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply]
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, â† smul_assoc, smul_comm,
    â† smul_assoc, real_smul, real_smul, smul_eq_mul]
  push_cast
  ring_nf

/-- Norm of the `w`-derivative of the Fourier transform integrand. -/
lemma norm_fderiv_fourier_transform_integrand_right
    (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) (w : W) :
    â€–(-2 * â†‘Ï€ * I) â€¢ (fourierChar [-L v w] â€¢ mul_L L f v)â€– = (2 * Ï€) * â€–L vâ€– * â€–f vâ€– :=
  by rw [norm_smul, norm_smul, norm_eq_abs (fourierChar _ : â„‚), abs_coe_circle, one_mul, norm_mul,
    norm_eq_abs I, abs_I, mul_one, norm_mul, norm_eq_abs ((_ : â„) : â„‚),
    Complex.abs_of_nonneg pi_pos.le, norm_neg,  norm_eq_abs (2 : â„‚), Complex.abs_two, norm_mul_L,
      â† mul_assoc]

lemma norm_fderiv_fourier_transform_integrand_right_le
    (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) (w : W) :
    â€–(-2 * â†‘Ï€ * I) â€¢ (fourierChar [-L v w] â€¢ (mul_L L f v))â€–
      â‰¤ (2 * Ï€) * â€–Lâ€– * â€–vâ€– * â€–f vâ€– := by
  rw [norm_fderiv_fourier_transform_integrand_right]
  refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
  conv_rhs => rw [mul_assoc]
  exact mul_le_mul_of_nonneg_left (L.le_op_norm _) two_pi_pos.le

/-- Main theorem of this section: if both `f` and `Î» x, â€–xâ€– * f x` are integrable, then the
Fourier transform of `f` is has a Frechet derivative (everwhere in its domain) and its derivative is
the Fourier transform of `mul_L L f`. -/
theorem hasFDerivAt_fourier [CompleteSpace E] [MeasurableSpace V] [BorelSpace V] {Î¼ : Measure V}
    [SecondCountableTopologyEither V (W â†’L[â„] â„)] (L : V â†’L[â„] W â†’L[â„] â„)
    {f : V â†’ E} (hf : Integrable f Î¼) (hf' : Integrable (fun v : V â†¦ â€–vâ€– * â€–f vâ€–) Î¼) (w : W) :
    HasFDerivAt (VectorFourier.fourierIntegral fourierChar Î¼ L.toLinearMapâ‚‚ f)
      ((-2 * â†‘Ï€ * I : â„‚) â€¢ (VectorFourier.fourierIntegral fourierChar Î¼ L.toLinearMapâ‚‚
      (mul_L L f) w)) w := by
  let F : W â†’ V â†’ E := fun w' v â†¦ fourierChar [-L v w'] â€¢ f v
  let F' : W â†’ V â†’ (W â†’L[â„] E) :=
    fun w' v â†¦ (-2 * â†‘Ï€ * I) â€¢ (fourierChar [-L v w'] â€¢ mul_L L f v)
  let B : V â†’ â„ := fun v : V â†¦ 2 * Ï€ * â€–Lâ€– * â€–vâ€– * â€–f vâ€–
  have h0 : âˆ€ (w' : W), Integrable (F w') Î¼
  Â· exact fun w' â†¦ (VectorFourier.fourier_integral_convergent_iff continuous_fourierChar
      (by apply L.continuousâ‚‚ : Continuous (fun p : V Ã— W â†¦ L.toLinearMapâ‚‚ p.1 p.2)) w').mp hf
  have h1 : âˆ€á¶  (w' : W) in ğ“ w, AEStronglyMeasurable (F w') Î¼ :=
    eventually_of_forall (fun w' â†¦ (h0 w').aestronglyMeasurable)
  have h2 : Integrable (F w) Î¼ := h0 w
  have h3 : AEStronglyMeasurable (F' w) Î¼
  Â· change AEStronglyMeasurable
      ((-2 * â†‘Ï€ * I : â„‚) â€¢ (fun v â†¦ (fourierChar [-L v w] â€¢ mul_L L f v))) Î¼
    refine (AEStronglyMeasurable.smul ?_ ?_).const_smul _
    Â· refine (continuous_subtype_val.comp (continuous_fourierChar.comp ?_)).aestronglyMeasurable
      exact continuous_ofAdd.comp (L.continuousâ‚‚.comp (Continuous.Prod.mk_left w)).neg
    Â· exact aestronglyMeasurable_smulRight L.continuous.aestronglyMeasurable hf.1
  have h4 : (âˆ€áµ (v : V) âˆ‚Î¼, âˆ€ (w' : W), w' âˆˆ Metric.ball w 1 â†’ â€–F' w' vâ€– â‰¤ B v)
  Â· refine ae_of_all _ (fun v w' _ â†¦ ?_)
    exact norm_fderiv_fourier_transform_integrand_right_le L f v w'
  have h5 : Integrable B Î¼ := by simpa only [â† mul_assoc] using hf'.const_mul (2 * Ï€ * â€–Lâ€–)
  have h6 : âˆ€áµ (v : V) âˆ‚Î¼, âˆ€ (w' : W), w' âˆˆ Metric.ball w 1 â†’
    HasFDerivAt (fun (w'' : W) â†¦ (F w'' v)) (F' w' v) w'
  Â· refine ae_of_all _ (fun v w' _ â†¦ ?_)
    exact HasFDerivAt_fourier_transform_integrand_right L f v w'
  simpa only [integral_smul] using
    hasFDerivAt_integral_of_dominated_of_fderiv_le zero_lt_one h1 h2 h3 h4 h5 h6

end VectorFourier

section inner

variable [NormedAddCommGroup V] [InnerProductSpace â„ V] [MeasurableSpace V]
  [BorelSpace V] [FiniteDimensional â„ V] [CompleteSpace E]

/-- Notation for the Fourier transform on a real inner product space -/
abbrev integralFourier (f : V â†’ E) :=
  VectorFourier.fourierIntegral fourierChar (volume : Measure V) (innerâ‚›â‚— â„) f

/-- The FrÃ©chet derivative of the Fourier transform of `f` is the Fourier transform of
    `fun v â†¦ ((-2 * Ï€ * I) â€¢ f v) âŠ— (innerSL â„ v)`. -/
theorem InnerProductSpace.hasFDerivAt_fourier {f : V â†’ E} (hf_int : Integrable f)
    (hvf_int : Integrable (fun v â†¦ â€–vâ€– * â€–f vâ€–)) (x : V) :
    HasFDerivAt (integralFourier f) (integralFourier (fun v â†¦
      ((ContinuousLinearMap.toSpanSingleton â„ (-(2 * Ï€ * I) â€¢ f v)) âˆ˜L (innerSL â„) v)) x) x := by
  convert VectorFourier.hasFDerivAt_fourier (innerSL â„) hf_int hvf_int x
  simp_rw [integralFourier, VectorFourier.fourierIntegral, â† integral_smul (-2 * â†‘Ï€ * I : â„‚)]
  congr 1 with v w
  let p : â„‚ := â†‘(fourierChar (Multiplicative.ofAdd (-((innerâ‚›â‚— â„) v) x)))
  change (p â€¢ (ContinuousLinearMap.toSpanSingleton â„ (-(2 * â†‘Ï€ * I) â€¢ f v)).comp
    (innerSL â„ v)) w = ((-2 * â†‘Ï€ * I) â€¢ p â€¢ VectorFourier.mul_L (innerSL â„) f v) w
  rw [smul_comm _ p, ContinuousLinearMap.smul_apply, ContinuousLinearMap.comp_apply,
    ContinuousLinearMap.smul_apply]
  congr 1
  rw [ContinuousLinearMap.toSpanSingleton_apply, smul_comm, ContinuousLinearMap.smul_apply,
    neg_mul, neg_mul]
  rfl

end inner

section scalar

open VectorFourier

lemma hasDerivAt_fourierIntegral [CompleteSpace E]
    {f : â„ â†’ E} (hf : Integrable f) (hf' : Integrable (fun x : â„ â†¦ x â€¢ f x)) (w : â„) :
    HasDerivAt (ğ“• f) ((-2 * â†‘Ï€ * I : â„‚) â€¢ ğ“• (fun x : â„ â†¦ x â€¢ f x) w) w := by
  have hf'' : Integrable (fun v : â„ â†¦ â€–vâ€– * â€–f vâ€–) := by simpa only [norm_smul] using hf'.norm
  let L := ContinuousLinearMap.mul â„ â„
  have h_int : Integrable fun v â†¦ mul_L L f v
  Â· convert ((ContinuousLinearMap.ring_lmap_equiv_self â„
      E).symm.toContinuousLinearEquiv.toContinuousLinearMap).integrable_comp hf' with v
    ext1
    rw [mul_L, ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
      ContinuousLinearMap.map_smul]
    exact congr_arg (fun x â†¦ v â€¢ x) (one_smul â„ (f v)).symm
  rw [fourier_integral_convergent_iff continuous_fourierChar L.continuousâ‚‚ w] at h_int
  convert (hasFDerivAt_fourier L hf hf'' w).hasDerivAt
  simp_rw [Real.fourierIntegral, Fourier.fourierIntegral, VectorFourier.fourierIntegral,
    ContinuousLinearMap.smul_apply]
  erw [ContinuousLinearMap.integral_apply h_int]
  simp_rw [ContinuousLinearMap.smul_apply, mul_L, ContinuousLinearMap.smulRight_apply,
    ContinuousLinearMap.mul_apply', mul_one]

end scalar
