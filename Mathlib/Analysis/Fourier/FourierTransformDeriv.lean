/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, David Loeffler, Heather Macbeth
-/
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
/-!
# Derivative of the Fourier transform

In this file we compute the Fr√©chet derivative of `ùìï f`, where `f` is a function such that both
`f` and `v ‚Ü¶ ‚Äñv‚Äñ * ‚Äñf v‚Äñ` are integrable. Here `ùìï` is understood as an operator `(V ‚Üí E) ‚Üí (W ‚Üí E)`,
where `V` and `W` are normed `‚Ñù`-vector spaces and the Fourier transform is taken with respect to a
continuous `‚Ñù`-bilinear pairing `L : V √ó W ‚Üí ‚Ñù`.

We also give a separate lemma for the most common case when `V = W = ‚Ñù` and `L` is the obvious
multiplication map.
-/

noncomputable section

open Real Complex MeasureTheory Filter TopologicalSpace

open scoped FourierTransform Topology

lemma Real.hasDerivAt_fourierChar (x : ‚Ñù) :
    HasDerivAt (fun y : ‚Ñù ‚Ü¶ (fourierChar (Multiplicative.ofAdd y) : ‚ÑÇ))
      (2 * œÄ * I * (fourierChar (Multiplicative.ofAdd x) : ‚ÑÇ)) x := by
  have h1 (y : ‚Ñù) : (fourierChar (Multiplicative.ofAdd y) : ‚ÑÇ) =
      fourier 1 (y : UnitAddCircle) := by
    rw [fourierChar_apply, fourier_coe_apply]
    push_cast
    ring_nf
  simpa only [h1, Int.cast_one, ofReal_one, div_one, mul_one]
    using hasDerivAt_fourier 1 1 x

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E]

namespace VectorFourier

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace ‚Ñù V]
  [NormedAddCommGroup W] [NormedSpace ‚Ñù W] (L : V ‚ÜíL[‚Ñù] W ‚ÜíL[‚Ñù] ‚Ñù) (f : V ‚Üí E)

/-- Send a function `f : V ‚Üí E` to the function `f : V ‚Üí Hom (W, E)` given by
`v ‚Ü¶ (w ‚Ü¶ -2 * œÄ * I * L(v, w) ‚Ä¢ f v)`. -/
def mul_L (v : V) : (W ‚ÜíL[‚Ñù] E) := -(2 * œÄ * I) ‚Ä¢ (L v).smulRight (f v)

/-- The `w`-derivative of the Fourier transform integrand. -/
lemma hasFDerivAt_fourier_transform_integrand_right (v : V) (w : W) :
    HasFDerivAt (fun w' ‚Ü¶ fourierChar [-L v w'] ‚Ä¢ f v) (fourierChar [-L v w] ‚Ä¢ mul_L L f v) w := by
  have ha : HasFDerivAt (fun w' : W ‚Ü¶ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert ((hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg).smul_const (f v)
  ext1 w'
  simp_rw [mul_L, ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply]
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, ‚Üê smul_assoc, smul_comm,
    ‚Üê smul_assoc, real_smul, real_smul, smul_eq_mul]
  push_cast
  ring_nf

/-- Norm of the `w`-derivative of the Fourier transform integrand. -/
lemma norm_fderiv_fourier_transform_integrand_right
    (L : V ‚ÜíL[‚Ñù] W ‚ÜíL[‚Ñù] ‚Ñù) (f : V ‚Üí E) (v : V) (w : W) :
    ‚ÄñfourierChar [-L v w] ‚Ä¢ mul_L L f v‚Äñ = (2 * œÄ) * ‚ÄñL v‚Äñ * ‚Äñf v‚Äñ := by
  rw [norm_smul, norm_eq_abs (fourierChar _ : ‚ÑÇ), abs_coe_circle, one_mul, mul_L, norm_smul,
    norm_neg, norm_mul, norm_mul, norm_eq_abs I, abs_I, mul_one, norm_eq_abs ((_ : ‚Ñù) : ‚ÑÇ),
    Complex.abs_of_nonneg pi_pos.le, norm_eq_abs (2 : ‚ÑÇ), Complex.abs_two,
    ContinuousLinearMap.norm_smulRight_apply, ‚Üê mul_assoc]

lemma norm_fderiv_fourier_transform_integrand_right_le (v : V) (w : W) :
    ‚ÄñfourierChar [-L v w] ‚Ä¢ (mul_L L f v)‚Äñ ‚â§ (2 * œÄ) * ‚ÄñL‚Äñ * ‚Äñv‚Äñ * ‚Äñf v‚Äñ := by
  rw [norm_fderiv_fourier_transform_integrand_right]
  refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
  conv_rhs => rw [mul_assoc]
  exact mul_le_mul_of_nonneg_left (L.le_opNorm _) two_pi_pos.le

variable {f}

/-- Main theorem of this section: if both `f` and `x ‚Ü¶ ‚Äñx‚Äñ * ‚Äñf x‚Äñ` are integrable, then the
Fourier transform of `f` has a Fr√©chet derivative (everywhere in its domain) and its derivative is
the Fourier transform of `mul_L L f`. -/
theorem hasFDerivAt_fourier [CompleteSpace E] [MeasurableSpace V] [BorelSpace V] {Œº : Measure V}
    [SecondCountableTopologyEither V (W ‚ÜíL[‚Ñù] ‚Ñù)]
    (hf : Integrable f Œº) (hf' : Integrable (fun v : V ‚Ü¶ ‚Äñv‚Äñ * ‚Äñf v‚Äñ) Œº) (w : W) :
    HasFDerivAt (VectorFourier.fourierIntegral fourierChar Œº L.toLinearMap‚ÇÇ f)
      (VectorFourier.fourierIntegral fourierChar Œº L.toLinearMap‚ÇÇ (mul_L L f) w) w := by
  let F : W ‚Üí V ‚Üí E := fun w' v ‚Ü¶ fourierChar [-L v w'] ‚Ä¢ f v
  let F' : W ‚Üí V ‚Üí W ‚ÜíL[‚Ñù] E := fun w' v ‚Ü¶ fourierChar [-L v w'] ‚Ä¢ mul_L L f v
  let B : V ‚Üí ‚Ñù := fun v ‚Ü¶ 2 * œÄ * ‚ÄñL‚Äñ * ‚Äñv‚Äñ * ‚Äñf v‚Äñ
  have h0 (w' : W) : Integrable (F w') Œº :=
    (VectorFourier.fourier_integral_convergent_iff continuous_fourierChar
      (by apply L.continuous‚ÇÇ : Continuous (fun p : V √ó W ‚Ü¶ L.toLinearMap‚ÇÇ p.1 p.2)) w').mp hf
  have h1 : ‚àÄ·∂† w' in ùìù w, AEStronglyMeasurable (F w') Œº :=
    eventually_of_forall (fun w' ‚Ü¶ (h0 w').aestronglyMeasurable)
  have h2 : Integrable (F w) Œº := h0 w
  have h3 : AEStronglyMeasurable (F' w) Œº := by
    simp only [F']
    refine AEStronglyMeasurable.smul ?_ ?_
    ¬∑ refine (continuous_subtype_val.comp (continuous_fourierChar.comp ?_)).aestronglyMeasurable
      exact continuous_ofAdd.comp (L.continuous‚ÇÇ.comp (Continuous.Prod.mk_left w)).neg
    ¬∑ apply AEStronglyMeasurable.const_smul'
      have aux0 : Continuous fun p : (W ‚ÜíL[‚Ñù] ‚Ñù) √ó E ‚Ü¶ p.1.smulRight p.2 :=
        (ContinuousLinearMap.smulRightL ‚Ñù W E).continuous‚ÇÇ
      have aux1 : AEStronglyMeasurable (fun v ‚Ü¶ (L v, f v)) Œº :=
        L.continuous.aestronglyMeasurable.prod_mk hf.1
      apply aux0.comp_aestronglyMeasurable aux1
  have h4 : (‚àÄ·µê v ‚àÇŒº, ‚àÄ (w' : W), w' ‚àà Metric.ball w 1 ‚Üí ‚ÄñF' w' v‚Äñ ‚â§ B v) := by
    refine ae_of_all _ (fun v w' _ ‚Ü¶ ?_)
    exact norm_fderiv_fourier_transform_integrand_right_le L f v w'
  have h5 : Integrable B Œº := by simpa only [‚Üê mul_assoc] using hf'.const_mul (2 * œÄ * ‚ÄñL‚Äñ)
  have h6 : ‚àÄ·µê v ‚àÇŒº, ‚àÄ w', w' ‚àà Metric.ball w 1 ‚Üí HasFDerivAt (fun x ‚Ü¶ F x v) (F' w' v) w' :=
    ae_of_all _ (fun v w' _ ‚Ü¶ hasFDerivAt_fourier_transform_integrand_right L f v w')
  exact hasFDerivAt_integral_of_dominated_of_fderiv_le one_pos h1 h2 h3 h4 h5 h6

section inner

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ‚Ñù V] [SecondCountableTopology V]
  [MeasurableSpace V] [BorelSpace V] [CompleteSpace E]

/-- Notation for the Fourier transform on a real inner product space -/
abbrev integralFourier (f : V ‚Üí E) (Œº : Measure V := by volume_tac) :=
  fourierIntegral fourierChar Œº (inner‚Çõ‚Çó ‚Ñù) f

/-- The Fr√©chet derivative of the Fourier transform of `f` is the Fourier transform of
    `fun v ‚Ü¶ ((-2 * œÄ * I) ‚Ä¢ f v) ‚äó (innerSL ‚Ñù v)`. -/
theorem InnerProductSpace.hasFDerivAt_fourier {f : V ‚Üí E} {Œº : Measure V}
    (hf_int : Integrable f Œº) (hvf_int : Integrable (fun v ‚Ü¶ ‚Äñv‚Äñ * ‚Äñf v‚Äñ) Œº) (x : V) :
    HasFDerivAt (integralFourier f Œº) (integralFourier (mul_L (innerSL ‚Ñù) f) Œº x) x := by
  haveI : SecondCountableTopologyEither V (V ‚ÜíL[‚Ñù] ‚Ñù) :=
    secondCountableTopologyEither_of_left V _ -- for some reason it fails to synthesize this?
  exact VectorFourier.hasFDerivAt_fourier (innerSL ‚Ñù) hf_int hvf_int x

end inner

end VectorFourier

open VectorFourier

lemma hasDerivAt_fourierIntegral [CompleteSpace E]
    {f : ‚Ñù ‚Üí E} (hf : Integrable f) (hf' : Integrable (fun x : ‚Ñù ‚Ü¶ x ‚Ä¢ f x)) (w : ‚Ñù) :
    HasDerivAt (ùìï f) (ùìï (fun x : ‚Ñù ‚Ü¶ (-2 * ‚ÜëœÄ * I * x) ‚Ä¢ f x) w) w := by
  have hf'' : Integrable (fun v : ‚Ñù ‚Ü¶ ‚Äñv‚Äñ * ‚Äñf v‚Äñ) := by simpa only [norm_smul] using hf'.norm
  let L := ContinuousLinearMap.mul ‚Ñù ‚Ñù
  have h_int : Integrable fun v ‚Ü¶ mul_L L f v := by
    suffices Integrable fun v ‚Ü¶ ContinuousLinearMap.smulRight (L v) (f v) by
      simpa only [mul_L, neg_smul, neg_mul, Pi.smul_apply] using this.smul (-2 * œÄ * I)
    convert ((ContinuousLinearMap.ring_lmap_equiv_self ‚Ñù
      E).symm.toContinuousLinearEquiv.toContinuousLinearMap).integrable_comp hf' using 2 with v
    apply ContinuousLinearMap.ext_ring
    rw [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
      ContinuousLinearMap.map_smul]
    exact congr_arg (fun x ‚Ü¶ v ‚Ä¢ x) (one_smul ‚Ñù (f v)).symm
  rw [fourier_integral_convergent_iff continuous_fourierChar L.continuous‚ÇÇ w] at h_int
  convert (hasFDerivAt_fourier L hf hf'' w).hasDerivAt using 1
  erw [ContinuousLinearMap.integral_apply h_int]
  simp_rw [ContinuousLinearMap.smul_apply, mul_L, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
    ‚Üê neg_mul, mul_smul]
  rfl
