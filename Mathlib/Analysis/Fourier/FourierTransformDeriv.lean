/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alex Kontorovich, David Loeffler, Heather Macbeth
-/
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.Analysis.Fourier.AddCircle
import Mathlib.Analysis.Fourier.FourierTransform
/-!
# Derivative of the Fourier transform

In this file we compute the FrÃ©chet derivative of `ğ“• f`, where `f` is a function such that both
`f` and `v â†¦ â€–vâ€– * â€–f vâ€–` are integrable. Here `ğ“•` is understood as an operator `(V â†’ E) â†’ (W â†’ E)`,
where `V` and `W` are normed `â„`-vector spaces and the Fourier transform is taken with respect to a
continuous `â„`-bilinear pairing `L : V Ã— W â†’ â„`.

We also give a separate lemma for the most common case when `V = W = â„` and `L` is the obvious
multiplication map.
-/

noncomputable section

open Real Complex MeasureTheory Filter TopologicalSpace

open scoped FourierTransform Topology

lemma Real.hasDerivAt_fourierChar (x : â„) :
    HasDerivAt (fun y : â„ â†¦ (fourierChar (Multiplicative.ofAdd y) : â„‚))
      (2 * Ï€ * I * (fourierChar (Multiplicative.ofAdd x) : â„‚)) x := by
  have h1 (y : â„) : (fourierChar (Multiplicative.ofAdd y) : â„‚) =
    fourier 1 (y : UnitAddCircle)
  Â· rw [fourierChar_apply, fourier_coe_apply]
    push_cast
    ring_nf
  simpa only [h1, Int.cast_one, ofReal_one, div_one, mul_one]
    using hasDerivAt_fourier 1 1 x

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E]

namespace VectorFourier

variable {V W : Type*} [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup W] [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E)

/-- Send a function `f : V â†’ E` to the function `f : V â†’ Hom (W, E)` given by
`v â†’ (w â†’ -2 * Ï€ * I * L(v, w) â€¢ f v)`. -/
def mul_L (v : V) : (W â†’L[â„] E) := -(2 * Ï€ * I) â€¢ (L v).smulRight (f v)

/-- Alternate description of `VectorFourier.mulL`. -/
lemma mulL_eq_toSpanSingleton_comp : mul_L L f =
    fun v â†¦ ((ContinuousLinearMap.toSpanSingleton â„ (-(2 * Ï€ * I) â€¢ f v)) âˆ˜L L v) := by
  ext v v'
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.toSpanSingleton_apply, smul_comm,
    VectorFourier.mul_L, ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply]

/-- The `w`-derivative of the Fourier transform integrand. -/
lemma hasFDerivAt_fourier_transform_integrand_right (v : V) (w : W) :
    HasFDerivAt (fun w' â†¦ fourierChar [-L v w'] â€¢ f v) (fourierChar [-L v w] â€¢ mul_L L f v) w := by
  have ha : HasFDerivAt (fun w' : W â†¦ L v w') (L v) w := ContinuousLinearMap.hasFDerivAt (L v)
  convert ((hasDerivAt_fourierChar (-L v w)).hasFDerivAt.comp w ha.neg).smul_const (f v)
  ext1 w'
  simp_rw [mul_L, ContinuousLinearMap.smul_apply, ContinuousLinearMap.smulRight_apply]
  rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.neg_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.one_apply, â† smul_assoc, smul_comm,
    â† smul_assoc, real_smul, real_smul, smul_eq_mul]
  push_cast
  ring_nf

/-- Norm of the `w`-derivative of the Fourier transform integrand. -/
lemma norm_fderiv_fourier_transform_integrand_right
    (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E) (v : V) (w : W) :
    â€–fourierChar [-L v w] â€¢ mul_L L f vâ€– = (2 * Ï€) * â€–L vâ€– * â€–f vâ€– :=
  by rw [norm_smul, norm_eq_abs (fourierChar _ : â„‚), abs_coe_circle, one_mul, mul_L, norm_smul,
    norm_neg, norm_mul, norm_mul, norm_eq_abs I, abs_I, mul_one, norm_eq_abs ((_ : â„) : â„‚),
    Complex.abs_of_nonneg pi_pos.le, norm_eq_abs (2 : â„‚), Complex.abs_two,
    ContinuousLinearMap.norm_smulRight_apply, â† mul_assoc]

lemma norm_fderiv_fourier_transform_integrand_right_le (v : V) (w : W) :
    â€–fourierChar [-L v w] â€¢ (mul_L L f v)â€– â‰¤ (2 * Ï€) * â€–Lâ€– * â€–vâ€– * â€–f vâ€– := by
  rw [norm_fderiv_fourier_transform_integrand_right]
  refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
  conv_rhs => rw [mul_assoc]
  exact mul_le_mul_of_nonneg_left (L.le_op_norm _) two_pi_pos.le

variable {f}

/-- Main theorem of this section: if both `f` and `x â†¦ â€–xâ€– * â€–f xâ€–` are integrable, then the
Fourier transform of `f` has a FrÃ©chet derivative (everywhere in its domain) and its derivative is
the Fourier transform of `mul_L L f`. -/
theorem hasFDerivAt_fourier [CompleteSpace E] [MeasurableSpace V] [BorelSpace V] {Î¼ : Measure V}
    [SecondCountableTopologyEither V (W â†’L[â„] â„)]
    (hf : Integrable f Î¼) (hf' : Integrable (fun v : V â†¦ â€–vâ€– * â€–f vâ€–) Î¼) (w : W) :
    HasFDerivAt (VectorFourier.fourierIntegral fourierChar Î¼ L.toLinearMapâ‚‚ f)
      (VectorFourier.fourierIntegral fourierChar Î¼ L.toLinearMapâ‚‚ (mul_L L f) w) w := by
  let F : W â†’ V â†’ E := fun w' v â†¦ fourierChar [-L v w'] â€¢ f v
  let F' : W â†’ V â†’ W â†’L[â„] E := fun w' v â†¦ fourierChar [-L v w'] â€¢ mul_L L f v
  let B : V â†’ â„ := fun v â†¦ 2 * Ï€ * â€–Lâ€– * â€–vâ€– * â€–f vâ€–
  have h0 (w' : W) : Integrable (F w') Î¼ :=
    (VectorFourier.fourier_integral_convergent_iff continuous_fourierChar
      (by apply L.continuousâ‚‚ : Continuous (fun p : V Ã— W â†¦ L.toLinearMapâ‚‚ p.1 p.2)) w').mp hf
  have h1 : âˆ€á¶  w' in ğ“ w, AEStronglyMeasurable (F w') Î¼ :=
    eventually_of_forall (fun w' â†¦ (h0 w').aestronglyMeasurable)
  have h2 : Integrable (F w) Î¼ := h0 w
  have h3 : AEStronglyMeasurable (F' w) Î¼
  Â· change AEStronglyMeasurable (fun v â†¦ fourierChar [-L v w] â€¢ mul_L L f v) Î¼
    refine AEStronglyMeasurable.smul ?_ ?_
    Â· refine (continuous_subtype_val.comp (continuous_fourierChar.comp ?_)).aestronglyMeasurable
      exact continuous_ofAdd.comp (L.continuousâ‚‚.comp (Continuous.Prod.mk_left w)).neg
    Â· apply AEStronglyMeasurable.const_smul'
      have aux0 : Continuous fun p : (W â†’L[â„] â„) Ã— E â†¦ p.1.smulRight p.2 :=
        (ContinuousLinearMap.smulRightL â„ W E).continuousâ‚‚
      have aux1 : AEStronglyMeasurable (fun v â†¦ (L v, f v)) Î¼ :=
        L.continuous.aestronglyMeasurable.prod_mk hf.1
      apply aux0.comp_aestronglyMeasurable aux1
  have h4 : (âˆ€áµ v âˆ‚Î¼, âˆ€ (w' : W), w' âˆˆ Metric.ball w 1 â†’ â€–F' w' vâ€– â‰¤ B v)
  Â· refine ae_of_all _ (fun v w' _ â†¦ ?_)
    exact norm_fderiv_fourier_transform_integrand_right_le L f v w'
  have h5 : Integrable B Î¼ := by simpa only [â† mul_assoc] using hf'.const_mul (2 * Ï€ * â€–Lâ€–)
  have h6 : âˆ€áµ v âˆ‚Î¼, âˆ€ w', w' âˆˆ Metric.ball w 1 â†’ HasFDerivAt (fun x â†¦ F x v) (F' w' v) w' :=
    ae_of_all _ (fun v w' _ â†¦ hasFDerivAt_fourier_transform_integrand_right L f v w')
  exact hasFDerivAt_integral_of_dominated_of_fderiv_le one_pos h1 h2 h3 h4 h5 h6

section inner

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [SecondCountableTopology V]
  [MeasurableSpace V] [BorelSpace V] [CompleteSpace E]

/-- Notation for the Fourier transform on a real inner product space -/
abbrev integralFourier (f : V â†’ E) (Î¼ : Measure V := by volume_tac) :=
  fourierIntegral fourierChar Î¼ (innerâ‚›â‚— â„) f

/-- The FrÃ©chet derivative of the Fourier transform of `f` is the Fourier transform of
    `fun v â†¦ ((-2 * Ï€ * I) â€¢ f v) âŠ— (innerSL â„ v)`. -/
theorem InnerProductSpace.hasFDerivAt_fourier {f : V â†’ E} {Î¼ : Measure V}
    (hf_int : Integrable f Î¼) (hvf_int : Integrable (fun v â†¦ â€–vâ€– * â€–f vâ€–) Î¼) (x : V) :
    HasFDerivAt (integralFourier f Î¼) (integralFourier (fun v â†¦
      ((ContinuousLinearMap.toSpanSingleton â„ (-(2 * Ï€ * I) â€¢ f v)) âˆ˜L (innerSL â„) v)) Î¼ x) x := by
  haveI : SecondCountableTopologyEither V (V â†’L[â„] â„) :=
    secondCountableTopologyEither_of_left V _ -- for some reason it fails to synthesize this?
  simpa only [VectorFourier.mulL_eq_toSpanSingleton_comp]
    using VectorFourier.hasFDerivAt_fourier (innerSL â„) hf_int hvf_int x

end inner

end VectorFourier

open VectorFourier

lemma hasDerivAt_fourierIntegral [CompleteSpace E]
    {f : â„ â†’ E} (hf : Integrable f) (hf' : Integrable (fun x : â„ â†¦ x â€¢ f x)) (w : â„) :
    HasDerivAt (ğ“• f) (ğ“• (fun x : â„ â†¦ (-2 * â†‘Ï€ * I * x) â€¢ f x) w) w := by
  have hf'' : Integrable (fun v : â„ â†¦ â€–vâ€– * â€–f vâ€–) := by simpa only [norm_smul] using hf'.norm
  let L := ContinuousLinearMap.mul â„ â„
  have h_int : Integrable fun v â†¦ mul_L L f v
  Â· suffices : Integrable fun v â†¦ ContinuousLinearMap.smulRight (L v) (f v)
    Â· simpa only [mul_L, neg_smul, neg_mul, Pi.smul_apply] using this.smul (-2 * Ï€ * I)
    convert ((ContinuousLinearMap.ring_lmap_equiv_self â„
      E).symm.toContinuousLinearEquiv.toContinuousLinearMap).integrable_comp hf' using 2 with v
    apply ContinuousLinearMap.ext_ring
    rw [ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
      ContinuousLinearMap.map_smul]
    exact congr_arg (fun x â†¦ v â€¢ x) (one_smul â„ (f v)).symm
  rw [fourier_integral_convergent_iff continuous_fourierChar L.continuousâ‚‚ w] at h_int
  convert (hasFDerivAt_fourier L hf hf'' w).hasDerivAt using 1
  erw [ContinuousLinearMap.integral_apply h_int]
  simp_rw [ContinuousLinearMap.smul_apply, mul_L, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.smulRight_apply, ContinuousLinearMap.mul_apply', mul_one,
    â† neg_mul, mul_smul]
  rfl
