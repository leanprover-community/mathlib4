/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.MeasureTheory.Integral.PeakFunction
import Mathlib.Analysis.SpecialFunctions.Gaussian.FourierTransform

/-!
# Fourier inversion formula

In a finite-dimensional real inner product space, we show the Fourier inversion formula, i.e.,
`ğ“•â» (ğ“• f) v = f v` if `f` and `ğ“• f` are integrable, and `f` is continuous at `v`. This is proved
in `MeasureTheory.Integrable.fourier_inversion`. See also `Continuous.fourier_inversion`
giving `ğ“•â» (ğ“• f) = f` under an additional continuity assumption for `f`.

We use the following proof. A naÃ¯ve computation gives
`ğ“•â» (ğ“• f) v
= âˆ«_w exp (2 I Ï€ âŸªw, vâŸ«) ğ“• f (w) dw
= âˆ«_w exp (2 I Ï€ âŸªw, vâŸ«) âˆ«_x, exp (-2 I Ï€ âŸªw, xâŸ«) f x dx) dw
= âˆ«_x (âˆ«_ w, exp (2 I Ï€ âŸªw, v - xâŸ« dw) f x dx `

However, the Fubini step does not make sense for lack of integrability, and the middle integral
`âˆ«_ w, exp (2 I Ï€ âŸªw, v - xâŸ« dw` (which one would like to be a Dirac at `v - x`) is not defined.
To gain integrability, one multiplies with a Gaussian function `exp (-câ»Â¹ â€–wâ€–^2)`, with a large
(but finite) `c`. As this function converges pointwise to `1` when `c â†’ âˆ`, we get
`âˆ«_w exp (2 I Ï€ âŸªw, vâŸ«) ğ“• f (w) dw = lim_c âˆ«_w exp (-câ»Â¹ â€–wâ€–^2 + 2 I Ï€ âŸªw, vâŸ«) ğ“• f (w) dw`.
One can perform Fubini on the right hand side for fixed `c`, writing the integral as
`âˆ«_x (âˆ«_w exp (-câ»Â¹â€–wâ€–^2 + 2 I Ï€ âŸªw, v - xâŸ« dw)) f x dx`.
The middle factor is the Fourier transform of a more and more flat function
(converging to the constant `1`), hence it becomes more and more concentrated, around the
point `v`. (Morally, it converges to the Dirac at `v`). Moreover, it has integral one.
Therefore, multiplying by `f` and integrating, one gets a term converging to `f v` as `c â†’ âˆ`.
Since it also converges to `ğ“•â» (ğ“• f) v`, this proves the result.

To check the concentration property of the middle factor and the fact that it has integral one, we
rely on the explicit computation of the Fourier transform of Gaussians.
-/

open Filter MeasureTheory Complex FiniteDimensional Metric Real Bornology

open scoped Topology FourierTransform RealInnerProductSpace Complex

variable {V E : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V]
  [MeasurableSpace V] [BorelSpace V] [FiniteDimensional â„ V]
  [NormedAddCommGroup E] [NormedSpace â„‚ E] {f : V â†’ E}

namespace Real

lemma tendsto_integral_cexp_sq_smul (hf : Integrable f) :
    Tendsto (fun (c : â„) â†¦ (âˆ« v : V, cexp (- câ»Â¹ * â€–vâ€–^2) â€¢ f v))
      atTop (ğ“ (âˆ« v : V, f v)) := by
  apply tendsto_integral_filter_of_dominated_convergence _ _ _ hf.norm
  Â· filter_upwards with v
    nth_rewrite 2 [show f v = cexp (- (0 : â„) * â€–vâ€–^2) â€¢ f v by simp]
    apply (Tendsto.cexp _).smul_const
    exact tendsto_inv_atTop_zero.ofReal.neg.mul_const _
  Â· filter_upwards with c using
      AEStronglyMeasurable.smul (Continuous.aestronglyMeasurable (by continuity)) hf.1
  Â· filter_upwards [Ici_mem_atTop (0 : â„)] with c (hc : 0 â‰¤ c)
    filter_upwards with v
    simp only [ofReal_inv, neg_mul, norm_smul, Complex.norm_eq_abs]
    norm_cast
    conv_rhs => rw [â† one_mul (â€–f vâ€–)]
    gcongr
    simp only [abs_exp, exp_le_one_iff, Left.neg_nonpos_iff]
    positivity

variable [CompleteSpace E]

lemma tendsto_integral_gaussian_smul (hf : Integrable f) (h'f : Integrable (ğ“• f)) (v : V) :
    Tendsto (fun (c : â„) â†¦
      âˆ« w : V, ((Ï€ * c) ^ (finrank â„ V / 2 : â„‚) * cexp (-Ï€ ^ 2 * c * â€–v - wâ€– ^ 2)) â€¢ f w)
    atTop (ğ“ (ğ“•â» (ğ“• f) v)) := by
  have A : Tendsto (fun (c : â„) â†¦ (âˆ« w : V, cexp (- câ»Â¹ * â€–wâ€–^2 + 2 * Ï€ * I * âŸªv, wâŸ«)
       â€¢ (ğ“• f) w)) atTop (ğ“ (ğ“•â» (ğ“• f) v)) := by
    have : Integrable (fun w â†¦ ğ âŸªw, vâŸ« â€¢ (ğ“• f) w) := by
      have B : Continuous fun p : V Ã— V => (- innerâ‚— V) p.1 p.2 := continuous_inner.neg
      simpa using
        (VectorFourier.fourierIntegral_convergent_iff Real.continuous_fourierChar B v).2 h'f
    convert tendsto_integral_cexp_sq_smul this using 4 with c w
    Â· rw [Submonoid.smul_def, Real.fourierChar_apply, smul_smul, â† Complex.exp_add, real_inner_comm]
      congr 3
      simp only [ofReal_mul, ofReal_ofNat]
      ring
    Â· simp [fourierIntegralInv_eq]
  have B : Tendsto (fun (c : â„) â†¦ (âˆ« w : V,
        ğ“• (fun w â†¦ cexp (- câ»Â¹ * â€–wâ€–^2 + 2 * Ï€ * I * âŸªv, wâŸ«)) w â€¢ f w)) atTop
      (ğ“ (ğ“•â» (ğ“• f) v)) := by
    apply A.congr'
    filter_upwards [Ioi_mem_atTop 0] with c (hc : 0 < c)
    have J : Integrable (fun w â†¦ cexp (- câ»Â¹ * â€–wâ€–^2 + 2 * Ï€ * I * âŸªv, wâŸ«)) :=
      GaussianFourier.integrable_cexp_neg_mul_sq_norm_add (by simpa) _ _
    simpa using (VectorFourier.integral_fourierIntegral_smul_eq_flip (L := innerâ‚— V)
      Real.continuous_fourierChar continuous_inner J hf).symm
  apply B.congr'
  filter_upwards [Ioi_mem_atTop 0] with c (hc : 0 < c)
  congr with w
  rw [fourierIntegral_gaussian_innerProductSpace' (by simpa)]
  congr
  Â· simp
  Â· simp; ring

lemma tendsto_integral_gaussian_smul' (hf : Integrable f) {v : V} (h'f : ContinuousAt f v) :
    Tendsto (fun (c : â„) â†¦
      âˆ« w : V, ((Ï€ * c : â„‚) ^ (finrank â„ V / 2 : â„‚) * cexp (-Ï€ ^ 2 * c * â€–v - wâ€– ^ 2)) â€¢ f w)
    atTop (ğ“ (f v)) := by
  let Ï† : V â†’ â„ := fun w â†¦ Ï€ ^ (finrank â„ V / 2 : â„) * Real.exp (-Ï€^2 * â€–wâ€–^2)
  have A : Tendsto (fun (c : â„) â†¦ âˆ« w : V, (c ^ finrank â„ V * Ï† (c â€¢ (v - w))) â€¢ f w)
      atTop (ğ“ (f v)) := by
    apply tendsto_integral_comp_smul_smul_of_integrable'
    Â· exact fun x â†¦ by positivity
    Â· rw [integral_mul_left, GaussianFourier.integral_rexp_neg_mul_sq_norm (by positivity)]
      nth_rewrite 2 [â† pow_one Ï€]
      rw [â† rpow_natCast, â† rpow_natCast, â† rpow_sub pi_pos, â† rpow_mul pi_nonneg,
        â† rpow_add pi_pos]
      ring_nf
      exact rpow_zero _
    Â· have A : Tendsto (fun (w : V) â†¦ Ï€^2 * â€–wâ€–^2) (cobounded V) atTop := by
        rw [tendsto_const_mul_atTop_of_pos (by positivity)]
        apply (tendsto_pow_atTop two_ne_zero).comp tendsto_norm_cobounded_atTop
      have B := tendsto_rpow_mul_exp_neg_mul_atTop_nhds_zero (finrank â„ V / 2) 1
        zero_lt_one |>.comp A |>.const_mul (Ï€ ^ (-finrank â„ V / 2 : â„))
      rw [mul_zero] at B
      convert B using 2 with x
      simp only [neg_mul, one_mul, Function.comp_apply, â† mul_assoc, â† rpow_natCast, Ï†]
      congr 1
      rw [mul_rpow (by positivity) (by positivity), â† rpow_mul pi_nonneg,
        â† rpow_mul (norm_nonneg _), â† mul_assoc, â† rpow_add pi_pos, mul_comm]
      congr <;> ring
    Â· exact hf
    Â· exact h'f
  have B : Tendsto
      (fun (c : â„) â†¦ âˆ« w : V, ((c^(1/2:â„)) ^ finrank â„ V * Ï† ((c^(1/2:â„)) â€¢ (v - w))) â€¢ f w)
      atTop (ğ“ (f v)) :=
    A.comp (tendsto_rpow_atTop (by norm_num))
  apply B.congr'
  filter_upwards [Ioi_mem_atTop 0] with c (hc : 0 < c)
  congr with w
  rw [â† coe_smul]
  congr
  rw [ofReal_mul, ofReal_mul, ofReal_exp, â† mul_assoc]
  congr
  Â· rw [mul_cpow_ofReal_nonneg pi_nonneg hc.le, â† rpow_natCast, â† rpow_mul hc.le, mul_comm,
      ofReal_cpow pi_nonneg, ofReal_cpow hc.le]
    simp [div_eq_inv_mul]
  Â· norm_cast
    simp only [one_div, norm_smul, Real.norm_eq_abs, mul_pow, _root_.sq_abs, neg_mul, neg_inj,
      â† rpow_natCast, â† rpow_mul hc.le, mul_assoc]
    norm_num

end Real

variable [CompleteSpace E]

/-- **Fourier inversion formula**: If a function `f` on a finite-dimensional real inner product
space is integrable, and its Fourier transform `ğ“• f` is also integrable, then `ğ“•â» (ğ“• f) = f` at
continuity points of `f`. -/
theorem MeasureTheory.Integrable.fourier_inversion
    (hf : Integrable f) (h'f : Integrable (ğ“• f)) {v : V}
    (hv : ContinuousAt f v) : ğ“•â» (ğ“• f) v = f v :=
  tendsto_nhds_unique (Real.tendsto_integral_gaussian_smul hf h'f v)
    (Real.tendsto_integral_gaussian_smul' hf hv)

/-- **Fourier inversion formula**: If a function `f` on a finite-dimensional real inner product
space is continuous, integrable, and its Fourier transform `ğ“• f` is also integrable,
then `ğ“•â» (ğ“• f) = f`. -/
theorem Continuous.fourier_inversion (h : Continuous f)
    (hf : Integrable f) (h'f : Integrable (ğ“• f)) :
    ğ“•â» (ğ“• f) = f := by
  ext v
  exact hf.fourier_inversion h'f h.continuousAt

/-- **Fourier inversion formula**: If a function `f` on a finite-dimensional real inner product
space is integrable, and its Fourier transform `ğ“• f` is also integrable, then `ğ“• (ğ“•â» f) = f` at
continuity points of `f`. -/
theorem MeasureTheory.Integrable.fourier_inversion_inv
    (hf : Integrable f) (h'f : Integrable (ğ“• f)) {v : V}
    (hv : ContinuousAt f v) : ğ“• (ğ“•â» f) v = f v := by
  rw [fourierIntegralInv_comm]
  exact fourier_inversion hf h'f hv

/-- **Fourier inversion formula**: If a function `f` on a finite-dimensional real inner product
space is continuous, integrable, and its Fourier transform `ğ“• f` is also integrable,
then `ğ“• (ğ“•â» f) = f`. -/
theorem Continuous.fourier_inversion_inv (h : Continuous f)
    (hf : Integrable f) (h'f : Integrable (ğ“• f)) :
    ğ“• (ğ“•â» f) = f := by
  ext v
  exact hf.fourier_inversion_inv h'f h.continuousAt
