/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Convolution
public import Mathlib.Analysis.Distribution.SchwartzSpace.Fourier

/-! # The Fourier transform of the convolution

In this file we calculate the Fourier transform of a convolution.

## Main definitions
* `SchwartzMap.convolution`: The convolution on Schwartz functions is defined via the Fourier
  transform.

## Main statements
* `Real.fourier_bilin_convolution_eq`: The Fourier transform of a convolution is the bilinear map
  applied to the Fourier transform of the functions.
* `Real.fourier_smul_convolution_eq`: Variant for scalar multiplication.
* `Real.fourier_mul_convolution_eq`: Variant for multiplication.
* `SchwartzMap.fourier_convolution`: The Fourier transform of the Schwartz convolution is given by
  the pairing of the Fourier transformed Schwartz functions.
* `SchwartzMap.convolution_apply`: The Schwartz function convolution coincides with the convolution
  for functions.

-/

@[expose] public section

variable {ğ•œ R E F Fâ‚ Fâ‚‚ Fâ‚ƒ : Type*}

namespace Real

open MeasureTheory Convolution

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
  [NormedAddCommGroup Fâ‚] [NormedAddCommGroup Fâ‚‚] [NormedAddCommGroup Fâ‚ƒ]
  [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]
  [NormedSpace ğ•œ Fâ‚] [NormedSpace ğ•œ Fâ‚‚] [NormedSpace ğ•œ Fâ‚ƒ]

/- The norm of the integrant of the convolution is integrable if the functions are integrable
and continuous. -/
theorem integrable_prod_sub (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) {fâ‚ : E â†’ Fâ‚} {fâ‚‚ : E â†’ Fâ‚‚}
    (hfâ‚ : Integrable fâ‚) (hfâ‚‚ : Integrable fâ‚‚) (hfâ‚' : Continuous fâ‚) (hfâ‚‚' : Continuous fâ‚‚) :
    Integrable (fun (p : E Ã— E) â†¦ â€–Bâ€– * (â€–fâ‚ (p.1 - p.2)â€– * â€–fâ‚‚ p.2â€–)) (volume.prod volume) := by
  apply Integrable.const_mul
  rw [integrable_prod_iff' (by measurability)]
  constructor
  Â· filter_upwards with x
    exact (hfâ‚.comp_sub_right x).norm.mul_const _
  have : Integrable (fun x â†¦ ((âˆ« y, â€–fâ‚ yâ€–) * â€–fâ‚‚ xâ€–)) := by
    apply hfâ‚‚.norm.bdd_mul (by measurability) (c := â€–(âˆ« y, â€–fâ‚ yâ€–)â€–)
    filter_upwards with; rfl
  convert this using 1
  ext x
  simp_rw [norm_mul, norm_norm]
  rw [integral_mul_const]
  congr 1
  convert integral_sub_right_eq_self _ x (Î¼ := volume)
  rfl

open FourierTransform

variable [NormedSpace â„‚ Fâ‚ƒ]

/-- Calculate the Fourier transform of the convolution as a symmetric integral. -/
theorem fourier_bilin_convolution_eq_integral (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) {fâ‚ : E â†’ Fâ‚} {fâ‚‚ : E â†’ Fâ‚‚}
    (hfâ‚ : Integrable fâ‚) (hfâ‚‚ : Integrable fâ‚‚) (hfâ‚' : Continuous fâ‚) (hfâ‚‚' : Continuous fâ‚‚)
    (Î¾ : E) :
    ğ“• (fâ‚ â‹†[B] fâ‚‚) Î¾ = âˆ« y, âˆ« x, ğ (-inner â„ (y + x) Î¾) â€¢ B (fâ‚ x) (fâ‚‚ y) := calc
  _ = ğ“• (fâ‚‚ â‹†[B.flip] fâ‚) Î¾ := by
    rw [convolution_flip]
  _ = âˆ« x, ğ (-inner â„ x Î¾) â€¢ âˆ« y, B (fâ‚ (x - y)) (fâ‚‚ y) := by rfl
  _ = âˆ« x, âˆ« y, ğ (-inner â„ x Î¾) â€¢ B (fâ‚ (x - y)) (fâ‚‚ y) := by
    congr
    ext x
    simp_rw [Circle.smul_def, integral_smul]
  _ = âˆ« y, âˆ« x, ğ (-inner â„ x Î¾) â€¢ B (fâ‚ (x - y)) (fâ‚‚ y) := by
    refine integral_integral_swap ?_
    apply (integrable_prod_sub B hfâ‚ hfâ‚‚ hfâ‚' hfâ‚‚').mono (by measurability)
    filter_upwards with âŸ¨y, xâŸ©
    have : â€–(B (fâ‚ (y - x))) (fâ‚‚ x)â€– â‰¤ â€–Bâ€– * (â€–fâ‚ (y - x)â€– * â€–fâ‚‚ xâ€–) := by
      grw [B.le_opNormâ‚‚ (fâ‚ (y - x)) (fâ‚‚ x), mul_assoc]
    simpa
  _ = âˆ« y, âˆ« x, ğ (-inner â„ (y + x) Î¾) â€¢ B (fâ‚ x) (fâ‚‚ y) := by
    congr
    ext y
    -- Linear change of variables
    convert integral_sub_right_eq_self _ y (Î¼ := volume)
    congr
    simp

variable [CompleteSpace Fâ‚] [CompleteSpace Fâ‚‚] [CompleteSpace Fâ‚ƒ]
  [NormedSpace â„‚ Fâ‚] [NormedSpace â„‚ Fâ‚‚]

open ContinuousLinearMap

/-- The Fourier transform of the convolution is given by the bilinear map applied to the Fourier
transform of the individual functions. -/
theorem fourier_bilin_convolution_eq (B : Fâ‚ â†’L[â„‚] Fâ‚‚ â†’L[â„‚] Fâ‚ƒ) {fâ‚ : E â†’ Fâ‚} {fâ‚‚ : E â†’ Fâ‚‚}
    (hfâ‚ : Integrable fâ‚) (hfâ‚‚ : Integrable fâ‚‚) (hfâ‚' : Continuous fâ‚) (hfâ‚‚' : Continuous fâ‚‚)
    (Î¾ : E) :
    ğ“• (fâ‚ â‹†[B] fâ‚‚) Î¾ = B (ğ“• fâ‚ Î¾) (ğ“• fâ‚‚ Î¾) := calc
  _ = âˆ« y, âˆ« x, ğ (-inner â„ (y + x) Î¾) â€¢ B (fâ‚ x) (fâ‚‚ y) :=
    fourier_bilin_convolution_eq_integral B hfâ‚ hfâ‚‚ hfâ‚' hfâ‚‚' _
  _ = âˆ« y, âˆ« x, ğ (-inner â„ y Î¾) â€¢ ğ (-inner â„ x Î¾) â€¢ B (fâ‚ x) (fâ‚‚ y) := by
    congr
    ext y
    congr
    ext x
    rw [smul_smul, â† AddChar.map_add_eq_mul, inner_add_left]
    congr
    grind
  _ = âˆ« y, (âˆ« x, B (ğ (-inner â„ x Î¾) â€¢ fâ‚ x)) (ğ (-inner â„ y Î¾) â€¢ fâ‚‚ y) := by
    congr
    ext y
    simp_rw [Circle.smul_def, map_smul, MeasureTheory.integral_smul]
    congr
    rw [integral_apply ?_ (fâ‚‚ y)]
    Â· simp
    have : MeasureTheory.Integrable (fun x â†¦ â€–Bâ€– * â€–fâ‚ xâ€–) MeasureTheory.volume :=
      hfâ‚.norm.const_mul _
    apply this.mono (by measurability)
    filter_upwards with x
    simpa [â† Circle.smul_def] using le_opNorm B (fâ‚ x)
  _ = B (âˆ« x, ğ (-inner â„ x Î¾) â€¢ fâ‚ x) (âˆ« y, ğ (-inner â„ y Î¾) â€¢ fâ‚‚ y) := by
    rw [â† integral_comp_comm _ (by simpa using hfâ‚‚), â† integral_comp_comm _ (by simpa using hfâ‚)]

/-- The Fourier transform of the convolution is given by the multiplication of the Fourier transform
of the individual functions.

Version for scalar multiplication. -/
theorem fourier_smul_convolution_eq {fâ‚ : E â†’ â„‚} {fâ‚‚ : E â†’ Fâ‚}
    (hfâ‚ : Integrable fâ‚) (hfâ‚‚ : Integrable fâ‚‚) (hfâ‚' : Continuous fâ‚) (hfâ‚‚' : Continuous fâ‚‚)
    (Î¾ : E) :
    ğ“• (fâ‚ â‹†[lsmul â„‚ â„‚] fâ‚‚) Î¾ = (ğ“• fâ‚ Î¾) â€¢ (ğ“• fâ‚‚ Î¾) :=
  fourier_bilin_convolution_eq (lsmul â„‚ â„‚) hfâ‚ hfâ‚‚ hfâ‚' hfâ‚‚' Î¾

variable [NormedRing R] [NormedSpace â„‚ R] [IsScalarTower â„‚ R R] [SMulCommClass â„‚ R R]
  [CompleteSpace R]

/-- The Fourier transform of the convolution is given by the multiplication of the Fourier transform
of the individual functions.

Version for multiplication. -/
theorem fourier_mul_convolution_eq {fâ‚ : E â†’ R} {fâ‚‚ : E â†’ R}
    (hfâ‚ : Integrable fâ‚) (hfâ‚‚ : Integrable fâ‚‚) (hfâ‚' : Continuous fâ‚) (hfâ‚‚' : Continuous fâ‚‚)
    (Î¾ : E) :
    ğ“• (fâ‚ â‹†[mul â„‚ R] fâ‚‚) Î¾ = (ğ“• fâ‚ Î¾) * (ğ“• fâ‚‚ Î¾) :=
  fourier_bilin_convolution_eq (mul â„‚ R) hfâ‚ hfâ‚‚ hfâ‚' hfâ‚‚' Î¾

end Real

namespace SchwartzMap

variable [RCLike ğ•œ]
  [NormedAddCommGroup E] [InnerProductSpace â„ E] [FiniteDimensional â„ E] [MeasurableSpace E]
  [BorelSpace E]
  [NormedAddCommGroup Fâ‚] [NormedSpace â„‚ Fâ‚] [NormedSpace ğ•œ Fâ‚] [SMulCommClass â„‚ ğ•œ Fâ‚]
  [NormedAddCommGroup Fâ‚‚] [NormedSpace â„‚ Fâ‚‚] [NormedSpace ğ•œ Fâ‚‚] [SMulCommClass â„‚ ğ•œ Fâ‚‚]
  [NormedAddCommGroup Fâ‚ƒ] [NormedSpace â„‚ Fâ‚ƒ] [NormedSpace ğ•œ Fâ‚ƒ] [SMulCommClass â„‚ ğ•œ Fâ‚ƒ]

open FourierTransform Convolution

/-- The bilinear convolution of Schwartz functions.

The continuity in the left argument is provided in `SchwartzMap.convolution_continuous_left`. -/
noncomputable
def convolution (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) : ğ“¢(E, Fâ‚) â†’â‚—[ğ•œ] ğ“¢(E, Fâ‚‚) â†’L[ğ•œ] ğ“¢(E, Fâ‚ƒ) where
  toFun f := fourierInvCLM ğ•œ ğ“¢(E, Fâ‚ƒ) âˆ˜L pairing B (ğ“• f) âˆ˜L fourierCLM ğ•œ ğ“¢(E, Fâ‚‚)
  map_add' := by simp [FourierTransform.fourier_add]
  map_smul' := by simp [FourierTransform.fourier_smul]

@[simp]
theorem convolution_flip (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚)) :
    convolution B.flip g f = convolution B f g := rfl

/-- The convolution is continuous in the left argument.

Note that since `ğ“¢(E, F)` is not a normed space, uncurried and curried continuity do not
coincide. -/
@[fun_prop]
theorem convolution_continuous_left (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) (g : ğ“¢(E, Fâ‚‚)) :
    Continuous (convolution B Â· g) := (convolution B.flip g).continuous

variable [CompleteSpace Fâ‚ƒ]

theorem fourier_convolution (B : Fâ‚ â†’L[ğ•œ] Fâ‚‚ â†’L[ğ•œ] Fâ‚ƒ) (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚)) :
    ğ“• (convolution B f g) = pairing B (ğ“• f) (ğ“• g) := by simp [convolution]

variable [CompleteSpace Fâ‚] [CompleteSpace Fâ‚‚]

open MeasureTheory

theorem fourier_convolution_apply (B : Fâ‚ â†’L[â„‚] Fâ‚‚ â†’L[â„‚] Fâ‚ƒ) (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚)) (x : E) :
    ğ“• (convolution B f g) x = ğ“• (f â‹†[B] g) x := by
  simp [fourier_convolution, fourier_coe,
    Real.fourier_bilin_convolution_eq B f.integrable g.integrable f.continuous g.continuous]

/-- The convolution on Schwartz functions is equal to the convolution on functions. -/
theorem convolution_apply (B : Fâ‚ â†’L[â„‚] Fâ‚‚ â†’L[â„‚] Fâ‚ƒ) (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚)) (x : E) :
    convolution B f g x = (f â‹†[B] g) x := calc
  _ = ğ“•â» (ğ“• (convolution B f g)) x := by simp
  _ = ğ“•â» (fun y â†¦ ğ“• (f â‹†[B] g) y) x := by
    rw [fourierInv_coe]
    apply MeasureTheory.integral_congr_ae
    filter_upwards with x
    rw [fourier_convolution_apply]
  _ = _ := by
    rw [Continuous.fourierInv_fourier_eq]
    Â· refine BddAbove.continuous_convolution_right_of_integrable B ?_ f.integrable g.continuous
      exact âŸ¨SchwartzMap.seminorm â„ 0 0 g, fun x âŸ¨y, hyâŸ© â†¦ hy â–¸ norm_le_seminorm â„ g yâŸ©
    Â· exact f.integrable.integrable_convolution B g.integrable
    Â· have : Integrable (fun Î¾ â†¦ B (ğ“• f Î¾) (ğ“• g Î¾)) volume := (pairing B (ğ“• f) (ğ“• g)).integrable
      convert this
      rw [â† fourier_convolution_apply B f g, fourier_convolution, pairing_apply_apply]


end SchwartzMap
