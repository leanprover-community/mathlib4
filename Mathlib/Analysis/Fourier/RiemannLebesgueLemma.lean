/-
Copyright (c) 2022 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Fourier.FourierTransform
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.InnerProductSpace.EuclideanDist
import Mathlib.MeasureTheory.Function.ContinuousMapDense
import Mathlib.MeasureTheory.Group.Integral
import Mathlib.MeasureTheory.Integral.SetIntegral
import Mathlib.MeasureTheory.Measure.Haar.NormedSpace
import Mathlib.Topology.EMetricSpace.Paracompact

#align_import analysis.fourier.riemann_lebesgue_lemma from "leanprover-community/mathlib"@"fd5edc43dc4f10b85abfe544b88f82cf13c5f844"

/-!
# The Riemann-Lebesgue Lemma

In this file we prove the Riemann-Lebesgue lemma, for functions on finite-dimensional real vector
spaces `V`: if `f` is a function on `V` (valued in a complete normed space `E`), then the
Fourier transform of `f`, viewed as a function on the dual space of `V`, tends to 0 along the
cocompact filter. Here the Fourier transform is defined by

`Œª w : V ‚ÜíL[‚Ñù] ‚Ñù, ‚à´ (v : V), exp (‚Üë(2 * œÄ * w v) * I) ‚Ä¢ f x`.

This is true for arbitrary functions, but is only interesting for `L¬π` functions (if `f` is not
integrable then the integral is zero for all `w`). This is proved first for continuous
compactly-supported functions on inner-product spaces; then we pass to arbitrary functions using the
density of continuous compactly-supported functions in `L¬π` space. Finally we generalise from
inner-product spaces to arbitrary finite-dimensional spaces, by choosing a continuous linear
equivalence to an inner-product space.

## Main results

- `tendsto_integral_exp_inner_smul_cocompact` : for `V` a finite-dimensional real inner product
  space and `f : V ‚Üí E`, the function `Œª w : V, ‚à´ v : V, exp (2 * œÄ * ‚ü™w, v‚ü´ * I) ‚Ä¢ f v` tends to 0
  along `cocompact V`.
- `tendsto_integral_exp_smul_cocompact` : for `V` a finite-dimensional real vector space (endowed
  with its unique Hausdorff topological vector space structure), and `W` the dual of `V`, the
  function `Œª w : W, ‚à´ v : V, exp (2 * œÄ * w v * I) ‚Ä¢ f v` tends to along `cocompact W`.
- `Real.tendsto_integral_exp_smul_cocompact`: special case of functions on `‚Ñù`.
- `Real.zero_at_infty_fourierIntegral` and `Real.zero_at_infty_vector_fourierIntegral`:
  reformulations explicitly using the Fourier integral.
-/

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220

noncomputable section

open MeasureTheory Filter Complex Set FiniteDimensional

open scoped Filter Topology Real ENNReal FourierTransform RealInnerProductSpace NNReal

variable {E V : Type*} [NormedAddCommGroup E] [NormedSpace ‚ÑÇ E] {f : V ‚Üí E}

local notation "e" => Real.fourierChar

section InnerProductSpace

variable [NormedAddCommGroup V] [MeasurableSpace V] [BorelSpace V] [InnerProductSpace ‚Ñù V]
  [FiniteDimensional ‚Ñù V]

/-- The integrand in the Riemann-Lebesgue lemma for `f` is integrable iff `f` is. -/
theorem fourier_integrand_integrable (w : V) :
    Integrable f ‚Üî Integrable fun v : V => e[-‚ü™v, w‚ü´] ‚Ä¢ f v := by
  have hL : Continuous fun p : V √ó V => BilinForm.toLin bilinFormOfRealInner p.1 p.2 :=
    continuous_inner
  rw [VectorFourier.fourier_integral_convergent_iff Real.continuous_fourierChar hL w]
  simp only [BilinForm.toLin_apply, bilinFormOfRealInner_apply]
#align fourier_integrand_integrable fourier_integrand_integrable

variable [CompleteSpace E]

-- Porting note: binary operations appear not to work?
-- local notation "i" => fun w => (1 / (2 * ‚Äñw‚Äñ ^ 2)) ‚Ä¢ w
local notation "i" => fun (w : V) => HDiv.hDiv (1 : ‚Ñù) (HMul.hMul (2 : ‚Ñù) (HPow.hPow ‚Äñw‚Äñ 2)) ‚Ä¢ w

/-- Shifting `f` by `(1 / (2 * ‚Äñw‚Äñ ^ 2)) ‚Ä¢ w` negates the integral in the Riemann-Lebesgue lemma. -/
theorem fourier_integral_half_period_translate {w : V} (hw : w ‚â† 0) :
    (‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ f (v + i w)) = -‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ f v := by
  have hiw : ‚ü™i w, w‚ü´ = 1 / 2 := by
    rw [inner_smul_left, inner_self_eq_norm_sq_to_K, IsROrC.ofReal_real_eq_id, id.def,
      IsROrC.conj_to_real, ‚Üê div_div, div_mul_cancel]
    rwa [Ne.def, sq_eq_zero_iff, norm_eq_zero]
  have :
    (fun v : V => e[-‚ü™v, w‚ü´] ‚Ä¢ f (v + i w)) =
      fun v : V => (fun x : V => -e[-‚ü™x, w‚ü´] ‚Ä¢ f x) (v + i w) := by
    ext1 v
    simp_rw [inner_add_left, hiw, Real.fourierChar_apply, neg_add, mul_add, ofReal_add, add_mul,
      exp_add]
    have : 2 * œÄ * -(1 / 2) = -œÄ := by field_simp; ring
    rw [this, ofReal_neg, neg_mul, exp_neg, exp_pi_mul_I, inv_neg, inv_one, mul_neg_one, neg_neg]
  rw [this]
  -- Porting note:
  -- The next three lines had just been
  -- rw [integral_add_right_eq_self (fun (x : V) ‚Ü¶ -(e[-‚ü™x, w‚ü´]) ‚Ä¢ f x)
  --       ((fun w ‚Ü¶ (1 / (2 * ‚Äñw‚Äñ ^ (2 : ‚Ñï))) ‚Ä¢ w) w)]
  -- Unfortunately now we need to specify `volume`, and call `dsimp`.
  have := @integral_add_right_eq_self _ _ _ _ _ volume _ _ _ (fun (x : V) ‚Ü¶ -(e[-‚ü™x, w‚ü´]) ‚Ä¢ f x)
    ((fun w ‚Ü¶ (1 / (2 * ‚Äñw‚Äñ ^ (2 : ‚Ñï))) ‚Ä¢ w) w)
  erw [this] -- Porting note, we can avoid `erw` by first calling `dsimp at this ‚ä¢`.
  simp only [neg_smul, integral_neg]
#align fourier_integral_half_period_translate fourier_integral_half_period_translate

/-- Rewrite the Fourier integral in a form that allows us to use uniform continuity. -/
theorem fourier_integral_eq_half_sub_half_period_translate {w : V} (hw : w ‚â† 0)
    (hf : Integrable f) :
    ‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ f v = (1 / (2 : ‚ÑÇ)) ‚Ä¢ ‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ (f v - f (v + i w)) := by
  simp_rw [smul_sub]
  rw [integral_sub, fourier_integral_half_period_translate hw, sub_eq_add_neg, neg_neg, ‚Üê
    two_smul ‚ÑÇ _, ‚Üê @smul_assoc _ _ _ _ _ _ (IsScalarTower.left ‚ÑÇ), smul_eq_mul]
  norm_num
  exacts [(fourier_integrand_integrable w).mp hf,
    (fourier_integrand_integrable w).mp (hf.comp_add_right _)]
#align fourier_integral_eq_half_sub_half_period_translate fourier_integral_eq_half_sub_half_period_translate

/-- Riemann-Lebesgue Lemma for continuous and compactly-supported functions: the integral
`‚à´ v, exp (-2 * œÄ * ‚ü™w, v‚ü´ * I) ‚Ä¢ f v` tends to 0 wrt `cocompact V`. Note that this is primarily
of interest as a preparatory step for the more general result
`tendsto_integral_exp_inner_smul_cocompact` in which `f` can be arbitrary. -/
theorem tendsto_integral_exp_inner_smul_cocompact_of_continuous_compact_support (hf1 : Continuous f)
    (hf2 : HasCompactSupport f) :
    Tendsto (fun w : V => ‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ f v) (cocompact V) (ùìù 0) := by
  refine' NormedAddCommGroup.tendsto_nhds_zero.mpr fun Œµ hŒµ => _
  suffices ‚àÉ T : ‚Ñù, ‚àÄ w : V, T ‚â§ ‚Äñw‚Äñ ‚Üí ‚Äñ‚à´ v : V, e[-‚ü™v, w‚ü´] ‚Ä¢ f v‚Äñ < Œµ by
    simp_rw [‚Üê comap_dist_left_atTop_eq_cocompact (0 : V), eventually_comap, eventually_atTop,
      dist_eq_norm', sub_zero]
    exact
      let ‚ü®T, hT‚ü© := this
      ‚ü®T, fun b hb v hv => hT v (hv.symm ‚ñ∏ hb)‚ü©
  obtain ‚ü®R, -, hR_bd‚ü© : ‚àÉ R : ‚Ñù, 0 < R ‚àß ‚àÄ x : V, R ‚â§ ‚Äñx‚Äñ ‚Üí f x = 0
  exact hf2.exists_pos_le_norm
  let A := {v : V | ‚Äñv‚Äñ ‚â§ R + 1}
  have mA : MeasurableSet A := by
    suffices A = Metric.closedBall (0 : V) (R + 1) by
      rw [this]
      exact Metric.isClosed_ball.measurableSet
    simp_rw [Metric.closedBall, dist_eq_norm, sub_zero]
  obtain ‚ü®B, hB_pos, hB_vol‚ü© : ‚àÉ B : ‚Ñù‚â•0, 0 < B ‚àß volume A ‚â§ B := by
    have hc : IsCompact A := by
      simpa only [Metric.closedBall, dist_eq_norm, sub_zero] using isCompact_closedBall (0 : V) _
    let B‚ÇÄ := volume A
    replace hc : B‚ÇÄ < ‚ä§ := hc.measure_lt_top
    refine' ‚ü®B‚ÇÄ.toNNReal + 1, add_pos_of_nonneg_of_pos B‚ÇÄ.toNNReal.coe_nonneg one_pos, _‚ü©
    rw [ENNReal.coe_add, ENNReal.coe_one, ENNReal.coe_toNNReal hc.ne]
    exact le_self_add
  --* Use uniform continuity to choose Œ¥ such that `‚Äñx - y‚Äñ < Œ¥` implies `‚Äñf x - f y‚Äñ < Œµ / B`.
  obtain ‚ü®Œ¥, hŒ¥1, hŒ¥2‚ü© :=
    Metric.uniformContinuous_iff.mp (hf2.uniformContinuous_of_continuous hf1) (Œµ / B)
      (div_pos hŒµ hB_pos)
  refine' ‚ü®1 / 2 + 1 / (2 * Œ¥), fun w hw_bd => _‚ü©
  have hw_ne : w ‚â† 0 := by
    contrapose! hw_bd; rw [hw_bd, norm_zero]
    exact add_pos one_half_pos (one_div_pos.mpr <| mul_pos two_pos hŒ¥1)
  have hw'_nm : ‚Äñi w‚Äñ = 1 / (2 * ‚Äñw‚Äñ) := by
    rw [norm_smul, norm_div, Real.norm_of_nonneg (mul_nonneg two_pos.le <| sq_nonneg _), norm_one,
      sq, ‚Üê div_div, ‚Üê div_div, ‚Üê div_div, div_mul_cancel _ (norm_eq_zero.not.mpr hw_ne)]
  --* Rewrite integral in terms of `f v - f (v + w')`.
  -- Porting note: this was
  -- rw [norm_eq_abs, ‚Üê Complex.ofReal_one, ‚Üê ofReal_bit0, ‚Üê of_real_div,
  --   Complex.abs_of_nonneg one_half_pos.le]
  have : ‚Äñ(1 / 2 : ‚ÑÇ)‚Äñ = 1 / 2 := by norm_num
  rw [fourier_integral_eq_half_sub_half_period_translate hw_ne
      (hf1.integrable_of_hasCompactSupport hf2),
    norm_smul, this]
  have : Œµ = 1 / 2 * (2 * Œµ) := by field_simp; rw [mul_comm]
  rw [this, mul_lt_mul_left (one_half_pos : (0 : ‚Ñù) < 1 / 2)]
  refine' lt_of_le_of_lt (norm_integral_le_integral_norm _) _
  simp_rw [norm_smul, norm_eq_abs, abs_coe_circle, one_mul]
  --* Show integral can be taken over A only.
  have int_A : ‚à´ v : V, ‚Äñf v - f (v + i w)‚Äñ = ‚à´ v in A, ‚Äñf v - f (v + i w)‚Äñ := by
    refine' (set_integral_eq_integral_of_forall_compl_eq_zero fun v hv => _).symm
    dsimp only at hv
    simp only [mem_setOf, not_le] at hv
    rw [hR_bd v _, hR_bd (v + i w) _, sub_zero, norm_zero]
    ¬∑ rw [‚Üê sub_neg_eq_add]
      refine' le_trans _ (norm_sub_norm_le _ _)
      rw [le_sub_iff_add_le, norm_neg]
      refine' le_trans _ hv.le
      rw [add_le_add_iff_left, hw'_nm, ‚Üê div_div]
      refine' (div_le_one <| norm_pos_iff.mpr hw_ne).mpr _
      refine' le_trans (le_add_of_nonneg_right <| one_div_nonneg.mpr <| _) hw_bd
      exact (mul_pos (zero_lt_two' ‚Ñù) hŒ¥1).le
    ¬∑ exact ((le_add_iff_nonneg_right _).mpr zero_le_one).trans hv.le
  rw [int_A]; clear int_A
  --* Bound integral using fact that `‚Äñf v - f (v + w')‚Äñ` is small.
  have bdA : ‚àÄ v : V, v ‚àà A ‚Üí ‚Äñ‚Äñf v - f (v + i w)‚Äñ‚Äñ ‚â§ Œµ / B := by
    simp_rw [norm_norm]
    simp_rw [dist_eq_norm] at hŒ¥2
    refine' fun x _ => (hŒ¥2 _).le
    rw [sub_add_cancel', norm_neg, hw'_nm, ‚Üê div_div, div_lt_iff (norm_pos_iff.mpr hw_ne), ‚Üê
      div_lt_iff' hŒ¥1, div_div]
    refine' (lt_add_of_pos_left _ _).trans_le hw_bd
    exact one_half_pos
  have bdA2 := norm_set_integral_le_of_norm_le_const (hB_vol.trans_lt ENNReal.coe_lt_top) bdA ?_
  swap;
  ¬∑ apply Continuous.aestronglyMeasurable
    exact
      continuous_norm.comp <|
        Continuous.sub hf1 <| Continuous.comp hf1 <| continuous_id'.add continuous_const
  have : ‚Äñ_‚Äñ = ‚à´ v : V in A, ‚Äñf v - f (v + i w)‚Äñ :=
    Real.norm_of_nonneg (set_integral_nonneg mA fun x _ => norm_nonneg _)
  rw [this] at bdA2
  refine' bdA2.trans_lt _
  rw [div_mul_eq_mul_div, div_lt_iff (NNReal.coe_pos.mpr hB_pos), mul_comm (2 : ‚Ñù), mul_assoc,
    mul_lt_mul_left hŒµ]
  rw [‚Üê ENNReal.toReal_le_toReal] at hB_vol
  ¬∑ refine' hB_vol.trans_lt _
    rw [(by rfl : (‚ÜëB : ENNReal).toReal = ‚ÜëB), two_mul]
    exact lt_add_of_pos_left _ hB_pos
  exacts [(hB_vol.trans_lt ENNReal.coe_lt_top).ne, ENNReal.coe_lt_top.ne]
#align tendsto_integral_exp_inner_smul_cocompact_of_continuous_compact_support tendsto_integral_exp_inner_smul_cocompact_of_continuous_compact_support

variable (f)

/-- Riemann-Lebesgue lemma for functions on a real inner-product space: the integral
`‚à´ v, exp (-2 * œÄ * ‚ü™w, v‚ü´ * I) ‚Ä¢ f v` tends to 0 as `w ‚Üí ‚àû`. -/
theorem tendsto_integral_exp_inner_smul_cocompact :
    Tendsto (fun w : V => ‚à´ v, e[-‚ü™v, w‚ü´] ‚Ä¢ f v) (cocompact V) (ùìù 0) := by
  by_cases hfi : Integrable f; swap
  ¬∑ convert tendsto_const_nhds (a := (0 : E)) with w
    apply integral_undef
    rwa [‚Üê fourier_integrand_integrable w]
  refine' Metric.tendsto_nhds.mpr fun Œµ hŒµ => _
  obtain ‚ü®g, hg_supp, hfg, hg_cont, -‚ü© :=
    hfi.exists_hasCompactSupport_integral_sub_le (div_pos hŒµ two_pos)
  refine'
    ((Metric.tendsto_nhds.mp
            (tendsto_integral_exp_inner_smul_cocompact_of_continuous_compact_support hg_cont
              hg_supp))
          _ (div_pos hŒµ two_pos)).mp
      (eventually_of_forall fun w hI => _)
  rw [dist_eq_norm] at hI ‚ä¢
  have : ‚Äñ(‚à´ v, e[-‚ü™v, w‚ü´] ‚Ä¢ f v) - ‚à´ v, e[-‚ü™v, w‚ü´] ‚Ä¢ g v‚Äñ ‚â§ Œµ / 2 := by
    refine' le_trans _ hfg
    simp_rw [‚Üê
      integral_sub ((fourier_integrand_integrable w).mp hfi)
        ((fourier_integrand_integrable w).mp (hg_cont.integrable_of_hasCompactSupport hg_supp)),
      ‚Üê smul_sub, ‚Üê Pi.sub_apply]
    exact
      VectorFourier.norm_fourierIntegral_le_integral_norm e volume
        (BilinForm.toLin bilinFormOfRealInner) (f - g) w
  replace := add_lt_add_of_le_of_lt this hI
  rw [add_halves] at this
  refine' ((le_of_eq _).trans (norm_add_le _ _)).trans_lt this
  simp only [sub_zero, sub_add_cancel]
#align tendsto_integral_exp_inner_smul_cocompact tendsto_integral_exp_inner_smul_cocompact

/-- The Riemann-Lebesgue lemma for functions on `‚Ñù`. -/
theorem Real.tendsto_integral_exp_smul_cocompact (f : ‚Ñù ‚Üí E) :
    Tendsto (fun w : ‚Ñù => ‚à´ v : ‚Ñù, e[-(v * w)] ‚Ä¢ f v) (cocompact ‚Ñù) (ùìù 0) :=
  tendsto_integral_exp_inner_smul_cocompact f
#align real.tendsto_integral_exp_smul_cocompact Real.tendsto_integral_exp_smul_cocompact

/-- The Riemann-Lebesgue lemma for functions on `‚Ñù`, formulated via `Real.fourierIntegral`. -/
theorem Real.zero_at_infty_fourierIntegral (f : ‚Ñù ‚Üí E) : Tendsto (ùìï f) (cocompact ‚Ñù) (ùìù 0) :=
  tendsto_integral_exp_inner_smul_cocompact f
#align real.zero_at_infty_fourier_integral Real.zero_at_infty_fourierIntegral

/-- Riemann-Lebesgue lemma for functions on a finite-dimensional inner-product space, formulated
via dual space. **Do not use** -- it is only a stepping stone to
`tendsto_integral_exp_smul_cocompact` where the inner-product-space structure isn't required. -/
theorem tendsto_integral_exp_smul_cocompact_of_inner_product (Œº : Measure V) [Œº.IsAddHaarMeasure] :
    Tendsto (fun w : V ‚ÜíL[‚Ñù] ‚Ñù => ‚à´ v, e[-w v] ‚Ä¢ f v ‚àÇŒº) (cocompact (V ‚ÜíL[‚Ñù] ‚Ñù)) (ùìù 0) := by
  obtain ‚ü®C, _, _, hC‚ü© := Œº.isAddHaarMeasure_eq_smul_isAddHaarMeasure volume
  rw [hC]
  simp_rw [integral_smul_measure]
  rw [‚Üê (smul_zero _ : C.toReal ‚Ä¢ (0 : E) = 0)]
  apply Tendsto.const_smul
  let A := (InnerProductSpace.toDual ‚Ñù V).symm
  have : (fun w : V ‚ÜíL[‚Ñù] ‚Ñù => ‚à´ v, e[-w v] ‚Ä¢ f v) = (fun w : V => ‚à´ v, e[-‚ü™v, w‚ü´] ‚Ä¢ f v) ‚àò A := by
    ext1 w
    congr 1 with v : 1
    rw [‚Üê inner_conj_symm, IsROrC.conj_to_real, InnerProductSpace.toDual_symm_apply,
      Real.fourierChar_apply]
  rw [this]
  exact
    (tendsto_integral_exp_inner_smul_cocompact f).comp
      A.toHomeomorph.toCocompactMap.cocompact_tendsto'
#align tendsto_integral_exp_smul_cocompact_of_inner_product tendsto_integral_exp_smul_cocompact_of_inner_product

end InnerProductSpace

section NoInnerProduct

variable (f) [AddCommGroup V] [TopologicalSpace V] [TopologicalAddGroup V] [T2Space V]
  [MeasurableSpace V] [BorelSpace V] [Module ‚Ñù V] [ContinuousSMul ‚Ñù V] [FiniteDimensional ‚Ñù V]
  [CompleteSpace E]

/-- Riemann-Lebesgue lemma for functions on a finite-dimensional real vector space, formulated via
dual space. -/
theorem tendsto_integral_exp_smul_cocompact (Œº : Measure V) [Œº.IsAddHaarMeasure] :
    Tendsto (fun w : V ‚ÜíL[‚Ñù] ‚Ñù => ‚à´ v, e[-w v] ‚Ä¢ f v ‚àÇŒº) (cocompact (V ‚ÜíL[‚Ñù] ‚Ñù)) (ùìù 0) := by
  -- We have already proved the result for inner-product spaces, formulated in a way which doesn't
  -- refer to the inner product. So we choose an arbitrary inner-product space isomorphic to V
  -- and port the result over from there.
  let V' := EuclideanSpace ‚Ñù (Fin (finrank ‚Ñù V))
  have A : V ‚âÉL[‚Ñù] V' := toEuclidean
  borelize V'
  -- various equivs derived from A
  let A‚Çò : MeasurableEquiv V V' := A.toHomeomorph.toMeasurableEquiv
  -- isomorphism between duals derived from A -- need to do continuity as a separate step in order
  -- to apply `LinearMap.continuous_of_finiteDimensional`.
  let Adual‚Çó : (V ‚ÜíL[‚Ñù] ‚Ñù) ‚âÉ‚Çó[‚Ñù] V' ‚ÜíL[‚Ñù] ‚Ñù :=
    { toFun := fun t => t.comp A.symm.toContinuousLinearMap
      invFun := fun t => t.comp A.toContinuousLinearMap
      map_add' := by
        intro t s
        ext1 v
        simp only [ContinuousLinearMap.coe_comp', Function.comp_apply,
          ContinuousLinearMap.add_apply]
      map_smul' := by
        intro x f
        ext1 v
        simp only [RingHom.id_apply, ContinuousLinearMap.coe_comp', Function.comp_apply,
          ContinuousLinearMap.smul_apply]
      left_inv := by
        intro w
        ext1 v
        simp only [ContinuousLinearMap.coe_comp',
          ContinuousLinearEquiv.coe_coe, Function.comp_apply,
          ContinuousLinearEquiv.symm_apply_apply]
      right_inv := by
        intro w
        ext1 v
        simp only [ContinuousLinearMap.coe_comp',
          ContinuousLinearEquiv.coe_coe, Function.comp_apply,
          ContinuousLinearEquiv.apply_symm_apply] }
  let Adual : (V ‚ÜíL[‚Ñù] ‚Ñù) ‚âÉL[‚Ñù] V' ‚ÜíL[‚Ñù] ‚Ñù :=
    { Adual‚Çó with
      continuous_toFun := Adual‚Çó.toLinearMap.continuous_of_finiteDimensional
      continuous_invFun := Adual‚Çó.symm.toLinearMap.continuous_of_finiteDimensional }
  have : (Œº.map A‚Çò).IsAddHaarMeasure := Measure.MapContinuousLinearEquiv.isAddHaarMeasure _ A
  convert
    (tendsto_integral_exp_smul_cocompact_of_inner_product (f ‚àò A.symm) (Œº.map A‚Çò)).comp
      Adual.toHomeomorph.toCocompactMap.cocompact_tendsto' with w
  rw [Function.comp_apply, integral_map_equiv]
  congr 1 with v : 1
  congr
  ¬∑ -- Porting note: added `congr_arg`
    apply congr_arg w
    exact (ContinuousLinearEquiv.symm_apply_apply A v).symm
  ¬∑ exact (ContinuousLinearEquiv.symm_apply_apply A v).symm
#align tendsto_integral_exp_smul_cocompact tendsto_integral_exp_smul_cocompact

/-- The Riemann-Lebesgue lemma, formulated in terms of `VectorFourier.fourierIntegral` (with the
pairing in the definition of `fourier_integral` taken to be the canonical pairing between `V` and
its dual space). -/
theorem Real.zero_at_infty_vector_fourierIntegral (Œº : Measure V) [Œº.IsAddHaarMeasure] :
    Tendsto (VectorFourier.fourierIntegral e Œº (topDualPairing ‚Ñù V).flip f) (cocompact (V ‚ÜíL[‚Ñù] ‚Ñù))
      (ùìù 0) :=
  _root_.tendsto_integral_exp_smul_cocompact f Œº
#align real.zero_at_infty_vector_fourier_integral Real.zero_at_infty_vector_fourierIntegral

end NoInnerProduct
