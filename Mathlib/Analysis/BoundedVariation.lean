/-
Copyright (c) 2022 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel
-/
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Calculus.FDeriv.Equiv
import Mathlib.Analysis.Calculus.FDeriv.Prod
import Mathlib.Analysis.Calculus.Monotone
import Mathlib.Data.Set.Function
import Mathlib.Algebra.Group.Basic
import Mathlib.Tactic.WLOG

#align_import analysis.bounded_variation from "leanprover-community/mathlib"@"3bce8d800a6f2b8f63fe1e588fd76a9ff4adcebe"

/-!
# Functions of bounded variation

We study functions of bounded variation. In particular, we show that a bounded variation function
is a difference of monotone functions, and differentiable almost everywhere. This implies that
Lipschitz functions from the real line into finite-dimensional vector space are also differentiable
almost everywhere.

## Main definitions and results

* `eVariationOn f s` is the total variation of the function `f` on the set `s`, in `‚Ñù‚â•0‚àû`.
* `BoundedVariationOn f s` registers that the variation of `f` on `s` is finite.
* `LocallyBoundedVariationOn f s` registers that `f` has finite variation on any compact
  subinterval of `s`.
* `variationOnFromTo f s a b` is the signed variation of `f` on `s ‚à© Icc a b`, converted to `‚Ñù`.

* `eVariationOn.Icc_add_Icc` states that the variation of `f` on `[a, c]` is the sum of its
  variations on `[a, b]` and `[b, c]`.
* `LocallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn` proves that a function
  with locally bounded variation is the difference of two monotone functions.
* `LipschitzWith.locallyBoundedVariationOn` shows that a Lipschitz function has locally
  bounded variation.
* `LocallyBoundedVariationOn.ae_differentiableWithinAt` shows that a bounded variation
  function into a finite dimensional real vector space is differentiable almost everywhere.
* `LipschitzOnWith.ae_differentiableWithinAt` is the same result for Lipschitz functions.

We also give several variations around these results.

## Implementation

We define the variation as an extended nonnegative real, to allow for infinite variation. This makes
it possible to use the complete linear order structure of `‚Ñù‚â•0‚àû`. The proofs would be much
more tedious with an `‚Ñù`-valued or `‚Ñù‚â•0`-valued variation, since one would always need to check
that the sets one uses are nonempty and bounded above as these are only conditionally complete.
-/


open scoped NNReal ENNReal Topology UniformConvergence

open Set MeasureTheory Filter

-- Porting note: sectioned variables because a `wlog` was broken due to extra variables in context
variable {Œ± : Type*} [LinearOrder Œ±] {E : Type*} [PseudoEMetricSpace E]

/-- The (extended real valued) variation of a function `f` on a set `s` inside a linear order is
the supremum of the sum of `edist (f (u (i+1))) (f (u i))` over all finite increasing
sequences `u` in `s`. -/
noncomputable def eVariationOn (f : Œ± ‚Üí E) (s : Set Œ±) : ‚Ñù‚â•0‚àû :=
  ‚®Ü p : ‚Ñï √ó { u : ‚Ñï ‚Üí Œ± // Monotone u ‚àß ‚àÄ i, u i ‚àà s },
    ‚àë i ‚àà Finset.range p.1, edist (f (p.2.1 (i + 1))) (f (p.2.1 i))
#align evariation_on eVariationOn

/-- A function has bounded variation on a set `s` if its total variation there is finite. -/
def BoundedVariationOn (f : Œ± ‚Üí E) (s : Set Œ±) :=
  eVariationOn f s ‚â† ‚àû
#align has_bounded_variation_on BoundedVariationOn

/-- A function has locally bounded variation on a set `s` if, given any interval `[a, b]` with
endpoints in `s`, then the function has finite variation on `s ‚à© [a, b]`. -/
def LocallyBoundedVariationOn (f : Œ± ‚Üí E) (s : Set Œ±) :=
  ‚àÄ a b, a ‚àà s ‚Üí b ‚àà s ‚Üí BoundedVariationOn f (s ‚à© Icc a b)
#align has_locally_bounded_variation_on LocallyBoundedVariationOn

/-! ## Basic computations of variation -/

namespace eVariationOn

theorem nonempty_monotone_mem {s : Set Œ±} (hs : s.Nonempty) :
    Nonempty { u // Monotone u ‚àß ‚àÄ i : ‚Ñï, u i ‚àà s } := by
  obtain ‚ü®x, hx‚ü© := hs
  exact ‚ü®‚ü®fun _ => x, fun i j _ => le_rfl, fun _ => hx‚ü©‚ü©
#align evariation_on.nonempty_monotone_mem eVariationOn.nonempty_monotone_mem

theorem eq_of_edist_zero_on {f f' : Œ± ‚Üí E} {s : Set Œ±} (h : ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí edist (f x) (f' x) = 0) :
    eVariationOn f s = eVariationOn f' s := by
  dsimp only [eVariationOn]
  congr 1 with p : 1
  congr 1 with i : 1
  rw [edist_congr_right (h <| p.snd.prop.2 (i + 1)), edist_congr_left (h <| p.snd.prop.2 i)]
#align evariation_on.eq_of_edist_zero_on eVariationOn.eq_of_edist_zero_on

theorem eq_of_eqOn {f f' : Œ± ‚Üí E} {s : Set Œ±} (h : EqOn f f' s) :
    eVariationOn f s = eVariationOn f' s :=
  eq_of_edist_zero_on fun x xs => by rw [h xs, edist_self]
#align evariation_on.eq_of_eq_on eVariationOn.eq_of_eqOn

theorem sum_le (f : Œ± ‚Üí E) {s : Set Œ±} (n : ‚Ñï) {u : ‚Ñï ‚Üí Œ±} (hu : Monotone u) (us : ‚àÄ i, u i ‚àà s) :
    (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) ‚â§ eVariationOn f s :=
  le_iSup_of_le ‚ü®n, u, hu, us‚ü© le_rfl
#align evariation_on.sum_le eVariationOn.sum_le

theorem sum_le_of_monotoneOn_Icc (f : Œ± ‚Üí E) {s : Set Œ±} {m n : ‚Ñï} {u : ‚Ñï ‚Üí Œ±}
    (hu : MonotoneOn u (Icc m n)) (us : ‚àÄ i ‚àà Icc m n, u i ‚àà s) :
    (‚àë i ‚àà Finset.Ico m n, edist (f (u (i + 1))) (f (u i))) ‚â§ eVariationOn f s := by
  rcases le_total n m with hnm | hmn
  ¬∑ simp [Finset.Ico_eq_empty_of_le hnm]
  let œÄ := projIcc m n hmn
  let v i := u (œÄ i)
  calc
    ‚àë i ‚àà Finset.Ico m n, edist (f (u (i + 1))) (f (u i))
        = ‚àë i ‚àà Finset.Ico m n, edist (f (v (i + 1))) (f (v i)) :=
      Finset.sum_congr rfl fun i hi ‚Ü¶ by
        rw [Finset.mem_Ico] at hi
        simp only [v, œÄ, projIcc_of_mem hmn ‚ü®hi.1, hi.2.le‚ü©,
          projIcc_of_mem hmn ‚ü®hi.1.trans i.le_succ, hi.2‚ü©]
    _ ‚â§ ‚àë i ‚àà Finset.range n, edist (f (v (i + 1))) (f (v i)) :=
      Finset.sum_mono_set _ (Nat.Iio_eq_range ‚ñ∏ Finset.Ico_subset_Iio_self)
    _ ‚â§ eVariationOn f s :=
      sum_le _ _ (fun i j h ‚Ü¶ hu (œÄ i).2 (œÄ j).2 (monotone_projIcc hmn h)) fun i ‚Ü¶ us _ (œÄ i).2
#align evariation_on.sum_le_of_monotone_on_Icc eVariationOn.sum_le_of_monotoneOn_Icc

theorem sum_le_of_monotoneOn_Iic (f : Œ± ‚Üí E) {s : Set Œ±} {n : ‚Ñï} {u : ‚Ñï ‚Üí Œ±}
    (hu : MonotoneOn u (Iic n)) (us : ‚àÄ i ‚â§ n, u i ‚àà s) :
    (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) ‚â§ eVariationOn f s := by
  simpa using sum_le_of_monotoneOn_Icc f (m := 0) (hu.mono Icc_subset_Iic_self) fun i hi ‚Ü¶ us i hi.2
#align evariation_on.sum_le_of_monotone_on_Iic eVariationOn.sum_le_of_monotoneOn_Iic

theorem mono (f : Œ± ‚Üí E) {s t : Set Œ±} (hst : t ‚äÜ s) : eVariationOn f t ‚â§ eVariationOn f s := by
  apply iSup_le _
  rintro ‚ü®n, ‚ü®u, hu, ut‚ü©‚ü©
  exact sum_le f n hu fun i => hst (ut i)
#align evariation_on.mono eVariationOn.mono

theorem _root_.BoundedVariationOn.mono {f : Œ± ‚Üí E} {s : Set Œ±} (h : BoundedVariationOn f s)
    {t : Set Œ±} (ht : t ‚äÜ s) : BoundedVariationOn f t :=
  ne_top_of_le_ne_top h (eVariationOn.mono f ht)
#align has_bounded_variation_on.mono BoundedVariationOn.mono

theorem _root_.BoundedVariationOn.locallyBoundedVariationOn {f : Œ± ‚Üí E} {s : Set Œ±}
    (h : BoundedVariationOn f s) : LocallyBoundedVariationOn f s := fun _ _ _ _ =>
  h.mono (inter_subset_left _ _)
#align has_bounded_variation_on.has_locally_bounded_variation_on BoundedVariationOn.locallyBoundedVariationOn

theorem edist_le (f : Œ± ‚Üí E) {s : Set Œ±} {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s) :
    edist (f x) (f y) ‚â§ eVariationOn f s := by
  wlog hxy : y ‚â§ x generalizing x y
  ¬∑ rw [edist_comm]
    exact this hy hx (le_of_not_le hxy)
  let u : ‚Ñï ‚Üí Œ± := fun n => if n = 0 then y else x
  have hu : Monotone u := monotone_nat_of_le_succ fun
  | 0 => hxy
  | (_ + 1) => le_rfl
  have us : ‚àÄ i, u i ‚àà s := fun
  | 0 => hy
  | (_ + 1) => hx
  simpa only [Finset.sum_range_one] using sum_le f 1 hu us
#align evariation_on.edist_le eVariationOn.edist_le

theorem eq_zero_iff (f : Œ± ‚Üí E) {s : Set Œ±} :
    eVariationOn f s = 0 ‚Üî ‚àÄ x ‚àà s, ‚àÄ y ‚àà s, edist (f x) (f y) = 0 := by
  constructor
  ¬∑ rintro h x xs y ys
    rw [‚Üê le_zero_iff, ‚Üê h]
    exact edist_le f xs ys
  ¬∑ rintro h
    dsimp only [eVariationOn]
    rw [ENNReal.iSup_eq_zero]
    rintro ‚ü®n, u, um, us‚ü©
    exact Finset.sum_eq_zero fun i _ => h _ (us i.succ) _ (us i)
#align evariation_on.eq_zero_iff eVariationOn.eq_zero_iff

theorem constant_on {f : Œ± ‚Üí E} {s : Set Œ±} (hf : (f '' s).Subsingleton) :
    eVariationOn f s = 0 := by
  rw [eq_zero_iff]
  rintro x xs y ys
  rw [hf ‚ü®x, xs, rfl‚ü© ‚ü®y, ys, rfl‚ü©, edist_self]
#align evariation_on.constant_on eVariationOn.constant_on

@[simp]
protected theorem subsingleton (f : Œ± ‚Üí E) {s : Set Œ±} (hs : s.Subsingleton) :
    eVariationOn f s = 0 :=
  constant_on (hs.image f)
#align evariation_on.subsingleton eVariationOn.subsingleton

theorem lowerSemicontinuous_aux {Œπ : Type*} {F : Œπ ‚Üí Œ± ‚Üí E} {p : Filter Œπ} {f : Œ± ‚Üí E} {s : Set Œ±}
    (Ffs : ‚àÄ x ‚àà s, Tendsto (fun i => F i x) p (ùìù (f x))) {v : ‚Ñù‚â•0‚àû} (hv : v < eVariationOn f s) :
    ‚àÄ·∂† n : Œπ in p, v < eVariationOn (F n) s := by
  obtain ‚ü®‚ü®n, ‚ü®u, um, us‚ü©‚ü©, hlt‚ü© :
    ‚àÉ p : ‚Ñï √ó { u : ‚Ñï ‚Üí Œ± // Monotone u ‚àß ‚àÄ i, u i ‚àà s },
      v < ‚àë i ‚àà Finset.range p.1, edist (f ((p.2 : ‚Ñï ‚Üí Œ±) (i + 1))) (f ((p.2 : ‚Ñï ‚Üí Œ±) i)) :=
    lt_iSup_iff.mp hv
  have : Tendsto (fun j => ‚àë i ‚àà Finset.range n, edist (F j (u (i + 1))) (F j (u i))) p
      (ùìù (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i)))) := by
    apply tendsto_finset_sum
    exact fun i _ => Tendsto.edist (Ffs (u i.succ) (us i.succ)) (Ffs (u i) (us i))
  exact (eventually_gt_of_tendsto_gt hlt this).mono fun i h => h.trans_le (sum_le (F i) n um us)
#align evariation_on.lower_continuous_aux eVariationOn.lowerSemicontinuous_aux

/-- The map `(eVariationOn ¬∑ s)` is lower semicontinuous for pointwise convergence *on `s`*.
Pointwise convergence on `s` is encoded here as uniform convergence on the family consisting of the
singletons of elements of `s`.
-/
protected theorem lowerSemicontinuous (s : Set Œ±) :
    LowerSemicontinuous fun f : Œ± ‚Üí·µ§[s.image singleton] E => eVariationOn f s := fun f ‚Ü¶ by
  apply @lowerSemicontinuous_aux _ _ _ _ (UniformOnFun Œ± E (s.image singleton)) id (ùìù f) f s _
  simpa only [UniformOnFun.tendsto_iff_tendstoUniformlyOn, mem_image, forall_exists_index, and_imp,
    forall_apply_eq_imp_iff‚ÇÇ, tendstoUniformlyOn_singleton_iff_tendsto] using @tendsto_id _ (ùìù f)
#align evariation_on.lower_semicontinuous eVariationOn.lowerSemicontinuous

/-- The map `(eVariationOn ¬∑ s)` is lower semicontinuous for uniform convergence on `s`.  -/
theorem lowerSemicontinuous_uniformOn (s : Set Œ±) :
    LowerSemicontinuous fun f : Œ± ‚Üí·µ§[{s}] E => eVariationOn f s := fun f ‚Ü¶ by
  apply @lowerSemicontinuous_aux _ _ _ _ (UniformOnFun Œ± E {s}) id (ùìù f) f s _
  have := @tendsto_id _ (ùìù f)
  rw [UniformOnFun.tendsto_iff_tendstoUniformlyOn] at this
  simp_rw [‚Üê tendstoUniformlyOn_singleton_iff_tendsto]
  exact fun x xs => (this s rfl).mono (singleton_subset_iff.mpr xs)
#align evariation_on.lower_semicontinuous_uniform_on eVariationOn.lowerSemicontinuous_uniformOn

theorem _root_.BoundedVariationOn.dist_le {E : Type*} [PseudoMetricSpace E] {f : Œ± ‚Üí E}
    {s : Set Œ±} (h : BoundedVariationOn f s) {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s) :
    dist (f x) (f y) ‚â§ (eVariationOn f s).toReal := by
  rw [‚Üê ENNReal.ofReal_le_ofReal_iff ENNReal.toReal_nonneg, ENNReal.ofReal_toReal h, ‚Üê edist_dist]
  exact edist_le f hx hy
#align has_bounded_variation_on.dist_le BoundedVariationOn.dist_le

theorem _root_.BoundedVariationOn.sub_le {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} (h : BoundedVariationOn f s)
    {x y : Œ±} (hx : x ‚àà s) (hy : y ‚àà s) : f x - f y ‚â§ (eVariationOn f s).toReal := by
  apply (le_abs_self _).trans
  rw [‚Üê Real.dist_eq]
  exact h.dist_le hx hy
#align has_bounded_variation_on.sub_le BoundedVariationOn.sub_le

/-- Consider a monotone function `u` parameterizing some points of a set `s`. Given `x ‚àà s`, then
one can find another monotone function `v` parameterizing the same points as `u`, with `x` added.
In particular, the variation of a function along `u` is bounded by its variation along `v`. -/
theorem add_point (f : Œ± ‚Üí E) {s : Set Œ±} {x : Œ±} (hx : x ‚àà s) (u : ‚Ñï ‚Üí Œ±) (hu : Monotone u)
    (us : ‚àÄ i, u i ‚àà s) (n : ‚Ñï) :
    ‚àÉ (v : ‚Ñï ‚Üí Œ±) (m : ‚Ñï), Monotone v ‚àß (‚àÄ i, v i ‚àà s) ‚àß x ‚àà v '' Iio m ‚àß
      (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) ‚â§
        ‚àë j ‚àà Finset.range m, edist (f (v (j + 1))) (f (v j)) := by
  rcases le_or_lt (u n) x with (h | h)
  ¬∑ let v i := if i ‚â§ n then u i else x
    have vs : ‚àÄ i, v i ‚àà s := fun i ‚Ü¶ by
      simp only [v]
      split_ifs
      ¬∑ exact us i
      ¬∑ exact hx
    have hv : Monotone v := by
      refine monotone_nat_of_le_succ fun i => ?_
      simp only [v]
      rcases lt_trichotomy i n with (hi | rfl | hi)
      ¬∑ have : i + 1 ‚â§ n := Nat.succ_le_of_lt hi
        simp only [hi.le, this, if_true]
        exact hu (Nat.le_succ i)
      ¬∑ simp only [le_refl, if_true, add_le_iff_nonpos_right, Nat.le_zero, Nat.one_ne_zero,
          if_false, h]
      ¬∑ have A : ¬¨i ‚â§ n := hi.not_le
        have B : ¬¨i + 1 ‚â§ n := fun h => A (i.le_succ.trans h)
        simp only [A, B, if_false, le_rfl]
    refine ‚ü®v, n + 2, hv, vs, (mem_image _ _ _).2 ‚ü®n + 1, ?_, ?_‚ü©, ?_‚ü©
    ¬∑ rw [mem_Iio]; exact Nat.lt_succ_self (n + 1)
    ¬∑ have : ¬¨n + 1 ‚â§ n := Nat.not_succ_le_self n
      simp only [v, this, ite_eq_right_iff, IsEmpty.forall_iff]
    ¬∑ calc
        (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) =
            ‚àë i ‚àà Finset.range n, edist (f (v (i + 1))) (f (v i)) := by
          apply Finset.sum_congr rfl fun i hi => ?_
          simp only [Finset.mem_range] at hi
          have : i + 1 ‚â§ n := Nat.succ_le_of_lt hi
          simp only [v, hi.le, this, if_true]
        _ ‚â§ ‚àë j ‚àà Finset.range (n + 2), edist (f (v (j + 1))) (f (v j)) :=
          Finset.sum_le_sum_of_subset (Finset.range_mono (Nat.le_add_right n 2))
  have exists_N : ‚àÉ N, N ‚â§ n ‚àß x < u N := ‚ü®n, le_rfl, h‚ü©
  let N := Nat.find exists_N
  have hN : N ‚â§ n ‚àß x < u N := Nat.find_spec exists_N
  let w : ‚Ñï ‚Üí Œ± := fun i => if i < N then u i else if i = N then x else u (i - 1)
  have ws : ‚àÄ i, w i ‚àà s := by
    dsimp only [w]
    intro i
    split_ifs
    exacts [us _, hx, us _]
  have hw : Monotone w := by
    apply monotone_nat_of_le_succ fun i => ?_
    dsimp only [w]
    rcases lt_trichotomy (i + 1) N with (hi | hi | hi)
    ¬∑ have : i < N := Nat.lt_of_le_of_lt (Nat.le_succ i) hi
      simp only [hi, this, if_true]
      exact hu (Nat.le_succ _)
    ¬∑ have A : i < N := hi ‚ñ∏ i.lt_succ_self
      have B : ¬¨i + 1 < N := by rw [‚Üê hi]; exact fun h => h.ne rfl
      rw [if_pos A, if_neg B, if_pos hi]
      have T := Nat.find_min exists_N A
      push_neg at T
      exact T (A.le.trans hN.1)
    ¬∑ have A : ¬¨i < N := (Nat.lt_succ_iff.mp hi).not_lt
      have B : ¬¨i + 1 < N := hi.not_lt
      have C : ¬¨i + 1 = N := hi.ne.symm
      have D : i + 1 - 1 = i := Nat.pred_succ i
      rw [if_neg A, if_neg B, if_neg C, D]
      split_ifs
      ¬∑ exact hN.2.le.trans (hu (le_of_not_lt A))
      ¬∑ exact hu (Nat.pred_le _)
  refine ‚ü®w, n + 1, hw, ws, (mem_image _ _ _).2 ‚ü®N, hN.1.trans_lt (Nat.lt_succ_self n), ?_‚ü©, ?_‚ü©
  ¬∑ dsimp only [w]; rw [if_neg (lt_irrefl N), if_pos rfl]
  rcases eq_or_lt_of_le (zero_le N) with (Npos | Npos)
  ¬∑ calc
      (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) =
          ‚àë i ‚àà Finset.range n, edist (f (w (1 + i + 1))) (f (w (1 + i))) := by
        apply Finset.sum_congr rfl fun i _hi => ?_
        dsimp only [w]
        simp only [‚Üê Npos, Nat.not_lt_zero, Nat.add_succ_sub_one, add_zero, if_false,
          add_eq_zero_iff, Nat.one_ne_zero, false_and_iff, Nat.succ_add_sub_one, zero_add]
        rw [add_comm 1 i]
      _ = ‚àë i ‚àà Finset.Ico 1 (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        rw [Finset.range_eq_Ico]
        exact Finset.sum_Ico_add (fun i => edist (f (w (i + 1))) (f (w i))) 0 n 1
      _ ‚â§ ‚àë j ‚àà Finset.range (n + 1), edist (f (w (j + 1))) (f (w j)) := by
        apply Finset.sum_le_sum_of_subset _
        rw [Finset.range_eq_Ico]
        exact Finset.Ico_subset_Ico zero_le_one le_rfl
  ¬∑ calc
      (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) =
          ((‚àë i ‚àà Finset.Ico 0 (N - 1), edist (f (u (i + 1))) (f (u i))) +
              ‚àë i ‚àà Finset.Ico (N - 1) N, edist (f (u (i + 1))) (f (u i))) +
            ‚àë i ‚àà Finset.Ico N n, edist (f (u (i + 1))) (f (u i)) := by
        rw [Finset.sum_Ico_consecutive, Finset.sum_Ico_consecutive, Finset.range_eq_Ico]
        ¬∑ exact zero_le _
        ¬∑ exact hN.1
        ¬∑ exact zero_le _
        ¬∑ exact Nat.pred_le _
      _ = (‚àë i ‚àà Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              edist (f (u N)) (f (u (N - 1))) +
            ‚àë i ‚àà Finset.Ico N n, edist (f (w (1 + i + 1))) (f (w (1 + i))) := by
        congr 1
        ¬∑ congr 1
          ¬∑ apply Finset.sum_congr rfl fun i hi => ?_
            simp only [Finset.mem_Ico, zero_le', true_and_iff] at hi
            dsimp only [w]
            have A : i + 1 < N := Nat.lt_pred_iff.1 hi
            have B : i < N := Nat.lt_of_succ_lt A
            rw [if_pos A, if_pos B]
          ¬∑ have A : N - 1 + 1 = N := Nat.succ_pred_eq_of_pos Npos
            have : Finset.Ico (N - 1) N = {N - 1} := by rw [‚Üê Nat.Ico_succ_singleton, A]
            simp only [this, A, Finset.sum_singleton]
        ¬∑ apply Finset.sum_congr rfl fun i hi => ?_
          rw [Finset.mem_Ico] at hi
          dsimp only [w]
          have A : ¬¨1 + i + 1 < N := by omega
          have B : ¬¨1 + i + 1 = N := by omega
          have C : ¬¨1 + i < N := by omega
          have D : ¬¨1 + i = N := by omega
          rw [if_neg A, if_neg B, if_neg C, if_neg D]
          congr 3 <;> ¬∑ rw [add_comm, Nat.sub_one]; apply Nat.pred_succ
      _ = (‚àë i ‚àà Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              edist (f (w (N + 1))) (f (w (N - 1))) +
            ‚àë i ‚àà Finset.Ico (N + 1) (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        congr 1
        ¬∑ congr 1
          ¬∑ dsimp only [w]
            have A : ¬¨N + 1 < N := Nat.not_succ_lt_self
            have B : N - 1 < N := Nat.pred_lt Npos.ne'
            simp only [A, not_and, not_lt, Nat.succ_ne_self, Nat.add_succ_sub_one, add_zero,
              if_false, B, if_true]
        ¬∑ exact Finset.sum_Ico_add (fun i => edist (f (w (i + 1))) (f (w i))) N n 1
      _ ‚â§ ((‚àë i ‚àà Finset.Ico 0 (N - 1), edist (f (w (i + 1))) (f (w i))) +
              ‚àë i ‚àà Finset.Ico (N - 1) (N + 1), edist (f (w (i + 1))) (f (w i))) +
            ‚àë i ‚àà Finset.Ico (N + 1) (n + 1), edist (f (w (i + 1))) (f (w i)) := by
        refine add_le_add (add_le_add le_rfl ?_) le_rfl
        have A : N - 1 + 1 = N := Nat.succ_pred_eq_of_pos Npos
        have B : N - 1 + 1 < N + 1 := A.symm ‚ñ∏ N.lt_succ_self
        have C : N - 1 < N + 1 := lt_of_le_of_lt N.pred_le N.lt_succ_self
        rw [Finset.sum_eq_sum_Ico_succ_bot C, Finset.sum_eq_sum_Ico_succ_bot B, A, Finset.Ico_self,
          Finset.sum_empty, add_zero, add_comm (edist _ _)]
        exact edist_triangle _ _ _
      _ = ‚àë j ‚àà Finset.range (n + 1), edist (f (w (j + 1))) (f (w j)) := by
        rw [Finset.sum_Ico_consecutive, Finset.sum_Ico_consecutive, Finset.range_eq_Ico]
        ¬∑ exact zero_le _
        ¬∑ exact Nat.succ_le_succ hN.left
        ¬∑ exact zero_le _
        ¬∑ exact N.pred_le.trans N.le_succ
#align evariation_on.add_point eVariationOn.add_point

/-- The variation of a function on the union of two sets `s` and `t`, with `s` to the left of `t`,
bounds the sum of the variations along `s` and `t`. -/
theorem add_le_union (f : Œ± ‚Üí E) {s t : Set Œ±} (h : ‚àÄ x ‚àà s, ‚àÄ y ‚àà t, x ‚â§ y) :
    eVariationOn f s + eVariationOn f t ‚â§ eVariationOn f (s ‚à™ t) := by
  by_cases hs : s = ‚àÖ
  ¬∑ simp [hs]
  have : Nonempty { u // Monotone u ‚àß ‚àÄ i : ‚Ñï, u i ‚àà s } :=
    nonempty_monotone_mem (nonempty_iff_ne_empty.2 hs)
  by_cases ht : t = ‚àÖ
  ¬∑ simp [ht]
  have : Nonempty { u // Monotone u ‚àß ‚àÄ i : ‚Ñï, u i ‚àà t } :=
    nonempty_monotone_mem (nonempty_iff_ne_empty.2 ht)
  refine ENNReal.iSup_add_iSup_le ?_
  /- We start from two sequences `u` and `v` along `s` and `t` respectively, and we build a new
    sequence `w` along `s ‚à™ t` by juxtaposing them. Its variation is larger than the sum of the
    variations. -/
  rintro ‚ü®n, ‚ü®u, hu, us‚ü©‚ü© ‚ü®m, ‚ü®v, hv, vt‚ü©‚ü©
  let w i := if i ‚â§ n then u i else v (i - (n + 1))
  have wst : ‚àÄ i, w i ‚àà s ‚à™ t := by
    intro i
    by_cases hi : i ‚â§ n
    ¬∑ simp [w, hi, us]
    ¬∑ simp [w, hi, vt]
  have hw : Monotone w := by
    intro i j hij
    dsimp only [w]
    split_ifs with h_1 h_2 h_2
    ¬∑ exact hu hij
    ¬∑ apply h _ (us _) _ (vt _)
    ¬∑ exfalso; exact h_1 (hij.trans h_2)
    ¬∑ apply hv (tsub_le_tsub hij le_rfl)
  calc
    ((‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) +
          ‚àë i ‚àà Finset.range m, edist (f (v (i + 1))) (f (v i))) =
        (‚àë i ‚àà Finset.range n, edist (f (w (i + 1))) (f (w i))) +
          ‚àë i ‚àà Finset.range m, edist (f (w (n + 1 + i + 1))) (f (w (n + 1 + i))) := by
      dsimp only [w]
      congr 1
      ¬∑ refine Finset.sum_congr rfl fun i hi => ?_
        simp only [Finset.mem_range] at hi
        have : i + 1 ‚â§ n := Nat.succ_le_of_lt hi
        simp [hi.le, this]
      ¬∑ refine Finset.sum_congr rfl fun i hi => ?_
        simp only [Finset.mem_range] at hi
        have B : ¬¨n + 1 + i ‚â§ n := by omega
        have A : ¬¨n + 1 + i + 1 ‚â§ n := fun h => B ((n + 1 + i).le_succ.trans h)
        have C : n + 1 + i - n = i + 1 := by
          rw [tsub_eq_iff_eq_add_of_le]
          ¬∑ abel
          ¬∑ exact n.le_succ.trans (n.succ.le_add_right i)
        simp only [A, B, C, Nat.succ_sub_succ_eq_sub, if_false, add_tsub_cancel_left]
    _ = (‚àë i ‚àà Finset.range n, edist (f (w (i + 1))) (f (w i))) +
          ‚àë i ‚àà Finset.Ico (n + 1) (n + 1 + m), edist (f (w (i + 1))) (f (w i)) := by
      congr 1
      rw [Finset.range_eq_Ico]
      convert Finset.sum_Ico_add (fun i : ‚Ñï => edist (f (w (i + 1))) (f (w i))) 0 m (n + 1)
        using 3 <;> abel
    _ ‚â§ ‚àë i ‚àà Finset.range (n + 1 + m), edist (f (w (i + 1))) (f (w i)) := by
      rw [‚Üê Finset.sum_union]
      ¬∑ apply Finset.sum_le_sum_of_subset _
        rintro i hi
        simp only [Finset.mem_union, Finset.mem_range, Finset.mem_Ico] at hi ‚ä¢
        cases' hi with hi hi
        ¬∑ exact lt_of_lt_of_le hi (n.le_succ.trans (n.succ.le_add_right m))
        ¬∑ exact hi.2
      ¬∑ refine Finset.disjoint_left.2 fun i hi h'i => ?_
        simp only [Finset.mem_Ico, Finset.mem_range] at hi h'i
        exact hi.not_lt (Nat.lt_of_succ_le h'i.left)
    _ ‚â§ eVariationOn f (s ‚à™ t) := sum_le f _ hw wst
#align evariation_on.add_le_union eVariationOn.add_le_union

/-- If a set `s` is to the left of a set `t`, and both contain the boundary point `x`, then
the variation of `f` along `s ‚à™ t` is the sum of the variations. -/
theorem union (f : Œ± ‚Üí E) {s t : Set Œ±} {x : Œ±} (hs : IsGreatest s x) (ht : IsLeast t x) :
    eVariationOn f (s ‚à™ t) = eVariationOn f s + eVariationOn f t := by
  classical
  apply le_antisymm _ (eVariationOn.add_le_union f fun a ha b hb => le_trans (hs.2 ha) (ht.2 hb))
  apply iSup_le _
  rintro ‚ü®n, ‚ü®u, hu, ust‚ü©‚ü©
  obtain ‚ü®v, m, hv, vst, xv, huv‚ü© : ‚àÉ (v : ‚Ñï ‚Üí Œ±) (m : ‚Ñï),
    Monotone v ‚àß (‚àÄ i, v i ‚àà s ‚à™ t) ‚àß x ‚àà v '' Iio m ‚àß
      (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) ‚â§
        ‚àë j ‚àà Finset.range m, edist (f (v (j + 1))) (f (v j)) :=
    eVariationOn.add_point f (mem_union_left t hs.1) u hu ust n
  obtain ‚ü®N, hN, Nx‚ü© : ‚àÉ N, N < m ‚àß v N = x := xv
  calc
    (‚àë j ‚àà Finset.range n, edist (f (u (j + 1))) (f (u j))) ‚â§
        ‚àë j ‚àà Finset.range m, edist (f (v (j + 1))) (f (v j)) :=
      huv
    _ = (‚àë j ‚àà Finset.Ico 0 N, edist (f (v (j + 1))) (f (v j))) +
          ‚àë j ‚àà Finset.Ico N m, edist (f (v (j + 1))) (f (v j)) := by
      rw [Finset.range_eq_Ico, Finset.sum_Ico_consecutive _ (zero_le _) hN.le]
    _ ‚â§ eVariationOn f s + eVariationOn f t := by
      refine add_le_add ?_ ?_
      ¬∑ apply sum_le_of_monotoneOn_Icc _ (hv.monotoneOn _) fun i hi => ?_
        rcases vst i with (h | h); ¬∑ exact h
        have : v i = x := by
          apply le_antisymm
          ¬∑ rw [‚Üê Nx]; exact hv hi.2
          ¬∑ exact ht.2 h
        rw [this]
        exact hs.1
      ¬∑ apply sum_le_of_monotoneOn_Icc _ (hv.monotoneOn _) fun i hi => ?_
        rcases vst i with (h | h); swap; ¬∑ exact h
        have : v i = x := by
          apply le_antisymm
          ¬∑ exact hs.2 h
          ¬∑ rw [‚Üê Nx]; exact hv hi.1
        rw [this]
        exact ht.1
#align evariation_on.union eVariationOn.union

theorem Icc_add_Icc (f : Œ± ‚Üí E) {s : Set Œ±} {a b c : Œ±} (hab : a ‚â§ b) (hbc : b ‚â§ c) (hb : b ‚àà s) :
    eVariationOn f (s ‚à© Icc a b) + eVariationOn f (s ‚à© Icc b c) = eVariationOn f (s ‚à© Icc a c) := by
  have A : IsGreatest (s ‚à© Icc a b) b :=
    ‚ü®‚ü®hb, hab, le_rfl‚ü©, (inter_subset_right _ _).trans Icc_subset_Iic_self‚ü©
  have B : IsLeast (s ‚à© Icc b c) b :=
    ‚ü®‚ü®hb, le_rfl, hbc‚ü©, (inter_subset_right _ _).trans Icc_subset_Ici_self‚ü©
  rw [‚Üê eVariationOn.union f A B, ‚Üê inter_union_distrib_left, Icc_union_Icc_eq_Icc hab hbc]
#align evariation_on.Icc_add_Icc eVariationOn.Icc_add_Icc

section Monotone

variable {Œ≤ : Type*} [LinearOrder Œ≤]

theorem comp_le_of_monotoneOn (f : Œ± ‚Üí E) {s : Set Œ±} {t : Set Œ≤} (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : MonotoneOn œÜ t)
    (œÜst : MapsTo œÜ t s) : eVariationOn (f ‚àò œÜ) t ‚â§ eVariationOn f s :=
  iSup_le fun ‚ü®n, u, hu, ut‚ü© =>
    le_iSup_of_le ‚ü®n, œÜ ‚àò u, fun x y xy => hœÜ (ut x) (ut y) (hu xy), fun i => œÜst (ut i)‚ü© le_rfl
#align evariation_on.comp_le_of_monotone_on eVariationOn.comp_le_of_monotoneOn

theorem comp_le_of_antitoneOn (f : Œ± ‚Üí E) {s : Set Œ±} {t : Set Œ≤} (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : AntitoneOn œÜ t)
    (œÜst : MapsTo œÜ t s) : eVariationOn (f ‚àò œÜ) t ‚â§ eVariationOn f s := by
  refine iSup_le ?_
  rintro ‚ü®n, u, hu, ut‚ü©
  rw [‚Üê Finset.sum_range_reflect]
  refine (Finset.sum_congr rfl fun x hx => ?_).trans_le <| le_iSup_of_le
    ‚ü®n, fun i => œÜ (u <| n - i), fun x y xy => hœÜ (ut _) (ut _) (hu <| Nat.sub_le_sub_left xy n),
      fun i => œÜst (ut _)‚ü©
    le_rfl
  rw [Finset.mem_range] at hx
  dsimp only [Subtype.coe_mk, Function.comp_apply]
  rw [edist_comm]
  congr 4 <;> omega
#align evariation_on.comp_le_of_antitone_on eVariationOn.comp_le_of_antitoneOn

theorem comp_eq_of_monotoneOn (f : Œ± ‚Üí E) {t : Set Œ≤} (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : MonotoneOn œÜ t) :
    eVariationOn (f ‚àò œÜ) t = eVariationOn f (œÜ '' t) := by
  apply le_antisymm (comp_le_of_monotoneOn f œÜ hœÜ (mapsTo_image œÜ t))
  cases isEmpty_or_nonempty Œ≤
  ¬∑ convert zero_le (_ : ‚Ñù‚â•0‚àû)
    exact eVariationOn.subsingleton f <|
      (subsingleton_of_subsingleton.image _).anti (surjOn_image œÜ t)
  let œà := œÜ.invFunOn t
  have œàœÜs : EqOn (œÜ ‚àò œà) id (œÜ '' t) := (surjOn_image œÜ t).rightInvOn_invFunOn
  have œàts : MapsTo œà (œÜ '' t) t := (surjOn_image œÜ t).mapsTo_invFunOn
  have hœà : MonotoneOn œà (œÜ '' t) := Function.monotoneOn_of_rightInvOn_of_mapsTo hœÜ œàœÜs œàts
  change eVariationOn (f ‚àò id) (œÜ '' t) ‚â§ eVariationOn (f ‚àò œÜ) t
  rw [‚Üê eq_of_eqOn (œàœÜs.comp_left : EqOn (f ‚àò œÜ ‚àò œà) (f ‚àò id) (œÜ '' t))]
  exact comp_le_of_monotoneOn _ œà hœà œàts
#align evariation_on.comp_eq_of_monotone_on eVariationOn.comp_eq_of_monotoneOn

theorem comp_inter_Icc_eq_of_monotoneOn (f : Œ± ‚Üí E) {t : Set Œ≤} (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : MonotoneOn œÜ t)
    {x y : Œ≤} (hx : x ‚àà t) (hy : y ‚àà t) :
    eVariationOn (f ‚àò œÜ) (t ‚à© Icc x y) = eVariationOn f (œÜ '' t ‚à© Icc (œÜ x) (œÜ y)) := by
  rcases le_total x y with (h | h)
  ¬∑ convert comp_eq_of_monotoneOn f œÜ (hœÜ.mono (Set.inter_subset_left t (Icc x y)))
    apply le_antisymm
    ¬∑ rintro _ ‚ü®‚ü®u, us, rfl‚ü©, vœÜx, vœÜy‚ü©
      rcases le_total x u with (xu | ux)
      ¬∑ rcases le_total u y with (uy | yu)
        ¬∑ exact ‚ü®u, ‚ü®us, ‚ü®xu, uy‚ü©‚ü©, rfl‚ü©
        ¬∑ rw [le_antisymm vœÜy (hœÜ hy us yu)]
          exact ‚ü®y, ‚ü®hy, ‚ü®h, le_rfl‚ü©‚ü©, rfl‚ü©
      ¬∑ rw [‚Üê le_antisymm vœÜx (hœÜ us hx ux)]
        exact ‚ü®x, ‚ü®hx, ‚ü®le_rfl, h‚ü©‚ü©, rfl‚ü©
    ¬∑ rintro _ ‚ü®u, ‚ü®‚ü®hu, xu, uy‚ü©, rfl‚ü©‚ü©
      exact ‚ü®‚ü®u, hu, rfl‚ü©, ‚ü®hœÜ hx hu xu, hœÜ hu hy uy‚ü©‚ü©
  ¬∑ rw [eVariationOn.subsingleton, eVariationOn.subsingleton]
    exacts [(Set.subsingleton_Icc_of_ge (hœÜ hy hx h)).anti (Set.inter_subset_right _ _),
      (Set.subsingleton_Icc_of_ge h).anti (Set.inter_subset_right _ _)]
#align evariation_on.comp_inter_Icc_eq_of_monotone_on eVariationOn.comp_inter_Icc_eq_of_monotoneOn

theorem comp_eq_of_antitoneOn (f : Œ± ‚Üí E) {t : Set Œ≤} (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : AntitoneOn œÜ t) :
    eVariationOn (f ‚àò œÜ) t = eVariationOn f (œÜ '' t) := by
  apply le_antisymm (comp_le_of_antitoneOn f œÜ hœÜ (mapsTo_image œÜ t))
  cases isEmpty_or_nonempty Œ≤
  ¬∑ convert zero_le (_ : ‚Ñù‚â•0‚àû)
    exact eVariationOn.subsingleton f <| (subsingleton_of_subsingleton.image _).anti
      (surjOn_image œÜ t)
  let œà := œÜ.invFunOn t
  have œàœÜs : EqOn (œÜ ‚àò œà) id (œÜ '' t) := (surjOn_image œÜ t).rightInvOn_invFunOn
  have œàts := (surjOn_image œÜ t).mapsTo_invFunOn
  have hœà : AntitoneOn œà (œÜ '' t) := Function.antitoneOn_of_rightInvOn_of_mapsTo hœÜ œàœÜs œàts
  change eVariationOn (f ‚àò id) (œÜ '' t) ‚â§ eVariationOn (f ‚àò œÜ) t
  rw [‚Üê eq_of_eqOn (œàœÜs.comp_left : EqOn (f ‚àò œÜ ‚àò œà) (f ‚àò id) (œÜ '' t))]
  exact comp_le_of_antitoneOn _ œà hœà œàts
#align evariation_on.comp_eq_of_antitone_on eVariationOn.comp_eq_of_antitoneOn

open OrderDual

theorem comp_ofDual (f : Œ± ‚Üí E) (s : Set Œ±) :
    eVariationOn (f ‚àò ofDual) (ofDual ‚Åª¬π' s) = eVariationOn f s := by
  convert comp_eq_of_antitoneOn f ofDual fun _ _ _ _ => id
  simp only [Equiv.image_preimage]
#align evariation_on.comp_of_dual eVariationOn.comp_ofDual

end Monotone

end eVariationOn

/-! ## Monotone functions and bounded variation -/

theorem MonotoneOn.eVariationOn_le {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} (hf : MonotoneOn f s) {a b : Œ±}
    (as : a ‚àà s) (bs : b ‚àà s) : eVariationOn f (s ‚à© Icc a b) ‚â§ ENNReal.ofReal (f b - f a) := by
  apply iSup_le _
  rintro ‚ü®n, ‚ü®u, hu, us‚ü©‚ü©
  calc
    (‚àë i ‚àà Finset.range n, edist (f (u (i + 1))) (f (u i))) =
        ‚àë i ‚àà Finset.range n, ENNReal.ofReal (f (u (i + 1)) - f (u i)) := by
      refine Finset.sum_congr rfl fun i hi => ?_
      simp only [Finset.mem_range] at hi
      rw [edist_dist, Real.dist_eq, abs_of_nonneg]
      exact sub_nonneg_of_le (hf (us i).1 (us (i + 1)).1 (hu (Nat.le_succ _)))
    _ = ENNReal.ofReal (‚àë i ‚àà Finset.range n, (f (u (i + 1)) - f (u i))) := by
      rw [ENNReal.ofReal_sum_of_nonneg]
      intro i _
      exact sub_nonneg_of_le (hf (us i).1 (us (i + 1)).1 (hu (Nat.le_succ _)))
    _ = ENNReal.ofReal (f (u n) - f (u 0)) := by rw [Finset.sum_range_sub fun i => f (u i)]
    _ ‚â§ ENNReal.ofReal (f b - f a) := by
      apply ENNReal.ofReal_le_ofReal
      exact sub_le_sub (hf (us n).1 bs (us n).2.2) (hf as (us 0).1 (us 0).2.1)
#align monotone_on.evariation_on_le MonotoneOn.eVariationOn_le

theorem MonotoneOn.locallyBoundedVariationOn {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} (hf : MonotoneOn f s) :
    LocallyBoundedVariationOn f s := fun _ _ as bs =>
  ((hf.eVariationOn_le as bs).trans_lt ENNReal.ofReal_lt_top).ne
#align monotone_on.has_locally_bounded_variation_on MonotoneOn.locallyBoundedVariationOn

/-- The **signed** variation of `f` on the interval `Icc a b` intersected with the set `s`,
squashed to a real (therefore only really meaningful if the variation is finite)
-/
noncomputable def variationOnFromTo (f : Œ± ‚Üí E) (s : Set Œ±) (a b : Œ±) : ‚Ñù :=
  if a ‚â§ b then (eVariationOn f (s ‚à© Icc a b)).toReal else -(eVariationOn f (s ‚à© Icc b a)).toReal
#align variation_on_from_to variationOnFromTo

namespace variationOnFromTo

variable (f : Œ± ‚Üí E) (s : Set Œ±)

protected theorem self (a : Œ±) : variationOnFromTo f s a a = 0 := by
  dsimp only [variationOnFromTo]
  rw [if_pos le_rfl, Icc_self, eVariationOn.subsingleton, ENNReal.zero_toReal]
  exact fun x hx y hy => hx.2.trans hy.2.symm
#align variation_on_from_to.self variationOnFromTo.self

protected theorem nonneg_of_le {a b : Œ±} (h : a ‚â§ b) : 0 ‚â§ variationOnFromTo f s a b := by
  simp only [variationOnFromTo, if_pos h, ENNReal.toReal_nonneg]
#align variation_on_from_to.nonneg_of_le variationOnFromTo.nonneg_of_le

protected theorem eq_neg_swap (a b : Œ±) :
    variationOnFromTo f s a b = -variationOnFromTo f s b a := by
  rcases lt_trichotomy a b with (ab | rfl | ba)
  ¬∑ simp only [variationOnFromTo, if_pos ab.le, if_neg ab.not_le, neg_neg]
  ¬∑ simp only [variationOnFromTo.self, neg_zero]
  ¬∑ simp only [variationOnFromTo, if_pos ba.le, if_neg ba.not_le, neg_neg]
#align variation_on_from_to.eq_neg_swap variationOnFromTo.eq_neg_swap

protected theorem nonpos_of_ge {a b : Œ±} (h : b ‚â§ a) : variationOnFromTo f s a b ‚â§ 0 := by
  rw [variationOnFromTo.eq_neg_swap]
  exact neg_nonpos_of_nonneg (variationOnFromTo.nonneg_of_le f s h)
#align variation_on_from_to.nonpos_of_ge variationOnFromTo.nonpos_of_ge

protected theorem eq_of_le {a b : Œ±} (h : a ‚â§ b) :
    variationOnFromTo f s a b = (eVariationOn f (s ‚à© Icc a b)).toReal :=
  if_pos h
#align variation_on_from_to.eq_of_le variationOnFromTo.eq_of_le

protected theorem eq_of_ge {a b : Œ±} (h : b ‚â§ a) :
    variationOnFromTo f s a b = -(eVariationOn f (s ‚à© Icc b a)).toReal := by
  rw [variationOnFromTo.eq_neg_swap, neg_inj, variationOnFromTo.eq_of_le f s h]
#align variation_on_from_to.eq_of_ge variationOnFromTo.eq_of_ge

protected theorem add {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s) {a b c : Œ±}
    (ha : a ‚àà s) (hb : b ‚àà s) (hc : c ‚àà s) :
    variationOnFromTo f s a b + variationOnFromTo f s b c = variationOnFromTo f s a c := by
  symm
  refine additive_of_isTotal ((¬∑ : Œ±) ‚â§ ¬∑) (variationOnFromTo f s) (¬∑ ‚àà s) ?_ ?_ ha hb hc
  ¬∑ rintro x y _xs _ys
    simp only [variationOnFromTo.eq_neg_swap f s y x, Subtype.coe_mk, add_right_neg,
      forall_true_left]
  ¬∑ rintro x y z xy yz xs ys zs
    rw [variationOnFromTo.eq_of_le f s xy, variationOnFromTo.eq_of_le f s yz,
      variationOnFromTo.eq_of_le f s (xy.trans yz),
      ‚Üê ENNReal.toReal_add (hf x y xs ys) (hf y z ys zs), eVariationOn.Icc_add_Icc f xy yz ys]
#align variation_on_from_to.add variationOnFromTo.add

protected theorem edist_zero_of_eq_zero {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s)
    {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) (h : variationOnFromTo f s a b = 0) :
    edist (f a) (f b) = 0 := by
  wlog h' : a ‚â§ b
  ¬∑ rw [edist_comm]
    apply this f s hf hb ha _ (le_of_not_le h')
    rw [variationOnFromTo.eq_neg_swap, h, neg_zero]
  ¬∑ apply le_antisymm _ (zero_le _)
    rw [‚Üê ENNReal.ofReal_zero, ‚Üê h, variationOnFromTo.eq_of_le f s h',
      ENNReal.ofReal_toReal (hf a b ha hb)]
    apply eVariationOn.edist_le
    exacts [‚ü®ha, ‚ü®le_rfl, h'‚ü©‚ü©, ‚ü®hb, ‚ü®h', le_rfl‚ü©‚ü©]
#align variation_on_from_to.edist_zero_of_eq_zero variationOnFromTo.edist_zero_of_eq_zero

protected theorem eq_left_iff {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s)
    {a b c : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) (hc : c ‚àà s) :
    variationOnFromTo f s a b = variationOnFromTo f s a c ‚Üî variationOnFromTo f s b c = 0 := by
  simp only [‚Üê variationOnFromTo.add hf ha hb hc, self_eq_add_right]
#align variation_on_from_to.eq_left_iff variationOnFromTo.eq_left_iff

protected theorem eq_zero_iff_of_le {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s)
    {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) (ab : a ‚â§ b) :
    variationOnFromTo f s a b = 0 ‚Üî
      ‚àÄ ‚¶Éx‚¶Ñ (_hx : x ‚àà s ‚à© Icc a b) ‚¶Éy‚¶Ñ (_hy : y ‚àà s ‚à© Icc a b), edist (f x) (f y) = 0 := by
  rw [variationOnFromTo.eq_of_le _ _ ab, ENNReal.toReal_eq_zero_iff, or_iff_left (hf a b ha hb),
    eVariationOn.eq_zero_iff]
#align variation_on_from_to.eq_zero_iff_of_le variationOnFromTo.eq_zero_iff_of_le

protected theorem eq_zero_iff_of_ge {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s)
    {a b : Œ±} (ha : a ‚àà s) (hb : b ‚àà s) (ba : b ‚â§ a) :
    variationOnFromTo f s a b = 0 ‚Üî
      ‚àÄ ‚¶Éx‚¶Ñ (_hx : x ‚àà s ‚à© Icc b a) ‚¶Éy‚¶Ñ (_hy : y ‚àà s ‚à© Icc b a), edist (f x) (f y) = 0 := by
  rw [variationOnFromTo.eq_of_ge _ _ ba, neg_eq_zero, ENNReal.toReal_eq_zero_iff,
    or_iff_left (hf b a hb ha), eVariationOn.eq_zero_iff]
#align variation_on_from_to.eq_zero_iff_of_ge variationOnFromTo.eq_zero_iff_of_ge

protected theorem eq_zero_iff {f : Œ± ‚Üí E} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s) {a b : Œ±}
    (ha : a ‚àà s) (hb : b ‚àà s) :
    variationOnFromTo f s a b = 0 ‚Üî
      ‚àÄ ‚¶Éx‚¶Ñ (_hx : x ‚àà s ‚à© uIcc a b) ‚¶Éy‚¶Ñ (_hy : y ‚àà s ‚à© uIcc a b), edist (f x) (f y) = 0 := by
  rcases le_total a b with (ab | ba)
  ¬∑ rw [uIcc_of_le ab]
    exact variationOnFromTo.eq_zero_iff_of_le hf ha hb ab
  ¬∑ rw [uIcc_of_ge ba]
    exact variationOnFromTo.eq_zero_iff_of_ge hf ha hb ba
#align variation_on_from_to.eq_zero_iff variationOnFromTo.eq_zero_iff

variable {f} {s}

protected theorem monotoneOn (hf : LocallyBoundedVariationOn f s) {a : Œ±} (as : a ‚àà s) :
    MonotoneOn (variationOnFromTo f s a) s := by
  rintro b bs c cs bc
  rw [‚Üê variationOnFromTo.add hf as bs cs]
  exact le_add_of_nonneg_right (variationOnFromTo.nonneg_of_le f s bc)
#align variation_on_from_to.monotone_on variationOnFromTo.monotoneOn

protected theorem antitoneOn (hf : LocallyBoundedVariationOn f s) {b : Œ±} (bs : b ‚àà s) :
    AntitoneOn (fun a => variationOnFromTo f s a b) s := by
  rintro a as c cs ac
  dsimp only
  rw [‚Üê variationOnFromTo.add hf as cs bs]
  exact le_add_of_nonneg_left (variationOnFromTo.nonneg_of_le f s ac)
#align variation_on_from_to.antitone_on variationOnFromTo.antitoneOn

protected theorem sub_self_monotoneOn {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±} (hf : LocallyBoundedVariationOn f s)
    {a : Œ±} (as : a ‚àà s) : MonotoneOn (variationOnFromTo f s a - f) s := by
  rintro b bs c cs bc
  rw [Pi.sub_apply, Pi.sub_apply, le_sub_iff_add_le, add_comm_sub, ‚Üê le_sub_iff_add_le']
  calc
    f c - f b ‚â§ |f c - f b| := le_abs_self _
    _ = dist (f b) (f c) := by rw [dist_comm, Real.dist_eq]
    _ ‚â§ variationOnFromTo f s b c := by
      rw [variationOnFromTo.eq_of_le f s bc, dist_edist]
      apply ENNReal.toReal_mono (hf b c bs cs)
      apply eVariationOn.edist_le f
      exacts [‚ü®bs, le_rfl, bc‚ü©, ‚ü®cs, bc, le_rfl‚ü©]
    _ = variationOnFromTo f s a c - variationOnFromTo f s a b := by
      rw [‚Üê variationOnFromTo.add hf as bs cs, add_sub_cancel_left]

#align variation_on_from_to.sub_self_monotone_on variationOnFromTo.sub_self_monotoneOn

protected theorem comp_eq_of_monotoneOn {Œ≤ : Type*} [LinearOrder Œ≤] (f : Œ± ‚Üí E) {t : Set Œ≤}
    (œÜ : Œ≤ ‚Üí Œ±) (hœÜ : MonotoneOn œÜ t) {x y : Œ≤} (hx : x ‚àà t) (hy : y ‚àà t) :
    variationOnFromTo (f ‚àò œÜ) t x y = variationOnFromTo f (œÜ '' t) (œÜ x) (œÜ y) := by
  rcases le_total x y with (h | h)
  ¬∑ rw [variationOnFromTo.eq_of_le _ _ h, variationOnFromTo.eq_of_le _ _ (hœÜ hx hy h),
      eVariationOn.comp_inter_Icc_eq_of_monotoneOn f œÜ hœÜ hx hy]
  ¬∑ rw [variationOnFromTo.eq_of_ge _ _ h, variationOnFromTo.eq_of_ge _ _ (hœÜ hy hx h),
      eVariationOn.comp_inter_Icc_eq_of_monotoneOn f œÜ hœÜ hy hx]
#align variation_on_from_to.comp_eq_of_monotone_on variationOnFromTo.comp_eq_of_monotoneOn

end variationOnFromTo

/-- If a real valued function has bounded variation on a set, then it is a difference of monotone
functions there. -/
theorem LocallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn {f : Œ± ‚Üí ‚Ñù} {s : Set Œ±}
    (h : LocallyBoundedVariationOn f s) :
    ‚àÉ p q : Œ± ‚Üí ‚Ñù, MonotoneOn p s ‚àß MonotoneOn q s ‚àß f = p - q := by
  rcases eq_empty_or_nonempty s with (rfl | ‚ü®c, cs‚ü©)
  ¬∑ exact ‚ü®f, 0, subsingleton_empty.monotoneOn _, subsingleton_empty.monotoneOn _,
      (sub_zero f).symm‚ü©
  ¬∑ exact ‚ü®_, _, variationOnFromTo.monotoneOn h cs, variationOnFromTo.sub_self_monotoneOn h cs,
      (sub_sub_cancel _ _).symm‚ü©
#align has_locally_bounded_variation_on.exists_monotone_on_sub_monotone_on LocallyBoundedVariationOn.exists_monotoneOn_sub_monotoneOn

/-! ## Lipschitz functions and bounded variation -/

section LipschitzOnWith

variable {F : Type*} [PseudoEMetricSpace F]

theorem LipschitzOnWith.comp_eVariationOn_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} {t : Set E}
    (h : LipschitzOnWith C f t) {g : Œ± ‚Üí E} {s : Set Œ±} (hg : MapsTo g s t) :
    eVariationOn (f ‚àò g) s ‚â§ C * eVariationOn g s := by
  apply iSup_le _
  rintro ‚ü®n, ‚ü®u, hu, us‚ü©‚ü©
  calc
    (‚àë i ‚àà Finset.range n, edist (f (g (u (i + 1)))) (f (g (u i)))) ‚â§
        ‚àë i ‚àà Finset.range n, C * edist (g (u (i + 1))) (g (u i)) :=
      Finset.sum_le_sum fun i _ => h (hg (us _)) (hg (us _))
    _ = C * ‚àë i ‚àà Finset.range n, edist (g (u (i + 1))) (g (u i)) := by rw [Finset.mul_sum]
    _ ‚â§ C * eVariationOn g s := mul_le_mul_left' (eVariationOn.sum_le _ _ hu us) _
#align lipschitz_on_with.comp_evariation_on_le LipschitzOnWith.comp_eVariationOn_le

theorem LipschitzOnWith.comp_boundedVariationOn {f : E ‚Üí F} {C : ‚Ñù‚â•0} {t : Set E}
    (hf : LipschitzOnWith C f t) {g : Œ± ‚Üí E} {s : Set Œ±} (hg : MapsTo g s t)
    (h : BoundedVariationOn g s) : BoundedVariationOn (f ‚àò g) s :=
  ne_top_of_le_ne_top (ENNReal.mul_ne_top ENNReal.coe_ne_top h) (hf.comp_eVariationOn_le hg)
#align lipschitz_on_with.comp_has_bounded_variation_on LipschitzOnWith.comp_boundedVariationOn

theorem LipschitzOnWith.comp_locallyBoundedVariationOn {f : E ‚Üí F} {C : ‚Ñù‚â•0} {t : Set E}
    (hf : LipschitzOnWith C f t) {g : Œ± ‚Üí E} {s : Set Œ±} (hg : MapsTo g s t)
    (h : LocallyBoundedVariationOn g s) : LocallyBoundedVariationOn (f ‚àò g) s :=
  fun x y xs ys =>
  hf.comp_boundedVariationOn (hg.mono_left (inter_subset_left _ _)) (h x y xs ys)
#align lipschitz_on_with.comp_has_locally_bounded_variation_on LipschitzOnWith.comp_locallyBoundedVariationOn

theorem LipschitzWith.comp_boundedVariationOn {f : E ‚Üí F} {C : ‚Ñù‚â•0} (hf : LipschitzWith C f)
    {g : Œ± ‚Üí E} {s : Set Œ±} (h : BoundedVariationOn g s) : BoundedVariationOn (f ‚àò g) s :=
  (hf.lipschitzOnWith univ).comp_boundedVariationOn (mapsTo_univ _ _) h
#align lipschitz_with.comp_has_bounded_variation_on LipschitzWith.comp_boundedVariationOn

theorem LipschitzWith.comp_locallyBoundedVariationOn {f : E ‚Üí F} {C : ‚Ñù‚â•0}
    (hf : LipschitzWith C f) {g : Œ± ‚Üí E} {s : Set Œ±} (h : LocallyBoundedVariationOn g s) :
    LocallyBoundedVariationOn (f ‚àò g) s :=
  (hf.lipschitzOnWith univ).comp_locallyBoundedVariationOn (mapsTo_univ _ _) h
#align lipschitz_with.comp_has_locally_bounded_variation_on LipschitzWith.comp_locallyBoundedVariationOn

theorem LipschitzOnWith.locallyBoundedVariationOn {f : ‚Ñù ‚Üí E} {C : ‚Ñù‚â•0} {s : Set ‚Ñù}
    (hf : LipschitzOnWith C f s) : LocallyBoundedVariationOn f s :=
  hf.comp_locallyBoundedVariationOn (mapsTo_id _)
    (@monotoneOn_id ‚Ñù _ s).locallyBoundedVariationOn
#align lipschitz_on_with.has_locally_bounded_variation_on LipschitzOnWith.locallyBoundedVariationOn

theorem LipschitzWith.locallyBoundedVariationOn {f : ‚Ñù ‚Üí E} {C : ‚Ñù‚â•0} (hf : LipschitzWith C f)
    (s : Set ‚Ñù) : LocallyBoundedVariationOn f s :=
  (hf.lipschitzOnWith s).locallyBoundedVariationOn
#align lipschitz_with.has_locally_bounded_variation_on LipschitzWith.locallyBoundedVariationOn

end LipschitzOnWith

/-! ## Almost everywhere differentiability of functions with locally bounded variation -/

variable {V : Type*} [NormedAddCommGroup V] [NormedSpace ‚Ñù V] [FiniteDimensional ‚Ñù V]

namespace LocallyBoundedVariationOn

/-- A bounded variation function into `‚Ñù` is differentiable almost everywhere. Superseded by
`ae_differentiableWithinAt_of_mem`. -/
theorem ae_differentiableWithinAt_of_mem_real {f : ‚Ñù ‚Üí ‚Ñù} {s : Set ‚Ñù}
    (h : LocallyBoundedVariationOn f s) : ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù f s x := by
  obtain ‚ü®p, q, hp, hq, rfl‚ü© : ‚àÉ p q, MonotoneOn p s ‚àß MonotoneOn q s ‚àß f = p - q :=
    h.exists_monotoneOn_sub_monotoneOn
  filter_upwards [hp.ae_differentiableWithinAt_of_mem, hq.ae_differentiableWithinAt_of_mem] with
    x hxp hxq xs
  exact (hxp xs).sub (hxq xs)
#align has_locally_bounded_variation_on.ae_differentiable_within_at_of_mem_real LocallyBoundedVariationOn.ae_differentiableWithinAt_of_mem_real

/-- A bounded variation function into a finite dimensional product vector space is differentiable
almost everywhere. Superseded by `ae_differentiableWithinAt_of_mem`. -/
theorem ae_differentiableWithinAt_of_mem_pi {Œπ : Type*} [Fintype Œπ] {f : ‚Ñù ‚Üí Œπ ‚Üí ‚Ñù} {s : Set ‚Ñù}
    (h : LocallyBoundedVariationOn f s) : ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù f s x := by
  have A : ‚àÄ i : Œπ, LipschitzWith 1 fun x : Œπ ‚Üí ‚Ñù => x i := fun i => LipschitzWith.eval i
  have : ‚àÄ i : Œπ, ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù (fun x : ‚Ñù => f x i) s x := fun i ‚Ü¶ by
    apply ae_differentiableWithinAt_of_mem_real
    exact LipschitzWith.comp_locallyBoundedVariationOn (A i) h
  filter_upwards [ae_all_iff.2 this] with x hx xs
  exact differentiableWithinAt_pi.2 fun i => hx i xs
#align has_locally_bounded_variation_on.ae_differentiable_within_at_of_mem_pi LocallyBoundedVariationOn.ae_differentiableWithinAt_of_mem_pi

/-- A real function into a finite dimensional real vector space with bounded variation on a set
is differentiable almost everywhere in this set. -/
theorem ae_differentiableWithinAt_of_mem {f : ‚Ñù ‚Üí V} {s : Set ‚Ñù}
    (h : LocallyBoundedVariationOn f s) : ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù f s x := by
  let A := (Basis.ofVectorSpace ‚Ñù V).equivFun.toContinuousLinearEquiv
  suffices H : ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù (A ‚àò f) s x by
    filter_upwards [H] with x hx xs
    have : f = (A.symm ‚àò A) ‚àò f := by
      simp only [ContinuousLinearEquiv.symm_comp_self, Function.id_comp]
    rw [this]
    exact A.symm.differentiableAt.comp_differentiableWithinAt x (hx xs)
  apply ae_differentiableWithinAt_of_mem_pi
  exact A.lipschitz.comp_locallyBoundedVariationOn h
#align has_locally_bounded_variation_on.ae_differentiable_within_at_of_mem LocallyBoundedVariationOn.ae_differentiableWithinAt_of_mem

/-- A real function into a finite dimensional real vector space with bounded variation on a set
is differentiable almost everywhere in this set. -/
theorem ae_differentiableWithinAt {f : ‚Ñù ‚Üí V} {s : Set ‚Ñù} (h : LocallyBoundedVariationOn f s)
    (hs : MeasurableSet s) : ‚àÄ·µê x ‚àÇvolume.restrict s, DifferentiableWithinAt ‚Ñù f s x := by
  rw [ae_restrict_iff' hs]
  exact h.ae_differentiableWithinAt_of_mem
#align has_locally_bounded_variation_on.ae_differentiable_within_at LocallyBoundedVariationOn.ae_differentiableWithinAt

/-- A real function into a finite dimensional real vector space with bounded variation
is differentiable almost everywhere. -/
theorem ae_differentiableAt {f : ‚Ñù ‚Üí V} (h : LocallyBoundedVariationOn f univ) :
    ‚àÄ·µê x, DifferentiableAt ‚Ñù f x := by
  filter_upwards [h.ae_differentiableWithinAt_of_mem] with x hx
  rw [differentiableWithinAt_univ] at hx
  exact hx (mem_univ _)
#align has_locally_bounded_variation_on.ae_differentiable_at LocallyBoundedVariationOn.ae_differentiableAt

end LocallyBoundedVariationOn

/-- A real function into a finite dimensional real vector space which is Lipschitz on a set
is differentiable almost everywhere in this set. For the general Rademacher theorem assuming
that the source space is finite dimensional, see `LipschitzOnWith.ae_differentiableWithinAt_of_mem`.
-/
theorem LipschitzOnWith.ae_differentiableWithinAt_of_mem_real {C : ‚Ñù‚â•0} {f : ‚Ñù ‚Üí V} {s : Set ‚Ñù}
    (h : LipschitzOnWith C f s) : ‚àÄ·µê x, x ‚àà s ‚Üí DifferentiableWithinAt ‚Ñù f s x :=
  h.locallyBoundedVariationOn.ae_differentiableWithinAt_of_mem
#align lipschitz_on_with.ae_differentiable_within_at_of_mem LipschitzOnWith.ae_differentiableWithinAt_of_mem_real

/-- A real function into a finite dimensional real vector space which is Lipschitz on a set
is differentiable almost everywhere in this set. For the general Rademacher theorem assuming
that the source space is finite dimensional, see `LipschitzOnWith.ae_differentiableWithinAt`. -/
theorem LipschitzOnWith.ae_differentiableWithinAt_real {C : ‚Ñù‚â•0} {f : ‚Ñù ‚Üí V} {s : Set ‚Ñù}
    (h : LipschitzOnWith C f s) (hs : MeasurableSet s) :
    ‚àÄ·µê x ‚àÇvolume.restrict s, DifferentiableWithinAt ‚Ñù f s x :=
  h.locallyBoundedVariationOn.ae_differentiableWithinAt hs
#align lipschitz_on_with.ae_differentiable_within_at LipschitzOnWith.ae_differentiableWithinAt_real

/-- A real Lipschitz function into a finite dimensional real vector space is differentiable
almost everywhere. For the general Rademacher theorem assuming
that the source space is finite dimensional, see `LipschitzWith.ae_differentiableAt`. -/
theorem LipschitzWith.ae_differentiableAt_real {C : ‚Ñù‚â•0} {f : ‚Ñù ‚Üí V} (h : LipschitzWith C f) :
    ‚àÄ·µê x, DifferentiableAt ‚Ñù f x :=
  (h.locallyBoundedVariationOn univ).ae_differentiableAt
#align lipschitz_with.ae_differentiable_at LipschitzWith.ae_differentiableAt_real
