/-
Copyright (c) 2021 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Ya√´l Dillies
-/
import Mathlib.Analysis.Normed.Group.Pointwise
import Mathlib.Analysis.NormedSpace.Basic

#align_import analysis.normed_space.pointwise from "leanprover-community/mathlib"@"bc91ed7093bf098d253401e69df601fc33dde156"

/-!
# Properties of pointwise scalar multiplication of sets in normed spaces.

We explore the relationships between scalar multiplication of sets in vector spaces, and the norm.
Notably, we express arbitrary balls as rescaling of other balls, and we show that the
multiplication of bounded sets remain bounded.
-/


open Metric Set

open Pointwise Topology

variable {ùïú E : Type*}

section SMulZeroClass

variable [SeminormedAddCommGroup ùïú] [SeminormedAddCommGroup E]

variable [SMulZeroClass ùïú E] [BoundedSMul ùïú E]

theorem ediam_smul_le (c : ùïú) (s : Set E) : EMetric.diam (c ‚Ä¢ s) ‚â§ ‚Äñc‚Äñ‚Çä ‚Ä¢ EMetric.diam s :=
  (lipschitzWith_smul c).ediam_image_le s
#align ediam_smul_le ediam_smul_le

end SMulZeroClass

section DivisionRing

variable [NormedDivisionRing ùïú] [SeminormedAddCommGroup E]

variable [Module ùïú E] [BoundedSMul ùïú E]

theorem ediam_smul‚ÇÄ (c : ùïú) (s : Set E) : EMetric.diam (c ‚Ä¢ s) = ‚Äñc‚Äñ‚Çä ‚Ä¢ EMetric.diam s := by
  refine' le_antisymm (ediam_smul_le c s) _
  obtain rfl | hc := eq_or_ne c 0
  ¬∑ obtain rfl | hs := s.eq_empty_or_nonempty
    ¬∑ simp
    simp [zero_smul_set hs, ‚Üê Set.singleton_zero]
  ¬∑ have := (lipschitzWith_smul c‚Åª¬π).ediam_image_le (c ‚Ä¢ s)
    rwa [‚Üê smul_eq_mul, ‚Üê ENNReal.smul_def, Set.image_smul, inv_smul_smul‚ÇÄ hc s, nnnorm_inv,
      ENNReal.le_inv_smul_iff (nnnorm_ne_zero_iff.mpr hc)] at this
#align ediam_smul‚ÇÄ ediam_smul‚ÇÄ

theorem diam_smul‚ÇÄ (c : ùïú) (x : Set E) : diam (c ‚Ä¢ x) = ‚Äñc‚Äñ * diam x := by
  simp_rw [diam, ediam_smul‚ÇÄ, ENNReal.toReal_smul, NNReal.smul_def, coe_nnnorm, smul_eq_mul]
#align diam_smul‚ÇÄ diam_smul‚ÇÄ

theorem infEdist_smul‚ÇÄ {c : ùïú} (hc : c ‚â† 0) (s : Set E) (x : E) :
    EMetric.infEdist (c ‚Ä¢ x) (c ‚Ä¢ s) = ‚Äñc‚Äñ‚Çä ‚Ä¢ EMetric.infEdist x s := by
  simp_rw [EMetric.infEdist]
  have : Function.Surjective ((c ‚Ä¢ ¬∑) : E ‚Üí E) :=
    Function.RightInverse.surjective (smul_inv_smul‚ÇÄ hc)
  trans ‚®Ö (y) (_ : y ‚àà s), ‚Äñc‚Äñ‚Çä ‚Ä¢ edist x y
  ¬∑ refine' (this.iInf_congr _ fun y => _).symm
    simp_rw [smul_mem_smul_set_iff‚ÇÄ hc, edist_smul‚ÇÄ]
  ¬∑ have : (‚Äñc‚Äñ‚Çä : ENNReal) ‚â† 0 := by simp [hc]
    simp_rw [ENNReal.smul_def, smul_eq_mul, ENNReal.mul_iInf_of_ne this ENNReal.coe_ne_top]
#align inf_edist_smul‚ÇÄ infEdist_smul‚ÇÄ

theorem infDist_smul‚ÇÄ {c : ùïú} (hc : c ‚â† 0) (s : Set E) (x : E) :
    Metric.infDist (c ‚Ä¢ x) (c ‚Ä¢ s) = ‚Äñc‚Äñ * Metric.infDist x s := by
  simp_rw [Metric.infDist, infEdist_smul‚ÇÄ hc s, ENNReal.toReal_smul, NNReal.smul_def, coe_nnnorm,
    smul_eq_mul]
#align inf_dist_smul‚ÇÄ infDist_smul‚ÇÄ

end DivisionRing


variable [NormedField ùïú]

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup E] [NormedSpace ùïú E]

theorem smul_ball {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) : c ‚Ä¢ ball x r = ball (c ‚Ä¢ x) (‚Äñc‚Äñ * r) := by
  ext y
  rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ hc]
  conv_lhs => rw [‚Üê inv_smul_smul‚ÇÄ hc x]
  simp [‚Üê div_eq_inv_mul, div_lt_iff (norm_pos_iff.2 hc), mul_comm _ r, dist_smul‚ÇÄ]
#align smul_ball smul_ball

theorem smul_unitBall {c : ùïú} (hc : c ‚â† 0) : c ‚Ä¢ ball (0 : E) (1 : ‚Ñù) = ball (0 : E) ‚Äñc‚Äñ := by
  rw [_root_.smul_ball hc, smul_zero, mul_one]
#align smul_unit_ball smul_unitBall

theorem smul_sphere' {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
    c ‚Ä¢ sphere x r = sphere (c ‚Ä¢ x) (‚Äñc‚Äñ * r) := by
  ext y
  rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ hc]
  conv_lhs => rw [‚Üê inv_smul_smul‚ÇÄ hc x]
  simp only [mem_sphere, dist_smul‚ÇÄ, norm_inv, ‚Üê div_eq_inv_mul, div_eq_iff (norm_pos_iff.2 hc).ne',
    mul_comm r]
#align smul_sphere' smul_sphere'

theorem smul_closedBall' {c : ùïú} (hc : c ‚â† 0) (x : E) (r : ‚Ñù) :
    c ‚Ä¢ closedBall x r = closedBall (c ‚Ä¢ x) (‚Äñc‚Äñ * r) := by
  simp only [‚Üê ball_union_sphere, Set.smul_set_union, _root_.smul_ball hc, smul_sphere' hc]
#align smul_closed_ball' smul_closedBall'

/-- Image of a bounded set in a normed space under scalar multiplication by a constant is
bounded. See also `Metric.Bounded.smul` for a similar lemma about an isometric action. -/
theorem Bornology.IsBounded.smul‚ÇÄ {s : Set E} (hs : IsBounded s) (c : ùïú) : IsBounded (c ‚Ä¢ s) :=
  (lipschitzWith_smul c).isBounded_image hs
#align metric.bounded.smul Bornology.IsBounded.smul‚ÇÄ

/-- If `s` is a bounded set, then for small enough `r`, the set `{x} + r ‚Ä¢ s` is contained in any
fixed neighborhood of `x`. -/
theorem eventually_singleton_add_smul_subset {x : E} {s : Set E} (hs : Bornology.IsBounded s)
    {u : Set E} (hu : u ‚àà ùìù x) : ‚àÄ·∂† r in ùìù (0 : ùïú), {x} + r ‚Ä¢ s ‚äÜ u := by
  obtain ‚ü®Œµ, Œµpos, hŒµ‚ü© : ‚àÉ Œµ : ‚Ñù, 0 < Œµ ‚àß closedBall x Œµ ‚äÜ u := nhds_basis_closedBall.mem_iff.1 hu
  obtain ‚ü®R, Rpos, hR‚ü© : ‚àÉ R : ‚Ñù, 0 < R ‚àß s ‚äÜ closedBall 0 R := hs.subset_closedBall_lt 0 0
  have : Metric.closedBall (0 : ùïú) (Œµ / R) ‚àà ùìù (0 : ùïú) := closedBall_mem_nhds _ (div_pos Œµpos Rpos)
  filter_upwards [this] with r hr
  simp only [image_add_left, singleton_add]
  intro y hy
  obtain ‚ü®z, zs, hz‚ü© : ‚àÉ z : E, z ‚àà s ‚àß r ‚Ä¢ z = -x + y := by simpa [mem_smul_set] using hy
  have I : ‚Äñr ‚Ä¢ z‚Äñ ‚â§ Œµ :=
    calc
      ‚Äñr ‚Ä¢ z‚Äñ = ‚Äñr‚Äñ * ‚Äñz‚Äñ := norm_smul _ _
      _ ‚â§ Œµ / R * R :=
        (mul_le_mul (mem_closedBall_zero_iff.1 hr) (mem_closedBall_zero_iff.1 (hR zs))
          (norm_nonneg _) (div_pos Œµpos Rpos).le)
      _ = Œµ := by field_simp
  have : y = x + r ‚Ä¢ z := by simp only [hz, add_neg_cancel_left]
  apply hŒµ
  simpa only [this, dist_eq_norm, add_sub_cancel', mem_closedBall] using I
#align eventually_singleton_add_smul_subset eventually_singleton_add_smul_subset

variable [NormedSpace ‚Ñù E] {x y z : E} {Œ¥ Œµ : ‚Ñù}

/-- In a real normed space, the image of the unit ball under scalar multiplication by a positive
constant `r` is the ball of radius `r`. -/
theorem smul_unitBall_of_pos {r : ‚Ñù} (hr : 0 < r) : r ‚Ä¢ ball (0 : E) 1 = ball (0 : E) r := by
  rw [smul_unitBall hr.ne', Real.norm_of_nonneg hr.le]
#align smul_unit_ball_of_pos smul_unitBall_of_pos

-- This is also true for `‚Ñö`-normed spaces
theorem exists_dist_eq (x z : E) {a b : ‚Ñù} (ha : 0 ‚â§ a) (hb : 0 ‚â§ b) (hab : a + b = 1) :
    ‚àÉ y, dist x y = b * dist x z ‚àß dist y z = a * dist x z := by
  use a ‚Ä¢ x + b ‚Ä¢ z
  nth_rw 1 [‚Üê one_smul ‚Ñù x]
  nth_rw 4 [‚Üê one_smul ‚Ñù z]
  simp [dist_eq_norm, ‚Üê hab, add_smul, ‚Üê smul_sub, norm_smul_of_nonneg, ha, hb]
#align exists_dist_eq exists_dist_eq

theorem exists_dist_le_le (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 ‚â§ Œµ) (h : dist x z ‚â§ Œµ + Œ¥) :
    ‚àÉ y, dist x y ‚â§ Œ¥ ‚àß dist y z ‚â§ Œµ := by
  obtain rfl | hŒµ' := hŒµ.eq_or_lt
  ¬∑ exact ‚ü®z, by rwa [zero_add] at h, (dist_self _).le‚ü©
  have hŒµŒ¥ := add_pos_of_pos_of_nonneg hŒµ' hŒ¥
  refine' (exists_dist_eq x z (div_nonneg hŒµ <| add_nonneg hŒµ hŒ¥)
    (div_nonneg hŒ¥ <| add_nonneg hŒµ hŒ¥) <| by
      rw [‚Üê add_div, div_self hŒµŒ¥.ne']).imp
    fun y hy => _
  rw [hy.1, hy.2, div_mul_comm, div_mul_comm Œµ]
  rw [‚Üê div_le_one hŒµŒ¥] at h
  exact ‚ü®mul_le_of_le_one_left hŒ¥ h, mul_le_of_le_one_left hŒµ h‚ü©
#align exists_dist_le_le exists_dist_le_le

-- This is also true for `‚Ñö`-normed spaces
theorem exists_dist_le_lt (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 < Œµ) (h : dist x z < Œµ + Œ¥) :
    ‚àÉ y, dist x y ‚â§ Œ¥ ‚àß dist y z < Œµ := by
  refine' (exists_dist_eq x z (div_nonneg hŒµ.le <| add_nonneg hŒµ.le hŒ¥)
    (div_nonneg hŒ¥ <| add_nonneg hŒµ.le hŒ¥) <| by
      rw [‚Üê add_div, div_self (add_pos_of_pos_of_nonneg hŒµ hŒ¥).ne']).imp
    fun y hy => _
  rw [hy.1, hy.2, div_mul_comm, div_mul_comm Œµ]
  rw [‚Üê div_lt_one (add_pos_of_pos_of_nonneg hŒµ hŒ¥)] at h
  exact ‚ü®mul_le_of_le_one_left hŒ¥ h.le, mul_lt_of_lt_one_left hŒµ h‚ü©
#align exists_dist_le_lt exists_dist_le_lt

-- This is also true for `‚Ñö`-normed spaces
theorem exists_dist_lt_le (hŒ¥ : 0 < Œ¥) (hŒµ : 0 ‚â§ Œµ) (h : dist x z < Œµ + Œ¥) :
    ‚àÉ y, dist x y < Œ¥ ‚àß dist y z ‚â§ Œµ := by
  obtain ‚ü®y, yz, xy‚ü© :=
    exists_dist_le_lt hŒµ hŒ¥ (show dist z x < Œ¥ + Œµ by simpa only [dist_comm, add_comm] using h)
  exact ‚ü®y, by simp [dist_comm x y, dist_comm y z, *]‚ü©
#align exists_dist_lt_le exists_dist_lt_le

-- This is also true for `‚Ñö`-normed spaces
theorem exists_dist_lt_lt (hŒ¥ : 0 < Œ¥) (hŒµ : 0 < Œµ) (h : dist x z < Œµ + Œ¥) :
    ‚àÉ y, dist x y < Œ¥ ‚àß dist y z < Œµ := by
  refine' (exists_dist_eq x z (div_nonneg hŒµ.le <| add_nonneg hŒµ.le hŒ¥.le)
    (div_nonneg hŒ¥.le <| add_nonneg hŒµ.le hŒ¥.le) <| by
      rw [‚Üê add_div, div_self (add_pos hŒµ hŒ¥).ne']).imp
    fun y hy => _
  rw [hy.1, hy.2, div_mul_comm, div_mul_comm Œµ]
  rw [‚Üê div_lt_one (add_pos hŒµ hŒ¥)] at h
  exact ‚ü®mul_lt_of_lt_one_left hŒ¥ h, mul_lt_of_lt_one_left hŒµ h‚ü©
#align exists_dist_lt_lt exists_dist_lt_lt

-- This is also true for `‚Ñö`-normed spaces
theorem disjoint_ball_ball_iff (hŒ¥ : 0 < Œ¥) (hŒµ : 0 < Œµ) :
    Disjoint (ball x Œ¥) (ball y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y := by
  refine' ‚ü®fun h => le_of_not_lt fun hxy => _, ball_disjoint_ball‚ü©
  rw [add_comm] at hxy
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_lt_lt hŒ¥ hŒµ hxy
  rw [dist_comm] at hxz
  exact h.le_bot ‚ü®hxz, hzy‚ü©
#align disjoint_ball_ball_iff disjoint_ball_ball_iff

-- This is also true for `‚Ñö`-normed spaces
theorem disjoint_ball_closedBall_iff (hŒ¥ : 0 < Œ¥) (hŒµ : 0 ‚â§ Œµ) :
    Disjoint (ball x Œ¥) (closedBall y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y := by
  refine' ‚ü®fun h => le_of_not_lt fun hxy => _, ball_disjoint_closedBall‚ü©
  rw [add_comm] at hxy
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_lt_le hŒ¥ hŒµ hxy
  rw [dist_comm] at hxz
  exact h.le_bot ‚ü®hxz, hzy‚ü©
#align disjoint_ball_closed_ball_iff disjoint_ball_closedBall_iff

-- This is also true for `‚Ñö`-normed spaces
theorem disjoint_closedBall_ball_iff (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 < Œµ) :
    Disjoint (closedBall x Œ¥) (ball y Œµ) ‚Üî Œ¥ + Œµ ‚â§ dist x y := by
  rw [disjoint_comm, disjoint_ball_closedBall_iff hŒµ hŒ¥, add_comm, dist_comm]
#align disjoint_closed_ball_ball_iff disjoint_closedBall_ball_iff

theorem disjoint_closedBall_closedBall_iff (hŒ¥ : 0 ‚â§ Œ¥) (hŒµ : 0 ‚â§ Œµ) :
    Disjoint (closedBall x Œ¥) (closedBall y Œµ) ‚Üî Œ¥ + Œµ < dist x y := by
  refine' ‚ü®fun h => lt_of_not_ge fun hxy => _, closedBall_disjoint_closedBall‚ü©
  rw [add_comm] at hxy
  obtain ‚ü®z, hxz, hzy‚ü© := exists_dist_le_le hŒ¥ hŒµ hxy
  rw [dist_comm] at hxz
  exact h.le_bot ‚ü®hxz, hzy‚ü©
#align disjoint_closed_ball_closed_ball_iff disjoint_closedBall_closedBall_iff

open EMetric ENNReal

@[simp]
theorem infEdist_thickening (hŒ¥ : 0 < Œ¥) (s : Set E) (x : E) :
    infEdist x (thickening Œ¥ s) = infEdist x s - ENNReal.ofReal Œ¥ := by
  obtain hs | hs := lt_or_le (infEdist x s) (ENNReal.ofReal Œ¥)
  ¬∑ rw [infEdist_zero_of_mem, tsub_eq_zero_of_le hs.le]
    exact hs
  refine' (tsub_le_iff_right.2 infEdist_le_infEdist_thickening_add).antisymm' _
  refine' le_sub_of_add_le_right ofReal_ne_top _
  refine' le_infEdist.2 fun z hz => le_of_forall_lt' fun r h => _
  cases' r with r
  ¬∑ exact add_lt_top.2 ‚ü®lt_top_iff_ne_top.2 <| infEdist_ne_top ‚ü®z, self_subset_thickening hŒ¥ _ hz‚ü©,
      ofReal_lt_top‚ü©
  have hr : 0 < ‚Üër - Œ¥ := by
    refine' sub_pos_of_lt _
    have := hs.trans_lt ((infEdist_le_edist_of_mem hz).trans_lt h)
    rw [ofReal_eq_coe_nnreal hŒ¥.le, some_eq_coe] at this
    exact_mod_cast this
  rw [some_eq_coe, edist_lt_coe, ‚Üê dist_lt_coe, ‚Üê add_sub_cancel'_right Œ¥ ‚Üër] at h
  obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_lt hr hŒ¥ h
  refine' (ENNReal.add_lt_add_right ofReal_ne_top <|
    infEdist_lt_iff.2 ‚ü®_, mem_thickening_iff.2 ‚ü®_, hz, hyz‚ü©, edist_lt_ofReal.2 hxy‚ü©).trans_le _
  rw [‚Üê ofReal_add hr.le hŒ¥.le, sub_add_cancel, ofReal_coe_nnreal]
  exact le_rfl
#align inf_edist_thickening infEdist_thickening

@[simp]
theorem thickening_thickening (hŒµ : 0 < Œµ) (hŒ¥ : 0 < Œ¥) (s : Set E) :
    thickening Œµ (thickening Œ¥ s) = thickening (Œµ + Œ¥) s :=
  (thickening_thickening_subset _ _ _).antisymm fun x => by
    simp_rw [mem_thickening_iff]
    rintro ‚ü®z, hz, hxz‚ü©
    rw [add_comm] at hxz
    obtain ‚ü®y, hxy, hyz‚ü© := exists_dist_lt_lt hŒµ hŒ¥ hxz
    exact ‚ü®y, ‚ü®_, hz, hyz‚ü©, hxy‚ü©
#align thickening_thickening thickening_thickening

@[simp]
theorem cthickening_thickening (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 < Œ¥) (s : Set E) :
    cthickening Œµ (thickening Œ¥ s) = cthickening (Œµ + Œ¥) s :=
  (cthickening_thickening_subset hŒµ _ _).antisymm fun x => by
    simp_rw [mem_cthickening_iff, ENNReal.ofReal_add hŒµ hŒ¥.le, infEdist_thickening hŒ¥]
    exact tsub_le_iff_right.2
#align cthickening_thickening cthickening_thickening

-- Note: `interior (cthickening Œ¥ s) ‚â† thickening Œ¥ s` in general
@[simp]
theorem closure_thickening (hŒ¥ : 0 < Œ¥) (s : Set E) :
    closure (thickening Œ¥ s) = cthickening Œ¥ s := by
  rw [‚Üê cthickening_zero, cthickening_thickening le_rfl hŒ¥, zero_add]
#align closure_thickening closure_thickening

@[simp]
theorem infEdist_cthickening (Œ¥ : ‚Ñù) (s : Set E) (x : E) :
    infEdist x (cthickening Œ¥ s) = infEdist x s - ENNReal.ofReal Œ¥ := by
  obtain hŒ¥ | hŒ¥ := le_or_lt Œ¥ 0
  ¬∑ rw [cthickening_of_nonpos hŒ¥, infEdist_closure, ofReal_of_nonpos hŒ¥, tsub_zero]
  ¬∑ rw [‚Üê closure_thickening hŒ¥, infEdist_closure, infEdist_thickening hŒ¥]
#align inf_edist_cthickening infEdist_cthickening

@[simp]
theorem thickening_cthickening (hŒµ : 0 < Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (s : Set E) :
    thickening Œµ (cthickening Œ¥ s) = thickening (Œµ + Œ¥) s := by
  obtain rfl | hŒ¥ := hŒ¥.eq_or_lt
  ¬∑ rw [cthickening_zero, thickening_closure, add_zero]
  ¬∑ rw [‚Üê closure_thickening hŒ¥, thickening_closure, thickening_thickening hŒµ hŒ¥]
#align thickening_cthickening thickening_cthickening

@[simp]
theorem cthickening_cthickening (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (s : Set E) :
    cthickening Œµ (cthickening Œ¥ s) = cthickening (Œµ + Œ¥) s :=
  (cthickening_cthickening_subset hŒµ hŒ¥ _).antisymm fun x => by
    simp_rw [mem_cthickening_iff, ENNReal.ofReal_add hŒµ hŒ¥, infEdist_cthickening]
    exact tsub_le_iff_right.2
#align cthickening_cthickening cthickening_cthickening

@[simp]
theorem thickening_ball (hŒµ : 0 < Œµ) (hŒ¥ : 0 < Œ¥) (x : E) :
    thickening Œµ (ball x Œ¥) = ball x (Œµ + Œ¥) := by
  rw [‚Üê thickening_singleton, thickening_thickening hŒµ hŒ¥, thickening_singleton]
#align thickening_ball thickening_ball

@[simp]
theorem thickening_closedBall (hŒµ : 0 < Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (x : E) :
    thickening Œµ (closedBall x Œ¥) = ball x (Œµ + Œ¥) := by
  rw [‚Üê cthickening_singleton _ hŒ¥, thickening_cthickening hŒµ hŒ¥, thickening_singleton]
#align thickening_closed_ball thickening_closedBall

@[simp]
theorem cthickening_ball (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 < Œ¥) (x : E) :
    cthickening Œµ (ball x Œ¥) = closedBall x (Œµ + Œ¥) := by
  rw [‚Üê thickening_singleton, cthickening_thickening hŒµ hŒ¥,
      cthickening_singleton _ (add_nonneg hŒµ hŒ¥.le)]
#align cthickening_ball cthickening_ball

@[simp]
theorem cthickening_closedBall (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (x : E) :
    cthickening Œµ (closedBall x Œ¥) = closedBall x (Œµ + Œ¥) := by
  rw [‚Üê cthickening_singleton _ hŒ¥, cthickening_cthickening hŒµ hŒ¥,
      cthickening_singleton _ (add_nonneg hŒµ hŒ¥)]
#align cthickening_closed_ball cthickening_closedBall

theorem ball_add_ball (hŒµ : 0 < Œµ) (hŒ¥ : 0 < Œ¥) (a b : E) :
    ball a Œµ + ball b Œ¥ = ball (a + b) (Œµ + Œ¥) := by
  rw [ball_add, thickening_ball hŒµ hŒ¥ b, Metric.vadd_ball, vadd_eq_add]
#align ball_add_ball ball_add_ball

theorem ball_sub_ball (hŒµ : 0 < Œµ) (hŒ¥ : 0 < Œ¥) (a b : E) :
    ball a Œµ - ball b Œ¥ = ball (a - b) (Œµ + Œ¥) := by
  simp_rw [sub_eq_add_neg, neg_ball, ball_add_ball hŒµ hŒ¥]
#align ball_sub_ball ball_sub_ball

theorem ball_add_closedBall (hŒµ : 0 < Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (a b : E) :
    ball a Œµ + closedBall b Œ¥ = ball (a + b) (Œµ + Œ¥) := by
  rw [ball_add, thickening_closedBall hŒµ hŒ¥ b, Metric.vadd_ball, vadd_eq_add]
#align ball_add_closed_ball ball_add_closedBall

theorem ball_sub_closedBall (hŒµ : 0 < Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (a b : E) :
    ball a Œµ - closedBall b Œ¥ = ball (a - b) (Œµ + Œ¥) := by
  simp_rw [sub_eq_add_neg, neg_closedBall, ball_add_closedBall hŒµ hŒ¥]
#align ball_sub_closed_ball ball_sub_closedBall

theorem closedBall_add_ball (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 < Œ¥) (a b : E) :
    closedBall a Œµ + ball b Œ¥ = ball (a + b) (Œµ + Œ¥) := by
  rw [add_comm, ball_add_closedBall hŒ¥ hŒµ b, add_comm, add_comm Œ¥]
#align closed_ball_add_ball closedBall_add_ball

theorem closedBall_sub_ball (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 < Œ¥) (a b : E) :
    closedBall a Œµ - ball b Œ¥ = ball (a - b) (Œµ + Œ¥) := by
  simp_rw [sub_eq_add_neg, neg_ball, closedBall_add_ball hŒµ hŒ¥]
#align closed_ball_sub_ball closedBall_sub_ball

theorem closedBall_add_closedBall [ProperSpace E] (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (a b : E) :
    closedBall a Œµ + closedBall b Œ¥ = closedBall (a + b) (Œµ + Œ¥) := by
  rw [(isCompact_closedBall _ _).add_closedBall hŒ¥ b, cthickening_closedBall hŒ¥ hŒµ a,
    Metric.vadd_closedBall, vadd_eq_add, add_comm, add_comm Œ¥]
#align closed_ball_add_closed_ball closedBall_add_closedBall

theorem closedBall_sub_closedBall [ProperSpace E] (hŒµ : 0 ‚â§ Œµ) (hŒ¥ : 0 ‚â§ Œ¥) (a b : E) :
    closedBall a Œµ - closedBall b Œ¥ = closedBall (a - b) (Œµ + Œ¥) := by
  rw [sub_eq_add_neg, neg_closedBall, closedBall_add_closedBall hŒµ hŒ¥, sub_eq_add_neg]
#align closed_ball_sub_closed_ball closedBall_sub_closedBall

end SeminormedAddCommGroup

section NormedAddCommGroup

variable [NormedAddCommGroup E] [NormedSpace ùïú E]

theorem smul_closedBall (c : ùïú) (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    c ‚Ä¢ closedBall x r = closedBall (c ‚Ä¢ x) (‚Äñc‚Äñ * r) := by
  rcases eq_or_ne c 0 with (rfl | hc)
  ¬∑ simp [hr, zero_smul_set, Set.singleton_zero, ‚Üê nonempty_closedBall]
  ¬∑ exact smul_closedBall' hc x r
#align smul_closed_ball smul_closedBall

theorem smul_closedUnitBall (c : ùïú) : c ‚Ä¢ closedBall (0 : E) (1 : ‚Ñù) = closedBall (0 : E) ‚Äñc‚Äñ :=
  by rw [smul_closedBall _ _ zero_le_one, smul_zero, mul_one]
#align smul_closed_unit_ball smul_closedUnitBall

variable [NormedSpace ‚Ñù E]

/-- In a real normed space, the image of the unit closed ball under multiplication by a nonnegative
number `r` is the closed ball of radius `r` with center at the origin. -/
theorem smul_closedUnitBall_of_nonneg {r : ‚Ñù} (hr : 0 ‚â§ r) :
    r ‚Ä¢ closedBall (0 : E) 1 = closedBall (0 : E) r := by
  rw [smul_closedUnitBall, Real.norm_of_nonneg hr]
#align smul_closed_unit_ball_of_nonneg smul_closedUnitBall_of_nonneg

/-- In a nontrivial real normed space, a sphere is nonempty if and only if its radius is
nonnegative. -/
@[simp]
theorem NormedSpace.sphere_nonempty [Nontrivial E] {x : E} {r : ‚Ñù} :
    (sphere x r).Nonempty ‚Üî 0 ‚â§ r := by
  obtain ‚ü®y, hy‚ü© := exists_ne x
  refine' ‚ü®fun h => nonempty_closedBall.1 (h.mono sphere_subset_closedBall), fun hr =>
    ‚ü®r ‚Ä¢ ‚Äñy - x‚Äñ‚Åª¬π ‚Ä¢ (y - x) + x, _‚ü©‚ü©
  have : ‚Äñy - x‚Äñ ‚â† 0 := by simpa [sub_eq_zero]
  simp [norm_smul, this, Real.norm_of_nonneg hr]
  rw [inv_mul_cancel this, mul_one, abs_eq_self.mpr hr]
#align normed_space.sphere_nonempty NormedSpace.sphere_nonempty

theorem smul_sphere [Nontrivial E] (c : ùïú) (x : E) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    c ‚Ä¢ sphere x r = sphere (c ‚Ä¢ x) (‚Äñc‚Äñ * r) := by
  rcases eq_or_ne c 0 with (rfl | hc)
  ¬∑ simp [zero_smul_set, Set.singleton_zero, hr]
  ¬∑ exact smul_sphere' hc x r
#align smul_sphere smul_sphere

/-- Any ball `Metric.ball x r`, `0 < r` is the image of the unit ball under `fun y ‚Ü¶ x + r ‚Ä¢ y`. -/
theorem affinity_unitBall {r : ‚Ñù} (hr : 0 < r) (x : E) : x +·µ• r ‚Ä¢ ball (0 : E) 1 = ball x r := by
  rw [smul_unitBall_of_pos hr, vadd_ball_zero]
#align affinity_unit_ball affinity_unitBall

/-- Any closed ball `Metric.closedBall x r`, `0 ‚â§ r` is the image of the unit closed ball under
`fun y ‚Ü¶ x + r ‚Ä¢ y`. -/
theorem affinity_unitClosedBall {r : ‚Ñù} (hr : 0 ‚â§ r) (x : E) :
    x +·µ• r ‚Ä¢ closedBall (0 : E) 1 = closedBall x r := by
  rw [smul_closedUnitBall, Real.norm_of_nonneg hr, vadd_closedBall_zero]
#align affinity_unit_closed_ball affinity_unitClosedBall

end NormedAddCommGroup
