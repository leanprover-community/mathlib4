/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm
import Mathlib.Topology.MetricSpace.Baire
import Mathlib.Topology.Algebra.Module.Basic

#align_import analysis.normed_space.banach_steinhaus from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# The Banach-Steinhaus theorem: Uniform Boundedness Principle

Herein we prove the Banach-Steinhaus theorem: any collection of bounded linear maps
from a Banach space into a normed space which is pointwise bounded is uniformly bounded.

## TODO

For now, we only prove the standard version by appeal to the Baire category theorem.
Much more general versions exist (in particular, for maps from barrelled spaces to locally
convex spaces), but these are not yet in `mathlib`.
-/


open Set

variable {E F ùïú ùïú‚ÇÇ : Type*} [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]
  [NontriviallyNormedField ùïú] [NontriviallyNormedField ùïú‚ÇÇ] [NormedSpace ùïú E] [NormedSpace ùïú‚ÇÇ F]
  {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ} [RingHomIsometric œÉ‚ÇÅ‚ÇÇ]

/-- This is the standard Banach-Steinhaus theorem, or Uniform Boundedness Principle.
If a family of continuous linear maps from a Banach space into a normed space is pointwise
bounded, then the norms of these linear maps are uniformly bounded. -/
theorem banach_steinhaus {Œπ : Type*} [CompleteSpace E] {g : Œπ ‚Üí E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F}
    (h : ‚àÄ x, ‚àÉ C, ‚àÄ i, ‚Äñg i x‚Äñ ‚â§ C) : ‚àÉ C', ‚àÄ i, ‚Äñg i‚Äñ ‚â§ C' := by
  -- sequence of subsets consisting of those `x : E` with norms `‚Äñg i x‚Äñ` bounded by `n`
  let e : ‚Ñï ‚Üí Set E := fun n => ‚ãÇ i : Œπ, { x : E | ‚Äñg i x‚Äñ ‚â§ n }
  -- each of these sets is closed
  have hc : ‚àÄ n : ‚Ñï, IsClosed (e n) := fun i =>
    isClosed_iInter fun i => isClosed_le (Continuous.norm (g i).cont) continuous_const
  -- the union is the entire space; this is where we use `h`
  have hU : ‚ãÉ n : ‚Ñï, e n = univ := by
    refine' eq_univ_of_forall fun x => _
    cases' h x with C hC
    obtain ‚ü®m, hm‚ü© := exists_nat_ge C
    exact ‚ü®e m, mem_range_self m, mem_iInter.mpr fun i => le_trans (hC i) hm‚ü©
  -- apply the Baire category theorem to conclude that for some `m : ‚Ñï`, `e m` contains some `x`
  rcases nonempty_interior_of_iUnion_of_closed hc hU with ‚ü®m, x, hx‚ü©
  rcases Metric.isOpen_iff.mp isOpen_interior x hx with ‚ü®Œµ, Œµ_pos, hŒµ‚ü©
  obtain ‚ü®k, hk‚ü© := NormedField.exists_one_lt_norm ùïú
  -- show all elements in the ball have norm bounded by `m` after applying any `g i`
  have real_norm_le : ‚àÄ z : E, z ‚àà Metric.ball x Œµ ‚Üí ‚àÄ i : Œπ, ‚Äñg i z‚Äñ ‚â§ m := by
    intro z hz i
    replace hz := mem_iInter.mp (interior_iInter_subset _ (hŒµ hz)) i
    apply interior_subset hz
  have Œµk_pos : 0 < Œµ / ‚Äñk‚Äñ := div_pos Œµ_pos (zero_lt_one.trans hk)
  refine' ‚ü®(m + m : ‚Ñï) / (Œµ / ‚Äñk‚Äñ), fun i => ContinuousLinearMap.op_norm_le_of_shell Œµ_pos _ hk _‚ü©
  ¬∑ exact div_nonneg (Nat.cast_nonneg _) Œµk_pos.le
  intro y le_y y_lt
  calc
    ‚Äñg i y‚Äñ = ‚Äñg i (y + x) - g i x‚Äñ := by rw [ContinuousLinearMap.map_add, add_sub_cancel]
    _ ‚â§ ‚Äñg i (y + x)‚Äñ + ‚Äñg i x‚Äñ := (norm_sub_le _ _)
    _ ‚â§ m + m :=
      (add_le_add (real_norm_le (y + x) (by rwa [add_comm, add_mem_ball_iff_norm]) i)
        (real_norm_le x (Metric.mem_ball_self Œµ_pos) i))
    _ = (m + m : ‚Ñï) := (m.cast_add m).symm
    _ ‚â§ (m + m : ‚Ñï) * (‚Äñy‚Äñ / (Œµ / ‚Äñk‚Äñ)) :=
      (le_mul_of_one_le_right (Nat.cast_nonneg _)
        ((one_le_div <| div_pos Œµ_pos (zero_lt_one.trans hk)).2 le_y))
    _ = (m + m : ‚Ñï) / (Œµ / ‚Äñk‚Äñ) * ‚Äñy‚Äñ := (mul_comm_div _ _ _).symm
#align banach_steinhaus banach_steinhaus

open ENNReal

open ENNReal

/-- This version of Banach-Steinhaus is stated in terms of suprema of `‚Üë‚Äñ¬∑‚Äñ‚Çä : ‚Ñù‚â•0‚àû`
for convenience. -/
theorem banach_steinhaus_iSup_nnnorm {Œπ : Type*} [CompleteSpace E] {g : Œπ ‚Üí E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F}
    (h : ‚àÄ x, ‚®Ü i, ‚Üë‚Äñg i x‚Äñ‚Çä < ‚àû) : ‚®Ü i, ‚Üë‚Äñg i‚Äñ‚Çä < ‚àû := by
  have h' : ‚àÄ x : E, ‚àÉ C : ‚Ñù, ‚àÄ i : Œπ, ‚Äñg i x‚Äñ ‚â§ C := by
    intro x
    rcases lt_iff_exists_coe.mp (h x) with ‚ü®p, hp‚ÇÅ, _‚ü©
    refine' ‚ü®p, fun i => _‚ü©
    exact_mod_cast
      calc
        (‚Äñg i x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) ‚â§ ‚®Ü j, ‚Üë‚Äñg j x‚Äñ‚Çä := le_iSup (fun j => (‚Äñg j x‚Äñ‚Çä : ‚Ñù‚â•0‚àû)) i
        _ = p := hp‚ÇÅ
  cases' banach_steinhaus h' with C' hC'
  refine' (iSup_le fun i => _).trans_lt (@coe_lt_top C'.toNNReal)
  rw [‚Üê norm_toNNReal]
  exact coe_mono (Real.toNNReal_le_toNNReal <| hC' i)
#align banach_steinhaus_supr_nnnorm banach_steinhaus_iSup_nnnorm

open Topology

open Filter

/-- Given a *sequence* of continuous linear maps which converges pointwise and for which the
domain is complete, the Banach-Steinhaus theorem is used to guarantee that the limit map
is a *continuous* linear map as well. -/
def continuousLinearMapOfTendsto [CompleteSpace E] [T2Space F] (g : ‚Ñï ‚Üí E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) {f : E ‚Üí F}
    (h : Tendsto (fun n x => g n x) atTop (ùìù f)) : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F where
  toFun := f
  map_add' := (linearMapOfTendsto _ _ h).map_add'
  map_smul' := (linearMapOfTendsto _ _ h).map_smul'
  cont := by
    -- show that the maps are pointwise bounded and apply `banach_steinhaus`
    have h_point_bdd : ‚àÄ x : E, ‚àÉ C : ‚Ñù, ‚àÄ n : ‚Ñï, ‚Äñg n x‚Äñ ‚â§ C := by
      intro x
      rcases cauchySeq_bdd (tendsto_pi_nhds.mp h x).cauchySeq with ‚ü®C, -, hC‚ü©
      refine' ‚ü®C + ‚Äñg 0 x‚Äñ, fun n => _‚ü©
      simp_rw [dist_eq_norm] at hC
      calc
        ‚Äñg n x‚Äñ ‚â§ ‚Äñg 0 x‚Äñ + ‚Äñg n x - g 0 x‚Äñ := norm_le_insert' _ _
        _ ‚â§ C + ‚Äñg 0 x‚Äñ := by linarith [hC n 0]
    cases' banach_steinhaus h_point_bdd with C' hC'
    /- show the uniform bound from `banach_steinhaus` is a norm bound of the limit map
             by allowing "an `Œµ` of room." -/
    refine'
      AddMonoidHomClass.continuous_of_bound (linearMapOfTendsto _ _ h) C' fun x =>
        le_of_forall_pos_lt_add fun Œµ Œµ_pos => _
    cases' Metric.tendsto_atTop.mp (tendsto_pi_nhds.mp h x) Œµ Œµ_pos with n hn
    have lt_Œµ : ‚Äñg n x - f x‚Äñ < Œµ := by
      rw [‚Üê dist_eq_norm]
      exact hn n (le_refl n)
    calc
      ‚Äñf x‚Äñ ‚â§ ‚Äñg n x‚Äñ + ‚Äñg n x - f x‚Äñ := norm_le_insert _ _
      _ < ‚Äñg n‚Äñ * ‚Äñx‚Äñ + Œµ := by linarith [lt_Œµ, (g n).le_op_norm x]
      _ ‚â§ C' * ‚Äñx‚Äñ + Œµ := by nlinarith [hC' n, norm_nonneg x]
#align continuous_linear_map_of_tendsto continuousLinearMapOfTendsto
