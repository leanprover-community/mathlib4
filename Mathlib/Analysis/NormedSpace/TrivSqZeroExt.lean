/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.Analysis.NormedSpace.Exponential
import Mathlib.Topology.Instances.TrivSqZeroExt

#align_import analysis.normed_space.triv_sq_zero_ext from "leanprover-community/mathlib"@"88a563b158f59f2983cfad685664da95502e8cdd"

/-!
# Results on `TrivSqZeroExt R M` related to the norm

For now, this file contains results about `exp` for this type.

## Main results

* `TrivSqZeroExt.fst_exp`
* `TrivSqZeroExt.snd_exp`
* `TrivSqZeroExt.exp_inl`
* `TrivSqZeroExt.exp_inr`

## TODO

* Actually define a sensible norm on `TrivSqZeroExt R M`, so that we have access to lemmas
  like `exp_add`.
* Generalize more of these results to non-commutative `R`. In principle, under sufficient conditions
  we should expect
 `(exp ğ•œ x).snd = âˆ« t in 0..1, exp ğ•œ (t â€¢ x.fst) â€¢ op (exp ğ•œ ((1 - t) â€¢ x.fst)) â€¢ x.snd`
  ([Physics.SE](https://physics.stackexchange.com/a/41671/185147), and
  https://link.springer.com/chapter/10.1007/978-3-540-44953-9_2).

-/


variable (ğ•œ : Type*) {R M : Type*}

local notation "tsze" => TrivSqZeroExt

open NormedSpace -- For `exp`.

namespace TrivSqZeroExt

section Topology

variable [TopologicalSpace R] [TopologicalSpace M]

/-- If `exp R x.fst` converges to `e` then `(exp R x).fst` converges to `e`. -/
theorem hasSum_fst_expSeries [Field ğ•œ] [Ring R] [AddCommGroup M] [Algebra ğ•œ R] [Module R M]
    [Module Ráµáµ’áµ– M] [SMulCommClass R Ráµáµ’áµ– M] [Module ğ•œ M] [IsScalarTower ğ•œ R M]
    [IsScalarTower ğ•œ Ráµáµ’áµ– M] [TopologicalRing R] [TopologicalAddGroup M] [ContinuousSMul R M]
    [ContinuousSMul Ráµáµ’áµ– M] (x : tsze R M) {e : R}
    (h : HasSum (fun n => expSeries ğ•œ R n fun _ => x.fst) e) :
    HasSum (fun n => fst (expSeries ğ•œ (tsze R M) n fun _ => x)) e := by
  simpa [expSeries_apply_eq] using h
#align triv_sq_zero_ext.has_sum_fst_exp_series TrivSqZeroExt.hasSum_fst_expSeries

/-- If `exp R x.fst` converges to `e` then `(exp R x).snd` converges to `e â€¢ x.snd`. -/
theorem hasSum_snd_expSeries_of_smul_comm [Field ğ•œ] [CharZero ğ•œ] [Ring R] [AddCommGroup M]
    [Algebra ğ•œ R] [Module R M] [Module Ráµáµ’áµ– M] [SMulCommClass R Ráµáµ’áµ– M] [Module ğ•œ M]
    [IsScalarTower ğ•œ R M] [IsScalarTower ğ•œ Ráµáµ’áµ– M] [TopologicalRing R] [TopologicalAddGroup M]
    [ContinuousSMul R M] [ContinuousSMul Ráµáµ’áµ– M] (x : tsze R M)
    (hx : MulOpposite.op x.fst â€¢ x.snd = x.fst â€¢ x.snd) {e : R}
    (h : HasSum (fun n => expSeries ğ•œ R n fun _ => x.fst) e) :
    HasSum (fun n => snd (expSeries ğ•œ (tsze R M) n fun _ => x)) (e â€¢ x.snd) := by
  simp_rw [expSeries_apply_eq] at *
  conv =>
    congr
    ext n
    rw [snd_smul, snd_pow_of_smul_comm _ _ hx, nsmul_eq_smul_cast ğ•œ n, smul_smul, inv_mul_eq_div, â†
      inv_div, â† smul_assoc]
  apply HasSum.smul_const
  rw [â† hasSum_nat_add_iff' 1]
  rw [Finset.range_one, Finset.sum_singleton, Nat.cast_zero, div_zero, inv_zero, zero_smul,
    sub_zero]
  simp_rw [â† Nat.succ_eq_add_one, Nat.pred_succ, Nat.factorial_succ, Nat.cast_mul, â†
    Nat.succ_eq_add_one,
    mul_div_cancel_left _ ((@Nat.cast_ne_zero ğ•œ _ _ _).mpr <| Nat.succ_ne_zero _)]
  exact h
#align triv_sq_zero_ext.has_sum_snd_exp_series_of_smul_comm TrivSqZeroExt.hasSum_snd_expSeries_of_smul_comm

/-- If `exp R x.fst` converges to `e` then `exp R x` converges to `inl e + inr (e â€¢ x.snd)`. -/
theorem hasSum_expSeries_of_smul_comm [Field ğ•œ] [CharZero ğ•œ] [Ring R] [AddCommGroup M] [Algebra ğ•œ R]
    [Module R M] [Module Ráµáµ’áµ– M] [SMulCommClass R Ráµáµ’áµ– M] [Module ğ•œ M] [IsScalarTower ğ•œ R M]
    [IsScalarTower ğ•œ Ráµáµ’áµ– M] [TopologicalRing R] [TopologicalAddGroup M] [ContinuousSMul R M]
    [ContinuousSMul Ráµáµ’áµ– M] (x : tsze R M) (hx : MulOpposite.op x.fst â€¢ x.snd = x.fst â€¢ x.snd)
    {e : R} (h : HasSum (fun n => expSeries ğ•œ R n fun _ => x.fst) e) :
    HasSum (fun n => expSeries ğ•œ (tsze R M) n fun _ => x) (inl e + inr (e â€¢ x.snd)) := by
  simpa only [inl_fst_add_inr_snd_eq] using
    (hasSum_inl _ <| hasSum_fst_expSeries ğ•œ x h).add
      (hasSum_inr _ <| hasSum_snd_expSeries_of_smul_comm ğ•œ x hx h)
#align triv_sq_zero_ext.has_sum_exp_series_of_smul_comm TrivSqZeroExt.hasSum_expSeries_of_smul_comm

end Topology

section NormedRing

variable [IsROrC ğ•œ] [NormedRing R] [AddCommGroup M]

variable [NormedAlgebra ğ•œ R] [Module R M] [Module Ráµáµ’áµ– M] [SMulCommClass R Ráµáµ’áµ– M]

variable [Module ğ•œ M] [IsScalarTower ğ•œ R M] [IsScalarTower ğ•œ Ráµáµ’áµ– M]

variable [TopologicalSpace M] [TopologicalRing R]

variable [TopologicalAddGroup M] [ContinuousSMul R M] [ContinuousSMul Ráµáµ’áµ– M]

variable [CompleteSpace R] [T2Space R] [T2Space M]

theorem exp_def_of_smul_comm (x : tsze R M) (hx : MulOpposite.op x.fst â€¢ x.snd = x.fst â€¢ x.snd) :
    exp ğ•œ x = inl (exp ğ•œ x.fst) + inr (exp ğ•œ x.fst â€¢ x.snd) := by
  simp_rw [exp, FormalMultilinearSeries.sum]
  refine' (hasSum_expSeries_of_smul_comm ğ•œ x hx _).tsum_eq
  exact expSeries_hasSum_exp _
#align triv_sq_zero_ext.exp_def_of_smul_comm TrivSqZeroExt.exp_def_of_smul_comm

@[simp]
theorem exp_inl (x : R) : exp ğ•œ (inl x : tsze R M) = inl (exp ğ•œ x) := by
  rw [exp_def_of_smul_comm, snd_inl, fst_inl, smul_zero, inr_zero, add_zero]
  Â· rw [snd_inl, fst_inl, smul_zero, smul_zero]
#align triv_sq_zero_ext.exp_inl TrivSqZeroExt.exp_inl

@[simp]
theorem exp_inr (m : M) : exp ğ•œ (inr m : tsze R M) = 1 + inr m := by
  rw [exp_def_of_smul_comm, snd_inr, fst_inr, exp_zero, one_smul, inl_one]
  Â· rw [snd_inr, fst_inr, MulOpposite.op_zero, zero_smul, zero_smul]
#align triv_sq_zero_ext.exp_inr TrivSqZeroExt.exp_inr

end NormedRing

section NormedCommRing

variable [IsROrC ğ•œ] [NormedCommRing R] [AddCommGroup M]

variable [NormedAlgebra ğ•œ R] [Module R M] [Module Ráµáµ’áµ– M] [IsCentralScalar R M]

variable [Module ğ•œ M] [IsScalarTower ğ•œ R M]

variable [TopologicalSpace M] [TopologicalRing R]

variable [TopologicalAddGroup M] [ContinuousSMul R M]

variable [CompleteSpace R] [T2Space R] [T2Space M]

theorem exp_def (x : tsze R M) : exp ğ•œ x = inl (exp ğ•œ x.fst) + inr (exp ğ•œ x.fst â€¢ x.snd) :=
  exp_def_of_smul_comm ğ•œ x (op_smul_eq_smul _ _)
#align triv_sq_zero_ext.exp_def TrivSqZeroExt.exp_def

@[simp]
theorem fst_exp (x : tsze R M) : fst (exp ğ•œ x) = exp ğ•œ x.fst := by
  rw [exp_def, fst_add, fst_inl, fst_inr, add_zero]
#align triv_sq_zero_ext.fst_exp TrivSqZeroExt.fst_exp

@[simp]
theorem snd_exp (x : tsze R M) : snd (exp ğ•œ x) = exp ğ•œ x.fst â€¢ x.snd := by
  rw [exp_def, snd_add, snd_inl, snd_inr, zero_add]
#align triv_sq_zero_ext.snd_exp TrivSqZeroExt.snd_exp

/-- Polar form of trivial-square-zero extension. -/
theorem eq_smul_exp_of_invertible (x : tsze R M) [Invertible x.fst] :
    x = x.fst â€¢ exp ğ•œ (â…Ÿ x.fst â€¢ inr x.snd) := by
  rw [â† inr_smul, exp_inr, smul_add, â† inl_one, â† inl_smul, â† inr_smul, smul_eq_mul, mul_one,
    smul_smul, mul_invOf_self, one_smul, inl_fst_add_inr_snd_eq]
#align triv_sq_zero_ext.eq_smul_exp_of_invertible TrivSqZeroExt.eq_smul_exp_of_invertible

end NormedCommRing

section NormedField

variable [IsROrC ğ•œ] [NormedField R] [AddCommGroup M]

variable [NormedAlgebra ğ•œ R] [Module R M] [Module Ráµáµ’áµ– M] [IsCentralScalar R M]

variable [Module ğ•œ M] [IsScalarTower ğ•œ R M]

variable [TopologicalSpace M] [TopologicalRing R]

variable [TopologicalAddGroup M] [ContinuousSMul R M]

variable [CompleteSpace R] [T2Space R] [T2Space M]

/-- More convenient version of `TrivSqZeroExt.eq_smul_exp_of_invertible` for when `R` is a
field. -/
theorem eq_smul_exp_of_ne_zero (x : tsze R M) (hx : x.fst â‰  0) :
    x = x.fst â€¢ exp ğ•œ (x.fstâ»Â¹ â€¢ inr x.snd) :=
  letI : Invertible x.fst := invertibleOfNonzero hx
  eq_smul_exp_of_invertible _ _
#align triv_sq_zero_ext.eq_smul_exp_of_ne_zero TrivSqZeroExt.eq_smul_exp_of_ne_zero

end NormedField

end TrivSqZeroExt
