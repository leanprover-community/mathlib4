/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.NormedSpace.lpSpace
import Mathlib.Analysis.NormedSpace.PiLp
import Mathlib.Topology.ContinuousFunction.Bounded

#align_import analysis.normed_space.lp_equiv from "leanprover-community/mathlib"@"6afc9b06856ad973f6a2619e3e8a0a8d537a58f2"

/-!
# Equivalences among $L^p$ spaces

In this file we collect a variety of equivalences among various $L^p$ spaces.  In particular,
when `Î±` is a `Fintype`, given `E : Î± â†’ Type u` and `p : â„â‰¥0âˆ`, there is a natural linear isometric
equivalence `lpPiLpâ‚—áµ¢â‚“ : lp E p â‰ƒâ‚—áµ¢ PiLp p E`. In addition, when `Î±` is a discrete topological
space, the bounded continuous functions `Î± â†’áµ‡ Î²` correspond exactly to `lp (Î» _, Î²) âˆ`. Here there
can be more structure, including ring and algebra structures, and we implement these equivalences
accordingly as well.

We keep this as a separate file so that the various $L^p$ space files don't import the others.

Recall that `PiLp` is just a type synonym for `Î  i, E i` but given a different metric and norm
structure, although the topological, uniform and bornological structures coincide definitionally.
These structures are only defined on `PiLp` for `Fintype Î±`, so there are no issues of convergence
to consider.

While `PreLp` is also a type synonym for `Î  i, E i`, it allows for infinite index types. On this
type there is a predicate `Memâ„“p` which says that the relevant `p`-norm is finite and `lp E p` is
the subtype of `PreLp` satisfying `Memâ„“p`.

## TODO

* Equivalence between `lp` and `MeasureTheory.Lp`, for `f : Î± â†’ E` (i.e., functions rather than
  pi-types) and the counting measure on `Î±`

-/


open scoped ENNReal

section LpPiLp

set_option linter.uppercaseLean3 false

variable {Î± : Type*} {E : Î± â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)] {p : â„â‰¥0âˆ}

/-- When `Î±` is `Finite`, every `f : PreLp E p` satisfies `Memâ„“p f p`. -/
theorem Memâ„“p.all [Finite Î±] (f : âˆ€ i, E i) : Memâ„“p f p := by
  rcases p.trichotomy with (rfl | rfl | _h)
  Â· exact memâ„“p_zero_iff.mpr { i : Î± | f i â‰  0 }.toFinite
  Â· exact memâ„“p_infty_iff.mpr (Set.Finite.bddAbove (Set.range fun i : Î± => â€–f iâ€–).toFinite)
  Â· cases nonempty_fintype Î±; exact memâ„“p_gen âŸ¨Finset.univ.sum _, hasSum_fintype _âŸ©
#align mem_â„“p.all Memâ„“p.all

variable [Fintype Î±]

/-- The canonical `Equiv` between `lp E p â‰ƒ PiLp p E` when `E : Î± â†’ Type u` with `[Fintype Î±]`. -/
def Equiv.lpPiLp : lp E p â‰ƒ PiLp p E where
  toFun f := â‡‘f
  invFun f := âŸ¨f, Memâ„“p.all fâŸ©
  left_inv _f := lp.ext <| funext fun _x => rfl
  right_inv _f := funext fun _x => rfl
#align equiv.lp_pi_Lp Equiv.lpPiLp

theorem coe_equiv_lpPiLp (f : lp E p) : Equiv.lpPiLp f = â‡‘f :=
  rfl
#align coe_equiv_lp_pi_Lp coe_equiv_lpPiLp

theorem coe_equiv_lpPiLp_symm (f : PiLp p E) : (Equiv.lpPiLp.symm f : âˆ€ i, E i) = f :=
  rfl
#align coe_equiv_lp_pi_Lp_symm coe_equiv_lpPiLp_symm

theorem equiv_lpPiLp_norm (f : lp E p) : â€–Equiv.lpPiLp fâ€– = â€–fâ€– := by
  rcases p.trichotomy with (rfl | rfl | h)
  Â· simp [Equiv.lpPiLp, PiLp.norm_eq_card, lp.norm_eq_card_dsupport]
  Â· rw [PiLp.norm_eq_ciSup, lp.norm_eq_ciSup]; rfl
  Â· rw [PiLp.norm_eq_sum h, lp.norm_eq_tsum_rpow h, tsum_fintype]; rfl
#align equiv_lp_pi_Lp_norm equiv_lpPiLp_norm

/-- The canonical `AddEquiv` between `lp E p` and `PiLp p E` when `E : Î± â†’ Type u` with
`[Fintype Î±]`. -/
def AddEquiv.lpPiLp : lp E p â‰ƒ+ PiLp p E :=
  { Equiv.lpPiLp with map_add' := fun _f _g => rfl }
#align add_equiv.lp_pi_Lp AddEquiv.lpPiLp

theorem coe_addEquiv_lpPiLp  (f : lp E p) : AddEquiv.lpPiLp f = â‡‘f :=
  rfl
#align coe_add_equiv_lp_pi_Lp coe_addEquiv_lpPiLp

theorem coe_addEquiv_lpPiLp_symm (f : PiLp p E) :
    (AddEquiv.lpPiLp.symm f : âˆ€ i, E i) = f :=
  rfl
#align coe_add_equiv_lp_pi_Lp_symm coe_addEquiv_lpPiLp_symm

section Equivâ‚—áµ¢

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] [âˆ€ i, NormedSpace ğ•œ (E i)]
variable (E)
/- porting note: Lean is unable to work with `lpPiLpâ‚—áµ¢` if `E` is implicit without
annotating with `(E := E)` everywhere, so we just make it explicit. This file has no
dependencies. -/

/-- The canonical `LinearIsometryEquiv` between `lp E p` and `PiLp p E` when `E : Î± â†’ Type u`
with `[Fintype Î±]` and `[Fact (1 â‰¤ p)]`. -/
noncomputable def lpPiLpâ‚—áµ¢ [Fact (1 â‰¤ p)] : lp E p â‰ƒâ‚—áµ¢[ğ•œ] PiLp p E :=
  { AddEquiv.lpPiLp with
    map_smul' := fun _k _f => rfl
    norm_map' := equiv_lpPiLp_norm }
#align lp_pi_Lpâ‚—áµ¢ lpPiLpâ‚—áµ¢â‚“
-- porting note: `#align`ed with an `â‚“` because `E` is now explicit, see above

variable {ğ•œ E}

theorem coe_lpPiLpâ‚—áµ¢ [Fact (1 â‰¤ p)] (f : lp E p) : (lpPiLpâ‚—áµ¢ E ğ•œ f : âˆ€ i, E i) = â‡‘f :=
  rfl
#align coe_lp_pi_Lpâ‚—áµ¢ coe_lpPiLpâ‚—áµ¢

theorem coe_lpPiLpâ‚—áµ¢_symm [Fact (1 â‰¤ p)] (f : PiLp p E) :
    ((lpPiLpâ‚—áµ¢ E ğ•œ).symm f : âˆ€ i, E i) = f :=
  rfl
#align coe_lp_pi_Lpâ‚—áµ¢_symm coe_lpPiLpâ‚—áµ¢_symm

end Equivâ‚—áµ¢

end LpPiLp

section LpBcf

open scoped BoundedContinuousFunction

open BoundedContinuousFunction

-- note: `R` and `A` are explicit because otherwise Lean has elaboration problems
variable {Î± E : Type*} (R A ğ•œ : Type*) [TopologicalSpace Î±] [DiscreteTopology Î±]

variable [NormedRing A] [NormOneClass A] [NontriviallyNormedField ğ•œ] [NormedAlgebra ğ•œ A]

variable [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NonUnitalNormedRing R]

section NormedAddCommGroup

/-- The canonical map between `lp (fun _ : Î± â†¦ E) âˆ` and `Î± â†’áµ‡ E` as an `AddEquiv`. -/
noncomputable def AddEquiv.lpBcf : lp (fun _ : Î± => E) âˆ â‰ƒ+ (Î± â†’áµ‡ E) where
  toFun f := ofNormedAddCommGroupDiscrete f â€–fâ€– <| le_ciSup (memâ„“p_infty_iff.mp f.prop)
  invFun f := âŸ¨â‡‘f, f.bddAbove_range_norm_compâŸ©
  left_inv _f := lp.ext rfl
  right_inv _f := BoundedContinuousFunction.ext fun _x => rfl
  map_add' _f _g := BoundedContinuousFunction.ext fun _x => rfl

#align add_equiv.lp_bcf AddEquiv.lpBcf

theorem coe_addEquiv_lpBcf (f : lp (fun _ : Î± => E) âˆ) : (AddEquiv.lpBcf f : Î± â†’ E) = f :=
  rfl
#align coe_add_equiv_lp_bcf coe_addEquiv_lpBcf

theorem coe_addEquiv_lpBcf_symm (f : Î± â†’áµ‡ E) : (AddEquiv.lpBcf.symm f : Î± â†’ E) = f :=
  rfl
#align coe_add_equiv_lp_bcf_symm coe_addEquiv_lpBcf_symm

variable (E)
/- porting note: Lean is unable to work with `lpPiLpâ‚—áµ¢` if `E` is implicit without
annotating with `(E := E)` everywhere, so we just make it explicit. This file has no
dependencies. -/

/-- The canonical map between `lp (fun _ : Î± â†¦ E) âˆ` and `Î± â†’áµ‡ E` as a `LinearIsometryEquiv`. -/
noncomputable def lpBcfâ‚—áµ¢ : lp (fun _ : Î± => E) âˆ â‰ƒâ‚—áµ¢[ğ•œ] Î± â†’áµ‡ E :=
  { AddEquiv.lpBcf with
    map_smul' := fun k f => rfl
    norm_map' := fun f => by simp only [norm_eq_iSup_norm, lp.norm_eq_ciSup]; rfl }
#align lp_bcfâ‚—áµ¢ lpBcfâ‚—áµ¢â‚“
-- porting note: `#align`ed with an `â‚“` because `E` is now explicit, see above

variable {ğ•œ E}

theorem coe_lpBcfâ‚—áµ¢ (f : lp (fun _ : Î± => E) âˆ) : (lpBcfâ‚—áµ¢ E ğ•œ f : Î± â†’ E) = f :=
  rfl
#align coe_lp_bcfâ‚—áµ¢ coe_lpBcfâ‚—áµ¢

theorem coe_lpBcfâ‚—áµ¢_symm (f : Î± â†’áµ‡ E) : ((lpBcfâ‚—áµ¢ E ğ•œ).symm f : Î± â†’ E) = f :=
  rfl
#align coe_lp_bcfâ‚—áµ¢_symm coe_lpBcfâ‚—áµ¢_symm

end NormedAddCommGroup

section RingAlgebra

/-- The canonical map between `lp (fun _ : Î± â†¦ R) âˆ` and `Î± â†’áµ‡ R` as a `RingEquiv`. -/
noncomputable def RingEquiv.lpBcf : lp (fun _ : Î± => R) âˆ â‰ƒ+* (Î± â†’áµ‡ R) :=
  { @AddEquiv.lpBcf _ R _ _ _ with
    map_mul' := fun _f _g => BoundedContinuousFunction.ext fun _x => rfl }
#align ring_equiv.lp_bcf RingEquiv.lpBcf

variable {R}

theorem coe_ringEquiv_lpBcf (f : lp (fun _ : Î± => R) âˆ) : (RingEquiv.lpBcf R f : Î± â†’ R) = f :=
  rfl
#align coe_ring_equiv_lp_bcf coe_ringEquiv_lpBcf

theorem coe_ringEquiv_lpBcf_symm (f : Î± â†’áµ‡ R) : ((RingEquiv.lpBcf R).symm f : Î± â†’ R) = f :=
  rfl
#align coe_ring_equiv_lp_bcf_symm coe_ringEquiv_lpBcf_symm

variable (Î±)

-- even `Î±` needs to be explicit here for elaboration
-- the `NormOneClass A` shouldn't really be necessary, but currently it is for
-- `one_memâ„“p_infty` to get the `Ring` instance on `lp`.
/-- The canonical map between `lp (fun _ : Î± â†¦ A) âˆ` and `Î± â†’áµ‡ A` as an `AlgEquiv`. -/
noncomputable def AlgEquiv.lpBcf : lp (fun _ : Î± => A) âˆ â‰ƒâ‚[ğ•œ] Î± â†’áµ‡ A :=
  { RingEquiv.lpBcf A with commutes' := fun _k => rfl }
#align alg_equiv.lp_bcf AlgEquiv.lpBcf

variable {Î± A ğ•œ}

theorem coe_algEquiv_lpBcf (f : lp (fun _ : Î± => A) âˆ) : (AlgEquiv.lpBcf Î± A ğ•œ f : Î± â†’ A) = f :=
  rfl
#align coe_alg_equiv_lp_bcf coe_algEquiv_lpBcf

theorem coe_algEquiv_lpBcf_symm (f : Î± â†’áµ‡ A) : ((AlgEquiv.lpBcf Î± A ğ•œ).symm f : Î± â†’ A) = f :=
  rfl
#align coe_alg_equiv_lp_bcf_symm coe_algEquiv_lpBcf_symm

end RingAlgebra

end LpBcf
