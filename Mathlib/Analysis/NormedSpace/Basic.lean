/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import Mathlib.Algebra.Algebra.Pi
import Mathlib.Algebra.Algebra.RestrictScalars
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.Normed.MulAction
import Mathlib.Data.Real.Sqrt
import Mathlib.Topology.Algebra.Module.Basic

#align_import analysis.normed_space.basic from "leanprover-community/mathlib"@"bc91ed7093bf098d253401e69df601fc33dde156"

/-!
# Normed spaces

In this file we define (semi)normed spaces and algebras. We also prove some theorems
about these definitions.
-/


variable {Œ± : Type*} {Œ≤ : Type*} {Œ≥ : Type*} {Œπ : Type*}

open Filter Metric Function Set Topology BigOperators NNReal ENNReal uniformity

section SeminormedAddCommGroup

section Prio

-- set_option extends_priority 920 -- Porting note: option unsupported

-- Here, we set a rather high priority for the instance `[NormedSpace Œ± Œ≤] : Module Œ± Œ≤`
-- to take precedence over `Semiring.toModule` as this leads to instance paths with better
-- unification properties.
/-- A normed space over a normed field is a vector space endowed with a norm which satisfies the
equality `‚Äñc ‚Ä¢ x‚Äñ = ‚Äñc‚Äñ ‚Äñx‚Äñ`. We require only `‚Äñc ‚Ä¢ x‚Äñ ‚â§ ‚Äñc‚Äñ ‚Äñx‚Äñ` in the definition, then prove
`‚Äñc ‚Ä¢ x‚Äñ = ‚Äñc‚Äñ ‚Äñx‚Äñ` in `norm_smul`.

Note that since this requires `SeminormedAddCommGroup` and not `NormedAddCommGroup`, this
typeclass can be used for "semi normed spaces" too, just as `Module` can be used for
"semi modules". -/
class NormedSpace (Œ± : Type*) (Œ≤ : Type*) [NormedField Œ±] [SeminormedAddCommGroup Œ≤] extends
  Module Œ± Œ≤ where
  norm_smul_le : ‚àÄ (a : Œ±) (b : Œ≤), ‚Äña ‚Ä¢ b‚Äñ ‚â§ ‚Äña‚Äñ * ‚Äñb‚Äñ
#align normed_space NormedSpace

attribute [inherit_doc NormedSpace] NormedSpace.norm_smul_le

end Prio

variable [NormedField Œ±] [SeminormedAddCommGroup Œ≤]

-- see Note [lower instance priority]
instance (priority := 100) NormedSpace.boundedSMul [NormedSpace Œ± Œ≤] : BoundedSMul Œ± Œ≤ :=
  BoundedSMul.of_norm_smul_le NormedSpace.norm_smul_le
#align normed_space.has_bounded_smul NormedSpace.boundedSMul

instance NormedField.toNormedSpace : NormedSpace Œ± Œ± where norm_smul_le a b := norm_mul_le a b
#align normed_field.to_normed_space NormedField.toNormedSpace

-- shortcut instance
instance NormedField.to_boundedSMul : BoundedSMul Œ± Œ± :=
  NormedSpace.boundedSMul
#align normed_field.to_has_bounded_smul NormedField.to_boundedSMul

theorem norm_zsmul (Œ±) [NormedField Œ±] [NormedSpace Œ± Œ≤] (n : ‚Ñ§) (x : Œ≤) :
    ‚Äñn ‚Ä¢ x‚Äñ = ‚Äñ(n : Œ±)‚Äñ * ‚Äñx‚Äñ := by rw [‚Üê norm_smul, ‚Üê Int.smul_one_eq_coe, smul_assoc, one_smul]
#align norm_zsmul norm_zsmul

@[simp]
theorem abs_norm (z : Œ≤) : |‚Äñz‚Äñ| = ‚Äñz‚Äñ := abs_of_nonneg <| norm_nonneg _
#align abs_norm abs_norm

theorem inv_norm_smul_mem_closed_unit_ball [NormedSpace ‚Ñù Œ≤] (x : Œ≤) :
    ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x ‚àà closedBall (0 : Œ≤) 1 := by
  simp only [mem_closedBall_zero_iff, norm_smul, norm_inv, norm_norm, ‚Üê _root_.div_eq_inv_mul,
    div_self_le_one]
#align inv_norm_smul_mem_closed_unit_ball inv_norm_smul_mem_closed_unit_ball

theorem norm_smul_of_nonneg [NormedSpace ‚Ñù Œ≤] {t : ‚Ñù} (ht : 0 ‚â§ t) (x : Œ≤) : ‚Äñt ‚Ä¢ x‚Äñ = t * ‚Äñx‚Äñ := by
  rw [norm_smul, Real.norm_eq_abs, abs_of_nonneg ht]
#align norm_smul_of_nonneg norm_smul_of_nonneg

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace Œ± E]

variable {F : Type*} [SeminormedAddCommGroup F] [NormedSpace Œ± F]

theorem eventually_nhds_norm_smul_sub_lt (c : Œ±) (x : E) {Œµ : ‚Ñù} (h : 0 < Œµ) :
    ‚àÄ·∂† y in ùìù x, ‚Äñc ‚Ä¢ (y - x)‚Äñ < Œµ :=
  have : Tendsto (fun y => ‚Äñc ‚Ä¢ (y - x)‚Äñ) (ùìù x) (ùìù 0) :=
    ((continuous_id.sub continuous_const).const_smul _).norm.tendsto' _ _ (by simp)
  this.eventually (gt_mem_nhds h)
#align eventually_nhds_norm_smul_sub_lt eventually_nhds_norm_smul_sub_lt

theorem Filter.Tendsto.zero_smul_isBoundedUnder_le {f : Œπ ‚Üí Œ±} {g : Œπ ‚Üí E} {l : Filter Œπ}
    (hf : Tendsto f l (ùìù 0)) (hg : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (Norm.norm ‚àò g)) :
    Tendsto (fun x => f x ‚Ä¢ g x) l (ùìù 0) :=
  hf.op_zero_isBoundedUnder_le hg (¬∑ ‚Ä¢ ¬∑) norm_smul_le
#align filter.tendsto.zero_smul_is_bounded_under_le Filter.Tendsto.zero_smul_isBoundedUnder_le

theorem Filter.IsBoundedUnder.smul_tendsto_zero {f : Œπ ‚Üí Œ±} {g : Œπ ‚Üí E} {l : Filter Œπ}
    (hf : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f)) (hg : Tendsto g l (ùìù 0)) :
    Tendsto (fun x => f x ‚Ä¢ g x) l (ùìù 0) :=
  hg.op_zero_isBoundedUnder_le hf (flip (¬∑ ‚Ä¢ ¬∑)) fun x y =>
    (norm_smul_le y x).trans_eq (mul_comm _ _)
#align filter.is_bounded_under.smul_tendsto_zero Filter.IsBoundedUnder.smul_tendsto_zero

theorem closure_ball [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    closure (ball x r) = closedBall x r := by
  refine' Subset.antisymm closure_ball_subset_closedBall fun y hy => _
  have : ContinuousWithinAt (fun c : ‚Ñù => c ‚Ä¢ (y - x) + x) (Ico 0 1) 1 :=
    ((continuous_id.smul continuous_const).add continuous_const).continuousWithinAt
  convert this.mem_closure _ _
  ¬∑ rw [one_smul, sub_add_cancel]
  ¬∑ simp [closure_Ico zero_ne_one, zero_le_one]
  ¬∑ rintro c ‚ü®hc0, hc1‚ü©
    rw [mem_ball, dist_eq_norm, add_sub_cancel, norm_smul, Real.norm_eq_abs, abs_of_nonneg hc0,
      mul_comm, ‚Üê mul_one r]
    rw [mem_closedBall, dist_eq_norm] at hy
    replace hr : 0 < r := ((norm_nonneg _).trans hy).lt_of_ne hr.symm
    apply mul_lt_mul' <;> assumption
#align closure_ball closure_ball

theorem frontier_ball [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    frontier (ball x r) = sphere x r := by
  rw [frontier, closure_ball x hr, isOpen_ball.interior_eq, closedBall_diff_ball]
#align frontier_ball frontier_ball

theorem interior_closedBall [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    interior (closedBall x r) = ball x r := by
  cases' hr.lt_or_lt with hr hr
  ¬∑ rw [closedBall_eq_empty.2 hr, ball_eq_empty.2 hr.le, interior_empty]
  refine' Subset.antisymm _ ball_subset_interior_closedBall
  intro y hy
  rcases (mem_closedBall.1 <| interior_subset hy).lt_or_eq with (hr | rfl)
  ¬∑ exact hr
  set f : ‚Ñù ‚Üí E := fun c : ‚Ñù => c ‚Ä¢ (y - x) + x
  suffices f ‚Åª¬π' closedBall x (dist y x) ‚äÜ Icc (-1) 1 by
    have hfc : Continuous f := (continuous_id.smul continuous_const).add continuous_const
    have hf1 : (1 : ‚Ñù) ‚àà f ‚Åª¬π' interior (closedBall x <| dist y x) := by simpa
    have h1 : (1 : ‚Ñù) ‚àà interior (Icc (-1 : ‚Ñù) 1) :=
      interior_mono this (preimage_interior_subset_interior_preimage hfc hf1)
    contrapose h1
    simp
  intro c hc
  rw [mem_Icc, ‚Üê abs_le, ‚Üê Real.norm_eq_abs, ‚Üê mul_le_mul_right hr]
  simpa [dist_eq_norm, norm_smul] using hc
#align interior_closed_ball interior_closedBall

theorem frontier_closedBall [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall x hr, closedBall_diff_ball]
#align frontier_closed_ball frontier_closedBall

theorem interior_sphere [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    interior (sphere x r) = ‚àÖ := by
  rw [‚Üê frontier_closedBall x hr, interior_frontier isClosed_ball]
#align interior_sphere interior_sphere

theorem frontier_sphere [NormedSpace ‚Ñù E] (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere x hr, diff_empty]
#align frontier_sphere frontier_sphere

instance NormedSpace.discreteTopology_zmultiples
    {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñö E] (e : E) :
    DiscreteTopology <| AddSubgroup.zmultiples e := by
  rcases eq_or_ne e 0 with (rfl | he)
  ¬∑ rw [AddSubgroup.zmultiples_zero_eq_bot]
    refine Subsingleton.discreteTopology (Œ± := ‚Üë(‚ä• : Subspace ‚Ñö E))
  ¬∑ rw [discreteTopology_iff_open_singleton_zero, isOpen_induced_iff]
    refine' ‚ü®Metric.ball 0 ‚Äñe‚Äñ, Metric.isOpen_ball, _‚ü©
    ext ‚ü®x, hx‚ü©
    obtain ‚ü®k, rfl‚ü© := AddSubgroup.mem_zmultiples_iff.mp hx
    rw [mem_preimage, mem_ball_zero_iff, AddSubgroup.coe_mk, mem_singleton_iff, Subtype.ext_iff,
      AddSubgroup.coe_mk, AddSubgroup.coe_zero, norm_zsmul ‚Ñö k e, Int.norm_cast_rat,
      Int.norm_eq_abs, mul_lt_iff_lt_one_left (norm_pos_iff.mpr he), ‚Üê
      @Int.cast_one ‚Ñù _, Int.cast_lt, Int.abs_lt_one_iff, smul_eq_zero, or_iff_left he]

open NormedField

instance ULift.normedSpace : NormedSpace Œ± (ULift E) :=
  { ULift.seminormedAddCommGroup (E := E), ULift.module' with
    norm_smul_le := fun s x => (norm_smul_le s x.down : _) }

/-- The product of two normed spaces is a normed space, with the sup norm. -/
instance Prod.normedSpace : NormedSpace Œ± (E √ó F) :=
  { Prod.seminormedAddCommGroup (E := E) (F := F), Prod.instModule with
    norm_smul_le := fun s x => by
      simp only [norm_smul, Prod.norm_def, Prod.smul_snd, Prod.smul_fst,
        mul_max_of_nonneg, norm_nonneg, le_rfl] }
#align prod.normed_space Prod.normedSpace

/-- The product of finitely many normed spaces is a normed space, with the sup norm. -/
instance Pi.normedSpace {E : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, SeminormedAddCommGroup (E i)]
    [‚àÄ i, NormedSpace Œ± (E i)] : NormedSpace Œ± (‚àÄ i, E i) where
  norm_smul_le a f := by
    simp_rw [‚Üê coe_nnnorm, ‚Üê NNReal.coe_mul, NNReal.coe_le_coe, Pi.nnnorm_def,
      NNReal.mul_finset_sup]
    exact Finset.sup_mono_fun fun _ _ => norm_smul_le a _
#align pi.normed_space Pi.normedSpace

instance MulOpposite.normedSpace : NormedSpace Œ± E·µê·µí·µñ :=
  { MulOpposite.seminormedAddCommGroup (E := E·µê·µí·µñ), MulOpposite.module _ with
    norm_smul_le := fun s x => norm_smul_le s x.unop }
#align mul_opposite.normed_space MulOpposite.normedSpace

/-- A subspace of a normed space is also a normed space, with the restriction of the norm. -/
instance Submodule.normedSpace {ùïú R : Type*} [SMul ùïú R] [NormedField ùïú] [Ring R] {E : Type*}
    [SeminormedAddCommGroup E] [NormedSpace ùïú E] [Module R E] [IsScalarTower ùïú R E]
    (s : Submodule R E) : NormedSpace ùïú s where norm_smul_le c x := norm_smul_le c (x : E)
#align submodule.normed_space Submodule.normedSpace

end SeminormedAddCommGroup

/-- A linear map from a `Module` to a `NormedSpace` induces a `NormedSpace` structure on the
domain, using the `SeminormedAddCommGroup.induced` norm.

See note [reducible non-instances] -/
@[reducible]
def NormedSpace.induced {F : Type*} (Œ± Œ≤ Œ≥ : Type*) [NormedField Œ±] [AddCommGroup Œ≤] [Module Œ± Œ≤]
    [SeminormedAddCommGroup Œ≥] [NormedSpace Œ± Œ≥] [LinearMapClass F Œ± Œ≤ Œ≥] (f : F) :
    @NormedSpace Œ± Œ≤ _ (SeminormedAddCommGroup.induced Œ≤ Œ≥ f) := by
  -- Porting note: trouble inferring SeminormedAddCommGroup Œ≤ and Module Œ± Œ≤
  -- unfolding the induced semi-norm is fiddly
  refine @NormedSpace.mk (Œ± := Œ±) (Œ≤ := Œ≤) _ ?_ ?_ ?_
  ¬∑ infer_instance
  ¬∑ intro a b
    change ‚Äñ(‚áëf) (a ‚Ä¢ b)‚Äñ ‚â§ ‚Äña‚Äñ * ‚Äñ(‚áëf) b‚Äñ
    exact (map_smul f a b).symm ‚ñ∏ norm_smul_le a (f b)
#align normed_space.induced NormedSpace.induced

section NormedAddCommGroup

variable [NormedField Œ±]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace Œ± E]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace Œ± F]

open NormedField

/-- While this may appear identical to `NormedSpace.toModule`, it contains an implicit argument
involving `NormedAddCommGroup.toSeminormedAddCommGroup` that typeclass inference has trouble
inferring.

Specifically, the following instance cannot be found without this `NormedSpace.toModule'`:
```lean
example
  (ùïú Œπ : Type*) (E : Œπ ‚Üí Type*)
  [NormedField ùïú] [Œ† i, NormedAddCommGroup (E i)] [Œ† i, NormedSpace ùïú (E i)] :
  Œ† i, Module ùïú (E i) := by infer_instance
```

[This Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/Typeclass.20resolution.20under.20binders/near/245151099)
gives some more context. -/
instance (priority := 100) NormedSpace.toModule' : Module Œ± F :=
  NormedSpace.toModule
#align normed_space.to_module' NormedSpace.toModule'

section Surj

variable (E)

variable [NormedSpace ‚Ñù E] [Nontrivial E]

theorem exists_norm_eq {c : ‚Ñù} (hc : 0 ‚â§ c) : ‚àÉ x : E, ‚Äñx‚Äñ = c := by
  rcases exists_ne (0 : E) with ‚ü®x, hx‚ü©
  rw [‚Üê norm_ne_zero_iff] at hx
  use c ‚Ä¢ ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x
  simp [norm_smul, Real.norm_of_nonneg hc, abs_of_nonneg hc, inv_mul_cancel hx]
#align exists_norm_eq exists_norm_eq

@[simp]
theorem range_norm : range (norm : E ‚Üí ‚Ñù) = Ici 0 :=
  Subset.antisymm (range_subset_iff.2 norm_nonneg) fun _ => exists_norm_eq E
#align range_norm range_norm

theorem nnnorm_surjective : Surjective (nnnorm : E ‚Üí ‚Ñù‚â•0) := fun c =>
  (exists_norm_eq E c.coe_nonneg).imp fun _ h => NNReal.eq h
#align nnnorm_surjective nnnorm_surjective

@[simp]
theorem range_nnnorm : range (nnnorm : E ‚Üí ‚Ñù‚â•0) = univ :=
  (nnnorm_surjective E).range_eq
#align range_nnnorm range_nnnorm

end Surj

/-- If `E` is a nontrivial topological module over `‚Ñù`, then `E` has no isolated points.
This is a particular case of `Module.punctured_nhds_neBot`. -/
instance Real.punctured_nhds_module_neBot {E : Type*} [AddCommGroup E] [TopologicalSpace E]
    [ContinuousAdd E] [Nontrivial E] [Module ‚Ñù E] [ContinuousSMul ‚Ñù E] (x : E) : NeBot (ùìù[‚â†] x) :=
  Module.punctured_nhds_neBot ‚Ñù E x
#align real.punctured_nhds_module_ne_bot Real.punctured_nhds_module_neBot

theorem interior_closedBall' [NormedSpace ‚Ñù E] [Nontrivial E] (x : E) (r : ‚Ñù) :
    interior (closedBall x r) = ball x r := by
  rcases eq_or_ne r 0 with (rfl | hr)
  ¬∑ rw [closedBall_zero, ball_zero, interior_singleton]
  ¬∑ exact interior_closedBall x hr
#align interior_closed_ball' interior_closedBall'

theorem frontier_closedBall' [NormedSpace ‚Ñù E] [Nontrivial E] (x : E) (r : ‚Ñù) :
    frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall' x r, closedBall_diff_ball]
#align frontier_closed_ball' frontier_closedBall'

@[simp]
theorem interior_sphere' [NormedSpace ‚Ñù E] [Nontrivial E] (x : E) (r : ‚Ñù) :
    interior (sphere x r) = ‚àÖ := by rw [‚Üê frontier_closedBall' x, interior_frontier isClosed_ball]
#align interior_sphere' interior_sphere'

@[simp]
theorem frontier_sphere' [NormedSpace ‚Ñù E] [Nontrivial E] (x : E) (r : ‚Ñù) :
    frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere' x, diff_empty]
#align frontier_sphere' frontier_sphere'

end NormedAddCommGroup

section NontriviallyNormedSpace

variable (ùïú E : Type*) [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
  [Nontrivial E]

/-- If `E` is a nontrivial normed space over a nontrivially normed field `ùïú`, then `E` is unbounded:
for any `c : ‚Ñù`, there exists a vector `x : E` with norm strictly greater than `c`. -/
theorem NormedSpace.exists_lt_norm (c : ‚Ñù) : ‚àÉ x : E, c < ‚Äñx‚Äñ := by
  rcases exists_ne (0 : E) with ‚ü®x, hx‚ü©
  rcases NormedField.exists_lt_norm ùïú (c / ‚Äñx‚Äñ) with ‚ü®r, hr‚ü©
  use r ‚Ä¢ x
  rwa [norm_smul, ‚Üê _root_.div_lt_iff]
  rwa [norm_pos_iff]
#align normed_space.exists_lt_norm NormedSpace.exists_lt_norm

protected theorem NormedSpace.unboundedSpace : UnboundedSpace E :=
  ‚ü®fun h ‚Ü¶
    let ‚ü®R, hR‚ü© := h.exists_norm_le
    let ‚ü®x, hx‚ü© := NormedSpace.exists_lt_norm ùïú E R
    hx.not_le (hR x trivial)‚ü©
#align normed_space.unbounded_univ NormedSpace.unboundedSpace

/-- A normed vector space over a nontrivially normed field is a noncompact space. This cannot be
an instance because in order to apply it, Lean would have to search for `NormedSpace ùïú E` with
unknown `ùïú`. We register this as an instance in two cases: `ùïú = E` and `ùïú = ‚Ñù`. -/
protected theorem NormedSpace.noncompactSpace : NoncompactSpace E :=
  have := NormedSpace.unboundedSpace ùïú E; inferInstance
#align normed_space.noncompact_space NormedSpace.noncompactSpace

instance (priority := 100) NontriviallyNormedField.unboundedSpace : UnboundedSpace ùïú :=
  NormedSpace.unboundedSpace ùïú ùïú
#align nontrivially_normed_field.noncompact_space NontriviallyNormedField.unboundedSpace

instance (priority := 100) RealNormedSpace.unboundedSpace [NormedSpace ‚Ñù E] : UnboundedSpace E :=
  NormedSpace.unboundedSpace ‚Ñù E
#align real_normed_space.noncompact_space RealNormedSpace.unboundedSpace

end NontriviallyNormedSpace

section NormedAlgebra

/-- A normed algebra `ùïú'` over `ùïú` is normed module that is also an algebra.

See the implementation notes for `Algebra` for a discussion about non-unital algebras. Following
the strategy there, a non-unital *normed* algebra can be written as:
```lean
variables [NormedField ùïú] [NonunitalSeminormedRing ùïú']
variables [NormedModule ùïú ùïú'] [SMulCommClass ùïú ùïú' ùïú'] [IsScalarTower ùïú ùïú' ùïú']
```
-/
class NormedAlgebra (ùïú : Type*) (ùïú' : Type*) [NormedField ùïú] [SeminormedRing ùïú'] extends
  Algebra ùïú ùïú' where
  norm_smul_le : ‚àÄ (r : ùïú) (x : ùïú'), ‚Äñr ‚Ä¢ x‚Äñ ‚â§ ‚Äñr‚Äñ * ‚Äñx‚Äñ
#align normed_algebra NormedAlgebra

attribute [inherit_doc NormedAlgebra] NormedAlgebra.norm_smul_le

variable {ùïú : Type*} (ùïú' : Type*) [NormedField ùïú] [SeminormedRing ùïú'] [NormedAlgebra ùïú ùïú']

instance (priority := 100) NormedAlgebra.toNormedSpace : NormedSpace ùïú ùïú' :=
  -- Porting note: previous Lean could figure out what we were extending
  { NormedAlgebra.toAlgebra.toModule with
  norm_smul_le := NormedAlgebra.norm_smul_le }
#align normed_algebra.to_normed_space NormedAlgebra.toNormedSpace

/-- While this may appear identical to `NormedAlgebra.toNormedSpace`, it contains an implicit
argument involving `NormedRing.toSeminormedRing` that typeclass inference has trouble inferring.

Specifically, the following instance cannot be found without this `NormedSpace.toModule'`:
```lean
example
  (ùïú Œπ : Type*) (E : Œπ ‚Üí Type*)
  [NormedField ùïú] [Œ† i, NormedRing (E i)] [Œ† i, NormedAlgebra ùïú (E i)] :
  Œ† i, Module ùïú (E i) := by infer_instance
```

See `NormedSpace.toModule'` for a similar situation. -/
instance (priority := 100) NormedAlgebra.toNormedSpace' {ùïú'} [NormedRing ùïú'] [NormedAlgebra ùïú ùïú'] :
    NormedSpace ùïú ùïú' := by infer_instance
#align normed_algebra.to_normed_space' NormedAlgebra.toNormedSpace'

theorem norm_algebraMap (x : ùïú) : ‚ÄñalgebraMap ùïú ùïú' x‚Äñ = ‚Äñx‚Äñ * ‚Äñ(1 : ùïú')‚Äñ := by
  rw [Algebra.algebraMap_eq_smul_one]
  exact norm_smul _ _
#align norm_algebra_map norm_algebraMap

theorem nnnorm_algebraMap (x : ùïú) : ‚ÄñalgebraMap ùïú ùïú' x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä * ‚Äñ(1 : ùïú')‚Äñ‚Çä :=
  Subtype.ext <| norm_algebraMap ùïú' x
#align nnnorm_algebra_map nnnorm_algebraMap

@[simp]
theorem norm_algebraMap' [NormOneClass ùïú'] (x : ùïú) : ‚ÄñalgebraMap ùïú ùïú' x‚Äñ = ‚Äñx‚Äñ := by
  rw [norm_algebraMap, norm_one, mul_one]
#align norm_algebra_map' norm_algebraMap'

@[simp]
theorem nnnorm_algebraMap' [NormOneClass ùïú'] (x : ùïú) : ‚ÄñalgebraMap ùïú ùïú' x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  Subtype.ext <| norm_algebraMap' _ _
#align nnnorm_algebra_map' nnnorm_algebraMap'

section NNReal

variable [NormOneClass ùïú'] [NormedAlgebra ‚Ñù ùïú']

@[simp]
theorem norm_algebraMap_nNReal (x : ‚Ñù‚â•0) : ‚ÄñalgebraMap ‚Ñù‚â•0 ùïú' x‚Äñ = x :=
  (norm_algebraMap' ùïú' (x : ‚Ñù)).symm ‚ñ∏ Real.norm_of_nonneg x.prop
#align norm_algebra_map_nnreal norm_algebraMap_nNReal

@[simp]
theorem nnnorm_algebraMap_nNReal (x : ‚Ñù‚â•0) : ‚ÄñalgebraMap ‚Ñù‚â•0 ùïú' x‚Äñ‚Çä = x :=
  Subtype.ext <| norm_algebraMap_nNReal ùïú' x
#align nnnorm_algebra_map_nnreal nnnorm_algebraMap_nNReal

end NNReal

variable (ùïú)

/-- In a normed algebra, the inclusion of the base field in the extended field is an isometry. -/
theorem algebraMap_isometry [NormOneClass ùïú'] : Isometry (algebraMap ùïú ùïú') := by
  refine' Isometry.of_dist_eq fun x y => _
  rw [dist_eq_norm, dist_eq_norm, ‚Üê RingHom.map_sub, norm_algebraMap']
#align algebra_map_isometry algebraMap_isometry

instance NormedAlgebra.id : NormedAlgebra ùïú ùïú :=
  { NormedField.toNormedSpace, Algebra.id ùïú with }
#align normed_algebra.id NormedAlgebra.id

-- Porting note: cannot synth scalar tower ‚Ñö ‚Ñù k
/-- Any normed characteristic-zero division ring that is a normed algebra over the reals is also a
normed algebra over the rationals.

Phrased another way, if `ùïú` is a normed algebra over the reals, then `AlgebraRat` respects that
norm. -/
instance normedAlgebraRat {ùïú} [NormedDivisionRing ùïú] [CharZero ùïú] [NormedAlgebra ‚Ñù ùïú] :
    NormedAlgebra ‚Ñö ùïú where
  norm_smul_le q x := by
    rw [‚Üê smul_one_smul ‚Ñù q x, Rat.smul_one_eq_coe, norm_smul, Rat.norm_cast_real]
#align normed_algebra_rat normedAlgebraRat

instance PUnit.normedAlgebra : NormedAlgebra ùïú PUnit where
  norm_smul_le q _ := by simp only [norm_eq_zero, mul_zero, le_refl]
#align punit.normed_algebra PUnit.normedAlgebra

instance : NormedAlgebra ùïú (ULift ùïú') :=
  { ULift.normedSpace, ULift.algebra with }

/-- The product of two normed algebras is a normed algebra, with the sup norm. -/
instance Prod.normedAlgebra {E F : Type*} [SeminormedRing E] [SeminormedRing F] [NormedAlgebra ùïú E]
    [NormedAlgebra ùïú F] : NormedAlgebra ùïú (E √ó F) :=
  { Prod.normedSpace, Prod.algebra ùïú E F with }
#align prod.normed_algebra Prod.normedAlgebra

-- Porting note: Lean 3 could synth the algebra instances for Pi Pr
/-- The product of finitely many normed algebras is a normed algebra, with the sup norm. -/
instance Pi.normedAlgebra {E : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, SeminormedRing (E i)]
    [‚àÄ i, NormedAlgebra ùïú (E i)] : NormedAlgebra ùïú (‚àÄ i, E i) :=
  { Pi.normedSpace, Pi.algebra _ E with }
#align pi.normed_algebra Pi.normedAlgebra

variable {E : Type*} [SeminormedRing E] [NormedAlgebra ùïú E]

instance MulOpposite.normedAlgebra {E : Type*} [SeminormedRing E] [NormedAlgebra ùïú E] :
    NormedAlgebra ùïú E·µê·µí·µñ :=
  { MulOpposite.normedSpace, MulOpposite.instAlgebra with }

#align mul_opposite.normed_algebra MulOpposite.normedAlgebra

end NormedAlgebra

/-- A non-unital algebra homomorphism from an `Algebra` to a `NormedAlgebra` induces a
`NormedAlgebra` structure on the domain, using the `SeminormedRing.induced` norm.

See note [reducible non-instances] -/
@[reducible]
def NormedAlgebra.induced {F : Type*} (Œ± Œ≤ Œ≥ : Type*) [NormedField Œ±] [Ring Œ≤] [Algebra Œ± Œ≤]
    [SeminormedRing Œ≥] [NormedAlgebra Œ± Œ≥] [NonUnitalAlgHomClass F Œ± Œ≤ Œ≥] (f : F) :
    @NormedAlgebra Œ± Œ≤ _ (SeminormedRing.induced Œ≤ Œ≥ f) := by
  -- Porting note: trouble with SeminormedRing Œ≤, Algebra Œ± Œ≤, and unfolding seminorm
  refine @NormedAlgebra.mk (ùïú := Œ±) (ùïú' := Œ≤) _ ?_ ?_ ?_
  ¬∑ infer_instance
  ¬∑ intro a b
    change ‚Äñ(‚áëf) (a ‚Ä¢ b)‚Äñ ‚â§ ‚Äña‚Äñ * ‚Äñ(‚áëf) b‚Äñ
    exact (map_smul f a b).symm ‚ñ∏ norm_smul_le a (f b)
#align normed_algebra.induced NormedAlgebra.induced

-- Porting note: failed to synth NonunitalAlgHomClass
instance Subalgebra.toNormedAlgebra {ùïú A : Type*} [SeminormedRing A] [NormedField ùïú]
    [NormedAlgebra ùïú A] (S : Subalgebra ùïú A) : NormedAlgebra ùïú S :=
  @NormedAlgebra.induced _ ùïú S A _ (SubringClass.toRing S) _ _ _ _ S.val
#align subalgebra.to_normed_algebra Subalgebra.toNormedAlgebra

section RestrictScalars

variable (ùïú : Type*) (ùïú' : Type*) [NormedField ùïú] [NormedField ùïú'] [NormedAlgebra ùïú ùïú']
  (E : Type*) [SeminormedAddCommGroup E] [NormedSpace ùïú' E]

instance {ùïú : Type*} {ùïú' : Type*} {E : Type*} [I : SeminormedAddCommGroup E] :
    SeminormedAddCommGroup (RestrictScalars ùïú ùïú' E) :=
  I

instance {ùïú : Type*} {ùïú' : Type*} {E : Type*} [I : NormedAddCommGroup E] :
    NormedAddCommGroup (RestrictScalars ùïú ùïú' E) :=
  I

/-- If `E` is a normed space over `ùïú'` and `ùïú` is a normed algebra over `ùïú'`, then
`RestrictScalars.module` is additionally a `NormedSpace`. -/
instance RestrictScalars.normedSpace : NormedSpace ùïú (RestrictScalars ùïú ùïú' E) :=
  { RestrictScalars.module ùïú ùïú' E with
    norm_smul_le := fun c x =>
      (norm_smul_le (algebraMap ùïú ùïú' c) (_ : E)).trans_eq <| by rw [norm_algebraMap'] }

-- If you think you need this, consider instead reproducing `RestrictScalars.lsmul`
-- appropriately modified here.
/-- The action of the original normed_field on `RestrictScalars ùïú ùïú' E`.
This is not an instance as it would be contrary to the purpose of `RestrictScalars`.
-/
def Module.RestrictScalars.normedSpaceOrig {ùïú : Type*} {ùïú' : Type*} {E : Type*} [NormedField ùïú']
    [SeminormedAddCommGroup E] [I : NormedSpace ùïú' E] : NormedSpace ùïú' (RestrictScalars ùïú ùïú' E) :=
  I
#align module.restrict_scalars.normed_space_orig Module.RestrictScalars.normedSpaceOrig

/-- Warning: This declaration should be used judiciously.
Please consider using `IsScalarTower` and/or `RestrictScalars ùïú ùïú' E` instead.

This definition allows the `RestrictScalars.normedSpace` instance to be put directly on `E`
rather on `RestrictScalars ùïú ùïú' E`. This would be a very bad instance; both because `ùïú'` cannot be
inferred, and because it is likely to create instance diamonds.
-/
def NormedSpace.restrictScalars : NormedSpace ùïú E :=
  RestrictScalars.normedSpace _ ùïú' _
#align normed_space.restrict_scalars NormedSpace.restrictScalars

end RestrictScalars
