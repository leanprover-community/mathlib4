/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import Mathlib.Algebra.Algebra.Pi
import Mathlib.Algebra.Algebra.RestrictScalars
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.Normed.MulAction
import Mathlib.Data.Real.Sqrt
import Mathlib.Topology.Algebra.Module.Basic

#align_import analysis.normed_space.basic from "leanprover-community/mathlib"@"bc91ed7093bf098d253401e69df601fc33dde156"

/-!
# Normed spaces

In this file we define (semi)normed spaces and algebras. We also prove some theorems
about these definitions.
-/


variable {Î± : Type*} {Î² : Type*} {Î³ : Type*} {Î¹ : Type*}

open Filter Metric Function Set Topology Bornology
open scoped BigOperators NNReal ENNReal uniformity

section SeminormedAddCommGroup

section Prio

-- set_option extends_priority 920 -- Porting note: option unsupported

-- Here, we set a rather high priority for the instance `[NormedSpace Î± Î²] : Module Î± Î²`
-- to take precedence over `Semiring.toModule` as this leads to instance paths with better
-- unification properties.
/-- A normed space over a normed field is a vector space endowed with a norm which satisfies the
equality `â€–c â€¢ xâ€– = â€–câ€– â€–xâ€–`. We require only `â€–c â€¢ xâ€– â‰¤ â€–câ€– â€–xâ€–` in the definition, then prove
`â€–c â€¢ xâ€– = â€–câ€– â€–xâ€–` in `norm_smul`.

Note that since this requires `SeminormedAddCommGroup` and not `NormedAddCommGroup`, this
typeclass can be used for "semi normed spaces" too, just as `Module` can be used for
"semi modules". -/
class NormedSpace (Î± : Type*) (Î² : Type*) [NormedField Î±] [SeminormedAddCommGroup Î²] extends
  Module Î± Î² where
  norm_smul_le : âˆ€ (a : Î±) (b : Î²), â€–a â€¢ bâ€– â‰¤ â€–aâ€– * â€–bâ€–
#align normed_space NormedSpace

attribute [inherit_doc NormedSpace] NormedSpace.norm_smul_le

end Prio

variable [NormedField Î±] [SeminormedAddCommGroup Î²]

-- see Note [lower instance priority]
instance (priority := 100) NormedSpace.boundedSMul [NormedSpace Î± Î²] : BoundedSMul Î± Î² :=
  BoundedSMul.of_norm_smul_le NormedSpace.norm_smul_le
#align normed_space.has_bounded_smul NormedSpace.boundedSMul

instance NormedField.toNormedSpace : NormedSpace Î± Î± where norm_smul_le a b := norm_mul_le a b
#align normed_field.to_normed_space NormedField.toNormedSpace

-- shortcut instance
instance NormedField.to_boundedSMul : BoundedSMul Î± Î± :=
  NormedSpace.boundedSMul
#align normed_field.to_has_bounded_smul NormedField.to_boundedSMul

theorem norm_zsmul (Î±) [NormedField Î±] [NormedSpace Î± Î²] (n : â„¤) (x : Î²) :
    â€–n â€¢ xâ€– = â€–(n : Î±)â€– * â€–xâ€– := by rw [â† norm_smul, â† Int.smul_one_eq_coe, smul_assoc, one_smul]
#align norm_zsmul norm_zsmul

@[simp]
theorem abs_norm (z : Î²) : |â€–zâ€–| = â€–zâ€– := abs_of_nonneg <| norm_nonneg _
#align abs_norm abs_norm

theorem inv_norm_smul_mem_closed_unit_ball [NormedSpace â„ Î²] (x : Î²) :
    â€–xâ€–â»Â¹ â€¢ x âˆˆ closedBall (0 : Î²) 1 := by
  simp only [mem_closedBall_zero_iff, norm_smul, norm_inv, norm_norm, â† _root_.div_eq_inv_mul,
    div_self_le_one]
#align inv_norm_smul_mem_closed_unit_ball inv_norm_smul_mem_closed_unit_ball

theorem norm_smul_of_nonneg [NormedSpace â„ Î²] {t : â„} (ht : 0 â‰¤ t) (x : Î²) : â€–t â€¢ xâ€– = t * â€–xâ€– := by
  rw [norm_smul, Real.norm_eq_abs, abs_of_nonneg ht]
#align norm_smul_of_nonneg norm_smul_of_nonneg

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace Î± E]

variable {F : Type*} [SeminormedAddCommGroup F] [NormedSpace Î± F]

theorem eventually_nhds_norm_smul_sub_lt (c : Î±) (x : E) {Îµ : â„} (h : 0 < Îµ) :
    âˆ€á¶  y in ğ“ x, â€–c â€¢ (y - x)â€– < Îµ :=
  have : Tendsto (fun y => â€–c â€¢ (y - x)â€–) (ğ“ x) (ğ“ 0) :=
    ((continuous_id.sub continuous_const).const_smul _).norm.tendsto' _ _ (by simp)
  this.eventually (gt_mem_nhds h)
#align eventually_nhds_norm_smul_sub_lt eventually_nhds_norm_smul_sub_lt

theorem Filter.Tendsto.zero_smul_isBoundedUnder_le {f : Î¹ â†’ Î±} {g : Î¹ â†’ E} {l : Filter Î¹}
    (hf : Tendsto f l (ğ“ 0)) (hg : IsBoundedUnder (Â· â‰¤ Â·) l (Norm.norm âˆ˜ g)) :
    Tendsto (fun x => f x â€¢ g x) l (ğ“ 0) :=
  hf.op_zero_isBoundedUnder_le hg (Â· â€¢ Â·) norm_smul_le
#align filter.tendsto.zero_smul_is_bounded_under_le Filter.Tendsto.zero_smul_isBoundedUnder_le

theorem Filter.IsBoundedUnder.smul_tendsto_zero {f : Î¹ â†’ Î±} {g : Î¹ â†’ E} {l : Filter Î¹}
    (hf : IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f)) (hg : Tendsto g l (ğ“ 0)) :
    Tendsto (fun x => f x â€¢ g x) l (ğ“ 0) :=
  hg.op_zero_isBoundedUnder_le hf (flip (Â· â€¢ Â·)) fun x y =>
    (norm_smul_le y x).trans_eq (mul_comm _ _)
#align filter.is_bounded_under.smul_tendsto_zero Filter.IsBoundedUnder.smul_tendsto_zero

theorem closure_ball [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    closure (ball x r) = closedBall x r := by
  refine' Subset.antisymm closure_ball_subset_closedBall fun y hy => _
  have : ContinuousWithinAt (fun c : â„ => c â€¢ (y - x) + x) (Ico 0 1) 1 :=
    ((continuous_id.smul continuous_const).add continuous_const).continuousWithinAt
  convert this.mem_closure _ _
  Â· rw [one_smul, sub_add_cancel]
  Â· simp [closure_Ico zero_ne_one, zero_le_one]
  Â· rintro c âŸ¨hc0, hc1âŸ©
    rw [mem_ball, dist_eq_norm, add_sub_cancel, norm_smul, Real.norm_eq_abs, abs_of_nonneg hc0,
      mul_comm, â† mul_one r]
    rw [mem_closedBall, dist_eq_norm] at hy
    replace hr : 0 < r := ((norm_nonneg _).trans hy).lt_of_ne hr.symm
    apply mul_lt_mul' <;> assumption
#align closure_ball closure_ball

theorem frontier_ball [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    frontier (ball x r) = sphere x r := by
  rw [frontier, closure_ball x hr, isOpen_ball.interior_eq, closedBall_diff_ball]
#align frontier_ball frontier_ball

theorem interior_closedBall [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    interior (closedBall x r) = ball x r := by
  cases' hr.lt_or_lt with hr hr
  Â· rw [closedBall_eq_empty.2 hr, ball_eq_empty.2 hr.le, interior_empty]
  refine' Subset.antisymm _ ball_subset_interior_closedBall
  intro y hy
  rcases (mem_closedBall.1 <| interior_subset hy).lt_or_eq with (hr | rfl)
  Â· exact hr
  set f : â„ â†’ E := fun c : â„ => c â€¢ (y - x) + x
  suffices f â»Â¹' closedBall x (dist y x) âŠ† Icc (-1) 1 by
    have hfc : Continuous f := (continuous_id.smul continuous_const).add continuous_const
    have hf1 : (1 : â„) âˆˆ f â»Â¹' interior (closedBall x <| dist y x) := by simpa
    have h1 : (1 : â„) âˆˆ interior (Icc (-1 : â„) 1) :=
      interior_mono this (preimage_interior_subset_interior_preimage hfc hf1)
    contrapose h1
    simp
  intro c hc
  rw [mem_Icc, â† abs_le, â† Real.norm_eq_abs, â† mul_le_mul_right hr]
  simpa [dist_eq_norm, norm_smul] using hc
#align interior_closed_ball interior_closedBall

theorem frontier_closedBall [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall x hr, closedBall_diff_ball]
#align frontier_closed_ball frontier_closedBall

theorem interior_sphere [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    interior (sphere x r) = âˆ… := by
  rw [â† frontier_closedBall x hr, interior_frontier isClosed_ball]
#align interior_sphere interior_sphere

theorem frontier_sphere [NormedSpace â„ E] (x : E) {r : â„} (hr : r â‰  0) :
    frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere x hr, diff_empty]
#align frontier_sphere frontier_sphere

instance NormedSpace.discreteTopology_zmultiples
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„š E] (e : E) :
    DiscreteTopology <| AddSubgroup.zmultiples e := by
  rcases eq_or_ne e 0 with (rfl | he)
  Â· rw [AddSubgroup.zmultiples_zero_eq_bot]
    refine Subsingleton.discreteTopology (Î± := â†‘(âŠ¥ : Subspace â„š E))
  Â· rw [discreteTopology_iff_isOpen_singleton_zero, isOpen_induced_iff]
    refine' âŸ¨Metric.ball 0 â€–eâ€–, Metric.isOpen_ball, _âŸ©
    ext âŸ¨x, hxâŸ©
    obtain âŸ¨k, rflâŸ© := AddSubgroup.mem_zmultiples_iff.mp hx
    rw [mem_preimage, mem_ball_zero_iff, AddSubgroup.coe_mk, mem_singleton_iff, Subtype.ext_iff,
      AddSubgroup.coe_mk, AddSubgroup.coe_zero, norm_zsmul â„š k e, Int.norm_cast_rat,
      Int.norm_eq_abs, mul_lt_iff_lt_one_left (norm_pos_iff.mpr he), â†
      @Int.cast_one â„ _, Int.cast_lt, Int.abs_lt_one_iff, smul_eq_zero, or_iff_left he]

open NormedField

instance ULift.normedSpace : NormedSpace Î± (ULift E) :=
  { ULift.seminormedAddCommGroup (E := E), ULift.module' with
    norm_smul_le := fun s x => (norm_smul_le s x.down : _) }

/-- The product of two normed spaces is a normed space, with the sup norm. -/
instance Prod.normedSpace : NormedSpace Î± (E Ã— F) :=
  { Prod.seminormedAddCommGroup (E := E) (F := F), Prod.instModule with
    norm_smul_le := fun s x => by
      simp only [norm_smul, Prod.norm_def, Prod.smul_snd, Prod.smul_fst,
        mul_max_of_nonneg, norm_nonneg, le_rfl] }
#align prod.normed_space Prod.normedSpace

/-- The product of finitely many normed spaces is a normed space, with the sup norm. -/
instance Pi.normedSpace {E : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, SeminormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace Î± (E i)] : NormedSpace Î± (âˆ€ i, E i) where
  norm_smul_le a f := by
    simp_rw [â† coe_nnnorm, â† NNReal.coe_mul, NNReal.coe_le_coe, Pi.nnnorm_def,
      NNReal.mul_finset_sup]
    exact Finset.sup_mono_fun fun _ _ => norm_smul_le a _
#align pi.normed_space Pi.normedSpace

instance MulOpposite.normedSpace : NormedSpace Î± Eáµáµ’áµ– :=
  { MulOpposite.seminormedAddCommGroup (E := Eáµáµ’áµ–), MulOpposite.module _ with
    norm_smul_le := fun s x => norm_smul_le s x.unop }
#align mul_opposite.normed_space MulOpposite.normedSpace

/-- A subspace of a normed space is also a normed space, with the restriction of the norm. -/
instance Submodule.normedSpace {ğ•œ R : Type*} [SMul ğ•œ R] [NormedField ğ•œ] [Ring R] {E : Type*}
    [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] [Module R E] [IsScalarTower ğ•œ R E]
    (s : Submodule R E) : NormedSpace ğ•œ s where norm_smul_le c x := norm_smul_le c (x : E)
#align submodule.normed_space Submodule.normedSpace

end SeminormedAddCommGroup

/-- A linear map from a `Module` to a `NormedSpace` induces a `NormedSpace` structure on the
domain, using the `SeminormedAddCommGroup.induced` norm.

See note [reducible non-instances] -/
@[reducible]
def NormedSpace.induced {F : Type*} (Î± Î² Î³ : Type*) [NormedField Î±] [AddCommGroup Î²] [Module Î± Î²]
    [SeminormedAddCommGroup Î³] [NormedSpace Î± Î³] [LinearMapClass F Î± Î² Î³] (f : F) :
    @NormedSpace Î± Î² _ (SeminormedAddCommGroup.induced Î² Î³ f) := by
  -- Porting note: trouble inferring SeminormedAddCommGroup Î² and Module Î± Î²
  -- unfolding the induced semi-norm is fiddly
  refine @NormedSpace.mk (Î± := Î±) (Î² := Î²) _ ?_ ?_ ?_
  Â· infer_instance
  Â· intro a b
    change â€–(â‡‘f) (a â€¢ b)â€– â‰¤ â€–aâ€– * â€–(â‡‘f) bâ€–
    exact (map_smul f a b).symm â–¸ norm_smul_le a (f b)
#align normed_space.induced NormedSpace.induced

section NormedAddCommGroup

variable [NormedField Î±]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace Î± E]

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace Î± F]

open NormedField

/-- While this may appear identical to `NormedSpace.toModule`, it contains an implicit argument
involving `NormedAddCommGroup.toSeminormedAddCommGroup` that typeclass inference has trouble
inferring.

Specifically, the following instance cannot be found without this `NormedSpace.toModule'`:
```lean
example
  (ğ•œ Î¹ : Type*) (E : Î¹ â†’ Type*)
  [NormedField ğ•œ] [Î  i, NormedAddCommGroup (E i)] [Î  i, NormedSpace ğ•œ (E i)] :
  Î  i, Module ğ•œ (E i) := by infer_instance
```

[This Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/113488-general/topic/Typeclass.20resolution.20under.20binders/near/245151099)
gives some more context. -/
instance (priority := 100) NormedSpace.toModule' : Module Î± F :=
  NormedSpace.toModule
#align normed_space.to_module' NormedSpace.toModule'

section Surj

variable (E)

variable [NormedSpace â„ E] [Nontrivial E]

theorem exists_norm_eq {c : â„} (hc : 0 â‰¤ c) : âˆƒ x : E, â€–xâ€– = c := by
  rcases exists_ne (0 : E) with âŸ¨x, hxâŸ©
  rw [â† norm_ne_zero_iff] at hx
  use c â€¢ â€–xâ€–â»Â¹ â€¢ x
  simp [norm_smul, Real.norm_of_nonneg hc, abs_of_nonneg hc, inv_mul_cancel hx]
#align exists_norm_eq exists_norm_eq

@[simp]
theorem range_norm : range (norm : E â†’ â„) = Ici 0 :=
  Subset.antisymm (range_subset_iff.2 norm_nonneg) fun _ => exists_norm_eq E
#align range_norm range_norm

theorem nnnorm_surjective : Surjective (nnnorm : E â†’ â„â‰¥0) := fun c =>
  (exists_norm_eq E c.coe_nonneg).imp fun _ h => NNReal.eq h
#align nnnorm_surjective nnnorm_surjective

@[simp]
theorem range_nnnorm : range (nnnorm : E â†’ â„â‰¥0) = univ :=
  (nnnorm_surjective E).range_eq
#align range_nnnorm range_nnnorm

end Surj

/-- If `E` is a nontrivial topological module over `â„`, then `E` has no isolated points.
This is a particular case of `Module.punctured_nhds_neBot`. -/
instance Real.punctured_nhds_module_neBot {E : Type*} [AddCommGroup E] [TopologicalSpace E]
    [ContinuousAdd E] [Nontrivial E] [Module â„ E] [ContinuousSMul â„ E] (x : E) : NeBot (ğ“[â‰ ] x) :=
  Module.punctured_nhds_neBot â„ E x
#align real.punctured_nhds_module_ne_bot Real.punctured_nhds_module_neBot

theorem interior_closedBall' [NormedSpace â„ E] [Nontrivial E] (x : E) (r : â„) :
    interior (closedBall x r) = ball x r := by
  rcases eq_or_ne r 0 with (rfl | hr)
  Â· rw [closedBall_zero, ball_zero, interior_singleton]
  Â· exact interior_closedBall x hr
#align interior_closed_ball' interior_closedBall'

theorem frontier_closedBall' [NormedSpace â„ E] [Nontrivial E] (x : E) (r : â„) :
    frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall' x r, closedBall_diff_ball]
#align frontier_closed_ball' frontier_closedBall'

@[simp]
theorem interior_sphere' [NormedSpace â„ E] [Nontrivial E] (x : E) (r : â„) :
    interior (sphere x r) = âˆ… := by rw [â† frontier_closedBall' x, interior_frontier isClosed_ball]
#align interior_sphere' interior_sphere'

@[simp]
theorem frontier_sphere' [NormedSpace â„ E] [Nontrivial E] (x : E) (r : â„) :
    frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere' x, diff_empty]
#align frontier_sphere' frontier_sphere'

end NormedAddCommGroup

section NontriviallyNormedSpace

variable (ğ•œ E : Type*) [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [Nontrivial E]

/-- If `E` is a nontrivial normed space over a nontrivially normed field `ğ•œ`, then `E` is unbounded:
for any `c : â„`, there exists a vector `x : E` with norm strictly greater than `c`. -/
theorem NormedSpace.exists_lt_norm (c : â„) : âˆƒ x : E, c < â€–xâ€– := by
  rcases exists_ne (0 : E) with âŸ¨x, hxâŸ©
  rcases NormedField.exists_lt_norm ğ•œ (c / â€–xâ€–) with âŸ¨r, hrâŸ©
  use r â€¢ x
  rwa [norm_smul, â† _root_.div_lt_iff]
  rwa [norm_pos_iff]
#align normed_space.exists_lt_norm NormedSpace.exists_lt_norm

protected theorem NormedSpace.unbounded_univ : Â¬Bornology.IsBounded (univ : Set E) := fun h =>
  let âŸ¨R, hRâŸ© := isBounded_iff_forall_norm_le.1 h
  let âŸ¨x, hxâŸ© := NormedSpace.exists_lt_norm ğ•œ E R
  hx.not_le (hR x trivial)
#align normed_space.unbounded_univ NormedSpace.unbounded_univ

protected lemma NormedSpace.cobounded_neBot : NeBot (cobounded E) := by
  rw [neBot_iff, Ne.def, cobounded_eq_bot_iff, â† isBounded_univ]
  exact NormedSpace.unbounded_univ ğ•œ E

instance (priority := 100) NontriviallyNormedField.cobounded_neBot : NeBot (cobounded ğ•œ) :=
  NormedSpace.cobounded_neBot ğ•œ ğ•œ

instance (priority := 80) RealNormedSpace.cobounded_neBot [NormedSpace â„ E] :
    NeBot (cobounded E) := NormedSpace.cobounded_neBot â„ E

instance (priority := 80) NontriviallyNormedField.infinite : Infinite ğ•œ :=
  âŸ¨fun _ â†¦ NormedSpace.unbounded_univ ğ•œ ğ•œ (Set.toFinite _).isBoundedâŸ©

end NontriviallyNormedSpace

section NormedSpace

variable (ğ•œ E : Type*) [NormedField ğ•œ] [Infinite ğ•œ] [NormedAddCommGroup E] [Nontrivial E]
  [NormedSpace ğ•œ E]

/-- A normed vector space over an infinite normed field is a noncompact space.
This cannot be an instance because in order to apply it,
Lean would have to search for `NormedSpace ğ•œ E` with unknown `ğ•œ`.
We register this as an instance in two cases: `ğ•œ = E` and `ğ•œ = â„`. -/
protected theorem NormedSpace.noncompactSpace : NoncompactSpace E := by
  by_cases H : âˆƒ c : ğ•œ, c â‰  0 âˆ§ â€–câ€– â‰  1
  Â· letI := NontriviallyNormedField.ofNormNeOne H
    exact âŸ¨fun h â†¦ NormedSpace.unbounded_univ ğ•œ E h.isBoundedâŸ©
  Â· push_neg at H
    rcases exists_ne (0 : E) with âŸ¨x, hxâŸ©
    suffices ClosedEmbedding (Infinite.natEmbedding ğ•œ Â· â€¢ x) from this.noncompactSpace
    refine closedEmbedding_of_pairwise_le_dist (norm_pos_iff.2 hx) fun k n hne â†¦ ?_
    simp only [dist_eq_norm, â† sub_smul, norm_smul]
    rw [H, one_mul]
    rwa [sub_ne_zero, (Embedding.injective _).ne_iff]
#align normed_space.noncompact_space NormedSpace.noncompactSpace

instance (priority := 100) NormedField.noncompactSpace : NoncompactSpace ğ•œ :=
  NormedSpace.noncompactSpace ğ•œ ğ•œ
#align nontrivially_normed_field.noncompact_space NormedField.noncompactSpace

instance (priority := 100) RealNormedSpace.noncompactSpace [NormedSpace â„ E] : NoncompactSpace E :=
  NormedSpace.noncompactSpace â„ E
#align real_normed_space.noncompact_space RealNormedSpace.noncompactSpace

end NormedSpace

section NormedAlgebra

/-- A normed algebra `ğ•œ'` over `ğ•œ` is normed module that is also an algebra.

See the implementation notes for `Algebra` for a discussion about non-unital algebras. Following
the strategy there, a non-unital *normed* algebra can be written as:
```lean
variables [NormedField ğ•œ] [NonUnitalSeminormedRing ğ•œ']
variables [NormedSpace ğ•œ ğ•œ'] [SMulCommClass ğ•œ ğ•œ' ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ']
```
-/
class NormedAlgebra (ğ•œ : Type*) (ğ•œ' : Type*) [NormedField ğ•œ] [SeminormedRing ğ•œ'] extends
  Algebra ğ•œ ğ•œ' where
  norm_smul_le : âˆ€ (r : ğ•œ) (x : ğ•œ'), â€–r â€¢ xâ€– â‰¤ â€–râ€– * â€–xâ€–
#align normed_algebra NormedAlgebra

attribute [inherit_doc NormedAlgebra] NormedAlgebra.norm_smul_le

variable {ğ•œ : Type*} (ğ•œ' : Type*) [NormedField ğ•œ] [SeminormedRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']

instance (priority := 100) NormedAlgebra.toNormedSpace : NormedSpace ğ•œ ğ•œ' :=
  -- Porting note: previous Lean could figure out what we were extending
  { NormedAlgebra.toAlgebra.toModule with
  norm_smul_le := NormedAlgebra.norm_smul_le }
#align normed_algebra.to_normed_space NormedAlgebra.toNormedSpace

/-- While this may appear identical to `NormedAlgebra.toNormedSpace`, it contains an implicit
argument involving `NormedRing.toSeminormedRing` that typeclass inference has trouble inferring.

Specifically, the following instance cannot be found without this `NormedSpace.toModule'`:
```lean
example
  (ğ•œ Î¹ : Type*) (E : Î¹ â†’ Type*)
  [NormedField ğ•œ] [Î  i, NormedRing (E i)] [Î  i, NormedAlgebra ğ•œ (E i)] :
  Î  i, Module ğ•œ (E i) := by infer_instance
```

See `NormedSpace.toModule'` for a similar situation. -/
instance (priority := 100) NormedAlgebra.toNormedSpace' {ğ•œ'} [NormedRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] :
    NormedSpace ğ•œ ğ•œ' := by infer_instance
#align normed_algebra.to_normed_space' NormedAlgebra.toNormedSpace'

theorem norm_algebraMap (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€– = â€–xâ€– * â€–(1 : ğ•œ')â€– := by
  rw [Algebra.algebraMap_eq_smul_one]
  exact norm_smul _ _
#align norm_algebra_map norm_algebraMap

theorem nnnorm_algebraMap (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š * â€–(1 : ğ•œ')â€–â‚Š :=
  Subtype.ext <| norm_algebraMap ğ•œ' x
#align nnnorm_algebra_map nnnorm_algebraMap

@[simp]
theorem norm_algebraMap' [NormOneClass ğ•œ'] (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€– = â€–xâ€– := by
  rw [norm_algebraMap, norm_one, mul_one]
#align norm_algebra_map' norm_algebraMap'

@[simp]
theorem nnnorm_algebraMap' [NormOneClass ğ•œ'] (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| norm_algebraMap' _ _
#align nnnorm_algebra_map' nnnorm_algebraMap'

section NNReal

variable [NormOneClass ğ•œ'] [NormedAlgebra â„ ğ•œ']

@[simp]
theorem norm_algebraMap_nNReal (x : â„â‰¥0) : â€–algebraMap â„â‰¥0 ğ•œ' xâ€– = x :=
  (norm_algebraMap' ğ•œ' (x : â„)).symm â–¸ Real.norm_of_nonneg x.prop
#align norm_algebra_map_nnreal norm_algebraMap_nNReal

@[simp]
theorem nnnorm_algebraMap_nNReal (x : â„â‰¥0) : â€–algebraMap â„â‰¥0 ğ•œ' xâ€–â‚Š = x :=
  Subtype.ext <| norm_algebraMap_nNReal ğ•œ' x
#align nnnorm_algebra_map_nnreal nnnorm_algebraMap_nNReal

end NNReal

variable (ğ•œ)

/-- In a normed algebra, the inclusion of the base field in the extended field is an isometry. -/
theorem algebraMap_isometry [NormOneClass ğ•œ'] : Isometry (algebraMap ğ•œ ğ•œ') := by
  refine' Isometry.of_dist_eq fun x y => _
  rw [dist_eq_norm, dist_eq_norm, â† RingHom.map_sub, norm_algebraMap']
#align algebra_map_isometry algebraMap_isometry

instance NormedAlgebra.id : NormedAlgebra ğ•œ ğ•œ :=
  { NormedField.toNormedSpace, Algebra.id ğ•œ with }
#align normed_algebra.id NormedAlgebra.id

-- Porting note: cannot synth scalar tower â„š â„ k
/-- Any normed characteristic-zero division ring that is a normed algebra over the reals is also a
normed algebra over the rationals.

Phrased another way, if `ğ•œ` is a normed algebra over the reals, then `AlgebraRat` respects that
norm. -/
instance normedAlgebraRat {ğ•œ} [NormedDivisionRing ğ•œ] [CharZero ğ•œ] [NormedAlgebra â„ ğ•œ] :
    NormedAlgebra â„š ğ•œ where
  norm_smul_le q x := by
    rw [â† smul_one_smul â„ q x, Rat.smul_one_eq_coe, norm_smul, Rat.norm_cast_real]
#align normed_algebra_rat normedAlgebraRat

instance PUnit.normedAlgebra : NormedAlgebra ğ•œ PUnit where
  norm_smul_le q _ := by simp only [norm_eq_zero, mul_zero, le_refl]
#align punit.normed_algebra PUnit.normedAlgebra

instance : NormedAlgebra ğ•œ (ULift ğ•œ') :=
  { ULift.normedSpace, ULift.algebra with }

/-- The product of two normed algebras is a normed algebra, with the sup norm. -/
instance Prod.normedAlgebra {E F : Type*} [SeminormedRing E] [SeminormedRing F] [NormedAlgebra ğ•œ E]
    [NormedAlgebra ğ•œ F] : NormedAlgebra ğ•œ (E Ã— F) :=
  { Prod.normedSpace, Prod.algebra ğ•œ E F with }
#align prod.normed_algebra Prod.normedAlgebra

-- Porting note: Lean 3 could synth the algebra instances for Pi Pr
/-- The product of finitely many normed algebras is a normed algebra, with the sup norm. -/
instance Pi.normedAlgebra {E : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, SeminormedRing (E i)]
    [âˆ€ i, NormedAlgebra ğ•œ (E i)] : NormedAlgebra ğ•œ (âˆ€ i, E i) :=
  { Pi.normedSpace, Pi.algebra _ E with }
#align pi.normed_algebra Pi.normedAlgebra

variable {E : Type*} [SeminormedRing E] [NormedAlgebra ğ•œ E]

instance MulOpposite.normedAlgebra {E : Type*} [SeminormedRing E] [NormedAlgebra ğ•œ E] :
    NormedAlgebra ğ•œ Eáµáµ’áµ– :=
  { MulOpposite.normedSpace, MulOpposite.instAlgebra with }

#align mul_opposite.normed_algebra MulOpposite.normedAlgebra

end NormedAlgebra

/-- A non-unital algebra homomorphism from an `Algebra` to a `NormedAlgebra` induces a
`NormedAlgebra` structure on the domain, using the `SeminormedRing.induced` norm.

See note [reducible non-instances] -/
@[reducible]
def NormedAlgebra.induced {F : Type*} (Î± Î² Î³ : Type*) [NormedField Î±] [Ring Î²] [Algebra Î± Î²]
    [SeminormedRing Î³] [NormedAlgebra Î± Î³] [NonUnitalAlgHomClass F Î± Î² Î³] (f : F) :
    @NormedAlgebra Î± Î² _ (SeminormedRing.induced Î² Î³ f) := by
  -- Porting note: trouble with SeminormedRing Î², Algebra Î± Î², and unfolding seminorm
  refine @NormedAlgebra.mk (ğ•œ := Î±) (ğ•œ' := Î²) _ ?_ ?_ ?_
  Â· infer_instance
  Â· intro a b
    change â€–(â‡‘f) (a â€¢ b)â€– â‰¤ â€–aâ€– * â€–(â‡‘f) bâ€–
    exact (map_smul f a b).symm â–¸ norm_smul_le a (f b)
#align normed_algebra.induced NormedAlgebra.induced

-- Porting note: failed to synth NonunitalAlgHomClass
instance Subalgebra.toNormedAlgebra {ğ•œ A : Type*} [SeminormedRing A] [NormedField ğ•œ]
    [NormedAlgebra ğ•œ A] (S : Subalgebra ğ•œ A) : NormedAlgebra ğ•œ S :=
  @NormedAlgebra.induced _ ğ•œ S A _ (SubringClass.toRing S) _ _ _ _ S.val
#align subalgebra.to_normed_algebra Subalgebra.toNormedAlgebra

section RestrictScalars

variable (ğ•œ : Type*) (ğ•œ' : Type*) [NormedField ğ•œ] [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  (E : Type*) [SeminormedAddCommGroup E] [NormedSpace ğ•œ' E]

instance {ğ•œ : Type*} {ğ•œ' : Type*} {E : Type*} [I : SeminormedAddCommGroup E] :
    SeminormedAddCommGroup (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance {ğ•œ : Type*} {ğ•œ' : Type*} {E : Type*} [I : NormedAddCommGroup E] :
    NormedAddCommGroup (RestrictScalars ğ•œ ğ•œ' E) :=
  I

/-- If `E` is a normed space over `ğ•œ'` and `ğ•œ` is a normed algebra over `ğ•œ'`, then
`RestrictScalars.module` is additionally a `NormedSpace`. -/
instance RestrictScalars.normedSpace : NormedSpace ğ•œ (RestrictScalars ğ•œ ğ•œ' E) :=
  { RestrictScalars.module ğ•œ ğ•œ' E with
    norm_smul_le := fun c x =>
      (norm_smul_le (algebraMap ğ•œ ğ•œ' c) (_ : E)).trans_eq <| by rw [norm_algebraMap'] }

-- If you think you need this, consider instead reproducing `RestrictScalars.lsmul`
-- appropriately modified here.
/-- The action of the original normed_field on `RestrictScalars ğ•œ ğ•œ' E`.
This is not an instance as it would be contrary to the purpose of `RestrictScalars`.
-/
def Module.RestrictScalars.normedSpaceOrig {ğ•œ : Type*} {ğ•œ' : Type*} {E : Type*} [NormedField ğ•œ']
    [SeminormedAddCommGroup E] [I : NormedSpace ğ•œ' E] : NormedSpace ğ•œ' (RestrictScalars ğ•œ ğ•œ' E) :=
  I
#align module.restrict_scalars.normed_space_orig Module.RestrictScalars.normedSpaceOrig

/-- Warning: This declaration should be used judiciously.
Please consider using `IsScalarTower` and/or `RestrictScalars ğ•œ ğ•œ' E` instead.

This definition allows the `RestrictScalars.normedSpace` instance to be put directly on `E`
rather on `RestrictScalars ğ•œ ğ•œ' E`. This would be a very bad instance; both because `ğ•œ'` cannot be
inferred, and because it is likely to create instance diamonds.
-/
def NormedSpace.restrictScalars : NormedSpace ğ•œ E :=
  RestrictScalars.normedSpace _ ğ•œ' _
#align normed_space.restrict_scalars NormedSpace.restrictScalars

end RestrictScalars
