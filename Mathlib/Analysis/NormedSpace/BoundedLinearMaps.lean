/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import Mathlib.Analysis.NormedSpace.Multilinear.Basic
import Mathlib.Analysis.NormedSpace.Units
import Mathlib.Analysis.NormedSpace.OperatorNorm.Completeness
import Mathlib.Analysis.NormedSpace.OperatorNorm.Mul


#align_import analysis.normed_space.bounded_linear_maps from "leanprover-community/mathlib"@"ce11c3c2a285bbe6937e26d9792fda4e51f3fe1a"

/-!
# Bounded linear maps

This file defines a class stating that a map between normed vector spaces is (bi)linear and
continuous.
Instead of asking for continuity, the definition takes the equivalent condition (because the space
is normed) that `â€–f xâ€–` is bounded by a multiple of `â€–xâ€–`. Hence the "bounded" in the name refers to
`â€–f xâ€–/â€–xâ€–` rather than `â€–f xâ€–` itself.

## Main definitions

* `IsBoundedLinearMap`: Class stating that a map `f : E â†’ F` is linear and has `â€–f xâ€–` bounded
  by a multiple of `â€–xâ€–`.
* `IsBoundedBilinearMap`: Class stating that a map `f : E Ã— F â†’ G` is bilinear and continuous,
  but through the simpler to provide statement that `â€–f (x, y)â€–` is bounded by a multiple of
  `â€–xâ€– * â€–yâ€–`
* `IsBoundedBilinearMap.linearDeriv`: Derivative of a continuous bilinear map as a linear map.
* `IsBoundedBilinearMap.deriv`: Derivative of a continuous bilinear map as a continuous linear
  map. The proof that it is indeed the derivative is `IsBoundedBilinearMap.hasFDerivAt` in
  `Analysis.Calculus.FDeriv`.

## Main theorems

* `IsBoundedBilinearMap.continuous`: A bounded bilinear map is continuous.
* `ContinuousLinearEquiv.isOpen`: The continuous linear equivalences are an open subset of the
  set of continuous linear maps between a pair of Banach spaces.  Placed in this file because its
  proof uses `IsBoundedBilinearMap.continuous`.

## Notes

The main use of this file is `IsBoundedBilinearMap`.
The file `Analysis.NormedSpace.Multilinear.Basic`
already expounds the theory of multilinear maps,
but the `2`-variables case is sufficiently simpler to currently deserve its own treatment.

`IsBoundedLinearMap` is effectively an unbundled version of `ContinuousLinearMap` (defined
in `Topology.Algebra.Module.Basic`, theory over normed spaces developed in
`Analysis.NormedSpace.OperatorNorm`), albeit the name disparity. A bundled
`ContinuousLinearMap` is to be preferred over an `IsBoundedLinearMap` hypothesis. Historical
artifact, really.
-/


noncomputable section

open Topology

open Filter (Tendsto)

open Metric ContinuousLinearMap

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]

/-- A function `f` satisfies `IsBoundedLinearMap ğ•œ f` if it is linear and satisfies the
inequality `â€–f xâ€– â‰¤ M * â€–xâ€–` for some positive constant `M`. -/
structure IsBoundedLinearMap (ğ•œ : Type*) [NormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] (f : E â†’ F) extends
  IsLinearMap ğ•œ f : Prop where
  bound : âˆƒ M, 0 < M âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ M * â€–xâ€–
#align is_bounded_linear_map IsBoundedLinearMap

theorem IsLinearMap.with_bound {f : E â†’ F} (hf : IsLinearMap ğ•œ f) (M : â„)
    (h : âˆ€ x : E, â€–f xâ€– â‰¤ M * â€–xâ€–) : IsBoundedLinearMap ğ•œ f :=
  âŸ¨hf,
    by_cases
      (fun (this : M â‰¤ 0) =>
        âŸ¨1, zero_lt_one, fun x =>
          (h x).trans <| mul_le_mul_of_nonneg_right (this.trans zero_le_one) (norm_nonneg x)âŸ©)
      fun (this : Â¬M â‰¤ 0) => âŸ¨M, lt_of_not_ge this, hâŸ©âŸ©
#align is_linear_map.with_bound IsLinearMap.with_bound

/-- A continuous linear map satisfies `IsBoundedLinearMap` -/
theorem ContinuousLinearMap.isBoundedLinearMap (f : E â†’L[ğ•œ] F) : IsBoundedLinearMap ğ•œ f :=
  { f.toLinearMap.isLinear with bound := f.bound }
#align continuous_linear_map.is_bounded_linear_map ContinuousLinearMap.isBoundedLinearMap

namespace IsBoundedLinearMap

/-- Construct a linear map from a function `f` satisfying `IsBoundedLinearMap ğ•œ f`. -/
def toLinearMap (f : E â†’ F) (h : IsBoundedLinearMap ğ•œ f) : E â†’â‚—[ğ•œ] F :=
  IsLinearMap.mk' _ h.toIsLinearMap
#align is_bounded_linear_map.to_linear_map IsBoundedLinearMap.toLinearMap

/-- Construct a continuous linear map from `IsBoundedLinearMap`. -/
def toContinuousLinearMap {f : E â†’ F} (hf : IsBoundedLinearMap ğ•œ f) : E â†’L[ğ•œ] F :=
  { toLinearMap f hf with
    cont :=
      let âŸ¨C, _, hCâŸ© := hf.bound
      AddMonoidHomClass.continuous_of_bound (toLinearMap f hf) C hC }
#align is_bounded_linear_map.to_continuous_linear_map IsBoundedLinearMap.toContinuousLinearMap

theorem zero : IsBoundedLinearMap ğ•œ fun _ : E => (0 : F) :=
  (0 : E â†’â‚—[ğ•œ] F).isLinear.with_bound 0 <| by simp [le_refl]
#align is_bounded_linear_map.zero IsBoundedLinearMap.zero

theorem id : IsBoundedLinearMap ğ•œ fun x : E => x :=
  LinearMap.id.isLinear.with_bound 1 <| by simp [le_refl]
#align is_bounded_linear_map.id IsBoundedLinearMap.id

theorem fst : IsBoundedLinearMap ğ•œ fun x : E Ã— F => x.1 := by
  refine (LinearMap.fst ğ•œ E F).isLinear.with_bound 1 fun x => ?_
  rw [one_mul]
  exact le_max_left _ _
#align is_bounded_linear_map.fst IsBoundedLinearMap.fst

theorem snd : IsBoundedLinearMap ğ•œ fun x : E Ã— F => x.2 := by
  refine (LinearMap.snd ğ•œ E F).isLinear.with_bound 1 fun x => ?_
  rw [one_mul]
  exact le_max_right _ _
#align is_bounded_linear_map.snd IsBoundedLinearMap.snd

variable {f g : E â†’ F}

theorem smul (c : ğ•œ) (hf : IsBoundedLinearMap ğ•œ f) : IsBoundedLinearMap ğ•œ (c â€¢ f) :=
  let âŸ¨hlf, M, _, hMâŸ© := hf
  (c â€¢ hlf.mk' f).isLinear.with_bound (â€–câ€– * M) fun x =>
    calc
      â€–c â€¢ f xâ€– = â€–câ€– * â€–f xâ€– := norm_smul c (f x)
      _ â‰¤ â€–câ€– * (M * â€–xâ€–) := mul_le_mul_of_nonneg_left (hM _) (norm_nonneg _)
      _ = â€–câ€– * M * â€–xâ€– := (mul_assoc _ _ _).symm

#align is_bounded_linear_map.smul IsBoundedLinearMap.smul

theorem neg (hf : IsBoundedLinearMap ğ•œ f) : IsBoundedLinearMap ğ•œ fun e => -f e := by
  rw [show (fun e => -f e) = fun e => (-1 : ğ•œ) â€¢ f e by funext; simp]
  exact smul (-1) hf
#align is_bounded_linear_map.neg IsBoundedLinearMap.neg

theorem add (hf : IsBoundedLinearMap ğ•œ f) (hg : IsBoundedLinearMap ğ•œ g) :
    IsBoundedLinearMap ğ•œ fun e => f e + g e :=
  let âŸ¨hlf, Mf, _, hMfâŸ© := hf
  let âŸ¨hlg, Mg, _, hMgâŸ© := hg
  (hlf.mk' _ + hlg.mk' _).isLinear.with_bound (Mf + Mg) fun x =>
    calc
      â€–f x + g xâ€– â‰¤ Mf * â€–xâ€– + Mg * â€–xâ€– := norm_add_le_of_le (hMf x) (hMg x)
      _ â‰¤ (Mf + Mg) * â€–xâ€– := by rw [add_mul]

#align is_bounded_linear_map.add IsBoundedLinearMap.add

theorem sub (hf : IsBoundedLinearMap ğ•œ f) (hg : IsBoundedLinearMap ğ•œ g) :
    IsBoundedLinearMap ğ•œ fun e => f e - g e := by simpa [sub_eq_add_neg] using add hf (neg hg)
#align is_bounded_linear_map.sub IsBoundedLinearMap.sub

theorem comp {g : F â†’ G} (hg : IsBoundedLinearMap ğ•œ g) (hf : IsBoundedLinearMap ğ•œ f) :
    IsBoundedLinearMap ğ•œ (g âˆ˜ f) :=
  (hg.toContinuousLinearMap.comp hf.toContinuousLinearMap).isBoundedLinearMap
#align is_bounded_linear_map.comp IsBoundedLinearMap.comp

protected theorem tendsto (x : E) (hf : IsBoundedLinearMap ğ•œ f) : Tendsto f (ğ“ x) (ğ“ (f x)) :=
  let âŸ¨hf, M, _, hMâŸ© := hf
  tendsto_iff_norm_sub_tendsto_zero.2 <|
    squeeze_zero (fun e => norm_nonneg _)
      (fun e =>
        calc
          â€–f e - f xâ€– = â€–hf.mk' f (e - x)â€– := by rw [(hf.mk' _).map_sub e x]; rfl
          _ â‰¤ M * â€–e - xâ€– := hM (e - x)
          )
      (suffices Tendsto (fun e : E => M * â€–e - xâ€–) (ğ“ x) (ğ“ (M * 0)) by simpa
      tendsto_const_nhds.mul (tendsto_norm_sub_self _))
#align is_bounded_linear_map.tendsto IsBoundedLinearMap.tendsto

theorem continuous (hf : IsBoundedLinearMap ğ•œ f) : Continuous f :=
  continuous_iff_continuousAt.2 fun _ => hf.tendsto _
#align is_bounded_linear_map.continuous IsBoundedLinearMap.continuous

theorem lim_zero_bounded_linear_map (hf : IsBoundedLinearMap ğ•œ f) : Tendsto f (ğ“ 0) (ğ“ 0) :=
  (hf.1.mk' _).map_zero â–¸ continuous_iff_continuousAt.1 hf.continuous 0
#align is_bounded_linear_map.lim_zero_bounded_linear_map IsBoundedLinearMap.lim_zero_bounded_linear_map

section

open Asymptotics Filter

theorem isBigO_id {f : E â†’ F} (h : IsBoundedLinearMap ğ•œ f) (l : Filter E) : f =O[l] fun x => x :=
  let âŸ¨_, _, hMâŸ© := h.bound
  IsBigO.of_bound _ (mem_of_superset univ_mem fun x _ => hM x)
set_option linter.uppercaseLean3 false in
#align is_bounded_linear_map.is_O_id IsBoundedLinearMap.isBigO_id

theorem isBigO_comp {E : Type*} {g : F â†’ G} (hg : IsBoundedLinearMap ğ•œ g) {f : E â†’ F}
    (l : Filter E) : (fun x' => g (f x')) =O[l] f :=
  (hg.isBigO_id âŠ¤).comp_tendsto le_top
set_option linter.uppercaseLean3 false in
#align is_bounded_linear_map.is_O_comp IsBoundedLinearMap.isBigO_comp

theorem isBigO_sub {f : E â†’ F} (h : IsBoundedLinearMap ğ•œ f) (l : Filter E) (x : E) :
    (fun x' => f (x' - x)) =O[l] fun x' => x' - x :=
  isBigO_comp h l
set_option linter.uppercaseLean3 false in
#align is_bounded_linear_map.is_O_sub IsBoundedLinearMap.isBigO_sub

end

end IsBoundedLinearMap

section

variable {Î¹ : Type*} [Fintype Î¹]

/-- Taking the cartesian product of two continuous multilinear maps is a bounded linear
operation. -/
theorem isBoundedLinearMap_prod_multilinear {E : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace ğ•œ (E i)] :
    IsBoundedLinearMap ğ•œ fun p : ContinuousMultilinearMap ğ•œ E F Ã— ContinuousMultilinearMap ğ•œ E G =>
      p.1.prod p.2 where
  map_add pâ‚ pâ‚‚ := by ext : 1; rfl
  map_smul c p := by ext : 1; rfl
  bound := by
    refine âŸ¨1, zero_lt_one, fun p â†¦ ?_âŸ©
    rw [one_mul]
    apply ContinuousMultilinearMap.opNorm_le_bound _ (norm_nonneg _) _
    intro m
    rw [ContinuousMultilinearMap.prod_apply, norm_prod_le_iff]
    constructor
    Â· exact (p.1.le_opNorm m).trans (mul_le_mul_of_nonneg_right (norm_fst_le p) <| by positivity)
    Â· exact (p.2.le_opNorm m).trans (mul_le_mul_of_nonneg_right (norm_snd_le p) <| by positivity)
#align is_bounded_linear_map_prod_multilinear isBoundedLinearMap_prod_multilinear

/-- Given a fixed continuous linear map `g`, associating to a continuous multilinear map `f` the
continuous multilinear map `f (g mâ‚, ..., g mâ‚™)` is a bounded linear operation. -/
theorem isBoundedLinearMap_continuousMultilinearMap_comp_linear (g : G â†’L[ğ•œ] E) :
    IsBoundedLinearMap ğ•œ fun f : ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => E) F =>
      f.compContinuousLinearMap fun _ => g := by
  refine
    IsLinearMap.with_bound
      âŸ¨fun fâ‚ fâ‚‚ => by ext; rfl,
        fun c f => by ext; rflâŸ©
      (â€–gâ€– ^ Fintype.card Î¹) fun f => ?_
  apply ContinuousMultilinearMap.opNorm_le_bound _ _ _
  Â· apply_rules [mul_nonneg, pow_nonneg, norm_nonneg]
  intro m
  calc
    â€–f (g âˆ˜ m)â€– â‰¤ â€–fâ€– * âˆ i, â€–g (m i)â€– := f.le_opNorm _
    _ â‰¤ â€–fâ€– * âˆ i, â€–gâ€– * â€–m iâ€– := by
      apply mul_le_mul_of_nonneg_left _ (norm_nonneg _)
      exact Finset.prod_le_prod (fun i _ => norm_nonneg _) fun i _ => g.le_opNorm _
    _ = â€–gâ€– ^ Fintype.card Î¹ * â€–fâ€– * âˆ i, â€–m iâ€– := by
      simp only [Finset.prod_mul_distrib, Finset.prod_const, Finset.card_univ]
      ring

#align is_bounded_linear_map_continuous_multilinear_map_comp_linear isBoundedLinearMap_continuousMultilinearMap_comp_linear

end

section BilinearMap

namespace ContinuousLinearMap

/-! We prove some computation rules for continuous (semi-)bilinear maps in their first argument.
  If `f` is a continuous bilinear map, to use the corresponding rules for the second argument, use
  `(f _).map_add` and similar.

We have to assume that `F` and `G` are normed spaces in this section, to use
`ContinuousLinearMap.toNormedAddCommGroup`, but we don't need to assume this for the first
argument of `f`.
-/


variable {R : Type*}
variable {ğ•œâ‚‚ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NontriviallyNormedField ğ•œâ‚‚]
variable {M : Type*} [TopologicalSpace M]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œâ‚‚ G'] [NormedSpace ğ•œ' G']
variable [SMulCommClass ğ•œâ‚‚ ğ•œ' G']

section Semiring

variable [Semiring R] [AddCommMonoid M] [Module R M] {Ïâ‚â‚‚ : R â†’+* ğ•œ'}

theorem map_addâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x x' : M) (y : F) :
    f (x + x') y = f x y + f x' y := by rw [f.map_add, add_apply]
#align continuous_linear_map.map_addâ‚‚ ContinuousLinearMap.map_addâ‚‚

theorem map_zeroâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (y : F) : f 0 y = 0 := by
  rw [f.map_zero, zero_apply]
#align continuous_linear_map.map_zeroâ‚‚ ContinuousLinearMap.map_zeroâ‚‚

theorem map_smulâ‚›â‚—â‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (c : R) (x : M) (y : F) :
    f (c â€¢ x) y = Ïâ‚â‚‚ c â€¢ f x y := by rw [f.map_smulâ‚›â‚—, smul_apply]
#align continuous_linear_map.map_smulâ‚›â‚—â‚‚ ContinuousLinearMap.map_smulâ‚›â‚—â‚‚

end Semiring

section Ring

variable [Ring R] [AddCommGroup M] [Module R M] {Ïâ‚â‚‚ : R â†’+* ğ•œ'}

theorem map_subâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x x' : M) (y : F) :
    f (x - x') y = f x y - f x' y := by rw [f.map_sub, sub_apply]
#align continuous_linear_map.map_subâ‚‚ ContinuousLinearMap.map_subâ‚‚

theorem map_negâ‚‚ (f : M â†’SL[Ïâ‚â‚‚] F â†’SL[Ïƒâ‚â‚‚] G') (x : M) (y : F) : f (-x) y = -f x y := by
  rw [f.map_neg, neg_apply]
#align continuous_linear_map.map_negâ‚‚ ContinuousLinearMap.map_negâ‚‚

end Ring

theorem map_smulâ‚‚ (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (c : ğ•œ) (x : E) (y : F) : f (c â€¢ x) y = c â€¢ f x y := by
  rw [f.map_smul, smul_apply]
#align continuous_linear_map.map_smulâ‚‚ ContinuousLinearMap.map_smulâ‚‚

end ContinuousLinearMap

variable (ğ•œ)

/-- A map `f : E Ã— F â†’ G` satisfies `IsBoundedBilinearMap ğ•œ f` if it is bilinear and
continuous. -/
structure IsBoundedBilinearMap (f : E Ã— F â†’ G) : Prop where
  add_left : âˆ€ (xâ‚ xâ‚‚ : E) (y : F), f (xâ‚ + xâ‚‚, y) = f (xâ‚, y) + f (xâ‚‚, y)
  smul_left : âˆ€ (c : ğ•œ) (x : E) (y : F), f (c â€¢ x, y) = c â€¢ f (x, y)
  add_right : âˆ€ (x : E) (yâ‚ yâ‚‚ : F), f (x, yâ‚ + yâ‚‚) = f (x, yâ‚) + f (x, yâ‚‚)
  smul_right : âˆ€ (c : ğ•œ) (x : E) (y : F), f (x, c â€¢ y) = c â€¢ f (x, y)
  bound : âˆƒ C > 0, âˆ€ (x : E) (y : F), â€–f (x, y)â€– â‰¤ C * â€–xâ€– * â€–yâ€–
#align is_bounded_bilinear_map IsBoundedBilinearMap

variable {ğ•œ}
variable {f : E Ã— F â†’ G}

theorem ContinuousLinearMap.isBoundedBilinearMap (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) :
    IsBoundedBilinearMap ğ•œ fun x : E Ã— F => f x.1 x.2 :=
  { add_left := f.map_addâ‚‚
    smul_left := f.map_smulâ‚‚
    add_right := fun x => (f x).map_add
    smul_right := fun c x => (f x).map_smul c
    bound :=
      âŸ¨max â€–fâ€– 1, zero_lt_one.trans_le (le_max_right _ _), fun x y =>
        (f.le_opNormâ‚‚ x y).trans <| by
          apply_rules [mul_le_mul_of_nonneg_right, norm_nonneg, le_max_left] âŸ© }
#align continuous_linear_map.is_bounded_bilinear_map ContinuousLinearMap.isBoundedBilinearMap

-- Porting note (#11445): new definition
/-- A bounded bilinear map `f : E Ã— F â†’ G` defines a continuous linear map
`f : E â†’L[ğ•œ] F â†’L[ğ•œ] G`. -/
def IsBoundedBilinearMap.toContinuousLinearMap (hf : IsBoundedBilinearMap ğ•œ f) :
    E â†’L[ğ•œ] F â†’L[ğ•œ] G :=
  LinearMap.mkContinuousOfExistsBoundâ‚‚
    (LinearMap.mkâ‚‚ _ f.curry hf.add_left hf.smul_left hf.add_right hf.smul_right) <|
    hf.bound.imp fun _ â†¦ And.right

protected theorem IsBoundedBilinearMap.isBigO (h : IsBoundedBilinearMap ğ•œ f) :
    f =O[âŠ¤] fun p : E Ã— F => â€–p.1â€– * â€–p.2â€– :=
  let âŸ¨C, Cpos, hCâŸ© := h.bound
  Asymptotics.IsBigO.of_bound _ <|
    Filter.eventually_of_forall fun âŸ¨x, yâŸ© => by simpa [mul_assoc] using hC x y
set_option linter.uppercaseLean3 false in
#align is_bounded_bilinear_map.is_O IsBoundedBilinearMap.isBigO

theorem IsBoundedBilinearMap.isBigO_comp {Î± : Type*} (H : IsBoundedBilinearMap ğ•œ f) {g : Î± â†’ E}
    {h : Î± â†’ F} {l : Filter Î±} : (fun x => f (g x, h x)) =O[l] fun x => â€–g xâ€– * â€–h xâ€– :=
  H.isBigO.comp_tendsto le_top
set_option linter.uppercaseLean3 false in
#align is_bounded_bilinear_map.is_O_comp IsBoundedBilinearMap.isBigO_comp

protected theorem IsBoundedBilinearMap.isBigO' (h : IsBoundedBilinearMap ğ•œ f) :
    f =O[âŠ¤] fun p : E Ã— F => â€–pâ€– * â€–pâ€– :=
  h.isBigO.trans <|
    (@Asymptotics.isBigO_fst_prod' _ E F _ _ _ _).norm_norm.mul
      (@Asymptotics.isBigO_snd_prod' _ E F _ _ _ _).norm_norm
set_option linter.uppercaseLean3 false in
#align is_bounded_bilinear_map.is_O' IsBoundedBilinearMap.isBigO'

theorem IsBoundedBilinearMap.map_sub_left (h : IsBoundedBilinearMap ğ•œ f) {x y : E} {z : F} :
    f (x - y, z) = f (x, z) - f (y, z) :=
  (h.toContinuousLinearMap.flip z).map_sub x y
#align is_bounded_bilinear_map.map_sub_left IsBoundedBilinearMap.map_sub_left

theorem IsBoundedBilinearMap.map_sub_right (h : IsBoundedBilinearMap ğ•œ f) {x : E} {y z : F} :
    f (x, y - z) = f (x, y) - f (x, z) :=
  (h.toContinuousLinearMap x).map_sub y z
#align is_bounded_bilinear_map.map_sub_right IsBoundedBilinearMap.map_sub_right

open Asymptotics in
/-- Useful to use together with `Continuous.compâ‚‚`. -/
theorem IsBoundedBilinearMap.continuous (h : IsBoundedBilinearMap ğ•œ f) : Continuous f := by
  refine continuous_iff_continuousAt.2 fun x â†¦ tendsto_sub_nhds_zero_iff.1 ?_
  suffices Tendsto (fun y : E Ã— F â†¦ f (y.1 - x.1, y.2) + f (x.1, y.2 - x.2)) (ğ“ x) (ğ“ (0 + 0)) by
    simpa only [h.map_sub_left, h.map_sub_right, sub_add_sub_cancel, zero_add] using this
  apply Tendsto.add
  Â· rw [â† isLittleO_one_iff â„, â† one_mul 1]
    refine h.isBigO_comp.trans_isLittleO ?_
    refine (IsLittleO.norm_left ?_).mul_isBigO (IsBigO.norm_left ?_)
    Â· exact (isLittleO_one_iff _).2 (tendsto_sub_nhds_zero_iff.2 (continuous_fst.tendsto _))
    Â· exact (continuous_snd.tendsto _).isBigO_one â„
  Â· refine Continuous.tendsto' ?_ _ _ (by rw [h.map_sub_right, sub_self])
    exact ((h.toContinuousLinearMap x.1).continuous).comp (continuous_snd.sub continuous_const)
#align is_bounded_bilinear_map.continuous IsBoundedBilinearMap.continuous

theorem IsBoundedBilinearMap.continuous_left (h : IsBoundedBilinearMap ğ•œ f) {eâ‚‚ : F} :
    Continuous fun eâ‚ => f (eâ‚, eâ‚‚) :=
  h.continuous.comp (continuous_id.prod_mk continuous_const)
#align is_bounded_bilinear_map.continuous_left IsBoundedBilinearMap.continuous_left

theorem IsBoundedBilinearMap.continuous_right (h : IsBoundedBilinearMap ğ•œ f) {eâ‚ : E} :
    Continuous fun eâ‚‚ => f (eâ‚, eâ‚‚) :=
  h.continuous.comp (continuous_const.prod_mk continuous_id)
#align is_bounded_bilinear_map.continuous_right IsBoundedBilinearMap.continuous_right

/-- Useful to use together with `Continuous.compâ‚‚`. -/
theorem ContinuousLinearMap.continuousâ‚‚ (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) :
    Continuous (Function.uncurry fun x y => f x y) :=
  f.isBoundedBilinearMap.continuous
#align continuous_linear_map.continuousâ‚‚ ContinuousLinearMap.continuousâ‚‚

theorem IsBoundedBilinearMap.isBoundedLinearMap_left (h : IsBoundedBilinearMap ğ•œ f) (y : F) :
    IsBoundedLinearMap ğ•œ fun x => f (x, y) :=
  (h.toContinuousLinearMap.flip y).isBoundedLinearMap
#align is_bounded_bilinear_map.is_bounded_linear_map_left IsBoundedBilinearMap.isBoundedLinearMap_left

theorem IsBoundedBilinearMap.isBoundedLinearMap_right (h : IsBoundedBilinearMap ğ•œ f) (x : E) :
    IsBoundedLinearMap ğ•œ fun y => f (x, y) :=
  (h.toContinuousLinearMap x).isBoundedLinearMap
#align is_bounded_bilinear_map.is_bounded_linear_map_right IsBoundedBilinearMap.isBoundedLinearMap_right

theorem isBoundedBilinearMap_smul {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] {E : Type*}
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E] :
    IsBoundedBilinearMap ğ•œ fun p : ğ•œ' Ã— E => p.1 â€¢ p.2 :=
  (lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E).isBoundedBilinearMap
#align is_bounded_bilinear_map_smul isBoundedBilinearMap_smul

theorem isBoundedBilinearMap_mul : IsBoundedBilinearMap ğ•œ fun p : ğ•œ Ã— ğ•œ => p.1 * p.2 := by
  simp_rw [â† smul_eq_mul]
  exact isBoundedBilinearMap_smul
#align is_bounded_bilinear_map_mul isBoundedBilinearMap_mul

theorem isBoundedBilinearMap_comp :
    IsBoundedBilinearMap ğ•œ fun p : (F â†’L[ğ•œ] G) Ã— (E â†’L[ğ•œ] F) => p.1.comp p.2 :=
  (compL ğ•œ E F G).isBoundedBilinearMap
#align is_bounded_bilinear_map_comp isBoundedBilinearMap_comp

theorem ContinuousLinearMap.isBoundedLinearMap_comp_left (g : F â†’L[ğ•œ] G) :
    IsBoundedLinearMap ğ•œ fun f : E â†’L[ğ•œ] F => ContinuousLinearMap.comp g f :=
  isBoundedBilinearMap_comp.isBoundedLinearMap_right _
#align continuous_linear_map.is_bounded_linear_map_comp_left ContinuousLinearMap.isBoundedLinearMap_comp_left

theorem ContinuousLinearMap.isBoundedLinearMap_comp_right (f : E â†’L[ğ•œ] F) :
    IsBoundedLinearMap ğ•œ fun g : F â†’L[ğ•œ] G => ContinuousLinearMap.comp g f :=
  isBoundedBilinearMap_comp.isBoundedLinearMap_left _
#align continuous_linear_map.is_bounded_linear_map_comp_right ContinuousLinearMap.isBoundedLinearMap_comp_right

theorem isBoundedBilinearMap_apply : IsBoundedBilinearMap ğ•œ fun p : (E â†’L[ğ•œ] F) Ã— E => p.1 p.2 :=
  (ContinuousLinearMap.flip (apply ğ•œ F : E â†’L[ğ•œ] (E â†’L[ğ•œ] F) â†’L[ğ•œ] F)).isBoundedBilinearMap
#align is_bounded_bilinear_map_apply isBoundedBilinearMap_apply

/-- The function `ContinuousLinearMap.smulRight`, associating to a continuous linear map
`f : E â†’ ğ•œ` and a scalar `c : F` the tensor product `f âŠ— c` as a continuous linear map from `E` to
`F`, is a bounded bilinear map. -/
theorem isBoundedBilinearMap_smulRight :
    IsBoundedBilinearMap ğ•œ fun p =>
      (ContinuousLinearMap.smulRight : (E â†’L[ğ•œ] ğ•œ) â†’ F â†’ E â†’L[ğ•œ] F) p.1 p.2 :=
  (smulRightL ğ•œ E F).isBoundedBilinearMap
#align is_bounded_bilinear_map_smul_right isBoundedBilinearMap_smulRight

/-- The composition of a continuous linear map with a continuous multilinear map is a bounded
bilinear operation. -/
theorem isBoundedBilinearMap_compMultilinear {Î¹ : Type*} {E : Î¹ â†’ Type*} [Fintype Î¹]
    [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)] :
    IsBoundedBilinearMap ğ•œ fun p : (F â†’L[ğ•œ] G) Ã— ContinuousMultilinearMap ğ•œ E F =>
      p.1.compContinuousMultilinearMap p.2 :=
  (compContinuousMultilinearMapL ğ•œ E F G).isBoundedBilinearMap
#align is_bounded_bilinear_map_comp_multilinear isBoundedBilinearMap_compMultilinear

/-- Definition of the derivative of a bilinear map `f`, given at a point `p` by
`q â†¦ f(p.1, q.2) + f(q.1, p.2)` as in the standard formula for the derivative of a product.
We define this function here as a linear map `E Ã— F â†’â‚—[ğ•œ] G`, then `IsBoundedBilinearMap.deriv`
strengthens it to a continuous linear map `E Ã— F â†’L[ğ•œ] G`.
-/
def IsBoundedBilinearMap.linearDeriv (h : IsBoundedBilinearMap ğ•œ f) (p : E Ã— F) : E Ã— F â†’â‚—[ğ•œ] G :=
  (h.toContinuousLinearMap.derivâ‚‚ p).toLinearMap
#align is_bounded_bilinear_map.linear_deriv IsBoundedBilinearMap.linearDeriv

/-- The derivative of a bounded bilinear map at a point `p : E Ã— F`, as a continuous linear map
from `E Ã— F` to `G`. The statement that this is indeed the derivative of `f` is
`IsBoundedBilinearMap.hasFDerivAt` in `Analysis.Calculus.FDeriv`. -/
def IsBoundedBilinearMap.deriv (h : IsBoundedBilinearMap ğ•œ f) (p : E Ã— F) : E Ã— F â†’L[ğ•œ] G :=
  h.toContinuousLinearMap.derivâ‚‚ p
#align is_bounded_bilinear_map.deriv IsBoundedBilinearMap.deriv

@[simp]
theorem IsBoundedBilinearMap.deriv_apply (h : IsBoundedBilinearMap ğ•œ f) (p q : E Ã— F) :
    h.deriv p q = f (p.1, q.2) + f (q.1, p.2) :=
  rfl
#align is_bounded_bilinear_map_deriv_coe IsBoundedBilinearMap.deriv_apply

variable (ğ•œ)

/-- The function `ContinuousLinearMap.mulLeftRight : ğ•œ' Ã— ğ•œ' â†’ (ğ•œ' â†’L[ğ•œ] ğ•œ')` is a bounded
bilinear map. -/
theorem ContinuousLinearMap.mulLeftRight_isBoundedBilinear (ğ•œ' : Type*) [NormedRing ğ•œ']
    [NormedAlgebra ğ•œ ğ•œ'] :
    IsBoundedBilinearMap ğ•œ fun p : ğ•œ' Ã— ğ•œ' => ContinuousLinearMap.mulLeftRight ğ•œ ğ•œ' p.1 p.2 :=
  (ContinuousLinearMap.mulLeftRight ğ•œ ğ•œ').isBoundedBilinearMap
#align continuous_linear_map.mul_left_right_is_bounded_bilinear ContinuousLinearMap.mulLeftRight_isBoundedBilinear

variable {ğ•œ}

/-- Given a bounded bilinear map `f`, the map associating to a point `p` the derivative of `f` at
`p` is itself a bounded linear map. -/
theorem IsBoundedBilinearMap.isBoundedLinearMap_deriv (h : IsBoundedBilinearMap ğ•œ f) :
    IsBoundedLinearMap ğ•œ fun p : E Ã— F => h.deriv p :=
  h.toContinuousLinearMap.derivâ‚‚.isBoundedLinearMap
#align is_bounded_bilinear_map.is_bounded_linear_map_deriv IsBoundedBilinearMap.isBoundedLinearMap_deriv

end BilinearMap

@[continuity]
theorem Continuous.clm_comp {X} [TopologicalSpace X] {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F}
    (hg : Continuous g) (hf : Continuous f) : Continuous fun x => (g x).comp (f x) :=
  (compL ğ•œ E F G).continuousâ‚‚.compâ‚‚ hg hf
#align continuous.clm_comp Continuous.clm_comp

theorem ContinuousOn.clm_comp {X} [TopologicalSpace X] {g : X â†’ F â†’L[ğ•œ] G} {f : X â†’ E â†’L[ğ•œ] F}
    {s : Set X} (hg : ContinuousOn g s) (hf : ContinuousOn f s) :
    ContinuousOn (fun x => (g x).comp (f x)) s :=
  (compL ğ•œ E F G).continuousâ‚‚.comp_continuousOn (hg.prod hf)
#align continuous_on.clm_comp ContinuousOn.clm_comp

@[continuity]
theorem Continuous.clm_apply {X} [TopologicalSpace X] {f : X â†’ (E â†’L[ğ•œ] F)} {g : X â†’ E}
    (hf : Continuous f) (hg : Continuous g) : Continuous (fun x â†¦ (f x) (g x)) :=
  isBoundedBilinearMap_apply.continuous.compâ‚‚ hf hg

theorem ContinuousOn.clm_apply {X} [TopologicalSpace X] {f : X â†’ (E â†’L[ğ•œ] F)} {g : X â†’ E}
    {s : Set X} (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun x â†¦ f x (g x)) s :=
  isBoundedBilinearMap_apply.continuous.comp_continuousOn (hf.prod hg)

namespace ContinuousLinearEquiv

open Set

/-!
### The set of continuous linear equivalences between two Banach spaces is open

In this section we establish that the set of continuous linear equivalences between two Banach
spaces is an open subset of the space of linear maps between them.
-/

protected theorem isOpen [CompleteSpace E] : IsOpen (range ((â†‘) : (E â‰ƒL[ğ•œ] F) â†’ E â†’L[ğ•œ] F)) := by
  rw [isOpen_iff_mem_nhds, forall_mem_range]
  refine fun e => IsOpen.mem_nhds ?_ (mem_range_self _)
  let O : (E â†’L[ğ•œ] F) â†’ E â†’L[ğ•œ] E := fun f => (e.symm : F â†’L[ğ•œ] E).comp f
  have h_O : Continuous O := isBoundedBilinearMap_comp.continuous_right
  convert show IsOpen (O â»Â¹' { x | IsUnit x }) from Units.isOpen.preimage h_O using 1
  ext f'
  constructor
  Â· rintro âŸ¨e', rflâŸ©
    exact âŸ¨(e'.trans e.symm).toUnit, rflâŸ©
  Â· rintro âŸ¨w, hwâŸ©
    use (unitsEquiv ğ•œ E w).trans e
    ext x
    simp [O, hw]
#align continuous_linear_equiv.is_open ContinuousLinearEquiv.isOpen

protected theorem nhds [CompleteSpace E] (e : E â‰ƒL[ğ•œ] F) :
    range ((â†‘) : (E â‰ƒL[ğ•œ] F) â†’ E â†’L[ğ•œ] F) âˆˆ ğ“ (e : E â†’L[ğ•œ] F) :=
  IsOpen.mem_nhds ContinuousLinearEquiv.isOpen (by simp)
#align continuous_linear_equiv.nhds ContinuousLinearEquiv.nhds

end ContinuousLinearEquiv
