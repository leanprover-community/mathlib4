/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Ya√´l Dillies
-/
import Mathlib.LinearAlgebra.Ray
import Mathlib.Analysis.NormedSpace.Basic

#align_import analysis.normed_space.ray from "leanprover-community/mathlib"@"92ca63f0fb391a9ca5f22d2409a6080e786d99f7"

/-!
# Rays in a real normed vector space

In this file we prove some lemmas about the `SameRay` predicate in case of a real normed space. In
this case, for two vectors `x y` in the same ray, the norm of their sum is equal to the sum of their
norms and `‚Äñy‚Äñ ‚Ä¢ x = ‚Äñx‚Äñ ‚Ä¢ y`.
-/


open Real

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E] {F : Type _}
  [NormedAddCommGroup F] [NormedSpace ‚Ñù F]

namespace SameRay

variable {x y : E}

/-- If `x` and `y` are on the same ray, then the triangle inequality becomes the equality: the norm
of `x + y` is the sum of the norms of `x` and `y`. The converse is true for a strictly convex
space. -/
theorem norm_add (h : SameRay ‚Ñù x y) : ‚Äñx + y‚Äñ = ‚Äñx‚Äñ + ‚Äñy‚Äñ := by
  rcases h.exists_eq_smul with ‚ü®u, a, b, ha, hb, -, rfl, rfl‚ü©
  -- ‚ä¢ ‚Äña ‚Ä¢ u + b ‚Ä¢ u‚Äñ = ‚Äña ‚Ä¢ u‚Äñ + ‚Äñb ‚Ä¢ u‚Äñ
  rw [‚Üê add_smul, norm_smul_of_nonneg (add_nonneg ha hb), norm_smul_of_nonneg ha,
    norm_smul_of_nonneg hb, add_mul]
#align same_ray.norm_add SameRay.norm_add

theorem norm_sub (h : SameRay ‚Ñù x y) : ‚Äñx - y‚Äñ = |‚Äñx‚Äñ - ‚Äñy‚Äñ| := by
  rcases h.exists_eq_smul with ‚ü®u, a, b, ha, hb, -, rfl, rfl‚ü©
  -- ‚ä¢ ‚Äña ‚Ä¢ u - b ‚Ä¢ u‚Äñ = |‚Äña ‚Ä¢ u‚Äñ - ‚Äñb ‚Ä¢ u‚Äñ|
  wlog hab : b ‚â§ a with H
  -- ‚ä¢ ‚Äña ‚Ä¢ u - b ‚Ä¢ u‚Äñ = |‚Äña ‚Ä¢ u‚Äñ - ‚Äñb ‚Ä¢ u‚Äñ|
  ¬∑ rw [SameRay.sameRay_comm] at h
    -- ‚ä¢ ‚Äña ‚Ä¢ u - b ‚Ä¢ u‚Äñ = |‚Äña ‚Ä¢ u‚Äñ - ‚Äñb ‚Ä¢ u‚Äñ|
    rw [norm_sub_rev, abs_sub_comm]
    -- ‚ä¢ ‚Äñb ‚Ä¢ u - a ‚Ä¢ u‚Äñ = |‚Äñb ‚Ä¢ u‚Äñ - ‚Äña ‚Ä¢ u‚Äñ|
    have := @H E _ _ ‚Ñù
    -- ‚ä¢ ‚Äñb ‚Ä¢ u - a ‚Ä¢ u‚Äñ = |‚Äñb ‚Ä¢ u‚Äñ - ‚Äña ‚Ä¢ u‚Äñ|
    exact this u b a hb ha h (le_of_not_le hab)
    -- üéâ no goals
  rw [‚Üê sub_nonneg] at hab
  -- ‚ä¢ ‚Äña ‚Ä¢ u - b ‚Ä¢ u‚Äñ = |‚Äña ‚Ä¢ u‚Äñ - ‚Äñb ‚Ä¢ u‚Äñ|
  rw [‚Üê sub_smul, norm_smul_of_nonneg hab, norm_smul_of_nonneg ha, norm_smul_of_nonneg hb, ‚Üê
    sub_mul, abs_of_nonneg (mul_nonneg hab (norm_nonneg _))]
#align same_ray.norm_sub SameRay.norm_sub

theorem norm_smul_eq (h : SameRay ‚Ñù x y) : ‚Äñx‚Äñ ‚Ä¢ y = ‚Äñy‚Äñ ‚Ä¢ x := by
  rcases h.exists_eq_smul with ‚ü®u, a, b, ha, hb, -, rfl, rfl‚ü©
  -- ‚ä¢ ‚Äña ‚Ä¢ u‚Äñ ‚Ä¢ b ‚Ä¢ u = ‚Äñb ‚Ä¢ u‚Äñ ‚Ä¢ a ‚Ä¢ u
  simp only [norm_smul_of_nonneg, *, mul_smul]
  -- ‚ä¢ a ‚Ä¢ ‚Äñu‚Äñ ‚Ä¢ b ‚Ä¢ u = b ‚Ä¢ ‚Äñu‚Äñ ‚Ä¢ a ‚Ä¢ u
  rw [smul_comm, smul_comm b, smul_comm a b u]
  -- üéâ no goals
#align same_ray.norm_smul_eq SameRay.norm_smul_eq

end SameRay

variable {x y : F}

theorem norm_injOn_ray_left (hx : x ‚â† 0) : { y | SameRay ‚Ñù x y }.InjOn norm := by
  rintro y hy z hz h
  -- ‚ä¢ y = z
  rcases hy.exists_nonneg_left hx with ‚ü®r, hr, rfl‚ü©
  -- ‚ä¢ r ‚Ä¢ x = z
  rcases hz.exists_nonneg_left hx with ‚ü®s, hs, rfl‚ü©
  -- ‚ä¢ r ‚Ä¢ x = s ‚Ä¢ x
  rw [norm_smul, norm_smul, mul_left_inj' (norm_ne_zero_iff.2 hx), norm_of_nonneg hr,
    norm_of_nonneg hs] at h
  rw [h]
  -- üéâ no goals
#align norm_inj_on_ray_left norm_injOn_ray_left

theorem norm_injOn_ray_right (hy : y ‚â† 0) : { x | SameRay ‚Ñù x y }.InjOn norm := by
  simpa only [SameRay.sameRay_comm] using norm_injOn_ray_left hy
  -- üéâ no goals
#align norm_inj_on_ray_right norm_injOn_ray_right

theorem sameRay_iff_norm_smul_eq : SameRay ‚Ñù x y ‚Üî ‚Äñx‚Äñ ‚Ä¢ y = ‚Äñy‚Äñ ‚Ä¢ x :=
  ‚ü®SameRay.norm_smul_eq, fun h =>
    or_iff_not_imp_left.2 fun hx =>
      or_iff_not_imp_left.2 fun hy => ‚ü®‚Äñy‚Äñ, ‚Äñx‚Äñ, norm_pos_iff.2 hy, norm_pos_iff.2 hx, h.symm‚ü©‚ü©
#align same_ray_iff_norm_smul_eq sameRay_iff_norm_smul_eq

/-- Two nonzero vectors `x y` in a real normed space are on the same ray if and only if the unit
vectors `‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x` and `‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y` are equal. -/
theorem sameRay_iff_inv_norm_smul_eq_of_ne (hx : x ‚â† 0) (hy : y ‚â† 0) :
    SameRay ‚Ñù x y ‚Üî ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x = ‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y := by
  rw [inv_smul_eq_iff‚ÇÄ, smul_comm, eq_comm, inv_smul_eq_iff‚ÇÄ, sameRay_iff_norm_smul_eq] <;>
  -- ‚ä¢ ‚Äñy‚Äñ ‚â† 0
    rwa [norm_ne_zero_iff]
    -- üéâ no goals
    -- üéâ no goals
#align same_ray_iff_inv_norm_smul_eq_of_ne sameRay_iff_inv_norm_smul_eq_of_ne

alias ‚ü®SameRay.inv_norm_smul_eq, _‚ü© := sameRay_iff_inv_norm_smul_eq_of_ne
#align same_ray.inv_norm_smul_eq SameRay.inv_norm_smul_eq

/-- Two vectors `x y` in a real normed space are on the ray if and only if one of them is zero or
the unit vectors `‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x` and `‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y` are equal. -/
theorem sameRay_iff_inv_norm_smul_eq : SameRay ‚Ñù x y ‚Üî x = 0 ‚à® y = 0 ‚à® ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x = ‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y := by
  rcases eq_or_ne x 0 with (rfl | hx); ¬∑ simp [SameRay.zero_left]
  -- ‚ä¢ SameRay ‚Ñù 0 y ‚Üî 0 = 0 ‚à® y = 0 ‚à® ‚Äñ0‚Äñ‚Åª¬π ‚Ä¢ 0 = ‚Äñy‚Äñ‚Åª¬π ‚Ä¢ y
                                         -- üéâ no goals
  rcases eq_or_ne y 0 with (rfl | hy); ¬∑ simp [SameRay.zero_right]
  -- ‚ä¢ SameRay ‚Ñù x 0 ‚Üî x = 0 ‚à® 0 = 0 ‚à® ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x = ‚Äñ0‚Äñ‚Åª¬π ‚Ä¢ 0
                                         -- üéâ no goals
  simp only [sameRay_iff_inv_norm_smul_eq_of_ne hx hy, *, false_or_iff]
  -- üéâ no goals
#align same_ray_iff_inv_norm_smul_eq sameRay_iff_inv_norm_smul_eq

/-- Two vectors of the same norm are on the same ray if and only if they are equal. -/
theorem sameRay_iff_of_norm_eq (h : ‚Äñx‚Äñ = ‚Äñy‚Äñ) : SameRay ‚Ñù x y ‚Üî x = y := by
  obtain rfl | hy := eq_or_ne y 0
  -- ‚ä¢ SameRay ‚Ñù x 0 ‚Üî x = 0
  ¬∑ rw [norm_zero, norm_eq_zero] at h
    -- ‚ä¢ SameRay ‚Ñù x 0 ‚Üî x = 0
    exact iff_of_true (SameRay.zero_right _) h
    -- üéâ no goals
  ¬∑ exact ‚ü®fun hxy => norm_injOn_ray_right hy hxy SameRay.rfl h, fun hxy => hxy ‚ñ∏ SameRay.rfl‚ü©
    -- üéâ no goals
#align same_ray_iff_of_norm_eq sameRay_iff_of_norm_eq

theorem not_sameRay_iff_of_norm_eq (h : ‚Äñx‚Äñ = ‚Äñy‚Äñ) : ¬¨SameRay ‚Ñù x y ‚Üî x ‚â† y :=
  (sameRay_iff_of_norm_eq h).not
#align not_same_ray_iff_of_norm_eq not_sameRay_iff_of_norm_eq

/-- If two points on the same ray have the same norm, then they are equal. -/
theorem SameRay.eq_of_norm_eq (h : SameRay ‚Ñù x y) (hn : ‚Äñx‚Äñ = ‚Äñy‚Äñ) : x = y :=
  (sameRay_iff_of_norm_eq hn).mp h
#align same_ray.eq_of_norm_eq SameRay.eq_of_norm_eq

/-- The norms of two vectors on the same ray are equal if and only if they are equal. -/
theorem SameRay.norm_eq_iff (h : SameRay ‚Ñù x y) : ‚Äñx‚Äñ = ‚Äñy‚Äñ ‚Üî x = y :=
  ‚ü®h.eq_of_norm_eq, fun h => h ‚ñ∏ rfl‚ü©
#align same_ray.norm_eq_iff SameRay.norm_eq_iff
