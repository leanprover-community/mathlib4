/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.UniformLimitsDeriv
import Mathlib.Topology.Algebra.InfiniteSum.UniformOn

/-!
# Continuity of series of functions

We show that series of functions are continuous when each individual function in the series is and
additionally suitable uniform summable bounds are satisfied, in `continuous_tsum`.

For smoothness of series of functions, see the file `Analysis.Calculus.SmoothSeries`.
-/

open Set Metric TopologicalSpace Function Filter

open scoped Topology NNReal

variable {Î± Î² F : Type*} [NormedAddCommGroup F] [CompleteSpace F] {u : Î± â†’ â„}

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version relative to a set, with general index set. -/
theorem tendstoUniformlyOn_tsum {f : Î± â†’ Î² â†’ F} (hu : Summable u) {s : Set Î²}
    (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
    TendstoUniformlyOn (fun t : Finset Î± => fun x => âˆ‘ n âˆˆ t, f n x) (fun x => âˆ‘' n, f n x) atTop
      s := by
  refine tendstoUniformlyOn_iff.2 fun Îµ Îµpos => ?_
  filter_upwards [(tendsto_order.1 (tendsto_tsum_compl_atTop_zero u)).2 _ Îµpos] with t ht x hx
  have A : Summable fun n => â€–f n xâ€– :=
    .of_nonneg_of_le (fun _ â†¦ norm_nonneg _) (fun n => hfu n x hx) hu
  rw [dist_eq_norm, â† A.of_norm.sum_add_tsum_subtype_compl t, add_sub_cancel_left]
  apply lt_of_le_of_lt _ ht
  apply (norm_tsum_le_tsum_norm (A.subtype _)).trans
  exact (A.subtype _).tsum_le_tsum (fun n => hfu _ _ hx) (hu.subtype _)

theorem HasSumUniformlyOn_of_bounded {f : Î± â†’ Î² â†’ F} (hu : Summable u) {s : Set Î²}
    (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) : HasSumUniformlyOn f (fun x => âˆ‘' n, f n x) {s} :=  by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn, tendstoUniformlyOn_tsum hu hfu]

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version relative to a set, with index set `â„•`. -/
theorem tendstoUniformlyOn_tsum_nat {f : â„• â†’ Î² â†’ F} {u : â„• â†’ â„} (hu : Summable u) {s : Set Î²}
    (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
    TendstoUniformlyOn (fun N => fun x => âˆ‘ n âˆˆ Finset.range N, f n x) (fun x => âˆ‘' n, f n x) atTop
      s :=
  fun v hv => tendsto_finset_range.eventually (tendstoUniformlyOn_tsum hu hfu v hv)

/-- An infinite sum of functions with eventually summable sup norm is the uniform limit of its
partial sums. Version relative to a set, with general index set. -/
theorem tendstoUniformlyOn_tsum_of_cofinite_eventually {Î¹ : Type*} {f : Î¹ â†’ Î² â†’ F} {u : Î¹ â†’ â„}
    (hu : Summable u) {s : Set Î²} (hfu : âˆ€á¶  n in cofinite, âˆ€ x âˆˆ s, â€–f n xâ€– â‰¤ u n) :
    TendstoUniformlyOn (fun t x => âˆ‘ n âˆˆ t, f n x) (fun x => âˆ‘' n, f n x) atTop s := by
  classical
  refine tendstoUniformlyOn_iff.2 fun Îµ Îµpos => ?_
  have := (tendsto_order.1 (tendsto_tsum_compl_atTop_zero u)).2 _ Îµpos
  simp only [not_forall, Classical.not_imp, not_le, gt_iff_lt,
    eventually_atTop, ge_iff_le, Finset.le_eq_subset] at *
  obtain âŸ¨t, htâŸ© := this
  rw [eventually_iff_exists_mem] at hfu
  obtain âŸ¨N, hN, HNâŸ© := hfu
  refine âŸ¨hN.toFinset âˆª t, fun n hn x hx => ?_âŸ©
  have A : Summable fun n => â€–f n xâ€– := by
    apply Summable.add_compl (s := hN.toFinset) Summable.of_finite
    apply Summable.of_nonneg_of_le (fun _ â†¦ norm_nonneg _) _ (hu.subtype _)
    simp only [comp_apply, Subtype.forall, Set.mem_compl_iff, Finset.mem_coe]
    aesop
  rw [dist_eq_norm, â† A.of_norm.sum_add_tsum_subtype_compl n, add_sub_cancel_left]
  apply lt_of_le_of_lt _ (ht n (Finset.union_subset_right hn))
  apply (norm_tsum_le_tsum_norm (A.subtype _)).trans
  apply (A.subtype _).tsum_le_tsum _ (hu.subtype _)
  simp only [comp_apply, Subtype.forall, imp_false]
  apply fun i hi => HN i ?_ x hx
  have : Â¬ i âˆˆ hN.toFinset := fun hg â†¦ hi (Finset.union_subset_left hn hg)
  aesop

theorem HasSumUniformlyOn_of_cofinite_eventually {Î¹ : Type*} {f : Î¹ â†’ Î² â†’ F} {u : Î¹ â†’ â„}
    (hu : Summable u) {s : Set Î²} (hfu : âˆ€á¶  n in cofinite, âˆ€ x âˆˆ s, â€–f n xâ€– â‰¤ u n) :
    HasSumUniformlyOn f (fun x => âˆ‘' n, f n x) {s} := by
  simp [hasSumUniformlyOn_iff_tendstoUniformlyOn,
    tendstoUniformlyOn_tsum_of_cofinite_eventually hu hfu]

theorem tendstoUniformlyOn_tsum_nat_eventually {Î± F : Type*} [NormedAddCommGroup F]
    [CompleteSpace F] {f : â„• â†’ Î± â†’ F} {u : â„• â†’ â„} (hu : Summable u) {s : Set Î±}
    (hfu : âˆ€á¶  n in atTop, âˆ€ x âˆˆ s, â€–f n xâ€– â‰¤ u n) :
    TendstoUniformlyOn (fun N x => âˆ‘ n âˆˆ Finset.range N, f n x)
       (fun x => âˆ‘' n, f n x) atTop s :=
  fun v hv â†¦ tendsto_finset_range.eventually <|
    tendstoUniformlyOn_tsum_of_cofinite_eventually hu (Nat.cofinite_eq_atTop â–¸ hfu) v hv

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version with general index set. -/
theorem tendstoUniformly_tsum {f : Î± â†’ Î² â†’ F} (hu : Summable u) (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) :
    TendstoUniformly (fun t : Finset Î± => fun x => âˆ‘ n âˆˆ t, f n x)
      (fun x => âˆ‘' n, f n x) atTop := by
  rw [â† tendstoUniformlyOn_univ]; exact tendstoUniformlyOn_tsum hu fun n x _ => hfu n x

/-- An infinite sum of functions with summable sup norm is the uniform limit of its partial sums.
Version with index set `â„•`. -/
theorem tendstoUniformly_tsum_nat {f : â„• â†’ Î² â†’ F} {u : â„• â†’ â„} (hu : Summable u)
    (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) :
    TendstoUniformly (fun N => fun x => âˆ‘ n âˆˆ Finset.range N, f n x) (fun x => âˆ‘' n, f n x)
      atTop :=
  fun v hv => tendsto_finset_range.eventually (tendstoUniformly_tsum hu hfu v hv)

/-- An infinite sum of functions with eventually summable sup norm is the uniform limit of its
partial sums. Version with general index set. -/
theorem tendstoUniformly_tsum_of_cofinite_eventually {Î¹ : Type*} {f : Î¹ â†’ Î² â†’ F} {u : Î¹ â†’ â„}
    (hu : Summable u) (hfu : âˆ€á¶  (n : Î¹) in cofinite, âˆ€ x : Î², â€–f n xâ€– â‰¤ u n) :
    TendstoUniformly (fun t x => âˆ‘ n âˆˆ t, f n x) (fun x => âˆ‘' n, f n x) atTop := by
  rw [â† tendstoUniformlyOn_univ]
  apply tendstoUniformlyOn_tsum_of_cofinite_eventually hu
  simpa using hfu

/-- An infinite sum of functions with summable sup norm is continuous on a set if each individual
function is. -/
theorem continuousOn_tsum [TopologicalSpace Î²] {f : Î± â†’ Î² â†’ F} {s : Set Î²}
    (hf : âˆ€ i, ContinuousOn (f i) s) (hu : Summable u) (hfu : âˆ€ n x, x âˆˆ s â†’ â€–f n xâ€– â‰¤ u n) :
    ContinuousOn (fun x => âˆ‘' n, f n x) s := by
  classical
    refine (tendstoUniformlyOn_tsum hu hfu).continuousOn (Eventually.of_forall ?_)
    intro t
    exact continuousOn_finset_sum _ fun i _ => hf i

/-- An infinite sum of functions with summable sup norm is continuous if each individual
function is. -/
theorem continuous_tsum [TopologicalSpace Î²] {f : Î± â†’ Î² â†’ F} (hf : âˆ€ i, Continuous (f i))
    (hu : Summable u) (hfu : âˆ€ n x, â€–f n xâ€– â‰¤ u n) : Continuous fun x => âˆ‘' n, f n x := by
  simp_rw [continuous_iff_continuousOn_univ] at hf âŠ¢
  exact continuousOn_tsum hf hu fun n x _ => hfu n x

variable {ð•œ ð•œ' Î¹: Type*} [NormedAddCommGroup ð•œ'] [CompleteSpace ð•œ'] [TopologicalSpace ð•œ]
  [LocallyCompactSpace ð•œ]

lemma SummableLocallyUniformlyOn.of_locally_bounded (f : Î¹ â†’ ð•œ â†’ ð•œ') {s : Set ð•œ} (hs : IsOpen s)
    (hu : âˆ€ K âŠ† s, IsCompact K â†’ âˆƒ u : Î¹ â†’ â„, Summable u âˆ§ âˆ€ n (k : K), â€–f n kâ€– â‰¤ u n) :
    SummableLocallyUniformlyOn f s := by
  apply HasSumLocallyUniformlyOn.summableLocallyUniformlyOn (g := (fun x => âˆ‘' i, f i x))
  rw [hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn,
    tendstoLocallyUniformlyOn_iff_forall_isCompact hs]
  intro K hK hKc
  obtain âŸ¨u, hu1, hu2âŸ© := hu K hK hKc
  apply tendstoUniformlyOn_tsum hu1 fun n x hx â†¦ hu2 n âŸ¨x, hxâŸ©

theorem derivWithin_tsum {Î¹ F E : Type*} [NontriviallyNormedField E] [IsRCLikeNormedField E]
    [LocallyCompactSpace E] [NormedField F] [NormedSpace E F] (f : Î¹ â†’ E â†’ F) {s : Set E}
    (hs : IsOpen s) {x : E} (hx : x âˆˆ s) (hf : âˆ€ y âˆˆ s, Summable fun n â†¦ f n y)
    (h : SummableLocallyUniformlyOn (fun n â†¦ (derivWithin (fun z â†¦ f n z) s)) s)
    (hf2 : âˆ€ n r, r âˆˆ s â†’ DifferentiableAt E (f n) r) :
    derivWithin (fun z â†¦ âˆ‘' n , f n z) s x = âˆ‘' n, derivWithin (fun z â†¦ f n z) s x := by
  apply HasDerivWithinAt.derivWithin ?_ (IsOpen.uniqueDiffWithinAt hs hx)
  apply HasDerivAt.hasDerivWithinAt
  apply hasDerivAt_of_tendstoLocallyUniformlyOn hs _ _ (fun y hy â†¦ Summable.hasSum (hf y hy)) hx
  Â· use fun n : Finset Î¹ â†¦ fun a â†¦ âˆ‘ i âˆˆ n, derivWithin (fun z â†¦ f i z) s a
  Â· obtain âŸ¨g, hgâŸ© := h
    apply (hasSumLocallyUniformlyOn_iff_tendstoLocallyUniformlyOn.mp hg).congr_right
    exact fun â¦ƒbâ¦„ hb â†¦ Eq.symm (HasSumLocallyUniformlyOn.tsum_eqOn hg hb)
  Â· filter_upwards with t r hr
    apply HasDerivAt.sum
    intro q hq
    apply HasDerivWithinAt.hasDerivAt
    Â· exact DifferentiableWithinAt.hasDerivWithinAt (hf2 q r hr).differentiableWithinAt
    Â· exact IsOpen.mem_nhds hs hr
