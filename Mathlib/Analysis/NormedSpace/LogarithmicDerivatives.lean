/-
Copyright (c) 2024 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Analysis.Complex.LocallyUniformLimit
import Mathlib.Analysis.SpecialFunctions.Trigonometric.Deriv
import Mathlib.Analysis.SpecialFunctions.Complex.LogDeriv

/-!
# Logarithmic Derivatives

We define the logarithmic derivative of a function f as `deriv f / f`. We then prove some basic
facts about this, including how it changes under multiplication and composition.

We conclude by showing that the logarithmic derivative of a sequence of functions converging
locally uniformly to a function is the logarithmic derivative of the limit function. This is useful
for example for the Mittag-Leffler expansion of the cotangent function.
-/

noncomputable section

open Filter Function

open scoped Topology BigOperators Classical

variable {ùïú ùïú': Type*} [NontriviallyNormedField ùïú] [NontriviallyNormedField ùïú']
  [NormedAlgebra ùïú ùïú']

/-- The logarithmic derivative of a function defined as `deriv f /f`. Note that it will be zero
at `x` if `f` is not DifferentiableAt `x`. -/
def logDeriv (f : ùïú ‚Üí ùïú') :=
  deriv f / f

lemma logDeriv_eq_zero_of_not_differentiableAt (f : ùïú ‚Üí ùïú') (x : ùïú) (h : ¬¨DifferentiableAt ùïú f x) :
    logDeriv f x = 0 := by
  simp only [logDeriv, Pi.div_apply, deriv_zero_of_not_differentiableAt h, zero_div]

@[simp]
theorem logDeriv_id (x : ùïú) : logDeriv id x = 1 / x := by
  rw [logDeriv]
  simp only [deriv_id', Pi.div_apply, id_eq, one_div]

@[simp]
theorem logDeriv_const (a : ùïú') : logDeriv (fun _ : ùïú ‚Ü¶ a) = 0 := by
  rw [logDeriv]
  ext1 x
  simp only [deriv_const', Pi.div_apply, zero_div, Pi.zero_apply]

theorem logDeriv_mul {f g : ùïú ‚Üí ùïú'} (x : ùïú) (hf : f x ‚â† 0) (hg : g x ‚â† 0)
    (hdf : DifferentiableAt ùïú f x) (hdg : DifferentiableAt ùïú g x) :
      logDeriv (fun z => f z * g z) x = logDeriv f x + logDeriv g x := by
  simp only [logDeriv, Pi.div_apply, deriv_mul hdf hdg]
  field_simp [hf, hg , mul_comm]

theorem logDeriv_mul_const {f : ùïú ‚Üí ùïú'} (x : ùïú) (a : ùïú') (ha : a ‚â† 0):
    logDeriv (fun z => f z * a) x = logDeriv f x := by
  simp only [logDeriv, deriv_mul_const_field', Pi.div_apply]
  rw [mul_div_mul_right (deriv (fun x ‚Ü¶ f x) x) (f x) ha]

theorem logDeriv_const_mul {f : ùïú ‚Üí ùïú'} (x : ùïú) (a : ùïú') (ha : a ‚â† 0):
    logDeriv (fun z => a * f z) x = logDeriv f x := by
  simp only [logDeriv, deriv_const_mul_field', Pi.div_apply]
  rw [mul_div_mul_left (deriv (fun x ‚Ü¶ f x) x) (f x) ha]

/-- The logarithmic derivative of a finite product is the sum of the logarithmic derivatives. -/
theorem logDeriv_prod {Œ± : Type*} (s : Finset Œ±) (f : Œ± ‚Üí ùïú ‚Üí ùïú') (t : ùïú) (hf : ‚àÄ x ‚àà s, f x t ‚â† 0)
    (hd : ‚àÄ x ‚àà s, DifferentiableAt ùïú (f x) t) :
    logDeriv (‚àè i in s, f i) t = ‚àë i in s, logDeriv (f i) t := by
  induction' s using Finset.cons_induction_on with a s ha ih
  ¬∑ simp only [Finset.prod_empty, Finset.sum_empty]
    exact congrFun (logDeriv_const (1 : ùïú')) t
  ¬∑ rw [Finset.forall_mem_cons] at hf
    rw [Finset.cons_eq_insert _ _ ha, Finset.prod_insert ha, Finset.sum_insert ha]
    have := logDeriv_mul (f := f a) (g := ‚àè i in s, f i) t ?_ ?_ ?_ ?_
    ¬∑ simp only [ne_eq, Finset.cons_eq_insert, Finset.mem_insert, forall_eq_or_imp,
        Finset.prod_apply] at *
      rw [ih hf.2 (fun _ hx ‚Ü¶ hd.2 _ hx)] at this
      rw [‚Üê this]
      congr
      exact Finset.prod_fn s fun c ‚Ü¶ f c
    ¬∑ exact hf.1
    ¬∑ simp only [Finset.prod_apply, Finset.prod_ne_zero_iff]
      exact hf.2
    ¬∑ apply hd
      simp only [Finset.cons_eq_insert, Finset.mem_insert, eq_self_iff_true, true_or_iff]
    ¬∑ rw [Finset.prod_fn]
      apply DifferentiableAt.finset_prod
      intro r hr
      apply hd
      simp only [Finset.cons_eq_insert, Finset.mem_insert, hr, or_true]

lemma logDeriv_pow {x : ùïú} {n : ‚Ñï} (hn : n ‚â† 0) (hx : x ‚â† 0) :
    logDeriv (fun z => z ^ n) x = n * logDeriv id x := by
  simp only [logDeriv, deriv_pow', Pi.div_apply, deriv_id', id_eq, one_div]
  field_simp [hx, mul_assoc, pow_sub_one_mul hn x]

lemma logDeriv_zpow {x : ùïú} {n : ‚Ñ§} (hx : x ‚â† 0) :
    logDeriv (fun z => z ^ n) x = n * logDeriv id x := by
  simp [logDeriv, deriv_pow', Pi.div_apply, deriv_id', id_eq, one_div, zpow_sub_one‚ÇÄ hx]
  field_simp [hx, mul_assoc, mul_comm (x^n) x]
  simpa using (mul_div_mul_right (n : ùïú) 1 ( mul_ne_zero hx  (zpow_ne_zero n hx)))

theorem logDeriv_comp {f g : ùïú ‚Üí ùïú} {x : ùïú} (hf : DifferentiableAt ùïú f (g x))
    (hg : DifferentiableAt ùïú g x) : logDeriv (f ‚àò g) x = (logDeriv f) (g x) * deriv g x := by
  simp only [logDeriv, Pi.div_apply, deriv.comp _ hf hg, comp_apply]
  ring

lemma logDeriv_fun_pow {f : ùïú ‚Üí ùïú} {x : ùïú} {n : ‚Ñï} (hn : n ‚â† 0) (hf : f x ‚â† 0)
    (hdf : DifferentiableAt ùïú f x) :  logDeriv (fun z => (f z) ^ n) x = n * logDeriv f x := by
  rw [‚Üê comp_def (fun z => z^n) f,
    logDeriv_comp (f := fun z => z^n) (g := f) (differentiableAt_pow n) hdf, logDeriv_pow hn hf]
  simp only [logDeriv, deriv_id', Pi.div_apply, id_eq, one_div]
  ring

lemma logDeriv_fun_zpow {f : ùïú ‚Üí ùïú} {x : ùïú} {n : ‚Ñ§} (hf : f x ‚â† 0) (hdf : DifferentiableAt ùïú f x) :
    logDeriv (fun z => (f z) ^ n) x = n * logDeriv f x := by
  rw [show (fun z => (f z)^n) = (fun z => z^n) ‚àò f by rfl,
    logDeriv_comp (f := fun z => z^n) (g := f) ?_ hdf, logDeriv_zpow hf]
  ¬∑ simp only [logDeriv, deriv_id', Pi.div_apply, id_eq, one_div]
    ring
  ¬∑ simp only [differentiableAt_zpow, ne_eq, hf, not_false_eq_true, true_or]

/-- The logarithmic derivative of a sequence of functions converging locally uniformly to a
function is the logarithmic derivative of the limit function-/
theorem logDeriv_tendsto {Œπ : Type*} {p : Filter Œπ} (f : Œπ ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ) (g : ‚ÑÇ ‚Üí ‚ÑÇ)
    {s : Set ‚ÑÇ} (hs : IsOpen s) (x : s) (hF : TendstoLocallyUniformlyOn f g p s)
    (hf : ‚àÄ·∂† n : Œπ in p, DifferentiableOn ‚ÑÇ (f n) s) (hg : g x ‚â† 0) :
    Tendsto (fun n : Œπ => logDeriv (f n) x) p (ùìù ((logDeriv g) x)) := by
  simp_rw [logDeriv]
  apply Tendsto.div ((hF.deriv hf hs).tendsto_at x.2) (hF.tendsto_at x.2) hg

section examples

theorem logDeriv_sin : logDeriv (Complex.sin) = Complex.cot := by
  ext
  rw [logDeriv, Complex.deriv_sin, Pi.div_apply, Complex.cot]

theorem logDeriv_cos : logDeriv (Complex.cos) = -Complex.tan := by
  ext
  rw [logDeriv, Complex.deriv_cos', Pi.div_apply, Pi.neg_apply, Complex.tan, neg_div]

end examples

section log

/-- The derivative of `log ‚àò f` is the logarithmic derivative provided `f` is differentiable and
we are on the slitPlane. -/
lemma Complex.deriv_log_comp_eq_logDeriv {f : ‚ÑÇ ‚Üí ‚ÑÇ} {x : ‚ÑÇ} (h‚ÇÅ : DifferentiableAt ‚ÑÇ f x)
    (h‚ÇÇ : f x ‚àà Complex.slitPlane) : deriv (Complex.log ‚àò f) x = logDeriv f x := by
  have A := (HasDerivAt.clog h‚ÇÅ.hasDerivAt h‚ÇÇ).deriv
  rw [‚Üê h‚ÇÅ.hasDerivAt.deriv] at A
  simp only [logDeriv, Pi.div_apply, ‚Üê A, Function.comp_def]

/-- The derivative of `log ‚àò f` is the logarithmic derivative provided `f` is differentiable and
`f x  ‚â† 0`. -/
lemma Real.deriv_log_comp_eq_logDeriv {f : ‚Ñù ‚Üí ‚Ñù} {x : ‚Ñù} (h‚ÇÅ : DifferentiableAt ‚Ñù f x)
    (h‚ÇÇ : f x ‚â† 0) : deriv (Real.log ‚àò f) x = logDeriv f x := by
  simp only [ne_eq, logDeriv, Pi.div_apply, ‚Üê deriv.log h‚ÇÅ h‚ÇÇ]
  rfl

end log
