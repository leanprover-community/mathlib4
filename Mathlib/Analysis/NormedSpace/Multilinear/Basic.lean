/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Sophie Morel, Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace
import Mathlib.Logic.Embedding.Basic
import Mathlib.Data.Fintype.CardEmbedding
import Mathlib.Topology.Algebra.Module.Multilinear.Topology

#align_import analysis.normed_space.multilinear from "leanprover-community/mathlib"@"f40476639bac089693a489c9e354ebd75dc0f886"

/-!
# Operator norm on the space of continuous multilinear maps

When `f` is a continuous multilinear map in finitely many variables, we define its norm `â€–fâ€–` as the
smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`.

We show that it is indeed a norm, and prove its basic properties.

## Main results

Let `f` be a multilinear map in finitely many variables.
* `exists_bound_of_continuous` asserts that, if `f` is continuous, then there exists `C > 0`
  with `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` for all `m`.
* `continuous_of_bound`, conversely, asserts that this bound implies continuity.
* `mkContinuous` constructs the associated continuous multilinear map.

Let `f` be a continuous multilinear map in finitely many variables.
* `â€–fâ€–` is its norm, i.e., the smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for
  all `m`.
* `le_opNorm f m` asserts the fundamental inequality `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–`.
* `norm_image_sub_le f mâ‚ mâ‚‚` gives a control of the difference `f mâ‚ - f mâ‚‚` in terms of
  `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`.

## Implementation notes

We mostly follow the API (and the proofs) of `OperatorNorm.lean`, with the additional complexity
that we should deal with multilinear maps in several variables. The currying/uncurrying
constructions are based on those in `Multilinear.lean`.

From the mathematical point of view, all the results follow from the results on operator norm in
one variable, by applying them to one variable after the other through currying. However, this
is only well defined when there is an order on the variables (for instance on `Fin n`) although
the final result is independent of the order. While everything could be done following this
approach, it turns out that direct proofs are easier and more efficient.
-/

suppress_compilation

noncomputable section

open scoped NNReal Topology Uniformity
open Finset Metric Function Filter

/-
Porting note: These lines are not required in Mathlib4.
```lean
attribute [local instance 1001]
  AddCommGroup.toAddCommMonoid NormedAddCommGroup.toAddCommGroup NormedSpace.toModule'
```
-/

/-!
### Type variables

We use the following type variables in this file:

* `ğ•œ` : a `NontriviallyNormedField`;
* `Î¹`, `Î¹'` : finite index types with decidable equality;
* `E`, `Eâ‚` : families of normed vector spaces over `ğ•œ` indexed by `i : Î¹`;
* `E'` : a family of normed vector spaces over `ğ•œ` indexed by `i' : Î¹'`;
* `Ei` : a family of normed vector spaces over `ğ•œ` indexed by `i : Fin (Nat.succ n)`;
* `G`, `G'` : normed vector spaces over `ğ•œ`.
-/

universe u v v' wE wEâ‚ wE' wG wG'

section Seminorm

variable {ğ•œ : Type u} {Î¹ : Type v} {Î¹' : Type v'} {E : Î¹ â†’ Type wE} {Eâ‚ : Î¹ â†’ Type wEâ‚}
  {E' : Î¹' â†’ Type wE'} {G : Type wG} {G' : Type wG'} [Fintype Î¹]
  [Fintype Î¹'] [NontriviallyNormedField ğ•œ] [âˆ€ i, SeminormedAddCommGroup (E i)]
  [âˆ€ i, NormedSpace ğ•œ (E i)] [âˆ€ i, SeminormedAddCommGroup (Eâ‚ i)] [âˆ€ i, NormedSpace ğ•œ (Eâ‚ i)]
  [âˆ€ i, SeminormedAddCommGroup (E' i)] [âˆ€ i, NormedSpace ğ•œ (E' i)]
  [SeminormedAddCommGroup G] [NormedSpace ğ•œ G] [SeminormedAddCommGroup G'] [NormedSpace ğ•œ G']

/-!
### Continuity properties of multilinear maps

We relate continuity of multilinear maps to the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, in
both directions. Along the way, we prove useful bounds on the difference `â€–f mâ‚ - f mâ‚‚â€–`.
-/

namespace MultilinearMap

variable (f : MultilinearMap ğ•œ E G)

/-- If `f` is a continuous multilinear map in finitely many variables on `E` and `m` is an element
of `âˆ€ i, E i` such that one of the `m i` has norm `0`, then `f m` has norm `0`.

Note that we cannot drop the continuity assumption because `f (m : Unit â†’ E) = f (m ())`,
where the domain has zero norm and the codomain has a nonzero norm
does not satisfy this condition. -/
lemma norm_map_coord_zero (hf : Continuous f) {m : âˆ€ i, E i} {i : Î¹} (hi : â€–m iâ€– = 0) :
    â€–f mâ€– = 0 := by
  classical
  rw [â† inseparable_zero_iff_norm] at hi âŠ¢
  have : Inseparable (update m i 0) m := inseparable_pi.2 <|
    (forall_update_iff m fun i a â†¦ Inseparable a (m i)).2 âŸ¨hi.symm, fun _ _ â†¦ rflâŸ©
  simpa only [map_update_zero] using this.symm.map hf

theorem bound_of_shell_of_norm_map_coord_zero (hfâ‚€ : âˆ€ {m i}, â€–m iâ€– = 0 â†’ â€–f mâ€– = 0)
    {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < â€–c iâ€–)
    (hf : âˆ€ m : âˆ€ i, E i, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : âˆ€ i, E i) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– := by
  rcases em (âˆƒ i, â€–m iâ€– = 0) with (âŸ¨i, hiâŸ© | hm)
  Â· rw [hfâ‚€ hi, prod_eq_zero (mem_univ i) hi, mul_zero]
  push_neg at hm
  choose Î´ hÎ´0 hÎ´m_lt hle_Î´m _ using fun i => rescale_to_shell_semi_normed (hc i) (hÎµ i) (hm i)
  have hÎ´0 : 0 < âˆ i, â€–Î´ iâ€– := prod_pos fun i _ => norm_pos_iff.2 (hÎ´0 i)
  simpa [map_smul_univ, norm_smul, prod_mul_distrib, mul_left_comm C, mul_le_mul_left hÎ´0] using
    hf (fun i => Î´ i â€¢ m i) hle_Î´m hÎ´m_lt

/-- If a continuous multilinear map in finitely many variables on normed spaces satisfies
the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ i / â€–c iâ€– < â€–m iâ€– < Îµ i` for some positive
numbers `Îµ i` and elements `c i : ğ•œ`, `1 < â€–c iâ€–`, then it satisfies this inequality for all `m`. -/
theorem bound_of_shell_of_continuous (hfc : Continuous f)
    {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < â€–c iâ€–)
    (hf : âˆ€ m : âˆ€ i, E i, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : âˆ€ i, E i) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  bound_of_shell_of_norm_map_coord_zero f (norm_map_coord_zero f hfc) hÎµ hc hf m

/-- If a multilinear map in finitely many variables on normed spaces is continuous, then it
satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, for some `C` which can be chosen to be
positive. -/
theorem exists_bound_of_continuous (hf : Continuous f) :
    âˆƒ C : â„, 0 < C âˆ§ âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– := by
  cases isEmpty_or_nonempty Î¹
  Â· refine âŸ¨â€–f 0â€– + 1, add_pos_of_nonneg_of_pos (norm_nonneg _) zero_lt_one, fun m => ?_âŸ©
    obtain rfl : m = 0 := funext (IsEmpty.elim â€¹_â€º)
    simp [univ_eq_empty, zero_le_one]
  obtain âŸ¨Îµ : â„, Îµ0 : 0 < Îµ, hÎµ : âˆ€ m : âˆ€ i, E i, â€–m - 0â€– < Îµ â†’ â€–f m - f 0â€– < 1âŸ© :=
    NormedAddCommGroup.tendsto_nhds_nhds.1 (hf.tendsto 0) 1 zero_lt_one
  simp only [sub_zero, f.map_zero] at hÎµ
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  have : 0 < (â€–câ€– / Îµ) ^ Fintype.card Î¹ := pow_pos (div_pos (zero_lt_one.trans hc) Îµ0) _
  refine âŸ¨_, this, ?_âŸ©
  refine f.bound_of_shell_of_continuous hf (fun _ => Îµ0) (fun _ => hc) fun m hcm hm => ?_
  refine (hÎµ m ((pi_norm_lt_iff Îµ0).2 hm)).le.trans ?_
  rw [â† div_le_iff' this, one_div, â† inv_pow, inv_div, Fintype.card, â† prod_const]
  exact prod_le_prod (fun _ _ => div_nonneg Îµ0.le (norm_nonneg _)) fun i _ => hcm i
#align multilinear_map.exists_bound_of_continuous MultilinearMap.exists_bound_of_continuous

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a precise but hard to use version. See
`norm_image_sub_le_of_bound` for a less precise but more usable version. The bound reads
`â€–f m - f m'â€– â‰¤
  C * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
theorem norm_image_sub_le_of_bound' [DecidableEq Î¹] {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : âˆ€ i, E i) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– := by
  have A :
    âˆ€ s : Finset Î¹,
      â€–f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)â€– â‰¤
        C * âˆ‘ i âˆˆ s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– := by
    intro s
    induction' s using Finset.induction with i s his Hrec
    Â· simp
    have I :
      â€–f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)â€– â‰¤
        C * âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– := by
      have A : (insert i s).piecewise mâ‚‚ mâ‚ = Function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚‚ i) :=
        s.piecewise_insert _ _ _
      have B : s.piecewise mâ‚‚ mâ‚ = Function.update (s.piecewise mâ‚‚ mâ‚) i (mâ‚ i) := by
        simp [eq_update_iff, his]
      rw [B, A, â† f.map_sub]
      apply le_trans (H _)
      gcongr with j
      Â· exact fun j _ => norm_nonneg _
      by_cases h : j = i
      Â· rw [h]
        simp
      Â· by_cases h' : j âˆˆ s <;> simp [h', h, le_refl]
    calc
      â€–f mâ‚ - f ((insert i s).piecewise mâ‚‚ mâ‚)â€– â‰¤
          â€–f mâ‚ - f (s.piecewise mâ‚‚ mâ‚)â€– +
            â€–f (s.piecewise mâ‚‚ mâ‚) - f ((insert i s).piecewise mâ‚‚ mâ‚)â€– := by
        rw [â† dist_eq_norm, â† dist_eq_norm, â† dist_eq_norm]
        exact dist_triangle _ _ _
      _ â‰¤ (C * âˆ‘ i âˆˆ s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–) +
            C * âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
        (add_le_add Hrec I)
      _ = C * âˆ‘ i âˆˆ insert i s, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– := by
        simp [his, add_comm, left_distrib]
  convert A univ
  simp
#align multilinear_map.norm_image_sub_le_of_bound' MultilinearMap.norm_image_sub_le_of_bound'

/-- If `f` satisfies a boundedness property around `0`, one can deduce a bound on `f mâ‚ - f mâ‚‚`
using the multilinearity. Here, we give a usable but not very precise version. See
`norm_image_sub_le_of_bound'` for a more precise but less usable version. The bound is
`â€–f m - f m'â€– â‰¤ C * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
theorem norm_image_sub_le_of_bound {C : â„} (hC : 0 â‰¤ C) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (mâ‚ mâ‚‚ : âˆ€ i, E i) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * Fintype.card Î¹ * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– := by
  classical
  have A :
    âˆ€ i : Î¹,
      âˆ j, (if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–) â‰¤
        â€–mâ‚ - mâ‚‚â€– * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) := by
    intro i
    calc
      âˆ j, (if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€–) â‰¤
          âˆ j : Î¹, Function.update (fun _ => max â€–mâ‚â€– â€–mâ‚‚â€–) i â€–mâ‚ - mâ‚‚â€– j := by
        apply Finset.prod_le_prod
        Â· intro j _
          by_cases h : j = i <;> simp [h, norm_nonneg]
        Â· intro j _
          by_cases h : j = i
          Â· rw [h]
            simp only [ite_true, Function.update_same]
            exact norm_le_pi_norm (mâ‚ - mâ‚‚) i
          Â· simp [h, -le_max_iff, -max_le_iff, max_le_max, norm_le_pi_norm (_ : âˆ€ i, E i)]
      _ = â€–mâ‚ - mâ‚‚â€– * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) := by
        rw [prod_update_of_mem (Finset.mem_univ _)]
        simp [card_univ_diff]
  calc
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
      f.norm_image_sub_le_of_bound' hC H mâ‚ mâ‚‚
    _ â‰¤ C * âˆ‘ _i, â€–mâ‚ - mâ‚‚â€– * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) := by gcongr; apply A
    _ = C * Fintype.card Î¹ * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– := by
      rw [sum_const, card_univ, nsmul_eq_mul]
      ring
#align multilinear_map.norm_image_sub_le_of_bound MultilinearMap.norm_image_sub_le_of_bound

/-- If a multilinear map satisfies an inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, then it is
continuous. -/
theorem continuous_of_bound (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : Continuous f := by
  let D := max C 1
  have D_pos : 0 â‰¤ D := le_trans zero_le_one (le_max_right _ _)
  replace H (m) : â€–f mâ€– â‰¤ D * âˆ i, â€–m iâ€– :=
    (H m).trans (mul_le_mul_of_nonneg_right (le_max_left _ _) <| by positivity)
  refine continuous_iff_continuousAt.2 fun m => ?_
  refine
    continuousAt_of_locally_lipschitz zero_lt_one
      (D * Fintype.card Î¹ * (â€–mâ€– + 1) ^ (Fintype.card Î¹ - 1)) fun m' h' => ?_
  rw [dist_eq_norm, dist_eq_norm]
  have : max â€–m'â€– â€–mâ€– â‰¤ â€–mâ€– + 1 := by
    simp [zero_le_one, norm_le_of_mem_closedBall (le_of_lt h')]
  calc
    â€–f m' - f mâ€– â‰¤ D * Fintype.card Î¹ * max â€–m'â€– â€–mâ€– ^ (Fintype.card Î¹ - 1) * â€–m' - mâ€– :=
      f.norm_image_sub_le_of_bound D_pos H m' m
    _ â‰¤ D * Fintype.card Î¹ * (â€–mâ€– + 1) ^ (Fintype.card Î¹ - 1) * â€–m' - mâ€– := by gcongr
#align multilinear_map.continuous_of_bound MultilinearMap.continuous_of_bound

/-- Constructing a continuous multilinear map from a multilinear map satisfying a boundedness
condition. -/
def mkContinuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : ContinuousMultilinearMap ğ•œ E G :=
  { f with cont := f.continuous_of_bound C H }
#align multilinear_map.mk_continuous MultilinearMap.mkContinuous

@[simp]
theorem coe_mkContinuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â‡‘(f.mkContinuous C H) = f :=
  rfl
#align multilinear_map.coe_mk_continuous MultilinearMap.coe_mkContinuous

/-- Given a multilinear map in `n` variables, if one restricts it to `k` variables putting `z` on
the other coordinates, then the resulting restricted function satisfies an inequality
`â€–f.restr vâ€– â‰¤ C * â€–zâ€–^(n-k) * Î  â€–v iâ€–` if the original function satisfies `â€–f vâ€– â‰¤ C * Î  â€–v iâ€–`. -/
theorem restr_norm_le {k n : â„•} (f : (MultilinearMap ğ•œ (fun _ : Fin n => G) G' : _))
    (s : Finset (Fin n)) (hk : s.card = k) (z : G) {C : â„} (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (v : Fin k â†’ G) : â€–f.restr s hk z vâ€– â‰¤ C * â€–zâ€– ^ (n - k) * âˆ i, â€–v iâ€– := by
  rw [mul_right_comm, mul_assoc]
  convert H _ using 2
  simp only [apply_dite norm, Fintype.prod_dite, prod_const â€–zâ€–, Finset.card_univ,
    Fintype.card_of_subtype sá¶œ fun _ => mem_compl, card_compl, Fintype.card_fin, hk, mk_coe, â†
    (s.orderIsoOfFin hk).symm.bijective.prod_comp fun x => â€–v xâ€–]
  convert rfl
#align multilinear_map.restr_norm_le MultilinearMap.restr_norm_le

end MultilinearMap

/-!
### Continuous multilinear maps

We define the norm `â€–fâ€–` of a continuous multilinear map `f` in finitely many variables as the
smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`. We show that this
defines a normed space structure on `ContinuousMultilinearMap ğ•œ E G`.
-/


namespace ContinuousMultilinearMap

variable (c : ğ•œ) (f g : ContinuousMultilinearMap ğ•œ E G) (m : âˆ€ i, E i)

theorem bound : âˆƒ C : â„, 0 < C âˆ§ âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.toMultilinearMap.exists_bound_of_continuous f.2
#align continuous_multilinear_map.bound ContinuousMultilinearMap.bound

open Real

/-- The operator norm of a continuous multilinear map is the inf of all its bounds. -/
def opNorm :=
  sInf { c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€– }
#align continuous_multilinear_map.op_norm ContinuousMultilinearMap.opNorm

instance hasOpNorm : Norm (ContinuousMultilinearMap ğ•œ E G) :=
  âŸ¨opNormâŸ©
#align continuous_multilinear_map.has_op_norm ContinuousMultilinearMap.hasOpNorm

/-- An alias of `ContinuousMultilinearMap.hasOpNorm` with non-dependent types to help typeclass
search. -/
instance hasOpNorm' : Norm (ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => G) G') :=
  ContinuousMultilinearMap.hasOpNorm
#align continuous_multilinear_map.has_op_norm' ContinuousMultilinearMap.hasOpNorm'

theorem norm_def : â€–fâ€– = sInf { c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€– } :=
  rfl
#align continuous_multilinear_map.norm_def ContinuousMultilinearMap.norm_def

-- So that invocations of `le_csInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
theorem bounds_nonempty {f : ContinuousMultilinearMap ğ•œ E G} :
    âˆƒ c, c âˆˆ { c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€– } :=
  let âŸ¨M, hMp, hMbâŸ© := f.bound
  âŸ¨M, le_of_lt hMp, hMbâŸ©
#align continuous_multilinear_map.bounds_nonempty ContinuousMultilinearMap.bounds_nonempty

theorem bounds_bddBelow {f : ContinuousMultilinearMap ğ•œ E G} :
    BddBelow { c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€– } :=
  âŸ¨0, fun _ âŸ¨hn, _âŸ© => hnâŸ©
#align continuous_multilinear_map.bounds_bdd_below ContinuousMultilinearMap.bounds_bddBelow

theorem isLeast_opNorm : IsLeast {c : â„ | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} â€–fâ€– := by
  refine IsClosed.isLeast_csInf ?_ bounds_nonempty bounds_bddBelow
  simp only [Set.setOf_and, Set.setOf_forall]
  exact isClosed_Ici.inter (isClosed_iInter fun m â†¦
    isClosed_le continuous_const (continuous_id.mul continuous_const))

@[deprecated (since := "2024-02-02")] alias isLeast_op_norm := isLeast_opNorm

theorem opNorm_nonneg : 0 â‰¤ â€–fâ€– :=
  Real.sInf_nonneg _ fun _ âŸ¨hx, _âŸ© => hx
#align continuous_multilinear_map.op_norm_nonneg ContinuousMultilinearMap.opNorm_nonneg

@[deprecated (since := "2024-02-02")] alias op_norm_nonneg := opNorm_nonneg

/-- The fundamental property of the operator norm of a continuous multilinear map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`. -/
theorem le_opNorm : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– := f.isLeast_opNorm.1.2 m
#align continuous_multilinear_map.le_op_norm ContinuousMultilinearMap.le_opNorm

@[deprecated (since := "2024-02-02")] alias le_op_norm := le_opNorm

variable {f m}

theorem le_mul_prod_of_le_opNorm_of_le {C : â„} {b : Î¹ â†’ â„} (hC : â€–fâ€– â‰¤ C) (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) :
    â€–f mâ€– â‰¤ C * âˆ i, b i :=
  (f.le_opNorm m).trans <| mul_le_mul hC (prod_le_prod (fun _ _ â†¦ norm_nonneg _) fun _ _ â†¦ hm _)
    (by positivity) ((opNorm_nonneg _).trans hC)

@[deprecated (since := "2024-02-02")]
alias le_mul_prod_of_le_op_norm_of_le := le_mul_prod_of_le_opNorm_of_le

variable (f)

theorem le_opNorm_mul_prod_of_le {b : Î¹ â†’ â„} (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, b i :=
  le_mul_prod_of_le_opNorm_of_le le_rfl hm
#align continuous_multilinear_map.le_op_norm_mul_prod_of_le ContinuousMultilinearMap.le_opNorm_mul_prod_of_le

@[deprecated (since := "2024-02-02")] alias le_op_norm_mul_prod_of_le := le_opNorm_mul_prod_of_le

theorem le_opNorm_mul_pow_card_of_le {b : â„} (hm : â€–mâ€– â‰¤ b) :
    â€–f mâ€– â‰¤ â€–fâ€– * b ^ Fintype.card Î¹ := by
  simpa only [prod_const] using f.le_opNorm_mul_prod_of_le fun i => (norm_le_pi_norm m i).trans hm
#align continuous_multilinear_map.le_op_norm_mul_pow_card_of_le ContinuousMultilinearMap.le_opNorm_mul_pow_card_of_le

@[deprecated (since := "2024-02-02")]
alias le_op_norm_mul_pow_card_of_le := le_opNorm_mul_pow_card_of_le

theorem le_opNorm_mul_pow_of_le {n : â„•} {Ei : Fin n â†’ Type*} [âˆ€ i, SeminormedAddCommGroup (Ei i)]
    [âˆ€ i, NormedSpace ğ•œ (Ei i)] (f : ContinuousMultilinearMap ğ•œ Ei G) {m : âˆ€ i, Ei i} {b : â„}
    (hm : â€–mâ€– â‰¤ b) : â€–f mâ€– â‰¤ â€–fâ€– * b ^ n := by
  simpa only [Fintype.card_fin] using f.le_opNorm_mul_pow_card_of_le hm
#align continuous_multilinear_map.le_op_norm_mul_pow_of_le ContinuousMultilinearMap.le_opNorm_mul_pow_of_le

@[deprecated (since := "2024-02-02")] alias le_op_norm_mul_pow_of_le := le_opNorm_mul_pow_of_le

variable {f} (m)

theorem le_of_opNorm_le {C : â„} (h : â€–fâ€– â‰¤ C) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  le_mul_prod_of_le_opNorm_of_le h fun _ â†¦ le_rfl
#align continuous_multilinear_map.le_of_op_norm_le ContinuousMultilinearMap.le_of_opNorm_le

@[deprecated (since := "2024-02-02")] alias le_of_op_norm_le := le_of_opNorm_le

variable (f)

theorem ratio_le_opNorm : (â€–f mâ€– / âˆ i, â€–m iâ€–) â‰¤ â€–fâ€– :=
  div_le_of_nonneg_of_le_mul (by positivity) (opNorm_nonneg _) (f.le_opNorm m)
#align continuous_multilinear_map.ratio_le_op_norm ContinuousMultilinearMap.ratio_le_opNorm

@[deprecated (since := "2024-02-02")] alias ratio_le_op_norm := ratio_le_opNorm

/-- The image of the unit ball under a continuous multilinear map is bounded. -/
theorem unit_le_opNorm (h : â€–mâ€– â‰¤ 1) : â€–f mâ€– â‰¤ â€–fâ€– :=
  (le_opNorm_mul_pow_card_of_le f h).trans <| by simp
#align continuous_multilinear_map.unit_le_op_norm ContinuousMultilinearMap.unit_le_opNorm

@[deprecated (since := "2024-02-02")] alias unit_le_op_norm := unit_le_opNorm

/-- If one controls the norm of every `f x`, then one controls the norm of `f`. -/
theorem opNorm_le_bound {M : â„} (hMp : 0 â‰¤ M) (hM : âˆ€ m, â€–f mâ€– â‰¤ M * âˆ i, â€–m iâ€–) : â€–fâ€– â‰¤ M :=
  csInf_le bounds_bddBelow âŸ¨hMp, hMâŸ©
#align continuous_multilinear_map.op_norm_le_bound ContinuousMultilinearMap.opNorm_le_bound

@[deprecated (since := "2024-02-02")] alias op_norm_le_bound := opNorm_le_bound

theorem opNorm_le_iff {C : â„} (hC : 0 â‰¤ C) : â€–fâ€– â‰¤ C â†” âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  âŸ¨fun h _ â†¦ le_of_opNorm_le _ h, opNorm_le_bound _ hCâŸ©

@[deprecated (since := "2024-02-02")] alias op_norm_le_iff := opNorm_le_iff

/-- The operator norm satisfies the triangle inequality. -/
theorem opNorm_add_le : â€–f + gâ€– â‰¤ â€–fâ€– + â€–gâ€– :=
  opNorm_le_bound _ (add_nonneg (opNorm_nonneg _) (opNorm_nonneg _)) fun x => by
    rw [add_mul]
    exact norm_add_le_of_le (le_opNorm _ _) (le_opNorm _ _)
#align continuous_multilinear_map.op_norm_add_le ContinuousMultilinearMap.opNorm_add_le

@[deprecated (since := "2024-02-02")] alias op_norm_add_le := opNorm_add_le

theorem opNorm_zero : â€–(0 : ContinuousMultilinearMap ğ•œ E G)â€– = 0 :=
  (opNorm_nonneg _).antisymm' <| opNorm_le_bound 0 le_rfl fun m => by simp
#align continuous_multilinear_map.op_norm_zero ContinuousMultilinearMap.opNorm_zero

@[deprecated (since := "2024-02-02")] alias op_norm_zero := opNorm_zero

section

variable {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' G] [SMulCommClass ğ•œ ğ•œ' G]

theorem opNorm_smul_le (c : ğ•œ') : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– :=
  (c â€¢ f).opNorm_le_bound (mul_nonneg (norm_nonneg _) (opNorm_nonneg _)) fun m â†¦ by
    rw [smul_apply, norm_smul, mul_assoc]
    exact mul_le_mul_of_nonneg_left (le_opNorm _ _) (norm_nonneg _)
#align continuous_multilinear_map.op_norm_smul_le ContinuousMultilinearMap.opNorm_smul_le

@[deprecated (since := "2024-02-02")] alias op_norm_smul_le := opNorm_smul_le

theorem opNorm_neg : â€–-fâ€– = â€–fâ€– := by
  rw [norm_def]
  apply congr_arg
  ext
  simp
#align continuous_multilinear_map.op_norm_neg ContinuousMultilinearMap.opNorm_neg

@[deprecated (since := "2024-02-02")] alias op_norm_neg := opNorm_neg

variable (ğ•œ E G) in
/-- Operator seminorm on the space of continuous multilinear maps, as `Seminorm`.

We use this seminorm
to define a `SeminormedAddCommGroup` structure on `ContinuousMultilinearMap ğ•œ E G`,
but we have to override the projection `UniformSpace`
so that it is definitionally equal to the one coming from the topologies on `E` and `G`. -/
protected def seminorm : Seminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G) :=
  .ofSMulLE norm opNorm_zero opNorm_add_le fun c f â†¦ opNorm_smul_le f c

private lemma uniformity_eq_seminorm :
    ğ“¤ (ContinuousMultilinearMap ğ•œ E G) = â¨… r > 0, ğ“Ÿ {f | â€–f.1 - f.2â€– < r} := by
  refine (ContinuousMultilinearMap.seminorm ğ•œ E G).uniformity_eq_of_hasBasis
    (ContinuousMultilinearMap.hasBasis_nhds_zero_of_basis Metric.nhds_basis_closedBall)
    ?_ fun (s, r) âŸ¨hs, hrâŸ© â†¦ ?_
  Â· rcases NormedField.exists_lt_norm ğ•œ 1 with âŸ¨c, hcâŸ©
    have hcâ‚€ : 0 < â€–câ€– := one_pos.trans hc
    simp only [hasBasis_nhds_zero.mem_iff, Prod.exists]
    use 1, closedBall 0 â€–câ€–, closedBall 0 1
    suffices âˆ€ f : ContinuousMultilinearMap ğ•œ E G, (âˆ€ x, â€–xâ€– â‰¤ â€–câ€– â†’ â€–f xâ€– â‰¤ 1) â†’ â€–fâ€– â‰¤ 1 by
      simpa [NormedSpace.isVonNBounded_closedBall, closedBall_mem_nhds, Set.subset_def, Set.MapsTo]
    intro f hf
    refine opNorm_le_bound _ (by positivity) <|
      f.1.bound_of_shell_of_continuous f.2 (fun _ â†¦ hcâ‚€) (fun _ â†¦ hc) fun x hcx hx â†¦ ?_
    calc
      â€–f xâ€– â‰¤ 1 := hf _ <| (pi_norm_le_iff_of_nonneg (norm_nonneg c)).2 fun i â†¦ (hx i).le
      _ = âˆ i : Î¹, 1 := by simp
      _ â‰¤ âˆ i, â€–x iâ€– := Finset.prod_le_prod (fun _ _ â†¦ zero_le_one) fun i _ â†¦ by
        simpa only [div_self hcâ‚€.ne'] using hcx i
      _ = 1 * âˆ i, â€–x iâ€– := (one_mul _).symm
  Â· rcases (NormedSpace.isVonNBounded_iff' _).1 hs with âŸ¨Îµ, hÎµâŸ©
    rcases exists_pos_mul_lt hr (Îµ ^ Fintype.card Î¹) with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    refine âŸ¨Î´, hÎ´â‚€, fun f hf x hx â†¦ ?_âŸ©
    simp only [Seminorm.mem_ball_zero, mem_closedBall_zero_iff] at hf âŠ¢
    replace hf : â€–fâ€– â‰¤ Î´ := hf.le
    replace hx : â€–xâ€– â‰¤ Îµ := hÎµ x hx
    calc
      â€–f xâ€– â‰¤ â€–fâ€– * Îµ ^ Fintype.card Î¹ := le_opNorm_mul_pow_card_of_le f hx
      _ â‰¤ Î´ * Îµ ^ Fintype.card Î¹ := by have := (norm_nonneg x).trans hx; gcongr
      _ â‰¤ r := (mul_comm _ _).trans_le hÎ´.le

instance instPseudoMetricSpace : PseudoMetricSpace (ContinuousMultilinearMap ğ•œ E G) :=
  .replaceUniformity
    (ContinuousMultilinearMap.seminorm ğ•œ E G).toSeminormedAddCommGroup.toPseudoMetricSpace
    uniformity_eq_seminorm

/-- Continuous multilinear maps themselves form a seminormed space with respect to
    the operator norm. -/
instance seminormedAddCommGroup :
    SeminormedAddCommGroup (ContinuousMultilinearMap ğ•œ E G) := âŸ¨fun _ _ â†¦ rflâŸ©

/-- An alias of `ContinuousMultilinearMap.seminormedAddCommGroup` with non-dependent types to help
typeclass search. -/
instance seminormedAddCommGroup' :
    SeminormedAddCommGroup (ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => G) G') :=
  ContinuousMultilinearMap.seminormedAddCommGroup

instance normedSpace : NormedSpace ğ•œ' (ContinuousMultilinearMap ğ•œ E G) :=
  âŸ¨fun c f => f.opNorm_smul_le câŸ©
#align continuous_multilinear_map.normed_space ContinuousMultilinearMap.normedSpace

/-- An alias of `ContinuousMultilinearMap.normedSpace` with non-dependent types to help typeclass
search. -/
instance normedSpace' : NormedSpace ğ•œ' (ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => G') G) :=
  ContinuousMultilinearMap.normedSpace
#align continuous_multilinear_map.normed_space' ContinuousMultilinearMap.normedSpace'

/-- The fundamental property of the operator norm of a continuous multilinear map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`, `nnnorm` version. -/
theorem le_opNNNorm : â€–f mâ€–â‚Š â‰¤ â€–fâ€–â‚Š * âˆ i, â€–m iâ€–â‚Š :=
  NNReal.coe_le_coe.1 <| by
    push_cast
    exact f.le_opNorm m
#align continuous_multilinear_map.le_op_nnnorm ContinuousMultilinearMap.le_opNNNorm

@[deprecated (since := "2024-02-02")] alias le_op_nnnorm := le_opNNNorm

theorem le_of_opNNNorm_le {C : â„â‰¥0} (h : â€–fâ€–â‚Š â‰¤ C) : â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š :=
  (f.le_opNNNorm m).trans <| mul_le_mul' h le_rfl
#align continuous_multilinear_map.le_of_op_nnnorm_le ContinuousMultilinearMap.le_of_opNNNorm_le

@[deprecated (since := "2024-02-02")] alias le_of_op_nnnorm_le := le_of_opNNNorm_le

theorem opNNNorm_le_iff {C : â„â‰¥0} : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ m, â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š := by
  simp only [â† NNReal.coe_le_coe]; simp [opNorm_le_iff _ C.coe_nonneg, NNReal.coe_prod]

@[deprecated (since := "2024-02-02")] alias op_nnnorm_le_iff := opNNNorm_le_iff

theorem isLeast_opNNNorm : IsLeast {C : â„â‰¥0 | âˆ€ m, â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š} â€–fâ€–â‚Š := by
  simpa only [â† opNNNorm_le_iff] using isLeast_Ici

@[deprecated (since := "2024-02-02")] alias isLeast_op_nnnorm := isLeast_opNNNorm

theorem opNNNorm_prod (f : ContinuousMultilinearMap ğ•œ E G) (g : ContinuousMultilinearMap ğ•œ E G') :
    â€–f.prod gâ€–â‚Š = max â€–fâ€–â‚Š â€–gâ€–â‚Š :=
  eq_of_forall_ge_iff fun _ â†¦ by
    simp only [opNNNorm_le_iff, prod_apply, Prod.nnnorm_def', max_le_iff, forall_and]

@[deprecated (since := "2024-02-02")] alias op_nnnorm_prod := opNNNorm_prod

theorem opNorm_prod (f : ContinuousMultilinearMap ğ•œ E G) (g : ContinuousMultilinearMap ğ•œ E G') :
    â€–f.prod gâ€– = max â€–fâ€– â€–gâ€– :=
  congr_arg NNReal.toReal (opNNNorm_prod f g)
#align continuous_multilinear_map.op_norm_prod ContinuousMultilinearMap.opNorm_prod

@[deprecated (since := "2024-02-02")] alias op_norm_prod := opNorm_prod

theorem opNNNorm_pi
    [âˆ€ i', SeminormedAddCommGroup (E' i')] [âˆ€ i', NormedSpace ğ•œ (E' i')]
    (f : âˆ€ i', ContinuousMultilinearMap ğ•œ E (E' i')) : â€–pi fâ€–â‚Š = â€–fâ€–â‚Š :=
  eq_of_forall_ge_iff fun _ â†¦ by simpa [opNNNorm_le_iff, pi_nnnorm_le_iff] using forall_swap

theorem opNorm_pi {Î¹' : Type v'} [Fintype Î¹'] {E' : Î¹' â†’ Type wE'}
    [âˆ€ i', SeminormedAddCommGroup (E' i')] [âˆ€ i', NormedSpace ğ•œ (E' i')]
    (f : âˆ€ i', ContinuousMultilinearMap ğ•œ E (E' i')) :
    â€–pi fâ€– = â€–fâ€– :=
  congr_arg NNReal.toReal (opNNNorm_pi f)
#align continuous_multilinear_map.norm_pi ContinuousMultilinearMap.opNorm_pi

@[deprecated (since := "2024-02-02")] alias op_norm_pi := opNorm_pi

section

@[simp]
theorem norm_ofSubsingleton [Subsingleton Î¹] (i : Î¹) (f : G â†’L[ğ•œ] G') :
    â€–ofSubsingleton ğ•œ G G' i fâ€– = â€–fâ€– := by
  letI : Unique Î¹ := uniqueOfSubsingleton i
  simp [norm_def, ContinuousLinearMap.norm_def, (Equiv.funUnique _ _).symm.surjective.forall]

@[simp]
theorem nnnorm_ofSubsingleton [Subsingleton Î¹] (i : Î¹) (f : G â†’L[ğ•œ] G') :
    â€–ofSubsingleton ğ•œ G G' i fâ€–â‚Š = â€–fâ€–â‚Š :=
  NNReal.eq <| norm_ofSubsingleton i f

variable (ğ•œ G)

/-- Linear isometry between continuous linear maps from `G` to `G'`
and continuous `1`-multilinear maps from `G` to `G'`. -/
@[simps apply symm_apply]
def ofSubsingletonâ‚—áµ¢ [Subsingleton Î¹] (i : Î¹) :
    (G â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ G) G' :=
  { ofSubsingleton ğ•œ G G' i with
    map_add' := fun _ _ â†¦ rfl
    map_smul' := fun _ _ â†¦ rfl
    norm_map' := norm_ofSubsingleton i }

theorem norm_ofSubsingleton_id_le [Subsingleton Î¹] (i : Î¹) :
    â€–ofSubsingleton ğ•œ G G i (.id _ _)â€– â‰¤ 1 := by
  rw [norm_ofSubsingleton]
  apply ContinuousLinearMap.norm_id_le
#align continuous_multilinear_map.norm_of_subsingleton_le ContinuousMultilinearMap.norm_ofSubsingleton_id_le

theorem nnnorm_ofSubsingleton_id_le [Subsingleton Î¹] (i : Î¹) :
    â€–ofSubsingleton ğ•œ G G i (.id _ _)â€–â‚Š â‰¤ 1 :=
  norm_ofSubsingleton_id_le _ _ _
#align continuous_multilinear_map.nnnorm_of_subsingleton_le ContinuousMultilinearMap.nnnorm_ofSubsingleton_id_le

variable {G} (E)

@[simp]
theorem norm_constOfIsEmpty [IsEmpty Î¹] (x : G) : â€–constOfIsEmpty ğ•œ E xâ€– = â€–xâ€– := by
  apply le_antisymm
  Â· refine opNorm_le_bound _ (norm_nonneg _) fun x => ?_
    rw [Fintype.prod_empty, mul_one, constOfIsEmpty_apply]
  Â· simpa using (constOfIsEmpty ğ•œ E x).le_opNorm 0
#align continuous_multilinear_map.norm_const_of_is_empty ContinuousMultilinearMap.norm_constOfIsEmpty

@[simp]
theorem nnnorm_constOfIsEmpty [IsEmpty Î¹] (x : G) : â€–constOfIsEmpty ğ•œ E xâ€–â‚Š = â€–xâ€–â‚Š :=
  NNReal.eq <| norm_constOfIsEmpty _ _ _
#align continuous_multilinear_map.nnnorm_const_of_is_empty ContinuousMultilinearMap.nnnorm_constOfIsEmpty

end

section

variable (ğ•œ E E' G G')

/-- `ContinuousMultilinearMap.prod` as a `LinearIsometryEquiv`. -/
def prodL :
    ContinuousMultilinearMap ğ•œ E G Ã— ContinuousMultilinearMap ğ•œ E G' â‰ƒâ‚—áµ¢[ğ•œ]
      ContinuousMultilinearMap ğ•œ E (G Ã— G') where
  toFun f := f.1.prod f.2
  invFun f :=
    ((ContinuousLinearMap.fst ğ•œ G G').compContinuousMultilinearMap f,
      (ContinuousLinearMap.snd ğ•œ G G').compContinuousMultilinearMap f)
  map_add' f g := rfl
  map_smul' c f := rfl
  left_inv f := by ext <;> rfl
  right_inv f := by ext <;> rfl
  norm_map' f := opNorm_prod f.1 f.2
set_option linter.uppercaseLean3 false in
#align continuous_multilinear_map.prodL ContinuousMultilinearMap.prodL

/-- `ContinuousMultilinearMap.pi` as a `LinearIsometryEquiv`. -/
def piâ‚—áµ¢ {Î¹' : Type v'} [Fintype Î¹'] {E' : Î¹' â†’ Type wE'} [âˆ€ i', NormedAddCommGroup (E' i')]
    [âˆ€ i', NormedSpace ğ•œ (E' i')] :
    @LinearIsometryEquiv ğ•œ ğ•œ _ _ (RingHom.id ğ•œ) _ _ _ (âˆ€ i', ContinuousMultilinearMap ğ•œ E (E' i'))
      (ContinuousMultilinearMap ğ•œ E (âˆ€ i, E' i)) _ _ (@Pi.module Î¹' _ ğ•œ _ _ fun _ => inferInstance)
      _ where
  toLinearEquiv := piLinearEquiv
  norm_map' := opNorm_pi
#align continuous_multilinear_map.piâ‚—áµ¢ ContinuousMultilinearMap.piâ‚—áµ¢

end

end

section RestrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
variable [NormedSpace ğ•œ' G] [IsScalarTower ğ•œ' ğ•œ G]
variable [âˆ€ i, NormedSpace ğ•œ' (E i)] [âˆ€ i, IsScalarTower ğ•œ' ğ•œ (E i)]

@[simp]
theorem norm_restrictScalars : â€–f.restrictScalars ğ•œ'â€– = â€–fâ€– := rfl
#align continuous_multilinear_map.norm_restrict_scalars ContinuousMultilinearMap.norm_restrictScalars

variable (ğ•œ')

/-- `ContinuousMultilinearMap.restrictScalars` as a `LinearIsometry`. -/
def restrictScalarsâ‚—áµ¢ : ContinuousMultilinearMap ğ•œ E G â†’â‚—áµ¢[ğ•œ'] ContinuousMultilinearMap ğ•œ' E G where
  toFun := restrictScalars ğ•œ'
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' _ := rfl
#align continuous_multilinear_map.restrict_scalarsâ‚—áµ¢ ContinuousMultilinearMap.restrictScalarsâ‚—áµ¢

/-- `ContinuousMultilinearMap.restrictScalars` as a `ContinuousLinearMap`. -/
def restrictScalarsLinear : ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ'] ContinuousMultilinearMap ğ•œ' E G :=
  (restrictScalarsâ‚—áµ¢ ğ•œ').toContinuousLinearMap
#align continuous_multilinear_map.restrict_scalars_linear ContinuousMultilinearMap.restrictScalarsLinear

variable {ğ•œ'}

theorem continuous_restrictScalars :
    Continuous
      (restrictScalars ğ•œ' : ContinuousMultilinearMap ğ•œ E G â†’ ContinuousMultilinearMap ğ•œ' E G) :=
  (restrictScalarsLinear ğ•œ').continuous
#align continuous_multilinear_map.continuous_restrict_scalars ContinuousMultilinearMap.continuous_restrictScalars

end RestrictScalars

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, precise version.
For a less precise but more usable version, see `norm_image_sub_le`. The bound reads
`â€–f m - f m'â€– â‰¤
  â€–fâ€– * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
theorem norm_image_sub_le' [DecidableEq Î¹] (mâ‚ mâ‚‚ : âˆ€ i, E i) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound' (norm_nonneg _) f.le_opNorm _ _
#align continuous_multilinear_map.norm_image_sub_le' ContinuousMultilinearMap.norm_image_sub_le'

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, less precise
version. For a more precise but less usable version, see `norm_image_sub_le'`.
The bound is `â€–f m - f m'â€– â‰¤ â€–fâ€– * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
theorem norm_image_sub_le (mâ‚ mâ‚‚ : âˆ€ i, E i) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * Fintype.card Î¹ * max â€–mâ‚â€– â€–mâ‚‚â€– ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound (norm_nonneg _) f.le_opNorm _ _
#align continuous_multilinear_map.norm_image_sub_le ContinuousMultilinearMap.norm_image_sub_le

/-- Applying a multilinear map to a vector is continuous in both coordinates. -/
theorem continuous_eval : Continuous
    fun p : ContinuousMultilinearMap ğ•œ E G Ã— âˆ€ i, E i => p.1 p.2 := by
  apply continuous_iff_continuousAt.2 fun p => ?_
  apply
    continuousAt_of_locally_lipschitz zero_lt_one
      ((â€–pâ€– + 1) * Fintype.card Î¹ * (â€–pâ€– + 1) ^ (Fintype.card Î¹ - 1) + âˆ i, â€–p.2 iâ€–) fun q hq => ?_
  have : 0 â‰¤ max â€–q.2â€– â€–p.2â€– := by simp
  have : 0 â‰¤ â€–pâ€– + 1 := zero_le_one.trans ((le_add_iff_nonneg_left 1).2 <| norm_nonneg p)
  have A : â€–qâ€– â‰¤ â€–pâ€– + 1 := norm_le_of_mem_closedBall hq.le
  have : max â€–q.2â€– â€–p.2â€– â‰¤ â€–pâ€– + 1 :=
    (max_le_max (norm_snd_le q) (norm_snd_le p)).trans (by simp [A, zero_le_one])
  have : âˆ€ i : Î¹, i âˆˆ univ â†’ 0 â‰¤ â€–p.2 iâ€– := fun i _ => norm_nonneg _
  calc
    dist (q.1 q.2) (p.1 p.2) â‰¤ dist (q.1 q.2) (q.1 p.2) + dist (q.1 p.2) (p.1 p.2) :=
      dist_triangle _ _ _
    _ = â€–q.1 q.2 - q.1 p.2â€– + â€–q.1 p.2 - p.1 p.2â€– := by rw [dist_eq_norm, dist_eq_norm]
    _ â‰¤ â€–q.1â€– * Fintype.card Î¹ * max â€–q.2â€– â€–p.2â€– ^ (Fintype.card Î¹ - 1) * â€–q.2 - p.2â€– +
        â€–q.1 - p.1â€– * âˆ i, â€–p.2 iâ€– :=
      (add_le_add (norm_image_sub_le _ _ _) ((q.1 - p.1).le_opNorm p.2))
    _ â‰¤ (â€–pâ€– + 1) * Fintype.card Î¹ * (â€–pâ€– + 1) ^ (Fintype.card Î¹ - 1) * â€–q - pâ€– +
        â€–q - pâ€– * âˆ i, â€–p.2 iâ€– := by
      apply_rules [add_le_add, mul_le_mul, le_refl, le_trans (norm_fst_le q) A, Nat.cast_nonneg,
        mul_nonneg, pow_le_pow_left, pow_nonneg, norm_snd_le (q - p), norm_nonneg,
        norm_fst_le (q - p), prod_nonneg]
    _ = ((â€–pâ€– + 1) * Fintype.card Î¹ * (â€–pâ€– + 1) ^ (Fintype.card Î¹ - 1) + âˆ i, â€–p.2 iâ€–)
          * dist q p := by
      rw [dist_eq_norm]
      ring
#align continuous_multilinear_map.continuous_eval ContinuousMultilinearMap.continuous_eval

end ContinuousMultilinearMap

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mkContinuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
theorem MultilinearMap.mkContinuous_norm_le (f : MultilinearMap ğ•œ E G) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ C :=
  ContinuousMultilinearMap.opNorm_le_bound _ hC fun m => H m
#align multilinear_map.mk_continuous_norm_le MultilinearMap.mkContinuous_norm_le

/-- If a continuous multilinear map is constructed from a multilinear map via the constructor
`mkContinuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
theorem MultilinearMap.mkContinuous_norm_le' (f : MultilinearMap ğ•œ E G) {C : â„}
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ max C 0 :=
  ContinuousMultilinearMap.opNorm_le_bound _ (le_max_right _ _) fun m â†¦ (H m).trans <|
    mul_le_mul_of_nonneg_right (le_max_left _ _) <| by positivity
#align multilinear_map.mk_continuous_norm_le' MultilinearMap.mkContinuous_norm_le'

namespace ContinuousMultilinearMap

/-- Given a continuous multilinear map `f` on `n` variables (parameterized by `Fin n`) and a subset
`s` of `k` of these variables, one gets a new continuous multilinear map on `Fin k` by varying
these variables, and fixing the other ones equal to a given value `z`. It is denoted by
`f.restr s hk z`, where `hk` is a proof that the cardinality of `s` is `k`. The implicit
identification between `Fin k` and `s` that we use is the canonical (increasing) bijection. -/
def restr {k n : â„•} (f : (G[Ã—n]â†’L[ğ•œ] G' : _)) (s : Finset (Fin n)) (hk : s.card = k) (z : G) :
    G[Ã—k]â†’L[ğ•œ] G' :=
  (f.toMultilinearMap.restr s hk z).mkContinuous (â€–fâ€– * â€–zâ€– ^ (n - k)) fun _ =>
    MultilinearMap.restr_norm_le _ _ _ _ f.le_opNorm _
#align continuous_multilinear_map.restr ContinuousMultilinearMap.restr

theorem norm_restr {k n : â„•} (f : G[Ã—n]â†’L[ğ•œ] G') (s : Finset (Fin n)) (hk : s.card = k) (z : G) :
    â€–f.restr s hk zâ€– â‰¤ â€–fâ€– * â€–zâ€– ^ (n - k) := by
  apply MultilinearMap.mkContinuous_norm_le
  exact mul_nonneg (norm_nonneg _) (pow_nonneg (norm_nonneg _) _)
#align continuous_multilinear_map.norm_restr ContinuousMultilinearMap.norm_restr

section

variable {A : Type*} [NormedCommRing A] [NormedAlgebra ğ•œ A]

@[simp]
theorem norm_mkPiAlgebra_le [Nonempty Î¹] : â€–ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ Aâ€– â‰¤ 1 := by
  refine opNorm_le_bound _ zero_le_one fun m => ?_
  simp only [ContinuousMultilinearMap.mkPiAlgebra_apply, one_mul]
  exact norm_prod_le' _ univ_nonempty _
#align continuous_multilinear_map.norm_mk_pi_algebra_le ContinuousMultilinearMap.norm_mkPiAlgebra_le

theorem norm_mkPiAlgebra_of_empty [IsEmpty Î¹] :
    â€–ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ Aâ€– = â€–(1 : A)â€– := by
  apply le_antisymm
  Â· apply opNorm_le_bound <;> simp
  Â· -- Porting note: have to annotate types to get mvars to unify
    convert ratio_le_opNorm (ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ A) fun _ => (1 : A)
    simp [eq_empty_of_isEmpty (univ : Finset Î¹)]
#align continuous_multilinear_map.norm_mk_pi_algebra_of_empty ContinuousMultilinearMap.norm_mkPiAlgebra_of_empty

@[simp]
theorem norm_mkPiAlgebra [NormOneClass A] : â€–ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ Aâ€– = 1 := by
  cases isEmpty_or_nonempty Î¹
  Â· simp [norm_mkPiAlgebra_of_empty]
  Â· refine le_antisymm norm_mkPiAlgebra_le ?_
    convert ratio_le_opNorm (ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ A) fun _ => 1
    simp
#align continuous_multilinear_map.norm_mk_pi_algebra ContinuousMultilinearMap.norm_mkPiAlgebra

end

section

variable {n : â„•} {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A]

theorem norm_mkPiAlgebraFin_succ_le : â€–ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ n.succ Aâ€– â‰¤ 1 := by
  refine opNorm_le_bound _ zero_le_one fun m => ?_
  simp only [ContinuousMultilinearMap.mkPiAlgebraFin_apply, one_mul, List.ofFn_eq_map,
    Fin.prod_univ_def, Multiset.map_coe, Multiset.prod_coe]
  refine (List.norm_prod_le' ?_).trans_eq ?_
  Â· rw [Ne, List.map_eq_nil, List.finRange_eq_nil]
    exact Nat.succ_ne_zero _
  rw [List.map_map, Function.comp_def]
#align continuous_multilinear_map.norm_mk_pi_algebra_fin_succ_le ContinuousMultilinearMap.norm_mkPiAlgebraFin_succ_le

theorem norm_mkPiAlgebraFin_le_of_pos (hn : 0 < n) :
    â€–ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ n Aâ€– â‰¤ 1 := by
  obtain âŸ¨n, rflâŸ© := Nat.exists_eq_succ_of_ne_zero hn.ne'
  exact norm_mkPiAlgebraFin_succ_le
#align continuous_multilinear_map.norm_mk_pi_algebra_fin_le_of_pos ContinuousMultilinearMap.norm_mkPiAlgebraFin_le_of_pos

theorem norm_mkPiAlgebraFin_zero : â€–ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ 0 Aâ€– = â€–(1 : A)â€– := by
  refine le_antisymm ?_ ?_
  Â· refine opNorm_le_bound _ (norm_nonneg (1 : A)) ?_
    simp
  Â· convert ratio_le_opNorm (ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ 0 A) fun _ => (1 : A)
    simp
#align continuous_multilinear_map.norm_mk_pi_algebra_fin_zero ContinuousMultilinearMap.norm_mkPiAlgebraFin_zero

@[simp]
theorem norm_mkPiAlgebraFin [NormOneClass A] :
    â€–ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ n Aâ€– = 1 := by
  cases n
  Â· rw [norm_mkPiAlgebraFin_zero]
    simp
  Â· refine le_antisymm norm_mkPiAlgebraFin_succ_le ?_
    refine le_of_eq_of_le ?_ <|
      ratio_le_opNorm (ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ (Nat.succ _) A) fun _ => 1
    simp
#align continuous_multilinear_map.norm_mk_pi_algebra_fin ContinuousMultilinearMap.norm_mkPiAlgebraFin

end

@[simp]
theorem nnnorm_smulRight (f : ContinuousMultilinearMap ğ•œ E ğ•œ) (z : G) :
    â€–f.smulRight zâ€–â‚Š = â€–fâ€–â‚Š * â€–zâ€–â‚Š := by
  refine le_antisymm ?_ ?_
  Â· refine (opNNNorm_le_iff _ |>.2 fun m => (nnnorm_smul_le _ _).trans ?_)
    rw [mul_right_comm]
    gcongr
    exact le_opNNNorm _ _
  Â· obtain hz | hz := eq_or_ne â€–zâ€–â‚Š 0
    Â· simp [hz]
    rw [â† NNReal.le_div_iff hz, opNNNorm_le_iff]
    intro m
    rw [div_mul_eq_mul_div, NNReal.le_div_iff hz]
    refine le_trans ?_ ((f.smulRight z).le_opNNNorm m)
    rw [smulRight_apply, nnnorm_smul]

@[simp]
theorem norm_smulRight (f : ContinuousMultilinearMap ğ•œ E ğ•œ) (z : G) :
    â€–f.smulRight zâ€– = â€–fâ€– * â€–zâ€– :=
  congr_arg NNReal.toReal (nnnorm_smulRight f z)

@[simp]
theorem norm_mkPiRing (z : G) : â€–ContinuousMultilinearMap.mkPiRing ğ•œ Î¹ zâ€– = â€–zâ€– := by
  rw [ContinuousMultilinearMap.mkPiRing, norm_smulRight, norm_mkPiAlgebra, one_mul]
#align continuous_multilinear_map.norm_mk_pi_field ContinuousMultilinearMap.norm_mkPiRing

variable (ğ•œ E G) in
/-- Continuous bilinear map realizing `(f, z) â†¦ f.smulRight z`. -/
def smulRightL : ContinuousMultilinearMap ğ•œ E ğ•œ â†’L[ğ•œ] G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
  LinearMap.mkContinuousâ‚‚
    { toFun := fun f â†¦
        { toFun := fun z â†¦ f.smulRight z
          map_add' := fun x y â†¦ by ext; simp
          map_smul' := fun c x â†¦ by ext; simp [smul_smul, mul_comm] }
      map_add' := fun f g â†¦ by ext; simp [add_smul]
      map_smul' := fun c f â†¦ by ext; simp [smul_smul] }
    1 (fun f z â†¦ by simp [norm_smulRight])

@[simp] lemma smulRightL_apply (f : ContinuousMultilinearMap ğ•œ E ğ•œ) (z : G) :
  smulRightL ğ•œ E G f z = f.smulRight z := rfl

#adaptation_note
/--
Before https://github.com/leanprover/lean4/pull/4119 we had to create a local instance:
```
letI : SeminormedAddCommGroup
  (ContinuousMultilinearMap ğ•œ E ğ•œ â†’L[ğ•œ] G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G) :=
  ContinuousLinearMap.toSeminormedAddCommGroup
    (F := G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G) (Ïƒâ‚â‚‚ := RingHom.id ğ•œ)
```
-/
set_option maxSynthPendingDepth 2 in
lemma norm_smulRightL_le : â€–smulRightL ğ•œ E Gâ€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _

variable (ğ•œ Î¹ G)

/-- Continuous multilinear maps on `ğ•œ^n` with values in `G` are in bijection with `G`, as such a
continuous multilinear map is completely determined by its value on the constant vector made of
ones. We register this bijection as a linear isometry in
`ContinuousMultilinearMap.piFieldEquiv`. -/
protected def piFieldEquiv : G â‰ƒâ‚—áµ¢[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => ğ•œ) G where
  toFun z := ContinuousMultilinearMap.mkPiRing ğ•œ Î¹ z
  invFun f := f fun i => 1
  map_add' z z' := by
    ext m
    simp [smul_add]
  map_smul' c z := by
    ext m
    simp [smul_smul, mul_comm]
  left_inv z := by simp
  right_inv f := f.mkPiRing_apply_one_eq_self
  norm_map' := norm_mkPiRing
#align continuous_multilinear_map.pi_field_equiv ContinuousMultilinearMap.piFieldEquiv

end ContinuousMultilinearMap

namespace ContinuousLinearMap

theorem norm_compContinuousMultilinearMap_le (g : G â†’L[ğ•œ] G') (f : ContinuousMultilinearMap ğ•œ E G) :
    â€–g.compContinuousMultilinearMap fâ€– â‰¤ â€–gâ€– * â€–fâ€– :=
  ContinuousMultilinearMap.opNorm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) fun m =>
    calc
      â€–g (f m)â€– â‰¤ â€–gâ€– * (â€–fâ€– * âˆ i, â€–m iâ€–) := g.le_opNorm_of_le <| f.le_opNorm _
      _ = _ := (mul_assoc _ _ _).symm
#align continuous_linear_map.norm_comp_continuous_multilinear_map_le ContinuousLinearMap.norm_compContinuousMultilinearMap_le

variable (ğ•œ E G G')

set_option linter.uppercaseLean3 false

/-- `ContinuousLinearMap.compContinuousMultilinearMap` as a bundled continuous bilinear map. -/
def compContinuousMultilinearMapL :
    (G â†’L[ğ•œ] G') â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G' :=
  LinearMap.mkContinuousâ‚‚
    (LinearMap.mkâ‚‚ ğ•œ compContinuousMultilinearMap (fun fâ‚ fâ‚‚ g => rfl) (fun c f g => rfl)
      (fun f gâ‚ gâ‚‚ => by ext1; apply f.map_add)
      (fun c f g => by ext1; simp))
    1
    fun f g => by rw [one_mul]; exact f.norm_compContinuousMultilinearMap_le g
#align continuous_linear_map.comp_continuous_multilinear_mapL ContinuousLinearMap.compContinuousMultilinearMapL

variable {ğ•œ G G'}

/-- `ContinuousLinearMap.compContinuousMultilinearMap` as a bundled
continuous linear equiv. -/
nonrec
def _root_.ContinuousLinearEquiv.compContinuousMultilinearMapL (g : G â‰ƒL[ğ•œ] G') :
    ContinuousMultilinearMap ğ•œ E G â‰ƒL[ğ•œ] ContinuousMultilinearMap ğ•œ E G' :=
  { compContinuousMultilinearMapL ğ•œ E G G' g.toContinuousLinearMap with
    invFun := compContinuousMultilinearMapL ğ•œ E G' G g.symm.toContinuousLinearMap
    left_inv := by
      intro f
      ext1 m
      simp [compContinuousMultilinearMapL]
    right_inv := by
      intro f
      ext1 m
      simp [compContinuousMultilinearMapL]
    continuous_toFun := (compContinuousMultilinearMapL ğ•œ E G G' g.toContinuousLinearMap).continuous
    continuous_invFun :=
      (compContinuousMultilinearMapL ğ•œ E G' G g.symm.toContinuousLinearMap).continuous }
#align continuous_linear_equiv.comp_continuous_multilinear_mapL ContinuousLinearEquiv.compContinuousMultilinearMapL

@[simp]
theorem _root_.ContinuousLinearEquiv.compContinuousMultilinearMapL_symm (g : G â‰ƒL[ğ•œ] G') :
    (g.compContinuousMultilinearMapL E).symm = g.symm.compContinuousMultilinearMapL E :=
  rfl
#align continuous_linear_equiv.comp_continuous_multilinear_mapL_symm ContinuousLinearEquiv.compContinuousMultilinearMapL_symm

variable {E}

@[simp]
theorem _root_.ContinuousLinearEquiv.compContinuousMultilinearMapL_apply (g : G â‰ƒL[ğ•œ] G')
    (f : ContinuousMultilinearMap ğ•œ E G) :
    g.compContinuousMultilinearMapL E f = (g : G â†’L[ğ•œ] G').compContinuousMultilinearMap f :=
  rfl
#align continuous_linear_equiv.comp_continuous_multilinear_mapL_apply ContinuousLinearEquiv.compContinuousMultilinearMapL_apply

/-- Flip arguments in `f : G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G'` to get
`ContinuousMultilinearMap ğ•œ E (G â†’L[ğ•œ] G')` -/
@[simps! apply_apply]
def flipMultilinear (f : G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G') :
    ContinuousMultilinearMap ğ•œ E (G â†’L[ğ•œ] G') :=
  MultilinearMap.mkContinuous
    { toFun := fun m =>
        LinearMap.mkContinuous
          { toFun := fun x => f x m
            map_add' := fun x y => by simp only [map_add, ContinuousMultilinearMap.add_apply]
            map_smul' := fun c x => by
              simp only [ContinuousMultilinearMap.smul_apply, map_smul, RingHom.id_apply] }
          (â€–fâ€– * âˆ i, â€–m iâ€–) fun x => by
          rw [mul_right_comm]
          exact (f x).le_of_opNorm_le _ (f.le_opNorm x)
      map_add' := fun m i x y => by
        ext1
        simp only [add_apply, ContinuousMultilinearMap.map_add, LinearMap.coe_mk,
          LinearMap.mkContinuous_apply, AddHom.coe_mk]
      map_smul' := fun m i c x => by
        ext1
        simp only [coe_smul', ContinuousMultilinearMap.map_smul, LinearMap.coe_mk,
          LinearMap.mkContinuous_apply, Pi.smul_apply, AddHom.coe_mk] }
    â€–fâ€– fun m => by
      dsimp only [MultilinearMap.coe_mk]
      exact LinearMap.mkContinuous_norm_le _ (by positivity) _
#align continuous_linear_map.flip_multilinear ContinuousLinearMap.flipMultilinear
#align continuous_linear_map.flip_multilinear_apply_apply ContinuousLinearMap.flipMultilinear_apply_apply

end ContinuousLinearMap

theorem LinearIsometry.norm_compContinuousMultilinearMap (g : G â†’â‚—áµ¢[ğ•œ] G')
    (f : ContinuousMultilinearMap ğ•œ E G) :
    â€–g.toContinuousLinearMap.compContinuousMultilinearMap fâ€– = â€–fâ€– := by
  simp only [ContinuousLinearMap.compContinuousMultilinearMap_coe,
    LinearIsometry.coe_toContinuousLinearMap, LinearIsometry.norm_map,
    ContinuousMultilinearMap.norm_def, Function.comp_apply]
#align linear_isometry.norm_comp_continuous_multilinear_map LinearIsometry.norm_compContinuousMultilinearMap

open ContinuousMultilinearMap

namespace MultilinearMap

/-- Given a map `f : G â†’â‚—[ğ•œ] MultilinearMap ğ•œ E G'` and an estimate
`H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–`, construct a continuous linear
map from `G` to `ContinuousMultilinearMap ğ•œ E G'`.

In order to lift, e.g., a map `f : (MultilinearMap ğ•œ E G) â†’â‚—[ğ•œ] MultilinearMap ğ•œ E' G'`
to a map `(ContinuousMultilinearMap ğ•œ E G) â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E' G'`,
one can apply this construction to `f.comp ContinuousMultilinearMap.toMultilinearMapLinear`
which is a linear map from `ContinuousMultilinearMap ğ•œ E G` to `MultilinearMap ğ•œ E' G'`. -/
def mkContinuousLinear (f : G â†’â‚—[ğ•œ] MultilinearMap ğ•œ E G') (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G' :=
  LinearMap.mkContinuous
    { toFun := fun x => (f x).mkContinuous (C * â€–xâ€–) <| H x
      map_add' := fun x y => by
        ext1
        simp only [_root_.map_add]
        rfl
      map_smul' := fun c x => by
        ext1
        simp only [_root_.map_smul]
        rfl }
    (max C 0) fun x => by
      rw [LinearMap.coe_mk, AddHom.coe_mk] -- Porting note: added
      exact ((f x).mkContinuous_norm_le' _).trans_eq <| by
        rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]
#align multilinear_map.mk_continuous_linear MultilinearMap.mkContinuousLinear

theorem mkContinuousLinear_norm_le' (f : G â†’â‚—[ğ•œ] MultilinearMap ğ•œ E G') (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ max C 0 := by
  dsimp only [mkContinuousLinear]
  exact LinearMap.mkContinuous_norm_le _ (le_max_right _ _) _
#align multilinear_map.mk_continuous_linear_norm_le' MultilinearMap.mkContinuousLinear_norm_le'

theorem mkContinuousLinear_norm_le (f : G â†’â‚—[ğ•œ] MultilinearMap ğ•œ E G') {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ C :=
  (mkContinuousLinear_norm_le' f C H).trans_eq (max_eq_left hC)
#align multilinear_map.mk_continuous_linear_norm_le MultilinearMap.mkContinuousLinear_norm_le

/-- Given a map `f : MultilinearMap ğ•œ E (MultilinearMap ğ•œ E' G)` and an estimate
`H : âˆ€ m m', â€–f m m'â€– â‰¤ C * âˆ i, â€–m iâ€– * âˆ i, â€–m' iâ€–`, upgrade all `MultilinearMap`s in the type to
`ContinuousMultilinearMap`s. -/
def mkContinuousMultilinear (f : MultilinearMap ğ•œ E (MultilinearMap ğ•œ E' G)) (C : â„)
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    ContinuousMultilinearMap ğ•œ E (ContinuousMultilinearMap ğ•œ E' G) :=
  mkContinuous
    { toFun := fun m => mkContinuous (f m) (C * âˆ i, â€–m iâ€–) <| H m
      map_add' := fun m i x y => by
        ext1
        simp
      map_smul' := fun m i c x => by
        ext1
        simp }
    (max C 0) fun m => by
      simp only [coe_mk]
      refine ((f m).mkContinuous_norm_le' _).trans_eq ?_
      rw [max_mul_of_nonneg, zero_mul]
      positivity
#align multilinear_map.mk_continuous_multilinear MultilinearMap.mkContinuousMultilinear

@[simp]
theorem mkContinuousMultilinear_apply (f : MultilinearMap ğ•œ E (MultilinearMap ğ•œ E' G)) {C : â„}
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) (m : âˆ€ i, E i) :
    â‡‘(mkContinuousMultilinear f C H m) = f m :=
  rfl
#align multilinear_map.mk_continuous_multilinear_apply MultilinearMap.mkContinuousMultilinear_apply

theorem mkContinuousMultilinear_norm_le' (f : MultilinearMap ğ•œ E (MultilinearMap ğ•œ E' G)) (C : â„)
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousMultilinear f C Hâ€– â‰¤ max C 0 := by
  dsimp only [mkContinuousMultilinear]
  exact mkContinuous_norm_le _ (le_max_right _ _) _
#align multilinear_map.mk_continuous_multilinear_norm_le' MultilinearMap.mkContinuousMultilinear_norm_le'

theorem mkContinuousMultilinear_norm_le (f : MultilinearMap ğ•œ E (MultilinearMap ğ•œ E' G)) {C : â„}
    (hC : 0 â‰¤ C) (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousMultilinear f C Hâ€– â‰¤ C :=
  (mkContinuousMultilinear_norm_le' f C H).trans_eq (max_eq_left hC)
#align multilinear_map.mk_continuous_multilinear_norm_le MultilinearMap.mkContinuousMultilinear_norm_le

end MultilinearMap

namespace ContinuousMultilinearMap

set_option linter.uppercaseLean3 false

theorem norm_compContinuousLinearMap_le (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) : â€–g.compContinuousLinearMap fâ€– â‰¤ â€–gâ€– * âˆ i, â€–f iâ€– :=
  opNorm_le_bound _ (by positivity) fun m =>
    calc
      â€–g fun i => f i (m i)â€– â‰¤ â€–gâ€– * âˆ i, â€–f i (m i)â€– := g.le_opNorm _
      _ â‰¤ â€–gâ€– * âˆ i, â€–f iâ€– * â€–m iâ€– :=
        (mul_le_mul_of_nonneg_left
          (prod_le_prod (fun _ _ => norm_nonneg _) fun i _ => (f i).le_opNorm (m i))
          (norm_nonneg g))
      _ = (â€–gâ€– * âˆ i, â€–f iâ€–) * âˆ i, â€–m iâ€– := by rw [prod_mul_distrib, mul_assoc]
#align continuous_multilinear_map.norm_comp_continuous_linear_le ContinuousMultilinearMap.norm_compContinuousLinearMap_le

theorem norm_compContinuous_linearIsometry_le (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â†’â‚—áµ¢[ğ•œ] Eâ‚ i) :
    â€–g.compContinuousLinearMap fun i => (f i).toContinuousLinearMapâ€– â‰¤ â€–gâ€– := by
  refine opNorm_le_bound _ (norm_nonneg _) fun m => ?_
  apply (g.le_opNorm _).trans _
  simp only [ContinuousLinearMap.coe_coe, LinearIsometry.coe_toContinuousLinearMap,
    LinearIsometry.norm_map, le_rfl]
#align continuous_multilinear_map.norm_comp_continuous_linear_isometry_le ContinuousMultilinearMap.norm_compContinuous_linearIsometry_le

theorem norm_compContinuous_linearIsometryEquiv (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â‰ƒâ‚—áµ¢[ğ•œ] Eâ‚ i) :
    â€–g.compContinuousLinearMap fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i)â€– = â€–gâ€– := by
  apply le_antisymm (g.norm_compContinuous_linearIsometry_le fun i => (f i).toLinearIsometry)
  have : g = (g.compContinuousLinearMap fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i)).compContinuousLinearMap
      fun i => ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i) := by
    ext1 m
    simp only [compContinuousLinearMap_apply, LinearIsometryEquiv.coe_coe'',
      LinearIsometryEquiv.apply_symm_apply]
  conv_lhs => rw [this]
  apply (g.compContinuousLinearMap fun i =>
    (f i : E i â†’L[ğ•œ] Eâ‚ i)).norm_compContinuous_linearIsometry_le
      fun i => (f i).symm.toLinearIsometry
#align continuous_multilinear_map.norm_comp_continuous_linear_isometry_equiv ContinuousMultilinearMap.norm_compContinuous_linearIsometryEquiv

/-- `ContinuousMultilinearMap.compContinuousLinearMap` as a bundled continuous linear map.
This implementation fixes `f : Î  i, E i â†’L[ğ•œ] Eâ‚ i`.

Actually, the map is multilinear in `f`,
see `ContinuousMultilinearMap.compContinuousLinearMapContinuousMultilinear`.

For a version fixing `g` and varying `f`, see `compContinuousLinearMapLRight`. -/
def compContinuousLinearMapL (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) :
    ContinuousMultilinearMap ğ•œ Eâ‚ G â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
  LinearMap.mkContinuous
    { toFun := fun g => g.compContinuousLinearMap f
      map_add' := fun _ _ => rfl
      map_smul' := fun _ _ => rfl }
    (âˆ i, â€–f iâ€–)
    fun _ => (norm_compContinuousLinearMap_le _ _).trans_eq (mul_comm _ _)
#align continuous_multilinear_map.comp_continuous_linear_mapL ContinuousMultilinearMap.compContinuousLinearMapL

@[simp]
theorem compContinuousLinearMapL_apply (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) : compContinuousLinearMapL f g = g.compContinuousLinearMap f :=
  rfl
#align continuous_multilinear_map.comp_continuous_linear_mapL_apply ContinuousMultilinearMap.compContinuousLinearMapL_apply

variable (G) in
theorem norm_compContinuousLinearMapL_le (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) :
    â€–compContinuousLinearMapL (G := G) fâ€– â‰¤ âˆ i, â€–f iâ€– :=
  LinearMap.mkContinuous_norm_le _ (by positivity) _
#align continuous_multilinear_map.norm_comp_continuous_linear_mapL_le ContinuousMultilinearMap.norm_compContinuousLinearMapL_le

/-- `ContinuousMultilinearMap.compContinuousLinearMap` as a bundled continuous linear map.
This implementation fixes `g : ContinuousMultilinearMap ğ•œ Eâ‚ G`.

Actually, the map is linear in `g`,
see `ContinuousMultilinearMap.compContinuousLinearMapContinuousMultilinear`.

For a version fixing `f` and varying `g`, see `compContinuousLinearMapL`. -/
def compContinuousLinearMapLRight (g : ContinuousMultilinearMap ğ•œ Eâ‚ G) :
    ContinuousMultilinearMap ğ•œ (fun i â†¦ E i â†’L[ğ•œ] Eâ‚ i) (ContinuousMultilinearMap ğ•œ E G) :=
  MultilinearMap.mkContinuous
    { toFun := fun f => g.compContinuousLinearMap f
      map_add' := by
        intro h f i fâ‚ fâ‚‚
        ext x
        simp only [compContinuousLinearMap_apply, add_apply]
        convert g.map_add (fun j â†¦ f j (x j)) i (fâ‚ (x i)) (fâ‚‚ (x i)) <;>
          exact apply_update (fun (i : Î¹) (f : E i â†’L[ğ•œ] Eâ‚ i) â†¦ f (x i)) f i _ _
      map_smul' := by
        intro h f i a fâ‚€
        ext x
        simp only [compContinuousLinearMap_apply, smul_apply]
        convert g.map_smul (fun j â†¦ f j (x j)) i a (fâ‚€ (x i)) <;>
          exact apply_update (fun (i : Î¹) (f : E i â†’L[ğ•œ] Eâ‚ i) â†¦ f (x i)) f i _ _ }
    (â€–gâ€–) (fun f â†¦ by simp [norm_compContinuousLinearMap_le])

@[simp]
theorem compContinuousLinearMapLRight_apply (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â†’L[ğ•œ] Eâ‚ i) : compContinuousLinearMapLRight g f = g.compContinuousLinearMap f :=
  rfl

variable (E) in
theorem norm_compContinuousLinearMapLRight_le (g : ContinuousMultilinearMap ğ•œ Eâ‚ G) :
    â€–compContinuousLinearMapLRight (E := E) gâ€– â‰¤ â€–gâ€– :=
  MultilinearMap.mkContinuous_norm_le _ (norm_nonneg _) _

variable (ğ•œ E Eâ‚ G)

open Function in
/-- If `f` is a collection of continuous linear maps, then the construction
`ContinuousMultilinearMap.compContinuousLinearMap`
sending a continuous multilinear map `g` to `g (fâ‚ Â·, ..., fâ‚™ Â·)`
is continuous-linear in `g` and multilinear in `fâ‚, ..., fâ‚™`. -/
noncomputable def compContinuousLinearMapMultilinear :
    MultilinearMap ğ•œ (fun i â†¦ E i â†’L[ğ•œ] Eâ‚ i)
      ((ContinuousMultilinearMap ğ•œ Eâ‚ G) â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G) where
  toFun := compContinuousLinearMapL
  map_add' f i fâ‚ fâ‚‚ := by
    ext g x
    change (g fun j â†¦ update f i (fâ‚ + fâ‚‚) j <| x j) =
        (g fun j â†¦ update f i fâ‚ j <| x j) + g fun j â†¦ update f i fâ‚‚ j (x j)
    convert g.map_add (fun j â†¦ f j (x j)) i (fâ‚ (x i)) (fâ‚‚ (x i)) <;>
      exact apply_update (fun (i : Î¹) (f : E i â†’L[ğ•œ] Eâ‚ i) â†¦ f (x i)) f i _ _
  map_smul' f i a fâ‚€ := by
    ext g x
    change (g fun j â†¦ update f i (a â€¢ fâ‚€) j <| x j) = a â€¢ g fun j â†¦ update f i fâ‚€ j (x j)
    convert g.map_smul (fun j â†¦ f j (x j)) i a (fâ‚€ (x i)) <;>
      exact apply_update (fun (i : Î¹) (f : E i â†’L[ğ•œ] Eâ‚ i) â†¦ f (x i)) f i _ _

/-- If `f` is a collection of continuous linear maps, then the construction
`ContinuousMultilinearMap.compContinuousLinearMap`
sending a continuous multilinear map `g` to `g (fâ‚ Â·, ..., fâ‚™ Â·)` is continuous-linear in `g` and
continuous-multilinear in `fâ‚, ..., fâ‚™`. -/
noncomputable def compContinuousLinearMapContinuousMultilinear :
    ContinuousMultilinearMap ğ•œ (fun i â†¦ E i â†’L[ğ•œ] Eâ‚ i)
      ((ContinuousMultilinearMap ğ•œ Eâ‚ G) â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G) :=
  @MultilinearMap.mkContinuous ğ•œ Î¹ (fun i â†¦ E i â†’L[ğ•œ] Eâ‚ i)
    ((ContinuousMultilinearMap ğ•œ Eâ‚ G) â†’L[ğ•œ] ContinuousMultilinearMap ğ•œ E G) _ _
    (fun _ â†¦ ContinuousLinearMap.toSeminormedAddCommGroup)
    (fun _ â†¦ ContinuousLinearMap.toNormedSpace) _ _
    (compContinuousLinearMapMultilinear ğ•œ E Eâ‚ G) 1
    fun f â†¦ by simpa using norm_compContinuousLinearMapL_le G f

variable {ğ•œ E Eâ‚}

/-- `ContinuousMultilinearMap.compContinuousLinearMap` as a bundled continuous linear equiv,
given `f : Î  i, E i â‰ƒL[ğ•œ] Eâ‚ i`. -/
def compContinuousLinearMapEquivL (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    ContinuousMultilinearMap ğ•œ Eâ‚ G â‰ƒL[ğ•œ] ContinuousMultilinearMap ğ•œ E G :=
  { compContinuousLinearMapL fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i) with
    invFun := compContinuousLinearMapL fun i => ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i)
    continuous_toFun := (compContinuousLinearMapL fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i)).continuous
    continuous_invFun :=
      (compContinuousLinearMapL fun i => ((f i).symm : Eâ‚ i â†’L[ğ•œ] E i)).continuous
    left_inv := by
      intro g
      ext1 m
      simp only [LinearMap.toFun_eq_coe, ContinuousLinearMap.coe_coe,
        compContinuousLinearMapL_apply, compContinuousLinearMap_apply,
        ContinuousLinearEquiv.coe_coe, ContinuousLinearEquiv.apply_symm_apply]
    right_inv := by
      intro g
      ext1 m
      simp only [compContinuousLinearMapL_apply, LinearMap.toFun_eq_coe,
        ContinuousLinearMap.coe_coe, compContinuousLinearMap_apply,
        ContinuousLinearEquiv.coe_coe, ContinuousLinearEquiv.symm_apply_apply] }
#align continuous_multilinear_map.comp_continuous_linear_map_equivL ContinuousMultilinearMap.compContinuousLinearMapEquivL

@[simp]
theorem compContinuousLinearMapEquivL_symm (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    (compContinuousLinearMapEquivL G f).symm =
      compContinuousLinearMapEquivL G fun i : Î¹ => (f i).symm :=
  rfl
#align continuous_multilinear_map.comp_continuous_linear_map_equivL_symm ContinuousMultilinearMap.compContinuousLinearMapEquivL_symm

variable {G}

@[simp]
theorem compContinuousLinearMapEquivL_apply (g : ContinuousMultilinearMap ğ•œ Eâ‚ G)
    (f : âˆ€ i, E i â‰ƒL[ğ•œ] Eâ‚ i) :
    compContinuousLinearMapEquivL G f g =
      g.compContinuousLinearMap fun i => (f i : E i â†’L[ğ•œ] Eâ‚ i) :=
  rfl
#align continuous_multilinear_map.comp_continuous_linear_map_equivL_apply ContinuousMultilinearMap.compContinuousLinearMapEquivL_apply

/-- One of the components of the iterated derivative of a continuous multilinear map. Given a
bijection `e` between a type `Î±` (typically `Fin k`) and a subset `s` of `Î¹`, this component is a
continuous multilinear map of `k` vectors `vâ‚, ..., vâ‚–`, mapping them
to `f (xâ‚, (v_{e.symm 2})â‚‚, xâ‚ƒ, ...)`, where at indices `i` in `s` one uses the `i`-th coordinate of
the vector `v_{e.symm i}` and otherwise one uses the `i`-th coordinate of a reference vector `x`.
This is continuous multilinear in the components of `x` outside of `s`, and in the `v_j`. -/
noncomputable def iteratedFDerivComponent {Î± : Type*} [Fintype Î±] [DecidableEq Î¹]
    (f : ContinuousMultilinearMap ğ•œ Eâ‚ G) {s : Set Î¹} (e : Î± â‰ƒ s) [DecidablePred (Â· âˆˆ s)] :
    ContinuousMultilinearMap ğ•œ (fun (i : {a : Î¹ // a âˆ‰ s}) â†¦ Eâ‚ i)
      (ContinuousMultilinearMap ğ•œ (fun (_ : Î±) â†¦ (âˆ€ i, Eâ‚ i)) G) :=
  (f.toMultilinearMap.iteratedFDerivComponent e).mkContinuousMultilinear â€–fâ€– <| by
    intro x m
    simp only [MultilinearMap.iteratedFDerivComponent, MultilinearMap.domDomRestrictâ‚—,
      MultilinearMap.coe_mk, MultilinearMap.domDomRestrict_apply, coe_coe]
    apply (f.le_opNorm _).trans _
    rw [â† prod_compl_mul_prod s.toFinset, mul_assoc]
    gcongr
    Â· apply le_of_eq
      have : âˆ€ x, x âˆˆ s.toFinsetá¶œ â†” (fun x â†¦ x âˆ‰ s) x := by simp
      rw [prod_subtype _ this]
      congr with i
      simp [i.2]
    Â· rw [prod_subtype _ (fun _ â†¦ s.mem_toFinset), â† Equiv.prod_comp e.symm]
      apply Finset.prod_le_prod (fun i _ â†¦ norm_nonneg _) (fun i _ â†¦ ?_)
      simpa only [i.2, â†“reduceDIte, Subtype.coe_eta] using norm_le_pi_norm (m (e.symm i)) â†‘i

@[simp] lemma iteratedFDerivComponent_apply {Î± : Type*} [Fintype Î±] [DecidableEq Î¹]
    (f : ContinuousMultilinearMap ğ•œ Eâ‚ G) {s : Set Î¹} (e : Î± â‰ƒ s) [DecidablePred (Â· âˆˆ s)]
    (v : âˆ€ i : {a : Î¹ // a âˆ‰ s}, Eâ‚ i) (w : Î± â†’ (âˆ€ i, Eâ‚ i)) :
    f.iteratedFDerivComponent e v w =
      f (fun j â†¦ if h : j âˆˆ s then w (e.symm âŸ¨j, hâŸ©) j else v âŸ¨j, hâŸ©) := by
  simp [iteratedFDerivComponent, MultilinearMap.iteratedFDerivComponent,
    MultilinearMap.domDomRestrictâ‚—]

lemma norm_iteratedFDerivComponent_le {Î± : Type*} [Fintype Î±] [DecidableEq Î¹]
    (f : ContinuousMultilinearMap ğ•œ Eâ‚ G) {s : Set Î¹} (e : Î± â‰ƒ s) [DecidablePred (Â· âˆˆ s)]
    (x : (i : Î¹) â†’ Eâ‚ i) :
    â€–f.iteratedFDerivComponent e (x Â·)â€– â‰¤ â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - Fintype.card Î±) := calc
  â€–f.iteratedFDerivComponent e (fun i â†¦ x i)â€–
    â‰¤ â€–f.iteratedFDerivComponent eâ€– * âˆ i : {a : Î¹ // a âˆ‰ s}, â€–x iâ€– :=
      ContinuousMultilinearMap.le_opNorm _ _
  _ â‰¤ â€–fâ€– * âˆ _i : {a : Î¹ // a âˆ‰ s}, â€–xâ€– := by
      gcongr
      Â· exact MultilinearMap.mkContinuousMultilinear_norm_le _ (norm_nonneg _) _
      Â· exact fun _ _ â†¦ norm_nonneg _
      Â· exact norm_le_pi_norm _ _
  _ = â€–fâ€– * â€–xâ€– ^ (Fintype.card {a : Î¹ // a âˆ‰ s}) := by rw [prod_const, card_univ]
  _ = â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - Fintype.card Î±) := by simp [Fintype.card_congr e]

open Classical in
/-- The `k`-th iterated derivative of a continuous multilinear map `f` at the point `x`. It is a
continuous multilinear map of `k` vectors `vâ‚, ..., vâ‚–` (with the same type as `x`), mapping them
to `âˆ‘ f (xâ‚, (v_{iâ‚})â‚‚, xâ‚ƒ, ...)`, where at each index `j` one uses either `xâ±¼` or one
of the `(váµ¢)â±¼`, and each `váµ¢` has to be used exactly once.
The sum is parameterized by the embeddings of `Fin k` in the index type `Î¹` (or, equivalently,
by the subsets `s` of `Î¹` of cardinality `k` and then the bijections between `Fin k` and `s`).

The fact that this is indeed the iterated FrÃ©chet derivative is proved in
`ContinuousMultilinearMap.iteratedFDeriv_eq`.
-/
protected def iteratedFDeriv (f : ContinuousMultilinearMap ğ•œ Eâ‚ G) (k : â„•) (x : (i : Î¹) â†’ Eâ‚ i) :
    ContinuousMultilinearMap ğ•œ (fun (_ : Fin k) â†¦ (âˆ€ i, Eâ‚ i)) G :=
  âˆ‘ e : Fin k â†ª Î¹, iteratedFDerivComponent f e.toEquivRange (Pi.compRightL ğ•œ _ Subtype.val x)

/-- Controlling the norm of `f.iteratedFDeriv` when `f` is continuous multilinear. For the same
bound on the iterated derivative of `f` in the calculus sense,
see `ContinuousMultilinearMap.norm_iteratedFDeriv_le`. -/
lemma norm_iteratedFDeriv_le' (f : ContinuousMultilinearMap ğ•œ Eâ‚ G) (k : â„•) (x : (i : Î¹) â†’ Eâ‚ i) :
    â€–f.iteratedFDeriv k xâ€–
      â‰¤ Nat.descFactorial (Fintype.card Î¹) k * â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - k) := by
  classical
  calc â€–f.iteratedFDeriv k xâ€–
  _ â‰¤ âˆ‘ e : Fin k â†ª Î¹, â€–iteratedFDerivComponent f e.toEquivRange (fun i â†¦ x i)â€– := norm_sum_le _ _
  _ â‰¤ âˆ‘ _ : Fin k â†ª Î¹, â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - k) := by
    gcongr with e _
    simpa using norm_iteratedFDerivComponent_le f e.toEquivRange x
  _ = Nat.descFactorial (Fintype.card Î¹) k * â€–fâ€– * â€–xâ€– ^ (Fintype.card Î¹ - k) := by
    simp [card_univ, mul_assoc]

end ContinuousMultilinearMap

end Seminorm

section Norm

namespace ContinuousMultilinearMap

/-! Results that are only true if the target space is a `NormedAddCommGroup` (and not just a
`SeminormedAddCommGroup`). -/

variable {ğ•œ : Type u} {Î¹ : Type v} {E : Î¹ â†’ Type wE} {G : Type wG} {G' : Type wG'} [Fintype Î¹]
  [NontriviallyNormedField ğ•œ] [âˆ€ i, SeminormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G] [SeminormedAddCommGroup G'] [NormedSpace ğ•œ G']

variable (f : ContinuousMultilinearMap ğ•œ E G)

/-- A continuous linear map is zero iff its norm vanishes. -/
theorem opNorm_zero_iff : â€–fâ€– = 0 â†” f = 0 := by
  simp [â† (opNorm_nonneg f).le_iff_eq, opNorm_le_iff f le_rfl, ext_iff]
#align continuous_multilinear_map.op_norm_zero_iff ContinuousMultilinearMap.opNorm_zero_iff

@[deprecated (since := "2024-02-02")] alias op_norm_zero_iff := opNorm_zero_iff

/-- Continuous multilinear maps themselves form a normed group with respect to
    the operator norm. -/
instance normedAddCommGroup : NormedAddCommGroup (ContinuousMultilinearMap ğ•œ E G) :=
  NormedAddCommGroup.ofSeparation (fun f â†¦ (opNorm_zero_iff f).mp)
#align continuous_multilinear_map.normed_add_comm_group ContinuousMultilinearMap.normedAddCommGroup

/-- An alias of `ContinuousMultilinearMap.normedAddCommGroup` with non-dependent types to help
typeclass search. -/
instance normedAddCommGroup' :
    NormedAddCommGroup (ContinuousMultilinearMap ğ•œ (fun _ : Î¹ => G') G) :=
  ContinuousMultilinearMap.normedAddCommGroup
#align continuous_multilinear_map.normed_add_comm_group' ContinuousMultilinearMap.normedAddCommGroup'

variable (ğ•œ G)

theorem norm_ofSubsingleton_id [Subsingleton Î¹] [Nontrivial G] (i : Î¹) :
    â€–ofSubsingleton ğ•œ G G i (.id _ _)â€– = 1 := by simp
#align continuous_multilinear_map.norm_of_subsingleton ContinuousMultilinearMap.norm_ofSubsingleton_id

theorem nnnorm_ofSubsingleton_id [Subsingleton Î¹] [Nontrivial G] (i : Î¹) :
    â€–ofSubsingleton ğ•œ G G i (.id _ _)â€–â‚Š = 1 :=
  NNReal.eq <| norm_ofSubsingleton_id _ _ _
#align continuous_multilinear_map.nnnorm_of_subsingleton ContinuousMultilinearMap.nnnorm_ofSubsingleton_id

variable {ğ•œ G}

open Topology Filter

/-- If the target space is complete, the space of continuous multilinear maps with its norm is also
complete. The proof is essentially the same as for the space of continuous linear maps (modulo the
addition of `Finset.prod` where needed). The duplication could be avoided by deducing the linear
case from the multilinear case via a currying isomorphism. However, this would mess up imports,
and it is more satisfactory to have the simplest case as a standalone proof. -/
instance completeSpace [CompleteSpace G] : CompleteSpace (ContinuousMultilinearMap ğ•œ E G) := by
  -- We show that every Cauchy sequence converges.
  refine Metric.complete_of_cauchySeq_tendsto fun f hf => ?_
  -- We now expand out the definition of a Cauchy sequence,
  rcases cauchySeq_iff_le_tendsto_0.1 hf with âŸ¨b, b0, b_bound, b_limâŸ©
  -- and establish that the evaluation at any point `v : Î  i, E i` is Cauchy.
  have cau : âˆ€ v, CauchySeq fun n => f n v := by
    intro v
    apply cauchySeq_iff_le_tendsto_0.2 âŸ¨fun n => b n * âˆ i, â€–v iâ€–, _, _, _âŸ©
    Â· intro n
      have := b0 n
      positivity
    Â· intro n m N hn hm
      rw [dist_eq_norm]
      apply le_trans ((f n - f m).le_opNorm v) _
      exact mul_le_mul_of_nonneg_right (b_bound n m N hn hm) <| by positivity
    Â· simpa using b_lim.mul tendsto_const_nhds
  -- We assemble the limits points of those Cauchy sequences
  -- (which exist as `G` is complete)
  -- into a function which we call `F`.
  choose F hF using fun v => cauchySeq_tendsto_of_complete (cau v)
  -- Next, we show that this `F` is multilinear,
  let Fmult : MultilinearMap ğ•œ E G :=
    { toFun := F
      map_add' := fun v i x y => by
        have A := hF (Function.update v i (x + y))
        have B := (hF (Function.update v i x)).add (hF (Function.update v i y))
        simp? at A B says simp only [map_add] at A B
        exact tendsto_nhds_unique A B
      map_smul' := fun v i c x => by
        have A := hF (Function.update v i (c â€¢ x))
        have B := Filter.Tendsto.smul (tendsto_const_nhds (x := c)) (hF (Function.update v i x))
        simp? at A B says simp only [map_smul] at A B
        exact tendsto_nhds_unique A B }
  -- and that `F` has norm at most `(b 0 + â€–f 0â€–)`.
  have Fnorm : âˆ€ v, â€–F vâ€– â‰¤ (b 0 + â€–f 0â€–) * âˆ i, â€–v iâ€– := by
    intro v
    have A : âˆ€ n, â€–f n vâ€– â‰¤ (b 0 + â€–f 0â€–) * âˆ i, â€–v iâ€– := by
      intro n
      apply le_trans ((f n).le_opNorm _) _
      apply mul_le_mul_of_nonneg_right _ <| by positivity
      calc
        â€–f nâ€– = â€–f n - f 0 + f 0â€– := by
          congr 1
          abel
        _ â‰¤ â€–f n - f 0â€– + â€–f 0â€– := norm_add_le _ _
        _ â‰¤ b 0 + â€–f 0â€– := by
          apply add_le_add_right
          simpa [dist_eq_norm] using b_bound n 0 0 (zero_le _) (zero_le _)
    exact le_of_tendsto (hF v).norm (eventually_of_forall A)
  -- Thus `F` is continuous, and we propose that as the limit point of our original Cauchy sequence.
  let Fcont := Fmult.mkContinuous _ Fnorm
  use Fcont
  -- Our last task is to establish convergence to `F` in norm.
  have : âˆ€ n, â€–f n - Fcontâ€– â‰¤ b n := by
    intro n
    apply opNorm_le_bound _ (b0 n) fun v => ?_
    have A : âˆ€á¶  m in atTop, â€–(f n - f m) vâ€– â‰¤ b n * âˆ i, â€–v iâ€– := by
      refine eventually_atTop.2 âŸ¨n, fun m hm => ?_âŸ©
      apply le_trans ((f n - f m).le_opNorm _) _
      exact mul_le_mul_of_nonneg_right (b_bound n m n le_rfl hm) <| by positivity
    have B : Tendsto (fun m => â€–(f n - f m) vâ€–) atTop (ğ“ â€–(f n - Fcont) vâ€–) :=
      Tendsto.norm (tendsto_const_nhds.sub (hF v))
    exact le_of_tendsto B A
  rw [tendsto_iff_norm_sub_tendsto_zero]
  exact squeeze_zero (fun n => norm_nonneg _) this b_lim

end ContinuousMultilinearMap

end Norm

section Norm

/-! Results that are only true if the source is a `NormedAddCommGroup` (and not just a
`SeminormedAddCommGroup`). -/

variable {ğ•œ : Type u} {Î¹ : Type v} {E : Î¹ â†’ Type wE} {G : Type wG} [Fintype Î¹]
  [NontriviallyNormedField ğ•œ] [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
  [SeminormedAddCommGroup G] [NormedSpace ğ•œ G]

namespace MultilinearMap

variable (f : MultilinearMap ğ•œ E G)

/-- If a multilinear map in finitely many variables on normed spaces satisfies the inequality
`â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ i / â€–c iâ€– < â€–m iâ€– < Îµ i` for some positive numbers `Îµ i`
and elements `c i : ğ•œ`, `1 < â€–c iâ€–`, then it satisfies this inequality for all `m`. -/
theorem bound_of_shell {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < â€–c iâ€–)
    (hf : âˆ€ m : âˆ€ i, E i, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : âˆ€ i, E i) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  bound_of_shell_of_norm_map_coord_zero f
    (fun h â†¦ by rw [map_coord_zero f _ (norm_eq_zero.1 h), norm_zero]) hÎµ hc hf m
#align multilinear_map.bound_of_shell MultilinearMap.bound_of_shell

end MultilinearMap

end Norm
