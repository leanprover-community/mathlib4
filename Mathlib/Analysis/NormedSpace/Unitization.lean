/-
Copyright (c) 2023 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Algebra.Algebra.Unitization
import Mathlib.Analysis.NormedSpace.OperatorNorm

/-!
# Unitization norms

Given a not necessarily unital normed `ğ•œ`-algebra `A`, it is frequently of interest to equip its
`Unitization` with a norm which simultaneously makes it into a normed algebra and also satisfies
two properties:

- `â€–1â€– = 1` (i.e., `NormOneClass`)
- The embedding of `A` in `Unitization ğ•œ A` is an isometry. (i.e., `Isometry Unitization.inr`)

One way to do this is to equip it with the norm from `PiLp 1` (actually, it should be
`ProdLp 1`, but that doesn't exist), that is, `â€–(k, a)â€– = â€–kâ€– + â€–aâ€–`. However, when the norm on `A`
is *regular* (i.e., `ContinuousLinearMap.mul`) is an isometry, there is another natural choice:
the pullback of the norm on `ğ•œ Ã— (A â†’L[ğ•œ] A)` under the map
`(k, a) â†¦ (k, k â€¢ 1 + ContinuousLinearMap.mul ğ•œ A a)`. It turns out that among all norms on the
unitization satisfying the properties specified above, the norm inherited from `PiLp 1` is maximal,
and the norm inherited from this pullback is minimal.

For possibly non-unital `RegularNormedAlgebra`s  `A` (over `ğ•œ`), we construct a `NormedAlgebra`
structure on `Unitization ğ•œ A` using the pullback described above. The reason for choosing this norm
is that for a Câ‹†-algebra `A` its norm is always regular, and the pullback norm on `Unitization ğ•œ A`
is then also a Câ‹†-norm.

## Main definitions

- `NonUnitalAlgHom.Lmul : A â†’â‚™â‚[ğ•œ] A â†’L[ğ•œ] A`: `ContinuousLinearMap.mul` upgraded to a non-unital
  algebra homomorphism.
- `Unitization.leftRegRep : Unitization ğ•œ A â†’â‚[ğ•œ] (ğ•œ Ã— (A â†’L[ğ•œ] A))`: The first coordinate of this
  map is just `Unitization.fst` and the second is the left regular representation of
  `Unitization ğ•œ A` *acting on `A`*. This is the `Unitization.lift` of `NonUnitalAlgHom.Lmul`.
  We use this map to pull back the `NormedRing` and `NormedAlgebra` structures.

## Implementation details

We ensure that the uniform structure, and hence also the topological structure, is definitionally
equal to `instUniformSpaceProd` (viewing `Unitization ğ•œ A` as `ğ•œ Ã— A`) by means of forgetful
inheritance. The same is true of the bornology.

-/

variable (ğ•œ A : Type _) [NontriviallyNormedField ğ•œ] [NonUnitalNormedRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]

open ContinuousLinearMap

/-- Multiplication on the left in a non-unital algebra `A` as a non-unital algebra homomorphism
into the algebra of *continuous* linear maps. This has more algebraic structure than
`ContinuousLinearMap.mul`, but there is no longer continuity bundled in the first coordinate. -/
noncomputable def NonUnitalAlgHom.Lmul : A â†’â‚™â‚[ğ•œ] A â†’L[ğ•œ] A :=
  { mul ğ•œ A with
    toFun := fun a => mul ğ•œ A a
    map_mul' := fun a b => by ext x; simp [mul_assoc a b x]
    map_zero' := by ext x; simp only [map_zero] }

variable {ğ•œ A}

@[simp]
theorem NonUnitalAlgHom.coe_Lmul : â‡‘(NonUnitalAlgHom.Lmul ğ•œ A) = mul ğ•œ A :=
  rfl

variable (ğ•œ A)

namespace Unitization

/-- `leftRegRep` stands for "left regular representation" which is multiplication on the left. So,
given `(k, a) : Unitization ğ•œ A`, the second coordinate of `Unitization.leftRegRep (k, a)` should
be the representation of `Unitization ğ•œ A` on `A â†’L[ğ•œ] A`; note that this is not just
`NonUnitalAlgHom.Lmul` for a few reasons: (a) that would either be `A` acting on `A`, or
(b) `Unitization ğ•œ A` acting on `Unitization ğ•œ A`, and (c) that's a `NonUnitalAlgHom` but
here we need an `AlgHom`. In addition, the first coordinate of `Unitization.leftRegRep (k, a)`
should just be `k`. See `Unitization.leftRegRep_apply` also. -/
noncomputable def leftRegRep : Unitization ğ•œ A â†’â‚[ğ•œ] ğ•œ Ã— (A â†’L[ğ•œ] A) :=
  (lift 0).prod (lift <| NonUnitalAlgHom.Lmul ğ•œ A)

variable {ğ•œ A}

@[simp]
theorem leftRegRep_apply (x : Unitization ğ•œ A) :
    leftRegRep ğ•œ A x = (x.fst, algebraMap ğ•œ (A â†’L[ğ•œ] A) x.fst + mul ğ•œ A x.snd) :=
  show (x.fst + 0, _) = (x.fst, _) by rw [add_zero]; rfl

/-- this lemma establishes that if `ContinuousLinearMap.mul ğ•œ A` is injective, then so is
`Unitization.leftRegRep ğ•œ A`. When `A` is a `RegularNormedAlgebra`, then
`ContinuousLinearMap.mul ğ•œ A` is an isometry, and is therefore automatically injective. -/
theorem leftRegRep_injective_of_clm_mul_injective
    (h : Function.Injective (mul ğ•œ A)) :
    Function.Injective (leftRegRep ğ•œ A) := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  induction x using Unitization.ind
  rw [map_add] at hx
  simp only [Prod.mk_add_mk, add_zero, fst_inl, leftRegRep_apply,
    snd_inl, snd_inr, Prod.mk_eq_zero, zero_add, fst_inr,
    map_zero, leftRegRep_apply, add_zero, Prod.mk_eq_zero] at hx
  obtain âŸ¨rfl, hxâŸ© := hx
  simp only [map_zero, zero_add, inl_zero] at hx âŠ¢
  rw [â† map_zero (mul ğ•œ A)] at hx
  rw [h hx, inr_zero]

variable [RegularNormedAlgebra ğ•œ A]
variable (ğ•œ A)

/- In a `RegularNormedAlgebra`, the map `Unitization.leftRegRep ğ•œ A` is injective. We will use this
to pull back the norm from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A`. -/
theorem leftRegRep_injective : Function.Injective (leftRegRep ğ•œ A) :=
  leftRegRep_injective_of_clm_mul_injective (isometry_mul ğ•œ A).injective

variable {ğ•œ A}

section Aux

/-- Pull back the normed ring structure from `(A â†’L[ğ•œ] A) Ã— ğ•œ` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.leftRegRep ğ•œ A`. This does not give us the desired topology,
uniformity or bornology on `Unitization ğ•œ A` (which we want to agree with `Prod`), so we only use
it as a local instance to build the real one. -/
@[reducible]
noncomputable def normedRingAux : NormedRing (Unitization ğ•œ A) :=
  @NormedRing.induced _ (Unitization ğ•œ A) (ğ•œ Ã— (A â†’L[ğ•œ] A)) Unitization.instRing
    Prod.normedRing _ (leftRegRep ğ•œ A) (leftRegRep_injective ğ•œ A)
-- ummmm... what? why does Lean need me to fill in these instances?

attribute [local instance] Unitization.normedRingAux

/-- Pull back the normed algebra structure from `(A â†’L[ğ•œ] A) Ã— ğ•œ` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.leftRegRep ğ•œ A`. This uses the wrong `NormedRing` instance (i.e.,
`Unitization.normedRingAux`), so we only use it as a local instance to build the real one.-/
@[reducible]
noncomputable def normedAlgebraAux : NormedAlgebra ğ•œ (Unitization ğ•œ A) :=
  NormedAlgebra.induced ğ•œ (Unitization ğ•œ A) (ğ•œ Ã— (A â†’L[ğ•œ] A)) (leftRegRep ğ•œ A)

attribute [local instance] Unitization.normedAlgebraAux

theorem norm_def (x : Unitization ğ•œ A) : â€–xâ€– = â€–leftRegRep ğ•œ A xâ€– :=
  rfl

/-- This is often the more useful lemma to rewrite the norm as opposed to `Unitization.norm_def`. -/
theorem norm_eq_sup (x : Unitization ğ•œ A) :
    â€–xâ€– = â€–x.fstâ€– âŠ” â€–algebraMap ğ•œ (A â†’L[ğ•œ] A) x.fst + mul ğ•œ A x.sndâ€– := by
  rw [norm_def, leftRegRep_apply, Prod.norm_def, sup_eq_max]

variable (ğ•œ A)

/-- The identity map between `Unitization ğ•œ A` and `ğ•œ Ã— A` as an `AddEquiv`. -/
protected def addEquiv : Unitization ğ•œ A â‰ƒ+ ğ•œ Ã— A :=
  AddEquiv.refl _

variable {ğ•œ A}

theorem lipschitzWith_addEquiv :
    LipschitzWith (2 : â„).toNNReal (Unitization.addEquiv ğ•œ A) := by
  refine' AddMonoidHomClass.lipschitz_of_bound (Unitization.addEquiv ğ•œ A) 2 fun x => _
  rw [norm_eq_sup, Prod.norm_def]
  refine' max_le _ _
  Â· rw [sup_eq_max, mul_max_of_nonneg _ _ (zero_le_two : (0 : â„) â‰¤ 2)]
    exact le_max_of_le_left ((le_add_of_nonneg_left (norm_nonneg _)).trans_eq (two_mul _).symm)
  Â· change â€–x.sndâ€– â‰¤ _
    nontriviality A
    rw [two_mul]
    calc
      â€–x.sndâ€– = â€–mul ğ•œ A x.sndâ€– :=
        ((AddMonoidHomClass.isometry_iff_norm (mul ğ•œ A)).mp (isometry_mul ğ•œ A) _).symm
      _ â‰¤ â€–algebraMap ğ•œ _ x.fst + mul ğ•œ A x.sndâ€– + â€–x.fstâ€– := by
        simpa only [add_comm _ (mul ğ•œ A x.snd), norm_algebraMap'] using
          norm_le_add_norm_add (mul ğ•œ A x.snd) (algebraMap ğ•œ _ x.fst)
      _ â‰¤ _ := add_le_add le_sup_right le_sup_left

theorem antilipschitzWith_addEquiv :
    AntilipschitzWith 2 (Unitization.addEquiv ğ•œ A) := by
  refine' AddMonoidHomClass.antilipschitz_of_bound (Unitization.addEquiv ğ•œ A) fun x => _
  rw [norm_eq_sup, Prod.norm_def, NNReal.coe_two]
  refine' max_le _ _
  Â· rw [mul_max_of_nonneg _ _ (zero_le_two : (0 : â„) â‰¤ 2)]
    exact le_max_of_le_left ((le_add_of_nonneg_left (norm_nonneg _)).trans_eq (two_mul _).symm)
  Â· nontriviality A
    calc
      â€–algebraMap ğ•œ _ x.fst + mul ğ•œ A x.sndâ€– â‰¤ â€–algebraMap ğ•œ _ x.fstâ€– + â€–mul ğ•œ A x.sndâ€– :=
        norm_add_le _ _
      _ = â€–x.fstâ€– + â€–x.sndâ€– := by
        rw [norm_algebraMap', (AddMonoidHomClass.isometry_iff_norm (mul ğ•œ A)).mp (isometry_mul ğ•œ A)]
      _ â‰¤ _ := (add_le_add (le_max_left _ _) (le_max_right _ _)).trans_eq (two_mul _).symm

open Bornology Filter
open scoped Uniformity

theorem uniformity_eq_aux :
    @uniformity (Unitization ğ•œ A) instUniformSpaceProd = ğ“¤ (Unitization ğ•œ A) := by
  have key : UniformInducing (Unitization.addEquiv ğ•œ A) :=
    antilipschitzWith_addEquiv.uniformInducing lipschitzWith_addEquiv.uniformContinuous
  rw [â† key.comap_uniformity]
  exact comap_id.symm

theorem cobounded_eq_aux :
    @cobounded (Unitization ğ•œ A) Prod.instBornology = cobounded (Unitization ğ•œ A) :=
  calc
    _ = comap (Unitization.addEquiv ğ•œ A) (cobounded _) := comap_id.symm
    _ = cobounded (Unitization ğ•œ A) :=
      le_antisymm lipschitzWith_addEquiv.comap_cobounded_le
        antilipschitzWith_addEquiv.tendsto_cobounded.le_comap

end Aux

/-- The uniformity on `Unitization ğ•œ A` is inherited from `ğ•œ Ã— A`. -/
instance instUniformSpace : UniformSpace (Unitization ğ•œ A) :=
  instUniformSpaceProd

/-- The bornology on `Unitization ğ•œ A` is inherited from `ğ•œ Ã— A`. -/
instance instBornology : Bornology (Unitization ğ•œ A) :=
  Prod.instBornology

/-- `Unitization ğ•œ A` is complete whenever `ğ•œ` and `A` are also.  -/
instance instCompleteSpace [CompleteSpace ğ•œ] [CompleteSpace A] :
    CompleteSpace (Unitization ğ•œ A) :=
  CompleteSpace.prod

/-- Pull back the metric structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.leftRegRep ğ•œ A`, but replace the bornology and the uniformity so
that they coincide with `ğ•œ Ã— A`. -/
noncomputable instance instMetricSpace : MetricSpace (Unitization ğ•œ A) :=
  (normedRingAux.toMetricSpace.replaceUniformity uniformity_eq_aux).replaceBornology
    fun s => Filter.ext_iff.1 cobounded_eq_aux (sá¶œ)

/-- Pull back the normed ring structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.leftRegRep ğ•œ A`. -/
noncomputable instance instNormedRing : NormedRing (Unitization ğ•œ A)
    where
  dist_eq := normedRingAux.dist_eq
  norm_mul := normedRingAux.norm_mul
  norm := normedRingAux.norm

/-- Pull back the normed algebra structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.leftRegRep ğ•œ A`. -/
instance instNormedAlgebra : NormedAlgebra ğ•œ (Unitization ğ•œ A) where
  norm_smul_le k x := by
    rw [norm_def, map_smul, norm_smul, â† norm_def]

-- this should go in `Algebra.Algebra.Unitization`
instance instNontrivial {ğ•œ A} [Nontrivial ğ•œ] [Nonempty A] :
    Nontrivial (Unitization ğ•œ A) :=
  nontrivial_prod_left

instance instNormOneClass : NormOneClass (Unitization ğ•œ A) where
  norm_one := by simpa only [norm_eq_sup, fst_one, norm_one, snd_one, map_one, map_zero,
      add_zero, ge_iff_le, sup_eq_left] using op_norm_le_bound _ zero_le_one fun x => by simp

lemma norm_inr (a : A) : â€–(a : Unitization ğ•œ A)â€– = â€–aâ€– := by
  simp [norm_eq_sup]

lemma isometry_inr : Isometry ((â†‘) : A â†’ Unitization ğ•œ A) :=
  AddMonoidHomClass.isometry_of_norm (inrNonUnitalAlgHom ğ•œ A) norm_inr

/- These examples verify that the bornology and uniformity (hence also the topology) are the
correct ones. -/
example : (instNormedRing (ğ•œ := ğ•œ) (A := A)).toMetricSpace = instMetricSpace := rfl
example : (instMetricSpace (ğ•œ := ğ•œ) (A := A)).toBornology = instBornology := rfl
example : (instMetricSpace (ğ•œ := ğ•œ) (A := A)).toUniformSpace = instUniformSpace := rfl
