/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov, Heather Macbeth, Patrick Massot
-/
import Mathlib.Topology.Algebra.Module.Alternating.Topology
import Mathlib.Analysis.NormedSpace.Multilinear.Basic

/-!
# Operator norm on the space of continuous alternating maps

In this file we show that continuous alternating maps
from a seminormed space to a (semi)normed space form a (semi)normed space.
We also prove basic facts about this norm
and define bundled versions of some operations on continuous alternating maps.

Most proofs just invoke the corresponding fact about continuous multilinear maps.
-/

noncomputable section

open scoped BigOperators NNReal
open Finset Metric

/-!
### Type variables

We use the following type variables in this file:

* `ğ•œ` : a nontrivially normed field;
* `Î¹`: a finite index type;
* `E`, `F`, `G`: (semi)normed vector spaces over `ğ•œ`.
-/

/-- Applying a continuous alternating map to a vector is continuous
in the pair (map, vector).

Continuity in in the vector holds by definition
and continuity in the map holds if both the domain and the codomain are topological vector spaces.
However, continuity in the pair (map, vector) needs the domain to be a locally bounded TVS.
We have no typeclass for a locally bounded TVS,
so we require it to be a seminormed space instead. -/
instance ContinuousAlternatingMap.instContinuousEval {ğ•œ Î¹ E F : Type*}
    [NormedField ğ•œ] [Finite Î¹] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
    [TopologicalSpace F] [AddCommGroup F] [IsTopologicalAddGroup F] [Module ğ•œ F] :
    ContinuousEval (E [â‹€^Î¹]â†’L[ğ•œ] F) (Î¹ â†’ E) F :=
  .of_continuous_forget continuous_toContinuousMultilinearMap

section Seminorm

universe u wE wF wG v
variable {ğ•œ : Type u} {n : â„•} {E : Type wE} {F : Type wF} {G : Type wG} {Î¹ : Type v}
  [NontriviallyNormedField ğ•œ]
  [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
  [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]
  [SeminormedAddCommGroup G] [NormedSpace ğ•œ G]

/-!
### Continuity properties of alternating maps

We relate continuity of alternating maps to the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, in
both directions. Along the way, we prove useful bounds on the difference `â€–f mâ‚ - f mâ‚‚â€–`.
-/
namespace AlternatingMap

/-- If `f` is a continuous alternating map on `E`
and `m` is an element of `Î¹ â†’ E` such that one of the `m i` has norm `0`, then `f m` has norm `0`.

Note that we cannot drop the continuity assumption.
Indeed, let `â„â‚€` be a copy or `â„` with zero norm and indiscrete topology.
Then `f : (Unit â†’ â„â‚€) â†’ â„` given by `f x = x ()`
sends vector `1` with zero norm to number `1` with nonzero norm. -/
theorem norm_map_coord_zero (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (hf : Continuous f)
    {m : Î¹ â†’ E} {i : Î¹} (hi : â€–m iâ€– = 0) : â€–f mâ€– = 0 :=
  f.1.norm_map_coord_zero hf hi

variable [Fintype Î¹]

/-- If an alternating map in finitely many variables on seminormed spaces
sends vectors with a component of norm zero to vectors of norm zero
and satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ i / â€–c iâ€– < â€–m iâ€– < Îµ i`
for some positive numbers `Îµ i` and elements `c i : ğ•œ`, `1 < â€–c iâ€–`,
then it satisfies this inequality for all `m`.

The first assumption is automatically satisfied on normed spaces, see `bound_of_shell` below.
For seminormed spaces, it follows from continuity of `f`,
see lemma `bound_of_shell_of_continuous` below. -/
theorem bound_of_shell_of_norm_map_coord_zero (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F)
    (hfâ‚€ : âˆ€ {m i}, â€–m iâ€– = 0 â†’ â€–f mâ€– = 0)
    {Îµ : Î¹ â†’ â„} {C : â„} (hÎµ : âˆ€ i, 0 < Îµ i) {c : Î¹ â†’ ğ•œ} (hc : âˆ€ i, 1 < â€–c iâ€–)
    (hf : âˆ€ m : Î¹ â†’ E, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : Î¹ â†’ E) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.bound_of_shell_of_norm_map_coord_zero hfâ‚€ hÎµ hc hf m

/-- If a continuous alternating map in finitely many variables on normed spaces
satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`
on a shell `Îµ / â€–câ€– < â€–m iâ€– < Îµ` for some positive number `Îµ` and an elements `c : ğ•œ`, `1 < â€–câ€–`,
then it satisfies this inequality for all `m`.

If the domain is a Hausdorff space, then the continuity assumption is redundant,
see `bound_of_shell` below. -/
theorem bound_of_shell_of_continuous (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (hfc : Continuous f)
    {Îµ : â„} {C : â„} (hÎµ : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
    (hf : âˆ€ m : Î¹ â†’ E, (âˆ€ i, Îµ / â€–câ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : Î¹ â†’ E) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.bound_of_shell_of_continuous hfc (fun _ â†¦ hÎµ) (fun _ â†¦ hc) hf m

/-- If an alternating map in finitely many variables on a seminormed space is continuous,
then it satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`,
for some `C` which can be chosen to be positive. -/
theorem exists_bound_of_continuous (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (hf : Continuous f) :
    âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
  f.1.exists_bound_of_continuous hf

/-- If an alternating map `f` satisfies a boundedness property around `0`,
one can deduce a bound on `f mâ‚ - f mâ‚‚` using the multilinearity.
Here, we give a precise but hard to use version.
See `AlternatingMap.norm_image_sub_le_of_bound` for a less precise but more usable version.
The bound reads
`â€–f m - f m'â€– â‰¤
  C * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
theorem norm_image_sub_le_of_bound' [DecidableEq Î¹] (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound' hC H mâ‚ mâ‚‚

/-- If an alternating map `f` satisfies a boundedness property around `0`,
one can deduce a bound on `f mâ‚ - f mâ‚‚` using the multilinearity.
Here, we give a usable but not very precise version.
See `AlternatingMap.norm_image_sub_le_of_bound'` for a more precise but less usable version.
The bound is `â€–f m - f m'â€– â‰¤ C * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
theorem norm_image_sub_le_of_bound (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * (Fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound hC H mâ‚ mâ‚‚

/-- If an alternating map satisfies an inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`,
then it is continuous. -/
theorem continuous_of_bound (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
    Continuous f :=
  f.toMultilinearMap.continuous_of_bound C H

/-- Construct a continuous alternating map
from a alternating map satisfying a boundedness condition. -/
def mkContinuous (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : E [â‹€^Î¹]â†’L[ğ•œ] F :=
  { f with cont := f.continuous_of_bound C H }

@[simp] theorem coe_mkContinuous (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
    (f.mkContinuous C H : (Î¹ â†’ E) â†’ F) = f :=
  rfl

end AlternatingMap

/-!
### Continuous alternating maps

We define the norm `â€–fâ€–` of a continuous alternating map `f` in finitely many variables
as the smallest nonnegative number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`.
We show that this defines a normed space structure on `E [â‹€^Î¹]â†’L[ğ•œ] F`.
-/

namespace ContinuousAlternatingMap

variable [Fintype Î¹] {f : E [â‹€^Î¹]â†’L[ğ•œ] F} {m : Î¹ â†’ E}

theorem bound (f : E [â‹€^Î¹]â†’L[ğ•œ] F) : âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
  f.toContinuousMultilinearMap.bound

/-- Continuous alternating maps form a seminormed additive commutative group.
We override projection to `PseudoMetricSpace` to ensure that instances commute
in `with_reducible_and_instances`. -/
instance instSeminormedAddCommGroup : SeminormedAddCommGroup (E [â‹€^Î¹]â†’L[ğ•œ] F) where
  toPseudoMetricSpace := .induced toContinuousMultilinearMap inferInstance
  __ := SeminormedAddCommGroup.induced _ _ (toMultilinearAddHom : E [â‹€^Î¹]â†’L[ğ•œ] F â†’+ _)
  norm f := â€–f.toContinuousMultilinearMapâ€–

@[simp] theorem norm_toContinuousMultilinearMap (f : E [â‹€^Î¹]â†’L[ğ•œ] F) : â€–f.1â€– = â€–fâ€– := rfl
@[simp] theorem nnnorm_toContinuousMultilinearMap (f : E [â‹€^Î¹]â†’L[ğ•œ] F) : â€–f.1â€–â‚Š = â€–fâ€–â‚Š := rfl
@[simp] theorem enorm_toContinuousMultilinearMap (f : E [â‹€^Î¹]â†’L[ğ•œ] F) : â€–f.1â€–â‚‘ = â€–fâ€–â‚‘ := rfl

/-- The inclusion of `E [â‹€^Î¹]â†’L[ğ•œ] F` into `ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) F`
as a linear isometry. -/
@[simps!]
def toContinuousMultilinearMapLI :
    E [â‹€^Î¹]â†’L[ğ•œ] F â†’â‚—áµ¢[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) F where
  toLinearMap := toContinuousMultilinearMapLinear
  norm_map' _ := rfl

theorem norm_def (f : E [â‹€^Î¹]â†’L[ğ•œ] F) :
    â€–fâ€– = sInf {c : â„ | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
  rfl

theorem bounds_nonempty :
    âˆƒ c, c âˆˆ {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
  ContinuousMultilinearMap.bounds_nonempty

theorem bounds_bddBelow {f : E [â‹€^Î¹]â†’L[ğ•œ] F} :
    BddBelow {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
  ContinuousMultilinearMap.bounds_bddBelow

theorem isLeast_opNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) :
    IsLeast {c : â„ | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} â€–fâ€– :=
  f.1.isLeast_opNorm

/-- The fundamental property of the operator norm of a continuous alternating map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`. -/
theorem le_opNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (m : Î¹ â†’ E) : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– := f.1.le_opNorm m

theorem le_mul_prod_of_opNorm_le_of_le
    {m : Î¹ â†’ E} {C : â„} {b : Î¹ â†’ â„} (hC : â€–fâ€– â‰¤ C) (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) :
    â€–f mâ€– â‰¤ C * âˆ i, b i :=
  f.1.le_mul_prod_of_opNorm_le_of_le hC hm

theorem le_opNorm_mul_prod_of_le (f : E [â‹€^Î¹]â†’L[ğ•œ] F) {b : Î¹ â†’ â„} (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) :
    â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, b i :=
  f.1.le_opNorm_mul_prod_of_le hm

theorem le_opNorm_mul_pow_card_of_le (f : E [â‹€^Î¹]â†’L[ğ•œ] F) {m b} (hm : â€–mâ€– â‰¤ b) :
    â€–f mâ€– â‰¤ â€–fâ€– * b ^ Fintype.card Î¹ :=
  f.1.le_opNorm_mul_pow_card_of_le hm

theorem le_opNorm_mul_pow_of_le {n} (f : E [â‹€^Fin n]â†’L[ğ•œ] F) {m b} (hm : â€–mâ€– â‰¤ b) :
    â€–f mâ€– â‰¤ â€–fâ€– * b ^ n :=
  f.1.le_opNorm_mul_pow_of_le hm

theorem le_of_opNorm_le {C : â„} (h : â€–fâ€– â‰¤ C) (m : Î¹ â†’ E) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.le_of_opNorm_le h m

theorem ratio_le_opNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (m : Î¹ â†’ E) : â€–f mâ€– / âˆ i, â€–m iâ€– â‰¤ â€–fâ€– :=
  f.1.ratio_le_opNorm m

/-- The image of the unit ball under a continuous alternating map is bounded. -/
theorem unit_le_opNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (h : â€–mâ€– â‰¤ 1) : â€–f mâ€– â‰¤ â€–fâ€– := f.1.unit_le_opNorm h

/-- If one controls the norm of every `f x`, then one controls the norm of `f`. -/
theorem opNorm_le_bound (f : E [â‹€^Î¹]â†’L[ğ•œ] F) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ m, â€–f mâ€– â‰¤ M * âˆ i, â€–m iâ€–) : â€–fâ€– â‰¤ M :=
  f.1.opNorm_le_bound hMp hM

theorem opNorm_le_iff {C : â„} (hC : 0 â‰¤ C) : â€–fâ€– â‰¤ C â†” âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.opNorm_le_iff hC

/-- The fundamental property of the operator norm of a continuous alternating map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`, `nnnorm` version. -/
theorem le_opNNNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (m : Î¹ â†’ E) : â€–f mâ€–â‚Š â‰¤ â€–fâ€–â‚Š * âˆ i, â€–m iâ€–â‚Š :=
  f.1.le_opNNNorm m

theorem le_of_opNNNorm_le {C : â„â‰¥0} (h : â€–fâ€–â‚Š â‰¤ C) (m : Î¹ â†’ E) : â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š :=
  f.1.le_of_opNNNorm_le h m

theorem opNNNorm_le_iff {C : â„â‰¥0} : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ m, â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š :=
  f.1.opNNNorm_le_iff

theorem isLeast_opNNNorm (f : E [â‹€^Î¹]â†’L[ğ•œ] F) :
    IsLeast {C : â„â‰¥0 | âˆ€ m, â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š} â€–fâ€–â‚Š :=
  f.1.isLeast_opNNNorm

theorem opNNNorm_prod (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (g : E [â‹€^Î¹]â†’L[ğ•œ] G) :
    â€–f.prod gâ€–â‚Š = max (â€–fâ€–â‚Š) (â€–gâ€–â‚Š) :=
  f.1.opNNNorm_prod g.1

theorem opNorm_prod (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (g : E [â‹€^Î¹]â†’L[ğ•œ] G) : â€–f.prod gâ€– = max (â€–fâ€–) (â€–gâ€–) :=
  f.1.opNorm_prod g.1

theorem opNNNorm_pi {Î¹' : Type*} [Fintype Î¹'] {F : Î¹' â†’ Type*} [âˆ€ i', SeminormedAddCommGroup (F i')]
    [âˆ€ i', NormedSpace ğ•œ (F i')] (f : âˆ€ i', E [â‹€^Î¹]â†’L[ğ•œ] F i') : â€–pi fâ€–â‚Š = â€–fâ€–â‚Š :=
  ContinuousMultilinearMap.opNNNorm_pi fun i â†¦ (f i).1

theorem opNorm_pi {Î¹' : Type*} [Fintype Î¹'] {F : Î¹' â†’ Type*} [âˆ€ i', SeminormedAddCommGroup (F i')]
    [âˆ€ i', NormedSpace ğ•œ (F i')] (f : âˆ€ i', E [â‹€^Î¹]â†’L[ğ•œ] F i') : â€–pi fâ€– = â€–fâ€– :=
  ContinuousMultilinearMap.opNorm_pi fun i â†¦ (f i).1

instance instNormedSpace {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass ğ•œ ğ•œ' F] :
    NormedSpace ğ•œ' (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  âŸ¨fun c f â†¦ f.1.opNorm_smul_le câŸ©

section

@[simp] theorem norm_ofSubsingleton [Subsingleton Î¹] (i : Î¹) (f : E â†’L[ğ•œ] F) :
    â€–ofSubsingleton ğ•œ E F i fâ€– = â€–fâ€– :=
  ContinuousMultilinearMap.norm_ofSubsingleton i f

@[simp] theorem nnnorm_ofSubsingleton [Subsingleton Î¹] (i : Î¹) (f : E â†’L[ğ•œ] F) :
    â€–ofSubsingleton ğ•œ E F i fâ€–â‚Š = â€–fâ€–â‚Š :=
  NNReal.eq <| norm_ofSubsingleton i f

/-- `ContinuousAlternatingMap.ofSubsingleton` as a linear isometry. -/
@[simps +simpRhs]
def ofSubsingletonLIE [Subsingleton Î¹] (i : Î¹) : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] F) where
  __ := ofSubsingleton ğ•œ E F i
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' := norm_ofSubsingleton i

theorem norm_ofSubsingleton_id_le [Subsingleton Î¹] (i : Î¹) :
    â€–ofSubsingleton ğ•œ E E i (.id _ _)â€– â‰¤ 1 :=
  ContinuousMultilinearMap.norm_ofSubsingleton_id_le ..

theorem nnnorm_ofSubsingleton_id_le [Subsingleton Î¹] (i : Î¹) :
    â€–ofSubsingleton ğ•œ E E i (.id _ _)â€–â‚Š â‰¤ 1 :=
  ContinuousMultilinearMap.nnnorm_ofSubsingleton_id_le ..

variable (ğ•œ E)

@[simp] theorem norm_constOfIsEmpty [IsEmpty Î¹] (x : F) : â€–constOfIsEmpty ğ•œ E Î¹ xâ€– = â€–xâ€– :=
  ContinuousMultilinearMap.norm_constOfIsEmpty _ _ _

@[simp] theorem nnnorm_constOfIsEmpty [IsEmpty Î¹] (x : F) : â€–constOfIsEmpty ğ•œ E Î¹ xâ€–â‚Š = â€–xâ€–â‚Š :=
  NNReal.eq <| norm_constOfIsEmpty _ _ _

variable (Î¹ F) in
/-- `constOfIsEmpty` as a linear isometry equivalence. -/
@[simps]
def constOfIsEmptyLIE [IsEmpty Î¹] : F â‰ƒâ‚—áµ¢[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] F) where
  toFun := constOfIsEmpty _ _ _
  invFun f := f 0
  left_inv x := by simp
  right_inv f := by ext x; simp [Subsingleton.allEq x 0]
  map_add' f g := rfl
  map_smul' c f := rfl
  norm_map' := norm_constOfIsEmpty _ _

end

variable (ğ•œ E F G) in
/-- `ContinuousAlternatingMap.prod` as a `LinearIsometryEquiv`. -/
@[simps]
def prodLIE : (E [â‹€^Î¹]â†’L[ğ•œ] F) Ã— (E [â‹€^Î¹]â†’L[ğ•œ] G) â‰ƒâ‚—áµ¢[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] (F Ã— G)) where
  toFun f := f.1.prod f.2
  invFun f := ((ContinuousLinearMap.fst ğ•œ F G).compContinuousAlternatingMap f,
    (ContinuousLinearMap.snd ğ•œ F G).compContinuousAlternatingMap f)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' f := opNorm_prod f.1 f.2

variable (ğ•œ E) in
/-- `ContinuousAlternatingMap.pi` as a `LinearIsometryEquiv`. -/
@[simps!]
def piLIE {Î¹' : Type*} [Fintype Î¹'] {F : Î¹' â†’ Type*} [âˆ€ i', SeminormedAddCommGroup (F i')]
    [âˆ€ i', NormedSpace ğ•œ (F i')] :
    (âˆ€ i', E [â‹€^Î¹]â†’L[ğ•œ] F i') â‰ƒâ‚—áµ¢[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] (âˆ€ i, F i)) where
  toLinearEquiv := piLinearEquiv
  norm_map' := opNorm_pi

section restrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
variable [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ' ğ•œ F]
variable [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E]

@[simp] theorem norm_restrictScalars : â€–f.restrictScalars ğ•œ'â€– = â€–fâ€– := rfl

variable (ğ•œ')

/-- `ContinuousAlternatingMap.restrictScalars` as a `LinearIsometry`. -/
@[simps]
def restrictScalarsLI : E [â‹€^Î¹]â†’L[ğ•œ] F â†’â‚—áµ¢[ğ•œ'] E [â‹€^Î¹]â†’L[ğ•œ'] F where
  toFun := restrictScalars ğ•œ'
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' _ := rfl

variable {ğ•œ'}

end restrictScalars

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, precise version.
For a less precise but more usable version, see `norm_image_sub_le`. The bound reads
`â€–f m - f m'â€– â‰¤
  â€–fâ€– * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
theorem norm_image_sub_le' [DecidableEq Î¹] (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
  f.1.norm_image_sub_le' mâ‚ mâ‚‚

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`,
less precise version.
For a more precise but less usable version, see `norm_image_sub_le'`.
The bound is `â€–f m - f m'â€– â‰¤ â€–fâ€– * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
theorem norm_image_sub_le (f : E [â‹€^Î¹]â†’L[ğ•œ] F) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * (Fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
  f.1.norm_image_sub_le mâ‚ mâ‚‚

end ContinuousAlternatingMap

variable [Fintype Î¹]

/-- If a continuous alternating map is constructed from a alternating map via the constructor
`mkContinuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
theorem AlternatingMap.mkContinuous_norm_le (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ C :=
  f.toMultilinearMap.mkContinuous_norm_le hC H

/-- If a continuous alternating map is constructed from a alternating map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
theorem AlternatingMap.mkContinuous_norm_le' (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] F) {C : â„}
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ max C 0 :=
  ContinuousMultilinearMap.opNorm_le_bound (le_max_right _ _) fun m â†¦ (H m).trans <| by
    gcongr
    apply le_max_left

namespace ContinuousLinearMap

theorem norm_compContinuousAlternatingMap_le (g : F â†’L[ğ•œ] G) (f : E [â‹€^Î¹]â†’L[ğ•œ] F) :
    â€–g.compContinuousAlternatingMap fâ€– â‰¤ â€–gâ€– * â€–fâ€– :=
  g.norm_compContinuousMultilinearMap_le f.1

variable (ğ•œ E F G) in
/-- `ContinuousLinearMap.compContinuousAlternatingMap` as a bundled continuous bilinear map. -/
@[simps! apply_apply]
def compContinuousAlternatingMapCLM : (F â†’L[ğ•œ] G) â†’L[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] F) â†’L[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] G) :=
  LinearMap.mkContinuousâ‚‚ (compContinuousAlternatingMapâ‚— ğ•œ E F G) 1 fun f g â†¦ by
    simpa using f.norm_compContinuousAlternatingMap_le g

/-- `ContinuousLinearMap.compContinuousAlternatingMap` as a bundled continuous linear equiv. -/
@[simps +simpRhs apply]
def _root_.ContinuousLinearEquiv.continuousAlternatingMapCongrRight (g : F â‰ƒL[ğ•œ] G) :
    (E [â‹€^Î¹]â†’L[ğ•œ] F) â‰ƒL[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] G) where
  __ := g.continuousAlternatingMapCongrRightEquiv
  __ := compContinuousAlternatingMapCLM ğ•œ E F G g.toContinuousLinearMap
  continuous_toFun :=
    (compContinuousAlternatingMapCLM ğ•œ E F G g.toContinuousLinearMap).continuous
  continuous_invFun :=
    (compContinuousAlternatingMapCLM ğ•œ E G F g.symm.toContinuousLinearMap).continuous

@[simp]
theorem _root_.ContinuousLinearEquiv.continuousAlternatingMapCongrRight_symm (g : F â‰ƒL[ğ•œ] G) :
    (g.continuousAlternatingMapCongrRight (Î¹ := Î¹) (E := E)).symm =
      g.symm.continuousAlternatingMapCongrRight :=
  rfl

/-- Flip arguments in `f : F â†’L[ğ•œ] E [â‹€^Î¹]â†’L[ğ•œ] G` to get `â‹€^Î¹âŸ®ğ•œ; E; F â†’L[ğ•œ] GâŸ¯` -/
@[simps! apply_apply]
def flipAlternating (f : F â†’L[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] G)) : E [â‹€^Î¹]â†’L[ğ•œ] (F â†’L[ğ•œ] G) where
  toContinuousMultilinearMap :=
    ((ContinuousAlternatingMap.toContinuousMultilinearMapCLM ğ•œ).comp f).flipMultilinear
  map_eq_zero_of_eq' v i j hv hne := by ext x; simp [(f x).map_eq_zero_of_eq v hv hne]

end ContinuousLinearMap

theorem LinearIsometry.norm_compContinuousAlternatingMap (g : F â†’â‚—áµ¢[ğ•œ] G) (f : E [â‹€^Î¹]â†’L[ğ•œ] F) :
    â€–g.toContinuousLinearMap.compContinuousAlternatingMap fâ€– = â€–fâ€– :=
  g.norm_compContinuousMultilinearMap f.1

open ContinuousAlternatingMap

section

theorem ContinuousAlternatingMap.norm_compContinuousLinearMap_le (f : F [â‹€^Î¹]â†’L[ğ•œ] G)
    (g : E â†’L[ğ•œ] F) : â€–f.compContinuousLinearMap gâ€– â‰¤ â€–fâ€– * (â€–gâ€– ^ Fintype.card Î¹) :=
  (f.1.norm_compContinuousLinearMap_le _).trans_eq <| by simp

/-- Composition of a continuous alternating map and a continuous linear map
as a bundled continuous linear map. -/
def ContinuousAlternatingMap.compContinuousLinearMapCLM (f : E â†’L[ğ•œ] F) :
    (F [â‹€^Î¹]â†’L[ğ•œ] G) â†’L[ğ•œ] (E [â‹€^Î¹]â†’L[ğ•œ] G) :=
  LinearMap.mkContinuous
    (ContinuousAlternatingMap.compContinuousLinearMapâ‚— f) (â€–fâ€– ^ Fintype.card Î¹) fun g â†¦
      (g.norm_compContinuousLinearMap_le f).trans_eq (mul_comm _ _)

/-- Given a continuous linear isomorphism between the domains,
generate a continuous linear isomorphism between the spaces of continuous alternating maps.

This is `ContinuousAlternatingMap.compContinuousLinearMap` as an equivalence,
and is the continuous version of `AlternatingMap.domLCongr`. -/
@[simps apply]
def ContinuousLinearEquiv.continuousAlternatingMapCongrLeft (f : E â‰ƒL[ğ•œ] F) :
    E [â‹€^Î¹]â†’L[ğ•œ] G â‰ƒL[ğ•œ] (F [â‹€^Î¹]â†’L[ğ•œ] G) where
  __ := f.continuousAlternatingMapCongrLeftEquiv
  __ := ContinuousAlternatingMap.compContinuousLinearMapCLM (f.symm : F â†’L[ğ•œ] E)
  toFun g := g.compContinuousLinearMap (f.symm : F â†’L[ğ•œ] E)
  continuous_invFun :=
    (ContinuousAlternatingMap.compContinuousLinearMapCLM (f : E â†’L[ğ•œ] F)).cont
  continuous_toFun :=
    (ContinuousAlternatingMap.compContinuousLinearMapCLM (f.symm : F â†’L[ğ•œ] E)).cont

variable
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ğ•œ F']

/-- Continuous linear equivalences between the domains and the codomains
generate a continuous linear equivalence between the spaces of continuous alternating maps. -/
@[simps! apply]
def ContinuousLinearEquiv.continuousAlternatingMapCongr (e : E â‰ƒL[ğ•œ] E') (e' : F â‰ƒL[ğ•œ] F') :
    (E [â‹€^Î¹]â†’L[ğ•œ] F) â‰ƒL[ğ•œ] (E' [â‹€^Î¹]â†’L[ğ•œ] F') :=
  e.continuousAlternatingMapCongrLeft.trans <| e'.continuousAlternatingMapCongrRight

end

open ContinuousAlternatingMap

namespace AlternatingMap

/-- Given a map `f : F â†’â‚—[ğ•œ] E [â‹€^Î¹]â†’â‚—[ğ•œ] G` and an estimate
`H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–`, construct a continuous linear
map from `F` to `E [â‹€^Î¹]â†’L[ğ•œ] G`.

In order to lift, e.g., a map `f : (E [â‹€^Î¹]â†’â‚—[ğ•œ] F) â†’â‚—[ğ•œ] E' [â‹€^Î¹]â†’â‚—[ğ•œ] G`
to a map `(E [â‹€^Î¹]â†’L[ğ•œ] F) â†’L[ğ•œ] E' [â‹€^Î¹]â†’L[ğ•œ] G`,
one can apply this construction to `f.comp ContinuousAlternatingMap.toAlternatingMapLinear`
which is a linear map from `E [â‹€^Î¹]â†’L[ğ•œ] F` to `E' [â‹€^Î¹]â†’â‚—[ğ•œ] G`. -/
def mkContinuousLinear (f : F â†’â‚—[ğ•œ] E [â‹€^Î¹]â†’â‚—[ğ•œ] G) (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : F â†’L[ğ•œ] E [â‹€^Î¹]â†’L[ğ•œ] G :=
  LinearMap.mkContinuous
    { toFun x :=  (f x).mkContinuous (C * â€–xâ€–) <| H x
      map_add' x y := by ext1; simp
      map_smul' c x := by ext1; simp }
    (max C 0) fun x â†¦ by
      rw [LinearMap.coe_mk, AddHom.coe_mk]
      exact (mkContinuous_norm_le' _ _).trans_eq <| by
        rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

theorem mkContinuousLinear_norm_le_max (f : F â†’â‚—[ğ•œ] E [â‹€^Î¹]â†’â‚—[ğ•œ] G) (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ max C 0 :=
  LinearMap.mkContinuous_norm_le _ (le_max_right _ _) _

theorem mkContinuousLinear_norm_le (f : F â†’â‚—[ğ•œ] E [â‹€^Î¹]â†’â‚—[ğ•œ] G) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ C :=
  (mkContinuousLinear_norm_le_max f C H).trans_eq (max_eq_left hC)

variable {Î¹' : Type*} [Fintype Î¹']

/-- Given a map `f : E [â‹€^Î¹]â†’â‚—[ğ•œ] (F [â‹€^Î¹']â†’â‚—[ğ•œ] G)` and an estimate
`H : âˆ€ m m', â€–f m m'â€– â‰¤ C * âˆ i, â€–m iâ€– * âˆ i, â€–m' iâ€–`, upgrade all `AlternatingMap`s in the type
to `ContinuousAlternatingMap`s. -/
def mkContinuousAlternating (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] (F [â‹€^Î¹']â†’â‚—[ğ•œ] G))
    (C : â„) (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    E [â‹€^Î¹]â†’L[ğ•œ] (F [â‹€^Î¹']â†’L[ğ•œ] G)  :=
  mkContinuous
    { toFun m := mkContinuous (f m) (C * âˆ i, â€–m iâ€–) <| H m
      map_update_add' m i x y := by ext1; simp
      map_update_smul' m i c x := by ext1; simp
      map_eq_zero_of_eq' v i j hv hij := by
        ext v'
        have : f v = 0 := by simpa using f.map_eq_zero_of_eq' v i j hv hij
        simp [this] }
    (max C 0) fun m => by
      simp only [coe_mk, MultilinearMap.coe_mk]
      refine ((f m).mkContinuous_norm_le' _).trans_eq ?_
      rw [max_mul_of_nonneg, zero_mul]
      positivity

@[simp]
theorem mkContinuousAlternating_apply (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] (F [â‹€^Î¹']â†’â‚—[ğ•œ] G)) {C : â„}
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) (m : Î¹ â†’ E) :
    â‡‘(mkContinuousAlternating f C H m) = f m :=
  rfl

theorem mkContinuousAlternating_norm_le_max (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] (F [â‹€^Î¹']â†’â‚—[ğ•œ] G)) {C : â„}
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousAlternating f C Hâ€– â‰¤ max C 0 := by
  dsimp only [mkContinuousAlternating]
  exact mkContinuous_norm_le _ (le_max_right _ _) _

theorem mkContinuousAlternating_norm_le (f : E [â‹€^Î¹]â†’â‚—[ğ•œ] (F [â‹€^Î¹']â†’â‚—[ğ•œ] G)) {C : â„}
    (hC : 0 â‰¤ C) (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousAlternating f C Hâ€– â‰¤ C :=
  (mkContinuousAlternating_norm_le_max f H).trans_eq (max_eq_left hC)

end AlternatingMap

end Seminorm

section Norm

/-! Results that are only true if the target space is a `NormedAddCommGroup`
(and not just a `SeminormedAddCommGroup`). -/

universe u wE wF v
variable {ğ•œ : Type u} {n : â„•} {E : Type wE} {F : Type wF} {Î¹ : Type v}
  [Fintype Î¹]
  [NontriviallyNormedField ğ•œ]
  [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace ContinuousAlternatingMap

/-- Continuous alternating maps themselves form a normed group with respect to the operator norm. -/
instance instNormedAddCommGroup : NormedAddCommGroup (E [â‹€^Î¹]â†’L[ğ•œ] F) :=
  NormedAddCommGroup.ofSeparation fun _f hf â†¦
    toContinuousMultilinearMap_injective <| norm_eq_zero.mp hf

variable (ğ•œ F) in
theorem norm_ofSubsingleton_id [Subsingleton Î¹] [Nontrivial F] (i : Î¹) :
    â€–ofSubsingleton ğ•œ F F i (.id _ _)â€– = 1 :=
  ContinuousMultilinearMap.norm_ofSubsingleton_id ğ•œ F i

variable (ğ•œ F) in
theorem nnnorm_ofSubsingleton_id [Subsingleton Î¹] [Nontrivial F] (i : Î¹) :
    â€–ofSubsingleton ğ•œ F F i (.id _ _)â€–â‚Š = 1 :=
  NNReal.eq <| norm_ofSubsingleton_id ..

end ContinuousAlternatingMap

namespace AlternatingMap

/-- If an alternating map in finitely many variables on a normed space satisfies the inequality
`â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ i / â€–c iâ€– < â€–m iâ€– < Îµ i` for some positive numbers `Îµ i`
and elements `c i : ğ•œ`, `1 < â€–c iâ€–`, then it satisfies this inequality for all `m`. -/
theorem bound_of_shell (f : F [â‹€^Î¹]â†’â‚—[ğ•œ] E) {Îµ : Î¹ â†’ â„} {C : â„} {c : Î¹ â†’ ğ•œ}
    (hÎµ : âˆ€ i, 0 < Îµ i) (hc : âˆ€ i, 1 < â€–c iâ€–)
    (hf : âˆ€ m : Î¹ â†’ F, (âˆ€ i, Îµ i / â€–c iâ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ i) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : Î¹ â†’ F) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.bound_of_shell hÎµ hc hf m

end AlternatingMap

end Norm
