/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.Alternating.Curry
import Mathlib.LinearAlgebra.Alternating.Uncurry.Fin

/-!
# Uncurrying continuous alternating maps

Given a continuous function `f` which is linear in the first argument
and is alternating form in the other `n` arguments,
this file defines a continuous alternating form `ContinuousAlternatingMap.uncurryFin f`
in `n + 1` arguments.

This function is given by
```
ContinuousAlternatingMap.uncurryFin f v =
  âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v)
```

Given a continuous alternating map `f` of `n + 1` arguments,
each term in the sum above written for `f.curryLeft` equals the original map,
thus `f.curryLeft.uncurryFin = (n + 1) â€¢ f`.

We do not multiply the result of `uncurryFin` by `(n + 1)â»Â¹`
so that the construction works for `ð•œ`-multilinear maps over any normed field `ð•œ`,
not only a field of characteristic zero.

## Main results

- `ContinuousAlternatingMap.uncurryFin_curryLeft`:
  the round-trip formula for currying/uncurrying, see above.

- `ContinuousAlternatingMap.uncurryFin_uncurryFinLM_comp_of_symmetric`:
  If `f` is a symmetric bilinear map taking values in the space of continuous alternating maps,
  then the twice uncurried `f` is zero.

The latter theorem will be used
to prove that the second exterior derivative of a differential form is zero.
-/

open Fin Function

namespace ContinuousAlternatingMap

variable {ð•œ E F : Type*} [NontriviallyNormedField ð•œ]
  [NormedAddCommGroup E] [NormedSpace ð•œ E]
  [NormedAddCommGroup F] [NormedSpace ð•œ F]
  {n : â„•}

/-- If `f` is a continuous `(n + 1)`-multilinear alternating map, `x` is an element of the domain,
and `v` is an `n`-vector, then the value of `f` at `v` with `x` inserted at the `p`th place
equals `(-1) ^ p` times the value of `f` at `v` with `x` prepended. -/
theorem map_insertNth (f : E [â‹€^Fin (n + 1)]â†’L[ð•œ] F) (p : Fin (n + 1)) (x : E) (v : Fin n â†’ E) :
    f (p.insertNth x v) = (-1) ^ (p : â„•) â€¢ f (Matrix.vecCons x v) :=
  f.toAlternatingMap.map_insertNth p x v

theorem neg_one_pow_smul_map_insertNth (f : E [â‹€^Fin (n + 1)]â†’L[ð•œ] F) (p : Fin (n + 1)) (x : E)
    (v : Fin n â†’ E) : (-1) ^ (p : â„•) â€¢ f (p.insertNth x v) = f (Matrix.vecCons x v) :=
  f.toAlternatingMap.neg_one_pow_smul_map_insertNth p x v

/-- Let `v` be an `(n + 1)`-tuple with two equal elements `v i = v j`, `i â‰  j`.
Let `w i` (resp., `w j`) be the vector `v` with `i`th (resp., `j`th) element removed.
Then `(-1) ^ i â€¢ f (w i) + (-1) ^ j â€¢ f (w j) = 0`.
This follows from the fact that these two vectors differ by a permutation of sign `(-1) ^ (i + j)`.

These are the only two nonzero terms in the proof of `map_eq_zero_of_eq`
in the definition of `AlternatingMap.uncurryFin`. -/
theorem neg_one_pow_smul_map_removeNth_add_eq_zero_of_eq (f : E [â‹€^Fin n]â†’L[ð•œ] F)
    {v : Fin (n + 1) â†’ E} {i j : Fin (n + 1)} (hvij : v i = v j) (hij : i â‰  j) :
    (-1) ^ (i : â„•) â€¢ f (i.removeNth v) + (-1) ^ (j : â„•) â€¢ f (j.removeNth v) = 0 :=
  f.toAlternatingMap.neg_one_pow_smul_map_removeNth_add_eq_zero_of_eq hvij hij

private def uncurryFinCLM.aux : (E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) â†’â‚—[ð•œ] E [â‹€^Fin (n + 1)]â†’â‚—[ð•œ] F :=
  AlternatingMap.uncurryFinLM âˆ˜â‚— (toAlternatingMapLinear (R := ð•œ)).compRight (S := ð•œ) âˆ˜â‚—
    ContinuousLinearMap.coeLM ð•œ

private lemma uncurryFinCLM.aux_apply (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) (v : Fin (n + 1) â†’ E) :
    aux f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (i.removeNth v) := by
  simp [aux, AlternatingMap.uncurryFin_apply]

variable (ð•œ E F) in
/-- `AlternaringMap.uncurryFin` as a linear map. -/
@[irreducible]
noncomputable def uncurryFinCLM : (E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) â†’L[ð•œ] E [â‹€^Fin (n + 1)]â†’L[ð•œ] F :=
  AlternatingMap.mkContinuousLinear uncurryFinCLM.aux (n + 1) fun f v â†¦ calc
    â€–uncurryFinCLM.aux f vâ€– â‰¤ âˆ‘ i : Fin (n + 1), â€–fâ€– * âˆ i, â€–v iâ€– := by
      rw [uncurryFinCLM.aux_apply]
      refine norm_sum_le_of_le _ fun i hi â†¦ ?_
      rw [norm_isUnit_zsmul _ (.pow _ isUnit_one.neg), i.prod_univ_succAbove, â† mul_assoc]
      apply (f (v i)).le_of_opNorm_le
      apply f.le_opNorm
    _ = (n + 1) * â€–fâ€– * âˆ i, â€–v iâ€– := by simp [mul_assoc]

lemma norm_uncurryFinCLM_le : â€–uncurryFinCLM (n := n) ð•œ E Fâ€– â‰¤ n + 1 := by
  rw [uncurryFinCLM]
  apply AlternatingMap.mkContinuousLinear_norm_le
  positivity

/-- Given a continuous function which is linear in the first argument
and is alternating in the other `n` arguments,
build a continuous alternating form in `n + 1` arguments.

The function is given by
```
uncurryFin f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v)
```

Note that the round-trip with `curryLeft` multiplies the form by `n + 1`,
since we want to avoid division in this definition. -/
noncomputable def uncurryFin (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) : E [â‹€^Fin (n + 1)]â†’L[ð•œ] F :=
  uncurryFinCLM ð•œ E F f

@[simp]
lemma uncurryFinCLM_apply (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) : uncurryFinCLM ð•œ E F f = uncurryFin f :=
  rfl

lemma norm_uncurryFin_le (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) : â€–uncurryFin fâ€– â‰¤ (n + 1) * â€–fâ€– :=
  (uncurryFinCLM ð•œ E F).le_of_opNorm_le norm_uncurryFinCLM_le f

theorem uncurryFin_apply (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) (v : Fin (n + 1) â†’ E) :
    uncurryFin f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v) := by
  rw [uncurryFin, uncurryFinCLM]
  apply uncurryFinCLM.aux_apply

lemma toAlternatingMap_uncurryFin (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) :
    (uncurryFin f).toAlternatingMap =
      AlternatingMap.uncurryFin (toAlternatingMapLinear âˆ˜â‚— (f : E â†’â‚—[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F)) := by
  ext
  simp [uncurryFin_apply, AlternatingMap.uncurryFin_apply]

@[simp]
theorem uncurryFin_add (f g : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) :
    uncurryFin (f + g) = uncurryFin f + uncurryFin g :=
  map_add (uncurryFinCLM ð•œ E F) f g

@[simp]
lemma uncurryFin_curryLeft (f : E [â‹€^Fin (n + 1)]â†’L[ð•œ] F) :
    uncurryFin (curryLeft f) = (n + 1) â€¢ f := by
  ext v
  simp [uncurryFin_apply, â† map_insertNth]

@[simp]
theorem uncurryFin_smul {S : Type*} [Monoid S] [DistribMulAction S F] [ContinuousConstSMul S F]
    [SMulCommClass ð•œ S F] (c : S) (f : E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F) :
    uncurryFin (c â€¢ f) = c â€¢ uncurryFin f := by
  ext v
  simp [uncurryFin_apply, smul_comm _ c, Finset.smul_sum]

/-- If `f` is a symmetric continuous bilinear map
taking values in the space of continuous alternating maps,
then the twice uncurried `f` is zero. -/
theorem uncurryFin_uncurryFinCLM_comp_of_symmetric {f : E â†’L[ð•œ] E â†’L[ð•œ] E [â‹€^Fin n]â†’L[ð•œ] F}
    (hf : âˆ€ x y, f x y = f y x) :
    uncurryFin (uncurryFinCLM ð•œ E F âˆ˜L f) = 0 := by
  apply toAlternatingMap_injective
  rw [toAlternatingMap_zero, â† AlternatingMap.uncurryFin_uncurryFinLM_comp_of_symmetric
    (f := f.toLinearMapâ‚â‚‚.comprâ‚‚ toAlternatingMapLinear)
    (fun x y â†¦ congr($(hf x y) |>.toAlternatingMap))]
  rw [toAlternatingMap_uncurryFin]
  congr 1
  ext
  simp [uncurryFin_apply, AlternatingMap.uncurryFin_apply]

end ContinuousAlternatingMap
