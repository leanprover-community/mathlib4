/-
Copyright (c) 2025 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.Alternating.Curry
import Mathlib.LinearAlgebra.Alternating.Uncurry.Fin

/-!
# Uncurrying continuous alternating maps

Given a continuous function `f` which is linear in the first argument
and is alternating form in the other `n` arguments,
this file defines a continuous alternating form `ContinuousAlternatingMap.alternatizeUncurryFin f`
in `n + 1` arguments.

This function is given by
```
ContinuousAlternatingMap.alternatizeUncurryFin f v =
  âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v)
```

Given a continuous alternating map `f` of `n + 1` arguments,
each term in the sum above written for `f.curryLeft` equals the original map,
thus `f.curryLeft.alternatizeUncurryFin = (n + 1) â€¢ f`.

We do not multiply the result of `alternatizeUncurryFin` by `(n + 1)â»Â¹`
so that the construction works for `ğ•œ`-multilinear maps over any normed field `ğ•œ`,
not only a field of characteristic zero.

## Main results

- `ContinuousAlternatingMap.alternatizeUncurryFin_curryLeft`:
  the round-trip formula for currying/uncurrying, see above.

- `ContinuousAlternatingMap.alternatizeUncurryFin_alternatizeUncurryFinLM_comp_of_symmetric`:
  If `f` is a symmetric bilinear map taking values in the space of continuous alternating maps,
  then the twice uncurried `f` is zero.

The latter theorem will be used
to prove that the second exterior derivative of a differential form is zero.
-/

open Fin Function

namespace ContinuousAlternatingMap

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  {n : â„•}

/-- If `f` is a continuous `(n + 1)`-multilinear alternating map, `x` is an element of the domain,
and `v` is an `n`-vector, then the value of `f` at `v` with `x` inserted at the `p`th place
equals `(-1) ^ p` times the value of `f` at `v` with `x` prepended. -/
theorem map_insertNth (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) (p : Fin (n + 1)) (x : E) (v : Fin n â†’ E) :
    f (p.insertNth x v) = (-1) ^ (p : â„•) â€¢ f (Matrix.vecCons x v) :=
  f.toAlternatingMap.map_insertNth p x v

theorem neg_one_pow_smul_map_insertNth (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) (p : Fin (n + 1)) (x : E)
    (v : Fin n â†’ E) : (-1) ^ (p : â„•) â€¢ f (p.insertNth x v) = f (Matrix.vecCons x v) :=
  f.toAlternatingMap.neg_one_pow_smul_map_insertNth p x v

/-- Let `v` be an `(n + 1)`-tuple with two equal elements `v i = v j`, `i â‰  j`.
Let `w i` (resp., `w j`) be the vector `v` with `i`th (resp., `j`th) element removed.
Then `(-1) ^ i â€¢ f (w i) + (-1) ^ j â€¢ f (w j) = 0`.
This follows from the fact that these two vectors differ by a permutation of sign `(-1) ^ (i + j)`.

These are the only two nonzero terms in the proof of `map_eq_zero_of_eq`
in the definition of `AlternatingMap.alternatizeUncurryFin`. -/
theorem neg_one_pow_smul_map_removeNth_add_eq_zero_of_eq (f : E [â‹€^Fin n]â†’L[ğ•œ] F)
    {v : Fin (n + 1) â†’ E} {i j : Fin (n + 1)} (hvij : v i = v j) (hij : i â‰  j) :
    (-1) ^ (i : â„•) â€¢ f (i.removeNth v) + (-1) ^ (j : â„•) â€¢ f (j.removeNth v) = 0 :=
  f.toAlternatingMap.neg_one_pow_smul_map_removeNth_add_eq_zero_of_eq hvij hij

private def alternatizeUncurryFinCLM.aux :
    (E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) â†’â‚—[ğ•œ] E [â‹€^Fin (n + 1)]â†’â‚—[ğ•œ] F :=
  AlternatingMap.alternatizeUncurryFinLM âˆ˜â‚— (toAlternatingMapLinear (R := ğ•œ)).compRight (S := ğ•œ) âˆ˜â‚—
    ContinuousLinearMap.coeLM ğ•œ

private lemma alternatizeUncurryFinCLM.aux_apply (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F)
    (v : Fin (n + 1) â†’ E) :
    aux f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (i.removeNth v) := by
  simp [aux, AlternatingMap.alternatizeUncurryFin_apply]

variable (ğ•œ E F) in
/-- `AlternaringMap.alternatizeUncurryFin` as a continuous linear map. -/
@[irreducible]
noncomputable def alternatizeUncurryFinCLM :
    (E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) â†’L[ğ•œ] E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  AlternatingMap.mkContinuousLinear alternatizeUncurryFinCLM.aux (n + 1) fun f v â†¦ calc
    â€–alternatizeUncurryFinCLM.aux f vâ€– â‰¤ âˆ‘ i : Fin (n + 1), â€–fâ€– * âˆ i, â€–v iâ€– := by
      rw [alternatizeUncurryFinCLM.aux_apply]
      refine norm_sum_le_of_le _ fun i hi â†¦ ?_
      rw [norm_isUnit_zsmul _ (.pow _ isUnit_one.neg), i.prod_univ_succAbove, â† mul_assoc]
      apply (f (v i)).le_of_opNorm_le
      apply f.le_opNorm
    _ = (n + 1) * â€–fâ€– * âˆ i, â€–v iâ€– := by simp [mul_assoc]

lemma norm_alternatizeUncurryFinCLM_le : â€–alternatizeUncurryFinCLM (n := n) ğ•œ E Fâ€– â‰¤ n + 1 := by
  rw [alternatizeUncurryFinCLM]
  apply AlternatingMap.mkContinuousLinear_norm_le
  positivity

/-- Given a continuous function which is linear in the first argument
and is alternating in the other `n` arguments,
build a continuous alternating form in `n + 1` arguments.

The function is given by
```
alternatizeUncurryFin f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v)
```

Note that the round-trip with `curryLeft` multiplies the form by `n + 1`,
since we want to avoid division in this definition. -/
noncomputable def alternatizeUncurryFin (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F :=
  alternatizeUncurryFinCLM ğ•œ E F f

@[simp]
lemma alternatizeUncurryFinCLM_apply (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    alternatizeUncurryFinCLM ğ•œ E F f = alternatizeUncurryFin f :=
  rfl

lemma norm_alternatizeUncurryFin_le (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    â€–alternatizeUncurryFin fâ€– â‰¤ (n + 1) * â€–fâ€– :=
  (alternatizeUncurryFinCLM ğ•œ E F).le_of_opNorm_le norm_alternatizeUncurryFinCLM_le f

theorem alternatizeUncurryFin_apply (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) (v : Fin (n + 1) â†’ E) :
    alternatizeUncurryFin f v = âˆ‘ i : Fin (n + 1), (-1) ^ (i : â„•) â€¢ f (v i) (removeNth i v) := by
  rw [alternatizeUncurryFin, alternatizeUncurryFinCLM]
  apply alternatizeUncurryFinCLM.aux_apply

lemma toAlternatingMap_alternatizeUncurryFin (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    (alternatizeUncurryFin f).toAlternatingMap =
      .alternatizeUncurryFin (toAlternatingMapLinear âˆ˜â‚— (f : E â†’â‚—[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F)) := by
  ext
  simp [alternatizeUncurryFin_apply, AlternatingMap.alternatizeUncurryFin_apply]

@[simp]
theorem alternatizeUncurryFin_add (f g : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    alternatizeUncurryFin (f + g) = alternatizeUncurryFin f + alternatizeUncurryFin g :=
  map_add (alternatizeUncurryFinCLM ğ•œ E F) f g

@[simp]
lemma alternatizeUncurryFin_curryLeft (f : E [â‹€^Fin (n + 1)]â†’L[ğ•œ] F) :
    alternatizeUncurryFin (curryLeft f) = (n + 1) â€¢ f := by
  ext v
  simp [alternatizeUncurryFin_apply, â† map_insertNth]

@[simp]
theorem alternatizeUncurryFin_smul {S : Type*} [Monoid S] [DistribMulAction S F]
    [ContinuousConstSMul S F] [SMulCommClass ğ•œ S F] (c : S) (f : E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) :
    alternatizeUncurryFin (c â€¢ f) = c â€¢ alternatizeUncurryFin f := by
  ext v
  simp [alternatizeUncurryFin_apply, smul_comm _ c, Finset.smul_sum]

theorem alternatizeUncurryFin_constOfIsEmptyLIE_comp (f : E â†’L[ğ•œ] F) :
    alternatizeUncurryFin (constOfIsEmptyLIE ğ•œ E F (Fin 0) âˆ˜L f) =
      ofSubsingleton _ _ _ (0 : Fin 1) f := by
  ext
  simp [alternatizeUncurryFin_apply]

@[simp]
lemma _root_.Fin.removeNth_fun_const {Î± : Type*} {n : â„•} (i : Fin (n + 1)) (a : Î±) :
    i.removeNth (fun _ â†¦ a) = (fun _ â†¦ a) :=
  rfl

/-- If `f` is a continuous bilinear map taking values in the space of continuous alternating maps,
then evaluation of the twice uncurried `f` on a tuple of vectors `v`
can be represented as a sum of

$$
f(v_i, v_j; v_0, \dots, \hat{v_i}, \dots, \hat{v_j}-) -
f(v_j, v_i; v_0, \dots, \hat{v_i}, \dots, \hat{v_j}-)
$$

over all `(i j : Fin (n + 2))`, `i < j`, taken with appropriate signs.
Here $$\hat{v_i}$$ and $$\hat{v_j}$$ mean that these vectors are removed from the tuple.

We use pairs of `i j : Fin (n + 1)`, `i â‰¤ j`,
to encode pairs `(i.castSucc : Fin (n + 2), j.succ : Fin (n + 2))`,
so the power of `-1` is off by one compared to the informal texts.

In particular, if `f` is symmetric in the first two arguments,
then the resulting alternating map is zero,
see `alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_of_symmetric` below.
-/
theorem alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_apply
    (f : E â†’L[ğ•œ] E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F) (v : Fin (n + 2) â†’ E) :
    alternatizeUncurryFin (alternatizeUncurryFinCLM ğ•œ E F âˆ˜L f) v =
      âˆ‘ (i : Fin (n + 1)), âˆ‘ j â‰¥ i,
        (-1 : â„¤) ^ (i + j : â„•) â€¢
          (f (v i.castSucc) (v j.succ) (j.removeNth <| i.castSucc.removeNth v) -
            f (v j.succ) (v i.castSucc) (j.removeNth <| i.castSucc.removeNth v)) := by
  simpa [alternatizeUncurryFin_apply, AlternatingMap.alternatizeUncurryFin_apply]
    using AlternatingMap.alternatizeUncurryFin_alternatizeUncurryFinLM_comp_apply
      (R := ğ•œ) (M := E) (N := F)
      (f.toLinearMapâ‚â‚‚.comprâ‚‚ (toAlternatingMapLinear (R := ğ•œ))) v

/-- If `f` is a symmetric continuous bilinear map
taking values in the space of continuous alternating maps,
then the twice uncurried `f` is zero. -/
theorem alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_of_symmetric
    {f : E â†’L[ğ•œ] E â†’L[ğ•œ] E [â‹€^Fin n]â†’L[ğ•œ] F}
    (hf : âˆ€ x y, f x y = f y x) :
    alternatizeUncurryFin (alternatizeUncurryFinCLM ğ•œ E F âˆ˜L f) = 0 := by
  ext v
  simp [alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_apply, hf]

/-- Derivative of `compContinuousLinearMap` can be represented
in terms of `alternatizeUncurryFinCLM`. -/
theorem fderivCompContinuousLinearMap_eq_alternatizeUncurryFin (f : F [â‹€^Fin (n + 1)]â†’L[ğ•œ] G)
    (g : E â†’L[ğ•œ] F) :
    f.fderivCompContinuousLinearMap g = alternatizeUncurryFinCLM ğ•œ E G âˆ˜L
      ((compContinuousLinearMapCLM g âˆ˜L f.curryLeft).postcomp E) := by
  ext dg v
  have (i j : Fin (n + 1)) :
      i.insertNth (Î± := fun _ â†¦ E â†’L[ğ•œ] F) dg (fun _ â†¦ g) j (v j) =
        i.insertNth (Î± := fun _ â†¦ F) (dg (v i)) (g âˆ˜ i.removeNth v) j := by
    cases j using i.succAboveCases <;> simp [Fin.removeNth]
  simp [alternatizeUncurryFin_apply, â† Fin.insertNth_removeNth, â† map_insertNth, this]

/-- Alternatized uncurry of `fderivCompContinuousLinearMap f g`
composed with a symmetric bilinear map is zero. -/
theorem alternatizeUncurryFin_fderivCompContinuousLinearMap_eq_zero (f : F [â‹€^Fin n]â†’L[ğ•œ] G)
    (g : E â†’L[ğ•œ] F) {h : E â†’L[ğ•œ] E â†’L[ğ•œ] F} (hsymm : âˆ€ x y, h x y = h y x) :
    alternatizeUncurryFin (f.fderivCompContinuousLinearMap g âˆ˜L h) = 0 := by
  cases n with
  | zero =>
    simp [fderivCompContinuousLinearMap_of_isEmpty, â† alternatizeUncurryFinCLM_apply]
  | succ n =>
    rw [fderivCompContinuousLinearMap_eq_alternatizeUncurryFin,
      ContinuousLinearMap.comp_assoc,
      alternatizeUncurryFin_alternatizeUncurryFinCLM_comp_of_symmetric]
    intro x y
    ext v
    simp [hsymm]

end ContinuousAlternatingMap
