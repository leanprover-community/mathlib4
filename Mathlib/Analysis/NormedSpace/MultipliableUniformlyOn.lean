/-
Copyright (c) 2025 Chris Birkbeck. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Chris Birkbeck
-/
import Mathlib.Algebra.Order.Ring.Star
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.NormedSpace.FunctionSeries
import Mathlib.Analysis.SpecialFunctions.Log.Summable
import Mathlib.Topology.Algebra.InfiniteSum.UniformOn

/-!
# Uniform convergence of products of functions

We gather some results about the uniform convergence of infinite products, in particular those of
the form `âˆ' i, (1 + f i x)` for a sequence `f` of complex valued functions.
-/

open Filter Function Complex Finset Topology

variable {Î± Î² Î¹ : Type*} [UniformSpace Î²] [AddGroup Î²] [IsUniformAddGroup Î²] [LinearOrder Î²]
  [OrderTopology Î²] [AddLeftMono Î²] [AddRightMono Î²]
section cexp_clog

/- These next three results feel like they should go elsewhere, but find_home says to leave them
here. -/
lemma TendstoUniformlyOn.eventually_forall_lt {f : Î¹ â†’ Î± â†’ Î²} {p : Filter Î¹} {g : Î± â†’ Î²}
    {K : Set Î±} {u v : Î²} (huv : u < v) (hf : TendstoUniformlyOn f g p K) (hg : âˆ€ x âˆˆ K, g x â‰¤ u) :
    âˆ€á¶  i in p, âˆ€ x âˆˆ K, f i x < v := by
  simp only [tendstoUniformlyOn_iff_tendsto, uniformity_eq_comap_neg_add_nhds_zero,
    tendsto_iff_eventually, eventually_comap, Prod.forall] at *
  conv at hf => enter [2]; rw [eventually_iff_exists_mem]
  have hf2 := hf (fun x â†¦ -x.1 + x.2 < -u + v) âŸ¨_, (isOpen_gt' (-u + v)).mem_nhds (by simp [huv]),
    fun y hy a b hab => (hab.symm â–¸ hy :)âŸ©
  filter_upwards [eventually_prod_principal_iff.mp hf2] with i hi x hx
  simpa using add_lt_add_of_le_of_lt (hg x hx) (hi x hx)

lemma TendstoUniformlyOn.eventually_forall_le {f : Î¹ â†’ Î± â†’ Î²} {p : Filter Î¹} {g : Î± â†’ Î²}
    {K : Set Î±} {u v : Î²} (huv : u < v) (hf : TendstoUniformlyOn f g p K) (hg : âˆ€ x âˆˆ K, g x â‰¤ u) :
    âˆ€á¶  i in p, âˆ€ x âˆˆ K, f i x â‰¤ v := by
  filter_upwards [hf.eventually_forall_lt huv hg] with i hi x hx using (hi x hx).le

lemma TendstoUniformlyOn.comp_cexp {p : Filter Î¹} {f : Î¹ â†’ Î± â†’ â„‚} {g : Î± â†’ â„‚}
    {K : Set Î±} (hf : TendstoUniformlyOn f g p K) (hg : BddAbove <| (fun x â†¦ (g x).re) '' K) :
    TendstoUniformlyOn (cexp âˆ˜ f Â·) (cexp âˆ˜ g) p K := by
  obtain âŸ¨v, hvâŸ© : âˆƒ v, âˆ€ x âˆˆ K, (g x).re â‰¤ v := hg.imp fun _ h â†¦ by simpa [mem_upperBounds] using h
  have : âˆ€á¶  i in p, âˆ€ x âˆˆ K, (f i x).re â‰¤ v + 1 := hf.re.eventually_forall_le (lt_add_one v) hv
  refine (UniformContinuousOn.cexp _).comp_tendstoUniformlyOn_eventually (by simpa) ?_ hf
  simpa using fun a ha â†¦ (hv a ha).trans (lt_add_one v).le

lemma Complex.HasSumUniformlyOn_log_one_add {Î± : Type*} {f : Î¹ â†’ Î± â†’ â„‚} (K : Set Î±)
    {u : Î¹ â†’ â„} (hu : Summable u) (h : âˆ€á¶  n in cofinite, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n) :
    HasSumUniformlyOn (fun i a â†¦ log (1 + f i a)) (fun a â†¦ âˆ‘' i, log (1 + f i a)) {K} := by
  simp only [hasSumUniformlyOn_iff_tendstoUniformlyOn, Set.mem_singleton_iff, forall_eq]
  apply tendstoUniformlyOn_tsum_of_cofinite_eventually <| hu.mul_left (3 / 2)
  filter_upwards [h, hu.tendsto_cofinite_zero.eventually_le_const one_half_pos] with i hi hn' x hx
    using (norm_log_one_add_half_le_self <| (hi x hx).trans hn').trans (by simpa using hi x hx)

lemma Complex.tendstoUniformlyOn_tsum_nat_log_one_add {Î± : Type*} {f : â„• â†’ Î± â†’ â„‚} (K : Set Î±)
    {u : â„• â†’ â„} (hu : Summable u) (h : âˆ€á¶  n in atTop, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n) :
    TendstoUniformlyOn (fun n a => âˆ‘ i âˆˆ Finset.range n,
    (Complex.log (1 + f i a))) (fun a => âˆ‘' i : â„•, Complex.log (1 + f i a)) atTop K := by
  rw [â† Nat.cofinite_eq_atTop] at h
  simpa only [Set.mem_singleton_iff, forall_eq] using
    hasSumUniformlyOn_tendstoUniformlyOn_nat (Complex.HasSumUniformlyOn_log_one_add K hu h)

end cexp_clog

section UniformlyOn

lemma hasProdUniformlyOn_of_clog {f : Î¹ â†’ Î± â†’ â„‚} {ğ”– : Set (Set Î±)}
    (hf : SummableUniformlyOn (fun i a => log (f i a)) ğ”–) (hfn : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆ€ i, f i x â‰  0)
    (hg : âˆ€ K âˆˆ ğ”–, BddAbove <| (fun x => (âˆ‘' n : Î¹, log (f n x)).re) '' K) :
    HasProdUniformlyOn f (fun a => âˆ' i, f i a) ğ”– := by
  simp only [hasProdUniformlyOn_iff_tendstoUniformlyOn, Set.mem_singleton_iff, forall_eq]
  obtain âŸ¨r, hrâŸ© := hf.exists
  intro K hK
  suffices H : TendstoUniformlyOn (fun s a => âˆ i âˆˆ s, f i a) (cexp âˆ˜ r) atTop K by
    apply TendstoUniformlyOn.congr_right H
    apply Set.EqOn.trans (Set.EqOn.comp_left (hr.tsum_eqOn hK)).symm
    exact fun x hx â†¦ (cexp_tsum_eq_tprod (hfn K hK x hx) (hf.summable hK hx))
  have h1 := hr.tsum_eqOn hK
  simp only [hasSumUniformlyOn_iff_tendstoUniformlyOn, Set.mem_singleton_iff, forall_eq] at hr
  apply TendstoUniformlyOn.congr ((hr K hK).comp_cexp ?_)
  Â· filter_upwards with s i hi using by simp [exp_sum, fun y â†¦ exp_log (hfn K hK i hi y)]
  Â· convert hg K hK
    simp_all only [h1 _, ne_eq]

lemma multipliableUniformlyOn_of_clog {f : Î¹ â†’ Î± â†’ â„‚} {ğ”– : Set (Set Î±)}
    (hf : SummableUniformlyOn (fun i a => log (f i a)) ğ”–) (hfn : âˆ€ K âˆˆ ğ”–, âˆ€ x âˆˆ K, âˆ€ i, f i x â‰  0)
    (hg : âˆ€ K âˆˆ ğ”–, BddAbove <| (fun x => (âˆ‘' n : Î¹, log (f n x)).re) '' K) :
    MultipliableUniformlyOn f ğ”– :=
    âŸ¨(fun a => âˆ' i, f i a), hasProdUniformlyOn_of_clog  hf hfn hgâŸ©

lemma hasProdUniformlyOn_nat_one_add [TopologicalSpace Î±] {f : â„• â†’ Î± â†’ â„‚} {K : Set Î±}
    (hK : IsCompact K) {u : â„• â†’ â„} (hu : Summable u) (h : âˆ€á¶  n in atTop, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n)
    (hfn : âˆ€ x âˆˆ K, âˆ€ n, 1 + f n x â‰  0) (hcts : âˆ€ n, ContinuousOn (f n) K) :
    HasProdUniformlyOn (fun n a => (1 + f n a)) (fun a => âˆ' i, (1 + f i a)) {K} := by
  refine hasProdUniformlyOn_of_clog ?_ (by simpa using hfn) ?_
  Â· apply HasSumUniformlyOn.summableUniformlyOn (g := fun x => âˆ‘' i, log (1 + f i x))
    apply Complex.HasSumUniformlyOn_log_one_add K hu (Nat.cofinite_eq_atTop â–¸ h)
  Â· simp only [Set.mem_singleton_iff, forall_eq]
    apply (hK.bddAbove_image)
    apply (tendstoUniformlyOn_tsum_nat_log_one_add K hu h).re.continuousOn
    simp only [re_sum, log_re]
    filter_upwards with n using continuousOn_finset_sum _ fun c _ â†¦
      (continuousOn_const.add (hcts c)).norm.log (fun z hz â†¦ by simpa using hfn z hz c)

lemma multipliableUniformlyOn_nat_one_add [TopologicalSpace Î±] {f : â„• â†’ Î± â†’ â„‚} {K : Set Î±}
    (hK : IsCompact K) {u : â„• â†’ â„} (hu : Summable u) (h : âˆ€á¶  n in atTop, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n)
    (hfn : âˆ€ x âˆˆ K, âˆ€ n, 1 + f n x â‰  0) (hcts : âˆ€ n, ContinuousOn (f n) K) :
    MultipliableUniformlyOn (fun n a => (1 + f n a)) {K} :=
  âŸ¨(fun a => âˆ' i, (1 + f i a)), hasProdUniformlyOn_nat_one_add hK hu h hfn hctsâŸ©

lemma hasProdLocallyUniformlyOn_nat_one_add [TopologicalSpace Î±] [LocallyCompactSpace Î±]
    {f : â„• â†’ Î± â†’ â„‚} {K : Set Î±} (hK : IsOpen K) {u : â„• â†’ â„} (hu : Summable u)
    (h : âˆ€á¶  n in atTop, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n) (hfn : âˆ€ x âˆˆ K, âˆ€ n, 1 + f n x â‰  0)
    (hcts : âˆ€ n, ContinuousOn (f n) K) :
    HasProdLocallyUniformlyOn (fun n a => (1 + f n a)) (fun a => âˆ' i, (1 + (f i a))) K := by
  apply hasProdLocallyUniformlyOn_of_forall_compact hK
  refine fun S hS hC => hasProdUniformlyOn_nat_one_add hC hu ?_ (by tauto) fun n => (hcts n).mono hS
  filter_upwards [h] with n hn a ha using hn a (hS ha)

lemma multipliableLocallyUniformlyOn_nat_one_add [TopologicalSpace Î±] [LocallyCompactSpace Î±]
    {f : â„• â†’ Î± â†’ â„‚} {K : Set Î±} (hK : IsOpen K) {u : â„• â†’ â„} (hu : Summable u)
    (h : âˆ€á¶  n in atTop, âˆ€ x âˆˆ K, â€–f n xâ€– â‰¤ u n) (hfn : âˆ€ x âˆˆ K, âˆ€ n, 1 + f n x â‰  0)
    (hcts : âˆ€ n, ContinuousOn (f n) K) :
    MultipliableLocallyUniformlyOn (fun n a => (1 + f n a)) K :=
  âŸ¨(fun a => âˆ' i, (1 + (f i a))), hasProdLocallyUniformlyOn_nat_one_add hK hu h hfn hctsâŸ©

end UniformlyOn
