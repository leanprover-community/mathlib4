/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Patrick Massot, Eric Wieser, Ya√´l Dillies
-/
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.Topology.Algebra.Module.Basic

#align_import analysis.normed_space.basic from "leanprover-community/mathlib"@"bc91ed7093bf098d253401e69df601fc33dde156"

/-!
# Basic facts about real (semi)normed spaces

In this file we prove some theorems about (semi)normed spaces over real numberes.

## Main results

- `closure_ball`, `frontier_ball`, `interior_closedBall`, `frontier_closedBall`, `interior_sphere`,
  `frontier_sphere`: formulas for the closure/interior/frontier
  of nontrivial balls and spheres in a real seminormed space;

- `interior_closedBall'`, `frontier_closedBall'`, `interior_sphere'`, `frontier_sphere'`:
  similar lemmas assuming that the ambient space is separated and nontrivial instead of `r ‚â† 0`.
-/

open Metric Set Function Filter
open scoped NNReal Topology

/-- If `E` is a nontrivial topological module over `‚Ñù`, then `E` has no isolated points.
This is a particular case of `Module.punctured_nhds_neBot`. -/
instance Real.punctured_nhds_module_neBot {E : Type*} [AddCommGroup E] [TopologicalSpace E]
    [ContinuousAdd E] [Nontrivial E] [Module ‚Ñù E] [ContinuousSMul ‚Ñù E] (x : E) : NeBot (ùìù[‚â†] x) :=
  Module.punctured_nhds_neBot ‚Ñù E x
#align real.punctured_nhds_module_ne_bot Real.punctured_nhds_module_neBot

section Seminormed

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]

theorem inv_norm_smul_mem_closed_unit_ball (x : E) :
    ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x ‚àà closedBall (0 : E) 1 := by
  simp only [mem_closedBall_zero_iff, norm_smul, norm_inv, norm_norm, ‚Üê div_eq_inv_mul,
    div_self_le_one]
#align inv_norm_smul_mem_closed_unit_ball inv_norm_smul_mem_closed_unit_ball

theorem norm_smul_of_nonneg {t : ‚Ñù} (ht : 0 ‚â§ t) (x : E) : ‚Äñt ‚Ä¢ x‚Äñ = t * ‚Äñx‚Äñ := by
  rw [norm_smul, Real.norm_eq_abs, abs_of_nonneg ht]
#align norm_smul_of_nonneg norm_smul_of_nonneg

theorem dist_smul_add_one_sub_smul_le {r : ‚Ñù} {x y : E} (h : r ‚àà Icc 0 1) :
    dist (r ‚Ä¢ x + (1 - r) ‚Ä¢ y) x ‚â§ dist y x :=
  calc
    dist (r ‚Ä¢ x + (1 - r) ‚Ä¢ y) x = ‚Äñ1 - r‚Äñ * ‚Äñx - y‚Äñ := by
      simp_rw [dist_eq_norm', ‚Üê norm_smul, sub_smul, one_smul, smul_sub, ‚Üê sub_sub, ‚Üê sub_add,
        sub_right_comm]
    _ = (1 - r) * dist y x := by
      rw [Real.norm_eq_abs, abs_eq_self.mpr (sub_nonneg.mpr h.2), dist_eq_norm']
    _ ‚â§ (1 - 0) * dist y x := by gcongr; exact h.1
    _ = dist y x := by rw [sub_zero, one_mul]

theorem closure_ball (x : E) {r : ‚Ñù} (hr : r ‚â† 0) : closure (ball x r) = closedBall x r := by
  refine' Subset.antisymm closure_ball_subset_closedBall fun y hy => _
  have : ContinuousWithinAt (fun c : ‚Ñù => c ‚Ä¢ (y - x) + x) (Ico 0 1) 1 :=
    ((continuous_id.smul continuous_const).add continuous_const).continuousWithinAt
  convert this.mem_closure _ _
  ¬∑ rw [one_smul, sub_add_cancel]
  ¬∑ simp [closure_Ico zero_ne_one, zero_le_one]
  ¬∑ rintro c ‚ü®hc0, hc1‚ü©
    rw [mem_ball, dist_eq_norm, add_sub_cancel_right, norm_smul, Real.norm_eq_abs,
      abs_of_nonneg hc0, mul_comm, ‚Üê mul_one r]
    rw [mem_closedBall, dist_eq_norm] at hy
    replace hr : 0 < r := ((norm_nonneg _).trans hy).lt_of_ne hr.symm
    apply mul_lt_mul' <;> assumption
#align closure_ball closure_ball

theorem frontier_ball (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    frontier (ball x r) = sphere x r := by
  rw [frontier, closure_ball x hr, isOpen_ball.interior_eq, closedBall_diff_ball]
#align frontier_ball frontier_ball

theorem interior_closedBall (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    interior (closedBall x r) = ball x r := by
  cases' hr.lt_or_lt with hr hr
  ¬∑ rw [closedBall_eq_empty.2 hr, ball_eq_empty.2 hr.le, interior_empty]
  refine' Subset.antisymm _ ball_subset_interior_closedBall
  intro y hy
  rcases (mem_closedBall.1 <| interior_subset hy).lt_or_eq with (hr | rfl)
  ¬∑ exact hr
  set f : ‚Ñù ‚Üí E := fun c : ‚Ñù => c ‚Ä¢ (y - x) + x
  suffices f ‚Åª¬π' closedBall x (dist y x) ‚äÜ Icc (-1) 1 by
    have hfc : Continuous f := (continuous_id.smul continuous_const).add continuous_const
    have hf1 : (1 : ‚Ñù) ‚àà f ‚Åª¬π' interior (closedBall x <| dist y x) := by simpa [f]
    have h1 : (1 : ‚Ñù) ‚àà interior (Icc (-1 : ‚Ñù) 1) :=
      interior_mono this (preimage_interior_subset_interior_preimage hfc hf1)
    simp at h1
  intro c hc
  rw [mem_Icc, ‚Üê abs_le, ‚Üê Real.norm_eq_abs, ‚Üê mul_le_mul_right hr]
  simpa [f, dist_eq_norm, norm_smul] using hc
#align interior_closed_ball interior_closedBall

theorem frontier_closedBall (x : E) {r : ‚Ñù} (hr : r ‚â† 0) :
    frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall x hr, closedBall_diff_ball]
#align frontier_closed_ball frontier_closedBall

theorem interior_sphere (x : E) {r : ‚Ñù} (hr : r ‚â† 0) : interior (sphere x r) = ‚àÖ := by
  rw [‚Üê frontier_closedBall x hr, interior_frontier isClosed_ball]
#align interior_sphere interior_sphere

theorem frontier_sphere (x : E) {r : ‚Ñù} (hr : r ‚â† 0) : frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere x hr, diff_empty]
#align frontier_sphere frontier_sphere

end Seminormed

section Normed

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [Nontrivial E]

section Surj

variable (E)

theorem exists_norm_eq {c : ‚Ñù} (hc : 0 ‚â§ c) : ‚àÉ x : E, ‚Äñx‚Äñ = c := by
  rcases exists_ne (0 : E) with ‚ü®x, hx‚ü©
  rw [‚Üê norm_ne_zero_iff] at hx
  use c ‚Ä¢ ‚Äñx‚Äñ‚Åª¬π ‚Ä¢ x
  simp [norm_smul, Real.norm_of_nonneg hc, abs_of_nonneg hc, inv_mul_cancel hx]
#align exists_norm_eq exists_norm_eq

@[simp]
theorem range_norm : range (norm : E ‚Üí ‚Ñù) = Ici 0 :=
  Subset.antisymm (range_subset_iff.2 norm_nonneg) fun _ => exists_norm_eq E
#align range_norm range_norm

theorem nnnorm_surjective : Surjective (nnnorm : E ‚Üí ‚Ñù‚â•0) := fun c =>
  (exists_norm_eq E c.coe_nonneg).imp fun _ h => NNReal.eq h
#align nnnorm_surjective nnnorm_surjective

@[simp]
theorem range_nnnorm : range (nnnorm : E ‚Üí ‚Ñù‚â•0) = univ :=
  (nnnorm_surjective E).range_eq
#align range_nnnorm range_nnnorm

end Surj

theorem interior_closedBall' (x : E) (r : ‚Ñù) : interior (closedBall x r) = ball x r := by
  rcases eq_or_ne r 0 with (rfl | hr)
  ¬∑ rw [closedBall_zero, ball_zero, interior_singleton]
  ¬∑ exact interior_closedBall x hr
#align interior_closed_ball' interior_closedBall'

theorem frontier_closedBall' (x : E) (r : ‚Ñù) : frontier (closedBall x r) = sphere x r := by
  rw [frontier, closure_closedBall, interior_closedBall' x r, closedBall_diff_ball]
#align frontier_closed_ball' frontier_closedBall'

@[simp]
theorem interior_sphere' (x : E) (r : ‚Ñù) : interior (sphere x r) = ‚àÖ := by
  rw [‚Üê frontier_closedBall' x, interior_frontier isClosed_ball]
#align interior_sphere' interior_sphere'

@[simp]
theorem frontier_sphere' (x : E) (r : ‚Ñù) : frontier (sphere x r) = sphere x r := by
  rw [isClosed_sphere.frontier_eq, interior_sphere' x, diff_empty]
#align frontier_sphere' frontier_sphere'

end Normed
