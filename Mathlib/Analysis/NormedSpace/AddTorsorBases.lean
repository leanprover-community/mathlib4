/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.NormedSpace.FiniteDimension
import Mathlib.Analysis.Calculus.AffineMap
import Mathlib.Analysis.Convex.Combination
import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional

#align_import analysis.normed_space.add_torsor_bases from "leanprover-community/mathlib"@"2f4cdce0c2f2f3b8cd58f05d556d03b468e1eb2e"

/-!
# Bases in normed affine spaces.

This file contains results about bases in normed affine spaces.

## Main definitions:

 * `continuous_barycentric_coord`
 * `isOpenMap_barycentric_coord`
 * `AffineBasis.interior_convexHull`
 * `IsOpen.exists_subset_affineIndependent_span_eq_top`
 * `interior_convexHull_nonempty_iff_affineSpan_eq_top`
-/


section Barycentric

variable {Œπ ùïú E P : Type*} [NontriviallyNormedField ùïú] [CompleteSpace ùïú]
variable [NormedAddCommGroup E] [NormedSpace ùïú E]
variable [MetricSpace P] [NormedAddTorsor E P]

theorem isOpenMap_barycentric_coord [Nontrivial Œπ] (b : AffineBasis Œπ ùïú P) (i : Œπ) :
    IsOpenMap (b.coord i) :=
  AffineMap.isOpenMap_linear_iff.mp <|
    (b.coord i).linear.isOpenMap_of_finiteDimensional <|
      (b.coord i).linear_surjective_iff.mpr (b.surjective_coord i)
#align is_open_map_barycentric_coord isOpenMap_barycentric_coord

variable [FiniteDimensional ùïú E] (b : AffineBasis Œπ ùïú P)

@[continuity]
theorem continuous_barycentric_coord (i : Œπ) : Continuous (b.coord i) :=
  (b.coord i).continuous_of_finiteDimensional
#align continuous_barycentric_coord continuous_barycentric_coord

theorem smooth_barycentric_coord (b : AffineBasis Œπ ùïú E) (i : Œπ) : ContDiff ùïú ‚ä§ (b.coord i) :=
  (‚ü®b.coord i, continuous_barycentric_coord b i‚ü© : E ‚Üí·¥¨[ùïú] ùïú).contDiff
#align smooth_barycentric_coord smooth_barycentric_coord

end Barycentric

open Set

/-- Given a finite-dimensional normed real vector space, the interior of the convex hull of an
affine basis is the set of points whose barycentric coordinates are strictly positive with respect
to this basis.

TODO Restate this result for affine spaces (instead of vector spaces) once the definition of
convexity is generalised to this setting. -/
theorem AffineBasis.interior_convexHull {Œπ E : Type*} [Finite Œπ] [NormedAddCommGroup E]
    [NormedSpace ‚Ñù E] (b : AffineBasis Œπ ‚Ñù E) :
    interior (convexHull ‚Ñù (range b)) = {x | ‚àÄ i, 0 < b.coord i x} := by
  cases subsingleton_or_nontrivial Œπ
  ¬∑ -- The zero-dimensional case.
    have : range b = univ :=
      AffineSubspace.eq_univ_of_subsingleton_span_eq_top (subsingleton_range _) b.tot
    simp [this]
  ¬∑ -- The positive-dimensional case.
    haveI : FiniteDimensional ‚Ñù E := b.finiteDimensional
    have : convexHull ‚Ñù (range b) = ‚ãÇ i, b.coord i ‚Åª¬π' Ici 0 := by
      rw [b.convexHull_eq_nonneg_coord, setOf_forall]; rfl
    ext
    simp only [this, interior_iInter_of_finite, ‚Üê
      IsOpenMap.preimage_interior_eq_interior_preimage (isOpenMap_barycentric_coord b _)
        (continuous_barycentric_coord b _),
      interior_Ici, mem_iInter, mem_setOf_eq, mem_Ioi, mem_preimage]
#align affine_basis.interior_convex_hull AffineBasis.interior_convexHull

variable {V P : Type*} [NormedAddCommGroup V] [NormedSpace ‚Ñù V] [MetricSpace P]
  [NormedAddTorsor V P]

open AffineMap

/-- Given a set `s` of affine-independent points belonging to an open set `u`, we may extend `s` to
an affine basis, all of whose elements belong to `u`. -/
theorem IsOpen.exists_between_affineIndependent_span_eq_top {s u : Set P} (hu : IsOpen u)
    (hsu : s ‚äÜ u) (hne : s.Nonempty) (h : AffineIndependent ‚Ñù ((‚Üë) : s ‚Üí P)) :
    ‚àÉ t : Set P, s ‚äÜ t ‚àß t ‚äÜ u ‚àß AffineIndependent ‚Ñù ((‚Üë) : t ‚Üí P) ‚àß affineSpan ‚Ñù t = ‚ä§ := by
  obtain ‚ü®q, hq‚ü© := hne
  obtain ‚ü®Œµ, Œµ0, hŒµu‚ü© := Metric.nhds_basis_closedBall.mem_iff.1 (hu.mem_nhds <| hsu hq)
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ, ht‚ÇÉ‚ü© := exists_subset_affineIndependent_affineSpan_eq_top h
  let f : P ‚Üí P := fun y => lineMap q y (Œµ / dist y q)
  have hf : ‚àÄ y, f y ‚àà u := by
    refine fun y => hŒµu ?_
    simp only [f]
    rw [Metric.mem_closedBall, lineMap_apply, dist_vadd_left, norm_smul, Real.norm_eq_abs,
      dist_eq_norm_vsub V y q, abs_div, abs_of_pos Œµ0, abs_of_nonneg (norm_nonneg _), div_mul_comm]
    exact mul_le_of_le_one_left Œµ0.le (div_self_le_one _)
  have hŒµyq : ‚àÄ y ‚àâ s, Œµ / dist y q ‚â† 0 := fun y hy =>
    div_ne_zero Œµ0.ne' (dist_ne_zero.2 (ne_of_mem_of_not_mem hq hy).symm)
  classical
  let w : t ‚Üí ‚ÑùÀ£ := fun p => if hp : (p : P) ‚àà s then 1 else Units.mk0 _ (hŒµyq (‚Üëp) hp)
  refine ‚ü®Set.range fun p : t => lineMap q p (w p : ‚Ñù), ?_, ?_, ?_, ?_‚ü©
  ¬∑ intro p hp; use ‚ü®p, ht‚ÇÅ hp‚ü©; simp [w, hp]
  ¬∑ rintro y ‚ü®‚ü®p, hp‚ü©, rfl‚ü©
    by_cases hps : p ‚àà s <;>
    simp only [w, hps, lineMap_apply_one, Units.val_mk0, dif_neg, dif_pos, not_false_iff,
      Units.val_one, Subtype.coe_mk] <;>
    [exact hsu hps; exact hf p]
  ¬∑ exact (ht‚ÇÇ.units_lineMap ‚ü®q, ht‚ÇÅ hq‚ü© w).range
  ¬∑ rw [affineSpan_eq_affineSpan_lineMap_units (ht‚ÇÅ hq) w, ht‚ÇÉ]
#align is_open.exists_between_affine_independent_span_eq_top IsOpen.exists_between_affineIndependent_span_eq_top

theorem IsOpen.exists_subset_affineIndependent_span_eq_top {u : Set P} (hu : IsOpen u)
    (hne : u.Nonempty) : ‚àÉ s ‚äÜ u, AffineIndependent ‚Ñù ((‚Üë) : s ‚Üí P) ‚àß affineSpan ‚Ñù s = ‚ä§ := by
  rcases hne with ‚ü®x, hx‚ü©
  rcases hu.exists_between_affineIndependent_span_eq_top (singleton_subset_iff.mpr hx)
    (singleton_nonempty _) (affineIndependent_of_subsingleton _ _) with ‚ü®s, -, hsu, hs‚ü©
  exact ‚ü®s, hsu, hs‚ü©
#align is_open.exists_subset_affine_independent_span_eq_top IsOpen.exists_subset_affineIndependent_span_eq_top

/-- The affine span of a nonempty open set is `‚ä§`. -/
theorem IsOpen.affineSpan_eq_top {u : Set P} (hu : IsOpen u) (hne : u.Nonempty) :
    affineSpan ‚Ñù u = ‚ä§ :=
  let ‚ü®_, hsu, _, hs'‚ü© := hu.exists_subset_affineIndependent_span_eq_top hne
  top_unique <| hs' ‚ñ∏ affineSpan_mono _ hsu
#align is_open.affine_span_eq_top IsOpen.affineSpan_eq_top

theorem affineSpan_eq_top_of_nonempty_interior {s : Set V}
    (hs : (interior <| convexHull ‚Ñù s).Nonempty) : affineSpan ‚Ñù s = ‚ä§ :=
  top_unique <| isOpen_interior.affineSpan_eq_top hs ‚ñ∏
    (affineSpan_mono _ interior_subset).trans_eq (affineSpan_convexHull _)
#align affine_span_eq_top_of_nonempty_interior affineSpan_eq_top_of_nonempty_interior

theorem AffineBasis.centroid_mem_interior_convexHull {Œπ} [Fintype Œπ] (b : AffineBasis Œπ ‚Ñù V) :
    Finset.univ.centroid ‚Ñù b ‚àà interior (convexHull ‚Ñù (range b)) := by
  haveI := b.nonempty
  simp only [b.interior_convexHull, mem_setOf_eq, b.coord_apply_centroid (Finset.mem_univ _),
    inv_pos, Nat.cast_pos, Finset.card_pos, Finset.univ_nonempty, forall_true_iff]
#align affine_basis.centroid_mem_interior_convex_hull AffineBasis.centroid_mem_interior_convexHull

theorem interior_convexHull_nonempty_iff_affineSpan_eq_top [FiniteDimensional ‚Ñù V] {s : Set V} :
    (interior (convexHull ‚Ñù s)).Nonempty ‚Üî affineSpan ‚Ñù s = ‚ä§ := by
  refine ‚ü®affineSpan_eq_top_of_nonempty_interior, fun h => ?_‚ü©
  obtain ‚ü®t, hts, b, hb‚ü© := AffineBasis.exists_affine_subbasis h
  suffices (interior (convexHull ‚Ñù (range b))).Nonempty by
    rw [hb, Subtype.range_coe_subtype, setOf_mem_eq] at this
    refine this.mono (by gcongr)
  lift t to Finset V using b.finite_set
  exact ‚ü®_, b.centroid_mem_interior_convexHull‚ü©
#align interior_convex_hull_nonempty_iff_affine_span_eq_top interior_convexHull_nonempty_iff_affineSpan_eq_top

theorem Convex.interior_nonempty_iff_affineSpan_eq_top [FiniteDimensional ‚Ñù V] {s : Set V}
    (hs : Convex ‚Ñù s) : (interior s).Nonempty ‚Üî affineSpan ‚Ñù s = ‚ä§ := by
  rw [‚Üê interior_convexHull_nonempty_iff_affineSpan_eq_top, hs.convexHull_eq]
#align convex.interior_nonempty_iff_affine_span_eq_top Convex.interior_nonempty_iff_affineSpan_eq_top
