/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Analysis.Asymptotics.Defs
/-!
# Asymptotic statements about the operator norm

This file contains lemmas about how operator norm on continuous linear maps interacts with `IsBigO`.

-/

open Asymptotics


variable {ð•œ ð•œâ‚‚ ð•œâ‚ƒ E F G : Type*}
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [SeminormedAddCommGroup G]
variable [NontriviallyNormedField ð•œ] [NontriviallyNormedField ð•œâ‚‚] [NontriviallyNormedField ð•œâ‚ƒ]
  [NormedSpace ð•œ E] [NormedSpace ð•œâ‚‚ F] [NormedSpace ð•œâ‚ƒ G] {Ïƒâ‚â‚‚ : ð•œ â†’+* ð•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ð•œâ‚‚ â†’+* ð•œâ‚ƒ}

namespace ContinuousLinearMap

variable [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) (l : Filter E)

theorem isBigOWith_id : IsBigOWith â€–fâ€– l f fun x => x :=
  isBigOWith_of_le' _ f.le_opNorm

theorem isBigO_id : f =O[l] fun x => x :=
  (f.isBigOWith_id l).isBigO

theorem isBigOWith_comp [RingHomIsometric Ïƒâ‚‚â‚ƒ] {Î± : Type*} (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : Î± â†’ F)
    (l : Filter Î±) : IsBigOWith â€–gâ€– l (fun x' => g (f x')) f :=
  (g.isBigOWith_id âŠ¤).comp_tendsto le_top

theorem isBigO_comp [RingHomIsometric Ïƒâ‚‚â‚ƒ] {Î± : Type*} (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : Î± â†’ F)
    (l : Filter Î±) : (fun x' => g (f x')) =O[l] f :=
  (g.isBigOWith_comp f l).isBigO

theorem isBigOWith_sub (x : E) :
    IsBigOWith â€–fâ€– l (fun x' => f (x' - x)) fun x' => x' - x :=
  f.isBigOWith_comp _ l

theorem isBigO_sub (x : E) :
    (fun x' => f (x' - x)) =O[l] fun x' => x' - x :=
  f.isBigO_comp _ l

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable {Ïƒâ‚‚â‚ : ð•œâ‚‚ â†’+* ð•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F)

section

variable [RingHomIsometric Ïƒâ‚â‚‚]

theorem isBigO_comp {Î± : Type*} (f : Î± â†’ E) (l : Filter Î±) : (fun x' => e (f x')) =O[l] f :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).isBigO_comp f l

theorem isBigO_sub (l : Filter E) (x : E) : (fun x' => e (x' - x)) =O[l] fun x' => x' - x :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).isBigO_sub l x

end

section

variable [RingHomIsometric Ïƒâ‚‚â‚]

theorem isBigO_comp_rev {Î± : Type*} (f : Î± â†’ E) (l : Filter Î±) : f =O[l] fun x' => e (f x') :=
  (e.symm.isBigO_comp _ l).congr_left fun _ => e.symm_apply_apply _

theorem isBigO_sub_rev (l : Filter E) (x : E) : (fun x' => x' - x) =O[l] fun x' => e (x' - x) :=
  e.isBigO_comp_rev _ _

end

end ContinuousLinearEquiv
