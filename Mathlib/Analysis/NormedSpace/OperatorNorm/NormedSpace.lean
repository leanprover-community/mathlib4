/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm.Bilinear
import Mathlib.Analysis.NormedSpace.OperatorNorm.NNNorm
import Mathlib.Analysis.NormedSpace.Span

/-!
# Operator norm for maps on normed spaces

This file contains statements about operator norm for which it really matters that the
underlying space has a norm (rather than just a seminorm).
-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped Classical NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E Eâ‚— F Fâ‚— G Gâ‚— ğ“• : Type*}


section Normed

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
  [NormedAddCommGroup Fâ‚—]

open Metric ContinuousLinearMap

section

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G] [NormedSpace ğ•œ Fâ‚—] (c : ğ•œ)
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} (f g : E â†’SL[Ïƒâ‚â‚‚] F) (x y z : E)

namespace LinearMap

theorem bound_of_shell [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ}
    (hc : 1 < â€–câ€–) (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
    â€–f xâ€– â‰¤ C * â€–xâ€– := by
  by_cases hx : x = 0; Â· simp [hx]
  exact SemilinearMapClass.bound_of_shell_semi_normed f Îµ_pos hc hf (norm_ne_zero_iff.2 hx)
#align linear_map.bound_of_shell LinearMap.bound_of_shell

/-- `LinearMap.bound_of_ball_bound'` is a version of this lemma over a field satisfying `RCLike`
that produces a concrete bound.
-/
theorem bound_of_ball_bound {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] Fâ‚—)
    (h : âˆ€ z âˆˆ Metric.ball (0 : E) r, â€–f zâ€– â‰¤ c) : âˆƒ C, âˆ€ z : E, â€–f zâ€– â‰¤ C * â€–zâ€– := by
  cases' @NontriviallyNormedField.non_trivial ğ•œ _ with k hk
  use c * (â€–kâ€– / r)
  intro z
  refine bound_of_shell _ r_pos hk (fun x hko hxo => ?_) _
  calc
    â€–f xâ€– â‰¤ c := h _ (mem_ball_zero_iff.mpr hxo)
    _ â‰¤ c * (â€–xâ€– * â€–kâ€– / r) := le_mul_of_one_le_right ?_ ?_
    _ = _ := by ring
  Â· exact le_trans (norm_nonneg _) (h 0 (by simp [r_pos]))
  Â· rw [div_le_iff (zero_lt_one.trans hk)] at hko
    exact (one_le_div r_pos).mpr hko
#align linear_map.bound_of_ball_bound LinearMap.bound_of_ball_bound

theorem antilipschitz_of_comap_nhds_le [h : RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F)
    (hf : (ğ“ 0).comap f â‰¤ ğ“ 0) : âˆƒ K, AntilipschitzWith K f := by
  rcases ((nhds_basis_ball.comap _).le_basis_iff nhds_basis_ball).1 hf 1 one_pos with âŸ¨Îµ, Îµ0, hÎµâŸ©
  simp only [Set.subset_def, Set.mem_preimage, mem_ball_zero_iff] at hÎµ
  lift Îµ to â„â‰¥0 using Îµ0.le
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  refine âŸ¨Îµâ»Â¹ * â€–câ€–â‚Š, AddMonoidHomClass.antilipschitz_of_bound f fun x => ?_âŸ©
  by_cases hx : f x = 0
  Â· rw [â† hx] at hf
    obtain rfl : x = 0 := Specializes.eq (specializes_iff_pure.2 <|
      ((Filter.tendsto_pure_pure _ _).mono_right (pure_le_nhds _)).le_comap.trans hf)
    exact norm_zero.trans_le (mul_nonneg (NNReal.coe_nonneg _) (norm_nonneg _))
  have hcâ‚€ : c â‰  0 := norm_pos_iff.1 (one_pos.trans hc)
  rw [â† h.1] at hc
  rcases rescale_to_shell_zpow hc Îµ0 hx with âŸ¨n, -, hlt, -, hleâŸ©
  simp only [â† map_zpowâ‚€, h.1, â† map_smulâ‚›â‚—] at hlt hle
  calc
    â€–xâ€– = â€–c ^ nâ€–â»Â¹ * â€–c ^ n â€¢ xâ€– := by
      rwa [â† norm_inv, â† norm_smul, inv_smul_smulâ‚€ (zpow_ne_zero _ _)]
    _ â‰¤ â€–c ^ nâ€–â»Â¹ * 1 := (mul_le_mul_of_nonneg_left (hÎµ _ hlt).le (inv_nonneg.2 (norm_nonneg _)))
    _ â‰¤ Îµâ»Â¹ * â€–câ€– * â€–f xâ€– := by rwa [mul_one]
#align linear_map.antilipschitz_of_comap_nhds_le LinearMap.antilipschitz_of_comap_nhds_le

end LinearMap

namespace ContinuousLinearMap

section OpNorm

open Set Real

/-- An operator is zero iff its norm vanishes. -/
theorem opNorm_zero_iff [RingHomIsometric Ïƒâ‚â‚‚] : â€–fâ€– = 0 â†” f = 0 :=
  Iff.intro
    (fun hn => ContinuousLinearMap.ext fun x => norm_le_zero_iff.1
      (calc
        _ â‰¤ â€–fâ€– * â€–xâ€– := le_opNorm _ _
        _ = _ := by rw [hn, zero_mul]))
    (by
      rintro rfl
      exact opNorm_zero)
#align continuous_linear_map.op_norm_zero_iff ContinuousLinearMap.opNorm_zero_iff

@[deprecated] alias op_norm_zero_iff := opNorm_zero_iff -- deprecated on 2024-02-02

/-- If a normed space is non-trivial, then the norm of the identity equals `1`. -/
@[simp]
theorem norm_id [Nontrivial E] : â€–id ğ•œ Eâ€– = 1 := by
  refine norm_id_of_nontrivial_seminorm ?_
  obtain âŸ¨x, hxâŸ© := exists_ne (0 : E)
  exact âŸ¨x, ne_of_gt (norm_pos_iff.2 hx)âŸ©
#align continuous_linear_map.norm_id ContinuousLinearMap.norm_id

@[simp]
lemma nnnorm_id [Nontrivial E] : â€–id ğ•œ Eâ€–â‚Š = 1 := NNReal.eq norm_id

instance normOneClass [Nontrivial E] : NormOneClass (E â†’L[ğ•œ] E) :=
  âŸ¨norm_idâŸ©
#align continuous_linear_map.norm_one_class ContinuousLinearMap.normOneClass

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance toNormedAddCommGroup [RingHomIsometric Ïƒâ‚â‚‚] : NormedAddCommGroup (E â†’SL[Ïƒâ‚â‚‚] F) :=
  NormedAddCommGroup.ofSeparation fun f => (opNorm_zero_iff f).mp
#align continuous_linear_map.to_normed_add_comm_group ContinuousLinearMap.toNormedAddCommGroup

/-- Continuous linear maps form a normed ring with respect to the operator norm. -/
instance toNormedRing : NormedRing (E â†’L[ğ•œ] E) :=
  { ContinuousLinearMap.toNormedAddCommGroup, ContinuousLinearMap.toSemiNormedRing with }
#align continuous_linear_map.to_normed_ring ContinuousLinearMap.toNormedRing

variable {f}

theorem homothety_norm [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (f : E â†’SL[Ïƒâ‚â‚‚] F) {a : â„}
    (hf : âˆ€ x, â€–f xâ€– = a * â€–xâ€–) : â€–fâ€– = a := by
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0
  rw [â† norm_pos_iff] at hx
  have ha : 0 â‰¤ a := by simpa only [hf, hx, mul_nonneg_iff_of_pos_right] using norm_nonneg (f x)
  apply le_antisymm (f.opNorm_le_bound ha fun y => le_of_eq (hf y))
  simpa only [hf, hx, mul_le_mul_right] using f.le_opNorm x
#align continuous_linear_map.homothety_norm ContinuousLinearMap.homothety_norm

variable (f)

/-- If a continuous linear map is a topology embedding, then it is expands the distances
by a positive factor. -/
theorem antilipschitz_of_embedding (f : E â†’L[ğ•œ] Fâ‚—) (hf : Embedding f) :
    âˆƒ K, AntilipschitzWith K f :=
  f.toLinearMap.antilipschitz_of_comap_nhds_le <| map_zero f â–¸ (hf.nhds_eq_comap 0).ge
#align continuous_linear_map.antilipschitz_of_embedding ContinuousLinearMap.antilipschitz_of_embedding

end OpNorm

end ContinuousLinearMap

namespace LinearIsometry

@[simp]
theorem norm_toContinuousLinearMap [Nontrivial E] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—áµ¢[Ïƒâ‚â‚‚] F) :
    â€–f.toContinuousLinearMapâ€– = 1 :=
  f.toContinuousLinearMap.homothety_norm <| by simp
#align linear_isometry.norm_to_continuous_linear_map LinearIsometry.norm_toContinuousLinearMap

variable {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

/-- Postcomposition of a continuous linear map with a linear isometry preserves
the operator norm. -/
theorem norm_toContinuousLinearMap_comp [RingHomIsometric Ïƒâ‚â‚‚] (f : F â†’â‚›â‚—áµ¢[Ïƒâ‚‚â‚ƒ] G)
    {g : E â†’SL[Ïƒâ‚â‚‚] F} : â€–f.toContinuousLinearMap.comp gâ€– = â€–gâ€– :=
  opNorm_ext (f.toContinuousLinearMap.comp g) g fun x => by
    simp only [norm_map, coe_toContinuousLinearMap, coe_comp', Function.comp_apply]
#align linear_isometry.norm_to_continuous_linear_map_comp LinearIsometry.norm_toContinuousLinearMap_comp

end LinearIsometry

end

namespace ContinuousLinearMap

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G] [NormedSpace ğ•œ Fâ‚—] (c : ğ•œ)
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ}

variable {ğ•œâ‚‚' : Type*} [NontriviallyNormedField ğ•œâ‚‚'] {F' : Type*} [NormedAddCommGroup F']
  [NormedSpace ğ•œâ‚‚' F'] {Ïƒâ‚‚' : ğ•œâ‚‚' â†’+* ğ•œâ‚‚} {Ïƒâ‚‚'' : ğ•œâ‚‚ â†’+* ğ•œâ‚‚'} {Ïƒâ‚‚â‚ƒ' : ğ•œâ‚‚' â†’+* ğ•œâ‚ƒ}
  [RingHomInvPair Ïƒâ‚‚' Ïƒâ‚‚''] [RingHomInvPair Ïƒâ‚‚'' Ïƒâ‚‚'] [RingHomCompTriple Ïƒâ‚‚' Ïƒâ‚‚â‚ƒ Ïƒâ‚‚â‚ƒ']
  [RingHomCompTriple Ïƒâ‚‚'' Ïƒâ‚‚â‚ƒ' Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚‚']
  [RingHomIsometric Ïƒâ‚‚''] [RingHomIsometric Ïƒâ‚‚â‚ƒ']

/-- Precomposition with a linear isometry preserves the operator norm. -/
theorem opNorm_comp_linearIsometryEquiv (f : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (g : F' â‰ƒâ‚›â‚—áµ¢[Ïƒâ‚‚'] F) :
    â€–f.comp g.toLinearIsometry.toContinuousLinearMapâ€– = â€–fâ€– := by
  cases subsingleton_or_nontrivial F'
  Â· haveI := g.symm.toLinearEquiv.toEquiv.subsingleton
    simp
  refine le_antisymm ?_ ?_
  Â· convert f.opNorm_comp_le g.toLinearIsometry.toContinuousLinearMap
    simp [g.toLinearIsometry.norm_toContinuousLinearMap]
  Â· convert (f.comp g.toLinearIsometry.toContinuousLinearMap).opNorm_comp_le
        g.symm.toLinearIsometry.toContinuousLinearMap
    Â· ext
      simp
    haveI := g.symm.surjective.nontrivial
    simp [g.symm.toLinearIsometry.norm_toContinuousLinearMap]
#align continuous_linear_map.op_norm_comp_linear_isometry_equiv ContinuousLinearMap.opNorm_comp_linearIsometryEquiv

@[deprecated] -- deprecated on 2024-02-02
alias op_norm_comp_linearIsometryEquiv := opNorm_comp_linearIsometryEquiv

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp]
theorem norm_smulRight_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRight c fâ€– = â€–câ€– * â€–fâ€– := by
  refine le_antisymm ?_ ?_
  Â· refine opNorm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) fun x => ?_
    calc
      â€–c x â€¢ fâ€– = â€–c xâ€– * â€–fâ€– := norm_smul _ _
      _ â‰¤ â€–câ€– * â€–xâ€– * â€–fâ€– := mul_le_mul_of_nonneg_right (le_opNorm _ _) (norm_nonneg _)
      _ = â€–câ€– * â€–fâ€– * â€–xâ€– := by ring
  Â· by_cases h : f = 0
    Â· simp [h]
    Â· have : 0 < â€–fâ€– := norm_pos_iff.2 h
      rw [â† le_div_iff this]
      refine opNorm_le_bound _ (div_nonneg (norm_nonneg _) (norm_nonneg f)) fun x => ?_
      rw [div_mul_eq_mul_div, le_div_iff this]
      calc
        â€–c xâ€– * â€–fâ€– = â€–c x â€¢ fâ€– := (norm_smul _ _).symm
        _ = â€–smulRight c f xâ€– := rfl
        _ â‰¤ â€–smulRight c fâ€– * â€–xâ€– := le_opNorm _ _
#align continuous_linear_map.norm_smul_right_apply ContinuousLinearMap.norm_smulRight_apply

/-- The non-negative norm of the tensor product of a scalar linear map and of an element of a normed
space is the product of the non-negative norms. -/
@[simp]
theorem nnnorm_smulRight_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRight c fâ€–â‚Š = â€–câ€–â‚Š * â€–fâ€–â‚Š :=
  NNReal.eq <| c.norm_smulRight_apply f
#align continuous_linear_map.nnnorm_smul_right_apply ContinuousLinearMap.nnnorm_smulRight_apply

variable (ğ•œ E Fâ‚—)

set_option linter.uppercaseLean3 false

/-- `ContinuousLinearMap.smulRight` as a continuous trilinear map:
`smulRightL (c : E â†’L[ğ•œ] ğ•œ) (f : F) (x : E) = c x â€¢ f`. -/
def smulRightL : (E â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Fâ‚— :=
  LinearMap.mkContinuousâ‚‚
    { toFun := smulRightâ‚—
      map_add' := fun câ‚ câ‚‚ => by
        ext x
        simp only [add_smul, coe_smulRightâ‚—, add_apply, smulRight_apply, LinearMap.add_apply]
      map_smul' := fun m c => by
        ext x
        dsimp
        rw [smul_smul] }
    1 fun c x => by
      simp only [coe_smulRightâ‚—, one_mul, norm_smulRight_apply, LinearMap.coe_mk, AddHom.coe_mk,
        le_refl]
#align continuous_linear_map.smul_rightL ContinuousLinearMap.smulRightL

variable {ğ•œ E Fâ‚—}

@[simp]
theorem norm_smulRightL_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRightL ğ•œ E Fâ‚— c fâ€– = â€–câ€– * â€–fâ€– :=
  norm_smulRight_apply c f
#align continuous_linear_map.norm_smul_rightL_apply ContinuousLinearMap.norm_smulRightL_apply

@[simp]
theorem norm_smulRightL (c : E â†’L[ğ•œ] ğ•œ) [Nontrivial Fâ‚—] : â€–smulRightL ğ•œ E Fâ‚— câ€– = â€–câ€– :=
  ContinuousLinearMap.homothety_norm _ c.norm_smulRight_apply
#align continuous_linear_map.norm_smul_rightL ContinuousLinearMap.norm_smulRightL

#adaptation_note
/--
Before https://github.com/leanprover/lean4/pull/4119 we had to create a local instance:
```
letI : SeminormedAddCommGroup ((E â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Fâ‚—) :=
      toSeminormedAddCommGroup (F := Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Fâ‚—) (ğ•œ := ğ•œ) (Ïƒâ‚â‚‚ := RingHom.id ğ•œ)
```
-/
set_option maxSynthPendingDepth 2 in
lemma norm_smulRightL_le : â€–smulRightL ğ•œ E Fâ‚—â€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _

end ContinuousLinearMap

namespace Submodule

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}

theorem norm_subtypeL (K : Submodule ğ•œ E) [Nontrivial K] : â€–K.subtypeLâ€– = 1 :=
  K.subtypeâ‚—áµ¢.norm_toContinuousLinearMap
set_option linter.uppercaseLean3 false in
#align submodule.norm_subtypeL Submodule.norm_subtypeL

end Submodule

namespace ContinuousLinearEquiv

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚]
  [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

section

variable [RingHomIsometric Ïƒâ‚‚â‚]

protected theorem antilipschitz (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    AntilipschitzWith â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š e :=
  e.symm.lipschitz.to_rightInverse e.left_inv
#align continuous_linear_equiv.antilipschitz ContinuousLinearEquiv.antilipschitz

theorem one_le_norm_mul_norm_symm [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    1 â‰¤ â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€– * â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– := by
  rw [mul_comm]
  convert (e.symm : F â†’SL[Ïƒâ‚‚â‚] E).opNorm_comp_le (e : E â†’SL[Ïƒâ‚â‚‚] F)
  rw [e.coe_symm_comp_coe, ContinuousLinearMap.norm_id]
#align continuous_linear_equiv.one_le_norm_mul_norm_symm ContinuousLinearEquiv.one_le_norm_mul_norm_symm

theorem norm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€– :=
  pos_of_mul_pos_left (lt_of_lt_of_le zero_lt_one e.one_le_norm_mul_norm_symm) (norm_nonneg _)
#align continuous_linear_equiv.norm_pos ContinuousLinearEquiv.norm_pos

theorem norm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– :=
  pos_of_mul_pos_right (zero_lt_one.trans_le e.one_le_norm_mul_norm_symm) (norm_nonneg _)
#align continuous_linear_equiv.norm_symm_pos ContinuousLinearEquiv.norm_symm_pos

theorem nnnorm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š :=
  e.norm_symm_pos
#align continuous_linear_equiv.nnnorm_symm_pos ContinuousLinearEquiv.nnnorm_symm_pos

theorem subsingleton_or_norm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    Subsingleton E âˆ¨ 0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– := by
  rcases subsingleton_or_nontrivial E with (_i | _i)
  Â· left
    infer_instance
  Â· right
    exact e.norm_symm_pos
#align continuous_linear_equiv.subsingleton_or_norm_symm_pos ContinuousLinearEquiv.subsingleton_or_norm_symm_pos

theorem subsingleton_or_nnnorm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    Subsingleton E âˆ¨ 0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š :=
  subsingleton_or_norm_symm_pos e
#align continuous_linear_equiv.subsingleton_or_nnnorm_symm_pos ContinuousLinearEquiv.subsingleton_or_nnnorm_symm_pos

variable (ğ•œ)

@[simp]
theorem coord_norm (x : E) (h : x â‰  0) : â€–coord ğ•œ x hâ€– = â€–xâ€–â»Â¹ := by
  have hx : 0 < â€–xâ€– := norm_pos_iff.mpr h
  haveI : Nontrivial (ğ•œ âˆ™ x) := Submodule.nontrivial_span_singleton h
  exact ContinuousLinearMap.homothety_norm _ fun y =>
    homothety_inverse _ hx _ (LinearEquiv.toSpanNonzeroSingleton_homothety ğ•œ x h) _
#align continuous_linear_equiv.coord_norm ContinuousLinearEquiv.coord_norm

end

end ContinuousLinearEquiv

end Normed

/-- A bounded bilinear form `B` in a real normed space is *coercive*
if there is some positive constant C such that `C * â€–uâ€– * â€–uâ€– â‰¤ B u u`.
-/
def IsCoercive [NormedAddCommGroup E] [NormedSpace â„ E] (B : E â†’L[â„] E â†’L[â„] â„) : Prop :=
  âˆƒ C, 0 < C âˆ§ âˆ€ u, C * â€–uâ€– * â€–uâ€– â‰¤ B u u
#align is_coercive IsCoercive

section Equicontinuous

variable {Î¹ : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [RingHomIsometric Ïƒâ‚â‚‚] [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] (f : Î¹ â†’ E â†’SL[Ïƒâ‚â‚‚] F)

/-- Equivalent characterizations for equicontinuity of a family of continuous linear maps
between normed spaces. See also `WithSeminorms.equicontinuous_TFAE` for similar characterizations
between spaces satisfying `WithSeminorms`. -/
protected theorem NormedSpace.equicontinuous_TFAE : List.TFAE
    [ EquicontinuousAt ((â†‘) âˆ˜ f) 0,
      Equicontinuous ((â†‘) âˆ˜ f),
      UniformEquicontinuous ((â†‘) âˆ˜ f),
      âˆƒ C, âˆ€ i x, â€–f i xâ€– â‰¤ C * â€–xâ€–,
      âˆƒ C â‰¥ 0, âˆ€ i x, â€–f i xâ€– â‰¤ C * â€–xâ€–,
      âˆƒ C, âˆ€ i, â€–f iâ€– â‰¤ C,
      âˆƒ C â‰¥ 0, âˆ€ i, â€–f iâ€– â‰¤ C,
      BddAbove (Set.range (â€–f Â·â€–)),
      (â¨† i, (â€–f iâ€–â‚Š : ENNReal)) < âŠ¤ ] := by
  -- `1 â†” 2 â†” 3` follows from `uniformEquicontinuous_of_equicontinuousAt_zero`
  tfae_have 1 â†’ 3
  Â· exact uniformEquicontinuous_of_equicontinuousAt_zero f
  tfae_have 3 â†’ 2
  Â· exact UniformEquicontinuous.equicontinuous
  tfae_have 2 â†’ 1
  Â· exact fun H â†¦ H 0
  -- `4 â†” 5 â†” 6 â†” 7 â†” 8 â†” 9` is morally trivial, we just have to use a lot of rewriting
  -- and `congr` lemmas
  tfae_have 4 â†” 5
  Â· rw [exists_ge_and_iff_exists]
    exact fun Câ‚ Câ‚‚ hC â†¦ forallâ‚‚_imp fun i x â†¦ le_trans' <| by gcongr
  tfae_have 5 â†” 7
  Â· refine exists_congr (fun C â†¦ and_congr_right fun hC â†¦ forall_congr' fun i â†¦ ?_)
    rw [ContinuousLinearMap.opNorm_le_iff hC]
  tfae_have 7 â†” 8
  Â· simp_rw [bddAbove_iff_exists_ge (0 : â„), Set.forall_mem_range]
  tfae_have 6 â†” 8
  Â· simp_rw [bddAbove_def, Set.forall_mem_range]
  tfae_have 8 â†” 9
  Â· rw [ENNReal.iSup_coe_lt_top, â† NNReal.bddAbove_coe, â† Set.range_comp]
    rfl
  -- `3 â†” 4` is the interesting part of the result. It is essentially a combination of
  -- `WithSeminorms.uniformEquicontinuous_iff_exists_continuous_seminorm` which turns
  -- equicontinuity into existence of some continuous seminorm and
  -- `Seminorm.bound_of_continuous_normedSpace` which characterize such seminorms.
  tfae_have 3 â†” 4
  Â· refine ((norm_withSeminorms ğ•œâ‚‚ F).uniformEquicontinuous_iff_exists_continuous_seminorm _).trans
      ?_
    rw [forall_const]
    constructor
    Â· intro âŸ¨p, hp, hpfâŸ©
      rcases p.bound_of_continuous_normedSpace hp with âŸ¨C, -, hCâŸ©
      exact âŸ¨C, fun i x â†¦ (hpf i x).trans (hC x)âŸ©
    Â· intro âŸ¨C, hCâŸ©
      refine âŸ¨C.toNNReal â€¢ normSeminorm ğ•œ E,
        ((norm_withSeminorms ğ•œ E).continuous_seminorm 0).const_smul C.toNNReal, fun i x â†¦ ?_âŸ©
      exact (hC i x).trans (mul_le_mul_of_nonneg_right (C.le_coe_toNNReal) (norm_nonneg x))
  tfae_finish

end Equicontinuous
