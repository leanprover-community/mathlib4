/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/

import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace

/-!
# Results about operator norms in normed algebras

This file (split off from `OperatorNorm.lean`) contains results about the operator norm
of multiplication and scalar-multiplication operations in normed algebras and normed modules.
-/

suppress_compilation

set_option linter.uppercaseLean3 false

open Metric
open scoped Classical NNReal Topology Uniformity

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ]

section SemiNormed

variable [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

namespace ContinuousLinearMap

section MultiplicationLinear

section NonUnital

variable (ğ•œ) (ğ•œ' : Type*) [NonUnitalSeminormedRing ğ•œ']
variable [NormedSpace ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ'] [SMulCommClass ğ•œ ğ•œ' ğ•œ']

/-- Multiplication in a non-unital normed algebra as a continuous bilinear map. -/
def mul : ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  (LinearMap.mul ğ•œ ğ•œ').mkContinuousâ‚‚ 1 fun x y => by simpa using norm_mul_le x y
#align continuous_linear_map.mul ContinuousLinearMap.mul

@[simp]
theorem mul_apply' (x y : ğ•œ') : mul ğ•œ ğ•œ' x y = x * y :=
  rfl
#align continuous_linear_map.mul_apply' ContinuousLinearMap.mul_apply'

@[simp]
theorem opNorm_mul_apply_le (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€– â‰¤ â€–xâ€– :=
  opNorm_le_bound _ (norm_nonneg x) (norm_mul_le x)
#align continuous_linear_map.op_norm_mul_apply_le ContinuousLinearMap.opNorm_mul_apply_le

@[deprecated] alias op_norm_mul_apply_le := opNorm_mul_apply_le -- deprecated on 2024-02-02

theorem opNorm_mul_le : â€–mul ğ•œ ğ•œ'â€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _
#align continuous_linear_map.op_norm_mul_le ContinuousLinearMap.opNorm_mul_le

@[deprecated] alias op_norm_mul_le := opNorm_mul_le -- deprecated on 2024-02-02

/-- Multiplication on the left in a non-unital normed algebra `ğ•œ'` as a non-unital algebra
homomorphism into the algebra of *continuous* linear maps. This is the left regular representation
of `A` acting on itself.

This has more algebraic structure than `ContinuousLinearMap.mul`, but there is no longer continuity
bundled in the first coordinate.  An alternative viewpoint is that this upgrades
`NonUnitalAlgHom.lmul` from a homomorphism into linear maps to a homomorphism into *continuous*
linear maps. -/
def _root_.NonUnitalAlgHom.Lmul : ğ•œ' â†’â‚™â‚[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  { mul ğ•œ ğ•œ' with
    map_mul' := fun _ _ â†¦ ext fun _ â†¦ mul_assoc _ _ _
    map_zero' := ext fun _ â†¦ zero_mul _ }

variable {ğ•œ ğ•œ'} in
@[simp]
theorem _root_.NonUnitalAlgHom.coe_Lmul : â‡‘(NonUnitalAlgHom.Lmul ğ•œ ğ•œ') = mul ğ•œ ğ•œ' :=
  rfl

/-- Simultaneous left- and right-multiplication in a non-unital normed algebra, considered as a
continuous trilinear map. This is akin to its non-continuous version `LinearMap.mulLeftRight`,
but there is a minor difference: `LinearMap.mulLeftRight` is uncurried. -/
def mulLeftRight : ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  ((compL ğ•œ ğ•œ' ğ•œ' ğ•œ').comp (mul ğ•œ ğ•œ').flip).flip.comp (mul ğ•œ ğ•œ')
#align continuous_linear_map.mul_left_right ContinuousLinearMap.mulLeftRight

@[simp]
theorem mulLeftRight_apply (x y z : ğ•œ') : mulLeftRight ğ•œ ğ•œ' x y z = x * z * y :=
  rfl
#align continuous_linear_map.mul_left_right_apply ContinuousLinearMap.mulLeftRight_apply

theorem opNorm_mulLeftRight_apply_apply_le (x y : ğ•œ') : â€–mulLeftRight ğ•œ ğ•œ' x yâ€– â‰¤ â€–xâ€– * â€–yâ€– :=
  (opNorm_comp_le _ _).trans <|
    (mul_comm _ _).trans_le <|
      mul_le_mul (opNorm_mul_apply_le _ _ _)
        (opNorm_le_bound _ (norm_nonneg _) fun _ => (norm_mul_le _ _).trans_eq (mul_comm _ _))
        (norm_nonneg _) (norm_nonneg _)
#align continuous_linear_map.op_norm_mul_left_right_apply_apply_le ContinuousLinearMap.opNorm_mulLeftRight_apply_apply_le

@[deprecated] -- deprecated on 2024-02-02
alias op_norm_mulLeftRight_apply_apply_le :=
  opNorm_mulLeftRight_apply_apply_le

theorem opNorm_mulLeftRight_apply_le (x : ğ•œ') : â€–mulLeftRight ğ•œ ğ•œ' xâ€– â‰¤ â€–xâ€– :=
  opNorm_le_bound _ (norm_nonneg x) (opNorm_mulLeftRight_apply_apply_le ğ•œ ğ•œ' x)
#align continuous_linear_map.op_norm_mul_left_right_apply_le ContinuousLinearMap.opNorm_mulLeftRight_apply_le

@[deprecated] alias op_norm_mulLeftRight_apply_le := opNorm_mulLeftRight_apply_le -- 2024-02-02

#adaptation_note
/--
Before https://github.com/leanprover/lean4/pull/4119
we had to create a local instance in the signature:
```
letI : Norm (ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ') :=
  hasOpNorm (ğ•œâ‚‚ := ğ•œ) (E := ğ•œ') (F := ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ')
```
-/
set_option maxSynthPendingDepth 2 in
theorem opNorm_mulLeftRight_le :
    â€–mulLeftRight ğ•œ ğ•œ'â€– â‰¤ 1 :=
  opNorm_le_bound _ zero_le_one fun x => (one_mul â€–xâ€–).symm â–¸ opNorm_mulLeftRight_apply_le ğ•œ ğ•œ' x
#align continuous_linear_map.op_norm_mul_left_right_le ContinuousLinearMap.opNorm_mulLeftRight_le

@[deprecated] alias op_norm_mulLeftRight_le := opNorm_mulLeftRight_le -- deprecated on 2024-02-02

/-- This is a mixin class for non-unital normed algebras which states that the left-regular
representation of the algebra on itself is isometric. Every unital normed algebra with `â€–1â€– = 1` is
a regular normed algebra (see `NormedAlgebra.instRegularNormedAlgebra`). In addition, so is every
Câ‹†-algebra, non-unital included (see `CstarRing.instRegularNormedAlgebra`), but there are yet other
examples. Any algebra with an approximate identity (e.g., $$L^1$$) is also regular.

This is a useful class because it gives rise to a nice norm on the unitization; in particular it is
a Câ‹†-norm when the norm on `A` is a Câ‹†-norm. -/
class _root_.RegularNormedAlgebra : Prop :=
  /-- The left regular representation of the algebra on itself is an isometry. -/
  isometry_mul' : Isometry (mul ğ•œ ğ•œ')

/-- Every (unital) normed algebra such that `â€–1â€– = 1` is a `RegularNormedAlgebra`. -/
instance _root_.NormedAlgebra.instRegularNormedAlgebra {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ]
    [SeminormedRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormOneClass ğ•œ'] : RegularNormedAlgebra ğ•œ ğ•œ' where
  isometry_mul' := AddMonoidHomClass.isometry_of_norm (mul ğ•œ ğ•œ') <|
    fun x => le_antisymm (opNorm_mul_apply_le _ _ _) <| by
      convert ratio_le_opNorm ((mul ğ•œ ğ•œ') x) (1 : ğ•œ')
      simp [norm_one]

variable [RegularNormedAlgebra ğ•œ ğ•œ']

lemma isometry_mul : Isometry (mul ğ•œ ğ•œ') :=
  RegularNormedAlgebra.isometry_mul'

@[simp]
lemma opNorm_mul_apply (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€– = â€–xâ€– :=
  (AddMonoidHomClass.isometry_iff_norm (mul ğ•œ ğ•œ')).mp (isometry_mul ğ•œ ğ•œ') x
#align continuous_linear_map.op_norm_mul_apply ContinuousLinearMap.opNorm_mul_applyâ‚“

@[deprecated] alias op_norm_mul_apply := opNorm_mul_apply -- deprecated on 2024-02-02

@[simp]
lemma opNNNorm_mul_apply (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| opNorm_mul_apply ğ•œ ğ•œ' x

@[deprecated] alias op_nnnorm_mul_apply := opNNNorm_mul_apply -- deprecated on 2024-02-02

/-- Multiplication in a normed algebra as a linear isometry to the space of
continuous linear maps. -/
def mulâ‚—áµ¢ : ğ•œ' â†’â‚—áµ¢[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' where
  toLinearMap := mul ğ•œ ğ•œ'
  norm_map' x := opNorm_mul_apply ğ•œ ğ•œ' x
#align continuous_linear_map.mulâ‚—áµ¢ ContinuousLinearMap.mulâ‚—áµ¢â‚“

@[simp]
theorem coe_mulâ‚—áµ¢ : â‡‘(mulâ‚—áµ¢ ğ•œ ğ•œ') = mul ğ•œ ğ•œ' :=
  rfl
#align continuous_linear_map.coe_mulâ‚—áµ¢ ContinuousLinearMap.coe_mulâ‚—áµ¢â‚“

end NonUnital

section RingEquiv

variable (ğ•œ E)

/-- If `M` is a normed space over `ğ•œ`, then the space of maps `ğ•œ â†’L[ğ•œ] M` is linearly equivalent
to `M`. (See `ring_lmap_equiv_self` for a stronger statement.) -/
def ring_lmap_equiv_selfâ‚— : (ğ•œ â†’L[ğ•œ] E) â‰ƒâ‚—[ğ•œ] E where
  toFun := fun f â†¦ f 1
  invFun := (ContinuousLinearMap.id ğ•œ ğ•œ).smulRight
  map_smul' := fun a f â†¦ by simp only [coe_smul', Pi.smul_apply, RingHom.id_apply]
  map_add' := fun f g â†¦ by simp only [add_apply]
  left_inv := fun f â†¦ by ext; simp only [smulRight_apply, coe_id', _root_.id, one_smul]
  right_inv := fun m â†¦ by simp only [smulRight_apply, id_apply, one_smul]

/-- If `M` is a normed space over `ğ•œ`, then the space of maps `ğ•œ â†’L[ğ•œ] M` is linearly isometrically
equivalent to `M`. -/
def ring_lmap_equiv_self : (ğ•œ â†’L[ğ•œ] E) â‰ƒâ‚—áµ¢[ğ•œ] E where
  toLinearEquiv := ring_lmap_equiv_selfâ‚— ğ•œ E
  norm_map' := by
    refine fun f â†¦ le_antisymm ?_ ?_
    Â· simpa only [norm_one, mul_one] using le_opNorm f 1
    Â· refine opNorm_le_bound' f (norm_nonneg <| f 1) (fun x _ â†¦ ?_)
      rw [(by rw [smul_eq_mul, mul_one] : f x = f (x â€¢ 1)), ContinuousLinearMap.map_smul,
        norm_smul, mul_comm, (by rfl : ring_lmap_equiv_selfâ‚— ğ•œ E f = f 1)]

end RingEquiv

end MultiplicationLinear

section SMulLinear

variable (ğ•œ) (ğ•œ' : Type*) [NormedField ğ•œ']
variable [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]

/-- Scalar multiplication as a continuous bilinear map. -/
def lsmul : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E :=
  ((Algebra.lsmul ğ•œ ğ•œ E).toLinearMap : ğ•œ' â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] E).mkContinuousâ‚‚ 1 fun c x => by
    simpa only [one_mul] using norm_smul_le c x
#align continuous_linear_map.lsmul ContinuousLinearMap.lsmul

@[simp]
theorem lsmul_apply (c : ğ•œ') (x : E) : lsmul ğ•œ ğ•œ' c x = c â€¢ x :=
  rfl
#align continuous_linear_map.lsmul_apply ContinuousLinearMap.lsmul_apply

variable {ğ•œ'}

theorem norm_toSpanSingleton (x : E) : â€–toSpanSingleton ğ•œ xâ€– = â€–xâ€– := by
  refine opNorm_eq_of_bounds (norm_nonneg _) (fun x => ?_) fun N _ h => ?_
  Â· rw [toSpanSingleton_apply, norm_smul, mul_comm]
  Â· specialize h 1
    rw [toSpanSingleton_apply, norm_smul, mul_comm] at h
    exact (mul_le_mul_right (by simp)).mp h
#align continuous_linear_map.norm_to_span_singleton ContinuousLinearMap.norm_toSpanSingleton

variable {ğ•œ}

theorem opNorm_lsmul_apply_le (x : ğ•œ') : â€–(lsmul ğ•œ ğ•œ' x : E â†’L[ğ•œ] E)â€– â‰¤ â€–xâ€– :=
  ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg x) fun y => norm_smul_le x y
#align continuous_linear_map.op_norm_lsmul_apply_le ContinuousLinearMap.opNorm_lsmul_apply_le

@[deprecated] alias op_norm_lsmul_apply_le := opNorm_lsmul_apply_le -- deprecated on 2024-02-02

/-- The norm of `lsmul` is at most 1 in any semi-normed group. -/
theorem opNorm_lsmul_le : â€–(lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– â‰¤ 1 := by
  refine ContinuousLinearMap.opNorm_le_bound _ zero_le_one fun x => ?_
  simp_rw [one_mul]
  exact opNorm_lsmul_apply_le _
#align continuous_linear_map.op_norm_lsmul_le ContinuousLinearMap.opNorm_lsmul_le

@[deprecated] alias op_norm_lsmul_le := opNorm_lsmul_le -- deprecated on 2024-02-02

end SMulLinear

end ContinuousLinearMap

end SemiNormed

section Normed

namespace ContinuousLinearMap

variable [NormedAddCommGroup E] [NormedSpace ğ•œ E] (c : ğ•œ)
variable (ğ•œ) (ğ•œ' : Type*)

section

variable [NonUnitalNormedRing ğ•œ'] [NormedSpace ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ']
variable [SMulCommClass ğ•œ ğ•œ' ğ•œ'] [RegularNormedAlgebra ğ•œ ğ•œ'] [Nontrivial ğ•œ']

@[simp]
theorem opNorm_mul : â€–mul ğ•œ ğ•œ'â€– = 1 :=
  (mulâ‚—áµ¢ ğ•œ ğ•œ').norm_toContinuousLinearMap
#align continuous_linear_map.op_norm_mul ContinuousLinearMap.opNorm_mulâ‚“

@[deprecated] alias op_norm_mul := opNorm_mul -- deprecated on 2024-02-02

@[simp]
theorem opNNNorm_mul : â€–mul ğ•œ ğ•œ'â€–â‚Š = 1 :=
  Subtype.ext <| opNorm_mul ğ•œ ğ•œ'
#align continuous_linear_map.op_nnnorm_mul ContinuousLinearMap.opNNNorm_mulâ‚“

@[deprecated] alias op_nnnorm_mul := opNNNorm_mul -- deprecated on 2024-02-02

end

/-- The norm of `lsmul` equals 1 in any nontrivial normed group.

This is `ContinuousLinearMap.opNorm_lsmul_le` as an equality. -/
@[simp]
theorem opNorm_lsmul [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' E]
    [IsScalarTower ğ•œ ğ•œ' E] [Nontrivial E] : â€–(lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– = 1 := by
  refine ContinuousLinearMap.opNorm_eq_of_bounds zero_le_one (fun x => ?_) fun N _ h => ?_
  Â· rw [one_mul]
    apply opNorm_lsmul_apply_le
  obtain âŸ¨y, hyâŸ© := exists_ne (0 : E)
  have := le_of_opNorm_le _ (h 1) y
  simp_rw [lsmul_apply, one_smul, norm_one, mul_one] at this
  refine le_of_mul_le_mul_right ?_ (norm_pos_iff.mpr hy)
  simp_rw [one_mul, this]
#align continuous_linear_map.op_norm_lsmul ContinuousLinearMap.opNorm_lsmul

@[deprecated] alias op_norm_lsmul := opNorm_lsmul -- deprecated on 2024-02-02

end ContinuousLinearMap

end Normed
