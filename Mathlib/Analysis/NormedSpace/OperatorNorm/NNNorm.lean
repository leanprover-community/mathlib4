/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic

/-!
# Operator norm as an `NNNorm`

Operator norm as an `NNNorm`, i.e. taking values in non-negative reals.

-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped Classical NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E Eâ‚— F Fâ‚— G Gâ‚— ğ“• : Type*}

section SemiNormed

open Metric ContinuousLinearMap

variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup Eâ‚—] [SeminormedAddCommGroup F]
  [SeminormedAddCommGroup Fâ‚—] [SeminormedAddCommGroup G] [SeminormedAddCommGroup Gâ‚—]

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ Eâ‚—] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Fâ‚—] [NormedSpace ğ•œâ‚ƒ G]
  [NormedSpace ğ•œ Gâ‚—] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

variable [FunLike ğ“• E F]

namespace ContinuousLinearMap

section OpNorm

open Set Real

section

variable [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ] (f g : E â†’SL[Ïƒâ‚â‚‚] F) (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G)
  (x : E)

theorem nnnorm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€–â‚Š = sInf { c | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š } := by
  ext
  rw [NNReal.coe_sInf, coe_nnnorm, norm_def, NNReal.coe_image]
  simp_rw [â† NNReal.coe_le_coe, NNReal.coe_mul, coe_nnnorm, mem_setOf_eq, NNReal.coe_mk,
    exists_prop]
#align continuous_linear_map.nnnorm_def ContinuousLinearMap.nnnorm_def

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem opNNNorm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) : â€–fâ€–â‚Š â‰¤ M :=
  opNorm_le_bound f (zero_le M) hM
#align continuous_linear_map.op_nnnorm_le_bound ContinuousLinearMap.opNNNorm_le_bound

@[deprecated] alias op_nnnorm_le_bound := opNNNorm_le_bound -- deprecated on 2024-02-02

/-- If one controls the norm of every `A x`, `â€–xâ€–â‚Š â‰  0`, then one controls the norm of `A`. -/
theorem opNNNorm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–xâ€–â‚Š â‰  0 â†’ â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) :
    â€–fâ€–â‚Š â‰¤ M :=
  opNorm_le_bound' f (zero_le M) fun x hx => hM x <| by rwa [â† NNReal.coe_ne_zero]
#align continuous_linear_map.op_nnnorm_le_bound' ContinuousLinearMap.opNNNorm_le_bound'

@[deprecated] alias op_nnnorm_le_bound' := opNNNorm_le_bound' -- deprecated on 2024-02-02

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€–â‚Š = 1`, then
one controls the norm of `f`. -/
theorem opNNNorm_le_of_unit_nnnorm [NormedSpace â„ E] [NormedSpace â„ F] {f : E â†’L[â„] F} {C : â„â‰¥0}
    (hf : âˆ€ x, â€–xâ€–â‚Š = 1 â†’ â€–f xâ€–â‚Š â‰¤ C) : â€–fâ€–â‚Š â‰¤ C :=
  opNorm_le_of_unit_norm C.coe_nonneg fun x hx => hf x <| by rwa [â† NNReal.coe_eq_one]
#align continuous_linear_map.op_nnnorm_le_of_unit_nnnorm ContinuousLinearMap.opNNNorm_le_of_unit_nnnorm

@[deprecated] alias op_nnnorm_le_of_unit_nnnorm := opNNNorm_le_of_unit_nnnorm -- 2024-02-02

theorem opNNNorm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : LipschitzWith K f) :
    â€–fâ€–â‚Š â‰¤ K :=
  opNorm_le_of_lipschitz hf
#align continuous_linear_map.op_nnnorm_le_of_lipschitz ContinuousLinearMap.opNNNorm_le_of_lipschitz

@[deprecated] alias op_nnnorm_le_of_lipschitz := opNNNorm_le_of_lipschitz -- 2024-02-02

theorem opNNNorm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} (M : â„â‰¥0) (h_above : âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š)
    (h_below : âˆ€ N, (âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ N * â€–xâ€–â‚Š) â†’ M â‰¤ N) : â€–Ï†â€–â‚Š = M :=
  Subtype.ext <| opNorm_eq_of_bounds (zero_le M) h_above <| Subtype.forall'.mpr h_below
#align continuous_linear_map.op_nnnorm_eq_of_bounds ContinuousLinearMap.opNNNorm_eq_of_bounds

@[deprecated] alias op_nnnorm_eq_of_bounds := opNNNorm_eq_of_bounds -- 2024-02-02

theorem opNNNorm_le_iff {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„â‰¥0} : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š :=
  opNorm_le_iff C.2

@[deprecated] alias op_nnnorm_le_iff := opNNNorm_le_iff -- 2024-02-02

theorem isLeast_opNNNorm : IsLeast {C : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š} â€–fâ€–â‚Š := by
  simpa only [â† opNNNorm_le_iff] using isLeast_Ici

@[deprecated] alias isLeast_op_nnnorm := isLeast_opNNNorm -- deprecated on 2024-02-02

theorem opNNNorm_comp_le [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€–â‚Š â‰¤ â€–hâ€–â‚Š * â€–fâ€–â‚Š :=
  opNorm_comp_le h f
#align continuous_linear_map.op_nnnorm_comp_le ContinuousLinearMap.opNNNorm_comp_le

@[deprecated] alias op_nnnorm_comp_le := opNNNorm_comp_le -- deprecated on 2024-02-02

theorem le_opNNNorm : â€–f xâ€–â‚Š â‰¤ â€–fâ€–â‚Š * â€–xâ€–â‚Š :=
  f.le_opNorm x
#align continuous_linear_map.le_op_nnnorm ContinuousLinearMap.le_opNNNorm

@[deprecated] alias le_op_nnnorm := le_opNNNorm -- deprecated on 2024-02-02

theorem nndist_le_opNNNorm (x y : E) : nndist (f x) (f y) â‰¤ â€–fâ€–â‚Š * nndist x y :=
  dist_le_opNorm f x y
#align continuous_linear_map.nndist_le_op_nnnorm ContinuousLinearMap.nndist_le_opNNNorm

@[deprecated] alias nndist_le_op_nnnorm := nndist_le_opNNNorm -- deprecated on 2024-02-02

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz : LipschitzWith â€–fâ€–â‚Š f :=
  AddMonoidHomClass.lipschitz_of_bound_nnnorm f _ f.le_opNNNorm
#align continuous_linear_map.lipschitz ContinuousLinearMap.lipschitz

/-- Evaluation of a continuous linear map `f` at a point is Lipschitz continuous in `f`. -/
theorem lipschitz_apply (x : E) : LipschitzWith â€–xâ€–â‚Š fun f : E â†’SL[Ïƒâ‚â‚‚] F => f x :=
  lipschitzWith_iff_norm_sub_le.2 fun f g => ((f - g).le_opNorm x).trans_eq (mul_comm _ _)
#align continuous_linear_map.lipschitz_apply ContinuousLinearMap.lipschitz_apply

end

section Sup

variable [RingHomIsometric Ïƒâ‚â‚‚]

theorem exists_mul_lt_apply_of_lt_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0} (hr : r < â€–fâ€–â‚Š) :
    âˆƒ x, r * â€–xâ€–â‚Š < â€–f xâ€–â‚Š := by
  simpa only [not_forall, not_le, Set.mem_setOf] using
    not_mem_of_lt_csInf (nnnorm_def f â–¸ hr : r < sInf { c : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š })
      (OrderBot.bddBelow _)
#align continuous_linear_map.exists_mul_lt_apply_of_lt_op_nnnorm ContinuousLinearMap.exists_mul_lt_apply_of_lt_opNNNorm

@[deprecated] -- deprecated on 2024-02-02
alias exists_mul_lt_apply_of_lt_op_nnnorm := exists_mul_lt_apply_of_lt_opNNNorm

theorem exists_mul_lt_of_lt_opNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„} (hrâ‚€ : 0 â‰¤ r) (hr : r < â€–fâ€–) :
    âˆƒ x, r * â€–xâ€– < â€–f xâ€– := by
  lift r to â„â‰¥0 using hrâ‚€
  exact f.exists_mul_lt_apply_of_lt_opNNNorm hr
#align continuous_linear_map.exists_mul_lt_of_lt_op_norm ContinuousLinearMap.exists_mul_lt_of_lt_opNorm

@[deprecated] alias exists_mul_lt_of_lt_op_norm := exists_mul_lt_of_lt_opNorm -- 2024-02-02

theorem exists_lt_apply_of_lt_opNNNorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0}
    (hr : r < â€–fâ€–â‚Š) : âˆƒ x : E, â€–xâ€–â‚Š < 1 âˆ§ r < â€–f xâ€–â‚Š := by
  obtain âŸ¨y, hyâŸ© := f.exists_mul_lt_apply_of_lt_opNNNorm hr
  have hy' : â€–yâ€–â‚Š â‰  0 :=
    nnnorm_ne_zero_iff.2 fun heq => by
      simp [heq, nnnorm_zero, map_zero, not_lt_zero'] at hy
  have hfy : â€–f yâ€–â‚Š â‰  0 := (zero_le'.trans_lt hy).ne'
  rw [â† inv_inv â€–f yâ€–â‚Š, NNReal.lt_inv_iff_mul_lt (inv_ne_zero hfy), mul_assoc, mul_comm â€–yâ€–â‚Š, â†
    mul_assoc, â† NNReal.lt_inv_iff_mul_lt hy'] at hy
  obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© := NormedField.exists_lt_nnnorm_lt ğ•œ hy
  refine âŸ¨k â€¢ y, (nnnorm_smul k y).symm â–¸ (NNReal.lt_inv_iff_mul_lt hy').1 hkâ‚‚, ?_âŸ©
  have : â€–Ïƒâ‚â‚‚ kâ€–â‚Š = â€–kâ€–â‚Š := Subtype.ext RingHomIsometric.is_iso
  rwa [map_smulâ‚›â‚— f, nnnorm_smul, â† NNReal.div_lt_iff hfy, div_eq_mul_inv, this]
#align continuous_linear_map.exists_lt_apply_of_lt_op_nnnorm ContinuousLinearMap.exists_lt_apply_of_lt_opNNNorm

@[deprecated] alias exists_lt_apply_of_lt_op_nnnorm := exists_lt_apply_of_lt_opNNNorm -- 2024-02-02

theorem exists_lt_apply_of_lt_opNorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„}
    (hr : r < â€–fâ€–) : âˆƒ x : E, â€–xâ€– < 1 âˆ§ r < â€–f xâ€– := by
  by_cases hrâ‚€ : r < 0
  Â· exact âŸ¨0, by simpa using hrâ‚€âŸ©
  Â· lift r to â„â‰¥0 using not_lt.1 hrâ‚€
    exact f.exists_lt_apply_of_lt_opNNNorm hr
#align continuous_linear_map.exists_lt_apply_of_lt_op_norm ContinuousLinearMap.exists_lt_apply_of_lt_opNorm

@[deprecated] alias exists_lt_apply_of_lt_op_norm := exists_lt_apply_of_lt_opNorm -- 2024-02-02

theorem sSup_unit_ball_eq_nnnorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' ball 0 1) = â€–fâ€–â‚Š := by
  refine csSup_eq_of_forall_le_of_forall_lt_exists_gt ((nonempty_ball.mpr zero_lt_one).image _) ?_
    fun ub hub => ?_
  Â· rintro - âŸ¨x, hx, rflâŸ©
    simpa only [mul_one] using f.le_opNorm_of_le (mem_ball_zero_iff.1 hx).le
  Â· obtain âŸ¨x, hx, hxfâŸ© := f.exists_lt_apply_of_lt_opNNNorm hub
    exact âŸ¨_, âŸ¨x, mem_ball_zero_iff.2 hx, rflâŸ©, hxfâŸ©
#align continuous_linear_map.Sup_unit_ball_eq_nnnorm ContinuousLinearMap.sSup_unit_ball_eq_nnnorm

theorem sSup_unit_ball_eq_norm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E] [SeminormedAddCommGroup F]
    [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [NormedSpace ğ•œ E]
    [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' ball 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using NNReal.coe_inj.2 f.sSup_unit_ball_eq_nnnorm
#align continuous_linear_map.Sup_unit_ball_eq_norm ContinuousLinearMap.sSup_unit_ball_eq_norm

theorem sSup_closed_unit_ball_eq_nnnorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' closedBall 0 1) = â€–fâ€–â‚Š := by
  have hbdd : âˆ€ y âˆˆ (fun x => â€–f xâ€–â‚Š) '' closedBall 0 1, y â‰¤ â€–fâ€–â‚Š := by
    rintro - âŸ¨x, hx, rflâŸ©
    exact f.unit_le_opNorm x (mem_closedBall_zero_iff.1 hx)
  refine le_antisymm (csSup_le ((nonempty_closedBall.mpr zero_le_one).image _) hbdd) ?_
  rw [â† sSup_unit_ball_eq_nnnorm]
  exact csSup_le_csSup âŸ¨â€–fâ€–â‚Š, hbddâŸ© ((nonempty_ball.2 zero_lt_one).image _)
    (Set.image_subset _ ball_subset_closedBall)
#align continuous_linear_map.Sup_closed_unit_ball_eq_nnnorm ContinuousLinearMap.sSup_closed_unit_ball_eq_nnnorm

theorem sSup_closed_unit_ball_eq_norm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' closedBall 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using
    NNReal.coe_inj.2 f.sSup_closed_unit_ball_eq_nnnorm
#align continuous_linear_map.Sup_closed_unit_ball_eq_norm ContinuousLinearMap.sSup_closed_unit_ball_eq_norm

end Sup

end OpNorm

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚â‚‚]
variable (e : E â‰ƒSL[Ïƒâ‚â‚‚] F)

protected theorem lipschitz : LipschitzWith â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€–â‚Š e :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).lipschitz
#align continuous_linear_equiv.lipschitz ContinuousLinearEquiv.lipschitz

end ContinuousLinearEquiv

end SemiNormed
