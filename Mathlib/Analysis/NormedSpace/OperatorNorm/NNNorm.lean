/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Analysis.Normed.Module.RCLike.Real

/-!
# Operator norm as an `NNNorm`

Operator norm as an `NNNorm`, i.e. taking values in non-negative reals.

-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped NNReal Topology Uniformity
open Metric ContinuousLinearMap
open Set Real

variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E F G : Type*}

section NontriviallySemiNormed

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [SeminormedAddCommGroup G]
variable [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G]
variable {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]
variable [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚ƒ]

namespace ContinuousLinearMap

theorem nnnorm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€–â‚Š = sInf { c | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š } := by
  ext
  rw [NNReal.coe_sInf, coe_nnnorm, norm_def, NNReal.coe_image]
  simp_rw [â† NNReal.coe_le_coe, NNReal.coe_mul, coe_nnnorm, mem_setOf_eq, NNReal.coe_mk,
    exists_prop]

@[simp, nontriviality]
theorem opNNNorm_subsingleton [Subsingleton E] (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€–â‚Š = 0 :=
  NNReal.eq <| f.opNorm_subsingleton

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem opNNNorm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) : â€–fâ€–â‚Š â‰¤ M :=
  opNorm_le_bound f (zero_le M) hM

/-- If one controls the norm of every `A x`, `â€–xâ€–â‚Š â‰  0`, then one controls the norm of `A`. -/
theorem opNNNorm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–xâ€–â‚Š â‰  0 â†’ â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) :
    â€–fâ€–â‚Š â‰¤ M :=
  opNorm_le_bound' f (zero_le M) fun x hx => hM x <| by rwa [â† NNReal.coe_ne_zero]

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€–â‚Š = 1`, then
one controls the norm of `f`. -/
theorem opNNNorm_le_of_unit_nnnorm [NormedAlgebra â„ ğ•œ] {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„â‰¥0}
    (hf : âˆ€ x, â€–xâ€–â‚Š = 1 â†’ â€–f xâ€–â‚Š â‰¤ C) : â€–fâ€–â‚Š â‰¤ C :=
  opNorm_le_of_unit_norm C.coe_nonneg fun x hx => hf x <| by rwa [â† NNReal.coe_eq_one]

theorem opNNNorm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : LipschitzWith K f) :
    â€–fâ€–â‚Š â‰¤ K :=
  opNorm_le_of_lipschitz hf

theorem opNNNorm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} (M : â„â‰¥0) (h_above : âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š)
    (h_below : âˆ€ N, (âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ N * â€–xâ€–â‚Š) â†’ M â‰¤ N) : â€–Ï†â€–â‚Š = M :=
  Subtype.ext <| opNorm_eq_of_bounds (zero_le M) h_above <| Subtype.forall'.mpr h_below

theorem opNNNorm_le_iff {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„â‰¥0} : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š :=
  opNorm_le_iff C.2

theorem isLeast_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) : IsLeast {C : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š} â€–fâ€–â‚Š := by
  simpa only [â† opNNNorm_le_iff] using isLeast_Ici

theorem opNNNorm_comp_le (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€–â‚Š â‰¤ â€–hâ€–â‚Š * â€–fâ€–â‚Š :=
  opNorm_comp_le h f

lemma opENorm_comp_le (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€–â‚‘ â‰¤ â€–hâ€–â‚‘ * â€–fâ€–â‚‘ := by
  simpa [enorm, â† ENNReal.coe_mul] using opNNNorm_comp_le h f

theorem le_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) (x : E) : â€–f xâ€–â‚Š â‰¤ â€–fâ€–â‚Š * â€–xâ€–â‚Š :=
  f.le_opNorm x

lemma le_opENorm (f : E â†’SL[Ïƒâ‚â‚‚] F) (x : E) : â€–f xâ€–â‚‘ â‰¤ â€–fâ€–â‚‘ * â€–xâ€–â‚‘ := by
  dsimp [enorm]; exact mod_cast le_opNNNorm ..

theorem nndist_le_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) (x y : E) : nndist (f x) (f y) â‰¤ â€–fâ€–â‚Š * nndist x y :=
  dist_le_opNorm f x y

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz (f : E â†’SL[Ïƒâ‚â‚‚] F) : LipschitzWith â€–fâ€–â‚Š f :=
  AddMonoidHomClass.lipschitz_of_bound_nnnorm f _ f.le_opNNNorm

/-- Evaluation of a continuous linear map `f` at a point is Lipschitz continuous in `f`. -/
theorem lipschitz_apply (x : E) : LipschitzWith â€–xâ€–â‚Š fun f : E â†’SL[Ïƒâ‚â‚‚] F => f x :=
  lipschitzWith_iff_norm_sub_le.2 fun f g => ((f - g).le_opNorm x).trans_eq (mul_comm _ _)

theorem exists_mul_lt_apply_of_lt_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0} (hr : r < â€–fâ€–â‚Š) :
    âˆƒ x, r * â€–xâ€–â‚Š < â€–f xâ€–â‚Š := by
  simpa only [not_forall, not_le, Set.mem_setOf] using
    notMem_of_lt_csInf (nnnorm_def f â–¸ hr : r < sInf { c : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š })
      (OrderBot.bddBelow _)

theorem exists_mul_lt_of_lt_opNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„} (hrâ‚€ : 0 â‰¤ r) (hr : r < â€–fâ€–) :
    âˆƒ x, r * â€–xâ€– < â€–f xâ€– := by
  lift r to â„â‰¥0 using hrâ‚€
  exact f.exists_mul_lt_apply_of_lt_opNNNorm hr

end ContinuousLinearMap

namespace ContinuousLinearEquiv
variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

protected theorem lipschitz (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) : LipschitzWith â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€–â‚Š e :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).lipschitz

end ContinuousLinearEquiv

end NontriviallySemiNormed

section DenselyNormedDomain
variable [NormedAddCommGroup E] [SeminormedAddCommGroup F]
variable [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚]
variable [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [RingHomIsometric Ïƒâ‚â‚‚]

namespace ContinuousLinearMap

theorem exists_lt_apply_of_lt_opNNNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0}
    (hr : r < â€–fâ€–â‚Š) : âˆƒ x : E, â€–xâ€–â‚Š < 1 âˆ§ r < â€–f xâ€–â‚Š := by
  obtain âŸ¨y, hyâŸ© := f.exists_mul_lt_apply_of_lt_opNNNorm hr
  have hy' : â€–yâ€–â‚Š â‰  0 :=
    nnnorm_ne_zero_iff.2 fun heq => by
      simp [heq, nnnorm_zero, map_zero] at hy
  have hfy : â€–f yâ€–â‚Š â‰  0 := (zero_le'.trans_lt hy).ne'
  rw [â† inv_inv â€–f yâ€–â‚Š, NNReal.lt_inv_iff_mul_lt (inv_ne_zero hfy), mul_assoc, mul_comm â€–yâ€–â‚Š, â†
    mul_assoc, â† NNReal.lt_inv_iff_mul_lt hy'] at hy
  obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© := NormedField.exists_lt_nnnorm_lt ğ•œ hy
  refine âŸ¨k â€¢ y, (nnnorm_smul k y).symm â–¸ (NNReal.lt_inv_iff_mul_lt hy').1 hkâ‚‚, ?_âŸ©
  rwa [map_smulâ‚›â‚— f, nnnorm_smul, â† div_lt_iffâ‚€ hfy.bot_lt, div_eq_mul_inv,
    RingHomIsometric.nnnorm_map]

theorem exists_lt_apply_of_lt_opNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„}
    (hr : r < â€–fâ€–) : âˆƒ x : E, â€–xâ€– < 1 âˆ§ r < â€–f xâ€– := by
  by_cases hrâ‚€ : r < 0
  Â· exact âŸ¨0, by simpa using hrâ‚€âŸ©
  Â· lift r to â„â‰¥0 using not_lt.1 hrâ‚€
    exact f.exists_lt_apply_of_lt_opNNNorm hr

theorem sSup_unit_ball_eq_nnnorm (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' ball 0 1) = â€–fâ€–â‚Š := by
  refine csSup_eq_of_forall_le_of_forall_lt_exists_gt ((nonempty_ball.mpr zero_lt_one).image _) ?_
    fun ub hub => ?_
  Â· rintro - âŸ¨x, hx, rflâŸ©
    simpa only [mul_one] using f.le_opNorm_of_le (mem_ball_zero_iff.1 hx).le
  Â· obtain âŸ¨x, hx, hxfâŸ© := f.exists_lt_apply_of_lt_opNNNorm hub
    exact âŸ¨_, âŸ¨x, mem_ball_zero_iff.2 hx, rflâŸ©, hxfâŸ©

theorem sSup_unit_ball_eq_norm (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' ball 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using NNReal.coe_inj.2 f.sSup_unit_ball_eq_nnnorm

theorem sSup_unitClosedBall_eq_nnnorm (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' closedBall 0 1) = â€–fâ€–â‚Š := by
  have hbdd : âˆ€ y âˆˆ (fun x => â€–f xâ€–â‚Š) '' closedBall 0 1, y â‰¤ â€–fâ€–â‚Š := by
    rintro - âŸ¨x, hx, rflâŸ©
    exact f.unit_le_opNorm x (mem_closedBall_zero_iff.1 hx)
  refine le_antisymm (csSup_le ((nonempty_closedBall.mpr zero_le_one).image _) hbdd) ?_
  rw [â† sSup_unit_ball_eq_nnnorm]
  gcongr
  exacts [âŸ¨â€–fâ€–â‚Š, hbddâŸ©, ball_subset_closedBall]

theorem sSup_unitClosedBall_eq_norm (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' closedBall 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using
    NNReal.coe_inj.2 f.sSup_unitClosedBall_eq_nnnorm

theorem exists_nnnorm_eq_one_lt_apply_of_lt_opNNNorm [NormedAlgebra â„ ğ•œ]
    (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0} (hr : r < â€–fâ€–â‚Š) :
    âˆƒ x : E, â€–xâ€–â‚Š = 1 âˆ§ r < â€–f xâ€–â‚Š := by
  obtain âŸ¨x, hlt, hrâŸ© := exists_lt_apply_of_lt_opNNNorm f hr
  obtain rfl | hx0 := eq_zero_or_nnnorm_pos x
  Â· simp at hr
  use algebraMap â„ ğ•œ â€–xâ€–â»Â¹ â€¢ x
  suffices r < â€–xâ€–â‚Šâ»Â¹ * â€–f xâ€–â‚Š by simpa [nnnorm_smul, inv_mul_cancelâ‚€ hx0.ne'] using this
  calc
    r < 1â»Â¹ * â€–f xâ€–â‚Š := by simpa
    _ < â€–xâ€–â‚Šâ»Â¹ * â€–f xâ€–â‚Š := by gcongr; exact (zero_le r).trans_lt hr

/-- When the domain is a real normed space, `ContinuousLinearMap.sSup_unitClosedBall_eq_nnnorm` can
be tightened to take the supremum over only the `Metric.sphere`. -/
theorem sSup_sphere_eq_nnnorm [NormedAlgebra â„ ğ•œ] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' Metric.sphere 0 1) = â€–fâ€–â‚Š := by
  cases subsingleton_or_nontrivial E
  Â· simp [sphere_eq_empty_of_subsingleton one_ne_zero]
  have : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ E
  refine csSup_eq_of_forall_le_of_forall_lt_exists_gt
      ((NormedSpace.sphere_nonempty.mpr zero_le_one).image _) ?_ fun ub hub => ?_
  Â· rintro - âŸ¨x, hx, rflâŸ©
    simpa only [mul_one] using f.le_opNorm_of_le (mem_sphere_zero_iff_norm.1 hx).le
  Â· obtain âŸ¨x, hx, hxfâŸ© := f.exists_nnnorm_eq_one_lt_apply_of_lt_opNNNorm hub
    exact âŸ¨_, âŸ¨x, by simpa using congrArg NNReal.toReal hx, rflâŸ©, hxfâŸ©

/-- When the domain is a real normed space, `ContinuousLinearMap.sSup_unitClosedBall_eq_norm` can be
tightened to take the supremum over only the `Metric.sphere`. -/
theorem sSup_sphere_eq_norm [NormedAlgebra â„ ğ•œ] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' Metric.sphere 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using NNReal.coe_inj.2 f.sSup_sphere_eq_nnnorm

end ContinuousLinearMap

end DenselyNormedDomain
