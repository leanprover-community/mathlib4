/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤
-/
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Analysis.NormedSpace.Pointwise

#align_import analysis.normed_space.is_R_or_C from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Normed spaces over R or C

This file is about results on normed spaces over the fields `â„` and `â„‚`.

## Main definitions

None.

## Main theorems

* `ContinuousLinearMap.opNorm_bound_of_ball_bound`: A bound on the norms of values of a linear
  map in a ball yields a bound on the operator norm.

## Notes

This file exists mainly to avoid importing `RCLike` in the main normed space theory files.
-/


open Metric

variable {ğ•œ : Type*} [RCLike ğ•œ] {E : Type*} [NormedAddCommGroup E]

theorem RCLike.norm_coe_norm {z : E} : â€–(â€–zâ€– : ğ•œ)â€– = â€–zâ€– := by simp
#align is_R_or_C.norm_coe_norm RCLike.norm_coe_norm

variable [NormedSpace ğ•œ E]

/-- Lemma to normalize a vector in a normed space `E` over either `â„‚` or `â„` to unit length. -/
@[simp]
theorem norm_smul_inv_norm {x : E} (hx : x â‰  0) : â€–(â€–xâ€–â»Â¹ : ğ•œ) â€¢ xâ€– = 1 := by
  have : â€–xâ€– â‰  0 := by simp [hx]
  field_simp [norm_smul]
#align norm_smul_inv_norm norm_smul_inv_norm

/-- Lemma to normalize a vector in a normed space `E` over either `â„‚` or `â„` to length `r`. -/
theorem norm_smul_inv_norm' {r : â„} (r_nonneg : 0 â‰¤ r) {x : E} (hx : x â‰  0) :
    â€–((r : ğ•œ) * (â€–xâ€– : ğ•œ)â»Â¹) â€¢ xâ€– = r := by
  have : â€–xâ€– â‰  0 := by simp [hx]
  field_simp [norm_smul, r_nonneg, rclike_simps]
#align norm_smul_inv_norm' norm_smul_inv_norm'

theorem LinearMap.bound_of_sphere_bound {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ)
    (h : âˆ€ z âˆˆ sphere (0 : E) r, â€–f zâ€– â‰¤ c) (z : E) : â€–f zâ€– â‰¤ c / r * â€–zâ€– := by
  by_cases z_zero : z = 0
  Â· rw [z_zero]
    simp only [LinearMap.map_zero, norm_zero, mul_zero]
    exact le_rfl
  set zâ‚ := ((r : ğ•œ) * (â€–zâ€– : ğ•œ)â»Â¹) â€¢ z with hzâ‚
  have norm_f_zâ‚ : â€–f zâ‚â€– â‰¤ c := by
    apply h
    rw [mem_sphere_zero_iff_norm]
    exact norm_smul_inv_norm' r_pos.le z_zero
  have r_ne_zero : (r : ğ•œ) â‰  0 := RCLike.ofReal_ne_zero.mpr r_pos.ne'
  have eq : f z = â€–zâ€– / r * f zâ‚ := by
    rw [hzâ‚, LinearMap.map_smul, smul_eq_mul]
    rw [â† mul_assoc, â† mul_assoc, div_mul_cancelâ‚€ _ r_ne_zero, mul_inv_cancel, one_mul]
    simp only [z_zero, RCLike.ofReal_eq_zero, norm_eq_zero, Ne, not_false_iff]
  rw [eq, norm_mul, norm_div, RCLike.norm_coe_norm, RCLike.norm_of_nonneg r_pos.le,
    div_mul_eq_mul_div, div_mul_eq_mul_div, mul_comm]
  apply div_le_div _ _ r_pos rfl.ge
  Â· exact mul_nonneg ((norm_nonneg _).trans norm_f_zâ‚) (norm_nonneg z)
  apply mul_le_mul norm_f_zâ‚ rfl.le (norm_nonneg z) ((norm_nonneg _).trans norm_f_zâ‚)
#align linear_map.bound_of_sphere_bound LinearMap.bound_of_sphere_bound

/-- `LinearMap.bound_of_ball_bound` is a version of this over arbitrary nontrivially normed fields.
It produces a less precise bound so we keep both versions. -/
theorem LinearMap.bound_of_ball_bound' {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] ğ•œ)
    (h : âˆ€ z âˆˆ closedBall (0 : E) r, â€–f zâ€– â‰¤ c) (z : E) : â€–f zâ€– â‰¤ c / r * â€–zâ€– :=
  f.bound_of_sphere_bound r_pos c (fun z hz => h z hz.le) z
#align linear_map.bound_of_ball_bound' LinearMap.bound_of_ball_bound'

theorem ContinuousLinearMap.opNorm_bound_of_ball_bound {r : â„} (r_pos : 0 < r) (c : â„)
    (f : E â†’L[ğ•œ] ğ•œ) (h : âˆ€ z âˆˆ closedBall (0 : E) r, â€–f zâ€– â‰¤ c) : â€–fâ€– â‰¤ c / r := by
  apply ContinuousLinearMap.opNorm_le_bound
  Â· apply div_nonneg _ r_pos.le
    exact
      (norm_nonneg _).trans
        (h 0 (by simp only [norm_zero, mem_closedBall, dist_zero_left, r_pos.le]))
  apply LinearMap.bound_of_ball_bound' r_pos
  exact fun z hz => h z hz
#align continuous_linear_map.op_norm_bound_of_ball_bound ContinuousLinearMap.opNorm_bound_of_ball_bound

@[deprecated] -- 2024-02-02
alias ContinuousLinearMap.op_norm_bound_of_ball_bound :=
  ContinuousLinearMap.opNorm_bound_of_ball_bound

variable (ğ•œ)

theorem NormedSpace.sphere_nonempty_rclike [Nontrivial E] {r : â„} (hr : 0 â‰¤ r) :
    Nonempty (sphere (0 : E) r) :=
  letI : NormedSpace â„ E := NormedSpace.restrictScalars â„ ğ•œ E
  (NormedSpace.sphere_nonempty.mpr hr).coe_sort
#align normed_space.sphere_nonempty_is_R_or_C NormedSpace.sphere_nonempty_rclike
