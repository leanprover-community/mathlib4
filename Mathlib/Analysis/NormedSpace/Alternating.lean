/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov, Heather Macbeth, Patrick Massot
-/
import Mathlib.Topology.Algebra.Module.Alternating.Basic
import Mathlib.Analysis.NormedSpace.Multilinear.Basic

/-!
# Operator norm on the space of continuous alternating maps

-/

noncomputable section

open scoped BigOperators NNReal
open Finset Metric

/-!
### Type variables

We use the following type variables in this file:

* `ğ•œ` : a `NontriviallyNormedField`;
* `Î¹`, `Î¹'` : finite index types with decidable equality;
* `E`, `Eâ‚` : families of normed vector spaces over `ğ•œ` indexed by `i : Î¹`;
* `E'` : a family of normed vector spaces over `ğ•œ` indexed by `i' : Î¹'`;
* `Ei` : a family of normed vector spaces over `ğ•œ` indexed by `i : Fin (Nat.succ n)`;
* `G`, `G'` : normed vector spaces over `ğ•œ`.
-/

universe u v v' wE wEâ‚ wE' wEi wG wG'
variable {ğ•œ : Type u} {n : â„•}
  {E : Type wE} {Eâ‚ : Type wEâ‚} {E' : Type wE'} {Ei : Type wEi}
  {G : Type wG} {G' : Type wG'} {Î¹ : Type v} {Î¹' : Type v'}
  [Fintype Î¹] [Fintype Î¹'] [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup Eâ‚] [NormedSpace ğ•œ Eâ‚]
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  [NormedAddCommGroup Ei] [NormedSpace ğ•œ Ei]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]
  [NormedAddCommGroup G'] [NormedSpace ğ•œ G']

/-!
### Continuity properties of alternating maps

We relate continuity of alternating maps to the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`, in
both directions. Along the way, we prove useful bounds on the difference `â€–f mâ‚ - f mâ‚‚â€–`.
-/
namespace AlternatingMap

variable (f : E [Î›^Î¹]â†’â‚—[ğ•œ] G)

/-- If an alternating map in finitely many variables on normed spaces satisfies the inequality
`â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–` on a shell `Îµ / â€–câ€– < â€–m iâ€– < Îµ` for some positive number `Îµ`
and an elements `c : ğ•œ`, `1 < â€–câ€–`, then it satisfies this inequality for all `m`. -/
lemma bound_of_shell {Îµ : â„} {C : â„} (hÎµ : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
    (hf : âˆ€ m : Î¹ â†’ E, (âˆ€ i, Îµ / â€–câ€– â‰¤ â€–m iâ€–) â†’ (âˆ€ i, â€–m iâ€– < Îµ) â†’ â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–)
    (m : Î¹ â†’ E) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.toMultilinearMap.bound_of_shell (fun _ â†¦ hÎµ) (fun _ â†¦ hc) hf m

/-- If a alternating map in finitely many variables on normed spaces is continuous,
then it satisfies the inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`,
for some `C` which can be chosen to be positive. -/
theorem exists_bound_of_continuous (hf : Continuous f) :
    âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
  f.toMultilinearMap.exists_bound_of_continuous hf

/-- If `f` satisfies a boundedness property around `0`,
one can deduce a bound on `f mâ‚ - f mâ‚‚` using the multilinearity.
Here, we give a precise but hard to use version.
See `AlternatingMap.norm_image_sub_le_of_bound` for a less precise but more usable version.
The bound reads
`â€–f m - f m'â€– â‰¤
  C * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`. -/
lemma norm_image_sub_le_of_bound' [DecidableEq Î¹] {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound' hC H mâ‚ mâ‚‚

/-- If `f` satisfies a boundedness property around `0`,
one can deduce a bound on `f mâ‚ - f mâ‚‚` using the multilinearity.
Here, we give a usable but not very precise version.
See `AlternatingMap.norm_image_sub_le_of_bound'` for a more precise but less usable version.
The bound is `â€–f m - f m'â€– â‰¤ C * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`. -/
lemma norm_image_sub_le_of_bound {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ C * (Fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
  f.toMultilinearMap.norm_image_sub_le_of_bound hC H mâ‚ mâ‚‚

/-- If an alternating map satisfies an inequality `â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–`,
then it is continuous. -/
theorem continuous_of_bound (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
    Continuous f :=
  f.toMultilinearMap.continuous_of_bound C H

/-- Constructing a continuous alternating map from a alternating map
satisfying a boundedness condition. -/
def mkContinuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : E [Î›^Î¹]â†’L[ğ•œ] G :=
  { f with cont := f.continuous_of_bound C H }

@[simp] lemma coe_mk_continuous (C : â„) (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :
    (f.mkContinuous C H : (Î¹ â†’ E) â†’ G) = f :=
  rfl

end AlternatingMap

/-!
### Continuous alternating maps

We define the norm `â€–fâ€–` of a continuous alternating map `f` in finitely many variables as the
smallest number such that `â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€–` for all `m`. We show that this
defines a normed space structure on `ContinuousMultilinearMap ğ•œ E G`.
-/

namespace ContinuousAlternatingMap

variable (c : ğ•œ) (f g : E [Î›^Î¹]â†’L[ğ•œ] G) (m : Î¹ â†’ E)

theorem bound : âˆƒ (C : â„), 0 < C âˆ§ (âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) :=
  f.toContinuousMultilinearMap.bound

instance : NormedAddCommGroup (E [Î›^Î¹]â†’L[ğ•œ] G) :=
  NormedAddCommGroup.induced _ _
    (toMultilinearAddHom : E [Î›^Î¹]â†’L[ğ•œ] G â†’+ _)
    toContinuousMultilinearMap_injective

@[simp] lemma norm_toContinuousMultilinearMap : â€–f.1â€– = â€–fâ€– := rfl

/-- The inclusion of `E [Î›^Î¹]â†’L[ğ•œ] G` into `ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G`
as a linear isometry. -/
@[simps!] def toContinuousMultilinearMapLinearIsometry :
    E [Î›^Î¹]â†’L[ğ•œ] G â†’â‚—áµ¢[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G :=
  { (toContinuousMultilinearMapLinear :
      E [Î›^Î¹]â†’L[ğ•œ] G â†’â‚—[ğ•œ] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G) with
    norm_map' := fun _ â†¦ rfl }

lemma embedding_toContinuousMultilinearMap :
    Embedding (toContinuousMultilinearMap : E [Î›^Î¹]â†’L[ğ•œ] G â†’
      ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G) :=
  toContinuousMultilinearMap_injective.embedding_induced

lemma uniformEmbedding_toContinuousMultilinearMap :
    UniformEmbedding (toContinuousMultilinearMap : E [Î›^Î¹]â†’L[ğ•œ] G â†’
      ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G) :=
  âŸ¨âŸ¨rflâŸ©, toContinuousMultilinearMap_injectiveâŸ©

lemma isClosed_range_toContinuousMultilinearMap :
    IsClosed (Set.range (toContinuousMultilinearMap : E [Î›^Î¹]â†’L[ğ•œ] G â†’
      ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G)) := by
  simp only [range_toContinuousMultilinearMap, Set.setOf_forall]
  repeat apply isClosed_iInter; intro
  exact isClosed_singleton.preimage (ContinuousMultilinearMap.continuous_eval_left _)

lemma closedEmbedding_toContinuousMultilinearMap :
    ClosedEmbedding (toContinuousMultilinearMap : E [Î›^Î¹]â†’L[ğ•œ] G â†’
      ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G) :=
  âŸ¨embedding_toContinuousMultilinearMap, isClosed_range_toContinuousMultilinearMapâŸ©

lemma continuous_toContinuousMultilinearMap :
    Continuous (toContinuousMultilinearMap : E [Î›^Î¹]â†’L[ğ•œ] G â†’
      ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G) :=
  embedding_toContinuousMultilinearMap.continuous

lemma norm_def : â€–fâ€– = sInf {c | 0 â‰¤ (c : â„) âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} := rfl

-- So that invocations of `le_cInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
lemma bounds_nonempty {f : E [Î›^Î¹]â†’L[ğ•œ] G} :
    âˆƒ c, c âˆˆ {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
  ContinuousMultilinearMap.bounds_nonempty

lemma bounds_bddBelow {f : E [Î›^Î¹]â†’L[ğ•œ] G} :
    BddBelow {c | 0 â‰¤ c âˆ§ âˆ€ m, â€–f mâ€– â‰¤ c * âˆ i, â€–m iâ€–} :=
  ContinuousMultilinearMap.bounds_bddBelow

/-- The fundamental property of the operator norm of a continuous alternating map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`. -/
theorem le_op_norm : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, â€–m iâ€– := f.1.le_op_norm m

theorem le_of_op_norm_le {C : â„} (h : â€–fâ€– â‰¤ C) : â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€– :=
  f.1.le_of_op_norm_le m h

theorem le_op_norm_of_le {C : Î¹ â†’ â„} (h : âˆ€ i, â€–m iâ€– â‰¤ C i) : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, C i :=
  f.1.le_op_norm_mul_prod_of_le h

lemma ratio_le_op_norm : â€–f mâ€– / âˆ i, â€–m iâ€– â‰¤ â€–fâ€– := f.1.ratio_le_op_norm m

/-- The image of the unit ball under a continuous alternating map is bounded. -/
lemma unit_le_op_norm (h : â€–mâ€– â‰¤ 1) : â€–f mâ€– â‰¤ â€–fâ€– := f.1.unit_le_op_norm m h

/-- If one controls the norm of every `f x`, then one controls the norm of `f`. -/
lemma op_norm_le_bound {M : â„} (hMp : 0 â‰¤ M) (hM : âˆ€ m, â€–f mâ€– â‰¤ M * âˆ i, â€–m iâ€–) :
    â€–fâ€– â‰¤ M :=
  f.1.op_norm_le_bound hMp hM

section

variable {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' G] [SMulCommClass ğ•œ ğ•œ' G]

instance instNormedSpace : NormedSpace ğ•œ' (E [Î›^Î¹]â†’L[ğ•œ] G) :=
  âŸ¨fun c f â†¦ f.1.op_norm_smul_le câŸ©

variable (ğ•œ')

/-- The inclusion of *alternating* continuous multi-linear maps into continuous multi-linear maps
as a continuous linear map. -/
@[simps!]
def toContinuousMultilinearMapL :
    E [Î›^Î¹]â†’L[ğ•œ] G â†’L[ğ•œ'] ContinuousMultilinearMap ğ•œ (fun _ : Î¹ â†¦ E) G :=
  âŸ¨toContinuousMultilinearMapLinear, continuous_induced_domâŸ©

variable {ğ•œ'}

theorem le_op_norm_mul_prod_of_le {b : Î¹ â†’ â„} (hm : âˆ€ i, â€–m iâ€– â‰¤ b i) : â€–f mâ€– â‰¤ â€–fâ€– * âˆ i, b i :=
  f.1.le_op_norm_mul_prod_of_le hm

theorem le_op_norm_mul_pow_card_of_le {b : â„} (hm : â€–mâ€– â‰¤ b) : â€–f mâ€– â‰¤ â€–fâ€– * b ^ Fintype.card Î¹ :=
  f.1.le_op_norm_mul_pow_card_of_le hm

theorem le_op_norm_mul_pow_of_le (f : E [Î›^Fin n]â†’L[ğ•œ] G) (m : Fin n â†’ E) {b : â„} (hm : â€–mâ€– â‰¤ b) :
    â€–f mâ€– â‰¤ â€–fâ€– * b ^ n :=
  f.1.le_op_norm_mul_pow_of_le hm

/-- The fundamental property of the operator norm of a continuous alternating map:
`â€–f mâ€–` is bounded by `â€–fâ€–` times the product of the `â€–m iâ€–`, `nnnorm` version. -/
theorem le_op_nnnorm : â€–f mâ€–â‚Š â‰¤ â€–fâ€–â‚Š * âˆ i, â€–m iâ€–â‚Š := f.1.le_op_nnnorm m

theorem le_of_op_nnnorm_le {C : â„â‰¥0} (h : â€–fâ€–â‚Š â‰¤ C) : â€–f mâ€–â‚Š â‰¤ C * âˆ i, â€–m iâ€–â‚Š :=
  f.1.le_of_op_nnnorm_le m h

lemma op_norm_prod (f : E [Î›^Î¹]â†’L[ğ•œ] G) (g : E [Î›^Î¹]â†’L[ğ•œ] G') :
    â€–f.prod gâ€– = max (â€–fâ€–) (â€–gâ€–) :=
  f.1.op_norm_prod g.1

lemma op_norm_pi {Î¹' : Type v'} [Fintype Î¹'] {E' : Î¹' â†’ Type wE'} [âˆ€ i', NormedAddCommGroup (E' i')]
    [âˆ€ i', NormedSpace ğ•œ (E' i')] (f : âˆ€ i', ContinuousAlternatingMap ğ•œ E (E' i') Î¹) :
    â€–pi fâ€– = â€–fâ€– :=
  ContinuousMultilinearMap.op_norm_pi fun i â†¦ (f i).1

section

@[simp] lemma norm_of_subsingleton [Subsingleton Î¹] (i : Î¹) (f : E â†’L[ğ•œ] G) :
    â€–ofSubsingleton ğ•œ E G i fâ€– = â€–fâ€– :=
  ContinuousMultilinearMap.norm_ofSubsingleton i f

@[simp] lemma nnnorm_of_subsingleton [Subsingleton Î¹] (i : Î¹) (f : E â†’L[ğ•œ] G) :
    â€–ofSubsingleton ğ•œ E G i fâ€–â‚Š = â€–fâ€–â‚Š :=
  NNReal.eq <| norm_of_subsingleton i f

variable (ğ•œ E)

@[simp] lemma norm_constOfIsEmpty [IsEmpty Î¹] (x : G) : â€–constOfIsEmpty ğ•œ E Î¹ xâ€– = â€–xâ€– :=
  ContinuousMultilinearMap.norm_constOfIsEmpty _ _ _

@[simp] lemma nnnorm_constOfIsEmpty [IsEmpty Î¹] (x : G) : â€–constOfIsEmpty ğ•œ E Î¹ xâ€–â‚Š = â€–xâ€–â‚Š :=
  NNReal.eq <| norm_constOfIsEmpty _ _ _

end

section

variable (ğ•œ E E' G G')

/-- `ContinuousMultilinearMap.prod` as a `LinearIsometryEquiv`. -/
def prodâ‚—áµ¢ :
    (E [Î›^Î¹]â†’L[ğ•œ] G) Ã— (E [Î›^Î¹]â†’L[ğ•œ] G') â‰ƒâ‚—áµ¢[ğ•œ]
      ContinuousAlternatingMap ğ•œ E (G Ã— G') Î¹ where
  toFun f := f.1.prod f.2
  invFun f := ((ContinuousLinearMap.fst ğ•œ G G').compContinuousAlternatingMap f,
    (ContinuousLinearMap.snd ğ•œ G G').compContinuousAlternatingMap f)
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  left_inv _ := rfl
  right_inv _ := rfl
  norm_map' f := op_norm_prod f.1 f.2

/-- `ContinuousMultilinearMap.pi` as a `LinearIsometryEquiv`. -/
def piâ‚—áµ¢ {Î¹' : Type v'} [Fintype Î¹'] {G : Î¹' â†’ Type wE'} [âˆ€ i', NormedAddCommGroup (G i')]
    [âˆ€ i', NormedSpace ğ•œ (G i')] :
    (âˆ€ i', E [Î›^Î¹]â†’L[ğ•œ] G i') â‰ƒâ‚—áµ¢[ğ•œ] (E [Î›^Î¹]â†’L[ğ•œ] (âˆ€ i, G i)) where
  toLinearEquiv := piLinearEquiv
  norm_map' := op_norm_pi

end

end

section restrict_scalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
variable [NormedSpace ğ•œ' G] [IsScalarTower ğ•œ' ğ•œ G]
variable [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E]

@[simp] lemma norm_restrict_scalars : â€–f.restrictScalars ğ•œ'â€– = â€–fâ€– := rfl

variable (ğ•œ')

/-- `ContinuousMultilinearMap.restrict_scalars` as a `linear_isometry`. -/
def restrictScalarsâ‚—áµ¢ : E [Î›^Î¹]â†’L[ğ•œ] G â†’â‚—áµ¢[ğ•œ'] E [Î›^Î¹]â†’L[ğ•œ'] G where
  toFun := restrictScalars ğ•œ'
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' _ := rfl

variable {ğ•œ'}

lemma continuous_restrictScalars :
    Continuous (restrictScalars ğ•œ' : E [Î›^Î¹]â†’L[ğ•œ] G â†’ E [Î›^Î¹]â†’L[ğ•œ'] G) :=
  (restrictScalarsâ‚—áµ¢ ğ•œ').continuous

end restrict_scalars

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, precise version.
For a less precise but more usable version, see `norm_image_sub_le`. The bound reads
`â€–f m - f m'â€– â‰¤
  â€–fâ€– * â€–m 1 - m' 1â€– * max â€–m 2â€– â€–m' 2â€– * max â€–m 3â€– â€–m' 3â€– * ... * max â€–m nâ€– â€–m' nâ€– + ...`,
where the other terms in the sum are the same products where `1` is replaced by any `i`.-/
lemma norm_image_sub_le' [DecidableEq Î¹] (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * âˆ‘ i, âˆ j, if j = i then â€–mâ‚ i - mâ‚‚ iâ€– else max â€–mâ‚ jâ€– â€–mâ‚‚ jâ€– :=
  f.1.norm_image_sub_le' mâ‚ mâ‚‚

/-- The difference `f mâ‚ - f mâ‚‚` is controlled in terms of `â€–fâ€–` and `â€–mâ‚ - mâ‚‚â€–`, less precise
version. For a more precise but less usable version, see `norm_image_sub_le'`.
The bound is `â€–f m - f m'â€– â‰¤ â€–fâ€– * card Î¹ * â€–m - m'â€– * (max â€–mâ€– â€–m'â€–) ^ (card Î¹ - 1)`.-/
lemma norm_image_sub_le (mâ‚ mâ‚‚ : Î¹ â†’ E) :
    â€–f mâ‚ - f mâ‚‚â€– â‰¤ â€–fâ€– * (Fintype.card Î¹) * (max â€–mâ‚â€– â€–mâ‚‚â€–) ^ (Fintype.card Î¹ - 1) * â€–mâ‚ - mâ‚‚â€– :=
  f.1.norm_image_sub_le mâ‚ mâ‚‚

/-- Applying a alternating map to a vector is continuous in both coordinates. -/
lemma continuous_eval :
    Continuous (fun p : E [Î›^Î¹]â†’L[ğ•œ] G Ã— (Î¹ â†’ E) â†¦ p.1 p.2) :=
  (@ContinuousMultilinearMap.continuous_eval ğ•œ Î¹ (fun _ â†¦ E) G _ _ _ _ _ _).comp
    (continuous_toContinuousMultilinearMap.prod_map continuous_id)

lemma continuous_eval_left (m : Î¹ â†’ E) : Continuous fun p : E [Î›^Î¹]â†’L[ğ•œ] G â†¦ p m :=
  continuous_eval.compâ‚‚ continuous_id continuous_const

lemma hasSum_eval {Î± : Type*} {p : Î± â†’ E [Î›^Î¹]â†’L[ğ•œ] G} {q : E [Î›^Î¹]â†’L[ğ•œ] G}
    (h : HasSum p q) (m : Î¹ â†’ E) : HasSum (p Â· m) (q m) := by
  dsimp only [HasSum] at h âŠ¢
  convert ((continuous_eval_left m).tendsto _).comp h
  simp

lemma tsum_eval {Î± : Type*} {p : Î± â†’ E [Î›^Î¹]â†’L[ğ•œ] G} (hp : Summable p)
    (m : Î¹ â†’ E) : (âˆ‘' a, p a) m = âˆ‘' a, p a m :=
  (hasSum_eval hp.hasSum m).tsum_eq.symm

open scoped Topology
open filter

/-- If the target space is complete, the space of continuous alternating maps with its norm is also
complete. -/
instance [CompleteSpace G] : CompleteSpace (E [Î›^Î¹]â†’L[ğ•œ] G) :=
  (completeSpace_iff_isComplete_range uniformEmbedding_toContinuousMultilinearMap.1).2
    isClosed_range_toContinuousMultilinearMap.isComplete

end ContinuousAlternatingMap

/-- If a continuous alternating map is constructed from a alternating map via the constructor
`mkContinuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma AlternatingMap.mkContinuous_norm_le (f : E [Î›^Î¹]â†’â‚—[ğ•œ] G) {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ C :=
  f.toMultilinearMap.mkContinuous_norm_le hC H

/-- If a continuous alternating map is constructed from a alternating map via the constructor
`mk_continuous`, then its norm is bounded by the bound given to the constructor if it is
nonnegative. -/
lemma AlternatingMap.mkContinuous_norm_le' (f : E [Î›^Î¹]â†’â‚—[ğ•œ] G) {C : â„}
    (H : âˆ€ m, â€–f mâ€– â‰¤ C * âˆ i, â€–m iâ€–) : â€–f.mkContinuous C Hâ€– â‰¤ max C 0 :=
  ContinuousMultilinearMap.op_norm_le_bound _ (le_max_right _ _) fun m â†¦ (H m).trans <| by
    gcongr
    Â· apply prod_nonneg; intros; apply norm_nonneg
    Â· apply le_max_left

namespace ContinuousLinearMap

lemma norm_compContinuousAlternatingMap_le (g : G â†’L[ğ•œ] G') (f : E [Î›^Î¹]â†’L[ğ•œ] G) :
    â€–g.compContinuousAlternatingMap fâ€– â‰¤ â€–gâ€– * â€–fâ€– :=
  g.norm_compContinuousMultilinearMap_le f.1

variable (ğ•œ E G G')

/-- `continuous_linear_map.comp_ContinuousAlternatingMap` as a bundled continuous bilinear map. -/
def compContinuousAlternatingMapL : (G â†’L[ğ•œ] G') â†’L[ğ•œ] E [Î›^Î¹]â†’L[ğ•œ] G â†’L[ğ•œ] (E [Î›^Î¹]â†’L[ğ•œ] G') :=
  LinearMap.mkContinuousâ‚‚ (compContinuousAlternatingMapâ‚— ğ•œ E G G') 1 fun f g â†¦ by
    simpa using f.norm_compContinuousAlternatingMap_le g

variable {ğ•œ G G'}

/-- `ContinuousLinearMap.compContinuousAlternatingMap` as a bundled continuous linear equiv. -/
nonrec def _root_.ContinuousLinearEquiv.compContinuousAlternatingMapL (g : G â‰ƒL[ğ•œ] G') :
    (E [Î›^Î¹]â†’L[ğ•œ] G) â‰ƒL[ğ•œ] (E [Î›^Î¹]â†’L[ğ•œ] G') :=
  { g.compContinuousAlternatingMap,
      compContinuousAlternatingMapL ğ•œ E G G' g.toContinuousLinearMap with
    invFun := compContinuousAlternatingMapL ğ•œ E G' G g.symm.toContinuousLinearMap
    continuous_toFun :=
      (compContinuousAlternatingMapL ğ•œ E G G' g.toContinuousLinearMap).continuous
    continuous_invFun :=
      (compContinuousAlternatingMapL ğ•œ E G' G g.symm.toContinuousLinearMap).continuous }

@[simp]
lemma _root_.ContinuousLinearEquiv.compContinuousAlternatingMapL_symm (g : G â‰ƒL[ğ•œ] G') :
    (g.compContinuousAlternatingMapL (Î¹ := Î¹) E).symm = g.symm.compContinuousAlternatingMapL E :=
  rfl

variable {E}

@[simp]
lemma _root_.continuous_linear_equiv.comp_ContinuousAlternatingMapL_apply
    (g : G â‰ƒL[ğ•œ] G') (f : E [Î›^Î¹]â†’L[ğ•œ] G) :
    g.compContinuousAlternatingMapL E f = (g : G â†’L[ğ•œ] G').compContinuousAlternatingMap f :=
  rfl

/-- Flip arguments in `f : G â†’L[ğ•œ] E [Î›^Î¹]â†’L[ğ•œ] G'` to get `Î›^Î¹âŸ®ğ•œ; E; G â†’L[ğ•œ] G'âŸ¯` -/
def flipAlternating (f : G â†’L[ğ•œ] (E [Î›^Î¹]â†’L[ğ•œ] G')) : E [Î›^Î¹]â†’L[ğ•œ] (G â†’L[ğ•œ] G') where
  toContinuousMultilinearMap :=
    ((ContinuousAlternatingMap.toContinuousMultilinearMapL ğ•œ).comp f).flipMultilinear
  map_eq_zero_of_eq' v i j hv hne := by ext x; simp [(f x).map_eq_zero_of_eq v hv hne]

end ContinuousLinearMap

lemma LinearIsometry.norm_compContinuousAlternatingMap (g : G â†’â‚—áµ¢[ğ•œ] G') (f : E [Î›^Î¹]â†’L[ğ•œ] G) :
    â€–g.toContinuousLinearMap.compContinuousAlternatingMap fâ€– = â€–fâ€– :=
  g.norm_compContinuousMultilinearMap f.1

open ContinuousAlternatingMap

section

lemma ContinuousAlternatingMap.norm_compContinuousLinearMap_le (f : E' [Î›^Î¹]â†’L[ğ•œ] G)
    (g : E â†’L[ğ•œ] E') : â€–f.compContinuousLinearMap gâ€– â‰¤ â€–fâ€– * (â€–gâ€– ^ Fintype.card Î¹) :=
  (f.1.norm_compContinuousLinearMap_le _).trans_eq <| by simp [Fintype.card]

def ContinuousAlternatingMap.compContinuousLinearMapL (f : E â†’L[ğ•œ] E') :
    (E' [Î›^Î¹]â†’L[ğ•œ] G) â†’L[ğ•œ] (E [Î›^Î¹]â†’L[ğ•œ] G) :=
  LinearMap.mkContinuous
    (ContinuousAlternatingMap.compContinuousLinearMapâ‚— f) (â€–fâ€– ^ Fintype.card Î¹) fun g â†¦
      (g.norm_compContinuousLinearMap_le f).trans_eq (mul_comm _ _)

def ContinuousAlternatingMap.compContinuousLinearEquivL (f : E â‰ƒL[ğ•œ] E') :
    E [Î›^Î¹]â†’L[ğ•œ] G â‰ƒL[ğ•œ] (E' [Î›^Î¹]â†’L[ğ•œ] G) :=
  { f.continuousAlternatingMapComp,
      ContinuousAlternatingMap.compContinuousLinearMapL (f.symm : E' â†’L[ğ•œ] E) with
    continuous_invFun :=
      (ContinuousAlternatingMap.compContinuousLinearMapL (f : E â†’L[ğ•œ] E')).cont
    continuous_toFun :=
      (ContinuousAlternatingMap.compContinuousLinearMapL (f.symm : E' â†’L[ğ•œ] E)).cont }

def ContinuousLinearEquiv.continuousAlternatingMapCongrL (e : E â‰ƒL[ğ•œ] E') (e' : G â‰ƒL[ğ•œ] G') :
    (E [Î›^Î¹]â†’L[ğ•œ] G) â‰ƒL[ğ•œ] (E' [Î›^Î¹]â†’L[ğ•œ] G') :=
  (ContinuousAlternatingMap.compContinuousLinearEquivL e).trans <|
    e'.compContinuousAlternatingMapL E'

@[simp]
lemma ContinuousLinearEquiv.continuousAlternatingMapCongrL_apply (e : E â‰ƒL[ğ•œ] E')
    (e' : G â‰ƒL[ğ•œ] G') (f : E [Î›^Î¹]â†’L[ğ•œ] G) :
    e.continuousAlternatingMapCongrL e' f =
      e'.compContinuousAlternatingMap (f.compContinuousLinearMap â†‘e.symm) :=
  rfl

end

open ContinuousAlternatingMap

namespace AlternatingMap

/-- Given a map `f : G â†’â‚—[ğ•œ] E [Î›^Î¹]â†’â‚—[ğ•œ] G'` and an estimate
`H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–`, construct a continuous linear
map from `G` to `E [Î›^Î¹]â†’L[ğ•œ] G'`.

In order to lift, e.g., a map `f : (E [Î›^Î¹]â†’â‚—[ğ•œ] G) â†’â‚—[ğ•œ] E' [Î›^Î¹]â†’â‚—[ğ•œ] G'`
to a map `(E [Î›^Î¹]â†’L[ğ•œ] G) â†’L[ğ•œ] E' [Î›^Î¹]â†’L[ğ•œ] G'`,
one can apply this construction to `f.comp ContinuousAlternatingMap.toAlternatingMapLinear`
which is a linear map from `E [Î›^Î¹]â†’L[ğ•œ] G` to `E' [Î›^Î¹]â†’â‚—[ğ•œ] G'`. -/
def mkContinuousLinear (f : G â†’â‚—[ğ•œ] E [Î›^Î¹]â†’â‚—[ğ•œ] G') (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : G â†’L[ğ•œ] E [Î›^Î¹]â†’L[ğ•œ] G' :=
  LinearMap.mkContinuous
    { toFun := fun x => (f x).mkContinuous (C * â€–xâ€–) <| H x
      map_add' := fun x y => by
        ext1
        simp only [_root_.map_add]
        rfl
      map_smul' := fun c x => by
        ext1
        simp only [SMulHomClass.map_smul]
        rfl }
    (max C 0) fun x => by
      rw [LinearMap.coe_mk, AddHom.coe_mk]
      exact (mkContinuous_norm_le' _ _).trans_eq <| by
        rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

theorem mkContinuousLinear_norm_le' (f : G â†’â‚—[ğ•œ] E [Î›^Î¹]â†’â‚—[ğ•œ] G') (C : â„)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ max C 0 :=
  LinearMap.mkContinuous_norm_le _ (le_max_right _ _) _

theorem mkContinuousLinear_norm_le (f : G â†’â‚—[ğ•œ] E [Î›^Î¹]â†’â‚—[ğ•œ] G') {C : â„} (hC : 0 â‰¤ C)
    (H : âˆ€ x m, â€–f x mâ€– â‰¤ C * â€–xâ€– * âˆ i, â€–m iâ€–) : â€–mkContinuousLinear f C Hâ€– â‰¤ C :=
  (mkContinuousLinear_norm_le' f C H).trans_eq (max_eq_left hC)

/-- Given a map `f : E [Î›^Î¹]â†’â‚—[ğ•œ] (E' [Î›^Î¹']â†’â‚—[ğ•œ] G)` and an estimate
`H : âˆ€ m m', â€–f m m'â€– â‰¤ C * âˆ i, â€–m iâ€– * âˆ i, â€–m' iâ€–`, upgrade all `AlternatingMap`s in the type to
`ContinuousAlternatingMap`s. -/
def mkContinuousAlternating (f : E [Î›^Î¹]â†’â‚—[ğ•œ] (E' [Î›^Î¹']â†’â‚—[ğ•œ] G)) (C : â„)
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    E [Î›^Î¹]â†’L[ğ•œ] (E' [Î›^Î¹']â†’L[ğ•œ] G)  :=
  mkContinuous
    { toFun := fun m => mkContinuous (f m) (C * âˆ i, â€–m iâ€–) <| H m
      map_add' := fun m i x y => by
        ext1
        simp
      map_smul' := fun m i c x => by
        ext1
        simp
      map_eq_zero_of_eq' := by
        intros v i j hv hij
        ext v'
        have : f v = 0 := by simpa using f.map_eq_zero_of_eq' v i j hv hij
        simp [this] }
    (max C 0) fun m => by
      simp only [coe_mk, MultilinearMap.coe_mk, ge_iff_le]
      refine ((f m).mkContinuous_norm_le' _).trans_eq ?_
      rw [max_mul_of_nonneg, zero_mul]
      exact prod_nonneg fun _ _ => norm_nonneg _

@[simp]
theorem mkContinuousAlternating_apply (f : E [Î›^Î¹]â†’â‚—[ğ•œ] (E' [Î›^Î¹']â†’â‚—[ğ•œ] G)) {C : â„}
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) (m : Î¹ â†’ E) :
    â‡‘(mkContinuousAlternating f C H m) = f m :=
  rfl

theorem mkContinuousAlternating_norm_le' (f : E [Î›^Î¹]â†’â‚—[ğ•œ]  (E' [Î›^Î¹']â†’â‚—[ğ•œ] G)) (C : â„)
    (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousAlternating f C Hâ€– â‰¤ max C 0 := by
  dsimp only [mkContinuousAlternating]
  exact mkContinuous_norm_le _ (le_max_right _ _) _

theorem mkContinuousAlternating_norm_le (f : E [Î›^Î¹]â†’â‚—[ğ•œ] (E' [Î›^Î¹']â†’â‚—[ğ•œ] G)) {C : â„}
    (hC : 0 â‰¤ C) (H : âˆ€ mâ‚ mâ‚‚, â€–f mâ‚ mâ‚‚â€– â‰¤ (C * âˆ i, â€–mâ‚ iâ€–) * âˆ i, â€–mâ‚‚ iâ€–) :
    â€–mkContinuousAlternating f C Hâ€– â‰¤ C :=
  (mkContinuousAlternating_norm_le' f C H).trans_eq (max_eq_left hC)

end AlternatingMap

section Smul

variable {R : Type*} [Semiring R] [Module R G] [SMulCommClass ğ•œ R G] [ContinuousConstSMul R G]

instance ContinuousAlternatingMap.continuousConstSMul :
    ContinuousConstSMul R (E [Î›^Î¹]â†’L[ğ•œ] G) :=
  âŸ¨fun c =>
    (ContinuousLinearMap.compContinuousAlternatingMapL ğ•œ E G G (c â€¢ ContinuousLinearMap.id ğ•œ G)).2âŸ©

end Smul
