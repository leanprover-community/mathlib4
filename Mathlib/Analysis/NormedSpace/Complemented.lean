/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.NormedSpace.Banach
import Mathlib.Topology.Algebra.Module.FiniteDimension

#align_import analysis.normed_space.complemented from "leanprover-community/mathlib"@"3397560e65278e5f31acefcdea63138bd53d1cd4"

/-!
# Complemented subspaces of normed vector spaces

A submodule `p` of a topological module `E` over `R` is called *complemented* if there exists
a continuous linear projection `f : E â†’â‚—[R] p`, `âˆ€ x : p, f x = x`. We prove that for
a closed subspace of a normed space this condition is equivalent to existence of a closed
subspace `q` such that `p âŠ“ q = âŠ¥`, `p âŠ” q = âŠ¤`. We also prove that a subspace of finite codimension
is always a complemented subspace.

## Tags

complemented subspace, normed vector space
-/


variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

noncomputable section

open LinearMap (ker range)

namespace ContinuousLinearMap

section

variable [CompleteSpace ğ•œ]

theorem ker_closedComplemented_of_finiteDimensional_range (f : E â†’L[ğ•œ] F)
    [FiniteDimensional ğ•œ (range f)] : (ker f).ClosedComplemented := by
  set f' : E â†’L[ğ•œ] range f := f.codRestrict _ (LinearMap.mem_range_self (f : E â†’â‚—[ğ•œ] F))
  rcases f'.exists_right_inverse_of_surjective (f : E â†’â‚—[ğ•œ] F).range_rangeRestrict with âŸ¨g, hgâŸ©
  simpa only [f', ker_codRestrict] using f'.closedComplemented_ker_of_rightInverse g (ext_iff.1 hg)
#align continuous_linear_map.ker_closed_complemented_of_finite_dimensional_range ContinuousLinearMap.ker_closedComplemented_of_finiteDimensional_range

end

variable [CompleteSpace E] [CompleteSpace (F Ã— G)]

/-- If `f : E â†’L[R] F` and `g : E â†’L[R] G` are two surjective linear maps and
their kernels are complement of each other, then `x â†¦ (f x, g x)` defines
a linear equivalence `E â‰ƒL[R] F Ã— G`. -/
nonrec def equivProdOfSurjectiveOfIsCompl (f : E â†’L[ğ•œ] F) (g : E â†’L[ğ•œ] G) (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) : E â‰ƒL[ğ•œ] F Ã— G :=
  (f.equivProdOfSurjectiveOfIsCompl (g : E â†’â‚—[ğ•œ] G) hf hg hfg).toContinuousLinearEquivOfContinuous
    (f.continuous.prod_mk g.continuous)
#align continuous_linear_map.equiv_prod_of_surjective_of_is_compl ContinuousLinearMap.equivProdOfSurjectiveOfIsCompl

@[simp]
theorem coe_equivProdOfSurjectiveOfIsCompl {f : E â†’L[ğ•œ] F} {g : E â†’L[ğ•œ] G} (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) :
    (equivProdOfSurjectiveOfIsCompl f g hf hg hfg : E â†’â‚—[ğ•œ] F Ã— G) = f.prod g := rfl
#align continuous_linear_map.coe_equiv_prod_of_surjective_of_is_compl ContinuousLinearMap.coe_equivProdOfSurjectiveOfIsCompl

@[simp]
theorem equivProdOfSurjectiveOfIsCompl_toLinearEquiv {f : E â†’L[ğ•œ] F} {g : E â†’L[ğ•œ] G}
    (hf : range f = âŠ¤) (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) :
    (equivProdOfSurjectiveOfIsCompl f g hf hg hfg).toLinearEquiv =
      LinearMap.equivProdOfSurjectiveOfIsCompl f g hf hg hfg := rfl
#align continuous_linear_map.equiv_prod_of_surjective_of_is_compl_to_linear_equiv ContinuousLinearMap.equivProdOfSurjectiveOfIsCompl_toLinearEquiv

@[simp]
theorem equivProdOfSurjectiveOfIsCompl_apply {f : E â†’L[ğ•œ] F} {g : E â†’L[ğ•œ] G} (hf : range f = âŠ¤)
    (hg : range g = âŠ¤) (hfg : IsCompl (ker f) (ker g)) (x : E) :
    equivProdOfSurjectiveOfIsCompl f g hf hg hfg x = (f x, g x) := rfl
#align continuous_linear_map.equiv_prod_of_surjective_of_is_compl_apply ContinuousLinearMap.equivProdOfSurjectiveOfIsCompl_apply

end ContinuousLinearMap

namespace Submodule

variable [CompleteSpace E] (p q : Subspace ğ•œ E)

/-- If `q` is a closed complement of a closed subspace `p`, then `p Ã— q` is continuously
isomorphic to `E`. -/
def prodEquivOfClosedCompl (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) : (p Ã— q) â‰ƒL[ğ•œ] E := by
  haveI := hp.completeSpace_coe; haveI := hq.completeSpace_coe
  refine (p.prodEquivOfIsCompl q h).toContinuousLinearEquivOfContinuous ?_
  exact (p.subtypeL.coprod q.subtypeL).continuous
#align subspace.prod_equiv_of_closed_compl Submodule.prodEquivOfClosedCompl

/-- Projection to a closed submodule along a closed complement. -/
def linearProjOfClosedCompl (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) : E â†’L[ğ•œ] p :=
  ContinuousLinearMap.fst ğ•œ p q âˆ˜L â†‘(prodEquivOfClosedCompl p q h hp hq).symm
#align subspace.linear_proj_of_closed_compl Submodule.linearProjOfClosedCompl

variable {p q}

@[simp]
theorem coe_prodEquivOfClosedCompl (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) :
    â‡‘(p.prodEquivOfClosedCompl q h hp hq) = p.prodEquivOfIsCompl q h := rfl
#align subspace.coe_prod_equiv_of_closed_compl Submodule.coe_prodEquivOfClosedCompl

@[simp]
theorem coe_prodEquivOfClosedCompl_symm (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) :
    â‡‘(p.prodEquivOfClosedCompl q h hp hq).symm = (p.prodEquivOfIsCompl q h).symm := rfl
#align subspace.coe_prod_equiv_of_closed_compl_symm Submodule.coe_prodEquivOfClosedCompl_symm

@[simp]
theorem coe_continuous_linearProjOfClosedCompl (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) :
    (p.linearProjOfClosedCompl q h hp hq : E â†’â‚—[ğ•œ] p) = p.linearProjOfIsCompl q h := rfl
#align subspace.coe_continuous_linear_proj_of_closed_compl Submodule.coe_continuous_linearProjOfClosedCompl

@[simp]
theorem coe_continuous_linearProjOfClosedCompl' (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) :
    â‡‘(p.linearProjOfClosedCompl q h hp hq) = p.linearProjOfIsCompl q h := rfl
#align subspace.coe_continuous_linear_proj_of_closed_compl' Submodule.coe_continuous_linearProjOfClosedCompl'

theorem ClosedComplemented.of_isCompl_isClosed (h : IsCompl p q) (hp : IsClosed (p : Set E))
    (hq : IsClosed (q : Set E)) : p.ClosedComplemented :=
  âŸ¨p.linearProjOfClosedCompl q h hp hq, Submodule.linearProjOfIsCompl_apply_left hâŸ©
#align subspace.closed_complemented_of_closed_compl Submodule.ClosedComplemented.of_isCompl_isClosed

alias IsCompl.closedComplemented_of_isClosed := ClosedComplemented.of_isCompl_isClosed

theorem closedComplemented_iff_isClosed_exists_isClosed_isCompl :
    p.ClosedComplemented â†”
      IsClosed (p : Set E) âˆ§ âˆƒ q : Submodule ğ•œ E, IsClosed (q : Set E) âˆ§ IsCompl p q :=
  âŸ¨fun h => âŸ¨h.isClosed, h.exists_isClosed_isComplâŸ©,
    fun âŸ¨hp, âŸ¨_, hq, hpqâŸ©âŸ© => .of_isCompl_isClosed hpq hp hqâŸ©
#align subspace.closed_complemented_iff_has_closed_compl Submodule.closedComplemented_iff_isClosed_exists_isClosed_isCompl

theorem ClosedComplemented.of_quotient_finiteDimensional [CompleteSpace ğ•œ]
    [FiniteDimensional ğ•œ (E â§¸ p)] (hp : IsClosed (p : Set E)) : p.ClosedComplemented := by
  obtain âŸ¨q, hqâŸ© : âˆƒ q, IsCompl p q := p.exists_isCompl
  haveI : FiniteDimensional ğ•œ q := (p.quotientEquivOfIsCompl q hq).finiteDimensional
  exact .of_isCompl_isClosed hq hp q.closed_of_finiteDimensional
#align subspace.closed_complemented_of_quotient_finite_dimensional Submodule.ClosedComplemented.of_quotient_finiteDimensional

end Submodule
