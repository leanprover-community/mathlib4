/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Algebra.Algebra.Tower
import Mathlib.Analysis.Asymptotics.Asymptotics
import Mathlib.Analysis.NormedSpace.ContinuousLinearMap
import Mathlib.Analysis.NormedSpace.LinearIsometry
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Algebra.Module.StrongTopology
import Mathlib.Tactic.SuppressCompilation

#align_import analysis.normed_space.operator_norm from "leanprover-community/mathlib"@"f7ebde7ee0d1505dfccac8644ae12371aa3c1c9f"

/-!
# Operator norm on the space of continuous linear maps

Define the operator norm on the space of continuous (semi)linear maps between normed spaces, and
prove its basic properties. In particular, show that this space is itself a normed space.

Since a lot of elementary properties don't require `â€–xâ€– = 0 â†’ x = 0` we start setting up the
theory for `SeminormedAddCommGroup` and we specialize to `NormedAddCommGroup` at the end.

Note that most of statements that apply to semilinear maps only hold when the ring homomorphism
is isometric, as expressed by the typeclass `[RingHomIsometric Ïƒ]`.

-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped Classical NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E Eâ‚— F Fâ‚— G Gâ‚— ğ“• : Type*}

section SemiNormed

open Metric ContinuousLinearMap

variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup Eâ‚—] [SeminormedAddCommGroup F]
  [SeminormedAddCommGroup Fâ‚—] [SeminormedAddCommGroup G] [SeminormedAddCommGroup Gâ‚—]

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ Eâ‚—] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Fâ‚—] [NormedSpace ğ•œâ‚ƒ G]
  [NormedSpace ğ•œ Gâ‚—] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

/-- If `â€–xâ€– = 0` and `f` is continuous then `â€–f xâ€– = 0`. -/
theorem norm_image_of_norm_zero [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•) (hf : Continuous f) {x : E}
    (hx : â€–xâ€– = 0) : â€–f xâ€– = 0 := by
  rw [â† mem_closure_zero_iff_norm, â† specializes_iff_mem_closure, â† map_zero f] at *
  exact hx.map hf
#align norm_image_of_norm_zero norm_image_of_norm_zero

section

variable [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ]

theorem SemilinearMapClass.bound_of_shell_semi_normed [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
    (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) {x : E} (hx : â€–xâ€– â‰  0) :
    â€–f xâ€– â‰¤ C * â€–xâ€– :=
  (normSeminorm ğ•œ E).bound_of_shell ((normSeminorm ğ•œâ‚‚ F).comp âŸ¨âŸ¨f, map_add fâŸ©, map_smulâ‚›â‚— fâŸ©)
    Îµ_pos hc hf hx
#align semilinear_map_class.bound_of_shell_semi_normed SemilinearMapClass.bound_of_shell_semi_normed

/-- A continuous linear map between seminormed spaces is bounded when the field is nontrivially
normed. The continuity ensures boundedness on a ball of some radius `Îµ`. The nontriviality of the
norm is then used to rescale any element into an element of norm in `[Îµ/C, Îµ]`, whose image has a
controlled norm. The norm control for the original element follows by rescaling. -/
theorem SemilinearMapClass.bound_of_continuous [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    (hf : Continuous f) : âˆƒ C, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€– :=
  let Ï† : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F := âŸ¨âŸ¨f, map_add fâŸ©, map_smulâ‚›â‚— fâŸ©
  ((normSeminorm ğ•œâ‚‚ F).comp Ï†).bound_of_continuous_normedSpace (continuous_norm.comp hf)
#align semilinear_map_class.bound_of_continuous SemilinearMapClass.bound_of_continuous

end

namespace ContinuousLinearMap

theorem bound [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) : âˆƒ C, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€– :=
  SemilinearMapClass.bound_of_continuous f f.2
#align continuous_linear_map.bound ContinuousLinearMap.bound

section

open Filter

variable (ğ•œ E)

/-- Given a unit-length element `x` of a normed space `E` over a field `ğ•œ`, the natural linear
    isometry map from `ğ•œ` to `E` by taking multiples of `x`.-/
def _root_.LinearIsometry.toSpanSingleton {v : E} (hv : â€–vâ€– = 1) : ğ•œ â†’â‚—áµ¢[ğ•œ] E :=
  { LinearMap.toSpanSingleton ğ•œ E v with norm_map' := fun x => by simp [norm_smul, hv] }
#align linear_isometry.to_span_singleton LinearIsometry.toSpanSingleton

variable {ğ•œ E}

@[simp]
theorem _root_.LinearIsometry.toSpanSingleton_apply {v : E} (hv : â€–vâ€– = 1) (a : ğ•œ) :
    LinearIsometry.toSpanSingleton ğ•œ E hv a = a â€¢ v :=
  rfl
#align linear_isometry.to_span_singleton_apply LinearIsometry.toSpanSingleton_apply

@[simp]
theorem _root_.LinearIsometry.coe_toSpanSingleton {v : E} (hv : â€–vâ€– = 1) :
    (LinearIsometry.toSpanSingleton ğ•œ E hv).toLinearMap = LinearMap.toSpanSingleton ğ•œ E v :=
  rfl
#align linear_isometry.coe_to_span_singleton LinearIsometry.coe_toSpanSingleton

end

section OpNorm

open Set Real

/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def opNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) :=
  sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– }
#align continuous_linear_map.op_norm ContinuousLinearMap.opNorm

instance hasOpNorm : Norm (E â†’SL[Ïƒâ‚â‚‚] F) :=
  âŸ¨opNormâŸ©
#align continuous_linear_map.has_op_norm ContinuousLinearMap.hasOpNorm

theorem norm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€– = sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  rfl
#align continuous_linear_map.norm_def ContinuousLinearMap.norm_def

-- So that invocations of `le_csInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
theorem bounds_nonempty [RingHomIsometric Ïƒâ‚â‚‚] {f : E â†’SL[Ïƒâ‚â‚‚] F} :
    âˆƒ c, c âˆˆ { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  let âŸ¨M, hMp, hMbâŸ© := f.bound
  âŸ¨M, le_of_lt hMp, hMbâŸ©
#align continuous_linear_map.bounds_nonempty ContinuousLinearMap.bounds_nonempty

theorem bounds_bddBelow {f : E â†’SL[Ïƒâ‚â‚‚] F} : BddBelow { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  âŸ¨0, fun _ âŸ¨hn, _âŸ© => hnâŸ©
#align continuous_linear_map.bounds_bdd_below ContinuousLinearMap.bounds_bddBelow

theorem isLeast_op_norm [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    IsLeast {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€–} â€–fâ€– := by
  refine IsClosed.isLeast_csInf ?_ bounds_nonempty bounds_bddBelow
  simp only [setOf_and, setOf_forall]
  refine isClosed_Ici.inter <| isClosed_iInter fun _ â†¦ isClosed_le ?_ ?_ <;> continuity

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem op_norm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp : 0 â‰¤ M) (hM : âˆ€ x, â€–f xâ€– â‰¤ M * â€–xâ€–) :
    â€–fâ€– â‰¤ M :=
  csInf_le bounds_bddBelow âŸ¨hMp, hMâŸ©
#align continuous_linear_map.op_norm_le_bound ContinuousLinearMap.op_norm_le_bound

/-- If one controls the norm of every `A x`, `â€–xâ€– â‰  0`, then one controls the norm of `A`. -/
theorem op_norm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– â‰  0 â†’ â€–f xâ€– â‰¤ M * â€–xâ€–) : â€–fâ€– â‰¤ M :=
  op_norm_le_bound f hMp fun x =>
    (ne_or_eq â€–xâ€– 0).elim (hM x) fun h => by
      simp only [h, mul_zero, norm_image_of_norm_zero f f.2 h, le_refl]
#align continuous_linear_map.op_norm_le_bound' ContinuousLinearMap.op_norm_le_bound'

theorem op_norm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : LipschitzWith K f) : â€–fâ€– â‰¤ K :=
  f.op_norm_le_bound K.2 fun x => by
    simpa only [dist_zero_right, f.map_zero] using hf.dist_le_mul x 0
#align continuous_linear_map.op_norm_le_of_lipschitz ContinuousLinearMap.op_norm_le_of_lipschitz

theorem op_norm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} {M : â„} (M_nonneg : 0 â‰¤ M)
    (h_above : âˆ€ x, â€–Ï† xâ€– â‰¤ M * â€–xâ€–) (h_below : âˆ€ N â‰¥ 0, (âˆ€ x, â€–Ï† xâ€– â‰¤ N * â€–xâ€–) â†’ M â‰¤ N) :
    â€–Ï†â€– = M :=
  le_antisymm (Ï†.op_norm_le_bound M_nonneg h_above)
    ((le_csInf_iff ContinuousLinearMap.bounds_bddBelow âŸ¨M, M_nonneg, h_aboveâŸ©).mpr
      fun N âŸ¨N_nonneg, hNâŸ© => h_below N N_nonneg hN)
#align continuous_linear_map.op_norm_eq_of_bounds ContinuousLinearMap.op_norm_eq_of_bounds

theorem op_norm_neg (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–-fâ€– = â€–fâ€– := by simp only [norm_def, neg_apply, norm_neg]
#align continuous_linear_map.op_norm_neg ContinuousLinearMap.op_norm_neg

theorem op_norm_nonneg (f : E â†’SL[Ïƒâ‚â‚‚] F) : 0 â‰¤ â€–fâ€– :=
  Real.sInf_nonneg _ fun _ â†¦ And.left
#align continuous_linear_map.op_norm_nonneg ContinuousLinearMap.op_norm_nonneg

/-- The norm of the `0` operator is `0`. -/
theorem op_norm_zero : â€–(0 : E â†’SL[Ïƒâ‚â‚‚] F)â€– = 0 :=
  le_antisymm (op_norm_le_bound _ le_rfl fun _ â†¦ by simp) (op_norm_nonneg _)
#align continuous_linear_map.op_norm_zero ContinuousLinearMap.op_norm_zero

/-- The norm of the identity is at most `1`. It is in fact `1`, except when the space is trivial
where it is `0`. It means that one can not do better than an inequality in general. -/
theorem norm_id_le : â€–id ğ•œ Eâ€– â‰¤ 1 :=
  op_norm_le_bound _ zero_le_one fun x => by simp
#align continuous_linear_map.norm_id_le ContinuousLinearMap.norm_id_le

section

variable [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ] (f g : E â†’SL[Ïƒâ‚â‚‚] F) (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G)
  (x : E)

/-- The fundamental property of the operator norm: `â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€–`. -/
theorem le_op_norm : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := (isLeast_op_norm f).1.2 x
#align continuous_linear_map.le_op_norm ContinuousLinearMap.le_op_norm

theorem dist_le_op_norm (x y : E) : dist (f x) (f y) â‰¤ â€–fâ€– * dist x y := by
  simp_rw [dist_eq_norm, â† map_sub, f.le_op_norm]
#align continuous_linear_map.dist_le_op_norm ContinuousLinearMap.dist_le_op_norm

theorem le_of_op_norm_le_of_le {x} {a b : â„} (hf : â€–fâ€– â‰¤ a) (hx : â€–xâ€– â‰¤ b) :
    â€–f xâ€– â‰¤ a * b :=
  (f.le_op_norm x).trans <| by gcongr; exact (op_norm_nonneg f).trans hf

theorem le_op_norm_of_le {c : â„} {x} (h : â€–xâ€– â‰¤ c) : â€–f xâ€– â‰¤ â€–fâ€– * c :=
  f.le_of_op_norm_le_of_le le_rfl h
#align continuous_linear_map.le_op_norm_of_le ContinuousLinearMap.le_op_norm_of_le

theorem le_of_op_norm_le {c : â„} (h : â€–fâ€– â‰¤ c) (x : E) : â€–f xâ€– â‰¤ c * â€–xâ€– :=
  f.le_of_op_norm_le_of_le h le_rfl
#align continuous_linear_map.le_of_op_norm_le ContinuousLinearMap.le_of_op_norm_le

theorem op_norm_le_iff {f : E â†’SL[Ïƒâ‚â‚‚] F} {M : â„} (hMp : 0 â‰¤ M) :
    â€–fâ€– â‰¤ M â†” âˆ€ x, â€–f xâ€– â‰¤ M * â€–xâ€– :=
  âŸ¨f.le_of_op_norm_le, op_norm_le_bound f hMpâŸ©

theorem ratio_le_op_norm : â€–f xâ€– / â€–xâ€– â‰¤ â€–fâ€– :=
  div_le_of_nonneg_of_le_mul (norm_nonneg _) f.op_norm_nonneg (le_op_norm _ _)
#align continuous_linear_map.ratio_le_op_norm ContinuousLinearMap.ratio_le_op_norm

/-- The image of the unit ball under a continuous linear map is bounded. -/
theorem unit_le_op_norm : â€–xâ€– â‰¤ 1 â†’ â€–f xâ€– â‰¤ â€–fâ€– :=
  mul_one â€–fâ€– â–¸ f.le_op_norm_of_le
#align continuous_linear_map.unit_le_op_norm ContinuousLinearMap.unit_le_op_norm

theorem op_norm_le_of_shell {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C) {c : ğ•œ}
    (hc : 1 < â€–câ€–) (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
  f.op_norm_le_bound' hC fun _ hx => SemilinearMapClass.bound_of_shell_semi_normed f Îµ_pos hc hf hx
#align continuous_linear_map.op_norm_le_of_shell ContinuousLinearMap.op_norm_le_of_shell

theorem op_norm_le_of_ball {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ : â„} {C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C)
    (hf : âˆ€ x âˆˆ ball (0 : E) Îµ, â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  refine' op_norm_le_of_shell Îµ_pos hC hc fun x _ hx => hf x _
  rwa [ball_zero_eq]
#align continuous_linear_map.op_norm_le_of_ball ContinuousLinearMap.op_norm_le_of_ball

theorem op_norm_le_of_nhds_zero {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„} (hC : 0 â‰¤ C)
    (hf : âˆ€á¶  x in ğ“ (0 : E), â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
  let âŸ¨_, Îµ0, hÎµâŸ© := Metric.eventually_nhds_iff_ball.1 hf
  op_norm_le_of_ball Îµ0 hC hÎµ
#align continuous_linear_map.op_norm_le_of_nhds_zero ContinuousLinearMap.op_norm_le_of_nhds_zero

theorem op_norm_le_of_shell' {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C) {c : ğ•œ}
    (hc : â€–câ€– < 1) (hf : âˆ€ x, Îµ * â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C := by
  by_cases h0 : c = 0
  Â· refine' op_norm_le_of_ball Îµ_pos hC fun x hx => hf x _ _
    Â· simp [h0]
    Â· rwa [ball_zero_eq] at hx
  Â· rw [â† inv_inv c, norm_inv, inv_lt_one_iff_of_pos (norm_pos_iff.2 <| inv_ne_zero h0)] at hc
    refine' op_norm_le_of_shell Îµ_pos hC hc _
    rwa [norm_inv, div_eq_mul_inv, inv_inv]
#align continuous_linear_map.op_norm_le_of_shell' ContinuousLinearMap.op_norm_le_of_shell'

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€– = 1`, then
one controls the norm of `f`. -/
theorem op_norm_le_of_unit_norm [NormedSpace â„ E] [NormedSpace â„ F] {f : E â†’L[â„] F} {C : â„}
    (hC : 0 â‰¤ C) (hf : âˆ€ x, â€–xâ€– = 1 â†’ â€–f xâ€– â‰¤ C) : â€–fâ€– â‰¤ C := by
  refine' op_norm_le_bound' f hC fun x hx => _
  have Hâ‚ : â€–â€–xâ€–â»Â¹ â€¢ xâ€– = 1 := by rw [norm_smul, norm_inv, norm_norm, inv_mul_cancel hx]
  have Hâ‚‚ := hf _ Hâ‚
  rwa [map_smul, norm_smul, norm_inv, norm_norm, â† div_eq_inv_mul, _root_.div_le_iff] at Hâ‚‚
  exact (norm_nonneg x).lt_of_ne' hx
#align continuous_linear_map.op_norm_le_of_unit_norm ContinuousLinearMap.op_norm_le_of_unit_norm

/-- The operator norm satisfies the triangle inequality. -/
theorem op_norm_add_le : â€–f + gâ€– â‰¤ â€–fâ€– + â€–gâ€– :=
  (f + g).op_norm_le_bound (add_nonneg f.op_norm_nonneg g.op_norm_nonneg) fun x =>
    (norm_add_le_of_le (f.le_op_norm x) (g.le_op_norm x)).trans_eq (add_mul _ _ _).symm
#align continuous_linear_map.op_norm_add_le ContinuousLinearMap.op_norm_add_le

/-- If there is an element with norm different from `0`, then the norm of the identity equals `1`.
(Since we are working with seminorms supposing that the space is non-trivial is not enough.) -/
theorem norm_id_of_nontrivial_seminorm (h : âˆƒ x : E, â€–xâ€– â‰  0) : â€–id ğ•œ Eâ€– = 1 :=
  le_antisymm norm_id_le <| by
    let âŸ¨x, hxâŸ© := h
    have := (id ğ•œ E).ratio_le_op_norm x
    rwa [id_apply, div_self hx] at this
#align continuous_linear_map.norm_id_of_nontrivial_seminorm ContinuousLinearMap.norm_id_of_nontrivial_seminorm

theorem op_norm_smul_le {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass ğ•œâ‚‚ ğ•œ' F]
    (c : ğ•œ') (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– :=
  (c â€¢ f).op_norm_le_bound (mul_nonneg (norm_nonneg _) (op_norm_nonneg _)) fun _ => by
    erw [norm_smul, mul_assoc]
    exact mul_le_mul_of_nonneg_left (le_op_norm _ _) (norm_nonneg _)
#align continuous_linear_map.op_norm_smul_le ContinuousLinearMap.op_norm_smul_le

/-- Operator seminorm on the space of continuous (semi)linear maps, as `Seminorm`.

We use this seminorm to define a `SeminormedGroup` structure on `E â†’SL[Ïƒ] F`,
but we have to override the projection `UniformSpace`
so that it is definitionally equal to the one coming from the topologies on `E` and `F`. -/
protected def seminorm : Seminorm ğ•œâ‚‚ (E â†’SL[Ïƒâ‚â‚‚] F) :=
  .ofSMulLE norm op_norm_zero op_norm_add_le op_norm_smul_le

private lemma uniformity_eq_seminorm :
    ğ“¤ (E â†’SL[Ïƒâ‚â‚‚] F) = â¨… r > 0, ğ“Ÿ {f | â€–f.1 - f.2â€– < r} := by
  refine ContinuousLinearMap.seminorm.uniformity_eq_of_hasBasis
    (ContinuousLinearMap.hasBasis_nhds_zero_of_basis Metric.nhds_basis_closedBall)
    ?_ fun (s, r) âŸ¨hs, hrâŸ© â†¦ ?_
  Â· rcases NormedField.exists_lt_norm ğ•œ 1 with âŸ¨c, hcâŸ©
    refine âŸ¨â€–câ€–, ContinuousLinearMap.hasBasis_nhds_zero.mem_iff.2
      âŸ¨(closedBall 0 1, closedBall 0 1), ?_âŸ©âŸ©
    suffices âˆ€ f : E â†’SL[Ïƒâ‚â‚‚] F, (âˆ€ x, â€–xâ€– â‰¤ 1 â†’ â€–f xâ€– â‰¤ 1) â†’ â€–fâ€– â‰¤ â€–câ€– by
      simpa [NormedSpace.isVonNBounded_closedBall, closedBall_mem_nhds, subset_def] using this
    intro f hf
    refine op_norm_le_of_shell (f := f) one_pos (norm_nonneg c) hc fun x hcx hx â†¦ ?_
    exact (hf x hx.le).trans ((div_le_iff' <| one_pos.trans hc).1 hcx)
  Â· rcases (NormedSpace.isVonNBounded_iff' _ _ _).1 hs with âŸ¨Îµ, hÎµâŸ©
    rcases exists_pos_mul_lt hr Îµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    refine âŸ¨Î´, hÎ´â‚€, fun f hf x hx â†¦ ?_âŸ©
    simp only [Seminorm.mem_ball_zero, mem_closedBall_zero_iff] at hf âŠ¢
    rw [mul_comm] at hÎ´
    exact le_trans (le_of_op_norm_le_of_le _ hf.le (hÎµ _ hx)) hÎ´.le

instance toPseudoMetricSpace : PseudoMetricSpace (E â†’SL[Ïƒâ‚â‚‚] F) := .replaceUniformity
  ContinuousLinearMap.seminorm.toSeminormedAddCommGroup.toPseudoMetricSpace uniformity_eq_seminorm
#align continuous_linear_map.to_pseudo_metric_space ContinuousLinearMap.toPseudoMetricSpace

/-- Continuous linear maps themselves form a seminormed space with respect to
    the operator norm. -/
instance toSeminormedAddCommGroup : SeminormedAddCommGroup (E â†’SL[Ïƒâ‚â‚‚] F) where
  dist_eq _ _ := rfl
#align continuous_linear_map.to_seminormed_add_comm_group ContinuousLinearMap.toSeminormedAddCommGroup

#noalign continuous_linear_map.tmp_seminormed_add_comm_group
#noalign continuous_linear_map.tmp_pseudo_metric_space
#noalign continuous_linear_map.tmp_uniform_space
#noalign continuous_linear_map.tmp_topological_space
#noalign continuous_linear_map.tmp_topological_add_group
#noalign continuous_linear_map.tmp_closed_ball_div_subset
#noalign continuous_linear_map.tmp_topology_eq
#noalign continuous_linear_map.tmp_uniform_space_eq

theorem nnnorm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€–â‚Š = sInf { c | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š } := by
  ext
  rw [NNReal.coe_sInf, coe_nnnorm, norm_def, NNReal.coe_image]
  simp_rw [â† NNReal.coe_le_coe, NNReal.coe_mul, coe_nnnorm, mem_setOf_eq, NNReal.coe_mk,
    exists_prop]
#align continuous_linear_map.nnnorm_def ContinuousLinearMap.nnnorm_def

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem op_nnnorm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) : â€–fâ€–â‚Š â‰¤ M :=
  op_norm_le_bound f (zero_le M) hM
#align continuous_linear_map.op_nnnorm_le_bound ContinuousLinearMap.op_nnnorm_le_bound

/-- If one controls the norm of every `A x`, `â€–xâ€–â‚Š â‰  0`, then one controls the norm of `A`. -/
theorem op_nnnorm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) (M : â„â‰¥0) (hM : âˆ€ x, â€–xâ€–â‚Š â‰  0 â†’ â€–f xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š) :
    â€–fâ€–â‚Š â‰¤ M :=
  op_norm_le_bound' f (zero_le M) fun x hx => hM x <| by rwa [â† NNReal.coe_ne_zero]
#align continuous_linear_map.op_nnnorm_le_bound' ContinuousLinearMap.op_nnnorm_le_bound'

/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€–â‚Š = 1`, then
one controls the norm of `f`. -/
theorem op_nnnorm_le_of_unit_nnnorm [NormedSpace â„ E] [NormedSpace â„ F] {f : E â†’L[â„] F} {C : â„â‰¥0}
    (hf : âˆ€ x, â€–xâ€–â‚Š = 1 â†’ â€–f xâ€–â‚Š â‰¤ C) : â€–fâ€–â‚Š â‰¤ C :=
  op_norm_le_of_unit_norm C.coe_nonneg fun x hx => hf x <| by rwa [â† NNReal.coe_eq_one]
#align continuous_linear_map.op_nnnorm_le_of_unit_nnnorm ContinuousLinearMap.op_nnnorm_le_of_unit_nnnorm

theorem op_nnnorm_le_of_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} (hf : LipschitzWith K f) :
    â€–fâ€–â‚Š â‰¤ K :=
  op_norm_le_of_lipschitz hf
#align continuous_linear_map.op_nnnorm_le_of_lipschitz ContinuousLinearMap.op_nnnorm_le_of_lipschitz

theorem op_nnnorm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} (M : â„â‰¥0) (h_above : âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ M * â€–xâ€–â‚Š)
    (h_below : âˆ€ N, (âˆ€ x, â€–Ï† xâ€–â‚Š â‰¤ N * â€–xâ€–â‚Š) â†’ M â‰¤ N) : â€–Ï†â€–â‚Š = M :=
  Subtype.ext <| op_norm_eq_of_bounds (zero_le M) h_above <| Subtype.forall'.mpr h_below
#align continuous_linear_map.op_nnnorm_eq_of_bounds ContinuousLinearMap.op_nnnorm_eq_of_bounds

theorem op_nnnorm_le_iff {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„â‰¥0} : â€–fâ€–â‚Š â‰¤ C â†” âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š :=
  op_norm_le_iff C.2

theorem isLeast_op_nnnorm : IsLeast {C : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š} â€–fâ€–â‚Š := by
  simpa only [â† op_nnnorm_le_iff] using isLeast_Ici

instance toNormedSpace {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass ğ•œâ‚‚ ğ•œ' F] :
    NormedSpace ğ•œ' (E â†’SL[Ïƒâ‚â‚‚] F) :=
  âŸ¨op_norm_smul_leâŸ©
#align continuous_linear_map.to_normed_space ContinuousLinearMap.toNormedSpace

/-- The operator norm is submultiplicative. -/
theorem op_norm_comp_le (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€– â‰¤ â€–hâ€– * â€–fâ€– :=
  csInf_le bounds_bddBelow
    âŸ¨mul_nonneg (op_norm_nonneg _) (op_norm_nonneg _), fun x => by
      rw [mul_assoc]
      exact h.le_op_norm_of_le (f.le_op_norm x)âŸ©
#align continuous_linear_map.op_norm_comp_le ContinuousLinearMap.op_norm_comp_le

-- Porting note: restatement of `op_norm_comp_le` for linear maps.
/-- The operator norm is submultiplicative. -/
theorem op_norm_comp_le' (h : Eâ‚— â†’L[ğ•œ] Fâ‚—) (f : E â†’L[ğ•œ] Eâ‚—) : â€–h.comp fâ€– â‰¤ â€–hâ€– * â€–fâ€– :=
  op_norm_comp_le h f

theorem op_nnnorm_comp_le [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€–â‚Š â‰¤ â€–hâ€–â‚Š * â€–fâ€–â‚Š :=
  op_norm_comp_le h f
#align continuous_linear_map.op_nnnorm_comp_le ContinuousLinearMap.op_nnnorm_comp_le

/-- Continuous linear maps form a seminormed ring with respect to the operator norm. -/
instance toSemiNormedRing : SeminormedRing (E â†’L[ğ•œ] E) :=
  { ContinuousLinearMap.toSeminormedAddCommGroup, ContinuousLinearMap.ring with
    norm_mul := fun f g => op_norm_comp_le f g }
#align continuous_linear_map.to_semi_normed_ring ContinuousLinearMap.toSemiNormedRing

-- Porting FIXME: replacing `(algebra : Algebra ğ•œ (E â†’L[ğ•œ] E))` with
-- just `algebra` below causes a massive timeout.
/-- For a normed space `E`, continuous linear endomorphisms form a normed algebra with
respect to the operator norm. -/
instance toNormedAlgebra : NormedAlgebra ğ•œ (E â†’L[ğ•œ] E) :=
  { (algebra : Algebra ğ•œ (E â†’L[ğ•œ] E)) with
    norm_smul_le := by
      intro c f
      apply op_norm_smul_le c f}
#align continuous_linear_map.to_normed_algebra ContinuousLinearMap.toNormedAlgebra

theorem le_op_nnnorm : â€–f xâ€–â‚Š â‰¤ â€–fâ€–â‚Š * â€–xâ€–â‚Š :=
  f.le_op_norm x
#align continuous_linear_map.le_op_nnnorm ContinuousLinearMap.le_op_nnnorm

theorem nndist_le_op_nnnorm (x y : E) : nndist (f x) (f y) â‰¤ â€–fâ€–â‚Š * nndist x y :=
  dist_le_op_norm f x y
#align continuous_linear_map.nndist_le_op_nnnorm ContinuousLinearMap.nndist_le_op_nnnorm

/-- continuous linear maps are Lipschitz continuous. -/
theorem lipschitz : LipschitzWith â€–fâ€–â‚Š f :=
  AddMonoidHomClass.lipschitz_of_bound_nnnorm f _ f.le_op_nnnorm
#align continuous_linear_map.lipschitz ContinuousLinearMap.lipschitz

/-- Evaluation of a continuous linear map `f` at a point is Lipschitz continuous in `f`. -/
theorem lipschitz_apply (x : E) : LipschitzWith â€–xâ€–â‚Š fun f : E â†’SL[Ïƒâ‚â‚‚] F => f x :=
  lipschitzWith_iff_norm_sub_le.2 fun f g => ((f - g).le_op_norm x).trans_eq (mul_comm _ _)
#align continuous_linear_map.lipschitz_apply ContinuousLinearMap.lipschitz_apply

end

section Sup

variable [RingHomIsometric Ïƒâ‚â‚‚]

theorem exists_mul_lt_apply_of_lt_op_nnnorm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0} (hr : r < â€–fâ€–â‚Š) :
    âˆƒ x, r * â€–xâ€–â‚Š < â€–f xâ€–â‚Š := by
  simpa only [not_forall, not_le, Set.mem_setOf] using
    not_mem_of_lt_csInf (nnnorm_def f â–¸ hr : r < sInf { c : â„â‰¥0 | âˆ€ x, â€–f xâ€–â‚Š â‰¤ c * â€–xâ€–â‚Š })
      (OrderBot.bddBelow _)
#align continuous_linear_map.exists_mul_lt_apply_of_lt_op_nnnorm ContinuousLinearMap.exists_mul_lt_apply_of_lt_op_nnnorm

theorem exists_mul_lt_of_lt_op_norm (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„} (hrâ‚€ : 0 â‰¤ r) (hr : r < â€–fâ€–) :
    âˆƒ x, r * â€–xâ€– < â€–f xâ€– := by
  lift r to â„â‰¥0 using hrâ‚€
  exact f.exists_mul_lt_apply_of_lt_op_nnnorm hr
#align continuous_linear_map.exists_mul_lt_of_lt_op_norm ContinuousLinearMap.exists_mul_lt_of_lt_op_norm

theorem exists_lt_apply_of_lt_op_nnnorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„â‰¥0}
    (hr : r < â€–fâ€–â‚Š) : âˆƒ x : E, â€–xâ€–â‚Š < 1 âˆ§ r < â€–f xâ€–â‚Š := by
  obtain âŸ¨y, hyâŸ© := f.exists_mul_lt_apply_of_lt_op_nnnorm hr
  have hy' : â€–yâ€–â‚Š â‰  0 :=
    nnnorm_ne_zero_iff.2 fun heq => by
      simp [heq, nnnorm_zero, map_zero, not_lt_zero'] at hy
  have hfy : â€–f yâ€–â‚Š â‰  0 := (zero_le'.trans_lt hy).ne'
  rw [â† inv_inv â€–f yâ€–â‚Š, NNReal.lt_inv_iff_mul_lt (inv_ne_zero hfy), mul_assoc, mul_comm â€–yâ€–â‚Š, â†
    mul_assoc, â† NNReal.lt_inv_iff_mul_lt hy'] at hy
  obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© := NormedField.exists_lt_nnnorm_lt ğ•œ hy
  refine' âŸ¨k â€¢ y, (nnnorm_smul k y).symm â–¸ (NNReal.lt_inv_iff_mul_lt hy').1 hkâ‚‚, _âŸ©
  have : â€–Ïƒâ‚â‚‚ kâ€–â‚Š = â€–kâ€–â‚Š := Subtype.ext RingHomIsometric.is_iso
  rwa [map_smulâ‚›â‚— f, nnnorm_smul, â† NNReal.div_lt_iff hfy, div_eq_mul_inv, this]
#align continuous_linear_map.exists_lt_apply_of_lt_op_nnnorm ContinuousLinearMap.exists_lt_apply_of_lt_op_nnnorm

theorem exists_lt_apply_of_lt_op_norm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) {r : â„}
    (hr : r < â€–fâ€–) : âˆƒ x : E, â€–xâ€– < 1 âˆ§ r < â€–f xâ€– := by
  by_cases hrâ‚€ : r < 0
  Â· exact âŸ¨0, by simpa using hrâ‚€âŸ©
  Â· lift r to â„â‰¥0 using not_lt.1 hrâ‚€
    exact f.exists_lt_apply_of_lt_op_nnnorm hr
#align continuous_linear_map.exists_lt_apply_of_lt_op_norm ContinuousLinearMap.exists_lt_apply_of_lt_op_norm

theorem sSup_unit_ball_eq_nnnorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' ball 0 1) = â€–fâ€–â‚Š := by
  refine' csSup_eq_of_forall_le_of_forall_lt_exists_gt ((nonempty_ball.mpr zero_lt_one).image _) _
    fun ub hub => _
  Â· rintro - âŸ¨x, hx, rflâŸ©
    simpa only [mul_one] using f.le_op_norm_of_le (mem_ball_zero_iff.1 hx).le
  Â· obtain âŸ¨x, hx, hxfâŸ© := f.exists_lt_apply_of_lt_op_nnnorm hub
    exact âŸ¨_, âŸ¨x, mem_ball_zero_iff.2 hx, rflâŸ©, hxfâŸ©
#align continuous_linear_map.Sup_unit_ball_eq_nnnorm ContinuousLinearMap.sSup_unit_ball_eq_nnnorm

theorem sSup_unit_ball_eq_norm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E] [SeminormedAddCommGroup F]
    [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [NormedSpace ğ•œ E]
    [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' ball 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using NNReal.coe_eq.2 f.sSup_unit_ball_eq_nnnorm
#align continuous_linear_map.Sup_unit_ball_eq_norm ContinuousLinearMap.sSup_unit_ball_eq_norm

theorem sSup_closed_unit_ball_eq_nnnorm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–â‚Š) '' closedBall 0 1) = â€–fâ€–â‚Š := by
  have hbdd : âˆ€ y âˆˆ (fun x => â€–f xâ€–â‚Š) '' closedBall 0 1, y â‰¤ â€–fâ€–â‚Š := by
    rintro - âŸ¨x, hx, rflâŸ©
    exact f.unit_le_op_norm x (mem_closedBall_zero_iff.1 hx)
  refine' le_antisymm (csSup_le ((nonempty_closedBall.mpr zero_le_one).image _) hbdd) _
  rw [â† sSup_unit_ball_eq_nnnorm]
  exact csSup_le_csSup âŸ¨â€–fâ€–â‚Š, hbddâŸ© ((nonempty_ball.2 zero_lt_one).image _)
    (Set.image_subset _ ball_subset_closedBall)
#align continuous_linear_map.Sup_closed_unit_ball_eq_nnnorm ContinuousLinearMap.sSup_closed_unit_ball_eq_nnnorm

theorem sSup_closed_unit_ball_eq_norm {ğ•œ ğ•œâ‚‚ E F : Type*} [NormedAddCommGroup E]
    [SeminormedAddCommGroup F] [DenselyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
    [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    sSup ((fun x => â€–f xâ€–) '' closedBall 0 1) = â€–fâ€– := by
  simpa only [NNReal.coe_sSup, Set.image_image] using
    NNReal.coe_eq.2 f.sSup_closed_unit_ball_eq_nnnorm
#align continuous_linear_map.Sup_closed_unit_ball_eq_norm ContinuousLinearMap.sSup_closed_unit_ball_eq_norm

end Sup

section

theorem op_norm_ext [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚‚] F) (g : E â†’SL[Ïƒâ‚â‚ƒ] G)
    (h : âˆ€ x, â€–f xâ€– = â€–g xâ€–) : â€–fâ€– = â€–gâ€– :=
  op_norm_eq_of_bounds (norm_nonneg _)
    (fun x => by
      rw [h x]
      exact le_op_norm _ _)
    fun c hc hâ‚‚ =>
    op_norm_le_bound _ hc fun z => by
      rw [â† h z]
      exact hâ‚‚ z
#align continuous_linear_map.op_norm_ext ContinuousLinearMap.op_norm_ext

variable [RingHomIsometric Ïƒâ‚‚â‚ƒ]

theorem op_norm_le_boundâ‚‚ (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) {C : â„} (h0 : 0 â‰¤ C)
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–fâ€– â‰¤ C :=
  f.op_norm_le_bound h0 fun x => (f x).op_norm_le_bound (mul_nonneg h0 (norm_nonneg _)) <| hC x
#align continuous_linear_map.op_norm_le_boundâ‚‚ ContinuousLinearMap.op_norm_le_boundâ‚‚

theorem le_op_normâ‚‚ [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x : E) (y : F) :
    â€–f x yâ€– â‰¤ â€–fâ€– * â€–xâ€– * â€–yâ€– :=
  (f x).le_of_op_norm_le (f.le_op_norm x) y
#align continuous_linear_map.le_op_normâ‚‚ ContinuousLinearMap.le_op_normâ‚‚

-- porting note: new theorem
theorem le_of_op_normâ‚‚_le_of_le [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) {x : E} {y : F}
    {a b c : â„} (hf : â€–fâ€– â‰¤ a) (hx : â€–xâ€– â‰¤ b) (hy : â€–yâ€– â‰¤ c) :
    â€–f x yâ€– â‰¤ a * b * c :=
  (f x).le_of_op_norm_le_of_le (f.le_of_op_norm_le_of_le hf hx) hy

end

@[simp]
theorem op_norm_prod (f : E â†’L[ğ•œ] Fâ‚—) (g : E â†’L[ğ•œ] Gâ‚—) : â€–f.prod gâ€– = â€–(f, g)â€– :=
  le_antisymm
      (op_norm_le_bound _ (norm_nonneg _) fun x => by
        simpa only [prod_apply, Prod.norm_def, max_mul_of_nonneg, norm_nonneg] using
          max_le_max (le_op_norm f x) (le_op_norm g x)) <|
    max_le
      (op_norm_le_bound _ (norm_nonneg _) fun x =>
        (le_max_left _ _).trans ((f.prod g).le_op_norm x))
      (op_norm_le_bound _ (norm_nonneg _) fun x =>
        (le_max_right _ _).trans ((f.prod g).le_op_norm x))
#align continuous_linear_map.op_norm_prod ContinuousLinearMap.op_norm_prod

@[simp]
theorem op_nnnorm_prod (f : E â†’L[ğ•œ] Fâ‚—) (g : E â†’L[ğ•œ] Gâ‚—) : â€–f.prod gâ€–â‚Š = â€–(f, g)â€–â‚Š :=
  Subtype.ext <| op_norm_prod f g
#align continuous_linear_map.op_nnnorm_prod ContinuousLinearMap.op_nnnorm_prod

/-- `ContinuousLinearMap.prod` as a `LinearIsometryEquiv`. -/
def prodâ‚—áµ¢ (R : Type*) [Semiring R] [Module R Fâ‚—] [Module R Gâ‚—] [ContinuousConstSMul R Fâ‚—]
    [ContinuousConstSMul R Gâ‚—] [SMulCommClass ğ•œ R Fâ‚—] [SMulCommClass ğ•œ R Gâ‚—] :
    (E â†’L[ğ•œ] Fâ‚—) Ã— (E â†’L[ğ•œ] Gâ‚—) â‰ƒâ‚—áµ¢[R] E â†’L[ğ•œ] Fâ‚— Ã— Gâ‚— :=
  âŸ¨prodâ‚— R, fun âŸ¨f, gâŸ© => op_norm_prod f gâŸ©
#align continuous_linear_map.prodâ‚—áµ¢ ContinuousLinearMap.prodâ‚—áµ¢

variable [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F)

@[simp, nontriviality]
theorem op_norm_subsingleton [Subsingleton E] : â€–fâ€– = 0 := by
  refine' le_antisymm _ (norm_nonneg _)
  apply op_norm_le_bound _ rfl.ge
  intro x
  simp [Subsingleton.elim x 0]
#align continuous_linear_map.op_norm_subsingleton ContinuousLinearMap.op_norm_subsingleton

end OpNorm

section IsO

set_option linter.uppercaseLean3 false

variable [RingHomIsometric Ïƒâ‚â‚‚] (c : ğ•œ) (f g : E â†’SL[Ïƒâ‚â‚‚] F) (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x y z : E)

open Asymptotics

theorem isBigOWith_id (l : Filter E) : IsBigOWith â€–fâ€– l f fun x => x :=
  isBigOWith_of_le' _ f.le_op_norm
#align continuous_linear_map.is_O_with_id ContinuousLinearMap.isBigOWith_id

theorem isBigO_id (l : Filter E) : f =O[l] fun x => x :=
  (f.isBigOWith_id l).isBigO
#align continuous_linear_map.is_O_id ContinuousLinearMap.isBigO_id

theorem isBigOWith_comp [RingHomIsometric Ïƒâ‚‚â‚ƒ] {Î± : Type*} (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : Î± â†’ F)
    (l : Filter Î±) : IsBigOWith â€–gâ€– l (fun x' => g (f x')) f :=
  (g.isBigOWith_id âŠ¤).comp_tendsto le_top
#align continuous_linear_map.is_O_with_comp ContinuousLinearMap.isBigOWith_comp

theorem isBigO_comp [RingHomIsometric Ïƒâ‚‚â‚ƒ] {Î± : Type*} (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (f : Î± â†’ F)
    (l : Filter Î±) : (fun x' => g (f x')) =O[l] f :=
  (g.isBigOWith_comp f l).isBigO
#align continuous_linear_map.is_O_comp ContinuousLinearMap.isBigO_comp

theorem isBigOWith_sub (f : E â†’SL[Ïƒâ‚â‚‚] F) (l : Filter E) (x : E) :
    IsBigOWith â€–fâ€– l (fun x' => f (x' - x)) fun x' => x' - x :=
  f.isBigOWith_comp _ l
#align continuous_linear_map.is_O_with_sub ContinuousLinearMap.isBigOWith_sub

theorem isBigO_sub (f : E â†’SL[Ïƒâ‚â‚‚] F) (l : Filter E) (x : E) :
    (fun x' => f (x' - x)) =O[l] fun x' => x' - x :=
  f.isBigO_comp _ l
#align continuous_linear_map.is_O_sub ContinuousLinearMap.isBigO_sub

end IsO

end ContinuousLinearMap

namespace LinearIsometry

theorem norm_toContinuousLinearMap_le (f : E â†’â‚›â‚—áµ¢[Ïƒâ‚â‚‚] F) : â€–f.toContinuousLinearMapâ€– â‰¤ 1 :=
  f.toContinuousLinearMap.op_norm_le_bound zero_le_one fun x => by simp
#align linear_isometry.norm_to_continuous_linear_map_le LinearIsometry.norm_toContinuousLinearMap_le

end LinearIsometry

namespace LinearMap

/-- If a continuous linear map is constructed from a linear map via the constructor `mkContinuous`,
then its norm is bounded by the bound given to the constructor if it is nonnegative. -/
theorem mkContinuous_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (hC : 0 â‰¤ C) (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    â€–f.mkContinuous C hâ€– â‰¤ C :=
  ContinuousLinearMap.op_norm_le_bound _ hC h
#align linear_map.mk_continuous_norm_le LinearMap.mkContinuous_norm_le

/-- If a continuous linear map is constructed from a linear map via the constructor `mkContinuous`,
then its norm is bounded by the bound or zero if bound is negative. -/
theorem mkContinuous_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    â€–f.mkContinuous C hâ€– â‰¤ max C 0 :=
  ContinuousLinearMap.op_norm_le_bound _ (le_max_right _ _) fun x =>
    (h x).trans <| mul_le_mul_of_nonneg_right (le_max_left _ _) (norm_nonneg x)
#align linear_map.mk_continuous_norm_le' LinearMap.mkContinuous_norm_le'

variable [RingHomIsometric Ïƒâ‚‚â‚ƒ]

lemma norm_mkContinuousâ‚‚_aux (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) (C : â„)
    (h : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) (x : E) :
    â€–(f x).mkContinuous (C * â€–xâ€–) (h x)â€– â‰¤ max C 0 * â€–xâ€– :=
  (mkContinuous_norm_le' (f x) (h x)).trans_eq <| by
    rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

/-- Create a bilinear map (represented as a map `E â†’L[ğ•œ] F â†’L[ğ•œ] G`) from the corresponding linear
map and existence of a bound on the norm of the image. The linear map can be constructed using
`LinearMap.mkâ‚‚`.

If you have an explicit bound, use `LinearMap.mkContinuousâ‚‚` instead, as a norm estimate will
follow automatically in `LinearMap.mkContinuousâ‚‚_norm_le`. -/
def mkContinuousOfExistsBoundâ‚‚ (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G)
    (h : âˆƒ C, âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
  LinearMap.mkContinuousOfExistsBound
    { toFun := fun x => (f x).mkContinuousOfExistsBound <| let âŸ¨C, hCâŸ© := h; âŸ¨C * â€–xâ€–, hC xâŸ©
      map_add' := fun x y => by
        ext z
        rw [ContinuousLinearMap.add_apply, mkContinuousOfExistsBound_apply,
          mkContinuousOfExistsBound_apply, mkContinuousOfExistsBound_apply, map_add, add_apply]
      map_smul' := fun c x => by
        ext z
        rw [ContinuousLinearMap.smul_apply, mkContinuousOfExistsBound_apply,
          mkContinuousOfExistsBound_apply, map_smulâ‚›â‚—, smul_apply] } <|
    let âŸ¨C, hCâŸ© := h; âŸ¨max C 0, norm_mkContinuousâ‚‚_aux f C hCâŸ©

/-- Create a bilinear map (represented as a map `E â†’L[ğ•œ] F â†’L[ğ•œ] G`) from the corresponding linear
map and a bound on the norm of the image. The linear map can be constructed using
`LinearMap.mkâ‚‚`. Lemmas `LinearMap.mkContinuousâ‚‚_norm_le'` and `LinearMap.mkContinuousâ‚‚_norm_le`
provide estimates on the norm of an operator constructed using this function. -/
def mkContinuousâ‚‚ (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) (C : â„) (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) :
    E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
  mkContinuousOfExistsBoundâ‚‚ f âŸ¨C, hCâŸ©
#align linear_map.mk_continuousâ‚‚ LinearMap.mkContinuousâ‚‚

@[simp]
theorem mkContinuousâ‚‚_apply (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) (x : E) (y : F) : f.mkContinuousâ‚‚ C hC x y = f x y :=
  rfl
#align linear_map.mk_continuousâ‚‚_apply LinearMap.mkContinuousâ‚‚_apply

theorem mkContinuousâ‚‚_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–f.mkContinuousâ‚‚ C hCâ€– â‰¤ max C 0 :=
  mkContinuous_norm_le _ (le_max_iff.2 <| Or.inr le_rfl) (norm_mkContinuousâ‚‚_aux f C hC)
#align linear_map.mk_continuousâ‚‚_norm_le' LinearMap.mkContinuousâ‚‚_norm_le'

theorem mkContinuousâ‚‚_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„} (h0 : 0 â‰¤ C)
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–f.mkContinuousâ‚‚ C hCâ€– â‰¤ C :=
  (f.mkContinuousâ‚‚_norm_le' hC).trans_eq <| max_eq_left h0
#align linear_map.mk_continuousâ‚‚_norm_le LinearMap.mkContinuousâ‚‚_norm_le

end LinearMap

namespace ContinuousLinearMap

variable [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚ƒ]

/-- Flip the order of arguments of a continuous bilinear map.
For a version bundled as `LinearIsometryEquiv`, see
`ContinuousLinearMap.flipL`. -/
def flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : F â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G :=
  LinearMap.mkContinuousâ‚‚
    -- Porting note: the `simp only`s below used to be `rw`.
    -- Now that doesn't work as we need to do some beta reduction along the way.
    (LinearMap.mkâ‚‚'â‚›â‚— Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ (fun y x => f x y) (fun x y z => (f z).map_add x y)
      (fun c y x => (f x).map_smulâ‚›â‚— c y) (fun z x y => by simp only [f.map_add, add_apply])
        (fun c y x => by simp only [f.map_smulâ‚›â‚—, smul_apply]))
    â€–fâ€– fun y x => (f.le_op_normâ‚‚ x y).trans_eq <| by simp only [mul_right_comm]
#align continuous_linear_map.flip ContinuousLinearMap.flip

-- Porting note: in mathlib3, in the proof `norm_nonneg (flip f)` was just `norm_nonneg _`,
-- but this causes a defeq error now.
private theorem le_norm_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : â€–fâ€– â‰¤ â€–flip fâ€– :=
  f.op_norm_le_boundâ‚‚ (norm_nonneg (flip f)) fun x y => by
    rw [mul_right_comm]
    exact (flip f).le_op_normâ‚‚ y x

@[simp]
theorem flip_apply (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x : E) (y : F) : f.flip y x = f x y :=
  rfl
#align continuous_linear_map.flip_apply ContinuousLinearMap.flip_apply

@[simp]
theorem flip_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : f.flip.flip = f := by
  ext
  rfl
#align continuous_linear_map.flip_flip ContinuousLinearMap.flip_flip

@[simp]
theorem op_norm_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : â€–f.flipâ€– = â€–fâ€– :=
  le_antisymm (by simpa only [flip_flip] using le_norm_flip f.flip) (le_norm_flip f)
#align continuous_linear_map.op_norm_flip ContinuousLinearMap.op_norm_flip

@[simp]
theorem flip_add (f g : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : (f + g).flip = f.flip + g.flip :=
  rfl
#align continuous_linear_map.flip_add ContinuousLinearMap.flip_add

@[simp]
theorem flip_smul (c : ğ•œâ‚ƒ) (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : (c â€¢ f).flip = c â€¢ f.flip :=
  rfl
#align continuous_linear_map.flip_smul ContinuousLinearMap.flip_smul

variable (E F G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ)

/-- Flip the order of arguments of a continuous bilinear map.
This is a version bundled as a `LinearIsometryEquiv`.
For an unbundled version see `ContinuousLinearMap.flip`. -/
def flipâ‚—áµ¢' : (E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) â‰ƒâ‚—áµ¢[ğ•œâ‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G where
  toFun := flip
  invFun := flip
  map_add' := flip_add
  map_smul' := flip_smul
  left_inv := flip_flip
  right_inv := flip_flip
  norm_map' := op_norm_flip
#align continuous_linear_map.flipâ‚—áµ¢' ContinuousLinearMap.flipâ‚—áµ¢'

variable {E F G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ}

@[simp]
theorem flipâ‚—áµ¢'_symm : (flipâ‚—áµ¢' E F G Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ).symm = flipâ‚—áµ¢' F E G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ :=
  rfl
#align continuous_linear_map.flipâ‚—áµ¢'_symm ContinuousLinearMap.flipâ‚—áµ¢'_symm

@[simp]
theorem coe_flipâ‚—áµ¢' : â‡‘(flipâ‚—áµ¢' E F G Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ) = flip :=
  rfl
#align continuous_linear_map.coe_flipâ‚—áµ¢' ContinuousLinearMap.coe_flipâ‚—áµ¢'

variable (ğ•œ E Fâ‚— Gâ‚—)

/-- Flip the order of arguments of a continuous bilinear map.
This is a version bundled as a `LinearIsometryEquiv`.
For an unbundled version see `ContinuousLinearMap.flip`. -/
def flipâ‚—áµ¢ : (E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) â‰ƒâ‚—áµ¢[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚— where
  toFun := flip
  invFun := flip
  map_add' := flip_add
  map_smul' := flip_smul
  left_inv := flip_flip
  right_inv := flip_flip
  norm_map' := op_norm_flip
#align continuous_linear_map.flipâ‚—áµ¢ ContinuousLinearMap.flipâ‚—áµ¢

variable {ğ•œ E Fâ‚— Gâ‚—}

@[simp]
theorem flipâ‚—áµ¢_symm : (flipâ‚—áµ¢ ğ•œ E Fâ‚— Gâ‚—).symm = flipâ‚—áµ¢ ğ•œ Fâ‚— E Gâ‚— :=
  rfl
#align continuous_linear_map.flipâ‚—áµ¢_symm ContinuousLinearMap.flipâ‚—áµ¢_symm

@[simp]
theorem coe_flipâ‚—áµ¢ : â‡‘(flipâ‚—áµ¢ ğ•œ E Fâ‚— Gâ‚—) = flip :=
  rfl
#align continuous_linear_map.coe_flipâ‚—áµ¢ ContinuousLinearMap.coe_flipâ‚—áµ¢

variable (F Ïƒâ‚â‚‚) [RingHomIsometric Ïƒâ‚â‚‚]

/-- The continuous semilinear map obtained by applying a continuous semilinear map at a given
vector.

This is the continuous version of `LinearMap.applyâ‚—`. -/
def apply' : E â†’SL[Ïƒâ‚â‚‚] (E â†’SL[Ïƒâ‚â‚‚] F) â†’L[ğ•œâ‚‚] F :=
  flip (id ğ•œâ‚‚ (E â†’SL[Ïƒâ‚â‚‚] F))
#align continuous_linear_map.apply' ContinuousLinearMap.apply'

variable {F Ïƒâ‚â‚‚}

@[simp]
theorem apply_apply' (v : E) (f : E â†’SL[Ïƒâ‚â‚‚] F) : apply' F Ïƒâ‚â‚‚ v f = f v :=
  rfl
#align continuous_linear_map.apply_apply' ContinuousLinearMap.apply_apply'

variable (ğ•œ Fâ‚—)

/-- The continuous semilinear map obtained by applying a continuous semilinear map at a given
vector.

This is the continuous version of `LinearMap.applyâ‚—`. -/
def apply : E â†’L[ğ•œ] (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Fâ‚— :=
  flip (id ğ•œ (E â†’L[ğ•œ] Fâ‚—))
#align continuous_linear_map.apply ContinuousLinearMap.apply

variable {ğ•œ Fâ‚—}

@[simp]
theorem apply_apply (v : E) (f : E â†’L[ğ•œ] Fâ‚—) : apply ğ•œ Fâ‚— v f = f v :=
  rfl
#align continuous_linear_map.apply_apply ContinuousLinearMap.apply_apply

variable (Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E F G)

set_option linter.uppercaseLean3 false

/-- Composition of continuous semilinear maps as a continuous semibilinear map. -/
def compSL : (F â†’SL[Ïƒâ‚‚â‚ƒ] G) â†’L[ğ•œâ‚ƒ] (E â†’SL[Ïƒâ‚â‚‚] F) â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G :=
  LinearMap.mkContinuousâ‚‚
    (LinearMap.mkâ‚‚'â‚›â‚— (RingHom.id ğ•œâ‚ƒ) Ïƒâ‚‚â‚ƒ comp add_comp smul_comp comp_add fun c f g => by
      ext
      simp only [ContinuousLinearMap.map_smulâ‚›â‚—, coe_smul', coe_comp', Function.comp_apply,
        Pi.smul_apply])
    1 fun f g => by simpa only [one_mul] using op_norm_comp_le f g
#align continuous_linear_map.compSL ContinuousLinearMap.compSL

theorem norm_compSL_le :
    -- porting note: added
    letI : Norm ((F â†’SL[Ïƒâ‚‚â‚ƒ] G) â†’L[ğ•œâ‚ƒ] (E â†’SL[Ïƒâ‚â‚‚] F) â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G) :=
      hasOpNorm (E := F â†’SL[Ïƒâ‚‚â‚ƒ] G) (F := (E â†’SL[Ïƒâ‚â‚‚] F) â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G)
    â€–compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒâ€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _
#align continuous_linear_map.norm_compSL_le ContinuousLinearMap.norm_compSL_le

variable {Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E F G}

@[simp]
theorem compSL_apply (f : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (g : E â†’SL[Ïƒâ‚â‚‚] F) : compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ f g = f.comp g :=
  rfl
#align continuous_linear_map.compSL_apply ContinuousLinearMap.compSL_apply

theorem _root_.Continuous.const_clm_comp {X} [TopologicalSpace X] {f : X â†’ E â†’SL[Ïƒâ‚â‚‚] F}
    (hf : Continuous f) (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) :
    Continuous (fun x => g.comp (f x) : X â†’ E â†’SL[Ïƒâ‚â‚ƒ] G) :=
  (compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ g).continuous.comp hf
#align continuous.const_clm_comp Continuous.const_clm_comp

-- Giving the implicit argument speeds up elaboration significantly
theorem _root_.Continuous.clm_comp_const {X} [TopologicalSpace X] {g : X â†’ F â†’SL[Ïƒâ‚‚â‚ƒ] G}
    (hg : Continuous g) (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    Continuous (fun x => (g x).comp f : X â†’ E â†’SL[Ïƒâ‚â‚ƒ] G) :=
  (@ContinuousLinearMap.flip _ _ _ _ _ (E â†’SL[Ïƒâ‚â‚ƒ] G) _ _ _ _ _ _ _ _ _ _ _ _ _
    (compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ) f).continuous.comp hg
#align continuous.clm_comp_const Continuous.clm_comp_const

variable (ğ•œ Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E Fâ‚— Gâ‚—)

/-- Composition of continuous linear maps as a continuous bilinear map. -/
def compL : (Fâ‚— â†’L[ğ•œ] Gâ‚—) â†’L[ğ•œ] (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚— :=
  compSL E Fâ‚— Gâ‚— (RingHom.id ğ•œ) (RingHom.id ğ•œ)
#align continuous_linear_map.compL ContinuousLinearMap.compL

theorem norm_compL_le :
    letI : Norm ((Fâ‚— â†’L[ğ•œ] Gâ‚—) â†’L[ğ•œ] (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚—) :=
      hasOpNorm (E := Fâ‚— â†’L[ğ•œ] Gâ‚—) (F := (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚—)
    â€–compL ğ•œ E Fâ‚— Gâ‚—â€– â‰¤ 1 :=
  norm_compSL_le _ _ _ _ _
#align continuous_linear_map.norm_compL_le ContinuousLinearMap.norm_compL_le

@[simp]
theorem compL_apply (f : Fâ‚— â†’L[ğ•œ] Gâ‚—) (g : E â†’L[ğ•œ] Fâ‚—) : compL ğ•œ E Fâ‚— Gâ‚— f g = f.comp g :=
  rfl
#align continuous_linear_map.compL_apply ContinuousLinearMap.compL_apply

variable (Eâ‚—) {ğ•œ E Fâ‚— Gâ‚—}

/-- Apply `L(x,-)` pointwise to bilinear maps, as a continuous bilinear map -/
@[simps! apply]
def precompR (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : E â†’L[ğ•œ] (Eâ‚— â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚— :=
  (compL ğ•œ Eâ‚— Fâ‚— Gâ‚—).comp L
#align continuous_linear_map.precompR ContinuousLinearMap.precompR

/-- Apply `L(-,y)` pointwise to bilinear maps, as a continuous bilinear map -/
def precompL (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : (Eâ‚— â†’L[ğ•œ] E) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚— :=
  (precompR Eâ‚— (flip L)).flip
#align continuous_linear_map.precompL ContinuousLinearMap.precompL

theorem norm_precompR_le (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) :
    -- porting note: added
    letI : SeminormedAddCommGroup ((Eâ‚— â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚—) := inferInstance
    letI : NormedSpace ğ•œ ((Eâ‚— â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚—) := inferInstance
    â€–precompR Eâ‚— Lâ€– â‰¤ â€–Lâ€– :=
  calc
    â€–precompR Eâ‚— Lâ€– â‰¤ â€–compL ğ•œ Eâ‚— Fâ‚— Gâ‚—â€– * â€–Lâ€– := op_norm_comp_le _ _
    _ â‰¤ 1 * â€–Lâ€– := (mul_le_mul_of_nonneg_right (norm_compL_le _ _ _ _) (norm_nonneg _))
    _ = â€–Lâ€– := by rw [one_mul]
#align continuous_linear_map.norm_precompR_le ContinuousLinearMap.norm_precompR_le

theorem norm_precompL_le (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) :
    -- porting note: added
    letI : Norm ((Eâ‚— â†’L[ğ•œ] E) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚—) :=
      hasOpNorm (E := Eâ‚— â†’L[ğ•œ] E) (F := Fâ‚— â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚—)
    â€–precompL Eâ‚— Lâ€– â‰¤ â€–Lâ€– := by
  rw [precompL, op_norm_flip, â† op_norm_flip L]
  exact norm_precompR_le _ L.flip
#align continuous_linear_map.norm_precompL_le ContinuousLinearMap.norm_precompL_le

section Prod

universe uâ‚ uâ‚‚ uâ‚ƒ uâ‚„

variable (Mâ‚ : Type uâ‚) [SeminormedAddCommGroup Mâ‚] [NormedSpace ğ•œ Mâ‚] (Mâ‚‚ : Type uâ‚‚)
  [SeminormedAddCommGroup Mâ‚‚] [NormedSpace ğ•œ Mâ‚‚] (Mâ‚ƒ : Type uâ‚ƒ) [SeminormedAddCommGroup Mâ‚ƒ]
  [NormedSpace ğ•œ Mâ‚ƒ] (Mâ‚„ : Type uâ‚„) [SeminormedAddCommGroup Mâ‚„] [NormedSpace ğ•œ Mâ‚„]

variable {Eâ‚—} (ğ•œ)

/-- `ContinuousLinearMap.prodMap` as a continuous linear map. -/
def prodMapL : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) Ã— (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„) â†’L[ğ•œ] Mâ‚ Ã— Mâ‚ƒ â†’L[ğ•œ] Mâ‚‚ Ã— Mâ‚„ :=
  ContinuousLinearMap.copy
    (have Î¦â‚ : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) â†’L[ğ•œ] Mâ‚ â†’L[ğ•œ] Mâ‚‚ Ã— Mâ‚„ :=
      ContinuousLinearMap.compL ğ•œ Mâ‚ Mâ‚‚ (Mâ‚‚ Ã— Mâ‚„) (ContinuousLinearMap.inl ğ•œ Mâ‚‚ Mâ‚„)
    have Î¦â‚‚ : (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„) â†’L[ğ•œ] Mâ‚ƒ â†’L[ğ•œ] Mâ‚‚ Ã— Mâ‚„ :=
      ContinuousLinearMap.compL ğ•œ Mâ‚ƒ Mâ‚„ (Mâ‚‚ Ã— Mâ‚„) (ContinuousLinearMap.inr ğ•œ Mâ‚‚ Mâ‚„)
    have Î¦â‚' :=
      (ContinuousLinearMap.compL ğ•œ (Mâ‚ Ã— Mâ‚ƒ) Mâ‚ (Mâ‚‚ Ã— Mâ‚„)).flip (ContinuousLinearMap.fst ğ•œ Mâ‚ Mâ‚ƒ)
    have Î¦â‚‚' :=
      (ContinuousLinearMap.compL ğ•œ (Mâ‚ Ã— Mâ‚ƒ) Mâ‚ƒ (Mâ‚‚ Ã— Mâ‚„)).flip (ContinuousLinearMap.snd ğ•œ Mâ‚ Mâ‚ƒ)
    have Î¨â‚ : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) Ã— (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„) â†’L[ğ•œ] Mâ‚ â†’L[ğ•œ] Mâ‚‚ :=
      ContinuousLinearMap.fst ğ•œ (Mâ‚ â†’L[ğ•œ] Mâ‚‚) (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„)
    have Î¨â‚‚ : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) Ã— (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„) â†’L[ğ•œ] Mâ‚ƒ â†’L[ğ•œ] Mâ‚„ :=
      ContinuousLinearMap.snd ğ•œ (Mâ‚ â†’L[ğ•œ] Mâ‚‚) (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„)
    Î¦â‚' âˆ˜L Î¦â‚ âˆ˜L Î¨â‚ + Î¦â‚‚' âˆ˜L Î¦â‚‚ âˆ˜L Î¨â‚‚)
    (fun p : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) Ã— (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„) => p.1.prodMap p.2) (by
      apply funext
      rintro âŸ¨Ï†, ÏˆâŸ©
      refine' ContinuousLinearMap.ext fun âŸ¨xâ‚, xâ‚‚âŸ© => _
      -- Porting note: mathport suggested:
      -- ```
      -- simp only [add_apply, coe_comp', coe_fst', Function.comp_apply, compL_apply, flip_apply,
      --   coe_snd', inl_apply, inr_apply, Prod.mk_add_mk, add_zero, zero_add, coe_prodMap'
      --   Prod_map, Prod.mk.inj_iff, eq_self_iff_true, and_self_iff]
      -- rfl
      -- ```
      -- Frustratingly, in `mathlib3` we can use:
      -- ```
      -- dsimp   -- âŠ¢ (â‡‘Ï† x.fst, â‡‘Ïˆ x.snd) = (â‡‘Ï† x.fst + 0, 0 + â‡‘Ïˆ x.snd)
      -- simp
      -- ```
      -- Here neither `dsimp` or `simp` seem to make progress.
      -- We have to use `rw` to access `.default` reducibility. `simp` cannot
      -- separate `rw`'s are faster than a single block
      rw [add_apply, add_apply, comp_apply, comp_apply, comp_apply, comp_apply]
      rw [flip_apply, flip_apply]
      rw [compL_apply, compL_apply, compL_apply, compL_apply]
      rw [comp_apply, comp_apply, comp_apply, comp_apply]
      simp only [coe_prodMap', Prod_map, coe_fst', inl_apply, coe_snd', inr_apply, Prod.mk_add_mk,
        add_zero, zero_add])
#align continuous_linear_map.prod_mapL ContinuousLinearMap.prodMapL

variable {Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„}

@[simp]
theorem prodMapL_apply (p : (Mâ‚ â†’L[ğ•œ] Mâ‚‚) Ã— (Mâ‚ƒ â†’L[ğ•œ] Mâ‚„)) :
    ContinuousLinearMap.prodMapL ğ•œ Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„ p = p.1.prodMap p.2 :=
  rfl
#align continuous_linear_map.prod_mapL_apply ContinuousLinearMap.prodMapL_apply

variable {X : Type*} [TopologicalSpace X]

theorem _root_.Continuous.prod_mapL {f : X â†’ Mâ‚ â†’L[ğ•œ] Mâ‚‚} {g : X â†’ Mâ‚ƒ â†’L[ğ•œ] Mâ‚„} (hf : Continuous f)
    (hg : Continuous g) : Continuous fun x => (f x).prodMap (g x) :=
  (prodMapL ğ•œ Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„).continuous.comp (hf.prod_mk hg)
#align continuous.prod_mapL Continuous.prod_mapL

theorem _root_.Continuous.prod_map_equivL {f : X â†’ Mâ‚ â‰ƒL[ğ•œ] Mâ‚‚} {g : X â†’ Mâ‚ƒ â‰ƒL[ğ•œ] Mâ‚„}
    (hf : Continuous fun x => (f x : Mâ‚ â†’L[ğ•œ] Mâ‚‚)) (hg : Continuous fun x => (g x : Mâ‚ƒ â†’L[ğ•œ] Mâ‚„)) :
    Continuous fun x => ((f x).prod (g x) : Mâ‚ Ã— Mâ‚ƒ â†’L[ğ•œ] Mâ‚‚ Ã— Mâ‚„) :=
  (prodMapL ğ•œ Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„).continuous.comp (hf.prod_mk hg)
#align continuous.prod_map_equivL Continuous.prod_map_equivL

theorem _root_.ContinuousOn.prod_mapL {f : X â†’ Mâ‚ â†’L[ğ•œ] Mâ‚‚} {g : X â†’ Mâ‚ƒ â†’L[ğ•œ] Mâ‚„} {s : Set X}
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun x => (f x).prodMap (g x)) s :=
  ((prodMapL ğ•œ Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„).continuous.comp_continuousOn (hf.prod hg) : _)
#align continuous_on.prod_mapL ContinuousOn.prod_mapL

theorem _root_.ContinuousOn.prod_map_equivL {f : X â†’ Mâ‚ â‰ƒL[ğ•œ] Mâ‚‚} {g : X â†’ Mâ‚ƒ â‰ƒL[ğ•œ] Mâ‚„} {s : Set X}
    (hf : ContinuousOn (fun x => (f x : Mâ‚ â†’L[ğ•œ] Mâ‚‚)) s)
    (hg : ContinuousOn (fun x => (g x : Mâ‚ƒ â†’L[ğ•œ] Mâ‚„)) s) :
    ContinuousOn (fun x => ((f x).prod (g x) : Mâ‚ Ã— Mâ‚ƒ â†’L[ğ•œ] Mâ‚‚ Ã— Mâ‚„)) s :=
  (prodMapL ğ•œ Mâ‚ Mâ‚‚ Mâ‚ƒ Mâ‚„).continuous.comp_continuousOn (hf.prod hg)
#align continuous_on.prod_map_equivL ContinuousOn.prod_map_equivL

end Prod

section MultiplicationLinear

section NonUnital

variable (ğ•œ) (ğ•œ' : Type*) [NonUnitalSeminormedRing ğ•œ']

variable [NormedSpace ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ'] [SMulCommClass ğ•œ ğ•œ' ğ•œ']

/-- Multiplication in a non-unital normed algebra as a continuous bilinear map. -/
def mul : ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  (LinearMap.mul ğ•œ ğ•œ').mkContinuousâ‚‚ 1 fun x y => by simpa using norm_mul_le x y
#align continuous_linear_map.mul ContinuousLinearMap.mul

@[simp]
theorem mul_apply' (x y : ğ•œ') : mul ğ•œ ğ•œ' x y = x * y :=
  rfl
#align continuous_linear_map.mul_apply' ContinuousLinearMap.mul_apply'

@[simp]
theorem op_norm_mul_apply_le (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€– â‰¤ â€–xâ€– :=
  op_norm_le_bound _ (norm_nonneg x) (norm_mul_le x)
#align continuous_linear_map.op_norm_mul_apply_le ContinuousLinearMap.op_norm_mul_apply_le

theorem op_norm_mul_le : â€–mul ğ•œ ğ•œ'â€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _
#align continuous_linear_map.op_norm_mul_le ContinuousLinearMap.op_norm_mul_le

/-- Multiplication on the left in a non-unital normed algebra `ğ•œ'` as a non-unital algebra
homomorphism into the algebra of *continuous* linear maps. This is the left regular representation
of `A` acting on itself.

This has more algebraic structure than `ContinuousLinearMap.mul`, but there is no longer continuity
bundled in the first coordinate.  An alternative viewpoint is that this upgrades
`NonUnitalAlgHom.lmul` from a homomorphism into linear maps to a homomorphism into *continuous*
linear maps. -/
def _root_.NonUnitalAlgHom.Lmul : ğ•œ' â†’â‚™â‚[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  { mul ğ•œ ğ•œ' with
    map_mul' := fun _ _ â†¦ ext fun _ â†¦ mul_assoc _ _ _
    map_zero' := ext fun _ â†¦ zero_mul _ }

variable {ğ•œ ğ•œ'} in
@[simp]
theorem _root_.NonUnitalAlgHom.coe_Lmul : â‡‘(NonUnitalAlgHom.Lmul ğ•œ ğ•œ') = mul ğ•œ ğ•œ' :=
  rfl

/-- Simultaneous left- and right-multiplication in a non-unital normed algebra, considered as a
continuous trilinear map. This is akin to its non-continuous version `LinearMap.mulLeftRight`,
but there is a minor difference: `LinearMap.mulLeftRight` is uncurried. -/
def mulLeftRight : ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' :=
  ((compL ğ•œ ğ•œ' ğ•œ' ğ•œ').comp (mul ğ•œ ğ•œ').flip).flip.comp (mul ğ•œ ğ•œ')
#align continuous_linear_map.mul_left_right ContinuousLinearMap.mulLeftRight

@[simp]
theorem mulLeftRight_apply (x y z : ğ•œ') : mulLeftRight ğ•œ ğ•œ' x y z = x * z * y :=
  rfl
#align continuous_linear_map.mul_left_right_apply ContinuousLinearMap.mulLeftRight_apply

theorem op_norm_mulLeftRight_apply_apply_le (x y : ğ•œ') : â€–mulLeftRight ğ•œ ğ•œ' x yâ€– â‰¤ â€–xâ€– * â€–yâ€– :=
  (op_norm_comp_le _ _).trans <|
    (mul_comm _ _).trans_le <|
      mul_le_mul (op_norm_mul_apply_le _ _ _)
        (op_norm_le_bound _ (norm_nonneg _) fun _ => (norm_mul_le _ _).trans_eq (mul_comm _ _))
        (norm_nonneg _) (norm_nonneg _)
#align continuous_linear_map.op_norm_mul_left_right_apply_apply_le ContinuousLinearMap.op_norm_mulLeftRight_apply_apply_le

theorem op_norm_mulLeftRight_apply_le (x : ğ•œ') : â€–mulLeftRight ğ•œ ğ•œ' xâ€– â‰¤ â€–xâ€– :=
  op_norm_le_bound _ (norm_nonneg x) (op_norm_mulLeftRight_apply_apply_le ğ•œ ğ•œ' x)
#align continuous_linear_map.op_norm_mul_left_right_apply_le ContinuousLinearMap.op_norm_mulLeftRight_apply_le

theorem op_norm_mulLeftRight_le :
    letI : Norm (ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ') := hasOpNorm (E := ğ•œ') (F := ğ•œ' â†’L[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ')
    â€–mulLeftRight ğ•œ ğ•œ'â€– â‰¤ 1 :=
  op_norm_le_bound _ zero_le_one fun x => (one_mul â€–xâ€–).symm â–¸ op_norm_mulLeftRight_apply_le ğ•œ ğ•œ' x
#align continuous_linear_map.op_norm_mul_left_right_le ContinuousLinearMap.op_norm_mulLeftRight_le

/-- This is a mixin class for non-unital normed algebras which states that the left-regular
representation of the algebra on itself is isometric. Every unital normed algebra with `â€–1â€– = 1` is
a regular normed algebra (see `NormedAlgebra.instRegularNormedAlgebra`). In addition, so is every
Câ‹†-algebra, non-unital included (see `CstarRing.instRegularNormedAlgebra`), but there are yet other
examples. Any algebra with an approximate identity (e.g., $$L^1$$) is also regular.

This is a useful class because it gives rise to a nice norm on the unitization; in particular it is
a Câ‹†-norm when the norm on `A` is a Câ‹†-norm. -/
class _root_.RegularNormedAlgebra : Prop :=
  /-- The left regular representation of the algebra on itself is an isometry. -/
  isometry_mul' : Isometry (mul ğ•œ ğ•œ')

/-- Every (unital) normed algebra such that `â€–1â€– = 1` is a `RegularNormedAlgebra`. -/
instance _root_.NormedAlgebra.instRegularNormedAlgebra {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ]
    [SeminormedRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormOneClass ğ•œ'] : RegularNormedAlgebra ğ•œ ğ•œ' where
  isometry_mul' := AddMonoidHomClass.isometry_of_norm (mul ğ•œ ğ•œ') <|
    fun x => le_antisymm (op_norm_mul_apply_le _ _ _) <| by
      convert ratio_le_op_norm ((mul ğ•œ ğ•œ') x) (1 : ğ•œ')
      simp [norm_one]

variable [RegularNormedAlgebra ğ•œ ğ•œ']

lemma isometry_mul : Isometry (mul ğ•œ ğ•œ') :=
  RegularNormedAlgebra.isometry_mul'

@[simp]
lemma op_norm_mul_apply (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€– = â€–xâ€– :=
  (AddMonoidHomClass.isometry_iff_norm (mul ğ•œ ğ•œ')).mp (isometry_mul ğ•œ ğ•œ') x
#align continuous_linear_map.op_norm_mul_apply ContinuousLinearMap.op_norm_mul_applyâ‚“

@[simp]
lemma op_nnnorm_mul_apply (x : ğ•œ') : â€–mul ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| op_norm_mul_apply ğ•œ ğ•œ' x

/-- Multiplication in a normed algebra as a linear isometry to the space of
continuous linear maps. -/
def mulâ‚—áµ¢ : ğ•œ' â†’â‚—áµ¢[ğ•œ] ğ•œ' â†’L[ğ•œ] ğ•œ' where
  toLinearMap := mul ğ•œ ğ•œ'
  norm_map' x := op_norm_mul_apply ğ•œ ğ•œ' x
#align continuous_linear_map.mulâ‚—áµ¢ ContinuousLinearMap.mulâ‚—áµ¢â‚“

@[simp]
theorem coe_mulâ‚—áµ¢ : â‡‘(mulâ‚—áµ¢ ğ•œ ğ•œ') = mul ğ•œ ğ•œ' :=
  rfl
#align continuous_linear_map.coe_mulâ‚—áµ¢ ContinuousLinearMap.coe_mulâ‚—áµ¢â‚“

end NonUnital

end MultiplicationLinear

section SMulLinear

variable (ğ•œ) (ğ•œ' : Type*) [NormedField ğ•œ']

variable [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]

/-- Scalar multiplication as a continuous bilinear map. -/
def lsmul : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E :=
  ((Algebra.lsmul ğ•œ ğ•œ E).toLinearMap : ğ•œ' â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] E).mkContinuousâ‚‚ 1 fun c x => by
    simpa only [one_mul] using norm_smul_le c x
#align continuous_linear_map.lsmul ContinuousLinearMap.lsmul

@[simp]
theorem lsmul_apply (c : ğ•œ') (x : E) : lsmul ğ•œ ğ•œ' c x = c â€¢ x :=
  rfl
#align continuous_linear_map.lsmul_apply ContinuousLinearMap.lsmul_apply

variable {ğ•œ'}

theorem norm_toSpanSingleton (x : E) : â€–toSpanSingleton ğ•œ xâ€– = â€–xâ€– := by
  refine' op_norm_eq_of_bounds (norm_nonneg _) (fun x => _) fun N _ h => _
  Â· rw [toSpanSingleton_apply, norm_smul, mul_comm]
  Â· specialize h 1
    rw [toSpanSingleton_apply, norm_smul, mul_comm] at h
    exact (mul_le_mul_right (by simp)).mp h
#align continuous_linear_map.norm_to_span_singleton ContinuousLinearMap.norm_toSpanSingleton

variable {ğ•œ}

theorem op_norm_lsmul_apply_le (x : ğ•œ') : â€–(lsmul ğ•œ ğ•œ' x : E â†’L[ğ•œ] E)â€– â‰¤ â€–xâ€– :=
  ContinuousLinearMap.op_norm_le_bound _ (norm_nonneg x) fun y => norm_smul_le x y
#align continuous_linear_map.op_norm_lsmul_apply_le ContinuousLinearMap.op_norm_lsmul_apply_le

/-- The norm of `lsmul` is at most 1 in any semi-normed group. -/
theorem op_norm_lsmul_le : â€–(lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– â‰¤ 1 := by
  refine' ContinuousLinearMap.op_norm_le_bound _ zero_le_one fun x => _
  simp_rw [one_mul]
  exact op_norm_lsmul_apply_le _
#align continuous_linear_map.op_norm_lsmul_le ContinuousLinearMap.op_norm_lsmul_le

end SMulLinear

section RestrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]

variable [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E]

variable [NormedSpace ğ•œ' Fâ‚—] [IsScalarTower ğ•œ' ğ•œ Fâ‚—]

@[simp]
theorem norm_restrictScalars (f : E â†’L[ğ•œ] Fâ‚—) : â€–f.restrictScalars ğ•œ'â€– = â€–fâ€– :=
  le_antisymm (op_norm_le_bound _ (norm_nonneg _) fun x => f.le_op_norm x)
    (op_norm_le_bound _ (norm_nonneg _) fun x => f.le_op_norm x)
#align continuous_linear_map.norm_restrict_scalars ContinuousLinearMap.norm_restrictScalars

variable (ğ•œ E Fâ‚— ğ•œ') (ğ•œ'' : Type*) [Ring ğ•œ'']

variable [Module ğ•œ'' Fâ‚—] [ContinuousConstSMul ğ•œ'' Fâ‚—]
  [SMulCommClass ğ•œ ğ•œ'' Fâ‚—] [SMulCommClass ğ•œ' ğ•œ'' Fâ‚—]

/-- `ContinuousLinearMap.restrictScalars` as a `LinearIsometry`. -/
def restrictScalarsIsometry : (E â†’L[ğ•œ] Fâ‚—) â†’â‚—áµ¢[ğ•œ''] E â†’L[ğ•œ'] Fâ‚— :=
  âŸ¨restrictScalarsâ‚— ğ•œ E Fâ‚— ğ•œ' ğ•œ'', norm_restrictScalarsâŸ©
#align continuous_linear_map.restrict_scalars_isometry ContinuousLinearMap.restrictScalarsIsometry

variable {ğ•œ''}

@[simp]
theorem coe_restrictScalarsIsometry :
    â‡‘(restrictScalarsIsometry ğ•œ E Fâ‚— ğ•œ' ğ•œ'') = restrictScalars ğ•œ' :=
  rfl
#align continuous_linear_map.coe_restrict_scalars_isometry ContinuousLinearMap.coe_restrictScalarsIsometry

@[simp]
theorem restrictScalarsIsometry_toLinearMap :
    (restrictScalarsIsometry ğ•œ E Fâ‚— ğ•œ' ğ•œ'').toLinearMap = restrictScalarsâ‚— ğ•œ E Fâ‚— ğ•œ' ğ•œ'' :=
  rfl
#align continuous_linear_map.restrict_scalars_isometry_to_linear_map ContinuousLinearMap.restrictScalarsIsometry_toLinearMap

variable (ğ•œ'')

set_option linter.uppercaseLean3 false

/-- `ContinuousLinearMap.restrictScalars` as a `ContinuousLinearMap`. -/
def restrictScalarsL : (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ''] E â†’L[ğ•œ'] Fâ‚— :=
  (restrictScalarsIsometry ğ•œ E Fâ‚— ğ•œ' ğ•œ'').toContinuousLinearMap
#align continuous_linear_map.restrict_scalarsL ContinuousLinearMap.restrictScalarsL

variable {ğ•œ E Fâ‚— ğ•œ' ğ•œ''}

@[simp]
theorem coe_restrictScalarsL : (restrictScalarsL ğ•œ E Fâ‚— ğ•œ' ğ•œ'' : (E â†’L[ğ•œ] Fâ‚—) â†’â‚—[ğ•œ''] E â†’L[ğ•œ'] Fâ‚—) =
    restrictScalarsâ‚— ğ•œ E Fâ‚— ğ•œ' ğ•œ'' :=
  rfl
#align continuous_linear_map.coe_restrict_scalarsL ContinuousLinearMap.coe_restrictScalarsL

@[simp]
theorem coe_restrict_scalarsL' : â‡‘(restrictScalarsL ğ•œ E Fâ‚— ğ•œ' ğ•œ'') = restrictScalars ğ•œ' :=
  rfl
#align continuous_linear_map.coe_restrict_scalarsL' ContinuousLinearMap.coe_restrict_scalarsL'

end RestrictScalars

end ContinuousLinearMap

namespace Submodule

theorem norm_subtypeL_le (K : Submodule ğ•œ E) : â€–K.subtypeLâ€– â‰¤ 1 :=
  K.subtypeâ‚—áµ¢.norm_toContinuousLinearMap_le
set_option linter.uppercaseLean3 false in
#align submodule.norm_subtypeL_le Submodule.norm_subtypeL_le

end Submodule

namespace ContinuousLinearEquiv

set_option linter.uppercaseLean3 false

section

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚â‚‚]

variable (e : E â‰ƒSL[Ïƒâ‚â‚‚] F)

protected theorem lipschitz : LipschitzWith â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€–â‚Š e :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).lipschitz
#align continuous_linear_equiv.lipschitz ContinuousLinearEquiv.lipschitz

theorem isBigO_comp {Î± : Type*} (f : Î± â†’ E) (l : Filter Î±) : (fun x' => e (f x')) =O[l] f :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).isBigO_comp f l
#align continuous_linear_equiv.is_O_comp ContinuousLinearEquiv.isBigO_comp

theorem isBigO_sub (l : Filter E) (x : E) : (fun x' => e (x' - x)) =O[l] fun x' => x' - x :=
  (e : E â†’SL[Ïƒâ‚â‚‚] F).isBigO_sub l x
#align continuous_linear_equiv.is_O_sub ContinuousLinearEquiv.isBigO_sub

end

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

variable [RingHomIsometric Ïƒâ‚‚â‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F)

theorem isBigO_comp_rev {Î± : Type*} (f : Î± â†’ E) (l : Filter Î±) : f =O[l] fun x' => e (f x') :=
  (e.symm.isBigO_comp _ l).congr_left fun _ => e.symm_apply_apply _
#align continuous_linear_equiv.is_O_comp_rev ContinuousLinearEquiv.isBigO_comp_rev

theorem isBigO_sub_rev (l : Filter E) (x : E) : (fun x' => x' - x) =O[l] fun x' => e (x' - x) :=
  e.isBigO_comp_rev _ _
#align continuous_linear_equiv.is_O_sub_rev ContinuousLinearEquiv.isBigO_sub_rev

end ContinuousLinearEquiv

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

namespace ContinuousLinearMap

variable {E' F' : Type*} [SeminormedAddCommGroup E'] [SeminormedAddCommGroup F']

variable {ğ•œâ‚' : Type*} {ğ•œâ‚‚' : Type*} [NontriviallyNormedField ğ•œâ‚'] [NontriviallyNormedField ğ•œâ‚‚']
  [NormedSpace ğ•œâ‚' E'] [NormedSpace ğ•œâ‚‚' F'] {Ïƒâ‚' : ğ•œâ‚' â†’+* ğ•œ} {Ïƒâ‚â‚ƒ' : ğ•œâ‚' â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚' : ğ•œâ‚‚' â†’+* ğ•œâ‚‚}
  {Ïƒâ‚‚â‚ƒ' : ğ•œâ‚‚' â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚' Ïƒâ‚â‚ƒ Ïƒâ‚â‚ƒ'] [RingHomCompTriple Ïƒâ‚‚' Ïƒâ‚‚â‚ƒ Ïƒâ‚‚â‚ƒ']
  [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚ƒ'] [RingHomIsometric Ïƒâ‚‚â‚ƒ']

/-- Compose a bilinear map `E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G` with two linear maps
`E' â†’SL[Ïƒâ‚'] E` and `F' â†’SL[Ïƒâ‚‚'] F`.  -/
def bilinearComp (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (gE : E' â†’SL[Ïƒâ‚'] E) (gF : F' â†’SL[Ïƒâ‚‚'] F) :
    E' â†’SL[Ïƒâ‚â‚ƒ'] F' â†’SL[Ïƒâ‚‚â‚ƒ'] G :=
  ((f.comp gE).flip.comp gF).flip
#align continuous_linear_map.bilinear_comp ContinuousLinearMap.bilinearComp

@[simp]
theorem bilinearComp_apply (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (gE : E' â†’SL[Ïƒâ‚'] E) (gF : F' â†’SL[Ïƒâ‚‚'] F)
    (x : E') (y : F') : f.bilinearComp gE gF x y = f (gE x) (gF y) :=
  rfl
#align continuous_linear_map.bilinear_comp_apply ContinuousLinearMap.bilinearComp_apply

variable [RingHomIsometric Ïƒâ‚â‚ƒ] [RingHomIsometric Ïƒâ‚'] [RingHomIsometric Ïƒâ‚‚']

/-- Derivative of a continuous bilinear map `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G` interpreted as a map `E Ã— F â†’ G`
at point `p : E Ã— F` evaluated at `q : E Ã— F`, as a continuous bilinear map. -/
def derivâ‚‚ (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : E Ã— Fâ‚— â†’L[ğ•œ] E Ã— Fâ‚— â†’L[ğ•œ] Gâ‚— :=
  f.bilinearComp (fst _ _ _) (snd _ _ _) + f.flip.bilinearComp (snd _ _ _) (fst _ _ _)
#align continuous_linear_map.derivâ‚‚ ContinuousLinearMap.derivâ‚‚

@[simp]
theorem coe_derivâ‚‚ (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) (p : E Ã— Fâ‚—) :
    â‡‘(f.derivâ‚‚ p) = fun q : E Ã— Fâ‚— => f p.1 q.2 + f q.1 p.2 :=
  rfl
#align continuous_linear_map.coe_derivâ‚‚ ContinuousLinearMap.coe_derivâ‚‚

theorem map_add_add (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) (x x' : E) (y y' : Fâ‚—) :
    f (x + x') (y + y') = f x y + f.derivâ‚‚ (x, y) (x', y') + f x' y' := by
  simp only [map_add, add_apply, coe_derivâ‚‚, add_assoc]
  abel
#align continuous_linear_map.map_add_add ContinuousLinearMap.map_add_add

end ContinuousLinearMap

end SemiNormed

section Normed

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
  [NormedAddCommGroup Fâ‚—]

open Metric ContinuousLinearMap

section

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G] [NormedSpace ğ•œ Fâ‚—] (c : ğ•œ)
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} (f g : E â†’SL[Ïƒâ‚â‚‚] F) (x y z : E)

namespace LinearMap

theorem bound_of_shell [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ}
    (hc : 1 < â€–câ€–) (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
    â€–f xâ€– â‰¤ C * â€–xâ€– := by
  by_cases hx : x = 0; Â· simp [hx]
  exact SemilinearMapClass.bound_of_shell_semi_normed f Îµ_pos hc hf (norm_ne_zero_iff.2 hx)
#align linear_map.bound_of_shell LinearMap.bound_of_shell

/-- `LinearMap.bound_of_ball_bound'` is a version of this lemma over a field satisfying `IsROrC`
that produces a concrete bound.
-/
theorem bound_of_ball_bound {r : â„} (r_pos : 0 < r) (c : â„) (f : E â†’â‚—[ğ•œ] Fâ‚—)
    (h : âˆ€ z âˆˆ Metric.ball (0 : E) r, â€–f zâ€– â‰¤ c) : âˆƒ C, âˆ€ z : E, â€–f zâ€– â‰¤ C * â€–zâ€– := by
  cases' @NontriviallyNormedField.non_trivial ğ•œ _ with k hk
  use c * (â€–kâ€– / r)
  intro z
  refine' bound_of_shell _ r_pos hk (fun x hko hxo => _) _
  calc
    â€–f xâ€– â‰¤ c := h _ (mem_ball_zero_iff.mpr hxo)
    _ â‰¤ c * (â€–xâ€– * â€–kâ€– / r) := (le_mul_of_one_le_right ?_ ?_)
    _ = _ := by ring
  Â· exact le_trans (norm_nonneg _) (h 0 (by simp [r_pos]))
  Â· rw [div_le_iff (zero_lt_one.trans hk)] at hko
    exact (one_le_div r_pos).mpr hko
#align linear_map.bound_of_ball_bound LinearMap.bound_of_ball_bound

theorem antilipschitz_of_comap_nhds_le [h : RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F)
    (hf : (ğ“ 0).comap f â‰¤ ğ“ 0) : âˆƒ K, AntilipschitzWith K f := by
  rcases ((nhds_basis_ball.comap _).le_basis_iff nhds_basis_ball).1 hf 1 one_pos with âŸ¨Îµ, Îµ0, hÎµâŸ©
  simp only [Set.subset_def, Set.mem_preimage, mem_ball_zero_iff] at hÎµ
  lift Îµ to â„â‰¥0 using Îµ0.le
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  refine' âŸ¨Îµâ»Â¹ * â€–câ€–â‚Š, AddMonoidHomClass.antilipschitz_of_bound f fun x => _âŸ©
  by_cases hx : f x = 0
  Â· rw [â† hx] at hf
    obtain rfl : x = 0 := Specializes.eq (specializes_iff_pure.2 <|
      ((Filter.tendsto_pure_pure _ _).mono_right (pure_le_nhds _)).le_comap.trans hf)
    exact norm_zero.trans_le (mul_nonneg (NNReal.coe_nonneg _) (norm_nonneg _))
  have hcâ‚€ : c â‰  0 := norm_pos_iff.1 (one_pos.trans hc)
  rw [â† h.1] at hc
  rcases rescale_to_shell_zpow hc Îµ0 hx with âŸ¨n, -, hlt, -, hleâŸ©
  simp only [â† map_zpowâ‚€, h.1, â† map_smulâ‚›â‚—] at hlt hle
  calc
    â€–xâ€– = â€–c ^ nâ€–â»Â¹ * â€–c ^ n â€¢ xâ€– := by
      rwa [â† norm_inv, â† norm_smul, inv_smul_smulâ‚€ (zpow_ne_zero _ _)]
    _ â‰¤ â€–c ^ nâ€–â»Â¹ * 1 := (mul_le_mul_of_nonneg_left (hÎµ _ hlt).le (inv_nonneg.2 (norm_nonneg _)))
    _ â‰¤ Îµâ»Â¹ * â€–câ€– * â€–f xâ€– := by rwa [mul_one]
#align linear_map.antilipschitz_of_comap_nhds_le LinearMap.antilipschitz_of_comap_nhds_le

end LinearMap

namespace ContinuousLinearMap

section OpNorm

open Set Real

/-- An operator is zero iff its norm vanishes. -/
theorem op_norm_zero_iff [RingHomIsometric Ïƒâ‚â‚‚] : â€–fâ€– = 0 â†” f = 0 :=
  Iff.intro
    (fun hn => ContinuousLinearMap.ext fun x => norm_le_zero_iff.1
      (calc
        _ â‰¤ â€–fâ€– * â€–xâ€– := le_op_norm _ _
        _ = _ := by rw [hn, zero_mul]))
    (by
      rintro rfl
      exact op_norm_zero)
#align continuous_linear_map.op_norm_zero_iff ContinuousLinearMap.op_norm_zero_iff

/-- If a normed space is non-trivial, then the norm of the identity equals `1`. -/
@[simp]
theorem norm_id [Nontrivial E] : â€–id ğ•œ Eâ€– = 1 := by
  refine' norm_id_of_nontrivial_seminorm _
  obtain âŸ¨x, hxâŸ© := exists_ne (0 : E)
  exact âŸ¨x, ne_of_gt (norm_pos_iff.2 hx)âŸ©
#align continuous_linear_map.norm_id ContinuousLinearMap.norm_id

@[simp]
lemma nnnorm_id [Nontrivial E] : â€–id ğ•œ Eâ€–â‚Š = 1 := NNReal.eq norm_id

instance normOneClass [Nontrivial E] : NormOneClass (E â†’L[ğ•œ] E) :=
  âŸ¨norm_idâŸ©
#align continuous_linear_map.norm_one_class ContinuousLinearMap.normOneClass

/-- Continuous linear maps themselves form a normed space with respect to
    the operator norm. -/
instance toNormedAddCommGroup [RingHomIsometric Ïƒâ‚â‚‚] : NormedAddCommGroup (E â†’SL[Ïƒâ‚â‚‚] F) :=
  NormedAddCommGroup.ofSeparation fun f => (op_norm_zero_iff f).mp
#align continuous_linear_map.to_normed_add_comm_group ContinuousLinearMap.toNormedAddCommGroup

/-- Continuous linear maps form a normed ring with respect to the operator norm. -/
instance toNormedRing : NormedRing (E â†’L[ğ•œ] E) :=
  { ContinuousLinearMap.toNormedAddCommGroup, ContinuousLinearMap.toSemiNormedRing with }
#align continuous_linear_map.to_normed_ring ContinuousLinearMap.toNormedRing

variable {f}

theorem homothety_norm [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (f : E â†’SL[Ïƒâ‚â‚‚] F) {a : â„}
    (hf : âˆ€ x, â€–f xâ€– = a * â€–xâ€–) : â€–fâ€– = a := by
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0
  rw [â† norm_pos_iff] at hx
  have ha : 0 â‰¤ a := by simpa only [hf, hx, mul_nonneg_iff_of_pos_right] using norm_nonneg (f x)
  apply le_antisymm (f.op_norm_le_bound ha fun y => le_of_eq (hf y))
  simpa only [hf, hx, mul_le_mul_right] using f.le_op_norm x
#align continuous_linear_map.homothety_norm ContinuousLinearMap.homothety_norm

variable (f)

/-- If a continuous linear map is a topology embedding, then it is expands the distances
by a positive factor.-/
theorem antilipschitz_of_embedding (f : E â†’L[ğ•œ] Fâ‚—) (hf : Embedding f) :
    âˆƒ K, AntilipschitzWith K f :=
  f.toLinearMap.antilipschitz_of_comap_nhds_le <| map_zero f â–¸ (hf.nhds_eq_comap 0).ge
#align continuous_linear_map.antilipschitz_of_embedding ContinuousLinearMap.antilipschitz_of_embedding

section Completeness

open Topology

open Filter

variable {E' : Type*} [SeminormedAddCommGroup E'] [NormedSpace ğ•œ E'] [RingHomIsometric Ïƒâ‚â‚‚]

/-- Construct a bundled continuous (semi)linear map from a map `f : E â†’ F` and a proof of the fact
that it belongs to the closure of the image of a bounded set `s : Set (E â†’SL[Ïƒâ‚â‚‚] F)` under coercion
to function. Coercion to function of the result is definitionally equal to `f`. -/
@[simps! (config := .asFn) apply]
def ofMemClosureImageCoeBounded (f : E' â†’ F) {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)} (hs : IsBounded s)
    (hf : f âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) : E' â†’SL[Ïƒâ‚â‚‚] F := by
  -- `f` is a linear map due to `linearMapOfMemClosureRangeCoe`
  refine' (linearMapOfMemClosureRangeCoe f _).mkContinuousOfExistsBound _
  Â· refine' closure_mono (image_subset_iff.2 fun g _ => _) hf
    exact âŸ¨g, rflâŸ©
  Â· -- We need to show that `f` has bounded norm. Choose `C` such that `â€–gâ€– â‰¤ C` for all `g âˆˆ s`.
    rcases isBounded_iff_forall_norm_le.1 hs with âŸ¨C, hCâŸ©
    -- Then `â€–g xâ€– â‰¤ C * â€–xâ€–` for all `g âˆˆ s`, `x : E`, hence `â€–f xâ€– â‰¤ C * â€–xâ€–` for all `x`.
    have : âˆ€ x, IsClosed { g : E' â†’ F | â€–g xâ€– â‰¤ C * â€–xâ€– } := fun x =>
      isClosed_Iic.preimage (@continuous_apply E' (fun _ => F) _ x).norm
    refine' âŸ¨C, fun x => (this x).closure_subset_iff.2 (image_subset_iff.2 fun g hg => _) hfâŸ©
    exact g.le_of_op_norm_le (hC _ hg) _
#align continuous_linear_map.of_mem_closure_image_coe_bounded ContinuousLinearMap.ofMemClosureImageCoeBounded

/-- Let `f : E â†’ F` be a map, let `g : Î± â†’ E â†’SL[Ïƒâ‚â‚‚] F` be a family of continuous (semi)linear maps
that takes values in a bounded set and converges to `f` pointwise along a nontrivial filter. Then
`f` is a continuous (semi)linear map. -/
@[simps! (config := .asFn) apply]
def ofTendstoOfBoundedRange {Î± : Type*} {l : Filter Î±} [l.NeBot] (f : E' â†’ F)
    (g : Î± â†’ E' â†’SL[Ïƒâ‚â‚‚] F) (hf : Tendsto (fun a x => g a x) l (ğ“ f))
    (hg : IsBounded (Set.range g)) : E' â†’SL[Ïƒâ‚â‚‚] F :=
  ofMemClosureImageCoeBounded f hg <| mem_closure_of_tendsto hf <|
    eventually_of_forall fun _ => mem_image_of_mem _ <| Set.mem_range_self _
#align continuous_linear_map.of_tendsto_of_bounded_range ContinuousLinearMap.ofTendstoOfBoundedRange

/-- If a Cauchy sequence of continuous linear map converges to a continuous linear map pointwise,
then it converges to the same map in norm. This lemma is used to prove that the space of continuous
linear maps is complete provided that the codomain is a complete space. -/
theorem tendsto_of_tendsto_pointwise_of_cauchySeq {f : â„• â†’ E' â†’SL[Ïƒâ‚â‚‚] F} {g : E' â†’SL[Ïƒâ‚â‚‚] F}
    (hg : Tendsto (fun n x => f n x) atTop (ğ“ g)) (hf : CauchySeq f) : Tendsto f atTop (ğ“ g) := by
  /- Since `f` is a Cauchy sequence, there exists `b â†’ 0` such that `â€–f n - f mâ€– â‰¤ b N` for any
    `m, n â‰¥ N`. -/
  rcases cauchySeq_iff_le_tendsto_0.1 hf with âŸ¨b, hbâ‚€, hfb, hb_limâŸ©
  -- Since `b â†’ 0`, it suffices to show that `â€–f n x - g xâ€– â‰¤ b n * â€–xâ€–` for all `n` and `x`.
  suffices : âˆ€ n x, â€–f n x - g xâ€– â‰¤ b n * â€–xâ€–
  exact tendsto_iff_norm_sub_tendsto_zero.2
    (squeeze_zero (fun n => norm_nonneg _) (fun n => op_norm_le_bound _ (hbâ‚€ n) (this n)) hb_lim)
  intro n x
  -- Note that `f m x â†’ g x`, hence `â€–f n x - f m xâ€– â†’ â€–f n x - g xâ€–` as `m â†’ âˆ`
  have : Tendsto (fun m => â€–f n x - f m xâ€–) atTop (ğ“ â€–f n x - g xâ€–) :=
    (tendsto_const_nhds.sub <| tendsto_pi_nhds.1 hg _).norm
  -- Thus it suffices to verify `â€–f n x - f m xâ€– â‰¤ b n * â€–xâ€–` for `m â‰¥ n`.
  refine' le_of_tendsto this (eventually_atTop.2 âŸ¨n, fun m hm => _âŸ©)
  -- This inequality follows from `â€–f n - f mâ€– â‰¤ b n`.
  exact (f n - f m).le_of_op_norm_le (hfb _ _ _ le_rfl hm) _
#align continuous_linear_map.tendsto_of_tendsto_pointwise_of_cauchy_seq ContinuousLinearMap.tendsto_of_tendsto_pointwise_of_cauchySeq

/-- If the target space is complete, the space of continuous linear maps with its norm is also
complete. This works also if the source space is seminormed. -/
instance [CompleteSpace F] : CompleteSpace (E' â†’SL[Ïƒâ‚â‚‚] F) := by
  -- We show that every Cauchy sequence converges.
  refine' Metric.complete_of_cauchySeq_tendsto fun f hf => _
  -- The evaluation at any point `v : E` is Cauchy.
  have cau : âˆ€ v, CauchySeq fun n => f n v := fun v => hf.map (lipschitz_apply v).uniformContinuous
  -- We assemble the limits points of those Cauchy sequences
  -- (which exist as `F` is complete)
  -- into a function which we call `G`.
  choose G hG using fun v => cauchySeq_tendsto_of_complete (cau v)
  -- Next, we show that this `G` is a continuous linear map.
  -- This is done in `ContinuousLinearMap.ofTendstoOfBoundedRange`.
  set Glin : E' â†’SL[Ïƒâ‚â‚‚] F :=
    ofTendstoOfBoundedRange _ _ (tendsto_pi_nhds.mpr hG) hf.isBounded_range
  -- Finally, `f n` converges to `Glin` in norm because of
  -- `ContinuousLinearMap.tendsto_of_tendsto_pointwise_of_cauchySeq`
  exact âŸ¨Glin, tendsto_of_tendsto_pointwise_of_cauchySeq (tendsto_pi_nhds.2 hG) hfâŸ©

/-- Let `s` be a bounded set in the space of continuous (semi)linear maps `E â†’SL[Ïƒ] F` taking values
in a proper space. Then `s` interpreted as a set in the space of maps `E â†’ F` with topology of
pointwise convergence is precompact: its closure is a compact set. -/
theorem isCompact_closure_image_coe_of_bounded [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) : IsCompact (closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) :=
  have : âˆ€ x, IsCompact (closure (apply' F Ïƒâ‚â‚‚ x '' s)) := fun x =>
    ((apply' F Ïƒâ‚â‚‚ x).lipschitz.isBounded_image hb).isCompact_closure
  isCompact_closure_of_subset_compact (isCompact_pi_infinite this)
    (image_subset_iff.2 fun _ hg _ => subset_closure <| mem_image_of_mem _ hg)
#align continuous_linear_map.is_compact_closure_image_coe_of_bounded ContinuousLinearMap.isCompact_closure_image_coe_of_bounded

/-- Let `s` be a bounded set in the space of continuous (semi)linear maps `E â†’SL[Ïƒ] F` taking values
in a proper space. If `s` interpreted as a set in the space of maps `E â†’ F` with topology of
pointwise convergence is closed, then it is compact.

TODO: reformulate this in terms of a type synonym with the right topology. -/
theorem isCompact_image_coe_of_bounded_of_closed_image [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) (hc : IsClosed (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) :
    IsCompact (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  hc.closure_eq â–¸ isCompact_closure_image_coe_of_bounded hb
#align continuous_linear_map.is_compact_image_coe_of_bounded_of_closed_image ContinuousLinearMap.isCompact_image_coe_of_bounded_of_closed_image

/-- If a set `s` of semilinear functions is bounded and is closed in the weak-* topology, then its
image under coercion to functions `E â†’ F` is a closed set. We don't have a name for `E â†’SL[Ïƒ] F`
with weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`).

TODO: reformulate this in terms of a type synonym with the right topology. -/
theorem isClosed_image_coe_of_bounded_of_weak_closed {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)} (hb : IsBounded s)
    (hc : âˆ€ f : E' â†’SL[Ïƒâ‚â‚‚] F,
      (â‡‘f : E' â†’ F) âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) â†’ f âˆˆ s) :
    IsClosed (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  isClosed_of_closure_subset fun f hf =>
    âŸ¨ofMemClosureImageCoeBounded f hb hf, hc (ofMemClosureImageCoeBounded f hb hf) hf, rflâŸ©
#align continuous_linear_map.is_closed_image_coe_of_bounded_of_weak_closed ContinuousLinearMap.isClosed_image_coe_of_bounded_of_weak_closed

/-- If a set `s` of semilinear functions is bounded and is closed in the weak-* topology, then its
image under coercion to functions `E â†’ F` is a compact set. We don't have a name for `E â†’SL[Ïƒ] F`
with weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`).
-/
theorem isCompact_image_coe_of_bounded_of_weak_closed [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) (hc : âˆ€ f : E' â†’SL[Ïƒâ‚â‚‚] F,
      (â‡‘f : E' â†’ F) âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) â†’ f âˆˆ s) :
    IsCompact (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  isCompact_image_coe_of_bounded_of_closed_image hb <|
    isClosed_image_coe_of_bounded_of_weak_closed hb hc
#align continuous_linear_map.is_compact_image_coe_of_bounded_of_weak_closed ContinuousLinearMap.isCompact_image_coe_of_bounded_of_weak_closed

/-- A closed ball is closed in the weak-* topology. We don't have a name for `E â†’SL[Ïƒ] F` with
weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`). -/
theorem is_weak_closed_closedBall (fâ‚€ : E' â†’SL[Ïƒâ‚â‚‚] F) (r : â„) â¦ƒf : E' â†’SL[Ïƒâ‚â‚‚] Fâ¦„
    (hf : â‡‘f âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' closedBall fâ‚€ r)) :
    f âˆˆ closedBall fâ‚€ r := by
  have hr : 0 â‰¤ r := nonempty_closedBall.1 (closure_nonempty_iff.1 âŸ¨_, hfâŸ©).of_image
  refine' mem_closedBall_iff_norm.2 (op_norm_le_bound _ hr fun x => _)
  have : IsClosed { g : E' â†’ F | â€–g x - fâ‚€ xâ€– â‰¤ r * â€–xâ€– } :=
    isClosed_Iic.preimage ((@continuous_apply E' (fun _ => F) _ x).sub continuous_const).norm
  refine' this.closure_subset_iff.2 (image_subset_iff.2 fun g hg => _) hf
  exact (g - fâ‚€).le_of_op_norm_le (mem_closedBall_iff_norm.1 hg) _
#align continuous_linear_map.is_weak_closed_closed_ball ContinuousLinearMap.is_weak_closed_closedBall

/-- The set of functions `f : E â†’ F` that represent continuous linear maps `f : E â†’SL[Ïƒâ‚â‚‚] F`
at distance `â‰¤ r` from `fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F` is closed in the topology of pointwise convergence.
This is one of the key steps in the proof of the **Banach-Alaoglu** theorem. -/
theorem isClosed_image_coe_closedBall (fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F) (r : â„) :
    IsClosed (((â†‘) : (E â†’SL[Ïƒâ‚â‚‚] F) â†’ E â†’ F) '' closedBall fâ‚€ r) :=
  isClosed_image_coe_of_bounded_of_weak_closed isBounded_closedBall (is_weak_closed_closedBall fâ‚€ r)
#align continuous_linear_map.is_closed_image_coe_closed_ball ContinuousLinearMap.isClosed_image_coe_closedBall

/-- **Banach-Alaoglu** theorem. The set of functions `f : E â†’ F` that represent continuous linear
maps `f : E â†’SL[Ïƒâ‚â‚‚] F` at distance `â‰¤ r` from `fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F` is compact in the topology of
pointwise convergence. Other versions of this theorem can be found in
`Analysis.NormedSpace.WeakDual`. -/
theorem isCompact_image_coe_closedBall [ProperSpace F] (fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F) (r : â„) :
    IsCompact (((â†‘) : (E â†’SL[Ïƒâ‚â‚‚] F) â†’ E â†’ F) '' closedBall fâ‚€ r) :=
  isCompact_image_coe_of_bounded_of_weak_closed isBounded_closedBall <|
    is_weak_closed_closedBall fâ‚€ r
#align continuous_linear_map.is_compact_image_coe_closed_ball ContinuousLinearMap.isCompact_image_coe_closedBall

end Completeness

section UniformlyExtend

variable [CompleteSpace F] (e : E â†’L[ğ•œ] Fâ‚—) (h_dense : DenseRange e)

section

variable (h_e : UniformInducing e)

/-- Extension of a continuous linear map `f : E â†’SL[Ïƒâ‚â‚‚] F`, with `E` a normed space and `F` a
complete normed space, along a uniform and dense embedding `e : E â†’L[ğ•œ] Fâ‚—`.  -/
def extend : Fâ‚— â†’SL[Ïƒâ‚â‚‚] F :=
  -- extension of `f` is continuous
  have cont := (uniformContinuous_uniformly_extend h_e h_dense f.uniformContinuous).continuous
  -- extension of `f` agrees with `f` on the domain of the embedding `e`
  have eq := uniformly_extend_of_ind h_e h_dense f.uniformContinuous
  { toFun := (h_e.denseInducing h_dense).extend f
    map_add' := by
      refine' h_dense.induction_onâ‚‚ _ _
      Â· exact isClosed_eq (cont.comp continuous_add)
          ((cont.comp continuous_fst).add (cont.comp continuous_snd))
      Â· intro x y
        simp only [eq, â† e.map_add]
        exact f.map_add _ _
    map_smul' := fun k => by
      refine' fun b => h_dense.induction_on b _ _
      Â· exact isClosed_eq (cont.comp (continuous_const_smul _))
          ((continuous_const_smul _).comp cont)
      Â· intro x
        rw [â† map_smul]
        simp only [eq]
        exact ContinuousLinearMap.map_smulâ‚›â‚— _ _ _
    cont }
#align continuous_linear_map.extend ContinuousLinearMap.extend

-- Porting note: previously `(h_e.denseInducing h_dense)` was inferred.
@[simp]
theorem extend_eq (x : E) : extend f e h_dense h_e (e x) = f x :=
  DenseInducing.extend_eq (h_e.denseInducing h_dense) f.cont _
#align continuous_linear_map.extend_eq ContinuousLinearMap.extend_eq

theorem extend_unique (g : Fâ‚— â†’SL[Ïƒâ‚â‚‚] F) (H : g.comp e = f) : extend f e h_dense h_e = g :=
  ContinuousLinearMap.coeFn_injective <|
    uniformly_extend_unique h_e h_dense (ContinuousLinearMap.ext_iff.1 H) g.continuous
#align continuous_linear_map.extend_unique ContinuousLinearMap.extend_unique

@[simp]
theorem extend_zero : extend (0 : E â†’SL[Ïƒâ‚â‚‚] F) e h_dense h_e = 0 :=
  extend_unique _ _ _ _ _ (zero_comp _)
#align continuous_linear_map.extend_zero ContinuousLinearMap.extend_zero

end

section

variable {N : â„â‰¥0} (h_e : âˆ€ x, â€–xâ€– â‰¤ N * â€–e xâ€–) [RingHomIsometric Ïƒâ‚â‚‚]

/-- If a dense embedding `e : E â†’L[ğ•œ] G` expands the norm by a constant factor `Nâ»Â¹`, then the
norm of the extension of `f` along `e` is bounded by `N * â€–fâ€–`. -/
theorem op_norm_extend_le :
    â€–f.extend e h_dense (uniformEmbedding_of_bound _ h_e).toUniformInducingâ€– â‰¤ N * â€–fâ€– := by
  -- Add `op_norm_le_of_dense`?
  refine op_norm_le_bound _ ?_ (isClosed_property h_dense (isClosed_le ?_ ?_) fun x â†¦ ?_)
  Â· cases le_total 0 N with
    | inl hN => exact mul_nonneg hN (norm_nonneg _)
    | inr hN =>
      have : Unique E := âŸ¨âŸ¨0âŸ©, fun x â†¦ norm_le_zero_iff.mp <|
        (h_e x).trans (mul_nonpos_of_nonpos_of_nonneg hN (norm_nonneg _))âŸ©
      obtain rfl : f = 0 := Subsingleton.elim ..
      simp
  Â· exact (cont _).norm
  Â· exact continuous_const.mul continuous_norm
  Â· rw [extend_eq]
    calc
      â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_op_norm _ _
      _ â‰¤ â€–fâ€– * (N * â€–e xâ€–) := (mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _))
      _ â‰¤ N * â€–fâ€– * â€–e xâ€– := by rw [mul_comm â†‘N â€–fâ€–, mul_assoc]
#align continuous_linear_map.op_norm_extend_le ContinuousLinearMap.op_norm_extend_le

end

end UniformlyExtend

end OpNorm

end ContinuousLinearMap

namespace LinearIsometry

@[simp]
theorem norm_toContinuousLinearMap [Nontrivial E] [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’â‚›â‚—áµ¢[Ïƒâ‚â‚‚] F) :
    â€–f.toContinuousLinearMapâ€– = 1 :=
  f.toContinuousLinearMap.homothety_norm <| by simp
#align linear_isometry.norm_to_continuous_linear_map LinearIsometry.norm_toContinuousLinearMap

variable {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

/-- Postcomposition of a continuous linear map with a linear isometry preserves
the operator norm. -/
theorem norm_toContinuousLinearMap_comp [RingHomIsometric Ïƒâ‚â‚‚] (f : F â†’â‚›â‚—áµ¢[Ïƒâ‚‚â‚ƒ] G)
    {g : E â†’SL[Ïƒâ‚â‚‚] F} : â€–f.toContinuousLinearMap.comp gâ€– = â€–gâ€– :=
  op_norm_ext (f.toContinuousLinearMap.comp g) g fun x => by
    simp only [norm_map, coe_toContinuousLinearMap, coe_comp', Function.comp_apply]
#align linear_isometry.norm_to_continuous_linear_map_comp LinearIsometry.norm_toContinuousLinearMap_comp

end LinearIsometry

end

namespace ContinuousLinearMap

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚ƒ G] [NormedSpace ğ•œ Fâ‚—] (c : ğ•œ)
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ}

variable {ğ•œâ‚‚' : Type*} [NontriviallyNormedField ğ•œâ‚‚'] {F' : Type*} [NormedAddCommGroup F']
  [NormedSpace ğ•œâ‚‚' F'] {Ïƒâ‚‚' : ğ•œâ‚‚' â†’+* ğ•œâ‚‚} {Ïƒâ‚‚'' : ğ•œâ‚‚ â†’+* ğ•œâ‚‚'} {Ïƒâ‚‚â‚ƒ' : ğ•œâ‚‚' â†’+* ğ•œâ‚ƒ}
  [RingHomInvPair Ïƒâ‚‚' Ïƒâ‚‚''] [RingHomInvPair Ïƒâ‚‚'' Ïƒâ‚‚'] [RingHomCompTriple Ïƒâ‚‚' Ïƒâ‚‚â‚ƒ Ïƒâ‚‚â‚ƒ']
  [RingHomCompTriple Ïƒâ‚‚'' Ïƒâ‚‚â‚ƒ' Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚‚']
  [RingHomIsometric Ïƒâ‚‚''] [RingHomIsometric Ïƒâ‚‚â‚ƒ']

/-- Precomposition with a linear isometry preserves the operator norm. -/
theorem op_norm_comp_linearIsometryEquiv (f : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (g : F' â‰ƒâ‚›â‚—áµ¢[Ïƒâ‚‚'] F) :
    â€–f.comp g.toLinearIsometry.toContinuousLinearMapâ€– = â€–fâ€– := by
  cases subsingleton_or_nontrivial F'
  Â· haveI := g.symm.toLinearEquiv.toEquiv.subsingleton
    simp
  refine' le_antisymm _ _
  Â· convert f.op_norm_comp_le g.toLinearIsometry.toContinuousLinearMap
    simp [g.toLinearIsometry.norm_toContinuousLinearMap]
  Â· convert (f.comp g.toLinearIsometry.toContinuousLinearMap).op_norm_comp_le
        g.symm.toLinearIsometry.toContinuousLinearMap
    Â· ext
      simp
    haveI := g.symm.surjective.nontrivial
    simp [g.symm.toLinearIsometry.norm_toContinuousLinearMap]
#align continuous_linear_map.op_norm_comp_linear_isometry_equiv ContinuousLinearMap.op_norm_comp_linearIsometryEquiv

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp]
theorem norm_smulRight_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRight c fâ€– = â€–câ€– * â€–fâ€– := by
  refine' le_antisymm _ _
  Â· refine' op_norm_le_bound _ (mul_nonneg (norm_nonneg _) (norm_nonneg _)) fun x => _
    calc
      â€–c x â€¢ fâ€– = â€–c xâ€– * â€–fâ€– := norm_smul _ _
      _ â‰¤ â€–câ€– * â€–xâ€– * â€–fâ€– := (mul_le_mul_of_nonneg_right (le_op_norm _ _) (norm_nonneg _))
      _ = â€–câ€– * â€–fâ€– * â€–xâ€– := by ring
  Â· by_cases h : f = 0
    Â· simp [h]
    Â· have : 0 < â€–fâ€– := norm_pos_iff.2 h
      rw [â† le_div_iff this]
      refine' op_norm_le_bound _ (div_nonneg (norm_nonneg _) (norm_nonneg f)) fun x => _
      rw [div_mul_eq_mul_div, le_div_iff this]
      calc
        â€–c xâ€– * â€–fâ€– = â€–c x â€¢ fâ€– := (norm_smul _ _).symm
        _ = â€–smulRight c f xâ€– := rfl
        _ â‰¤ â€–smulRight c fâ€– * â€–xâ€– := le_op_norm _ _
#align continuous_linear_map.norm_smul_right_apply ContinuousLinearMap.norm_smulRight_apply

/-- The non-negative norm of the tensor product of a scalar linear map and of an element of a normed
space is the product of the non-negative norms. -/
@[simp]
theorem nnnorm_smulRight_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRight c fâ€–â‚Š = â€–câ€–â‚Š * â€–fâ€–â‚Š :=
  NNReal.eq <| c.norm_smulRight_apply f
#align continuous_linear_map.nnnorm_smul_right_apply ContinuousLinearMap.nnnorm_smulRight_apply

variable (ğ•œ E Fâ‚—)

set_option linter.uppercaseLean3 false

/-- `ContinuousLinearMap.smulRight` as a continuous trilinear map:
`smulRightL (c : E â†’L[ğ•œ] ğ•œ) (f : F) (x : E) = c x â€¢ f`. -/
def smulRightL : (E â†’L[ğ•œ] ğ•œ) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Fâ‚— :=
  LinearMap.mkContinuousâ‚‚
    { toFun := smulRightâ‚—
      map_add' := fun câ‚ câ‚‚ => by
        ext x
        simp only [add_smul, coe_smulRightâ‚—, add_apply, smulRight_apply, LinearMap.add_apply]
      map_smul' := fun m c => by
        ext x
        simp only [smul_smul, coe_smulRightâ‚—, Algebra.id.smul_eq_mul, coe_smul', smulRight_apply,
          LinearMap.smul_apply, RingHom.id_apply, Pi.smul_apply] }
    1 fun c x => by
      simp only [coe_smulRightâ‚—, one_mul, norm_smulRight_apply, LinearMap.coe_mk, AddHom.coe_mk,
        le_refl]
#align continuous_linear_map.smul_rightL ContinuousLinearMap.smulRightL

variable {ğ•œ E Fâ‚—}

@[simp]
theorem norm_smulRightL_apply (c : E â†’L[ğ•œ] ğ•œ) (f : Fâ‚—) : â€–smulRightL ğ•œ E Fâ‚— c fâ€– = â€–câ€– * â€–fâ€– :=
  norm_smulRight_apply c f
#align continuous_linear_map.norm_smul_rightL_apply ContinuousLinearMap.norm_smulRightL_apply

@[simp]
theorem norm_smulRightL (c : E â†’L[ğ•œ] ğ•œ) [Nontrivial Fâ‚—] : â€–smulRightL ğ•œ E Fâ‚— câ€– = â€–câ€– :=
  ContinuousLinearMap.homothety_norm _ c.norm_smulRight_apply
#align continuous_linear_map.norm_smul_rightL ContinuousLinearMap.norm_smulRightL

variable (ğ•œ) (ğ•œ' : Type*)

section

variable [NonUnitalNormedRing ğ•œ'] [NormedSpace ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ']
variable [SMulCommClass ğ•œ ğ•œ' ğ•œ'] [RegularNormedAlgebra ğ•œ ğ•œ'] [Nontrivial ğ•œ']

@[simp]
theorem op_norm_mul : â€–mul ğ•œ ğ•œ'â€– = 1 :=
  (mulâ‚—áµ¢ ğ•œ ğ•œ').norm_toContinuousLinearMap
#align continuous_linear_map.op_norm_mul ContinuousLinearMap.op_norm_mulâ‚“

@[simp]
theorem op_nnnorm_mul : â€–mul ğ•œ ğ•œ'â€–â‚Š = 1 :=
  Subtype.ext <| op_norm_mul ğ•œ ğ•œ'
#align continuous_linear_map.op_nnnorm_mul ContinuousLinearMap.op_nnnorm_mulâ‚“

end

/-- The norm of `lsmul` equals 1 in any nontrivial normed group.

This is `ContinuousLinearMap.op_norm_lsmul_le` as an equality. -/
@[simp]
theorem op_norm_lsmul [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ'] [NormedSpace ğ•œ' E]
    [IsScalarTower ğ•œ ğ•œ' E] [Nontrivial E] : â€–(lsmul ğ•œ ğ•œ' : ğ•œ' â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– = 1 := by
  refine' ContinuousLinearMap.op_norm_eq_of_bounds zero_le_one (fun x => _) fun N _ h => _
  Â· rw [one_mul]
    apply op_norm_lsmul_apply_le
  obtain âŸ¨y, hyâŸ© := exists_ne (0 : E)
  have := le_of_op_norm_le _ (h 1) y
  simp_rw [lsmul_apply, one_smul, norm_one, mul_one] at this
  refine' le_of_mul_le_mul_right _ (norm_pos_iff.mpr hy)
  simp_rw [one_mul, this]
#align continuous_linear_map.op_norm_lsmul ContinuousLinearMap.op_norm_lsmul

end ContinuousLinearMap

namespace Submodule

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}

theorem norm_subtypeL (K : Submodule ğ•œ E) [Nontrivial K] : â€–K.subtypeLâ€– = 1 :=
  K.subtypeâ‚—áµ¢.norm_toContinuousLinearMap
set_option linter.uppercaseLean3 false in
#align submodule.norm_subtypeL Submodule.norm_subtypeL

end Submodule

namespace ContinuousLinearEquiv

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚]
  [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

section

variable [RingHomIsometric Ïƒâ‚‚â‚]

protected theorem antilipschitz (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    AntilipschitzWith â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š e :=
  e.symm.lipschitz.to_rightInverse e.left_inv
#align continuous_linear_equiv.antilipschitz ContinuousLinearEquiv.antilipschitz

theorem one_le_norm_mul_norm_symm [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    1 â‰¤ â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€– * â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– := by
  rw [mul_comm]
  convert (e.symm : F â†’SL[Ïƒâ‚‚â‚] E).op_norm_comp_le (e : E â†’SL[Ïƒâ‚â‚‚] F)
  rw [e.coe_symm_comp_coe, ContinuousLinearMap.norm_id]
#align continuous_linear_equiv.one_le_norm_mul_norm_symm ContinuousLinearEquiv.one_le_norm_mul_norm_symm

theorem norm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e : E â†’SL[Ïƒâ‚â‚‚] F)â€– :=
  pos_of_mul_pos_left (lt_of_lt_of_le zero_lt_one e.one_le_norm_mul_norm_symm) (norm_nonneg _)
#align continuous_linear_equiv.norm_pos ContinuousLinearEquiv.norm_pos

theorem norm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– :=
  pos_of_mul_pos_right (zero_lt_one.trans_le e.one_le_norm_mul_norm_symm) (norm_nonneg _)
#align continuous_linear_equiv.norm_symm_pos ContinuousLinearEquiv.norm_symm_pos

theorem nnnorm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] [Nontrivial E] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š :=
  e.norm_symm_pos
#align continuous_linear_equiv.nnnorm_symm_pos ContinuousLinearEquiv.nnnorm_symm_pos

theorem subsingleton_or_norm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    Subsingleton E âˆ¨ 0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€– := by
  rcases subsingleton_or_nontrivial E with (_i | _i) <;> skip
  Â· left
    infer_instance
  Â· right
    exact e.norm_symm_pos
#align continuous_linear_equiv.subsingleton_or_norm_symm_pos ContinuousLinearEquiv.subsingleton_or_norm_symm_pos

theorem subsingleton_or_nnnorm_symm_pos [RingHomIsometric Ïƒâ‚â‚‚] (e : E â‰ƒSL[Ïƒâ‚â‚‚] F) :
    Subsingleton E âˆ¨ 0 < â€–(e.symm : F â†’SL[Ïƒâ‚‚â‚] E)â€–â‚Š :=
  subsingleton_or_norm_symm_pos e
#align continuous_linear_equiv.subsingleton_or_nnnorm_symm_pos ContinuousLinearEquiv.subsingleton_or_nnnorm_symm_pos

variable (ğ•œ)

@[simp]
theorem coord_norm (x : E) (h : x â‰  0) : â€–coord ğ•œ x hâ€– = â€–xâ€–â»Â¹ := by
  have hx : 0 < â€–xâ€– := norm_pos_iff.mpr h
  haveI : Nontrivial (ğ•œ âˆ™ x) := Submodule.nontrivial_span_singleton h
  exact ContinuousLinearMap.homothety_norm _ fun y =>
    homothety_inverse _ hx _ (toSpanNonzeroSingleton_homothety ğ•œ x h) _
#align continuous_linear_equiv.coord_norm ContinuousLinearEquiv.coord_norm

end

end ContinuousLinearEquiv

end Normed

/-- A bounded bilinear form `B` in a real normed space is *coercive*
if there is some positive constant C such that `C * â€–uâ€– * â€–uâ€– â‰¤ B u u`.
-/
def IsCoercive [NormedAddCommGroup E] [NormedSpace â„ E] (B : E â†’L[â„] E â†’L[â„] â„) : Prop :=
  âˆƒ C, 0 < C âˆ§ âˆ€ u, C * â€–uâ€– * â€–uâ€– â‰¤ B u u
#align is_coercive IsCoercive

section Equicontinuous

variable {Î¹ : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [RingHomIsometric Ïƒâ‚â‚‚] [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] (f : Î¹ â†’ E â†’SL[Ïƒâ‚â‚‚] F)

/-- Equivalent characterizations for equicontinuity of a family of continuous linear maps
between normed spaces. See also `WithSeminorms.equicontinuous_TFAE` for similar characterizations
between spaces satisfying `WithSeminorms`. -/
protected theorem NormedSpace.equicontinuous_TFAE : List.TFAE
    [ EquicontinuousAt ((â†‘) âˆ˜ f) 0,
      Equicontinuous ((â†‘) âˆ˜ f),
      UniformEquicontinuous ((â†‘) âˆ˜ f),
      âˆƒ C, âˆ€ i x, â€–f i xâ€– â‰¤ C * â€–xâ€–,
      âˆƒ C â‰¥ 0, âˆ€ i x, â€–f i xâ€– â‰¤ C * â€–xâ€–,
      âˆƒ C, âˆ€ i, â€–f iâ€– â‰¤ C,
      âˆƒ C â‰¥ 0, âˆ€ i, â€–f iâ€– â‰¤ C,
      BddAbove (Set.range (â€–f Â·â€–)),
      (â¨† i, (â€–f iâ€–â‚Š : ENNReal)) < âŠ¤ ] := by
  -- `1 â†” 2 â†” 3` follows from `uniformEquicontinuous_of_equicontinuousAt_zero`
  tfae_have 1 â†’ 3
  Â· exact uniformEquicontinuous_of_equicontinuousAt_zero f
  tfae_have 3 â†’ 2
  Â· exact UniformEquicontinuous.equicontinuous
  tfae_have 2 â†’ 1
  Â· exact fun H â†¦ H 0
  -- `4 â†” 5 â†” 6 â†” 7 â†” 8 â†” 9` is morally trivial, we just have to use a lot of rewriting
  -- and `congr` lemmas
  tfae_have 4 â†” 5
  Â· rw [exists_ge_and_iff_exists]
    exact fun Câ‚ Câ‚‚ hC â†¦ forallâ‚‚_imp fun i x â†¦ le_trans' <| by gcongr
  tfae_have 5 â†” 7
  Â· refine exists_congr (fun C â†¦ and_congr_right fun hC â†¦ forall_congr' fun i â†¦ ?_)
    rw [ContinuousLinearMap.op_norm_le_iff hC]
  tfae_have 7 â†” 8
  Â· simp_rw [bddAbove_iff_exists_ge (0 : â„), Set.forall_range_iff]
  tfae_have 6 â†” 8
  Â· simp_rw [bddAbove_def, Set.forall_range_iff]
  tfae_have 8 â†” 9
  Â· rw [ENNReal.iSup_coe_lt_top, â† NNReal.bddAbove_coe, â† Set.range_comp]
    rfl
  -- `3 â†” 4` is the interesting part of the result. It is essentially a combination of
  -- `WithSeminorms.uniformEquicontinuous_iff_exists_continuous_seminorm` which turns
  -- equicontinuity into existence of some continuous seminorm and
  -- `Seminorm.bound_of_continuous_normedSpace` which characterize such seminorms.
  tfae_have 3 â†” 4
  Â· refine ((norm_withSeminorms ğ•œâ‚‚ F).uniformEquicontinuous_iff_exists_continuous_seminorm _).trans
      ?_
    rw [forall_const]
    constructor
    Â· intro âŸ¨p, hp, hpfâŸ©
      rcases p.bound_of_continuous_normedSpace hp with âŸ¨C, -, hCâŸ©
      exact âŸ¨C, fun i x â†¦ (hpf i x).trans (hC x)âŸ©
    Â· intro âŸ¨C, hCâŸ©
      refine âŸ¨C.toNNReal â€¢ normSeminorm ğ•œ E,
        ((norm_withSeminorms ğ•œ E).continuous_seminorm 0).const_smul C.toNNReal, fun i x â†¦ ?_âŸ©
      refine (hC i x).trans (mul_le_mul_of_nonneg_right (C.le_coe_toNNReal) (norm_nonneg x))
  tfae_finish

end Equicontinuous
