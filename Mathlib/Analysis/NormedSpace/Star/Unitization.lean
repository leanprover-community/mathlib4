/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.NormedSpace.Star.Basic
import Mathlib.Analysis.NormedSpace.Unitization

#align_import analysis.normed_space.star.mul from "leanprover-community/mathlib"@"b2ff9a3d7a15fd5b0f060b135421d6a89a999c2f"
/-! # The minimal unitization of a Câ‹†-algebra

This file shows that when `E` is a Câ‹†-algebra (over a densely normed field `ğ•œ`), that the minimal
`Unitization` is as well. In order to ensure that the norm structure is available, we must first
show that every Câ‹†-algebra is a `RegularNormedAlgebra`.

In addition, we show that in a `RegularNormedAlgebra` which is a `StarRing` for which the
involution is isometric, that multiplication on the right is also an isometry (i.e.,
`Isometry (ContinuousLinearMap.mul ğ•œ E).flip`).
-/

open ContinuousLinearMap

local postfix:max "â‹†" => star

variable (ğ•œ : Type*) {E : Type*}

namespace ContinuousLinearMap

variable [NontriviallyNormedField ğ•œ] [NonUnitalNormedRing E] [StarRing E] [NormedStarGroup E]
variable [NormedSpace ğ•œ E] [IsScalarTower ğ•œ E E] [SMulCommClass ğ•œ E E] [RegularNormedAlgebra ğ•œ E]

lemma opNorm_mul_flip_apply (a : E) : â€–(mul ğ•œ E).flip aâ€– = â€–aâ€– := by
  refine le_antisymm
    (opNorm_le_bound _ (norm_nonneg _) fun b => by simpa only [mul_comm] using norm_mul_le b a) ?_
  suffices â€–mul ğ•œ E (star a)â€– â‰¤ â€–(mul ğ•œ E).flip aâ€– by simpa using this
  refine opNorm_le_bound _ (norm_nonneg _) fun b => ?_
  calc â€–mul ğ•œ E (star a) bâ€– = â€–(mul ğ•œ E).flip a (star b)â€– := by simpa using norm_star (star b * a)
    _ â‰¤ â€–(mul ğ•œ E).flip aâ€– * â€–bâ€– := by simpa using le_opNorm ((mul ğ•œ E).flip a) (star b)

@[deprecated] alias op_norm_mul_flip_apply := opNorm_mul_flip_apply -- deprecated on 2024-02-02

lemma opNNNorm_mul_flip_apply (a : E) : â€–(mul ğ•œ E).flip aâ€–â‚Š = â€–aâ€–â‚Š :=
  Subtype.ext (opNorm_mul_flip_apply ğ•œ a)

@[deprecated] alias op_nnnorm_mul_flip_apply := opNNNorm_mul_flip_apply -- deprecated on 2024-02-02

variable (E)

lemma isometry_mul_flip : Isometry (mul ğ•œ E).flip :=
  AddMonoidHomClass.isometry_of_norm _ (opNorm_mul_flip_apply ğ•œ)

end ContinuousLinearMap

variable [DenselyNormedField ğ•œ] [NonUnitalNormedRing E] [StarRing E] [CstarRing E]
variable [NormedSpace ğ•œ E] [IsScalarTower ğ•œ E E] [SMulCommClass ğ•œ E E]
variable (E)

/-- A Câ‹†-algebra over a densely normed field is a regular normed algebra. -/
instance CstarRing.instRegularNormedAlgebra : RegularNormedAlgebra ğ•œ E where
  isometry_mul' := AddMonoidHomClass.isometry_of_norm (mul ğ•œ E) fun a => NNReal.eq_iff.mpr <|
    show â€–mul ğ•œ E aâ€–â‚Š = â€–aâ€–â‚Š by
    rw [â† sSup_closed_unit_ball_eq_nnnorm]
    refine csSup_eq_of_forall_le_of_forall_lt_exists_gt ?_ ?_ fun r hr => ?_
    Â· exact (Metric.nonempty_closedBall.mpr zero_le_one).image _
    Â· rintro - âŸ¨x, hx, rflâŸ©
      exact
        ((mul ğ•œ E a).unit_le_opNorm x <| mem_closedBall_zero_iff.mp hx).trans
          (opNorm_mul_apply_le ğ•œ E a)
    Â· have ha : 0 < â€–aâ€–â‚Š := zero_le'.trans_lt hr
      rw [â† inv_inv â€–aâ€–â‚Š, NNReal.lt_inv_iff_mul_lt (inv_ne_zero ha.ne')] at hr
      obtain âŸ¨k, hkâ‚, hkâ‚‚âŸ© :=
        NormedField.exists_lt_nnnorm_lt ğ•œ (mul_lt_mul_of_pos_right hr <| inv_pos.2 ha)
      refine âŸ¨_, âŸ¨k â€¢ star a, ?_, rflâŸ©, ?_âŸ©
      Â· simpa only [mem_closedBall_zero_iff, norm_smul, one_mul, norm_star] using
          (NNReal.le_inv_iff_mul_le ha.ne').1 (one_mul â€–aâ€–â‚Šâ»Â¹ â–¸ hkâ‚‚.le : â€–kâ€–â‚Š â‰¤ â€–aâ€–â‚Šâ»Â¹)
      Â· simp only [map_smul, nnnorm_smul, mul_apply', mul_smul_comm, CstarRing.nnnorm_self_mul_star]
        rwa [â† NNReal.div_lt_iff (mul_pos ha ha).ne', div_eq_mul_inv, mul_inv, â† mul_assoc]

section CStarProperty

variable [StarRing ğ•œ] [CstarRing ğ•œ] [StarModule ğ•œ E]
variable {E}

/-- This is the key lemma used to establish the instance `Unitization.instCstarRing`
(i.e., proving that the norm on `Unitization ğ•œ E` satisfies the Câ‹†-property). We split this one
out so that declaring the `CstarRing` instance doesn't time out. -/
theorem Unitization.norm_splitMul_snd_sq (x : Unitization ğ•œ E) :
    â€–(Unitization.splitMul ğ•œ E x).sndâ€– ^ 2 â‰¤ â€–(Unitization.splitMul ğ•œ E (star x * x)).sndâ€– := by
  /- The key idea is that we can use `sSup_closed_unit_ball_eq_norm` to make this about
  applying this linear map to elements of norm at most one. There is a bit of `sqrt` and `sq`
  shuffling that needs to occur, which is primarily just an annoyance. -/
  refine (Real.le_sqrt (norm_nonneg _) (norm_nonneg _)).mp ?_
  simp only [Unitization.splitMul_apply]
  rw [â† sSup_closed_unit_ball_eq_norm]
  refine csSup_le ((Metric.nonempty_closedBall.2 zero_le_one).image _) ?_
  rintro - âŸ¨b, hb, rflâŸ©
  simp only
  -- rewrite to a more convenient form; this is where we use the Câ‹†-property
  rw [â† Real.sqrt_sq (norm_nonneg _), Real.sqrt_le_sqrt_iff (norm_nonneg _), sq,
    â† CstarRing.norm_star_mul_self, ContinuousLinearMap.add_apply, star_add, mul_apply',
    Algebra.algebraMap_eq_smul_one, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.one_apply, star_mul, star_smul, add_mul, smul_mul_assoc, â† mul_smul_comm,
    mul_assoc, â† mul_add, â† sSup_closed_unit_ball_eq_norm]
  refine (norm_mul_le _ _).trans ?_
  calc
    _ â‰¤ â€–star x.fst â€¢ (x.fst â€¢ b + x.snd * b) + star x.snd * (x.fst â€¢ b + x.snd * b)â€– := by
      nth_rewrite 2 [â† one_mul â€–_ + _â€–]
      gcongr
      exact (norm_star b).symm â–¸ mem_closedBall_zero_iff.1 hb
    _ â‰¤ sSup (_ '' Metric.closedBall 0 1) := le_csSup ?_ âŸ¨b, hb, ?_âŸ©
  -- now we just check the side conditions for `le_csSup`. There is nothing of interest here.
  Â· refine âŸ¨â€–(star x * x).fstâ€– + â€–(star x * x).sndâ€–, ?_âŸ©
    rintro _ âŸ¨y, hy, rflâŸ©
    refine (norm_add_le _ _).trans ?_
    gcongr
    Â· rw [Algebra.algebraMap_eq_smul_one]
      refine (norm_smul _ _).trans_le ?_
      simpa only [mul_one] using
        mul_le_mul_of_nonneg_left (mem_closedBall_zero_iff.1 hy) (norm_nonneg (star x * x).fst)
    Â· exact (unit_le_opNorm _ y <| mem_closedBall_zero_iff.1 hy).trans (opNorm_mul_apply_le _ _ _)
  Â· simp only [ContinuousLinearMap.add_apply, mul_apply', Unitization.snd_star, Unitization.snd_mul,
      Unitization.fst_mul, Unitization.fst_star, Algebra.algebraMap_eq_smul_one, smul_apply,
      one_apply, smul_add, mul_add, add_mul]
    simp only [smul_smul, smul_mul_assoc, â† add_assoc, â† mul_assoc, mul_smul_comm]

variable {ğ•œ}

/-- The norm on `Unitization ğ•œ E` satisfies the Câ‹†-property -/
instance Unitization.instCstarRing : CstarRing (Unitization ğ•œ E) where
  norm_star_mul_self {x} := by
    -- rewrite both sides as a `âŠ”`
    simp only [Unitization.norm_def, Prod.norm_def, â† sup_eq_max]
    -- Show that `(Unitization.splitMul ğ•œ E x).snd` satisifes the Câ‹†-property, in two stages:
    have hâ‚ : âˆ€ x : Unitization ğ•œ E,
        â€–(Unitization.splitMul ğ•œ E x).sndâ€– â‰¤ â€–(Unitization.splitMul ğ•œ E (star x)).sndâ€– := by
      simp only [add_zero, Unitization.splitMul_apply, Unitization.snd_star, Unitization.fst_star]
      intro x
      /- split based on whether the term inside the norm is zero or not. If so, it's trivial.
      If not, then apply `norm_splitMul_snd_sq` and cancel one copy of the norm -/
      by_cases h : algebraMap ğ•œ (E â†’L[ğ•œ] E) x.fst + mul ğ•œ E x.snd = 0
      Â· simp only [h, norm_zero, norm_le_zero_iff]
        exact norm_nonneg _
      Â· have : â€–(Unitization.splitMul ğ•œ E x).sndâ€– ^ 2 â‰¤
          â€–(Unitization.splitMul ğ•œ E (star x)).sndâ€– * â€–(Unitization.splitMul ğ•œ E x).sndâ€– :=
          (norm_splitMul_snd_sq ğ•œ x).trans <| by
            rw [map_mul, Prod.snd_mul]
            exact norm_mul_le _ _
        rw [sq] at this
        rw [â† Ne, â† norm_pos_iff] at h
        simp only [add_zero, Unitization.splitMul_apply, Unitization.snd_star,
          Unitization.fst_star, star_star] at this
        exact (mul_le_mul_right h).mp this
    -- in this step we make use of the key lemma `norm_splitMul_snd_sq`
    have hâ‚‚ : â€–(Unitization.splitMul ğ•œ E (star x * x)).sndâ€–
        = â€–(Unitization.splitMul ğ•œ E x).sndâ€– ^ 2 := by
      refine le_antisymm ?_ (norm_splitMul_snd_sq ğ•œ x)
      rw [map_mul, Prod.snd_mul]
      exact (norm_mul_le _ _).trans <| by
        rw [sq]
        gcongr
        simpa only [star_star] using hâ‚ (star x)
    -- Show that `(Unitization.splitMul ğ•œ E x).fst` satisfies the Câ‹†-property
    have hâ‚ƒ : â€–(Unitization.splitMul ğ•œ E (star x * x)).fstâ€–
        = â€–(Unitization.splitMul ğ•œ E x).fstâ€– ^ 2 := by
      simp only [Unitization.splitMul_apply, Unitization.fst_mul, Unitization.fst_star, add_zero,
        norm_mul, norm_star, sq]
    rw [hâ‚‚, hâ‚ƒ]
    /- use the definition of the norm, and split into cases based on whether the norm in the first
    coordinate is bigger or smaller than the norm in the second coordinate. -/
    by_cases h : â€–(Unitization.splitMul ğ•œ E x).fstâ€– â‰¤ â€–(Unitization.splitMul ğ•œ E x).sndâ€–
    Â· rw [sq, sq, sup_eq_right.mpr h, sup_eq_right.mpr (mul_self_le_mul_self (norm_nonneg _) h)]
    Â· replace h := (not_le.mp h).le
      rw [sq, sq, sup_eq_left.mpr h, sup_eq_left.mpr (mul_self_le_mul_self (norm_nonneg _) h)]

end CStarProperty
