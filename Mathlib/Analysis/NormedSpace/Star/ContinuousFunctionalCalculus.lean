/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.NormedSpace.Star.GelfandDuality
import Mathlib.Topology.Algebra.StarSubalgebra

#align_import analysis.normed_space.star.continuous_functional_calculus from "leanprover-community/mathlib"@"31c24aa72e7b3e5ed97a8412470e904f82b81004"

/-! # Continuous functional calculus

In this file we construct the `continuousFunctionalCalculus` for a normal element `a` of a
(unital) C‚ãÜ-algebra over `‚ÑÇ`. This is a star algebra equivalence
`C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elementalStarAlgebra ‚ÑÇ a` which sends the (restriction of) the
identity map `ContinuousMap.id ‚ÑÇ` to the (unique) preimage of `a` under the coercion of
`elementalStarAlgebra ‚ÑÇ a` to `A`.

Being a star algebra equivalence between C‚ãÜ-algebras, this map is continuous (even an isometry),
and by the Stone-Weierstrass theorem it is the unique star algebra equivalence which extends the
polynomial functional calculus (i.e., `Polynomial.aeval`).

For any continuous function `f : spectrum ‚ÑÇ a ‚Üí ‚ÑÇ`, this makes it possible to define an element
`f a` (not valid notation) in the original algebra, which heuristically has the same eigenspaces as
`a` and acts on eigenvector of `a` for an eigenvalue `Œª` as multiplication by `f Œª`. This
description is perfectly accurate in finite dimension, but only heuristic in infinite dimension as
there might be no genuine eigenvector. In particular, when `f` is a polynomial `‚àë c·µ¢ X‚Å±`, then
`f a` is `‚àë c·µ¢ a‚Å±`. Also, `id a = a`.

This file also includes a proof of the **spectral permanence** theorem for (unital) C‚ãÜ-algebras
(see `StarSubalgebra.spectrum_eq`)

## Main definitions

* `continuousFunctionalCalculus : C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elementalStarAlgebra ‚ÑÇ a`: this
  is the composition of the inverse of the `gelfandStarTransform` with the natural isomorphism
  induced by the homeomorphism `elementalStarAlgebra.characterSpaceHomeo`.
* `elementalStarAlgebra.characterSpaceHomeo` :
  `characterSpace ‚ÑÇ (elementalStarAlgebra ‚ÑÇ a) ‚âÉ‚Çú spectrum ‚ÑÇ a`: this homeomorphism is defined
  by evaluating a character `œÜ` at `a`, and noting that `œÜ a ‚àà spectrum ‚ÑÇ a` since `œÜ` is an
  algebra homomorphism. Moreover, this map is continuous and bijective and since the spaces involved
  are compact Hausdorff, it is a homeomorphism.

## Main statements

* `StarSubalgebra.coe_isUnit`: for `x : S` in a C‚ãÜ-Subalgebra `S` of `A`, then `‚Üëx : A` is a Unit
  if and only if `x` is a unit.
* `StarSubalgebra.spectrum_eq`: **spectral_permanence** for `x : S`, where `S` is a C‚ãÜ-Subalgebra
  of `A`, `spectrum ‚ÑÇ x = spectrum ‚ÑÇ (x : A)`.

## Notes

The result we have established here is the strongest possible, but it is likely not the version
which will be most useful in practice. Future work will include developing an appropriate API for
the continuous functional calculus (including one for real-valued functions with real argument that
applies to self-adjoint elements of the algebra). -/


open scoped Pointwise ENNReal NNReal ComplexOrder

open WeakDual WeakDual.CharacterSpace elementalStarAlgebra

variable {A : Type*} [NormedRing A] [NormedAlgebra ‚ÑÇ A]

variable [StarRing A] [CstarRing A] [StarModule ‚ÑÇ A]

instance {R A : Type*} [CommRing R] [StarRing R] [NormedRing A] [Algebra R A] [StarRing A]
    [ContinuousStar A] [StarModule R A] (a : A) [IsStarNormal a] :
    NormedCommRing (elementalStarAlgebra R a) :=
  { SubringClass.toNormedRing (elementalStarAlgebra R a) with
    mul_comm := mul_comm }

-- porting note: these hack instances no longer seem to be necessary
#noalign elemental_star_algebra.complex.normed_algebra

variable [CompleteSpace A] (a : A) [IsStarNormal a] (S : StarSubalgebra ‚ÑÇ A)

/-- This lemma is used in the proof of `elementalStarAlgebra.isUnit_of_isUnit_of_isStarNormal`,
which in turn is the key to spectral permanence `StarSubalgebra.spectrum_eq`, which is itself
necessary for the continuous functional calculus. Using the continuous functional calculus, this
lemma can be superseded by one that omits the `IsStarNormal` hypothesis. -/
theorem spectrum_star_mul_self_of_isStarNormal :
    spectrum ‚ÑÇ (star a * a) ‚äÜ Set.Icc (0 : ‚ÑÇ) ‚Äñstar a * a‚Äñ := by
  -- this instance should be found automatically, but without providing it Lean goes on a wild
  -- goose chase when trying to apply `spectrum.gelfandTransform_eq`.
  --letI := elementalStarAlgebra.Complex.normedAlgebra a
  rcases subsingleton_or_nontrivial A with ‚ü®‚ü©
  -- ‚ä¢ spectrum ‚ÑÇ (star a * a) ‚äÜ Set.Icc 0 ‚Üë‚Äñstar a * a‚Äñ
  ¬∑ simp only [spectrum.of_subsingleton, Set.empty_subset]
    -- üéâ no goals
  ¬∑ set a' : elementalStarAlgebra ‚ÑÇ a := ‚ü®a, self_mem ‚ÑÇ a‚ü©
    -- ‚ä¢ spectrum ‚ÑÇ (star a * a) ‚äÜ Set.Icc 0 ‚Üë‚Äñstar a * a‚Äñ
    refine' (spectrum.subset_starSubalgebra (star a' * a')).trans _
    -- ‚ä¢ spectrum ‚ÑÇ (star a' * a') ‚äÜ Set.Icc 0 ‚Üë‚Äñstar a * a‚Äñ
    rw [‚Üê spectrum.gelfandTransform_eq (star a' * a'), ContinuousMap.spectrum_eq_range]
    -- ‚ä¢ Set.range ‚Üë(‚Üë(gelfandTransform ‚ÑÇ { x // x ‚àà elementalStarAlgebra ‚ÑÇ a }) (sta ‚Ä¶
    rintro - ‚ü®œÜ, rfl‚ü©
    -- ‚ä¢ ‚Üë(‚Üë(gelfandTransform ‚ÑÇ { x // x ‚àà elementalStarAlgebra ‚ÑÇ a }) (star a' * a') ‚Ä¶
    rw [gelfandTransform_apply_apply ‚ÑÇ _ (star a' * a') œÜ, map_mul œÜ, map_star œÜ]
    -- ‚ä¢ star (‚ÜëœÜ a') * ‚ÜëœÜ a' ‚àà Set.Icc 0 ‚Üë‚Äñstar a * a‚Äñ
    rw [Complex.eq_coe_norm_of_nonneg (star_mul_self_nonneg _), ‚Üê map_star, ‚Üê map_mul]
    -- ‚ä¢ ‚Üë‚Äñ‚ÜëœÜ (star a' * a')‚Äñ ‚àà Set.Icc 0 ‚Üë‚Äñstar a * a‚Äñ
    exact
      ‚ü®Complex.zero_le_real.2 (norm_nonneg _),
        Complex.real_le_real.2 (AlgHom.norm_apply_le_self œÜ (star a' * a'))‚ü©
#align spectrum_star_mul_self_of_is_star_normal spectrum_star_mul_self_of_isStarNormal

variable {a}

/-- This is the key lemma on the way to establishing spectral permanence for C‚ãÜ-algebras, which is
established in `StarSubalgebra.spectrum_eq`. This lemma is superseded by
`StarSubalgebra.coe_isUnit`, which does not require an `IsStarNormal` hypothesis and holds for
any closed star subalgebra. -/
theorem elementalStarAlgebra.isUnit_of_isUnit_of_isStarNormal (h : IsUnit a) :
    IsUnit (‚ü®a, self_mem ‚ÑÇ a‚ü© : elementalStarAlgebra ‚ÑÇ a) := by
  /- Sketch of proof: Because `a` is normal, it suffices to prove that `star a * a` is invertible
    in `elementalStarAlgebra ‚ÑÇ a`. For this it suffices to prove that it is sufficiently close to a
    unit, namely `algebraMap ‚ÑÇ _ ‚Äñstar a * a‚Äñ`, and in this case the required distance is
    `‚Äñstar a * a‚Äñ`. So one must show `‚Äñstar a * a - algebraMap ‚ÑÇ _ ‚Äñstar a * a‚Äñ‚Äñ < ‚Äñstar a * a‚Äñ`.
    Since `star a * a - algebraMap ‚ÑÇ _ ‚Äñstar a * a‚Äñ` is selfadjoint, by a corollary of Gelfand's
    formula for the spectral radius (`IsSelfAdjoint.spectralRadius_eq_nnnorm`) its norm is the
    supremum of the norms of elements in its spectrum (we may use the spectrum in `A` here because
    the norm in `A` and the norm in the subalgebra coincide).

    By `spectrum_star_mul_self_of_isStarNormal`, the spectrum (in the algebra `A`) of `star a * a`
    is contained in the interval `[0, ‚Äñstar a * a‚Äñ]`, and since `a` (and hence `star a * a`) is
    invertible in `A`, we may omit `0` from this interval. Therefore, by basic spectral mapping
    properties, the spectrum (in the algebra `A`) of `star a * a - algebraMap ‚ÑÇ _ ‚Äñstar a * a‚Äñ` is
    contained in `[0, ‚Äñstar a * a‚Äñ)`. The supremum of the (norms of) elements of the spectrum must
    be *strictly* less that `‚Äñstar a * a‚Äñ` because the spectrum is compact, which completes the
    proof. -/
  /- We may assume `A` is nontrivial. It suffices to show that `star a * a` is invertible in the
    commutative (because `a` is normal) ring `elementalStarAlgebra ‚ÑÇ a`. Indeed, by commutativity,
    if `star a * a` is invertible, then so is `a`. -/
  nontriviality A
  -- ‚ä¢ IsUnit { val := a, property := (_ : a ‚àà elementalStarAlgebra ‚ÑÇ a) }
  set a' : elementalStarAlgebra ‚ÑÇ a := ‚ü®a, self_mem ‚ÑÇ a‚ü©
  -- ‚ä¢ IsUnit a'
  suffices : IsUnit (star a' * a'); exact (IsUnit.mul_iff.1 this).2
  -- ‚ä¢ IsUnit a'
                                    -- ‚ä¢ IsUnit (star a' * a')
  replace h := (show Commute (star a) a from star_comm_self' a).isUnit_mul_iff.2 ‚ü®h.star, h‚ü©
  -- ‚ä¢ IsUnit (star a' * a')
  /- Since `a` is invertible, `‚Äñstar a * a‚Äñ ‚â† 0`, so `‚Äñstar a * a‚Äñ ‚Ä¢ 1` is invertible in
    `elementalStarAlgebra ‚ÑÇ a`, and so it suffices to show that the distance between this unit and
    `star a * a` is less than `‚Äñstar a * a‚Äñ`. -/
  have h‚ÇÅ : (‚Äñstar a * a‚Äñ : ‚ÑÇ) ‚â† 0 := Complex.ofReal_ne_zero.mpr (norm_ne_zero_iff.mpr h.ne_zero)
  -- ‚ä¢ IsUnit (star a' * a')
  set u : Units (elementalStarAlgebra ‚ÑÇ a) :=
    Units.map (algebraMap ‚ÑÇ (elementalStarAlgebra ‚ÑÇ a)).toMonoidHom (Units.mk0 _ h‚ÇÅ)
  refine' ‚ü®u.ofNearby _ _, rfl‚ü©
  -- ‚ä¢ ‚Äñstar a' * a' - ‚Üëu‚Äñ < ‚Äñ‚Üëu‚Åª¬π‚Äñ‚Åª¬π
  simp only [Units.coe_map, Units.val_inv_eq_inv_val, RingHom.toMonoidHom_eq_coe, Units.val_mk0,
    Units.coe_map_inv, MonoidHom.coe_coe, norm_algebraMap', norm_inv, Complex.norm_eq_abs,
    Complex.abs_ofReal, abs_norm, inv_inv]
    --RingHom.coe_monoidHom,
    -- Complex.abs_ofReal, map_inv‚ÇÄ,
  --rw [norm_algebraMap', inv_inv, Complex.norm_eq_abs, abs_norm]I-
  /- Since `a` is invertible, by `spectrum_star_mul_self_of_isStarNormal`, the spectrum (in `A`)
    of `star a * a` is contained in the half-open interval `(0, ‚Äñstar a * a‚Äñ]`. Therefore, by basic
    spectral mapping properties, the spectrum of `‚Äñstar a * a‚Äñ ‚Ä¢ 1 - star a * a` is contained in
    `[0, ‚Äñstar a * a‚Äñ)`. -/
  have h‚ÇÇ : ‚àÄ z ‚àà spectrum ‚ÑÇ (algebraMap ‚ÑÇ A ‚Äñstar a * a‚Äñ - star a * a), ‚Äñz‚Äñ‚Çä < ‚Äñstar a * a‚Äñ‚Çä := by
    intro z hz
    rw [‚Üê spectrum.singleton_sub_eq, Set.singleton_sub] at hz
    have h‚ÇÉ : z ‚àà Set.Icc (0 : ‚ÑÇ) ‚Äñstar a * a‚Äñ := by
      replace hz := Set.image_subset _ (spectrum_star_mul_self_of_isStarNormal a) hz
      rwa [Set.image_const_sub_Icc, sub_self, sub_zero] at hz
    refine' lt_of_le_of_ne (Complex.real_le_real.1 <| Complex.eq_coe_norm_of_nonneg h‚ÇÉ.1 ‚ñ∏ h‚ÇÉ.2) _
    ¬∑ intro hz'
      replace hz' := congr_arg (fun x : ‚Ñù‚â•0 => ((x : ‚Ñù) : ‚ÑÇ)) hz'
      simp only [coe_nnnorm] at hz'
      rw [‚Üê Complex.eq_coe_norm_of_nonneg h‚ÇÉ.1] at hz'
      obtain ‚ü®w, hw‚ÇÅ, hw‚ÇÇ‚ü© := hz
      refine' (spectrum.zero_not_mem_iff ‚ÑÇ).mpr h _
      rw [hz', sub_eq_self] at hw‚ÇÇ
      rwa [hw‚ÇÇ] at hw‚ÇÅ
  /- The norm of `‚Äñstar a * a‚Äñ ‚Ä¢ 1 - star a * a` in the subalgebra and in `A` coincide. In `A`,
    because this element is selfadjoint, by `IsSelfAdjoint.spectralRadius_eq_nnnorm`, its norm is
    the supremum of the norms of the elements of the spectrum, which is strictly less than
    `‚Äñstar a * a‚Äñ` by `h‚ÇÇ` and because the spectrum is compact. -/
  exact ENNReal.coe_lt_coe.1
    (calc
      (‚Äñstar a' * a' - algebraMap ‚ÑÇ _ ‚Äñstar a * a‚Äñ‚Äñ‚Çä : ‚Ñù‚â•0‚àû) =
          ‚ÄñalgebraMap ‚ÑÇ A ‚Äñstar a * a‚Äñ - star a * a‚Äñ‚Çä :=
        by rw [‚Üê nnnorm_neg, neg_sub]; rfl
      _ = spectralRadius ‚ÑÇ (algebraMap ‚ÑÇ A ‚Äñstar a * a‚Äñ - star a * a) := by
        refine' (IsSelfAdjoint.spectralRadius_eq_nnnorm _).symm
        rw [IsSelfAdjoint, star_sub, star_mul, star_star, ‚Üê algebraMap_star_comm]
        congr!
        exact IsROrC.conj_ofReal _
      _ < ‚Äñstar a * a‚Äñ‚Çä := spectrum.spectralRadius_lt_of_forall_lt _ h‚ÇÇ)
#align elemental_star_algebra.is_unit_of_is_unit_of_is_star_normal elementalStarAlgebra.isUnit_of_isUnit_of_isStarNormal

/-- For `x : A` which is invertible in `A`, the inverse lies in any unital C‚ãÜ-subalgebra `S`
containing `x`. -/
theorem StarSubalgebra.isUnit_coe_inv_mem {S : StarSubalgebra ‚ÑÇ A} (hS : IsClosed (S : Set A))
    {x : A} (h : IsUnit x) (hxS : x ‚àà S) : ‚Üëh.unit‚Åª¬π ‚àà S := by
  have hx := h.star.mul h
  -- ‚ä¢ ‚Üë(IsUnit.unit h)‚Åª¬π ‚àà S
  suffices this : (‚Üëhx.unit‚Åª¬π : A) ‚àà S
  -- ‚ä¢ ‚Üë(IsUnit.unit h)‚Åª¬π ‚àà S
  ¬∑ rw [‚Üê one_mul (‚Üëh.unit‚Åª¬π : A), ‚Üê hx.unit.inv_mul, mul_assoc, IsUnit.unit_spec, mul_assoc,
      h.mul_val_inv, mul_one]
    exact mul_mem this (star_mem hxS)
    -- üéâ no goals
  refine' le_of_isClosed_of_mem ‚ÑÇ hS (mul_mem (star_mem hxS) hxS) _
  -- ‚ä¢ ‚Üë(IsUnit.unit hx)‚Åª¬π ‚àà elementalStarAlgebra ‚ÑÇ (star x * x)
  haveI := (IsSelfAdjoint.star_mul_self x).isStarNormal
  -- ‚ä¢ ‚Üë(IsUnit.unit hx)‚Åª¬π ‚àà elementalStarAlgebra ‚ÑÇ (star x * x)
  have hx' := elementalStarAlgebra.isUnit_of_isUnit_of_isStarNormal hx
  -- ‚ä¢ ‚Üë(IsUnit.unit hx)‚Åª¬π ‚àà elementalStarAlgebra ‚ÑÇ (star x * x)
  convert (‚Üëhx'.unit‚Åª¬π : elementalStarAlgebra ‚ÑÇ (star x * x)).prop using 1
  -- ‚ä¢ ‚Üë(IsUnit.unit hx)‚Åª¬π = ‚Üë‚Üë(IsUnit.unit hx')‚Åª¬π
  refine left_inv_eq_right_inv hx.unit.inv_mul ?_
  -- ‚ä¢ ‚Üë(IsUnit.unit hx) * ‚Üë‚Üë(IsUnit.unit hx')‚Åª¬π = 1
  exact (congr_arg ((‚Üë) : _ ‚Üí A) hx'.unit.mul_inv)
  -- üéâ no goals
#align star_subalgebra.is_unit_coe_inv_mem StarSubalgebra.isUnit_coe_inv_mem

/-- For a unital C‚ãÜ-subalgebra `S` of `A` and `x : S`, if `‚Üëx : A` is invertible in `A`, then
`x` is invertible in `S`. -/
theorem StarSubalgebra.coe_isUnit {S : StarSubalgebra ‚ÑÇ A} (hS : IsClosed (S : Set A)) {x : S} :
    IsUnit (x : A) ‚Üî IsUnit x := by
  refine'
    ‚ü®fun hx => ‚ü®‚ü®x, ‚ü®(‚Üëhx.unit‚Åª¬π : A), StarSubalgebra.isUnit_coe_inv_mem hS hx x.prop‚ü©, _, _‚ü©, rfl‚ü©,
      fun hx => hx.map S.subtype‚ü©
  exacts [Subtype.coe_injective hx.mul_val_inv, Subtype.coe_injective hx.val_inv_mul]
  -- üéâ no goals
#align star_subalgebra.coe_is_unit StarSubalgebra.coe_isUnit

theorem StarSubalgebra.mem_spectrum_iff {S : StarSubalgebra ‚ÑÇ A} (hS : IsClosed (S : Set A)) {x : S}
    {z : ‚ÑÇ} : z ‚àà spectrum ‚ÑÇ x ‚Üî z ‚àà spectrum ‚ÑÇ (x : A) :=
  not_iff_not.2 (StarSubalgebra.coe_isUnit hS).symm
#align star_subalgebra.mem_spectrum_iff StarSubalgebra.mem_spectrum_iff

/-- **Spectral permanence.** The spectrum of an element is invariant of the (closed)
`StarSubalgebra` in which it is contained. -/
theorem StarSubalgebra.spectrum_eq {S : StarSubalgebra ‚ÑÇ A} (hS : IsClosed (S : Set A)) (x : S) :
    spectrum ‚ÑÇ x = spectrum ‚ÑÇ (x : A) :=
  Set.ext fun _z => StarSubalgebra.mem_spectrum_iff hS
#align star_subalgebra.spectrum_eq StarSubalgebra.spectrum_eq

variable (a)

/-- The natural map from `characterSpace ‚ÑÇ (elementalStarAlgebra ‚ÑÇ x)` to `spectrum ‚ÑÇ x` given
by evaluating `œÜ` at `x`. This is essentially just evaluation of the `gelfandTransform` of `x`,
but because we want something in `spectrum ‚ÑÇ x`, as opposed to
`spectrum ‚ÑÇ ‚ü®x, elementalStarAlgebra.self_mem ‚ÑÇ x‚ü©` there is slightly more work to do. -/
@[simps]
noncomputable def elementalStarAlgebra.characterSpaceToSpectrum (x : A)
    (œÜ : characterSpace ‚ÑÇ (elementalStarAlgebra ‚ÑÇ x)) : spectrum ‚ÑÇ x where
  val := œÜ ‚ü®x, self_mem ‚ÑÇ x‚ü©
  property := by
    simpa only [StarSubalgebra.spectrum_eq (elementalStarAlgebra.isClosed ‚ÑÇ x)
        ‚ü®x, self_mem ‚ÑÇ x‚ü©] using
      AlgHom.apply_mem_spectrum œÜ ‚ü®x, self_mem ‚ÑÇ x‚ü©
#align elemental_star_algebra.character_space_to_spectrum elementalStarAlgebra.characterSpaceToSpectrum

theorem elementalStarAlgebra.continuous_characterSpaceToSpectrum (x : A) :
    Continuous (elementalStarAlgebra.characterSpaceToSpectrum x) :=
  continuous_induced_rng.2
    (map_continuous <| gelfandTransform ‚ÑÇ (elementalStarAlgebra ‚ÑÇ x) ‚ü®x, self_mem ‚ÑÇ x‚ü©)
#align elemental_star_algebra.continuous_character_space_to_spectrum elementalStarAlgebra.continuous_characterSpaceToSpectrum

theorem elementalStarAlgebra.bijective_characterSpaceToSpectrum :
    Function.Bijective (elementalStarAlgebra.characterSpaceToSpectrum a) := by
  refine' ‚ü®fun œÜ œà h => starAlgHomClass_ext ‚ÑÇ _ _ _, _‚ü©
  ¬∑ exact (map_continuous œÜ)
    -- üéâ no goals
  ¬∑ exact (map_continuous œà)
    -- üéâ no goals
  ¬∑ simpa only [elementalStarAlgebra.characterSpaceToSpectrum, Subtype.mk_eq_mk,
      ContinuousMap.coe_mk] using h
  ¬∑ rintro ‚ü®z, hz‚ü©
    -- ‚ä¢ ‚àÉ a_1, characterSpaceToSpectrum a a_1 = { val := z, property := hz }
    have hz' := (StarSubalgebra.spectrum_eq (elementalStarAlgebra.isClosed ‚ÑÇ a)
      ‚ü®a, self_mem ‚ÑÇ a‚ü©).symm.subst hz
    rw [CharacterSpace.mem_spectrum_iff_exists] at hz'
    -- ‚ä¢ ‚àÉ a_1, characterSpaceToSpectrum a a_1 = { val := z, property := hz }
    obtain ‚ü®œÜ, rfl‚ü© := hz'
    -- ‚ä¢ ‚àÉ a_1, characterSpaceToSpectrum a a_1 = { val := ‚ÜëœÜ { val := a, property :=  ‚Ä¶
    exact ‚ü®œÜ, rfl‚ü©
    -- üéâ no goals
#align elemental_star_algebra.bijective_character_space_to_spectrum elementalStarAlgebra.bijective_characterSpaceToSpectrum

-- porting note: it would be good to understand why and where Lean is having trouble here
set_option synthInstance.maxHeartbeats 40000 in
/-- The homeomorphism between the character space of the unital C‚ãÜ-subalgebra generated by a
single normal element `a : A` and `spectrum ‚ÑÇ a`. -/
noncomputable def elementalStarAlgebra.characterSpaceHomeo :
    characterSpace ‚ÑÇ (elementalStarAlgebra ‚ÑÇ a) ‚âÉ‚Çú spectrum ‚ÑÇ a :=
  @Continuous.homeoOfEquivCompactToT2 _ _ _ _ _ _
    (Equiv.ofBijective (elementalStarAlgebra.characterSpaceToSpectrum a)
      (elementalStarAlgebra.bijective_characterSpaceToSpectrum a))
    (elementalStarAlgebra.continuous_characterSpaceToSpectrum a)
#align elemental_star_algebra.character_space_homeo elementalStarAlgebra.characterSpaceHomeo

-- porting note: it would be good to understand why and where Lean is having trouble here
set_option maxHeartbeats 350000 in
/-- **Continuous functional calculus.** Given a normal element `a : A` of a unital C‚ãÜ-algebra,
the continuous functional calculus is a `StarAlgEquiv` from the complex-valued continuous
functions on the spectrum of `a` to the unital C‚ãÜ-subalgebra generated by `a`. Moreover, this
equivalence identifies `(ContinuousMap.id ‚ÑÇ).restrict (spectrum ‚ÑÇ a))` with `a`; see
`continuousFunctionalCalculus_map_id`. As such it extends the polynomial functional calculus. -/
noncomputable def continuousFunctionalCalculus :
    C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elementalStarAlgebra ‚ÑÇ a :=
  ((elementalStarAlgebra.characterSpaceHomeo a).compStarAlgEquiv' ‚ÑÇ ‚ÑÇ).trans
    (gelfandStarTransform (elementalStarAlgebra ‚ÑÇ a)).symm
#align continuous_functional_calculus continuousFunctionalCalculus

theorem continuousFunctionalCalculus_map_id :
    continuousFunctionalCalculus a ((ContinuousMap.id ‚ÑÇ).restrict (spectrum ‚ÑÇ a)) =
      ‚ü®a, self_mem ‚ÑÇ a‚ü© :=
  (gelfandStarTransform (elementalStarAlgebra ‚ÑÇ a)).symm_apply_apply _
#align continuous_functional_calculus_map_id continuousFunctionalCalculus_map_id
