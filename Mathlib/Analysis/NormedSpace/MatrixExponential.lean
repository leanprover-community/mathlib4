/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.NormedSpace.Exponential
import Mathlib.Analysis.Matrix
import Mathlib.LinearAlgebra.Matrix.ZPow
import Mathlib.LinearAlgebra.Matrix.Hermitian
import Mathlib.LinearAlgebra.Matrix.Symmetric
import Mathlib.Topology.UniformSpace.Matrix

#align_import analysis.normed_space.matrix_exponential from "leanprover-community/mathlib"@"1e3201306d4d9eb1fd54c60d7c4510ad5126f6f9"

/-!
# Lemmas about the matrix exponential

In this file, we provide results about `exp` on `Matrix`s over a topological or normed algebra.
Note that generic results over all topological spaces such as `exp_zero` can be used on matrices
without issue, so are not repeated here. The topological results specific to matrices are:

* `Matrix.exp_transpose`
* `Matrix.exp_conjTranspose`
* `Matrix.exp_diagonal`
* `Matrix.exp_blockDiagonal`
* `Matrix.exp_blockDiagonal'`

Lemmas like `exp_add_of_commute` require a canonical norm on the type; while there are multiple
sensible choices for the norm of a `Matrix` (`Matrix.normedAddCommGroup`,
`Matrix.frobeniusNormedAddCommGroup`, `Matrix.linftyOpNormedAddCommGroup`), none of them
are canonical. In an application where a particular norm is chosen using
`local attribute [instance]`, then the usual lemmas about `exp` are fine. When choosing a norm is
undesirable, the results in this file can be used.

In this file, we copy across the lemmas about `exp`, but hide the requirement for a norm inside the
proof.

* `Matrix.exp_add_of_commute`
* `Matrix.exp_sum_of_commute`
* `Matrix.exp_nsmul`
* `Matrix.isUnit_exp`
* `Matrix.exp_units_conj`
* `Matrix.exp_units_conj'`

Additionally, we prove some results about `matrix.has_inv` and `matrix.div_inv_monoid`, as the
results for general rings are instead stated about `Ring.inverse`:

* `Matrix.exp_neg`
* `Matrix.exp_zsmul`
* `Matrix.exp_conj`
* `Matrix.exp_conj'`

## Implementation notes

This file runs into some sharp edges on typeclass search in lean 3, especially regarding pi types.
To work around this, we copy a handful of instances for when lean can't find them by itself.
Hopefully we will be able to remove these in Lean 4.

## TODO

* Show that `Matrix.det (exp ğ•‚ A) = exp ğ•‚ (Matrix.trace A)`

## References

* https://en.wikipedia.org/wiki/Matrix_exponential
-/


open scoped Matrix BigOperators

section HacksForPiInstanceSearch

/-- A special case of `Pi.instTopologicalRing` for when `R` is not dependently typed. -/
instance Function.topologicalRing (I : Type*) (R : Type*) [NonUnitalRing R] [TopologicalSpace R]
    [TopologicalRing R] : TopologicalRing (I â†’ R) :=
  Pi.instTopologicalRing
#align function.topological_ring Function.topologicalRing

/-- A special case of `Function.algebra` for when A is a `Ring` not a `Semiring` -/
instance Function.algebraRing (I : Type*) {R : Type*} (A : Type*) [CommSemiring R] [Ring A]
    [Algebra R A] : Algebra R (I â†’ A) :=
  Pi.algebra _ _
#align function.algebra_ring Function.algebraRing

/-- A special case of `Pi.algebra` for when `f = Î» i, Matrix (m i) (m i) A`. -/
instance Pi.matrixAlgebra (I R A : Type*) (m : I â†’ Type*) [CommSemiring R] [Semiring A]
    [Algebra R A] [âˆ€ i, Fintype (m i)] [âˆ€ i, DecidableEq (m i)] :
    Algebra R (âˆ€ i, Matrix (m i) (m i) A) :=
  @Pi.algebra I R (fun i => Matrix (m i) (m i) A) _ _ fun _ => Matrix.instAlgebra
#align pi.matrix_algebra Pi.matrixAlgebra

/-- A special case of `Pi.instTopologicalRing` for when `f = Î» i, Matrix (m i) (m i) A`. -/
instance Pi.matrix_topologicalRing (I A : Type*) (m : I â†’ Type*) [Ring A] [TopologicalSpace A]
    [TopologicalRing A] [âˆ€ i, Fintype (m i)] : TopologicalRing (âˆ€ i, Matrix (m i) (m i) A) :=
  @Pi.instTopologicalRing _ (fun i => Matrix (m i) (m i) A) _ _ fun _ => Matrix.topologicalRing
#align pi.matrix_topological_ring Pi.matrix_topologicalRing

end HacksForPiInstanceSearch

variable (ğ•‚ : Type*) {m n p : Type*} {n' : m â†’ Type*} {ğ”¸ : Type*}

namespace Matrix

section Topological

section Ring

variable [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n] [âˆ€ i, Fintype (n' i)]
  [âˆ€ i, DecidableEq (n' i)] [Field ğ•‚] [Ring ğ”¸] [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]
  [Algebra ğ•‚ ğ”¸] [T2Space ğ”¸]

theorem exp_diagonal (v : m â†’ ğ”¸) : exp ğ•‚ (diagonal v) = diagonal (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, diagonal_pow, â† diagonal_smul, â† diagonal_tsum]
#align matrix.exp_diagonal Matrix.exp_diagonal

theorem exp_blockDiagonal (v : m â†’ Matrix n n ğ”¸) :
    exp ğ•‚ (blockDiagonal v) = blockDiagonal (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, â† blockDiagonal_pow, â† blockDiagonal_smul, â† blockDiagonal_tsum]
#align matrix.exp_block_diagonal Matrix.exp_blockDiagonal

theorem exp_blockDiagonal' (v : âˆ€ i, Matrix (n' i) (n' i) ğ”¸) :
    exp ğ•‚ (blockDiagonal' v) = blockDiagonal' (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, â† blockDiagonal'_pow, â† blockDiagonal'_smul, â† blockDiagonal'_tsum]
#align matrix.exp_block_diagonal' Matrix.exp_blockDiagonal'

theorem exp_conjTranspose [StarRing ğ”¸] [ContinuousStar ğ”¸] (A : Matrix m m ğ”¸) :
    exp ğ•‚ Aá´´ = (exp ğ•‚ A)á´´ :=
  (star_exp A).symm
#align matrix.exp_conj_transpose Matrix.exp_conjTranspose

theorem IsHermitian.exp [StarRing ğ”¸] [ContinuousStar ğ”¸] {A : Matrix m m ğ”¸} (h : A.IsHermitian) :
    (exp ğ•‚ A).IsHermitian :=
  (exp_conjTranspose _ _).symm.trans <| congr_arg _ h
#align matrix.is_hermitian.exp Matrix.IsHermitian.exp

end Ring

section CommRing

variable [Fintype m] [DecidableEq m] [Field ğ•‚] [CommRing ğ”¸] [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]
  [Algebra ğ•‚ ğ”¸] [T2Space ğ”¸]

theorem exp_transpose (A : Matrix m m ğ”¸) : exp ğ•‚ Aáµ€ = (exp ğ•‚ A)áµ€ := by
  simp_rw [exp_eq_tsum, transpose_tsum, transpose_smul, transpose_pow]
#align matrix.exp_transpose Matrix.exp_transpose

theorem IsSymm.exp {A : Matrix m m ğ”¸} (h : A.IsSymm) : (exp ğ•‚ A).IsSymm :=
  (exp_transpose _ _).symm.trans <| congr_arg _ h
#align matrix.is_symm.exp Matrix.IsSymm.exp

end CommRing

end Topological

section Normed

variable [IsROrC ğ•‚] [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n] [âˆ€ i, Fintype (n' i)]
  [âˆ€ i, DecidableEq (n' i)] [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

nonrec theorem exp_add_of_commute (A B : Matrix m m ğ”¸) (h : Commute A B) :
    exp ğ•‚ (A + B) = exp ğ•‚ A â¬ exp ğ•‚ B := by
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact exp_add_of_commute h
#align matrix.exp_add_of_commute Matrix.exp_add_of_commute

nonrec theorem exp_sum_of_commute {Î¹} (s : Finset Î¹) (f : Î¹ â†’ Matrix m m ğ”¸)
    (h : (s : Set Î¹).Pairwise fun i j => Commute (f i) (f j)) :
    exp ğ•‚ (âˆ‘ i in s, f i) =
      s.noncommProd (fun i => exp ğ•‚ (f i)) fun i hi j hj _ => (h.of_refl hi hj).exp ğ•‚ := by
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact exp_sum_of_commute s f h
#align matrix.exp_sum_of_commute Matrix.exp_sum_of_commute

nonrec theorem exp_nsmul (n : â„•) (A : Matrix m m ğ”¸) : exp ğ•‚ (n â€¢ A) = exp ğ•‚ A ^ n := by
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact exp_nsmul n A
#align matrix.exp_nsmul Matrix.exp_nsmul

nonrec theorem isUnit_exp (A : Matrix m m ğ”¸) : IsUnit (exp ğ•‚ A) := by
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact isUnit_exp _ A
#align matrix.is_unit_exp Matrix.isUnit_exp

nonrec theorem exp_units_conj (U : (Matrix m m ğ”¸)Ë£) (A : Matrix m m ğ”¸) :
    exp ğ•‚ (â†‘U â¬ A â¬ â†‘Uâ»Â¹ : Matrix m m ğ”¸) = â†‘U â¬ exp ğ•‚ A â¬ â†‘Uâ»Â¹ := by
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact exp_units_conj _ U A
#align matrix.exp_units_conj Matrix.exp_units_conj

theorem exp_units_conj' (U : (Matrix m m ğ”¸)Ë£) (A : Matrix m m ğ”¸) :
    exp ğ•‚ (â†‘Uâ»Â¹ â¬ A â¬ U : Matrix m m ğ”¸) = â†‘Uâ»Â¹ â¬ exp ğ•‚ A â¬ U :=
  exp_units_conj ğ•‚ Uâ»Â¹ A
#align matrix.exp_units_conj' Matrix.exp_units_conj'

end Normed

section NormedComm

variable [IsROrC ğ•‚] [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n] [âˆ€ i, Fintype (n' i)]
  [âˆ€ i, DecidableEq (n' i)] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

theorem exp_neg (A : Matrix m m ğ”¸) : exp ğ•‚ (-A) = (exp ğ•‚ A)â»Â¹ := by
  rw [nonsing_inv_eq_ring_inverse]
  letI : SeminormedRing (Matrix m m ğ”¸) := Matrix.linftyOpSemiNormedRing
  letI : NormedRing (Matrix m m ğ”¸) := Matrix.linftyOpNormedRing
  letI : NormedAlgebra ğ•‚ (Matrix m m ğ”¸) := Matrix.linftyOpNormedAlgebra
  exact (Ring.inverse_exp _ A).symm
#align matrix.exp_neg Matrix.exp_neg

theorem exp_zsmul (z : â„¤) (A : Matrix m m ğ”¸) : exp ğ•‚ (z â€¢ A) = exp ğ•‚ A ^ z := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· rw [zpow_ofNat, coe_nat_zsmul, exp_nsmul]
  Â· have : IsUnit (exp ğ•‚ A).det := (Matrix.isUnit_iff_isUnit_det _).mp (isUnit_exp _ _)
    rw [Matrix.zpow_neg this, zpow_ofNat, neg_smul, exp_neg, coe_nat_zsmul, exp_nsmul]
#align matrix.exp_zsmul Matrix.exp_zsmul

theorem exp_conj (U : Matrix m m ğ”¸) (A : Matrix m m ğ”¸) (hy : IsUnit U) :
    exp ğ•‚ (U â¬ A â¬ Uâ»Â¹) = U â¬ exp ğ•‚ A â¬ Uâ»Â¹ :=
  let âŸ¨u, huâŸ© := hy
  hu â–¸ by simpa only [Matrix.coe_units_inv] using exp_units_conj ğ•‚ u A
#align matrix.exp_conj Matrix.exp_conj

theorem exp_conj' (U : Matrix m m ğ”¸) (A : Matrix m m ğ”¸) (hy : IsUnit U) :
    exp ğ•‚ (Uâ»Â¹ â¬ A â¬ U) = Uâ»Â¹ â¬ exp ğ•‚ A â¬ U :=
  let âŸ¨u, huâŸ© := hy
  hu â–¸ by simpa only [Matrix.coe_units_inv] using exp_units_conj' ğ•‚ u A
#align matrix.exp_conj' Matrix.exp_conj'

end NormedComm

end Matrix
