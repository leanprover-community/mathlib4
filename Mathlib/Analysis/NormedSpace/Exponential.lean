/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Eric Wieser
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Complex.Basic
import Mathlib.Analysis.Normed.Field.InfiniteSum
import Mathlib.Data.Nat.Choose.Cast
import Mathlib.Data.Finset.NoncommProd
import Mathlib.Topology.Algebra.Algebra

#align_import analysis.normed_space.exponential from "leanprover-community/mathlib"@"62748956a1ece9b26b33243e2e3a2852176666f5"

/-!
# Exponential in a Banach algebra

In this file, we define `exp ğ•‚ : ğ”¸ â†’ ğ”¸`, the exponential map in a topological algebra `ğ”¸` over a
field `ğ•‚`.

While for most interesting results we need `ğ”¸` to be normed algebra, we do not require this in the
definition in order to make `exp` independent of a particular choice of norm. The definition also
does not require that `ğ”¸` be complete, but we need to assume it for most results.

We then prove some basic results, but we avoid importing derivatives here to minimize dependencies.
Results involving derivatives and comparisons with `Real.exp` and `Complex.exp` can be found in
`Analysis.SpecialFunctions.Exponential`.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `NormedSpace.exp_add_of_commute_of_mem_ball` : if `ğ•‚` has characteristic zero,
  then given two commuting elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `NormedSpace.exp_add_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative,
  then given two elements `x` and `y` in the disk of convergence, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `NormedSpace.exp_neg_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is a division ring,
  then given an element `x` in the disk of convergence, we have `exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹`.

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `expSeries_radius_eq_top` : the `FormalMultilinearSeries` defining `exp ğ•‚` has infinite
  radius of convergence
- `NormedSpace.exp_add_of_commute` : given two commuting elements `x` and `y`, we have
  `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
- `NormedSpace.exp_add` : if `ğ”¸` is commutative, then we have `exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`
  for any `x` and `y`
- `NormedSpace.exp_neg` : if `ğ”¸` is a division ring, then we have `exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹`.
- `exp_sum_of_commute` : the analogous result to `NormedSpace.exp_add_of_commute` for `Finset.sum`.
- `exp_sum` : the analogous result to `NormedSpace.exp_add` for `Finset.sum`.
- `NormedSpace.exp_nsmul` : repeated addition in the domain corresponds to
  repeated multiplication in the codomain.
- `NormedSpace.exp_zsmul` : repeated addition in the domain corresponds to
  repeated multiplication in the codomain.

### Other useful compatibility results

- `NormedSpace.exp_eq_exp` : if `ğ”¸` is a normed algebra over two fields `ğ•‚` and `ğ•‚'`,
  then `exp ğ•‚ = exp ğ•‚' ğ”¸`

### Notes

We put nearly all the statements in this file in the `NormedSpace` namespace,
to avoid collisions with the `Real` or `Complex` namespaces.

As of 2023-11-16 due to bad instances in Mathlib
```
import Mathlib

open Real

#time example (x : â„) : 0 < exp x      := exp_pos _ -- 250ms
#time example (x : â„) : 0 < Real.exp x := exp_pos _ -- 2ms
```
This is because `exp x` tries the `NormedSpace.exp` function defined here,
and generates a slow coercion search from `Real` to `Type`, to fit the first argument here.
We will resolve this slow coercion separately,
but we want to move `exp` out of the root namespace in any case to avoid this ambiguity.

In the long term is may be possible to replace `Real.exp` and `Complex.exp` with this one.

-/


namespace NormedSpace

open Filter RCLike ContinuousMultilinearMap NormedField Asymptotics

open scoped Nat Topology ENNReal

section TopologicalAlgebra

variable (ğ•‚ ğ”¸ : Type*) [Field ğ•‚] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]

/-- `expSeries ğ•‚ ğ”¸` is the `FormalMultilinearSeries` whose `n`-th term is the map
`(xáµ¢) : ğ”¸â¿ â†¦ (1/n! : ğ•‚) â€¢ âˆ xáµ¢`. Its sum is the exponential map `exp ğ•‚ : ğ”¸ â†’ ğ”¸`. -/
def expSeries : FormalMultilinearSeries ğ•‚ ğ”¸ ğ”¸ := fun n =>
  (n !â»Â¹ : ğ•‚) â€¢ ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸
#align exp_series NormedSpace.expSeries

variable {ğ”¸}

/-- `exp ğ•‚ : ğ”¸ â†’ ğ”¸` is the exponential map determined by the action of `ğ•‚` on `ğ”¸`.
It is defined as the sum of the `FormalMultilinearSeries` `expSeries ğ•‚ ğ”¸`.

Note that when `ğ”¸ = Matrix n n ğ•‚`, this is the **Matrix Exponential**; see
[`Analysis.NormedSpace.MatrixExponential`](./MatrixExponential) for lemmas specific to that
case. -/
noncomputable def exp (x : ğ”¸) : ğ”¸ :=
  (expSeries ğ•‚ ğ”¸).sum x
#align exp NormedSpace.exp

variable {ğ•‚}

theorem expSeries_apply_eq (x : ğ”¸) (n : â„•) :
    (expSeries ğ•‚ ğ”¸ n fun _ => x) = (n !â»Â¹ : ğ•‚) â€¢ x ^ n := by simp [expSeries]
#align exp_series_apply_eq NormedSpace.expSeries_apply_eq

theorem expSeries_apply_eq' (x : ğ”¸) :
    (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) = fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  funext (expSeries_apply_eq x)
#align exp_series_apply_eq' NormedSpace.expSeries_apply_eq'

theorem expSeries_sum_eq (x : ğ”¸) : (expSeries ğ•‚ ğ”¸).sum x = âˆ‘' n : â„•, (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  tsum_congr fun n => expSeries_apply_eq x n
#align exp_series_sum_eq NormedSpace.expSeries_sum_eq

theorem exp_eq_tsum : exp ğ•‚ = fun x : ğ”¸ => âˆ‘' n : â„•, (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  funext expSeries_sum_eq
#align exp_eq_tsum NormedSpace.exp_eq_tsum

theorem expSeries_apply_zero (n : â„•) :
    (expSeries ğ•‚ ğ”¸ n fun _ => (0 : ğ”¸)) = Pi.single (f := fun _ => ğ”¸) 0 1 n := by
  rw [expSeries_apply_eq]
  cases' n with n
  Â· rw [pow_zero, Nat.factorial_zero, Nat.cast_one, inv_one, one_smul, Pi.single_eq_same]
  Â· rw [zero_pow (Nat.succ_ne_zero _), smul_zero, Pi.single_eq_of_ne n.succ_ne_zero]
#align exp_series_apply_zero NormedSpace.expSeries_apply_zero

@[simp]
theorem exp_zero : exp ğ•‚ (0 : ğ”¸) = 1 := by
  simp_rw [exp_eq_tsum, â† expSeries_apply_eq, expSeries_apply_zero, tsum_pi_single]
#align exp_zero NormedSpace.exp_zero

@[simp]
theorem exp_op [T2Space ğ”¸] (x : ğ”¸) : exp ğ•‚ (MulOpposite.op x) = MulOpposite.op (exp ğ•‚ x) := by
  simp_rw [exp, expSeries_sum_eq, â† MulOpposite.op_pow, â† MulOpposite.op_smul, tsum_op]
#align exp_op NormedSpace.exp_op

@[simp]
theorem exp_unop [T2Space ğ”¸] (x : ğ”¸áµáµ’áµ–) :
    exp ğ•‚ (MulOpposite.unop x) = MulOpposite.unop (exp ğ•‚ x) := by
  simp_rw [exp, expSeries_sum_eq, â† MulOpposite.unop_pow, â† MulOpposite.unop_smul, tsum_unop]
#align exp_unop NormedSpace.exp_unop

theorem star_exp [T2Space ğ”¸] [StarRing ğ”¸] [ContinuousStar ğ”¸] (x : ğ”¸) :
    star (exp ğ•‚ x) = exp ğ•‚ (star x) := by
  simp_rw [exp_eq_tsum, â† star_pow, â† star_inv_natCast_smul, â† tsum_star]
#align star_exp NormedSpace.star_exp

variable (ğ•‚)

theorem _root_.IsSelfAdjoint.exp [T2Space ğ”¸] [StarRing ğ”¸] [ContinuousStar ğ”¸] {x : ğ”¸}
    (h : IsSelfAdjoint x) : IsSelfAdjoint (exp ğ•‚ x) :=
  (star_exp x).trans <| h.symm â–¸ rfl
#align is_self_adjoint.exp IsSelfAdjoint.exp

theorem _root_.Commute.exp_right [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) :
    Commute x (exp ğ•‚ y) := by
  rw [exp_eq_tsum]
  exact Commute.tsum_right x fun n => (h.pow_right n).smul_right _
#align commute.exp_right Commute.exp_right

theorem _root_.Commute.exp_left [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) : Commute (exp ğ•‚ x) y :=
  (h.symm.exp_right ğ•‚).symm
#align commute.exp_left Commute.exp_left

theorem _root_.Commute.exp [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) : Commute (exp ğ•‚ x) (exp ğ•‚ y) :=
  (h.exp_left _).exp_right _
#align commute.exp Commute.exp

end TopologicalAlgebra

section TopologicalDivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [Field ğ•‚] [DivisionRing ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸]
  [TopologicalRing ğ”¸]

theorem expSeries_apply_eq_div (x : ğ”¸) (n : â„•) : (expSeries ğ•‚ ğ”¸ n fun _ => x) = x ^ n / n ! := by
  rw [div_eq_mul_inv, â† (Nat.cast_commute n ! (x ^ n)).inv_leftâ‚€.eq, â† smul_eq_mul,
    expSeries_apply_eq, inv_natCast_smul_eq ğ•‚ ğ”¸]
#align exp_series_apply_eq_div NormedSpace.expSeries_apply_eq_div

theorem expSeries_apply_eq_div' (x : ğ”¸) :
    (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) = fun n => x ^ n / n ! :=
  funext (expSeries_apply_eq_div x)
#align exp_series_apply_eq_div' NormedSpace.expSeries_apply_eq_div'

theorem expSeries_sum_eq_div (x : ğ”¸) : (expSeries ğ•‚ ğ”¸).sum x = âˆ‘' n : â„•, x ^ n / n ! :=
  tsum_congr (expSeries_apply_eq_div x)
#align exp_series_sum_eq_div NormedSpace.expSeries_sum_eq_div

theorem exp_eq_tsum_div : exp ğ•‚ = fun x : ğ”¸ => âˆ‘' n : â„•, x ^ n / n ! :=
  funext expSeries_sum_eq_div
#align exp_eq_tsum_div NormedSpace.exp_eq_tsum_div

end TopologicalDivisionAlgebra

section Normed

section AnyFieldAnyAlgebra

variable {ğ•‚ ğ”¸ ğ”¹ : Type*} [NontriviallyNormedField ğ•‚]
variable [NormedRing ğ”¸] [NormedRing ğ”¹] [NormedAlgebra ğ•‚ ğ”¸] [NormedAlgebra ğ•‚ ğ”¹]

theorem norm_expSeries_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => â€–expSeries ğ•‚ ğ”¸ n fun _ => xâ€– :=
  (expSeries ğ•‚ ğ”¸).summable_norm_apply hx
#align norm_exp_series_summable_of_mem_ball NormedSpace.norm_expSeries_summable_of_mem_ball

theorem norm_expSeries_summable_of_mem_ball' (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => â€–(n !â»Â¹ : ğ•‚) â€¢ x ^ nâ€– := by
  change Summable (norm âˆ˜ _)
  rw [â† expSeries_apply_eq']
  exact norm_expSeries_summable_of_mem_ball x hx
#align norm_exp_series_summable_of_mem_ball' NormedSpace.norm_expSeries_summable_of_mem_ball'

section CompleteAlgebra

variable [CompleteSpace ğ”¸]

theorem expSeries_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => expSeries ğ•‚ ğ”¸ n fun _ => x :=
  (norm_expSeries_summable_of_mem_ball x hx).of_norm
#align exp_series_summable_of_mem_ball NormedSpace.expSeries_summable_of_mem_ball

theorem expSeries_summable_of_mem_ball' (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  (norm_expSeries_summable_of_mem_ball' x hx).of_norm
#align exp_series_summable_of_mem_ball' NormedSpace.expSeries_summable_of_mem_ball'

theorem expSeries_hasSum_exp_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) (exp ğ•‚ x) :=
  FormalMultilinearSeries.hasSum (expSeries ğ•‚ ğ”¸) hx
#align exp_series_has_sum_exp_of_mem_ball NormedSpace.expSeries_hasSum_exp_of_mem_ball

theorem expSeries_hasSum_exp_of_mem_ball' (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n) (exp ğ•‚ x) := by
  rw [â† expSeries_apply_eq']
  exact expSeries_hasSum_exp_of_mem_ball x hx
#align exp_series_has_sum_exp_of_mem_ball' NormedSpace.expSeries_hasSum_exp_of_mem_ball'

theorem hasFPowerSeriesOnBall_exp_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasFPowerSeriesOnBall (exp ğ•‚) (expSeries ğ•‚ ğ”¸) 0 (expSeries ğ•‚ ğ”¸).radius :=
  (expSeries ğ•‚ ğ”¸).hasFPowerSeriesOnBall h
#align has_fpower_series_on_ball_exp_of_radius_pos NormedSpace.hasFPowerSeriesOnBall_exp_of_radius_pos

theorem hasFPowerSeriesAt_exp_zero_of_radius_pos (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasFPowerSeriesAt (exp ğ•‚) (expSeries ğ•‚ ğ”¸) 0 :=
  (hasFPowerSeriesOnBall_exp_of_radius_pos h).hasFPowerSeriesAt
#align has_fpower_series_at_exp_zero_of_radius_pos NormedSpace.hasFPowerSeriesAt_exp_zero_of_radius_pos

theorem continuousOn_exp : ContinuousOn (exp ğ•‚ : ğ”¸ â†’ ğ”¸) (EMetric.ball 0 (expSeries ğ•‚ ğ”¸).radius) :=
  FormalMultilinearSeries.continuousOn
#align continuous_on_exp NormedSpace.continuousOn_exp

theorem analyticAt_exp_of_mem_ball (x : ğ”¸) (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    AnalyticAt ğ•‚ (exp ğ•‚) x := by
  by_cases h : (expSeries ğ•‚ ğ”¸).radius = 0
  Â· rw [h] at hx; exact (ENNReal.not_lt_zero hx).elim
  Â· have h := pos_iff_ne_zero.mpr h
    exact (hasFPowerSeriesOnBall_exp_of_radius_pos h).analyticAt_of_mem hx
#align analytic_at_exp_of_mem_ball NormedSpace.analyticAt_exp_of_mem_ball

/-- In a Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero, if `x` and `y` are
in the disk of convergence and commute, then `exp ğ•‚ (x + y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
theorem exp_add_of_commute_of_mem_ball [CharZero ğ•‚] {x y : ğ”¸} (hxy : Commute x y)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius)
    (hy : y âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp ğ•‚ (x + y) = exp ğ•‚ x * exp ğ•‚ y := by
  rw [exp_eq_tsum,
    tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm
      (norm_expSeries_summable_of_mem_ball' x hx) (norm_expSeries_summable_of_mem_ball' y hy)]
  dsimp only
  conv_lhs =>
    congr
    ext
    rw [hxy.add_pow' _, Finset.smul_sum]
  refine tsum_congr fun n => Finset.sum_congr rfl fun kl hkl => ?_
  rw [nsmul_eq_smul_cast ğ•‚, smul_smul, smul_mul_smul, â† Finset.mem_antidiagonal.mp hkl,
    Nat.cast_add_choose, Finset.mem_antidiagonal.mp hkl]
  congr 1
  have : (n ! : ğ•‚) â‰  0 := Nat.cast_ne_zero.mpr n.factorial_ne_zero
  field_simp [this]
#align exp_add_of_commute_of_mem_ball NormedSpace.exp_add_of_commute_of_mem_ball

/-- `exp ğ•‚ x` has explicit two-sided inverse `exp ğ•‚ (-x)`. -/
noncomputable def invertibleExpOfMemBall [CharZero ğ•‚] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : Invertible (exp ğ•‚ x) where
  invOf := exp ğ•‚ (-x)
  invOf_mul_self := by
    have hnx : -x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius := by
      rw [EMetric.mem_ball, â† neg_zero, edist_neg_neg]
      exact hx
    rw [â† exp_add_of_commute_of_mem_ball (Commute.neg_left <| Commute.refl x) hnx hx, neg_add_self,
      exp_zero]
  mul_invOf_self := by
    have hnx : -x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius := by
      rw [EMetric.mem_ball, â† neg_zero, edist_neg_neg]
      exact hx
    rw [â† exp_add_of_commute_of_mem_ball (Commute.neg_right <| Commute.refl x) hx hnx, add_neg_self,
      exp_zero]
#align invertible_exp_of_mem_ball NormedSpace.invertibleExpOfMemBall

theorem isUnit_exp_of_mem_ball [CharZero ğ•‚] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : IsUnit (exp ğ•‚ x) :=
  @isUnit_of_invertible _ _ _ (invertibleExpOfMemBall hx)
#align is_unit_exp_of_mem_ball NormedSpace.isUnit_exp_of_mem_ball

theorem invOf_exp_of_mem_ball [CharZero ğ•‚] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) [Invertible (exp ğ•‚ x)] :
    â…Ÿ (exp ğ•‚ x) = exp ğ•‚ (-x) := by
  letI := invertibleExpOfMemBall hx; convert (rfl : â…Ÿ (exp ğ•‚ x) = _)
#align inv_of_exp_of_mem_ball NormedSpace.invOf_exp_of_mem_ball

/-- Any continuous ring homomorphism commutes with `exp`. -/
theorem map_exp_of_mem_ball {F} [FunLike F ğ”¸ ğ”¹] [RingHomClass F ğ”¸ ğ”¹] (f : F) (hf : Continuous f)
    (x : ğ”¸) (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    f (exp ğ•‚ x) = exp ğ•‚ (f x) := by
  rw [exp_eq_tsum, exp_eq_tsum]
  refine ((expSeries_summable_of_mem_ball' _ hx).hasSum.map f hf).tsum_eq.symm.trans ?_
  dsimp only [Function.comp_def]
  simp_rw [map_inv_natCast_smul f ğ•‚ ğ•‚, map_pow]
#align map_exp_of_mem_ball NormedSpace.map_exp_of_mem_ball

end CompleteAlgebra

theorem algebraMap_exp_comm_of_mem_ball [CompleteSpace ğ•‚] (x : ğ•‚)
    (hx : x âˆˆ EMetric.ball (0 : ğ•‚) (expSeries ğ•‚ ğ•‚).radius) :
    algebraMap ğ•‚ ğ”¸ (exp ğ•‚ x) = exp ğ•‚ (algebraMap ğ•‚ ğ”¸ x) :=
  map_exp_of_mem_ball _ (continuous_algebraMap ğ•‚ ğ”¸) _ hx
#align algebra_map_exp_comm_of_mem_ball NormedSpace.algebraMap_exp_comm_of_mem_ball

end AnyFieldAnyAlgebra

section AnyFieldDivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable (ğ•‚)

theorem norm_expSeries_div_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => â€–x ^ n / (n ! : ğ”¸)â€– := by
  change Summable (norm âˆ˜ _)
  rw [â† expSeries_apply_eq_div' (ğ•‚ := ğ•‚) x]
  exact norm_expSeries_summable_of_mem_ball x hx
#align norm_exp_series_div_summable_of_mem_ball NormedSpace.norm_expSeries_div_summable_of_mem_ball

theorem expSeries_div_summable_of_mem_ball [CompleteSpace ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : Summable fun n => x ^ n / n ! :=
  (norm_expSeries_div_summable_of_mem_ball ğ•‚ x hx).of_norm
#align exp_series_div_summable_of_mem_ball NormedSpace.expSeries_div_summable_of_mem_ball

theorem expSeries_div_hasSum_exp_of_mem_ball [CompleteSpace ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => x ^ n / n !) (exp ğ•‚ x) := by
  rw [â† expSeries_apply_eq_div' (ğ•‚ := ğ•‚) x]
  exact expSeries_hasSum_exp_of_mem_ball x hx
#align exp_series_div_has_sum_exp_of_mem_ball NormedSpace.expSeries_div_hasSum_exp_of_mem_ball

variable {ğ•‚}

theorem exp_neg_of_mem_ball [CharZero ğ•‚] [CompleteSpace ğ”¸] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹ :=
  letI := invertibleExpOfMemBall hx
  invOf_eq_inv (exp ğ•‚ x)
#align exp_neg_of_mem_ball NormedSpace.exp_neg_of_mem_ball

end AnyFieldDivisionAlgebra

section AnyFieldCommAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  [CompleteSpace ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero,
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)` for all `x`, `y` in the disk of convergence. -/
theorem exp_add_of_mem_ball [CharZero ğ•‚] {x y : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius)
    (hy : y âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp ğ•‚ (x + y) = exp ğ•‚ x * exp ğ•‚ y :=
  exp_add_of_commute_of_mem_ball (Commute.all x y) hx hy
#align exp_add_of_mem_ball NormedSpace.exp_add_of_mem_ball

end AnyFieldCommAlgebra

section RCLike

section AnyAlgebra

variable (ğ•‚ ğ”¸ ğ”¹ : Type*) [RCLike ğ•‚] [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable [NormedRing ğ”¹] [NormedAlgebra ğ•‚ ğ”¹]

/-- In a normed algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, the series defining the exponential map
has an infinite radius of convergence. -/
theorem expSeries_radius_eq_top : (expSeries ğ•‚ ğ”¸).radius = âˆ := by
  refine (expSeries ğ•‚ ğ”¸).radius_eq_top_of_summable_norm fun r => ?_
  refine .of_norm_bounded_eventually _ (Real.summable_pow_div_factorial r) ?_
  filter_upwards [eventually_cofinite_ne 0] with n hn
  rw [norm_mul, norm_norm (expSeries ğ•‚ ğ”¸ n), expSeries]
  rw [norm_smul (n ! : ğ•‚)â»Â¹ (ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸)]
  -- Porting note: Lean needed this to be explicit for some reason
  rw [norm_inv, norm_pow, NNReal.norm_eq, norm_natCast, mul_comm, â† mul_assoc, â† div_eq_mul_inv]
  have : â€–ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸â€– â‰¤ 1 :=
    norm_mkPiAlgebraFin_le_of_pos (Ei := fun _ => ğ”¸) (Nat.pos_of_ne_zero hn)
  exact mul_le_of_le_one_right (div_nonneg (pow_nonneg r.coe_nonneg n) n !.cast_nonneg) this
#align exp_series_radius_eq_top NormedSpace.expSeries_radius_eq_top

theorem expSeries_radius_pos : 0 < (expSeries ğ•‚ ğ”¸).radius := by
  rw [expSeries_radius_eq_top]
  exact WithTop.zero_lt_top
#align exp_series_radius_pos NormedSpace.expSeries_radius_pos

variable {ğ•‚ ğ”¸ ğ”¹}

theorem norm_expSeries_summable (x : ğ”¸) : Summable fun n => â€–expSeries ğ•‚ ğ”¸ n fun _ => xâ€– :=
  norm_expSeries_summable_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align norm_exp_series_summable NormedSpace.norm_expSeries_summable

theorem norm_expSeries_summable' (x : ğ”¸) : Summable fun n => â€–(n !â»Â¹ : ğ•‚) â€¢ x ^ nâ€– :=
  norm_expSeries_summable_of_mem_ball' x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align norm_exp_series_summable' NormedSpace.norm_expSeries_summable'

section CompleteAlgebra

variable [CompleteSpace ğ”¸]

theorem expSeries_summable (x : ğ”¸) : Summable fun n => expSeries ğ•‚ ğ”¸ n fun _ => x :=
  (norm_expSeries_summable x).of_norm
#align exp_series_summable NormedSpace.expSeries_summable

theorem expSeries_summable' (x : ğ”¸) : Summable fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  (norm_expSeries_summable' x).of_norm
#align exp_series_summable' NormedSpace.expSeries_summable'

theorem expSeries_hasSum_exp (x : ğ”¸) : HasSum (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) (exp ğ•‚ x) :=
  expSeries_hasSum_exp_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_series_has_sum_exp NormedSpace.expSeries_hasSum_exp

theorem exp_series_hasSum_exp' (x : ğ”¸) : HasSum (fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n) (exp ğ•‚ x) :=
  expSeries_hasSum_exp_of_mem_ball' x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_series_has_sum_exp' NormedSpace.exp_series_hasSum_exp'

theorem exp_hasFPowerSeriesOnBall : HasFPowerSeriesOnBall (exp ğ•‚) (expSeries ğ•‚ ğ”¸) 0 âˆ :=
  expSeries_radius_eq_top ğ•‚ ğ”¸ â–¸ hasFPowerSeriesOnBall_exp_of_radius_pos (expSeries_radius_pos _ _)
#align exp_has_fpower_series_on_ball NormedSpace.exp_hasFPowerSeriesOnBall

theorem exp_hasFPowerSeriesAt_zero : HasFPowerSeriesAt (exp ğ•‚) (expSeries ğ•‚ ğ”¸) 0 :=
  exp_hasFPowerSeriesOnBall.hasFPowerSeriesAt
#align exp_has_fpower_series_at_zero NormedSpace.exp_hasFPowerSeriesAt_zero

@[continuity]
theorem exp_continuous : Continuous (exp ğ•‚ : ğ”¸ â†’ ğ”¸) := by
  rw [continuous_iff_continuousOn_univ, â† Metric.eball_top_eq_univ (0 : ğ”¸), â†
    expSeries_radius_eq_top ğ•‚ ğ”¸]
  exact continuousOn_exp
#align exp_continuous NormedSpace.exp_continuous

open Topology in
lemma _root_.Filter.Tendsto.exp {Î± : Type*} {l : Filter Î±} {f : Î± â†’ ğ”¸} {a : ğ”¸}
    (hf : Tendsto f l (ğ“ a)) :
    Tendsto (fun x => exp ğ•‚ (f x)) l (ğ“ (exp ğ•‚ a)) :=
  (exp_continuous.tendsto _).comp hf

theorem exp_analytic (x : ğ”¸) : AnalyticAt ğ•‚ (exp ğ•‚) x :=
  analyticAt_exp_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_analytic NormedSpace.exp_analytic

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if `x` and `y` commute, then
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
theorem exp_add_of_commute {x y : ğ”¸} (hxy : Commute x y) : exp ğ•‚ (x + y) = exp ğ•‚ x * exp ğ•‚ y :=
  exp_add_of_commute_of_mem_ball hxy ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_add_of_commute NormedSpace.exp_add_of_commute

section

variable (ğ•‚)

/-- `exp ğ•‚ x` has explicit two-sided inverse `exp ğ•‚ (-x)`. -/
noncomputable def invertibleExp (x : ğ”¸) : Invertible (exp ğ•‚ x) :=
  invertibleExpOfMemBall <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _
#align invertible_exp NormedSpace.invertibleExp

theorem isUnit_exp (x : ğ”¸) : IsUnit (exp ğ•‚ x) :=
  isUnit_exp_of_mem_ball <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _
#align is_unit_exp NormedSpace.isUnit_exp

theorem invOf_exp (x : ğ”¸) [Invertible (exp ğ•‚ x)] : â…Ÿ (exp ğ•‚ x) = exp ğ•‚ (-x) :=
  invOf_exp_of_mem_ball <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _
#align inv_of_exp NormedSpace.invOf_exp

theorem _root_.Ring.inverse_exp (x : ğ”¸) : Ring.inverse (exp ğ•‚ x) = exp ğ•‚ (-x) :=
  letI := invertibleExp ğ•‚ x
  Ring.inverse_invertible _
#align ring.inverse_exp Ring.inverse_exp

theorem exp_mem_unitary_of_mem_skewAdjoint [StarRing ğ”¸] [ContinuousStar ğ”¸] {x : ğ”¸}
    (h : x âˆˆ skewAdjoint ğ”¸) : exp ğ•‚ x âˆˆ unitary ğ”¸ := by
  rw [unitary.mem_iff, star_exp, skewAdjoint.mem_iff.mp h, â†
    exp_add_of_commute (Commute.refl x).neg_left, â† exp_add_of_commute (Commute.refl x).neg_right,
    add_left_neg, add_right_neg, exp_zero, and_self_iff]
#align exp_mem_unitary_of_mem_skew_adjoint NormedSpace.exp_mem_unitary_of_mem_skewAdjoint

end

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if a family of elements `f i` mutually
commute then `exp ğ•‚ (âˆ‘ i, f i) = âˆ i, exp ğ•‚ (f i)`. -/
theorem exp_sum_of_commute {Î¹} (s : Finset Î¹) (f : Î¹ â†’ ğ”¸)
    (h : (s : Set Î¹).Pairwise fun i j => Commute (f i) (f j)) :
    exp ğ•‚ (âˆ‘ i âˆˆ s, f i) =
      s.noncommProd (fun i => exp ğ•‚ (f i)) fun i hi j hj _ => (h.of_refl hi hj).exp ğ•‚ := by
  classical
    induction' s using Finset.induction_on with a s ha ih
    Â· simp
    rw [Finset.noncommProd_insert_of_not_mem _ _ _ _ ha, Finset.sum_insert ha, exp_add_of_commute,
      ih (h.mono <| Finset.subset_insert _ _)]
    refine Commute.sum_right _ _ _ fun i hi => ?_
    exact h.of_refl (Finset.mem_insert_self _ _) (Finset.mem_insert_of_mem hi)
#align exp_sum_of_commute NormedSpace.exp_sum_of_commute

theorem exp_nsmul (n : â„•) (x : ğ”¸) : exp ğ•‚ (n â€¢ x) = exp ğ•‚ x ^ n := by
  induction' n with n ih
  Â· rw [zero_smul, pow_zero, exp_zero]
  Â· rw [succ_nsmul, pow_succ, exp_add_of_commute ((Commute.refl x).smul_left n), ih]
#align exp_nsmul NormedSpace.exp_nsmul

variable (ğ•‚)

/-- Any continuous ring homomorphism commutes with `NormedSpace.exp`. -/
theorem map_exp {F} [FunLike F ğ”¸ ğ”¹] [RingHomClass F ğ”¸ ğ”¹] (f : F) (hf : Continuous f) (x : ğ”¸) :
    f (exp ğ•‚ x) = exp ğ•‚ (f x) :=
  map_exp_of_mem_ball f hf x <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _
#align map_exp NormedSpace.map_exp

theorem exp_smul {G} [Monoid G] [MulSemiringAction G ğ”¸] [ContinuousConstSMul G ğ”¸] (g : G) (x : ğ”¸) :
    exp ğ•‚ (g â€¢ x) = g â€¢ exp ğ•‚ x :=
  (map_exp ğ•‚ (MulSemiringAction.toRingHom G ğ”¸ g) (continuous_const_smul g) x).symm
#align exp_smul NormedSpace.exp_smul

theorem exp_units_conj (y : ğ”¸Ë£) (x : ğ”¸) : exp ğ•‚ (y * x * â†‘yâ»Â¹ : ğ”¸) = y * exp ğ•‚ x * â†‘yâ»Â¹ :=
  exp_smul _ (ConjAct.toConjAct y) x
#align exp_units_conj NormedSpace.exp_units_conj

theorem exp_units_conj' (y : ğ”¸Ë£) (x : ğ”¸) : exp ğ•‚ (â†‘yâ»Â¹ * x * y) = â†‘yâ»Â¹ * exp ğ•‚ x * y :=
  exp_units_conj _ _ _
#align exp_units_conj' NormedSpace.exp_units_conj'

@[simp]
theorem _root_.Prod.fst_exp [CompleteSpace ğ”¹] (x : ğ”¸ Ã— ğ”¹) : (exp ğ•‚ x).fst = exp ğ•‚ x.fst :=
  map_exp _ (RingHom.fst ğ”¸ ğ”¹) continuous_fst x
#align prod.fst_exp Prod.fst_exp

@[simp]
theorem _root_.Prod.snd_exp [CompleteSpace ğ”¹] (x : ğ”¸ Ã— ğ”¹) : (exp ğ•‚ x).snd = exp ğ•‚ x.snd :=
  map_exp _ (RingHom.snd ğ”¸ ğ”¹) continuous_snd x
#align prod.snd_exp Prod.snd_exp

@[simp]
theorem _root_.Pi.exp_apply {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Finite Î¹] [âˆ€ i, NormedRing (ğ”¸ i)]
    [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)] [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i) (i : Î¹) :
    exp ğ•‚ x i = exp ğ•‚ (x i) :=
  let âŸ¨_âŸ© := nonempty_fintype Î¹
  map_exp _ (Pi.evalRingHom ğ”¸ i) (continuous_apply _) x
#align pi.exp_apply Pi.exp_apply

theorem _root_.Pi.exp_def {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Finite Î¹] [âˆ€ i, NormedRing (ğ”¸ i)]
    [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)] [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i) :
    exp ğ•‚ x = fun i => exp ğ•‚ (x i) :=
  funext <| Pi.exp_apply ğ•‚ x
#align pi.exp_def Pi.exp_def

theorem _root_.Function.update_exp {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Finite Î¹] [DecidableEq Î¹]
    [âˆ€ i, NormedRing (ğ”¸ i)] [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)] [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i)
    (j : Î¹) (xj : ğ”¸ j) :
    Function.update (exp ğ•‚ x) j (exp ğ•‚ xj) = exp ğ•‚ (Function.update x j xj) := by
  ext i
  simp_rw [Pi.exp_def]
  exact (Function.apply_update (fun i => exp ğ•‚) x j xj i).symm
#align function.update_exp Function.update_exp

end CompleteAlgebra

theorem algebraMap_exp_comm (x : ğ•‚) : algebraMap ğ•‚ ğ”¸ (exp ğ•‚ x) = exp ğ•‚ (algebraMap ğ•‚ ğ”¸ x) :=
  algebraMap_exp_comm_of_mem_ball x <| (expSeries_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _
#align algebra_map_exp_comm NormedSpace.algebraMap_exp_comm

end AnyAlgebra

section DivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable (ğ•‚)

theorem norm_expSeries_div_summable (x : ğ”¸) : Summable fun n => â€–(x ^ n / n ! : ğ”¸)â€– :=
  norm_expSeries_div_summable_of_mem_ball ğ•‚ x
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align norm_exp_series_div_summable NormedSpace.norm_expSeries_div_summable

variable [CompleteSpace ğ”¸]

theorem expSeries_div_summable (x : ğ”¸) : Summable fun n => x ^ n / n ! :=
  (norm_expSeries_div_summable ğ•‚ x).of_norm
#align exp_series_div_summable NormedSpace.expSeries_div_summable

theorem expSeries_div_hasSum_exp (x : ğ”¸) : HasSum (fun n => x ^ n / n !) (exp ğ•‚ x) :=
  expSeries_div_hasSum_exp_of_mem_ball ğ•‚ x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_series_div_has_sum_exp NormedSpace.expSeries_div_hasSum_exp

variable {ğ•‚}

theorem exp_neg (x : ğ”¸) : exp ğ•‚ (-x) = (exp ğ•‚ x)â»Â¹ :=
  exp_neg_of_mem_ball <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _
#align exp_neg NormedSpace.exp_neg

theorem exp_zsmul (z : â„¤) (x : ğ”¸) : exp ğ•‚ (z â€¢ x) = exp ğ•‚ x ^ z := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· rw [zpow_natCast, natCast_zsmul, exp_nsmul]
  Â· rw [zpow_neg, zpow_natCast, neg_smul, exp_neg, natCast_zsmul, exp_nsmul]
#align exp_zsmul NormedSpace.exp_zsmul

theorem exp_conj (y : ğ”¸) (x : ğ”¸) (hy : y â‰  0) : exp ğ•‚ (y * x * yâ»Â¹) = y * exp ğ•‚ x * yâ»Â¹ :=
  exp_units_conj _ (Units.mk0 y hy) x
#align exp_conj NormedSpace.exp_conj

theorem exp_conj' (y : ğ”¸) (x : ğ”¸) (hy : y â‰  0) : exp ğ•‚ (yâ»Â¹ * x * y) = yâ»Â¹ * exp ğ•‚ x * y :=
  exp_units_conj' _ (Units.mk0 y hy) x
#align exp_conj' NormedSpace.exp_conj'

end DivisionAlgebra

section CommAlgebra

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`,
`exp ğ•‚ (x+y) = (exp ğ•‚ x) * (exp ğ•‚ y)`. -/
theorem exp_add {x y : ğ”¸} : exp ğ•‚ (x + y) = exp ğ•‚ x * exp ğ•‚ y :=
  exp_add_of_mem_ball ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
#align exp_add NormedSpace.exp_add

/-- A version of `NormedSpace.exp_sum_of_commute` for a commutative Banach-algebra. -/
theorem exp_sum {Î¹} (s : Finset Î¹) (f : Î¹ â†’ ğ”¸) : exp ğ•‚ (âˆ‘ i âˆˆ s, f i) = âˆ i âˆˆ s, exp ğ•‚ (f i) := by
  rw [exp_sum_of_commute, Finset.noncommProd_eq_prod]
  exact fun i _hi j _hj _ => Commute.all _ _
#align exp_sum NormedSpace.exp_sum

end CommAlgebra

end RCLike

end Normed

section ScalarTower

variable (ğ•‚ ğ•‚' ğ”¸ : Type*) [Field ğ•‚] [Field ğ•‚'] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [Algebra ğ•‚' ğ”¸]
  [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
`expSeries` on `ğ”¸`. -/
theorem expSeries_eq_expSeries (n : â„•) (x : ğ”¸) :
    (expSeries ğ•‚ ğ”¸ n fun _ => x) = expSeries ğ•‚' ğ”¸ n fun _ => x := by
  rw [expSeries_apply_eq, expSeries_apply_eq, inv_natCast_smul_eq ğ•‚ ğ•‚']
#align exp_series_eq_exp_series NormedSpace.expSeries_eq_expSeries

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
exponential function on `ğ”¸`. -/
theorem exp_eq_exp : (exp ğ•‚ : ğ”¸ â†’ ğ”¸) = exp ğ•‚' := by
  ext x
  rw [exp, exp]
  refine tsum_congr fun n => ?_
  rw [expSeries_eq_expSeries ğ•‚ ğ•‚' ğ”¸ n x]
#align exp_eq_exp NormedSpace.exp_eq_exp

theorem exp_â„_â„‚_eq_exp_â„‚_â„‚ : (exp â„ : â„‚ â†’ â„‚) = exp â„‚ :=
  exp_eq_exp â„ â„‚ â„‚
#align exp_â„_â„‚_eq_exp_â„‚_â„‚ NormedSpace.exp_â„_â„‚_eq_exp_â„‚_â„‚

/-- A version of `Complex.ofReal_exp` for `NormedSpace.exp` instead of `Complex.exp` -/
@[simp, norm_cast]
theorem of_real_exp_â„_â„ (r : â„) : â†‘(exp â„ r) = exp â„‚ (r : â„‚) :=
  (map_exp â„ (algebraMap â„ â„‚) (continuous_algebraMap _ _) r).trans (congr_fun exp_â„_â„‚_eq_exp_â„‚_â„‚ _)
#align of_real_exp_â„_â„ NormedSpace.of_real_exp_â„_â„

end ScalarTower
