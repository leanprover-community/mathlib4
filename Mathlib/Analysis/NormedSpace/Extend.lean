/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/
import Mathlib.Algebra.Algebra.RestrictScalars
import Mathlib.Analysis.NormedSpace.OperatorNorm.Basic
import Mathlib.Analysis.RCLike.Basic

#align_import analysis.normed_space.extend from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Extending a continuous `â„`-linear map to a continuous `ğ•œ`-linear map

In this file we provide a way to extend a continuous `â„`-linear map to a continuous `ğ•œ`-linear map
in a way that bounds the norm by the norm of the original map, when `ğ•œ` is either `â„` (the
extension is trivial) or `â„‚`. We formulate the extension uniformly, by assuming `RCLike ğ•œ`.

We motivate the form of the extension as follows. Note that `fc : F â†’â‚—[ğ•œ] ğ•œ` is determined fully by
`re fc`: for all `x : F`, `fc (I â€¢ x) = I * fc x`, so `im (fc x) = -re (fc (I â€¢ x))`. Therefore,
given an `fr : F â†’â‚—[â„] â„`, we define `fc x = fr x - fr (I â€¢ x) * I`.

## Main definitions

* `LinearMap.extendToğ•œ`
* `ContinuousLinearMap.extendToğ•œ`

## Implementation details

For convenience, the main definitions above operate in terms of `RestrictScalars â„ ğ•œ F`.
Alternate forms which operate on `[IsScalarTower â„ ğ•œ F]` instead are provided with a primed name.

-/


open RCLike

open ComplexConjugate

variable {ğ•œ : Type*} [RCLike ğ•œ] {F : Type*} [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

namespace LinearMap

variable [Module â„ F] [IsScalarTower â„ ğ•œ F]

/-- Extend `fr : F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ` in a way that will also be continuous and have its norm
bounded by `â€–frâ€–` if `fr` is continuous. -/
noncomputable def extendToğ•œ' (fr : F â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ := by
  let fc : F â†’ ğ•œ := fun x => (fr x : ğ•œ) - (I : ğ•œ) * fr ((I : ğ•œ) â€¢ x)
  have add : âˆ€ x y : F, fc (x + y) = fc x + fc y := by
    intro x y
    simp only [fc, smul_add, LinearMap.map_add, ofReal_add]
    rw [mul_add]
    abel
  have A : âˆ€ (c : â„) (x : F), (fr ((c : ğ•œ) â€¢ x) : ğ•œ) = (c : ğ•œ) * (fr x : ğ•œ) := by
    intro c x
    rw [â† ofReal_mul]
    congr 1
    rw [RCLike.ofReal_alg, smul_assoc, fr.map_smul, Algebra.id.smul_eq_mul, one_smul]
  have smul_â„ : âˆ€ (c : â„) (x : F), fc ((c : ğ•œ) â€¢ x) = (c : ğ•œ) * fc x := by
    intro c x
    dsimp only [fc]
    rw [A c x, smul_smul, mul_comm I (c : ğ•œ), â† smul_smul, A, mul_sub]
    ring
  have smul_I : âˆ€ x : F, fc ((I : ğ•œ) â€¢ x) = (I : ğ•œ) * fc x := by
    intro x
    dsimp only [fc]
    cases' @I_mul_I_ax ğ•œ _ with h h
    Â· simp [h]
    rw [mul_sub, â† mul_assoc, smul_smul, h]
    simp only [neg_mul, LinearMap.map_neg, one_mul, one_smul, mul_neg, ofReal_neg, neg_smul,
      sub_neg_eq_add, add_comm]
  have smul_ğ•œ : âˆ€ (c : ğ•œ) (x : F), fc (c â€¢ x) = c â€¢ fc x := by
    intro c x
    rw [â† re_add_im c, add_smul, add_smul, add, smul_â„, â† smul_smul, smul_â„, smul_I, â† mul_assoc]
    rfl
  exact
    { toFun := fc
      map_add' := add
      map_smul' := smul_ğ•œ }
#align linear_map.extend_to_ğ•œ' LinearMap.extendToğ•œ'

theorem extendToğ•œ'_apply (fr : F â†’â‚—[â„] â„) (x : F) :
    fr.extendToğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl
#align linear_map.extend_to_ğ•œ'_apply LinearMap.extendToğ•œ'_apply

@[simp]
theorem extendToğ•œ'_apply_re (fr : F â†’â‚—[â„] â„) (x : F) : re (fr.extendToğ•œ' x : ğ•œ) = fr x := by
  simp only [extendToğ•œ'_apply, map_sub, zero_mul, mul_zero, sub_zero,
    rclike_simps]
#align linear_map.extend_to_ğ•œ'_apply_re LinearMap.extendToğ•œ'_apply_re

theorem norm_extendToğ•œ'_apply_sq (fr : F â†’â‚—[â„] â„) (x : F) :
    â€–(fr.extendToğ•œ' x : ğ•œ)â€– ^ 2 = fr (conj (fr.extendToğ•œ' x : ğ•œ) â€¢ x) :=
  calc
    â€–(fr.extendToğ•œ' x : ğ•œ)â€– ^ 2 = re (conj (fr.extendToğ•œ' x) * fr.extendToğ•œ' x : ğ•œ) := by
      rw [RCLike.conj_mul, â† ofReal_pow, ofReal_re]
    _ = fr (conj (fr.extendToğ•œ' x : ğ•œ) â€¢ x) := by
      rw [â† smul_eq_mul, â† map_smul, extendToğ•œ'_apply_re]
#align linear_map.norm_extend_to_ğ•œ'_apply_sq LinearMap.norm_extendToğ•œ'_apply_sq

end LinearMap

namespace ContinuousLinearMap

variable [NormedSpace â„ F] [IsScalarTower â„ ğ•œ F]

/-- The norm of the extension is bounded by `â€–frâ€–`. -/
theorem norm_extendToğ•œ'_bound (fr : F â†’L[â„] â„) (x : F) :
    â€–(fr.toLinearMap.extendToğ•œ' x : ğ•œ)â€– â‰¤ â€–frâ€– * â€–xâ€– := by
  set lm : F â†’â‚—[ğ•œ] ğ•œ := fr.toLinearMap.extendToğ•œ'
  by_cases h : lm x = 0
  Â· rw [h, norm_zero]
    apply mul_nonneg <;> exact norm_nonneg _
  rw [â† mul_le_mul_left (norm_pos_iff.2 h), â† sq]
  calc
    â€–lm xâ€– ^ 2 = fr (conj (lm x : ğ•œ) â€¢ x) := fr.toLinearMap.norm_extendToğ•œ'_apply_sq x
    _ â‰¤ â€–fr (conj (lm x : ğ•œ) â€¢ x)â€– := le_abs_self _
    _ â‰¤ â€–frâ€– * â€–conj (lm x : ğ•œ) â€¢ xâ€– := le_opNorm _ _
    _ = â€–(lm x : ğ•œ)â€– * (â€–frâ€– * â€–xâ€–) := by rw [norm_smul, norm_conj, mul_left_comm]
#align continuous_linear_map.norm_extend_to_ğ•œ'_bound ContinuousLinearMap.norm_extendToğ•œ'_bound

/-- Extend `fr : F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def extendToğ•œ' (fr : F â†’L[â„] â„) : F â†’L[ğ•œ] ğ•œ :=
  LinearMap.mkContinuous _ â€–frâ€– fr.norm_extendToğ•œ'_bound
#align continuous_linear_map.extend_to_ğ•œ' ContinuousLinearMap.extendToğ•œ'

theorem extendToğ•œ'_apply (fr : F â†’L[â„] â„) (x : F) :
    fr.extendToğ•œ' x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl
#align continuous_linear_map.extend_to_ğ•œ'_apply ContinuousLinearMap.extendToğ•œ'_apply

@[simp]
theorem norm_extendToğ•œ' (fr : F â†’L[â„] â„) : â€–(fr.extendToğ•œ' : F â†’L[ğ•œ] ğ•œ)â€– = â€–frâ€– :=
  le_antisymm (LinearMap.mkContinuous_norm_le _ (norm_nonneg _) _) <|
    opNorm_le_bound _ (norm_nonneg _) fun x =>
      calc
        â€–fr xâ€– = â€–re (fr.extendToğ•œ' x : ğ•œ)â€– := congr_arg norm (fr.extendToğ•œ'_apply_re x).symm
        _ â‰¤ â€–(fr.extendToğ•œ' x : ğ•œ)â€– := abs_re_le_norm _
        _ â‰¤ â€–(fr.extendToğ•œ' : F â†’L[ğ•œ] ğ•œ)â€– * â€–xâ€– := le_opNorm _ _
#align continuous_linear_map.norm_extend_to_ğ•œ' ContinuousLinearMap.norm_extendToğ•œ'

end ContinuousLinearMap

-- Porting note (#10754): Added a new instance. This instance is needed for the rest of the file.
instance : NormedSpace ğ•œ (RestrictScalars â„ ğ•œ F) := by
  unfold RestrictScalars
  infer_instance

/-- Extend `fr : RestrictScalars â„ ğ•œ F â†’â‚—[â„] â„` to `F â†’â‚—[ğ•œ] ğ•œ`. -/
noncomputable def LinearMap.extendToğ•œ (fr : RestrictScalars â„ ğ•œ F â†’â‚—[â„] â„) : F â†’â‚—[ğ•œ] ğ•œ :=
  fr.extendToğ•œ'
#align linear_map.extend_to_ğ•œ LinearMap.extendToğ•œ

theorem LinearMap.extendToğ•œ_apply (fr : RestrictScalars â„ ğ•œ F â†’â‚—[â„] â„) (x : F) :
    fr.extendToğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl
#align linear_map.extend_to_ğ•œ_apply LinearMap.extendToğ•œ_apply

/-- Extend `fr : RestrictScalars â„ ğ•œ F â†’L[â„] â„` to `F â†’L[ğ•œ] ğ•œ`. -/
noncomputable def ContinuousLinearMap.extendToğ•œ (fr : RestrictScalars â„ ğ•œ F â†’L[â„] â„) : F â†’L[ğ•œ] ğ•œ :=
  fr.extendToğ•œ'
#align continuous_linear_map.extend_to_ğ•œ ContinuousLinearMap.extendToğ•œ

theorem ContinuousLinearMap.extendToğ•œ_apply (fr : RestrictScalars â„ ğ•œ F â†’L[â„] â„) (x : F) :
    fr.extendToğ•œ x = (fr x : ğ•œ) - (I : ğ•œ) * (fr ((I : ğ•œ) â€¢ x) : ğ•œ) := rfl
#align continuous_linear_map.extend_to_ğ•œ_apply ContinuousLinearMap.extendToğ•œ_apply

@[simp]
theorem ContinuousLinearMap.norm_extendToğ•œ (fr : RestrictScalars â„ ğ•œ F â†’L[â„] â„) :
    â€–fr.extendToğ•œâ€– = â€–frâ€– :=
  fr.norm_extendToğ•œ'
#align continuous_linear_map.norm_extend_to_ğ•œ ContinuousLinearMap.norm_extendToğ•œ
