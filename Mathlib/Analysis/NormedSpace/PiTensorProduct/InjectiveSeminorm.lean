/-
Copyright (c) 2024 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel
-/
import Mathlib.Analysis.NormedSpace.PiTensorProduct.ProjectiveSeminorm
import Mathlib.LinearAlgebra.Isomorphisms

/-!
# Injective seminorm on the tensor of a finite family of normed spaces.

Let `ğ•œ` be a nontrivially normed field and `E` be a family of normed `ğ•œ`-vector spaces `Eáµ¢`,
indexed by a finite type `Î¹`. We define a seminorm on `â¨‚[ğ•œ] i, Eáµ¢`, which we call the
"injective seminorm". It is chosen to satisfy the following property: for every
normed `ğ•œ`-vector space `F`, the linear equivalence
`MultilinearMap ğ•œ E F â‰ƒâ‚—[ğ•œ] (â¨‚[ğ•œ] i, Eáµ¢) â†’â‚—[ğ•œ] F`
expressing the universal property of the tensor product induces an isometric linear equivalence
`ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—áµ¢[ğ•œ] (â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`.

The idea is the following: Every normed `ğ•œ`-vector space `F` defines a linear map
from `â¨‚[ğ•œ] i, Eáµ¢` to `ContinuousMultilinearMap ğ•œ E F â†’â‚—[ğ•œ] F`, which sends `x` to the map
`f â†¦ f.lift x`. Thanks to `PiTensorProduct.norm_eval_le_projectiveSeminorm`, this map lands in
`ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F`. As this last space has a natural operator (semi)norm,
we get an induced seminorm on `â¨‚[ğ•œ] i, Eáµ¢`, which, by
`PiTensorProduct.norm_eval_le_projectiveSeminorm`, is bounded above by the projective seminorm
`PiTensorProduct.projectiveSeminorm`. We then take the `sup` of these seminorms as `F` varies;
as this family of seminorms is bounded, its `sup` has good properties.

In fact, we cannot take the `sup` over all normed spaces `F` because of set-theoretical issues,
so we only take spaces `F` in the same universe as `â¨‚[ğ•œ] i, Eáµ¢`. We prove in
`norm_eval_le_injectiveSeminorm` that this gives the same result, because every multilinear map
from `E = Î áµ¢ Eáµ¢` to `F` factors though a normed vector space in the same universe as
`â¨‚[ğ•œ] i, Eáµ¢`.

We then prove the universal property and the functoriality of `â¨‚[ğ•œ] i, Eáµ¢` as a normed vector
space.

## Main definitions

* `PiTensorProduct.toDualContinuousMultilinearMap`: The `ğ•œ`-linear map from
  `â¨‚[ğ•œ] i, Eáµ¢` to `ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F` sending `x` to the map
  `f â†¦ f x`.
* `PiTensorProduct.injectiveSeminorm`: The injective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.liftEquiv`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as a continuous linear equivalence.
* `PiTensorProduct.liftIsometry`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as an isometric linear equivalence.
* `PiTensorProduct.tprodL`: The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢`
  to `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.mapL`: The continuous linear map from `â¨‚[ğ•œ] i, Eáµ¢` to `â¨‚[ğ•œ] i, E'áµ¢`
  induced by a family of continuous linear maps `Eáµ¢ â†’L[ğ•œ] E'áµ¢`.
* `PiTensorProduct.mapLMultilinear`: The continuous multilinear map from
  `Î áµ¢ (Eáµ¢ â†’L[ğ•œ] E'áµ¢)` to `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] (â¨‚[ğ•œ] i, E'áµ¢)` sending a family
  `f` to `PiTensorProduct.mapL f`.

## Main results

* `PiTensorProduct.norm_eval_le_injectiveSeminorm`: The main property of the injective seminorm
  on `â¨‚[ğ•œ] i, Eáµ¢`: for every `x` in `â¨‚[ğ•œ] i, Eáµ¢` and every continuous multilinear map `f` from
`E = Î áµ¢ Eáµ¢` to a normed space `F`, we have `â€–f.lift xâ€– â‰¤ â€–fâ€– * injectiveSeminorm x `.
* `PiTensorProduct.mapL_opNorm`: If `f` is a family of continuous linear maps
  `fáµ¢ : Eáµ¢ â†’L[ğ•œ] Fáµ¢`, then `â€–PiTensorProduct.mapL fâ€– â‰¤ âˆ i, â€–fáµ¢â€–`.
* `PiTensorProduct.mapLMultilinear_opNorm` : If `F` is a normed vecteor space, then
  `â€–mapLMultilinear ğ•œ E Fâ€– â‰¤ 1`.

## TODO

* If all `Eáµ¢` are separated and satisfy `SeparatingDual`, then the seminorm on
  `â¨‚[ğ•œ] i, Eáµ¢` is a norm. This uses the construction of a basis of the `PiTensorProduct`, hence
  depends on PR https://github.com/leanprover-community/mathlib4/pull/11156.
  It should probably go in a separate file.

* Adapt the remaining functoriality constructions/properties from `PiTensorProduct`.

-/

universe uÎ¹ uğ•œ uE uF

variable {Î¹ : Type uÎ¹} [Fintype Î¹]
variable {ğ•œ : Type uğ•œ} [NontriviallyNormedField ğ•œ]
variable {E : Î¹ â†’ Type uE} [âˆ€ i, SeminormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]
variable {F : Type uF} [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

open scoped TensorProduct

namespace PiTensorProduct

section seminorm

variable (F) in
/-- The linear map from `â¨‚[ğ•œ] i, Eáµ¢` to `ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F` sending
`x` in `â¨‚[ğ•œ] i, Eáµ¢` to the map `f â†¦ f.lift x`.
-/
@[simps!]
noncomputable def toDualContinuousMultilinearMap : (â¨‚[ğ•œ] i, E i) â†’â‚—[ğ•œ]
    ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F where
  toFun x := LinearMap.mkContinuous
    ((LinearMap.flip (lift (R := ğ•œ) (s := E) (E := F)).toLinearMap x) âˆ˜â‚—
    ContinuousMultilinearMap.toMultilinearMapLinear)
    (projectiveSeminorm x)
    (fun _ â†¦ by simp only [LinearMap.coe_comp, Function.comp_apply,
                  ContinuousMultilinearMap.toMultilinearMapLinear_apply, LinearMap.flip_apply,
                  LinearEquiv.coe_coe]
                exact norm_eval_le_projectiveSeminorm _ _ _)
  map_add' x y := by
    ext _
    simp only [map_add, LinearMap.mkContinuous_apply, LinearMap.coe_comp, Function.comp_apply,
      ContinuousMultilinearMap.toMultilinearMapLinear_apply, LinearMap.add_apply,
      LinearMap.flip_apply, LinearEquiv.coe_coe, ContinuousLinearMap.add_apply]
  map_smul' a x := by
    ext _
    simp only [map_smul, LinearMap.mkContinuous_apply, LinearMap.coe_comp, Function.comp_apply,
      ContinuousMultilinearMap.toMultilinearMapLinear_apply, LinearMap.smul_apply,
      LinearMap.flip_apply, LinearEquiv.coe_coe, RingHom.id_apply, ContinuousLinearMap.coe_smul',
      Pi.smul_apply]

theorem toDualContinuousMultilinearMap_le_projectiveSeminorm (x : â¨‚[ğ•œ] i, E i) :
    â€–toDualContinuousMultilinearMap F xâ€– â‰¤ projectiveSeminorm x := by
  simp only [toDualContinuousMultilinearMap, LinearMap.coe_mk, AddHom.coe_mk]
  apply LinearMap.mkContinuous_norm_le _ (apply_nonneg _ _)

/-- The injective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`. Morally, it sends `x` in `â¨‚[ğ•œ] i, Eáµ¢` to the
`sup` of the operator norms of the `PiTensorProduct.toDualContinuousMultilinearMap F x`, for all
normed vector spaces `F`. In fact, we only take in the same universe as `â¨‚[ğ•œ] i, Eáµ¢`, and then
prove in `PiTensorProduct.norm_eval_le_injectiveSeminorm` that this gives the same result.
-/
noncomputable irreducible_def injectiveSeminorm : Seminorm ğ•œ (â¨‚[ğ•œ] i, E i) :=
  sSup {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE)) (_ : SeminormedAddCommGroup G)
  (_ : NormedSpace ğ•œ G), p = Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
  (toDualContinuousMultilinearMap G (ğ•œ := ğ•œ) (E := E))}

lemma dualSeminorms_bounded : BddAbove {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE))
    (_ : SeminormedAddCommGroup G) (_ : NormedSpace ğ•œ G),
    p = Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
    (toDualContinuousMultilinearMap G (ğ•œ := ğ•œ) (E := E))} := by
  existsi projectiveSeminorm
  rw [mem_upperBounds]
  simp only [Set.mem_setOf_eq, forall_exists_index]
  intro p G _ _ hp
  rw [hp]
  intro x
  simp only [Seminorm.comp_apply, coe_normSeminorm]
  exact toDualContinuousMultilinearMap_le_projectiveSeminorm _

theorem injectiveSeminorm_apply (x : â¨‚[ğ•œ] i, E i) :
    injectiveSeminorm x = â¨† p : {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE))
    (_ : SeminormedAddCommGroup G) (_ : NormedSpace ğ•œ G), p = Seminorm.comp (normSeminorm ğ•œ
    (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
    (toDualContinuousMultilinearMap G (ğ•œ := ğ•œ) (E := E))}, p.1 x := by
  simpa only [injectiveSeminorm, Set.coe_setOf, Set.mem_setOf_eq]
    using Seminorm.sSup_apply dualSeminorms_bounded

theorem norm_eval_le_injectiveSeminorm (f : ContinuousMultilinearMap ğ•œ E F) (x : â¨‚[ğ•œ] i, E i) :
    â€–lift f.toMultilinearMap xâ€– â‰¤ â€–fâ€– * injectiveSeminorm x := by
    /- If `F` were in `Type (max uÎ¹ uğ•œ uE)` (which is the type of `â¨‚[ğ•œ] i, E i`), then the
    property that we want to prove would hold by definition of `injectiveSeminorm`. This is
    not necessarily true, but we will show that there exists a normed vector space `G` in
    `Type (max uÎ¹ uğ•œ uE)` and an injective isometry from `G` to `F` such that `f` factors
    through a continuous multilinear map `f'` from `E = Î  i, E i` to `G`, to which we can apply
    the definition of `injectiveSeminorm`. The desired inequality for `f` then follows
    immediately.
    The idea is very simple: the multilinear map `f` corresponds by `PiTensorProduct.lift`
    to a linear map from `â¨‚[ğ•œ] i, E i` to `F`, say `l`. We want to take `G` to be the image of
    `l`, with the norm induced from that of `F`; to make sure that we are in the correct universe,
    it is actually more convenient to take `G` equal to the coimage of `l` (i.e. the quotient
    of `â¨‚[ğ•œ] i, E i` by the kernel of `l`), which is canonically isomorphic to its image by
    `LinearMap.quotKerEquivRange`. -/
  set G := (â¨‚[ğ•œ] i, E i) â§¸ LinearMap.ker (lift f.toMultilinearMap)
  set G' := LinearMap.range (lift f.toMultilinearMap)
  set e := LinearMap.quotKerEquivRange (lift f.toMultilinearMap)
  letI := SeminormedAddCommGroup.induced G G' e
  letI := NormedSpace.induced ğ•œ G G' e
  set f'â‚€ := lift.symm (e.symm.toLinearMap âˆ˜â‚— LinearMap.rangeRestrict (lift f.toMultilinearMap))
  have hf'â‚€ : âˆ€ (x : Î  (i : Î¹), E i), â€–f'â‚€ xâ€– â‰¤ â€–fâ€– * âˆ i, â€–x iâ€– := fun x â†¦ by
    change â€–e (f'â‚€ x)â€– â‰¤ _
    simp only [lift_symm, LinearMap.compMultilinearMap_apply, LinearMap.coe_comp,
        LinearEquiv.coe_coe, Function.comp_apply, LinearEquiv.apply_symm_apply, Submodule.coe_norm,
        LinearMap.codRestrict_apply, lift.tprod, ContinuousMultilinearMap.coe_coe, e, f'â‚€]
    exact f.le_opNorm x
  set f' := MultilinearMap.mkContinuous f'â‚€ â€–fâ€– hf'â‚€
  have hnorm : â€–f'â€– â‰¤ â€–fâ€– := (f'.opNorm_le_iff (norm_nonneg f)).mpr hf'â‚€
  have heq : e (lift f'.toMultilinearMap x) = lift f.toMultilinearMap x := by
    induction x using PiTensorProduct.induction_on with
    | smul_tprod =>
      simp only [lift_symm, map_smul, lift.tprod, ContinuousMultilinearMap.coe_coe,
      MultilinearMap.coe_mkContinuous, LinearMap.compMultilinearMap_apply, LinearMap.coe_comp,
      LinearEquiv.coe_coe, Function.comp_apply, LinearEquiv.apply_symm_apply, SetLike.val_smul,
      LinearMap.codRestrict_apply, f', f'â‚€]
    | add _ _ hx hy => simp only [map_add, Submodule.coe_add, hx, hy]
  suffices h : â€–lift f'.toMultilinearMap xâ€– â‰¤ â€–f'â€– * injectiveSeminorm x by
    change â€–(e (lift f'.toMultilinearMap x)).1â€– â‰¤ _ at h
    rw [heq] at h
    exact le_trans h (mul_le_mul_of_nonneg_right hnorm (apply_nonneg _ _))
  have hle : Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
      (toDualContinuousMultilinearMap G (ğ•œ := ğ•œ) (E := E)) â‰¤ injectiveSeminorm := by
    simp only [injectiveSeminorm]
    refine le_csSup dualSeminorms_bounded ?_
    rw [Set.mem_setOf]
    existsi G, inferInstance, inferInstance
    rfl
  refine le_trans ?_ (mul_le_mul_of_nonneg_left (hle x) (norm_nonneg f'))
  simp only [Seminorm.comp_apply, coe_normSeminorm, â† toDualContinuousMultilinearMap_apply_apply]
  rw [mul_comm]
  exact ContinuousLinearMap.le_opNorm _ _

theorem injectiveSeminorm_le_projectiveSeminorm :
    injectiveSeminorm (ğ•œ := ğ•œ) (E := E) â‰¤ projectiveSeminorm := by
  rw [injectiveSeminorm]
  refine csSup_le ?_ ?_
  Â· existsi 0
    simp only [Set.mem_setOf_eq]
    existsi PUnit, inferInstance, inferInstance
    ext x
    simp only [Seminorm.zero_apply, Seminorm.comp_apply, coe_normSeminorm]
    rw [Subsingleton.elim (toDualContinuousMultilinearMap PUnit x) 0, norm_zero]
  Â· intro p hp
    simp only [Set.mem_setOf_eq] at hp
    obtain âŸ¨G, _, _, hâŸ© := hp
    rw [h]; intro x; simp only [Seminorm.comp_apply, coe_normSeminorm]
    exact toDualContinuousMultilinearMap_le_projectiveSeminorm _

theorem injectiveSeminorm_tprod_le (m : Î  (i : Î¹), E i) :
    injectiveSeminorm (â¨‚â‚œ[ğ•œ] i, m i) â‰¤ âˆ i, â€–m iâ€– :=
  le_trans (injectiveSeminorm_le_projectiveSeminorm _) (projectiveSeminorm_tprod_le m)

noncomputable instance : SeminormedAddCommGroup (â¨‚[ğ•œ] i, E i) :=
  AddGroupSeminorm.toSeminormedAddCommGroup injectiveSeminorm.toAddGroupSeminorm

noncomputable instance : NormedSpace ğ•œ (â¨‚[ğ•œ] i, E i) where
  norm_smul_le a x := by
    change injectiveSeminorm.toFun (a â€¢ x) â‰¤ _
    rw [injectiveSeminorm.smul']
    rfl

variable (ğ•œ E F)

/-- The linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
induced by `PiTensorProduct.lift`, for every normed space `F`.
-/
@[simps]
noncomputable def liftEquiv : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F where
  toFun f := LinearMap.mkContinuous (lift f.toMultilinearMap) â€–fâ€– fun x â†¦
    norm_eval_le_injectiveSeminorm f x
  map_add' f g := by ext _; simp only [ContinuousMultilinearMap.toMultilinearMap_add, map_add,
    LinearMap.mkContinuous_apply, LinearMap.add_apply, ContinuousLinearMap.add_apply]
  map_smul' a f := by ext _; simp only [ContinuousMultilinearMap.toMultilinearMap_smul, map_smul,
    LinearMap.mkContinuous_apply, LinearMap.smul_apply, RingHom.id_apply,
    ContinuousLinearMap.coe_smul', Pi.smul_apply]
  invFun l := MultilinearMap.mkContinuous (lift.symm l.toLinearMap) â€–lâ€– fun x â†¦ by
    simp only [lift_symm, LinearMap.compMultilinearMap_apply, ContinuousLinearMap.coe_coe]
    exact ContinuousLinearMap.le_opNorm_of_le _ (injectiveSeminorm_tprod_le x)
  left_inv f := by ext x; simp only [LinearMap.mkContinuous_coe, LinearEquiv.symm_apply_apply,
      MultilinearMap.coe_mkContinuous, ContinuousMultilinearMap.coe_coe]
  right_inv l := by
    rw [â† ContinuousLinearMap.coe_inj]
    apply PiTensorProduct.ext; ext m
    simp only [lift_symm, LinearMap.mkContinuous_coe, LinearMap.compMultilinearMap_apply,
      lift.tprod, ContinuousMultilinearMap.coe_coe, MultilinearMap.coe_mkContinuous,
      ContinuousLinearMap.coe_coe]

/-- For a normed space `F`, we have constructed in `PiTensorProduct.liftEquiv` the canonical
linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
(induced by `PiTensorProduct.lift`). Here we give the upgrade of this equivalence to
an isometric linear equivalence; in particular, it is a continuous linear equivalence.
-/
noncomputable def liftIsometry : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—áµ¢[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F :=
  { liftEquiv ğ•œ E F with
    norm_map' := by
      intro f
      refine le_antisymm ?_ ?_
      Â· simp only [liftEquiv_apply]
        exact LinearMap.mkContinuous_norm_le _ (norm_nonneg f) _
      Â· conv_lhs => rw [â† (liftEquiv ğ•œ E F).symm_apply_apply f]
        rw [liftEquiv_symm_apply]
        exact MultilinearMap.mkContinuous_norm_le _ (norm_nonneg _) _ }

variable {ğ•œ E F}

@[simp]
theorem liftIsometry_apply_apply (f : ContinuousMultilinearMap ğ•œ E F) (x : â¨‚[ğ•œ] i, E i) :
    liftIsometry ğ•œ E F f x = lift f.toMultilinearMap x := by
  simp only [liftIsometry, LinearIsometryEquiv.coe_mk, liftEquiv_apply,
    LinearMap.mkContinuous_apply]

variable (ğ•œ) in
/-- The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢` to `â¨‚[ğ•œ] i, Eáµ¢`.
-/
@[simps!]
noncomputable def tprodL : ContinuousMultilinearMap ğ•œ E (â¨‚[ğ•œ] i, E i) :=
  (liftIsometry ğ•œ E _).symm (ContinuousLinearMap.id ğ•œ _)

@[simp]
theorem tprodL_coe : (tprodL ğ•œ).toMultilinearMap = tprod ğ•œ (s := E) := by
  ext m
  simp only [ContinuousMultilinearMap.coe_coe, tprodL_toFun]

@[simp]
theorem liftIsometry_symm_apply (l : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F) :
    (liftIsometry ğ•œ E F).symm l = l.compContinuousMultilinearMap (tprodL ğ•œ) := by
  ext m
  change (liftEquiv ğ•œ E F).symm l m = _
  simp only [liftEquiv_symm_apply, lift_symm, MultilinearMap.coe_mkContinuous,
    LinearMap.compMultilinearMap_apply, ContinuousLinearMap.coe_coe,
    ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply, tprodL_toFun]

@[simp]
theorem liftIsometry_tprodL :
    liftIsometry ğ•œ E _ (tprodL ğ•œ) = ContinuousLinearMap.id ğ•œ (â¨‚[ğ•œ] i, E i) := by
  ext _
  simp only [liftIsometry_apply_apply, tprodL_coe, lift_tprod, LinearMap.id_coe, id_eq,
    ContinuousLinearMap.coe_id']

end seminorm

section map

variable {E' E'' : Î¹ â†’ Type*}
variable [âˆ€ i, SeminormedAddCommGroup (E' i)] [âˆ€ i, NormedSpace ğ•œ (E' i)]
variable [âˆ€ i, SeminormedAddCommGroup (E'' i)] [âˆ€ i, NormedSpace ğ•œ (E'' i)]
variable (g : Î  i, E' i â†’L[ğ•œ] E'' i) (f : Î  i, E i â†’L[ğ•œ] E' i)

/--
Let `Eáµ¢` and `E'áµ¢` be two families of normed `ğ•œ`-vector spaces.
Let `f` be a family of continuous `ğ•œ`-linear maps between `Eáµ¢` and `E'áµ¢`, i.e.
`f : Î áµ¢ Eáµ¢ â†’L[ğ•œ] E'áµ¢`, then there is an induced continuous linear map
`â¨‚áµ¢ Eáµ¢ â†’ â¨‚áµ¢ E'áµ¢` by `â¨‚ aáµ¢ â†¦ â¨‚ fáµ¢ aáµ¢`.
-/
noncomputable def mapL : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i :=
  liftIsometry ğ•œ E _ <| (tprodL ğ•œ).compContinuousLinearMap f

@[simp]
theorem mapL_coe : (mapL f).toLinearMap = map (fun i â†¦ (f i).toLinearMap) := by
  ext
  simp only [mapL, LinearMap.compMultilinearMap_apply, ContinuousLinearMap.coe_coe,
    liftIsometry_apply_apply, lift.tprod, ContinuousMultilinearMap.coe_coe,
    ContinuousMultilinearMap.compContinuousLinearMap_apply, tprodL_toFun, map_tprod]

@[simp]
theorem mapL_apply (x : â¨‚[ğ•œ] i, E i) : mapL f x = map (fun i â†¦ (f i).toLinearMap) x := by
  induction x using PiTensorProduct.induction_on with
  | smul_tprod =>
    simp only [mapL, map_smul, liftIsometry_apply_apply, lift.tprod,
    ContinuousMultilinearMap.coe_coe, ContinuousMultilinearMap.compContinuousLinearMap_apply,
    tprodL_toFun, map_tprod, ContinuousLinearMap.coe_coe]
  | add _ _ hx hy => simp only [map_add, hx, hy]

/-- Given submodules `páµ¢ âŠ† Eáµ¢`, this is the natural map: `â¨‚[ğ•œ] i, páµ¢ â†’ â¨‚[ğ•œ] i, Eáµ¢`.
This is the continuous version of `PiTensorProduct.mapIncl`.
-/
@[simp]
noncomputable def mapLIncl (p : Î  i, Submodule ğ•œ (E i)) : (â¨‚[ğ•œ] i, p i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i :=
  mapL fun (i : Î¹) â†¦ (p i).subtypeL

theorem mapL_comp : mapL (fun (i : Î¹) â†¦ g i âˆ˜L f i) = mapL g âˆ˜L mapL f := by
  apply ContinuousLinearMap.coe_injective
  ext
  simp only [mapL_coe, ContinuousLinearMap.coe_comp, LinearMap.compMultilinearMap_apply, map_tprod,
    LinearMap.coe_comp, ContinuousLinearMap.coe_coe, Function.comp_apply]

theorem liftIsometry_comp_mapL (h : ContinuousMultilinearMap ğ•œ E' F) :
    liftIsometry ğ•œ E' F h âˆ˜L mapL f = liftIsometry ğ•œ E F (h.compContinuousLinearMap f) := by
  apply ContinuousLinearMap.coe_injective
  ext
  simp only [ContinuousLinearMap.coe_comp, mapL_coe, LinearMap.compMultilinearMap_apply,
    LinearMap.coe_comp, ContinuousLinearMap.coe_coe, Function.comp_apply, map_tprod,
    liftIsometry_apply_apply, lift.tprod, ContinuousMultilinearMap.coe_coe,
    ContinuousMultilinearMap.compContinuousLinearMap_apply]

@[simp]
theorem mapL_id : mapL (fun i â†¦ ContinuousLinearMap.id ğ•œ (E i)) = ContinuousLinearMap.id _ _ := by
  apply ContinuousLinearMap.coe_injective
  ext
  simp only [mapL_coe, ContinuousLinearMap.coe_id, map_id, LinearMap.compMultilinearMap_apply,
    LinearMap.id_coe, id_eq]

@[simp]
theorem mapL_one : mapL (fun (i : Î¹) â†¦ (1 : E i â†’L[ğ•œ] E i)) = 1 :=
  mapL_id

theorem mapL_mul (fâ‚ fâ‚‚ : Î  i, E i â†’L[ğ•œ] E i) :
    mapL (fun i â†¦ fâ‚ i * fâ‚‚ i) = mapL fâ‚ * mapL fâ‚‚ :=
  mapL_comp fâ‚ fâ‚‚

/-- Upgrading `PiTensorProduct.mapL` to a `MonoidHom` when `E = E'`. -/
@[simps]
noncomputable def mapLMonoidHom : (Î  i, E i â†’L[ğ•œ] E i) â†’* ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i) where
  toFun := mapL
  map_one' := mapL_one
  map_mul' := mapL_mul

@[simp]
protected theorem mapL_pow (f : Î  i, E i â†’L[ğ•œ] E i) (n : â„•) :
    mapL (f ^ n) = mapL f ^ n := MonoidHom.map_pow mapLMonoidHom _ _

-- We redeclare `Î¹` here, and later dependent arguments,
-- to avoid the `[Fintype Î¹]` assumption present throughout the rest of the file.
open Function in
private theorem mapL_add_smul_aux {Î¹ : Type uÎ¹}
    {E : Î¹ â†’ Type uE} [(i : Î¹) â†’ SeminormedAddCommGroup (E i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E i)]
    {E' : Î¹ â†’ Type u_1} [(i : Î¹) â†’ SeminormedAddCommGroup (E' i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E' i)]
    (f : (i : Î¹) â†’ E i â†’L[ğ•œ] E' i)
    [DecidableEq Î¹] (i : Î¹) (u : E i â†’L[ğ•œ] E' i) :
    (fun j â†¦ (update f i u j).toLinearMap) =
      update (fun j â†¦ (f j).toLinearMap) i u.toLinearMap := by
  symm
  rw [update_eq_iff]
  constructor
  Â· simp only [update_self]
  Â· exact fun _ h â†¦ by simp only [ne_eq, h, not_false_eq_true, update_of_ne]

open Function in
protected theorem mapL_add [DecidableEq Î¹] (i : Î¹) (u v : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (u + v)) = mapL (update f i u) + mapL (update f i v) := by
  ext x
  simp only [mapL_apply, mapL_add_smul_aux, ContinuousLinearMap.coe_add,
    PiTensorProduct.map_update_add, LinearMap.add_apply, ContinuousLinearMap.add_apply]

open Function in
protected theorem mapL_smul [DecidableEq Î¹] (i : Î¹) (c : ğ•œ) (u : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (c â€¢ u)) = c â€¢ mapL (update f i u) := by
  ext x
  simp only [mapL_apply, mapL_add_smul_aux, ContinuousLinearMap.coe_smul,
    PiTensorProduct.map_update_smul, LinearMap.smul_apply, ContinuousLinearMap.coe_smul',
    Pi.smul_apply]

theorem mapL_opNorm : â€–mapL fâ€– â‰¤ âˆ i, â€–f iâ€– := by
  rw [ContinuousLinearMap.opNorm_le_iff (by positivity)]
  intro x
  rw [mapL, liftIsometry]
  simp only [LinearIsometryEquiv.coe_mk, liftEquiv_apply, LinearMap.mkContinuous_apply]
  refine le_trans (norm_eval_le_injectiveSeminorm _ _)
    (mul_le_mul_of_nonneg_right ?_ (norm_nonneg x))
  rw [ContinuousMultilinearMap.opNorm_le_iff (Finset.prod_nonneg (fun _ _ â†¦ norm_nonneg _))]
  intro m
  simp only [ContinuousMultilinearMap.compContinuousLinearMap_apply]
  refine le_trans (injectiveSeminorm_tprod_le (fun i â†¦ (f i) (m i))) ?_
  rw [â† Finset.prod_mul_distrib]
  exact Finset.prod_le_prod (fun _ _ â†¦ norm_nonneg _) (fun _ _ â†¦ ContinuousLinearMap.le_opNorm _ _ )

variable (ğ•œ E E')

/-- The tensor of a family of linear maps from `Eáµ¢` to `E'áµ¢`, as a continuous multilinear map of
the family.
-/
@[simps!]
noncomputable def mapLMultilinear : ContinuousMultilinearMap ğ•œ (fun (i : Î¹) â†¦ E i â†’L[ğ•œ] E' i)
    ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i) :=
  MultilinearMap.mkContinuous
  { toFun := mapL
    map_update_smul' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_smul _ _ _ _
    map_update_add' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_add _ _ _ _ }
  1 (fun f â†¦ by rw [one_mul]; exact mapL_opNorm f)

variable {ğ•œ E E'}

theorem mapLMultilinear_opNorm : â€–mapLMultilinear ğ•œ E E'â€– â‰¤ 1 :=
  MultilinearMap.mkContinuous_norm_le _ zero_le_one _

end map

end PiTensorProduct
