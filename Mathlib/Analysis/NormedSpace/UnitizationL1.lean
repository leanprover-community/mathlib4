/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.Unitization
import Mathlib.Analysis.NormedSpace.ProdLp

/-! # Unitization equipped with the $L^1$ norm

In another file, the `Unitization ğ•œ A` of a non-unital normed `ğ•œ`-algebra `A` is equipped with the
norm inherited as the pullback via a map (closely related to) the left-regular representation of the
algebra on itself (see `Unitization.instNormedRing`).

However, this construction is only valid (and an isometry) when `A` is a `RegularNormedAlgebra`.
Sometimes it is useful to consider the unitization of a non-unital algebra with the $L^1$ norm
instead. This file provides that norm on the type synonym `WithLp 1 (Unitization ğ•œ A)`, along
with the algebra isomomorphism between `Unitization ğ•œ A` and `WithLp 1 (Unitization ğ•œ A)`.
Note that `TrivSqZeroExt` is also equipped with the $L^1$ norm in the analogous way, but it is
registered as an instance without the type synonym.

One application of this is a straightforward proof that the quasispectrum of an element in a
non-unital Banach algebra is compact, which can be established by passing to the unitization.
-/

variable (ğ•œ A : Type*) [NormedField ğ•œ] [NonUnitalNormedRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]

namespace WithLp

open Unitization

/-- The natural map between `Unitization ğ•œ A` and `ğ•œ Ã— A`, transferred to their `WithLp 1`
synonyms. -/
noncomputable def unitization_addEquiv_prod : WithLp 1 (Unitization ğ•œ A) â‰ƒ+ WithLp 1 (ğ•œ Ã— A) :=
  (WithLp.linearEquiv 1 ğ•œ (Unitization ğ•œ A)).toAddEquiv.trans <|
    (addEquiv ğ•œ A).trans (WithLp.linearEquiv 1 ğ•œ (ğ•œ Ã— A)).symm.toAddEquiv

noncomputable instance instUnitizationNormedAddCommGroup :
    NormedAddCommGroup (WithLp 1 (Unitization ğ•œ A)) :=
  NormedAddCommGroup.induced (WithLp 1 (Unitization ğ•œ A)) (WithLp 1 (ğ•œ Ã— A))
    (unitization_addEquiv_prod ğ•œ A) (AddEquiv.injective _)

/-- Bundle `WithLp.unitization_addEquiv_prod` as a `UniformEquiv`. -/
noncomputable def uniformEquiv_unitization_addEquiv_prod :
    WithLp 1 (Unitization ğ•œ A) â‰ƒáµ¤ WithLp 1 (ğ•œ Ã— A) :=
  { unitization_addEquiv_prod ğ•œ A with
    uniformContinuous_invFun := uniformContinuous_comap' uniformContinuous_id
    uniformContinuous_toFun := uniformContinuous_iff.mpr le_rfl }

instance instCompleteSpace [CompleteSpace ğ•œ] [CompleteSpace A] :
    CompleteSpace (WithLp 1 (Unitization ğ•œ A)) :=
  completeSpace_congr (uniformEquiv_unitization_addEquiv_prod ğ•œ A).uniformEmbedding |>.mpr
    CompleteSpace.prod

variable {ğ•œ A}

open ENNReal in
lemma unitization_norm_def (x : WithLp 1 (Unitization ğ•œ A)) :
    â€–xâ€– = â€–(WithLp.equiv 1 _ x).fstâ€– + â€–(WithLp.equiv 1 _ x).sndâ€– := calc
  â€–xâ€– = (â€–(WithLp.equiv 1 _ x).fstâ€– ^ (1 : â„â‰¥0âˆ).toReal +
      â€–(WithLp.equiv 1 _ x).sndâ€– ^ (1 : â„â‰¥0âˆ).toReal) ^ (1 / (1 : â„â‰¥0âˆ).toReal) :=
    WithLp.prod_norm_eq_add (by simp : 0 < (1 : â„â‰¥0âˆ).toReal) _
  _   = â€–(WithLp.equiv 1 _ x).fstâ€– + â€–(WithLp.equiv 1 _ x).sndâ€– := by simp

lemma unitization_nnnorm_def (x : WithLp 1 (Unitization ğ•œ A)) :
    â€–xâ€–â‚Š = â€–(WithLp.equiv 1 _ x).fstâ€–â‚Š + â€–(WithLp.equiv 1 _ x).sndâ€–â‚Š :=
  Subtype.ext <| unitization_norm_def x

lemma unitization_norm_inr (x : A) : â€–(WithLp.equiv 1 (Unitization ğ•œ A)).symm xâ€– = â€–xâ€– := by
  simp [unitization_norm_def]

lemma unitization_nnnorm_inr (x : A) : â€–(WithLp.equiv 1 (Unitization ğ•œ A)).symm xâ€–â‚Š = â€–xâ€–â‚Š := by
  simp [unitization_nnnorm_def]

lemma unitization_isometry_inr :
    Isometry (fun x : A â†¦ (WithLp.equiv 1 (Unitization ğ•œ A)).symm x) :=
  AddMonoidHomClass.isometry_of_norm
    ((WithLp.linearEquiv 1 ğ•œ (Unitization ğ•œ A)).symm.comp <| Unitization.inrHom ğ•œ A)
    unitization_norm_inr

instance instUnitizationRing : Ring (WithLp 1 (Unitization ğ•œ A)) :=
  inferInstanceAs (Ring (Unitization ğ•œ A))

@[simp]
lemma unitization_mul (x y : WithLp 1 (Unitization ğ•œ A)) :
    WithLp.equiv 1 _ (x * y) = (WithLp.equiv 1 _ x) * (WithLp.equiv 1 _ y) :=
  rfl

instance {R : Type*} [CommSemiring R] [Algebra R ğ•œ] [DistribMulAction R A] [IsScalarTower R ğ•œ A] :
    Algebra R (WithLp 1 (Unitization ğ•œ A)) :=
  inferInstanceAs (Algebra R (Unitization ğ•œ A))

@[simp]
lemma unitization_algebraMap (r : ğ•œ) :
    WithLp.equiv 1 _ (algebraMap ğ•œ (WithLp 1 (Unitization ğ•œ A)) r) =
      algebraMap ğ•œ (Unitization ğ•œ A) r :=
  rfl

/-- `WithLp.equiv` bundled as an algebra isomorphism with `Unitization ğ•œ A`. -/
@[simps!]
def unitizationAlgEquiv (R : Type*) [CommSemiring R] [Algebra R ğ•œ] [DistribMulAction R A]
    [IsScalarTower R ğ•œ A] : WithLp 1 (Unitization ğ•œ A) â‰ƒâ‚[R] Unitization ğ•œ A :=
  { WithLp.equiv 1 (Unitization ğ•œ A) with
    map_mul' := fun _ _ â†¦ rfl
    map_add' := fun _ _ â†¦ rfl
    commutes' := fun _ â†¦ rfl }

noncomputable instance instUnitizationNormedRing : NormedRing (WithLp 1 (Unitization ğ•œ A)) where
  dist_eq := dist_eq_norm
  norm_mul x y := by
    simp_rw [unitization_norm_def, add_mul, mul_add, unitization_mul, fst_mul, snd_mul]
    rw [add_assoc, add_assoc]
    gcongr
    Â· exact norm_mul_le _ _
    Â· apply (norm_add_le _ _).trans
      gcongr
      Â· simp [norm_smul]
      Â· apply (norm_add_le _ _).trans
        gcongr
        Â· simp [norm_smul, mul_comm]
        Â· exact norm_mul_le _ _

noncomputable instance instUnitizationNormedAlgebra :
    NormedAlgebra ğ•œ (WithLp 1 (Unitization ğ•œ A)) where
  norm_smul_le r x := by
    simp_rw [unitization_norm_def, equiv_smul, fst_smul, snd_smul, norm_smul, mul_add]
    exact le_rfl

end WithLp
