/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.FieldTheory.IsAlgClosed.Spectrum
import Mathlib.Analysis.Complex.Liouville
import Mathlib.Analysis.Complex.Polynomial
import Mathlib.Analysis.Analytic.RadiusLiminf
import Mathlib.Topology.Algebra.Module.CharacterSpace
import Mathlib.Analysis.NormedSpace.Exponential

#align_import analysis.normed_space.spectrum from "leanprover-community/mathlib"@"d608fc5d4e69d4cc21885913fb573a88b0deb521"

/-!
# The spectrum of elements in a complete normed algebra

This file contains the basic theory for the resolvent and spectrum of a Banach algebra.

## Main definitions

* `spectralRadius : â„â‰¥0âˆ`: supremum of `â€–kâ€–â‚Š` for all `k âˆˆ spectrum ğ•œ a`
* `NormedRing.algEquivComplexOfComplete`: **Gelfand-Mazur theorem** For a complex
  Banach division algebra, the natural `algebraMap â„‚ A` is an algebra isomorphism whose inverse
  is given by selecting the (unique) element of `spectrum â„‚ a`

## Main statements

* `spectrum.isOpen_resolventSet`: the resolvent set is open.
* `spectrum.isClosed`: the spectrum is closed.
* `spectrum.subset_closedBall_norm`: the spectrum is a subset of closed disk of radius
  equal to the norm.
* `spectrum.isCompact`: the spectrum is compact.
* `spectrum.spectralRadius_le_nnnorm`: the spectral radius is bounded above by the norm.
* `spectrum.hasDerivAt_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius`: Gelfand's formula for the
  spectral radius in Banach algebras over `â„‚`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.


## TODO

* compute all derivatives of `resolvent a`.

-/


open scoped ENNReal NNReal

/-- The *spectral radius* is the supremum of the `nnnorm` (`â€–Â·â€–â‚Š`) of elements in the spectrum,
    coerced into an element of `â„â‰¥0âˆ`. Note that it is possible for `spectrum ğ•œ a = âˆ…`. In this
    case, `spectralRadius a = 0`. It is also possible that `spectrum ğ•œ a` be unbounded (though
    not for Banach algebras, see `spectrum.is_bounded`, below).  In this case,
    `spectralRadius a = âˆ`. -/
noncomputable def spectralRadius (ğ•œ : Type*) {A : Type*} [NormedField ğ•œ] [Ring A] [Algebra ğ•œ A]
    (a : A) : â„â‰¥0âˆ :=
  â¨† k âˆˆ spectrum ğ•œ a, â€–kâ€–â‚Š
#align spectral_radius spectralRadius

variable {ğ•œ : Type*} {A : Type*}

namespace spectrum

section SpectrumCompact

open Filter

variable [NormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]

local notation "Ïƒ" => spectrum ğ•œ
local notation "Ï" => resolventSet ğ•œ
local notation "â†‘â‚" => algebraMap ğ•œ A

@[simp]
theorem SpectralRadius.of_subsingleton [Subsingleton A] (a : A) : spectralRadius ğ•œ a = 0 := by
  simp [spectralRadius]
#align spectrum.spectral_radius.of_subsingleton spectrum.SpectralRadius.of_subsingleton

@[simp]
theorem spectralRadius_zero : spectralRadius ğ•œ (0 : A) = 0 := by
  nontriviality A
  simp [spectralRadius]
#align spectrum.spectral_radius_zero spectrum.spectralRadius_zero

theorem mem_resolventSet_of_spectralRadius_lt {a : A} {k : ğ•œ} (h : spectralRadius ğ•œ a < â€–kâ€–â‚Š) :
    k âˆˆ Ï a :=
  Classical.not_not.mp fun hn => h.not_le <| le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) k hn
#align spectrum.mem_resolvent_set_of_spectral_radius_lt spectrum.mem_resolventSet_of_spectralRadius_lt

variable [CompleteSpace A]

theorem isOpen_resolventSet (a : A) : IsOpen (Ï a) :=
  Units.isOpen.preimage ((continuous_algebraMap ğ•œ A).sub continuous_const)
#align spectrum.is_open_resolvent_set spectrum.isOpen_resolventSet

protected theorem isClosed (a : A) : IsClosed (Ïƒ a) :=
  (isOpen_resolventSet a).isClosed_compl
#align spectrum.is_closed spectrum.isClosed

theorem mem_resolventSet_of_norm_lt_mul {a : A} {k : ğ•œ} (h : â€–aâ€– * â€–(1 : A)â€– < â€–kâ€–) : k âˆˆ Ï a := by
  rw [resolventSet, Set.mem_setOf_eq, Algebra.algebraMap_eq_smul_one]
  nontriviality A
  have hk : k â‰  0 :=
    ne_zero_of_norm_ne_zero ((mul_nonneg (norm_nonneg _) (norm_nonneg _)).trans_lt h).ne'
  letI ku := Units.map â†‘â‚.toMonoidHom (Units.mk0 k hk)
  rw [â† inv_inv â€–(1 : A)â€–,
    mul_inv_lt_iff (inv_pos.2 <| norm_pos_iff.2 (one_ne_zero : (1 : A) â‰  0))] at h
  have hku : â€–-aâ€– < â€–(â†‘kuâ»Â¹ : A)â€–â»Â¹ := by simpa [norm_algebraMap] using h
  simpa [sub_eq_add_neg, Algebra.algebraMap_eq_smul_one] using (ku.add (-a) hku).isUnit
#align spectrum.mem_resolvent_set_of_norm_lt_mul spectrum.mem_resolventSet_of_norm_lt_mul

theorem mem_resolventSet_of_norm_lt [NormOneClass A] {a : A} {k : ğ•œ} (h : â€–aâ€– < â€–kâ€–) : k âˆˆ Ï a :=
  mem_resolventSet_of_norm_lt_mul (by rwa [norm_one, mul_one])
#align spectrum.mem_resolvent_set_of_norm_lt spectrum.mem_resolventSet_of_norm_lt

theorem norm_le_norm_mul_of_mem {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) : â€–kâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
  le_of_not_lt <| mt mem_resolventSet_of_norm_lt_mul hk
#align spectrum.norm_le_norm_mul_of_mem spectrum.norm_le_norm_mul_of_mem

theorem norm_le_norm_of_mem [NormOneClass A] {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) : â€–kâ€– â‰¤ â€–aâ€– :=
  le_of_not_lt <| mt mem_resolventSet_of_norm_lt hk
#align spectrum.norm_le_norm_of_mem spectrum.norm_le_norm_of_mem

theorem subset_closedBall_norm_mul (a : A) : Ïƒ a âŠ† Metric.closedBall (0 : ğ•œ) (â€–aâ€– * â€–(1 : A)â€–) :=
  fun k hk => by simp [norm_le_norm_mul_of_mem hk]
#align spectrum.subset_closed_ball_norm_mul spectrum.subset_closedBall_norm_mul

theorem subset_closedBall_norm [NormOneClass A] (a : A) : Ïƒ a âŠ† Metric.closedBall (0 : ğ•œ) â€–aâ€– :=
  fun k hk => by simp [norm_le_norm_of_mem hk]
#align spectrum.subset_closed_ball_norm spectrum.subset_closedBall_norm

theorem is_bounded (a : A) : Metric.Bounded (Ïƒ a) :=
  (Metric.bounded_iff_subset_ball 0).mpr âŸ¨â€–aâ€– * â€–(1 : A)â€–, subset_closedBall_norm_mul aâŸ©
#align spectrum.is_bounded spectrum.is_bounded

protected theorem isCompact [ProperSpace ğ•œ] (a : A) : IsCompact (Ïƒ a) :=
  Metric.isCompact_of_isClosed_bounded (spectrum.isClosed a) (is_bounded a)
#align spectrum.is_compact spectrum.isCompact

theorem spectralRadius_le_nnnorm [NormOneClass A] (a : A) : spectralRadius ğ•œ a â‰¤ â€–aâ€–â‚Š := by
  refine' iSupâ‚‚_le fun k hk => _
  exact_mod_cast norm_le_norm_of_mem hk
#align spectrum.spectral_radius_le_nnnorm spectrum.spectralRadius_le_nnnorm

theorem exists_nnnorm_eq_spectralRadius_of_nonempty [ProperSpace ğ•œ] {a : A} (ha : (Ïƒ a).Nonempty) :
    âˆƒ k âˆˆ Ïƒ a, (â€–kâ€–â‚Š : â„â‰¥0âˆ) = spectralRadius ğ•œ a := by
  obtain âŸ¨k, hk, hâŸ© := (spectrum.isCompact a).exists_forall_ge ha continuous_nnnorm.continuousOn
  exact âŸ¨k, hk, le_antisymm (le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) k hk) (iSupâ‚‚_le <| by exact_mod_cast h)âŸ©
#align spectrum.exists_nnnorm_eq_spectral_radius_of_nonempty spectrum.exists_nnnorm_eq_spectralRadius_of_nonempty

theorem spectralRadius_lt_of_forall_lt_of_nonempty [ProperSpace ğ•œ] {a : A} (ha : (Ïƒ a).Nonempty)
    {r : â„â‰¥0} (hr : âˆ€ k âˆˆ Ïƒ a, â€–kâ€–â‚Š < r) : spectralRadius ğ•œ a < r :=
  sSup_image.symm.trans_lt <|
    ((spectrum.isCompact a).sSup_lt_iff_of_continuous ha
          (ENNReal.continuous_coe.comp continuous_nnnorm).continuousOn (r : â„â‰¥0âˆ)).mpr
      (by dsimp only [(Â· âˆ˜ Â·)]; exact_mod_cast hr)
#align spectrum.spectral_radius_lt_of_forall_lt_of_nonempty spectrum.spectralRadius_lt_of_forall_lt_of_nonempty

open ENNReal Polynomial

variable (ğ•œ)

theorem spectralRadius_le_pow_nnnorm_pow_one_div (a : A) (n : â„•) :
    spectralRadius ğ•œ a â‰¤ (â€–a ^ (n + 1)â€–â‚Š : â„â‰¥0âˆ) ^ (1 / (n + 1) : â„) *
      (â€–(1 : A)â€–â‚Š : â„â‰¥0âˆ) ^ (1 / (n + 1) : â„) := by
  refine' iSupâ‚‚_le fun k hk => _
  -- apply easy direction of the spectral mapping theorem for polynomials
  have pow_mem : k ^ (n + 1) âˆˆ Ïƒ (a ^ (n + 1)) := by
    simpa only [one_mul, Algebra.algebraMap_eq_smul_one, one_smul, aeval_monomial, one_mul,
      eval_monomial] using subset_polynomial_aeval a (@monomial ğ•œ _ (n + 1) (1 : ğ•œ)) âŸ¨k, hk, rflâŸ©
  -- power of the norm is bounded by norm of the power
  have nnnorm_pow_le : (â†‘(â€–kâ€–â‚Š ^ (n + 1)) : â„â‰¥0âˆ) â‰¤ â€–a ^ (n + 1)â€–â‚Š * â€–(1 : A)â€–â‚Š := by
    simpa only [Real.toNNReal_mul (norm_nonneg _), norm_toNNReal, nnnorm_pow k (n + 1),
      ENNReal.coe_mul] using coe_mono (Real.toNNReal_mono (norm_le_norm_mul_of_mem pow_mem))
  -- take (n + 1)áµ—Ê° roots and clean up the left-hand side
  have hn : 0 < ((n + 1 : â„•) : â„) := by exact_mod_cast Nat.succ_pos'
  convert monotone_rpow_of_nonneg (one_div_pos.mpr hn).le nnnorm_pow_le using 1
  all_goals dsimp
  erw [coe_pow, â† rpow_nat_cast, â† rpow_mul, mul_one_div_cancel hn.ne', rpow_one]
  rw [Nat.cast_succ, ENNReal.coe_mul_rpow]
#align spectrum.spectral_radius_le_pow_nnnorm_pow_one_div spectrum.spectralRadius_le_pow_nnnorm_pow_one_div

theorem spectralRadius_le_liminf_pow_nnnorm_pow_one_div (a : A) :
    spectralRadius ğ•œ a â‰¤ atTop.liminf fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„) := by
  refine' ENNReal.le_of_forall_lt_one_mul_le fun Îµ hÎµ => _
  by_cases Îµ = 0
  Â· simp only [h, MulZeroClass.zero_mul, zero_le']
  have hÎµ' : Îµâ»Â¹ â‰  âˆ := fun h' =>
    h (by simpa only [inv_inv, inv_top] using congr_arg (fun x : â„â‰¥0âˆ => xâ»Â¹) h')
  simp only [ENNReal.mul_le_iff_le_inv h (hÎµ.trans_le le_top).ne, mul_comm Îµâ»Â¹,
    liminf_eq_iSup_iInf_of_nat', ENNReal.iSup_mul]
  conv_rhs => arg 1; intro i; rw [ENNReal.iInf_mul hÎµ']
  rw [â† ENNReal.inv_lt_inv, inv_one] at hÎµ
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp
    (ENNReal.eventually_pow_one_div_le (ENNReal.coe_ne_top : â†‘â€–(1 : A)â€–â‚Š â‰  âˆ) hÎµ)
  refine' le_trans _ (le_iSup _ (N + 1))
  refine' le_iInf fun n => _
  simp only [â† add_assoc]
  refine' (spectralRadius_le_pow_nnnorm_pow_one_div ğ•œ a (n + N)).trans _
  norm_cast
  exact mul_le_mul_left' (hN (n + N + 1) (by linarith)) _
#align spectrum.spectral_radius_le_liminf_pow_nnnorm_pow_one_div spectrum.spectralRadius_le_liminf_pow_nnnorm_pow_one_div

end SpectrumCompact

section resolvent

open Filter Asymptotics

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "Ï" => resolventSet ğ•œ
local notation "â†‘â‚" => algebraMap ğ•œ A

theorem hasDerivAt_resolvent {a : A} {k : ğ•œ} (hk : k âˆˆ Ï a) :
    HasDerivAt (resolvent a) (-resolvent a k ^ 2) k := by
  have Hâ‚ : HasFDerivAt Ring.inverse _ (â†‘â‚ k - a) := hasFDerivAt_ring_inverse (ğ•œ := ğ•œ) hk.unit
  have Hâ‚‚ : HasDerivAt (fun k => â†‘â‚ k - a) 1 k := by
    simpa using (Algebra.linearMap ğ•œ A).hasDerivAt.sub_const a
  simpa [resolvent, sq, hk.unit_spec, â† Ring.inverse_unit hk.unit] using Hâ‚.comp_hasDerivAt k Hâ‚‚
#align spectrum.has_deriv_at_resolvent spectrum.hasDerivAt_resolvent

/- TODO: Once there is sufficient API for bornology, we should get a nice filter / asymptotics
version of this, for example: `Tendsto (resolvent a) (cobounded ğ•œ) (ğ“ 0)` or more specifically
`(resolvent a) =O[cobounded ğ•œ] (fun z â†¦ zâ»Â¹)`. -/
theorem norm_resolvent_le_forall (a : A) :
    âˆ€ Îµ > 0, âˆƒ R > 0, âˆ€ z : ğ•œ, R â‰¤ â€–zâ€– â†’ â€–resolvent a zâ€– â‰¤ Îµ := by
  obtain âŸ¨c, c_pos, hcâŸ© := (@NormedRing.inverse_one_sub_norm A _ _).exists_pos
  rw [isBigOWith_iff, eventually_iff, Metric.mem_nhds_iff] at hc
  rcases hc with âŸ¨Î´, Î´_pos, hÎ´âŸ©
  simp only [CstarRing.norm_one, mul_one] at hÎ´
  intro Îµ hÎµ
  have haâ‚ : 0 < â€–aâ€– + 1 := lt_of_le_of_lt (norm_nonneg a) (lt_add_one _)
  have min_pos : 0 < min (Î´ * (â€–aâ€– + 1)â»Â¹) (Îµ * câ»Â¹) :=
    lt_min (mul_pos Î´_pos (inv_pos.mpr haâ‚)) (mul_pos hÎµ (inv_pos.mpr c_pos))
  refine' âŸ¨(min (Î´ * (â€–aâ€– + 1)â»Â¹) (Îµ * câ»Â¹))â»Â¹, inv_pos.mpr min_pos, fun z hz => _âŸ©
  have hnz : z â‰  0 := norm_pos_iff.mp (lt_of_lt_of_le (inv_pos.mpr min_pos) hz)
  replace hz := inv_le_of_inv_le min_pos hz
  rcases (âŸ¨Units.mk0 z hnz, Units.val_mk0 hnzâŸ© : IsUnit z) with âŸ¨z, rflâŸ©
  have lt_Î´ : â€–zâ»Â¹ â€¢ aâ€– < Î´ := by
    rw [Units.smul_def, norm_smul, Units.val_inv_eq_inv_val, norm_inv]
    calc
      â€–(z : ğ•œ)â€–â»Â¹ * â€–aâ€– â‰¤ Î´ * (â€–aâ€– + 1)â»Â¹ * â€–aâ€– :=
        mul_le_mul_of_nonneg_right (hz.trans (min_le_left _ _)) (norm_nonneg _)
      _ < Î´ := by
        conv => rw [mul_assoc]; rhs; rw [(mul_one Î´).symm]
        exact mul_lt_mul_of_pos_left
          ((inv_mul_lt_iff haâ‚).mpr ((mul_one (â€–aâ€– + 1)).symm â–¸ lt_add_one _)) Î´_pos
  rw [â† inv_smul_smul z (resolvent a (z : ğ•œ)), units_smul_resolvent_self, resolvent,
    Algebra.algebraMap_eq_smul_one, one_smul, Units.smul_def, norm_smul, Units.val_inv_eq_inv_val,
    norm_inv]
  calc
    _ â‰¤ Îµ * câ»Â¹ * c :=
      mul_le_mul (hz.trans (min_le_right _ _)) (hÎ´ (mem_ball_zero_iff.mpr lt_Î´)) (norm_nonneg _)
        (mul_pos hÎµ (inv_pos.mpr c_pos)).le
    _ = _ := inv_mul_cancel_rightâ‚€ c_pos.ne.symm Îµ
#align spectrum.norm_resolvent_le_forall spectrum.norm_resolvent_le_forall

end resolvent

section OneSubSmul

open ContinuousMultilinearMap ENNReal FormalMultilinearSeries

open scoped NNReal ENNReal

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]

variable (ğ•œ)

/-- In a Banach algebra `A` over a nontrivially normed field `ğ•œ`, for any `a : A` the
power series with coefficients `a ^ n` represents the function `(1 - z â€¢ a)â»Â¹` in a disk of
radius `â€–aâ€–â‚Šâ»Â¹`. -/
theorem hasFPowerSeriesOnBall_inverse_one_sub_smul [CompleteSpace A] (a : A) :
    HasFPowerSeriesOnBall (fun z : ğ•œ => Ring.inverse (1 - z â€¢ a))
      (fun n => ContinuousMultilinearMap.mkPiField ğ•œ (Fin n) (a ^ n)) 0 â€–aâ€–â‚Šâ»Â¹ :=
  { r_le := by
      refine'
        le_of_forall_nnreal_lt fun r hr => le_radius_of_bound_nnreal _ (max 1 â€–(1 : A)â€–â‚Š) fun n => _
      rw [â† norm_toNNReal, norm_mkPiField, norm_toNNReal]
      cases' n with n
      Â· simp only [Nat.zero_eq, le_refl, mul_one, or_true_iff, le_max_iff, pow_zero]
      Â· refine'
          le_trans (le_trans (mul_le_mul_right' (nnnorm_pow_le' a n.succ_pos) (r ^ n.succ)) _)
            (le_max_left _ _)
        Â· by_cases â€–aâ€–â‚Š = 0
          Â· simp only [h, MulZeroClass.zero_mul, zero_le', pow_succ]
          Â· rw [â† coe_inv h, coe_lt_coe, NNReal.lt_inv_iff_mul_lt h] at hr
            simpa only [â† mul_pow, mul_comm] using pow_le_one' hr.le n.succ
    r_pos := ENNReal.inv_pos.mpr coe_ne_top
    hasSum := fun {y} hy => by
      have norm_lt : â€–y â€¢ aâ€– < 1 := by
        by_cases h : â€–aâ€–â‚Š = 0
        Â· simp only [nnnorm_eq_zero.mp h, norm_zero, zero_lt_one, smul_zero]
        Â· have nnnorm_lt : â€–yâ€–â‚Š < â€–aâ€–â‚Šâ»Â¹ := by
            simpa only [â† coe_inv h, mem_ball_zero_iff, Metric.emetric_ball_nnreal] using hy
          rwa [â† coe_nnnorm, â† Real.lt_toNNReal_iff_coe_lt, Real.toNNReal_one, nnnorm_smul,
            â† NNReal.lt_inv_iff_mul_lt h]
      simpa [â† smul_pow, (NormedRing.summable_geometric_of_norm_lt_1 _ norm_lt).hasSum_iff] using
        (NormedRing.inverse_one_sub _ norm_lt).symm }
#align spectrum.has_fpower_series_on_ball_inverse_one_sub_smul spectrum.hasFPowerSeriesOnBall_inverse_one_sub_smul

variable {ğ•œ}

theorem isUnit_one_sub_smul_of_lt_inv_radius {a : A} {z : ğ•œ} (h : â†‘â€–zâ€–â‚Š < (spectralRadius ğ•œ a)â»Â¹) :
    IsUnit (1 - z â€¢ a) := by
  by_cases hz : z = 0
  Â· simp only [hz, isUnit_one, sub_zero, zero_smul]
  Â· let u := Units.mk0 z hz
    suffices hu : IsUnit (uâ»Â¹ â€¢ (1 : A) - a)
    Â· rwa [IsUnit.smul_sub_iff_sub_inv_smul, inv_inv u] at hu
    Â· rw [Units.smul_def, â† Algebra.algebraMap_eq_smul_one, â† mem_resolventSet_iff]
      refine' mem_resolventSet_of_spectralRadius_lt _
      rwa [Units.val_inv_eq_inv_val, nnnorm_inv,
        coe_inv (nnnorm_ne_zero_iff.mpr (Units.val_mk0 hz â–¸ hz : (u : ğ•œ) â‰  0)), lt_inv_iff_lt_inv]
#align spectrum.is_unit_one_sub_smul_of_lt_inv_radius spectrum.isUnit_one_sub_smul_of_lt_inv_radius

/-- In a Banach algebra `A` over `ğ•œ`, for `a : A` the function `fun z â†¦ (1 - z â€¢ a)â»Â¹` is
differentiable on any closed ball centered at zero of radius `r < (spectralRadius ğ•œ a)â»Â¹`. -/
theorem differentiableOn_inverse_one_sub_smul [CompleteSpace A] {a : A} {r : â„â‰¥0}
    (hr : (r : â„â‰¥0âˆ) < (spectralRadius ğ•œ a)â»Â¹) :
    DifferentiableOn ğ•œ (fun z : ğ•œ => Ring.inverse (1 - z â€¢ a)) (Metric.closedBall 0 r) := by
  intro z z_mem
  apply DifferentiableAt.differentiableWithinAt
  have hu : IsUnit (1 - z â€¢ a) := by
    refine' isUnit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono _) hr)
    simpa only [norm_toNNReal, Real.toNNReal_coe] using
      Real.toNNReal_mono (mem_closedBall_zero_iff.mp z_mem)
  have Hâ‚ : Differentiable ğ•œ fun w : ğ•œ => 1 - w â€¢ a := (differentiable_id.smul_const a).const_sub 1
  exact DifferentiableAt.comp z (differentiableAt_inverse hu) Hâ‚.differentiableAt
#align spectrum.differentiable_on_inverse_one_sub_smul spectrum.differentiableOn_inverse_one_sub_smul

end OneSubSmul

section GelfandFormula

open Filter ENNReal ContinuousMultilinearMap

open scoped Topology

variable [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A]

/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
theorem limsup_pow_nnnorm_pow_one_div_le_spectralRadius (a : A) :
    limsup (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop â‰¤ spectralRadius â„‚ a := by
  refine' ENNReal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt fun r r_pos r_lt => _)
  simp_rw [inv_limsup, â† one_div]
  let p : FormalMultilinearSeries â„‚ â„‚ A := fun n =>
    ContinuousMultilinearMap.mkPiField â„‚ (Fin n) (a ^ n)
  suffices h : (r : â„â‰¥0âˆ) â‰¤ p.radius
  Â· convert h
    simp only [p.radius_eq_liminf, â† norm_toNNReal, norm_mkPiField]
    congr
    ext n
    rw [norm_toNNReal, ENNReal.coe_rpow_def â€–a ^ nâ€–â‚Š (1 / n : â„), if_neg]
    exact fun ha => (lt_self_iff_false _).mp
      (ha.2.trans_le (one_div_nonneg.mpr n.cast_nonneg : 0 â‰¤ (1 / n : â„)))
  Â· have Hâ‚ := (differentiableOn_inverse_one_sub_smul r_lt).hasFPowerSeriesOnBall r_pos
    exact ((hasFPowerSeriesOnBall_inverse_one_sub_smul â„‚ a).exchange_radius Hâ‚).r_le
#align spectrum.limsup_pow_nnnorm_pow_one_div_le_spectral_radius spectrum.limsup_pow_nnnorm_pow_one_div_le_spectralRadius

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop (ğ“ (spectralRadius â„‚ a)) :=
  tendsto_of_le_liminf_of_limsup_le (spectralRadius_le_liminf_pow_nnnorm_pow_one_div â„‚ a)
    (limsup_pow_nnnorm_pow_one_div_le_spectralRadius a)
#align spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectral_radius spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => ENNReal.ofReal (â€–a ^ nâ€– ^ (1 / n : â„))) atTop
      (ğ“ (spectralRadius â„‚ a)) := by
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius a using 1
  ext1
  rw [â† ofReal_rpow_of_nonneg (norm_nonneg _) _, â† coe_nnnorm, coe_nnreal_eq]
  exact one_div_nonneg.mpr (by exact_mod_cast zero_le _)
#align spectrum.pow_norm_pow_one_div_tendsto_nhds_spectral_radius spectrum.pow_norm_pow_one_div_tendsto_nhds_spectralRadius

end GelfandFormula

section NonemptySpectrum

variable [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A] [Nontrivial A] (a : A)

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
protected theorem nonempty : (spectrum â„‚ a).Nonempty := by
  /- Suppose `Ïƒ a = âˆ…`, then resolvent set is `â„‚`, any `(z â€¢ 1 - a)` is a unit, and `resolvent`
    is differentiable on `â„‚`. -/
  rw [Set.nonempty_iff_ne_empty]
  by_contra h
  have Hâ‚€ : resolventSet â„‚ a = Set.univ := by rwa [spectrum, Set.compl_empty_iff] at h
  have Hâ‚ : Differentiable â„‚ fun z : â„‚ => resolvent a z := fun z =>
    (hasDerivAt_resolvent (Hâ‚€.symm â–¸ Set.mem_univ z : z âˆˆ resolventSet â„‚ a)).differentiableAt
  /- The norm of the resolvent is small for all sufficiently large `z`, and by compactness and
    continuity it is bounded on the complement of a large ball, thus uniformly bounded on `â„‚`.
    By Liouville's theorem `fun z â†¦ resolvent a z` is constant. -/
  have Hâ‚‚ := norm_resolvent_le_forall (ğ•œ := â„‚) a
  have Hâ‚ƒ : âˆ€ z : â„‚, resolvent a z = resolvent a (0 : â„‚) := by
    refine' fun z => Hâ‚.apply_eq_apply_of_bounded (bounded_iff_forall_norm_le.mpr _) z 0
    rcases Hâ‚‚ 1 zero_lt_one with âŸ¨R, _, hRâŸ©
    rcases (ProperSpace.isCompact_closedBall (0 : â„‚) R).exists_bound_of_continuousOn
        Hâ‚.continuous.continuousOn with
      âŸ¨C, hCâŸ©
    use max C 1
    rintro _ âŸ¨w, rflâŸ©
    refine' Or.elim (em (â€–wâ€– â‰¤ R)) (fun hw => _) fun hw => _
    Â· exact (hC w (mem_closedBall_zero_iff.mpr hw)).trans (le_max_left _ _)
    Â· exact (hR w (not_le.mp hw).le).trans (le_max_right _ _)
  -- `resolvent a 0 = 0`, which is a contradiction because it isn't a unit.
  have Hâ‚… : resolvent a (0 : â„‚) = 0 := by
    refine' norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add fun Îµ hÎµ => _) (norm_nonneg _))
    rcases Hâ‚‚ Îµ hÎµ with âŸ¨R, _, hRâŸ©
    simpa only [Hâ‚ƒ R] using (zero_add Îµ).symm.subst (hR R (by simp [le_abs_self]))
  -- `not_isUnit_zero` is where we need `Nontrivial A`, it is unavoidable.
  exact not_isUnit_zero
    (Hâ‚….subst (isUnit_resolvent.mp (mem_resolventSet_iff.mp (Hâ‚€.symm â–¸ Set.mem_univ 0))))
#align spectrum.nonempty spectrum.nonempty

/-- In a complex Banach algebra, the spectral radius is always attained by some element of the
spectrum. -/
theorem exists_nnnorm_eq_spectralRadius : âˆƒ z âˆˆ spectrum â„‚ a, (â€–zâ€–â‚Š : â„â‰¥0âˆ) = spectralRadius â„‚ a :=
  exists_nnnorm_eq_spectralRadius_of_nonempty (spectrum.nonempty a)
#align spectrum.exists_nnnorm_eq_spectral_radius spectrum.exists_nnnorm_eq_spectralRadius

/-- In a complex Banach algebra, if every element of the spectrum has norm strictly less than
`r : â„â‰¥0`, then the spectral radius is also strictly less than `r`. -/
theorem spectralRadius_lt_of_forall_lt {r : â„â‰¥0} (hr : âˆ€ z âˆˆ spectrum â„‚ a, â€–zâ€–â‚Š < r) :
    spectralRadius â„‚ a < r :=
  spectralRadius_lt_of_forall_lt_of_nonempty (spectrum.nonempty a) hr
#align spectrum.spectral_radius_lt_of_forall_lt spectrum.spectralRadius_lt_of_forall_lt

open scoped Polynomial

open Polynomial

/-- The **spectral mapping theorem** for polynomials in a Banach algebra over `â„‚`. -/
theorem map_polynomial_aeval (p : â„‚[X]) :
    spectrum â„‚ (aeval a p) = (fun k => eval k p) '' spectrum â„‚ a :=
  map_polynomial_aeval_of_nonempty a p (spectrum.nonempty a)
#align spectrum.map_polynomial_aeval spectrum.map_polynomial_aeval

-- Porting note: Replaced `x ^ n` with `HPow.hPow x n`
/-- A specialization of the spectral mapping theorem for polynomials in a Banach algebra over `â„‚`
to monic monomials. -/
protected theorem map_pow (n : â„•) :
    spectrum â„‚ (a ^ n) = (fun x : â„‚ => HPow.hPow x n) '' spectrum â„‚ a := by
  simpa only [aeval_X_pow, eval_pow, eval_X] using map_polynomial_aeval a (X ^ n)
#align spectrum.map_pow spectrum.map_pow

end NonemptySpectrum

section GelfandMazurIsomorphism

variable [NormedRing A] [NormedAlgebra â„‚ A] (hA : âˆ€ {a : A}, IsUnit a â†” a â‰  0)

local notation "Ïƒ" => spectrum â„‚

theorem algebraMap_eq_of_mem {a : A} {z : â„‚} (h : z âˆˆ Ïƒ a) : algebraMap â„‚ A z = a := by
  rwa [mem_iff, hA, Classical.not_not, sub_eq_zero] at h
#align spectrum.algebra_map_eq_of_mem spectrum.algebraMap_eq_of_mem

/-- **Gelfand-Mazur theorem**: For a complex Banach division algebra, the natural `algebraMap â„‚ A`
is an algebra isomorphism whose inverse is given by selecting the (unique) element of
`spectrum â„‚ a`. In addition, `algebraMap_isometry` guarantees this map is an isometry.

Note: because `NormedDivisionRing` requires the field `norm_mul' : âˆ€ a b, â€–a * bâ€– = â€–aâ€– * â€–bâ€–`, we
don't use this type class and instead opt for a `NormedRing` in which the nonzero elements are
precisely the units. This allows for the application of this isomorphism in broader contexts, e.g.,
to the quotient of a complex Banach algebra by a maximal ideal. In the case when `A` is actually a
`NormedDivisionRing`, one may fill in the argument `hA` with the lemma `isUnit_iff_ne_zero`. -/
@[simps]
noncomputable def _root_.NormedRing.algEquivComplexOfComplete [CompleteSpace A] : â„‚ â‰ƒâ‚[â„‚] A :=
  let nt : Nontrivial A := âŸ¨âŸ¨1, 0, hA.mp âŸ¨âŸ¨1, 1, mul_one _, mul_one _âŸ©, rflâŸ©âŸ©âŸ©
  { Algebra.ofId â„‚ A with
    toFun := algebraMap â„‚ A
    invFun := fun a => (@spectrum.nonempty _ _ _ _ nt a).some
    left_inv := fun z => by
      simpa only [@scalar_eq _ _ _ _ _ nt _] using
        (@spectrum.nonempty _ _ _ _ nt <| algebraMap â„‚ A z).some_mem
    right_inv := fun a => algebraMap_eq_of_mem (@hA) (@spectrum.nonempty _ _ _ _ nt a).some_mem }
#align normed_ring.alg_equiv_complex_of_complete NormedRing.algEquivComplexOfComplete

end GelfandMazurIsomorphism

section ExpMapping

local notation "â†‘â‚" => algebraMap ğ•œ A

/-- For `ğ•œ = â„` or `ğ•œ = â„‚`, `exp ğ•œ` maps the spectrum of `a` into the spectrum of `exp ğ•œ a`. -/
theorem exp_mem_exp [IsROrC ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] (a : A) {z : ğ•œ}
    (hz : z âˆˆ spectrum ğ•œ a) : exp ğ•œ z âˆˆ spectrum ğ•œ (exp ğ•œ a) := by
  have hexpmul : exp ğ•œ a = exp ğ•œ (a - â†‘â‚ z) * â†‘â‚ (exp ğ•œ z) := by
    rw [algebraMap_exp_comm z, â† exp_add_of_commute (Algebra.commutes z (a - â†‘â‚ z)).symm,
      sub_add_cancel]
  let b := âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ n
  have hb : Summable fun n : â„• => ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ n := by
    refine' summable_of_norm_bounded_eventually _ (Real.summable_pow_div_factorial â€–a - â†‘â‚ zâ€–) _
    filter_upwards [Filter.eventually_cofinite_ne 0] with n hn
    rw [norm_smul, mul_comm, norm_inv, IsROrC.norm_natCast, â† div_eq_mul_inv]
    exact div_le_div (pow_nonneg (norm_nonneg _) n) (norm_pow_le' (a - â†‘â‚ z) (zero_lt_iff.mpr hn))
      (by exact_mod_cast Nat.factorial_pos n) (by exact_mod_cast Nat.factorial_le (lt_add_one n).le)
  have hâ‚€ : (âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ (n + 1)) = (a - â†‘â‚ z) * b := by
    simpa only [mul_smul_comm, pow_succ] using hb.tsum_mul_left (a - â†‘â‚ z)
  have hâ‚ : (âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ (n + 1)) = b * (a - â†‘â‚ z) := by
    simpa only [pow_succ', Algebra.smul_mul_assoc] using hb.tsum_mul_right (a - â†‘â‚ z)
  have hâ‚ƒ : exp ğ•œ (a - â†‘â‚ z) = 1 + (a - â†‘â‚ z) * b := by
    rw [exp_eq_tsum]
    convert tsum_eq_zero_add (expSeries_summable' (ğ•‚ := ğ•œ) (a - â†‘â‚ z))
    simp only [Nat.factorial_zero, Nat.cast_one, inv_one, pow_zero, one_smul]
    exact hâ‚€.symm
  rw [spectrum.mem_iff, IsUnit.sub_iff, â† one_mul (â†‘â‚ (exp ğ•œ z)), hexpmul, â† _root_.sub_mul,
    Commute.isUnit_mul_iff (Algebra.commutes (exp ğ•œ z) (exp ğ•œ (a - â†‘â‚ z) - 1)).symm,
    sub_eq_iff_eq_add'.mpr hâ‚ƒ, Commute.isUnit_mul_iff (hâ‚€ â–¸ hâ‚ : (a - â†‘â‚ z) * b = b * (a - â†‘â‚ z))]
  exact not_and_of_not_left _ (not_and_of_not_left _ ((not_iff_not.mpr IsUnit.sub_iff).mp hz))
#align spectrum.exp_mem_exp spectrum.exp_mem_exp

end ExpMapping

end spectrum

namespace AlgHom

section NormedField

variable {F : Type*} [NormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "â†‘â‚" => algebraMap ğ•œ A

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). See note [lower instance priority] -/
instance (priority := 100) [AlgHomClass F ğ•œ A ğ•œ] : ContinuousLinearMapClass F ğ•œ A ğ•œ :=
  { AlgHomClass.linearMapClass with
    map_continuous := fun Ï† =>
      AddMonoidHomClass.continuous_of_bound Ï† â€–(1 : A)â€– fun a =>
        mul_comm â€–aâ€– â€–(1 : A)â€– â–¸ spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum Ï† _) }

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
def toContinuousLinearMap (Ï† : A â†’â‚[ğ•œ] ğ•œ) : A â†’L[ğ•œ] ğ•œ :=
  { Ï†.toLinearMap with cont := map_continuous Ï† }
#align alg_hom.to_continuous_linear_map AlgHom.toContinuousLinearMap

@[simp]
theorem coe_toContinuousLinearMap (Ï† : A â†’â‚[ğ•œ] ğ•œ) : â‡‘Ï†.toContinuousLinearMap = Ï† :=
  rfl
#align alg_hom.coe_to_continuous_linear_map AlgHom.coe_toContinuousLinearMap

theorem norm_apply_le_self_mul_norm_one [AlgHomClass F ğ•œ A ğ•œ] (f : F) (a : A) :
    â€–f aâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
  spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum f _)
#align alg_hom.norm_apply_le_self_mul_norm_one AlgHom.norm_apply_le_self_mul_norm_one

theorem norm_apply_le_self [NormOneClass A] [AlgHomClass F ğ•œ A ğ•œ] (f : F) (a : A) : â€–f aâ€– â‰¤ â€–aâ€– :=
  spectrum.norm_le_norm_of_mem (apply_mem_spectrum f _)
#align alg_hom.norm_apply_le_self AlgHom.norm_apply_le_self

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "â†‘â‚" => algebraMap ğ•œ A

@[simp]
theorem toContinuousLinearMap_norm [NormOneClass A] (Ï† : A â†’â‚[ğ•œ] ğ•œ) :
    â€–Ï†.toContinuousLinearMapâ€– = 1 :=
  ContinuousLinearMap.op_norm_eq_of_bounds zero_le_one
    (fun a => (one_mul â€–aâ€–).symm â–¸ spectrum.norm_le_norm_of_mem (apply_mem_spectrum Ï† _))
    fun _ _ h => by simpa only [coe_toContinuousLinearMap, map_one, norm_one, mul_one] using h 1
#align alg_hom.to_continuous_linear_map_norm AlgHom.toContinuousLinearMap_norm

end NontriviallyNormedField

end AlgHom

namespace WeakDual

namespace CharacterSpace

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [CompleteSpace A]

variable [NormedAlgebra ğ•œ A]

/-- The equivalence between characters and algebra homomorphisms into the base field. -/
def equivAlgHom : characterSpace ğ•œ A â‰ƒ (A â†’â‚[ğ•œ] ğ•œ) where
  toFun := toAlgHom
  invFun f :=
    { val := f.toContinuousLinearMap
      property := by rw [eq_set_map_one_map_mul]; exact âŸ¨map_one f, map_mul fâŸ© }
  left_inv f := Subtype.ext <| ContinuousLinearMap.ext fun x => rfl
  right_inv f := AlgHom.ext fun x => rfl
#align weak_dual.character_space.equiv_alg_hom WeakDual.CharacterSpace.equivAlgHom

@[simp]
theorem equivAlgHom_coe (f : characterSpace ğ•œ A) : â‡‘(equivAlgHom f) = f :=
  rfl
#align weak_dual.character_space.equiv_alg_hom_coe WeakDual.CharacterSpace.equivAlgHom_coe

@[simp]
theorem equivAlgHom_symm_coe (f : A â†’â‚[ğ•œ] ğ•œ) : â‡‘(equivAlgHom.symm f) = f :=
  rfl
#align weak_dual.character_space.equiv_alg_hom_symm_coe WeakDual.CharacterSpace.equivAlgHom_symm_coe

end CharacterSpace

end WeakDual
