/-
Copyright (c) 2022 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Normed.Group.Completion
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace
import Mathlib.Topology.Algebra.UniformRing

#align_import analysis.normed_space.completion from "leanprover-community/mathlib"@"d3af0609f6db8691dffdc3e1fb7feb7da72698f2"

/-!
# Normed space structure on the completion of a normed space

If `E` is a normed space over `ùïú`, then so is `UniformSpace.Completion E`. In this file we provide
necessary instances and define `UniformSpace.Completion.toCompl‚Çó·µ¢` - coercion
`E ‚Üí UniformSpace.Completion E` as a bundled linear isometry.

We also show that if `A` is a normed algebra over `ùïú`, then so is `UniformSpace.Completion A`.

TODO: Generalise the results here from the concrete `completion` to any `AbstractCompletion`.
-/


noncomputable section

namespace UniformSpace

namespace Completion

variable (ùïú E : Type*) [NormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]

instance (priority := 100) NormedSpace.to_uniformContinuousConstSMul :
    UniformContinuousConstSMul ùïú E :=
  ‚ü®fun c => (lipschitzWith_smul c).uniformContinuous‚ü©
#align uniform_space.completion.normed_space.to_has_uniform_continuous_const_smul UniformSpace.Completion.NormedSpace.to_uniformContinuousConstSMul

instance : NormedSpace ùïú (Completion E) :=
  { Completion.instModule with
    norm_smul_le := fun c x =>
      induction_on x
        (isClosed_le (continuous_const_smul _).norm (continuous_const.mul continuous_norm)) fun y =>
        by simp only [‚Üê coe_smul, norm_coe, norm_smul, le_rfl] }

variable {ùïú E}

/-- Embedding of a normed space to its completion as a linear isometry. -/
def toCompl‚Çó·µ¢ : E ‚Üí‚Çó·µ¢[ùïú] Completion E :=
  { toCompl with
    toFun := (‚Üë)
    map_smul' := coe_smul
    norm_map' := norm_coe }
#align uniform_space.completion.to_compl‚Çó·µ¢ UniformSpace.Completion.toCompl‚Çó·µ¢

@[simp]
theorem coe_toCompl‚Çó·µ¢ : ‚áë(toCompl‚Çó·µ¢ : E ‚Üí‚Çó·µ¢[ùïú] Completion E) = ((‚Üë) : E ‚Üí Completion E) :=
  rfl
#align uniform_space.completion.coe_to_compl‚Çó·µ¢ UniformSpace.Completion.coe_toCompl‚Çó·µ¢

/-- Embedding of a normed space to its completion as a continuous linear map. -/
def toComplL : E ‚ÜíL[ùïú] Completion E :=
  toCompl‚Çó·µ¢.toContinuousLinearMap
set_option linter.uppercaseLean3 false in
#align uniform_space.completion.to_complL UniformSpace.Completion.toComplL

@[simp]
theorem coe_toComplL : ‚áë(toComplL : E ‚ÜíL[ùïú] Completion E) = ((‚Üë) : E ‚Üí Completion E) :=
  rfl
set_option linter.uppercaseLean3 false in
#align uniform_space.completion.coe_to_complL UniformSpace.Completion.coe_toComplL

@[simp]
theorem norm_toComplL {ùïú E : Type*} [NontriviallyNormedField ùïú] [NormedAddCommGroup E]
    [NormedSpace ùïú E] [Nontrivial E] : ‚Äñ(toComplL : E ‚ÜíL[ùïú] Completion E)‚Äñ = 1 :=
  (toCompl‚Çó·µ¢ : E ‚Üí‚Çó·µ¢[ùïú] Completion E).norm_toContinuousLinearMap
set_option linter.uppercaseLean3 false in
#align uniform_space.completion.norm_to_complL UniformSpace.Completion.norm_toComplL

section Algebra

variable (ùïú) (A : Type*)

instance [SeminormedRing A] : NormedRing (Completion A) :=
  { Completion.ring,
    Completion.instMetricSpace with
    dist_eq := fun x y => by
      refine Completion.induction_on‚ÇÇ x y ?_ ?_ <;> clear x y
      ¬∑ refine' isClosed_eq (Completion.uniformContinuous_extension‚ÇÇ _).continuous _
        exact Continuous.comp Completion.continuous_extension continuous_sub
      ¬∑ intro x y
        rw [‚Üê Completion.coe_sub, norm_coe, Completion.dist_eq, dist_eq_norm]
    norm_mul := fun x y => by
      refine Completion.induction_on‚ÇÇ x y ?_ ?_ <;> clear x y
      ¬∑ exact
          isClosed_le (Continuous.comp continuous_norm continuous_mul)
            (Continuous.comp _root_.continuous_mul
              (Continuous.prod_map continuous_norm continuous_norm))
      ¬∑ intro x y
        simp only [‚Üê coe_mul, norm_coe]
        exact norm_mul_le x y }

instance [SeminormedCommRing A] [NormedAlgebra ùïú A] [UniformContinuousConstSMul ùïú A] :
    NormedAlgebra ùïú (Completion A) :=
  { Completion.algebra A ùïú with
    norm_smul_le := fun r x => by
      refine Completion.induction_on x ?_ ?_ <;> clear x
      ¬∑ exact
          isClosed_le (Continuous.comp continuous_norm (continuous_const_smul r))
            (Continuous.comp (continuous_mul_left _) continuous_norm)
      ¬∑ intro x
        simp only [‚Üê coe_smul, norm_coe]
        exact norm_smul_le r x }

end Algebra

end Completion

end UniformSpace
