/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Topology.Instances.RealVectorSpace
import Mathlib.Analysis.NormedSpace.AffineIsometry

#align_import analysis.normed_space.mazur_ulam from "leanprover-community/mathlib"@"78261225eb5cedc61c5c74ecb44e5b385d13b733"

/-!
# Mazur-Ulam Theorem

Mazur-Ulam theorem states that an isometric bijection between two normed affine spaces over `‚Ñù` is
affine. We formalize it in three definitions:

* `IsometryEquiv.toRealLinearIsometryEquivOfMapZero` : given `E ‚âÉ·µ¢ F` sending `0` to `0`,
  returns `E ‚âÉ‚Çó·µ¢[‚Ñù] F` with the same `toFun` and `invFun`;
* `IsometryEquiv.toRealLinearIsometryEquiv` : given `f : E ‚âÉ·µ¢ F`, returns a linear isometry
  equivalence `g : E ‚âÉ‚Çó·µ¢[‚Ñù] F` with `g x = f x - f 0`.
* `IsometryEquiv.toRealAffineIsometryEquiv` : given `f : PE ‚âÉ·µ¢ PF`, returns an affine isometry
  equivalence `g : PE ‚âÉ·µÉ‚Å±[‚Ñù] PF` whose underlying `IsometryEquiv` is `f`

The formalization is based on [Jussi V√§is√§l√§, *A Proof of the Mazur-Ulam Theorem*][Vaisala_2003].

## Tags

isometry, affine map, linear map
-/


variable {E PE F PF : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [MetricSpace PE]
  [NormedAddTorsor E PE] [NormedAddCommGroup F] [NormedSpace ‚Ñù F] [MetricSpace PF]
  [NormedAddTorsor F PF]

open Set AffineMap AffineIsometryEquiv

noncomputable section

namespace IsometryEquiv

/-- If an isometric self-homeomorphism of a normed vector space over `‚Ñù` fixes `x` and `y`,
then it fixes the midpoint of `[x, y]`. This is a lemma for a more general Mazur-Ulam theorem,
see below. -/
theorem midpoint_fixed {x y : PE} :
    ‚àÄ e : PE ‚âÉ·µ¢ PE, e x = x ‚Üí e y = y ‚Üí e (midpoint ‚Ñù x y) = midpoint ‚Ñù x y := by
  set z := midpoint ‚Ñù x y
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  -- Consider the set of `e : E ‚âÉ·µ¢ E` such that `e x = x` and `e y = y`
  set s := { e : PE ‚âÉ·µ¢ PE | e x = x ‚àß e y = y }
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  haveI : Nonempty s := ‚ü®‚ü®IsometryEquiv.refl PE, rfl, rfl‚ü©‚ü©
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  -- On the one hand, `e` cannot send the midpoint `z` of `[x, y]` too far
  have h_bdd : BddAbove (range fun e : s => dist ((e : PE ‚âÉ·µ¢ PE) z) z) := by
    refine' ‚ü®dist x z + dist x z, forall_range_iff.2 <| Subtype.forall.2 _‚ü©
    rintro e ‚ü®hx, _‚ü©
    calc
      dist (e z) z ‚â§ dist (e z) x + dist x z := dist_triangle (e z) x z
      _ = dist (e x) (e z) + dist x z := by rw [hx, dist_comm]
      _ = dist x z + dist x z := by erw [e.dist_eq x z]
  -- On the other hand, consider the map `f : (E ‚âÉ·µ¢ E) ‚Üí (E ‚âÉ·µ¢ E)`
  -- sending each `e` to `R ‚àò e‚Åª¬π ‚àò R ‚àò e`, where `R` is the point reflection in the
  -- midpoint `z` of `[x, y]`.
  set R : PE ‚âÉ·µ¢ PE := (pointReflection ‚Ñù z).toIsometryEquiv
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  set f : PE ‚âÉ·µ¢ PE ‚Üí PE ‚âÉ·µ¢ PE := fun e => ((e.trans R).trans e.symm).trans R
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  -- Note that `f` doubles the value of `dist (e z) z`
  have hf_dist : ‚àÄ e, dist (f e z) z = 2 * dist (e z) z := by
    intro e
    dsimp
    rw [dist_pointReflection_fixed, ‚Üê e.dist_eq, e.apply_symm_apply,
      dist_pointReflection_self_real, dist_comm]
  -- Also note that `f` maps `s` to itself
  have hf_maps_to : MapsTo f s s := by
    rintro e ‚ü®hx, hy‚ü©
    constructor <;> simp [hx, hy, e.symm_apply_eq.2 hx.symm, e.symm_apply_eq.2 hy.symm]
  -- Therefore, `dist (e z) z = 0` for all `e ‚àà s`.
  set c := ‚®Ü e : s, dist ((e : PE ‚âÉ·µ¢ PE) z) z
  -- ‚ä¢ ‚àÄ (e : PE ‚âÉ·µ¢ PE), ‚Üëe x = x ‚Üí ‚Üëe y = y ‚Üí ‚Üëe z = z
  have : c ‚â§ c / 2 := by
    apply ciSup_le
    rintro ‚ü®e, he‚ü©
    simp only [Subtype.coe_mk, le_div_iff' (zero_lt_two' ‚Ñù), ‚Üê hf_dist]
    exact le_ciSup h_bdd ‚ü®f e, hf_maps_to he‚ü©
  replace : c ‚â§ 0
  -- ‚ä¢ c ‚â§ 0
  ¬∑ linarith
    -- üéâ no goals
  refine' fun e hx hy => dist_le_zero.1 (le_trans _ this)
  -- ‚ä¢ dist (‚Üëe z) z ‚â§ c
  exact le_ciSup h_bdd ‚ü®e, hx, hy‚ü©
  -- üéâ no goals
#align isometry_equiv.midpoint_fixed IsometryEquiv.midpoint_fixed

/-- A bijective isometry sends midpoints to midpoints. -/
theorem map_midpoint (f : PE ‚âÉ·µ¢ PF) (x y : PE) : f (midpoint ‚Ñù x y) = midpoint ‚Ñù (f x) (f y) := by
  set e : PE ‚âÉ·µ¢ PE :=
    ((f.trans <| (pointReflection ‚Ñù <| midpoint ‚Ñù (f x) (f y)).toIsometryEquiv).trans f.symm).trans
      (pointReflection ‚Ñù <| midpoint ‚Ñù x y).toIsometryEquiv
  have hx : e x = x := by simp
  -- ‚ä¢ ‚Üëf (midpoint ‚Ñù x y) = midpoint ‚Ñù (‚Üëf x) (‚Üëf y)
  have hy : e y = y := by simp
  -- ‚ä¢ ‚Üëf (midpoint ‚Ñù x y) = midpoint ‚Ñù (‚Üëf x) (‚Üëf y)
  have hm := e.midpoint_fixed hx hy
  -- ‚ä¢ ‚Üëf (midpoint ‚Ñù x y) = midpoint ‚Ñù (‚Üëf x) (‚Üëf y)
  simp only [trans_apply] at hm
  -- ‚ä¢ ‚Üëf (midpoint ‚Ñù x y) = midpoint ‚Ñù (‚Üëf x) (‚Üëf y)
  rwa [‚Üê eq_symm_apply, toIsometryEquiv_symm, pointReflection_symm, coe_toIsometryEquiv,
    coe_toIsometryEquiv, pointReflection_self, symm_apply_eq, @pointReflection_fixed_iff] at hm
#align isometry_equiv.map_midpoint IsometryEquiv.map_midpoint

/-!
Since `f : PE ‚âÉ·µ¢ PF` sends midpoints to midpoints, it is an affine map.
We define a conversion to a `ContinuousLinearEquiv` first, then a conversion to an `AffineMap`.
-/


/-- **Mazur-Ulam Theorem**: if `f` is an isometric bijection between two normed vector spaces
over `‚Ñù` and `f 0 = 0`, then `f` is a linear isometry equivalence. -/
def toRealLinearIsometryEquivOfMapZero (f : E ‚âÉ·µ¢ F) (h0 : f 0 = 0) : E ‚âÉ‚Çó·µ¢[‚Ñù] F :=
  { (AddMonoidHom.ofMapMidpoint ‚Ñù ‚Ñù f h0 f.map_midpoint).toRealLinearMap f.continuous, f with
    norm_map' := fun x => show ‚Äñf x‚Äñ = ‚Äñx‚Äñ by simp only [‚Üê dist_zero_right, ‚Üê h0, f.dist_eq] }
                                              -- üéâ no goals
#align isometry_equiv.to_real_linear_isometry_equiv_of_map_zero IsometryEquiv.toRealLinearIsometryEquivOfMapZero

@[simp]
theorem coe_toRealLinearIsometryEquivOfMapZero (f : E ‚âÉ·µ¢ F) (h0 : f 0 = 0) :
    ‚áë(f.toRealLinearIsometryEquivOfMapZero h0) = f :=
  rfl
#align isometry_equiv.coe_to_real_linear_equiv_of_map_zero IsometryEquiv.coe_toRealLinearIsometryEquivOfMapZero

@[simp]
theorem coe_toRealLinearIsometryEquivOfMapZero_symm (f : E ‚âÉ·µ¢ F) (h0 : f 0 = 0) :
    ‚áë(f.toRealLinearIsometryEquivOfMapZero h0).symm = f.symm :=
  rfl
#align isometry_equiv.coe_to_real_linear_equiv_of_map_zero_symm IsometryEquiv.coe_toRealLinearIsometryEquivOfMapZero_symm

/-- **Mazur-Ulam Theorem**: if `f` is an isometric bijection between two normed vector spaces
over `‚Ñù`, then `x ‚Ü¶ f x - f 0` is a linear isometry equivalence. -/
def toRealLinearIsometryEquiv (f : E ‚âÉ·µ¢ F) : E ‚âÉ‚Çó·µ¢[‚Ñù] F :=
  (f.trans (IsometryEquiv.addRight (f 0)).symm).toRealLinearIsometryEquivOfMapZero
    (by simpa only [sub_eq_add_neg] using sub_self (f 0))
        -- üéâ no goals
#align isometry_equiv.to_real_linear_isometry_equiv IsometryEquiv.toRealLinearIsometryEquiv

@[simp]
theorem toRealLinearIsometryEquiv_apply (f : E ‚âÉ·µ¢ F) (x : E) :
    (f.toRealLinearIsometryEquiv : E ‚Üí F) x = f x - f 0 :=
  (sub_eq_add_neg (f x) (f 0)).symm
#align isometry_equiv.to_real_linear_equiv_apply IsometryEquiv.toRealLinearIsometryEquiv_apply

@[simp]
theorem toRealLinearIsometryEquiv_symm_apply (f : E ‚âÉ·µ¢ F) (y : F) :
    (f.toRealLinearIsometryEquiv.symm : F ‚Üí E) y = f.symm (y + f 0) :=
  rfl
#align isometry_equiv.to_real_linear_isometry_equiv_symm_apply IsometryEquiv.toRealLinearIsometryEquiv_symm_apply

/-- **Mazur-Ulam Theorem**: if `f` is an isometric bijection between two normed add-torsors over
normed vector spaces over `‚Ñù`, then `f` is an affine isometry equivalence. -/
def toRealAffineIsometryEquiv (f : PE ‚âÉ·µ¢ PF) : PE ‚âÉ·µÉ‚Å±[‚Ñù] PF :=
  AffineIsometryEquiv.mk' f
    ((vaddConst (Classical.arbitrary PE)).trans <|
        f.trans (vaddConst (f <| Classical.arbitrary PE)).symm).toRealLinearIsometryEquiv
    (Classical.arbitrary PE) fun p => by simp
                                         -- üéâ no goals
#align isometry_equiv.to_real_affine_isometry_equiv IsometryEquiv.toRealAffineIsometryEquiv

@[simp]
theorem coeFn_toRealAffineIsometryEquiv (f : PE ‚âÉ·µ¢ PF) : ‚áëf.toRealAffineIsometryEquiv = f :=
  rfl
#align isometry_equiv.coe_fn_to_real_affine_isometry_equiv IsometryEquiv.coeFn_toRealAffineIsometryEquiv

@[simp]
theorem coe_toRealAffineIsometryEquiv (f : PE ‚âÉ·µ¢ PF) :
    f.toRealAffineIsometryEquiv.toIsometryEquiv = f := by
  ext
  -- ‚ä¢ ‚Üë(toIsometryEquiv (toRealAffineIsometryEquiv f)) x‚úù = ‚Üëf x‚úù
  rfl
  -- üéâ no goals
#align isometry_equiv.coe_to_real_affine_isometry_equiv IsometryEquiv.coe_toRealAffineIsometryEquiv

end IsometryEquiv
