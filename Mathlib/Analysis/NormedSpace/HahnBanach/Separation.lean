/-
Copyright (c) 2022 Bhavik Mehta. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Bhavik Mehta, YaÃ«l Dillies
-/
import Mathlib.Analysis.Convex.Cone.Extension
import Mathlib.Analysis.Convex.Gauge
import Mathlib.Topology.Algebra.Module.FiniteDimension
import Mathlib.Topology.Algebra.Module.LocallyConvex
import Mathlib.Analysis.RCLike.Basic

/-!
# Separation Hahn-Banach theorem

In this file we prove the geometric Hahn-Banach theorem. For any two disjoint convex sets, there
exists a continuous linear functional separating them, geometrically meaning that we can intercalate
a plane between them.

We provide many variations to stricten the result under more assumptions on the convex sets:
* `geometric_hahn_banach_open`: One set is open. Weak separation.
* `geometric_hahn_banach_open_point`, `geometric_hahn_banach_point_open`: One set is open, the
  other is a singleton. Weak separation.
* `geometric_hahn_banach_open_open`: Both sets are open. Semistrict separation.
* `geometric_hahn_banach_compact_closed`, `geometric_hahn_banach_closed_compact`: One set is closed,
  the other one is compact. Strict separation.
* `geometric_hahn_banach_point_closed`, `geometric_hahn_banach_closed_point`: One set is closed, the
  other one is a singleton. Strict separation.
* `geometric_hahn_banach_point_point`: Both sets are singletons. Strict separation.

## TODO

* Eidelheit's theorem
* `Convex â„ s â†’ interior (closure s) âŠ† s`
-/


open Set

open Pointwise

variable {ğ•œ E : Type*}

/-- Given a set `s` which is a convex neighbourhood of `0` and a point `xâ‚€` outside of it, there is
a continuous linear functional `f` separating `xâ‚€` and `s`, in the sense that it sends `xâ‚€` to 1 and
all of `s` to values strictly below `1`. -/
theorem separate_convex_open_set [TopologicalSpace E] [AddCommGroup E] [TopologicalAddGroup E]
    [Module â„ E] [ContinuousSMul â„ E] {s : Set E} (hsâ‚€ : (0 : E) âˆˆ s) (hsâ‚ : Convex â„ s)
    (hsâ‚‚ : IsOpen s) {xâ‚€ : E} (hxâ‚€ : xâ‚€ âˆ‰ s) : âˆƒ f : E â†’L[â„] â„, f xâ‚€ = 1 âˆ§ âˆ€ x âˆˆ s, f x < 1 := by
  let f : E â†’â‚—.[â„] â„ := LinearPMap.mkSpanSingleton xâ‚€ 1 (ne_of_mem_of_not_mem hsâ‚€ hxâ‚€).symm
  have := exists_extension_of_le_sublinear f (gauge s) (fun c hc => gauge_smul_of_nonneg hc.le)
    (gauge_add_le hsâ‚ <| absorbent_nhds_zero <| hsâ‚‚.mem_nhds hsâ‚€) ?_
  Â· obtain âŸ¨Ï†, hÏ†â‚, hÏ†â‚‚âŸ© := this
    have hÏ†â‚ƒ : Ï† xâ‚€ = 1 := by
      rw [â† f.domain.coe_mk xâ‚€ (Submodule.mem_span_singleton_self _), hÏ†â‚,
        LinearPMap.mkSpanSingleton'_apply_self]
    have hÏ†â‚„ : âˆ€ x âˆˆ s, Ï† x < 1 := fun x hx =>
      (hÏ†â‚‚ x).trans_lt (gauge_lt_one_of_mem_of_isOpen hsâ‚‚ hx)
    refine âŸ¨âŸ¨Ï†, ?_âŸ©, hÏ†â‚ƒ, hÏ†â‚„âŸ©
    refine
      Ï†.continuous_of_nonzero_on_open _ (hsâ‚‚.vadd (-xâ‚€)) (Nonempty.vadd_set âŸ¨0, hsâ‚€âŸ©)
        (vadd_set_subset_iff.mpr fun x hx => ?_)
    change Ï† (-xâ‚€ + x) â‰  0
    rw [map_add, map_neg]
    specialize hÏ†â‚„ x hx
    linarith
  rintro âŸ¨x, hxâŸ©
  obtain âŸ¨y, rflâŸ© := Submodule.mem_span_singleton.1 hx
  rw [LinearPMap.mkSpanSingleton'_apply]
  simp only [mul_one, Algebra.id.smul_eq_mul, Submodule.coe_mk]
  obtain h | h := le_or_lt y 0
  Â· exact h.trans (gauge_nonneg _)
  Â· rw [gauge_smul_of_nonneg h.le, smul_eq_mul, le_mul_iff_one_le_right h]
    exact
      one_le_gauge_of_not_mem (hsâ‚.starConvex hsâ‚€)
        (absorbent_nhds_zero <| hsâ‚‚.mem_nhds hsâ‚€).absorbs hxâ‚€

variable [TopologicalSpace E] [AddCommGroup E] [TopologicalAddGroup E] [Module â„ E]
  [ContinuousSMul â„ E] {s t : Set E} {x y : E}

/-- A version of the **Hahn-Banach theorem**: given disjoint convex sets `s`, `t` where `s` is open,
there is a continuous linear functional which separates them. -/
theorem geometric_hahn_banach_open (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsOpen s) (ht : Convex â„ t)
    (disj : Disjoint s t) : âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ âˆ€ b âˆˆ t, u â‰¤ f b := by
  obtain rfl | âŸ¨aâ‚€, haâ‚€âŸ© := s.eq_empty_or_nonempty
  Â· exact âŸ¨0, 0, by simp, fun b _hb => le_rflâŸ©
  obtain rfl | âŸ¨bâ‚€, hbâ‚€âŸ© := t.eq_empty_or_nonempty
  Â· exact âŸ¨0, 1, fun a _ha => zero_lt_one, by simpâŸ©
  let xâ‚€ := bâ‚€ - aâ‚€
  let C := xâ‚€ +áµ¥ (s - t)
  have : (0 : E) âˆˆ C :=
    âŸ¨aâ‚€ - bâ‚€, sub_mem_sub haâ‚€ hbâ‚€, by simp_rw [xâ‚€, vadd_eq_add, sub_add_sub_cancel', sub_self]âŸ©
  have : Convex â„ C := (hsâ‚.sub ht).vadd _
  have : xâ‚€ âˆ‰ C := by
    intro hxâ‚€
    rw [â† add_zero xâ‚€] at hxâ‚€
    exact disj.zero_not_mem_sub_set (vadd_mem_vadd_set_iff.1 hxâ‚€)
  obtain âŸ¨f, hfâ‚, hfâ‚‚âŸ© := separate_convex_open_set â€¹0 âˆˆ Câ€º â€¹_â€º (hsâ‚‚.sub_right.vadd _) â€¹xâ‚€ âˆ‰ Câ€º
  have : f bâ‚€ = f aâ‚€ + 1 := by simp [xâ‚€, â† hfâ‚]
  have forall_le : âˆ€ a âˆˆ s, âˆ€ b âˆˆ t, f a â‰¤ f b := by
    intro a ha b hb
    have := hfâ‚‚ (xâ‚€ + (a - b)) (vadd_mem_vadd_set <| sub_mem_sub ha hb)
    simp only [f.map_add, f.map_sub, hfâ‚] at this
    linarith
  refine âŸ¨f, sInf (f '' t), image_subset_iff.1 (?_ : f '' s âŠ† Iio (sInf (f '' t))), fun b hb => ?_âŸ©
  Â· rw [â† interior_Iic]
    refine interior_maximal (image_subset_iff.2 fun a ha => ?_) (f.isOpenMap_of_ne_zero ?_ _ hsâ‚‚)
    Â· exact le_csInf (Nonempty.image _ âŸ¨_, hbâ‚€âŸ©) (forall_mem_image.2 <| forall_le _ ha)
    Â· rintro rfl
      simp at hfâ‚
  Â· exact csInf_le âŸ¨f aâ‚€, forall_mem_image.2 <| forall_le _ haâ‚€âŸ© (mem_image_of_mem _ hb)

theorem geometric_hahn_banach_open_point (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsOpen s) (disj : x âˆ‰ s) :
    âˆƒ f : E â†’L[â„] â„, âˆ€ a âˆˆ s, f a < f x :=
  let âŸ¨f, _s, hs, hxâŸ© :=
    geometric_hahn_banach_open hsâ‚ hsâ‚‚ (convex_singleton x) (disjoint_singleton_right.2 disj)
  âŸ¨f, fun a ha => lt_of_lt_of_le (hs a ha) (hx x (mem_singleton _))âŸ©

theorem geometric_hahn_banach_point_open (htâ‚ : Convex â„ t) (htâ‚‚ : IsOpen t) (disj : x âˆ‰ t) :
    âˆƒ f : E â†’L[â„] â„, âˆ€ b âˆˆ t, f x < f b :=
  let âŸ¨f, hfâŸ© := geometric_hahn_banach_open_point htâ‚ htâ‚‚ disj
  âŸ¨-f, by simpaâŸ©

theorem geometric_hahn_banach_open_open (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsOpen s) (htâ‚ : Convex â„ t)
    (htâ‚ƒ : IsOpen t) (disj : Disjoint s t) :
    âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ âˆ€ b âˆˆ t, u < f b := by
  obtain rfl | âŸ¨aâ‚€, haâ‚€âŸ© := s.eq_empty_or_nonempty
  Â· exact âŸ¨0, -1, by simp, fun b _hb => by norm_numâŸ©
  obtain rfl | âŸ¨bâ‚€, hbâ‚€âŸ© := t.eq_empty_or_nonempty
  Â· exact âŸ¨0, 1, fun a _ha => by norm_num, by simpâŸ©
  obtain âŸ¨f, s, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open hsâ‚ hsâ‚‚ htâ‚ disj
  have hf : IsOpenMap f := by
    refine f.isOpenMap_of_ne_zero ?_
    rintro rfl
    simp_rw [ContinuousLinearMap.zero_apply] at hfâ‚ hfâ‚‚
    exact (hfâ‚ _ haâ‚€).not_le (hfâ‚‚ _ hbâ‚€)
  refine âŸ¨f, s, hfâ‚, image_subset_iff.1 (?_ : f '' t âŠ† Ioi s)âŸ©
  rw [â† interior_Ici]
  refine interior_maximal (image_subset_iff.2 hfâ‚‚) (f.isOpenMap_of_ne_zero ?_ _ htâ‚ƒ)
  rintro rfl
  simp_rw [ContinuousLinearMap.zero_apply] at hfâ‚ hfâ‚‚
  exact (hfâ‚ _ haâ‚€).not_le (hfâ‚‚ _ hbâ‚€)

variable [LocallyConvexSpace â„ E]

/-- A version of the **Hahn-Banach theorem**: given disjoint convex sets `s`, `t` where `s` is
compact and `t` is closed, there is a continuous linear functional which strongly separates them. -/
theorem geometric_hahn_banach_compact_closed (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsCompact s)
    (htâ‚ : Convex â„ t) (htâ‚‚ : IsClosed t) (disj : Disjoint s t) :
    âˆƒ (f : E â†’L[â„] â„) (u v : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < v âˆ§ âˆ€ b âˆˆ t, v < f b := by
  obtain rfl | hs := s.eq_empty_or_nonempty
  Â· exact âŸ¨0, -2, -1, by simp, by norm_num, fun b _hb => by norm_numâŸ©
  obtain rfl | _ht := t.eq_empty_or_nonempty
  Â· exact âŸ¨0, 1, 2, fun a _ha => by norm_num, by norm_num, by simpâŸ©
  obtain âŸ¨U, V, hU, hV, hUâ‚, hVâ‚, sU, tV, disj'âŸ© := disj.exists_open_convexes hsâ‚ hsâ‚‚ htâ‚ htâ‚‚
  obtain âŸ¨f, u, hfâ‚, hfâ‚‚âŸ© := geometric_hahn_banach_open_open hUâ‚ hU hVâ‚ hV disj'
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := hsâ‚‚.exists_isMaxOn hs f.continuous.continuousOn
  have : f x < u := hfâ‚ x (sU hxâ‚)
  exact
    âŸ¨f, (f x + u) / 2, u,
      fun a ha => by have := hxâ‚‚ ha; dsimp at this; linarith,
      by linarith,
      fun b hb => hfâ‚‚ b (tV hb)âŸ©

/-- A version of the **Hahn-Banach theorem**: given disjoint convex sets `s`, `t` where `s` is
closed, and `t` is compact, there is a continuous linear functional which strongly separates them.
-/
theorem geometric_hahn_banach_closed_compact (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsClosed s)
    (htâ‚ : Convex â„ t) (htâ‚‚ : IsCompact t) (disj : Disjoint s t) :
    âˆƒ (f : E â†’L[â„] â„) (u v : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < v âˆ§ âˆ€ b âˆˆ t, v < f b :=
  let âŸ¨f, s, t, hs, st, htâŸ© := geometric_hahn_banach_compact_closed htâ‚ htâ‚‚ hsâ‚ hsâ‚‚ disj.symm
  âŸ¨-f, -t, -s, by simpa using ht, by simpa using st, by simpa using hsâŸ©

theorem geometric_hahn_banach_point_closed (htâ‚ : Convex â„ t) (htâ‚‚ : IsClosed t) (disj : x âˆ‰ t) :
    âˆƒ (f : E â†’L[â„] â„) (u : â„), f x < u âˆ§ âˆ€ b âˆˆ t, u < f b :=
  let âŸ¨f, _u, v, ha, hst, hbâŸ© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) isCompact_singleton htâ‚ htâ‚‚
      (disjoint_singleton_left.2 disj)
  âŸ¨f, v, hst.trans' <| ha x <| mem_singleton _, hbâŸ©

theorem geometric_hahn_banach_closed_point (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsClosed s) (disj : x âˆ‰ s) :
    âˆƒ (f : E â†’L[â„] â„) (u : â„), (âˆ€ a âˆˆ s, f a < u) âˆ§ u < f x :=
  let âŸ¨f, s, _t, ha, hst, hbâŸ© :=
    geometric_hahn_banach_closed_compact hsâ‚ hsâ‚‚ (convex_singleton x) isCompact_singleton
      (disjoint_singleton_right.2 disj)
  âŸ¨f, s, ha, hst.trans <| hb x <| mem_singleton _âŸ©

/-- See also `NormedSpace.eq_iff_forall_dual_eq`. -/
theorem geometric_hahn_banach_point_point [T1Space E] (hxy : x â‰  y) :
    âˆƒ f : E â†’L[â„] â„, f x < f y := by
  obtain âŸ¨f, s, t, hs, st, htâŸ© :=
    geometric_hahn_banach_compact_closed (convex_singleton x) isCompact_singleton
      (convex_singleton y) isClosed_singleton (disjoint_singleton.2 hxy)
  exact âŸ¨f, by linarith [hs x rfl, ht y rfl]âŸ©

/-- A closed convex set is the intersection of the halfspaces containing it. -/
theorem iInter_halfspaces_eq (hsâ‚ : Convex â„ s) (hsâ‚‚ : IsClosed s) :
    â‹‚ l : E â†’L[â„] â„, { x | âˆƒ y âˆˆ s, l x â‰¤ l y } = s := by
  rw [Set.iInter_setOf]
  refine Set.Subset.antisymm (fun x hx => ?_) fun x hx l => âŸ¨x, hx, le_rflâŸ©
  by_contra h
  obtain âŸ¨l, s, hlA, hlâŸ© := geometric_hahn_banach_closed_point hsâ‚ hsâ‚‚ h
  obtain âŸ¨y, hy, hxyâŸ© := hx l
  exact ((hxy.trans_lt (hlA y hy)).trans hl).not_le le_rfl

section RCLike

open RCLike

variable [RCLike ğ•œ] [TopologicalSpace E] [AddCommGroup E] [TopologicalAddGroup E]
  [Module ğ•œ E] [Module â„ E] [ContinuousSMul ğ•œ E] [IsScalarTower â„ ğ•œ E] {s t : Set E} {x y : E} (a : â„)

/-
This is interesting. We want a linear map from â„-linear functionals to ğ•œ-linear functionals.
How does one map an â„-linear functional to a ğ•œ-linear functional?

We want a "lift", here. But maybe not the lift tactic right away.

We want this to have the property that the real functional below is essentially the real
part of the functional above. Does this require knowing something about the structure of
RCLike fields? I don't know. The original idea I had was to take Ï† and map it to ofReal âˆ˜ Ï†.
That would work as a function. Does this even make sense?

If x, y are in E, then Ï†(x + y)= Ï†(x) + Ï†(y) by the linearity, and the addition is in ğ•œ on
the right. What about smul? In this case, we have Ï†(m â€¢ x)= m * Ï†(x) because Ï† is linear.
the goal is then to pass this through the ofReal...and I do NOT see how that is going to work.

In fact, I don't believe it will.
-/
def RCLinearMapDual : (E â†’L[â„] â„) â†’â‚—[â„] (E â†’L[â„] ğ•œ) where
  toFun := fun
    | .mk toLinearMap cont => {
      toFun := fun x â†¦ ofReal (toLinearMap x) - (I : ğ•œ) * ofReal (toLinearMap ((I : ğ•œ) â€¢ x))
      map_add' := by
        intro x y
        simp only [map_add, smul_add, mul_add]
        exact
          add_sub_add_comm ((algebraMap â„ ğ•œ) (toLinearMap x)) ((algebraMap â„ ğ•œ) (toLinearMap y))
            (I * (algebraMap â„ ğ•œ) (toLinearMap (I â€¢ x)))
            (I * (algebraMap â„ ğ•œ) (toLinearMap (I â€¢ y)))
      map_smul' := by
        intro m x
        simp only [LinearMapClass.map_smul, map_mul, RingHom.id_apply, smul_sub, smul_eq_mul,
          real_smul_ofReal, sub_right_inj]
        rw [smul_comm, LinearMapClass.map_smul]
        sorry
      cont := {
        isOpen_preimage := by
          intro s hs
          sorry
      }
    }
  map_add' := by
    intro f g
    simp only [LinearMap.add_apply, ContinuousLinearMap.coe_coe, map_add]
    ext x
    simp only [ContinuousLinearMap.coe_mk', LinearMap.coe_mk, AddHom.coe_mk,
      ContinuousLinearMap.add_apply]
    rw [mul_add]
    exact
      add_sub_add_comm ((algebraMap â„ ğ•œ) (f x)) ((algebraMap â„ ğ•œ) (g x))
        (I * (algebraMap â„ ğ•œ) (f (I â€¢ x))) (I * (algebraMap â„ ğ•œ) (g (I â€¢ x)))
  map_smul' := sorry

end RCLike
