/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Analysis.Convex.Cone.Extension
import Mathlib.Analysis.NormedSpace.RCLike
import Mathlib.Analysis.NormedSpace.Extend
import Mathlib.Analysis.RCLike.Lemmas

/-!
# Extension Hahn-Banach theorem

In this file we prove the analytic Hahn-Banach theorem. For any continuous linear function on a
subspace, we can extend it to a function on the entire space without changing its norm.

We prove
* `Real.exists_extension_norm_eq`: Hahn-Banach theorem for continuous linear functions on normed
  spaces over `‚Ñù`.
* `exists_extension_norm_eq`: Hahn-Banach theorem for continuous linear functions on normed spaces
  over `‚Ñù` or `‚ÑÇ`.

In order to state and prove the corollaries uniformly, we prove the statements for a field `ùïú`
satisfying `RCLike ùïú`.

In this setting, `exists_dual_vector` states that, for any nonzero `x`, there exists a continuous
linear form `g` of norm `1` with `g x = ‚Äñx‚Äñ` (where the norm has to be interpreted as an element
of `ùïú`).
-/


universe u v

namespace Real

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- **Hahn-Banach theorem** for continuous linear functions over `‚Ñù`.
See also `exists_extension_norm_eq` in the root namespace for a more general version
that works both for `‚Ñù` and `‚ÑÇ`. -/
theorem exists_extension_norm_eq (p : Subspace ‚Ñù E) (f : p ‚ÜíL[‚Ñù] ‚Ñù) :
    ‚àÉ g : E ‚ÜíL[‚Ñù] ‚Ñù, (‚àÄ x : p, g x = f x) ‚àß ‚Äñg‚Äñ = ‚Äñf‚Äñ := by
  rcases exists_extension_of_le_sublinear ‚ü®p, f‚ü© (fun x ‚Ü¶ ‚Äñf‚Äñ * ‚Äñx‚Äñ)
      (fun c hc x ‚Ü¶ by simp only [norm_smul c x, Real.norm_eq_abs, abs_of_pos hc, mul_left_comm])
      (fun x y ‚Ü¶ by
        rw [‚Üê left_distrib]
        exact mul_le_mul_of_nonneg_left (norm_add_le x y) (@norm_nonneg _ _ f))
      fun x ‚Ü¶ le_trans (le_abs_self _) (f.le_opNorm _) with ‚ü®g, g_eq, g_le‚ü©
  set g' :=
    g.mkContinuous ‚Äñf‚Äñ fun x ‚Ü¶ abs_le.2 ‚ü®neg_le.1 <| g.map_neg x ‚ñ∏ norm_neg x ‚ñ∏ g_le (-x), g_le x‚ü©
  refine ‚ü®g', g_eq, ?_‚ü©
  apply le_antisymm (g.mkContinuous_norm_le (norm_nonneg f) _)
  refine f.opNorm_le_bound (norm_nonneg _) fun x ‚Ü¶ ?_
  dsimp at g_eq
  rw [‚Üê g_eq]
  apply g'.le_opNorm

end Real

section RCLike

open RCLike

variable {ùïú : Type*} [NontriviallyNormedField ùïú] [IsRCLikeNormedField ùïú] {E F : Type*}
  [SeminormedAddCommGroup E] [NormedSpace ùïú E]
  [NormedAddCommGroup F] [NormedSpace ùïú F]

/-- **Hahn-Banach theorem** for continuous linear functions over `ùïú`
satisfying `IsRCLikeNormedField ùïú`. -/
theorem exists_extension_norm_eq (p : Subspace ùïú E) (f : p ‚ÜíL[ùïú] ùïú) :
    ‚àÉ g : E ‚ÜíL[ùïú] ùïú, (‚àÄ x : p, g x = f x) ‚àß ‚Äñg‚Äñ = ‚Äñf‚Äñ := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  letI : Module ‚Ñù E := RestrictScalars.module ‚Ñù ùïú E
  letI : IsScalarTower ‚Ñù ùïú E := RestrictScalars.isScalarTower _ _ _
  letI : NormedSpace ‚Ñù E := NormedSpace.restrictScalars _ ùïú _
  -- Let `fr: p ‚ÜíL[‚Ñù] ‚Ñù` be the real part of `f`.
  let fr := reCLM.comp (f.restrictScalars ‚Ñù)
  -- Use the real version to get a norm-preserving extension of `fr`, which
  -- we'll call `g : E ‚ÜíL[‚Ñù] ‚Ñù`.
  rcases Real.exists_extension_norm_eq (p.restrictScalars ‚Ñù) fr with ‚ü®g, ‚ü®hextends, hnormeq‚ü©‚ü©
  -- Now `g` can be extended to the `E ‚ÜíL[ùïú] ùïú` we need.
  refine ‚ü®g.extendToùïú, ?_‚ü©
  -- It is an extension of `f`.
  have h : ‚àÄ x : p, g.extendToùïú x = f x := by
    intro x
    rw [ContinuousLinearMap.extendToùïú_apply, ‚Üê Submodule.coe_smul]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    -- The goal has a coercion from `RestrictScalars ‚Ñù ùïú E ‚ÜíL[‚Ñù] ‚Ñù`, but
    -- `hextends` involves a coercion from `E ‚ÜíL[‚Ñù] ‚Ñù`.
    erw [hextends]
    erw [hextends]
    have :
        (fr x : ùïú) - I * ‚Üë(fr ((I : ùïú) ‚Ä¢ x)) = (re (f x) : ùïú) - (I : ùïú) * re (f ((I : ùïú) ‚Ä¢ x)) := by
      rfl
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [this]
    apply ext
    ¬∑ simp only [add_zero, Algebra.id.smul_eq_mul, I_re, ofReal_im, AddMonoidHom.map_add, zero_sub,
        I_im', zero_mul, ofReal_re, sub_zero, mul_neg, ofReal_neg,
        mul_re, mul_zero, sub_neg_eq_add, ContinuousLinearMap.map_smul]
    ¬∑ simp only [Algebra.id.smul_eq_mul, I_re, ofReal_im, AddMonoidHom.map_add, zero_sub, I_im',
        zero_mul, ofReal_re, mul_neg, mul_im, zero_add, ofReal_neg, mul_re,
        sub_neg_eq_add, ContinuousLinearMap.map_smul]
  -- And we derive the equality of the norms by bounding on both sides.
  refine ‚ü®h, le_antisymm ?_ ?_‚ü©
  ¬∑ calc
      ‚Äñg.extendToùïú‚Äñ = ‚Äñg‚Äñ := g.norm_extendToùïú
      _ = ‚Äñfr‚Äñ := hnormeq
      _ ‚â§ ‚ÄñreCLM‚Äñ * ‚Äñf‚Äñ := ContinuousLinearMap.opNorm_comp_le _ _
      _ = ‚Äñf‚Äñ := by rw [reCLM_norm, one_mul]
  ¬∑ exact f.opNorm_le_bound g.extendToùïú.opNorm_nonneg fun x ‚Ü¶ h x ‚ñ∏ g.extendToùïú.le_opNorm x

open Module

/-- Corollary of the **Hahn-Banach theorem**: if `f : p ‚Üí F` is a continuous linear map
from a submodule of a normed space `E` over `ùïú`, `ùïú = ‚Ñù` or `ùïú = ‚ÑÇ`,
with a finite dimensional range, then `f` admits an extension to a continuous linear map `E ‚Üí F`.

Note that contrary to the case `F = ùïú`, see `exists_extension_norm_eq`,
we provide no estimates on the norm of the extension.
-/
lemma ContinuousLinearMap.exist_extension_of_finiteDimensional_range {p : Submodule ùïú E}
    (f : p ‚ÜíL[ùïú] F) [FiniteDimensional ùïú (LinearMap.range f)] :
    ‚àÉ g : E ‚ÜíL[ùïú] F, f = g.comp p.subtypeL := by
  letI : RCLike ùïú := IsRCLikeNormedField.rclike ùïú
  set b := Module.finBasis ùïú (LinearMap.range f)
  set e := b.equivFunL
  set fi := fun i ‚Ü¶ (LinearMap.toContinuousLinearMap (b.coord i)).comp
    (f.codRestrict _ <| LinearMap.mem_range_self _)
  choose gi hgf _ using fun i ‚Ü¶ exists_extension_norm_eq p (fi i)
  use (LinearMap.range f).subtypeL.comp <| e.symm.toContinuousLinearMap.comp (.pi gi)
  ext x
  simp [fi, e, hgf]

/-- A finite dimensional submodule over `‚Ñù` or `‚ÑÇ` is `Submodule.ClosedComplemented`. -/
lemma Submodule.ClosedComplemented.of_finiteDimensional (p : Submodule ùïú F)
    [FiniteDimensional ùïú p] : p.ClosedComplemented :=
  let ‚ü®g, hg‚ü© := (ContinuousLinearMap.id ùïú p).exist_extension_of_finiteDimensional_range
  ‚ü®g, DFunLike.congr_fun hg.symm‚ü©

end RCLike

section DualVector

variable (ùïú : Type v) [RCLike ùïú]
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ùïú E]

open ContinuousLinearEquiv Submodule

theorem coord_norm' {x : E} (h : x ‚â† 0) : ‚Äñ(‚Äñx‚Äñ : ùïú) ‚Ä¢ coord ùïú x h‚Äñ = 1 := by
  rw [norm_smul (Œ± := ùïú) (x := coord ùïú x h), RCLike.norm_coe_norm, coord_norm,
    mul_inv_cancel‚ÇÄ (mt norm_eq_zero.mp h)]

/-- Corollary of Hahn-Banach. Given a nonzero element `x` of a normed space, there exists an
element of the dual space, of norm `1`, whose value on `x` is `‚Äñx‚Äñ`. -/
theorem exists_dual_vector (x : E) (h : x ‚â† 0) : ‚àÉ g : E ‚ÜíL[ùïú] ùïú, ‚Äñg‚Äñ = 1 ‚àß g x = ‚Äñx‚Äñ := by
  let p : Submodule ùïú E := ùïú ‚àô x
  let f := (‚Äñx‚Äñ : ùïú) ‚Ä¢ coord ùïú x h
  obtain ‚ü®g, hg‚ü© := exists_extension_norm_eq p f
  refine ‚ü®g, ?_, ?_‚ü©
  ¬∑ rw [hg.2, coord_norm']
  ¬∑ calc
      g x = g (‚ü®x, mem_span_singleton_self x‚ü© : ùïú ‚àô x) := by rw [Submodule.coe_mk]
      _ = ((‚Äñx‚Äñ : ùïú) ‚Ä¢ coord ùïú x h) (‚ü®x, mem_span_singleton_self x‚ü© : ùïú ‚àô x) := by rw [‚Üê hg.1]
      _ = ‚Äñx‚Äñ := by simp [-algebraMap_smul]

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, and choosing
the dual element arbitrarily when `x = 0`. -/
theorem exists_dual_vector' [Nontrivial E] (x : E) : ‚àÉ g : E ‚ÜíL[ùïú] ùïú, ‚Äñg‚Äñ = 1 ‚àß g x = ‚Äñx‚Äñ := by
  by_cases hx : x = 0
  ¬∑ obtain ‚ü®y, hy‚ü© := exists_ne (0 : E)
    obtain ‚ü®g, hg‚ü© : ‚àÉ g : E ‚ÜíL[ùïú] ùïú, ‚Äñg‚Äñ = 1 ‚àß g y = ‚Äñy‚Äñ := exists_dual_vector ùïú y hy
    refine ‚ü®g, hg.left, ?_‚ü©
    simp [hx]
  ¬∑ exact exists_dual_vector ùïú x hx

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, but only ensuring that
the dual element has norm at most `1` (this can not be improved for the trivial
vector space). -/
theorem exists_dual_vector'' (x : E) : ‚àÉ g : E ‚ÜíL[ùïú] ùïú, ‚Äñg‚Äñ ‚â§ 1 ‚àß g x = ‚Äñx‚Äñ := by
  by_cases hx : x = 0
  ¬∑ refine ‚ü®0, by simp, ?_‚ü©
    symm
    simp [hx]
  ¬∑ rcases exists_dual_vector ùïú x hx with ‚ü®g, g_norm, g_eq‚ü©
    exact ‚ü®g, g_norm.le, g_eq‚ü©

end DualVector
