/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Heather Macbeth
-/
import Mathlib.Analysis.Convex.Cone.Extension
import Mathlib.Analysis.NormedSpace.RCLike
import Mathlib.Analysis.NormedSpace.Extend
import Mathlib.Analysis.RCLike.Lemmas

#align_import analysis.normed_space.hahn_banach.extension from "leanprover-community/mathlib"@"915591b2bb3ea303648db07284a161a7f2a9e3d4"

/-!
# Extension Hahn-Banach theorem

In this file we prove the analytic Hahn-Banach theorem. For any continuous linear function on a
subspace, we can extend it to a function on the entire space without changing its norm.

We prove
* `Real.exists_extension_norm_eq`: Hahn-Banach theorem for continuous linear functions on normed
  spaces over `â„`.
* `exists_extension_norm_eq`: Hahn-Banach theorem for continuous linear functions on normed spaces
  over `â„` or `â„‚`.

In order to state and prove the corollaries uniformly, we prove the statements for a field `ğ•œ`
satisfying `RCLike ğ•œ`.

In this setting, `exists_dual_vector` states that, for any nonzero `x`, there exists a continuous
linear form `g` of norm `1` with `g x = â€–xâ€–` (where the norm has to be interpreted as an element
of `ğ•œ`).

-/


universe u v

namespace Real

variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace â„ E]

/-- **Hahn-Banach theorem** for continuous linear functions over `â„`.
See also `exists_extension_norm_eq` in the root namespace for a more general version
that works both for `â„` and `â„‚`. -/
theorem exists_extension_norm_eq (p : Subspace â„ E) (f : p â†’L[â„] â„) :
    âˆƒ g : E â†’L[â„] â„, (âˆ€ x : p, g x = f x) âˆ§ â€–gâ€– = â€–fâ€– := by
  rcases exists_extension_of_le_sublinear âŸ¨p, fâŸ© (fun x => â€–fâ€– * â€–xâ€–)
      (fun c hc x => by simp only [norm_smul c x, Real.norm_eq_abs, abs_of_pos hc, mul_left_comm])
      (fun x y => by -- Porting note: placeholder filled here
        rw [â† left_distrib]
        exact mul_le_mul_of_nonneg_left (norm_add_le x y) (@norm_nonneg _ _ f))
      fun x => le_trans (le_abs_self _) (f.le_opNorm _) with âŸ¨g, g_eq, g_leâŸ©
  set g' :=
    g.mkContinuous â€–fâ€– fun x => abs_le.2 âŸ¨neg_le.1 <| g.map_neg x â–¸ norm_neg x â–¸ g_le (-x), g_le xâŸ©
  refine âŸ¨g', g_eq, ?_âŸ©
  apply le_antisymm (g.mkContinuous_norm_le (norm_nonneg f) _)
  refine f.opNorm_le_bound (norm_nonneg _) fun x => ?_
  dsimp at g_eq
  rw [â† g_eq]
  apply g'.le_opNorm
#align real.exists_extension_norm_eq Real.exists_extension_norm_eq

end Real

section RCLike

open RCLike

variable {ğ•œ : Type*} [RCLike ğ•œ] {E F : Type*}
  [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- **Hahn-Banach theorem** for continuous linear functions over `ğ•œ` satisfying `RCLike ğ•œ`. -/
theorem exists_extension_norm_eq (p : Subspace ğ•œ E) (f : p â†’L[ğ•œ] ğ•œ) :
    âˆƒ g : E â†’L[ğ•œ] ğ•œ, (âˆ€ x : p, g x = f x) âˆ§ â€–gâ€– = â€–fâ€– := by
  letI : Module â„ E := RestrictScalars.module â„ ğ•œ E
  letI : IsScalarTower â„ ğ•œ E := RestrictScalars.isScalarTower _ _ _
  letI : NormedSpace â„ E := NormedSpace.restrictScalars _ ğ•œ _
  -- Let `fr: p â†’L[â„] â„` be the real part of `f`.
  let fr := reCLM.comp (f.restrictScalars â„)
  -- Use the real version to get a norm-preserving extension of `fr`, which
  -- we'll call `g : E â†’L[â„] â„`.
  rcases Real.exists_extension_norm_eq (p.restrictScalars â„) fr with âŸ¨g, âŸ¨hextends, hnormeqâŸ©âŸ©
  -- Now `g` can be extended to the `E â†’L[ğ•œ] ğ•œ` we need.
  refine âŸ¨g.extendToğ•œ, ?_âŸ©
  -- It is an extension of `f`.
  have h : âˆ€ x : p, g.extendToğ•œ x = f x := by
    intro x
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [ContinuousLinearMap.extendToğ•œ_apply, â† Submodule.coe_smul, hextends, hextends]
    have : (fr x : ğ•œ) - I * â†‘(fr (I â€¢ x)) = (re (f x) : ğ•œ) - (I : ğ•œ) * re (f ((I : ğ•œ) â€¢ x)) := by
      rfl
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    erw [this]
    apply ext
    Â· simp only [add_zero, Algebra.id.smul_eq_mul, I_re, ofReal_im, AddMonoidHom.map_add, zero_sub,
        I_im', zero_mul, ofReal_re, eq_self_iff_true, sub_zero, mul_neg, ofReal_neg,
        mul_re, mul_zero, sub_neg_eq_add, ContinuousLinearMap.map_smul]
    Â· simp only [Algebra.id.smul_eq_mul, I_re, ofReal_im, AddMonoidHom.map_add, zero_sub, I_im',
        zero_mul, ofReal_re, mul_neg, mul_im, zero_add, ofReal_neg, mul_re,
        sub_neg_eq_add, ContinuousLinearMap.map_smul]
  -- And we derive the equality of the norms by bounding on both sides.
  refine âŸ¨h, le_antisymm ?_ ?_âŸ©
  Â· calc
      â€–g.extendToğ•œâ€– = â€–gâ€– := g.norm_extendToğ•œ
      _ = â€–frâ€– := hnormeq
      _ â‰¤ â€–reCLMâ€– * â€–fâ€– := ContinuousLinearMap.opNorm_comp_le _ _
      _ = â€–fâ€– := by rw [reCLM_norm, one_mul]
  Â· exact f.opNorm_le_bound g.extendToğ•œ.opNorm_nonneg fun x => h x â–¸ g.extendToğ•œ.le_opNorm x
#align exists_extension_norm_eq exists_extension_norm_eq

open FiniteDimensional

/-- Corollary of the **Hahn-Banach theorem**: if `f : p â†’ F` is a continuous linear map
from a submodule of a normed space `E` over `ğ•œ`, `ğ•œ = â„` or `ğ•œ = â„‚`,
with a finite dimensional range,
then `f` admits an extension to a continuous linear map `E â†’ F`.

Note that contrary to the case `F = ğ•œ`, see `exists_extension_norm_eq`,
we provide no estimates on the norm of the extension.
-/
lemma ContinuousLinearMap.exist_extension_of_finiteDimensional_range {p : Submodule ğ•œ E}
    (f : p â†’L[ğ•œ] F) [FiniteDimensional ğ•œ (LinearMap.range f)] :
    âˆƒ g : E â†’L[ğ•œ] F, f = g.comp p.subtypeL := by
  set b := finBasis ğ•œ (LinearMap.range f)
  set e := b.equivFunL
  set fi := fun i â†¦ (LinearMap.toContinuousLinearMap (b.coord i)).comp
    (f.codRestrict _ <| LinearMap.mem_range_self _)
  choose gi hgf _ using fun i â†¦ exists_extension_norm_eq p (fi i)
  use (LinearMap.range f).subtypeL.comp <| e.symm.toContinuousLinearMap.comp (.pi gi)
  ext x
  simp [fi, e, hgf]

/-- A finite dimensional submodule over `â„` or `â„‚` is `Submodule.ClosedComplemented`. -/
lemma Submodule.ClosedComplemented.of_finiteDimensional (p : Submodule ğ•œ F)
    [FiniteDimensional ğ•œ p] : p.ClosedComplemented :=
  let âŸ¨g, hgâŸ© := (ContinuousLinearMap.id ğ•œ p).exist_extension_of_finiteDimensional_range
  âŸ¨g, DFunLike.congr_fun hg.symmâŸ©

end RCLike

section DualVector

variable (ğ•œ : Type v) [RCLike ğ•œ]
variable {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

open ContinuousLinearEquiv Submodule

open scoped Classical

theorem coord_norm' {x : E} (h : x â‰  0) : â€–(â€–xâ€– : ğ•œ) â€¢ coord ğ•œ x hâ€– = 1 := by
  rw [norm_smul (Î± := ğ•œ) (x := coord ğ•œ x h), RCLike.norm_coe_norm, coord_norm,
    mul_inv_cancel (mt norm_eq_zero.mp h)]
#align coord_norm' coord_norm'

/-- Corollary of Hahn-Banach. Given a nonzero element `x` of a normed space, there exists an
    element of the dual space, of norm `1`, whose value on `x` is `â€–xâ€–`. -/
theorem exists_dual_vector (x : E) (h : x â‰  0) : âˆƒ g : E â†’L[ğ•œ] ğ•œ, â€–gâ€– = 1 âˆ§ g x = â€–xâ€– := by
  let p : Submodule ğ•œ E := ğ•œ âˆ™ x
  let f := (â€–xâ€– : ğ•œ) â€¢ coord ğ•œ x h
  obtain âŸ¨g, hgâŸ© := exists_extension_norm_eq p f
  refine âŸ¨g, ?_, ?_âŸ©
  Â· rw [hg.2, coord_norm']
  Â· calc
      g x = g (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) := by rw [coe_mk]
      _ = ((â€–xâ€– : ğ•œ) â€¢ coord ğ•œ x h) (âŸ¨x, mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) := by rw [â† hg.1]
      _ = â€–xâ€– := by simp
#align exists_dual_vector exists_dual_vector

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, and choosing
    the dual element arbitrarily when `x = 0`. -/
theorem exists_dual_vector' [Nontrivial E] (x : E) : âˆƒ g : E â†’L[ğ•œ] ğ•œ, â€–gâ€– = 1 âˆ§ g x = â€–xâ€– := by
  by_cases hx : x = 0
  Â· obtain âŸ¨y, hyâŸ© := exists_ne (0 : E)
    obtain âŸ¨g, hgâŸ© : âˆƒ g : E â†’L[ğ•œ] ğ•œ, â€–gâ€– = 1 âˆ§ g y = â€–yâ€– := exists_dual_vector ğ•œ y hy
    refine âŸ¨g, hg.left, ?_âŸ©
    simp [hx]
  Â· exact exists_dual_vector ğ•œ x hx
#align exists_dual_vector' exists_dual_vector'

/-- Variant of Hahn-Banach, eliminating the hypothesis that `x` be nonzero, but only ensuring that
    the dual element has norm at most `1` (this can not be improved for the trivial
    vector space). -/
theorem exists_dual_vector'' (x : E) : âˆƒ g : E â†’L[ğ•œ] ğ•œ, â€–gâ€– â‰¤ 1 âˆ§ g x = â€–xâ€– := by
  by_cases hx : x = 0
  Â· refine âŸ¨0, by simp, ?_âŸ©
    symm
    simp [hx]
  Â· rcases exists_dual_vector ğ•œ x hx with âŸ¨g, g_norm, g_eqâŸ©
    exact âŸ¨g, g_norm.le, g_eqâŸ©
#align exists_dual_vector'' exists_dual_vector''

end DualVector
