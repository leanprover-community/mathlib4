/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension
import Mathlib.Analysis.NormedSpace.HahnBanach.Separation

/-!
# Spaces with separating dual

We introduce a typeclass `SeparatingDual R V`, registering that the points of the topological
module `V` over `R` can be separated by continuous linear forms.

This property is satisfied for normed spaces over `â„` or `â„‚` (by the analytic Hahn-Banach theorem)
and for locally convex topological spaces over `â„` (by the geometric Hahn-Banach theorem).

Under the assumption `SeparatingDual R V`, we show in
`SeparatingDual.exists_continuousLinearMap_apply_eq` that the group of continuous linear
equivalences acts transitively on the set of nonzero vectors.
-/

/-- When `E` is a topological module over a topological ring `R`, the class `SeparatingDual R E`
registers that continuous linear forms on `E` separate points of `E`. -/
class SeparatingDual (R V : Type*) [Ring R] [AddCommGroup V] [TopologicalSpace V]
    [TopologicalSpace R] [Module R V] : Prop :=
  /-- Any nonzero vector can be mapped by a continuous linear map to a nonzero scalar. -/
  exists_ne_zero' : âˆ€ (x : V), x â‰  0 â†’ âˆƒ f : V â†’L[R] R, f x â‰  0

instance {E : Type*} [TopologicalSpace E] [AddCommGroup E] [TopologicalAddGroup E]
    [Module â„ E] [ContinuousSMul â„ E] [LocallyConvexSpace â„ E] [T1Space E] : SeparatingDual â„ E :=
  âŸ¨fun x hx â†¦ by
    rcases geometric_hahn_banach_point_point hx.symm with âŸ¨f, hfâŸ©
    -- âŠ¢ âˆƒ f, â†‘f x â‰  0
    simp only [map_zero] at hf
    -- âŠ¢ âˆƒ f, â†‘f x â‰  0
    exact âŸ¨f, hf.ne'âŸ© âŸ©
    -- ğŸ‰ no goals

instance {E ğ•œ : Type*} [IsROrC ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] : SeparatingDual ğ•œ E :=
  âŸ¨fun x hx â†¦ by
    rcases exists_dual_vector ğ•œ x hx with âŸ¨f, -, hfâŸ©
    -- âŠ¢ âˆƒ f, â†‘f x â‰  0
    refine âŸ¨f, ?_âŸ©
    -- âŠ¢ â†‘f x â‰  0
    simpa [hf] using hxâŸ©
    -- ğŸ‰ no goals

namespace SeparatingDual

section Ring

variable {R V : Type*} [Ring R] [AddCommGroup V] [TopologicalSpace V]
  [TopologicalSpace R] [Module R V] [SeparatingDual R V]

lemma exists_ne_zero {x : V} (hx : x â‰  0) :
    âˆƒ f : V â†’L[R] R, f x â‰  0 :=
  exists_ne_zero' x hx

theorem exists_separating_of_ne {x y : V} (h : x â‰  y) :
    âˆƒ f : V â†’L[R] R, f x â‰  f y := by
  rcases exists_ne_zero (R := R) (sub_ne_zero_of_ne h) with âŸ¨f, hfâŸ©
  -- âŠ¢ âˆƒ f, â†‘f x â‰  â†‘f y
  exact âŸ¨f, by simpa [sub_ne_zero] using hfâŸ©
  -- ğŸ‰ no goals

protected theorem t1Space [T1Space R] : T1Space V := by
  apply t1Space_iff_exists_open.2 (fun x y hxy â†¦ ?_)
  -- âŠ¢ âˆƒ U, IsOpen U âˆ§ x âˆˆ U âˆ§ Â¬y âˆˆ U
  rcases exists_separating_of_ne (R := R) hxy with âŸ¨f, hfâŸ©
  -- âŠ¢ âˆƒ U, IsOpen U âˆ§ x âˆˆ U âˆ§ Â¬y âˆˆ U
  exact âŸ¨f â»Â¹' {f y}á¶œ, isOpen_compl_singleton.preimage f.continuous, hf, by simpâŸ©
  -- ğŸ‰ no goals

protected theorem t2Space [T2Space R] : T2Space V := by
  apply (t2Space_iff _).2 (fun {x} {y} hxy â†¦ ?_)
  -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ Disjoint u v
  rcases exists_separating_of_ne (R := R) hxy with âŸ¨f, hfâŸ©
  -- âŠ¢ âˆƒ u v, IsOpen u âˆ§ IsOpen v âˆ§ x âˆˆ u âˆ§ y âˆˆ v âˆ§ Disjoint u v
  exact separated_by_continuous f.continuous hf
  -- ğŸ‰ no goals

end Ring

section Field

variable {R V : Type*} [Field R] [AddCommGroup V] [TopologicalSpace R] [TopologicalSpace V]
  [TopologicalRing R] [TopologicalAddGroup V] [Module R V] [SeparatingDual R V]

lemma exists_eq_one {x : V} (hx : x â‰  0) :
    âˆƒ f : V â†’L[R] R, f x = 1 := by
  rcases exists_ne_zero (R := R) hx with âŸ¨f, hfâŸ©
  -- âŠ¢ âˆƒ f, â†‘f x = 1
  exact âŸ¨(f x)â»Â¹ â€¢ f, inv_mul_cancel hfâŸ©
  -- ğŸ‰ no goals

theorem exists_eq_one_ne_zero_of_ne_zero_pair {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    âˆƒ f : V â†’L[R] R, f x = 1 âˆ§ f y â‰  0 := by
  obtain âŸ¨u, uxâŸ© : âˆƒ u : V â†’L[R] R, u x = 1 := exists_eq_one hx
  -- âŠ¢ âˆƒ f, â†‘f x = 1 âˆ§ â†‘f y â‰  0
  rcases ne_or_eq (u y) 0 with uy|uy
  -- âŠ¢ âˆƒ f, â†‘f x = 1 âˆ§ â†‘f y â‰  0
  Â· exact âŸ¨u, ux, uyâŸ©
    -- ğŸ‰ no goals
  obtain âŸ¨v, vyâŸ© : âˆƒ v : V â†’L[R] R, v y = 1 := exists_eq_one hy
  -- âŠ¢ âˆƒ f, â†‘f x = 1 âˆ§ â†‘f y â‰  0
  rcases ne_or_eq (v x) 0 with vx|vx
  -- âŠ¢ âˆƒ f, â†‘f x = 1 âˆ§ â†‘f y â‰  0
  Â· exact âŸ¨(v x)â»Â¹ â€¢ v, inv_mul_cancel vx, show (v x)â»Â¹ * v y â‰  0 by simp [vx, vy]âŸ©
    -- ğŸ‰ no goals
  Â· exact âŸ¨u + v, by simp [ux, vx], by simp [uy, vy]âŸ©
    -- ğŸ‰ no goals

/-- In a topological vector space with separating dual, the group of continuous linear equivalences
acts transitively on the set of nonzero vectors: given two nonzero vectors `x` and `y`, there
exists `A : V â‰ƒL[R] V` mapping `x` to `y`. -/
theorem exists_continuousLinearEquiv_apply_eq [ContinuousSMul R V]
    {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    âˆƒ A : V â‰ƒL[R] V, A x = y := by
  obtain âŸ¨G, Gx, GyâŸ© : âˆƒ G : V â†’L[R] R, G x = 1 âˆ§ G y â‰  0 :=
    exists_eq_one_ne_zero_of_ne_zero_pair hx hy
  let A : V â‰ƒL[R] V :=
  { toFun := fun z â†¦ z + G z â€¢ (y - x)
    invFun := fun z â†¦ z + ((G y) â»Â¹ * G z) â€¢ (x - y)
    map_add' := fun a b â†¦ by simp [add_smul]; abel
    map_smul' := by simp [smul_smul]
    left_inv := fun z â†¦ by
      simp only [id_eq, eq_mpr_eq_cast, RingHom.id_apply, smul_eq_mul, AddHom.toFun_eq_coe,
        AddHom.coe_mk, map_add, map_smulâ‚›â‚—, map_sub, Gx, mul_sub, mul_one, add_sub_cancel'_right]
      rw [mul_comm (G z), â† mul_assoc, inv_mul_cancel Gy]
      simp only [smul_sub, one_mul]
      abel
    right_inv := fun z â†¦ by
      simp only [map_add, map_smulâ‚›â‚—, map_mul, map_invâ‚€, RingHom.id_apply, map_sub, Gx,
        smul_eq_mul, mul_sub, mul_one]
      rw [mul_comm _ (G y), â† mul_assoc, mul_inv_cancel Gy]
      simp only [smul_sub, one_mul, add_sub_cancel'_right]
      abel
    continuous_toFun := continuous_id.add (G.continuous.smul continuous_const)
    continuous_invFun :=
      continuous_id.add ((continuous_const.mul G.continuous).smul continuous_const) }
  exact âŸ¨A, show x + G x â€¢ (y - x) = y by simp [Gx]âŸ©
  -- ğŸ‰ no goals

end Field

end SeparatingDual
