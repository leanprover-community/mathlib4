/-
Copyright (c) 2023 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Filippo A. E. Nuccio
-/
import Mathlib.Algebra.Central.Defs
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension
import Mathlib.Analysis.NormedSpace.HahnBanach.Separation
import Mathlib.Analysis.NormedSpace.Multilinear.Basic
import Mathlib.LinearAlgebra.Dual.Lemmas

/-!
# Spaces with separating dual

We introduce a typeclass `SeparatingDual R V`, registering that the points of the topological
module `V` over `R` can be separated by continuous linear forms.

This property is satisfied for normed spaces over `‚Ñù` or `‚ÑÇ` (by the analytic Hahn-Banach theorem)
and for locally convex topological spaces over `‚Ñù` (by the geometric Hahn-Banach theorem).

We show in `SeparatingDual.exists_ne_zero` that given any non-zero vector in an `R`-module `V`
satisfying `SeparatingDual R V`, there exists a continuous linear functional whose value on `v` is
non-zero.

As a consequence of the existence of `SeparatingDual.exists_ne_zero`, a generalization of
Hahn-Banach beyond the normed setting, we show that if `V` and `W` are nontrivial topological vector
spaces over a topological field `R` that acts continuously on `W`, and if `SeparatingDual R V`,
there are nontrivial continuous `R`-linear operators between `V` and `W`. This is recorded in the
instance `SeparatingDual.instNontrivialContinuousLinearMapIdOfContinuousSMul`.

Under the assumption `SeparatingDual R V`, we show in
`SeparatingDual.exists_continuousLinearEquiv_apply_eq` that the group of continuous linear
equivalences acts transitively on the set of nonzero vectors.
-/
/-- When `E` is a topological module over a topological ring `R`, the class `SeparatingDual R E`
registers that continuous linear forms on `E` separate points of `E`. -/
@[mk_iff separatingDual_def]
class SeparatingDual (R V : Type*) [Ring R] [AddCommGroup V] [TopologicalSpace V]
    [TopologicalSpace R] [Module R V] : Prop where
  /-- Any nonzero vector can be mapped by a continuous linear map to a nonzero scalar. -/
  exists_ne_zero' : ‚àÄ (x : V), x ‚â† 0 ‚Üí ‚àÉ f : StrongDual R V, f x ‚â† 0

instance {E : Type*} [TopologicalSpace E] [AddCommGroup E] [IsTopologicalAddGroup E]
    [Module ‚Ñù E] [ContinuousSMul ‚Ñù E] [LocallyConvexSpace ‚Ñù E] [T1Space E] : SeparatingDual ‚Ñù E :=
  ‚ü®fun x hx ‚Ü¶ by
    rcases geometric_hahn_banach_point_point hx.symm with ‚ü®f, hf‚ü©
    simp only [map_zero] at hf
    exact ‚ü®f, hf.ne'‚ü©‚ü©

instance {E ùïú : Type*} [RCLike ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E] : SeparatingDual ùïú E :=
  ‚ü®fun x hx ‚Ü¶ by
    rcases exists_dual_vector ùïú x hx with ‚ü®f, -, hf‚ü©
    refine ‚ü®f, ?_‚ü©
    simpa [hf] using hx‚ü©

namespace SeparatingDual

section Ring

variable {R V : Type*} [Ring R] [AddCommGroup V] [TopologicalSpace V]
  [TopologicalSpace R] [Module R V] [SeparatingDual R V]

lemma exists_ne_zero {x : V} (hx : x ‚â† 0) :
    ‚àÉ f : StrongDual R V, f x ‚â† 0 :=
  exists_ne_zero' x hx

theorem exists_separating_of_ne {x y : V} (h : x ‚â† y) :
    ‚àÉ f : StrongDual R V, f x ‚â† f y := by
  rcases exists_ne_zero (R := R) (sub_ne_zero_of_ne h) with ‚ü®f, hf‚ü©
  exact ‚ü®f, by simpa [sub_ne_zero] using hf‚ü©

protected theorem t1Space [T1Space R] : T1Space V := by
  apply t1Space_iff_exists_open.2 (fun x y hxy ‚Ü¶ ?_)
  rcases exists_separating_of_ne (R := R) hxy with ‚ü®f, hf‚ü©
  exact ‚ü®f ‚Åª¬π' {f y}·∂ú, isOpen_compl_singleton.preimage f.continuous, hf, by simp‚ü©

protected theorem t2Space [T2Space R] : T2Space V := by
  apply (t2Space_iff _).2 (fun {x} {y} hxy ‚Ü¶ ?_)
  rcases exists_separating_of_ne (R := R) hxy with ‚ü®f, hf‚ü©
  exact separated_by_continuous f.continuous hf

end Ring

section Field

variable {R V : Type*} [Field R] [AddCommGroup V] [TopologicalSpace R] [TopologicalSpace V]
  [IsTopologicalRing R] [Module R V]

-- TODO (@alreadydone): this could generalize to CommRing R if we were to add a section
theorem _root_.separatingDual_iff_injective : SeparatingDual R V ‚Üî
    Function.Injective (ContinuousLinearMap.coeLM (R := R) R (M := V) (N‚ÇÉ := R)).flip := by
  simp_rw [separatingDual_def, Ne, injective_iff_map_eq_zero]
  congrm ‚àÄ v, ?_
  rw [not_imp_comm, LinearMap.ext_iff]
  push_neg; rfl

variable [SeparatingDual R V]

open Function

/-- Given a finite-dimensional subspace `W` of a space `V` with separating dual, any
  linear functional on `W` extends to a continuous linear functional on `V`.
  This is stated more generally for an injective linear map from `W` to `V`. -/
theorem dualMap_surjective_iff {W} [AddCommGroup W] [Module R W] [FiniteDimensional R W]
    {f : W ‚Üí‚Çó[R] V} : Surjective (f.dualMap ‚àò ContinuousLinearMap.toLinearMap) ‚Üî Injective f := by
  constructor <;> intro hf
  ¬∑ exact LinearMap.dualMap_surjective_iff.mp hf.of_comp
  have := (separatingDual_iff_injective.mp ‚Äπ_‚Ä∫).comp hf
  rw [‚Üê LinearMap.coe_comp] at this
  exact LinearMap.flip_surjective_iff‚ÇÅ.mpr this

variable (V) in
open ContinuousLinearMap in
/- As a consequence of the existence of non-zero linear maps, itself a consequence of Hahn-Banach
in the normed setting, we show that if `V` and `W` are nontrivial topological vector spaces over a
topological field `R` that acts continuously on `W`, and if `SeparatingDual R V`, there are
nontrivial continuous `R`-linear operators between `V` and `W`. -/
instance (W) [AddCommGroup W] [TopologicalSpace W] [Module R W] [Nontrivial W]
   [ContinuousSMul R W] [Nontrivial V] : Nontrivial (V ‚ÜíL[R] W) := by
  obtain ‚ü®v, hv‚ü© := exists_ne (0 : V)
  obtain ‚ü®w, hw‚ü© := exists_ne (0 : W)
  obtain ‚ü®œà, hœà‚ü© := exists_ne_zero (R := R) hv
  exact ‚ü®œà.smulRight w, 0, DFunLike.ne_iff.mpr ‚ü®v, by simp_all‚ü©‚ü©

lemma exists_eq_one {x : V} (hx : x ‚â† 0) :
    ‚àÉ f : StrongDual R V, f x = 1 := by
  rcases exists_ne_zero (R := R) hx with ‚ü®f, hf‚ü©
  exact ‚ü®(f x)‚Åª¬π ‚Ä¢ f, inv_mul_cancel‚ÇÄ hf‚ü©

theorem exists_eq_one_ne_zero_of_ne_zero_pair {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚àÉ f : StrongDual R V, f x = 1 ‚àß f y ‚â† 0 := by
  obtain ‚ü®u, ux‚ü© : ‚àÉ u : StrongDual R V, u x = 1 := exists_eq_one hx
  rcases ne_or_eq (u y) 0 with uy|uy
  ¬∑ exact ‚ü®u, ux, uy‚ü©
  obtain ‚ü®v, vy‚ü© : ‚àÉ v : StrongDual R V, v y = 1 := exists_eq_one hy
  rcases ne_or_eq (v x) 0 with vx|vx
  ¬∑ exact ‚ü®(v x)‚Åª¬π ‚Ä¢ v, inv_mul_cancel‚ÇÄ vx, show (v x)‚Åª¬π * v y ‚â† 0 by simp [vx, vy]‚ü©
  ¬∑ exact ‚ü®u + v, by simp [ux, vx], by simp [uy, vy]‚ü©

variable [IsTopologicalAddGroup V]

/-- The center of continuous linear maps on a topological vector space
with separating dual is trivial, in other words, it is a central algebra. -/
instance _root_.Algebra.IsCentral.continuousLinearMap [ContinuousSMul R V] :
    Algebra.IsCentral R (V ‚ÜíL[R] V) where
  out T hT := by
    have h' (f : StrongDual R V) (y v : V) : f (T v) ‚Ä¢ y = f v ‚Ä¢ T y := by
      simpa using congr($(Subalgebra.mem_center_iff.mp hT <| f.smulRight y) v)
    nontriviality V
    obtain ‚ü®x, hx‚ü© := exists_ne (0 : V)
    obtain ‚ü®f, hf‚ü© := exists_eq_one (R := R) hx
    exact ‚ü®f (T x), ContinuousLinearMap.ext fun _ => by simp [h', hf]‚ü©

/-- In a topological vector space with separating dual, the group of continuous linear equivalences
acts transitively on the set of nonzero vectors: given two nonzero vectors `x` and `y`, there
exists `A : V ‚âÉL[R] V` mapping `x` to `y`. -/
theorem exists_continuousLinearEquiv_apply_eq [ContinuousSMul R V]
    {x y : V} (hx : x ‚â† 0) (hy : y ‚â† 0) :
    ‚àÉ A : V ‚âÉL[R] V, A x = y := by
  obtain ‚ü®G, Gx, Gy‚ü© : ‚àÉ G : StrongDual R V, G x = 1 ‚àß G y ‚â† 0 :=
    exists_eq_one_ne_zero_of_ne_zero_pair hx hy
  let A : V ‚âÉL[R] V :=
  { toFun := fun z ‚Ü¶ z + G z ‚Ä¢ (y - x)
    invFun := fun z ‚Ü¶ z + ((G y) ‚Åª¬π * G z) ‚Ä¢ (x - y)
    map_add' := fun a b ‚Ü¶ by simp [add_smul]; abel
    map_smul' := by simp [smul_smul]
    left_inv := fun z ‚Ü¶ by
      simp only [RingHom.id_apply, smul_eq_mul,
        -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 had to change `map_smul‚Çõ‚Çó` into `map_smul‚Çõ‚Çó _`
        map_add, map_smul‚Çõ‚Çó _, map_sub, Gx, mul_sub, mul_one, add_sub_cancel]
      rw [mul_comm (G z), ‚Üê mul_assoc, inv_mul_cancel‚ÇÄ Gy]
      simp only [smul_sub, one_mul]
      abel
    right_inv := fun z ‚Ü¶ by
        -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 had to change `map_smul‚Çõ‚Çó` into `map_smul‚Çõ‚Çó _`
      simp only [map_add, map_smul‚Çõ‚Çó _, map_mul, map_inv‚ÇÄ, RingHom.id_apply, map_sub, Gx,
        smul_eq_mul, mul_sub, mul_one]
      rw [mul_comm _ (G y), ‚Üê mul_assoc, mul_inv_cancel‚ÇÄ Gy]
      simp only [smul_sub, one_mul, add_sub_cancel]
      abel
    continuous_toFun := continuous_id.add (G.continuous.smul continuous_const)
    continuous_invFun :=
      continuous_id.add ((continuous_const.mul G.continuous).smul continuous_const) }
  exact ‚ü®A, show x + G x ‚Ä¢ (y - x) = y by simp [Gx]‚ü©

open Filter
open scoped Topology

section
variable (ùïú E F : Type*) [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E]
    [NormedAddCommGroup F] [NormedSpace ùïú F] [SeparatingDual ùïú E] [Nontrivial E]

/-- If a space of linear maps from `E` to `F` is complete, and `E` is nontrivial, then `F` is
complete. -/
lemma completeSpace_of_completeSpace_continuousLinearMap [CompleteSpace (E ‚ÜíL[ùïú] F)] :
    CompleteSpace F := by
  refine Metric.complete_of_cauchySeq_tendsto fun f hf => ?_
  obtain ‚ü®v, hv‚ü© : ‚àÉ (v : E), v ‚â† 0 := exists_ne 0
  obtain ‚ü®œÜ, hœÜ‚ü© : ‚àÉ œÜ : StrongDual ùïú E, œÜ v = 1 := exists_eq_one hv
  let g : ‚Ñï ‚Üí (E ‚ÜíL[ùïú] F) := fun n ‚Ü¶ ContinuousLinearMap.smulRightL ùïú E F œÜ (f n)
  have : CauchySeq g := (ContinuousLinearMap.smulRightL ùïú E F œÜ).lipschitz.cauchySeq_comp hf
  obtain ‚ü®a, ha‚ü© : ‚àÉ a, Tendsto g atTop (ùìù a) := cauchy_iff_exists_le_nhds.mp this
  refine ‚ü®a v, ?_‚ü©
  have : Tendsto (fun n ‚Ü¶ g n v) atTop (ùìù (a v)) := by
    have : Continuous (fun (i : E ‚ÜíL[ùïú] F) ‚Ü¶ i v) := by fun_prop
    exact (this.tendsto _).comp ha
  simpa [g, ContinuousLinearMap.smulRightL, hœÜ]

lemma completeSpace_continuousLinearMap_iff :
    CompleteSpace (E ‚ÜíL[ùïú] F) ‚Üî CompleteSpace F :=
  ‚ü®fun _h ‚Ü¶ completeSpace_of_completeSpace_continuousLinearMap ùïú E F, fun _h ‚Ü¶ inferInstance‚ü©

open ContinuousMultilinearMap

variable {Œπ : Type*} [Finite Œπ] {M : Œπ ‚Üí Type*} [‚àÄ i, NormedAddCommGroup (M i)]
  [‚àÄ i, NormedSpace ùïú (M i)] [‚àÄ i, SeparatingDual ùïú (M i)]

/-- If a space of multilinear maps from `Œ† i, E i` to `F` is complete, and each `E i` has a nonzero
element, then `F` is complete. -/
lemma completeSpace_of_completeSpace_continuousMultilinearMap
    [CompleteSpace (ContinuousMultilinearMap ùïú M F)]
    {m : ‚àÄ i, M i} (hm : ‚àÄ i, m i ‚â† 0) : CompleteSpace F := by
  refine Metric.complete_of_cauchySeq_tendsto fun f hf => ?_
  have : ‚àÄ i, ‚àÉ œÜ : StrongDual ùïú (M i), œÜ (m i) = 1 := fun i ‚Ü¶ exists_eq_one (hm i)
  choose œÜ hœÜ using this
  cases nonempty_fintype Œπ
  let g : ‚Ñï ‚Üí (ContinuousMultilinearMap ùïú M F) := fun n ‚Ü¶
    compContinuousLinearMapL œÜ
    (ContinuousMultilinearMap.smulRightL ùïú _ F ((ContinuousMultilinearMap.mkPiAlgebra ùïú Œπ ùïú)) (f n))
  have : CauchySeq g := by
    refine (ContinuousLinearMap.lipschitz _).cauchySeq_comp ?_
    exact (ContinuousLinearMap.lipschitz _).cauchySeq_comp hf
  obtain ‚ü®a, ha‚ü© : ‚àÉ a, Tendsto g atTop (ùìù a) := cauchy_iff_exists_le_nhds.mp this
  refine ‚ü®a m, ?_‚ü©
  have : Tendsto (fun n ‚Ü¶ g n m) atTop (ùìù (a m)) := ((continuous_eval_const _).tendsto _).comp ha
  simpa [g, hœÜ]

lemma completeSpace_continuousMultilinearMap_iff {m : ‚àÄ i, M i} (hm : ‚àÄ i, m i ‚â† 0) :
    CompleteSpace (ContinuousMultilinearMap ùïú M F) ‚Üî CompleteSpace F :=
  ‚ü®fun _h ‚Ü¶ completeSpace_of_completeSpace_continuousMultilinearMap ùïú F hm, fun _h ‚Ü¶ inferInstance‚ü©

end

end Field

end SeparatingDual
