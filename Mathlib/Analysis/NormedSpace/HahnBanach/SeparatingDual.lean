/-
Copyright (c) 2023 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension
import Mathlib.Analysis.NormedSpace.HahnBanach.Separation
import Mathlib.LinearAlgebra.Dual
import Mathlib.Analysis.NormedSpace.BoundedLinearMaps

/-!
# Spaces with separating dual

We introduce a typeclass `SeparatingDual R V`, registering that the points of the topological
module `V` over `R` can be separated by continuous linear forms.

This property is satisfied for normed spaces over `â„` or `â„‚` (by the analytic Hahn-Banach theorem)
and for locally convex topological spaces over `â„` (by the geometric Hahn-Banach theorem).

Under the assumption `SeparatingDual R V`, we show in
`SeparatingDual.exists_continuousLinearMap_apply_eq` that the group of continuous linear
equivalences acts transitively on the set of nonzero vectors.
-/

/-- When `E` is a topological module over a topological ring `R`, the class `SeparatingDual R E`
registers that continuous linear forms on `E` separate points of `E`. -/
@[mk_iff separatingDual_def]
class SeparatingDual (R V : Type*) [Ring R] [AddCommGroup V] [TopologicalSpace V]
    [TopologicalSpace R] [Module R V] : Prop :=
  /-- Any nonzero vector can be mapped by a continuous linear map to a nonzero scalar. -/
  exists_ne_zero' : âˆ€ (x : V), x â‰  0 â†’ âˆƒ f : V â†’L[R] R, f x â‰  0

instance {E : Type*} [TopologicalSpace E] [AddCommGroup E] [TopologicalAddGroup E]
    [Module â„ E] [ContinuousSMul â„ E] [LocallyConvexSpace â„ E] [T1Space E] : SeparatingDual â„ E :=
  âŸ¨fun x hx â†¦ by
    rcases geometric_hahn_banach_point_point hx.symm with âŸ¨f, hfâŸ©
    simp only [map_zero] at hf
    exact âŸ¨f, hf.ne'âŸ©âŸ©

instance {E ğ•œ : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] : SeparatingDual ğ•œ E :=
  âŸ¨fun x hx â†¦ by
    rcases exists_dual_vector ğ•œ x hx with âŸ¨f, -, hfâŸ©
    refine âŸ¨f, ?_âŸ©
    simpa [hf] using hxâŸ©

namespace SeparatingDual

section Ring

variable {R V : Type*} [Ring R] [AddCommGroup V] [TopologicalSpace V]
  [TopologicalSpace R] [Module R V] [SeparatingDual R V]

lemma exists_ne_zero {x : V} (hx : x â‰  0) :
    âˆƒ f : V â†’L[R] R, f x â‰  0 :=
  exists_ne_zero' x hx

theorem exists_separating_of_ne {x y : V} (h : x â‰  y) :
    âˆƒ f : V â†’L[R] R, f x â‰  f y := by
  rcases exists_ne_zero (R := R) (sub_ne_zero_of_ne h) with âŸ¨f, hfâŸ©
  exact âŸ¨f, by simpa [sub_ne_zero] using hfâŸ©

protected theorem t1Space [T1Space R] : T1Space V := by
  apply t1Space_iff_exists_open.2 (fun x y hxy â†¦ ?_)
  rcases exists_separating_of_ne (R := R) hxy with âŸ¨f, hfâŸ©
  exact âŸ¨f â»Â¹' {f y}á¶œ, isOpen_compl_singleton.preimage f.continuous, hf, by simpâŸ©

protected theorem t2Space [T2Space R] : T2Space V := by
  apply (t2Space_iff _).2 (fun {x} {y} hxy â†¦ ?_)
  rcases exists_separating_of_ne (R := R) hxy with âŸ¨f, hfâŸ©
  exact separated_by_continuous f.continuous hf

end Ring

section Field

variable {R V : Type*} [Field R] [AddCommGroup V] [TopologicalSpace R] [TopologicalSpace V]
  [TopologicalRing R] [TopologicalAddGroup V] [Module R V] [SeparatingDual R V]

-- TODO (@alreadydone): this could generalize to CommRing R if we were to add a section
theorem _root_.separatingDual_iff_injective : SeparatingDual R V â†”
    Function.Injective (ContinuousLinearMap.coeLM (R := R) R (M := V) (Nâ‚ƒ := R)).flip := by
  simp_rw [separatingDual_def, Ne, injective_iff_map_eq_zero]
  congrm âˆ€ v, ?_
  rw [not_imp_comm, LinearMap.ext_iff]
  push_neg; rfl

open Function in
/-- Given a finite-dimensional subspace `W` of a space `V` with separating dual, any
  linear functional on `W` extends to a continuous linear functional on `V`.
  This is stated more generally for an injective linear map from `W` to `V`. -/
theorem dualMap_surjective_iff {W} [AddCommGroup W] [Module R W] [FiniteDimensional R W]
    {f : W â†’â‚—[R] V} : Surjective (f.dualMap âˆ˜ ContinuousLinearMap.toLinearMap) â†” Injective f := by
  constructor <;> intro hf
  Â· exact LinearMap.dualMap_surjective_iff.mp hf.of_comp
  have := (separatingDual_iff_injective.mp â€¹_â€º).comp hf
  rw [â† LinearMap.coe_comp] at this
  exact LinearMap.flip_surjective_iffâ‚.mpr this

lemma exists_eq_one {x : V} (hx : x â‰  0) :
    âˆƒ f : V â†’L[R] R, f x = 1 := by
  rcases exists_ne_zero (R := R) hx with âŸ¨f, hfâŸ©
  exact âŸ¨(f x)â»Â¹ â€¢ f, inv_mul_cancel hfâŸ©

theorem exists_eq_one_ne_zero_of_ne_zero_pair {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    âˆƒ f : V â†’L[R] R, f x = 1 âˆ§ f y â‰  0 := by
  obtain âŸ¨u, uxâŸ© : âˆƒ u : V â†’L[R] R, u x = 1 := exists_eq_one hx
  rcases ne_or_eq (u y) 0 with uy|uy
  Â· exact âŸ¨u, ux, uyâŸ©
  obtain âŸ¨v, vyâŸ© : âˆƒ v : V â†’L[R] R, v y = 1 := exists_eq_one hy
  rcases ne_or_eq (v x) 0 with vx|vx
  Â· exact âŸ¨(v x)â»Â¹ â€¢ v, inv_mul_cancel vx, show (v x)â»Â¹ * v y â‰  0 by simp [vx, vy]âŸ©
  Â· exact âŸ¨u + v, by simp [ux, vx], by simp [uy, vy]âŸ©

/-- In a topological vector space with separating dual, the group of continuous linear equivalences
acts transitively on the set of nonzero vectors: given two nonzero vectors `x` and `y`, there
exists `A : V â‰ƒL[R] V` mapping `x` to `y`. -/
theorem exists_continuousLinearEquiv_apply_eq [ContinuousSMul R V]
    {x y : V} (hx : x â‰  0) (hy : y â‰  0) :
    âˆƒ A : V â‰ƒL[R] V, A x = y := by
  obtain âŸ¨G, Gx, GyâŸ© : âˆƒ G : V â†’L[R] R, G x = 1 âˆ§ G y â‰  0 :=
    exists_eq_one_ne_zero_of_ne_zero_pair hx hy
  let A : V â‰ƒL[R] V :=
  { toFun := fun z â†¦ z + G z â€¢ (y - x)
    invFun := fun z â†¦ z + ((G y) â»Â¹ * G z) â€¢ (x - y)
    map_add' := fun a b â†¦ by simp [add_smul]; abel
    map_smul' := by simp [smul_smul]
    left_inv := fun z â†¦ by
      simp only [id_eq, eq_mpr_eq_cast, RingHom.id_apply, smul_eq_mul, AddHom.toFun_eq_coe,
        -- Note: #8386 had to change `map_smulâ‚›â‚—` into `map_smulâ‚›â‚— _`
        AddHom.coe_mk, map_add, map_smulâ‚›â‚— _, map_sub, Gx, mul_sub, mul_one, add_sub_cancel]
      rw [mul_comm (G z), â† mul_assoc, inv_mul_cancel Gy]
      simp only [smul_sub, one_mul]
      abel
    right_inv := fun z â†¦ by
        -- Note: #8386 had to change `map_smulâ‚›â‚—` into `map_smulâ‚›â‚— _`
      simp only [map_add, map_smulâ‚›â‚— _, map_mul, map_invâ‚€, RingHom.id_apply, map_sub, Gx,
        smul_eq_mul, mul_sub, mul_one]
      rw [mul_comm _ (G y), â† mul_assoc, mul_inv_cancel Gy]
      simp only [smul_sub, one_mul, add_sub_cancel]
      abel
    continuous_toFun := continuous_id.add (G.continuous.smul continuous_const)
    continuous_invFun :=
      continuous_id.add ((continuous_const.mul G.continuous).smul continuous_const) }
  exact âŸ¨A, show x + G x â€¢ (y - x) = y by simp [Gx]âŸ©

open Filter
open scoped Topology

section
variable (ğ•œ E F : Type*) [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    [NormedAddCommGroup F] [NormedSpace ğ•œ F] [SeparatingDual ğ•œ E] [Nontrivial E]

/-- If a space of linear maps from `E` to `F` is complete, and `E` is nontrivial, then `F` is
complete. -/
lemma completeSpace_of_completeSpace_continuousLinearMap [CompleteSpace (E â†’L[ğ•œ] F)] :
    CompleteSpace F := by
  refine Metric.complete_of_cauchySeq_tendsto fun f hf => ?_
  obtain âŸ¨v, hvâŸ© : âˆƒ (v : E), v â‰  0 := exists_ne 0
  obtain âŸ¨Ï†, hÏ†âŸ© : âˆƒ Ï† : E â†’L[ğ•œ] ğ•œ, Ï† v = 1 := exists_eq_one hv
  let g : â„• â†’ (E â†’L[ğ•œ] F) := fun n â†¦ ContinuousLinearMap.smulRightL ğ•œ E F Ï† (f n)
  have : CauchySeq g := (ContinuousLinearMap.smulRightL ğ•œ E F Ï†).lipschitz.cauchySeq_comp hf
  obtain âŸ¨a, haâŸ© : âˆƒ a, Tendsto g atTop (ğ“ a) := cauchy_iff_exists_le_nhds.mp this
  refine âŸ¨a v, ?_âŸ©
  have : Tendsto (fun n â†¦ g n v) atTop (ğ“ (a v)) := by
    have : Continuous (fun (i : E â†’L[ğ•œ] F) â†¦ i v) := by continuity
    exact (this.tendsto _).comp ha
  simpa [g, ContinuousLinearMap.smulRightL, hÏ†]

lemma completeSpace_continuousLinearMap_iff :
    CompleteSpace (E â†’L[ğ•œ] F) â†” CompleteSpace F :=
  âŸ¨fun _h â†¦ completeSpace_of_completeSpace_continuousLinearMap ğ•œ E F, fun _h â†¦ inferInstanceâŸ©

open ContinuousMultilinearMap

variable {Î¹ : Type*} [Fintype Î¹] {M : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (M i)]
  [âˆ€ i, NormedSpace ğ•œ (M i)] [âˆ€ i, SeparatingDual ğ•œ (M i)]

/-- If a space of multilinear maps from `Î  i, E i` to `F` is complete, and each `E i` has a nonzero
element, then `F` is complete. -/
lemma completeSpace_of_completeSpace_continuousMultilinearMap
    [CompleteSpace (ContinuousMultilinearMap ğ•œ M F)]
    {m : âˆ€ i, M i} (hm : âˆ€ i, m i â‰  0) : CompleteSpace F := by
  refine Metric.complete_of_cauchySeq_tendsto fun f hf => ?_
  have : âˆ€ i, âˆƒ Ï† : M i â†’L[ğ•œ] ğ•œ, Ï† (m i) = 1 := fun i â†¦ exists_eq_one (hm i)
  choose Ï† hÏ† using this
  let g : â„• â†’ (ContinuousMultilinearMap ğ•œ M F) := fun n â†¦
    compContinuousLinearMapL Ï†
    (ContinuousMultilinearMap.smulRightL ğ•œ _ F ((ContinuousMultilinearMap.mkPiAlgebra ğ•œ Î¹ ğ•œ)) (f n))
  have : CauchySeq g := by
    refine (ContinuousLinearMap.lipschitz _).cauchySeq_comp ?_
    exact (ContinuousLinearMap.lipschitz _).cauchySeq_comp hf
  obtain âŸ¨a, haâŸ© : âˆƒ a, Tendsto g atTop (ğ“ a) := cauchy_iff_exists_le_nhds.mp this
  refine âŸ¨a m, ?_âŸ©
  have : Tendsto (fun n â†¦ g n m) atTop (ğ“ (a m)) := ((continuous_eval_const _).tendsto _).comp ha
  simpa [g, hÏ†]

lemma completeSpace_continuousMultilinearMap_iff {m : âˆ€ i, M i} (hm : âˆ€ i, m i â‰  0) :
    CompleteSpace (ContinuousMultilinearMap ğ•œ M F) â†” CompleteSpace F :=
  âŸ¨fun _h â†¦ completeSpace_of_completeSpace_continuousMultilinearMap ğ•œ F hm, fun _h â†¦ inferInstanceâŸ©

end

end Field

end SeparatingDual
