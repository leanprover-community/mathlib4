/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, Yury Kudryashov
-/
import Mathlib.Algebra.CharP.Invertible
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.Analysis.Normed.Group.AddTorsor
import Mathlib.LinearAlgebra.AffineSpace.AffineSubspace
import Mathlib.Topology.Instances.RealVectorSpace

#align_import analysis.normed_space.add_torsor from "leanprover-community/mathlib"@"837f72de63ad6cd96519cde5f1ffd5ed8d280ad0"

/-!
# Torsors of normed space actions.

This file contains lemmas about normed additive torsors over normed spaces.
-/


noncomputable section

open NNReal Topology

open Filter

variable {Î± V P W Q : Type*} [SeminormedAddCommGroup V] [PseudoMetricSpace P] [NormedAddTorsor V P]
  [NormedAddCommGroup W] [MetricSpace Q] [NormedAddTorsor W Q]

section NormedSpace

variable {ğ•œ : Type*} [NormedField ğ•œ] [NormedSpace ğ•œ V] [NormedSpace ğ•œ W]

open AffineMap

theorem AffineSubspace.isClosed_direction_iff (s : AffineSubspace ğ•œ Q) :
    IsClosed (s.direction : Set W) â†” IsClosed (s : Set Q) := by
  rcases s.eq_bot_or_nonempty with (rfl | âŸ¨x, hxâŸ©); Â· simp [isClosed_singleton]
  rw [â† (IsometryEquiv.vaddConst x).toHomeomorph.symm.isClosed_image,
    AffineSubspace.coe_direction_eq_vsub_set_right hx]
  rfl
#align affine_subspace.is_closed_direction_iff AffineSubspace.isClosed_direction_iff

@[simp]
theorem dist_center_homothety (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    dist pâ‚ (homothety pâ‚ c pâ‚‚) = â€–câ€– * dist pâ‚ pâ‚‚ := by
  simp [homothety_def, norm_smul, â† dist_eq_norm_vsub, dist_comm]
#align dist_center_homothety dist_center_homothety

@[simp]
theorem nndist_center_homothety (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist pâ‚ (homothety pâ‚ c pâ‚‚) = â€–câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_center_homothety _ _ _
#align nndist_center_homothety nndist_center_homothety

@[simp]
theorem dist_homothety_center (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    dist (homothety pâ‚ c pâ‚‚) pâ‚ = â€–câ€– * dist pâ‚ pâ‚‚ := by rw [dist_comm, dist_center_homothety]
#align dist_homothety_center dist_homothety_center

@[simp]
theorem nndist_homothety_center (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist (homothety pâ‚ c pâ‚‚) pâ‚ = â€–câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_homothety_center _ _ _
#align nndist_homothety_center nndist_homothety_center

@[simp]
theorem dist_lineMap_lineMap (pâ‚ pâ‚‚ : P) (câ‚ câ‚‚ : ğ•œ) :
    dist (lineMap pâ‚ pâ‚‚ câ‚) (lineMap pâ‚ pâ‚‚ câ‚‚) = dist câ‚ câ‚‚ * dist pâ‚ pâ‚‚ := by
  rw [dist_comm pâ‚ pâ‚‚]
  simp only [lineMap_apply, dist_eq_norm_vsub, vadd_vsub_vadd_cancel_right,
    â† sub_smul, norm_smul, vsub_eq_sub]
#align dist_line_map_line_map dist_lineMap_lineMap

@[simp]
theorem nndist_lineMap_lineMap (pâ‚ pâ‚‚ : P) (câ‚ câ‚‚ : ğ•œ) :
    nndist (lineMap pâ‚ pâ‚‚ câ‚) (lineMap pâ‚ pâ‚‚ câ‚‚) = nndist câ‚ câ‚‚ * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_lineMap_lineMap _ _ _ _
#align nndist_line_map_line_map nndist_lineMap_lineMap

theorem lipschitzWith_lineMap (pâ‚ pâ‚‚ : P) : LipschitzWith (nndist pâ‚ pâ‚‚) (lineMap pâ‚ pâ‚‚ : ğ•œ â†’ P) :=
  LipschitzWith.of_dist_le_mul fun câ‚ câ‚‚ =>
    ((dist_lineMap_lineMap pâ‚ pâ‚‚ câ‚ câ‚‚).trans (mul_comm _ _)).le
#align lipschitz_with_line_map lipschitzWith_lineMap

@[simp]
theorem dist_lineMap_left (pâ‚ pâ‚‚ : P) (c : ğ•œ) : dist (lineMap pâ‚ pâ‚‚ c) pâ‚ = â€–câ€– * dist pâ‚ pâ‚‚ := by
  simpa only [lineMap_apply_zero, dist_zero_right] using dist_lineMap_lineMap pâ‚ pâ‚‚ c 0
#align dist_line_map_left dist_lineMap_left

@[simp]
theorem nndist_lineMap_left (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist (lineMap pâ‚ pâ‚‚ c) pâ‚ = â€–câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_lineMap_left _ _ _
#align nndist_line_map_left nndist_lineMap_left

@[simp]
theorem dist_left_lineMap (pâ‚ pâ‚‚ : P) (c : ğ•œ) : dist pâ‚ (lineMap pâ‚ pâ‚‚ c) = â€–câ€– * dist pâ‚ pâ‚‚ :=
  (dist_comm _ _).trans (dist_lineMap_left _ _ _)
#align dist_left_line_map dist_left_lineMap

@[simp]
theorem nndist_left_lineMap (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist pâ‚ (lineMap pâ‚ pâ‚‚ c) = â€–câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_left_lineMap _ _ _
#align nndist_left_line_map nndist_left_lineMap

@[simp]
theorem dist_lineMap_right (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    dist (lineMap pâ‚ pâ‚‚ c) pâ‚‚ = â€–1 - câ€– * dist pâ‚ pâ‚‚ := by
  simpa only [lineMap_apply_one, dist_eq_norm'] using dist_lineMap_lineMap pâ‚ pâ‚‚ c 1
#align dist_line_map_right dist_lineMap_right

@[simp]
theorem nndist_lineMap_right (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist (lineMap pâ‚ pâ‚‚ c) pâ‚‚ = â€–1 - câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_lineMap_right _ _ _
#align nndist_line_map_right nndist_lineMap_right

@[simp]
theorem dist_right_lineMap (pâ‚ pâ‚‚ : P) (c : ğ•œ) : dist pâ‚‚ (lineMap pâ‚ pâ‚‚ c) = â€–1 - câ€– * dist pâ‚ pâ‚‚ :=
  (dist_comm _ _).trans (dist_lineMap_right _ _ _)
#align dist_right_line_map dist_right_lineMap

@[simp]
theorem nndist_right_lineMap (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist pâ‚‚ (lineMap pâ‚ pâ‚‚ c) = â€–1 - câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_right_lineMap _ _ _
#align nndist_right_line_map nndist_right_lineMap

@[simp]
theorem dist_homothety_self (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    dist (homothety pâ‚ c pâ‚‚) pâ‚‚ = â€–1 - câ€– * dist pâ‚ pâ‚‚ := by
  rw [homothety_eq_lineMap, dist_lineMap_right]
#align dist_homothety_self dist_homothety_self

@[simp]
theorem nndist_homothety_self (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist (homothety pâ‚ c pâ‚‚) pâ‚‚ = â€–1 - câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_homothety_self _ _ _
#align nndist_homothety_self nndist_homothety_self

@[simp]
theorem dist_self_homothety (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    dist pâ‚‚ (homothety pâ‚ c pâ‚‚) = â€–1 - câ€– * dist pâ‚ pâ‚‚ := by rw [dist_comm, dist_homothety_self]
#align dist_self_homothety dist_self_homothety

@[simp]
theorem nndist_self_homothety (pâ‚ pâ‚‚ : P) (c : ğ•œ) :
    nndist pâ‚‚ (homothety pâ‚ c pâ‚‚) = â€–1 - câ€–â‚Š * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_self_homothety _ _ _
#align nndist_self_homothety nndist_self_homothety

section invertibleTwo

variable [Invertible (2 : ğ•œ)]

@[simp]
theorem dist_left_midpoint (pâ‚ pâ‚‚ : P) : dist pâ‚ (midpoint ğ•œ pâ‚ pâ‚‚) = â€–(2 : ğ•œ)â€–â»Â¹ * dist pâ‚ pâ‚‚ := by
  rw [midpoint, dist_comm, dist_lineMap_left, invOf_eq_inv, â† norm_inv]
#align dist_left_midpoint dist_left_midpoint

@[simp]
theorem nndist_left_midpoint (pâ‚ pâ‚‚ : P) :
    nndist pâ‚ (midpoint ğ•œ pâ‚ pâ‚‚) = â€–(2 : ğ•œ)â€–â‚Šâ»Â¹ * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_left_midpoint _ _
#align nndist_left_midpoint nndist_left_midpoint

@[simp]
theorem dist_midpoint_left (pâ‚ pâ‚‚ : P) : dist (midpoint ğ•œ pâ‚ pâ‚‚) pâ‚ = â€–(2 : ğ•œ)â€–â»Â¹ * dist pâ‚ pâ‚‚ := by
  rw [dist_comm, dist_left_midpoint]
#align dist_midpoint_left dist_midpoint_left

@[simp]
theorem nndist_midpoint_left (pâ‚ pâ‚‚ : P) :
    nndist (midpoint ğ•œ pâ‚ pâ‚‚) pâ‚ = â€–(2 : ğ•œ)â€–â‚Šâ»Â¹ * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_midpoint_left _ _
#align nndist_midpoint_left nndist_midpoint_left

@[simp]
theorem dist_midpoint_right (pâ‚ pâ‚‚ : P) :
    dist (midpoint ğ•œ pâ‚ pâ‚‚) pâ‚‚ = â€–(2 : ğ•œ)â€–â»Â¹ * dist pâ‚ pâ‚‚ := by
  rw [midpoint_comm, dist_midpoint_left, dist_comm]
#align dist_midpoint_right dist_midpoint_right

@[simp]
theorem nndist_midpoint_right (pâ‚ pâ‚‚ : P) :
    nndist (midpoint ğ•œ pâ‚ pâ‚‚) pâ‚‚ = â€–(2 : ğ•œ)â€–â‚Šâ»Â¹ * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_midpoint_right _ _
#align nndist_midpoint_right nndist_midpoint_right

@[simp]
theorem dist_right_midpoint (pâ‚ pâ‚‚ : P) :
    dist pâ‚‚ (midpoint ğ•œ pâ‚ pâ‚‚) = â€–(2 : ğ•œ)â€–â»Â¹ * dist pâ‚ pâ‚‚ := by
  rw [dist_comm, dist_midpoint_right]
#align dist_right_midpoint dist_right_midpoint

@[simp]
theorem nndist_right_midpoint (pâ‚ pâ‚‚ : P) :
    nndist pâ‚‚ (midpoint ğ•œ pâ‚ pâ‚‚) = â€–(2 : ğ•œ)â€–â‚Šâ»Â¹ * nndist pâ‚ pâ‚‚ :=
  NNReal.eq <| dist_right_midpoint _ _
#align nndist_right_midpoint nndist_right_midpoint

theorem dist_midpoint_midpoint_le' (pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : P) :
    dist (midpoint ğ•œ pâ‚ pâ‚‚) (midpoint ğ•œ pâ‚ƒ pâ‚„) â‰¤ (dist pâ‚ pâ‚ƒ + dist pâ‚‚ pâ‚„) / â€–(2 : ğ•œ)â€– := by
  rw [dist_eq_norm_vsub V, dist_eq_norm_vsub V, dist_eq_norm_vsub V, midpoint_vsub_midpoint]
  rw [midpoint_eq_smul_add, norm_smul, invOf_eq_inv, norm_inv, â† div_eq_inv_mul]
  exact div_le_div_of_nonneg_right (norm_add_le _ _) (norm_nonneg _)
#align dist_midpoint_midpoint_le' dist_midpoint_midpoint_le'

theorem nndist_midpoint_midpoint_le' (pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : P) :
    nndist (midpoint ğ•œ pâ‚ pâ‚‚) (midpoint ğ•œ pâ‚ƒ pâ‚„) â‰¤ (nndist pâ‚ pâ‚ƒ + nndist pâ‚‚ pâ‚„) / â€–(2 : ğ•œ)â€–â‚Š :=
  dist_midpoint_midpoint_le' _ _ _ _
#align nndist_midpoint_midpoint_le' nndist_midpoint_midpoint_le'

end invertibleTwo

@[simp] theorem dist_pointReflection_left (p q : P) :
    dist (Equiv.pointReflection p q) p = dist p q := by
  simp [dist_eq_norm_vsub V, Equiv.pointReflection_vsub_left (G := V)]

@[simp] theorem dist_left_pointReflection (p q : P) :
    dist p (Equiv.pointReflection p q) = dist p q :=
  (dist_comm _ _).trans (dist_pointReflection_left _ _)

variable (ğ•œ) in
theorem dist_pointReflection_right (p q : P) :
    dist (Equiv.pointReflection p q) q = â€–(2 : ğ•œ)â€– * dist p q := by
  simp [dist_eq_norm_vsub V, Equiv.pointReflection_vsub_right (G := V),
    nsmul_eq_smul_cast ğ•œ, norm_smul]

variable (ğ•œ) in
theorem dist_right_pointReflection (p q : P) :
    dist q (Equiv.pointReflection p q) = â€–(2 : ğ•œ)â€– * dist p q :=
  (dist_comm _ _).trans (dist_pointReflection_right ğ•œ _ _)

theorem antilipschitzWith_lineMap {pâ‚ pâ‚‚ : Q} (h : pâ‚ â‰  pâ‚‚) :
    AntilipschitzWith (nndist pâ‚ pâ‚‚)â»Â¹ (lineMap pâ‚ pâ‚‚ : ğ•œ â†’ Q) :=
  AntilipschitzWith.of_le_mul_dist fun câ‚ câ‚‚ => by
    rw [dist_lineMap_lineMap, NNReal.coe_inv, â† dist_nndist, mul_left_comm,
      inv_mul_cancel (dist_ne_zero.2 h), mul_one]
#align antilipschitz_with_line_map antilipschitzWith_lineMap

variable (ğ•œ)

theorem eventually_homothety_mem_of_mem_interior (x : Q) {s : Set Q} {y : Q} (hy : y âˆˆ interior s) :
    âˆ€á¶  Î´ in ğ“ (1 : ğ•œ), homothety x Î´ y âˆˆ s := by
  rw [(NormedAddCommGroup.nhds_basis_norm_lt (1 : ğ•œ)).eventually_iff]
  rcases eq_or_ne y x with h | h
  Â· use 1
    simp [h.symm, interior_subset hy]
  have hxy : 0 < â€–y -áµ¥ xâ€– := by rwa [norm_pos_iff, vsub_ne_zero]
  obtain âŸ¨u, huâ‚, huâ‚‚, huâ‚ƒâŸ© := mem_interior.mp hy
  obtain âŸ¨Îµ, hÎµ, hyÎµâŸ© := Metric.isOpen_iff.mp huâ‚‚ y huâ‚ƒ
  refine âŸ¨Îµ / â€–y -áµ¥ xâ€–, div_pos hÎµ hxy, fun Î´ (hÎ´ : â€–Î´ - 1â€– < Îµ / â€–y -áµ¥ xâ€–) => huâ‚ (hyÎµ ?_)âŸ©
  rw [lt_div_iff hxy, â† norm_smul, sub_smul, one_smul] at hÎ´
  rwa [homothety_apply, Metric.mem_ball, dist_eq_norm_vsub W, vadd_vsub_eq_sub_vsub]
#align eventually_homothety_mem_of_mem_interior eventually_homothety_mem_of_mem_interior

theorem eventually_homothety_image_subset_of_finite_subset_interior (x : Q) {s : Set Q} {t : Set Q}
    (ht : t.Finite) (h : t âŠ† interior s) : âˆ€á¶  Î´ in ğ“ (1 : ğ•œ), homothety x Î´ '' t âŠ† s := by
  suffices âˆ€ y âˆˆ t, âˆ€á¶  Î´ in ğ“ (1 : ğ•œ), homothety x Î´ y âˆˆ s by
    simp_rw [Set.image_subset_iff]
    exact (Filter.eventually_all_finite ht).mpr this
  intro y hy
  exact eventually_homothety_mem_of_mem_interior ğ•œ x (h hy)
#align eventually_homothety_image_subset_of_finite_subset_interior eventually_homothety_image_subset_of_finite_subset_interior

end NormedSpace

variable [NormedSpace â„ V] [NormedSpace â„ W]

theorem dist_midpoint_midpoint_le (pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : V) :
    dist (midpoint â„ pâ‚ pâ‚‚) (midpoint â„ pâ‚ƒ pâ‚„) â‰¤ (dist pâ‚ pâ‚ƒ + dist pâ‚‚ pâ‚„) / 2 := by
  simpa using dist_midpoint_midpoint_le' (ğ•œ := â„) pâ‚ pâ‚‚ pâ‚ƒ pâ‚„
#align dist_midpoint_midpoint_le dist_midpoint_midpoint_le

theorem nndist_midpoint_midpoint_le (pâ‚ pâ‚‚ pâ‚ƒ pâ‚„ : V) :
    nndist (midpoint â„ pâ‚ pâ‚‚) (midpoint â„ pâ‚ƒ pâ‚„) â‰¤ (nndist pâ‚ pâ‚ƒ + nndist pâ‚‚ pâ‚„) / 2 :=
  dist_midpoint_midpoint_le _ _ _ _
#align nndist_midpoint_midpoint_le nndist_midpoint_midpoint_le

/-- A continuous map between two normed affine spaces is an affine map provided that
it sends midpoints to midpoints. -/
def AffineMap.ofMapMidpoint (f : P â†’ Q) (h : âˆ€ x y, f (midpoint â„ x y) = midpoint â„ (f x) (f y))
    (hfc : Continuous f) : P â†’áµƒ[â„] Q :=
  let c := Classical.arbitrary P
  AffineMap.mk' f (â†‘((AddMonoidHom.ofMapMidpoint â„ â„
    ((AffineEquiv.vaddConst â„ (f <| c)).symm âˆ˜ f âˆ˜ AffineEquiv.vaddConst â„ c) (by simp)
    fun x y => by -- Porting note: was `by simp [h]`
      simp only [c, Function.comp_apply, AffineEquiv.vaddConst_apply,
        AffineEquiv.vaddConst_symm_apply]
      conv_lhs => rw [(midpoint_self â„ (Classical.arbitrary P)).symm, midpoint_vadd_midpoint, h, h,
          midpoint_vsub_midpoint]).toRealLinearMap <| by
        apply_rules [Continuous.vadd, Continuous.vsub, continuous_const, hfc.comp, continuous_id]))
    c fun p => by simp
#align affine_map.of_map_midpoint AffineMap.ofMapMidpoint

end

section

open Dilation

variable {ğ•œ E : Type*} [NormedDivisionRing ğ•œ] [SeminormedAddCommGroup E]
variable [Module ğ•œ E] [BoundedSMul ğ•œ E] {P : Type*} [PseudoMetricSpace P] [NormedAddTorsor E P]

-- TODO: define `ContinuousAffineEquiv` and reimplement this as one of those.
/-- Scaling by an element `k` of the scalar ring as a `DilationEquiv` with ratio `â€–kâ€–â‚Š`, mapping
from a normed space to a normed torsor over that space sending `0` to `c`. -/
@[simps]
def DilationEquiv.smulTorsor (c : P) {k : ğ•œ} (hk : k â‰  0) : E â‰ƒáµˆ P where
  toFun := (k â€¢ Â· +áµ¥ c)
  invFun := kâ»Â¹ â€¢ (Â· -áµ¥ c)
  left_inv x := by simp [inv_smul_smulâ‚€ hk]
  right_inv p := by simp [smul_inv_smulâ‚€ hk]
  edist_eq' := âŸ¨â€–kâ€–â‚Š, nnnorm_ne_zero_iff.mpr hk, fun x y â†¦ by
    rw [show edist (k â€¢ x +áµ¥ c) (k â€¢ y +áµ¥ c) = _ from (IsometryEquiv.vaddConst c).isometry ..]
    exact edist_smulâ‚€ ..âŸ©

@[simp]
lemma DilationEquiv.smulTorsor_ratio {c : P} {k : ğ•œ} (hk : k â‰  0) {x y : E}
    (h : dist x y â‰  0) : ratio (smulTorsor c hk) = â€–kâ€–â‚Š :=
  Eq.symm <| ratio_unique_of_dist_ne_zero h <| by simp [dist_eq_norm, â† smul_sub, norm_smul]

@[simp]
lemma DilationEquiv.smulTorsor_preimage_ball {c : P} {k : ğ•œ} (hk : k â‰  0) :
    smulTorsor c hk â»Â¹' (Metric.ball c â€–kâ€–) = Metric.ball (0 : E) 1 := by
  aesop (add simp norm_smul)

end
