import Mathlib.Analysis.Calculus.Deriv.Abs
import Mathlib.Analysis.Calculus.Rademacher
import Mathlib.Analysis.Normed.Module.WeakDual
import Mathlib.Data.Real.Sign
import Mathlib.LinearAlgebra.Dimension.Finrank

open Real NNReal Set Filter Topology FiniteDimensional MeasureTheory Module Submodule LinearMap

variable {E : Type*}
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

theorem dense_of_ae {X : Type*} [TopologicalSpace X] [MeasurableSpace X]
    {Î¼ : Measure X} [Î¼.IsOpenPosMeasure]
    {p : X â†’ Prop} (hp : âˆ€áµ x âˆ‚Î¼, p x) : Dense {x | p x} := by
  rw [dense_iff_closure_eq, closure_eq_compl_interior_compl, compl_univ_iff]
  exact Î¼.interior_eq_empty_of_null hp

section OfTopLeSpan

variable {K V : Type*} [DivisionRing K] [AddCommGroup V] [Module K V]
variable {s t : Set V}

namespace Basis

noncomputable instance [Module.Finite K V] (hs : LinearIndependent K ((â†‘) : s â†’ V)) (hst : s âŠ† t) :
    Fintype (hs.extend hst) := by
  refine Classical.choice (Cardinal.lt_aleph0_iff_fintype.1 ?_)
  refine lt_of_le_of_lt (LinearIndependent.cardinal_le_rank' (hs.linearIndependent_extend hst)) ?_
  exact rank_lt_aleph0 K V

end Basis

end OfTopLeSpan

variable [NormedAddCommGroup E] [NormedSpace â„ E]

theorem span_eq_top_of_ne_zero {R M : Type*} [CommRing R] [AddCommGroup M]
    [Module R M] [IsReflexive R M]
    {s : Set (M â†’â‚—[R] R)} [Free R ((M â†’â‚—[R] R) â§¸ (span R s))]
    (h : âˆ€ z : M, z â‰  0 â†’ âˆƒ f âˆˆ s, f z â‰  0) :
    âŠ¤ â‰¤ span R s := by
  by_contra hn
  replace hn := (ne_of_not_le hn).symm.lt_top
  rcases exists_dual_map_eq_bot_of_lt_top hn inferInstance with âŸ¨Ï†, Ï†ne, hÏ†âŸ©
  let Ï†s := (Module.evalEquiv R M).symm Ï†
  have : âˆ€ f âˆˆ s, f Ï†s = 0 := by
    intro f hf
    rw [â† mem_bot R, â† hÏ†, Submodule.mem_map]
    exact âŸ¨f, Submodule.subset_span hf, (apply_evalEquiv_symm_apply R M f Ï†).symmâŸ©
  have Ï†sn : Ï†s â‰  0 := by simp [Ï†ne, Ï†s]
  rcases h Ï†s Ï†sn with âŸ¨x, xs, hxâŸ©
  exact hx <| this x xs

theorem hasFDerivAt_norm_smul {x : E} {t : â„} (ht : t â‰  0)
    {f : E â†’L[â„] â„} (hx : HasFDerivAt (â€–Â·â€–) f x) :
    HasFDerivAt (â€–Â·â€–) ((SignType.sign t : â„) â€¢ f) (t â€¢ x) := by
  replace hx := (hx.hasFDerivAtFilter le_rfl).isLittleO
  constructor
  rw [Asymptotics.isLittleO_iff] at *
  intro c hc
  have := hx hc
  rw [eventually_iff, â† smul_mem_nhds_smul_iffâ‚€ ht] at this
  filter_upwards [this]
  rintro - âŸ¨Îµ, hÎµ, rflâŸ©
  simp only
  rw [norm_smul, norm_smul, â† smul_sub, _root_.map_smul, â† ContinuousLinearMap.smul_apply,
    smul_smul, self_mul_sign, ContinuousLinearMap.smul_apply, â† norm_eq_abs, smul_eq_mul,
    â† mul_sub, â† mul_sub, norm_mul, norm_norm, norm_smul, â† mul_assoc, mul_comm c, mul_assoc,
    _root_.mul_le_mul_left]
  Â· exact hÎµ
  Â· exact norm_pos_iff.2 ht

theorem differentiableAt_norm_smul {x : E} {t : â„} (ht : t â‰  0) :
    DifferentiableAt â„ (â€–Â·â€–) x â†” DifferentiableAt â„ (â€–Â·â€–) (t â€¢ x) where
  mp hd := (hasFDerivAt_norm_smul ht hd.hasFDerivAt).differentiableAt
  mpr hd := by
    convert (hasFDerivAt_norm_smul (inv_ne_zero ht) hd.hasFDerivAt).differentiableAt
    rw [smul_smul, inv_mul_cancelâ‚€ ht, one_smul]

theorem not_differentiableAt_norm_zero (E : Type*) [NormedAddCommGroup E] [NormedSpace â„ E]
    [Nontrivial E] :
    Â¬DifferentiableAt â„ (â€–Â·â€–) (0 : E) := by
  obtain âŸ¨x, hxâŸ© := NormedSpace.exists_lt_norm â„ E 0
  intro h
  have : DifferentiableAt â„ (fun t : â„ â†¦ â€–t â€¢ xâ€–) 0 := DifferentiableAt.comp _ (by simpa) (by simp)
  have : DifferentiableAt â„ (|Â·|) (0 : â„) := by
    simp_rw [norm_smul, norm_eq_abs] at this
    have aux : abs = fun t â†¦ (1 / â€–xâ€–) * (|t| * â€–xâ€–) := by
      ext t
      field_simp
    rw [aux]
    exact this.const_mul _
  exact not_differentiableAt_abs_zero this

theorem differentiableAt_norm_of_smul {x : E} {t : â„} (h : DifferentiableAt â„ (â€–Â·â€–) (t â€¢ x)) :
    DifferentiableAt â„ (â€–Â·â€–) x := by
  rcases eq_or_ne t 0 with rfl | ht
  Â· by_cases hE : Nontrivial E
    Â· rw [zero_smul] at h
      exact not_differentiableAt_norm_zero E h |>.elim
    Â· rw [not_nontrivial_iff_subsingleton] at hE
      exact (hasFDerivAt_of_subsingleton _ _).differentiableAt
  Â· exact differentiableAt_norm_smul ht |>.2 h

theorem fderiv_norm_self {x : E} (h : DifferentiableAt â„ (â€–Â·â€–) x) :
    fderiv â„ (â€–Â·â€–) x x = â€–xâ€– := by
  rw [â† h.lineDeriv_eq_fderiv, lineDeriv]
  have this (t : â„) : â€–x + t â€¢ xâ€– = |1 + t| * â€–xâ€– := by
    rw [â† norm_eq_abs, â† norm_smul, add_smul, one_smul]
  simp_rw [this]
  rw [deriv_mul_const]
  Â· conv_lhs => enter [1, 1]; change abs âˆ˜ (fun t â†¦ 1 + t)
    rw [deriv.comp, deriv_abs, deriv_const_add]
    Â· simp
    Â· exact differentiableAt_abs (by norm_num)
    Â· exact differentiableAt_id.const_add _
  Â· exact (differentiableAt_abs (by norm_num)).comp _ (differentiableAt_id.const_add _)

theorem fderiv_norm_smul (x : E) (t : â„) :
    fderiv â„ (â€–Â·â€–) (t â€¢ x) = (SignType.sign t : â„) â€¢ (fderiv â„ (â€–Â·â€–) x) := by
  by_cases hE : Nontrivial E
  Â· by_cases hd : DifferentiableAt â„ (â€–Â·â€–) x
    Â· rcases eq_or_ne t 0 with rfl | ht
      Â· simp only [zero_smul, _root_.sign_zero, SignType.coe_zero]
        exact fderiv_zero_of_not_differentiableAt <| not_differentiableAt_norm_zero E
      Â· rw [(hasFDerivAt_norm_smul ht hd.hasFDerivAt).fderiv]
    Â· rw [fderiv_zero_of_not_differentiableAt hd, fderiv_zero_of_not_differentiableAt]
      Â· simp
      Â· exact mt differentiableAt_norm_of_smul hd
  Â· rw [not_nontrivial_iff_subsingleton] at hE
    simp_rw [(hasFDerivAt_of_subsingleton _ _).fderiv, smul_zero]

theorem fderiv_norm_smul_pos {x : E} {t : â„} (ht : 0 < t) :
    fderiv â„ (â€–Â·â€–) (t â€¢ x) = fderiv â„ (â€–Â·â€–) x := by
  simp [fderiv_norm_smul, ht]

theorem fderiv_norm_smul_neg {x : E} {t : â„} (ht : t < 0) :
    fderiv â„ (â€–Â·â€–) (t â€¢ x) = -fderiv â„ (â€–Â·â€–) x := by
  simp [fderiv_norm_smul, ht]

theorem norm_fderiv_norm [Nontrivial E] {x : E} (h : DifferentiableAt â„ (â€–Â·â€–) x) :
    â€–fderiv â„ (â€–Â·â€–) xâ€– = 1 := by
  have : x â‰  0 := by
    refine fun hx â†¦ not_differentiableAt_norm_zero E ?_
    rwa [â† hx]
  apply le_antisymm
  Â· rw [show (1 : â„) = â†‘(1 : â„â‰¥0) by rfl]
    exact norm_fderiv_le_of_lipschitz â„ lipschitzWith_one_norm
  Â· apply le_of_mul_le_mul_right _ (norm_pos_iff.2 this)
    calc
      1 * â€–xâ€– = fderiv â„ (â€–Â·â€–) x x := by rw [one_mul, fderiv_norm_self h]
      _ â‰¤ â€–fderiv â„ (â€–Â·â€–) x xâ€– := le_norm_self _
      _ â‰¤ â€–fderiv â„ (â€–Â·â€–) xâ€– * â€–xâ€– := ContinuousLinearMap.le_opNorm _ _

noncomputable def CoeffSpan {x : E} (nx : x â‰  0) : span â„ {x} â†’â‚—[â„] â„ where
  toFun y := (mem_span_singleton.1 y.2).choose
  map_add' y z := by
    have h1 := (mem_span_singleton.1 y.2).choose_spec
    have h2 := (mem_span_singleton.1 z.2).choose_spec
    have h3 : (mem_span_singleton.1 (y + z).2).choose â€¢ x = y + z :=
      (mem_span_singleton.1 (y + z).2).choose_spec
    rw [â† h1, â† h2, â† add_smul] at h3
    exact smul_left_injective â„ nx h3
  map_smul' t y := by
    have h1 := (mem_span_singleton.1 y.2).choose_spec
    have h2 : (mem_span_singleton.1 (t â€¢ y).2).choose â€¢ x = t â€¢ y :=
      (mem_span_singleton.1 (t â€¢ y).2).choose_spec
    rw [â† h1, smul_smul] at h2
    exact smul_left_injective â„ nx h2

theorem coeffSpan_smul {x : E} (nx : x â‰  0) (y : span â„ {x}) : (CoeffSpan nx y) â€¢ x = y :=
  (mem_span_singleton.1 y.2).choose_spec

theorem coeffSpan_self {x : E} (nx : x â‰  0) :
    CoeffSpan nx âŸ¨x, mem_span_singleton_self xâŸ© = 1 := by
  have hx : x âˆˆ span â„ {x} := mem_span_singleton_self x
  have : (CoeffSpan nx âŸ¨x, hxâŸ©) â€¢ x = x := coeffSpan_smul nx _
  apply smul_left_injective â„ nx
  simp [this]

theorem exists_eq_norm [Nontrivial E] (x : E) : âˆƒ f : E â†’L[â„] â„, â€–fâ€– = 1 âˆ§ f x = â€–xâ€– := by
  wlog nx : x â‰  0
  Â· cases not_ne_iff.1 nx
    obtain âŸ¨x, nxâŸ© := exists_ne (0 : E)
    obtain âŸ¨f, nf, -âŸ© := this x nx
    exact âŸ¨f, nf, by simpâŸ©
  let g' : span â„ {x} â†’â‚—[â„] â„ :=
    { toFun := fun y â†¦ (CoeffSpan nx y) * â€–xâ€–
      map_add' := fun y z â†¦ by simp [add_mul]
      map_smul' := fun t y â†¦ by simp [mul_assoc] }
  let g := LinearMap.toContinuousLinearMap g'
  have ng y : â€–g yâ€– = â€–yâ€– := by
    change â€–(CoeffSpan nx y) * â€–xâ€–â€– = â€–yâ€–
    rw [â† norm_coe y, â† coeffSpan_smul nx y, norm_smul, norm_mul, norm_norm]
  rcases Real.exists_extension_norm_eq (span â„ {x}) g with âŸ¨f, hf, nfâŸ©
  have hx : x âˆˆ span â„ {x} := mem_span_singleton_self x
  refine âŸ¨f, ?_, ?_âŸ©
  Â· rw [nf]
    apply le_antisymm
    Â· refine g.opNorm_le_bound (by norm_num) (fun y â†¦ ?_)
      simp [ng]
    Â· apply le_of_mul_le_mul_right _ (norm_pos_iff.2 nx)
      rw [one_mul, show â€–xâ€– = â€–(âŸ¨x, hxâŸ© : span â„ {x})â€– by rfl]
      nth_rw 1 [â† ng âŸ¨x, hxâŸ©]
      exact g.le_opNorm _
  Â· change f (âŸ¨x, hxâŸ© : span â„ {x}) = â€–(âŸ¨x, hxâŸ© : span â„ {x})â€–
    rw [hf]
    change (CoeffSpan nx âŸ¨x, hxâŸ©) * â€–xâ€– = â€–xâ€–
    rw [coeffSpan_self, one_mul]

section LowerSemicontinuous

open WeakDual ContinuousLinearMap in
theorem lowerSemicontinuous_norm :
    LowerSemicontinuous (fun f : WeakDual â„ E â†¦ â€–toNormedDual fâ€–) := by
  intro f r hrf
  obtain hr | hr := lt_or_le r 0
  Â· exact Eventually.of_forall fun _ â†¦ lt_of_lt_of_le hr (norm_nonneg _)
  Â· obtain âŸ¨x, nx, hxâŸ© := exists_lt_apply_of_lt_opNorm f hrf
    wlog hfx : 0 â‰¤ f x
    Â· apply this f r hrf hr (-x)
      Â· rwa [norm_neg]
      Â· rwa [map_neg, norm_neg]
      Â· rw [map_neg]
        linarith
    Â· let U : Set (WeakDual â„ E) := (fun (f : WeakDual â„ E) â†¦ f x) â»Â¹' Ioi r
      have : U âˆˆ ğ“ f := by
        apply (isOpen_Ioi.preimage (eval_continuous x)).mem_nhds
        rw [norm_of_nonneg hfx] at hx
        simpa
      apply eventually_of_mem this
      intro g hg
      rw [â† not_le, (opNorm_le_iff hr).not]
      push_neg
      use x
      apply lt_of_le_of_lt (b := r)
      Â· nth_rw 2 [â† mul_one r]
        exact mul_le_mul_of_nonneg_left nx.le hr
      Â· exact lt_of_lt_of_le hg (le_abs_self _)

end LowerSemicontinuous

theorem le_opNorm_of {f : E â†’L[â„] F} {x : E} {C : â„} (hx : x â‰  0) (h : C * â€–xâ€– â‰¤ â€–f xâ€–) :
    C â‰¤ â€–fâ€– := by
  rw [â† _root_.mul_le_mul_right (norm_pos_iff.2 hx)]
  exact h.trans (ContinuousLinearMap.le_opNorm _ _)

theorem le_opNorm_of' {f : E â†’L[â„] F} {x : E} {C : â„} (hx : â€–xâ€– = 1) (h : C â‰¤ â€–f xâ€–) :
    C â‰¤ â€–fâ€– := by
  apply le_opNorm_of (norm_ne_zero_iff.1 (hx â–¸ (by norm_num : (1 : â„) â‰  0)))
  rwa [hx, mul_one]

theorem le_opNorm_of'' {f : E â†’L[â„] F} {x : E} {C : â„} (hx : x â‰  0) (nx : â€–xâ€– â‰¤ 1) (h : C â‰¤ â€–f xâ€–) :
    C â‰¤ â€–fâ€– := by
  obtain hC | hC := le_total C 0
  Â· exact hC.trans (norm_nonneg _)
  Â· exact le_opNorm_of hx (le_trans (mul_le_of_le_one_right hC nx) h)
