/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Winston Yin
-/
import Mathlib.Analysis.SpecialFunctions.Integrals.Basic
import Mathlib.Topology.Algebra.Order.Floor
import Mathlib.Topology.MetricSpace.Contracting

/-!
# Picard-Lindel√∂f (Cauchy-Lipschitz) Theorem

We prove the (local) existence of integral curves and flows to time-dependent vector fields.

Let `f : ‚Ñù ‚Üí E ‚Üí E` be a time-dependent (local) vector field on a Banach space, and let `t‚ÇÄ : ‚Ñù`
and `x‚ÇÄ : E`. If `f` is Lipschitz continuous in `x` within a closed ball around `x‚ÇÄ` of radius
`a ‚â• 0` at every `t` and continuous in `t` at every `x`, then there exists a (local) solution
`Œ± : ‚Ñù ‚Üí E` to the initial value problem `Œ± t‚ÇÄ = x‚ÇÄ` and `deriv Œ± t = f t (Œ± t)` for all
`t ‚àà Icc tmin tmax`, where `L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a`.

We actually prove a more general version of this theorem for the existence of local flows. If there
is some `r ‚â• 0` such that `L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a - r`, then for every
`x ‚àà closedBall x‚ÇÄ r`, there exists a (local) solution `Œ± x` with the initial condition `Œ± t‚ÇÄ = x`.
In other words, there exists a local flow `Œ± : E ‚Üí ‚Ñù ‚Üí E` defined on `closedBall x‚ÇÄ r` and
`Icc tmin tmax`.

The proof relies on demonstrating the existence of a solution `Œ±` to the following integral
equation:
$$\alpha(t) = x_0 + \int_{t_0}^t f(\tau, \alpha(\tau))\,\mathrm{d}\tau.$$
This is done via the contraction mapping theorem, applied to the space of Lipschitz continuous
functions from a closed interval to a Banach space. The needed contraction map is constructed by
repeated applications of the right-hand side of this equation.

## Main definitions and results

* `picard f t‚ÇÄ x‚ÇÄ Œ± t`: the Picard iteration, applied to the curve `Œ±`
* `IsPicardLindelof`: the structure holding the assumptions of the Picard-Lindel√∂f theorem
* `IsPicardLindelof.exists_eq_forall_mem_Icc_hasDerivWithinAt`: the existence theorem for local
  solutions to time-dependent ODEs
* `IsPicardLindelof.exists_forall_mem_closedBall_eq_forall_mem_Icc_hasDerivWithinAt`: the existence
  theorem for local flows to time-dependent vector fields
* `IsPicardLindelof.exists_forall_mem_closedBall_eq_hasDerivWithinAt_lipschitzOnWith`: there exists
  a local flow to time-dependent vector fields, and it is Lipschitz-continuous with respect to the
  starting point.

## Implementation notes

* The structure `FunSpace` and theorems within this namespace are implementation details of the
  proof of the Picard-Lindel√∂f theorem and are not intended to be used outside of this file.
* Some sources, such as Lang, define `FunSpace` as the space of continuous functions from a closed
  interval to a closed ball. We instead define `FunSpace` here as the space of Lipschitz continuous
  functions from a closed interval. This slightly stronger condition allows us to postpone the usage
  of the completeness condition on the space `E` until the application of the contraction mapping
  theorem.
* We have chosen to formalise many of the real constants as `‚Ñù‚â•0`, so that the non-negativity of
  certain quantities constructed from them can be shown more easily. When subtraction is involved,
  especially note whether it is the usual subtraction between two reals or the truncated subtraction
  between two non-negative reals.
* In this file, We only prove the existence of a solution. For uniqueness, see `ODE_solution_unique`
  and related theorems in `Mathlib/Analysis/ODE/Gronwall.lean`.

## Tags

differential equation, dynamical system, initial value problem, Picard-Lindel√∂f theorem,
Cauchy-Lipschitz theorem

-/

open Function intervalIntegral MeasureTheory Metric Set
open scoped Nat NNReal Topology

/-! ## Assumptions of the Picard-Lindel√∂f theorem-/

/-- Prop structure holding the assumptions of the Picard-Lindel√∂f theorem.
`IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K`, where `t‚ÇÄ ‚àà Icc tmin tmax`, means that the time-dependent vector
field `f` satisfies the conditions to admit an integral curve `Œ± : ‚Ñù ‚Üí E` to `f` defined on
`Icc tmin tmax` with the initial condition `Œ± t‚ÇÄ = x`, where `‚Äñx - x‚ÇÄ‚Äñ ‚â§ r`. Note that the initial
point `x` is allowed to differ from the point `x‚ÇÄ` about which the conditions on `f` are stated. -/
structure IsPicardLindelof {E : Type*} [NormedAddCommGroup E]
    (f : ‚Ñù ‚Üí E ‚Üí E) {tmin tmax : ‚Ñù} (t‚ÇÄ : Icc tmin tmax) (x‚ÇÄ : E) (a r L K : ‚Ñù‚â•0) : Prop where
  /-- The vector field at any time is Lipschitz with constant `K` within a closed ball. -/
  lipschitzOnWith : ‚àÄ t ‚àà Icc tmin tmax, LipschitzOnWith K (f t) (closedBall x‚ÇÄ a)
  /-- The vector field is continuous in time within a closed ball. -/
  continuousOn : ‚àÄ x ‚àà closedBall x‚ÇÄ a, ContinuousOn (f ¬∑ x) (Icc tmin tmax)
  /-- `L` is an upper bound of the norm of the vector field. -/
  norm_le : ‚àÄ t ‚àà Icc tmin tmax, ‚àÄ x ‚àà closedBall x‚ÇÄ a, ‚Äñf t x‚Äñ ‚â§ L
  /-- The time interval of validity -/
  mul_max_le : L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a - r

namespace ODE

/-! ## Integral equation

For any time-dependent vector field `f : ‚Ñù ‚Üí E ‚Üí E`, we define an integral equation that is
equivalent to the initial value problem defined by `f`.
-/

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {Œ± : ‚Ñù ‚Üí E} {s : Set ‚Ñù} {u : Set E} {t‚ÇÄ tmin tmax : ‚Ñù}

/-- The Picard iteration. It will be shown that if `Œ± : ‚Ñù ‚Üí E` and `picard f t‚ÇÄ x‚ÇÄ Œ±` agree on an
interval containing `t‚ÇÄ`, then `Œ±` is a solution to `f` with `Œ± t‚ÇÄ = x‚ÇÄ` on this interval. -/
noncomputable def picard (f : ‚Ñù ‚Üí E ‚Üí E) (t‚ÇÄ : ‚Ñù) (x‚ÇÄ : E) (Œ± : ‚Ñù ‚Üí E) : ‚Ñù ‚Üí E :=
  fun t ‚Ü¶ x‚ÇÄ + ‚à´ œÑ in t‚ÇÄ..t, f œÑ (Œ± œÑ)

@[simp]
lemma picard_apply {x‚ÇÄ : E} {t : ‚Ñù} : picard f t‚ÇÄ x‚ÇÄ Œ± t = x‚ÇÄ + ‚à´ œÑ in t‚ÇÄ..t, f œÑ (Œ± œÑ) := rfl

lemma picard_apply‚ÇÄ {x‚ÇÄ : E} : picard f t‚ÇÄ x‚ÇÄ Œ± t‚ÇÄ = x‚ÇÄ := by simp

/-- Given a $C^n$ time-dependent vector field `f` and a $C^n$ curve `Œ±`, the composition `f t (Œ± t)`
is $C^n$ in `t`. -/
lemma contDiffOn_comp {n : WithTop ‚Ñï‚àû}
    (hf : ContDiffOn ‚Ñù n (uncurry f) (s √óÀ¢ u))
    (hŒ± : ContDiffOn ‚Ñù n Œ± s) (hmem : ‚àÄ t ‚àà s, Œ± t ‚àà u) :
    ContDiffOn ‚Ñù n (fun t ‚Ü¶ f t (Œ± t)) s := by
  have : (fun t ‚Ü¶ f t (Œ± t)) = (uncurry f) ‚àò fun t ‚Ü¶ (t, Œ± t) := rfl
  rw [this]
  apply hf.comp (by fun_prop)
  intro _ ht
  rw [mem_prod]
  exact ‚ü®ht, hmem _ ht‚ü©

/-- Given a continuous time-dependent vector field `f` and a continuous curve `Œ±`, the composition
`f t (Œ± t)` is continuous in `t`. -/
lemma continuousOn_comp
    (hf : ContinuousOn (uncurry f) (s √óÀ¢ u)) (hŒ± : ContinuousOn Œ± s) (hmem : MapsTo Œ± s u) :
    ContinuousOn (fun t ‚Ü¶ f t (Œ± t)) s :=
  contDiffOn_zero.mp <| (contDiffOn_comp (contDiffOn_zero.mpr hf) (contDiffOn_zero.mpr hŒ±) hmem)

end

/-! ## Space of Lipschitz functions on a closed interval

We define the space of Lipschitz continuous functions from a closed interval. This will be shown to
be a complete metric space on which `picard` is a contracting map, leading to a fixed point that
will serve as the solution to the ODE. The domain is a closed interval in order to easily inherit
the sup metric from continuous maps on compact spaces. We cannot use functions `‚Ñù ‚Üí E` with junk
values outside the domain, as the supremum within a closed interval will only be a pseudo-metric,
and the contracting map will fail to have a fixed point. In order to accommodate flows, we do not
require a specific initial condition. Rather, `FunSpace` contains curves whose initial condition is
within a closed ball.
-/

/-- The space of `L`-Lipschitz functions `Œ± : Icc tmin tmax ‚Üí E` -/
structure FunSpace {E : Type*} [NormedAddCommGroup E]
    {tmin tmax : ‚Ñù} (t‚ÇÄ : Icc tmin tmax) (x‚ÇÄ : E) (r L : ‚Ñù‚â•0) where
  /-- The domain is `Icc tmin tmax`. -/
  toFun : Icc tmin tmax ‚Üí E
  lipschitzWith : LipschitzWith L toFun
  mem_closedBall‚ÇÄ : toFun t‚ÇÄ ‚àà closedBall x‚ÇÄ r

namespace FunSpace

variable {E : Type*} [NormedAddCommGroup E]

section

variable {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ : E} {a r L : ‚Ñù‚â•0}

instance : CoeFun (FunSpace t‚ÇÄ x‚ÇÄ r L) fun _ ‚Ü¶ Icc tmin tmax ‚Üí E := ‚ü®fun Œ± ‚Ü¶ Œ±.toFun‚ü©

/-- `FunSpace t‚ÇÄ x‚ÇÄ r L` contains the constant map at `x‚ÇÄ`. -/
instance : Inhabited (FunSpace t‚ÇÄ x‚ÇÄ r L) :=
  ‚ü®fun _ ‚Ü¶ x‚ÇÄ, (LipschitzWith.const _).weaken (zero_le _), mem_closedBall_self r.2‚ü©

protected lemma continuous (Œ± : FunSpace t‚ÇÄ x‚ÇÄ L r) : Continuous Œ± := Œ±.lipschitzWith.continuous

/-- The embedding of `FunSpace` into the space of continuous maps -/
def toContinuousMap : FunSpace t‚ÇÄ x‚ÇÄ r L ‚Ü™ C(Icc tmin tmax, E) :=
  ‚ü®fun Œ± ‚Ü¶ ‚ü®Œ±, Œ±.continuous‚ü©, fun Œ± Œ≤ h ‚Ü¶ by cases Œ±; cases Œ≤; simpa using h‚ü©

@[simp]
lemma toContinuousMap_apply_eq_apply (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) (t : Icc tmin tmax) :
    Œ±.toContinuousMap t = Œ± t := rfl

/-- The metric between two curves `Œ±` and `Œ≤` is the supremum of the metric between `Œ± t` and `Œ≤ t`
over all `t` in the domain. This is finite when the domain is compact, such as a closed
interval in our case. -/
noncomputable instance : MetricSpace (FunSpace t‚ÇÄ x‚ÇÄ r L) :=
  MetricSpace.induced toContinuousMap toContinuousMap.injective inferInstance

lemma isUniformInducing_toContinuousMap :
    IsUniformInducing fun Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L ‚Ü¶ Œ±.toContinuousMap := ‚ü®rfl‚ü©

lemma range_toContinuousMap :
    range (fun Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L ‚Ü¶ Œ±.toContinuousMap) =
      { Œ± : C(Icc tmin tmax, E) | LipschitzWith L Œ± ‚àß Œ± t‚ÇÄ ‚àà closedBall x‚ÇÄ r } := by
  ext Œ±
  constructor
  ¬∑ rintro ‚ü®‚ü®Œ±, hŒ±1, hŒ±2‚ü©, rfl‚ü©
    exact ‚ü®hŒ±1, hŒ±2‚ü©
  ¬∑ rintro ‚ü®hŒ±1, hŒ±2‚ü©
    exact ‚ü®‚ü®Œ±, hŒ±1, hŒ±2‚ü©, rfl‚ü©

/-- We show that `FunSpace` is complete in order to apply the contraction mapping theorem. -/
instance [CompleteSpace E] : CompleteSpace (FunSpace t‚ÇÄ x‚ÇÄ r L) := by
  rw [completeSpace_iff_isComplete_range isUniformInducing_toContinuousMap]
  apply IsClosed.isComplete
  rw [range_toContinuousMap, setOf_and]
  apply isClosed_setOf_lipschitzWith L |>.preimage continuous_coeFun |>.inter
  simp_rw [mem_closedBall_iff_norm]
  exact isClosed_le (by fun_prop) continuous_const

/-- Extend the domain of `Œ±` from `Icc tmin tmax` to `‚Ñù` such that `Œ± t = Œ± tmin` for all `t ‚â§ tmin`
and `Œ± t = Œ± tmax` for all `t ‚â• tmax`. -/
noncomputable def compProj (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) (t : ‚Ñù) : E :=
  Œ± <| projIcc tmin tmax (le_trans t‚ÇÄ.2.1 t‚ÇÄ.2.2) t

@[simp]
lemma compProj_apply {Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L} {t : ‚Ñù} :
    Œ±.compProj t = Œ± (projIcc tmin tmax (le_trans t‚ÇÄ.2.1 t‚ÇÄ.2.2) t) := rfl

lemma compProj_val {Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L} {t : Icc tmin tmax} :
    Œ±.compProj t = Œ± t := by simp only [compProj_apply, projIcc_val]

lemma compProj_of_mem {Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L} {t : ‚Ñù} (ht : t ‚àà Icc tmin tmax) :
    Œ±.compProj t = Œ± ‚ü®t, ht‚ü© := by rw [compProj_apply, projIcc_of_mem]

@[continuity, fun_prop]
lemma continuous_compProj (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) : Continuous Œ±.compProj :=
  Œ±.continuous.comp continuous_projIcc

/-- The image of a function in `FunSpace` is contained within a closed ball. -/
protected lemma mem_closedBall
    {Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L} (h : L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a - r) {t : Icc tmin tmax} :
    Œ± t ‚àà closedBall x‚ÇÄ a := by
  rw [mem_closedBall, dist_eq_norm]
  calc
    ‚ÄñŒ± t - x‚ÇÄ‚Äñ ‚â§ ‚ÄñŒ± t - Œ± t‚ÇÄ‚Äñ + ‚ÄñŒ± t‚ÇÄ - x‚ÇÄ‚Äñ := norm_sub_le_norm_sub_add_norm_sub ..
    _ ‚â§ L * |t.1 - t‚ÇÄ.1| + r := by
      apply add_le_add _ <| mem_closedBall_iff_norm.mp Œ±.mem_closedBall‚ÇÄ
      rw [‚Üê dist_eq_norm]
      exact Œ±.lipschitzWith.dist_le_mul t t‚ÇÄ
    _ ‚â§ L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) + r := by
      gcongr
      exact abs_sub_le_max_sub t.2.1 t.2.2 _
    _ ‚â§ a - r + r := add_le_add_right h _
    _ = a := sub_add_cancel _ _

lemma compProj_mem_closedBall
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) (h : L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a - r) {t : ‚Ñù} :
    Œ±.compProj t ‚àà closedBall x‚ÇÄ a := by
  rw [compProj_apply]
  exact Œ±.mem_closedBall h

end

/-! ## Contracting map on the space of Lipschitz functions -/

section

variable [NormedSpace ‚Ñù E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ x y : E} {a r L K : ‚Ñù‚â•0}

/-- The integrand in `next` is continuous. -/
lemma continuousOn_comp_compProj (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) :
    ContinuousOn (fun t' ‚Ü¶ f t' (Œ±.compProj t')) (Icc tmin tmax) :=
  continuousOn_comp
    (continuousOn_prod_of_continuousOn_lipschitzOnWith' (uncurry f) K hf.lipschitzOnWith
      hf.continuousOn)
    Œ±.continuous_compProj.continuousOn
    fun _ _ ‚Ü¶ Œ±.mem_closedBall hf.mul_max_le

/-- The integrand in `next` is integrable. -/
lemma intervalIntegrable_comp_compProj (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) (t : Icc tmin tmax) :
    IntervalIntegrable (fun t' ‚Ü¶ f t' (Œ±.compProj t')) volume t‚ÇÄ t := by
  apply ContinuousOn.intervalIntegrable
  apply Œ±.continuousOn_comp_compProj hf |>.mono
  exact uIcc_subset_Icc t‚ÇÄ.2 t.2

/-- The map on `FunSpace` defined by `picard`, some `n`-th iterate of which will be a contracting
map -/
noncomputable def next (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r)
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) : FunSpace t‚ÇÄ x‚ÇÄ r L where
  toFun t := picard f t‚ÇÄ x Œ±.compProj t
  lipschitzWith := LipschitzWith.of_dist_le_mul fun t‚ÇÅ t‚ÇÇ ‚Ü¶ by
    rw [dist_eq_norm, picard_apply, picard_apply, add_sub_add_left_eq_sub,
      integral_interval_sub_left (intervalIntegrable_comp_compProj hf _ t‚ÇÅ)
        (intervalIntegrable_comp_compProj hf _ t‚ÇÇ), Subtype.dist_eq, Real.dist_eq]
    apply intervalIntegral.norm_integral_le_of_norm_le_const
    intro t ht
    -- Can `grind` do this in the future?
    have ht : t ‚àà Icc tmin tmax := subset_trans uIoc_subset_uIcc (uIcc_subset_Icc t‚ÇÇ.2 t‚ÇÅ.2) ht
    exact hf.norm_le _ ht _ <| Œ±.mem_closedBall hf.mul_max_le
  mem_closedBall‚ÇÄ := by simp [hx]

@[simp]
lemma next_apply (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r)
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) {t : Icc tmin tmax} :
    next hf hx Œ± t = picard f t‚ÇÄ x Œ±.compProj t := rfl

lemma next_apply‚ÇÄ (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r)
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) : next hf hx Œ± t‚ÇÄ = x := by simp

/-- A key step in the inductive case of `dist_iterate_next_apply_le` -/
lemma dist_comp_iterate_next_le (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (hx : x ‚àà closedBall x‚ÇÄ r) (n : ‚Ñï) (t : Icc tmin tmax)
    {Œ± Œ≤ : FunSpace t‚ÇÄ x‚ÇÄ r L}
    (h : dist ((next hf hx)^[n] Œ± t) ((next hf hx)^[n] Œ≤ t) ‚â§
      (K * |t - t‚ÇÄ.1|) ^ n / n ! * dist Œ± Œ≤) :
    dist (f t ((next hf hx)^[n] Œ± t)) (f t ((next hf hx)^[n] Œ≤ t)) ‚â§
      K ^ (n + 1) * |t - t‚ÇÄ.1| ^ n / n ! * dist Œ± Œ≤ :=
  calc
    _ ‚â§ K * dist ((next hf hx)^[n] Œ± t) ((next hf hx)^[n] Œ≤ t) :=
      hf.lipschitzOnWith t.1 t.2 |>.dist_le_mul
        _ (FunSpace.mem_closedBall hf.mul_max_le) _ (FunSpace.mem_closedBall hf.mul_max_le)
    _ ‚â§ K ^ (n + 1) * |t - t‚ÇÄ.1| ^ n / n ! * dist Œ± Œ≤ := by
      rw [pow_succ', mul_assoc, mul_div_assoc, mul_assoc]
      gcongr
      rwa [‚Üê mul_pow]

/-- A time-dependent bound on the distance between the `n`-th iterates of `next` on two curves -/
lemma dist_iterate_next_apply_le (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (hx : x ‚àà closedBall x‚ÇÄ r) (Œ± Œ≤ : FunSpace t‚ÇÄ x‚ÇÄ r L) (n : ‚Ñï) (t : Icc tmin tmax) :
    dist ((next hf hx)^[n] Œ± t) ((next hf hx)^[n] Œ≤ t) ‚â§
      (K * |t.1 - t‚ÇÄ.1|) ^ n / n ! * dist Œ± Œ≤ := by
  induction n generalizing t with
  | zero => simpa using
      ContinuousMap.dist_apply_le_dist (f := toContinuousMap Œ±) (g := toContinuousMap Œ≤) _
  | succ n hn =>
    rw [iterate_succ_apply', iterate_succ_apply', dist_eq_norm, next_apply,
      next_apply, picard_apply, picard_apply, add_sub_add_left_eq_sub,
      ‚Üê intervalIntegral.integral_sub (intervalIntegrable_comp_compProj hf _ t)
        (intervalIntegrable_comp_compProj hf _ t)]
    calc
      _ ‚â§ ‚à´ œÑ in uIoc t‚ÇÄ.1 t.1, K ^ (n + 1) * |œÑ - t‚ÇÄ| ^ n / n ! * dist Œ± Œ≤ := by
        rw [intervalIntegral.norm_intervalIntegral_eq]
        apply MeasureTheory.norm_integral_le_of_norm_le (Continuous.integrableOn_uIoc (by fun_prop))
        apply ae_restrict_mem measurableSet_Ioc |>.mono
        intro t' ht'
        -- Can `grind` do this in the future?
        have ht' : t' ‚àà Icc tmin tmax :=
          subset_trans uIoc_subset_uIcc (uIcc_subset_Icc t‚ÇÄ.2 t.2) ht'
        rw [‚Üê dist_eq_norm, compProj_of_mem, compProj_of_mem]
        exact dist_comp_iterate_next_le hf hx _ ‚ü®t', ht'‚ü© (hn _)
      _ ‚â§ (K * |t.1 - t‚ÇÄ.1|) ^ (n + 1) / (n + 1) ! * dist Œ± Œ≤ := by
        apply le_of_abs_le
        -- critical: `integral_pow_abs_sub_uIoc`
        rw [‚Üê intervalIntegral.abs_intervalIntegral_eq, intervalIntegral.integral_mul_const,
          intervalIntegral.integral_div, intervalIntegral.integral_const_mul, abs_mul, abs_div,
          abs_mul, intervalIntegral.abs_intervalIntegral_eq, integral_pow_abs_sub_uIoc, abs_div,
          abs_pow, abs_pow, abs_dist, NNReal.abs_eq, abs_abs, mul_div, div_div, ‚Üê abs_mul,
          ‚Üê Nat.cast_succ, ‚Üê Nat.cast_mul, ‚Üê Nat.factorial_succ, Nat.abs_cast, ‚Üê mul_pow]

/-- The `n`-th iterate of `next` is Lipschitz continuous with respect to `FunSpace`, with constant
$(K \max(t_{\mathrm{max}}, t_{\mathrm{min}})^n / n!$. -/
lemma dist_iterate_next_iterate_next_le (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (hx : x ‚àà closedBall x‚ÇÄ r) (Œ± Œ≤ : FunSpace t‚ÇÄ x‚ÇÄ r L) (n : ‚Ñï) :
    dist ((next hf hx)^[n] Œ±) ((next hf hx)^[n] Œ≤) ‚â§
      (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin)) ^ n / n ! * dist Œ± Œ≤ := by
  rw [‚Üê MetricSpace.isometry_induced FunSpace.toContinuousMap FunSpace.toContinuousMap.injective
    |>.dist_eq, ContinuousMap.dist_le]
  ¬∑ intro t
    apply le_trans <| dist_iterate_next_apply_le hf hx Œ± Œ≤ n t
    gcongr
    exact abs_sub_le_max_sub t.2.1 t.2.2 _
  ¬∑ have : 0 ‚â§ max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) := le_max_of_le_left <| sub_nonneg_of_le t‚ÇÄ.2.2
    positivity

/-- Some `n`-th iterate of `next` is a contracting map, and its associated Lipschitz constant is
independent of the initial point. -/
lemma exists_contractingWith_iterate_next (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ (n : ‚Ñï) (C : ‚Ñù‚â•0), ‚àÄ (x : E) (hx : x ‚àà closedBall x‚ÇÄ r),
      ContractingWith C (next hf hx)^[n] := by
  obtain ‚ü®n, hn‚ü© := FloorSemiring.tendsto_pow_div_factorial_atTop (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin))
    |>.eventually (gt_mem_nhds zero_lt_one) |>.exists
  have : (0 : ‚Ñù) ‚â§ (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin)) ^ n / n ! := by
    have : 0 ‚â§ max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) := le_max_of_le_left <| sub_nonneg_of_le t‚ÇÄ.2.2
    positivity
  refine ‚ü®n, ‚ü®_, this‚ü©, fun x hx ‚Ü¶ ?_‚ü©
  exact ‚ü®hn, LipschitzWith.of_dist_le_mul fun Œ± Œ≤ ‚Ü¶ dist_iterate_next_iterate_next_le hf hx Œ± Œ≤ n‚ü©

/-- The map `next` has a fixed point in the space of curves. This will be used to construct a
solution `Œ± : ‚Ñù ‚Üí E` to the ODE. -/
lemma exists_isFixedPt_next [CompleteSpace E] (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (hx : x ‚àà closedBall x‚ÇÄ r) :
    ‚àÉ Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L, IsFixedPt (next hf hx) Œ± :=
  let ‚ü®_, _, h‚ü© := exists_contractingWith_iterate_next hf
  ‚ü®_, h x hx |>.isFixedPt_fixedPoint_iterate‚ü©

/-! ## Lipschitz continuity of the solution with respect to the initial condition

The proof relies on the fact that the repeated application of `next` to any curve `Œ±` converges to
the fixed point of `next`, so it suffices to bound the distance between `Œ±` and `next^[n] Œ±`. Since
there is some `m : ‚Ñï` such that `next^[m]` is a contracting map, it further suffices to bound the
distance between `Œ±` and `next^[m]^[n] Œ±`.
-/

/-- A key step in the base case of `exists_forall_closedBall_funSpace_dist_le_mul` -/
lemma dist_next_next (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r)
    (hy : y ‚àà closedBall x‚ÇÄ r) (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) :
    dist (next hf hx Œ±) (next hf hy Œ±) = dist x y := by
  have : Nonempty (Icc tmin tmax) := ‚ü®t‚ÇÄ‚ü© -- needed for `ciSup_const`
  rw [‚Üê MetricSpace.isometry_induced FunSpace.toContinuousMap FunSpace.toContinuousMap.injective
    |>.dist_eq, dist_eq_norm, ContinuousMap.norm_eq_iSup_norm]
  simp [add_sub_add_right_eq_sub, dist_eq_norm]

lemma dist_iterate_next_le (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r)
    (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) (n : ‚Ñï) :
    dist Œ± ((next hf hx)^[n] Œ±) ‚â§
      (‚àë i ‚àà Finset.range n, (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin)) ^ i / i !)
        * dist Œ± (next hf hx Œ±) := by
  nth_rw 1 [‚Üê iterate_zero_apply (next hf hx) Œ±]
  rw [Finset.sum_mul]
  apply dist_le_range_sum_of_dist_le (f := fun i ‚Ü¶ (next hf hx)^[i] Œ±)
  intro i hi
  rw [iterate_succ_apply]
  exact dist_iterate_next_iterate_next_le hf hx _ _ i

lemma dist_iterate_iterate_next_le_of_lipschitzWith (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K)
    (hx : x ‚àà closedBall x‚ÇÄ r) (Œ± : FunSpace t‚ÇÄ x‚ÇÄ r L) {m : ‚Ñï} {C : ‚Ñù‚â•0}
    (hm : LipschitzWith C (next hf hx)^[m]) (n : ‚Ñï) :
    dist Œ± ((next hf hx)^[m]^[n] Œ±) ‚â§
      (‚àë i ‚àà Finset.range m, (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin)) ^ i / i !) *
        (‚àë i ‚àà Finset.range n, (C : ‚Ñù) ^ i) * dist Œ± (next hf hx Œ±) := by
  nth_rw 1 [‚Üê iterate_zero_apply (next hf hx) Œ±]
  rw [Finset.mul_sum, Finset.sum_mul]
  apply dist_le_range_sum_of_dist_le (f := fun i ‚Ü¶ (next hf hx)^[m]^[i] Œ±)
  intro i hi
  rw [iterate_succ_apply]
  apply le_trans <| hm.dist_iterate_succ_le_geometric Œ± i
  rw [mul_assoc, mul_comm ((C : ‚Ñù) ^ i), ‚Üê mul_assoc]
  gcongr
  exact dist_iterate_next_le hf hx Œ± m

/-- The pointwise distance between any two integral curves `Œ±` and `Œ≤` over their domains is bounded
by a constant `L'` times the distance between their respective initial points. This is the result of
taking the limit of `dist_iterate_iterate_next_le_of_lipschitzWith` as `n ‚Üí ‚àû`. This implies that
the local solution of a vector field is Lipschitz continuous in the initial condition. -/
lemma exists_forall_closedBall_funSpace_dist_le_mul [CompleteSpace E]
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ L' : ‚Ñù‚â•0, ‚àÄ (x y : E) (hx : x ‚àà closedBall x‚ÇÄ r) (hy : y ‚àà closedBall x‚ÇÄ r)
      (Œ± Œ≤ : FunSpace t‚ÇÄ x‚ÇÄ r L) (_ : IsFixedPt (next hf hx) Œ±) (_ : IsFixedPt (next hf hy) Œ≤),
      dist Œ± Œ≤ ‚â§ L' * dist x y := by
  obtain ‚ü®m, C, h‚ü© := exists_contractingWith_iterate_next hf
  let L' := (‚àë i ‚àà Finset.range m, (K * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin)) ^ i / i !) * (1 - C)‚Åª¬π
  have hL' : 0 ‚â§ L' := by
    have : 0 ‚â§ max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) := le_max_of_le_left <| sub_nonneg_of_le t‚ÇÄ.2.2
    positivity
  refine ‚ü®‚ü®L', hL'‚ü©, fun x y hx hy Œ± Œ≤ hŒ± hŒ≤ ‚Ü¶ ?_‚ü©
  rw [NNReal.coe_mk]
  apply le_of_tendsto_of_tendsto' (b := Filter.atTop) _ _ <|
    dist_iterate_iterate_next_le_of_lipschitzWith hf hy Œ± (h y hy).2
  ¬∑ apply Filter.Tendsto.comp (y := ùìù Œ≤) (tendsto_const_nhds.dist Filter.tendsto_id)
    rw [h y hy |>.fixedPoint_unique (hŒ≤.iterate m)]
    exact h y hy |>.tendsto_iterate_fixedPoint Œ±
  ¬∑ nth_rw 1 [‚Üê hŒ±, dist_next_next]
    apply Filter.Tendsto.mul_const
    apply Filter.Tendsto.const_mul
    convert hasSum_geometric_of_lt_one C.2 (h y hy).1 |>.tendsto_sum_nat
    simp [NNReal.coe_sub <| le_of_lt (h y hy).1, NNReal.coe_one]

end

end FunSpace

/-! ## Properties of the integral equation -/

section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {Œ± : ‚Ñù ‚Üí E} {s : Set ‚Ñù} {u : Set E} {t‚ÇÄ tmin tmax : ‚Ñù}

-- TODO: generalise to open sets and `Ici` and `Iic`
/-- If the time-dependent vector field `f` and the curve `Œ±` are continuous, then `f t (Œ± t)` is the
derivative of `picard f t‚ÇÄ x‚ÇÄ Œ±`. -/
lemma hasDerivWithinAt_picard_Icc
    (ht‚ÇÄ : t‚ÇÄ ‚àà Icc tmin tmax)
    (hf : ContinuousOn (uncurry f) ((Icc tmin tmax) √óÀ¢ u))
    (hŒ± : ContinuousOn Œ± (Icc tmin tmax))
    (hmem : ‚àÄ t ‚àà Icc tmin tmax, Œ± t ‚àà u) (x‚ÇÄ : E)
    {t : ‚Ñù} (ht : t ‚àà Icc tmin tmax) :
    HasDerivWithinAt (picard f t‚ÇÄ x‚ÇÄ Œ±) (f t (Œ± t)) (Icc tmin tmax) t := by
  apply HasDerivWithinAt.const_add
  have : Fact (t ‚àà Icc tmin tmax) := ‚ü®ht‚ü© -- needed to synthesise `FTCFilter` for `Icc`
  apply intervalIntegral.integral_hasDerivWithinAt_right _ -- need `CompleteSpace E` and `Icc`
    (continuousOn_comp hf hŒ± hmem |>.stronglyMeasurableAtFilter_nhdsWithin measurableSet_Icc t)
    (continuousOn_comp hf hŒ± hmem _ ht)
  apply ContinuousOn.intervalIntegrable
  apply continuousOn_comp hf hŒ± hmem |>.mono
  by_cases h : t < t‚ÇÄ
  ¬∑ rw [uIcc_of_gt h]
    exact Icc_subset_Icc ht.1 ht‚ÇÄ.2
  ¬∑ rw [uIcc_of_le (not_lt.mp h)]
    exact Icc_subset_Icc ht‚ÇÄ.1 ht.2

/-- Converse of `hasDerivWithinAt_picard_Icc`: if `f` is the derivative along `Œ±`, then `Œ±`
satisfies the integral equation. -/
lemma picard_eq_of_hasDerivAt {t : ‚Ñù}
    (hf : ContinuousOn (uncurry f) ((uIcc t‚ÇÄ t) √óÀ¢ u))
    (hŒ± : ‚àÄ t' ‚àà uIcc t‚ÇÄ t, HasDerivWithinAt Œ± (f t' (Œ± t')) (uIcc t‚ÇÄ t) t')
    (hmap : MapsTo Œ± (uIcc t‚ÇÄ t) u) :
    picard f t‚ÇÄ (Œ± t‚ÇÄ) Œ± t = Œ± t := by
  rw [‚Üê add_sub_cancel (Œ± t‚ÇÄ) (Œ± t), picard_apply,
    integral_eq_sub_of_hasDeriv_right (HasDerivWithinAt.continuousOn hŒ±) _
      (continuousOn_comp hf (HasDerivWithinAt.continuousOn hŒ±) hmap |>.intervalIntegrable)]
  intro t' ht'
  apply HasDerivAt.hasDerivWithinAt
  exact hŒ± t' (Ioo_subset_Icc_self ht') |>.hasDerivAt <| Icc_mem_nhds ht'.1 ht'.2

/-- If the time-dependent vector field `f` is $C^n$ and the curve `Œ±` is continuous, then
`interate f t‚ÇÄ x‚ÇÄ Œ±` is also $C^n$. This version works for `n : ‚Ñï`. -/
lemma contDiffOn_nat_picard_Icc
    (ht‚ÇÄ : t‚ÇÄ ‚àà Icc tmin tmax) {n : ‚Ñï}
    (hf : ContDiffOn ‚Ñù n (uncurry f) ((Icc tmin tmax) √óÀ¢ u))
    (hŒ± : ContinuousOn Œ± (Icc tmin tmax))
    (hmem : ‚àÄ t ‚àà Icc tmin tmax, Œ± t ‚àà u) (x‚ÇÄ : E)
    (heqon : ‚àÄ t ‚àà Icc tmin tmax, Œ± t = picard f t‚ÇÄ x‚ÇÄ Œ± t) :
    ContDiffOn ‚Ñù n (picard f t‚ÇÄ x‚ÇÄ Œ±) (Icc tmin tmax) := by
  by_cases hlt : tmin < tmax
  ¬∑ have (t) (ht : t ‚àà Icc tmin tmax) :=
      hasDerivWithinAt_picard_Icc ht‚ÇÄ hf.continuousOn hŒ± hmem x‚ÇÄ ht
    induction n with
    | zero =>
      simp only [Nat.cast_zero, contDiffOn_zero] at *
      exact HasDerivWithinAt.continuousOn this
    | succ n hn =>
      simp only [Nat.cast_add, Nat.cast_one] at *
      rw [contDiffOn_succ_iff_derivWithin <| uniqueDiffOn_Icc hlt]
      refine ‚ü®fun t ht ‚Ü¶ HasDerivWithinAt.differentiableWithinAt (this t ht), by simp, ?_‚ü©
      apply contDiffOn_comp hf.of_succ (ContDiffOn.congr (hn hf.of_succ) heqon) hmem |>.congr
      intro t ht
      exact HasDerivWithinAt.derivWithin (this t ht) <| (uniqueDiffOn_Icc hlt).uniqueDiffWithinAt ht
  ¬∑ rw [(subsingleton_Icc_of_ge (not_lt.mp hlt)).eq_singleton_of_mem ht‚ÇÄ]
    intro t ht
    rw [eq_of_mem_singleton ht]
    exact contDiffWithinAt_singleton

/-- If the time-dependent vector field `f` is $C^n$ and the curve `Œ±` is continuous, then
`interate f t‚ÇÄ x‚ÇÄ Œ±` is also $C^n$. This version works for `n : ‚Ñï‚àû`.

TODO: Extend to the analytic `n = ‚ä§` case. -/
lemma contDiffOn_enat_picard_Icc
    (ht‚ÇÄ : t‚ÇÄ ‚àà Icc tmin tmax) {n : ‚Ñï‚àû}
    (hf : ContDiffOn ‚Ñù n (uncurry f) ((Icc tmin tmax) √óÀ¢ u))
    (hŒ± : ContinuousOn Œ± (Icc tmin tmax))
    (hmem : ‚àÄ t ‚àà Icc tmin tmax, Œ± t ‚àà u) (x‚ÇÄ : E)
    (heqon : ‚àÄ t ‚àà Icc tmin tmax, Œ± t = picard f t‚ÇÄ x‚ÇÄ Œ± t) :
    ContDiffOn ‚Ñù n (picard f t‚ÇÄ x‚ÇÄ Œ±) (Icc tmin tmax) := by
  induction n with
  | top =>
    rw [contDiffOn_infty] at *
    exact fun k ‚Ü¶ contDiffOn_nat_picard_Icc ht‚ÇÄ (hf k) hŒ± hmem x‚ÇÄ heqon
  | coe n =>
    simp only [WithTop.coe_natCast] at *
    exact contDiffOn_nat_picard_Icc ht‚ÇÄ hf hŒ± hmem x‚ÇÄ heqon

/-- Solutions to ODEs defined by $C^n$ vector fields are also $C^n$. -/
theorem contDiffOn_enat_Icc_of_hasDerivWithinAt {n : ‚Ñï‚àû}
    (hf : ContDiffOn ‚Ñù n (uncurry f) ((Icc tmin tmax) √óÀ¢ u))
    (hŒ± : ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt Œ± (f t (Œ± t)) (Icc tmin tmax) t)
    (hmem : MapsTo Œ± (Icc tmin tmax) u) :
    ContDiffOn ‚Ñù n Œ± (Icc tmin tmax) := by
  by_cases hlt : tmin < tmax
  ¬∑ set t‚ÇÄ := (tmin + tmax) / 2 with h
    have ht‚ÇÄ : t‚ÇÄ ‚àà Icc tmin tmax := ‚ü®by linarith, by linarith‚ü©
    have : ‚àÄ t ‚àà Icc tmin tmax, Œ± t = picard f t‚ÇÄ (Œ± t‚ÇÄ) Œ± t := by
      intro t ht
      have : uIcc t‚ÇÄ t ‚äÜ Icc tmin tmax := uIcc_subset_Icc ht‚ÇÄ ht
      rw [picard_eq_of_hasDerivAt (hf.continuousOn.mono (prod_subset_prod_left this))
        (fun t' ht' ‚Ü¶ hŒ± t' (this ht') |>.mono this) (hmem.mono_left this)]
    exact contDiffOn_enat_picard_Icc ht‚ÇÄ hf (HasDerivWithinAt.continuousOn hŒ±) hmem (Œ± t‚ÇÄ) this
      |>.congr this
  ¬∑ rw [not_lt, le_iff_lt_or_eq] at hlt
    cases hlt with
    | inl h =>
      intro _ ht
      rw [Icc_eq_empty (not_le.mpr h)] at ht
      exfalso
      exact notMem_empty _ ht
    | inr h =>
      rw [h, Icc_self]
      intro _ ht
      rw [eq_of_mem_singleton ht]
      exact contDiffWithinAt_singleton

end

end ODE

namespace IsPicardLindelof

/-! ## Properties of `IsPicardLindelof` -/

section

variable {E : Type*} [NormedAddCommGroup E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ x : E} {a r L K : ‚Ñù‚â•0}

lemma continuousOn_uncurry (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ContinuousOn (uncurry f) ((Icc tmin tmax) √óÀ¢ (closedBall x‚ÇÄ a)) :=
  continuousOn_prod_of_continuousOn_lipschitzOnWith' _ K hf.lipschitzOnWith hf.continuousOn

/-- The special case where the vector field is independent of time -/
lemma of_time_independent
    {f : E ‚Üí E} {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ : E} {a r L K : ‚Ñù‚â•0}
    (hb : ‚àÄ x ‚àà closedBall x‚ÇÄ a, ‚Äñf x‚Äñ ‚â§ L)
    (hl : LipschitzOnWith K f (closedBall x‚ÇÄ a))
    (hm : L * max (tmax - t‚ÇÄ) (t‚ÇÄ - tmin) ‚â§ a - r) :
    (IsPicardLindelof (fun _ ‚Ü¶ f) t‚ÇÄ x‚ÇÄ a r L K) where
  lipschitzOnWith := fun _ _ ‚Ü¶ hl
  continuousOn := fun _ _ ‚Ü¶ continuousOn_const
  norm_le := fun _ _ ‚Ü¶ hb
  mul_max_le := hm

/-- A time-independent, continuously differentiable ODE satisfies the hypotheses of the
Picard-Lindel√∂f theorem. -/
lemma of_contDiffAt_one [NormedSpace ‚Ñù E]
    {f : E ‚Üí E} {x‚ÇÄ : E} (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ (Œµ : ‚Ñù) (hŒµ : 0 < Œµ) (a r L K : ‚Ñù‚â•0) (_ : 0 < r), IsPicardLindelof (fun _ ‚Ü¶ f)
      (tmin := t‚ÇÄ - Œµ) (tmax := t‚ÇÄ + Œµ) ‚ü®t‚ÇÄ, (by simp [le_of_lt hŒµ])‚ü© x‚ÇÄ a r L K := by
  -- Obtain ball of radius `a` within the domain in which f is `K`-lipschitz
  obtain ‚ü®K, s, hs, hl‚ü© := hf.exists_lipschitzOnWith
  obtain ‚ü®a, ha : 0 < a, has‚ü© := Metric.mem_nhds_iff.mp hs
  set L := K * a + ‚Äñf x‚ÇÄ‚Äñ + 1 with hL
  have hL0 : 0 < L := by positivity
  have hb (x : E) (hx : x ‚àà closedBall x‚ÇÄ (a / 2)) : ‚Äñf x‚Äñ ‚â§ L := by
    rw [hL]
    calc
      ‚Äñf x‚Äñ ‚â§ ‚Äñf x - f x‚ÇÄ‚Äñ + ‚Äñf x‚ÇÄ‚Äñ := norm_le_norm_sub_add _ _
      _ ‚â§ K * ‚Äñx - x‚ÇÄ‚Äñ + ‚Äñf x‚ÇÄ‚Äñ := by
        gcongr
        rw [‚Üê dist_eq_norm, ‚Üê dist_eq_norm]
        apply hl.dist_le_mul x _ x‚ÇÄ (mem_of_mem_nhds hs)
        apply subset_trans _ has hx
        exact closedBall_subset_ball <| half_lt_self ha -- this is where we need `a / 2`
      _ ‚â§ K * a + ‚Äñf x‚ÇÄ‚Äñ := by
        gcongr
        rw [‚Üê mem_closedBall_iff_norm]
        exact closedBall_subset_closedBall (half_le_self (le_of_lt ha)) hx
      _ ‚â§ L := le_add_of_nonneg_right zero_le_one
  let Œµ := a / L / 2 / 2
  have hŒµ0 : 0 < Œµ := by positivity
  refine ‚ü®Œµ, hŒµ0,
    ‚ü®a / 2, le_of_lt <| half_pos ha‚ü©, ‚ü®a / 2, le_of_lt <| half_pos ha‚ü© / 2,
    ‚ü®L, le_of_lt hL0‚ü©, K, half_pos <| half_pos ha, ?_‚ü©
  apply of_time_independent hb <|
    hl.mono <| subset_trans (closedBall_subset_ball (half_lt_self ha)) has
  rw [NNReal.coe_mk, add_sub_cancel_left, sub_sub_cancel, max_self, NNReal.coe_div,
    NNReal.coe_two, NNReal.coe_mk, mul_comm, ‚Üê le_div_iff‚ÇÄ hL0, sub_half, div_right_comm (a / 2),
    div_right_comm a]

end

/-! ## Existence of solutions to ODEs -/

open ODE

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {f : ‚Ñù ‚Üí E ‚Üí E} {tmin tmax : ‚Ñù} {t‚ÇÄ : Icc tmin tmax} {x‚ÇÄ x : E} {a r L K : ‚Ñù‚â•0}

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, integral form. This version shows the existence
of a local solution whose initial point `x` may be different from the centre `x‚ÇÄ` of the closed
ball within which the properties of the vector field hold. -/
theorem exists_eq_forall_mem_Icc_eq_picard
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x ‚àß ‚àÄ t ‚àà Icc tmin tmax, Œ± t = ODE.picard f t‚ÇÄ x Œ± t := by
  obtain ‚ü®Œ±, hŒ±‚ü© := FunSpace.exists_isFixedPt_next hf hx
  refine ‚ü®(FunSpace.next hf hx Œ±).compProj, by simp, fun t ht ‚Ü¶ ?_‚ü©
  rw [FunSpace.compProj_apply, FunSpace.next_apply, hŒ±, projIcc_of_mem _ ht]

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local solution whose initial point `x` may be different from the centre `x‚ÇÄ` of
the closed ball within which the properties of the vector field hold. -/
theorem exists_eq_forall_mem_Icc_hasDerivWithinAt
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) (hx : x ‚àà closedBall x‚ÇÄ r) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x ‚àß
      ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt Œ± (f t (Œ± t)) (Icc tmin tmax) t := by
  obtain ‚ü®Œ±, hŒ±‚ü© := FunSpace.exists_isFixedPt_next hf hx
  refine ‚ü®Œ±.compProj, by rw [FunSpace.compProj_val, ‚Üê hŒ±, FunSpace.next_apply‚ÇÄ], fun t ht ‚Ü¶ ?_‚ü©
  apply hasDerivWithinAt_picard_Icc t‚ÇÄ.2 hf.continuousOn_uncurry
    Œ±.continuous_compProj.continuousOn (fun _ ht' ‚Ü¶ Œ±.compProj_mem_closedBall hf.mul_max_le)
    x ht |>.congr_of_mem _ ht
  intro t' ht'
  nth_rw 1 [‚Üê hŒ±]
  rw [FunSpace.compProj_of_mem ht', FunSpace.next_apply]

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. -/
theorem exists_eq_forall_mem_Icc_hasDerivWithinAt‚ÇÄ
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a 0 L K) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x‚ÇÄ ‚àß
      ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt Œ± (f t (Œ± t)) (Icc tmin tmax) t :=
  exists_eq_forall_mem_Icc_hasDerivWithinAt hf (mem_closedBall_self le_rfl)

@[deprecated (since := "2025-06-24")] alias exists_forall_hasDerivWithinAt_Icc_eq :=
  exists_eq_forall_mem_Icc_hasDerivWithinAt‚ÇÄ

open Classical in
/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow and that it is Lipschitz continuous in the initial point. -/
theorem exists_forall_mem_closedBall_eq_hasDerivWithinAt_lipschitzOnWith
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, (‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± x t‚ÇÄ = x ‚àß
      ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt (Œ± x) (f t (Œ± x t)) (Icc tmin tmax) t) ‚àß
      ‚àÉ L' : ‚Ñù‚â•0, ‚àÄ t ‚àà Icc tmin tmax, LipschitzOnWith L' (Œ± ¬∑ t) (closedBall x‚ÇÄ r) := by
  have (x) (hx : x ‚àà closedBall x‚ÇÄ r) := FunSpace.exists_isFixedPt_next hf hx
  choose Œ± hŒ± using this
  set Œ±' := fun (x : E) ‚Ü¶ if hx : x ‚àà closedBall x‚ÇÄ r then
    Œ± x hx |>.compProj else 0 with hŒ±'
  refine ‚ü®Œ±', fun x hx ‚Ü¶ ‚ü®?_, fun t ht ‚Ü¶ ?_‚ü©, ?_‚ü©
  ¬∑ rw [hŒ±']
    beta_reduce
    rw [dif_pos hx, FunSpace.compProj_val, ‚Üê hŒ±, FunSpace.next_apply‚ÇÄ]
  ¬∑ rw [hŒ±']
    beta_reduce
    rw [dif_pos hx, FunSpace.compProj_apply]
    apply hasDerivWithinAt_picard_Icc t‚ÇÄ.2 hf.continuousOn_uncurry
      (Œ± x hx |>.continuous_compProj.continuousOn)
      (fun _ ht' ‚Ü¶ Œ± x hx |>.compProj_mem_closedBall hf.mul_max_le)
      x ht |>.congr_of_mem _ ht
    intro t' ht'
    nth_rw 1 [‚Üê hŒ±]
    rw [FunSpace.compProj_of_mem ht', FunSpace.next_apply]
  ¬∑ obtain ‚ü®L', h‚ü© := FunSpace.exists_forall_closedBall_funSpace_dist_le_mul hf
    refine ‚ü®L', fun t ht ‚Ü¶ LipschitzOnWith.of_dist_le_mul fun x hx y hy ‚Ü¶ ?_‚ü©
    simp_rw [hŒ±']
    rw [dif_pos hx, dif_pos hy, FunSpace.compProj_apply, FunSpace.compProj_apply,
      ‚Üê FunSpace.toContinuousMap_apply_eq_apply, ‚Üê FunSpace.toContinuousMap_apply_eq_apply]
    have : Nonempty (Icc tmin tmax) := ‚ü®t‚ÇÄ‚ü©
    apply ContinuousMap.dist_le_iff_of_nonempty.mp
    exact h x y hx hy (Œ± x hx) (Œ± y hy) (hŒ± x hx) (hŒ± y hy)

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow and that it is continuous on its domain as a (partial) map `E √ó ‚Ñù ‚Üí E`. -/
theorem exists_forall_mem_closedBall_eq_hasDerivWithinAt_continuousOn
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E √ó ‚Ñù ‚Üí E, (‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± ‚ü®x, t‚ÇÄ‚ü© = x ‚àß
      ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt (Œ± ‚ü®x, ¬∑‚ü©) (f t (Œ± ‚ü®x, t‚ü©)) (Icc tmin tmax) t) ‚àß
      ContinuousOn Œ± (closedBall x‚ÇÄ r √óÀ¢ Icc tmin tmax) := by
  obtain ‚ü®Œ±, hŒ±1, L', hŒ±2‚ü© := hf.exists_forall_mem_closedBall_eq_hasDerivWithinAt_lipschitzOnWith
  refine ‚ü®uncurry Œ±, hŒ±1, ?_‚ü©
  apply continuousOn_prod_of_continuousOn_lipschitzOnWith _ L' _ hŒ±2
  exact fun x hx ‚Ü¶ HasDerivWithinAt.continuousOn (hŒ±1 x hx).2

/-- **Picard-Lindel√∂f (Cauchy-Lipschitz) theorem**, differential form. This version shows the
existence of a local flow. -/
theorem exists_forall_mem_closedBall_eq_forall_mem_Icc_hasDerivWithinAt
    (hf : IsPicardLindelof f t‚ÇÄ x‚ÇÄ a r L K) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, ‚àÄ x ‚àà closedBall x‚ÇÄ r, Œ± x t‚ÇÄ = x ‚àß
      ‚àÄ t ‚àà Icc tmin tmax, HasDerivWithinAt (Œ± x) (f t (Œ± x t)) (Icc tmin tmax) t :=
  have ‚ü®Œ±, hŒ±‚ü© := exists_forall_mem_closedBall_eq_hasDerivWithinAt_lipschitzOnWith hf
  ‚ü®Œ±, hŒ±.1‚ü©

end IsPicardLindelof

/-! ## $C^1$ vector field -/

namespace ContDiffAt

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] [CompleteSpace E]
  {f : E ‚Üí E} {x‚ÇÄ : E}

/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits an
integral curve `Œ± : ‚Ñù ‚Üí E` defined on an open interval, with initial condition `Œ± t‚ÇÄ = x`, where
`x` may be different from `x‚ÇÄ`. -/
theorem exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ r > (0 : ‚Ñù), ‚àÉ Œµ > (0 : ‚Ñù), ‚àÄ x ‚àà closedBall x‚ÇÄ r, ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x ‚àß
      ‚àÄ t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), HasDerivAt Œ± (f (Œ± t)) t := by
  have ‚ü®Œµ, hŒµ, a, r, _, _, hr, hpl‚ü© := IsPicardLindelof.of_contDiffAt_one hf t‚ÇÄ
  refine ‚ü®r, hr, Œµ, hŒµ, fun x hx ‚Ü¶ ?_‚ü©
  have ‚ü®Œ±, hŒ±1, hŒ±2‚ü© := hpl.exists_eq_forall_mem_Icc_hasDerivWithinAt hx
  refine ‚ü®Œ±, hŒ±1, fun t ht ‚Ü¶ ?_‚ü©
  exact hŒ±2 t (Ioo_subset_Icc_self ht) |>.hasDerivAt (Icc_mem_nhds ht.1 ht.2)

/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits an
integral curve `Œ± : ‚Ñù ‚Üí E` defined on an open interval, with initial condition `Œ± t‚ÇÄ = x‚ÇÄ`. -/
theorem exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt‚ÇÄ
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ Œ± : ‚Ñù ‚Üí E, Œ± t‚ÇÄ = x‚ÇÄ ‚àß ‚àÉ Œµ > (0 : ‚Ñù),
      ‚àÄ t ‚àà Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), HasDerivAt Œ± (f (Œ± t)) t :=
  have ‚ü®_, hr, Œµ, hŒµ, H‚ü© := exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt hf t‚ÇÄ
  have ‚ü®Œ±, hŒ±1, hŒ±2‚ü© := H x‚ÇÄ (mem_closedBall_self (le_of_lt hr))
  ‚ü®Œ±, hŒ±1, Œµ, hŒµ, hŒ±2‚ü©

@[deprecated (since := "2025-06-24")] alias exists_forall_hasDerivAt_Ioo_eq_of_contDiffAt :=
  exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt‚ÇÄ

open Classical in
/-- If a vector field `f : E ‚Üí E` is continuously differentiable at `x‚ÇÄ : E`, then it admits a flow
`Œ± : E ‚Üí ‚Ñù ‚Üí E` defined on an open domain, with initial condition `Œ± x t‚ÇÄ = x` for all `x` within
the domain. -/
theorem exists_eventually_eq_hasDerivAt
    (hf : ContDiffAt ‚Ñù 1 f x‚ÇÄ) (t‚ÇÄ : ‚Ñù) :
    ‚àÉ Œ± : E ‚Üí ‚Ñù ‚Üí E, ‚àÄ·∂† xt in ùìù x‚ÇÄ √óÀ¢ ùìù t‚ÇÄ,
      Œ± xt.1 t‚ÇÄ = xt.1 ‚àß HasDerivAt (Œ± xt.1) (f (Œ± xt.1 xt.2)) xt.2 := by
  obtain ‚ü®r, hr, Œµ, hŒµ, H‚ü© := exists_forall_mem_closedBall_exists_eq_forall_mem_Ioo_hasDerivAt hf t‚ÇÄ
  choose Œ± hŒ± using H
  refine ‚ü®fun (x : E) ‚Ü¶ if hx : x ‚àà closedBall x‚ÇÄ r then Œ± x hx else 0, ?_‚ü©
  rw [Filter.eventually_iff_exists_mem]
  refine ‚ü®closedBall x‚ÇÄ r √óÀ¢ Ioo (t‚ÇÄ - Œµ) (t‚ÇÄ + Œµ), ?_, ?_‚ü©
  ¬∑ rw [Filter.prod_mem_prod_iff]
    exact ‚ü®closedBall_mem_nhds x‚ÇÄ hr, Ioo_mem_nhds (by linarith) (by linarith)‚ü©
  ¬∑ grind

end ContDiffAt
