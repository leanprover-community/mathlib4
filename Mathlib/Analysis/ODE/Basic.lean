/-
Copyright (c) 2025 Winston Yin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Winston Yin
-/
module

public import Mathlib.Analysis.Calculus.Deriv.Basic

/-!
# Integral curves of vector fields on a Banach space

Let `E` be a Banach space and `v : E â†’ E` be a vector field on `E`. An integral curve of `v` is a
function `Î³ : â„ â†’ E` such that the derivative of `Î³` at `t` equals `v t (Î³ t)`. The integral curve
may only be defined for all `t` within some subset of `â„`.

## Main definitions

Let `v : E â†’ E` be a vector field on `E`, and let `Î³ : â„ â†’ E`.
* `IsIntegralCurve Î³ v`: `Î³ t` is tangent to `v (Î³ t)` for all `t : â„`. That is, `Î³` is a global
  integral curve of `v`.
* `IsIntegralCurveOn Î³ v s`: `Î³ t` is tangent to `v (Î³ t)` for all `t âˆˆ s`, where `s : Set â„`.
* `IsIntegralCurveAt Î³ v tâ‚€`: `Î³ t` is tangent to `v (Î³ t)` for all `t` in some open interval
  around `tâ‚€`. That is, `Î³` is a local integral curve of `v`.

For `IsIntegralCurveOn Î³ v s` and `IsIntegralCurveAt Î³ v tâ‚€`, even though `Î³` is defined for all
time, its value outside of the set `s` or a small interval around `tâ‚€` is irrelevant and considered
junk.

## TODO

* Implement `IsIntegralCurveWithinAt`.

## Tags

integral curve, vector field
-/

@[expose] public section

open scoped Topology

open Set

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]

/-- `IsIntegralCurveOn Î³ v s` means `Î³ t` is tangent to `v (Î³ t)` within `s` for all `t âˆˆ s`. The
value of `Î³` outside of `s` is irrelevant and considered junk. -/
def IsIntegralCurveOn (Î³ : â„ â†’ E) (v : E â†’ E) (s : Set â„) : Prop :=
  âˆ€ t âˆˆ s, HasDerivWithinAt Î³ (v (Î³ t)) s t

/-- `IsIntegralCurveAt Î³ v tâ‚€` means `Î³ : â„ â†’ E` is a local integral curve of `v` in a neighbourhood
containing `tâ‚€`. The value of `Î³` outside of this neighbourhood is irrelevant and considered
junk. -/
def IsIntegralCurveAt (Î³ : â„ â†’ E) (v : E â†’ E) (tâ‚€ : â„) : Prop :=
  âˆ€á¶  t in ð“ tâ‚€, HasDerivAt Î³ (v (Î³ t)) t

/-- `IsIntegralCurve Î³ v` means `Î³ : â„ â†’ E` is a global integral curve of `v`. That is, `Î³ t` is
tangent to `v (Î³ t)` for all `t : â„`. -/
def IsIntegralCurve (Î³ : â„ â†’ E) (v : E â†’ E) : Prop :=
  âˆ€ t : â„, HasDerivAt Î³ (v (Î³ t)) t

variable {Î³ Î³' : â„ â†’ E} {v : E â†’ E} {s s' : Set â„} {tâ‚€ : â„}

lemma IsIntegralCurve.isIntegralCurveOn (h : IsIntegralCurve Î³ v) (s : Set â„) :
    IsIntegralCurveOn Î³ v s := fun t _ â†¦ (h t).hasDerivWithinAt

lemma isIntegralCurveOn_univ :
    IsIntegralCurveOn Î³ v univ â†” IsIntegralCurve Î³ v :=
  âŸ¨fun h t â†¦ (h t (mem_univ _)).hasDerivAt Filter.univ_mem, fun h â†¦ h.isIntegralCurveOn _âŸ©

lemma isIntegralCurveAt_iff_exists_mem_nhds :
    IsIntegralCurveAt Î³ v tâ‚€ â†” âˆƒ s âˆˆ ð“ tâ‚€, IsIntegralCurveOn Î³ v s := by
  constructor
  Â· intro h
    rw [IsIntegralCurveAt, Filter.eventually_iff_exists_mem] at h
    obtain âŸ¨s, hs, hâŸ© := h
    exact âŸ¨s, hs, fun t ht â†¦ (h t ht).hasDerivWithinAtâŸ©
  Â· intro h
    rw [IsIntegralCurveAt, Filter.eventually_iff_exists_mem]
    obtain âŸ¨s, hs, hâŸ© := h
    rw [mem_nhds_iff] at hs
    obtain âŸ¨s', h1, h2, h3âŸ© := hs
    refine âŸ¨s', h2.mem_nhds h3, ?_âŸ©
    intro t ht
    apply (h t (h1 ht)).hasDerivAt
    rw [mem_nhds_iff]
    exact âŸ¨s', h1, h2, htâŸ©

/-- `Î³` is an integral curve for `v` at `tâ‚€` iff `Î³` is an integral curve on some interval
containing `tâ‚€`. -/
lemma isIntegralCurveAt_iff_exists_pos :
    IsIntegralCurveAt Î³ v tâ‚€ â†” âˆƒ Îµ > 0, IsIntegralCurveOn Î³ v (Metric.ball tâ‚€ Îµ) := by
  rw [IsIntegralCurveAt, Metric.eventually_nhds_iff_ball]
  congrm âˆƒ Îµ > 0, âˆ€ (y : â„) (hy : y âˆˆ Metric.ball tâ‚€ Îµ), ?_
  exact âŸ¨HasDerivAt.hasDerivWithinAt, fun h â†¦ h.hasDerivAt (Metric.isOpen_ball.mem_nhds hy)âŸ©

lemma IsIntegralCurve.isIntegralCurveAt (h : IsIntegralCurve Î³ v) (t : â„) :
    IsIntegralCurveAt Î³ v t :=
  isIntegralCurveAt_iff_exists_mem_nhds.mpr
    âŸ¨univ, Filter.univ_mem, fun t _ â†¦ (h t).hasDerivWithinAtâŸ©

lemma isIntegralCurve_iff_isIntegralCurveAt :
    IsIntegralCurve Î³ v â†” âˆ€ t : â„, IsIntegralCurveAt Î³ v t :=
  âŸ¨fun h â†¦ h.isIntegralCurveAt, fun h t â†¦ by
    obtain âŸ¨s, hs, hâŸ© := isIntegralCurveAt_iff_exists_mem_nhds.mp (h t)
    exact h t (mem_of_mem_nhds hs) |>.hasDerivAt hsâŸ©

lemma IsIntegralCurveOn.mono (h : IsIntegralCurveOn Î³ v s) (hs : s' âŠ† s) :
    IsIntegralCurveOn Î³ v s' := fun t ht â†¦ (h t (hs ht)).mono hs

lemma IsIntegralCurveAt.hasDerivAt (h : IsIntegralCurveAt Î³ v tâ‚€) :
    HasDerivAt Î³ (v (Î³ tâ‚€)) tâ‚€ :=
  have âŸ¨_, hs, hâŸ© := isIntegralCurveAt_iff_exists_mem_nhds.mp h
  h tâ‚€ (mem_of_mem_nhds hs) |>.hasDerivAt hs

lemma IsIntegralCurveOn.isIntegralCurveAt (h : IsIntegralCurveOn Î³ v s) (hs : s âˆˆ ð“ tâ‚€) :
    IsIntegralCurveAt Î³ v tâ‚€ := isIntegralCurveAt_iff_exists_mem_nhds.mpr âŸ¨s, hs, hâŸ©

/-- If `Î³` is an integral curve at each `t âˆˆ s`, it is an integral curve on `s`. -/
lemma IsIntegralCurveAt.isIntegralCurveOn (h : âˆ€ t âˆˆ s, IsIntegralCurveAt Î³ v t) :
    IsIntegralCurveOn Î³ v s := by
  intros t ht
  apply HasDerivAt.hasDerivWithinAt
  obtain âŸ¨s', hs', hâŸ© := Filter.eventually_iff_exists_mem.mp (h t ht)
  exact h _ (mem_of_mem_nhds hs')

lemma isIntegralCurveOn_iff_isIntegralCurveAt (hs : IsOpen s) :
    IsIntegralCurveOn Î³ v s â†” âˆ€ t âˆˆ s, IsIntegralCurveAt Î³ v t :=
  âŸ¨fun h _ ht â†¦ h.isIntegralCurveAt (hs.mem_nhds ht), IsIntegralCurveAt.isIntegralCurveOnâŸ©

lemma IsIntegralCurveOn.continuousWithinAt (hÎ³ : IsIntegralCurveOn Î³ v s) (ht : tâ‚€ âˆˆ s) :
    ContinuousWithinAt Î³ s tâ‚€ := (hÎ³ tâ‚€ ht).continuousWithinAt

lemma IsIntegralCurveOn.continuousOn (hÎ³ : IsIntegralCurveOn Î³ v s) :
    ContinuousOn Î³ s := fun t ht â†¦ (hÎ³ t ht).continuousWithinAt

lemma IsIntegralCurveAt.continuousAt (hÎ³ : IsIntegralCurveAt Î³ v tâ‚€) :
    ContinuousAt Î³ tâ‚€ :=
  have âŸ¨_, hs, hÎ³âŸ© := isIntegralCurveAt_iff_exists_mem_nhds.mp hÎ³
  hÎ³.continuousWithinAt (mem_of_mem_nhds hs) |>.continuousAt hs

lemma IsIntegralCurve.continuous (hÎ³ : IsIntegralCurve Î³ v) : Continuous Î³ :=
  continuous_iff_continuousAt.mpr fun t â†¦ (hÎ³.isIntegralCurveAt t).continuousAt
