/-
Copyright (c) 2023 Junyan Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Junyan Xu
-/
import Mathlib.Algebra.Polynomial.AlgebraMap
import Mathlib.Algebra.MvPolynomial.Basic
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Topology.Algebra.Module.FiniteDimension

/-!
# Polynomials are analytic

This file combines the analysis and algebra libraries and shows that evaluation of a polynomial
is an analytic function.
-/

variable {ğ•œ E A B : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [CommSemiring A] {z : E} {s : Set E}

section Polynomial
open Polynomial

variable [NormedRing B] [NormedAlgebra ğ•œ B] [Algebra A B] {f : E â†’ B}

theorem AnalyticAt.aeval_polynomial (hf : AnalyticAt ğ•œ f z) (p : A[X]) :
    AnalyticAt ğ•œ (fun x â†¦ aeval (f x) p) z := by
  refine p.induction_on (fun k â†¦ ?_) (fun p q hp hq â†¦ ?_) fun p i hp â†¦ ?_
  Â· simp_rw [aeval_C]; apply analyticAt_const
  Â· simp_rw [aeval_add]; exact hp.add hq
  Â· convert hp.mul hf
    simp_rw [pow_succ, aeval_mul, â† mul_assoc, aeval_X]

theorem AnalyticOn.aeval_polynomial (hf : AnalyticOn ğ•œ f s) (p : A[X]) :
    AnalyticOn ğ•œ (fun x â†¦ aeval (f x) p) s := fun x hx â†¦ (hf x hx).aeval_polynomial p

theorem AnalyticOn.eval_polynomial {A} [NormedCommRing A] [NormedAlgebra ğ•œ A] (p : A[X]) :
    AnalyticOn ğ•œ (eval Â· p) Set.univ := (analyticOn_id ğ•œ).aeval_polynomial p

end Polynomial

section MvPolynomial
open MvPolynomial

variable [NormedCommRing B] [NormedAlgebra ğ•œ B] [Algebra A B] {Ïƒ : Type*} {f : E â†’ Ïƒ â†’ B}

theorem AnalyticAt.aeval_mvPolynomial (hf : âˆ€ i, AnalyticAt ğ•œ (f Â· i) z) (p : MvPolynomial Ïƒ A) :
    AnalyticAt ğ•œ (fun x â†¦ aeval (f x) p) z := by
  apply p.induction_on (fun k â†¦ ?_) (fun p q hp hq â†¦ ?_) fun p i hp â†¦ ?_ -- `refine` doesn't work
  Â· simp_rw [aeval_C]; apply analyticAt_const
  Â· simp_rw [map_add]; exact hp.add hq
  Â· simp_rw [map_mul, aeval_X]; exact hp.mul (hf i)

theorem AnalyticOn.aeval_mvPolynomial (hf : âˆ€ i, AnalyticOn ğ•œ (f Â· i) s) (p : MvPolynomial Ïƒ A) :
    AnalyticOn ğ•œ (fun x â†¦ aeval (f x) p) s := fun x hx â†¦ .aeval_mvPolynomial (hf Â· x hx) p

theorem AnalyticOn.eval_continuousLinearMap (f : E â†’L[ğ•œ] Ïƒ â†’ B) (p : MvPolynomial Ïƒ B) :
    AnalyticOn ğ•œ (fun x â†¦ eval (f x) p) Set.univ :=
  fun x _ â†¦ .aeval_mvPolynomial (fun i â†¦ ((ContinuousLinearMap.proj i).comp f).analyticAt x) p

theorem AnalyticOn.eval_continuousLinearMap' (f : Ïƒ â†’ E â†’L[ğ•œ] B) (p : MvPolynomial Ïƒ B) :
    AnalyticOn ğ•œ (fun x â†¦ eval (f Â· x) p) Set.univ :=
  fun x _ â†¦ .aeval_mvPolynomial (fun i â†¦ (f i).analyticAt x) p

variable [CompleteSpace ğ•œ] [T2Space E] [FiniteDimensional ğ•œ E]

theorem AnalyticOn.eval_linearMap (f : E â†’â‚—[ğ•œ] Ïƒ â†’ B) (p : MvPolynomial Ïƒ B) :
    AnalyticOn ğ•œ (fun x â†¦ eval (f x) p) Set.univ :=
  AnalyticOn.eval_continuousLinearMap { f with cont := f.continuous_of_finiteDimensional } p

theorem AnalyticOn.eval_linearMap' (f : Ïƒ â†’ E â†’â‚—[ğ•œ] B) (p : MvPolynomial Ïƒ B) :
    AnalyticOn ğ•œ (fun x â†¦ eval (f Â· x) p) Set.univ := AnalyticOn.eval_linearMap (.pi f) p

theorem AnalyticOn.eval_mvPolynomial [Fintype Ïƒ] (p : MvPolynomial Ïƒ ğ•œ) :
    AnalyticOn ğ•œ (eval Â· p) Set.univ := AnalyticOn.eval_linearMap (.id (R := ğ•œ) (M := Ïƒ â†’ ğ•œ)) p

end MvPolynomial
