/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara
-/
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Calculus.DSlope
import Mathlib.Analysis.Calculus.FDeriv.Analytic
import Mathlib.Analysis.Analytic.Uniqueness
import Mathlib.Topology.Perfect

/-!
# Principle of isolated zeros

This file proves the fact that the zeros of a non-constant analytic function of one variable are
isolated. It also introduces a little bit of API in the `HasFPowerSeriesAt` namespace that is
useful in this setup.

## Main results

* `AnalyticAt.eventually_eq_zero_or_eventually_ne_zero` is the main statement that if a function is
  analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
  vanish in a punctured neighborhood of `zâ‚€`.
* `AnalyticOnNhd.eqOn_of_preconnected_of_frequently_eq` is the identity theorem for analytic
  functions: if a function `f` is analytic on a connected set `U` and is zero on a set with an
  accumulation point in `U` then `f` is identically `0` on `U`.
-/

open Filter Function Nat FormalMultilinearSeries EMetric Set

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {s : E} {p q : FormalMultilinearSeries ğ•œ ğ•œ E} {f g : ğ•œ â†’ E} {n : â„•} {z zâ‚€ : ğ•œ}

namespace HasSum

variable {a : â„• â†’ E}

theorem hasSum_at_zero (a : â„• â†’ E) : HasSum (fun n => (0 : ğ•œ) ^ n â€¢ a n) (a 0) := by
  convert hasSum_single (Î± := E) 0 fun b h â†¦ _ <;> simp [*]

theorem exists_hasSum_smul_of_apply_eq_zero (hs : HasSum (fun m => z ^ m â€¢ a m) s)
    (ha : âˆ€ k < n, a k = 0) : âˆƒ t : E, z ^ n â€¢ t = s âˆ§ HasSum (fun m => z ^ m â€¢ a (m + n)) t := by
  obtain rfl | hn := n.eq_zero_or_pos
  Â· simpa
  by_cases h : z = 0
  Â· have : s = 0 := hs.unique (by simpa [ha 0 hn, h] using hasSum_at_zero a)
    exact âŸ¨a n, by simp [h, hn.ne', this], by simpa [h] using hasSum_at_zero fun m => a (m + n)âŸ©
  Â· refine âŸ¨(z ^ n)â»Â¹ â€¢ s, by field_simp [smul_smul], ?_âŸ©
    have h1 : âˆ‘ i âˆˆ Finset.range n, z ^ i â€¢ a i = 0 :=
      Finset.sum_eq_zero fun k hk => by simp [ha k (Finset.mem_range.mp hk)]
    have h2 : HasSum (fun m => z ^ (m + n) â€¢ a (m + n)) s := by
      simpa [h1] using (hasSum_nat_add_iff' n).mpr hs
    convert h2.const_smul (zâ»Â¹ ^ n) using 1
    Â· field_simp [pow_add, smul_smul]
    Â· simp only [inv_pow]

end HasSum

namespace HasFPowerSeriesAt

theorem has_fpower_series_dslope_fslope (hp : HasFPowerSeriesAt f p zâ‚€) :
    HasFPowerSeriesAt (dslope f zâ‚€) p.fslope zâ‚€ := by
  have hpd : deriv f zâ‚€ = p.coeff 1 := hp.deriv
  have hp0 : p.coeff 0 = f zâ‚€ := hp.coeff_zero 1
  simp only [hasFPowerSeriesAt_iff, apply_eq_pow_smul_coeff, coeff_fslope] at hp âŠ¢
  refine hp.mono fun x hx => ?_
  by_cases h : x = 0
  Â· convert hasSum_single (Î± := E) 0 _ <;> intros <;> simp [*]
  Â· have hxx : âˆ€ n : â„•, xâ»Â¹ * x ^ (n + 1) = x ^ n := fun n => by field_simp [h, _root_.pow_succ]
    suffices HasSum (fun n => xâ»Â¹ â€¢ x ^ (n + 1) â€¢ p.coeff (n + 1)) (xâ»Â¹ â€¢ (f (zâ‚€ + x) - f zâ‚€)) by
      simpa [dslope, slope, h, smul_smul, hxx] using this
    simpa [hp0] using ((hasSum_nat_add_iff' 1).mpr hx).const_smul xâ»Â¹

theorem has_fpower_series_iterate_dslope_fslope (n : â„•) (hp : HasFPowerSeriesAt f p zâ‚€) :
    HasFPowerSeriesAt ((swap dslope zâ‚€)^[n] f) (fslope^[n] p) zâ‚€ := by
  induction n generalizing f p with
  | zero => exact hp
  | succ n ih => simpa using ih (has_fpower_series_dslope_fslope hp)

theorem iterate_dslope_fslope_ne_zero (hp : HasFPowerSeriesAt f p zâ‚€) (h : p â‰  0) :
    (swap dslope zâ‚€)^[p.order] f zâ‚€ â‰  0 := by
  rw [â† coeff_zero (has_fpower_series_iterate_dslope_fslope p.order hp) 1]
  simpa [coeff_eq_zero] using apply_order_ne_zero h

theorem eq_pow_order_mul_iterate_dslope (hp : HasFPowerSeriesAt f p zâ‚€) :
    âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ p.order â€¢ (swap dslope zâ‚€)^[p.order] f z := by
  have hq := hasFPowerSeriesAt_iff'.mp (has_fpower_series_iterate_dslope_fslope p.order hp)
  filter_upwards [hq, hasFPowerSeriesAt_iff'.mp hp] with x hx1 hx2
  have : âˆ€ k < p.order, p.coeff k = 0 := fun k hk => by
    simpa [coeff_eq_zero] using apply_eq_zero_of_lt_order hk
  obtain âŸ¨s, hs1, hs2âŸ© := HasSum.exists_hasSum_smul_of_apply_eq_zero hx2 this
  convert hs1.symm
  simp only [coeff_iterate_fslope] at hx1
  exact hx1.unique hs2

theorem locally_ne_zero (hp : HasFPowerSeriesAt f p zâ‚€) (h : p â‰  0) : âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0 := by
  rw [eventually_nhdsWithin_iff]
  have h2 := (has_fpower_series_iterate_dslope_fslope p.order hp).continuousAt
  have h3 := h2.eventually_ne (iterate_dslope_fslope_ne_zero hp h)
  filter_upwards [eq_pow_order_mul_iterate_dslope hp, h3] with z e1 e2 e3
  simpa [e1, e2, e3] using pow_ne_zero p.order (sub_ne_zero.mpr e3)

theorem locally_zero_iff (hp : HasFPowerSeriesAt f p zâ‚€) : (âˆ€á¶  z in ğ“ zâ‚€, f z = 0) â†” p = 0 :=
  âŸ¨fun hf => hp.eq_zero_of_eventually hf, fun h => eventually_eq_zero (ğ•œ := ğ•œ) (by rwa [h] at hp)âŸ©

end HasFPowerSeriesAt

namespace AnalyticAt

/-- The *principle of isolated zeros* for an analytic function, local version: if a function is
analytic at `zâ‚€`, then either it is identically zero in a neighborhood of `zâ‚€`, or it does not
vanish in a punctured neighborhood of `zâ‚€`. -/
theorem eventually_eq_zero_or_eventually_ne_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    (âˆ€á¶  z in ğ“ zâ‚€, f z = 0) âˆ¨ âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0 := by
  rcases hf with âŸ¨p, hpâŸ©
  by_cases h : p = 0
  Â· exact Or.inl (HasFPowerSeriesAt.eventually_eq_zero (by rwa [h] at hp))
  Â· exact Or.inr (hp.locally_ne_zero h)

theorem eventually_eq_or_eventually_ne (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    (âˆ€á¶  z in ğ“ zâ‚€, f z = g z) âˆ¨ âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  g z := by
  simpa [sub_eq_zero] using (hf.sub hg).eventually_eq_zero_or_eventually_ne_zero

theorem frequently_zero_iff_eventually_zero {f : ğ•œ â†’ E} {w : ğ•œ} (hf : AnalyticAt ğ•œ f w) :
    (âˆƒá¶  z in ğ“[â‰ ] w, f z = 0) â†” âˆ€á¶  z in ğ“ w, f z = 0 :=
  âŸ¨hf.eventually_eq_zero_or_eventually_ne_zero.resolve_right, fun h =>
    (h.filter_mono nhdsWithin_le_nhds).frequentlyâŸ©

theorem frequently_eq_iff_eventually_eq (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    (âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = g z) â†” âˆ€á¶  z in ğ“ zâ‚€, f z = g z := by
  simpa [sub_eq_zero] using frequently_zero_iff_eventually_zero (hf.sub hg)

/-- For a function `f` on `ğ•œ`, and `zâ‚€ âˆˆ ğ•œ`, there exists at most one `n` such that on a punctured
neighbourhood of `zâ‚€` we have `f z = (z - zâ‚€) ^ n â€¢ g z`, with `g` analytic and nonvanishing at
`zâ‚€`. We formulate this with `n : â„¤`, and deduce the case `n : â„•` later, for applications to
meromorphic functions. -/
lemma unique_eventuallyEq_zpow_smul_nonzero {m n : â„¤}
    (hm : âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z = (z - zâ‚€) ^ m â€¢ g z)
    (hn : âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z) :
    m = n := by
  wlog h_le : n â‰¤ m generalizing m n
  Â· exact ((this hn hm) (not_le.mp h_le).le).symm
  let âŸ¨g, hg_an, _, hg_eqâŸ© := hm
  let âŸ¨j, hj_an, hj_ne, hj_eqâŸ© := hn
  contrapose! hj_ne
  have : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, j z = (z - zâ‚€) ^ (m - n) â€¢ g z := by
    apply Filter.Eventually.frequently
    rw [eventually_nhdsWithin_iff] at hg_eq hj_eq âŠ¢
    filter_upwards [hg_eq, hj_eq] with z hfz hfz' hz
    rw [â† add_sub_cancel_left n m, add_sub_assoc, zpow_addâ‚€ <| sub_ne_zero.mpr hz, mul_smul,
      hfz' hz, smul_right_inj <| zpow_ne_zero _ <| sub_ne_zero.mpr hz] at hfz
    exact hfz hz
  rw [frequently_eq_iff_eventually_eq hj_an] at this
  Â· rw [EventuallyEq.eq_of_nhds this, sub_self, zero_zpow _ (sub_ne_zero.mpr hj_ne), zero_smul]
  conv => enter [2, z, 1]; rw [â† Int.toNat_sub_of_le h_le, zpow_natCast]
  exact ((analyticAt_id.sub analyticAt_const).pow _).smul hg_an

/-- For a function `f` on `ğ•œ`, and `zâ‚€ âˆˆ ğ•œ`, there exists at most one `n` such that on a
neighbourhood of `zâ‚€` we have `f z = (z - zâ‚€) ^ n â€¢ g z`, with `g` analytic and nonvanishing at
`zâ‚€`. -/
lemma unique_eventuallyEq_pow_smul_nonzero {m n : â„•}
    (hm : âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ m â€¢ g z)
    (hn : âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z) :
    m = n := by
  simp_rw [â† zpow_natCast] at hm hn
  exact Int.ofNat_inj.mp <| unique_eventuallyEq_zpow_smul_nonzero
    (let âŸ¨g, hâ‚, hâ‚‚, hâ‚ƒâŸ© := hm; âŸ¨g, hâ‚, hâ‚‚, hâ‚ƒ.filter_mono nhdsWithin_le_nhdsâŸ©)
    (let âŸ¨g, hâ‚, hâ‚‚, hâ‚ƒâŸ© := hn; âŸ¨g, hâ‚, hâ‚‚, hâ‚ƒ.filter_mono nhdsWithin_le_nhdsâŸ©)

/-- If `f` is analytic at `zâ‚€`, then exactly one of the following two possibilities occurs: either
`f` vanishes identically near `zâ‚€`, or locally around `zâ‚€` it has the form `z â†¦ (z - zâ‚€) ^ n â€¢ g z`
for some `n` and some `g` which is analytic and non-vanishing at `zâ‚€`. -/
theorem exists_eventuallyEq_pow_smul_nonzero_iff (hf : AnalyticAt ğ•œ f zâ‚€) :
    (âˆƒ (n : â„•), âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
    âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z) â†” (Â¬âˆ€á¶  z in ğ“ zâ‚€, f z = 0) := by
  constructor
  Â· rintro âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ©
    contrapose! hg_ne
    apply EventuallyEq.eq_of_nhds
    rw [EventuallyEq, â† AnalyticAt.frequently_eq_iff_eventually_eq hg_an analyticAt_const]
    refine (eventually_nhdsWithin_iff.mpr ?_).frequently
    filter_upwards [hg_eq, hg_ne] with z hf_eq hf0 hz
    rwa [hf0, eq_comm, smul_eq_zero_iff_right] at hf_eq
    exact pow_ne_zero _ (sub_ne_zero.mpr hz)
  Â· intro hf_ne
    rcases hf with âŸ¨p, hpâŸ©
    exact âŸ¨p.order, _, âŸ¨_, hp.has_fpower_series_iterate_dslope_fslope p.orderâŸ©,
      hp.iterate_dslope_fslope_ne_zero (hf_ne.imp hp.locally_zero_iff.mpr),
      hp.eq_pow_order_mul_iterate_dslopeâŸ©

open scoped Classical in
/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•âˆ`.

This is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f z = (z - zâ‚€) ^ n â€¢ g z` with `g` analytic and non-vanishing at `zâ‚€`. See
`AnalyticAt.order_eq_top_iff` and `AnalyticAt.order_eq_nat_iff` for these equivalences. -/
noncomputable def order (hf : AnalyticAt ğ•œ f zâ‚€) : ENat :=
  if h : âˆ€á¶  z in ğ“ zâ‚€, f z = 0 then âŠ¤
  else â†‘(hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose

lemma order_eq_top_iff (hf : AnalyticAt ğ•œ f zâ‚€) : hf.order = âŠ¤ â†” âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by
  unfold order
  split_ifs with h
  Â· rwa [eq_self, true_iff]
  Â· simpa only [ne_eq, ENat.coe_ne_top, false_iff] using h

lemma order_eq_nat_iff (hf : AnalyticAt ğ•œ f zâ‚€) (n : â„•) : hf.order = â†‘n â†”
    âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold order
  split_ifs with h
  Â· simp only [ENat.top_ne_coe, false_iff]
    contrapose! h
    rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff]
    exact âŸ¨n, hâŸ©
  Â· rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff] at h
    refine âŸ¨fun hn â†¦ (WithTop.coe_inj.mp hn : h.choose = n) â–¸ h.choose_spec, fun h' â†¦ ?_âŸ©
    rw [unique_eventuallyEq_pow_smul_nonzero h.choose_spec h']

/- An analytic function `f` has finite order at a point `zâ‚€` iff it locally looks
  like `(z - zâ‚€) ^ order â€¢ g`, where `g` is analytic and does not vanish at
  `zâ‚€`. -/
lemma order_neq_top_iff (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order â‰  âŠ¤ â†” âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0
      âˆ§ f =á¶ [ğ“ zâ‚€] fun z â†¦ (z - zâ‚€) ^ (hf.order.toNat) â€¢ g z := by
  simp only [â† ENat.coe_toNat_eq_self, Eq.comm, EventuallyEq, â† hf.order_eq_nat_iff]

/- An analytic function has order zero at a point iff it does not vanish there. -/
lemma order_eq_zero_iff (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order = 0 â†” f zâ‚€ â‰  0 := by
  rw [â† ENat.coe_zero, order_eq_nat_iff hf 0]
  constructor
  Â· intro âŸ¨g, _, _, hgâŸ©
    simpa [hg.self_of_nhds]
  Â· exact fun hz â†¦ âŸ¨f, hf, hz, by simpâŸ©

/- An analytic function vanishes at a point if its order is nonzero when converted to â„•. -/
lemma apply_eq_zero_of_order_toNat_ne_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order.toNat â‰  0 â†’ f zâ‚€ = 0 := by
  simp [hf.order_eq_zero_iff]
  tauto

/- Helper lemma for `AnalyticAt.order_mul` -/
lemma order_mul_of_order_eq_top {f g : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€)
    (hg : AnalyticAt ğ•œ g zâ‚€) (h'f : hf.order = âŠ¤) :
    (hf.mul hg).order = âŠ¤ := by
  rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff] at *
  obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := h'f
  exact âŸ¨t, fun y hy â†¦ (by simp [hâ‚t y hy]), hâ‚‚t, hâ‚ƒtâŸ©

/-- The order is additive when multiplying analytic functions. -/
theorem order_mul {f g : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    (hf.mul hg).order = hf.order + hg.order := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  by_cases hâ‚‚f : hf.order = âŠ¤
  Â· simp [hf.order_mul_of_order_eq_top hg hâ‚‚f, hâ‚‚f]
  by_cases hâ‚‚g : hg.order = âŠ¤
  Â· simp [mul_comm f g, hg.order_mul_of_order_eq_top hf hâ‚‚g, hâ‚‚g]
  -- Non-trivial case: both functions do not vanish around zâ‚€
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hf.order_neq_top_iff.1 hâ‚‚f
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := hg.order_neq_top_iff.1 hâ‚‚g
  rw [â† ENat.coe_toNat hâ‚‚f, â† ENat.coe_toNat hâ‚‚g, â† ENat.coe_add, (hf.mul hg).order_eq_nat_iff]
  use gâ‚ * gâ‚‚, by exact hâ‚gâ‚.mul hâ‚gâ‚‚
  constructor
  Â· simp
    tauto
  Â· obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := eventually_nhds_iff.1 hâ‚ƒgâ‚
    obtain âŸ¨s, hâ‚s, hâ‚‚s, hâ‚ƒsâŸ© := eventually_nhds_iff.1 hâ‚ƒgâ‚‚
    exact eventually_nhds_iff.2
      âŸ¨t âˆ© s, fun y hy â†¦ (by simp [hâ‚t y hy.1, hâ‚s y hy.2]; ring), hâ‚‚t.inter hâ‚‚s, hâ‚ƒt, hâ‚ƒsâŸ©

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem order_pow {f : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) {n : â„•} :
    (hf.pow n).order = n â€¢ hf.order := by
  induction n
  case zero =>
    simp [AnalyticAt.order_eq_zero_iff]
  case succ n hn =>
    simp [add_mul, pow_add, (hf.pow n).order_mul hf, hn]

end AnalyticAt

namespace AnalyticOnNhd

variable {U : Set ğ•œ}

/-- The *principle of isolated zeros* for an analytic function, global version: if a function is
analytic on a connected set `U` and vanishes in arbitrary neighborhoods of a point `zâ‚€ âˆˆ U`, then
it is identically zero in `U`.
For higher-dimensional versions requiring that the function vanishes in a neighborhood of `zâ‚€`,
see `AnalyticOnNhd.eqOn_zero_of_preconnected_of_eventuallyEq_zero`. -/
theorem eqOn_zero_of_preconnected_of_frequently_eq_zero (hf : AnalyticOnNhd ğ•œ f U)
    (hU : IsPreconnected U) (hâ‚€ : zâ‚€ âˆˆ U) (hfw : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = 0) : EqOn f 0 U :=
  hf.eqOn_zero_of_preconnected_of_eventuallyEq_zero hU hâ‚€
    ((hf zâ‚€ hâ‚€).frequently_zero_iff_eventually_zero.1 hfw)

theorem eqOn_zero_or_eventually_ne_zero_of_preconnected (hf : AnalyticOnNhd ğ•œ f U)
    (hU : IsPreconnected U) : EqOn f 0 U âˆ¨ âˆ€á¶  x in codiscreteWithin U, f x â‰  0 := by
  simp only [or_iff_not_imp_right, ne_eq, eventually_iff, mem_codiscreteWithin,
    disjoint_principal_right, not_forall]
  rintro âŸ¨x, hx, hx2âŸ©
  refine hf.eqOn_zero_of_preconnected_of_frequently_eq_zero hU hx fun nh â†¦ hx2 ?_
  filter_upwards [nh] with a ha
  simp_all

theorem eqOn_zero_of_preconnected_of_mem_closure (hf : AnalyticOnNhd ğ•œ f U) (hU : IsPreconnected U)
    (hâ‚€ : zâ‚€ âˆˆ U) (hfzâ‚€ : zâ‚€ âˆˆ closure ({z | f z = 0} \ {zâ‚€})) : EqOn f 0 U :=
  hf.eqOn_zero_of_preconnected_of_frequently_eq_zero hU hâ‚€
    (mem_closure_ne_iff_frequently_within.mp hfzâ‚€)

/-- The *identity principle* for analytic functions, global version: if two functions are
analytic on a connected set `U` and coincide at points which accumulate to a point `zâ‚€ âˆˆ U`, then
they coincide globally in `U`.
For higher-dimensional versions requiring that the functions coincide in a neighborhood of `zâ‚€`,
see `AnalyticOnNhd.eqOn_of_preconnected_of_eventuallyEq`. -/
theorem eqOn_of_preconnected_of_frequently_eq (hf : AnalyticOnNhd ğ•œ f U) (hg : AnalyticOnNhd ğ•œ g U)
    (hU : IsPreconnected U) (hâ‚€ : zâ‚€ âˆˆ U) (hfg : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = g z) : EqOn f g U := by
  have hfg' : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, (f - g) z = 0 :=
    hfg.mono fun z h => by rw [Pi.sub_apply, h, sub_self]
  simpa [sub_eq_zero] using fun z hz =>
    (hf.sub hg).eqOn_zero_of_preconnected_of_frequently_eq_zero hU hâ‚€ hfg' hz

theorem eqOn_or_eventually_ne_of_preconnected (hf : AnalyticOnNhd ğ•œ f U) (hg : AnalyticOnNhd ğ•œ g U)
    (hU : IsPreconnected U) : EqOn f g U âˆ¨ âˆ€á¶  x in codiscreteWithin U, f x â‰  g x :=
  (eqOn_zero_or_eventually_ne_zero_of_preconnected (hf.sub hg) hU).imp
    (fun h _ hx â†¦ eq_of_sub_eq_zero (h hx))
    (by simp only [Pi.sub_apply, ne_eq, sub_eq_zero, imp_self])

theorem eqOn_of_preconnected_of_mem_closure (hf : AnalyticOnNhd ğ•œ f U) (hg : AnalyticOnNhd ğ•œ g U)
    (hU : IsPreconnected U) (hâ‚€ : zâ‚€ âˆˆ U) (hfg : zâ‚€ âˆˆ closure ({z | f z = g z} \ {zâ‚€})) :
    EqOn f g U :=
  hf.eqOn_of_preconnected_of_frequently_eq hg hU hâ‚€ (mem_closure_ne_iff_frequently_within.mp hfg)

/-- The *identity principle* for analytic functions, global version: if two functions on a normed
field `ğ•œ` are analytic everywhere and coincide at points which accumulate to a point `zâ‚€`, then
they coincide globally.
For higher-dimensional versions requiring that the functions coincide in a neighborhood of `zâ‚€`,
see `AnalyticOnNhd.eq_of_eventuallyEq`. -/
theorem eq_of_frequently_eq [ConnectedSpace ğ•œ] (hf : AnalyticOnNhd ğ•œ f univ)
    (hg : AnalyticOnNhd ğ•œ g univ) (hfg : âˆƒá¶  z in ğ“[â‰ ] zâ‚€, f z = g z) : f = g :=
  funext fun x =>
    eqOn_of_preconnected_of_frequently_eq hf hg isPreconnected_univ (mem_univ zâ‚€) hfg (mem_univ x)

@[deprecated (since := "2024-09-26")]
alias _root_.AnalyticOn.eq_of_frequently_eq := eq_of_frequently_eq

/-- The set where an analytic function has infinite order is clopen in its domain of analyticity. -/
theorem isClopen_setOf_order_eq_top (hâ‚f : AnalyticOnNhd ğ•œ f U) :
    IsClopen { u : U | (hâ‚f u.1 u.2).order = âŠ¤ } := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hâ‚f z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† (hâ‚f z.1 z.2).order_eq_top_iff] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [(hâ‚f _ w.2).order_eq_zero_iff.2 ((hâ‚t' w hw) (Subtype.coe_ne_coe.mpr hâ‚w))]
          exact ENat.zero_ne_top
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff]
    simp only [Set.mem_setOf_eq] at hz
    rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [Set.mem_compl_iff, Set.mem_singleton_iff, isOpen_induced hâ‚‚t', Set.mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y â†¦ hâ‚t' y hâ‚y.1, hâ‚‚t'.sdiff isClosed_singleton
    apply (Set.mem_diff w).1
    exact âŸ¨hw, Set.mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

section Mul
/-!
###Â Vanishing of products of analytic functions
-/

variable {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A]
  {B : Type*} [NormedAddCommGroup B] [NormedSpace ğ•œ B] [Module A B]

/-- If `f, g` are analytic on a neighbourhood of the preconnected open set `U`, and `f â€¢ g = 0`
on `U`, then either `f = 0` on `U` or `g = 0` on `U`. -/
lemma eq_zero_or_eq_zero_of_smul_eq_zero [NoZeroSMulDivisors A B]
    {f : ğ•œ â†’ A} {g : ğ•œ â†’ B} (hf : AnalyticOnNhd ğ•œ f U) (hg : AnalyticOnNhd ğ•œ g U)
    (hfg : âˆ€ z âˆˆ U, f z â€¢ g z = 0) (hU : IsPreconnected U) :
    (âˆ€ z âˆˆ U, f z = 0) âˆ¨ (âˆ€ z âˆˆ U, g z = 0) := by
  -- We want to apply `IsPreconnected.preperfect_of_nontrivial` which requires `U` to have at least
  -- two elements. So we need to dispose of the cases `#U = 0` and `#U = 1` first.
  by_cases hU' : U = âˆ…
  Â· simp [hU']
  obtain âŸ¨z, hzâŸ© : âˆƒ z, z âˆˆ U := nonempty_iff_ne_empty.mpr hU'
  by_cases hU'' : U = {z}
  Â· simpa [hU''] using hfg z hz
  apply (nontrivial_iff_ne_singleton hz).mpr at hU''
  -- Now connectedness implies that `z` is an accumulation point of `U`, so at least one of
  -- `f` and `g` must vanish frequently in a neighbourhood of `z`.
  have : âˆƒá¶  w in ğ“[â‰ ] z, w âˆˆ U :=
    frequently_mem_iff_neBot.mpr <| hU.preperfect_of_nontrivial hU'' z hz
  have : âˆƒá¶  w in ğ“[â‰ ] z, f w = 0 âˆ¨ g w = 0 :=
    this.mp <| by filter_upwards with w hw using smul_eq_zero.mp (hfg w hw)
  cases frequently_or_distrib.mp this with
  | inl h => exact Or.inl <| hf.eqOn_zero_of_preconnected_of_frequently_eq_zero hU hz h
  | inr h => exact Or.inr <| hg.eqOn_zero_of_preconnected_of_frequently_eq_zero hU hz h

/-- If `f, g` are analytic on a neighbourhood of the preconnected open set `U`, and `f * g = 0`
on `U`, then either `f = 0` on `U` or `g = 0` on `U`. -/
lemma eq_zero_or_eq_zero_of_mul_eq_zero [NoZeroDivisors A]
    {f g : ğ•œ â†’ A} (hf : AnalyticOnNhd ğ•œ f U) (hg : AnalyticOnNhd ğ•œ g U)
    (hfg : âˆ€ z âˆˆ U, f z * g z = 0) (hU : IsPreconnected U) :
    (âˆ€ z âˆˆ U, f z = 0) âˆ¨ (âˆ€ z âˆˆ U, g z = 0) :=
  eq_zero_or_eq_zero_of_smul_eq_zero hf hg hfg hU

end Mul

end AnalyticOnNhd
