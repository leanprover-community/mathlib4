/-
Copyright (c) 2023 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Geoffrey Irving
-/
import Mathlib.Analysis.Analytic.Composition
import Mathlib.Analysis.Analytic.Linear
import Mathlib.Analysis.NormedSpace.OperatorNorm.Mul

/-!
# Various ways to combine analytic functions

We show that the following are analytic:

1. Cartesian products of analytic functions
2. Arithmetic on analytic functions: `mul`, `smul`, `inv`, `div`
3. Finite sums and products: `Finset.sum`, `Finset.prod`
-/

noncomputable section

open scoped Classical
open Topology NNReal Filter ENNReal

open Set Filter Asymptotics

variable {Î± : Type*}
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E F G H : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G] [NormedAddCommGroup H]
  [NormedSpace ğ•œ H]

variable {ğ• : Type*} [NontriviallyNormedField ğ•] [NormedAlgebra ğ•œ ğ•]
variable {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A]

/-!
### Cartesian products are analytic
-/

/-- The radius of the Cartesian product of two formal series is the minimum of their radii. -/
lemma FormalMultilinearSeries.radius_prod_eq_min
    (p : FormalMultilinearSeries ğ•œ E F) (q : FormalMultilinearSeries ğ•œ E G) :
    (p.prod q).radius = min p.radius q.radius := by
  apply le_antisymm
  Â· refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
    rw [le_min_iff]
    have := (p.prod q).isLittleO_one_of_lt_radius hr
    constructor
    all_goals
      apply FormalMultilinearSeries.le_radius_of_isBigO
      refine (isBigO_of_le _ fun n â†¦ ?_).trans this.isBigO
      rw [norm_mul, norm_norm, norm_mul, norm_norm]
      refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
      rw [FormalMultilinearSeries.prod, ContinuousMultilinearMap.opNorm_prod]
    Â· apply le_max_left
    Â· apply le_max_right
  Â· refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
    rw [lt_min_iff] at hr
    have := ((p.isLittleO_one_of_lt_radius hr.1).add
      (q.isLittleO_one_of_lt_radius hr.2)).isBigO
    refine (p.prod q).le_radius_of_isBigO ((isBigO_of_le _ fun n â†¦ ?_).trans this)
    rw [norm_mul, norm_norm, â† add_mul, norm_mul]
    refine mul_le_mul_of_nonneg_right ?_ (norm_nonneg _)
    rw [FormalMultilinearSeries.prod, ContinuousMultilinearMap.opNorm_prod]
    refine (max_le_add_of_nonneg (norm_nonneg _) (norm_nonneg _)).trans ?_
    apply Real.le_norm_self

lemma HasFPowerSeriesOnBall.prod {e : E} {f : E â†’ F} {g : E â†’ G} {r s : â„â‰¥0âˆ}
    {p : FormalMultilinearSeries ğ•œ E F} {q : FormalMultilinearSeries ğ•œ E G}
    (hf : HasFPowerSeriesOnBall f p e r) (hg : HasFPowerSeriesOnBall g q e s) :
    HasFPowerSeriesOnBall (fun x â†¦ (f x, g x)) (p.prod q) e (min r s) where
  r_le := by
    rw [p.radius_prod_eq_min]
    exact min_le_min hf.r_le hg.r_le
  r_pos := lt_min hf.r_pos hg.r_pos
  hasSum := by
    intro y hy
    simp_rw [FormalMultilinearSeries.prod, ContinuousMultilinearMap.prod_apply]
    refine (hf.hasSum ?_).prod_mk (hg.hasSum ?_)
    Â· exact EMetric.mem_ball.mpr (lt_of_lt_of_le hy (min_le_left _ _))
    Â· exact EMetric.mem_ball.mpr (lt_of_lt_of_le hy (min_le_right _ _))

lemma HasFPowerSeriesAt.prod {e : E} {f : E â†’ F} {g : E â†’ G}
    {p : FormalMultilinearSeries ğ•œ E F} {q : FormalMultilinearSeries ğ•œ E G}
    (hf : HasFPowerSeriesAt f p e) (hg : HasFPowerSeriesAt g q e) :
    HasFPowerSeriesAt (fun x â†¦ (f x, g x)) (p.prod q) e := by
  rcases hf with âŸ¨_, hfâŸ©
  rcases hg with âŸ¨_, hgâŸ©
  exact âŸ¨_, hf.prod hgâŸ©

/-- The Cartesian product of analytic functions is analytic. -/
lemma AnalyticAt.prod {e : E} {f : E â†’ F} {g : E â†’ G}
    (hf : AnalyticAt ğ•œ f e) (hg : AnalyticAt ğ•œ g e) :
    AnalyticAt ğ•œ (fun x â†¦ (f x, g x)) e := by
  rcases hf with âŸ¨_, hfâŸ©
  rcases hg with âŸ¨_, hgâŸ©
  exact âŸ¨_, hf.prod hgâŸ©

/-- The Cartesian product of analytic functions is analytic. -/
lemma AnalyticOn.prod {f : E â†’ F} {g : E â†’ G} {s : Set E}
    (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g s) :
    AnalyticOn ğ•œ (fun x â†¦ (f x, g x)) s :=
  fun x hx â†¦ (hf x hx).prod (hg x hx)

/-- `AnalyticAt.comp` for functions on product spaces -/
theorem AnalyticAt.compâ‚‚ {h : F Ã— G â†’ H} {f : E â†’ F} {g : E â†’ G} {x : E}
    (ha : AnalyticAt ğ•œ h (f x, g x)) (fa : AnalyticAt ğ•œ f x)
    (ga : AnalyticAt ğ•œ g x) :
    AnalyticAt ğ•œ (fun x â†¦ h (f x, g x)) x :=
  AnalyticAt.comp ha (fa.prod ga)

/-- `AnalyticOn.comp` for functions on product spaces -/
theorem AnalyticOn.compâ‚‚ {h : F Ã— G â†’ H} {f : E â†’ F} {g : E â†’ G} {s : Set (F Ã— G)} {t : Set E}
    (ha : AnalyticOn ğ•œ h s) (fa : AnalyticOn ğ•œ f t) (ga : AnalyticOn ğ•œ g t)
    (m : âˆ€ x, x âˆˆ t â†’ (f x, g x) âˆˆ s) : AnalyticOn ğ•œ (fun x â†¦ h (f x, g x)) t :=
  fun _ xt â†¦ (ha _ (m _ xt)).compâ‚‚ (fa _ xt) (ga _ xt)

/-- Analytic functions on products are analytic in the first coordinate -/
theorem AnalyticAt.curry_left {f : E Ã— F â†’ G} {p : E Ã— F} (fa : AnalyticAt ğ•œ f p) :
    AnalyticAt ğ•œ (fun x â†¦ f (x, p.2)) p.1 :=
  AnalyticAt.compâ‚‚ fa (analyticAt_id _ _) analyticAt_const
alias AnalyticAt.along_fst := AnalyticAt.curry_left

/-- Analytic functions on products are analytic in the second coordinate -/
theorem AnalyticAt.curry_right {f : E Ã— F â†’ G} {p : E Ã— F} (fa : AnalyticAt ğ•œ f p) :
    AnalyticAt ğ•œ (fun y â†¦ f (p.1, y)) p.2 :=
  AnalyticAt.compâ‚‚ fa analyticAt_const (analyticAt_id _ _)
alias AnalyticAt.along_snd := AnalyticAt.curry_right

/-- Analytic functions on products are analytic in the first coordinate -/
theorem AnalyticOn.curry_left {f : E Ã— F â†’ G} {s : Set (E Ã— F)} {y : F} (fa : AnalyticOn ğ•œ f s) :
    AnalyticOn ğ•œ (fun x â†¦ f (x, y)) {x | (x, y) âˆˆ s} :=
  fun x m â†¦ (fa (x, y) m).along_fst
alias AnalyticOn.along_fst := AnalyticOn.curry_left

/-- Analytic functions on products are analytic in the second coordinate -/
theorem AnalyticOn.curry_right {f : E Ã— F â†’ G} {x : E} {s : Set (E Ã— F)} (fa : AnalyticOn ğ•œ f s) :
    AnalyticOn ğ•œ (fun y â†¦ f (x, y)) {y | (x, y) âˆˆ s} :=
  fun y m â†¦ (fa (x, y) m).along_snd
alias AnalyticOn.along_snd := AnalyticOn.curry_right

/-!
### Arithmetic on analytic functions
-/

/-- Scalar multiplication is analytic (jointly in both variables). The statement is a little
pedantic to allow towers of field extensions.

TODO: can we replace `ğ•œ'` with a "normed module" in such a way that `analyticAt_mul` is a special
case of this? -/
lemma analyticAt_smul [NormedSpace ğ• E] [IsScalarTower ğ•œ ğ• E] (z : ğ• Ã— E) :
    AnalyticAt ğ•œ (fun x : ğ• Ã— E â†¦ x.1 â€¢ x.2) z :=
  (ContinuousLinearMap.lsmul ğ•œ ğ•).analyticAt_bilinear z

/-- Multiplication in a normed algebra over `ğ•œ` is analytic. -/
lemma analyticAt_mul (z : A Ã— A) : AnalyticAt ğ•œ (fun x : A Ã— A â†¦ x.1 * x.2) z :=
  (ContinuousLinearMap.mul ğ•œ A).analyticAt_bilinear z

/-- Scalar multiplication of one analytic function by another. -/
lemma AnalyticAt.smul [NormedSpace ğ• F] [IsScalarTower ğ•œ ğ• F] {f : E â†’ ğ•} {g : E â†’ F} {z : E}
    (hf : AnalyticAt ğ•œ f z) (hg : AnalyticAt ğ•œ g z) :
    AnalyticAt ğ•œ (fun x â†¦ f x â€¢ g x) z :=
  (analyticAt_smul _).compâ‚‚ hf hg

/-- Scalar multiplication of one analytic function by another. -/
lemma AnalyticOn.smul [NormedSpace ğ• F] [IsScalarTower ğ•œ ğ• F] {f : E â†’ ğ•} {g : E â†’ F} {s : Set E}
    (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g s) :
    AnalyticOn ğ•œ (fun x â†¦ f x â€¢ g x) s :=
  fun _ m â†¦ (hf _ m).smul (hg _ m)

/-- Multiplication of analytic functions (valued in a normed `ğ•œ`-algebra) is analytic. -/
lemma AnalyticAt.mul {f g : E â†’ A} {z : E} (hf : AnalyticAt ğ•œ f z) (hg : AnalyticAt ğ•œ g z) :
    AnalyticAt ğ•œ (fun x â†¦ f x * g x) z :=
  (analyticAt_mul _).compâ‚‚ hf hg

/-- Multiplication of analytic functions (valued in a normed `ğ•œ`-algebra) is analytic. -/
lemma AnalyticOn.mul {f g : E â†’ A} {s : Set E} (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g s) :
    AnalyticOn ğ•œ (fun x â†¦ f x * g x) s :=
  fun _ m â†¦ (hf _ m).mul (hg _ m)

/-- Powers of analytic functions (into a normed `ğ•œ`-algebra) are analytic. -/
lemma AnalyticAt.pow {f : E â†’ A} {z : E} (hf : AnalyticAt ğ•œ f z) (n : â„•) :
    AnalyticAt ğ•œ (fun x â†¦ f x ^ n) z := by
  induction n with
  | zero =>
    simp only [Nat.zero_eq, pow_zero]
    apply analyticAt_const
  | succ m hm =>
    simp only [pow_succ]
    exact hm.mul hf

/-- Powers of analytic functions (into a normed `ğ•œ`-algebra) are analytic. -/
lemma AnalyticOn.pow {f : E â†’ A} {s : Set E} (hf : AnalyticOn ğ•œ f s) (n : â„•) :
    AnalyticOn ğ•œ (fun x â†¦ f x ^ n) s :=
  fun _ m â†¦ (hf _ m).pow n

section Geometric

variable (ğ•œ A : Type*) [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]

/-- The geometric series `1 + x + x ^ 2 + ...` as a `FormalMultilinearSeries`. -/
def formalMultilinearSeries_geometric : FormalMultilinearSeries ğ•œ A A :=
  fun n â†¦ ContinuousMultilinearMap.mkPiAlgebraFin ğ•œ n A

lemma formalMultilinearSeries_geometric_apply_norm_le (n : â„•) :
    â€–formalMultilinearSeries_geometric ğ•œ A nâ€– â‰¤ max 1 â€–(1 : A)â€– :=
  ContinuousMultilinearMap.norm_mkPiAlgebraFin_le

lemma formalMultilinearSeries_geometric_apply_norm [NormOneClass A] (n : â„•) :
    â€–formalMultilinearSeries_geometric ğ•œ A nâ€– = 1 :=
  ContinuousMultilinearMap.norm_mkPiAlgebraFin

end Geometric

lemma one_le_formalMultilinearSeries_geometric_radius (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    (A : Type*) [NormedRing A] [NormedAlgebra ğ•œ A] :
    1 â‰¤ (formalMultilinearSeries_geometric ğ•œ A).radius := by
  refine le_of_forall_nnreal_lt (fun r hr â†¦ ?_)
  rw [â† Nat.cast_one, ENNReal.coe_lt_natCast, Nat.cast_one] at hr
  apply FormalMultilinearSeries.le_radius_of_isBigO
  apply isBigO_of_le' (c := max 1 â€–(1 : A)â€–) atTop (fun n â†¦ ?_)
  simp only [norm_mul, norm_norm, norm_pow, Real.norm_eq_abs, NNReal.abs_eq, norm_one, mul_one,
    abs_norm]
  apply le_trans ?_ (formalMultilinearSeries_geometric_apply_norm_le ğ•œ A n)
  conv_rhs => rw [â† mul_one (â€–formalMultilinearSeries_geometric ğ•œ A nâ€–)]
  gcongr
  exact pow_le_one _ (coe_nonneg r) hr.le

lemma formalMultilinearSeries_geometric_radius (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    (A : Type*) [NormedRing A] [NormOneClass A] [NormedAlgebra ğ•œ A] :
    (formalMultilinearSeries_geometric ğ•œ A).radius = 1 := by
  apply le_antisymm ?_  (one_le_formalMultilinearSeries_geometric_radius ğ•œ A)
  refine le_of_forall_nnreal_lt (fun r hr â†¦ ?_)
  rw [â† ENNReal.coe_one, ENNReal.coe_le_coe]
  have := FormalMultilinearSeries.isLittleO_one_of_lt_radius _ hr
  simp_rw [formalMultilinearSeries_geometric_apply_norm, one_mul] at this
  contrapose! this
  simp_rw [IsLittleO, IsBigOWith, not_forall, norm_one, mul_one,
    not_eventually]
  refine âŸ¨1, one_pos, ?_âŸ©
  refine ((eventually_ne_atTop 0).mp (eventually_of_forall ?_)).frequently
  intro n hn
  push_neg
  rwa [norm_pow, one_lt_pow_iff_of_nonneg (norm_nonneg _) hn,
    Real.norm_of_nonneg (NNReal.coe_nonneg _), â† NNReal.coe_one,
    NNReal.coe_lt_coe]

lemma hasFPowerSeriesOnBall_inverse_one_sub
    (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    (A : Type*) [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] :
    HasFPowerSeriesOnBall (fun x : A â†¦ Ring.inverse (1 - x))
      (formalMultilinearSeries_geometric ğ•œ A) 0 1 := by
  constructor
  Â· exact one_le_formalMultilinearSeries_geometric_radius ğ•œ A
  Â· exact one_pos
  Â· intro y hy
    simp only [EMetric.mem_ball, edist_dist, dist_zero_right, ofReal_lt_one] at hy
    simp only [zero_add, NormedRing.inverse_one_sub _ hy, Units.oneSub, Units.inv_mk,
      formalMultilinearSeries_geometric, ContinuousMultilinearMap.mkPiAlgebraFin_apply,
      List.ofFn_const, List.prod_replicate]
    exact (NormedRing.summable_geometric_of_norm_lt_one _ hy).hasSum

lemma analyticAt_inverse_one_sub (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    (A : Type*) [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] :
    AnalyticAt ğ•œ (fun x : A â†¦ Ring.inverse (1 - x)) 0 :=
  âŸ¨_, âŸ¨_, hasFPowerSeriesOnBall_inverse_one_sub ğ•œ AâŸ©âŸ©

--lemma foo (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
--    (A : Type*) [NormedAddCommGroup A] [NormedSpace ğ•œ A] [Subsingleton A]


/-- If `A` is a complete normed algebra over `ğ•œ`, then inversion on `A` is analytic at any unit. -/
lemma analyticAt_inverse (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    (A : Type*) [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] {z : AË£} :
    AnalyticAt ğ•œ Ring.inverse (z : A) := by
  rcases subsingleton_or_nontrivial A with hA|hA
  Â· sorry
  Â· let f1 : A â†’ A := fun a â†¦ a * z.inv
    let f2 : A â†’ A := fun b â†¦ Ring.inverse (1 - b)
    let f3 : A â†’ A := fun c â†¦ 1 - z.inv * c
    have feq : âˆ€á¶  y in ğ“ (z : A), (f1 âˆ˜ f2 âˆ˜ f3) y = Ring.inverse y := by
      have : Metric.ball (z : A) (â€–(â†‘zâ»Â¹ : A)â€–â»Â¹) âˆˆ ğ“ (z : A) := by
        apply Metric.ball_mem_nhds
        simp
      filter_upwards [this] with y hy
      simp only [Metric.mem_ball, dist_eq_norm] at hy
      have : y = Units.ofNearby z y hy := rfl
      rw [this, Eq.comm]
      simp only [Ring.inverse_unit, Function.comp_apply]
      simp [Units.ofNearby, f1, f2, f3, Units.add, _root_.mul_sub]
      rw [â† Ring.inverse_unit]
      congr
      simp
    apply AnalyticAt.congr _ feq
    apply ((analyticAt_id ğ•œ _).mul analyticAt_const).comp
    apply AnalyticAt.comp
    Â· simp only [Units.inv_eq_val_inv, Units.inv_mul, sub_self, f2, f3]
      exact analyticAt_inverse_one_sub ğ•œ A
    Â· exact analyticAt_const.sub (analyticAt_const.mul (analyticAt_id _ _))



lemma hasFPowerSeriesOnBall_inv_one_sub
    (ğ•œ ğ• : Type*) [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•] [NormedAlgebra ğ•œ ğ•] :
    HasFPowerSeriesOnBall (fun x : ğ• â†¦ (1 - x)â»Â¹) (formalMultilinearSeries_geometric ğ•œ ğ•) 0 1 := by
  constructor
  Â· exact le_of_eq (formalMultilinearSeries_geometric_radius ğ•œ ğ•).symm
  Â· exact one_pos
  Â· intro y hy
    simp_rw [zero_add, formalMultilinearSeries_geometric,
        ContinuousMultilinearMap.mkPiAlgebraFin_apply,
        List.prod_ofFn, Finset.prod_const,
        Finset.card_univ, Fintype.card_fin]
    apply hasSum_geometric_of_norm_lt_one
    simpa only [â† ofReal_one, Metric.emetric_ball, Metric.ball,
      dist_eq_norm, sub_zero] using hy

lemma analyticAt_inv_one_sub (ğ• : Type*) [NontriviallyNormedField ğ•] [NormedAlgebra ğ•œ ğ•] :
    AnalyticAt ğ•œ (fun x : ğ• â†¦ (1 - x)â»Â¹) 0 :=
  âŸ¨_, âŸ¨_, hasFPowerSeriesOnBall_inv_one_sub ğ•œ ğ•âŸ©âŸ©

/-- If `ğ•` is a normed field extension of `ğ•œ`, then the inverse map `ğ• â†’ ğ•` is `ğ•œ`-analytic
away from 0. -/
lemma analyticAt_inv {z : ğ•} (hz : z â‰  0) : AnalyticAt ğ•œ Inv.inv z := by
  let f1 : ğ• â†’ ğ• := fun a â†¦ 1 / z * a
  let f2 : ğ• â†’ ğ• := fun b â†¦ (1 - b)â»Â¹
  let f3 : ğ• â†’ ğ• := fun c â†¦ 1 - c / z
  have feq : f1 âˆ˜ f2 âˆ˜ f3 = Inv.inv := by
    ext1 x
    dsimp only [f1, f2, f3, Function.comp_apply]
    field_simp
  have f3val : f3 z = 0 := by simp only [f3, div_self hz, sub_self]
  have f3an : AnalyticAt ğ•œ f3 z := by
    apply analyticAt_const.sub
    simpa only [div_eq_inv_mul] using analyticAt_const.mul (analyticAt_id ğ•œ z)
  exact feq â–¸ (analyticAt_const.mul (analyticAt_id _ _)).comp
    ((f3val.symm â–¸ analyticAt_inv_one_sub ğ•).comp f3an)

/-- `xâ»Â¹` is analytic away from zero -/
lemma analyticOn_inv : AnalyticOn ğ•œ (fun z â†¦ zâ»Â¹) {z : ğ• | z â‰  0} := by
  intro z m; exact analyticAt_inv m

/-- `(f x)â»Â¹` is analytic away from `f x = 0` -/
theorem AnalyticAt.inv {f : E â†’ ğ•} {x : E} (fa : AnalyticAt ğ•œ f x) (f0 : f x â‰  0) :
    AnalyticAt ğ•œ (fun x â†¦ (f x)â»Â¹) x :=
  (analyticAt_inv f0).comp fa

/-- `xâ»Â¹` is analytic away from zero -/
theorem AnalyticOn.inv {f : E â†’ ğ•} {s : Set E} (fa : AnalyticOn ğ•œ f s) (f0 : âˆ€ x âˆˆ s, f x â‰  0) :
    AnalyticOn ğ•œ (fun x â†¦ (f x)â»Â¹) s :=
  fun x m â†¦ (fa x m).inv (f0 x m)

/-- `f x / g x` is analytic away from `g x = 0` -/
theorem AnalyticAt.div {f g : E â†’ ğ•} {x : E}
    (fa : AnalyticAt ğ•œ f x) (ga : AnalyticAt ğ•œ g x) (g0 : g x â‰  0) :
    AnalyticAt ğ•œ (fun x â†¦ f x / g x) x := by
  simp_rw [div_eq_mul_inv]; exact fa.mul (ga.inv g0)

/-- `f x / g x` is analytic away from `g x = 0` -/
theorem AnalyticOn.div {f g : E â†’ ğ•} {s : Set E}
    (fa : AnalyticOn ğ•œ f s) (ga : AnalyticOn ğ•œ g s) (g0 : âˆ€ x âˆˆ s, g x â‰  0) :
    AnalyticOn ğ•œ (fun x â†¦ f x / g x) s := fun x m â†¦
  (fa x m).div (ga x m) (g0 x m)

/-!
### Finite sums and products of analytic functions
-/

/-- Finite sums of analytic functions are analytic -/
theorem Finset.analyticAt_sum {f : Î± â†’ E â†’ F} {c : E}
    (N : Finset Î±) (h : âˆ€ n âˆˆ N, AnalyticAt ğ•œ (f n) c) :
    AnalyticAt ğ•œ (fun z â†¦ âˆ‘ n âˆˆ N, f n z) c := by
  induction' N using Finset.induction with a B aB hB
  Â· simp only [Finset.sum_empty]
    exact analyticAt_const
  Â· simp_rw [Finset.sum_insert aB]
    simp only [Finset.mem_insert] at h
    exact (h a (Or.inl rfl)).add (hB fun b m â†¦ h b (Or.inr m))

/-- Finite sums of analytic functions are analytic -/
theorem Finset.analyticOn_sum {f : Î± â†’ E â†’ F} {s : Set E}
    (N : Finset Î±) (h : âˆ€ n âˆˆ N, AnalyticOn ğ•œ (f n) s) :
    AnalyticOn ğ•œ (fun z â†¦ âˆ‘ n âˆˆ N, f n z) s :=
  fun z zs â†¦ N.analyticAt_sum (fun n m â†¦ h n m z zs)

/-- Finite products of analytic functions are analytic -/
theorem Finset.analyticAt_prod {A : Type*} [NormedCommRing A] [NormedAlgebra ğ•œ A]
    {f : Î± â†’ E â†’ A} {c : E} (N : Finset Î±) (h : âˆ€ n âˆˆ N, AnalyticAt ğ•œ (f n) c) :
    AnalyticAt ğ•œ (fun z â†¦ âˆ n âˆˆ N, f n z) c := by
  induction' N using Finset.induction with a B aB hB
  Â· simp only [Finset.prod_empty]
    exact analyticAt_const
  Â· simp_rw [Finset.prod_insert aB]
    simp only [Finset.mem_insert] at h
    exact (h a (Or.inl rfl)).mul (hB fun b m â†¦ h b (Or.inr m))

/-- Finite products of analytic functions are analytic -/
theorem Finset.analyticOn_prod {A : Type*} [NormedCommRing A] [NormedAlgebra ğ•œ A]
    {f : Î± â†’ E â†’ A} {s : Set E} (N : Finset Î±) (h : âˆ€ n âˆˆ N, AnalyticOn ğ•œ (f n) s) :
    AnalyticOn ğ•œ (fun z â†¦ âˆ n âˆˆ N, f n z) s :=
  fun z zs â†¦ N.analyticAt_prod (fun n m â†¦ h n m z zs)
