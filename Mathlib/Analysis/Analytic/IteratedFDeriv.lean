/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Analysis.Calculus.ContDiff.CPolynomial
import Mathlib.Data.Fintype.Perm

/-!
# The iterated derivative of an analytic function

If a function is analytic, written as `f (x + y) = âˆ‘ pâ‚™ (y, ..., y)` then its `n`-th iterated
derivative at `x` is given by `(vâ‚, ..., vâ‚™) â†¦ âˆ‘ pâ‚™ (v_{Ïƒ (1)}, ..., v_{Ïƒ (n)})` where the sum
is over all permutations of `{1, ..., n}`. In particular, it is symmetric.

This generalizes the result of `HasFPowerSeriesOnBall.factorial_smul` giving
`D^n f (v, ..., v) = n! * pâ‚™ (v, ..., v)`.

## Main result

* `HasFPowerSeriesOnBall.iteratedFDeriv_eq_sum` shows that
  `iteratedFDeriv ğ•œ n f x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i))`,
  when `f` has `p` as power series within the set `s` on the ball `B (x, r)`.
* `ContDiffAt.iteratedFDeriv_comp_perm` proves the symmetry of the iterated derivative of an
  analytic function, in the form `iteratedFDeriv ğ•œ n f x (v âˆ˜ Ïƒ) = iteratedFDeriv ğ•œ n f x v`
  for any permutation `Ïƒ` of `Fin n`.

Versions within sets are also given.

## Implementation

To prove the formula for the iterated derivative, we decompose an analytic function as
the sum of `fun y â†¦ pâ‚™ (y, ..., y)` and the rest. For the former, its iterated derivative follows
from the formula for iterated derivatives of multilinear maps
(see `ContinuousMultilinearMap.iteratedFDeriv_comp_diagonal`). For the latter, we show by
induction on `n` that if the `n`-th term in a power series is zero, then the `n`-th iterated
derivative vanishes (see `HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_zero`).

All these results are proved assuming additionally that the function is analytic on the relevant
set (which does not follow from the fact that the function has a power series, if the target space
is not complete). This makes it possible to avoid all completeness assumptions in the final
statements. When needed, we give versions of some statements assuming completeness and dropping
analyticity, for ease of use.
-/

open scoped ENNReal Topology ContDiff
open Equiv Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F} {s : Set E} {x : E} {r : â„â‰¥0âˆ}

/-- Formal multilinear series associated to the iterated derivative, defined by iterating
`p â†¦ p.derivSeries` and currying suitably. It is defined so that, if a function has `p` as a power
series, then its iterated derivative of order `k` has `p.iteratedFDerivSeries k` as a power
series. -/
noncomputable def FormalMultilinearSeries.iteratedFDerivSeries
    (p : FormalMultilinearSeries ğ•œ E F) (k : â„•) :
    FormalMultilinearSeries ğ•œ E (E [Ã—k]â†’L[ğ•œ] F) :=
  match k with
  | 0 => (continuousMultilinearCurryFin0 ğ•œ E F).symm
      |>.toContinuousLinearEquiv.toContinuousLinearMap.compFormalMultilinearSeries p
  | (k + 1) => (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (k + 1) â†¦ E) F).symm
      |>.toContinuousLinearEquiv.toContinuousLinearMap.compFormalMultilinearSeries
      (p.iteratedFDerivSeries k).derivSeries

/-- If a function has a power series on a ball, then so do its iterated derivatives. -/
protected theorem HasFPowerSeriesWithinOnBall.iteratedFDerivWithin
    (h : HasFPowerSeriesWithinOnBall f p s x r) (h' : AnalyticOn ğ•œ f s)
    (k : â„•) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) :
    HasFPowerSeriesWithinOnBall (iteratedFDerivWithin ğ•œ k f s)
      (p.iteratedFDerivSeries k) s x r := by
  induction k with
  | zero =>
    exact (continuousMultilinearCurryFin0 ğ•œ E F).symm
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesWithinOnBall h
  | succ k ih =>
    rw [iteratedFDerivWithin_succ_eq_comp_left]
    apply (continuousMultilinearCurryLeftEquiv ğ•œ (fun _ : Fin (k + 1) â†¦ E) F).symm
      |>.toContinuousLinearEquiv.toContinuousLinearMap.comp_hasFPowerSeriesWithinOnBall
        (ih.fderivWithin_of_mem_of_analyticOn (h'.iteratedFDerivWithin hs _) hs hx)

lemma FormalMultilinearSeries.iteratedFDerivSeries_eq_zero {k n : â„•}
    (h : p (n + k) = 0) : p.iteratedFDerivSeries k n = 0 := by
  induction k generalizing n with
  | zero =>
    ext
    have : p n = 0 := p.congr_zero rfl h
    simp [FormalMultilinearSeries.iteratedFDerivSeries, this]
  | succ k ih =>
    ext
    simp only [iteratedFDerivSeries, Nat.succ_eq_add_one,
      ContinuousLinearMap.compFormalMultilinearSeries_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, ContinuousLinearEquiv.coe_coe,
      LinearIsometryEquiv.coe_toContinuousLinearEquiv, Function.comp_apply,
      continuousMultilinearCurryLeftEquiv_symm_apply, ContinuousMultilinearMap.zero_apply,
      ContinuousLinearMap.zero_apply,
      derivSeries_eq_zero _ (ih (p.congr_zero (Nat.succ_add_eq_add_succ _ _).symm h))]

/-- If the `n`-th term in a power series is zero, then the `n`-th derivative of the corresponding
function vanishes. -/
lemma HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_zero
    (h : HasFPowerSeriesWithinOnBall f p s x r) (h' : AnalyticOn ğ•œ f s)
    (hu : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (hn : p n = 0) :
    iteratedFDerivWithin ğ•œ n f s x = 0 := by
  have : iteratedFDerivWithin ğ•œ n f s x = p.iteratedFDerivSeries n 0 (fun _ â†¦ 0) :=
    ((h.iteratedFDerivWithin h' n hu hx).coeff_zero _).symm
  rw [this, p.iteratedFDerivSeries_eq_zero (p.congr_zero (Nat.zero_add n).symm hn),
    ContinuousMultilinearMap.zero_apply]

lemma ContinuousMultilinearMap.iteratedFDeriv_comp_diagonal
    {n : â„•} (f : E [Ã—n]â†’L[ğ•œ] F) (x : E) (v : Fin n â†’ E) :
    iteratedFDeriv ğ•œ n (fun x â†¦ f (fun _ â†¦ x)) x v = âˆ‘ Ïƒ : Perm (Fin n), f (fun i â†¦ v (Ïƒ i)) := by
  rw [â† sum_comp (Equiv.inv (Perm (Fin n)))]
  let g : E â†’L[ğ•œ] (Fin n â†’ E) := ContinuousLinearMap.pi (fun i â†¦ ContinuousLinearMap.id ğ•œ E)
  change iteratedFDeriv ğ•œ n (f âˆ˜ g) x v = _
  rw [ContinuousLinearMap.iteratedFDeriv_comp_right _ f.contDiff _ le_rfl, f.iteratedFDeriv_eq]
  simp only [ContinuousMultilinearMap.iteratedFDeriv,
    ContinuousMultilinearMap.compContinuousLinearMap_apply, ContinuousMultilinearMap.sum_apply,
    ContinuousMultilinearMap.iteratedFDerivComponent_apply, Set.mem_range, Pi.compRightL_apply]
  rw [â† sum_comp (Equiv.embeddingEquivOfFinite (Fin n))]
  congr with Ïƒ
  congr with i
  have A : âˆƒ y, Ïƒ y = i := by
    have : Function.Bijective Ïƒ := (Fintype.bijective_iff_injective_and_card _).2 âŸ¨Ïƒ.injective, rflâŸ©
    exact this.surjective i
  rcases A with âŸ¨y, rflâŸ©
  simp only [EmbeddingLike.apply_eq_iff_eq, exists_eq, â†“reduceDIte,
    Function.Embedding.toEquivRange_symm_apply_self, ContinuousLinearMap.coe_pi',
    ContinuousLinearMap.coe_id', id_eq, g]
  congr 1
  symm
  simp [coe_fn_mk, inv_apply, Perm.inv_def,
    ofBijective_symm_apply_apply, Function.Embedding.equivOfFiniteSelfEmbedding]

private lemma HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_sum_of_subset
    (h : HasFPowerSeriesWithinOnBall f p s x r) (h' : AnalyticOn ğ•œ f s)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s)
    {n : â„•} (v : Fin n â†’ E) (h's : s âŠ† EMetric.ball x r) :
    iteratedFDerivWithin ğ•œ n f s x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i)) := by
  have I : insert x s âˆ© EMetric.ball x r = s := by
    rw [Set.insert_eq_of_mem hx]
    exact Set.inter_eq_left.2 h's
  have fcont : ContDiffOn ğ•œ (â†‘n) f s := by
    apply AnalyticOn.contDiffOn _ hs
    simpa [I] using h'
  let g : E â†’ F := fun z â†¦ p n (fun _ â†¦ z - x)
  have gcont : ContDiff ğ•œ Ï‰ g := by
    apply (p n).contDiff.comp
    exact contDiff_pi.2 (fun i â†¦ contDiff_id.sub contDiff_const)
  let q : FormalMultilinearSeries ğ•œ E F := fun k â†¦ if h : n = k then (h â–¸ p n) else 0
  have A : HasFiniteFPowerSeriesOnBall g q x (n + 1) r := by
    apply HasFiniteFPowerSeriesOnBall.mk' _ h.r_pos
    Â· intro y hy
      rw [Finset.sum_eq_single_of_mem n]
      Â· simp [q, g]
      Â· simp
      Â· intro i hi h'i
        simp [q, h'i.symm]
    Â· intro m hm
      have : n â‰  m := by omega
      simp [q, this]
  have B : HasFPowerSeriesWithinOnBall g q s x r :=
    A.toHasFPowerSeriesOnBall.hasFPowerSeriesWithinOnBall
  have J1 : iteratedFDerivWithin ğ•œ n f s x =
      iteratedFDerivWithin ğ•œ n g s x + iteratedFDerivWithin ğ•œ n (f - g) s x := by
    have : f = g + (f - g) := by abel
    nth_rewrite 1 [this]
    rw [iteratedFDerivWithin_add_apply (gcont.of_le le_top).contDiffWithinAt
      (by exact (fcont _ hx).sub (gcont.of_le le_top).contDiffWithinAt) hs hx]
  have J2 : iteratedFDerivWithin ğ•œ n (f - g) s x = 0 := by
    apply (h.sub B).iteratedFDerivWithin_eq_zero (h'.sub ?_) hs hx
    Â· simp [q]
    Â· apply gcont.contDiffOn.analyticOn
  have J3 : iteratedFDerivWithin ğ•œ n g s x = iteratedFDeriv ğ•œ n g x :=
    iteratedFDerivWithin_eq_iteratedFDeriv hs (gcont.of_le le_top).contDiffAt hx
  simp only [J1, J3, J2, add_zero]
  let g' : E â†’ F := fun z â†¦ p n (fun _ â†¦ z)
  have : g = fun z â†¦ g' (z - x) := rfl
  rw [this, iteratedFDeriv_comp_sub]
  exact (p n).iteratedFDeriv_comp_diagonal _ v

/-- If a function has a power series in a ball, then its `n`-th iterated derivative is given by
`(vâ‚, ..., vâ‚™) â†¦ âˆ‘ pâ‚™ (v_{Ïƒ (1)}, ..., v_{Ïƒ (n)})` where the sum is over all
permutations of `{1, ..., n}`. -/
theorem HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_sum
    (h : HasFPowerSeriesWithinOnBall f p s x r) (h' : AnalyticOn ğ•œ f s)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (v : Fin n â†’ E) :
    iteratedFDerivWithin ğ•œ n f s x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i)) := by
  have : iteratedFDerivWithin ğ•œ n f s x
      = iteratedFDerivWithin ğ•œ n f (s âˆ© EMetric.ball x r) x :=
    (iteratedFDerivWithin_inter_open EMetric.isOpen_ball (EMetric.mem_ball_self h.r_pos)).symm
  rw [this]
  apply HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_sum_of_subset
  Â· exact h.mono inter_subset_left
  Â· exact h'.mono inter_subset_left
  Â· exact hs.inter EMetric.isOpen_ball
  Â· exact âŸ¨hx, EMetric.mem_ball_self h.r_posâŸ©
  Â· exact inter_subset_right

/-- If a function has a power series in a ball, then its `n`-th iterated derivative is given by
`(vâ‚, ..., vâ‚™) â†¦ âˆ‘ pâ‚™ (v_{Ïƒ (1)}, ..., v_{Ïƒ (n)})` where the sum is over all
permutations of `{1, ..., n}`. -/
theorem HasFPowerSeriesOnBall.iteratedFDeriv_eq_sum
    (h : HasFPowerSeriesOnBall f p x r) (h' : AnalyticOn ğ•œ f univ) {n : â„•} (v : Fin n â†’ E) :
    iteratedFDeriv ğ•œ n f x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i)) := by
  simp only [â† iteratedFDerivWithin_univ, â† hasFPowerSeriesWithinOnBall_univ] at h âŠ¢
  exact h.iteratedFDerivWithin_eq_sum h' uniqueDiffOn_univ (mem_univ x) v

/-- If a function has a power series in a ball, then its `n`-th iterated derivative is given by
`(vâ‚, ..., vâ‚™) â†¦ âˆ‘ pâ‚™ (v_{Ïƒ (1)}, ..., v_{Ïƒ (n)})` where the sum is over all
permutations of `{1, ..., n}`. -/
theorem HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_sum_of_completeSpace [CompleteSpace F]
    (h : HasFPowerSeriesWithinOnBall f p s x r)
    (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (v : Fin n â†’ E) :
    iteratedFDerivWithin ğ•œ n f s x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i)) := by
  have : iteratedFDerivWithin ğ•œ n f s x
      = iteratedFDerivWithin ğ•œ n f (s âˆ© EMetric.ball x r) x :=
    (iteratedFDerivWithin_inter_open EMetric.isOpen_ball (EMetric.mem_ball_self h.r_pos)).symm
  rw [this]
  apply HasFPowerSeriesWithinOnBall.iteratedFDerivWithin_eq_sum_of_subset
  Â· exact h.mono inter_subset_left
  Â· apply h.analyticOn.mono
    rw [insert_eq_of_mem hx]
  Â· exact hs.inter EMetric.isOpen_ball
  Â· exact âŸ¨hx, EMetric.mem_ball_self h.r_posâŸ©
  Â· exact inter_subset_right

/-- If a function has a power series in a ball, then its `n`-th iterated derivative is given by
`(vâ‚, ..., vâ‚™) â†¦ âˆ‘ pâ‚™ (v_{Ïƒ (1)}, ..., v_{Ïƒ (n)})` where the sum is over all
permutations of `{1, ..., n}`. -/
theorem HasFPowerSeriesOnBall.iteratedFDeriv_eq_sum_of_completeSpace [CompleteSpace F]
    (h : HasFPowerSeriesOnBall f p x r) {n : â„•} (v : Fin n â†’ E) :
    iteratedFDeriv ğ•œ n f x v = âˆ‘ Ïƒ : Perm (Fin n), p n (fun i â†¦ v (Ïƒ i)) := by
  simp only [â† iteratedFDerivWithin_univ, â† hasFPowerSeriesWithinOnBall_univ] at h âŠ¢
  exact h.iteratedFDerivWithin_eq_sum_of_completeSpace uniqueDiffOn_univ (mem_univ _) v

/-- The `n`-th iterated derivative of an analytic function on a set is symmetric. -/
theorem AnalyticOn.iteratedFDerivWithin_comp_perm
    (h : AnalyticOn ğ•œ f s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (v : Fin n â†’ E)
    (Ïƒ : Perm (Fin n)) :
    iteratedFDerivWithin ğ•œ n f s x (v âˆ˜ Ïƒ) = iteratedFDerivWithin ğ•œ n f s x v := by
  rcases h x hx with âŸ¨p, r, hpâŸ©
  rw [hp.iteratedFDerivWithin_eq_sum h hs hx, hp.iteratedFDerivWithin_eq_sum h hs hx]
  conv_rhs => rw [â† Equiv.sum_comp (Equiv.mulLeft Ïƒ)]
  simp only [coe_mulLeft, Perm.coe_mul, Function.comp_apply]

theorem AnalyticOn.domDomCongr_iteratedFDerivWithin
    (h : AnalyticOn ğ•œ f s) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (Ïƒ : Perm (Fin n)) :
    (iteratedFDerivWithin ğ•œ n f s x).domDomCongr Ïƒ = iteratedFDerivWithin ğ•œ n f s x := by
  ext
  exact h.iteratedFDerivWithin_comp_perm hs hx _ _

/-- The `n`-th iterated derivative of an analytic function on a set is symmetric. -/
theorem ContDiffWithinAt.iteratedFDerivWithin_comp_perm
    (h : ContDiffWithinAt ğ•œ Ï‰ f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•} (v : Fin n â†’ E)
    (Ïƒ : Perm (Fin n)) :
    iteratedFDerivWithin ğ•œ n f s x (v âˆ˜ Ïƒ) = iteratedFDerivWithin ğ•œ n f s x v := by
  rcases h.contDiffOn' le_rfl (by simp) with âŸ¨u, u_open, xu, huâŸ©
  rw [insert_eq_of_mem hx] at hu
  have : iteratedFDerivWithin ğ•œ n f (s âˆ© u) x = iteratedFDerivWithin ğ•œ n f s x :=
    iteratedFDerivWithin_inter_open u_open xu
  rw [â† this]
  exact AnalyticOn.iteratedFDerivWithin_comp_perm hu.analyticOn (hs.inter u_open) âŸ¨hx, xuâŸ© _ _

theorem ContDiffWithinAt.domDomCongr_iteratedFDerivWithin
    (h : ContDiffWithinAt ğ•œ Ï‰ f s x) (hs : UniqueDiffOn ğ•œ s) (hx : x âˆˆ s) {n : â„•}
    (Ïƒ : Perm (Fin n)) :
    (iteratedFDerivWithin ğ•œ n f s x).domDomCongr Ïƒ = iteratedFDerivWithin ğ•œ n f s x := by
  ext
  exact h.iteratedFDerivWithin_comp_perm hs hx _ _

/-- The `n`-th iterated derivative of an analytic function is symmetric. -/
theorem AnalyticOn.iteratedFDeriv_comp_perm
    (h : AnalyticOn ğ•œ f univ) {n : â„•} (v : Fin n â†’ E) (Ïƒ : Perm (Fin n)) :
    iteratedFDeriv ğ•œ n f x (v âˆ˜ Ïƒ) = iteratedFDeriv ğ•œ n f x v := by
  rw [â† iteratedFDerivWithin_univ]
  exact h.iteratedFDerivWithin_comp_perm uniqueDiffOn_univ (mem_univ x) _ _

theorem AnalyticOn.domDomCongr_iteratedFDeriv (h : AnalyticOn ğ•œ f univ) {n : â„•} (Ïƒ : Perm (Fin n)) :
    (iteratedFDeriv ğ•œ n f x).domDomCongr Ïƒ = iteratedFDeriv ğ•œ n f x := by
  rw [â† iteratedFDerivWithin_univ]
  exact h.domDomCongr_iteratedFDerivWithin uniqueDiffOn_univ (mem_univ x) _

/-- The `n`-th iterated derivative of an analytic function is symmetric. -/
theorem ContDiffAt.iteratedFDeriv_comp_perm
    (h : ContDiffAt ğ•œ Ï‰ f x) {n : â„•} (v : Fin n â†’ E) (Ïƒ : Perm (Fin n)) :
    iteratedFDeriv ğ•œ n f x (v âˆ˜ Ïƒ) = iteratedFDeriv ğ•œ n f x v := by
  rw [â† iteratedFDerivWithin_univ]
  exact h.iteratedFDerivWithin_comp_perm uniqueDiffOn_univ (mem_univ x) _ _

theorem ContDiffAt.domDomCongr_iteratedFDeriv (h : ContDiffAt ğ•œ Ï‰ f x) {n : â„•} (Ïƒ : Perm (Fin n)) :
    (iteratedFDeriv ğ•œ n f x).domDomCongr Ïƒ = iteratedFDeriv ğ•œ n f x := by
  rw [â† iteratedFDerivWithin_univ]
  exact h.domDomCongr_iteratedFDerivWithin uniqueDiffOn_univ (mem_univ x) _
