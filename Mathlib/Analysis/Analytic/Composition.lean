/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Johan Commelin
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.CPolynomialDef
import Mathlib.Combinatorics.Enumerative.Composition

/-!
# Composition of analytic functions

In this file we prove that the composition of analytic functions is analytic.

The argument is the following. Assume `g z = âˆ‘' qâ‚™ (z, ..., z)` and `f y = âˆ‘' pâ‚– (y, ..., y)`. Then

`g (f y) = âˆ‘' qâ‚™ (âˆ‘' pâ‚– (y, ..., y), ..., âˆ‘' pâ‚– (y, ..., y))
= âˆ‘' qâ‚™ (p_{iâ‚} (y, ..., y), ..., p_{iâ‚™} (y, ..., y))`.

For each `n` and `iâ‚, ..., iâ‚™`, define a `iâ‚ + ... + iâ‚™` multilinear function mapping
`(yâ‚€, ..., y_{iâ‚ + ... + iâ‚™ - 1})` to
`qâ‚™ (p_{iâ‚} (yâ‚€, ..., y_{iâ‚-1}), p_{iâ‚‚} (y_{iâ‚}, ..., y_{iâ‚ + iâ‚‚ - 1}), ..., p_{iâ‚™} (....)))`.
Then `g âˆ˜ f` is obtained by summing all these multilinear functions.

To formalize this, we use compositions of an integer `N`, i.e., its decompositions into
a sum `iâ‚ + ... + iâ‚™` of positive integers. Given such a composition `c` and two formal
multilinear series `q` and `p`, let `q.compAlongComposition p c` be the above multilinear
function. Then the `N`-th coefficient in the power series expansion of `g âˆ˜ f` is the sum of these
terms over all `c : Composition N`.

To complete the proof, we need to show that this power series has a positive radius of convergence.
This follows from the fact that `Composition N` has cardinality `2^(N-1)` and estimates on
the norm of `qâ‚™` and `pâ‚–`, which give summability. We also need to show that it indeed converges to
`g âˆ˜ f`. For this, we note that the composition of partial sums converges to `g âˆ˜ f`, and that it
corresponds to a part of the whole sum, on a subset that increases to the whole space. By
summability of the norms, this implies the overall convergence.

## Main results

* `q.comp p` is the formal composition of the formal multilinear series `q` and `p`.
* `HasFPowerSeriesAt.comp` states that if two functions `g` and `f` admit power series expansions
  `q` and `p`, then `g âˆ˜ f` admits a power series expansion given by `q.comp p`.
* `AnalyticAt.comp` states that the composition of analytic functions is analytic.
* `FormalMultilinearSeries.comp_assoc` states that composition is associative on formal
  multilinear series.

## Implementation details

The main technical difficulty is to write down things. In particular, we need to define precisely
`q.compAlongComposition p c` and to show that it is indeed a continuous multilinear
function. This requires a whole interface built on the class `Composition`. Once this is set,
the main difficulty is to reorder the sums, writing the composition of the partial sums as a sum
over some subset of `Î£ n, Composition n`. We need to check that the reordering is a bijection,
running over difficulties due to the dependent nature of the types under consideration, that are
controlled thanks to the interface for `Composition`.

The associativity of composition on formal multilinear series is a nontrivial result: it does not
follow from the associativity of composition of analytic functions, as there is no uniqueness for
the formal multilinear series representing a function (and also, it holds even when the radius of
convergence of the series is `0`). Instead, we give a direct proof, which amounts to reordering
double sums in a careful way. The change of variables is a canonical (combinatorial) bijection
`Composition.sigmaEquivSigmaPi` between `(Î£ (a : Composition n), Composition a.length)` and
`(Î£ (c : Composition n), Î  (i : Fin c.length), Composition (c.blocksFun i))`, and is described
in more details below in the paragraph on associativity.
-/


noncomputable section

variable {ğ•œ : Type*} {E F G H : Type*}

open Filter List

open scoped Topology NNReal ENNReal

section Topological

variable [CommRing ğ•œ] [AddCommGroup E] [AddCommGroup F] [AddCommGroup G]
variable [Module ğ•œ E] [Module ğ•œ F] [Module ğ•œ G]
variable [TopologicalSpace E] [TopologicalSpace F] [TopologicalSpace G]

/-! ### Composing formal multilinear series -/


namespace FormalMultilinearSeries

variable [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]
variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
variable [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œ G]

/-!
In this paragraph, we define the composition of formal multilinear series, by summing over all
possible compositions of `n`.
-/


/-- Given a formal multilinear series `p`, a composition `c` of `n` and the index `i` of a
block of `c`, we may define a function on `Fin n â†’ E` by picking the variables in the `i`-th block
of `n`, and applying the corresponding coefficient of `p` to these variables. This function is
called `p.applyComposition c v i` for `v : Fin n â†’ E` and `i : Fin c.length`. -/
def applyComposition (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (c : Composition n) :
    (Fin n â†’ E) â†’ Fin c.length â†’ F := fun v i => p (c.blocksFun i) (v âˆ˜ c.embedding i)

theorem applyComposition_ones (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) :
    p.applyComposition (Composition.ones n) = fun v i =>
      p 1 fun _ => v (Fin.castLE (Composition.length_le _) i) := by
  funext v i
  apply p.congr (Composition.ones_blocksFun _ _)
  intro j hjn hj1
  obtain rfl : j = 0 := by cutsat
  refine congr_arg v ?_
  rw [Fin.ext_iff, Fin.coe_castLE, Composition.ones_embedding, Fin.val_mk]

theorem applyComposition_single (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (hn : 0 < n)
    (v : Fin n â†’ E) : p.applyComposition (Composition.single n hn) v = fun _j => p n v := by
  ext j
  refine p.congr (by simp) fun i hi1 hi2 => ?_
  dsimp
  congr 1
  convert Composition.single_embedding hn âŸ¨i, hi2âŸ© using 1
  obtain âŸ¨j_val, j_propertyâŸ© := j
  have : j_val = 0 := le_bot_iff.1 (Nat.lt_succ_iff.1 j_property)
  congr!
  simp

@[simp]
theorem removeZero_applyComposition (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (c : Composition n) : p.removeZero.applyComposition c = p.applyComposition c := by
  ext v i
  simp [applyComposition, zero_lt_one.trans_le (c.one_le_blocksFun i), removeZero_of_pos]

/-- Technical lemma stating how `p.applyComposition` commutes with updating variables. This
will be the key point to show that functions constructed from `applyComposition` retain
multilinearity. -/
theorem applyComposition_update (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (c : Composition n)
    (j : Fin n) (v : Fin n â†’ E) (z : E) :
    p.applyComposition c (Function.update v j z) =
      Function.update (p.applyComposition c v) (c.index j)
        (p (c.blocksFun (c.index j))
          (Function.update (v âˆ˜ c.embedding (c.index j)) (c.invEmbedding j) z)) := by
  ext k
  by_cases h : k = c.index j
  Â· rw [h]
    let r : Fin (c.blocksFun (c.index j)) â†’ Fin n := c.embedding (c.index j)
    simp only [Function.update_self]
    change p (c.blocksFun (c.index j)) (Function.update v j z âˆ˜ r) = _
    let j' := c.invEmbedding j
    suffices B : Function.update v j z âˆ˜ r = Function.update (v âˆ˜ r) j' z by rw [B]
    suffices C : Function.update v (r j') z âˆ˜ r = Function.update (v âˆ˜ r) j' z by
      convert C; exact (c.embedding_comp_inv j).symm
    exact Function.update_comp_eq_of_injective _ (c.embedding _).injective _ _
  Â· simp only [h, Function.update_of_ne, Ne, not_false_iff]
    let r : Fin (c.blocksFun k) â†’ Fin n := c.embedding k
    change p (c.blocksFun k) (Function.update v j z âˆ˜ r) = p (c.blocksFun k) (v âˆ˜ r)
    suffices B : Function.update v j z âˆ˜ r = v âˆ˜ r by rw [B]
    apply Function.update_comp_eq_of_notMem_range
    rwa [c.mem_range_embedding_iff']

@[simp]
theorem compContinuousLinearMap_applyComposition {n : â„•} (p : FormalMultilinearSeries ğ•œ F G)
    (f : E â†’L[ğ•œ] F) (c : Composition n) (v : Fin n â†’ E) :
    (p.compContinuousLinearMap f).applyComposition c v = p.applyComposition c (f âˆ˜ v) := by
  ext
  simp [applyComposition, Function.comp_def]

end FormalMultilinearSeries

namespace ContinuousMultilinearMap

open FormalMultilinearSeries

variable [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]
variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]

/-- Given a formal multilinear series `p`, a composition `c` of `n` and a continuous multilinear
map `f` in `c.length` variables, one may form a continuous multilinear map in `n` variables by
applying the right coefficient of `p` to each block of the composition, and then applying `f` to
the resulting vector. It is called `f.compAlongComposition p c`. -/
def compAlongComposition {n : â„•} (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n)
    (f : F [Ã—c.length]â†’L[ğ•œ] G) : E [Ã—n]â†’L[ğ•œ] G where
  toMultilinearMap :=
    MultilinearMap.mk' (fun v â†¦ f (p.applyComposition c v))
      (fun v i x y â†¦ by simp only [applyComposition_update, map_update_add])
      (fun v i c x â†¦ by simp only [applyComposition_update, map_update_smul])
  cont :=
    f.cont.comp <|
      continuous_pi fun _ => (coe_continuous _).comp <| continuous_pi fun _ => continuous_apply _

@[simp]
theorem compAlongComposition_apply {n : â„•} (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n)
    (f : F [Ã—c.length]â†’L[ğ•œ] G) (v : Fin n â†’ E) :
    (f.compAlongComposition p c) v = f (p.applyComposition c v) :=
  rfl

end ContinuousMultilinearMap

namespace FormalMultilinearSeries

variable [IsTopologicalAddGroup E] [ContinuousConstSMul ğ•œ E]
variable [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
variable [IsTopologicalAddGroup G] [ContinuousConstSMul ğ•œ G]

/-- Given two formal multilinear series `q` and `p` and a composition `c` of `n`, one may
form a continuous multilinear map in `n` variables by applying the right coefficient of `p` to each
block of the composition, and then applying `q c.length` to the resulting vector. It is
called `q.compAlongComposition p c`. -/
def compAlongComposition {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n) : (E [Ã—n]â†’L[ğ•œ] G) :=
  (q c.length).compAlongComposition p c

@[simp]
theorem compAlongComposition_apply {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n) (v : Fin n â†’ E) :
    (q.compAlongComposition p c) v = q c.length (p.applyComposition c v) :=
  rfl

/-- Formal composition of two formal multilinear series. The `n`-th coefficient in the composition
is defined to be the sum of `q.compAlongComposition p c` over all compositions of
`n`. In other words, this term (as a multilinear function applied to `v_0, ..., v_{n-1}`) is
`âˆ‘'_{k} âˆ‘'_{iâ‚ + ... + iâ‚– = n} qâ‚– (p_{i_1} (...), ..., p_{i_k} (...))`, where one puts all variables
`v_0, ..., v_{n-1}` in increasing order in the dots.

In general, the composition `q âˆ˜ p` only makes sense when the constant coefficient of `p` vanishes.
We give a general formula but which ignores the value of `p 0` instead.
-/
protected def comp (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F) :
    FormalMultilinearSeries ğ•œ E G := fun n => âˆ‘ c : Composition n, q.compAlongComposition p c

/-- The `0`-th coefficient of `q.comp p` is `q 0`. Since these maps are multilinear maps in zero
variables, but on different spaces, we cannot state this directly, so we state it when applied to
arbitrary vectors (which have to be the zero vector). -/
theorem comp_coeff_zero (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F)
    (v : Fin 0 â†’ E) (v' : Fin 0 â†’ F) : (q.comp p) 0 v = q 0 v' := by
  let c : Composition 0 := Composition.ones 0
  dsimp [FormalMultilinearSeries.comp]
  have : {c} = (Finset.univ : Finset (Composition 0)) := by
    apply Finset.eq_of_subset_of_card_le <;> simp [Finset.card_univ, composition_card 0]
  rw [â† this, Finset.sum_singleton, compAlongComposition_apply]
  symm; congr!

@[simp]
theorem comp_coeff_zero' (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F)
    (v : Fin 0 â†’ E) : (q.comp p) 0 v = q 0 fun _i => 0 :=
  q.comp_coeff_zero p v _

/-- The `0`-th coefficient of `q.comp p` is `q 0`. When `p` goes from `E` to `E`, this can be
expressed as a direct equality -/
theorem comp_coeff_zero'' (q : FormalMultilinearSeries ğ•œ E F) (p : FormalMultilinearSeries ğ•œ E E) :
    (q.comp p) 0 = q 0 := by ext v; exact q.comp_coeff_zero p _ _

/-- The first coefficient of a composition of formal multilinear series is the composition of the
first coefficients seen as continuous linear maps. -/
theorem comp_coeff_one (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F)
    (v : Fin 1 â†’ E) : (q.comp p) 1 v = q 1 fun _i => p 1 v := by
  have : {Composition.ones 1} = (Finset.univ : Finset (Composition 1)) :=
    Finset.eq_univ_of_card _ (by simp [composition_card])
  simp only [FormalMultilinearSeries.comp, compAlongComposition_apply, â† this,
    Finset.sum_singleton]
  refine q.congr (by simp) fun i hi1 hi2 => ?_
  simp only [applyComposition_ones]
  exact p.congr rfl fun j _hj1 hj2 => by congr!

/-- Only `0`-th coefficient of `q.comp p` depends on `q 0`. -/
theorem removeZero_comp_of_pos (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (hn : 0 < n) :
    q.removeZero.comp p n = q.comp p n := by
  ext v
  simp only [FormalMultilinearSeries.comp, compAlongComposition,
    ContinuousMultilinearMap.compAlongComposition_apply, ContinuousMultilinearMap.sum_apply]
  refine Finset.sum_congr rfl fun c _hc => ?_
  rw [removeZero_of_pos _ (c.length_pos_of_pos hn)]

@[simp]
theorem comp_removeZero (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F) :
    q.comp p.removeZero = q.comp p := by ext n; simp [FormalMultilinearSeries.comp]

end FormalMultilinearSeries

end Topological

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G] [NormedAddCommGroup H]
  [NormedSpace ğ•œ H]

namespace FormalMultilinearSeries

/-- The norm of `f.compAlongComposition p c` is controlled by the product of
the norms of the relevant bits of `f` and `p`. -/
theorem compAlongComposition_bound {n : â„•} (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n)
    (f : F [Ã—c.length]â†’L[ğ•œ] G) (v : Fin n â†’ E) :
    â€–f.compAlongComposition p c vâ€– â‰¤ (â€–fâ€– * âˆ i, â€–p (c.blocksFun i)â€–) * âˆ i : Fin n, â€–v iâ€– :=
  calc
    â€–f.compAlongComposition p c vâ€– = â€–f (p.applyComposition c v)â€– := rfl
    _ â‰¤ â€–fâ€– * âˆ i, â€–p.applyComposition c v iâ€– := ContinuousMultilinearMap.le_opNorm _ _
    _ â‰¤ â€–fâ€– * âˆ i, â€–p (c.blocksFun i)â€– * âˆ j : Fin (c.blocksFun i), â€–(v âˆ˜ c.embedding i) jâ€– := by
      gcongr with i
      apply ContinuousMultilinearMap.le_opNorm
    _ = (â€–fâ€– * âˆ i, â€–p (c.blocksFun i)â€–) *
        âˆ i, âˆ j : Fin (c.blocksFun i), â€–(v âˆ˜ c.embedding i) jâ€– := by
      rw [Finset.prod_mul_distrib, mul_assoc]
    _ = (â€–fâ€– * âˆ i, â€–p (c.blocksFun i)â€–) * âˆ i : Fin n, â€–v iâ€– := by
      rw [â† c.blocksFinEquiv.prod_comp, â† Finset.univ_sigma_univ, Finset.prod_sigma]
      congr

/-- The norm of `q.compAlongComposition p c` is controlled by the product of
the norms of the relevant bits of `q` and `p`. -/
theorem compAlongComposition_norm {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n) :
    â€–q.compAlongComposition p câ€– â‰¤ â€–q c.lengthâ€– * âˆ i, â€–p (c.blocksFun i)â€– :=
  ContinuousMultilinearMap.opNorm_le_bound (by positivity) (compAlongComposition_bound _ _ _)

theorem compAlongComposition_nnnorm {n : â„•} (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (c : Composition n) :
    â€–q.compAlongComposition p câ€–â‚Š â‰¤ â€–q c.lengthâ€–â‚Š * âˆ i, â€–p (c.blocksFun i)â€–â‚Š := by
  rw [â† NNReal.coe_le_coe]; push_cast; exact q.compAlongComposition_norm p c

/-!
### The identity formal power series

We will now define the identity power series, and show that it is a neutral element for left and
right composition.
-/


section

variable (ğ•œ E)

/-- The identity formal multilinear series, with all coefficients equal to `0` except for `n = 1`
where it is (the continuous multilinear version of) the identity. We allow an arbitrary
constant coefficient `x`. -/
def id (x : E) : FormalMultilinearSeries ğ•œ E E
  | 0 => ContinuousMultilinearMap.uncurry0 ğ•œ _ x
  | 1 => (continuousMultilinearCurryFin1 ğ•œ E E).symm (ContinuousLinearMap.id ğ•œ E)
  | _ => 0

@[simp] theorem id_apply_zero (x : E) (v : Fin 0 â†’ E) :
    (FormalMultilinearSeries.id ğ•œ E x) 0 v = x := rfl

/-- The first coefficient of `id ğ•œ E` is the identity. -/
@[simp]
theorem id_apply_one (x : E) (v : Fin 1 â†’ E) : (FormalMultilinearSeries.id ğ•œ E x) 1 v = v 0 :=
  rfl

/-- The `n`th coefficient of `id ğ•œ E` is the identity when `n = 1`. We state this in a dependent
way, as it will often appear in this form. -/
theorem id_apply_one' (x : E) {n : â„•} (h : n = 1) (v : Fin n â†’ E) :
    (id ğ•œ E x) n v = v âŸ¨0, h.symm â–¸ zero_lt_oneâŸ© := by
  subst n
  apply id_apply_one

/-- For `n â‰  1`, the `n`-th coefficient of `id ğ•œ E` is zero, by definition. -/
@[simp]
theorem id_apply_of_one_lt (x : E) {n : â„•} (h : 1 < n) :
    (FormalMultilinearSeries.id ğ•œ E x) n = 0 := by
  match n with
    | 0 => contradiction
    | 1 => contradiction
    | n + 2 => rfl

end

@[simp]
theorem comp_id (p : FormalMultilinearSeries ğ•œ E F) (x : E) : p.comp (id ğ•œ E x) = p := by
  ext1 n
  dsimp [FormalMultilinearSeries.comp]
  rw [Finset.sum_eq_single (Composition.ones n)]
  Â· show compAlongComposition p (id ğ•œ E x) (Composition.ones n) = p n
    ext v
    rw [compAlongComposition_apply]
    apply p.congr (Composition.ones_length n)
    intros
    rw [applyComposition_ones]
    refine congr_arg v ?_
    rw [Fin.ext_iff, Fin.coe_castLE, Fin.val_mk]
  Â· change
    âˆ€ b : Composition n,
      b âˆˆ Finset.univ â†’ b â‰  Composition.ones n â†’ compAlongComposition p (id ğ•œ E x) b = 0
    intro b _ hb
    obtain âŸ¨k, hk, lt_kâŸ© : âˆƒ (k : â„•), k âˆˆ Composition.blocks b âˆ§ 1 < k :=
      Composition.ne_ones_iff.1 hb
    obtain âŸ¨i, hiâŸ© : âˆƒ (i : Fin b.blocks.length), b.blocks[i] = k :=
      List.get_of_mem hk
    let j : Fin b.length := âŸ¨i.val, b.blocks_length â–¸ i.propâŸ©
    have A : 1 < b.blocksFun j := by convert lt_k
    ext v
    rw [compAlongComposition_apply, ContinuousMultilinearMap.zero_apply]
    apply ContinuousMultilinearMap.map_coord_zero _ j
    dsimp [applyComposition]
    rw [id_apply_of_one_lt _ _ _ A, ContinuousMultilinearMap.zero_apply]
  Â· simp

@[simp]
theorem id_comp (p : FormalMultilinearSeries ğ•œ E F) (v0 : Fin 0 â†’ E) :
    (id ğ•œ F (p 0 v0)).comp p = p := by
  ext1 n
  obtain rfl | n_pos := n.eq_zero_or_pos
  Â· ext v
    simp only [comp_coeff_zero', id_apply_zero]
    congr with i
    exact i.elim0
  Â· dsimp [FormalMultilinearSeries.comp]
    rw [Finset.sum_eq_single (Composition.single n n_pos)]
    Â· show compAlongComposition (id ğ•œ F (p 0 v0)) p (Composition.single n n_pos) = p n
      ext v
      rw [compAlongComposition_apply, id_apply_one' _ _ _ (Composition.single_length n_pos)]
      dsimp [applyComposition]
      refine p.congr rfl fun i him hin => congr_arg v <| ?_
      ext; simp
    Â· change
      âˆ€ b : Composition n, b âˆˆ Finset.univ â†’ b â‰  Composition.single n n_pos â†’
        compAlongComposition (id ğ•œ F (p 0 v0)) p b = 0
      intro b _ hb
      have A : 1 < b.length := by
        have : b.length â‰  1 := by simpa [Composition.eq_single_iff_length] using hb
        have : 0 < b.length := Composition.length_pos_of_pos b n_pos
        omega
      ext v
      rw [compAlongComposition_apply, id_apply_of_one_lt _ _ _ A,
        ContinuousMultilinearMap.zero_apply, ContinuousMultilinearMap.zero_apply]
    Â· simp

/-- Variant of `id_comp` in which the zero coefficient is given by an equality hypothesis instead
of a definitional equality. Useful for rewriting or simplifying out in some situations. -/
theorem id_comp' (p : FormalMultilinearSeries ğ•œ E F) (x : F) (v0 : Fin 0 â†’ E) (h : x = p 0 v0) :
    (id ğ•œ F x).comp p = p := by
  simp [h]

/-! ### Summability properties of the composition of formal power series -/


section

/-- If two formal multilinear series have positive radius of convergence, then the terms appearing
in the definition of their composition are also summable (when multiplied by a suitable positive
geometric term). -/
theorem comp_summable_nnreal (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F)
    (hq : 0 < q.radius) (hp : 0 < p.radius) :
    âˆƒ r > (0 : â„â‰¥0),
      Summable fun i : Î£ n, Composition n => â€–q.compAlongComposition p i.2â€–â‚Š * r ^ i.1 := by
  /- This follows from the fact that the growth rate of `â€–qâ‚™â€–` and `â€–pâ‚™â€–` is at most geometric,
    giving a geometric bound on each `â€–q.compAlongComposition p opâ€–`, together with the
    fact that there are `2^(n-1)` compositions of `n`, giving at most a geometric loss. -/
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 (lt_min zero_lt_one hq) with âŸ¨rq, rq_pos, hrqâŸ©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 (lt_min zero_lt_one hp) with âŸ¨rp, rp_pos, hrpâŸ©
  simp only [lt_min_iff, ENNReal.coe_lt_one_iff, ENNReal.coe_pos] at hrp hrq rp_pos rq_pos
  obtain âŸ¨Cq, _hCq0, hCqâŸ© : âˆƒ Cq > 0, âˆ€ n, â€–q nâ€–â‚Š * rq ^ n â‰¤ Cq :=
    q.nnnorm_mul_pow_le_of_lt_radius hrq.2
  obtain âŸ¨Cp, hCp1, hCpâŸ© : âˆƒ Cp â‰¥ 1, âˆ€ n, â€–p nâ€–â‚Š * rp ^ n â‰¤ Cp := by
    rcases p.nnnorm_mul_pow_le_of_lt_radius hrp.2 with âŸ¨Cp, -, hCpâŸ©
    exact âŸ¨max Cp 1, le_max_right _ _, fun n => (hCp n).trans (le_max_left _ _)âŸ©
  let r0 : â„â‰¥0 := (4 * Cp)â»Â¹
  have r0_pos : 0 < r0 := inv_pos.2 (mul_pos zero_lt_four (zero_lt_one.trans_le hCp1))
  set r : â„â‰¥0 := rp * rq * r0
  have r_pos : 0 < r := mul_pos (mul_pos rp_pos rq_pos) r0_pos
  have I :
    âˆ€ i : Î£ n : â„•, Composition n, â€–q.compAlongComposition p i.2â€–â‚Š * r ^ i.1 â‰¤ Cq / 4 ^ i.1 := by
    rintro âŸ¨n, câŸ©
    have A := calc
      â€–q c.lengthâ€–â‚Š * rq ^ n â‰¤ â€–q c.lengthâ€–â‚Š * rq ^ c.length :=
        mul_le_mul' le_rfl (pow_le_pow_of_le_one rq.2 hrq.1.le c.length_le)
      _ â‰¤ Cq := hCq _
    have B := calc
      (âˆ i, â€–p (c.blocksFun i)â€–â‚Š) * rp ^ n = âˆ i, â€–p (c.blocksFun i)â€–â‚Š * rp ^ c.blocksFun i := by
        simp only [Finset.prod_mul_distrib, Finset.prod_pow_eq_pow_sum, c.sum_blocksFun]
      _ â‰¤ âˆ _i : Fin c.length, Cp := Finset.prod_le_prod' fun i _ => hCp _
      _ = Cp ^ c.length := by simp
      _ â‰¤ Cp ^ n := pow_right_monoâ‚€ hCp1 c.length_le
    calc
      â€–q.compAlongComposition p câ€–â‚Š * r ^ n â‰¤
          (â€–q c.lengthâ€–â‚Š * âˆ i, â€–p (c.blocksFun i)â€–â‚Š) * r ^ n := by
        grw [q.compAlongComposition_nnnorm p c]
      _ = â€–q c.lengthâ€–â‚Š * rq ^ n * ((âˆ i, â€–p (c.blocksFun i)â€–â‚Š) * rp ^ n) * r0 ^ n := by
        ring
      _ â‰¤ Cq * Cp ^ n * r0 ^ n := mul_le_mul' (mul_le_mul' A B) le_rfl
      _ = Cq / 4 ^ n := by
        simp only [r0]
        simp [field, mul_pow]
  refine âŸ¨r, r_pos, NNReal.summable_of_le I ?_âŸ©
  simp_rw [div_eq_mul_inv]
  refine Summable.mul_left _ ?_
  have : âˆ€ n : â„•, HasSum (fun c : Composition n => (4 ^ n : â„â‰¥0)â»Â¹) (2 ^ (n - 1) / 4 ^ n) := by
    intro n
    convert hasSum_fintype fun c : Composition n => (4 ^ n : â„â‰¥0)â»Â¹
    simp [Finset.card_univ, composition_card, div_eq_mul_inv]
  refine NNReal.summable_sigma.2 âŸ¨fun n => (this n).summable, (NNReal.summable_nat_add_iff 1).1 ?_âŸ©
  convert (NNReal.summable_geometric (NNReal.div_lt_one_of_lt one_lt_two)).mul_left (1 / 4) using 1
  ext1 n
  rw [(this _).tsum_eq, add_tsub_cancel_right]
  simp [field, pow_succ, mul_pow, show (4 : â„â‰¥0) = 2 * 2 by norm_num]

end

/-- Bounding below the radius of the composition of two formal multilinear series assuming
summability over all compositions. -/
theorem le_comp_radius_of_summable (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) (r : â„â‰¥0)
    (hr : Summable fun i : Î£ n, Composition n => â€–q.compAlongComposition p i.2â€–â‚Š * r ^ i.1) :
    (r : â„â‰¥0âˆ) â‰¤ (q.comp p).radius := by
  refine
    le_radius_of_bound_nnreal _
      (âˆ‘' i : Î£ n, Composition n, â€–compAlongComposition q p i.sndâ€–â‚Š * r ^ i.fst) fun n => ?_
  calc
    â€–FormalMultilinearSeries.comp q p nâ€–â‚Š * r ^ n â‰¤
        âˆ‘' c : Composition n, â€–compAlongComposition q p câ€–â‚Š * r ^ n := by
      rw [tsum_fintype, â† Finset.sum_mul]
      exact mul_le_mul' (nnnorm_sum_le _ _) le_rfl
    _ â‰¤ âˆ‘' i : Î£ n : â„•, Composition n, â€–compAlongComposition q p i.sndâ€–â‚Š * r ^ i.fst :=
      NNReal.tsum_comp_le_tsum_of_inj hr sigma_mk_injective

/-!
### Composing analytic functions

Now, we will prove that the composition of the partial sums of `q` and `p` up to order `N` is
given by a sum over some large subset of `Î£ n, Composition n` of `q.compAlongComposition p`, to
deduce that the series for `q.comp p` indeed converges to `g âˆ˜ f` when `q` is a power series for
`g` and `p` is a power series for `f`.

This proof is a big reindexing argument of a sum. Since it is a bit involved, we define first
the source of the change of variables (`compPartialSumSource`), its target
(`compPartialSumTarget`) and the change of variables itself (`compChangeOfVariables`) before
giving the main statement in `comp_partialSum`. -/


/-- Source set in the change of variables to compute the composition of partial sums of formal
power series.
See also `comp_partialSum`. -/
def compPartialSumSource (m M N : â„•) : Finset (Î£ n, Fin n â†’ â„•) :=
  Finset.sigma (Finset.Ico m M) (fun n : â„• => Fintype.piFinset fun _i : Fin n => Finset.Ico 1 N :)

@[simp]
theorem mem_compPartialSumSource_iff (m M N : â„•) (i : Î£ n, Fin n â†’ â„•) :
    i âˆˆ compPartialSumSource m M N â†”
      (m â‰¤ i.1 âˆ§ i.1 < M) âˆ§ âˆ€ a : Fin i.1, 1 â‰¤ i.2 a âˆ§ i.2 a < N := by
  simp only [compPartialSumSource, Finset.mem_Ico, Fintype.mem_piFinset, Finset.mem_sigma]

/-- Change of variables appearing to compute the composition of partial sums of formal
power series -/
def compChangeOfVariables (m M N : â„•) (i : Î£ n, Fin n â†’ â„•) (hi : i âˆˆ compPartialSumSource m M N) :
    Î£ n, Composition n := by
  rcases i with âŸ¨n, fâŸ©
  rw [mem_compPartialSumSource_iff] at hi
  refine âŸ¨âˆ‘ j, f j, ofFn fun a => f a, fun {i} hi' => ?_, by simp [sum_ofFn]âŸ©
  obtain âŸ¨j, rflâŸ© : âˆƒ j : Fin n, f j = i := by rwa [mem_ofFn', Set.mem_range] at hi'
  exact (hi.2 j).1

@[simp]
theorem compChangeOfVariables_length (m M N : â„•) {i : Î£ n, Fin n â†’ â„•}
    (hi : i âˆˆ compPartialSumSource m M N) :
    Composition.length (compChangeOfVariables m M N i hi).2 = i.1 := by
  rcases i with âŸ¨k, blocks_funâŸ©
  dsimp [compChangeOfVariables]
  simp only [Composition.length, length_ofFn]

theorem compChangeOfVariables_blocksFun (m M N : â„•) {i : Î£ n, Fin n â†’ â„•}
    (hi : i âˆˆ compPartialSumSource m M N) (j : Fin i.1) :
    (compChangeOfVariables m M N i hi).2.blocksFun
        âŸ¨j, (compChangeOfVariables_length m M N hi).symm â–¸ j.2âŸ© =
      i.2 j := by
  rcases i with âŸ¨n, fâŸ©
  dsimp [Composition.blocksFun, Composition.blocks, compChangeOfVariables]
  simp only [List.getElem_ofFn]

/-- Target set in the change of variables to compute the composition of partial sums of formal
power series, here given a a set. -/
def compPartialSumTargetSet (m M N : â„•) : Set (Î£ n, Composition n) :=
  {i | m â‰¤ i.2.length âˆ§ i.2.length < M âˆ§ âˆ€ j : Fin i.2.length, i.2.blocksFun j < N}

theorem compPartialSumTargetSet_image_compPartialSumSource (m M N : â„•)
    (i : Î£ n, Composition n) (hi : i âˆˆ compPartialSumTargetSet m M N) :
    âˆƒ (j : _) (hj : j âˆˆ compPartialSumSource m M N), compChangeOfVariables m M N j hj = i := by
  rcases i with âŸ¨n, câŸ©
  refine âŸ¨âŸ¨c.length, c.blocksFunâŸ©, ?_, ?_âŸ©
  Â· simp only [compPartialSumTargetSet, Set.mem_setOf_eq] at hi
    simp only [mem_compPartialSumSource_iff, hi.left, hi.right, true_and, and_true]
    exact fun a => c.one_le_blocks' _
  Â· dsimp [compChangeOfVariables]
    rw [Composition.sigma_eq_iff_blocks_eq]
    simp only [Composition.blocksFun]
    conv_rhs => rw [â† List.ofFn_get c.blocks]

/-- Target set in the change of variables to compute the composition of partial sums of formal
power series, here given a a finset.
See also `comp_partialSum`. -/
def compPartialSumTarget (m M N : â„•) : Finset (Î£ n, Composition n) :=
  Set.Finite.toFinset <|
    ((Finset.finite_toSet _).dependent_image _).subset <|
      compPartialSumTargetSet_image_compPartialSumSource m M N

@[simp]
theorem mem_compPartialSumTarget_iff {m M N : â„•} {a : Î£ n, Composition n} :
    a âˆˆ compPartialSumTarget m M N â†”
      m â‰¤ a.2.length âˆ§ a.2.length < M âˆ§ âˆ€ j : Fin a.2.length, a.2.blocksFun j < N := by
  simp [compPartialSumTarget, compPartialSumTargetSet]

/-- `compChangeOfVariables m M N` is a bijection between `compPartialSumSource m M N`
and `compPartialSumTarget m M N`, yielding equal sums for functions that correspond to each
other under the bijection. As `compChangeOfVariables m M N` is a dependent function, stating
that it is a bijection is not directly possible, but the consequence on sums can be stated
more easily. -/
theorem compChangeOfVariables_sum {Î± : Type*} [AddCommMonoid Î±] (m M N : â„•)
    (f : (Î£ n : â„•, Fin n â†’ â„•) â†’ Î±) (g : (Î£ n, Composition n) â†’ Î±)
    (h : âˆ€ (e) (he : e âˆˆ compPartialSumSource m M N), f e = g (compChangeOfVariables m M N e he)) :
    âˆ‘ e âˆˆ compPartialSumSource m M N, f e = âˆ‘ e âˆˆ compPartialSumTarget m M N, g e := by
  apply Finset.sum_bij (compChangeOfVariables m M N)
  -- We should show that the correspondence we have set up is indeed a bijection
  -- between the index sets of the two sums.
  -- 1 - show that the image belongs to `compPartialSumTarget m N N`
  Â· rintro âŸ¨k, blocks_funâŸ© H
    rw [mem_compPartialSumSource_iff] at H
    simp only [mem_compPartialSumTarget_iff, Composition.length, H.left,
      length_ofFn, true_and, compChangeOfVariables]
    intro j
    simp only [Composition.blocksFun, (H.right _).right, List.get_ofFn]
  -- 2 - show that the map is injective
  Â· rintro âŸ¨k, blocks_funâŸ© H âŸ¨k', blocks_fun'âŸ© H' heq
    obtain rfl : k = k' := by
      have := (compChangeOfVariables_length m M N H).symm
      rwa [heq, compChangeOfVariables_length] at this
    congr
    funext i
    calc
      blocks_fun i = (compChangeOfVariables m M N _ H).2.blocksFun _ :=
        (compChangeOfVariables_blocksFun m M N H i).symm
      _ = (compChangeOfVariables m M N _ H').2.blocksFun _ := by
        grind
      _ = blocks_fun' i := compChangeOfVariables_blocksFun m M N H' i
  -- 3 - show that the map is surjective
  Â· intro i hi
    apply compPartialSumTargetSet_image_compPartialSumSource m M N i
    simpa [compPartialSumTarget] using hi
  -- 4 - show that the composition gives the `compAlongComposition` application
  Â· assumption

/-- The auxiliary set corresponding to the composition of partial sums asymptotically contains
all possible compositions. -/
theorem compPartialSumTarget_tendsto_prod_atTop :
    Tendsto (fun (p : â„• Ã— â„•) => compPartialSumTarget 0 p.1 p.2) atTop atTop := by
  apply Monotone.tendsto_atTop_finset
  Â· intro m n hmn a ha
    have : âˆ€ i, i < m.1 â†’ i < n.1 := fun i hi => lt_of_lt_of_le hi hmn.1
    have : âˆ€ i, i < m.2 â†’ i < n.2 := fun i hi => lt_of_lt_of_le hi hmn.2
    simp_all
  Â· rintro âŸ¨n, câŸ©
    simp only [mem_compPartialSumTarget_iff]
    obtain âŸ¨n, hnâŸ© : BddAbove ((Finset.univ.image fun i : Fin c.length => c.blocksFun i) : Set â„•) :=
      Finset.bddAbove _
    refine
      âŸ¨max n c.length + 1, bot_le, lt_of_le_of_lt (le_max_right n c.length) (lt_add_one _), fun j =>
        lt_of_le_of_lt (le_trans ?_ (le_max_left _ _)) (lt_add_one _)âŸ©
    apply hn
    simp only [Finset.mem_image_of_mem, Finset.mem_coe, Finset.mem_univ]

/-- The auxiliary set corresponding to the composition of partial sums asymptotically contains
all possible compositions. -/
theorem compPartialSumTarget_tendsto_atTop :
    Tendsto (fun N => compPartialSumTarget 0 N N) atTop atTop := by
  apply Tendsto.comp compPartialSumTarget_tendsto_prod_atTop tendsto_atTop_diagonal

/-- Composing the partial sums of two multilinear series coincides with the sum over all
compositions in `compPartialSumTarget 0 N N`. This is precisely the motivation for the
definition of `compPartialSumTarget`. -/
theorem comp_partialSum (q : FormalMultilinearSeries ğ•œ F G) (p : FormalMultilinearSeries ğ•œ E F)
    (M N : â„•) (z : E) :
    q.partialSum M (âˆ‘ i âˆˆ Finset.Ico 1 N, p i fun _j => z) =
      âˆ‘ i âˆˆ compPartialSumTarget 0 M N, q.compAlongComposition p i.2 fun _j => z := by
  -- we expand the composition, using the multilinearity of `q` to expand along each coordinate.
  suffices H :
    (âˆ‘ n âˆˆ Finset.range M,
        âˆ‘ r âˆˆ Fintype.piFinset fun i : Fin n => Finset.Ico 1 N,
          q n fun i : Fin n => p (r i) fun _j => z) =
      âˆ‘ i âˆˆ compPartialSumTarget 0 M N, q.compAlongComposition p i.2 fun _j => z by
    simpa only [FormalMultilinearSeries.partialSum, ContinuousMultilinearMap.map_sum_finset] using H
  -- rewrite the first sum as a big sum over a sigma type, in the finset
  -- `compPartialSumTarget 0 N N`
  rw [Finset.range_eq_Ico, Finset.sum_sigma']
  -- use `compChangeOfVariables_sum`, saying that this change of variables respects sums
  apply compChangeOfVariables_sum 0 M N
  rintro âŸ¨k, blocks_funâŸ© H
  apply congr _ (compChangeOfVariables_length 0 M N H).symm
  intros
  rw [â† compChangeOfVariables_blocksFun 0 M N H, applyComposition, Function.comp_def]

end FormalMultilinearSeries

open FormalMultilinearSeries

/-- If two functions `g` and `f` have power series `q` and `p` respectively at `f x` and `x`, within
two sets `s` and `t` such that `f` maps `s` to `t`, then `g âˆ˜ f` admits the power
series `q.comp p` at `x` within `s`. -/
theorem HasFPowerSeriesWithinAt.comp {g : F â†’ G} {f : E â†’ F} {q : FormalMultilinearSeries ğ•œ F G}
    {p : FormalMultilinearSeries ğ•œ E F} {x : E} {t : Set F} {s : Set E}
    (hg : HasFPowerSeriesWithinAt g q t (f x)) (hf : HasFPowerSeriesWithinAt f p s x)
    (hs : Set.MapsTo f s t) : HasFPowerSeriesWithinAt (g âˆ˜ f) (q.comp p) s x := by
  /- Consider `rf` and `rg` such that `f` and `g` have power series expansion on the disks
    of radius `rf` and `rg`. -/
  rcases hg with âŸ¨rg, HgâŸ©
  rcases hf with âŸ¨rf, HfâŸ©
  -- The terms defining `q.comp p` are geometrically summable in a disk of some radius `r`.
  rcases q.comp_summable_nnreal p Hg.radius_pos Hf.radius_pos with âŸ¨r, r_pos : 0 < r, hrâŸ©
  /- We will consider `y` which is smaller than `r` and `rf`, and also small enough that
    `f (x + y)` is close enough to `f x` to be in the disk where `g` is well behaved. Let
    `min (r, rf, Î´)` be this new radius. -/
  obtain âŸ¨Î´, Î´pos, hÎ´âŸ© :
    âˆƒ Î´ : â„â‰¥0âˆ, 0 < Î´ âˆ§ âˆ€ {z : E}, z âˆˆ insert x s âˆ© EMetric.ball x Î´
      â†’ f z âˆˆ insert (f x) t âˆ© EMetric.ball (f x) rg := by
    have : insert (f x) t âˆ© EMetric.ball (f x) rg âˆˆ ğ“[insert (f x) t] (f x) := by
      apply inter_mem_nhdsWithin
      exact EMetric.ball_mem_nhds _ Hg.r_pos
    have := Hf.analyticWithinAt.continuousWithinAt_insert.tendsto_nhdsWithin (hs.insert x) this
    rcases EMetric.mem_nhdsWithin_iff.1 this with âŸ¨Î´, Î´pos, HÎ´âŸ©
    exact âŸ¨Î´, Î´pos, fun {z} hz => HÎ´ (by rwa [Set.inter_comm])âŸ©
  let rf' := min rf Î´
  have min_pos : 0 < min rf' r := by
    simp only [rf', r_pos, Hf.r_pos, Î´pos, lt_min_iff, ENNReal.coe_pos, and_self_iff]
  /- We will show that `g âˆ˜ f` admits the power series `q.comp p` in the disk of
    radius `min (r, rf', Î´)`. -/
  refine âŸ¨min rf' r, ?_âŸ©
  refine
    âŸ¨le_trans (min_le_right rf' r) (FormalMultilinearSeries.le_comp_radius_of_summable q p r hr),
      min_pos, fun {y} h'y hy â†¦ ?_âŸ©
  /- Let `y` satisfy `â€–yâ€– < min (r, rf', Î´)`. We want to show that `g (f (x + y))` is the sum of
    `q.comp p` applied to `y`. -/
  -- First, check that `y` is small enough so that estimates for `f` and `g` apply.
  have y_mem : y âˆˆ EMetric.ball (0 : E) rf :=
    (EMetric.ball_subset_ball (le_trans (min_le_left _ _) (min_le_left _ _))) hy
  have fy_mem : f (x + y) âˆˆ insert (f x) t âˆ© EMetric.ball (f x) rg := by
    apply hÎ´
    have : y âˆˆ EMetric.ball (0 : E) Î´ :=
      (EMetric.ball_subset_ball (le_trans (min_le_left _ _) (min_le_right _ _))) hy
    simpa [-Set.mem_insert_iff, edist_eq_enorm_sub, h'y]
  /- Now the proof starts. To show that the sum of `q.comp p` at `y` is `g (f (x + y))`,
    we will write `q.comp p` applied to `y` as a big sum over all compositions.
    Since the sum is summable, to get its convergence it suffices to get
    the convergence along some increasing sequence of sets.
    We will use the sequence of sets `compPartialSumTarget 0 n n`,
    along which the sum is exactly the composition of the partial sums of `q` and `p`, by design.
    To show that it converges to `g (f (x + y))`, pointwise convergence would not be enough,
    but we have uniform convergence to save the day. -/
  -- First step: the partial sum of `p` converges to `f (x + y)`.
  have A : Tendsto (fun n â†¦ (n, âˆ‘ a âˆˆ Finset.Ico 1 n, p a fun _ â†¦ y))
      atTop (atTop Ã—Ë¢ ğ“ (f (x + y) - f x)) := by
    apply Tendsto.prodMk tendsto_id
    have L : âˆ€á¶  n in atTop, (âˆ‘ a âˆˆ Finset.range n, p a fun _b â†¦ y) - f x
        = âˆ‘ a âˆˆ Finset.Ico 1 n, p a fun _b â†¦ y := by
      rw [eventually_atTop]
      refine âŸ¨1, fun n hn => ?_âŸ©
      symm
      rw [eq_sub_iff_add_eq', Finset.range_eq_Ico, â† Hf.coeff_zero fun _i => y,
        Finset.sum_eq_sum_Ico_succ_bot hn]
    have :
      Tendsto (fun n => (âˆ‘ a âˆˆ Finset.range n, p a fun _b => y) - f x) atTop
        (ğ“ (f (x + y) - f x)) :=
      (Hf.hasSum h'y y_mem).tendsto_sum_nat.sub tendsto_const_nhds
    exact Tendsto.congr' L this
  -- Second step: the composition of the partial sums of `q` and `p` converges to `g (f (x + y))`.
  have B : Tendsto (fun n => q.partialSum n (âˆ‘ a âˆˆ Finset.Ico 1 n, p a fun _b â†¦ y)) atTop
      (ğ“ (g (f (x + y)))) := by
    -- we use the fact that the partial sums of `q` converge to `g (f (x + y))`, uniformly on a
    -- neighborhood of `f (x + y)`.
    have : Tendsto (fun (z : â„• Ã— F) â†¦ q.partialSum z.1 z.2)
        (atTop Ã—Ë¢ ğ“ (f (x + y) - f x)) (ğ“ (g (f x + (f (x + y) - f x)))) := by
      apply Hg.tendsto_partialSum_prod (y := f (x + y) - f x)
      Â· simpa [edist_eq_enorm_sub] using fy_mem.2
      Â· simpa using fy_mem.1
    simpa using this.comp A
  -- Third step: the sum over all compositions in `compPartialSumTarget 0 n n` converges to
  -- `g (f (x + y))`. As this sum is exactly the composition of the partial sum, this is a direct
  -- consequence of the second step
  have C :
    Tendsto
      (fun n => âˆ‘ i âˆˆ compPartialSumTarget 0 n n, q.compAlongComposition p i.2 fun _j => y)
      atTop (ğ“ (g (f (x + y)))) := by
    simpa [comp_partialSum] using B
  -- Fourth step: the sum over all compositions is `g (f (x + y))`. This follows from the
  -- convergence along a subsequence proved in the third step, and the fact that the sum is Cauchy
  -- thanks to the summability properties.
  have D :
    HasSum (fun i : Î£ n, Composition n => q.compAlongComposition p i.2 fun _j => y)
      (g (f (x + y))) :=
    haveI cau :
      CauchySeq fun s : Finset (Î£ n, Composition n) =>
        âˆ‘ i âˆˆ s, q.compAlongComposition p i.2 fun _j => y := by
      apply cauchySeq_finset_of_norm_bounded (NNReal.summable_coe.2 hr) _
      simp only [coe_nnnorm, NNReal.coe_mul, NNReal.coe_pow]
      rintro âŸ¨n, câŸ©
      calc
        â€–(compAlongComposition q p c) fun _j : Fin n => yâ€– â‰¤
            â€–compAlongComposition q p câ€– * âˆ _j : Fin n, â€–yâ€– := by
          apply ContinuousMultilinearMap.le_opNorm
        _ â‰¤ â€–compAlongComposition q p câ€– * (r : â„) ^ n := by
          rw [Finset.prod_const, Finset.card_fin]
          gcongr
          rw [EMetric.mem_ball, edist_zero_eq_enorm] at hy
          have := le_trans (le_of_lt hy) (min_le_right _ _)
          rwa [enorm_le_coe, â† NNReal.coe_le_coe, coe_nnnorm] at this
    tendsto_nhds_of_cauchySeq_of_subseq cau compPartialSumTarget_tendsto_atTop C
  -- Fifth step: the sum over `n` of `q.comp p n` can be expressed as a particular resummation of
  -- the sum over all compositions, by grouping together the compositions of the same
  -- integer `n`. The convergence of the whole sum therefore implies the converence of the sum
  -- of `q.comp p n`
  have E : HasSum (fun n => (q.comp p) n fun _j => y) (g (f (x + y))) := by
    apply D.sigma
    intro n
    simp only [compAlongComposition_apply, FormalMultilinearSeries.comp,
      ContinuousMultilinearMap.sum_apply]
    exact hasSum_fintype _
  rw [Function.comp_apply]
  exact E

/-- If two functions `g` and `f` have power series `q` and `p` respectively at `f x` and `x`,
then `g âˆ˜ f` admits the power series `q.comp p` at `x`. -/
theorem HasFPowerSeriesAt.comp {g : F â†’ G} {f : E â†’ F} {q : FormalMultilinearSeries ğ•œ F G}
    {p : FormalMultilinearSeries ğ•œ E F} {x : E}
    (hg : HasFPowerSeriesAt g q (f x)) (hf : HasFPowerSeriesAt f p x) :
    HasFPowerSeriesAt (g âˆ˜ f) (q.comp p) x := by
  rw [â† hasFPowerSeriesWithinAt_univ] at hf hg âŠ¢
  apply hg.comp hf (by simp)

/-- If two functions `g` and `f` are analytic respectively at `f x` and `x`, within
two sets `s` and `t` such that `f` maps `s` to `t`, then `g âˆ˜ f` is analytic at `x` within `s`. -/
theorem AnalyticWithinAt.comp {g : F â†’ G} {f : E â†’ F} {x : E} {t : Set F} {s : Set E}
    (hg : AnalyticWithinAt ğ•œ g t (f x)) (hf : AnalyticWithinAt ğ•œ f s x) (h : Set.MapsTo f s t) :
    AnalyticWithinAt ğ•œ (g âˆ˜ f) s x := by
  let âŸ¨_q, hqâŸ© := hg
  let âŸ¨_p, hpâŸ© := hf
  exact (hq.comp hp h).analyticWithinAt

/-- Version of `AnalyticWithinAt.comp` where point equality is a separate hypothesis. -/
theorem AnalyticWithinAt.comp_of_eq {g : F â†’ G} {f : E â†’ F} {y : F} {x : E} {t : Set F} {s : Set E}
    (hg : AnalyticWithinAt ğ•œ g t y) (hf : AnalyticWithinAt ğ•œ f s x) (h : Set.MapsTo f s t)
    (hy : f x = y) :
    AnalyticWithinAt ğ•œ (g âˆ˜ f) s x := by
  rw [â† hy] at hg
  exact hg.comp hf h

lemma AnalyticOn.comp {f : F â†’ G} {g : E â†’ F} {s : Set F}
    {t : Set E} (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g t) (h : Set.MapsTo g t s) :
    AnalyticOn ğ•œ (f âˆ˜ g) t :=
  fun x m â†¦ (hf _ (h m)).comp (hg x m) h

/-- If two functions `g` and `f` are analytic respectively at `f x` and `x`, then `g âˆ˜ f` is
analytic at `x`. -/
@[fun_prop]
theorem AnalyticAt.comp {g : F â†’ G} {f : E â†’ F} {x : E} (hg : AnalyticAt ğ•œ g (f x))
    (hf : AnalyticAt ğ•œ f x) : AnalyticAt ğ•œ (g âˆ˜ f) x := by
  rw [â† analyticWithinAt_univ] at hg hf âŠ¢
  apply hg.comp hf (by simp)

/-- If two functions `g` and `f` are analytic respectively at `f x` and `x`, then `g âˆ˜ f` is
analytic at `x`. -/
@[fun_prop]
theorem AnalyticAt.comp' {g : F â†’ G} {f : E â†’ F} {x : E} (hg : AnalyticAt ğ•œ g (f x))
    (hf : AnalyticAt ğ•œ f x) : AnalyticAt ğ•œ (fun z â†¦ g (f z)) x :=
  hg.comp hf

/-- Version of `AnalyticAt.comp` where point equality is a separate hypothesis. -/
theorem AnalyticAt.comp_of_eq {g : F â†’ G} {f : E â†’ F} {y : F} {x : E} (hg : AnalyticAt ğ•œ g y)
    (hf : AnalyticAt ğ•œ f x) (hy : f x = y) : AnalyticAt ğ•œ (g âˆ˜ f) x := by
  rw [â† hy] at hg
  exact hg.comp hf

/-- Version of `AnalyticAt.comp` where point equality is a separate hypothesis. -/
theorem AnalyticAt.comp_of_eq' {g : F â†’ G} {f : E â†’ F} {y : F} {x : E} (hg : AnalyticAt ğ•œ g y)
    (hf : AnalyticAt ğ•œ f x) (hy : f x = y) : AnalyticAt ğ•œ (fun z â†¦ g (f z)) x := by
  apply hg.comp_of_eq hf hy

theorem AnalyticAt.comp_analyticWithinAt {g : F â†’ G} {f : E â†’ F} {x : E} {s : Set E}
    (hg : AnalyticAt ğ•œ g (f x)) (hf : AnalyticWithinAt ğ•œ f s x) :
    AnalyticWithinAt ğ•œ (g âˆ˜ f) s x := by
  rw [â† analyticWithinAt_univ] at hg
  exact hg.comp hf (Set.mapsTo_univ _ _)

theorem AnalyticAt.comp_analyticWithinAt_of_eq {g : F â†’ G} {f : E â†’ F} {x : E} {y : F} {s : Set E}
    (hg : AnalyticAt ğ•œ g y) (hf : AnalyticWithinAt ğ•œ f s x) (h : f x = y) :
    AnalyticWithinAt ğ•œ (g âˆ˜ f) s x := by
  rw [â† h] at hg
  exact hg.comp_analyticWithinAt hf

/-- If two functions `g` and `f` are analytic respectively on `s.image f` and `s`, then `g âˆ˜ f` is
analytic on `s`. -/
theorem AnalyticOnNhd.comp' {s : Set E} {g : F â†’ G} {f : E â†’ F} (hg : AnalyticOnNhd ğ•œ g (s.image f))
    (hf : AnalyticOnNhd ğ•œ f s) : AnalyticOnNhd ğ•œ (g âˆ˜ f) s :=
  fun z hz => (hg (f z) (Set.mem_image_of_mem f hz)).comp (hf z hz)

theorem AnalyticOnNhd.comp {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F}
    (hg : AnalyticOnNhd ğ•œ g t) (hf : AnalyticOnNhd ğ•œ f s) (st : Set.MapsTo f s t) :
    AnalyticOnNhd ğ•œ (g âˆ˜ f) s :=
  comp' (mono hg (Set.mapsTo_iff_image_subset.mp st)) hf

lemma AnalyticOnNhd.comp_analyticOn {f : F â†’ G} {g : E â†’ F} {s : Set F}
    {t : Set E} (hf : AnalyticOnNhd ğ•œ f s) (hg : AnalyticOn ğ•œ g t) (h : Set.MapsTo g t s) :
    AnalyticOn ğ•œ (f âˆ˜ g) t :=
  fun x m â†¦ (hf _ (h m)).comp_analyticWithinAt (hg x m)

/-- If two functions `g` and `f` have finite power series `q` and `p` respectively at `f x` and `x`,
then `g âˆ˜ f` admits the finite power series `q.comp p` at `x`. -/
theorem HasFiniteFPowerSeriesAt.comp {m n : â„•} {g : F â†’ G} {f : E â†’ F}
    {q : FormalMultilinearSeries ğ•œ F G} {p : FormalMultilinearSeries ğ•œ E F} {x : E}
    (hg : HasFiniteFPowerSeriesAt g q (f x) m) (hf : HasFiniteFPowerSeriesAt f p x n) (hn : 0 < n) :
    HasFiniteFPowerSeriesAt (g âˆ˜ f) (q.comp p) x (m * n) := by
  rcases hg.hasFPowerSeriesAt.comp hf.hasFPowerSeriesAt with âŸ¨r, hrâŸ©
  refine âŸ¨r, hr, fun i hi â†¦ ?_âŸ©
  apply Finset.sum_eq_zero
  rintro c -
  ext v
  simp only [compAlongComposition_apply, ContinuousMultilinearMap.zero_apply]
  rcases le_or_gt m c.length with hc | hc
  Â· simp [hg.finite _ hc]
  obtain âŸ¨j, hjâŸ© : âˆƒ j, n â‰¤ c.blocksFun j := by
    contrapose! hi
    rw [â† c.sum_blocksFun]
    rcases eq_zero_or_pos c.length with h'c | h'c
    Â· have : âˆ‘ j : Fin c.length, c.blocksFun j = 0 := by
        apply Finset.sum_eq_zero (fun j hj â†¦ ?_)
        have := j.2
        grind
      rw [this]
      exact mul_pos (by grind) hn
    Â· calc âˆ‘ j : Fin c.length, c.blocksFun j
      _ < âˆ‘ j : Fin c.length, n := by
        apply Finset.sum_lt_sum (fun j hj â†¦ (hi j).le)
        exact âŸ¨âŸ¨0, h'câŸ©, Finset.mem_univ _, hi _âŸ©
      _ = c.length * n := by simp
      _ â‰¤ m * n := by gcongr
  apply ContinuousMultilinearMap.map_coord_zero _ j
  simp [applyComposition, hf.finite _ hj]

/-- If two functions `g` and `f` are continuously polynomial respectively at `f x` and `x`,
then `g âˆ˜ f` is continuously polynomial at `x`. -/
theorem CPolynomialAt.comp {g : F â†’ G} {f : E â†’ F} {x : E}
    (hg : CPolynomialAt ğ•œ g (f x)) (hf : CPolynomialAt ğ•œ f x) :
    CPolynomialAt ğ•œ (g âˆ˜ f) x := by
  rcases hg with âŸ¨q, m, hmâŸ©
  rcases hf with âŸ¨p, n, hnâŸ©
  refine âŸ¨q.comp p, m * (n + 1), ?_âŸ©
  exact hm.comp (hn.of_le (Nat.le_succ n)) (Nat.zero_lt_succ n)

/-- If two functions `g` and `f` are continuously polynomial respectively at `f x` and `x`,
then `g âˆ˜ f` is continuously polynomial at `x`. -/
theorem CPolynomialAt.fun_comp {g : F â†’ G} {f : E â†’ F} {x : E}
    (hg : CPolynomialAt ğ•œ g (f x)) (hf : CPolynomialAt ğ•œ f x) :
    CPolynomialAt ğ•œ (fun z â†¦ g (f z)) x :=
  hg.comp hf

/-- Version of `CPolynomialAt.comp` where point equality is a separate hypothesis. -/
theorem CPolynomialAt.comp_of_eq {g : F â†’ G} {f : E â†’ F} {y : F} {x : E} (hg : CPolynomialAt ğ•œ g y)
    (hf : CPolynomialAt ğ•œ f x) (hy : f x = y) : CPolynomialAt ğ•œ (g âˆ˜ f) x := by
  rw [â† hy] at hg
  exact hg.comp hf

/-- Version of `CPolynomialAt.comp` where point equality is a separate hypothesis. -/
theorem CPolynomialAt.fun_comp_of_eq {g : F â†’ G} {f : E â†’ F} {y : F} {x : E}
    (hg : CPolynomialAt ğ•œ g y) (hf : CPolynomialAt ğ•œ f x) (hy : f x = y) :
    CPolynomialAt ğ•œ (fun z â†¦ g (f z)) x :=
  hg.comp_of_eq hf hy

/-- If two functions `g` and `f` are continuously polynomial respectively on `s.image f` and `s`,
then `g âˆ˜ f` is continuously polynomial on `s`. -/
theorem CPolynomialOn.comp' {s : Set E} {g : F â†’ G} {f : E â†’ F} (hg : CPolynomialOn ğ•œ g (s.image f))
    (hf : CPolynomialOn ğ•œ f s) : CPolynomialOn ğ•œ (g âˆ˜ f) s :=
  fun z hz => (hg (f z) (Set.mem_image_of_mem f hz)).comp (hf z hz)

theorem CPolynomialOn.comp {s : Set E} {t : Set F} {g : F â†’ G} {f : E â†’ F}
    (hg : CPolynomialOn ğ•œ g t) (hf : CPolynomialOn ğ•œ f s) (st : Set.MapsTo f s t) :
    CPolynomialOn ğ•œ (g âˆ˜ f) s :=
  comp' (mono hg (Set.mapsTo_iff_image_subset.mp st)) hf

/-!
### Associativity of the composition of formal multilinear series

In this paragraph, we prove the associativity of the composition of formal power series.
By definition,
```
(r.comp q).comp p n v
= âˆ‘_{iâ‚ + ... + iâ‚– = n} (r.comp q)â‚– (p_{iâ‚} (vâ‚€, ..., v_{iâ‚ -1}), p_{iâ‚‚} (...), ..., p_{iâ‚–}(...))
= âˆ‘_{a : Composition n} (r.comp q) a.length (applyComposition p a v)
```
decomposing `r.comp q` in the same way, we get
```
(r.comp q).comp p n v
= âˆ‘_{a : Composition n} âˆ‘_{b : Composition a.length}
  r b.length (applyComposition q b (applyComposition p a v))
```
On the other hand,
```
r.comp (q.comp p) n v = âˆ‘_{c : Composition n} r c.length (applyComposition (q.comp p) c v)
```
Here, `applyComposition (q.comp p) c v` is a vector of length `c.length`, whose `i`-th term is
given by `(q.comp p) (c.blocksFun i) (v_l, v_{l+1}, ..., v_{m-1})` where `{l, ..., m-1}` is the
`i`-th block in the composition `c`, of length `c.blocksFun i` by definition. To compute this term,
we expand it as `âˆ‘_{dáµ¢ : Composition (c.blocksFun i)} q dáµ¢.length (applyComposition p dáµ¢ v')`,
where `v' = (v_l, v_{l+1}, ..., v_{m-1})`. Therefore, we get
```
r.comp (q.comp p) n v =
âˆ‘_{c : Composition n} âˆ‘_{dâ‚€ : Composition (c.blocksFun 0),
  ..., d_{c.length - 1} : Composition (c.blocksFun (c.length - 1))}
  r c.length (fun i â†¦ q dáµ¢.length (applyComposition p dáµ¢ v'áµ¢))
```
To show that these terms coincide, we need to explain how to reindex the sums to put them in
bijection (and then the terms we are summing will correspond to each other). Suppose we have a
composition `a` of `n`, and a composition `b` of `a.length`. Then `b` indicates how to group
together some blocks of `a`, giving altogether `b.length` blocks of blocks. These blocks of blocks
can be called `dâ‚€, ..., d_{a.length - 1}`, and one obtains a composition `c` of `n` by saying that
each `dáµ¢` is one single block. Conversely, if one starts from `c` and the `dáµ¢`s, one can concatenate
the `dáµ¢`s to obtain a composition `a` of `n`, and register the lengths of the `dáµ¢`s in a composition
`b` of `a.length`.

An example might be enlightening. Suppose `a = [2, 2, 3, 4, 2]`. It is a composition of
length 5 of 13. The content of the blocks may be represented as `0011222333344`.
Now take `b = [2, 3]` as a composition of `a.length = 5`. It says that the first 2 blocks of `a`
should be merged, and the last 3 blocks of `a` should be merged, giving a new composition of `13`
made of two blocks of length `4` and `9`, i.e., `c = [4, 9]`. But one can also remember that
the new first block was initially made of two blocks of size `2`, so `dâ‚€ = [2, 2]`, and the new
second block was initially made of three blocks of size `3`, `4` and `2`, so `dâ‚ = [3, 4, 2]`.

This equivalence is called `Composition.sigmaEquivSigmaPi n` below.

We start with preliminary results on compositions, of a very specialized nature, then define the
equivalence `Composition.sigmaEquivSigmaPi n`, and we deduce finally the associativity of
composition of formal multilinear series in `FormalMultilinearSeries.comp_assoc`.
-/


namespace Composition

variable {n : â„•}

/-- Rewriting equality in the dependent type `Î£ (a : Composition n), Composition a.length)` in
non-dependent terms with lists, requiring that the blocks coincide. -/
theorem sigma_composition_eq_iff (i j : Î£ a : Composition n, Composition a.length) :
    i = j â†” i.1.blocks = j.1.blocks âˆ§ i.2.blocks = j.2.blocks := by
  refine âŸ¨by rintro rfl; exact âŸ¨rfl, rflâŸ©, ?_âŸ©
  rcases i with âŸ¨a, bâŸ©
  rcases j with âŸ¨a', b'âŸ©
  rintro âŸ¨h, h'âŸ©
  obtain rfl : a = a' := by ext1; exact h
  obtain rfl : b = b' := by ext1; exact h'
  rfl

/-- Rewriting equality in the dependent type
`Î£ (c : Composition n), Î  (i : Fin c.length), Composition (c.blocksFun i)` in
non-dependent terms with lists, requiring that the lists of blocks coincide. -/
theorem sigma_pi_composition_eq_iff
    (u v : Î£ c : Composition n, âˆ€ i : Fin c.length, Composition (c.blocksFun i)) :
    u = v â†” (ofFn fun i => (u.2 i).blocks) = ofFn fun i => (v.2 i).blocks := by
  refine âŸ¨fun H => by rw [H], fun H => ?_âŸ©
  rcases u with âŸ¨a, bâŸ©
  rcases v with âŸ¨a', b'âŸ©
  dsimp at H
  obtain rfl : a = a' := by
    ext1
    have :
      map List.sum (ofFn fun i : Fin (Composition.length a) => (b i).blocks) =
        map List.sum (ofFn fun i : Fin (Composition.length a') => (b' i).blocks) := by
      rw [H]
    simp only [map_ofFn] at this
    change
      (ofFn fun i : Fin (Composition.length a) => (b i).blocks.sum) =
        ofFn fun i : Fin (Composition.length a') => (b' i).blocks.sum at this
    simpa [Composition.blocks_sum, Composition.ofFn_blocksFun] using this
  ext1
  Â· rfl
  Â· simp only [heq_eq_eq, ofFn_inj] at H âŠ¢
    ext1 i
    ext1
    exact congrFun H i

/-- When `a` is a composition of `n` and `b` is a composition of `a.length`, `a.gather b` is the
composition of `n` obtained by gathering all the blocks of `a` corresponding to a block of `b`.
For instance, if `a = [6, 5, 3, 5, 2]` and `b = [2, 3]`, one should gather together
the first two blocks of `a` and its last three blocks, giving `a.gather b = [11, 10]`. -/
def gather (a : Composition n) (b : Composition a.length) : Composition n where
  blocks := (a.blocks.splitWrtComposition b).map sum
  blocks_pos := by
    rw [forall_mem_map]
    intro j hj
    suffices H : âˆ€ i âˆˆ j, 1 â‰¤ i from calc
      0 < j.length := length_pos_of_mem_splitWrtComposition hj
      _ â‰¤ j.sum := length_le_sum_of_one_le _ H
    intro i hi
    apply a.one_le_blocks
    rw [â† a.blocks.flatten_splitWrtComposition b]
    exact mem_flatten_of_mem hj hi
  blocks_sum := by rw [â† sum_flatten, flatten_splitWrtComposition, a.blocks_sum]

theorem length_gather (a : Composition n) (b : Composition a.length) :
    length (a.gather b) = b.length :=
  show (map List.sum (a.blocks.splitWrtComposition b)).length = b.blocks.length by
    rw [length_map, length_splitWrtComposition]

/-- An auxiliary function used in the definition of `sigmaEquivSigmaPi` below, associating to
two compositions `a` of `n` and `b` of `a.length`, and an index `i` bounded by the length of
`a.gather b`, the subcomposition of `a` made of those blocks belonging to the `i`-th block of
`a.gather b`. -/
def sigmaCompositionAux (a : Composition n) (b : Composition a.length)
    (i : Fin (a.gather b).length) : Composition ((a.gather b).blocksFun i) where
  blocks :=
    (a.blocks.splitWrtComposition b)[i.val]'(by
      rw [length_splitWrtComposition, â† length_gather]; exact i.2)
  blocks_pos {i} hi :=
    a.blocks_pos
      (by
        rw [â† a.blocks.flatten_splitWrtComposition b]
        exact mem_flatten_of_mem (List.getElem_mem _) hi)
  blocks_sum := by simp [Composition.blocksFun, getElem_map, Composition.gather]

theorem length_sigmaCompositionAux (a : Composition n) (b : Composition a.length)
    (i : Fin b.length) :
    Composition.length (Composition.sigmaCompositionAux a b âŸ¨i, (length_gather a b).symm â–¸ i.2âŸ©) =
      Composition.blocksFun b i :=
  show List.length ((splitWrtComposition a.blocks b)[i.1]) = blocksFun b i by
    rw [getElem_map_rev List.length, getElem_of_eq (map_length_splitWrtComposition _ _), blocksFun,
      get_eq_getElem]

theorem blocksFun_sigmaCompositionAux (a : Composition n) (b : Composition a.length)
    (i : Fin b.length) (j : Fin (blocksFun b i)) :
    blocksFun (sigmaCompositionAux a b âŸ¨i, (length_gather a b).symm â–¸ i.2âŸ©)
        âŸ¨j, (length_sigmaCompositionAux a b i).symm â–¸ j.2âŸ© =
      blocksFun a (embedding b i j) := by
  unfold sigmaCompositionAux
  rw [blocksFun, get_eq_getElem, getElem_of_eq (getElem_splitWrtComposition _ _ _ _),
    getElem_drop, getElem_take]; rfl

/-- Auxiliary lemma to prove that the composition of formal multilinear series is associative.

Consider a composition `a` of `n` and a composition `b` of `a.length`. Grouping together some
blocks of `a` according to `b` as in `a.gather b`, one can compute the total size of the blocks
of `a` up to an index `sizeUpTo b i + j` (where the `j` corresponds to a set of blocks of `a`
that do not fill a whole block of `a.gather b`). The first part corresponds to a sum of blocks
in `a.gather b`, and the second one to a sum of blocks in the next block of
`sigmaCompositionAux a b`. This is the content of this lemma. -/
theorem sizeUpTo_sizeUpTo_add (a : Composition n) (b : Composition a.length) {i j : â„•}
    (hi : i < b.length) (hj : j < blocksFun b âŸ¨i, hiâŸ©) :
    sizeUpTo a (sizeUpTo b i + j) =
      sizeUpTo (a.gather b) i +
        sizeUpTo (sigmaCompositionAux a b âŸ¨i, (length_gather a b).symm â–¸ hiâŸ©) j := by
  induction j with
  | zero =>
    change
      sum (take (b.blocks.take i).sum a.blocks) =
        sum (take i (map sum (splitWrtComposition a.blocks b)))
    induction i with
    | zero => rfl
    | succ i IH =>
      have A : i < b.length := Nat.lt_of_succ_lt hi
      have B : i < List.length (map List.sum (splitWrtComposition a.blocks b)) := by simp [A]
      have C : 0 < blocksFun b âŸ¨i, AâŸ© := Composition.blocks_pos' _ _ _
      rw [sum_take_succ _ _ B, â† IH A C]
      have :
        take (sum (take i b.blocks)) a.blocks =
          take (sum (take i b.blocks)) (take (sum (take (i + 1) b.blocks)) a.blocks) := by
        rw [take_take, min_eq_left]
        apply monotone_sum_take _ (Nat.le_succ _)
      rw [this, getElem_map, getElem_splitWrtComposition, â†
        take_append_drop (sum (take i b.blocks)) (take (sum (take (Nat.succ i) b.blocks)) a.blocks),
        sum_append]
      congr
      rw [take_append_drop]
  | succ j IHj =>
    have A : j < blocksFun b âŸ¨i, hiâŸ© := lt_trans (lt_add_one j) hj
    have B : j < length (sigmaCompositionAux a b âŸ¨i, (length_gather a b).symm â–¸ hiâŸ©) := by
      convert A; rw [â† length_sigmaCompositionAux]
    have C : sizeUpTo b i + j < sizeUpTo b (i + 1) := by
      simp only [sizeUpTo_succ b hi, add_lt_add_iff_left]
      exact A
    have D : sizeUpTo b i + j < length a := lt_of_lt_of_le C (b.sizeUpTo_le _)
    have : sizeUpTo b i + Nat.succ j = (sizeUpTo b i + j).succ := rfl
    rw [this, sizeUpTo_succ _ D, IHj A, sizeUpTo_succ _ B]
    simp only [sigmaCompositionAux, add_assoc]
    rw [getElem_of_eq (getElem_splitWrtComposition _ _ _ _), getElem_drop, getElem_take]

/-- Natural equivalence between `(Î£ (a : Composition n), Composition a.length)` and
`(Î£ (c : Composition n), Î  (i : Fin c.length), Composition (c.blocksFun i))`, that shows up as a
change of variables in the proof that composition of formal multilinear series is associative.

Consider a composition `a` of `n` and a composition `b` of `a.length`. Then `b` indicates how to
group together some blocks of `a`, giving altogether `b.length` blocks of blocks. These blocks of
blocks can be called `dâ‚€, ..., d_{a.length - 1}`, and one obtains a composition `c` of `n` by
saying that each `dáµ¢` is one single block. The map `âŸ¨a, bâŸ© â†’ âŸ¨c, (dâ‚€, ..., d_{a.length - 1})âŸ©` is
the direct map in the equiv.

Conversely, if one starts from `c` and the `dáµ¢`s, one can join the `dáµ¢`s to obtain a composition
`a` of `n`, and register the lengths of the `dáµ¢`s in a composition `b` of `a.length`. This is the
inverse map of the equiv.
-/
def sigmaEquivSigmaPi (n : â„•) :
    (Î£ a : Composition n, Composition a.length) â‰ƒ
      Î£ c : Composition n, âˆ€ i : Fin c.length, Composition (c.blocksFun i) where
  toFun i := âŸ¨i.1.gather i.2, i.1.sigmaCompositionAux i.2âŸ©
  invFun i :=
    âŸ¨{  blocks := (ofFn fun j => (i.2 j).blocks).flatten
        blocks_pos := by
          simp only [and_imp, List.mem_flatten, exists_imp, forall_mem_ofFn_iff]
          exact fun {i} j hj => Composition.blocks_pos _ hj
        blocks_sum := by simp [sum_ofFn, Composition.blocks_sum, Composition.sum_blocksFun] },
      { blocks := ofFn fun j => (i.2 j).length
        blocks_pos := by
          intro k hk
          refine ((forall_mem_ofFn_iff (P := fun i => 0 < i)).2 fun j => ?_) k hk
          exact Composition.length_pos_of_pos _ (Composition.blocks_pos' _ _ _)
        blocks_sum := by dsimp only [Composition.length]; simp [sum_ofFn] }âŸ©
  left_inv := by
    -- the fact that we have a left inverse is essentially `join_splitWrtComposition`,
    -- but we need to massage it to take care of the dependent setting.
    rintro âŸ¨a, bâŸ©
    rw [sigma_composition_eq_iff]
    dsimp
    constructor
    Â· conv_rhs =>
        rw [â† flatten_splitWrtComposition a.blocks b, â† ofFn_get (splitWrtComposition a.blocks b)]
      have A : length (gather a b) = List.length (splitWrtComposition a.blocks b) := by
        simp only [length, gather, length_map, length_splitWrtComposition]
      congr! 2
      exact (Fin.heq_fun_iff A (Î± := List â„•)).2 fun i => rfl
    Â· have B : Composition.length (Composition.gather a b) = List.length b.blocks :=
        Composition.length_gather _ _
      conv_rhs => rw [â† ofFn_getElem b.blocks]
      congr 1
      refine (Fin.heq_fun_iff B).2 fun i => ?_
      rw [sigmaCompositionAux, Composition.length, List.getElem_map_rev List.length,
        List.getElem_of_eq (map_length_splitWrtComposition _ _)]
  right_inv := by
    -- the fact that we have a right inverse is essentially `splitWrtComposition_join`,
    -- but we need to massage it to take care of the dependent setting.
    rintro âŸ¨c, dâŸ©
    have : map List.sum (ofFn fun i : Fin (Composition.length c) => (d i).blocks) = c.blocks := by
      simp [map_ofFn, Function.comp_def, Composition.blocks_sum, Composition.ofFn_blocksFun]
    rw [sigma_pi_composition_eq_iff]
    dsimp
    congr! 1
    Â· congr
      ext1
      dsimp [Composition.gather]
      rwa [splitWrtComposition_flatten]
      simp only [map_ofFn, Function.comp_def]
    Â· rw [Fin.heq_fun_iff]
      Â· intro i
        dsimp [Composition.sigmaCompositionAux]
        rw [getElem_of_eq (splitWrtComposition_flatten _ _ _)]
        Â· simp only [List.getElem_ofFn]
        Â· simp only [map_ofFn, Function.comp_def]
      Â· congr

end Composition

namespace FormalMultilinearSeries

open Composition

theorem comp_assoc (r : FormalMultilinearSeries ğ•œ G H) (q : FormalMultilinearSeries ğ•œ F G)
    (p : FormalMultilinearSeries ğ•œ E F) : (r.comp q).comp p = r.comp (q.comp p) := by
  ext n v
  /- First, rewrite the two compositions appearing in the theorem as two sums over complicated
    sigma types, as in the description of the proof above. -/
  let f : (Î£ a : Composition n, Composition a.length) â†’ H := fun c =>
    r c.2.length (applyComposition q c.2 (applyComposition p c.1 v))
  let g : (Î£ c : Composition n, âˆ€ i : Fin c.length, Composition (c.blocksFun i)) â†’ H := fun c =>
    r c.1.length fun i : Fin c.1.length =>
      q (c.2 i).length (applyComposition p (c.2 i) (v âˆ˜ c.1.embedding i))
  suffices âˆ‘ c, f c = âˆ‘ c, g c by
    simpa +unfoldPartialApp only [FormalMultilinearSeries.comp,
      ContinuousMultilinearMap.sum_apply, compAlongComposition_apply, Finset.sum_sigma',
      applyComposition, ContinuousMultilinearMap.map_sum]
  /- Now, we use `Composition.sigmaEquivSigmaPi n` to change
    variables in the second sum, and check that we get exactly the same sums. -/
  rw [â† (sigmaEquivSigmaPi n).sum_comp]
  /- To check that we have the same terms, we should check that we apply the same component of
    `r`, and the same component of `q`, and the same component of `p`, to the same coordinate of
    `v`. This is true by definition, but at each step one needs to convince Lean that the types
    one considers are the same, using a suitable congruence lemma to avoid dependent type issues.
    This dance has to be done three times, one for `r`, one for `q` and one for `p`. -/
  apply Finset.sum_congr rfl
  rintro âŸ¨a, bâŸ© _
  dsimp [sigmaEquivSigmaPi]
  -- check that the `r` components are the same. Based on `Composition.length_gather`
  apply r.congr (Composition.length_gather a b).symm
  intro i hi1 hi2
  -- check that the `q` components are the same. Based on `length_sigmaCompositionAux`
  apply q.congr (length_sigmaCompositionAux a b _).symm
  intro j hj1 hj2
  -- check that the `p` components are the same. Based on `blocksFun_sigmaCompositionAux`
  apply p.congr (blocksFun_sigmaCompositionAux a b _ _).symm
  intro k hk1 hk2
  -- finally, check that the coordinates of `v` one is using are the same. Based on
  -- `sizeUpTo_sizeUpTo_add`.
  refine congr_arg v (Fin.ext ?_)
  dsimp [Composition.embedding]
  rw [sizeUpTo_sizeUpTo_add _ _ hi1 hj1, add_assoc]

end FormalMultilinearSeries
