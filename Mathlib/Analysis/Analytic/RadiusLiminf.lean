/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal

#align_import analysis.analytic.radius_liminf from "leanprover-community/mathlib"@"0b9eaaa7686280fad8cce467f5c3c57ee6ce77f8"

/-!
# Representation of `FormalMultilinearSeries.radius` as a `liminf`

In this file we prove that the radius of convergence of a `FormalMultilinearSeries` is equal to
$\liminf_{n\to\infty} \frac{1}{\sqrt[n]{‚Äñp n‚Äñ}}$. This lemma can't go to `Analysis.Analytic.Basic`
because this would create a circular dependency once we redefine `exp` using
`FormalMultilinearSeries`.
-/


variable {ùïú : Type*} [NontriviallyNormedField ùïú] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ùïú E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ùïú F]

open scoped Topology Classical NNReal ENNReal

open Filter Asymptotics

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ùïú E F)

/-- The radius of a formal multilinear series is equal to
$\liminf_{n\to\infty} \frac{1}{\sqrt[n]{‚Äñp n‚Äñ}}$. The actual statement uses `‚Ñù‚â•0` and some
coercions. -/
theorem radius_eq_liminf :
    p.radius = liminf (fun n => (1 / (‚Äñp n‚Äñ‚Çä ^ (1 / (n : ‚Ñù)) : ‚Ñù‚â•0) : ‚Ñù‚â•0‚àû)) atTop := by
  -- Porting note: added type ascription to make elaborated statement match Lean 3 version
  have :
    ‚àÄ (r : ‚Ñù‚â•0) {n : ‚Ñï},
      0 < n ‚Üí ((r : ‚Ñù‚â•0‚àû) ‚â§ 1 / ‚Üë(‚Äñp n‚Äñ‚Çä ^ (1 / (n : ‚Ñù))) ‚Üî ‚Äñp n‚Äñ‚Çä * r ^ n ‚â§ 1) := by
    intro r n hn
    have : 0 < (n : ‚Ñù) := Nat.cast_pos.2 hn
    conv_lhs =>
      rw [one_div, ENNReal.le_inv_iff_mul_le, ‚Üê ENNReal.coe_mul, ENNReal.coe_le_one_iff, one_div, ‚Üê
        NNReal.rpow_one r, ‚Üê mul_inv_cancel this.ne', NNReal.rpow_mul, ‚Üê NNReal.mul_rpow, ‚Üê
        NNReal.one_rpow n‚Åª¬π, NNReal.rpow_le_rpow_iff (inv_pos.2 this), mul_comm,
        NNReal.rpow_natCast]
  apply le_antisymm <;> refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  ¬∑ have := ((TFAE_exists_lt_isLittleO_pow (fun n => ‚Äñp n‚Äñ * r ^ n) 1).out 1 7).1
      (p.isLittleO_of_lt_radius hr)
    obtain ‚ü®a, ha, H‚ü© := this
    apply le_liminf_of_le
    ¬∑ infer_param
    ¬∑ rw [‚Üê eventually_map]
      refine
        H.mp ((eventually_gt_atTop 0).mono fun n hn‚ÇÄ hn => (this _ hn‚ÇÄ).2 (NNReal.coe_le_coe.1 ?_))
      push_cast
      exact (le_abs_self _).trans (hn.trans (pow_le_one _ ha.1.le ha.2.le))
  ¬∑ refine p.le_radius_of_isBigO (IsBigO.of_bound 1 ?_)
    refine (eventually_lt_of_lt_liminf hr).mp ((eventually_gt_atTop 0).mono fun n hn‚ÇÄ hn => ?_)
    simpa using NNReal.coe_le_coe.2 ((this _ hn‚ÇÄ).1 hn.le)
#align formal_multilinear_series.radius_eq_liminf FormalMultilinearSeries.radius_eq_liminf

end FormalMultilinearSeries
