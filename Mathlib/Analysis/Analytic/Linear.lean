/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Analytic.CPolynomialDef

/-!
# Linear functions are analytic

In this file we prove that a `ContinuousLinearMap` defines an analytic function with
the formal power series `f x = f a + f (x - a)`. We also prove similar results for bilinear maps.

We deduce this fact from the stronger result that continuous linear maps are continuously
polynomial, i.e., they admit a finite power series.
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]

open scoped Topology NNReal ENNReal
open Set Filter Asymptotics

noncomputable section

namespace ContinuousLinearMap

@[simp]
theorem fpowerSeries_radius (f : E â†’L[ğ•œ] F) (x : E) : (f.fpowerSeries x).radius = âˆ :=
  (f.fpowerSeries x).radius_eq_top_of_forall_image_add_eq_zero 2 fun _ => rfl

protected theorem hasFiniteFPowerSeriesOnBall (f : E â†’L[ğ•œ] F) (x : E) :
    HasFiniteFPowerSeriesOnBall f (f.fpowerSeries x) x 2 âˆ where
  r_le := by simp
  r_pos := ENNReal.coe_lt_top
  hasSum := fun _ => (hasSum_nat_add_iff' 2).1 <| by
    simp [Finset.sum_range_succ, hasSum_zero, fpowerSeries]
  finite := by
    intro m hm
    match m with
    | 0 | 1 => linarith
    | n + 2 => simp [fpowerSeries]

protected theorem hasFPowerSeriesOnBall (f : E â†’L[ğ•œ] F) (x : E) :
    HasFPowerSeriesOnBall f (f.fpowerSeries x) x âˆ :=
  (f.hasFiniteFPowerSeriesOnBall x).toHasFPowerSeriesOnBall

protected theorem hasFPowerSeriesAt (f : E â†’L[ğ•œ] F) (x : E) :
    HasFPowerSeriesAt f (f.fpowerSeries x) x :=
  âŸ¨âˆ, f.hasFPowerSeriesOnBall xâŸ©

protected theorem cpolynomialAt (f : E â†’L[ğ•œ] F) (x : E) : CPolynomialAt ğ•œ f x :=
  (f.hasFiniteFPowerSeriesOnBall x).cpolynomialAt

protected theorem analyticAt (f : E â†’L[ğ•œ] F) (x : E) : AnalyticAt ğ•œ f x :=
  (f.hasFPowerSeriesAt x).analyticAt

protected theorem cpolynomialOn (f : E â†’L[ğ•œ] F) (s : Set E) : CPolynomialOn ğ•œ f s :=
  fun x _ â†¦ f.cpolynomialAt x

protected theorem analyticOnNhd (f : E â†’L[ğ•œ] F) (s : Set E) : AnalyticOnNhd ğ•œ f s :=
  fun x _ â†¦ f.analyticAt x

protected theorem analyticWithinAt (f : E â†’L[ğ•œ] F) (s : Set E) (x : E) : AnalyticWithinAt ğ•œ f s x :=
  (f.analyticAt x).analyticWithinAt

protected theorem analyticOn (f : E â†’L[ğ•œ] F) (s : Set E) : AnalyticOn ğ•œ f s :=
  fun x _ â†¦ f.analyticWithinAt _ x

/-- Reinterpret a bilinear map `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G` as a multilinear map
`(E Ã— F) [Ã—2]â†’L[ğ•œ] G`. This multilinear map is the second term in the formal
multilinear series expansion of `uncurry f`. It is given by
`f.uncurryBilinear ![(x, y), (x', y')] = f x y'`. -/
def uncurryBilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) : E Ã— F[Ã—2]â†’L[ğ•œ] G :=
  @ContinuousLinearMap.uncurryLeft ğ•œ 1 (fun _ => E Ã— F) G _ _ _ _ _ <|
    (â†‘(continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm : (E Ã— F â†’L[ğ•œ] G) â†’L[ğ•œ] _).comp <|
      f.bilinearComp (fst _ _ _) (snd _ _ _)

@[simp]
theorem uncurryBilinear_apply (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (m : Fin 2 â†’ E Ã— F) :
    f.uncurryBilinear m = f (m 0).1 (m 1).2 :=
  rfl

/-- Formal multilinear series expansion of a bilinear function `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G`. -/
def fpowerSeriesBilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) : FormalMultilinearSeries ğ•œ (E Ã— F) G
  | 0 => ContinuousMultilinearMap.uncurry0 ğ•œ _ (f x.1 x.2)
  | 1 => (continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm (f.derivâ‚‚ x)
  | 2 => f.uncurryBilinear
  | _ => 0

@[simp]
theorem fpowerSeriesBilinear_apply_zero (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 0 = ContinuousMultilinearMap.uncurry0 ğ•œ _ (f x.1 x.2) :=
  rfl

@[simp]
theorem fpowerSeriesBilinear_apply_one (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 1 = (continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm (f.derivâ‚‚ x) :=
  rfl

@[simp]
theorem fpowerSeriesBilinear_apply_two (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 2 = f.uncurryBilinear :=
  rfl

@[simp]
theorem fpowerSeriesBilinear_apply_add_three (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) (n) :
    fpowerSeriesBilinear f x (n + 3) = 0 :=
  rfl

@[simp]
theorem fpowerSeriesBilinear_radius (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    (f.fpowerSeriesBilinear x).radius = âˆ :=
  (f.fpowerSeriesBilinear x).radius_eq_top_of_forall_image_add_eq_zero 3 fun _ => rfl

protected theorem hasFPowerSeriesOnBall_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    HasFPowerSeriesOnBall (fun x : E Ã— F => f x.1 x.2) (f.fpowerSeriesBilinear x) x âˆ :=
  { r_le := by simp
    r_pos := ENNReal.coe_lt_top
    hasSum := fun _ =>
      (hasSum_nat_add_iff' 3).1 <| by
        simp only [Finset.sum_range_succ, Prod.fst_add, Prod.snd_add, f.map_add_add]
        simp [fpowerSeriesBilinear, hasSum_zero] }

protected theorem hasFPowerSeriesAt_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    HasFPowerSeriesAt (fun x : E Ã— F => f x.1 x.2) (f.fpowerSeriesBilinear x) x :=
  âŸ¨âˆ, f.hasFPowerSeriesOnBall_bilinear xâŸ©

protected theorem analyticAt_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    AnalyticAt ğ•œ (fun x : E Ã— F => f x.1 x.2) x :=
  (f.hasFPowerSeriesAt_bilinear x).analyticAt

protected theorem analyticWithinAt_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (s : Set (E Ã— F)) (x : E Ã— F) :
    AnalyticWithinAt ğ•œ (fun x : E Ã— F => f x.1 x.2) s x :=
  (f.analyticAt_bilinear x).analyticWithinAt

protected theorem analyticOnNhd_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (s : Set (E Ã— F)) :
    AnalyticOnNhd ğ•œ (fun x : E Ã— F => f x.1 x.2) s :=
  fun x _ â†¦ f.analyticAt_bilinear x

protected theorem analyticOn_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (s : Set (E Ã— F)) :
    AnalyticOn ğ•œ (fun x : E Ã— F => f x.1 x.2) s :=
  (f.analyticOnNhd_bilinear s).analyticOn

end ContinuousLinearMap

variable {s : Set E} {z : E} {t : Set (E Ã— F)} {p : E Ã— F}

@[fun_prop]
lemma analyticAt_id : AnalyticAt ğ•œ (id : E â†’ E) z :=
  (ContinuousLinearMap.id ğ•œ E).analyticAt z

lemma analyticWithinAt_id : AnalyticWithinAt ğ•œ (id : E â†’ E) s z :=
  analyticAt_id.analyticWithinAt

/-- `id` is entire -/
theorem analyticOnNhd_id : AnalyticOnNhd ğ•œ (fun x : E â†¦ x) s :=
  fun _ _ â†¦ analyticAt_id

theorem analyticOn_id : AnalyticOn ğ•œ (fun x : E â†¦ x) s :=
  fun _ _ â†¦ analyticWithinAt_id

/-- `fst` is analytic -/
theorem analyticAt_fst : AnalyticAt ğ•œ (fun p : E Ã— F â†¦ p.fst) p :=
  (ContinuousLinearMap.fst ğ•œ E F).analyticAt p

theorem analyticWithinAt_fst : AnalyticWithinAt ğ•œ (fun p : E Ã— F â†¦ p.fst) t p :=
  analyticAt_fst.analyticWithinAt

/-- `snd` is analytic -/
theorem analyticAt_snd : AnalyticAt ğ•œ (fun p : E Ã— F â†¦ p.snd) p :=
  (ContinuousLinearMap.snd ğ•œ E F).analyticAt p

theorem analyticWithinAt_snd : AnalyticWithinAt ğ•œ (fun p : E Ã— F â†¦ p.snd) t p :=
  analyticAt_snd.analyticWithinAt

/-- `fst` is entire -/
theorem analyticOnNhd_fst : AnalyticOnNhd ğ•œ (fun p : E Ã— F â†¦ p.fst) t :=
  fun _ _ â†¦ analyticAt_fst

theorem analyticOn_fst : AnalyticOn ğ•œ (fun p : E Ã— F â†¦ p.fst) t :=
  fun _ _ â†¦ analyticWithinAt_fst

/-- `snd` is entire -/
theorem analyticOnNhd_snd : AnalyticOnNhd ğ•œ (fun p : E Ã— F â†¦ p.snd) t :=
  fun _ _ â†¦ analyticAt_snd

theorem analyticOn_snd : AnalyticOn ğ•œ (fun p : E Ã— F â†¦ p.snd) t :=
  fun _ _ â†¦ analyticWithinAt_snd

namespace ContinuousLinearEquiv

variable (f : E â‰ƒL[ğ•œ] F) (s : Set E) (x : E)

protected theorem analyticAt : AnalyticAt ğ•œ f x :=
  ((f : E â†’L[ğ•œ] F).hasFPowerSeriesAt x).analyticAt

protected theorem analyticOnNhd : AnalyticOnNhd ğ•œ f s :=
  fun x _ â†¦ f.analyticAt x

protected theorem analyticWithinAt : AnalyticWithinAt ğ•œ f s x :=
  (f.analyticAt x).analyticWithinAt

protected theorem analyticOn : AnalyticOn ğ•œ f s :=
  fun x _ â†¦ f.analyticWithinAt _ x

end ContinuousLinearEquiv

namespace LinearIsometryEquiv

variable (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) (s : Set E) (x : E)

protected theorem analyticAt : AnalyticAt ğ•œ f x :=
  ((f : E â†’L[ğ•œ] F).hasFPowerSeriesAt x).analyticAt

protected theorem analyticOnNhd : AnalyticOnNhd ğ•œ f s :=
  fun x _ â†¦ f.analyticAt x

protected theorem analyticWithinAt : AnalyticWithinAt ğ•œ f s x :=
  (f.analyticAt x).analyticWithinAt

protected theorem analyticOn : AnalyticOn ğ•œ f s :=
  fun x _ â†¦ f.analyticWithinAt _ x

end LinearIsometryEquiv
