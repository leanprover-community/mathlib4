/-
Copyright (c) 2021 Yury G. Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury G. Kudryashov
-/
import Mathlib.Analysis.Analytic.Composition

#align_import analysis.analytic.linear from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Linear functions are analytic

In this file we prove that a `ContinuousLinearMap` defines an analytic function with
the formal power series `f x = f a + f (x - a)`. We also prove similar results for multilinear maps.
-/


variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] {G : Type*}
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]

open scoped Topology Classical BigOperators NNReal ENNReal

open Set Filter Asymptotics

noncomputable section

namespace ContinuousLinearMap

@[simp]
theorem fpowerSeries_radius (f : E â†’L[ğ•œ] F) (x : E) : (f.fpowerSeries x).radius = âˆ :=
  (f.fpowerSeries x).radius_eq_top_of_forall_image_add_eq_zero 2 fun _ => rfl
#align continuous_linear_map.fpower_series_radius ContinuousLinearMap.fpowerSeries_radius

protected theorem hasFPowerSeriesOnBall (f : E â†’L[ğ•œ] F) (x : E) :
    HasFPowerSeriesOnBall f (f.fpowerSeries x) x âˆ :=
  { r_le := by simp
    r_pos := ENNReal.coe_lt_top
    hasSum := fun _ => (hasSum_nat_add_iff' 2).1 <| by
      simp [Finset.sum_range_succ, â† sub_sub, hasSum_zero, fpowerSeries] }
#align continuous_linear_map.has_fpower_series_on_ball ContinuousLinearMap.hasFPowerSeriesOnBall

protected theorem hasFPowerSeriesAt (f : E â†’L[ğ•œ] F) (x : E) :
    HasFPowerSeriesAt f (f.fpowerSeries x) x :=
  âŸ¨âˆ, f.hasFPowerSeriesOnBall xâŸ©
#align continuous_linear_map.has_fpower_series_at ContinuousLinearMap.hasFPowerSeriesAt

protected theorem analyticAt (f : E â†’L[ğ•œ] F) (x : E) : AnalyticAt ğ•œ f x :=
  (f.hasFPowerSeriesAt x).analyticAt
#align continuous_linear_map.analytic_at ContinuousLinearMap.analyticAt

/-- Reinterpret a bilinear map `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G` as a multilinear map
`(E Ã— F) [Ã—2]â†’L[ğ•œ] G`. This multilinear map is the second term in the formal
multilinear series expansion of `uncurry f`. It is given by
`f.uncurryBilinear ![(x, y), (x', y')] = f x y'`. -/
def uncurryBilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) : E Ã— F[Ã—2]â†’L[ğ•œ] G :=
  @ContinuousLinearMap.uncurryLeft ğ•œ 1 (fun _ => E Ã— F) G _ _ _ _ _ <|
    (â†‘(continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm : (E Ã— F â†’L[ğ•œ] G) â†’L[ğ•œ] _).comp <|
      f.bilinearComp (fst _ _ _) (snd _ _ _)
#align continuous_linear_map.uncurry_bilinear ContinuousLinearMap.uncurryBilinear

@[simp]
theorem uncurryBilinear_apply (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (m : Fin 2 â†’ E Ã— F) :
    f.uncurryBilinear m = f (m 0).1 (m 1).2 :=
  rfl
#align continuous_linear_map.uncurry_bilinear_apply ContinuousLinearMap.uncurryBilinear_apply

/-- Formal multilinear series expansion of a bilinear function `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G`. -/
def fpowerSeriesBilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) : FormalMultilinearSeries ğ•œ (E Ã— F) G
  | 0 => ContinuousMultilinearMap.curry0 ğ•œ _ (f x.1 x.2)
  | 1 => (continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm (f.derivâ‚‚ x)
  | 2 => f.uncurryBilinear
  | _ => 0
#align continuous_linear_map.fpower_series_bilinear ContinuousLinearMap.fpowerSeriesBilinear

theorem fpowerSeriesBilinear_apply_zero (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 0 = ContinuousMultilinearMap.curry0 ğ•œ _ (f x.1 x.2) :=
  rfl

theorem fpowerSeriesBilinear_apply_one (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 1 = (continuousMultilinearCurryFin1 ğ•œ (E Ã— F) G).symm (f.derivâ‚‚ x) :=
  rfl

theorem fpowerSeriesBilinear_apply_two (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    fpowerSeriesBilinear f x 2 = f.uncurryBilinear :=
  rfl

theorem fpowerSeriesBilinear_apply_add_three (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) (n) :
    fpowerSeriesBilinear f x (n + 3) = 0 :=
  rfl

attribute
  [eqns
    fpowerSeriesBilinear_apply_zero
    fpowerSeriesBilinear_apply_one
    fpowerSeriesBilinear_apply_two
    fpowerSeriesBilinear_apply_add_three] fpowerSeriesBilinear
attribute [simp] fpowerSeriesBilinear

@[simp]
theorem fpowerSeriesBilinear_radius (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    (f.fpowerSeriesBilinear x).radius = âˆ :=
  (f.fpowerSeriesBilinear x).radius_eq_top_of_forall_image_add_eq_zero 3 fun _ => rfl
#align continuous_linear_map.fpower_series_bilinear_radius ContinuousLinearMap.fpowerSeriesBilinear_radius

protected theorem hasFPowerSeriesOnBall_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    HasFPowerSeriesOnBall (fun x : E Ã— F => f x.1 x.2) (f.fpowerSeriesBilinear x) x âˆ :=
  { r_le := by simp
    r_pos := ENNReal.coe_lt_top
    hasSum := fun _ =>
      (hasSum_nat_add_iff' 3).1 <| by
        simp only [Finset.sum_range_succ, Finset.sum_range_one, Prod.fst_add, Prod.snd_add,
          f.map_add_add]
        simp [fpowerSeriesBilinear, hasSum_zero] }
#align continuous_linear_map.has_fpower_series_on_ball_bilinear ContinuousLinearMap.hasFPowerSeriesOnBall_bilinear

protected theorem hasFPowerSeriesAt_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    HasFPowerSeriesAt (fun x : E Ã— F => f x.1 x.2) (f.fpowerSeriesBilinear x) x :=
  âŸ¨âˆ, f.hasFPowerSeriesOnBall_bilinear xâŸ©
#align continuous_linear_map.has_fpower_series_at_bilinear ContinuousLinearMap.hasFPowerSeriesAt_bilinear

protected theorem analyticAt_bilinear (f : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (x : E Ã— F) :
    AnalyticAt ğ•œ (fun x : E Ã— F => f x.1 x.2) x :=
  (f.hasFPowerSeriesAt_bilinear x).analyticAt
#align continuous_linear_map.analytic_at_bilinear ContinuousLinearMap.analyticAt_bilinear

end ContinuousLinearMap

variable (ğ•œ)

lemma analyticAt_id (z : E) : AnalyticAt ğ•œ (id : E â†’ E) z :=
  (ContinuousLinearMap.id ğ•œ E).analyticAt z

/-- Scalar multiplication is analytic (jointly in both variables). The statement is a little
pedantic to allow towers of field extensions.

TODO: can we replace `ğ•œ'` with a "normed module" in such a way that `analyticAt_mul` is a special
case of this? -/
lemma analyticAt_smul
    {ğ• : Type*} [NormedField ğ•] [NormedAlgebra ğ•œ ğ•] [NormedSpace ğ• E] [IsScalarTower ğ•œ ğ• E]
    (z : ğ• Ã— E) : AnalyticAt ğ•œ (fun x : ğ• Ã— E â†¦ x.1 â€¢ x.2) z :=
  (ContinuousLinearMap.lsmul ğ•œ ğ•).analyticAt_bilinear z

/-- Multiplication in a normed algebra over `ğ•œ` is -/
lemma analyticAt_mul {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A] (z : A Ã— A) :
    AnalyticAt ğ•œ (fun x : A Ã— A â†¦ x.1 * x.2) z :=
  (ContinuousLinearMap.mul ğ•œ A).analyticAt_bilinear z

namespace AnalyticAt
variable {ğ•œ}

/-- Scalar multiplication of one analytic function by another. -/
lemma smul {ğ• : Type*} [NontriviallyNormedField ğ•] [NormedSpace ğ• F] [NormedAlgebra ğ•œ ğ•]
    [IsScalarTower ğ•œ ğ• F] {f : E â†’ ğ•} {g : E â†’ F} {z : E}
    (hf : AnalyticAt ğ•œ f z) (hg : AnalyticAt ğ•œ g z) :
    AnalyticAt ğ•œ (f â€¢ g) z :=
  @AnalyticAt.comp ğ•œ E (ğ• Ã— F) F _ _ _ _ _ _ _
    (fun x â†¦ x.1 â€¢ x.2) (fun e â†¦ (f e, g e)) z (analyticAt_smul _ _) (hf.prod hg)

/-- Multiplication of analytic functions (valued in a normd `ğ•œ`-algebra) is analytic. -/
lemma mul {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A]
    {f g : E â†’ A} {z : E}
    (hf : AnalyticAt ğ•œ f z) (hg : AnalyticAt ğ•œ g z) : AnalyticAt ğ•œ (f * g) z :=
  @AnalyticAt.comp ğ•œ E (A Ã— A) A _ _ _ _ _ _ _
    (fun x â†¦ x.1 * x.2) (fun e â†¦ (f e, g e)) z (analyticAt_mul _ (f z, g z)) (hf.prod hg)

/-- Powers of analytic functions (into a normed `ğ•œ`-algebra) are analytic. -/
lemma pow {A : Type*} [NormedRing A] [NormedAlgebra ğ•œ A]
    {f : E â†’ A} {z : E} (hf : AnalyticAt ğ•œ f z) (n : â„•) :
    AnalyticAt ğ•œ (f ^ n) z := by
  induction' n with m hm
  Â· rw [pow_zero]
    exact (analyticAt_const : AnalyticAt ğ•œ (fun _ â†¦ (1 : A)) z)
  Â· exact pow_succ f m â–¸ hf.mul hm

end AnalyticAt

/-- If `ğ•` is a normed field extension of `ğ•œ`, then the inverse map `ğ• â†’ ğ•` is `ğ•œ`-analytic
away from 0. -/
lemma analyticAt_inv {ğ• : Type*} [NontriviallyNormedField ğ•] [NormedAlgebra ğ•œ ğ•]
    {z : ğ•} (hz : z â‰  0) : AnalyticAt ğ•œ Inv.inv z := by
  let f1 : ğ• â†’ ğ• := fun a â†¦ 1 / z * a
  let f2 : ğ• â†’ ğ• := fun b â†¦ (1 - b)â»Â¹
  let f3 : ğ• â†’ ğ• := fun c â†¦ 1 - c / z
  have feq : f1 âˆ˜ f2 âˆ˜ f3 = Inv.inv
  Â· ext1 x
    dsimp only [Function.comp_apply]
    field_simp
  have f3val : f3 z = 0 := by simp only [div_self hz, sub_self]
  have f3an : AnalyticAt ğ•œ f3 z
  Â· apply analyticAt_const.sub
    simpa only [div_eq_inv_mul] using analyticAt_const.mul (analyticAt_id ğ•œ z)
  exact feq â–¸ (analyticAt_const.mul (analyticAt_id _ _)).comp
    ((f3val.symm â–¸ analyticAt_inv_one_sub ğ•).comp f3an)
