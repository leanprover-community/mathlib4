/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FormalMultilinearSeries
import Mathlib.Analysis.SpecificLimits.Normed

/-!
# Radius of convergence of a power series

This file introduces the notion of the radius of convergence of a power series.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : â„•`.

* `p.radius`: the largest `r : â„â‰¥0âˆ` such that `â€–p nâ€– * r^n` grows subexponentially.
* `p.le_radius_of_bound`, `p.le_radius_of_bound_nnreal`, `p.le_radius_of_isBigO`: if `â€–p nâ€– * r ^ n`
  is bounded above, then `r â‰¤ p.radius`;
* `p.isLittleO_of_lt_radius`, `p.norm_mul_pow_le_mul_pow_of_lt_radius`,
  `p.isLittleO_one_of_lt_radius`,
  `p.norm_mul_pow_le_of_lt_radius`, `p.nnnorm_mul_pow_le_of_lt_radius`: if `r < p.radius`, then
  `â€–p nâ€– * r ^ n` tends to zero exponentially;
* `p.lt_radius_of_isBigO`: if `r â‰  0` and `â€–p nâ€– * r ^ n = O(a ^ n)` for some `-1 < a < 1`, then
  `r < p.radius`;
* `p.partialSum n x`: the sum `âˆ‘_{i = 0}^{n-1} páµ¢ xâ±`.
* `p.sum x`: the sum `âˆ‘'_{i = 0}^{âˆ} páµ¢ xâ±`.

## Implementation details

We only introduce the radius of convergence of a power series, as `p.radius`.
For a power series in finitely many dimensions, there is a finer (directional, coordinate-dependent)
notion, describing the polydisk of convergence. This notion is more specific, and not necessary to
build the general theory. We do not define it here.
-/

noncomputable section

variable {ğ•œ E F G : Type*}

open Topology NNReal Filter ENNReal Set Asymptotics
open scoped Pointwise

namespace FormalMultilinearSeries

variable [Semiring ğ•œ] [AddCommMonoid E] [AddCommMonoid F] [Module ğ•œ E] [Module ğ•œ F]
variable [TopologicalSpace E] [TopologicalSpace F]
variable [ContinuousAdd E] [ContinuousAdd F]
variable [ContinuousConstSMul ğ•œ E] [ContinuousConstSMul ğ•œ F]

/-- Given a formal multilinear series `p` and a vector `x`, then `p.sum x` is the sum `Î£ pâ‚™ xâ¿`. A
priori, it only behaves well when `â€–xâ€– < p.radius`. -/
protected def sum (p : FormalMultilinearSeries ğ•œ E F) (x : E) : F :=
  âˆ‘' n : â„•, p n fun _ => x

/-- Given a formal multilinear series `p` and a vector `x`, then `p.partialSum n x` is the sum
`Î£ pâ‚– xáµ` for `k âˆˆ {0,..., n-1}`. -/
def partialSum (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) (x : E) : F :=
  âˆ‘ k âˆˆ Finset.range n, p k fun _ : Fin k => x

/-- The partial sums of a formal multilinear series are continuous. -/
theorem partialSum_continuous (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) :
    Continuous (p.partialSum n) := by
  unfold partialSum
  fun_prop

end FormalMultilinearSeries

/-! ### The radius of a formal multilinear series -/

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}

/-- The radius of a formal multilinear series is the largest `r` such that the sum `Î£ â€–pâ‚™â€– â€–yâ€–â¿`
converges for all `â€–yâ€– < r`. This implies that `Î£ pâ‚™ yâ¿` converges for all `â€–yâ€– < r`, but these
definitions are *not* equivalent in general. -/
def radius (p : FormalMultilinearSeries ğ•œ E F) : â„â‰¥0âˆ :=
  â¨† (r : â„â‰¥0) (C : â„) (_ : âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C), (r : â„â‰¥0âˆ)

/-- If `â€–pâ‚™â€– râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound (C : â„) {r : â„â‰¥0} (h : âˆ€ n : â„•, â€–p nâ€– * (r : â„) ^ n â‰¤ C) :
    (r : â„â‰¥0âˆ) â‰¤ p.radius :=
  le_iSup_of_le r <| le_iSup_of_le C <| le_iSup (fun _ => (r : â„â‰¥0âˆ)) h

/-- If `â€–pâ‚™â€– râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound_nnreal (C : â„â‰¥0) {r : â„â‰¥0} (h : âˆ€ n : â„•, â€–p nâ€–â‚Š * r ^ n â‰¤ C) :
    (r : â„â‰¥0âˆ) â‰¤ p.radius :=
  p.le_radius_of_bound C fun n => mod_cast h n

/-- If `â€–pâ‚™â€– râ¿ = O(1)`, as `n â†’ âˆ`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_isBigO (h : (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] fun _ => (1 : â„)) :
    â†‘r â‰¤ p.radius :=
  Exists.elim (isBigO_one_nat_atTop_iff.1 h) fun C hC =>
    p.le_radius_of_bound C fun n => (le_abs_self _).trans (hC n)

theorem le_radius_of_eventually_le (C) (h : âˆ€á¶  n in atTop, â€–p nâ€– * (r : â„) ^ n â‰¤ C) :
    â†‘r â‰¤ p.radius :=
  p.le_radius_of_isBigO <| IsBigO.of_bound C <| h.mono fun n hn => by simpa

theorem le_radius_of_summable_nnnorm (h : Summable fun n => â€–p nâ€–â‚Š * r ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_bound_nnreal (âˆ‘' n, â€–p nâ€–â‚Š * r ^ n) fun _ => h.le_tsum' _

theorem le_radius_of_summable (h : Summable fun n => â€–p nâ€– * (r : â„) ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_summable_nnnorm <| by
    simp only [â† coe_nnnorm] at h
    exact mod_cast h

theorem radius_eq_top_of_forall_nnreal_isBigO
    (h : âˆ€ r : â„â‰¥0, (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] fun _ => (1 : â„)) : p.radius = âˆ :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_isBigO (h r)

theorem radius_eq_top_of_eventually_eq_zero (h : âˆ€á¶  n in atTop, p n = 0) : p.radius = âˆ :=
  p.radius_eq_top_of_forall_nnreal_isBigO fun r =>
    (isBigO_zero _ _).congr' (h.mono fun n hn => by simp [hn]) EventuallyEq.rfl

theorem radius_eq_top_of_forall_image_add_eq_zero (n : â„•) (hn : âˆ€ m, p (m + n) = 0) :
    p.radius = âˆ :=
  p.radius_eq_top_of_eventually_eq_zero <|
    mem_atTop_sets.2 âŸ¨n, fun _ hk => tsub_add_cancel_of_le hk â–¸ hn _âŸ©

@[simp]
theorem constFormalMultilinearSeries_radius {v : F} :
    (constFormalMultilinearSeries ğ•œ E v).radius = âŠ¤ :=
  (constFormalMultilinearSeries ğ•œ E v).radius_eq_top_of_forall_image_add_eq_zero 1
    (by simp [constFormalMultilinearSeries])

/-- `0` has infinite radius of convergence -/
@[simp] lemma zero_radius : (0 : FormalMultilinearSeries ğ•œ E F).radius = âˆ := by
  rw [â† constFormalMultilinearSeries_zero]
  exact constFormalMultilinearSeries_radius

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` tends to zero exponentially:
for some `0 < a < 1`, `â€–p nâ€– râ¿ = o(aâ¿)`. -/
theorem isLittleO_of_lt_radius (h : â†‘r < p.radius) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1, (fun n => â€–p nâ€– * (r : â„) ^ n) =o[atTop] (a ^ Â·) := by
  have := (TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 4
  rw [this]
  -- Porting note: was
  -- rw [(TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 4]
  simp only [radius, lt_iSup_iff] at h
  rcases h with âŸ¨t, C, hC, rtâŸ©
  rw [ENNReal.coe_lt_coe, â† NNReal.coe_lt_coe] at rt
  have : 0 < (t : â„) := r.coe_nonneg.trans_lt rt
  rw [â† div_lt_one this] at rt
  refine âŸ¨_, rt, C, Or.inr zero_lt_one, fun n => ?_âŸ©
  calc
    |â€–p nâ€– * (r : â„) ^ n| = â€–p nâ€– * (t : â„) ^ n * (r / t : â„) ^ n := by
      simp [field, abs_mul, div_pow]
    _ â‰¤ C * (r / t : â„) ^ n := by gcongr; apply hC

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿ = o(1)`. -/
theorem isLittleO_one_of_lt_radius (h : â†‘r < p.radius) :
    (fun n => â€–p nâ€– * (r : â„) ^ n) =o[atTop] (fun _ => 1 : â„• â†’ â„) :=
  let âŸ¨_, ha, hpâŸ© := p.isLittleO_of_lt_radius h
  hp.trans <| (isLittleO_pow_pow_of_lt_left ha.1.le ha.2).congr (fun _ => rfl) one_pow

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` tends to zero exponentially:
for some `0 < a < 1` and `C > 0`, `â€–p nâ€– * r ^ n â‰¤ C * a ^ n`. -/
theorem norm_mul_pow_le_mul_pow_of_lt_radius (h : â†‘r < p.radius) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C * a ^ n := by
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 5).mp
    (p.isLittleO_of_lt_radius h)
  rcases this with âŸ¨a, ha, C, hC, HâŸ©
  exact âŸ¨a, ha, C, hC, fun n => (le_abs_self _).trans (H n)âŸ©

/-- If `r â‰  0` and `â€–pâ‚™â€– râ¿ = O(aâ¿)` for some `-1 < a < 1`, then `r < p.radius`. -/
theorem lt_radius_of_isBigO (hâ‚€ : r â‰  0) {a : â„} (ha : a âˆˆ Ioo (-1 : â„) 1)
    (hp : (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] (a ^ Â·)) : â†‘r < p.radius := by
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 2 5)
  rcases this.mp âŸ¨a, ha, hpâŸ© with âŸ¨a, ha, C, hC, hpâŸ©
  rw [â† pos_iff_ne_zero, â† NNReal.coe_pos] at hâ‚€
  lift a to â„â‰¥0 using ha.1.le
  have : (r : â„) < r / a := by
    simpa only [div_one] using (div_lt_div_iff_of_pos_left hâ‚€ zero_lt_one ha.1).2 ha.2
  norm_cast at this
  rw [â† ENNReal.coe_lt_coe] at this
  refine this.trans_le (p.le_radius_of_bound C fun n => ?_)
  rw [NNReal.coe_div, div_pow, â† mul_div_assoc, div_le_iffâ‚€ (pow_pos ha.1 n)]
  exact (le_abs_self _).trans (hp n)

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem norm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C :=
  let âŸ¨_, ha, C, hC, hâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  âŸ¨C, hC, fun n => (h n).trans <| mul_le_of_le_one_right hC.lt.le (pow_le_oneâ‚€ ha.1.le ha.2.le)âŸ©

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem norm_le_div_pow_of_pos_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h0 : 0 < r) (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€– â‰¤ C / (r : â„) ^ n :=
  let âŸ¨C, hC, hpâŸ© := p.norm_mul_pow_le_of_lt_radius h
  âŸ¨C, hC, fun n => Iff.mpr (le_div_iffâ‚€ (pow_pos h0 _)) (hp n)âŸ©

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem nnnorm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€–â‚Š * r ^ n â‰¤ C :=
  let âŸ¨C, hC, hpâŸ© := p.norm_mul_pow_le_of_lt_radius h
  âŸ¨âŸ¨C, hC.lt.leâŸ©, hC, mod_cast hpâŸ©

theorem le_radius_of_tendsto (p : FormalMultilinearSeries ğ•œ E F) {l : â„}
    (h : Tendsto (fun n => â€–p nâ€– * (r : â„) ^ n) atTop (ğ“ l)) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_isBigO (h.isBigO_one _)

theorem le_radius_of_summable_norm (p : FormalMultilinearSeries ğ•œ E F)
    (hs : Summable fun n => â€–p nâ€– * (r : â„) ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_tendsto hs.tendsto_atTop_zero

theorem not_summable_norm_of_radius_lt_nnnorm (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (h : p.radius < â€–xâ€–â‚Š) : Â¬Summable fun n => â€–p nâ€– * â€–xâ€– ^ n :=
  fun hs => not_le_of_gt h (p.le_radius_of_summable_norm hs)

theorem summable_norm_mul_pow (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0} (h : â†‘r < p.radius) :
    Summable fun n : â„• => â€–p nâ€– * (r : â„) ^ n := by
  obtain âŸ¨a, ha : a âˆˆ Ioo (0 : â„) 1, C, - : 0 < C, hpâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  exact .of_nonneg_of_le (fun _ â†¦ by positivity)
    hp ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _)

theorem summable_norm_apply (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : Summable fun n : â„• => â€–p n fun _ => xâ€– := by
  rw [mem_emetric_ball_zero_iff] at hx
  refine .of_nonneg_of_le
    (fun _ â†¦ norm_nonneg _) (fun n â†¦ ((p n).le_opNorm _).trans_eq ?_) (p.summable_norm_mul_pow hx)
  simp

theorem summable_nnnorm_mul_pow (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0} (h : â†‘r < p.radius) :
    Summable fun n : â„• => â€–p nâ€–â‚Š * r ^ n := by
  rw [â† NNReal.summable_coe]
  push_cast
  exact p.summable_norm_mul_pow h

protected theorem summable [CompleteSpace F] (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : Summable fun n : â„• => p n fun _ => x :=
  (p.summable_norm_apply hx).of_norm

theorem radius_eq_top_of_summable_norm (p : FormalMultilinearSeries ğ•œ E F)
    (hs : âˆ€ r : â„â‰¥0, Summable fun n => â€–p nâ€– * (r : â„) ^ n) : p.radius = âˆ :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_summable_norm (hs r)

theorem radius_eq_top_iff_summable_norm (p : FormalMultilinearSeries ğ•œ E F) :
    p.radius = âˆ â†” âˆ€ r : â„â‰¥0, Summable fun n => â€–p nâ€– * (r : â„) ^ n := by
  constructor
  Â· intro h r
    obtain âŸ¨a, ha : a âˆˆ Ioo (0 : â„) 1, C, - : 0 < C, hpâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius
      (show (r : â„â‰¥0âˆ) < p.radius from h.symm â–¸ ENNReal.coe_lt_top)
    refine .of_norm_bounded
      (g := fun n â†¦ (C : â„) * a ^ n) ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _)
      fun n â†¦ ?_
    specialize hp n
    rwa [Real.norm_of_nonneg (by positivity)]
  Â· exact p.radius_eq_top_of_summable_norm

/-- If the radius of `p` is positive, then `â€–pâ‚™â€–` grows at most geometrically. -/
theorem le_mul_pow_of_radius_pos (p : FormalMultilinearSeries ğ•œ E F) (h : 0 < p.radius) :
    âˆƒ (C r : _) (_ : 0 < C) (_ : 0 < r), âˆ€ n, â€–p nâ€– â‰¤ C * r ^ n := by
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 h with âŸ¨r, r0, rltâŸ©
  have rpos : 0 < (r : â„) := by simp [ENNReal.coe_pos.1 r0]
  rcases norm_le_div_pow_of_pos_of_lt_radius p rpos rlt with âŸ¨C, Cpos, hCpâŸ©
  refine âŸ¨C, râ»Â¹, Cpos, by simp only [inv_pos, rpos], fun n => ?_âŸ©
  rw [inv_pow, â† div_eq_mul_inv]
  exact hCp n

lemma radius_le_of_le {ğ•œ' E' F' : Type*}
    [NontriviallyNormedField ğ•œ'] [NormedAddCommGroup E'] [NormedSpace ğ•œ' E']
    [NormedAddCommGroup F'] [NormedSpace ğ•œ' F']
    {p : FormalMultilinearSeries ğ•œ E F} {q : FormalMultilinearSeries ğ•œ' E' F'}
    (h : âˆ€ n, â€–p nâ€– â‰¤ â€–q nâ€–) : q.radius â‰¤ p.radius := by
  apply le_of_forall_nnreal_lt (fun r hr â†¦ ?_)
  rcases norm_mul_pow_le_of_lt_radius _ hr with âŸ¨C, -, hCâŸ©
  apply le_radius_of_bound _ C (fun n â†¦ ?_)
  apply le_trans _ (hC n)
  gcongr
  exact h n

/-- The radius of the sum of two formal series is at least the minimum of their two radii. -/
theorem min_radius_le_radius_add (p q : FormalMultilinearSeries ğ•œ E F) :
    min p.radius q.radius â‰¤ (p + q).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  rw [lt_min_iff] at hr
  have := ((p.isLittleO_one_of_lt_radius hr.1).add (q.isLittleO_one_of_lt_radius hr.2)).isBigO
  refine (p + q).le_radius_of_isBigO ((isBigO_of_le _ fun n => ?_).trans this)
  rw [â† add_mul, norm_mul, norm_mul, norm_norm]
  gcongr
  exact (norm_add_le _ _).trans (le_abs_self _)

@[simp]
theorem radius_neg (p : FormalMultilinearSeries ğ•œ E F) : (-p).radius = p.radius := by
  simp only [radius, neg_apply, norm_neg]

theorem radius_le_smul {p : FormalMultilinearSeries ğ•œ E F} {c : ğ•œ} : p.radius â‰¤ (c â€¢ p).radius := by
  simp only [radius, smul_apply]
  refine iSup_mono fun r â†¦ iSup_mono' fun C â†¦ âŸ¨â€–câ€– * C, iSup_mono' fun h â†¦ ?_âŸ©
  simp only [le_refl, exists_prop, and_true]
  intro n
  rw [norm_smul c (p n), mul_assoc]
  gcongr
  exact h n

theorem radius_smul_eq (p : FormalMultilinearSeries ğ•œ E F) {c : ğ•œ} (hc : c â‰  0) :
    (c â€¢ p).radius = p.radius := by
  apply eq_of_le_of_ge _ radius_le_smul
  exact radius_le_smul.trans_eq (congr_arg _ <| inv_smul_smulâ‚€ hc p)

lemma norm_compContinuousLinearMap_le (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) (n : â„•) :
    â€–p.compContinuousLinearMap u nâ€– â‰¤ â€–p nâ€– * â€–uâ€– ^ n := by
  simp only [compContinuousLinearMap]
  apply le_trans (ContinuousMultilinearMap.norm_compContinuousLinearMap_le _ _)
  simp

lemma enorm_compContinuousLinearMap_le (p : FormalMultilinearSeries ğ•œ F G)
    (u : E â†’L[ğ•œ] F) (n : â„•) : â€–p.compContinuousLinearMap u nâ€–â‚‘ â‰¤ â€–p nâ€–â‚‘ * â€–uâ€–â‚‘ ^ n := by
  rw [â† ofReal_norm, â† ofReal_norm, â† ofReal_norm,
    â† ENNReal.ofReal_pow (by simp), â† ENNReal.ofReal_mul (by simp)]
  gcongr
  apply norm_compContinuousLinearMap_le

lemma nnnorm_compContinuousLinearMap_le (p : FormalMultilinearSeries ğ•œ F G)
    (u : E â†’L[ğ•œ] F) (n : â„•) : â€–p.compContinuousLinearMap u nâ€–â‚Š â‰¤ â€–p nâ€–â‚Š * â€–uâ€–â‚Š ^ n :=
  norm_compContinuousLinearMap_le p u n

theorem div_le_radius_compContinuousLinearMap (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) :
    p.radius / â€–uâ€–â‚‘ â‰¤ (p.compContinuousLinearMap u).radius := by
  obtain (rfl | h_zero) := eq_zero_or_nnnorm_pos u
  Â· simp
  rw [ENNReal.div_le_iff (by simpa using h_zero) (by simp)]
  refine le_of_forall_nnreal_lt fun r hr â†¦ ?_
  rw [â† ENNReal.div_le_iff (by simpa using h_zero) (by simp), enorm_eq_nnnorm, â† coe_div h_zero.ne']
  obtain âŸ¨C, hC_pos, hCâŸ© := p.norm_mul_pow_le_of_lt_radius hr
  refine le_radius_of_bound _ C fun n â†¦ ?_
  calc
    â€–p.compContinuousLinearMap u nâ€– * â†‘(r / â€–uâ€–â‚Š) ^ n â‰¤ â€–p nâ€– * â€–uâ€– ^ n * â†‘(r / â€–uâ€–â‚Š) ^ n := by
      gcongr
      exact nnnorm_compContinuousLinearMap_le p u n
    _ = â€–p nâ€– * r ^ n := by
      simp only [NNReal.coe_div, coe_nnnorm, div_pow, mul_assoc]
      rw [mul_div_cancelâ‚€]
      rw [â† NNReal.coe_pos] at h_zero
      positivity
    _ â‰¤ C := hC n

theorem le_radius_compContinuousLinearMap (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’â‚—áµ¢[ğ•œ] F) :
    p.radius â‰¤ (p.compContinuousLinearMap u.toContinuousLinearMap).radius := by
  obtain (h | h) := subsingleton_or_nontrivial E
  Â· simp [Subsingleton.elim u.toContinuousLinearMap 0]
  Â· simpa [u.norm_toContinuousLinearMap]
      using div_le_radius_compContinuousLinearMap p u.toContinuousLinearMap

theorem radius_compContinuousLinearMap_le [Nontrivial F]
    (p : FormalMultilinearSeries ğ•œ F G) (u : E â‰ƒL[ğ•œ] F) :
    (p.compContinuousLinearMap u.toContinuousLinearMap).radius â‰¤
    â€–u.symm.toContinuousLinearMapâ€–â‚‘ * p.radius := by
  have := (p.compContinuousLinearMap u.toContinuousLinearMap).div_le_radius_compContinuousLinearMap
    u.symm.toContinuousLinearMap
  simp only [compContinuousLinearMap_comp, ContinuousLinearEquiv.coe_comp_coe_symm,
    compContinuousLinearMap_id] at this
  rwa [ENNReal.div_le_iff' (by simpa [DFunLike.ext_iff] using exists_ne 0) (by simp)] at this

@[simp]
theorem radius_compContinuousLinearMap_linearIsometryEquiv_eq [Nontrivial E]
    (p : FormalMultilinearSeries ğ•œ F G) (u : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    (p.compContinuousLinearMap u.toLinearIsometry.toContinuousLinearMap).radius = p.radius := by
  refine le_antisymm ?_ <| le_radius_compContinuousLinearMap _ _
  have _ : Nontrivial F := u.symm.toEquiv.nontrivial
  convert radius_compContinuousLinearMap_le p u.toContinuousLinearEquiv
  have : u.toContinuousLinearEquiv.symm.toContinuousLinearMap =
    u.symm.toLinearIsometry.toContinuousLinearMap := rfl
  simp [this]

/-- This is a version of `radius_compContinuousLinearMap_linearIsometryEquiv_eq` with better
opportunity for unification, at the cost of manually supplying some hypotheses. -/
theorem radius_compContinuousLinearMap_eq [Nontrivial E]
    (p : FormalMultilinearSeries ğ•œ F G) (u : E â†’L[ğ•œ] F) (hu_iso : Isometry u)
    (hu_surj : Function.Surjective u) :
    (p.compContinuousLinearMap u).radius = p.radius :=
  let v : E â‰ƒâ‚—áµ¢[ğ•œ] F :=
    { LinearEquiv.ofBijective u.toLinearMap âŸ¨hu_iso.injective, hu_surjâŸ© with
      norm_map' := hu_iso.norm_map_of_map_zero (map_zero u) }
  radius_compContinuousLinearMap_linearIsometryEquiv_eq p v

@[simp]
theorem radius_compNeg [Nontrivial E] (p : FormalMultilinearSeries ğ•œ E F) :
    (p.compContinuousLinearMap (-(.id _ _))).radius = p.radius :=
  radius_compContinuousLinearMap_linearIsometryEquiv_eq _ (.neg ğ•œ)

@[simp]
theorem radius_shift (p : FormalMultilinearSeries ğ•œ E F) : p.shift.radius = p.radius := by
  simp only [radius, shift, Nat.succ_eq_add_one, ContinuousMultilinearMap.curryRight_norm]
  congr
  ext r
  apply eq_of_le_of_ge
  Â· apply iSup_mono'
    intro C
    use â€–p 0â€– âŠ” (C * r)
    apply iSup_mono'
    intro h
    simp only [le_refl, le_sup_iff, exists_prop, and_true]
    intro n
    rcases n with - | m
    Â· simp
    right
    rw [pow_succ, â† mul_assoc]
    gcongr; apply h
  Â· apply iSup_mono'
    intro C
    use â€–p 1â€– âŠ” C / r
    apply iSup_mono'
    intro h
    simp only [le_refl, le_sup_iff, exists_prop, and_true]
    intro n
    cases eq_zero_or_pos r with
    | inl hr =>
      rw [hr]
      cases n <;> simp
    | inr hr =>
      right
      rw [â† NNReal.coe_pos] at hr
      specialize h (n + 1)
      rw [le_div_iffâ‚€ hr]
      rwa [pow_succ, â† mul_assoc] at h

@[simp]
theorem radius_unshift (p : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F)) (z : F) :
    (p.unshift z).radius = p.radius := by
  rw [â† radius_shift, unshift_shift]

protected theorem hasSum [CompleteSpace F] (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : HasSum (fun n : â„• => p n fun _ => x) (p.sum x) :=
  (p.summable hx).hasSum

theorem radius_le_radius_continuousLinearMap_comp (p : FormalMultilinearSeries ğ•œ E F)
    (f : F â†’L[ğ•œ] G) : p.radius â‰¤ (f.compFormalMultilinearSeries p).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  apply le_radius_of_isBigO
  apply (IsBigO.trans_isLittleO _ (p.isLittleO_one_of_lt_radius hr)).isBigO
  refine IsBigO.mul (@IsBigOWith.isBigO _ _ _ _ _ â€–fâ€– _ _ _ ?_) (isBigO_refl _ _)
  refine IsBigOWith.of_bound (Eventually.of_forall fun n => ?_)
  simpa only [norm_norm] using f.norm_compContinuousMultilinearMap_le (p n)

end FormalMultilinearSeries
