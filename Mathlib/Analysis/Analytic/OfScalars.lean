/-
Copyright (c) 2024 Edward Watine. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Edward Watine
-/
import Mathlib.Analysis.Analytic.Basic

/-!
# Scalar series

This file contains API for analytic functions `‚àë c·µ¢ ‚Ä¢ x‚Å±` defined in terms of scalars
`c‚ÇÄ, c‚ÇÅ, c‚ÇÇ, ‚Ä¶`.
## Main definitions / results:
 * `FormalMultilinearSeries.ofScalars`: the formal power series `‚àë c·µ¢ ‚Ä¢ x‚Å±`.
 * `FormalMultilinearSeries.ofScalarsSum`: the sum of such a power series, if it exists, and zero
   otherwise.
 * `FormalMultilinearSeries.ofScalars_radius_eq_(zero/inv/top)_of_tendsto`:
   the ratio test for an analytic function defined in terms of a formal power series `‚àë c·µ¢ ‚Ä¢ x‚Å±`.
 * `FormalMultilinearSeries.ofScalars_radius_eq_inv_of_tendsto_ENNReal`:
   the ratio test for an analytic function using ENNReal division for all values `‚Ñù‚â•0‚àû`.
-/

namespace FormalMultilinearSeries

section Field

variable {ùïú : Type*} (E : Type*) [Field ùïú] [Ring E] [Algebra ùïú E] [TopologicalSpace E]
  [TopologicalRing E] (c : ‚Ñï ‚Üí ùïú)

/-- Formal power series of `‚àë c·µ¢ ‚Ä¢ x‚Å±` for some scalar field `ùïú` and ring algebra `E`-/
def ofScalars : FormalMultilinearSeries ùïú E E :=
  fun n ‚Ü¶ c n ‚Ä¢ ContinuousMultilinearMap.mkPiAlgebraFin ùïú n E

variable {E}

/-- The sum of the formal power series. Takes the value `0` outside the radius of convergence. -/
noncomputable def ofScalarsSum (x : E) := (ofScalars E c).sum x

theorem ofScalars_apply_eq (x : E) (n : ‚Ñï) :
    ofScalars E c n (fun _ ‚Ü¶ x) = c n ‚Ä¢ x ^ n := by
  simp [ofScalars]

/-- This naming follows the convention of `NormedSpace.expSeries_apply_eq'`. -/
theorem ofScalars_apply_eq' (x : E) :
    (fun n ‚Ü¶ ofScalars E c n (fun _ ‚Ü¶ x)) = fun n ‚Ü¶ c n ‚Ä¢ x ^ n := by
  simp [ofScalars]

theorem ofScalars_sum_eq (x : E) : ofScalarsSum c x =
    ‚àë' n, c n ‚Ä¢ x ^ n := tsum_congr fun n => ofScalars_apply_eq c x n

theorem ofScalarsSum_eq_tsum : ofScalarsSum c =
    fun (x : E) => ‚àë' n : ‚Ñï, c n ‚Ä¢ x ^ n := funext (ofScalars_sum_eq c)

theorem ofScalars_apply_zero (n : ‚Ñï) :
    (ofScalars E c n fun _ => 0) = Pi.single (f := fun _ => E) 0 (c 0 ‚Ä¢ 1) n := by
  rw [ofScalars]
  cases n <;> simp

@[simp]
theorem ofScalarsSum_zero : ofScalarsSum c (0 : E) = c 0 ‚Ä¢ 1 := by
  simp [ofScalarsSum_eq_tsum, ‚Üê ofScalars_apply_eq, ofScalars_apply_zero]

@[simp]
theorem ofScalars_of_subsingleton [Subsingleton E] {n : ‚Ñï} : ofScalars E c n = 0 := by
  rw [ofScalars, smul_eq_zero (c := c n) (x := ContinuousMultilinearMap.mkPiAlgebraFin ùïú n E)]
  exact Or.inr (ContinuousMultilinearMap.ext fun f ‚Ü¶ Subsingleton.allEq _ _)

@[simp]
theorem ofScalarsSum_of_subsingleton [Subsingleton E] {x : E} : ofScalarsSum c x = 0 := by
  simp [Subsingleton.eq_zero x, Subsingleton.eq_zero (1 : E)]

@[simp]
theorem ofScalars_op [T2Space E] (x : E) :
    ofScalarsSum c (MulOpposite.op x) = MulOpposite.op (ofScalarsSum c x) := by
  simp [ofScalars, ofScalars_sum_eq, ‚Üê MulOpposite.op_pow, ‚Üê MulOpposite.op_smul, tsum_op]

@[simp]
theorem ofScalars_unop [T2Space E] (x : E·µê·µí·µñ) :
    ofScalarsSum c (MulOpposite.unop x) = MulOpposite.unop (ofScalarsSum c x) := by
  simp [ofScalars, ofScalars_sum_eq, ‚Üê MulOpposite.unop_pow, ‚Üê MulOpposite.unop_smul, tsum_unop]

@[simp]
theorem ofScalars_eq_zero [Nontrivial E] (n : ‚Ñï) : ofScalars E c n = 0 ‚Üî c n = 0 := by
  rw [ofScalars, smul_eq_zero (c := c n) (x := ContinuousMultilinearMap.mkPiAlgebraFin ùïú n E)]
  refine or_iff_left (ContinuousMultilinearMap.ext_iff.1.mt <| not_forall_of_exists_not ?_)
  use fun _ ‚Ü¶ 1
  simp

@[simp]
theorem ofScalars_eq_zero_of_scalar_zero (n : ‚Ñï) (hc : c n = 0) : ofScalars E c n = 0 := by
  rw [ofScalars, hc, zero_smul ùïú (ContinuousMultilinearMap.mkPiAlgebraFin ùïú n E)]

end Field

section Normed

open Filter ENNReal
open scoped Topology NNReal

variable {ùïú : Type*} (E : Type*) [NontriviallyNormedField ùïú] [NormedRing E]
    [NormedAlgebra ùïú E] (c : ‚Ñï ‚Üí ùïú) (n : ‚Ñï)

theorem ofScalars_norm_eq_mul :
    ‚ÄñofScalars E c n‚Äñ = ‚Äñc n‚Äñ * ‚ÄñContinuousMultilinearMap.mkPiAlgebraFin ùïú n E‚Äñ := by
  rw [ofScalars, norm_smul (c n) (ContinuousMultilinearMap.mkPiAlgebraFin ùïú n E)]

theorem ofScalars_norm_le (hn : n > 0) : ‚ÄñofScalars E c n‚Äñ ‚â§ ‚Äñc n‚Äñ := by
  simp only [ofScalars_norm_eq_mul]
  exact (mul_le_of_le_one_right (norm_nonneg _)
    (ContinuousMultilinearMap.norm_mkPiAlgebraFin_le_of_pos hn))

@[simp]
theorem ofScalars_norm [NormOneClass E] : ‚ÄñofScalars E c n‚Äñ = ‚Äñc n‚Äñ := by
  simp [ofScalars_norm_eq_mul]

private theorem tendsto_succ_norm_div_norm {r r' : ‚Ñù‚â•0} (hr' : r' ‚â† 0)
    (hc : Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop (ùìù r)) :
      Tendsto (fun n ‚Ü¶ ‚Äñ‚Äñc (n + 1)‚Äñ * r' ^ (n + 1)‚Äñ /
        ‚Äñ‚Äñc n‚Äñ * r' ^ n‚Äñ) atTop (ùìù ‚Üë(r' * r)) := by
  simp_rw [norm_mul, norm_norm, mul_div_mul_comm, ‚Üê norm_div, pow_succ,
    mul_div_right_comm, div_self (pow_ne_zero _ (NNReal.coe_ne_zero.mpr hr')
    ), one_mul, norm_div, NNReal.norm_eq]
  exact mul_comm r' r ‚ñ∏ hc.mul tendsto_const_nhds

theorem ofScalars_radius_le_inv_of_tendsto {r : ‚Ñù‚â•0} (hr : r ‚â† 0)
    (hc : Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop (ùìù r)) :
      (ofScalars E c).radius ‚â• ofNNReal r‚Åª¬π := by
  refine le_of_forall_nnreal_lt (fun r' hr' ‚Ü¶ ?_)
  rw [coe_lt_coe, NNReal.lt_inv_iff_mul_lt hr] at hr'
  by_cases hrz : r' = 0
  ¬∑ simp [hrz]
  ¬∑ apply FormalMultilinearSeries.le_radius_of_summable_norm
    refine Summable.of_norm_bounded_eventually (fun n ‚Ü¶ ‚Äñ‚Äñc n‚Äñ * r' ^ n‚Äñ) ?_ ?_
    ¬∑ refine summable_of_ratio_test_tendsto_lt_one hr' ?_ ?_
      ¬∑ refine (hc.eventually_ne (NNReal.coe_ne_zero.mpr hr)).mp (Eventually.of_forall ?_)
        aesop
      ¬∑ simp_rw [norm_norm]
        exact tendsto_succ_norm_div_norm c hrz hc
    ¬∑ filter_upwards [eventually_cofinite_ne 0] with n hn
      simp only [norm_mul, norm_norm, norm_pow, NNReal.norm_eq]
      gcongr
      exact ofScalars_norm_le E c n (Nat.pos_iff_ne_zero.mpr hn)

/-- The radius of convergence of a scalar series is the inverse of the non-zero limit
`fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ`. -/
theorem ofScalars_radius_eq_inv_of_tendsto [NormOneClass E] {r : ‚Ñù‚â•0} (hr : r ‚â† 0)
    (hc : Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop (ùìù r)) :
      (ofScalars E c).radius = ofNNReal r‚Åª¬π := by
  refine le_antisymm ?_ (ofScalars_radius_le_inv_of_tendsto E c hr hc)
  refine le_of_forall_nnreal_lt (fun r' hr' ‚Ü¶ ?_)
  rw [coe_le_coe, NNReal.le_inv_iff_mul_le hr]
  have := FormalMultilinearSeries.summable_norm_mul_pow _ hr'
  contrapose! this
  apply not_summable_of_ratio_test_tendsto_gt_one this
  simp_rw [ofScalars_norm]
  exact tendsto_succ_norm_div_norm c (by aesop) hc

/-- A convenience lemma restating the result of `ofScalars_radius_eq_inv_of_tendsto` under
the inverse ratio. -/
theorem ofScalars_radius_eq_of_tendsto [NormOneClass E] {r : NNReal} (hr : r ‚â† 0)
    (hc : Tendsto (fun n ‚Ü¶ ‚Äñc n‚Äñ / ‚Äñc n.succ‚Äñ) atTop (ùìù r)) :
      (ofScalars E c).radius = ofNNReal r := by
  suffices Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop (ùìù r‚Åª¬π) by
    convert ofScalars_radius_eq_inv_of_tendsto E c (inv_ne_zero hr) this
    simp
  convert hc.inv‚ÇÄ (NNReal.coe_ne_zero.mpr hr) using 1
  simp

/-- The ratio test stating that if `‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ` tends to zero, the radius is unbounded.
This requires that the coefficients are eventually non-zero as
`‚Äñc n.succ‚Äñ / 0 = 0` by convention. -/
theorem ofScalars_radius_eq_top_of_tendsto (hc : ‚àÄ·∂† n in atTop, c n ‚â† 0)
    (hc' : Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop (ùìù 0)) : (ofScalars E c).radius = ‚ä§ := by
  refine radius_eq_top_of_summable_norm _ fun r' ‚Ü¶ ?_
  by_cases hrz : r' = 0
  ¬∑ apply Summable.comp_nat_add (k := 1)
    simp [hrz]
    exact (summable_const_iff 0).mpr rfl
  ¬∑ refine Summable.of_norm_bounded_eventually (fun n ‚Ü¶ ‚Äñ‚Äñc n‚Äñ * r' ^ n‚Äñ) ?_ ?_
    ¬∑ apply summable_of_ratio_test_tendsto_lt_one zero_lt_one (hc.mp (Eventually.of_forall ?_))
      ¬∑ simp only [norm_norm]
        exact mul_zero (_ : ‚Ñù) ‚ñ∏ tendsto_succ_norm_div_norm _ hrz (NNReal.coe_zero ‚ñ∏ hc')
      ¬∑ aesop
    ¬∑ filter_upwards [eventually_cofinite_ne 0] with n hn
      simp only [norm_mul, norm_norm, norm_pow, NNReal.norm_eq]
      gcongr
      exact ofScalars_norm_le E c n (Nat.pos_iff_ne_zero.mpr hn)

/-- If `‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ` is unbounded, then the radius of convergence is zero. -/
theorem ofScalars_radius_eq_zero_of_tendsto [NormOneClass E]
    (hc : Tendsto (fun n ‚Ü¶ ‚Äñc n.succ‚Äñ / ‚Äñc n‚Äñ) atTop atTop) : (ofScalars E c).radius = 0 := by
  suffices (ofScalars E c).radius ‚â§ 0 by aesop
  refine le_of_forall_nnreal_lt (fun r hr ‚Ü¶ ?_)
  rw [‚Üê coe_zero, coe_le_coe]
  have := FormalMultilinearSeries.summable_norm_mul_pow _ hr
  contrapose! this
  apply not_summable_of_ratio_norm_eventually_ge one_lt_two
  ¬∑ contrapose! hc
    apply not_tendsto_atTop_of_tendsto_nhds (a:=0)
    rw [not_frequently] at hc
    apply Tendsto.congr' ?_ tendsto_const_nhds
    filter_upwards [hc] with n hc'
    rw [ofScalars_norm, norm_mul, norm_norm, not_ne_iff, mul_eq_zero] at hc'
    cases hc' <;> aesop
  ¬∑ filter_upwards [hc.eventually_ge_atTop (2*r‚Åª¬π), eventually_ne_atTop 0] with n hc hn
    simp only [ofScalars_norm, norm_mul, norm_norm, norm_pow, NNReal.norm_eq]
    rw [mul_comm ‚Äñc n‚Äñ, ‚Üê mul_assoc, ‚Üê div_le_div_iff, mul_div_assoc]
    ¬∑ convert hc
      rw [pow_succ, div_mul_cancel_left‚ÇÄ, NNReal.coe_inv]
      aesop
    ¬∑ aesop
    ¬∑ refine Ne.lt_of_le (fun hr' ‚Ü¶ Not.elim ?_ hc) (norm_nonneg _)
      rw [‚Üê hr']
      simp [this]

/-- This theorem combines the results of the .-/
theorem ofScalars_radius_eq_inv_of_tendsto_ENNReal [NormOneClass E] {r : ‚Ñù‚â•0‚àû}
    (hc' : Tendsto (fun n ‚Ü¶ ENNReal.ofReal ‚Äñc n.succ‚Äñ / ENNReal.ofReal ‚Äñc n‚Äñ) atTop (ùìù r)) :
      (ofScalars E c).radius = r‚Åª¬π := by
  rcases ENNReal.trichotomy r with (hr | hr | hr)
  ¬∑ simp_rw [hr, inv_zero] at hc' ‚ä¢
    by_cases h : (‚àÄ·∂† (n : ‚Ñï) in atTop, c n ‚â† 0)
    ¬∑ apply ofScalars_radius_eq_top_of_tendsto E c h ?_
      refine Tendsto.congr' ?_ <| (tendsto_toReal zero_ne_top).comp hc'
      filter_upwards [h]
      simp
    ¬∑ apply (ofScalars E c).radius_eq_top_of_eventually_eq_zero
      simp only [eventually_atTop, not_exists, not_forall, Classical.not_imp, not_not] at h ‚ä¢
      obtain ‚ü®ti, hti‚ü© := eventually_atTop.mp (hc'.eventually_ne zero_ne_top)
      obtain ‚ü®zi, hzi, z‚ü© := h ti
      refine ‚ü®zi, fun n hn ‚Ü¶ ?_‚ü©
      induction n, hn using Nat.le_induction with
      | base => exact ofScalars_eq_zero_of_scalar_zero c zi z
      | succ n hmn a =>
        nontriviality E
        simp only [ofScalars_eq_zero] at a ‚ä¢
        contrapose! hti
        exact ‚ü®n, hzi.trans hmn, ENNReal.div_eq_top.mpr (by simp [a, hti])‚ü©
  ¬∑ simp_rw [hr, inv_top] at hc' ‚ä¢
    apply ofScalars_radius_eq_zero_of_tendsto E c ((tendsto_add_atTop_iff_nat 1).mp ?_)
    refine tendsto_ofReal_nhds_top.mp (Tendsto.congr' ?_ ((tendsto_add_atTop_iff_nat 1).mpr hc'))
    filter_upwards [hc'.eventually_ne top_ne_zero] with n hn
    apply (ofReal_div_of_pos (Ne.lt_of_le (Ne.symm ?_) (norm_nonneg _))).symm
    aesop
  ¬∑ have hr' := toReal_ne_zero.mp hr.ne.symm
    have hr'' := toNNReal_ne_zero.mpr hr' -- this result could go in ENNReal
    convert ofScalars_radius_eq_inv_of_tendsto E c hr'' ?_
    ¬∑ simp [ENNReal.coe_inv hr'', ENNReal.coe_toNNReal (toReal_ne_zero.mp hr.ne.symm).2]
    ¬∑ simp_rw [ENNReal.coe_toNNReal_eq_toReal]
      refine Tendsto.congr' ?_ <| (tendsto_toReal hr'.2).comp hc'
      filter_upwards [hc'.eventually_ne hr'.1, hc'.eventually_ne hr'.2]
      simp

end Normed

end FormalMultilinearSeries
