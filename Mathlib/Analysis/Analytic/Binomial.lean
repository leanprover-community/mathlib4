/-
Copyright (c) 2025 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
module

public import Mathlib.Analysis.Calculus.IteratedDeriv.ConvergenceOnBall
public import Mathlib.Analysis.Complex.OperatorNorm
public import Mathlib.Analysis.SpecialFunctions.Complex.Analytic
public import Mathlib.Analysis.SpecialFunctions.OrdinaryHypergeometric
public import Mathlib.Analysis.SpecialFunctions.Pow.Deriv
public import Mathlib.RingTheory.Binomial

/-!
# Binomial Series

This file introduces the binomial series:
$$
\sum_{k=0}^{\infty} \; \binom{a}{k} \; x^k = 1 + a x + \frac{a(a-1)}{2!} x^2 +
  \frac{a(a-1)(a-2)}{3!} x^3 + \cdots
$$
where $a$ is an element of a normed field $\mathbb{K}$,
and $x$ is an element of a normed algebra over $\mathbb{K}$.

## Main Statements

* `binomialSeries_radius_eq_one`: The radius of convergence of the binomial series is `1` when `a`
  is not a natural number.
* `binomialSeries_radius_eq_top_of_nat`: In case `a` is natural, the series converges everywhere,
  since it is finite.
-/

@[expose] public section

open scoped Nat

universe u v

@[norm_cast]
lemma Complex.ofReal_choose (a : ‚Ñù) (n : ‚Ñï) :
    ‚Üë(Ring.choose a n) = Ring.choose (a : ‚ÑÇ) n :=
  Ring.map_choose (algebraMap ‚Ñù ‚ÑÇ) _ _

/-- **Binomial series**: the (scalar) formal multilinear series with coefficients given
by `Ring.choose a`. The sum of this series is `fun x ‚Ü¶ (1 + x) ^ a` within the radius
of convergence. -/
noncomputable def binomialSeries {ùïÇ : Type u} [Field ùïÇ] [CharZero ùïÇ] (ùî∏ : Type v)
    [Ring ùî∏] [Algebra ùïÇ ùî∏] [TopologicalSpace ùî∏] [IsTopologicalRing ùî∏] (a : ùïÇ) :
    FormalMultilinearSeries ùïÇ ùî∏ ùî∏ :=
  .ofScalars ùî∏ (Ring.choose a ¬∑)

@[simp]
theorem binomialSeries_apply {ùïÇ : Type u} [Field ùïÇ] [CharZero ùïÇ] (ùî∏ : Type v)
    [Ring ùî∏] [Algebra ùïÇ ùî∏] [TopologicalSpace ùî∏] [IsTopologicalRing ùî∏] (a : ùïÇ) {n} (v : Fin n ‚Üí ùî∏) :
    binomialSeries ùî∏ a n v = Ring.choose a n ‚Ä¢ (List.ofFn v).prod := by
  simp [binomialSeries, FormalMultilinearSeries.ofScalars]

theorem binomialSeries_eq_ordinaryHypergeometricSeries {ùïÇ : Type u} [Field ùïÇ] [CharZero ùïÇ]
    {ùî∏ : Type v} [Ring ùî∏] [Algebra ùïÇ ùî∏] [TopologicalSpace ùî∏] [IsTopologicalRing ùî∏] {a b : ùïÇ}
    (h : ‚àÄ (k : ‚Ñï), (k : ùïÇ) ‚â† -b) :
    binomialSeries ùî∏ a =
    (ordinaryHypergeometricSeries ùî∏ (-a) b b).compContinuousLinearMap (-(.id _ _)) := by
  simp only [binomialSeries, ordinaryHypergeometricSeries,
    FormalMultilinearSeries.ofScalars_comp_neg_id]
  congr! with n
  rw [ordinaryHypergeometricCoefficient,
    mul_inv_cancel_right‚ÇÄ (by simp [ascPochhammer_eval_eq_zero_iff]; grind)]
  simp only [Ring.choose_eq_smul, Polynomial.descPochhammer_smeval_eq_ascPochhammer,
    Polynomial.ascPochhammer_smeval_cast, Polynomial.ascPochhammer_smeval_eq_eval,
    ascPochhammer_eval_neg_eq_descPochhammer, descPochhammer_eval_eq_ascPochhammer]
  ring_nf
  simp

/-- The radius of convergence of `binomialSeries ùî∏ a` is `‚ä§` for natural `a`. -/
theorem binomialSeries_radius_eq_top_of_nat {ùïÇ : Type v} [RCLike ùïÇ] {ùî∏ : Type u}
    [NormedDivisionRing ùî∏] [NormedAlgebra ùïÇ ùî∏] {a : ‚Ñï} :
    (binomialSeries ùî∏ (a : ùïÇ)).radius = ‚ä§ := by
  simp [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ùïÇ)) (by norm_cast; simp),
    ordinaryHypergeometric_radius_top_of_neg_nat‚ÇÅ]

/-- The radius of convergence of `binomialSeries ùî∏ a` is `1`, when `a` is not natural. -/
theorem binomialSeries_radius_eq_one {ùïÇ : Type v} [RCLike ùïÇ] {ùî∏ : Type u} [NormedDivisionRing ùî∏]
    [NormedAlgebra ùïÇ ùî∏] {a : ùïÇ} (ha : ‚àÄ (k : ‚Ñï), a ‚â† k) : (binomialSeries ùî∏ a).radius = 1 := by
  simp only [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ùïÇ)) (by norm_cast; simp),
    FormalMultilinearSeries.radius_compNeg]
  conv at ha => ext; rw [ne_comm]
  exact ordinaryHypergeometricSeries_radius_eq_one _ _ _ _ (by norm_cast; grind)

theorem binomialSeries_radius_ge_one {ùïÇ : Type*} [RCLike ùïÇ] {ùî∏ : Type*} [NormedDivisionRing ùî∏]
    [NormedAlgebra ùïÇ ùî∏] {a : ùïÇ} :
    1 ‚â§ (binomialSeries ùî∏ a).radius := by
  by_cases ha : ‚àÄ (k : ‚Ñï), a ‚â† k
  ¬∑ rw [binomialSeries_radius_eq_one ha]
  ¬∑ push_neg at ha
    rcases ha with ‚ü®k, rfl‚ü©
    simp [binomialSeries_radius_eq_top_of_nat]

theorem one_add_cpow_hasFPowerSeriesOnBall_zero {a : ‚ÑÇ} :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ (1 + x) ^ a) (binomialSeries ‚ÑÇ a) 0 1 := by
  suffices (binomialSeries ‚ÑÇ a = FormalMultilinearSeries.ofScalars ‚ÑÇ
      fun n ‚Ü¶ iteratedDeriv n (fun (x : ‚ÑÇ) ‚Ü¶ (1 + x) ^ a) 0 / n !) by
    convert AnalyticOn.hasFPowerSeriesOnSubball _ _ _
    ¬∑ norm_num
    ¬∑ -- TODO: use `fun_prop` for this subgoal
      apply AnalyticOn.cpow (analyticOn_const.add analyticOn_id) analyticOn_const
      intro z hz
      apply Complex.mem_slitPlane_of_norm_lt_one
      rw [‚Üê ENNReal.ofReal_one, Metric.emetric_ball] at hz
      simpa using hz
    ¬∑ rw [‚Üê this]
      exact binomialSeries_radius_ge_one
  simp only [binomialSeries, FormalMultilinearSeries.ofScalars_series_eq_iff]
  ext n
  rw [eq_div_iff_mul_eq (by simp [Nat.factorial_ne_zero]), ‚Üê nsmul_eq_mul',
    ‚Üê Ring.descPochhammer_eq_factorial_smul_choose]
  let B := Metric.ball (0 : ‚ÑÇ) 1
  suffices Set.EqOn (iteratedDerivWithin n (fun x ‚Ü¶ (1 + x) ^ a) B)
      (fun x ‚Ü¶ (descPochhammer ‚Ñ§ n).smeval a * (1 + x) ^ (a - n)) B by
    specialize this (show 0 ‚àà _ by simp [B])
    rw [iteratedDerivWithin_of_isOpen Metric.isOpen_ball (by simp)] at this
    symm
    simpa using this
  induction n with
  | zero => simp [Set.EqOn]
  | succ n ih =>
    have : iteratedDerivWithin (n + 1) (fun (x : ‚ÑÇ) ‚Ü¶ (1 + x) ^ a) B =
        derivWithin (iteratedDerivWithin n (fun x ‚Ü¶ (1 + x) ^ a) B) B := by
      ext z
      rw [iteratedDerivWithin_succ]
    rw [this]
    have : Set.EqOn (derivWithin (iteratedDerivWithin n (fun (x : ‚ÑÇ) ‚Ü¶ (1 + x) ^ a) B) B)
        (derivWithin (fun x ‚Ü¶ (descPochhammer ‚Ñ§ n).smeval a * (1 + x) ^ (a - ‚Üën)) B) B := by
      intro z hz
      rw [derivWithin_congr (fun _ hz ‚Ü¶ ih hz) (ih hz)]
    apply Set.EqOn.trans this
    intro z hz
    simp only [Nat.cast_add, Nat.cast_one, B, derivWithin_of_isOpen Metric.isOpen_ball hz,
      deriv_const_mul_field']
    rw [deriv_cpow_const (by fun_prop), deriv_const_add', deriv_id'', mul_one,
      show a - (n + 1) = a - n - 1 by ring, ‚Üê mul_assoc]
    ¬∑ congr
      simp [descPochhammer_succ_right, Polynomial.smeval_mul, Polynomial.smeval_natCast]
    ¬∑ apply Complex.mem_slitPlane_of_norm_lt_one
      simpa [B] using hz

theorem one_add_cpow_hasFPowerSeriesAt_zero {a : ‚ÑÇ} :
    HasFPowerSeriesAt (fun x ‚Ü¶ (1 + x) ^ a) (binomialSeries ‚ÑÇ a) 0 :=
  one_add_cpow_hasFPowerSeriesOnBall_zero.hasFPowerSeriesAt

attribute [local simp‚Üê ] Complex.ofReal_choose in
attribute [-simp] FormalMultilinearSeries.apply_eq_prod_smul_coeff in
theorem one_add_rpow_hasFPowerSeriesOnBall_zero {a : ‚Ñù} :
    HasFPowerSeriesOnBall (fun x ‚Ü¶ (1 + x) ^ a) (binomialSeries ‚Ñù a) 0 1 := by
  have : binomialSeries ‚ÑÇ a = (binomialSeries ‚ÑÇ (a : ‚ÑÇ)).restrictScalars (ùïú := ‚Ñù) := by aesop
  have : HasFPowerSeriesOnBall (fun x ‚Ü¶ (1 + x) ^ (a : ‚ÑÇ)) (binomialSeries ‚ÑÇ a) (.ofRealCLM 0) 1 :=
    Complex.ofRealCLM.map_zero ‚ñ∏ this ‚ñ∏ one_add_cpow_hasFPowerSeriesOnBall_zero.restrictScalars
  convert (Complex.reCLM.comp_hasFPowerSeriesOnBall this.compContinuousLinearMap).congr ?_
  ¬∑ ext; simp [Function.comp_def]
  ¬∑ simp
  ¬∑ intro x hx; simp_all; norm_cast

theorem one_add_rpow_hasFPowerSeriesAt_zero {a : ‚Ñù} :
    HasFPowerSeriesAt (fun x ‚Ü¶ (1 + x) ^ a) (binomialSeries ‚Ñù a) 0 :=
  one_add_rpow_hasFPowerSeriesOnBall_zero.hasFPowerSeriesAt
