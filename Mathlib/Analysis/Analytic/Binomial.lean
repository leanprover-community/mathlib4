/-
Copyright (c) 2025 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
import Mathlib.Analysis.SpecialFunctions.OrdinaryHypergeometric
import Mathlib.RingTheory.Binomial

/-!
# Binomial Series

This file introduces the binomial series:
$$
\sum_{k=0}^{\infty} \; \binom{a}{k} \; x^k = 1 + a x + \frac{a(a-1)}{2!} x^2 +
  \frac{a(a-1)(a-2)}{3!} x^3 + \cdots
$$
where $a$ is an element of a normed field $\mathbb{K}$,
and $x$ is an element of a normed algebra over $\mathbb{K}$.

## Main Statements

* `binomialSeries_radius_eq_one`: The radius of convergence of the binomial series is `1` when `a`
  is not a natural number.
* `binomialSeries_radius_eq_top_of_nat`: In case `a` is natural, the series converges everywhere,
  since it is finite.
-/

open scoped Nat

universe u v

/-- **Binomial series**: the (scalar) formal multilinear series with coefficients given
by `Ring.choose a`. The sum of this series is `fun x â†¦ (1 + x) ^ a` within the radius
of convergence. -/
noncomputable def binomialSeries {ð•‚ : Type u} [Field ð•‚] [CharZero ð•‚] (ð”¸ : Type v)
    [Ring ð”¸] [Algebra ð•‚ ð”¸] [TopologicalSpace ð”¸] [IsTopologicalRing ð”¸] (a : ð•‚) :
    FormalMultilinearSeries ð•‚ ð”¸ ð”¸ :=
  .ofScalars ð”¸ (Ring.choose a Â·)

theorem binomialSeries_eq_ordinaryHypergeometricSeries {ð•‚ : Type u} [Field ð•‚] [CharZero ð•‚]
    {ð”¸ : Type v} [Ring ð”¸] [Algebra ð•‚ ð”¸] [TopologicalSpace ð”¸] [IsTopologicalRing ð”¸] {a b : ð•‚}
    (h : âˆ€ (k : â„•), (k : ð•‚) â‰  -b) :
    binomialSeries ð”¸ a =
    (ordinaryHypergeometricSeries ð”¸ (-a) b b).compContinuousLinearMap (-(.id _ _)) := by
  simp only [binomialSeries, ordinaryHypergeometricSeries,
    FormalMultilinearSeries.ofScalars_comp_neg_id]
  congr! with n
  rw [ordinaryHypergeometricCoefficient,
    mul_inv_cancel_rightâ‚€ (by simp [ascPochhammer_eval_eq_zero_iff]; grind)]
  simp only [Ring.choose_eq_smul, Polynomial.descPochhammer_smeval_eq_ascPochhammer,
    Polynomial.ascPochhammer_smeval_cast, Polynomial.ascPochhammer_smeval_eq_eval,
    ascPochhammer_eval_neg_eq_descPochhammer, descPochhammer_eval_eq_ascPochhammer]
  ring_nf
  simp

/-- The radius of convergence of `binomialSeries ð”¸ a` is `âŠ¤` for natural `a`. -/
theorem binomialSeries_radius_eq_top_of_nat {ð•‚ : Type v} [RCLike ð•‚] {ð”¸ : Type u}
    [NormedDivisionRing ð”¸] [NormedAlgebra ð•‚ ð”¸] {a : â„•} :
    (binomialSeries ð”¸ (a : ð•‚)).radius = âŠ¤ := by
  simp [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ð•‚)) (by norm_cast; simp),
    ordinaryHypergeometric_radius_top_of_neg_natâ‚]

/-- The radius of convergence of `binomialSeries ð”¸ a` is `1`, when `a` is not natural. -/
theorem binomialSeries_radius_eq_one {ð•‚ : Type v} [RCLike ð•‚] {ð”¸ : Type u} [NormedDivisionRing ð”¸]
    [NormedAlgebra ð•‚ ð”¸] {a : ð•‚} (ha : âˆ€ (k : â„•), a â‰  k) : (binomialSeries ð”¸ a).radius = 1 := by
  simp only [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ð•‚)) (by norm_cast; simp),
    FormalMultilinearSeries.radius_compNeg]
  conv at ha => ext; rw [ne_comm]
  exact ordinaryHypergeometricSeries_radius_eq_one _ _ _ _ (by norm_cast; grind)
