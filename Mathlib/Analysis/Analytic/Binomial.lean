/-
Copyright (c) 2025 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
import Mathlib.Analysis.SpecialFunctions.OrdinaryHypergeometric
import Mathlib.RingTheory.Binomial

/-!
# Binomial Series

This file introduces the binomial series:
$$
\sum_{k=0}^{\infty} \; \binom{a}{k} \; x^k = 1 + a x + \frac{a(a-1)}{2!} x^2 +
  \frac{a(a-1)(a-2)}{3!} x^3 + \cdots
$$
where $a$ is an element of a normed field $\mathbb{K}$,
and $x$ is an element of a normed algebra over $\mathbb{K}$.

## Main Statements

* `binomialSeries_radius_eq_one`: The radius of convergence of the binomial series is `1`.
-/

open scoped Nat

universe u v

/-- Binomial series:
$$
\sum_{k=0}^{\infty} \; \binom{a}{k} \; x^k = 1 + a x + \frac{a(a-1)}{2!} x^2 +
  \frac{a(a-1)(a-2)}{3!} x^3 + \cdots
$$
-/
noncomputable def binomialSeries {ğ•‚ : Type u} [Field ğ•‚] [CharZero ğ•‚] (ğ”¸ : Type v)
    [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸] [IsTopologicalRing ğ”¸] (a : ğ•‚) :
    FormalMultilinearSeries ğ•‚ ğ”¸ ğ”¸ := .ofScalars ğ”¸ (Ring.choose a Â·)

theorem binomialSeries_eq_ordinaryHypergeometricSeries {ğ•‚ : Type u} [Field ğ•‚] [CharZero ğ•‚]
    {ğ”¸ : Type v} [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸] [IsTopologicalRing ğ”¸] {a b : ğ•‚}
    (h : âˆ€ (k : â„•), (k : ğ•‚) â‰  -b) :
    binomialSeries ğ”¸ a =
    (ordinaryHypergeometricSeries ğ”¸ (-a) b b).compContinuousLinearMap (-(.id _ _)) := by
  simp only [binomialSeries, ordinaryHypergeometricSeries]
  ext n v
  simp only [FormalMultilinearSeries.ofScalars, ContinuousMultilinearMap.smul_apply,
    ContinuousMultilinearMap.mkPiAlgebraFin_apply,
    FormalMultilinearSeries.compContinuousLinearMap_apply, ordinaryHypergeometricCoefficient]
  rw [mul_inv_cancel_rightâ‚€ (by simp [ascPochhammer_eval_eq_zero_iff]; grind)]
  have : ((-ContinuousLinearMap.id ğ•‚ ğ”¸ : _) : ğ”¸ â†’ ğ”¸) = Neg.neg := by ext; simp
  simp only [Ring.choose_eq_smul, Polynomial.descPochhammer_smeval_eq_ascPochhammer,
    Polynomial.ascPochhammer_smeval_cast, Polynomial.ascPochhammer_smeval_eq_eval, smul_eq_mul,
    ascPochhammer_eval_neg_eq_descPochhammer, descPochhammer_eval_eq_ascPochhammer, this, â†
    List.map_ofFn, List.prod_map_neg (List.ofFn v), List.length_ofFn]
  rcases n.even_or_odd with (h | h) <;> simp [h.neg_one_pow]

/-- The radius of convergence of `binomialSeries ğ”¸ a` is `âŠ¤` for natural `a`. -/
theorem binomialSeries_radius_eq_top_of_nat {ğ•‚ : Type v} [RCLike ğ•‚] {ğ”¸ : Type u}
    [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] {a : â„•} :
    (binomialSeries ğ”¸ (a : ğ•‚)).radius = âŠ¤ := by
  simp [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ğ•‚)) (by norm_cast; simp),
    ordinaryHypergeometric_radius_top_of_neg_natâ‚]

/-- The radius of convergence of `binomialSeries ğ”¸ a` is `1`, when `a` is not natural. -/
theorem binomialSeries_radius_eq_one {ğ•‚ : Type v} [RCLike ğ•‚] {ğ”¸ : Type u} [NormedDivisionRing ğ”¸]
    [NormedAlgebra ğ•‚ ğ”¸] {a : ğ•‚} (ha : âˆ€ (k : â„•), a â‰  k) : (binomialSeries ğ”¸ a).radius = 1 := by
  simp [binomialSeries_eq_ordinaryHypergeometricSeries (b := (1 : ğ•‚)) (by norm_cast; simp)]
  conv at ha => ext; rw [ne_comm]
  exact ordinaryHypergeometricSeries_radius_eq_one _ _ _ _ (by norm_cast; simp [ha])
