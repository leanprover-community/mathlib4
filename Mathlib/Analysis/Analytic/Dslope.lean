/-
Copyright (c) 2025 Maksym Radziwill. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Maksym Radziwill
-/
import Mathlib.Analysis.Analytic.Basic
import Mathlib.Analysis.Meromorphic.Basic
import Mathlib.Analysis.Meromorphic.Order

/-!
# Analyticity of dslope 

``dslope`` is defined in ``Mathlib.Analysis.Calculus.DSlope``

    ``dslope f a = Function.update (slope f a) a (deriv f a)``

where


	``slope f a b = (b - a)â»Â¹ â€¢ (f b -áµ¥ f a)``

We show in ``AnalyticOnNhd.dslope`` that if ``f`` is analytic on a set 
then for any ``a`` the function ``dslope f a`` is analytic on the same 
set. 

-/

variable {ð•œ : Type*} {E : Type*} [NontriviallyNormedField ð•œ] 
variable [NormedAddCommGroup E] [NormedSpace ð•œ E]
 
lemma MeromorphicAt.slope {f : ð•œ â†’ E} {a c : ð•œ}
    (hf : MeromorphicAt f c) : MeromorphicAt (slope f a) c :=
  ((MeromorphicAt.id c).sub (MeromorphicAt.const a c)).inv.smul
    (hf.sub (MeromorphicAt.const (f a) c)) 
 
lemma MeromorphicAt.dslope {f : ð•œ â†’ E} {a c : ð•œ}
    (hf : MeromorphicAt f c) : MeromorphicAt (dslope f a) c := by
  classical exact hf.slope.update a (deriv f a)
 
lemma ContinuousAt.dslope {f : ð•œ â†’ E} {a c : ð•œ}
    (hf : DifferentiableAt ð•œ f c) : ContinuousAt (dslope f a) c := by
  by_cases h : c = a
  Â· rwa [â† h, continuousAt_dslope_same] 
  Â· rw [continuousAt_dslope_of_ne h]; exact hf.continuousAt 
 
theorem AnalyticAt.dslope {f : ð•œ â†’ E} {a c : ð•œ}
    (hf : AnalyticAt ð•œ f c) : AnalyticAt ð•œ (dslope f a) c :=
 (MeromorphicAt.dslope hf.meromorphicAt).analyticAt (ContinuousAt.dslope hf.differentiableAt)

theorem AnalyticOnNhd.dslope {f : ð•œ â†’ E} {s : Set ð•œ} {a : ð•œ}
    (hf : AnalyticOnNhd ð•œ f s) : AnalyticOnNhd ð•œ (dslope f a) s :=
  fun x hx => (hf x hx).dslope

