/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara, Stefan Kebekus
-/
import Mathlib.Analysis.Analytic.IsolatedZeros

/-!
# Vanishing Order of Analytic Functions

This file defines the order of vanishing of an analytic function `f` at a point `z‚ÇÄ`, as an element
of `‚Ñï‚àû`.

TODO: Uniformize API between analytic and meromorphic functions
-/

open Filter  Set
open scoped Topology

variable {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {f f‚ÇÅ f‚ÇÇ g : ùïú ‚Üí E} {n : ‚Ñï} {z‚ÇÄ : ùïú}

/-!
## Vanishing Order at a Point: Definition and Characterization
-/

namespace AnalyticAt

open scoped Classical in

/-- The order of vanishing of `f` at `z‚ÇÄ`, as an element of `‚Ñï‚àû`.

The order is defined to be `‚àû` if `f` is identically 0 on a neighbourhood of `z‚ÇÄ`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - z‚ÇÄ) ^ n ‚Ä¢ g z`, where `g` is analytic
and does not vanish at `z‚ÇÄ`. See `AnalyticAt.order_eq_top_iff` and `AnalyticAt.order_eq_nat_iff` for
these equivalences. -/
noncomputable def order (hf : AnalyticAt ùïú f z‚ÇÄ) : ENat :=
  if h : ‚àÄ·∂† z in ùìù z‚ÇÄ, f z = 0 then ‚ä§
  else ‚Üë(hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose

/-- The order of an analytic function `f` at a `z‚ÇÄ` is infinity iff `f` vanishes locally around
`z‚ÇÄ`. -/
lemma order_eq_top_iff (hf : AnalyticAt ùïú f z‚ÇÄ) : hf.order = ‚ä§ ‚Üî ‚àÄ·∂† z in ùìù z‚ÇÄ, f z = 0 := by
  unfold order
  split_ifs with h
  ¬∑ rwa [eq_self, true_iff]
  ¬∑ simpa only [ne_eq, ENat.coe_ne_top, false_iff] using h

/-- The order of an analytic function `f` at `z‚ÇÄ` equals a natural number `n` iff `f` can locally
be written as `f z = (z - z‚ÇÄ) ^ n ‚Ä¢ g z`, where `g` is analytic and does not vanish at `z‚ÇÄ`. -/
lemma order_eq_nat_iff (hf : AnalyticAt ùïú f z‚ÇÄ) (n : ‚Ñï) : hf.order = ‚Üën ‚Üî
    ‚àÉ (g : ùïú ‚Üí E), AnalyticAt ùïú g z‚ÇÄ ‚àß g z‚ÇÄ ‚â† 0 ‚àß ‚àÄ·∂† z in ùìù z‚ÇÄ, f z = (z - z‚ÇÄ) ^ n ‚Ä¢ g z := by
  unfold order
  split_ifs with h
  ¬∑ simp only [ENat.top_ne_coe, false_iff]
    contrapose! h
    rw [‚Üê hf.exists_eventuallyEq_pow_smul_nonzero_iff]
    exact ‚ü®n, h‚ü©
  ¬∑ rw [‚Üê hf.exists_eventuallyEq_pow_smul_nonzero_iff] at h
    refine ‚ü®fun hn ‚Ü¶ (WithTop.coe_inj.mp hn : h.choose = n) ‚ñ∏ h.choose_spec, fun h' ‚Ü¶ ?_‚ü©
    rw [unique_eventuallyEq_pow_smul_nonzero h.choose_spec h']

/-- The order of an analytic function `f` at `z‚ÇÄ` is finite iff `f` can locally be written as
`f z = (z - z‚ÇÄ) ^ order ‚Ä¢ g z`, where `g` is analytic and does not vanish at `z‚ÇÄ`. -/
lemma order_ne_top_iff (hf : AnalyticAt ùïú f z‚ÇÄ) :
    hf.order ‚â† ‚ä§ ‚Üî ‚àÉ (g : ùïú ‚Üí E), AnalyticAt ùïú g z‚ÇÄ ‚àß g z‚ÇÄ ‚â† 0
      ‚àß f =·∂†[ùìù z‚ÇÄ] fun z ‚Ü¶ (z - z‚ÇÄ) ^ (hf.order.toNat) ‚Ä¢ g z := by
  simp only [‚Üê ENat.coe_toNat_eq_self, Eq.comm, EventuallyEq, ‚Üê hf.order_eq_nat_iff]

@[deprecated (since := "2025-02-03")]
alias order_neq_top_iff := order_ne_top_iff

/-- If two functions agree in a neighborhood of `z‚ÇÄ`, then their orders at `z‚ÇÄ` agree. -/
theorem order_congr (hf‚ÇÅ : AnalyticAt ùïú f‚ÇÅ z‚ÇÄ) (h : f‚ÇÅ =·∂†[ùìù z‚ÇÄ] f‚ÇÇ) :
    (hf‚ÇÅ.congr h).order = hf‚ÇÅ.order := by
  -- Trivial case: f‚ÇÅ vanishes identially around z‚ÇÄ
  by_cases h‚ÇÅf‚ÇÅ : hf‚ÇÅ.order = ‚ä§
  ¬∑ rw [h‚ÇÅf‚ÇÅ, order_eq_top_iff]
    filter_upwards [hf‚ÇÅ.order_eq_top_iff.1 h‚ÇÅf‚ÇÅ, h]
    intro a h‚ÇÅa h‚ÇÇa
    rwa [‚Üê h‚ÇÇa]
  -- General case
  lift hf‚ÇÅ.order to ‚Ñï using h‚ÇÅf‚ÇÅ with n hn
  rw [eq_comm] at hn
  rw [AnalyticAt.order_eq_nat_iff] at *
  obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := hn
  use g, h‚ÇÅg, h‚ÇÇg
  filter_upwards [h, h‚ÇÉg]
  intro a h‚ÇÅa h‚ÇÇa
  rw [‚Üê h‚ÇÇa, h‚ÇÅa]

/-- The order of an analytic function `f` at `z‚ÇÄ` is zero iff `f` does not vanish at `z‚ÇÄ`. -/
lemma order_eq_zero_iff (hf : AnalyticAt ùïú f z‚ÇÄ) :
    hf.order = 0 ‚Üî f z‚ÇÄ ‚â† 0 := by
  rw [‚Üê ENat.coe_zero, order_eq_nat_iff hf 0]
  constructor
  ¬∑ intro ‚ü®g, _, _, hg‚ü©
    simpa [hg.self_of_nhds]
  ¬∑ exact fun hz ‚Ü¶ ‚ü®f, hf, hz, by simp‚ü©

/-- An analytic function vanishes at a point if its order is nonzero when converted to ‚Ñï. -/
lemma apply_eq_zero_of_order_toNat_ne_zero (hf : AnalyticAt ùïú f z‚ÇÄ) :
    hf.order.toNat ‚â† 0 ‚Üí f z‚ÇÄ = 0 := by
  simp [hf.order_eq_zero_iff]
  tauto

/-!
## Vanishing Order at a Point: Elementary Computations
-/

/-- Helper lemma, required to state analyticAt_order_centeredMonomial below -/
lemma analyticAt_centeredMonomial (z‚ÇÄ : ùïú) (n : ‚Ñï) :
    AnalyticAt ùïú ((¬∑ - z‚ÇÄ) ^ n) z‚ÇÄ := by fun_prop

/-- Simplifier lemma for the order of a centered monomial -/
@[simp]
lemma analyticAt_order_centeredMonomial {z‚ÇÄ : ùïú} {n : ‚Ñï} :
    (analyticAt_centeredMonomial z‚ÇÄ n).order = n := by
  rw [AnalyticAt.order_eq_nat_iff]
  use 1
  simp only [Pi.one_apply, ne_eq, one_ne_zero, not_false_eq_true, Pi.pow_apply, smul_eq_mul,
    mul_one, eventually_true, and_self, and_true]
  exact analyticAt_const

/-!
## Vanishing Order at a Point: Behaviour under Standard Operations

The theorems `AnalyticAt.order_mul` and `AnalyticAt.order_pow` establish additivity of the order
under multiplication and taking powers. The theorem `AnalyticAt.order_add` establishes behavior
under addition.
-/

/-- Helper lemma for `AnalyticAt.order_smul` -/
lemma order_smul_of_order_eq_top‚ÇÅ {f : ùïú ‚Üí ùïú} {g : ùïú ‚Üí E} (hf : AnalyticAt ùïú f z‚ÇÄ)
    (hg : AnalyticAt ùïú g z‚ÇÄ) (h‚ÇÅf : hf.order = ‚ä§) :
    (hf.smul hg).order = ‚ä§ := by
  rw [AnalyticAt.order_eq_top_iff] at *
  filter_upwards [h‚ÇÅf]
  exact fun _ ha ‚Ü¶ by simp [ha]

/-- Helper lemma for `AnalyticAt.order_smul` -/
lemma order_smul_of_order_eq_top‚ÇÇ {f : ùïú ‚Üí ùïú} {g : ùïú ‚Üí E} (hf : AnalyticAt ùïú f z‚ÇÄ)
    (hg : AnalyticAt ùïú g z‚ÇÄ) (h‚ÇÅg : hg.order = ‚ä§) :
    (hf.smul hg).order = ‚ä§ := by
  rw [AnalyticAt.order_eq_top_iff] at *
  filter_upwards [h‚ÇÅg]
  exact fun _ ha ‚Ü¶ by simp [ha]

/-- The order is additive when scalar multiplying analytic functions. -/
theorem order_smul {f : ùïú ‚Üí ùïú} {g : ùïú ‚Üí E} (hf : AnalyticAt ùïú f z‚ÇÄ)
    (hg : AnalyticAt ùïú g z‚ÇÄ) :
    (hf.smul hg).order = hf.order + hg.order := by
  -- Trivial cases: one of the functions vanishes around z‚ÇÄ
  by_cases h‚ÇÇf : hf.order = ‚ä§
  ¬∑ simp [hf.order_smul_of_order_eq_top‚ÇÅ hg h‚ÇÇf, h‚ÇÇf]
  by_cases h‚ÇÇg : hg.order = ‚ä§
  ¬∑ simp [hf.order_smul_of_order_eq_top‚ÇÇ hg h‚ÇÇg, h‚ÇÇg]
  -- Non-trivial case: both functions do not vanish around z‚ÇÄ
  obtain ‚ü®g‚ÇÅ, h‚ÇÅg‚ÇÅ, h‚ÇÇg‚ÇÅ, h‚ÇÉg‚ÇÅ‚ü© := hf.order_ne_top_iff.1 h‚ÇÇf
  obtain ‚ü®g‚ÇÇ, h‚ÇÅg‚ÇÇ, h‚ÇÇg‚ÇÇ, h‚ÇÉg‚ÇÇ‚ü© := hg.order_ne_top_iff.1 h‚ÇÇg
  rw [‚Üê ENat.coe_toNat h‚ÇÇf, ‚Üê ENat.coe_toNat h‚ÇÇg, ‚Üê ENat.coe_add, (hf.smul hg).order_eq_nat_iff]
  use g‚ÇÅ ‚Ä¢ g‚ÇÇ, by exact h‚ÇÅg‚ÇÅ.smul h‚ÇÅg‚ÇÇ
  constructor
  ¬∑ simp only [Pi.smul_apply', ne_eq, smul_eq_zero, not_or]
    tauto
  ¬∑ filter_upwards [h‚ÇÉg‚ÇÅ, h‚ÇÉg‚ÇÇ]
    intro a h‚ÇÅa h‚ÇÇa
    rw [Pi.smul_apply', Pi.smul_apply', h‚ÇÇa, ‚Üê smul_assoc, ‚Üê smul_assoc]
    congr 1
    rw [h‚ÇÅa, smul_eq_mul, smul_eq_mul, smul_eq_mul]
    ring

/-- Helper lemma for `AnalyticAt.order_mul` -/
lemma order_mul_of_order_eq_top {f g : ùïú ‚Üí ùïú} (hf : AnalyticAt ùïú f z‚ÇÄ)
    (hg : AnalyticAt ùïú g z‚ÇÄ) (h'f : hf.order = ‚ä§) :
    (hf.mul hg).order = ‚ä§ := by
  rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff] at *
  obtain ‚ü®t, h‚ÇÅt, h‚ÇÇt, h‚ÇÉt‚ü© := h'f
  exact ‚ü®t, fun y hy ‚Ü¶ (by simp [h‚ÇÅt y hy]), h‚ÇÇt, h‚ÇÉt‚ü©

/-- The order is additive when multiplying analytic functions. -/
theorem order_mul {f g : ùïú ‚Üí ùïú} (hf : AnalyticAt ùïú f z‚ÇÄ) (hg : AnalyticAt ùïú g z‚ÇÄ) :
    (hf.mul hg).order = hf.order + hg.order := by apply hf.order_smul

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem order_pow {f : ùïú ‚Üí ùïú} (hf : AnalyticAt ùïú f z‚ÇÄ) {n : ‚Ñï} :
    (hf.pow n).order = n ‚Ä¢ hf.order := by
  induction n
  case zero =>
    simp [AnalyticAt.order_eq_zero_iff]
  case succ n hn =>
    simp [add_mul, pow_add, (hf.pow n).order_mul hf, hn]

/-- Helper lemma for AnalyticAt.order_add: adding a locally vanishing function does not
affect the order. -/
lemma order_add_top (hf‚ÇÅ : AnalyticAt ùïú f‚ÇÅ z‚ÇÄ) (hf‚ÇÇ : AnalyticAt ùïú f‚ÇÇ z‚ÇÄ) (h : hf‚ÇÇ.order = ‚ä§) :
    (hf‚ÇÅ.add hf‚ÇÇ).order = hf‚ÇÅ.order := by
  apply hf‚ÇÅ.order_congr
  filter_upwards [hf‚ÇÇ.order_eq_top_iff.1 h]
  intro a h‚ÇÅa
  simp [h‚ÇÅa]

/-- The order of a sub at least the minimum of the orders of the summands. -/
theorem order_add (hf‚ÇÅ : AnalyticAt ùïú f‚ÇÅ z‚ÇÄ) (hf‚ÇÇ : AnalyticAt ùïú f‚ÇÇ z‚ÇÄ) :
    min hf‚ÇÅ.order hf‚ÇÇ.order ‚â§ (hf‚ÇÅ.add hf‚ÇÇ).order := by
  -- Trivial case: f‚ÇÅ vanishes identically around z‚ÇÄ
  by_cases h‚ÇÅf‚ÇÅ : hf‚ÇÅ.order = ‚ä§
  ¬∑ rw [h‚ÇÅf‚ÇÅ]
    simp only [le_top, inf_of_le_right]
    simp_rw [AddCommMagma.add_comm f‚ÇÅ f‚ÇÇ]
    rw [hf‚ÇÇ.order_add_top hf‚ÇÅ h‚ÇÅf‚ÇÅ]
  -- Trivial case: f‚ÇÇ vanishes identically around z‚ÇÄ
  by_cases h‚ÇÅf‚ÇÇ : hf‚ÇÇ.order = ‚ä§
  ¬∑ rw [h‚ÇÅf‚ÇÇ]
    simp only [le_top, inf_of_le_left]
    rw [hf‚ÇÅ.order_add_top hf‚ÇÇ h‚ÇÅf‚ÇÇ]
  -- General case
  lift hf‚ÇÅ.order to ‚Ñï using h‚ÇÅf‚ÇÅ with n‚ÇÅ hn‚ÇÅ
  lift hf‚ÇÇ.order to ‚Ñï using h‚ÇÅf‚ÇÇ with n‚ÇÇ hn‚ÇÇ
  rw [eq_comm, AnalyticAt.order_eq_nat_iff] at *
  obtain ‚ü®g‚ÇÅ, h‚ÇÅg‚ÇÅ, h‚ÇÇg‚ÇÅ, h‚ÇÉg‚ÇÅ‚ü© := hn‚ÇÅ
  obtain ‚ü®g‚ÇÇ, h‚ÇÅg‚ÇÇ, h‚ÇÇg‚ÇÇ, h‚ÇÉg‚ÇÇ‚ü© := hn‚ÇÇ
  let m := min n‚ÇÅ n‚ÇÇ
  let G := fun z ‚Ü¶ (z - z‚ÇÄ) ^ (n‚ÇÅ - m) ‚Ä¢ g‚ÇÅ z + (z - z‚ÇÄ) ^ (n‚ÇÇ - m) ‚Ä¢ g‚ÇÇ z
  have hG : AnalyticAt ùïú G z‚ÇÄ := by fun_prop
  have : f‚ÇÅ + f‚ÇÇ =·∂†[ùìù z‚ÇÄ] (¬∑ - z‚ÇÄ) ^ m ‚Ä¢ G := by
    dsimp [G]
    filter_upwards [h‚ÇÉg‚ÇÅ, h‚ÇÉg‚ÇÇ]
    intro a h‚ÇÅa h‚ÇÇa
    simp only [Pi.add_apply, h‚ÇÅa, h‚ÇÇa, Pi.smul_apply', Pi.pow_apply, smul_add, G]
    congr 1
    repeat
      simp [‚Üê smul_assoc, smul_eq_mul, ‚Üê pow_add, m]
  rw [‚Üê (hf‚ÇÅ.add hf‚ÇÇ).order_congr this, AnalyticAt.order_smul _ hG,
    analyticAt_order_centeredMonomial]
  simp only [m, G]
  exact le_self_add

/-- Helper lemma for AnalyticAt.order_add_of_unequal_order -/
lemma order_add_of_order_lt_order (hf‚ÇÅ : AnalyticAt ùïú f‚ÇÅ z‚ÇÄ) (hf‚ÇÇ : AnalyticAt ùïú f‚ÇÇ z‚ÇÄ)
    (h : hf‚ÇÅ.order < hf‚ÇÇ.order) :
    (hf‚ÇÅ.add hf‚ÇÇ).order = hf‚ÇÅ.order := by
  -- Trivial case: f‚ÇÇ vanishes identically around z‚ÇÄ
  by_cases h‚ÇÅf‚ÇÇ : hf‚ÇÇ.order = ‚ä§
  ¬∑ apply hf‚ÇÅ.order_congr
    filter_upwards [hf‚ÇÇ.order_eq_top_iff.1 h‚ÇÅf‚ÇÇ]
    intro a h‚ÇÅa
    simp [h‚ÇÅa]
  -- General case
  lift hf‚ÇÇ.order to ‚Ñï using h‚ÇÅf‚ÇÇ with n‚ÇÇ hn‚ÇÇ
  lift hf‚ÇÅ.order to ‚Ñï using h.ne_top with n‚ÇÅ hn‚ÇÅ
  rw [Nat.cast_lt] at h
  rw [eq_comm] at hn‚ÇÅ hn‚ÇÇ
  rw [AnalyticAt.order_eq_nat_iff] at *
  obtain ‚ü®g‚ÇÅ, h‚ÇÅg‚ÇÅ, h‚ÇÇg‚ÇÅ, h‚ÇÉg‚ÇÅ‚ü© := hn‚ÇÅ
  obtain ‚ü®g‚ÇÇ, h‚ÇÅg‚ÇÇ, h‚ÇÇg‚ÇÇ, h‚ÇÉg‚ÇÇ‚ü© := hn‚ÇÇ
  use g‚ÇÅ + (¬∑ - z‚ÇÄ) ^ (n‚ÇÇ - n‚ÇÅ) ‚Ä¢ g‚ÇÇ, by fun_prop
  constructor
  ¬∑ simpa [Nat.sub_ne_zero_iff_lt.mpr h]
  ¬∑ filter_upwards [h‚ÇÉg‚ÇÅ, h‚ÇÉg‚ÇÇ]
    intro a h‚ÇÅa h‚ÇÇa
    simp only [Pi.add_apply, h‚ÇÅa, h‚ÇÇa, Pi.smul_apply', Pi.pow_apply, smul_add, ‚Üê smul_assoc,
      smul_eq_mul, add_right_inj]
    rw [‚Üê pow_add, add_comm, eq_comm, Nat.sub_add_cancel (Nat.le_of_succ_le h)]

/-- If two functions have unequal orders, then the order of their sum is exactly the minimum
of the orders of the summands. -/
theorem order_add_of_unequal_order (hf‚ÇÅ : AnalyticAt ùïú f‚ÇÅ z‚ÇÄ) (hf‚ÇÇ : AnalyticAt ùïú f‚ÇÇ z‚ÇÄ)
    (h : hf‚ÇÅ.order ‚â† hf‚ÇÇ.order) :
    (hf‚ÇÅ.add hf‚ÇÇ).order = min hf‚ÇÅ.order hf‚ÇÇ.order := by
  by_cases h‚ÇÅ : hf‚ÇÅ.order < hf‚ÇÇ.order
  ¬∑ rw [min_eq_left (le_of_lt h‚ÇÅ)]
    exact hf‚ÇÅ.order_add_of_order_lt_order hf‚ÇÇ h‚ÇÅ
  ¬∑ rw [min_eq_right (le_of_not_lt h‚ÇÅ)]
    simp_rw [AddCommMagma.add_comm f‚ÇÅ f‚ÇÇ]
    exact hf‚ÇÇ.order_add_of_order_lt_order hf‚ÇÅ (lt_of_le_of_ne (le_of_not_lt h‚ÇÅ) h.symm)

end AnalyticAt

/-!
## Level Sets of the Order Function

TODO:

- Draw conclusions about behaviour of the order function on connected domains of analyticity.

- Prove that the set where an analytic function has order in [1,‚àû) is discrete within its domain of
  analyticity.
-/

namespace AnalyticOnNhd

variable {U : Set ùïú}

/-- The set where an analytic function has infinite order is clopen in its domain of analyticity. -/
theorem isClopen_setOf_order_eq_top (h‚ÇÅf : AnalyticOnNhd ùïú f U) :
    IsClopen { u : U | (h‚ÇÅf u.1 u.2).order = ‚ä§ } := by
  constructor
  ¬∑ rw [‚Üê isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (h‚ÇÅf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    ¬∑ -- Case: f is locally zero in a punctured neighborhood of z
      rw [‚Üê (h‚ÇÅf z.1 z.2).order_eq_top_iff] at h
      tauto
    ¬∑ -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain ‚ü®t', h‚ÇÅt', h‚ÇÇt', h‚ÇÉt'‚ü© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val ‚Åª¬π' t'
      constructor
      ¬∑ intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases h‚ÇÅw : w = z
        ¬∑ rwa [h‚ÇÅw]
        ¬∑ rw [(h‚ÇÅf _ w.2).order_eq_zero_iff.2 ((h‚ÇÅt' w hw) (Subtype.coe_ne_coe.mpr h‚ÇÅw))]
          exact ENat.zero_ne_top
      ¬∑ exact ‚ü®isOpen_induced h‚ÇÇt', h‚ÇÉt'‚ü©
  ¬∑ apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff] at hz
    obtain ‚ü®t', h‚ÇÅt', h‚ÇÇt', h‚ÇÉt'‚ü© := hz
    use Subtype.val ‚Åª¬π' t'
    simp only [mem_compl_iff, mem_singleton_iff, isOpen_induced h‚ÇÇt', mem_preimage,
      h‚ÇÉt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases h‚ÇÅw : w = z
    ¬∑ rw [h‚ÇÅw]
      tauto
    -- Nontrivial case: w ‚â† z
    use t' \ {z.1}, fun y h‚ÇÅy ‚Ü¶ h‚ÇÅt' y h‚ÇÅy.1, h‚ÇÇt'.sdiff isClosed_singleton
    apply (mem_diff w).1
    exact ‚ü®hw, mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 h‚ÇÅw)‚ü©

end AnalyticOnNhd
