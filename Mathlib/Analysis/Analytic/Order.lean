/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara, Stefan Kebekus
-/
module

public import Mathlib.Analysis.Analytic.IsolatedZeros
public import Mathlib.Analysis.Calculus.Deriv.Mul
public import Mathlib.Analysis.Calculus.Deriv.Pow
public import Mathlib.Analysis.Calculus.InverseFunctionTheorem.Analytic

/-!
# Vanishing Order of Analytic Functions

This file defines the order of vanishing of an analytic function `f` at a point `zâ‚€`, as an element
of `â„•âˆ`.

## TODO

Uniformize API between analytic and meromorphic functions
-/

@[expose] public section

open Filter Set
open scoped Topology

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-!
## Vanishing Order at a Point: Definition and Characterization
-/

section NormedSpace
variable {f g : ğ•œ â†’ E} {n : â„•} {zâ‚€ : ğ•œ}

open scoped Classical in
/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•âˆ`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `AnalyticAt.analyticOrderAt_eq_top` and
`AnalyticAt.analyticOrderAt_eq_natCast` for these equivalences.

If `f` isn't analytic at `zâ‚€`, then `analyticOrderAt f zâ‚€` returns a junk value of `0`. -/
noncomputable def analyticOrderAt (f : ğ•œ â†’ E) (zâ‚€ : ğ•œ) : â„•âˆ :=
  if hf : AnalyticAt ğ•œ f zâ‚€ then
    if h : âˆ€á¶  z in ğ“ zâ‚€, f z = 0 then âŠ¤
    else â†‘(hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose
  else 0

/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•`.

The order is defined to be `0` if `f` is identically zero on a neighbourhood of `zâ‚€`,
and is otherwise the unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`,
where `g` is analytic and does not vanish at `zâ‚€`. See `AnalyticAt.analyticOrderAt_eq_top` and
`AnalyticAt.analyticOrderAt_eq_natCast` for these equivalences.

If `f` isn't analytic at `zâ‚€`, then `analyticOrderNatAt f zâ‚€` returns a junk value of `0`. -/
noncomputable def analyticOrderNatAt (f : ğ•œ â†’ E) (zâ‚€ : ğ•œ) : â„• := (analyticOrderAt f zâ‚€).toNat

@[simp]
lemma analyticOrderAt_of_not_analyticAt (hf : Â¬ AnalyticAt ğ•œ f zâ‚€) : analyticOrderAt f zâ‚€ = 0 :=
  dif_neg hf

@[simp]
lemma analyticOrderNatAt_of_not_analyticAt (hf : Â¬ AnalyticAt ğ•œ f zâ‚€) :
    analyticOrderNatAt f zâ‚€ = 0 := by simp [analyticOrderNatAt, hf]

@[simp] lemma Nat.cast_analyticOrderNatAt (hf : analyticOrderAt f zâ‚€ â‰  âŠ¤) :
    analyticOrderNatAt f zâ‚€ = analyticOrderAt f zâ‚€ := ENat.coe_toNat hf

/-- The order of a function `f` at a `zâ‚€` is infinity iff `f` vanishes locally around `zâ‚€`. -/
lemma analyticOrderAt_eq_top : analyticOrderAt f zâ‚€ = âŠ¤ â†” âˆ€á¶  z in ğ“ zâ‚€, f z = 0 where
  mp hf := by unfold analyticOrderAt at hf; split_ifs at hf with h <;> simp [*] at *
  mpr hf := by unfold analyticOrderAt; simp [hf, analyticAt_congr hf, analyticAt_const]

lemma eventuallyConst_iff_analyticOrderAt_sub_eq_top :
    EventuallyConst f (ğ“ zâ‚€) â†” analyticOrderAt (f Â· - f zâ‚€) zâ‚€ = âŠ¤ := by
  simpa [eventuallyConst_iff_exists_eventuallyEq, analyticOrderAt_eq_top, sub_eq_zero]
    using âŸ¨fun âŸ¨c, hcâŸ© â†¦ (show f zâ‚€ = c from hc.self_of_nhds) â–¸ hc, fun h â†¦ âŸ¨_, hâŸ©âŸ©

/-- The order of an analytic function `f` at `zâ‚€` equals a natural number `n` iff `f` can locally
be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma AnalyticAt.analyticOrderAt_eq_natCast (hf : AnalyticAt ğ•œ f zâ‚€) :
    analyticOrderAt f zâ‚€ = n â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold analyticOrderAt
  split_ifs with h
  Â· simp only [ENat.top_ne_coe, false_iff]
    contrapose h
    rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff]
    exact âŸ¨n, hâŸ©
  Â· rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff] at h
    refine âŸ¨fun hn â†¦ (WithTop.coe_inj.mp hn : h.choose = n) â–¸ h.choose_spec, fun h' â†¦ ?_âŸ©
    rw [AnalyticAt.unique_eventuallyEq_pow_smul_nonzero h.choose_spec h']

/-- The order of an analytic function `f` at `zâ‚€` equals a natural number `n` iff `f` can locally
be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma AnalyticAt.analyticOrderNatAt_eq_iff (hf : AnalyticAt ğ•œ f zâ‚€) (hf' : analyticOrderAt f zâ‚€ â‰  âŠ¤)
    {n : â„•} :
    analyticOrderNatAt f zâ‚€ = n â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  simp [â† Nat.cast_inj (R := â„•âˆ), Nat.cast_analyticOrderNatAt hf', hf.analyticOrderAt_eq_natCast]

/-- The order of an analytic function `f` at `zâ‚€` is finite iff `f` can locally be written as `f z =
  (z - zâ‚€) ^ analyticOrderNatAt f zâ‚€ â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`.

See `MeromorphicNFAt.order_eq_zero_iff` for an analogous statement about meromorphic functions in
normal form.
-/
lemma AnalyticAt.analyticOrderAt_ne_top (hf : AnalyticAt ğ•œ f zâ‚€) :
    analyticOrderAt f zâ‚€ â‰  âŠ¤ â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
        f =á¶ [ğ“ zâ‚€] fun z â†¦ (z - zâ‚€) ^ analyticOrderNatAt f zâ‚€ â€¢ g z := by
  simp only [â† ENat.coe_toNat_eq_self, Eq.comm, EventuallyEq, â† hf.analyticOrderAt_eq_natCast,
    analyticOrderNatAt]

lemma analyticOrderAt_eq_zero : analyticOrderAt f zâ‚€ = 0 â†” Â¬ AnalyticAt ğ•œ f zâ‚€ âˆ¨ f zâ‚€ â‰  0 := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· rw [â† ENat.coe_zero, hf.analyticOrderAt_eq_natCast]
    constructor
    Â· intro âŸ¨g, _, _, hgâŸ©
      simpa [hf, hg.self_of_nhds]
    Â· exact fun hz â†¦ âŸ¨f, hf, hz.resolve_left <| not_not_intro hf, by simpâŸ©
  Â· simp [hf]

lemma analyticOrderAt_ne_zero : analyticOrderAt f zâ‚€ â‰  0 â†” AnalyticAt ğ•œ f zâ‚€ âˆ§ f zâ‚€ = 0 := by
  simp [analyticOrderAt_eq_zero]

/-- The order of an analytic function `f` at `zâ‚€` is zero iff `f` does not vanish at `zâ‚€`. -/
protected lemma AnalyticAt.analyticOrderAt_eq_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    analyticOrderAt f zâ‚€ = 0 â†” f zâ‚€ â‰  0 := by simp [hf, analyticOrderAt_eq_zero]

/-- The order of an analytic function `f` at `zâ‚€` is zero iff `f` does not vanish at `zâ‚€`. -/
protected lemma AnalyticAt.analyticOrderAt_ne_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    analyticOrderAt f zâ‚€ â‰  0 â†” f zâ‚€ = 0 := hf.analyticOrderAt_eq_zero.not_left

/-- A function vanishes at a point if its analytic order is nonzero in `â„•âˆ`. -/
lemma apply_eq_zero_of_analyticOrderAt_ne_zero (hf : analyticOrderAt f zâ‚€ â‰  0) :
    f zâ‚€ = 0 := by
  by_cases hf' : AnalyticAt ğ•œ f zâ‚€ <;> simp_all [analyticOrderAt_eq_zero]

/-- A function vanishes at a point if its analytic order is nonzero when converted to â„•. -/
lemma apply_eq_zero_of_analyticOrderNatAt_ne_zero (hf : analyticOrderNatAt f zâ‚€ â‰  0) :
    f zâ‚€ = 0 := by
  by_cases hf' : AnalyticAt ğ•œ f zâ‚€ <;> simp_all [analyticOrderNatAt, analyticOrderAt_eq_zero]

/-- Characterization of which natural numbers are `â‰¤ hf.order`. Useful for avoiding case splits,
since it applies whether or not the order is `âˆ`. -/
lemma natCast_le_analyticOrderAt (hf : AnalyticAt ğ•œ f zâ‚€) {n : â„•} :
    n â‰¤ analyticOrderAt f zâ‚€ â†”
      âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold analyticOrderAt
  split_ifs with h
  Â· simpa using âŸ¨0, analyticAt_const .., by simpaâŸ©
  Â· let m := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose
    obtain âŸ¨g, hg, hg_ne, hmâŸ© := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose_spec
    rw [ENat.coe_le_coe]
    refine âŸ¨fun hmn â†¦ âŸ¨fun z â†¦ (z - zâ‚€) ^ (m - n) â€¢ g z, by fun_prop, ?_âŸ©, fun âŸ¨h, hh, hfhâŸ© â†¦ ?_âŸ©
    Â· filter_upwards [hm] with z hz using by rwa [â† mul_smul, â† pow_add, Nat.add_sub_of_le hmn]
    Â· contrapose! hg_ne
      have : ContinuousAt (fun z â†¦ (z - zâ‚€) ^ (n - m) â€¢ h z) zâ‚€ := by fun_prop
      rw [tendsto_nhds_unique_of_eventuallyEq (l := ğ“[â‰ ] zâ‚€)
        hg.continuousAt.continuousWithinAt this.continuousWithinAt ?_]
      Â· simp [m, Nat.sub_ne_zero_of_lt hg_ne]
      Â· filter_upwards [self_mem_nhdsWithin, hm.filter_mono nhdsWithin_le_nhds,
          hfh.filter_mono nhdsWithin_le_nhds] with z hz hf' hf''
        rw [â† inv_smul_eq_iffâ‚€ (pow_ne_zero _ <| sub_ne_zero_of_ne hz), hf'', smul_comm,
          â† mul_smul] at hf'
        rw [pow_subâ‚€ _ (sub_ne_zero_of_ne hz) (by lia), â† hf']

/-- If two functions agree in a neighborhood of `zâ‚€`, then their orders at `zâ‚€` agree. -/
lemma analyticOrderAt_congr (hfg : f =á¶ [ğ“ zâ‚€] g) :
    analyticOrderAt f zâ‚€ = analyticOrderAt g zâ‚€ := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· refine ENat.eq_of_forall_natCast_le_iff fun n â†¦ ?_
    simp only [natCast_le_analyticOrderAt, hf, hf.congr hfg]
    congr! 3
    exact hfg.congr_left
  Â· rw [analyticOrderAt_of_not_analyticAt hf,
      analyticOrderAt_of_not_analyticAt fun hg â†¦ hf <| hg.congr hfg.symm]

@[simp] lemma analyticOrderAt_id : analyticOrderAt (ğ•œ := ğ•œ) id 0 = 1 :=
  analyticAt_id.analyticOrderAt_eq_natCast.mpr âŸ¨fun _ â†¦ 1, by fun_prop, by simp, by simpâŸ©

@[simp] lemma analyticOrderAt_neg : analyticOrderAt (-f) zâ‚€ = analyticOrderAt f zâ‚€ := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· refine ENat.eq_of_forall_natCast_le_iff fun n â†¦ ?_
    simp only [natCast_le_analyticOrderAt, hf, hf.neg]
    exact (Equiv.neg _).exists_congr <| by simp [neg_eq_iff_eq_neg]
  Â· rw [analyticOrderAt_of_not_analyticAt hf,
      analyticOrderAt_of_not_analyticAt <| analyticAt_neg.not.2 hf]

/-- The order of a sum is at least the minimum of the orders of the summands. -/
theorem le_analyticOrderAt_add :
    min (analyticOrderAt f zâ‚€) (analyticOrderAt g zâ‚€) â‰¤ analyticOrderAt (f + g) zâ‚€ := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· by_cases hg : AnalyticAt ğ•œ g zâ‚€
    Â· refine ENat.forall_natCast_le_iff_le.mp fun n â†¦ ?_
      simp only [le_min_iff, natCast_le_analyticOrderAt, hf, hg, hf.add hg]
      refine fun âŸ¨âŸ¨F, hF, hF'âŸ©, âŸ¨G, hG, hG'âŸ©âŸ© â†¦ âŸ¨F + G, hF.add hG, ?_âŸ©
      filter_upwards [hF', hG'] with z using by simp +contextual
    Â· simp [*]
  Â· simp [*]

lemma le_analyticOrderAt_sub :
    min (analyticOrderAt f zâ‚€) (analyticOrderAt g zâ‚€) â‰¤ analyticOrderAt (f - g) zâ‚€ := by
  simpa [sub_eq_add_neg] using le_analyticOrderAt_add (f := f) (g := -g)

lemma analyticOrderAt_add_eq_left_of_lt (hfg : analyticOrderAt f zâ‚€ < analyticOrderAt g zâ‚€) :
    analyticOrderAt (f + g) zâ‚€ = analyticOrderAt f zâ‚€ :=
  le_antisymm (by simpa [hfg.not_ge] using le_analyticOrderAt_sub (f := f + g) (g := g) (zâ‚€ := zâ‚€))
    (by simpa [hfg.le] using le_analyticOrderAt_add (f := f) (g := g) (zâ‚€ := zâ‚€))

lemma analyticOrderAt_add_eq_right_of_lt (hgf : analyticOrderAt g zâ‚€ < analyticOrderAt f zâ‚€) :
    analyticOrderAt (f + g) zâ‚€ = analyticOrderAt g zâ‚€ := by
  rw [add_comm, analyticOrderAt_add_eq_left_of_lt hgf]

/-- If two functions have unequal orders, then the order of their sum is exactly the minimum
of the orders of the summands. -/
lemma analyticOrderAt_add_of_ne (hfg : analyticOrderAt f zâ‚€ â‰  analyticOrderAt g zâ‚€) :
    analyticOrderAt (f + g) zâ‚€ = min (analyticOrderAt f zâ‚€) (analyticOrderAt g zâ‚€) := by
  obtain hfg | hgf := hfg.lt_or_gt
  Â· simpa [hfg.le] using analyticOrderAt_add_eq_left_of_lt hfg
  Â· simpa [hgf.le] using analyticOrderAt_add_eq_right_of_lt hgf

lemma analyticOrderAt_smul_eq_top_of_left {f : ğ•œ â†’ ğ•œ} (hf : analyticOrderAt f zâ‚€ = âŠ¤) :
     analyticOrderAt (f â€¢ g) zâ‚€ = âŠ¤ := by
  rw [analyticOrderAt_eq_top, eventually_nhds_iff] at *
  obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hf
  exact âŸ¨t, fun y hy â†¦ by simp [hâ‚t y hy], hâ‚‚t, hâ‚ƒtâŸ©

lemma analyticOrderAt_smul_eq_top_of_right {f : ğ•œ â†’ ğ•œ} (hg : analyticOrderAt g zâ‚€ = âŠ¤) :
    analyticOrderAt (f â€¢ g) zâ‚€ = âŠ¤ := by
  rw [analyticOrderAt_eq_top, eventually_nhds_iff] at *
  obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hg
  exact âŸ¨t, fun y hy â†¦ by simp [hâ‚t y hy], hâ‚‚t, hâ‚ƒtâŸ©

/-- The order is additive when scalar multiplying analytic functions. -/
lemma analyticOrderAt_smul {f : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    analyticOrderAt (f â€¢ g) zâ‚€ = analyticOrderAt f zâ‚€ + analyticOrderAt g zâ‚€ := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  by_cases hf' : analyticOrderAt f zâ‚€ = âŠ¤
  Â· simp [analyticOrderAt_smul_eq_top_of_left, *]
  by_cases hg' : analyticOrderAt g zâ‚€ = âŠ¤
  Â· simp [analyticOrderAt_smul_eq_top_of_right, *]
  -- Non-trivial case: both functions do not vanish around zâ‚€
  obtain âŸ¨f', hâ‚f', hâ‚‚f', hâ‚ƒf'âŸ© := hf.analyticOrderAt_ne_top.1 hf'
  obtain âŸ¨g', hâ‚g', hâ‚‚g', hâ‚ƒg'âŸ© := hg.analyticOrderAt_ne_top.1 hg'
  rw [â† Nat.cast_analyticOrderNatAt hf', â† Nat.cast_analyticOrderNatAt hg', â† ENat.coe_add,
      (hf.smul hg).analyticOrderAt_eq_natCast]
  refine âŸ¨f' â€¢ g', hâ‚f'.smul hâ‚g', ?_, ?_âŸ©
  Â· simp
    tauto
  Â· obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := eventually_nhds_iff.1 hâ‚ƒf'
    obtain âŸ¨s, hâ‚s, hâ‚‚s, hâ‚ƒsâŸ© := eventually_nhds_iff.1 hâ‚ƒg'
    exact eventually_nhds_iff.2
      âŸ¨t âˆ© s, fun y hy â†¦ (by simp [hâ‚t y hy.1, hâ‚s y hy.2]; module), hâ‚‚t.inter hâ‚‚s, hâ‚ƒt, hâ‚ƒsâŸ©

theorem AnalyticAt.analyticOrderAt_deriv_add_one {x : ğ•œ} (hf : AnalyticAt ğ•œ f x)
    [CompleteSpace E] [CharZero ğ•œ] :
    analyticOrderAt (deriv f) x + 1 = analyticOrderAt (f Â· - f x) x := by
  generalize h : analyticOrderAt (f Â· - f x) x = r
  cases r with
  | top =>
    suffices analyticOrderAt (deriv f) x = âŠ¤ by simp_all
    simp only [analyticOrderAt_eq_top, sub_eq_zero] at h âŠ¢
    obtain âŸ¨U, hUf, hUo, hUxâŸ© := eventually_nhds_iff.mp h
    filter_upwards [hUo.mem_nhds hUx] with y hy
    simp [(eventuallyEq_of_mem (hUo.mem_nhds hy) hUf).deriv_eq]
  | coe r =>
    have hrne : r â‰  0 := by
      intro hr
      rw [hr, ENat.coe_zero, AnalyticAt.analyticOrderAt_eq_zero (by fun_prop)] at h
      grind
    obtain âŸ¨s, rflâŸ© := Nat.exists_add_one_eq.mpr (Nat.pos_of_ne_zero hrne)
    rw [Nat.cast_succ]
    congr 1
    rw [analyticOrderAt_eq_natCast (by fun_prop)] at h
    obtain âŸ¨F, hFa, hFne, hfFâŸ© := h
    simp only [sub_eq_iff_eq_add] at hfF
    obtain âŸ¨U, hUf, hUo, hUxâŸ© := eventually_nhds_iff.mp (hfF.and hFa.eventually_analyticAt)
    have : âˆ€ y âˆˆ U, deriv f y =
        (y - x) ^ (s + 1) â€¢ deriv F y + (s + 1) â€¢ (y - x) ^ s â€¢ F y := by
      intro y hy
      rw [EventuallyEq.deriv_eq (eventually_of_mem (hUo.mem_nhds hy) (fun u hu â†¦ (hUf u hu).1)),
        deriv_add_const, deriv_fun_smul (by fun_prop) (hUf y hy).2.differentiableAt]
      simp [mul_smul, add_smul, Nat.cast_smul_eq_nsmul]
    rw [analyticOrderAt_congr (eventually_of_mem (hUo.mem_nhds hUx) this)]
    have : analyticOrderAt (fun y â†¦ (s + 1) â€¢ (y - x) ^ s â€¢ F y) x = s := by
      rw [analyticOrderAt_eq_natCast]
      Â· refine âŸ¨fun z â†¦ (â†‘(s + 1) : ğ•œ) â€¢ F z, hFa.fun_const_smul, ?_, .of_forall fun y â†¦ ?_âŸ©
        Â· simpa using âŸ¨by norm_cast, hFneâŸ©
        Â· simpa only [Nat.cast_smul_eq_nsmul] using smul_comm ..
      Â· simp_rw [â† Nat.cast_smul_eq_nsmul ğ•œ]
        fun_prop
    rwa [â† Pi.add_def, analyticOrderAt_add_eq_right_of_lt]
    rw [this, â† Order.succ_le_iff_of_not_isMax (not_isMax_iff.mpr âŸ¨âŠ¤, ENat.coe_lt_top sâŸ©),
      ENat.succ_def, â† Nat.cast_add_one, natCast_le_analyticOrderAt (by fun_prop)]
    exact âŸ¨deriv F, hFa.deriv, by simpâŸ©

theorem AnalyticAt.analyticOrderAt_sub_eq_one_of_deriv_ne_zero {x : ğ•œ} (hf : AnalyticAt ğ•œ f x)
    (hf' : deriv f x â‰  0) : analyticOrderAt (f Â· - f x) x = 1 := by
  generalize h : analyticOrderAt (f Â· - f x) x = r
  cases r with
  | top =>
    simp_rw [analyticOrderAt_eq_top, sub_eq_zero] at h
    refine (hf' ?_).elim
    rw [EventuallyEq.deriv_eq h, deriv_const]
  | coe r =>
    norm_cast
    obtain âŸ¨F, hFa, hFne, hfFâŸ© := (analyticOrderAt_eq_natCast (by fun_prop)).mp h
    apply eq_of_ge_of_le
    Â· by_contra! hr
      have := hfF.self_of_nhds
      simp_all
    Â· contrapose! hf'
      simp_rw [sub_eq_iff_eq_add] at hfF
      rw [EventuallyEq.deriv_eq hfF, deriv_add_const, deriv_fun_smul (by fun_prop) (by fun_prop),
        deriv_fun_pow (by fun_prop), sub_self, zero_pow (by omega), zero_pow (by omega),
        mul_zero, zero_mul, zero_smul, zero_smul, add_zero]

lemma natCast_le_analyticOrderAt_iff_iteratedDeriv_eq_zero [CharZero ğ•œ] [CompleteSpace E]
    (hf : AnalyticAt ğ•œ f zâ‚€) :
    n â‰¤ analyticOrderAt f zâ‚€ â†” âˆ€ i < n, iteratedDeriv i f zâ‚€ = 0 := by
  induction n generalizing f with
  | zero => simp
  | succ n IH =>
    by_cases hfz : f zâ‚€ = 0; swap
    Â· simpa [analyticOrderAt_eq_zero.mpr (.inr hfz)] using âŸ¨0, by simp, by simpaâŸ©
    have : analyticOrderAt (deriv f) zâ‚€ + 1 = analyticOrderAt f zâ‚€ := by
      simpa [hfz] using hf.analyticOrderAt_deriv_add_one
    simp [â† this, IH hf.deriv, iteratedDeriv_succ',
      -Order.lt_add_one_iff, Nat.forall_lt_succ_left, hfz]

end NormedSpace

/-!
## Vanishing Order at a Point: Elementary Computations
-/

/-- Simplifier lemma for the order of a centered monomial -/
@[simp]
lemma analyticOrderAt_centeredMonomial {zâ‚€ : ğ•œ} {n : â„•} :
    analyticOrderAt ((Â· - zâ‚€) ^ n) zâ‚€ = n := by
  rw [AnalyticAt.analyticOrderAt_eq_natCast (by fun_prop)]
  exact âŸ¨1, by simp [Pi.one_def, analyticAt_const]âŸ©

section NontriviallyNormedField
variable {f g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}

lemma analyticOrderAt_mul_eq_top_of_left (hf : analyticOrderAt f zâ‚€ = âŠ¤) :
    analyticOrderAt (f * g) zâ‚€ = âŠ¤ := analyticOrderAt_smul_eq_top_of_left hf

lemma analyticOrderAt_mul_eq_top_of_right (hg : analyticOrderAt g zâ‚€ = âŠ¤) :
    analyticOrderAt (f * g) zâ‚€ = âŠ¤ := analyticOrderAt_smul_eq_top_of_right hg

/-- The order is additive when multiplying analytic functions. -/
theorem analyticOrderAt_mul (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    analyticOrderAt (f * g) zâ‚€ = analyticOrderAt f zâ‚€ + analyticOrderAt g zâ‚€ :=
  analyticOrderAt_smul hf hg

/-- The order is additive when multiplying analytic functions. -/
theorem analyticOrderNatAt_mul (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€)
    (hf' : analyticOrderAt f zâ‚€ â‰  âŠ¤) (hg' : analyticOrderAt g zâ‚€ â‰  âŠ¤) :
    analyticOrderNatAt (f * g) zâ‚€ = analyticOrderNatAt f zâ‚€ + analyticOrderNatAt g zâ‚€ := by
  simp [analyticOrderNatAt, analyticOrderAt_mul, ENat.toNat_add, *]

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem analyticOrderAt_pow (hf : AnalyticAt ğ•œ f zâ‚€) :
    âˆ€ n, analyticOrderAt (f ^ n) zâ‚€ = n â€¢ analyticOrderAt f zâ‚€
  | 0 => by simp [analyticOrderAt_eq_zero]
  | n + 1 => by simp [add_mul, pow_add, analyticOrderAt_mul (hf.pow n), analyticOrderAt_pow, hf]

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem analyticOrderNatAt_pow (hf : AnalyticAt ğ•œ f zâ‚€) (n : â„•) :
    analyticOrderNatAt (f ^ n) zâ‚€ = n â€¢ analyticOrderNatAt f zâ‚€ := by
  simp [analyticOrderNatAt, analyticOrderAt_pow, hf]

end NontriviallyNormedField

section comp

/-!
## Vanishing Order at a Point: Composition
-/
variable {f : ğ•œ â†’ E} {g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}

/-- Analytic order of a composition of analytic functions. -/
lemma AnalyticAt.analyticOrderAt_comp (hf : AnalyticAt ğ•œ f (g zâ‚€)) (hg : AnalyticAt ğ•œ g zâ‚€) :
    analyticOrderAt (f âˆ˜ g) zâ‚€ = analyticOrderAt f (g zâ‚€) * analyticOrderAt (g Â· - g zâ‚€) zâ‚€ := by
  by_cases hg_nc : EventuallyConst g (ğ“ zâ‚€)
  Â· -- If `g` is eventually constant, both sides are either `âŠ¤` or `0`.
    have := hg_nc.comp f
    rw [eventuallyConst_iff_analyticOrderAt_sub_eq_top] at hg_nc this
    rw [hg_nc]
    by_cases hf' : f (g zâ‚€) = 0
    Â· simpa [hf', show analyticOrderAt f (g zâ‚€) â‰  0 by grind [analyticOrderAt_ne_zero]]
    Â· rw [show analyticOrderAt f (g zâ‚€) = 0 from ?_, zero_mul] <;>
      grind [hf.comp hg, AnalyticAt.analyticOrderAt_eq_zero]
  by_cases hf' : analyticOrderAt f (g zâ‚€) = âŠ¤
  Â· -- If `f` is eventually constant but `g` is not, we have `âŠ¤ = âŠ¤ * (non-zero thing)`
    rw [hf', analyticOrderAt_eq_top.mpr
      (EventuallyEq.comp_tendsto (analyticOrderAt_eq_top.mp hf') hg.continuousAt), ENat.top_mul]
    rw [AnalyticAt.analyticOrderAt_ne_zero (by fun_prop), sub_eq_zero]
  Â· -- The interesting case: both orders are finite. First unpack the data:
    rw [eventuallyConst_iff_analyticOrderAt_sub_eq_top] at hg_nc
    obtain âŸ¨r, hrâŸ© := ENat.ne_top_iff_exists.mp hf'
    obtain âŸ¨s, hsâŸ© := ENat.ne_top_iff_exists.mp hg_nc
    rw [â† hr, â† hs, â† ENat.coe_mul, (hf.comp hg).analyticOrderAt_eq_natCast]
    rw [Eq.comm, hf.analyticOrderAt_eq_natCast] at hr
    rcases hr with âŸ¨F, hFa, hFne, hfFâŸ©
    rw [Eq.comm, AnalyticAt.analyticOrderAt_eq_natCast (by fun_prop)] at hs
    rcases hs with âŸ¨G, hGa, hGne, hgGâŸ©
    -- Now write `f âˆ˜ g` locally as the product of `(z - zâ‚€) ^ (r * s)` and the
    -- non-vanishing analytic function `fun z â†¦ (G z) ^ r â€¢ F (g z)`.
    refine âŸ¨fun z â†¦ (G z) ^ r â€¢ F (g z), by fun_prop, by aesop, ?_âŸ©
    filter_upwards [EventuallyEq.comp_tendsto hfF hg.continuousAt, hgG] with z hfz hgz
    simp only [hfz, Function.comp_def, hgz, smul_eq_mul, mul_pow, mul_smul, mul_comm r s, pow_mul]

/-- If `g` is analytic at `x`, and `g' x â‰  0`, then the analytic order of
`f âˆ˜ g` at `x` is the analytic order of `f` at `g x` (even if `f` is not analytic). -/
lemma analyticOrderAt_comp_of_deriv_ne_zero (hg : AnalyticAt ğ•œ g zâ‚€) (hg' : deriv g zâ‚€ â‰  0)
    [CompleteSpace ğ•œ] [CharZero ğ•œ] :
    analyticOrderAt (f âˆ˜ g) zâ‚€ = analyticOrderAt f (g zâ‚€) := by
  by_cases hf : AnalyticAt ğ•œ f (g zâ‚€)
  Â· simp [hf.analyticOrderAt_comp hg, hg.analyticOrderAt_sub_eq_one_of_deriv_ne_zero hg']
  Â· rw [analyticOrderAt_of_not_analyticAt hf, analyticOrderAt_of_not_analyticAt]
    rwa [analyticAt_comp_iff_of_deriv_ne_zero hg hg']

end comp

/-!
## Level Sets of the Order Function
-/

namespace AnalyticOnNhd

variable {U : Set ğ•œ} {f : ğ•œ â†’ E}

/-- The set where an analytic function has infinite order is clopen in its domain of analyticity. -/
theorem isClopen_setOf_analyticOrderAt_eq_top (hf : AnalyticOnNhd ğ•œ f U) :
    IsClopen {u : U | analyticOrderAt f u = âŠ¤} := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† analyticOrderAt_eq_top] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        push _ âˆˆ _
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [(hf _ w.2).analyticOrderAt_eq_zero.2 ((hâ‚t' w hw) (Subtype.coe_ne_coe.mpr hâ‚w))]
          exact ENat.zero_ne_top
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [analyticOrderAt_eq_top, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [analyticOrderAt_eq_top, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [isOpen_induced hâ‚‚t', mem_preimage, hâ‚ƒt', and_self, and_true]
    grind

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_analyticOrderAt_ne_top_iff_forall (hf : AnalyticOnNhd ğ•œ f U) (hU : IsConnected U) :
    (âˆƒ u : U, analyticOrderAt f u â‰  âŠ¤) â†” (âˆ€ u : U, analyticOrderAt f u â‰  âŠ¤) := by
  have : ConnectedSpace U := Subtype.connectedSpace hU
  obtain âŸ¨vâŸ© : Nonempty U := inferInstance
  suffices (âˆ€ (u : U), analyticOrderAt f u â‰  âŠ¤) âˆ¨ âˆ€ (u : U), analyticOrderAt f u = âŠ¤ by tauto
  simpa [Set.eq_empty_iff_forall_notMem, Set.eq_univ_iff_forall] using
      isClopen_iff.1 hf.isClopen_setOf_analyticOrderAt_eq_top

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem analyticOrderAt_ne_top_of_isPreconnected {x y : ğ•œ} (hf : AnalyticOnNhd ğ•œ f U)
    (hU : IsPreconnected U) (hâ‚x : x âˆˆ U) (hy : y âˆˆ U) (hâ‚‚x : analyticOrderAt f x â‰  âŠ¤) :
    analyticOrderAt f y â‰  âŠ¤ :=
  (hf.exists_analyticOrderAt_ne_top_iff_forall âŸ¨nonempty_of_mem hâ‚x, hUâŸ©).1 (by use âŸ¨x, hâ‚xâŸ©)
    âŸ¨y, hyâŸ©

/-- The set where an analytic function has zero or infinite order is discrete within its domain of
analyticity. -/
theorem codiscrete_setOf_analyticOrderAt_eq_zero_or_top (hf : AnalyticOnNhd ğ•œ f U) :
    {u : U | analyticOrderAt f u = 0 âˆ¨ analyticOrderAt f u = âŠ¤} âˆˆ Filter.codiscrete U := by
  simp_rw [mem_codiscrete_subtype_iff_mem_codiscreteWithin, mem_codiscreteWithin,
    disjoint_principal_right]
  intro x hx
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚f.eventually_nhds] with a ha
    simp [analyticOrderAt_eq_top, ha]
  Â· filter_upwards [hâ‚f] with a ha
    simp +contextual [(hf a _).analyticOrderAt_eq_zero, ha]

/--
The set where an analytic function has zero or infinite order is discrete within its domain of
analyticity.
-/
theorem codiscreteWithin_setOf_analyticOrderAt_eq_zero_or_top (hf : AnalyticOnNhd ğ•œ f U) :
    {u : ğ•œ | analyticOrderAt f u = 0 âˆ¨ analyticOrderAt f u = âŠ¤} âˆˆ codiscreteWithin U := by
  simp_rw [mem_codiscreteWithin, disjoint_principal_right]
  intro x hx
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚f.eventually_nhds] with a ha
    simp [analyticOrderAt_eq_top, ha]
  Â· filter_upwards [hâ‚f] with a ha
    simp +contextual [(hf a _).analyticOrderAt_eq_zero, ha]

/--
If an analytic function `f` is not constantly zero on a connected set `U`, then its set of zeros is
codiscrete within `U`.

See `AnalyticOnNhd.preimage_mem_codiscreteWithin` for a more general statement in preimages of
codiscrete sets.
-/
theorem preimage_zero_mem_codiscreteWithin {x : ğ•œ} (hâ‚f : AnalyticOnNhd ğ•œ f U) (hâ‚‚f : f x â‰  0)
    (hx : x âˆˆ U) (hU : IsConnected U) :
    f â»Â¹' {0}á¶œ âˆˆ codiscreteWithin U := by
  filter_upwards [hâ‚f.codiscreteWithin_setOf_analyticOrderAt_eq_zero_or_top,
    self_mem_codiscreteWithin U] with a ha hâ‚‚a
  rw [â† (hâ‚f x hx).analyticOrderAt_eq_zero] at hâ‚‚f
  have {u : U} : analyticOrderAt f u â‰  âŠ¤ := by
    apply (hâ‚f.exists_analyticOrderAt_ne_top_iff_forall hU).1
    use âŸ¨x, hxâŸ©
    simp_all
  simp_all [(hâ‚f a hâ‚‚a).analyticOrderAt_eq_zero]

/--
If an analytic function `f` is not constantly zero on `ğ•œ`, then its set of zeros is codiscrete.

See `AnalyticOnNhd.preimage_mem_codiscreteWithin` for a more general statement in preimages of
codiscrete sets.
-/
theorem preimage_zero_mem_codiscrete [ConnectedSpace ğ•œ] {x : ğ•œ} (hf : AnalyticOnNhd ğ•œ f Set.univ)
    (hx : f x â‰  0) :
    f â»Â¹' {0}á¶œ âˆˆ codiscrete ğ•œ :=
  hf.preimage_zero_mem_codiscreteWithin hx trivial isConnected_univ

end AnalyticOnNhd
