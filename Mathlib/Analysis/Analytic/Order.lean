/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara, Stefan Kebekus
-/
import Mathlib.Analysis.Analytic.IsolatedZeros

/-!
# Vanishing Order of Analytic Functions

This file defines the order of vanishing of an analytic function `f` at a point `zâ‚€`, as an element
of `â„•âˆ`.

TODO: Uniformize API between analytic and meromorphic functions
-/

open Filter  Set
open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f fâ‚ fâ‚‚ g : ğ•œ â†’ E} {n : â„•} {zâ‚€ : ğ•œ}

/-!
## Vanishing Order at a Point: Definition and Characterization
-/

namespace AnalyticAt

open scoped Classical in

/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•âˆ`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `AnalyticAt.order_eq_top_iff` and `AnalyticAt.order_eq_nat_iff` for
these equivalences. -/
noncomputable def order (hf : AnalyticAt ğ•œ f zâ‚€) : ENat :=
  if h : âˆ€á¶  z in ğ“ zâ‚€, f z = 0 then âŠ¤
  else â†‘(hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose

/-- The order of an analytic function `f` at a `zâ‚€` is infinity iff `f` vanishes locally around
`zâ‚€`. -/
lemma order_eq_top_iff (hf : AnalyticAt ğ•œ f zâ‚€) : hf.order = âŠ¤ â†” âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by
  unfold order
  split_ifs with h
  Â· rwa [eq_self, true_iff]
  Â· simpa only [ne_eq, ENat.coe_ne_top, false_iff] using h

/-- The order of an analytic function `f` at `zâ‚€` equals a natural number `n` iff `f` can locally
be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma order_eq_nat_iff {n : â„•} (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order = â†‘n â†”
    âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold order
  split_ifs with h
  Â· simp only [ENat.top_ne_coe, false_iff]
    contrapose! h
    rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff]
    exact âŸ¨n, hâŸ©
  Â· rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff] at h
    refine âŸ¨fun hn â†¦ (WithTop.coe_inj.mp hn : h.choose = n) â–¸ h.choose_spec, fun h' â†¦ ?_âŸ©
    rw [unique_eventuallyEq_pow_smul_nonzero h.choose_spec h']

/-- The order of an analytic function `f` at `zâ‚€` is finite iff `f` can locally be written as `f z =
  (z - zâ‚€) ^ order â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`.

See `MeromorphicNFAt.order_eq_zero_iff` for an analogous statement about meromorphic functions in
normal form.
-/
lemma order_ne_top_iff (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order â‰  âŠ¤ â†” âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0
      âˆ§ f =á¶ [ğ“ zâ‚€] fun z â†¦ (z - zâ‚€) ^ (hf.order.toNat) â€¢ g z := by
  simp only [â† ENat.coe_toNat_eq_self, Eq.comm, EventuallyEq, â† hf.order_eq_nat_iff]

@[deprecated (since := "2025-02-03")]
alias order_neq_top_iff := order_ne_top_iff

/-- The order of an analytic function `f` at `zâ‚€` is zero iff `f` does not vanish at `zâ‚€`. -/
lemma order_eq_zero_iff (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order = 0 â†” f zâ‚€ â‰  0 := by
  rw [â† ENat.coe_zero, hf.order_eq_nat_iff]
  constructor
  Â· intro âŸ¨g, _, _, hgâŸ©
    simpa [hg.self_of_nhds]
  Â· exact fun hz â†¦ âŸ¨f, hf, hz, by simpâŸ©

/-- An analytic function vanishes at a point if its order is nonzero when converted to â„•. -/
lemma apply_eq_zero_of_order_toNat_ne_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    hf.order.toNat â‰  0 â†’ f zâ‚€ = 0 := by
  simp [hf.order_eq_zero_iff]
  tauto

/-- Characterization of which natural numbers are `â‰¤ hf.order`. Useful for avoiding case splits,
since it applies whether or not the order is `âˆ`. -/
lemma natCast_le_order_iff (hf : AnalyticAt ğ•œ f zâ‚€) {n : â„•} :
    n â‰¤ hf.order â†” âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold order
  split_ifs with h
  Â· simpa using âŸ¨0, analyticAt_const .., by simpaâŸ©
  Â· let m := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose
    obtain âŸ¨g, hg, hg_ne, hmâŸ© := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose_spec
    rw [ENat.coe_le_coe]
    refine âŸ¨fun hmn â†¦ âŸ¨fun z â†¦ (z - zâ‚€) ^ (m - n) â€¢ g z, by fun_prop, ?_âŸ©, fun âŸ¨h, hh, hfhâŸ© â†¦ ?_âŸ©
    Â· filter_upwards [hm] with z hz using by rwa [â† mul_smul, â† pow_add, Nat.add_sub_of_le hmn]
    Â· contrapose! hg_ne
      have : ContinuousAt (fun z â†¦ (z - zâ‚€) ^ (n - m) â€¢ h z) zâ‚€ := by fun_prop
      rw [tendsto_nhds_unique_of_eventuallyEq (l := ğ“[â‰ ] zâ‚€)
        hg.continuousAt.continuousWithinAt this.continuousWithinAt ?_]
      Â· simp [m, Nat.sub_ne_zero_of_lt hg_ne]
      Â· filter_upwards [self_mem_nhdsWithin, hm.filter_mono nhdsWithin_le_nhds,
          hfh.filter_mono nhdsWithin_le_nhds] with z hz hf' hf''
        rw [â† inv_smul_eq_iffâ‚€ (pow_ne_zero _ <| sub_ne_zero_of_ne hz), hf'', smul_comm,
          â† mul_smul] at hf'
        rw [pow_subâ‚€ _ (sub_ne_zero_of_ne hz) (by omega), â† hf']

/-- If two functions agree in a neighborhood of `zâ‚€`, then their orders at `zâ‚€` agree. -/
theorem order_congr (hfâ‚ : AnalyticAt ğ•œ fâ‚ zâ‚€) (h : fâ‚ =á¶ [ğ“ zâ‚€] fâ‚‚) :
    (hfâ‚.congr h).order = hfâ‚.order := by
  refine ENat.eq_of_forall_natCast_le_iff fun n â†¦ ?_
  simpa only [natCast_le_order_iff] using âŸ¨fun âŸ¨g, hg, hfgâŸ© â†¦ âŸ¨g, hg, h.trans hfgâŸ©,
    fun âŸ¨g, hg, hfgâŸ© â†¦ âŸ¨g, hg, h.symm.trans hfgâŸ©âŸ©

/-!
## Vanishing Order at a Point: Elementary Computations
-/

/-- Helper lemma, required to state analyticAt_order_centeredMonomial below -/
lemma analyticAt_centeredMonomial (zâ‚€ : ğ•œ) (n : â„•) :
    AnalyticAt ğ•œ ((Â· - zâ‚€) ^ n) zâ‚€ := by fun_prop

/-- Simplifier lemma for the order of a centered monomial -/
@[simp]
lemma analyticAt_order_centeredMonomial {zâ‚€ : ğ•œ} {n : â„•} :
    (analyticAt_centeredMonomial zâ‚€ n).order = n := by
  rw [AnalyticAt.order_eq_nat_iff]
  use 1
  simp only [Pi.one_apply, ne_eq, one_ne_zero, not_false_eq_true, Pi.pow_apply, smul_eq_mul,
    mul_one, eventually_true, and_self, and_true]
  exact analyticAt_const

/-!
## Vanishing Order at a Point: Behaviour under Standard Operations

The theorems `AnalyticAt.order_mul` and `AnalyticAt.order_pow` establish additivity of the order
under multiplication and taking powers. The theorem `AnalyticAt.order_add` establishes behavior
under addition.
-/

/-- The order is additive when scalar multiplying analytic functions. -/
theorem order_smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    (hf.smul hg).order = hf.order + hg.order := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  by_cases hâ‚‚f : hf.order = âŠ¤
  Â· rw [hâ‚‚f, top_add, order_eq_top_iff]
    filter_upwards [hf.order_eq_top_iff.mp hâ‚‚f] using by simp +contextual
  by_cases hâ‚‚g : hg.order = âŠ¤
  Â· rw [hâ‚‚g, add_top, order_eq_top_iff]
    filter_upwards [hg.order_eq_top_iff.mp hâ‚‚g] using by simp +contextual
  -- Non-trivial case: both functions do not vanish around zâ‚€
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hf.order_ne_top_iff.1 hâ‚‚f
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := hg.order_ne_top_iff.1 hâ‚‚g
  rw [â† ENat.coe_toNat hâ‚‚f, â† ENat.coe_toNat hâ‚‚g, â† ENat.coe_add, (hf.smul hg).order_eq_nat_iff]
  refine âŸ¨_, hâ‚gâ‚.smul hâ‚gâ‚‚, by simp [hâ‚‚gâ‚, hâ‚‚gâ‚‚], ?_âŸ©
  filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚] with a hâ‚a hâ‚‚a
  simp [hâ‚a, hâ‚‚a, â† smul_assoc, pow_add, mul_right_comm]

/-- The order is additive when multiplying analytic functions. -/
theorem order_mul {f g : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    (hf.mul hg).order = hf.order + hg.order :=
  hf.order_smul hg

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem order_pow {f : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) {n : â„•} :
    (hf.pow n).order = n â€¢ hf.order := by
  induction n
  case zero =>
    simp [AnalyticAt.order_eq_zero_iff]
  case succ n hn =>
    simp [add_mul, pow_add, (hf.pow n).order_mul hf, hn]

/-- The order of a sum is at least the minimum of the orders of the summands. -/
theorem order_add (hfâ‚ : AnalyticAt ğ•œ fâ‚ zâ‚€) (hfâ‚‚ : AnalyticAt ğ•œ fâ‚‚ zâ‚€) :
    min hfâ‚.order hfâ‚‚.order â‰¤ (hfâ‚.add hfâ‚‚).order := by
  refine ENat.forall_natCast_le_iff_le.mp fun n â†¦ ?_
  simp only [le_min_iff, natCast_le_order_iff]
  refine fun âŸ¨âŸ¨F, hF, hF'âŸ©, âŸ¨G, hG, hG'âŸ©âŸ© â†¦ âŸ¨F + G, hF.add hG, ?_âŸ©
  filter_upwards [hF', hG'] with z using by simp +contextual

/-- Helper lemma for AnalyticAt.order_add_of_unequal_order -/
lemma order_add_of_order_lt_order (hfâ‚ : AnalyticAt ğ•œ fâ‚ zâ‚€) (hfâ‚‚ : AnalyticAt ğ•œ fâ‚‚ zâ‚€)
    (h : hfâ‚.order < hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = hfâ‚.order := by
  lift hfâ‚.order to â„• using h.ne_top with nâ‚ hnâ‚
  simp only [eq_comm (a := (nâ‚ : â„•âˆ)), order_eq_nat_iff] at hnâ‚ âŠ¢
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hnâ‚
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚âŸ© := hfâ‚‚.natCast_le_order_iff.mp (Order.add_one_le_of_lt h)
  refine âŸ¨gâ‚ + (Â· - zâ‚€) â€¢ gâ‚‚, by fun_prop, by simpa using hâ‚‚gâ‚, ?_âŸ©
  filter_upwards [hâ‚ƒgâ‚, hâ‚‚gâ‚‚] with a hâ‚a hâ‚‚a
  simp [mul_smul, pow_succ, hâ‚a, hâ‚‚a]

/-- If two functions have unequal orders, then the order of their sum is exactly the minimum
of the orders of the summands. -/
theorem order_add_of_order_ne_order (hfâ‚ : AnalyticAt ğ•œ fâ‚ zâ‚€) (hfâ‚‚ : AnalyticAt ğ•œ fâ‚‚ zâ‚€)
    (h : hfâ‚.order â‰  hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = min hfâ‚.order hfâ‚‚.order := by
  rcases min_cases hfâ‚.order hfâ‚‚.order with (âŸ¨hm, hâ‚âŸ© | âŸ¨hm, hâ‚âŸ©)
  Â· simpa [hm] using hfâ‚.order_add_of_order_lt_order hfâ‚‚ (hâ‚.lt_of_ne h)
  Â· simpa [hm, add_comm] using hfâ‚‚.order_add_of_order_lt_order hfâ‚ hâ‚

end AnalyticAt

/-!
## Level Sets of the Order Function
-/

namespace AnalyticOnNhd

variable {U : Set ğ•œ} (hf : AnalyticOnNhd ğ•œ f U)

/-- The set where an analytic function has infinite order is clopen in its domain of analyticity. -/
theorem isClopen_setOf_order_eq_top :
    IsClopen { u : U | (hf u.1 u.2).order = âŠ¤ } := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† (hf z.1 z.2).order_eq_top_iff] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [(hf _ w.2).order_eq_zero_iff.2 ((hâ‚t' w hw) (Subtype.coe_ne_coe.mpr hâ‚w))]
          exact ENat.zero_ne_top
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [AnalyticAt.order_eq_top_iff, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [mem_compl_iff, mem_singleton_iff, isOpen_induced hâ‚‚t', mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y â†¦ hâ‚t' y hâ‚y.1, hâ‚‚t'.sdiff isClosed_singleton
    apply (mem_diff w).1
    exact âŸ¨hw, mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_order_ne_top_iff_forall (hU : IsConnected U) :
    (âˆƒ u : U, (hf u u.2).order â‰  âŠ¤) â†” (âˆ€ u : U, (hf u u.2).order â‰  âŠ¤) := by
  have : ConnectedSpace U := Subtype.connectedSpace hU
  obtain âŸ¨vâŸ© : Nonempty U := inferInstance
  suffices (âˆ€ (u : U), (hf u u.2).order â‰  âŠ¤) âˆ¨ âˆ€ (u : U), (hf u u.2).order = âŠ¤ by tauto
  simpa [Set.eq_empty_iff_forall_not_mem, Set.eq_univ_iff_forall] using
      isClopen_iff.1 hf.isClopen_setOf_order_eq_top

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem order_ne_top_of_isPreconnected {x y : ğ•œ} (hU : IsPreconnected U) (hâ‚x : x âˆˆ U) (hy : y âˆˆ U)
    (hâ‚‚x : (hf x hâ‚x).order â‰  âŠ¤) :
    (hf y hy).order â‰  âŠ¤ :=
  (hf.exists_order_ne_top_iff_forall âŸ¨nonempty_of_mem hâ‚x, hUâŸ©).1 (by use âŸ¨x, hâ‚xâŸ©) âŸ¨y, hyâŸ©

/-- The set where an analytic function has zero or infinite order is discrete within its domain of
analyticity. -/
theorem codiscrete_setOf_order_eq_zero_or_top :
    {u : U | (hf u u.2).order = 0 âˆ¨ (hf u u.2).order = âŠ¤} âˆˆ Filter.codiscrete U := by
  rw [mem_codiscrete_subtype_iff_mem_codiscreteWithin, mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right]
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚f.eventually_nhds] with a ha
    simp +contextual [(hf a _).order_eq_top_iff, ha]
  Â· filter_upwards [hâ‚f] with a ha
    simp +contextual [(hf a _).order_eq_zero_iff, ha]

end AnalyticOnNhd
