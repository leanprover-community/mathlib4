/-
Copyright (c) 2022 Vincent Beffara. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vincent Beffara, Stefan Kebekus
-/
import Mathlib.Analysis.Analytic.IsolatedZeros

/-!
# Vanishing Order of Analytic Functions

This file defines the order of vanishing of an analytic function `f` at a point `zâ‚€`, as an element
of `â„•âˆ`.

## TODO

Uniformize API between analytic and meromorphic functions
-/

open Filter  Set
open scoped Topology

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-!
## Vanishing Order at a Point: Definition and Characterization
-/

section NormedSpace
variable {f g : ğ•œ â†’ E} {n : â„•} {zâ‚€ : ğ•œ}

open scoped Classical in

/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•âˆ`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `AnalyticAt.eanalyticOrderAt_eq_top` and
`AnalyticAt.eanalyticOrderAt_eq_natCast` for these equivalences. -/
noncomputable def eanalyticOrderAt (f : ğ•œ â†’ E) (zâ‚€ : ğ•œ) : â„•âˆ :=
  if hf : AnalyticAt ğ•œ f zâ‚€ then
    if h : âˆ€á¶  z in ğ“ zâ‚€, f z = 0 then âŠ¤
    else â†‘(hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose
  else 0

@[deprecated (since := "2025-05-02")] alias AnalyticAt.order := eanalyticOrderAt

/-- The order of vanishing of `f` at `zâ‚€`, as an element of `â„•âˆ`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `AnalyticAt.eanalyticOrderAt_eq_top` and
`AnalyticAt.eanalyticOrderAt_eq_natCast` for these equivalences. -/
noncomputable def analyticOrderAt (f : ğ•œ â†’ E) (zâ‚€ : ğ•œ) : â„• := (eanalyticOrderAt f zâ‚€).toNat

@[simp]
lemma eanalyticOrderAt_of_not_analyticAt (hf : Â¬ AnalyticAt ğ•œ f zâ‚€) : eanalyticOrderAt f zâ‚€ = 0 :=
  dif_neg hf

@[simp]
lemma analyticOrderAt_of_not_analyticAt (hf : Â¬ AnalyticAt ğ•œ f zâ‚€) : analyticOrderAt f zâ‚€ = 0 := by
  simp [analyticOrderAt, hf]

@[simp] lemma Nat.cast_analyticOrderAt (hf : eanalyticOrderAt f zâ‚€ â‰  âŠ¤) :
    analyticOrderAt f zâ‚€ = eanalyticOrderAt f zâ‚€ := ENat.coe_toNat hf

lemma eanalyticOrderAt_eq_top :
    eanalyticOrderAt f zâ‚€ = âŠ¤ â†” AnalyticAt ğ•œ f zâ‚€ âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by
  unfold eanalyticOrderAt; split_ifs with hf h <;> simp [*]

/-- The order of an analytic function `f` at a `zâ‚€` is infinity iff `f` vanishes locally around
`zâ‚€`. -/
protected lemma AnalyticAt.eanalyticOrderAt_eq_top (hf : AnalyticAt ğ•œ f zâ‚€) :
    eanalyticOrderAt f zâ‚€ = âŠ¤ â†” âˆ€á¶  z in ğ“ zâ‚€, f z = 0 := by simp [eanalyticOrderAt_eq_top, hf]

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_eq_top_iff := AnalyticAt.eanalyticOrderAt_eq_top

/-- The order of an analytic function `f` at `zâ‚€` equals a natural number `n` iff `f` can locally
be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma AnalyticAt.eanalyticOrderAt_eq_natCast (hf : AnalyticAt ğ•œ f zâ‚€) :
    eanalyticOrderAt f zâ‚€ = n â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold eanalyticOrderAt
  split_ifs with h
  Â· simp only [ENat.top_ne_coe, false_iff]
    contrapose! h
    rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff]
    exact âŸ¨n, hâŸ©
  Â· rw [â† hf.exists_eventuallyEq_pow_smul_nonzero_iff] at h
    refine âŸ¨fun hn â†¦ (WithTop.coe_inj.mp hn : h.choose = n) â–¸ h.choose_spec, fun h' â†¦ ?_âŸ©
    rw [AnalyticAt.unique_eventuallyEq_pow_smul_nonzero h.choose_spec h']

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_eq_nat_iff := AnalyticAt.eanalyticOrderAt_eq_natCast

/-- The order of an analytic function `f` at `zâ‚€` equals a natural number `n` iff `f` can locally
be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma AnalyticAt.analyticOrderAt_eq_iff (hf : AnalyticAt ğ•œ f zâ‚€) (hf' : eanalyticOrderAt f zâ‚€ â‰  âŠ¤)
    {n : â„•} :
    analyticOrderAt f zâ‚€ = n â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  simp [â† Nat.cast_inj (R := â„•âˆ), Nat.cast_analyticOrderAt hf', hf.eanalyticOrderAt_eq_natCast]

/-- The order of an analytic function `f` at `zâ‚€` is finite iff `f` can locally be written as `f z =
  (z - zâ‚€) ^ analyticOrderAt f zâ‚€ â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`.

See `MeromorphicNFAt.order_eq_zero_iff` for an analogous statement about meromorphic functions in
normal form.
-/
lemma AnalyticAt.eanalyticOrderAt_ne_top (hf : AnalyticAt ğ•œ f zâ‚€) :
    eanalyticOrderAt f zâ‚€ â‰  âŠ¤ â†”
      âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
        f =á¶ [ğ“ zâ‚€] fun z â†¦ (z - zâ‚€) ^ analyticOrderAt f zâ‚€ â€¢ g z := by
  simp only [â† ENat.coe_toNat_eq_self, Eq.comm, EventuallyEq, â† hf.eanalyticOrderAt_eq_natCast,
    analyticOrderAt]

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_ne_top_iff := AnalyticAt.eanalyticOrderAt_ne_top

@[deprecated (since := "2025-02-03")]
alias AnalyticAt.order_neq_top_iff := AnalyticAt.eanalyticOrderAt_ne_top

lemma eanalyticOrderAt_eq_zero : eanalyticOrderAt f zâ‚€ = 0 â†” Â¬ AnalyticAt ğ•œ f zâ‚€ âˆ¨ f zâ‚€ â‰  0 := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· rw [â† ENat.coe_zero, hf.eanalyticOrderAt_eq_natCast]
    constructor
    Â· intro âŸ¨g, _, _, hgâŸ©
      simpa [hf, hg.self_of_nhds]
    Â· exact fun hz â†¦ âŸ¨f, hf, hz.resolve_left <| not_not_intro hf, by simpâŸ©
  Â· simp [hf]

lemma eanalyticOrderAt_ne_zero : eanalyticOrderAt f zâ‚€ â‰  0 â†” AnalyticAt ğ•œ f zâ‚€ âˆ§ f zâ‚€ = 0 := by
  simp [eanalyticOrderAt_eq_zero]

/-- The order of an analytic function `f` at `zâ‚€` is zero iff `f` does not vanish at `zâ‚€`. -/
protected lemma AnalyticAt.eanalyticOrderAt_eq_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    eanalyticOrderAt f zâ‚€ = 0 â†” f zâ‚€ â‰  0 := by simp [hf, eanalyticOrderAt_eq_zero]

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_eq_zero_iff := AnalyticAt.eanalyticOrderAt_eq_zero

/-- The order of an analytic function `f` at `zâ‚€` is zero iff `f` does not vanish at `zâ‚€`. -/
protected lemma AnalyticAt.eanalyticOrderAt_ne_zero (hf : AnalyticAt ğ•œ f zâ‚€) :
    eanalyticOrderAt f zâ‚€ â‰  0 â†” f zâ‚€ = 0 := hf.eanalyticOrderAt_eq_zero.not_left

/-- An analytic function vanishes at a point if its order is nonzero when converted to â„•. -/
lemma apply_eq_zero_of_analyticOrderAt_ne_zero (hf : analyticOrderAt f zâ‚€ â‰  0) : f zâ‚€ = 0 := by
  by_cases hf' : AnalyticAt ğ•œ f zâ‚€ <;> simp_all [analyticOrderAt, eanalyticOrderAt_eq_zero]

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.apply_eq_zero_of_order_toNat_ne_zero := apply_eq_zero_of_analyticOrderAt_ne_zero

/-- Characterization of which natural numbers are `â‰¤ hf.order`. Useful for avoiding case splits,
since it applies whether or not the order is `âˆ`. -/
lemma natCast_le_eanalyticOrderAt (hf : AnalyticAt ğ•œ f zâ‚€) {n : â„•} :
    n â‰¤ eanalyticOrderAt f zâ‚€ â†”
      âˆƒ g, AnalyticAt ğ•œ g zâ‚€ âˆ§ âˆ€á¶  z in ğ“ zâ‚€, f z = (z - zâ‚€) ^ n â€¢ g z := by
  unfold eanalyticOrderAt
  split_ifs with h
  Â· simpa using âŸ¨0, analyticAt_const .., by simpaâŸ©
  Â· let m := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose
    obtain âŸ¨g, hg, hg_ne, hmâŸ© := (hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h).choose_spec
    rw [ENat.coe_le_coe]
    refine âŸ¨fun hmn â†¦ âŸ¨fun z â†¦ (z - zâ‚€) ^ (m - n) â€¢ g z, by fun_prop, ?_âŸ©, fun âŸ¨h, hh, hfhâŸ© â†¦ ?_âŸ©
    Â· filter_upwards [hm] with z hz using by rwa [â† mul_smul, â† pow_add, Nat.add_sub_of_le hmn]
    Â· contrapose! hg_ne
      have : ContinuousAt (fun z â†¦ (z - zâ‚€) ^ (n - m) â€¢ h z) zâ‚€ := by fun_prop
      rw [tendsto_nhds_unique_of_eventuallyEq (l := ğ“[â‰ ] zâ‚€)
        hg.continuousAt.continuousWithinAt this.continuousWithinAt ?_]
      Â· simp [m, Nat.sub_ne_zero_of_lt hg_ne]
      Â· filter_upwards [self_mem_nhdsWithin, hm.filter_mono nhdsWithin_le_nhds,
          hfh.filter_mono nhdsWithin_le_nhds] with z hz hf' hf''
        rw [â† inv_smul_eq_iffâ‚€ (pow_ne_zero _ <| sub_ne_zero_of_ne hz), hf'', smul_comm,
          â† mul_smul] at hf'
        rw [pow_subâ‚€ _ (sub_ne_zero_of_ne hz) (by omega), â† hf']

@[deprecated (since := "2025-05-03")] alias natCast_le_order_iff := natCast_le_eanalyticOrderAt

/-- If two functions agree in a neighborhood of `zâ‚€`, then their orders at `zâ‚€` agree. -/
lemma eanalyticOrderAt_congr (hfg : f =á¶ [ğ“ zâ‚€] g) :
    eanalyticOrderAt f zâ‚€ = eanalyticOrderAt g zâ‚€ := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· refine ENat.eq_of_forall_natCast_le_iff fun n â†¦ ?_
    simp only [natCast_le_eanalyticOrderAt, hf, hf.congr hfg]
    congr! 3
    exact hfg.congr_left
  Â· rw [eanalyticOrderAt_of_not_analyticAt hf,
      eanalyticOrderAt_of_not_analyticAt fun hg â†¦ hf <| hg.congr hfg.symm]

@[deprecated (since := "2025-05-03")] alias AnalyticAt.order_congr := eanalyticOrderAt_congr

@[simp] lemma eanalyticOrderAt_neg : eanalyticOrderAt (-f) zâ‚€ = eanalyticOrderAt f zâ‚€ := by
  by_cases hf : AnalyticAt ğ•œ f zâ‚€
  Â· refine ENat.eq_of_forall_natCast_le_iff fun n â†¦ ?_
    simp only [ natCast_le_eanalyticOrderAt, hf, hf.neg]
    exact (Equiv.neg _).exists_congr <| by simp [neg_eq_iff_eq_neg]
  Â· rw [eanalyticOrderAt_of_not_analyticAt hf,
      eanalyticOrderAt_of_not_analyticAt <| analyticAt_neg.not.2 hf]

/-- The order of a sum is at least the minimum of the orders of the summands. -/
theorem le_eanalyticOrderAt_add (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    min (eanalyticOrderAt f zâ‚€) (eanalyticOrderAt g zâ‚€) â‰¤ eanalyticOrderAt (f + g) zâ‚€ := by
  refine ENat.forall_natCast_le_iff_le.mp fun n â†¦ ?_
  simp only [le_min_iff, natCast_le_eanalyticOrderAt, hf, hg, hf.add hg]
  refine fun âŸ¨âŸ¨F, hF, hF'âŸ©, âŸ¨G, hG, hG'âŸ©âŸ© â†¦ âŸ¨F + G, hF.add hG, ?_âŸ©
  filter_upwards [hF', hG'] with z using by simp +contextual [mul_add]

@[deprecated (since := "2025-05-03")] alias AnalyticAt.order_add := le_eanalyticOrderAt_add

lemma le_eanalyticOrderAt_sub (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    min (eanalyticOrderAt f zâ‚€) (eanalyticOrderAt g zâ‚€) â‰¤ eanalyticOrderAt (f - g) zâ‚€ := by
  simpa [sub_eq_add_neg] using le_eanalyticOrderAt_add hf hg.neg

lemma eanalyticOrderAt_add_eq_left_of_lt (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€)
    (hfg : eanalyticOrderAt f zâ‚€ < eanalyticOrderAt g zâ‚€) :
    eanalyticOrderAt (f + g) zâ‚€ = eanalyticOrderAt f zâ‚€ :=
  le_antisymm (by simpa [hfg.not_le] using le_eanalyticOrderAt_sub (hf.add hg) hg)
    (by simpa [hfg.le] using le_eanalyticOrderAt_add hf hg)

lemma eanalyticOrderAt_add_eq_right_of_lt (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€)
    (hgf : eanalyticOrderAt g zâ‚€ < eanalyticOrderAt f zâ‚€) :
    eanalyticOrderAt (f + g) zâ‚€ = eanalyticOrderAt g zâ‚€ := by
  rw [add_comm, eanalyticOrderAt_add_eq_left_of_lt hg hf hgf]

@[deprecated (since := "2025-05-03")] alias order_add_of_order_lt_order := le_eanalyticOrderAt_add

/-- If two functions have unequal orders, then the order of their sum is exactly the minimum
of the orders of the summands. -/
theorem eanalyticOrderAt_add_of_ne (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€)
    (hfg : eanalyticOrderAt f zâ‚€ â‰  eanalyticOrderAt g zâ‚€) :
    eanalyticOrderAt (f + g) zâ‚€ = min (eanalyticOrderAt f zâ‚€) (eanalyticOrderAt g zâ‚€) := by
  obtain hfg | hgf := hfg.lt_or_lt
  Â· simpa [hfg.le] using eanalyticOrderAt_add_eq_left_of_lt hf hg hfg
  Â· simpa [hgf.le] using eanalyticOrderAt_add_eq_right_of_lt hf hg hgf

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_add_of_order_ne_order := eanalyticOrderAt_add_of_ne

lemma eanalyticOrderAt_smul_eq_top_of_left {f : ğ•œ â†’ ğ•œ} (hg : AnalyticAt ğ•œ g zâ‚€)
    (hf : eanalyticOrderAt f zâ‚€ = âŠ¤) : eanalyticOrderAt (f â€¢ g) zâ‚€ = âŠ¤ := by
  rw [eanalyticOrderAt_eq_top, eventually_nhds_iff] at *
  obtain âŸ¨hf, t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hf
  exact âŸ¨hf.smul hg, t, fun y hy â†¦ by simp [hâ‚t y hy], hâ‚‚t, hâ‚ƒtâŸ©

lemma eanalyticOrderAt_smul_eq_top_of_right {f : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€)
    (hg : eanalyticOrderAt g zâ‚€ = âŠ¤) : eanalyticOrderAt (f â€¢ g) zâ‚€ = âŠ¤ := by
  rw [eanalyticOrderAt_eq_top, eventually_nhds_iff] at *
  obtain âŸ¨hg, t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hg
  exact âŸ¨hf.smul hg, t, fun y hy â†¦ by simp [hâ‚t y hy], hâ‚‚t, hâ‚ƒtâŸ©

/-- The order is additive when scalar multiplying analytic functions. -/
lemma eanalyticOrderAt_smul {f : ğ•œ â†’ ğ•œ} (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    eanalyticOrderAt (f â€¢ g) zâ‚€ = eanalyticOrderAt f zâ‚€ + eanalyticOrderAt g zâ‚€ := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  by_cases hf' : eanalyticOrderAt f zâ‚€ = âŠ¤
  Â· simp [eanalyticOrderAt_smul_eq_top_of_left, *]
  by_cases hg' : eanalyticOrderAt g zâ‚€ = âŠ¤
  Â· simp [eanalyticOrderAt_smul_eq_top_of_right, *]
  -- Non-trivial case: both functions do not vanish around zâ‚€
  obtain âŸ¨f', hâ‚f', hâ‚‚f', hâ‚ƒf'âŸ© := hf.eanalyticOrderAt_ne_top.1 hf'
  obtain âŸ¨g', hâ‚g', hâ‚‚g', hâ‚ƒg'âŸ© := hg.eanalyticOrderAt_ne_top.1 hg'
  rw [â† Nat.cast_analyticOrderAt hf', â† Nat.cast_analyticOrderAt hg', â† ENat.coe_add,
      (hf.smul hg).eanalyticOrderAt_eq_natCast]
  refine âŸ¨f' â€¢ g', hâ‚f'.smul hâ‚g', ?_, ?_âŸ©
  Â· simp
    tauto
  Â· obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := eventually_nhds_iff.1 hâ‚ƒf'
    obtain âŸ¨s, hâ‚s, hâ‚‚s, hâ‚ƒsâŸ© := eventually_nhds_iff.1 hâ‚ƒg'
    exact eventually_nhds_iff.2
      âŸ¨t âˆ© s, fun y hy â†¦ (by simp [hâ‚t y hy.1, hâ‚s y hy.2]; module), hâ‚‚t.inter hâ‚‚s, hâ‚ƒt, hâ‚ƒsâŸ©

end NormedSpace

/-!
## Vanishing Order at a Point: Elementary Computations
-/

/-- Simplifier lemma for the order of a centered monomial -/
@[simp]
lemma eanalyticOrderAt_centeredMonomial {zâ‚€ : ğ•œ} {n : â„•} :
    eanalyticOrderAt ((Â· - zâ‚€) ^ n) zâ‚€ = n := by
  rw [AnalyticAt.eanalyticOrderAt_eq_natCast (by fun_prop)]
  exact âŸ¨1, by simp [Pi.one_def, analyticAt_const]âŸ©

@[deprecated (since := "2025-05-03")]
alias analyticAt_order_centeredMonomial := eanalyticOrderAt_centeredMonomial

section NontriviallyNormedField
variable {f g : ğ•œ â†’ ğ•œ} {zâ‚€ : ğ•œ}

/-- Helper lemma for `eanalyticOrderAt_mul` -/
lemma eanalyticOrderAt_mul_eq_top_of_left (hg : AnalyticAt ğ•œ g zâ‚€)
    (hf : eanalyticOrderAt f zâ‚€ = âŠ¤) : eanalyticOrderAt (f * g) zâ‚€ = âŠ¤ :=
  eanalyticOrderAt_smul_eq_top_of_left hg hf

@[deprecated (since := "2025-05-03")]
alias AnalyticAt.order_mul_of_order_eq_top := eanalyticOrderAt_mul_eq_top_of_left

/-- Helper lemma for `eanalyticOrderAt_mul` -/
lemma eanalyticOrderAt_mul_eq_top_of_right (hf : AnalyticAt ğ•œ f zâ‚€)
    (hg : eanalyticOrderAt g zâ‚€ = âŠ¤) : eanalyticOrderAt (f * g) zâ‚€ = âŠ¤ :=
  eanalyticOrderAt_smul_eq_top_of_right hf hg

/-- The order is additive when multiplying analytic functions. -/
theorem eanalyticOrderAt_mul (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) :
    eanalyticOrderAt (f * g) zâ‚€ = eanalyticOrderAt f zâ‚€ + eanalyticOrderAt g zâ‚€ :=
  eanalyticOrderAt_smul hf hg

@[deprecated (since := "2025-05-03")] alias AnalyticAt.order_mul := eanalyticOrderAt_mul

/-- The order is additive when multiplying analytic functions. -/
theorem analyticOrderAt_mul (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€)
    (hf' : eanalyticOrderAt f zâ‚€ â‰  âŠ¤) (hg' : eanalyticOrderAt g zâ‚€ â‰  âŠ¤) :
    analyticOrderAt (f * g) zâ‚€ = analyticOrderAt f zâ‚€ + analyticOrderAt g zâ‚€ := by
  simp [analyticOrderAt, eanalyticOrderAt_mul, ENat.toNat_add, *]

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem eanalyticOrderAt_pow (hf : AnalyticAt ğ•œ f zâ‚€) :
    âˆ€ n, eanalyticOrderAt (f ^ n) zâ‚€ = n â€¢ eanalyticOrderAt f zâ‚€
  | 0 => by simp [eanalyticOrderAt_eq_zero]
  | n + 1 => by simp [add_mul, pow_add, eanalyticOrderAt_mul (hf.pow n), eanalyticOrderAt_pow, hf]

@[deprecated (since := "2025-05-03")] alias AnalyticAt.order_pow := eanalyticOrderAt_pow

/-- The order multiplies by `n` when taking an analytic function to its `n`th power. -/
theorem analyticOrderAt_pow (hf : AnalyticAt ğ•œ f zâ‚€) (n : â„•) :
    analyticOrderAt (f ^ n) zâ‚€ = n â€¢ analyticOrderAt f zâ‚€ := by
  simp [analyticOrderAt, eanalyticOrderAt_pow, hf]


end NontriviallyNormedField

/-!
## Level Sets of the Order Function
-/

namespace AnalyticOnNhd

variable {U : Set ğ•œ} {f : ğ•œ â†’ E} (hf : AnalyticOnNhd ğ•œ f U)
include hf

/-- The set where an analytic function has infinite order is clopen in its domain of analyticity. -/
theorem isClopen_setOf_eanalyticOrderAt_eq_top : IsClopen {u : U | eanalyticOrderAt f u = âŠ¤} := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† (hf z.1 z.2).eanalyticOrderAt_eq_top] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [(hf _ w.2).eanalyticOrderAt_eq_zero.2 ((hâ‚t' w hw) (Subtype.coe_ne_coe.mpr hâ‚w))]
          exact ENat.zero_ne_top
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [(hf _ <| Subtype.prop _).eanalyticOrderAt_eq_top, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [(hf _ <| Subtype.prop _).eanalyticOrderAt_eq_top, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [mem_compl_iff, mem_singleton_iff, isOpen_induced hâ‚‚t', mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y â†¦ hâ‚t' y hâ‚y.1, hâ‚‚t'.sdiff isClosed_singleton
    apply (mem_diff w).1
    exact âŸ¨hw, mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_eanalyticOrderAt_ne_top_iff_forall (hU : IsConnected U) :
    (âˆƒ u : U, eanalyticOrderAt f u â‰  âŠ¤) â†” âˆ€ u : U, eanalyticOrderAt f u â‰  âŠ¤ := by
  have : ConnectedSpace U := Subtype.connectedSpace hU
  obtain âŸ¨vâŸ© : Nonempty U := inferInstance
  suffices (âˆ€ (u : U), eanalyticOrderAt f u â‰  âŠ¤) âˆ¨ âˆ€ (u : U), eanalyticOrderAt f u = âŠ¤ by tauto
  simpa [Set.eq_empty_iff_forall_not_mem, Set.eq_univ_iff_forall] using
      isClopen_iff.1 hf.isClopen_setOf_eanalyticOrderAt_eq_top

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem eanalyticOrderAt_ne_top_of_isPreconnected {x y : ğ•œ} (hU : IsPreconnected U) (hâ‚x : x âˆˆ U)
    (hy : y âˆˆ U) (hâ‚‚x : eanalyticOrderAt f x â‰  âŠ¤) : eanalyticOrderAt f y â‰  âŠ¤ :=
  (hf.exists_eanalyticOrderAt_ne_top_iff_forall âŸ¨nonempty_of_mem hâ‚x, hUâŸ©).1 (by use âŸ¨x, hâ‚xâŸ©)
    âŸ¨y, hyâŸ©

/-- The set where an analytic function has zero or infinite order is discrete within its domain of
analyticity. -/
theorem codiscrete_setOf_eanalyticOrderAt_eq_zero_or_top :
    {u : U | eanalyticOrderAt f u = 0 âˆ¨ eanalyticOrderAt f u = âŠ¤} âˆˆ Filter.codiscrete U := by
  rw [mem_codiscrete_subtype_iff_mem_codiscreteWithin, mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right]
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚f.eventually_nhds] with a ha
    simp +contextual [(hf a _).eanalyticOrderAt_eq_top, ha]
  Â· filter_upwards [hâ‚f] with a ha
    simp +contextual [(hf a _).eanalyticOrderAt_eq_zero, ha]

end AnalyticOnNhd
