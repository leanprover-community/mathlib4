/-
Copyright (c) 2020 S√©bastien Gou√´zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: S√©bastien Gou√´zel, Yury Kudryashov
-/
import Mathlib.Analysis.Calculus.FormalMultilinearSeries
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Logic.Equiv.Fin
import Mathlib.Topology.Algebra.InfiniteSum.Module

#align_import analysis.analytic.basic from "leanprover-community/mathlib"@"32253a1a1071173b33dc7d6a218cf722c6feb514"

/-!
# Analytic functions

A function is analytic in one dimension around `0` if it can be written as a converging power series
`Œ£ p‚Çô z‚Åø`. This definition can be extended to any dimension (even in infinite dimension) by
requiring that `p‚Çô` is a continuous `n`-multilinear map. In general, `p‚Çô` is not unique (in two
dimensions, taking `p‚ÇÇ (x, y) (x', y') = x y'` or `y x'` gives the same map when applied to a
vector `(x, y) (x, y)`). A way to guarantee uniqueness is to take a symmetric `p‚Çô`, but this is not
always possible in nonzero characteristic (in characteristic 2, the previous example has no
symmetric representative). Therefore, we do not insist on symmetry or uniqueness in the definition,
and we only require the existence of a converging series.

The general framework is important to say that the exponential map on bounded operators on a Banach
space is analytic, as well as the inverse on invertible operators.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : ‚Ñï`.

* `p.radius`: the largest `r : ‚Ñù‚â•0‚àû` such that `‚Äñp n‚Äñ * r^n` grows subexponentially.
* `p.le_radius_of_bound`, `p.le_radius_of_bound_nnreal`, `p.le_radius_of_isBigO`: if `‚Äñp n‚Äñ * r ^ n`
  is bounded above, then `r ‚â§ p.radius`;
* `p.isLittleO_of_lt_radius`, `p.norm_mul_pow_le_mul_pow_of_lt_radius`,
  `p.isLittleO_one_of_lt_radius`,
  `p.norm_mul_pow_le_of_lt_radius`, `p.nnnorm_mul_pow_le_of_lt_radius`: if `r < p.radius`, then
  `‚Äñp n‚Äñ * r ^ n` tends to zero exponentially;
* `p.lt_radius_of_isBigO`: if `r ‚â† 0` and `‚Äñp n‚Äñ * r ^ n = O(a ^ n)` for some `-1 < a < 1`, then
  `r < p.radius`;
* `p.partialSum n x`: the sum `‚àë_{i = 0}^{n-1} p·µ¢ x‚Å±`.
* `p.sum x`: the sum `‚àë'_{i = 0}^{‚àû} p·µ¢ x‚Å±`.

Additionally, let `f` be a function from `E` to `F`.

* `HasFPowerSeriesOnBall f p x r`: on the ball of center `x` with radius `r`,
  `f (x + y) = ‚àë'_n p‚Çô y‚Åø`.
* `HasFPowerSeriesAt f p x`: on some ball of center `x` with positive radius, holds
  `HasFPowerSeriesOnBall f p x r`.
* `AnalyticAt ùïú f x`: there exists a power series `p` such that holds `HasFPowerSeriesAt f p x`.
* `AnalyticOn ùïú f s`: the function `f` is analytic at every point of `s`.

We develop the basic properties of these notions, notably:
* If a function admits a power series, it is continuous (see
  `HasFPowerSeriesOnBall.continuousOn` and `HasFPowerSeriesAt.continuousAt` and
  `AnalyticAt.continuousAt`).
* In a complete space, the sum of a formal power series with positive radius is well defined on the
  disk of convergence, see `FormalMultilinearSeries.hasFPowerSeriesOnBall`.
* If a function admits a power series in a ball, then it is analytic at any point `y` of this ball,
  and the power series there can be expressed in terms of the initial power series `p` as
  `p.changeOrigin y`. See `HasFPowerSeriesOnBall.changeOrigin`. It follows in particular that
  the set of points at which a given function is analytic is open, see `isOpen_analyticAt`.

## Implementation details

We only introduce the radius of convergence of a power series, as `p.radius`.
For a power series in finitely many dimensions, there is a finer (directional, coordinate-dependent)
notion, describing the polydisk of convergence. This notion is more specific, and not necessary to
build the general theory. We do not define it here.
-/

noncomputable section

variable {ùïú E F G : Type*}

open scoped Classical
open Topology NNReal Filter ENNReal

open Set Filter Asymptotics

namespace FormalMultilinearSeries

variable [Ring ùïú] [AddCommGroup E] [AddCommGroup F] [Module ùïú E] [Module ùïú F]
variable [TopologicalSpace E] [TopologicalSpace F]
variable [TopologicalAddGroup E] [TopologicalAddGroup F]
variable [ContinuousConstSMul ùïú E] [ContinuousConstSMul ùïú F]

/-- Given a formal multilinear series `p` and a vector `x`, then `p.sum x` is the sum `Œ£ p‚Çô x‚Åø`. A
priori, it only behaves well when `‚Äñx‚Äñ < p.radius`. -/
protected def sum (p : FormalMultilinearSeries ùïú E F) (x : E) : F :=
  ‚àë' n : ‚Ñï, p n fun _ => x
#align formal_multilinear_series.sum FormalMultilinearSeries.sum

/-- Given a formal multilinear series `p` and a vector `x`, then `p.partialSum n x` is the sum
`Œ£ p‚Çñ x·µè` for `k ‚àà {0,..., n-1}`. -/
def partialSum (p : FormalMultilinearSeries ùïú E F) (n : ‚Ñï) (x : E) : F :=
  ‚àë k ‚àà Finset.range n, p k fun _ : Fin k => x
#align formal_multilinear_series.partial_sum FormalMultilinearSeries.partialSum

/-- The partial sums of a formal multilinear series are continuous. -/
theorem partialSum_continuous (p : FormalMultilinearSeries ùïú E F) (n : ‚Ñï) :
    Continuous (p.partialSum n) := by
  unfold partialSum -- Porting note: added
  continuity
#align formal_multilinear_series.partial_sum_continuous FormalMultilinearSeries.partialSum_continuous

end FormalMultilinearSeries

/-! ### The radius of a formal multilinear series -/

variable [NontriviallyNormedField ùïú] [NormedAddCommGroup E] [NormedSpace ùïú E] [NormedAddCommGroup F]
  [NormedSpace ùïú F] [NormedAddCommGroup G] [NormedSpace ùïú G]

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0}

/-- The radius of a formal multilinear series is the largest `r` such that the sum `Œ£ ‚Äñp‚Çô‚Äñ ‚Äñy‚Äñ‚Åø`
converges for all `‚Äñy‚Äñ < r`. This implies that `Œ£ p‚Çô y‚Åø` converges for all `‚Äñy‚Äñ < r`, but these
definitions are *not* equivalent in general. -/
def radius (p : FormalMultilinearSeries ùïú E F) : ‚Ñù‚â•0‚àû :=
  ‚®Ü (r : ‚Ñù‚â•0) (C : ‚Ñù) (_ : ‚àÄ n, ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n ‚â§ C), (r : ‚Ñù‚â•0‚àû)
#align formal_multilinear_series.radius FormalMultilinearSeries.radius

/-- If `‚Äñp‚Çô‚Äñ r‚Åø` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound (C : ‚Ñù) {r : ‚Ñù‚â•0} (h : ‚àÄ n : ‚Ñï, ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n ‚â§ C) :
    (r : ‚Ñù‚â•0‚àû) ‚â§ p.radius :=
  le_iSup_of_le r <| le_iSup_of_le C <| le_iSup (fun _ => (r : ‚Ñù‚â•0‚àû)) h
#align formal_multilinear_series.le_radius_of_bound FormalMultilinearSeries.le_radius_of_bound

/-- If `‚Äñp‚Çô‚Äñ r‚Åø` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound_nnreal (C : ‚Ñù‚â•0) {r : ‚Ñù‚â•0} (h : ‚àÄ n : ‚Ñï, ‚Äñp n‚Äñ‚Çä * r ^ n ‚â§ C) :
    (r : ‚Ñù‚â•0‚àû) ‚â§ p.radius :=
  p.le_radius_of_bound C fun n => mod_cast h n
#align formal_multilinear_series.le_radius_of_bound_nnreal FormalMultilinearSeries.le_radius_of_bound_nnreal

/-- If `‚Äñp‚Çô‚Äñ r‚Åø = O(1)`, as `n ‚Üí ‚àû`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_isBigO (h : (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) =O[atTop] fun _ => (1 : ‚Ñù)) :
    ‚Üër ‚â§ p.radius :=
  Exists.elim (isBigO_one_nat_atTop_iff.1 h) fun C hC =>
    p.le_radius_of_bound C fun n => (le_abs_self _).trans (hC n)
set_option linter.uppercaseLean3 false in
#align formal_multilinear_series.le_radius_of_is_O FormalMultilinearSeries.le_radius_of_isBigO

theorem le_radius_of_eventually_le (C) (h : ‚àÄ·∂† n in atTop, ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n ‚â§ C) :
    ‚Üër ‚â§ p.radius :=
  p.le_radius_of_isBigO <| IsBigO.of_bound C <| h.mono fun n hn => by simpa
#align formal_multilinear_series.le_radius_of_eventually_le FormalMultilinearSeries.le_radius_of_eventually_le

theorem le_radius_of_summable_nnnorm (h : Summable fun n => ‚Äñp n‚Äñ‚Çä * r ^ n) : ‚Üër ‚â§ p.radius :=
  p.le_radius_of_bound_nnreal (‚àë' n, ‚Äñp n‚Äñ‚Çä * r ^ n) fun _ => le_tsum' h _
#align formal_multilinear_series.le_radius_of_summable_nnnorm FormalMultilinearSeries.le_radius_of_summable_nnnorm

theorem le_radius_of_summable (h : Summable fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) : ‚Üër ‚â§ p.radius :=
  p.le_radius_of_summable_nnnorm <| by
    simp only [‚Üê coe_nnnorm] at h
    exact mod_cast h
#align formal_multilinear_series.le_radius_of_summable FormalMultilinearSeries.le_radius_of_summable

theorem radius_eq_top_of_forall_nnreal_isBigO
    (h : ‚àÄ r : ‚Ñù‚â•0, (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) =O[atTop] fun _ => (1 : ‚Ñù)) : p.radius = ‚àû :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_isBigO (h r)
set_option linter.uppercaseLean3 false in
#align formal_multilinear_series.radius_eq_top_of_forall_nnreal_is_O FormalMultilinearSeries.radius_eq_top_of_forall_nnreal_isBigO

theorem radius_eq_top_of_eventually_eq_zero (h : ‚àÄ·∂† n in atTop, p n = 0) : p.radius = ‚àû :=
  p.radius_eq_top_of_forall_nnreal_isBigO fun r =>
    (isBigO_zero _ _).congr' (h.mono fun n hn => by simp [hn]) EventuallyEq.rfl
#align formal_multilinear_series.radius_eq_top_of_eventually_eq_zero FormalMultilinearSeries.radius_eq_top_of_eventually_eq_zero

theorem radius_eq_top_of_forall_image_add_eq_zero (n : ‚Ñï) (hn : ‚àÄ m, p (m + n) = 0) :
    p.radius = ‚àû :=
  p.radius_eq_top_of_eventually_eq_zero <|
    mem_atTop_sets.2 ‚ü®n, fun _ hk => tsub_add_cancel_of_le hk ‚ñ∏ hn _‚ü©
#align formal_multilinear_series.radius_eq_top_of_forall_image_add_eq_zero FormalMultilinearSeries.radius_eq_top_of_forall_image_add_eq_zero

@[simp]
theorem constFormalMultilinearSeries_radius {v : F} :
    (constFormalMultilinearSeries ùïú E v).radius = ‚ä§ :=
  (constFormalMultilinearSeries ùïú E v).radius_eq_top_of_forall_image_add_eq_zero 1
    (by simp [constFormalMultilinearSeries])
#align formal_multilinear_series.const_formal_multilinear_series_radius FormalMultilinearSeries.constFormalMultilinearSeries_radius

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø` tends to zero exponentially:
for some `0 < a < 1`, `‚Äñp n‚Äñ r‚Åø = o(a‚Åø)`. -/
theorem isLittleO_of_lt_radius (h : ‚Üër < p.radius) :
    ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) =o[atTop] (a ^ ¬∑) := by
  have := (TFAE_exists_lt_isLittleO_pow (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) 1).out 1 4
  rw [this]
  -- Porting note: was
  -- rw [(TFAE_exists_lt_isLittleO_pow (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) 1).out 1 4]
  simp only [radius, lt_iSup_iff] at h
  rcases h with ‚ü®t, C, hC, rt‚ü©
  rw [ENNReal.coe_lt_coe, ‚Üê NNReal.coe_lt_coe] at rt
  have : 0 < (t : ‚Ñù) := r.coe_nonneg.trans_lt rt
  rw [‚Üê div_lt_one this] at rt
  refine ‚ü®_, rt, C, Or.inr zero_lt_one, fun n => ?_‚ü©
  calc
    |‚Äñp n‚Äñ * (r : ‚Ñù) ^ n| = ‚Äñp n‚Äñ * (t : ‚Ñù) ^ n * (r / t : ‚Ñù) ^ n := by
      field_simp [mul_right_comm, abs_mul]
    _ ‚â§ C * (r / t : ‚Ñù) ^ n := by gcongr; apply hC
#align formal_multilinear_series.is_o_of_lt_radius FormalMultilinearSeries.isLittleO_of_lt_radius

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø = o(1)`. -/
theorem isLittleO_one_of_lt_radius (h : ‚Üër < p.radius) :
    (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) =o[atTop] (fun _ => 1 : ‚Ñï ‚Üí ‚Ñù) :=
  let ‚ü®_, ha, hp‚ü© := p.isLittleO_of_lt_radius h
  hp.trans <| (isLittleO_pow_pow_of_lt_left ha.1.le ha.2).congr (fun _ => rfl) one_pow
#align formal_multilinear_series.is_o_one_of_lt_radius FormalMultilinearSeries.isLittleO_one_of_lt_radius

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø` tends to zero exponentially:
for some `0 < a < 1` and `C > 0`, `‚Äñp n‚Äñ * r ^ n ‚â§ C * a ^ n`. -/
theorem norm_mul_pow_le_mul_pow_of_lt_radius (h : ‚Üër < p.radius) :
    ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ n, ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n ‚â§ C * a ^ n := by
  -- Porting note: moved out of `rcases`
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) 1).out 1 5).mp
    (p.isLittleO_of_lt_radius h)
  rcases this with ‚ü®a, ha, C, hC, H‚ü©
  exact ‚ü®a, ha, C, hC, fun n => (le_abs_self _).trans (H n)‚ü©
#align formal_multilinear_series.norm_mul_pow_le_mul_pow_of_lt_radius FormalMultilinearSeries.norm_mul_pow_le_mul_pow_of_lt_radius

/-- If `r ‚â† 0` and `‚Äñp‚Çô‚Äñ r‚Åø = O(a‚Åø)` for some `-1 < a < 1`, then `r < p.radius`. -/
theorem lt_radius_of_isBigO (h‚ÇÄ : r ‚â† 0) {a : ‚Ñù} (ha : a ‚àà Ioo (-1 : ‚Ñù) 1)
    (hp : (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) =O[atTop] (a ^ ¬∑)) : ‚Üër < p.radius := by
  -- Porting note: moved out of `rcases`
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) 1).out 2 5)
  rcases this.mp ‚ü®a, ha, hp‚ü© with ‚ü®a, ha, C, hC, hp‚ü©
  rw [‚Üê pos_iff_ne_zero, ‚Üê NNReal.coe_pos] at h‚ÇÄ
  lift a to ‚Ñù‚â•0 using ha.1.le
  have : (r : ‚Ñù) < r / a := by
    simpa only [div_one] using (div_lt_div_left h‚ÇÄ zero_lt_one ha.1).2 ha.2
  norm_cast at this
  rw [‚Üê ENNReal.coe_lt_coe] at this
  refine this.trans_le (p.le_radius_of_bound C fun n => ?_)
  rw [NNReal.coe_div, div_pow, ‚Üê mul_div_assoc, div_le_iff (pow_pos ha.1 n)]
  exact (le_abs_self _).trans (hp n)
set_option linter.uppercaseLean3 false in
#align formal_multilinear_series.lt_radius_of_is_O FormalMultilinearSeries.lt_radius_of_isBigO

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø` is bounded. -/
theorem norm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0}
    (h : (r : ‚Ñù‚â•0‚àû) < p.radius) : ‚àÉ C > 0, ‚àÄ n, ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n ‚â§ C :=
  let ‚ü®_, ha, C, hC, h‚ü© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  ‚ü®C, hC, fun n => (h n).trans <| mul_le_of_le_one_right hC.lt.le (pow_le_one _ ha.1.le ha.2.le)‚ü©
#align formal_multilinear_series.norm_mul_pow_le_of_lt_radius FormalMultilinearSeries.norm_mul_pow_le_of_lt_radius

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø` is bounded. -/
theorem norm_le_div_pow_of_pos_of_lt_radius (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0}
    (h0 : 0 < r) (h : (r : ‚Ñù‚â•0‚àû) < p.radius) : ‚àÉ C > 0, ‚àÄ n, ‚Äñp n‚Äñ ‚â§ C / (r : ‚Ñù) ^ n :=
  let ‚ü®C, hC, hp‚ü© := p.norm_mul_pow_le_of_lt_radius h
  ‚ü®C, hC, fun n => Iff.mpr (le_div_iff (pow_pos h0 _)) (hp n)‚ü©
#align formal_multilinear_series.norm_le_div_pow_of_pos_of_lt_radius FormalMultilinearSeries.norm_le_div_pow_of_pos_of_lt_radius

/-- For `r` strictly smaller than the radius of `p`, then `‚Äñp‚Çô‚Äñ r‚Åø` is bounded. -/
theorem nnnorm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0}
    (h : (r : ‚Ñù‚â•0‚àû) < p.radius) : ‚àÉ C > 0, ‚àÄ n, ‚Äñp n‚Äñ‚Çä * r ^ n ‚â§ C :=
  let ‚ü®C, hC, hp‚ü© := p.norm_mul_pow_le_of_lt_radius h
  ‚ü®‚ü®C, hC.lt.le‚ü©, hC, mod_cast hp‚ü©
#align formal_multilinear_series.nnnorm_mul_pow_le_of_lt_radius FormalMultilinearSeries.nnnorm_mul_pow_le_of_lt_radius

theorem le_radius_of_tendsto (p : FormalMultilinearSeries ùïú E F) {l : ‚Ñù}
    (h : Tendsto (fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) atTop (ùìù l)) : ‚Üër ‚â§ p.radius :=
  p.le_radius_of_isBigO (h.isBigO_one _)
#align formal_multilinear_series.le_radius_of_tendsto FormalMultilinearSeries.le_radius_of_tendsto

theorem le_radius_of_summable_norm (p : FormalMultilinearSeries ùïú E F)
    (hs : Summable fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) : ‚Üër ‚â§ p.radius :=
  p.le_radius_of_tendsto hs.tendsto_atTop_zero
#align formal_multilinear_series.le_radius_of_summable_norm FormalMultilinearSeries.le_radius_of_summable_norm

theorem not_summable_norm_of_radius_lt_nnnorm (p : FormalMultilinearSeries ùïú E F) {x : E}
    (h : p.radius < ‚Äñx‚Äñ‚Çä) : ¬¨Summable fun n => ‚Äñp n‚Äñ * ‚Äñx‚Äñ ^ n :=
  fun hs => not_le_of_lt h (p.le_radius_of_summable_norm hs)
#align formal_multilinear_series.not_summable_norm_of_radius_lt_nnnorm FormalMultilinearSeries.not_summable_norm_of_radius_lt_nnnorm

theorem summable_norm_mul_pow (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0} (h : ‚Üër < p.radius) :
    Summable fun n : ‚Ñï => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n := by
  obtain ‚ü®a, ha : a ‚àà Ioo (0 : ‚Ñù) 1, C, - : 0 < C, hp‚ü© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  exact .of_nonneg_of_le (fun n => mul_nonneg (norm_nonneg _) (pow_nonneg r.coe_nonneg _))
    hp ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _)
#align formal_multilinear_series.summable_norm_mul_pow FormalMultilinearSeries.summable_norm_mul_pow

theorem summable_norm_apply (p : FormalMultilinearSeries ùïú E F) {x : E}
    (hx : x ‚àà EMetric.ball (0 : E) p.radius) : Summable fun n : ‚Ñï => ‚Äñp n fun _ => x‚Äñ := by
  rw [mem_emetric_ball_zero_iff] at hx
  refine .of_nonneg_of_le
    (fun _ ‚Ü¶ norm_nonneg _) (fun n ‚Ü¶ ((p n).le_opNorm _).trans_eq ?_) (p.summable_norm_mul_pow hx)
  simp
#align formal_multilinear_series.summable_norm_apply FormalMultilinearSeries.summable_norm_apply

theorem summable_nnnorm_mul_pow (p : FormalMultilinearSeries ùïú E F) {r : ‚Ñù‚â•0} (h : ‚Üër < p.radius) :
    Summable fun n : ‚Ñï => ‚Äñp n‚Äñ‚Çä * r ^ n := by
  rw [‚Üê NNReal.summable_coe]
  push_cast
  exact p.summable_norm_mul_pow h
#align formal_multilinear_series.summable_nnnorm_mul_pow FormalMultilinearSeries.summable_nnnorm_mul_pow

protected theorem summable [CompleteSpace F] (p : FormalMultilinearSeries ùïú E F) {x : E}
    (hx : x ‚àà EMetric.ball (0 : E) p.radius) : Summable fun n : ‚Ñï => p n fun _ => x :=
  (p.summable_norm_apply hx).of_norm
#align formal_multilinear_series.summable FormalMultilinearSeries.summable

theorem radius_eq_top_of_summable_norm (p : FormalMultilinearSeries ùïú E F)
    (hs : ‚àÄ r : ‚Ñù‚â•0, Summable fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n) : p.radius = ‚àû :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_summable_norm (hs r)
#align formal_multilinear_series.radius_eq_top_of_summable_norm FormalMultilinearSeries.radius_eq_top_of_summable_norm

theorem radius_eq_top_iff_summable_norm (p : FormalMultilinearSeries ùïú E F) :
    p.radius = ‚àû ‚Üî ‚àÄ r : ‚Ñù‚â•0, Summable fun n => ‚Äñp n‚Äñ * (r : ‚Ñù) ^ n := by
  constructor
  ¬∑ intro h r
    obtain ‚ü®a, ha : a ‚àà Ioo (0 : ‚Ñù) 1, C, - : 0 < C, hp‚ü© := p.norm_mul_pow_le_mul_pow_of_lt_radius
      (show (r : ‚Ñù‚â•0‚àû) < p.radius from h.symm ‚ñ∏ ENNReal.coe_lt_top)
    refine .of_norm_bounded
      (fun n ‚Ü¶ (C : ‚Ñù) * a ^ n) ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _) fun n ‚Ü¶ ?_
    specialize hp n
    rwa [Real.norm_of_nonneg (mul_nonneg (norm_nonneg _) (pow_nonneg r.coe_nonneg n))]
  ¬∑ exact p.radius_eq_top_of_summable_norm
#align formal_multilinear_series.radius_eq_top_iff_summable_norm FormalMultilinearSeries.radius_eq_top_iff_summable_norm

/-- If the radius of `p` is positive, then `‚Äñp‚Çô‚Äñ` grows at most geometrically. -/
theorem le_mul_pow_of_radius_pos (p : FormalMultilinearSeries ùïú E F) (h : 0 < p.radius) :
    ‚àÉ (C r : _) (hC : 0 < C) (_ : 0 < r), ‚àÄ n, ‚Äñp n‚Äñ ‚â§ C * r ^ n := by
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 h with ‚ü®r, r0, rlt‚ü©
  have rpos : 0 < (r : ‚Ñù) := by simp [ENNReal.coe_pos.1 r0]
  rcases norm_le_div_pow_of_pos_of_lt_radius p rpos rlt with ‚ü®C, Cpos, hCp‚ü©
  refine ‚ü®C, r‚Åª¬π, Cpos, by simp only [inv_pos, rpos], fun n => ?_‚ü©
  -- Porting note: was `convert`
  rw [inv_pow, ‚Üê div_eq_mul_inv]
  exact hCp n
#align formal_multilinear_series.le_mul_pow_of_radius_pos FormalMultilinearSeries.le_mul_pow_of_radius_pos

/-- The radius of the sum of two formal series is at least the minimum of their two radii. -/
theorem min_radius_le_radius_add (p q : FormalMultilinearSeries ùïú E F) :
    min p.radius q.radius ‚â§ (p + q).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  rw [lt_min_iff] at hr
  have := ((p.isLittleO_one_of_lt_radius hr.1).add (q.isLittleO_one_of_lt_radius hr.2)).isBigO
  refine (p + q).le_radius_of_isBigO ((isBigO_of_le _ fun n => ?_).trans this)
  rw [‚Üê add_mul, norm_mul, norm_mul, norm_norm]
  exact mul_le_mul_of_nonneg_right ((norm_add_le _ _).trans (le_abs_self _)) (norm_nonneg _)
#align formal_multilinear_series.min_radius_le_radius_add FormalMultilinearSeries.min_radius_le_radius_add

@[simp]
theorem radius_neg (p : FormalMultilinearSeries ùïú E F) : (-p).radius = p.radius := by
  simp only [radius, neg_apply, norm_neg]
#align formal_multilinear_series.radius_neg FormalMultilinearSeries.radius_neg

protected theorem hasSum [CompleteSpace F] (p : FormalMultilinearSeries ùïú E F) {x : E}
    (hx : x ‚àà EMetric.ball (0 : E) p.radius) : HasSum (fun n : ‚Ñï => p n fun _ => x) (p.sum x) :=
  (p.summable hx).hasSum
#align formal_multilinear_series.has_sum FormalMultilinearSeries.hasSum

theorem radius_le_radius_continuousLinearMap_comp (p : FormalMultilinearSeries ùïú E F)
    (f : F ‚ÜíL[ùïú] G) : p.radius ‚â§ (f.compFormalMultilinearSeries p).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  apply le_radius_of_isBigO
  apply (IsBigO.trans_isLittleO _ (p.isLittleO_one_of_lt_radius hr)).isBigO
  refine IsBigO.mul (@IsBigOWith.isBigO _ _ _ _ _ ‚Äñf‚Äñ _ _ _ ?_) (isBigO_refl _ _)
  refine IsBigOWith.of_bound (eventually_of_forall fun n => ?_)
  simpa only [norm_norm] using f.norm_compContinuousMultilinearMap_le (p n)
#align formal_multilinear_series.radius_le_radius_continuous_linear_map_comp FormalMultilinearSeries.radius_le_radius_continuousLinearMap_comp

end FormalMultilinearSeries

/-! ### Expanding a function as a power series -/


section

variable {f g : E ‚Üí F} {p pf pg : FormalMultilinearSeries ùïú E F} {x : E} {r r' : ‚Ñù‚â•0‚àû}

/-- Given a function `f : E ‚Üí F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series on the ball of radius `r > 0` around `x` if `f (x + y) = ‚àë' p‚Çô y‚Åø` for all `‚Äñy‚Äñ < r`.
-/
structure HasFPowerSeriesOnBall (f : E ‚Üí F) (p : FormalMultilinearSeries ùïú E F) (x : E) (r : ‚Ñù‚â•0‚àû) :
    Prop where
  r_le : r ‚â§ p.radius
  r_pos : 0 < r
  hasSum :
    ‚àÄ {y}, y ‚àà EMetric.ball (0 : E) r ‚Üí HasSum (fun n : ‚Ñï => p n fun _ : Fin n => y) (f (x + y))
#align has_fpower_series_on_ball HasFPowerSeriesOnBall

/-- Given a function `f : E ‚Üí F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series around `x` if `f (x + y) = ‚àë' p‚Çô y‚Åø` for all `y` in a neighborhood of `0`. -/
def HasFPowerSeriesAt (f : E ‚Üí F) (p : FormalMultilinearSeries ùïú E F) (x : E) :=
  ‚àÉ r, HasFPowerSeriesOnBall f p x r
#align has_fpower_series_at HasFPowerSeriesAt

variable (ùïú)

/-- Given a function `f : E ‚Üí F`, we say that `f` is analytic at `x` if it admits a convergent power
series expansion around `x`. -/
def AnalyticAt (f : E ‚Üí F) (x : E) :=
  ‚àÉ p : FormalMultilinearSeries ùïú E F, HasFPowerSeriesAt f p x
#align analytic_at AnalyticAt

/-- Given a function `f : E ‚Üí F`, we say that `f` is analytic on a set `s` if it is analytic around
every point of `s`. -/
def AnalyticOn (f : E ‚Üí F) (s : Set E) :=
  ‚àÄ x, x ‚àà s ‚Üí AnalyticAt ùïú f x
#align analytic_on AnalyticOn

variable {ùïú}

theorem HasFPowerSeriesOnBall.hasFPowerSeriesAt (hf : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesAt f p x :=
  ‚ü®r, hf‚ü©
#align has_fpower_series_on_ball.has_fpower_series_at HasFPowerSeriesOnBall.hasFPowerSeriesAt

theorem HasFPowerSeriesAt.analyticAt (hf : HasFPowerSeriesAt f p x) : AnalyticAt ùïú f x :=
  ‚ü®p, hf‚ü©
#align has_fpower_series_at.analytic_at HasFPowerSeriesAt.analyticAt

theorem HasFPowerSeriesOnBall.analyticAt (hf : HasFPowerSeriesOnBall f p x r) : AnalyticAt ùïú f x :=
  hf.hasFPowerSeriesAt.analyticAt
#align has_fpower_series_on_ball.analytic_at HasFPowerSeriesOnBall.analyticAt

theorem HasFPowerSeriesOnBall.congr (hf : HasFPowerSeriesOnBall f p x r)
    (hg : EqOn f g (EMetric.ball x r)) : HasFPowerSeriesOnBall g p x r :=
  { r_le := hf.r_le
    r_pos := hf.r_pos
    hasSum := fun {y} hy => by
      convert hf.hasSum hy using 1
      apply hg.symm
      simpa [edist_eq_coe_nnnorm_sub] using hy }
#align has_fpower_series_on_ball.congr HasFPowerSeriesOnBall.congr

/-- If a function `f` has a power series `p` around `x`, then the function `z ‚Ü¶ f (z - y)` has the
same power series around `x + y`. -/
theorem HasFPowerSeriesOnBall.comp_sub (hf : HasFPowerSeriesOnBall f p x r) (y : E) :
    HasFPowerSeriesOnBall (fun z => f (z - y)) p (x + y) r :=
  { r_le := hf.r_le
    r_pos := hf.r_pos
    hasSum := fun {z} hz => by
      convert hf.hasSum hz using 2
      abel }
#align has_fpower_series_on_ball.comp_sub HasFPowerSeriesOnBall.comp_sub

theorem HasFPowerSeriesOnBall.hasSum_sub (hf : HasFPowerSeriesOnBall f p x r) {y : E}
    (hy : y ‚àà EMetric.ball x r) : HasSum (fun n : ‚Ñï => p n fun _ => y - x) (f y) := by
  have : y - x ‚àà EMetric.ball (0 : E) r := by simpa [edist_eq_coe_nnnorm_sub] using hy
  simpa only [add_sub_cancel] using hf.hasSum this
#align has_fpower_series_on_ball.has_sum_sub HasFPowerSeriesOnBall.hasSum_sub

theorem HasFPowerSeriesOnBall.radius_pos (hf : HasFPowerSeriesOnBall f p x r) : 0 < p.radius :=
  lt_of_lt_of_le hf.r_pos hf.r_le
#align has_fpower_series_on_ball.radius_pos HasFPowerSeriesOnBall.radius_pos

theorem HasFPowerSeriesAt.radius_pos (hf : HasFPowerSeriesAt f p x) : 0 < p.radius :=
  let ‚ü®_, hr‚ü© := hf
  hr.radius_pos
#align has_fpower_series_at.radius_pos HasFPowerSeriesAt.radius_pos

theorem HasFPowerSeriesOnBall.mono (hf : HasFPowerSeriesOnBall f p x r) (r'_pos : 0 < r')
    (hr : r' ‚â§ r) : HasFPowerSeriesOnBall f p x r' :=
  ‚ü®le_trans hr hf.1, r'_pos, fun hy => hf.hasSum (EMetric.ball_subset_ball hr hy)‚ü©
#align has_fpower_series_on_ball.mono HasFPowerSeriesOnBall.mono

theorem HasFPowerSeriesAt.congr (hf : HasFPowerSeriesAt f p x) (hg : f =·∂†[ùìù x] g) :
    HasFPowerSeriesAt g p x := by
  rcases hf with ‚ü®r‚ÇÅ, h‚ÇÅ‚ü©
  rcases EMetric.mem_nhds_iff.mp hg with ‚ü®r‚ÇÇ, h‚ÇÇpos, h‚ÇÇ‚ü©
  exact ‚ü®min r‚ÇÅ r‚ÇÇ,
    (h‚ÇÅ.mono (lt_min h‚ÇÅ.r_pos h‚ÇÇpos) inf_le_left).congr
      fun y hy => h‚ÇÇ (EMetric.ball_subset_ball inf_le_right hy)‚ü©
#align has_fpower_series_at.congr HasFPowerSeriesAt.congr

protected theorem HasFPowerSeriesAt.eventually (hf : HasFPowerSeriesAt f p x) :
    ‚àÄ·∂† r : ‚Ñù‚â•0‚àû in ùìù[>] 0, HasFPowerSeriesOnBall f p x r :=
  let ‚ü®_, hr‚ü© := hf
  mem_of_superset (Ioo_mem_nhdsWithin_Ioi (left_mem_Ico.2 hr.r_pos)) fun _ hr' =>
    hr.mono hr'.1 hr'.2.le
#align has_fpower_series_at.eventually HasFPowerSeriesAt.eventually

theorem HasFPowerSeriesOnBall.eventually_hasSum (hf : HasFPowerSeriesOnBall f p x r) :
    ‚àÄ·∂† y in ùìù 0, HasSum (fun n : ‚Ñï => p n fun _ : Fin n => y) (f (x + y)) := by
  filter_upwards [EMetric.ball_mem_nhds (0 : E) hf.r_pos] using fun _ => hf.hasSum
#align has_fpower_series_on_ball.eventually_has_sum HasFPowerSeriesOnBall.eventually_hasSum

theorem HasFPowerSeriesAt.eventually_hasSum (hf : HasFPowerSeriesAt f p x) :
    ‚àÄ·∂† y in ùìù 0, HasSum (fun n : ‚Ñï => p n fun _ : Fin n => y) (f (x + y)) :=
  let ‚ü®_, hr‚ü© := hf
  hr.eventually_hasSum
#align has_fpower_series_at.eventually_has_sum HasFPowerSeriesAt.eventually_hasSum

theorem HasFPowerSeriesOnBall.eventually_hasSum_sub (hf : HasFPowerSeriesOnBall f p x r) :
    ‚àÄ·∂† y in ùìù x, HasSum (fun n : ‚Ñï => p n fun _ : Fin n => y - x) (f y) := by
  filter_upwards [EMetric.ball_mem_nhds x hf.r_pos] with y using hf.hasSum_sub
#align has_fpower_series_on_ball.eventually_has_sum_sub HasFPowerSeriesOnBall.eventually_hasSum_sub

theorem HasFPowerSeriesAt.eventually_hasSum_sub (hf : HasFPowerSeriesAt f p x) :
    ‚àÄ·∂† y in ùìù x, HasSum (fun n : ‚Ñï => p n fun _ : Fin n => y - x) (f y) :=
  let ‚ü®_, hr‚ü© := hf
  hr.eventually_hasSum_sub
#align has_fpower_series_at.eventually_has_sum_sub HasFPowerSeriesAt.eventually_hasSum_sub

theorem HasFPowerSeriesOnBall.eventually_eq_zero
    (hf : HasFPowerSeriesOnBall f (0 : FormalMultilinearSeries ùïú E F) x r) :
    ‚àÄ·∂† z in ùìù x, f z = 0 := by
  filter_upwards [hf.eventually_hasSum_sub] with z hz using hz.unique hasSum_zero
#align has_fpower_series_on_ball.eventually_eq_zero HasFPowerSeriesOnBall.eventually_eq_zero

theorem HasFPowerSeriesAt.eventually_eq_zero
    (hf : HasFPowerSeriesAt f (0 : FormalMultilinearSeries ùïú E F) x) : ‚àÄ·∂† z in ùìù x, f z = 0 :=
  let ‚ü®_, hr‚ü© := hf
  hr.eventually_eq_zero
#align has_fpower_series_at.eventually_eq_zero HasFPowerSeriesAt.eventually_eq_zero

theorem hasFPowerSeriesOnBall_const {c : F} {e : E} :
    HasFPowerSeriesOnBall (fun _ => c) (constFormalMultilinearSeries ùïú E c) e ‚ä§ := by
  refine ‚ü®by simp, WithTop.zero_lt_top, fun _ => hasSum_single 0 fun n hn => ?_‚ü©
  simp [constFormalMultilinearSeries_apply hn]
#align has_fpower_series_on_ball_const hasFPowerSeriesOnBall_const

theorem hasFPowerSeriesAt_const {c : F} {e : E} :
    HasFPowerSeriesAt (fun _ => c) (constFormalMultilinearSeries ùïú E c) e :=
  ‚ü®‚ä§, hasFPowerSeriesOnBall_const‚ü©
#align has_fpower_series_at_const hasFPowerSeriesAt_const

theorem analyticAt_const {v : F} : AnalyticAt ùïú (fun _ => v) x :=
  ‚ü®constFormalMultilinearSeries ùïú E v, hasFPowerSeriesAt_const‚ü©
#align analytic_at_const analyticAt_const

theorem analyticOn_const {v : F} {s : Set E} : AnalyticOn ùïú (fun _ => v) s :=
  fun _ _ => analyticAt_const
#align analytic_on_const analyticOn_const

theorem HasFPowerSeriesOnBall.add (hf : HasFPowerSeriesOnBall f pf x r)
    (hg : HasFPowerSeriesOnBall g pg x r) : HasFPowerSeriesOnBall (f + g) (pf + pg) x r :=
  { r_le := le_trans (le_min_iff.2 ‚ü®hf.r_le, hg.r_le‚ü©) (pf.min_radius_le_radius_add pg)
    r_pos := hf.r_pos
    hasSum := fun hy => (hf.hasSum hy).add (hg.hasSum hy) }
#align has_fpower_series_on_ball.add HasFPowerSeriesOnBall.add

theorem HasFPowerSeriesAt.add (hf : HasFPowerSeriesAt f pf x) (hg : HasFPowerSeriesAt g pg x) :
    HasFPowerSeriesAt (f + g) (pf + pg) x := by
  rcases (hf.eventually.and hg.eventually).exists with ‚ü®r, hr‚ü©
  exact ‚ü®r, hr.1.add hr.2‚ü©
#align has_fpower_series_at.add HasFPowerSeriesAt.add

theorem AnalyticAt.congr (hf : AnalyticAt ùïú f x) (hg : f =·∂†[ùìù x] g) : AnalyticAt ùïú g x :=
  let ‚ü®_, hpf‚ü© := hf
  (hpf.congr hg).analyticAt

theorem analyticAt_congr (h : f =·∂†[ùìù x] g) : AnalyticAt ùïú f x ‚Üî AnalyticAt ùïú g x :=
  ‚ü®fun hf ‚Ü¶ hf.congr h, fun hg ‚Ü¶ hg.congr h.symm‚ü©

theorem AnalyticAt.add (hf : AnalyticAt ùïú f x) (hg : AnalyticAt ùïú g x) : AnalyticAt ùïú (f + g) x :=
  let ‚ü®_, hpf‚ü© := hf
  let ‚ü®_, hqf‚ü© := hg
  (hpf.add hqf).analyticAt
#align analytic_at.add AnalyticAt.add

theorem HasFPowerSeriesOnBall.neg (hf : HasFPowerSeriesOnBall f pf x r) :
    HasFPowerSeriesOnBall (-f) (-pf) x r :=
  { r_le := by
      rw [pf.radius_neg]
      exact hf.r_le
    r_pos := hf.r_pos
    hasSum := fun hy => (hf.hasSum hy).neg }
#align has_fpower_series_on_ball.neg HasFPowerSeriesOnBall.neg

theorem HasFPowerSeriesAt.neg (hf : HasFPowerSeriesAt f pf x) : HasFPowerSeriesAt (-f) (-pf) x :=
  let ‚ü®_, hrf‚ü© := hf
  hrf.neg.hasFPowerSeriesAt
#align has_fpower_series_at.neg HasFPowerSeriesAt.neg

theorem AnalyticAt.neg (hf : AnalyticAt ùïú f x) : AnalyticAt ùïú (-f) x :=
  let ‚ü®_, hpf‚ü© := hf
  hpf.neg.analyticAt
#align analytic_at.neg AnalyticAt.neg

theorem HasFPowerSeriesOnBall.sub (hf : HasFPowerSeriesOnBall f pf x r)
    (hg : HasFPowerSeriesOnBall g pg x r) : HasFPowerSeriesOnBall (f - g) (pf - pg) x r := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_fpower_series_on_ball.sub HasFPowerSeriesOnBall.sub

theorem HasFPowerSeriesAt.sub (hf : HasFPowerSeriesAt f pf x) (hg : HasFPowerSeriesAt g pg x) :
    HasFPowerSeriesAt (f - g) (pf - pg) x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align has_fpower_series_at.sub HasFPowerSeriesAt.sub

theorem AnalyticAt.sub (hf : AnalyticAt ùïú f x) (hg : AnalyticAt ùïú g x) :
    AnalyticAt ùïú (f - g) x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg
#align analytic_at.sub AnalyticAt.sub

theorem AnalyticOn.mono {s t : Set E} (hf : AnalyticOn ùïú f t) (hst : s ‚äÜ t) : AnalyticOn ùïú f s :=
  fun z hz => hf z (hst hz)
#align analytic_on.mono AnalyticOn.mono

theorem AnalyticOn.congr' {s : Set E} (hf : AnalyticOn ùïú f s) (hg : f =·∂†[ùìùÀ¢ s] g) :
    AnalyticOn ùïú g s :=
  fun z hz => (hf z hz).congr (mem_nhdsSet_iff_forall.mp hg z hz)

theorem analyticOn_congr' {s : Set E} (h : f =·∂†[ùìùÀ¢ s] g) : AnalyticOn ùïú f s ‚Üî AnalyticOn ùïú g s :=
  ‚ü®fun hf => hf.congr' h, fun hg => hg.congr' h.symm‚ü©

theorem AnalyticOn.congr {s : Set E} (hs : IsOpen s) (hf : AnalyticOn ùïú f s) (hg : s.EqOn f g) :
    AnalyticOn ùïú g s :=
  hf.congr' <| mem_nhdsSet_iff_forall.mpr
    (fun _ hz => eventuallyEq_iff_exists_mem.mpr ‚ü®s, hs.mem_nhds hz, hg‚ü©)

theorem analyticOn_congr {s : Set E} (hs : IsOpen s) (h : s.EqOn f g) : AnalyticOn ùïú f s ‚Üî
    AnalyticOn ùïú g s := ‚ü®fun hf => hf.congr hs h, fun hg => hg.congr hs h.symm‚ü©

theorem AnalyticOn.add {s : Set E} (hf : AnalyticOn ùïú f s) (hg : AnalyticOn ùïú g s) :
    AnalyticOn ùïú (f + g) s :=
  fun z hz => (hf z hz).add (hg z hz)
#align analytic_on.add AnalyticOn.add

theorem AnalyticOn.sub {s : Set E} (hf : AnalyticOn ùïú f s) (hg : AnalyticOn ùïú g s) :
    AnalyticOn ùïú (f - g) s :=
  fun z hz => (hf z hz).sub (hg z hz)
#align analytic_on.sub AnalyticOn.sub

theorem HasFPowerSeriesOnBall.coeff_zero (hf : HasFPowerSeriesOnBall f pf x r) (v : Fin 0 ‚Üí E) :
    pf 0 v = f x := by
  have v_eq : v = fun i => 0 := Subsingleton.elim _ _
  have zero_mem : (0 : E) ‚àà EMetric.ball (0 : E) r := by simp [hf.r_pos]
  have : ‚àÄ i, i ‚â† 0 ‚Üí (pf i fun j => 0) = 0 := by
    intro i hi
    have : 0 < i := pos_iff_ne_zero.2 hi
    exact ContinuousMultilinearMap.map_coord_zero _ (‚ü®0, this‚ü© : Fin i) rfl
  have A := (hf.hasSum zero_mem).unique (hasSum_single _ this)
  simpa [v_eq] using A.symm
#align has_fpower_series_on_ball.coeff_zero HasFPowerSeriesOnBall.coeff_zero

theorem HasFPowerSeriesAt.coeff_zero (hf : HasFPowerSeriesAt f pf x) (v : Fin 0 ‚Üí E) :
    pf 0 v = f x :=
  let ‚ü®_, hrf‚ü© := hf
  hrf.coeff_zero v
#align has_fpower_series_at.coeff_zero HasFPowerSeriesAt.coeff_zero

/-- If a function `f` has a power series `p` on a ball and `g` is linear, then `g ‚àò f` has the
power series `g ‚àò p` on the same ball. -/
theorem ContinuousLinearMap.comp_hasFPowerSeriesOnBall (g : F ‚ÜíL[ùïú] G)
    (h : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesOnBall (g ‚àò f) (g.compFormalMultilinearSeries p) x r :=
  { r_le := h.r_le.trans (p.radius_le_radius_continuousLinearMap_comp _)
    r_pos := h.r_pos
    hasSum := fun hy => by
      simpa only [ContinuousLinearMap.compFormalMultilinearSeries_apply,
        ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply] using
        g.hasSum (h.hasSum hy) }
#align continuous_linear_map.comp_has_fpower_series_on_ball ContinuousLinearMap.comp_hasFPowerSeriesOnBall

/-- If a function `f` is analytic on a set `s` and `g` is linear, then `g ‚àò f` is analytic
on `s`. -/
theorem ContinuousLinearMap.comp_analyticOn {s : Set E} (g : F ‚ÜíL[ùïú] G) (h : AnalyticOn ùïú f s) :
    AnalyticOn ùïú (g ‚àò f) s := by
  rintro x hx
  rcases h x hx with ‚ü®p, r, hp‚ü©
  exact ‚ü®g.compFormalMultilinearSeries p, r, g.comp_hasFPowerSeriesOnBall hp‚ü©
#align continuous_linear_map.comp_analytic_on ContinuousLinearMap.comp_analyticOn

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence.

This version provides an upper estimate that decreases both in `‚Äñy‚Äñ` and `n`. See also
`HasFPowerSeriesOnBall.uniform_geometric_approx` for a weaker version. -/
theorem HasFPowerSeriesOnBall.uniform_geometric_approx' {r' : ‚Ñù‚â•0}
    (hf : HasFPowerSeriesOnBall f p x r) (h : (r' : ‚Ñù‚â•0‚àû) < r) :
    ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ y ‚àà Metric.ball (0 : E) r', ‚àÄ n,
      ‚Äñf (x + y) - p.partialSum n y‚Äñ ‚â§ C * (a * (‚Äñy‚Äñ / r')) ^ n := by
  obtain ‚ü®a, ha, C, hC, hp‚ü© : ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ n, ‚Äñp n‚Äñ * (r' : ‚Ñù) ^ n ‚â§ C * a ^ n :=
    p.norm_mul_pow_le_mul_pow_of_lt_radius (h.trans_le hf.r_le)
  refine ‚ü®a, ha, C / (1 - a), div_pos hC (sub_pos.2 ha.2), fun y hy n => ?_‚ü©
  have yr' : ‚Äñy‚Äñ < r' := by
    rw [ball_zero_eq] at hy
    exact hy
  have hr'0 : 0 < (r' : ‚Ñù) := (norm_nonneg _).trans_lt yr'
  have : y ‚àà EMetric.ball (0 : E) r := by
    refine mem_emetric_ball_zero_iff.2 (lt_trans ?_ h)
    exact mod_cast yr'
  rw [norm_sub_rev, ‚Üê mul_div_right_comm]
  have ya : a * (‚Äñy‚Äñ / ‚Üër') ‚â§ a :=
    mul_le_of_le_one_right ha.1.le (div_le_one_of_le yr'.le r'.coe_nonneg)
  suffices ‚Äñp.partialSum n y - f (x + y)‚Äñ ‚â§ C * (a * (‚Äñy‚Äñ / r')) ^ n / (1 - a * (‚Äñy‚Äñ / r')) by
    refine this.trans ?_
    have : 0 < a := ha.1
    gcongr
    apply_rules [sub_pos.2, ha.2]
  apply norm_sub_le_of_geometric_bound_of_hasSum (ya.trans_lt ha.2) _ (hf.hasSum this)
  intro n
  calc
    ‚Äñ(p n) fun _ : Fin n => y‚Äñ
    _ ‚â§ ‚Äñp n‚Äñ * ‚àè _i : Fin n, ‚Äñy‚Äñ := ContinuousMultilinearMap.le_opNorm _ _
    _ = ‚Äñp n‚Äñ * (r' : ‚Ñù) ^ n * (‚Äñy‚Äñ / r') ^ n := by field_simp [mul_right_comm]
    _ ‚â§ C * a ^ n * (‚Äñy‚Äñ / r') ^ n := by gcongr ?_ * _; apply hp
    _ ‚â§ C * (a * (‚Äñy‚Äñ / r')) ^ n := by rw [mul_pow, mul_assoc]
#align has_fpower_series_on_ball.uniform_geometric_approx' HasFPowerSeriesOnBall.uniform_geometric_approx'

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence. -/
theorem HasFPowerSeriesOnBall.uniform_geometric_approx {r' : ‚Ñù‚â•0}
    (hf : HasFPowerSeriesOnBall f p x r) (h : (r' : ‚Ñù‚â•0‚àû) < r) :
    ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1,
      ‚àÉ C > 0, ‚àÄ y ‚àà Metric.ball (0 : E) r', ‚àÄ n, ‚Äñf (x + y) - p.partialSum n y‚Äñ ‚â§ C * a ^ n := by
  obtain ‚ü®a, ha, C, hC, hp‚ü© : ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ y ‚àà Metric.ball (0 : E) r', ‚àÄ n,
      ‚Äñf (x + y) - p.partialSum n y‚Äñ ‚â§ C * (a * (‚Äñy‚Äñ / r')) ^ n :=
    hf.uniform_geometric_approx' h
  refine ‚ü®a, ha, C, hC, fun y hy n => (hp y hy n).trans ?_‚ü©
  have yr' : ‚Äñy‚Äñ < r' := by rwa [ball_zero_eq] at hy
  have := ha.1.le -- needed to discharge a side goal on the next line
  gcongr
  exact mul_le_of_le_one_right ha.1.le (div_le_one_of_le yr'.le r'.coe_nonneg)
#align has_fpower_series_on_ball.uniform_geometric_approx HasFPowerSeriesOnBall.uniform_geometric_approx

/-- Taylor formula for an analytic function, `IsBigO` version. -/
theorem HasFPowerSeriesAt.isBigO_sub_partialSum_pow (hf : HasFPowerSeriesAt f p x) (n : ‚Ñï) :
    (fun y : E => f (x + y) - p.partialSum n y) =O[ùìù 0] fun y => ‚Äñy‚Äñ ^ n := by
  rcases hf with ‚ü®r, hf‚ü©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hf.r_pos with ‚ü®r', r'0, h‚ü©
  obtain ‚ü®a, -, C, -, hp‚ü© : ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ y ‚àà Metric.ball (0 : E) r', ‚àÄ n,
      ‚Äñf (x + y) - p.partialSum n y‚Äñ ‚â§ C * (a * (‚Äñy‚Äñ / r')) ^ n :=
    hf.uniform_geometric_approx' h
  refine isBigO_iff.2 ‚ü®C * (a / r') ^ n, ?_‚ü©
  replace r'0 : 0 < (r' : ‚Ñù) := mod_cast r'0
  filter_upwards [Metric.ball_mem_nhds (0 : E) r'0] with y hy
  simpa [mul_pow, mul_div_assoc, mul_assoc, div_mul_eq_mul_div] using hp y hy n
set_option linter.uppercaseLean3 false in
#align has_fpower_series_at.is_O_sub_partial_sum_pow HasFPowerSeriesAt.isBigO_sub_partialSum_pow

/-- If `f` has formal power series `‚àë n, p‚Çô` on a ball of radius `r`, then for `y, z` in any smaller
ball, the norm of the difference `f y - f z - p 1 (fun _ ‚Ü¶ y - z)` is bounded above by
`C * (max ‚Äñy - x‚Äñ ‚Äñz - x‚Äñ) * ‚Äñy - z‚Äñ`. This lemma formulates this property using `IsBigO` and
`Filter.principal` on `E √ó E`. -/
theorem HasFPowerSeriesOnBall.isBigO_image_sub_image_sub_deriv_principal
    (hf : HasFPowerSeriesOnBall f p x r) (hr : r' < r) :
    (fun y : E √ó E => f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) =O[ùìü (EMetric.ball (x, x) r')]
      fun y => ‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ := by
  lift r' to ‚Ñù‚â•0 using ne_top_of_lt hr
  rcases (zero_le r').eq_or_lt with (rfl | hr'0)
  ¬∑ simp only [isBigO_bot, EMetric.ball_zero, principal_empty, ENNReal.coe_zero]
  obtain ‚ü®a, ha, C, hC : 0 < C, hp‚ü© :
      ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ n : ‚Ñï, ‚Äñp n‚Äñ * (r' : ‚Ñù) ^ n ‚â§ C * a ^ n :=
    p.norm_mul_pow_le_mul_pow_of_lt_radius (hr.trans_le hf.r_le)
  simp only [‚Üê le_div_iff (pow_pos (NNReal.coe_pos.2 hr'0) _)] at hp
  set L : E √ó E ‚Üí ‚Ñù := fun y =>
    C * (a / r') ^ 2 * (‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ) * (a / (1 - a) ^ 2 + 2 / (1 - a))
  have hL : ‚àÄ y ‚àà EMetric.ball (x, x) r', ‚Äñf y.1 - f y.2 - p 1 fun _ => y.1 - y.2‚Äñ ‚â§ L y := by
    intro y hy'
    have hy : y ‚àà EMetric.ball x r √óÀ¢ EMetric.ball x r := by
      rw [EMetric.ball_prod_same]
      exact EMetric.ball_subset_ball hr.le hy'
    set A : ‚Ñï ‚Üí F := fun n => (p n fun _ => y.1 - x) - p n fun _ => y.2 - x
    have hA : HasSum (fun n => A (n + 2)) (f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) := by
      convert (hasSum_nat_add_iff' 2).2 ((hf.hasSum_sub hy.1).sub (hf.hasSum_sub hy.2)) using 1
      rw [Finset.sum_range_succ, Finset.sum_range_one, hf.coeff_zero, hf.coeff_zero, sub_self,
        zero_add, ‚Üê Subsingleton.pi_single_eq (0 : Fin 1) (y.1 - x), Pi.single,
        ‚Üê Subsingleton.pi_single_eq (0 : Fin 1) (y.2 - x), Pi.single, ‚Üê (p 1).map_sub, ‚Üê Pi.single,
        Subsingleton.pi_single_eq, sub_sub_sub_cancel_right]
    rw [EMetric.mem_ball, edist_eq_coe_nnnorm_sub, ENNReal.coe_lt_coe] at hy'
    set B : ‚Ñï ‚Üí ‚Ñù := fun n => C * (a / r') ^ 2 * (‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ) * ((n + 2) * a ^ n)
    have hAB : ‚àÄ n, ‚ÄñA (n + 2)‚Äñ ‚â§ B n := fun n =>
      calc
        ‚ÄñA (n + 2)‚Äñ ‚â§ ‚Äñp (n + 2)‚Äñ * ‚Üë(n + 2) * ‚Äñy - (x, x)‚Äñ ^ (n + 1) * ‚Äñy.1 - y.2‚Äñ := by
          -- Porting note: `pi_norm_const` was `pi_norm_const (_ : E)`
          simpa only [Fintype.card_fin, pi_norm_const, Prod.norm_def, Pi.sub_def,
            Prod.fst_sub, Prod.snd_sub, sub_sub_sub_cancel_right] using
            (p <| n + 2).norm_image_sub_le (fun _ => y.1 - x) fun _ => y.2 - x
        _ = ‚Äñp (n + 2)‚Äñ * ‚Äñy - (x, x)‚Äñ ^ n * (‚Üë(n + 2) * ‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ) := by
          rw [pow_succ ‚Äñy - (x, x)‚Äñ]
          ring
        -- Porting note: the two `‚Üë` in `‚Üër'` are new, without them, Lean fails to synthesize
        -- instances `HDiv ‚Ñù ‚Ñù‚â•0 ?m` or `HMul ‚Ñù ‚Ñù‚â•0 ?m`
        _ ‚â§ C * a ^ (n + 2) / ‚Üër' ^ (n + 2)
            * ‚Üër' ^ n * (‚Üë(n + 2) * ‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ) := by
          have : 0 < a := ha.1
          gcongr
          ¬∑ apply hp
          ¬∑ apply hy'.le
        _ = B n := by
          field_simp [B, pow_succ]
          simp only [mul_assoc, mul_comm, mul_left_comm]
    have hBL : HasSum B (L y) := by
      apply HasSum.mul_left
      simp only [add_mul]
      have : ‚Äña‚Äñ < 1 := by simp only [Real.norm_eq_abs, abs_of_pos ha.1, ha.2]
      rw [div_eq_mul_inv, div_eq_mul_inv]
      exact (hasSum_coe_mul_geometric_of_norm_lt_one this).add  -- Porting note: was `convert`!
          ((hasSum_geometric_of_norm_lt_one this).mul_left 2)
    exact hA.norm_le_of_bounded hBL hAB
  suffices L =O[ùìü (EMetric.ball (x, x) r')] fun y => ‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ by
    refine (IsBigO.of_bound 1 (eventually_principal.2 fun y hy => ?_)).trans this
    rw [one_mul]
    exact (hL y hy).trans (le_abs_self _)
  simp_rw [L, mul_right_comm _ (_ * _)]
  exact (isBigO_refl _ _).const_mul_left _
set_option linter.uppercaseLean3 false in
#align has_fpower_series_on_ball.is_O_image_sub_image_sub_deriv_principal HasFPowerSeriesOnBall.isBigO_image_sub_image_sub_deriv_principal

/-- If `f` has formal power series `‚àë n, p‚Çô` on a ball of radius `r`, then for `y, z` in any smaller
ball, the norm of the difference `f y - f z - p 1 (fun _ ‚Ü¶ y - z)` is bounded above by
`C * (max ‚Äñy - x‚Äñ ‚Äñz - x‚Äñ) * ‚Äñy - z‚Äñ`. -/
theorem HasFPowerSeriesOnBall.image_sub_sub_deriv_le (hf : HasFPowerSeriesOnBall f p x r)
    (hr : r' < r) :
    ‚àÉ C, ‚àÄ·µâ (y ‚àà EMetric.ball x r') (z ‚àà EMetric.ball x r'),
      ‚Äñf y - f z - p 1 fun _ => y - z‚Äñ ‚â§ C * max ‚Äñy - x‚Äñ ‚Äñz - x‚Äñ * ‚Äñy - z‚Äñ := by
  simpa only [isBigO_principal, mul_assoc, norm_mul, norm_norm, Prod.forall, EMetric.mem_ball,
    Prod.edist_eq, max_lt_iff, and_imp, @forall_swap (_ < _) E] using
    hf.isBigO_image_sub_image_sub_deriv_principal hr
#align has_fpower_series_on_ball.image_sub_sub_deriv_le HasFPowerSeriesOnBall.image_sub_sub_deriv_le

/-- If `f` has formal power series `‚àë n, p‚Çô` at `x`, then
`f y - f z - p 1 (fun _ ‚Ü¶ y - z) = O(‚Äñ(y, z) - (x, x)‚Äñ * ‚Äñy - z‚Äñ)` as `(y, z) ‚Üí (x, x)`.
In particular, `f` is strictly differentiable at `x`. -/
theorem HasFPowerSeriesAt.isBigO_image_sub_norm_mul_norm_sub (hf : HasFPowerSeriesAt f p x) :
    (fun y : E √ó E => f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) =O[ùìù (x, x)] fun y =>
      ‚Äñy - (x, x)‚Äñ * ‚Äñy.1 - y.2‚Äñ := by
  rcases hf with ‚ü®r, hf‚ü©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hf.r_pos with ‚ü®r', r'0, h‚ü©
  refine (hf.isBigO_image_sub_image_sub_deriv_principal h).mono ?_
  exact le_principal_iff.2 (EMetric.ball_mem_nhds _ r'0)
set_option linter.uppercaseLean3 false in
#align has_fpower_series_at.is_O_image_sub_norm_mul_norm_sub HasFPowerSeriesAt.isBigO_image_sub_norm_mul_norm_sub

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f (x + y)`
is the uniform limit of `p.partialSum n y` there. -/
theorem HasFPowerSeriesOnBall.tendstoUniformlyOn {r' : ‚Ñù‚â•0} (hf : HasFPowerSeriesOnBall f p x r)
    (h : (r' : ‚Ñù‚â•0‚àû) < r) :
    TendstoUniformlyOn (fun n y => p.partialSum n y) (fun y => f (x + y)) atTop
      (Metric.ball (0 : E) r') := by
  obtain ‚ü®a, ha, C, -, hp‚ü© : ‚àÉ a ‚àà Ioo (0 : ‚Ñù) 1, ‚àÉ C > 0, ‚àÄ y ‚àà Metric.ball (0 : E) r', ‚àÄ n,
    ‚Äñf (x + y) - p.partialSum n y‚Äñ ‚â§ C * a ^ n := hf.uniform_geometric_approx h
  refine Metric.tendstoUniformlyOn_iff.2 fun Œµ Œµpos => ?_
  have L : Tendsto (fun n => (C : ‚Ñù) * a ^ n) atTop (ùìù ((C : ‚Ñù) * 0)) :=
    tendsto_const_nhds.mul (tendsto_pow_atTop_nhds_zero_of_lt_one ha.1.le ha.2)
  rw [mul_zero] at L
  refine (L.eventually (gt_mem_nhds Œµpos)).mono fun n hn y hy => ?_
  rw [dist_eq_norm]
  exact (hp y hy n).trans_lt hn
#align has_fpower_series_on_ball.tendsto_uniformly_on HasFPowerSeriesOnBall.tendstoUniformlyOn

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the partial sums of this power series on the disk of convergence, i.e., `f (x + y)`
is the locally uniform limit of `p.partialSum n y` there. -/
theorem HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn (hf : HasFPowerSeriesOnBall f p x r) :
    TendstoLocallyUniformlyOn (fun n y => p.partialSum n y) (fun y => f (x + y)) atTop
      (EMetric.ball (0 : E) r) := by
  intro u hu x hx
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hx with ‚ü®r', xr', hr'‚ü©
  have : EMetric.ball (0 : E) r' ‚àà ùìù x := IsOpen.mem_nhds EMetric.isOpen_ball xr'
  refine ‚ü®EMetric.ball (0 : E) r', mem_nhdsWithin_of_mem_nhds this, ?_‚ü©
  simpa [Metric.emetric_ball_nnreal] using hf.tendstoUniformlyOn hr' u hu
#align has_fpower_series_on_ball.tendsto_locally_uniformly_on HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f y`
is the uniform limit of `p.partialSum n (y - x)` there. -/
theorem HasFPowerSeriesOnBall.tendstoUniformlyOn' {r' : ‚Ñù‚â•0} (hf : HasFPowerSeriesOnBall f p x r)
    (h : (r' : ‚Ñù‚â•0‚àû) < r) :
    TendstoUniformlyOn (fun n y => p.partialSum n (y - x)) f atTop (Metric.ball (x : E) r') := by
  convert (hf.tendstoUniformlyOn h).comp fun y => y - x using 1
  ¬∑ simp [(¬∑ ‚àò ¬∑)]
  ¬∑ ext z
    simp [dist_eq_norm]
#align has_fpower_series_on_ball.tendsto_uniformly_on' HasFPowerSeriesOnBall.tendstoUniformlyOn'

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the partial sums of this power series on the disk of convergence, i.e., `f y`
is the locally uniform limit of `p.partialSum n (y - x)` there. -/
theorem HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn' (hf : HasFPowerSeriesOnBall f p x r) :
    TendstoLocallyUniformlyOn (fun n y => p.partialSum n (y - x)) f atTop
      (EMetric.ball (x : E) r) := by
  have A : ContinuousOn (fun y : E => y - x) (EMetric.ball (x : E) r) :=
    (continuous_id.sub continuous_const).continuousOn
  convert hf.tendstoLocallyUniformlyOn.comp (fun y : E => y - x) _ A using 1
  ¬∑ ext z
    simp
  ¬∑ intro z
    simp [edist_eq_coe_nnnorm, edist_eq_coe_nnnorm_sub]
#align has_fpower_series_on_ball.tendsto_locally_uniformly_on' HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn'

/-- If a function admits a power series expansion on a disk, then it is continuous there. -/
protected theorem HasFPowerSeriesOnBall.continuousOn (hf : HasFPowerSeriesOnBall f p x r) :
    ContinuousOn f (EMetric.ball x r) :=
  hf.tendstoLocallyUniformlyOn'.continuousOn <|
    eventually_of_forall fun n =>
      ((p.partialSum_continuous n).comp (continuous_id.sub continuous_const)).continuousOn
#align has_fpower_series_on_ball.continuous_on HasFPowerSeriesOnBall.continuousOn

protected theorem HasFPowerSeriesAt.continuousAt (hf : HasFPowerSeriesAt f p x) :
    ContinuousAt f x :=
  let ‚ü®_, hr‚ü© := hf
  hr.continuousOn.continuousAt (EMetric.ball_mem_nhds x hr.r_pos)
#align has_fpower_series_at.continuous_at HasFPowerSeriesAt.continuousAt

protected theorem AnalyticAt.continuousAt (hf : AnalyticAt ùïú f x) : ContinuousAt f x :=
  let ‚ü®_, hp‚ü© := hf
  hp.continuousAt
#align analytic_at.continuous_at AnalyticAt.continuousAt

protected theorem AnalyticOn.continuousOn {s : Set E} (hf : AnalyticOn ùïú f s) : ContinuousOn f s :=
  fun x hx => (hf x hx).continuousAt.continuousWithinAt
#align analytic_on.continuous_on AnalyticOn.continuousOn

/-- Analytic everywhere implies continuous -/
theorem AnalyticOn.continuous {f : E ‚Üí F} (fa : AnalyticOn ùïú f univ) : Continuous f := by
  rw [continuous_iff_continuousOn_univ]; exact fa.continuousOn

/-- In a complete space, the sum of a converging power series `p` admits `p` as a power series.
This is not totally obvious as we need to check the convergence of the series. -/
protected theorem FormalMultilinearSeries.hasFPowerSeriesOnBall [CompleteSpace F]
    (p : FormalMultilinearSeries ùïú E F) (h : 0 < p.radius) :
    HasFPowerSeriesOnBall p.sum p 0 p.radius :=
  { r_le := le_rfl
    r_pos := h
    hasSum := fun hy => by
      rw [zero_add]
      exact p.hasSum hy }
#align formal_multilinear_series.has_fpower_series_on_ball FormalMultilinearSeries.hasFPowerSeriesOnBall

theorem HasFPowerSeriesOnBall.sum (h : HasFPowerSeriesOnBall f p x r) {y : E}
    (hy : y ‚àà EMetric.ball (0 : E) r) : f (x + y) = p.sum y :=
  (h.hasSum hy).tsum_eq.symm
#align has_fpower_series_on_ball.sum HasFPowerSeriesOnBall.sum

/-- The sum of a converging power series is continuous in its disk of convergence. -/
protected theorem FormalMultilinearSeries.continuousOn [CompleteSpace F] :
    ContinuousOn p.sum (EMetric.ball 0 p.radius) := by
  rcases (zero_le p.radius).eq_or_lt with h | h
  ¬∑ simp [‚Üê h, continuousOn_empty]
  ¬∑ exact (p.hasFPowerSeriesOnBall h).continuousOn
#align formal_multilinear_series.continuous_on FormalMultilinearSeries.continuousOn

end

/-!
### Uniqueness of power series
If a function `f : E ‚Üí F` has two representations as power series at a point `x : E`, corresponding
to formal multilinear series `p‚ÇÅ` and `p‚ÇÇ`, then these representations agree term-by-term. That is,
for any `n : ‚Ñï` and `y : E`, `p‚ÇÅ n (fun i ‚Ü¶ y) = p‚ÇÇ n (fun i ‚Ü¶ y)`. In the one-dimensional case,
when `f : ùïú ‚Üí E`, the continuous multilinear maps `p‚ÇÅ n` and `p‚ÇÇ n` are given by
`ContinuousMultilinearMap.mkPiRing`, and hence are determined completely by the value of
`p‚ÇÅ n (fun i ‚Ü¶ 1)`, so `p‚ÇÅ = p‚ÇÇ`. Consequently, the radius of convergence for one series can be
transferred to the other.
-/


section Uniqueness

open ContinuousMultilinearMap

theorem Asymptotics.IsBigO.continuousMultilinearMap_apply_eq_zero {n : ‚Ñï} {p : E[√ón]‚ÜíL[ùïú] F}
    (h : (fun y => p fun _ => y) =O[ùìù 0] fun y => ‚Äñy‚Äñ ^ (n + 1)) (y : E) : (p fun _ => y) = 0 := by
  obtain ‚ü®c, c_pos, hc‚ü© := h.exists_pos
  obtain ‚ü®t, ht, t_open, z_mem‚ü© := eventually_nhds_iff.mp (isBigOWith_iff.mp hc)
  obtain ‚ü®Œ¥, Œ¥_pos, Œ¥Œµ‚ü© := (Metric.isOpen_iff.mp t_open) 0 z_mem
  clear h hc z_mem
  cases' n with n
  ¬∑ exact norm_eq_zero.mp (by
      -- Porting note: the symmetric difference of the `simpa only` sets:
      -- added `Nat.zero_eq, zero_add, pow_one`
      -- removed `zero_pow, Ne.def, Nat.one_ne_zero, not_false_iff`
      simpa only [Nat.zero_eq, fin0_apply_norm, norm_eq_zero, norm_zero, zero_add, pow_one,
        mul_zero, norm_le_zero_iff] using ht 0 (Œ¥Œµ (Metric.mem_ball_self Œ¥_pos)))
  ¬∑ refine Or.elim (Classical.em (y = 0))
      (fun hy => by simpa only [hy] using p.map_zero) fun hy => ?_
    replace hy := norm_pos_iff.mpr hy
    refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add fun Œµ Œµ_pos => ?_) (norm_nonneg _))
    have h‚ÇÄ := _root_.mul_pos c_pos (pow_pos hy (n.succ + 1))
    obtain ‚ü®k, k_pos, k_norm‚ü© := NormedField.exists_norm_lt ùïú
      (lt_min (mul_pos Œ¥_pos (inv_pos.mpr hy)) (mul_pos Œµ_pos (inv_pos.mpr h‚ÇÄ)))
    have h‚ÇÅ : ‚Äñk ‚Ä¢ y‚Äñ < Œ¥ := by
      rw [norm_smul]
      exact inv_mul_cancel_right‚ÇÄ hy.ne.symm Œ¥ ‚ñ∏
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_left _ _)) hy
    have h‚ÇÇ :=
      calc
        ‚Äñp fun _ => k ‚Ä¢ y‚Äñ ‚â§ c * ‚Äñk ‚Ä¢ y‚Äñ ^ (n.succ + 1) := by
          -- Porting note: now Lean wants `_root_.`
          simpa only [norm_pow, _root_.norm_norm] using ht (k ‚Ä¢ y) (Œ¥Œµ (mem_ball_zero_iff.mpr h‚ÇÅ))
          --simpa only [norm_pow, norm_norm] using ht (k ‚Ä¢ y) (Œ¥Œµ (mem_ball_zero_iff.mpr h‚ÇÅ))
        _ = ‚Äñk‚Äñ ^ n.succ * (‚Äñk‚Äñ * (c * ‚Äñy‚Äñ ^ (n.succ + 1))) := by
          -- Porting note: added `Nat.succ_eq_add_one` since otherwise `ring` does not conclude.
          simp only [norm_smul, mul_pow, Nat.succ_eq_add_one]
          -- Porting note: removed `rw [pow_succ]`, since it now becomes superfluous.
          ring
    have h‚ÇÉ : ‚Äñk‚Äñ * (c * ‚Äñy‚Äñ ^ (n.succ + 1)) < Œµ :=
      inv_mul_cancel_right‚ÇÄ h‚ÇÄ.ne.symm Œµ ‚ñ∏
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_right _ _)) h‚ÇÄ
    calc
      ‚Äñp fun _ => y‚Äñ = ‚Äñk‚Åª¬π ^ n.succ‚Äñ * ‚Äñp fun _ => k ‚Ä¢ y‚Äñ := by
        simpa only [inv_smul_smul‚ÇÄ (norm_pos_iff.mp k_pos), norm_smul, Finset.prod_const,
          Finset.card_fin] using
          congr_arg norm (p.map_smul_univ (fun _ : Fin n.succ => k‚Åª¬π) fun _ : Fin n.succ => k ‚Ä¢ y)
      _ ‚â§ ‚Äñk‚Åª¬π ^ n.succ‚Äñ * (‚Äñk‚Äñ ^ n.succ * (‚Äñk‚Äñ * (c * ‚Äñy‚Äñ ^ (n.succ + 1)))) := by gcongr
      _ = ‚Äñ(k‚Åª¬π * k) ^ n.succ‚Äñ * (‚Äñk‚Äñ * (c * ‚Äñy‚Äñ ^ (n.succ + 1))) := by
        rw [‚Üê mul_assoc]
        simp [norm_mul, mul_pow]
      _ ‚â§ 0 + Œµ := by
        rw [inv_mul_cancel (norm_pos_iff.mp k_pos)]
        simpa using h‚ÇÉ.le
set_option linter.uppercaseLean3 false in
#align asymptotics.is_O.continuous_multilinear_map_apply_eq_zero Asymptotics.IsBigO.continuousMultilinearMap_apply_eq_zero

/-- If a formal multilinear series `p` represents the zero function at `x : E`, then the
terms `p n (fun i ‚Ü¶ y)` appearing in the sum are zero for any `n : ‚Ñï`, `y : E`. -/
theorem HasFPowerSeriesAt.apply_eq_zero {p : FormalMultilinearSeries ùïú E F} {x : E}
    (h : HasFPowerSeriesAt 0 p x) (n : ‚Ñï) : ‚àÄ y : E, (p n fun _ => y) = 0 := by
  refine Nat.strong_induction_on n fun k hk => ?_
  have psum_eq : p.partialSum (k + 1) = fun y => p k fun _ => y := by
    funext z
    refine Finset.sum_eq_single _ (fun b hb hnb => ?_) fun hn => ?_
    ¬∑ have := Finset.mem_range_succ_iff.mp hb
      simp only [hk b (this.lt_of_ne hnb), Pi.zero_apply]
    ¬∑ exact False.elim (hn (Finset.mem_range.mpr (lt_add_one k)))
  replace h := h.isBigO_sub_partialSum_pow k.succ
  simp only [psum_eq, zero_sub, Pi.zero_apply, Asymptotics.isBigO_neg_left] at h
  exact h.continuousMultilinearMap_apply_eq_zero
#align has_fpower_series_at.apply_eq_zero HasFPowerSeriesAt.apply_eq_zero

/-- A one-dimensional formal multilinear series representing the zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero {p : FormalMultilinearSeries ùïú ùïú E} {x : ùïú}
    (h : HasFPowerSeriesAt 0 p x) : p = 0 := by
  -- Porting note: `funext; ext` was `ext (n x)`
  funext n
  ext x
  rw [‚Üê mkPiRing_apply_one_eq_self (p n)]
  simp [h.apply_eq_zero n 1]
#align has_fpower_series_at.eq_zero HasFPowerSeriesAt.eq_zero

/-- One-dimensional formal multilinear series representing the same function are equal. -/
theorem HasFPowerSeriesAt.eq_formalMultilinearSeries {p‚ÇÅ p‚ÇÇ : FormalMultilinearSeries ùïú ùïú E}
    {f : ùïú ‚Üí E} {x : ùïú} (h‚ÇÅ : HasFPowerSeriesAt f p‚ÇÅ x) (h‚ÇÇ : HasFPowerSeriesAt f p‚ÇÇ x) : p‚ÇÅ = p‚ÇÇ :=
  sub_eq_zero.mp (HasFPowerSeriesAt.eq_zero (by simpa only [sub_self] using h‚ÇÅ.sub h‚ÇÇ))
#align has_fpower_series_at.eq_formal_multilinear_series HasFPowerSeriesAt.eq_formalMultilinearSeries

theorem HasFPowerSeriesAt.eq_formalMultilinearSeries_of_eventually
    {p q : FormalMultilinearSeries ùïú ùïú E} {f g : ùïú ‚Üí E} {x : ùïú} (hp : HasFPowerSeriesAt f p x)
    (hq : HasFPowerSeriesAt g q x) (heq : ‚àÄ·∂† z in ùìù x, f z = g z) : p = q :=
  (hp.congr heq).eq_formalMultilinearSeries hq
#align has_fpower_series_at.eq_formal_multilinear_series_of_eventually HasFPowerSeriesAt.eq_formalMultilinearSeries_of_eventually

/-- A one-dimensional formal multilinear series representing a locally zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero_of_eventually {p : FormalMultilinearSeries ùïú ùïú E} {f : ùïú ‚Üí E}
    {x : ùïú} (hp : HasFPowerSeriesAt f p x) (hf : f =·∂†[ùìù x] 0) : p = 0 :=
  (hp.congr hf).eq_zero
#align has_fpower_series_at.eq_zero_of_eventually HasFPowerSeriesAt.eq_zero_of_eventually

/-- If a function `f : ùïú ‚Üí E` has two power series representations at `x`, then the given radii in
which convergence is guaranteed may be interchanged. This can be useful when the formal multilinear
series in one representation has a particularly nice form, but the other has a larger radius. -/
theorem HasFPowerSeriesOnBall.exchange_radius {p‚ÇÅ p‚ÇÇ : FormalMultilinearSeries ùïú ùïú E} {f : ùïú ‚Üí E}
    {r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0‚àû} {x : ùïú} (h‚ÇÅ : HasFPowerSeriesOnBall f p‚ÇÅ x r‚ÇÅ)
    (h‚ÇÇ : HasFPowerSeriesOnBall f p‚ÇÇ x r‚ÇÇ) : HasFPowerSeriesOnBall f p‚ÇÅ x r‚ÇÇ :=
  h‚ÇÇ.hasFPowerSeriesAt.eq_formalMultilinearSeries h‚ÇÅ.hasFPowerSeriesAt ‚ñ∏ h‚ÇÇ
#align has_fpower_series_on_ball.exchange_radius HasFPowerSeriesOnBall.exchange_radius

/-- If a function `f : ùïú ‚Üí E` has power series representation `p` on a ball of some radius and for
each positive radius it has some power series representation, then `p` converges to `f` on the whole
`ùïú`. -/
theorem HasFPowerSeriesOnBall.r_eq_top_of_exists {f : ùïú ‚Üí E} {r : ‚Ñù‚â•0‚àû} {x : ùïú}
    {p : FormalMultilinearSeries ùïú ùïú E} (h : HasFPowerSeriesOnBall f p x r)
    (h' : ‚àÄ (r' : ‚Ñù‚â•0) (_ : 0 < r'), ‚àÉ p' : FormalMultilinearSeries ùïú ùïú E,
      HasFPowerSeriesOnBall f p' x r') :
    HasFPowerSeriesOnBall f p x ‚àû :=
  { r_le := ENNReal.le_of_forall_pos_nnreal_lt fun r hr _ =>
      let ‚ü®_, hp'‚ü© := h' r hr
      (h.exchange_radius hp').r_le
    r_pos := ENNReal.coe_lt_top
    hasSum := fun {y} _ =>
      let ‚ü®r', hr'‚ü© := exists_gt ‚Äñy‚Äñ‚Çä
      let ‚ü®_, hp'‚ü© := h' r' hr'.ne_bot.bot_lt
      (h.exchange_radius hp').hasSum <| mem_emetric_ball_zero_iff.mpr (ENNReal.coe_lt_coe.2 hr') }
#align has_fpower_series_on_ball.r_eq_top_of_exists HasFPowerSeriesOnBall.r_eq_top_of_exists

end Uniqueness

/-!
### Changing origin in a power series

If a function is analytic in a disk `D(x, R)`, then it is analytic in any disk contained in that
one. Indeed, one can write
$$
f (x + y + z) = \sum_{n} p_n (y + z)^n = \sum_{n, k} \binom{n}{k} p_n y^{n-k} z^k
= \sum_{k} \Bigl(\sum_{n} \binom{n}{k} p_n y^{n-k}\Bigr) z^k.
$$
The corresponding power series has thus a `k`-th coefficient equal to
$\sum_{n} \binom{n}{k} p_n y^{n-k}$. In the general case where `p‚Çô` is a multilinear map, this has
to be interpreted suitably: instead of having a binomial coefficient, one should sum over all
possible subsets `s` of `Fin n` of cardinality `k`, and attribute `z` to the indices in `s` and
`y` to the indices outside of `s`.

In this paragraph, we implement this. The new power series is called `p.changeOrigin y`. Then, we
check its convergence and the fact that its sum coincides with the original sum. The outcome of this
discussion is that the set of points where a function is analytic is open.
-/


namespace FormalMultilinearSeries

section

variable (p : FormalMultilinearSeries ùïú E F) {x y : E} {r R : ‚Ñù‚â•0}

/-- A term of `FormalMultilinearSeries.changeOriginSeries`.

Given a formal multilinear series `p` and a point `x` in its ball of convergence,
`p.changeOrigin x` is a formal multilinear series such that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense. Each term of `p.changeOrigin x`
is itself an analytic function of `x` given by the series `p.changeOriginSeries`. Each term in
`changeOriginSeries` is the sum of `changeOriginSeriesTerm`'s over all `s` of cardinality `l`.
The definition is such that `p.changeOriginSeriesTerm k l s hs (fun _ ‚Ü¶ x) (fun _ ‚Ü¶ y) =
p (k + l) (s.piecewise (fun _ ‚Ü¶ x) (fun _ ‚Ü¶ y))`
-/
def changeOriginSeriesTerm (k l : ‚Ñï) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    E[√ól]‚ÜíL[ùïú] E[√ók]‚ÜíL[ùïú] F := by
  let a := ContinuousMultilinearMap.curryFinFinset ùïú E F hs
    (by erw [Finset.card_compl, Fintype.card_fin, hs, add_tsub_cancel_right])
  exact a (p (k + l))
#align formal_multilinear_series.change_origin_series_term FormalMultilinearSeries.changeOriginSeriesTerm

theorem changeOriginSeriesTerm_apply (k l : ‚Ñï) (s : Finset (Fin (k + l))) (hs : s.card = l)
    (x y : E) :
    (p.changeOriginSeriesTerm k l s hs (fun _ => x) fun _ => y) =
      p (k + l) (s.piecewise (fun _ => x) fun _ => y) :=
  ContinuousMultilinearMap.curryFinFinset_apply_const _ _ _ _ _
#align formal_multilinear_series.change_origin_series_term_apply FormalMultilinearSeries.changeOriginSeriesTerm_apply

@[simp]
theorem norm_changeOriginSeriesTerm (k l : ‚Ñï) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    ‚Äñp.changeOriginSeriesTerm k l s hs‚Äñ = ‚Äñp (k + l)‚Äñ := by
  simp only [changeOriginSeriesTerm, LinearIsometryEquiv.norm_map]
#align formal_multilinear_series.norm_change_origin_series_term FormalMultilinearSeries.norm_changeOriginSeriesTerm

@[simp]
theorem nnnorm_changeOriginSeriesTerm (k l : ‚Ñï) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    ‚Äñp.changeOriginSeriesTerm k l s hs‚Äñ‚Çä = ‚Äñp (k + l)‚Äñ‚Çä := by
  simp only [changeOriginSeriesTerm, LinearIsometryEquiv.nnnorm_map]
#align formal_multilinear_series.nnnorm_change_origin_series_term FormalMultilinearSeries.nnnorm_changeOriginSeriesTerm

theorem nnnorm_changeOriginSeriesTerm_apply_le (k l : ‚Ñï) (s : Finset (Fin (k + l)))
    (hs : s.card = l) (x y : E) :
    ‚Äñp.changeOriginSeriesTerm k l s hs (fun _ => x) fun _ => y‚Äñ‚Çä ‚â§
      ‚Äñp (k + l)‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä ^ l * ‚Äñy‚Äñ‚Çä ^ k := by
  rw [‚Üê p.nnnorm_changeOriginSeriesTerm k l s hs, ‚Üê Fin.prod_const, ‚Üê Fin.prod_const]
  apply ContinuousMultilinearMap.le_of_opNNNorm_le
  apply ContinuousMultilinearMap.le_opNNNorm
#align formal_multilinear_series.nnnorm_change_origin_series_term_apply_le FormalMultilinearSeries.nnnorm_changeOriginSeriesTerm_apply_le

/-- The power series for `f.changeOrigin k`.

Given a formal multilinear series `p` and a point `x` in its ball of convergence,
`p.changeOrigin x` is a formal multilinear series such that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense. Its `k`-th term is the sum of
the series `p.changeOriginSeries k`. -/
def changeOriginSeries (k : ‚Ñï) : FormalMultilinearSeries ùïú E (E[√ók]‚ÜíL[ùïú] F) := fun l =>
  ‚àë s : { s : Finset (Fin (k + l)) // Finset.card s = l }, p.changeOriginSeriesTerm k l s s.2
#align formal_multilinear_series.change_origin_series FormalMultilinearSeries.changeOriginSeries

theorem nnnorm_changeOriginSeries_le_tsum (k l : ‚Ñï) :
    ‚Äñp.changeOriginSeries k l‚Äñ‚Çä ‚â§
      ‚àë' _ : { s : Finset (Fin (k + l)) // s.card = l }, ‚Äñp (k + l)‚Äñ‚Çä :=
  (nnnorm_sum_le _ (fun t => changeOriginSeriesTerm p k l (Subtype.val t) t.prop)).trans_eq <| by
    simp_rw [tsum_fintype, nnnorm_changeOriginSeriesTerm (p := p) (k := k) (l := l)]
#align formal_multilinear_series.nnnorm_change_origin_series_le_tsum FormalMultilinearSeries.nnnorm_changeOriginSeries_le_tsum

theorem nnnorm_changeOriginSeries_apply_le_tsum (k l : ‚Ñï) (x : E) :
    ‚Äñp.changeOriginSeries k l fun _ => x‚Äñ‚Çä ‚â§
      ‚àë' _ : { s : Finset (Fin (k + l)) // s.card = l }, ‚Äñp (k + l)‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä ^ l := by
  rw [NNReal.tsum_mul_right, ‚Üê Fin.prod_const]
  exact (p.changeOriginSeries k l).le_of_opNNNorm_le _ (p.nnnorm_changeOriginSeries_le_tsum _ _)
#align formal_multilinear_series.nnnorm_change_origin_series_apply_le_tsum FormalMultilinearSeries.nnnorm_changeOriginSeries_apply_le_tsum

/-- Changing the origin of a formal multilinear series `p`, so that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense.
-/
def changeOrigin (x : E) : FormalMultilinearSeries ùïú E F :=
  fun k => (p.changeOriginSeries k).sum x
#align formal_multilinear_series.change_origin FormalMultilinearSeries.changeOrigin

/-- An auxiliary equivalence useful in the proofs about
`FormalMultilinearSeries.changeOriginSeries`: the set of triples `(k, l, s)`, where `s` is a
`Finset (Fin (k + l))` of cardinality `l` is equivalent to the set of pairs `(n, s)`, where `s` is a
`Finset (Fin n)`.

The forward map sends `(k, l, s)` to `(k + l, s)` and the inverse map sends `(n, s)` to
`(n - Finset.card s, Finset.card s, s)`. The actual definition is less readable because of problems
with non-definitional equalities. -/
@[simps]
def changeOriginIndexEquiv :
    (Œ£ k l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l }) ‚âÉ Œ£ n : ‚Ñï, Finset (Fin n) where
  toFun s := ‚ü®s.1 + s.2.1, s.2.2‚ü©
  invFun s :=
    ‚ü®s.1 - s.2.card, s.2.card,
      ‚ü®s.2.map
        (finCongr <| (tsub_add_cancel_of_le <| card_finset_fin_le s.2).symm).toEmbedding,
        Finset.card_map _‚ü©‚ü©
  left_inv := by
    rintro ‚ü®k, l, ‚ü®s : Finset (Fin <| k + l), hs : s.card = l‚ü©‚ü©
    dsimp only [Subtype.coe_mk]
    -- Lean can't automatically generalize `k' = k + l - s.card`, `l' = s.card`, so we explicitly
    -- formulate the generalized goal
    suffices ‚àÄ k' l', k' = k ‚Üí l' = l ‚Üí ‚àÄ (hkl : k + l = k' + l') (hs'),
        (‚ü®k', l', ‚ü®s.map (finCongr hkl).toEmbedding, hs'‚ü©‚ü© :
          Œ£k l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l }) = ‚ü®k, l, ‚ü®s, hs‚ü©‚ü© by
      apply this <;> simp only [hs, add_tsub_cancel_right]
    rintro _ _ rfl rfl hkl hs'
    simp only [Equiv.refl_toEmbedding, finCongr_refl, Finset.map_refl, eq_self_iff_true,
      OrderIso.refl_toEquiv, and_self_iff, heq_iff_eq]
  right_inv := by
    rintro ‚ü®n, s‚ü©
    simp [tsub_add_cancel_of_le (card_finset_fin_le s), finCongr_eq_equivCast]
#align formal_multilinear_series.change_origin_index_equiv FormalMultilinearSeries.changeOriginIndexEquiv

lemma changeOriginSeriesTerm_changeOriginIndexEquiv_symm (n t) :
    let s := changeOriginIndexEquiv.symm ‚ü®n, t‚ü©
    p.changeOriginSeriesTerm s.1 s.2.1 s.2.2 s.2.2.2 (fun _ ‚Ü¶ x) (fun _ ‚Ü¶ y) =
    p n (t.piecewise (fun _ ‚Ü¶ x) fun _ ‚Ü¶ y) := by
  have : ‚àÄ (m) (hm : n = m), p n (t.piecewise (fun _ ‚Ü¶ x) fun _ ‚Ü¶ y) =
      p m ((t.map (finCongr hm).toEmbedding).piecewise (fun _ ‚Ü¶ x) fun _ ‚Ü¶ y) := by
    rintro m rfl
    simp (config := { unfoldPartialApp := true }) [Finset.piecewise]
  simp_rw [changeOriginSeriesTerm_apply, eq_comm]; apply this

theorem changeOriginSeries_summable_aux‚ÇÅ {r r' : ‚Ñù‚â•0} (hr : (r + r' : ‚Ñù‚â•0‚àû) < p.radius) :
    Summable fun s : Œ£k l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l } =>
      ‚Äñp (s.1 + s.2.1)‚Äñ‚Çä * r ^ s.2.1 * r' ^ s.1 := by
  rw [‚Üê changeOriginIndexEquiv.symm.summable_iff]
  dsimp only [Function.comp_def, changeOriginIndexEquiv_symm_apply_fst,
    changeOriginIndexEquiv_symm_apply_snd_fst]
  have : ‚àÄ n : ‚Ñï,
      HasSum (fun s : Finset (Fin n) => ‚Äñp (n - s.card + s.card)‚Äñ‚Çä * r ^ s.card * r' ^ (n - s.card))
        (‚Äñp n‚Äñ‚Çä * (r + r') ^ n) := by
    intro n
    -- TODO: why `simp only [tsub_add_cancel_of_le (card_finset_fin_le _)]` fails?
    convert_to HasSum (fun s : Finset (Fin n) => ‚Äñp n‚Äñ‚Çä * (r ^ s.card * r' ^ (n - s.card))) _
    ¬∑ ext1 s
      rw [tsub_add_cancel_of_le (card_finset_fin_le _), mul_assoc]
    rw [‚Üê Fin.sum_pow_mul_eq_add_pow]
    exact (hasSum_fintype _).mul_left _
  refine NNReal.summable_sigma.2 ‚ü®fun n => (this n).summable, ?_‚ü©
  simp only [(this _).tsum_eq]
  exact p.summable_nnnorm_mul_pow hr
#align formal_multilinear_series.change_origin_series_summable_aux‚ÇÅ FormalMultilinearSeries.changeOriginSeries_summable_aux‚ÇÅ

theorem changeOriginSeries_summable_aux‚ÇÇ (hr : (r : ‚Ñù‚â•0‚àû) < p.radius) (k : ‚Ñï) :
    Summable fun s : Œ£l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l } =>
      ‚Äñp (k + s.1)‚Äñ‚Çä * r ^ s.1 := by
  rcases ENNReal.lt_iff_exists_add_pos_lt.1 hr with ‚ü®r', h0, hr'‚ü©
  simpa only [mul_inv_cancel_right‚ÇÄ (pow_pos h0 _).ne'] using
    ((NNReal.summable_sigma.1 (p.changeOriginSeries_summable_aux‚ÇÅ hr')).1 k).mul_right (r' ^ k)‚Åª¬π
#align formal_multilinear_series.change_origin_series_summable_aux‚ÇÇ FormalMultilinearSeries.changeOriginSeries_summable_aux‚ÇÇ

theorem changeOriginSeries_summable_aux‚ÇÉ {r : ‚Ñù‚â•0} (hr : ‚Üër < p.radius) (k : ‚Ñï) :
    Summable fun l : ‚Ñï => ‚Äñp.changeOriginSeries k l‚Äñ‚Çä * r ^ l := by
  refine NNReal.summable_of_le
    (fun n => ?_) (NNReal.summable_sigma.1 <| p.changeOriginSeries_summable_aux‚ÇÇ hr k).2
  simp only [NNReal.tsum_mul_right]
  exact mul_le_mul' (p.nnnorm_changeOriginSeries_le_tsum _ _) le_rfl
#align formal_multilinear_series.change_origin_series_summable_aux‚ÇÉ FormalMultilinearSeries.changeOriginSeries_summable_aux‚ÇÉ

theorem le_changeOriginSeries_radius (k : ‚Ñï) : p.radius ‚â§ (p.changeOriginSeries k).radius :=
  ENNReal.le_of_forall_nnreal_lt fun _r hr =>
    le_radius_of_summable_nnnorm _ (p.changeOriginSeries_summable_aux‚ÇÉ hr k)
#align formal_multilinear_series.le_change_origin_series_radius FormalMultilinearSeries.le_changeOriginSeries_radius

theorem nnnorm_changeOrigin_le (k : ‚Ñï) (h : (‚Äñx‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < p.radius) :
    ‚Äñp.changeOrigin x k‚Äñ‚Çä ‚â§
      ‚àë' s : Œ£l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l }, ‚Äñp (k + s.1)‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä ^ s.1 := by
  refine tsum_of_nnnorm_bounded ?_ fun l => p.nnnorm_changeOriginSeries_apply_le_tsum k l x
  have := p.changeOriginSeries_summable_aux‚ÇÇ h k
  refine HasSum.sigma this.hasSum fun l => ?_
  exact ((NNReal.summable_sigma.1 this).1 l).hasSum
#align formal_multilinear_series.nnnorm_change_origin_le FormalMultilinearSeries.nnnorm_changeOrigin_le

/-- The radius of convergence of `p.changeOrigin x` is at least `p.radius - ‚Äñx‚Äñ`. In other words,
`p.changeOrigin x` is well defined on the largest ball contained in the original ball of
convergence. -/
theorem changeOrigin_radius : p.radius - ‚Äñx‚Äñ‚Çä ‚â§ (p.changeOrigin x).radius := by
  refine ENNReal.le_of_forall_pos_nnreal_lt fun r _h0 hr => ?_
  rw [lt_tsub_iff_right, add_comm] at hr
  have hr' : (‚Äñx‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < p.radius := (le_add_right le_rfl).trans_lt hr
  apply le_radius_of_summable_nnnorm
  have : ‚àÄ k : ‚Ñï,
      ‚Äñp.changeOrigin x k‚Äñ‚Çä * r ^ k ‚â§
        (‚àë' s : Œ£l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l }, ‚Äñp (k + s.1)‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä ^ s.1) *
          r ^ k :=
    fun k => mul_le_mul_right' (p.nnnorm_changeOrigin_le k hr') (r ^ k)
  refine NNReal.summable_of_le this ?_
  simpa only [‚Üê NNReal.tsum_mul_right] using
    (NNReal.summable_sigma.1 (p.changeOriginSeries_summable_aux‚ÇÅ hr)).2
#align formal_multilinear_series.change_origin_radius FormalMultilinearSeries.changeOrigin_radius

/-- `derivSeries p` is a power series for `fderiv ùïú f` if `p` is a power series for `f`,
see `HasFPowerSeriesOnBall.fderiv`. -/
def derivSeries : FormalMultilinearSeries ùïú E (E ‚ÜíL[ùïú] F) :=
  (continuousMultilinearCurryFin1 ùïú E F : (E[√ó1]‚ÜíL[ùïú] F) ‚ÜíL[ùïú] E ‚ÜíL[ùïú] F)
    |>.compFormalMultilinearSeries (p.changeOriginSeries 1)

end

-- From this point on, assume that the space is complete, to make sure that series that converge
-- in norm also converge in `F`.
variable [CompleteSpace F] (p : FormalMultilinearSeries ùïú E F) {x y : E} {r R : ‚Ñù‚â•0}

theorem hasFPowerSeriesOnBall_changeOrigin (k : ‚Ñï) (hr : 0 < p.radius) :
    HasFPowerSeriesOnBall (fun x => p.changeOrigin x k) (p.changeOriginSeries k) 0 p.radius :=
  have := p.le_changeOriginSeries_radius k
  ((p.changeOriginSeries k).hasFPowerSeriesOnBall (hr.trans_le this)).mono hr this
#align formal_multilinear_series.has_fpower_series_on_ball_change_origin FormalMultilinearSeries.hasFPowerSeriesOnBall_changeOrigin

/-- Summing the series `p.changeOrigin x` at a point `y` gives back `p (x + y)`. -/
theorem changeOrigin_eval (h : (‚Äñx‚Äñ‚Çä + ‚Äñy‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < p.radius) :
    (p.changeOrigin x).sum y = p.sum (x + y) := by
  have radius_pos : 0 < p.radius := lt_of_le_of_lt (zero_le _) h
  have x_mem_ball : x ‚àà EMetric.ball (0 : E) p.radius :=
    mem_emetric_ball_zero_iff.2 ((le_add_right le_rfl).trans_lt h)
  have y_mem_ball : y ‚àà EMetric.ball (0 : E) (p.changeOrigin x).radius := by
    refine mem_emetric_ball_zero_iff.2 (lt_of_lt_of_le ?_ p.changeOrigin_radius)
    rwa [lt_tsub_iff_right, add_comm]
  have x_add_y_mem_ball : x + y ‚àà EMetric.ball (0 : E) p.radius := by
    refine mem_emetric_ball_zero_iff.2 (lt_of_le_of_lt ?_ h)
    exact mod_cast nnnorm_add_le x y
  set f : (Œ£ k l : ‚Ñï, { s : Finset (Fin (k + l)) // s.card = l }) ‚Üí F := fun s =>
    p.changeOriginSeriesTerm s.1 s.2.1 s.2.2 s.2.2.2 (fun _ => x) fun _ => y
  have hsf : Summable f := by
    refine .of_nnnorm_bounded _ (p.changeOriginSeries_summable_aux‚ÇÅ h) ?_
    rintro ‚ü®k, l, s, hs‚ü©
    dsimp only [Subtype.coe_mk]
    exact p.nnnorm_changeOriginSeriesTerm_apply_le _ _ _ _ _ _
  have hf : HasSum f ((p.changeOrigin x).sum y) := by
    refine HasSum.sigma_of_hasSum ((p.changeOrigin x).summable y_mem_ball).hasSum (fun k => ?_) hsf
    ¬∑ dsimp only [f]
      refine ContinuousMultilinearMap.hasSum_eval ?_ _
      have := (p.hasFPowerSeriesOnBall_changeOrigin k radius_pos).hasSum x_mem_ball
      rw [zero_add] at this
      refine HasSum.sigma_of_hasSum this (fun l => ?_) ?_
      ¬∑ simp only [changeOriginSeries, ContinuousMultilinearMap.sum_apply]
        apply hasSum_fintype
      ¬∑ refine .of_nnnorm_bounded _
          (p.changeOriginSeries_summable_aux‚ÇÇ (mem_emetric_ball_zero_iff.1 x_mem_ball) k)
            fun s => ?_
        refine (ContinuousMultilinearMap.le_opNNNorm _ _).trans_eq ?_
        simp
  refine hf.unique (changeOriginIndexEquiv.symm.hasSum_iff.1 ?_)
  refine HasSum.sigma_of_hasSum
    (p.hasSum x_add_y_mem_ball) (fun n => ?_) (changeOriginIndexEquiv.symm.summable_iff.2 hsf)
  erw [(p n).map_add_univ (fun _ => x) fun _ => y]
  simp_rw [‚Üê changeOriginSeriesTerm_changeOriginIndexEquiv_symm]
  exact hasSum_fintype (fun c => f (changeOriginIndexEquiv.symm ‚ü®n, c‚ü©))
#align formal_multilinear_series.change_origin_eval FormalMultilinearSeries.changeOrigin_eval

/-- Power series terms are analytic as we vary the origin -/
theorem analyticAt_changeOrigin (p : FormalMultilinearSeries ùïú E F) (rp : p.radius > 0) (n : ‚Ñï) :
    AnalyticAt ùïú (fun x ‚Ü¶ p.changeOrigin x n) 0 :=
  (FormalMultilinearSeries.hasFPowerSeriesOnBall_changeOrigin p n rp).analyticAt

end FormalMultilinearSeries

section

variable [CompleteSpace F] {f : E ‚Üí F} {p : FormalMultilinearSeries ùïú E F} {x y : E} {r : ‚Ñù‚â•0‚àû}

/-- If a function admits a power series expansion `p` on a ball `B (x, r)`, then it also admits a
power series on any subball of this ball (even with a different center), given by `p.changeOrigin`.
-/
theorem HasFPowerSeriesOnBall.changeOrigin (hf : HasFPowerSeriesOnBall f p x r)
    (h : (‚Äñy‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < r) : HasFPowerSeriesOnBall f (p.changeOrigin y) (x + y) (r - ‚Äñy‚Äñ‚Çä) :=
  { r_le := by
      apply le_trans _ p.changeOrigin_radius
      exact tsub_le_tsub hf.r_le le_rfl
    r_pos := by simp [h]
    hasSum := fun {z} hz => by
      have : f (x + y + z) =
          FormalMultilinearSeries.sum (FormalMultilinearSeries.changeOrigin p y) z := by
        rw [mem_emetric_ball_zero_iff, lt_tsub_iff_right, add_comm] at hz
        rw [p.changeOrigin_eval (hz.trans_le hf.r_le), add_assoc, hf.sum]
        refine mem_emetric_ball_zero_iff.2 (lt_of_le_of_lt ?_ hz)
        exact mod_cast nnnorm_add_le y z
      rw [this]
      apply (p.changeOrigin y).hasSum
      refine EMetric.ball_subset_ball (le_trans ?_ p.changeOrigin_radius) hz
      exact tsub_le_tsub hf.r_le le_rfl }
#align has_fpower_series_on_ball.change_origin HasFPowerSeriesOnBall.changeOrigin

/-- If a function admits a power series expansion `p` on an open ball `B (x, r)`, then
it is analytic at every point of this ball. -/
theorem HasFPowerSeriesOnBall.analyticAt_of_mem (hf : HasFPowerSeriesOnBall f p x r)
    (h : y ‚àà EMetric.ball x r) : AnalyticAt ùïú f y := by
  have : (‚Äñy - x‚Äñ‚Çä : ‚Ñù‚â•0‚àû) < r := by simpa [edist_eq_coe_nnnorm_sub] using h
  have := hf.changeOrigin this
  rw [add_sub_cancel] at this
  exact this.analyticAt
#align has_fpower_series_on_ball.analytic_at_of_mem HasFPowerSeriesOnBall.analyticAt_of_mem

theorem HasFPowerSeriesOnBall.analyticOn (hf : HasFPowerSeriesOnBall f p x r) :
    AnalyticOn ùïú f (EMetric.ball x r) :=
  fun _y hy => hf.analyticAt_of_mem hy
#align has_fpower_series_on_ball.analytic_on HasFPowerSeriesOnBall.analyticOn

variable (ùïú f)

/-- For any function `f` from a normed vector space to a Banach space, the set of points `x` such
that `f` is analytic at `x` is open. -/
theorem isOpen_analyticAt : IsOpen { x | AnalyticAt ùïú f x } := by
  rw [isOpen_iff_mem_nhds]
  rintro x ‚ü®p, r, hr‚ü©
  exact mem_of_superset (EMetric.ball_mem_nhds _ hr.r_pos) fun y hy => hr.analyticAt_of_mem hy
#align is_open_analytic_at isOpen_analyticAt

variable {ùïú}

theorem AnalyticAt.eventually_analyticAt {f : E ‚Üí F} {x : E} (h : AnalyticAt ùïú f x) :
    ‚àÄ·∂† y in ùìù x, AnalyticAt ùïú f y :=
(isOpen_analyticAt ùïú f).mem_nhds h

theorem AnalyticAt.exists_mem_nhds_analyticOn {f : E ‚Üí F} {x : E} (h : AnalyticAt ùïú f x) :
    ‚àÉ s ‚àà ùìù x, AnalyticOn ùïú f s :=
h.eventually_analyticAt.exists_mem

/-- If we're analytic at a point, we're analytic in a nonempty ball -/
theorem AnalyticAt.exists_ball_analyticOn {f : E ‚Üí F} {x : E} (h : AnalyticAt ùïú f x) :
    ‚àÉ r : ‚Ñù, 0 < r ‚àß AnalyticOn ùïú f (Metric.ball x r) :=
  Metric.isOpen_iff.mp (isOpen_analyticAt _ _) _ h

end

section

open FormalMultilinearSeries

variable {p : FormalMultilinearSeries ùïú ùïú E} {f : ùïú ‚Üí E} {z‚ÇÄ : ùïú}

/-- A function `f : ùïú ‚Üí E` has `p` as power series expansion at a point `z‚ÇÄ` iff it is the sum of
`p` in a neighborhood of `z‚ÇÄ`. This makes some proofs easier by hiding the fact that
`HasFPowerSeriesAt` depends on `p.radius`. -/
theorem hasFPowerSeriesAt_iff :
    HasFPowerSeriesAt f p z‚ÇÄ ‚Üî ‚àÄ·∂† z in ùìù 0, HasSum (fun n => z ^ n ‚Ä¢ p.coeff n) (f (z‚ÇÄ + z)) := by
  refine ‚ü®fun ‚ü®r, _, r_pos, h‚ü© =>
    eventually_of_mem (EMetric.ball_mem_nhds 0 r_pos) fun _ => by simpa using h, ?_‚ü©
  simp only [Metric.eventually_nhds_iff]
  rintro ‚ü®r, r_pos, h‚ü©
  refine ‚ü®p.radius ‚äì r.toNNReal, by simp, ?_, ?_‚ü©
  ¬∑ simp only [r_pos.lt, lt_inf_iff, ENNReal.coe_pos, Real.toNNReal_pos, and_true_iff]
    obtain ‚ü®z, z_pos, le_z‚ü© := NormedField.exists_norm_lt ùïú r_pos.lt
    have : (‚Äñz‚Äñ‚Çä : ENNReal) ‚â§ p.radius := by
      simp only [dist_zero_right] at h
      apply FormalMultilinearSeries.le_radius_of_tendsto
      convert tendsto_norm.comp (h le_z).summable.tendsto_atTop_zero
      simp [norm_smul, mul_comm]
    refine lt_of_lt_of_le ?_ this
    simp only [ENNReal.coe_pos]
    exact zero_lt_iff.mpr (nnnorm_ne_zero_iff.mpr (norm_pos_iff.mp z_pos))
  ¬∑ simp only [EMetric.mem_ball, lt_inf_iff, edist_lt_coe, apply_eq_pow_smul_coeff, and_imp,
      dist_zero_right] at h ‚ä¢
    refine fun {y} _ hyr => h ?_
    simpa [nndist_eq_nnnorm, Real.lt_toNNReal_iff_coe_lt] using hyr
#align has_fpower_series_at_iff hasFPowerSeriesAt_iff

theorem hasFPowerSeriesAt_iff' :
    HasFPowerSeriesAt f p z‚ÇÄ ‚Üî ‚àÄ·∂† z in ùìù z‚ÇÄ, HasSum (fun n => (z - z‚ÇÄ) ^ n ‚Ä¢ p.coeff n) (f z) := by
  rw [‚Üê map_add_left_nhds_zero, eventually_map, hasFPowerSeriesAt_iff]
  simp_rw [add_sub_cancel_left]
#align has_fpower_series_at_iff' hasFPowerSeriesAt_iff'

end
