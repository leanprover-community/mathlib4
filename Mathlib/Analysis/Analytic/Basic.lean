/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
import Mathlib.Algebra.Star.Order
import Mathlib.Analysis.Calculus.FormalMultilinearSeries
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Logic.Equiv.Fin
import Mathlib.Topology.Algebra.InfiniteSum.Module

/-!
# Analytic functions

A function is analytic in one dimension around `0` if it can be written as a converging power series
`Î£ pâ‚™ zâ¿`. This definition can be extended to any dimension (even in infinite dimension) by
requiring that `pâ‚™` is a continuous `n`-multilinear map. In general, `pâ‚™` is not unique (in two
dimensions, taking `pâ‚‚ (x, y) (x', y') = x y'` or `y x'` gives the same map when applied to a
vector `(x, y) (x, y)`). A way to guarantee uniqueness is to take a symmetric `pâ‚™`, but this is not
always possible in nonzero characteristic (in characteristic 2, the previous example has no
symmetric representative). Therefore, we do not insist on symmetry or uniqueness in the definition,
and we only require the existence of a converging series.

The general framework is important to say that the exponential map on bounded operators on a Banach
space is analytic, as well as the inverse on invertible operators.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : â„•`.

* `p.radius`: the largest `r : â„â‰¥0âˆ` such that `â€–p nâ€– * r^n` grows subexponentially.
* `p.le_radius_of_bound`, `p.le_radius_of_bound_nnreal`, `p.le_radius_of_isBigO`: if `â€–p nâ€– * r ^ n`
  is bounded above, then `r â‰¤ p.radius`;
* `p.isLittleO_of_lt_radius`, `p.norm_mul_pow_le_mul_pow_of_lt_radius`,
  `p.isLittleO_one_of_lt_radius`,
  `p.norm_mul_pow_le_of_lt_radius`, `p.nnnorm_mul_pow_le_of_lt_radius`: if `r < p.radius`, then
  `â€–p nâ€– * r ^ n` tends to zero exponentially;
* `p.lt_radius_of_isBigO`: if `r â‰  0` and `â€–p nâ€– * r ^ n = O(a ^ n)` for some `-1 < a < 1`, then
  `r < p.radius`;
* `p.partialSum n x`: the sum `âˆ‘_{i = 0}^{n-1} páµ¢ xâ±`.
* `p.sum x`: the sum `âˆ‘'_{i = 0}^{âˆ} páµ¢ xâ±`.

Additionally, let `f` be a function from `E` to `F`.

* `HasFPowerSeriesOnBall f p x r`: on the ball of center `x` with radius `r`,
  `f (x + y) = âˆ‘'_n pâ‚™ yâ¿`.
* `HasFPowerSeriesAt f p x`: on some ball of center `x` with positive radius, holds
  `HasFPowerSeriesOnBall f p x r`.
* `AnalyticAt ğ•œ f x`: there exists a power series `p` such that holds `HasFPowerSeriesAt f p x`.
* `AnalyticOn ğ•œ f s`: the function `f` is analytic at every point of `s`.

We develop the basic properties of these notions, notably:
* If a function admits a power series, it is continuous (see
  `HasFPowerSeriesOnBall.continuousOn` and `HasFPowerSeriesAt.continuousAt` and
  `AnalyticAt.continuousAt`).
* In a complete space, the sum of a formal power series with positive radius is well defined on the
  disk of convergence, see `FormalMultilinearSeries.hasFPowerSeriesOnBall`.
* If a function admits a power series in a ball, then it is analytic at any point `y` of this ball,
  and the power series there can be expressed in terms of the initial power series `p` as
  `p.changeOrigin y`. See `HasFPowerSeriesOnBall.changeOrigin`. It follows in particular that
  the set of points at which a given function is analytic is open, see `isOpen_analyticAt`.

## Implementation details

We only introduce the radius of convergence of a power series, as `p.radius`.
For a power series in finitely many dimensions, there is a finer (directional, coordinate-dependent)
notion, describing the polydisk of convergence. This notion is more specific, and not necessary to
build the general theory. We do not define it here.
-/

noncomputable section

variable {ğ•œ E F G : Type*}

open scoped Classical
open Topology NNReal Filter ENNReal

open Set Filter Asymptotics

namespace FormalMultilinearSeries

variable [Ring ğ•œ] [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F]
variable [TopologicalSpace E] [TopologicalSpace F]
variable [TopologicalAddGroup E] [TopologicalAddGroup F]
variable [ContinuousConstSMul ğ•œ E] [ContinuousConstSMul ğ•œ F]

/-- Given a formal multilinear series `p` and a vector `x`, then `p.sum x` is the sum `Î£ pâ‚™ xâ¿`. A
priori, it only behaves well when `â€–xâ€– < p.radius`. -/
protected def sum (p : FormalMultilinearSeries ğ•œ E F) (x : E) : F :=
  âˆ‘' n : â„•, p n fun _ => x

/-- Given a formal multilinear series `p` and a vector `x`, then `p.partialSum n x` is the sum
`Î£ pâ‚– xáµ` for `k âˆˆ {0,..., n-1}`. -/
def partialSum (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) (x : E) : F :=
  âˆ‘ k âˆˆ Finset.range n, p k fun _ : Fin k => x

/-- The partial sums of a formal multilinear series are continuous. -/
theorem partialSum_continuous (p : FormalMultilinearSeries ğ•œ E F) (n : â„•) :
    Continuous (p.partialSum n) := by
  unfold partialSum -- Porting note: added
  continuity

end FormalMultilinearSeries

/-! ### The radius of a formal multilinear series -/

variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

namespace FormalMultilinearSeries

variable (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}

/-- The radius of a formal multilinear series is the largest `r` such that the sum `Î£ â€–pâ‚™â€– â€–yâ€–â¿`
converges for all `â€–yâ€– < r`. This implies that `Î£ pâ‚™ yâ¿` converges for all `â€–yâ€– < r`, but these
definitions are *not* equivalent in general. -/
def radius (p : FormalMultilinearSeries ğ•œ E F) : â„â‰¥0âˆ :=
  â¨† (r : â„â‰¥0) (C : â„) (_ : âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C), (r : â„â‰¥0âˆ)

/-- If `â€–pâ‚™â€– râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound (C : â„) {r : â„â‰¥0} (h : âˆ€ n : â„•, â€–p nâ€– * (r : â„) ^ n â‰¤ C) :
    (r : â„â‰¥0âˆ) â‰¤ p.radius :=
  le_iSup_of_le r <| le_iSup_of_le C <| le_iSup (fun _ => (r : â„â‰¥0âˆ)) h

/-- If `â€–pâ‚™â€– râ¿` is bounded in `n`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_bound_nnreal (C : â„â‰¥0) {r : â„â‰¥0} (h : âˆ€ n : â„•, â€–p nâ€–â‚Š * r ^ n â‰¤ C) :
    (r : â„â‰¥0âˆ) â‰¤ p.radius :=
  p.le_radius_of_bound C fun n => mod_cast h n

/-- If `â€–pâ‚™â€– râ¿ = O(1)`, as `n â†’ âˆ`, then the radius of `p` is at least `r`. -/
theorem le_radius_of_isBigO (h : (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] fun _ => (1 : â„)) :
    â†‘r â‰¤ p.radius :=
  Exists.elim (isBigO_one_nat_atTop_iff.1 h) fun C hC =>
    p.le_radius_of_bound C fun n => (le_abs_self _).trans (hC n)

theorem le_radius_of_eventually_le (C) (h : âˆ€á¶  n in atTop, â€–p nâ€– * (r : â„) ^ n â‰¤ C) :
    â†‘r â‰¤ p.radius :=
  p.le_radius_of_isBigO <| IsBigO.of_bound C <| h.mono fun n hn => by simpa

theorem le_radius_of_summable_nnnorm (h : Summable fun n => â€–p nâ€–â‚Š * r ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_bound_nnreal (âˆ‘' n, â€–p nâ€–â‚Š * r ^ n) fun _ => le_tsum' h _

theorem le_radius_of_summable (h : Summable fun n => â€–p nâ€– * (r : â„) ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_summable_nnnorm <| by
    simp only [â† coe_nnnorm] at h
    exact mod_cast h

theorem radius_eq_top_of_forall_nnreal_isBigO
    (h : âˆ€ r : â„â‰¥0, (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] fun _ => (1 : â„)) : p.radius = âˆ :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_isBigO (h r)

theorem radius_eq_top_of_eventually_eq_zero (h : âˆ€á¶  n in atTop, p n = 0) : p.radius = âˆ :=
  p.radius_eq_top_of_forall_nnreal_isBigO fun r =>
    (isBigO_zero _ _).congr' (h.mono fun n hn => by simp [hn]) EventuallyEq.rfl

theorem radius_eq_top_of_forall_image_add_eq_zero (n : â„•) (hn : âˆ€ m, p (m + n) = 0) :
    p.radius = âˆ :=
  p.radius_eq_top_of_eventually_eq_zero <|
    mem_atTop_sets.2 âŸ¨n, fun _ hk => tsub_add_cancel_of_le hk â–¸ hn _âŸ©

@[simp]
theorem constFormalMultilinearSeries_radius {v : F} :
    (constFormalMultilinearSeries ğ•œ E v).radius = âŠ¤ :=
  (constFormalMultilinearSeries ğ•œ E v).radius_eq_top_of_forall_image_add_eq_zero 1
    (by simp [constFormalMultilinearSeries])

/-- `0` has infinite radius of convergence -/
@[simp] lemma zero_radius : (0 : FormalMultilinearSeries ğ•œ E F).radius = âˆ := by
  rw [â† constFormalMultilinearSeries_zero]
  exact constFormalMultilinearSeries_radius

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` tends to zero exponentially:
for some `0 < a < 1`, `â€–p nâ€– râ¿ = o(aâ¿)`. -/
theorem isLittleO_of_lt_radius (h : â†‘r < p.radius) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1, (fun n => â€–p nâ€– * (r : â„) ^ n) =o[atTop] (a ^ Â·) := by
  have := (TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 4
  rw [this]
  -- Porting note: was
  -- rw [(TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 4]
  simp only [radius, lt_iSup_iff] at h
  rcases h with âŸ¨t, C, hC, rtâŸ©
  rw [ENNReal.coe_lt_coe, â† NNReal.coe_lt_coe] at rt
  have : 0 < (t : â„) := r.coe_nonneg.trans_lt rt
  rw [â† div_lt_one this] at rt
  refine âŸ¨_, rt, C, Or.inr zero_lt_one, fun n => ?_âŸ©
  calc
    |â€–p nâ€– * (r : â„) ^ n| = â€–p nâ€– * (t : â„) ^ n * (r / t : â„) ^ n := by
      field_simp [mul_right_comm, abs_mul]
    _ â‰¤ C * (r / t : â„) ^ n := by gcongr; apply hC

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿ = o(1)`. -/
theorem isLittleO_one_of_lt_radius (h : â†‘r < p.radius) :
    (fun n => â€–p nâ€– * (r : â„) ^ n) =o[atTop] (fun _ => 1 : â„• â†’ â„) :=
  let âŸ¨_, ha, hpâŸ© := p.isLittleO_of_lt_radius h
  hp.trans <| (isLittleO_pow_pow_of_lt_left ha.1.le ha.2).congr (fun _ => rfl) one_pow

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` tends to zero exponentially:
for some `0 < a < 1` and `C > 0`, `â€–p nâ€– * r ^ n â‰¤ C * a ^ n`. -/
theorem norm_mul_pow_le_mul_pow_of_lt_radius (h : â†‘r < p.radius) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C * a ^ n := by
  -- Porting note: moved out of `rcases`
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 1 5).mp
    (p.isLittleO_of_lt_radius h)
  rcases this with âŸ¨a, ha, C, hC, HâŸ©
  exact âŸ¨a, ha, C, hC, fun n => (le_abs_self _).trans (H n)âŸ©

/-- If `r â‰  0` and `â€–pâ‚™â€– râ¿ = O(aâ¿)` for some `-1 < a < 1`, then `r < p.radius`. -/
theorem lt_radius_of_isBigO (hâ‚€ : r â‰  0) {a : â„} (ha : a âˆˆ Ioo (-1 : â„) 1)
    (hp : (fun n => â€–p nâ€– * (r : â„) ^ n) =O[atTop] (a ^ Â·)) : â†‘r < p.radius := by
  -- Porting note: moved out of `rcases`
  have := ((TFAE_exists_lt_isLittleO_pow (fun n => â€–p nâ€– * (r : â„) ^ n) 1).out 2 5)
  rcases this.mp âŸ¨a, ha, hpâŸ© with âŸ¨a, ha, C, hC, hpâŸ©
  rw [â† pos_iff_ne_zero, â† NNReal.coe_pos] at hâ‚€
  lift a to â„â‰¥0 using ha.1.le
  have : (r : â„) < r / a := by
    simpa only [div_one] using (div_lt_div_left hâ‚€ zero_lt_one ha.1).2 ha.2
  norm_cast at this
  rw [â† ENNReal.coe_lt_coe] at this
  refine this.trans_le (p.le_radius_of_bound C fun n => ?_)
  rw [NNReal.coe_div, div_pow, â† mul_div_assoc, div_le_iff (pow_pos ha.1 n)]
  exact (le_abs_self _).trans (hp n)

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem norm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€– * (r : â„) ^ n â‰¤ C :=
  let âŸ¨_, ha, C, hC, hâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  âŸ¨C, hC, fun n => (h n).trans <| mul_le_of_le_one_right hC.lt.le (pow_le_one _ ha.1.le ha.2.le)âŸ©

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem norm_le_div_pow_of_pos_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h0 : 0 < r) (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€– â‰¤ C / (r : â„) ^ n :=
  let âŸ¨C, hC, hpâŸ© := p.norm_mul_pow_le_of_lt_radius h
  âŸ¨C, hC, fun n => Iff.mpr (le_div_iff (pow_pos h0 _)) (hp n)âŸ©

/-- For `r` strictly smaller than the radius of `p`, then `â€–pâ‚™â€– râ¿` is bounded. -/
theorem nnnorm_mul_pow_le_of_lt_radius (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0}
    (h : (r : â„â‰¥0âˆ) < p.radius) : âˆƒ C > 0, âˆ€ n, â€–p nâ€–â‚Š * r ^ n â‰¤ C :=
  let âŸ¨C, hC, hpâŸ© := p.norm_mul_pow_le_of_lt_radius h
  âŸ¨âŸ¨C, hC.lt.leâŸ©, hC, mod_cast hpâŸ©

theorem le_radius_of_tendsto (p : FormalMultilinearSeries ğ•œ E F) {l : â„}
    (h : Tendsto (fun n => â€–p nâ€– * (r : â„) ^ n) atTop (ğ“ l)) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_isBigO (h.isBigO_one _)

theorem le_radius_of_summable_norm (p : FormalMultilinearSeries ğ•œ E F)
    (hs : Summable fun n => â€–p nâ€– * (r : â„) ^ n) : â†‘r â‰¤ p.radius :=
  p.le_radius_of_tendsto hs.tendsto_atTop_zero

theorem not_summable_norm_of_radius_lt_nnnorm (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (h : p.radius < â€–xâ€–â‚Š) : Â¬Summable fun n => â€–p nâ€– * â€–xâ€– ^ n :=
  fun hs => not_le_of_lt h (p.le_radius_of_summable_norm hs)

theorem summable_norm_mul_pow (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0} (h : â†‘r < p.radius) :
    Summable fun n : â„• => â€–p nâ€– * (r : â„) ^ n := by
  obtain âŸ¨a, ha : a âˆˆ Ioo (0 : â„) 1, C, - : 0 < C, hpâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius h
  exact .of_nonneg_of_le (fun n => mul_nonneg (norm_nonneg _) (pow_nonneg r.coe_nonneg _))
    hp ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _)

theorem summable_norm_apply (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : Summable fun n : â„• => â€–p n fun _ => xâ€– := by
  rw [mem_emetric_ball_zero_iff] at hx
  refine .of_nonneg_of_le
    (fun _ â†¦ norm_nonneg _) (fun n â†¦ ((p n).le_opNorm _).trans_eq ?_) (p.summable_norm_mul_pow hx)
  simp

theorem summable_nnnorm_mul_pow (p : FormalMultilinearSeries ğ•œ E F) {r : â„â‰¥0} (h : â†‘r < p.radius) :
    Summable fun n : â„• => â€–p nâ€–â‚Š * r ^ n := by
  rw [â† NNReal.summable_coe]
  push_cast
  exact p.summable_norm_mul_pow h

protected theorem summable [CompleteSpace F] (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : Summable fun n : â„• => p n fun _ => x :=
  (p.summable_norm_apply hx).of_norm

theorem radius_eq_top_of_summable_norm (p : FormalMultilinearSeries ğ•œ E F)
    (hs : âˆ€ r : â„â‰¥0, Summable fun n => â€–p nâ€– * (r : â„) ^ n) : p.radius = âˆ :=
  ENNReal.eq_top_of_forall_nnreal_le fun r => p.le_radius_of_summable_norm (hs r)

theorem radius_eq_top_iff_summable_norm (p : FormalMultilinearSeries ğ•œ E F) :
    p.radius = âˆ â†” âˆ€ r : â„â‰¥0, Summable fun n => â€–p nâ€– * (r : â„) ^ n := by
  constructor
  Â· intro h r
    obtain âŸ¨a, ha : a âˆˆ Ioo (0 : â„) 1, C, - : 0 < C, hpâŸ© := p.norm_mul_pow_le_mul_pow_of_lt_radius
      (show (r : â„â‰¥0âˆ) < p.radius from h.symm â–¸ ENNReal.coe_lt_top)
    refine .of_norm_bounded
      (fun n â†¦ (C : â„) * a ^ n) ((summable_geometric_of_lt_one ha.1.le ha.2).mul_left _) fun n â†¦ ?_
    specialize hp n
    rwa [Real.norm_of_nonneg (mul_nonneg (norm_nonneg _) (pow_nonneg r.coe_nonneg n))]
  Â· exact p.radius_eq_top_of_summable_norm

/-- If the radius of `p` is positive, then `â€–pâ‚™â€–` grows at most geometrically. -/
theorem le_mul_pow_of_radius_pos (p : FormalMultilinearSeries ğ•œ E F) (h : 0 < p.radius) :
    âˆƒ (C r : _) (hC : 0 < C) (_ : 0 < r), âˆ€ n, â€–p nâ€– â‰¤ C * r ^ n := by
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 h with âŸ¨r, r0, rltâŸ©
  have rpos : 0 < (r : â„) := by simp [ENNReal.coe_pos.1 r0]
  rcases norm_le_div_pow_of_pos_of_lt_radius p rpos rlt with âŸ¨C, Cpos, hCpâŸ©
  refine âŸ¨C, râ»Â¹, Cpos, by simp only [inv_pos, rpos], fun n => ?_âŸ©
  -- Porting note: was `convert`
  rw [inv_pow, â† div_eq_mul_inv]
  exact hCp n

/-- The radius of the sum of two formal series is at least the minimum of their two radii. -/
theorem min_radius_le_radius_add (p q : FormalMultilinearSeries ğ•œ E F) :
    min p.radius q.radius â‰¤ (p + q).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  rw [lt_min_iff] at hr
  have := ((p.isLittleO_one_of_lt_radius hr.1).add (q.isLittleO_one_of_lt_radius hr.2)).isBigO
  refine (p + q).le_radius_of_isBigO ((isBigO_of_le _ fun n => ?_).trans this)
  rw [â† add_mul, norm_mul, norm_mul, norm_norm]
  exact mul_le_mul_of_nonneg_right ((norm_add_le _ _).trans (le_abs_self _)) (norm_nonneg _)

@[simp]
theorem radius_neg (p : FormalMultilinearSeries ğ•œ E F) : (-p).radius = p.radius := by
  simp only [radius, neg_apply, norm_neg]

protected theorem hasSum [CompleteSpace F] (p : FormalMultilinearSeries ğ•œ E F) {x : E}
    (hx : x âˆˆ EMetric.ball (0 : E) p.radius) : HasSum (fun n : â„• => p n fun _ => x) (p.sum x) :=
  (p.summable hx).hasSum

theorem radius_le_radius_continuousLinearMap_comp (p : FormalMultilinearSeries ğ•œ E F)
    (f : F â†’L[ğ•œ] G) : p.radius â‰¤ (f.compFormalMultilinearSeries p).radius := by
  refine ENNReal.le_of_forall_nnreal_lt fun r hr => ?_
  apply le_radius_of_isBigO
  apply (IsBigO.trans_isLittleO _ (p.isLittleO_one_of_lt_radius hr)).isBigO
  refine IsBigO.mul (@IsBigOWith.isBigO _ _ _ _ _ â€–fâ€– _ _ _ ?_) (isBigO_refl _ _)
  refine IsBigOWith.of_bound (eventually_of_forall fun n => ?_)
  simpa only [norm_norm] using f.norm_compContinuousMultilinearMap_le (p n)

end FormalMultilinearSeries

/-! ### Expanding a function as a power series -/


section

variable {f g : E â†’ F} {p pf pg : FormalMultilinearSeries ğ•œ E F} {x : E} {r r' : â„â‰¥0âˆ}

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series on the ball of radius `r > 0` around `x` if `f (x + y) = âˆ‘' pâ‚™ yâ¿` for all `â€–yâ€– < r`.
-/
structure HasFPowerSeriesOnBall (f : E â†’ F) (p : FormalMultilinearSeries ğ•œ E F) (x : E) (r : â„â‰¥0âˆ) :
    Prop where
  r_le : r â‰¤ p.radius
  r_pos : 0 < r
  hasSum :
    âˆ€ {y}, y âˆˆ EMetric.ball (0 : E) r â†’ HasSum (fun n : â„• => p n fun _ : Fin n => y) (f (x + y))

/-- Given a function `f : E â†’ F` and a formal multilinear series `p`, we say that `f` has `p` as
a power series around `x` if `f (x + y) = âˆ‘' pâ‚™ yâ¿` for all `y` in a neighborhood of `0`. -/
def HasFPowerSeriesAt (f : E â†’ F) (p : FormalMultilinearSeries ğ•œ E F) (x : E) :=
  âˆƒ r, HasFPowerSeriesOnBall f p x r

variable (ğ•œ)

/-- Given a function `f : E â†’ F`, we say that `f` is analytic at `x` if it admits a convergent power
series expansion around `x`. -/
def AnalyticAt (f : E â†’ F) (x : E) :=
  âˆƒ p : FormalMultilinearSeries ğ•œ E F, HasFPowerSeriesAt f p x

/-- Given a function `f : E â†’ F`, we say that `f` is analytic on a set `s` if it is analytic around
every point of `s`. -/
def AnalyticOn (f : E â†’ F) (s : Set E) :=
  âˆ€ x, x âˆˆ s â†’ AnalyticAt ğ•œ f x

variable {ğ•œ}

theorem HasFPowerSeriesOnBall.hasFPowerSeriesAt (hf : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesAt f p x :=
  âŸ¨r, hfâŸ©

theorem HasFPowerSeriesAt.analyticAt (hf : HasFPowerSeriesAt f p x) : AnalyticAt ğ•œ f x :=
  âŸ¨p, hfâŸ©

theorem HasFPowerSeriesOnBall.analyticAt (hf : HasFPowerSeriesOnBall f p x r) : AnalyticAt ğ•œ f x :=
  hf.hasFPowerSeriesAt.analyticAt

theorem HasFPowerSeriesOnBall.congr (hf : HasFPowerSeriesOnBall f p x r)
    (hg : EqOn f g (EMetric.ball x r)) : HasFPowerSeriesOnBall g p x r :=
  { r_le := hf.r_le
    r_pos := hf.r_pos
    hasSum := fun {y} hy => by
      convert hf.hasSum hy using 1
      apply hg.symm
      simpa [edist_eq_coe_nnnorm_sub] using hy }

/-- If a function `f` has a power series `p` around `x`, then the function `z â†¦ f (z - y)` has the
same power series around `x + y`. -/
theorem HasFPowerSeriesOnBall.comp_sub (hf : HasFPowerSeriesOnBall f p x r) (y : E) :
    HasFPowerSeriesOnBall (fun z => f (z - y)) p (x + y) r :=
  { r_le := hf.r_le
    r_pos := hf.r_pos
    hasSum := fun {z} hz => by
      convert hf.hasSum hz using 2
      abel }

theorem HasFPowerSeriesOnBall.hasSum_sub (hf : HasFPowerSeriesOnBall f p x r) {y : E}
    (hy : y âˆˆ EMetric.ball x r) : HasSum (fun n : â„• => p n fun _ => y - x) (f y) := by
  have : y - x âˆˆ EMetric.ball (0 : E) r := by simpa [edist_eq_coe_nnnorm_sub] using hy
  simpa only [add_sub_cancel] using hf.hasSum this

theorem HasFPowerSeriesOnBall.radius_pos (hf : HasFPowerSeriesOnBall f p x r) : 0 < p.radius :=
  lt_of_lt_of_le hf.r_pos hf.r_le

theorem HasFPowerSeriesAt.radius_pos (hf : HasFPowerSeriesAt f p x) : 0 < p.radius :=
  let âŸ¨_, hrâŸ© := hf
  hr.radius_pos

theorem HasFPowerSeriesOnBall.mono (hf : HasFPowerSeriesOnBall f p x r) (r'_pos : 0 < r')
    (hr : r' â‰¤ r) : HasFPowerSeriesOnBall f p x r' :=
  âŸ¨le_trans hr hf.1, r'_pos, fun hy => hf.hasSum (EMetric.ball_subset_ball hr hy)âŸ©

theorem HasFPowerSeriesAt.congr (hf : HasFPowerSeriesAt f p x) (hg : f =á¶ [ğ“ x] g) :
    HasFPowerSeriesAt g p x := by
  rcases hf with âŸ¨râ‚, hâ‚âŸ©
  rcases EMetric.mem_nhds_iff.mp hg with âŸ¨râ‚‚, hâ‚‚pos, hâ‚‚âŸ©
  exact âŸ¨min râ‚ râ‚‚,
    (hâ‚.mono (lt_min hâ‚.r_pos hâ‚‚pos) inf_le_left).congr
      fun y hy => hâ‚‚ (EMetric.ball_subset_ball inf_le_right hy)âŸ©

protected theorem HasFPowerSeriesAt.eventually (hf : HasFPowerSeriesAt f p x) :
    âˆ€á¶  r : â„â‰¥0âˆ in ğ“[>] 0, HasFPowerSeriesOnBall f p x r :=
  let âŸ¨_, hrâŸ© := hf
  mem_of_superset (Ioo_mem_nhdsWithin_Ioi (left_mem_Ico.2 hr.r_pos)) fun _ hr' =>
    hr.mono hr'.1 hr'.2.le

theorem HasFPowerSeriesOnBall.eventually_hasSum (hf : HasFPowerSeriesOnBall f p x r) :
    âˆ€á¶  y in ğ“ 0, HasSum (fun n : â„• => p n fun _ : Fin n => y) (f (x + y)) := by
  filter_upwards [EMetric.ball_mem_nhds (0 : E) hf.r_pos] using fun _ => hf.hasSum

theorem HasFPowerSeriesAt.eventually_hasSum (hf : HasFPowerSeriesAt f p x) :
    âˆ€á¶  y in ğ“ 0, HasSum (fun n : â„• => p n fun _ : Fin n => y) (f (x + y)) :=
  let âŸ¨_, hrâŸ© := hf
  hr.eventually_hasSum

theorem HasFPowerSeriesOnBall.eventually_hasSum_sub (hf : HasFPowerSeriesOnBall f p x r) :
    âˆ€á¶  y in ğ“ x, HasSum (fun n : â„• => p n fun _ : Fin n => y - x) (f y) := by
  filter_upwards [EMetric.ball_mem_nhds x hf.r_pos] with y using hf.hasSum_sub

theorem HasFPowerSeriesAt.eventually_hasSum_sub (hf : HasFPowerSeriesAt f p x) :
    âˆ€á¶  y in ğ“ x, HasSum (fun n : â„• => p n fun _ : Fin n => y - x) (f y) :=
  let âŸ¨_, hrâŸ© := hf
  hr.eventually_hasSum_sub

theorem HasFPowerSeriesOnBall.eventually_eq_zero
    (hf : HasFPowerSeriesOnBall f (0 : FormalMultilinearSeries ğ•œ E F) x r) :
    âˆ€á¶  z in ğ“ x, f z = 0 := by
  filter_upwards [hf.eventually_hasSum_sub] with z hz using hz.unique hasSum_zero

theorem HasFPowerSeriesAt.eventually_eq_zero
    (hf : HasFPowerSeriesAt f (0 : FormalMultilinearSeries ğ•œ E F) x) : âˆ€á¶  z in ğ“ x, f z = 0 :=
  let âŸ¨_, hrâŸ© := hf
  hr.eventually_eq_zero

theorem hasFPowerSeriesOnBall_const {c : F} {e : E} :
    HasFPowerSeriesOnBall (fun _ => c) (constFormalMultilinearSeries ğ•œ E c) e âŠ¤ := by
  refine âŸ¨by simp, WithTop.zero_lt_top, fun _ => hasSum_single 0 fun n hn => ?_âŸ©
  simp [constFormalMultilinearSeries_apply hn]

theorem hasFPowerSeriesAt_const {c : F} {e : E} :
    HasFPowerSeriesAt (fun _ => c) (constFormalMultilinearSeries ğ•œ E c) e :=
  âŸ¨âŠ¤, hasFPowerSeriesOnBall_constâŸ©

theorem analyticAt_const {v : F} : AnalyticAt ğ•œ (fun _ => v) x :=
  âŸ¨constFormalMultilinearSeries ğ•œ E v, hasFPowerSeriesAt_constâŸ©

theorem analyticOn_const {v : F} {s : Set E} : AnalyticOn ğ•œ (fun _ => v) s :=
  fun _ _ => analyticAt_const

theorem HasFPowerSeriesOnBall.add (hf : HasFPowerSeriesOnBall f pf x r)
    (hg : HasFPowerSeriesOnBall g pg x r) : HasFPowerSeriesOnBall (f + g) (pf + pg) x r :=
  { r_le := le_trans (le_min_iff.2 âŸ¨hf.r_le, hg.r_leâŸ©) (pf.min_radius_le_radius_add pg)
    r_pos := hf.r_pos
    hasSum := fun hy => (hf.hasSum hy).add (hg.hasSum hy) }

theorem HasFPowerSeriesAt.add (hf : HasFPowerSeriesAt f pf x) (hg : HasFPowerSeriesAt g pg x) :
    HasFPowerSeriesAt (f + g) (pf + pg) x := by
  rcases (hf.eventually.and hg.eventually).exists with âŸ¨r, hrâŸ©
  exact âŸ¨r, hr.1.add hr.2âŸ©

theorem AnalyticAt.congr (hf : AnalyticAt ğ•œ f x) (hg : f =á¶ [ğ“ x] g) : AnalyticAt ğ•œ g x :=
  let âŸ¨_, hpfâŸ© := hf
  (hpf.congr hg).analyticAt

theorem analyticAt_congr (h : f =á¶ [ğ“ x] g) : AnalyticAt ğ•œ f x â†” AnalyticAt ğ•œ g x :=
  âŸ¨fun hf â†¦ hf.congr h, fun hg â†¦ hg.congr h.symmâŸ©

theorem AnalyticAt.add (hf : AnalyticAt ğ•œ f x) (hg : AnalyticAt ğ•œ g x) : AnalyticAt ğ•œ (f + g) x :=
  let âŸ¨_, hpfâŸ© := hf
  let âŸ¨_, hqfâŸ© := hg
  (hpf.add hqf).analyticAt

theorem HasFPowerSeriesOnBall.neg (hf : HasFPowerSeriesOnBall f pf x r) :
    HasFPowerSeriesOnBall (-f) (-pf) x r :=
  { r_le := by
      rw [pf.radius_neg]
      exact hf.r_le
    r_pos := hf.r_pos
    hasSum := fun hy => (hf.hasSum hy).neg }

theorem HasFPowerSeriesAt.neg (hf : HasFPowerSeriesAt f pf x) : HasFPowerSeriesAt (-f) (-pf) x :=
  let âŸ¨_, hrfâŸ© := hf
  hrf.neg.hasFPowerSeriesAt

theorem AnalyticAt.neg (hf : AnalyticAt ğ•œ f x) : AnalyticAt ğ•œ (-f) x :=
  let âŸ¨_, hpfâŸ© := hf
  hpf.neg.analyticAt

theorem HasFPowerSeriesOnBall.sub (hf : HasFPowerSeriesOnBall f pf x r)
    (hg : HasFPowerSeriesOnBall g pg x r) : HasFPowerSeriesOnBall (f - g) (pf - pg) x r := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem HasFPowerSeriesAt.sub (hf : HasFPowerSeriesAt f pf x) (hg : HasFPowerSeriesAt g pg x) :
    HasFPowerSeriesAt (f - g) (pf - pg) x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem AnalyticAt.sub (hf : AnalyticAt ğ•œ f x) (hg : AnalyticAt ğ•œ g x) :
    AnalyticAt ğ•œ (f - g) x := by
  simpa only [sub_eq_add_neg] using hf.add hg.neg

theorem AnalyticOn.mono {s t : Set E} (hf : AnalyticOn ğ•œ f t) (hst : s âŠ† t) : AnalyticOn ğ•œ f s :=
  fun z hz => hf z (hst hz)

theorem AnalyticOn.congr' {s : Set E} (hf : AnalyticOn ğ•œ f s) (hg : f =á¶ [ğ“Ë¢ s] g) :
    AnalyticOn ğ•œ g s :=
  fun z hz => (hf z hz).congr (mem_nhdsSet_iff_forall.mp hg z hz)

theorem analyticOn_congr' {s : Set E} (h : f =á¶ [ğ“Ë¢ s] g) : AnalyticOn ğ•œ f s â†” AnalyticOn ğ•œ g s :=
  âŸ¨fun hf => hf.congr' h, fun hg => hg.congr' h.symmâŸ©

theorem AnalyticOn.congr {s : Set E} (hs : IsOpen s) (hf : AnalyticOn ğ•œ f s) (hg : s.EqOn f g) :
    AnalyticOn ğ•œ g s :=
  hf.congr' <| mem_nhdsSet_iff_forall.mpr
    (fun _ hz => eventuallyEq_iff_exists_mem.mpr âŸ¨s, hs.mem_nhds hz, hgâŸ©)

theorem analyticOn_congr {s : Set E} (hs : IsOpen s) (h : s.EqOn f g) : AnalyticOn ğ•œ f s â†”
    AnalyticOn ğ•œ g s := âŸ¨fun hf => hf.congr hs h, fun hg => hg.congr hs h.symmâŸ©

theorem AnalyticOn.add {s : Set E} (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g s) :
    AnalyticOn ğ•œ (f + g) s :=
  fun z hz => (hf z hz).add (hg z hz)

theorem AnalyticOn.neg {s : Set E} (hf : AnalyticOn ğ•œ f s) : AnalyticOn ğ•œ (-f) s :=
  fun z hz â†¦ (hf z hz).neg

theorem AnalyticOn.sub {s : Set E} (hf : AnalyticOn ğ•œ f s) (hg : AnalyticOn ğ•œ g s) :
    AnalyticOn ğ•œ (f - g) s :=
  fun z hz => (hf z hz).sub (hg z hz)

theorem HasFPowerSeriesOnBall.coeff_zero (hf : HasFPowerSeriesOnBall f pf x r) (v : Fin 0 â†’ E) :
    pf 0 v = f x := by
  have v_eq : v = fun i => 0 := Subsingleton.elim _ _
  have zero_mem : (0 : E) âˆˆ EMetric.ball (0 : E) r := by simp [hf.r_pos]
  have : âˆ€ i, i â‰  0 â†’ (pf i fun j => 0) = 0 := by
    intro i hi
    have : 0 < i := pos_iff_ne_zero.2 hi
    exact ContinuousMultilinearMap.map_coord_zero _ (âŸ¨0, thisâŸ© : Fin i) rfl
  have A := (hf.hasSum zero_mem).unique (hasSum_single _ this)
  simpa [v_eq] using A.symm

theorem HasFPowerSeriesAt.coeff_zero (hf : HasFPowerSeriesAt f pf x) (v : Fin 0 â†’ E) :
    pf 0 v = f x :=
  let âŸ¨_, hrfâŸ© := hf
  hrf.coeff_zero v

/-- If a function `f` has a power series `p` on a ball and `g` is linear, then `g âˆ˜ f` has the
power series `g âˆ˜ p` on the same ball. -/
theorem ContinuousLinearMap.comp_hasFPowerSeriesOnBall (g : F â†’L[ğ•œ] G)
    (h : HasFPowerSeriesOnBall f p x r) :
    HasFPowerSeriesOnBall (g âˆ˜ f) (g.compFormalMultilinearSeries p) x r :=
  { r_le := h.r_le.trans (p.radius_le_radius_continuousLinearMap_comp _)
    r_pos := h.r_pos
    hasSum := fun hy => by
      simpa only [ContinuousLinearMap.compFormalMultilinearSeries_apply,
        ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply] using
        g.hasSum (h.hasSum hy) }

/-- If a function `f` is analytic on a set `s` and `g` is linear, then `g âˆ˜ f` is analytic
on `s`. -/
theorem ContinuousLinearMap.comp_analyticOn {s : Set E} (g : F â†’L[ğ•œ] G) (h : AnalyticOn ğ•œ f s) :
    AnalyticOn ğ•œ (g âˆ˜ f) s := by
  rintro x hx
  rcases h x hx with âŸ¨p, r, hpâŸ©
  exact âŸ¨g.compFormalMultilinearSeries p, r, g.comp_hasFPowerSeriesOnBall hpâŸ©

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence.

This version provides an upper estimate that decreases both in `â€–yâ€–` and `n`. See also
`HasFPowerSeriesOnBall.uniform_geometric_approx` for a weaker version. -/
theorem HasFPowerSeriesOnBall.uniform_geometric_approx' {r' : â„â‰¥0}
    (hf : HasFPowerSeriesOnBall f p x r) (h : (r' : â„â‰¥0âˆ) < r) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ y âˆˆ Metric.ball (0 : E) r', âˆ€ n,
      â€–f (x + y) - p.partialSum n yâ€– â‰¤ C * (a * (â€–yâ€– / r')) ^ n := by
  obtain âŸ¨a, ha, C, hC, hpâŸ© : âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ n, â€–p nâ€– * (r' : â„) ^ n â‰¤ C * a ^ n :=
    p.norm_mul_pow_le_mul_pow_of_lt_radius (h.trans_le hf.r_le)
  refine âŸ¨a, ha, C / (1 - a), div_pos hC (sub_pos.2 ha.2), fun y hy n => ?_âŸ©
  have yr' : â€–yâ€– < r' := by
    rw [ball_zero_eq] at hy
    exact hy
  have hr'0 : 0 < (r' : â„) := (norm_nonneg _).trans_lt yr'
  have : y âˆˆ EMetric.ball (0 : E) r := by
    refine mem_emetric_ball_zero_iff.2 (lt_trans ?_ h)
    exact mod_cast yr'
  rw [norm_sub_rev, â† mul_div_right_comm]
  have ya : a * (â€–yâ€– / â†‘r') â‰¤ a :=
    mul_le_of_le_one_right ha.1.le (div_le_one_of_le yr'.le r'.coe_nonneg)
  suffices â€–p.partialSum n y - f (x + y)â€– â‰¤ C * (a * (â€–yâ€– / r')) ^ n / (1 - a * (â€–yâ€– / r')) by
    refine this.trans ?_
    have : 0 < a := ha.1
    gcongr
    apply_rules [sub_pos.2, ha.2]
  apply norm_sub_le_of_geometric_bound_of_hasSum (ya.trans_lt ha.2) _ (hf.hasSum this)
  intro n
  calc
    â€–(p n) fun _ : Fin n => yâ€–
    _ â‰¤ â€–p nâ€– * âˆ _i : Fin n, â€–yâ€– := ContinuousMultilinearMap.le_opNorm _ _
    _ = â€–p nâ€– * (r' : â„) ^ n * (â€–yâ€– / r') ^ n := by field_simp [mul_right_comm]
    _ â‰¤ C * a ^ n * (â€–yâ€– / r') ^ n := by gcongr ?_ * _; apply hp
    _ â‰¤ C * (a * (â€–yâ€– / r')) ^ n := by rw [mul_pow, mul_assoc]

/-- If a function admits a power series expansion, then it is exponentially close to the partial
sums of this power series on strict subdisks of the disk of convergence. -/
theorem HasFPowerSeriesOnBall.uniform_geometric_approx {r' : â„â‰¥0}
    (hf : HasFPowerSeriesOnBall f p x r) (h : (r' : â„â‰¥0âˆ) < r) :
    âˆƒ a âˆˆ Ioo (0 : â„) 1,
      âˆƒ C > 0, âˆ€ y âˆˆ Metric.ball (0 : E) r', âˆ€ n, â€–f (x + y) - p.partialSum n yâ€– â‰¤ C * a ^ n := by
  obtain âŸ¨a, ha, C, hC, hpâŸ© : âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ y âˆˆ Metric.ball (0 : E) r', âˆ€ n,
      â€–f (x + y) - p.partialSum n yâ€– â‰¤ C * (a * (â€–yâ€– / r')) ^ n :=
    hf.uniform_geometric_approx' h
  refine âŸ¨a, ha, C, hC, fun y hy n => (hp y hy n).trans ?_âŸ©
  have yr' : â€–yâ€– < r' := by rwa [ball_zero_eq] at hy
  have := ha.1.le -- needed to discharge a side goal on the next line
  gcongr
  exact mul_le_of_le_one_right ha.1.le (div_le_one_of_le yr'.le r'.coe_nonneg)

/-- Taylor formula for an analytic function, `IsBigO` version. -/
theorem HasFPowerSeriesAt.isBigO_sub_partialSum_pow (hf : HasFPowerSeriesAt f p x) (n : â„•) :
    (fun y : E => f (x + y) - p.partialSum n y) =O[ğ“ 0] fun y => â€–yâ€– ^ n := by
  rcases hf with âŸ¨r, hfâŸ©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hf.r_pos with âŸ¨r', r'0, hâŸ©
  obtain âŸ¨a, -, C, -, hpâŸ© : âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ y âˆˆ Metric.ball (0 : E) r', âˆ€ n,
      â€–f (x + y) - p.partialSum n yâ€– â‰¤ C * (a * (â€–yâ€– / r')) ^ n :=
    hf.uniform_geometric_approx' h
  refine isBigO_iff.2 âŸ¨C * (a / r') ^ n, ?_âŸ©
  replace r'0 : 0 < (r' : â„) := mod_cast r'0
  filter_upwards [Metric.ball_mem_nhds (0 : E) r'0] with y hy
  simpa [mul_pow, mul_div_assoc, mul_assoc, div_mul_eq_mul_div] using hp y hy n

/-- If `f` has formal power series `âˆ‘ n, pâ‚™` on a ball of radius `r`, then for `y, z` in any smaller
ball, the norm of the difference `f y - f z - p 1 (fun _ â†¦ y - z)` is bounded above by
`C * (max â€–y - xâ€– â€–z - xâ€–) * â€–y - zâ€–`. This lemma formulates this property using `IsBigO` and
`Filter.principal` on `E Ã— E`. -/
theorem HasFPowerSeriesOnBall.isBigO_image_sub_image_sub_deriv_principal
    (hf : HasFPowerSeriesOnBall f p x r) (hr : r' < r) :
    (fun y : E Ã— E => f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) =O[ğ“Ÿ (EMetric.ball (x, x) r')]
      fun y => â€–y - (x, x)â€– * â€–y.1 - y.2â€– := by
  lift r' to â„â‰¥0 using ne_top_of_lt hr
  rcases (zero_le r').eq_or_lt with (rfl | hr'0)
  Â· simp only [isBigO_bot, EMetric.ball_zero, principal_empty, ENNReal.coe_zero]
  obtain âŸ¨a, ha, C, hC : 0 < C, hpâŸ© :
      âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ n : â„•, â€–p nâ€– * (r' : â„) ^ n â‰¤ C * a ^ n :=
    p.norm_mul_pow_le_mul_pow_of_lt_radius (hr.trans_le hf.r_le)
  simp only [â† le_div_iff (pow_pos (NNReal.coe_pos.2 hr'0) _)] at hp
  set L : E Ã— E â†’ â„ := fun y =>
    C * (a / r') ^ 2 * (â€–y - (x, x)â€– * â€–y.1 - y.2â€–) * (a / (1 - a) ^ 2 + 2 / (1 - a))
  have hL : âˆ€ y âˆˆ EMetric.ball (x, x) r', â€–f y.1 - f y.2 - p 1 fun _ => y.1 - y.2â€– â‰¤ L y := by
    intro y hy'
    have hy : y âˆˆ EMetric.ball x r Ã—Ë¢ EMetric.ball x r := by
      rw [EMetric.ball_prod_same]
      exact EMetric.ball_subset_ball hr.le hy'
    set A : â„• â†’ F := fun n => (p n fun _ => y.1 - x) - p n fun _ => y.2 - x
    have hA : HasSum (fun n => A (n + 2)) (f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) := by
      convert (hasSum_nat_add_iff' 2).2 ((hf.hasSum_sub hy.1).sub (hf.hasSum_sub hy.2)) using 1
      rw [Finset.sum_range_succ, Finset.sum_range_one, hf.coeff_zero, hf.coeff_zero, sub_self,
        zero_add, â† Subsingleton.pi_single_eq (0 : Fin 1) (y.1 - x), Pi.single,
        â† Subsingleton.pi_single_eq (0 : Fin 1) (y.2 - x), Pi.single, â† (p 1).map_sub, â† Pi.single,
        Subsingleton.pi_single_eq, sub_sub_sub_cancel_right]
    rw [EMetric.mem_ball, edist_eq_coe_nnnorm_sub, ENNReal.coe_lt_coe] at hy'
    set B : â„• â†’ â„ := fun n => C * (a / r') ^ 2 * (â€–y - (x, x)â€– * â€–y.1 - y.2â€–) * ((n + 2) * a ^ n)
    have hAB : âˆ€ n, â€–A (n + 2)â€– â‰¤ B n := fun n =>
      calc
        â€–A (n + 2)â€– â‰¤ â€–p (n + 2)â€– * â†‘(n + 2) * â€–y - (x, x)â€– ^ (n + 1) * â€–y.1 - y.2â€– := by
          -- Porting note: `pi_norm_const` was `pi_norm_const (_ : E)`
          simpa only [Fintype.card_fin, pi_norm_const, Prod.norm_def, Pi.sub_def,
            Prod.fst_sub, Prod.snd_sub, sub_sub_sub_cancel_right] using
            (p <| n + 2).norm_image_sub_le (fun _ => y.1 - x) fun _ => y.2 - x
        _ = â€–p (n + 2)â€– * â€–y - (x, x)â€– ^ n * (â†‘(n + 2) * â€–y - (x, x)â€– * â€–y.1 - y.2â€–) := by
          rw [pow_succ â€–y - (x, x)â€–]
          ring
        -- Porting note: the two `â†‘` in `â†‘r'` are new, without them, Lean fails to synthesize
        -- instances `HDiv â„ â„â‰¥0 ?m` or `HMul â„ â„â‰¥0 ?m`
        _ â‰¤ C * a ^ (n + 2) / â†‘r' ^ (n + 2)
            * â†‘r' ^ n * (â†‘(n + 2) * â€–y - (x, x)â€– * â€–y.1 - y.2â€–) := by
          have : 0 < a := ha.1
          gcongr
          Â· apply hp
          Â· apply hy'.le
        _ = B n := by
          field_simp [B, pow_succ]
          simp only [mul_assoc, mul_comm, mul_left_comm]
    have hBL : HasSum B (L y) := by
      apply HasSum.mul_left
      simp only [add_mul]
      have : â€–aâ€– < 1 := by simp only [Real.norm_eq_abs, abs_of_pos ha.1, ha.2]
      rw [div_eq_mul_inv, div_eq_mul_inv]
      exact (hasSum_coe_mul_geometric_of_norm_lt_one this).add  -- Porting note: was `convert`!
          ((hasSum_geometric_of_norm_lt_one this).mul_left 2)
    exact hA.norm_le_of_bounded hBL hAB
  suffices L =O[ğ“Ÿ (EMetric.ball (x, x) r')] fun y => â€–y - (x, x)â€– * â€–y.1 - y.2â€– by
    refine (IsBigO.of_bound 1 (eventually_principal.2 fun y hy => ?_)).trans this
    rw [one_mul]
    exact (hL y hy).trans (le_abs_self _)
  simp_rw [L, mul_right_comm _ (_ * _)]
  exact (isBigO_refl _ _).const_mul_left _

/-- If `f` has formal power series `âˆ‘ n, pâ‚™` on a ball of radius `r`, then for `y, z` in any smaller
ball, the norm of the difference `f y - f z - p 1 (fun _ â†¦ y - z)` is bounded above by
`C * (max â€–y - xâ€– â€–z - xâ€–) * â€–y - zâ€–`. -/
theorem HasFPowerSeriesOnBall.image_sub_sub_deriv_le (hf : HasFPowerSeriesOnBall f p x r)
    (hr : r' < r) :
    âˆƒ C, âˆ€áµ‰ (y âˆˆ EMetric.ball x r') (z âˆˆ EMetric.ball x r'),
      â€–f y - f z - p 1 fun _ => y - zâ€– â‰¤ C * max â€–y - xâ€– â€–z - xâ€– * â€–y - zâ€– := by
  simpa only [isBigO_principal, mul_assoc, norm_mul, norm_norm, Prod.forall, EMetric.mem_ball,
    Prod.edist_eq, max_lt_iff, and_imp, @forall_swap (_ < _) E] using
    hf.isBigO_image_sub_image_sub_deriv_principal hr

/-- If `f` has formal power series `âˆ‘ n, pâ‚™` at `x`, then
`f y - f z - p 1 (fun _ â†¦ y - z) = O(â€–(y, z) - (x, x)â€– * â€–y - zâ€–)` as `(y, z) â†’ (x, x)`.
In particular, `f` is strictly differentiable at `x`. -/
theorem HasFPowerSeriesAt.isBigO_image_sub_norm_mul_norm_sub (hf : HasFPowerSeriesAt f p x) :
    (fun y : E Ã— E => f y.1 - f y.2 - p 1 fun _ => y.1 - y.2) =O[ğ“ (x, x)] fun y =>
      â€–y - (x, x)â€– * â€–y.1 - y.2â€– := by
  rcases hf with âŸ¨r, hfâŸ©
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hf.r_pos with âŸ¨r', r'0, hâŸ©
  refine (hf.isBigO_image_sub_image_sub_deriv_principal h).mono ?_
  exact le_principal_iff.2 (EMetric.ball_mem_nhds _ r'0)

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f (x + y)`
is the uniform limit of `p.partialSum n y` there. -/
theorem HasFPowerSeriesOnBall.tendstoUniformlyOn {r' : â„â‰¥0} (hf : HasFPowerSeriesOnBall f p x r)
    (h : (r' : â„â‰¥0âˆ) < r) :
    TendstoUniformlyOn (fun n y => p.partialSum n y) (fun y => f (x + y)) atTop
      (Metric.ball (0 : E) r') := by
  obtain âŸ¨a, ha, C, -, hpâŸ© : âˆƒ a âˆˆ Ioo (0 : â„) 1, âˆƒ C > 0, âˆ€ y âˆˆ Metric.ball (0 : E) r', âˆ€ n,
    â€–f (x + y) - p.partialSum n yâ€– â‰¤ C * a ^ n := hf.uniform_geometric_approx h
  refine Metric.tendstoUniformlyOn_iff.2 fun Îµ Îµpos => ?_
  have L : Tendsto (fun n => (C : â„) * a ^ n) atTop (ğ“ ((C : â„) * 0)) :=
    tendsto_const_nhds.mul (tendsto_pow_atTop_nhds_zero_of_lt_one ha.1.le ha.2)
  rw [mul_zero] at L
  refine (L.eventually (gt_mem_nhds Îµpos)).mono fun n hn y hy => ?_
  rw [dist_eq_norm]
  exact (hp y hy n).trans_lt hn

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the partial sums of this power series on the disk of convergence, i.e., `f (x + y)`
is the locally uniform limit of `p.partialSum n y` there. -/
theorem HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn (hf : HasFPowerSeriesOnBall f p x r) :
    TendstoLocallyUniformlyOn (fun n y => p.partialSum n y) (fun y => f (x + y)) atTop
      (EMetric.ball (0 : E) r) := by
  intro u hu x hx
  rcases ENNReal.lt_iff_exists_nnreal_btwn.1 hx with âŸ¨r', xr', hr'âŸ©
  have : EMetric.ball (0 : E) r' âˆˆ ğ“ x := IsOpen.mem_nhds EMetric.isOpen_ball xr'
  refine âŸ¨EMetric.ball (0 : E) r', mem_nhdsWithin_of_mem_nhds this, ?_âŸ©
  simpa [Metric.emetric_ball_nnreal] using hf.tendstoUniformlyOn hr' u hu

/-- If a function admits a power series expansion at `x`, then it is the uniform limit of the
partial sums of this power series on strict subdisks of the disk of convergence, i.e., `f y`
is the uniform limit of `p.partialSum n (y - x)` there. -/
theorem HasFPowerSeriesOnBall.tendstoUniformlyOn' {r' : â„â‰¥0} (hf : HasFPowerSeriesOnBall f p x r)
    (h : (r' : â„â‰¥0âˆ) < r) :
    TendstoUniformlyOn (fun n y => p.partialSum n (y - x)) f atTop (Metric.ball (x : E) r') := by
  convert (hf.tendstoUniformlyOn h).comp fun y => y - x using 1
  Â· simp [(Â· âˆ˜ Â·)]
  Â· ext z
    simp [dist_eq_norm]

/-- If a function admits a power series expansion at `x`, then it is the locally uniform limit of
the partial sums of this power series on the disk of convergence, i.e., `f y`
is the locally uniform limit of `p.partialSum n (y - x)` there. -/
theorem HasFPowerSeriesOnBall.tendstoLocallyUniformlyOn' (hf : HasFPowerSeriesOnBall f p x r) :
    TendstoLocallyUniformlyOn (fun n y => p.partialSum n (y - x)) f atTop
      (EMetric.ball (x : E) r) := by
  have A : ContinuousOn (fun y : E => y - x) (EMetric.ball (x : E) r) :=
    (continuous_id.sub continuous_const).continuousOn
  convert hf.tendstoLocallyUniformlyOn.comp (fun y : E => y - x) _ A using 1
  Â· ext z
    simp
  Â· intro z
    simp [edist_eq_coe_nnnorm, edist_eq_coe_nnnorm_sub]

/-- If a function admits a power series expansion on a disk, then it is continuous there. -/
protected theorem HasFPowerSeriesOnBall.continuousOn (hf : HasFPowerSeriesOnBall f p x r) :
    ContinuousOn f (EMetric.ball x r) :=
  hf.tendstoLocallyUniformlyOn'.continuousOn <|
    eventually_of_forall fun n =>
      ((p.partialSum_continuous n).comp (continuous_id.sub continuous_const)).continuousOn

protected theorem HasFPowerSeriesAt.continuousAt (hf : HasFPowerSeriesAt f p x) :
    ContinuousAt f x :=
  let âŸ¨_, hrâŸ© := hf
  hr.continuousOn.continuousAt (EMetric.ball_mem_nhds x hr.r_pos)

protected theorem AnalyticAt.continuousAt (hf : AnalyticAt ğ•œ f x) : ContinuousAt f x :=
  let âŸ¨_, hpâŸ© := hf
  hp.continuousAt

protected theorem AnalyticOn.continuousOn {s : Set E} (hf : AnalyticOn ğ•œ f s) : ContinuousOn f s :=
  fun x hx => (hf x hx).continuousAt.continuousWithinAt

/-- Analytic everywhere implies continuous -/
theorem AnalyticOn.continuous {f : E â†’ F} (fa : AnalyticOn ğ•œ f univ) : Continuous f := by
  rw [continuous_iff_continuousOn_univ]; exact fa.continuousOn

/-- In a complete space, the sum of a converging power series `p` admits `p` as a power series.
This is not totally obvious as we need to check the convergence of the series. -/
protected theorem FormalMultilinearSeries.hasFPowerSeriesOnBall [CompleteSpace F]
    (p : FormalMultilinearSeries ğ•œ E F) (h : 0 < p.radius) :
    HasFPowerSeriesOnBall p.sum p 0 p.radius :=
  { r_le := le_rfl
    r_pos := h
    hasSum := fun hy => by
      rw [zero_add]
      exact p.hasSum hy }

theorem HasFPowerSeriesOnBall.sum (h : HasFPowerSeriesOnBall f p x r) {y : E}
    (hy : y âˆˆ EMetric.ball (0 : E) r) : f (x + y) = p.sum y :=
  (h.hasSum hy).tsum_eq.symm

/-- The sum of a converging power series is continuous in its disk of convergence. -/
protected theorem FormalMultilinearSeries.continuousOn [CompleteSpace F] :
    ContinuousOn p.sum (EMetric.ball 0 p.radius) := by
  rcases (zero_le p.radius).eq_or_lt with h | h
  Â· simp [â† h, continuousOn_empty]
  Â· exact (p.hasFPowerSeriesOnBall h).continuousOn

end

/-!
### Uniqueness of power series
If a function `f : E â†’ F` has two representations as power series at a point `x : E`, corresponding
to formal multilinear series `pâ‚` and `pâ‚‚`, then these representations agree term-by-term. That is,
for any `n : â„•` and `y : E`, `pâ‚ n (fun i â†¦ y) = pâ‚‚ n (fun i â†¦ y)`. In the one-dimensional case,
when `f : ğ•œ â†’ E`, the continuous multilinear maps `pâ‚ n` and `pâ‚‚ n` are given by
`ContinuousMultilinearMap.mkPiRing`, and hence are determined completely by the value of
`pâ‚ n (fun i â†¦ 1)`, so `pâ‚ = pâ‚‚`. Consequently, the radius of convergence for one series can be
transferred to the other.
-/


section Uniqueness

open ContinuousMultilinearMap

theorem Asymptotics.IsBigO.continuousMultilinearMap_apply_eq_zero {n : â„•} {p : E[Ã—n]â†’L[ğ•œ] F}
    (h : (fun y => p fun _ => y) =O[ğ“ 0] fun y => â€–yâ€– ^ (n + 1)) (y : E) : (p fun _ => y) = 0 := by
  obtain âŸ¨c, c_pos, hcâŸ© := h.exists_pos
  obtain âŸ¨t, ht, t_open, z_memâŸ© := eventually_nhds_iff.mp (isBigOWith_iff.mp hc)
  obtain âŸ¨Î´, Î´_pos, Î´ÎµâŸ© := (Metric.isOpen_iff.mp t_open) 0 z_mem
  clear h hc z_mem
  cases' n with n
  Â· exact norm_eq_zero.mp (by
      -- Porting note: the symmetric difference of the `simpa only` sets:
      -- added `Nat.zero_eq, zero_add, pow_one`
      -- removed `zero_pow, Ne.def, Nat.one_ne_zero, not_false_iff`
      simpa only [Nat.zero_eq, fin0_apply_norm, norm_eq_zero, norm_zero, zero_add, pow_one,
        mul_zero, norm_le_zero_iff] using ht 0 (Î´Îµ (Metric.mem_ball_self Î´_pos)))
  Â· refine Or.elim (Classical.em (y = 0))
      (fun hy => by simpa only [hy] using p.map_zero) fun hy => ?_
    replace hy := norm_pos_iff.mpr hy
    refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add fun Îµ Îµ_pos => ?_) (norm_nonneg _))
    have hâ‚€ := _root_.mul_pos c_pos (pow_pos hy (n.succ + 1))
    obtain âŸ¨k, k_pos, k_normâŸ© := NormedField.exists_norm_lt ğ•œ
      (lt_min (mul_pos Î´_pos (inv_pos.mpr hy)) (mul_pos Îµ_pos (inv_pos.mpr hâ‚€)))
    have hâ‚ : â€–k â€¢ yâ€– < Î´ := by
      rw [norm_smul]
      exact inv_mul_cancel_rightâ‚€ hy.ne.symm Î´ â–¸
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_left _ _)) hy
    have hâ‚‚ :=
      calc
        â€–p fun _ => k â€¢ yâ€– â‰¤ c * â€–k â€¢ yâ€– ^ (n.succ + 1) := by
          -- Porting note: now Lean wants `_root_.`
          simpa only [norm_pow, _root_.norm_norm] using ht (k â€¢ y) (Î´Îµ (mem_ball_zero_iff.mpr hâ‚))
          --simpa only [norm_pow, norm_norm] using ht (k â€¢ y) (Î´Îµ (mem_ball_zero_iff.mpr hâ‚))
        _ = â€–kâ€– ^ n.succ * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1))) := by
          -- Porting note: added `Nat.succ_eq_add_one` since otherwise `ring` does not conclude.
          simp only [norm_smul, mul_pow, Nat.succ_eq_add_one]
          -- Porting note: removed `rw [pow_succ]`, since it now becomes superfluous.
          ring
    have hâ‚ƒ : â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1)) < Îµ :=
      inv_mul_cancel_rightâ‚€ hâ‚€.ne.symm Îµ â–¸
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_right _ _)) hâ‚€
    calc
      â€–p fun _ => yâ€– = â€–kâ»Â¹ ^ n.succâ€– * â€–p fun _ => k â€¢ yâ€– := by
        simpa only [inv_smul_smulâ‚€ (norm_pos_iff.mp k_pos), norm_smul, Finset.prod_const,
          Finset.card_fin] using
          congr_arg norm (p.map_smul_univ (fun _ : Fin n.succ => kâ»Â¹) fun _ : Fin n.succ => k â€¢ y)
      _ â‰¤ â€–kâ»Â¹ ^ n.succâ€– * (â€–kâ€– ^ n.succ * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1)))) := by gcongr
      _ = â€–(kâ»Â¹ * k) ^ n.succâ€– * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1))) := by
        rw [â† mul_assoc]
        simp [norm_mul, mul_pow]
      _ â‰¤ 0 + Îµ := by
        rw [inv_mul_cancel (norm_pos_iff.mp k_pos)]
        simpa using hâ‚ƒ.le

/-- If a formal multilinear series `p` represents the zero function at `x : E`, then the
terms `p n (fun i â†¦ y)` appearing in the sum are zero for any `n : â„•`, `y : E`. -/
theorem HasFPowerSeriesAt.apply_eq_zero {p : FormalMultilinearSeries ğ•œ E F} {x : E}
    (h : HasFPowerSeriesAt 0 p x) (n : â„•) : âˆ€ y : E, (p n fun _ => y) = 0 := by
  refine Nat.strong_induction_on n fun k hk => ?_
  have psum_eq : p.partialSum (k + 1) = fun y => p k fun _ => y := by
    funext z
    refine Finset.sum_eq_single _ (fun b hb hnb => ?_) fun hn => ?_
    Â· have := Finset.mem_range_succ_iff.mp hb
      simp only [hk b (this.lt_of_ne hnb), Pi.zero_apply]
    Â· exact False.elim (hn (Finset.mem_range.mpr (lt_add_one k)))
  replace h := h.isBigO_sub_partialSum_pow k.succ
  simp only [psum_eq, zero_sub, Pi.zero_apply, Asymptotics.isBigO_neg_left] at h
  exact h.continuousMultilinearMap_apply_eq_zero

/-- A one-dimensional formal multilinear series representing the zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero {p : FormalMultilinearSeries ğ•œ ğ•œ E} {x : ğ•œ}
    (h : HasFPowerSeriesAt 0 p x) : p = 0 := by
  ext n x
  rw [â† mkPiRing_apply_one_eq_self (p n)]
  simp [h.apply_eq_zero n 1]

/-- One-dimensional formal multilinear series representing the same function are equal. -/
theorem HasFPowerSeriesAt.eq_formalMultilinearSeries {pâ‚ pâ‚‚ : FormalMultilinearSeries ğ•œ ğ•œ E}
    {f : ğ•œ â†’ E} {x : ğ•œ} (hâ‚ : HasFPowerSeriesAt f pâ‚ x) (hâ‚‚ : HasFPowerSeriesAt f pâ‚‚ x) : pâ‚ = pâ‚‚ :=
  sub_eq_zero.mp (HasFPowerSeriesAt.eq_zero (by simpa only [sub_self] using hâ‚.sub hâ‚‚))

theorem HasFPowerSeriesAt.eq_formalMultilinearSeries_of_eventually
    {p q : FormalMultilinearSeries ğ•œ ğ•œ E} {f g : ğ•œ â†’ E} {x : ğ•œ} (hp : HasFPowerSeriesAt f p x)
    (hq : HasFPowerSeriesAt g q x) (heq : âˆ€á¶  z in ğ“ x, f z = g z) : p = q :=
  (hp.congr heq).eq_formalMultilinearSeries hq

/-- A one-dimensional formal multilinear series representing a locally zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero_of_eventually {p : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
    {x : ğ•œ} (hp : HasFPowerSeriesAt f p x) (hf : f =á¶ [ğ“ x] 0) : p = 0 :=
  (hp.congr hf).eq_zero

/-- If a function `f : ğ•œ â†’ E` has two power series representations at `x`, then the given radii in
which convergence is guaranteed may be interchanged. This can be useful when the formal multilinear
series in one representation has a particularly nice form, but the other has a larger radius. -/
theorem HasFPowerSeriesOnBall.exchange_radius {pâ‚ pâ‚‚ : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
    {râ‚ râ‚‚ : â„â‰¥0âˆ} {x : ğ•œ} (hâ‚ : HasFPowerSeriesOnBall f pâ‚ x râ‚)
    (hâ‚‚ : HasFPowerSeriesOnBall f pâ‚‚ x râ‚‚) : HasFPowerSeriesOnBall f pâ‚ x râ‚‚ :=
  hâ‚‚.hasFPowerSeriesAt.eq_formalMultilinearSeries hâ‚.hasFPowerSeriesAt â–¸ hâ‚‚

/-- If a function `f : ğ•œ â†’ E` has power series representation `p` on a ball of some radius and for
each positive radius it has some power series representation, then `p` converges to `f` on the whole
`ğ•œ`. -/
theorem HasFPowerSeriesOnBall.r_eq_top_of_exists {f : ğ•œ â†’ E} {r : â„â‰¥0âˆ} {x : ğ•œ}
    {p : FormalMultilinearSeries ğ•œ ğ•œ E} (h : HasFPowerSeriesOnBall f p x r)
    (h' : âˆ€ (r' : â„â‰¥0) (_ : 0 < r'), âˆƒ p' : FormalMultilinearSeries ğ•œ ğ•œ E,
      HasFPowerSeriesOnBall f p' x r') :
    HasFPowerSeriesOnBall f p x âˆ :=
  { r_le := ENNReal.le_of_forall_pos_nnreal_lt fun r hr _ =>
      let âŸ¨_, hp'âŸ© := h' r hr
      (h.exchange_radius hp').r_le
    r_pos := ENNReal.coe_lt_top
    hasSum := fun {y} _ =>
      let âŸ¨r', hr'âŸ© := exists_gt â€–yâ€–â‚Š
      let âŸ¨_, hp'âŸ© := h' r' hr'.ne_bot.bot_lt
      (h.exchange_radius hp').hasSum <| mem_emetric_ball_zero_iff.mpr (ENNReal.coe_lt_coe.2 hr') }

end Uniqueness

/-!
### Changing origin in a power series

If a function is analytic in a disk `D(x, R)`, then it is analytic in any disk contained in that
one. Indeed, one can write
$$
f (x + y + z) = \sum_{n} p_n (y + z)^n = \sum_{n, k} \binom{n}{k} p_n y^{n-k} z^k
= \sum_{k} \Bigl(\sum_{n} \binom{n}{k} p_n y^{n-k}\Bigr) z^k.
$$
The corresponding power series has thus a `k`-th coefficient equal to
$\sum_{n} \binom{n}{k} p_n y^{n-k}$. In the general case where `pâ‚™` is a multilinear map, this has
to be interpreted suitably: instead of having a binomial coefficient, one should sum over all
possible subsets `s` of `Fin n` of cardinality `k`, and attribute `z` to the indices in `s` and
`y` to the indices outside of `s`.

In this paragraph, we implement this. The new power series is called `p.changeOrigin y`. Then, we
check its convergence and the fact that its sum coincides with the original sum. The outcome of this
discussion is that the set of points where a function is analytic is open.
-/


namespace FormalMultilinearSeries

section

variable (p : FormalMultilinearSeries ğ•œ E F) {x y : E} {r R : â„â‰¥0}

/-- A term of `FormalMultilinearSeries.changeOriginSeries`.

Given a formal multilinear series `p` and a point `x` in its ball of convergence,
`p.changeOrigin x` is a formal multilinear series such that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense. Each term of `p.changeOrigin x`
is itself an analytic function of `x` given by the series `p.changeOriginSeries`. Each term in
`changeOriginSeries` is the sum of `changeOriginSeriesTerm`'s over all `s` of cardinality `l`.
The definition is such that `p.changeOriginSeriesTerm k l s hs (fun _ â†¦ x) (fun _ â†¦ y) =
p (k + l) (s.piecewise (fun _ â†¦ x) (fun _ â†¦ y))`
-/
def changeOriginSeriesTerm (k l : â„•) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    E[Ã—l]â†’L[ğ•œ] E[Ã—k]â†’L[ğ•œ] F := by
  let a := ContinuousMultilinearMap.curryFinFinset ğ•œ E F hs
    (by erw [Finset.card_compl, Fintype.card_fin, hs, add_tsub_cancel_right])
  exact a (p (k + l))

theorem changeOriginSeriesTerm_apply (k l : â„•) (s : Finset (Fin (k + l))) (hs : s.card = l)
    (x y : E) :
    (p.changeOriginSeriesTerm k l s hs (fun _ => x) fun _ => y) =
      p (k + l) (s.piecewise (fun _ => x) fun _ => y) :=
  ContinuousMultilinearMap.curryFinFinset_apply_const _ _ _ _ _

@[simp]
theorem norm_changeOriginSeriesTerm (k l : â„•) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    â€–p.changeOriginSeriesTerm k l s hsâ€– = â€–p (k + l)â€– := by
  simp only [changeOriginSeriesTerm, LinearIsometryEquiv.norm_map]

@[simp]
theorem nnnorm_changeOriginSeriesTerm (k l : â„•) (s : Finset (Fin (k + l))) (hs : s.card = l) :
    â€–p.changeOriginSeriesTerm k l s hsâ€–â‚Š = â€–p (k + l)â€–â‚Š := by
  simp only [changeOriginSeriesTerm, LinearIsometryEquiv.nnnorm_map]

theorem nnnorm_changeOriginSeriesTerm_apply_le (k l : â„•) (s : Finset (Fin (k + l)))
    (hs : s.card = l) (x y : E) :
    â€–p.changeOriginSeriesTerm k l s hs (fun _ => x) fun _ => yâ€–â‚Š â‰¤
      â€–p (k + l)â€–â‚Š * â€–xâ€–â‚Š ^ l * â€–yâ€–â‚Š ^ k := by
  rw [â† p.nnnorm_changeOriginSeriesTerm k l s hs, â† Fin.prod_const, â† Fin.prod_const]
  apply ContinuousMultilinearMap.le_of_opNNNorm_le
  apply ContinuousMultilinearMap.le_opNNNorm

/-- The power series for `f.changeOrigin k`.

Given a formal multilinear series `p` and a point `x` in its ball of convergence,
`p.changeOrigin x` is a formal multilinear series such that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense. Its `k`-th term is the sum of
the series `p.changeOriginSeries k`. -/
def changeOriginSeries (k : â„•) : FormalMultilinearSeries ğ•œ E (E[Ã—k]â†’L[ğ•œ] F) := fun l =>
  âˆ‘ s : { s : Finset (Fin (k + l)) // Finset.card s = l }, p.changeOriginSeriesTerm k l s s.2

theorem nnnorm_changeOriginSeries_le_tsum (k l : â„•) :
    â€–p.changeOriginSeries k lâ€–â‚Š â‰¤
      âˆ‘' _ : { s : Finset (Fin (k + l)) // s.card = l }, â€–p (k + l)â€–â‚Š :=
  (nnnorm_sum_le _ (fun t => changeOriginSeriesTerm p k l (Subtype.val t) t.prop)).trans_eq <| by
    simp_rw [tsum_fintype, nnnorm_changeOriginSeriesTerm (p := p) (k := k) (l := l)]

theorem nnnorm_changeOriginSeries_apply_le_tsum (k l : â„•) (x : E) :
    â€–p.changeOriginSeries k l fun _ => xâ€–â‚Š â‰¤
      âˆ‘' _ : { s : Finset (Fin (k + l)) // s.card = l }, â€–p (k + l)â€–â‚Š * â€–xâ€–â‚Š ^ l := by
  rw [NNReal.tsum_mul_right, â† Fin.prod_const]
  exact (p.changeOriginSeries k l).le_of_opNNNorm_le _ (p.nnnorm_changeOriginSeries_le_tsum _ _)

/-- Changing the origin of a formal multilinear series `p`, so that
`p.sum (x+y) = (p.changeOrigin x).sum y` when this makes sense.
-/
def changeOrigin (x : E) : FormalMultilinearSeries ğ•œ E F :=
  fun k => (p.changeOriginSeries k).sum x

/-- An auxiliary equivalence useful in the proofs about
`FormalMultilinearSeries.changeOriginSeries`: the set of triples `(k, l, s)`, where `s` is a
`Finset (Fin (k + l))` of cardinality `l` is equivalent to the set of pairs `(n, s)`, where `s` is a
`Finset (Fin n)`.

The forward map sends `(k, l, s)` to `(k + l, s)` and the inverse map sends `(n, s)` to
`(n - Finset.card s, Finset.card s, s)`. The actual definition is less readable because of problems
with non-definitional equalities. -/
@[simps]
def changeOriginIndexEquiv :
    (Î£ k l : â„•, { s : Finset (Fin (k + l)) // s.card = l }) â‰ƒ Î£ n : â„•, Finset (Fin n) where
  toFun s := âŸ¨s.1 + s.2.1, s.2.2âŸ©
  invFun s :=
    âŸ¨s.1 - s.2.card, s.2.card,
      âŸ¨s.2.map
        (finCongr <| (tsub_add_cancel_of_le <| card_finset_fin_le s.2).symm).toEmbedding,
        Finset.card_map _âŸ©âŸ©
  left_inv := by
    rintro âŸ¨k, l, âŸ¨s : Finset (Fin <| k + l), hs : s.card = lâŸ©âŸ©
    dsimp only [Subtype.coe_mk]
    -- Lean can't automatically generalize `k' = k + l - s.card`, `l' = s.card`, so we explicitly
    -- formulate the generalized goal
    suffices âˆ€ k' l', k' = k â†’ l' = l â†’ âˆ€ (hkl : k + l = k' + l') (hs'),
        (âŸ¨k', l', âŸ¨s.map (finCongr hkl).toEmbedding, hs'âŸ©âŸ© :
          Î£k l : â„•, { s : Finset (Fin (k + l)) // s.card = l }) = âŸ¨k, l, âŸ¨s, hsâŸ©âŸ© by
      apply this <;> simp only [hs, add_tsub_cancel_right]
    rintro _ _ rfl rfl hkl hs'
    simp only [Equiv.refl_toEmbedding, finCongr_refl, Finset.map_refl, eq_self_iff_true,
      OrderIso.refl_toEquiv, and_self_iff, heq_iff_eq]
  right_inv := by
    rintro âŸ¨n, sâŸ©
    simp [tsub_add_cancel_of_le (card_finset_fin_le s), finCongr_eq_equivCast]

lemma changeOriginSeriesTerm_changeOriginIndexEquiv_symm (n t) :
    let s := changeOriginIndexEquiv.symm âŸ¨n, tâŸ©
    p.changeOriginSeriesTerm s.1 s.2.1 s.2.2 s.2.2.2 (fun _ â†¦ x) (fun _ â†¦ y) =
    p n (t.piecewise (fun _ â†¦ x) fun _ â†¦ y) := by
  have : âˆ€ (m) (hm : n = m), p n (t.piecewise (fun _ â†¦ x) fun _ â†¦ y) =
      p m ((t.map (finCongr hm).toEmbedding).piecewise (fun _ â†¦ x) fun _ â†¦ y) := by
    rintro m rfl
    simp (config := { unfoldPartialApp := true }) [Finset.piecewise]
  simp_rw [changeOriginSeriesTerm_apply, eq_comm]; apply this

theorem changeOriginSeries_summable_auxâ‚ {r r' : â„â‰¥0} (hr : (r + r' : â„â‰¥0âˆ) < p.radius) :
    Summable fun s : Î£k l : â„•, { s : Finset (Fin (k + l)) // s.card = l } =>
      â€–p (s.1 + s.2.1)â€–â‚Š * r ^ s.2.1 * r' ^ s.1 := by
  rw [â† changeOriginIndexEquiv.symm.summable_iff]
  dsimp only [Function.comp_def, changeOriginIndexEquiv_symm_apply_fst,
    changeOriginIndexEquiv_symm_apply_snd_fst]
  have : âˆ€ n : â„•,
      HasSum (fun s : Finset (Fin n) => â€–p (n - s.card + s.card)â€–â‚Š * r ^ s.card * r' ^ (n - s.card))
        (â€–p nâ€–â‚Š * (r + r') ^ n) := by
    intro n
    -- TODO: why `simp only [tsub_add_cancel_of_le (card_finset_fin_le _)]` fails?
    convert_to HasSum (fun s : Finset (Fin n) => â€–p nâ€–â‚Š * (r ^ s.card * r' ^ (n - s.card))) _
    Â· ext1 s
      rw [tsub_add_cancel_of_le (card_finset_fin_le _), mul_assoc]
    rw [â† Fin.sum_pow_mul_eq_add_pow]
    exact (hasSum_fintype _).mul_left _
  refine NNReal.summable_sigma.2 âŸ¨fun n => (this n).summable, ?_âŸ©
  simp only [(this _).tsum_eq]
  exact p.summable_nnnorm_mul_pow hr

theorem changeOriginSeries_summable_auxâ‚‚ (hr : (r : â„â‰¥0âˆ) < p.radius) (k : â„•) :
    Summable fun s : Î£l : â„•, { s : Finset (Fin (k + l)) // s.card = l } =>
      â€–p (k + s.1)â€–â‚Š * r ^ s.1 := by
  rcases ENNReal.lt_iff_exists_add_pos_lt.1 hr with âŸ¨r', h0, hr'âŸ©
  simpa only [mul_inv_cancel_rightâ‚€ (pow_pos h0 _).ne'] using
    ((NNReal.summable_sigma.1 (p.changeOriginSeries_summable_auxâ‚ hr')).1 k).mul_right (r' ^ k)â»Â¹

theorem changeOriginSeries_summable_auxâ‚ƒ {r : â„â‰¥0} (hr : â†‘r < p.radius) (k : â„•) :
    Summable fun l : â„• => â€–p.changeOriginSeries k lâ€–â‚Š * r ^ l := by
  refine NNReal.summable_of_le
    (fun n => ?_) (NNReal.summable_sigma.1 <| p.changeOriginSeries_summable_auxâ‚‚ hr k).2
  simp only [NNReal.tsum_mul_right]
  exact mul_le_mul' (p.nnnorm_changeOriginSeries_le_tsum _ _) le_rfl

theorem le_changeOriginSeries_radius (k : â„•) : p.radius â‰¤ (p.changeOriginSeries k).radius :=
  ENNReal.le_of_forall_nnreal_lt fun _r hr =>
    le_radius_of_summable_nnnorm _ (p.changeOriginSeries_summable_auxâ‚ƒ hr k)

theorem nnnorm_changeOrigin_le (k : â„•) (h : (â€–xâ€–â‚Š : â„â‰¥0âˆ) < p.radius) :
    â€–p.changeOrigin x kâ€–â‚Š â‰¤
      âˆ‘' s : Î£l : â„•, { s : Finset (Fin (k + l)) // s.card = l }, â€–p (k + s.1)â€–â‚Š * â€–xâ€–â‚Š ^ s.1 := by
  refine tsum_of_nnnorm_bounded ?_ fun l => p.nnnorm_changeOriginSeries_apply_le_tsum k l x
  have := p.changeOriginSeries_summable_auxâ‚‚ h k
  refine HasSum.sigma this.hasSum fun l => ?_
  exact ((NNReal.summable_sigma.1 this).1 l).hasSum

/-- The radius of convergence of `p.changeOrigin x` is at least `p.radius - â€–xâ€–`. In other words,
`p.changeOrigin x` is well defined on the largest ball contained in the original ball of
convergence. -/
theorem changeOrigin_radius : p.radius - â€–xâ€–â‚Š â‰¤ (p.changeOrigin x).radius := by
  refine ENNReal.le_of_forall_pos_nnreal_lt fun r _h0 hr => ?_
  rw [lt_tsub_iff_right, add_comm] at hr
  have hr' : (â€–xâ€–â‚Š : â„â‰¥0âˆ) < p.radius := (le_add_right le_rfl).trans_lt hr
  apply le_radius_of_summable_nnnorm
  have : âˆ€ k : â„•,
      â€–p.changeOrigin x kâ€–â‚Š * r ^ k â‰¤
        (âˆ‘' s : Î£l : â„•, { s : Finset (Fin (k + l)) // s.card = l }, â€–p (k + s.1)â€–â‚Š * â€–xâ€–â‚Š ^ s.1) *
          r ^ k :=
    fun k => mul_le_mul_right' (p.nnnorm_changeOrigin_le k hr') (r ^ k)
  refine NNReal.summable_of_le this ?_
  simpa only [â† NNReal.tsum_mul_right] using
    (NNReal.summable_sigma.1 (p.changeOriginSeries_summable_auxâ‚ hr)).2

/-- `derivSeries p` is a power series for `fderiv ğ•œ f` if `p` is a power series for `f`,
see `HasFPowerSeriesOnBall.fderiv`. -/
def derivSeries : FormalMultilinearSeries ğ•œ E (E â†’L[ğ•œ] F) :=
  (continuousMultilinearCurryFin1 ğ•œ E F : (E[Ã—1]â†’L[ğ•œ] F) â†’L[ğ•œ] E â†’L[ğ•œ] F)
    |>.compFormalMultilinearSeries (p.changeOriginSeries 1)

end

-- From this point on, assume that the space is complete, to make sure that series that converge
-- in norm also converge in `F`.
variable [CompleteSpace F] (p : FormalMultilinearSeries ğ•œ E F) {x y : E} {r R : â„â‰¥0}

theorem hasFPowerSeriesOnBall_changeOrigin (k : â„•) (hr : 0 < p.radius) :
    HasFPowerSeriesOnBall (fun x => p.changeOrigin x k) (p.changeOriginSeries k) 0 p.radius :=
  have := p.le_changeOriginSeries_radius k
  ((p.changeOriginSeries k).hasFPowerSeriesOnBall (hr.trans_le this)).mono hr this

/-- Summing the series `p.changeOrigin x` at a point `y` gives back `p (x + y)`. -/
theorem changeOrigin_eval (h : (â€–xâ€–â‚Š + â€–yâ€–â‚Š : â„â‰¥0âˆ) < p.radius) :
    (p.changeOrigin x).sum y = p.sum (x + y) := by
  have radius_pos : 0 < p.radius := lt_of_le_of_lt (zero_le _) h
  have x_mem_ball : x âˆˆ EMetric.ball (0 : E) p.radius :=
    mem_emetric_ball_zero_iff.2 ((le_add_right le_rfl).trans_lt h)
  have y_mem_ball : y âˆˆ EMetric.ball (0 : E) (p.changeOrigin x).radius := by
    refine mem_emetric_ball_zero_iff.2 (lt_of_lt_of_le ?_ p.changeOrigin_radius)
    rwa [lt_tsub_iff_right, add_comm]
  have x_add_y_mem_ball : x + y âˆˆ EMetric.ball (0 : E) p.radius := by
    refine mem_emetric_ball_zero_iff.2 (lt_of_le_of_lt ?_ h)
    exact mod_cast nnnorm_add_le x y
  set f : (Î£ k l : â„•, { s : Finset (Fin (k + l)) // s.card = l }) â†’ F := fun s =>
    p.changeOriginSeriesTerm s.1 s.2.1 s.2.2 s.2.2.2 (fun _ => x) fun _ => y
  have hsf : Summable f := by
    refine .of_nnnorm_bounded _ (p.changeOriginSeries_summable_auxâ‚ h) ?_
    rintro âŸ¨k, l, s, hsâŸ©
    dsimp only [Subtype.coe_mk]
    exact p.nnnorm_changeOriginSeriesTerm_apply_le _ _ _ _ _ _
  have hf : HasSum f ((p.changeOrigin x).sum y) := by
    refine HasSum.sigma_of_hasSum ((p.changeOrigin x).summable y_mem_ball).hasSum (fun k => ?_) hsf
    Â· dsimp only [f]
      refine ContinuousMultilinearMap.hasSum_eval ?_ _
      have := (p.hasFPowerSeriesOnBall_changeOrigin k radius_pos).hasSum x_mem_ball
      rw [zero_add] at this
      refine HasSum.sigma_of_hasSum this (fun l => ?_) ?_
      Â· simp only [changeOriginSeries, ContinuousMultilinearMap.sum_apply]
        apply hasSum_fintype
      Â· refine .of_nnnorm_bounded _
          (p.changeOriginSeries_summable_auxâ‚‚ (mem_emetric_ball_zero_iff.1 x_mem_ball) k)
            fun s => ?_
        refine (ContinuousMultilinearMap.le_opNNNorm _ _).trans_eq ?_
        simp
  refine hf.unique (changeOriginIndexEquiv.symm.hasSum_iff.1 ?_)
  refine HasSum.sigma_of_hasSum
    (p.hasSum x_add_y_mem_ball) (fun n => ?_) (changeOriginIndexEquiv.symm.summable_iff.2 hsf)
  erw [(p n).map_add_univ (fun _ => x) fun _ => y]
  simp_rw [â† changeOriginSeriesTerm_changeOriginIndexEquiv_symm]
  exact hasSum_fintype (fun c => f (changeOriginIndexEquiv.symm âŸ¨n, câŸ©))

/-- Power series terms are analytic as we vary the origin -/
theorem analyticAt_changeOrigin (p : FormalMultilinearSeries ğ•œ E F) (rp : p.radius > 0) (n : â„•) :
    AnalyticAt ğ•œ (fun x â†¦ p.changeOrigin x n) 0 :=
  (FormalMultilinearSeries.hasFPowerSeriesOnBall_changeOrigin p n rp).analyticAt

end FormalMultilinearSeries

section

variable [CompleteSpace F] {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F} {x y : E} {r : â„â‰¥0âˆ}

/-- If a function admits a power series expansion `p` on a ball `B (x, r)`, then it also admits a
power series on any subball of this ball (even with a different center), given by `p.changeOrigin`.
-/
theorem HasFPowerSeriesOnBall.changeOrigin (hf : HasFPowerSeriesOnBall f p x r)
    (h : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) : HasFPowerSeriesOnBall f (p.changeOrigin y) (x + y) (r - â€–yâ€–â‚Š) :=
  { r_le := by
      apply le_trans _ p.changeOrigin_radius
      exact tsub_le_tsub hf.r_le le_rfl
    r_pos := by simp [h]
    hasSum := fun {z} hz => by
      have : f (x + y + z) =
          FormalMultilinearSeries.sum (FormalMultilinearSeries.changeOrigin p y) z := by
        rw [mem_emetric_ball_zero_iff, lt_tsub_iff_right, add_comm] at hz
        rw [p.changeOrigin_eval (hz.trans_le hf.r_le), add_assoc, hf.sum]
        refine mem_emetric_ball_zero_iff.2 (lt_of_le_of_lt ?_ hz)
        exact mod_cast nnnorm_add_le y z
      rw [this]
      apply (p.changeOrigin y).hasSum
      refine EMetric.ball_subset_ball (le_trans ?_ p.changeOrigin_radius) hz
      exact tsub_le_tsub hf.r_le le_rfl }

/-- If a function admits a power series expansion `p` on an open ball `B (x, r)`, then
it is analytic at every point of this ball. -/
theorem HasFPowerSeriesOnBall.analyticAt_of_mem (hf : HasFPowerSeriesOnBall f p x r)
    (h : y âˆˆ EMetric.ball x r) : AnalyticAt ğ•œ f y := by
  have : (â€–y - xâ€–â‚Š : â„â‰¥0âˆ) < r := by simpa [edist_eq_coe_nnnorm_sub] using h
  have := hf.changeOrigin this
  rw [add_sub_cancel] at this
  exact this.analyticAt

theorem HasFPowerSeriesOnBall.analyticOn (hf : HasFPowerSeriesOnBall f p x r) :
    AnalyticOn ğ•œ f (EMetric.ball x r) :=
  fun _y hy => hf.analyticAt_of_mem hy

variable (ğ•œ f)

/-- For any function `f` from a normed vector space to a Banach space, the set of points `x` such
that `f` is analytic at `x` is open. -/
theorem isOpen_analyticAt : IsOpen { x | AnalyticAt ğ•œ f x } := by
  rw [isOpen_iff_mem_nhds]
  rintro x âŸ¨p, r, hrâŸ©
  exact mem_of_superset (EMetric.ball_mem_nhds _ hr.r_pos) fun y hy => hr.analyticAt_of_mem hy

variable {ğ•œ}

theorem AnalyticAt.eventually_analyticAt {f : E â†’ F} {x : E} (h : AnalyticAt ğ•œ f x) :
    âˆ€á¶  y in ğ“ x, AnalyticAt ğ•œ f y :=
(isOpen_analyticAt ğ•œ f).mem_nhds h

theorem AnalyticAt.exists_mem_nhds_analyticOn {f : E â†’ F} {x : E} (h : AnalyticAt ğ•œ f x) :
    âˆƒ s âˆˆ ğ“ x, AnalyticOn ğ•œ f s :=
h.eventually_analyticAt.exists_mem

/-- If we're analytic at a point, we're analytic in a nonempty ball -/
theorem AnalyticAt.exists_ball_analyticOn {f : E â†’ F} {x : E} (h : AnalyticAt ğ•œ f x) :
    âˆƒ r : â„, 0 < r âˆ§ AnalyticOn ğ•œ f (Metric.ball x r) :=
  Metric.isOpen_iff.mp (isOpen_analyticAt _ _) _ h

end

section

open FormalMultilinearSeries

variable {p : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ}

/-- A function `f : ğ•œ â†’ E` has `p` as power series expansion at a point `zâ‚€` iff it is the sum of
`p` in a neighborhood of `zâ‚€`. This makes some proofs easier by hiding the fact that
`HasFPowerSeriesAt` depends on `p.radius`. -/
theorem hasFPowerSeriesAt_iff :
    HasFPowerSeriesAt f p zâ‚€ â†” âˆ€á¶  z in ğ“ 0, HasSum (fun n => z ^ n â€¢ p.coeff n) (f (zâ‚€ + z)) := by
  refine âŸ¨fun âŸ¨r, _, r_pos, hâŸ© =>
    eventually_of_mem (EMetric.ball_mem_nhds 0 r_pos) fun _ => by simpa using h, ?_âŸ©
  simp only [Metric.eventually_nhds_iff]
  rintro âŸ¨r, r_pos, hâŸ©
  refine âŸ¨p.radius âŠ“ r.toNNReal, by simp, ?_, ?_âŸ©
  Â· simp only [r_pos.lt, lt_inf_iff, ENNReal.coe_pos, Real.toNNReal_pos, and_true_iff]
    obtain âŸ¨z, z_pos, le_zâŸ© := NormedField.exists_norm_lt ğ•œ r_pos.lt
    have : (â€–zâ€–â‚Š : ENNReal) â‰¤ p.radius := by
      simp only [dist_zero_right] at h
      apply FormalMultilinearSeries.le_radius_of_tendsto
      convert tendsto_norm.comp (h le_z).summable.tendsto_atTop_zero
      simp [norm_smul, mul_comm]
    refine lt_of_lt_of_le ?_ this
    simp only [ENNReal.coe_pos]
    exact zero_lt_iff.mpr (nnnorm_ne_zero_iff.mpr (norm_pos_iff.mp z_pos))
  Â· simp only [EMetric.mem_ball, lt_inf_iff, edist_lt_coe, apply_eq_pow_smul_coeff, and_imp,
      dist_zero_right] at h âŠ¢
    refine fun {y} _ hyr => h ?_
    simpa [nndist_eq_nnnorm, Real.lt_toNNReal_iff_coe_lt] using hyr

theorem hasFPowerSeriesAt_iff' :
    HasFPowerSeriesAt f p zâ‚€ â†” âˆ€á¶  z in ğ“ zâ‚€, HasSum (fun n => (z - zâ‚€) ^ n â€¢ p.coeff n) (f z) := by
  rw [â† map_add_left_nhds_zero, eventually_map, hasFPowerSeriesAt_iff]
  simp_rw [add_sub_cancel_left]

end
