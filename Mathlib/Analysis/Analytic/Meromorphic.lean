/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Algebra.Order.Group.WithTop

/-!
# Meromorphic functions

Main statements:

* `MeromorphicAt`: definition of meromorphy at a point
* `MeromorphicAt.iff_eventuallyEq_zpow_smul_analyticAt`: `f` is meromorphic at `zâ‚€` iff we have
  `f z = (z - zâ‚€) ^ n â€¢ g z` on a punctured nhd of `zâ‚€`, for some `n : â„¤` and `g` analytic at zâ‚€.
* `MeromorphicAt.order`: order of vanishing at `zâ‚€`, as an element of `â„¤ âˆª {âˆ}`
-/

open Filter

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Meromorphy of `f` at `x` (more precisely, on a punctured neighbourhood of `x`; the value at
`x` itself is irrelevant). -/
def MeromorphicAt (f : ğ•œ â†’ E) (x : ğ•œ) :=
  âˆƒ (n : â„•), AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ n â€¢ f z) x

lemma AnalyticAt.meromorphicAt {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    MeromorphicAt f x :=
  âŸ¨0, by simpa only [pow_zero, one_smul]âŸ©

namespace MeromorphicAt

lemma id (x : ğ•œ) : MeromorphicAt id x := (analyticAt_id ğ•œ x).meromorphicAt

lemma const (e : E) (x : ğ•œ) : MeromorphicAt (fun _ â†¦ e) x :=
  analyticAt_const.meromorphicAt

lemma add {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨max m n, ?_âŸ©
  have : (fun z â†¦ (z - x) ^ max m n â€¢ (f + g) z) = fun z â†¦ (z - x) ^ (max m n - m) â€¢
      ((z - x) ^ m â€¢ f z) + (z - x) ^ (max m n - n) â€¢ ((z - x) ^ n â€¢ g z) := by
    simp_rw [â† mul_smul, â† pow_add, Nat.sub_add_cancel (Nat.le_max_left _ _),
      Nat.sub_add_cancel (Nat.le_max_right _ _), Pi.add_apply, smul_add]
  rw [this]
  exact ((((analyticAt_id ğ•œ x).sub analyticAt_const).pow _).smul hf).add
   ((((analyticAt_id ğ•œ x).sub analyticAt_const).pow _).smul hg)

lemma smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f â€¢ g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨m + n, ?_âŸ©
  convert hf.smul hg using 2 with z
  rw [smul_eq_mul, â† mul_smul, mul_assoc, mul_comm (f z), â† mul_assoc, pow_add,
    â† smul_eq_mul (a' := f z), smul_assoc, Pi.smul_apply']

lemma mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f * g) x :=
  hf.smul hg

lemma neg {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (-f) x := by
  convert (MeromorphicAt.const (-1 : ğ•œ) x).smul hf using 1
  ext1 z
  simp only [Pi.neg_apply, Pi.smul_apply', neg_smul, one_smul]

@[simp]
lemma neg_iff {f : ğ•œ â†’ E} {x : ğ•œ} :
    MeromorphicAt (-f) x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, MeromorphicAt.negâŸ©

lemma sub {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x := by
  convert hf.add hg.neg using 1
  ext1 z
  simp_rw [Pi.sub_apply, Pi.add_apply, Pi.neg_apply, sub_eq_add_neg]

/-- With our definitions, `MeromorphicAt f x` depends only on the values of `f` on a punctured
neighbourhood of `x` (not on `f x`) -/
lemma congr {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hfg : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt g x := by
  rcases hf with âŸ¨m, hfâŸ©
  refine âŸ¨m + 1, ?_âŸ©
  have : AnalyticAt ğ•œ (fun z â†¦ z - x) x := (analyticAt_id ğ•œ x).sub analyticAt_const
  refine (this.smul hf).congr ?_
  rw [eventuallyEq_nhdsWithin_iff] at hfg
  filter_upwards [hfg] with z hz
  rcases eq_or_ne z x with rfl | hn
  Â· simp
  Â· rw [hz (Set.mem_compl_singleton_iff.mp hn), pow_succ', mul_smul]

lemma inv {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt fâ»Â¹ x := by
  rcases hf with âŸ¨m, hfâŸ©
  by_cases h_eq : (fun z â†¦ (z - x) ^ m â€¢ f z) =á¶ [ğ“ x] 0
  Â· -- silly case: f locally 0 near x
    refine (MeromorphicAt.const 0 x).congr ?_
    rw [eventuallyEq_nhdsWithin_iff]
    filter_upwards [h_eq] with z hfz hz
    rw [Pi.inv_apply, (smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)).mp hfz,
      inv_zero]
  Â· -- interesting case: use local formula for `f`
    obtain âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ© := hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h_eq
    have : AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ (m + 1)) x :=
      ((analyticAt_id ğ•œ x).sub analyticAt_const).pow _
    -- use `m + 1` rather than `m` to damp out any silly issues with the value at `z = x`
    refine âŸ¨n + 1, (this.smul <| hg_an.inv hg_ne).congr ?_âŸ©
    filter_upwards [hg_eq, hg_an.continuousAt.eventually_ne hg_ne] with z hfg hg_ne'
    rcases eq_or_ne z x with rfl | hz_ne
    Â· simp only [sub_self, pow_succ, mul_zero, zero_smul]
    Â· simp_rw [smul_eq_mul] at hfg âŠ¢
      have aux1 : f z â‰  0 := by
        have : (z - x) ^ n * g z â‰  0 := mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz_ne)) hg_ne'
        rw [â† hfg, mul_ne_zero_iff] at this
        exact this.2
      field_simp [sub_ne_zero.mpr hz_ne]
      rw [pow_succ', mul_assoc, hfg]
      ring

@[simp]
lemma inv_iff {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} :
    MeromorphicAt fâ»Â¹ x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, MeromorphicAt.invâŸ©

lemma div {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f / g) x :=
  (div_eq_mul_inv f g).symm â–¸ (hf.mul hg.inv)

lemma pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„•) : MeromorphicAt (f ^ n) x := by
  induction' n with m hm
  Â· simpa only [Nat.zero_eq, pow_zero] using MeromorphicAt.const 1 x
  Â· simpa only [pow_succ] using hm.mul hf

lemma zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : MeromorphicAt (f ^ n) x := by
  induction' n with m m
  Â· simpa only [Int.ofNat_eq_coe, zpow_natCast] using hf.pow m
  Â· simpa only [zpow_negSucc, inv_iff] using hf.pow (m + 1)

/-- The order of vanishing of a meromorphic function, as an element of `â„¤ âˆª âˆ` (to include the
case of functions identically 0 near `x`). -/
noncomputable def order {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : WithTop â„¤ :=
  (hf.choose_spec.order.map (â†‘Â· : â„• â†’ â„¤)) - hf.choose

open WithTop.LinearOrderedAddCommGroup

lemma order_eq_top_iff {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) :
    hf.order = âŠ¤ â†” âˆ€á¶  z in ğ“[â‰ ] x, f z = 0 := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, WithTop.map_top, â† WithTop.coe_natCast,
      top_sub, eq_self, true_iff, eventually_nhdsWithin_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    filter_upwards [h] with z hf hz
    rwa [smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)] at hf
  Â· obtain âŸ¨m, hmâŸ© := WithTop.ne_top_iff_exists.mp h
    rw [â† hm, WithTop.map_coe, sub_eq_top_iff, eq_false_intro WithTop.coe_ne_top, false_or]
    simp only [WithTop.natCast_ne_top, false_iff]
    contrapose! h
    rw [AnalyticAt.order_eq_top_iff]
    rw [â† hf.choose_spec.frequently_eq_iff_eventually_eq analyticAt_const]
    apply Eventually.frequently
    filter_upwards [h] with z hfz
    rw [hfz, smul_zero]

lemma order_eq_int_iff {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : hf.order = n â†”
    âˆƒ g : ğ•œ â†’ E, AnalyticAt ğ•œ g x âˆ§ g x â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, WithTop.map_top, â† WithTop.coe_natCast, top_sub,
      eq_false_intro WithTop.top_ne_coe, false_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    refine fun âŸ¨g, hg_an, hg_ne, hg_eqâŸ© â†¦ hg_ne ?_
    apply EventuallyEq.eq_of_nhds
    rw [EventuallyEq, â† AnalyticAt.frequently_eq_iff_eventually_eq hg_an analyticAt_const]
    apply Eventually.frequently
    rw [eventually_nhdsWithin_iff] at hg_eq âŠ¢
    filter_upwards [h, hg_eq] with z hfz hfz_eq hz
    rwa [hfz_eq hz, â† mul_smul, smul_eq_zero_iff_right] at hfz
    exact mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz)) (zpow_ne_zero _ (sub_ne_zero.mpr hz))
  Â· obtain âŸ¨m, hâŸ© := WithTop.ne_top_iff_exists.mp h
    rw [â† h, WithTop.map_coe, â† WithTop.coe_natCast, â† coe_sub, WithTop.coe_inj]
    obtain âŸ¨g, hg_an, hg_ne, hg_eqâŸ© := (AnalyticAt.order_eq_nat_iff _ _).mp h.symm
    replace hg_eq : âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] x, f z = (z - x) ^ (â†‘m - â†‘hf.choose : â„¤) â€¢ g z := by
      rw [eventually_nhdsWithin_iff]
      filter_upwards [hg_eq] with z hg_eq hz
      rwa [â† smul_right_inj <| zpow_ne_zero _ (sub_ne_zero.mpr hz), â† mul_smul,
        â† zpow_addâ‚€ (sub_ne_zero.mpr hz), â† add_sub_assoc, add_sub_cancel_left, zpow_natCast,
        zpow_natCast]
    exact âŸ¨fun h â†¦ âŸ¨g, hg_an, hg_ne, h â–¸ hg_eqâŸ©,
      AnalyticAt.unique_eventuallyEq_zpow_smul_nonzero âŸ¨g, hg_an, hg_ne, hg_eqâŸ©âŸ©

/-- Compatibility of notions of `order` for analytic and meromorphic functions. -/
lemma _root_.AnalyticAt.meromorphicAt_order {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    hf.meromorphicAt.order = hf.order.map (â†‘) := by
  rcases eq_or_ne hf.order âŠ¤ with ho | ho
  Â· rw [ho, WithTop.map_top, order_eq_top_iff]
    exact (hf.order_eq_top_iff.mp ho).filter_mono nhdsWithin_le_nhds
  Â· obtain âŸ¨n, hnâŸ© := WithTop.ne_top_iff_exists.mp ho
    simp_rw [â† hn, WithTop.map_coe, order_eq_int_iff, zpow_natCast]
    rcases (hf.order_eq_nat_iff _).mp hn.symm with âŸ¨g, h1, h2, h3âŸ©
    exact âŸ¨g, h1, h2, h3.filter_mono nhdsWithin_le_nhdsâŸ©

lemma iff_eventuallyEq_zpow_smul_analyticAt {f : ğ•œ â†’ E} {x : ğ•œ} : MeromorphicAt f x â†”
    âˆƒ (n : â„¤) (g : ğ•œ â†’ E), AnalyticAt ğ•œ g x âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  refine âŸ¨fun âŸ¨n, hnâŸ© â†¦ âŸ¨-n, _, âŸ¨hn, eventually_nhdsWithin_iff.mpr ?_âŸ©âŸ©, ?_âŸ©
  Â· filter_upwards with z hz
    rw [â† mul_smul, â† zpow_natCast, â† zpow_addâ‚€ (sub_ne_zero.mpr hz), add_left_neg,
      zpow_zero, one_smul]
  Â· refine fun âŸ¨n, g, hg_an, hg_eqâŸ© â†¦ MeromorphicAt.congr ?_ (EventuallyEq.symm hg_eq)
    exact (((MeromorphicAt.id x).sub (.const _ x)).zpow _).smul hg_an.meromorphicAt

end MeromorphicAt

/-- Meromorphy of a function on a set. -/
def MeromorphicOn (f : ğ•œ â†’ E) (U : Set ğ•œ) : Prop := âˆ€ x âˆˆ U, MeromorphicAt f x

lemma AnalyticOn.meromorphicOn {f : ğ•œ â†’ E} {U : Set ğ•œ} (hf : AnalyticOn ğ•œ f U) :
    MeromorphicOn f U :=
  fun x hx â†¦ (hf x hx).meromorphicAt


namespace MeromorphicOn

variable {s t : ğ•œ â†’ ğ•œ} {f g : ğ•œ â†’ E} {U : Set ğ•œ}
  (hs : MeromorphicOn s U) (ht : MeromorphicOn t U)
  (hf : MeromorphicOn f U) (hg : MeromorphicOn g U)

lemma id {U : Set ğ•œ} : MeromorphicOn id U := fun x _ â†¦ .id x

lemma const (e : E) {U : Set ğ•œ} : MeromorphicOn (fun _ â†¦ e) U :=
  fun x _ â†¦ .const e x

section arithmetic

lemma mono_set {V : Set ğ•œ} (hv : V âŠ† U) : MeromorphicOn f V := fun x hx â†¦ hf x (hv hx)

lemma add : MeromorphicOn (f + g) U := fun x hx â†¦ (hf x hx).add (hg x hx)

lemma sub : MeromorphicOn (f - g) U := fun x hx â†¦ (hf x hx).sub (hg x hx)

lemma neg : MeromorphicOn (-f) U := fun x hx â†¦ (hf x hx).neg

@[simp] lemma neg_iff : MeromorphicOn (-f) U â†” MeromorphicOn f U :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, negâŸ©

lemma smul : MeromorphicOn (s â€¢ f) U := fun x hx â†¦ (hs x hx).smul (hf x hx)

lemma mul : MeromorphicOn (s * t) U := fun x hx â†¦ (hs x hx).mul (ht x hx)

lemma inv : MeromorphicOn sâ»Â¹ U := fun x hx â†¦ (hs x hx).inv

@[simp] lemma inv_iff : MeromorphicOn sâ»Â¹ U â†” MeromorphicOn s U :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, invâŸ©

lemma div : MeromorphicOn (s / t) U := fun x hx â†¦ (hs x hx).div (ht x hx)

lemma pow (n : â„•) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).pow _

lemma zpow (n : â„¤) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).zpow _

end arithmetic

lemma congr (h_eq : Set.EqOn f g U) (hu : IsOpen U) : MeromorphicOn g U := by
  refine fun x hx â†¦ (hf x hx).congr (EventuallyEq.filter_mono ?_ nhdsWithin_le_nhds)
  exact eventually_of_mem (hu.mem_nhds hx) h_eq

end MeromorphicOn
