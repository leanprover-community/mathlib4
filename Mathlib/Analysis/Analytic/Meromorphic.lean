/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.Analysis.Analytic.IsolatedZeros

/-!
# Meromorphic functions
-/

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Meromorphy of `f` at `x` (more precisely, on a punctured neighbourhood of `x`; the value at
`x` itself is irrelevant). -/
def MeromorphicAt (f : ğ•œ â†’ E) (x : ğ•œ) :=
  âˆƒ (n : â„•), AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ n â€¢ f z) x

lemma AnalyticAt.meromorphicAt {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    MeromorphicAt f x :=
  âŸ¨0, by simpa only [pow_zero, one_smul]âŸ©

lemma meromorphicAt_id (x : ğ•œ) : MeromorphicAt id x := (analyticAt_id ğ•œ x).meromorphicAt

lemma meromorphicAt_const (e : E) (x : ğ•œ) : MeromorphicAt (fun _ â†¦ e) x :=
  analyticAt_const.meromorphicAt

namespace MeromorphicAt

lemma add {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨max m n, ?_âŸ©
  have : (fun z â†¦ (z - x) ^ max m n â€¢ (f + g) z) = fun z â†¦ (z - x) ^ (max m n - m) â€¢
    ((z - x) ^ m â€¢ f z) + (z - x) ^ (max m n - n) â€¢ ((z - x) ^ n â€¢ g z)
  Â· simp_rw [â† mul_smul, â† pow_add, Nat.sub_add_cancel (Nat.le_max_left _ _),
      Nat.sub_add_cancel (Nat.le_max_right _ _), Pi.add_apply, smul_add]
  rw [this]
  exact ((((analyticAt_id ğ•œ x).sub analyticAt_const).pow _).smul hf).add
   ((((analyticAt_id ğ•œ x).sub analyticAt_const).pow _).smul hg)

lemma smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f â€¢ g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨m + n, ?_âŸ©
  convert hf.smul hg using 2 with z
  rw [smul_eq_mul, â† mul_smul, mul_assoc, mul_comm (f z), â† mul_assoc, pow_add,
    â† smul_eq_mul (a' := f z), smul_assoc, Pi.smul_apply']

lemma mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f * g) x :=
  hf.smul hg

lemma neg {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (-f) x := by
  convert (meromorphicAt_const (-1 : ğ•œ) x).smul hf using 1
  ext1 z
  simp only [Pi.neg_apply, Pi.smul_apply', neg_smul, one_smul]

@[simp]
lemma neg_iff {f : ğ•œ â†’ E} {x : ğ•œ} :
    MeromorphicAt f x â†” MeromorphicAt (-f) x :=
  âŸ¨MeromorphicAt.neg, fun h â†¦ by simpa only [neg_neg] using h.negâŸ©

lemma sub {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x := by
  convert hf.add hg.neg using 1
  ext1 z
  simp_rw [Pi.sub_apply, Pi.add_apply, Pi.neg_apply, sub_eq_add_neg]

/-- With our definitions, `MeromorphicAt f x` depends only on the values of `f` on a punctured
neighbourhood of `x` (not on `f x`) -/
lemma congr {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hfg : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt g x := by
  rcases hf with âŸ¨m, hfâŸ©
  refine âŸ¨m + 1, ?_âŸ©
  have : AnalyticAt ğ•œ (fun z â†¦ z - x) x := (analyticAt_id ğ•œ x).sub analyticAt_const
  refine (this.smul hf).congr ?_
  rw [eventuallyEq_nhdsWithin_iff] at hfg
  filter_upwards [hfg] with z hz
  rcases eq_or_ne z x with rfl | hn
  Â· simp
  Â· rw [hz (Set.mem_compl_singleton_iff.mp hn), pow_succ, mul_smul]

lemma inv {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt fâ»Â¹ x := by
  rcases hf with âŸ¨m, hfâŸ©
  by_cases h_eq : (fun z â†¦ (z - x) ^ m â€¢ f z) =á¶ [ğ“ x] 0
  Â· -- silly case: f locally 0 near x
    apply (meromorphicAt_const 0 x).congr
    rw [eventuallyEq_nhdsWithin_iff]
    filter_upwards [h_eq] with z hfz hz
    rw [Pi.inv_apply, (smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)).mp hfz,
      inv_zero]
  Â· -- interesting case: use local formula for `f`
    obtain âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ© := hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h_eq
    have : AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ (m + 1)) x :=
      ((analyticAt_id ğ•œ x).sub analyticAt_const).pow _
    -- use `m + 1` rather than `m` to damp out any silly issues with the value at `z = x`
    refine âŸ¨n + 1, (this.smul <| hg_an.inv hg_ne).congr ?_âŸ©
    filter_upwards [hg_eq, hg_an.continuousAt.eventually_ne hg_ne] with z hfg hg_ne'
    rcases eq_or_ne z x with rfl | hz_ne
    Â· simp only [sub_self, pow_succ, zero_mul, zero_smul]
    Â· simp_rw [smul_eq_mul] at hfg âŠ¢
      have aux1 : f z â‰  0
      Â· have : (z - x) ^ n * g z â‰  0 := mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz_ne)) hg_ne'
        rw [â† hfg, mul_ne_zero_iff] at this
        exact this.2
      field_simp [sub_ne_zero.mpr hz_ne]
      rw [pow_succ, mul_assoc, hfg]
      ring

@[simp]
lemma inv_iff {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} :
    MeromorphicAt f x â†” MeromorphicAt fâ»Â¹ x :=
  âŸ¨MeromorphicAt.inv, fun h â†¦ by simpa only [inv_inv] using h.invâŸ©

lemma div {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f / g) x :=
  (div_eq_mul_inv f g).symm â–¸ (hf.mul hg.inv)

end MeromorphicAt
