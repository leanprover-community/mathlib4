/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Stefan Kebekus
-/
import Mathlib.Analysis.Analytic.IsolatedZeros
import Mathlib.Algebra.Order.AddGroupWithTop

/-!
# Meromorphic functions

Main statements:

* `MeromorphicAt`: definition of meromorphy at a point
* `MeromorphicAt.iff_eventuallyEq_zpow_smul_analyticAt`: `f` is meromorphic at `zâ‚€` iff we have
  `f z = (z - zâ‚€) ^ n â€¢ g z` on a punctured nhd of `zâ‚€`, for some `n : â„¤` and `g` analytic at zâ‚€.
* `MeromorphicAt.order`: order of vanishing at `zâ‚€`, as an element of `â„¤ âˆª {âˆ}`
-/

open Filter

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Meromorphy of `f` at `x` (more precisely, on a punctured neighbourhood of `x`; the value at
`x` itself is irrelevant). -/
@[fun_prop]
def MeromorphicAt (f : ğ•œ â†’ E) (x : ğ•œ) :=
  âˆƒ (n : â„•), AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ n â€¢ f z) x

@[fun_prop]
lemma AnalyticAt.meromorphicAt {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    MeromorphicAt f x :=
  âŸ¨0, by simpa only [pow_zero, one_smul]âŸ©

/- Analogue of the principle of isolated zeros for an analytic function: if a function is
meromorphic at `zâ‚€`, then either it is identically zero in a punctured neighborhood of `zâ‚€`, or it
does not vanish there at all. -/
theorem MeromorphicAt.eventually_eq_zero_or_eventually_ne_zero {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : MeromorphicAt f zâ‚€) :
    (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z = 0) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) := by
  obtain âŸ¨n, hâŸ© := hf
  rcases h.eventually_eq_zero_or_eventually_ne_zero with hâ‚ | hâ‚‚
  Â· left
    filter_upwards [nhdsWithin_le_nhds hâ‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    rcases (smul_eq_zero.1 hâ‚y) with hâ‚ƒ | hâ‚„
    Â· exact False.elim (hâ‚‚y (sub_eq_zero.1 (pow_eq_zero_iff'.1 hâ‚ƒ).1))
    Â· assumption
  Â· right
    filter_upwards [hâ‚‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    exact (smul_ne_zero_iff.1 hâ‚y).2

namespace MeromorphicAt

lemma id (x : ğ•œ) : MeromorphicAt id x := analyticAt_id.meromorphicAt

@[fun_prop]
lemma const (e : E) (x : ğ•œ) : MeromorphicAt (fun _ â†¦ e) x :=
  analyticAt_const.meromorphicAt

@[fun_prop]
lemma add {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨max m n, ?_âŸ©
  have : (fun z â†¦ (z - x) ^ max m n â€¢ (f + g) z) = fun z â†¦ (z - x) ^ (max m n - m) â€¢
      ((z - x) ^ m â€¢ f z) + (z - x) ^ (max m n - n) â€¢ ((z - x) ^ n â€¢ g z) := by
    simp_rw [â† mul_smul, â† pow_add, Nat.sub_add_cancel (Nat.le_max_left _ _),
      Nat.sub_add_cancel (Nat.le_max_right _ _), Pi.add_apply, smul_add]
  rw [this]
  exact (((analyticAt_id.sub analyticAt_const).pow _).smul hf).add
   (((analyticAt_id.sub analyticAt_const).pow _).smul hg)

@[fun_prop]
lemma add' {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z + g z) x :=
  hf.add hg

@[fun_prop]
lemma smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f â€¢ g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨m + n, ?_âŸ©
  convert hf.smul' hg using 2 with z
  rw [Pi.smul_apply', smul_eq_mul]
  module

@[fun_prop]
lemma smul' {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z â€¢ g z) x :=
  hf.smul hg

@[fun_prop]
lemma mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f * g) x :=
  hf.smul hg

@[fun_prop]
lemma mul' {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z * g z) x :=
  hf.smul hg

@[fun_prop]
lemma neg {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (-f) x := by
  convert (MeromorphicAt.const (-1 : ğ•œ) x).smul hf using 1
  ext1 z
  simp only [Pi.neg_apply, Pi.smul_apply', neg_smul, one_smul]

@[fun_prop]
lemma neg' {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (fun z â†¦ -f z) x :=
  hf.neg

@[simp]
lemma neg_iff {f : ğ•œ â†’ E} {x : ğ•œ} :
    MeromorphicAt (-f) x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, MeromorphicAt.negâŸ©

@[fun_prop]
lemma sub {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x := by
  convert hf.add hg.neg using 1
  ext1 z
  simp_rw [Pi.sub_apply, Pi.add_apply, Pi.neg_apply, sub_eq_add_neg]

@[fun_prop]
lemma sub' {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z - g z) x :=
  hf.sub hg

/-- With our definitions, `MeromorphicAt f x` depends only on the values of `f` on a punctured
neighbourhood of `x` (not on `f x`) -/
lemma congr {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hfg : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt g x := by
  rcases hf with âŸ¨m, hfâŸ©
  refine âŸ¨m + 1, ?_âŸ©
  have : AnalyticAt ğ•œ (fun z â†¦ z - x) x := analyticAt_id.sub analyticAt_const
  refine (this.smul' hf).congr ?_
  rw [eventuallyEq_nhdsWithin_iff] at hfg
  filter_upwards [hfg] with z hz
  rcases eq_or_ne z x with rfl | hn
  Â· simp
  Â· rw [hz (Set.mem_compl_singleton_iff.mp hn), pow_succ', mul_smul]

@[fun_prop]
lemma inv {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt fâ»Â¹ x := by
  rcases hf with âŸ¨m, hfâŸ©
  by_cases h_eq : (fun z â†¦ (z - x) ^ m â€¢ f z) =á¶ [ğ“ x] 0
  Â· -- silly case: f locally 0 near x
    refine (MeromorphicAt.const 0 x).congr ?_
    rw [eventuallyEq_nhdsWithin_iff]
    filter_upwards [h_eq] with z hfz hz
    rw [Pi.inv_apply, (smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)).mp hfz,
      inv_zero]
  Â· -- interesting case: use local formula for `f`
    obtain âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ© := hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h_eq
    have : AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ (m + 1)) x :=
      (analyticAt_id.sub analyticAt_const).pow _
    -- use `m + 1` rather than `m` to damp out any silly issues with the value at `z = x`
    refine âŸ¨n + 1, (this.smul' <| hg_an.inv hg_ne).congr ?_âŸ©
    filter_upwards [hg_eq, hg_an.continuousAt.eventually_ne hg_ne] with z hfg hg_ne'
    rcases eq_or_ne z x with rfl | hz_ne
    Â· simp only [sub_self, pow_succ, mul_zero, zero_smul]
    Â· simp_rw [smul_eq_mul] at hfg âŠ¢
      have aux1 : f z â‰  0 := by
        have : (z - x) ^ n * g z â‰  0 := mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz_ne)) hg_ne'
        rw [â† hfg, mul_ne_zero_iff] at this
        exact this.2
      field_simp [sub_ne_zero.mpr hz_ne]
      rw [pow_succ', mul_assoc, hfg]
      ring

@[fun_prop]
lemma inv' {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (fun z â†¦ (f z)â»Â¹) x :=
  hf.inv

@[simp]
lemma inv_iff {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} :
    MeromorphicAt fâ»Â¹ x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, MeromorphicAt.invâŸ©

@[fun_prop]
lemma div {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f / g) x :=
  (div_eq_mul_inv f g).symm â–¸ (hf.mul hg.inv)

@[fun_prop]
lemma div' {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z / g z) x :=
  hf.div hg

@[fun_prop]
lemma pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„•) : MeromorphicAt (f ^ n) x := by
  induction n with
  | zero => simpa only [pow_zero] using MeromorphicAt.const 1 x
  | succ m hm => simpa only [pow_succ] using hm.mul hf

@[fun_prop]
lemma pow' {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„•) :
    MeromorphicAt (fun z â†¦ (f z) ^ n) x :=
  hf.pow n

@[fun_prop]
lemma zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : MeromorphicAt (f ^ n) x := by
  induction n with
  | ofNat m => simpa only [Int.ofNat_eq_coe, zpow_natCast] using hf.pow m
  | negSucc m => simpa only [zpow_negSucc, inv_iff] using hf.pow (m + 1)

@[fun_prop]
lemma zpow' {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) :
    MeromorphicAt (fun z â†¦ (f z) ^ n) x :=
  hf.zpow n

theorem eventually_analyticAt [CompleteSpace E] {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicAt f x) : âˆ€á¶  y in ğ“[â‰ ] x, AnalyticAt ğ•œ f y := by
  rw [MeromorphicAt] at h
  obtain âŸ¨n, hâŸ© := h
  apply AnalyticAt.eventually_analyticAt at h
  refine (h.filter_mono ?_).mp ?_
  Â· simp [nhdsWithin]
  Â· rw [eventually_nhdsWithin_iff]
    apply Filter.Eventually.of_forall
    intro y hy hf
    rw [Set.mem_compl_iff, Set.mem_singleton_iff] at hy
    have := ((analyticAt_id (ğ•œ := ğ•œ).sub analyticAt_const).pow n).inv
      (pow_ne_zero _ (sub_ne_zero_of_ne hy))
    apply (this.smul hf).congr âˆ˜ (eventually_ne_nhds hy).mono
    intro z hz
    simp [smul_smul, hz, sub_eq_zero]

/-- The order of vanishing of a meromorphic function, as an element of `â„¤ âˆª âˆ` (to include the
case of functions identically 0 near `x`). -/
noncomputable def order {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : WithTop â„¤ :=
  (hf.choose_spec.order.map (â†‘Â· : â„• â†’ â„¤)) - hf.choose

open WithTop.LinearOrderedAddCommGroup

lemma order_eq_top_iff {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) :
    hf.order = âŠ¤ â†” âˆ€á¶  z in ğ“[â‰ ] x, f z = 0 := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, ENat.map_top, â† WithTop.coe_natCast,
      top_sub, eq_self, true_iff, eventually_nhdsWithin_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    filter_upwards [h] with z hf hz
    rwa [smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)] at hf
  Â· obtain âŸ¨m, hmâŸ© := ENat.ne_top_iff_exists.mp h
    simp only [â† hm, ENat.map_coe, WithTop.coe_natCast, sub_eq_top_iff, WithTop.natCast_ne_top,
      or_self, false_iff]
    contrapose! h
    rw [AnalyticAt.order_eq_top_iff]
    rw [â† hf.choose_spec.frequently_eq_iff_eventually_eq analyticAt_const]
    apply Eventually.frequently
    filter_upwards [h] with z hfz
    rw [hfz, smul_zero]

lemma order_eq_int_iff {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : hf.order = n â†”
    âˆƒ g : ğ•œ â†’ E, AnalyticAt ğ•œ g x âˆ§ g x â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, ENat.map_top, â† WithTop.coe_natCast, top_sub,
      eq_false_intro WithTop.top_ne_coe, false_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    refine fun âŸ¨g, hg_an, hg_ne, hg_eqâŸ© â†¦ hg_ne ?_
    apply EventuallyEq.eq_of_nhds
    rw [EventuallyEq, â† AnalyticAt.frequently_eq_iff_eventually_eq hg_an analyticAt_const]
    apply Eventually.frequently
    rw [eventually_nhdsWithin_iff] at hg_eq âŠ¢
    filter_upwards [h, hg_eq] with z hfz hfz_eq hz
    rwa [hfz_eq hz, â† mul_smul, smul_eq_zero_iff_right] at hfz
    exact mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz)) (zpow_ne_zero _ (sub_ne_zero.mpr hz))
  Â· obtain âŸ¨m, hâŸ© := ENat.ne_top_iff_exists.mp h
    rw [â† h, ENat.map_coe, â† WithTop.coe_natCast, â† coe_sub, WithTop.coe_inj]
    obtain âŸ¨g, hg_an, hg_ne, hg_eqâŸ© := (AnalyticAt.order_eq_nat_iff _ _).mp h.symm
    replace hg_eq : âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] x, f z = (z - x) ^ (â†‘m - â†‘hf.choose : â„¤) â€¢ g z := by
      rw [eventually_nhdsWithin_iff]
      filter_upwards [hg_eq] with z hg_eq hz
      rwa [â† smul_right_inj <| zpow_ne_zero _ (sub_ne_zero.mpr hz), â† mul_smul,
        â† zpow_addâ‚€ (sub_ne_zero.mpr hz), â† add_sub_assoc, add_sub_cancel_left, zpow_natCast,
        zpow_natCast]
    exact âŸ¨fun h â†¦ âŸ¨g, hg_an, hg_ne, h â–¸ hg_eqâŸ©,
      AnalyticAt.unique_eventuallyEq_zpow_smul_nonzero âŸ¨g, hg_an, hg_ne, hg_eqâŸ©âŸ©

/-- Compatibility of notions of `order` for analytic and meromorphic functions. -/
lemma _root_.AnalyticAt.meromorphicAt_order {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    hf.meromorphicAt.order = hf.order.map (â†‘) := by
  rcases eq_or_ne hf.order âŠ¤ with ho | ho
  Â· rw [ho, ENat.map_top, order_eq_top_iff]
    exact (hf.order_eq_top_iff.mp ho).filter_mono nhdsWithin_le_nhds
  Â· obtain âŸ¨n, hnâŸ© := ENat.ne_top_iff_exists.mp ho
    simp_rw [â† hn, ENat.map_coe, order_eq_int_iff, zpow_natCast]
    rcases (hf.order_eq_nat_iff _).mp hn.symm with âŸ¨g, h1, h2, h3âŸ©
    exact âŸ¨g, h1, h2, h3.filter_mono nhdsWithin_le_nhdsâŸ©

/-- The order is additive when multiplying scalar-valued and vector-valued meromorphic functions. -/
theorem order_smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ}
    (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    (hf.smul hg).order = hf.order + hg.order := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  cases' hâ‚‚f : hf.order with m hâ‚‚f
  Â· simp only [top_add, order_eq_top_iff] at hâ‚‚f âŠ¢
    filter_upwards [hâ‚‚f] with z hz using by simp [hz]
  cases' hâ‚‚g : hg.order with n hâ‚‚f
  Â· simp only [add_top, order_eq_top_iff] at hâ‚‚g âŠ¢
    filter_upwards [hâ‚‚g] with z hz using by simp [hz]
  -- Non-trivial case: both functions do not vanish around zâ‚€
  rw [â† WithTop.coe_add, order_eq_int_iff]
  obtain âŸ¨F, hâ‚F, hâ‚‚F, hâ‚ƒFâŸ© := (hf.order_eq_int_iff _).1 hâ‚‚f
  obtain âŸ¨G, hâ‚G, hâ‚‚G, hâ‚ƒGâŸ© := (hg.order_eq_int_iff _).1 hâ‚‚g
  use F â€¢ G, hâ‚F.smul hâ‚G, by simp [hâ‚‚F, hâ‚‚G]
  filter_upwards [self_mem_nhdsWithin, hâ‚ƒF, hâ‚ƒG] with a ha hfa hga
  simp [hfa, hga, smul_comm (F a), zpow_addâ‚€ (sub_ne_zero.mpr ha), mul_smul]

/-- The order is additive when multiplying meromorphic functions. -/
theorem order_mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    (hf.mul hg).order = hf.order + hg.order :=
  hf.order_smul hg

lemma iff_eventuallyEq_zpow_smul_analyticAt {f : ğ•œ â†’ E} {x : ğ•œ} : MeromorphicAt f x â†”
    âˆƒ (n : â„¤) (g : ğ•œ â†’ E), AnalyticAt ğ•œ g x âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  refine âŸ¨fun âŸ¨n, hnâŸ© â†¦ âŸ¨-n, _, âŸ¨hn, eventually_nhdsWithin_iff.mpr ?_âŸ©âŸ©, ?_âŸ©
  Â· filter_upwards with z hz
    match_scalars
    field_simp [sub_ne_zero.mpr hz]
  Â· refine fun âŸ¨n, g, hg_an, hg_eqâŸ© â†¦ MeromorphicAt.congr ?_ (EventuallyEq.symm hg_eq)
    exact (((MeromorphicAt.id x).sub (.const _ x)).zpow _).smul hg_an.meromorphicAt

end MeromorphicAt

/-- Meromorphy of a function on a set. -/
def MeromorphicOn (f : ğ•œ â†’ E) (U : Set ğ•œ) : Prop := âˆ€ x âˆˆ U, MeromorphicAt f x

lemma AnalyticOnNhd.meromorphicOn {f : ğ•œ â†’ E} {U : Set ğ•œ} (hf : AnalyticOnNhd ğ•œ f U) :
    MeromorphicOn f U :=
  fun x hx â†¦ (hf x hx).meromorphicAt

@[deprecated (since := "2024-09-26")]
alias AnalyticOn.meromorphicOn := AnalyticOnNhd.meromorphicOn

namespace MeromorphicOn

variable {s t : ğ•œ â†’ ğ•œ} {f g : ğ•œ â†’ E} {U : Set ğ•œ}
  (hs : MeromorphicOn s U) (ht : MeromorphicOn t U)
  (hf : MeromorphicOn f U) (hg : MeromorphicOn g U)

lemma id {U : Set ğ•œ} : MeromorphicOn id U := fun x _ â†¦ .id x

lemma const (e : E) {U : Set ğ•œ} : MeromorphicOn (fun _ â†¦ e) U :=
  fun x _ â†¦ .const e x

/-- The set where a meromorphic function has infinite order is clopen in its domain of meromorphy.
-/
theorem isClopen_setOf_order_eq_top {U : Set ğ•œ} (hf : MeromorphicOn f U) :
    IsClopen { u : U | (hf u.1 u.2).order = âŠ¤ } := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† (hf z.1 z.2).order_eq_top_iff] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [Set.mem_compl_iff, Set.mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [MeromorphicAt.order_eq_top_iff, not_eventually]
          apply Filter.Eventually.frequently
          rw [eventually_nhdsWithin_iff, eventually_nhds_iff]
          use t' \ {z.1}, fun y hâ‚y hâ‚‚y â†¦ hâ‚t' y hâ‚y.1 hâ‚y.2, hâ‚‚t'.sdiff isClosed_singleton, hw,
            Set.mem_singleton_iff.not.2 (Subtype.coe_ne_coe.mpr hâ‚w)
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [MeromorphicAt.order_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff]
    simp only [Set.mem_setOf_eq] at hz
    rw [MeromorphicAt.order_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [Set.mem_compl_iff, Set.mem_singleton_iff, isOpen_induced hâ‚‚t', Set.mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [Set.mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y _ â†¦ hâ‚t' y (Set.mem_of_mem_diff hâ‚y) (Set.mem_of_mem_inter_right hâ‚y)
    constructor
    Â· exact hâ‚‚t'.sdiff isClosed_singleton
    Â· apply (Set.mem_diff w).1
      exact âŸ¨hw, Set.mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_order_ne_top_iff_forall {U : Set ğ•œ} (hf : MeromorphicOn f U) (hU : IsConnected U) :
    (âˆƒ u : U, (hf u u.2).order â‰  âŠ¤) â†” (âˆ€ u : U, (hf u u.2).order â‰  âŠ¤) := by
  constructor
  Â· intro hâ‚‚f
    have := isPreconnected_iff_preconnectedSpace.1 hU.isPreconnected
    rcases isClopen_iff.1 hf.isClopen_setOf_order_eq_top with h | h
    Â· intro u
      have : u âˆ‰ (âˆ… : Set U) := by exact fun a => a
      rw [â† h] at this
      tauto
    Â· obtain âŸ¨u, hUâŸ© := hâ‚‚f
      have : u âˆˆ Set.univ := by trivial
      rw [â† h] at this
      tauto
  Â· intro hâ‚‚f
    obtain âŸ¨v, hvâŸ© := hU.nonempty
    use âŸ¨v, hvâŸ©, hâ‚‚f âŸ¨v, hvâŸ©

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem order_ne_top_of_isPreconnected {U : Set ğ•œ} {x y : ğ•œ} (hf : MeromorphicOn f U)
    (hU : IsPreconnected U) (hâ‚x : x âˆˆ U) (hy : y âˆˆ U) (hâ‚‚x : (hf x hâ‚x).order â‰  âŠ¤) :
    (hf y hy).order â‰  âŠ¤ :=
  (hf.exists_order_ne_top_iff_forall âŸ¨Set.nonempty_of_mem hâ‚x, hUâŸ©).1 (by use âŸ¨x, hâ‚xâŸ©) âŸ¨y, hyâŸ©

section arithmetic

include hf in
lemma mono_set {V : Set ğ•œ} (hv : V âŠ† U) : MeromorphicOn f V := fun x hx â†¦ hf x (hv hx)

include hf hg in
lemma add : MeromorphicOn (f + g) U := fun x hx â†¦ (hf x hx).add (hg x hx)

include hf hg in
lemma sub : MeromorphicOn (f - g) U := fun x hx â†¦ (hf x hx).sub (hg x hx)

include hf in
lemma neg : MeromorphicOn (-f) U := fun x hx â†¦ (hf x hx).neg

@[simp] lemma neg_iff : MeromorphicOn (-f) U â†” MeromorphicOn f U :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, negâŸ©

include hs hf in
lemma smul : MeromorphicOn (s â€¢ f) U := fun x hx â†¦ (hs x hx).smul (hf x hx)

include hs ht in
lemma mul : MeromorphicOn (s * t) U := fun x hx â†¦ (hs x hx).mul (ht x hx)

include hs in
lemma inv : MeromorphicOn sâ»Â¹ U := fun x hx â†¦ (hs x hx).inv

@[simp] lemma inv_iff : MeromorphicOn sâ»Â¹ U â†” MeromorphicOn s U :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, invâŸ©

include hs ht in
lemma div : MeromorphicOn (s / t) U := fun x hx â†¦ (hs x hx).div (ht x hx)

include hs in
lemma pow (n : â„•) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).pow _

include hs in
lemma zpow (n : â„¤) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).zpow _

end arithmetic

include hf in
lemma congr (h_eq : Set.EqOn f g U) (hu : IsOpen U) : MeromorphicOn g U := by
  refine fun x hx â†¦ (hf x hx).congr (EventuallyEq.filter_mono ?_ nhdsWithin_le_nhds)
  exact eventually_of_mem (hu.mem_nhds hx) h_eq

theorem eventually_codiscreteWithin_analyticAt
    [CompleteSpace E] (f : ğ•œ â†’ E) (h : MeromorphicOn f U) :
    âˆ€á¶  (y : ğ•œ) in codiscreteWithin U, AnalyticAt ğ•œ f y := by
  rw [eventually_iff, mem_codiscreteWithin]
  intro x hx
  rw [disjoint_principal_right]
  apply Filter.mem_of_superset ((h x hx).eventually_analyticAt)
  intro x hx
  simp [hx]

end MeromorphicOn
