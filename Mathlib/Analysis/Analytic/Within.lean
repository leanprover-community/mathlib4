/-
Copyright (c) 2024 Geoffrey Irving. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Geoffrey Irving
-/
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Calculus.FDeriv.Analytic

/-!
# Properties of analyticity restricted to a set

From `Mathlib.Analysis.Analytic.Basic`, we have the definitons

1. `AnalyticWithinAt ğ•œ f s x` means a power series at `x` converges to `f` on `ğ“[s] x`, and
    `f` is continuous within `s` at `x`.
2. `AnalyticWithinOn ğ•œ f s t` means `âˆ€ x âˆˆ t, AnalyticWithinAt ğ•œ f s x`.

This means there exists an extension of `f` which is analytic and agrees with `f` on `s âˆª {x}`, but
`f` is allowed to be arbitrary elsewhere.  Requiring `ContinuousWithinAt` is essential if `x âˆ‰ s`:
it is required for composition and smoothness to follow without extra hypotheses (we could
alternately require convergence at `x` even if `x âˆ‰ s`).

Here we prove basic properties of these definitions. Where convenient we assume completeness of the
ambient space, which allows us to related `AnalyticWithinAt` to analyticity of a local extension.
-/

noncomputable section

open Topology Filter ENNReal

open Set Filter

variable {Î± : Type*}

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]

variable {E F G H : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G] [NormedAddCommGroup H]
  [NormedSpace ğ•œ H]

/-!
### Basic properties
-/

@[simp] lemma hasFPowerSeriesWithinOnBall_univ {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F}
    {x : E} {r : â„â‰¥0âˆ} :
    HasFPowerSeriesWithinOnBall f p univ x r â†” HasFPowerSeriesOnBall f p x r := by
  constructor
  Â· intro h
    exact âŸ¨h.r_le, h.r_pos, fun {y} m â†¦ h.hasSum (mem_univ _) mâŸ©
  Â· intro h
    refine âŸ¨h.r_le, h.r_pos, fun {y} _ m => h.hasSum m, ?_âŸ©
    exact (h.continuousOn.continuousAt (EMetric.ball_mem_nhds x h.r_pos)).continuousWithinAt

@[simp] lemma hasFPowerSeriesWithinAt_univ {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F} {x : E} :
    HasFPowerSeriesWithinAt f p univ x â†” HasFPowerSeriesAt f p x := by
  simp only [HasFPowerSeriesWithinAt, hasFPowerSeriesWithinOnBall_univ, HasFPowerSeriesAt]

@[simp] lemma analyticWithinAt_univ {f : E â†’ F} {x : E} :
    AnalyticWithinAt ğ•œ f univ x â†” AnalyticAt ğ•œ f x := by
  simp only [AnalyticWithinAt, hasFPowerSeriesWithinAt_univ, AnalyticAt]

lemma analyticWithinOn_univ {f : E â†’ F} :
    AnalyticWithinOn ğ•œ f univ â†” AnalyticOn ğ•œ f univ := by
  simp only [AnalyticWithinOn, analyticWithinAt_univ, AnalyticOn]

lemma HasFPowerSeriesWithinAt.continuousWithinAt {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F}
    {s : Set E} {x : E} (h : HasFPowerSeriesWithinAt f p s x) : ContinuousWithinAt f s x := by
  rcases h with âŸ¨r, hâŸ©
  exact h.continuousWithinAt

lemma AnalyticWithinAt.continuousWithinAt {f : E â†’ F} {s : Set E} {x : E}
    (h : AnalyticWithinAt ğ•œ f s x) : ContinuousWithinAt f s x := by
  rcases h with âŸ¨p, hâŸ©
  exact h.continuousWithinAt

/-- `AnalyticWithinAt` is trivial if `{x} âˆˆ ğ“[s] x` -/
lemma analyticWithinAt_of_singleton_mem {f : E â†’ F} {s : Set E} {x : E} (h : {x} âˆˆ ğ“[s] x) :
    AnalyticWithinAt ğ•œ f s x := by
  have fc : ContinuousWithinAt f s x :=
    Filter.Tendsto.mono_left (tendsto_pure_nhds _ _) (Filter.le_pure_iff.mpr h)
  rcases mem_nhdsWithin.mp h with âŸ¨t, ot, xt, stâŸ©
  rcases Metric.mem_nhds_iff.mp (ot.mem_nhds xt) with âŸ¨r, r0, rtâŸ©
  exact âŸ¨constFormalMultilinearSeries ğ•œ E (f x), .ofReal r, {
    r_le := by simp only [FormalMultilinearSeries.constFormalMultilinearSeries_radius, le_top]
    r_pos := by positivity
    hasSum := by
      intro y ys yr
      simp only [subset_singleton_iff, mem_inter_iff, and_imp] at st
      specialize st (x + y) (rt (by simpa using yr)) ys
      simp only [st]
      apply (hasFPowerSeriesOnBall_const (e := 0)).hasSum
      simp only [Metric.emetric_ball_top, mem_univ]
    continuousWithinAt := fc
  }âŸ©

/-- Analyticity implies analyticity within any `s` -/
lemma AnalyticAt.analyticWithinAt {f : E â†’ F} {s : Set E} {x : E} (h : AnalyticAt ğ•œ f x) :
    AnalyticWithinAt ğ•œ f s x := by
  rcases h with âŸ¨p, r, hpâŸ©
  exact âŸ¨p, r, {
    r_le := hp.r_le
    r_pos := hp.r_pos
    hasSum := fun {y} _ yr â†¦ hp.hasSum yr
    continuousWithinAt :=
      (hp.continuousOn.continuousAt (EMetric.ball_mem_nhds x hp.r_pos)).continuousWithinAt
  }âŸ©

/-- Analyticity on `s` implies analyticity within `s` -/
lemma AnalyticOn.analyticWithinOn {f : E â†’ F} {s : Set E} (h : AnalyticOn ğ•œ f s) :
    AnalyticWithinOn ğ•œ f s :=
  fun x m â†¦ (h x m).analyticWithinAt

lemma AnalyticWithinOn.continuousOn {f : E â†’ F} {s : Set E} (h : AnalyticWithinOn ğ•œ f s) :
    ContinuousOn f s :=
  fun x m â†¦ (h x m).continuousWithinAt

/-- If `f` is `AnalyticWithinOn` near each point in a set, it is `AnalyticWithinOn` the set -/
lemma analyticWithinOn_of_locally_analyticWithinOn {f : E â†’ F} {s : Set E}
    (h : âˆ€ x âˆˆ s, âˆƒ u, IsOpen u âˆ§ x âˆˆ u âˆ§ AnalyticWithinOn ğ•œ f (s âˆ© u)) :
    AnalyticWithinOn ğ•œ f s := by
  intro x m
  rcases h x m with âŸ¨u, ou, xu, fuâŸ©
  rcases Metric.mem_nhds_iff.mp (ou.mem_nhds xu) with âŸ¨r, r0, ruâŸ©
  rcases fu x âŸ¨m, xuâŸ© with âŸ¨p, t, fpâŸ©
  exact âŸ¨p, min (.ofReal r) t, {
    r_pos := lt_min (by positivity) fp.r_pos
    r_le := min_le_of_right_le fp.r_le
    hasSum := by
      intro y ys yr
      simp only [EMetric.mem_ball, lt_min_iff, edist_lt_ofReal, dist_zero_right] at yr
      apply fp.hasSum âŸ¨ys, ru ?_âŸ©
      Â· simp only [EMetric.mem_ball, yr]
      Â· simp only [Metric.mem_ball, dist_self_add_left, yr]
    continuousWithinAt := by
      refine (fu.continuousOn x âŸ¨m, xuâŸ©).mono_left (le_of_eq ?_)
      exact nhdsWithin_eq_nhdsWithin xu ou (by simp only [inter_assoc, inter_self])
  }âŸ©

/-- On open sets, `AnalyticOn` and `AnalyticWithinOn` coincide -/
@[simp] lemma IsOpen.analyticWithinOn_iff_analyticOn {f : E â†’ F} {s : Set E} (hs : IsOpen s) :
    AnalyticWithinOn ğ•œ f s â†” AnalyticOn ğ•œ f s := by
  refine âŸ¨?_, AnalyticOn.analyticWithinOnâŸ©
  intro hf x m
  rcases Metric.mem_nhds_iff.mp (hs.mem_nhds m) with âŸ¨r, r0, rsâŸ©
  rcases hf x m with âŸ¨p, t, fpâŸ©
  exact âŸ¨p, min (.ofReal r) t, {
    r_pos := lt_min (by positivity) fp.r_pos
    r_le := min_le_of_right_le fp.r_le
    hasSum := by
      intro y ym
      simp only [EMetric.mem_ball, lt_min_iff, edist_lt_ofReal, dist_zero_right] at ym
      refine fp.hasSum (rs ?_) ym.2
      simp only [Metric.mem_ball, dist_self_add_left, ym.1]
  }âŸ©

/-!
### Equivalence to analyticity of a local extension

We show that `HasFPowerSeriesWithinOnBall`, `HasFPowerSeriesWithinAt`, and `AnalyticWithinAt` are
equivalent to the existence of a local extension with full analyticity.  We do not yet show a
result for `AnalyticWithinOn`, as this requires a bit more work to show that local extensions can
be stitched together.
-/

/-- `f` has power series `p` at `x` iff some local extension of `f` has that series -/
lemma hasFPowerSeriesWithinOnBall_iff_exists_hasFPowerSeriesOnBall [CompleteSpace F] {f : E â†’ F}
    {p : FormalMultilinearSeries ğ•œ E F} {s : Set E} {x : E} {r : â„â‰¥0âˆ} :
    HasFPowerSeriesWithinOnBall f p s x r â†”
      ContinuousWithinAt f s x âˆ§ âˆƒ g, EqOn f g (s âˆ© EMetric.ball x r) âˆ§
        HasFPowerSeriesOnBall g p x r := by
  constructor
  Â· intro h
    refine âŸ¨h.continuousWithinAt, fun y â†¦ p.sum (y - x), ?_, ?_âŸ©
    Â· intro y âŸ¨ys,ybâŸ©
      simp only [EMetric.mem_ball, edist_eq_coe_nnnorm_sub] at yb
      have e0 := p.hasSum (x := y - x) ?_
      have e1 := (h.hasSum (y := y - x) ?_ ?_)
      Â· simp only [add_sub_cancel] at e1
        exact e1.unique e0
      Â· simpa only [add_sub_cancel]
      Â· simpa only [EMetric.mem_ball, edist_eq_coe_nnnorm]
      Â· simp only [EMetric.mem_ball, edist_eq_coe_nnnorm]
        exact lt_of_lt_of_le yb h.r_le
    Â· refine âŸ¨h.r_le, h.r_pos, ?_âŸ©
      intro y lt
      simp only [add_sub_cancel_left]
      apply p.hasSum
      simp only [EMetric.mem_ball] at lt âŠ¢
      exact lt_of_lt_of_le lt h.r_le
  Â· intro âŸ¨mem, g, hfg, hgâŸ©
    refine âŸ¨hg.r_le, hg.r_pos, ?_, memâŸ©
    intro y ys lt
    rw [hfg]
    Â· exact hg.hasSum lt
    Â· refine âŸ¨ys, ?_âŸ©
      simpa only [EMetric.mem_ball, edist_eq_coe_nnnorm_sub, add_sub_cancel_left, sub_zero] using lt

/-- `f` has power series `p` at `x` iff some local extension of `f` has that series -/
lemma hasFPowerSeriesWithinAt_iff_exists_hasFPowerSeriesAt [CompleteSpace F] {f : E â†’ F}
    {p : FormalMultilinearSeries ğ•œ E F} {s : Set E} {x : E} :
    HasFPowerSeriesWithinAt f p s x â†”
      ContinuousWithinAt f s x âˆ§ âˆƒ g, f =á¶ [ğ“[s] x] g âˆ§ HasFPowerSeriesAt g p x := by
  constructor
  Â· intro âŸ¨r, hâŸ©
    rcases hasFPowerSeriesWithinOnBall_iff_exists_hasFPowerSeriesOnBall.mp h with âŸ¨fc, g, e, hâŸ©
    refine âŸ¨fc, g, ?_, âŸ¨r, hâŸ©âŸ©
    refine Filter.eventuallyEq_iff_exists_mem.mpr âŸ¨_, ?_, eâŸ©
    exact inter_mem_nhdsWithin _ (EMetric.ball_mem_nhds _ h.r_pos)
  Â· intro âŸ¨mem, g, hfg, âŸ¨r, hgâŸ©âŸ©
    simp only [eventuallyEq_nhdsWithin_iff, Metric.eventually_nhds_iff] at hfg
    rcases hfg with âŸ¨e, e0, hfgâŸ©
    refine âŸ¨min r (.ofReal e), ?_âŸ©
    refine hasFPowerSeriesWithinOnBall_iff_exists_hasFPowerSeriesOnBall.mpr âŸ¨mem, g, ?_, ?_âŸ©
    Â· intro y âŸ¨ys, xyâŸ©
      refine hfg ?_ ys
      simp only [EMetric.mem_ball, lt_min_iff, edist_lt_ofReal] at xy
      exact xy.2
    Â· exact hg.mono (lt_min hg.r_pos (by positivity)) (min_le_left _ _)

/-- `f` is analytic within `s` at `x` iff some local extension of `f` is analytic at `x` -/
lemma analyticWithinAt_iff_exists_analyticAt [CompleteSpace F] {f : E â†’ F} {s : Set E} {x : E} :
    AnalyticWithinAt ğ•œ f s x â†”
      ContinuousWithinAt f s x âˆ§ âˆƒ g, f =á¶ [ğ“[s] x] g âˆ§ AnalyticAt ğ•œ g x := by
  simp only [AnalyticWithinAt, AnalyticAt, hasFPowerSeriesWithinAt_iff_exists_hasFPowerSeriesAt]
  tauto

/-- If `f` is analytic within `s` at `x`, some local extension of `f` is analytic at `x` -/
lemma AnalyticWithinAt.exists_analyticAt [CompleteSpace F] {f : E â†’ F} {s : Set E} {x : E}
    (h : AnalyticWithinAt ğ•œ f s x) : âˆƒ g, f x = g x âˆ§ f =á¶ [ğ“[s] x] g âˆ§ AnalyticAt ğ•œ g x := by
  by_cases s0 : ğ“[s] x = âŠ¥
  Â· refine âŸ¨fun _ â†¦ f x, rfl, ?_, analyticAt_constâŸ©
    simp only [EventuallyEq, s0, eventually_bot]
  Â· rcases analyticWithinAt_iff_exists_analyticAt.mp h with âŸ¨_, g, fg, hgâŸ©
    refine âŸ¨g, ?_, fg, hgâŸ©
    exact tendsto_nhds_unique' âŸ¨s0âŸ© h.continuousWithinAt
      (hg.continuousAt.continuousWithinAt.congr' fg.symm)

/-!
### Congruence

We require completeness to use equivalence to locally extensions, but this is nonessential.
-/

lemma AnalyticWithinAt.congr_of_eventuallyEq [CompleteSpace F] {f g : E â†’ F} {s : Set E} {x : E}
    (hf : AnalyticWithinAt ğ•œ f s x) (hs : f =á¶ [ğ“[s] x] g) (hx : f x = g x) :
    AnalyticWithinAt ğ•œ g s x := by
  rcases hf.exists_analyticAt with âŸ¨f', fx, ef, hf'âŸ©
  rw [analyticWithinAt_iff_exists_analyticAt]
  have eg := hs.symm.trans ef
  refine âŸ¨?_, f', eg, hf'âŸ©
  exact hf'.continuousAt.continuousWithinAt.congr_of_eventuallyEq eg (hx.symm.trans fx)

lemma AnalyticWithinAt.congr [CompleteSpace F] {f g : E â†’ F} {s : Set E} {x : E}
    (hf : AnalyticWithinAt ğ•œ f s x) (hs : EqOn f g s) (hx : f x = g x) :
    AnalyticWithinAt ğ•œ g s x :=
  hf.congr_of_eventuallyEq hs.eventuallyEq_nhdsWithin hx

lemma AnalyticWithinOn.congr [CompleteSpace F] {f g : E â†’ F} {s : Set E}
    (hf : AnalyticWithinOn ğ•œ f s) (hs : EqOn f g s) :
    AnalyticWithinOn ğ•œ g s :=
  fun x m â†¦ (hf x m).congr hs (hs m)

/-!
### Monotonicity w.r.t. the set we're analytic within
-/

lemma HasFPowerSeriesWithinOnBall.mono {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F}
    {s t : Set E} {x : E} {r : â„â‰¥0âˆ} (h : HasFPowerSeriesWithinOnBall f p t x r)
    (hs : s âŠ† t) : HasFPowerSeriesWithinOnBall f p s x r where
  r_le := h.r_le
  r_pos := h.r_pos
  hasSum {_} ys yb := h.hasSum (hs ys) yb
  continuousWithinAt := h.continuousWithinAt.mono hs

lemma HasFPowerSeriesWithinAt.mono {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F}
    {s t : Set E} {x : E} (h : HasFPowerSeriesWithinAt f p t x)
    (hs : s âŠ† t) : HasFPowerSeriesWithinAt f p s x := by
  rcases h with âŸ¨r, hrâŸ©
  exact âŸ¨r, hr.mono hsâŸ©

lemma AnalyticWithinAt.mono {f : E â†’ F} {s t : Set E} {x : E} (h : AnalyticWithinAt ğ•œ f t x)
    (hs : s âŠ† t) : AnalyticWithinAt ğ•œ f s x := by
  rcases h with âŸ¨p, hpâŸ©
  exact âŸ¨p, hp.mono hsâŸ©

lemma AnalyticWithinOn.mono {f : E â†’ F} {s t : Set E} (h : AnalyticWithinOn ğ•œ f t)
    (hs : s âŠ† t) : AnalyticWithinOn ğ•œ f s :=
  fun _ m â†¦ (h _ (hs m)).mono hs

/-!
### Analyticity within respects composition

Currently we require `CompleteSpace`s to use equivalence to local extensions, but this is not
essential.
-/

lemma AnalyticWithinAt.comp [CompleteSpace F] [CompleteSpace G] {f : F â†’ G} {g : E â†’ F} {s : Set F}
    {t : Set E} {x : E} (hf : AnalyticWithinAt ğ•œ f s (g x)) (hg : AnalyticWithinAt ğ•œ g t x)
    (h : MapsTo g t s) : AnalyticWithinAt ğ•œ (f âˆ˜ g) t x := by
  rcases hf.exists_analyticAt with âŸ¨f', _, ef, hf'âŸ©
  rcases hg.exists_analyticAt with âŸ¨g', gx, eg, hg'âŸ©
  refine analyticWithinAt_iff_exists_analyticAt.mpr âŸ¨?_, f' âˆ˜ g', ?_, ?_âŸ©
  Â· exact hf.continuousWithinAt.comp hg.continuousWithinAt h
  Â· have gt := hg.continuousWithinAt.tendsto_nhdsWithin h
    filter_upwards [eg, gt.eventually ef]
    intro y gy fgy
    simp only [Function.comp_apply, fgy, â† gy]
  Â· exact hf'.comp_of_eq hg' gx.symm

lemma AnalyticWithinOn.comp [CompleteSpace F] [CompleteSpace G] {f : F â†’ G} {g : E â†’ F} {s : Set F}
    {t : Set E} (hf : AnalyticWithinOn ğ•œ f s) (hg : AnalyticWithinOn ğ•œ g t) (h : MapsTo g t s) :
    AnalyticWithinOn ğ•œ (f âˆ˜ g) t :=
  fun x m â†¦ (hf _ (h m)).comp (hg x m) h

/-!
### Analyticity within implies smoothness
-/

lemma AnalyticWithinAt.contDiffWithinAt [CompleteSpace F] {f : E â†’ F} {s : Set E} {x : E}
    (h : AnalyticWithinAt ğ•œ f s x) {n : â„•âˆ} : ContDiffWithinAt ğ•œ n f s x := by
  rcases h.exists_analyticAt with âŸ¨g, fx, fg, hgâŸ©
  exact hg.contDiffAt.contDiffWithinAt.congr_of_eventuallyEq fg fx

lemma AnalyticWithinOn.contDiffOn [CompleteSpace F] {f : E â†’ F} {s : Set E}
    (h : AnalyticWithinOn ğ•œ f s) {n : â„•âˆ} : ContDiffOn ğ•œ n f s :=
  fun x m â†¦ (h x m).contDiffWithinAt

/-!
### Analyticity within respects products
-/

lemma HasFPowerSeriesWithinOnBall.prod {e : E} {f : E â†’ F} {g : E â†’ G} {s : Set E} {r t : â„â‰¥0âˆ}
    {p : FormalMultilinearSeries ğ•œ E F} {q : FormalMultilinearSeries ğ•œ E G}
    (hf : HasFPowerSeriesWithinOnBall f p s e r) (hg : HasFPowerSeriesWithinOnBall g q s e t) :
    HasFPowerSeriesWithinOnBall (fun x â†¦ (f x, g x)) (p.prod q) s e (min r t) where
  r_le := by
    rw [p.radius_prod_eq_min]
    exact min_le_min hf.r_le hg.r_le
  r_pos := lt_min hf.r_pos hg.r_pos
  hasSum := by
    intro y m hy
    simp_rw [FormalMultilinearSeries.prod, ContinuousMultilinearMap.prod_apply]
    refine (hf.hasSum m ?_).prod_mk (hg.hasSum m ?_)
    Â· exact EMetric.mem_ball.mpr (lt_of_lt_of_le hy (min_le_left _ _))
    Â· exact EMetric.mem_ball.mpr (lt_of_lt_of_le hy (min_le_right _ _))
  continuousWithinAt := hf.continuousWithinAt.prod hg.continuousWithinAt

lemma HasFPowerSeriesWithinAt.prod {e : E} {f : E â†’ F} {g : E â†’ G} {s : Set E}
    {p : FormalMultilinearSeries ğ•œ E F} {q : FormalMultilinearSeries ğ•œ E G}
    (hf : HasFPowerSeriesWithinAt f p s e) (hg : HasFPowerSeriesWithinAt g q s e) :
    HasFPowerSeriesWithinAt (fun x â†¦ (f x, g x)) (p.prod q) s e := by
  rcases hf with âŸ¨_, hfâŸ©
  rcases hg with âŸ¨_, hgâŸ©
  exact âŸ¨_, hf.prod hgâŸ©

lemma AnalyticWithinAt.prod {e : E} {f : E â†’ F} {g : E â†’ G} {s : Set E}
    (hf : AnalyticWithinAt ğ•œ f s e) (hg : AnalyticWithinAt ğ•œ g s e) :
    AnalyticWithinAt ğ•œ (fun x â†¦ (f x, g x)) s e := by
  rcases hf with âŸ¨_, hfâŸ©
  rcases hg with âŸ¨_, hgâŸ©
  exact âŸ¨_, hf.prod hgâŸ©

lemma AnalyticWithinOn.prod {f : E â†’ F} {g : E â†’ G} {s : Set E}
    (hf : AnalyticWithinOn ğ•œ f s) (hg : AnalyticWithinOn ğ•œ g s) :
    AnalyticWithinOn ğ•œ (fun x â†¦ (f x, g x)) s :=
  fun x hx â†¦ (hf x hx).prod (hg x hx)
