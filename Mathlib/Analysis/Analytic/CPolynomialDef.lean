/-
Copyright (c) 2023 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel
-/
import Mathlib.Analysis.Analytic.ChangeOrigin

/-! We specialize the theory of analytic functions to the case of functions that admit a
development given by a *finite* formal multilinear series. We call them "continuously polynomial",
which is abbreviated to `CPolynomial`. One reason to do that is that we no longer need a
completeness assumption on the target space `F` to make the series converge, so some of the results
are more general. The class of continuously polynomial functions includes functions defined by
polynomials on a normed `ğ•œ`-algebra and continuous multilinear maps.

## Main definitions

Let `p` be a formal multilinear series from `E` to `F`, i.e., `p n` is a multilinear map on `E^n`
for `n : â„•`, and let `f` be a function from `E` to `F`.

* `HasFiniteFPowerSeriesOnBall f p x n r`: on the ball of center `x` with radius `r`,
  `f (x + y) = âˆ‘'_n pâ‚˜ yáµ`, and moreover `pâ‚˜ = 0` if `n â‰¤ m`.
* `HasFiniteFPowerSeriesAt f p x n`: on some ball of center `x` with positive radius, holds
  `HasFiniteFPowerSeriesOnBall f p x n r`.
* `CPolynomialAt ğ•œ f x`: there exists a power series `p` and a natural number `n` such that
  holds `HasFPowerSeriesAt f p x n`.
* `CPolynomialOn ğ•œ f s`: the function `f` is analytic at every point of `s`.

In this file, we develop the basic properties of these notions, notably:
* If a function is continuously polynomial, then it is analytic, see
  `HasFiniteFPowerSeriesOnBall.hasFPowerSeriesOnBall`, `HasFiniteFPowerSeriesAt.hasFPowerSeriesAt`,
  `CPolynomialAt.analyticAt` and `CPolynomialOn.analyticOnNhd`.
* The sum of a finite formal power series with positive radius is well defined on the whole space,
  see `FormalMultilinearSeries.hasFiniteFPowerSeriesOnBall_of_finite`.
* If a function admits a finite power series in a ball, then it is continuously polynomial at
  any point `y` of this ball, and the power series there can be expressed in terms of the initial
  power series `p` as `p.changeOrigin y`, which is finite (with the same bound as `p`) by
  `changeOrigin_finite_of_finite`. See `HasFiniteFPowerSeriesOnBall.changeOrigin`. It follows in
  particular that the set of points at which a given function is continuously polynomial is open,
  see `isOpen_cpolynomialAt`.

More API is available in the file `Mathlib/Analysis/Analytic/CPolynomial.lean`, with heavier
imports.
-/

variable {ğ•œ E F G : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedAddCommGroup G] [NormedSpace ğ•œ G]

open scoped Topology
open Set Filter Asymptotics NNReal ENNReal

variable {f g : E â†’ F} {p pf pg : FormalMultilinearSeries ğ•œ E F} {x : E} {r r' : â„â‰¥0âˆ} {n m : â„•}

section FiniteFPowerSeries

/-- Given a function `f : E â†’ F`, a formal multilinear series `p` and `n : â„•`, we say that
`f` has `p` as a finite power series on the ball of radius `r > 0` around `x` if
`f (x + y) = âˆ‘' pâ‚˜ yáµ` for all `â€–yâ€– < r` and `pâ‚™ = 0` for `n â‰¤ m`. -/
structure HasFiniteFPowerSeriesOnBall (f : E â†’ F) (p : FormalMultilinearSeries ğ•œ E F) (x : E)
    (n : â„•) (r : â„â‰¥0âˆ) : Prop extends HasFPowerSeriesOnBall f p x r where
  finite : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0

theorem HasFiniteFPowerSeriesOnBall.mk' {f : E â†’ F} {p : FormalMultilinearSeries ğ•œ E F} {x : E}
    {n : â„•} {r : â„â‰¥0âˆ} (finite : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (pos : 0 < r)
    (sum_eq : âˆ€ y âˆˆ EMetric.ball 0 r, (âˆ‘ i âˆˆ Finset.range n, p i fun _ â†¦ y) = f (x + y)) :
    HasFiniteFPowerSeriesOnBall f p x n r where
  r_le := p.radius_eq_top_of_eventually_eq_zero (Filter.eventually_atTop.mpr âŸ¨n, finiteâŸ©) â–¸ le_top
  r_pos := pos
  hasSum hy := sum_eq _ hy â–¸ hasSum_sum_of_ne_finset_zero fun m hm â†¦ by
    rw [Finset.mem_range, not_lt] at hm; rw [finite m hm]; rfl
  finite := finite

/-- Given a function `f : E â†’ F`, a formal multilinear series `p` and `n : â„•`, we say that
`f` has `p` as a finite power series around `x` if `f (x + y) = âˆ‘' pâ‚™ yâ¿` for all `y` in a
neighborhood of `0`and `pâ‚™ = 0` for `n â‰¤ m`. -/
def HasFiniteFPowerSeriesAt (f : E â†’ F) (p : FormalMultilinearSeries ğ•œ E F) (x : E) (n : â„•) :=
  âˆƒ r, HasFiniteFPowerSeriesOnBall f p x n r

theorem HasFiniteFPowerSeriesAt.hasFPowerSeriesAt
    (hf : HasFiniteFPowerSeriesAt f p x n) : HasFPowerSeriesAt f p x :=
  let âŸ¨r, hfâŸ© := hf
  âŸ¨r, hf.toHasFPowerSeriesOnBallâŸ©

theorem HasFiniteFPowerSeriesAt.finite (hf : HasFiniteFPowerSeriesAt f p x n) :
    âˆ€ m : â„•, n â‰¤ m â†’ p m = 0 := let âŸ¨_, hfâŸ© := hf; hf.finite

variable (ğ•œ)

/-- Given a function `f : E â†’ F`, we say that `f` is continuously polynomial (cpolynomial)
at `x` if it admits a finite power series expansion around `x`. -/
def CPolynomialAt (f : E â†’ F) (x : E) :=
  âˆƒ (p : FormalMultilinearSeries ğ•œ E F) (n : â„•), HasFiniteFPowerSeriesAt f p x n

/-- Given a function `f : E â†’ F`, we say that `f` is continuously polynomial on a set `s`
if it is continuously polynomial around every point of `s`. -/
def CPolynomialOn (f : E â†’ F) (s : Set E) :=
  âˆ€ x, x âˆˆ s â†’ CPolynomialAt ğ•œ f x

variable {ğ•œ}

theorem HasFiniteFPowerSeriesOnBall.hasFiniteFPowerSeriesAt
    (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    HasFiniteFPowerSeriesAt f p x n :=
  âŸ¨r, hfâŸ©

theorem HasFiniteFPowerSeriesAt.cpolynomialAt (hf : HasFiniteFPowerSeriesAt f p x n) :
    CPolynomialAt ğ•œ f x :=
  âŸ¨p, n, hfâŸ©

theorem HasFiniteFPowerSeriesOnBall.cpolynomialAt (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    CPolynomialAt ğ•œ f x :=
  hf.hasFiniteFPowerSeriesAt.cpolynomialAt

theorem CPolynomialAt.analyticAt (hf : CPolynomialAt ğ•œ f x) : AnalyticAt ğ•œ f x :=
  let âŸ¨p, _, hpâŸ© := hf
  âŸ¨p, hp.hasFPowerSeriesAtâŸ©

theorem CPolynomialAt.analyticWithinAt {s : Set E} (hf : CPolynomialAt ğ•œ f x) :
    AnalyticWithinAt ğ•œ f s x :=
  hf.analyticAt.analyticWithinAt

theorem CPolynomialOn.analyticOnNhd {s : Set E} (hf : CPolynomialOn ğ•œ f s) : AnalyticOnNhd ğ•œ f s :=
  fun x hx â†¦ (hf x hx).analyticAt

theorem CPolynomialOn.analyticOn {s : Set E} (hf : CPolynomialOn ğ•œ f s) : AnalyticOn ğ•œ f s :=
  hf.analyticOnNhd.analyticOn

theorem HasFiniteFPowerSeriesOnBall.congr (hf : HasFiniteFPowerSeriesOnBall f p x n r)
    (hg : EqOn f g (EMetric.ball x r)) : HasFiniteFPowerSeriesOnBall g p x n r :=
  âŸ¨hf.1.congr hg, hf.finiteâŸ©

theorem HasFiniteFPowerSeriesOnBall.of_le {m n : â„•}
    (h : HasFiniteFPowerSeriesOnBall f p x n r) (hmn : n â‰¤ m) :
    HasFiniteFPowerSeriesOnBall f p x m r :=
  âŸ¨h.toHasFPowerSeriesOnBall, fun i hi â†¦ h.finite i (hmn.trans hi)âŸ©

theorem HasFiniteFPowerSeriesAt.of_le {m n : â„•}
    (h : HasFiniteFPowerSeriesAt f p x n) (hmn : n â‰¤ m) :
    HasFiniteFPowerSeriesAt f p x m := by
  rcases h with âŸ¨r, hrâŸ©
  exact âŸ¨r, hr.of_le hmnâŸ©

/-- If a function `f` has a finite power series `p` around `x`, then the function
`z â†¦ f (z - y)` has the same finite power series around `x + y`. -/
theorem HasFiniteFPowerSeriesOnBall.comp_sub (hf : HasFiniteFPowerSeriesOnBall f p x n r) (y : E) :
    HasFiniteFPowerSeriesOnBall (fun z => f (z - y)) p (x + y) n r :=
  âŸ¨hf.1.comp_sub y, hf.finiteâŸ©

theorem HasFiniteFPowerSeriesOnBall.mono (hf : HasFiniteFPowerSeriesOnBall f p x n r)
    (r'_pos : 0 < r') (hr : r' â‰¤ r) : HasFiniteFPowerSeriesOnBall f p x n r' :=
  âŸ¨hf.1.mono r'_pos hr, hf.finiteâŸ©

theorem HasFiniteFPowerSeriesAt.congr (hf : HasFiniteFPowerSeriesAt f p x n) (hg : f =á¶ [ğ“ x] g) :
    HasFiniteFPowerSeriesAt g p x n :=
  Exists.imp (fun _ hg â†¦ âŸ¨hg, hf.finiteâŸ©) (hf.hasFPowerSeriesAt.congr hg)

protected theorem HasFiniteFPowerSeriesAt.eventually (hf : HasFiniteFPowerSeriesAt f p x n) :
    âˆ€á¶  r : â„â‰¥0âˆ in ğ“[>] 0, HasFiniteFPowerSeriesOnBall f p x n r :=
  hf.hasFPowerSeriesAt.eventually.mono fun _ h â†¦ âŸ¨h, hf.finiteâŸ©

theorem CPolynomialAt.congr (hf : CPolynomialAt ğ•œ f x) (hg : f =á¶ [ğ“ x] g) : CPolynomialAt ğ•œ g x :=
  let âŸ¨_, _, hpfâŸ© := hf
  (hpf.congr hg).cpolynomialAt

theorem CPolynomialAt_congr (h : f =á¶ [ğ“ x] g) : CPolynomialAt ğ•œ f x â†” CPolynomialAt ğ•œ g x :=
  âŸ¨fun hf â†¦ hf.congr h, fun hg â†¦ hg.congr h.symmâŸ©

theorem CPolynomialOn.mono {s t : Set E} (hf : CPolynomialOn ğ•œ f t) (hst : s âŠ† t) :
    CPolynomialOn ğ•œ f s :=
  fun z hz => hf z (hst hz)

theorem CPolynomialOn.congr' {s : Set E} (hf : CPolynomialOn ğ•œ f s) (hg : f =á¶ [ğ“Ë¢ s] g) :
    CPolynomialOn ğ•œ g s :=
  fun z hz => (hf z hz).congr (mem_nhdsSet_iff_forall.mp hg z hz)

theorem CPolynomialOn_congr' {s : Set E} (h : f =á¶ [ğ“Ë¢ s] g) :
    CPolynomialOn ğ•œ f s â†” CPolynomialOn ğ•œ g s :=
  âŸ¨fun hf => hf.congr' h, fun hg => hg.congr' h.symmâŸ©

theorem CPolynomialOn.congr {s : Set E} (hs : IsOpen s) (hf : CPolynomialOn ğ•œ f s)
    (hg : s.EqOn f g) : CPolynomialOn ğ•œ g s :=
  hf.congr' <| mem_nhdsSet_iff_forall.mpr
    (fun _ hz => eventuallyEq_iff_exists_mem.mpr âŸ¨s, hs.mem_nhds hz, hgâŸ©)

theorem CPolynomialOn_congr {s : Set E} (hs : IsOpen s) (h : s.EqOn f g) :
    CPolynomialOn ğ•œ f s â†” CPolynomialOn ğ•œ g s :=
  âŸ¨fun hf => hf.congr hs h, fun hg => hg.congr hs h.symmâŸ©

/-- If a function `f` has a finite power series `p` on a ball and `g` is a continuous linear map,
then `g âˆ˜ f` has the finite power series `g âˆ˜ p` on the same ball. -/
theorem ContinuousLinearMap.comp_hasFiniteFPowerSeriesOnBall (g : F â†’L[ğ•œ] G)
    (h : HasFiniteFPowerSeriesOnBall f p x n r) :
    HasFiniteFPowerSeriesOnBall (g âˆ˜ f) (g.compFormalMultilinearSeries p) x n r :=
  âŸ¨g.comp_hasFPowerSeriesOnBall h.1, fun m hm â†¦ by
    rw [compFormalMultilinearSeries_apply, h.finite m hm]
    ext; exact map_zero gâŸ©

/-- If a function `f` is continuously polynomial on a set `s` and `g` is a continuous linear map,
then `g âˆ˜ f` is continuously polynomial on `s`. -/
theorem ContinuousLinearMap.comp_cpolynomialOn {s : Set E} (g : F â†’L[ğ•œ] G)
    (h : CPolynomialOn ğ•œ f s) : CPolynomialOn ğ•œ (g âˆ˜ f) s := by
  rintro x hx
  rcases h x hx with âŸ¨p, n, r, hpâŸ©
  exact âŸ¨g.compFormalMultilinearSeries p, n, r, g.comp_hasFiniteFPowerSeriesOnBall hpâŸ©

/-- If a function admits a finite power series expansion bounded by `n`, then it is equal to
the `m`th partial sums of this power series at every point of the disk for `n â‰¤ m`. -/
theorem HasFiniteFPowerSeriesOnBall.eq_partialSum
    (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    âˆ€ y âˆˆ EMetric.ball (0 : E) r, âˆ€ m, n â‰¤ m â†’
    f (x + y) = p.partialSum m y :=
  fun y hy m hm â†¦ (hf.hasSum hy).unique (hasSum_sum_of_ne_finset_zero
    (f := fun m => p m (fun _ => y)) (s := Finset.range m)
    (fun N hN => by simp only; simp only [Finset.mem_range, not_lt] at hN
                    rw [hf.finite _ (le_trans hm hN), ContinuousMultilinearMap.zero_apply]))

/-- Variant of the previous result with the variable expressed as `y` instead of `x + y`. -/
theorem HasFiniteFPowerSeriesOnBall.eq_partialSum'
    (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    âˆ€ y âˆˆ EMetric.ball x r, âˆ€ m, n â‰¤ m â†’
    f y = p.partialSum m (y - x) := by
  intro y hy m hm
  rw [EMetric.mem_ball, edist_eq_enorm_sub, â† mem_emetric_ball_zero_iff] at hy
  rw [â† (HasFiniteFPowerSeriesOnBall.eq_partialSum hf _ hy m hm), add_sub_cancel]

/-! The particular cases where `f` has a finite power series bounded by `0` or `1`. -/

/-- If `f` has a formal power series on a ball bounded by `0`, then `f` is equal to `0` on
the ball. -/
theorem HasFiniteFPowerSeriesOnBall.eq_zero_of_bound_zero
    (hf : HasFiniteFPowerSeriesOnBall f pf x 0 r) : âˆ€ y âˆˆ EMetric.ball x r, f y = 0 := by
  intro y hy
  rw [hf.eq_partialSum' y hy 0 le_rfl, FormalMultilinearSeries.partialSum]
  simp only [Finset.range_zero, Finset.sum_empty]

theorem HasFiniteFPowerSeriesOnBall.bound_zero_of_eq_zero (hf : âˆ€ y âˆˆ EMetric.ball x r, f y = 0)
    (r_pos : 0 < r) (hp : âˆ€ n, p n = 0) : HasFiniteFPowerSeriesOnBall f p x 0 r := by
  refine âŸ¨âŸ¨?_, r_pos, ?_âŸ©, fun n _ â†¦ hp nâŸ©
  Â· rw [p.radius_eq_top_of_forall_image_add_eq_zero 0 (fun n â†¦ by rw [add_zero]; exact hp n)]
    exact le_top
  Â· intro y hy
    rw [hf (x + y)]
    Â· convert hasSum_zero
      rw [hp, ContinuousMultilinearMap.zero_apply]
    Â· rwa [EMetric.mem_ball, edist_eq_enorm_sub, add_comm, add_sub_cancel_right,
        â† edist_zero_eq_enorm, â† EMetric.mem_ball]

/-- If `f` has a formal power series at `x` bounded by `0`, then `f` is equal to `0` in a
neighborhood of `x`. -/
theorem HasFiniteFPowerSeriesAt.eventually_zero_of_bound_zero
    (hf : HasFiniteFPowerSeriesAt f pf x 0) : f =á¶ [ğ“ x] 0 :=
  Filter.eventuallyEq_iff_exists_mem.mpr (let âŸ¨r, hfâŸ© := hf; âŸ¨EMetric.ball x r,
    EMetric.ball_mem_nhds x hf.r_pos, fun y hy â†¦ hf.eq_zero_of_bound_zero y hyâŸ©)

/-- If `f` has a formal power series on a ball bounded by `1`, then `f` is constant equal
to `f x` on the ball. -/
theorem HasFiniteFPowerSeriesOnBall.eq_const_of_bound_one
    (hf : HasFiniteFPowerSeriesOnBall f pf x 1 r) : âˆ€ y âˆˆ EMetric.ball x r, f y = f x := by
  intro y hy
  rw [hf.eq_partialSum' y hy 1 le_rfl, hf.eq_partialSum' x
    (by rw [EMetric.mem_ball, edist_self]; exact hf.r_pos) 1 le_rfl]
  simp only [FormalMultilinearSeries.partialSum, Finset.range_one, Finset.sum_singleton]
  congr
  apply funext
  simp only [IsEmpty.forall_iff]

/-- If `f` has a formal power series at x bounded by `1`, then `f` is constant equal
to `f x` in a neighborhood of `x`. -/
theorem HasFiniteFPowerSeriesAt.eventually_const_of_bound_one
    (hf : HasFiniteFPowerSeriesAt f pf x 1) : f =á¶ [ğ“ x] (fun _ => f x) :=
  Filter.eventuallyEq_iff_exists_mem.mpr (let âŸ¨r, hfâŸ© := hf; âŸ¨EMetric.ball x r,
    EMetric.ball_mem_nhds x hf.r_pos, fun y hy â†¦ hf.eq_const_of_bound_one y hyâŸ©)

/-- If a function admits a finite power series expansion on a disk, then it is continuous there. -/
protected theorem HasFiniteFPowerSeriesOnBall.continuousOn
    (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    ContinuousOn f (EMetric.ball x r) := hf.1.continuousOn

protected theorem HasFiniteFPowerSeriesAt.continuousAt (hf : HasFiniteFPowerSeriesAt f p x n) :
    ContinuousAt f x := hf.hasFPowerSeriesAt.continuousAt

protected theorem CPolynomialAt.continuousAt (hf : CPolynomialAt ğ•œ f x) : ContinuousAt f x :=
  hf.analyticAt.continuousAt

protected theorem CPolynomialOn.continuousOn {s : Set E} (hf : CPolynomialOn ğ•œ f s) :
    ContinuousOn f s :=
  hf.analyticOnNhd.continuousOn

/-- Continuously polynomial everywhere implies continuous -/
theorem CPolynomialOn.continuous {f : E â†’ F} (fa : CPolynomialOn ğ•œ f univ) : Continuous f := by
  rw [â† continuousOn_univ]; exact fa.continuousOn

protected theorem FormalMultilinearSeries.sum_of_finite (p : FormalMultilinearSeries ğ•œ E F)
    {n : â„•} (hn : âˆ€ m, n â‰¤ m â†’ p m = 0) (x : E) :
    p.sum x = p.partialSum n x :=
  tsum_eq_sum fun m hm â†¦ by rw [Finset.mem_range, not_lt] at hm; rw [hn m hm]; rfl

/-- A finite formal multilinear series sums to its sum at every point. -/
protected theorem FormalMultilinearSeries.hasSum_of_finite (p : FormalMultilinearSeries ğ•œ E F)
    {n : â„•} (hn : âˆ€ m, n â‰¤ m â†’ p m = 0) (x : E) :
    HasSum (fun n : â„• => p n fun _ => x) (p.sum x) :=
  summable_of_ne_finset_zero (s := .range n)
    (fun m hm â†¦ by rw [Finset.mem_range, not_lt] at hm; rw [hn m hm]; rfl)
    |>.hasSum

/-- The sum of a finite power series `p` admits `p` as a power series. -/
protected theorem FormalMultilinearSeries.hasFiniteFPowerSeriesOnBall_of_finite
    (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (hn : âˆ€ m, n â‰¤ m â†’ p m = 0) :
    HasFiniteFPowerSeriesOnBall p.sum p 0 n âŠ¤ where
  r_le := by rw [radius_eq_top_of_forall_image_add_eq_zero p n fun _ => hn _ (Nat.le_add_left _ _)]
  r_pos := zero_lt_top
  finite := hn
  hasSum {y} _ := by rw [zero_add]; exact p.hasSum_of_finite hn y

theorem HasFiniteFPowerSeriesOnBall.sum (h : HasFiniteFPowerSeriesOnBall f p x n r) {y : E}
    (hy : y âˆˆ EMetric.ball (0 : E) r) : f (x + y) = p.sum y :=
  (h.hasSum hy).tsum_eq.symm

/-- The sum of a finite power series is continuous. -/
protected theorem FormalMultilinearSeries.continuousOn_of_finite
    (p : FormalMultilinearSeries ğ•œ E F) {n : â„•} (hn : âˆ€ m, n â‰¤ m â†’ p m = 0) :
    Continuous p.sum := by
  rw [â† continuousOn_univ, â† Metric.emetric_ball_top]
  exact (p.hasFiniteFPowerSeriesOnBall_of_finite hn).continuousOn

end FiniteFPowerSeries

namespace FormalMultilinearSeries

section

/-! We study what happens when we change the origin of a finite formal multilinear series `p`. The
main point is that the new series `p.changeOrigin x` is still finite, with the same bound. -/

/-- If `p` is a formal multilinear series such that `p m = 0` for `n â‰¤ m`, then
`p.changeOriginSeriesTerm k l = 0` for `n â‰¤ k + l`. -/
lemma changeOriginSeriesTerm_bound (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (k l : â„•) {s : Finset (Fin (k + l))}
    (hs : s.card = l) (hkl : n â‰¤ k + l) :
    p.changeOriginSeriesTerm k l s hs = 0 := by
  rw [changeOriginSeriesTerm, hn _ hkl, map_zero]

/-- If `p` is a finite formal multilinear series, then so is `p.changeOriginSeries k` for every
`k` in `â„•`. More precisely, if `p m = 0` for `n â‰¤ m`, then `p.changeOriginSeries k m = 0` for
`n â‰¤ k + m`. -/
lemma changeOriginSeries_finite_of_finite (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (k : â„•) : âˆ€ {m : â„•}, n â‰¤ k + m â†’
    p.changeOriginSeries k m = 0 := by
  intro m hm
  rw [changeOriginSeries]
  exact Finset.sum_eq_zero (fun _ _ => p.changeOriginSeriesTerm_bound hn _ _ _ hm)

lemma changeOriginSeries_sum_eq_partialSum_of_finite (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (k : â„•) :
    (p.changeOriginSeries k).sum = (p.changeOriginSeries k).partialSum (n - k) := by
  ext x
  rw [partialSum, FormalMultilinearSeries.sum,
    tsum_eq_sum (f := fun m => p.changeOriginSeries k m (fun _ => x)) (s := Finset.range (n - k))]
  intro m hm
  rw [Finset.mem_range, not_lt] at hm
  rw [p.changeOriginSeries_finite_of_finite hn k (by rw [add_comm]; exact Nat.le_add_of_sub_le hm),
    ContinuousMultilinearMap.zero_apply]

/-- If `p` is a formal multilinear series such that `p m = 0` for `n â‰¤ m`, then
`p.changeOrigin x k = 0` for `n â‰¤ k`. -/
lemma changeOrigin_finite_of_finite (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) {k : â„•} (hk : n â‰¤ k) :
    p.changeOrigin x k = 0 := by
  rw [changeOrigin, p.changeOriginSeries_sum_eq_partialSum_of_finite hn]
  apply Finset.sum_eq_zero
  intro m hm
  rw [Finset.mem_range] at hm
  rw [p.changeOriginSeries_finite_of_finite hn k (le_add_of_le_left hk),
    ContinuousMultilinearMap.zero_apply]

theorem hasFiniteFPowerSeriesOnBall_changeOrigin (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (k : â„•) (hn : âˆ€ (m : â„•), n + k â‰¤ m â†’ p m = 0) :
    HasFiniteFPowerSeriesOnBall (p.changeOrigin Â· k) (p.changeOriginSeries k) 0 n âŠ¤ :=
  (p.changeOriginSeries k).hasFiniteFPowerSeriesOnBall_of_finite
    (fun _ hm => p.changeOriginSeries_finite_of_finite hn k
    (by rw [add_comm n k]; apply add_le_add_left hm))

theorem changeOrigin_eval_of_finite (p : FormalMultilinearSeries ğ•œ E F) {n : â„•}
    (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (x y : E) :
    (p.changeOrigin x).sum y = p.sum (x + y) := by
  let f (s : Î£ k l : â„•, { s : Finset (Fin (k + l)) // s.card = l }) : F :=
    p.changeOriginSeriesTerm s.1 s.2.1 s.2.2 s.2.2.2 (fun _ â†¦ x) fun _ â†¦ y
  have finsupp : f.support.Finite := by
    apply Set.Finite.subset (s := changeOriginIndexEquiv â»Â¹' (Sigma.fst â»Â¹' {m | m < n}))
    Â· apply Set.Finite.preimage (Equiv.injective _).injOn
      simp_rw [â† {m | m < n}.iUnion_of_singleton_coe, preimage_iUnion, â† range_sigmaMk]
      exact finite_iUnion fun _ â†¦ finite_range _
    Â· refine fun s â†¦ Not.imp_symm fun hs â†¦ ?_
      simp only [preimage_setOf_eq, changeOriginIndexEquiv_apply_fst, mem_setOf, not_lt] at hs
      dsimp only [f]
      rw [changeOriginSeriesTerm_bound p hn _ _ _ hs, ContinuousMultilinearMap.zero_apply,
        ContinuousMultilinearMap.zero_apply]
  have hfkl k l : HasSum (f âŸ¨k, l, Â·âŸ©) (changeOriginSeries p k l (fun _ â†¦ x) fun _ â†¦ y) := by
    simp_rw [changeOriginSeries, ContinuousMultilinearMap.sum_apply]; apply hasSum_fintype
  have hfk k : HasSum (f âŸ¨k, Â·âŸ©) (changeOrigin p x k fun _ â†¦ y) := by
    have (m) (hm : m âˆ‰ Finset.range n) : changeOriginSeries p k m (fun _ â†¦ x) = 0 := by
      rw [Finset.mem_range, not_lt] at hm
      rw [changeOriginSeries_finite_of_finite _ hn _ (le_add_of_le_right hm),
        ContinuousMultilinearMap.zero_apply]
    rw [changeOrigin, FormalMultilinearSeries.sum,
      ContinuousMultilinearMap.tsum_eval (summable_of_ne_finset_zero this)]
    refine (summable_of_ne_finset_zero (s := Finset.range n) fun m hm â†¦ ?_).hasSum.sigma_of_hasSum
      (hfkl k) (summable_of_finite_support <| finsupp.preimage sigma_mk_injective.injOn)
    rw [this m hm, ContinuousMultilinearMap.zero_apply]
  have hf : HasSum f ((p.changeOrigin x).sum y) :=
    ((p.changeOrigin x).hasSum_of_finite (fun _ â†¦ changeOrigin_finite_of_finite p hn) _)
      |>.sigma_of_hasSum hfk (summable_of_finite_support finsupp)
  refine hf.unique (changeOriginIndexEquiv.symm.hasSum_iff.1 ?_)
  refine (p.hasSum_of_finite hn (x + y)).sigma_of_hasSum (fun n â†¦ ?_)
    (changeOriginIndexEquiv.symm.summable_iff.2 hf.summable)
  rw [â† Pi.add_def, (p n).map_add_univ (fun _ â†¦ x) fun _ â†¦ y]
  simp_rw [â† changeOriginSeriesTerm_changeOriginIndexEquiv_symm]
  exact hasSum_fintype fun c â†¦ f (changeOriginIndexEquiv.symm âŸ¨n, câŸ©)

/-- The terms of the formal multilinear series `p.changeOrigin` are continuously polynomial
as we vary the origin -/
theorem cpolynomialAt_changeOrigin_of_finite (p : FormalMultilinearSeries ğ•œ E F)
    {n : â„•} (hn : âˆ€ (m : â„•), n â‰¤ m â†’ p m = 0) (k : â„•) :
    CPolynomialAt ğ•œ (p.changeOrigin Â· k) 0 :=
  (p.hasFiniteFPowerSeriesOnBall_changeOrigin k fun _ h â†¦ hn _ (le_self_add.trans h)).cpolynomialAt

end

end FormalMultilinearSeries

section

variable {x y : E}

theorem HasFiniteFPowerSeriesOnBall.changeOrigin (hf : HasFiniteFPowerSeriesOnBall f p x n r)
    (h : (â€–yâ€–â‚Š : â„â‰¥0âˆ) < r) :
    HasFiniteFPowerSeriesOnBall f (p.changeOrigin y) (x + y) n (r - â€–yâ€–â‚Š) where
  r_le := (tsub_le_tsub_right hf.r_le _).trans p.changeOrigin_radius
  r_pos := by simp [h]
  finite _ hm := p.changeOrigin_finite_of_finite hf.finite hm
  hasSum {z} hz := by
    have : f (x + y + z) =
        FormalMultilinearSeries.sum (FormalMultilinearSeries.changeOrigin p y) z := by
      rw [mem_emetric_ball_zero_iff, lt_tsub_iff_right, add_comm] at hz
      rw [p.changeOrigin_eval_of_finite hf.finite, add_assoc, hf.sum]
      exact mem_emetric_ball_zero_iff.2 ((enorm_add_le _ _).trans_lt hz)
    rw [this]
    apply (p.changeOrigin y).hasSum_of_finite fun _ => p.changeOrigin_finite_of_finite hf.finite

/-- If a function admits a finite power series expansion `p` on an open ball `B (x, r)`, then
it is continuously polynomial at every point of this ball. -/
theorem HasFiniteFPowerSeriesOnBall.cpolynomialAt_of_mem
    (hf : HasFiniteFPowerSeriesOnBall f p x n r) (h : y âˆˆ EMetric.ball x r) :
    CPolynomialAt ğ•œ f y := by
  have : (â€–y - xâ€–â‚Š : â„â‰¥0âˆ) < r := by simpa [edist_eq_enorm_sub] using h
  have := hf.changeOrigin this
  rw [add_sub_cancel] at this
  exact this.cpolynomialAt

theorem HasFiniteFPowerSeriesOnBall.cpolynomialOn (hf : HasFiniteFPowerSeriesOnBall f p x n r) :
    CPolynomialOn ğ•œ f (EMetric.ball x r) :=
  fun _y hy => hf.cpolynomialAt_of_mem hy

variable (ğ•œ f)

/-- For any function `f` from a normed vector space to a normed vector space, the set of points
`x` such that `f` is continuously polynomial at `x` is open. -/
theorem isOpen_cpolynomialAt : IsOpen { x | CPolynomialAt ğ•œ f x } := by
  rw [isOpen_iff_mem_nhds]
  rintro x âŸ¨p, n, r, hrâŸ©
  exact mem_of_superset (EMetric.ball_mem_nhds _ hr.r_pos) fun y hy => hr.cpolynomialAt_of_mem hy

variable {ğ•œ}

theorem CPolynomialAt.eventually_cpolynomialAt {f : E â†’ F} {x : E} (h : CPolynomialAt ğ•œ f x) :
    âˆ€á¶  y in ğ“ x, CPolynomialAt ğ•œ f y :=
  (isOpen_cpolynomialAt ğ•œ f).mem_nhds h

theorem CPolynomialAt.exists_mem_nhds_cpolynomialOn {f : E â†’ F} {x : E} (h : CPolynomialAt ğ•œ f x) :
    âˆƒ s âˆˆ ğ“ x, CPolynomialOn ğ•œ f s :=
  h.eventually_cpolynomialAt.exists_mem

/-- If `f` is continuously polynomial at a point, then it is continuously polynomial in a
nonempty ball around that point. -/
theorem CPolynomialAt.exists_ball_cpolynomialOn {f : E â†’ F} {x : E} (h : CPolynomialAt ğ•œ f x) :
    âˆƒ r : â„, 0 < r âˆ§ CPolynomialOn ğ•œ f (Metric.ball x r) :=
  Metric.isOpen_iff.mp (isOpen_cpolynomialAt _ _) _ h

end
