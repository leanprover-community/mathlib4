/-
Copyright (c) 2022 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Analytic.Linear
import Mathlib.Analysis.Analytic.Composition
import Mathlib.Analysis.Analytic.Constructions
import Mathlib.Analysis.Normed.Module.Completion
import Mathlib.Analysis.Analytic.ChangeOrigin

/-!
# Uniqueness principle for analytic functions

We show that two analytic functions which coincide around a point coincide on whole connected sets,
in `AnalyticOnNhd.eqOn_of_preconnected_of_eventuallyEq`.
-/


variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

open Set

open scoped Topology ENNReal NNReal

/-!
### Uniqueness of power series
If a function `f : E â†’ F` has two representations as power series at a point `x : E`, corresponding
to formal multilinear series `pâ‚` and `pâ‚‚`, then these representations agree term-by-term. That is,
for any `n : â„•` and `y : E`, `pâ‚ n (fun i â†¦ y) = pâ‚‚ n (fun i â†¦ y)`. In the one-dimensional case,
when `f : ğ•œ â†’ E`, the continuous multilinear maps `pâ‚ n` and `pâ‚‚ n` are given by
`ContinuousMultilinearMap.mkPiRing`, and hence are determined completely by the value of
`pâ‚ n (fun i â†¦ 1)`, so `pâ‚ = pâ‚‚`. Consequently, the radius of convergence for one series can be
transferred to the other.
-/

section Uniqueness

open ContinuousMultilinearMap

theorem Asymptotics.IsBigO.continuousMultilinearMap_apply_eq_zero {n : â„•} {p : E [Ã—n]â†’L[ğ•œ] F}
    (h : (fun y => p fun _ => y) =O[ğ“ 0] fun y => â€–yâ€– ^ (n + 1)) (y : E) : (p fun _ => y) = 0 := by
  obtain âŸ¨c, c_pos, hcâŸ© := h.exists_pos
  obtain âŸ¨t, ht, t_open, z_memâŸ© := eventually_nhds_iff.mp (isBigOWith_iff.mp hc)
  obtain âŸ¨Î´, Î´_pos, Î´ÎµâŸ© := (Metric.isOpen_iff.mp t_open) 0 z_mem
  clear h hc z_mem
  rcases n with - | n
  Â· exact norm_eq_zero.mp (by
      simpa only [fin0_apply_norm, norm_eq_zero, norm_zero, zero_add, pow_one,
        mul_zero, norm_le_zero_iff] using ht 0 (Î´Îµ (Metric.mem_ball_self Î´_pos)))
  Â· refine Or.elim (Classical.em (y = 0))
      (fun hy => by simpa only [hy] using p.map_zero) fun hy => ?_
    replace hy := norm_pos_iff.mpr hy
    refine norm_eq_zero.mp (le_antisymm (le_of_forall_pos_le_add fun Îµ Îµ_pos => ?_) (norm_nonneg _))
    have hâ‚€ := _root_.mul_pos c_pos (pow_pos hy (n.succ + 1))
    obtain âŸ¨k, k_pos, k_normâŸ© := NormedField.exists_norm_lt ğ•œ
      (lt_min (mul_pos Î´_pos (inv_pos.mpr hy)) (mul_pos Îµ_pos (inv_pos.mpr hâ‚€)))
    have hâ‚ : â€–k â€¢ yâ€– < Î´ := by
      rw [norm_smul]
      exact inv_mul_cancel_rightâ‚€ hy.ne.symm Î´ â–¸
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_left _ _)) hy
    have hâ‚‚ :=
      calc
        â€–p fun _ => k â€¢ yâ€– â‰¤ c * â€–k â€¢ yâ€– ^ (n.succ + 1) := by
          -- Porting note: now Lean wants `_root_.`
          simpa only [norm_pow, _root_.norm_norm] using ht (k â€¢ y) (Î´Îµ (mem_ball_zero_iff.mpr hâ‚))
          --simpa only [norm_pow, norm_norm] using ht (k â€¢ y) (Î´Îµ (mem_ball_zero_iff.mpr hâ‚))
        _ = â€–kâ€– ^ n.succ * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1))) := by
          simp only [norm_smul, mul_pow]
          ring
    have hâ‚ƒ : â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1)) < Îµ :=
      inv_mul_cancel_rightâ‚€ hâ‚€.ne.symm Îµ â–¸
        mul_lt_mul_of_pos_right (lt_of_lt_of_le k_norm (min_le_right _ _)) hâ‚€
    calc
      â€–p fun _ => yâ€– = â€–kâ»Â¹ ^ n.succâ€– * â€–p fun _ => k â€¢ yâ€– := by
        simpa only [inv_smul_smulâ‚€ (norm_pos_iff.mp k_pos), norm_smul, Finset.prod_const,
          Finset.card_fin] using
          congr_arg norm (p.map_smul_univ (fun _ : Fin n.succ => kâ»Â¹) fun _ : Fin n.succ => k â€¢ y)
      _ â‰¤ â€–kâ»Â¹ ^ n.succâ€– * (â€–kâ€– ^ n.succ * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1)))) := by gcongr
      _ = â€–(kâ»Â¹ * k) ^ n.succâ€– * (â€–kâ€– * (c * â€–yâ€– ^ (n.succ + 1))) := by
        rw [â† mul_assoc]
        simp [norm_mul, mul_pow]
      _ â‰¤ 0 + Îµ := by
        rw [inv_mul_cancelâ‚€ (norm_pos_iff.mp k_pos)]
        simpa using hâ‚ƒ.le

/-- If a formal multilinear series `p` represents the zero function at `x : E`, then the
terms `p n (fun i â†¦ y)` appearing in the sum are zero for any `n : â„•`, `y : E`. -/
theorem HasFPowerSeriesAt.apply_eq_zero {p : FormalMultilinearSeries ğ•œ E F} {x : E}
    (h : HasFPowerSeriesAt 0 p x) (n : â„•) : âˆ€ y : E, (p n fun _ => y) = 0 := by
  refine Nat.strong_induction_on n fun k hk => ?_
  have psum_eq : p.partialSum (k + 1) = fun y => p k fun _ => y := by
    funext z
    refine Finset.sum_eq_single _ (fun b hb hnb => ?_) fun hn => ?_
    Â· have := Finset.mem_range_succ_iff.mp hb
      simp only [hk b (this.lt_of_ne hnb), Pi.zero_apply]
    Â· exact False.elim (hn (Finset.mem_range.mpr (lt_add_one k)))
  replace h := h.isBigO_sub_partialSum_pow k.succ
  simp only [psum_eq, zero_sub, Pi.zero_apply, Asymptotics.isBigO_neg_left] at h
  exact h.continuousMultilinearMap_apply_eq_zero

/-- A one-dimensional formal multilinear series representing the zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero {p : FormalMultilinearSeries ğ•œ ğ•œ E} {x : ğ•œ}
    (h : HasFPowerSeriesAt 0 p x) : p = 0 := by
  ext n
  rw [â† mkPiRing_apply_one_eq_self (p n)]
  simp [h.apply_eq_zero n 1]

/-- One-dimensional formal multilinear series representing the same function are equal. -/
theorem HasFPowerSeriesAt.eq_formalMultilinearSeries {pâ‚ pâ‚‚ : FormalMultilinearSeries ğ•œ ğ•œ E}
    {f : ğ•œ â†’ E} {x : ğ•œ} (hâ‚ : HasFPowerSeriesAt f pâ‚ x) (hâ‚‚ : HasFPowerSeriesAt f pâ‚‚ x) : pâ‚ = pâ‚‚ :=
  sub_eq_zero.mp (HasFPowerSeriesAt.eq_zero (x := x) (by simpa only [sub_self] using hâ‚.sub hâ‚‚))

theorem HasFPowerSeriesAt.eq_formalMultilinearSeries_of_eventually
    {p q : FormalMultilinearSeries ğ•œ ğ•œ E} {f g : ğ•œ â†’ E} {x : ğ•œ} (hp : HasFPowerSeriesAt f p x)
    (hq : HasFPowerSeriesAt g q x) (heq : âˆ€á¶  z in ğ“ x, f z = g z) : p = q :=
  (hp.congr heq).eq_formalMultilinearSeries hq

/-- A one-dimensional formal multilinear series representing a locally zero function is zero. -/
theorem HasFPowerSeriesAt.eq_zero_of_eventually {p : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
    {x : ğ•œ} (hp : HasFPowerSeriesAt f p x) (hf : f =á¶ [ğ“ x] 0) : p = 0 :=
  (hp.congr hf).eq_zero

/-- If a function `f : ğ•œ â†’ E` has two power series representations at `x`, then the given radii in
which convergence is guaranteed may be interchanged. This can be useful when the formal multilinear
series in one representation has a particularly nice form, but the other has a larger radius. -/
theorem HasFPowerSeriesOnBall.exchange_radius {pâ‚ pâ‚‚ : FormalMultilinearSeries ğ•œ ğ•œ E} {f : ğ•œ â†’ E}
    {râ‚ râ‚‚ : â„â‰¥0âˆ} {x : ğ•œ} (hâ‚ : HasFPowerSeriesOnBall f pâ‚ x râ‚)
    (hâ‚‚ : HasFPowerSeriesOnBall f pâ‚‚ x râ‚‚) : HasFPowerSeriesOnBall f pâ‚ x râ‚‚ :=
  hâ‚‚.hasFPowerSeriesAt.eq_formalMultilinearSeries hâ‚.hasFPowerSeriesAt â–¸ hâ‚‚

/-- If a function `f : ğ•œ â†’ E` has power series representation `p` on a ball of some radius and for
each positive radius it has some power series representation, then `p` converges to `f` on the whole
`ğ•œ`. -/
theorem HasFPowerSeriesOnBall.r_eq_top_of_exists {f : ğ•œ â†’ E} {r : â„â‰¥0âˆ} {x : ğ•œ}
    {p : FormalMultilinearSeries ğ•œ ğ•œ E} (h : HasFPowerSeriesOnBall f p x r)
    (h' : âˆ€ (r' : â„â‰¥0) (_ : 0 < r'), âˆƒ p' : FormalMultilinearSeries ğ•œ ğ•œ E,
      HasFPowerSeriesOnBall f p' x r') :
    HasFPowerSeriesOnBall f p x âˆ :=
  { r_le := ENNReal.le_of_forall_pos_nnreal_lt fun r hr _ =>
      let âŸ¨_, hp'âŸ© := h' r hr
      (h.exchange_radius hp').r_le
    r_pos := ENNReal.coe_lt_top
    hasSum := fun {y} _ =>
      let âŸ¨r', hr'âŸ© := exists_gt â€–yâ€–â‚Š
      let âŸ¨_, hp'âŸ© := h' r' hr'.ne_bot.bot_lt
      (h.exchange_radius hp').hasSum <| mem_emetric_ball_zero_iff.mpr (ENNReal.coe_lt_coe.2 hr') }

end Uniqueness

namespace AnalyticOnNhd

/-- If an analytic function vanishes around a point, then it is uniformly zero along
a connected set. Superseded by `AnalyticOnNhd.eqOn_zero_of_preconnected_of_eventuallyEq_zero` which
does not assume completeness of the target space. -/
theorem eqOn_zero_of_preconnected_of_eventuallyEq_zero_aux [CompleteSpace F] {f : E â†’ F} {U : Set E}
    (hf : AnalyticOnNhd ğ•œ f U) (hU : IsPreconnected U)
    {zâ‚€ : E} (hâ‚€ : zâ‚€ âˆˆ U) (hfzâ‚€ : f =á¶ [ğ“ zâ‚€] 0) :
    EqOn f 0 U := by
  /- Let `u` be the set of points around which `f` vanishes. It is clearly open. We have to show
    that its limit points in `U` still belong to it, from which the inclusion `U âŠ† u` will follow
    by connectedness. -/
  let u := {x | f =á¶ [ğ“ x] 0}
  suffices main : closure u âˆ© U âŠ† u by
    have Uu : U âŠ† u :=
      hU.subset_of_closure_inter_subset isOpen_setOf_eventually_nhds âŸ¨zâ‚€, hâ‚€, hfzâ‚€âŸ© main
    intro z hz
    simpa using mem_of_mem_nhds (Uu hz)
  /- Take a limit point `x`, then a ball `B (x, r)` on which it has a power series expansion, and
    then `y âˆˆ B (x, r/2) âˆ© u`. Then `f` has a power series expansion on `B (y, r/2)` as it is
    contained in `B (x, r)`. All the coefficients in this series expansion vanish, as `f` is zero
    on a neighborhood of `y`. Therefore, `f` is zero on `B (y, r/2)`. As this ball contains `x`,
    it follows that `f` vanishes on a neighborhood of `x`, proving the claim. -/
  rintro x âŸ¨xu, xUâŸ©
  rcases hf x xU with âŸ¨p, r, hpâŸ©
  obtain âŸ¨y, yu, hxyâŸ© : âˆƒ y âˆˆ u, edist x y < r / 2 :=
    EMetric.mem_closure_iff.1 xu (r / 2) (ENNReal.half_pos hp.r_pos.ne')
  let q := p.changeOrigin (y - x)
  have has_series : HasFPowerSeriesOnBall f q y (r / 2) := by
    have A : (â€–y - xâ€–â‚Š : â„â‰¥0âˆ) < r / 2 := by rwa [edist_comm, edist_eq_enorm_sub] at hxy
    have := hp.changeOrigin (A.trans_le ENNReal.half_le_self)
    simp only [add_sub_cancel] at this
    apply this.mono (ENNReal.half_pos hp.r_pos.ne')
    apply ENNReal.le_sub_of_add_le_left ENNReal.coe_ne_top
    apply (add_le_add A.le (le_refl (r / 2))).trans (le_of_eq _)
    exact ENNReal.add_halves _
  have M : EMetric.ball y (r / 2) âˆˆ ğ“ x := EMetric.isOpen_ball.mem_nhds hxy
  filter_upwards [M] with z hz
  have A : HasSum (fun n : â„• => q n fun _ : Fin n => z - y) (f z) := has_series.hasSum_sub hz
  have B : HasSum (fun n : â„• => q n fun _ : Fin n => z - y) 0 := by
    have : HasFPowerSeriesAt 0 q y := has_series.hasFPowerSeriesAt.congr yu
    convert hasSum_zero (Î± := F) using 2
    ext n
    exact this.apply_eq_zero n _
  exact HasSum.unique A B

/-- The *identity principle* for analytic functions: If an analytic function vanishes in a whole
neighborhood of a point `zâ‚€`, then it is uniformly zero along a connected set. For a one-dimensional
version assuming only that the function vanishes at some points arbitrarily close to `zâ‚€`, see
`AnalyticOnNhd.eqOn_zero_of_preconnected_of_frequently_eq_zero`. -/
theorem eqOn_zero_of_preconnected_of_eventuallyEq_zero {f : E â†’ F} {U : Set E}
    (hf : AnalyticOnNhd ğ•œ f U) (hU : IsPreconnected U)
    {zâ‚€ : E} (hâ‚€ : zâ‚€ âˆˆ U) (hfzâ‚€ : f =á¶ [ğ“ zâ‚€] 0) :
    EqOn f 0 U := by
  let F' := UniformSpace.Completion F
  set e : F â†’L[ğ•œ] F' := UniformSpace.Completion.toComplL
  have : AnalyticOnNhd ğ•œ (e âˆ˜ f) U := fun x hx => (e.analyticAt _).comp (hf x hx)
  have A : EqOn (e âˆ˜ f) 0 U := by
    apply eqOn_zero_of_preconnected_of_eventuallyEq_zero_aux this hU hâ‚€
    filter_upwards [hfzâ‚€] with x hx
    simp only [hx, Function.comp_apply, Pi.zero_apply, map_zero]
  intro z hz
  have : e (f z) = e 0 := by simpa only using A hz
  exact UniformSpace.Completion.coe_injective F this

/-- The *identity principle* for analytic functions: If two analytic functions coincide in a whole
neighborhood of a point `zâ‚€`, then they coincide globally along a connected set.
For a one-dimensional version assuming only that the functions coincide at some points
arbitrarily close to `zâ‚€`, see `AnalyticOnNhd.eqOn_of_preconnected_of_frequently_eq`. -/
theorem eqOn_of_preconnected_of_eventuallyEq {f g : E â†’ F} {U : Set E} (hf : AnalyticOnNhd ğ•œ f U)
    (hg : AnalyticOnNhd ğ•œ g U) (hU : IsPreconnected U) {zâ‚€ : E} (hâ‚€ : zâ‚€ âˆˆ U) (hfg : f =á¶ [ğ“ zâ‚€] g) :
    EqOn f g U := by
  have hfg' : f - g =á¶ [ğ“ zâ‚€] 0 := hfg.mono fun z h => by simp [h]
  simpa [sub_eq_zero] using fun z hz =>
    (hf.sub hg).eqOn_zero_of_preconnected_of_eventuallyEq_zero hU hâ‚€ hfg' hz

/-- The *identity principle* for analytic functions: If two analytic functions on a normed space
coincide in a neighborhood of a point `zâ‚€`, then they coincide everywhere.
For a one-dimensional version assuming only that the functions coincide at some points
arbitrarily close to `zâ‚€`, see `AnalyticOnNhd.eq_of_frequently_eq`. -/
theorem eq_of_eventuallyEq {f g : E â†’ F} [PreconnectedSpace E] (hf : AnalyticOnNhd ğ•œ f univ)
    (hg : AnalyticOnNhd ğ•œ g univ) {zâ‚€ : E} (hfg : f =á¶ [ğ“ zâ‚€] g) : f = g :=
  funext fun x =>
    eqOn_of_preconnected_of_eventuallyEq hf hg isPreconnected_univ (mem_univ zâ‚€) hfg (mem_univ x)

end AnalyticOnNhd
