/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Stefan Kebekus
-/
import Mathlib.Analysis.Analytic.IsolatedZeros

/-!
# Meromorphic functions

Main statements:

* `MeromorphicAt`: definition of meromorphy at a point
* `MeromorphicAt.iff_eventuallyEq_zpow_smul_analyticAt`: `f` is meromorphic at `zâ‚€` iff we have
  `f z = (z - zâ‚€) ^ n â€¢ g z` on a punctured neighborhood of `zâ‚€`, for some `n : â„¤`
  and `g` analytic at `zâ‚€`.
-/

open Filter

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Meromorphy of `f` at `x` (more precisely, on a punctured neighbourhood of `x`; the value at
`x` itself is irrelevant). -/
@[fun_prop]
def MeromorphicAt (f : ğ•œ â†’ E) (x : ğ•œ) :=
  âˆƒ (n : â„•), AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ n â€¢ f z) x

@[fun_prop]
lemma AnalyticAt.meromorphicAt {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    MeromorphicAt f x :=
  âŸ¨0, by simpa only [pow_zero, one_smul]âŸ©

/- Analogue of the principle of isolated zeros for an analytic function: if a function is
meromorphic at `zâ‚€`, then either it is identically zero in a punctured neighborhood of `zâ‚€`, or it
does not vanish there at all. -/
theorem MeromorphicAt.eventually_eq_zero_or_eventually_ne_zero {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : MeromorphicAt f zâ‚€) :
    (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z = 0) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) := by
  obtain âŸ¨n, hâŸ© := hf
  rcases h.eventually_eq_zero_or_eventually_ne_zero with hâ‚ | hâ‚‚
  Â· left
    filter_upwards [nhdsWithin_le_nhds hâ‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    rw [Set.mem_compl_iff, Set.mem_singleton_iff, â† sub_eq_zero] at hâ‚‚y
    exact smul_eq_zero_iff_right (pow_ne_zero n hâ‚‚y) |>.mp hâ‚y
  Â· right
    filter_upwards [hâ‚‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    exact (smul_ne_zero_iff.1 hâ‚y).2

namespace MeromorphicAt

lemma id (x : ğ•œ) : MeromorphicAt id x := analyticAt_id.meromorphicAt

@[fun_prop]
lemma const (e : E) (x : ğ•œ) : MeromorphicAt (fun _ â†¦ e) x :=
  analyticAt_const.meromorphicAt

@[fun_prop]
lemma add {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨max m n, ?_âŸ©
  have : (fun z â†¦ (z - x) ^ max m n â€¢ (f + g) z) = fun z â†¦ (z - x) ^ (max m n - m) â€¢
      ((z - x) ^ m â€¢ f z) + (z - x) ^ (max m n - n) â€¢ ((z - x) ^ n â€¢ g z) := by
    simp_rw [â† mul_smul, â† pow_add, Nat.sub_add_cancel (Nat.le_max_left _ _),
      Nat.sub_add_cancel (Nat.le_max_right _ _), Pi.add_apply, smul_add]
  rw [this]
  exact (((analyticAt_id.sub analyticAt_const).pow _).smul hf).add
    (((analyticAt_id.sub analyticAt_const).pow _).smul hg)

@[fun_prop]
lemma fun_add {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z + g z) x :=
  hf.add hg

@[deprecated (since := "2025-05-09")] alias add' := fun_add

@[fun_prop]
lemma smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f â€¢ g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨m + n, ?_âŸ©
  convert hf.fun_smul hg using 2 with z
  rw [Pi.smul_apply', smul_eq_mul]
  module

@[fun_prop]
lemma fun_smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z â€¢ g z) x :=
  hf.smul hg

@[deprecated (since := "2025-05-09")] alias smul' := fun_smul

@[fun_prop]
lemma mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f * g) x :=
  hf.smul hg

@[fun_prop]
lemma fun_mul {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z * g z) x :=
  hf.smul hg

@[deprecated (since := "2025-05-09")] alias mul' := fun_mul

/-- Finite products of meromorphic functions are analytic. -/
@[fun_prop]
theorem prod {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ} {x : ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicAt (f Ïƒ) x) :
    MeromorphicAt (âˆ n âˆˆ s, f n) x := by
  classical
  induction s using Finset.induction with
  | empty =>
    rw [Finset.prod_empty]
    exact analyticAt_const.meromorphicAt
  | insert Ïƒ s hÏƒ hind =>
    rw [Finset.prod_insert hÏƒ]
    exact (h Ïƒ).mul hind

/-- Finite products of meromorphic functions are analytic. -/
@[fun_prop]
theorem fun_prod {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ} {x : ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicAt (f Ïƒ) x) :
    MeromorphicAt (fun z â†¦ âˆ n âˆˆ s, f n z) x := by
  convert prod h (s := s)
  simp

@[fun_prop]
lemma neg {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (-f) x := by
  convert (MeromorphicAt.const (-1 : ğ•œ) x).smul hf using 1
  ext1 z
  simp only [Pi.neg_apply, Pi.smul_apply', neg_smul, one_smul]

@[fun_prop]
lemma fun_neg {f : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (fun z â†¦ -f z) x :=
  hf.neg

@[deprecated (since := "2025-05-09")] alias neg' := fun_neg

@[simp]
lemma neg_iff {f : ğ•œ â†’ E} {x : ğ•œ} :
    MeromorphicAt (-f) x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, MeromorphicAt.negâŸ©

@[fun_prop]
lemma sub {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x := by
  convert hf.add hg.neg using 1
  ext1 z
  simp_rw [Pi.sub_apply, Pi.add_apply, Pi.neg_apply, sub_eq_add_neg]

@[fun_prop]
lemma fun_sub {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z - g z) x :=
  hf.sub hg

@[deprecated (since := "2025-05-09")] alias sub' := fun_sub

/-- With our definitions, `MeromorphicAt f x` depends only on the values of `f` on a punctured
neighbourhood of `x` (not on `f x`) -/
lemma congr {f g : ğ•œ â†’ E} {x : ğ•œ} (hf : MeromorphicAt f x) (hfg : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt g x := by
  rcases hf with âŸ¨m, hfâŸ©
  refine âŸ¨m + 1, ?_âŸ©
  have : AnalyticAt ğ•œ (fun z â†¦ z - x) x := analyticAt_id.sub analyticAt_const
  refine (this.fun_smul hf).congr ?_
  rw [eventuallyEq_nhdsWithin_iff] at hfg
  filter_upwards [hfg] with z hz
  rcases eq_or_ne z x with rfl | hn
  Â· simp
  Â· rw [hz (Set.mem_compl_singleton_iff.mp hn), pow_succ', mul_smul]

/--
If two functions agree on a punctured neighborhood, then one is meromorphic iff the other is so.
-/
lemma meromorphicAt_congr {f g : ğ•œ â†’ E} {x : ğ•œ} (h : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt f x â†” MeromorphicAt g x :=
  âŸ¨fun hf â†¦ hf.congr h, fun hg â†¦ hg.congr h.symmâŸ©

@[fun_prop]
lemma inv {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt fâ»Â¹ x := by
  rcases hf with âŸ¨m, hfâŸ©
  by_cases h_eq : (fun z â†¦ (z - x) ^ m â€¢ f z) =á¶ [ğ“ x] 0
  Â· -- silly case: f locally 0 near x
    refine (MeromorphicAt.const 0 x).congr ?_
    rw [eventuallyEq_nhdsWithin_iff]
    filter_upwards [h_eq] with z hfz hz
    rw [Pi.inv_apply, (smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)).mp hfz,
      inv_zero]
  Â· -- interesting case: use local formula for `f`
    obtain âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ© := hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h_eq
    have : AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ (m + 1)) x :=
      (analyticAt_id.sub analyticAt_const).pow _
    -- use `m + 1` rather than `m` to damp out any silly issues with the value at `z = x`
    refine âŸ¨n + 1, (this.fun_smul <| hg_an.inv hg_ne).congr ?_âŸ©
    filter_upwards [hg_eq, hg_an.continuousAt.eventually_ne hg_ne] with z hfg hg_ne'
    rcases eq_or_ne z x with rfl | hz_ne
    Â· simp only [sub_self, pow_succ, mul_zero, zero_smul]
    Â· simp_rw [smul_eq_mul] at hfg âŠ¢
      have aux1 : f z â‰  0 := by
        have : (z - x) ^ n * g z â‰  0 := mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz_ne)) hg_ne'
        rw [â† hfg, mul_ne_zero_iff] at this
        exact this.2
      simp [field]
      rw [pow_succ', mul_assoc, hfg]
      ring

@[fun_prop]
lemma fun_inv {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt (fun z â†¦ (f z)â»Â¹) x :=
  hf.inv

@[deprecated (since := "2025-05-09")] alias inv' := fun_inv

@[simp]
lemma inv_iff {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} :
    MeromorphicAt fâ»Â¹ x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, MeromorphicAt.invâŸ©

@[fun_prop]
lemma div {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f / g) x :=
  (div_eq_mul_inv f g).symm â–¸ (hf.mul hg.inv)

@[fun_prop]
lemma fun_div {f g : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (fun z â†¦ f z / g z) x :=
  hf.div hg

@[deprecated (since := "2025-05-09")] alias div' := fun_div

@[fun_prop]
lemma pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„•) : MeromorphicAt (f ^ n) x := by
  induction n with
  | zero => simpa only [pow_zero] using MeromorphicAt.const 1 x
  | succ m hm => simpa only [pow_succ] using hm.mul hf

@[fun_prop]
lemma fun_pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„•) :
    MeromorphicAt (fun z â†¦ (f z) ^ n) x :=
  hf.pow n

@[deprecated (since := "2025-05-09")] alias pow' := fun_pow

@[fun_prop]
lemma zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : MeromorphicAt (f ^ n) x := by
  cases n with
  | ofNat m => simpa only [Int.ofNat_eq_coe, zpow_natCast] using hf.pow m
  | negSucc m => simpa only [zpow_negSucc, inv_iff] using hf.pow (m + 1)

@[fun_prop]
lemma fun_zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) :
    MeromorphicAt (fun z â†¦ (f z) ^ n) x :=
  hf.zpow n

@[deprecated (since := "2025-05-09")] alias zpow' := fun_zpow

/-- If a function is meromorphic at a point, then it is continuous at nearby points. -/
theorem eventually_continuousAt {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicAt f x) : âˆ€á¶  y in ğ“[â‰ ] x, ContinuousAt f y := by
  obtain âŸ¨n, hâŸ© := h
  have : âˆ€á¶  y in ğ“[â‰ ] x, ContinuousAt (fun z â†¦ (z - x) ^ n â€¢ f z) y :=
    nhdsWithin_le_nhds h.eventually_continuousAt
  filter_upwards [this, self_mem_nhdsWithin] with y hy h'y
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff] at h'y
  have : ContinuousAt (fun z â†¦ ((z - x) ^ n)â»Â¹) y :=
    ContinuousAt.invâ‚€ (by fun_prop) (by simp [sub_eq_zero, h'y])
  apply (this.smul hy).congr
  filter_upwards [eventually_ne_nhds h'y] with z hz
  simp [smul_smul, hz, sub_eq_zero]

/-- In a complete space, a function which is meromorphic at a point is analytic at all nearby
points. The completeness assumption can be dispensed with if one assumes that `f` is meromorphic
on a set around `x`, see `MeromorphicOn.eventually_analyticAt`. -/
theorem eventually_analyticAt [CompleteSpace E] {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicAt f x) : âˆ€á¶  y in ğ“[â‰ ] x, AnalyticAt ğ•œ f y := by
  obtain âŸ¨n, hâŸ© := h
  apply AnalyticAt.eventually_analyticAt at h
  refine (h.filter_mono ?_).mp ?_
  Â· simp [nhdsWithin]
  Â· rw [eventually_nhdsWithin_iff]
    apply Filter.Eventually.of_forall
    intro y hy hf
    rw [Set.mem_compl_iff, Set.mem_singleton_iff] at hy
    have := ((analyticAt_id (ğ•œ := ğ•œ).sub analyticAt_const).pow n).inv
      (pow_ne_zero _ (sub_ne_zero_of_ne hy))
    apply (this.smul hf).congr âˆ˜ (eventually_ne_nhds hy).mono
    intro z hz
    simp [smul_smul, hz, sub_eq_zero]

lemma iff_eventuallyEq_zpow_smul_analyticAt {f : ğ•œ â†’ E} {x : ğ•œ} : MeromorphicAt f x â†”
    âˆƒ (n : â„¤) (g : ğ•œ â†’ E), AnalyticAt ğ•œ g x âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  refine âŸ¨fun âŸ¨n, hnâŸ© â†¦ âŸ¨-n, _, âŸ¨hn, eventually_nhdsWithin_iff.mpr ?_âŸ©âŸ©, ?_âŸ©
  Â· filter_upwards with z hz
    match_scalars
    simp [sub_ne_zero.mpr hz]
  Â· refine fun âŸ¨n, g, hg_an, hg_eqâŸ© â†¦ MeromorphicAt.congr ?_ (EventuallyEq.symm hg_eq)
    exact (((MeromorphicAt.id x).sub (.const _ x)).zpow _).smul hg_an.meromorphicAt

end MeromorphicAt

/-- Meromorphy of a function on a set. -/
def MeromorphicOn (f : ğ•œ â†’ E) (U : Set ğ•œ) : Prop := âˆ€ x âˆˆ U, MeromorphicAt f x

lemma AnalyticOnNhd.meromorphicOn {f : ğ•œ â†’ E} {U : Set ğ•œ} (hf : AnalyticOnNhd ğ•œ f U) :
    MeromorphicOn f U :=
  fun x hx â†¦ (hf x hx).meromorphicAt

namespace MeromorphicOn

variable {s t : ğ•œ â†’ ğ•œ} {f g : ğ•œ â†’ E} {U : Set ğ•œ}
  (hs : MeromorphicOn s U) (ht : MeromorphicOn t U)
  (hf : MeromorphicOn f U) (hg : MeromorphicOn g U)

/--
If `f` is meromorphic on `U`, if `g` agrees with `f` on a codiscrete subset of `U` and outside of
`U`, then `g` is also meromorphic on `U`.
-/
theorem congr_codiscreteWithin_of_eqOn_compl (hf : MeromorphicOn f U)
    (hâ‚ : f =á¶ [codiscreteWithin U] g) (hâ‚‚ : Set.EqOn f g Uá¶œ) :
    MeromorphicOn g U := by
  intro x hx
  apply (hf x hx).congr
  simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
    disjoint_principal_right] at hâ‚
  filter_upwards [hâ‚ x hx] with a ha
  simp at ha
  tauto

/--
If `f` is meromorphic on an open set `U`, if `g` agrees with `f` on a codiscrete subset of `U`, then
`g` is also meromorphic on `U`.
-/
theorem congr_codiscreteWithin (hf : MeromorphicOn f U) (hâ‚ : f =á¶ [codiscreteWithin U] g)
    (hâ‚‚ : IsOpen U) :
    MeromorphicOn g U := by
  intro x hx
  apply (hf x hx).congr
  simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
    disjoint_principal_right] at hâ‚
  have : U âˆˆ ğ“[â‰ ] x := by
    apply mem_nhdsWithin.mpr
    use U, hâ‚‚, hx, Set.inter_subset_left
  filter_upwards [this, hâ‚ x hx] with a hâ‚a hâ‚‚a
  simp only [Set.mem_compl_iff, Set.mem_diff, Set.mem_setOf_eq, not_and] at hâ‚‚a
  tauto

lemma id {U : Set ğ•œ} : MeromorphicOn id U := fun x _ â†¦ .id x

lemma const (e : E) {U : Set ğ•œ} : MeromorphicOn (fun _ â†¦ e) U :=
  fun x _ â†¦ .const e x

section arithmetic

include hf in
lemma mono_set {V : Set ğ•œ} (hv : V âŠ† U) : MeromorphicOn f V := fun x hx â†¦ hf x (hv hx)

include hf hg in
lemma add : MeromorphicOn (f + g) U := fun x hx â†¦ (hf x hx).add (hg x hx)

include hf hg in
lemma fun_add : MeromorphicOn (fun z â†¦ f z + g z) U := fun x hx â†¦ (hf x hx).add (hg x hx)

include hf hg in
lemma sub : MeromorphicOn (f - g) U := fun x hx â†¦ (hf x hx).sub (hg x hx)

include hf hg in
lemma fun_sub : MeromorphicOn (fun z â†¦ f z - g z) U := fun x hx â†¦ (hf x hx).sub (hg x hx)

include hf in
lemma neg : MeromorphicOn (-f) U := fun x hx â†¦ (hf x hx).neg

include hf in
lemma fun_neg : MeromorphicOn (fun z â†¦ -f z) U := fun x hx â†¦ (hf x hx).neg

@[simp] lemma neg_iff : MeromorphicOn (-f) U â†” MeromorphicOn f U :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, negâŸ©

include hs hf in
lemma smul : MeromorphicOn (s â€¢ f) U := fun x hx â†¦ (hs x hx).smul (hf x hx)

include hs hf in
lemma fun_smul : MeromorphicOn (fun z â†¦ s z â€¢ f z) U := fun x hx â†¦ (hs x hx).smul (hf x hx)

include hs ht in
lemma mul : MeromorphicOn (s * t) U := fun x hx â†¦ (hs x hx).mul (ht x hx)

include hs ht in
lemma fun_mul : MeromorphicOn (fun z â†¦ s z * t z) U := fun x hx â†¦ (hs x hx).mul (ht x hx)

/-- Finite products of meromorphic functions are analytic. -/
lemma prod {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (âˆ n âˆˆ s, f n) U :=
  fun z hz â†¦ MeromorphicAt.prod (fun Ïƒ â†¦ h Ïƒ z hz)

/-- Finite products of meromorphic functions are analytic. -/
lemma fun_prod {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (fun z â†¦ âˆ n âˆˆ s, f n z) U :=
  fun z hz â†¦ MeromorphicAt.fun_prod (fun Ïƒ â†¦ h Ïƒ z hz)

include hs in
lemma inv : MeromorphicOn sâ»Â¹ U := fun x hx â†¦ (hs x hx).inv

include hs in
lemma fun_inv : MeromorphicOn (fun z â†¦ sâ»Â¹ z) U := fun x hx â†¦ (hs x hx).inv

@[simp] lemma inv_iff : MeromorphicOn sâ»Â¹ U â†” MeromorphicOn s U :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, invâŸ©

include hs ht in
lemma div : MeromorphicOn (s / t) U := fun x hx â†¦ (hs x hx).div (ht x hx)

include hs ht in
lemma fun_div : MeromorphicOn (fun z â†¦ s z / t z) U := fun x hx â†¦ (hs x hx).div (ht x hx)

include hs in
lemma pow (n : â„•) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).pow _

include hs in
lemma fun_pow (n : â„•) : MeromorphicOn (fun z â†¦ s z ^ n) U := fun x hx â†¦ (hs x hx).pow _

include hs in
lemma zpow (n : â„¤) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).zpow _

include hs in
lemma fun_zpow (n : â„¤) : MeromorphicOn (fun z â†¦ s z ^ n) U := fun x hx â†¦ (hs x hx).zpow _

end arithmetic

include hf in
lemma congr (h_eq : Set.EqOn f g U) (hu : IsOpen U) : MeromorphicOn g U := by
  refine fun x hx â†¦ (hf x hx).congr (EventuallyEq.filter_mono ?_ nhdsWithin_le_nhds)
  exact eventually_of_mem (hu.mem_nhds hx) h_eq

theorem eventually_codiscreteWithin_analyticAt
    [CompleteSpace E] (f : ğ•œ â†’ E) (h : MeromorphicOn f U) :
    âˆ€á¶  (y : ğ•œ) in codiscreteWithin U, AnalyticAt ğ•œ f y := by
  rw [eventually_iff, mem_codiscreteWithin]
  intro x hx
  rw [disjoint_principal_right]
  apply Filter.mem_of_superset ((h x hx).eventually_analyticAt)
  intro x hx
  simp [hx]

end MeromorphicOn
