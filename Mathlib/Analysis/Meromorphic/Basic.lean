/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Stefan Kebekus
-/
module

public import Mathlib.Analysis.Analytic.Order
public import Mathlib.Analysis.Analytic.IsolatedZeros
public import Mathlib.Analysis.Calculus.Deriv.ZPow
public import Mathlib.MeasureTheory.Constructions.BorelSpace.Basic

/-!
# Meromorphic functions

Main statements:

* `MeromorphicAt`: definition of meromorphy at a point
* `MeromorphicAt.iff_eventuallyEq_zpow_smul_analyticAt`: `f` is meromorphic at `zâ‚€` iff we have
  `f z = (z - zâ‚€) ^ n â€¢ g z` on a punctured neighborhood of `zâ‚€`, for some `n : â„¤`
  and `g` analytic at `zâ‚€`.
-/

@[expose] public section

open Filter Set

open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Meromorphy of `f` at `x` (more precisely, on a punctured neighbourhood of `x`; the value at
`x` itself is irrelevant). -/
@[fun_prop]
def MeromorphicAt (f : ğ•œ â†’ E) (x : ğ•œ) :=
  âˆƒ (n : â„•), AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ n â€¢ f z) x

@[fun_prop]
lemma AnalyticAt.meromorphicAt {f : ğ•œ â†’ E} {x : ğ•œ} (hf : AnalyticAt ğ•œ f x) :
    MeromorphicAt f x :=
  âŸ¨0, by simpa only [pow_zero, one_smul]âŸ©

/- Analogue of the principle of isolated zeros for an analytic function: if a function is
meromorphic at `zâ‚€`, then either it is identically zero in a punctured neighborhood of `zâ‚€`, or it
does not vanish there at all. -/
theorem MeromorphicAt.eventually_eq_zero_or_eventually_ne_zero {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : MeromorphicAt f zâ‚€) :
    (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z = 0) âˆ¨ (âˆ€á¶  z in ğ“[â‰ ] zâ‚€, f z â‰  0) := by
  obtain âŸ¨n, hâŸ© := hf
  rcases h.eventually_eq_zero_or_eventually_ne_zero with hâ‚ | hâ‚‚
  Â· left
    filter_upwards [nhdsWithin_le_nhds hâ‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    rw [Set.mem_compl_iff, Set.mem_singleton_iff, â† sub_eq_zero] at hâ‚‚y
    exact smul_eq_zero_iff_right (pow_ne_zero n hâ‚‚y) |>.mp hâ‚y
  Â· right
    filter_upwards [hâ‚‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    exact (smul_ne_zero_iff.1 hâ‚y).2

namespace MeromorphicAt

variable {Î¹ : Type*} {s : Finset Î¹} {F : Î¹ â†’ ğ•œ â†’ ğ•œ} {G : Î¹ â†’ ğ•œ â†’ E}

@[fun_prop]
lemma id (x : ğ•œ) : MeromorphicAt id x := analyticAt_id.meromorphicAt

@[fun_prop, simp]
lemma const (e : E) (x : ğ•œ) : MeromorphicAt (fun _ â†¦ e) x :=
  analyticAt_const.meromorphicAt

variable {x : ğ•œ}

@[to_fun (attr := fun_prop)]
lemma add {f g : ğ•œ â†’ E} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨max m n, ?_âŸ©
  have : (fun z â†¦ (z - x) ^ max m n â€¢ (f + g) z) = fun z â†¦ (z - x) ^ (max m n - m) â€¢
      ((z - x) ^ m â€¢ f z) + (z - x) ^ (max m n - n) â€¢ ((z - x) ^ n â€¢ g z) := by
    simp_rw [â† mul_smul, â† pow_add, Nat.sub_add_cancel (Nat.le_max_left _ _),
      Nat.sub_add_cancel (Nat.le_max_right _ _), Pi.add_apply, smul_add]
  rw [this]
  exact (((analyticAt_id.sub analyticAt_const).pow _).smul hf).add
    (((analyticAt_id.sub analyticAt_const).pow _).smul hg)

@[to_fun (attr := fun_prop)]
lemma smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f â€¢ g) x := by
  rcases hf with âŸ¨m, hfâŸ©
  rcases hg with âŸ¨n, hgâŸ©
  refine âŸ¨m + n, ?_âŸ©
  convert hf.smul hg using 2 with z
  simp
  module

@[to_fun (attr := fun_prop)]
lemma mul {f g : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f * g) x :=
  hf.smul hg

/-- Finite products of meromorphic functions are meromorphic. -/
@[fun_prop] -- TODO: to_fun generates an unreadable statement, see #32866
theorem prod (h : âˆ€ Ïƒ, MeromorphicAt (F Ïƒ) x) :
    MeromorphicAt (âˆ n âˆˆ s, F n) x := by
  classical
  induction s using Finset.induction with
  | empty =>
    rw [Finset.prod_empty]
    exact analyticAt_const.meromorphicAt
  | insert Ïƒ s hÏƒ hind =>
    rw [Finset.prod_insert hÏƒ]
    exact (h Ïƒ).mul hind

/-- Finite products of meromorphic functions are meromorphic. -/
@[fun_prop]
theorem fun_prod (h : âˆ€ Ïƒ, MeromorphicAt (F Ïƒ) x) :
    MeromorphicAt (fun z â†¦ âˆ n âˆˆ s, F n z) x := by
  convert prod h (s := s)
  simp

/-- Finite sums of meromorphic functions are meromorphic. -/
@[fun_prop] -- TODO: to_fun generates an unreadable statement, see #32866
theorem sum (h : âˆ€ Ïƒ, MeromorphicAt (G Ïƒ) x) :
    MeromorphicAt (âˆ‘ n âˆˆ s, G n) x := by
  classical
  induction s using Finset.induction with
  | empty =>
    simp only [Finset.sum_empty]
    exact analyticAt_const.meromorphicAt
  | insert Ïƒ s hÏƒ hind =>
    rw [Finset.sum_insert hÏƒ]
    exact (h Ïƒ).add hind

/-- Finite sums of meromorphic functions are meromorphic. -/
@[fun_prop]
theorem fun_sum (h : âˆ€ Ïƒ, MeromorphicAt (G Ïƒ) x) :
    MeromorphicAt (fun z â†¦ âˆ‘ n âˆˆ s, G n z) x := by
  convert sum h (s := s)
  simp

@[to_fun (attr := fun_prop)]
lemma neg {f : ğ•œ â†’ E} (hf : MeromorphicAt f x) : MeromorphicAt (-f) x := by
  convert (MeromorphicAt.const (-1 : ğ•œ) x).smul hf using 1
  ext1 z
  simp only [Pi.neg_apply, Pi.smul_apply', neg_smul, one_smul]

@[simp]
lemma neg_iff {f : ğ•œ â†’ E} :
    MeromorphicAt (-f) x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, MeromorphicAt.negâŸ©

@[to_fun (attr := fun_prop)]
lemma sub {f g : ğ•œ â†’ E} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x := by
  convert hf.add hg.neg using 1
  ext1 z
  simp_rw [Pi.sub_apply, Pi.add_apply, Pi.neg_apply, sub_eq_add_neg]

/--
If `f` is meromorphic at `x`, then `f + g` is meromorphic at `x` if and only if `g` is meromorphic
at `x`.
-/
lemma meromorphicAt_add_iff_meromorphicAtâ‚ {f g : ğ•œ â†’ E} (hf : MeromorphicAt f x) :
    MeromorphicAt (f + g) x â†” MeromorphicAt g x := by
  exact âŸ¨fun h â†¦ by simpa using h.sub hf, fun _ â†¦ by fun_propâŸ©

/--
If `g` is meromorphic at `x`, then `f + g` is meromorphic at `x` if and only if `f` is meromorphic
at `x`.
-/
lemma meromorphicAt_add_iff_meromorphicAtâ‚‚ {f g : ğ•œ â†’ E} (hg : MeromorphicAt g x) :
    MeromorphicAt (f + g) x â†” MeromorphicAt f x := by
  rw [add_comm]
  exact meromorphicAt_add_iff_meromorphicAtâ‚ hg

/--
If `f` is meromorphic at `x`, then `f - g` is meromorphic at `x` if and only if `g` is meromorphic
at `x`.
-/
lemma meromorphicAt_sub_iff_meromorphicAtâ‚ {f g : ğ•œ â†’ E} (hf : MeromorphicAt f x) :
    MeromorphicAt (f - g) x â†” MeromorphicAt g x := by
  exact âŸ¨fun h â†¦ by simpa using h.sub hf, fun _ â†¦ by fun_propâŸ©

/--
If `g` is meromorphic at `x`, then `f - g` is meromorphic at `x` if and only if `f` is meromorphic
at `x`.
-/
lemma meromorphicAt_sub_iff_meromorphicAtâ‚‚ {f g : ğ•œ â†’ E} (hg : MeromorphicAt g x) :
    MeromorphicAt (f - g) x â†” MeromorphicAt f x := by
  exact âŸ¨fun h â†¦ by simpa using h.add hg, fun _ â†¦ by fun_propâŸ©

/-- With our definitions, `MeromorphicAt f x` depends only on the values of `f` on a punctured
neighbourhood of `x` (not on `f x`) -/
lemma congr {f g : ğ•œ â†’ E} (hf : MeromorphicAt f x) (hfg : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt g x := by
  rcases hf with âŸ¨m, hfâŸ©
  refine âŸ¨m + 1, ?_âŸ©
  have : AnalyticAt ğ•œ (fun z â†¦ z - x) x := analyticAt_id.sub analyticAt_const
  refine (this.fun_smul hf).congr ?_
  rw [eventuallyEq_nhdsWithin_iff] at hfg
  filter_upwards [hfg] with z hz
  rcases eq_or_ne z x with rfl | hn
  Â· simp
  Â· rw [hz (Set.mem_compl_singleton_iff.mp hn), pow_succ', mul_smul]

/--
If two functions agree on a punctured neighborhood, then one is meromorphic iff the other is so.
-/
lemma meromorphicAt_congr {f g : ğ•œ â†’ E} (h : f =á¶ [ğ“[â‰ ] x] g) :
    MeromorphicAt f x â†” MeromorphicAt g x :=
  âŸ¨fun hf â†¦ hf.congr h, fun hg â†¦ hg.congr h.symmâŸ©

@[simp]
lemma update_iff [DecidableEq ğ•œ] {f : ğ•œ â†’ E} {z w : ğ•œ} {e : E} :
    MeromorphicAt (Function.update f w e) z â†” MeromorphicAt f z :=
  meromorphicAt_congr (Function.update_eventuallyEq_nhdsNE f w z e)

@[fun_prop]
lemma update [DecidableEq ğ•œ] {f : ğ•œ â†’ E} {z} (hf : MeromorphicAt f z) (w e) :
    MeromorphicAt (Function.update f w e) z :=
  update_iff.mpr hf

@[to_fun (attr := fun_prop)]
lemma inv {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) : MeromorphicAt fâ»Â¹ x := by
  rcases hf with âŸ¨m, hfâŸ©
  by_cases h_eq : (fun z â†¦ (z - x) ^ m â€¢ f z) =á¶ [ğ“ x] 0
  Â· -- silly case: f locally 0 near x
    refine (MeromorphicAt.const 0 x).congr ?_
    rw [eventuallyEq_nhdsWithin_iff]
    filter_upwards [h_eq] with z hfz hz
    rw [Pi.inv_apply, (smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)).mp hfz,
      inv_zero]
  Â· -- interesting case: use local formula for `f`
    obtain âŸ¨n, g, hg_an, hg_ne, hg_eqâŸ© := hf.exists_eventuallyEq_pow_smul_nonzero_iff.mpr h_eq
    have : AnalyticAt ğ•œ (fun z â†¦ (z - x) ^ (m + 1)) x :=
      (analyticAt_id.sub analyticAt_const).pow _
    -- use `m + 1` rather than `m` to damp out any silly issues with the value at `z = x`
    refine âŸ¨n + 1, (this.fun_smul <| hg_an.inv hg_ne).congr ?_âŸ©
    filter_upwards [hg_eq, hg_an.continuousAt.eventually_ne hg_ne] with z hfg hg_ne'
    rcases eq_or_ne z x with rfl | hz_ne
    Â· simp only [sub_self, pow_succ, mul_zero, zero_smul]
    Â· simp_rw [smul_eq_mul] at hfg âŠ¢
      have aux1 : f z â‰  0 := by
        have : (z - x) ^ n * g z â‰  0 := mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz_ne)) hg_ne'
        rw [â† hfg, mul_ne_zero_iff] at this
        exact this.2
      simp [field, pow_succ', mul_assoc, hfg]

@[simp]
lemma inv_iff {f : ğ•œ â†’ ğ•œ} :
    MeromorphicAt fâ»Â¹ x â†” MeromorphicAt f x :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, MeromorphicAt.invâŸ©

@[to_fun (attr := fun_prop)]
lemma div {f g : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    MeromorphicAt (f / g) x :=
  (div_eq_mul_inv f g).symm â–¸ (hf.mul hg.inv)

@[to_fun (attr := fun_prop)]
lemma pow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (n : â„•) : MeromorphicAt (f ^ n) x := by
  induction n with
  | zero => simpa only [pow_zero] using MeromorphicAt.const 1 x
  | succ m hm => simpa only [pow_succ] using hm.mul hf

@[to_fun (attr := fun_prop)]
lemma zpow {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (n : â„¤) : MeromorphicAt (f ^ n) x := by
  cases n with
  | ofNat m => simpa only [Int.ofNat_eq_natCast, zpow_natCast] using hf.pow m
  | negSucc m => simpa only [zpow_negSucc, inv_iff] using hf.pow (m + 1)

/-- If a function is meromorphic at a point, then it is continuous at nearby points. -/
theorem eventually_continuousAt {f : ğ•œ â†’ E}
    (h : MeromorphicAt f x) : âˆ€á¶  y in ğ“[â‰ ] x, ContinuousAt f y := by
  obtain âŸ¨n, hâŸ© := h
  have : âˆ€á¶  y in ğ“[â‰ ] x, ContinuousAt (fun z â†¦ (z - x) ^ n â€¢ f z) y :=
    nhdsWithin_le_nhds h.eventually_continuousAt
  filter_upwards [this, self_mem_nhdsWithin] with y hy h'y
  simp only [Set.mem_compl_iff, Set.mem_singleton_iff] at h'y
  have : ContinuousAt (fun z â†¦ ((z - x) ^ n)â»Â¹) y :=
    ContinuousAt.invâ‚€ (by fun_prop) (by simp [sub_eq_zero, h'y])
  apply (this.smul hy).congr
  filter_upwards [eventually_ne_nhds h'y] with z hz
  simp [smul_smul, hz, sub_eq_zero]

/-- In a complete space, a function which is meromorphic at a point is analytic at all nearby
points. The completeness assumption can be dispensed with if one assumes that `f` is meromorphic
on a set around `x`, see `MeromorphicOn.eventually_analyticAt`. -/
theorem eventually_analyticAt [CompleteSpace E] {f : ğ•œ â†’ E}
    (h : MeromorphicAt f x) : âˆ€á¶  y in ğ“[â‰ ] x, AnalyticAt ğ•œ f y := by
  obtain âŸ¨n, hâŸ© := h
  apply AnalyticAt.eventually_analyticAt at h
  refine (h.filter_mono ?_).mp ?_
  Â· simp [nhdsWithin]
  Â· rw [eventually_nhdsWithin_iff]
    apply Filter.Eventually.of_forall
    intro y hy hf
    rw [Set.mem_compl_iff, Set.mem_singleton_iff] at hy
    have := ((analyticAt_id (ğ•œ := ğ•œ).sub analyticAt_const).pow n).inv
      (pow_ne_zero _ (sub_ne_zero_of_ne hy))
    apply (this.smul hf).congr âˆ˜ (eventually_ne_nhds hy).mono
    intro z hz
    simp [smul_smul, hz, sub_eq_zero]

lemma iff_eventuallyEq_zpow_smul_analyticAt {f : ğ•œ â†’ E} : MeromorphicAt f x â†”
    âˆƒ (n : â„¤) (g : ğ•œ â†’ E), AnalyticAt ğ•œ g x âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  refine âŸ¨fun âŸ¨n, hnâŸ© â†¦ âŸ¨-n, _, âŸ¨hn, eventually_nhdsWithin_iff.mpr ?_âŸ©âŸ©, ?_âŸ©
  Â· filter_upwards with z hz
    match_scalars
    simp [sub_ne_zero.mpr hz]
  Â· refine fun âŸ¨n, g, hg_an, hg_eqâŸ© â†¦ MeromorphicAt.congr ?_ (EventuallyEq.symm hg_eq)
    exact (((MeromorphicAt.id x).sub (.const _ x)).zpow _).smul hg_an.meromorphicAt

/--
Derivatives of meromorphic functions are meromorphic.
-/
@[fun_prop]
protected theorem deriv [CompleteSpace E] {f : ğ•œ â†’ E} {x : ğ•œ} (h : MeromorphicAt f x) :
    MeromorphicAt (deriv f) x := by
  rw [MeromorphicAt.iff_eventuallyEq_zpow_smul_analyticAt] at h
  obtain âŸ¨n, g, hâ‚g, hâ‚‚gâŸ© := h
  have : _root_.deriv (fun z â†¦ (z - x) ^ n â€¢ g z)
      =á¶ [ğ“[â‰ ] x] fun z â†¦ (n * (z - x) ^ (n - 1)) â€¢ g z + (z - x) ^ n â€¢ _root_.deriv g z := by
    filter_upwards [eventually_nhdsWithin_of_eventually_nhds hâ‚g.eventually_analyticAt,
      eventually_nhdsWithin_of_forall fun _ a â†¦ a] with zâ‚€ hâ‚ hâ‚‚
    rw [deriv_fun_smul (DifferentiableAt.zpow (by fun_prop) (by simp_all [sub_ne_zero_of_ne hâ‚‚]))
      (by fun_prop), add_comm, deriv_comp_sub_const (f := (Â· ^ n))]
    aesop
  rw [MeromorphicAt.meromorphicAt_congr (Filter.EventuallyEq.nhdsNE_deriv hâ‚‚g),
    MeromorphicAt.meromorphicAt_congr this]
  fun_prop

/--
Iterated derivatives of meromorphic functions are meromorphic.
-/
@[fun_prop] theorem iterated_deriv [CompleteSpace E] {n : â„•} {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicAt f x) :
    MeromorphicAt (_root_.deriv^[n] f) x := by
  induction n with
  | zero => exact h
  | succ n IH => simpa only [Function.iterate_succ', Function.comp_apply] using IH.deriv

end MeromorphicAt

section smul_iff

variable {g : ğ•œ â†’ ğ•œ} {x : ğ•œ}

lemma meromorphicAt_smul_iff_of_ne_zero {f : ğ•œ â†’ E} (hg : AnalyticAt ğ•œ g x) (hg' : g x â‰  0) :
    MeromorphicAt (g â€¢ f) x â†” MeromorphicAt f x := by
  refine âŸ¨fun hfg â†¦ ?_, hg.meromorphicAt.smulâŸ©
  refine (hg.inv hg').meromorphicAt.smul hfg |>.congr ?_
  filter_upwards [(hg.continuousAt.mono_left nhdsWithin_le_nhds).eventually_ne hg'] with z hz
  simp [inv_smul_smulâ‚€ hz]

lemma meromorphicAt_mul_iff_of_ne_zero {f : ğ•œ â†’ ğ•œ} (hg : AnalyticAt ğ•œ g x) (hg' : g x â‰  0) :
    MeromorphicAt (g * f) x â†” MeromorphicAt f x :=
  meromorphicAt_smul_iff_of_ne_zero hg hg'

end smul_iff

section composition
/-!
### Composition with an analytic function
-/

variable
  {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
  {x : ğ•œ}

/--
The composition of a meromorphic and an analytic function is meromorphic.
-/
lemma MeromorphicAt.comp_analyticAt {f : ğ•œ' â†’ F} {g : ğ•œ â†’ ğ•œ'}
    (hf : MeromorphicAt f (g x)) (hg : AnalyticAt ğ•œ g x) :
    MeromorphicAt (f âˆ˜ g) x := by
  obtain âŸ¨r, hrâŸ© := hf
  by_cases hg' : analyticOrderAt (g Â· - g x) x = âŠ¤
  Â· -- trivial case: `g` is locally constant near `x`
    refine .congr (.const (f (g x)) x) ?_
    filter_upwards [nhdsWithin_le_nhds <| analyticOrderAt_eq_top.mp hg'] with z hz
    grind
  Â· -- interesting case: `g z - g x` looks like `(z - x) ^ n` times a non-vanishing function
    obtain âŸ¨n, hnâŸ© := WithTop.ne_top_iff_exists.mp hg'
    obtain âŸ¨h, han, hne, heqâŸ© := (hg.fun_sub analyticAt_const).analyticOrderAt_eq_natCast.mp hn.symm
    set j := fun z â†¦ (z - g x) ^ r â€¢ f z
    have : AnalyticAt ğ•œ (fun i â†¦ (h i)â»Â¹ ^ r â€¢ j (g i)) x :=
      ((han.fun_inv hne).fun_pow r).fun_smul (hr.restrictScalars.comp' hg)
    refine âŸ¨n * r, this.congr ?_âŸ©
    filter_upwards [heq, han.continuousAt.tendsto.eventually_ne hne] with z hz hzne
    simp only [j, inv_pow, Function.comp_apply, inv_smul_eq_iffâ‚€ (pow_ne_zero r hzne)]
    rw [hz, smul_comm, â† smul_assoc, pow_mul, smul_pow]

lemma meromorphicAt_comp_iff_of_deriv_ne_zero [CompleteSpace ğ•œ] [CharZero ğ•œ] {f : ğ•œ â†’ E}
    {g : ğ•œ â†’ ğ•œ} (hg : AnalyticAt ğ•œ g x) (hg' : deriv g x â‰  0) :
    MeromorphicAt (f âˆ˜ g) x â†” MeromorphicAt f (g x) := by
  refine âŸ¨fun hf â†¦ ?_, (MeromorphicAt.comp_analyticAt Â· hg)âŸ©
  let r := hg.hasStrictDerivAt.localInverse _ _ _ hg'
  have hra : AnalyticAt ğ•œ r (g x) := hg.analyticAt_localInverse hg'
  have : r (g x) = x := HasStrictFDerivAt.localInverse_apply_image ..
  rw [â† this] at hf
  refine (hf.comp_analyticAt hra).congr (.filter_mono ?_ nhdsWithin_le_nhds)
  exact EventuallyEq.fun_comp (HasStrictDerivAt.eventually_right_inverse ..) f

end composition


/-- Meromorphy of a function on a set. -/
def MeromorphicOn (f : ğ•œ â†’ E) (U : Set ğ•œ) : Prop := âˆ€ x âˆˆ U, MeromorphicAt f x

lemma AnalyticOnNhd.meromorphicOn {f : ğ•œ â†’ E} {U : Set ğ•œ} (hf : AnalyticOnNhd ğ•œ f U) :
    MeromorphicOn f U :=
  fun x hx â†¦ (hf x hx).meromorphicAt

namespace MeromorphicOn

variable {s t : ğ•œ â†’ ğ•œ} {f g : ğ•œ â†’ E} {U : Set ğ•œ}
  (hs : MeromorphicOn s U) (ht : MeromorphicOn t U)
  (hf : MeromorphicOn f U) (hg : MeromorphicOn g U)

/--
If `f` is meromorphic on `U`, if `g` agrees with `f` on a codiscrete subset of `U` and outside of
`U`, then `g` is also meromorphic on `U`.
-/
theorem congr_codiscreteWithin_of_eqOn_compl (hf : MeromorphicOn f U)
    (hâ‚ : f =á¶ [codiscreteWithin U] g) (hâ‚‚ : Set.EqOn f g Uá¶œ) :
    MeromorphicOn g U := by
  intro x hx
  apply (hf x hx).congr
  simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
    disjoint_principal_right] at hâ‚
  filter_upwards [hâ‚ x hx] with a ha
  simp at ha
  tauto

/--
If `f` is meromorphic on an open set `U`, if `g` agrees with `f` on a codiscrete subset of `U`, then
`g` is also meromorphic on `U`.
-/
theorem congr_codiscreteWithin (hf : MeromorphicOn f U) (hâ‚ : f =á¶ [codiscreteWithin U] g)
    (hâ‚‚ : IsOpen U) :
    MeromorphicOn g U := by
  intro x hx
  apply (hf x hx).congr
  simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
    disjoint_principal_right] at hâ‚
  have : U âˆˆ ğ“[â‰ ] x := by
    apply mem_nhdsWithin.mpr
    use U, hâ‚‚, hx, Set.inter_subset_left
  filter_upwards [this, hâ‚ x hx] with a hâ‚a hâ‚‚a
  simp only [Set.mem_compl_iff, Set.mem_diff, Set.mem_setOf_eq, not_and] at hâ‚‚a
  tauto

lemma id {U : Set ğ•œ} : MeromorphicOn id U := fun x _ â†¦ .id x

lemma const (e : E) {U : Set ğ•œ} : MeromorphicOn (fun _ â†¦ e) U :=
  fun x _ â†¦ .const e x

section arithmetic

include hf in
lemma mono_set {V : Set ğ•œ} (hv : V âŠ† U) : MeromorphicOn f V := fun x hx â†¦ hf x (hv hx)

include hf hg in
@[to_fun] lemma add : MeromorphicOn (f + g) U := fun x hx â†¦ (hf x hx).add (hg x hx)

include hf hg in
@[to_fun] lemma sub : MeromorphicOn (f - g) U := fun x hx â†¦ (hf x hx).sub (hg x hx)

include hf in
@[to_fun] lemma neg : MeromorphicOn (-f) U := fun x hx â†¦ (hf x hx).neg

@[simp] lemma neg_iff : MeromorphicOn (-f) U â†” MeromorphicOn f U :=
  âŸ¨fun h â†¦ by simpa only [neg_neg] using h.neg, negâŸ©

include hs hf in
@[to_fun] lemma smul : MeromorphicOn (s â€¢ f) U := fun x hx â†¦ (hs x hx).smul (hf x hx)

include hs ht in
@[to_fun] lemma mul : MeromorphicOn (s * t) U := fun x hx â†¦ (hs x hx).mul (ht x hx)

/-- Finite products of meromorphic functions are meromorphic. -/
lemma prod {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (âˆ n âˆˆ s, f n) U :=
  fun z hz â†¦ MeromorphicAt.prod (fun Ïƒ â†¦ h Ïƒ z hz)

/-- Finite products of meromorphic functions are meromorphic. -/
lemma fun_prod {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (fun z â†¦ âˆ n âˆˆ s, f n z) U :=
  fun z hz â†¦ MeromorphicAt.fun_prod (fun Ïƒ â†¦ h Ïƒ z hz)

/-- Finite sums of meromorphic functions are meromorphic. -/
lemma sum {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ E} (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (âˆ‘ n âˆˆ s, f n) U :=
  fun z hz â†¦ MeromorphicAt.sum (fun Ïƒ â†¦ h Ïƒ z hz)

/-- Finite sums of meromorphic functions are meromorphic. -/
lemma fun_sum {U : Set ğ•œ} {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ E}
    (h : âˆ€ Ïƒ, MeromorphicOn (f Ïƒ) U) :
    MeromorphicOn (fun z â†¦ âˆ‘ n âˆˆ s, f n z) U :=
  fun z hz â†¦ MeromorphicAt.fun_sum (fun Ïƒ â†¦ h Ïƒ z hz)

include hs in
@[to_fun] lemma inv : MeromorphicOn sâ»Â¹ U := fun x hx â†¦ (hs x hx).inv

@[simp] lemma inv_iff : MeromorphicOn sâ»Â¹ U â†” MeromorphicOn s U :=
  âŸ¨fun h â†¦ by simpa only [inv_inv] using h.inv, invâŸ©

include hs ht in
@[to_fun] lemma div : MeromorphicOn (s / t) U := fun x hx â†¦ (hs x hx).div (ht x hx)

include hs in
@[to_fun] lemma pow (n : â„•) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).pow _

include hs in
@[to_fun] lemma zpow (n : â„¤) : MeromorphicOn (s ^ n) U := fun x hx â†¦ (hs x hx).zpow _

include hf in
/-- Derivatives of meromorphic functions are meromorphic. -/
protected theorem deriv [CompleteSpace E] : MeromorphicOn (deriv f) U := fun z hz â†¦ (hf z hz).deriv

include hf in
/-- Iterated derivatives of meromorphic functions are meromorphic. -/
theorem iterated_deriv [CompleteSpace E] {n : â„•} : MeromorphicOn (_root_.deriv^[n] f) U :=
  fun z hz â†¦ (hf z hz).iterated_deriv

end arithmetic

include hf in
lemma congr (h_eq : Set.EqOn f g U) (hu : IsOpen U) : MeromorphicOn g U := by
  refine fun x hx â†¦ (hf x hx).congr (EventuallyEq.filter_mono ?_ nhdsWithin_le_nhds)
  exact eventually_of_mem (hu.mem_nhds hx) h_eq

theorem eventually_codiscreteWithin_analyticAt
    [CompleteSpace E] (f : ğ•œ â†’ E) (h : MeromorphicOn f U) :
    âˆ€á¶  (y : ğ•œ) in codiscreteWithin U, AnalyticAt ğ•œ f y := by
  rw [eventually_iff, mem_codiscreteWithin]
  intro x hx
  rw [disjoint_principal_right]
  apply Filter.mem_of_superset ((h x hx).eventually_analyticAt)
  intro x hx
  simp [hx]

/--
The singular set of a meromorphic function is countable.
-/
theorem countable_compl_analyticAt_inter [SecondCountableTopology ğ•œ] [CompleteSpace E]
    (h : MeromorphicOn f U) :
    ({z | AnalyticAt ğ•œ f z}á¶œ âˆ© U).Countable := by
  apply (HereditarilyLindelof_LindelofSets _).countable_of_isDiscrete
    (isDiscrete_of_codiscreteWithin _)
  simpa using eventually_codiscreteWithin_analyticAt f h

end MeromorphicOn

/-- Meromorphy of a function on all of ğ•œ. -/
@[fun_prop]
def Meromorphic (f : ğ•œ â†’ E) := âˆ€ x, MeromorphicAt f x

/-- A function is meromorphic iff it is meromorphic on Set.univ. -/
@[simp]
lemma meromorphicOn_univ {f : ğ•œ â†’ E} : MeromorphicOn f Set.univ â†” Meromorphic f := by tauto

namespace Meromorphic

variable
  {Î¹ : Type*} {s : Finset Î¹}
  {f g : ğ•œ â†’ E} {F : Î¹ â†’ ğ•œ â†’ ğ•œ} {G : Î¹ â†’ ğ•œ â†’ E}

@[fun_prop]
lemma meromorphicAt {x : ğ•œ} (hf : Meromorphic f) : MeromorphicAt f x := hf x

lemma meromorphicOn {s : Set ğ•œ} (hf : Meromorphic f) : MeromorphicOn f s := fun x _ â†¦ hf x

@[fun_prop]
lemma const (x : E) : Meromorphic fun _ : ğ•œ â†¦ x := fun _ â†¦ .const _ _

@[to_fun (attr := fun_prop)]
lemma neg (hf : Meromorphic f) : Meromorphic (-f) := fun x â†¦ (hf x).neg

@[to_fun (attr := fun_prop)]
lemma add (hf : Meromorphic f) (hg : Meromorphic g) :
    Meromorphic (f + g) := fun x â†¦ (hf x).add (hg x)

@[to_fun (attr := fun_prop)]
theorem sum (h : âˆ€ Ïƒ, Meromorphic (G Ïƒ)) :
    Meromorphic (âˆ‘ n âˆˆ s, G n) := fun x â†¦ MeromorphicAt.sum (h Â· x)

@[to_fun (attr := fun_prop)]
lemma sub (hf : Meromorphic f) (hg : Meromorphic g) :
    Meromorphic (f - g) := fun x â†¦ (hf x).sub (hg x)

@[to_fun (attr := fun_prop)]
lemma smul {f : ğ•œ â†’ ğ•œ} (hf : Meromorphic f) (hg : Meromorphic g) :
    Meromorphic (f â€¢ g) := fun x â†¦ (hf x).smul (hg x)

@[to_fun (attr := fun_prop)]
lemma mul {f g : ğ•œ â†’ ğ•œ} (hf : Meromorphic f) (hg : Meromorphic g) :
    Meromorphic (f * g) := fun x â†¦ (hf x).mul (hg x)

@[to_fun (attr := fun_prop)]
lemma inv {f : ğ•œ â†’ ğ•œ} (hf : Meromorphic f) : Meromorphic fâ»Â¹ := fun x â†¦ (hf x).inv

@[to_fun (attr := fun_prop)]
theorem prod (h : âˆ€ Ïƒ, Meromorphic (F Ïƒ)) :
    Meromorphic (âˆ n âˆˆ s, F n) := fun x â†¦ MeromorphicAt.prod (h Â· x)

@[to_fun (attr := fun_prop)]
lemma div {f g : ğ•œ â†’ ğ•œ} (hf : Meromorphic f) (hg : Meromorphic g) :
    Meromorphic (f / g) := fun x â†¦ (hf x).div (hg x)

@[to_fun (attr := fun_prop)]
lemma pow {f : ğ•œ â†’ ğ•œ} {n : â„•} (hf : Meromorphic f) : Meromorphic (f ^ n) := fun x â†¦ (hf x).pow n

@[to_fun (attr := fun_prop)]
lemma zpow {f : ğ•œ â†’ ğ•œ} {n : â„¤} (hf : Meromorphic f) : Meromorphic (f ^ n) := fun x â†¦ (hf x).zpow n

@[fun_prop]
protected lemma deriv [CompleteSpace E] (hf : Meromorphic f) : Meromorphic (deriv f) :=
    fun x â†¦ (hf x).deriv

@[fun_prop]
lemma iterated_deriv [CompleteSpace E] {n : â„•} (hf : Meromorphic f) :
    Meromorphic (deriv^[n] f) := fun x â†¦ (hf x).iterated_deriv

/--
The singular set of a meromorphic function is countable.
-/
theorem countable_compl_analyticAt [SecondCountableTopology ğ•œ] [CompleteSpace E]
    (h : Meromorphic f) :
    {z | AnalyticAt ğ•œ f z}á¶œ.Countable := by
  simpa using (h.meromorphicOn (s := univ)).countable_compl_analyticAt_inter

@[deprecated (since := "2025-12-21")] alias MeromorphicOn.countable_compl_analyticAt :=
  countable_compl_analyticAt

/--
Meromorphic functions are measurable.
-/
theorem measurable [MeasurableSpace ğ•œ] [SecondCountableTopology ğ•œ] [BorelSpace ğ•œ]
    [MeasurableSpace E] [CompleteSpace E] [BorelSpace E] (h : Meromorphic f) :
    Measurable f := by
  set s := {z : ğ•œ | AnalyticAt ğ•œ f z}
  have hâ‚ : sá¶œ.Countable := by simpa using h.countable_compl_analyticAt
  have hâ‚' := hâ‚.to_subtype
  have hâ‚‚ : IsOpen s := isOpen_analyticAt ğ•œ f
  have hâ‚ƒ : ContinuousOn f s := fun z hz â†¦ hz.continuousAt.continuousWithinAt
  exact .of_union_range_cover (.subtype_coe hâ‚‚.measurableSet) (.subtype_coe hâ‚.measurableSet)
    (by simp [-mem_compl_iff]) hâ‚ƒ.restrict.measurable (measurable_of_countable _)

@[deprecated (since := "2025-12-21")] alias MeromorphicOn.measurable := measurable

end Meromorphic
