/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.Meromorphic.NormalForm

/-!
# Laurent Polynomials

This file discusses Laurent polynomials as examples of meromorphic functions in normal form. Laurent
polynomials are functions on a non-trivially normed field `ğ•œ` of the form `(âˆá¶  u, fun z â†¦ (z - u) ^
d u)`, where `d : ğ•œ â†’ â„¤` has finite support. We show that Laurent polynomials are meromorphic in
normal form, with divisor equal to `d`.

## Implementation Notice

For simplicity of notation, we use the abbreviation `LaurentPolynomial d` to describe the function
`âˆá¶  u, fun z â†¦ (z - u) ^ d u`.
-/

open Real Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]
  {U : Set ğ•œ}
  {z : ğ•œ}

/--
Laurent polynomials are functions on a non-trivially normed field `ğ•œ` of the form
`(âˆá¶  u, fun z â†¦ (z - u) ^ d u)`.
-/
noncomputable abbrev LaurentPolynomial (d : ğ•œ â†’ â„¤) := (âˆá¶  u, (Â· - u) ^ d u)

private lemma analyticAt_finLaurentPolynomial_off_support (d : ğ•œ â†’ â„¤) (P : Finset ğ•œ)
    (hz : z âˆ‰ P) :
    AnalyticAt ğ•œ (âˆ u âˆˆ P, fun z â†¦ (z - u) ^ d u) z := by
  rw [Finset.prod_fn]
  apply Finset.analyticAt_prod
  intro u hu
  apply AnalyticAt.zpow (by fun_prop)
  rw [sub_ne_zero, ne_comm]
  exact ne_of_mem_of_not_mem hu hz

/-- Laurent polynomials are meromorphic in normal form on `âŠ¤`. -/
theorem meromorphicNFOn_laurentPolynomial_top (d : ğ•œ â†’ â„¤) :
    MeromorphicNFOn (LaurentPolynomial d) âŠ¤ := by
  classical
  by_cases hd : (Function.mulSupport fun u => (Â· - u) ^ d u).Finite
  Â· rw [LaurentPolynomial, finprod_eq_prod _ hd]
    intro z hz
    by_cases hâ‚‚z : z âˆˆ hd.toFinset
    Â· rw [â† Finset.mul_prod_erase hd.toFinset _ hâ‚‚z]
      right
      use d z, âˆ x âˆˆ hd.toFinset.erase z, fun z => (z - x) ^ d x,
        analyticAt_finLaurentPolynomial_off_support d (hd.toFinset.erase z)
          (Finset.not_mem_erase z hd.toFinset)
      constructor
      Â· rw [Finset.prod_apply, Finset.prod_ne_zero_iff]
        intro u hu
        apply zpow_ne_zero
        rw [sub_ne_zero]
        by_contra hCon
        rw [hCon] at hu
        have := Finset.not_mem_erase u hd.toFinset
        tauto
      Â· exact Filter.Eventually.of_forall (congrFun rfl)
    Â· exact (analyticAt_finLaurentPolynomial_off_support d hd.toFinset hâ‚‚z).meromorphicNFAt
  Â· rw [LaurentPolynomial, finprod_of_infinite_mulSupport hd]
    apply analyticOnNhd_const.meromorphicNFOn

/-- Laurent polynomials are meromorphic in normal form on arbitrary subsets of `ğ•œ`. -/
theorem meromorphicNFOn_laurentPolynomial (d : ğ•œ â†’ â„¤) (U : Set ğ•œ) :
    MeromorphicNFOn (LaurentPolynomial d) U := by
  intro z hz
  exact meromorphicNFOn_laurentPolynomial_top d (trivial)

/--
Helper Lemma: Identifying the support of `d` as the mulsupport of the product defining the Laurent
polynomial.
-/
lemma Function.mulSupport_laurentPolynomial (d : ğ•œ â†’ â„¤) :
    (Function.mulSupport fun u â†¦ (Â· - u) ^ d u) = d.support := by
  ext u
  constructor
  Â· intro h
    simp_all only [Function.mem_mulSupport, ne_eq, Function.mem_support]
    by_contra hCon
    simp only [hCon, zpow_zero] at h
    tauto
  Â· intro h
    simp only [Function.mem_mulSupport, ne_eq]
    by_contra hCon
    have := congrFun hCon u
    simp only [Pi.pow_apply, sub_self, Pi.ofNat_apply] at this
    have : (0 : ğ•œ) ^ d u â‰  0 := ne_zero_of_eq_one this
    rw [zpow_ne_zero_iff h] at this
    tauto

/-- The order of the Laurent polynomial `(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` at z equals `d z`. -/
theorem order_laurentPolynomial {z : ğ•œ} (d : ğ•œ â†’ â„¤) (hâ‚d : Set.Finite d.support) :
    (((meromorphicNFOn_laurentPolynomial_top d).meromorphicOn) z trivial).order = d z := by
  classical
  rw [MeromorphicAt.order_eq_int_iff]
  use âˆ x âˆˆ hâ‚d.toFinset.erase z, fun z => (z - x) ^ d x,
    analyticAt_finLaurentPolynomial_off_support d (hâ‚d.toFinset.erase z)
      (Finset.not_mem_erase z hâ‚d.toFinset)
  constructor
  Â· simp only [Finset.prod_apply]
    rw [Finset.prod_ne_zero_iff]
    intro u hu
    apply zpow_ne_zero
    rw [sub_ne_zero]
    by_contra hCon
    rw [hCon] at hu
    have := Finset.not_mem_erase u hâ‚d.toFinset
    tauto
  Â· apply Filter.Eventually.of_forall
    intro x
    have tâ‚€ : (Function.mulSupport fun u => (Â· - u) ^ d u).Finite := by
      rwa [Function.mulSupport_laurentPolynomial d]
    have tâ‚ : hâ‚d.toFinset = tâ‚€.toFinset := by
      simp [eq_comm, Function.mulSupport_laurentPolynomial d]
    rw [LaurentPolynomial, finprod_eq_prod _ tâ‚€, tâ‚, eq_comm]
    simp only [Finset.prod_apply, smul_eq_mul]
    by_cases hz : z âˆˆ hâ‚d.toFinset
    Â· rw [tâ‚] at hz
      simp_rw [â† Finset.mul_prod_erase tâ‚€.toFinset _ hz]
      simp
    Â· have : tâ‚€.toFinset = tâ‚€.toFinset.erase z := by
        rw [eq_comm]
        apply Finset.erase_eq_of_not_mem
        rwa [tâ‚] at hz
      rw [this]
      have : (x - z) ^ d z = 1 := by
        simp only [Set.Finite.mem_toFinset, Function.mem_support, ne_eq, Decidable.not_not] at hz
        simp [hz]
      rw [this]
      simp

/--
Laurent polynomials are nowhere locally constant zero.
-/
theorem order_LaurentPolynomial_ne_top {z : ğ•œ} (d : ğ•œ â†’ â„¤) :
    (meromorphicNFOn_laurentPolynomial_top d (trivial : z âˆˆ âŠ¤)).meromorphicAt.order â‰  âŠ¤ := by
  by_cases hd : Set.Finite (Function.support d)
  Â· simp [order_laurentPolynomial d hd]
  Â· rw [â† Function.mulSupport_laurentPolynomial] at hd
    have : AnalyticAt ğ•œ (1 : ğ•œ â†’ ğ•œ) z := analyticAt_const
    simp [finprod_of_infinite_mulSupport hd, this.meromorphicAt_order,
      this.order_eq_zero_iff.2 (by simp)]

/--
The divisor function associated with the divisor of the Laurent polynomial
`(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` equals `d`.
-/
theorem divisor_LaurentPolynomial [CompleteSpace ğ•œ] (d : ğ•œ â†’ â„¤) (hâ‚d : Set.Finite d.support) :
    MeromorphicOn.divisor (LaurentPolynomial d) âŠ¤ = d := by
  ext z
  simp_rw [(meromorphicNFOn_laurentPolynomial_top d).meromorphicOn.divisor_apply
    (by simp : z âˆˆ Set.univ)]
  rw [order_laurentPolynomial d hâ‚d]
  simp

/--
If `D` is a divisor, then the function associated with the divisor of the Laurent polynomial equals
`D`.
-/
theorem divisor_laurentPolynomial_within [CompleteSpace ğ•œ] {U : Set ğ•œ}
    (D : Function.locallyFinsuppWithin U â„¤) (hD : Set.Finite D.support) :
    MeromorphicOn.divisor (LaurentPolynomial D) U = D := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp [(meromorphicNFOn_laurentPolynomial D U).meromorphicOn, hz,
      MeromorphicOn.divisor_apply, order_laurentPolynomial D hD]
  Â· simp [hz]
