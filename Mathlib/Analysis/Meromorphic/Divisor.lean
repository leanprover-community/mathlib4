/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Algebra.Order.WithTop.Untop0
import Mathlib.Analysis.Meromorphic.Order
import Mathlib.Data.LocallyFinsupp

/-!
# The Divisor of a meromorphic function

This file defines the divisor of a meromorphic function and proves the most basic lemmas about those
divisors.

## TODO

- Compatibility with restriction of divisors/functions
- Congruence lemmas for `codiscreteWithin`
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ} {z : ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]

namespace MeromorphicOn

/-!
## Definition of the Divisor
-/

open Classical in
/--
The divisor of a meromorphic function `f`, mapping a point `z` to the order of `f` at `z`, and to
zero if the order is infinite.
-/
noncomputable def divisor (f : ğ•œ â†’ E) (U : Set ğ•œ) :
    Function.locallyFinsuppWithin U â„¤ where
  toFun := fun z â†¦ if h : MeromorphicOn f U âˆ§ z âˆˆ U then ((h.1 z h.2).order.untopâ‚€) else 0
  supportWithinDomain' z hz := by
    by_contra hâ‚‚z
    simp [hâ‚‚z] at hz
  supportLocallyFiniteWithinDomain' := by
    simp_all only [Function.support_subset_iff, ne_eq, dite_eq_right_iff, WithTop.untopâ‚€_eq_zero,
      not_forall, not_or, forall_exists_index, implies_true,
      â† supportDiscreteWithin_iff_locallyFiniteWithin]
    by_cases hf : MeromorphicOn f U
    Â· filter_upwards [mem_codiscrete_subtype_iff_mem_codiscreteWithin.1
        hf.codiscrete_setOf_order_eq_zero_or_top]
      simp only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, exists_and_right,
        exists_eq_right, Pi.zero_apply, dite_eq_right_iff, WithTop.untopâ‚€_eq_zero,
        WithTop.coe_zero]
      tauto
    Â· simp only [hf, false_and, â†“reduceDIte]
      exact (Eq.eventuallyEq rfl)

open Classical in
/-- Definition of the divisor -/
theorem divisor_def (f : ğ•œ â†’ E) (U : Set ğ•œ) :
    divisor f U z = if h : MeromorphicOn f U âˆ§ z âˆˆ U then (h.1 z h.2).order.untopâ‚€ else 0 :=
  rfl

/--
Simplifier lemma: on `U`, the divisor of a function `f` that is meromorphic on `U` evaluates to
`order.untopâ‚€`.
-/
@[simp]
lemma divisor_apply {f : ğ•œ â†’ E} (hf : MeromorphicOn f U) (hz : z âˆˆ U) :
    divisor f U z = (hf z hz).order.untopâ‚€ := by simp_all [MeromorphicOn.divisor_def, hz]

/-!
## Divisors of Analytic Functions
-/

/-- Analytic functions have non-negative divisors. -/
theorem AnalyticOnNhd.divisor_nonneg {f : ğ•œ â†’ E} (hf : AnalyticOnNhd ğ•œ f U) :
    0 â‰¤ MeromorphicOn.divisor f U := by
  intro x
  by_cases hx : x âˆˆ U
  Â· simp [hf.meromorphicOn, hx, (hf x hx).meromorphicAt_order_nonneg]
  simp [hx]

/-!
## Behavior under Standard Operations
-/

/--
If orders are finite, the divisor of the scalar product of two meromorphic functions is the sum of
the divisors.

See `MeromorphicOn.exists_order_ne_top_iff_forall` and
`MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to guarantee conditions
`hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisor_smul [CompleteSpace ğ•œ] {fâ‚ : ğ•œ â†’ ğ•œ} {fâ‚‚ : ğ•œ â†’ E} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚ z hz).order â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚‚ z hz).order â‰  âŠ¤) :
    divisor (fâ‚ â€¢ fâ‚‚) U = divisor fâ‚ U + divisor fâ‚‚ U := by
  ext z
  by_cases hz : z âˆˆ U
  Â· lift (hâ‚fâ‚ z hz).order to â„¤ using (hâ‚‚fâ‚ z hz) with aâ‚ haâ‚
    lift (hâ‚fâ‚‚ z hz).order to â„¤ using (hâ‚‚fâ‚‚ z hz) with aâ‚‚ haâ‚‚
    simp [hâ‚fâ‚, hâ‚fâ‚‚, hâ‚fâ‚.smul hâ‚fâ‚‚, hz, (hâ‚fâ‚ z hz).order_smul (hâ‚fâ‚‚ z hz), â† haâ‚, â† haâ‚‚,
      â† WithTop.coe_add]
  Â· simp [hz]

/--
If orders are finite, the divisor of the product of two meromorphic functions is the sum of the
divisors.

See `MeromorphicOn.exists_order_ne_top_iff_forall` and
`MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to guarantee conditions
`hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisor_mul [CompleteSpace ğ•œ] {fâ‚ fâ‚‚ : ğ•œ â†’ ğ•œ} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚ z hz).order â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚‚ z hz).order â‰  âŠ¤) :
    divisor (fâ‚ * fâ‚‚) U = divisor fâ‚ U + divisor fâ‚‚ U :=
  divisor_smul hâ‚fâ‚ hâ‚fâ‚‚ hâ‚‚fâ‚ hâ‚‚fâ‚‚

/-- The divisor of the inverse is the negative of the divisor. -/
@[simp]
theorem divisor_inv [CompleteSpace ğ•œ] {f : ğ•œ â†’ ğ•œ} :
    divisor fâ»Â¹ U = -divisor f U := by
  ext z
  by_cases h : MeromorphicOn f U âˆ§ z âˆˆ U
  Â· simp [divisor_apply, h, (h.1 z h.2).order_inv]
  Â· simp [divisor_def, h]

/-- Adding an analytic function to a meromorphic one does not change the pole divisor. -/
theorem negPart_divisor_add_of_analyticNhdOn_right {fâ‚ fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hfâ‚‚ : AnalyticOnNhd ğ•œ fâ‚‚ U) :
    (divisor fâ‚ U)â» = (divisor (fâ‚ + fâ‚‚) U)â» := by
  ext x
  by_cases hx : x âˆˆ U
  Â· simp [negPart_def, hx, hfâ‚, hfâ‚.add hfâ‚‚.meromorphicOn]
    by_cases h : 0 â‰¤ (hfâ‚ x hx).order
    Â· simp only [Int.neg_nonpos_iff_nonneg, WithTop.untopâ‚€_nonneg, h, sup_of_le_right,
        right_eq_sup]
      calc 0
      _ â‰¤ min (hfâ‚ x hx).order (hfâ‚‚.meromorphicOn x hx).order := by
        exact le_inf h (hfâ‚‚ x hx).meromorphicAt_order_nonneg
      _ â‰¤ ((hfâ‚.add hfâ‚‚.meromorphicOn) x hx).order := by
        exact (hfâ‚ x hx).order_add (hfâ‚‚ x hx).meromorphicAt
    Â· simp at h
      rw [(hfâ‚ x hx).order_add_of_order_lt_order (hfâ‚‚.meromorphicOn x hx)]
      calc (hfâ‚ x hx).order
      _ < 0 := h
      _ â‰¤ (hfâ‚‚.meromorphicOn x hx).order := (hfâ‚‚ x hx).meromorphicAt_order_nonneg
  simp [hx]

end MeromorphicOn
