/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Algebra.Order.WithTop.Untop0
import Mathlib.Analysis.Meromorphic.Order
import Mathlib.Topology.LocallyFinsupp

/-!
# The Divisor of a meromorphic function

This file defines the divisor of a meromorphic function and proves the most basic lemmas about those
divisors. The lemma `MeromorphicOn.divisor_restrict` guarantees compatibility between restrictions
of divisors and of meromorphic functions to subsets of their domain of definition.
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ} {z : ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

open Filter Topology

namespace MeromorphicOn

/-!
## Definition of the Divisor
-/

open Classical in
/--
The divisor of a meromorphic function `f`, mapping a point `z` to the order of `f` at `z`, and to
zero if the order is infinite.
-/
noncomputable def divisor (f : ğ•œ â†’ E) (U : Set ğ•œ) :
    Function.locallyFinsuppWithin U â„¤ where
  toFun := fun z â†¦ if MeromorphicOn f U âˆ§ z âˆˆ U then (meromorphicOrderAt f z).untopâ‚€ else 0
  supportWithinDomain' z hz := by
    by_contra hâ‚‚z
    simp [hâ‚‚z] at hz
  supportLocallyFiniteWithinDomain' := by
    simp_all only [Function.support_subset_iff, ne_eq, ite_eq_right_iff, WithTop.untopâ‚€_eq_zero,
      and_imp, Classical.not_imp, not_or, implies_true,
      â† supportDiscreteWithin_iff_locallyFiniteWithin]
    by_cases hf : MeromorphicOn f U
    Â· filter_upwards [mem_codiscrete_subtype_iff_mem_codiscreteWithin.1
        hf.codiscrete_setOf_meromorphicOrderAt_eq_zero_or_top]
      simp only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, exists_and_left, exists_prop,
        exists_eq_right_right, Pi.ofNat_apply, ite_eq_right_iff, WithTop.untopâ‚€_eq_zero, and_imp]
      tauto
    Â· simp [hf, Pi.zero_def]

open Classical in
/-- Definition of the divisor -/
theorem divisor_def (f : ğ•œ â†’ E) (U : Set ğ•œ) :
    divisor f U z = if MeromorphicOn f U âˆ§ z âˆˆ U then (meromorphicOrderAt f z).untopâ‚€ else 0 :=
  rfl

/--
Simplifier lemma: on `U`, the divisor of a function `f` that is meromorphic on `U` evaluates to
`order.untopâ‚€`.
-/
@[simp]
lemma divisor_apply {f : ğ•œ â†’ E} (hf : MeromorphicOn f U) (hz : z âˆˆ U) :
    divisor f U z = (meromorphicOrderAt f z).untopâ‚€ := by simp_all [MeromorphicOn.divisor_def]

/-!
## Congruence Lemmas
-/

/--
If `fâ‚` is meromorphic on `U`, if `fâ‚‚` agrees with `fâ‚` on a codiscrete subset of `U` and outside of
`U`, then `fâ‚` and `fâ‚‚` induce the same divisors on `U`.
-/
theorem divisor_congr_codiscreteWithin_of_eqOn_compl {fâ‚ fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚ : fâ‚ =á¶ [Filter.codiscreteWithin U] fâ‚‚) (hâ‚‚ : Set.EqOn fâ‚ fâ‚‚ Uá¶œ) :
    divisor fâ‚ U = divisor fâ‚‚ U := by
  ext x
  by_cases hx : x âˆˆ U
  Â· simp only [hfâ‚, hx, divisor_apply, hfâ‚.congr_codiscreteWithin_of_eqOn_compl hâ‚ hâ‚‚]
    congr 1
    apply meromorphicOrderAt_congr
    simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
      disjoint_principal_right] at hâ‚
    filter_upwards [hâ‚ x hx] with a ha
    simp at ha
    tauto
  Â· simp [hx]

/--
If `fâ‚` is meromorphic on an open set `U`, if `fâ‚‚` agrees with `fâ‚` on a codiscrete subset of `U`,
then `fâ‚` and `fâ‚‚` induce the same divisors on`U`.
-/
theorem divisor_congr_codiscreteWithin {fâ‚ fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚ : fâ‚ =á¶ [Filter.codiscreteWithin U] fâ‚‚) (hâ‚‚ : IsOpen U) :
    divisor fâ‚ U = divisor fâ‚‚ U := by
  ext x
  by_cases hx : x âˆˆ U
  Â· simp only [hfâ‚, hx, divisor_apply, hfâ‚.congr_codiscreteWithin hâ‚ hâ‚‚]
    congr 1
    apply meromorphicOrderAt_congr
    simp_rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin,
      disjoint_principal_right] at hâ‚
    have : U âˆˆ ğ“[â‰ ] x := by
      apply mem_nhdsWithin.mpr
      use U, hâ‚‚, hx, Set.inter_subset_left
    filter_upwards [this, hâ‚ x hx] with a hâ‚a hâ‚‚a
    simp only [Set.mem_compl_iff, Set.mem_diff, Set.mem_setOf_eq, not_and] at hâ‚‚a
    tauto
  Â· simp [hx]

/-!
## Divisors of Analytic Functions
-/

/-- Analytic functions have non-negative divisors. -/
theorem AnalyticOnNhd.divisor_nonneg {f : ğ•œ â†’ E} (hf : AnalyticOnNhd ğ•œ f U) :
    0 â‰¤ MeromorphicOn.divisor f U := by
  intro x
  by_cases hx : x âˆˆ U
  Â· simp [hf.meromorphicOn, hx, (hf x hx).meromorphicOrderAt_nonneg]
  simp [hx]

/-!
## Behavior under Standard Operations
-/

/--
If orders are finite, the divisor of the scalar product of two meromorphic functions is the sum of
the divisors.

See `MeromorphicOn.exists_order_ne_top_iff_forall` and
`MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to guarantee conditions
`hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisor_smul {fâ‚ : ğ•œ â†’ ğ•œ} {fâ‚‚ : ğ•œ â†’ E} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z âˆˆ U, meromorphicOrderAt fâ‚ z â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z âˆˆ U, meromorphicOrderAt fâ‚‚ z â‰  âŠ¤) :
    divisor (fâ‚ â€¢ fâ‚‚) U = divisor fâ‚ U + divisor fâ‚‚ U := by
  ext z
  by_cases hz : z âˆˆ U
  Â· lift meromorphicOrderAt fâ‚ z to â„¤ using (hâ‚‚fâ‚ z hz) with aâ‚ haâ‚
    lift meromorphicOrderAt fâ‚‚ z to â„¤ using (hâ‚‚fâ‚‚ z hz) with aâ‚‚ haâ‚‚
    simp [hâ‚fâ‚, hâ‚fâ‚‚, hâ‚fâ‚.smul hâ‚fâ‚‚, hz, meromorphicOrderAt_smul (hâ‚fâ‚ z hz) (hâ‚fâ‚‚ z hz),
      â† haâ‚, â† haâ‚‚, â† WithTop.coe_add]
  Â· simp [hz]

/--
If orders are finite, the divisor of the product of two meromorphic functions is the sum of the
divisors.

See `MeromorphicOn.exists_order_ne_top_iff_forall` and
`MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to guarantee conditions
`hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisor_mul {fâ‚ fâ‚‚ : ğ•œ â†’ ğ•œ} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z âˆˆ U, meromorphicOrderAt fâ‚ z â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z âˆˆ U, meromorphicOrderAt fâ‚‚ z â‰  âŠ¤) :
    divisor (fâ‚ * fâ‚‚) U = divisor fâ‚ U + divisor fâ‚‚ U :=
  divisor_smul hâ‚fâ‚ hâ‚fâ‚‚ hâ‚‚fâ‚ hâ‚‚fâ‚‚

/-- The divisor of the inverse is the negative of the divisor. -/
@[simp]
theorem divisor_inv {f : ğ•œ â†’ ğ•œ} :
    divisor fâ»Â¹ U = -divisor f U := by
  ext z
  by_cases h : MeromorphicOn f U âˆ§ z âˆˆ U
  Â· simp [divisor_apply, h, meromorphicOrderAt_inv]
  Â· simp [divisor_def, h]

/--
Taking the divisor of a meromorphic function commutes with restriction.
-/
@[simp]
theorem divisor_restrict {f : ğ•œ â†’ E} {V : Set ğ•œ} (hf : MeromorphicOn f U) (hV : V âŠ† U) :
    (divisor f U).restrict hV = divisor f V := by
  ext x
  by_cases hx : x âˆˆ V
  Â· rw [Function.locallyFinsuppWithin.restrict_apply]
    simp [hf, hx, hf.mono_set hV, hV hx]
  Â· simp [hx]

/-- Adding an analytic function to a meromorphic one does not change the pole divisor. -/
theorem negPart_divisor_add_of_analyticNhdOn_right {fâ‚ fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : MeromorphicOn fâ‚ U)
    (hfâ‚‚ : AnalyticOnNhd ğ•œ fâ‚‚ U) :
    (divisor (fâ‚ + fâ‚‚) U)â» = (divisor fâ‚ U)â» := by
  ext x
  by_cases hx : x âˆˆ U
  Â· suffices -(meromorphicOrderAt (fâ‚ + fâ‚‚) x).untopâ‚€ âŠ” 0 = -(meromorphicOrderAt fâ‚ x).untopâ‚€ âŠ” 0 by
      simpa [negPart_def, hx, hfâ‚, hfâ‚.add hfâ‚‚.meromorphicOn]
    by_cases h : 0 â‰¤ meromorphicOrderAt fâ‚ x
    Â· suffices 0 â‰¤ meromorphicOrderAt (fâ‚ + fâ‚‚) x by simp_all
      calc 0
      _ â‰¤ min (meromorphicOrderAt fâ‚ x) (meromorphicOrderAt fâ‚‚ x) :=
        le_inf h (hfâ‚‚ x hx).meromorphicOrderAt_nonneg
      _ â‰¤ meromorphicOrderAt (fâ‚ + fâ‚‚) x :=
        meromorphicOrderAt_add (hfâ‚ x hx) (hfâ‚‚ x hx).meromorphicAt
    Â· suffices meromorphicOrderAt fâ‚ x < meromorphicOrderAt fâ‚‚ x by
        rwa [meromorphicOrderAt_add_eq_left_of_lt (hfâ‚‚.meromorphicOn x hx)]
      calc meromorphicOrderAt fâ‚ x
      _ < 0 := by simpa using h
      _ â‰¤ meromorphicOrderAt fâ‚‚ x := (hfâ‚‚ x hx).meromorphicOrderAt_nonneg
  simp [hx]

/-- Adding an analytic function to a meromorphic one does not change the pole divisor. -/
theorem negPart_divisor_add_of_analyticNhdOn_left {fâ‚ fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : AnalyticOnNhd ğ•œ fâ‚ U)
    (hfâ‚‚ : MeromorphicOn fâ‚‚ U) :
    (divisor (fâ‚ + fâ‚‚) U)â» = (divisor fâ‚‚ U)â» := by
  rw [add_comm]
  exact negPart_divisor_add_of_analyticNhdOn_right hfâ‚‚ hfâ‚

end MeromorphicOn
