/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# Divisors on subsets of normed fields

This file defines divisors, a standard book-keeping tool in complex analysis
used to keep track of pole/vanishing orders of meromorphic objects, typically
functions or differential forms. It provides supporting API and establishes
divisors as an instance of a lattice ordered commutative group.

Throughout the present file, `ğ•œ` denotes a nontrivially normed field, and `U` a
subset of `ğ•œ`.

## TODOs

- Constructions: The divisor of a meromorphic function, behavior under product
  of meromorphic functions, behavior under addition, behavior under restriction
- Construction: The divisor of a rational polynomial
-/

open Filter Metric Real Set Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ}

/-!
## Definition, coercion to functions and basic extensionality lemmas

A divisor on `U` is a function `ğ•œ â†’ â„¤` whose support is discrete within `U` and
entirely contained within `U`.  The theorem
`supportDiscreteWithin_iff_locallyFiniteWithin` shows that this is equivalent to
the textbook definition, which requires the support of `f` to be locally finite
within `U`.
-/

/-- A divisor on `U` is a triple specified below. -/
structure DivisorOn (U : Set ğ•œ) where
  /-- A function `ğ•œ â†’ â„¤` -/
  toFun : ğ•œ â†’ â„¤
  /-- A proof that the support of `toFun` is contained in `U` -/
  supportWithinDomain' : toFun.support âŠ† U
  /-- A proof the the support is discrete within `U` -/
  supportDiscreteWithinDomain' : toFun =á¶ [codiscreteWithin U] 0

/-- A divisor is a divisor on `âŠ¤ : Set ğ•œ`. -/
def Divisor (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] := DivisorOn (âŠ¤ : Set ğ•œ)

/-- The condition `supportDiscreteWithinU` in a divisor is equivalent to saying
that the support is locally finite near every point of `U`. -/
theorem supportDiscreteWithin_iff_locallyFiniteWithin {f : ğ•œ â†’ â„¤} (h : f.support âŠ† U) :
    f =á¶ [codiscreteWithin U] 0 â†” âˆ€ z âˆˆ U, âˆƒ t âˆˆ ğ“ z, Set.Finite (t âˆ© f.support) := by
  have : f.support = (U \ {x | f x = (0 : ğ•œ â†’ â„¤) x}) := by
    ext x
    simp only [Function.mem_support, ne_eq, Pi.zero_apply, mem_diff, mem_setOf_eq, iff_and_self]
    exact (h Â·)
  rw [EventuallyEq, Filter.Eventually, codiscreteWithin_iff_locallyFiniteComplementWithin, this]

namespace DivisorOn

/-- Divisors are `FunLike`: the coercion from divisors to functions is injective. -/
instance : FunLike (DivisorOn U) ğ•œ â„¤ where
  coe D := D.toFun
  coe_injective' := fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© â†¦ by simp

/-- This allows writing `D.support` instead of `Function.support D` -/
abbrev support (D : DivisorOn U)  := Function.support D

lemma supportWithinDomain (D : DivisorOn U) : D.support âŠ† U := D.supportWithinDomain'

lemma supportDiscreteWithinDomain (D : DivisorOn U) : D =á¶ [codiscreteWithin U] 0 :=
  D.supportDiscreteWithinDomain'

/-- Helper lemma for the `ext` tactic: two divisors are equal if their
associated functions agree. -/
@[ext]
theorem ext {Dâ‚ Dâ‚‚ : DivisorOn U} (h : âˆ€ a, Dâ‚ a = Dâ‚‚ a) : Dâ‚ = Dâ‚‚ := DFunLike.ext _ _ h

/-!
## Elementary properties of the support
-/

/-- The support of a divisor is discrete. -/
theorem discreteSupport (D : DivisorOn U) : DiscreteTopology D.support := by
  have : Function.support D = {x | D x = 0}á¶œ âˆ© U := by
    ext x
    constructor
    Â· exact fun hx â†¦ âŸ¨by tauto, D.supportWithinDomain hxâŸ©
    Â· intro hx
      rw [mem_inter_iff, mem_compl_iff, mem_setOf_eq] at hx
      tauto
  convert discreteTopology_of_codiscreteWithin (D.supportDiscreteWithinDomain)

/-- If `U` is closed, the the support of a divisor on `U` is also closed. -/
theorem closedSupport (D : DivisorOn U) (hU : IsClosed U) :
    IsClosed D.support := by
  convert closed_compl_of_codiscreteWithin D.supportDiscreteWithinDomain hU
  ext x
  constructor
  Â· intro hx
    simp_all [D.supportWithinDomain hx]
  Â· intro hx
    simp_all

/-- If `U` is closed, the the support of a divisor on `U` is finite. -/
theorem finiteSupport (D : DivisorOn U) (hU : IsCompact U) :
    Set.Finite D.support :=
  (hU.of_isClosed_subset (D.closedSupport hU.isClosed)
    D.supportWithinDomain).finite D.discreteSupport

/-!
## Lattice ordered group structure

This section equips divisors on `U` with the standard structure of a lattice
ordered group, where addition, comparison, min and max of divisors are defined
pointwise.
-/

/-- Divisors have a zero -/
instance : Zero (DivisorOn U) where
  zero := âŸ¨fun _ â†¦ 0, by simp, Eq.eventuallyEq rflâŸ©

/-- Helper lemma for the `simp` tactic: the function of the zero-divisor is the
zero function. -/
@[simp]
theorem zero_fun : ((0 : DivisorOn U) : ğ•œ â†’ â„¤) = 0 := rfl

/-- Divisors can be added -/
instance : Add (DivisorOn U) where
  add Dâ‚ Dâ‚‚ := {
    toFun := Dâ‚ + Dâ‚‚
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a â†¦ hx (Dâ‚.supportWithinDomain a),
        Function.nmem_support.1 fun a â†¦ hx (Dâ‚‚.supportWithinDomain a)]
    supportDiscreteWithinDomain' := Dâ‚.supportDiscreteWithinDomain.add
      Dâ‚‚.supportDiscreteWithinDomain
  }

/-- Helper lemma for the `simp` tactic: the function of the sum of two divisors
is the sum of the associated functions. -/
@[simp]
lemma coe_add {Dâ‚ Dâ‚‚ : DivisorOn U} : (â†‘(Dâ‚ + Dâ‚‚) : ğ•œ â†’ â„¤) = (Dâ‚ : ğ•œ â†’ â„¤) + (Dâ‚‚ : ğ•œ â†’ â„¤) := rfl

/-- Divisors have a negative -/
instance : Neg (DivisorOn U) where
  neg D := {
    toFun := -D
    supportWithinDomain' := by
      intro x hx
      rw [Function.support_neg', Function.mem_support, ne_eq] at hx
      exact D.supportWithinDomain hx
    supportDiscreteWithinDomain' := D.supportDiscreteWithinDomain.neg
  }

/-- Helper lemma for the `simp` tactic: the function of the negative divisor
is the negative of the associated function. -/
@[simp]
lemma neg_fun {D : DivisorOn U} : (â†‘(-D) : ğ•œ â†’ â„¤) = -(D: ğ•œ â†’ â„¤) := rfl

/-- Divisors have scalar multiplication with natural numbers -/
instance : SMul â„• (DivisorOn U) where
  smul n D := {
    toFun := fun z â†¦ n * D z
    supportWithinDomain' := by
      intro x hx
      simp at hx
      exact D.supportWithinDomain hx.2
    supportDiscreteWithinDomain' := by
      filter_upwards [D.supportDiscreteWithinDomain]
      intro x hx
      simp [hx]
  }

/-- Helper lemma for the `simp` tactic: the function of a scalar product
(natural number)Â·divisor is the scalar product of the natural number with the
associated function of the divisor. -/
@[simp]
lemma nsmul_fun {D : DivisorOn U} {n : â„•} : (â†‘(n â€¢ D) : ğ•œ â†’ â„¤) = n â€¢ (D : ğ•œ â†’ â„¤) := rfl

/-- Divisors have scalar multiplication with integers -/
instance : SMul â„¤ (DivisorOn U) where
  smul n D := {
    toFun := fun z â†¦ n * D z
    supportWithinDomain' := by
      intro x hx
      simp at hx
      exact D.supportWithinDomain hx.2
    supportDiscreteWithinDomain' := by
      filter_upwards [D.supportDiscreteWithinDomain]
      intro _ hx
      simp [hx]
  }

/-- Helper lemma for the `simp` tactic: the function of a scalar product
(integer)Â·divisor is the scalar product of the integer with the associated
function of the divisor. -/
@[simp]
lemma zsmul_fun {D : DivisorOn U} {n : â„¤} : (â†‘(n â€¢ D) : ğ•œ â†’ â„¤) = n â€¢ (D : ğ•œ â†’ â„¤) := rfl

/-- Divisors have a partial ordering by pointwise comparison of the associated
functions. -/
instance : LE (DivisorOn U) where
  le := fun Dâ‚ Dâ‚‚ â†¦ (Dâ‚ : ğ•œ â†’ â„¤) â‰¤ Dâ‚‚

/-- Helper lemma for the `simp` tactic: a divisor is smaller than another one
if the same relation holds with the associated functions. -/
@[simp]
lemma le_def {Dâ‚ Dâ‚‚ : DivisorOn U} : Dâ‚ â‰¤ Dâ‚‚ â†” (Dâ‚ : ğ•œ â†’ â„¤) â‰¤ (Dâ‚‚ : ğ•œ â†’ â„¤) := âŸ¨(Â·),(Â·)âŸ©

/-- Divisors form an ordered commutative group -/
instance : OrderedAddCommGroup (DivisorOn U) where
  add := (Â· + Â· )
  add_assoc := fun _ _ _ â†¦ by ext; simp [add_assoc]
  zero := 0
  zero_add := fun _ â†¦ by ext; simp
  add_zero := fun _ â†¦ by ext; simp
  nsmul := (Â· â€¢ Â·)
  neg := (- Â·)
  zsmul := (Â· â€¢ Â·)
  neg_add_cancel := fun _ â†¦ by ext; simp

  add_comm := fun _ _ â†¦ by ext; simp [add_comm]
  nsmul_zero := fun _ â†¦ by ext; simp
  nsmul_succ := fun _ _ â†¦ by ext; simp [add_one_mul]
  zsmul_zero' := fun _ â†¦ by ext; simp
  zsmul_succ' := fun _ _ â†¦ by ext; simp [add_one_mul]
  zsmul_neg' := fun _ _ â†¦ by ext; simp; apply negSucc_zsmul
  le := (Â· â‰¤ Â·)
  le_refl := by tauto
  le_trans := fun Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ â†¦ by simp only [le_def] at hâ‚â‚‚ hâ‚‚â‚ƒ; exact hâ‚â‚‚.trans hâ‚‚â‚ƒ
  le_antisymm := fun _ _ hâ‚â‚‚ hâ‚‚â‚ â†¦ by ext x; exact Int.le_antisymm (hâ‚â‚‚ x) (hâ‚‚â‚ x)
  add_le_add_left := fun _ _ _ _ â†¦ by simpa

/-- Divisors have a partial ordering by pointwise comparison of the associated
functions. -/
instance : LT (DivisorOn U) where
  lt := fun Dâ‚ Dâ‚‚ â†¦ Dâ‚.toFun < Dâ‚‚.toFun

/-- Helper lemma for the `simp` tactic: a divisor is smaller than another one
if the same relation holds with the associated functions. -/
@[simp]
lemma lt_fun {Dâ‚ Dâ‚‚ : DivisorOn U} : Dâ‚ < Dâ‚‚ â†” (Dâ‚ : ğ•œ â†’ â„¤) < (Dâ‚‚ : ğ•œ â†’ â„¤) := âŸ¨(Â·),(Â·)âŸ©

/-- Divisors have a max. -/
instance : Max (DivisorOn U) where
  max Dâ‚ Dâ‚‚ := {
    toFun := fun z â†¦ max (Dâ‚ z) (Dâ‚‚ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a â†¦ hx (Dâ‚.supportWithinDomain a),
        Function.nmem_support.1 fun a â†¦ hx (Dâ‚‚.supportWithinDomain a)]
    supportDiscreteWithinDomain' := by
      filter_upwards [Dâ‚.supportDiscreteWithinDomain, Dâ‚‚.supportDiscreteWithinDomain]
      intro _ hâ‚ hâ‚‚
      simp [hâ‚, hâ‚‚]
  }

/-- Helper lemma for the `simp` tactic: the function associated with the max of
two divisors is the pointwise max of the associated functions. -/
@[simp]
lemma max_fun {Dâ‚ Dâ‚‚ : DivisorOn U} {x : ğ•œ} : max Dâ‚ Dâ‚‚ x = max (Dâ‚ x) (Dâ‚‚ x) := rfl

/-- Divisors have a min. -/
instance : Min (DivisorOn U) where
  min Dâ‚ Dâ‚‚ := {
    toFun := fun z â†¦ min (Dâ‚ z) (Dâ‚‚ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a â†¦ hx (Dâ‚.supportWithinDomain a),
        Function.nmem_support.1 fun a â†¦ hx (Dâ‚‚.supportWithinDomain a)]
    supportDiscreteWithinDomain' := by
      filter_upwards [Dâ‚.supportDiscreteWithinDomain, Dâ‚‚.supportDiscreteWithinDomain]
      intro _ hâ‚ hâ‚‚
      simp [hâ‚, hâ‚‚]
  }

/-- Helper lemma for the `simp` tactic: the function associated with the max of
two divisors is the pointwise max of the associated functions. -/
@[simp]
lemma min_fun {Dâ‚ Dâ‚‚ : DivisorOn U} {x : ğ•œ} : min Dâ‚ Dâ‚‚ x = min (Dâ‚ x) (Dâ‚‚ x) := rfl

/-- Divisors form a lattice. -/
instance : Lattice (DivisorOn U) where
  le := (Â· â‰¤ Â·)
  le_refl := by simp
  le_trans := by exact fun Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ x â†¦ (hâ‚â‚‚ x).trans (hâ‚‚â‚ƒ x)
  le_antisymm := by
    intro Dâ‚ Dâ‚‚ hâ‚â‚‚ hâ‚‚â‚
    ext x
    exact Int.le_antisymm (hâ‚â‚‚ x) (hâ‚‚â‚ x)
  sup := (max Â· Â·)
  le_sup_left := fun Dâ‚ Dâ‚‚ x â†¦ by simp
  le_sup_right := fun Dâ‚ Dâ‚‚ x â†¦ by simp
  sup_le := fun Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚ƒ hâ‚‚â‚ƒ x â†¦ by simp [hâ‚â‚ƒ x, hâ‚‚â‚ƒ x]
  inf := (min Â· Â·)
  inf_le_left := fun Dâ‚ Dâ‚‚ x â†¦ by simp
  inf_le_right := fun Dâ‚ Dâ‚‚ x â†¦ by simp
  le_inf := fun Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚ƒ hâ‚‚â‚ƒ x â†¦ by simp [hâ‚â‚ƒ x, hâ‚‚â‚ƒ x]

/-!
## Restriction
-/

/-- If `V` is a subset of `U`, then a divisor on `U` restricts to a divisor in `V` by
setting its values to zero outside of `V`. -/
noncomputable def restrict {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    DivisorOn V where
  toFun := by
    classical
    exact fun z â†¦ if hz : z âˆˆ V then D z else 0
  supportWithinDomain' := by
    intro x hx
    simp_rw [dite_eq_ite, Function.mem_support, ne_eq, ite_eq_right_iff,
      Classical.not_imp] at hx
    exact hx.1
  supportDiscreteWithinDomain' := by
    apply Filter.codiscreteWithin.mono h
    filter_upwards [D.supportDiscreteWithinDomain]
    intro x hx
    simp [hx]

open Classical in
lemma restrict_apply {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) (z : ğ•œ) :
    (D.restrict h) z = if z âˆˆ V then D z else 0 := rfl

/-- Helper lemma for the `simp` tactic: restricting a divisor from `U` to a
subset `V` does not change its values on `V`. -/
@[simp]
lemma restrict_eqOn {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    Set.EqOn (D.restrict h) D V := by
  intro _ _
  simp_all [restrict_apply, dite_eq_ite, ite_eq_left_iff]

/-- Helper lemma for the `simp` tactic: restricting a divisor from `U` to a
subset `V` makes it zero outside of `V`. -/
@[simp]
lemma restrict_eqOn_compl {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    Set.EqOn (D.restrict h) 0 Vá¶œ := by
  intro _ hx
  simp_all [restrict_apply, dite_eq_ite, ite_eq_left_iff, hx]

/-- Restriction as an order-preserving morphism -/
noncomputable def restrict_orderHom {V : Set ğ•œ} (h : V âŠ† U) : DivisorOn U â†’o DivisorOn V where
  toFun := fun D â†¦ D.restrict h
  monotone' := by
    intro Dâ‚ Dâ‚‚ hâ‚â‚‚ x
    simp only [restrict_apply]
    split
    Â· apply hâ‚â‚‚
    Â· exact le_rfl

/-- Helper lemma for the `simp` tactic: `restrict_orderHom` restricts divisors. -/
@[simp]
lemma restrict_orderHom_fun {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    restrict_orderHom h D = D.restrict h := by rfl

/-- Restriction as a group morphism -/
noncomputable def restrict_groupHom {V : Set ğ•œ} (h : V âŠ† U) : DivisorOn U â†’+ DivisorOn V where
  toFun := fun D â†¦ D.restrict h
  map_zero' := by
    ext x
    simp [restrict_apply]
  map_add' := by
    intro Dâ‚ Dâ‚‚
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [restrict_apply, hx]

/-- Helper lemma for the `simp` tactic: `restrict_groupHom` restricts divisors. -/
@[simp]
lemma restrict_groupHom_fun {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    restrict_groupHom h D = D.restrict h := by rfl

/-- Restriction as a lattice morphism -/
noncomputable def restrict_latticeHom {V : Set ğ•œ} (h : V âŠ† U) :
    LatticeHom (DivisorOn U) (DivisorOn V) where
  toFun := fun D â†¦ D.restrict h
  map_sup' := by
    intro Dâ‚ Dâ‚‚
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [DivisorOn.restrict_apply, hx]
  map_inf' := by
    intro Dâ‚ Dâ‚‚
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [DivisorOn.restrict_apply, hx]

/-- Helper lemma for the `simp` tactic: `restrict_latticeHom` restricts divisors. -/
@[simp]
lemma restrict_latticeHom_fun {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    restrict_latticeHom h D = D.restrict h := by rfl

/-!
## Derived invariants
-/

/-- The degree of a divisor on U is the sum of its values, or 0 if the support is infinite. -/
noncomputable def deg (D : DivisorOn U) : â„¤ := âˆ‘á¶  z, D z

end DivisorOn

/-- The counting function for a divisor -/
noncomputable def Divisor.counting (D : Divisor ğ•œ) : â„ â†’ â„ :=
  fun r â†¦ âˆ‘á¶  z, D.restrict (by tauto : closedBall (0 : ğ•œ) |r| âŠ† âŠ¤) z

/-- The logarithmic counting function for a divisor -/
noncomputable def Divisor.logCounting (D : Divisor ğ•œ) : â„ â†’ â„ :=
  fun r â†¦ âˆ‘á¶  z, D.restrict (by tauto : closedBall (0 : ğ•œ) |r| âŠ† âŠ¤) z * (log r - log â€–zâ€–)
