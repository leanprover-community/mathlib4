/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# Divisors on subsets of normed fields

This file defines divisors, a standard book-keeping tool in complex analysis
used to keep track of pole/vanishing orders of meromorphic objects, typically
functions or differential forms. It provides supporting API and establishes
divisors as an instance of a lattice ordered commutative group.

Throughout the present file, `ùïú` denotes a nontrivially normed field, and `U` a
subset of `ùïú`.

## TODOs

- Constructions: The divisor of a meromorphic function, behavior under product
  of meromorphic functions, behavior under addition, behavior under restriction
- Construction: The divisor of a rational polynomial
-/

open Filter Metric Real Set Topology

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {U : Set ùïú}

/-!
## Definition, coercion to functions and basic extensionality lemmas

A divisor on `U` is a function `ùïú ‚Üí ‚Ñ§` whose support is discrete within `U` and
entirely contained within `U`.  The theorem
`supportDiscreteWithin_iff_locallyFiniteWithin` shows that this is equivalent to
the textbook definition, which requires the support of `f` to be locally finite
within `U`.
-/

/-- A divisor on `U` is a triple specified below. -/
structure Divisor (U : Set ùïú) where
  /-- A function `ùïú ‚Üí ‚Ñ§` -/
  toFun : ùïú ‚Üí ‚Ñ§
  /-- A proof that the support of `toFun` is contained in `U` -/
  supportWithinDomain : toFun.support ‚äÜ U
  /-- A proof the the support is discrete within `U` -/
  supportDiscreteWithinDomain : toFun =·∂†[codiscreteWithin U] 0

/-- The condition `supportDiscreteWithinU` in a divisor is equivalent to saying
that the support is locally finite near every point of `U`. -/
theorem supportDiscreteWithin_iff_locallyFiniteWithin {f : ùïú ‚Üí ‚Ñ§} (h : f.support ‚äÜ U) :
    f =·∂†[codiscreteWithin U] 0 ‚Üî ‚àÄ z ‚àà U, ‚àÉ t ‚àà ùìù z, Set.Finite (t ‚à© f.support) := by
  have : f.support = (U \ {x | f x = (0 : ùïú ‚Üí ‚Ñ§) x}) := by
    ext x
    simp only [Function.mem_support, ne_eq, Pi.zero_apply, mem_diff, mem_setOf_eq, iff_and_self]
    exact (h ¬∑)
  rw [EventuallyEq, Filter.Eventually, codiscreteWithin_iff_locallyFiniteComplementWithin, this]

namespace Divisor

/-- A divisor can be coerced into a function ùïú ‚Üí ‚Ñ§ -/
instance (U : Set ùïú) : CoeFun (Divisor U) (fun _ ‚Ü¶ ùïú ‚Üí ‚Ñ§) where
  coe := Divisor.toFun

/-- This allows writing `D.support` instead of `Function.support D` -/
abbrev support (D : Divisor U)  := Function.support D

/-- Divisors are `FunLike`: the coercion from divisors to functions is injective. -/
instance : FunLike (Divisor U) ùïú ‚Ñ§ where
  coe := fun D ‚Ü¶ D
  coe_injective' := fun ‚ü®_, _, _‚ü© ‚ü®_, _, _‚ü© ‚Ü¶ by simp

/-- Helper lemma for the `ext` tactic: two divisors are equal if their
associated functions agree. -/
@[ext]
theorem ext {D‚ÇÅ D‚ÇÇ : Divisor U} (h : ‚àÄ a, D‚ÇÅ a = D‚ÇÇ a) : D‚ÇÅ = D‚ÇÇ := DFunLike.ext _ _ h

/-!
## Elementary properties of the support
-/

/-- The support of a divisor is discrete. -/
theorem discreteSupport (D : Divisor U) : DiscreteTopology D.support := by
  have : Function.support D = {x | D x = 0}·∂ú ‚à© U := by
    ext x
    constructor
    ¬∑ exact fun hx ‚Ü¶ ‚ü®by tauto, D.supportWithinDomain hx‚ü©
    ¬∑ intro hx
      rw [mem_inter_iff, mem_compl_iff, mem_setOf_eq] at hx
      tauto
  convert discreteTopology_of_codiscreteWithin (D.supportDiscreteWithinDomain)

/-- If `U` is closed, the the support of a divisor on `U` is also closed. -/
theorem closedSupport (D : Divisor U) (hU : IsClosed U) :
    IsClosed D.support := by
  convert closed_compl_of_codiscreteWithin D.supportDiscreteWithinDomain hU
  ext x
  constructor
  ¬∑ intro hx
    simp_all [D.supportWithinDomain hx]
  ¬∑ intro hx
    simp_all

/-- If `U` is closed, the the support of a divisor on `U` is finite. -/
theorem finiteSupport (D : Divisor U) (hU : IsCompact U) :
    Set.Finite D.support :=
  (hU.of_isClosed_subset (D.closedSupport hU.isClosed)
    D.supportWithinDomain).finite D.discreteSupport

/-!
## Lattice ordered group structure

This section equips divisors on `U` with the standard structure of a lattice
ordered group, where addition, comparison, min and max of divisors are defined
pointwise.
-/

/-- Divisors have a zero -/
instance : Zero (Divisor U) where
  zero := ‚ü®fun _ ‚Ü¶ 0, by simp, Eq.eventuallyEq rfl‚ü©

/-- Helper lemma for the `simp` tactic: the function of the zero-divisor is the
zero function. -/
@[simp]
theorem zero_fun : (0 : Divisor U).toFun = 0 := rfl

/-- Divisors can be added -/
instance : Add (Divisor U) where
  add D‚ÇÅ D‚ÇÇ := {
    toFun := D‚ÇÅ + D‚ÇÇ
    supportWithinDomain := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÅ.supportWithinDomain a),
        Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÇ.supportWithinDomain a)]
    supportDiscreteWithinDomain := D‚ÇÅ.supportDiscreteWithinDomain.add
      D‚ÇÇ.supportDiscreteWithinDomain
  }

/-- Helper lemma for the `simp` tactic: the function of the sum of two divisors
is the sum of the associated functions. -/
@[simp]
lemma add_fun {D‚ÇÅ D‚ÇÇ : Divisor U} : (D‚ÇÅ + D‚ÇÇ).toFun = D‚ÇÅ.toFun + D‚ÇÇ.toFun := rfl

/-- Divisors have a negative -/
instance : Neg (Divisor U) where
  neg D := {
    toFun := -D
    supportWithinDomain := by
      intro x hx
      rw [Function.support_neg', Function.mem_support, ne_eq] at hx
      exact D.supportWithinDomain hx
    supportDiscreteWithinDomain := D.supportDiscreteWithinDomain.neg
  }

/-- Helper lemma for the `simp` tactic: the function of the negative divisor
is the negative of the associated function. -/
@[simp]
lemma neg_fun {D : Divisor U} : (-D).toFun = -(D.toFun) := rfl

/-- Divisors have scalar multiplication with natural numbers -/
instance : SMul ‚Ñï (Divisor U) where
  smul n D := {
    toFun := fun z ‚Ü¶ n * D z
    supportWithinDomain := by
      intro x hx
      simp at hx
      exact D.supportWithinDomain hx.2
    supportDiscreteWithinDomain := by
      filter_upwards [D.supportDiscreteWithinDomain]
      intro x hx
      simp [hx]
  }

/-- Helper lemma for the `simp` tactic: the function of a scalar product
(natural number)¬∑divisor is the scalar product of the natural number with the
associated function of the divisor. -/
@[simp]
lemma nsmul_fun {D : Divisor U} {n : ‚Ñï} : (n ‚Ä¢ D).toFun = n ‚Ä¢ (D.toFun) := rfl

/-- Divisors have scalar multiplication with integers -/
instance : SMul ‚Ñ§ (Divisor U) where
  smul n D := {
    toFun := fun z ‚Ü¶ n * D z
    supportWithinDomain := by
      intro x hx
      simp at hx
      exact D.supportWithinDomain hx.2
    supportDiscreteWithinDomain := by
      filter_upwards [D.supportDiscreteWithinDomain]
      intro _ hx
      simp [hx]
  }

/-- Helper lemma for the `simp` tactic: the function of a scalar product
(integer)¬∑divisor is the scalar product of the integer with the associated
function of the divisor. -/
@[simp]
lemma zsmul_fun {D : Divisor U} {n : ‚Ñ§} : (n ‚Ä¢ D).toFun = n ‚Ä¢ (D.toFun) := rfl

/-- Divisors have a partial ordering by pointwise comparison of the associated
functions. -/
instance : LE (Divisor U) where
  le := fun D‚ÇÅ D‚ÇÇ ‚Ü¶ D‚ÇÅ.toFun ‚â§ D‚ÇÇ.toFun

/-- Helper lemma for the `simp` tactic: a divisor is smaller than another one
if the same relation holds with the associated functions. -/
@[simp]
lemma le_fun {D‚ÇÅ D‚ÇÇ : Divisor U} : D‚ÇÅ ‚â§ D‚ÇÇ ‚Üî D‚ÇÅ.toFun ‚â§ D‚ÇÇ.toFun := ‚ü®(¬∑),(¬∑)‚ü©

/-- Divisors form an ordered commutative group -/
instance : OrderedAddCommGroup (Divisor U) where
  add := (¬∑ + ¬∑ )
  add_assoc := fun _ _ _ ‚Ü¶ by ext; simp [add_assoc]
  zero := 0
  zero_add := fun _ ‚Ü¶ by ext; simp
  add_zero := fun _ ‚Ü¶ by ext; simp
  nsmul := (¬∑ ‚Ä¢ ¬∑)
  neg := (- ¬∑)
  zsmul := (¬∑ ‚Ä¢ ¬∑)
  neg_add_cancel := fun _ ‚Ü¶ by ext; simp
  add_comm := fun _ _ ‚Ü¶ by ext; simp [add_comm]
  nsmul_zero := fun _ ‚Ü¶ by ext; simp
  nsmul_succ := fun _ _ ‚Ü¶ by ext; simp [add_one_mul]
  zsmul_zero' := fun _ ‚Ü¶ by ext; simp
  zsmul_succ' := fun _ _ ‚Ü¶ by ext; simp [add_one_mul]
  zsmul_neg' := fun _ _ ‚Ü¶ by ext; simp; apply negSucc_zsmul
  le := (¬∑ ‚â§ ¬∑)
  le_refl := by tauto
  le_trans := fun D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ ‚Ü¶ by simp [le_fun,
    Preorder.le_trans (D‚ÇÅ.toFun) (D‚ÇÇ.toFun) (D‚ÇÉ.toFun) h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ]
  le_antisymm := fun _ _ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ ‚Ü¶ by ext x; exact Int.le_antisymm (h‚ÇÅ‚ÇÇ x) (h‚ÇÇ‚ÇÅ x)
  add_le_add_left := fun _ _ _ _ ‚Ü¶ by simpa

/-- Divisors have a partial ordering by pointwise comparison of the associated
functions. -/
instance : LT (Divisor U) where
  lt := fun D‚ÇÅ D‚ÇÇ ‚Ü¶ D‚ÇÅ.toFun < D‚ÇÇ.toFun

/-- Helper lemma for the `simp` tactic: a divisor is smaller than another one
if the same relation holds with the associated functions. -/
@[simp]
lemma lt_fun {D‚ÇÅ D‚ÇÇ : Divisor U} : D‚ÇÅ < D‚ÇÇ ‚Üî D‚ÇÅ.toFun < D‚ÇÇ.toFun := ‚ü®(¬∑),(¬∑)‚ü©

/-- Divisors have a max. -/
instance : Max (Divisor U) where
  max D‚ÇÅ D‚ÇÇ := {
    toFun := fun z ‚Ü¶ max (D‚ÇÅ z) (D‚ÇÇ z)
    supportWithinDomain := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÅ.supportWithinDomain a),
        Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÇ.supportWithinDomain a)]
    supportDiscreteWithinDomain := by
      filter_upwards [D‚ÇÅ.supportDiscreteWithinDomain, D‚ÇÇ.supportDiscreteWithinDomain]
      intro _ h‚ÇÅ h‚ÇÇ
      simp [h‚ÇÅ, h‚ÇÇ]
  }

/-- Helper lemma for the `simp` tactic: the function associated with the max of
two divisors is the pointwise max of the associated functions. -/
@[simp]
lemma max_fun {D‚ÇÅ D‚ÇÇ : Divisor U} {x : ùïú} : max D‚ÇÅ D‚ÇÇ x = max (D‚ÇÅ x) (D‚ÇÇ x) := rfl

/-- Divisors have a min. -/
instance : Min (Divisor U) where
  min D‚ÇÅ D‚ÇÇ := {
    toFun := fun z ‚Ü¶ min (D‚ÇÅ z) (D‚ÇÇ z)
    supportWithinDomain := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÅ.supportWithinDomain a),
        Function.nmem_support.1 fun a ‚Ü¶ hx (D‚ÇÇ.supportWithinDomain a)]
    supportDiscreteWithinDomain := by
      filter_upwards [D‚ÇÅ.supportDiscreteWithinDomain, D‚ÇÇ.supportDiscreteWithinDomain]
      intro _ h‚ÇÅ h‚ÇÇ
      simp [h‚ÇÅ, h‚ÇÇ]
  }

/-- Helper lemma for the `simp` tactic: the function associated with the max of
two divisors is the pointwise max of the associated functions. -/
@[simp]
lemma min_fun {D‚ÇÅ D‚ÇÇ : Divisor U} {x : ùïú} : min D‚ÇÅ D‚ÇÇ x = min (D‚ÇÅ x) (D‚ÇÇ x) := rfl

/-- Divisors form a lattice. -/
instance : Lattice (Divisor U) where
  le := (¬∑ ‚â§ ¬∑)
  le_refl := by simp
  le_trans := by exact fun D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÉ x ‚Ü¶ (h‚ÇÅ‚ÇÇ x).trans (h‚ÇÇ‚ÇÉ x)
  le_antisymm := by
    intro D‚ÇÅ D‚ÇÇ h‚ÇÅ‚ÇÇ h‚ÇÇ‚ÇÅ
    ext x
    exact Int.le_antisymm (h‚ÇÅ‚ÇÇ x) (h‚ÇÇ‚ÇÅ x)
  sup := (max ¬∑ ¬∑)
  le_sup_left := fun D‚ÇÅ D‚ÇÇ x ‚Ü¶ by simp
  le_sup_right := fun D‚ÇÅ D‚ÇÇ x ‚Ü¶ by simp
  sup_le := fun D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÉ x ‚Ü¶ by simp [h‚ÇÅ‚ÇÉ x, h‚ÇÇ‚ÇÉ x]
  inf := (min ¬∑ ¬∑)
  inf_le_left := fun D‚ÇÅ D‚ÇÇ x ‚Ü¶ by simp
  inf_le_right := fun D‚ÇÅ D‚ÇÇ x ‚Ü¶ by simp
  le_inf := fun D‚ÇÅ D‚ÇÇ D‚ÇÉ h‚ÇÅ‚ÇÉ h‚ÇÇ‚ÇÉ x ‚Ü¶ by simp [h‚ÇÅ‚ÇÉ x, h‚ÇÇ‚ÇÉ x]

/-!
## Restriction
-/

/-- If `V` is a subset of `U`, then a divisor on `U` restricts to a divisor in `V` by
setting its values to zero outside of `V`. -/
noncomputable def restrict {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    Divisor V where
  toFun := by
    classical
    exact fun z ‚Ü¶ if hz : z ‚àà V then D z else 0
  supportWithinDomain := by
    intro x hx
    simp_rw [dite_eq_ite, Function.mem_support, ne_eq, ite_eq_right_iff,
      Classical.not_imp] at hx
    exact hx.1
  supportDiscreteWithinDomain := by
    apply Filter.codiscreteWithin.mono h
    filter_upwards [D.supportDiscreteWithinDomain]
    intro x hx
    simp [hx]

/-- Helper lemma for the `simp` tactic: restricting a divisor from `U` to a
subset `V` does not change its values on `V`. -/
@[simp]
lemma restrict_fun_on_V {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    Set.EqOn (D.restrict h) D V := by
  intro _ _
  simp_all [restrict, dite_eq_ite, ite_eq_left_iff]

/-- Helper lemma for the `simp` tactic: restricting a divisor from `U` to a
subset `V` makes it zero outside of `V`. -/
@[simp]
lemma restrict_fun_on_V_compl {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    Set.EqOn (D.restrict h) 0 V·∂ú := by
  intro _ hx
  simp_all [restrict, dite_eq_ite, ite_eq_left_iff, hx]

/-- Restriction as an order-preserving morphism -/
noncomputable def restrict_orderHom {V : Set ùïú} (h : V ‚äÜ U) : Divisor U ‚Üío Divisor V where
  toFun := fun D ‚Ü¶ D.restrict h
  monotone' := by
    intro D‚ÇÅ D‚ÇÇ h‚ÇÅ‚ÇÇ
    simp only [le_fun, Divisor.restrict]
    intro x
    by_cases hx : x ‚àà V
    <;> simp [hx, reduceDIte, h‚ÇÅ‚ÇÇ x]

/-- Helper lemma for the `simp` tactic: `restrict_orderHom` restricts divisors. -/
@[simp]
lemma restrict_orderHom_fun {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    restrict_orderHom h D = D.restrict h := by rfl

/-- Restriction as a group morphism -/
noncomputable def restrict_groupHom {V : Set ùïú} (h : V ‚äÜ U) : Divisor U ‚Üí+ Divisor V where
  toFun := fun D ‚Ü¶ D.restrict h
  map_zero' := by
    ext x
    simp [restrict]
  map_add' := by
    intro D‚ÇÅ D‚ÇÇ
    ext x
    by_cases hx : x ‚àà V
    <;> simp [restrict, hx]

/-- Helper lemma for the `simp` tactic: `restrict_groupHom` restricts divisors. -/
@[simp]
lemma restrict_groupHom_fun {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    restrict_groupHom h D = D.restrict h := by rfl

/-- Restriction as a lattice morphism -/
noncomputable def restrict_latticeHom {V : Set ùïú} (h : V ‚äÜ U) :
    LatticeHom (Divisor U) (Divisor V) where
  toFun := fun D ‚Ü¶ D.restrict h
  map_sup' := by
    intro D‚ÇÅ D‚ÇÇ
    ext x
    by_cases hx : x ‚àà V
    <;> simp [Divisor.restrict, hx]
  map_inf' := by
    intro D‚ÇÅ D‚ÇÇ
    ext x
    by_cases hx : x ‚àà V
    <;> simp [Divisor.restrict, hx]

/-- Helper lemma for the `simp` tactic: `restrict_latticeHom` restricts divisors. -/
@[simp]
lemma restrict_latticeHom_fun {V : Set ùïú} (D : Divisor U) (h : V ‚äÜ U) :
    restrict_latticeHom h D = D.restrict h := by rfl

/-!
## Derived invariants
-/

/-- The degree of a divisor is the sum of its values, or 0 if the support is
infinite. -/
noncomputable def deg (D : Divisor U) : ‚Ñ§ := ‚àë·∂† z, D z

/-- The counting function for a divisor defined on ‚ä§ -/
noncomputable def counting (D : Divisor (‚ä§ : Set ùïú)) :
    ‚Ñù ‚Üí ‚Ñù :=
  fun r ‚Ü¶ ‚àë·∂† z, D.restrict (by tauto : closedBall (0 : ùïú) |r| ‚äÜ ‚ä§) z

/-- The logarithmic counting function for a divisor defined on ‚ä§ -/
noncomputable def logCounting (D : Divisor (‚ä§ : Set ùïú)) :
    ‚Ñù ‚Üí ‚Ñù :=
  fun r ‚Ü¶ ‚àë·∂† z, D.restrict (by tauto : closedBall (0 : ùïú) |r| ‚äÜ ‚ä§) z * (log r - log ‚Äñz‚Äñ)

end Divisor
