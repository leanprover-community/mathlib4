/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.Normed.Ring.Lemmas

/-!
# Divisors on subsets of normed fields

This file defines divisors, a standard book-keeping tool in complex analysis
used to keep track of pole/vanishing orders of meromorphic objects, typically
functions or differential forms. It provides supporting API and establishes
divisors as an instance of a lattice ordered commutative group.

Throughout the present file, `ğ•œ` denotes a nontrivially normed field, and `U` a
subset of `ğ•œ`.

## TODOs

- Constructions: The divisor of a meromorphic function, behavior under product
  of meromorphic functions, behavior under addition, behavior under restriction
- Construction: The divisor of a rational polynomial
-/

open Filter Metric Real Set Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {U : Set ğ•œ}

/-!
## Definition, coercion to functions and basic extensionality lemmas

A divisor on `U` is a function `ğ•œ â†’ â„¤` whose support is discrete within `U` and
entirely contained within `U`.  The theorem
`supportDiscreteWithin_iff_locallyFiniteWithin` shows that this is equivalent to
the textbook definition, which requires the support of `f` to be locally finite
within `U`.
-/

/-- A divisor on `U` is a triple specified below. -/
structure DivisorOn (U : Set ğ•œ) where
  /-- A function `ğ•œ â†’ â„¤` -/
  toFun : ğ•œ â†’ â„¤
  /-- A proof that the support of `toFun` is contained in `U` -/
  supportWithinDomain' : toFun.support âŠ† U
  /-- A proof the the support is discrete within `U` -/
  supportDiscreteWithinDomain' : toFun =á¶ [codiscreteWithin U] 0

/-- A divisor is a divisor on `âŠ¤ : Set ğ•œ`. -/
def Divisor (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] := DivisorOn (âŠ¤ : Set ğ•œ)

/-- The condition `supportDiscreteWithinU` in a divisor is equivalent to saying
that the support is locally finite near every point of `U`. -/
theorem supportDiscreteWithin_iff_locallyFiniteWithin {f : ğ•œ â†’ â„¤} (h : f.support âŠ† U) :
    f =á¶ [codiscreteWithin U] 0 â†” âˆ€ z âˆˆ U, âˆƒ t âˆˆ ğ“ z, Set.Finite (t âˆ© f.support) := by
  have : f.support = (U \ {x | f x = (0 : ğ•œ â†’ â„¤) x}) := by
    ext x
    simp only [Function.mem_support, ne_eq, Pi.zero_apply, mem_diff, mem_setOf_eq, iff_and_self]
    exact (h Â·)
  rw [EventuallyEq, Filter.Eventually, codiscreteWithin_iff_locallyFiniteComplementWithin, this]

namespace DivisorOn

/-- Divisors are `FunLike`: the coercion from divisors to functions is injective. -/
instance : FunLike (DivisorOn U) ğ•œ â„¤ where
  coe D := D.toFun
  coe_injective' := fun âŸ¨_, _, _âŸ© âŸ¨_, _, _âŸ© â†¦ by simp

/-- This allows writing `D.support` instead of `Function.support D` -/
abbrev support (D : DivisorOn U)  := Function.support D

lemma supportWithinDomain (D : DivisorOn U) : D.support âŠ† U := D.supportWithinDomain'

lemma supportDiscreteWithinDomain (D : DivisorOn U) : D =á¶ [codiscreteWithin U] 0 :=
  D.supportDiscreteWithinDomain'

@[ext]
lemma ext {Dâ‚ Dâ‚‚ : DivisorOn U} (h : âˆ€ a, Dâ‚ a = Dâ‚‚ a) : Dâ‚ = Dâ‚‚ := DFunLike.ext _ _ h

lemma coe_injective : Function.Injective (Â· : DivisorOn U â†’ ğ•œ â†’ â„¤) := DFunLike.coe_injective

/-!
## Elementary properties of the support
-/

/-- Simplifier lemma: A divisor on `U` evaluates to zero outside of `U`. -/
@[simp]
lemma apply_eq_zero_of_not_mem {z : ğ•œ} (D : DivisorOn U) (hz : z âˆ‰ U) :
    D z = 0 := Function.nmem_support.mp fun a â†¦ hz (D.supportWithinDomain a)

/-- The support of a divisor is discrete. -/
theorem discreteSupport (D : DivisorOn U) : DiscreteTopology D.support := by
  have : Function.support D = {x | D x = 0}á¶œ âˆ© U := by
    ext x
    constructor
    Â· exact fun hx â†¦ âŸ¨by tauto, D.supportWithinDomain hxâŸ©
    Â· intro hx
      rw [mem_inter_iff, mem_compl_iff, mem_setOf_eq] at hx
      tauto
  convert discreteTopology_of_codiscreteWithin (D.supportDiscreteWithinDomain)

/-- If `U` is closed, the the support of a divisor on `U` is also closed. -/
theorem closedSupport (D : DivisorOn U) (hU : IsClosed U) :
    IsClosed D.support := by
  convert isClosed_sdiff_of_codiscreteWithin D.supportDiscreteWithinDomain hU
  ext x
  constructor
  Â· intro hx
    simp_all [D.supportWithinDomain hx]
  Â· intro hx
    simp_all

/-- If `U` is closed, the the support of a divisor on `U` is finite. -/
theorem finiteSupport (D : DivisorOn U) (hU : IsCompact U) :
    Set.Finite D.support :=
  (hU.of_isClosed_subset (D.closedSupport hU.isClosed)
    D.supportWithinDomain).finite D.discreteSupport

/-!
## Lattice ordered group structure

This section equips divisors on `U` with the standard structure of a lattice
ordered group, where addition, comparison, min and max of divisors are defined
pointwise.
-/

variable (U) in
/-- Divisors form an additive subgroup of functions ğ•œ â†’ â„¤ -/
protected def addSubgroup : AddSubgroup (ğ•œ â†’ â„¤) where
  carrier := {f | f.support âŠ† U âˆ§ f =á¶ [codiscreteWithin U] 0}
  zero_mem' := by simp
  add_mem' {f g} hf hg := by
    refine âŸ¨?_, hf.2.add hg.2âŸ©
    intro x hx
    contrapose! hx
    simp [Function.nmem_support.1 fun a â†¦ hx (hf.1 a),
      Function.nmem_support.1 fun a â†¦ hx (hg.1 a)]
  neg_mem' {f} hf := âŸ¨fun x hx â†¦ hf.1 <| by simpa using hx, hf.2.negâŸ©

protected lemma memAddSubgroup (D : DivisorOn U) :
    (D : ğ•œ â†’ â„¤) âˆˆ DivisorOn.addSubgroup U :=
  âŸ¨D.supportWithinDomain, D.supportDiscreteWithinDomainâŸ©

/-- Assign a divisor to a function in the subgroup -/
@[simps]
def mk_of_mem (f : ğ•œ â†’ â„¤) (hf : f âˆˆ DivisorOn.addSubgroup U) : DivisorOn U :=
  âŸ¨f, hf.1, hf.2âŸ©

instance : Zero (DivisorOn U) where
  zero := mk_of_mem 0 <| zero_mem _

instance : Add (DivisorOn U) where
  add Dâ‚ Dâ‚‚ := mk_of_mem (Dâ‚ + Dâ‚‚) <| add_mem Dâ‚.memAddSubgroup Dâ‚‚.memAddSubgroup

instance : Neg (DivisorOn U) where
  neg D := mk_of_mem (-D) <| neg_mem D.memAddSubgroup

instance : Sub (DivisorOn U) where
  sub Dâ‚ Dâ‚‚ := mk_of_mem (Dâ‚ - Dâ‚‚) <| sub_mem Dâ‚.memAddSubgroup Dâ‚‚.memAddSubgroup

instance : SMul â„• (DivisorOn U) where
  smul n D := mk_of_mem (n â€¢ D) <| nsmul_mem D.memAddSubgroup n

instance : SMul â„¤ (DivisorOn U) where
  smul n D := mk_of_mem (n â€¢ D) <| zsmul_mem D.memAddSubgroup n

@[simp] lemma coe_zero : ((0 : DivisorOn U) : ğ•œ â†’ â„¤) = 0 := rfl
@[simp] lemma coe_add (Dâ‚ Dâ‚‚ : DivisorOn U) : (â†‘(Dâ‚ + Dâ‚‚) : ğ•œ â†’ â„¤) = Dâ‚ + Dâ‚‚ := rfl
@[simp] lemma coe_neg (D : DivisorOn U) : (â†‘(-D) : ğ•œ â†’ â„¤) = -(D : ğ•œ â†’ â„¤) := rfl
@[simp] lemma coe_sub (Dâ‚ Dâ‚‚ : DivisorOn U) : (â†‘(Dâ‚ - Dâ‚‚) : ğ•œ â†’ â„¤) = Dâ‚ - Dâ‚‚ := rfl
@[simp] lemma coe_nsmul (D : DivisorOn U) (n : â„•) : (â†‘(n â€¢ D) : ğ•œ â†’ â„¤) = n â€¢ (D : ğ•œ â†’ â„¤) := rfl
@[simp] lemma coe_zsmul (D : DivisorOn U) (n : â„¤) : (â†‘(n â€¢ D) : ğ•œ â†’ â„¤) = n â€¢ (D : ğ•œ â†’ â„¤) := rfl

instance : AddCommGroup (DivisorOn U) :=
  Function.Injective.addCommGroup (Mâ‚ := DivisorOn U) (Mâ‚‚ := ğ•œ â†’ â„¤)
    _ coe_injective coe_zero coe_add coe_neg coe_sub coe_nsmul coe_zsmul

instance : LE (DivisorOn U) where
  le := fun Dâ‚ Dâ‚‚ â†¦ (Dâ‚ : ğ•œ â†’ â„¤) â‰¤ Dâ‚‚

lemma le_def {Dâ‚ Dâ‚‚ : DivisorOn U} : Dâ‚ â‰¤ Dâ‚‚ â†” (Dâ‚ : ğ•œ â†’ â„¤) â‰¤ (Dâ‚‚ : ğ•œ â†’ â„¤) := âŸ¨(Â·),(Â·)âŸ©

instance : LT (DivisorOn U) where
  lt := fun Dâ‚ Dâ‚‚ â†¦ (Dâ‚ : ğ•œ â†’ â„¤) < Dâ‚‚

lemma lt_def {Dâ‚ Dâ‚‚ : DivisorOn U} : Dâ‚ < Dâ‚‚ â†” (Dâ‚ : ğ•œ â†’ â„¤) < (Dâ‚‚ : ğ•œ â†’ â„¤) := âŸ¨(Â·),(Â·)âŸ©

instance : Max (DivisorOn U) where
  max Dâ‚ Dâ‚‚ :=
  { toFun z := max (Dâ‚ z) (Dâ‚‚ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a â†¦ hx (Dâ‚.supportWithinDomain a),
        Function.nmem_support.1 fun a â†¦ hx (Dâ‚‚.supportWithinDomain a)]
    supportDiscreteWithinDomain' := by
      filter_upwards [Dâ‚.supportDiscreteWithinDomain, Dâ‚‚.supportDiscreteWithinDomain]
      intro _ hâ‚ hâ‚‚
      simp [hâ‚, hâ‚‚] }

@[simp]
lemma max_apply {Dâ‚ Dâ‚‚ : DivisorOn U} {x : ğ•œ} : max Dâ‚ Dâ‚‚ x = max (Dâ‚ x) (Dâ‚‚ x) := rfl

instance : Min (DivisorOn U) where
  min Dâ‚ Dâ‚‚ :=
  { toFun z := min (Dâ‚ z) (Dâ‚‚ z)
    supportWithinDomain' := by
      intro x
      contrapose
      intro hx
      simp [Function.nmem_support.1 fun a â†¦ hx (Dâ‚.supportWithinDomain a),
        Function.nmem_support.1 fun a â†¦ hx (Dâ‚‚.supportWithinDomain a)]
    supportDiscreteWithinDomain' := by
      filter_upwards [Dâ‚.supportDiscreteWithinDomain, Dâ‚‚.supportDiscreteWithinDomain]
      intro _ hâ‚ hâ‚‚
      simp [hâ‚, hâ‚‚] }

@[simp]
lemma min_def {Dâ‚ Dâ‚‚ : DivisorOn U} {x : ğ•œ} : min Dâ‚ Dâ‚‚ x = min (Dâ‚ x) (Dâ‚‚ x) := rfl

instance : Lattice (DivisorOn U) where
  le := (Â· â‰¤ Â·)
  lt := (Â· < Â·)
  le_refl := by simp [le_def]
  le_trans Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚‚ hâ‚‚â‚ƒ := fun x â†¦ (hâ‚â‚‚ x).trans (hâ‚‚â‚ƒ x)
  le_antisymm Dâ‚ Dâ‚‚ hâ‚â‚‚ hâ‚‚â‚ := by
    ext x
    exact Int.le_antisymm (hâ‚â‚‚ x) (hâ‚‚â‚ x)
  sup := max
  le_sup_left Dâ‚ Dâ‚‚ := fun x â†¦ by simp
  le_sup_right Dâ‚ Dâ‚‚ := fun x â†¦ by simp
  sup_le Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚ƒ hâ‚‚â‚ƒ := fun x â†¦ by simp [hâ‚â‚ƒ x, hâ‚‚â‚ƒ x]
  inf := min
  inf_le_left Dâ‚ Dâ‚‚ := fun x â†¦ by simp
  inf_le_right Dâ‚ Dâ‚‚ := fun x â†¦ by simp
  le_inf Dâ‚ Dâ‚‚ Dâ‚ƒ hâ‚â‚ƒ hâ‚‚â‚ƒ := fun x â†¦ by simp [hâ‚â‚ƒ x, hâ‚‚â‚ƒ x]

/-- Divisors form an ordered commutative group -/
instance : OrderedAddCommGroup (DivisorOn U) where
  __ := inferInstanceAs (AddCommGroup (DivisorOn U))
  __ := inferInstanceAs (Lattice (DivisorOn U))
  add_le_add_left := fun _ _ _ _ â†¦ by simpa [le_def]

/-!
## Restriction
-/

/-- If `V` is a subset of `U`, then a divisor on `U` restricts to a divisor in `V` by
setting its values to zero outside of `V`. -/
noncomputable def restrict {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    DivisorOn V where
  toFun := by
    classical
    exact fun z â†¦ if hz : z âˆˆ V then D z else 0
  supportWithinDomain' := by
    intro x hx
    simp_rw [dite_eq_ite, Function.mem_support, ne_eq, ite_eq_right_iff,
      Classical.not_imp] at hx
    exact hx.1
  supportDiscreteWithinDomain' := by
    apply Filter.codiscreteWithin.mono h
    filter_upwards [D.supportDiscreteWithinDomain]
    intro x hx
    simp [hx]

open Classical in
lemma restrict_apply {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) (z : ğ•œ) :
    (D.restrict h) z = if z âˆˆ V then D z else 0 := rfl

lemma restrict_eqOn {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    Set.EqOn (D.restrict h) D V := by
  intro _ _
  simp_all [restrict_apply, dite_eq_ite, ite_eq_left_iff]

lemma restrict_eqOn_compl {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    Set.EqOn (D.restrict h) 0 Vá¶œ := by
  intro _ hx
  simp_all [restrict_apply, dite_eq_ite, ite_eq_left_iff, hx]

/-- Restriction as a group morphism -/
noncomputable def restrictMonoidHom {V : Set ğ•œ} (h : V âŠ† U) : DivisorOn U â†’+ DivisorOn V where
  toFun D := D.restrict h
  map_zero' := by
    ext x
    simp [restrict_apply]
  map_add' Dâ‚ Dâ‚‚ := by
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [restrict_apply, hx]

@[simp]
lemma restrictMonoidHom_apply {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    restrictMonoidHom h D = D.restrict h := by rfl

/-- Restriction as a lattice morphism -/
noncomputable def restrictLatticeHom {V : Set ğ•œ} (h : V âŠ† U) :
    LatticeHom (DivisorOn U) (DivisorOn V) where
  toFun D := D.restrict h
  map_sup' Dâ‚ Dâ‚‚ := by
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [DivisorOn.restrict_apply, hx]
  map_inf' Dâ‚ Dâ‚‚ := by
    ext x
    by_cases hx : x âˆˆ V
    <;> simp [DivisorOn.restrict_apply, hx]

@[simp]
lemma restrictLatticeHom_apply {V : Set ğ•œ} (D : DivisorOn U) (h : V âŠ† U) :
    restrictLatticeHom h D = D.restrict h := by rfl

end DivisorOn
