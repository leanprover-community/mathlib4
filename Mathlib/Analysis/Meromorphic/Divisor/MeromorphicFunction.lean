/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/

import Mathlib.Analysis.Meromorphic.Order
import Mathlib.Analysis.Meromorphic.Divisor.Basic

/-!
# The Divisor of a Meromorphic Function

This file defines the divisor of a meromorphic function and proves the most
basic lemmas about those divisors.

## TODO

- Remove the assumption `CompleteSpace E`.
- Compatibility with restriction of divisors/functions
- Non-negativity of the divisor for an analytic function
- Behavior under addition of functions
- Congruence lemmas for `codiscreteWithin`
-/

open Classical

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {U : Set ğ•œ}
  {z : ğ•œ}
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [CompleteSpace E]

namespace MeromorphicOn

/-!
## Definition of the Divisor
-/

/-- The divisor of a meromorphic function `f`, mapping a point `z` to the order
  of `f` at `z`, and to zero if the order is infinite. -/
noncomputable def divisorOn {f : ğ•œ â†’ E} (hf : MeromorphicOn f U) :
    DivisorOn U where
  toFun := fun z â†¦ if hz : z âˆˆ U then ((hf z hz).order.untopD 0) else 0
  supportWithinDomain' := by
    intro z hz
    simp at hz
    by_contra hâ‚‚z
    simp [hâ‚‚z] at hz
  supportDiscreteWithinDomain' := by
    filter_upwards [mem_codiscrete_subtype_iff_mem_codiscreteWithin.1
      hf.codiscrete_setOf_order_eq_zero_or_top]
    intro _ _
    simp_all only [Set.mem_image, Set.mem_setOf_eq, Subtype.exists, exists_and_right,
      exists_eq_right, Pi.zero_apply, dite_eq_right_iff, WithTop.untopD_eq_self_iff,
      WithTop.coe_zero]
    tauto

/-- Definition of the divisor. -/
theorem divisorOn_def {f : ğ•œ â†’ E} (hf : MeromorphicOn f U) :
    hf.divisorOn z = if hz : z âˆˆ U then (hf z hz).order.untopD 0 else 0 := rfl

/-- Simplifier lemma: On `U`, the divisor of a function `f` that is meromorphic on `U` evaluates to
  `order.untopD`. -/
@[simp]
lemma divisorOn_apply {f : ğ•œ â†’ E} (hf : MeromorphicOn f U) (hz : z âˆˆ U) :
    hf.divisorOn z = (hf z hz).order.untopD 0 := by simp_all [hf.divisorOn_def, hz]

/-!
## Behavior under Standard Operations
-/

/-- If orders are finite, the divisor of the scalar product of two meromorphic
  functions is the sum of the divisors.

  See `MeromorphicOn.exists_order_ne_top_iff_forall` and
  `MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to
  guarantee conditions `hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisorOn_smul [CompleteSpace ğ•œ] {fâ‚ : ğ•œ â†’ ğ•œ} {fâ‚‚ : ğ•œ â†’ E} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚ z hz).order â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚‚ z hz).order â‰  âŠ¤) :
    (hâ‚fâ‚.smul hâ‚fâ‚‚).divisorOn = hâ‚fâ‚.divisorOn + hâ‚fâ‚‚.divisorOn := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp_all [(hâ‚fâ‚ z hz).order_smul (hâ‚fâ‚‚ z hz)]
    lift (hâ‚fâ‚ z hz).order to â„¤ using (hâ‚‚fâ‚ z hz) with aâ‚ haâ‚
    lift (hâ‚fâ‚‚ z hz).order to â„¤ using (hâ‚‚fâ‚‚ z hz) with aâ‚‚ haâ‚‚
    exact rfl
  Â· simp [hz]

/-- If orders are finite, the divisor of the product of two meromorphic
  functions is the sum of the divisors.

  See `MeromorphicOn.exists_order_ne_top_iff_forall` and
  `MeromorphicOn.order_ne_top_of_isPreconnected` for two convenient criteria to
  guarantee conditions `hâ‚‚fâ‚` and `hâ‚‚fâ‚‚`.
-/
theorem divisorOn_mul [CompleteSpace ğ•œ] {fâ‚ fâ‚‚ : ğ•œ â†’ ğ•œ} (hâ‚fâ‚ : MeromorphicOn fâ‚ U)
    (hâ‚fâ‚‚ : MeromorphicOn fâ‚‚ U) (hâ‚‚fâ‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚ z hz).order â‰  âŠ¤)
    (hâ‚‚fâ‚‚ : âˆ€ z, (hz : z âˆˆ U) â†’ (hâ‚fâ‚‚ z hz).order â‰  âŠ¤) :
    (hâ‚fâ‚.mul hâ‚fâ‚‚).divisorOn = hâ‚fâ‚.divisorOn + hâ‚fâ‚‚.divisorOn := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp_all [(hâ‚fâ‚ z hz).order_mul (hâ‚fâ‚‚ z hz)]
    lift (hâ‚fâ‚ z hz).order to â„¤ using (hâ‚‚fâ‚ z hz) with aâ‚ haâ‚
    lift (hâ‚fâ‚‚ z hz).order to â„¤ using (hâ‚‚fâ‚‚ z hz) with aâ‚‚ haâ‚‚
    exact rfl
  Â· simp [hz]

/-- The divisor of the inverse is the negative of the divisor. -/
theorem divisorOn_inv [CompleteSpace ğ•œ] {f: ğ•œ â†’ ğ•œ} (hâ‚f : MeromorphicOn f U) :
    hâ‚f.inv.divisorOn = -hâ‚f.divisorOn := by
  ext z
  by_cases hz : z âˆˆ U
  Â· simp only [hz, divisorOn_eval_outside_domain, DivisorOn.coe_neg, Pi.neg_apply]
    rw [(hâ‚f z hz).order_inv]
    by_cases ha : (hâ‚f z hz).inv.order = âŠ¤
    Â· simp only [ha, WithTop.untopD_top, LinearOrderedAddCommGroupWithTop.neg_top, neg_zero]
    lift (hâ‚f z hz).inv.order to â„¤ using ha with a ha
    rw [(by rfl : -a = (â†‘(-a) : WithTop â„¤)), WithTop.untopD_coe, WithTop.untopD_coe]
    simp
  Â· simp [hz]

end MeromorphicOn
