/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Order

/-!
# Normal form of meromorphic functions and continuous extension

If a function `f` is meromorphic on `U` and if `g` differs from `f` only along a
set that is codiscrete within `U`, then `g` is likewise meromorphic. The set of
meromorphic functions is therefore huge, and `=á¶ [codiscreteWithin U]` defines an
equivalence relation.

This file implements continuous extension to provide an API that allows picking
the 'unique best' representative of any given equivalence class, where 'best'
means that the representative can locally near any point `x` be written 'in
normal form', as `f =á¶ [ğ“ x] fun z â†¦ (z - x) ^ n â€¢ g` where `g` is analytic and
does not vanish at `x`.

## TODO

Establish the analogous notion `MeromorphicNFOn`.
-/

open Topology

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f : ğ•œ â†’ E} {g : ğ•œ â†’ ğ•œ}
  {x : ğ•œ}

/-!
# Normal form of meromorphic functions at a given point

## Definition and characterizations
-/

variable (f x) in
/-- A function is 'meromorphic in normal form' at `x` if it vanishes around `x`
or if it can locally be written as `fun z â†¦ (z - x) ^ n â€¢ g` where `g` is
analytic and does not vanish at `x`. -/
def MeromorphicNFAt :=
  f =á¶ [ğ“ x] 0 âˆ¨
    âˆƒ (n : â„¤) (g : ğ•œ â†’ E), AnalyticAt ğ•œ g x âˆ§ g x â‰  0 âˆ§ f =á¶ [ğ“ x] (Â· - x) ^ n â€¢ g

/-- A meromorphic function has normal form at `x` iff it is either analytic
there, or if it has a pole at `x` and takes the default value `0`. -/
theorem meromorphicNFAt_iff_analyticAt_or :
    MeromorphicNFAt f x â†” AnalyticAt ğ•œ f x âˆ¨ âˆƒ hf : MeromorphicAt f x, hf.order < 0 âˆ§ f x = 0 := by
  constructor
  Â· rintro (h | âŸ¨n, g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ©)
    Â· simp [(analyticAt_congr h).2 analyticAt_const]
    Â· have hf : MeromorphicAt f x := by
        apply MeromorphicAt.congr _ (hâ‚ƒg.filter_mono nhdsWithin_le_nhds).symm
        fun_prop
      have : hf.order = n := by
        rw [hf.order_eq_int_iff]
        use g, hâ‚g, hâ‚‚g
        exact eventually_nhdsWithin_of_eventually_nhds hâ‚ƒg
      by_cases hn : 0 â‰¤ n
      Â· left
        rw [analyticAt_congr hâ‚ƒg]
        apply (AnalyticAt.zpow_nonneg (by fun_prop) hn).smul hâ‚g
      Â· right
        use hf
        simp [this, WithTop.coe_lt_zero.2 (not_le.1 hn), hâ‚ƒg.eq_of_nhds,
          zero_zpow n (ne_of_not_le hn).symm]
  Â· rintro (h | âŸ¨hâ‚, hâ‚‚, hâ‚ƒâŸ©)
    Â· by_cases hâ‚‚f : h.order = âŠ¤
      Â· rw [AnalyticAt.order_eq_top_iff] at hâ‚‚f
        tauto
      Â· right
        use h.order.toNat
        have : h.order â‰  âŠ¤ := hâ‚‚f
        rw [â† ENat.coe_toNat_eq_self, eq_comm, AnalyticAt.order_eq_nat_iff] at this
        obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := this
        use g, hâ‚g, hâ‚‚g
        simpa
    Â· right
      lift hâ‚.order to â„¤ using LT.lt.ne_top hâ‚‚ with n hn
      obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (hâ‚.order_eq_int_iff n).1 hn.symm
      use n, g, hâ‚g, hâ‚‚g
      filter_upwards [eventually_nhdsWithin_iff.1 hâ‚ƒg]
      intro z hz
      by_cases hâ‚z : z = x
      Â· simp only [hâ‚z, hâ‚‚, Pi.smul_apply', Pi.pow_apply, sub_self]
        rw [hâ‚ƒ]
        apply (smul_eq_zero_of_left (zero_zpow n _) (g x)).symm
        by_contra hCon
        simp [hCon] at hâ‚‚
      Â· exact hz hâ‚z

/-!
## Relation to other properties of functions
-/

/-- If a function is meromorphic in normal form at `x`, then it is meromorphic at `x`. -/
theorem MeromorphicNFAt.meromorphicAt (hf : MeromorphicNFAt f x) :
    MeromorphicAt f x := by
  rw [meromorphicNFAt_iff_analyticAt_or] at hf
  rcases hf with h | h
  Â· exact h.meromorphicAt
  Â· obtain âŸ¨hf, _âŸ© := h
    exact hf

/-- If a function is meromorphic in normal form at `x`, then it has non-negative order iff it is
analytic. -/
theorem MeromorphicNFAt.order_nonneg_iff_analyticAt (hf : MeromorphicNFAt f x) :
    0 â‰¤ hf.meromorphicAt.order â†” AnalyticAt ğ•œ f x := by
  constructor <;> intro hâ‚‚f
  Â· rw [meromorphicNFAt_iff_analyticAt_or] at hf
    rcases hf with h | âŸ¨_, hâ‚ƒf, _âŸ©
    Â· exact h
    Â· by_contra h'
      exact lt_irrefl 0 (lt_of_le_of_lt hâ‚‚f hâ‚ƒf)
  Â· rw [hâ‚‚f.meromorphicAt_order]
    simp

/-- Analytic functions are meromorphic in normal form. -/
theorem AnalyticAt.meromorphicNFAt (hf : AnalyticAt ğ•œ f x) :
    MeromorphicNFAt f x := by
  simp [meromorphicNFAt_iff_analyticAt_or, hf]

/-- Meromorphic functions have normal form outside of a discrete subset in the domain of
meromorphicity. -/
theorem MeromorphicOn.meromorphicNFAt_mem_codiscreteWithin [CompleteSpace E] {U : Set ğ•œ}
    (hf : MeromorphicOn f U) :
    { x | MeromorphicNFAt f x } âˆˆ Filter.codiscreteWithin U := by
  filter_upwards [hf.analyticAt_mem_codiscreteWithin] with _ ha
  exact ha.meromorphicNFAt

/-!
## Vanishing and order
-/

/-- If `f` is meromorphic in normal form at `x`, then `f` has order zero iff it does not vanish at
`x`.

See `AnalyticAt.order_eq_zero_iff` for an analogous statement about analytic functions. -/
theorem MeromorphicNFAt.order_eq_zero_iff (hf : MeromorphicNFAt f x) :
    hf.meromorphicAt.order = 0 â†” f x â‰  0 := by
  constructor
  Â· intro hâ‚f
    have hâ‚‚f := hf.order_nonneg_iff_analyticAt.1 (le_of_eq hâ‚f.symm)
    apply hâ‚‚f.order_eq_zero_iff.1
    apply (ENat.map_natCast_eq_zero (Î± := â„¤)).1
    rwa [hâ‚‚f.meromorphicAt_order] at hâ‚f
  Â· intro h
    rcases id hf with hâ‚ | âŸ¨n, g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ©
    Â· have := hâ‚.eq_of_nhds
      tauto
    Â· have : n = 0 := by
        by_contra hContra
        have := hâ‚ƒg.eq_of_nhds
        simp only [Pi.smul_apply', Pi.pow_apply, sub_self, zero_zpow n hContra, zero_smul] at this
        tauto
      simp only [this, zpow_zero, smul_eq_mul, one_mul] at hâ‚ƒg
      apply (hf.meromorphicAt.order_eq_int_iff 0).2
      use g, hâ‚g, hâ‚‚g
      simp only [zpow_zero, smul_eq_mul, one_mul]
      exact hâ‚ƒg.filter_mono nhdsWithin_le_nhds

/-!
## Local nature of the definition and local identity theorem
-/

/-- **Local identity theorem**: two meromorphic functions in normal form agree in a
neighborhood iff they agree in a pointed neighborhood.

See `ContinuousAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd` for the analogous
statement for continuous functions.
-/
theorem MeromorphicNFAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd {g : ğ•œ â†’ E}
    (hf : MeromorphicNFAt f x) (hg : MeromorphicNFAt g x) :
    f =á¶ [ğ“[â‰ ] x] g â†” f =á¶ [ğ“ x] g := by
  constructor
  Â· intro h
    have tâ‚€ := hf.meromorphicAt.order_congr h
    by_cases cs : hf.meromorphicAt.order = 0
    Â· rw [cs] at tâ‚€
      have Z := (hf.order_nonneg_iff_analyticAt.1 (le_of_eq cs.symm)).continuousAt
      have W := (hg.order_nonneg_iff_analyticAt.1 (le_of_eq tâ‚€)).continuousAt
      exact (Z.eventuallyEq_nhd_iff_eventuallyEq_nhdNE W).1 h
    Â· apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE h
      let hâ‚f := cs
      rw [hf.order_eq_zero_iff] at hâ‚f
      let hâ‚g := cs
      rw [tâ‚€, hg.order_eq_zero_iff] at hâ‚g
      simp only [not_not] at *
      rw [hâ‚f, hâ‚g]
  Â· exact (Filter.EventuallyEq.filter_mono Â· nhdsWithin_le_nhds)

/-- Meromorphicity in normal form is a local property. -/
theorem meromorphicNFAt_congr {g : ğ•œ â†’ E} (hfg : f =á¶ [ğ“ x] g) :
    MeromorphicNFAt f x â†” MeromorphicNFAt g x := by
  constructor
  Â· rintro (h | âŸ¨n, h, hâ‚h, hâ‚‚h, hâ‚ƒhâŸ©)
    Â· exact .inl (hfg.symm.trans h)
    Â· exact .inr âŸ¨n, h, hâ‚h, hâ‚‚h, hfg.symm.trans hâ‚ƒhâŸ©
  Â· rintro (h | âŸ¨n, h, hâ‚h, hâ‚‚h, hâ‚ƒhâŸ©)
    Â· exact .inl (hfg.trans h)
    Â· exact .inr âŸ¨n, h, hâ‚h, hâ‚‚h, hfg.trans hâ‚ƒhâŸ©

/-!
## Criteria to guarantee normal form
-/

/-- Helper lemma for `meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic`: if
`f` is meromorphic in normal form at `x` and `g` is analytic without zero at
`x`, then `g â€¢ f` is meromorphic in normal form at `x`. -/
lemma MeromorphicNFAt.smul_analytic (hf : MeromorphicNFAt f x)
    (hâ‚g : AnalyticAt ğ•œ g x) (hâ‚‚g : g x â‰  0) :
    MeromorphicNFAt (g â€¢ f) x := by
  rcases hf with hâ‚f | âŸ¨n, g_f, hâ‚g_f, hâ‚‚g_f, hâ‚ƒg_fâŸ©
  Â· left
    filter_upwards [hâ‚f]
    simp_all
  Â· right
    use n, g â€¢ g_f, hâ‚g.smul hâ‚g_f
    constructor
    Â· simp [smul_ne_zero hâ‚‚g hâ‚‚g_f]
    Â· filter_upwards [hâ‚ƒg_f]
      intro y hy
      simp only [Pi.smul_apply', hy, Pi.pow_apply]
      rw [smul_comm]

/-- If `f` is any function and `g` is analytic without zero at `zâ‚€`, then `f` is meromorphic in
normal form at `zâ‚€` iff `g â€¢ f` is meromorphic in normal form at `zâ‚€`. -/
theorem meromorphicNFAt_smul_iff_right_of_analyticAt (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0) :
    MeromorphicNFAt (g â€¢ f) x â†” MeromorphicNFAt f x where
  mp hprod := by
    have : f =á¶ [ğ“ x] gâ»Â¹ â€¢ g â€¢ f := by
      filter_upwards [hâ‚g.continuousAt.preimage_mem_nhds (compl_singleton_mem_nhds_iff.mpr hâ‚‚g)]
      intro y hy
      rw [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage,
        Set.mem_singleton_iff] at hy
      simp [hy]
    rw [meromorphicNFAt_congr this]
    exact hprod.smul_analytic (hâ‚g.inv hâ‚‚g) (inv_ne_zero hâ‚‚g)
  mpr hf := hf.smul_analytic hâ‚g hâ‚‚g

/-- If `f` is any function and `g` is analytic without zero at `zâ‚€`, then `f` is meromorphic in
normal form at `zâ‚€` iff `g * f` is meromorphic in normal form at `zâ‚€`. -/
theorem meromorphicNFAt_mul_iff_right {f : ğ•œ â†’ ğ•œ} (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0) :
    MeromorphicNFAt (g * f) x â†” MeromorphicNFAt f x :=
  meromorphicNFAt_smul_iff_right_of_analyticAt hâ‚g hâ‚‚g

/-- If `f` is any function and `g` is analytic without zero at `zâ‚€`, then `f` is meromorphic in
normal form at `zâ‚€` iff `f * g` is meromorphic in normal form at `zâ‚€`. -/
theorem meromorphicNFAt_mul_iff_left {f : ğ•œ â†’ ğ•œ} (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0) :
    MeromorphicNFAt (f * g) x â†” MeromorphicNFAt f x := by
  rw [mul_comm, â† smul_eq_mul]
  exact meromorphicNFAt_smul_iff_right_of_analyticAt hâ‚g hâ‚‚g

/-!
## Continuous extension and conversion to normal form
-/

variable (f x) in
/-- If `f` is meromorphic at `x`, convert `f` to normal form at `x` by changing its value at `x`.
Otherwise, returns the 0 function. -/
noncomputable def toMeromorphicNFAt :
    ğ•œ â†’ E := by
  by_cases hf : MeromorphicAt f x
  Â· classical -- do not complain about decidability issues in Function.update
    apply Function.update f x
    by_cases hâ‚f : hf.order = (0 : â„¤)
    Â· rw [hf.order_eq_int_iff] at hâ‚f
      exact (Classical.choose hâ‚f) x
    Â· exact 0
  Â· exact 0

/-- Conversion to normal form at `x` changes the value only at x. -/
lemma MeromorphicAt.eqOn_compl_singleton_toMermomorphicNFAt (hf : MeromorphicAt f x) :
    Set.EqOn f (toMeromorphicNFAt f x) {x}á¶œ :=
  fun _ _ â†¦ by simp_all [toMeromorphicNFAt]

/-- If `f` is not meromorphic, conversion to normal form at `x` maps the function to `0`. -/
@[simp] lemma toMeromorphicNFAt_of_not_meromorphicAt (hf : Â¬MeromorphicAt f x) :
    toMeromorphicNFAt f x = 0 := by
  simp [toMeromorphicNFAt, hf]

/-- Conversion to normal form at `x` changes the value only at x. -/
lemma MeromorphicAt.eq_nhdNE_toMeromorphicNFAt (hf : MeromorphicAt f x) :
    f =á¶ [ğ“[â‰ ] x] toMeromorphicNFAt f x :=
  eventually_nhdsWithin_of_forall (fun _ hz â†¦ hf.eqOn_compl_singleton_toMermomorphicNFAt hz)

/-- Two analytic functions agree on a punctured neighborhood iff they agree on a neighborhood. -/
private lemma AnalyticAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd {g : ğ•œ â†’ E} {zâ‚€ : ğ•œ}
    (hf : AnalyticAt ğ•œ f zâ‚€) (hg : AnalyticAt ğ•œ g zâ‚€) (hfg : f =á¶ [ğ“[â‰ ] zâ‚€] g) :
    f =á¶ [ğ“ zâ‚€] g := by
  rcases ((hf.sub hg).eventually_eq_zero_or_eventually_ne_zero) with h | h
  Â· exact Filter.eventuallyEq_iff_sub.2 h
  Â· simpa using (Filter.eventually_and.2 âŸ¨Filter.eventuallyEq_iff_sub.mp hfg, hâŸ©).exists

/-- After conversion to normal form at `x`, the function has normal form. -/
theorem meromorphicNFAt_toMeromorphicNFAt :
    MeromorphicNFAt (toMeromorphicNFAt f x) x := by
  by_cases hf : MeromorphicAt f x
  Â· by_cases hâ‚‚f : hf.order = âŠ¤
    Â· have : toMeromorphicNFAt f x =á¶ [ğ“ x] 0 := by
        apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE
        Â· exact hf.eq_nhdNE_toMeromorphicNFAt.symm.trans (hf.order_eq_top_iff.1 hâ‚‚f)
        Â· simp [hâ‚‚f, toMeromorphicNFAt, hf]
      apply AnalyticAt.meromorphicNFAt
      rw [analyticAt_congr this]
      exact analyticAt_const
    Â· lift hf.order to â„¤ using hâ‚‚f with n hn
      obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (hf.order_eq_int_iff n).1 hn.symm
      right
      use n, g, hâ‚g, hâ‚‚g
      apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE (hf.eq_nhdNE_toMeromorphicNFAt.symm.trans hâ‚ƒg)
      simp only [toMeromorphicNFAt, hf, â†“reduceDIte, â† hn, WithTop.coe_zero,
        WithTop.coe_eq_zero, ne_eq, Function.update_self, sub_self]
      split_ifs with hâ‚ƒf
      Â· obtain âŸ¨hâ‚G, _, hâ‚ƒGâŸ© := Classical.choose_spec ((hf.order_eq_int_iff 0).1 (hâ‚ƒf â–¸ hn.symm))
        apply Filter.EventuallyEq.eq_of_nhds
        apply (hâ‚G.continuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE (by fun_prop)).1
        filter_upwards [hâ‚ƒg, hâ‚ƒG]
        simp_all
      Â· simp [hâ‚ƒf, zero_zpow]
  Â· simp only [toMeromorphicNFAt, hf, â†“reduceDIte]
    exact analyticAt_const.meromorphicNFAt

/-- If `f` has normal form at `x`, then `f` equals `f.toNF`. -/
@[simp] theorem toMeromorphicNFAt_eq_self :
    MeromorphicNFAt f x â†” f = toMeromorphicNFAt f x where
  mp hf := by
    funext z
    by_cases hz : z = x
    Â· rw [hz]
      simp only [toMeromorphicNFAt, hf.meromorphicAt, WithTop.coe_zero, ne_eq, Function.update_self]
      have hâ‚€f := hf
      rcases hf with hâ‚f | hâ‚f
      Â· simpa [(hâ‚€f.meromorphicAt.order_eq_top_iff).2 (hâ‚f.filter_mono nhdsWithin_le_nhds)]
          using hâ‚f.eq_of_nhds
      Â· obtain âŸ¨n, g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hâ‚f
        rw [Filter.EventuallyEq.eq_of_nhds hâ‚ƒg]
        have : hâ‚€f.meromorphicAt.order = n := by
          rw [MeromorphicAt.order_eq_int_iff (MeromorphicNFAt.meromorphicAt hâ‚€f) n]
          use g, hâ‚g, hâ‚‚g
          exact eventually_nhdsWithin_of_eventually_nhds hâ‚ƒg
        by_cases hâ‚ƒf : hâ‚€f.meromorphicAt.order = 0
        Â· simp only [Pi.smul_apply', Pi.pow_apply, sub_self, hâ‚ƒf, â†“reduceDIte]
          have hn : n = (0 : â„¤) := by
            rw [hâ‚ƒf] at this
            exact WithTop.coe_eq_zero.mp this.symm
          simp_rw [hn]
          simp only [zpow_zero, one_smul]
          have : g =á¶ [ğ“ x] (Classical.choose ((hâ‚€f.meromorphicAt.order_eq_int_iff 0).1 hâ‚ƒf)) := by
            obtain âŸ¨hâ‚€, hâ‚, hâ‚‚âŸ© := Classical.choose_spec
              ((hâ‚€f.meromorphicAt.order_eq_int_iff 0).1 hâ‚ƒf)
            rw [â† hâ‚g.continuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE hâ‚€.continuousAt]
            rw [hn] at hâ‚ƒg
            simp only [zpow_zero, one_smul, ne_eq] at hâ‚ƒg hâ‚‚
            exact (hâ‚ƒg.filter_mono nhdsWithin_le_nhds).symm.trans hâ‚‚
          simp only [Function.update_self]
          exact Filter.EventuallyEq.eq_of_nhds this
        Â· simp only [Pi.smul_apply', Pi.pow_apply, sub_self, hâ‚ƒf, â†“reduceDIte, smul_eq_zero,
            Function.update_self, smul_eq_zero]
          left
          apply zero_zpow n
          by_contra hn
          rw [hn] at this
          tauto
    Â· exact hf.meromorphicAt.eqOn_compl_singleton_toMermomorphicNFAt hz
  mpr hf := by
    rw [hf]
    exact meromorphicNFAt_toMeromorphicNFAt
