/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor

/-!
# Normal form of meromorphic functions and continuous extension

If a function `f` is meromorphic on `U` and if `g` differs from `f` only along a set that is
codiscrete within `U`, then `g` is likewise meromorphic. The set of meromorphic functions is
therefore huge, and `=·∂†[codiscreteWithin U]` defines an equivalence relation.

This file implements continuous extension to provide an API that allows picking the 'unique best'
representative of any given equivalence class, where 'best' means that the representative can
locally near any point `x` be written 'in normal form', as `f =·∂†[ùìù x] fun z ‚Ü¶ (z - x) ^ n ‚Ä¢ g`
where `g` is analytic and does not vanish at `x`.

The relevant notions are `MeromorphicNFAt` and `MeromorphicNFOn`; these guarantee normal
form at a single point and along a set, respectively.
-/

open Topology WithTop

variable
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {f : ùïú ‚Üí E} {g : ùïú ‚Üí ùïú}
  {x : ùïú}
  {U : Set ùïú}

/-!
# Normal form of meromorphic functions at a given point

## Definition and characterizations
-/

variable (f x) in
/-- A function is 'meromorphic in normal form' at `x` if it vanishes around `x`
or if it can locally be written as `fun z ‚Ü¶ (z - x) ^ n ‚Ä¢ g` where `g` is
analytic and does not vanish at `x`. -/
def MeromorphicNFAt :=
  f =·∂†[ùìù x] 0 ‚à®
    ‚àÉ (n : ‚Ñ§) (g : ùïú ‚Üí E), AnalyticAt ùïú g x ‚àß g x ‚â† 0 ‚àß f =·∂†[ùìù x] (¬∑ - x) ^ n ‚Ä¢ g

/-- A meromorphic function has normal form at `x` iff it is either analytic
there, or if it has a pole at `x` and takes the default value `0`. -/
theorem meromorphicNFAt_iff_analyticAt_or :
    MeromorphicNFAt f x ‚Üî
      AnalyticAt ùïú f x ‚à® (MeromorphicAt f x ‚àß meromorphicOrderAt f x < 0 ‚àß f x = 0) := by
  constructor
  ¬∑ rintro (h | ‚ü®n, g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü©)
    ¬∑ simp [(analyticAt_congr h).2 analyticAt_const]
    ¬∑ have hf : MeromorphicAt f x := by
        apply MeromorphicAt.congr _ (h‚ÇÉg.filter_mono nhdsWithin_le_nhds).symm
        fun_prop
      have : meromorphicOrderAt f x = n := by
        rw [meromorphicOrderAt_eq_int_iff hf]
        use g, h‚ÇÅg, h‚ÇÇg
        exact eventually_nhdsWithin_of_eventually_nhds h‚ÇÉg
      by_cases! hn : 0 ‚â§ n
      ¬∑ left
        rw [analyticAt_congr h‚ÇÉg]
        apply (AnalyticAt.zpow_nonneg (by fun_prop) hn).smul h‚ÇÅg
      ¬∑ right
        use hf
        simp [this, WithTop.coe_lt_zero.2 hn, h‚ÇÉg.eq_of_nhds,
          zero_zpow n hn.ne]
  ¬∑ rintro (h | ‚ü®h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©)
    ¬∑ by_cases h‚ÇÇf : analyticOrderAt f x = ‚ä§
      ¬∑ rw [analyticOrderAt_eq_top] at h‚ÇÇf
        tauto
      ¬∑ right
        use analyticOrderNatAt f x
        have : analyticOrderAt f x ‚â† ‚ä§ := h‚ÇÇf
        rw [‚Üê ENat.coe_toNat_eq_self, eq_comm, h.analyticOrderAt_eq_natCast] at this
        obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := this
        use g, h‚ÇÅg, h‚ÇÇg
        simpa
    ¬∑ right
      lift meromorphicOrderAt f x to ‚Ñ§ using LT.lt.ne_top h‚ÇÇ with n hn
      obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_eq_int_iff h‚ÇÅ).1 hn.symm
      use n, g, h‚ÇÅg, h‚ÇÇg
      filter_upwards [eventually_nhdsWithin_iff.1 h‚ÇÉg]
      intro z hz
      by_cases h‚ÇÅz : z = x
      ¬∑ simp only [h‚ÇÅz, Pi.smul_apply', Pi.pow_apply, sub_self]
        rw [h‚ÇÉ]
        apply (smul_eq_zero_of_left (zero_zpow n _) (g x)).symm
        by_contra hCon
        simp [hCon] at h‚ÇÇ
      ¬∑ exact hz h‚ÇÅz

/-!
## Relation to other properties of functions
-/

/-- If a function is meromorphic in normal form at `x`, then it is meromorphic at `x`. -/
theorem MeromorphicNFAt.meromorphicAt (hf : MeromorphicNFAt f x) :
    MeromorphicAt f x := by
  rw [meromorphicNFAt_iff_analyticAt_or] at hf
  rcases hf with h | h
  ¬∑ exact h.meromorphicAt
  ¬∑ obtain ‚ü®hf, _‚ü© := h
    exact hf

/-- If a function is meromorphic in normal form at `x`, then it has non-negative order iff it is
analytic. -/
theorem MeromorphicNFAt.meromorphicOrderAt_nonneg_iff_analyticAt (hf : MeromorphicNFAt f x) :
    0 ‚â§ meromorphicOrderAt f x ‚Üî AnalyticAt ùïú f x := by
  constructor <;> intro h‚ÇÇf
  ¬∑ rw [meromorphicNFAt_iff_analyticAt_or] at hf
    rcases hf with h | ‚ü®_, h‚ÇÉf, _‚ü©
    ¬∑ exact h
    ¬∑ by_contra h'
      exact lt_irrefl 0 (lt_of_le_of_lt h‚ÇÇf h‚ÇÉf)
  ¬∑ rw [h‚ÇÇf.meromorphicOrderAt_eq]
    simp

@[deprecated (since := "2025-05-22")] alias MeromorphicNFAt.order_nonneg_iff_analyticAt :=
  MeromorphicNFAt.meromorphicOrderAt_nonneg_iff_analyticAt

/-- Analytic functions are meromorphic in normal form. -/
theorem AnalyticAt.meromorphicNFAt (hf : AnalyticAt ùïú f x) :
    MeromorphicNFAt f x := by
  simp [meromorphicNFAt_iff_analyticAt_or, hf]

/-- Meromorphic functions have normal form outside of a discrete subset in the domain of
meromorphicity. -/
theorem MeromorphicOn.meromorphicNFAt_mem_codiscreteWithin {U : Set ùïú}
    (hf : MeromorphicOn f U) :
    { x | MeromorphicNFAt f x } ‚àà Filter.codiscreteWithin U := by
  filter_upwards [hf.analyticAt_mem_codiscreteWithin] with _ ha
  exact ha.meromorphicNFAt

/-!
## Vanishing and order
-/

/-- If `f` is meromorphic in normal form at `x`, then `f` has order zero iff it does not vanish at
`x`.

See `AnalyticAt.order_eq_zero_iff` for an analogous statement about analytic functions. -/
theorem MeromorphicNFAt.meromorphicOrderAt_eq_zero_iff (hf : MeromorphicNFAt f x) :
    meromorphicOrderAt f x = 0 ‚Üî f x ‚â† 0 := by
  constructor
  ¬∑ intro h‚ÇÅf
    have h‚ÇÇf := hf.meromorphicOrderAt_nonneg_iff_analyticAt.1 (le_of_eq h‚ÇÅf.symm)
    rw [‚Üê h‚ÇÇf.analyticOrderAt_eq_zero, ‚Üê ENat.map_natCast_eq_zero (Œ± := ‚Ñ§)]
    rwa [h‚ÇÇf.meromorphicOrderAt_eq] at h‚ÇÅf
  ¬∑ intro h
    rcases id hf with h‚ÇÅ | ‚ü®n, g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü©
    ¬∑ have := h‚ÇÅ.eq_of_nhds
      tauto
    ¬∑ have : n = 0 := by
        by_contra hContra
        have := h‚ÇÉg.eq_of_nhds
        simp only [Pi.smul_apply', Pi.pow_apply, sub_self, zero_zpow n hContra, zero_smul] at this
        tauto
      simp only [this, zpow_zero] at h‚ÇÉg
      apply (meromorphicOrderAt_eq_int_iff hf.meromorphicAt).2
      use g, h‚ÇÅg, h‚ÇÇg
      simp only [zpow_zero]
      exact h‚ÇÉg.filter_mono nhdsWithin_le_nhds

@[deprecated (since := "2025-05-22")] alias MeromorphicNFAt.order_eq_zero_iff :=
  MeromorphicNFAt.meromorphicOrderAt_eq_zero_iff
/-!
## Local nature of the definition and local identity theorem
-/

/-- **Local identity theorem**: two meromorphic functions in normal form agree in a
neighborhood iff they agree in a pointed neighborhood.

See `ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE` for the analogous
statement for continuous functions.
-/
theorem MeromorphicNFAt.eventuallyEq_nhdsNE_iff_eventuallyEq_nhds {g : ùïú ‚Üí E}
    (hf : MeromorphicNFAt f x) (hg : MeromorphicNFAt g x) :
    f =·∂†[ùìù[‚â†] x] g ‚Üî f =·∂†[ùìù x] g := by
  constructor
  ¬∑ intro h
    have t‚ÇÄ := meromorphicOrderAt_congr h
    by_cases cs : meromorphicOrderAt f x = 0
    ¬∑ rw [cs] at t‚ÇÄ
      have Z := (hf.meromorphicOrderAt_nonneg_iff_analyticAt.1 (le_of_eq cs.symm)).continuousAt
      have W := (hg.meromorphicOrderAt_nonneg_iff_analyticAt.1 (le_of_eq t‚ÇÄ)).continuousAt
      exact (Z.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE W).1 h
    ¬∑ apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE h
      let h‚ÇÅf := cs
      rw [hf.meromorphicOrderAt_eq_zero_iff] at h‚ÇÅf
      let h‚ÇÅg := cs
      rw [t‚ÇÄ, hg.meromorphicOrderAt_eq_zero_iff] at h‚ÇÅg
      simp only [not_not] at *
      rw [h‚ÇÅf, h‚ÇÅg]
  ¬∑ exact (Filter.EventuallyEq.filter_mono ¬∑ nhdsWithin_le_nhds)

@[deprecated (since := "2025-05-22")]
alias MeromorphicNFAt.eventuallyEq_nhdNE_iff_eventuallyEq_nhd :=
  MeromorphicNFAt.eventuallyEq_nhdsNE_iff_eventuallyEq_nhds

/-- Meromorphicity in normal form is a local property. -/
theorem meromorphicNFAt_congr {g : ùïú ‚Üí E} (hfg : f =·∂†[ùìù x] g) :
    MeromorphicNFAt f x ‚Üî MeromorphicNFAt g x := by
  constructor
  ¬∑ rintro (h | ‚ü®n, h, h‚ÇÅh, h‚ÇÇh, h‚ÇÉh‚ü©)
    ¬∑ exact .inl (hfg.symm.trans h)
    ¬∑ exact .inr ‚ü®n, h, h‚ÇÅh, h‚ÇÇh, hfg.symm.trans h‚ÇÉh‚ü©
  ¬∑ rintro (h | ‚ü®n, h, h‚ÇÅh, h‚ÇÇh, h‚ÇÉh‚ü©)
    ¬∑ exact .inl (hfg.trans h)
    ¬∑ exact .inr ‚ü®n, h, h‚ÇÅh, h‚ÇÇh, hfg.trans h‚ÇÉh‚ü©

/-!
## Criteria to guarantee normal form
-/

/-- Helper lemma for `meromorphicNFAt_iff_meromorphicNFAt_of_smul_analytic`: if
`f` is meromorphic in normal form at `x` and `g` is analytic without zero at
`x`, then `g ‚Ä¢ f` is meromorphic in normal form at `x`. -/
lemma MeromorphicNFAt.smul_analytic (hf : MeromorphicNFAt f x)
    (h‚ÇÅg : AnalyticAt ùïú g x) (h‚ÇÇg : g x ‚â† 0) :
    MeromorphicNFAt (g ‚Ä¢ f) x := by
  rcases hf with h‚ÇÅf | ‚ü®n, g_f, h‚ÇÅg_f, h‚ÇÇg_f, h‚ÇÉg_f‚ü©
  ¬∑ left
    filter_upwards [h‚ÇÅf]
    simp_all
  ¬∑ right
    use n, g ‚Ä¢ g_f, h‚ÇÅg.smul h‚ÇÅg_f
    constructor
    ¬∑ simp [smul_ne_zero h‚ÇÇg h‚ÇÇg_f]
    ¬∑ filter_upwards [h‚ÇÉg_f]
      intro y hy
      simp only [Pi.smul_apply', hy, Pi.pow_apply]
      rw [smul_comm]

/-- If `f` is any function and `g` is analytic without zero at `z‚ÇÄ`, then `f` is meromorphic in
normal form at `z‚ÇÄ` iff `g ‚Ä¢ f` is meromorphic in normal form at `z‚ÇÄ`. -/
theorem meromorphicNFAt_smul_iff_right_of_analyticAt (h‚ÇÅg : AnalyticAt ùïú g x)
    (h‚ÇÇg : g x ‚â† 0) :
    MeromorphicNFAt (g ‚Ä¢ f) x ‚Üî MeromorphicNFAt f x where
  mp hprod := by
    have : f =·∂†[ùìù x] g‚Åª¬π ‚Ä¢ g ‚Ä¢ f := by
      filter_upwards [h‚ÇÅg.continuousAt.preimage_mem_nhds (compl_singleton_mem_nhds_iff.mpr h‚ÇÇg)]
      intro y hy
      rw [Set.preimage_compl, Set.mem_compl_iff, Set.mem_preimage,
        Set.mem_singleton_iff] at hy
      simp [hy]
    rw [meromorphicNFAt_congr this]
    exact hprod.smul_analytic (h‚ÇÅg.inv h‚ÇÇg) (inv_ne_zero h‚ÇÇg)
  mpr hf := hf.smul_analytic h‚ÇÅg h‚ÇÇg

/-- If `f` is any function and `g` is analytic without zero at `z‚ÇÄ`, then `f` is meromorphic in
normal form at `z‚ÇÄ` iff `g * f` is meromorphic in normal form at `z‚ÇÄ`. -/
theorem meromorphicNFAt_mul_iff_right {f : ùïú ‚Üí ùïú} (h‚ÇÅg : AnalyticAt ùïú g x)
    (h‚ÇÇg : g x ‚â† 0) :
    MeromorphicNFAt (g * f) x ‚Üî MeromorphicNFAt f x :=
  meromorphicNFAt_smul_iff_right_of_analyticAt h‚ÇÅg h‚ÇÇg

/-- If `f` is any function and `g` is analytic without zero at `z‚ÇÄ`, then `f` is meromorphic in
normal form at `z‚ÇÄ` iff `f * g` is meromorphic in normal form at `z‚ÇÄ`. -/
theorem meromorphicNFAt_mul_iff_left {f : ùïú ‚Üí ùïú} (h‚ÇÅg : AnalyticAt ùïú g x)
    (h‚ÇÇg : g x ‚â† 0) :
    MeromorphicNFAt (f * g) x ‚Üî MeromorphicNFAt f x := by
  rw [mul_comm, ‚Üê smul_eq_mul]
  exact meromorphicNFAt_smul_iff_right_of_analyticAt h‚ÇÅg h‚ÇÇg

/-!
## Continuous extension and conversion to normal form
-/

variable (f x) in
/-- If `f` is meromorphic at `x`, convert `f` to normal form at `x` by changing its value at `x`.
Otherwise, returns the 0 function. -/
noncomputable def toMeromorphicNFAt :
    ùïú ‚Üí E := by
  by_cases hf : MeromorphicAt f x
  ¬∑ classical -- do not complain about decidability issues in Function.update
    apply Function.update f x
    by_cases h‚ÇÅf : meromorphicOrderAt f x = (0 : ‚Ñ§)
    ¬∑ rw [meromorphicOrderAt_eq_int_iff hf] at h‚ÇÅf
      exact (Classical.choose h‚ÇÅf) x
    ¬∑ exact 0
  ¬∑ exact 0

/-- Conversion to normal form at `x` changes the value only at x. -/
lemma MeromorphicAt.eqOn_compl_singleton_toMeromorphicNFAt (hf : MeromorphicAt f x) :
    Set.EqOn f (toMeromorphicNFAt f x) {x}·∂ú :=
  fun _ _ ‚Ü¶ by simp_all [toMeromorphicNFAt]

@[deprecated (since := "2025-07-27")]
alias MeromorphicAt.eqOn_compl_singleton_toMermomorphicNFAt :=
  MeromorphicAt.eqOn_compl_singleton_toMeromorphicNFAt

/-- If `f` is not meromorphic, conversion to normal form at `x` maps the function to `0`. -/
@[simp] lemma toMeromorphicNFAt_of_not_meromorphicAt (hf : ¬¨MeromorphicAt f x) :
    toMeromorphicNFAt f x = 0 := by
  simp [toMeromorphicNFAt, hf]

/-- Conversion to normal form at `x` changes the value only at x. -/
lemma MeromorphicAt.eq_nhdsNE_toMeromorphicNFAt (hf : MeromorphicAt f x) :
    f =·∂†[ùìù[‚â†] x] toMeromorphicNFAt f x :=
  eventually_nhdsWithin_of_forall (fun _ hz ‚Ü¶ hf.eqOn_compl_singleton_toMeromorphicNFAt hz)

@[deprecated (since := "2025-05-22")]
alias MeromorphicAt.eq_nhdNE_toMeromorphicNFAt := MeromorphicAt.eq_nhdsNE_toMeromorphicNFAt

/-- After conversion to normal form at `x`, the function has normal form. -/
theorem meromorphicNFAt_toMeromorphicNFAt :
    MeromorphicNFAt (toMeromorphicNFAt f x) x := by
  by_cases hf : MeromorphicAt f x
  ¬∑ by_cases h‚ÇÇf : meromorphicOrderAt f x = ‚ä§
    ¬∑ have : toMeromorphicNFAt f x =·∂†[ùìù x] 0 := by
        apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE
        ¬∑ exact hf.eq_nhdsNE_toMeromorphicNFAt.symm.trans (meromorphicOrderAt_eq_top_iff.1 h‚ÇÇf)
        ¬∑ simp [h‚ÇÇf, toMeromorphicNFAt, hf]
      apply AnalyticAt.meromorphicNFAt
      rw [analyticAt_congr this]
      exact analyticAt_const
    ¬∑ lift meromorphicOrderAt f x to ‚Ñ§ using h‚ÇÇf with n hn
      obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_eq_int_iff hf).1 hn.symm
      right
      use n, g, h‚ÇÅg, h‚ÇÇg
      apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE (hf.eq_nhdsNE_toMeromorphicNFAt.symm.trans h‚ÇÉg)
      simp only [toMeromorphicNFAt, hf, ‚ÜìreduceDIte, ‚Üê hn, WithTop.coe_zero,
        WithTop.coe_eq_zero, ne_eq, Function.update_self, sub_self]
      split_ifs with h‚ÇÉf
      ¬∑ obtain ‚ü®h‚ÇÅG, _, h‚ÇÉG‚ü© :=
          Classical.choose_spec ((meromorphicOrderAt_eq_int_iff hf).1 (h‚ÇÉf ‚ñ∏ hn.symm))
        apply Filter.EventuallyEq.eq_of_nhds
        apply (h‚ÇÅG.continuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE (by fun_prop)).1
        filter_upwards [h‚ÇÉg, h‚ÇÉG]
        simp_all
      ¬∑ simp [h‚ÇÉf, zero_zpow]
  ¬∑ simp only [toMeromorphicNFAt, hf, ‚ÜìreduceDIte]
    exact analyticAt_const.meromorphicNFAt

/-- If `f` has normal form at `x`, then `f` equals `f.toNF`. -/
@[simp] theorem toMeromorphicNFAt_eq_self :
    toMeromorphicNFAt f x = f ‚Üî MeromorphicNFAt f x where
  mp hf := by
    rw [hf.symm]
    exact meromorphicNFAt_toMeromorphicNFAt
  mpr hf := by
    funext z
    by_cases hz : z = x
    ¬∑ rw [hz]
      simp only [toMeromorphicNFAt, hf.meromorphicAt, WithTop.coe_zero, ne_eq]
      have h‚ÇÄf := hf
      rcases hf with h‚ÇÅf | h‚ÇÅf
      ¬∑ simpa [meromorphicOrderAt_eq_top_iff.2 (h‚ÇÅf.filter_mono nhdsWithin_le_nhds)]
          using h‚ÇÅf.eq_of_nhds.symm
      ¬∑ obtain ‚ü®n, g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := h‚ÇÅf
        rw [Filter.EventuallyEq.eq_of_nhds h‚ÇÉg]
        have : meromorphicOrderAt f x = n := by
          rw [meromorphicOrderAt_eq_int_iff h‚ÇÄf.meromorphicAt]
          use g, h‚ÇÅg, h‚ÇÇg
          exact eventually_nhdsWithin_of_eventually_nhds h‚ÇÉg
        by_cases h‚ÇÉf : meromorphicOrderAt f x = 0
        ¬∑ simp only [Pi.smul_apply', Pi.pow_apply, sub_self, h‚ÇÉf, ‚ÜìreduceDIte]
          have hn : n = (0 : ‚Ñ§) := by
            rw [h‚ÇÉf] at this
            exact WithTop.coe_eq_zero.mp this.symm
          simp_rw [hn]
          simp only [zpow_zero, one_smul]
          have : g =·∂†[ùìù x]
              Classical.choose ((meromorphicOrderAt_eq_int_iff h‚ÇÄf.meromorphicAt).1 h‚ÇÉf) := by
            obtain ‚ü®h‚ÇÄ, h‚ÇÅ, h‚ÇÇ‚ü© := Classical.choose_spec
              ((meromorphicOrderAt_eq_int_iff h‚ÇÄf.meromorphicAt).1 h‚ÇÉf)
            rw [‚Üê h‚ÇÅg.continuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE h‚ÇÄ.continuousAt]
            rw [hn] at h‚ÇÉg
            simp only [zpow_zero, one_smul, ne_eq] at h‚ÇÉg h‚ÇÇ
            exact (h‚ÇÉg.filter_mono nhdsWithin_le_nhds).symm.trans h‚ÇÇ
          simp only [Function.update_self]
          exact Filter.EventuallyEq.eq_of_nhds this.symm
        ¬∑ rw [eq_comm]
          simp only [Pi.smul_apply', Pi.pow_apply, sub_self, h‚ÇÉf, ‚ÜìreduceDIte, smul_eq_zero,
            Function.update_self, smul_eq_zero]
          left
          apply zero_zpow n
          by_contra hn
          rw [hn] at this
          tauto
    ¬∑ exact (hf.meromorphicAt.eqOn_compl_singleton_toMeromorphicNFAt hz).symm

/--
If `f` is meromorphic in normal form, then so is its inverse.
-/
theorem MeromorphicNFAt.inv {f : ùïú ‚Üí ùïú} (hf : MeromorphicNFAt f x) :
    MeromorphicNFAt f‚Åª¬π x := by
  rcases hf with h | ‚ü®n, g, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©
  ¬∑ left
    filter_upwards [h] with x hx
    simp [hx]
  ¬∑ right
    use -n, g‚Åª¬π, h‚ÇÅ.inv h‚ÇÇ, (by simp_all)
    filter_upwards [h‚ÇÉ] with y hy
    simp only [Pi.inv_apply, hy, Pi.smul_apply', Pi.pow_apply, smul_eq_mul, mul_inv_rev, zpow_neg]
    ring

/--
A function to ùïú is meromorphic in normal form at a point iff its inverse is.
-/
@[simp] theorem meromorphicNFAt_inv {f : ùïú ‚Üí ùïú} : MeromorphicNFAt f‚Åª¬π x ‚Üî MeromorphicNFAt f x where
  mp := by
    nth_rw 2 [‚Üê inv_inv f]
    exact .inv
  mpr hf := by simpa using hf.inv

/-!
# Normal form of meromorphic functions on a given set

## Definition
-/

/--
A function is 'meromorphic in normal form' on `U` if has normal form at every
point of `U`.
-/
def MeromorphicNFOn (f : ùïú ‚Üí E) (U : Set ùïú) := ‚àÄ ‚¶Éz‚¶Ñ, z ‚àà U ‚Üí MeromorphicNFAt f z

/-!
## Relation to other properties of functions
-/

/--
If a function is meromorphic in normal form on `U`, then it is meromorphic on
`U`.
-/
theorem MeromorphicNFOn.meromorphicOn (hf : MeromorphicNFOn f U) :
    MeromorphicOn f U := fun _ hz ‚Ü¶ (hf hz).meromorphicAt

/--
If a function is meromorphic in normal form on `U`, then its divisor is
non-negative iff it is analytic.
-/
theorem MeromorphicNFOn.divisor_nonneg_iff_analyticOnNhd
    (h‚ÇÅf : MeromorphicNFOn f U) :
    0 ‚â§ MeromorphicOn.divisor f U ‚Üî AnalyticOnNhd ùïú f U := by
  constructor <;> intro h x
  ¬∑ intro hx
    rw [‚Üê (h‚ÇÅf hx).meromorphicOrderAt_nonneg_iff_analyticAt]
    have := h x
    simp only [Function.locallyFinsuppWithin.coe_zero, Pi.zero_apply, h‚ÇÅf.meromorphicOn, hx,
      MeromorphicOn.divisor_apply, untop‚ÇÄ_nonneg] at this
    assumption
  ¬∑ by_cases hx : x ‚àà U
    ¬∑ simp only [Function.locallyFinsuppWithin.coe_zero, Pi.zero_apply, h‚ÇÅf.meromorphicOn, hx,
        MeromorphicOn.divisor_apply, untop‚ÇÄ_nonneg]
      exact (h‚ÇÅf hx).meromorphicOrderAt_nonneg_iff_analyticAt.2 (h x hx)
    ¬∑ simp [hx]

/-- Analytic functions are meromorphic in normal form. -/
theorem AnalyticOnNhd.meromorphicNFOn (h‚ÇÅf : AnalyticOnNhd ùïú f U) :
    MeromorphicNFOn f U := fun z hz ‚Ü¶ (h‚ÇÅf z hz).meromorphicNFAt

/-!
## Divisors and zeros of meromorphic functions in normal form.
-/

/--
If `f` is meromorphic in normal form on `U` and nowhere locally constant zero,
then its zero set equals the support of the associated divisor.
-/
theorem MeromorphicNFOn.zero_set_eq_divisor_support (h‚ÇÅf : MeromorphicNFOn f U)
    (h‚ÇÇf : ‚àÄ u : U, meromorphicOrderAt f u ‚â† ‚ä§) :
    U ‚à© f‚Åª¬π' {0} = Function.support (MeromorphicOn.divisor f U) := by
  ext u
  constructor <;> intro hu
  ¬∑ simp_all only [ne_eq, Subtype.forall, Set.mem_inter_iff, Set.mem_preimage,
      Set.mem_singleton_iff, Function.mem_support, h‚ÇÅf.meromorphicOn, MeromorphicOn.divisor_apply,
      WithTop.untop‚ÇÄ_eq_zero, (h‚ÇÅf hu.1).meromorphicOrderAt_eq_zero_iff, not_true_eq_false, or_self,
      not_false_eq_true]
  ¬∑ simp only [Function.mem_support, ne_eq] at hu
    constructor
    ¬∑ exact (MeromorphicOn.divisor f U).supportWithinDomain hu
    ¬∑ rw [Set.mem_preimage, Set.mem_singleton_iff]
      have := h‚ÇÅf ((MeromorphicOn.divisor f U).supportWithinDomain hu)
        |>.meromorphicOrderAt_eq_zero_iff.not
      simp only [h‚ÇÅf.meromorphicOn, (MeromorphicOn.divisor f U).supportWithinDomain hu,
        MeromorphicOn.divisor_apply, WithTop.untop‚ÇÄ_eq_zero, not_or] at hu
      simp_all [hu.1]

/-!
## Criteria to guarantee normal form
-/

/--
If `f` is any function and `g` is analytic without zero on `U`, then `f` is
meromorphic in normal form on `U` iff `g ‚Ä¢ f` is meromorphic in normal form on
`U`.
-/
theorem meromorphicNFOn_smul_iff_right_of_analyticOnNhd {g : ùïú ‚Üí ùïú} (h‚ÇÅg : AnalyticOnNhd ùïú g U)
    (h‚ÇÇg : ‚àÄ u ‚àà U, g u ‚â† 0) :
    MeromorphicNFOn (g ‚Ä¢ f) U ‚Üî MeromorphicNFOn f U := by
  constructor <;> intro h z hz
  ¬∑ rw [‚Üê meromorphicNFAt_smul_iff_right_of_analyticAt (h‚ÇÅg z hz) (h‚ÇÇg z hz)]
    exact h hz
  ¬∑ apply (h hz).smul_analytic (h‚ÇÅg z hz)
    exact h‚ÇÇg z hz

/--
If `f` is any function and `g` is analytic without zero in `U`, then `f` is
meromorphic in normal form on `U` iff `g * f` is meromorphic in normal form on
`U`.
-/
theorem meromorphicNFOn_mul_iff_right_of_analyticOnNhd {f g : ùïú ‚Üí ùïú} (h‚ÇÅg : AnalyticOnNhd ùïú g U)
    (h‚ÇÇg : ‚àÄ u ‚àà U, g u ‚â† 0) :
    MeromorphicNFOn (g * f) U ‚Üî MeromorphicNFOn f U := by
  rw [‚Üê smul_eq_mul]
  exact meromorphicNFOn_smul_iff_right_of_analyticOnNhd h‚ÇÅg h‚ÇÇg

/--
If `f` is any function and `g` is analytic without zero in `U`, then `f` is
meromorphic in normal form on `U` iff `f * g` is meromorphic in normal form on
`U`.
-/
theorem meromorphicNFOn_mul_iff_left_of_analyticOnNhd {f g : ùïú ‚Üí ùïú} (h‚ÇÅg : AnalyticOnNhd ùïú g U)
    (h‚ÇÇg : ‚àÄ u ‚àà U, g u ‚â† 0) :
    MeromorphicNFOn (f * g) U ‚Üî MeromorphicNFOn f U := by
  rw [mul_comm, ‚Üê smul_eq_mul]
  exact meromorphicNFOn_mul_iff_right_of_analyticOnNhd h‚ÇÅg h‚ÇÇg

/--
A function to ùïú is meromorphic in normal form on `U` iff its inverse is.
-/
theorem meromorphicNFOn_inv {f : ùïú ‚Üí ùïú} :
    MeromorphicNFOn f‚Åª¬π U ‚Üî MeromorphicNFOn f U where
  mp h _ hx := meromorphicNFAt_inv.1 (h hx)
  mpr h _ hx := meromorphicNFAt_inv.2 (h hx)

/-!
## Continuous extension and conversion to normal form
-/

variable (f U) in
/--
If `f` is meromorphic on `U`, convert `f` to normal form on `U` by changing its
values along a discrete subset within `U`. Otherwise, returns the 0 function.
-/
noncomputable def toMeromorphicNFOn :
    ùïú ‚Üí E := by
  by_cases h‚ÇÅf : MeromorphicOn f U
  ¬∑ intro z
    by_cases hz : z ‚àà U
    ¬∑ exact toMeromorphicNFAt f z z
    ¬∑ exact f z
  ¬∑ exact 0

/--
If `f` is not meromorphic on `U`, conversion to normal form maps the function
to `0`.
-/
@[simp] lemma toMeromorphicNFOn_of_not_meromorphicOn (hf : ¬¨MeromorphicOn f U) :
    toMeromorphicNFOn f U = 0 := by
  simp [toMeromorphicNFOn, hf]

/--
Conversion to normal form on `U` does not change values outside of `U`.
-/
@[simp] lemma toMeromorphicNFOn_eq_self_on_compl (hf : MeromorphicOn f U) :
    Set.EqOn (toMeromorphicNFOn f U) f U·∂ú := by
  intro x hx
  simp_all [toMeromorphicNFOn]

/--
Conversion to normal form on `U` changes the value only along a discrete subset
of `U`.
-/
theorem toMeromorphicNFOn_eqOn_codiscrete (hf : MeromorphicOn f U) :
    f =·∂†[Filter.codiscreteWithin U] toMeromorphicNFOn f U := by
  have : U ‚àà Filter.codiscreteWithin U := by
    simp [mem_codiscreteWithin.2]
  filter_upwards [hf.analyticAt_mem_codiscreteWithin, this] with a h‚ÇÅa h‚ÇÇa
  simp [toMeromorphicNFOn, hf, ‚Üê (toMeromorphicNFAt_eq_self.2 h‚ÇÅa.meromorphicNFAt).symm]

/--
If `f` is meromorphic on `U` and `x ‚àà U`, then `f` and its conversion to normal
form on `U` agree in a punctured neighborhood of `x`.
-/
theorem MeromorphicOn.toMeromorphicNFOn_eq_self_on_nhdsNE
    (hf : MeromorphicOn f U) (hx : x ‚àà U) :
    toMeromorphicNFOn f U =·∂†[ùìù[‚â†] x] f := by
  filter_upwards [hf.eventually_analyticAt_or_mem_compl hx] with a ha
  rcases ha with ha | ha
  ¬∑ simp [toMeromorphicNFOn, hf, ‚Üê (toMeromorphicNFAt_eq_self.2 ha.meromorphicNFAt).symm]
  ¬∑ simp only [Set.mem_compl_iff] at ha
    simp [toMeromorphicNFOn, ha, hf]

@[deprecated (since := "2025-05-22")] alias MeromorphicOn.toMeromorphicNFOn_eq_self_on_nhdNE :=
  MeromorphicOn.toMeromorphicNFOn_eq_self_on_nhdsNE

/--
If `f` is meromorphic on `U` and `x ‚àà U`, then conversion to normal form at `x`
and conversion to normal form on `U` agree in a neighborhood of `x`.
-/
theorem toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhds (hf : MeromorphicOn f U)
    (hx : x ‚àà U) :
    toMeromorphicNFOn f U =·∂†[ùìù x] toMeromorphicNFAt f x := by
  apply eventuallyEq_nhds_of_eventuallyEq_nhdsNE
  ¬∑ exact (hf.toMeromorphicNFOn_eq_self_on_nhdsNE hx).trans (hf x hx).eq_nhdsNE_toMeromorphicNFAt
  ¬∑ simp [toMeromorphicNFOn, hf, hx]

@[deprecated (since := "2025-05-22")]
alias toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhd :=
  toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhds

/--
If `f` is meromorphic on `U` and `x ‚àà U`, then conversion to normal form at `x`
and conversion to normal form on `U` agree at `x`.
-/
theorem toMeromorphicNFOn_eq_toMeromorphicNFAt (hf : MeromorphicOn f U)
    (hx : x ‚àà U) :
    toMeromorphicNFOn f U x = toMeromorphicNFAt f x x := by
  apply Filter.EventuallyEq.eq_of_nhds (g := toMeromorphicNFAt f x)
  simp [(toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhds hf hx).trans]

variable (f U) in
/--
After conversion to normal form on `U`, the function has normal form.
-/
theorem meromorphicNFOn_toMeromorphicNFOn :
    MeromorphicNFOn (toMeromorphicNFOn f U) U := by
  by_cases hf : MeromorphicOn f U
  ¬∑ intro z hz
    rw [meromorphicNFAt_congr (toMeromorphicNFOn_eq_toMeromorphicNFAt_on_nhds hf hz)]
    exact meromorphicNFAt_toMeromorphicNFAt
  ¬∑ simpa [hf] using analyticOnNhd_const.meromorphicNFOn

/--
If `f` has normal form on `U`, then `f` equals `toMeromorphicNFOn f U`.
-/
@[simp] theorem toMeromorphicNFOn_eq_self :
    toMeromorphicNFOn f U = f ‚Üî MeromorphicNFOn f U := by
  constructor <;> intro h
  ¬∑ rw [h.symm]
    apply meromorphicNFOn_toMeromorphicNFOn
  ¬∑ ext x
    by_cases hx : x ‚àà U
    ¬∑ simp only [toMeromorphicNFOn, h.meromorphicOn, ‚ÜìreduceDIte, hx]
      rw [toMeromorphicNFAt_eq_self.2 (h hx)]
    ¬∑ simp [toMeromorphicNFOn, h.meromorphicOn, hx]

/--
Conversion of normal form does not affect orders.
-/
@[simp] theorem meromorphicOrderAt_toMeromorphicNFOn (hf : MeromorphicOn f U) (hx : x ‚àà U) :
    meromorphicOrderAt (toMeromorphicNFOn f U) x = meromorphicOrderAt f x := by
  apply meromorphicOrderAt_congr
  exact hf.toMeromorphicNFOn_eq_self_on_nhdsNE hx

@[deprecated (since := "2025-05-22")]
alias order_toMeromorphicNFOn := meromorphicOrderAt_toMeromorphicNFOn

/--
Conversion of normal form does not affect divisors.
-/
@[simp] theorem MeromorphicOn.divisor_of_toMeromorphicNFOn (hf : MeromorphicOn f U) :
    divisor (toMeromorphicNFOn f U) U = divisor f U := by
  ext z
  by_cases hz : z ‚àà U <;> simp [hf, (meromorphicNFOn_toMeromorphicNFOn f U).meromorphicOn, hz]
