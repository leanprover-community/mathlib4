/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Stefan Kebekus
-/
import Mathlib.Algebra.Order.WithTop.Untop0
import Mathlib.Analysis.Analytic.Order
import Mathlib.Analysis.Meromorphic.Basic

/-!
# Orders of Meromorphic Functions

This file defines the order of a meromorphic function `f` at a point `zâ‚€`, as an element of
`â„¤ âˆª {âˆ}`.

We characterize the order being `< 0`, or `= 0`, or `> 0`, as the convergence of the function
to infinity, resp. a nonzero constant, resp. zero.

TODO: Uniformize API between analytic and meromorphic functions
-/

open Filter Set WithTop.LinearOrderedAddCommGroup
open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f fâ‚ fâ‚‚ : ğ•œ â†’ E} {x : ğ•œ}

/-!
## Order at a Point: Definition and Characterization
-/

namespace MeromorphicAt

/-- The order of a meromorphic function `f` at `zâ‚€`, as an element of `â„¤ âˆª {âˆ}`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `MeromorphicAt.order_eq_top_iff` and
`MeromorphicAt.order_eq_int_iff` for these equivalences. -/
noncomputable def order (hf : MeromorphicAt f x) : WithTop â„¤ :=
  (hf.choose_spec.order.map (â†‘Â· : â„• â†’ â„¤)) - hf.choose

/-- The order of a meromorphic function `f` at a `zâ‚€` is infinity iff `f` vanishes locally around
`zâ‚€`. -/
lemma order_eq_top_iff (hf : MeromorphicAt f x) :
    hf.order = âŠ¤ â†” âˆ€á¶  z in ğ“[â‰ ] x, f z = 0 := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, ENat.map_top, â† WithTop.coe_natCast,
      top_sub, eq_self, true_iff, eventually_nhdsWithin_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    filter_upwards [h] with z hf hz
    rwa [smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)] at hf
  Â· obtain âŸ¨m, hmâŸ© := ENat.ne_top_iff_exists.mp h
    simp only [â† hm, ENat.map_coe, WithTop.coe_natCast, sub_eq_top_iff, WithTop.natCast_ne_top,
      or_self, false_iff]
    contrapose! h
    rw [AnalyticAt.order_eq_top_iff]
    rw [â† hf.choose_spec.frequently_eq_iff_eventually_eq analyticAt_const]
    apply Eventually.frequently
    filter_upwards [h] with z hfz
    rw [hfz, smul_zero]

/-- The order of a meromorphic function `f` at `zâ‚€` equals an integer `n` iff `f` can locally be
written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma order_eq_int_iff {n : â„¤} (hf : MeromorphicAt f x) : hf.order = n â†”
    âˆƒ g : ğ•œ â†’ E, AnalyticAt ğ•œ g x âˆ§ g x â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  unfold order
  by_cases h : hf.choose_spec.order = âŠ¤
  Â· rw [h, ENat.map_top, â† WithTop.coe_natCast, top_sub,
      eq_false_intro WithTop.top_ne_coe, false_iff]
    rw [AnalyticAt.order_eq_top_iff] at h
    refine fun âŸ¨g, hg_an, hg_ne, hg_eqâŸ© â†¦ hg_ne ?_
    apply EventuallyEq.eq_of_nhds
    rw [EventuallyEq, â† AnalyticAt.frequently_eq_iff_eventually_eq hg_an analyticAt_const]
    apply Eventually.frequently
    rw [eventually_nhdsWithin_iff] at hg_eq âŠ¢
    filter_upwards [h, hg_eq] with z hfz hfz_eq hz
    rwa [hfz_eq hz, â† mul_smul, smul_eq_zero_iff_right] at hfz
    exact mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz)) (zpow_ne_zero _ (sub_ne_zero.mpr hz))
  Â· obtain âŸ¨m, hâŸ© := ENat.ne_top_iff_exists.mp h
    rw [â† h, ENat.map_coe, â† WithTop.coe_natCast, â† coe_sub, WithTop.coe_inj]
    obtain âŸ¨g, hg_an, hg_ne, hg_eqâŸ© := (AnalyticAt.order_eq_nat_iff _).mp h.symm
    replace hg_eq : âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] x, f z = (z - x) ^ (â†‘m - â†‘hf.choose : â„¤) â€¢ g z := by
      rw [eventually_nhdsWithin_iff]
      filter_upwards [hg_eq] with z hg_eq hz
      rwa [â† smul_right_inj <| zpow_ne_zero _ (sub_ne_zero.mpr hz), â† mul_smul,
        â† zpow_addâ‚€ (sub_ne_zero.mpr hz), â† add_sub_assoc, add_sub_cancel_left, zpow_natCast,
        zpow_natCast]
    exact âŸ¨fun h â†¦ âŸ¨g, hg_an, hg_ne, h â–¸ hg_eqâŸ©,
      AnalyticAt.unique_eventuallyEq_zpow_smul_nonzero âŸ¨g, hg_an, hg_ne, hg_eqâŸ©âŸ©

/--
The order of a meromorphic function `f` at `zâ‚€` is finite iff `f` can locally be
written as `f z = (z - zâ‚€) ^ order â€¢ g z`, where `g` is analytic and does not
vanish at `zâ‚€`.
-/
theorem order_ne_top_iff {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hf : MeromorphicAt f zâ‚€) :
    hf.order â‰  âŠ¤ â†” âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
      f =á¶ [ğ“[â‰ ] zâ‚€] fun z â†¦ (z - zâ‚€) ^ (hf.order.untopâ‚€) â€¢ g z :=
  âŸ¨fun h â†¦ hf.order_eq_int_iff.1 (WithTop.coe_untopâ‚€_of_ne_top h).symm,
    fun h â†¦ Option.ne_none_iff_exists'.2 âŸ¨hf.order.untopD 0, hf.order_eq_int_iff.2 hâŸ©âŸ©

/--
The order of a meromorphic function `f` at `zâ‚€` is finite iff `f` does not have
any zeros in a sufficiently small neighborhood of `zâ‚€`.
-/
theorem order_ne_top_iff_eventually_ne_zero {f : ğ•œ â†’ E} (hf : MeromorphicAt f x) :
    hf.order â‰  âŠ¤ â†” âˆ€á¶  x in ğ“[â‰ ] x, f x â‰  0 := by
  constructor
  Â· intro h
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hf.order_ne_top_iff.1 h
    filter_upwards [hâ‚ƒg, self_mem_nhdsWithin, eventually_nhdsWithin_of_eventually_nhds
      ((hâ‚g.continuousAt.ne_iff_eventually_ne continuousAt_const).mp hâ‚‚g)]
    simp_all [zpow_ne_zero, sub_ne_zero]
  Â· simp_all [hf.order_eq_top_iff, Eventually.frequently]

/-- If the order of a meromorphic function is negative, then this function converges to infinity
at this point. See also the iff version `tendsto_cobounded_iff_order_neg`. -/
lemma tendsto_cobounded_of_order_neg (hf : MeromorphicAt f x) (ho : hf.order < 0) :
    Tendsto f (ğ“[â‰ ] x) (Bornology.cobounded E) := by
  simp only [â† tendsto_norm_atTop_iff_cobounded]
  obtain âŸ¨m, hmâŸ© := WithTop.ne_top_iff_exists.mp ho.ne_top
  have m_neg : m < 0 := by simpa [â† hm] using ho
  rcases hf.order_eq_int_iff.1 hm.symm with âŸ¨g, g_an, gx, hgâŸ©
  have A : Tendsto (fun z â†¦ â€–(z - x) ^ m â€¢ g zâ€–) (ğ“[â‰ ] x) atTop := by
    simp only [norm_smul]
    apply Filter.Tendsto.atTop_mul_pos (C := â€–g xâ€–) (by simp [gx]) _
      g_an.continuousAt.continuousWithinAt.tendsto.norm
    have : Tendsto (fun z â†¦ z - x) (ğ“[â‰ ] x) (ğ“[â‰ ] 0) := by
      refine tendsto_nhdsWithin_iff.2 âŸ¨?_, ?_âŸ©
      Â· have : ContinuousWithinAt (fun z â†¦ z - x) ({x}á¶œ) x :=
          ContinuousAt.continuousWithinAt (by fun_prop)
        simpa using this.tendsto
      Â· filter_upwards [self_mem_nhdsWithin] with y hy
        simpa [sub_eq_zero] using hy
    apply Tendsto.comp (NormedField.tendsto_norm_zpow_nhdsNE_zero_atTop m_neg) this
  apply A.congr'
  filter_upwards [hg] with z hz using by simp [hz]

/-- If the order of a meromorphic function is zero, then this function converges to a nonzero
limit at this point. See also the iff version `tendsto_ne_zero_iff_order_eq_zero`. -/
lemma tendsto_ne_zero_of_order_eq_zero (hf : MeromorphicAt f x) (ho : hf.order = 0) :
    âˆƒ c â‰  0, Tendsto f (ğ“[â‰ ] x) (ğ“ c) := by
  rcases hf.order_eq_int_iff.1 ho with âŸ¨g, g_an, gx, hgâŸ©
  refine âŸ¨g x, gx, ?_âŸ©
  apply g_an.continuousAt.continuousWithinAt.tendsto.congr'
  filter_upwards [hg] with y hy using by simp [hy]

/-- If the order of a meromorphic function is positive, then this function converges to zero
at this point. See also the iff version `tendsto_zero_iff_order_pos`. -/
lemma tendsto_zero_of_order_pos (hf : MeromorphicAt f x) (ho : 0 < hf.order) :
    Tendsto f (ğ“[â‰ ] x) (ğ“ 0) := by
  cases h'o : hf.order with
  | top =>
    apply tendsto_const_nhds.congr'
    filter_upwards [hf.order_eq_top_iff.1 h'o] with y hy using hy.symm
  | coe n =>
    rcases hf.order_eq_int_iff.1 h'o with âŸ¨g, g_an, gx, hgâŸ©
    lift n to â„• using by simpa [h'o] using ho.le
    have : (0 : E) = (x - x) ^ n â€¢ g x := by
      have : 0 < n := by simpa [h'o] using ho
      simp [zero_pow_eq_zero.2 this.ne']
    rw [this]
    have : ContinuousAt (fun z â†¦ (z - x) ^ n â€¢ g z) x := by fun_prop
    apply this.continuousWithinAt.tendsto.congr'
    filter_upwards [hg] with y hy using by simp [hy]

/-- If the order of a meromorphic function is nonnegative, then this function converges
at this point. See also the iff version `tendsto_nhds_iff_order_nonneg`. -/
lemma tendsto_nhds_of_order_nonneg (hf : MeromorphicAt f x) (ho : 0 â‰¤ hf.order) :
    âˆƒ c, Tendsto f (ğ“[â‰ ] x) (ğ“ c) := by
  rcases ho.eq_or_lt with ho | ho
  Â· rcases hf.tendsto_ne_zero_of_order_eq_zero ho.symm with âŸ¨c, -, hcâŸ©
    exact âŸ¨c, hcâŸ©
  Â· exact âŸ¨0, hf.tendsto_zero_of_order_pos hoâŸ©

/-- A meromorphic function converges to infinity iff its order is negative. -/
lemma tendsto_cobounded_iff_order_neg (hf : MeromorphicAt f x) :
    Tendsto f (ğ“[â‰ ] x) (Bornology.cobounded E) â†” hf.order < 0 := by
  rcases lt_or_le hf.order 0 with ho | ho
  Â· simp [ho, hf.tendsto_cobounded_of_order_neg]
  Â· simp only [lt_iff_not_le, ho, not_true_eq_false, iff_false, â† tendsto_norm_atTop_iff_cobounded]
    obtain âŸ¨c, hcâŸ© := hf.tendsto_nhds_of_order_nonneg ho
    exact not_tendsto_atTop_of_tendsto_nhds hc.norm

/-- A meromorphic function converges to a limit iff its order is nonnegative. -/
lemma tendsto_nhds_iff_order_nonneg (hf : MeromorphicAt f x) :
    (âˆƒ c, Tendsto f (ğ“[â‰ ] x) (ğ“ c)) â†” 0 â‰¤ hf.order := by
  rcases lt_or_le hf.order 0 with ho | ho
  Â· simp only [â† not_lt, ho, not_true_eq_false, iff_false, not_exists]
    intro c hc
    apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact hf.tendsto_cobounded_of_order_neg ho
  Â· simp [ho, hf.tendsto_nhds_of_order_nonneg ho]

/-- A meromorphic function converges to a nonzero limit iff its order is zero. -/
lemma tendsto_ne_zero_iff_order_eq_zero (hf : MeromorphicAt f x) :
    (âˆƒ c â‰  0, Tendsto f (ğ“[â‰ ] x) (ğ“ c)) â†” hf.order = 0 := by
  rcases eq_or_ne hf.order 0 with ho | ho
  Â· simp [ho, hf.tendsto_ne_zero_of_order_eq_zero ho]
  simp only [ne_eq, ho, iff_false, not_exists, not_and]
  intro c c_ne hc
  rcases ho.lt_or_lt with ho | ho
  Â· apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact hf.tendsto_cobounded_of_order_neg ho
  Â· apply c_ne
    exact tendsto_nhds_unique hc (hf.tendsto_zero_of_order_pos ho)

/-- A meromorphic function converges to zero iff its order is positive. -/
lemma tendsto_zero_iff_order_pos (hf : MeromorphicAt f x) :
    (Tendsto f (ğ“[â‰ ] x) (ğ“ 0)) â†” 0 < hf.order := by
  rcases lt_or_le 0 hf.order with ho | ho
  Â· simp [ho, hf.tendsto_zero_of_order_pos ho]
  simp only [â† not_le, ho, not_true_eq_false, iff_false]
  intro hc
  rcases ho.eq_or_lt with ho | ho
  Â· obtain âŸ¨c, c_ne, h'câŸ© := hf.tendsto_ne_zero_of_order_eq_zero ho
    apply c_ne
    exact tendsto_nhds_unique h'c hc
  Â· apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact hf.tendsto_cobounded_of_order_neg ho

/-- Meromorphic functions that agree in a punctured neighborhood of `zâ‚€` have the same order at
`zâ‚€`. -/
theorem order_congr (hfâ‚ : MeromorphicAt fâ‚ x)
    (hfâ‚â‚‚ : fâ‚ =á¶ [ğ“[â‰ ] x] fâ‚‚) :
    hfâ‚.order = (hfâ‚.congr hfâ‚â‚‚).order := by
  by_cases hâ‚fâ‚ : hfâ‚.order = âŠ¤
  Â· rw [hâ‚fâ‚, eq_comm, (hfâ‚.congr hfâ‚â‚‚).order_eq_top_iff]
    rw [hfâ‚.order_eq_top_iff] at hâ‚fâ‚
    exact EventuallyEq.rw hâ‚fâ‚ (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm
  Â· obtain âŸ¨n, hn : hfâ‚.order = nâŸ© := Option.ne_none_iff_exists'.mp hâ‚fâ‚
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hfâ‚.order_eq_int_iff.1 hn
    rw [hn, eq_comm, (hfâ‚.congr hfâ‚â‚‚).order_eq_int_iff]
    use g, hâ‚g, hâ‚‚g
    exact EventuallyEq.rw hâ‚ƒg (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm

/-- Compatibility of notions of `order` for analytic and meromorphic functions. -/
lemma _root_.AnalyticAt.meromorphicAt_order (hf : AnalyticAt ğ•œ f x) :
    hf.meromorphicAt.order = hf.order.map (â†‘) := by
  rcases eq_or_ne hf.order âŠ¤ with ho | ho
  Â· rw [ho, ENat.map_top, order_eq_top_iff]
    exact (hf.order_eq_top_iff.mp ho).filter_mono nhdsWithin_le_nhds
  Â· obtain âŸ¨n, hnâŸ© := ENat.ne_top_iff_exists.mp ho
    simp_rw [â† hn, ENat.map_coe, order_eq_int_iff, zpow_natCast]
    rcases hf.order_eq_nat_iff.mp hn.symm with âŸ¨g, h1, h2, h3âŸ©
    exact âŸ¨g, h1, h2, h3.filter_mono nhdsWithin_le_nhdsâŸ©

/--
When seen as meromorphic functions, analytic functions have nonnegative order.
-/
theorem _root_.AnalyticAt.meromorphicAt_order_nonneg (hf : AnalyticAt ğ•œ f x) :
    0 â‰¤ hf.meromorphicAt.order := by
  simp [hf.meromorphicAt_order]

/-- If a function is both meromorphic and continuous at a point, then it is analytic there. -/
protected theorem analyticAt {f : ğ•œ â†’ E} {x : ğ•œ} (h : MeromorphicAt f x) (h' : ContinuousAt f x) :
    AnalyticAt ğ•œ f x := by
  cases ho : h.order with
  | top =>
    /- If the order is infinite, then `f` vanishes on a pointed neighborhood of `x`. By continuity,
    it also vanishes at `x`.-/
    have : AnalyticAt ğ•œ (fun _ â†¦ (0 : E)) x := analyticAt_const
    apply this.congr
    rw [â† ContinuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE continuousAt_const h']
    filter_upwards [h.order_eq_top_iff.1 ho] with y hy using by simp [hy]
  | coe n =>
    /- If the order is finite, then the order has to be nonnegative, as otherwise the norm of `f`
    would tend to infinity at `x`. Then the local expression of `f` coming from its meromorphicity
    shows that it coincides with an analytic function close to `x`, except maybe at `x`. By
    continuity of `f`, the two functions also coincide at `x`. -/
    rcases h.order_eq_int_iff.1 ho with âŸ¨g, g_an, gx, hgâŸ©
    have : 0 â‰¤ h.order := by
      apply h.tendsto_nhds_iff_order_nonneg.1
      exact âŸ¨f x, h'.continuousWithinAt.tendstoâŸ©
    lift n to â„• using by simpa [ho] using this
    have A : âˆ€á¶  (z : ğ•œ) in ğ“ x, (z - x) ^ n â€¢ g z = f z := by
      apply (ContinuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE (by fun_prop) h').1
      filter_upwards [hg] with z hz using by simpa using hz.symm
    exact AnalyticAt.congr (by fun_prop) A

/-!
## Order at a Point: Behaviour under Ring Operations

We establish additivity of the order under multiplication and taking powers.
-/

/-- The order is additive when multiplying scalar-valued and vector-valued meromorphic functions. -/
theorem order_smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    (hf.smul hg).order = hf.order + hg.order := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  cases hâ‚‚f : hf.order with
  | top =>
    simp only [top_add, order_eq_top_iff] at hâ‚‚f âŠ¢
    filter_upwards [hâ‚‚f] with z hz using by simp [hz]
  | coe m =>
    cases hâ‚‚g : hg.order with
    | top =>
      simp only [add_top, order_eq_top_iff] at hâ‚‚g âŠ¢
      filter_upwards [hâ‚‚g] with z hz using by simp [hz]
    | coe n => -- Non-trivial case: both functions do not vanish around zâ‚€
      rw [â† WithTop.coe_add, order_eq_int_iff]
      obtain âŸ¨F, hâ‚F, hâ‚‚F, hâ‚ƒFâŸ© := hf.order_eq_int_iff.1 hâ‚‚f
      obtain âŸ¨G, hâ‚G, hâ‚‚G, hâ‚ƒGâŸ© := hg.order_eq_int_iff.1 hâ‚‚g
      use F â€¢ G, hâ‚F.smul hâ‚G, by simp [hâ‚‚F, hâ‚‚G]
      filter_upwards [self_mem_nhdsWithin, hâ‚ƒF, hâ‚ƒG] with a ha hfa hga
      simp [hfa, hga, smul_comm (F a), zpow_addâ‚€ (sub_ne_zero.mpr ha), mul_smul]

/-- The order is additive when multiplying meromorphic functions. -/
theorem order_mul {f g : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    (hf.mul hg).order = hf.order + hg.order :=
  hf.order_smul hg

/-- The order multiplies by `n` when taking a meromorphic function to its `n`th power. -/
theorem order_pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) {n : â„•} :
    (hf.pow n).order = n * hf.order := by
  induction n
  case zero =>
    simp only [pow_zero, CharP.cast_eq_zero, zero_mul]
    rw [â† WithTop.coe_zero, MeromorphicAt.order_eq_int_iff]
    use 1, analyticAt_const
    simp
  case succ n hn =>
    simp only [pow_add, pow_one, (hf.pow n).order_mul hf, hn, Nat.cast_add, Nat.cast_one]
    cases hf.order
    Â· aesop
    Â· norm_cast
      simp only [Nat.cast_add, Nat.cast_one]
      ring

/-- The order multiplies by `n` when taking a meromorphic function to its `n`th power. -/
theorem order_zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) {n : â„¤} :
    (hf.zpow n).order = n * hf.order := by
  -- Trivial case: n = 0
  by_cases hn : n = 0
  Â· simp only [hn, zpow_zero, WithTop.coe_zero, zero_mul]
    rw [â† WithTop.coe_zero, MeromorphicAt.order_eq_int_iff]
    use 1
    simp only [Pi.one_apply, ne_eq, one_ne_zero, not_false_eq_true, zpow_zero, smul_eq_mul, mul_one,
      eventually_true, and_self, and_true]
    apply analyticAt_const
  -- Trivial case: f locally zero
  by_cases h : hf.order = âŠ¤
  Â· simp only [h, ne_eq, WithTop.coe_eq_zero, hn, not_false_eq_true, WithTop.mul_top]
    rw [MeromorphicAt.order_eq_top_iff] at *
    filter_upwards [h]
    intro y hy
    simp [hy, zero_zpow n hn]
  -- General case
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hf.order_ne_top_iff.1 h
  rw [â† WithTop.coe_untopâ‚€_of_ne_top h, â† WithTop.coe_mul, MeromorphicAt.order_eq_int_iff]
  use g ^ n, hâ‚g.zpow hâ‚‚g
  constructor
  Â· simp_all [zpow_eq_zero_iff hn]
  Â· filter_upwards [hâ‚ƒg]
    intro y hy
    rw [Pi.pow_apply, hy, smul_eq_mul, mul_zpow]
    congr 1
    rw [mul_comm, zpow_mul]

/-- The order of the inverse is the negative of the order. -/
theorem order_inv {f : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) :
    hf.inv.order = -hf.order := by
  by_cases hâ‚‚f : hf.order = âŠ¤
  Â· rw [hâ‚‚f, â† LinearOrderedAddCommGroupWithTop.neg_top, neg_neg]
    rw [MeromorphicAt.order_eq_top_iff] at *
    filter_upwards [hâ‚‚f]
    simp
  lift hf.order to â„¤ using hâ‚‚f with a ha
  apply hf.inv.order_eq_int_iff.2
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hf.order_eq_int_iff.1 ha.symm
  use gâ»Â¹, hâ‚g.inv hâ‚‚g, inv_eq_zero.not.2 hâ‚‚g
  rw [eventually_nhdsWithin_iff] at *
  filter_upwards [hâ‚ƒg]
  intro _ hâ‚a hâ‚‚a
  simp only [Pi.inv_apply, hâ‚a hâ‚‚a, smul_eq_mul, mul_inv_rev, zpow_neg]
  ring

/--
The order of a sum is at least the minimum of the orders of the summands.
-/
theorem order_add (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    min hfâ‚.order hfâ‚‚.order â‰¤ (hfâ‚.add hfâ‚‚).order := by
  -- Handle the trivial cases where one of the orders equals âŠ¤
  by_cases hâ‚‚fâ‚ : hfâ‚.order = âŠ¤
  Â· rw [hâ‚‚fâ‚, min_top_left, (hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚.order_eq_top_iff.1 hâ‚‚fâ‚]
    simp
  by_cases hâ‚‚fâ‚‚ : hfâ‚‚.order = âŠ¤
  Â· simp only [hâ‚‚fâ‚‚, le_top, inf_of_le_left]
    rw [(hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚‚.order_eq_top_iff.1 hâ‚‚fâ‚‚]
    simp
  -- General case
  lift hfâ‚.order to â„¤ using hâ‚‚fâ‚ with nâ‚ hnâ‚
  lift hfâ‚‚.order to â„¤ using hâ‚‚fâ‚‚ with nâ‚‚ hnâ‚‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hfâ‚.order_eq_int_iff.1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := hfâ‚‚.order_eq_int_iff.1 hnâ‚‚.symm
  let n := min nâ‚ nâ‚‚
  let g := (fun z â†¦ (z - x) ^ (nâ‚ - n)) â€¢ gâ‚ +  (fun z â†¦ (z - x) ^ (nâ‚‚ - n)) â€¢ gâ‚‚
  have hâ‚g : AnalyticAt ğ•œ g x := by
    apply AnalyticAt.add
    apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (min_le_left nâ‚ nâ‚‚))).smul hâ‚gâ‚
    apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (min_le_right nâ‚ nâ‚‚))).smul hâ‚gâ‚‚
  have : fâ‚ + fâ‚‚ =á¶ [ğ“[â‰ ] x] ((Â· - x) ^ n) â€¢ g := by
    filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin]
    simp_all [g, â† smul_assoc, â† zpow_add', sub_ne_zero]
  have tâ‚€ : MeromorphicAt ((Â·  - x) ^ n) x := by fun_prop
  have tâ‚ : tâ‚€.order = n := tâ‚€.order_eq_int_iff.2 âŸ¨1, analyticAt_const, by simpâŸ©
  rw [(hfâ‚.add hfâ‚‚).order_congr this, tâ‚€.order_smul hâ‚g.meromorphicAt, tâ‚]
  exact le_add_of_nonneg_right hâ‚g.meromorphicAt_order_nonneg

/--
Helper lemma for MeromorphicAt.order_add_of_order_ne.
-/
lemma order_add_of_order_lt_order (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x)
    (h : hfâ‚.order < hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = hfâ‚.order := by
  -- Trivial case: fâ‚‚ vanishes identically around zâ‚€
  by_cases hâ‚fâ‚‚ : hfâ‚‚.order = âŠ¤
  Â· rw [(hfâ‚.add hfâ‚‚).order_congr]
    filter_upwards [hfâ‚‚.order_eq_top_iff.1 hâ‚fâ‚‚]
    simp
  -- General case
  lift hfâ‚‚.order to â„¤ using hâ‚fâ‚‚ with nâ‚‚ hnâ‚‚
  lift hfâ‚.order to â„¤ using h.ne_top with nâ‚ hnâ‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hfâ‚.order_eq_int_iff.1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := hfâ‚‚.order_eq_int_iff.1 hnâ‚‚.symm
  rw [(hfâ‚.add hfâ‚‚).order_eq_int_iff]
  refine âŸ¨gâ‚ + (Â· - x) ^ (nâ‚‚ - nâ‚) â€¢ gâ‚‚, ?_, ?_, ?_âŸ©
  Â· apply hâ‚gâ‚.add (AnalyticAt.smul _ hâ‚gâ‚‚)
    apply AnalyticAt.zpow_nonneg (by fun_prop)
      (sub_nonneg.2 (le_of_lt (WithTop.coe_lt_coe.1 h)))
  Â· simpa [zero_zpow _ <| sub_ne_zero.mpr (WithTop.coe_lt_coe.1 h).ne']
  Â· filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin]
    simp_all [smul_add, â† smul_assoc, â† zpow_add', sub_ne_zero]

/--
If two meromorphic functions have unequal orders, then the order of their sum is
exactly the minimum of the orders of the summands.
-/
theorem order_add_of_order_ne (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x)
    (h : hfâ‚.order â‰  hfâ‚‚.order) :
    (hfâ‚.add hfâ‚‚).order = min hfâ‚.order hfâ‚‚.order := by
  rcases lt_or_lt_iff_ne.mpr h with h | h
  Â· simpa [h.le] using hfâ‚.order_add_of_order_lt_order hfâ‚‚ h
  Â· simpa [h.le, add_comm] using hfâ‚‚.order_add_of_order_lt_order hfâ‚ h

@[deprecated (since := "2025-04-27")]
alias order_add_of_unequal_order := order_add_of_order_ne

end MeromorphicAt

/-!
## Level Sets of the Order Function
-/

namespace MeromorphicOn

variable {U : Set ğ•œ} (hf : MeromorphicOn f U)

/-- The set where a meromorphic function has infinite order is clopen in its domain of meromorphy.
-/
theorem isClopen_setOf_order_eq_top : IsClopen { u : U | (hf u.1 u.2).order = âŠ¤ } := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† (hf z.1 z.2).order_eq_top_iff] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [MeromorphicAt.order_eq_top_iff, not_eventually]
          apply Filter.Eventually.frequently
          rw [eventually_nhdsWithin_iff, eventually_nhds_iff]
          use t' \ {z.1}, fun y hâ‚y hâ‚‚y â†¦ hâ‚t' y hâ‚y.1 hâ‚y.2, hâ‚‚t'.sdiff isClosed_singleton, hw,
            mem_singleton_iff.not.2 (Subtype.coe_ne_coe.mpr hâ‚w)
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [MeromorphicAt.order_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [MeromorphicAt.order_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [mem_compl_iff, mem_singleton_iff, isOpen_induced hâ‚‚t', mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y _ â†¦ hâ‚t' y (mem_of_mem_diff hâ‚y) (mem_of_mem_inter_right hâ‚y)
    constructor
    Â· exact hâ‚‚t'.sdiff isClosed_singleton
    Â· apply (mem_diff w).1
      exact âŸ¨hw, mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_order_ne_top_iff_forall (hU : IsConnected U) :
    (âˆƒ u : U, (hf u u.2).order â‰  âŠ¤) â†” (âˆ€ u : U, (hf u u.2).order â‰  âŠ¤) := by
  constructor
  Â· intro hâ‚‚f
    have := isPreconnected_iff_preconnectedSpace.1 hU.isPreconnected
    rcases isClopen_iff.1 hf.isClopen_setOf_order_eq_top with h | h
    Â· intro u
      have : u âˆ‰ (âˆ… : Set U) := by exact fun a => a
      rw [â† h] at this
      tauto
    Â· obtain âŸ¨u, hUâŸ© := hâ‚‚f
      have : u âˆˆ univ := by trivial
      rw [â† h] at this
      tauto
  Â· intro hâ‚‚f
    obtain âŸ¨v, hvâŸ© := hU.nonempty
    use âŸ¨v, hvâŸ©, hâ‚‚f âŸ¨v, hvâŸ©

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem order_ne_top_of_isPreconnected {y : ğ•œ} (hU : IsPreconnected U) (hâ‚x : x âˆˆ U) (hy : y âˆˆ U)
    (hâ‚‚x : (hf x hâ‚x).order â‰  âŠ¤) :
    (hf y hy).order â‰  âŠ¤ :=
  (hf.exists_order_ne_top_iff_forall âŸ¨nonempty_of_mem hâ‚x, hUâŸ©).1 (by use âŸ¨x, hâ‚xâŸ©) âŸ¨y, hyâŸ©

/-- If a function is meromorphic on a set `U`, then for each point in `U`, it is analytic at nearby
points in `U`. When the target space is complete, this can be strengthened to analyticity at all
nearby points, see `MeromorphicAt.eventually_analyticAt`. -/
theorem eventually_analyticAt {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicOn f U) (hx : x âˆˆ U) : âˆ€á¶  y in ğ“[U \ {x}] x, AnalyticAt ğ•œ f y := by
  /- At neighboring points in `U`, the function `f` is both meromorphic (by meromorphicity on `U`)
  and continuous (thanks to the formula for a meromorphic function around the point `x`), so it is
  analytic. -/
  have : âˆ€á¶  y in ğ“[U \ {x}] x, ContinuousAt f y := by
    have : U \ {x} âŠ† {x}á¶œ := by simp
    exact nhdsWithin_mono _ this (h x hx).eventually_continuousAt
  filter_upwards [this, self_mem_nhdsWithin] with y hy h'y
  exact (h y h'y.1).analyticAt hy

theorem eventually_analyticAt_or_mem_compl {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicOn f U) (hx : x âˆˆ U) : âˆ€á¶  y in ğ“[â‰ ] x, AnalyticAt ğ•œ f y âˆ¨ y âˆˆ Uá¶œ := by
  have : {x}á¶œ = (U \ {x}) âˆª Uá¶œ := by aesop (add simp Classical.em)
  rw [this, nhdsWithin_union]
  simp only [mem_compl_iff, eventually_sup]
  refine âŸ¨?_, ?_âŸ©
  Â· filter_upwards [h.eventually_analyticAt hx] with y hy using Or.inl hy
  Â· filter_upwards [self_mem_nhdsWithin] with y hy using Or.inr hy

/-- Meromorphic functions on `U` are analytic on `U`, outside of a discrete subset. -/
theorem analyticAt_mem_codiscreteWithin (hf : MeromorphicOn f U) :
    { x | AnalyticAt ğ•œ f x } âˆˆ Filter.codiscreteWithin U := by
  rw [mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right, â† Filter.eventually_mem_set]
  filter_upwards [hf.eventually_analyticAt_or_mem_compl hx] with y hy
  simp
  tauto

/-- The set where a meromorphic function has zero or infinite
order is codiscrete within its domain of meromorphicity. -/
theorem codiscrete_setOf_order_eq_zero_or_top :
    {u : U | (hf u u.2).order = 0 âˆ¨ (hf u u.2).order = âŠ¤} âˆˆ Filter.codiscrete U := by
  rw [mem_codiscrete_subtype_iff_mem_codiscreteWithin, mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right]
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_eventually_nhdsWithin.2 hâ‚f] with a hâ‚a
    suffices âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] a, f z = 0 by
      simp +contextual [(hf a _).order_eq_top_iff, hâ‚a, this]
    obtain rfl | hax := eq_or_ne a x
    Â· exact hâ‚a
    rw [eventually_nhdsWithin_iff, eventually_nhds_iff] at hâ‚a âŠ¢
    obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hâ‚a
    use t \ {x}, fun y hâ‚y _ â†¦ hâ‚t y hâ‚y.1 hâ‚y.2
    exact âŸ¨hâ‚‚t.sdiff isClosed_singleton, Set.mem_diff_of_mem hâ‚ƒt haxâŸ©
  Â· filter_upwards [hf.eventually_analyticAt_or_mem_compl hx, hâ‚f] with a hâ‚a h'â‚a
    simp only [mem_compl_iff, mem_diff, mem_image, mem_setOf_eq, Subtype.exists, exists_and_right,
      exists_eq_right, not_exists, not_or, not_and, not_forall, Decidable.not_not]
    rcases hâ‚a with h' | h'
    Â· simp +contextual [h'.meromorphicAt_order, h'.order_eq_zero_iff.2, h'â‚a]
    Â· exact fun ha â†¦ (h' ha).elim

end MeromorphicOn
