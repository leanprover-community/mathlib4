/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler, Stefan Kebekus
-/
module

public import Mathlib.Analysis.Meromorphic.Basic
public import Mathlib.Algebra.Order.WithTop.Untop0

/-!
# Orders of Meromorphic Functions

This file defines the order of a meromorphic function `f` at a point `zâ‚€`, as an element of
`â„¤ âˆª {âˆ}`.

We characterize the order being `< 0`, or `= 0`, or `> 0`, as the convergence of the function
to infinity, resp. a nonzero constant, resp. zero.

## TODO

Uniformize API between analytic and meromorphic functions
-/

@[expose] public section

open Filter Set WithTop.LinearOrderedAddCommGroup
open scoped Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f fâ‚ fâ‚‚ : ğ•œ â†’ E} {x : ğ•œ}

/-!
## Order at a Point: Definition and Characterization
-/

open scoped Classical in
/-- The order of a meromorphic function `f` at `zâ‚€`, as an element of `â„¤ âˆª {âˆ}`.

The order is defined to be `âˆ` if `f` is identically 0 on a neighbourhood of `zâ‚€`, and otherwise the
unique `n` such that `f` can locally be written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic
and does not vanish at `zâ‚€`. See `MeromorphicAt.meromorphicOrderAt_eq_top_iff` and
`MeromorphicAt.meromorphicOrderAt_eq_int_iff` for these equivalences.

If the function is not meromorphic at `x`, we use the junk value `0`. -/
noncomputable def meromorphicOrderAt (f : ğ•œ â†’ E) (x : ğ•œ) : WithTop â„¤ :=
  if hf : MeromorphicAt f x then
    ((analyticOrderAt (fun z â†¦ (z - x) ^ hf.choose â€¢ f z) x).map (â†‘Â· : â„• â†’ â„¤)) - hf.choose
  else 0

@[simp]
lemma meromorphicOrderAt_of_not_meromorphicAt (hf : Â¬ MeromorphicAt f x) :
    meromorphicOrderAt f x = 0 :=
  dif_neg hf

lemma meromorphicAt_of_meromorphicOrderAt_ne_zero (hf : meromorphicOrderAt f x â‰  0) :
    MeromorphicAt f x := by
  contrapose! hf
  simp [hf]

/-- The order of a meromorphic function `f` at a `zâ‚€` is infinity iff `f` vanishes locally around
`zâ‚€`. -/
lemma meromorphicOrderAt_eq_top_iff :
    meromorphicOrderAt f x = âŠ¤ â†” âˆ€á¶  z in ğ“[â‰ ] x, f z = 0 := by
  by_cases hf : MeromorphicAt f x; swap
  Â· simp only [hf, not_false_eq_true, meromorphicOrderAt_of_not_meromorphicAt, WithTop.zero_ne_top,
      false_iff]
    contrapose! hf
    exact (MeromorphicAt.const 0 x).congr (EventuallyEq.symm hf)
  simp only [meromorphicOrderAt, hf, â†“reduceDIte, sub_eq_top_iff, ENat.map_eq_top_iff,
    WithTop.natCast_ne_top, or_false]
  by_cases h : analyticOrderAt (fun z â†¦ (z - x) ^ hf.choose â€¢ f z) x = âŠ¤
  Â· simp only [h, eventually_nhdsWithin_iff, mem_compl_iff, mem_singleton_iff, true_iff]
    rw [analyticOrderAt_eq_top] at h
    filter_upwards [h] with z hf hz
    rwa [smul_eq_zero_iff_right <| pow_ne_zero _ (sub_ne_zero.mpr hz)] at hf
  Â· obtain âŸ¨m, hmâŸ© := ENat.ne_top_iff_exists.mp h
    simp only [â† hm, ENat.coe_ne_top, false_iff]
    contrapose! h
    rw [analyticOrderAt_eq_top]
    rw [â† hf.choose_spec.frequently_eq_iff_eventually_eq analyticAt_const]
    apply Eventually.frequently
    filter_upwards [h] with z hfz
    rw [hfz, smul_zero]

lemma eventuallyConst_nhdsNE_iff_meromorphicOrderAt_sub_eq_top :
    EventuallyConst f (ğ“[â‰ ] x) â†” âˆƒ c, meromorphicOrderAt (f Â· - c) x = âŠ¤ := by
  simp only [eventuallyConst_iff_exists_eventuallyEq, meromorphicOrderAt_eq_top_iff,
    sub_eq_zero, EventuallyEq]

/-- The order of a meromorphic function `f` at `zâ‚€` equals an integer `n` iff `f` can locally be
written as `f z = (z - zâ‚€) ^ n â€¢ g z`, where `g` is analytic and does not vanish at `zâ‚€`. -/
lemma meromorphicOrderAt_eq_int_iff {n : â„¤} (hf : MeromorphicAt f x) : meromorphicOrderAt f x = n â†”
    âˆƒ g : ğ•œ â†’ E, AnalyticAt ğ•œ g x âˆ§ g x â‰  0 âˆ§ âˆ€á¶  z in ğ“[â‰ ] x, f z = (z - x) ^ n â€¢ g z := by
  simp only [meromorphicOrderAt, hf, â†“reduceDIte]
  by_cases h : analyticOrderAt (fun z â†¦ (z - x) ^ hf.choose â€¢ f z) x = âŠ¤
  Â· rw [h, ENat.map_top, â† WithTop.coe_natCast, top_sub,
      eq_false_intro WithTop.top_ne_coe, false_iff]
    rw [analyticOrderAt_eq_top] at h
    refine fun âŸ¨g, hg_an, hg_ne, hg_eqâŸ© â†¦ hg_ne ?_
    apply EventuallyEq.eq_of_nhds
    rw [EventuallyEq, â† AnalyticAt.frequently_eq_iff_eventually_eq hg_an analyticAt_const]
    apply Eventually.frequently
    rw [eventually_nhdsWithin_iff] at hg_eq âŠ¢
    filter_upwards [h, hg_eq] with z hfz hfz_eq hz
    rwa [hfz_eq hz, â† mul_smul, smul_eq_zero_iff_right] at hfz
    exact mul_ne_zero (pow_ne_zero _ (sub_ne_zero.mpr hz)) (zpow_ne_zero _ (sub_ne_zero.mpr hz))
  Â· obtain âŸ¨m, hâŸ© := ENat.ne_top_iff_exists.mp h
    rw [â† h, ENat.map_coe, â† WithTop.coe_natCast, â† coe_sub, WithTop.coe_inj]
    obtain âŸ¨g, hg_an, hg_ne, hg_eqâŸ© := hf.choose_spec.analyticOrderAt_eq_natCast.mp h.symm
    replace hg_eq : âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] x, f z = (z - x) ^ (â†‘m - â†‘hf.choose : â„¤) â€¢ g z := by
      rw [eventually_nhdsWithin_iff]
      filter_upwards [hg_eq] with z hg_eq hz
      rwa [â† smul_right_inj <| zpow_ne_zero _ (sub_ne_zero.mpr hz), â† mul_smul,
        â† zpow_addâ‚€ (sub_ne_zero.mpr hz), â† add_sub_assoc, add_sub_cancel_left, zpow_natCast,
        zpow_natCast]
    exact âŸ¨fun h â†¦ âŸ¨g, hg_an, hg_ne, h â–¸ hg_eqâŸ©,
      AnalyticAt.unique_eventuallyEq_zpow_smul_nonzero âŸ¨g, hg_an, hg_ne, hg_eqâŸ©âŸ©

/--
The order of a meromorphic function `f` at `zâ‚€` is finite iff `f` can locally be
written as `f z = (z - zâ‚€) ^ order â€¢ g z`, where `g` is analytic and does not
vanish at `zâ‚€`.
-/
theorem meromorphicOrderAt_ne_top_iff {f : ğ•œ â†’ E} {zâ‚€ : ğ•œ} (hf : MeromorphicAt f zâ‚€) :
    meromorphicOrderAt f zâ‚€ â‰  âŠ¤ â†” âˆƒ (g : ğ•œ â†’ E), AnalyticAt ğ•œ g zâ‚€ âˆ§ g zâ‚€ â‰  0 âˆ§
      f =á¶ [ğ“[â‰ ] zâ‚€] fun z â†¦ (z - zâ‚€) ^ ((meromorphicOrderAt f zâ‚€).untopâ‚€) â€¢ g z :=
  âŸ¨fun h â†¦ (meromorphicOrderAt_eq_int_iff hf).1 (WithTop.coe_untopâ‚€_of_ne_top h).symm,
    fun h â†¦ Option.ne_none_iff_exists'.2
      âŸ¨(meromorphicOrderAt f zâ‚€).untopD 0, (meromorphicOrderAt_eq_int_iff hf).2 hâŸ©âŸ©

/--
The order of a meromorphic function `f` at `zâ‚€` is finite iff `f` does not have
any zeros in a sufficiently small neighborhood of `zâ‚€`.
-/
theorem meromorphicOrderAt_ne_top_iff_eventually_ne_zero {f : ğ•œ â†’ E} (hf : MeromorphicAt f x) :
    meromorphicOrderAt f x â‰  âŠ¤ â†” âˆ€á¶  x in ğ“[â‰ ] x, f x â‰  0 := by
  constructor
  Â· intro h
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff hf).1 h
    filter_upwards [hâ‚ƒg, self_mem_nhdsWithin, eventually_nhdsWithin_of_eventually_nhds
      ((hâ‚g.continuousAt.ne_iff_eventually_ne continuousAt_const).mp hâ‚‚g)]
    simp_all [zpow_ne_zero, sub_ne_zero]
  Â· simp_all [meromorphicOrderAt_eq_top_iff, Eventually.frequently]

/-- If the order of a meromorphic function is negative, then this function converges to infinity
at this point. See also the iff version `tendsto_cobounded_iff_meromorphicOrderAt_neg`. -/
lemma tendsto_cobounded_of_meromorphicOrderAt_neg (ho : meromorphicOrderAt f x < 0) :
    Tendsto f (ğ“[â‰ ] x) (Bornology.cobounded E) := by
  have hf : MeromorphicAt f x := meromorphicAt_of_meromorphicOrderAt_ne_zero ho.ne
  simp only [â† tendsto_norm_atTop_iff_cobounded]
  obtain âŸ¨m, hmâŸ© := WithTop.ne_top_iff_exists.mp ho.ne_top
  have m_neg : m < 0 := by simpa [â† hm] using ho
  rcases (meromorphicOrderAt_eq_int_iff hf).1 hm.symm with âŸ¨g, g_an, gx, hgâŸ©
  have A : Tendsto (fun z â†¦ â€–(z - x) ^ m â€¢ g zâ€–) (ğ“[â‰ ] x) atTop := by
    simp only [norm_smul]
    apply Filter.Tendsto.atTop_mul_pos (C := â€–g xâ€–) (by simp [gx]) _
      g_an.continuousAt.continuousWithinAt.tendsto.norm
    have : Tendsto (fun z â†¦ z - x) (ğ“[â‰ ] x) (ğ“[â‰ ] 0) := by
      refine tendsto_nhdsWithin_iff.2 âŸ¨?_, ?_âŸ©
      Â· have : ContinuousWithinAt (fun z â†¦ z - x) ({x}á¶œ) x :=
          ContinuousAt.continuousWithinAt (by fun_prop)
        simpa using this.tendsto
      Â· filter_upwards [self_mem_nhdsWithin] with y hy
        simpa [sub_eq_zero] using hy
    exact (tendsto_norm_cobounded_atTop.comp (tendsto_zpow_nhdsNE_zero_cobounded m_neg)).comp this
  apply A.congr'
  filter_upwards [hg] with z hz using by simp [hz]

/-- If the order of a meromorphic function is zero, then this function converges to a nonzero
limit at this point. See also the iff version `tendsto_ne_zero_iff_meromorphicOrderAt_eq_zero`. -/
lemma tendsto_ne_zero_of_meromorphicOrderAt_eq_zero
    (hf : MeromorphicAt f x) (ho : meromorphicOrderAt f x = 0) :
    âˆƒ c â‰  0, Tendsto f (ğ“[â‰ ] x) (ğ“ c) := by
  rcases (meromorphicOrderAt_eq_int_iff hf).1 ho with âŸ¨g, g_an, gx, hgâŸ©
  refine âŸ¨g x, gx, ?_âŸ©
  apply g_an.continuousAt.continuousWithinAt.tendsto.congr'
  filter_upwards [hg] with y hy using by simp [hy]

/-- If the order of a meromorphic function is positive, then this function converges to zero
at this point. See also the iff version `tendsto_zero_iff_meromorphicOrderAt_pos`. -/
lemma tendsto_zero_of_meromorphicOrderAt_pos (ho : 0 < meromorphicOrderAt f x) :
    Tendsto f (ğ“[â‰ ] x) (ğ“ 0) := by
  have hf : MeromorphicAt f x := meromorphicAt_of_meromorphicOrderAt_ne_zero ho.ne'
  cases h'o : meromorphicOrderAt f x with
  | top =>
    apply tendsto_const_nhds.congr'
    filter_upwards [meromorphicOrderAt_eq_top_iff.1 h'o] with y hy using hy.symm
  | coe n =>
    rcases (meromorphicOrderAt_eq_int_iff hf).1 h'o with âŸ¨g, g_an, gx, hgâŸ©
    lift n to â„• using by simpa [h'o] using ho.le
    have : (0 : E) = (x - x) ^ n â€¢ g x := by
      have : 0 < n := by simpa [h'o] using ho
      simp [zero_pow_eq_zero.2 this.ne']
    rw [this]
    have : ContinuousAt (fun z â†¦ (z - x) ^ n â€¢ g z) x := by fun_prop
    apply this.continuousWithinAt.tendsto.congr'
    filter_upwards [hg] with y hy using by simp [hy]

/-- If the order of a meromorphic function is nonnegative, then this function converges
at this point. See also the iff version `tendsto_nhds_iff_meromorphicOrderAt_nonneg`. -/
lemma tendsto_nhds_of_meromorphicOrderAt_nonneg
    (hf : MeromorphicAt f x) (ho : 0 â‰¤ meromorphicOrderAt f x) :
    âˆƒ c, Tendsto f (ğ“[â‰ ] x) (ğ“ c) := by
  rcases ho.eq_or_lt with ho | ho
  Â· rcases tendsto_ne_zero_of_meromorphicOrderAt_eq_zero hf ho.symm with âŸ¨c, -, hcâŸ©
    exact âŸ¨c, hcâŸ©
  Â· exact âŸ¨0, tendsto_zero_of_meromorphicOrderAt_pos hoâŸ©

/-- A meromorphic function converges to infinity iff its order is negative. -/
lemma tendsto_cobounded_iff_meromorphicOrderAt_neg (hf : MeromorphicAt f x) :
    Tendsto f (ğ“[â‰ ] x) (Bornology.cobounded E) â†” meromorphicOrderAt f x < 0 := by
  rcases lt_or_ge (meromorphicOrderAt f x) 0 with ho | ho
  Â· simp [ho, tendsto_cobounded_of_meromorphicOrderAt_neg]
  Â· simp only [lt_iff_not_ge, ho, not_true_eq_false, iff_false, â† tendsto_norm_atTop_iff_cobounded]
    obtain âŸ¨c, hcâŸ© := tendsto_nhds_of_meromorphicOrderAt_nonneg hf ho
    exact not_tendsto_atTop_of_tendsto_nhds hc.norm

/-- A meromorphic function converges to a limit iff its order is nonnegative. -/
lemma tendsto_nhds_iff_meromorphicOrderAt_nonneg (hf : MeromorphicAt f x) :
    (âˆƒ c, Tendsto f (ğ“[â‰ ] x) (ğ“ c)) â†” 0 â‰¤ meromorphicOrderAt f x := by
  rcases lt_or_ge (meromorphicOrderAt f x) 0 with ho | ho
  Â· simp only [â† not_lt, ho, not_true_eq_false, iff_false, not_exists]
    intro c hc
    apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact tendsto_cobounded_of_meromorphicOrderAt_neg ho
  Â· simp [ho, tendsto_nhds_of_meromorphicOrderAt_nonneg hf ho]

/-- A meromorphic function converges to a nonzero limit iff its order is zero. -/
lemma tendsto_ne_zero_iff_meromorphicOrderAt_eq_zero (hf : MeromorphicAt f x) :
    (âˆƒ c â‰  0, Tendsto f (ğ“[â‰ ] x) (ğ“ c)) â†” meromorphicOrderAt f x = 0 := by
  rcases eq_or_ne (meromorphicOrderAt f x) 0 with ho | ho
  Â· simp [ho, tendsto_ne_zero_of_meromorphicOrderAt_eq_zero hf ho]
  simp only [ne_eq, ho, iff_false, not_exists, not_and]
  intro c c_ne hc
  rcases ho.lt_or_gt with ho | ho
  Â· apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact tendsto_cobounded_of_meromorphicOrderAt_neg ho
  Â· apply c_ne
    exact tendsto_nhds_unique hc (tendsto_zero_of_meromorphicOrderAt_pos ho)

/-- A meromorphic function converges to zero iff its order is positive. -/
lemma tendsto_zero_iff_meromorphicOrderAt_pos (hf : MeromorphicAt f x) :
    (Tendsto f (ğ“[â‰ ] x) (ğ“ 0)) â†” 0 < meromorphicOrderAt f x := by
  rcases lt_or_ge 0 (meromorphicOrderAt f x) with ho | ho
  Â· simp [ho, tendsto_zero_of_meromorphicOrderAt_pos ho]
  simp only [â† not_le, ho, not_true_eq_false, iff_false]
  intro hc
  rcases ho.eq_or_lt with ho | ho
  Â· obtain âŸ¨c, c_ne, h'câŸ© := tendsto_ne_zero_of_meromorphicOrderAt_eq_zero hf ho
    apply c_ne
    exact tendsto_nhds_unique h'c hc
  Â· apply not_tendsto_atTop_of_tendsto_nhds hc.norm
    rw [tendsto_norm_atTop_iff_cobounded]
    exact tendsto_cobounded_of_meromorphicOrderAt_neg ho

/-- Meromorphic functions that agree in a punctured neighborhood of `zâ‚€` have the same order at
`zâ‚€`. -/
theorem meromorphicOrderAt_congr (hfâ‚â‚‚ : fâ‚ =á¶ [ğ“[â‰ ] x] fâ‚‚) :
    meromorphicOrderAt fâ‚ x = meromorphicOrderAt fâ‚‚ x := by
  by_cases hfâ‚ : MeromorphicAt fâ‚ x; swap
  Â· have : Â¬ MeromorphicAt fâ‚‚ x := by
      contrapose! hfâ‚
      exact hfâ‚.congr hfâ‚â‚‚.symm
    simp [hfâ‚, this]
  by_cases hâ‚fâ‚ : meromorphicOrderAt fâ‚ x = âŠ¤
  Â· rw [hâ‚fâ‚, eq_comm]
    rw [meromorphicOrderAt_eq_top_iff] at hâ‚fâ‚ âŠ¢
    exact EventuallyEq.rw hâ‚fâ‚ (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm
  Â· obtain âŸ¨n, hn : meromorphicOrderAt fâ‚ x = nâŸ© := Option.ne_none_iff_exists'.mp hâ‚fâ‚
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_eq_int_iff hfâ‚).1 hn
    rw [hn, eq_comm, meromorphicOrderAt_eq_int_iff (hfâ‚.congr hfâ‚â‚‚)]
    use g, hâ‚g, hâ‚‚g
    exact EventuallyEq.rw hâ‚ƒg (fun x => Eq (fâ‚‚ x)) hfâ‚â‚‚.symm

/-- Compatibility of notions of `order` for analytic and meromorphic functions. -/
lemma AnalyticAt.meromorphicOrderAt_eq (hf : AnalyticAt ğ•œ f x) :
    meromorphicOrderAt f x = (analyticOrderAt f x).map (â†‘) := by
  cases hn : analyticOrderAt f x
  Â· rw [ENat.map_top, meromorphicOrderAt_eq_top_iff]
    exact (analyticOrderAt_eq_top.mp hn).filter_mono nhdsWithin_le_nhds
  Â· simp_rw [ENat.map_coe, meromorphicOrderAt_eq_int_iff hf.meromorphicAt, zpow_natCast]
    rcases hf.analyticOrderAt_eq_natCast.mp hn with âŸ¨g, h1, h2, h3âŸ©
    exact âŸ¨g, h1, h2, h3.filter_mono nhdsWithin_le_nhdsâŸ©

/--
When seen as meromorphic functions, analytic functions have nonnegative order.
-/
theorem AnalyticAt.meromorphicOrderAt_nonneg (hf : AnalyticAt ğ•œ f x) :
    0 â‰¤ meromorphicOrderAt f x := by
  simp [hf.meromorphicOrderAt_eq]

/-- If a function is both meromorphic and continuous at a point, then it is analytic there. -/
protected theorem MeromorphicAt.analyticAt {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : MeromorphicAt f x) (h' : ContinuousAt f x) :
    AnalyticAt ğ•œ f x := by
  cases ho : meromorphicOrderAt f x with
  | top =>
    /- If the order is infinite, then `f` vanishes on a pointed neighborhood of `x`. By continuity,
    it also vanishes at `x`.-/
    have : AnalyticAt ğ•œ (fun _ â†¦ (0 : E)) x := analyticAt_const
    apply this.congr
    rw [â† ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE continuousAt_const h']
    filter_upwards [meromorphicOrderAt_eq_top_iff.1 ho] with y hy using by simp [hy]
  | coe n =>
    /- If the order is finite, then the order has to be nonnegative, as otherwise the norm of `f`
    would tend to infinity at `x`. Then the local expression of `f` coming from its meromorphicity
    shows that it coincides with an analytic function close to `x`, except maybe at `x`. By
    continuity of `f`, the two functions also coincide at `x`. -/
    rcases (meromorphicOrderAt_eq_int_iff h).1 ho with âŸ¨g, g_an, gx, hgâŸ©
    have : 0 â‰¤ meromorphicOrderAt f x := by
      apply (tendsto_nhds_iff_meromorphicOrderAt_nonneg h).1
      exact âŸ¨f x, h'.continuousWithinAt.tendstoâŸ©
    lift n to â„• using by simpa [ho] using this
    have A : âˆ€á¶  (z : ğ•œ) in ğ“ x, (z - x) ^ n â€¢ g z = f z := by
      apply (ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE (by fun_prop) h').1
      filter_upwards [hg] with z hz using by simpa using hz.symm
    exact AnalyticAt.congr (by fun_prop) A

lemma AnalyticAt.of_meromorphicOrderAt_pos {f : ğ•œ â†’ E} {x : ğ•œ}
    (h : 0 < meromorphicOrderAt f x) (hf : f x = 0) :
    AnalyticAt ğ•œ f x := by
  refine (meromorphicAt_of_meromorphicOrderAt_ne_zero h.ne').analyticAt ?_
  rw [continuousAt_iff_punctured_nhds, hf]
  exact tendsto_zero_of_meromorphicOrderAt_pos h

/--
The order of a constant function is `âŠ¤` if the constant is zero and `0` otherwise.
-/
theorem meromorphicOrderAt_const (zâ‚€ : ğ•œ) (e : E) [Decidable (e = 0)] :
    meromorphicOrderAt (fun _ â†¦ e) zâ‚€ = if e = 0 then âŠ¤ else (0 : WithTop â„¤) := by
  split_ifs with he
  Â· simp [he, meromorphicOrderAt_eq_top_iff]
  Â· exact (meromorphicOrderAt_eq_int_iff (.const e zâ‚€)).2 âŸ¨fun _ â†¦ e, by fun_prop, by simpaâŸ©

@[simp]
lemma meromorphicOrderAt_id : meromorphicOrderAt (ğ•œ := ğ•œ) id 0 = 1 := by
  simp [analyticAt_id.meromorphicOrderAt_eq]

/--
The order of a constant function is `âŠ¤` if the constant is zero and `0` otherwise.
-/
theorem meromorphicOrderAt_const_intCast (zâ‚€ : ğ•œ) (n : â„¤) [Decidable ((n : ğ•œ) = 0)] :
    meromorphicOrderAt (n : ğ•œ â†’ ğ•œ) zâ‚€ = if (n : ğ•œ) = 0 then âŠ¤ else (0 : WithTop â„¤) :=
  meromorphicOrderAt_const zâ‚€ (n : ğ•œ)

/--
The order of a constant function is `âŠ¤` if the constant is zero and `0` otherwise.
-/
theorem meromorphicOrderAt_const_natCast (zâ‚€ : ğ•œ) (n : â„•) [Decidable ((n : ğ•œ) = 0)] :
    meromorphicOrderAt (n : ğ•œ â†’ ğ•œ) zâ‚€ = if (n : ğ•œ) = 0 then âŠ¤ else (0 : WithTop â„¤) :=
  meromorphicOrderAt_const zâ‚€ (n : ğ•œ)

/--
The order of a constant function is `âŠ¤` if the constant is zero and `0` otherwise.
-/
@[simp] theorem meromorphicOrderAt_const_ofNat (zâ‚€ : ğ•œ) (n : â„•) [Decidable ((n : ğ•œ) = 0)] :
    meromorphicOrderAt (ofNat(n) : ğ•œ â†’ ğ•œ) zâ‚€ = if (n : ğ•œ) = 0 then âŠ¤ else (0 : WithTop â„¤) := by
  convert meromorphicOrderAt_const zâ‚€ (n : ğ•œ)
  simp [Semiring.toGrindSemiring_ofNat ğ•œ n]

/-!
## Order at a Point: Behaviour under Ring Operations

We establish additivity of the order under multiplication and taking powers.
-/

/-- The order is additive when multiplying scalar-valued and vector-valued meromorphic functions. -/
theorem meromorphicOrderAt_smul {f : ğ•œ â†’ ğ•œ} {g : ğ•œ â†’ E}
    (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    meromorphicOrderAt (f â€¢ g) x = meromorphicOrderAt f x + meromorphicOrderAt g x := by
  -- Trivial cases: one of the functions vanishes around zâ‚€
  cases hâ‚‚f : meromorphicOrderAt f x with
  | top =>
    simp only [top_add, meromorphicOrderAt_eq_top_iff] at hâ‚‚f âŠ¢
    filter_upwards [hâ‚‚f] with z hz using by simp [hz]
  | coe m =>
    cases hâ‚‚g : meromorphicOrderAt g x with
    | top =>
      simp only [add_top, meromorphicOrderAt_eq_top_iff] at hâ‚‚g âŠ¢
      filter_upwards [hâ‚‚g] with z hz using by simp [hz]
    | coe n => -- Non-trivial case: both functions do not vanish around zâ‚€
      rw [â† WithTop.coe_add, meromorphicOrderAt_eq_int_iff (hf.smul hg)]
      obtain âŸ¨F, hâ‚F, hâ‚‚F, hâ‚ƒFâŸ© := (meromorphicOrderAt_eq_int_iff hf).1 hâ‚‚f
      obtain âŸ¨G, hâ‚G, hâ‚‚G, hâ‚ƒGâŸ© := (meromorphicOrderAt_eq_int_iff hg).1 hâ‚‚g
      use F â€¢ G, hâ‚F.smul hâ‚G, by simp [hâ‚‚F, hâ‚‚G]
      filter_upwards [self_mem_nhdsWithin, hâ‚ƒF, hâ‚ƒG] with a ha hfa hga
      simp [hfa, hga, smul_comm (F a), zpow_addâ‚€ (sub_ne_zero.mpr ha), mul_smul]

/-- The order is additive when multiplying meromorphic functions. -/
theorem meromorphicOrderAt_mul {f g : ğ•œ â†’ ğ•œ} (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    meromorphicOrderAt (f * g) x = meromorphicOrderAt f x + meromorphicOrderAt g x :=
  meromorphicOrderAt_smul hf hg

/-- The order multiplies by `n` when taking a meromorphic function to its `n`th power. -/
theorem meromorphicOrderAt_pow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) {n : â„•} :
    meromorphicOrderAt (f ^ n) x = n * meromorphicOrderAt f x := by
  induction n
  case zero =>
    simp only [pow_zero, CharP.cast_eq_zero, zero_mul]
    rw [â† WithTop.coe_zero, meromorphicOrderAt_eq_int_iff]
    Â· exact âŸ¨1, analyticAt_const, by simpâŸ©
    Â· apply MeromorphicAt.const
  case succ n hn =>
    simp only [pow_add, pow_one, meromorphicOrderAt_mul (hf.pow n) hf, hn, Nat.cast_add,
      Nat.cast_one]
    cases meromorphicOrderAt f x
    Â· aesop
    Â· norm_cast
      simp only [Nat.cast_add, Nat.cast_one]
      ring

/-- The order multiplies by `n` when taking a meromorphic function to its `n`th power. -/
theorem meromorphicOrderAt_zpow {f : ğ•œ â†’ ğ•œ} {x : ğ•œ} (hf : MeromorphicAt f x) {n : â„¤} :
    meromorphicOrderAt (f ^ n) x = n * meromorphicOrderAt f x := by
  -- Trivial case: n = 0
  by_cases hn : n = 0
  Â· simp only [hn, zpow_zero, WithTop.coe_zero, zero_mul]
    rw [â† WithTop.coe_zero, meromorphicOrderAt_eq_int_iff]
    Â· exact âŸ¨1, analyticAt_const, by simpâŸ©
    Â· apply MeromorphicAt.const
  -- Trivial case: f locally zero
  by_cases h : meromorphicOrderAt f x = âŠ¤
  Â· simp only [h, ne_eq, WithTop.coe_eq_zero, hn, not_false_eq_true, WithTop.mul_top]
    rw [meromorphicOrderAt_eq_top_iff] at *
    filter_upwards [h]
    intro y hy
    simp [hy, zero_zpow n hn]
  -- General case
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff hf).1 h
  rw [â† WithTop.coe_untopâ‚€_of_ne_top h, â† WithTop.coe_mul,
    meromorphicOrderAt_eq_int_iff (hf.zpow n)]
  use g ^ n, hâ‚g.zpow hâ‚‚g
  constructor
  Â· simp_all [zpow_eq_zero_iff hn]
  Â· filter_upwards [hâ‚ƒg]
    intro y hy
    rw [Pi.pow_apply, hy, smul_eq_mul, mul_zpow]
    congr 1
    rw [mul_comm, zpow_mul]

/-- The order of the inverse is the negative of the order. -/
theorem meromorphicOrderAt_inv {f : ğ•œ â†’ ğ•œ} :
    meromorphicOrderAt (fâ»Â¹) x = -meromorphicOrderAt f x := by
  by_cases hf : MeromorphicAt f x; swap
  Â· have : Â¬ MeromorphicAt (fâ»Â¹) x := by
      contrapose! hf
      simpa using hf.inv
    simp [hf, this]
  by_cases hâ‚‚f : meromorphicOrderAt f x = âŠ¤
  Â· rw [hâ‚‚f, â† LinearOrderedAddCommGroupWithTop.neg_top, neg_neg]
    rw [meromorphicOrderAt_eq_top_iff] at *
    filter_upwards [hâ‚‚f]
    simp
  lift meromorphicOrderAt f x to â„¤ using hâ‚‚f with a ha
  apply (meromorphicOrderAt_eq_int_iff hf.inv).2
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_eq_int_iff hf).1 ha.symm
  use gâ»Â¹, hâ‚g.inv hâ‚‚g, inv_eq_zero.not.2 hâ‚‚g
  rw [eventually_nhdsWithin_iff] at *
  filter_upwards [hâ‚ƒg]
  intro _ hâ‚a hâ‚‚a
  simp only [Pi.inv_apply, hâ‚a hâ‚‚a, smul_eq_mul, mul_inv_rev, zpow_neg]
  ring

/--
Adding a locally vanishing function does not change the order.
-/
@[simp]
theorem meromorphicOrderAt_add_of_top_left
    {fâ‚ fâ‚‚ : ğ•œ â†’ E} {x : ğ•œ} (hfâ‚ : meromorphicOrderAt fâ‚ x = âŠ¤) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = meromorphicOrderAt fâ‚‚ x := by
  rw [meromorphicOrderAt_congr]
  filter_upwards [meromorphicOrderAt_eq_top_iff.1 hfâ‚] with z hz
  simp_all

/--
Adding a locally vanishing function does not change the order.
-/
@[simp]
theorem meromorphicOrderAt_add_of_top_right
    {fâ‚ fâ‚‚ : ğ•œ â†’ E} {x : ğ•œ} (hfâ‚‚ : meromorphicOrderAt fâ‚‚ x = âŠ¤) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = meromorphicOrderAt fâ‚ x := by
  rw [add_comm, meromorphicOrderAt_add_of_top_left hfâ‚‚]

/--
The order of a sum is at least the minimum of the orders of the summands.
-/
theorem meromorphicOrderAt_add (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    min (meromorphicOrderAt fâ‚ x) (meromorphicOrderAt fâ‚‚ x) â‰¤ meromorphicOrderAt (fâ‚ + fâ‚‚) x := by
  -- Handle the trivial cases where one of the orders equals âŠ¤
  by_cases hâ‚‚fâ‚ : meromorphicOrderAt fâ‚ x = âŠ¤
  Â· rw [hâ‚‚fâ‚, min_top_left, meromorphicOrderAt_congr]
    filter_upwards [meromorphicOrderAt_eq_top_iff.1 hâ‚‚fâ‚]
    simp
  by_cases hâ‚‚fâ‚‚ : meromorphicOrderAt fâ‚‚ x = âŠ¤
  Â· simp only [hâ‚‚fâ‚‚, le_top, inf_of_le_left]
    rw [meromorphicOrderAt_congr]
    filter_upwards [meromorphicOrderAt_eq_top_iff.1 hâ‚‚fâ‚‚]
    simp
  -- General case
  lift meromorphicOrderAt fâ‚ x to â„¤ using hâ‚‚fâ‚ with nâ‚ hnâ‚
  lift meromorphicOrderAt fâ‚‚ x to â„¤ using hâ‚‚fâ‚‚ with nâ‚‚ hnâ‚‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := (meromorphicOrderAt_eq_int_iff hfâ‚).1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := (meromorphicOrderAt_eq_int_iff hfâ‚‚).1 hnâ‚‚.symm
  let n := min nâ‚ nâ‚‚
  let g := (fun z â†¦ (z - x) ^ (nâ‚ - n)) â€¢ gâ‚ + (fun z â†¦ (z - x) ^ (nâ‚‚ - n)) â€¢ gâ‚‚
  have hâ‚g : AnalyticAt ğ•œ g x := by
    apply AnalyticAt.add
    Â· apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (min_le_left nâ‚ nâ‚‚))).smul hâ‚gâ‚
    apply (AnalyticAt.zpow_nonneg (by fun_prop) (sub_nonneg.2 (min_le_right nâ‚ nâ‚‚))).smul hâ‚gâ‚‚
  have : fâ‚ + fâ‚‚ =á¶ [ğ“[â‰ ] x] ((Â· - x) ^ n) â€¢ g := by
    filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin]
    simp_all [g, â† smul_assoc, â† zpow_add', sub_ne_zero]
  have tâ‚€ : MeromorphicAt ((Â· - x) ^ n) x := by fun_prop
  have tâ‚ : meromorphicOrderAt ((Â· - x) ^ n) x = n :=
    (meromorphicOrderAt_eq_int_iff tâ‚€).2 âŸ¨1, analyticAt_const, by simpâŸ©
  rw [meromorphicOrderAt_congr this, meromorphicOrderAt_smul tâ‚€ hâ‚g.meromorphicAt, tâ‚]
  exact le_add_of_nonneg_right hâ‚g.meromorphicOrderAt_nonneg

/--
Helper lemma for meromorphicOrderAt_add_of_ne.
-/
lemma meromorphicOrderAt_add_eq_left_of_lt (hfâ‚‚ : MeromorphicAt fâ‚‚ x)
    (h : meromorphicOrderAt fâ‚ x < meromorphicOrderAt fâ‚‚ x) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = meromorphicOrderAt fâ‚ x := by
  by_cases hfâ‚ : MeromorphicAt fâ‚ x; swap
  Â· have : Â¬ (MeromorphicAt (fâ‚ + fâ‚‚) x) := by
      contrapose! hfâ‚
      simpa using hfâ‚.sub hfâ‚‚
    simp [this, hfâ‚]
  -- Trivial case: fâ‚‚ vanishes identically around zâ‚€
  by_cases hâ‚fâ‚‚ : meromorphicOrderAt fâ‚‚ x = âŠ¤
  Â· rw [meromorphicOrderAt_congr]
    filter_upwards [meromorphicOrderAt_eq_top_iff.1 hâ‚fâ‚‚]
    simp
  -- General case
  lift meromorphicOrderAt fâ‚‚ x to â„¤ using hâ‚fâ‚‚ with nâ‚‚ hnâ‚‚
  lift meromorphicOrderAt fâ‚ x to â„¤ using h.ne_top with nâ‚ hnâ‚
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := (meromorphicOrderAt_eq_int_iff hfâ‚).1 hnâ‚.symm
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := (meromorphicOrderAt_eq_int_iff hfâ‚‚).1 hnâ‚‚.symm
  rw [meromorphicOrderAt_eq_int_iff (hfâ‚.add hfâ‚‚)]
  refine âŸ¨gâ‚ + (Â· - x) ^ (nâ‚‚ - nâ‚) â€¢ gâ‚‚, ?_, ?_, ?_âŸ©
  Â· apply hâ‚gâ‚.add (AnalyticAt.smul _ hâ‚gâ‚‚)
    apply AnalyticAt.zpow_nonneg (by fun_prop)
      (sub_nonneg.2 (le_of_lt (WithTop.coe_lt_coe.1 h)))
  Â· simpa [zero_zpow _ <| sub_ne_zero.mpr (WithTop.coe_lt_coe.1 h).ne']
  Â· filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin]
    simp_all [smul_add, â† smul_assoc, â† zpow_add', sub_ne_zero]

/--
Helper lemma for meromorphicOrderAt_add_of_ne.
-/
lemma meromorphicOrderAt_add_eq_right_of_lt (hfâ‚ : MeromorphicAt fâ‚ x)
    (h : meromorphicOrderAt fâ‚‚ x < meromorphicOrderAt fâ‚ x) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = meromorphicOrderAt fâ‚‚ x := by
  rw [add_comm]
  exact meromorphicOrderAt_add_eq_left_of_lt hfâ‚ h

/--
If two meromorphic functions have unequal orders, then the order of their sum is
exactly the minimum of the orders of the summands.
-/
theorem meromorphicOrderAt_add_of_ne
    (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x)
    (h : meromorphicOrderAt fâ‚ x â‰  meromorphicOrderAt fâ‚‚ x) :
    meromorphicOrderAt (fâ‚ + fâ‚‚) x = min (meromorphicOrderAt fâ‚ x) (meromorphicOrderAt fâ‚‚ x) := by
  rcases lt_or_lt_iff_ne.mpr h with h | h
  Â· simpa [h.le] using meromorphicOrderAt_add_eq_left_of_lt hfâ‚‚ h
  Â· simpa [h.le] using meromorphicOrderAt_add_eq_right_of_lt hfâ‚ h

/-!
## Level Sets of the Order Function
-/

namespace MeromorphicOn

variable {U : Set ğ•œ}

/-- The set where a meromorphic function has infinite order is clopen in its domain of meromorphy.
-/
theorem isClopen_setOf_meromorphicOrderAt_eq_top (hf : MeromorphicOn f U) :
    IsClopen { u : U | meromorphicOrderAt f u = âŠ¤ } := by
  constructor
  Â· rw [â† isOpen_compl_iff, isOpen_iff_forall_mem_open]
    intro z hz
    rcases (hf z.1 z.2).eventually_eq_zero_or_eventually_ne_zero with h | h
    Â· -- Case: f is locally zero in a punctured neighborhood of z
      rw [â† meromorphicOrderAt_eq_top_iff] at h
      tauto
    Â· -- Case: f is locally nonzero in a punctured neighborhood of z
      obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := eventually_nhds_iff.1 (eventually_nhdsWithin_iff.1 h)
      use Subtype.val â»Â¹' t'
      constructor
      Â· intro w hw
        simp only [mem_compl_iff, mem_setOf_eq]
        by_cases hâ‚w : w = z
        Â· rwa [hâ‚w]
        Â· rw [meromorphicOrderAt_eq_top_iff, not_eventually]
          apply Filter.Eventually.frequently
          rw [eventually_nhdsWithin_iff, eventually_nhds_iff]
          use t' \ {z.1}, fun y hâ‚y hâ‚‚y â†¦ hâ‚t' y hâ‚y.1 hâ‚y.2, hâ‚‚t'.sdiff isClosed_singleton, hw,
            mem_singleton_iff.not.2 (Subtype.coe_ne_coe.mpr hâ‚w)
      Â· exact âŸ¨isOpen_induced hâ‚‚t', hâ‚ƒt'âŸ©
  Â· apply isOpen_iff_forall_mem_open.mpr
    intro z hz
    conv =>
      arg 1; intro; left; right; arg 1; intro
      rw [meromorphicOrderAt_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff]
    simp only [mem_setOf_eq] at hz
    rw [meromorphicOrderAt_eq_top_iff, eventually_nhdsWithin_iff, eventually_nhds_iff] at hz
    obtain âŸ¨t', hâ‚t', hâ‚‚t', hâ‚ƒt'âŸ© := hz
    use Subtype.val â»Â¹' t'
    simp only [mem_compl_iff, mem_singleton_iff, isOpen_induced hâ‚‚t', mem_preimage,
      hâ‚ƒt', and_self, and_true]
    intro w hw
    simp only [mem_setOf_eq]
    -- Trivial case: w = z
    by_cases hâ‚w : w = z
    Â· rw [hâ‚w]
      tauto
    -- Nontrivial case: w â‰  z
    use t' \ {z.1}, fun y hâ‚y _ â†¦ hâ‚t' y (mem_of_mem_diff hâ‚y) (mem_of_mem_inter_right hâ‚y)
    constructor
    Â· exact hâ‚‚t'.sdiff isClosed_singleton
    Â· apply (mem_diff w).1
      exact âŸ¨hw, mem_singleton_iff.not.1 (Subtype.coe_ne_coe.2 hâ‚w)âŸ©

/-- On a connected set, there exists a point where a meromorphic function `f` has finite order iff
`f` has finite order at every point. -/
theorem exists_meromorphicOrderAt_ne_top_iff_forall (hf : MeromorphicOn f U) (hU : IsConnected U) :
    (âˆƒ u : U, meromorphicOrderAt f u â‰  âŠ¤) â†” (âˆ€ u : U, meromorphicOrderAt f u â‰  âŠ¤) := by
  constructor
  Â· intro hâ‚‚f
    have := isPreconnected_iff_preconnectedSpace.1 hU.isPreconnected
    rcases isClopen_iff.1 hf.isClopen_setOf_meromorphicOrderAt_eq_top with h | h
    Â· intro u
      have : u âˆ‰ (âˆ… : Set U) := by exact fun a => a
      rw [â† h] at this
      tauto
    Â· obtain âŸ¨u, hUâŸ© := hâ‚‚f
      have : u âˆˆ univ := by trivial
      rw [â† h] at this
      tauto
  Â· intro hâ‚‚f
    obtain âŸ¨v, hvâŸ© := hU.nonempty
    use âŸ¨v, hvâŸ©, hâ‚‚f âŸ¨v, hvâŸ©

/-- On a preconnected set, a meromorphic function has finite order at one point if it has finite
order at another point. -/
theorem meromorphicOrderAt_ne_top_of_isPreconnected (hf : MeromorphicOn f U) {y : ğ•œ}
    (hU : IsPreconnected U) (hâ‚x : x âˆˆ U) (hy : y âˆˆ U) (hâ‚‚x : meromorphicOrderAt f x â‰  âŠ¤) :
    meromorphicOrderAt f y â‰  âŠ¤ :=
  (hf.exists_meromorphicOrderAt_ne_top_iff_forall âŸ¨nonempty_of_mem hâ‚x, hUâŸ©).1
    (by use âŸ¨x, hâ‚xâŸ©) âŸ¨y, hyâŸ©

/-- If a function is meromorphic on a set `U`, then for each point in `U`, it is analytic at nearby
points in `U`. When the target space is complete, this can be strengthened to analyticity at all
nearby points, see `MeromorphicAt.eventually_analyticAt`. -/
theorem eventually_analyticAt (h : MeromorphicOn f U) (hx : x âˆˆ U) :
    âˆ€á¶  y in ğ“[U \ {x}] x, AnalyticAt ğ•œ f y := by
  /- At neighboring points in `U`, the function `f` is both meromorphic (by meromorphicity on `U`)
  and continuous (thanks to the formula for a meromorphic function around the point `x`), so it is
  analytic. -/
  have : âˆ€á¶  y in ğ“[U \ {x}] x, ContinuousAt f y := by
    have : U \ {x} âŠ† {x}á¶œ := by simp
    exact nhdsWithin_mono _ this (h x hx).eventually_continuousAt
  filter_upwards [this, self_mem_nhdsWithin] with y hy h'y
  exact (h y h'y.1).analyticAt hy

theorem eventually_analyticAt_or_mem_compl (h : MeromorphicOn f U) (hx : x âˆˆ U) :
    âˆ€á¶  y in ğ“[â‰ ] x, AnalyticAt ğ•œ f y âˆ¨ y âˆˆ Uá¶œ := by
  have : {x}á¶œ = (U \ {x}) âˆª Uá¶œ := by aesop (add simp Classical.em)
  rw [this, nhdsWithin_union]
  simp only [mem_compl_iff, eventually_sup]
  refine âŸ¨?_, ?_âŸ©
  Â· filter_upwards [h.eventually_analyticAt hx] with y hy using Or.inl hy
  Â· filter_upwards [self_mem_nhdsWithin] with y hy using Or.inr hy

/-- Meromorphic functions on `U` are analytic on `U`, outside of a discrete subset. -/
theorem analyticAt_mem_codiscreteWithin (hf : MeromorphicOn f U) :
    { x | AnalyticAt ğ•œ f x } âˆˆ Filter.codiscreteWithin U := by
  rw [mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right, â† Filter.eventually_mem_set]
  filter_upwards [hf.eventually_analyticAt_or_mem_compl hx] with y hy
  simp
  tauto

/-- The set where a meromorphic function has zero or infinite
order is codiscrete within its domain of meromorphicity. -/
theorem codiscrete_setOf_meromorphicOrderAt_eq_zero_or_top (hf : MeromorphicOn f U) :
    {u : U | meromorphicOrderAt f u = 0 âˆ¨ meromorphicOrderAt f u = âŠ¤} âˆˆ Filter.codiscrete U := by
  rw [mem_codiscrete_subtype_iff_mem_codiscreteWithin, mem_codiscreteWithin]
  intro x hx
  rw [Filter.disjoint_principal_right]
  rcases (hf x hx).eventually_eq_zero_or_eventually_ne_zero with hâ‚f | hâ‚f
  Â· filter_upwards [eventually_eventually_nhdsWithin.2 hâ‚f] with a hâ‚a
    suffices âˆ€á¶  (z : ğ•œ) in ğ“[â‰ ] a, f z = 0 by
      simp +contextual [meromorphicOrderAt_eq_top_iff, this]
    obtain rfl | hax := eq_or_ne a x
    Â· exact hâ‚a
    rw [eventually_nhdsWithin_iff, eventually_nhds_iff] at hâ‚a âŠ¢
    obtain âŸ¨t, hâ‚t, hâ‚‚t, hâ‚ƒtâŸ© := hâ‚a
    use t \ {x}, fun y hâ‚y _ â†¦ hâ‚t y hâ‚y.1 hâ‚y.2
    exact âŸ¨hâ‚‚t.sdiff isClosed_singleton, Set.mem_diff_of_mem hâ‚ƒt haxâŸ©
  Â· filter_upwards [hf.eventually_analyticAt_or_mem_compl hx, hâ‚f] with a hâ‚a h'â‚a
    simp only [mem_compl_iff, mem_diff, mem_image, mem_setOf_eq, Subtype.exists, exists_and_right,
      exists_eq_right, not_exists, not_or, not_and, not_forall, Decidable.not_not]
    rcases hâ‚a with h' | h'
    Â· simp +contextual [h'.meromorphicOrderAt_eq, h'.analyticOrderAt_eq_zero.2, h'â‚a]
    Â· exact fun ha â†¦ (h' ha).elim

end MeromorphicOn

section comp
/-!
## Order at a Point: Behaviour under Composition
-/
variable {x : ğ•œ} {f : ğ•œ â†’ E} {g : ğ•œ â†’ ğ•œ}

/-- If `g` is analytic at `x`, `f` is meromorphic at `g x`, and `g` is not locally constant near
`x`, the order of `f âˆ˜ g` is the product of the orders of `f` and `g Â· - g x`. -/
lemma MeromorphicAt.meromorphicOrderAt_comp (hf : MeromorphicAt f (g x)) (hg : AnalyticAt ğ•œ g x)
    (hg_nc : Â¬EventuallyConst g (ğ“ x)) :
    meromorphicOrderAt (f âˆ˜ g) x =
      (meromorphicOrderAt f (g x)) * (analyticOrderAt (g Â· - g x) x).map Nat.cast := by
  -- First deal with the silly case that `f` is identically zero around `g x`.
  rcases eq_or_ne (meromorphicOrderAt f (g x)) âŠ¤ with hf' | hf'
  Â· rw [hf', WithTop.top_mul]
    Â· rw [meromorphicOrderAt_eq_top_iff] at hf' âŠ¢
      rw [Function.comp_def, â† eventually_map (P := (f Â· = 0))]
      exact EventuallyEq.filter_mono hf' (hg.map_nhdsNE hg_nc)
    Â· simp [(show AnalyticAt ğ•œ (g Â· - g x) x by fun_prop).analyticOrderAt_eq_zero]
  -- Now the interesting case. First unpack the data
  have hr := (WithTop.coe_untopâ‚€_of_ne_top hf').symm
  rw [meromorphicOrderAt_ne_top_iff hf] at hf'
  set r := (meromorphicOrderAt f (g x)).untopâ‚€
  rw [hr]
  -- Now write `f = (Â· - g x) ^ r â€¢ F` for `F` analytic and nonzero at `g x`
  obtain âŸ¨F, hFan, hFne, hFevâŸ© := hf'
  have aux1 : f âˆ˜ g =á¶ [ğ“[â‰ ] x] (g Â· - g x) ^ r â€¢ (F âˆ˜ g) := hFev.comp_tendsto (hg.map_nhdsNE hg_nc)
  have aux2 : meromorphicOrderAt (F âˆ˜ g) x = 0 := by
    rw [AnalyticAt.meromorphicOrderAt_eq (by fun_prop),
      analyticOrderAt_eq_zero.mpr (by exact .inr hFne), ENat.map_zero, CharP.cast_eq_zero,
      WithTop.coe_zero]
  rw [meromorphicOrderAt_congr aux1, meromorphicOrderAt_smul ?_ (AnalyticAt.meromorphicAt ?_),
    aux2, add_zero, meromorphicOrderAt_zpow, AnalyticAt.meromorphicOrderAt_eq] <;>
  fun_prop

/-- If `g` is analytic at `x`, and `g' x â‰  0`, then the meromorphic order of
`f âˆ˜ g` at `x` is the meromorphic order of `f` at `g x` (even if `f` is not meromorphic). -/
lemma meromorphicOrderAt_comp_of_deriv_ne_zero (hg : AnalyticAt ğ•œ g x) (hg' : deriv g x â‰  0)
    [CompleteSpace ğ•œ] [CharZero ğ•œ] :
    meromorphicOrderAt (f âˆ˜ g) x = meromorphicOrderAt f (g x) := by
  by_cases hf : MeromorphicAt f (g x)
  Â· have hgo : analyticOrderAt _ x = 1 := hg.analyticOrderAt_sub_eq_one_of_deriv_ne_zero hg'
    rw [hf.meromorphicOrderAt_comp hg, hgo] <;>
    simp [eventuallyConst_iff_analyticOrderAt_sub_eq_top, hgo]
  Â· rw [meromorphicOrderAt_of_not_meromorphicAt hf, meromorphicOrderAt_of_not_meromorphicAt]
    rwa [meromorphicAt_comp_iff_of_deriv_ne_zero hg hg']

end comp

section smul

variable {g : ğ•œ â†’ ğ•œ}

lemma meromorphicOrderAt_smul_of_ne_zero (hg : AnalyticAt ğ•œ g x) (hg' : g x â‰  0) :
    meromorphicOrderAt (g â€¢ f) x = meromorphicOrderAt f x := by
  by_cases hf : MeromorphicAt f x
  Â· simp [meromorphicOrderAt_smul hg.meromorphicAt hf, hg.meromorphicOrderAt_eq,
      hg.analyticOrderAt_eq_zero.mpr hg']
  Â· rw [meromorphicOrderAt_of_not_meromorphicAt hf, meromorphicOrderAt_of_not_meromorphicAt]
    rwa [meromorphicAt_smul_iff_of_ne_zero hg hg']

lemma meromorphicOrderAt_mul_of_ne_zero {f : ğ•œ â†’ ğ•œ} (hg : AnalyticAt ğ•œ g x) (hg' : g x â‰  0) :
    meromorphicOrderAt (g * f) x = meromorphicOrderAt f x :=
  meromorphicOrderAt_smul_of_ne_zero hg hg'

end smul
