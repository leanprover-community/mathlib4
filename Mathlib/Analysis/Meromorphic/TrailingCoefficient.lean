/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Order

/-!
# The Trailing Coefficient of a Meromorphic Function

This file defines the trailing coefficient of a meromorphic function. If `f` is meromorphic at a
point `x`, the trailing coefficient is defined as the (unique!) value `g x` for a presentation of
`f` in the form `(z - x) ^ order â€¢ g z` with `g` analytic at `x`.

The lemma `meromorphicTrailingCoeffAt_eq_limit` expresses the trailing coefficient as a limit.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f g : ğ•œ â†’ E} {x : ğ•œ}

open Filter Topology

variable (f x) in
/--
If `f` is meromorphic of finite order at a point `x`, the trailing coefficient is defined as the
(unique!) value `g x` for a presentation of `f` in the form `(z - x) ^ order â€¢ g z` with `g`
analytic at `x`. In all other cases, the trailing coefficient is defined to be zero.
-/
noncomputable def meromorphicTrailingCoeffAt : E := by
  by_cases hâ‚ : MeromorphicAt f x
  Â· by_cases hâ‚‚ : meromorphicOrderAt f x = âŠ¤
    Â· exact 0
    Â· exact ((meromorphicOrderAt_ne_top_iff hâ‚).1 hâ‚‚).choose x
  Â· exact 0

/--
If `f` is not meromorphic at `x`, the trailing coefficient is zero by definition.
-/
@[simp] lemma meromorphicTrailingCoeffAt_of_not_MeromorphicAt (h : Â¬MeromorphicAt f x) :
    meromorphicTrailingCoeffAt f x = 0 := by simp [meromorphicTrailingCoeffAt, h]

/--
If `f` is meromorphic of infinite order at `x`, the trailing coefficient is zero by definition.
-/
@[simp] lemma MeromorphicAt.meromorphicTrailingCoeffAt_of_order_eq_top
    (h : meromorphicOrderAt f x = âŠ¤) :
    meromorphicTrailingCoeffAt f x = 0 := by simp_all [meromorphicTrailingCoeffAt]

/-!
## Characterization of the Leading Coefficient
-/

/--
Definition of the trailing coefficient in case where `f` is meromorphic and a presentation of the
form `f = (z - x) ^ order â€¢ g z` is given, with `g` analytic at `x`.
-/
lemma AnalyticAt.meromorphicTrailingCoeffAt_of_eq_nhdsNE (hâ‚g : AnalyticAt ğ•œ g x)
    (h : f =á¶ [ğ“[â‰ ] x] fun z â†¦ (z - x) ^ (meromorphicOrderAt f x).untopâ‚€ â€¢ g z) :
    meromorphicTrailingCoeffAt f x = g x := by
  have hâ‚f : MeromorphicAt f x := by
    rw [MeromorphicAt.meromorphicAt_congr h]
    fun_prop
  by_cases hâ‚ƒ : meromorphicOrderAt f x = âŠ¤
  Â· simp only [hâ‚ƒ, WithTop.untopâ‚€_top, zpow_zero, one_smul,
      MeromorphicAt.meromorphicTrailingCoeffAt_of_order_eq_top] at âŠ¢ h
    apply EventuallyEq.eq_of_nhds (f := 0)
    rw [â† ContinuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE (by fun_prop) (by fun_prop)]
    apply (h.symm.trans (meromorphicOrderAt_eq_top_iff.1 hâ‚ƒ)).symm
  Â· unfold meromorphicTrailingCoeffAt
    simp only [hâ‚f, reduceDIte, hâ‚ƒ, ne_eq]
    obtain âŸ¨h'â‚, h'â‚‚, h'â‚ƒâŸ© := ((meromorphicOrderAt_ne_top_iff hâ‚f).1 hâ‚ƒ).choose_spec
    apply Filter.EventuallyEq.eq_of_nhds
    rw [â† h'â‚.continuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE hâ‚g.continuousAt]
    filter_upwards [h, h'â‚ƒ, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y hâ‚ƒy
    rw [â† sub_eq_zero]
    rwa [hâ‚‚y, â† sub_eq_zero, â† smul_sub, smul_eq_zero_iff_right] at hâ‚y
    simp_all [zpow_ne_zero, sub_ne_zero]

/--
Variant of `meromorphicTrailingCoeffAt_of_order_eq_finite`: Definition of the trailing coefficient
in case where `f` is meromorphic of finite order and a presentation is given.
-/
lemma AnalyticAt.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE {n : â„¤} (hâ‚g : AnalyticAt ğ•œ g x)
    (hâ‚‚g : g x â‰  0) (h : f =á¶ [ğ“[â‰ ] x] fun z â†¦ (z - x) ^ n â€¢ g z) :
    meromorphicTrailingCoeffAt f x = g x := by
  have hâ‚„ : MeromorphicAt f x := by
    rw [MeromorphicAt.meromorphicAt_congr h]
    fun_prop
  have : meromorphicOrderAt f x = n := by
    simp only [meromorphicOrderAt_eq_int_iff hâ‚„, ne_eq]
    use g, hâ‚g, hâ‚‚g
    exact h
  simp_all [meromorphicTrailingCoeffAt_of_eq_nhdsNE hâ‚g]

/--
If `f` is analytic and does not vanish at `x`, then the trailing coefficient of `f` at `x` is `f x`.
-/
@[simp]
lemma AnalyticAt.meromorphicTrailingCoeffAt_of_ne_zero (hâ‚ : AnalyticAt ğ•œ f x) (hâ‚‚ : f x â‰  0) :
    meromorphicTrailingCoeffAt f x = f x := by
  rw [hâ‚.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE (n := 0) hâ‚‚]
  filter_upwards
  simp

/--
If `f` is meromorphic at `x`, then the trailing coefficient of `f` at `x` is the limit of the
function `(Â· - x) ^ (-order) â€¢ f`.
-/
lemma MeromorphicAt.tendsto_nhds_meromorphicTrailingCoeffAt (h : MeromorphicAt f x) :
    Tendsto ((Â· - x) ^ (-(meromorphicOrderAt f x).untopâ‚€) â€¢ f) (ğ“[â‰ ] x)
      (ğ“ (meromorphicTrailingCoeffAt f x)) := by
  by_cases hâ‚‚ : meromorphicOrderAt f x = âŠ¤
  Â· simp_all only [WithTop.untopâ‚€_top, neg_zero, zpow_zero, one_smul,
      meromorphicTrailingCoeffAt_of_order_eq_top]
    apply Tendsto.congr' (fâ‚ := 0)
    Â· filter_upwards [meromorphicOrderAt_eq_top_iff.1 hâ‚‚] with y hy
      simp_all
    Â· apply Tendsto.congr' (fâ‚ := 0) (by rfl) continuousWithinAt_const.tendsto
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff h).1 hâ‚‚
  apply Tendsto.congr' (fâ‚ := g)
  Â· filter_upwards [hâ‚ƒg, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    rw [zpow_neg, Pi.smul_apply', Pi.inv_apply, Pi.pow_apply, hâ‚y, â† smul_assoc, smul_eq_mul,
      â† zpow_neg, â† zpow_add', neg_add_cancel, zpow_zero, one_smul]
    left
    simp_all [sub_ne_zero]
  Â· rw [hâ‚g.meromorphicTrailingCoeffAt_of_eq_nhdsNE hâ‚ƒg]
    apply hâ‚g.continuousAt.continuousWithinAt

/-!
## Elementary Properties
-/

/--
If `f` is meromorphic of finite order at `x`, then the trailing coefficient is not zero.
-/
lemma MeromorphicAt.meromorphicTrailingCoeffAt_ne_zero (hâ‚ : MeromorphicAt f x)
    (hâ‚‚ : meromorphicOrderAt f x â‰  âŠ¤) :
    meromorphicTrailingCoeffAt f x â‰  0 := by
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff hâ‚).1 hâ‚‚
  simpa [hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE hâ‚‚g hâ‚ƒg] using hâ‚‚g

/--
The trailing coefficient of a constant function is the constant.
-/
@[simp]
theorem meromorphicTrailingCoeffAt_const {x : ğ•œ} {e : ğ•œ} :
    meromorphicTrailingCoeffAt (fun _ â†¦ e) x = e := by
  by_cases he : e = 0
  Â· rw [he]
    apply MeromorphicAt.meromorphicTrailingCoeffAt_of_order_eq_top
    rw [meromorphicOrderAt_eq_top_iff]
    simp
  Â· exact analyticAt_const.meromorphicTrailingCoeffAt_of_ne_zero he

/--
The trailing coefficient of `fun z â†¦ z - constant` at `zâ‚€` equals one if `zâ‚€ = constant`, or else
`zâ‚€ - constant`.
-/
theorem meromorphicTrailingCoeffAt_id_sub_const [DecidableEq ğ•œ] {x y : ğ•œ} :
    meromorphicTrailingCoeffAt (Â· - y) x = if x = y then 1 else x - y := by
  by_cases h : x = y
  Â· simp_all only [sub_self, ite_true]
    apply AnalyticAt.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE (n := 1) (by fun_prop)
      (by apply one_ne_zero)
    simp
  Â· simp_all only [ite_false]
    apply AnalyticAt.meromorphicTrailingCoeffAt_of_ne_zero (by fun_prop)
    simp_all [sub_ne_zero]

/-!
## Congruence Lemma
-/

/--
If two functions agree in a punctured neighborhood, then their trailing coefficients agree.
-/
lemma meromorphicTrailingCoeffAt_congr_nhdsNE {fâ‚ fâ‚‚ : ğ•œ â†’ E} (h : fâ‚ =á¶ [ğ“[â‰ ] x] fâ‚‚) :
    meromorphicTrailingCoeffAt fâ‚ x = meromorphicTrailingCoeffAt fâ‚‚ x := by
  by_cases hâ‚ : Â¬MeromorphicAt fâ‚ x
  Â· simp [hâ‚, (MeromorphicAt.meromorphicAt_congr h).not.1 hâ‚]
  rw [not_not] at hâ‚
  by_cases hâ‚‚ : meromorphicOrderAt fâ‚ x = âŠ¤
  Â· simp_all [meromorphicOrderAt_congr h]
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff hâ‚).1 hâ‚‚
  rw [hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE hâ‚‚g hâ‚ƒg,
    hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE hâ‚‚g (h.symm.trans hâ‚ƒg)]

/-!
## Behavior under Arithmetic Operations
-/

/--
The trailing coefficient of a scalar product is the scalar product of the trailing coefficients.
-/
lemma MeromorphicAt.meromorphicTrailingCoeffAt_smul {fâ‚ : ğ•œ â†’ ğ•œ} {fâ‚‚ : ğ•œ â†’ E}
    (hfâ‚ : MeromorphicAt fâ‚ x) (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    meromorphicTrailingCoeffAt (fâ‚ â€¢ fâ‚‚) x =
      (meromorphicTrailingCoeffAt fâ‚ x) â€¢ (meromorphicTrailingCoeffAt fâ‚‚ x) := by
  by_cases hâ‚fâ‚ : meromorphicOrderAt fâ‚ x = âŠ¤
  Â· simp_all [meromorphicOrderAt_smul hfâ‚ hfâ‚‚]
  by_cases hâ‚fâ‚‚ : meromorphicOrderAt fâ‚‚ x = âŠ¤
  Â· simp_all [meromorphicOrderAt_smul hfâ‚ hfâ‚‚]
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := (meromorphicOrderAt_ne_top_iff hfâ‚).1 hâ‚fâ‚
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := (meromorphicOrderAt_ne_top_iff hfâ‚‚).1 hâ‚fâ‚‚
  have : fâ‚ â€¢ fâ‚‚ =á¶ [ğ“[â‰ ] x]
      fun z â†¦ (z - x) ^ (meromorphicOrderAt (fâ‚ â€¢ fâ‚‚) x).untopâ‚€ â€¢ (gâ‚ â€¢ gâ‚‚) z := by
    filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y hâ‚ƒy
    simp_all [meromorphicOrderAt_smul hfâ‚ hfâ‚‚]
    rw [â† smul_assoc, â† smul_assoc, smul_eq_mul, smul_eq_mul, zpow_addâ‚€ (sub_ne_zero.2 hâ‚ƒy)]
    ring_nf
  rw [hâ‚gâ‚.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE hâ‚‚gâ‚ hâ‚ƒgâ‚,
    hâ‚gâ‚‚.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE hâ‚‚gâ‚‚ hâ‚ƒgâ‚‚,
    (hâ‚gâ‚.smul hâ‚gâ‚‚).meromorphicTrailingCoeffAt_of_eq_nhdsNE this]
  simp

/--
The trailing coefficient of a product is the product of the trailing coefficients.
-/
lemma MeromorphicAt.meromorphicTrailingCoeffAt_mul {fâ‚ fâ‚‚ : ğ•œ â†’ ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ x)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    meromorphicTrailingCoeffAt (fâ‚ * fâ‚‚) x =
      (meromorphicTrailingCoeffAt fâ‚ x) * (meromorphicTrailingCoeffAt fâ‚‚ x) :=
  meromorphicTrailingCoeffAt_smul hfâ‚ hfâ‚‚

/--
The trailing coefficient of a product is the product of the trailing coefficients.
-/
theorem meromorphicTrailingCoeffAt_prod {Î¹ : Type*} {s : Finset Î¹} {f : Î¹ â†’ ğ•œ â†’ ğ•œ} {x : ğ•œ}
    (h : âˆ€ Ïƒ, MeromorphicAt (f Ïƒ) x) :
    meromorphicTrailingCoeffAt (âˆ n âˆˆ s, f n) x = âˆ n âˆˆ s, meromorphicTrailingCoeffAt (f n) x := by
  classical
  induction s using Finset.induction with
  | empty =>
    apply meromorphicTrailingCoeffAt_const
  | insert Ïƒ sâ‚ hÏƒ hind =>
    rw [Finset.prod_insert hÏƒ, Finset.prod_insert hÏƒ, (h Ïƒ).meromorphicTrailingCoeffAt_mul
      (MeromorphicAt.prod h), hind]

/--
The trailing coefficient of the inverse function is the inverse of the trailing coefficient.
-/
lemma meromorphicTrailingCoeffAt_inv {f : ğ•œ â†’ ğ•œ} :
    meromorphicTrailingCoeffAt fâ»Â¹ x = (meromorphicTrailingCoeffAt f x)â»Â¹ := by
  by_cases hâ‚ : MeromorphicAt f x
  Â· by_cases hâ‚‚ : meromorphicOrderAt f x = âŠ¤
    Â· simp_all [meromorphicOrderAt_inv (f := f) (x := x)]
    have : fâ»Â¹ * f =á¶ [ğ“[â‰ ] x] 1 := by
      filter_upwards [(meromorphicOrderAt_ne_top_iff_eventually_ne_zero hâ‚).1 hâ‚‚]
      simp_all
    rw [â† mul_eq_one_iff_eq_invâ‚€ (hâ‚.meromorphicTrailingCoeffAt_ne_zero hâ‚‚),
      â† hâ‚.inv.meromorphicTrailingCoeffAt_mul hâ‚, meromorphicTrailingCoeffAt_congr_nhdsNE this,
      analyticAt_const.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE (n := 0)]
    Â· simp
    Â· simp only [zpow_zero, smul_eq_mul, mul_one]
      exact eventuallyEq_nhdsWithin_of_eqOn fun _ â†¦ congrFun rfl
  Â· simp_all

/--
The trailing coefficient of the power of a function is the power of the trailing coefficient.
-/
lemma MeromorphicAt.meromorphicTrailingCoeffAt_zpow {n : â„¤} {f : ğ•œ â†’ ğ•œ} (hâ‚ : MeromorphicAt f x) :
    meromorphicTrailingCoeffAt (f ^ n) x = (meromorphicTrailingCoeffAt f x) ^ n := by
  by_cases hâ‚‚ : meromorphicOrderAt f x = âŠ¤
  Â· by_cases hâ‚ƒ : n = 0
    Â· simp only [hâ‚ƒ, zpow_zero]
      apply analyticAt_const.meromorphicTrailingCoeffAt_of_ne_zero (ne_zero_of_eq_one rfl)
    Â· simp_all [meromorphicOrderAt_zpow hâ‚, zero_zpow n hâ‚ƒ]
  Â· obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := (meromorphicOrderAt_ne_top_iff hâ‚).1 hâ‚‚
    rw [hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE
        (n := (meromorphicOrderAt f x).untopâ‚€) hâ‚‚g hâ‚ƒg,
      (hâ‚g.zpow hâ‚‚g (n := n)).meromorphicTrailingCoeffAt_of_ne_zero_of_eq_nhdsNE
        (n := (meromorphicOrderAt (f ^ n) x).untopâ‚€)
        (by simp_all [zpow_ne_zero])]
    Â· simp only [Pi.pow_apply]
    Â· filter_upwards [hâ‚ƒg] with a ha
      simp_all [mul_zpow, â† zpow_mul, meromorphicOrderAt_zpow hâ‚, mul_comm]

/--
The trailing coefficient of the power of a function is the power of the trailing coefficient.
-/
lemma MeromorphicAt.meromorphicTrailingCoeffAt_pow {n : â„•} {f : ğ•œ â†’ ğ•œ} (hâ‚ : MeromorphicAt f x) :
    meromorphicTrailingCoeffAt (f ^ n) x = (meromorphicTrailingCoeffAt f x) ^ n := by
  convert hâ‚.meromorphicTrailingCoeffAt_zpow (n := n) <;> simp
