/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Divisor
import Mathlib.Analysis.Meromorphic.IsolatedZeros
import Mathlib.Analysis.Meromorphic.NormalForm
import Mathlib.Analysis.Meromorphic.TrailingCoefficient
import Mathlib.Analysis.SpecialFunctions.Log.Basic

/-!
# Factorized Rational Functions

This file discusses functions `ğ•œ â†’ ğ•œ` of the form `âˆá¶  u, (Â· - u) ^ d u`, where `d : ğ•œ â†’ â„¤` is
integer-valued. We show that these "factorized rational functions" are meromorphic in normal form,
with divisor equal to `d`.

Under suitable assumptions, we show that meromorphic functions are equivalent, modulo equality on
codiscrete sets, to the product of a factorized rational function and an analytic function without
zeros.

Implementation Note: For consistency, we use `âˆá¶  u, (Â· - u) ^ d u` throughout. If the support of `d`
is finite, then evaluation of functions commutes with finprod, and the helper lemma
`Function.FactorizedRational.finprod_eval` asserts that `âˆá¶  u, (Â· - u) ^ d u` equals the function
`fun x â†¦ âˆá¶  u, (x - u) ^ d u`. If `d` has infinite support, this equality is wrong in general.
There are elementary examples of functions `d` where `âˆá¶  u, (Â· - u) ^ d u` is constant one, while
`fun x â†¦ âˆá¶  u, (x - u) ^ d u` is not continuous.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {U : Set ğ•œ}

open Filter Function Real Set

namespace Function.FactorizedRational

/-!
## Elementary Properties of Factorized Rational Functions
-/

/--
Helper Lemma: Identify the support of `d` as the mulsupport of the product defining the factorized
rational function.
-/
lemma mulSupport (d : ğ•œ â†’ â„¤) :
    (fun u â†¦ (Â· - u) ^ d u).mulSupport = d.support := by
  ext u
  constructor <;> intro h
  Â· simp_all only [mem_mulSupport, ne_eq, mem_support]
    by_contra hCon
    simp_all [zpow_zero]
  Â· simp_all only [mem_mulSupport, ne_eq, ne_iff]
    use u
    simp_all [zero_zpow_eq_oneâ‚€]

/--
Helper Lemma: If the support of `d` is finite, then evaluation of functions commutes with finprod,
and the function `âˆá¶  u, (Â· - u) ^ d u` equals `fun x â†¦ âˆá¶  u, (x - u) ^ d u`.
-/
lemma finprod_eq_fun {d : ğ•œ â†’ â„¤} (h : d.support.Finite) :
    (âˆá¶  u, (Â· - u) ^ d u) = fun x â†¦ âˆá¶  u, (x - u) ^ d u := by
  ext x
  rw [finprod_eq_prod_of_mulSupport_subset (s := h.toFinset),
    finprod_eq_prod_of_mulSupport_subset (s := h.toFinset)]
  Â· simp
  Â· intro u
    contrapose
    simp_all
  Â· simp [mulSupport d]

/--
Factorized rational functions are analytic wherever the exponent is non-negative.
-/
theorem analyticAt {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : 0 â‰¤ d x) :
    AnalyticAt ğ•œ (âˆá¶  u, (Â· - u) ^ d u) x := by
  apply analyticAt_finprod
  intro u
  by_cases hâ‚‚ : x = u
  Â· apply AnalyticAt.fun_zpow_nonneg (by fun_prop)
    rwa [â† hâ‚‚]
  Â· apply AnalyticAt.fun_zpow (by fun_prop)
    rwa [sub_ne_zero]

/--
Factorized rational functions are non-zero wherever the exponent is zero.
-/
theorem ne_zero {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d x = 0) :
    (âˆá¶  u, (Â· - u) ^ d u) x â‰  0 := by
  by_cases hâ‚ : (fun u â†¦ (Â· - u) ^ d u).mulSupport.Finite
  Â· rw [finprod_eq_prod _ hâ‚, Finset.prod_apply, Finset.prod_ne_zero_iff]
    intro z hz
    simp only [Pi.pow_apply, ne_eq]
    by_cases hâ‚‚ : x = z <;> simp_all [zpow_ne_zero, sub_ne_zero]
  Â· simp [finprod_of_infinite_mulSupport hâ‚]

open Classical in
/--
Helper Lemma for Computations: Extract one factor out of a factorized rational function.
-/
lemma extractFactor {d : ğ•œ â†’ â„¤} (uâ‚€ : ğ•œ) (hd : d.support.Finite) :
    (âˆá¶  u, (Â· - u) ^ d u) = ((Â· - uâ‚€) ^ d uâ‚€) * (âˆá¶  u, (Â· - u) ^ (update d uâ‚€ 0 u)) := by
  by_cases hâ‚d : d uâ‚€ = 0
  Â· simp [â† eq_update_self_iff.2 hâ‚d, hâ‚d]
  Â· have : (fun u â†¦ (fun x â†¦ x - u) ^ d u).mulSupport âŠ† hd.toFinset := by
      simp [mulSupport]
    rw [finprod_eq_prod_of_mulSupport_subset _ this]
    have : uâ‚€ âˆˆ hd.toFinset := by simp_all
    rw [â† Finset.mul_prod_erase hd.toFinset _ this]
    congr 1
    have : (fun u â†¦ (Â· - u) ^ (update d uâ‚€ 0 u)).mulSupport âŠ† hd.toFinset.erase uâ‚€ := by
      rw [mulSupport]
      intro x hx
      by_cases hâ‚x : x = uâ‚€ <;> simp_all
    simp_all [finprod_eq_prod_of_mulSupport_subset _ this, Finset.prod_congr rfl]

/--
Factorized rational functions are meromorphic in normal form on `univ`.
-/
theorem meromorphicNFOn_univ (d : ğ•œ â†’ â„¤) :
    MeromorphicNFOn (âˆá¶  u, (Â· - u) ^ d u) univ := by
  classical
  by_cases hd : d.support.Finite
  Â· intro z hz
    rw [extractFactor z hd]
    right
    use d z, (âˆá¶  u, (Â· - u) ^ update d z 0 u)
    simp [analyticAt, ne_zero]
  Â· rw [â† mulSupport d] at hd
    rw [finprod_of_infinite_mulSupport hd]
    exact AnalyticOnNhd.meromorphicNFOn analyticOnNhd_const

/--
Factorized rational functions are meromorphic in normal form on arbitrary subsets of `ğ•œ`.
-/
theorem meromorphicNFOn (d : ğ•œ â†’ â„¤) (U : Set ğ•œ) :
    MeromorphicNFOn (âˆá¶  u, (Â· - u) ^ d u) U := fun _ _ â†¦ meromorphicNFOn_univ d (trivial)

/-!
## Orders and Divisors of Factorized Rational Functions
-/

/--
The order of the factorized rational function `(âˆá¶  u, fun z â†¦ (z - u) ^ d u)` at `z` equals `d z`.
-/
theorem meromorphicOrderAt_eq {z : ğ•œ} (d : ğ•œ â†’ â„¤) (hâ‚d : d.support.Finite) :
    meromorphicOrderAt (âˆá¶  u, (Â· - u) ^ d u) z = d z := by
  classical
  rw [meromorphicOrderAt_eq_int_iff ((meromorphicNFOn_univ d).meromorphicOn _ (mem_univ _))]
  use âˆá¶  u, (Â· - u) ^ update d z 0 u
  simp only [update_self, le_refl, analyticAt, ne_eq, ne_zero, not_false_eq_true, smul_eq_mul,
    true_and]
  filter_upwards
  simp [extractFactor z hâ‚d]

@[deprecated (since := "2025-05-22")] alias order := meromorphicOrderAt_eq

/--
Factorized rational functions are nowhere locally constant zero.
-/
theorem meromorphicOrderAt_ne_top {z : ğ•œ} (d : ğ•œ â†’ â„¤) :
    meromorphicOrderAt (âˆá¶  u, (Â· - u) ^ d u) z â‰  âŠ¤ := by
  by_cases hd : d.support.Finite
  Â· simp [meromorphicOrderAt_eq d hd]
  Â· rw [â† mulSupport] at hd
    have : AnalyticAt ğ•œ (1 : ğ•œ â†’ ğ•œ) z := analyticAt_const
    simp [finprod_of_infinite_mulSupport hd, this.meromorphicOrderAt_eq,
      this.analyticOrderAt_eq_zero.2 (by simp)]

@[deprecated (since := "2025-05-22")] alias order_ne_top := meromorphicOrderAt_ne_top

/--
If `D` is a divisor, then the divisor of the factorized rational function equals `D`.
-/
theorem divisor {U : Set ğ•œ} {D : locallyFinsuppWithin U â„¤} (hD : D.support.Finite) :
    MeromorphicOn.divisor (âˆá¶  u, (Â· - u) ^ D u) U = D := by
  ext z
  by_cases hz : z âˆˆ U
  <;> simp [(meromorphicNFOn D U).meromorphicOn, hz, meromorphicOrderAt_eq D hD]

open Classical in
private lemma mulSupport_update {d : ğ•œ â†’ â„¤} {x : ğ•œ}
    (h : d.support.Finite) :
    (fun u â†¦ (x - u) ^ Function.update d x 0 u).mulSupport âŠ† h.toFinset := by
  intro u
  contrapose
  simp only [mem_mulSupport, ne_eq, Decidable.not_not]
  by_cases hâ‚ : u = x
  Â· rw [hâ‚]
    simp
  Â· simp_all

open Classical in
/--
Compute the trailing coefficient of the factorized rational function associated with `d : ğ•œ â†’ â„¤`.
-/

/-
Low-priority TODO: Using that non-trivially normed fields contain infinitely many elements that are
no roots of unity, it might be possible to drop assumption `h` here and in some of the theorems
below.
-/
theorem meromorphicTrailingCoeffAt_factorizedRational {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d.support.Finite) :
    meromorphicTrailingCoeffAt (âˆá¶  u, (Â· - u) ^ d u) x = âˆá¶  u, (x - u) ^ update d x 0 u := by
  have : (fun u â†¦ (Â· - u) ^ d u).mulSupport âŠ† h.toFinset := by
    simp [Function.FactorizedRational.mulSupport]
  rw [finprod_eq_prod_of_mulSupport_subset _ this, meromorphicTrailingCoeffAt_prod
      (fun _ â†¦ by fun_prop), finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update h)]
  apply Finset.prod_congr rfl
  intro y hy
  rw [MeromorphicAt.meromorphicTrailingCoeffAt_zpow (by fun_prop)]
  by_cases hxy : x = y
  Â· rw [hxy, meromorphicTrailingCoeffAt_id_sub_const]
    simp_all
  Â· grind [Function.update_of_ne, meromorphicTrailingCoeffAt_id_sub_const]

/--
Variant of `meromorphicTrailingCoeffAt_factorizedRational`: Compute the trailing coefficient of the
factorized rational function associated with `d : ğ•œ â†’ â„¤` at points outside the support of `d`.
-/
theorem meromorphicTrailingCoeffAt_factorizedRational_off_support {d : ğ•œ â†’ â„¤} {x : ğ•œ}
    (hâ‚ : d.support.Finite) (hâ‚‚ : x âˆ‰ d.support) :
    meromorphicTrailingCoeffAt (âˆá¶  u, (Â· - u) ^ d u) x = âˆá¶  u, (x - u) ^ d u := by
  classical
  rw [meromorphicTrailingCoeffAt_factorizedRational hâ‚,
    finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update hâ‚)]
  have : (fun u â†¦ (x - u) ^ d u).mulSupport âŠ† hâ‚.toFinset := by
    intro u
    contrapose
    simp_all
  rw [finprod_eq_prod_of_mulSupport_subset _ this, Finset.prod_congr rfl]
  intro y hy
  congr
  apply Function.update_of_ne
  by_contra hCon
  simp_all

/--
Variant of `meromorphicTrailingCoeffAt_factorizedRational`: Compute log of the norm of the trailing
coefficient.  The convention that `log 0 = 0` gives a closed formula easier than the one in
`meromorphicTrailingCoeffAt_factorizedRational`.
-/
theorem log_norm_meromorphicTrailingCoeffAt {d : ğ•œ â†’ â„¤} {x : ğ•œ} (h : d.support.Finite) :
    log â€–meromorphicTrailingCoeffAt (âˆá¶  u, (Â· - u) ^ d u) xâ€– = âˆ‘á¶  u, (d u) * log â€–x - uâ€– := by
  classical
  rw [meromorphicTrailingCoeffAt_factorizedRational h,
    finprod_eq_prod_of_mulSupport_subset _ (mulSupport_update h)]
  have : âˆ€ y âˆˆ h.toFinset, â€–(x - y) ^ update d x 0 yâ€– â‰  0 := by
    intro y _
    by_cases h : x = y
    Â· rw [h]
      simp_all
    Â· simp_all [zpow_ne_zero, sub_ne_zero]
  rw [norm_prod, log_prod _ _ this]
  have : (fun u â†¦ (d u) * log â€–x - uâ€–).support âŠ† h.toFinset := by
    intro u
    contrapose
    simp_all
  rw [finsum_eq_sum_of_support_subset _ this]
  apply Finset.sum_congr rfl
  intro y hy
  rw [norm_zpow, Real.log_zpow]
  by_cases h : x = y
  Â· simp [h]
  Â· rw [Function.update_of_ne (by tauto)]

end Function.FactorizedRational

open Function.FactorizedRational

/-!
## Elimination of Zeros and Poles

This section shows that every meromorphic function with finitely many zeros and poles is equivalent,
modulo equality on codiscrete sets, to the product of a factorized rational function and an analytic
function without zeros.

We provide analogous results for functions of the form `log â€–meromorphicâ€–`.
-/

/-
TODO: Identify some of the terms that appear in the decomposition.
-/

/--
If `f` is meromorphic on an open set `U`, if `f` is nowhere locally constant zero, and if the
support of the divisor of `f` is finite, then there exists an analytic function `g` on `U` without
zeros such that `f` is equivalent, modulo equality on codiscrete sets, to the product of `g` and the
factorized rational function associated with the divisor of `f`.
-/
theorem MeromorphicOn.extract_zeros_poles {f : ğ•œ â†’ E} (hâ‚f : MeromorphicOn f U)
    (hâ‚‚f : âˆ€ u : U, meromorphicOrderAt f u â‰  âŠ¤) (hâ‚ƒf : (divisor f U).support.Finite) :
    âˆƒ g : ğ•œ â†’ E, AnalyticOnNhd ğ•œ g U âˆ§ (âˆ€ u : U, g u â‰  0) âˆ§
      f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ divisor f U u) â€¢ g := by
  -- Take `g` as the inverse of the Laurent polynomial defined below, converted to a meromorphic
  -- function in normal form. Then check all the properties.
  let Ï† := âˆá¶  u, (Â· - u) ^ (divisor f U u)
  have hÏ† : MeromorphicOn Ï† U := (meromorphicNFOn (divisor f U) U).meromorphicOn
  let g := toMeromorphicNFOn (Ï†â»Â¹ â€¢ f) U
  have hg : MeromorphicNFOn g U := by apply meromorphicNFOn_toMeromorphicNFOn
  refine âŸ¨g, ?_, ?_, ?_âŸ©
  Â· -- AnalyticOnNhd ğ•œ g U
    rw [â† hg.divisor_nonneg_iff_analyticOnNhd, divisor_of_toMeromorphicNFOn (hÏ†.inv.smul hâ‚f),
      divisor_smul hÏ†.inv hâ‚f _ (fun z hz â†¦ hâ‚‚f âŸ¨z, hzâŸ©), divisor_inv,
      Function.FactorizedRational.divisor hâ‚ƒf, neg_add_cancel]
    intro z hz
    simpa [meromorphicOrderAt_inv] using meromorphicOrderAt_ne_top (divisor f U)
  Â· -- âˆ€ (u : â†‘U), g â†‘u â‰  0
    intro âŸ¨u, huâŸ©
    rw [â† (hg hu).meromorphicOrderAt_eq_zero_iff, â† meromorphicOrderAt_congr
        (toMeromorphicNFOn_eq_self_on_nhdsNE (hÏ†.inv.smul hâ‚f) hu).symm,
      meromorphicOrderAt_smul (hÏ† u hu).inv (hâ‚f u hu), meromorphicOrderAt_inv,
      meromorphicOrderAt_eq _ hâ‚ƒf]
    simp only [hâ‚f, hu, divisor_apply]
    lift meromorphicOrderAt f u to â„¤ using (hâ‚‚f âŸ¨u, huâŸ©) with n hn
    rw [WithTop.untopâ‚€_coe, â† WithTop.LinearOrderedAddCommGroup.coe_neg, â† WithTop.coe_add]
    simp
  Â· -- f =á¶ [codiscreteWithin U] (âˆá¶  (u : ğ•œ), fun z â†¦ (z - u) ^ (divisor f U) u) * g
    filter_upwards [(divisor f U).eq_zero_codiscreteWithin,
      (hÏ†.inv.smul hâ‚f).meromorphicNFAt_mem_codiscreteWithin,
      self_mem_codiscreteWithin U] with a hâ‚‚a hâ‚ƒa hâ‚„a
    unfold g
    simp only [Pi.smul_apply', toMeromorphicNFOn_eq_toMeromorphicNFAt (hÏ†.inv.smul hâ‚f) hâ‚„a,
      toMeromorphicNFAt_eq_self.2 hâ‚ƒa, Pi.inv_apply]
    rw [â† smul_assoc, smul_eq_mul, mul_inv_cancelâ‚€ _, one_smul]
    rwa [â† ((meromorphicNFOn_univ (divisor f U)) trivial).meromorphicOrderAt_eq_zero_iff,
      meromorphicOrderAt_eq, hâ‚‚a, Pi.zero_apply, WithTop.coe_zero]

/--
In the setting of `MeromorphicOn.extract_zeros_poles`, the function `log â€–fâ€–` is equivalent, modulo
equality on codiscrete subsets, to `âˆ‘á¶  u, (divisor f U u * log â€–Â· - uâ€–) + log â€–g Â·â€–`.
-/
theorem MeromorphicOn.extract_zeros_poles_log {f g : ğ•œ â†’ E} {D : Function.locallyFinsuppWithin U â„¤}
    (hg : âˆ€ u : U, g u â‰  0) (h : f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    (log â€–f Â·â€–) =á¶ [codiscreteWithin U] âˆ‘á¶  u, (D u * log â€–Â· - uâ€–) + (log â€–g Â·â€–) := by
  -- Identify support of the sum in the goal
  have tâ‚ : (fun u â†¦ (D u * log â€–Â· - uâ€–)).support = D.support := by
    ext u
    rw [â† not_iff_not]
    simp only [ne_eq, not_not, Function.mem_support]
    constructor <;> intro hx
    Â· obtain âŸ¨y, hyâŸ© := NormedField.exists_one_lt_norm ğ•œ
      have := congrFun hx (y + u)
      simp only [add_sub_cancel_right, Pi.zero_apply, mul_eq_zero, Int.cast_eq_zero, log_eq_zero,
        norm_eq_zero] at this
      rcases this with h | h | h | h
      Â· assumption
      Â· simp only [h, norm_zero] at hy
        linarith
      Â· simp only [h, lt_self_iff_false] at hy
      Â· simp only [h, lt_neg_self_iff] at hy
        linarith
    Â· simp_all [Pi.zero_def]
  -- Trivial case: the support of D is infinite
  by_cases hâ‚ƒf : D.support.Finite
  case neg =>
    rw [finsum_of_infinite_support (by simpa [tâ‚] using hâ‚ƒf)]
    rw [finprod_of_infinite_mulSupport (by simpa [FactorizedRational.mulSupport] using hâ‚ƒf)] at h
    filter_upwards [h] with x hx
    simp [hx]
  -- General case
  filter_upwards [h, D.eq_zero_codiscreteWithin, self_mem_codiscreteWithin U] with z hz hâ‚‚z hâ‚ƒz
  rw [Pi.zero_apply] at hâ‚‚z
  rw [hz, finprod_eq_prod_of_mulSupport_subset (s := hâ‚ƒf.toFinset) _
      (by simp_all [FactorizedRational.mulSupport]),
    finsum_eq_sum_of_support_subset (s := hâ‚ƒf.toFinset) _ (by simp_all)]
  have : âˆ€ x âˆˆ hâ‚ƒf.toFinset, â€–z - xâ€– ^ D x â‰  0 := by
    intro x hx
    rw [Finite.mem_toFinset, Function.mem_support] at hx
    rw [ne_eq, zpow_eq_zero_iff hx, norm_eq_zero, sub_eq_zero, eq_comm]
    apply ne_of_apply_ne D
    rwa [hâ‚‚z]
  simp only [Pi.smul_apply', Finset.prod_apply, Pi.pow_apply, norm_smul, norm_prod, norm_zpow]
  rw [log_mul (Finset.prod_ne_zero_iff.2 this) (by simp [hg âŸ¨z, hâ‚ƒzâŸ©]), log_prod _ _ this]
  simp [log_zpow]

open Classical in
/--
In the setting of `MeromorphicOn.extract_zeros_poles`, compute the trailing
coefficient of `f` in terms of `divisor f U` and `g x`.
-/
theorem MeromorphicOn.meromorphicTrailingCoeffAt_extract_zeros_poles
    {x : ğ•œ} {f g : ğ•œ â†’ E} {D : ğ•œ â†’ â„¤} (hD : D.support.Finite) (hâ‚x : x âˆˆ U) (hâ‚‚x : AccPt x (ğ“Ÿ U))
    (hf : MeromorphicAt f x) (hâ‚g : AnalyticAt ğ•œ g x) (hâ‚‚g : g x â‰  0)
    (h : f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    meromorphicTrailingCoeffAt f x = (âˆá¶  u, (x - u) ^ Function.update D x 0 u) â€¢ g x := by
  have tâ‚€ : MeromorphicAt (âˆá¶  u, (Â· - u) ^ D u) x :=
    (FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x
  rw [meromorphicTrailingCoeffAt_congr_nhdsNE
      (hf.eventuallyEq_nhdsNE_of_eventuallyEq_codiscreteWithin (by fun_prop) hâ‚x hâ‚‚x h),
    tâ‚€.meromorphicTrailingCoeffAt_smul hâ‚g.meromorphicAt,
    hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero hâ‚‚g]
  simp [meromorphicTrailingCoeffAt_factorizedRational hD]

/--
In the setting of `MeromorphicOn.extract_zeros_poles`, compute the log of the
norm of the trailing coefficient of `f` in terms of `divisor f U` and `g x`.
-/
theorem MeromorphicOn.log_norm_meromorphicTrailingCoeffAt_extract_zeros_poles
    {x : ğ•œ} {f g : ğ•œ â†’ E} {D : ğ•œ â†’ â„¤} (hD : D.support.Finite) (hâ‚x : x âˆˆ U) (hâ‚‚x : AccPt x (ğ“Ÿ U))
    (hf : MeromorphicAt f x) (hâ‚g : AnalyticAt ğ•œ g x) (hâ‚‚g : g x â‰  0)
    (h : f =á¶ [codiscreteWithin U] (âˆá¶  u, (Â· - u) ^ D u) â€¢ g) :
    log â€–meromorphicTrailingCoeffAt f xâ€– = âˆ‘á¶  u, (D u) * log â€–x - uâ€– + log â€–g xâ€– := by
  rw [meromorphicTrailingCoeffAt_congr_nhdsNE
      (hf.eventuallyEq_nhdsNE_of_eventuallyEq_codiscreteWithin
        (((FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x).smul hâ‚g.meromorphicAt)
          hâ‚x hâ‚‚x h),
    ((FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x).meromorphicTrailingCoeffAt_smul
      hâ‚g.meromorphicAt, hâ‚g.meromorphicTrailingCoeffAt_of_ne_zero hâ‚‚g,
    norm_smul, log_mul, log_norm_meromorphicTrailingCoeffAt hD]
  Â· simp only [ne_eq, norm_eq_zero]
    apply MeromorphicAt.meromorphicTrailingCoeffAt_ne_zero
      ((FactorizedRational.meromorphicNFOn D U).meromorphicOn x hâ‚x)
    apply FactorizedRational.meromorphicOrderAt_ne_top
  Â· simp_all
