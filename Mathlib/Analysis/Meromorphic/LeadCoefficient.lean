/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Order

/-!
# The Leading Coefficient of a Meromorphic Function

This file defines the leading coefficient of a meromorphic function. If `f` is meromorphic at a
point `x`, the leading coefficient is defined as the (unique!) value `g x` for a presentation of `f`
in the form `(z - x) ^ order ‚Ä¢ g z` with `g` analytic at `x`.

The lemma `leadCoeff_eq_limit` expresses the leading coefficient as a limit.
-/

variable
  {ùïú : Type*} [NontriviallyNormedField ùïú]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ùïú E]
  {f g : ùïú ‚Üí E} {x : ùïú} {n : ‚Ñ§}

open Filter Topology

namespace MeromorphicAt

variable (f x) in
/--
If `f` is meromorphic of finite order at a point `x`, the leading coefficient is defined as the
(unique!) value `g x` for a presentation of `f` in the form `(z - x) ^ order ‚Ä¢ g z` with `g`
analytic at `x`. In all other cases, the leading coefficient is defined to be zero.
-/
noncomputable def leadCoeff : E := by
  by_cases h‚ÇÅ : MeromorphicAt f x
  ¬∑ by_cases h‚ÇÇ : meromorphicOrderAt f x = ‚ä§
    ¬∑ exact 0
    ¬∑ exact ((meromorphicOrderAt_ne_top_iff h‚ÇÅ).1 h‚ÇÇ).choose x
  ¬∑ exact 0

/--
If `f` is not meromorphic at `x`, the leading coefficient is zero by definition.
-/
@[simp] lemma leadCoeff_of_not_MeromorphicAt (h : ¬¨MeromorphicAt f x) :
    leadCoeff f x = 0 := by simp_all [leadCoeff]

/--
If `f` is meromorphic of infinite order at `x`, the leading coefficient is zero by definition.
-/
@[simp] lemma leadCoeff_of_order_eq_top (h‚ÇÅ : MeromorphicAt f x) (h‚ÇÇ : meromorphicOrderAt f x = ‚ä§) :
    leadCoeff f x = 0 := by simp_all [leadCoeff]

/-!
## Characterization of the Leading Coefficient
-/

/--
Definition of the leading coefficient in case where `f` is meromorphic of finite order and a
presentation is given.
-/
@[simp]
lemma leadCoeff_of_order_eq_finite (h‚ÇÅ : MeromorphicAt f x) (h‚ÇÇ : AnalyticAt ùïú g x)
    (h‚ÇÉ : meromorphicOrderAt f x ‚â† ‚ä§)
    (h‚ÇÑ : f =·∂†[ùìù[‚â†] x] fun z ‚Ü¶ (z - x) ^ (meromorphicOrderAt f x).untop‚ÇÄ ‚Ä¢ g z) :
    leadCoeff f x = g x := by
  unfold leadCoeff
  simp only [h‚ÇÅ, not_true_eq_false, reduceDIte, h‚ÇÉ, ne_eq]
  obtain ‚ü®h'‚ÇÅ, h'‚ÇÇ, h'‚ÇÉ‚ü© := ((meromorphicOrderAt_ne_top_iff h‚ÇÅ).1 h‚ÇÉ).choose_spec
  apply Filter.EventuallyEq.eq_of_nhds
  rw [‚Üê h'‚ÇÅ.continuousAt.eventuallyEq_nhds_iff_eventuallyEq_nhdsNE h‚ÇÇ.continuousAt]
  filter_upwards [h‚ÇÑ, h'‚ÇÉ, self_mem_nhdsWithin] with y h‚ÇÅy h‚ÇÇy h‚ÇÉy
  rw [‚Üê sub_eq_zero]
  rwa [h‚ÇÇy, ‚Üê sub_eq_zero, ‚Üê smul_sub, smul_eq_zero_iff_right] at h‚ÇÅy
  simp_all [zpow_ne_zero, sub_ne_zero]

/--
Variant of `leadCoeff_of_order_eq_finite`: Definition of the leading coefficient in case where
`f` is meromorphic of finite order and a presentation is given.
-/
@[simp]
lemma _root_.AnalyticAt.leadCoeff_of_order_eq_finite‚ÇÅ (h‚ÇÅ : AnalyticAt ùïú g x) (h‚ÇÇ : g x ‚â† 0)
    (h‚ÇÉ : f =·∂†[ùìù[‚â†] x] fun z ‚Ü¶ (z - x) ^ n ‚Ä¢ g z) :
    leadCoeff f x = g x := by
  have h‚ÇÑ : MeromorphicAt f x := by
    rw [MeromorphicAt.meromorphicAt_congr h‚ÇÉ]
    fun_prop
  have : meromorphicOrderAt f x = n := by
    simp only [meromorphicOrderAt_eq_int_iff h‚ÇÑ, ne_eq, zpow_natCast]
    use g, h‚ÇÅ, h‚ÇÇ
    exact h‚ÇÉ
  simp_all [leadCoeff_of_order_eq_finite h‚ÇÑ h‚ÇÅ, this]

/--
If `f` is analytic and does not vanish at `x`, then the leading coefficient of `f` at `x` is `f x`.
-/
@[simp]
lemma _root_.AnalyticAt.leadCoeff_of_nonvanish (h‚ÇÅ : AnalyticAt ùïú f x) (h‚ÇÇ : f x ‚â† 0) :
    leadCoeff f x = f x := by
  rw [h‚ÇÅ.leadCoeff_of_order_eq_finite‚ÇÅ (n := 0) h‚ÇÇ]
  filter_upwards
  simp

/--
If `f` is meromorphic at `x`, then the leading coefficient of `f` at `x` is the limit of the
function `(¬∑ - x) ^ (-h‚ÇÅ.order.untop‚ÇÄ) ‚Ä¢ f`.
-/
lemma leadCoeff_eq_limit (h : MeromorphicAt f x) :
    Tendsto ((¬∑ - x) ^ (-(meromorphicOrderAt f x).untop‚ÇÄ) ‚Ä¢ f) (ùìù[‚â†] x) (ùìù (leadCoeff f x)) := by
  by_cases h‚ÇÇ : meromorphicOrderAt f x = ‚ä§
  ¬∑ simp_all only [WithTop.untop‚ÇÄ_top, neg_zero, zpow_zero, one_smul,
      leadCoeff_of_order_eq_top]
    apply Tendsto.congr' (f‚ÇÅ := 0)
    ¬∑ filter_upwards [meromorphicOrderAt_eq_top_iff.1 h‚ÇÇ] with y hy
      simp_all
    ¬∑ apply Tendsto.congr' (f‚ÇÅ := 0) (by rfl) continuousWithinAt_const.tendsto
  obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_ne_top_iff h).1 h‚ÇÇ
  apply Tendsto.congr' (f‚ÇÅ := g)
  ¬∑ filter_upwards [h‚ÇÉg, self_mem_nhdsWithin] with y h‚ÇÅy h‚ÇÇy
    rw [zpow_neg, Pi.smul_apply', Pi.inv_apply, Pi.pow_apply, h‚ÇÅy, ‚Üê smul_assoc, smul_eq_mul,
      ‚Üê zpow_neg, ‚Üê zpow_add', neg_add_cancel, zpow_zero, one_smul]
    left
    simp_all [sub_ne_zero]
  ¬∑ rw [leadCoeff_of_order_eq_finite h h‚ÇÅg h‚ÇÇ h‚ÇÉg]
    apply h‚ÇÅg.continuousAt.continuousWithinAt

/-!
## Elementary Properties
-/

/--
If `f` is meromorphic of finite order at `x`, then the leading coefficient is not zero.
-/
lemma zero_ne_leadCoeff (h‚ÇÅ : MeromorphicAt f x) (h‚ÇÇ : meromorphicOrderAt f x ‚â† ‚ä§) :
    0 ‚â† leadCoeff f x := by
  obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_ne_top_iff h‚ÇÅ).1 h‚ÇÇ
  simpa [h‚ÇÅg.leadCoeff_of_order_eq_finite‚ÇÅ h‚ÇÇg h‚ÇÉg] using h‚ÇÇg.symm

/-!
## Congruence Lemma
-/

/--
If two functions agree in a punctured neighborhood, then their leading coefficients agree.
-/
lemma leadCoeff_congr_nhdNE {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí E} (h : f‚ÇÅ =·∂†[ùìù[‚â†] x] f‚ÇÇ) :
    leadCoeff f‚ÇÅ x = leadCoeff f‚ÇÇ x := by
  by_cases h‚ÇÅ : ¬¨MeromorphicAt f‚ÇÅ x
  ¬∑ simp [h‚ÇÅ, (MeromorphicAt.meromorphicAt_congr h).not.1 h‚ÇÅ]
  rw [not_not] at h‚ÇÅ
  by_cases h‚ÇÇ : meromorphicOrderAt f‚ÇÅ x = ‚ä§
  ¬∑ simp_all [h‚ÇÅ.congr h, meromorphicOrderAt_congr h]
  obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_ne_top_iff h‚ÇÅ).1 h‚ÇÇ
  rw [h‚ÇÅg.leadCoeff_of_order_eq_finite‚ÇÅ h‚ÇÇg h‚ÇÉg,
    h‚ÇÅg.leadCoeff_of_order_eq_finite‚ÇÅ h‚ÇÇg (h.symm.trans h‚ÇÉg)]

/-!
## Behavior under Arithmetic Operations
-/

/--
The leading coefficient of a scalar product is the scalar product of the leading coefficients.
-/
lemma leadCoeff_smul {f‚ÇÅ : ùïú ‚Üí ùïú} {f‚ÇÇ : ùïú ‚Üí E} (hf‚ÇÅ : MeromorphicAt f‚ÇÅ x)
    (hf‚ÇÇ : MeromorphicAt f‚ÇÇ x) :
    leadCoeff (f‚ÇÅ ‚Ä¢ f‚ÇÇ) x = (leadCoeff f‚ÇÅ x) ‚Ä¢ (leadCoeff f‚ÇÇ x) := by
  by_cases h‚ÇÅf‚ÇÅ : meromorphicOrderAt f‚ÇÅ x = ‚ä§
  ¬∑ simp_all [hf‚ÇÅ, hf‚ÇÅ.smul hf‚ÇÇ, meromorphicOrderAt_smul hf‚ÇÅ hf‚ÇÇ, h‚ÇÅf‚ÇÅ]
  by_cases h‚ÇÅf‚ÇÇ : meromorphicOrderAt f‚ÇÇ x = ‚ä§
  ¬∑ simp_all [hf‚ÇÅ, hf‚ÇÅ.smul hf‚ÇÇ, meromorphicOrderAt_smul hf‚ÇÅ hf‚ÇÇ, h‚ÇÅf‚ÇÅ]
  obtain ‚ü®g‚ÇÅ, h‚ÇÅg‚ÇÅ, h‚ÇÇg‚ÇÅ, h‚ÇÉg‚ÇÅ‚ü© := (meromorphicOrderAt_ne_top_iff hf‚ÇÅ).1 h‚ÇÅf‚ÇÅ
  obtain ‚ü®g‚ÇÇ, h‚ÇÅg‚ÇÇ, h‚ÇÇg‚ÇÇ, h‚ÇÉg‚ÇÇ‚ü© := (meromorphicOrderAt_ne_top_iff hf‚ÇÇ).1 h‚ÇÅf‚ÇÇ
  have : f‚ÇÅ ‚Ä¢ f‚ÇÇ =·∂†[ùìù[‚â†] x]
      fun z ‚Ü¶ (z - x) ^ (meromorphicOrderAt (f‚ÇÅ ‚Ä¢ f‚ÇÇ) x).untop‚ÇÄ ‚Ä¢ (g‚ÇÅ ‚Ä¢ g‚ÇÇ) z := by
    filter_upwards [h‚ÇÉg‚ÇÅ, h‚ÇÉg‚ÇÇ, self_mem_nhdsWithin] with y h‚ÇÅy h‚ÇÇy h‚ÇÉy
    simp_all [meromorphicOrderAt_smul hf‚ÇÅ hf‚ÇÇ]
    rw [‚Üê smul_assoc, ‚Üê smul_assoc, smul_eq_mul, smul_eq_mul, zpow_add‚ÇÄ (sub_ne_zero.2 h‚ÇÉy)]
    ring_nf
  rw [h‚ÇÅg‚ÇÅ.leadCoeff_of_order_eq_finite‚ÇÅ h‚ÇÇg‚ÇÅ h‚ÇÉg‚ÇÅ,
    h‚ÇÅg‚ÇÇ.leadCoeff_of_order_eq_finite‚ÇÅ h‚ÇÇg‚ÇÇ h‚ÇÉg‚ÇÇ,
    leadCoeff_of_order_eq_finite (hf‚ÇÅ.smul hf‚ÇÇ) (h‚ÇÅg‚ÇÅ.smul h‚ÇÅg‚ÇÇ)
      (by simp_all [meromorphicOrderAt_smul hf‚ÇÅ hf‚ÇÇ]) this]
  simp

/--
The leading coefficient of a product is the product of the leading coefficients.
-/
lemma leadCoeff_mul {f‚ÇÅ f‚ÇÇ : ùïú ‚Üí ùïú} (hf‚ÇÅ : MeromorphicAt f‚ÇÅ x)
    (hf‚ÇÇ : MeromorphicAt f‚ÇÇ x) :
    leadCoeff (f‚ÇÅ * f‚ÇÇ) x = (leadCoeff f‚ÇÅ x) * (leadCoeff f‚ÇÇ x) := by
  exact leadCoeff_smul hf‚ÇÅ hf‚ÇÇ

/--
The leading coefficient of the inverse function is the inverse of the leading coefficient.
-/
lemma leadCoeff_inv {f : ùïú ‚Üí ùïú} :
    leadCoeff f‚Åª¬π x = (leadCoeff f x)‚Åª¬π := by
  by_cases h‚ÇÅ : MeromorphicAt f x
  ¬∑ by_cases h‚ÇÇ : meromorphicOrderAt f x = ‚ä§
    ¬∑ simp_all [meromorphicOrderAt_inv (f := f) (x := x)]
    have : f‚Åª¬π * f =·∂†[ùìù[‚â†] x] 1 := by
      filter_upwards [(meromorphicOrderAt_ne_top_iff_eventually_ne_zero h‚ÇÅ).1 h‚ÇÇ]
      simp_all
    rw [‚Üê mul_eq_one_iff_eq_inv‚ÇÄ (h‚ÇÅ.zero_ne_leadCoeff h‚ÇÇ).symm,
      ‚Üê leadCoeff_mul h‚ÇÅ.inv h‚ÇÅ, leadCoeff_congr_nhdNE this,
      analyticAt_const.leadCoeff_of_order_eq_finite‚ÇÅ (n := 0)]
    <;> simp_all
    exact eventuallyEq_nhdsWithin_of_eqOn fun _ ‚Ü¶ congrFun rfl
  ¬∑ simp_all

/--
Except for edge cases, the leading coefficient of the power of a function is the power of the
leading coefficient.
-/
lemma leadCoeff_zpow‚ÇÅ {f : ùïú ‚Üí ùïú} (h‚ÇÅ : MeromorphicAt f x) (h‚ÇÇ : meromorphicOrderAt f x ‚â† ‚ä§) :
    leadCoeff (f ^ n) x = (leadCoeff f x) ^ n := by
  obtain ‚ü®g, h‚ÇÅg, h‚ÇÇg, h‚ÇÉg‚ü© := (meromorphicOrderAt_ne_top_iff h‚ÇÅ).1 h‚ÇÇ
  rw [h‚ÇÅg.leadCoeff_of_order_eq_finite‚ÇÅ (n := (meromorphicOrderAt f x).untop‚ÇÄ) h‚ÇÇg h‚ÇÉg,
    (h‚ÇÅg.zpow h‚ÇÇg (n := n)).leadCoeff_of_order_eq_finite‚ÇÅ
      (n := (meromorphicOrderAt (f ^ n) x).untop‚ÇÄ)
      (by simp_all [h‚ÇÇg, zpow_ne_zero])]
  ¬∑ simp only [Pi.pow_apply]
  ¬∑ filter_upwards [h‚ÇÉg] with a ha
    simp_all [ha, mul_zpow, ‚Üê zpow_mul, meromorphicOrderAt_zpow h‚ÇÅ, mul_comm]

/--
Except for edge cases, the leading coefficient of the power of a function is the power of the
leading coefficient.
-/
lemma leadCoeff_zpow‚ÇÇ {f : ùïú ‚Üí ùïú} (h : MeromorphicAt f x) (hn : n ‚â† 0):
    leadCoeff (f ^ n) x = (leadCoeff f x) ^ n := by
  by_cases h‚ÇÅ : meromorphicOrderAt f x = ‚ä§
  ¬∑ simp_all [meromorphicOrderAt_zpow h, h‚ÇÅ, h.zpow n, zero_zpow n hn]
  apply leadCoeff_zpow‚ÇÅ h h‚ÇÅ

/--
Except for edge cases, the leading coefficient of the power of a function is the power of the
leading coefficient.
-/
lemma leadCoeff_pow‚ÇÅ {n : ‚Ñï} {f : ùïú ‚Üí ùïú} (h‚ÇÅ : MeromorphicAt f x)
    (h‚ÇÇ : meromorphicOrderAt f x ‚â† ‚ä§) :
    leadCoeff (f ^ n) x = (leadCoeff f x) ^ n := by
  convert leadCoeff_zpow‚ÇÅ h‚ÇÅ h‚ÇÇ (n := n)
  <;> simp

/--
Except for edge cases, the leading coefficient of the power of a function is the power of the
leading coefficient.
-/
lemma leadCoeff_pow‚ÇÇ {n : ‚Ñï} {f : ùïú ‚Üí ùïú} (h : MeromorphicAt f x) (hn : n ‚â† 0):
    leadCoeff (f ^ n) x = (leadCoeff f x) ^ n := by
  convert leadCoeff_zpow‚ÇÇ h (n := n) (Int.ofNat_ne_zero.mpr hn)
  <;> simp

end MeromorphicAt
