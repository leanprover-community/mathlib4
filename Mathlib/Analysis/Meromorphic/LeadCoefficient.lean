/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Order

/-!
# The Leading Coefficient of a Meromorphic Function

This file defines the leading coefficient of a meromorphic function. If `f` is
meromorphic at a point `x`, the leading coefficient is defined as the (unique!)
value `g x` for a presentation of `f` in the form `(z - x) ^ order â€¢ g z` with
`g` analytic at `x`.

The lemma `leadCoefficient_eq_limit` expresses the leading term as a limit.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {f g : ğ•œ â†’ E} {x : ğ•œ} {n : â„¤}

open Filter Topology

namespace MeromorphicAt

variable (f x) in
/--
If `f` is meromorphic of finite order at a point `x`, the leading coefficient is
defined as the (unique!) value `g x` for a presentation of `f` in the form `(z -
x) ^ order â€¢ g z` with `g` analytic at `x`. In all other cases, the leading
coefficient is defined to be zero.
-/
noncomputable def leadCoefficient : E := by
  by_cases hâ‚ : Â¬MeromorphicAt f x
  Â· exact 0
  rw [not_not] at hâ‚
  by_cases hâ‚‚ : hâ‚.order = âŠ¤
  Â· exact 0
  exact (hâ‚.order_ne_top_iff.1 hâ‚‚).choose x

/--
If `f` is not meromorphic at `x`, the leading coefficient is zero by definition.
-/
@[simp] lemma leadCoefficient_of_not_MeromorphicAt (h : Â¬MeromorphicAt f x) :
    leadCoefficient f x = 0 := by simp_all [leadCoefficient]

/--
If `f` is meromorphic of infinite order at `x`, the leading coefficient is zero
by definition.
-/
@[simp] lemma leadCoefficient_of_order_eq_top (hâ‚ : MeromorphicAt f x) (hâ‚‚ : hâ‚.order = âŠ¤) :
    leadCoefficient f x = 0 := by simp_all [leadCoefficient]

/-!
## Characterization of the Leading Coefficient
-/

/--
Definition of the leading coefficient in case where `f` is meromorphic of finite
order and a presentation is given.
-/
@[simp]
lemma leadCoefficient_of_order_eq_finite (hâ‚ : MeromorphicAt f x) (hâ‚‚ : AnalyticAt ğ•œ g x)
    (hâ‚ƒ : hâ‚.order â‰  âŠ¤) (hâ‚„ : f =á¶ [ğ“[â‰ ] x] fun z â†¦ (z - x) ^ hâ‚.order.untopâ‚€ â€¢ g z) :
    leadCoefficient f x = g x := by
  unfold leadCoefficient
  simp only [hâ‚, not_true_eq_false, reduceDIte, hâ‚ƒ, ne_eq]
  obtain âŸ¨h'â‚, h'â‚‚, h'â‚ƒâŸ© := (hâ‚.order_ne_top_iff.1 hâ‚ƒ).choose_spec
  apply Filter.EventuallyEq.eq_of_nhds
  rw [â† h'â‚.continuousAt.eventuallyEq_nhd_iff_eventuallyEq_nhdNE hâ‚‚.continuousAt]
  filter_upwards [hâ‚„, h'â‚ƒ, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y hâ‚ƒy
  rw [â† sub_eq_zero]
  rwa [hâ‚‚y, â† sub_eq_zero, â† smul_sub, smul_eq_zero_iff_right] at hâ‚y
  simp_all [zpow_ne_zero, sub_ne_zero]

/--
Variant of `leadCoefficient_of_order_eq_finite`: Definition of the leading
coefficient in case where `f` is meromorphic of finite order and a presentation
is given.
-/
@[simp]
lemma _root_.AnalyticAt.leadCoefficient_of_order_eq_finiteâ‚ (hâ‚ : AnalyticAt ğ•œ g x) (hâ‚‚ : g x â‰  0)
    (hâ‚ƒ : f =á¶ [ğ“[â‰ ] x] fun z â†¦ (z - x) ^ n â€¢ g z) :
    leadCoefficient f x = g x := by
  have hâ‚„ : MeromorphicAt f x := by
    rw [MeromorphicAt.meromorphicAt_congr hâ‚ƒ]
    fun_prop
  have : hâ‚„.order = n := by
    simp only [hâ‚„.order_eq_int_iff, ne_eq, zpow_natCast]
    use g, hâ‚, hâ‚‚
    exact hâ‚ƒ
  simp_all [leadCoefficient_of_order_eq_finite hâ‚„ hâ‚, this]

/--
If `f` is analytic and does not vanish at `x`, then the leading coefficient of
`f` at `x` is `f x`.
-/
@[simp]
lemma _root_.AnalyticAt.leadCoefficient_of_nonvanish (hâ‚ : AnalyticAt ğ•œ f x) (hâ‚‚ : f x â‰  0) :
    leadCoefficient f x = f x := by
  rw [hâ‚.leadCoefficient_of_order_eq_finiteâ‚ (n := 0) hâ‚‚]
  filter_upwards
  simp

/--
If `f` is meromorphic at `x`, then the leading coefficient of `f` at `x` is the
limit of the function `(Â· - x) ^ (-hâ‚.order.untopâ‚€) â€¢ f`.
-/
lemma leadCoefficient_eq_limit (h : MeromorphicAt f x) :
    Tendsto ((Â· - x) ^ (-h.order.untopâ‚€) â€¢ f) (ğ“[â‰ ] x) (ğ“ (leadCoefficient f x)) := by
  by_cases hâ‚‚ : h.order = âŠ¤
  Â· simp_all only [WithTop.untopâ‚€_top, neg_zero, zpow_zero, one_smul, leadCoefficient_of_order_eq_top]
    apply Tendsto.congr' (fâ‚ := 0)
    filter_upwards [h.order_eq_top_iff.1 hâ‚‚] with y hy
    Â· simp_all
    Â· apply Tendsto.congr' (fâ‚ := 0) (by rfl) continuousWithinAt_const.tendsto
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := h.order_ne_top_iff.1 hâ‚‚
  apply Tendsto.congr' (fâ‚ := g)
  Â· filter_upwards [hâ‚ƒg, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y
    rw [zpow_neg, Pi.smul_apply', Pi.inv_apply, Pi.pow_apply, hâ‚y, â† smul_assoc, smul_eq_mul, â† zpow_neg,
      â† zpow_add', neg_add_cancel, zpow_zero, one_smul]
    left
    simp_all [sub_ne_zero]
  Â· rw [leadCoefficient_of_order_eq_finite h hâ‚g hâ‚‚ hâ‚ƒg]
    apply hâ‚g.continuousAt.continuousWithinAt

/-!
## Elementary Properties
-/

/--
If `f` is meromorphic of finite order at `x`, then the leading coefficient is
not zero.
-/
lemma zero_ne_leadCoefficient (hâ‚ : MeromorphicAt f x) (hâ‚‚ : hâ‚.order â‰  âŠ¤) :
    0 â‰  leadCoefficient f x := by
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hâ‚.order_ne_top_iff.1 hâ‚‚
  simpa [hâ‚g.leadCoefficient_of_order_eq_finiteâ‚ hâ‚‚g hâ‚ƒg] using hâ‚‚g.symm

/-!
## Congruence Lemma
-/

/--
If two functions agree in a punctured neighborhood, then their leading coefficients agree.
-/
lemma leadCoefficient_congr_nhdNE {fâ‚ fâ‚‚ : ğ•œ â†’ E} (h : fâ‚ =á¶ [ğ“[â‰ ] x] fâ‚‚) :
    leadCoefficient fâ‚ x = leadCoefficient fâ‚‚ x := by
  by_cases hâ‚ : Â¬MeromorphicAt fâ‚ x
  Â· simp [hâ‚, (MeromorphicAt.meromorphicAt_congr h).not.1 hâ‚]
  rw [not_not] at hâ‚
  by_cases hâ‚‚ : hâ‚.order = âŠ¤
  Â· simp_all [hâ‚.congr h, hâ‚.order_congr h]
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hâ‚.order_ne_top_iff.1 hâ‚‚
  rw [hâ‚g.leadCoefficient_of_order_eq_finiteâ‚ hâ‚‚g hâ‚ƒg,
    hâ‚g.leadCoefficient_of_order_eq_finiteâ‚ hâ‚‚g (h.symm.trans hâ‚ƒg)]

/-!
## Behavior under Arithmetic Operations
-/

/--
The leading coefficient of a scalar product is the scalar product of the leading coefficients.
-/
lemma leadCoefficient_smul {fâ‚ : ğ•œ â†’ ğ•œ} {fâ‚‚ : ğ•œ â†’ E} (hfâ‚ : MeromorphicAt fâ‚ x)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    leadCoefficient (fâ‚ â€¢ fâ‚‚) x = (leadCoefficient fâ‚ x) â€¢ (leadCoefficient fâ‚‚ x) := by
  by_cases hâ‚fâ‚ : hfâ‚.order = âŠ¤
  Â· simp_all [hfâ‚, hfâ‚.smul hfâ‚‚, hfâ‚.order_smul hfâ‚‚, hâ‚fâ‚]
  by_cases hâ‚fâ‚‚ : hfâ‚‚.order = âŠ¤
  Â· simp_all [hfâ‚, hfâ‚.smul hfâ‚‚, hfâ‚.order_smul hfâ‚‚, hâ‚fâ‚]
  obtain âŸ¨gâ‚, hâ‚gâ‚, hâ‚‚gâ‚, hâ‚ƒgâ‚âŸ© := hfâ‚.order_ne_top_iff.1 hâ‚fâ‚
  obtain âŸ¨gâ‚‚, hâ‚gâ‚‚, hâ‚‚gâ‚‚, hâ‚ƒgâ‚‚âŸ© := hfâ‚‚.order_ne_top_iff.1 hâ‚fâ‚‚
  have : fâ‚ â€¢ fâ‚‚ =á¶ [ğ“[â‰ ] x] fun z â†¦ (z - x) ^ (hfâ‚.smul hfâ‚‚).order.untopâ‚€ â€¢ (gâ‚ â€¢ gâ‚‚) z := by
    filter_upwards [hâ‚ƒgâ‚, hâ‚ƒgâ‚‚, self_mem_nhdsWithin] with y hâ‚y hâ‚‚y hâ‚ƒy
    simp_all [hfâ‚.order_smul hfâ‚‚]
    rw [â† smul_assoc, â† smul_assoc, smul_eq_mul, smul_eq_mul, zpow_addâ‚€ (sub_ne_zero.2 hâ‚ƒy)]
    ring_nf
  rw [hâ‚gâ‚.leadCoefficient_of_order_eq_finiteâ‚ hâ‚‚gâ‚ hâ‚ƒgâ‚,
    hâ‚gâ‚‚.leadCoefficient_of_order_eq_finiteâ‚ hâ‚‚gâ‚‚ hâ‚ƒgâ‚‚,
    leadCoefficient_of_order_eq_finite (hfâ‚.smul hfâ‚‚) (hâ‚gâ‚.smul hâ‚gâ‚‚)
      (by simp_all [hfâ‚.order_smul hfâ‚‚]) this]
  simp

/--
The leading coefficient of a product is the product of the leading coefficients.
-/
lemma leadCoefficient_mul {fâ‚ fâ‚‚ : ğ•œ â†’ ğ•œ} (hfâ‚ : MeromorphicAt fâ‚ x)
    (hfâ‚‚ : MeromorphicAt fâ‚‚ x) :
    leadCoefficient (fâ‚ * fâ‚‚) x = (leadCoefficient fâ‚ x) * (leadCoefficient fâ‚‚ x) := by
  exact leadCoefficient_smul hfâ‚ hfâ‚‚

theorem order_ne_top_iffâ‚‚ {f : ğ•œ â†’ E} (hf : MeromorphicAt f x) :
    hf.order â‰  âŠ¤ â†” âˆ€á¶  x in ğ“[â‰ ] x, f x â‰  0 := by
  constructor
  Â· intro h
    obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hf.order_ne_top_iff.1 h
    filter_upwards [hâ‚ƒg, self_mem_nhdsWithin, eventually_nhdsWithin_of_eventually_nhds
      ((hâ‚g.continuousAt.ne_iff_eventually_ne continuousAt_const).mp hâ‚‚g)]
    simp_all [zpow_ne_zero, sub_ne_zero]
  Â· simp_all [hf.order_eq_top_iff, Eventually.frequently]

/--
The leading coefficient of the inverse function is the inverse of the leading
coefficient.
-/
lemma leadCoefficient_inv {f : ğ•œ â†’ ğ•œ} :
    leadCoefficient fâ»Â¹ x = (leadCoefficient f x)â»Â¹ := by
  by_cases hâ‚ : MeromorphicAt f x
  Â· by_cases hâ‚‚ : hâ‚.order = âŠ¤
    Â· simp_all [hâ‚.order_inv]
    have : fâ»Â¹ * f =á¶ [ğ“[â‰ ] x] 1 := by
      filter_upwards [hâ‚.order_ne_top_iffâ‚‚.1 hâ‚‚]
      simp_all
    rw [â† mul_eq_one_iff_eq_invâ‚€ (hâ‚.zero_ne_leadCoefficient hâ‚‚).symm,
      â† leadCoefficient_mul hâ‚.inv hâ‚, leadCoefficient_congr_nhdNE this,
      analyticAt_const.leadCoefficient_of_order_eq_finiteâ‚ (n := 0)]
    <;> simp_all
    exact eventuallyEq_nhdsWithin_of_eqOn fun _ â†¦ congrFun rfl
  Â· simp_all

/--
Except for edge cases, the leading coefficient of the power of a function is the
power of the leading coefficient.
-/
lemma leadCoefficient_zpowâ‚ {f : ğ•œ â†’ ğ•œ} (hâ‚ : MeromorphicAt f x) (hâ‚‚ : hâ‚.order â‰  âŠ¤) :
    leadCoefficient (f ^ n) x = (leadCoefficient f x) ^ n := by
  obtain âŸ¨g, hâ‚g, hâ‚‚g, hâ‚ƒgâŸ© := hâ‚.order_ne_top_iff.1 hâ‚‚
  rw [hâ‚g.leadCoefficient_of_order_eq_finiteâ‚ (n := hâ‚.order.untopâ‚€) hâ‚‚g hâ‚ƒg,
    (hâ‚g.zpow hâ‚‚g (n := n)).leadCoefficient_of_order_eq_finiteâ‚ (n := (hâ‚.zpow n).order.untopâ‚€)
      (by simp_all [hâ‚‚g, zpow_ne_zero])]
  simp only [Pi.pow_apply]
  filter_upwards [hâ‚ƒg] with a ha
  simp_all [ha, mul_zpow, â† zpow_mul, hâ‚.order_zpow, mul_comm]

/--
Except for edge cases, the leading coefficient of the power of a function is the
power of the leading coefficient.
-/
lemma leadCoefficient_zpowâ‚‚ {f : ğ•œ â†’ ğ•œ} (h : MeromorphicAt f x) (hn : n â‰  0):
    leadCoefficient (f ^ n) x = (leadCoefficient f x) ^ n := by
  by_cases hâ‚ : h.order = âŠ¤
  Â· simp_all [h.order_zpow, hâ‚, h.zpow n, zero_zpow n hn]
  apply leadCoefficient_zpowâ‚ h hâ‚

/--
Except for edge cases, the leading coefficient of the power of a function is the
power of the leading coefficient.
-/
lemma leadCoefficient_powâ‚ {n : â„•} {f : ğ•œ â†’ ğ•œ} (hâ‚ : MeromorphicAt f x) (hâ‚‚ : hâ‚.order â‰  âŠ¤) :
    leadCoefficient (f ^ n) x = (leadCoefficient f x) ^ n := by
  convert leadCoefficient_zpowâ‚ hâ‚ hâ‚‚ (n := n)
  <;> simp

/--
Except for edge cases, the leading coefficient of the power of a function is the
power of the leading coefficient.
-/
lemma leadCoefficient_powâ‚‚ {n : â„•} {f : ğ•œ â†’ ğ•œ} (h : MeromorphicAt f x) (hn : n â‰  0):
    leadCoefficient (f ^ n) x = (leadCoefficient f x) ^ n := by
  convert leadCoefficient_zpowâ‚‚ h (n := n) (Int.ofNat_ne_zero.mpr hn)
  <;> simp

end MeromorphicAt
