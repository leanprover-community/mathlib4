/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Meromorphic.Basic

/-!
# Principles of Isolated Zeros and Identitiy Principles for Meromorphic Functions

In line with results in `Mathlib.Analysis.Analytic.IsolatedZeros` and
`Mathlib.Analysis.Analytic.Uniqueness`, this file establishes principles of isolated zeros and
identitiy principles for meromorphic functions.

Compared to the results for analytic function, the principles established here are a litte more
complicated to state. This is because meromorphic functions can be modified at will along discrete
subsets and still remain meromorphic.
-/

variable
  {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {U : Set ğ•œ} {x : ğ•œ} {f g : ğ•œ â†’ E}

open Filter Topology

namespace MeromorphicAt

/-!
## Principles of Isolated Zeros
-/

/--
If `f` is mermorphic at `x`, then `f` vanishes in a punctured neighborhood of `x` iff it vanishes at
points arbitrarily close to (but different from) `x`.

See `AnalyticAt.frequently_zero_iff_eventually_zero` for a stronger result in the analytic case.
-/
theorem frequently_zero_iff_eventually_zero (hf : MeromorphicAt f x) :
    (âˆƒá¶  z in ğ“[â‰ ] x, f z = 0) â†” âˆ€á¶  z in ğ“[â‰ ] x, f z = 0 :=
  âŸ¨hf.eventually_eq_zero_or_eventually_ne_zero.resolve_right, fun h â†¦ h.frequentlyâŸ©

/--
Variant of `MeromorphicAt.frequently_zero_iff_eventuallyEq_zero`: If `f` is mermorphic at `x`, then
`f` vanishes eventually in a punctured neighborhood of `x` iff it vanishes at points arbitrarily
close to (but different from) `x`.

See `AnalyticAt.frequently_zero_iff_eventually_zero` for a stronger result in the analytic case.
-/
theorem frequently_zero_iff_eventuallyEq_zero (hf : MeromorphicAt f x) :
    (âˆƒá¶  z in ğ“[â‰ ] x, f z = 0) â†” f =á¶ [ğ“[â‰ ] x] 0 :=
  âŸ¨hf.eventually_eq_zero_or_eventually_ne_zero.resolve_right, fun h â†¦ h.frequentlyâŸ©

/--
Variant of the principle of isolated zeros: Let `U` be a subset of `ğ•œ` and assume that `x âˆˆ U` is
not an isolated point of `U`. If a function `f` is meromorphic at `x` and vanishes along a subset
that is codiscrete within `U`, then `f` vanishes in a punctured neighbourhood of `f`.

For a typical application, let `U` be a path in the complex plane and let `x` be one of the end
points. If `f` is meromorphic at `x` and vanishes on `U`, then it will vanish in a punctured
neighbourhood of `x`, which intersects `U` non-trivally but is not contained in `U`.

The assumption that `x` is not an isolated point of `U` is expressed as `Uá¶œ âˆ‰ ğ“[â‰ ] x`.
-/
theorem eventuallyEq_zero_nhdNE_of_eventuallyEq_zero_codiscreteWithin (hf : MeromorphicAt f x)
    (hâ‚x : x âˆˆ U) (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x) (h : f =á¶ [codiscreteWithin U] 0) :
    f =á¶ [ğ“[â‰ ] x] 0 := by
  rw [â† hf.frequently_zero_iff_eventuallyEq_zero]
  by_contra hCon
  rw [EventuallyEq, Filter.Eventually, mem_codiscreteWithin] at h
  have := h x hâ‚x
  simp only [Pi.zero_apply, disjoint_principal_right, Set.compl_diff] at this
  have := inter_mem (not_frequently.1 hCon) this
  simp_all [Set.inter_union_distrib_left, (by tauto_set : {x | Â¬f x = 0} âˆ© {x | f x = 0} = âˆ…)]

/-!
## Identity Principles
-/

/--
Formulation of `MeromorphicAt.frequently_zero_iff_eventuallyEq_zero` as an identity principle: If
`f` and `g` are mermorphic at `x`, then `f` and `g` agree eventually in a punctured neighborhood of
`x` iff they agree at points arbitrarily close to (but different from) `x`.
-/
theorem frequently_eq_iff_eventuallyEq (hf : MeromorphicAt f x) (hg : MeromorphicAt g x) :
    (âˆƒá¶  z in ğ“[â‰ ] x, f z = g z) â†” f =á¶ [ğ“[â‰ ] x] g := by
  rw [eventuallyEq_iff_sub, â† (hf.sub hg).frequently_zero_iff_eventuallyEq_zero]
  simp_rw [Pi.sub_apply, sub_eq_zero]

/--
Formulation of `MeromorphicAt.eventuallyEq_zero_nhdNE_of_eventuallyEq_zero_codiscreteWithin` as an
identity principle: Let `U` be a subset of `ğ•œ` and assume that `x âˆˆ U` is not an isolated point of
`U`. If function `f` and `g` are meromorphic at `x` and agree along a subset that is codiscrete
within `U`, then `f` and `g` agree in a punctured neighbourhood of `f`.
-/
theorem eventuallyEq_nhdNE_of_eventuallyEq_codiscreteWithin (hf : MeromorphicAt f x)
    (hg : MeromorphicAt g x) (hâ‚x : x âˆˆ U) (hâ‚‚x : Uá¶œ âˆ‰ ğ“[â‰ ] x) (h : f =á¶ [codiscreteWithin U] g) :
    f =á¶ [ğ“[â‰ ] x] g := by
  rw [eventuallyEq_iff_sub] at *
  apply (hf.sub hg).eventuallyEq_zero_nhdNE_of_eventuallyEq_zero_codiscreteWithin hâ‚x hâ‚‚x h

end MeromorphicAt
