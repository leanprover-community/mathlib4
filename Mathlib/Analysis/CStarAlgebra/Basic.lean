/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Algebra.Star.Pi
import Mathlib.Algebra.Star.SelfAdjoint
import Mathlib.Algebra.Star.Subalgebra
import Mathlib.Algebra.Star.Unitary
import Mathlib.Data.Real.Star
import Mathlib.Topology.Algebra.Module.Star

/-!
# Normed star rings and algebras

A normed star group is a normed group with a compatible `star` which is isometric.

A Câ‹†-ring is a normed star group that is also a ring and that verifies the stronger
condition `â€–xâ€–^2 â‰¤ â€–xâ‹† * xâ€–` for all `x` (which actually implies equality). If a Câ‹†-ring is also
a star algebra, then it is a Câ‹†-algebra.

Note that the type classes corresponding to Câ‹†-algebras are defined in
`Mathlib/Analysis/CStarAlgebra/Classes`.

## TODO

- Show that `â€–xâ‹† * xâ€– = â€–xâ€–^2` is equivalent to `â€–xâ‹† * xâ€– = â€–xâ‹†â€– * â€–xâ€–`, which is used as the
  definition of C*-algebras in some sources (e.g. Wikipedia).

-/

assert_not_exists ContinuousLinearMap.hasOpNorm

open Topology

local postfix:max "â‹†" => star

/-- A normed star group is a normed group with a compatible `star` which is isometric. -/
class NormedStarGroup (E : Type*) [SeminormedAddCommGroup E] [StarAddMonoid E] : Prop where
  norm_star_le : âˆ€ x : E, â€–xâ‹†â€– â‰¤ â€–xâ€–

variable {ğ•œ E Î± : Type*}

section NormedStarGroup

variable [SeminormedAddCommGroup E] [StarAddMonoid E] [NormedStarGroup E]

@[simp]
lemma norm_star (x : E) : â€–xâ‹†â€– = â€–xâ€– :=
  le_antisymm (NormedStarGroup.norm_star_le x) (by simpa using NormedStarGroup.norm_star_le xâ‹†)

@[simp]
theorem nnnorm_star (x : E) : â€–star xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| norm_star _

/-- The `star` map in a normed star group is a normed group homomorphism. -/
def starNormedAddGroupHom : NormedAddGroupHom E E :=
  { starAddEquiv with bound' := âŸ¨1, fun _ => le_trans (norm_star _).le (one_mul _).symm.leâŸ© }

/-- The `star` map in a normed star group is an isometry -/
theorem star_isometry : Isometry (star : E â†’ E) :=
  show Isometry starAddEquiv from
    AddMonoidHomClass.isometry_of_norm starAddEquiv (show âˆ€ x, â€–xâ‹†â€– = â€–xâ€– from norm_star)

instance (priority := 100) NormedStarGroup.to_continuousStar : ContinuousStar E :=
  âŸ¨star_isometry.continuousâŸ©

noncomputable
instance [NormedField ğ•œ] [NormedSpace ğ•œ E] [Star ğ•œ] [TrivialStar ğ•œ] [StarModule ğ•œ E] :
    NormedSpace ğ•œ (selfAdjoint E) where
  norm_smul_le _ _ := norm_smul_le _ (_ : E)

end NormedStarGroup

instance RingHomIsometric.starRingEnd [NormedCommRing E] [StarRing E] [NormedStarGroup E] :
    RingHomIsometric (starRingEnd E) :=
  âŸ¨@norm_star _ _ _ _âŸ©

/-- A C*-ring is a normed star ring that satisfies the stronger condition `â€–xâ€– ^ 2 â‰¤ â€–xâ‹† * xâ€–`
for every `x`. Note that this condition actually implies equality, as is shown in
`norm_star_mul_self` below. -/
class CStarRing (E : Type*) [NonUnitalNormedRing E] [StarRing E] : Prop where
  norm_mul_self_le : âˆ€ x : E, â€–xâ€– * â€–xâ€– â‰¤ â€–xâ‹† * xâ€–

instance : CStarRing â„ where
  norm_mul_self_le x := by
    simp only [Real.norm_eq_abs, abs_mul_abs_self, star, id, norm_mul, le_refl]

namespace CStarRing

section NonUnital

lemma of_le_norm_mul_star_self
    [NonUnitalNormedRing E] [StarRing E]
    (h : âˆ€ x : E, â€–xâ€– * â€–xâ€– â‰¤ â€–x * xâ‹†â€–) : CStarRing E :=
  have : NormedStarGroup E :=
    { norm_star_le x := by
        obtain (hx | hx) := eq_zero_or_norm_pos xâ‹†
        Â· simp [hx]
        Â· refine le_of_mul_le_mul_right ?_ hx
          simpa [sq, mul_comm â€–xâ‹†â€–] using h xâ‹† |>.trans <| norm_mul_le _ _ }
  âŸ¨star_involutive.surjective.forall.mpr <| by simpaâŸ©

variable [NonUnitalNormedRing E] [StarRing E] [CStarRing E]

-- see Note [lower instance priority]
/-- In a C*-ring, star preserves the norm. -/
instance (priority := 100) to_normedStarGroup : NormedStarGroup E where
  norm_star_le x := by
    obtain (hx | hx) := eq_zero_or_norm_pos xâ‹†
    Â· simp [hx]
    Â· refine le_of_mul_le_mul_right ?_ hx
      simpa using norm_mul_self_le (x := xâ‹†) |>.trans <| norm_mul_le _ _

theorem norm_star_mul_self {x : E} : â€–xâ‹† * xâ€– = â€–xâ€– * â€–xâ€– :=
  le_antisymm ((norm_mul_le _ _).trans (by rw [norm_star])) (CStarRing.norm_mul_self_le x)

theorem norm_self_mul_star {x : E} : â€–x * xâ‹†â€– = â€–xâ€– * â€–xâ€– := by
  nth_rw 1 [â† star_star x]
  simp only [norm_star_mul_self, norm_star]

theorem norm_star_mul_self' {x : E} : â€–xâ‹† * xâ€– = â€–xâ‹†â€– * â€–xâ€– := by rw [norm_star_mul_self, norm_star]

theorem nnnorm_self_mul_star {x : E} : â€–x * xâ‹†â€–â‚Š = â€–xâ€–â‚Š * â€–xâ€–â‚Š :=
  Subtype.ext norm_self_mul_star

theorem nnnorm_star_mul_self {x : E} : â€–xâ‹† * xâ€–â‚Š = â€–xâ€–â‚Š * â€–xâ€–â‚Š :=
  Subtype.ext norm_star_mul_self

lemma _root_.IsSelfAdjoint.norm_mul_self {x : E} (hx : IsSelfAdjoint x) :
    â€–x * xâ€– = â€–xâ€– ^ 2 := by
  simpa [sq, hx.star_eq] using CStarRing.norm_star_mul_self (x := x)

lemma _root_.IsSelfAdjoint.nnnorm_mul_self {x : E} (hx : IsSelfAdjoint x) :
    â€–x * xâ€–â‚Š = â€–xâ€–â‚Š ^ 2 :=
  Subtype.ext hx.norm_mul_self

@[simp]
theorem star_mul_self_eq_zero_iff (x : E) : xâ‹† * x = 0 â†” x = 0 := by
  rw [â† norm_eq_zero, norm_star_mul_self]
  exact mul_self_eq_zero.trans norm_eq_zero

theorem star_mul_self_ne_zero_iff (x : E) : xâ‹† * x â‰  0 â†” x â‰  0 := by
  simp only [Ne, star_mul_self_eq_zero_iff]

@[simp]
theorem mul_star_self_eq_zero_iff (x : E) : x * xâ‹† = 0 â†” x = 0 := by
  simpa only [star_eq_zero, star_star] using @star_mul_self_eq_zero_iff _ _ _ _ (star x)

theorem mul_star_self_ne_zero_iff (x : E) : x * xâ‹† â‰  0 â†” x â‰  0 := by
  simp only [Ne, mul_star_self_eq_zero_iff]

end NonUnital

section ProdPi

variable {Î¹ Râ‚ Râ‚‚ : Type*} {R : Î¹ â†’ Type*}
variable [NonUnitalNormedRing Râ‚] [StarRing Râ‚] [CStarRing Râ‚]
variable [NonUnitalNormedRing Râ‚‚] [StarRing Râ‚‚] [CStarRing Râ‚‚]
variable [âˆ€ i, NonUnitalNormedRing (R i)] [âˆ€ i, StarRing (R i)]

/-- This instance exists to short circuit type class resolution because of problems with
inference involving Î -types. -/
instance _root_.Pi.starRing' : StarRing (âˆ€ i, R i) :=
  inferInstance

variable [Fintype Î¹] [âˆ€ i, CStarRing (R i)]

instance _root_.Prod.cstarRing : CStarRing (Râ‚ Ã— Râ‚‚) where
  norm_mul_self_le x := by
    dsimp only [norm]
    simp only [Prod.fst_mul, Prod.fst_star, Prod.snd_mul, Prod.snd_star, norm_star_mul_self, â† sq]
    rw [le_sup_iff]
    rcases le_total â€–x.fstâ€– â€–x.sndâ€– with (h | h) <;> simp [h]

instance _root_.Pi.cstarRing : CStarRing (âˆ€ i, R i) where
  norm_mul_self_le x := by
    refine le_of_eq (Eq.symm ?_)
    simp only [norm, Pi.mul_apply, Pi.star_apply, nnnorm_star_mul_self, â† sq]
    norm_cast
    exact
      (Finset.comp_sup_eq_sup_comp_of_is_total (fun x : NNReal => x ^ 2)
          (fun x y h => by simpa only [sq] using mul_le_mul' h h) (by simp)).symm

instance _root_.Pi.cstarRing' : CStarRing (Î¹ â†’ Râ‚) :=
  Pi.cstarRing

end ProdPi

namespace MulOpposite

instance {E : Type*} [NonUnitalNormedRing E] [StarRing E] [CStarRing E] : CStarRing Eáµáµ’áµ– where
  norm_mul_self_le x := CStarRing.norm_self_mul_star (x := MulOpposite.unop x) |>.symm.le

end MulOpposite

section Unital


variable [NormedRing E] [StarRing E] [CStarRing E]

theorem norm_one [Nontrivial E] : â€–(1 : E)â€– = 1 := by
  have : 0 < â€–(1 : E)â€– := norm_pos_iff.mpr one_ne_zero
  rw [â† mul_left_inj' this.ne', â† norm_star_mul_self, mul_one, star_one, one_mul]

-- see Note [lower instance priority]
instance (priority := 100) [Nontrivial E] : NormOneClass E :=
  âŸ¨norm_oneâŸ©

theorem norm_coe_unitary [Nontrivial E] (U : unitary E) : â€–(U : E)â€– = 1 := by
  rw [â† sq_eq_sqâ‚€ (norm_nonneg _) zero_le_one, one_pow 2, sq, â† CStarRing.norm_star_mul_self,
    Unitary.coe_star_mul_self, CStarRing.norm_one]

@[simp]
theorem norm_of_mem_unitary [Nontrivial E] {U : E} (hU : U âˆˆ unitary E) : â€–Uâ€– = 1 :=
  norm_coe_unitary âŸ¨U, hUâŸ©

@[simp]
theorem norm_coe_unitary_mul (U : unitary E) (A : E) : â€–(U : E) * Aâ€– = â€–Aâ€– := by
  nontriviality E
  refine le_antisymm ?_ ?_
  Â· calc
      _ â‰¤ â€–(U : E)â€– * â€–Aâ€– := norm_mul_le _ _
      _ = â€–Aâ€– := by rw [norm_coe_unitary, one_mul]
  Â· calc
      _ = â€–(U : E)â‹† * U * Aâ€– := by rw [Unitary.coe_star_mul_self U, one_mul]
      _ â‰¤ â€–(U : E)â‹†â€– * â€–(U : E) * Aâ€– := by
        rw [mul_assoc]
        exact norm_mul_le _ _
      _ = â€–(U : E) * Aâ€– := by rw [norm_star, norm_coe_unitary, one_mul]

@[simp]
theorem norm_unitary_smul (U : unitary E) (A : E) : â€–U â€¢ Aâ€– = â€–Aâ€– :=
  norm_coe_unitary_mul U A

theorem norm_mem_unitary_mul {U : E} (A : E) (hU : U âˆˆ unitary E) : â€–U * Aâ€– = â€–Aâ€– :=
  norm_coe_unitary_mul âŸ¨U, hUâŸ© A

@[simp]
theorem norm_mul_coe_unitary (A : E) (U : unitary E) : â€–A * Uâ€– = â€–Aâ€– :=
  calc
    _ = â€–((U : E)â‹† * Aâ‹†)â‹†â€– := by simp only [star_star, star_mul]
    _ = â€–(U : E)â‹† * Aâ‹†â€– := by rw [norm_star]
    _ = â€–Aâ‹†â€– := norm_mem_unitary_mul (star A) (Unitary.star_mem U.prop)
    _ = â€–Aâ€– := norm_star _

theorem norm_mul_mem_unitary (A : E) {U : E} (hU : U âˆˆ unitary E) : â€–A * Uâ€– = â€–Aâ€– :=
  norm_mul_coe_unitary A âŸ¨U, hUâŸ©

end Unital

end CStarRing

section SelfAdjoint

variable [NormedRing E] [StarRing E] [CStarRing E]

theorem IsSelfAdjoint.nnnorm_pow_two_pow {x : E} (hx : IsSelfAdjoint x) (n : â„•) :
    â€–x ^ 2 ^ nâ€–â‚Š = â€–xâ€–â‚Š ^ 2 ^ n := by
  induction n with
  | zero => simp only [pow_zero, pow_one]
  | succ k hk =>
    rw [pow_succ', pow_mul', sq, (hx.pow (2 ^ k)).nnnorm_mul_self, hk, pow_mul']

@[deprecated (since := "2025-10-07")]
alias selfAdjoint.nnnorm_pow_two_pow := IsSelfAdjoint.nnnorm_pow_two_pow

theorem IsSelfAdjoint.norm_pow_two_pow {x : E} (hx : IsSelfAdjoint x) (n : â„•) :
    â€–x ^ 2 ^ nâ€– = â€–xâ€– ^ 2 ^ n :=
  congr($(hx.nnnorm_pow_two_pow n))

end SelfAdjoint
section starâ‚—áµ¢

variable [CommSemiring ğ•œ] [StarRing ğ•œ]
variable [SeminormedAddCommGroup E] [StarAddMonoid E] [NormedStarGroup E]
variable [Module ğ•œ E] [StarModule ğ•œ E]

variable (ğ•œ) in
/-- `star` bundled as a linear isometric equivalence -/
def starâ‚—áµ¢ : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] E :=
  { starAddEquiv with
    map_smul' := star_smul
    norm_map' := norm_star }

@[simp]
theorem coe_starâ‚—áµ¢ : (starâ‚—áµ¢ ğ•œ : E â†’ E) = star :=
  rfl

theorem starâ‚—áµ¢_apply {x : E} : starâ‚—áµ¢ ğ•œ x = star x :=
  rfl

@[simp]
theorem starâ‚—áµ¢_toContinuousLinearEquiv :
    (starâ‚—áµ¢ ğ•œ : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] E).toContinuousLinearEquiv = (starL ğ•œ : E â‰ƒLâ‹†[ğ•œ] E) :=
  ContinuousLinearEquiv.ext rfl

end starâ‚—áµ¢

namespace StarSubalgebra

instance toNormedAlgebra {ğ•œ A : Type*} [NormedField ğ•œ] [StarRing ğ•œ] [SeminormedRing A] [StarRing A]
    [NormedAlgebra ğ•œ A] [StarModule ğ•œ A] (S : StarSubalgebra ğ•œ A) : NormedAlgebra ğ•œ S :=
  NormedAlgebra.induced ğ•œ S A S.subtype

instance to_cstarRing {R A} [CommRing R] [StarRing R] [NormedRing A] [StarRing A] [CStarRing A]
    [Algebra R A] [StarModule R A] (S : StarSubalgebra R A) : CStarRing S where
  norm_mul_self_le x := @CStarRing.norm_mul_self_le A _ _ _ x

end StarSubalgebra
