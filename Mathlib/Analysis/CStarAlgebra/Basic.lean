/-
Copyright (c) 2021 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Algebra.Star.Pi
import Mathlib.Algebra.Star.SelfAdjoint
import Mathlib.Algebra.Star.Subalgebra
import Mathlib.Algebra.Star.Unitary
import Mathlib.Data.Real.Star
import Mathlib.Topology.Algebra.Module.Star

/-!
# Normed star rings and algebras

A normed star group is a normed group with a compatible `star` which is isometric.

A C‚ãÜ-ring is a normed star group that is also a ring and that verifies the stronger
condition `‚Äñx‚Äñ^2 ‚â§ ‚Äñx‚ãÜ * x‚Äñ` for all `x` (which actually implies equality). If a C‚ãÜ-ring is also
a star algebra, then it is a C‚ãÜ-algebra.

Note that the type classes corresponding to C‚ãÜ-algebras are defined in
`Mathlib/Analysis/CStarAlgebra/Classes`.

## TODO

- Show that `‚Äñx‚ãÜ * x‚Äñ = ‚Äñx‚Äñ^2` is equivalent to `‚Äñx‚ãÜ * x‚Äñ = ‚Äñx‚ãÜ‚Äñ * ‚Äñx‚Äñ`, which is used as the
  definition of C*-algebras in some sources (e.g. Wikipedia).

-/

open Topology

local postfix:max "‚ãÜ" => star

/-- A normed star group is a normed group with a compatible `star` which is isometric. -/
class NormedStarGroup (E : Type*) [SeminormedAddCommGroup E] [StarAddMonoid E] : Prop where
  norm_star_le : ‚àÄ x : E, ‚Äñx‚ãÜ‚Äñ ‚â§ ‚Äñx‚Äñ

variable {ùïú E Œ± : Type*}

section NormedStarGroup

variable [SeminormedAddCommGroup E] [StarAddMonoid E] [NormedStarGroup E]

@[simp]
lemma norm_star (x : E) : ‚Äñx‚ãÜ‚Äñ = ‚Äñx‚Äñ :=
  le_antisymm (NormedStarGroup.norm_star_le x) (by simpa using NormedStarGroup.norm_star_le x‚ãÜ)

@[simp]
theorem nnnorm_star (x : E) : ‚Äñstar x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  Subtype.ext <| norm_star _

/-- The `star` map in a normed star group is a normed group homomorphism. -/
def starNormedAddGroupHom : NormedAddGroupHom E E :=
  { starAddEquiv with bound' := ‚ü®1, fun _ => le_trans (norm_star _).le (one_mul _).symm.le‚ü© }

/-- The `star` map in a normed star group is an isometry -/
theorem star_isometry : Isometry (star : E ‚Üí E) :=
  show Isometry starAddEquiv from
    AddMonoidHomClass.isometry_of_norm starAddEquiv (show ‚àÄ x, ‚Äñx‚ãÜ‚Äñ = ‚Äñx‚Äñ from norm_star)

instance (priority := 100) NormedStarGroup.to_continuousStar : ContinuousStar E :=
  ‚ü®star_isometry.continuous‚ü©

end NormedStarGroup

instance RingHomIsometric.starRingEnd [NormedCommRing E] [StarRing E] [NormedStarGroup E] :
    RingHomIsometric (starRingEnd E) :=
  ‚ü®@norm_star _ _ _ _‚ü©

/-- A C*-ring is a normed star ring that satisfies the stronger condition `‚Äñx‚Äñ ^ 2 ‚â§ ‚Äñx‚ãÜ * x‚Äñ`
for every `x`. Note that this condition actually implies equality, as is shown in
`norm_star_mul_self` below. -/
class CStarRing (E : Type*) [NonUnitalNormedRing E] [StarRing E] : Prop where
  norm_mul_self_le : ‚àÄ x : E, ‚Äñx‚Äñ * ‚Äñx‚Äñ ‚â§ ‚Äñx‚ãÜ * x‚Äñ

instance : CStarRing ‚Ñù where
  norm_mul_self_le x := by
    simp only [Real.norm_eq_abs, abs_mul_abs_self, star, id, norm_mul, le_refl]

namespace CStarRing

section NonUnital

lemma of_le_norm_mul_star_self
    [NonUnitalNormedRing E] [StarRing E]
    (h : ‚àÄ x : E, ‚Äñx‚Äñ * ‚Äñx‚Äñ ‚â§ ‚Äñx * x‚ãÜ‚Äñ) : CStarRing E :=
  have : NormedStarGroup E :=
    { norm_star_le x := by
        obtain (hx | hx) := eq_zero_or_norm_pos x‚ãÜ
        ¬∑ simp [hx]
        ¬∑ refine le_of_mul_le_mul_right ?_ hx
          simpa [sq, mul_comm ‚Äñx‚ãÜ‚Äñ] using h x‚ãÜ |>.trans <| norm_mul_le _ _ }
  ‚ü®star_involutive.surjective.forall.mpr <| by simpa‚ü©

variable [NonUnitalNormedRing E] [StarRing E] [CStarRing E]

-- see Note [lower instance priority]
/-- In a C*-ring, star preserves the norm. -/
instance (priority := 100) to_normedStarGroup : NormedStarGroup E where
  norm_star_le x := by
    obtain (hx | hx) := eq_zero_or_norm_pos x‚ãÜ
    ¬∑ simp [hx]
    ¬∑ refine le_of_mul_le_mul_right ?_ hx
      simpa using norm_mul_self_le (x := x‚ãÜ) |>.trans <| norm_mul_le _ _

theorem norm_star_mul_self {x : E} : ‚Äñx‚ãÜ * x‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ :=
  le_antisymm ((norm_mul_le _ _).trans (by rw [norm_star])) (CStarRing.norm_mul_self_le x)

theorem norm_self_mul_star {x : E} : ‚Äñx * x‚ãÜ‚Äñ = ‚Äñx‚Äñ * ‚Äñx‚Äñ := by
  nth_rw 1 [‚Üê star_star x]
  simp only [norm_star_mul_self, norm_star]

theorem norm_star_mul_self' {x : E} : ‚Äñx‚ãÜ * x‚Äñ = ‚Äñx‚ãÜ‚Äñ * ‚Äñx‚Äñ := by rw [norm_star_mul_self, norm_star]

theorem nnnorm_self_mul_star {x : E} : ‚Äñx * x‚ãÜ‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä :=
  Subtype.ext norm_self_mul_star

theorem nnnorm_star_mul_self {x : E} : ‚Äñx‚ãÜ * x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä :=
  Subtype.ext norm_star_mul_self

@[simp]
theorem star_mul_self_eq_zero_iff (x : E) : x‚ãÜ * x = 0 ‚Üî x = 0 := by
  rw [‚Üê norm_eq_zero, norm_star_mul_self]
  exact mul_self_eq_zero.trans norm_eq_zero

theorem star_mul_self_ne_zero_iff (x : E) : x‚ãÜ * x ‚â† 0 ‚Üî x ‚â† 0 := by
  simp only [Ne, star_mul_self_eq_zero_iff]

@[simp]
theorem mul_star_self_eq_zero_iff (x : E) : x * x‚ãÜ = 0 ‚Üî x = 0 := by
  simpa only [star_eq_zero, star_star] using @star_mul_self_eq_zero_iff _ _ _ _ (star x)

theorem mul_star_self_ne_zero_iff (x : E) : x * x‚ãÜ ‚â† 0 ‚Üî x ‚â† 0 := by
  simp only [Ne, mul_star_self_eq_zero_iff]

end NonUnital

section ProdPi

variable {Œπ R‚ÇÅ R‚ÇÇ : Type*} {R : Œπ ‚Üí Type*}
variable [NonUnitalNormedRing R‚ÇÅ] [StarRing R‚ÇÅ] [CStarRing R‚ÇÅ]
variable [NonUnitalNormedRing R‚ÇÇ] [StarRing R‚ÇÇ] [CStarRing R‚ÇÇ]
variable [‚àÄ i, NonUnitalNormedRing (R i)] [‚àÄ i, StarRing (R i)]

/-- This instance exists to short circuit type class resolution because of problems with
inference involving Œ†-types. -/
instance _root_.Pi.starRing' : StarRing (‚àÄ i, R i) :=
  inferInstance

variable [Fintype Œπ] [‚àÄ i, CStarRing (R i)]

instance _root_.Prod.cstarRing : CStarRing (R‚ÇÅ √ó R‚ÇÇ) where
  norm_mul_self_le x := by
    dsimp only [norm]
    simp only [Prod.fst_mul, Prod.fst_star, Prod.snd_mul, Prod.snd_star, norm_star_mul_self, ‚Üê sq]
    rw [le_sup_iff]
    rcases le_total ‚Äñx.fst‚Äñ ‚Äñx.snd‚Äñ with (h | h) <;> simp [h]

instance _root_.Pi.cstarRing : CStarRing (‚àÄ i, R i) where
  norm_mul_self_le x := by
    refine le_of_eq (Eq.symm ?_)
    simp only [norm, Pi.mul_apply, Pi.star_apply, nnnorm_star_mul_self, ‚Üê sq]
    norm_cast
    exact
      (Finset.comp_sup_eq_sup_comp_of_is_total (fun x : NNReal => x ^ 2)
          (fun x y h => by simpa only [sq] using mul_le_mul' h h) (by simp)).symm

instance _root_.Pi.cstarRing' : CStarRing (Œπ ‚Üí R‚ÇÅ) :=
  Pi.cstarRing

end ProdPi

namespace MulOpposite

instance {E : Type*} [NonUnitalNormedRing E] [StarRing E] [CStarRing E] : CStarRing E·µê·µí·µñ where
  norm_mul_self_le x := CStarRing.norm_self_mul_star (x := MulOpposite.unop x) |>.symm.le

end MulOpposite

section Unital


variable [NormedRing E] [StarRing E] [CStarRing E]

theorem norm_one [Nontrivial E] : ‚Äñ(1 : E)‚Äñ = 1 := by
  have : 0 < ‚Äñ(1 : E)‚Äñ := norm_pos_iff.mpr one_ne_zero
  rw [‚Üê mul_left_inj' this.ne', ‚Üê norm_star_mul_self, mul_one, star_one, one_mul]

-- see Note [lower instance priority]
instance (priority := 100) [Nontrivial E] : NormOneClass E :=
  ‚ü®norm_one‚ü©

theorem norm_coe_unitary [Nontrivial E] (U : unitary E) : ‚Äñ(U : E)‚Äñ = 1 := by
  rw [‚Üê sq_eq_sq‚ÇÄ (norm_nonneg _) zero_le_one, one_pow 2, sq, ‚Üê CStarRing.norm_star_mul_self,
    unitary.coe_star_mul_self, CStarRing.norm_one]

@[simp]
theorem norm_of_mem_unitary [Nontrivial E] {U : E} (hU : U ‚àà unitary E) : ‚ÄñU‚Äñ = 1 :=
  norm_coe_unitary ‚ü®U, hU‚ü©

@[simp]
theorem norm_coe_unitary_mul (U : unitary E) (A : E) : ‚Äñ(U : E) * A‚Äñ = ‚ÄñA‚Äñ := by
  nontriviality E
  refine le_antisymm ?_ ?_
  ¬∑ calc
      _ ‚â§ ‚Äñ(U : E)‚Äñ * ‚ÄñA‚Äñ := norm_mul_le _ _
      _ = ‚ÄñA‚Äñ := by rw [norm_coe_unitary, one_mul]
  ¬∑ calc
      _ = ‚Äñ(U : E)‚ãÜ * U * A‚Äñ := by rw [unitary.coe_star_mul_self U, one_mul]
      _ ‚â§ ‚Äñ(U : E)‚ãÜ‚Äñ * ‚Äñ(U : E) * A‚Äñ := by
        rw [mul_assoc]
        exact norm_mul_le _ _
      _ = ‚Äñ(U : E) * A‚Äñ := by rw [norm_star, norm_coe_unitary, one_mul]

@[simp]
theorem norm_unitary_smul (U : unitary E) (A : E) : ‚ÄñU ‚Ä¢ A‚Äñ = ‚ÄñA‚Äñ :=
  norm_coe_unitary_mul U A

theorem norm_mem_unitary_mul {U : E} (A : E) (hU : U ‚àà unitary E) : ‚ÄñU * A‚Äñ = ‚ÄñA‚Äñ :=
  norm_coe_unitary_mul ‚ü®U, hU‚ü© A

@[simp]
theorem norm_mul_coe_unitary (A : E) (U : unitary E) : ‚ÄñA * U‚Äñ = ‚ÄñA‚Äñ :=
  calc
    _ = ‚Äñ((U : E)‚ãÜ * A‚ãÜ)‚ãÜ‚Äñ := by simp only [star_star, star_mul]
    _ = ‚Äñ(U : E)‚ãÜ * A‚ãÜ‚Äñ := by rw [norm_star]
    _ = ‚ÄñA‚ãÜ‚Äñ := norm_mem_unitary_mul (star A) (unitary.star_mem U.prop)
    _ = ‚ÄñA‚Äñ := norm_star _

theorem norm_mul_mem_unitary (A : E) {U : E} (hU : U ‚àà unitary E) : ‚ÄñA * U‚Äñ = ‚ÄñA‚Äñ :=
  norm_mul_coe_unitary A ‚ü®U, hU‚ü©

end Unital

end CStarRing

theorem IsSelfAdjoint.nnnorm_pow_two_pow [NormedRing E] [StarRing E] [CStarRing E] {x : E}
    (hx : IsSelfAdjoint x) (n : ‚Ñï) : ‚Äñx ^ 2 ^ n‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä ^ 2 ^ n := by
  induction' n with k hk
  ¬∑ simp only [pow_zero, pow_one]
  ¬∑ rw [pow_succ', pow_mul', sq]
    nth_rw 1 [‚Üê selfAdjoint.mem_iff.mp hx]
    rw [‚Üê star_pow, CStarRing.nnnorm_star_mul_self, ‚Üê sq, hk, pow_mul']

theorem selfAdjoint.nnnorm_pow_two_pow [NormedRing E] [StarRing E] [CStarRing E] (x : selfAdjoint E)
    (n : ‚Ñï) : ‚Äñx ^ 2 ^ n‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä ^ 2 ^ n :=
  x.prop.nnnorm_pow_two_pow _

section star‚Çó·µ¢

variable [CommSemiring ùïú] [StarRing ùïú]
variable [SeminormedAddCommGroup E] [StarAddMonoid E] [NormedStarGroup E]
variable [Module ùïú E] [StarModule ùïú E]

variable (ùïú) in
/-- `star` bundled as a linear isometric equivalence -/
def star‚Çó·µ¢ : E ‚âÉ‚Çó·µ¢‚ãÜ[ùïú] E :=
  { starAddEquiv with
    map_smul' := star_smul
    norm_map' := norm_star }

@[simp]
theorem coe_star‚Çó·µ¢ : (star‚Çó·µ¢ ùïú : E ‚Üí E) = star :=
  rfl

theorem star‚Çó·µ¢_apply {x : E} : star‚Çó·µ¢ ùïú x = star x :=
  rfl

@[simp]
theorem star‚Çó·µ¢_toContinuousLinearEquiv :
    (star‚Çó·µ¢ ùïú : E ‚âÉ‚Çó·µ¢‚ãÜ[ùïú] E).toContinuousLinearEquiv = (starL ùïú : E ‚âÉL‚ãÜ[ùïú] E) :=
  ContinuousLinearEquiv.ext rfl

end star‚Çó·µ¢

namespace StarSubalgebra

instance toNormedAlgebra {ùïú A : Type*} [NormedField ùïú] [StarRing ùïú] [SeminormedRing A] [StarRing A]
    [NormedAlgebra ùïú A] [StarModule ùïú A] (S : StarSubalgebra ùïú A) : NormedAlgebra ùïú S :=
  NormedAlgebra.induced ùïú S A S.subtype

instance to_cstarRing {R A} [CommRing R] [StarRing R] [NormedRing A] [StarRing A] [CStarRing A]
    [Algebra R A] [StarModule R A] (S : StarSubalgebra R A) : CStarRing S where
  norm_mul_self_le x := @CStarRing.norm_mul_self_le A _ _ _ x

end StarSubalgebra
