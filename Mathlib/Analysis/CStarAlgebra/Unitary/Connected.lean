/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Continuity
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order
import Mathlib.Analysis.CStarAlgebra.Exponential
import Mathlib.Analysis.SpecialFunctions.Complex.Circle
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.ExpLog

/-! # The unitary group in a unital C‚ãÜ-algebra is locally path connected

When `A` is a unital C‚ãÜ-algebra and `u : unitary A` is a unitary element whose distance to `1` is
less that `2`, the spectrum of `u` is contained in the slit plane, so the principal branch of the
logarithm is continuous on the spectrum of `u` (or equivalently, `Complex.arg` is continuous on the
spectrum). The continuous functional calculus can then be used to define a selfadjoint element `x`
such that `u = exp (I ‚Ä¢ x)`. Moreover, there is a relatively nice relationship between the norm of
`x` and the norm of `u - 1`, namely `‚Äñu - 1‚Äñ ^ 2 = 2 * (1 - cos ‚Äñx‚Äñ)`. In fact, these maps `u ‚Ü¶ x`
and `x ‚Ü¶ u` establish a partial homeomorphism between `ball (1 : unitary A) 2` and
`ball (0 : selfAdjoint A) œÄ`.

The map `t ‚Ü¶ exp (t ‚Ä¢ (I ‚Ä¢ x))` constitutes a path from `1` to `u`, showing that unitary elements
sufficiently close (i.e., within a distance `2`) to `1 : unitary A` are path connected to `1`.
This property can be translated around the unitary group to show that if `u v : unitary A` are
unitary elements with `‚Äñu - v‚Äñ < 2`, then there is a path joining them. In fact, this path has the
property that it lies within `closedBall u ‚Äñu - v‚Äñ`, and consequently any ball of radius `Œ¥ < 2` in
`unitary A` is path connected. Therefore, the unitary group is locally path connected.

Finally, we provide the standard characterization of the path component of `1 : unitary A` as finite
products of exponential unitaries.

## Main results

+ `Unitary.argSelfAdjoint`: the selfadjoint element obtained by taking the argument (using the
  principal branch and the continuous functional calculus) of a unitary. This returns `0` if
  the principal branch of the logarithm is not continuous on the spectrum of the unitary element.
+ `selfAdjoint.norm_sq_expUnitary_sub_one`:
  `‚Äñ(selfAdjoint.expUnitary x - 1 : A)‚Äñ ^ 2 = 2 * (1 - Real.cos ‚Äñx‚Äñ)`
+ `Unitary.norm_argSelfAdjoint`:
  `‚ÄñUnitary.argSelfAdjoint u‚Äñ = Real.arccos (1 - ‚Äñ(u - 1 : A)‚Äñ ^ 2 / 2)`
+ `Unitary.openPartialHomeomorph`: the maps `Unitary.argSelfAdjoint` and `selfAdjoint.expUnitary`
  form a partial homeomorphism between `ball (1 : unitary A) 2` and `ball (0 : selfAdjoint A) œÄ`.
+ `selfAdjoint.expUnitaryPathToOne`: the path `t ‚Ü¶ expUnitary (t ‚Ä¢ x)` from `1` to
  `expUnitary x` for a selfadjoint element `x`.
+ `Unitary.isPathConnected_ball`: any ball of radius `Œ¥ < 2` in the unitary group of a unital
  C‚ãÜ-algebra is path connected.
+ `Unitary.instLocPathConnectedSpace`: the unitary group of a C‚ãÜ-algebra is locally path connected.
+ `Unitary.mem_pathComponentOne_iff`: The path component of the identity in the unitary group of a
  C‚ãÜ-algebra is the set of unitaries that can be expressed as a product of exponentials of
  selfadjoint elements.
-/

variable {A : Type*} [CStarAlgebra A]

open Complex Metric NormedSpace selfAdjoint Unitary
open scoped Real

lemma Unitary.two_mul_one_sub_le_norm_sub_one_sq {u : A} (hu : u ‚àà unitary A)
    {z : ‚ÑÇ} (hz : z ‚àà spectrum ‚ÑÇ u) :
    2 * (1 - z.re) ‚â§ ‚Äñu - 1‚Äñ ^ 2 := by
  rw [‚Üê Real.sqrt_le_left (by positivity)]
  have := spectrum.subset_circle_of_unitary hu hz
  simp only [mem_sphere_iff_norm, sub_zero] at this
  rw [‚Üê cfc_id' ‚ÑÇ u, ‚Üê cfc_one ‚ÑÇ u, ‚Üê cfc_sub ..]
  convert norm_apply_le_norm_cfc (fun z ‚Ü¶ z - 1) u hz
  simpa using congr(Real.sqrt $(norm_sub_one_sq_eq_of_norm_one this)).symm

@[deprecated (since := "2025-10-29")] alias unitary.two_mul_one_sub_le_norm_sub_one_sq :=
  Unitary.two_mul_one_sub_le_norm_sub_one_sq

lemma Unitary.norm_sub_one_sq_eq {u : A} (hu : u ‚àà unitary A) {x : ‚Ñù}
    (hz : IsLeast (re '' (spectrum ‚ÑÇ u)) x) :
    ‚Äñu - 1‚Äñ ^ 2 = 2 * (1 - x) := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  ¬∑ exfalso; apply hz.nonempty.of_image.ne_empty; simp
  rw [‚Üê cfc_id' ‚ÑÇ u, ‚Üê cfc_one ‚ÑÇ u, ‚Üê cfc_sub ..]
  have h_eqOn : (spectrum ‚ÑÇ u).EqOn (fun z ‚Ü¶ ‚Äñz - 1‚Äñ ^ 2) (fun z ‚Ü¶ 2 * (1 - z.re)) :=
    Complex.norm_sub_one_sq_eqOn_sphere.mono <| spectrum.subset_circle_of_unitary (ùïú := ‚ÑÇ) hu
  have h‚ÇÇ : IsGreatest ((fun z ‚Ü¶ 2 * (1 - z.re)) '' (spectrum ‚ÑÇ u)) (2 * (1 - x)) := by
    have : Antitone (fun y : ‚Ñù ‚Ü¶ 2 * (1 - y)) := by intro _ _ _; simp only; gcongr
    simpa [Set.image_image] using this.map_isLeast hz
  have h‚ÇÉ : IsGreatest ((‚Äñ¬∑ - 1‚Äñ ^ 2) '' spectrum ‚ÑÇ u) (‚Äñcfc (¬∑ - 1 : ‚ÑÇ ‚Üí ‚ÑÇ) u‚Äñ ^ 2) := by
    have := pow_left_monotoneOn (n := 2) |>.mono (s‚ÇÇ := ((‚Äñ¬∑ - 1‚Äñ) '' spectrum ‚ÑÇ u)) (by aesop)
    simpa [Set.image_image] using this.map_isGreatest (IsGreatest.norm_cfc (fun z : ‚ÑÇ ‚Ü¶ z - 1) u)
  exact h‚ÇÉ.unique (h_eqOn.image_eq ‚ñ∏ h‚ÇÇ)

@[deprecated (since := "2025-10-29")] alias unitary.norm_sub_one_sq_eq := Unitary.norm_sub_one_sq_eq

lemma Unitary.norm_sub_one_lt_two_iff {u : A} (hu : u ‚àà unitary A) :
    ‚Äñu - 1‚Äñ < 2 ‚Üî -1 ‚àâ spectrum ‚ÑÇ u := by
  nontriviality A
  rw [‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity)]
  constructor
  ¬∑ intro h h1
    have := two_mul_one_sub_le_norm_sub_one_sq hu h1 |>.trans_lt h
    norm_num at this
  ¬∑ contrapose!
    obtain ‚ü®x, hx‚ü© := spectrum.isCompact (ùïú := ‚ÑÇ) u |>.image continuous_re |>.exists_isLeast <|
      (spectrum.nonempty _).image _
    rw [norm_sub_one_sq_eq hu hx]
    obtain ‚ü®z, hz, rfl‚ü© := hx.1
    intro key
    replace key : z.re ‚â§ -1 := by linarith
    have hz_norm : ‚Äñz‚Äñ = 1 := spectrum.norm_eq_one_of_unitary hu hz
    rw [‚Üê hz_norm, ‚Üê RCLike.re_eq_complex_re, RCLike.re_le_neg_norm_iff_eq_neg_norm, hz_norm] at key
    exact key ‚ñ∏ hz

@[deprecated (since := "2025-10-29")] alias unitary.norm_sub_one_lt_two_iff :=
  Unitary.norm_sub_one_lt_two_iff

lemma Unitary.spectrum_subset_slitPlane_iff_norm_lt_two {u : A} (hu : u ‚àà unitary A) :
    spectrum ‚ÑÇ u ‚äÜ slitPlane ‚Üî ‚Äñu - 1‚Äñ < 2 := by
  simp [subset_slitPlane_iff_of_subset_sphere (spectrum.subset_circle_of_unitary hu),
    unitary.norm_sub_one_lt_two_iff hu]

@[deprecated (since := "2025-10-29")] alias unitary.spectrum_subset_slitPlane_iff_norm_lt_two :=
  Unitary.spectrum_subset_slitPlane_iff_norm_lt_two

@[aesop safe apply (rule_sets := [CStarAlgebra])]
lemma IsSelfAdjoint.cfc_arg (u : A) : IsSelfAdjoint (cfc (ofReal ‚àò arg : ‚ÑÇ ‚Üí ‚ÑÇ) u) := by
  simp [isSelfAdjoint_iff, ‚Üê cfc_star, Function.comp_def]

/-- The selfadjoint element obtained by taking the argument (using the principal branch and the
continuous functional calculus) of a unitary whose spectrum does not contain `-1`. This returns
`0` if the principal branch of the logarithm is not continuous on the spectrum of the unitary
element. -/
@[simps]
noncomputable def Unitary.argSelfAdjoint (u : unitary A) : selfAdjoint A :=
  ‚ü®cfc (arg ¬∑ : ‚ÑÇ ‚Üí ‚ÑÇ) (u : A), .cfc_arg (u : A)‚ü©

@[deprecated (since := "2025-10-29")] alias unitary.argSelfAdjoint := Unitary.argSelfAdjoint

lemma selfAdjoint.norm_sq_expUnitary_sub_one {x : selfAdjoint A} (hx : ‚Äñx‚Äñ ‚â§ œÄ) :
    ‚Äñ(expUnitary x - 1 : A)‚Äñ ^ 2 = 2 * (1 - Real.cos ‚Äñx‚Äñ) := by
  nontriviality A
  apply norm_sub_one_sq_eq (expUnitary x).2
  simp only [expUnitary_coe, AddSubgroupClass.coe_norm]
  rw [‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul I _ (x : A), cfc_map_spectrum ..,
    ‚Üê x.2.spectrumRestricts.algebraMap_image]
  simp only [Set.image_image, coe_algebraMap, smul_eq_mul, mul_comm I, ‚Üê exp_eq_exp_‚ÑÇ,
    exp_ofReal_mul_I_re]
  refine ‚ü®?_, ?_‚ü©
  ¬∑ cases CStarAlgebra.norm_or_neg_norm_mem_spectrum x.2 with
    | inl h => exact ‚ü®_, h, rfl‚ü©
    | inr h => exact ‚ü®_, h, by simp‚ü©
  ¬∑ rintro - ‚ü®y, hy, rfl‚ü©
    exact Real.cos_abs y ‚ñ∏ Real.cos_le_cos_of_nonneg_of_le_pi (by positivity) hx <|
      spectrum.norm_le_norm_of_mem hy

lemma argSelfAdjoint_expUnitary {x : selfAdjoint A} (hx : ‚Äñx‚Äñ < œÄ) :
    argSelfAdjoint (expUnitary x) = x := by
  nontriviality A
  ext
  have : spectrum ‚ÑÇ (expUnitary x : A) ‚äÜ slitPlane := by
    rw [spectrum_subset_slitPlane_iff_norm_lt_two (expUnitary x).2,
      ‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity), norm_sq_expUnitary_sub_one hx.le]
    calc
      2 * (1 - Real.cos ‚Äñx‚Äñ) < 2 * (1 - Real.cos œÄ) := by
        gcongr
        exact Real.cos_lt_cos_of_nonneg_of_le_pi (by positivity) le_rfl hx
      _ = 2 ^ 2 := by norm_num
  simp only [argSelfAdjoint_coe, expUnitary_coe]
  rw [‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul .., ‚Üê cfc_comp' (hg := ?hg)]
  case hg =>
    refine continuous_ofReal.comp_continuousOn <| continuousOn_arg.mono ?_
    rwa [expUnitary_coe, ‚Üê CFC.exp_eq_normedSpace_exp, ‚Üê cfc_comp_smul ..,
      cfc_map_spectrum ..] at this
  conv_rhs => rw [‚Üê cfc_id' ‚ÑÇ (x : A)]
  refine cfc_congr fun y hy ‚Ü¶ ?_
  rw [‚Üê x.2.spectrumRestricts.algebraMap_image] at hy
  obtain ‚ü®y, hy, rfl‚ü© := hy
  simp only [coe_algebraMap, smul_eq_mul, mul_comm I, ‚Üê exp_eq_exp_‚ÑÇ, ofReal_inj]
  replace hy : ‚Äñy‚Äñ < œÄ := spectrum.norm_le_norm_of_mem hy |>.trans_lt hx
  simp only [Real.norm_eq_abs, abs_lt] at hy
  rw [‚Üê Circle.coe_exp, Circle.arg_exp hy.1 hy.2.le]

lemma expUnitary_argSelfAdjoint {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    expUnitary (argSelfAdjoint u) = u := by
  ext
  have : ContinuousOn arg (spectrum ‚ÑÇ (u : A)) :=
    continuousOn_arg.mono <| (spectrum_subset_slitPlane_iff_norm_lt_two u.2).mpr hu
  rw [expUnitary_coe, argSelfAdjoint_coe, ‚Üê CFC.exp_eq_normedSpace_exp,
    ‚Üê cfc_comp_smul .., ‚Üê cfc_comp' ..]
  conv_rhs => rw [‚Üê cfc_id' ‚ÑÇ (u : A)]
  refine cfc_congr fun y hy ‚Ü¶ ?_
  have hy‚ÇÅ : ‚Äñy‚Äñ = 1 := spectrum.norm_eq_one_of_unitary u.2 hy
  have : I * y.arg = log y :=
    Complex.ext (by simp [log_re, spectrum.norm_eq_one_of_unitary u.2 hy]) (by simp [log_im])
  simpa [‚Üê exp_eq_exp_‚ÑÇ, this] using exp_log (by aesop)

lemma Unitary.norm_argSelfAdjoint_le_pi (u : unitary A) :
    ‚ÄñargSelfAdjoint u‚Äñ ‚â§ œÄ :=
  norm_cfc_le (by positivity) fun y hy ‚Ü¶ by simpa using abs_arg_le_pi y

@[deprecated (since := "2025-10-29")] alias unitary.norm_argSelfAdjoint_le_pi :=
  Unitary.norm_argSelfAdjoint_le_pi

lemma Unitary.two_mul_one_sub_cos_norm_argSelfAdjoint {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    2 * (1 - Real.cos ‚ÄñargSelfAdjoint u‚Äñ) = ‚Äñ(u - 1 : A)‚Äñ ^ 2 := by
  conv_rhs => rw [‚Üê expUnitary_argSelfAdjoint hu]
  exact Eq.symm <| norm_sq_expUnitary_sub_one <| norm_argSelfAdjoint_le_pi u

@[deprecated (since := "2025-10-29")] alias unitary.two_mul_one_sub_cos_norm_argSelfAdjoint :=
  Unitary.two_mul_one_sub_cos_norm_argSelfAdjoint

lemma Unitary.norm_argSelfAdjoint {u : unitary A} (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    ‚ÄñargSelfAdjoint u‚Äñ = Real.arccos (1 - ‚Äñ(u - 1 : A)‚Äñ ^ 2 / 2) := by
  refine Real.arccos_eq_of_eq_cos (by positivity) (norm_argSelfAdjoint_le_pi u) ?_ |>.symm
  linarith [two_mul_one_sub_cos_norm_argSelfAdjoint hu]

@[deprecated (since := "2025-10-29")] alias unitary.norm_argSelfAdjoint :=
  Unitary.norm_argSelfAdjoint

lemma Unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le (u : unitary A)
    {t : ‚Ñù} (ht : t ‚àà Set.Icc 0 1) (hu : ‚Äñ(u - 1 : A)‚Äñ < 2) :
    ‚Äñ(expUnitary (t ‚Ä¢ argSelfAdjoint u) - 1 : A)‚Äñ ‚â§ ‚Äñ(u - 1 : A)‚Äñ := by
  have key : ‚Äñt ‚Ä¢ argSelfAdjoint u‚Äñ ‚â§ ‚ÄñargSelfAdjoint u‚Äñ := by
    rw [‚Üê one_mul ‚ÄñargSelfAdjoint u‚Äñ]
    simp_rw [AddSubgroupClass.coe_norm, val_smul, norm_smul, Real.norm_eq_abs, abs_of_nonneg ht.1]
    gcongr
    exact ht.2
  rw [‚Üê sq_le_sq‚ÇÄ (by positivity) (by positivity)]
  rw [norm_sq_expUnitary_sub_one (key.trans <| norm_argSelfAdjoint_le_pi u)]
  trans 2 * (1 - Real.cos ‚ÄñargSelfAdjoint u‚Äñ)
  ¬∑ gcongr
    exact Real.cos_le_cos_of_nonneg_of_le_pi (by positivity) (norm_argSelfAdjoint_le_pi u) key
  ¬∑ exact (two_mul_one_sub_cos_norm_argSelfAdjoint hu).le

@[deprecated (since := "2025-10-29")] alias
  unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le :=
  Unitary.norm_expUnitary_smul_argSelfAdjoint_sub_one_le

@[fun_prop]
lemma Unitary.continuousOn_argSelfAdjoint :
    ContinuousOn (argSelfAdjoint : unitary A ‚Üí selfAdjoint A) (ball (1 : unitary A) 2) := by
  rw [Topology.IsInducing.subtypeVal.continuousOn_iff]
  simp only [SetLike.coe_sort_coe, Function.comp_def, argSelfAdjoint_coe]
  rw [isOpen_ball.continuousOn_iff]
  intro u (hu : dist u 1 < 2)
  obtain ‚ü®Œµ, huŒµ, hŒµ2‚ü© := exists_between (sq_lt_sq‚ÇÄ (by positivity) (by positivity) |>.mpr hu)
  have hŒµ : 0 < Œµ := lt_of_le_of_lt (by positivity) huŒµ
  have huŒµ' : dist u 1 < ‚àöŒµ := Real.lt_sqrt_of_sq_lt huŒµ
  apply ContinuousOn.continuousAt ?_ (closedBall_mem_nhds_of_mem huŒµ')
  apply ContinuousOn.image_comp_continuous ?_ continuous_subtype_val
  apply continuousOn_cfc A (s := sphere 0 1 ‚à© {z | 2 * (1 - z.re) ‚â§ Œµ}) ?_ _ ?_ |>.mono
  ¬∑ rintro - ‚ü®v, hv, rfl‚ü©
    simp only [Set.subset_inter_iff, Set.mem_setOf_eq]
    refine ‚ü®inferInstance, spectrum_subset_circle v, ?_‚ü©
    intro z hz
    simp only [Set.mem_setOf_eq]
    trans ‚Äñ(v - 1 : A)‚Äñ ^ 2
    ¬∑ exact two_mul_one_sub_le_norm_sub_one_sq v.2 hz
    ¬∑ refine Real.le_sqrt (by positivity) (by positivity) |>.mp ?_
      simpa [Subtype.dist_eq, dist_eq_norm] using hv
  ¬∑ exact isCompact_sphere 0 1 |>.inter_right <| isClosed_le (by fun_prop) (by fun_prop)
  ¬∑ refine continuous_ofReal.comp_continuousOn <| continuousOn_arg.mono ?_
    apply subset_slitPlane_iff_of_subset_sphere Set.inter_subset_left |>.mpr
    norm_num at hŒµ2 ‚ä¢
    exact hŒµ2

@[deprecated (since := "2025-10-29")] alias unitary.continuousOn_argSelfAdjoint :=
  Unitary.continuousOn_argSelfAdjoint

/-- the maps `unitary.argSelfAdjoint` and `selfAdjoint.expUnitary` form a partial
homeomorphism between `ball (1 : unitary A) 2` and `ball (0 : selfAdjoint A) œÄ`. -/
@[simps]
noncomputable def Unitary.openPartialHomeomorph :
    OpenPartialHomeomorph (unitary A) (selfAdjoint A) where
  toFun := argSelfAdjoint
  invFun := expUnitary
  source := ball 1 2
  target := ball 0 œÄ
  map_source' u hu := by
    simp only [mem_ball, Subtype.dist_eq, OneMemClass.coe_one, dist_eq_norm, sub_zero] at hu ‚ä¢
    rw [norm_argSelfAdjoint hu]
    calc
      Real.arccos (1 - ‚Äñ(u - 1 : A)‚Äñ ^ 2 / 2) < Real.arccos (1 - 2 ^ 2 / 2) := by
        apply Real.arccos_lt_arccos (by norm_num) (by gcongr)
        linarith [(by positivity : 0 ‚â§ ‚Äñ(u - 1 : A)‚Äñ ^ 2 / 2)]
      _ = œÄ := by norm_num
  map_target' x hx := by
    simp only [mem_ball, Subtype.dist_eq, OneMemClass.coe_one, dist_eq_norm, sub_zero] at hx ‚ä¢
    rw [‚Üê sq_lt_sq‚ÇÄ (by positivity) (by positivity), norm_sq_expUnitary_sub_one hx.le]
    have : -1 < Real.cos ‚Äñ(x : A)‚Äñ :=
      Real.cos_pi ‚ñ∏ Real.cos_lt_cos_of_nonneg_of_le_pi (by positivity) le_rfl hx
    simp only [AddSubgroupClass.coe_norm, mul_sub, mul_one, sq, gt_iff_lt]
    linarith
  left_inv' u hu := expUnitary_argSelfAdjoint <| by
    simpa [Subtype.dist_eq, dist_eq_norm] using hu
  right_inv' x hx := argSelfAdjoint_expUnitary <| by simpa using hx
  open_source := isOpen_ball
  open_target := isOpen_ball
  continuousOn_toFun := by fun_prop
  continuousOn_invFun := by fun_prop

@[deprecated (since := "2025-10-29")] alias unitary.openPartialHomeomorph :=
  Unitary.openPartialHomeomorph

lemma Unitary.norm_sub_eq (u v : unitary A) :
    ‚Äñ(u - v : A)‚Äñ = ‚Äñ((u * star v : unitary A) - 1 : A)‚Äñ := calc
  ‚Äñ(u - v : A)‚Äñ = ‚Äñ(u * star v - 1 : A) * v‚Äñ := by simp [sub_mul, mul_assoc]
  _ = ‚Äñ((u * star v : unitary A) - 1 : A)‚Äñ := by simp

@[deprecated (since := "2025-10-29")] alias unitary.norm_sub_eq := Unitary.norm_sub_eq

lemma Unitary.expUnitary_eq_mul_inv (u v : unitary A) (huv : ‚Äñ(u - v : A)‚Äñ < 2) :
    expUnitary (argSelfAdjoint (u * star v)) = u * star v :=
  expUnitary_argSelfAdjoint <| norm_sub_eq u v ‚ñ∏ huv

@[deprecated (since := "2025-10-29")] alias unitary.expUnitary_eq_mul_inv :=
  Unitary.expUnitary_eq_mul_inv

/-- For a selfadjoint element `x` in a C‚ãÜ-algebra, this is the path from `1` to `expUnitary x`
given by `t ‚Ü¶ expUnitary (t ‚Ä¢ x)`. -/
@[simps]
noncomputable def selfAdjoint.expUnitaryPathToOne (x : selfAdjoint A) :
    Path 1 (expUnitary x) where
  toFun t := expUnitary ((t : ‚Ñù) ‚Ä¢ x)
  continuous_toFun := by fun_prop
  source' := by simp
  target' := by simp

@[simp]
lemma selfAdjoint.joined_one_expUnitary (x : selfAdjoint A) :
    Joined (1 : unitary A) (expUnitary x) :=
  ‚ü®expUnitaryPathToOne x‚ü©

/-- The path `t ‚Ü¶ expUnitary (t ‚Ä¢ argSelfAdjoint (v * star u)) * u`
from `u : unitary A` to `v` when `‚Äñv - u‚Äñ < 2`. -/
@[simps]
noncomputable def Unitary.path (u v : unitary A) (huv : ‚Äñ(v - u : A)‚Äñ < 2) :
    Path u v where
  toFun t := expUnitary ((t : ‚Ñù) ‚Ä¢ argSelfAdjoint (v * star u)) * u
  continuous_toFun := by fun_prop
  source' := by ext; simp
  target' := by simp [expUnitary_eq_mul_inv v u huv, mul_assoc]

@[deprecated (since := "2025-10-29")] alias unitary.path := Unitary.path

/-- Two unitary elements `u` and `v` in a unital C‚ãÜ-algebra are joined by a path if the
distance between them is less than `2`. -/
lemma Unitary.joined (u v : unitary A) (huv : ‚Äñ(v - u : A)‚Äñ < 2) :
    Joined u v :=
  ‚ü®path u v huv‚ü©

@[deprecated (since := "2025-10-29")] alias unitary.joined := Unitary.joined

/-- Any ball of radius `Œ¥ < 2` in the unitary group of a unital C‚ãÜ-algebra is path connected. -/
lemma Unitary.isPathConnected_ball (u : unitary A) (Œ¥ : ‚Ñù) (hŒ¥‚ÇÄ : 0 < Œ¥) (hŒ¥‚ÇÇ : Œ¥ < 2) :
    IsPathConnected (ball (u : unitary A) Œ¥) := by
  suffices IsPathConnected (ball (1 : unitary A) Œ¥) by
    convert this |>.image (f := (u * ¬∑)) (by fun_prop)
    ext v
    rw [‚Üê inv_mul_cancel u]
    simp [- inv_mul_cancel, Subtype.dist_eq, dist_eq_norm, ‚Üê mul_sub]
  refine ‚ü®1, by simpa, fun {u} hu ‚Ü¶ ?_‚ü©
  have hu : ‚Äñ(u - 1 : A)‚Äñ < Œ¥ := by simpa [Subtype.dist_eq, dist_eq_norm] using hu
  refine ‚ü®path 1 u (hu.trans hŒ¥‚ÇÇ), fun t ‚Ü¶ ?_‚ü©
  simpa [Subtype.dist_eq, dist_eq_norm] using
    norm_expUnitary_smul_argSelfAdjoint_sub_one_le u t.2 (hu.trans hŒ¥‚ÇÇ) |>.trans_lt hu

@[deprecated (since := "2025-10-29")] alias unitary.isPathConnected_ball :=
  Unitary.isPathConnected_ball

/-- The unitary group in a C‚ãÜ-algebra is locally path connected. -/
instance Unitary.instLocPathConnectedSpace : LocPathConnectedSpace (unitary A) :=
  .of_bases (fun _ ‚Ü¶ nhds_basis_uniformity <| uniformity_basis_dist_lt zero_lt_two) <| by
    simpa using isPathConnected_ball

/-- The path component of the identity in the unitary group of a C‚ãÜ-algebra is the set of
unitaries that can be expressed as a product of exponential unitaries. -/
lemma Unitary.mem_pathComponentOne_iff {u : unitary A} :
    u ‚àà pathComponent 1 ‚Üî ‚àÉ l : List (selfAdjoint A), (l.map expUnitary).prod = u := by
  constructor
  ¬∑ revert u
    simp_rw [‚Üê Set.mem_range, ‚Üê Set.subset_def, pathComponent_eq_connectedComponent]
    refine IsClopen.connectedComponent_subset ?_ ‚ü®[], by simp‚ü©
    refine .of_thickening_subset_self zero_lt_two ?_
    intro u hu
    rw [mem_thickening_iff] at hu
    obtain ‚ü®v, ‚ü®‚ü®l, (hlv : (l.map expUnitary).prod = v)‚ü©, huv‚ü©‚ü© := hu
    refine ‚ü®argSelfAdjoint (u * star v) :: l, ?_‚ü©
    simp [hlv, mul_assoc,
      expUnitary_eq_mul_inv u v (by simpa [Subtype.dist_eq, dist_eq_norm] using huv)]
  ¬∑ rintro ‚ü®l, rfl‚ü©
    induction l with
    | nil => simp
    | cons x xs ih => simpa using (joined_one_expUnitary x).mul ih

@[deprecated (since := "2025-10-29")] alias unitary.mem_pathComponentOne_iff :=
  Unitary.mem_pathComponentOne_iff
