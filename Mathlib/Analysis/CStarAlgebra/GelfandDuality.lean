/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousMap
import Mathlib.Analysis.Normed.Group.Quotient
import Mathlib.Analysis.Normed.Algebra.Basic
import Mathlib.Topology.ContinuousMap.Units
import Mathlib.Topology.ContinuousMap.Compact
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Topology.ContinuousMap.Ideals
import Mathlib.Topology.ContinuousMap.StoneWeierstrass

/-!
# Gelfand Duality

The `gelfandTransform` is an algebra homomorphism from a topological `ğ•œ`-algebra `A` to
`C(characterSpace ğ•œ A, ğ•œ)`. In the case where `A` is a commutative complex Banach algebra, then
the Gelfand transform is actually spectrum-preserving (`spectrum.gelfandTransform_eq`). Moreover,
when `A` is a commutative Câ‹†-algebra over `â„‚`, then the Gelfand transform is a surjective isometry,
and even an equivalence between Câ‹†-algebras.

Consider the contravariant functors between compact Hausdorff spaces and commutative unital
Câ‹†algebras `F : Cpct â†’ CommCStarAlg := X â†¦ C(X, â„‚)` and
`G : CommCStarAlg â†’ Cpct := A â†’ characterSpace â„‚ A` whose actions on morphisms are given by
`WeakDual.CharacterSpace.compContinuousMap` and `ContinuousMap.compStarAlgHom'`, respectively.

Then `Î·â‚ : id â†’ F âˆ˜ G := gelfandStarTransform` and
`Î·â‚‚ : id â†’ G âˆ˜ F := WeakDual.CharacterSpace.homeoEval` are the natural isomorphisms implementing
**Gelfand Duality**, i.e., the (contravariant) equivalence of these categories.

## Main definitions

* `Ideal.toCharacterSpace` : constructs an element of the character space from a maximal ideal in
  a commutative complex Banach algebra
* `WeakDual.CharacterSpace.compContinuousMap`: The functorial map taking `Ïˆ : A â†’â‹†â‚[ğ•œ] B` to a
  continuous function `characterSpace ğ•œ B â†’ characterSpace ğ•œ A` given by pre-composition with `Ïˆ`.

## Main statements

* `spectrum.gelfandTransform_eq` : the Gelfand transform is spectrum-preserving when the algebra is
  a commutative complex Banach algebra.
* `gelfandTransform_isometry` : the Gelfand transform is an isometry when the algebra is a
  commutative (unital) Câ‹†-algebra over `â„‚`.
* `gelfandTransform_bijective` : the Gelfand transform is bijective when the algebra is a
  commutative (unital) Câ‹†-algebra over `â„‚`.
* `gelfandStarTransform_naturality`: The `gelfandStarTransform` is a natural isomorphism
* `WeakDual.CharacterSpace.homeoEval_naturality`: This map implements a natural isomorphism

## TODO

* After defining the category of commutative unital Câ‹†-algebras, bundle the existing unbundled
  **Gelfand duality** into an actual equivalence (duality) of categories associated to the
  functors `C(Â·, â„‚)` and `characterSpace â„‚ Â·` and the natural isomorphisms `gelfandStarTransform`
  and `WeakDual.CharacterSpace.homeoEval`.

## Tags

Gelfand transform, character space, Câ‹†-algebra
-/


open WeakDual

open scoped NNReal

section ComplexBanachAlgebra

open Ideal

variable {A : Type*} [NormedCommRing A] [NormedAlgebra â„‚ A] [CompleteSpace A] (I : Ideal A)
  [Ideal.IsMaximal I]

/-- Every maximal ideal in a commutative complex Banach algebra gives rise to a character on that
algebra. In particular, the character, which may be identified as an algebra homomorphism due to
`WeakDual.CharacterSpace.equivAlgHom`, is given by the composition of the quotient map and
the Gelfand-Mazur isomorphism `NormedRing.algEquivComplexOfComplete`. -/
noncomputable def Ideal.toCharacterSpace : characterSpace â„‚ A :=
  CharacterSpace.equivAlgHom.symm <|
    ((NormedRing.algEquivComplexOfComplete
      (letI := Quotient.field I; isUnit_iff_ne_zero (Gâ‚€ := A â§¸ I))).symm : A â§¸ I â†’â‚[â„‚] â„‚).comp <|
    Quotient.mkâ‚ â„‚ I

theorem Ideal.toCharacterSpace_apply_eq_zero_of_mem {a : A} (ha : a âˆˆ I) :
    I.toCharacterSpace a = 0 := by
  unfold Ideal.toCharacterSpace
  simp only [CharacterSpace.equivAlgHom_symm_coe, AlgHom.coe_comp, AlgHom.coe_coe,
    Quotient.mkâ‚_eq_mk, Function.comp_apply, NormedRing.algEquivComplexOfComplete_symm_apply]
  simp_rw [Quotient.eq_zero_iff_mem.mpr ha, spectrum.zero_eq]
  exact Set.eq_of_mem_singleton (Set.singleton_nonempty (0 : â„‚)).some_mem

/-- If `a : A` is not a unit, then some character takes the value zero at `a`. This is equivalent
to `gelfandTransform â„‚ A a` takes the value zero at some character. -/
theorem WeakDual.CharacterSpace.exists_apply_eq_zero {a : A} (ha : Â¬IsUnit a) :
    âˆƒ f : characterSpace â„‚ A, f a = 0 := by
  obtain âŸ¨M, hM, haMâŸ© := (span {a}).exists_le_maximal (span_singleton_ne_top ha)
  exact
    âŸ¨M.toCharacterSpace,
      M.toCharacterSpace_apply_eq_zero_of_mem
        (haM (mem_span_singleton.mpr âŸ¨1, (mul_one a).symmâŸ©))âŸ©

theorem WeakDual.CharacterSpace.mem_spectrum_iff_exists {a : A} {z : â„‚} :
    z âˆˆ spectrum â„‚ a â†” âˆƒ f : characterSpace â„‚ A, f a = z := by
  refine âŸ¨fun hz => ?_, ?_âŸ©
  Â· obtain âŸ¨f, hfâŸ© := WeakDual.CharacterSpace.exists_apply_eq_zero hz
    simp only [map_sub, sub_eq_zero, AlgHomClass.commutes] at hf
    exact âŸ¨_, hf.symmâŸ©
  Â· rintro âŸ¨f, rflâŸ©
    exact AlgHom.apply_mem_spectrum f a

/-- The Gelfand transform is spectrum-preserving. -/
theorem spectrum.gelfandTransform_eq (a : A) :
    spectrum â„‚ (gelfandTransform â„‚ A a) = spectrum â„‚ a := by
  ext z
  rw [ContinuousMap.spectrum_eq_range, WeakDual.CharacterSpace.mem_spectrum_iff_exists]
  exact Iff.rfl

instance [Nontrivial A] : Nonempty (characterSpace â„‚ A) :=
  âŸ¨Classical.choose <|
      WeakDual.CharacterSpace.exists_apply_eq_zero <| zero_mem_nonunits.2 zero_ne_oneâŸ©

end ComplexBanachAlgebra

section ComplexCStarAlgebra

variable {A : Type*} [CommCStarAlgebra A]

theorem gelfandTransform_map_star (a : A) :
    gelfandTransform â„‚ A (star a) = star (gelfandTransform â„‚ A a) :=
  ContinuousMap.ext fun Ï† => map_star Ï† a

variable (A)

/-- The Gelfand transform is an isometry when the algebra is a Câ‹†-algebra over `â„‚`. -/
theorem gelfandTransform_isometry : Isometry (gelfandTransform â„‚ A) := by
  nontriviality A
  refine AddMonoidHomClass.isometry_of_norm (gelfandTransform â„‚ A) fun a => ?_
  /- By `spectrum.gelfandTransform_eq`, the spectra of `star a * a` and its
    `gelfandTransform` coincide. Therefore, so do their spectral radii, and since they are
    self-adjoint, so also do their norms. Applying the Câ‹†-property of the norm and taking square
    roots shows that the norm is preserved. -/
  have : spectralRadius â„‚ (gelfandTransform â„‚ A (star a * a)) = spectralRadius â„‚ (star a * a) := by
    unfold spectralRadius; rw [spectrum.gelfandTransform_eq]
  rw [map_mul, (IsSelfAdjoint.star_mul_self a).spectralRadius_eq_nnnorm, gelfandTransform_map_star,
    (IsSelfAdjoint.star_mul_self (gelfandTransform â„‚ A a)).spectralRadius_eq_nnnorm] at this
  simp only [ENNReal.coe_inj, CStarRing.nnnorm_star_mul_self, â† sq] at this
  simpa only [Function.comp_apply, NNReal.sqrt_sq] using
    congr_arg (((â†‘) : â„â‰¥0 â†’ â„) âˆ˜ â‡‘NNReal.sqrt) this

/-- The Gelfand transform is bijective when the algebra is a Câ‹†-algebra over `â„‚`. -/
theorem gelfandTransform_bijective : Function.Bijective (gelfandTransform â„‚ A) := by
  refine âŸ¨(gelfandTransform_isometry A).injective, ?_âŸ©
  /- The range of `gelfandTransform â„‚ A` is actually a `StarSubalgebra`. The key lemma below may be
    hard to spot; it's `map_star` coming from `WeakDual.Complex.instStarHomClass`, which is a
    nontrivial result. -/
  let rng : StarSubalgebra â„‚ C(characterSpace â„‚ A, â„‚) :=
    { toSubalgebra := (gelfandTransform â„‚ A).range
      star_mem' := by
        rintro - âŸ¨a, rflâŸ©
        use star a
        ext1 Ï†
        dsimp
        simp only [map_star, RCLike.star_def] }
  suffices rng = âŠ¤ from
    fun x => show x âˆˆ rng from this.symm â–¸ StarSubalgebra.mem_top
  /- Because the `gelfandTransform â„‚ A` is an isometry, it has closed range, and so by the
    Stone-Weierstrass theorem, it suffices to show that the image of the Gelfand transform separates
    points in `C(characterSpace â„‚ A, â„‚)` and is closed under `star`. -/
  have h : rng.topologicalClosure = rng := le_antisymm
    (StarSubalgebra.topologicalClosure_minimal le_rfl
      (gelfandTransform_isometry A).isClosedEmbedding.isClosed_range)
    (StarSubalgebra.le_topologicalClosure _)
  refine h â–¸ ContinuousMap.starSubalgebra_topologicalClosure_eq_top_of_separatesPoints
    _ (fun _ _ => ?_)
  /- Separating points just means that elements of the `characterSpace` which agree at all points
    of `A` are the same functional, which is just extensionality. -/
  contrapose!
  exact fun h => Subtype.ext (ContinuousLinearMap.ext fun a =>
    h (gelfandTransform â„‚ A a) âŸ¨gelfandTransform â„‚ A a, âŸ¨a, rflâŸ©, rflâŸ©)

/-- The Gelfand transform as a `StarAlgEquiv` between a commutative unital Câ‹†-algebra over `â„‚`
and the continuous functions on its `characterSpace`. -/
@[simps!]
noncomputable def gelfandStarTransform : A â‰ƒâ‹†â‚[â„‚] C(characterSpace â„‚ A, â„‚) :=
  StarAlgEquiv.ofBijective
    (show A â†’â‹†â‚[â„‚] C(characterSpace â„‚ A, â„‚) from
      { gelfandTransform â„‚ A with map_star' := fun x => gelfandTransform_map_star x })
    (gelfandTransform_bijective A)

end ComplexCStarAlgebra

section Functoriality

namespace WeakDual

namespace CharacterSpace

variable {A B C ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] [StarRing A]
variable [NormedRing B] [NormedAlgebra ğ•œ B] [CompleteSpace B] [StarRing B]
variable [NormedRing C] [NormedAlgebra ğ•œ C] [CompleteSpace C] [StarRing C]

/-- The functorial map taking `Ïˆ : A â†’â‹†â‚[â„‚] B` to a continuous function
`characterSpace â„‚ B â†’ characterSpace â„‚ A` obtained by pre-composition with `Ïˆ`. -/
@[simps]
noncomputable def compContinuousMap (Ïˆ : A â†’â‹†â‚[ğ•œ] B) :
    C(characterSpace ğ•œ B, characterSpace ğ•œ A) where
  toFun Ï† := equivAlgHom.symm ((equivAlgHom Ï†).comp Ïˆ.toAlgHom)
  continuous_toFun :=
    Continuous.subtype_mk
      (continuous_of_continuous_eval fun a => map_continuous <| gelfandTransform ğ•œ B (Ïˆ a)) _

variable (A) in
/-- `WeakDual.CharacterSpace.compContinuousMap` sends the identity to the identity. -/
@[simp]
theorem compContinuousMap_id :
    compContinuousMap (StarAlgHom.id ğ•œ A) = ContinuousMap.id (characterSpace ğ•œ A) :=
  ContinuousMap.ext fun _a => ext fun _x => rfl

/-- `WeakDual.CharacterSpace.compContinuousMap` is functorial. -/
@[simp]
theorem compContinuousMap_comp (Ïˆâ‚‚ : B â†’â‹†â‚[ğ•œ] C) (Ïˆâ‚ : A â†’â‹†â‚[ğ•œ] B) :
    compContinuousMap (Ïˆâ‚‚.comp Ïˆâ‚) = (compContinuousMap Ïˆâ‚).comp (compContinuousMap Ïˆâ‚‚) :=
  ContinuousMap.ext fun _a => ext fun _x => rfl

end CharacterSpace

end WeakDual

end Functoriality

open CharacterSpace in
/--
Consider the contravariant functors between compact Hausdorff spaces and commutative unital
Câ‹†algebras `F : Cpct â†’ CommCStarAlg := X â†¦ C(X, â„‚)` and
`G : CommCStarAlg â†’ Cpct := A â†’ characterSpace â„‚ A` whose actions on morphisms are given by
`WeakDual.CharacterSpace.compContinuousMap` and `ContinuousMap.compStarAlgHom'`, respectively.

Then `Î· : id â†’ F âˆ˜ G := gelfandStarTransform` is a natural isomorphism implementing (half of)
the duality between these categories. That is, for commutative unital Câ‹†-algebras `A` and `B` and
`Ï† : A â†’â‹†â‚[â„‚] B` the following diagram commutes:

```
A  --- Î· A ---> C(characterSpace â„‚ A, â„‚)

|                     |

Ï†                  (F âˆ˜ G) Ï†

|                     |
V                     V

B  --- Î· B ---> C(characterSpace â„‚ B, â„‚)
```
-/
theorem gelfandStarTransform_naturality {A B : Type*} [CommCStarAlgebra A] [CommCStarAlgebra B]
    (Ï† : A â†’â‹†â‚[â„‚] B) :
    (gelfandStarTransform B : _ â†’â‹†â‚[â„‚] _).comp Ï† =
      (compContinuousMap Ï† |>.compStarAlgHom' â„‚ â„‚).comp (gelfandStarTransform A : _ â†’â‹†â‚[â„‚] _) := by
  rfl

/--
Consider the contravariant functors between compact Hausdorff spaces and commutative unital
Câ‹†algebras `F : Cpct â†’ CommCStarAlg := X â†¦ C(X, â„‚)` and
`G : CommCStarAlg â†’ Cpct := A â†’ characterSpace â„‚ A` whose actions on morphisms are given by
`WeakDual.CharacterSpace.compContinuousMap` and `ContinuousMap.compStarAlgHom'`, respectively.

Then `Î· : id â†’ G âˆ˜ F := WeakDual.CharacterSpace.homeoEval` is a natural isomorphism implementing
(half of) the duality between these categories. That is, for compact Hausdorff spaces `X` and `Y`,
`f : C(X, Y)` the following diagram commutes:

```
X  --- Î· X ---> characterSpace â„‚ C(X, â„‚)

|                     |

f                  (G âˆ˜ F) f

|                     |
V                     V

Y  --- Î· Y ---> characterSpace â„‚ C(Y, â„‚)
```
-/
lemma WeakDual.CharacterSpace.homeoEval_naturality {X Y ğ•œ : Type*} [RCLike ğ•œ] [TopologicalSpace X]
    [CompactSpace X] [T2Space X] [TopologicalSpace Y] [CompactSpace Y] [T2Space Y] (f : C(X, Y)) :
    (homeoEval Y ğ•œ : C(_, _)).comp f =
      (f.compStarAlgHom' ğ•œ ğ•œ |> compContinuousMap).comp (homeoEval X ğ•œ : C(_, _)) :=
  rfl
