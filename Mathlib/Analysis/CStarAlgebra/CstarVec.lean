/-
Copyright (c) 2024 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/

import Mathlib.Analysis.CStarAlgebra.Module

/-!
# Vectors with entries in a C‚ãÜ-algebra

This file creates a type copy of `n ‚Üí A` meant for vectors with entries in a C‚ãÜ-algebra `A`. It is
endowed with a Hilbert C‚ãÜ-module structure, which in particular comes with an `A`-valued
"inner product" `‚ü™w, v‚ü´ = ‚àë i, star (w i) * v i` and a norm `‚Äñv‚Äñ = ‚àö‚Äñ‚ü™v, v‚ü´‚Äñ`.

## Main declarations

+ `CstarVec n A`: the type copy
+ `instCstarModule`: the Hilbert C‚ãÜ-module instance

## Implementation notes

The norm on this type induces the product uniformity and bornology, but these are not defeq to
`Pi.uniformSpace` and `Pi.instBornology`. Hence, we prove the equality to the Pi instances and
replace the uniformity and bornology by the Pi ones when registering the
`NormedAddCommGroup (CstarVec n A)` instance. See the docstring of the `TopologyAux` section below
for more details.

-/

open scoped ComplexOrder RightActions Topology Uniformity Bornology

/-- A vector with entries in a C‚ãÜ-algebra and a Hilbert C‚ãÜ-module structure. -/
def CstarVec (n : Type*) (A : Type*) := n ‚Üí A

namespace CstarVec

section basic

variable {n R A : Type*} (v w : CstarVec n A)

instance [Inhabited A] : Inhabited (CstarVec n A) :=
  ‚ü®fun _ => default‚ü©

/-- The equivalence between `n ‚Üí A` and `CstarVec n A`. -/
def ofFun : (n ‚Üí A) ‚âÉ CstarVec n A := Equiv.refl _

@[simp] lemma ofFun_symm_apply {i : n} : ofFun.symm v i = v i := rfl

@[simp] lemma ofFun_apply {i : n} : ofFun v i = v i := rfl

/-The following should not be a `FunLike` instance because then the coercion `‚áë` would get
unfolded to `FunLike.coe` instead of `ofFun.symm`. -/
instance instCoeFun : CoeFun (CstarVec n A) (fun _ => (_ : n) ‚Üí A) where
  coe := ofFun.symm

@[ext] protected theorem ext (h : ‚àÄ i, v i = w i) : v = w := funext h

instance instZero [Zero A] : Zero (CstarVec n A) :=
  Pi.instZero

instance instNeg [Neg A] : Neg (CstarVec n A) :=
  Pi.instNeg

instance instAdd [Add A] : Add (CstarVec n A) :=
  Pi.instAdd

instance instSub [Sub A] : Sub (CstarVec n A) :=
  Pi.instSub

instance instAddSemigroup [AddSemigroup A] : AddSemigroup (CstarVec n A) :=
  { instAdd, Pi.addSemigroup with }

instance instAddMonoid [AddMonoid A] : AddMonoid (CstarVec n A) :=
  { instAdd, instAddSemigroup, instZero, Pi.addMonoid with }

instance instSubNegMonoid [SubNegMonoid A] : SubNegMonoid (CstarVec n A) :=
  { instAddMonoid, instNeg, instSub, Pi.subNegMonoid with }

instance instAddGroup [AddGroup A] : AddGroup (CstarVec n A) :=
  { instSubNegMonoid, Pi.addGroup with }

instance instAddCommMonoid [AddCommMonoid A] : AddCommMonoid (CstarVec n A) :=
  { instAddMonoid, Pi.addCommMonoid with }

instance instAddCommGroup [AddCommGroup A] : AddCommGroup (CstarVec n A) :=
  { instAddGroup, Pi.addCommGroup with }

variable (R) in
instance instSMul [SMul R A] : SMul R (CstarVec n A) :=
  Pi.instSMul

instance instModule [Semiring R] [AddCommMonoid A] [Module R A] : Module R (CstarVec n A) :=
  { instSMul R, Pi.module n (fun _ => A) R with }

@[simp] theorem zero_apply [Zero A] {i : n} : (0 : CstarVec n A) i = 0 := rfl

@[simp] theorem add_apply [Add A] {i : n} : (v + w) i = v i + w i := rfl

@[simp] theorem sub_apply [Sub A] {i : n} : (v - w) i = v i - w i := rfl

@[simp] theorem smul_apply (c : R) [SMul R A] {i : n} : (c ‚Ä¢ v) i = c ‚Ä¢ v i := rfl

@[simp] theorem neg_apply [Neg A] {i : n} : (-v) i = -v i := rfl

theorem finset_sum_fn {Œπ : Type*} {s : Finset Œπ} [AddCommMonoid A] {f : Œπ ‚Üí CstarVec n A} :
    ‚àë i ‚àà s, ofFun (f i) = (fun j => ‚àë i ‚àà s, f i j) := by
  rw [Finset.sum_fn]
  rfl

end basic

section inner

variable {n R A : Type*} [Fintype n] [NonUnitalNormedRing A] [StarRing A] [NormedSpace ‚ÑÇ A]
  [PartialOrder A] [StarOrderedRing A] [CstarRing A] [IsScalarTower ‚ÑÇ A A] [SMulCommClass ‚ÑÇ A A]
  [StarModule ‚ÑÇ A]

instance instInner : Inner A (CstarVec n A) where
  inner v w := ‚àë i : n, star (v i) * w i

noncomputable instance instNorm : Norm (CstarVec n A) where
  norm v := ‚àö‚Äñ‚ü™v, v‚ü´_A‚Äñ

lemma inner_eq_sum {v w : CstarVec n A} : ‚ü™v, w‚ü´_A = ‚àë i : n, star (v i) * w i := rfl

lemma norm_eq_sum {v : CstarVec n A} : ‚Äñv‚Äñ = ‚àö‚Äñ‚àë i : n, star (v i) * v i‚Äñ := rfl

lemma inner_single_eq_entry [DecidableEq n] {v : CstarVec n A} {i : n} {a : A} :
    ‚ü™ofFun (Pi.single i a), v‚ü´_A = star a * v i := by
  simp [inner_eq_sum, ofFun_apply, Pi.single_apply]
  have hmain :
      (fun x => star (if x = i then a else 0) * v x)
        = fun x => if x = i then star a * v x else 0 := by
    ext
    split_ifs with h <;> simp [h, mul_assoc]
  simp [hmain, Finset.sum_ite]

@[simp] lemma norm_single {i : n} {a : A} [DecidableEq n] : ‚ÄñofFun (Pi.single i a)‚Äñ = ‚Äña‚Äñ := by
  simp [norm_eq_sum, Pi.single_apply, CstarRing.norm_star_mul_self]

instance instCstarModule : CstarModule A (CstarVec n A) where
  inner_add_right {v} {w‚ÇÅ} {w‚ÇÇ} := by
    simp only [inner_eq_sum, add_apply, mul_add, Finset.sum_add_distrib]
  inner_self_nonneg {x} := by
    simp only [inner]
    exact Finset.sum_nonneg fun i _ => star_mul_self_nonneg (x i)
  inner_self {x} := by
    refine ‚ü®fun h => ?_, ?_‚ü©
    ¬∑ simp only [inner] at h
      have : ‚àÄ i ‚àà Finset.univ, 0 ‚â§ star (x i) * x i := fun i _ => star_mul_self_nonneg (x i)
      rw [Finset.sum_eq_zero_iff_of_nonneg this] at h
      ext i
      simp only [zero_apply]
      exact (CstarRing.star_mul_self_eq_zero_iff (x i)).mp <| h i (Finset.mem_univ i)
    ¬∑ rintro rfl; simp only [inner, CstarVec.zero_apply, star_zero, mul_zero, Finset.sum_const_zero]
  inner_op_smul_right {a} {x} {y} := by
    simp only [inner, smul_apply, smul_eq_mul, mul_assoc]
    apply Eq.symm
    simp [Finset.sum_mul, mul_assoc]
  inner_smul_right_complex {z} {x} {y} := by
    simp only [inner_eq_sum, smul_apply, smul_mul_assoc]
    apply Eq.symm
    simp only [Finset.smul_sum, mul_smul_comm]
  star_inner x y := by simp only [inner, star_sum, star_mul, star_star]
  norm_eq_sqrt_norm_inner_self v := rfl

lemma norm_entry_le_norm [DecidableEq n] [CompleteSpace A] {v : CstarVec n A} {i : n} :
    ‚Äñv i‚Äñ ‚â§ ‚Äñv‚Äñ := by
  by_cases htriv : 0 < ‚Äñv i‚Äñ
  ¬∑ have hmain := calc ‚Äñv i‚Äñ * ‚Äñv i‚Äñ = ‚Äñstar (v i) * v i‚Äñ  := CstarRing.norm_star_mul_self.symm
      _ = ‚Äñ‚ü™ofFun (Pi.single i (v i)), v‚ü´_A‚Äñ := by rw [inner_single_eq_entry]
      _ ‚â§ ‚ÄñofFun (Pi.single i (v i))‚Äñ * ‚Äñv‚Äñ :=
            CstarModule.norm_inner_le (E := CstarVec n A)
      _ = ‚Äñv i‚Äñ * ‚Äñv‚Äñ := by rw [norm_single]
    rwa [mul_le_mul_left htriv] at hmain
  ¬∑ push_neg at htriv
    simp only [norm_le_zero_iff] at htriv
    simp only [htriv, _root_.norm_zero]
    exact CstarModule.norm_nonneg

end inner

section TopologyAux
/-
## Replacing the uniformity and bornology

Note that while the norm that we have defined on `CstarVec n A` induces the product uniformity,
it is not defeq to `Pi.uniformSpace`. In this section, we show that the norm indeed does induce
the product topology and use this fact to properly set up the `NormedAddCommGroup (CstarVec n A)`
instance such that the uniformity is still `Pi.uniformSpace` and the bornology is
`Pi.instBornology`.

To do this, we first temporarily register the "bad" `NormedAddCommGroup (CstarVec n A)` instance
and show that the map from `ofFun.symm : CstarVec n A ‚Üí (n ‚Üí A)` is both Lipschitz and
Antilipschitz.  We then finally register the `NormedAddCommGroup (CstarVec n A)` instance via
`NormedAddCommGroup.ofCoreReplaceAll`.
-/

variable {A : Type*} [NonUnitalNormedRing A] [StarRing A] [CstarRing A] [PartialOrder A]
  [CompleteSpace A] [StarOrderedRing A] [NormedSpace ‚ÑÇ A] [StarModule ‚ÑÇ A] [IsScalarTower ‚ÑÇ A A]
  [SMulCommClass ‚ÑÇ A A]

variable {n : Type*} [Fintype n]

/-- A temporary "bad" instance of `NormedAddCommGroup` that induces a uniformity that is not
defeq with the product uniformity. -/
private noncomputable abbrev normedAddCommGroupAux : NormedAddCommGroup (CstarVec n A) :=
  NormedAddCommGroup.ofCore CstarModule.normedSpaceCore

attribute [local instance] normedAddCommGroupAux

/-- A temporary "bad" instance of `NormedSpace`. -/
private noncomputable abbrev normedSpaceAux : NormedSpace ‚ÑÇ (CstarVec n A) :=
  .ofCore CstarModule.normedSpaceCore

attribute [local instance] normedSpaceAux

open Finset Classical in
private lemma lipschitzWith_ofFun_symm_aux :
    LipschitzWith 1 (ofFun.symm : CstarVec n A ‚Üí n ‚Üí A) := by
  refine LipschitzWith.of_dist_le_mul fun f g => ?_
  simp only [dist_eq_norm, NNReal.coe_one, one_mul]
  let d := f - g
  change ‚ÄñCstarVec.ofFun.symm d‚Äñ ‚â§ ‚Äñd‚Äñ
  by_cases hn : Nonempty n
  ¬∑ obtain ‚ü®i, ‚ü®hi_univ, hi‚ü©‚ü© := exists_mem_eq_sup univ univ_nonempty fun i => ‚Äñd i‚Äñ‚Çä
    simp only [dist_eq_norm, Pi.norm_def, Pi.sub_apply, ofFun_symm_apply, hi, coe_nnnorm,
      NNReal.coe_one, CstarModule.norm_eq_sqrt_norm_inner_self, inner, sub_apply, star_sub,
      one_mul]
    rw [Finset.sum_eq_add_sum_diff_singleton hi_univ]
    have h‚ÇÅ : ‚Äñd i‚Äñ = Real.sqrt ‚Äñstar (d i) * d i‚Äñ := by
      simp only [‚Üê star_sub, CstarRing.norm_star_mul_self, norm_nonneg, Real.sqrt_mul_self]
    rw [h‚ÇÅ]
    gcongr
    refine CstarRing.norm_le_norm_of_nonneg_of_le (star_mul_self_nonneg (d i)) ?_
    calc _ = star (d i) * d i + ‚àë _ ‚àà univ \ {i}, 0 := by simp
      _ ‚â§ _ := by gcongr with j; exact star_mul_self_nonneg (d j)
  ¬∑ simp only [not_nonempty_iff] at hn
    simp [dist_eq_norm, Pi.norm_def, Pi.sub_apply, ofFun_symm_apply, coe_nnnorm, NNReal.coe_one,
      CstarModule.norm_eq_sqrt_norm_inner_self, inner, sub_apply, star_sub, one_mul]

open Finset Classical in
private lemma antilipschitzWith_ofFun_symm_aux :
    AntilipschitzWith (NNReal.sqrt (Fintype.card n)) (ofFun.symm : CstarVec n A ‚Üí n ‚Üí A) := by
  refine AntilipschitzWith.of_le_mul_dist fun f g => ?_
  let d := f - g
  let D := Fintype.card n
  simp only [dist_eq_norm]
  change ‚Äñd‚Äñ ‚â§ (NNReal.sqrt D) * ‚ÄñofFun.symm d‚Äñ
  by_cases hn : Nonempty n
  ¬∑ obtain ‚ü®i, ‚ü®_, hi‚ü©‚ü© := exists_mem_eq_sup univ univ_nonempty fun i => ‚Äñd i‚Äñ‚Çä
    simp only [dist_eq_norm, Pi.norm_def, Pi.sub_apply, ofFun_symm_apply, hi, coe_nnnorm,
      NNReal.coe_one, CstarModule.norm_eq_sqrt_norm_inner_self, inner, sub_apply, star_sub,
      one_mul]
    have h‚ÇÄ : ‚àÄ j, ‚Äñstar (d j) * d j‚Äñ ‚â§ ‚Äñstar (d i) * d i‚Äñ := fun j => by
      simp only [CstarRing.norm_star_mul_self]
      have : ‚Äñd j‚Äñ ‚â§ ‚Äñd i‚Äñ := by
        convert_to (‚Äñd j‚Äñ‚Çä : ‚Ñù) ‚â§ (‚Äñd i‚Äñ‚Çä : ‚Ñù)
        rw [‚Üê hi]
        gcongr
        exact Finset.le_sup (f := fun j => ‚Äñd j‚Äñ‚Çä) (mem_univ j)
      gcongr
    have h‚ÇÅ : Real.sqrt ‚Äñ‚àë j, star (d j) * d j‚Äñ ‚â§ Real.sqrt ‚Äñ‚àë (_ : n), star (d i) * d i‚Äñ := by
      gcongr
      calc _ ‚â§ ‚àë j : n, ‚Äñstar (d j) * d j‚Äñ := norm_sum_le univ fun i => star (d i) * d i
        _ ‚â§ ‚àë _ : n, ‚Äñstar (d i) * d i‚Äñ := by gcongr with j; exact h‚ÇÄ j
        _ = ‚Äñ‚àë _ : n, star (d i) * d i‚Äñ := by
                simp only [sum_const]
                exact Eq.symm (RCLike.norm_nsmul ‚ÑÇ univ.card (star (d i) * d i))
    refine h‚ÇÅ.trans ?_
    simp only [sum_const, card_univ, Real.coe_sqrt]
    have h‚ÇÇ : Real.sqrt ‚ÄñD ‚Ä¢ (star (d i) * d i)‚Äñ ‚â§ Real.sqrt (D * ‚Äñstar (d i) * d i‚Äñ) := by
      gcongr
      exact norm_nsmul_le _ _
    refine h‚ÇÇ.trans ?_
    simp only [Nat.cast_nonneg, Real.sqrt_mul, NNReal.coe_natCast, CstarRing.norm_self_mul_star]
    gcongr
    simp only [CstarRing.norm_star_mul_self, norm_nonneg, Real.sqrt_mul_self, le_refl]
  ¬∑ simp only [not_nonempty_iff] at hn
    simp [dist_eq_norm, Pi.norm_def, Pi.sub_apply, ofFun_symm_apply, coe_nnnorm, NNReal.coe_one,
      CstarModule.norm_eq_sqrt_norm_inner_self, inner, sub_apply, star_sub, one_mul]

private lemma uniformInducing_ofFun_symm_aux :
    UniformInducing (CstarVec.ofFun.symm : CstarVec n A ‚Üí n ‚Üí A) :=
  AntilipschitzWith.uniformInducing antilipschitzWith_ofFun_symm_aux
    lipschitzWith_ofFun_symm_aux.uniformContinuous

private lemma uniformity_eq_aux : ùì§ (CstarVec n A) = ùì§[Pi.uniformSpace _] := by
  have : (fun x : CstarVec n A √ó CstarVec n A => ‚ü®ofFun.symm x.1, ofFun.symm x.2‚ü©) = id := by
    ext i <;> rfl
  rw [‚Üê uniformInducing_ofFun_symm_aux.comap_uniformity, this, Filter.comap_id]

open Bornology in
private lemma cobounded_eq_aux : cobounded (CstarVec n A) = @cobounded _ Pi.instBornology := by
  have : cobounded (CstarVec n A) = Filter.comap ofFun.symm (cobounded _) := by
    refine le_antisymm ?_ ?_
    ¬∑ exact antilipschitzWith_ofFun_symm_aux.tendsto_cobounded.le_comap
    ¬∑ exact lipschitzWith_ofFun_symm_aux.comap_cobounded_le
  exact this.trans Filter.comap_id

end TopologyAux

section NormedSpace

variable {A : Type*} [NonUnitalNormedRing A] [StarRing A] [CstarRing A] [PartialOrder A]
  [CompleteSpace A] [StarOrderedRing A] [NormedSpace ‚ÑÇ A] [StarModule ‚ÑÇ A] [IsScalarTower ‚ÑÇ A A]
  [SMulCommClass ‚ÑÇ A A]

variable {n : Type*} [Fintype n]

instance : TopologicalSpace (CstarVec n A) := Pi.topologicalSpace

instance : UniformSpace (CstarVec n A) := Pi.uniformSpace _

instance : Bornology (CstarVec n A) := Pi.instBornology

instance : CompleteSpace (CstarVec n A) := Pi.complete _

noncomputable instance instNormedAddCommGroup : NormedAddCommGroup (CstarVec n A) :=
  NormedAddCommGroup.ofCoreReplaceAll CstarModule.normedSpaceCore
    uniformity_eq_aux.symm fun _ => Filter.ext_iff.1 cobounded_eq_aux.symm _

instance instNormedSpace : NormedSpace ‚ÑÇ (CstarVec n A) :=
  NormedSpace.ofCore CstarModule.normedSpaceCore

end NormedSpace

end CstarVec
