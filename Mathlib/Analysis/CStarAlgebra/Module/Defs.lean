/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order

/-!
# Hilbert Câ‹†-modules

A Hilbert Câ‹†-module is a complex module `E` together with a right `A`-module structure, where `A`
is a Câ‹†-algebra, and with an `A`-valued inner product. This inner product satisfies the
Cauchy-Schwarz inequality, and induces a norm that makes `E` a normed vector space over `â„‚`.

## Main declarations

+ `CStarModule`: The class containing the Hilbert Câ‹†-module structure
+ `CStarModule.normedSpaceCore`: The proof that a Hilbert Câ‹†-module is a normed vector
  space. This can be used with `NormedAddCommGroup.ofCore` and `NormedSpace.ofCore` to create
  the relevant instances on a type of interest.
+ `CStarModule.inner_mul_inner_swap_le`: The statement that
  `âŸªy, xâŸ« * âŸªx, yâŸ« â‰¤ â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«`, which can be viewed as a version of the Cauchy-Schwarz
  inequality for Hilbert Câ‹†-modules.
+ `CStarModule.norm_inner_le`, which states that `â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€–`, i.e. the
  Cauchy-Schwarz inequality.

## Implementation notes

The class `CStarModule A E` requires `E` to already have a `Norm E` instance on it, but
no other norm-related instances. We then include the fact that this norm agrees with the norm
induced by the inner product among the axioms of the class. Furthermore, instead of registering
`NormedAddCommGroup E` and `NormedSpace â„‚ E` instances (which might already be present on the type,
and which would send the type class search algorithm on a chase for `A`), we provide a
`NormedSpace.Core` structure which enables downstream users of the class to easily register
these instances themselves on a particular type.

Although the `Norm` is passed as a parameter, it almost never coincides with the norm on the
underlying type, unless that it is a purpose built type, as with the *standard Hilbert Câ‹†-module*.
However, with generic types already equipped with a norm, the norm as a Hilbert Câ‹†-module almost
never coincides with the norm on the underlying type. The two notable exceptions to this are when
we view `A` as a Câ‹†-module over itself, or when `A := â„‚`.  For this reason we will later use the
type synonym `WithCStarModule`.

As an example of just how different the norm can be, consider `CStarModule`s `E` and `F` over `A`.
One would like to put a `CStarModule` structure on (a type synonym of) `E Ã— F`, where the `A`-valued
inner product is given, for `x y : E Ã— F`, `âŸªx, yâŸ«_A := âŸªx.1, y.1âŸ«_A + âŸªx.2, y.2âŸ«_A`. The norm this
induces satisfies `â€–xâ€– ^ 2 = â€–âŸªx.1, y.1âŸ« + âŸªx.2, y.2âŸ«â€–`, but this doesn't coincide with *any*
natural norm on `E Ã— F` unless `A := â„‚`, in which case it is `WithLp 2 (E Ã— F)` because `E Ã— F` is
then an `InnerProductSpace` over `â„‚`.

## References

+ Erin Wittlich. *Formalizing Hilbert Modules in Câ‹†-algebras with the Lean Proof Assistant*,
  December 2022. Master's thesis, Southern Illinois University Edwardsville.
-/

open scoped ComplexOrder RightActions

/-- A *Hilbert Câ‹†-module* is a complex module `E` endowed with a right `A`-module structure
(where `A` is typically a Câ‹†-algebra) and an inner product `âŸªx, yâŸ«_A` which satisfies the
following properties. -/
class CStarModule (A : outParam <| Type*) (E : Type*) [NonUnitalSemiring A] [StarRing A]
    [Module â„‚ A] [AddCommGroup E] [Module â„‚ E] [PartialOrder A] [SMul Aáµáµ’áµ– E] [Norm A] [Norm E]
    extends Inner A E where
  inner_add_right {x} {y} {z} : inner x (y + z) = inner x y + inner x z
  inner_self_nonneg {x} : 0 â‰¤ inner x x
  inner_self {x} : inner x x = 0 â†” x = 0
  inner_op_smul_right {a : A} {x y : E} : inner x (y <â€¢ a) = inner x y * a
  inner_smul_right_complex {z : â„‚} {x} {y} : inner x (z â€¢ y) = z â€¢ inner x y
  star_inner x y : star (inner x y) = inner y x
  norm_eq_sqrt_norm_inner_self x : â€–xâ€– = âˆšâ€–inner x xâ€–

attribute [simp] CStarModule.inner_add_right CStarModule.star_inner
  CStarModule.inner_op_smul_right CStarModule.inner_smul_right_complex

@[deprecated (since := "2024-08-04")] alias CstarModule := CStarModule

namespace CStarModule

section general

variable {A E : Type*} [NonUnitalRing A] [StarRing A] [AddCommGroup E] [Module â„‚ A]
  [Module â„‚ E] [PartialOrder A] [SMul Aáµáµ’áµ– E] [Norm A] [Norm E] [CStarModule A E]

local notation "âŸª" x ", " y "âŸ«" => inner (ğ•œ := A) x y

@[simp]
lemma inner_add_left {x y z : E} : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« := by
  rw [â† star_star (r := âŸªx + y, zâŸ«)]
  simp only [inner_add_right, star_add, star_inner]

@[simp]
lemma inner_op_smul_left {a : A} {x y : E} : âŸªx <â€¢ a, yâŸ« = star a * âŸªx, yâŸ« := by
  rw [â† star_inner]; simp

section StarModule

variable [StarModule â„‚ A]

@[simp]
lemma inner_smul_left_complex {z : â„‚} {x y : E} : âŸªz â€¢ x, yâŸ« = star z â€¢ âŸªx, yâŸ« := by
  rw [â† star_inner]
  simp

@[simp]
lemma inner_smul_left_real {z : â„} {x y : E} : âŸªz â€¢ x, yâŸ« = z â€¢ âŸªx, yâŸ« := by
  have hâ‚ : z â€¢ x = (z : â„‚) â€¢ x := by simp
  rw [hâ‚, â† star_inner, inner_smul_right_complex]
  simp

@[simp]
lemma inner_smul_right_real {z : â„} {x y : E} : âŸªx, z â€¢ yâŸ« = z â€¢ âŸªx, yâŸ« := by
  have hâ‚ : z â€¢ y = (z : â„‚) â€¢ y := by simp
  rw [hâ‚, â† star_inner, inner_smul_left_complex]
  simp

/-- The function `âŸ¨x, yâŸ© â†¦ âŸªx, yâŸ«` bundled as a sesquilinear map. -/
def innerâ‚›â‚— : E â†’â‚—â‹†[â„‚] E â†’â‚—[â„‚] A where
  toFun x := { toFun := fun y => âŸªx, yâŸ«
               map_add' := fun z y => by simp
               map_smul' := fun z y => by simp }
  map_add' z y := by ext; simp
  map_smul' z y := by ext; simp

lemma innerâ‚›â‚—_apply {x y : E} : innerâ‚›â‚— x y = âŸªx, yâŸ« := rfl

@[simp] lemma inner_zero_right {x : E} : âŸªx, 0âŸ« = 0 := by simp [â† innerâ‚›â‚—_apply]
@[simp] lemma inner_zero_left {x : E} : âŸª0, xâŸ« = 0 := by simp [â† innerâ‚›â‚—_apply]
@[simp] lemma inner_neg_right {x y : E} : âŸªx, -yâŸ« = -âŸªx, yâŸ« := by simp [â† innerâ‚›â‚—_apply]
@[simp] lemma inner_neg_left {x y : E} : âŸª-x, yâŸ« = -âŸªx, yâŸ« := by simp [â† innerâ‚›â‚—_apply]
@[simp] lemma inner_sub_right {x y z : E} : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« := by
  simp [â† innerâ‚›â‚—_apply]
@[simp] lemma inner_sub_left {x y z : E} : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« := by
  simp [â† innerâ‚›â‚—_apply]

@[simp]
lemma inner_sum_right {Î¹ : Type*} {s : Finset Î¹} {x : E} {y : Î¹ â†’ E} :
    âŸªx, âˆ‘ i âˆˆ s, y iâŸ« = âˆ‘ i âˆˆ s, âŸªx, y iâŸ« :=
  map_sum (innerâ‚›â‚— x) ..

@[simp]
lemma inner_sum_left {Î¹ : Type*} {s : Finset Î¹} {x : Î¹ â†’ E} {y : E} :
    âŸªâˆ‘ i âˆˆ s, x i, yâŸ« = âˆ‘ i âˆˆ s, âŸªx i, yâŸ« :=
  map_sum (innerâ‚›â‚—.flip y) ..

end StarModule

@[simp]
lemma isSelfAdjoint_inner_self {x : E} : IsSelfAdjoint âŸªx, xâŸ« := star_inner _ _

end general

section norm

variable {A E : Type*} [NonUnitalNormedRing A] [StarRing A] [PartialOrder A]
  [AddCommGroup E] [NormedSpace â„‚ A] [Module â„‚ E] [SMul Aáµáµ’áµ– E] [Norm E] [CStarModule A E]

local notation "âŸª" x ", " y "âŸ«" => inner (ğ•œ := A) x y

/-- The norm associated with a Hilbert Câ‹†-module. It is not registered as a norm, since a type
might already have a norm defined on it. -/
noncomputable def norm (A : Type*) {E : Type*} [Norm A] [Inner A E] : Norm E where
  norm x := Real.sqrt â€–âŸªx, xâŸ«_Aâ€–

lemma norm_sq_eq {x : E} : â€–xâ€– ^ 2 = â€–âŸªx, xâŸ«â€– := by simp [norm_eq_sqrt_norm_inner_self]

section
include A

protected lemma norm_nonneg {x : E} : 0 â‰¤ â€–xâ€– := by simp [norm_eq_sqrt_norm_inner_self]

protected lemma norm_pos {x : E} (hx : x â‰  0) : 0 < â€–xâ€– := by
  simp only [norm_eq_sqrt_norm_inner_self, Real.sqrt_pos, norm_pos_iff]
  intro H
  rw [inner_self] at H
  exact hx H

variable [StarModule â„‚ A]

protected lemma norm_zero : â€–(0 : E)â€– = 0 := by simp [norm_eq_sqrt_norm_inner_self]

lemma norm_zero_iff (x : E) : â€–xâ€– = 0 â†” x = 0 :=
  âŸ¨fun h => by simpa [norm_eq_sqrt_norm_inner_self, inner_self] using h,
    fun h => by simp [norm, h, norm_eq_sqrt_norm_inner_self]âŸ©

end

variable [CStarRing A] [StarOrderedRing A] [StarModule â„‚ A]
  [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A]

/-- The Câ‹†-algebra-valued Cauchy-Schwarz inequality for Hilbert Câ‹†-modules. -/
lemma inner_mul_inner_swap_le [CompleteSpace A] {x y : E} : âŸªy, xâŸ« * âŸªx, yâŸ« â‰¤ â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ« := by
  rcases eq_or_ne x 0 with h|h
  Â· simp [h, CStarModule.norm_zero (E := E)]
  Â· have hâ‚ : âˆ€ (a : A),
        (0 : A) â‰¤ â€–xâ€– ^ 2 â€¢ (star a * a) - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := fun a => by
      calc (0 : A) â‰¤ âŸªx <â€¢ a - â€–xâ€– ^ 2 â€¢ y, x <â€¢ a - â€–xâ€– ^ 2 â€¢ yâŸ«_A := by
                      exact inner_self_nonneg
            _ = star a * âŸªx, xâŸ« * a - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := by
                      simp only [inner_sub_right, inner_op_smul_right, inner_sub_left,
                        inner_op_smul_left, inner_smul_left_real, sub_mul, smul_mul_assoc,
                        inner_smul_right_real, smul_sub]
                      abel
            _ â‰¤ â€–xâ€– ^ 2 â€¢ (star a * a) - â€–xâ€– ^ 2 â€¢ (âŸªy, xâŸ« * a)
                  - â€–xâ€– ^ 2 â€¢ (star a * âŸªx, yâŸ«) + â€–xâ€– ^ 2 â€¢ (â€–xâ€– ^ 2 â€¢ âŸªy, yâŸ«) := by
                      gcongr
                      calc _ â‰¤ â€–âŸªx, xâŸ«_Aâ€– â€¢ (star a * a) := CStarRing.conjugate_le_norm_smul
                        _ = (Real.sqrt â€–âŸªx, xâŸ«_Aâ€–) ^ 2 â€¢ (star a * a) := by
                                  congr
                                  have : 0 â‰¤ â€–âŸªx, xâŸ«_Aâ€– := by positivity
                                  rw [Real.sq_sqrt this]
                        _ = â€–xâ€– ^ 2 â€¢ (star a * a) := by rw [â† norm_eq_sqrt_norm_inner_self]
    specialize hâ‚ âŸªx, yâŸ«
    simp only [star_inner, sub_self, zero_sub, le_neg_add_iff_add_le, add_zero] at hâ‚
    rwa [smul_le_smul_iff_of_pos_left (pow_pos (CStarModule.norm_pos h) _)] at hâ‚

variable (E) in
/-- The Cauchy-Schwarz inequality for Hilbert Câ‹†-modules. -/
lemma norm_inner_le [CompleteSpace A] {x y : E} : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := by
  have := calc â€–âŸªx, yâŸ«â€– ^ 2 = â€–âŸªy, xâŸ« * âŸªx, yâŸ«â€– := by
                rw [â† star_inner x, CStarRing.norm_star_mul_self, pow_two]
    _ â‰¤ â€–â€–xâ€–^ 2 â€¢ âŸªy, yâŸ«â€– := by
                refine CStarRing.norm_le_norm_of_nonneg_of_le ?_ inner_mul_inner_swap_le
                rw [â† star_inner x]
                exact star_mul_self_nonneg âŸªx, yâŸ«_A
    _ = â€–xâ€– ^ 2 * â€–âŸªy, yâŸ«â€– := by simp [norm_smul]
    _ = â€–xâ€– ^ 2 * â€–yâ€– ^ 2 := by
                simp only [norm_eq_sqrt_norm_inner_self, norm_nonneg, Real.sq_sqrt]
    _ = (â€–xâ€– * â€–yâ€–) ^ 2 := by simp only [mul_pow]
  refine (pow_le_pow_iff_left (R := â„) (norm_nonneg âŸªx, yâŸ«_A) ?_ (by norm_num)).mp this
  exact mul_nonneg CStarModule.norm_nonneg CStarModule.norm_nonneg

protected lemma norm_triangle [CompleteSpace A] (x y : E) : â€–x + yâ€– â‰¤ â€–xâ€– + â€–yâ€– := by
  have h : â€–x + yâ€– ^ 2 â‰¤ (â€–xâ€– + â€–yâ€–) ^ 2 := by
    calc _ â‰¤ â€–âŸªx, xâŸ« + âŸªy, xâŸ«â€– + â€–âŸªx, yâŸ«â€– + â€–âŸªy, yâŸ«â€– := by
          simp only [norm_eq_sqrt_norm_inner_self, inner_add_right, inner_add_left, â† add_assoc,
            norm_nonneg, Real.sq_sqrt]
          exact norm_addâ‚ƒ_le _ _ _
      _ â‰¤ â€–âŸªx, xâŸ«â€– + â€–âŸªy, xâŸ«â€– + â€–âŸªx, yâŸ«â€– + â€–âŸªy, yâŸ«â€– := by gcongr; exact norm_add_le _ _
      _ â‰¤ â€–âŸªx, xâŸ«â€– + â€–yâ€– * â€–xâ€– + â€–xâ€– * â€–yâ€– + â€–âŸªy, yâŸ«â€– := by gcongr <;> exact norm_inner_le E
      _ = â€–xâ€– ^ 2 + â€–yâ€– * â€–xâ€– + â€–xâ€– * â€–yâ€– + â€–yâ€– ^ 2 := by
          simp [norm_eq_sqrt_norm_inner_self]
      _ = (â€–xâ€– + â€–yâ€–) ^ 2 := by simp only [add_pow_two, add_left_inj]; ring
  refine (pow_le_pow_iff_left CStarModule.norm_nonneg ?_ (by norm_num)).mp h
  exact add_nonneg CStarModule.norm_nonneg CStarModule.norm_nonneg

/-- This allows us to get `NormedAddCommGroup` and `NormedSpace` instances on `E` via
`NormedAddCommGroup.ofCore` and `NormedSpace.ofCore`. -/
lemma normedSpaceCore [CompleteSpace A] : NormedSpace.Core â„‚ E where
  norm_nonneg x := CStarModule.norm_nonneg
  norm_eq_zero_iff x := norm_zero_iff x
  norm_smul c x := by simp [norm_eq_sqrt_norm_inner_self, norm_smul, â† mul_assoc]
  norm_triangle x y := CStarModule.norm_triangle x y

/-- This is not listed as an instance because we often want to replace the topology, uniformity
and bornology instead of inheriting them from the norm. -/
abbrev normedAddCommGroup [CompleteSpace A] : NormedAddCommGroup E :=
  NormedAddCommGroup.ofCore CStarModule.normedSpaceCore

lemma norm_eq_csSup [CompleteSpace A] (v : E) :
    â€–vâ€– = sSup { â€–âŸªw, vâŸ«_Aâ€– | (w : E) (_ : â€–wâ€– â‰¤ 1) } := by
  let instNACG : NormedAddCommGroup E := NormedAddCommGroup.ofCore normedSpaceCore
  let instNS : NormedSpace â„‚ E := .ofCore normedSpaceCore
  refine Eq.symm <| IsGreatest.csSup_eq âŸ¨âŸ¨â€–vâ€–â»Â¹ â€¢ v, ?_, ?_âŸ©, ?_âŸ©
  Â· simpa only [norm_smul, norm_inv, norm_norm] using inv_mul_le_one_of_le le_rfl (by positivity)
  Â· simp [norm_smul, â† norm_sq_eq, pow_two, â† mul_assoc]
  Â· rintro - âŸ¨w, hw, rflâŸ©
    calc _ â‰¤ â€–wâ€– * â€–vâ€– := norm_inner_le E
      _ â‰¤ 1 * â€–vâ€– := by gcongr
      _ = â€–vâ€– := by simp

end norm

section NormedAddCommGroup

/- Note: one generally creates a `CStarModule` instance for a type `E` first before getting the
`NormedAddCommGroup` and `NormedSpace` instances via `CStarModule.normedSpaceCore`, especially by
using `NormedAddCommGroup.ofCoreReplaceAll` and `NormedSpace.ofCore`. See
`Analysis.CStarAlgebra.Module.Constructions` for examples. -/
variable {A E : Type*} [NonUnitalNormedRing A] [StarRing A] [CStarRing A] [PartialOrder A]
  [StarOrderedRing A] [NormedSpace â„‚ A] [SMul Aáµáµ’áµ– E] [CompleteSpace A]
  [NormedAddCommGroup E] [NormedSpace â„‚ E] [StarModule â„‚ A] [CStarModule A E] [IsScalarTower â„‚ A A]
  [SMulCommClass â„‚ A A]

/-- The function `âŸ¨x, yâŸ© â†¦ âŸªx, yâŸ«` bundled as a continuous sesquilinear map. -/
noncomputable def innerSL : E â†’Lâ‹†[â„‚] E â†’L[â„‚] A :=
  LinearMap.mkContinuousâ‚‚ (innerâ‚›â‚— : E â†’â‚—â‹†[â„‚] E â†’â‚—[â„‚] A) 1 <| fun x y => by
    simp [innerâ‚›â‚—_apply, norm_inner_le E]

lemma innerSL_apply {x y : E} : innerSL x y = âŸªx, yâŸ«_A := rfl

@[continuity, fun_prop]
lemma continuous_inner : Continuous (fun x : E Ã— E => âŸªx.1, x.2âŸ«_A) := by
  simp_rw [â† innerSL_apply]
  fun_prop

end NormedAddCommGroup

end CStarModule
