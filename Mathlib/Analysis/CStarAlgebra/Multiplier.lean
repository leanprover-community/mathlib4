/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux, Jon Bannon
-/
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.SpecialFunctions.Pow.NNReal

/-!
# Multiplier Algebra of a Câ‹†-algebra

Define the multiplier algebra of a Câ‹†-algebra as the algebra (over `ğ•œ`) of double centralizers,
for which we provide the localized notation `ğ“œ(ğ•œ, A)`.  A double centralizer is a pair of
continuous linear maps `L R : A â†’L[ğ•œ] A` satisfying the intertwining condition `R x * y = x * L y`.

There is a natural embedding `A â†’ ğ“œ(ğ•œ, A)` which sends `a : A` to the continuous linear maps
`L R : A â†’L[ğ•œ] A` given by left and right multiplication by `a`, and we provide this map as a
coercion.

The multiplier algebra corresponds to a non-commutative Stoneâ€“ÄŒech compactification in the sense
that when the algebra `A` is commutative, it can be identified with `Câ‚€(X, â„‚)` for some locally
compact Hausdorff space `X`, and in that case `ğ“œ(ğ•œ, A)` can be identified with `C(Î² X, â„‚)`.

## Implementation notes

We make the hypotheses on `ğ•œ` as weak as possible so that, in particular, this construction works
for both `ğ•œ = â„` and `ğ•œ = â„‚`.

The reader familiar with Câ‹†-algebra theory may recognize that one
only needs `L` and `R` to be functions instead of continuous linear maps, at least when `A` is a
Câ‹†-algebra. Our intention is simply to eventually provide a constructor for this situation.

We pull back the `NormedAlgebra` structure (and everything contained therein) through the
ring (even algebra) homomorphism
`DoubleCentralizer.toProdMulOppositeHom : ğ“œ(ğ•œ, A) â†’+* (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–` which
sends `a : ğ“œ(ğ•œ, A)` to `(a.fst, MulOpposite.op a.snd)`. The star structure is provided
separately.

## References

* https://en.wikipedia.org/wiki/Multiplier_algebra

## TODO

+ Define a type synonym for `ğ“œ(ğ•œ, A)` which is equipped with the strict uniform space structure
  and show it is complete
+ Show that the image of `A` in `ğ“œ(ğ•œ, A)` is an essential ideal
+ Prove the universal property of `ğ“œ(ğ•œ, A)`
+ Construct a double centralizer from a pair of maps (not necessarily linear or continuous)
  `L : A â†’ A`, `R : A â†’ A` satisfying the centrality condition `âˆ€ x y, R x * y = x * L y`.
+ Show that if `A` is unital, then `A â‰ƒâ‹†â‚[ğ•œ] ğ“œ(ğ•œ, A)`.
-/


open NNReal ENNReal ContinuousLinearMap MulOpposite

universe u v

/-- The type of *double centralizers*, also known as the *multiplier algebra* and denoted by
`ğ“œ(ğ•œ, A)`, of a non-unital normed algebra.

If `x : ğ“œ(ğ•œ, A)`, then `x.fst` and `x.snd` are what is usually referred to as $L$ and $R$. -/
structure DoubleCentralizer (ğ•œ : Type u) (A : Type v) [NontriviallyNormedField ğ•œ]
    [NonUnitalNormedRing A] [NormedSpace ğ•œ A] [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A] extends
    (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A) where
  /-- The centrality condition that the maps linear maps intertwine one another. -/
  central : âˆ€ x y : A, snd x * y = x * fst y

@[inherit_doc]
scoped[MultiplierAlgebra] notation "ğ“œ(" ğ•œ ", " A ")" => DoubleCentralizer ğ•œ A

open MultiplierAlgebra

@[ext]
lemma DoubleCentralizer.ext (ğ•œ : Type u) (A : Type v) [NontriviallyNormedField ğ•œ]
    [NonUnitalNormedRing A] [NormedSpace ğ•œ A] [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A]
    (a b : ğ“œ(ğ•œ, A)) (h : a.toProd = b.toProd) : a = b := by
  cases a
  cases b
  simpa using h

namespace DoubleCentralizer

section NontriviallyNormed

variable (ğ•œ A : Type*) [NontriviallyNormedField ğ•œ] [NonUnitalNormedRing A]
variable [NormedSpace ğ•œ A] [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A]

/-!
### Algebraic structure

Because the multiplier algebra is defined as the algebra of double centralizers, there is a natural
injection `DoubleCentralizer.toProdMulOpposite : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–`
defined by `fun a â†¦ (a.fst, MulOpposite.op a.snd)`. We use this map to pull back the ring, module
and algebra structure from `(A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–` to `ğ“œ(ğ•œ, A)`. -/

variable {ğ•œ A}

theorem range_toProd :
    Set.range toProd = { lr : (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A) | âˆ€ x y, lr.2 x * y = x * lr.1 y } :=
  Set.ext fun x =>
    âŸ¨by
      rintro âŸ¨a, rflâŸ©
      exact a.central, fun hx => âŸ¨âŸ¨x, hxâŸ©, rflâŸ©âŸ©

instance instAdd : Add ğ“œ(ğ•œ, A) where
  add a b :=
    { toProd := a.toProd + b.toProd
      central := fun x y =>
        show (a.snd + b.snd) x * y = x * (a.fst + b.fst) y by
          simp only [ContinuousLinearMap.add_apply, mul_add, add_mul, central] }

instance instZero : Zero ğ“œ(ğ•œ, A) where
  zero :=
    { toProd := 0
      central := fun x y => (zero_mul y).trans (mul_zero x).symm }

instance instNeg : Neg ğ“œ(ğ•œ, A) where
  neg a :=
    { toProd := -a.toProd
      central := fun x y =>
        show -a.snd x * y = x * -a.fst y by
          simp only [neg_mul, mul_neg, central] }

instance instSub : Sub ğ“œ(ğ•œ, A) where
  sub a b :=
    { toProd := a.toProd - b.toProd
      central := fun x y =>
        show (a.snd - b.snd) x * y = x * (a.fst - b.fst) y by
          simp only [ContinuousLinearMap.sub_apply, _root_.sub_mul, _root_.mul_sub, central] }

section Scalars

variable {S : Type*} [Monoid S] [DistribMulAction S A] [SMulCommClass ğ•œ S A]
  [ContinuousConstSMul S A] [IsScalarTower S A A] [SMulCommClass S A A]

instance instSMul : SMul S ğ“œ(ğ•œ, A) where
  smul s a :=
    { toProd := s â€¢ a.toProd
      central := fun x y =>
        show (s â€¢ a.snd) x * y = x * (s â€¢ a.fst) y by
          simp only [ContinuousLinearMap.smul_apply, mul_smul_comm, smul_mul_assoc, central] }

@[simp]
theorem smul_toProd (s : S) (a : ğ“œ(ğ•œ, A)) : (s â€¢ a).toProd = s â€¢ a.toProd :=
  rfl

theorem smul_fst (s : S) (a : ğ“œ(ğ•œ, A)) : (s â€¢ a).fst = s â€¢ a.fst :=
  rfl

theorem smul_snd (s : S) (a : ğ“œ(ğ•œ, A)) : (s â€¢ a).snd = s â€¢ a.snd :=
  rfl

variable {T : Type*} [Monoid T] [DistribMulAction T A] [SMulCommClass ğ•œ T A]
  [ContinuousConstSMul T A] [IsScalarTower T A A] [SMulCommClass T A A]

instance instIsScalarTower [SMul S T] [IsScalarTower S T A] : IsScalarTower S T ğ“œ(ğ•œ, A) where
  smul_assoc _ _ a := ext (ğ•œ := ğ•œ) (A := A) _ _ <| smul_assoc _ _ a.toProd

instance instSMulCommClass [SMulCommClass S T A] : SMulCommClass S T ğ“œ(ğ•œ, A) where
  smul_comm _ _ a := ext (ğ•œ := ğ•œ) (A := A) _ _ <| smul_comm _ _ a.toProd

instance instIsCentralScalar {R : Type*} [Semiring R] [Module R A] [SMulCommClass ğ•œ R A]
    [ContinuousConstSMul R A] [IsScalarTower R A A] [SMulCommClass R A A] [Module Ráµáµ’áµ– A]
    [IsCentralScalar R A] : IsCentralScalar R ğ“œ(ğ•œ, A) where
  op_smul_eq_smul _ a := ext (ğ•œ := ğ•œ) (A := A) _ _ <| op_smul_eq_smul _ a.toProd

end Scalars

instance instOne : One ğ“œ(ğ•œ, A) :=
  âŸ¨âŸ¨1, fun _x _y => rflâŸ©âŸ©

instance instMul : Mul ğ“œ(ğ•œ, A) where
  mul a b :=
    { toProd := (a.fst.comp b.fst, b.snd.comp a.snd)
      central := fun x y => show b.snd (a.snd x) * y = x * a.fst (b.fst y) by simp only [central] }

instance instNatCast : NatCast ğ“œ(ğ•œ, A) where
  natCast n :=
    âŸ¨n, fun x y => by
      rw [Prod.snd_natCast, Prod.fst_natCast]
      simp only [â† Nat.smul_one_eq_cast, smul_apply, one_apply, mul_smul_comm, smul_mul_assoc]âŸ©

instance instIntCast : IntCast ğ“œ(ğ•œ, A) where
  intCast n :=
    âŸ¨n, fun x y => by
      rw [Prod.snd_intCast, Prod.fst_intCast]
      simp only [â† Int.smul_one_eq_cast, smul_apply, one_apply, mul_smul_comm, smul_mul_assoc]âŸ©

instance instPow : Pow ğ“œ(ğ•œ, A) â„• where
  pow a n :=
    âŸ¨a.toProd ^ n, fun x y => by
      induction n generalizing x y with
      | zero => rfl
      | succ k hk =>
        rw [Prod.pow_snd, Prod.pow_fst] at hk âŠ¢
        rw [pow_succ' a.snd, mul_apply, a.central, hk, pow_succ a.fst, mul_apply]âŸ©

instance instInhabited : Inhabited ğ“œ(ğ•œ, A) :=
  âŸ¨0âŸ©

@[simp]
theorem add_toProd (a b : ğ“œ(ğ•œ, A)) : (a + b).toProd = a.toProd + b.toProd :=
  rfl

@[simp]
theorem zero_toProd : (0 : ğ“œ(ğ•œ, A)).toProd = 0 :=
  rfl

@[simp]
theorem neg_toProd (a : ğ“œ(ğ•œ, A)) : (-a).toProd = -a.toProd :=
  rfl

@[simp]
theorem sub_toProd (a b : ğ“œ(ğ•œ, A)) : (a - b).toProd = a.toProd - b.toProd :=
  rfl

@[simp]
theorem one_toProd : (1 : ğ“œ(ğ•œ, A)).toProd = 1 :=
  rfl

@[simp]
theorem natCast_toProd (n : â„•) : (n : ğ“œ(ğ•œ, A)).toProd = n :=
  rfl

@[simp]
theorem intCast_toProd (n : â„¤) : (n : ğ“œ(ğ•œ, A)).toProd = n :=
  rfl

@[simp]
theorem pow_toProd (n : â„•) (a : ğ“œ(ğ•œ, A)) : (a ^ n).toProd = a.toProd ^ n :=
  rfl

theorem add_fst (a b : ğ“œ(ğ•œ, A)) : (a + b).fst = a.fst + b.fst :=
  rfl

theorem add_snd (a b : ğ“œ(ğ•œ, A)) : (a + b).snd = a.snd + b.snd :=
  rfl

theorem zero_fst : (0 : ğ“œ(ğ•œ, A)).fst = 0 :=
  rfl

theorem zero_snd : (0 : ğ“œ(ğ•œ, A)).snd = 0 :=
  rfl

theorem neg_fst (a : ğ“œ(ğ•œ, A)) : (-a).fst = -a.fst :=
  rfl

theorem neg_snd (a : ğ“œ(ğ•œ, A)) : (-a).snd = -a.snd :=
  rfl

theorem sub_fst (a b : ğ“œ(ğ•œ, A)) : (a - b).fst = a.fst - b.fst :=
  rfl

theorem sub_snd (a b : ğ“œ(ğ•œ, A)) : (a - b).snd = a.snd - b.snd :=
  rfl

theorem one_fst : (1 : ğ“œ(ğ•œ, A)).fst = 1 :=
  rfl

theorem one_snd : (1 : ğ“œ(ğ•œ, A)).snd = 1 :=
  rfl

@[simp]
theorem mul_fst (a b : ğ“œ(ğ•œ, A)) : (a * b).fst = a.fst * b.fst :=
  rfl

@[simp]
theorem mul_snd (a b : ğ“œ(ğ•œ, A)) : (a * b).snd = b.snd * a.snd :=
  rfl

theorem natCast_fst (n : â„•) : (n : ğ“œ(ğ•œ, A)).fst = n :=
  rfl

theorem natCast_snd (n : â„•) : (n : ğ“œ(ğ•œ, A)).snd = n :=
  rfl

theorem intCast_fst (n : â„¤) : (n : ğ“œ(ğ•œ, A)).fst = n :=
  rfl

theorem intCast_snd (n : â„¤) : (n : ğ“œ(ğ•œ, A)).snd = n :=
  rfl

theorem pow_fst (n : â„•) (a : ğ“œ(ğ•œ, A)) : (a ^ n).fst = a.fst ^ n :=
  rfl

theorem pow_snd (n : â„•) (a : ğ“œ(ğ•œ, A)) : (a ^ n).snd = a.snd ^ n :=
  rfl

/-- The natural injection from `DoubleCentralizer.toProd` except the second coordinate inherits
`MulOpposite.op`. The ring structure on `ğ“œ(ğ•œ, A)` is the pullback under this map. -/
def toProdMulOpposite : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ– := fun a =>
  (a.fst, MulOpposite.op a.snd)

theorem toProdMulOpposite_injective :
    Function.Injective (toProdMulOpposite : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–) :=
  fun _a _b h =>
    let h' := Prod.ext_iff.mp h
    ext (ğ•œ := ğ•œ) (A := A) _ _ <| Prod.ext h'.1 <| MulOpposite.op_injective h'.2

theorem range_toProdMulOpposite :
    Set.range toProdMulOpposite =
      { lr : (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ– | âˆ€ x y, unop lr.2 x * y = x * lr.1 y } :=
  Set.ext fun x =>
    âŸ¨by
      rintro âŸ¨a, rflâŸ©
      exact a.central, fun hx => âŸ¨âŸ¨(x.1, unop x.2), hxâŸ©, Prod.ext rfl rflâŸ©âŸ©

/-- The ring structure is inherited as the pullback under the injective map
`DoubleCentralizer.toProdMulOpposite : ğ“œ(ğ•œ, A) â†’ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–` -/
instance instRing : Ring ğ“œ(ğ•œ, A) :=
  toProdMulOpposite_injective.ring _ rfl rfl (fun _ _ => rfl) (fun _ _ => rfl) (fun _ => rfl)
    (fun _ _ => rfl) (fun _x _n => Prod.ext rfl <| MulOpposite.op_smul _ _)
    (fun _x _n => Prod.ext rfl <| MulOpposite.op_smul _ _)
    (fun _x _n => Prod.ext rfl <| MulOpposite.op_pow _ _) (fun _ => rfl) fun _ => rfl

/-- The canonical map `DoubleCentralizer.toProd` as an additive group homomorphism. -/
@[simps]
noncomputable def toProdHom : ğ“œ(ğ•œ, A) â†’+ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A) where
  toFun := toProd
  map_zero' := rfl
  map_add' _x _y := rfl

/-- The canonical map `DoubleCentralizer.toProdMulOpposite` as a ring homomorphism. -/
@[simps]
def toProdMulOppositeHom : ğ“œ(ğ•œ, A) â†’+* (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ– where
  toFun := toProdMulOpposite
  map_zero' := rfl
  map_one' := rfl
  map_add' _x _y := rfl
  map_mul' _x _y := rfl

/-- The module structure is inherited as the pullback under the additive group monomorphism
`DoubleCentralizer.toProd : ğ“œ(ğ•œ, A) â†’+ (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)` -/
noncomputable instance instModule {S : Type*} [Semiring S] [Module S A] [SMulCommClass ğ•œ S A]
    [ContinuousConstSMul S A] [IsScalarTower S A A] [SMulCommClass S A A] : Module S ğ“œ(ğ•œ, A) :=
  Function.Injective.module S toProdHom (ext (ğ•œ := ğ•œ) (A := A)) fun _x _y => rfl

-- TODO: generalize to `Algebra S ğ“œ(ğ•œ, A)` once `ContinuousLinearMap.algebra` is generalized.
instance instAlgebra : Algebra ğ•œ ğ“œ(ğ•œ, A) where
  algebraMap :=
  { toFun k :=
      { toProd := algebraMap ğ•œ ((A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)) k
        central := fun x y => by
          simp_rw [Prod.algebraMap_apply, Algebra.algebraMap_eq_smul_one, smul_apply, one_apply,
            mul_smul_comm, smul_mul_assoc] }
    map_one' := ext (ğ•œ := ğ•œ) (A := A) _ _ <| map_one <| algebraMap ğ•œ ((A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A))
    map_mul' _ _ :=
      ext (ğ•œ := ğ•œ) (A := A) _ _ <|
        Prod.ext (map_mul (algebraMap ğ•œ (A â†’L[ğ•œ] A)) _ _)
          ((map_mul (algebraMap ğ•œ (A â†’L[ğ•œ] A)) _ _).trans (Algebra.commutes _ _))
    map_zero' := ext (ğ•œ := ğ•œ) (A := A) _ _ <| map_zero <| algebraMap ğ•œ ((A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A))
    map_add' _ _ := ext (ğ•œ := ğ•œ) (A := A) _ _ <|
      map_add (algebraMap ğ•œ ((A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A))) _ _ }
  commutes' _ _ := ext (ğ•œ := ğ•œ) (A := A) _ _ <|
    Prod.ext (Algebra.commutes _ _) (Algebra.commutes _ _).symm
  smul_def' _ _ := ext (ğ•œ := ğ•œ) (A := A) _ _ <|
    Prod.ext (Algebra.smul_def _ _) ((Algebra.smul_def _ _).trans <| Algebra.commutes _ _)

@[simp]
theorem algebraMap_toProd (k : ğ•œ) : (algebraMap ğ•œ ğ“œ(ğ•œ, A) k).toProd = algebraMap ğ•œ _ k :=
  rfl

theorem algebraMap_fst (k : ğ•œ) : (algebraMap ğ•œ ğ“œ(ğ•œ, A) k).fst = algebraMap ğ•œ _ k :=
  rfl

theorem algebraMap_snd (k : ğ•œ) : (algebraMap ğ•œ ğ“œ(ğ•œ, A) k).snd = algebraMap ğ•œ _ k :=
  rfl

/-!
### Star structure
-/


section Star

variable [StarRing ğ•œ] [StarRing A] [StarModule ğ•œ A] [NormedStarGroup A]

/-- The star operation on `a : ğ“œ(ğ•œ, A)` is given by
`(star a).toProd = (star âˆ˜ a.snd âˆ˜ star, star âˆ˜ a.fst âˆ˜ star)`. -/
instance instStar : Star ğ“œ(ğ•œ, A) where
  star a :=
    { fst :=
        (((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A).comp a.snd).comp
          ((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A)
      snd :=
        (((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A).comp a.fst).comp
          ((starâ‚—áµ¢ ğ•œ : A â‰ƒâ‚—áµ¢â‹†[ğ•œ] A) : A â†’Lâ‹†[ğ•œ] A)
      central := fun x y => by
        simpa only [star_mul, star_star] using (congr_arg star (a.central (star y) (star x))).symm }

@[simp]
theorem star_fst (a : ğ“œ(ğ•œ, A)) (b : A) : (star a).fst b = star (a.snd (star b)) :=
  rfl

@[simp]
theorem star_snd (a : ğ“œ(ğ•œ, A)) (b : A) : (star a).snd b = star (a.fst (star b)) :=
  rfl

instance instStarAddMonoid : StarAddMonoid ğ“œ(ğ•œ, A) :=
  { DoubleCentralizer.instStar with
    star_involutive := fun x => by ext <;> simp only [star_fst, star_snd, star_star]
    star_add := fun x y => by
      ext <;>
        simp only [star_fst, star_snd, add_fst, add_snd, ContinuousLinearMap.add_apply, star_add] }

instance instStarRing : StarRing ğ“œ(ğ•œ, A) :=
  { DoubleCentralizer.instStarAddMonoid with
    star_mul := fun a b => by
      ext <;>
        simp only [star_fst, star_snd, mul_fst, mul_snd, star_star, ContinuousLinearMap.coe_mul,
          Function.comp_apply] }

instance instStarModule : StarModule ğ•œ ğ“œ(ğ•œ, A) :=
  { DoubleCentralizer.instStarAddMonoid (ğ•œ := ğ•œ) (A := A) with
    star_smul := fun k a => by ext <;> exact star_smul _ _ }

end Star

/-!
### Coercion from an algebra into its multiplier algebra
-/

variable (ğ•œ) in
/-- The natural coercion of `A` into `ğ“œ(ğ•œ, A)` given by sending `a : A` to the pair of linear
maps `Lâ‚ Râ‚ : A â†’L[ğ•œ] A` given by left- and right-multiplication by `a`, respectively.

Warning: if `A = ğ•œ`, then this is a coercion which is not definitionally equal to the
`algebraMap ğ•œ ğ“œ(ğ•œ, ğ•œ)` coercion, but these are propositionally equal. See
`DoubleCentralizer.coe_eq_algebraMap` below. -/
@[coe]
protected noncomputable def coe (a : A) : ğ“œ(ğ•œ, A) :=
  { fst := ContinuousLinearMap.mul ğ•œ A a
    snd := (ContinuousLinearMap.mul ğ•œ A).flip a
    central := fun _x _y => mul_assoc _ _ _ }

/-- The natural coercion of `A` into `ğ“œ(ğ•œ, A)` given by sending `a : A` to the pair of linear
maps `Lâ‚ Râ‚ : A â†’L[ğ•œ] A` given by left- and right-multiplication by `a`, respectively.

Warning: if `A = ğ•œ`, then this is a coercion which is not definitionally equal to the
`algebraMap ğ•œ ğ“œ(ğ•œ, ğ•œ)` coercion, but these are propositionally equal. See
`DoubleCentralizer.coe_eq_algebraMap` below. -/
noncomputable instance : CoeTC A ğ“œ(ğ•œ, A) where
  coe := DoubleCentralizer.coe ğ•œ

@[simp, norm_cast]
theorem coe_fst (a : A) : (a : ğ“œ(ğ•œ, A)).fst = ContinuousLinearMap.mul ğ•œ A a :=
  rfl

@[simp, norm_cast]
theorem coe_snd (a : A) : (a : ğ“œ(ğ•œ, A)).snd = (ContinuousLinearMap.mul ğ•œ A).flip a :=
  rfl

theorem coe_eq_algebraMap : (DoubleCentralizer.coe ğ•œ : ğ•œ â†’ ğ“œ(ğ•œ, ğ•œ)) = algebraMap ğ•œ ğ“œ(ğ•œ, ğ•œ) := by
  ext x : 3
  Â· rfl -- `fst` is defeq
  Â· refine ContinuousLinearMap.ext fun y => ?_
    exact mul_comm y x  -- `snd` multiplies on the wrong side

/-- The coercion of an algebra into its multiplier algebra as a non-unital star algebra
homomorphism. -/
@[simps]
noncomputable def coeHom [StarRing ğ•œ] [StarRing A] [StarModule ğ•œ A] [NormedStarGroup A] :
    A â†’â‹†â‚™â‚[ğ•œ] ğ“œ(ğ•œ, A) where
  toFun a := a
  map_smul' _ _ := ext _ _ _ _ <| Prod.ext (map_smul _ _ _) (map_smul _ _ _)
  map_zero' := ext _ _ _ _ <| Prod.ext (map_zero _) (map_zero _)
  map_add' _ _ := ext _ _ _ _ <| Prod.ext (map_add _ _ _) (map_add _ _ _)
  map_mul' _ _ := ext _ _ _ _ <| Prod.ext
    (ContinuousLinearMap.ext fun _ => (mul_assoc _ _ _))
    (ContinuousLinearMap.ext fun _ => (mul_assoc _ _ _).symm)
  map_star' _ := ext _ _ _ _ <| Prod.ext
    (ContinuousLinearMap.ext fun _ => (star_star_mul _ _).symm)
    (ContinuousLinearMap.ext fun _ => (star_mul_star _ _).symm)

/-!
### Norm structures
We define the norm structure on `ğ“œ(ğ•œ, A)` as the pullback under
`DoubleCentralizer.toProdMulOppositeHom : ğ“œ(ğ•œ, A) â†’+* (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–`, which
provides a definitional isometric embedding. Consequently, completeness of `ğ“œ(ğ•œ, A)` is obtained
by proving that the range of this map is closed.

In addition, we prove that `ğ“œ(ğ•œ, A)` is a normed algebra, and, when `A` is a Câ‹†-algebra, we show
that `ğ“œ(ğ•œ, A)` is also a Câ‹†-algebra. Moreover, in this case, for `a : ğ“œ(ğ•œ, A)`,
`â€–aâ€– = â€–a.fstâ€– = â€–a.sndâ€–`. -/


/-- The normed group structure is inherited as the pullback under the ring monomorphism
`DoubleCentralizer.toProdMulOppositeHom : ğ“œ(ğ•œ, A) â†’+* (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–`. -/
noncomputable instance : NormedRing ğ“œ(ğ•œ, A) :=
  NormedRing.induced _ _ (toProdMulOppositeHom : ğ“œ(ğ•œ, A) â†’+* (A â†’L[ğ•œ] A) Ã— (A â†’L[ğ•œ] A)áµáµ’áµ–)
    (by simpa using toProdMulOpposite_injective)

-- even though the definition is actually in terms of `DoubleCentralizer.toProdMulOpposite`, we
-- choose to see through that here to avoid `MulOpposite.op` appearing.
theorem norm_def (a : ğ“œ(ğ•œ, A)) : â€–aâ€– = â€–toProdHom aâ€– :=
  rfl

theorem nnnorm_def (a : ğ“œ(ğ•œ, A)) : â€–aâ€–â‚Š = â€–toProdHom aâ€–â‚Š :=
  rfl

theorem norm_def' (a : ğ“œ(ğ•œ, A)) : â€–aâ€– = â€–toProdMulOppositeHom aâ€– :=
  rfl

theorem nnnorm_def' (a : ğ“œ(ğ•œ, A)) : â€–aâ€–â‚Š = â€–toProdMulOppositeHom aâ€–â‚Š :=
  rfl

noncomputable instance instNormedSpace : NormedSpace ğ•œ ğ“œ(ğ•œ, A) :=
  { DoubleCentralizer.instModule with
    norm_smul_le := fun k a => (norm_smul_le k a.toProdMulOpposite :) }

noncomputable instance instNormedAlgebra : NormedAlgebra ğ•œ ğ“œ(ğ•œ, A) :=
  { DoubleCentralizer.instAlgebra, DoubleCentralizer.instNormedSpace with }

theorem isUniformEmbedding_toProdMulOpposite :
    IsUniformEmbedding (toProdMulOpposite (ğ•œ := ğ•œ) (A := A)) :=
  isUniformEmbedding_comap toProdMulOpposite_injective

instance [CompleteSpace A] : CompleteSpace ğ“œ(ğ•œ, A) := by
  rw [completeSpace_iff_isComplete_range isUniformEmbedding_toProdMulOpposite.isUniformInducing]
  apply IsClosed.isComplete
  simp only [range_toProdMulOpposite, Set.setOf_forall]
  refine isClosed_iInter fun x => isClosed_iInter fun y => isClosed_eq ?_ ?_
  Â· exact
      ((ContinuousLinearMap.apply ğ•œ A _).continuous.comp <| continuous_unop.comp continuous_snd).mul
        continuous_const
  exact continuous_const.mul ((ContinuousLinearMap.apply ğ•œ A _).continuous.comp continuous_fst)

variable [StarRing A] [CStarRing A]

/-- For `a : ğ“œ(ğ•œ, A)`, the norms of `a.fst` and `a.snd` coincide, and hence these
also coincide with `â€–aâ€–` which is `max (â€–a.fstâ€–) (â€–a.sndâ€–)`. -/
theorem norm_fst_eq_snd (a : ğ“œ(ğ•œ, A)) : â€–a.fstâ€– = â€–a.sndâ€– := by
  -- a handy lemma for this proof
  have h0 : âˆ€ f : A â†’L[ğ•œ] A, âˆ€ C : â„â‰¥0, (âˆ€ b : A, â€–f bâ€–â‚Š ^ 2 â‰¤ C * â€–f bâ€–â‚Š * â€–bâ€–â‚Š) â†’ â€–fâ€–â‚Š â‰¤ C := by
    intro f C h
    have h1 : âˆ€ b, C * â€–f bâ€–â‚Š * â€–bâ€–â‚Š â‰¤ C * â€–fâ€–â‚Š * â€–bâ€–â‚Š ^ 2 := by
      intro b
      convert mul_le_mul_right' (mul_le_mul_left' (f.le_opNNNorm b) C) â€–bâ€–â‚Š using 1
      ring
    have := NNReal.div_le_of_le_mul <| f.opNNNorm_le_bound _ <| by
      simpa only [sqrt_sq, sqrt_mul] using fun b â†¦ sqrt_le_sqrt.2 <| (h b).trans (h1 b)
    convert NNReal.rpow_le_rpow this two_pos.le
    Â· simp only [NNReal.rpow_two, div_pow, sq_sqrt]
      simp only [sq, mul_self_div_self]
    Â· simp only [NNReal.rpow_two, sq_sqrt]
  have h1 : âˆ€ b, â€–a.fst bâ€–â‚Š ^ 2 â‰¤ â€–a.sndâ€–â‚Š * â€–a.fst bâ€–â‚Š * â€–bâ€–â‚Š := by
    intro b
    calc
      â€–a.fst bâ€–â‚Š ^ 2 = â€–star (a.fst b) * a.fst bâ€–â‚Š := by
        simpa only [â† sq] using CStarRing.nnnorm_star_mul_self.symm
      _ â‰¤ â€–a.snd (star (a.fst b))â€–â‚Š * â€–bâ€–â‚Š := (a.central (star (a.fst b)) b â–¸ nnnorm_mul_le _ _)
      _ â‰¤ â€–a.sndâ€–â‚Š * â€–a.fst bâ€–â‚Š * â€–bâ€–â‚Š :=
        nnnorm_star (a.fst b) â–¸ mul_le_mul_right' (a.snd.le_opNNNorm _) _
  have h2 : âˆ€ b, â€–a.snd bâ€–â‚Š ^ 2 â‰¤ â€–a.fstâ€–â‚Š * â€–a.snd bâ€–â‚Š * â€–bâ€–â‚Š := by
    intro b
    calc
      â€–a.snd bâ€–â‚Š ^ 2 = â€–a.snd b * star (a.snd b)â€–â‚Š := by
        simpa only [â† sq] using CStarRing.nnnorm_self_mul_star.symm
      _ â‰¤ â€–bâ€–â‚Š * â€–a.fst (star (a.snd b))â€–â‚Š :=
        ((a.central b (star (a.snd b))).symm â–¸ nnnorm_mul_le _ _)
      _ = â€–a.fst (star (a.snd b))â€–â‚Š * â€–bâ€–â‚Š := mul_comm _ _
      _ â‰¤ â€–a.fstâ€–â‚Š * â€–a.snd bâ€–â‚Š * â€–bâ€–â‚Š :=
        nnnorm_star (a.snd b) â–¸ mul_le_mul_right' (a.fst.le_opNNNorm _) _
  exact le_antisymm (h0 _ _ h1) (h0 _ _ h2)

theorem nnnorm_fst_eq_snd (a : ğ“œ(ğ•œ, A)) : â€–a.fstâ€–â‚Š = â€–a.sndâ€–â‚Š :=
  Subtype.ext <| norm_fst_eq_snd a

@[simp]
theorem norm_fst (a : ğ“œ(ğ•œ, A)) : â€–a.fstâ€– = â€–aâ€– := by
  simp only [norm_def, toProdHom_apply, Prod.norm_def, norm_fst_eq_snd, max_eq_right le_rfl]

@[simp]
theorem norm_snd (a : ğ“œ(ğ•œ, A)) : â€–a.sndâ€– = â€–aâ€– := by rw [â† norm_fst, norm_fst_eq_snd]

@[simp]
theorem nnnorm_fst (a : ğ“œ(ğ•œ, A)) : â€–a.fstâ€–â‚Š = â€–aâ€–â‚Š :=
  Subtype.ext (norm_fst a)

@[simp]
theorem nnnorm_snd (a : ğ“œ(ğ•œ, A)) : â€–a.sndâ€–â‚Š = â€–aâ€–â‚Š :=
  Subtype.ext (norm_snd a)

end NontriviallyNormed

section DenselyNormed

variable {ğ•œ A : Type*} [DenselyNormedField ğ•œ] [StarRing ğ•œ]
variable [NonUnitalNormedRing A] [StarRing A] [CStarRing A]
variable [NormedSpace ğ•œ A] [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A] [StarModule ğ•œ A]

instance instCStarRing : CStarRing ğ“œ(ğ•œ, A) where
  norm_mul_self_le := fun (a : ğ“œ(ğ•œ, A)) => le_of_eq <| Eq.symm <| congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <|
    show â€–star a * aâ€–â‚Š = â€–aâ€–â‚Š * â€–aâ€–â‚Š by
    /- The essence of the argument is this: let `a = (L,R)` and recall `â€–aâ€– = â€–Lâ€–`.
    `star a = (star âˆ˜ R âˆ˜ star, star âˆ˜ L âˆ˜ star)`. Then for any `x y : A`, we have
    `â€–star a * aâ€– = â€–(star a * a).sndâ€– = â€–R (star (L (star x))) * yâ€– = â€–star (L (star x)) * L yâ€–`
    Now, on the one hand,
    `â€–star (L (star x)) * L yâ€– â‰¤ â€–star (L (star x))â€– * â€–L yâ€– = â€–L (star x)â€– * â€–L yâ€– â‰¤ â€–Lâ€– ^ 2`
    whenever `â€–xâ€–, â€–yâ€– â‰¤ 1`, so the supremum over all such `x, y` is at most `â€–Lâ€– ^ 2`.
    On the other hand, for any `â€–zâ€– â‰¤ 1`, we may choose `x := star z` and `y := z` to get:
    `â€–star (L (star x)) * L yâ€– = â€–star (L z) * (L z)â€– = â€–L zâ€– ^ 2`, and taking the supremum over
    all such `z` yields that the supremum is at least `â€–Lâ€– ^ 2`. It is the latter part of the
    argument where `DenselyNormedField ğ•œ` is required (for `sSup_unitClosedBall_eq_nnnorm`). -/
      have hball : (Metric.closedBall (0 : A) 1).Nonempty :=
        Metric.nonempty_closedBall.2 zero_le_one
      have key :
        âˆ€ x y, â€–xâ€–â‚Š â‰¤ 1 â†’ â€–yâ€–â‚Š â‰¤ 1 â†’ â€–a.snd (star (a.fst (star x))) * yâ€–â‚Š â‰¤ â€–aâ€–â‚Š * â€–aâ€–â‚Š := by
        intro x y hx hy
        rw [a.central]
        calc
          â€–star (a.fst (star x)) * a.fst yâ€–â‚Š â‰¤ â€–a.fst (star x)â€–â‚Š * â€–a.fst yâ€–â‚Š :=
            nnnorm_star (a.fst (star x)) â–¸ nnnorm_mul_le _ _
          _ â‰¤ â€–a.fstâ€–â‚Š * 1 * (â€–a.fstâ€–â‚Š * 1) :=
            (mul_le_mul' (a.fst.le_opNorm_of_le ((nnnorm_star x).trans_le hx))
              (a.fst.le_opNorm_of_le hy))
          _ â‰¤ â€–aâ€–â‚Š * â€–aâ€–â‚Š := by simp only [mul_one, nnnorm_fst, le_rfl]
      rw [â† nnnorm_snd]
      simp only [mul_snd, â† sSup_unitClosedBall_eq_nnnorm, star_snd, mul_apply]
      simp only [â† @opNNNorm_mul_apply ğ•œ _ A]
      simp only [â† sSup_unitClosedBall_eq_nnnorm, mul_apply']
      refine csSup_eq_of_forall_le_of_forall_lt_exists_gt (hball.image _) ?_ fun r hr => ?_
      Â· rintro - âŸ¨x, hx, rflâŸ©
        refine csSup_le (hball.image _) ?_
        rintro - âŸ¨y, hy, rflâŸ©
        exact key x y (mem_closedBall_zero_iff.1 hx) (mem_closedBall_zero_iff.1 hy)
      Â· simp only [Set.mem_image, exists_exists_and_eq_and]
        have hr' : NNReal.sqrt r < â€–aâ€–â‚Š := â€–aâ€–â‚Š.sqrt_mul_self â–¸ NNReal.sqrt_lt_sqrt.2 hr
        simp_rw [â† nnnorm_fst, â† sSup_unitClosedBall_eq_nnnorm] at hr'
        obtain âŸ¨_, âŸ¨x, hx, rflâŸ©, hxrâŸ© := exists_lt_of_lt_csSup (hball.image _) hr'
        have hx' : â€–xâ€–â‚Š â‰¤ 1 := mem_closedBall_zero_iff.1 hx
        refine âŸ¨star x, mem_closedBall_zero_iff.2 ((nnnorm_star x).trans_le hx'), ?_âŸ©
        refine lt_csSup_of_lt ?_ âŸ¨x, hx, rflâŸ© ?_
        Â· refine âŸ¨â€–aâ€–â‚Š * â€–aâ€–â‚Š, ?_âŸ©
          rintro - âŸ¨y, hy, rflâŸ©
          exact key (star x) y ((nnnorm_star x).trans_le hx') (mem_closedBall_zero_iff.1 hy)
        Â· simpa only [a.central, star_star, CStarRing.nnnorm_star_mul_self, NNReal.sq_sqrt, â† sq]
            using pow_lt_pow_leftâ‚€ hxr zero_le' two_ne_zero

end DenselyNormed

noncomputable instance {A : Type*} [NonUnitalCStarAlgebra A] : CStarAlgebra ğ“œ(â„‚, A) where

end DoubleCentralizer
