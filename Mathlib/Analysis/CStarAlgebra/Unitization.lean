/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.Classes
import Mathlib.Analysis.Normed.Algebra.Unitization
/-! # The minimal unitization of a C‚ãÜ-algebra

This file shows that when `E` is a C‚ãÜ-algebra (over a densely normed field `ùïú`), that the minimal
`Unitization` is as well. In order to ensure that the norm structure is available, we must first
show that every C‚ãÜ-algebra is a `RegularNormedAlgebra`.

In addition, we show that in a `RegularNormedAlgebra` which is a `StarRing` for which the
involution is isometric, that multiplication on the right is also an isometry (i.e.,
`Isometry (ContinuousLinearMap.mul ùïú E).flip`).
-/

open ContinuousLinearMap

local postfix:max "‚ãÜ" => star

variable (ùïú : Type*) {E : Type*}

namespace ContinuousLinearMap

variable [NontriviallyNormedField ùïú] [NonUnitalNormedRing E] [StarRing E] [NormedStarGroup E]
variable [NormedSpace ùïú E] [IsScalarTower ùïú E E] [SMulCommClass ùïú E E] [RegularNormedAlgebra ùïú E]

lemma opNorm_mul_flip_apply (a : E) : ‚Äñ(mul ùïú E).flip a‚Äñ = ‚Äña‚Äñ := by
  refine le_antisymm
    (opNorm_le_bound _ (norm_nonneg _) fun b => by simpa only [mul_comm] using norm_mul_le b a) ?_
  suffices ‚Äñmul ùïú E (star a)‚Äñ ‚â§ ‚Äñ(mul ùïú E).flip a‚Äñ by
    simpa only [ge_iff_le, opNorm_mul_apply, norm_star] using this
  refine opNorm_le_bound _ (norm_nonneg _) fun b => ?_
  calc ‚Äñmul ùïú E (star a) b‚Äñ = ‚Äñ(mul ùïú E).flip a (star b)‚Äñ := by
        simpa only [mul_apply', flip_apply, star_mul, star_star] using norm_star (star b * a)
    _ ‚â§ ‚Äñ(mul ùïú E).flip a‚Äñ * ‚Äñb‚Äñ := by
        simpa only [flip_apply, mul_apply', norm_star] using le_opNorm ((mul ùïú E).flip a) (star b)

lemma opNNNorm_mul_flip_apply (a : E) : ‚Äñ(mul ùïú E).flip a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  Subtype.ext (opNorm_mul_flip_apply ùïú a)

variable (E)

lemma isometry_mul_flip : Isometry (mul ùïú E).flip :=
  AddMonoidHomClass.isometry_of_norm _ (opNorm_mul_flip_apply ùïú)

end ContinuousLinearMap

variable [DenselyNormedField ùïú] [NonUnitalNormedRing E] [StarRing E] [CStarRing E]
variable [NormedSpace ùïú E] [IsScalarTower ùïú E E] [SMulCommClass ùïú E E]
variable (E)

/-- A C‚ãÜ-algebra over a densely normed field is a regular normed algebra. -/
instance CStarRing.instRegularNormedAlgebra : RegularNormedAlgebra ùïú E where
  isometry_mul' := AddMonoidHomClass.isometry_of_norm (mul ùïú E) fun a => NNReal.eq_iff.mp <|
    show ‚Äñmul ùïú E a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä by
    rw [‚Üê sSup_unitClosedBall_eq_nnnorm]
    refine csSup_eq_of_forall_le_of_forall_lt_exists_gt ?_ ?_ fun r hr => ?_
    ¬∑ exact (Metric.nonempty_closedBall.mpr zero_le_one).image _
    ¬∑ rintro - ‚ü®x, hx, rfl‚ü©
      exact
        ((mul ùïú E a).unit_le_opNorm x <| mem_closedBall_zero_iff.mp hx).trans
          (opNorm_mul_apply_le ùïú E a)
    ¬∑ have ha : 0 < ‚Äña‚Äñ‚Çä := zero_le'.trans_lt hr
      rw [‚Üê inv_inv ‚Äña‚Äñ‚Çä, NNReal.lt_inv_iff_mul_lt (inv_ne_zero ha.ne')] at hr
      obtain ‚ü®k, hk‚ÇÅ, hk‚ÇÇ‚ü© :=
        NormedField.exists_lt_nnnorm_lt ùïú (mul_lt_mul_of_pos_right hr <| inv_pos.2 ha)
      refine ‚ü®_, ‚ü®k ‚Ä¢ star a, ?_, rfl‚ü©, ?_‚ü©
      ¬∑ simpa only [mem_closedBall_zero_iff, norm_smul, one_mul, norm_star] using
          (NNReal.le_inv_iff_mul_le ha.ne').1 (one_mul ‚Äña‚Äñ‚Çä‚Åª¬π ‚ñ∏ hk‚ÇÇ.le : ‚Äñk‚Äñ‚Çä ‚â§ ‚Äña‚Äñ‚Çä‚Åª¬π)
      ¬∑ simp only [map_smul, nnnorm_smul, mul_apply', CStarRing.nnnorm_self_mul_star]
        rwa [‚Üê div_lt_iff‚ÇÄ (mul_pos ha ha), div_eq_mul_inv, mul_inv, ‚Üê mul_assoc]

section CStarProperty

variable [StarRing ùïú] [StarModule ùïú E]
variable {E}

/-- This is the key lemma used to establish the instance `Unitization.instCStarRing`
(i.e., proving that the norm on `Unitization ùïú E` satisfies the C‚ãÜ-property). We split this one
out so that declaring the `CStarRing` instance doesn't time out. -/
theorem Unitization.norm_splitMul_snd_sq (x : Unitization ùïú E) :
    ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ ^ 2 ‚â§ ‚Äñ(Unitization.splitMul ùïú E (star x * x)).snd‚Äñ := by
  /- The key idea is that we can use `sSup_unitClosedBall_eq_norm` to make this about
  applying this linear map to elements of norm at most one. There is a bit of `sqrt` and `sq`
  shuffling that needs to occur, which is primarily just an annoyance. -/
  refine (Real.le_sqrt (norm_nonneg _) (norm_nonneg _)).mp ?_
  simp only [Unitization.splitMul_apply]
  rw [‚Üê sSup_unitClosedBall_eq_norm]
  refine csSup_le ((Metric.nonempty_closedBall.2 zero_le_one).image _) ?_
  rintro - ‚ü®b, hb, rfl‚ü©
  simp only
  -- rewrite to a more convenient form; this is where we use the C‚ãÜ-property
  rw [‚Üê Real.sqrt_sq (norm_nonneg _), Real.sqrt_le_sqrt_iff (norm_nonneg _), sq,
    ‚Üê CStarRing.norm_star_mul_self, ContinuousLinearMap.add_apply, star_add, mul_apply',
    Algebra.algebraMap_eq_smul_one, ContinuousLinearMap.smul_apply,
    ContinuousLinearMap.one_apply, star_mul, star_smul, add_mul, smul_mul_assoc, ‚Üê mul_smul_comm,
    mul_assoc, ‚Üê mul_add, ‚Üê sSup_unitClosedBall_eq_norm]
  refine (norm_mul_le _ _).trans ?_
  calc
    _ ‚â§ ‚Äñstar x.fst ‚Ä¢ (x.fst ‚Ä¢ b + x.snd * b) + star x.snd * (x.fst ‚Ä¢ b + x.snd * b)‚Äñ := by
      nth_rewrite 2 [‚Üê one_mul ‚Äñ_ + _‚Äñ]
      gcongr
      exact (norm_star b).symm ‚ñ∏ mem_closedBall_zero_iff.1 hb
    _ ‚â§ sSup (_ '' Metric.closedBall 0 1) := le_csSup ?_ ‚ü®b, hb, ?_‚ü©
  -- now we just check the side conditions for `le_csSup`. There is nothing of interest here.
  ¬∑ refine ‚ü®‚Äñ(star x * x).fst‚Äñ + ‚Äñ(star x * x).snd‚Äñ, ?_‚ü©
    rintro _ ‚ü®y, hy, rfl‚ü©
    refine (norm_add_le _ _).trans ?_
    gcongr
    ¬∑ rw [Algebra.algebraMap_eq_smul_one]
      refine (norm_smul _ _).trans_le ?_
      simpa only [mul_one] using
        mul_le_mul_of_nonneg_left (mem_closedBall_zero_iff.1 hy) (norm_nonneg (star x * x).fst)
    ¬∑ exact (unit_le_opNorm _ y <| mem_closedBall_zero_iff.1 hy).trans (opNorm_mul_apply_le _ _ _)
  ¬∑ simp only [ContinuousLinearMap.add_apply, mul_apply', Unitization.snd_star, Unitization.snd_mul,
      Unitization.fst_mul, Unitization.fst_star, Algebra.algebraMap_eq_smul_one, smul_apply,
      one_apply, smul_add, mul_add, add_mul]
    simp only [smul_smul, smul_mul_assoc, ‚Üê add_assoc, ‚Üê mul_assoc, mul_smul_comm]

variable {ùïú}
variable [CStarRing ùïú]

/-- The norm on `Unitization ùïú E` satisfies the C‚ãÜ-property -/
instance Unitization.instCStarRing : CStarRing (Unitization ùïú E) where
  norm_mul_self_le x := by
    -- rewrite both sides as a `‚äî`
    simp only [Unitization.norm_def, Prod.norm_def]
    -- Show that `(Unitization.splitMul ùïú E x).snd` satisfies the C‚ãÜ-property, in two stages:
    have h‚ÇÅ : ‚àÄ x : Unitization ùïú E,
        ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ ‚â§ ‚Äñ(Unitization.splitMul ùïú E (star x)).snd‚Äñ := by
      simp only [Unitization.splitMul_apply, Unitization.snd_star, Unitization.fst_star]
      intro x
      /- split based on whether the term inside the norm is zero or not. If so, it's trivial.
      If not, then apply `norm_splitMul_snd_sq` and cancel one copy of the norm -/
      by_cases h : algebraMap ùïú (E ‚ÜíL[ùïú] E) x.fst + mul ùïú E x.snd = 0
      ¬∑ simp only [h, norm_zero]
        exact norm_nonneg _
      ¬∑ have : ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ ^ 2 ‚â§
          ‚Äñ(Unitization.splitMul ùïú E (star x)).snd‚Äñ * ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ :=
          (norm_splitMul_snd_sq ùïú x).trans <| by
            rw [map_mul, Prod.snd_mul]
            exact norm_mul_le _ _
        rw [sq] at this
        rw [‚Üê Ne, ‚Üê norm_pos_iff] at h
        simp only [Unitization.splitMul_apply, Unitization.snd_star,
          Unitization.fst_star] at this
        exact (mul_le_mul_right h).mp this
    -- in this step we make use of the key lemma `norm_splitMul_snd_sq`
    have h‚ÇÇ : ‚Äñ(Unitization.splitMul ùïú E (star x * x)).snd‚Äñ
        = ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ ^ 2 := by
      refine le_antisymm ?_ (norm_splitMul_snd_sq ùïú x)
      rw [map_mul, Prod.snd_mul]
      exact (norm_mul_le _ _).trans <| by
        rw [sq]
        gcongr
        simpa only [star_star] using h‚ÇÅ (star x)
    -- Show that `(Unitization.splitMul ùïú E x).fst` satisfies the C‚ãÜ-property
    have h‚ÇÉ : ‚Äñ(Unitization.splitMul ùïú E (star x * x)).fst‚Äñ
        = ‚Äñ(Unitization.splitMul ùïú E x).fst‚Äñ ^ 2 := by
      simp only [Unitization.splitMul_apply, Unitization.fst_mul, Unitization.fst_star,
        norm_mul, norm_star, sq]
    rw [h‚ÇÇ, h‚ÇÉ]
    /- use the definition of the norm, and split into cases based on whether the norm in the first
    coordinate is bigger or smaller than the norm in the second coordinate. -/
    by_cases h : ‚Äñ(Unitization.splitMul ùïú E x).fst‚Äñ ‚â§ ‚Äñ(Unitization.splitMul ùïú E x).snd‚Äñ
    ¬∑ rw [sq, sq, sup_eq_right.mpr h, sup_eq_right.mpr (mul_self_le_mul_self (norm_nonneg _) h)]
    ¬∑ replace h := (not_le.mp h).le
      rw [sq, sq, sup_eq_left.mpr h, sup_eq_left.mpr (mul_self_le_mul_self (norm_nonneg _) h)]

/-- The minimal unitization (over `‚ÑÇ`) of a C‚ãÜ-algebra, equipped with the C‚ãÜ-norm. When `A` is
unital, `A‚Å∫¬π ‚âÉ‚ãÜ‚Çê[‚ÑÇ] (‚ÑÇ √ó A)`. -/
scoped[CStarAlgebra] postfix:max "‚Å∫¬π" => Unitization ‚ÑÇ

noncomputable instance Unitization.instCStarAlgebra {A : Type*} [NonUnitalCStarAlgebra A] :
    CStarAlgebra (Unitization ‚ÑÇ A) where

noncomputable instance Unitization.instCommCStarAlgebra {A : Type*} [NonUnitalCommCStarAlgebra A] :
    CommCStarAlgebra (Unitization ‚ÑÇ A) where

end CStarProperty
