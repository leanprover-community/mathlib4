/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order

/-! # Properties of Câ‹†-algebra homomorphisms

Here we collect properties of Câ‹†-algebra homomorphisms.

## Main declarations

+ `NonUnitalStarAlgHom.norm_map`: A non-unital star algebra monomorphism of complex Câ‹†-algebras
  is isometric.
-/

open CStarAlgebra in
lemma IsSelfAdjoint.map_spectrum_real {F A B : Type*} [CStarAlgebra A] [CStarAlgebra B]
    [FunLike F A B] [AlgHomClass F â„‚ A B] [StarHomClass F A B]
    {a : A} (ha : IsSelfAdjoint a) (Ï† : F) (hÏ† : Function.Injective Ï†) :
    spectrum â„ (Ï† a) = spectrum â„ a := by
  have h_spec := AlgHom.spectrum_apply_subset ((Ï† : A â†’â‹†â‚[â„‚] B).restrictScalars â„) a
  refine Set.eq_of_subset_of_subset h_spec fun x hx â†¦ ?_
  /- we prove the reverse inclusion by contradiction, so assume that `x âˆˆ spectrum â„ a`, but
  `x âˆ‰ spectrum â„ (Ï† a)`. Then by Urysohn's lemma we can get a function for which `f x = 1`, but
  `f = 0` on `spectrum â„ a`. -/
  by_contra hx'
  obtain âŸ¨f, h_eqOn, h_eqOn_x, -âŸ© := exists_continuous_zero_one_of_isClosed
    (spectrum.isClosed (ğ•œ := â„) (Ï† a)) (isClosed_singleton (x := x)) <| by simpa
  /- it suffices to show that `Ï† (f a) = 0`, for if so, then `f a = 0` by injectivity of `Ï†`, and
  hence `f = 0` on `spectrum â„ a`, contradicting the fact that `f x = 1`. -/
  suffices Ï† (cfc f a) = 0 by
    rw [map_eq_zero_iff Ï† hÏ†, â† cfc_zero â„ a, cfc_eq_cfc_iff_eqOn] at this
    exact zero_ne_one <| calc
      0 = f x := (this hx).symm
      _ = 1 := h_eqOn_x <| Set.mem_singleton x
  /- Finally, `Ï† (f a) = f (Ï† a) = 0`, where the last equality follows since `f = 0` on
  `spectrum â„ (Ï† a)`. -/
  calc Ï† (cfc f a) = cfc f (Ï† a) := StarAlgHomClass.map_cfc Ï† f a
    _ = cfc (0 : â„ â†’ â„) (Ï† a) := cfc_congr h_eqOn
    _ = 0 := by simp

namespace NonUnitalStarAlgHom

variable {F A B : Type*} [NonUnitalCStarAlgebra A] [NonUnitalCStarAlgebra B]
variable [FunLike F A B] [NonUnitalAlgHomClass F â„‚ A B] [StarHomClass F A B]

open CStarAlgebra Unitization in
/-- A non-unital star algebra monomorphism of complex Câ‹†-algebras is isometric. -/
lemma norm_map (Ï† : F) (hÏ† : Function.Injective Ï†) (a : A) : â€–Ï† aâ€– = â€–aâ€– := by
  /- Since passing to the unitization is functorial, and it is an isometric embedding, we may assume
  that `Ï†` is a unital star algebra monomorphism and that `A` and `B` are unital Câ‹†-algebras. -/
  suffices âˆ€ {Ïˆ : Unitization â„‚ A â†’â‹†â‚[â„‚] Unitization â„‚ B} (_ : Function.Injective Ïˆ)
      (a : Unitization â„‚ A), â€–Ïˆ aâ€– = â€–aâ€– by
    simpa [norm_inr] using this (starMap_injective (Ï† := (Ï† : A â†’â‹†â‚™â‚[â„‚] B)) hÏ†) a
  intro Ïˆ hÏˆ a
  -- to show `â€–Ïˆ aâ€– = â€–aâ€–`, by the Câ‹†-property it suffices to show `â€–Ïˆ (star a * a)â€– = â€–star a * aâ€–`
  rw [â† sq_eq_sq (by positivity) (by positivity)]
  simp only [sq, â† CStarRing.norm_star_mul_self, â† map_star, â† map_mul]
  /- since `star a * a` is selfadjoint, it has the same `â„`-spectrum as `Ïˆ (star a * a)`.
  Since the spectral radius over `â„` coincides with the norm, `â€–Ïˆ (star a * a)â€– = â€–star a * aâ€–`. -/
  have ha : IsSelfAdjoint (star a * a) := .star_mul_self a
  calc â€–Ïˆ (star a * a)â€– = (spectralRadius â„ (Ïˆ (star a * a))).toReal :=
      ha.map Ïˆ |>.toReal_spectralRadius_eq_norm.symm
    _ = (spectralRadius â„ (star a * a)).toReal := by
      simp only [spectralRadius, ha.map_spectrum_real Ïˆ hÏˆ]
    _ = â€–star a * aâ€– := ha.toReal_spectralRadius_eq_norm

/-- A non-unital star algebra monomorphism of complex Câ‹†-algebras is isometric. -/
lemma nnnorm_map (Ï† : F) (hÏ† : Function.Injective Ï†) (a : A) : â€–Ï† aâ€–â‚Š = â€–aâ€–â‚Š :=
  Subtype.ext <| norm_map Ï† hÏ† a

lemma isometry (Ï† : F) (hÏ† : Function.Injective Ï†) : Isometry Ï† :=
  AddMonoidHomClass.isometry_of_norm Ï† (norm_map Ï† hÏ†)

end NonUnitalStarAlgHom
