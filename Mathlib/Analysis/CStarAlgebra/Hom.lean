/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order

/-! # Properties of Câ‹†-algebra homomorphisms

Here we collect properties of Câ‹†-algebra homomorphisms.

## Main declarations

+ `NonUnitalStarAlgHom.norm_map`: A non-unital star algebra monomorphism of complex Câ‹†-algebras
  is isometric.
-/

namespace NonUnitalStarAlgHom

variable {F A B : Type*}
variable [NonUnitalNormedRing A] [CompleteSpace A] [StarRing A] [CStarRing A]
variable [NormedSpace â„‚ A] [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A] [StarModule â„‚ A]
variable [NonUnitalNormedRing B] [CompleteSpace B] [StarRing B] [CStarRing B]
variable [NormedSpace â„‚ B] [IsScalarTower â„‚ B B] [SMulCommClass â„‚ B B] [StarModule â„‚ B]
variable [FunLike F A B] [NonUnitalAlgHomClass F â„‚ A B] [NonUnitalStarAlgHomClass F â„‚ A B]

open CStarAlgebra Unitization in
/-- A non-unital star algebra monomorphism of complex Câ‹†-algebras is isometric. -/
lemma norm_map (Ï† : F) (hÏ† : Function.Injective Ï†) (a : A) : â€–Ï† aâ€– = â€–aâ€– := by
  /- by passing to the unitization is functorial, and it is an isometric embedding, we may assume
  that `Ï†` is a unital star algebra monomorphism and that `A` and `B` are unital Câ‹†-algebras. -/
  suffices âˆ€ {Ïˆ : Unitization â„‚ A â†’â‹†â‚[â„‚] Unitization â„‚ B} (_ : Function.Injective Ïˆ)
      (a : Unitization â„‚ A), â€–Ïˆ aâ€– = â€–aâ€– by
    simpa [norm_inr] using this (starLift_injective (Ï† := (Ï† : A â†’â‹†â‚™â‚[â„‚] B)) hÏ†) a
  intro Ïˆ hÏˆ a
  -- to show `â€–Ïˆ aâ€– = â€–aâ€–`, by the Câ‹†-property it suffices to show `â€–Ïˆ (star a * aâ€– = â€–star a * aâ€–`.
  rw [â† sq_eq_sq (by positivity) (by positivity)]
  simp only [sq, â† CStarRing.norm_star_mul_self, â† map_star, â† map_mul]
  /- since `star a * a` is selfadjoint, it suffices to show that it has the same `â„`-spectrum as
  `Ïˆ (star a * a)`, since the spectral radius coincides with the norm. -/
  suffices âˆ€ a, IsSelfAdjoint a â†’ spectrum â„ (Ïˆ a) = spectrum â„ a from
    have ha : IsSelfAdjoint (star a * a) := .star_mul_self a
    calc â€–Ïˆ (star a * a)â€– = (spectralRadius â„ (Ïˆ (star a * a))).toReal :=
        ha.map Ïˆ |>.toReal_spectralRadius_eq_norm.symm
      _ = (spectralRadius â„ (star a * a)).toReal := by simp only [spectralRadius, this _ ha]
      _ = â€–star a * aâ€– := ha.toReal_spectralRadius_eq_norm
  /- so suppose that `a` is selfadjoint. The inclusion `specturm â„ (Ïˆ a) âŠ† spectrum â„ a` is
  immediate because `Ïˆ` is a homomorphism. -/
  intro a ha
  have h_spec := AlgHom.spectrum_apply_subset (Ïˆ.restrictScalars â„) a
  refine Set.eq_of_subset_of_subset h_spec fun x hx â†¦ ?_
  /- we prove the reverse inclusion by contradiction, so assume that `x âˆˆ spectrum â„ a`, but
  `x âˆ‰ spectrum â„ (Ïˆ a)`. Then by Urysohn's lemma we can get a function for which `f x = 1`, but
  `f = 0` on `spectrum â„ a`. -/
  by_contra hx'
  obtain âŸ¨f, h_eqOn, h_eqOn_x, -âŸ© := exists_continuous_zero_one_of_isClosed
    (spectrum.isClosed (ğ•œ := â„) (Ïˆ a)) (isClosed_singleton (x := x)) <| by simpa
  /- it suffices to show that `Ïˆ (f a) = 0`, for if so, then `f a = 0` by injectivity of `Ïˆ`, and
  hence `f = 0` on `spectrum â„ a`, contradicting the fact that `f x = 1`. -/
  suffices Ïˆ (cfc f a) = 0 by
    rw [map_eq_zero_iff Ïˆ hÏˆ, â† cfc_zero â„ a, cfc_eq_cfc_iff_eqOn] at this
    exact zero_ne_one <| calc
      0 = f x := (this hx).symm
      _ = 1 := h_eqOn_x <| Set.mem_singleton x
  /- Finally, `Ïˆ (f a) = f (Ïˆ a) = 0`, where the last equality follows since `f = 0` on
  `spectrum â„ (Ïˆ a)`. -/
  calc Ïˆ (cfc f a) = cfc f (Ïˆ a) := Ïˆ.map_cfc f a
    _ = cfc (0 : â„ â†’ â„) (Ïˆ a) := cfc_congr h_eqOn
    _ = 0 := by simp

/-- A non-unital star algebra monomorphism of complex Câ‹†-algebras is isometric. -/
lemma nnnorm_map (Ï† : F) (hÏ† : Function.Injective Ï†) (a : A) : â€–Ï† aâ€–â‚Š = â€–aâ€–â‚Š :=
  Subtype.ext <| norm_map Ï† hÏ† a

lemma isometry (Ï† : F) (hÏ† : Function.Injective Ï†) : Isometry Ï† :=
  AddMonoidHomClass.isometry_of_norm Ï† (norm_map Ï† hÏ†)

end NonUnitalStarAlgHom
