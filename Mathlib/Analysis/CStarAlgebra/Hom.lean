/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order

/-! # Properties of C‚ãÜ-algebra homomorphisms

Here we collect properties of C‚ãÜ-algebra homomorphisms.

## Main declarations

+ `NonUnitalStarAlgHom.norm_map`: A non-unital star algebra monomorphism of complex C‚ãÜ-algebras
  is isometric.
-/

open CStarAlgebra in
lemma IsSelfAdjoint.map_spectrum_real {F A B : Type*}
    [NormedRing A] [CompleteSpace A] [StarRing A] [CStarRing A]
    [NormedAlgebra ‚ÑÇ A] [StarModule ‚ÑÇ A]
    [NormedRing B] [CompleteSpace B] [StarRing B] [CStarRing B]
    [NormedAlgebra ‚ÑÇ B] [StarModule ‚ÑÇ B]
    [FunLike F A B] [AlgHomClass F ‚ÑÇ A B] [StarHomClass F A B]
    {a : A} (ha : IsSelfAdjoint a) (œÜ : F) (hœÜ : Function.Injective œÜ) :
    spectrum ‚Ñù (œÜ a) = spectrum ‚Ñù a := by
  have h_spec := AlgHom.spectrum_apply_subset ((œÜ : A ‚Üí‚ãÜ‚Çê[‚ÑÇ] B).restrictScalars ‚Ñù) a
  refine Set.eq_of_subset_of_subset h_spec fun x hx ‚Ü¶ ?_
  /- we prove the reverse inclusion by contradiction, so assume that `x ‚àà spectrum ‚Ñù a`, but
  `x ‚àâ spectrum ‚Ñù (œÜ a)`. Then by Urysohn's lemma we can get a function for which `f x = 1`, but
  `f = 0` on `spectrum ‚Ñù a`. -/
  by_contra hx'
  obtain ‚ü®f, h_eqOn, h_eqOn_x, -‚ü© := exists_continuous_zero_one_of_isClosed
    (spectrum.isClosed (ùïú := ‚Ñù) (œÜ a)) (isClosed_singleton (x := x)) <| by simpa
  /- it suffices to show that `œÜ (f a) = 0`, for if so, then `f a = 0` by injectivity of `œÜ`, and
  hence `f = 0` on `spectrum ‚Ñù a`, contradicting the fact that `f x = 1`. -/
  suffices œÜ (cfc f a) = 0 by
    rw [map_eq_zero_iff œÜ hœÜ, ‚Üê cfc_zero ‚Ñù a, cfc_eq_cfc_iff_eqOn] at this
    exact zero_ne_one <| calc
      0 = f x := (this hx).symm
      _ = 1 := h_eqOn_x <| Set.mem_singleton x
  /- Finally, `œÜ (f a) = f (œÜ a) = 0`, where the last equality follows since `f = 0` on
  `spectrum ‚Ñù (œÜ a)`. -/
  calc œÜ (cfc f a) = cfc f (œÜ a) := StarAlgHomClass.map_cfc œÜ f a
    _ = cfc (0 : ‚Ñù ‚Üí ‚Ñù) (œÜ a) := cfc_congr h_eqOn
    _ = 0 := by simp

namespace NonUnitalStarAlgHom

variable {F A B : Type*}
variable [NonUnitalNormedRing A] [CompleteSpace A] [StarRing A] [CStarRing A]
variable [NormedSpace ‚ÑÇ A] [IsScalarTower ‚ÑÇ A A] [SMulCommClass ‚ÑÇ A A] [StarModule ‚ÑÇ A]
variable [NonUnitalNormedRing B] [CompleteSpace B] [StarRing B] [CStarRing B]
variable [NormedSpace ‚ÑÇ B] [IsScalarTower ‚ÑÇ B B] [SMulCommClass ‚ÑÇ B B] [StarModule ‚ÑÇ B]
variable [FunLike F A B] [NonUnitalAlgHomClass F ‚ÑÇ A B] [StarHomClass F A B]

open CStarAlgebra Unitization in
/-- A non-unital star algebra monomorphism of complex C‚ãÜ-algebras is isometric. -/
lemma norm_map (œÜ : F) (hœÜ : Function.Injective œÜ) (a : A) : ‚ÄñœÜ a‚Äñ = ‚Äña‚Äñ := by
  /- Since passing to the unitization is functorial, and it is an isometric embedding, we may assume
  that `œÜ` is a unital star algebra monomorphism and that `A` and `B` are unital C‚ãÜ-algebras. -/
  suffices ‚àÄ {œà : Unitization ‚ÑÇ A ‚Üí‚ãÜ‚Çê[‚ÑÇ] Unitization ‚ÑÇ B} (_ : Function.Injective œà)
      (a : Unitization ‚ÑÇ A), ‚Äñœà a‚Äñ = ‚Äña‚Äñ by
    simpa [norm_inr] using this (starMap_injective (œÜ := (œÜ : A ‚Üí‚ãÜ‚Çô‚Çê[‚ÑÇ] B)) hœÜ) a
  intro œà hœà a
  -- to show `‚Äñœà a‚Äñ = ‚Äña‚Äñ`, by the C‚ãÜ-property it suffices to show `‚Äñœà (star a * a)‚Äñ = ‚Äñstar a * a‚Äñ`
  rw [‚Üê sq_eq_sq (by positivity) (by positivity)]
  simp only [sq, ‚Üê CStarRing.norm_star_mul_self, ‚Üê map_star, ‚Üê map_mul]
  /- since `star a * a` is selfadjoint, it has the same `‚Ñù`-spectrum as `œà (star a * a)`.
  Since the spectral radius over `‚Ñù` coincides with the norm, `‚Äñœà (star a * a)‚Äñ = ‚Äñstar a * a‚Äñ`. -/
  have ha : IsSelfAdjoint (star a * a) := .star_mul_self a
  calc ‚Äñœà (star a * a)‚Äñ = (spectralRadius ‚Ñù (œà (star a * a))).toReal :=
      ha.map œà |>.toReal_spectralRadius_eq_norm.symm
    _ = (spectralRadius ‚Ñù (star a * a)).toReal := by
      simp only [spectralRadius, ha.map_spectrum_real œà hœà]
    _ = ‚Äñstar a * a‚Äñ := ha.toReal_spectralRadius_eq_norm

/-- A non-unital star algebra monomorphism of complex C‚ãÜ-algebras is isometric. -/
lemma nnnorm_map (œÜ : F) (hœÜ : Function.Injective œÜ) (a : A) : ‚ÄñœÜ a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  Subtype.ext <| norm_map œÜ hœÜ a

lemma isometry (œÜ : F) (hœÜ : Function.Injective œÜ) : Isometry œÜ :=
  AddMonoidHomClass.isometry_of_norm œÜ (norm_map œÜ hœÜ)

end NonUnitalStarAlgHom
