/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Instances

/-! # Isometric continuous functional calculus

This file adds a class for an *isometric* continuous functional calculus. This is separate from the
usual `ContinuousFunctionalCalculus` class because we prefer not to require a metric (or a norm) on
the algebra for reasons discussed in the module documentation for that file.

Of course, with a metric on the algebra and an isometric continuous functional calculus, the
algebra must *be* a Câ‹†-algebra already. As such, it may seem like this class is not useful. However,
the main purpose is to allow for the continuous functional calculus to be a isometric for the other
scalar rings `â„` and `â„â‰¥0` too.
-/

local notation "Ïƒ" => spectrum
local notation "Ïƒâ‚™" => quasispectrum

/-! ### Isometric continuous functional calculus for unital algebras -/
section Unital

/-- An extension of the `ContinuousFunctionalCalculus` requiring that `cfcHom` is an isometry. -/
class IsometricContinuousFunctionalCalculus (R A : Type*) (p : outParam (A â†’ Prop))
    [CommSemiring R] [StarRing R] [MetricSpace R] [IsTopologicalSemiring R] [ContinuousStar R]
    [Ring A] [StarRing A] [MetricSpace A] [Algebra R A] : Prop
    extends ContinuousFunctionalCalculus R A p where
  isometric (a : A) (ha : p a) : Isometry (cfcHom ha (R := R))

section MetricSpace

open scoped ContinuousFunctionalCalculus

lemma isometry_cfcHom {R A : Type*} {p : outParam (A â†’ Prop)} [CommSemiring R] [StarRing R]
    [MetricSpace R] [IsTopologicalSemiring R] [ContinuousStar R] [Ring A] [StarRing A]
    [MetricSpace A] [Algebra R A] [IsometricContinuousFunctionalCalculus R A p]
    (a : A) (ha : p a := by cfc_tac) :
    Isometry (cfcHom (show p a from ha) (R := R)) :=
  IsometricContinuousFunctionalCalculus.isometric a ha

end MetricSpace

section NormedRing

open scoped ContinuousFunctionalCalculus

variable {ğ•œ A : Type*} {p : outParam (A â†’ Prop)}
variable [RCLike ğ•œ] [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A]
variable [IsometricContinuousFunctionalCalculus ğ•œ A p]

lemma norm_cfcHom (a : A) (f : C(Ïƒ ğ•œ a, ğ•œ)) (ha : p a := by cfc_tac) :
    â€–cfcHom (show p a from ha) fâ€– = â€–fâ€– := by
  refine isometry_cfcHom a |>.norm_map_of_map_zero (map_zero _) f

lemma nnnorm_cfcHom (a : A) (f : C(Ïƒ ğ•œ a, ğ•œ)) (ha : p a := by cfc_tac) :
    â€–cfcHom (show p a from ha) fâ€–â‚Š = â€–fâ€–â‚Š :=
  Subtype.ext <| norm_cfcHom a f ha

lemma IsGreatest.norm_cfc [Nontrivial A] (f : ğ•œ â†’ ğ•œ) (a : A)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac) (ha : p a := by cfc_tac) :
    IsGreatest ((fun x â†¦ â€–f xâ€–) '' spectrum ğ•œ a) â€–cfc f aâ€– := by
  obtain âŸ¨x, hxâŸ© := ContinuousFunctionalCalculus.isCompact_spectrum a
    |>.image_of_continuousOn hf.norm |>.exists_isGreatest <|
    (ContinuousFunctionalCalculus.spectrum_nonempty a ha).image _
  obtain âŸ¨x, hx', rflâŸ© := hx.1
  convert hx
  rw [cfc_apply f a, norm_cfcHom a _]
  apply le_antisymm
  Â· apply ContinuousMap.norm_le _ (norm_nonneg _) |>.mpr
    rintro âŸ¨y, hyâŸ©
    exact hx.2 âŸ¨y, hy, rflâŸ©
  Â· exact le_trans (by simp) <| ContinuousMap.norm_coe_le_norm _ (âŸ¨x, hx'âŸ© : Ïƒ ğ•œ a)

lemma IsGreatest.nnnorm_cfc [Nontrivial A] (f : ğ•œ â†’ ğ•œ) (a : A)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac) (ha : p a := by cfc_tac) :
    IsGreatest ((fun x â†¦ â€–f xâ€–â‚Š) '' Ïƒ ğ•œ a) â€–cfc f aâ€–â‚Š := by
  convert Real.toNNReal_monotone.map_isGreatest (.norm_cfc f a)
  all_goals simp [Set.image_image, norm_toNNReal]

lemma norm_apply_le_norm_cfc (f : ğ•œ â†’ ğ•œ) (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒ ğ•œ a)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac) (ha : p a := by cfc_tac) :
    â€–f xâ€– â‰¤ â€–cfc f aâ€– := by
  revert hx
  nontriviality A
  exact (IsGreatest.norm_cfc f a hf ha |>.2 âŸ¨x, Â·, rflâŸ©)

lemma nnnorm_apply_le_nnnorm_cfc (f : ğ•œ â†’ ğ•œ) (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒ ğ•œ a)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac) (ha : p a := by cfc_tac) :
    â€–f xâ€–â‚Š â‰¤ â€–cfc f aâ€–â‚Š :=
  norm_apply_le_norm_cfc f a hx

lemma norm_cfc_le {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„} (hc : 0 â‰¤ c) (h : âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€– â‰¤ c) :
    â€–cfc f aâ€– â‰¤ c := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· simpa [Subsingleton.elim (cfc f a) 0]
  Â· refine cfc_cases (â€–Â·â€– â‰¤ c) a f (by simpa) fun hf ha â†¦ ?_
    simp only [â† cfc_apply f a, isLUB_le_iff (IsGreatest.norm_cfc f a hf ha |>.isLUB)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma norm_cfc_le_iff (f : ğ•œ â†’ ğ•œ) (a : A) {c : â„} (hc : 0 â‰¤ c)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac)
    (ha : p a := by cfc_tac) : â€–cfc f aâ€– â‰¤ c â†” âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€– â‰¤ c :=
  âŸ¨fun h _ hx â†¦ norm_apply_le_norm_cfc f a hx hf ha |>.trans h, norm_cfc_le hcâŸ©

lemma norm_cfc_lt {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„} (hc : 0 < c) (h : âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€– < c) :
    â€–cfc f aâ€– < c := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· simpa [Subsingleton.elim (cfc f a) 0]
  Â· refine cfc_cases (â€–Â·â€– < c) a f (by simpa) fun hf ha â†¦ ?_
    simp only [â† cfc_apply f a, (IsGreatest.norm_cfc f a hf ha |>.lt_iff)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma norm_cfc_lt_iff (f : ğ•œ â†’ ğ•œ) (a : A) {c : â„} (hc : 0 < c)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac)
    (ha : p a := by cfc_tac) : â€–cfc f aâ€– < c â†” âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€– < c :=
  âŸ¨fun h _ hx â†¦ norm_apply_le_norm_cfc f a hx hf ha |>.trans_lt h, norm_cfc_lt hcâŸ©

open NNReal

lemma nnnorm_cfc_le {f : ğ•œ â†’ ğ•œ} {a : A} (c : â„â‰¥0) (h : âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€–â‚Š â‰¤ c) :
    â€–cfc f aâ€–â‚Š â‰¤ c :=
  norm_cfc_le c.2 h

lemma nnnorm_cfc_le_iff (f : ğ•œ â†’ ğ•œ) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac)
    (ha : p a := by cfc_tac) : â€–cfc f aâ€–â‚Š â‰¤ c â†” âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€–â‚Š â‰¤ c :=
  norm_cfc_le_iff f a c.2

lemma nnnorm_cfc_lt {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„â‰¥0} (hc : 0 < c) (h : âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€–â‚Š < c) :
    â€–cfc f aâ€–â‚Š < c :=
  norm_cfc_lt hc h

lemma nnnorm_cfc_lt_iff (f : ğ•œ â†’ ğ•œ) (a : A) {c : â„â‰¥0} (hc : 0 < c)
    (hf : ContinuousOn f (Ïƒ ğ•œ a) := by cfc_cont_tac)
    (ha : p a := by cfc_tac) : â€–cfc f aâ€–â‚Š < c â†” âˆ€ x âˆˆ Ïƒ ğ•œ a, â€–f xâ€–â‚Š < c :=
  norm_cfc_lt_iff f a hc

namespace IsometricContinuousFunctionalCalculus

lemma isGreatest_norm_spectrum [Nontrivial A] (a : A) (ha : p a := by cfc_tac) :
    IsGreatest ((â€–Â·â€–) '' spectrum ğ•œ a) â€–aâ€– := by
  simpa only [cfc_id ğ•œ a] using IsGreatest.norm_cfc (id : ğ•œ â†’ ğ•œ) a

lemma norm_spectrum_le (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒ ğ•œ a) (ha : p a := by cfc_tac) :
    â€–xâ€– â‰¤ â€–aâ€– := by
  simpa only [cfc_id ğ•œ a] using norm_apply_le_norm_cfc (id : ğ•œ â†’ ğ•œ) a hx

lemma isGreatest_nnnorm_spectrum [Nontrivial A] (a : A) (ha : p a := by cfc_tac) :
    IsGreatest ((â€–Â·â€–â‚Š) '' spectrum ğ•œ a) â€–aâ€–â‚Š := by
  simpa only [cfc_id ğ•œ a] using IsGreatest.nnnorm_cfc (id : ğ•œ â†’ ğ•œ) a

lemma nnnorm_spectrum_le (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒ ğ•œ a) (ha : p a := by cfc_tac) :
    â€–xâ€–â‚Š â‰¤ â€–aâ€–â‚Š := by
  simpa only [cfc_id ğ•œ a] using nnnorm_apply_le_nnnorm_cfc (id : ğ•œ â†’ ğ•œ) a hx

end IsometricContinuousFunctionalCalculus

end NormedRing

namespace SpectrumRestricts

variable {R S A : Type*} {p q : A â†’ Prop}
variable [Semifield R] [StarRing R] [MetricSpace R] [IsTopologicalSemiring R] [ContinuousStar R]
variable [Semifield S] [StarRing S] [MetricSpace S] [IsTopologicalSemiring S] [ContinuousStar S]
variable [Ring A] [StarRing A] [Algebra S A]
variable [Algebra R S] [Algebra R A] [IsScalarTower R S A] [StarModule R S] [ContinuousSMul R S]
variable [MetricSpace A] [IsometricContinuousFunctionalCalculus S A q]
variable [CompleteSpace R] [ContinuousMap.UniqueHom R A]

open scoped ContinuousFunctionalCalculus in
protected theorem isometric_cfc (f : C(S, R)) (halg : Isometry (algebraMap R S)) (h0 : p 0)
    (h : âˆ€ a, p a â†” q a âˆ§ SpectrumRestricts a f) :
    IsometricContinuousFunctionalCalculus R A p where
  toContinuousFunctionalCalculus := SpectrumRestricts.cfc f halg.isUniformEmbedding h0 h
  isometric a ha := by
    obtain âŸ¨ha', hafâŸ© := h a |>.mp ha
    have := SpectrumRestricts.cfc f halg.isUniformEmbedding h0 h
    rw [cfcHom_eq_restrict f halg.isUniformEmbedding ha ha' haf]
    refine .of_dist_eq fun gâ‚ gâ‚‚ â†¦ ?_
    simp only [starAlgHom_apply, isometry_cfcHom a ha' |>.dist_eq]
    refine le_antisymm ?_ ?_
    all_goals refine ContinuousMap.dist_le dist_nonneg |>.mpr fun x â†¦ ?_
    Â· simpa [halg.dist_eq] using ContinuousMap.dist_apply_le_dist _
    Â· let x' : Ïƒ S a := Subtype.map (algebraMap R S) (fun _ â†¦ spectrum.algebraMap_mem S) x
      apply le_of_eq_of_le ?_ <| ContinuousMap.dist_apply_le_dist x'
      simp only [ContinuousMap.comp_apply, ContinuousMap.coe_mk, StarAlgHom.ofId_apply,
        halg.dist_eq, x']
      congr!
      all_goals ext; exact haf.left_inv _ |>.symm

end SpectrumRestricts

end Unital

/-! ### Isometric continuous functional calculus for non-unital algebras -/

section NonUnital

/-- An extension of the `NonUnitalContinuousFunctionalCalculus` requiring that `cfcâ‚™Hom` is an
isometry. -/
class NonUnitalIsometricContinuousFunctionalCalculus (R A : Type*) (p : outParam (A â†’ Prop))
    [CommSemiring R] [Nontrivial R] [StarRing R] [MetricSpace R] [IsTopologicalSemiring R]
    [ContinuousStar R] [NonUnitalRing A] [StarRing A] [MetricSpace A] [Module R A]
    [IsScalarTower R A A] [SMulCommClass R A A] : Prop
    extends NonUnitalContinuousFunctionalCalculus R A p where
  isometric (a : A) (ha : p a) : Isometry (cfcâ‚™Hom ha (R := R))

section MetricSpace

variable {R A : Type*} {p : outParam (A â†’ Prop)}
variable [CommSemiring R] [Nontrivial R] [StarRing R] [MetricSpace R] [IsTopologicalSemiring R]
variable [ContinuousStar R]
variable [NonUnitalRing A] [StarRing A] [MetricSpace A] [Module R A]
variable [IsScalarTower R A A] [SMulCommClass R A A]

open scoped NonUnitalContinuousFunctionalCalculus

variable [NonUnitalIsometricContinuousFunctionalCalculus R A p]

lemma isometry_cfcâ‚™Hom (a : A) (ha : p a := by cfc_tac) :
    Isometry (cfcâ‚™Hom (show p a from ha) (R := R)) :=
  NonUnitalIsometricContinuousFunctionalCalculus.isometric a ha

end MetricSpace

section NormedRing

variable {ğ•œ A : Type*} {p : outParam (A â†’ Prop)}
variable [RCLike ğ•œ] [NonUnitalNormedRing A] [StarRing A] [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A]
variable [SMulCommClass ğ•œ A A]
variable [NonUnitalIsometricContinuousFunctionalCalculus ğ•œ A p]

open NonUnitalIsometricContinuousFunctionalCalculus
open scoped ContinuousMapZero NonUnitalContinuousFunctionalCalculus

lemma norm_cfcâ‚™Hom (a : A) (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) (ha : p a := by cfc_tac) :
    â€–cfcâ‚™Hom (show p a from ha) fâ€– = â€–fâ€– := by
  refine isometry_cfcâ‚™Hom a |>.norm_map_of_map_zero (map_zero _) f

lemma nnnorm_cfcâ‚™Hom (a : A) (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) (ha : p a := by cfc_tac) :
    â€–cfcâ‚™Hom (show p a from ha) fâ€–â‚Š = â€–fâ€–â‚Š :=
  Subtype.ext <| norm_cfcâ‚™Hom a f ha

lemma IsGreatest.norm_cfcâ‚™ (f : ğ•œ â†’ ğ•œ) (a : A)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : IsGreatest ((fun x â†¦ â€–f xâ€–) '' Ïƒâ‚™ ğ•œ a) â€–cfcâ‚™ f aâ€– := by
  obtain âŸ¨x, hxâŸ© := NonUnitalContinuousFunctionalCalculus.isCompact_quasispectrum a
      |>.image_of_continuousOn hf.norm |>.exists_isGreatest <|
      (quasispectrum.nonempty ğ•œ a).image _
  obtain âŸ¨x, hx', rflâŸ© := hx.1
  convert hx
  rw [cfcâ‚™_apply f a, norm_cfcâ‚™Hom a _]
  apply le_antisymm
  Â· apply ContinuousMap.norm_le _ (norm_nonneg _) |>.mpr
    rintro âŸ¨y, hyâŸ©
    exact hx.2 âŸ¨y, hy, rflâŸ©
  Â· exact le_trans (by simp) <| ContinuousMap.norm_coe_le_norm _ (âŸ¨x, hx'âŸ© : Ïƒâ‚™ ğ•œ a)

lemma IsGreatest.nnnorm_cfcâ‚™ (f : ğ•œ â†’ ğ•œ) (a : A)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : IsGreatest ((fun x â†¦ â€–f xâ€–â‚Š) '' Ïƒâ‚™ ğ•œ a) â€–cfcâ‚™ f aâ€–â‚Š := by
  convert Real.toNNReal_monotone.map_isGreatest (.norm_cfcâ‚™ f a)
  all_goals simp [Set.image_image, norm_toNNReal]

lemma norm_apply_le_norm_cfcâ‚™ (f : ğ•œ â†’ ğ•œ) (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒâ‚™ ğ•œ a)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–f xâ€– â‰¤ â€–cfcâ‚™ f aâ€– :=
  IsGreatest.norm_cfcâ‚™ f a hf hfâ‚€ ha |>.2 âŸ¨x, hx, rflâŸ©

lemma nnnorm_apply_le_nnnorm_cfcâ‚™ (f : ğ•œ â†’ ğ•œ) (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒâ‚™ ğ•œ a)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–f xâ€–â‚Š â‰¤ â€–cfcâ‚™ f aâ€–â‚Š :=
  IsGreatest.nnnorm_cfcâ‚™ f a hf hfâ‚€ ha |>.2 âŸ¨x, hx, rflâŸ©

lemma norm_cfcâ‚™_le {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„} (h : âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€– â‰¤ c) :
    â€–cfcâ‚™ f aâ€– â‰¤ c := by
  refine cfcâ‚™_cases (â€–Â·â€– â‰¤ c) a f ?_ fun hf hf0 ha â†¦ ?_
  Â· simpa using (norm_nonneg _).trans <| h 0 (quasispectrum.zero_mem ğ•œ a)
  Â· simp only [â† cfcâ‚™_apply f a, isLUB_le_iff (IsGreatest.norm_cfcâ‚™ f a hf hf0 ha |>.isLUB)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma norm_cfcâ‚™_le_iff (f : ğ•œ â†’ ğ•œ) (a : A) (c : â„)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–cfcâ‚™ f aâ€– â‰¤ c â†” âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€– â‰¤ c :=
  âŸ¨fun h _ hx â†¦ norm_apply_le_norm_cfcâ‚™ f a hx hf hfâ‚€ ha |>.trans h, norm_cfcâ‚™_leâŸ©

lemma norm_cfcâ‚™_lt {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„} (h : âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€– < c) :
    â€–cfcâ‚™ f aâ€– < c := by
  refine cfcâ‚™_cases (â€–Â·â€– < c) a f ?_ fun hf hf0 ha â†¦ ?_
  Â· simpa using (norm_nonneg _).trans_lt <| h 0 (quasispectrum.zero_mem ğ•œ a)
  Â· simp only [â† cfcâ‚™_apply f a, (IsGreatest.norm_cfcâ‚™ f a hf hf0 ha |>.lt_iff)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma norm_cfcâ‚™_lt_iff (f : ğ•œ â†’ ğ•œ) (a : A) (c : â„)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–cfcâ‚™ f aâ€– < c â†” âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€– < c :=
  âŸ¨fun h _ hx â†¦ norm_apply_le_norm_cfcâ‚™ f a hx hf hfâ‚€ ha |>.trans_lt h, norm_cfcâ‚™_ltâŸ©

open NNReal

lemma nnnorm_cfcâ‚™_le {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„â‰¥0} (h : âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€–â‚Š â‰¤ c) :
    â€–cfcâ‚™ f aâ€–â‚Š â‰¤ c :=
  norm_cfcâ‚™_le h

lemma nnnorm_cfcâ‚™_le_iff (f : ğ•œ â†’ ğ•œ) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–cfcâ‚™ f aâ€–â‚Š â‰¤ c â†” âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€–â‚Š â‰¤ c :=
  norm_cfcâ‚™_le_iff f a c.1 hf hfâ‚€ ha

lemma nnnorm_cfcâ‚™_lt {f : ğ•œ â†’ ğ•œ} {a : A} {c : â„â‰¥0} (h : âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€–â‚Š < c) :
    â€–cfcâ‚™ f aâ€–â‚Š < c :=
  norm_cfcâ‚™_lt h

lemma nnnorm_cfcâ‚™_lt_iff (f : ğ•œ â†’ ğ•œ) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒâ‚™ ğ•œ a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : p a := by cfc_tac) : â€–cfcâ‚™ f aâ€–â‚Š < c â†” âˆ€ x âˆˆ Ïƒâ‚™ ğ•œ a, â€–f xâ€–â‚Š < c :=
  norm_cfcâ‚™_lt_iff f a c.1 hf hfâ‚€ ha

namespace NonUnitalIsometricContinuousFunctionalCalculus

lemma isGreatest_norm_quasispectrum (a : A) (ha : p a := by cfc_tac) :
    IsGreatest ((â€–Â·â€–) '' Ïƒâ‚™ ğ•œ a) â€–aâ€– := by
  simpa only [cfcâ‚™_id ğ•œ a] using IsGreatest.norm_cfcâ‚™ (id : ğ•œ â†’ ğ•œ) a

lemma norm_quasispectrum_le (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒâ‚™ ğ•œ a) (ha : p a := by cfc_tac) :
    â€–xâ€– â‰¤ â€–aâ€– := by
  simpa only [cfcâ‚™_id ğ•œ a] using norm_apply_le_norm_cfcâ‚™ (id : ğ•œ â†’ ğ•œ) a hx

lemma isGreatest_nnnorm_quasispectrum (a : A) (ha : p a := by cfc_tac) :
    IsGreatest ((â€–Â·â€–â‚Š) '' Ïƒâ‚™ ğ•œ a) â€–aâ€–â‚Š := by
  simpa only [cfcâ‚™_id ğ•œ a] using IsGreatest.nnnorm_cfcâ‚™ (id : ğ•œ â†’ ğ•œ) a

lemma nnnorm_quasispectrum_le (a : A) â¦ƒx : ğ•œâ¦„ (hx : x âˆˆ Ïƒâ‚™ ğ•œ a) (ha : p a := by cfc_tac) :
    â€–xâ€–â‚Š â‰¤ â€–aâ€–â‚Š := by
  simpa only [cfcâ‚™_id ğ•œ a] using nnnorm_apply_le_nnnorm_cfcâ‚™ (id : ğ•œ â†’ ğ•œ) a hx

end NonUnitalIsometricContinuousFunctionalCalculus

end NormedRing

namespace QuasispectrumRestricts

open NonUnitalIsometricContinuousFunctionalCalculus

variable {R S A : Type*} {p q : A â†’ Prop}
variable [Semifield R] [StarRing R] [MetricSpace R] [IsTopologicalSemiring R] [ContinuousStar R]
variable [Field S] [StarRing S] [MetricSpace S] [IsTopologicalRing S] [ContinuousStar S]
variable [NonUnitalRing A] [StarRing A] [Module S A] [IsScalarTower S A A]
variable [SMulCommClass S A A]
variable [Algebra R S] [Module R A] [IsScalarTower R S A] [StarModule R S] [ContinuousSMul R S]
variable [IsScalarTower R A A] [SMulCommClass R A A]
variable [MetricSpace A] [NonUnitalIsometricContinuousFunctionalCalculus S A q]
variable [CompleteSpace R] [ContinuousMapZero.UniqueHom R A]

open scoped NonUnitalContinuousFunctionalCalculus in
protected theorem isometric_cfc (f : C(S, R)) (halg : Isometry (algebraMap R S)) (h0 : p 0)
    (h : âˆ€ a, p a â†” q a âˆ§ QuasispectrumRestricts a f) :
    NonUnitalIsometricContinuousFunctionalCalculus R A p where
  toNonUnitalContinuousFunctionalCalculus := QuasispectrumRestricts.cfc f
    halg.isUniformEmbedding h0 h
  isometric a ha := by
    obtain âŸ¨ha', hafâŸ© := h a |>.mp ha
    have := QuasispectrumRestricts.cfc f halg.isUniformEmbedding h0 h
    rw [cfcâ‚™Hom_eq_restrict f halg.isUniformEmbedding ha ha' haf]
    refine .of_dist_eq fun gâ‚ gâ‚‚ â†¦ ?_
    simp only [nonUnitalStarAlgHom_apply, isometry_cfcâ‚™Hom a ha' |>.dist_eq]
    refine le_antisymm ?_ ?_
    all_goals refine ContinuousMap.dist_le dist_nonneg |>.mpr fun x â†¦ ?_
    Â· simpa [halg.dist_eq] using ContinuousMap.dist_apply_le_dist _
    Â· let x' : Ïƒâ‚™ S a := Subtype.map (algebraMap R S) (fun _ â†¦ quasispectrum.algebraMap_mem S) x
      apply le_of_eq_of_le ?_ <| ContinuousMap.dist_apply_le_dist x'
      simp only [ContinuousMap.coe_coe, ContinuousMapZero.comp_apply, ContinuousMapZero.coe_mk,
        ContinuousMap.coe_mk, StarAlgHom.ofId_apply, halg.dist_eq, x']
      congr! 2
      all_goals ext; exact haf.left_inv _ |>.symm

end QuasispectrumRestricts

end NonUnital

/-! ### Instances of isometric continuous functional calculi

The instances for `â„` and `â„‚` can be found in
`Mathlib/Analysis/CStarAlgebra/ContinuousFunctionalCalculus/Basic.lean`, as those require an actual
`CStarAlgebra` instance on `A`, whereas the one for `â„â‰¥0` is simply inherited from an existing
instance for `â„`.
-/

section Instances

section Unital

variable {A : Type*} [NormedRing A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
variable [NormedAlgebra â„ A] [IsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

open NNReal in
instance Nonneg.instIsometricContinuousFunctionalCalculus :
    IsometricContinuousFunctionalCalculus â„â‰¥0 A (0 â‰¤ Â·) :=
  SpectrumRestricts.isometric_cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isometry_subtype_coe le_rfl (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

end Unital

section NonUnital

variable {A : Type*} [NonUnitalNormedRing A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
variable [NormedSpace â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
variable [NonUnitalIsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

open NNReal in
instance Nonneg.instNonUnitalIsometricContinuousFunctionalCalculus :
    NonUnitalIsometricContinuousFunctionalCalculus â„â‰¥0 A (0 â‰¤ Â·) :=
  QuasispectrumRestricts.isometric_cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isometry_subtype_coe le_rfl (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

end NonUnital

end Instances

/-! ### Properties specific to `â„â‰¥0` -/

section NNReal

open NNReal

section Unital

variable {A : Type*} [NormedRing A] [StarRing A] [NormedAlgebra â„ A] [PartialOrder A]
variable [StarOrderedRing A] [IsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

lemma IsGreatest.nnnorm_cfc_nnreal [Nontrivial A] (f : â„â‰¥0 â†’ â„â‰¥0) (a : A)
    (hf : ContinuousOn f (Ïƒ â„â‰¥0 a) := by cfc_cont_tac) (ha : 0 â‰¤ a := by cfc_tac) :
    IsGreatest (f '' Ïƒ â„â‰¥0 a) â€–cfc f aâ€–â‚Š := by
  rw [cfc_nnreal_eq_real]
  obtain âŸ¨-, ha'âŸ© := nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts.mp ha
  rw [â† SpectrumRestricts] at ha'
  convert IsGreatest.nnnorm_cfc (fun x : â„ â†¦ (f x.toNNReal : â„)) a ?hf_cont
  case hf_cont => exact continuous_subtype_val.comp_continuousOn <|
    ContinuousOn.comp â€¹_â€º continuous_real_toNNReal.continuousOn <| ha'.image â–¸ Set.mapsTo_image ..
  ext x
  constructor
  all_goals rintro âŸ¨x, hx, rflâŸ©
  Â· exact âŸ¨x, spectrum.algebraMap_mem â„ hx, by simpâŸ©
  Â· exact âŸ¨x.toNNReal, ha'.apply_mem hx, by simpâŸ©

lemma apply_le_nnnorm_cfc_nnreal (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) â¦ƒx : â„â‰¥0â¦„ (hx : x âˆˆ Ïƒ â„â‰¥0 a)
    (hf : ContinuousOn f (Ïƒ â„â‰¥0 a) := by cfc_cont_tac) (ha : 0 â‰¤ a := by cfc_tac) :
    f x â‰¤ â€–cfc f aâ€–â‚Š := by
  revert hx
  nontriviality A
  exact (IsGreatest.nnnorm_cfc_nnreal f a hf ha |>.2 âŸ¨x, Â·, rflâŸ©)

lemma nnnorm_cfc_nnreal_le {f : â„â‰¥0 â†’ â„â‰¥0} {a : A} {c : â„â‰¥0} (h : âˆ€ x âˆˆ Ïƒ â„â‰¥0 a, f x â‰¤ c) :
    â€–cfc f aâ€–â‚Š â‰¤ c := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· rw [Subsingleton.elim (cfc f a) 0]
    simp
  Â· refine cfc_cases (â€–Â·â€–â‚Š â‰¤ c) a f (by simp) fun hf ha â†¦ ?_
    simp only [â† cfc_apply f a, isLUB_le_iff (IsGreatest.nnnorm_cfc_nnreal f a hf ha |>.isLUB)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma nnnorm_cfc_nnreal_le_iff (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒ â„â‰¥0 a) := by cfc_cont_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : â€–cfc f aâ€–â‚Š â‰¤ c â†” âˆ€ x âˆˆ Ïƒ â„â‰¥0 a, f x â‰¤ c :=
  âŸ¨fun h _ hx â†¦ apply_le_nnnorm_cfc_nnreal f a hx hf ha |>.trans h, nnnorm_cfc_nnreal_leâŸ©

lemma nnnorm_cfc_nnreal_lt {f : â„â‰¥0 â†’ â„â‰¥0} {a : A} {c : â„â‰¥0} (hc : 0 < c)
    (h : âˆ€ x âˆˆ Ïƒ â„â‰¥0 a, f x < c) : â€–cfc f aâ€–â‚Š < c := by
  obtain (_ | _) := subsingleton_or_nontrivial A
  Â· rw [Subsingleton.elim (cfc f a) 0]
    simpa
  Â· refine cfc_cases (â€–Â·â€–â‚Š < c) a f (by simpa) fun hf ha â†¦ ?_
    simp only [â† cfc_apply f a, (IsGreatest.nnnorm_cfc_nnreal f a hf ha |>.lt_iff)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma nnnorm_cfc_nnreal_lt_iff (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) {c : â„â‰¥0} (hc : 0 < c)
    (hf : ContinuousOn f (Ïƒ â„â‰¥0 a) := by cfc_cont_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : â€–cfc f aâ€–â‚Š < c â†” âˆ€ x âˆˆ Ïƒ â„â‰¥0 a, f x < c :=
  âŸ¨fun h _ hx â†¦ apply_le_nnnorm_cfc_nnreal f a hx hf ha |>.trans_lt h, nnnorm_cfc_nnreal_lt hcâŸ©

namespace IsometricContinuousFunctionalCalculus

lemma isGreatest_spectrum [Nontrivial A] (a : A) (ha : 0 â‰¤ a := by cfc_tac) :
    IsGreatest (Ïƒ â„â‰¥0 a) â€–aâ€–â‚Š := by
  simpa [cfc_id â„â‰¥0 a] using IsGreatest.nnnorm_cfc_nnreal id a

lemma spectrum_le (a : A) â¦ƒx : â„â‰¥0â¦„ (hx : x âˆˆ Ïƒ â„â‰¥0 a) (ha : 0 â‰¤ a := by cfc_tac) :
    x â‰¤ â€–aâ€–â‚Š := by
  simpa [cfc_id â„â‰¥0 a] using apply_le_nnnorm_cfc_nnreal id a hx

end IsometricContinuousFunctionalCalculus

open IsometricContinuousFunctionalCalculus in
lemma MonotoneOn.nnnorm_cfc [Nontrivial A] (f : â„â‰¥0 â†’ â„â‰¥0) (a : A)
    (hf : MonotoneOn f (Ïƒ â„â‰¥0 a)) (hfâ‚‚ : ContinuousOn f (Ïƒ â„â‰¥0 a) := by cfc_cont_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : â€–cfc f aâ€–â‚Š = f â€–aâ€–â‚Š :=
  IsGreatest.nnnorm_cfc_nnreal f a |>.unique <| hf.map_isGreatest (isGreatest_spectrum a)

end Unital

section NonUnital

variable {A : Type*} [NonUnitalNormedRing A] [StarRing A] [NormedSpace â„ A]
variable [IsScalarTower â„ A A] [SMulCommClass â„ A A] [PartialOrder A]
variable [StarOrderedRing A] [NonUnitalIsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

lemma IsGreatest.nnnorm_cfcâ‚™_nnreal (f : â„â‰¥0 â†’ â„â‰¥0) (a : A)
    (hf : ContinuousOn f (Ïƒâ‚™ â„â‰¥0 a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : IsGreatest (f '' Ïƒâ‚™ â„â‰¥0 a) â€–cfcâ‚™ f aâ€–â‚Š := by
  rw [cfcâ‚™_nnreal_eq_real]
  obtain âŸ¨-, ha'âŸ© := nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts.mp ha
  convert IsGreatest.nnnorm_cfcâ‚™ (fun x : â„ â†¦ (f x.toNNReal : â„)) a ?hf_cont (by simpa)
  case hf_cont => exact continuous_subtype_val.comp_continuousOn <|
    ContinuousOn.comp â€¹_â€º continuous_real_toNNReal.continuousOn <| ha'.image â–¸ Set.mapsTo_image ..
  ext x
  constructor
  all_goals rintro âŸ¨x, hx, rflâŸ©
  Â· exact âŸ¨x, quasispectrum.algebraMap_mem â„ hx, by simpâŸ©
  Â· exact âŸ¨x.toNNReal, ha'.apply_mem hx, by simpâŸ©

lemma apply_le_nnnorm_cfcâ‚™_nnreal (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) â¦ƒx : â„â‰¥0â¦„ (hx : x âˆˆ Ïƒâ‚™ â„â‰¥0 a)
    (hf : ContinuousOn f (Ïƒâ‚™ â„â‰¥0 a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : f x â‰¤ â€–cfcâ‚™ f aâ€–â‚Š := by
  revert hx
  exact (IsGreatest.nnnorm_cfcâ‚™_nnreal f a hf hf0 ha |>.2 âŸ¨x, Â·, rflâŸ©)

lemma nnnorm_cfcâ‚™_nnreal_le {f : â„â‰¥0 â†’ â„â‰¥0} {a : A} {c : â„â‰¥0} (h : âˆ€ x âˆˆ Ïƒâ‚™ â„â‰¥0 a, f x â‰¤ c) :
    â€–cfcâ‚™ f aâ€–â‚Š â‰¤ c := by
  refine cfcâ‚™_cases (â€–Â·â€–â‚Š â‰¤ c) a f (by simp) fun hf hf0 ha â†¦ ?_
  simp only [â† cfcâ‚™_apply f a, isLUB_le_iff (IsGreatest.nnnorm_cfcâ‚™_nnreal f a hf hf0 ha |>.isLUB)]
  rintro - âŸ¨x, hx, rflâŸ©
  exact h x hx

lemma nnnorm_cfcâ‚™_nnreal_le_iff (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒâ‚™ â„â‰¥0 a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : â€–cfcâ‚™ f aâ€–â‚Š â‰¤ c â†” âˆ€ x âˆˆ Ïƒâ‚™ â„â‰¥0 a, f x â‰¤ c :=
  âŸ¨fun h _ hx â†¦ apply_le_nnnorm_cfcâ‚™_nnreal f a hx hf hfâ‚€ ha |>.trans h, nnnorm_cfcâ‚™_nnreal_leâŸ©

lemma nnnorm_cfcâ‚™_nnreal_lt {f : â„â‰¥0 â†’ â„â‰¥0} {a : A} {c : â„â‰¥0} (h : âˆ€ x âˆˆ Ïƒâ‚™ â„â‰¥0 a, f x < c) :
    â€–cfcâ‚™ f aâ€–â‚Š < c := by
  refine cfcâ‚™_cases (â€–Â·â€–â‚Š < c) a f ?_ fun hf hf0 ha â†¦ ?_
  Â· simpa using zero_le (f 0) |>.trans_lt <| h 0 (quasispectrum.zero_mem â„â‰¥0 _)
  Â· simp only [â† cfcâ‚™_apply f a, (IsGreatest.nnnorm_cfcâ‚™_nnreal f a hf hf0 ha |>.lt_iff)]
    rintro - âŸ¨x, hx, rflâŸ©
    exact h x hx

lemma nnnorm_cfcâ‚™_nnreal_lt_iff (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) (c : â„â‰¥0)
    (hf : ContinuousOn f (Ïƒâ‚™ â„â‰¥0 a) := by cfc_cont_tac) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac)
    (ha : 0 â‰¤ a := by cfc_tac) : â€–cfcâ‚™ f aâ€–â‚Š < c â†” âˆ€ x âˆˆ Ïƒâ‚™ â„â‰¥0 a, f x < c :=
  âŸ¨fun h _ hx â†¦ apply_le_nnnorm_cfcâ‚™_nnreal f a hx hf hfâ‚€ ha |>.trans_lt h, nnnorm_cfcâ‚™_nnreal_ltâŸ©

namespace NonUnitalIsometricContinuousFunctionalCalculus

lemma isGreatest_quasispectrum (a : A) (ha : 0 â‰¤ a := by cfc_tac) :
    IsGreatest (Ïƒâ‚™ â„â‰¥0 a) â€–aâ€–â‚Š := by
  simpa [cfcâ‚™_id â„â‰¥0 a] using IsGreatest.nnnorm_cfcâ‚™_nnreal id a

lemma quasispectrum_le (a : A) â¦ƒx : â„â‰¥0â¦„ (hx : x âˆˆ Ïƒâ‚™ â„â‰¥0 a) (ha : 0 â‰¤ a := by cfc_tac) :
    x â‰¤ â€–aâ€–â‚Š := by
  simpa [cfcâ‚™_id â„â‰¥0 a] using apply_le_nnnorm_cfcâ‚™_nnreal id a hx

end NonUnitalIsometricContinuousFunctionalCalculus

open NonUnitalIsometricContinuousFunctionalCalculus in
lemma MonotoneOn.nnnorm_cfcâ‚™ (f : â„â‰¥0 â†’ â„â‰¥0) (a : A)
    (hf : MonotoneOn f (Ïƒâ‚™ â„â‰¥0 a)) (hfâ‚‚ : ContinuousOn f (Ïƒâ‚™ â„â‰¥0 a) := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) (ha : 0 â‰¤ a := by cfc_tac) :
    â€–cfcâ‚™ f aâ€–â‚Š = f â€–aâ€–â‚Š :=
  IsGreatest.nnnorm_cfcâ‚™_nnreal f a |>.unique <| hf.map_isGreatest (isGreatest_quasispectrum a)

end NonUnital

end NNReal

/-! ### Non-unital instance for unital algebras -/

namespace IsometricContinuousFunctionalCalculus

variable {ğ•œ A : Type*} {p : outParam (A â†’ Prop)}
variable [RCLike ğ•œ] [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A]
variable [IsometricContinuousFunctionalCalculus ğ•œ A p]

open scoped ContinuousFunctionalCalculus in
/-- An isometric continuous functional calculus on a unital algebra yields to a non-unital one. -/
instance toNonUnital : NonUnitalIsometricContinuousFunctionalCalculus ğ•œ A p where
  isometric a ha := by
    have : CompactSpace (Ïƒâ‚™ ğ•œ a) := by
      have h_cpct : CompactSpace (spectrum ğ•œ a) := inferInstance
      simp only [â† isCompact_iff_compactSpace, quasispectrum_eq_spectrum_union_zero] at h_cpct âŠ¢
      exact h_cpct |>.union isCompact_singleton
    rw [cfcâ‚™Hom_eq_cfcâ‚™Hom_of_cfcHom, cfcâ‚™Hom_of_cfcHom]
    refine isometry_cfcHom a |>.comp ?_
    simp only [MulHom.coe_coe, NonUnitalStarAlgHom.coe_toNonUnitalAlgHom]
    refine AddMonoidHomClass.isometry_of_norm _ fun f â†¦ ?_
    let Î¹ : C(Ïƒ ğ•œ a, Ïƒâ‚™ ğ•œ a) := âŸ¨_, continuous_inclusion <| spectrum_subset_quasispectrum ğ•œ aâŸ©
    change â€–(f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)).comp Î¹â€– = â€–(f : C(Ïƒâ‚™ ğ•œ a, ğ•œ))â€–
    apply le_antisymm (ContinuousMap.norm_le _ (by positivity) |>.mpr ?_)
      (ContinuousMap.norm_le _ (by positivity) |>.mpr ?_)
    Â· rintro âŸ¨x, hxâŸ©
      exact (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)).norm_coe_le_norm âŸ¨x, spectrum_subset_quasispectrum ğ•œ a hxâŸ©
    Â· rintro âŸ¨x, hxâŸ©
      obtain (rfl | hx') : x = 0 âˆ¨ x âˆˆ Ïƒ ğ•œ a := by
        simpa [quasispectrum_eq_spectrum_union_zero] using hx
      Â· change â€–f 0â€– â‰¤ _
        simp
      Â· exact (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)).comp Î¹ |>.norm_coe_le_norm âŸ¨x, hx'âŸ©

end IsometricContinuousFunctionalCalculus
