/-
Copyright (c) 2022 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Isometric
import Mathlib.Analysis.CStarAlgebra.GelfandDuality
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.PosPart.Basic

/-! # Continuous functional calculus

In this file we construct the `continuousFunctionalCalculus` for a normal element `a` of a
(unital) C‚ãÜ-algebra over `‚ÑÇ`. This is a star algebra equivalence
`C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elemental ‚ÑÇ a` which sends the (restriction of) the
identity map `ContinuousMap.id ‚ÑÇ` to the (unique) preimage of `a` under the coercion of
`elemental ‚ÑÇ a` to `A`.

Being a star algebra equivalence between C‚ãÜ-algebras, this map is continuous (even an isometry),
and by the Stone-Weierstrass theorem it is the unique star algebra equivalence which extends the
polynomial functional calculus (i.e., `Polynomial.aeval`).

For any continuous function `f : spectrum ‚ÑÇ a ‚Üí ‚ÑÇ`, this makes it possible to define an element
`f a` (not valid notation) in the original algebra, which heuristically has the same eigenspaces as
`a` and acts on eigenvector of `a` for an eigenvalue `Œª` as multiplication by `f Œª`. This
description is perfectly accurate in finite dimension, but only heuristic in infinite dimension as
there might be no genuine eigenvector. In particular, when `f` is a polynomial `‚àë c·µ¢ X‚Å±`, then
`f a` is `‚àë c·µ¢ a‚Å±`. Also, `id a = a`.

The result we have established here is the strongest possible, but it is not the version which is
most useful in practice. The generic API for the continuous functional calculus can be found in
`Analysis.CStarAlgebra.ContinuousFunctionalCalculus` in the `Unital` and `NonUnital` files. The
relevant instances on C‚ãÜ-algebra can be found in the `Instances` file.

## Main definitions

* `continuousFunctionalCalculus : C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elemental ‚ÑÇ a`: this
  is the composition of the inverse of the `gelfandStarTransform` with the natural isomorphism
  induced by the homeomorphism `elemental.characterSpaceHomeo`.
* `elemental.characterSpaceHomeo` :
  `characterSpace ‚ÑÇ (elemental ‚ÑÇ a) ‚âÉ‚Çú spectrum ‚ÑÇ a`: this homeomorphism is defined
  by evaluating a character `œÜ` at `a`, and noting that `œÜ a ‚àà spectrum ‚ÑÇ a` since `œÜ` is an
  algebra homomorphism. Moreover, this map is continuous and bijective and since the spaces involved
  are compact Hausdorff, it is a homeomorphism.
* `IsStarNormal.instContinuousFunctionalCalculus`: the continuous functional calculus for normal
  elements in a unital C‚ãÜ-algebra over `‚ÑÇ`.
* `CStarAlgebra.instNonnegSpectrumClass`: In a unital C‚ãÜ-algebra over `‚ÑÇ` which is also a
  `StarOrderedRing`, the spectrum of a nonnegative element is nonnegative.

-/


open scoped Pointwise ENNReal NNReal ComplexOrder

open WeakDual WeakDual.CharacterSpace

variable {A : Type*} [CStarAlgebra A]

namespace StarAlgebra.elemental

instance {R A : Type*} [CommRing R] [StarRing R] [NormedRing A] [Algebra R A] [StarRing A]
    [ContinuousStar A] [StarModule R A] (a : A) [IsStarNormal a] :
    NormedCommRing (elemental R a) :=
  { SubringClass.toNormedRing (elemental R a) with
    mul_comm := mul_comm }

noncomputable instance (a : A) [IsStarNormal a] : CommCStarAlgebra (elemental ‚ÑÇ a) where

variable (a : A) [IsStarNormal a]

/-- The natural map from `characterSpace ‚ÑÇ (elemental ‚ÑÇ x)` to `spectrum ‚ÑÇ x` given
by evaluating `œÜ` at `x`. This is essentially just evaluation of the `gelfandTransform` of `x`,
but because we want something in `spectrum ‚ÑÇ x`, as opposed to
`spectrum ‚ÑÇ ‚ü®x, elemental.self_mem ‚ÑÇ x‚ü©` there is slightly more work to do. -/
@[simps]
noncomputable def characterSpaceToSpectrum (x : A)
    (œÜ : characterSpace ‚ÑÇ (elemental ‚ÑÇ x)) : spectrum ‚ÑÇ x where
  val := œÜ ‚ü®x, self_mem ‚ÑÇ x‚ü©
  property := by
    simpa only [StarSubalgebra.spectrum_eq (hS := isClosed ‚ÑÇ x)
      (a := ‚ü®x, self_mem ‚ÑÇ x‚ü©)] using AlgHom.apply_mem_spectrum œÜ ‚ü®x, self_mem ‚ÑÇ x‚ü©

theorem continuous_characterSpaceToSpectrum (x : A) :
    Continuous (characterSpaceToSpectrum x) :=
  continuous_induced_rng.2
    (map_continuous <| gelfandTransform ‚ÑÇ (elemental ‚ÑÇ x) ‚ü®x, self_mem ‚ÑÇ x‚ü©)

theorem bijective_characterSpaceToSpectrum :
    Function.Bijective (characterSpaceToSpectrum a) := by
  refine ‚ü®fun œÜ œà h => starAlgHomClass_ext ‚ÑÇ ?_ ?_ ?_, ?_‚ü©
  ¬∑ exact (map_continuous œÜ)
  ¬∑ exact (map_continuous œà)
  ¬∑ simpa only [characterSpaceToSpectrum, Subtype.mk_eq_mk,
      ContinuousMap.coe_mk] using h
  ¬∑ rintro ‚ü®z, hz‚ü©
    have hz' := (StarSubalgebra.spectrum_eq (hS := isClosed ‚ÑÇ a)
      (a := ‚ü®a, self_mem ‚ÑÇ a‚ü©) ‚ñ∏ hz)
    rw [CharacterSpace.mem_spectrum_iff_exists] at hz'
    obtain ‚ü®œÜ, rfl‚ü© := hz'
    exact ‚ü®œÜ, rfl‚ü©

/-- The homeomorphism between the character space of the unital C‚ãÜ-subalgebra generated by a
single normal element `a : A` and `spectrum ‚ÑÇ a`. -/
noncomputable def characterSpaceHomeo :
    characterSpace ‚ÑÇ (elemental ‚ÑÇ a) ‚âÉ‚Çú spectrum ‚ÑÇ a :=
  @Continuous.homeoOfEquivCompactToT2 _ _ _ _ _ _
    (Equiv.ofBijective (characterSpaceToSpectrum a)
      (bijective_characterSpaceToSpectrum a))
    (continuous_characterSpaceToSpectrum a)

end StarAlgebra.elemental

open StarAlgebra elemental

variable (a : A) [IsStarNormal a]

/-- **Continuous functional calculus.** Given a normal element `a : A` of a unital C‚ãÜ-algebra,
the continuous functional calculus is a `StarAlgEquiv` from the complex-valued continuous
functions on the spectrum of `a` to the unital C‚ãÜ-subalgebra generated by `a`. Moreover, this
equivalence identifies `(ContinuousMap.id ‚ÑÇ).restrict (spectrum ‚ÑÇ a))` with `a`; see
`continuousFunctionalCalculus_map_id`. As such it extends the polynomial functional calculus. -/
noncomputable def continuousFunctionalCalculus :
    C(spectrum ‚ÑÇ a, ‚ÑÇ) ‚âÉ‚ãÜ‚Çê[‚ÑÇ] elemental ‚ÑÇ a :=
  ((characterSpaceHomeo a).compStarAlgEquiv' ‚ÑÇ ‚ÑÇ).trans
    (gelfandStarTransform (elemental ‚ÑÇ a)).symm

theorem continuousFunctionalCalculus_map_id :
    continuousFunctionalCalculus a ((ContinuousMap.id ‚ÑÇ).restrict (spectrum ‚ÑÇ a)) =
      ‚ü®a, self_mem ‚ÑÇ a‚ü© :=
  (gelfandStarTransform (elemental ‚ÑÇ a)).symm_apply_apply _

/-!
### Continuous functional calculus for normal elements
-/

local notation "œÉ‚Çô" => quasispectrum

section Normal

instance IsStarNormal.instContinuousFunctionalCalculus {A : Type*} [CStarAlgebra A] :
    ContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal where
  predicate_zero := .zero
  spectrum_nonempty a _ := spectrum.nonempty a
  exists_cfc_of_predicate a ha := by
    refine ‚ü®(StarAlgebra.elemental ‚ÑÇ a).subtype.comp <| continuousFunctionalCalculus a,
      ?hom_isClosedEmbedding, ?hom_id, ?hom_map_spectrum, ?predicate_hom‚ü©
    case hom_isClosedEmbedding =>
      exact Isometry.isClosedEmbedding <|
        isometry_subtype_coe.comp <| StarAlgEquiv.isometry (continuousFunctionalCalculus a)
    case hom_id => exact congr_arg Subtype.val <| continuousFunctionalCalculus_map_id a
    case hom_map_spectrum =>
      intro f
      simp only [StarAlgHom.comp_apply, StarAlgHom.coe_coe, StarSubalgebra.coe_subtype]
      rw [‚Üê StarSubalgebra.spectrum_eq (hS := StarAlgebra.elemental.isClosed ‚ÑÇ a),
        AlgEquiv.spectrum_eq (continuousFunctionalCalculus a), ContinuousMap.spectrum_eq_range]
    case predicate_hom => exact fun f ‚Ü¶ ‚ü®by rw [‚Üê map_star]; exact Commute.all (star f) f |>.map _‚ü©

lemma cfcHom_eq_of_isStarNormal {A : Type*} [CStarAlgebra A] (a : A) [ha : IsStarNormal a] :
    cfcHom ha = (StarAlgebra.elemental ‚ÑÇ a).subtype.comp (continuousFunctionalCalculus a) := by
  refine cfcHom_eq_of_continuous_of_map_id ha _ ?_ ?_
  ¬∑ exact continuous_subtype_val.comp <|
      (StarAlgEquiv.isometry (continuousFunctionalCalculus a)).continuous
  ¬∑ simp [continuousFunctionalCalculus_map_id a]

instance IsStarNormal.instNonUnitalContinuousFunctionalCalculus {A : Type*}
    [NonUnitalCStarAlgebra A] : NonUnitalContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal :=
  RCLike.nonUnitalContinuousFunctionalCalculus Unitization.isStarNormal_inr

open Unitization CStarAlgebra in
lemma inr_comp_cfc‚ÇôHom_eq_cfc‚ÇôAux {A : Type*} [NonUnitalCStarAlgebra A] (a : A)
    [ha : IsStarNormal a] : (inrNonUnitalStarAlgHom ‚ÑÇ A).comp (cfc‚ÇôHom ha) =
      cfc‚ÇôAux (isStarNormal_inr (R := ‚ÑÇ) (A := A)) a ha := by
  have h (a : A) := isStarNormal_inr (R := ‚ÑÇ) (A := A) (a := a)
  refine @ContinuousMapZero.UniqueHom.eq_of_continuous_of_map_id
    _ _ _ _ _ _ _ _ _ _ _ inferInstance inferInstance _ (œÉ‚Çô ‚ÑÇ a) _ _ _ _ ?_ ?_ ?_
  ¬∑ change Continuous (fun f ‚Ü¶ (cfc‚ÇôHom ha f : A‚Å∫¬π)); fun_prop
  ¬∑ exact isClosedEmbedding_cfc‚ÇôAux @(h) a ha |>.continuous
  ¬∑ trans (a : A‚Å∫¬π)
    ¬∑ congrm(inr $(cfc‚ÇôHom_id ha))
    ¬∑ exact cfc‚ÇôAux_id @(h) a ha |>.symm

end Normal

/-!
### The spectrum of a nonnegative element is nonnegative
-/

section SpectrumRestricts

open NNReal ENNReal

variable {A : Type*} [CStarAlgebra A]

lemma SpectrumRestricts.nnreal_iff_nnnorm {a : A} {t : ‚Ñù‚â•0} (ha : IsSelfAdjoint a) (ht : ‚Äña‚Äñ‚Çä ‚â§ t) :
    SpectrumRestricts a ContinuousMap.realToNNReal ‚Üî ‚ÄñalgebraMap ‚Ñù A t - a‚Äñ‚Çä ‚â§ t := by
  have : IsSelfAdjoint (algebraMap ‚Ñù A t - a) := IsSelfAdjoint.algebraMap A (.all (t : ‚Ñù)) |>.sub ha
  rw [‚Üê ENNReal.coe_le_coe, ‚Üê IsSelfAdjoint.spectralRadius_eq_nnnorm,
    ‚Üê SpectrumRestricts.spectralRadius_eq (f := Complex.reCLM)] at ht ‚ä¢
  ¬∑ exact SpectrumRestricts.nnreal_iff_spectralRadius_le ht
  all_goals
    try apply IsSelfAdjoint.spectrumRestricts
    assumption

lemma SpectrumRestricts.nnreal_add {a b : A} (ha‚ÇÅ : IsSelfAdjoint a)
    (hb‚ÇÅ : IsSelfAdjoint b) (ha‚ÇÇ : SpectrumRestricts a ContinuousMap.realToNNReal)
    (hb‚ÇÇ : SpectrumRestricts b ContinuousMap.realToNNReal) :
    SpectrumRestricts (a + b) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff_nnnorm (ha‚ÇÅ.add hb‚ÇÅ) (nnnorm_add_le a b), NNReal.coe_add,
    map_add, add_sub_add_comm]
  refine nnnorm_add_le _ _ |>.trans ?_
  gcongr
  all_goals rw [‚Üê SpectrumRestricts.nnreal_iff_nnnorm] <;> first | rfl | assumption

lemma IsSelfAdjoint.sq_spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts (a ^ 2) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff, ‚Üê cfc_id (R := ‚Ñù) a, ‚Üê cfc_pow .., cfc_map_spectrum ..]
  rintro - ‚ü®x, -, rfl‚ü©
  exact sq_nonneg x

open ComplexStarModule

lemma SpectrumRestricts.eq_zero_of_neg {a : A} (ha : IsSelfAdjoint a)
    (ha‚ÇÅ : SpectrumRestricts a ContinuousMap.realToNNReal)
    (ha‚ÇÇ : SpectrumRestricts (-a) ContinuousMap.realToNNReal) :
    a = 0 := by
  nontriviality A
  rw [SpectrumRestricts.nnreal_iff] at ha‚ÇÅ ha‚ÇÇ
  apply CFC.eq_zero_of_spectrum_subset_zero (R := ‚Ñù) a
  rw [Set.subset_singleton_iff]
  simp only [‚Üê spectrum.neg_eq, Set.mem_neg] at ha‚ÇÇ
  peel ha‚ÇÅ with x hx _
  linarith [ha‚ÇÇ (-x) ((neg_neg x).symm ‚ñ∏ hx)]

lemma SpectrumRestricts.smul_of_nonneg {A : Type*} [Ring A] [Algebra ‚Ñù A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    SpectrumRestricts (r ‚Ä¢ a) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff] at ha ‚ä¢
  nontriviality A
  intro x hx
  by_cases hr' : r = 0
  ¬∑ simp [hr'] at hx ‚ä¢
    exact hx.symm.le
  ¬∑ lift r to ‚ÑùÀ£ using IsUnit.mk0 r hr'
    rw [‚Üê Units.smul_def, spectrum.unit_smul_eq_smul, Set.mem_smul_set_iff_inv_smul_mem] at hx
    refine le_of_smul_le_smul_left ?_ (inv_pos.mpr <| lt_of_le_of_ne hr <| ne_comm.mpr hr')
    simpa [Units.smul_def] using ha _ hx

/-- The `‚Ñù`-spectrum of an element of the form `star b * b` in a C‚ãÜ-algebra is nonnegative.

This is the key result used to establish `CStarAlgebra.instNonnegSpectrumClass`. -/
lemma spectrum_star_mul_self_nonneg {b : A} : ‚àÄ x ‚àà spectrum ‚Ñù (star b * b), 0 ‚â§ x := by
  -- for convenience we'll work with `a := star b * b`, which is selfadjoint.
  set a := star b * b with a_def
  have ha : IsSelfAdjoint a := by simp [a_def]
  -- the key element to consider is `c := b * a‚Åª`, which satisfies `- (star c * c) = a‚Åª ^ 3`.
  set c := b * a‚Åª
  have h_eq_negPart_a : - (star c * c) = a‚Åª ^ 3 := calc
    -(star c * c) = - a‚Åª * a * a‚Åª := by
      simp only [star_mul, c, mul_assoc, ‚Üê mul_assoc (star b), ‚Üê a_def, CFC.negPart_def,
        neg_mul, IsSelfAdjoint.cfc‚Çô (f := (¬∑‚Åª)).star_eq]
    _ = - a‚Åª * (a‚Å∫ - a‚Åª) * a‚Åª :=
      congr(- a‚Åª * $(CFC.posPart_sub_negPart a ha) * a‚Åª).symm
    _ = a‚Åª ^ 3 := by simp [mul_sub, pow_succ]
  -- the spectrum of `- (star c * c) = a‚Åª ^ 3` is nonnegative, since the function on the right
  -- is nonnegative on the spectrum of `a`.
  have h_c_spec‚ÇÄ : SpectrumRestricts (- (star c * c)) (ContinuousMap.realToNNReal ¬∑) := by
    simp only [SpectrumRestricts.nnreal_iff, h_eq_negPart_a, CFC.negPart_def]
    rw [cfc‚Çô_eq_cfc (hf0 := by simp), ‚Üê cfc_pow (ha := ha) .., cfc_map_spectrum (ha := ha) ..]
    rintro - ‚ü®x, -, rfl‚ü©
    positivity
  -- the spectrum of `c * star c` is nonnegative, since squares of selfadjoint elements have
  -- nonnegative spectrum, and `c * star c = 2 ‚Ä¢ (‚Ñú c ^ 2 + ‚Ñë c ^ 2) + (- (star c * c))`,
  -- and selfadjoint elements with nonnegative spectrum are closed under addition.
  have h_c_spec‚ÇÅ : SpectrumRestricts (c * star c) ContinuousMap.realToNNReal := by
    rw [eq_sub_iff_add_eq'.mpr <| star_mul_self_add_self_mul_star c, sub_eq_add_neg, ‚Üê sq, ‚Üê sq]
    refine SpectrumRestricts.nnreal_add ?_ ?_ ?_ h_c_spec‚ÇÄ
    ¬∑ exact .smul (star_trivial _) <| ((‚Ñú c).prop.pow 2).add ((‚Ñë c).prop.pow 2)
    ¬∑ exact .neg <| .star_mul_self c
    ¬∑ rw [‚Üê Nat.cast_smul_eq_nsmul ‚Ñù]
      refine (‚Ñú c).2.sq_spectrumRestricts.nnreal_add ((‚Ñú c).2.pow 2) ((‚Ñë c).2.pow 2)
        (‚Ñë c).2.sq_spectrumRestricts |>.smul_of_nonneg <| by simp
  -- therefore `- (star c * c) = 0` and so `a‚Åª ^ 3 = 0`. By properties of the continuous functional
  -- calculus, `fun x ‚Ü¶ x‚Åª ^ 3` is zero on the spectrum of `a`, `0 ‚â§ x` for `x ‚àà spectrum ‚Ñù a`.
  rw [h_c_spec‚ÇÅ.mul_comm.eq_zero_of_neg (.star_mul_self c) h_c_spec‚ÇÄ, neg_zero, CFC.negPart_def,
    cfc‚Çô_eq_cfc (hf0 := by simp), ‚Üê cfc_pow _ _ (ha := ha), ‚Üê cfc_zero a (R := ‚Ñù)] at h_eq_negPart_a
  have h_eqOn := eqOn_of_cfc_eq_cfc (ha := ha) h_eq_negPart_a
  exact fun x hx ‚Ü¶ negPart_eq_zero.mp <| eq_zero_of_pow_eq_zero (h_eqOn hx).symm

lemma IsSelfAdjoint.coe_mem_spectrum_complex {A : Type*} [TopologicalSpace A] [Ring A]
    [StarRing A] [Algebra ‚ÑÇ A] [ContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal]
    {a : A} {x : ‚Ñù} (ha : IsSelfAdjoint a := by cfc_tac) :
    (x : ‚ÑÇ) ‚àà spectrum ‚ÑÇ a ‚Üî x ‚àà spectrum ‚Ñù a := by
  simp [‚Üê ha.spectrumRestricts.algebraMap_image]

end SpectrumRestricts

section NonnegSpectrumClass

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

instance CStarAlgebra.instNonnegSpectrumClass : NonnegSpectrumClass ‚Ñù A :=
  .of_spectrum_nonneg fun a ha ‚Ü¶ by
    rw [StarOrderedRing.nonneg_iff] at ha
    induction ha using AddSubmonoid.closure_induction with
    | mem x hx =>
      obtain ‚ü®b, rfl‚ü© := hx
      exact spectrum_star_mul_self_nonneg
    | zero =>
      nontriviality A
      simp
    | add x y x_mem y_mem hx hy =>
      rw [‚Üê SpectrumRestricts.nnreal_iff] at hx hy ‚ä¢
      rw [‚Üê StarOrderedRing.nonneg_iff] at x_mem y_mem
      exact hx.nnreal_add (.of_nonneg x_mem) (.of_nonneg y_mem) hy

open ComplexOrder in
instance CStarAlgebra.instNonnegSpectrumClassComplexUnital : NonnegSpectrumClass ‚ÑÇ A where
  quasispectrum_nonneg_of_nonneg a ha x := by
    rw [mem_quasispectrum_iff]
    refine (Or.elim ¬∑ ge_of_eq fun hx ‚Ü¶ ?_)
    obtain ‚ü®y, hy, rfl‚ü© := (IsSelfAdjoint.of_nonneg ha).spectrumRestricts.algebraMap_image ‚ñ∏ hx
    simpa using spectrum_nonneg_of_nonneg ha hy

end NonnegSpectrumClass

section SpectralOrder

variable (A : Type*) [NonUnitalCStarAlgebra A]

open scoped CStarAlgebra
/-- The partial order on a C‚ãÜ-algebra defined by `x ‚â§ y` if and only if `y - x` is
selfadjoint and has nonnegative spectrum.

This is not declared as an instance because one may already have a partial order with better
definitional properties. However, it can be useful to invoke this as an instance in proofs. -/
@[reducible]
def CStarAlgebra.spectralOrder : PartialOrder A where
  le x y := IsSelfAdjoint (y - x) ‚àß QuasispectrumRestricts (y - x) ContinuousMap.realToNNReal
  le_refl := by
    simp only [sub_self, IsSelfAdjoint.zero, true_and, forall_const]
    rw [quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ, SpectrumRestricts.nnreal_iff]
    nontriviality A
    simp
  le_antisymm x y hxy hyx := by
    rw [‚Üê Unitization.isSelfAdjoint_inr (R := ‚ÑÇ),
      quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ, Unitization.inr_sub ‚ÑÇ] at hxy hyx
    rw [‚Üê sub_eq_zero]
    apply Unitization.inr_injective (R := ‚ÑÇ)
    rw [Unitization.inr_zero, Unitization.inr_sub]
    exact hyx.2.eq_zero_of_neg hyx.1 (neg_sub (x : A‚Å∫¬π) (y : A‚Å∫¬π) ‚ñ∏ hxy.2)
  le_trans x y z hxy hyz := by
    simp +singlePass only [‚Üê Unitization.isSelfAdjoint_inr (R := ‚ÑÇ),
      quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ] at hxy hyz ‚ä¢
    exact ‚ü®by simpa using hyz.1.add hxy.1, by simpa using hyz.2.nnreal_add hyz.1 hxy.1 hxy.2‚ü©

/-- The `CStarAlgebra.spectralOrder` on a C‚ãÜ-algebra is a `StarOrderedRing`. -/
lemma CStarAlgebra.spectralOrderedRing : @StarOrderedRing A _ (CStarAlgebra.spectralOrder A) _ :=
  let _ := CStarAlgebra.spectralOrder A
  { le_iff := by
      intro x y
      constructor
      ¬∑ intro h
        obtain ‚ü®s, hs‚ÇÅ, _, hs‚ÇÇ‚ü© :=
          CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts h.1 h.2
        refine ‚ü®s * s, ?_, by rwa [eq_sub_iff_add_eq', eq_comm] at hs‚ÇÇ‚ü©
        exact AddSubmonoid.subset_closure ‚ü®s, by simp [hs‚ÇÅ.star_eq]‚ü©
      ¬∑ rintro ‚ü®p, hp, rfl‚ü©
        simp only [spectralOrder, add_sub_cancel_left]
        induction hp using AddSubmonoid.closure_induction with
        | mem x hx =>
          obtain ‚ü®s, rfl‚ü© := hx
          refine ‚ü®IsSelfAdjoint.star_mul_self s, ?_‚ü©
          rw [quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ,
            SpectrumRestricts.nnreal_iff, Unitization.inr_mul, Unitization.inr_star]
          exact spectrum_star_mul_self_nonneg
        | zero =>
          rw [quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ, SpectrumRestricts.nnreal_iff]
          nontriviality A
          simp
        | add x y _ _ hx hy =>
          simp +singlePass only [‚Üê Unitization.isSelfAdjoint_inr (R := ‚ÑÇ),
            quasispectrumRestricts_iff_spectrumRestricts_inr' ‚ÑÇ] at hx hy ‚ä¢
          rw [Unitization.inr_add]
          exact ‚ü®hx.1.add hy.1, hx.2.nnreal_add hx.1 hy.1 hy.2‚ü© }

end SpectralOrder

section NonnegSpectrumClass

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

open scoped CStarAlgebra in
instance CStarAlgebra.instNonnegSpectrumClass' : NonnegSpectrumClass ‚Ñù A where
  quasispectrum_nonneg_of_nonneg a ha := by
    rw [Unitization.quasispectrum_eq_spectrum_inr' _ ‚ÑÇ]
    -- should this actually be an instance on the `Unitization`? (probably scoped)
    let _ := CStarAlgebra.spectralOrder A‚Å∫¬π
    have := CStarAlgebra.spectralOrderedRing A‚Å∫¬π
    apply spectrum_nonneg_of_nonneg
    rw [StarOrderedRing.nonneg_iff] at ha ‚ä¢
    have := AddSubmonoid.mem_map_of_mem (Unitization.inrNonUnitalStarAlgHom ‚ÑÇ A) ha
    rw [AddMonoidHom.map_mclosure, ‚Üê Set.range_comp] at this
    apply AddSubmonoid.closure_mono ?_ this
    rintro _ ‚ü®s, rfl‚ü©
    exact ‚ü®s, by simp‚ü©

end NonnegSpectrumClass

section cfc_inr

open CStarAlgebra

variable {A : Type*} [NonUnitalCStarAlgebra A]

open scoped NonUnitalContinuousFunctionalCalculus in
/-- This lemma requires a lot from type class synthesis, and so one should instead favor the bespoke
versions for `‚Ñù‚â•0`, `‚Ñù`, and `‚ÑÇ`. -/
lemma Unitization.cfc‚Çô_eq_cfc_inr {R : Type*} [Semifield R] [StarRing R] [MetricSpace R]
    [IsTopologicalSemiring R] [ContinuousStar R] [Module R A] [IsScalarTower R A A]
    [SMulCommClass R A A] [CompleteSpace R] [Algebra R ‚ÑÇ] [IsScalarTower R ‚ÑÇ A]
    {p : A ‚Üí Prop} {p' : A‚Å∫¬π ‚Üí Prop} [NonUnitalContinuousFunctionalCalculus R A p]
    [ContinuousFunctionalCalculus R A‚Å∫¬π p']
    [ContinuousMapZero.UniqueHom R (Unitization ‚ÑÇ A)]
    (hp : ‚àÄ {a : A}, p' (a : A‚Å∫¬π) ‚Üî p a) (a : A) (f : R ‚Üí R) (hf‚ÇÄ : f 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô f a = cfc f (a : A‚Å∫¬π) := by
  by_cases h : ContinuousOn f (œÉ‚Çô R a) ‚àß p a
  ¬∑ obtain ‚ü®hf, ha‚ü© := h
    rw [‚Üê cfc‚Çô_eq_cfc (quasispectrum_inr_eq R ‚ÑÇ a ‚ñ∏ hf)]
    exact (inrNonUnitalStarAlgHom ‚ÑÇ A).map_cfc‚Çô f a
  ¬∑ obtain (hf | ha) := not_and_or.mp h
    ¬∑ rw [cfc‚Çô_apply_of_not_continuousOn a hf, inr_zero,
        cfc_apply_of_not_continuousOn _ (quasispectrum_eq_spectrum_inr' R ‚ÑÇ a ‚ñ∏ hf)]
    ¬∑ rw [cfc‚Çô_apply_of_not_predicate a ha, inr_zero,
        cfc_apply_of_not_predicate _ (not_iff_not.mpr hp |>.mpr ha)]

lemma Unitization.complex_cfc‚Çô_eq_cfc_inr (a : A) (f : ‚ÑÇ ‚Üí ‚ÑÇ) (hf‚ÇÄ : f 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô f a = cfc f (a : A‚Å∫¬π) :=
  Unitization.cfc‚Çô_eq_cfc_inr isStarNormal_inr ..

/-- note: the version for `‚Ñù‚â•0`, `Unitization.nnreal_cfc‚Çô_eq_cfc_inr`, can be found in
`Mathlib/Analysis/CStarAlgebra/ContinuousFunctionalCalculus/Order.lean` -/
lemma Unitization.real_cfc‚Çô_eq_cfc_inr (a : A) (f : ‚Ñù ‚Üí ‚Ñù) (hf‚ÇÄ : f 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô f a = cfc f (a : A‚Å∫¬π) :=
  Unitization.cfc‚Çô_eq_cfc_inr isSelfAdjoint_inr ..

end cfc_inr

/-! ### Instances of isometric continuous functional calculi -/

section Unital

variable {A : Type*} [CStarAlgebra A]

instance IsStarNormal.instIsometricContinuousFunctionalCalculus :
    IsometricContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal where
  isometric a ha := by
    rw [cfcHom_eq_of_isStarNormal]
    exact isometry_subtype_coe.comp <| StarAlgEquiv.isometry (continuousFunctionalCalculus a)

instance IsSelfAdjoint.instIsometricContinuousFunctionalCalculus :
    IsometricContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint :=
  SpectrumRestricts.isometric_cfc Complex.reCLM Complex.isometry_ofReal (.zero _)
    fun _ ‚Ü¶ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts

end Unital

section NonUnital

variable {A : Type*} [NonUnitalCStarAlgebra A]

open Unitization

open ContinuousMapZero in
instance IsStarNormal.instNonUnitalIsometricContinuousFunctionalCalculus :
    NonUnitalIsometricContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal where
  isometric a ha := by
    refine AddMonoidHomClass.isometry_of_norm _ fun f ‚Ü¶ ?_
    rw [‚Üê norm_inr (ùïú := ‚ÑÇ), ‚Üê inrNonUnitalStarAlgHom_apply, ‚Üê NonUnitalStarAlgHom.comp_apply,
      inr_comp_cfc‚ÇôHom_eq_cfc‚ÇôAux a, cfc‚ÇôAux]
    simp only [NonUnitalStarAlgHom.comp_assoc, NonUnitalStarAlgHom.comp_apply,
      toContinuousMapHom_apply, NonUnitalStarAlgHom.coe_coe]
    rw [norm_cfcHom (a : Unitization ‚ÑÇ A), StarAlgEquiv.norm_map]
    rfl

instance IsSelfAdjoint.instNonUnitalIsometricContinuousFunctionalCalculus :
    NonUnitalIsometricContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint :=
  QuasispectrumRestricts.isometric_cfc Complex.reCLM Complex.isometry_ofReal (.zero _)
    fun _ ‚Ü¶ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts

end NonUnital
