/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Restrict
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Analysis.CStarAlgebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique

/-! # Instances of the continuous functional calculus

## Main theorems

* `IsStarNormal.instContinuousFunctionalCalculus`: the continuous functional calculus for normal
  elements in a unital Câ‹†-algebra over `â„‚`.
* `IsSelfAdjoint.instContinuousFunctionalCalculus`: the continuous functional calculus for
  selfadjoint elements in a `â„‚`-algebra with a continuous functional calculus for normal elements
  and where every element has compact spectrum. In particular, this includes unital Câ‹†-algebras
  over `â„‚`.
* `Nonneg.instContinuousFunctionalCalculus`: the continuous functional calculus for nonnegative
  elements in an `â„`-algebra with a continuous functional calculus for selfadjoint elements,
  where every element has compact spectrum, and where nonnegative elements have nonnegative
  spectrum. In particular, this includes unital Câ‹†-algebras over `â„`.
* `CStarAlgebra.instNonnegSpectrumClass`: In a unital Câ‹†-algebra over `â„‚` which is also a
  `StarOrderedRing`, the spectrum of a nonnegative element is nonnegative.

## Tags

continuous functional calculus, normal, selfadjoint
-/

open Topology

noncomputable section

local notation "Ïƒâ‚™" => quasispectrum
local notation "Ïƒ" => spectrum

/-!
### Pull back a non-unital instance from a unital one on the unitization
-/

section RCLike

variable {ğ•œ A : Type*} [RCLike ğ•œ] [NonUnitalNormedRing A] [StarRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
variable [StarModule ğ•œ A] {p : A â†’ Prop} {pâ‚ : Unitization ğ•œ A â†’ Prop}

local postfix:max "âºÂ¹" => Unitization ğ•œ

variable (hpâ‚ : âˆ€ {x : A}, pâ‚ x â†” p x) (a : A) (ha : p a)
variable [ContinuousFunctionalCalculus ğ•œ pâ‚]

open scoped ContinuousMapZero


open Unitization in
/--
This is an auxiliary definition used for constructing an instance of the non-unital continuous
functional calculus given a instance of the unital one on the unitization.

This is the natural non-unital star homomorphism obtained from the chain
```lean
calc
  C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] C(Ïƒâ‚™ ğ•œ a, ğ•œ) := ContinuousMapZero.toContinuousMapHom
  _             â‰ƒâ‹†[ğ•œ] C(Ïƒ ğ•œ (â†‘a : AâºÂ¹), ğ•œ) := Homeomorph.compStarAlgEquiv'
  _             â†’â‹†â‚[ğ•œ] AâºÂ¹ := cfcHom
```
This range of this map is contained in the range of `(â†‘) : A â†’ AâºÂ¹` (see `cfcâ‚™Aux_mem_range_inr`),
and so we may restrict it to `A` to get the necessary homomorphism for the non-unital continuous
functional calculus.
-/
noncomputable def cfcâ‚™Aux : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] AâºÂ¹ :=
  (cfcHom (R := ğ•œ) (hpâ‚.mpr ha) : C(Ïƒ ğ•œ (a : AâºÂ¹), ğ•œ) â†’â‹†â‚™â‚[ğ•œ] AâºÂ¹) |>.comp
    (Homeomorph.compStarAlgEquiv' ğ•œ ğ•œ <| .setCongr <| (quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a).symm)
    |>.comp ContinuousMapZero.toContinuousMapHom

lemma cfcâ‚™Aux_id : cfcâ‚™Aux hpâ‚ a ha (ContinuousMapZero.id rfl) = a := cfcHom_id (hpâ‚.mpr ha)

open Unitization in
lemma isClosedEmbedding_cfcâ‚™Aux : IsClosedEmbedding (cfcâ‚™Aux hpâ‚ a ha) := by
  simp only [cfcâ‚™Aux, NonUnitalStarAlgHom.coe_comp]
  refine ((cfcHom_isClosedEmbedding (hpâ‚.mpr ha)).comp ?_).comp
    ContinuousMapZero.isClosedEmbedding_toContinuousMap
  let e : C(Ïƒâ‚™ ğ•œ a, ğ•œ) â‰ƒâ‚œ C(Ïƒ ğ•œ (a : AâºÂ¹), ğ•œ) :=
    (Homeomorph.setCongr (quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a)).arrowCongr (.refl _)
  exact e.isClosedEmbedding

@[deprecated (since := "2024-10-20")]
alias closedEmbedding_cfcâ‚™Aux := isClosedEmbedding_cfcâ‚™Aux

lemma spec_cfcâ‚™Aux (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) : Ïƒ ğ•œ (cfcâ‚™Aux hpâ‚ a ha f) = Set.range f := by
  rw [cfcâ‚™Aux, NonUnitalStarAlgHom.comp_assoc, NonUnitalStarAlgHom.comp_apply]
  simp only [NonUnitalStarAlgHom.comp_apply, NonUnitalStarAlgHom.coe_coe]
  rw [cfcHom_map_spectrum (hpâ‚.mpr ha) (R := ğ•œ) _]
  ext x
  constructor
  all_goals rintro âŸ¨x, rflâŸ©
  Â· exact âŸ¨âŸ¨x, (Unitization.quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a).symm â–¸ x.propertyâŸ©, rflâŸ©
  Â· exact âŸ¨âŸ¨x, Unitization.quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a â–¸ x.propertyâŸ©, rflâŸ©

variable [CompleteSpace A]

lemma cfcâ‚™Aux_mem_range_inr (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) :
    cfcâ‚™Aux hpâ‚ a ha f âˆˆ NonUnitalStarAlgHom.range (Unitization.inrNonUnitalStarAlgHom ğ•œ A) := by
  have hâ‚ := (isClosedEmbedding_cfcâ‚™Aux hpâ‚ a ha).continuous.range_subset_closure_image_dense
    (ContinuousMapZero.adjoin_id_dense (s := Ïƒâ‚™ ğ•œ a) rfl) âŸ¨f, rflâŸ©
  rw [â† SetLike.mem_coe]
  refine closure_minimal ?_ ?_ hâ‚
  Â· rw [â† NonUnitalStarSubalgebra.coe_map, SetLike.coe_subset_coe, NonUnitalStarSubalgebra.map_le]
    apply NonUnitalStarAlgebra.adjoin_le
    apply Set.singleton_subset_iff.mpr
    rw [SetLike.mem_coe, NonUnitalStarSubalgebra.mem_comap, cfcâ‚™Aux_id hpâ‚ a ha]
    exact âŸ¨a, rflâŸ©
  Â· have : Continuous (Unitization.fst (R := ğ•œ) (A := A)) :=
      Unitization.uniformEquivProd.continuous.fst
    simp only [NonUnitalStarAlgHom.coe_range]
    convert IsClosed.preimage this (isClosed_singleton (x := 0))
    aesop

variable [CStarRing A]

include hpâ‚ in
open Unitization NonUnitalStarAlgHom in
theorem RCLike.nonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus ğ•œ (p : A â†’ Prop) where
  predicate_zero := by
    rw [â† hpâ‚, Unitization.inr_zero ğ•œ]
    exact cfc_predicate_zero ğ•œ
  exists_cfc_of_predicate a ha := by
    let Ïˆ : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] A := comp (inrRangeEquiv ğ•œ A).symm <|
      codRestrict (cfcâ‚™Aux hpâ‚ a ha) _ (cfcâ‚™Aux_mem_range_inr hpâ‚ a ha)
    have coe_Ïˆ (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) : Ïˆ f = cfcâ‚™Aux hpâ‚ a ha f :=
      congr_arg Subtype.val <| (inrRangeEquiv ğ•œ A).apply_symm_apply
        âŸ¨cfcâ‚™Aux hpâ‚ a ha f, cfcâ‚™Aux_mem_range_inr hpâ‚ a ha fâŸ©
    refine âŸ¨Ïˆ, ?isClosedEmbedding, ?map_id, fun f â†¦ ?map_spec, fun f â†¦ ?isStarNormalâŸ©
    case isClosedEmbedding =>
      apply isometry_inr (ğ•œ := ğ•œ) (A := A) |>.isClosedEmbedding |>.of_comp_iff.mp
      have : inr âˆ˜ Ïˆ = cfcâ‚™Aux hpâ‚ a ha := by ext1; rw [Function.comp_apply, coe_Ïˆ]
      exact this â–¸ isClosedEmbedding_cfcâ‚™Aux hpâ‚ a ha
    case map_id => exact inr_injective (R := ğ•œ) <| coe_Ïˆ _ â–¸ cfcâ‚™Aux_id hpâ‚ a ha
    case map_spec =>
      exact quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ (Ïˆ f) â–¸ coe_Ïˆ _ â–¸ spec_cfcâ‚™Aux hpâ‚ a ha f
    case isStarNormal => exact hpâ‚.mp <| coe_Ïˆ _ â–¸ cfcHom_predicate (R := ğ•œ) (hpâ‚.mpr ha) _

end RCLike

/-!
### Continuous functional calculus for normal elements
-/

section Normal

instance IsStarNormal.instContinuousFunctionalCalculus {A : Type*} [CStarAlgebra A] :
    ContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop) where
  predicate_zero := isStarNormal_zero
  spectrum_nonempty a _ := spectrum.nonempty a
  exists_cfc_of_predicate a ha := by
    refine âŸ¨(StarAlgebra.elemental â„‚ a).subtype.comp <| continuousFunctionalCalculus a,
      ?hom_isClosedEmbedding, ?hom_id, ?hom_map_spectrum, ?predicate_homâŸ©
    case hom_isClosedEmbedding =>
      exact Isometry.isClosedEmbedding <|
        isometry_subtype_coe.comp <| StarAlgEquiv.isometry (continuousFunctionalCalculus a)
    case hom_id => exact congr_arg Subtype.val <| continuousFunctionalCalculus_map_id a
    case hom_map_spectrum =>
      intro f
      simp only [StarAlgHom.comp_apply, StarAlgHom.coe_coe, StarSubalgebra.coe_subtype]
      rw [â† StarSubalgebra.spectrum_eq (hS := StarAlgebra.elemental.isClosed â„‚ a),
        AlgEquiv.spectrum_eq (continuousFunctionalCalculus a), ContinuousMap.spectrum_eq_range]
    case predicate_hom => exact fun f â†¦ âŸ¨by rw [â† map_star]; exact Commute.all (star f) f |>.map _âŸ©

lemma cfcHom_eq_of_isStarNormal {A : Type*} [CStarAlgebra A] (a : A) [ha : IsStarNormal a] :
    cfcHom ha = (StarAlgebra.elemental â„‚ a).subtype.comp (continuousFunctionalCalculus a) := by
  refine cfcHom_eq_of_continuous_of_map_id ha _ ?_ ?_
  Â· exact continuous_subtype_val.comp <|
      (StarAlgEquiv.isometry (continuousFunctionalCalculus a)).continuous
  Â· simp [continuousFunctionalCalculus_map_id a]

instance IsStarNormal.instNonUnitalContinuousFunctionalCalculus {A : Type*}
    [NonUnitalCStarAlgebra A] : NonUnitalContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop) :=
  RCLike.nonUnitalContinuousFunctionalCalculus Unitization.isStarNormal_inr

open Unitization CStarAlgebra in
lemma inr_comp_cfcâ‚™Hom_eq_cfcâ‚™Aux {A : Type*} [NonUnitalCStarAlgebra A] (a : A)
    [ha : IsStarNormal a] : (inrNonUnitalStarAlgHom â„‚ A).comp (cfcâ‚™Hom ha) =
      cfcâ‚™Aux (isStarNormal_inr (R := â„‚) (A := A)) a ha := by
  have h (a : A) := isStarNormal_inr (R := â„‚) (A := A) (a := a)
  refine @UniqueNonUnitalContinuousFunctionalCalculus.eq_of_continuous_of_map_id
    _ _ _ _ _ _ _ _ _ _ _ inferInstance inferInstance _ (Ïƒâ‚™ â„‚ a) _ _ rfl _ _ ?_ ?_ ?_
  Â· show Continuous (fun f â†¦ (cfcâ‚™Hom ha f : AâºÂ¹)); fun_prop
  Â· exact isClosedEmbedding_cfcâ‚™Aux @(h) a ha |>.continuous
  Â· trans (a : AâºÂ¹)
    Â· congrm(inr $(cfcâ‚™Hom_id ha))
    Â· exact cfcâ‚™Aux_id @(h) a ha |>.symm

end Normal

/-!
### Continuous functional calculus for selfadjoint elements
-/

section SelfAdjointNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module â„‚ A]
  [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A]
  [NonUnitalContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]

/-- An element in a non-unital Câ‹†-algebra is selfadjoint if and only if it is normal and its
quasispectrum is contained in `â„`. -/
lemma isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts {a : A} :
    IsSelfAdjoint a â†” IsStarNormal a âˆ§ QuasispectrumRestricts a Complex.reCLM := by
  refine âŸ¨fun ha â†¦ âŸ¨ha.isStarNormal, âŸ¨fun x hx â†¦ ?_, Complex.ofReal_reâŸ©âŸ©, ?_âŸ©
  Â· have := eqOn_of_cfcâ‚™_eq_cfcâ‚™ <|
      (cfcâ‚™_star (id : â„‚ â†’ â„‚) a).symm â–¸ (cfcâ‚™_id â„‚ a).symm â–¸ ha.star_eq
    exact Complex.conj_eq_iff_re.mp (by simpa using this hx)
  Â· rintro âŸ¨haâ‚, haâ‚‚âŸ©
    rw [isSelfAdjoint_iff]
    nth_rw 2 [â† cfcâ‚™_id â„‚ a]
    rw [â† cfcâ‚™_star_id a (R := â„‚)]
    refine cfcâ‚™_congr fun x hx â†¦ ?_
    obtain âŸ¨x, -, rflâŸ© := haâ‚‚.algebraMap_image.symm â–¸ hx
    exact Complex.conj_ofReal _

alias âŸ¨IsSelfAdjoint.quasispectrumRestricts, _âŸ© :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts

/-- A normal element whose `â„‚`-quasispectrum is contained in `â„` is selfadjoint. -/
lemma QuasispectrumRestricts.isSelfAdjoint (a : A) (ha : QuasispectrumRestricts a Complex.reCLM)
    [IsStarNormal a] : IsSelfAdjoint a :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts.mpr âŸ¨â€¹_â€º, haâŸ©

instance IsSelfAdjoint.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop) :=
  QuasispectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ â†¦ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts)

end SelfAdjointNonUnital

section SelfAdjointUnital


variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra â„‚ A]
  [ContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]

/-
TODO: REMOVE
this is a duplicate of `isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts`, because of
`abbrev SpectrumRestricts := QuasispectrumRestricts` but we first need unital-to-non-unital
instance)
-/
/-- An element in a Câ‹†-algebra is selfadjoint if and only if it is normal and its spectrum is
contained in `â„`. -/
lemma isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts {a : A} :
    IsSelfAdjoint a â†” IsStarNormal a âˆ§ SpectrumRestricts a Complex.reCLM := by
  refine âŸ¨fun ha â†¦ âŸ¨ha.isStarNormal, .of_rightInvOn Complex.ofReal_re fun x hx â†¦ ?_âŸ©, ?_âŸ©
  Â· have := eqOn_of_cfc_eq_cfc <| (cfc_star (id : â„‚ â†’ â„‚) a).symm â–¸ (cfc_id â„‚ a).symm â–¸ ha.star_eq
    exact Complex.conj_eq_iff_re.mp (by simpa using this hx)
  Â· rintro âŸ¨haâ‚, haâ‚‚âŸ©
    rw [isSelfAdjoint_iff]
    nth_rw 2 [â† cfc_id â„‚ a]
    rw [â† cfc_star_id a (R := â„‚)]
    refine cfc_congr fun x hx â†¦ ?_
    obtain âŸ¨x, -, rflâŸ© := haâ‚‚.algebraMap_image.symm â–¸ hx
    exact Complex.conj_ofReal _

-- TODO: REMOVE (duplicate; see comment on `isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts`)
lemma IsSelfAdjoint.spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts a Complex.reCLM :=
  isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts.mp ha |>.right

-- TODO: REMOVE (duplicate; see comment on `isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts`)
/-- A normal element whose `â„‚`-spectrum is contained in `â„` is selfadjoint. -/
lemma SpectrumRestricts.isSelfAdjoint (a : A) (ha : SpectrumRestricts a Complex.reCLM)
    [IsStarNormal a] : IsSelfAdjoint a :=
  isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts.mpr âŸ¨â€¹_â€º, haâŸ©

instance IsSelfAdjoint.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop) :=
  SpectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ â†¦ isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts)

lemma IsSelfAdjoint.spectrum_nonempty {A : Type*} [Ring A] [StarRing A]
    [TopologicalSpace A] [Algebra â„ A] [ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
    [Nontrivial A] {a : A} (ha : IsSelfAdjoint a) : (Ïƒ â„ a).Nonempty :=
  CFC.spectrum_nonempty â„ a ha

end SelfAdjointUnital

/-!
### Continuous functional calculus for nonnegative elements
-/

section Nonneg

lemma CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts {A : Type*} [NonUnitalRing A]
    [StarRing A] [TopologicalSpace A] [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A ]
    [NonUnitalContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
    {a : A} (haâ‚ : IsSelfAdjoint a) (haâ‚‚ : QuasispectrumRestricts a ContinuousMap.realToNNReal) :
    âˆƒ x : A, IsSelfAdjoint x âˆ§ QuasispectrumRestricts x ContinuousMap.realToNNReal âˆ§ x * x = a := by
  use cfcâ‚™ Real.sqrt a, cfcâ‚™_predicate Real.sqrt a
  constructor
  Â· simpa only [QuasispectrumRestricts.nnreal_iff, cfcâ‚™_map_quasispectrum Real.sqrt a,
      Set.mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
        using fun x _ â†¦ Real.sqrt_nonneg x
  Â· rw [â† cfcâ‚™_mul ..]
    nth_rw 2 [â† cfcâ‚™_id â„ a]
    apply cfcâ‚™_congr fun x hx â†¦ ?_
    rw [QuasispectrumRestricts.nnreal_iff] at haâ‚‚
    apply haâ‚‚ x at hx
    simp [â† sq, Real.sq_sqrt hx]

variable {A : Type*} [NonUnitalRing A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
variable [TopologicalSpace A] [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
variable [NonUnitalContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
variable [NonnegSpectrumClass â„ A]

lemma nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts {a : A} :
    0 â‰¤ a â†” IsSelfAdjoint a âˆ§ QuasispectrumRestricts a ContinuousMap.realToNNReal := by
  refine âŸ¨fun ha â†¦ âŸ¨.of_nonneg ha, .nnreal_of_nonneg haâŸ©, ?_âŸ©
  rintro âŸ¨haâ‚, haâ‚‚âŸ©
  obtain âŸ¨x, hx, -, rflâŸ© := CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts haâ‚ haâ‚‚
  simpa [sq, hx.star_eq] using star_mul_self_nonneg x

open NNReal in
instance Nonneg.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus â„â‰¥0 (fun x : A â†¦ 0 â‰¤ x) :=
  QuasispectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl
    (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

open NNReal in
lemma NNReal.spectrum_nonempty {A : Type*} [Ring A] [StarRing A] [PartialOrder A]
    [TopologicalSpace A] [Algebra â„â‰¥0 A] [ContinuousFunctionalCalculus â„â‰¥0 (fun x : A â†¦ 0 â‰¤ x)]
    [Nontrivial A] {a : A} (ha : 0 â‰¤ a) : (spectrum â„â‰¥0 a).Nonempty :=
  CFC.spectrum_nonempty â„â‰¥0 a ha

end Nonneg


section Nonneg

-- TODO: REMOVE (duplicate; see comment on `isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts`)
lemma CFC.exists_sqrt_of_isSelfAdjoint_of_spectrumRestricts {A : Type*} [Ring A] [StarRing A]
    [TopologicalSpace A] [Algebra â„ A] [ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
    {a : A} (haâ‚ : IsSelfAdjoint a) (haâ‚‚ : SpectrumRestricts a ContinuousMap.realToNNReal) :
    âˆƒ x : A, IsSelfAdjoint x âˆ§ SpectrumRestricts x ContinuousMap.realToNNReal âˆ§ x ^ 2 = a := by
  use cfc Real.sqrt a, cfc_predicate Real.sqrt a
  constructor
  Â· simpa only [SpectrumRestricts.nnreal_iff, cfc_map_spectrum Real.sqrt a, Set.mem_image,
      forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚] using fun x _ â†¦ Real.sqrt_nonneg x
  Â· rw [â† cfc_pow ..]
    nth_rw 2 [â† cfc_id â„ a]
    apply cfc_congr fun x hx â†¦ ?_
    rw [SpectrumRestricts.nnreal_iff] at haâ‚‚
    apply haâ‚‚ x at hx
    simp [Real.sq_sqrt hx]

variable {A : Type*} [Ring A] [PartialOrder A] [StarRing A] [StarOrderedRing A] [TopologicalSpace A]
variable [Algebra â„ A] [ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
variable [NonnegSpectrumClass â„ A]

-- TODO: REMOVE (duplicate; see comment on `isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts`)
lemma nonneg_iff_isSelfAdjoint_and_spectrumRestricts {a : A} :
    0 â‰¤ a â†” IsSelfAdjoint a âˆ§ SpectrumRestricts a ContinuousMap.realToNNReal := by
  refine âŸ¨fun ha â†¦ âŸ¨.of_nonneg ha, .nnreal_of_nonneg haâŸ©, ?_âŸ©
  rintro âŸ¨haâ‚, haâ‚‚âŸ©
  obtain âŸ¨x, hx, -, rflâŸ© := CFC.exists_sqrt_of_isSelfAdjoint_of_spectrumRestricts haâ‚ haâ‚‚
  simpa [sq, hx.star_eq] using star_mul_self_nonneg x

open NNReal in
instance Nonneg.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„â‰¥0 (fun x : A â†¦ 0 â‰¤ x) :=
  SpectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_spectrumRestricts)

end Nonneg

/-!
### The spectrum of a nonnegative element is nonnegative
-/

section SpectrumRestricts

open NNReal ENNReal

variable {A : Type*} [CStarAlgebra A]

lemma SpectrumRestricts.nnreal_iff_nnnorm {a : A} {t : â„â‰¥0} (ha : IsSelfAdjoint a) (ht : â€–aâ€–â‚Š â‰¤ t) :
    SpectrumRestricts a ContinuousMap.realToNNReal â†” â€–algebraMap â„ A t - aâ€–â‚Š â‰¤ t := by
  have : IsSelfAdjoint (algebraMap â„ A t - a) := IsSelfAdjoint.algebraMap A (.all (t : â„)) |>.sub ha
  rw [â† ENNReal.coe_le_coe, â† IsSelfAdjoint.spectralRadius_eq_nnnorm,
    â† SpectrumRestricts.spectralRadius_eq (f := Complex.reCLM)] at ht âŠ¢
  Â· exact SpectrumRestricts.nnreal_iff_spectralRadius_le ht
  all_goals
    try apply IsSelfAdjoint.spectrumRestricts
    assumption

lemma SpectrumRestricts.nnreal_add {a b : A} (haâ‚ : IsSelfAdjoint a)
    (hbâ‚ : IsSelfAdjoint b) (haâ‚‚ : SpectrumRestricts a ContinuousMap.realToNNReal)
    (hbâ‚‚ : SpectrumRestricts b ContinuousMap.realToNNReal) :
    SpectrumRestricts (a + b) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff_nnnorm (haâ‚.add hbâ‚) (nnnorm_add_le a b), NNReal.coe_add,
    map_add, add_sub_add_comm]
  refine nnnorm_add_le _ _ |>.trans ?_
  gcongr
  all_goals rw [â† SpectrumRestricts.nnreal_iff_nnnorm] <;> first | rfl | assumption

lemma IsSelfAdjoint.sq_spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts (a ^ 2) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff, â† cfc_id (R := â„) a, â† cfc_pow .., cfc_map_spectrum ..]
  rintro - âŸ¨x, -, rflâŸ©
  exact sq_nonneg x

open ComplexStarModule

lemma SpectrumRestricts.eq_zero_of_neg {a : A} (ha : IsSelfAdjoint a)
    (haâ‚ : SpectrumRestricts a ContinuousMap.realToNNReal)
    (haâ‚‚ : SpectrumRestricts (-a) ContinuousMap.realToNNReal) :
    a = 0 := by
  nontriviality A
  rw [SpectrumRestricts.nnreal_iff] at haâ‚ haâ‚‚
  apply CFC.eq_zero_of_spectrum_subset_zero (R := â„) a
  rw [Set.subset_singleton_iff]
  simp only [â† spectrum.neg_eq, Set.mem_neg] at haâ‚‚
  peel haâ‚ with x hx _
  linarith [haâ‚‚ (-x) ((neg_neg x).symm â–¸ hx)]

lemma SpectrumRestricts.smul_of_nonneg {A : Type*} [Ring A] [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : â„} (hr : 0 â‰¤ r) :
    SpectrumRestricts (r â€¢ a) ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff] at ha âŠ¢
  nontriviality A
  intro x hx
  by_cases hr' : r = 0
  Â· simp [hr'] at hx âŠ¢
    exact hx.symm.le
  Â· lift r to â„Ë£ using IsUnit.mk0 r hr'
    rw [â† Units.smul_def, spectrum.unit_smul_eq_smul, Set.mem_smul_set_iff_inv_smul_mem] at hx
    refine le_of_smul_le_smul_left ?_ (inv_pos.mpr <| lt_of_le_of_ne hr <| ne_comm.mpr hr')
    simpa [Units.smul_def] using ha _ hx

lemma spectrum_star_mul_self_nonneg {b : A} : âˆ€ x âˆˆ spectrum â„ (star b * b), 0 â‰¤ x := by
  set a := star b * b
  have a_def : a = star b * b := rfl
  let a_neg : A := cfc (fun x â†¦ (- ContinuousMap.id â„ âŠ” 0) x) a
  set c := b * a_neg
  have h_eq_a_neg : - (star c * c) = a_neg ^ 3 := by
    simp only [c, a_neg, star_mul]
    rw [â† mul_assoc, mul_assoc _ _ b, â† cfc_star, â† cfc_id' â„ (star b * b), a_def, â† neg_mul]
    rw [â† cfc_mul _ _ (star b * b) (by simp; fun_prop), neg_mul]
    simp only [ContinuousMap.coe_neg, ContinuousMap.coe_id, Pi.sup_apply, Pi.neg_apply,
      star_trivial]
    rw [â† cfc_mul .., â† cfc_neg .., â† cfc_pow ..]
    congr
    ext x
    by_cases hx : x â‰¤ 0
    Â· rw [â† neg_nonneg] at hx
      simp [sup_eq_left.mpr hx, pow_succ]
    Â· rw [not_le, â† neg_neg_iff_pos] at hx
      simp [sup_eq_right.mpr hx.le]
  have h_c_specâ‚€ : SpectrumRestricts (- (star c * c)) (ContinuousMap.realToNNReal Â·) := by
    simp only [SpectrumRestricts.nnreal_iff, h_eq_a_neg]
    rw [â† cfc_pow _ _ (ha := .star_mul_self b)]
    simp only [a, cfc_map_spectrum (R := â„) (fun x => (-ContinuousMap.id â„ âŠ” 0) x ^ 3) (star b * b)]
    rintro - âŸ¨x, -, rflâŸ©
    simp
  have c_eq := star_mul_self_add_self_mul_star c
  rw [â† eq_sub_iff_add_eq', sub_eq_add_neg, â† sq, â† sq] at c_eq
  have h_c_specâ‚ : SpectrumRestricts (c * star c) ContinuousMap.realToNNReal := by
    rw [c_eq]
    refine SpectrumRestricts.nnreal_add ?_ ?_ ?_ h_c_specâ‚€
    Â· exact IsSelfAdjoint.smul (by rfl) <| ((â„œ c).prop.pow 2).add ((â„‘ c).prop.pow 2)
    Â· exact (IsSelfAdjoint.star_mul_self c).neg
    Â· rw [â† Nat.cast_smul_eq_nsmul â„]
      refine (â„œ c).2.sq_spectrumRestricts.nnreal_add ((â„œ c).2.pow 2) ((â„‘ c).2.pow 2)
        (â„‘ c).2.sq_spectrumRestricts |>.smul_of_nonneg <| by norm_num
  have h_c_specâ‚‚ : SpectrumRestricts (star c * c) ContinuousMap.realToNNReal := by
    rw [SpectrumRestricts.nnreal_iff] at h_c_specâ‚ âŠ¢
    intro x hx
    replace hx := Set.subset_diff_union _ {(0 : â„)} hx
    rw [spectrum.nonzero_mul_eq_swap_mul, Set.diff_union_self, Set.union_singleton,
      Set.mem_insert_iff] at hx
    obtain (rfl | hx) := hx
    exacts [le_rfl, h_c_specâ‚ x hx]
  rw [h_c_specâ‚‚.eq_zero_of_neg (.star_mul_self c) h_c_specâ‚€, neg_zero] at h_eq_a_neg
  simp only [a_neg] at h_eq_a_neg
  rw [â† cfc_pow _ _ (ha := .star_mul_self b), â† cfc_zero a (R := â„)] at h_eq_a_neg
  intro x hx
  by_contra! hx'
  rw [â† neg_pos] at hx'
  apply (pow_pos hx' 3).ne
  have h_eqOn := eqOn_of_cfc_eq_cfc (ha := IsSelfAdjoint.star_mul_self b) h_eq_a_neg
  simpa [sup_eq_left.mpr hx'.le] using h_eqOn hx

lemma IsSelfAdjoint.coe_mem_spectrum_complex {A : Type*} [TopologicalSpace A] [Ring A]
    [StarRing A] [Algebra â„‚ A] [ContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]
    {a : A} {x : â„} (ha : IsSelfAdjoint a := by cfc_tac) :
    (x : â„‚) âˆˆ spectrum â„‚ a â†” x âˆˆ spectrum â„ a := by
  simp [â† ha.spectrumRestricts.algebraMap_image]

end SpectrumRestricts

section NonnegSpectrumClass

variable {A : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

instance CStarAlgebra.instNonnegSpectrumClass : NonnegSpectrumClass â„ A :=
  .of_spectrum_nonneg fun a ha â†¦ by
    rw [StarOrderedRing.nonneg_iff] at ha
    induction ha using AddSubmonoid.closure_induction with
    | mem x hx =>
      obtain âŸ¨b, rflâŸ© := hx
      exact spectrum_star_mul_self_nonneg
    | one =>
      nontriviality A
      simp
    | mul x y x_mem y_mem hx hy =>
      rw [â† SpectrumRestricts.nnreal_iff] at hx hy âŠ¢
      rw [â† StarOrderedRing.nonneg_iff] at x_mem y_mem
      exact hx.nnreal_add (.of_nonneg x_mem) (.of_nonneg y_mem) hy

open ComplexOrder in
instance CStarAlgebra.instNonnegSpectrumClassComplexUnital : NonnegSpectrumClass â„‚ A where
  quasispectrum_nonneg_of_nonneg a ha x := by
    rw [mem_quasispectrum_iff]
    refine (Or.elim Â· ge_of_eq fun hx â†¦ ?_)
    obtain âŸ¨y, hy, rflâŸ© := (IsSelfAdjoint.of_nonneg ha).spectrumRestricts.algebraMap_image â–¸ hx
    simpa using spectrum_nonneg_of_nonneg ha hy

end NonnegSpectrumClass

section SpectralOrder

variable (A : Type*) [CStarAlgebra A]

/-- The partial order on a unital Câ‹†-algebra defined by `x â‰¤ y` if and only if `y - x` is
selfadjoint and has nonnegative spectrum.

This is not declared as an instance because one may already have a partial order with better
definitional properties. However, it can be useful to invoke this as an instance in proofs. -/
@[reducible]
def CStarAlgebra.spectralOrder : PartialOrder A where
  le x y := IsSelfAdjoint (y - x) âˆ§ SpectrumRestricts (y - x) ContinuousMap.realToNNReal
  le_refl := by
    simp only [sub_self, IsSelfAdjoint.zero, true_and, forall_const]
    rw [SpectrumRestricts.nnreal_iff]
    nontriviality A
    simp
  le_antisymm x y hxy hyx := by
    rw [â† sub_eq_zero]
    exact hyx.2.eq_zero_of_neg hyx.1 (neg_sub x y â–¸ hxy.2)
  le_trans x y z hxy hyz :=
    âŸ¨by simpa using hyz.1.add hxy.1, by simpa using hyz.2.nnreal_add hyz.1 hxy.1 hxy.2âŸ©

/-- The `CStarAlgebra.spectralOrder` on a unital Câ‹†-algebra is a `StarOrderedRing`. -/
lemma CStarAlgebra.spectralOrderedRing : @StarOrderedRing A _ (CStarAlgebra.spectralOrder A) _ :=
  let _ := CStarAlgebra.spectralOrder A
  { le_iff := by
      intro x y
      constructor
      Â· intro h
        obtain âŸ¨s, hsâ‚, _, hsâ‚‚âŸ© := CFC.exists_sqrt_of_isSelfAdjoint_of_spectrumRestricts h.1 h.2
        refine âŸ¨s ^ 2, ?_, by rwa [eq_sub_iff_add_eq', eq_comm] at hsâ‚‚âŸ©
        exact AddSubmonoid.subset_closure âŸ¨s, by simp [hsâ‚.star_eq, sq]âŸ©
      Â· rintro âŸ¨p, hp, rflâŸ©
        suffices IsSelfAdjoint p âˆ§ SpectrumRestricts p ContinuousMap.realToNNReal from
          âŸ¨by simpa using this.1, by simpa using this.2âŸ©
        induction hp using AddSubmonoid.closure_induction with
        | mem x hx =>
          obtain âŸ¨s, rflâŸ© := hx
          refine âŸ¨IsSelfAdjoint.star_mul_self s, ?_âŸ©
          rw [SpectrumRestricts.nnreal_iff]
          exact spectrum_star_mul_self_nonneg
        | one =>
          rw [SpectrumRestricts.nnreal_iff]
          nontriviality A
          simp
        | mul x y _ _ hx hy =>
          exact âŸ¨hx.1.add hy.1, hx.2.nnreal_add hx.1 hy.1 hy.2âŸ© }

end SpectralOrder

section NonnegSpectrumClass

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

open scoped CStarAlgebra in
instance CStarAlgebra.instNonnegSpectrumClass' : NonnegSpectrumClass â„ A where
  quasispectrum_nonneg_of_nonneg a ha := by
    rw [Unitization.quasispectrum_eq_spectrum_inr' _ â„‚]
    -- should this actually be an instance on the `Unitization`? (probably scoped)
    let _ := CStarAlgebra.spectralOrder AâºÂ¹
    have := CStarAlgebra.spectralOrderedRing AâºÂ¹
    apply spectrum_nonneg_of_nonneg
    rw [StarOrderedRing.nonneg_iff] at ha âŠ¢
    have := AddSubmonoid.mem_map_of_mem (Unitization.inrNonUnitalStarAlgHom â„‚ A) ha
    rw [AddMonoidHom.map_mclosure, â† Set.range_comp] at this
    apply AddSubmonoid.closure_mono ?_ this
    rintro _ âŸ¨s, rflâŸ©
    exact âŸ¨s, by simpâŸ©

end NonnegSpectrumClass

/-!
### The restriction of a continuous functional calculus is equal to the original one
-/
section RealEqComplex

variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra â„‚ A]
  [ContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]
  [UniqueContinuousFunctionalCalculus â„ A]

lemma cfcHom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfcHom ha =
      ha.spectrumRestricts.starAlgHom (R := â„) (S := â„‚)
        (cfcHom ha.isStarNormal) (f := Complex.reCLM) :=
  ha.spectrumRestricts.cfcHom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfc_real_eq_complex {a : A} (f : â„ â†’ â„) (ha : IsSelfAdjoint a := by cfc_tac)  :
    cfc f a = cfc (fun x â†¦ f x.re : â„‚ â†’ â„‚) a := by
  replace ha : IsSelfAdjoint a := ha -- hack to avoid issues caused by autoParam
  exact ha.spectrumRestricts.cfc_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

end RealEqComplex

section RealEqComplexNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module â„‚ A]
  [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A]
  [NonUnitalContinuousFunctionalCalculus â„‚ (IsStarNormal : A â†’ Prop)]
  [UniqueNonUnitalContinuousFunctionalCalculus â„ A]

lemma cfcâ‚™Hom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfcâ‚™Hom ha = (ha.quasispectrumRestricts.2).nonUnitalStarAlgHom (cfcâ‚™Hom ha.isStarNormal)
      (R := â„) (S := â„‚) (f := Complex.reCLM) :=
  ha.quasispectrumRestricts.2.cfcâ‚™Hom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfcâ‚™_real_eq_complex {a : A} (f : â„ â†’ â„) (ha : IsSelfAdjoint a := by cfc_tac)  :
    cfcâ‚™ f a = cfcâ‚™ (fun x â†¦ f x.re : â„‚ â†’ â„‚) a := by
  replace ha : IsSelfAdjoint a := ha -- hack to avoid issues caused by autoParam
  exact ha.quasispectrumRestricts.2.cfcâ‚™_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

end RealEqComplexNonUnital

section NNRealEqReal

open NNReal

variable {A : Type*} [TopologicalSpace A] [Ring A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Algebra â„ A] [TopologicalRing A]
  [ContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
  [ContinuousFunctionalCalculus â„â‰¥0 ((0 : A) â‰¤ Â·)] [UniqueContinuousFunctionalCalculus â„ A]
  [NonnegSpectrumClass â„ A]

lemma cfcHom_nnreal_eq_restrict {a : A} (ha : 0 â‰¤ a) :
    cfcHom ha = (SpectrumRestricts.nnreal_of_nonneg ha).starAlgHom
      (cfcHom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfcHom_eq_restrict _ isUniformEmbedding_subtype_val

lemma cfc_nnreal_eq_real {a : A} (f : â„â‰¥0 â†’ â„â‰¥0) (ha : 0 â‰¤ a := by cfc_tac)  :
    cfc f a = cfc (fun x â†¦ f x.toNNReal : â„ â†’ â„) a := by
  replace ha : 0 â‰¤ a := ha -- hack to avoid issues caused by autoParam
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfc_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

end NNRealEqReal

section NNRealEqRealNonUnital

open NNReal

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Module â„ A] [TopologicalRing A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
  [NonUnitalContinuousFunctionalCalculus â„ (IsSelfAdjoint : A â†’ Prop)]
  [NonUnitalContinuousFunctionalCalculus â„â‰¥0 ((0 : A) â‰¤ Â·)]
  [UniqueNonUnitalContinuousFunctionalCalculus â„ A]
  [NonnegSpectrumClass â„ A]

lemma cfcâ‚™Hom_nnreal_eq_restrict {a : A} (ha : 0 â‰¤ a) :
    cfcâ‚™Hom ha = (QuasispectrumRestricts.nnreal_of_nonneg ha).nonUnitalStarAlgHom
      (cfcâ‚™Hom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfcâ‚™Hom_eq_restrict _
    isUniformEmbedding_subtype_val

lemma cfcâ‚™_nnreal_eq_real {a : A} (f : â„â‰¥0 â†’ â„â‰¥0) (ha : 0 â‰¤ a := by cfc_tac)  :
    cfcâ‚™ f a = cfcâ‚™ (fun x â†¦ f x.toNNReal : â„ â†’ â„) a := by
  replace ha : 0 â‰¤ a := ha -- hack to avoid issues caused by autoParam
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfcâ‚™_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

end NNRealEqRealNonUnital

section cfc_inr

open CStarAlgebra

variable {A : Type*} [NonUnitalCStarAlgebra A]

open scoped NonUnitalContinuousFunctionalCalculus in
/-- This lemma requires a lot from type class synthesis, and so one should instead favor the bespoke
versions for `â„â‰¥0`, `â„`, and `â„‚`. -/
lemma Unitization.cfcâ‚™_eq_cfc_inr {R : Type*} [Semifield R] [StarRing R] [MetricSpace R]
    [TopologicalSemiring R] [ContinuousStar R] [Module R A] [IsScalarTower R A A]
    [SMulCommClass R A A] [CompleteSpace R] [Algebra R â„‚] [IsScalarTower R â„‚ A]
    {p : A â†’ Prop} {p' : AâºÂ¹ â†’ Prop} [NonUnitalContinuousFunctionalCalculus R p]
    [ContinuousFunctionalCalculus R p']
    [UniqueNonUnitalContinuousFunctionalCalculus R (Unitization â„‚ A)]
    (hp : âˆ€ {a : A}, p' (a : AâºÂ¹) â†” p a) (a : A) (f : R â†’ R) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac) :
    cfcâ‚™ f a = cfc f (a : AâºÂ¹) := by
  by_cases h : ContinuousOn f (Ïƒâ‚™ R a) âˆ§ p a
  Â· obtain âŸ¨hf, haâŸ© := h
    rw [â† cfcâ‚™_eq_cfc (quasispectrum_inr_eq R â„‚ a â–¸ hf)]
    exact (inrNonUnitalStarAlgHom â„‚ A).map_cfcâ‚™ f a
  Â· obtain (hf | ha) := not_and_or.mp h
    Â· rw [cfcâ‚™_apply_of_not_continuousOn a hf, inr_zero,
        cfc_apply_of_not_continuousOn _ (quasispectrum_eq_spectrum_inr' R â„‚ a â–¸ hf)]
    Â· rw [cfcâ‚™_apply_of_not_predicate a ha, inr_zero,
        cfc_apply_of_not_predicate _ (not_iff_not.mpr hp |>.mpr ha)]

lemma Unitization.complex_cfcâ‚™_eq_cfc_inr (a : A) (f : â„‚ â†’ â„‚) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac) :
    cfcâ‚™ f a = cfc f (a : AâºÂ¹) :=
  Unitization.cfcâ‚™_eq_cfc_inr isStarNormal_inr ..

/-- note: the version for `â„â‰¥0`, `Unization.nnreal_cfcâ‚™_eq_cfc_inr`, can be found in
`Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Order` -/
lemma Unitization.real_cfcâ‚™_eq_cfc_inr (a : A) (f : â„ â†’ â„) (hfâ‚€ : f 0 = 0 := by cfc_zero_tac) :
    cfcâ‚™ f a = cfc f (a : AâºÂ¹) :=
  Unitization.cfcâ‚™_eq_cfc_inr isSelfAdjoint_inr ..

end cfc_inr

end
