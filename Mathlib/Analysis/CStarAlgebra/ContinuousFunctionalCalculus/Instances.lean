/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.Complex.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Restrict
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.Normed.Algebra.Spectrum
import Mathlib.Analysis.RCLike.Lemmas

/-! # Instances of the continuous functional calculus

## Main theorems

* `IsSelfAdjoint.instContinuousFunctionalCalculus`: the continuous functional calculus for
  selfadjoint elements in a `â„‚`-algebra with a continuous functional calculus for normal elements
  and where every element has compact spectrum. In particular, this includes unital Câ‹†-algebras
  over `â„‚`.
* `Nonneg.instContinuousFunctionalCalculus`: the continuous functional calculus for nonnegative
  elements in an `â„`-algebra with a continuous functional calculus for selfadjoint elements,
  where every element has compact spectrum, and where nonnegative elements have nonnegative
  spectrum. In particular, this includes unital Câ‹†-algebras over `â„`.

## Tags

continuous functional calculus, normal, selfadjoint
-/

open Topology

noncomputable section

local notation "Ïƒâ‚™" => quasispectrum
local notation "Ïƒ" => spectrum

/-!
### Pull back a non-unital instance from a unital one on the unitization
-/

section RCLike

variable {ğ•œ A : Type*} [RCLike ğ•œ] [NonUnitalNormedRing A] [StarRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
variable [StarModule ğ•œ A] {p : A â†’ Prop} {pâ‚ : Unitization ğ•œ A â†’ Prop}

local postfix:max "âºÂ¹" => Unitization ğ•œ

variable (hpâ‚ : âˆ€ {x : A}, pâ‚ x â†” p x) (a : A) (ha : p a)
variable [ContinuousFunctionalCalculus ğ•œ (Unitization ğ•œ A) pâ‚]

open scoped ContinuousMapZero


open Unitization in
/--
This is an auxiliary definition used for constructing an instance of the non-unital continuous
functional calculus given a instance of the unital one on the unitization.

This is the natural non-unital star homomorphism obtained from the chain
```lean
calc
  C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] C(Ïƒâ‚™ ğ•œ a, ğ•œ) := ContinuousMapZero.toContinuousMapHom
  _             â‰ƒâ‹†[ğ•œ] C(Ïƒ ğ•œ (â†‘a : AâºÂ¹), ğ•œ) := Homeomorph.compStarAlgEquiv'
  _             â†’â‹†â‚[ğ•œ] AâºÂ¹ := cfcHom
```
This range of this map is contained in the range of `(â†‘) : A â†’ AâºÂ¹` (see `cfcâ‚™Aux_mem_range_inr`),
and so we may restrict it to `A` to get the necessary homomorphism for the non-unital continuous
functional calculus.
-/
noncomputable def cfcâ‚™Aux : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] AâºÂ¹ :=
  (cfcHom (R := ğ•œ) (hpâ‚.mpr ha) : C(Ïƒ ğ•œ (a : AâºÂ¹), ğ•œ) â†’â‹†â‚™â‚[ğ•œ] AâºÂ¹) |>.comp
    (Homeomorph.compStarAlgEquiv' ğ•œ ğ•œ <| .setCongr <| (quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a).symm)
    |>.comp ContinuousMapZero.toContinuousMapHom

lemma cfcâ‚™Aux_id : cfcâ‚™Aux hpâ‚ a ha (.id _) = a := cfcHom_id (hpâ‚.mpr ha)

open Unitization in
lemma isClosedEmbedding_cfcâ‚™Aux : IsClosedEmbedding (cfcâ‚™Aux hpâ‚ a ha) := by
  simp only [cfcâ‚™Aux, NonUnitalStarAlgHom.coe_comp]
  refine ((cfcHom_isClosedEmbedding (hpâ‚.mpr ha)).comp ?_).comp
    ContinuousMapZero.isClosedEmbedding_toContinuousMap
  let e : C(Ïƒâ‚™ ğ•œ a, ğ•œ) â‰ƒâ‚œ C(Ïƒ ğ•œ (a : AâºÂ¹), ğ•œ) :=
    (Homeomorph.setCongr (quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ a)).arrowCongr (.refl _)
  exact e.isClosedEmbedding

lemma spec_cfcâ‚™Aux (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) : Ïƒ ğ•œ (cfcâ‚™Aux hpâ‚ a ha f) = Set.range f := by
  rw [cfcâ‚™Aux, NonUnitalStarAlgHom.comp_assoc, NonUnitalStarAlgHom.comp_apply]
  simp only [NonUnitalStarAlgHom.comp_apply, NonUnitalStarAlgHom.coe_coe]
  rw [cfcHom_map_spectrum (hpâ‚.mpr ha) (R := ğ•œ) _]
  simp

variable [CompleteSpace A]

lemma cfcâ‚™Aux_mem_range_inr (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) :
    cfcâ‚™Aux hpâ‚ a ha f âˆˆ NonUnitalStarAlgHom.range (Unitization.inrNonUnitalStarAlgHom ğ•œ A) := by
  have hâ‚ := (isClosedEmbedding_cfcâ‚™Aux hpâ‚ a ha).continuous.range_subset_closure_image_dense
    (ContinuousMapZero.adjoin_id_dense (Ïƒâ‚™ ğ•œ a)) âŸ¨f, rflâŸ©
  rw [â† SetLike.mem_coe]
  refine closure_minimal ?_ ?_ hâ‚
  Â· rw [â† NonUnitalStarSubalgebra.coe_map, SetLike.coe_subset_coe, NonUnitalStarSubalgebra.map_le]
    apply NonUnitalStarAlgebra.adjoin_le
    apply Set.singleton_subset_iff.mpr
    rw [SetLike.mem_coe, NonUnitalStarSubalgebra.mem_comap, cfcâ‚™Aux_id hpâ‚ a ha]
    exact âŸ¨a, rflâŸ©
  Â· have : Continuous (Unitization.fst (R := ğ•œ) (A := A)) :=
      Unitization.uniformEquivProd.continuous.fst
    simp only [NonUnitalStarAlgHom.coe_range]
    convert IsClosed.preimage this (isClosed_singleton (x := 0))
    aesop

variable [CStarRing A]

include hpâ‚ in
open Unitization NonUnitalStarAlgHom in
theorem RCLike.nonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus ğ•œ A p where
  predicate_zero := by
    rw [â† hpâ‚, Unitization.inr_zero ğ•œ]
    exact cfc_predicate_zero ğ•œ
  exists_cfc_of_predicate a ha := by
    let Ïˆ : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€ â†’â‹†â‚™â‚[ğ•œ] A := comp (inrRangeEquiv ğ•œ A).symm <|
      codRestrict (cfcâ‚™Aux hpâ‚ a ha) _ (cfcâ‚™Aux_mem_range_inr hpâ‚ a ha)
    have coe_Ïˆ (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€) : Ïˆ f = cfcâ‚™Aux hpâ‚ a ha f :=
      congr_arg Subtype.val <| (inrRangeEquiv ğ•œ A).apply_symm_apply
        âŸ¨cfcâ‚™Aux hpâ‚ a ha f, cfcâ‚™Aux_mem_range_inr hpâ‚ a ha fâŸ©
    refine âŸ¨Ïˆ, ?isClosedEmbedding, ?map_id, fun f â†¦ ?map_spec, fun f â†¦ ?isStarNormalâŸ©
    case isClosedEmbedding =>
      apply isometry_inr (ğ•œ := ğ•œ) (A := A) |>.isClosedEmbedding |>.of_comp_iff.mp
      have : inr âˆ˜ Ïˆ = cfcâ‚™Aux hpâ‚ a ha := by ext1; rw [Function.comp_apply, coe_Ïˆ]
      exact this â–¸ isClosedEmbedding_cfcâ‚™Aux hpâ‚ a ha
    case map_id => exact inr_injective (R := ğ•œ) <| coe_Ïˆ _ â–¸ cfcâ‚™Aux_id hpâ‚ a ha
    case map_spec =>
      exact quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ (Ïˆ f) â–¸ coe_Ïˆ _ â–¸ spec_cfcâ‚™Aux hpâ‚ a ha f
    case isStarNormal => exact hpâ‚.mp <| coe_Ïˆ _ â–¸ cfcHom_predicate (R := ğ•œ) (hpâ‚.mpr ha) _

end RCLike

/-!
### Continuous functional calculus for selfadjoint elements
-/

section SelfAdjointNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module â„‚ A]
  [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A]
  [NonUnitalContinuousFunctionalCalculus â„‚ A IsStarNormal]

/-- An element in a non-unital Câ‹†-algebra is selfadjoint if and only if it is normal and its
quasispectrum is contained in `â„`. -/
lemma isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts {a : A} :
    IsSelfAdjoint a â†” IsStarNormal a âˆ§ QuasispectrumRestricts a Complex.reCLM := by
  refine âŸ¨fun ha â†¦ âŸ¨ha.isStarNormal, âŸ¨fun x hx â†¦ ?_, Complex.ofReal_reâŸ©âŸ©, ?_âŸ©
  Â· have := eqOn_of_cfcâ‚™_eq_cfcâ‚™ <|
      (cfcâ‚™_star (id : â„‚ â†’ â„‚) a).symm â–¸ (cfcâ‚™_id â„‚ a).symm â–¸ ha.star_eq
    exact Complex.conj_eq_iff_re.mp (by simpa using this hx)
  Â· rintro âŸ¨haâ‚, haâ‚‚âŸ©
    rw [isSelfAdjoint_iff]
    nth_rw 2 [â† cfcâ‚™_id â„‚ a]
    rw [â† cfcâ‚™_star_id a (R := â„‚)]
    refine cfcâ‚™_congr fun x hx â†¦ ?_
    obtain âŸ¨x, -, rflâŸ© := haâ‚‚.algebraMap_image.symm â–¸ hx
    exact Complex.conj_ofReal _

lemma IsSelfAdjoint.quasispectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    QuasispectrumRestricts a Complex.reCLM :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts |>.mp ha |>.2

@[deprecated (since := "2025-09-16")]
alias isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts

/-- A normal element whose `â„‚`-quasispectrum is contained in `â„` is selfadjoint. -/
lemma QuasispectrumRestricts.isSelfAdjoint (a : A) (ha : QuasispectrumRestricts a Complex.reCLM)
    [IsStarNormal a] : IsSelfAdjoint a :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts.mpr âŸ¨â€¹_â€º, haâŸ©

@[deprecated (since := "2025-09-16")]
alias SpectrumRestricts.isSelfAdjoint := QuasispectrumRestricts.isSelfAdjoint

instance IsSelfAdjoint.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint :=
  QuasispectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ â†¦ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts)

end SelfAdjointNonUnital

section SelfAdjointUnital


variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra â„‚ A]
  [ContinuousFunctionalCalculus â„‚ A IsStarNormal]

lemma IsSelfAdjoint.spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts a Complex.reCLM :=
  ha.quasispectrumRestricts

instance IsSelfAdjoint.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„ A IsSelfAdjoint :=
  SpectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ â†¦ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts)

lemma IsSelfAdjoint.spectrum_nonempty {A : Type*} [Ring A] [StarRing A]
    [TopologicalSpace A] [Algebra â„ A] [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]
    [Nontrivial A] {a : A} (ha : IsSelfAdjoint a) : (Ïƒ â„ a).Nonempty :=
  CFC.spectrum_nonempty â„ a ha

end SelfAdjointUnital

/-!
### Continuous functional calculus for nonnegative elements
-/

section Nonneg

lemma CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts {A : Type*} [NonUnitalRing A]
    [StarRing A] [TopologicalSpace A] [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
    [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
    {a : A} (haâ‚ : IsSelfAdjoint a) (haâ‚‚ : QuasispectrumRestricts a ContinuousMap.realToNNReal) :
    âˆƒ x : A, IsSelfAdjoint x âˆ§ QuasispectrumRestricts x ContinuousMap.realToNNReal âˆ§ x * x = a := by
  use cfcâ‚™ (âˆšÂ·) a, cfcâ‚™_predicate (âˆšÂ·) a
  constructor
  Â· simpa only [QuasispectrumRestricts.nnreal_iff, cfcâ‚™_map_quasispectrum (âˆšÂ·) a,
      Set.mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚]
        using fun x _ â†¦ Real.sqrt_nonneg x
  Â· rw [â† cfcâ‚™_mul ..]
    nth_rw 2 [â† cfcâ‚™_id â„ a]
    apply cfcâ‚™_congr fun x hx â†¦ ?_
    rw [QuasispectrumRestricts.nnreal_iff] at haâ‚‚
    apply haâ‚‚ x at hx
    simp [â† sq, Real.sq_sqrt hx]

@[deprecated (since := "2025-09-16")]
alias CFC.exists_sqrt_of_isSelfAdjoint_of_spectrumRestricts :=
  CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts

variable {A : Type*} [NonUnitalRing A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
variable [TopologicalSpace A] [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
variable [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

lemma nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts {a : A} :
    0 â‰¤ a â†” IsSelfAdjoint a âˆ§ QuasispectrumRestricts a ContinuousMap.realToNNReal := by
  refine âŸ¨fun ha â†¦ âŸ¨.of_nonneg ha, .nnreal_of_nonneg haâŸ©, ?_âŸ©
  rintro âŸ¨haâ‚, haâ‚‚âŸ©
  obtain âŸ¨x, hx, -, rflâŸ© := CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts haâ‚ haâ‚‚
  simpa [sq, hx.star_eq] using star_mul_self_nonneg x

@[deprecated (since := "2025-09-16")]
alias nonneg_iff_isSelfAdjoint_and_spectrumRestricts :=
  nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts

open NNReal in
instance Nonneg.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus â„â‰¥0 A (0 â‰¤ Â·) :=
  QuasispectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl
    (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

/-- In a Câ‹†-algebra, commuting nonnegative elements have nonnegative products. -/
lemma Commute.mul_nonneg {a b : A} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) (h : Commute a b) :
    0 â‰¤ a * b := by
  rw [nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts]
  refine âŸ¨ha.isSelfAdjoint.commute_iff hb.isSelfAdjoint |>.mp h, ?_âŸ©
  rw [nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts] at hb
  obtain âŸ¨x, hxâ‚, hxâ‚‚, rflâŸ© := CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts hb.1 hb.2
  have hx := nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts.mpr âŸ¨hxâ‚, hxâ‚‚âŸ©
  rw [â† mul_assoc, quasispectrumRestricts_iff, quasispectrum.mul_comm,
    â† quasispectrumRestricts_iff, â† mul_assoc]
  exact QuasispectrumRestricts.nnreal_of_nonneg <| conjugate_nonneg_of_nonneg ha hx

lemma commute_iff_mul_nonneg {a b : A} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) :
    Commute a b â†” 0 â‰¤ a * b :=
  âŸ¨Commute.mul_nonneg ha hb,
  fun h => ha.isSelfAdjoint.commute_iff hb.isSelfAdjoint |>.mpr h.isSelfAdjointâŸ©

open NNReal in
lemma NNReal.spectrum_nonempty {A : Type*} [Ring A] [StarRing A] [LE A]
    [TopologicalSpace A] [Algebra â„â‰¥0 A] [ContinuousFunctionalCalculus â„â‰¥0 A (0 â‰¤ Â·)]
    [Nontrivial A] {a : A} (ha : 0 â‰¤ a) : (spectrum â„â‰¥0 a).Nonempty :=
  CFC.spectrum_nonempty â„â‰¥0 a ha

end Nonneg

section Nonneg

variable {A : Type*} [Ring A] [PartialOrder A] [StarRing A] [StarOrderedRing A] [TopologicalSpace A]
variable [Algebra â„ A] [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]
variable [NonnegSpectrumClass â„ A]

open NNReal in
instance Nonneg.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus â„â‰¥0 A (0 â‰¤ Â·) :=
  SpectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl
    (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

end Nonneg

/-!
### The restriction of a continuous functional calculus is equal to the original one
-/
section RealEqComplex

variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra â„‚ A]
  [ContinuousFunctionalCalculus â„‚ A IsStarNormal] [T2Space A]

lemma cfcHom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfcHom ha =
      ha.spectrumRestricts.starAlgHom (R := â„) (S := â„‚)
        (cfcHom ha.isStarNormal) (f := Complex.reCLM) :=
  ha.spectrumRestricts.cfcHom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfc_real_eq_complex {a : A} (f : â„ â†’ â„) (ha : IsSelfAdjoint a := by cfc_tac) :
    cfc f a = cfc (fun x â†¦ f x.re : â„‚ â†’ â„‚) a := by
  exact ha.spectrumRestricts.cfc_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

lemma cfc_complex_eq_real {f : â„‚ â†’ â„‚} (a : A) (hf_real : âˆ€ x âˆˆ spectrum â„‚ a, star (f x) = f x)
    (ha : IsSelfAdjoint a := by cfc_tac) :
    cfc f a = cfc (fun x : â„ â†¦ (f x).re) a := by
  rw [cfc_real_eq_complex ..]
  refine cfc_congr fun x hx â†¦ ?_
  simp_rw [RCLike.star_def, RCLike.conj_eq_iff_re, RCLike.re_eq_complex_re,
    RCLike.ofReal_eq_complex_ofReal] at hf_real
  rw [â† SpectrumRestricts.real_iff.mp ha.spectrumRestricts _ hx, hf_real _ hx]

end RealEqComplex

section RealEqComplexNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module â„‚ A]
  [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A] [T2Space A]
  [NonUnitalContinuousFunctionalCalculus â„‚ A IsStarNormal]

lemma cfcâ‚™Hom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfcâ‚™Hom ha = ha.quasispectrumRestricts.nonUnitalStarAlgHom (cfcâ‚™Hom ha.isStarNormal)
      (R := â„) (S := â„‚) (f := Complex.reCLM) :=
  ha.quasispectrumRestricts.cfcâ‚™Hom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfcâ‚™_real_eq_complex {a : A} (f : â„ â†’ â„) (ha : IsSelfAdjoint a := by cfc_tac) :
    cfcâ‚™ f a = cfcâ‚™ (fun x â†¦ f x.re : â„‚ â†’ â„‚) a := by
  exact ha.quasispectrumRestricts.cfcâ‚™_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

lemma cfcâ‚™_complex_eq_real {f : â„‚ â†’ â„‚} (a : A) (hf_real : âˆ€ x âˆˆ Ïƒâ‚™ â„‚ a, star (f x) = f x)
    (ha : IsSelfAdjoint a := by cfc_tac) :
    cfcâ‚™ f a = cfcâ‚™ (fun x : â„ â†¦ (f x).re) a := by
  rw [cfcâ‚™_real_eq_complex ..]
  refine cfcâ‚™_congr fun x hx â†¦ ?_
  simp_rw [RCLike.star_def, RCLike.conj_eq_iff_re, RCLike.re_eq_complex_re,
    RCLike.ofReal_eq_complex_ofReal] at hf_real
  rw [â† QuasispectrumRestricts.real_iff.mp ha.quasispectrumRestricts _ hx, hf_real _ hx]

end RealEqComplexNonUnital

section NNRealEqReal

open NNReal

variable {A : Type*} [TopologicalSpace A] [Ring A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Algebra â„ A] [IsTopologicalRing A] [T2Space A]
  [ContinuousFunctionalCalculus â„ A IsSelfAdjoint]
  [NonnegSpectrumClass â„ A]

lemma cfcHom_nnreal_eq_restrict {a : A} (ha : 0 â‰¤ a) :
    cfcHom ha = (SpectrumRestricts.nnreal_of_nonneg ha).starAlgHom
      (cfcHom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfcHom_eq_restrict _ isUniformEmbedding_subtype_val

lemma cfc_nnreal_eq_real (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) (ha : 0 â‰¤ a := by cfc_tac) :
    cfc f a = cfc (fun x â†¦ f x.toNNReal : â„ â†’ â„) a := by
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfc_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

lemma cfc_real_eq_nnreal {f : â„ â†’ â„} (a : A) (hf_nonneg : âˆ€ x âˆˆ spectrum â„ a, 0 â‰¤ f x)
    (ha : 0 â‰¤ a := by cfc_tac) : cfc f a = cfc (fun x : â„â‰¥0 â†¦ (f x).toNNReal) a := by
  rw [cfc_nnreal_eq_real ..]
  refine cfc_congr fun x hx â†¦ ?_
  rw [x.coe_toNNReal (spectrum_nonneg_of_nonneg ha hx), (f x).coe_toNNReal (hf_nonneg x hx)]

end NNRealEqReal

section NNRealEqRealNonUnital

open NNReal

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Module â„ A] [IsTopologicalRing A] [IsScalarTower â„ A A] [SMulCommClass â„ A A]
  [T2Space A] [NonUnitalContinuousFunctionalCalculus â„ A IsSelfAdjoint]
  [NonnegSpectrumClass â„ A]

lemma cfcâ‚™Hom_nnreal_eq_restrict {a : A} (ha : 0 â‰¤ a) :
    cfcâ‚™Hom ha = (QuasispectrumRestricts.nnreal_of_nonneg ha).nonUnitalStarAlgHom
      (cfcâ‚™Hom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfcâ‚™Hom_eq_restrict _
    isUniformEmbedding_subtype_val

lemma cfcâ‚™_nnreal_eq_real (f : â„â‰¥0 â†’ â„â‰¥0) (a : A) (ha : 0 â‰¤ a := by cfc_tac) :
    cfcâ‚™ f a = cfcâ‚™ (fun x â†¦ f x.toNNReal : â„ â†’ â„) a := by
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfcâ‚™_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

lemma cfcâ‚™_real_eq_nnreal {f : â„ â†’ â„} (a : A) (hf_nonneg : âˆ€ x âˆˆ Ïƒâ‚™ â„ a, 0 â‰¤ f x)
    (ha : 0 â‰¤ a := by cfc_tac) : cfcâ‚™ f a = cfcâ‚™ (fun x : â„â‰¥0 â†¦ (f x).toNNReal) a := by
  rw [cfcâ‚™_nnreal_eq_real ..]
  refine cfcâ‚™_congr fun x hx â†¦ ?_
  rw [x.coe_toNNReal (quasispectrum_nonneg_of_nonneg _ ha _ hx),
    (f x).coe_toNNReal (hf_nonneg x hx)]

end NNRealEqRealNonUnital

end
