/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Restrict
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.Normed.Algebra.Spectrum

/-! # Instances of the continuous functional calculus

## Main theorems

* `IsSelfAdjoint.instContinuousFunctionalCalculus`: the continuous functional calculus for
  selfadjoint elements in a `‚ÑÇ`-algebra with a continuous functional calculus for normal elements
  and where every element has compact spectrum. In particular, this includes unital C‚ãÜ-algebras
  over `‚ÑÇ`.
* `Nonneg.instContinuousFunctionalCalculus`: the continuous functional calculus for nonnegative
  elements in an `‚Ñù`-algebra with a continuous functional calculus for selfadjoint elements,
  where every element has compact spectrum, and where nonnegative elements have nonnegative
  spectrum. In particular, this includes unital C‚ãÜ-algebras over `‚Ñù`.

## Tags

continuous functional calculus, normal, selfadjoint
-/

open Topology

noncomputable section

local notation "œÉ‚Çô" => quasispectrum
local notation "œÉ" => spectrum

/-!
### Pull back a non-unital instance from a unital one on the unitization
-/

section RCLike

variable {ùïú A : Type*} [RCLike ùïú] [NonUnitalNormedRing A] [StarRing A]
variable [NormedSpace ùïú A] [IsScalarTower ùïú A A] [SMulCommClass ùïú A A]
variable [StarModule ùïú A] {p : A ‚Üí Prop} {p‚ÇÅ : Unitization ùïú A ‚Üí Prop}

local postfix:max "‚Å∫¬π" => Unitization ùïú

variable (hp‚ÇÅ : ‚àÄ {x : A}, p‚ÇÅ x ‚Üî p x) (a : A) (ha : p a)
variable [ContinuousFunctionalCalculus ùïú (Unitization ùïú A) p‚ÇÅ]

open scoped ContinuousMapZero


open Unitization in
/--
This is an auxiliary definition used for constructing an instance of the non-unital continuous
functional calculus given a instance of the unital one on the unitization.

This is the natural non-unital star homomorphism obtained from the chain
```lean
calc
  C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[ùïú] C(œÉ‚Çô ùïú a, ùïú) := ContinuousMapZero.toContinuousMapHom
  _             ‚âÉ‚ãÜ[ùïú] C(œÉ ùïú (‚Üëa : A‚Å∫¬π), ùïú) := Homeomorph.compStarAlgEquiv'
  _             ‚Üí‚ãÜ‚Çê[ùïú] A‚Å∫¬π := cfcHom
```
This range of this map is contained in the range of `(‚Üë) : A ‚Üí A‚Å∫¬π` (see `cfc‚ÇôAux_mem_range_inr`),
and so we may restrict it to `A` to get the necessary homomorphism for the non-unital continuous
functional calculus.
-/
noncomputable def cfc‚ÇôAux : C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[ùïú] A‚Å∫¬π :=
  (cfcHom (R := ùïú) (hp‚ÇÅ.mpr ha) : C(œÉ ùïú (a : A‚Å∫¬π), ùïú) ‚Üí‚ãÜ‚Çô‚Çê[ùïú] A‚Å∫¬π) |>.comp
    (Homeomorph.compStarAlgEquiv' ùïú ùïú <| .setCongr <| (quasispectrum_eq_spectrum_inr' ùïú ùïú a).symm)
    |>.comp ContinuousMapZero.toContinuousMapHom

lemma cfc‚ÇôAux_id : cfc‚ÇôAux hp‚ÇÅ a ha (.id _) = a := cfcHom_id (hp‚ÇÅ.mpr ha)

open Unitization in
lemma isClosedEmbedding_cfc‚ÇôAux : IsClosedEmbedding (cfc‚ÇôAux hp‚ÇÅ a ha) := by
  simp only [cfc‚ÇôAux, NonUnitalStarAlgHom.coe_comp]
  refine ((cfcHom_isClosedEmbedding (hp‚ÇÅ.mpr ha)).comp ?_).comp
    ContinuousMapZero.isClosedEmbedding_toContinuousMap
  let e : C(œÉ‚Çô ùïú a, ùïú) ‚âÉ‚Çú C(œÉ ùïú (a : A‚Å∫¬π), ùïú) :=
    (Homeomorph.setCongr (quasispectrum_eq_spectrum_inr' ùïú ùïú a)).arrowCongr (.refl _)
  exact e.isClosedEmbedding

lemma spec_cfc‚ÇôAux (f : C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ) : œÉ ùïú (cfc‚ÇôAux hp‚ÇÅ a ha f) = Set.range f := by
  rw [cfc‚ÇôAux, NonUnitalStarAlgHom.comp_assoc, NonUnitalStarAlgHom.comp_apply]
  simp only [NonUnitalStarAlgHom.comp_apply, NonUnitalStarAlgHom.coe_coe]
  rw [cfcHom_map_spectrum (hp‚ÇÅ.mpr ha) (R := ùïú) _]
  simp

variable [CompleteSpace A]

lemma cfc‚ÇôAux_mem_range_inr (f : C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ) :
    cfc‚ÇôAux hp‚ÇÅ a ha f ‚àà NonUnitalStarAlgHom.range (Unitization.inrNonUnitalStarAlgHom ùïú A) := by
  have h‚ÇÅ := (isClosedEmbedding_cfc‚ÇôAux hp‚ÇÅ a ha).continuous.range_subset_closure_image_dense
    (ContinuousMapZero.adjoin_id_dense (œÉ‚Çô ùïú a)) ‚ü®f, rfl‚ü©
  rw [‚Üê SetLike.mem_coe]
  refine closure_minimal ?_ ?_ h‚ÇÅ
  ¬∑ rw [‚Üê NonUnitalStarSubalgebra.coe_map, SetLike.coe_subset_coe, NonUnitalStarSubalgebra.map_le]
    apply NonUnitalStarAlgebra.adjoin_le
    apply Set.singleton_subset_iff.mpr
    rw [SetLike.mem_coe, NonUnitalStarSubalgebra.mem_comap, cfc‚ÇôAux_id hp‚ÇÅ a ha]
    exact ‚ü®a, rfl‚ü©
  ¬∑ have : Continuous (Unitization.fst (R := ùïú) (A := A)) :=
      Unitization.uniformEquivProd.continuous.fst
    simp only [NonUnitalStarAlgHom.coe_range]
    convert IsClosed.preimage this (isClosed_singleton (x := 0))
    aesop

variable [CStarRing A]

include hp‚ÇÅ in
open Unitization NonUnitalStarAlgHom in
theorem RCLike.nonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus ùïú A p where
  predicate_zero := by
    rw [‚Üê hp‚ÇÅ, Unitization.inr_zero ùïú]
    exact cfc_predicate_zero ùïú
  exists_cfc_of_predicate a ha := by
    let œà : C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ ‚Üí‚ãÜ‚Çô‚Çê[ùïú] A := comp (inrRangeEquiv ùïú A).symm <|
      codRestrict (cfc‚ÇôAux hp‚ÇÅ a ha) _ (cfc‚ÇôAux_mem_range_inr hp‚ÇÅ a ha)
    have coe_œà (f : C(œÉ‚Çô ùïú a, ùïú)‚ÇÄ) : œà f = cfc‚ÇôAux hp‚ÇÅ a ha f :=
      congr_arg Subtype.val <| (inrRangeEquiv ùïú A).apply_symm_apply
        ‚ü®cfc‚ÇôAux hp‚ÇÅ a ha f, cfc‚ÇôAux_mem_range_inr hp‚ÇÅ a ha f‚ü©
    refine ‚ü®œà, ?isClosedEmbedding, ?map_id, fun f ‚Ü¶ ?map_spec, fun f ‚Ü¶ ?isStarNormal‚ü©
    case isClosedEmbedding =>
      apply isometry_inr (ùïú := ùïú) (A := A) |>.isClosedEmbedding |>.of_comp_iff.mp
      have : inr ‚àò œà = cfc‚ÇôAux hp‚ÇÅ a ha := by ext1; rw [Function.comp_apply, coe_œà]
      exact this ‚ñ∏ isClosedEmbedding_cfc‚ÇôAux hp‚ÇÅ a ha
    case map_id => exact inr_injective (R := ùïú) <| coe_œà _ ‚ñ∏ cfc‚ÇôAux_id hp‚ÇÅ a ha
    case map_spec =>
      exact quasispectrum_eq_spectrum_inr' ùïú ùïú (œà f) ‚ñ∏ coe_œà _ ‚ñ∏ spec_cfc‚ÇôAux hp‚ÇÅ a ha f
    case isStarNormal => exact hp‚ÇÅ.mp <| coe_œà _ ‚ñ∏ cfcHom_predicate (R := ùïú) (hp‚ÇÅ.mpr ha) _

end RCLike

/-!
### Continuous functional calculus for selfadjoint elements
-/

section SelfAdjointNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module ‚ÑÇ A]
  [IsScalarTower ‚ÑÇ A A] [SMulCommClass ‚ÑÇ A A]
  [NonUnitalContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal]

/-- An element in a non-unital C‚ãÜ-algebra is selfadjoint if and only if it is normal and its
quasispectrum is contained in `‚Ñù`. -/
lemma isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts {a : A} :
    IsSelfAdjoint a ‚Üî IsStarNormal a ‚àß QuasispectrumRestricts a Complex.reCLM := by
  refine ‚ü®fun ha ‚Ü¶ ‚ü®ha.isStarNormal, ‚ü®fun x hx ‚Ü¶ ?_, Complex.ofReal_re‚ü©‚ü©, ?_‚ü©
  ¬∑ have := eqOn_of_cfc‚Çô_eq_cfc‚Çô <|
      (cfc‚Çô_star (id : ‚ÑÇ ‚Üí ‚ÑÇ) a).symm ‚ñ∏ (cfc‚Çô_id ‚ÑÇ a).symm ‚ñ∏ ha.star_eq
    exact Complex.conj_eq_iff_re.mp (by simpa using this hx)
  ¬∑ rintro ‚ü®ha‚ÇÅ, ha‚ÇÇ‚ü©
    rw [isSelfAdjoint_iff]
    nth_rw 2 [‚Üê cfc‚Çô_id ‚ÑÇ a]
    rw [‚Üê cfc‚Çô_star_id a (R := ‚ÑÇ)]
    refine cfc‚Çô_congr fun x hx ‚Ü¶ ?_
    obtain ‚ü®x, -, rfl‚ü© := ha‚ÇÇ.algebraMap_image.symm ‚ñ∏ hx
    exact Complex.conj_ofReal _

lemma IsSelfAdjoint.quasispectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    QuasispectrumRestricts a Complex.reCLM :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts |>.mp ha |>.2

@[deprecated (since := "2025-09-16")]
alias isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts

/-- A normal element whose `‚ÑÇ`-quasispectrum is contained in `‚Ñù` is selfadjoint. -/
lemma QuasispectrumRestricts.isSelfAdjoint (a : A) (ha : QuasispectrumRestricts a Complex.reCLM)
    [IsStarNormal a] : IsSelfAdjoint a :=
  isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts.mpr ‚ü®‚Äπ_‚Ä∫, ha‚ü©

@[deprecated (since := "2025-09-16")]
alias SpectrumRestricts.isSelfAdjoint := QuasispectrumRestricts.isSelfAdjoint

instance IsSelfAdjoint.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint :=
  QuasispectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ ‚Ü¶ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts)

end SelfAdjointNonUnital

section SelfAdjointUnital


variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra ‚ÑÇ A]
  [ContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal]

lemma IsSelfAdjoint.spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts a Complex.reCLM :=
  ha.quasispectrumRestricts

instance IsSelfAdjoint.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint :=
  SpectrumRestricts.cfc (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal.isUniformEmbedding (.zero _)
    (fun _ ‚Ü¶ isSelfAdjoint_iff_isStarNormal_and_quasispectrumRestricts)

lemma IsSelfAdjoint.spectrum_nonempty {A : Type*} [Ring A] [StarRing A]
    [TopologicalSpace A] [Algebra ‚Ñù A] [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    [Nontrivial A] {a : A} (ha : IsSelfAdjoint a) : (œÉ ‚Ñù a).Nonempty :=
  CFC.spectrum_nonempty ‚Ñù a ha

end SelfAdjointUnital

/-!
### Continuous functional calculus for nonnegative elements
-/

section Nonneg

lemma CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts {A : Type*} [NonUnitalRing A]
    [StarRing A] [TopologicalSpace A] [Module ‚Ñù A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
    [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    {a : A} (ha‚ÇÅ : IsSelfAdjoint a) (ha‚ÇÇ : QuasispectrumRestricts a ContinuousMap.realToNNReal) :
    ‚àÉ x : A, IsSelfAdjoint x ‚àß QuasispectrumRestricts x ContinuousMap.realToNNReal ‚àß x * x = a := by
  use cfc‚Çô (‚àö¬∑) a, cfc‚Çô_predicate (‚àö¬∑) a
  constructor
  ¬∑ simpa only [QuasispectrumRestricts.nnreal_iff, cfc‚Çô_map_quasispectrum (‚àö¬∑) a,
      Set.mem_image, forall_exists_index, and_imp, forall_apply_eq_imp_iff‚ÇÇ]
        using fun x _ ‚Ü¶ Real.sqrt_nonneg x
  ¬∑ rw [‚Üê cfc‚Çô_mul ..]
    nth_rw 2 [‚Üê cfc‚Çô_id ‚Ñù a]
    apply cfc‚Çô_congr fun x hx ‚Ü¶ ?_
    rw [QuasispectrumRestricts.nnreal_iff] at ha‚ÇÇ
    apply ha‚ÇÇ x at hx
    simp [‚Üê sq, Real.sq_sqrt hx]

@[deprecated (since := "2025-09-16")]
alias CFC.exists_sqrt_of_isSelfAdjoint_of_spectrumRestricts :=
  CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts

variable {A : Type*} [NonUnitalRing A] [PartialOrder A] [StarRing A] [StarOrderedRing A]
variable [TopologicalSpace A] [Module ‚Ñù A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
variable [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
variable [NonnegSpectrumClass ‚Ñù A]

lemma nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts {a : A} :
    0 ‚â§ a ‚Üî IsSelfAdjoint a ‚àß QuasispectrumRestricts a ContinuousMap.realToNNReal := by
  refine ‚ü®fun ha ‚Ü¶ ‚ü®.of_nonneg ha, .nnreal_of_nonneg ha‚ü©, ?_‚ü©
  rintro ‚ü®ha‚ÇÅ, ha‚ÇÇ‚ü©
  obtain ‚ü®x, hx, -, rfl‚ü© := CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts ha‚ÇÅ ha‚ÇÇ
  simpa [sq, hx.star_eq] using star_mul_self_nonneg x

@[deprecated (since := "2025-09-16")]
alias nonneg_iff_isSelfAdjoint_and_spectrumRestricts :=
  nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts

open NNReal in
instance Nonneg.instNonUnitalContinuousFunctionalCalculus :
    NonUnitalContinuousFunctionalCalculus ‚Ñù‚â•0 A (0 ‚â§ ¬∑) :=
  QuasispectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl
    (fun _ ‚Ü¶ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

open NNReal in
lemma NNReal.spectrum_nonempty {A : Type*} [Ring A] [StarRing A] [LE A]
    [TopologicalSpace A] [Algebra ‚Ñù‚â•0 A] [ContinuousFunctionalCalculus ‚Ñù‚â•0 A (0 ‚â§ ¬∑)]
    [Nontrivial A] {a : A} (ha : 0 ‚â§ a) : (spectrum ‚Ñù‚â•0 a).Nonempty :=
  CFC.spectrum_nonempty ‚Ñù‚â•0 a ha

end Nonneg

section Nonneg

variable {A : Type*} [Ring A] [PartialOrder A] [StarRing A] [StarOrderedRing A] [TopologicalSpace A]
variable [Algebra ‚Ñù A] [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
variable [NonnegSpectrumClass ‚Ñù A]

open NNReal in
instance Nonneg.instContinuousFunctionalCalculus :
    ContinuousFunctionalCalculus ‚Ñù‚â•0 A (0 ‚â§ ¬∑) :=
  SpectrumRestricts.cfc (q := IsSelfAdjoint) ContinuousMap.realToNNReal
    isUniformEmbedding_subtype_val le_rfl
    (fun _ ‚Ü¶ nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts)

end Nonneg

/-!
### The restriction of a continuous functional calculus is equal to the original one
-/
section RealEqComplex

variable {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [Algebra ‚ÑÇ A]
  [ContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal] [T2Space A]

lemma cfcHom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfcHom ha =
      ha.spectrumRestricts.starAlgHom (R := ‚Ñù) (S := ‚ÑÇ)
        (cfcHom ha.isStarNormal) (f := Complex.reCLM) :=
  ha.spectrumRestricts.cfcHom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfc_real_eq_complex {a : A} (f : ‚Ñù ‚Üí ‚Ñù) (ha : IsSelfAdjoint a := by cfc_tac) :
    cfc f a = cfc (fun x ‚Ü¶ f x.re : ‚ÑÇ ‚Üí ‚ÑÇ) a := by
  exact ha.spectrumRestricts.cfc_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

end RealEqComplex

section RealEqComplexNonUnital

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [StarRing A] [Module ‚ÑÇ A]
  [IsScalarTower ‚ÑÇ A A] [SMulCommClass ‚ÑÇ A A] [T2Space A]
  [NonUnitalContinuousFunctionalCalculus ‚ÑÇ A IsStarNormal]

lemma cfc‚ÇôHom_real_eq_restrict {a : A} (ha : IsSelfAdjoint a) :
    cfc‚ÇôHom ha = ha.quasispectrumRestricts.nonUnitalStarAlgHom (cfc‚ÇôHom ha.isStarNormal)
      (R := ‚Ñù) (S := ‚ÑÇ) (f := Complex.reCLM) :=
  ha.quasispectrumRestricts.cfc‚ÇôHom_eq_restrict _ Complex.isometry_ofReal.isUniformEmbedding
    ha ha.isStarNormal

lemma cfc‚Çô_real_eq_complex {a : A} (f : ‚Ñù ‚Üí ‚Ñù) (ha : IsSelfAdjoint a := by cfc_tac) :
    cfc‚Çô f a = cfc‚Çô (fun x ‚Ü¶ f x.re : ‚ÑÇ ‚Üí ‚ÑÇ) a := by
  exact ha.quasispectrumRestricts.cfc‚Çô_eq_restrict (f := Complex.reCLM)
    Complex.isometry_ofReal.isUniformEmbedding ha ha.isStarNormal f

end RealEqComplexNonUnital

section NNRealEqReal

open NNReal

variable {A : Type*} [TopologicalSpace A] [Ring A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Algebra ‚Ñù A] [IsTopologicalRing A] [T2Space A]
  [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
  [NonnegSpectrumClass ‚Ñù A]

lemma cfcHom_nnreal_eq_restrict {a : A} (ha : 0 ‚â§ a) :
    cfcHom ha = (SpectrumRestricts.nnreal_of_nonneg ha).starAlgHom
      (cfcHom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfcHom_eq_restrict _ isUniformEmbedding_subtype_val

lemma cfc_nnreal_eq_real {a : A} (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (ha : 0 ‚â§ a := by cfc_tac) :
    cfc f a = cfc (fun x ‚Ü¶ f x.toNNReal : ‚Ñù ‚Üí ‚Ñù) a := by
  apply (SpectrumRestricts.nnreal_of_nonneg ha).cfc_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

end NNRealEqReal

section NNRealEqRealNonUnital

open NNReal

variable {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [Module ‚Ñù A] [IsTopologicalRing A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
  [T2Space A] [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
  [NonnegSpectrumClass ‚Ñù A]

lemma cfc‚ÇôHom_nnreal_eq_restrict {a : A} (ha : 0 ‚â§ a) :
    cfc‚ÇôHom ha = (QuasispectrumRestricts.nnreal_of_nonneg ha).nonUnitalStarAlgHom
      (cfc‚ÇôHom (IsSelfAdjoint.of_nonneg ha)) := by
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfc‚ÇôHom_eq_restrict _
    isUniformEmbedding_subtype_val

lemma cfc‚Çô_nnreal_eq_real {a : A} (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (ha : 0 ‚â§ a := by cfc_tac) :
    cfc‚Çô f a = cfc‚Çô (fun x ‚Ü¶ f x.toNNReal : ‚Ñù ‚Üí ‚Ñù) a := by
  apply (QuasispectrumRestricts.nnreal_of_nonneg ha).cfc‚Çô_eq_restrict _
    isUniformEmbedding_subtype_val ha (.of_nonneg ha)

end NNRealEqRealNonUnital

end
