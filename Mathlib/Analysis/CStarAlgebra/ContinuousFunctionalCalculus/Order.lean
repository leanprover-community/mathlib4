/-
Copyright (c) 2024 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Instances
import Mathlib.Topology.ContinuousFunction.StarOrdered
import Mathlib.Analysis.CStarAlgebra.Unitization

/-! # Facts about star-ordered rings that depend on the continuous functional calculus

This file contains various basic facts about star-ordered rings (i.e. mainly C‚ãÜ-algebras)
that depend on the continuous functional calculus.

We also put an order instance on `Unitization ‚ÑÇ A` when `A` is a C‚ãÜ-algebra via
the spectral order.

## Main theorems

* `IsSelfAdjoint.le_algebraMap_norm_self` and `IsSelfAdjoint.le_algebraMap_norm_self`,
  which respectively show that `a ‚â§ algebraMap ‚Ñù A ‚Äña‚Äñ` and `-(algebraMap ‚Ñù A ‚Äña‚Äñ) ‚â§ a` in a
  C‚ãÜ-algebra.
* `mul_star_le_algebraMap_norm_sq` and `star_mul_le_algebraMap_norm_sq`, which give similar
  statements for `a * star a` and `star a * a`.
* `CStarRing.norm_le_norm_of_nonneg_of_le`: in a non-unital C‚ãÜ-algebra, if `0 ‚â§ a ‚â§ b`, then
  `‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ`.
* `CStarRing.conjugate_le_norm_smul`: in a non-unital C‚ãÜ-algebra, we have that
  `star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a)` (and a primed version for the `a * b * star a` case).

## Tags

continuous functional calculus, normal, selfadjoint
-/

open scoped NNReal

namespace Unitization

variable {A : Type*} [NonUnitalNormedRing A] [CompleteSpace A]
  [PartialOrder A] [StarRing A] [StarOrderedRing A] [CStarRing A] [NormedSpace ‚ÑÇ A] [StarModule ‚ÑÇ A]
  [SMulCommClass ‚ÑÇ A A] [IsScalarTower ‚ÑÇ A A]

instance instPartialOrder : PartialOrder (Unitization ‚ÑÇ A) := CStarRing.spectralOrder _

instance instStarOrderedRing : StarOrderedRing (Unitization ‚ÑÇ A) := CStarRing.spectralOrderedRing _

lemma inr_le_iff (a b : A) (ha : IsSelfAdjoint a := by cfc_tac)
    (hb : IsSelfAdjoint b := by cfc_tac) :
    (a : Unitization ‚ÑÇ A) ‚â§ (b : Unitization ‚ÑÇ A) ‚Üî a ‚â§ b := by
  -- TODO: prove the more general result for star monomorphisms and use it here.
  rw [‚Üê sub_nonneg, ‚Üê sub_nonneg (a := b), StarOrderedRing.nonneg_iff_spectrum_nonneg (R := ‚Ñù) _,
    ‚Üê inr_sub ‚ÑÇ b a, ‚Üê Unitization.quasispectrum_eq_spectrum_inr' ‚Ñù ‚ÑÇ]
  exact StarOrderedRing.nonneg_iff_quasispectrum_nonneg _ |>.symm

@[simp, norm_cast]
lemma inr_nonneg_iff {a : A} : 0 ‚â§ (a : Unitization ‚ÑÇ A) ‚Üî 0 ‚â§ a := by
  by_cases ha : IsSelfAdjoint a
  ¬∑ exact inr_zero ‚ÑÇ (A := A) ‚ñ∏ inr_le_iff 0 a
  ¬∑ refine ‚ü®?_, ?_‚ü©
    all_goals refine fun h ‚Ü¶ (ha ?_).elim
    ¬∑ exact isSelfAdjoint_inr (R := ‚ÑÇ) |>.mp <| .of_nonneg h
    ¬∑ exact .of_nonneg h

end Unitization

/-- `cfc_le_iff` only applies to a scalar ring where `R` is an actual `Ring`, and not a `Semiring`.
However, this theorem still holds for `‚Ñù‚â•0` as long as the algebra `A` itself is an `‚Ñù`-algebra. -/
lemma cfc_nnreal_le_iff {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [PartialOrder A]
    [StarOrderedRing A] [Algebra ‚Ñù A] [TopologicalRing A] [NonnegSpectrumClass ‚Ñù A]
    [ContinuousFunctionalCalculus ‚Ñù (IsSelfAdjoint : A ‚Üí Prop)]
    [UniqueContinuousFunctionalCalculus ‚Ñù A]
    (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (g : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (a : A)
    (ha_spec : SpectrumRestricts a ContinuousMap.realToNNReal)
    (hf : ContinuousOn f (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac)
    (hg : ContinuousOn g (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac)
    (ha : 0 ‚â§ a := by cfc_tac) :
    cfc f a ‚â§ cfc g a ‚Üî ‚àÄ x ‚àà spectrum ‚Ñù‚â•0 a, f x ‚â§ g x := by
  have hf' := hf.ofReal_map_toNNReal <| ha_spec.image ‚ñ∏ Set.mapsTo_image ..
  have hg' := hg.ofReal_map_toNNReal <| ha_spec.image ‚ñ∏ Set.mapsTo_image ..
  rw [cfc_nnreal_eq_real, cfc_nnreal_eq_real, cfc_le_iff ..]
  simp [NNReal.coe_le_coe, ‚Üê ha_spec.image]

/-- In a unital `‚Ñù`-algebra `A` with a continuous functional calculus, an element `a : A` is larger
than some `algebraMap ‚Ñù A r` if and only if every element of the `‚Ñù`-spectrum is nonnegative. -/
lemma CFC.exists_pos_algebraMap_le_iff {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A]
    [PartialOrder A] [StarOrderedRing A] [Algebra ‚Ñù A] [NonnegSpectrumClass ‚Ñù A]
    [ContinuousFunctionalCalculus ‚Ñù (IsSelfAdjoint : A ‚Üí Prop)]
    {a : A} [CompactSpace (spectrum ‚Ñù a)]
    (h_non : (spectrum ‚Ñù a).Nonempty) (ha : IsSelfAdjoint a := by cfc_tac) :
    (‚àÉ r > 0, algebraMap ‚Ñù A r ‚â§ a) ‚Üî (‚àÄ x ‚àà spectrum ‚Ñù a, 0 < x) := by
  have h_cpct : IsCompact (spectrum ‚Ñù a) := isCompact_iff_compactSpace.mpr inferInstance
  simp_rw [algebraMap_le_iff_le_spectrum (a := a)]
  refine ‚ü®?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®r, hr, hr_le‚ü©
    exact (hr.trans_le <| hr_le ¬∑ ¬∑)
  ¬∑ obtain ‚ü®r, hr, hr_min‚ü© := h_cpct.exists_isMinOn h_non continuousOn_id
    exact ‚ü®r, h _ hr, hr_min‚ü©

section CStar_unital

variable {A : Type*} [NormedRing A] [StarRing A] [CStarRing A] [CompleteSpace A]
variable [NormedAlgebra ‚ÑÇ A] [StarModule ‚ÑÇ A]

section StarOrderedRing

variable [PartialOrder A] [StarOrderedRing A]

lemma IsSelfAdjoint.le_algebraMap_norm_self {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    a ‚â§ algebraMap ‚Ñù A ‚Äña‚Äñ := by
  by_cases nontriv : Nontrivial A
  ¬∑ refine le_algebraMap_of_spectrum_le fun r hr => ?_
    calc r ‚â§ ‚Äñr‚Äñ := Real.le_norm_self r
      _ ‚â§ ‚Äña‚Äñ := spectrum.norm_le_norm_of_mem hr
  ¬∑ rw [not_nontrivial_iff_subsingleton] at nontriv
    simp

lemma IsSelfAdjoint.neg_algebraMap_norm_le_self {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    - (algebraMap ‚Ñù A ‚Äña‚Äñ) ‚â§ a := by
  have : - a ‚â§ algebraMap ‚Ñù A ‚Äña‚Äñ := by
    rw [‚Üê norm_neg]
    exact IsSelfAdjoint.le_algebraMap_norm_self (neg ha)
  exact neg_le.mp this

lemma CStarRing.mul_star_le_algebraMap_norm_sq {a : A} : a * star a ‚â§ algebraMap ‚Ñù A (‚Äña‚Äñ ^ 2) := by
  have : a * star a ‚â§ algebraMap ‚Ñù A ‚Äña * star a‚Äñ := IsSelfAdjoint.le_algebraMap_norm_self
  rwa [CStarRing.norm_self_mul_star, ‚Üê pow_two] at this

lemma CStarRing.star_mul_le_algebraMap_norm_sq {a : A} : star a * a ‚â§ algebraMap ‚Ñù A (‚Äña‚Äñ ^ 2) := by
  have : star a * a ‚â§ algebraMap ‚Ñù A ‚Äñstar a * a‚Äñ := IsSelfAdjoint.le_algebraMap_norm_self
  rwa [CStarRing.norm_star_mul_self, ‚Üê pow_two] at this

end StarOrderedRing

lemma IsSelfAdjoint.toReal_spectralRadius_eq_norm {a : A} (ha : IsSelfAdjoint a) :
    (spectralRadius ‚Ñù a).toReal = ‚Äña‚Äñ := by
  simp [ha.spectrumRestricts.spectralRadius_eq, ha.spectralRadius_eq_nnnorm]

namespace CStarRing

lemma norm_or_neg_norm_mem_spectrum [Nontrivial A] {a : A}
    (ha : IsSelfAdjoint a := by cfc_tac) : ‚Äña‚Äñ ‚àà spectrum ‚Ñù a ‚à® -‚Äña‚Äñ ‚àà spectrum ‚Ñù a := by
  have ha' : SpectrumRestricts a Complex.reCLM := ha.spectrumRestricts
  rw [‚Üê ha.toReal_spectralRadius_eq_norm]
  exact Real.spectralRadius_mem_spectrum_or (ha'.image ‚ñ∏ (spectrum.nonempty a).image _)

variable [PartialOrder A] [StarOrderedRing A]

lemma nnnorm_mem_spectrum_of_nonneg [Nontrivial A] {a : A} (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚àà spectrum ‚Ñù‚â•0 a := by
  have : IsSelfAdjoint a := .of_nonneg ha
  convert NNReal.spectralRadius_mem_spectrum (a := a) ?_ (.nnreal_of_nonneg ha)
  ¬∑ simp [this.spectrumRestricts.spectralRadius_eq, this.spectralRadius_eq_nnnorm]
  ¬∑ exact this.spectrumRestricts.image ‚ñ∏ (spectrum.nonempty a).image _

lemma norm_mem_spectrum_of_nonneg [Nontrivial A] {a : A} (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚àà spectrum ‚Ñù a := by
  simpa using spectrum.algebraMap_mem ‚Ñù <| nnnorm_mem_spectrum_of_nonneg ha

lemma norm_le_iff_le_algebraMap (a : A) {r : ‚Ñù} (hr : 0 ‚â§ r) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ r ‚Üî a ‚â§ algebraMap ‚Ñù A r := by
  rw [le_algebraMap_iff_spectrum_le]
  obtain (h | _) := subsingleton_or_nontrivial A
  ¬∑ simp [Subsingleton.elim a 0, hr]
  ¬∑ exact ‚ü®fun h x hx ‚Ü¶ Real.le_norm_self x |>.trans (spectrum.norm_le_norm_of_mem hx) |>.trans h,
      fun h ‚Ü¶ h ‚Äña‚Äñ <| norm_mem_spectrum_of_nonneg‚ü©

lemma nnnorm_le_iff_of_nonneg (a : A) (r : ‚Ñù‚â•0) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ r ‚Üî a ‚â§ algebraMap ‚Ñù‚â•0 A r := by
  rw [‚Üê NNReal.coe_le_coe]
  exact norm_le_iff_le_algebraMap a r.2

lemma norm_le_one_iff_of_nonneg (a : A) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ 1 ‚Üî a ‚â§ 1 := by
  simpa using norm_le_iff_le_algebraMap a zero_le_one

lemma nnnorm_le_one_iff_of_nonneg (a : A) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ 1 ‚Üî a ‚â§ 1 := by
  rw [‚Üê NNReal.coe_le_coe]
  exact norm_le_one_iff_of_nonneg a

lemma norm_le_natCast_iff_of_nonneg (a : A) (n : ‚Ñï) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ n ‚Üî a ‚â§ n := by
  simpa using norm_le_iff_le_algebraMap a n.cast_nonneg

lemma nnnorm_le_natCast_iff_of_nonneg (a : A) (n : ‚Ñï) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ n ‚Üî a ‚â§ n := by
  simpa using nnnorm_le_iff_of_nonneg a n

end CStarRing

end CStar_unital

section CStar_nonunital

variable {A : Type*} [NonUnitalNormedRing A] [CompleteSpace A] [PartialOrder A] [StarRing A]
  [StarOrderedRing A] [CStarRing A] [NormedSpace ‚ÑÇ A] [StarModule ‚ÑÇ A]
  [SMulCommClass ‚ÑÇ A A] [IsScalarTower ‚ÑÇ A A]

namespace CStarRing

open ComplexOrder in
instance instNonnegSpectrumClassComplexNonUnital : NonnegSpectrumClass ‚ÑÇ A where
  quasispectrum_nonneg_of_nonneg a ha x hx := by
    rw [Unitization.quasispectrum_eq_spectrum_inr' ‚ÑÇ ‚ÑÇ a] at hx
    exact spectrum_nonneg_of_nonneg (Unitization.inr_nonneg_iff.mpr ha) hx

lemma norm_le_norm_of_nonneg_of_le {a b : A} (ha : 0 ‚â§ a := by cfc_tac) (hab : a ‚â§ b) :
    ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ := by
  suffices ‚àÄ a b : Unitization ‚ÑÇ A, 0 ‚â§ a ‚Üí a ‚â§ b ‚Üí ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ by
    have hb := ha.trans hab
    simpa only [ge_iff_le, Unitization.norm_inr] using
      this a b (by simpa) (by rwa [Unitization.inr_le_iff a b])
  intro a b ha hab
  have hb_nonneg : 0 ‚â§ b := ha.trans hab
  have : 0 ‚â§ a := by cfc_tac
  have h‚ÇÇ : cfc (id : ‚Ñù ‚Üí ‚Ñù) a ‚â§ cfc (fun _ => ‚Äñb‚Äñ) a := by
    calc _ = a := by rw [cfc_id ‚Ñù a]
      _ ‚â§ cfc id b := (cfc_id ‚Ñù b) ‚ñ∏ hab
      _ ‚â§ cfc (fun _ => ‚Äñb‚Äñ) b := by
          refine cfc_mono fun x hx => ?_
          calc x = ‚Äñx‚Äñ := (Real.norm_of_nonneg (spectrum_nonneg_of_nonneg hb_nonneg hx)).symm
            _ ‚â§ ‚Äñb‚Äñ := spectrum.norm_le_norm_of_mem hx
      _ = _ := by rw [cfc_const _ _, cfc_const _ _]
  rw [cfc_le_iff id (fun _ => ‚Äñb‚Äñ) a] at h‚ÇÇ
  exact h‚ÇÇ ‚Äña‚Äñ <| norm_mem_spectrum_of_nonneg ha

lemma conjugate_le_norm_smul {a b : A} (hb : IsSelfAdjoint b := by cfc_tac) :
    star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a) := by
  suffices ‚àÄ a b : Unitization ‚ÑÇ A, IsSelfAdjoint b ‚Üí star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a) by
    rw [‚Üê Unitization.inr_le_iff _ _ (by aesop) ((IsSelfAdjoint.all _).smul (.star_mul_self a))]
    simpa [Unitization.norm_inr] using this a b <| hb.inr ‚ÑÇ
  intro a b hb
  calc
    star a * b * a ‚â§ star a * (algebraMap ‚Ñù (Unitization ‚ÑÇ A) ‚Äñb‚Äñ) * a :=
      conjugate_le_conjugate hb.le_algebraMap_norm_self _
    _ = ‚Äñb‚Äñ ‚Ä¢ (star a * a) := by simp [Algebra.algebraMap_eq_smul_one]

lemma conjugate_le_norm_smul' {a b : A} (hb : IsSelfAdjoint b := by cfc_tac) :
    a * b * star a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (a * star a) := by
  have h‚ÇÅ : a * b * star a = star (star a) * b * star a := by simp
  have h‚ÇÇ : a * star a = star (star a) * star a := by simp
  simp only [h‚ÇÅ, h‚ÇÇ]
  exact conjugate_le_norm_smul

/-- The set of nonnegative elements in a C‚ãÜ-algebra is closed. -/
lemma isClosed_nonneg : IsClosed {a : A | 0 ‚â§ a} := by
  suffices IsClosed {a : Unitization ‚ÑÇ A | 0 ‚â§ a} by
    rw [Unitization.isometry_inr (ùïú := ‚ÑÇ) |>.closedEmbedding.closed_iff_image_closed]
    convert this.inter <| (Unitization.isometry_inr (ùïú := ‚ÑÇ)).closedEmbedding.isClosed_range
    ext a
    simp only [Set.mem_image, Set.mem_setOf_eq, Set.mem_inter_iff, Set.mem_range, ‚Üê exists_and_left]
    congr! 2 with x
    exact and_congr_left fun h ‚Ü¶ by simp [‚Üê h]
  simp only [nonneg_iff_isSelfAdjoint_and_spectrumRestricts,
    and_congr_right (SpectrumRestricts.nnreal_iff_nnnorm ¬∑ le_rfl), Set.setOf_and]
  refine isClosed_eq ?_ ?_ |>.inter <| isClosed_le ?_ ?_
  all_goals fun_prop

end CStarRing

end CStar_nonunital
