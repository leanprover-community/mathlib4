/-
Copyright (c) 2024 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Analysis.CStarAlgebra.Unitization
import Mathlib.Analysis.SpecialFunctions.ContinuousFunctionalCalculus.Rpow.Basic
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Isometric
import Mathlib.Topology.ContinuousMap.ContinuousSqrt

/-! # Facts about star-ordered rings that depend on the continuous functional calculus

This file contains various basic facts about star-ordered rings (i.e. mainly C‚ãÜ-algebras)
that depend on the continuous functional calculus.

We also put an order instance on `A‚Å∫¬π := Unitization ‚ÑÇ A` when `A` is a C‚ãÜ-algebra via
the spectral order.

## Main theorems

* `IsSelfAdjoint.le_algebraMap_norm_self` and `IsSelfAdjoint.le_algebraMap_norm_self`,
  which respectively show that `a ‚â§ algebraMap ‚Ñù A ‚Äña‚Äñ` and `-(algebraMap ‚Ñù A ‚Äña‚Äñ) ‚â§ a` in a
  C‚ãÜ-algebra.
* `mul_star_le_algebraMap_norm_sq` and `star_mul_le_algebraMap_norm_sq`, which give similar
  statements for `a * star a` and `star a * a`.
* `CStarAlgebra.norm_le_norm_of_nonneg_of_le`: in a non-unital C‚ãÜ-algebra, if `0 ‚â§ a ‚â§ b`, then
  `‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ`.
* `CStarAlgebra.conjugate_le_norm_smul`: in a non-unital C‚ãÜ-algebra, we have that
  `star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a)` (and a primed version for the `a * b * star a` case).
* `CStarAlgebra.inv_le_inv_iff`: in a unital C‚ãÜ-algebra, `b‚Åª¬π ‚â§ a‚Åª¬π` iff `a ‚â§ b`.

## Tags

continuous functional calculus, normal, selfadjoint
-/

open scoped NNReal CStarAlgebra

local notation "œÉ‚Çô" => quasispectrum

theorem cfc_tsub {A : Type*} [TopologicalSpace A] [Ring A] [PartialOrder A] [StarRing A]
    [StarOrderedRing A] [Algebra ‚Ñù A] [IsTopologicalRing A] [T2Space A]
    [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    [NonnegSpectrumClass ‚Ñù A] (f g : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0)
    (a : A) (hfg : ‚àÄ x ‚àà spectrum ‚Ñù‚â•0 a, g x ‚â§ f x) (ha : 0 ‚â§ a := by cfc_tac)
    (hf : ContinuousOn f (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac)
    (hg : ContinuousOn g (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac) :
    cfc (fun x ‚Ü¶ f x - g x) a = cfc f a - cfc g a := by
  have ha' := SpectrumRestricts.nnreal_of_nonneg ha
  have : (spectrum ‚Ñù a).EqOn (fun x ‚Ü¶ ((f x.toNNReal - g x.toNNReal : ‚Ñù‚â•0) : ‚Ñù))
      (fun x ‚Ü¶ f x.toNNReal - g x.toNNReal) :=
    fun x hx ‚Ü¶ NNReal.coe_sub <| hfg _ <| ha'.apply_mem hx
  rw [cfc_nnreal_eq_real .., cfc_nnreal_eq_real .., cfc_nnreal_eq_real .., cfc_congr this]
  refine cfc_sub _ _ a ?_ ?_
  all_goals
    exact continuous_subtype_val.comp_continuousOn <|
      ContinuousOn.comp ‚Äπ_‚Ä∫ continuous_real_toNNReal.continuousOn <| ha'.image ‚ñ∏ Set.mapsTo_image ..

theorem cfc‚Çô_tsub {A : Type*} [TopologicalSpace A] [NonUnitalRing A] [PartialOrder A] [StarRing A]
    [StarOrderedRing A] [Module ‚Ñù A] [IsScalarTower ‚Ñù A A] [SMulCommClass ‚Ñù A A]
    [IsTopologicalRing A] [T2Space A] [NonUnitalContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    [NonnegSpectrumClass ‚Ñù A] (f g : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0)
    (a : A) (hfg : ‚àÄ x ‚àà œÉ‚Çô ‚Ñù‚â•0 a, g x ‚â§ f x) (ha : 0 ‚â§ a := by cfc_tac)
    (hf : ContinuousOn f (œÉ‚Çô ‚Ñù‚â•0 a) := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (hg : ContinuousOn g (œÉ‚Çô ‚Ñù‚â•0 a) := by cfc_cont_tac) (hg0 : g 0 = 0 := by cfc_zero_tac) :
    cfc‚Çô (fun x ‚Ü¶ f x - g x) a = cfc‚Çô f a - cfc‚Çô g a := by
  have ha' := QuasispectrumRestricts.nnreal_of_nonneg ha
  have : (œÉ‚Çô ‚Ñù a).EqOn (fun x ‚Ü¶ ((f x.toNNReal - g x.toNNReal : ‚Ñù‚â•0) : ‚Ñù))
      (fun x ‚Ü¶ f x.toNNReal - g x.toNNReal) :=
    fun x hx ‚Ü¶ NNReal.coe_sub <| hfg _ <| ha'.apply_mem hx
  rw [cfc‚Çô_nnreal_eq_real .., cfc‚Çô_nnreal_eq_real .., cfc‚Çô_nnreal_eq_real .., cfc‚Çô_congr this]
  refine cfc‚Çô_sub _ _ a ?_ (by simpa) ?_
  all_goals
    exact continuous_subtype_val.comp_continuousOn <|
      ContinuousOn.comp ‚Äπ_‚Ä∫ continuous_real_toNNReal.continuousOn <| ha'.image ‚ñ∏ Set.mapsTo_image ..

namespace Unitization

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

noncomputable instance instPartialOrder : PartialOrder A‚Å∫¬π :=
    CStarAlgebra.spectralOrder _

instance instStarOrderedRing : StarOrderedRing A‚Å∫¬π :=
    CStarAlgebra.spectralOrderedRing _

lemma inr_le_iff (a b : A) (ha : IsSelfAdjoint a := by cfc_tac)
    (hb : IsSelfAdjoint b := by cfc_tac) :
    (a : A‚Å∫¬π) ‚â§ (b : A‚Å∫¬π) ‚Üî a ‚â§ b := by
  -- TODO: prove the more general result for star monomorphisms and use it here.
  rw [‚Üê sub_nonneg, ‚Üê sub_nonneg (a := b), StarOrderedRing.nonneg_iff_spectrum_nonneg (R := ‚Ñù) _,
    ‚Üê inr_sub ‚ÑÇ b a, ‚Üê Unitization.quasispectrum_eq_spectrum_inr' ‚Ñù ‚ÑÇ]
  exact StarOrderedRing.nonneg_iff_quasispectrum_nonneg _ |>.symm

@[simp, norm_cast]
lemma inr_nonneg_iff {a : A} : 0 ‚â§ (a : A‚Å∫¬π) ‚Üî 0 ‚â§ a := by
  by_cases ha : IsSelfAdjoint a
  ¬∑ exact inr_zero ‚ÑÇ (A := A) ‚ñ∏ inr_le_iff 0 a
  ¬∑ refine ‚ü®?_, ?_‚ü©
    all_goals refine fun h ‚Ü¶ (ha ?_).elim
    ¬∑ exact isSelfAdjoint_inr (R := ‚ÑÇ) |>.mp <| .of_nonneg h
    ¬∑ exact .of_nonneg h

lemma nnreal_cfc‚Çô_eq_cfc_inr (a : A) (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0)
    (hf‚ÇÄ : f 0 = 0 := by cfc_zero_tac) : cfc‚Çô f a = cfc f (a : A‚Å∫¬π) :=
  cfc‚Çô_eq_cfc_inr inr_nonneg_iff ..

end Unitization

/-- `cfc_le_iff` only applies to a scalar ring where `R` is an actual `Ring`, and not a `Semiring`.
However, this theorem still holds for `‚Ñù‚â•0` as long as the algebra `A` itself is an `‚Ñù`-algebra. -/
lemma cfc_nnreal_le_iff {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A] [PartialOrder A]
    [StarOrderedRing A] [Algebra ‚Ñù A] [IsTopologicalRing A] [NonnegSpectrumClass ‚Ñù A]
    [T2Space A] [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    (f : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (g : ‚Ñù‚â•0 ‚Üí ‚Ñù‚â•0) (a : A)
    (ha_spec : SpectrumRestricts a ContinuousMap.realToNNReal)
    (hf : ContinuousOn f (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac)
    (hg : ContinuousOn g (spectrum ‚Ñù‚â•0 a) := by cfc_cont_tac)
    (ha : 0 ‚â§ a := by cfc_tac) :
    cfc f a ‚â§ cfc g a ‚Üî ‚àÄ x ‚àà spectrum ‚Ñù‚â•0 a, f x ‚â§ g x := by
  have hf' := hf.ofReal_map_toNNReal <| ha_spec.image ‚ñ∏ Set.mapsTo_image ..
  have hg' := hg.ofReal_map_toNNReal <| ha_spec.image ‚ñ∏ Set.mapsTo_image ..
  rw [cfc_nnreal_eq_real .., cfc_nnreal_eq_real .., cfc_le_iff ..]
  simp [NNReal.coe_le_coe, ‚Üê ha_spec.image]

open ContinuousFunctionalCalculus in
/-- In a unital `‚Ñù`-algebra `A` with a continuous functional calculus, an element `a : A` is larger
than some `algebraMap ‚Ñù A r` if and only if every element of the `‚Ñù`-spectrum is nonnegative. -/
lemma CFC.exists_pos_algebraMap_le_iff {A : Type*} [TopologicalSpace A] [Ring A] [StarRing A]
    [PartialOrder A] [StarOrderedRing A] [Algebra ‚Ñù A] [NonnegSpectrumClass ‚Ñù A] [Nontrivial A]
    [ContinuousFunctionalCalculus ‚Ñù A IsSelfAdjoint]
    {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    (‚àÉ r > 0, algebraMap ‚Ñù A r ‚â§ a) ‚Üî (‚àÄ x ‚àà spectrum ‚Ñù a, 0 < x) := by
  have h_cpct : IsCompact (spectrum ‚Ñù a) := isCompact_iff_compactSpace.mpr inferInstance
  simp_rw [algebraMap_le_iff_le_spectrum (a := a)]
  refine ‚ü®?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ rintro ‚ü®r, hr, hr_le‚ü©
    exact (hr.trans_le <| hr_le ¬∑ ¬∑)
  ¬∑ obtain ‚ü®r, hr, hr_min‚ü© := h_cpct.exists_isMinOn (spectrum_nonempty ‚Ñù a ha) continuousOn_id
    exact ‚ü®r, h _ hr, hr_min‚ü©

section CStar_unital

variable {A : Type*} [CStarAlgebra A]

section StarOrderedRing

variable [PartialOrder A] [StarOrderedRing A]

lemma IsSelfAdjoint.le_algebraMap_norm_self {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    a ‚â§ algebraMap ‚Ñù A ‚Äña‚Äñ := by
  by_cases nontriv : Nontrivial A
  ¬∑ refine le_algebraMap_of_spectrum_le fun r hr => ?_
    calc r ‚â§ ‚Äñr‚Äñ := Real.le_norm_self r
      _ ‚â§ ‚Äña‚Äñ := spectrum.norm_le_norm_of_mem hr
  ¬∑ push_neg at nontriv
    simp

lemma IsSelfAdjoint.neg_algebraMap_norm_le_self {a : A} (ha : IsSelfAdjoint a := by cfc_tac) :
    - (algebraMap ‚Ñù A ‚Äña‚Äñ) ‚â§ a := by
  rw [neg_le, ‚Üê norm_neg]
  exact ha.neg.le_algebraMap_norm_self

lemma CStarAlgebra.mul_star_le_algebraMap_norm_sq {a : A} :
    a * star a ‚â§ algebraMap ‚Ñù A (‚Äña‚Äñ ^ 2) := by
  have : a * star a ‚â§ algebraMap ‚Ñù A ‚Äña * star a‚Äñ := IsSelfAdjoint.le_algebraMap_norm_self
  rwa [CStarRing.norm_self_mul_star, ‚Üê pow_two] at this

lemma CStarAlgebra.star_mul_le_algebraMap_norm_sq {a : A} :
    star a * a ‚â§ algebraMap ‚Ñù A (‚Äña‚Äñ ^ 2) := by
  have : star a * a ‚â§ algebraMap ‚Ñù A ‚Äñstar a * a‚Äñ := IsSelfAdjoint.le_algebraMap_norm_self
  rwa [CStarRing.norm_star_mul_self, ‚Üê pow_two] at this

end StarOrderedRing

lemma IsSelfAdjoint.toReal_spectralRadius_eq_norm {a : A} (ha : IsSelfAdjoint a) :
    (spectralRadius ‚Ñù a).toReal = ‚Äña‚Äñ := by
  simp [ha.spectrumRestricts.spectralRadius_eq, ha.spectralRadius_eq_nnnorm]

namespace CStarAlgebra

lemma norm_or_neg_norm_mem_spectrum [Nontrivial A] {a : A}
    (ha : IsSelfAdjoint a := by cfc_tac) : ‚Äña‚Äñ ‚àà spectrum ‚Ñù a ‚à® -‚Äña‚Äñ ‚àà spectrum ‚Ñù a := by
  have ha' : SpectrumRestricts a Complex.reCLM := ha.spectrumRestricts
  rw [‚Üê ha.toReal_spectralRadius_eq_norm]
  exact Real.spectralRadius_mem_spectrum_or (ha'.image ‚ñ∏ (spectrum.nonempty a).image _)

variable [PartialOrder A] [StarOrderedRing A]

lemma nnnorm_mem_spectrum_of_nonneg [Nontrivial A] {a : A} (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚àà spectrum ‚Ñù‚â•0 a := by
  have : IsSelfAdjoint a := .of_nonneg ha
  convert NNReal.spectralRadius_mem_spectrum (a := a) ?_ (.nnreal_of_nonneg ha)
  ¬∑ simp [this.spectrumRestricts.spectralRadius_eq, this.spectralRadius_eq_nnnorm]
  ¬∑ exact this.spectrumRestricts.image ‚ñ∏ (spectrum.nonempty a).image _

lemma norm_mem_spectrum_of_nonneg [Nontrivial A] {a : A} (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚àà spectrum ‚Ñù a := by
  simpa using spectrum.algebraMap_mem ‚Ñù <| nnnorm_mem_spectrum_of_nonneg ha

lemma norm_le_iff_le_algebraMap (a : A) {r : ‚Ñù} (hr : 0 ‚â§ r) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ r ‚Üî a ‚â§ algebraMap ‚Ñù A r := by
  rw [le_algebraMap_iff_spectrum_le]
  obtain (h | _) := subsingleton_or_nontrivial A
  ¬∑ simp [Subsingleton.elim a 0, hr]
  ¬∑ exact ‚ü®fun h x hx ‚Ü¶ Real.le_norm_self x |>.trans (spectrum.norm_le_norm_of_mem hx) |>.trans h,
      fun h ‚Ü¶ h ‚Äña‚Äñ <| norm_mem_spectrum_of_nonneg‚ü©

lemma nnnorm_le_iff_of_nonneg (a : A) (r : ‚Ñù‚â•0) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ r ‚Üî a ‚â§ algebraMap ‚Ñù‚â•0 A r := by
  rw [‚Üê NNReal.coe_le_coe]
  exact norm_le_iff_le_algebraMap a r.2

lemma norm_le_one_iff_of_nonneg (a : A) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ 1 ‚Üî a ‚â§ 1 := by
  simpa using norm_le_iff_le_algebraMap a zero_le_one

lemma nnnorm_le_one_iff_of_nonneg (a : A) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ 1 ‚Üî a ‚â§ 1 := by
  rw [‚Üê NNReal.coe_le_coe]
  exact norm_le_one_iff_of_nonneg a

lemma norm_le_natCast_iff_of_nonneg (a : A) (n : ‚Ñï) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ ‚â§ n ‚Üî a ‚â§ n := by
  simpa using norm_le_iff_le_algebraMap a n.cast_nonneg

lemma nnnorm_le_natCast_iff_of_nonneg (a : A) (n : ‚Ñï) (ha : 0 ‚â§ a := by cfc_tac) :
    ‚Äña‚Äñ‚Çä ‚â§ n ‚Üî a ‚â§ n := by
  simpa using nnnorm_le_iff_of_nonneg a n


section Icc

open Set

lemma mem_Icc_algebraMap_iff_norm_le {x : A} {r : ‚Ñù} (hr : 0 ‚â§ r) :
    x ‚àà Icc 0 (algebraMap ‚Ñù A r) ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ ‚â§ r := by
  rw [mem_Icc, and_congr_right_iff, iff_comm]
  exact (norm_le_iff_le_algebraMap _ hr ¬∑)

lemma mem_Icc_algebraMap_iff_nnnorm_le {x : A} {r : ‚Ñù‚â•0} :
    x ‚àà Icc 0 (algebraMap ‚Ñù‚â•0 A r) ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ‚Çä ‚â§ r :=
  mem_Icc_algebraMap_iff_norm_le (hr := r.2)

lemma mem_Icc_iff_norm_le_one {x : A} :
    x ‚àà Icc 0 1 ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ ‚â§ 1 := by
  simpa only [map_one] using mem_Icc_algebraMap_iff_norm_le zero_le_one (A := A)

lemma mem_Icc_iff_nnnorm_le_one {x : A} :
    x ‚àà Icc 0 1 ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ‚Çä ‚â§ 1 :=
  mem_Icc_iff_norm_le_one

end Icc

end CStarAlgebra

section Inv

open CFC

variable [PartialOrder A] [StarOrderedRing A]

lemma CFC.conjugate_rpow_neg_one_half (a : A) (ha : IsStrictlyPositive a := by cfc_tac) :
    a ^ (-(1 / 2) : ‚Ñù) * a * a ^ (-(1 / 2) : ‚Ñù) = 1 := by
  lift a to AÀ£ using ha.isUnit
  nth_rw 2 [‚Üê rpow_one (a : A)]
  simp only [‚Üê rpow_add a.isUnit]
  norm_num
  exact rpow_zero _

/-- In a unital C‚ãÜ-algebra, if `a` is strictly positive, and `a ‚â§ b`, then `b` is
invertible. -/
lemma CStarAlgebra.isUnit_of_le (a : A) {b : A} (hab : a ‚â§ b)
    (h : IsStrictlyPositive a := by cfc_tac) : IsUnit b := by
  have h‚ÇÄ := h.isUnit
  have ha := h.nonneg
  rw [‚Üê spectrum.zero_notMem_iff ‚Ñù‚â•0] at h‚ÇÄ ‚ä¢
  nontriviality A
  have hb := (show 0 ‚â§ a from ha).trans hab
  rw [zero_notMem_iff, SpectrumRestricts.nnreal_lt_iff (.nnreal_of_nonneg ‚Äπ_‚Ä∫),
    NNReal.coe_zero, ‚Üê CFC.exists_pos_algebraMap_le_iff (.of_nonneg ‚Äπ_‚Ä∫)] at h‚ÇÄ ‚ä¢
  peel h‚ÇÄ with r hr _
  exact this.trans hab

lemma le_iff_norm_sqrt_mul_rpow {a b : A} (hb : IsStrictlyPositive b) (ha : 0 ‚â§ a) :
    a ‚â§ b ‚Üî ‚Äñsqrt a * (b : A) ^ (-(1 / 2) : ‚Ñù)‚Äñ ‚â§ 1 := by
  lift b to AÀ£ using hb.isUnit
  have hbab : 0 ‚â§ (b : A) ^ (-(1 / 2) : ‚Ñù) * a * (b : A) ^ (-(1 / 2) : ‚Ñù) :=
    conjugate_nonneg_of_nonneg ha rpow_nonneg
  conv_rhs =>
    rw [‚Üê sq_le_one_iff‚ÇÄ (norm_nonneg _), sq, ‚Üê CStarRing.norm_star_mul_self, star_mul,
      IsSelfAdjoint.of_nonneg (sqrt_nonneg a), IsSelfAdjoint.of_nonneg rpow_nonneg,
      ‚Üê mul_assoc, mul_assoc _ _ (sqrt a), sqrt_mul_sqrt_self a,
      CStarAlgebra.norm_le_one_iff_of_nonneg _ hbab]
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü©
  ¬∑ calc
      _ ‚â§ ‚Üëb ^ (-(1 / 2) : ‚Ñù) * (b : A) * ‚Üëb ^ (-(1 / 2) : ‚Ñù) :=
        IsSelfAdjoint.of_nonneg rpow_nonneg |>.conjugate_le_conjugate h
      _ = 1 := conjugate_rpow_neg_one_half (b : A)
  ¬∑ calc
      a = (sqrt ‚Üëb * ‚Üëb ^ (-(1 / 2) : ‚Ñù)) * a * (‚Üëb ^ (-(1 / 2) : ‚Ñù) * sqrt ‚Üëb) := by
        simp only [CFC.sqrt_eq_rpow .., ‚Üê CFC.rpow_add b.isUnit]
        norm_num
        simp [CFC.rpow_zero (b : A)]
      _ = sqrt ‚Üëb * (‚Üëb ^ (-(1 / 2) : ‚Ñù) * a * ‚Üëb ^ (-(1 / 2) : ‚Ñù)) * sqrt ‚Üëb := by
        simp only [mul_assoc]
      _ ‚â§ b := conjugate_le_conjugate_of_nonneg h (sqrt_nonneg _) |>.trans <| by
        simp [CFC.sqrt_mul_sqrt_self (b : A)]

lemma le_iff_norm_sqrt_mul_sqrt_inv {a : A} {b : AÀ£} (ha : 0 ‚â§ a) (hb : 0 ‚â§ (b : A)) :
    a ‚â§ b ‚Üî ‚Äñsqrt a * sqrt (‚Üëb‚Åª¬π : A)‚Äñ ‚â§ 1 := by
  rw [CFC.sqrt_eq_rpow (a := (‚Üëb‚Åª¬π : A)), ‚Üê CFC.rpow_neg_one_eq_inv b,
    CFC.rpow_rpow (b : A) _ _ (by simp) (by simp),
    le_iff_norm_sqrt_mul_rpow (b.isUnit.isStrictlyPositive hb) ha]
  simp

namespace CStarAlgebra

/-- In a unital C‚ãÜ-algebra, if `0 ‚â§ a ‚â§ b` and `a` and `b` are units, then `b‚Åª¬π ‚â§ a‚Åª¬π`. -/
protected lemma inv_le_inv {a b : AÀ£} (ha : 0 ‚â§ (a : A))
    (hab : (a : A) ‚â§ b) : (‚Üëb‚Åª¬π : A) ‚â§ a‚Åª¬π := by
  have hb := ha.trans hab
  have hb_inv : (0 : A) ‚â§ b‚Åª¬π := inv_nonneg_of_nonneg b hb
  have ha_inv : (0 : A) ‚â§ a‚Åª¬π := inv_nonneg_of_nonneg a ha
  rw [le_iff_norm_sqrt_mul_sqrt_inv ha hb, ‚Üê sq_le_one_iff‚ÇÄ (norm_nonneg _), sq,
    ‚Üê CStarRing.norm_star_mul_self] at hab
  rw [le_iff_norm_sqrt_mul_sqrt_inv hb_inv ha_inv, inv_inv, ‚Üê sq_le_one_iff‚ÇÄ (norm_nonneg _), sq,
    ‚Üê CStarRing.norm_self_mul_star]
  rwa [star_mul, IsSelfAdjoint.of_nonneg (sqrt_nonneg _),
    IsSelfAdjoint.of_nonneg (sqrt_nonneg _)] at hab ‚ä¢

/-- In a unital C‚ãÜ-algebra, if `0 ‚â§ a` and `0 ‚â§ b` and `a` and `b` are units, then `a‚Åª¬π ‚â§ b‚Åª¬π`
if and only if `b ‚â§ a`. -/
protected lemma inv_le_inv_iff {a b : AÀ£} (ha : 0 ‚â§ (a : A)) (hb : 0 ‚â§ (b : A)) :
    (‚Üëa‚Åª¬π : A) ‚â§ b‚Åª¬π ‚Üî (b : A) ‚â§ a :=
  ‚ü®CStarAlgebra.inv_le_inv (inv_nonneg_of_nonneg a ha), CStarAlgebra.inv_le_inv hb‚ü©

lemma inv_le_iff {a b : AÀ£} (ha : 0 ‚â§ (a : A)) (hb : 0 ‚â§ (‚Üëb : A)) :
    (‚Üëa‚Åª¬π : A) ‚â§ b ‚Üî (‚Üëb‚Åª¬π : A) ‚â§ a := by
  simpa using CStarAlgebra.inv_le_inv_iff ha (inv_nonneg_of_nonneg b hb)

lemma le_inv_iff {a b : AÀ£} (ha : 0 ‚â§ (a : A)) (hb : 0 ‚â§ (‚Üëb : A)) :
    a ‚â§ (‚Üëb‚Åª¬π : A) ‚Üî b ‚â§ (‚Üëa‚Åª¬π : A) := by
  simpa using CStarAlgebra.inv_le_inv_iff (inv_nonneg_of_nonneg a ha) hb

lemma one_le_inv_iff_le_one {a : AÀ£} (ha : 0 ‚â§ (a : A)) :
    1 ‚â§ (‚Üëa‚Åª¬π : A) ‚Üî a ‚â§ 1 := by
  simpa using CStarAlgebra.le_inv_iff (a := 1) (by simp) ha

lemma inv_le_one_iff_one_le {a : AÀ£} (ha : 0 ‚â§ (a : A)) :
    (‚Üëa‚Åª¬π : A) ‚â§ 1 ‚Üî 1 ‚â§ a := by
  simpa using CStarAlgebra.inv_le_iff ha (b := 1) (by simp)

lemma inv_le_one {a : AÀ£} (ha : 1 ‚â§ a) : (‚Üëa‚Åª¬π : A) ‚â§ 1 :=
  CStarAlgebra.inv_le_one_iff_one_le (zero_le_one.trans ha) |>.mpr ha

lemma le_one_of_one_le_inv {a : AÀ£} (ha : 1 ‚â§ (‚Üëa‚Åª¬π : A)) : (a : A) ‚â§ 1 := by
  simpa using CStarAlgebra.inv_le_one ha

lemma rpow_neg_one_le_rpow_neg_one {a b : A} (ha : IsStrictlyPositive a) (hab : a ‚â§ b) :
    b ^ (-1 : ‚Ñù) ‚â§ a ^ (-1 : ‚Ñù) := by
  lift b to AÀ£ using isUnit_of_le a hab
  lift a to AÀ£ using ha.isUnit
  rw [rpow_neg_one_eq_inv a, rpow_neg_one_eq_inv b (ha.nonneg.trans hab)]
  exact CStarAlgebra.inv_le_inv ha.nonneg hab

lemma rpow_neg_one_le_one {a : A} (ha : 1 ‚â§ a) : a ^ (-1 : ‚Ñù) ‚â§ 1 := by
  lift a to AÀ£ using isUnit_of_le 1 ha
  rw [rpow_neg_one_eq_inv a (zero_le_one.trans ha)]
  exact inv_le_one ha

protected lemma _root_.IsStrictlyPositive.of_le {a b : A} (ha : IsStrictlyPositive a)
    (hab : a ‚â§ b) : IsStrictlyPositive b :=
  ‚ü®ha.nonneg.trans hab, CStarAlgebra.isUnit_of_le a hab‚ü©

theorem _root_.IsStrictlyPositive.add_nonneg {a b : A}
    (ha : IsStrictlyPositive a) (hb : 0 ‚â§ b) : IsStrictlyPositive (a + b) :=
  IsStrictlyPositive.of_le ha ((le_add_iff_nonneg_right a).mpr hb)

theorem _root_.IsStrictlyPositive.nonneg_add {a b : A}
    (ha : 0 ‚â§ a) (hb : IsStrictlyPositive b) : IsStrictlyPositive (a + b) :=
  add_comm a b ‚ñ∏ hb.add_nonneg ha

@[grind ‚Üê, aesop 90% apply]
lemma _root_.isStrictlyPositive_add {a b : A}
    (h : IsStrictlyPositive a ‚àß 0 ‚â§ b ‚à® 0 ‚â§ a ‚àß IsStrictlyPositive b) :
    IsStrictlyPositive (a + b) := by
  grind [IsStrictlyPositive.add_nonneg, IsStrictlyPositive.nonneg_add]

end CStarAlgebra

end Inv

end CStar_unital

section CStar_nonunital

variable {A : Type*} [NonUnitalCStarAlgebra A] [PartialOrder A] [StarOrderedRing A]

namespace CStarAlgebra

open ComplexOrder in
instance instNonnegSpectrumClassComplexNonUnital : NonnegSpectrumClass ‚ÑÇ A where
  quasispectrum_nonneg_of_nonneg a ha x hx := by
    rw [Unitization.quasispectrum_eq_spectrum_inr' ‚ÑÇ ‚ÑÇ a] at hx
    exact spectrum_nonneg_of_nonneg (Unitization.inr_nonneg_iff.mpr ha) hx

lemma norm_le_norm_of_nonneg_of_le {a b : A} (ha : 0 ‚â§ a := by cfc_tac) (hab : a ‚â§ b) :
    ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ := by
  suffices ‚àÄ a b : A‚Å∫¬π, 0 ‚â§ a ‚Üí a ‚â§ b ‚Üí ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ by
    have hb := ha.trans hab
    simpa only [ge_iff_le, Unitization.norm_inr] using
      this a b (by simpa) (by rwa [Unitization.inr_le_iff a b])
  intro a b ha hab
  have hb : 0 ‚â§ b := ha.trans hab
  -- these two `have`s are just for performance
  have := IsSelfAdjoint.of_nonneg ha; have := IsSelfAdjoint.of_nonneg hb
  have h‚ÇÇ : cfc (id : ‚Ñù ‚Üí ‚Ñù) a ‚â§ cfc (fun _ => ‚Äñb‚Äñ) a := by
    calc _ = a := by rw [cfc_id ‚Ñù a]
      _ ‚â§ cfc id b := (cfc_id ‚Ñù b) ‚ñ∏ hab
      _ ‚â§ cfc (fun _ => ‚Äñb‚Äñ) b := by
          refine cfc_mono fun x hx => ?_
          calc x = ‚Äñx‚Äñ := (Real.norm_of_nonneg (spectrum_nonneg_of_nonneg hb hx)).symm
            _ ‚â§ ‚Äñb‚Äñ := spectrum.norm_le_norm_of_mem hx
      _ = _ := by rw [cfc_const _ _, cfc_const _ _]
  rw [cfc_le_iff id (fun _ => ‚Äñb‚Äñ) a] at h‚ÇÇ
  exact h‚ÇÇ ‚Äña‚Äñ <| norm_mem_spectrum_of_nonneg ha

theorem nnnorm_le_nnnorm_of_nonneg_of_le {a : A} {b : A} (ha : 0 ‚â§ a := by cfc_tac) (hab : a ‚â§ b) :
    ‚Äña‚Äñ‚Çä ‚â§ ‚Äñb‚Äñ‚Çä :=
  norm_le_norm_of_nonneg_of_le ha hab

lemma conjugate_le_norm_smul {a b : A} (hb : IsSelfAdjoint b := by cfc_tac) :
    star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a) := by
  suffices ‚àÄ a b : A‚Å∫¬π, IsSelfAdjoint b ‚Üí star a * b * a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (star a * a) by
    rw [‚Üê Unitization.inr_le_iff _ _ (by aesop) ((IsSelfAdjoint.all _).smul (.star_mul_self a))]
    simpa [Unitization.norm_inr] using this a b <| hb.inr ‚ÑÇ
  intro a b hb
  calc
    star a * b * a ‚â§ star a * (algebraMap ‚Ñù A‚Å∫¬π ‚Äñb‚Äñ) * a :=
      conjugate_le_conjugate hb.le_algebraMap_norm_self _
    _ = ‚Äñb‚Äñ ‚Ä¢ (star a * a) := by simp [Algebra.algebraMap_eq_smul_one]

lemma conjugate_le_norm_smul' {a b : A} (hb : IsSelfAdjoint b := by cfc_tac) :
    a * b * star a ‚â§ ‚Äñb‚Äñ ‚Ä¢ (a * star a) := by
  have h‚ÇÅ : a * b * star a = star (star a) * b * star a := by simp
  have h‚ÇÇ : a * star a = star (star a) * star a := by simp
  simp only [h‚ÇÅ, h‚ÇÇ]
  exact conjugate_le_norm_smul

/-- The set of nonnegative elements in a C‚ãÜ-algebra is closed. -/
lemma isClosed_nonneg : IsClosed {a : A | 0 ‚â§ a} := by
  suffices IsClosed {a : A‚Å∫¬π | 0 ‚â§ a} by
    rw [Unitization.isometry_inr (ùïú := ‚ÑÇ) |>.isClosedEmbedding.isClosed_iff_image_isClosed]
    convert this.inter <| (Unitization.isometry_inr (ùïú := ‚ÑÇ)).isClosedEmbedding.isClosed_range
    ext a
    simp only [Set.mem_image, Set.mem_setOf_eq, Set.mem_inter_iff, Set.mem_range, ‚Üê exists_and_left]
    congr! 2 with x
    exact and_congr_left fun h ‚Ü¶ by simp [‚Üê h]
  simp only [nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts,
    and_congr_right (SpectrumRestricts.nnreal_iff_nnnorm ¬∑ le_rfl), Set.setOf_and]
  refine isClosed_eq ?_ ?_ |>.inter <| isClosed_le ?_ ?_
  all_goals fun_prop

instance : OrderClosedTopology A where
  isClosed_le' := isClosed_le_of_isClosed_nonneg isClosed_nonneg

section Icc

open Unitization Set Metric

lemma inr_mem_Icc_iff_norm_le {x : A} :
    (x : A‚Å∫¬π) ‚àà Icc 0 1 ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ ‚â§ 1 := by
  simp only [mem_Icc, inr_nonneg_iff, and_congr_right_iff]
  rw [‚Üê norm_inr (ùïú := ‚ÑÇ), ‚Üê inr_nonneg_iff, iff_comm]
  exact (norm_le_one_iff_of_nonneg _ ¬∑)

lemma inr_mem_Icc_iff_nnnorm_le {x : A} :
    (x : A‚Å∫¬π) ‚àà Icc 0 1 ‚Üî 0 ‚â§ x ‚àß ‚Äñx‚Äñ‚Çä ‚â§ 1 :=
  inr_mem_Icc_iff_norm_le

lemma preimage_inr_Icc_zero_one :
    ((‚Üë) : A ‚Üí A‚Å∫¬π) ‚Åª¬π' Icc 0 1 = {x : A | 0 ‚â§ x} ‚à© closedBall 0 1 := by
  ext
  simp [- mem_Icc, inr_mem_Icc_iff_norm_le]

end Icc

end CStarAlgebra

end CStar_nonunital

section Pow

namespace CStarAlgebra

variable {A : Type*} {B : Type*} [CStarAlgebra A] [PartialOrder A] [StarOrderedRing A]
  [NonUnitalCStarAlgebra B] [PartialOrder B] [StarOrderedRing B]

lemma pow_nonneg {a : A} (ha : 0 ‚â§ a := by cfc_tac) (n : ‚Ñï) : 0 ‚â§ a ^ n := by
  rw [‚Üê cfc_pow_id (R := ‚Ñù‚â•0) a]
  exact cfc_nonneg_of_predicate

lemma pow_monotone {a : A} (ha : 1 ‚â§ a) : Monotone (a ^ ¬∑ : ‚Ñï ‚Üí A) := by
  have ha' : 0 ‚â§ a := zero_le_one.trans ha
  intro n m hnm
  simp only
  rw [‚Üê cfc_pow_id (R := ‚Ñù) a, ‚Üê cfc_pow_id (R := ‚Ñù) a, cfc_le_iff ..]
  rw [CFC.one_le_iff (R := ‚Ñù) a] at ha
  peel ha with x hx _
  exact pow_le_pow_right‚ÇÄ (ha x hx) hnm

lemma pow_antitone {a : A} (ha‚ÇÄ : 0 ‚â§ a := by cfc_tac) (ha‚ÇÅ : a ‚â§ 1) :
    Antitone (a ^ ¬∑ : ‚Ñï ‚Üí A) := by
  intro n m hnm
  simp only
  rw [‚Üê cfc_pow_id (R := ‚Ñù) a, ‚Üê cfc_pow_id (R := ‚Ñù) a, cfc_le_iff ..]
  rw [CFC.le_one_iff (R := ‚Ñù) a] at ha‚ÇÅ
  peel ha‚ÇÅ with x hx _
  exact pow_le_pow_of_le_one (spectrum_nonneg_of_nonneg ha‚ÇÄ hx) (ha‚ÇÅ x hx) hnm

end CStarAlgebra

end Pow
