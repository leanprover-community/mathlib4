/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unique
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Prereqs

/-! # The range of `cfcHom` is `elementalStarAlgebra`

This file establishes that the range of `cfcHom` is the `elementalStarAlgebra` when the scalar field
is `â„` or `â„‚`, and moreover this is also the range of `cfc`. This leads to an induction principle
for terms of the form `cfcHom ha f`, which can then be used to prove theorems such as `cfc_commute`.
We collect those theorems here also.

## Main declarations

+ `CFC.induction_on`: Induction principle for terms of the form `cfcHom ha f`.

## TODO

+ establish analogues for non-unital algebras

-/

section RCLike

variable {ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [Ring A] [StarRing A] [Algebra ğ•œ A]
variable [TopologicalSpace A] [StarModule ğ•œ A] [ContinuousFunctionalCalculus ğ•œ p]

variable (ğ•œ) in
theorem cfc_range {a : A} (ha : p a) :
    Set.range (cfc (R := ğ•œ) Â· a) = (cfcHom ha (R := ğ•œ)).range := by
  ext x
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    by_cases hf : ContinuousOn f (spectrum ğ•œ a)
    Â· simpa only [cfc_apply f a, SetLike.mem_coe] using âŸ¨_, rflâŸ©
    Â· simpa only [cfc_apply_of_not_continuousOn a hf] using zero_mem _
  Â· rintro âŸ¨f, rflâŸ©
    classical
    let f' (x : ğ•œ) : ğ•œ := if hx : x âˆˆ spectrum ğ•œ a then f âŸ¨x, hxâŸ© else 0
    have hff' : f = Set.restrict (spectrum ğ•œ a) f'  := by ext; simp [f']
    have : ContinuousOn f' (spectrum ğ•œ a) :=
      continuousOn_iff_continuous_restrict.mpr (hff' â–¸ map_continuous f)
    use f'
    simp only [cfc_apply f' a]
    congr!
    exact hff'.symm

variable [TopologicalRing A] [ContinuousStar A]

variable (ğ•œ) in
theorem cfcHom_range {a : A} (ha : p a) [CompactSpace (spectrum ğ•œ a)] :
    (cfcHom ha (R := ğ•œ)).range = elementalStarAlgebra ğ•œ a := by
  rw [StarAlgHom.range_eq_map_top, â† polynomialFunctions.starClosure_topologicalClosure,
    â† StarSubalgebra.topologicalClosure_map _ _ (cfcHom_closedEmbedding ha (R := ğ•œ)),
    polynomialFunctions.starClosure_eq_adjoin_X, StarAlgHom.map_adjoin]
  congr
  rw [Set.image_singleton, Polynomial.toContinuousMapOnAlgHom_apply,
    Polynomial.toContinuousMapOn_X_eq_restrict_id, cfcHom_id ha]

theorem CFC.induction_on (P : A â†’ Prop) {a : A} (ha : p a)
    [CompactSpace (spectrum ğ•œ a)] (f : C(spectrum ğ•œ a, ğ•œ))
    (self : P a) (star_self : P (star a)) (algebraMap : âˆ€ r : ğ•œ, P (algebraMap ğ•œ A r))
    (add : âˆ€ gâ‚ gâ‚‚, P (cfcHom ha gâ‚) â†’ P (cfcHom ha gâ‚‚) â†’ P (cfcHom ha (R := ğ•œ) (gâ‚ + gâ‚‚)))
    (mul : âˆ€ gâ‚ gâ‚‚, P (cfcHom ha gâ‚) â†’ P (cfcHom ha gâ‚‚) â†’ P (cfcHom ha (R := ğ•œ) (gâ‚ * gâ‚‚)))
    (closure : âˆ€ s, (âˆ€ g âˆˆ s, P (cfcHom ha g)) â†’ âˆ€ g' âˆˆ closure s, P (cfcHom ha (R := ğ•œ) g')) :
    P (cfcHom ha f) := by
  have hf : cfcHom ha f âˆˆ elementalStarAlgebra ğ•œ a := cfcHom_range ğ•œ ha â–¸ Set.mem_range_self _
  refine elementalStarAlgebra.induction_on ğ•œ hf self star_self algebraMap ?_ ?_ ?_
  all_goals simp only [â† cfcHom_range ğ•œ ha]
  Â· rintro - âŸ¨f, rflâŸ© - âŸ¨g, rflâŸ© hf hg
    simpa using add f g hf hg
  Â· rintro - âŸ¨f, rflâŸ© - âŸ¨g, rflâŸ© hf hg
    simpa using mul f g hf hg
  Â· show âˆ€ s âŠ† Set.range (cfcHom ha), _
    simpa only [Set.forall_subset_range_iff, Set.forall_mem_image,
      (cfcHom_closedEmbedding ha).closure_image_eq] using closure

-- Do we actually want this version?
open Topology UniformOnFun in
theorem CFC.induction_on' {P : A â†’ Prop} {a x : A}
    [CompactSpace (spectrum ğ•œ a)] (hx : x âˆˆ Set.range (cfc (R := ğ•œ) Â· a))
    (self : P a) (star_self : P (star a)) (algebraMap : âˆ€ r : ğ•œ, P (algebraMap ğ•œ A r))
    (add : âˆ€ gâ‚ gâ‚‚, ContinuousOn gâ‚ (spectrum ğ•œ a) â†’ ContinuousOn gâ‚‚ (spectrum ğ•œ a) â†’
      P (cfc gâ‚ a) â†’ P (cfc gâ‚‚ a) â†’ P (cfc (gâ‚ + gâ‚‚) a))
    (mul : âˆ€ gâ‚ gâ‚‚, ContinuousOn gâ‚ (spectrum ğ•œ a) â†’ ContinuousOn gâ‚‚ (spectrum ğ•œ a) â†’
      P (cfc gâ‚ a) â†’ P (cfc gâ‚‚ a) â†’ P (cfc (gâ‚ * gâ‚‚) a))
    (closure : âˆ€ s : Set (ğ•œ â†’ ğ•œ), (âˆ€ g âˆˆ s, ContinuousOn g (spectrum ğ•œ a) âˆ§ P (cfc g a)) â†’
      âˆ€ g' âˆˆ closure (ofFun {spectrum ğ•œ a} '' s), P (cfc (toFun {spectrum ğ•œ a} g') a)) :
    P x := by
  obtain âŸ¨f, rflâŸ© := hx
  refine cfc_cases P a f (by simpa using algebraMap 0) fun hf ha â†¦ ?_
  have keyâ‚ (g : C(spectrum ğ•œ a, ğ•œ)) :
      ContinuousOn (Function.extend Subtype.val g (0 : ğ•œ â†’ ğ•œ)) (spectrum ğ•œ a) := by
    rw [continuousOn_iff_continuous_restrict]
    convert map_continuous g
    ext
    simp [Subtype.val_injective.extend_apply]
  have keyâ‚‚ (g : C(spectrum ğ•œ a, ğ•œ)) : g = âŸ¨_, (keyâ‚ g).restrictâŸ© := by
    ext
    simp [Subtype.val_injective.extend_apply]
  refine CFC.induction_on P ha _ self star_self algebraMap (fun gâ‚ gâ‚‚ â†¦ ?add')
    (fun gâ‚ gâ‚‚ â†¦ ?mul') (fun s hs g' hg' â†¦ ?closure')
  case add' =>
    rw [keyâ‚‚ (gâ‚ + gâ‚‚), keyâ‚‚ gâ‚, keyâ‚‚ gâ‚‚, â† cfc_apply _ a, â† cfc_apply _ a, â† cfc_apply _ a]
    convert add _ _ (keyâ‚ gâ‚) (keyâ‚ gâ‚‚) using 3
    refine cfc_congr fun x hx â†¦ ?_
    lift x to spectrum ğ•œ a using hx
    simp [Subtype.val_injective.extend_apply]
  case mul' =>
    rw [keyâ‚‚ (gâ‚ * gâ‚‚), keyâ‚‚ gâ‚, keyâ‚‚ gâ‚‚, â† cfc_apply _ a, â† cfc_apply _ a, â† cfc_apply _ a]
    convert mul _ _ (keyâ‚ gâ‚) (keyâ‚ gâ‚‚) using 3
    refine cfc_congr fun x hx â†¦ ?_
    lift x to spectrum ğ•œ a using hx
    simp [Subtype.val_injective.extend_apply]
  case closure' =>
    specialize
      closure ((fun g : C(spectrum ğ•œ a, ğ•œ) â†¦ Function.extend Subtype.val g (0 : ğ•œ â†’ ğ•œ)) '' s) <| by
        rintro - âŸ¨g, hg, rflâŸ©
        refine âŸ¨keyâ‚ g, ?_âŸ©
        simp only [cfc_apply _ a ha (keyâ‚ g), â† keyâ‚‚ g]
        exact hs g hg
    specialize closure (ofFun {spectrum ğ•œ a} <| Function.extend Subtype.val g' (0 : ğ•œ â†’ ğ•œ))
    simp only [toFun_ofFun, cfc_apply _ a ha (keyâ‚ g'), â† keyâ‚‚ g'] at closure
    apply closure
    rw [Set.image_image]
    refine mem_closure_image ?_ hg'
    simp only [ContinuousAt, UniformOnFun.tendsto_iff_tendstoUniformlyOn, Set.mem_singleton_iff,
      toFun_ofFun, forall_eq, tendstoUniformlyOn_iff_tendstoUniformly_comp_coe, Function.comp_def,
      Subtype.val_injective.extend_apply]
    rw [â† ContinuousMap.tendsto_iff_tendstoUniformly]
    exact Filter.tendsto_id

variable [T2Space A]

theorem commute_cfc {a b : A} [CompactSpace (spectrum ğ•œ a)] (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfc f a) b :=
  cfc_cases (fun x â†¦ Commute x b) a f (Commute.zero_left _) fun hf ha â†¦ by
    apply CFC.induction_on (P := fun x â†¦ Commute x b) ha âŸ¨_, hf.restrictâŸ© hbâ‚ hbâ‚‚
      (Algebra.commute_algebraMap_left Â· _) (fun _ _ â†¦ ?_) (fun _ _ â†¦ ?_) (fun s hs g hg â†¦ ?_)
    Â· simpa using Commute.add_left
    Â· simpa using Commute.mul_left
    Â· refine Set.EqOn.closure hs ?_ ?_ hg
      all_goals fun_prop

protected theorem IsSelfAdjoint.commute_cfc {a b : A} [CompactSpace (spectrum ğ•œ a)]
    (ha : IsSelfAdjoint a) (hbâ‚ : Commute a b) (f : ğ•œ â†’ ğ•œ) : Commute (cfc f a) b :=
  commute_cfc hbâ‚ (ha.star_eq.symm â–¸ hbâ‚) f

-- Move this to `Mathlib.Algebra.Star.Basic`, not necessary for this PR
theorem IsSelfAdjoint.commute_star_right {R : Type*} [Mul R] [StarMul R] {x : R}
    (hx : IsSelfAdjoint x) (y : R) : Commute x (star y) â†” Commute x y := by
  simpa [hx.star_eq] using commute_star_star (x := x) (y := y)

-- Move this to `Mathlib.Algebra.Star.Basic`, not necessary for this PR
theorem IsSelfAdjoint.commute_star_left {R : Type*} [Mul R] [StarMul R] {x : R}
    (hx : IsSelfAdjoint x) (y : R) : Commute (star y) x â†” Commute y x := by
  simpa [Commute.symm_iff (a := x)] using hx.commute_star_right y

end RCLike

section NonUnital

variable {ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [NonUnitalRing A] [StarRing A] [Module ğ•œ A]
variable [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A]
variable [TopologicalSpace A] [NonUnitalContinuousFunctionalCalculus ğ•œ p]

variable (ğ•œ) in
theorem cfcâ‚™_range {a : A} (ha : p a) :
    Set.range (cfcâ‚™ (R := ğ•œ) Â· a) = NonUnitalStarAlgHom.range (cfcâ‚™Hom ha (R := ğ•œ)) := by
  ext x
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    exact cfcâ‚™_cases (Â· âˆˆ NonUnitalStarAlgHom.range (cfcâ‚™Hom ha (R := ğ•œ))) a f (zero_mem _)
      (by simp)
  Â· simp only [NonUnitalStarAlgHom.coe_range, Set.mem_range]
    rintro âŸ¨f, rflâŸ©
    rw [cfcâ‚™Hom_eq_cfcâ‚™_extend 0]
    exact âŸ¨_, rflâŸ©

local notation "Ïƒâ‚™" => quasispectrum

open ContinuousMapZero in
@[simp]
lemma cfcâ‚™Hom_id' {R A : Type*} {p : A â†’ Prop} [CommSemiring R] [Nontrivial R] [StarRing R]
    [MetricSpace R] [TopologicalSemiring R] [ContinuousStar R] [NonUnitalRing A] [StarRing A]
    [TopologicalSpace A] [Module R A] [IsScalarTower R A A] [SMulCommClass R A A]
    [NonUnitalContinuousFunctionalCalculus R p] {a : A} (ha : p a) :
    (cfcâ‚™Hom ha) (.id rfl : C(Ïƒâ‚™ R a, R)â‚€) = a :=
  cfcâ‚™Hom_id ha


open ContinuousMapZero NonUnitalStarSubalgebra in
theorem CFC.induction_on'' (P : A â†’ Prop) {a : A} (ha : p a)
    [CompactSpace (Ïƒâ‚™ ğ•œ a)] (f : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€)
    (self : P a) (star_self : P (star a))
    (smul : âˆ€ r : ğ•œ, âˆ€ g, P (cfcâ‚™Hom ha g) â†’ P (cfcâ‚™Hom ha (R := ğ•œ) (r â€¢ g)))
    (add : âˆ€ gâ‚ gâ‚‚, P (cfcâ‚™Hom ha gâ‚) â†’ P (cfcâ‚™Hom ha gâ‚‚) â†’ P (cfcâ‚™Hom ha (R := ğ•œ) (gâ‚ + gâ‚‚)))
    (mul : âˆ€ gâ‚ gâ‚‚, P (cfcâ‚™Hom ha gâ‚) â†’ P (cfcâ‚™Hom ha gâ‚‚) â†’ P (cfcâ‚™Hom ha (R := ğ•œ) (gâ‚ * gâ‚‚)))
    (closure : âˆ€ s, (âˆ€ g âˆˆ s, P (cfcâ‚™Hom ha g)) â†’ âˆ€ g' âˆˆ closure s, P (cfcâ‚™Hom ha (R := ğ•œ) g')) :
    P (cfcâ‚™Hom ha f) := by
  refine closure (NonUnitalStarAlgebra.adjoin ğ•œ {(ContinuousMapZero.id rfl : C(Ïƒâ‚™ ğ•œ a, ğ•œ)â‚€)})
    (fun f hf â†¦ ?_) f <| by simp [(ContinuousMapZero.adjoin_id_dense (s := Ïƒâ‚™ ğ•œ a) rfl).closure_eq]
  rw [SetLike.mem_coe, â† mem_toNonUnitalSubalgebra,
    NonUnitalStarAlgebra.adjoin_toNonUnitalSubalgebra] at hf
  induction hf using NonUnitalAlgebra.adjoin_induction' with
  | mem g hg =>
    simp only [Set.star_singleton, Set.union_singleton, Set.mem_insert_iff,
      Set.mem_singleton_iff] at hg
    obtain (rfl | rfl) := hg
    all_goals simpa [map_star]
  | add gâ‚ _ gâ‚‚ _ hgâ‚ hgâ‚‚ => exact add _ _ hgâ‚ hgâ‚‚
  | zero => simpa using smul 0 (.id rfl) (by simpa)
  | mul gâ‚ _ gâ‚‚ _ hgâ‚ hgâ‚‚ => exact mul _ _ hgâ‚ hgâ‚‚
  | smul r g _ hg => exact smul r g hg

variable [T2Space A] [TopologicalRing A]

theorem commute_cfcâ‚™ {a b : A} [CompactSpace (Ïƒâ‚™ ğ•œ a)] (hbâ‚ : Commute a b)
    (hbâ‚‚ : Commute (star a) b) (f : ğ•œ â†’ ğ•œ) :
    Commute (cfcâ‚™ f a) b :=
  cfcâ‚™_cases (fun x â†¦ Commute x b) a f (Commute.zero_left _) fun hf hfâ‚€ ha â†¦ by
    apply CFC.induction_on'' (P := fun x â†¦ Commute x b) ha âŸ¨âŸ¨_, hf.restrictâŸ©, hfâ‚€âŸ© hbâ‚ hbâ‚‚
      (fun _ _ â†¦ ?_) (fun _ _ â†¦ ?_) (fun _ _ â†¦ ?_) (fun s hs g hg â†¦ ?_)
    Â· simpa using (Commute.smul_left Â· _)
    Â· simpa using Commute.add_left
    Â· simpa using Commute.mul_left
    Â· refine Set.EqOn.closure hs ?_ ?_ hg
      all_goals fun_prop

protected theorem IsSelfAdjoint.commute_cfcâ‚™ {a b : A} [CompactSpace (Ïƒâ‚™ ğ•œ a)]
    (ha : IsSelfAdjoint a) (hbâ‚ : Commute a b) (f : ğ•œ â†’ ğ•œ) : Commute (cfcâ‚™ f a) b :=
  commute_cfcâ‚™ hbâ‚ (ha.star_eq.symm â–¸ hbâ‚) f

variable [StarModule ğ•œ A] [TopologicalRing A] [ContinuousStar A] [ContinuousConstSMul ğ•œ A]

section fooâ‚

variable {F R A B : Type*} [CommSemiring R] [StarRing R]
variable [NonUnitalSemiring A] [StarRing A] [NonUnitalSemiring B] [StarRing B]
variable [Module R A] [IsScalarTower R A A] [SMulCommClass R A A] [StarModule R A]
variable [Module R B]
variable [FunLike F A B] [NonUnitalAlgHomClass F R A B] [NonUnitalStarAlgHomClass F R A B]

open NonUnitalStarSubalgebra in
lemma NonUnitalStarAlgHom.range_eq_map_top (Ï† : F) :
    NonUnitalStarAlgHom.range Ï† = map Ï† (âŠ¤ : NonUnitalStarSubalgebra R A) :=
  NonUnitalStarSubalgebra.ext fun x =>
    âŸ¨by rintro âŸ¨a, haâŸ©; exact âŸ¨a, by simp, haâŸ©, by rintro âŸ¨a, -, haâŸ©; exact âŸ¨a, haâŸ©âŸ©

end fooâ‚

section fooâ‚‚

theorem ContinuousMapZero.topologicalClosure_adjoin_id {ğ•œ : Type*} [RCLike ğ•œ] {s : Set ğ•œ} [Zero s]
    (h0 : (0 : s) = (0 : ğ•œ)) [CompactSpace s] :
    (NonUnitalStarAlgebra.adjoin ğ•œ {ContinuousMapZero.id h0}).topologicalClosure = âŠ¤ :=
  SetLike.ext'_iff.mpr (ContinuousMapZero.adjoin_id_dense h0).closure_eq

end fooâ‚‚


open NonUnitalStarSubalgebra in
-- it would be nice if this were about non-unital star subalgebras, but we don't have
-- the topological closure of those yet.
variable (ğ•œ) in
theorem cfcâ‚™Hom_range {a : A} (ha : p a) [CompactSpace (Ïƒâ‚™ ğ•œ a)] :
    NonUnitalStarAlgHom.range (cfcâ‚™Hom ha (R := ğ•œ)) =
      (NonUnitalStarAlgebra.adjoin ğ•œ {a}).topologicalClosure := by
  rw [NonUnitalStarAlgHom.range_eq_map_top, â† ContinuousMapZero.topologicalClosure_adjoin_id rfl,
    â† topologicalClosure_map _ _ (cfcâ‚™Hom_closedEmbedding ha (R := ğ•œ)),
    NonUnitalStarAlgHom.map_adjoin]
  congr!
  simp [cfcâ‚™Hom_id' ha]

open ContinuousMapZero

end NonUnital
