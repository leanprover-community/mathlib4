/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.Normed.Algebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unital
import Mathlib.MeasureTheory.Integral.SetIntegral

/-!
# Integrals and the continuous functional calculus

This file gives results about integrals of the form `âˆ« x, cfc (f x) a`. Most notably, we show
that the integral commutes with the continuous functional calculus under appropriate conditions.

## Main declarations

+ `cfc_integral`: given a function `f : X â†’ ð•œ â†’ ð•œ`, we have that
  `cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼`
  under appropriate conditions

## TODO

+ Prove a similar result for the non-unital case
+ Lift this to the case where the CFC is over `â„â‰¥0`
+ Use this to prove operator monotonicity and concavity/convexity of `rpow` and `log`
-/

open MeasureTheory

section unital

variable {X : Type*} {ð•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ð•œ]
  [MeasurableSpace X] {Î¼ : Measure X}
  [NormedRing A] [StarRing A] [NormedAlgebra ð•œ A] [NormedAlgebra â„ A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ð•œ p]

lemma cfcL_integral (a : A) (f : X â†’ C(spectrum ð•œ a, ð•œ)) (hfâ‚ : Integrable f Î¼) (ha : p a) :
    âˆ« x, cfcL ha (f x) âˆ‚Î¼ = cfcL ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcHom_integral (a : A) (f : X â†’ C(spectrum ð•œ a, ð•œ)) (hfâ‚ : Integrable f Î¼) (ha : p a) :
    âˆ« x, cfcHom ha (f x) âˆ‚Î¼ = cfcHom ha (âˆ« x, f x âˆ‚Î¼) := by
  have hâ‚ : âˆ« x, cfcHom ha (f x) âˆ‚Î¼ = âˆ« x, cfcL ha (f x) âˆ‚Î¼ := by
    refine integral_congr_ae ?_
    filter_upwards with x
    simp only [cfcHom_eq_cfcL ha]
  rw [hâ‚, cfcHom_eq_cfcL ha]
  exact cfcL_integral a f hfâ‚ ha

open ContinuousMap in
/-- The continuous functional calculus commutes with integration. -/
lemma cfc_integral [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ð•œ â†’ ð•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ð•œ a, ð•œ)]
    (hf : Continuous (fun x => (spectrum ð•œ a).restrict (f x)).uncurry)
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ð•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_integrable : Integrable bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  have ha : p a := ha   -- Needed due to weird autoparam bug
  have hcont : âˆ€ x, ContinuousOn (f x) (spectrum ð•œ a) := by
    intro x
    rw [continuousOn_iff_continuous_restrict]
    exact hf.uncurry_left x
  let fc : X â†’ C(spectrum ð•œ a, ð•œ) := fun x => âŸ¨_, (hcont x).restrictâŸ©
  have fc_cont : Continuous fc := by
    refine continuous_of_continuous_uncurry fc ?_
    simp only [coe_mk, restrict_apply, fc]
    exact hf
  have fc_integrable : Integrable fc Î¼ := by
    refine âŸ¨fc_cont.aestronglyMeasurable, ?_âŸ©
    refine HasFiniteIntegral.mono (g := bound) (hbound_integrable.hasFiniteIntegral) ?_
    refine Filter.Eventually.of_forall fun x => ?_
    rw [norm_le _ (norm_nonneg (bound x))]
    intro z
    simp only [coe_mk, restrict_apply, fc]
    exact hbound x z.1 z.2
  have hcontâ‚‚ : ContinuousOn (fun r => âˆ« x, f x r âˆ‚Î¼) (spectrum ð•œ a) := by
    have hâ‚ : (spectrum ð•œ a).restrict (fun r => âˆ« x, f x r âˆ‚Î¼) = fun r => (âˆ« x, fc x âˆ‚Î¼) r := by
      ext
      simp only [integral_apply fc_integrable, Set.restrict_apply, coe_mk, fc]
    rw [continuousOn_iff_continuous_restrict, hâ‚]
    exact ContinuousMap.continuous _
  have hrw : (fun x => cfc (f x) a) =áµ[Î¼] fun x => cfcHom ha (fc x) := by
    refine Filter.Eventually.of_forall fun x => ?_
    simp only
    rw [cfc_apply ..]
  rw [integral_congr_ae hrw, cfc_apply .., cfcHom_integral _ _ fc_integrable]
  congr 1
  ext
  simp only [coe_mk, restrict_apply, integral_apply fc_integrable, Set.restrict_apply, coe_mk, fc]

end unital
