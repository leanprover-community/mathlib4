/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Anatole Dedecker
-/

import Mathlib.Analysis.Normed.Algebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unital
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.NonUnital
import Mathlib.MeasureTheory.SpecificCodomains.ContinuousMapZero

/-!
# Integrals and the continuous functional calculus

This file gives results about integrals of the form `âˆ« x, cfc (f x) a`. Most notably, we show
that the integral commutes with the continuous functional calculus under appropriate conditions.

## Main declarations

+ `cfc_setIntegral` (resp. `cfc_integral`): given a function `f : X â†’ ğ•œ â†’ ğ•œ`, we have that
  `cfc (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼`
  under appropriate conditions (resp. with `s = univ`)
+ `cfcâ‚™_setIntegral`, `cfcâ‚™_integral`: the same for the non-unital continuous functional calculus
+ `integrableOn_cfc`, `integrableOn_cfcâ‚™`, `integrable_cfc`, `integrable_cfcâ‚™`:
  functions of the form `fun x => cfc (f x) a` are integrable.

## Implementation Notes

The lemmas mentioned above are stated under much stricter hypotheses than necessary
(typically, simultaneous continuity of `f` in the parameter and the spectrum element).
They all come with primed version which only assume what's needed, and may be used together
with the API developed in `Mathlib.MeasureTheory.SpecificCodomains.ContinuousMap`.

## TODO

+ Lift this to the case where the CFC is over `â„â‰¥0`
+ Use this to prove operator monotonicity and concavity/convexity of `rpow` and `log`
-/

open MeasureTheory Topology
open scoped ContinuousMapZero

section unital

open ContinuousMap

variable {X : Type*} {ğ•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
  [MeasurableSpace X] {Î¼ : Measure X}
  [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A]
  [ContinuousFunctionalCalculus ğ•œ A p]
  [CompleteSpace A]

lemma cfcL_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ)) (hfâ‚ : Integrable f Î¼)
    (ha : p a := by cfc_tac) :
    âˆ« x, cfcL (a := a) ha (f x) âˆ‚Î¼ = cfcL (a := a) ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcL_integrable (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ))
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x â†¦ cfcL (a := a) ha (f x)) Î¼ :=
  ContinuousLinearMap.integrable_comp _ hfâ‚

lemma cfcHom_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ))
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcHom (a := a) ha (f x) âˆ‚Î¼ = cfcHom (a := a) ha (âˆ« x, f x âˆ‚Î¼) :=
  cfcL_integral a f hfâ‚ ha

/-- An integrability criterion for the continuous functional calculus.
For a version with stronger assumptions which in practice are often easier to verify, see
`integrable_cfc`. -/
lemma integrable_cfc' (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hf : Integrable
      (fun x : X => mkD ((spectrum ğ•œ a).restrict (f x)) 0) Î¼)
    (ha : p a := by cfc_tac) :
    Integrable (fun x => cfc (f x) a) Î¼ := by
  conv in cfc _ _ => rw [cfc_eq_cfcL_mkD _ a]
  exact cfcL_integrable _ _ hf ha

/-- An integrability criterion for the continuous functional calculus.
For a version with stronger assumptions which in practice are often easier to verify, see
`integrableOn_cfc`. -/
lemma integrableOn_cfc' {s : Set X} (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hf : IntegrableOn
      (fun x : X => mkD ((spectrum ğ•œ a).restrict (f x)) 0) s Î¼)
    (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfc (f x) a) s Î¼ := by
  exact integrable_cfc' _ _ hf ha

open Set Function in
/-- An integrability criterion for the continuous functional calculus.
This version assumes joint continuity of `f`, see `integrable_cfc'` for a statement
with weaker assumptions. -/
lemma integrable_cfc [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (univ Ã—Ë¢ spectrum ğ•œ a))
    (bound_ge : âˆ€áµ x âˆ‚Î¼, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x => cfc (f x) a) Î¼ := by
  refine integrable_cfc' _ _ âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_mkD_restrict_of_uncurry _ _ hf
  Â· refine hasFiniteIntegral_mkD_restrict_of_bound f _ ?_ bound bound_int bound_ge
    exact .of_forall fun x â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨Set.mem_univ _, hzâŸ©

open Set Function in
/-- An integrability criterion for the continuous functional calculus.
This version assumes joint continuity of `f`, see `integrableOn_cfc'` for a statement
with weaker assumptions. -/
lemma integrableOn_cfc [TopologicalSpace X] [OpensMeasurableSpace X] {s : Set X}
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (s Ã—Ë¢ spectrum ğ•œ a))
    (bound_ge : âˆ€áµ x âˆ‚(Î¼.restrict s), âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfc (f x) a) s Î¼ := by
  refine integrableOn_cfc' _ _ âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_restrict_mkD_restrict_of_uncurry hs _ _ hf
  Â· refine hasFiniteIntegral_mkD_restrict_of_bound f _ ?_ bound bound_int bound_ge
    exact ae_restrict_of_forall_mem hs fun x hx â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨hx, hzâŸ©

open Set in
/-- The continuous functional calculus commutes with integration.
For a version with stronger assumptions which in practice are often easier to verify, see
`cfc_integral`. -/
lemma cfc_integral' [NormedSpace â„ A] (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hfâ‚ : âˆ€áµ x âˆ‚Î¼, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : Integrable
      (fun x : X => mkD ((spectrum ğ•œ a).restrict (f x)) 0) Î¼)
    (ha : p a := by cfc_tac) :
    cfc (fun z => âˆ« x, f x z âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  have keyâ‚ (z : spectrum ğ•œ a) :
      âˆ« x, f x z âˆ‚Î¼ = (âˆ« x, mkD ((spectrum ğ•œ a).restrict (f x)) 0 âˆ‚Î¼) z := by
    rw [integral_apply hfâ‚‚]
    refine integral_congr_ae ?_
    filter_upwards [hfâ‚] with x cont_x
    rw [mkD_apply_of_continuousOn cont_x]
  have keyâ‚‚ (z : spectrum ğ•œ a) :
      âˆ« x, f x z âˆ‚Î¼ = mkD ((spectrum ğ•œ a).restrict (fun z â†¦ âˆ« x, f x z âˆ‚Î¼)) 0 z := by
    rw [mkD_apply_of_continuousOn]
    rw [continuousOn_iff_continuous_restrict]
    refine continuous_congr keyâ‚ |>.mpr ?_
    exact map_continuous (âˆ« x, mkD ((spectrum ğ•œ a).restrict (f x)) 0 âˆ‚Î¼)
  simp_rw [cfc_eq_cfcL_mkD _ a, cfcL_integral a _ hfâ‚‚ ha]
  congr
  ext z
  rw [â† keyâ‚, keyâ‚‚]

open Set in
/-- The continuous functional calculus commutes with integration.
For a version with stronger assumptions which in practice are often easier to verify, see
`cfc_setIntegral`. -/
lemma cfc_setIntegral' {s : Set X} [NormedSpace â„ A] (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hfâ‚ : âˆ€áµ x âˆ‚(Î¼.restrict s), ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : IntegrableOn
      (fun x : X => mkD ((spectrum ğ•œ a).restrict (f x)) 0) s Î¼)
    (ha : p a := by cfc_tac) :
    cfc (fun z => âˆ« x in s, f x z âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼ :=
  cfc_integral' _ _ hfâ‚ hfâ‚‚ ha

open Function Set in
/-- The continuous functional calculus commutes with integration.
This version assumes joint continuity of `f`, see `cfc_integral'` for a statement
with weaker assumptions. -/
lemma cfc_integral [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X]
    (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (univ Ã—Ë¢ spectrum ğ•œ a))
    (bound_ge : âˆ€áµ x âˆ‚Î¼, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  have : âˆ€áµ (x : X) âˆ‚Î¼, ContinuousOn (f x) (spectrum ğ•œ a) := .of_forall fun x â†¦
    hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨Set.mem_univ _, hzâŸ©
  refine cfc_integral' _ _ this âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_mkD_restrict_of_uncurry _ _ hf
  Â· exact hasFiniteIntegral_mkD_restrict_of_bound f _ this bound bound_int bound_ge

open Function Set in
/-- The continuous functional calculus commutes with integration.
This version assumes joint continuity of `f`, see `cfc_setIntegral'` for a statement
with weaker assumptions. -/
lemma cfc_setIntegral [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] {s : Set X}
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (s Ã—Ë¢ spectrum ğ•œ a))
    (bound_ge : âˆ€áµ x âˆ‚(Î¼.restrict s), âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼ := by
  have : âˆ€áµ (x : X) âˆ‚(Î¼.restrict s), ContinuousOn (f x) (spectrum ğ•œ a) :=
    ae_restrict_of_forall_mem hs fun x hx â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨hx, hzâŸ©
  refine cfc_setIntegral' _ _ this âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_restrict_mkD_restrict_of_uncurry hs _ _ hf
  Â· exact hasFiniteIntegral_mkD_restrict_of_bound f _ this bound bound_int bound_ge

end unital

section nonunital

open ContinuousMapZero

variable {X : Type*} {ğ•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
  [MeasurableSpace X] {Î¼ : Measure X} [NonUnitalNormedRing A] [StarRing A]
  [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
  [NonUnitalContinuousFunctionalCalculus ğ•œ A p]
  [CompleteSpace A]

lemma cfcâ‚™L_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€)
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcâ‚™L (a := a) ha (f x) âˆ‚Î¼ = cfcâ‚™L (a := a) ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcâ‚™Hom_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€)
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcâ‚™Hom (a := a) ha (f x) âˆ‚Î¼ = cfcâ‚™Hom (a := a) ha (âˆ« x, f x âˆ‚Î¼) :=
  cfcâ‚™L_integral a f hfâ‚ ha

lemma cfcâ‚™L_integrable (a : A) (f : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€)
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x â†¦ cfcâ‚™L (a := a) ha (f x)) Î¼ :=
  ContinuousLinearMap.integrable_comp _ hfâ‚

/-- An integrability criterion for the continuous functional calculus.
For a version with stronger assumptions which in practice are often easier to verify, see
`integrable_cfcâ‚™`. -/
lemma integrable_cfcâ‚™' (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hf : Integrable
      (fun x : X => mkD ((quasispectrum ğ•œ a).restrict (f x)) 0) Î¼)
    (ha : p a := by cfc_tac) :
    Integrable (fun x => cfcâ‚™ (f x) a) Î¼ := by
  conv in cfcâ‚™ _ _ => rw [cfcâ‚™_eq_cfcâ‚™L_mkD _ a]
  exact cfcâ‚™L_integrable _ _ hf ha

/-- An integrability criterion for the continuous functional calculus.
For a version with stronger assumptions which in practice are often easier to verify, see
`integrableOn_cfcâ‚™`. -/
lemma integrableOn_cfcâ‚™' {s : Set X} (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hf : IntegrableOn
      (fun x : X => mkD ((quasispectrum ğ•œ a).restrict (f x)) 0) s Î¼)
    (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfcâ‚™ (f x) a) s Î¼ := by
  exact integrable_cfcâ‚™' _ _ hf ha

open Set Function in
/-- An integrability criterion for the continuous functional calculus.
This version assumes joint continuity of `f`, see `integrable_cfcâ‚™'` for a statement
with weaker assumptions. -/
lemma integrable_cfcâ‚™ [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (univ Ã—Ë¢ quasispectrum ğ•œ a))
    (f_zero : âˆ€áµ x âˆ‚Î¼, f x 0 = 0)
    (bound_ge : âˆ€áµ x âˆ‚Î¼, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x => cfcâ‚™ (f x) a) Î¼ := by
  refine integrable_cfcâ‚™' _ _ âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_mkD_restrict_of_uncurry _ _ hf f_zero
  Â· refine hasFiniteIntegral_mkD_restrict_of_bound f _ ?_ f_zero bound bound_int bound_ge
    exact .of_forall fun x â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨Set.mem_univ _, hzâŸ©

open Set Function in
/-- An integrability criterion for the continuous functional calculus.
This version assumes joint continuity of `f`, see `integrableOn_cfcâ‚™'` for a statement
with weaker assumptions. -/
lemma integrableOn_cfcâ‚™ [TopologicalSpace X] [OpensMeasurableSpace X] {s : Set X}
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (s Ã—Ë¢ quasispectrum ğ•œ a))
    (f_zero : âˆ€áµ x âˆ‚(Î¼.restrict s), f x 0 = 0)
    (bound_ge : âˆ€áµ x âˆ‚(Î¼.restrict s), âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfcâ‚™ (f x) a) s Î¼ := by
  refine integrableOn_cfcâ‚™' _ _ âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_restrict_mkD_restrict_of_uncurry hs _ _ hf f_zero
  Â· refine hasFiniteIntegral_mkD_restrict_of_bound f _ ?_ f_zero bound bound_int bound_ge
    exact ae_restrict_of_forall_mem hs fun x hx â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨hx, hzâŸ©

open Set in
/-- The continuous functional calculus commutes with integration.
For a version with stronger assumptions which in practice are often easier to verify, see
`cfcâ‚™_integral`. -/
lemma cfcâ‚™_integral' [NormedSpace â„ A] (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hfâ‚ : âˆ€áµ x âˆ‚Î¼, ContinuousOn (f x) (quasispectrum ğ•œ a))
    (hfâ‚‚ : âˆ€áµ x âˆ‚Î¼, f x 0 = 0)
    (hfâ‚ƒ : Integrable
      (fun x : X => mkD ((quasispectrum ğ•œ a).restrict (f x)) 0) Î¼)
    (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun z => âˆ« x, f x z âˆ‚Î¼) a = âˆ« x, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  have keyâ‚ (z : quasispectrum ğ•œ a) :
      âˆ« x, f x z âˆ‚Î¼ = (âˆ« x, mkD ((quasispectrum ğ•œ a).restrict (f x)) 0 âˆ‚Î¼) z := by
    rw [integral_apply hfâ‚ƒ]
    refine integral_congr_ae ?_
    filter_upwards [hfâ‚, hfâ‚‚] with x cont_x zero_x
    rw [mkD_apply_of_continuousOn cont_x zero_x]
  have keyâ‚‚ (z : quasispectrum ğ•œ a) :
      âˆ« x, f x z âˆ‚Î¼ = mkD ((quasispectrum ğ•œ a).restrict (fun z â†¦ âˆ« x, f x z âˆ‚Î¼)) 0 z := by
    rw [mkD_apply_of_continuousOn]
    Â· rw [continuousOn_iff_continuous_restrict]
      refine continuous_congr keyâ‚ |>.mpr ?_
      exact map_continuous (âˆ« x, mkD ((quasispectrum ğ•œ a).restrict (f x)) 0 âˆ‚Î¼)
    Â· exact integral_eq_zero_of_ae hfâ‚‚
  simp_rw [cfcâ‚™_eq_cfcâ‚™L_mkD _ a, cfcâ‚™L_integral a _ hfâ‚ƒ ha]
  congr
  ext z
  rw [â† keyâ‚, keyâ‚‚]

open Set in
/-- The continuous functional calculus commutes with integration.
For a version with stronger assumptions which in practice are often easier to verify, see
`cfcâ‚™_setIntegral`. -/
lemma cfcâ‚™_setIntegral' {s : Set X} [NormedSpace â„ A] (f : X â†’ ğ•œ â†’ ğ•œ) (a : A)
    (hfâ‚ : âˆ€áµ x âˆ‚(Î¼.restrict s), ContinuousOn (f x) (quasispectrum ğ•œ a))
    (hfâ‚‚ : âˆ€áµ x âˆ‚(Î¼.restrict s), f x 0 = 0)
    (hfâ‚ƒ : IntegrableOn
      (fun x : X => mkD ((quasispectrum ğ•œ a).restrict (f x)) 0) s Î¼)
    (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun z => âˆ« x in s, f x z âˆ‚Î¼) a = âˆ« x in s, cfcâ‚™ (f x) a âˆ‚Î¼ :=
  cfcâ‚™_integral' _ _ hfâ‚ hfâ‚‚ hfâ‚ƒ ha

open Function Set in
/-- The continuous functional calculus commutes with integration.
This version assumes joint continuity of `f`, see `cfcâ‚™_integral'` for a statement
with weaker assumptions. -/
lemma cfcâ‚™_integral [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X]
    (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (univ Ã—Ë¢ quasispectrum ğ•œ a))
    (f_zero : âˆ€áµ x âˆ‚Î¼, f x 0 = 0)
    (bound_ge : âˆ€áµ x âˆ‚Î¼, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  have : âˆ€áµ (x : X) âˆ‚Î¼, ContinuousOn (f x) (quasispectrum ğ•œ a) := .of_forall fun x â†¦
    hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨Set.mem_univ _, hzâŸ©
  refine cfcâ‚™_integral' _ _ this f_zero âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_mkD_restrict_of_uncurry _ _ hf f_zero
  Â· exact hasFiniteIntegral_mkD_restrict_of_bound f _ this f_zero bound bound_int bound_ge

open Function Set in
/-- The continuous functional calculus commutes with integration.
This version assumes joint continuity of `f`, see `cfcâ‚™_setIntegral'` for a statement
with weaker assumptions. -/
lemma cfcâ‚™_setIntegral [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] {s : Set X}
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A)
    [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)]
    (hf : ContinuousOn (uncurry f) (s Ã—Ë¢ quasispectrum ğ•œ a))
    (f_zero : âˆ€áµ x âˆ‚(Î¼.restrict s), f x 0 = 0)
    (bound_ge : âˆ€áµ x âˆ‚(Î¼.restrict s), âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ bound x)
    (bound_int : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  have : âˆ€áµ (x : X) âˆ‚(Î¼.restrict s), ContinuousOn (f x) (quasispectrum ğ•œ a) :=
    ae_restrict_of_forall_mem hs fun x hx â†¦
      hf.comp (Continuous.prodMk_right x).continuousOn fun _ hz â†¦ âŸ¨hx, hzâŸ©
  refine cfcâ‚™_setIntegral' _ _ this f_zero âŸ¨?_, ?_âŸ© ha
  Â· exact aeStronglyMeasurable_restrict_mkD_restrict_of_uncurry hs _ _ hf f_zero
  Â· exact hasFiniteIntegral_mkD_restrict_of_bound f _ this f_zero bound bound_int bound_ge

end nonunital
