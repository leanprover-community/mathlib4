/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.Normed.Algebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unital
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.NonUnital
import Mathlib.MeasureTheory.Integral.Bochner.Set

/-!
# Integrals and the continuous functional calculus

This file gives results about integrals of the form `âˆ« x, cfc (f x) a`. Most notably, we show
that the integral commutes with the continuous functional calculus under appropriate conditions.

## Main declarations

+ `cfc_setIntegral` (resp. `cfc_integral`): given a function `f : X â†’ ğ•œ â†’ ğ•œ`, we have that
  `cfc (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼`
  under appropriate conditions (resp. with `s = univ`)
+ `cfcâ‚™_integral`: the same for the non-unital continuous functional calculus
+ `integrable_cfc_set`, `integrable_cfcâ‚™_set`, `integrable_cfc`, `integrable_cfcâ‚™`:
  functions of the form `fun x => cfc (f x) a` are integrable.

## TODO

+ Lift this to the case where the CFC is over `â„â‰¥0`
+ Use this to prove operator monotonicity and concavity/convexity of `rpow` and `log`
-/

open MeasureTheory Topology
open scoped ContinuousMapZero

section unital

variable {X : Type*} {ğ•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
  [MeasurableSpace X] {Î¼ : Measure X}
  [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ğ•œ A p]

lemma cfcL_integral [NormedAlgebra â„ A] (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ)) (hfâ‚ : Integrable f Î¼)
    (ha : p a := by cfc_tac) :
    âˆ« x, cfcL (a := a) ha (f x) âˆ‚Î¼ = cfcL (a := a) ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcHom_integral [NormedAlgebra â„ A] (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ))
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcHom (a := a) ha (f x) âˆ‚Î¼ = cfcHom (a := a) ha (âˆ« x, f x âˆ‚Î¼) :=
  cfcL_integral a f hfâ‚ ha

open ContinuousMap Classical in
lemma integrable_cfc_set (s : Set X)
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A)
    (hfâ‚ : âˆ€ x âˆˆ s, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : AEStronglyMeasurable (fun x : X =>
      if h : x âˆˆ s then (âŸ¨_, (hfâ‚ x h).restrictâŸ© : C(spectrum ğ•œ a, ğ•œ)) else 0) (Î¼.restrict s))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfc (f x) a) s Î¼ := by
  have ha : p a := ha
  let fc : X â†’ C(spectrum ğ•œ a, ğ•œ) := fun x =>
    if h : x âˆˆ s then âŸ¨_, (hfâ‚ x h).restrictâŸ© else 0
  have hfc : s.EqOn (fun x r => fc x r) (fun x => (spectrum ğ•œ a).restrict (f x)) := by
    intro x hx
    ext
    simp [fc, hx]
  have fc_integrable : IntegrableOn fc s Î¼ := by
    refine âŸ¨hfâ‚‚, ?_âŸ©
    refine hbound_finite_integral.mono ?_
    filter_upwards [ae_restrict_mem hs] with x hx
    rw [norm_le _ (norm_nonneg (bound x))]
    intro z
    have := hfc hx
    simp only at this
    simp only [this, Set.restrict_apply]
    exact hbound x hx z.1 z.2
  have hâ‚ : s.EqOn (fun x : X => cfc (f x) a) (fun x : X => cfcL ha (fc x)) := by
    intro x hx
    dsimp
    rw [cfc_apply ..]
    unfold fc
    simp [hx]
  refine .congr_fun ?_ hâ‚.symm hs
  exact ContinuousLinearMap.integrable_comp _ fc_integrable

open ContinuousMap Classical in
lemma integrable_cfc_set' [TopologicalSpace X] [OpensMeasurableSpace X] (s : Set X)
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : Continuous (fun x : s Ã— spectrum ğ•œ a => f x.1 x.2))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfc (f x) a) s Î¼ := by
  let fc : C(s Ã— (spectrum ğ•œ a), ğ•œ) := âŸ¨fun x => f x.1 x.2, hfâŸ©
  let fcâ‚‚ := fc.curry
  refine integrable_cfc_set s hs f bound a ?_ ?_ hbound hbound_finite_integral
  Â· intro x xs
    rw [continuousOn_iff_continuous_restrict]
    exact (fcâ‚‚ âŸ¨x, xsâŸ©).continuous
  Â· refine ContinuousOn.aestronglyMeasurable ?_ hs
    rw [continuousOn_iff_continuous_restrict]
    refine fcâ‚‚.continuous.congr ?_
    intro âŸ¨x, hxâŸ©
    ext
    simp [fcâ‚‚, fc, hx]

open ContinuousMap Set in
lemma integrable_cfc [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hfâ‚ : âˆ€ x, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : AEStronglyMeasurable (fun x â†¦ (âŸ¨_, hfâ‚ x |>.restrictâŸ© : C(spectrum ğ•œ a, ğ•œ))) Î¼)
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x => cfc (f x) a) Î¼ := by
  rw [â† integrableOn_univ]
  refine integrable_cfc_set univ MeasurableSet.univ f bound a ?_ ?_ ?_ ?_ ha
  Â· exact fun x _ => hfâ‚ x
  Â· simp [hfâ‚‚]
  Â· exact fun x _ => hbound x
  Â· simp [hbound_finite_integral]

open ContinuousMap in
lemma integrable_cfc' [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : Continuous (fun x : X Ã— spectrum ğ•œ a => f x.1 x.2))
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    Integrable (fun x => cfc (f x) a) Î¼ := by
  let fc : C(X Ã— (spectrum ğ•œ a), ğ•œ) := âŸ¨fun x => f x.1 x.2, hfâŸ©
  let fcâ‚‚ := fc.curry
  refine integrable_cfc f bound a ?_ ?_ hbound hbound_finite_integral
  Â· intro x
    rw [continuousOn_iff_continuous_restrict]
    exact (fcâ‚‚ x).continuous
  Â· apply Continuous.aestronglyMeasurable
    exact ContinuousMap.curry âŸ¨_, hfâŸ© |>.continuous

open ContinuousMap Classical in
/-- The continuous functional calculus commutes with integration. -/
lemma cfc_setIntegral [NormedAlgebra â„ A] (s : Set X) (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A)
    (hfâ‚ : âˆ€ x âˆˆ s, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : AEStronglyMeasurable (fun x : X =>
      if h : x âˆˆ s then (âŸ¨_, (hfâ‚ x h).restrictâŸ© : C(spectrum ğ•œ a, ğ•œ)) else 0) (Î¼.restrict s))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼ := by
  let fc : X â†’ C(spectrum ğ•œ a, ğ•œ) := fun x =>
    if h : x âˆˆ s then âŸ¨_, (hfâ‚ x h).restrictâŸ© else 0
  have hfc : s.EqOn (fun x r => fc x r) (fun x => (spectrum ğ•œ a).restrict (f x)) := by
    intro x hx
    ext
    simp [fc, hx]
  have fc_integrable : IntegrableOn fc s Î¼ := by
    refine âŸ¨hfâ‚‚, ?_âŸ©
    refine hbound_finite_integral.mono ?_
    filter_upwards [ae_restrict_mem hs] with x hx
    rw [norm_le _ (norm_nonneg (bound x))]
    intro z
    have := hfc hx
    simp only at this
    simp only [this, Set.restrict_apply]
    exact hbound x hx z.1 z.2
  have ha : p a := ha
  have hâ‚ : s.EqOn (fun x : X => cfc (f x) a) (fun x : X => cfcL ha (fc x)) := by
    intro x hx
    dsimp
    rw [cfc_apply ..]
    unfold fc
    simp [hx]
  have hâ‚‚ : ((spectrum ğ•œ a).restrict
      fun r => âˆ« (x : X) in s, f x r âˆ‚Î¼) = (âˆ« (x : X) in s, fc x âˆ‚Î¼).toFun := by
    ext r
    simp only [Set.restrict_apply, toFun_eq_coe, fc, ContinuousMap.integral_apply fc_integrable _]
    refine integral_congr_ae ?_
    filter_upwards [ae_restrict_mem hs] with x hx
    simp [fc, hx]
  have hcontâ‚‚ : ContinuousOn (fun r => âˆ« x in s, f x r âˆ‚Î¼) (spectrum ğ•œ a) := by
    rw [continuousOn_iff_continuous_restrict]
    convert map_continuous (âˆ« x in s, fc x âˆ‚Î¼)
  rw [setIntegral_congr_fun hs hâ‚, ContinuousLinearMap.integral_comp_comm _ fc_integrable,
      cfcL_apply, cfc_apply ..]
  congr

open ContinuousMap Classical in
/-- The continuous functional calculus commutes with integration. -/
lemma cfc_setIntegral' [NormedAlgebra â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] (s : Set X)
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : Continuous (fun x : s Ã— spectrum ğ•œ a => f x.1 x.2))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfc (f x) a âˆ‚Î¼ := by
  let fc : C(s Ã— (spectrum ğ•œ a), ğ•œ) := âŸ¨fun x => f x.1 x.2, hfâŸ©
  let fcâ‚‚ := fc.curry
  refine cfc_setIntegral s hs f bound a ?_ ?_ hbound hbound_finite_integral
  Â· intro x xs
    rw [continuousOn_iff_continuous_restrict]
    exact (fcâ‚‚ âŸ¨x, xsâŸ©).continuous
  Â· refine ContinuousOn.aestronglyMeasurable ?_ hs
    rw [continuousOn_iff_continuous_restrict]
    refine fcâ‚‚.continuous.congr ?_
    intro âŸ¨x, hxâŸ©
    ext
    simp [fcâ‚‚, fc, hx]

open ContinuousMap Set in
/-- The continuous functional calculus commutes with integration. -/
lemma cfc_integral [NormedAlgebra â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hfâ‚ : âˆ€ x, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : AEStronglyMeasurable (fun x â†¦ (âŸ¨_, hfâ‚ x |>.restrictâŸ© : C(spectrum ğ•œ a, ğ•œ))) Î¼)
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  have : cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = cfc (fun r => âˆ« x in univ, f x r âˆ‚Î¼) a := by
    simp [â† setIntegral_univ]
  rw [â† setIntegral_univ, this]
  refine cfc_setIntegral univ MeasurableSet.univ f bound a ?_ ?_ ?_ ?_
  Â· exact fun x _ => hfâ‚ x
  Â· simp [hfâ‚‚]
  Â· exact fun x _ => hbound x
  Â· simp [hbound_finite_integral]

/-- The continuous functional calculus commutes with integration. -/
lemma cfc_integral' [NormedAlgebra â„ A] [TopologicalSpace X] [OpensMeasurableSpace X]
    (f : X â†’ ğ•œ â†’ ğ•œ) (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : Continuous (fun x : X Ã— spectrum ğ•œ a => f x.1 x.2))
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  let fc : C(X Ã— (spectrum ğ•œ a), ğ•œ) := âŸ¨fun x => f x.1 x.2, hfâŸ©
  let fcâ‚‚ := fc.curry
  refine cfc_integral f bound a ?_ ?_ hbound hbound_finite_integral
  Â· intro x
    rw [continuousOn_iff_continuous_restrict]
    exact (fcâ‚‚ x).continuous
  Â· apply Continuous.aestronglyMeasurable
    exact ContinuousMap.curry âŸ¨_, hfâŸ© |>.continuous

end unital

section nonunital

variable {X : Type*} {ğ•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
  [MeasurableSpace X] {Î¼ : Measure X} [NonUnitalNormedRing A] [StarRing A] [CompleteSpace A]
  [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]
  [NonUnitalContinuousFunctionalCalculus ğ•œ A p]

lemma cfcâ‚™L_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€)
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcâ‚™L (a := a) ha (f x) âˆ‚Î¼ = cfcâ‚™L (a := a) ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcâ‚™Hom_integral [NormedSpace â„ A] (a : A) (f : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€)
    (hfâ‚ : Integrable f Î¼) (ha : p a := by cfc_tac) :
    âˆ« x, cfcâ‚™Hom (a := a) ha (f x) âˆ‚Î¼ = cfcâ‚™Hom (a := a) ha (âˆ« x, f x âˆ‚Î¼) :=
  cfcâ‚™L_integral a f hfâ‚ ha

open ContinuousMapZero Classical in
lemma integrable_cfcâ‚™_set (s : Set X)
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A)
    (hfâ‚ : âˆ€ x âˆˆ s, ContinuousOn (f x) (quasispectrum ğ•œ a))
    (hfâ‚‚ : âˆ€ x âˆˆ s, f x 0 = 0)
    (hfâ‚ƒ : AEStronglyMeasurable (fun x : X =>
      if h : x âˆˆ s then
        (âŸ¨âŸ¨_, (hfâ‚ x h).restrictâŸ©, by simp [hfâ‚‚ x h]âŸ© : C(quasispectrum ğ•œ a, ğ•œ)â‚€)
      else 0) (Î¼.restrict s))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfcâ‚™ (f x) a) s Î¼ := by
  have ha : p a := ha
  let fc : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€ := fun x =>
    if h : x âˆˆ s then âŸ¨âŸ¨_, (hfâ‚ x h).restrictâŸ©, by simp [hfâ‚‚ x h]âŸ© else 0
  have hfc : s.EqOn (fun x r => fc x r) (fun x => (quasispectrum ğ•œ a).restrict (f x)) := by
    intro x hx
    ext
    simp [fc, hx]
  have fc_integrable : IntegrableOn fc s Î¼ := by
    refine âŸ¨hfâ‚ƒ, ?_âŸ©
    refine hbound_finite_integral.mono ?_
    filter_upwards [ae_restrict_mem hs] with x hx
    rw [norm_def, ContinuousMap.norm_le _ (norm_nonneg (bound x))]
    intro z
    have := hfc hx
    simp only at this
    simp only [ContinuousMap.coe_coe, this, Set.restrict_apply, Real.norm_eq_abs, ge_iff_le, fc]
    exact hbound x hx z.1 z.2
  have hâ‚ : s.EqOn (fun x : X => cfcâ‚™ (f x) a) (fun x : X => cfcâ‚™L ha (fc x)) := by
    intro x hx
    dsimp
    rw [cfcâ‚™_apply ..]
    unfold fc
    simp [hx]
  refine .congr_fun ?_ hâ‚.symm hs
  exact ContinuousLinearMap.integrable_comp _ fc_integrable

open ContinuousMap Classical in
lemma integrable_cfcâ‚™_set' [TopologicalSpace X] [OpensMeasurableSpace X] (s : Set X)
    (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)â‚€]
    (hf : Continuous (fun x : s Ã— quasispectrum ğ•œ a => f x.1 x.2))
    (hfâ‚‚ : âˆ€ x âˆˆ s, f x 0 = 0)
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    IntegrableOn (fun x => cfcâ‚™ (f x) a) s Î¼ := by
  let fc : C(s Ã— (quasispectrum ğ•œ a), ğ•œ) := âŸ¨fun x => f x.1 x.2, hfâŸ©
  let fcâ‚‚ : C(s, C(quasispectrum ğ•œ a, ğ•œ)â‚€) :=
    { toFun := fun x => âŸ¨fc.curry x, by simp [fc, hfâ‚‚]âŸ©
      continuous_toFun := by sorry }
  refine integrable_cfcâ‚™_set s hs f bound a ?_ hfâ‚‚ ?_ hbound hbound_finite_integral
  Â· intro x xs
    rw [continuousOn_iff_continuous_restrict]
    exact (fcâ‚‚ âŸ¨x, xsâŸ©).continuous
  Â· refine ContinuousOn.aestronglyMeasurable ?_ hs
    rw [continuousOn_iff_continuous_restrict]

    refine fcâ‚‚.continuous.congr ?_
    intro âŸ¨x, hxâŸ©
    ext
    simp [fcâ‚‚, fc, hx]

open ContinuousMapZero Classical in
/-- The continuous functional calculus commutes with integration. -/
lemma cfcâ‚™_setIntegral [NormedSpace â„ A] (s : Set X) (hs : MeasurableSet s) (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A)
    (hfâ‚ : âˆ€ x âˆˆ s, ContinuousOn (f x) (quasispectrum ğ•œ a))
    (hfâ‚‚ : âˆ€ x âˆˆ s, f x 0 = 0)
    (hfâ‚ƒ : AEStronglyMeasurable (fun x : X =>
      if h : x âˆˆ s then
        (âŸ¨âŸ¨_, (hfâ‚ x h).restrictâŸ©, by simp [hfâ‚‚ x h]âŸ© : C(quasispectrum ğ•œ a, ğ•œ)â‚€)
      else 0) (Î¼.restrict s))
    (hbound : âˆ€ x âˆˆ s, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound (Î¼.restrict s)) (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun r => âˆ« x in s, f x r âˆ‚Î¼) a = âˆ« x in s, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  let fc : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€ := fun x =>
    if h : x âˆˆ s then âŸ¨âŸ¨_, (hfâ‚ x h).restrictâŸ©, by simp [hfâ‚‚ x h]âŸ© else 0
  have hfc : s.EqOn (fun x r => fc x r) (fun x => (quasispectrum ğ•œ a).restrict (f x)) := by
    intro x hx
    ext
    simp [fc, hx]
  have fc_integrable : IntegrableOn fc s Î¼ := by
    refine âŸ¨hfâ‚ƒ, ?_âŸ©
    refine hbound_finite_integral.mono ?_
    filter_upwards [ae_restrict_mem hs] with x hx
    rw [norm_def, ContinuousMap.norm_le _ (norm_nonneg (bound x))]
    intro z
    have := hfc hx
    simp only at this
    simp only [ContinuousMap.coe_coe, this, Set.restrict_apply, Real.norm_eq_abs, ge_iff_le, fc]
    exact hbound x hx z.1 z.2
  have ha : p a := ha
  have hâ‚ : s.EqOn (fun x : X => cfcâ‚™ (f x) a) (fun x : X => cfcâ‚™L ha (fc x)) := by
    intro x hx
    dsimp
    rw [cfcâ‚™_apply ..]
    unfold fc
    simp [hx]
  have hâ‚‚ : ((quasispectrum ğ•œ a).restrict fun r => âˆ« (x : X) in s, f x r âˆ‚Î¼)
      = (âˆ« (x : X) in s, fc x âˆ‚Î¼).toFun := by
    ext r
    simp [Set.restrict_apply, ContinuousMap.toFun_eq_coe, fc, ContinuousMapZero.integral_apply fc_integrable _]
    sorry
    --refine integral_congr_ae ?_
    --filter_upwards [ae_restrict_mem hs] with x hx
    --simp [fc, hx]
  have hcontâ‚‚ : ContinuousOn (fun r => âˆ« x in s, f x r âˆ‚Î¼) (quasispectrum ğ•œ a) := by
    rw [continuousOn_iff_continuous_restrict]
    convert map_continuous (âˆ« x in s, fc x âˆ‚Î¼)
  sorry
  --rw [setIntegral_congr_fun hs hâ‚, ContinuousLinearMap.integral_comp_comm _ fc_integrable,
  --    cfcâ‚™L_apply, cfcâ‚™_apply ..]
  --congr

open ContinuousMapZero in
/-- The non-unital continuous functional calculus commutes with integration. -/
lemma cfcâ‚™_integral [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)â‚€]
    (hfâ‚ : âˆ€ x, ContinuousOn (f x) (quasispectrum ğ•œ a))
    (hfâ‚‚ : âˆ€ x, f x 0 = 0)
    (hfâ‚ƒ : Continuous (fun x â†¦ (âŸ¨âŸ¨_, hfâ‚ x |>.restrictâŸ©, hfâ‚‚ xâŸ© : C(quasispectrum ğ•œ a, ğ•œ)â‚€)))
    (hbound : âˆ€ x, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  let fc : X â†’ C(quasispectrum ğ•œ a, ğ•œ)â‚€ := fun x => âŸ¨âŸ¨_, (hfâ‚ x).restrictâŸ©, hfâ‚‚ xâŸ©
  have fc_integrable : Integrable fc Î¼ := by
    refine âŸ¨hfâ‚ƒ.aestronglyMeasurable, ?_âŸ©
    refine hbound_finite_integral.mono <| .of_forall fun x â†¦ ?_
    change â€–(fc x : C(quasispectrum  ğ•œ a, ğ•œ))â€– â‰¤ â€–bound xâ€–
    rw [ContinuousMap.norm_le _ (norm_nonneg (bound x))]
    exact fun z â†¦ hbound x z.1 z.2
  have h_int_fc : (quasispectrum ğ•œ a).restrict (âˆ« x, f x Â· âˆ‚Î¼) = âˆ« x, fc x âˆ‚Î¼ := by
    ext; simp [integral_apply fc_integrable, fc]
  have hcontâ‚‚ : ContinuousOn (fun r => âˆ« x, f x r âˆ‚Î¼) (quasispectrum ğ•œ a) := by
    rw [continuousOn_iff_continuous_restrict]
    convert map_continuous (âˆ« x, fc x âˆ‚Î¼)
  rw [integral_congr_ae (.of_forall fun _ â†¦ cfcâ‚™_apply ..), cfcâ‚™_apply ..,
    cfcâ‚™Hom_integral _ _ fc_integrable]
  congr

/-- The non-unital continuous functional calculus commutes with integration. -/
lemma cfcâ‚™_integral' [NormedSpace â„ A] [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(quasispectrum ğ•œ a, ğ•œ)â‚€]
    (hf : Continuous (fun x => (quasispectrum ğ•œ a).restrict (f x)).uncurry)
    (hfâ‚‚ : âˆ€ x, f x 0 = 0)
    (hbound : âˆ€ x, âˆ€ z âˆˆ quasispectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfcâ‚™ (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfcâ‚™ (f x) a âˆ‚Î¼ := by
  refine cfcâ‚™_integral f bound a ?_ hfâ‚‚ ?_ hbound hbound_finite_integral
  Â· exact (continuousOn_iff_continuous_restrict.mpr <| hf.uncurry_left Â·)
  Â· let g := ((â†‘) : C(quasispectrum ğ•œ a, ğ•œ)â‚€ â†’ C(quasispectrum ğ•œ a, ğ•œ))
    refine ((isInducing_iff g).mpr rfl).continuous_iff.mpr ?_
    exact ContinuousMap.curry âŸ¨_, hfâŸ© |>.continuous

end nonunital
