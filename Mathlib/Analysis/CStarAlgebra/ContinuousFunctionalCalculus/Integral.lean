/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.Normed.Algebra.Spectrum
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Unital
import Mathlib.MeasureTheory.Integral.SetIntegral

/-!
# Integrals and the continuous functional calculus

This file gives results about integrals of the form `âˆ« x, cfc (f x) a`. Most notably, we show
that the integral commutes with the continuous functional calculus under appropriate conditions.

## Main declarations

+ `cfc_integral`: given a function `f : X â†’ ğ•œ â†’ ğ•œ`, we have that
  `cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼`
  under appropriate conditions

## TODO

+ Prove a similar result for the non-unital case
+ Lift this to the case where the CFC is over `â„â‰¥0`
+ Use this to prove operator monotonicity and concavity/convexity of `rpow` and `log`
-/

open MeasureTheory

section unital

variable {X : Type*} {ğ•œ : Type*} {A : Type*} {p : A â†’ Prop} [RCLike ğ•œ]
  [MeasurableSpace X] {Î¼ : Measure X}
  [NormedRing A] [StarRing A] [NormedAlgebra ğ•œ A] [NormedAlgebra â„ A] [CompleteSpace A]
  [ContinuousFunctionalCalculus ğ•œ p]

lemma cfcL_integral (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ)) (hfâ‚ : Integrable f Î¼) (ha : p a) :
    âˆ« x, cfcL ha (f x) âˆ‚Î¼ = cfcL ha (âˆ« x, f x âˆ‚Î¼) := by
  rw [ContinuousLinearMap.integral_comp_comm _ hfâ‚]

lemma cfcHom_integral (a : A) (f : X â†’ C(spectrum ğ•œ a, ğ•œ)) (hfâ‚ : Integrable f Î¼) (ha : p a) :
    âˆ« x, cfcHom ha (f x) âˆ‚Î¼ = cfcHom ha (âˆ« x, f x âˆ‚Î¼) :=
  cfcL_integral a f hfâ‚ ha

open ContinuousMap in
/-- The continuous functional calculus commutes with integration. -/
lemma cfc_integral [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hfâ‚ : âˆ€ x, ContinuousOn (f x) (spectrum ğ•œ a))
    (hfâ‚‚ : Continuous (fun x â†¦ (âŸ¨_, hfâ‚ x |>.restrictâŸ© : C(spectrum ğ•œ a, ğ•œ))))
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  let fc : X â†’ C(spectrum ğ•œ a, ğ•œ) := fun x => âŸ¨_, (hfâ‚ x).restrictâŸ©
  have fc_integrable : Integrable fc Î¼ := by
    refine âŸ¨hfâ‚‚.aestronglyMeasurable, ?_âŸ©
    refine hbound_finite_integral.mono <| .of_forall fun x â†¦ ?_
    rw [norm_le _ (norm_nonneg (bound x))]
    exact fun z â†¦ hbound x z.1 z.2
  have h_int_fc : (spectrum ğ•œ a).restrict (âˆ« x, f x Â· âˆ‚Î¼) = âˆ« x, fc x âˆ‚Î¼ := by
    ext; simp [integral_apply fc_integrable, fc]
  have hcontâ‚‚ : ContinuousOn (fun r => âˆ« x, f x r âˆ‚Î¼) (spectrum ğ•œ a) := by
    rw [continuousOn_iff_continuous_restrict]
    convert map_continuous (âˆ« x, fc x âˆ‚Î¼)
  rw [integral_congr_ae (.of_forall fun _ â†¦ cfc_apply ..), cfc_apply ..,
    cfcHom_integral _ _ fc_integrable]
  congr

/-- The continuous functional calculus commutes with integration. -/
lemma cfc_integral' [TopologicalSpace X] [OpensMeasurableSpace X] (f : X â†’ ğ•œ â†’ ğ•œ)
    (bound : X â†’ â„) (a : A) [SecondCountableTopologyEither X C(spectrum ğ•œ a, ğ•œ)]
    (hf : Continuous (fun x => (spectrum ğ•œ a).restrict (f x)).uncurry)
    (hbound : âˆ€ x, âˆ€ z âˆˆ spectrum ğ•œ a, â€–f x zâ€– â‰¤ â€–bound xâ€–)
    (hbound_finite_integral : HasFiniteIntegral bound Î¼) (ha : p a := by cfc_tac) :
    cfc (fun r => âˆ« x, f x r âˆ‚Î¼) a = âˆ« x, cfc (f x) a âˆ‚Î¼ := by
  refine cfc_integral f bound a ?_ ?_ hbound hbound_finite_integral
  Â· exact (continuousOn_iff_continuous_restrict.mpr <| hf.uncurry_left Â·)
  Â· exact ContinuousMap.curry âŸ¨_, hfâŸ© |>.continuous

end unital
