/-
Copyright (c) 2025 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Isometric
import Mathlib.Topology.MetricSpace.UniformConvergence
import Mathlib.Topology.UniformSpace.CompactConvergence

/-! # Continuity of the continuous functional calculus in each variable

The continuous functional calculus is a map which takes a pair `a : A` (`A` is a Câ‹†-algebra) and
a function `f : C(spectrum R a, R)` where `a` satisfies some predicate `p`, depending on `R` and
returns another element of the algebra `A`. This is the map `cfcHom`. The class
`ContinuousFunctionalCalculus` declares that `cfcHom` is a continuous map from `C(spectrum R a, R)`
to `A`. However, users generally interact with the continuous functional calculus through `cfc`,
which operates on bare functions `f : R â†’ R` instead and takes a junk value when `f` is not
continuous on the spectrum of `a`.  In this file we provide some lemma concerning the continuity
of `cfc`, subject to natural hypotheses.

However, the continuous functional calculus is *also* continuous in the variable `a`, but there
are some conditions that must be satisfied. In particular, given a function `f : R â†’ R` the map
`a â†¦ cfc f a` is continuous so long as `a` varies over a collection of elements satisfying the
predicate `p` and their spectra are collectively contained in a compact set on which `f` is
continuous. Moreover, it is required that the continuous functional calculus be the isometric
variant.

Under the assumption of `IsometricContinuousFunctionalCalculus`, we show that the continuous
functional calculus is Lipschitz with constant 1 in the variable `f : R â†’áµ¤[{spectrum R a}] R`
on the set of functions which are continuous on the spectrum of `a`. Combining this with the
continuity of the continuous functional calculus in the variable `a`, we obtain a joint continuity
result for `cfc` in both variables.

Finally, all of this is developed for both the unital and non-unital functional calculi.
The continuity results in the function variable are valid for all scalar rings, but the continuity
results in the variable `a` come in two flavors: those for `RCLike ğ•œ` and those for `â„â‰¥0`.

## Main results


+ `tendsto_cfc_fun`: If `F : X â†’ R â†’ R` tends to `f : R â†’ R` uniformly on the spectrum of `a`, and
  all these functions are continuous on the spectrum, then `fun x â†¦ cfc (F x) a` tends
  to `cfc f a`.
+ `Filter.Tendsto.cfc`: If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `a : X â†’ A` tends to
  `aâ‚€ : A` along a filter `l` (such that eventually `a x` satisfies the predicate `p` associated to
  `ğ•œ` and has spectrum contained in `s`, as does `aâ‚€`), then `fun x â†¦ cfc f (a x)` tends to
  `cfc f aâ‚€`.
+ `lipschitzOnWith_cfc_fun`: The function `f â†¦ cfc f a` is Lipschitz with constant with constant 1
  with respect to supremum metric (on `R â†’áµ¤[{spectrum R a}] R`) on those functions which are
  continuous on the spectrum.
+ `continuousOn_cfc`: For `f : ğ•œ â†’ ğ•œ` continuous on a compact set `s`, `cfc f` is continuous on the
  set of `a : A` satisfying the predicate `p` (associated to `ğ•œ`) and whose `ğ•œ`-spectrum is
  contained in `s`.
+ `continuousOn_cfc_setProd`: Let `s : Set ğ•œ` be a compact set and consider pairs
  `(f, a) : (ğ•œ â†’ ğ•œ) Ã— A` where `f` is continuous on `s` and `spectrum ğ•œ a âŠ† s` and `a` satisfies
  the predicate `p a` for the continuous functional calculus. Then `cfc` is jointly continuous in
  both variables (i.e., continuous in its uncurried form) on this set of pairs when the function
  space is equipped with the topology of uniform convergence on `s`.
+ Versions of all of the above for non-unital algebras, and versions over `â„â‰¥0` as well.

-/

open scoped UniformConvergence NNReal
open Filter Topology

section Unital

section Left

section Generic

variable {X R A : Type*} {p : A â†’ Prop} [CommSemiring R] [StarRing R] [MetricSpace R]
    [IsTopologicalSemiring R] [ContinuousStar R] [Ring A] [StarRing A]
    [TopologicalSpace A] [Algebra R A] [ContinuousFunctionalCalculus R A p]

/-- If `F : X â†’ R â†’ R` tends to `f : R â†’ R` uniformly on the spectrum of `a`, and all
these functions are continuous on the spectrum, then `fun x â†¦ cfc (F x) a` tends
to `cfc f a`. -/
theorem tendsto_cfc_fun {l : Filter X} {F : X â†’ R â†’ R} {f : R â†’ R} {a : A}
    (h_tendsto : TendstoUniformlyOn F f l (spectrum R a))
    (hF : âˆ€á¶  x in l, ContinuousOn (F x) (spectrum R a)) :
    Tendsto (fun x â†¦ cfc (F x) a) l (ğ“ (cfc f a)) := by
  open scoped ContinuousFunctionalCalculus in
  obtain (rfl | hl) := l.eq_or_neBot
  Â· simp
  have hf := h_tendsto.continuousOn hF
  by_cases ha : p a
  Â· let s : Set X := {x | ContinuousOn (F x) (spectrum R a)}
    rw [â† tendsto_comap'_iff (i := ((â†‘) : s â†’ X)) (by simpa)]
    conv =>
      enter [1, x]
      rw [Function.comp_apply, cfc_apply (hf := x.2)]
    rw [cfc_apply ..]
    apply cfcHom_continuous _ |>.tendsto _ |>.comp
    rw [hf.tendsto_restrict_iff_tendstoUniformlyOn Subtype.property]
    intro t
    simp only [eventually_comap, Subtype.forall]
    peel h_tendsto t with ht x _
    simp_all
  Â· simpa [cfc_apply_of_not_predicate a ha] using tendsto_const_nhds

/-- If `f : X â†’ R â†’ R` tends to `f xâ‚€` uniformly (along `ğ“ xâ‚€`) on the spectrum of `a`,
and each `f x` is continuous on the spectrum of `a`, then `fun x â†¦ cfc (f x) a` is
continuous at `xâ‚€`. -/
theorem continuousAt_cfc_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A}
    {xâ‚€ : X} (h_tendsto : TendstoUniformlyOn f (f xâ‚€) (ğ“ xâ‚€) (spectrum R a))
    (hf : âˆ€á¶  x in ğ“ xâ‚€, ContinuousOn (f x) (spectrum R a)) :
    ContinuousAt (fun x â†¦ cfc (f x) a) xâ‚€ :=
  tendsto_cfc_fun h_tendsto hf

/-- If `f : X â†’ R â†’ R` tends to `f xâ‚€` uniformly (along `ğ“[s] xâ‚€`) on the spectrum of `a`,
and eventually each `f x` is continuous on the spectrum of `a`, then `fun x â†¦ cfc (f x) a` is
continuous at `xâ‚€` within `s`. -/
theorem continuousWithinAt_cfc_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A}
    {xâ‚€ : X} {s : Set X} (h_tendsto : TendstoUniformlyOn f (f xâ‚€) (ğ“[s] xâ‚€) (spectrum R a))
    (hf : âˆ€á¶  x in ğ“[s] xâ‚€, ContinuousOn (f x) (spectrum R a)) :
    ContinuousWithinAt (fun x â†¦ cfc (f x) a) s xâ‚€ :=
  tendsto_cfc_fun h_tendsto hf

open UniformOnFun in
/-- If `f : X â†’ R â†’ R` is continuous on `s : Set X` in the topology on
`X â†’ R â†’áµ¤[{spectrum R a}] â†’ R`, and each `f` is continuous on the spectrum, then `x â†¦ cfc (f x) a`
is continuous on `s` also. -/
theorem ContinuousOn.cfc_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A} {s : Set X}
    (h_cont : ContinuousOn (fun x â†¦ ofFun {spectrum R a} (f x)) s)
    (hf : âˆ€ x âˆˆ s, ContinuousOn (f x) (spectrum R a) := by cfc_cont_tac) :
    ContinuousOn (fun x â†¦ cfc (f x) a) s := by
  rw [ContinuousOn] at h_cont âŠ¢
  simp only [ContinuousWithinAt, UniformOnFun.tendsto_iff_tendstoUniformlyOn, Set.mem_singleton_iff,
    Function.comp_def, toFun_ofFun, forall_eq] at h_cont
  refine fun x hx â†¦ continuousWithinAt_cfc_fun (h_cont x hx) ?_
  filter_upwards [self_mem_nhdsWithin] with x hx
  exact hf x hx

open UniformOnFun in
/-- If `f : X â†’ R â†’ R` is continuous in the topology on `X â†’ R â†’áµ¤[{spectrum R a}] â†’ R`,
and each `f` is continuous on the spectrum, then `x â†¦ cfc (f x) a` is continuous. -/
theorem Continuous.cfc_fun [TopologicalSpace X] (f : X â†’ R â†’ R) (a : A)
    (h_cont : Continuous (fun x â†¦ ofFun {spectrum R a} (f x)))
    (hf : âˆ€ x, ContinuousOn (f x) (spectrum R a) := by cfc_cont_tac) :
    Continuous fun x â†¦ cfc (f x) a := by
  rw [â† continuousOn_univ] at h_cont âŠ¢
  exact h_cont.cfc_fun (fun x _ â†¦ hf x)

end Generic

section Isometric

variable {X R A : Type*} {p : A â†’ Prop} [CommSemiring R] [StarRing R] [MetricSpace R]
    [IsTopologicalSemiring R] [ContinuousStar R] [Ring A] [StarRing A]
    [MetricSpace A] [Algebra R A] [IsometricContinuousFunctionalCalculus R A p]

variable (R) in
open UniformOnFun in
open scoped ContinuousFunctionalCalculus in
/-- The function `f â†¦ cfc f a` is Lipschitz with constant 1 with respect to
supremum metric (on `R â†’áµ¤[{spectrum R a}] R`) on those functions which are continuous on
the spectrum. -/
lemma lipschitzOnWith_cfc_fun (a : A) :
    LipschitzOnWith 1 (fun f â†¦ cfc (toFun {spectrum R a} f) a)
      {f | ContinuousOn (toFun {spectrum R a} f) (spectrum R a)} := by
  by_cases ha : p a
  Â· intro f hf g hg
    simp only
    rw [cfc_apply .., cfc_apply .., isometry_cfcHom (R := R) a ha |>.edist_eq]
    simp only [ENNReal.coe_one, one_mul]
    rw [edist_continuousRestrict_of_singleton hf hg]
  Â· simpa [cfc_apply_of_not_predicate a ha] using LipschitzWith.const' 0 |>.lipschitzOnWith

open UniformOnFun in
open scoped ContinuousFunctionalCalculus in
/-- The function `f â†¦ cfc f a` is Lipschitz with constant 1 with respect to
supremum metric (on `R â†’áµ¤[{s}] R`) on those functions which are continuous on a set `s` containing
the spectrum. -/
lemma lipschitzOnWith_cfc_fun_of_subset (a : A) {s : Set R} (hs : spectrum R a âŠ† s) :
    LipschitzOnWith 1 (fun f â†¦ cfc (toFun {s} f) a)
      {f | ContinuousOn (toFun {s} f) (s)} := by
  have hâ‚ := lipschitzOnWith_cfc_fun R a
  have hâ‚‚ := lipschitzWith_one_ofFun_toFun' (ğ”– := {spectrum R a}) (ğ”— := {s}) (Î² := R) (by simpa)
  have hâ‚ƒ := hâ‚‚.lipschitzOnWith (s := {f | ContinuousOn (toFun {s} f) (s)})
  simpa using hâ‚.comp hâ‚ƒ (fun f hf â†¦ hf.mono hs)

end Isometric

end Left

section Right
section RCLike

variable {X ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [NormedRing A] [StarRing A]
    [NormedAlgebra ğ•œ A] [IsometricContinuousFunctionalCalculus ğ•œ A p]
    [ContinuousStar A]

/-- `cfcHomSuperset` is continuous in the variable `a : A` when `s : Set ğ•œ` is compact and `a`
varies over elements whose spectrum is contained in `s`, all of which satisfy the predicate `p`. -/
theorem continuous_cfcHomSuperset_left
    [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (f : C(s, ğ•œ))
    (a : X â†’ A) (ha_cont : Continuous a) (ha : âˆ€ x, spectrum ğ•œ (a x) âŠ† s)
    (ha' : âˆ€ x, p (a x) := by cfc_tac) :
    Continuous (fun x â†¦ cfcHomSuperset (ha' x) (ha x) f) := by
  open scoped ContinuousFunctionalCalculus in
  have : CompactSpace s := by rwa [isCompact_iff_compactSpace] at hs
  induction f using ContinuousMap.induction_on_of_compact with
  | const r =>
    have : ContinuousMap.const s r = algebraMap ğ•œ C(s, ğ•œ) r := rfl
    simpa only [this, AlgHomClass.commutes] using continuous_const
  | id =>
    simp only [cfcHomSuperset_id]
    fun_prop
  | star_id =>
    simp only [map_star, cfcHomSuperset_id]
    fun_prop
  | add f g hf hg => simpa using hf.add hg
  | mul f g hf hg => simpa using hf.mul hg
  | frequently f hf =>
    apply continuous_of_uniform_approx_of_continuous
    rw [Metric.uniformity_basis_dist_le.forall_iff (by aesop)]
    intro Îµ hÎµ
    simp only [Set.mem_setOf_eq, dist_eq_norm]
    obtain âŸ¨g, hg, g_contâŸ© := frequently_iff.mp hf (Metric.closedBall_mem_nhds f hÎµ)
    simp only [Metric.mem_closedBall, dist_comm g, dist_eq_norm] at hg
    refine âŸ¨_, g_cont, fun x â†¦ ?_âŸ©
    rw [â† map_sub, cfcHomSuperset_apply]
    rw [isometry_cfcHom (R := ğ•œ) _ (ha' x) |>.norm_map_of_map_zero (map_zero (cfcHom (ha' x)))]
    rw [ContinuousMap.norm_le _ hÎµ.le] at hg âŠ¢
    aesop

variable (A) in
/-- For `f : ğ•œ â†’ ğ•œ` continuous on a compact set `s`, `cfc f` is continuous on the set of `a : A`
satisfying the predicate `p` (associated to `ğ•œ`) and whose `ğ•œ`-spectrum is contained in `s`. -/
theorem continuousOn_cfc {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousOn (cfc f) {a | p a âˆ§ spectrum ğ•œ a âŠ† s} :=
  continuousOn_iff_continuous_restrict.mpr <| by
    convert continuous_cfcHomSuperset_left hs âŸ¨_, hf.restrictâŸ©
      ((â†‘) : {a | p a âˆ§ spectrum ğ•œ a âŠ† s} â†’ A) continuous_subtype_val (fun x â†¦ x.2.2) with x
    rw [cfcHomSuperset_apply, Set.restrict_apply, cfc_apply _ _ x.2.1 (hf.mono x.2.2)]
    congr!

open UniformOnFun in
/-- Let `s : Set ğ•œ` be a compact set and consider pairs `(f, a) : (ğ•œ â†’ ğ•œ) Ã— A` where `f` is
continuous on `s` and `spectrum ğ•œ a âŠ† s` and `a` satisfies the predicate `p a` for the continuous
functional calculus.

Then `cfc` is jointly continuous in both variables (i.e., continuous in its uncurried form) on this
set of pairs when the function space is equipped with the topology of uniform convergence on `s`. -/
theorem continuousOn_cfc_setProd {s : Set ğ•œ} (hs : IsCompact s) :
    ContinuousOn (fun fa : (ğ•œ â†’áµ¤[{s}] ğ•œ) Ã— A â†¦ cfc (toFun {s} fa.1) fa.2)
      ({f | ContinuousOn (toFun {s} f) s} Ã—Ë¢ {a | p a âˆ§ spectrum ğ•œ a âŠ† s}) :=
  continuousOn_prod_of_continuousOn_lipschitzOnWith _ 1
    (fun f hf â†¦ continuousOn_cfc A hs ((toFun {s}) f) hf)
    (fun a âŸ¨_, ha'âŸ© â†¦ lipschitzOnWith_cfc_fun_of_subset a ha')

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `a : X â†’ A` tends to `aâ‚€ : A` along a
filter `l` (such that eventually `a x` satisfies the predicate `p` associated to `ğ•œ` and has
spectrum contained in `s`, as does `aâ‚€`), then `fun x â†¦ cfc f (a x)` tends to `cfc f aâ‚€`. -/
protected theorem Filter.Tendsto.cfc {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} {aâ‚€ : A} {l : Filter X} (ha_tendsto : Tendsto a l (ğ“ aâ‚€))
    (ha : âˆ€á¶  x in l, spectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€á¶  x in l, p (a x))
    (haâ‚€ : spectrum ğ•œ aâ‚€ âŠ† s) (haâ‚€' : p aâ‚€) (hf : ContinuousOn f s := by cfc_cont_tac) :
    Tendsto (fun x â†¦ cfc f (a x)) l (ğ“ (cfc f aâ‚€)) := by
  apply continuousOn_cfc A hs f |>.continuousWithinAt âŸ¨haâ‚€', haâ‚€âŸ© |>.tendsto.comp
  rw [tendsto_nhdsWithin_iff]
  exact âŸ¨ha_tendsto, ha'.and haâŸ©

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `a : X â†’ A` is continuous at `xâ‚€`, and
eventually `a x` satisfies the predicate `p` associated to `ğ•œ` and has spectrum contained in `s`),
then `fun x â†¦ cfc f (a x)` is continuous at `xâ‚€`. -/
protected theorem ContinuousAt.cfc [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} {xâ‚€ : X} (ha_cont : ContinuousAt a xâ‚€)
    (ha : âˆ€á¶  x in ğ“ xâ‚€, spectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€á¶  x in ğ“ xâ‚€, p (a x))
    (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousAt (fun x â†¦ cfc f (a x)) xâ‚€ :=
  ha_cont.tendsto.cfc hs f ha ha' ha.self_of_nhds ha'.self_of_nhds

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `a : X â†’ A` is continuous at `xâ‚€` within
a set `t : Set X`, and eventually `a x` satisfies the predicate `p` associated to `ğ•œ` and has
spectrum contained in `s`), then `fun x â†¦ cfc f (a x)` is continuous at `xâ‚€` within `t`. -/
protected theorem ContinuousWithinAt.cfc [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s)
    (f : ğ•œ â†’ ğ•œ) {a : X â†’ A} {xâ‚€ : X} {t : Set X} (hxâ‚€ : xâ‚€ âˆˆ t)
    (ha_cont : ContinuousWithinAt a t xâ‚€) (ha : âˆ€á¶  x in ğ“[t] xâ‚€, spectrum ğ•œ (a x) âŠ† s)
    (ha' : âˆ€á¶  x in ğ“[t] xâ‚€, p (a x)) (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousWithinAt (fun x â†¦ cfc f (a x)) t xâ‚€ :=
  ha_cont.tendsto.cfc hs f ha ha' (ha.self_of_nhdsWithin hxâ‚€) (ha'.self_of_nhdsWithin hxâ‚€)

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `a : X â†’ A` is continuous on `t : Set X`,
and `a x` satisfies the predicate `p` associated to `ğ•œ` and has spectrum contained in `s` for all
`x âˆˆ t`, then `fun x â†¦ cfc f (a x)` is continuous on `t`. -/
protected theorem ContinuousOn.cfc [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s)
    (f : ğ•œ â†’ ğ•œ) {a : X â†’ A} {t : Set X} (ha_cont : ContinuousOn a t)
    (ha : âˆ€ x âˆˆ t, spectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€ x âˆˆ t, p (a x))
    (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousOn (fun x â†¦ cfc f (a x)) t := by
  rw [ContinuousOn] at ha_cont âŠ¢
  refine fun x hx â†¦ (ha_cont x hx).cfc hs f hx ?_ ?_ hf
  all_goals filter_upwards [self_mem_nhdsWithin] with x hx
  exacts [ha x hx, ha' x hx]

/-- `cfc` is continuous in the variable `a : A` when `s : Set ğ•œ` is compact and `a` varies over
elements whose spectrum is contained in `s`, all of which satisfy the predicate `p`, and the
function `f` is continuous on the spectrum of `a`. -/
protected theorem Continuous.cfc [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} (ha_cont : Continuous a) (ha : âˆ€ x, spectrum ğ•œ (a x) âŠ† s)
    (hf : ContinuousOn f s := by cfc_cont_tac) (ha' : âˆ€ x, p (a x) := by cfc_tac) :
    Continuous (fun x â†¦ cfc f (a x)) := by
  rw [â† continuousOn_univ] at ha_cont âŠ¢
  exact ha_cont.cfc hs f (fun x _ â†¦ ha x) (fun x _ â†¦ ha' x)

end RCLike

section NNReal

variable {X A : Type*} [NormedRing A] [StarRing A]
    [NormedAlgebra â„ A] [IsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
    [ContinuousStar A] [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A]
    [T2Space A] [IsTopologicalRing A]

variable (A) in
/-- A version of `continuousOn_cfc` over `â„â‰¥0` instead of `RCLike ğ•œ`. -/
theorem continuousOn_cfc_nnreal {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousOn (cfc f) {a : A | 0 â‰¤ a âˆ§ spectrum â„â‰¥0 a âŠ† s} := by
  have : {a : A | 0 â‰¤ a âˆ§ spectrum â„â‰¥0 a âŠ† s}.EqOn (cfc f) (cfc (fun x : â„ â†¦ f x.toNNReal)) :=
    fun a ha â†¦ cfc_nnreal_eq_real _ ha.1
  refine ContinuousOn.congr ?_ this
  replace hf : ContinuousOn (fun x â†¦ f x.toNNReal : â„ â†’ â„) (NNReal.toReal '' s) := by
    apply hf.ofReal_map_toNNReal
    rw [Set.mapsTo_image_iff]
    intro x hx
    simpa
  refine continuousOn_cfc A (hs.image NNReal.continuous_coe) _ hf |>.mono fun a ha â†¦ ?_
  simp only [Set.mem_setOf_eq, nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts] at ha âŠ¢
  rw [â† SpectrumRestricts] at ha
  refine âŸ¨ha.1.1, ?_âŸ©
  rw [â† ha.1.2.algebraMap_image]
  exact Set.image_mono ha.2

open UniformOnFun in
/-- Let `s : Set â„â‰¥0` be a compact set and consider pairs `(f, a) : (â„â‰¥0 â†’ â„â‰¥0) Ã— A` where `f` is
continuous on `s` and `spectrum â„â‰¥0 a âŠ† s` and `0 â‰¤ a`.

Then `cfc` is jointly continuous in both variables (i.e., continuous in its uncurried form) on this
set of pairs when the function space is equipped with the topology of uniform convergence on `s`. -/
theorem continuousOn_cfc_nnreal_setProd {s : Set â„â‰¥0} (hs : IsCompact s) :
    ContinuousOn (fun fa : (â„â‰¥0 â†’áµ¤[{s}] â„â‰¥0) Ã— A â†¦ cfc (toFun {s} fa.1) fa.2)
      ({f | ContinuousOn (toFun {s} f) s} Ã—Ë¢ {a | 0 â‰¤ a âˆ§ spectrum â„â‰¥0 a âŠ† s}) :=
  continuousOn_prod_of_continuousOn_lipschitzOnWith _ 1
    (fun f hf â†¦ continuousOn_cfc_nnreal A hs ((toFun {s}) f) hf)
    (fun a âŸ¨_, ha'âŸ© â†¦ lipschitzOnWith_cfc_fun_of_subset a ha')

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `a : X â†’ A` tends to `aâ‚€ : A` along a
filter `l` (such that eventually `0 â‰¤ a x` and has spectrum contained in `s`, as does `aâ‚€`), then
`fun x â†¦ cfc f (a x)` tends to `cfc f aâ‚€`. -/
theorem Filter.Tendsto.cfc_nnreal {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {aâ‚€ : A} {l : Filter X} (ha_tendsto : Tendsto a l (ğ“ aâ‚€))
    (ha : âˆ€á¶  x in l, spectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€á¶  x in l, 0 â‰¤ a x)
    (haâ‚€ : spectrum â„â‰¥0 aâ‚€ âŠ† s) (haâ‚€' : 0 â‰¤ aâ‚€) (hf : ContinuousOn f s := by cfc_cont_tac) :
    Tendsto (fun x â†¦ cfc f (a x)) l (ğ“ (cfc f aâ‚€)) := by
  apply continuousOn_cfc_nnreal A hs f |>.continuousWithinAt âŸ¨haâ‚€', haâ‚€âŸ© |>.tendsto.comp
  rw [tendsto_nhdsWithin_iff]
  exact âŸ¨ha_tendsto, ha'.and haâŸ©

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `a : X â†’ A` is continuous at `xâ‚€`, and
eventually `0 â‰¤ a x` and has spectrum contained in `s`), then `fun x â†¦ cfc f (a x)` is continuous
at `xâ‚€`. -/
theorem ContinuousAt.cfc_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {xâ‚€ : X} (ha_cont : ContinuousAt a xâ‚€)
    (ha : âˆ€á¶  x in ğ“ xâ‚€, spectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€á¶  x in ğ“ xâ‚€, 0 â‰¤ a x)
    (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousAt (fun x â†¦ cfc f (a x)) xâ‚€ :=
  ha_cont.tendsto.cfc_nnreal hs f ha ha' ha.self_of_nhds ha'.self_of_nhds

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `a : X â†’ A` is continuous at `xâ‚€`
within a set `t : Set X`, and eventually `0 â‰¤ a x` and has spectrum contained in `s`), then
`fun x â†¦ cfc f (a x)` is continuous at `xâ‚€` within `t`. -/
theorem ContinuousWithinAt.cfc_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {xâ‚€ : X} {t : Set X} (hxâ‚€ : xâ‚€ âˆˆ t)
    (ha_cont : ContinuousWithinAt a t xâ‚€) (ha : âˆ€á¶  x in ğ“[t] xâ‚€, spectrum â„â‰¥0 (a x) âŠ† s)
    (ha' : âˆ€á¶  x in ğ“[t] xâ‚€, 0 â‰¤ a x) (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousWithinAt (fun x â†¦ cfc f (a x)) t xâ‚€ :=
  ha_cont.tendsto.cfc_nnreal hs f ha ha' (ha.self_of_nhdsWithin hxâ‚€) (ha'.self_of_nhdsWithin hxâ‚€)

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `a : X â†’ A` is continuous on
`t : Set X`, and `0 â‰¤ a x` and has spectrum contained in `s` for all `x âˆˆ t`, then
`fun x â†¦ cfc f (a x)` is continuous on `t`. -/
theorem ContinuousOn.cfc_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {t : Set X} (ha_cont : ContinuousOn a t)
    (ha : âˆ€ x âˆˆ t, spectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€ x âˆˆ t, 0 â‰¤ a x)
    (hf : ContinuousOn f s := by cfc_cont_tac) :
    ContinuousOn (fun x â†¦ cfc f (a x)) t := by
  rw [ContinuousOn] at ha_cont âŠ¢
  refine fun x hx â†¦ (ha_cont x hx).cfc_nnreal hs f hx ?_ ?_ hf
  all_goals filter_upwards [self_mem_nhdsWithin] with x hx
  exacts [ha x hx, ha' x hx]

/-- `cfc` is continuous in the variable `a : A` when `s : Set â„â‰¥0` is compact and `a` varies over
nonnegative elements whose spectrum is contained in `s`, and the function `f` is
continuous on `s`. -/
theorem Continuous.cfc_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s) (f : â„â‰¥0 â†’ â„â‰¥0)
    {a : X â†’ A} (ha_cont : Continuous a) (ha : âˆ€ x, spectrum â„â‰¥0 (a x) âŠ† s)
    (hf : ContinuousOn f s := by cfc_cont_tac) (ha' : âˆ€ x, 0 â‰¤ a x := by cfc_tac) :
    Continuous (fun x â†¦ cfc f (a x)) := by
  rw [â† continuousOn_univ] at ha_cont âŠ¢
  exact ha_cont.cfc_nnreal hs f (fun x _ â†¦ ha x) (fun x _ â†¦ ha' x)

end NNReal

end Right

end Unital

section NonUnital

section Left

section Generic

variable {X R A : Type*} {p : A â†’ Prop} [CommSemiring R] [StarRing R] [MetricSpace R] [Nontrivial R]
    [IsTopologicalSemiring R] [ContinuousStar R] [NonUnitalRing A] [StarRing A]
    [TopologicalSpace A] [Module R A] [SMulCommClass R A A] [IsScalarTower R A A]
    [NonUnitalContinuousFunctionalCalculus R A p]

/-- If `F : X â†’ R â†’ R` tends to `f : R â†’ R` uniformly on the spectrum of `a`, and all
these functions are continuous on the spectrum and map zero to itself, then
`fun x â†¦ cfcâ‚™ (F x) a` tends to `cfcâ‚™ f a`. -/
theorem tendsto_cfcâ‚™_fun {l : Filter X} {F : X â†’ R â†’ R} {f : R â†’ R} {a : A}
    (h_tendsto : TendstoUniformlyOn F f l (quasispectrum R a))
    (hF : âˆ€á¶  x in l, ContinuousOn (F x) (quasispectrum R a)) (hF0 : âˆ€á¶  x in l, F x 0 = 0) :
    Tendsto (fun x â†¦ cfcâ‚™ (F x) a) l (ğ“ (cfcâ‚™ f a)) := by
  open scoped NonUnitalContinuousFunctionalCalculus in
  obtain (rfl | hl) := l.eq_or_neBot
  Â· simp
  have hf := h_tendsto.continuousOn hF
  have hf0 : f 0 = 0 := Eq.symm <|
    tendsto_nhds_unique (tendsto_const_nhds.congr' <| .symm hF0) <|
    h_tendsto.tendsto_at (quasispectrum.zero_mem R a)
  by_cases ha : p a
  Â· let s : Set X := {x | ContinuousOn (F x) (quasispectrum R a) âˆ§ F x 0 = 0}
    have hs : s âˆˆ l := hF.and hF0
    rw [â† tendsto_comap'_iff (i := ((â†‘) : s â†’ X)) (by simpa)]
    conv =>
      enter [1, x]
      rw [Function.comp_apply, cfcâ‚™_apply (hf := x.2.1) (hf0 := x.2.2)]
    rw [cfcâ‚™_apply ..]
    apply cfcâ‚™Hom_continuous _ |>.tendsto _ |>.comp
    rw [ContinuousMapZero.isEmbedding_toContinuousMap.isInducing.tendsto_nhds_iff]
    change Tendsto (fun x : s â†¦ (âŸ¨_, x.2.1.restrictâŸ© : C(quasispectrum R a, R))) _
      (ğ“ âŸ¨_, hf.restrictâŸ©)
    rw [hf.tendsto_restrict_iff_tendstoUniformlyOn (fun x â†¦ x.2.1)]
    intro t
    simp only [eventually_comap, Subtype.forall]
    peel h_tendsto t with ht x _
    simp_all
  Â· simpa [cfcâ‚™_apply_of_not_predicate a ha] using tendsto_const_nhds

/-- If `f : X â†’ R â†’ R` tends to `f xâ‚€` uniformly (along `ğ“ xâ‚€`) on the spectrum of `a`,
and each `f x` is continuous on the spectrum of `a` and maps zero to itself, then
`fun x â†¦ cfcâ‚™ (f x) a` is continuous at `xâ‚€`. -/
theorem continuousAt_cfcâ‚™_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A}
    {xâ‚€ : X} (h_tendsto : TendstoUniformlyOn f (f xâ‚€) (ğ“ xâ‚€) (quasispectrum R a))
    (hf : âˆ€á¶  x in ğ“ xâ‚€, ContinuousOn (f x) (quasispectrum R a))
    (hf0 : âˆ€á¶  x in ğ“ xâ‚€, f x 0 = 0) :
    ContinuousAt (fun x â†¦ cfcâ‚™ (f x) a) xâ‚€ :=
  tendsto_cfcâ‚™_fun h_tendsto hf hf0

/-- If `f : X â†’ R â†’ R` tends to `f xâ‚€` uniformly (along `ğ“[s] xâ‚€`) on the spectrum of `a`,
and eventually each `f x` is continuous on the spectrum of `a` and maps zero to itself, then
`fun x â†¦ cfcâ‚™ (f x) a` is continuous at `xâ‚€` within `s`. -/
theorem continuousWithinAt_cfcâ‚™_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A}
    {xâ‚€ : X} {s : Set X} (h_tendsto : TendstoUniformlyOn f (f xâ‚€) (ğ“[s] xâ‚€) (quasispectrum R a))
    (hf : âˆ€á¶  x in ğ“[s] xâ‚€, ContinuousOn (f x) (quasispectrum R a))
    (hf0 : âˆ€á¶  x in ğ“[s] xâ‚€, f x 0 = 0 := by cfc_zero_tac) :
    ContinuousWithinAt (fun x â†¦ cfcâ‚™ (f x) a) s xâ‚€ :=
  tendsto_cfcâ‚™_fun h_tendsto hf hf0

open UniformOnFun in
/-- If `f : X â†’ R â†’ R` is continuous on `s : Set X` in the topology on
`X â†’ R â†’áµ¤[{spectrum R a}] â†’ R`, and for each `x âˆˆ s`, `f x` is continuous on the spectrum and
maps zero to itself, then `x â†¦ cfcâ‚™ (f x) a` is continuous on `s` also. -/
theorem ContinuousOn.cfcâ‚™_fun [TopologicalSpace X] {f : X â†’ R â†’ R} {a : A} {s : Set X}
    (h_cont : ContinuousOn (fun x â†¦ ofFun {quasispectrum R a} (f x)) s)
    (hf : âˆ€ x âˆˆ s, ContinuousOn (f x) (quasispectrum R a))
    (hf0 : âˆ€ x âˆˆ s, f x 0 = 0) :
    ContinuousOn (fun x â†¦ cfcâ‚™ (f x) a) s := by
  rw [ContinuousOn] at h_cont âŠ¢
  simp only [ContinuousWithinAt, UniformOnFun.tendsto_iff_tendstoUniformlyOn, Set.mem_singleton_iff,
    Function.comp_def, toFun_ofFun, forall_eq] at h_cont
  refine fun x hx â†¦ continuousWithinAt_cfcâ‚™_fun (h_cont x hx) ?_ ?_
  all_goals filter_upwards [self_mem_nhdsWithin] with x hx
  exacts [hf x hx, hf0 x hx]

open UniformOnFun in
/-- If `f : X â†’ R â†’ R` is continuous in the topology on `X â†’ R â†’áµ¤[{spectrum R a}] â†’ R`,
and each `f` is continuous on the spectrum and maps zero to itself, then
`x â†¦ cfcâ‚™ (f x) a` is continuous. -/
theorem Continuous.cfcâ‚™_fun [TopologicalSpace X] (f : X â†’ R â†’ R) (a : A)
    (h_cont : Continuous (fun x â†¦ ofFun {quasispectrum R a} (f x)))
    (hf : âˆ€ x, ContinuousOn (f x) (quasispectrum R a) := by cfc_cont_tac)
    (hf0 : âˆ€ x, f x 0 = 0 := by cfc_zero_tac) :
    Continuous fun x â†¦ cfcâ‚™ (f x) a := by
  rw [â† continuousOn_univ] at h_cont âŠ¢
  exact h_cont.cfcâ‚™_fun (fun x _ â†¦ hf x) (fun x _ â†¦ hf0 x)

end Generic

section Isometric

variable {X R A : Type*} {p : A â†’ Prop} [CommSemiring R] [StarRing R] [MetricSpace R] [Nontrivial R]
    [IsTopologicalSemiring R] [ContinuousStar R] [NonUnitalRing A] [StarRing A]
    [MetricSpace A] [Module R A] [SMulCommClass R A A] [IsScalarTower R A A]
    [NonUnitalIsometricContinuousFunctionalCalculus R A p]

variable (R) in
open UniformOnFun in
open scoped NonUnitalContinuousFunctionalCalculus in
/-- The function `f â†¦ cfcâ‚™ f a` is Lipschitz with constant 1 with respect to
supremum metric (on `R â†’áµ¤[{quasispectrum R a}] R`) on those functions which are continuous on
the quasispectrum and map zero to itself. -/
lemma lipschitzOnWith_cfcâ‚™_fun (a : A) :
    LipschitzOnWith 1 (fun f â†¦ cfcâ‚™ (toFun {quasispectrum R a} f) a)
      {f | ContinuousOn (toFun {quasispectrum R a} f) (quasispectrum R a) âˆ§ f 0 = 0} := by
  by_cases ha : p a
  Â· rintro f âŸ¨hf, hf0âŸ© g âŸ¨hg, hg0âŸ©
    simp only
    rw [cfcâ‚™_apply .., cfcâ‚™_apply .., isometry_cfcâ‚™Hom (R := R) a ha |>.edist_eq]
    simp only [ENNReal.coe_one, one_mul]
    rw [â† ContinuousMapZero.isometry_toContinuousMap.edist_eq,
      edist_continuousRestrict_of_singleton hf hg]
  Â· simpa [cfcâ‚™_apply_of_not_predicate a ha] using LipschitzWith.const' 0 |>.lipschitzOnWith

open UniformOnFun in
open scoped ContinuousFunctionalCalculus in
/-- The function `f â†¦ cfcâ‚™ f a` is Lipschitz with constant 1 with respect to
supremum metric (on `R â†’áµ¤[{s}] R`) on those functions which are continuous on a set `s` containing
the quasispectrum and map zero to itself. -/
lemma lipschitzOnWith_cfcâ‚™_fun_of_subset (a : A) {s : Set R} (hs : quasispectrum R a âŠ† s) :
    LipschitzOnWith 1 (fun f â†¦ cfcâ‚™ (toFun {s} f) a)
      {f | ContinuousOn (toFun {s} f) (s) âˆ§ f 0 = 0} := by
  have hâ‚‚ := lipschitzWith_one_ofFun_toFun' (ğ”– := {quasispectrum R a}) (ğ”— := {s}) (Î² := R)
    (by simpa)
  have hâ‚ƒ := hâ‚‚.lipschitzOnWith (s := {f | ContinuousOn (toFun {s} f) (s) âˆ§ f 0 = 0})
  simpa using lipschitzOnWith_cfcâ‚™_fun R a |>.comp hâ‚ƒ (fun f â†¦ .imp_left fun hf â†¦ hf.mono hs)

end Isometric

end Left

section Right
section RCLike

variable {X ğ•œ A : Type*} {p : A â†’ Prop} [RCLike ğ•œ] [NonUnitalNormedRing A] [StarRing A]
    [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A] [ContinuousStar A]
    [NonUnitalIsometricContinuousFunctionalCalculus ğ•œ A p]

open scoped NonUnitalContinuousFunctionalCalculus ContinuousMapZero in
/-- `cfcâ‚™HomSuperset` is continuous in the variable `a : A` when `s : Set ğ•œ` is compact and `a`
varies over elements whose spectrum is contained in `s`, all of which satisfy the predicate `p`. -/
theorem continuous_cfcâ‚™HomSuperset_left
    [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) [hs0 : Fact (0 âˆˆ s)]
    (f : C(s, ğ•œ)â‚€) {a : X â†’ A} (ha_cont : Continuous a)
    (ha : âˆ€ x, quasispectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€ x, p (a x) := by cfc_tac) :
    Continuous (fun x â†¦ cfcâ‚™HomSuperset (ha' x) (ha x) f) := by
  have : CompactSpace s := by rwa [isCompact_iff_compactSpace] at hs
  induction f using ContinuousMapZero.induction_on_of_compact with
  | zero => simpa [map_zero] using continuous_const
  | id => simpa only [cfcâ‚™HomSuperset_id]
  | star_id => simp only [map_star, cfcâ‚™HomSuperset_id]; fun_prop
  | add f g hf hg => simpa only [map_add] using hf.add hg
  | mul f g hf hg => simpa only [map_mul] using hf.mul hg
  | smul r f hf => simpa only [map_smul] using hf.const_smul r
  | frequently f hf =>
    apply continuous_of_uniform_approx_of_continuous
    rw [Metric.uniformity_basis_dist_le.forall_iff (by aesop)]
    intro Îµ hÎµ
    simp only [Set.mem_setOf_eq, dist_eq_norm]
    obtain âŸ¨g, hg, g_contâŸ© := frequently_iff.mp hf (Metric.closedBall_mem_nhds f hÎµ)
    simp only [Metric.mem_closedBall, dist_comm g, dist_eq_norm] at hg
    refine âŸ¨_, g_cont, fun x â†¦ ?_âŸ©
    rw [â† map_sub, cfcâ‚™HomSuperset_apply]
    rw [isometry_cfcâ‚™Hom (R := ğ•œ) _ (ha' x) |>.norm_map_of_map_zero (map_zero (cfcâ‚™Hom (ha' x)))]
    rw [ContinuousMapZero.norm_def, ContinuousMap.norm_le _ hÎµ.le] at hg âŠ¢
    aesop

variable (A) in
/-- For `f : ğ•œ â†’ ğ•œ` continuous on a set `s` for which `f 0 = 0`, `cfcâ‚™ f` is continuous on the
set of `a : A` satisfying the predicate `p` (associated to `ğ•œ`) and whose `ğ•œ`-quasispectrum is
contained in `s`. -/
theorem continuousOn_cfcâ‚™ {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousOn (cfcâ‚™ f Â· : A â†’ A) {a | p a âˆ§ quasispectrum ğ•œ a âŠ† s} := by
  by_cases hs0 : 0 âˆˆ s
  Â· rw [continuousOn_iff_continuous_restrict]
    convert continuous_cfcâ‚™HomSuperset_left hs (hs0 := âŸ¨hs0âŸ©) âŸ¨âŸ¨_, hf.restrictâŸ©, hf0âŸ©
      (X := {a : A | p a âˆ§ quasispectrum ğ•œ a âŠ† s}) continuous_subtype_val (fun x â†¦ x.2.2) with x
    rw [cfcâ‚™HomSuperset_apply, Set.restrict_apply, cfcâ‚™_apply _ _ (hf.mono x.2.2) hf0 x.2.1]
    congr!
  Â· convert continuousOn_empty _
    rw [Set.eq_empty_iff_forall_notMem]
    exact fun a ha â†¦ hs0 <| ha.2 <| quasispectrum.zero_mem ğ•œ a

open UniformOnFun in
/-- Let `s : Set ğ•œ` be a compact set and consider pairs `(f, a) : (ğ•œ â†’ ğ•œ) Ã— A` where `f` is
continuous on `s`, maps zero itself, and `quasispectrum ğ•œ a âŠ† s` and `a` satisfies the predicate
`p a` for the continuous functional calculus.

Then `cfcâ‚™` is jointly continuous in both variables (i.e., continuous in its uncurried form) on this
set of pairs when the function space is equipped with the topology of uniform convergence on `s`. -/
theorem continuousOn_cfcâ‚™_setProd {s : Set ğ•œ} (hs : IsCompact s) :
    ContinuousOn (fun fa : (ğ•œ â†’áµ¤[{s}] ğ•œ) Ã— A â†¦ cfcâ‚™ (toFun {s} fa.1) fa.2)
      ({f | ContinuousOn (toFun {s} f) s âˆ§ f 0 = 0} Ã—Ë¢ {a | p a âˆ§ quasispectrum ğ•œ a âŠ† s}) :=
  continuousOn_prod_of_continuousOn_lipschitzOnWith _ 1
    (fun f hf â†¦ continuousOn_cfcâ‚™ A hs ((toFun {s}) f) hf.1 hf.2)
    (fun a âŸ¨_, ha'âŸ© â†¦ lipschitzOnWith_cfcâ‚™_fun_of_subset a ha')

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` tends to
`aâ‚€ : A` along a filter `l` (such that eventually `a x` satisfies the predicate `p` associated to
`ğ•œ` and has quasispectrum contained in `s`, as does `aâ‚€`), then `fun x â†¦ cfcâ‚™ f (a x)` tends to
`cfcâ‚™ f aâ‚€`. -/
protected theorem Filter.Tendsto.cfcâ‚™ {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} {aâ‚€ : A} {l : Filter X} (ha_tendsto : Tendsto a l (ğ“ aâ‚€))
    (ha : âˆ€á¶  x in l, quasispectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€á¶  x in l, p (a x))
    (haâ‚€ : quasispectrum ğ•œ aâ‚€ âŠ† s) (haâ‚€' : p aâ‚€) (hf : ContinuousOn f s := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) :
    Tendsto (fun x â†¦ cfcâ‚™ f (a x)) l (ğ“ (cfcâ‚™ f aâ‚€)) := by
  apply continuousOn_cfcâ‚™ A hs f |>.continuousWithinAt âŸ¨haâ‚€', haâ‚€âŸ© |>.tendsto.comp
  rw [tendsto_nhdsWithin_iff]
  exact âŸ¨ha_tendsto, ha'.and haâŸ©

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is continuous
at `xâ‚€`, and eventually `a x` satisfies the predicate `p` associated to `ğ•œ` and has quasispectrum
contained in `s`), then `fun x â†¦ cfcâ‚™ f (a x)` is continuous at `xâ‚€`. -/
protected theorem ContinuousAt.cfcâ‚™ [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} {xâ‚€ : X} (ha_cont : ContinuousAt a xâ‚€)
    (ha : âˆ€á¶  x in ğ“ xâ‚€, quasispectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€á¶  x in ğ“ xâ‚€, p (a x))
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousAt (fun x â†¦ cfcâ‚™ f (a x)) xâ‚€ :=
  ha_cont.tendsto.cfcâ‚™ hs f ha ha' ha.self_of_nhds ha'.self_of_nhds

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is continuous
at `xâ‚€` within a set `t : Set X`, and eventually `a x` satisfies the predicate `p` associated to `ğ•œ`
and has quasispectrum contained in `s`), then `fun x â†¦ cfcâ‚™ f (a x)` is continuous at `xâ‚€`
within `t`. -/
protected theorem ContinuousWithinAt.cfcâ‚™ [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s)
    (f : ğ•œ â†’ ğ•œ) {a : X â†’ A} {xâ‚€ : X} {t : Set X} (hxâ‚€ : xâ‚€ âˆˆ t)
    (ha_cont : ContinuousWithinAt a t xâ‚€) (ha : âˆ€á¶  x in ğ“[t] xâ‚€, quasispectrum ğ•œ (a x) âŠ† s)
    (ha' : âˆ€á¶  x in ğ“[t] xâ‚€, p (a x)) (hf : ContinuousOn f s := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousWithinAt (fun x â†¦ cfcâ‚™ f (a x)) t xâ‚€ :=
  ha_cont.tendsto.cfcâ‚™ hs f ha ha' (ha.self_of_nhdsWithin hxâ‚€) (ha'.self_of_nhdsWithin hxâ‚€)

/-- If `f : ğ•œ â†’ ğ•œ` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is continuous
on `t : Set X`, and `a x` satisfies the predicate `p` associated to `ğ•œ` and has quasispectrum
contained in `s` for all `x âˆˆ t`, then `fun x â†¦ cfcâ‚™ f (a x)` is continuous on `t`. -/
protected theorem ContinuousOn.cfcâ‚™ [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s)
    (f : ğ•œ â†’ ğ•œ) {a : X â†’ A} {t : Set X} (ha_cont : ContinuousOn a t)
    (ha : âˆ€ x âˆˆ t, quasispectrum ğ•œ (a x) âŠ† s) (ha' : âˆ€ x âˆˆ t, p (a x))
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousOn (fun x â†¦ cfcâ‚™ f (a x)) t := by
  rw [ContinuousOn] at ha_cont âŠ¢
  refine fun x hx â†¦ (ha_cont x hx).cfcâ‚™ hs f hx ?_ ?_ hf
  all_goals filter_upwards [self_mem_nhdsWithin] with x hx
  exacts [ha x hx, ha' x hx]

/-- `cfcâ‚™` is continuous in the variable `a : A` when `s : Set ğ•œ` is compact and `a` varies over
elements whose quasispectrum is contained in `s`, all of which satisfy the predicate `p`, and the
function `f` is continuous `s` and `f 0 = 0`. -/
protected theorem Continuous.cfcâ‚™ [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (f : ğ•œ â†’ ğ•œ)
    {a : X â†’ A} (ha_cont : Continuous a) (ha : âˆ€ x, quasispectrum ğ•œ (a x) âŠ† s)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha' : âˆ€ x, p (a x) := by cfc_tac) :
    Continuous (fun x â†¦ cfcâ‚™ f (a x)) := by
  rw [â† continuousOn_univ] at ha_cont âŠ¢
  exact ha_cont.cfcâ‚™ hs f (fun x _ â†¦ ha x) (fun x _ â†¦ ha' x)

/-- `cfcâ‚™` is continuous in the variable `a : A` when `s : Set ğ•œ` is compact and `a` varies over
elements whose spectrum is contained in `s`, all of which satisfy the predicate `p`, and the
function `f` is continuous on the spectrum of `a` and maps zero to itself. -/
theorem continuous_cfcâ‚™ [TopologicalSpace X] {s : Set ğ•œ} (hs : IsCompact s) (hs0 : 0 âˆˆ s)
    (f : ğ•œ â†’ ğ•œ) (a : X â†’ A) (ha_cont : Continuous a) (ha : âˆ€ x, quasispectrum ğ•œ (a x) âŠ† s)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha' : âˆ€ x, p (a x) := by cfc_tac) :
    Continuous (fun x â†¦ cfcâ‚™ f (a x)) := by
  convert continuous_cfcâ‚™HomSuperset_left hs (hs0 := âŸ¨hs0âŸ©) âŸ¨âŸ¨_, hf.restrictâŸ©, hf0âŸ© ha_cont ha
  rw [cfcâ‚™HomSuperset_apply, cfcâ‚™_apply (hf := hf.mono (ha _))]
  congr!

end RCLike

section NNReal

variable {X A : Type*} [NonUnitalNormedRing A] [StarRing A]
    [NormedSpace â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A] [ContinuousStar A]
    [NonUnitalIsometricContinuousFunctionalCalculus â„ A IsSelfAdjoint]
    [PartialOrder A] [StarOrderedRing A] [NonnegSpectrumClass â„ A]
    [T2Space A] [IsTopologicalRing A]

variable (A) in
/-- A version of `continuousOn_cfcâ‚™` over `â„â‰¥0` instead of `RCLike ğ•œ`. -/
theorem continuousOn_cfcâ‚™_nnreal {s : Set â„â‰¥0} (hs : IsCompact s) (f : â„â‰¥0 â†’ â„â‰¥0)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousOn (cfcâ‚™ f Â· : A â†’ A) {a : A | 0 â‰¤ a âˆ§ quasispectrum â„â‰¥0 a âŠ† s} := by
  have : {a : A | 0 â‰¤ a âˆ§ quasispectrum â„â‰¥0 a âŠ† s}.EqOn (cfcâ‚™ f)
      (cfcâ‚™ (fun x : â„ â†¦ f x.toNNReal)) :=
    fun a ha â†¦ cfcâ‚™_nnreal_eq_real _ ha.1
  refine ContinuousOn.congr ?_ this
  replace hf : ContinuousOn (fun x â†¦ f x.toNNReal : â„ â†’ â„) (NNReal.toReal '' s) := by
    apply hf.ofReal_map_toNNReal
    rw [Set.mapsTo_image_iff]
    intro x hx
    simpa
  refine continuousOn_cfcâ‚™ A (hs.image NNReal.continuous_coe) _ hf |>.mono fun a ha â†¦ ?_
  simp only [Set.mem_setOf_eq, nonneg_iff_isSelfAdjoint_and_quasispectrumRestricts] at ha âŠ¢
  refine âŸ¨ha.1.1, ?_âŸ©
  rw [â† ha.1.2.algebraMap_image]
  exact Set.image_mono ha.2

open UniformOnFun in
/-- Let `s : Set â„â‰¥0` be a compact set and consider pairs `(f, a) : (â„â‰¥0 â†’ â„â‰¥0) Ã— A` where `f` is
continuous on `s`, maps zero to itself, `spectrum â„â‰¥0 a âŠ† s` and `0 â‰¤ a`.

Then `cfcâ‚™` is jointly continuous in both variables (i.e., continuous in its uncurried form) on this
set of pairs when the function space is equipped with the topology of uniform convergence on `s`. -/
theorem continuousOn_cfcâ‚™_nnreal_setProd {s : Set â„â‰¥0} (hs : IsCompact s) :
    ContinuousOn (fun fa : (â„â‰¥0 â†’áµ¤[{s}] â„â‰¥0) Ã— A â†¦ cfcâ‚™ (toFun {s} fa.1) fa.2)
      ({f | ContinuousOn (toFun {s} f) s âˆ§ f 0 = 0} Ã—Ë¢ {a | 0 â‰¤ a âˆ§ quasispectrum â„â‰¥0 a âŠ† s}) :=
  continuousOn_prod_of_continuousOn_lipschitzOnWith _ 1
    (fun f hf â†¦ continuousOn_cfcâ‚™_nnreal A hs ((toFun {s}) f) hf.1 hf.2)
    (fun a âŸ¨_, ha'âŸ© â†¦ lipschitzOnWith_cfcâ‚™_fun_of_subset a ha')

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` tends to
`aâ‚€ : A` along a filter `l` (such that eventually `a x` satisfies the predicate `p` associated to
`â„â‰¥0` and has quasispectrum contained in `s`, as does `aâ‚€`), then `fun x â†¦ cfcâ‚™ f (a x)` tends to
`cfcâ‚™ f aâ‚€`. -/
theorem Filter.Tendsto.cfcâ‚™_nnreal {s : Set â„â‰¥0} (hs : IsCompact s) (f : â„â‰¥0 â†’ â„â‰¥0)
    {a : X â†’ A} {aâ‚€ : A} {l : Filter X} (ha_tendsto : Tendsto a l (ğ“ aâ‚€))
    (ha : âˆ€á¶  x in l, quasispectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€á¶  x in l, 0 â‰¤ a x)
    (haâ‚€ : quasispectrum â„â‰¥0 aâ‚€ âŠ† s) (haâ‚€' : 0 â‰¤ aâ‚€) (hf : ContinuousOn f s := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) :
    Tendsto (fun x â†¦ cfcâ‚™ f (a x)) l (ğ“ (cfcâ‚™ f aâ‚€)) := by
  apply continuousOn_cfcâ‚™_nnreal A hs f |>.continuousWithinAt âŸ¨haâ‚€', haâ‚€âŸ© |>.tendsto.comp
  rw [tendsto_nhdsWithin_iff]
  exact âŸ¨ha_tendsto, ha'.and haâŸ©

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is
continuous at `xâ‚€`, and eventually `a x` satisfies the predicate `p` associated to `â„â‰¥0` and has
quasispectrum contained in `s`), then `fun x â†¦ cfcâ‚™ f (a x)` is continuous at `xâ‚€`. -/
theorem ContinuousAt.cfcâ‚™_nnreal [TopologicalSpace X] {s : Set â„â‰¥0}
    (hs : IsCompact s) (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {xâ‚€ : X} (ha_cont : ContinuousAt a xâ‚€)
    (ha : âˆ€á¶  x in ğ“ xâ‚€, quasispectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€á¶  x in ğ“ xâ‚€, 0 â‰¤ a x)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousAt (fun x â†¦ cfcâ‚™ f (a x)) xâ‚€ :=
  ha_cont.tendsto.cfcâ‚™_nnreal hs f ha ha' ha.self_of_nhds ha'.self_of_nhds

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is
continuous at `xâ‚€` within a set `t : Set X`, and eventually `a x` satisfies the predicate `p`
associated to `â„â‰¥0` and has quasispectrum contained in `s`), then `fun x â†¦ cfcâ‚™ f (a x)` is
continuous at `xâ‚€` within `t`. -/
theorem ContinuousWithinAt.cfcâ‚™_nnreal [TopologicalSpace X] {s : Set â„â‰¥0}
    (hs : IsCompact s) (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {xâ‚€ : X} {t : Set X} (hxâ‚€ : xâ‚€ âˆˆ t)
    (ha_cont : ContinuousWithinAt a t xâ‚€) (ha : âˆ€á¶  x in ğ“[t] xâ‚€, quasispectrum â„â‰¥0 (a x) âŠ† s)
    (ha' : âˆ€á¶  x in ğ“[t] xâ‚€, 0 â‰¤ a x) (hf : ContinuousOn f s := by cfc_cont_tac)
    (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousWithinAt (fun x â†¦ cfcâ‚™ f (a x)) t xâ‚€ :=
  ha_cont.tendsto.cfcâ‚™_nnreal hs f ha ha' (ha.self_of_nhdsWithin hxâ‚€) (ha'.self_of_nhdsWithin hxâ‚€)

/-- If `f : â„â‰¥0 â†’ â„â‰¥0` is continuous on a compact set `s` and `f 0 = 0` and `a : X â†’ A` is
continuous on `t : Set X`, and `a x` satisfies the predicate `p` associated to `â„â‰¥0` and has
quasispectrum contained in `s` for all `x âˆˆ t`, then `fun x â†¦ cfcâ‚™ f (a x)` is continuous
on `t`. -/
theorem ContinuousOn.cfcâ‚™_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} {t : Set X} (ha_cont : ContinuousOn a t)
    (ha : âˆ€ x âˆˆ t, quasispectrum â„â‰¥0 (a x) âŠ† s) (ha' : âˆ€ x âˆˆ t, 0 â‰¤ a x)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac) :
    ContinuousOn (fun x â†¦ cfcâ‚™ f (a x)) t := by
  rw [ContinuousOn] at ha_cont âŠ¢
  refine fun x hx â†¦ (ha_cont x hx).cfcâ‚™_nnreal hs f hx ?_ ?_ hf
  all_goals filter_upwards [self_mem_nhdsWithin] with x hx
  exacts [ha x hx, ha' x hx]

/-- `cfcâ‚™` is continuous in the variable `a : A` when `s : Set â„â‰¥0` is compact and `a` varies over
elements whose quasispectrum is contained in `s`, all of which satisfy the predicate `p`, and the
function `f` is continuous `s` and `f 0 = 0`. -/
theorem Continuous.cfcâ‚™_nnreal [TopologicalSpace X] {s : Set â„â‰¥0} (hs : IsCompact s)
    (f : â„â‰¥0 â†’ â„â‰¥0) {a : X â†’ A} (ha_cont : Continuous a) (ha : âˆ€ x, quasispectrum â„â‰¥0 (a x) âŠ† s)
    (hf : ContinuousOn f s := by cfc_cont_tac) (hf0 : f 0 = 0 := by cfc_zero_tac)
    (ha' : âˆ€ x, 0 â‰¤ a x := by cfc_tac) :
    Continuous (fun x â†¦ cfcâ‚™ f (a x)) := by
  rw [â† continuousOn_univ] at ha_cont âŠ¢
  exact ha_cont.cfcâ‚™_nnreal hs f (fun x _ â†¦ ha x) (fun x _ â†¦ ha' x)

end NNReal

end Right

end NonUnital
