/-
Copyright (c) 2023 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/

import Mathlib.Algebra.Algebra.Unitization
import Mathlib.Analysis.NormedSpace.OperatorNorm.Mul

/-!
# Unitization norms

Given a not-necessarily-unital normed `ğ•œ`-algebra `A`, it is frequently of interest to equip its
`Unitization` with a norm which simultaneously makes it into a normed algebra and also satisfies
two properties:

- `â€–1â€– = 1` (i.e., `NormOneClass`)
- The embedding of `A` in `Unitization ğ•œ A` is an isometry. (i.e., `Isometry Unitization.inr`)

One way to do this is to pull back the norm from `WithLp 1 (ğ•œ Ã— A)`, that is,
`â€–(k, a)â€– = â€–kâ€– + â€–aâ€–` using `Unitization.addEquiv` (i.e., the identity map).
This is implemented for the type synonym `WithLp 1 (Unitization ğ•œ A)` in
`WithLp.instUnitizationNormedAddCommGroup`, and it is shown there that this is a Banach algebra.
However, when the norm on `A` is *regular* (i.e., `ContinuousLinearMap.mul` is an isometry), there
is another natural choice: the pullback of the norm on `ğ•œ Ã— (A â†’L[ğ•œ] A)` under the map
`(k, a) â†¦ (k, k â€¢ 1 + ContinuousLinearMap.mul ğ•œ A a)`. It turns out that among all norms on the
unitization satisfying the properties specified above, the norm inherited from
`WithLp 1 (ğ•œ Ã— A)` is maximal, and the norm inherited from this pullback is minimal.
Of course, this means that `WithLp.equiv : WithLp 1 (Unitization ğ•œ A) â†’ Unitization ğ•œ A` can be
upgraded to a continuous linear equivalence (when `ğ•œ` and `A` are complete).

structure on `Unitization ğ•œ A` using the pullback described above. The reason for choosing this norm
is that for a Câ‹†-algebra `A` its norm is always regular, and the pullback norm on `Unitization ğ•œ A`
is then also a Câ‹†-norm.

## Main definitions

- `Unitization.splitMul : Unitization ğ•œ A â†’â‚[ğ•œ] (ğ•œ Ã— (A â†’L[ğ•œ] A))`: The first coordinate of this
  map is just `Unitization.fst` and the second is the `Unitization.lift` of the left regular
  representation of `A` (i.e., `NonUnitalAlgHom.Lmul`). We use this map to pull back the
  `NormedRing` and `NormedAlgebra` structures.

## Main statements

- `Unitization.instNormedRing`, `Unitization.instNormedAlgebra`, `Unitization.instNormOneClass`,
  `Unitization.instCompleteSpace`: when `A` is a non-unital Banach `ğ•œ`-algebra with a regular norm,
  then `Unitization ğ•œ A` is a unital Banach `ğ•œ`-algebra with `â€–1â€– = 1`.
- `Unitization.norm_inr`, `Unitization.isometry_inr`: the natural inclusion `A â†’ Unitization ğ•œ A`
  is an isometry, or in mathematical parlance, the norm on `A` extends to a norm on
  `Unitization ğ•œ A`.

## Implementation details

We ensure that the uniform structure, and hence also the topological structure, is definitionally
equal to the pullback of `instUniformSpaceProd` along `Unitization.addEquiv` (this is essentially
viewing `Unitization ğ•œ A` as `ğ•œ Ã— A`) by means of forgetful inheritance. The same is true of the
bornology.

-/

suppress_compilation

variable (ğ•œ A : Type*) [NontriviallyNormedField ğ•œ] [NonUnitalNormedRing A]
variable [NormedSpace ğ•œ A] [IsScalarTower ğ•œ A A] [SMulCommClass ğ•œ A A]

open ContinuousLinearMap

namespace Unitization

/-- Given `(k, a) : Unitization ğ•œ A`, the second coordinate of `Unitization.splitMul (k, a)` is
the natural representation of `Unitization ğ•œ A` on `A` given by multiplication on the left in
`A â†’L[ğ•œ] A`; note that this is not just `NonUnitalAlgHom.Lmul` for a few reasons: (a) that would
either be `A` acting on `A`, or (b) `Unitization ğ•œ A` acting on `Unitization ğ•œ A`, and (c) that's a
`NonUnitalAlgHom` but here we need an `AlgHom`. In addition, the first coordinate of
`Unitization.splitMul (k, a)` should just be `k`. See `Unitization.splitMul_apply` also. -/
def splitMul : Unitization ğ•œ A â†’â‚[ğ•œ] ğ•œ Ã— (A â†’L[ğ•œ] A) :=
  (lift 0).prod (lift <| NonUnitalAlgHom.Lmul ğ•œ A)

variable {ğ•œ A}

@[simp]
theorem splitMul_apply (x : Unitization ğ•œ A) :
    splitMul ğ•œ A x = (x.fst, algebraMap ğ•œ (A â†’L[ğ•œ] A) x.fst + mul ğ•œ A x.snd) :=
  show (x.fst + 0, _) = (x.fst, _) by rw [add_zero]; rfl

/-- this lemma establishes that if `ContinuousLinearMap.mul ğ•œ A` is injective, then so is
`Unitization.splitMul ğ•œ A`. When `A` is a `RegularNormedAlgebra`, then
`ContinuousLinearMap.mul ğ•œ A` is an isometry, and is therefore automatically injective. -/
theorem splitMul_injective_of_clm_mul_injective
    (h : Function.Injective (mul ğ•œ A)) :
    Function.Injective (splitMul ğ•œ A) := by
  rw [injective_iff_map_eq_zero]
  intro x hx
  induction x
  rw [map_add] at hx
  simp only [splitMul_apply, fst_inl, snd_inl, map_zero, add_zero, fst_inr, snd_inr,
    zero_add, Prod.mk_add_mk, Prod.mk_eq_zero] at hx
  obtain âŸ¨rfl, hxâŸ© := hx
  simp only [map_zero, zero_add, inl_zero] at hx âŠ¢
  rw [â† map_zero (mul ğ•œ A)] at hx
  rw [h hx, inr_zero]

variable [RegularNormedAlgebra ğ•œ A]
variable (ğ•œ A)

/-- In a `RegularNormedAlgebra`, the map `Unitization.splitMul ğ•œ A` is injective.
We will use this to pull back the norm from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A`. -/
theorem splitMul_injective : Function.Injective (splitMul ğ•œ A) :=
  splitMul_injective_of_clm_mul_injective (isometry_mul ğ•œ A).injective

variable {ğ•œ A}

section Aux

/-- Pull back the normed ring structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.splitMul ğ•œ A`. This does not give us the desired topology,
uniformity or bornology on `Unitization ğ•œ A` (which we want to agree with `Prod`), so we only use
it as a local instance to build the real one. -/
noncomputable abbrev normedRingAux : NormedRing (Unitization ğ•œ A) :=
  NormedRing.induced (Unitization ğ•œ A) (ğ•œ Ã— (A â†’L[ğ•œ] A)) (splitMul ğ•œ A) (splitMul_injective ğ•œ A)

attribute [local instance] Unitization.normedRingAux

/-- Pull back the normed algebra structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.splitMul ğ•œ A`. This uses the wrong `NormedRing` instance (i.e.,
`Unitization.normedRingAux`), so we only use it as a local instance to build the real one. -/
noncomputable abbrev normedAlgebraAux : NormedAlgebra ğ•œ (Unitization ğ•œ A) :=
  NormedAlgebra.induced ğ•œ (Unitization ğ•œ A) (ğ•œ Ã— (A â†’L[ğ•œ] A)) (splitMul ğ•œ A)

attribute [local instance] Unitization.normedAlgebraAux

theorem norm_def (x : Unitization ğ•œ A) : â€–xâ€– = â€–splitMul ğ•œ A xâ€– :=
  rfl

theorem nnnorm_def (x : Unitization ğ•œ A) : â€–xâ€–â‚Š = â€–splitMul ğ•œ A xâ€–â‚Š :=
  rfl

/-- This is often the more useful lemma to rewrite the norm as opposed to `Unitization.norm_def`. -/
theorem norm_eq_sup (x : Unitization ğ•œ A) :
    â€–xâ€– = â€–x.fstâ€– âŠ” â€–algebraMap ğ•œ (A â†’L[ğ•œ] A) x.fst + mul ğ•œ A x.sndâ€– := by
  rw [norm_def, splitMul_apply, Prod.norm_def]

/-- This is often the more useful lemma to rewrite the norm as opposed to
`Unitization.nnnorm_def`. -/
theorem nnnorm_eq_sup (x : Unitization ğ•œ A) :
    â€–xâ€–â‚Š = â€–x.fstâ€–â‚Š âŠ” â€–algebraMap ğ•œ (A â†’L[ğ•œ] A) x.fst + mul ğ•œ A x.sndâ€–â‚Š :=
  NNReal.eq <| norm_eq_sup x

theorem lipschitzWith_addEquiv :
    LipschitzWith 2 (Unitization.addEquiv ğ•œ A) := by
  rw [â† Real.toNNReal_ofNat]
  refine AddMonoidHomClass.lipschitz_of_bound (Unitization.addEquiv ğ•œ A) 2 fun x => ?_
  rw [norm_eq_sup, Prod.norm_def]
  refine max_le ?_ ?_
  Â· rw [mul_max_of_nonneg _ _ (zero_le_two : (0 : â„) â‰¤ 2)]
    exact le_max_of_le_left ((le_add_of_nonneg_left (norm_nonneg _)).trans_eq (two_mul _).symm)
  Â· nontriviality A
    rw [two_mul]
    calc
      â€–x.sndâ€– = â€–mul ğ•œ A x.sndâ€– :=
        .symm <| (isometry_mul ğ•œ A).norm_map_of_map_zero (map_zero _) _
      _ â‰¤ â€–algebraMap ğ•œ _ x.fst + mul ğ•œ A x.sndâ€– + â€–x.fstâ€– := by
        simpa only [add_comm _ (mul ğ•œ A x.snd), norm_algebraMap'] using
          norm_le_add_norm_add (mul ğ•œ A x.snd) (algebraMap ğ•œ _ x.fst)
      _ â‰¤ _ := add_le_add le_sup_right le_sup_left

theorem antilipschitzWith_addEquiv :
    AntilipschitzWith 2 (addEquiv ğ•œ A) := by
  refine AddMonoidHomClass.antilipschitz_of_bound (addEquiv ğ•œ A) fun x => ?_
  rw [norm_eq_sup, Prod.norm_def, NNReal.coe_two]
  refine max_le ?_ ?_
  Â· rw [mul_max_of_nonneg _ _ (zero_le_two : (0 : â„) â‰¤ 2)]
    exact le_max_of_le_left ((le_add_of_nonneg_left (norm_nonneg _)).trans_eq (two_mul _).symm)
  Â· nontriviality A
    calc
      â€–algebraMap ğ•œ _ x.fst + mul ğ•œ A x.sndâ€– â‰¤ â€–algebraMap ğ•œ _ x.fstâ€– + â€–mul ğ•œ A x.sndâ€– :=
        norm_add_le _ _
      _ = â€–x.fstâ€– + â€–x.sndâ€– := by
        rw [norm_algebraMap', (AddMonoidHomClass.isometry_iff_norm (mul ğ•œ A)).mp (isometry_mul ğ•œ A)]
      _ â‰¤ _ := (add_le_add (le_max_left _ _) (le_max_right _ _)).trans_eq (two_mul _).symm

open Bornology Filter
open scoped Uniformity Topology

theorem uniformity_eq_aux :
    ğ“¤[instUniformSpaceProd.comap <| addEquiv ğ•œ A] = ğ“¤ (Unitization ğ•œ A) := by
  have key : IsUniformInducing (addEquiv ğ•œ A) :=
    antilipschitzWith_addEquiv.isUniformInducing lipschitzWith_addEquiv.uniformContinuous
  rw [â† key.comap_uniformity]
  rfl

theorem cobounded_eq_aux :
    @cobounded _ (Bornology.induced <| addEquiv ğ•œ A) = cobounded (Unitization ğ•œ A) :=
  le_antisymm lipschitzWith_addEquiv.comap_cobounded_le
    antilipschitzWith_addEquiv.tendsto_cobounded.le_comap

end Aux

/-- The uniformity on `Unitization ğ•œ A` is inherited from `ğ•œ Ã— A`. -/
instance instUniformSpace : UniformSpace (Unitization ğ•œ A) :=
  instUniformSpaceProd.comap (addEquiv ğ•œ A)

/-- The natural equivalence between `Unitization ğ•œ A` and `ğ•œ Ã— A` as a uniform equivalence. -/
def uniformEquivProd : (Unitization ğ•œ A) â‰ƒáµ¤ (ğ•œ Ã— A) :=
  Equiv.toUniformEquivOfIsUniformInducing (addEquiv ğ•œ A) âŸ¨rflâŸ©

/-- The bornology on `Unitization ğ•œ A` is inherited from `ğ•œ Ã— A`. -/
instance instBornology : Bornology (Unitization ğ•œ A) :=
  Bornology.induced <| addEquiv ğ•œ A

theorem isUniformEmbedding_addEquiv {ğ•œ} [NontriviallyNormedField ğ•œ] :
    IsUniformEmbedding (addEquiv ğ•œ A) where
  comap_uniformity := rfl
  injective := (addEquiv ğ•œ A).injective

/-- `Unitization ğ•œ A` is complete whenever `ğ•œ` and `A` are also. -/
instance instCompleteSpace [CompleteSpace ğ•œ] [CompleteSpace A] :
    CompleteSpace (Unitization ğ•œ A) :=
  uniformEquivProd.completeSpace_iff.2 .prod

/-- Pull back the metric structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.splitMul ğ•œ A`, but replace the bornology and the uniformity so
that they coincide with `ğ•œ Ã— A`. -/
noncomputable instance instMetricSpace : MetricSpace (Unitization ğ•œ A) :=
  (normedRingAux.toMetricSpace.replaceUniformity uniformity_eq_aux).replaceBornology
    fun s => Filter.ext_iff.1 cobounded_eq_aux (sá¶œ)

/-- Pull back the normed ring structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.splitMul ğ•œ A`. -/
noncomputable instance instNormedRing : NormedRing (Unitization ğ•œ A) where
  dist_eq := normedRingAux.dist_eq
  norm_mul_le := normedRingAux.norm_mul_le
  norm := normedRingAux.norm

/-- Pull back the normed algebra structure from `ğ•œ Ã— (A â†’L[ğ•œ] A)` to `Unitization ğ•œ A` using the
algebra homomorphism `Unitization.splitMul ğ•œ A`. -/
instance instNormedAlgebra : NormedAlgebra ğ•œ (Unitization ğ•œ A) where
  norm_smul_le k x := by
    rw [norm_def, map_smul]
    -- Note: this used to be `rw [norm_smul, â† norm_def]` before https://github.com/leanprover-community/mathlib4/pull/8386
    exact (norm_smul k (splitMul ğ•œ A x)).le

instance instNormOneClass : NormOneClass (Unitization ğ•œ A) where
  norm_one := by simpa only [norm_eq_sup, fst_one, norm_one, snd_one, map_one, map_zero,
      add_zero, sup_eq_left] using opNorm_le_bound _ zero_le_one fun x => by simp

lemma norm_inr (a : A) : â€–(a : Unitization ğ•œ A)â€– = â€–aâ€– := by
  simp [norm_eq_sup]

lemma nnnorm_inr (a : A) : â€–(a : Unitization ğ•œ A)â€–â‚Š = â€–aâ€–â‚Š :=
  NNReal.eq <| norm_inr a

lemma isometry_inr : Isometry ((â†‘) : A â†’ Unitization ğ•œ A) :=
  AddMonoidHomClass.isometry_of_norm (inrNonUnitalAlgHom ğ•œ A) norm_inr

@[fun_prop]
theorem continuous_inr : Continuous (inr : A â†’ Unitization ğ•œ A) :=
  isometry_inr.continuous

lemma dist_inr (a b : A) : dist (a : Unitization ğ•œ A) (b : Unitization ğ•œ A) = dist a b :=
  isometry_inr.dist_eq a b

lemma nndist_inr (a b : A) : nndist (a : Unitization ğ•œ A) (b : Unitization ğ•œ A) = nndist a b :=
  isometry_inr.nndist_eq a b

/- These examples verify that the bornology and uniformity (hence also the topology) are the
correct ones. -/
example : (instNormedRing (ğ•œ := ğ•œ) (A := A)).toMetricSpace = instMetricSpace := rfl
example : (instMetricSpace (ğ•œ := ğ•œ) (A := A)).toBornology = instBornology := rfl
example : (instMetricSpace (ğ•œ := ğ•œ) (A := A)).toUniformSpace = instUniformSpace := rfl

end Unitization
