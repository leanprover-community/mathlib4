/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.Analytic.RadiusLiminf
import Mathlib.Analysis.Complex.Liouville
import Mathlib.Analysis.Complex.Polynomial.Basic
import Mathlib.Analysis.Normed.Algebra.Spectrum

/-!
# Gelfand's formula and other results on the spectrum in complex Banach algebras

This file contains results on the spectrum of elements in a complex Banach algebra, including
**Gelfand's formula** and the **Gelfand-Mazur theorem** and the fact that every element in a
complex Banach algebra has nonempty spectrum.

## Main results

* `spectrum.hasDerivAt_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius`: Gelfand's formula for the
  spectral radius in Banach algebras over `â„‚`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.
* `NormedRing.algEquivComplexOfComplete`: **Gelfand-Mazur theorem** For a complex
  Banach division algebra, the natural `algebraMap â„‚ A` is an algebra isomorphism whose inverse
  is given by selecting the (unique) element of `spectrum â„‚ a`

-/

variable {ğ•œ A : Type*}

open scoped NNReal Topology
open Filter ENNReal

namespace spectrum

theorem hasDerivAt_resolvent [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]
    [CompleteSpace A] {a : A} {k : ğ•œ} (hk : k âˆˆ resolventSet ğ•œ a) :
    HasDerivAt (resolvent a) (-resolvent a k ^ 2) k := by
  have Hâ‚ : HasFDerivAt Ring.inverse _ (algebraMap ğ•œ A k - a) :=
    hasFDerivAt_ringInverse (ğ•œ := ğ•œ) hk.unit
  have Hâ‚‚ : HasDerivAt (fun k => algebraMap ğ•œ A k - a) 1 k := by
    simpa using (Algebra.linearMap ğ•œ A).hasDerivAt.sub_const a
  simpa [resolvent, sq, hk.unit_spec, â† Ring.inverse_unit hk.unit] using Hâ‚.comp_hasDerivAt k Hâ‚‚

open ENNReal in
/-- In a Banach algebra `A` over `ğ•œ`, for `a : A` the function `fun z â†¦ (1 - z â€¢ a)â»Â¹` is
differentiable on any closed ball centered at zero of radius `r < (spectralRadius ğ•œ a)â»Â¹`. -/
theorem differentiableOn_inverse_one_sub_smul [NontriviallyNormedField ğ•œ] [NormedRing A]
    [NormedAlgebra ğ•œ A] [CompleteSpace A] {a : A} {r : â„â‰¥0}
    (hr : (r : â„â‰¥0âˆ) < (spectralRadius ğ•œ a)â»Â¹) :
    DifferentiableOn ğ•œ (fun z : ğ•œ => Ring.inverse (1 - z â€¢ a)) (Metric.closedBall 0 r) := by
  intro z z_mem
  apply DifferentiableAt.differentiableWithinAt
  have hu : IsUnit (1 - z â€¢ a) := by
    refine isUnit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono ?_) hr)
    simpa only [norm_toNNReal, Real.toNNReal_coe] using
      Real.toNNReal_mono (mem_closedBall_zero_iff.mp z_mem)
  have Hâ‚ : Differentiable ğ•œ fun w : ğ•œ => 1 - w â€¢ a := (differentiable_id.smul_const a).const_sub 1
  exact DifferentiableAt.comp z (differentiableAt_inverse hu) Hâ‚.differentiableAt

section Complex

variable [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A]

open ContinuousMultilinearMap in
/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
theorem limsup_pow_nnnorm_pow_one_div_le_spectralRadius (a : A) :
    limsup (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop â‰¤ spectralRadius â„‚ a := by
  refine ENNReal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt fun r r_pos r_lt => ?_)
  simp_rw [inv_limsup, â† one_div]
  let p : FormalMultilinearSeries â„‚ â„‚ A := fun n =>
    ContinuousMultilinearMap.mkPiRing â„‚ (Fin n) (a ^ n)
  suffices h : (r : â„â‰¥0âˆ) â‰¤ p.radius by
    convert h
    simp only [p, p.radius_eq_liminf, â† norm_toNNReal, norm_mkPiRing]
    congr
    ext n
    rw [norm_toNNReal, ENNReal.coe_rpow_def â€–a ^ nâ€–â‚Š (1 / n : â„), if_neg]
    exact fun ha => (lt_self_iff_false _).mp
      (ha.2.trans_le (one_div_nonneg.mpr n.cast_nonneg : 0 â‰¤ (1 / n : â„)))
  have Hâ‚ := (differentiableOn_inverse_one_sub_smul r_lt).hasFPowerSeriesOnBall r_pos
  exact ((hasFPowerSeriesOnBall_inverse_one_sub_smul â„‚ a).exchange_radius Hâ‚).r_le

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop (ğ“ (spectralRadius â„‚ a)) :=
  tendsto_of_le_liminf_of_limsup_le (spectralRadius_le_liminf_pow_nnnorm_pow_one_div â„‚ a)
    (limsup_pow_nnnorm_pow_one_div_le_spectralRadius a)

alias gelfand_formula := pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => ENNReal.ofReal (â€–a ^ nâ€– ^ (1 / n : â„))) atTop
      (ğ“ (spectralRadius â„‚ a)) := by
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius a using 1
  ext1
  rw [â† ofReal_rpow_of_nonneg (norm_nonneg _) _, â† coe_nnnorm, coe_nnreal_eq]
  exact one_div_nonneg.mpr (mod_cast zero_le _)

section Nontrivial

variable [Nontrivial A]

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
protected theorem nonempty (a : A) : (spectrum â„‚ a).Nonempty := by
  /- Suppose `Ïƒ a = âˆ…`, then resolvent set is `â„‚`, any `(z â€¢ 1 - a)` is a unit, and `resolvent a`
    is differentiable on `â„‚`. -/
  by_contra! h
  have Hâ‚€ : resolventSet â„‚ a = Set.univ := by rwa [spectrum, Set.compl_empty_iff] at h
  have Hâ‚ : Differentiable â„‚ fun z : â„‚ => resolvent a z := fun z =>
    (hasDerivAt_resolvent (Hâ‚€.symm â–¸ Set.mem_univ z : z âˆˆ resolventSet â„‚ a)).differentiableAt
  /- Since `resolvent a` tends to zero at infinity, by Liouville's theorem `resolvent a = 0`,
  which contradicts that `resolvent a z` is invertible. -/
  have Hâ‚ƒ := Hâ‚.apply_eq_of_tendsto_cocompact 0 <| by
    simpa [Metric.cobounded_eq_cocompact] using resolvent_tendsto_cobounded a (ğ•œ := â„‚)
  exact not_isUnit_zero <| Hâ‚ƒ â–¸ (isUnit_resolvent.mp <| Hâ‚€.symm â–¸ Set.mem_univ 0)

/-- In a complex Banach algebra, the spectral radius is always attained by some element of the
spectrum. -/
theorem exists_nnnorm_eq_spectralRadius (a : A) :
    âˆƒ z âˆˆ spectrum â„‚ a, (â€–zâ€–â‚Š : â„â‰¥0âˆ) = spectralRadius â„‚ a :=
  exists_nnnorm_eq_spectralRadius_of_nonempty (spectrum.nonempty a)

/-- In a complex Banach algebra, if every element of the spectrum has norm strictly less than
`r : â„â‰¥0`, then the spectral radius is also strictly less than `r`. -/
theorem spectralRadius_lt_of_forall_lt (a : A) {r : â„â‰¥0}
    (hr : âˆ€ z âˆˆ spectrum â„‚ a, â€–zâ€–â‚Š < r) : spectralRadius â„‚ a < r :=
  spectralRadius_lt_of_forall_lt_of_nonempty (spectrum.nonempty a) hr


open Polynomial in
/-- The **spectral mapping theorem** for polynomials in a Banach algebra over `â„‚`. -/
theorem map_polynomial_aeval (a : A) (p : â„‚[X]) :
    spectrum â„‚ (aeval a p) = (fun k => eval k p) '' spectrum â„‚ a :=
  map_polynomial_aeval_of_nonempty a p (spectrum.nonempty a)

open Polynomial in
/-- A specialization of the spectral mapping theorem for polynomials in a Banach algebra over `â„‚`
to monic monomials. -/
protected theorem map_pow (a : A) (n : â„•) :
    spectrum â„‚ (a ^ n) = (Â· ^ n) '' spectrum â„‚ a := by
  simpa only [aeval_X_pow, eval_X_pow] using map_polynomial_aeval a (X ^ n)

end Nontrivial

omit [CompleteSpace A] in
theorem algebraMap_eq_of_mem (hA : âˆ€ {a : A}, IsUnit a â†” a â‰  0) {a : A} {z : â„‚}
    (h : z âˆˆ spectrum â„‚ a) : algebraMap â„‚ A z = a := by
  rwa [mem_iff, hA, Classical.not_not, sub_eq_zero] at h

/-- **Gelfand-Mazur theorem**: For a complex Banach division algebra, the natural `algebraMap â„‚ A`
is an algebra isomorphism whose inverse is given by selecting the (unique) element of
`spectrum â„‚ a`. In addition, `algebraMap_isometry` guarantees this map is an isometry.

Note: because `NormedDivisionRing` requires the field `norm_mul : âˆ€ a b, â€–a * bâ€– = â€–aâ€– * â€–bâ€–`, we
don't use this type class and instead opt for a `NormedRing` in which the nonzero elements are
precisely the units. This allows for the application of this isomorphism in broader contexts, e.g.,
to the quotient of a complex Banach algebra by a maximal ideal. In the case when `A` is actually a
`NormedDivisionRing`, one may fill in the argument `hA` with the lemma `isUnit_iff_ne_zero`. -/
@[simps]
noncomputable def _root_.NormedRing.algEquivComplexOfComplete (hA : âˆ€ {a : A}, IsUnit a â†” a â‰  0)
    [CompleteSpace A] : â„‚ â‰ƒâ‚[â„‚] A :=
  let nt : Nontrivial A := âŸ¨âŸ¨1, 0, hA.mp âŸ¨âŸ¨1, 1, mul_one _, mul_one _âŸ©, rflâŸ©âŸ©âŸ©
  { Algebra.ofId â„‚ A with
    toFun := algebraMap â„‚ A
    invFun := fun a => (@spectrum.nonempty _ _ _ _ nt a).some
    left_inv := fun z => by
      simpa only [@scalar_eq _ _ _ _ _ nt _] using
        (@spectrum.nonempty _ _ _ _ nt <| algebraMap â„‚ A z).some_mem
    right_inv := fun a => algebraMap_eq_of_mem (@hA) (@spectrum.nonempty _ _ _ _ nt a).some_mem }

end Complex

end spectrum
