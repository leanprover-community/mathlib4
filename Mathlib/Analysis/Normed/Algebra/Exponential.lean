/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Eric Wieser
-/
import Mathlib.Analysis.Analytic.ChangeOrigin
import Mathlib.Analysis.Complex.Basic
import Mathlib.Data.Nat.Choose.Cast

import Mathlib.Analysis.Analytic.OfScalars
import Mathlib.Analysis.SpecificLimits.RCLike

/-!
# Exponential in a Banach algebra

In this file, we define `NormedSpace.exp : ğ”¸ â†’ ğ”¸`,
the exponential map in a topological algebra `ğ”¸` over the field `â„š`.

While for most interesting results we need `ğ”¸` to be normed algebra, we do not require this in the
definition in order to make `NormedSpace.exp` independent of a particular choice of norm. The
definition also does not require that `ğ”¸` be complete, but we need to assume it for most results.

We then prove some basic results, but we avoid importing derivatives here to minimize dependencies.
Results involving derivatives and comparisons with `Real.exp` and `Complex.exp` can be found in
`Analysis.SpecialFunctions.Exponential`.

## Main results

We prove most result for an arbitrary field `ğ•‚`, and then specialize to `ğ•‚ = â„` or `ğ•‚ = â„‚`.

### General case

- `NormedSpace.exp_add_of_commute_of_mem_ball` : if `ğ•‚` has characteristic zero,
  then given two commuting elements `x` and `y` in the disk of convergence, we have
  `NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`
- `NormedSpace.exp_add_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is commutative,
  then given two elements `x` and `y` in the disk of convergence, we have
  `NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`
- `NormedSpace.exp_neg_of_mem_ball` : if `ğ•‚` has characteristic zero and `ğ”¸` is a division ring,
  then given an element `x` in the disk of convergence,
  we have `NormedSpace.exp (-x) = (NormedSpace.exp x)â»Â¹`.

### `ğ•‚ = â„` or `ğ•‚ = â„‚`

- `expSeries_radius_eq_top` : the `FormalMultilinearSeries` defining `NormedSpace.exp`
  has infinite radius of convergence
- `NormedSpace.exp_add_of_commute` : given two commuting elements `x` and `y`, we have
  `NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`
- `NormedSpace.exp_add` : if `ğ”¸` is commutative, then we have
  `NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)` for any `x` and `y`
- `NormedSpace.exp_neg` : if `ğ”¸` is a division ring, then we have
  `NormedSpace.exp (-x) = (NormedSpace.exp x)â»Â¹`.
- `NormedSpace.exp_sum_of_commute` : the analogous result to `NormedSpace.exp_add_of_commute`
  for `Finset.sum`.
- `NormedSpace.exp_sum` : the analogous result to `NormedSpace.exp_add` for `Finset.sum`.
- `NormedSpace.exp_nsmul` : repeated addition in the domain corresponds to
  repeated multiplication in the codomain.
- `NormedSpace.exp_zsmul` : repeated addition in the domain corresponds to
  repeated multiplication in the codomain.

### Notes

We put nearly all the statements in this file in the `NormedSpace` namespace,
to avoid collisions with the `Real` or `Complex` namespaces.

As of 2023-11-16 due to bad instances in Mathlib
```
import Mathlib

open Real

#time example (x : â„) : 0 < exp x      := exp_pos _ -- 250ms
#time example (x : â„) : 0 < Real.exp x := exp_pos _ -- 2ms
```
This is because `exp x` tries the `NormedSpace.exp` function defined here,
and generates a slow coercion search from `Real` to `Type`, to fit the first argument here.
We will resolve this slow coercion separately,
but we want to move `exp` out of the root namespace in any case to avoid this ambiguity.

In the long term is may be possible to replace `Real.exp` and `Complex.exp` with this one.

-/


namespace NormedSpace

open Filter RCLike ContinuousMultilinearMap NormedField Asymptotics FormalMultilinearSeries

open scoped Nat Topology ENNReal

section TopologicalAlgebra

variable (ğ•‚ ğ”¸ : Type*) [Field ğ•‚] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]

/-- `expSeries ğ•‚ ğ”¸` is the `FormalMultilinearSeries` whose `n`-th term is the map
`(xáµ¢) : ğ”¸â¿ â†¦ (1/n! : ğ•‚) â€¢ âˆ xáµ¢`. Its sum is the exponential map `NormedSpace.exp : ğ”¸ â†’ ğ”¸`. -/
def expSeries : FormalMultilinearSeries ğ•‚ ğ”¸ ğ”¸ := fun n =>
  (n !â»Â¹ : ğ•‚) â€¢ ContinuousMultilinearMap.mkPiAlgebraFin ğ•‚ n ğ”¸

/-- The exponential series as an `ofScalars` series. -/
theorem expSeries_eq_ofScalars : expSeries ğ•‚ ğ”¸ = ofScalars ğ”¸ fun n â†¦ (n !â»Â¹ : ğ•‚) := by
  simp_rw [FormalMultilinearSeries.ext_iff, expSeries, ofScalars, implies_true]

variable {ğ”¸}

/-- `NormedSpace.exp : ğ”¸ â†’ ğ”¸` is the exponential map determined by the action of `ğ•‚` on `ğ”¸`.
It is defined as the sum of the `FormalMultilinearSeries` `expSeries ğ•‚ ğ”¸`.

Note that when `ğ”¸ = Matrix n n ğ•‚`, this is the **Matrix Exponential**; see
[`MatrixExponential`](./Mathlib/Analysis/Normed/Algebra/MatrixExponential) for lemmas
specific to that case. -/
noncomputable def exp [Algebra â„š ğ”¸] (x : ğ”¸) : ğ”¸ :=
  (expSeries â„š ğ”¸).sum x

variable {ğ•‚}

theorem expSeries_apply_eq (x : ğ”¸) (n : â„•) :
    (expSeries ğ•‚ ğ”¸ n fun _ => x) = (n !â»Â¹ : ğ•‚) â€¢ x ^ n := by simp [expSeries]

variable (ğ•‚) in
theorem expSeries_apply_eq' (x : ğ”¸) :
    (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) = fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  funext (expSeries_apply_eq x)

theorem expSeries_sum_eq (x : ğ”¸) : (expSeries ğ•‚ ğ”¸).sum x = âˆ‘' n : â„•, (n !â»Â¹ : ğ•‚) â€¢ x ^ n :=
  tsum_congr fun n => expSeries_apply_eq x n

theorem expSeries_sum_eq_rat [Algebra â„š ğ”¸] : (expSeries ğ•‚ ğ”¸).sum = (expSeries â„š ğ”¸).sum := by
  ext; simp_rw [expSeries_sum_eq, inv_nat_cast_smul_eq ğ•‚ â„š]

variable (ğ•‚) in
theorem expSeries_eq_expSeries_rat [Algebra â„š ğ”¸] (n : â„•) :
    â‡‘(expSeries ğ•‚ ğ”¸ n) = expSeries â„š ğ”¸ n:= by
  ext c
  simp [expSeries, inv_nat_cast_smul_eq ğ•‚ â„š]

theorem exp_eq_tsum [Algebra â„š ğ”¸] : exp = fun x : ğ”¸ => âˆ‘' n : â„•, (n !â»Â¹ : â„š) â€¢ x ^ n :=
  funext expSeries_sum_eq

/-- The exponential sum as an `ofScalarsSum`. -/
theorem exp_eq_ofScalarsSum : exp ğ•‚ = ofScalarsSum (E := ğ”¸) fun n â†¦ (n !â»Â¹ : ğ•‚) := by
  rw [exp_eq_tsum, ofScalarsSum_eq_tsum]

theorem expSeries_apply_zero (n : â„•) :
    (expSeries ğ•‚ ğ”¸ n fun _ => (0 : ğ”¸)) = Pi.single (f := fun _ => ğ”¸) 0 1 n := by
  rw [expSeries_apply_eq]
  cases' n with n
  Â· rw [pow_zero, Nat.factorial_zero, Nat.cast_one, inv_one, one_smul, Pi.single_eq_same]
  Â· rw [zero_pow (Nat.succ_ne_zero _), smul_zero, Pi.single_eq_of_ne n.succ_ne_zero]

@[simp]
theorem exp_zero [Algebra â„š ğ”¸] : exp (0 : ğ”¸) = 1 := by
  simp_rw [exp_eq_tsum, â† expSeries_apply_eq, expSeries_apply_zero, tsum_pi_single]

@[simp]
theorem exp_op [Algebra â„š ğ”¸] [T2Space ğ”¸] (x : ğ”¸) :
    exp (MulOpposite.op x) = MulOpposite.op (exp x) := by
  simp_rw [exp, expSeries_sum_eq, â† MulOpposite.op_pow, â† MulOpposite.op_smul, tsum_op]

@[simp]
theorem exp_unop [Algebra â„š ğ”¸] [T2Space ğ”¸] (x : ğ”¸áµáµ’áµ–) :
    exp (MulOpposite.unop x) = MulOpposite.unop (exp x) := by
  simp_rw [exp, expSeries_sum_eq, â† MulOpposite.unop_pow, â† MulOpposite.unop_smul, tsum_unop]

theorem star_exp [Algebra â„š ğ”¸] [T2Space ğ”¸] [StarRing ğ”¸] [ContinuousStar ğ”¸] (x : ğ”¸) :
    star (exp x) = exp (star x) := by
  simp_rw [exp_eq_tsum, â† star_pow, â† star_inv_nat_cast_smul, â† tsum_star]

variable (ğ•‚)

@[aesop safe apply]
theorem _root_.IsSelfAdjoint.exp [Algebra â„š ğ”¸] [T2Space ğ”¸] [StarRing ğ”¸] [ContinuousStar ğ”¸] {x : ğ”¸}
    (h : IsSelfAdjoint x) : IsSelfAdjoint (exp x) :=
  (star_exp x).trans <| h.symm â–¸ rfl

theorem _root_.Commute.exp_right [Algebra â„š ğ”¸] [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) :
    Commute x (exp y) := by
  rw [exp_eq_tsum]
  exact Commute.tsum_right x fun n => (h.pow_right n).smul_right _

theorem _root_.Commute.exp_left [Algebra â„š ğ”¸] [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) :
    Commute (exp x) y :=
  h.symm.exp_right.symm

theorem _root_.Commute.exp [Algebra â„š ğ”¸] [T2Space ğ”¸] {x y : ğ”¸} (h : Commute x y) :
    Commute (exp x) (exp y) :=
  h.exp_left.exp_right

end TopologicalAlgebra

section TopologicalDivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [Field ğ•‚] [DivisionRing ğ”¸] [Algebra ğ•‚ ğ”¸] [TopologicalSpace ğ”¸]
  [TopologicalRing ğ”¸]

theorem expSeries_apply_eq_div (x : ğ”¸) (n : â„•) : (expSeries ğ•‚ ğ”¸ n fun _ => x) = x ^ n / n ! := by
  rw [div_eq_mul_inv, â† (Nat.cast_commute n ! (x ^ n)).inv_leftâ‚€.eq, â† smul_eq_mul,
    expSeries_apply_eq, inv_natCast_smul_eq ğ•‚ ğ”¸]

theorem expSeries_apply_eq_div' (x : ğ”¸) :
    (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) = fun n => x ^ n / n ! :=
  funext (expSeries_apply_eq_div x)

theorem expSeries_sum_eq_div (x : ğ”¸) : (expSeries ğ•‚ ğ”¸).sum x = âˆ‘' n : â„•, x ^ n / n ! :=
  tsum_congr (expSeries_apply_eq_div x)

theorem exp_eq_tsum_div [Algebra â„š ğ”¸] : exp = fun x : ğ”¸ => âˆ‘' n : â„•, x ^ n / n ! :=
  funext expSeries_sum_eq_div

end TopologicalDivisionAlgebra

section Normed

section AnyFieldAnyAlgebra

variable {ğ•‚ ğ”¸ ğ”¹ : Type*} [NontriviallyNormedField ğ•‚]
variable [NormedRing ğ”¸] [NormedRing ğ”¹] [NormedAlgebra ğ•‚ ğ”¸]

theorem norm_expSeries_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => â€–expSeries ğ•‚ ğ”¸ n fun _ => xâ€– :=
  (expSeries ğ•‚ ğ”¸).summable_norm_apply hx

theorem norm_expSeries_summable_of_mem_ball' [Algebra â„š ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => â€–(n !â»Â¹ : â„š) â€¢ x ^ nâ€– := by
  change Summable (norm âˆ˜ _)
  rw [â† expSeries_apply_eq']
  convert norm_expSeries_summable_of_mem_ball x hx
  simp_rw [expSeries_eq_expSeries_rat, Function.comp_apply]

section CompleteAlgebra

variable [CompleteSpace ğ”¸]

theorem expSeries_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => expSeries ğ•‚ ğ”¸ n fun _ => x :=
  (norm_expSeries_summable_of_mem_ball x hx).of_norm

theorem expSeries_summable_of_mem_ball' [Algebra â„š ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    Summable fun n => (n !â»Â¹ : â„š) â€¢ x ^ n :=
  (norm_expSeries_summable_of_mem_ball' x hx).of_norm

theorem expSeries_hasSum_exp_of_mem_ball [Algebra â„š ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) (exp x) := by
  simpa only [exp, expSeries_sum_eq_rat] using FormalMultilinearSeries.hasSum (expSeries ğ•‚ ğ”¸) hx

theorem expSeries_hasSum_exp_of_mem_ball' [Algebra â„š ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n) (exp x) := by
  rw [â† expSeries_apply_eq']
  exact expSeries_hasSum_exp_of_mem_ball x hx

theorem hasFPowerSeriesOnBall_exp_of_radius_pos [Algebra â„š ğ”¸] (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasFPowerSeriesOnBall exp (expSeries ğ•‚ ğ”¸) 0 (expSeries ğ•‚ ğ”¸).radius := by
  simpa only [exp, expSeries_sum_eq_rat] using (expSeries ğ•‚ ğ”¸).hasFPowerSeriesOnBall h

theorem hasFPowerSeriesAt_exp_zero_of_radius_pos [Algebra â„š ğ”¸] (h : 0 < (expSeries ğ•‚ ğ”¸).radius) :
    HasFPowerSeriesAt exp (expSeries ğ•‚ ğ”¸) 0 := by
  simpa only [exp, expSeries_sum_eq_rat] using
  (hasFPowerSeriesOnBall_exp_of_radius_pos h).hasFPowerSeriesAt

theorem continuousOn_exp [Algebra â„š ğ”¸] :
    ContinuousOn (exp : ğ”¸ â†’ ğ”¸) (EMetric.ball 0 (expSeries ğ•‚ ğ”¸).radius) := by
  have := @FormalMultilinearSeries.continuousOn _ _ _ _ _ _ _ _ (expSeries ğ•‚ ğ”¸)
  simpa only [exp, expSeries_sum_eq_rat] using this

theorem analyticAt_exp_of_mem_ball [Algebra â„š ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : AnalyticAt ğ•‚ exp x := by
  by_cases h : (expSeries ğ•‚ ğ”¸).radius = 0
  Â· rw [h] at hx; exact (ENNReal.not_lt_zero hx).elim
  Â· have h := pos_iff_ne_zero.mpr h
    exact (hasFPowerSeriesOnBall_exp_of_radius_pos h).analyticAt_of_mem hx

variable (ğ•‚)

/-- In a Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero, if `x` and `y` are
in the disk of convergence and commute, then
`NormedSpace.exp (x + y) = (NormedSpace.exp x) * (NormedSpace.exp y)`. -/
theorem exp_add_of_commute_of_mem_ball [Algebra â„š ğ”¸] {x y : ğ”¸} (hxy : Commute x y)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius)
    (hy : y âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp (x + y) = exp x * exp y := by
  rw [exp_eq_tsum,
    tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm
      (norm_expSeries_summable_of_mem_ball' x hx) (norm_expSeries_summable_of_mem_ball' y hy)]
  dsimp only
  conv_lhs =>
    congr
    ext
    rw [hxy.add_pow' _, Finset.smul_sum]
  refine tsum_congr fun n => Finset.sum_congr rfl fun kl hkl => ?_
  rw [â† Nat.cast_smul_eq_nsmul â„š, smul_smul, smul_mul_smul_comm, â† Finset.mem_antidiagonal.mp hkl,
    Nat.cast_add_choose, Finset.mem_antidiagonal.mp hkl]
  congr 1
  have : (n ! : â„š) â‰  0 := Nat.cast_ne_zero.mpr n.factorial_ne_zero
  field_simp [this]

/-- `NormedSpace.exp x` has explicit two-sided inverse `NormedSpace.exp (-x)`. -/
noncomputable def invertibleExpOfMemBall [Algebra â„š ğ”¸] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : Invertible (exp x)
    where
  invOf := exp (-x)
  invOf_mul_self := by
    have hnx : -x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius := by
      rw [EMetric.mem_ball, â† neg_zero, edist_neg_neg]
      exact hx
    rw [â† exp_add_of_commute_of_mem_ball _ (Commute.neg_left <| Commute.refl x) hnx hx,
      neg_add_cancel, exp_zero]
  mul_invOf_self := by
    have hnx : -x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius := by
      rw [EMetric.mem_ball, â† neg_zero, edist_neg_neg]
      exact hx
    rw [â† exp_add_of_commute_of_mem_ball _ (Commute.neg_right <| Commute.refl x) hx hnx, add_neg_self,
      exp_zero]

theorem isUnit_exp_of_mem_ball [Algebra â„š ğ”¸] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : IsUnit (exp x) :=
  @isUnit_of_invertible _ _ _ (invertibleExpOfMemBall _ hx)

theorem invOf_exp_of_mem_ball [Algebra â„š ğ”¸] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) [Invertible (exp x)] :
    â…Ÿ (exp x) = exp (-x) := by
  letI := invertibleExpOfMemBall _ hx; convert (rfl : â…Ÿ (exp x) = _)

/-- Any continuous ring homomorphism commutes with `NormedSpace.exp`. -/
theorem map_exp_of_mem_ball [Algebra â„š ğ”¸] [Algebra â„š ğ”¹] {F} [FunLike F ğ”¸ ğ”¹] [RingHomClass F ğ”¸ ğ”¹]
    (f : F) (hf : Continuous f) (x : ğ”¸) (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    f (exp x) = exp (f x) := by
  rw [exp_eq_tsum, exp_eq_tsum]
  refine ((expSeries_summable_of_mem_ball' _ hx).hasSum.map f hf).tsum_eq.symm.trans ?_
  dsimp only [Function.comp_def]
  simp_rw [map_inv_natCast_smul f â„š â„š, map_pow]

end CompleteAlgebra

theorem algebraMap_exp_comm_of_mem_ball [Algebra â„š ğ”¸] [CharZero ğ•‚] [CompleteSpace ğ•‚] (x : ğ•‚)
    (hx : x âˆˆ EMetric.ball (0 : ğ•‚) (expSeries ğ•‚ ğ•‚).radius) :
    algebraMap ğ•‚ ğ”¸ (exp x) = exp (algebraMap ğ•‚ ğ”¸ x) :=
  map_exp_of_mem_ball _ (algebraMap _ _) (algebraMapCLM _ _).continuous _ hx

end AnyFieldAnyAlgebra

section AnyFieldDivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable (ğ•‚)

theorem norm_expSeries_div_summable_of_mem_ball (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : Summable fun n => â€–x ^ n / (n !)â€– := by
  change Summable (norm âˆ˜ _)
  rw [â† expSeries_apply_eq_div' (ğ•‚ := ğ•‚) x]
  exact norm_expSeries_summable_of_mem_ball x hx

theorem expSeries_div_summable_of_mem_ball [CompleteSpace ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : Summable fun n => x ^ n / n ! :=
  (norm_expSeries_div_summable_of_mem_ball ğ•‚ x hx).of_norm

theorem expSeries_div_hasSum_exp_of_mem_ball [Algebra â„š ğ”¸] [CompleteSpace ğ”¸] (x : ğ”¸)
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) :
    HasSum (fun n => x ^ n / n !) (exp x) := by
  rw [â† expSeries_apply_eq_div' (ğ•‚ := ğ•‚) x]
  exact expSeries_hasSum_exp_of_mem_ball x hx

theorem exp_neg_of_mem_ball [Algebra â„š ğ”¸] [CompleteSpace ğ”¸] {x : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp (-x) = (exp x)â»Â¹ :=
  letI := invertibleExpOfMemBall _ hx
  invOf_eq_inv (exp x)

end AnyFieldDivisionAlgebra

section AnyFieldCommAlgebra

variable {ğ•‚ ğ”¸ : Type*} [NontriviallyNormedField ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
  [CompleteSpace ğ”¸]

variable (ğ•‚)

/-- In a commutative Banach-algebra `ğ”¸` over a normed field `ğ•‚` of characteristic zero,
`NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`
for all `x`, `y` in the disk of convergence. -/
theorem exp_add_of_mem_ball [Algebra â„š ğ”¸] {x y : ğ”¸}
    (hx : x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius)
    (hy : y âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius) : exp (x + y) = exp x * exp y :=
  exp_add_of_commute_of_mem_ball ğ•‚ (Commute.all x y) hx hy

end AnyFieldCommAlgebra

section RCLike

section AnyAlgebra

variable (ğ•‚ ğ”¸ ğ”¹ : Type*) [RCLike ğ•‚] [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable [NormedRing ğ”¹]

/-- In a normed algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, the series defining the exponential map
has an infinite radius of convergence. -/
theorem expSeries_radius_eq_top : (expSeries ğ•‚ ğ”¸).radius = âˆ := by
  have {n : â„•} : (Nat.factorial n : ğ•‚) â‰  0 := Nat.cast_ne_zero.mpr (Nat.factorial_ne_zero n)
  apply expSeries_eq_ofScalars ğ•‚ ğ”¸ â–¸
    ofScalars_radius_eq_top_of_tendsto ğ”¸ _ (Eventually.of_forall fun n => ?_)
  Â· simp_rw [â† norm_div, Nat.factorial_succ, Nat.cast_mul, mul_inv_rev, mul_div_right_comm,
      inv_div_inv, norm_mul, div_self this, norm_one, one_mul]
    apply norm_zero (E := ğ•‚) â–¸ Filter.Tendsto.norm
    apply (Filter.tendsto_add_atTop_iff_nat (f := fun n => (n : ğ•‚)â»Â¹) 1).mpr
    exact RCLike.tendsto_inverse_atTop_nhds_zero_nat ğ•‚
  Â· simp [this]

theorem expSeries_radius_pos : 0 < (expSeries ğ•‚ ğ”¸).radius := by
  rw [expSeries_radius_eq_top]
  exact WithTop.top_pos

variable {ğ•‚ ğ”¸ ğ”¹}

theorem norm_expSeries_summable (x : ğ”¸) : Summable fun n => â€–expSeries ğ•‚ ğ”¸ n fun _ => xâ€– :=
  norm_expSeries_summable_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variable (ğ•‚)

theorem norm_expSeries_summable' [Algebra â„š ğ”¸] (x : ğ”¸) : Summable fun n => â€–(n !â»Â¹ : â„š) â€¢ x ^ nâ€– :=
  norm_expSeries_summable_of_mem_ball' x
    (show x âˆˆ EMetric.ball (0 : ğ”¸) (expSeries ğ•‚ ğ”¸).radius from
      (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variable {ğ•‚}

section CompleteAlgebra

variable [CompleteSpace ğ”¸]

theorem expSeries_summable (x : ğ”¸) : Summable fun n => expSeries ğ•‚ ğ”¸ n fun _ => x :=
  (norm_expSeries_summable x).of_norm

variable (ğ•‚) in
theorem expSeries_summable' [Algebra â„š ğ”¸] (x : ğ”¸) : Summable fun n => (n !â»Â¹ : â„š) â€¢ x ^ n :=
  (norm_expSeries_summable' ğ•‚ x).of_norm

variable [Algebra â„š ğ”¸] [Algebra â„š ğ”¹]

theorem expSeries_hasSum_exp (x : ğ”¸) : HasSum (fun n => expSeries ğ•‚ ğ”¸ n fun _ => x) (exp x) :=
  expSeries_hasSum_exp_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

theorem exp_series_hasSum_exp' (x : ğ”¸) : HasSum (fun n => (n !â»Â¹ : ğ•‚) â€¢ x ^ n) (exp x) :=
  expSeries_hasSum_exp_of_mem_ball' x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

theorem exp_hasFPowerSeriesOnBall : HasFPowerSeriesOnBall exp (expSeries ğ•‚ ğ”¸) 0 âˆ :=
  expSeries_radius_eq_top ğ•‚ ğ”¸ â–¸ hasFPowerSeriesOnBall_exp_of_radius_pos (expSeries_radius_pos _ _)

theorem exp_hasFPowerSeriesAt_zero : HasFPowerSeriesAt exp (expSeries ğ•‚ ğ”¸) 0 :=
  exp_hasFPowerSeriesOnBall.hasFPowerSeriesAt

section
variable (ğ•‚)

@[continuity]
theorem exp_continuous : Continuous (exp : ğ”¸ â†’ ğ”¸) := by
  rw [continuous_iff_continuousOn_univ, â† Metric.eball_top_eq_univ (0 : ğ”¸), â†
    expSeries_radius_eq_top ğ•‚ ğ”¸]
  exact continuousOn_exp

open Topology in
lemma _root_.Filter.Tendsto.exp {Î± : Type*} {l : Filter Î±} {f : Î± â†’ ğ”¸} {a : ğ”¸}
    (hf : Tendsto f l (ğ“ a)) :
    Tendsto (fun x => exp (f x)) l (ğ“ (exp a)) :=
  (exp_continuous ğ•‚ |>.tendsto _).comp hf

end

theorem exp_analytic (x : ğ”¸) : AnalyticAt ğ•‚ exp x :=
  analyticAt_exp_of_mem_ball x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variable (ğ•‚)

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if `x` and `y` commute, then
`NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`. -/
theorem exp_add_of_commute {x y : ğ”¸} (hxy : Commute x y) : exp (x + y) = exp x * exp y :=
  exp_add_of_commute_of_mem_ball ğ•‚ hxy ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

section

/-- `NormedSace.exp x` has explicit two-sided inverse `NormedSace.exp  ğ•‚ (-x)`. -/
noncomputable def invertibleExp (x : ğ”¸) : Invertible (exp ğ•‚ x) :=
  invertibleExpOfMemBall <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem invOf_exp (x : ğ”¸) [Invertible (exp x)] : â…Ÿ (exp x) = exp (-x) :=
  invOf_exp_of_mem_ball ğ•‚ <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem _root_.Ring.inverse_exp (x : ğ”¸) : Ring.inverse (exp x) = exp (-x) :=
  letI := invertibleExp ğ•‚ x
  Ring.inverse_invertible _

theorem exp_mem_unitary_of_mem_skewAdjoint [StarRing ğ”¸] [ContinuousStar ğ”¸] {x : ğ”¸}
    (h : x âˆˆ skewAdjoint ğ”¸) : exp x âˆˆ unitary ğ”¸ := by
  rw [unitary.mem_iff, star_exp, skewAdjoint.mem_iff.mp h, â†
    exp_add_of_commute ğ•‚ (Commute.refl x).neg_left, â†
    exp_add_of_commute ğ•‚ (Commute.refl x).neg_right, neg_add_cancel, add_neg_cancel, exp_zero,
    and_self_iff]

end

/-- In a Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`, if a family of elements `f i` mutually
commute then `NormedSpace.exp (âˆ‘ i, f i) = âˆ i, NormedSpace.exp (f i)`. -/
theorem exp_sum_of_commute {Î¹} (s : Finset Î¹) (f : Î¹ â†’ ğ”¸)
    (h : (s : Set Î¹).Pairwise (Commute on f)) :
    exp (âˆ‘ i âˆˆ s, f i) =
      s.noncommProd (fun i => exp (f i)) fun _ hi _ hj _ => (h.of_refl hi hj).exp := by
  classical
    induction' s using Finset.induction_on with a s ha ih
    Â· simp
    rw [Finset.noncommProd_insert_of_not_mem _ _ _ _ ha, Finset.sum_insert ha, exp_add_of_commute ğ•‚,
      ih (h.mono <| Finset.subset_insert _ _)]
    refine Commute.sum_right _ _ _ fun i hi => ?_
    exact h.of_refl (Finset.mem_insert_self _ _) (Finset.mem_insert_of_mem hi)

theorem exp_nsmul (n : â„•) (x : ğ”¸) : exp (n â€¢ x) = exp x ^ n := by
  induction n with
  | zero => rw [zero_smul, pow_zero, exp_zero]
  | succ n ih => rw [succ_nsmul, pow_succ, exp_add_of_commute ğ•‚ ((Commute.refl x).smul_left n), ih]

/-- Any continuous ring homomorphism commutes with `NormedSpace.exp`. -/
theorem map_exp {F} [FunLike F ğ”¸ ğ”¹] [RingHomClass F ğ”¸ ğ”¹] (f : F) (hf : Continuous f) (x : ğ”¸) :
    f (exp x) = exp (f x) :=
  map_exp_of_mem_ball ğ•‚ f hf x <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem exp_smul {G} [Monoid G] [MulSemiringAction G ğ”¸] [ContinuousConstSMul G ğ”¸] (g : G) (x : ğ”¸) :
    exp (g â€¢ x) = g â€¢ exp x :=
  (map_exp ğ•‚ (MulSemiringAction.toRingHom G ğ”¸ g) (continuous_const_smul g) x).symm

theorem exp_units_conj (y : ğ”¸Ë£) (x : ğ”¸) : exp (y * x * â†‘yâ»Â¹ : ğ”¸) = y * exp x * â†‘yâ»Â¹ :=
  exp_smul ğ•‚ (ConjAct.toConjAct y) x

theorem exp_units_conj' (y : ğ”¸Ë£) (x : ğ”¸) : exp (â†‘yâ»Â¹ * x * y) = â†‘yâ»Â¹ * exp x * y :=
  exp_units_conj ğ•‚ _ _

-- @[simp]
theorem _root_.Prod.fst_exp [NormedAlgebra ğ•‚ ğ”¹] [CompleteSpace ğ”¹] (x : ğ”¸ Ã— ğ”¹) :
    (exp x).fst = exp x.fst :=
  map_exp ğ•‚ (RingHom.fst ğ”¸ ğ”¹) continuous_fst x

-- @[simp]
theorem _root_.Prod.snd_exp [NormedAlgebra ğ•‚ ğ”¹] [CompleteSpace ğ”¹] (x : ğ”¸ Ã— ğ”¹) :
    (exp x).snd = exp x.snd :=
  map_exp ğ•‚ (RingHom.snd ğ”¸ ğ”¹) continuous_snd x

-- @[simp]
theorem _root_.Pi.coe_exp {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, NormedRing (ğ”¸ i)]
    [âˆ€ i, Algebra â„š (ğ”¸ i)] [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)] [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i)
    (i : Î¹) :
    exp x i = exp (x i) :=
  -- porting note: Lean can now handle Î -types in type class inference!
  map_exp ğ•‚ (Pi.evalRingHom ğ”¸ i) (continuous_apply _) x

theorem _root_.Pi.exp_def {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, NormedRing (ğ”¸ i)]
    [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)] [âˆ€ i, Algebra â„š (ğ”¸ i)] [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i) :
    exp x = fun i => exp (x i) :=
  funext <| Pi.coe_exp ğ•‚ x

theorem _root_.Function.update_exp {Î¹ : Type*} {ğ”¸ : Î¹ â†’ Type*} [Fintype Î¹] [DecidableEq Î¹]
    [âˆ€ i, NormedRing (ğ”¸ i)] [âˆ€ i, Algebra â„š (ğ”¸ i)] [âˆ€ i, NormedAlgebra ğ•‚ (ğ”¸ i)]
    [âˆ€ i, CompleteSpace (ğ”¸ i)] (x : âˆ€ i, ğ”¸ i) (j : Î¹) (xj : ğ”¸ j) :
    Function.update (exp x) j (exp xj) = exp (Function.update x j xj) := by
  ext i
  simp_rw [Pi.exp_def ğ•‚]
  exact (Function.apply_update (fun i => exp) x j xj i).symm

end CompleteAlgebra

theorem algebraMap_exp_comm [Algebra â„š ğ”¸] (x : ğ•‚) :
    algebraMap ğ•‚ ğ”¸ (exp x) = exp (algebraMap ğ•‚ ğ”¸ x) :=
  algebraMap_exp_comm_of_mem_ball x <| (expSeries_radius_eq_top ğ•‚ ğ•‚).symm â–¸ edist_lt_top _ _

end AnyAlgebra

section DivisionAlgebra

variable {ğ•‚ ğ”¸ : Type*} [RCLike ğ•‚] [NormedDivisionRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸]
variable (ğ•‚)
include ğ•‚

theorem norm_expSeries_div_summable (x : ğ”¸) : Summable fun n => â€–(x ^ n / n ! : ğ”¸)â€– :=
  norm_expSeries_div_summable_of_mem_ball ğ•‚ x
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

variable [CompleteSpace ğ”¸]

theorem expSeries_div_summable (x : ğ”¸) : Summable fun n => x ^ n / n ! :=
  (norm_expSeries_div_summable ğ•‚ x).of_norm

variable [Algebra â„š ğ”¸]

theorem expSeries_div_hasSum_exp (x : ğ”¸) : HasSum (fun n => x ^ n / n !) (exp x) :=
  expSeries_div_hasSum_exp_of_mem_ball ğ•‚ x ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

theorem exp_neg (x : ğ”¸) : exp (-x) = (exp x)â»Â¹ :=
  exp_neg_of_mem_ball ğ•‚ <| (expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _

theorem exp_zsmul (z : â„¤) (x : ğ”¸) : exp (z â€¢ x) = exp x ^ z := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· rw [zpow_natCast, natCast_zsmul, exp_nsmul ğ•‚]
  Â· rw [zpow_neg, zpow_natCast, neg_smul, exp_neg ğ•‚, natCast_zsmul, exp_nsmul ğ•‚]

theorem exp_conj (y : ğ”¸) (x : ğ”¸) (hy : y â‰  0) : exp (y * x * yâ»Â¹) = y * exp x * yâ»Â¹ :=
  exp_units_conj ğ•‚ (Units.mk0 y hy) x

theorem exp_conj' (y : ğ”¸) (x : ğ”¸) (hy : y â‰  0) : exp (yâ»Â¹ * x * y) = yâ»Â¹ * exp x * y :=
  exp_units_conj' ğ•‚ (Units.mk0 y hy) x

end DivisionAlgebra

section CommAlgebra

variable {ğ•‚ ğ”¸ : Type _} [RCLike ğ•‚] [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

variable [Algebra â„š ğ”¸]

/-- In a commutative Banach-algebra `ğ”¸` over `ğ•‚ = â„` or `ğ•‚ = â„‚`,
`NormedSpace.exp (x+y) = (NormedSpace.exp x) * (NormedSpace.exp y)`. -/
theorem exp_add {x y : ğ”¸} : exp (x + y) = exp x * exp y :=
  exp_add_of_mem_ball ğ•‚ ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)
    ((expSeries_radius_eq_top ğ•‚ ğ”¸).symm â–¸ edist_lt_top _ _)

/-- A version of `NormedSpace.exp_sum_of_commute` for a commutative Banach-algebra. -/
theorem exp_sum {Î¹} (s : Finset Î¹) (f : Î¹ â†’ ğ”¸) : exp (âˆ‘ i âˆˆ s, f i) = âˆ i âˆˆ s, exp (f i) := by
  rw [exp_sum_of_commute ğ•‚, Finset.noncommProd_eq_prod]
  exact fun i _hi j _hj _ => Commute.all _ _

end CommAlgebra

end RCLike

end Normed

section ScalarTower

variable (ğ•‚ ğ•‚' ğ”¸ : Type*) [Field ğ•‚] [Field ğ•‚'] [Ring ğ”¸] [Algebra ğ•‚ ğ”¸] [Algebra ğ•‚' ğ”¸]
  [TopologicalSpace ğ”¸] [TopologicalRing ğ”¸]

/-- If a normed ring `ğ”¸` is a normed algebra over two fields, then they define the same
`expSeries` on `ğ”¸`. -/
theorem expSeries_eq_expSeries (n : â„•) (x : ğ”¸) :
    (expSeries ğ•‚ ğ”¸ n fun _ => x) = expSeries ğ•‚' ğ”¸ n fun _ => x := by
  rw [expSeries_apply_eq, expSeries_apply_eq, inv_natCast_smul_eq ğ•‚ ğ•‚']

/-- A version of `Complex.ofReal_exp` for `NormedSpace.exp` instead of `Complex.exp` -/
@[simp, norm_cast]
theorem of_real_exp_â„_â„ (r : â„) : â†‘(exp r) = exp (r : â„‚) :=
  map_exp â„ (algebraMap â„ â„‚) (continuous_algebraMap _ _) r

end ScalarTower

end NormedSpace
