/-
Copyright (c) 2022 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Analysis.Normed.Algebra.Exponential
import Mathlib.Analysis.Matrix
import Mathlib.LinearAlgebra.Matrix.ZPow
import Mathlib.LinearAlgebra.Matrix.Hermitian
import Mathlib.LinearAlgebra.Matrix.Symmetric
import Mathlib.Topology.UniformSpace.Matrix

/-!
# Lemmas about the matrix exponential

In this file, we provide results about `NormedSpace.exp` on `Matrix`s
over a topological or normed algebra.
Note that generic results over all topological spaces such as `NormedSpace.exp_zero`
can be used on matrices without issue, so are not repeated here.
The topological results specific to matrices are:

* `Matrix.exp_transpose`
* `Matrix.exp_conjTranspose`
* `Matrix.exp_diagonal`
* `Matrix.exp_blockDiagonal`
* `Matrix.exp_blockDiagonal'`

Lemmas like `NormedSpace.exp_add_of_commute` require a canonical norm on the type;
while there are multiple sensible choices for the norm of a `Matrix` (`Matrix.normedAddCommGroup`,
`Matrix.frobeniusNormedAddCommGroup`, `Matrix.linftyOpNormedAddCommGroup`), none of them
are canonical. In an application where a particular norm is chosen using
`attribute [local instance]`, then the usual lemmas about `NormedSpace.exp` are fine.
When choosing a norm is undesirable, the results in this file can be used.

In this file, we copy across the lemmas about `NormedSpace.exp`,
but hide the requirement for a norm inside the proof.

* `Matrix.exp_add_of_commute`
* `Matrix.exp_sum_of_commute`
* `Matrix.exp_nsmul`
* `Matrix.isUnit_exp`
* `Matrix.exp_units_conj`
* `Matrix.exp_units_conj'`

Additionally, we prove some results about `matrix.has_inv` and `matrix.div_inv_monoid`, as the
results for general rings are instead stated about `Ring.inverse`:

* `Matrix.exp_neg`
* `Matrix.exp_zsmul`
* `Matrix.exp_conj`
* `Matrix.exp_conj'`

## TODO

* Show that `Matrix.det (NormedSpace.exp ğ•‚ A) = NormedSpace.exp ğ•‚ (Matrix.trace A)`

## References

* https://en.wikipedia.org/wiki/Matrix_exponential
-/


open scoped Matrix

open NormedSpace -- For `exp`.

variable (ğ•‚ : Type*) {m n : Type*} {n' : m â†’ Type*} {ğ”¸ : Type*}

namespace Matrix

section Topological

section Ring

variable [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n] [âˆ€ i, Fintype (n' i)]
  [âˆ€ i, DecidableEq (n' i)] [Field ğ•‚] [Ring ğ”¸] [TopologicalSpace ğ”¸] [IsTopologicalRing ğ”¸]
  [Algebra ğ•‚ ğ”¸] [T2Space ğ”¸]

theorem exp_diagonal (v : m â†’ ğ”¸) : exp ğ•‚ (diagonal v) = diagonal (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, diagonal_pow, â† diagonal_smul, â† diagonal_tsum]

theorem exp_blockDiagonal (v : m â†’ Matrix n n ğ”¸) :
    exp ğ•‚ (blockDiagonal v) = blockDiagonal (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, â† blockDiagonal_pow, â† blockDiagonal_smul, â† blockDiagonal_tsum]

theorem exp_blockDiagonal' (v : âˆ€ i, Matrix (n' i) (n' i) ğ”¸) :
    exp ğ•‚ (blockDiagonal' v) = blockDiagonal' (exp ğ•‚ v) := by
  simp_rw [exp_eq_tsum, â† blockDiagonal'_pow, â† blockDiagonal'_smul, â† blockDiagonal'_tsum]

theorem exp_conjTranspose [StarRing ğ”¸] [ContinuousStar ğ”¸] (A : Matrix m m ğ”¸) :
    exp ğ•‚ Aá´´ = (exp ğ•‚ A)á´´ :=
  (star_exp A).symm

theorem IsHermitian.exp [StarRing ğ”¸] [ContinuousStar ğ”¸] {A : Matrix m m ğ”¸} (h : A.IsHermitian) :
    (exp ğ•‚ A).IsHermitian :=
  (exp_conjTranspose _ _).symm.trans <| congr_arg _ h

end Ring

section CommRing

variable [Fintype m] [DecidableEq m] [Field ğ•‚] [CommRing ğ”¸] [TopologicalSpace ğ”¸]
  [IsTopologicalRing ğ”¸] [Algebra ğ•‚ ğ”¸] [T2Space ğ”¸]

theorem exp_transpose (A : Matrix m m ğ”¸) : exp ğ•‚ Aáµ€ = (exp ğ•‚ A)áµ€ := by
  simp_rw [exp_eq_tsum, transpose_tsum, transpose_smul, transpose_pow]

theorem IsSymm.exp {A : Matrix m m ğ”¸} (h : A.IsSymm) : (exp ğ•‚ A).IsSymm :=
  (exp_transpose _ _).symm.trans <| congr_arg _ h

end CommRing

end Topological

section Normed

variable [RCLike ğ•‚] [Fintype m] [DecidableEq m]
  [NormedRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

nonrec theorem exp_add_of_commute (A B : Matrix m m ğ”¸) (h : Commute A B) :
    exp ğ•‚ (A + B) = exp ğ•‚ A * exp ğ•‚ B :=
  open scoped Norms.Operator in exp_add_of_commute h

open scoped Function in -- required for scoped `on` notation
nonrec theorem exp_sum_of_commute {Î¹} (s : Finset Î¹) (f : Î¹ â†’ Matrix m m ğ”¸)
    (h : (s : Set Î¹).Pairwise (Commute on f)) :
    exp ğ•‚ (âˆ‘ i âˆˆ s, f i) =
      s.noncommProd (fun i => exp ğ•‚ (f i)) fun _ hi _ hj _ => (h.of_refl hi hj).exp ğ•‚ :=
  open scoped Norms.Operator in exp_sum_of_commute s f h

nonrec theorem exp_nsmul (n : â„•) (A : Matrix m m ğ”¸) : exp ğ•‚ (n â€¢ A) = exp ğ•‚ A ^ n :=
  open scoped Norms.Operator in exp_nsmul n A

nonrec theorem isUnit_exp (A : Matrix m m ğ”¸) : IsUnit (exp ğ•‚ A) :=
  open scoped Norms.Operator in isUnit_exp _ A

nonrec theorem exp_units_conj (U : (Matrix m m ğ”¸)Ë£) (A : Matrix m m ğ”¸) :
    exp ğ•‚ (U * A * Uâ»Â¹) = U * exp ğ•‚ A * Uâ»Â¹ :=
  open scoped Norms.Operator in exp_units_conj _ U A

theorem exp_units_conj' (U : (Matrix m m ğ”¸)Ë£) (A : Matrix m m ğ”¸) :
    exp ğ•‚ (Uâ»Â¹ * A * U) = Uâ»Â¹ * exp ğ•‚ A * U :=
  exp_units_conj ğ•‚ Uâ»Â¹ A

end Normed

section NormedComm

variable [RCLike ğ•‚] [Fintype m] [DecidableEq m]
  [NormedCommRing ğ”¸] [NormedAlgebra ğ•‚ ğ”¸] [CompleteSpace ğ”¸]

theorem exp_neg (A : Matrix m m ğ”¸) : exp ğ•‚ (-A) = (exp ğ•‚ A)â»Â¹ := by
  rw [nonsing_inv_eq_ringInverse]
  open scoped Norms.Operator in exact (Ring.inverse_exp _ A).symm

theorem exp_zsmul (z : â„¤) (A : Matrix m m ğ”¸) : exp ğ•‚ (z â€¢ A) = exp ğ•‚ A ^ z := by
  obtain âŸ¨n, rfl | rflâŸ© := z.eq_nat_or_neg
  Â· rw [zpow_natCast, natCast_zsmul, exp_nsmul]
  Â· have : IsUnit (exp ğ•‚ A).det := (Matrix.isUnit_iff_isUnit_det _).mp (isUnit_exp _ _)
    rw [Matrix.zpow_neg this, zpow_natCast, neg_smul, exp_neg, natCast_zsmul, exp_nsmul]

theorem exp_conj (U : Matrix m m ğ”¸) (A : Matrix m m ğ”¸) (hy : IsUnit U) :
    exp ğ•‚ (U * A * Uâ»Â¹) = U * exp ğ•‚ A * Uâ»Â¹ :=
  let âŸ¨u, huâŸ© := hy
  hu â–¸ by simpa only [Matrix.coe_units_inv] using exp_units_conj ğ•‚ u A

theorem exp_conj' (U : Matrix m m ğ”¸) (A : Matrix m m ğ”¸) (hy : IsUnit U) :
    exp ğ•‚ (Uâ»Â¹ * A * U) = Uâ»Â¹ * exp ğ•‚ A * U :=
  let âŸ¨u, huâŸ© := hy
  hu â–¸ by simpa only [Matrix.coe_units_inv] using exp_units_conj' ğ•‚ u A

end NormedComm

end Matrix
