/-
Copyright (c) 2021 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Algebra.Algebra.Spectrum.Quasispectrum
import Mathlib.FieldTheory.IsAlgClosed.Spectrum
import Mathlib.Analysis.Complex.Liouville
import Mathlib.Analysis.Complex.Polynomial.Basic
import Mathlib.Analysis.Analytic.RadiusLiminf
import Mathlib.Topology.Algebra.Module.CharacterSpace
import Mathlib.Analysis.Normed.Algebra.Exponential
import Mathlib.Analysis.Normed.Algebra.UnitizationL1
import Mathlib.Tactic.ContinuousFunctionalCalculus

/-!
# The spectrum of elements in a complete normed algebra

This file contains the basic theory for the resolvent and spectrum of a Banach algebra.

## Main definitions

* `spectralRadius : â„â‰¥0âˆ`: supremum of `â€–kâ€–â‚Š` for all `k âˆˆ spectrum ğ•œ a`
* `NormedRing.algEquivComplexOfComplete`: **Gelfand-Mazur theorem** For a complex
  Banach division algebra, the natural `algebraMap â„‚ A` is an algebra isomorphism whose inverse
  is given by selecting the (unique) element of `spectrum â„‚ a`

## Main statements

* `spectrum.isOpen_resolventSet`: the resolvent set is open.
* `spectrum.isClosed`: the spectrum is closed.
* `spectrum.subset_closedBall_norm`: the spectrum is a subset of closed disk of radius
  equal to the norm.
* `spectrum.isCompact`: the spectrum is compact.
* `spectrum.spectralRadius_le_nnnorm`: the spectral radius is bounded above by the norm.
* `spectrum.hasDerivAt_resolvent`: the resolvent function is differentiable on the resolvent set.
* `spectrum.pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius`: Gelfand's formula for the
  spectral radius in Banach algebras over `â„‚`.
* `spectrum.nonempty`: the spectrum of any element in a complex Banach algebra is nonempty.


## TODO

* compute all derivatives of `resolvent a`.

-/

open NormedSpace Topology -- For `NormedSpace.exp`.
open scoped ENNReal NNReal

/-- The *spectral radius* is the supremum of the `nnnorm` (`â€–Â·â€–â‚Š`) of elements in the spectrum,
    coerced into an element of `â„â‰¥0âˆ`. Note that it is possible for `spectrum ğ•œ a = âˆ…`. In this
    case, `spectralRadius a = 0`. It is also possible that `spectrum ğ•œ a` be unbounded (though
    not for Banach algebras, see `spectrum.isBounded`, below).  In this case,
    `spectralRadius a = âˆ`. -/
noncomputable def spectralRadius (ğ•œ : Type*) {A : Type*} [NormedField ğ•œ] [Ring A] [Algebra ğ•œ A]
    (a : A) : â„â‰¥0âˆ :=
  â¨† k âˆˆ spectrum ğ•œ a, â€–kâ€–â‚Š

variable {ğ•œ : Type*} {A : Type*}

namespace spectrum

section SpectrumCompact

open Filter

variable [NormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]

local notation "Ïƒ" => spectrum ğ•œ
local notation "Ï" => resolventSet ğ•œ
local notation "â†‘â‚" => algebraMap ğ•œ A

@[simp]
theorem SpectralRadius.of_subsingleton [Subsingleton A] (a : A) : spectralRadius ğ•œ a = 0 := by
  simp [spectralRadius]

@[simp]
theorem spectralRadius_zero : spectralRadius ğ•œ (0 : A) = 0 := by
  nontriviality A
  simp [spectralRadius]

theorem mem_resolventSet_of_spectralRadius_lt {a : A} {k : ğ•œ} (h : spectralRadius ğ•œ a < â€–kâ€–â‚Š) :
    k âˆˆ Ï a :=
  Classical.not_not.mp fun hn => h.not_ge <| le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) k hn

variable [CompleteSpace A]

theorem isOpen_resolventSet (a : A) : IsOpen (Ï a) :=
  Units.isOpen.preimage ((continuous_algebraMap ğ•œ A).sub continuous_const)

protected theorem isClosed (a : A) : IsClosed (Ïƒ a) :=
  (isOpen_resolventSet a).isClosed_compl

theorem mem_resolventSet_of_norm_lt_mul {a : A} {k : ğ•œ} (h : â€–aâ€– * â€–(1 : A)â€– < â€–kâ€–) : k âˆˆ Ï a := by
  rw [resolventSet, Set.mem_setOf_eq, Algebra.algebraMap_eq_smul_one]
  nontriviality A
  have hk : k â‰  0 :=
    ne_zero_of_norm_ne_zero ((mul_nonneg (norm_nonneg _) (norm_nonneg _)).trans_lt h).ne'
  letI ku := Units.map â†‘â‚.toMonoidHom (Units.mk0 k hk)
  rw [â† inv_inv â€–(1 : A)â€–,
    mul_inv_lt_iffâ‚€' (inv_pos.2 <| norm_pos_iff.2 (one_ne_zero : (1 : A) â‰  0))] at h
  have hku : â€–-aâ€– < â€–(â†‘kuâ»Â¹ : A)â€–â»Â¹ := by simpa [ku, norm_algebraMap] using h
  simpa [ku, sub_eq_add_neg, Algebra.algebraMap_eq_smul_one] using (ku.add (-a) hku).isUnit

theorem mem_resolventSet_of_norm_lt [NormOneClass A] {a : A} {k : ğ•œ} (h : â€–aâ€– < â€–kâ€–) : k âˆˆ Ï a :=
  mem_resolventSet_of_norm_lt_mul (by rwa [norm_one, mul_one])

theorem norm_le_norm_mul_of_mem {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) : â€–kâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
  le_of_not_gt <| mt mem_resolventSet_of_norm_lt_mul hk

theorem norm_le_norm_of_mem [NormOneClass A] {a : A} {k : ğ•œ} (hk : k âˆˆ Ïƒ a) : â€–kâ€– â‰¤ â€–aâ€– :=
  le_of_not_gt <| mt mem_resolventSet_of_norm_lt hk

theorem subset_closedBall_norm_mul (a : A) : Ïƒ a âŠ† Metric.closedBall (0 : ğ•œ) (â€–aâ€– * â€–(1 : A)â€–) :=
  fun k hk => by simp [norm_le_norm_mul_of_mem hk]

theorem subset_closedBall_norm [NormOneClass A] (a : A) : Ïƒ a âŠ† Metric.closedBall (0 : ğ•œ) â€–aâ€– :=
  fun k hk => by simp [norm_le_norm_of_mem hk]

theorem isBounded (a : A) : Bornology.IsBounded (Ïƒ a) :=
  Metric.isBounded_closedBall.subset (subset_closedBall_norm_mul a)

protected theorem isCompact [ProperSpace ğ•œ] (a : A) : IsCompact (Ïƒ a) :=
  Metric.isCompact_of_isClosed_isBounded (spectrum.isClosed a) (isBounded a)

instance instCompactSpace [ProperSpace ğ•œ] (a : A) : CompactSpace (spectrum ğ•œ a) :=
  isCompact_iff_compactSpace.mp <| spectrum.isCompact a

instance instCompactSpaceNNReal {A : Type*} [NormedRing A] [NormedAlgebra â„ A]
    (a : A) [CompactSpace (spectrum â„ a)] : CompactSpace (spectrum â„â‰¥0 a) := by
  rw [â† isCompact_iff_compactSpace] at *
  rw [â† preimage_algebraMap â„]
  exact isClosed_nonneg.isClosedEmbedding_subtypeVal.isCompact_preimage <| by assumption

section QuasispectrumCompact

variable {B : Type*} [NonUnitalNormedRing B] [NormedSpace ğ•œ B] [CompleteSpace B]
variable [IsScalarTower ğ•œ B B] [SMulCommClass ğ•œ B B] [ProperSpace ğ•œ]

theorem _root_.quasispectrum.isCompact (a : B) : IsCompact (quasispectrum ğ•œ a) := by
  rw [Unitization.quasispectrum_eq_spectrum_inr' ğ•œ ğ•œ,
    â† AlgEquiv.spectrum_eq (WithLp.unitizationAlgEquiv ğ•œ).symm (a : Unitization ğ•œ B)]
  exact spectrum.isCompact _

instance _root_.quasispectrum.instCompactSpace (a : B) :
    CompactSpace (quasispectrum ğ•œ a) :=
  isCompact_iff_compactSpace.mp <| quasispectrum.isCompact a

instance _root_.quasispectrum.instCompactSpaceNNReal [NormedSpace â„ B] [IsScalarTower â„ B B]
    [SMulCommClass â„ B B] (a : B) [CompactSpace (quasispectrum â„ a)] :
    CompactSpace (quasispectrum â„â‰¥0 a) := by
  rw [â† isCompact_iff_compactSpace] at *
  rw [â† quasispectrum.preimage_algebraMap â„]
  exact isClosed_nonneg.isClosedEmbedding_subtypeVal.isCompact_preimage <| by assumption

end QuasispectrumCompact

section NNReal

open NNReal

variable {A : Type*} [NormedRing A] [NormedAlgebra â„ A] [CompleteSpace A] [NormOneClass A]

theorem le_nnnorm_of_mem {a : A} {r : â„â‰¥0} (hr : r âˆˆ spectrum â„â‰¥0 a) :
    r â‰¤ â€–aâ€–â‚Š := calc
  r â‰¤ â€–(r : â„)â€– := Real.le_norm_self _
  _ â‰¤ â€–aâ€–       := norm_le_norm_of_mem hr

theorem coe_le_norm_of_mem {a : A} {r : â„â‰¥0} (hr : r âˆˆ spectrum â„â‰¥0 a) :
    r â‰¤ â€–aâ€– :=
  coe_mono <| le_nnnorm_of_mem hr

end NNReal

theorem spectralRadius_le_nnnorm [NormOneClass A] (a : A) : spectralRadius ğ•œ a â‰¤ â€–aâ€–â‚Š := by
  refine iSupâ‚‚_le fun k hk => ?_
  exact mod_cast norm_le_norm_of_mem hk

theorem exists_nnnorm_eq_spectralRadius_of_nonempty [ProperSpace ğ•œ] {a : A} (ha : (Ïƒ a).Nonempty) :
    âˆƒ k âˆˆ Ïƒ a, (â€–kâ€–â‚Š : â„â‰¥0âˆ) = spectralRadius ğ•œ a := by
  obtain âŸ¨k, hk, hâŸ© := (spectrum.isCompact a).exists_isMaxOn ha continuous_nnnorm.continuousOn
  exact âŸ¨k, hk, le_antisymm (le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) k hk) (iSupâ‚‚_le <| mod_cast h)âŸ©

theorem spectralRadius_lt_of_forall_lt_of_nonempty [ProperSpace ğ•œ] {a : A} (ha : (Ïƒ a).Nonempty)
    {r : â„â‰¥0} (hr : âˆ€ k âˆˆ Ïƒ a, â€–kâ€–â‚Š < r) : spectralRadius ğ•œ a < r :=
  sSup_image.symm.trans_lt <| ((spectrum.isCompact a).sSup_lt_iff_of_continuous ha
    continuous_enorm.continuousOn (r : â„â‰¥0âˆ)).mpr (by simpa using hr)

open ENNReal Polynomial

variable (ğ•œ)

theorem spectralRadius_le_pow_nnnorm_pow_one_div (a : A) (n : â„•) :
    spectralRadius ğ•œ a â‰¤ (â€–a ^ (n + 1)â€–â‚Š : â„â‰¥0âˆ) ^ (1 / (n + 1) : â„) *
      (â€–(1 : A)â€–â‚Š : â„â‰¥0âˆ) ^ (1 / (n + 1) : â„) := by
  refine iSupâ‚‚_le fun k hk => ?_
  -- apply easy direction of the spectral mapping theorem for polynomials
  have pow_mem : k ^ (n + 1) âˆˆ Ïƒ (a ^ (n + 1)) := by
    simpa only [one_mul, Algebra.algebraMap_eq_smul_one, one_smul, aeval_monomial, one_mul,
      eval_monomial] using subset_polynomial_aeval a (@monomial ğ•œ _ (n + 1) (1 : ğ•œ)) âŸ¨k, hk, rflâŸ©
  -- power of the norm is bounded by norm of the power
  have nnnorm_pow_le : (â†‘(â€–kâ€–â‚Š ^ (n + 1)) : â„â‰¥0âˆ) â‰¤ â€–a ^ (n + 1)â€–â‚Š * â€–(1 : A)â€–â‚Š := by
    simpa only [Real.toNNReal_mul (norm_nonneg _), norm_toNNReal, nnnorm_pow k (n + 1),
      ENNReal.coe_mul] using coe_mono (Real.toNNReal_mono (norm_le_norm_mul_of_mem pow_mem))
  -- take (n + 1)áµ—Ê° roots and clean up the left-hand side
  have hn : 0 < ((n + 1 : â„•) : â„) := mod_cast Nat.succ_pos'
  convert monotone_rpow_of_nonneg (one_div_pos.mpr hn).le nnnorm_pow_le using 1
  all_goals dsimp
  Â· rw [one_div, pow_rpow_inv_natCast]
    positivity
  rw [Nat.cast_succ, ENNReal.coe_mul_rpow]

theorem spectralRadius_le_liminf_pow_nnnorm_pow_one_div (a : A) :
    spectralRadius ğ•œ a â‰¤ atTop.liminf fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„) := by
  refine ENNReal.le_of_forall_lt_one_mul_le fun Îµ hÎµ => ?_
  by_cases h : Îµ = 0
  Â· simp only [h, zero_mul, zero_le']
  simp only [ENNReal.mul_le_iff_le_inv h (hÎµ.trans_le le_top).ne, mul_comm Îµâ»Â¹,
    liminf_eq_iSup_iInf_of_nat', ENNReal.iSup_mul]
  conv_rhs => arg 1; intro i; rw [ENNReal.iInf_mul (by simp [h])]
  rw [â† ENNReal.inv_lt_inv, inv_one] at hÎµ
  obtain âŸ¨N, hNâŸ© := eventually_atTop.mp
    (ENNReal.eventually_pow_one_div_le (ENNReal.coe_ne_top : â†‘â€–(1 : A)â€–â‚Š â‰  âˆ) hÎµ)
  refine le_trans ?_ (le_iSup _ (N + 1))
  refine le_iInf fun n => ?_
  simp only [â† add_assoc]
  refine (spectralRadius_le_pow_nnnorm_pow_one_div ğ•œ a (n + N)).trans ?_
  norm_cast
  exact mul_le_mul_left' (hN (n + N + 1) (by omega)) _

end SpectrumCompact

section resolvent

open Filter Asymptotics Bornology Topology

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "Ï" => resolventSet ğ•œ
local notation "â†‘â‚" => algebraMap ğ•œ A

theorem hasDerivAt_resolvent {a : A} {k : ğ•œ} (hk : k âˆˆ Ï a) :
    HasDerivAt (resolvent a) (-resolvent a k ^ 2) k := by
  have Hâ‚ : HasFDerivAt Ring.inverse _ (â†‘â‚ k - a) := hasFDerivAt_ringInverse (ğ•œ := ğ•œ) hk.unit
  have Hâ‚‚ : HasDerivAt (fun k => â†‘â‚ k - a) 1 k := by
    simpa using (Algebra.linearMap ğ•œ A).hasDerivAt.sub_const a
  simpa [resolvent, sq, hk.unit_spec, â† Ring.inverse_unit hk.unit] using Hâ‚.comp_hasDerivAt k Hâ‚‚

-- refactored so this result was no longer necessary or useful

theorem eventually_isUnit_resolvent (a : A) : âˆ€á¶  z in cobounded ğ•œ, IsUnit (resolvent a z) := by
  rw [atTop_basis_Ioi.cobounded_of_norm.eventually_iff]
  exact âŸ¨â€–aâ€– * â€–(1 : A)â€–, trivial, fun _ â†¦ isUnit_resolvent.mp âˆ˜ mem_resolventSet_of_norm_lt_mulâŸ©

theorem resolvent_isBigO_inv (a : A) : resolvent a =O[cobounded ğ•œ] Inv.inv :=
  have h : (fun z â†¦ resolvent (zâ»Â¹ â€¢ a) (1 : ğ•œ)) =O[cobounded ğ•œ] (fun _ â†¦ (1 : â„)) := by
    simpa [Function.comp_def, resolvent] using
      (NormedRing.inverse_one_sub_norm (R := A)).comp_tendsto
        (by simpa using (tendsto_invâ‚€_cobounded (Î± := ğ•œ)).smul_const a)
  calc
    resolvent a =á¶ [cobounded ğ•œ] fun z â†¦ zâ»Â¹ â€¢ resolvent (zâ»Â¹ â€¢ a) (1 : ğ•œ) := by
      filter_upwards [isBounded_singleton (x := 0)] with z hz
      lift z to ğ•œË£ using Ne.isUnit hz
      simpa [Units.smul_def] using congr(zâ»Â¹ â€¢ $(units_smul_resolvent_self (r := z) (a := a)))
    _ =O[cobounded ğ•œ] (Â· â»Â¹) := .of_norm_right <| by
      simpa using (isBigO_refl (Â· â»Â¹) (cobounded ğ•œ)).norm_right.smul h

theorem resolvent_tendsto_cobounded (a : A) : Tendsto (resolvent a) (cobounded ğ•œ) (ğ“ 0) :=
  resolvent_isBigO_inv a |>.trans_tendsto tendsto_invâ‚€_cobounded

end resolvent

section OneSubSMul

open ContinuousMultilinearMap ENNReal FormalMultilinearSeries

open scoped NNReal ENNReal

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A]

variable (ğ•œ) in
/-- In a Banach algebra `A` over a nontrivially normed field `ğ•œ`, for any `a : A` the
power series with coefficients `a ^ n` represents the function `(1 - z â€¢ a)â»Â¹` in a disk of
radius `â€–aâ€–â‚Šâ»Â¹`. -/
theorem hasFPowerSeriesOnBall_inverse_one_sub_smul [HasSummableGeomSeries A] (a : A) :
    HasFPowerSeriesOnBall (fun z : ğ•œ => Ring.inverse (1 - z â€¢ a))
      (fun n => ContinuousMultilinearMap.mkPiRing ğ•œ (Fin n) (a ^ n)) 0 â€–aâ€–â‚Šâ»Â¹ :=
  { r_le := by
      refine le_of_forall_nnreal_lt fun r hr =>
        le_radius_of_bound_nnreal _ (max 1 â€–(1 : A)â€–â‚Š) fun n => ?_
      rw [â† norm_toNNReal, norm_mkPiRing, norm_toNNReal]
      rcases n with - | n
      Â· simp only [le_refl, mul_one, or_true, le_max_iff, pow_zero]
      Â· refine
          le_trans (le_trans (mul_le_mul_right' (nnnorm_pow_le' a n.succ_pos) (r ^ n.succ)) ?_)
            (le_max_left _ _)
        by_cases h : â€–aâ€–â‚Š = 0
        Â· simp only [h, zero_mul, zero_le', pow_succ']
        Â· rw [â† coe_inv h, coe_lt_coe, NNReal.lt_inv_iff_mul_lt h] at hr
          simpa only [â† mul_pow, mul_comm] using pow_le_one' hr.le n.succ
    r_pos := ENNReal.inv_pos.mpr coe_ne_top
    hasSum := fun {y} hy => by
      have norm_lt : â€–y â€¢ aâ€– < 1 := by
        by_cases h : â€–aâ€–â‚Š = 0
        Â· simp only [nnnorm_eq_zero.mp h, norm_zero, zero_lt_one, smul_zero]
        Â· have nnnorm_lt : â€–yâ€–â‚Š < â€–aâ€–â‚Šâ»Â¹ := by
            simpa only [â† coe_inv h, mem_ball_zero_iff, Metric.emetric_ball_nnreal] using hy
          rwa [â† coe_nnnorm, â† Real.lt_toNNReal_iff_coe_lt, Real.toNNReal_one, nnnorm_smul,
            â† NNReal.lt_inv_iff_mul_lt h]
      simpa [â† smul_pow, (summable_geometric_of_norm_lt_one norm_lt).hasSum_iff] using
        (NormedRing.inverse_one_sub _ norm_lt).symm }

theorem isUnit_one_sub_smul_of_lt_inv_radius {a : A} {z : ğ•œ} (h : â†‘â€–zâ€–â‚Š < (spectralRadius ğ•œ a)â»Â¹) :
    IsUnit (1 - z â€¢ a) := by
  by_cases hz : z = 0
  Â· simp only [hz, isUnit_one, sub_zero, zero_smul]
  Â· let u := Units.mk0 z hz
    suffices hu : IsUnit (uâ»Â¹ â€¢ (1 : A) - a) by
      rwa [IsUnit.smul_sub_iff_sub_inv_smul, inv_inv u] at hu
    rw [Units.smul_def, â† Algebra.algebraMap_eq_smul_one, â† mem_resolventSet_iff]
    refine mem_resolventSet_of_spectralRadius_lt ?_
    rwa [Units.val_inv_eq_inv_val, nnnorm_inv,
      coe_inv (nnnorm_ne_zero_iff.mpr (Units.val_mk0 hz â–¸ hz : (u : ğ•œ) â‰  0)), lt_inv_iff_lt_inv]

/-- In a Banach algebra `A` over `ğ•œ`, for `a : A` the function `fun z â†¦ (1 - z â€¢ a)â»Â¹` is
differentiable on any closed ball centered at zero of radius `r < (spectralRadius ğ•œ a)â»Â¹`. -/
theorem differentiableOn_inverse_one_sub_smul [CompleteSpace A] {a : A} {r : â„â‰¥0}
    (hr : (r : â„â‰¥0âˆ) < (spectralRadius ğ•œ a)â»Â¹) :
    DifferentiableOn ğ•œ (fun z : ğ•œ => Ring.inverse (1 - z â€¢ a)) (Metric.closedBall 0 r) := by
  intro z z_mem
  apply DifferentiableAt.differentiableWithinAt
  have hu : IsUnit (1 - z â€¢ a) := by
    refine isUnit_one_sub_smul_of_lt_inv_radius (lt_of_le_of_lt (coe_mono ?_) hr)
    simpa only [norm_toNNReal, Real.toNNReal_coe] using
      Real.toNNReal_mono (mem_closedBall_zero_iff.mp z_mem)
  have Hâ‚ : Differentiable ğ•œ fun w : ğ•œ => 1 - w â€¢ a := (differentiable_id.smul_const a).const_sub 1
  exact DifferentiableAt.comp z (differentiableAt_inverse hu) Hâ‚.differentiableAt

end OneSubSMul

section GelfandFormula

open Filter ENNReal ContinuousMultilinearMap

open scoped Topology

variable [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A]

/-- The `limsup` relationship for the spectral radius used to prove `spectrum.gelfand_formula`. -/
theorem limsup_pow_nnnorm_pow_one_div_le_spectralRadius (a : A) :
    limsup (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop â‰¤ spectralRadius â„‚ a := by
  refine ENNReal.inv_le_inv.mp (le_of_forall_pos_nnreal_lt fun r r_pos r_lt => ?_)
  simp_rw [inv_limsup, â† one_div]
  let p : FormalMultilinearSeries â„‚ â„‚ A := fun n =>
    ContinuousMultilinearMap.mkPiRing â„‚ (Fin n) (a ^ n)
  suffices h : (r : â„â‰¥0âˆ) â‰¤ p.radius by
    convert h
    simp only [p, p.radius_eq_liminf, â† norm_toNNReal, norm_mkPiRing]
    congr
    ext n
    rw [norm_toNNReal, ENNReal.coe_rpow_def â€–a ^ nâ€–â‚Š (1 / n : â„), if_neg]
    exact fun ha => (lt_self_iff_false _).mp
      (ha.2.trans_le (one_div_nonneg.mpr n.cast_nonneg : 0 â‰¤ (1 / n : â„)))
  have Hâ‚ := (differentiableOn_inverse_one_sub_smul r_lt).hasFPowerSeriesOnBall r_pos
  exact ((hasFPowerSeriesOnBall_inverse_one_sub_smul â„‚ a).exchange_radius Hâ‚).r_le

/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => (â€–a ^ nâ€–â‚Š : â„â‰¥0âˆ) ^ (1 / n : â„)) atTop (ğ“ (spectralRadius â„‚ a)) :=
  tendsto_of_le_liminf_of_limsup_le (spectralRadius_le_liminf_pow_nnnorm_pow_one_div â„‚ a)
    (limsup_pow_nnnorm_pow_one_div_le_spectralRadius a)

/- This is the same as `pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius` but for `norm`
instead of `nnnorm`. -/
/-- **Gelfand's formula**: Given an element `a : A` of a complex Banach algebra, the
`spectralRadius` of `a` is the limit of the sequence `â€–a ^ nâ€–â‚Š ^ (1 / n)`. -/
theorem pow_norm_pow_one_div_tendsto_nhds_spectralRadius (a : A) :
    Tendsto (fun n : â„• => ENNReal.ofReal (â€–a ^ nâ€– ^ (1 / n : â„))) atTop
      (ğ“ (spectralRadius â„‚ a)) := by
  convert pow_nnnorm_pow_one_div_tendsto_nhds_spectralRadius a using 1
  ext1
  rw [â† ofReal_rpow_of_nonneg (norm_nonneg _) _, â† coe_nnnorm, coe_nnreal_eq]
  exact one_div_nonneg.mpr (mod_cast zero_le _)

end GelfandFormula

section NonemptySpectrum

variable [NormedRing A] [NormedAlgebra â„‚ A] [CompleteSpace A] [Nontrivial A] (a : A)

/-- In a (nontrivial) complex Banach algebra, every element has nonempty spectrum. -/
protected theorem nonempty : (spectrum â„‚ a).Nonempty := by
  /- Suppose `Ïƒ a = âˆ…`, then resolvent set is `â„‚`, any `(z â€¢ 1 - a)` is a unit, and `resolvent a`
    is differentiable on `â„‚`. -/
  by_contra! h
  have Hâ‚€ : resolventSet â„‚ a = Set.univ := by rwa [spectrum, Set.compl_empty_iff] at h
  have Hâ‚ : Differentiable â„‚ fun z : â„‚ => resolvent a z := fun z =>
    (hasDerivAt_resolvent (Hâ‚€.symm â–¸ Set.mem_univ z : z âˆˆ resolventSet â„‚ a)).differentiableAt
  /- Since `resolvent a` tends to zero at infinity, by Liouville's theorem `resolvent a = 0`,
  which contradicts that `resolvent a z` is invertible. -/
  have Hâ‚ƒ := Hâ‚.apply_eq_of_tendsto_cocompact 0 <| by
    simpa [Metric.cobounded_eq_cocompact] using resolvent_tendsto_cobounded a (ğ•œ := â„‚)
  exact not_isUnit_zero <| Hâ‚ƒ â–¸ (isUnit_resolvent.mp <| Hâ‚€.symm â–¸ Set.mem_univ 0)

/-- In a complex Banach algebra, the spectral radius is always attained by some element of the
spectrum. -/
theorem exists_nnnorm_eq_spectralRadius : âˆƒ z âˆˆ spectrum â„‚ a, (â€–zâ€–â‚Š : â„â‰¥0âˆ) = spectralRadius â„‚ a :=
  exists_nnnorm_eq_spectralRadius_of_nonempty (spectrum.nonempty a)

/-- In a complex Banach algebra, if every element of the spectrum has norm strictly less than
`r : â„â‰¥0`, then the spectral radius is also strictly less than `r`. -/
theorem spectralRadius_lt_of_forall_lt {r : â„â‰¥0} (hr : âˆ€ z âˆˆ spectrum â„‚ a, â€–zâ€–â‚Š < r) :
    spectralRadius â„‚ a < r :=
  spectralRadius_lt_of_forall_lt_of_nonempty (spectrum.nonempty a) hr

open scoped Polynomial

open Polynomial

/-- The **spectral mapping theorem** for polynomials in a Banach algebra over `â„‚`. -/
theorem map_polynomial_aeval (p : â„‚[X]) :
    spectrum â„‚ (aeval a p) = (fun k => eval k p) '' spectrum â„‚ a :=
  map_polynomial_aeval_of_nonempty a p (spectrum.nonempty a)

/-- A specialization of the spectral mapping theorem for polynomials in a Banach algebra over `â„‚`
to monic monomials. -/
protected theorem map_pow (n : â„•) :
    spectrum â„‚ (a ^ n) = (Â· ^ n) '' spectrum â„‚ a := by
  simpa only [aeval_X_pow, eval_pow, eval_X] using map_polynomial_aeval a (X ^ n)

end NonemptySpectrum

section GelfandMazurIsomorphism

variable [NormedRing A] [NormedAlgebra â„‚ A] (hA : âˆ€ {a : A}, IsUnit a â†” a â‰  0)
include hA

local notation "Ïƒ" => spectrum â„‚

theorem algebraMap_eq_of_mem {a : A} {z : â„‚} (h : z âˆˆ Ïƒ a) : algebraMap â„‚ A z = a := by
  rwa [mem_iff, hA, Classical.not_not, sub_eq_zero] at h

/-- **Gelfand-Mazur theorem**: For a complex Banach division algebra, the natural `algebraMap â„‚ A`
is an algebra isomorphism whose inverse is given by selecting the (unique) element of
`spectrum â„‚ a`. In addition, `algebraMap_isometry` guarantees this map is an isometry.

Note: because `NormedDivisionRing` requires the field `norm_mul : âˆ€ a b, â€–a * bâ€– = â€–aâ€– * â€–bâ€–`, we
don't use this type class and instead opt for a `NormedRing` in which the nonzero elements are
precisely the units. This allows for the application of this isomorphism in broader contexts, e.g.,
to the quotient of a complex Banach algebra by a maximal ideal. In the case when `A` is actually a
`NormedDivisionRing`, one may fill in the argument `hA` with the lemma `isUnit_iff_ne_zero`. -/
@[simps]
noncomputable def _root_.NormedRing.algEquivComplexOfComplete [CompleteSpace A] : â„‚ â‰ƒâ‚[â„‚] A :=
  let nt : Nontrivial A := âŸ¨âŸ¨1, 0, hA.mp âŸ¨âŸ¨1, 1, mul_one _, mul_one _âŸ©, rflâŸ©âŸ©âŸ©
  { Algebra.ofId â„‚ A with
    toFun := algebraMap â„‚ A
    invFun := fun a => (@spectrum.nonempty _ _ _ _ nt a).some
    left_inv := fun z => by
      simpa only [@scalar_eq _ _ _ _ _ nt _] using
        (@spectrum.nonempty _ _ _ _ nt <| algebraMap â„‚ A z).some_mem
    right_inv := fun a => algebraMap_eq_of_mem (@hA) (@spectrum.nonempty _ _ _ _ nt a).some_mem }

end GelfandMazurIsomorphism

section ExpMapping

local notation "â†‘â‚" => algebraMap ğ•œ A

/-- For `ğ•œ = â„` or `ğ•œ = â„‚`, `exp ğ•œ` maps the spectrum of `a` into the spectrum of `exp ğ•œ a`. -/
theorem exp_mem_exp [RCLike ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A] (a : A)
    {z : ğ•œ} (hz : z âˆˆ spectrum ğ•œ a) : exp ğ•œ z âˆˆ spectrum ğ•œ (exp ğ•œ a) := by
  have hexpmul : exp ğ•œ a = exp ğ•œ (a - â†‘â‚ z) * â†‘â‚ (exp ğ•œ z) := by
    rw [algebraMap_exp_comm z, â† exp_add_of_commute (Algebra.commutes z (a - â†‘â‚ z)).symm,
      sub_add_cancel]
  let b := âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ n
  have hb : Summable fun n : â„• => ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ n := by
    refine .of_norm_bounded_eventually (Real.summable_pow_div_factorial â€–a - â†‘â‚ zâ€–) ?_
    filter_upwards [Filter.eventually_cofinite_ne 0] with n hn
    rw [norm_smul, mul_comm, norm_inv, RCLike.norm_natCast, â† div_eq_mul_inv]
    gcongr
    Â· exact norm_pow_le' _ (pos_iff_ne_zero.mpr hn)
    Â· exact n.le_succ
  have hâ‚€ : (âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ (n + 1)) = (a - â†‘â‚ z) * b := by
    simpa only [mul_smul_comm, pow_succ'] using hb.tsum_mul_left (a - â†‘â‚ z)
  have hâ‚ : (âˆ‘' n : â„•, ((n + 1).factorialâ»Â¹ : ğ•œ) â€¢ (a - â†‘â‚ z) ^ (n + 1)) = b * (a - â†‘â‚ z) := by
    simpa only [pow_succ, Algebra.smul_mul_assoc] using hb.tsum_mul_right (a - â†‘â‚ z)
  have hâ‚ƒ : exp ğ•œ (a - â†‘â‚ z) = 1 + (a - â†‘â‚ z) * b := by
    rw [exp_eq_tsum]
    convert (expSeries_summable' (ğ•‚ := ğ•œ) (a - â†‘â‚ z)).tsum_eq_zero_add
    Â· simp only [Nat.factorial_zero, Nat.cast_one, inv_one, pow_zero, one_smul]
    Â· exact hâ‚€.symm
  rw [spectrum.mem_iff, IsUnit.sub_iff, â† one_mul (â†‘â‚ (exp ğ•œ z)), hexpmul, â† _root_.sub_mul,
    Commute.isUnit_mul_iff (Algebra.commutes (exp ğ•œ z) (exp ğ•œ (a - â†‘â‚ z) - 1)).symm,
    sub_eq_iff_eq_add'.mpr hâ‚ƒ, Commute.isUnit_mul_iff (hâ‚€ â–¸ hâ‚ : (a - â†‘â‚ z) * b = b * (a - â†‘â‚ z))]
  exact not_and_of_not_left _ (not_and_of_not_left _ ((not_iff_not.mpr IsUnit.sub_iff).mp hz))

end ExpMapping

end spectrum

namespace AlgHom

section NormedField

variable {F : Type*} [NormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "â†‘â‚" => algebraMap ğ•œ A

instance (priority := 100) [FunLike F A ğ•œ] [AlgHomClass F ğ•œ A ğ•œ] :
    ContinuousLinearMapClass F ğ•œ A ğ•œ :=
  { AlgHomClass.linearMapClass with
    map_continuous := fun Ï† =>
      AddMonoidHomClass.continuous_of_bound Ï† â€–(1 : A)â€– fun a =>
        mul_comm â€–aâ€– â€–(1 : A)â€– â–¸ spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum Ï† _) }

/-- An algebra homomorphism into the base field, as a continuous linear map (since it is
automatically bounded). -/
def toContinuousLinearMap (Ï† : A â†’â‚[ğ•œ] ğ•œ) : A â†’L[ğ•œ] ğ•œ :=
  { Ï†.toLinearMap with cont := map_continuous Ï† }

@[simp]
theorem coe_toContinuousLinearMap (Ï† : A â†’â‚[ğ•œ] ğ•œ) : â‡‘Ï†.toContinuousLinearMap = Ï† :=
  rfl

theorem norm_apply_le_self_mul_norm_one [FunLike F A ğ•œ] [AlgHomClass F ğ•œ A ğ•œ] (f : F) (a : A) :
    â€–f aâ€– â‰¤ â€–aâ€– * â€–(1 : A)â€– :=
  spectrum.norm_le_norm_mul_of_mem (apply_mem_spectrum f _)

theorem norm_apply_le_self [NormOneClass A] [FunLike F A ğ•œ] [AlgHomClass F ğ•œ A ğ•œ]
    (f : F) (a : A) : â€–f aâ€– â‰¤ â€–aâ€– :=
  spectrum.norm_le_norm_of_mem (apply_mem_spectrum f _)

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [NormedAlgebra ğ•œ A] [CompleteSpace A]

local notation "â†‘â‚" => algebraMap ğ•œ A

@[simp]
theorem toContinuousLinearMap_norm [NormOneClass A] (Ï† : A â†’â‚[ğ•œ] ğ•œ) :
    â€–Ï†.toContinuousLinearMapâ€– = 1 :=
  ContinuousLinearMap.opNorm_eq_of_bounds zero_le_one
    (fun a => (one_mul â€–aâ€–).symm â–¸ spectrum.norm_le_norm_of_mem (apply_mem_spectrum Ï† _))
    fun _ _ h => by simpa only [coe_toContinuousLinearMap, map_one, norm_one, mul_one] using h 1

end NontriviallyNormedField

end AlgHom

namespace WeakDual

namespace CharacterSpace

variable [NontriviallyNormedField ğ•œ] [NormedRing A] [CompleteSpace A]
variable [NormedAlgebra ğ•œ A]

/-- The equivalence between characters and algebra homomorphisms into the base field. -/
def equivAlgHom : characterSpace ğ•œ A â‰ƒ (A â†’â‚[ğ•œ] ğ•œ) where
  toFun := toAlgHom
  invFun f :=
    { val := f.toContinuousLinearMap
      property := by rw [eq_set_map_one_map_mul]; exact âŸ¨map_one f, map_mul fâŸ© }

@[simp]
theorem equivAlgHom_coe (f : characterSpace ğ•œ A) : â‡‘(equivAlgHom f) = f :=
  rfl

@[simp]
theorem equivAlgHom_symm_coe (f : A â†’â‚[ğ•œ] ğ•œ) : â‡‘(equivAlgHom.symm f) = f :=
  rfl

end CharacterSpace

end WeakDual

section BoundarySpectrum

local notation "Ïƒ" => spectrum

variable {ğ•œ A SA : Type*} [NormedRing A] [CompleteSpace A] [SetLike SA A] [SubringClass SA A]

open Topology Filter Set

section NormedField

variable [NormedField ğ•œ] [NormedAlgebra ğ•œ A] [instSMulMem : SMulMemClass SA ğ•œ A]
variable (S : SA) [hS : IsClosed (S : Set A)] (x : S)

open SubalgebraClass in
include instSMulMem in
/-- Let `S` be a closed subalgebra of a Banach algebra `A`. If `a : S` is invertible in `A`,
and for all `x : S` sufficiently close to `a` within some filter `l`, `x` is invertible in `S`,
then `a` is invertible in `S` as well. -/
lemma _root_.Subalgebra.isUnit_of_isUnit_val_of_eventually {l : Filter S} {a : S}
    (ha : IsUnit (a : A)) (hla : l â‰¤ ğ“ a) (hl : âˆ€á¶  x in l, IsUnit x) (hl' : l.NeBot) :
    IsUnit a := by
  have hlaâ‚‚ : Tendsto Ring.inverse (map (val S) l) (ğ“ (â†‘ha.unitâ»Â¹ : A)) := by
    rw [â† Ring.inverse_unit]
    exact (NormedRing.inverse_continuousAt _).tendsto.comp <|
      continuousAt_subtype_val.tendsto.comp <| map_mono hla
  suffices mem : (â†‘ha.unitâ»Â¹ : A) âˆˆ S by
    refine âŸ¨âŸ¨a, âŸ¨(â†‘ha.unitâ»Â¹ : A), memâŸ©, ?_, ?_âŸ©, rflâŸ©
    all_goals ext; simp
  apply hS.mem_of_tendsto hlaâ‚‚
  rw [Filter.eventually_map]
  apply hl.mono fun x hx â†¦ ?_
  suffices Ring.inverse (val S x) = (val S â†‘hx.unitâ»Â¹) from this â–¸ Subtype.property _
  rw [â† (hx.map (val S)).unit_spec, Ring.inverse_unit (hx.map (val S)).unit, val]
  apply Units.mul_eq_one_iff_inv_eq.mp
  simpa [-IsUnit.mul_val_inv] using congr(($hx.mul_val_inv : A))

/-- If `S : Subalgebra ğ•œ A` is a closed subalgebra of a Banach algebra `A`, then for any
`x : S`, the boundary of the spectrum of `x` relative to `S` is a subset of the spectrum of
`â†‘x : A` relative to `A`. -/
lemma _root_.Subalgebra.frontier_spectrum : frontier (Ïƒ ğ•œ x) âŠ† Ïƒ ğ•œ (x : A) := by
  have : CompleteSpace S := hS.completeSpace_coe
  intro Î¼ hÎ¼
  by_contra h
  rw [spectrum.notMem_iff] at h
  rw [â† frontier_compl, (spectrum.isClosed _).isOpen_compl.frontier_eq, mem_diff] at hÎ¼
  obtain âŸ¨hÎ¼â‚, hÎ¼â‚‚âŸ© := hÎ¼
  rw [mem_closure_iff_clusterPt] at hÎ¼â‚
  apply hÎ¼â‚‚
  rw [mem_compl_iff, spectrum.notMem_iff]
  refine Subalgebra.isUnit_of_isUnit_val_of_eventually S h ?_ ?_ <| .map hÎ¼â‚ (algebraMap ğ•œ S Â· - x)
  Â· calc
      _ â‰¤ map _ (ğ“ Î¼) := map_mono (by simp)
      _ â‰¤ _ := by rw [â† Filter.Tendsto, â† ContinuousAt]; fun_prop
  Â· rw [eventually_map]
    apply Eventually.filter_mono inf_le_right
    simp [spectrum.notMem_iff]

/-- If `S` is a closed subalgebra of a Banach algebra `A`, then for any `x : S`, the boundary of
the spectrum of `x` relative to `S` is a subset of the boundary of the spectrum of `â†‘x : A`
relative to `A`. -/
lemma Subalgebra.frontier_subset_frontier :
    frontier (Ïƒ ğ•œ x) âŠ† frontier (Ïƒ ğ•œ (x : A)) := by
  rw [frontier_eq_closure_inter_closure (s := Ïƒ ğ•œ (x : A)),
    (spectrum.isClosed (x : A)).closure_eq]
  apply subset_inter (frontier_spectrum S x)
  rw [frontier_eq_closure_inter_closure]
  exact inter_subset_right |>.trans <|
    closure_mono <| compl_subset_compl.mpr <| spectrum.subset_subalgebra x

open Set Notation

/-- If `S` is a closed subalgebra of a Banach algebra `A`, then for any `x : S`, the spectrum of `x`
is the spectrum of `â†‘x : A` along with the connected components of the complement of the spectrum of
`â†‘x : A` which contain an element of the spectrum of `x : S`. -/
lemma Subalgebra.spectrum_sUnion_connectedComponentIn :
    Ïƒ ğ•œ x = Ïƒ ğ•œ (x : A) âˆª (â‹ƒ z âˆˆ (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)), connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z) := by
  suffices IsClopen ((Ïƒ ğ•œ (x : A))á¶œ â†“âˆ© (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A))) by
    rw [â† this.biUnion_connectedComponentIn (diff_subset_compl _ _),
      union_diff_cancel (spectrum.subset_subalgebra x)]
  have : CompleteSpace S := hS.completeSpace_coe
  have h_open : IsOpen (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)) := by
    rw [â† (spectrum.isClosed (ğ•œ := ğ•œ) x).closure_eq, closure_eq_interior_union_frontier,
      union_diff_distrib, diff_eq_empty.mpr (frontier_spectrum S x),
      diff_eq_compl_inter, union_empty]
    exact (spectrum.isClosed _).isOpen_compl.inter isOpen_interior
  apply isClopen_preimage_val h_open
  suffices h_frontier : frontier (Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A)) âŠ† frontier (Ïƒ ğ•œ (x : A)) from
    disjoint_of_subset_left h_frontier <| disjoint_compl_right.frontier_left
      (spectrum.isClosed _).isOpen_compl
  rw [diff_eq_compl_inter]
  apply (frontier_inter_subset _ _).trans
  rw [frontier_compl]
  apply union_subset <| inter_subset_left
  refine inter_subset_inter_right _ ?_ |>.trans <| inter_subset_right
  exact frontier_subset_frontier S x

/-- Let `S` be a closed subalgebra of a Banach algebra `A`, and let `x : S`. If `z` is in the
spectrum of `x`, then the connected component of `z` in the complement of the spectrum of `â†‘x : A`
is bounded (or else `z` actually belongs to the spectrum of `â†‘x : A`). -/
lemma Subalgebra.spectrum_isBounded_connectedComponentIn {z : ğ•œ} (hz : z âˆˆ Ïƒ ğ•œ x) :
    Bornology.IsBounded (connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z) := by
  by_cases hz' : z âˆˆ Ïƒ ğ•œ (x : A)
  Â· simp [connectedComponentIn_eq_empty (show z âˆ‰ (Ïƒ ğ•œ (x : A))á¶œ from not_not.mpr hz')]
  Â· have : CompleteSpace S := hS.completeSpace_coe
    suffices connectedComponentIn (Ïƒ ğ•œ (x : A))á¶œ z âŠ† Ïƒ ğ•œ x from spectrum.isBounded x |>.subset this
    rw [spectrum_sUnion_connectedComponentIn S]
    exact subset_biUnion_of_mem (mem_diff_of_mem hz hz') |>.trans subset_union_right

end NormedField

variable [NontriviallyNormedField ğ•œ] [NormedAlgebra ğ•œ A] [SMulMemClass SA ğ•œ A]
variable (S : SA) [hS : IsClosed (S : Set A)] (x : S)

/-- Let `S` be a closed subalgebra of a Banach algebra `A`. If for `x : S` the complement of the
spectrum of `â†‘x : A` is connected, then `spectrum ğ•œ x = spectrum ğ•œ (x : A)`. -/
lemma Subalgebra.spectrum_eq_of_isPreconnected_compl (h : IsPreconnected (Ïƒ ğ•œ (x : A))á¶œ) :
    Ïƒ ğ•œ x = Ïƒ ğ•œ (x : A) := by
  nontriviality A
  suffices Ïƒ ğ•œ x \ Ïƒ ğ•œ (x : A) = âˆ… by
    rw [spectrum_sUnion_connectedComponentIn, this]
    simp
  refine eq_empty_of_forall_notMem fun z hz â†¦ NormedSpace.unbounded_univ ğ•œ ğ•œ ?_
  obtain âŸ¨hz, hz'âŸ© := mem_diff _ |>.mp hz
  have := (spectrum.isBounded (x : A)).union <|
    h.connectedComponentIn hz' â–¸ spectrum_isBounded_connectedComponentIn S x hz
  simpa

end BoundarySpectrum

namespace SpectrumRestricts

open NNReal ENNReal

/-- If `ğ•œâ‚` is a normed field contained as subfield of a larger normed field `ğ•œâ‚‚`, and if `a : A`
is an element whose `ğ•œâ‚‚` spectrum restricts to `ğ•œâ‚`, then the spectral radii over each scalar
field coincide. -/
lemma spectralRadius_eq {ğ•œâ‚ ğ•œâ‚‚ A : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚]
    [NormedRing A] [NormedAlgebra ğ•œâ‚ A] [NormedAlgebra ğ•œâ‚‚ A] [NormedAlgebra ğ•œâ‚ ğ•œâ‚‚]
    [IsScalarTower ğ•œâ‚ ğ•œâ‚‚ A] {f : ğ•œâ‚‚ â†’ ğ•œâ‚} {a : A} (h : SpectrumRestricts a f) :
    spectralRadius ğ•œâ‚ a = spectralRadius ğ•œâ‚‚ a := by
  rw [spectralRadius, spectralRadius]
  have := algebraMap_isometry ğ•œâ‚ ğ•œâ‚‚ |>.nnnorm_map_of_map_zero (map_zero _)
  apply le_antisymm
  all_goals apply iSupâ‚‚_le fun x hx â†¦ ?_
  Â· refine congr_arg ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) (this x) |>.symm.trans_le <| le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) _ ?_
    exact (spectrum.algebraMap_mem_iff _).mpr hx
  Â· have âŸ¨y, hy, hy'âŸ© := h.algebraMap_image.symm â–¸ hx
    subst hy'
    exact this y â–¸ le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) y hy

variable {A : Type*} [Ring A]

lemma nnreal_iff [Algebra â„ A] {a : A} :
    SpectrumRestricts a ContinuousMap.realToNNReal â†” âˆ€ x âˆˆ spectrum â„ a, 0 â‰¤ x := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨x, -, rflâŸ© := h.algebraMap_image.symm â–¸ hx
    exact coe_nonneg x
  Â· exact .of_subset_range_algebraMap (fun _ â†¦ Real.toNNReal_coe) fun x hx â†¦ âŸ¨âŸ¨x, h x hxâŸ©, rflâŸ©

lemma nnreal_of_nonneg {A : Type*} [Ring A] [PartialOrder A] [Algebra â„ A]
    [NonnegSpectrumClass â„ A] {a : A} (ha : 0 â‰¤ a) :
    SpectrumRestricts a ContinuousMap.realToNNReal :=
  nnreal_iff.mpr <| spectrum_nonneg_of_nonneg ha

lemma real_iff [Algebra â„‚ A] {a : A} :
    SpectrumRestricts a Complex.reCLM â†” âˆ€ x âˆˆ spectrum â„‚ a, x = x.re := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨x, -, rflâŸ© := h.algebraMap_image.symm â–¸ hx
    simp
  Â· exact .of_subset_range_algebraMap Complex.ofReal_re fun x hx â†¦ âŸ¨x.re, (h x hx).symmâŸ©

lemma nnreal_le_iff [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ spectrum â„â‰¥0 a, r â‰¤ x) â†” âˆ€ x âˆˆ spectrum â„ a, r â‰¤ x := by
  simp [â† ha.algebraMap_image]

lemma nnreal_lt_iff [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ spectrum â„â‰¥0 a, r < x) â†” âˆ€ x âˆˆ spectrum â„ a, r < x := by
  simp [â† ha.algebraMap_image]

lemma le_nnreal_iff [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ spectrum â„â‰¥0 a, x â‰¤ r) â†” âˆ€ x âˆˆ spectrum â„ a, x â‰¤ r := by
  simp [â† ha.algebraMap_image]

lemma lt_nnreal_iff [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ spectrum â„â‰¥0 a, x < r) â†” âˆ€ x âˆˆ spectrum â„ a, x < r := by
  simp [â† ha.algebraMap_image]

lemma nnreal_iff_spectralRadius_le [Algebra â„ A] {a : A} {t : â„â‰¥0} (ht : spectralRadius â„ a â‰¤ t) :
    SpectrumRestricts a ContinuousMap.realToNNReal â†”
      spectralRadius â„ (algebraMap â„ A t - a) â‰¤ t := by
  have : spectrum â„ a âŠ† Set.Icc (-t) t := by
    intro x hx
    rw [Set.mem_Icc, â† abs_le, â† Real.norm_eq_abs, â† coe_nnnorm, NNReal.coe_le_coe,
      â† ENNReal.coe_le_coe]
    exact le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) x hx |>.trans ht
  rw [nnreal_iff]
  refine âŸ¨fun h â†¦ iSupâ‚‚_le fun x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [â† spectrum.singleton_sub_eq] at hx
    obtain âŸ¨y, hy, rflâŸ© : âˆƒ y âˆˆ spectrum â„ a, â†‘t - y = x := by simpa using hx
    obtain âŸ¨hty, hytâŸ© := Set.mem_Icc.mp <| this hy
    lift y to â„â‰¥0 using h y hy
    rw [â† NNReal.coe_sub (by exact_mod_cast hyt)]
    simp
  Â· replace h : âˆ€ x âˆˆ spectrum â„ a, â€–t - xâ€–â‚Š â‰¤ t := by
      simpa [spectralRadius, iSupâ‚‚_le_iff, â† spectrum.singleton_sub_eq] using h
    peel h with x hx h_le
    rw [â† NNReal.coe_le_coe, coe_nnnorm, Real.norm_eq_abs, abs_le] at h_le
    linarith [h_le.2]

lemma _root_.NNReal.spectralRadius_mem_spectrum {A : Type*} [NormedRing A] [NormedAlgebra â„ A]
    [CompleteSpace A] {a : A} (ha : (spectrum â„ a).Nonempty)
    (ha' : SpectrumRestricts a ContinuousMap.realToNNReal) :
    (spectralRadius â„ a).toNNReal âˆˆ spectrum â„â‰¥0 a := by
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := spectrum.exists_nnnorm_eq_spectralRadius_of_nonempty ha
  rw [â† hxâ‚‚, ENNReal.toNNReal_coe, â† spectrum.algebraMap_mem_iff â„, NNReal.algebraMap_eq_coe]
  have : 0 â‰¤ x := ha'.rightInvOn hxâ‚ â–¸ NNReal.zero_le_coe
  convert hxâ‚
  simpa

lemma _root_.Real.spectralRadius_mem_spectrum {A : Type*} [NormedRing A] [NormedAlgebra â„ A]
    [CompleteSpace A] {a : A} (ha : (spectrum â„ a).Nonempty)
    (ha' : SpectrumRestricts a ContinuousMap.realToNNReal) :
    (spectralRadius â„ a).toReal âˆˆ spectrum â„ a :=
  NNReal.spectralRadius_mem_spectrum ha ha'

lemma _root_.Real.spectralRadius_mem_spectrum_or {A : Type*} [NormedRing A] [NormedAlgebra â„ A]
    [CompleteSpace A] {a : A} (ha : (spectrum â„ a).Nonempty) :
    (spectralRadius â„ a).toReal âˆˆ spectrum â„ a âˆ¨ -(spectralRadius â„ a).toReal âˆˆ spectrum â„ a := by
  obtain âŸ¨x, hxâ‚, hxâ‚‚âŸ© := spectrum.exists_nnnorm_eq_spectralRadius_of_nonempty ha
  simp only [â† hxâ‚‚, ENNReal.coe_toReal, coe_nnnorm, Real.norm_eq_abs]
  exact abs_choice x |>.imp (fun h â†¦ by rwa [h]) (fun h â†¦ by simpa [h])

end SpectrumRestricts

namespace QuasispectrumRestricts

open NNReal ENNReal
local notation "Ïƒâ‚™" => quasispectrum

lemma compactSpace {R S A : Type*} [Semifield R] [Field S] [NonUnitalRing A]
    [Algebra R S] [Module R A] [Module S A] [IsScalarTower S A A] [SMulCommClass S A A]
    [IsScalarTower R S A] [TopologicalSpace R] [TopologicalSpace S] {a : A} (f : C(S, R))
    (h : QuasispectrumRestricts a f) [h_cpct : CompactSpace (Ïƒâ‚™ S a)] :
    CompactSpace (Ïƒâ‚™ R a) := by
  rw [â† isCompact_iff_compactSpace] at h_cpct âŠ¢
  exact h.image â–¸ h_cpct.image (map_continuous f)

variable {A : Type*} [NonUnitalRing A]

lemma nnreal_iff [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A] {a : A} :
    QuasispectrumRestricts a ContinuousMap.realToNNReal â†” âˆ€ x âˆˆ Ïƒâ‚™ â„ a, 0 â‰¤ x := by
  rw [quasispectrumRestricts_iff_spectrumRestricts_inr,
    Unitization.quasispectrum_eq_spectrum_inr' _ â„, SpectrumRestricts.nnreal_iff]

lemma nnreal_of_nonneg [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A] [PartialOrder A]
    [NonnegSpectrumClass â„ A] {a : A} (ha : 0 â‰¤ a) :
    QuasispectrumRestricts a ContinuousMap.realToNNReal :=
  nnreal_iff.mpr <| quasispectrum_nonneg_of_nonneg _ ha

lemma real_iff [Module â„‚ A] [IsScalarTower â„‚ A A] [SMulCommClass â„‚ A A] {a : A} :
    QuasispectrumRestricts a Complex.reCLM â†” âˆ€ x âˆˆ Ïƒâ‚™ â„‚ a, x = x.re := by
  rw [quasispectrumRestricts_iff_spectrumRestricts_inr,
    Unitization.quasispectrum_eq_spectrum_inr' _ â„‚, SpectrumRestricts.real_iff]

lemma le_nnreal_iff [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A] {a : A}
    (ha : QuasispectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ quasispectrum â„â‰¥0 a, x â‰¤ r) â†” âˆ€ x âˆˆ quasispectrum â„ a, x â‰¤ r := by
  simp [â† ha.algebraMap_image]

lemma lt_nnreal_iff [Module â„ A] [IsScalarTower â„ A A] [SMulCommClass â„ A A] {a : A}
    (ha : QuasispectrumRestricts a ContinuousMap.realToNNReal) {r : â„â‰¥0} :
    (âˆ€ x âˆˆ quasispectrum â„â‰¥0 a, x < r) â†” âˆ€ x âˆˆ quasispectrum â„ a, x < r := by
  simp [â† ha.algebraMap_image]

end QuasispectrumRestricts

variable {A : Type*} [Ring A] [PartialOrder A]

lemma coe_mem_spectrum_real_of_nonneg [Algebra â„ A] [NonnegSpectrumClass â„ A] {a : A} {x : â„â‰¥0}
    (ha : 0 â‰¤ a := by cfc_tac) :
    (x : â„) âˆˆ spectrum â„ a â†” x âˆˆ spectrum â„â‰¥0 a := by
  simp [â† (SpectrumRestricts.nnreal_of_nonneg ha).algebraMap_image, Set.mem_image,
    NNReal.algebraMap_eq_coe]
