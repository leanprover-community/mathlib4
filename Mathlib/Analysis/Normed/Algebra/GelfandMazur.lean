/-
Copyright (c) 2025 Michael Stoll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Stoll
-/
import Mathlib.Analysis.Polynomial.Factorization
import Mathlib.Analysis.Normed.Algebra.Basic

/-!
# A (new?) proof of the Gelfand-Mazur Theorem

We provide a formalization of proofs of the following versions of the *Gelfand-Mazur Theorem*.

* `NormedAlgebra.Complex.algEquivOfNormMul`: if `F` is a nontrivial normed `‚ÑÇ`-algebra
  with multiplicative norm, then we obtain a `‚ÑÇ`-algebra equivalence with `‚ÑÇ`.

  This differs from `NormedRing.algEquivComplexOfComplete` in the assumptions: there,
  * `F` is assumed to be complete,
  * `F` is assumed to be a (nontrivial) division ring,
  * but the norm is only required to be submultiplicative.
* `NormedAlgebra.Complex.nonempty_algEquiv`: A nontrivial normed `‚ÑÇ`-algebra
  with multiplicative norm is isomorphic to `‚ÑÇ` as a `‚ÑÇ`-algebra.
* `NormedAlgebra.Real.nonempty_algEquiv_or`: if a field `F` is a normed `‚Ñù`-algebra,
  then `F` is isomorphic as an `‚Ñù`-algebra either to `‚Ñù` or to `‚ÑÇ`.

  With some additional work (TODO), this implies a
  [Theorem of Ostrowski](https://en.wikipedia.org/wiki/Ostrowski%27s_theorem#Another_Ostrowski's_theorem),
  which says that any field that is complete with respect to an archimedean absolute value
  is isomorphic to either `‚Ñù` or `‚ÑÇ` as a field with absolute value. The additional input needed
  for this is to show that any such field is in fact a normed `‚Ñù`-algebra.

### The complex case

The proof we use here is a variant of a proof for the complex case (any normed `‚ÑÇ`-algebra
is isomorphic to `‚ÑÇ`) that is originally due to Ostrowski
[A. Ostrowski, *√úber einige L√∂sungen der Funktionalgleichung œÜ(x)‚ãÖœÜ(y)=œÜ(xy)*
  (Section 7)][ostrowski1916].
See also the concise version provided by Peter Scholze on
[Math Overflow](https://mathoverflow.net/questions/10535/ways-to-prove-the-fundamental-theorem-of-algebra/420803#420803).

(In the following, we write `a ‚Ä¢ 1` instead of `algebraMap _ F a` for easier reading.
In the code, we use `algebraMap`.)

This proof goes as follows. Let `x : F` be arbitrary; we need to show that `x = z ‚Ä¢ 1`
for some `z : ‚ÑÇ`. We consider the function `z ‚Ü¶ ‚Äñx - z ‚Ä¢ 1‚Äñ`. It has a minimum `M`,
which it attains at some point `z‚ÇÄ`, which (upon replacing `x` by `x - z‚ÇÄ ‚Ä¢ 1`) we can
assume to be zero. If `M = 0`, we are done, so assume not. For `n : ‚Ñï`,
a primitive `n`th root of unity `Œ∂ : ‚ÑÇ`, and `z : ‚ÑÇ` with `|z| < M = ‚Äñx‚Äñ` we then have that
`M ‚â§ ‚Äñx - z ‚Ä¢ 1‚Äñ = ‚Äñx ^ n - z ^ n ‚Ä¢ 1‚Äñ / ‚àè 0 < k < n, ‚Äñx - (Œ∂ ^ k * z) ‚Ä¢ 1‚Äñ`,
which is bounded by `(M ^ n + |z| ^ n) / M ^ (n - 1) = M * (1 + (|z| / M) ^ n)`.
Letting `n` tend to infinity then shows that `‚Äñx - z ‚Ä¢ 1‚Äñ = M`
(see `NormedAlgebra.norm_eq_of_isMinOn_of_forall_le`).
This implies that the set of `z` such that `‚Äñx - z ‚Ä¢ 1‚Äñ = M` is closed and open
(and nonempty), so it is all of `‚ÑÇ`, which contradicts `‚Äñx - z ‚Ä¢ 1‚Äñ ‚â• |z| - M`
when `|z|` is sufficiently large.

### The real case

The usual proof for the real case is "either `F` contains a square root of `-1`;
then `F` is in fact a normed `‚ÑÇ`-agebra and we can use the result above, or else
we adjoin a square root of `-1` to `F` to obtain a normed `‚ÑÇ`-agebra `F'` and
apply the result to `F'`". The difficulty with formalizing this is that (as of October 2025)
Mathlib does not provide a normed `‚ÑÇ`-algebra instance for `F'` (neither for
`F' := AdjoinRoot (X ^ 2 + 1 : F[X])` nor for `F' := TensorProduct ‚Ñù ‚ÑÇ F`),
and it is not so straight-forward to set this up. So we take inspiration from the
proof sketched above for the complex case to obtain a direct proof.

Since irreducible polynomials over `‚Ñù` have degree at most `2`, it must be the case
that each element is annihilated by a monic polynomial of degree `2`.
We fix `x : F` in the following.

The space `‚Ñù √ó ‚Ñù` of monic polynomials of degree `2` is complete and locally compact
and hence `‚Äñaeval x p‚Äñ` gets large when `p` has large coefficients.
This is actually slightly subtle. It is certainly true for `‚Äñx - r ‚Ä¢ 1‚Äñ` with `r : ‚Ñù`.
If the minimum of this is zero, then the minimum for monic polynomials of degree `2`
will also be zero (and is attained on a one-dimensional subset). Otherwise, one can
indeed show that a bound on `‚Äñx ^ 2 - a ‚Ä¢ x + b ‚Ä¢ 1‚Äñ` implies bounds on `|a|` and `|b|`.

By the first sentence of the previous paragraph, there will be some `p‚ÇÄ`
such that `‚Äñaeval x p‚ÇÄ‚Äñ` attains a minimum (see `NormedAlgebra.Real.exists_isMinOn_norm_œÜ`).
We assume that this is positive and derive a contradiction. Let `M := ‚Äñaeval x p‚ÇÄ‚Äñ > 0`
be the minimal value.
Since every monic polynomial `f : ‚Ñù[X]` of even degree can be written as a product
of monic polynomials of degree `2`
(see `Polynomial.IsMonicOfDegree.eq_isMonicOfDegree_two_mul_isMonicOfDegree`),
it follows that `‚Äñaeval x f‚Äñ ‚â• M ^ (f.natDegree / 2)`.

The goal is now to show that when `a` and `b` achieve the minimum `M` of `‚Äñx ^ 2 - a ‚Ä¢ x + b ‚Ä¢ 1‚Äñ`
and `M > 0`, then we can find some neighborhood `U` of `(a, b)` in `‚Ñù √ó ‚Ñù`
such that `‚Äñx ^ 2 - a' ‚Ä¢ x + b' ‚Ä¢ 1‚Äñ = M` for all `(a', b') ‚àà U`
Then the set `S = {(a', b') | ‚Äñx ^ 2 - a' ‚Ä¢ x + b' ‚Ä¢ 1‚Äñ = M}` must be all of `‚Ñù √ó ‚Ñù` (as it is
closed, open, and nonempty). (see `NormedAlgebra.Real.norm_œÜ_eq_norm_œÜ_of_isMinOn`).
This will lead to a contradiction with the growth of `‚Äñx ^ 2 - a ‚Ä¢ x‚Äñ` as `|a|` gets large.

To get there, the idea is, similarly to the complex case, to use the fact that
`X ^ 2 - a' * X + b'` divides the difference
`(X ^ 2 - a * X + b) ^ n - ((a' - a) * X - (b' - b)) ^ n`;
this gives us a monic polynomial `p` of degree `2 * (n - 1)` such that `(X ^ 2 - a' * X + b') * p`
equals this difference. By the above, `‚Äñaeval x p‚Äñ ‚â• M ^ (n - 1)`.

Since `(a', b') ‚Ü¶ x ^ 2 - a' ‚Ä¢ x + b' ‚Ä¢ 1` is continuous, there will be a neighborhood `U`
of `(a, b)` such that
`‚Äñ(a' - a) ‚Ä¢ x - (b' - b) ‚Ä¢ 1‚Äñ = ‚Äñ(x ^ 2 - a ‚Ä¢ x + b ‚Ä¢ 1) - (x ^ 2 -a' ‚Ä¢ x + b' ‚Ä¢ 1)‚Äñ`
is less than `M` for `(a', b') ‚àà U`. For such `(a', b')`, it follows that
`‚Äñx ^ 2 - a' ‚Ä¢ x + b' ‚Ä¢ 1‚Äñ ‚â§ ‚Äñ(x ^ 2 - a ‚Ä¢ x + b ‚Ä¢ 1) ^ n - ((a' - a) ‚Ä¢ x - (b' - b) ‚Ä¢ 1) ^ n‚Äñ /`
`‚Äñaeval x p‚Äñ`,
which is bounded by `(M ^ n + c ^ n) / M ^ (n - 1) = M * (1 + (c / M) ^ n)`, where
`c = ‚Äñ(a' - a) ‚Ä¢ x - (b' - b) ‚Ä¢ 1‚Äñ < M`. So, letting `n` tend to infinity, we obtain that
`M ‚â§ ‚Äñx ^ 2 - a' ‚Ä¢ x + b' ‚Ä¢ 1‚Äñ ‚â§ M`, as desired.
-/

/-!
### Auxiliary results used in both cases
-/

open Polynomial

namespace NormedAlgebra

open Filter Topology Set in
/- The key step: show that the norm of a suitable function is constant if the norm takes
a positive minimum and condition `H` below is satisfied. -/
private lemma norm_eq_of_isMinOn_of_forall_le {X E : Type*} [TopologicalSpace X]
    [PreconnectedSpace X] [SeminormedAddCommGroup E] {f : X ‚Üí E} {M : ‚Ñù} {x : X} (hM : 0 < M)
    (hx : ‚Äñf x‚Äñ = M) (h : IsMinOn (‚Äñf ¬∑‚Äñ) univ x) (hf : Continuous f)
    (H : ‚àÄ {y} z, ‚Äñf y‚Äñ = M ‚Üí ‚àÄ n > 0, ‚Äñf z‚Äñ ‚â§ M * (1 + (‚Äñf z - f y‚Äñ / M) ^ n)) (y : X) :
    ‚Äñf y‚Äñ = M := by
  suffices {y | ‚Äñf y‚Äñ = M} = univ by simpa only [‚Üê this, hx] using mem_univ y
  refine IsClopen.eq_univ ‚ü®isClosed_eq (by fun_prop) (by fun_prop), ?_‚ü© <| nonempty_of_mem hx
  rw [isOpen_iff_eventually]
  intro w hw
  filter_upwards [mem_map.mp <| hf.tendsto w (Metric.ball_mem_nhds (f w) hM)] with u hu
  simp only [mem_preimage, Metric.mem_ball, dist_eq_norm, ‚Üê div_lt_one‚ÇÄ hM] at hu
  refine le_antisymm ?_ (hx ‚ñ∏ isMinOn_univ_iff.mp h u)
  suffices Tendsto (fun n : ‚Ñï ‚Ü¶ M * (1 + (‚Äñf u - f w‚Äñ / M) ^ n)) atTop (ùìù (M * (1 + 0))) by
    refine ge_of_tendsto (by simpa) ?_
    filter_upwards [Ioi_mem_atTop 0] with n hn
    exact H u hw n hn
  exact tendsto_pow_atTop_nhds_zero_of_lt_one (by positivity) hu |>.const_add 1 |>.const_mul M

open Filter Bornology Set in
/-- In a normed algebra `F` over a normed field `ùïú` that is a proper space, the function
`z : ùïú ‚Ü¶ ‚Äñx - algebraMap ùïú F z‚Äñ` achieves a global minimum for every `x : F`. -/
lemma exists_isMinOn_norm_sub_smul (ùïú : Type*) {F : Type*} [NormedField ùïú] [ProperSpace ùïú]
    [SeminormedRing F] [NormedAlgebra ùïú F] [NormOneClass F] (x : F) :
    ‚àÉ z : ùïú, IsMinOn (‚Äñx - algebraMap ùïú F ¬∑‚Äñ) univ z := by
  have : Tendsto (‚Äñx - algebraMap ùïú F ¬∑‚Äñ) (cobounded ùïú) atTop := by
    exact tendsto_norm_cobounded_atTop |>.comp <| tendsto_const_sub_cobounded x |>.comp <| by simp
  simp only [isMinOn_univ_iff]
  refine (show Continuous fun z : ùïú ‚Ü¶ ‚Äñx - algebraMap ùïú F z‚Äñ by fun_prop)
    |>.exists_forall_le_of_isBounded 0 ?_
  simpa [isBounded_def, compl_setOf, Ioi] using this (Ioi_mem_atTop ‚Äñx - (0 : ùïú) ‚Ä¢ 1‚Äñ)

/-!
### The complex case
-/

namespace Complex

variable {F : Type*} [NormedRing F] [NormOneClass F] [NormMulClass F] [NormedAlgebra ‚ÑÇ F]

/- If the norm of every monic linear polynomial over `‚ÑÇ`, evaluated at some `x : F`,
is bounded below by `M`, then the norm of the value at `x - algebraMap ‚ÑÇ F c` of a monic polynomial
of degree `n` is bounded below by `M ^ n`. This follows by induction from the fact that
every monic polynomial over `‚ÑÇ` factors as a product of monic linear polynomials. -/
private lemma le_aeval_of_isMonicOfDegree (x : F) {M : ‚Ñù} (hM : 0 ‚â§ M)
    (h : ‚àÄ z' : ‚ÑÇ, M ‚â§ ‚Äñx - algebraMap ‚ÑÇ F z'‚Äñ) {p : ‚ÑÇ[X]} {n : ‚Ñï} (hp : IsMonicOfDegree p n)
    (c : ‚ÑÇ) :
    M ^ n ‚â§ ‚Äñaeval (x - algebraMap ‚ÑÇ F c) p‚Äñ := by
  induction n generalizing p with
  | zero => simp [isMonicOfDegree_zero_iff.mp hp]
  | succ n ih =>
    obtain ‚ü®f‚ÇÅ, f‚ÇÇ, hf‚ÇÅ, hf‚ÇÇ, H‚ü© := hp.eq_isMonicOfDegree_one_mul_isMonicOfDegree
    obtain ‚ü®r, rfl‚ü© := isMonicOfDegree_one_iff.mp hf‚ÇÅ
    have H' (y : F) : aeval y (X + C r) = y + algebraMap ‚ÑÇ F r := by simp
    simpa only [pow_succ, mul_comm, H, aeval_mul, H', sub_add, ‚Üê map_sub, norm_mul]
      using mul_le_mul (ih hf‚ÇÇ) (h (c - r)) hM (norm_nonneg _)

open Set in
/- We show that when `z ‚Ü¶ ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ` is never zero (and attains a minimum), then
it is constant. This uses the auxiliary result `norm_eq_of_isMinOn_of_forall_le`. -/
private lemma norm_sub_eq_norm_sub_of_isMinOn {x : F} {z : ‚ÑÇ}
    (hz : IsMinOn (‚Äñx - algebraMap ‚ÑÇ F ¬∑‚Äñ) univ z) (H : ‚àÄ z' : ‚ÑÇ, ‚Äñx - algebraMap ‚ÑÇ F z'‚Äñ ‚â† 0)
    (c : ‚ÑÇ) :
    ‚Äñx - algebraMap ‚ÑÇ F c‚Äñ = ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ := by
  set M := ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ with hMdef
  have hM‚ÇÄ : 0 < M := by have := H z; positivity
  refine norm_eq_of_isMinOn_of_forall_le (f := (x - algebraMap ‚ÑÇ F ¬∑)) hM‚ÇÄ hMdef.symm hz
    (by fun_prop) (fun {y} w hy n hn ‚Ü¶ ?_) c
  dsimp only at hy ‚ä¢
  -- show
  --  `‚Äñx - algebraMap ‚ÑÇ F w‚Äñ ‚â§ M * (1 + (‚Äñx - algebraMap ‚ÑÇ F w - (x - algebraMap ‚ÑÇ F y)‚Äñ / M) ^ n)`
  rw [sub_sub_sub_cancel_left, ‚Üê map_sub, norm_algebraMap, norm_sub_rev y w, norm_one, mul_one,
    show M * (1 + (‚Äñw - y‚Äñ / M) ^ n) = (M ^ n + ‚Äñw - y‚Äñ ^ n) / M ^ (n - 1) by
      simp only [field, div_pow, ‚Üê pow_succ', Nat.sub_add_cancel hn],
    le_div_iff‚ÇÄ (by positivity)]
  obtain ‚ü®p, hp, hrel‚ü© :=
    (isMonicOfDegree_X_pow ‚ÑÇ n).of_dvd_sub (by grind)
      (isMonicOfDegree_X_sub_one (w - y)) (by compute_degree!) <| sub_dvd_pow_sub_pow X _ n
  grw [le_aeval_of_isMonicOfDegree x hM‚ÇÄ.le (isMinOn_univ_iff.mp hz) hp y]
  rw [eq_comm, ‚Üê eq_sub_iff_add_eq, mul_comm] at hrel
  apply_fun (‚Äñaeval (x - algebraMap ‚ÑÇ F y) ¬∑‚Äñ) at hrel
  simp only [map_sub, map_mul, aeval_X, aeval_C, sub_sub_sub_cancel_right, norm_mul, map_pow]
    at hrel
  rw [hrel]
  exact (norm_sub_le ..).trans <| by simp [hy, ‚Üê map_sub]

/-- If `F` is a normed `‚ÑÇ`-algebra and `x : F`, then there is a complex number `z` such that
`‚Äñx - algebraMap ‚ÑÇ F z‚Äñ = 0` (whence `x = algebraMap ‚ÑÇ F z`). -/
lemma exists_norm_sub_smul_one_eq_zero (x : F) :
    ‚àÉ z : ‚ÑÇ, ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ = 0 := by
  -- there is a minimizing `z : ‚ÑÇ`; get it.
  obtain ‚ü®z, hz‚ü© := exists_isMinOn_norm_sub_smul ‚ÑÇ x
  set M := ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ with hM
  rcases eq_or_lt_of_le (show 0 ‚â§ M from norm_nonneg _) with hM‚ÇÄ | hM‚ÇÄ
    -- minimum is zero: nothing to do
  ¬∑ exact ‚ü®z, hM‚ÇÄ.symm‚ü©
  -- otherwise, use the result from above that `z ‚Ü¶ ‚Äñx - algebraMap ‚ÑÇ F z‚Äñ` is constant
  -- to derive a contradiction.
  by_contra! H
  have key := norm_sub_eq_norm_sub_of_isMinOn hz H (‚Äñx‚Äñ + M + 1)
  rw [‚Üê hM, norm_sub_rev] at key
  replace key := (norm_sub_norm_le ..).trans_eq key
  rw [norm_algebraMap, norm_one, mul_one] at key
  norm_cast at key
  rw [Real.norm_eq_abs, abs_of_nonneg (by positivity)] at key
  linarith

variable (F) [Nontrivial F]

open Algebra in
/-- A version of the **Gelfand-Mazur Theorem**.

If `F` is a nontrivial normed `‚ÑÇ`-algebra with multiplicative norm, then we obtain a
`‚ÑÇ`-algebra equivalence with `‚ÑÇ`. -/
noncomputable
def algEquivOfNormMul : ‚ÑÇ ‚âÉ‚Çê[‚ÑÇ] F :=
  .ofBijective (ofId ‚ÑÇ F) <| by
    refine ‚ü®FaithfulSMul.algebraMap_injective ‚ÑÇ F, fun x ‚Ü¶ ?_‚ü©
    obtain ‚ü®z, hz‚ü© := exists_norm_sub_smul_one_eq_zero x
    refine ‚ü®z, ?_‚ü©
    rwa [norm_eq_zero, sub_eq_zero, eq_comm, ‚Üê ofId_apply] at hz

/-- A version of the **Gelfand-Mazur Theorem** for nontrivial normed `‚ÑÇ`-algebras `F`
with multiplicative norm: any such `F` is isomorphic to `‚ÑÇ` as a `‚ÑÇ`-algebra. -/
theorem nonempty_algEquiv : Nonempty (‚ÑÇ ‚âÉ‚Çê[‚ÑÇ] F) := ‚ü®algEquivOfNormMul F‚ü©

end Complex


/-!
### The real case
-/

namespace Real

variable {F : Type*} [NormedRing F] [NormedAlgebra ‚Ñù F]

/- An abbreviation introduced for conciseness below.
We will show that for every `x : F`, `œÜ x` takes the value zero. -/
private abbrev œÜ (x : F) (u : ‚Ñù √ó ‚Ñù) : F := x ^ 2 - u.1 ‚Ä¢ x + algebraMap ‚Ñù F u.2

private lemma continuous_œÜ (x : F) : Continuous (œÜ x) := by fun_prop

private lemma aeval_eq_œÜ (x : F) (u : ‚Ñù √ó ‚Ñù) : aeval x (X ^ 2 - C u.1 * X + C u.2) = œÜ x u := by
  simp [Algebra.algebraMap_eq_smul_one]

variable [NormOneClass F] [NormMulClass F]

/- If, for some `x : F`, `‚ÄñœÜ x ¬∑‚Äñ` is bounded below by `M`, then the value at `x` of any monic
polynomial over `‚Ñù` of degree `2 * n` has norm bounded below by `M ^ n`. This follows by
induction from the fact that a real monic polynomial of even degree is a product of monic
polynomials of degree `2`. -/
private lemma le_aeval_of_isMonicOfDegree {x : F} {M : ‚Ñù} (hM : 0 ‚â§ M)
    (h : ‚àÄ z : ‚Ñù √ó ‚Ñù, M ‚â§ ‚ÄñœÜ x z‚Äñ) {p : ‚Ñù[X]} {n : ‚Ñï} (hp : IsMonicOfDegree p (2 * n)) :
    M ^ n ‚â§ ‚Äñaeval x p‚Äñ := by
  induction n generalizing p with
  | zero => simp_all
  | succ n ih =>
    rw [mul_add, mul_one] at hp
    obtain ‚ü®f‚ÇÅ, f‚ÇÇ, hf‚ÇÅ, hf‚ÇÇ, H‚ü© := hp.eq_isMonicOfDegree_two_mul_isMonicOfDegree
    obtain ‚ü®a, b, hab‚ü© := isMonicOfDegree_two_iff'.mp hf‚ÇÅ
    rw [H, aeval_mul, norm_mul, mul_comm, pow_succ, hab, aeval_eq_œÜ x (a, b)]
    exact mul_le_mul (ih hf‚ÇÇ) (h (a, b)) hM (norm_nonneg _)

/- The key step in the proof: if `a` and `b` are real numbers minimizing `‚ÄñœÜ x (a, b)‚Äñ`,
and the minimal value is strictly positive, then the function `(s, t) ‚Ü¶ ‚ÄñœÜ x (s, t)‚Äñ`
is constant. -/
private lemma norm_œÜ_eq_norm_œÜ_of_isMinOn {x : F} {z : ‚Ñù √ó ‚Ñù} (h : IsMinOn (‚ÄñœÜ x ¬∑‚Äñ) Set.univ z)
    (H : ‚ÄñœÜ x z‚Äñ ‚â† 0) (w : ‚Ñù √ó ‚Ñù) :
    ‚ÄñœÜ x w‚Äñ = ‚ÄñœÜ x z‚Äñ := by
  set M : ‚Ñù := ‚ÄñœÜ x z‚Äñ with hMdef
  have hM‚ÇÄ : 0 < M := by positivity
  -- we use the key result `norm_eq_of_isMinOn_of_forall_le`
  refine norm_eq_of_isMinOn_of_forall_le hM‚ÇÄ hMdef.symm h (continuous_œÜ x)
    (fun {w} u hw n hn ‚Ü¶ ?_) w
  -- show `‚ÄñœÜ x u‚Äñ ‚â§ M * (1 + (‚ÄñœÜ x u - œÜ x w‚Äñ / M) ^ n)`
  have HH : M * (1 + (‚ÄñœÜ x u - œÜ x w‚Äñ / M) ^ n) = (M ^ n + ‚ÄñœÜ x u - œÜ x w‚Äñ ^ n) / M ^ (n - 1) := by
    simp only [field, div_pow, ‚Üê pow_succ', Nat.sub_add_cancel hn]
  rw [HH, le_div_iff‚ÇÄ (by positivity)]; clear HH
  -- show `‚ÄñœÜ x u‚Äñ * M ^ (n - 1) ‚â§ M ^ n + ‚ÄñœÜ x u - œÜ x w‚Äñ ^ n`
  let q (y : ‚Ñù √ó ‚Ñù) : ‚Ñù[X] := X ^ 2 - C y.1 * X + C y.2
  have hq (y : ‚Ñù √ó ‚Ñù) : IsMonicOfDegree (q y) 2 := isMonicOfDegree_sub_add_two ..
  have hsub : q w - q u = (C u.1 - C w.1) * X + C w.2 - C u.2 := by simp only [q]; ring
  have hdvd : q u ‚à£ q w ^ n - (q w - q u) ^ n := by
    nth_rewrite 1 [‚Üê sub_sub_self (q w) (q u)]
    exact sub_dvd_pow_sub_pow ..
  have H' : ((q w - q u) ^ n).natDegree < 2 * n := by rw [hsub]; compute_degree; grind
  -- write `q w ^ n = p * q u + (q w - q u) ^ n` with a monic polynomial `p` of deg. `2 * (n - 1)`,
  -- where `aeval x (q u) = œÜ x u` (*).
  obtain ‚ü®p, hp, hrel‚ü© := ((hq w).pow n).of_dvd_sub (by grind) (hq u) H' hdvd; clear H' hdvd hsub
  rw [show 2 * n - 2 = 2 * (n - 1) by grind] at hp
  -- use that `‚Äñaeval p x‚Äñ ‚â• M ^ (n - 1)`.
  grw [le_aeval_of_isMonicOfDegree hM‚ÇÄ.le (isMinOn_univ_iff.mp h) hp]
  rw [‚Üê sub_eq_iff_eq_add, eq_comm, mul_comm] at hrel
  apply_fun (‚Äñaeval x ¬∑‚Äñ) at hrel
  rw [map_mul, norm_mul, map_sub, aeval_eq_œÜ x u] at hrel
  -- from (*) above, deduce
  -- `‚ÄñœÜ x u‚Äñ * ‚Äñ(aeval x) p‚Äñ = ‚Äñ(aeval x) (q w ^ n) - (aeval x) ((q w - q u) ^ n)‚Äñ`
  -- and use that.
  rw [hrel, norm_sub_rev (œÜ ..)]
  exact (norm_sub_le ..).trans <| by simp [q, aeval_eq_œÜ, hw]

/- Existence of a minimizing monic polynomial of degree 2 -/

/- Assuming that `‚Äñx - algebraMap ‚Ñù F ¬∑‚Äñ` is bounded below by a positive constant, we show that
`œÜ x w` grows unboundedly as `w : ‚Ñù √ó ‚Ñù` does. We will use this to obtain a contradiction
when `œÜ x` does not attain the value zero. -/
open Filter Topology Bornology in
private lemma tendsto_œÜ_cobounded {x : F} {c : ‚Ñù} (hc‚ÇÄ : 0 < c)
    (hbd : ‚àÄ r : ‚Ñù, c ‚â§ ‚Äñx - algebraMap ‚Ñù F r‚Äñ) :
    Tendsto (œÜ x ¬∑) (cobounded (‚Ñù √ó ‚Ñù)) (cobounded F) := by
  simp_rw [œÜ, sub_add]
  refine tendsto_const_sub_cobounded _ |>.comp ?_
  rw [‚Üê tendsto_norm_atTop_iff_cobounded]
  -- split into statements involving each of the two components separately.
  refine .cobounded_prod (fun s hs ‚Ü¶ ?_) ?_
    -- the first component is bounded and the second one is unbounded
  ¬∑ obtain ‚ü®M, hM_pos, hM‚ü© : ‚àÉ M > 0, ‚àÄ y ‚àà s, ‚Äñy‚Äñ ‚â§ M := hs.exists_pos_norm_le
    suffices Tendsto (fun y ‚Ü¶ ‚ÄñalgebraMap ‚Ñù F y.2‚Äñ - M * ‚Äñx‚Äñ) (ùìü s √óÀ¢ cobounded ‚Ñù) atTop by
      refine tendsto_atTop_mono' _ ?_ this
      filter_upwards [prod_mem_prod (mem_principal_self s) univ_mem] with w hw
      rw [norm_sub_rev]
      refine le_trans ?_ (norm_sub_norm_le ..)
      simp only [norm_algebraMap', norm_smul]--, norm_one, mul_one]
      gcongr
      exact hM _ (Set.mem_prod.mp hw).1
    simp only [norm_algebraMap', sub_eq_add_neg]
    exact tendsto_atTop_add_const_right _ _ <| tendsto_norm_atTop_iff_cobounded.mpr tendsto_snd
    -- the first component is unbounded and the second one is arbitrary
  ¬∑ suffices Tendsto (fun y : ‚Ñù √ó ‚Ñù ‚Ü¶ ‚Äñy.1‚Äñ * c) (cobounded ‚Ñù √óÀ¢ ‚ä§) atTop by
      refine tendsto_atTop_mono' _ ?_ this
      filter_upwards [prod_mem_prod (isBounded_singleton (x := 0)) univ_mem] with y hy
      calc ‚Äñy.1‚Äñ * c
        _ ‚â§ ‚Äñy.1‚Äñ * ‚Äñx - algebraMap ‚Ñù F (y.1‚Åª¬π * y.2)‚Äñ := by gcongr; exact hbd _
        _ = ‚Äñy.1 ‚Ä¢ x - algebraMap ‚Ñù F y.2‚Äñ := by
          simp only [‚Üê norm_smul, smul_sub, smul_smul, Algebra.algebraMap_eq_smul_one]
          simp_all
    rw [tendsto_mul_const_atTop_of_pos hc‚ÇÄ, tendsto_norm_atTop_iff_cobounded]
    exact tendsto_fst

/- The norm of `‚ÄñœÜ x ¬∑‚Äñ` attains a minimum on `‚Ñù √ó ‚Ñù`. -/
open Bornology Filter in
private lemma exists_isMinOn_norm_œÜ (x : F) : ‚àÉ z : ‚Ñù √ó ‚Ñù, IsMinOn (‚ÄñœÜ x ¬∑‚Äñ) Set.univ z := by
  -- use that `‚Äñx - algebraMap ‚Ñù F ¬∑‚Äñ` has a minimum.
  obtain ‚ü®u, hu‚ü© := exists_isMinOn_norm_sub_smul ‚Ñù x
  rcases eq_or_lt_of_le (norm_nonneg (x - algebraMap ‚Ñù F u)) with hc‚ÇÄ | hc‚ÇÄ
  ¬∑ -- if this minimum is zero, use `(u, 0)`.
    rw [eq_comm, norm_eq_zero, sub_eq_zero] at hc‚ÇÄ
    exact ‚ü®(u, 0), fun z' ‚Ü¶ by simp [œÜ, hc‚ÇÄ, sq, Algebra.smul_def]‚ü©
  -- otherwise, use `tendsto_œÜ_cobounded`.
  set c := ‚Äñx - algebraMap ‚Ñù F u‚Äñ
  simp only [isMinOn_univ_iff]
  refine (continuous_œÜ x).norm.exists_forall_le_of_isBounded (0, 0) ?_
  rw [isMinOn_univ_iff] at hu
  simpa [isBounded_def, Set.compl_setOf, Set.Ioi] using
    tendsto_norm_cobounded_atTop.comp (tendsto_œÜ_cobounded hc‚ÇÄ hu) (Ioi_mem_atTop (‚ÄñœÜ x (0, 0)‚Äñ))

open Algebra in
/-- If `F` is a normed `‚Ñù`-algebra with a multiplicative norm (and such that `‚Äñ1‚Äñ = 1`),
e.g., a normed division ring, then every `x : F` is the root of a monic quadratic polynomial
with real coefficients. -/
lemma exists_isMonicOfDegree_two_and_aeval_eq_zero (x : F) :
    ‚àÉ p : ‚Ñù[X], IsMonicOfDegree p 2 ‚àß aeval x p = 0 := by
  -- take the minimizer of `‚ÄñœÜ x ¬∑‚Äñ` ...
  obtain ‚ü®z, h‚ü© := exists_isMinOn_norm_œÜ x
  -- ... and show that the minimum is zero.
  suffices œÜ x z = 0 from ‚ü®_, isMonicOfDegree_sub_add_two z.1 z.2, by rwa [aeval_eq_œÜ]‚ü©
  by_contra! H
  set M := ‚ÄñœÜ x z‚Äñ
  -- use that `‚ÄñœÜ x ¬∑‚Äñ` is constant *and* is unbounded to produce a contradiction.
  have h' (r : ‚Ñù) : ‚àöM ‚â§ ‚Äñx - algebraMap ‚Ñù F r‚Äñ := by
    rw [‚Üê sq_le_sq‚ÇÄ M.sqrt_nonneg (norm_nonneg _), Real.sq_sqrt (norm_nonneg _), ‚Üê norm_pow,
      Commute.sub_sq <| algebraMap_eq_smul_one (A := F) r ‚ñ∏ commute_algebraMap_right r x]
    convert isMinOn_univ_iff.mp h (2 * r, r ^ 2) using 4 <;>
      simp [two_mul, add_mul, ‚Üê commutes, smul_def, mul_add]
  have := tendsto_norm_atTop_iff_cobounded.mpr <| tendsto_œÜ_cobounded (by positivity) h'
  simp only [norm_œÜ_eq_norm_œÜ_of_isMinOn h (norm_ne_zero_iff.mpr H)] at this
  exact Filter.not_tendsto_const_atTop _ _ this

/-- A version of the **Gelfand-Mazur Theorem** over `‚Ñù`.

If a field `F` is a normed `‚Ñù`-algebra, then `F` is isomorphic as an `‚Ñù`-algebra
either to `‚Ñù` or to `‚ÑÇ`. -/
theorem nonempty_algEquiv_or (F : Type*) [NormedField F] [NormedAlgebra ‚Ñù F] :
    Nonempty (F ‚âÉ‚Çê[‚Ñù] ‚Ñù) ‚à® Nonempty (F ‚âÉ‚Çê[‚Ñù] ‚ÑÇ) := by
  have : Algebra.IsAlgebraic ‚Ñù F := by
    refine ‚ü®fun x ‚Ü¶ ?_‚ü©
    obtain ‚ü®f, hf, hfx‚ü© := exists_isMonicOfDegree_two_and_aeval_eq_zero x
    exact ‚ü®f, hf.ne_zero, hfx‚ü©
  exact _root_.Real.nonempty_algEquiv_or F

end Real

end NormedAlgebra
