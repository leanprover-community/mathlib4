/-
Copyright (c) 2020 Ruben Van de Velde. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ruben Van de Velde
-/
module

public import Mathlib.Analysis.RCLike.Extend
public import Mathlib.Analysis.Normed.Operator.Basic

/-!
# Norm properties of the extension of continuous `â„`-linear functionals to `ğ•œ`-linear functionals

This file shows that `StrongDual.extendRCLike` preserves the norm of the functional.
-/

public section

open RCLike ContinuousLinearMap
open scoped ComplexConjugate

namespace StrongDual


variable {ğ•œ F : Type*} [RCLike ğ•œ] [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]
variable [NormedSpace â„ F] [IsScalarTower â„ ğ•œ F]

/-- The norm of the extension is bounded by `â€–frâ€–`. -/
theorem norm_extendRCLike_bound (fr : StrongDual â„ F) (x : F) :
    â€–(fr.extendRCLike x : ğ•œ)â€– â‰¤ â€–frâ€– * â€–xâ€– := by
  set lm : StrongDual ğ•œ F := fr.extendRCLike
  by_cases h : lm x = 0
  Â· rw [h, norm_zero]
    positivity
  rw [â† mul_le_mul_iff_rightâ‚€ (norm_pos_iff.2 h), â† sq]
  calc
    â€–lm xâ€– ^ 2 = fr (conj (lm x : ğ•œ) â€¢ x) := Module.Dual.norm_extendRCLike_apply_sq fr.toLinearMap x
    _ â‰¤ â€–fr (conj (lm x : ğ•œ) â€¢ x)â€– := le_abs_self _
    _ â‰¤ â€–frâ€– * â€–conj (lm x : ğ•œ) â€¢ xâ€– := le_opNorm _ _
    _ = â€–(lm x : ğ•œ)â€– * (â€–frâ€– * â€–xâ€–) := by rw [norm_smul, norm_conj, mul_left_comm]

@[simp]
theorem norm_extendRCLike (fr : StrongDual â„ F) : â€–(fr.extendRCLike : StrongDual ğ•œ F)â€– = â€–frâ€– :=
  le_antisymm (ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg _) fr.norm_extendRCLike_bound) <|
    opNorm_le_bound _ (norm_nonneg _) fun x =>
      calc
        â€–fr xâ€– = â€–re (fr.extendRCLike x : ğ•œ)â€– := by simp
        _ â‰¤ â€–(fr.extendRCLike x : ğ•œ)â€– := abs_re_le_norm _
        _ â‰¤ â€–(fr.extendRCLike : StrongDual ğ•œ F)â€– * â€–xâ€– := le_opNorm _ _

/-- `StrongDual.extendRCLike` bundled into a linear isometry equivalence. -/
noncomputable def extendRCLikeâ‚—áµ¢ : StrongDual â„ F â‰ƒâ‚—áµ¢[â„] StrongDual ğ•œ F where
  toLinearEquiv := StrongDual.extendRCLikeâ‚—
  norm_map' := norm_extendRCLike

end StrongDual

namespace ContinuousLinearMap
open StrongDual

@[deprecated (since := "2026-01-28")] alias norm_extendToğ•œ'_bound := norm_extendRCLike_bound
@[deprecated (since := "2026-01-28")] alias norm_extendToğ•œ' := norm_extendRCLike
@[deprecated (since := "2026-01-28")] alias norm_extendToğ•œ := norm_extendRCLike

end ContinuousLinearMap
