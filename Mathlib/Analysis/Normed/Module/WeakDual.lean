/-
Copyright (c) 2021 Kalle KytÃ¶lÃ¤. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kalle KytÃ¶lÃ¤, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Normed.Module.Dual
public import Mathlib.Analysis.Normed.Operator.Completeness
public import Mathlib.Topology.Algebra.Module.WeakDual
public import Mathlib.Topology.MetricSpace.PiNat

/-!
# Weak dual of normed space

Let `E` be a normed space over a field `ğ•œ`. This file is concerned with properties of the weak-*
topology on the dual of `E`. By the dual, we mean either of the type synonyms
`StrongDual ğ•œ E` or `WeakDual ğ•œ E`, depending on whether it is viewed as equipped with its usual
operator norm topology or the weak-* topology.

It is shown that the canonical mapping `StrongDual ğ•œ E â†’ WeakDual ğ•œ E` is continuous, and
as a consequence the weak-* topology is coarser than the topology obtained from the operator norm
(dual norm).

In this file, we also establish the Banach-Alaoglu theorem about the compactness of closed balls
in the dual of `E` (as well as sets of somewhat more general form) with respect to the weak-*
topology.

## Main definitions

The main definitions concern the canonical mapping `StrongDual ğ•œ E â†’ WeakDual ğ•œ E`.

* `StrongDual.toWeakDual` and `WeakDual.toStrongDual`: Linear equivalences from `StrongDual ğ•œ E` to
`WeakDual ğ•œ E` and in the converse direction.
* `NormedSpace.Dual.continuousLinearMapToWeakDual`: A continuous linear mapping from
  `StrongDual ğ•œ E` to `WeakDual ğ•œ E` (same as `StrongDual.toWeakDual` but different bundled data).

## Main results

The first main result concerns the comparison of the operator norm topology on `StrongDual ğ•œ E` and
the weak-* topology on (its type synonym) `WeakDual ğ•œ E`:
* `dual_norm_topology_le_weak_dual_topology`: The weak-* topology on the dual of a normed space is
  coarser (not necessarily strictly) than the operator norm topology.
* `WeakDual.isCompact_polar` (a version of the Banach-Alaoglu theorem): The polar set of a
  neighborhood of the origin in a normed space `E` over `ğ•œ` is compact in `WeakDual _ E`, if the
  nontrivially normed field `ğ•œ` is proper as a topological space.
* `WeakDual.isCompact_closedBall` (the most common special case of the Banach-Alaoglu theorem):
  Closed balls in the dual of a normed space `E` over `â„` or `â„‚` are compact in the weak-star
  topology.

## TODO
* Add that in finite dimensions, the weak-* topology and the dual norm topology coincide.
* Add that in infinite dimensions, the weak-* topology is strictly coarser than the dual norm
  topology.
* Add metrizability of the dual unit ball (more generally weak-star compact subsets) of
  `WeakDual ğ•œ E` under the assumption of separability of `E`.
* Add the sequential Banach-Alaoglu theorem: the dual unit ball of a separable normed space `E`
  is sequentially compact in the weak-star topology. This would follow from the metrizability above.

## Implementation notes

Weak-* topology is defined generally in the file `Mathlib/Topology/Algebra/Module/WeakDual.lean`.

When `M` is a vector space, the duals `StrongDual ğ•œ M` and `WeakDual ğ•œ M` are type synonyms with
different topology instances.

For the proof of Banach-Alaoglu theorem, the weak dual of `E` is embedded in the space of
functions `E â†’ ğ•œ` with the topology of pointwise convergence.

The polar set `polar ğ•œ s` of a subset `s` of `E` is originally defined as a subset of the dual
`StrongDual ğ•œ E`. We care about properties of these w.r.t. weak-* topology, and for this purpose
give the definition `WeakDual.polar ğ•œ s` for the "same" subset viewed as a subset of `WeakDual ğ•œ E`
(a type synonym of the dual but with a different topology instance).

## References

* https://en.wikipedia.org/wiki/Weak_topology#Weak-*_topology
* https://en.wikipedia.org/wiki/Banach%E2%80%93Alaoglu_theorem

## Tags

weak-star, weak dual

-/

@[expose] public section


noncomputable section

open Filter Function Bornology Metric Set

open Topology Filter

namespace StrongDual

section

variable {R : Type*} [CommSemiring R] [TopologicalSpace R] [ContinuousAdd R]
  [ContinuousConstSMul R R]
variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] [Module R M]

/-- For vector spaces `M`, there is a canonical map `StrongDual R M â†’ WeakDual R M` (the "identity"
mapping). It is a linear equivalence. -/
def toWeakDual : StrongDual R M â‰ƒâ‚—[R] WeakDual R M :=
  LinearEquiv.refl R (StrongDual R M)

@[deprecated (since := "2025-08-3")] alias _root_.NormedSpace.Dual.toWeakDual := toWeakDual

@[simp]
theorem coe_toWeakDual (x' : StrongDual R M) : toWeakDual x' = x' :=
  rfl

@[deprecated (since := "2025-08-3")] alias _root_.NormedSpace.Dual.coe_toWeakDual := coe_toWeakDual

@[simp]
theorem toWeakDual_inj (x' y' : StrongDual R M) : toWeakDual x' = toWeakDual y' â†” x' = y' :=
  (LinearEquiv.injective toWeakDual).eq_iff

@[deprecated (since := "2025-08-3")] alias _root_.NormedSpace.Dual.toWeakDual_inj := toWeakDual_inj

end

end StrongDual

namespace WeakDual

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [AddCommMonoid E] [TopologicalSpace E] [Module ğ•œ E]

/-- For vector spaces `E`, there is a canonical map `WeakDual ğ•œ E â†’ StrongDual ğ•œ E` (the "identity"
mapping). It is a linear equivalence. Here it is implemented as the inverse of the linear
equivalence `StrongDual.toWeakDual` in the other direction. -/
def toStrongDual : WeakDual ğ•œ E â‰ƒâ‚—[ğ•œ] StrongDual ğ•œ E :=
  StrongDual.toWeakDual.symm

@[deprecated (since := "2025-08-03")] alias toNormedDual := toStrongDual

theorem toStrongDual_apply (x : WeakDual ğ•œ E) (y : E) : (toStrongDual x) y = x y :=
  rfl

@[deprecated (since := "2025-08-03")] alias toNormedDual_apply := toStrongDual_apply

@[simp]
theorem coe_toStrongDual (x' : WeakDual ğ•œ E) : toStrongDual x' = x' :=
  rfl

@[deprecated (since := "2025-08-03")] alias coe_toNormedDual := coe_toStrongDual

@[simp]
theorem toStrongDual_inj (x' y' : WeakDual ğ•œ E) : toStrongDual x' = toStrongDual y' â†” x' = y' :=
  (LinearEquiv.injective toStrongDual).eq_iff

@[deprecated (since := "2025-08-03")] alias toNormedDual_inj := toStrongDual_inj

variable (ğ•œ)

/-- The polar set `polar ğ•œ s` of `s : Set E` seen as a subset of the dual of `E` with the
weak-star topology is `WeakDual.polar ğ•œ s`. -/
def polar (s : Set E) : Set (WeakDual ğ•œ E) :=
  toStrongDual â»Â¹' (StrongDual.polar ğ•œ) s

theorem polar_def (s : Set E) : polar ğ•œ s = { f : WeakDual ğ•œ E | âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ 1 } :=
  rfl

/-- The polar `polar ğ•œ s` of a set `s : E` is a closed subset when the weak star topology
is used. -/
theorem isClosed_polar (s : Set E) : IsClosed (polar ğ•œ s) := by
  simp only [polar_def, setOf_forall]
  exact isClosed_biInter fun x hx => isClosed_Iic.preimage (WeakBilin.eval_continuous _ _).norm

end WeakDual

/-!
### Weak star topology on duals of normed spaces

In this section, we prove properties about the weak-* topology on duals of normed spaces.
We prove in particular that the canonical mapping `StrongDual ğ•œ E â†’ WeakDual ğ•œ E` is continuous,
i.e., that the weak-* topology is coarser (not necessarily strictly) than the topology given
by the dual-norm (i.e. the operator-norm).
-/

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

namespace NormedSpace

namespace Dual

theorem toWeakDual_continuous : Continuous fun x' : StrongDual ğ•œ E => StrongDual.toWeakDual x' :=
  WeakBilin.continuous_of_continuous_eval _ fun z => (inclusionInDoubleDual ğ•œ E z).continuous

/-- For a normed space `E`, according to `toWeakDual_continuous` the "identity mapping"
`StrongDual ğ•œ E â†’ WeakDual ğ•œ E` is continuous. This definition implements it as a continuous linear
map. -/
def continuousLinearMapToWeakDual : StrongDual ğ•œ E â†’L[ğ•œ] WeakDual ğ•œ E :=
  { StrongDual.toWeakDual with cont := toWeakDual_continuous }

/-- The weak-star topology is coarser than the dual-norm topology. -/
theorem dual_norm_topology_le_weak_dual_topology :
    (UniformSpace.toTopologicalSpace : TopologicalSpace (StrongDual ğ•œ E)) â‰¤
      (instTopologicalSpaceWeakDual .. : TopologicalSpace (WeakDual ğ•œ E)) := by
  convert (@toWeakDual_continuous _ _ _ _ (by assumption)).le_induced
  exact induced_id.symm

end Dual

end NormedSpace

namespace WeakDual

open NormedSpace

theorem isClosed_closedBall (x' : StrongDual ğ•œ E) (r : â„) :
    IsClosed (toStrongDual â»Â¹' closedBall x' r) :=
  isClosed_induced_iff'.2 (ContinuousLinearMap.is_weak_closed_closedBall x' r)

/-!
### Polar sets in the weak dual space
-/

/-- While the coercion `â†‘ : WeakDual ğ•œ E â†’ (E â†’ ğ•œ)` is not a closed map, it sends *bounded*
closed sets to closed sets. -/
theorem isClosed_image_coe_of_bounded_of_closed {s : Set (WeakDual ğ•œ E)}
    (hb : IsBounded (StrongDual.toWeakDual â»Â¹' s)) (hc : IsClosed s) :
    IsClosed (((â†‘) : WeakDual ğ•œ E â†’ E â†’ ğ•œ) '' s) :=
  ContinuousLinearMap.isClosed_image_coe_of_bounded_of_weak_closed hb (isClosed_induced_iff'.1 hc)

theorem isCompact_of_bounded_of_closed [ProperSpace ğ•œ] {s : Set (WeakDual ğ•œ E)}
    (hb : IsBounded (StrongDual.toWeakDual â»Â¹' s)) (hc : IsClosed s) : IsCompact s :=
  DFunLike.coe_injective.isEmbedding_induced.isCompact_iff.mpr <|
    ContinuousLinearMap.isCompact_image_coe_of_bounded_of_closed_image hb <|
      isClosed_image_coe_of_bounded_of_closed hb hc

variable (ğ•œ)

/-- The image under `â†‘ : WeakDual ğ•œ E â†’ (E â†’ ğ•œ)` of a polar `WeakDual.polar ğ•œ s` of a
neighborhood `s` of the origin is a closed set. -/
theorem isClosed_image_polar_of_mem_nhds {s : Set E} (s_nhds : s âˆˆ ğ“ (0 : E)) :
    IsClosed (((â†‘) : WeakDual ğ•œ E â†’ E â†’ ğ•œ) '' polar ğ•œ s) :=
  isClosed_image_coe_of_bounded_of_closed (isBounded_polar_of_mem_nhds_zero ğ•œ s_nhds)
    (isClosed_polar _ _)

/-- The image under `â†‘ : StrongDual ğ•œ E â†’ (E â†’ ğ•œ)` of a polar `polar ğ•œ s` of a
neighborhood `s` of the origin is a closed set. -/
theorem _root_.NormedSpace.Dual.isClosed_image_polar_of_mem_nhds {s : Set E}
    (s_nhds : s âˆˆ ğ“ (0 : E)) :
    IsClosed (((â†‘) : StrongDual ğ•œ E â†’ E â†’ ğ•œ) '' StrongDual.polar ğ•œ s) :=
  WeakDual.isClosed_image_polar_of_mem_nhds ğ•œ s_nhds

/-- The **Banach-Alaoglu theorem**: the polar set of a neighborhood `s` of the origin in a
normed space `E` is a compact subset of `WeakDual ğ•œ E`. -/
theorem isCompact_polar [ProperSpace ğ•œ] {s : Set E} (s_nhds : s âˆˆ ğ“ (0 : E)) :
    IsCompact (polar ğ•œ s) :=
  isCompact_of_bounded_of_closed (isBounded_polar_of_mem_nhds_zero ğ•œ s_nhds) (isClosed_polar _ _)

/-- The **Banach-Alaoglu theorem**: closed balls of the dual of a normed space `E` are compact in
the weak-star topology. -/
theorem isCompact_closedBall [ProperSpace ğ•œ] (x' : StrongDual ğ•œ E) (r : â„) :
    IsCompact (toStrongDual â»Â¹' closedBall x' r) :=
  isCompact_of_bounded_of_closed isBounded_closedBall (isClosed_closedBall x' r)

open TopologicalSpace

variable (ğ•œ V : Type*) [NontriviallyNormedField ğ•œ] [SeminormedAddCommGroup V] [NormedSpace ğ•œ V]
variable [TopologicalSpace.SeparableSpace V] (K : Set (WeakDual ğ•œ V))

/-- In a separable normed space, there exists a sequence of continuous functions that
separates points of the weak dual. -/
lemma exists_countable_separating : âˆƒ (gs : â„• â†’ (WeakDual ğ•œ V) â†’ ğ•œ),
    (âˆ€ n, Continuous (gs n)) âˆ§ (âˆ€ â¦ƒx yâ¦„, x â‰  y â†’ âˆƒ n, gs n x â‰  gs n y) := by
  use (fun n Ï† â†¦ Ï† (denseSeq V n))
  constructor
  Â· exact fun _ â†¦ eval_continuous _
  Â· intro w y w_ne_y
    contrapose! w_ne_y
    exact DFunLike.ext'_iff.mpr <| (map_continuous w).ext_on
      (denseRange_denseSeq V) (map_continuous y) (by grind [Set.eqOn_range])

/-- A compact subset of the dual space of a separable space is metrizable. -/
lemma metrizable_of_isCompact (K_cpt : IsCompact K) : TopologicalSpace.MetrizableSpace K := by
  have : CompactSpace K := isCompact_iff_compactSpace.mp K_cpt
  obtain âŸ¨gs, gs_cont, gs_sepâŸ© := exists_countable_separating ğ•œ V
  exact Metric.PiNatEmbed.TopologicalSpace.MetrizableSpace.of_countable_separating
    (fun n k â†¦ gs n k) (fun n â†¦ (gs_cont n).comp continuous_subtype_val)
    fun x y hxy â†¦ gs_sep <| Subtype.val_injective.ne hxy

variable [ProperSpace ğ•œ] (K_cpt : IsCompact K)

theorem isSeqCompact_of_isBounded_of_isClosed {s : Set (WeakDual ğ•œ V)}
    (hb : Bornology.IsBounded (StrongDual.toWeakDual â»Â¹' s)) (hc : IsClosed s) :
    IsSeqCompact s := by
  have b_isCompact' : CompactSpace s :=
    isCompact_iff_compactSpace.mp <| isCompact_of_bounded_of_closed hb hc
  have b_isMetrizable : TopologicalSpace.MetrizableSpace s :=
    metrizable_of_isCompact ğ•œ V s <| isCompact_of_bounded_of_closed hb hc
  have seq_cont_phi : SeqContinuous (fun Ï† : s â†¦ (Ï† : WeakDual ğ•œ V)) :=
    continuous_iff_seqContinuous.mp continuous_subtype_val
  simpa using IsSeqCompact.range seq_cont_phi

/-- The **Sequential Banach-Alaoglu theorem**: the polar set of a neighborhood `s` of the origin in
a separable normed space `V` is a sequentially compact subset of `WeakDual ğ•œ V`. -/
theorem isSeqCompact_polar {s : Set V} (s_nhd : s âˆˆ ğ“ (0 : V)) :
    IsSeqCompact (polar ğ•œ s) :=
  isSeqCompact_of_isBounded_of_isClosed (s := polar ğ•œ s) _ _
    (NormedSpace.isBounded_polar_of_mem_nhds_zero ğ•œ s_nhd) (isClosed_polar _ _)

/-- The **Sequential Banach-Alaoglu theorem**: closed balls of the dual of a separable
normed space `V` are sequentially compact in the weak-* topology. -/
theorem isSeqCompact_closedBall (x' : StrongDual ğ•œ V) (r : â„) :
    IsSeqCompact (toStrongDual â»Â¹' Metric.closedBall x' r) :=
  isSeqCompact_of_isBounded_of_isClosed ğ•œ V Metric.isBounded_closedBall
    (isClosed_closedBall x' r)

end WeakDual
