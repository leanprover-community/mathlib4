/-
Copyright (c) 2023 MichaÅ‚ ÅšwiÄ™tek. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: MichaÅ‚ ÅšwiÄ™tek
-/
module

public import Mathlib.Analysis.Normed.Module.WeakDual
public import Mathlib.Analysis.Normed.Operator.BanachSteinhaus
public import Mathlib.Tactic

@[expose] public section

noncomputable section

open Filter Topology LinearMap Set

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {X : Type*} [NormedAddCommGroup X] [NormedSpace ğ•œ X]

/-- A Schauder basis is a sequence (e n) of vectors in X such that there exists a sequence of
    continuous linear functionals (f n) (the coordinate functionals) satisfying:
    1) f i (e j) = Î´_{ij}
    2) for every x : X, the series âˆ‘_{n=0}^âˆ f n (x) e n converges to x.

    In other words, every vector in X can be uniquely represented as a convergent series of basis
    vectors, with coefficients given by the coordinate functionals. -/
structure SchauderBasis (ğ•œ : Type*) (X : Type*) [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup X] [NormedSpace ğ•œ X] (e : â„• â†’ X) where
  coord : â„• â†’ StrongDual ğ•œ X
  -- Biorthogonality
  ortho : âˆ€ i j, coord i (e j) = if i = j then 1 else 0
  -- Convergence of partial sums
  basis_expansion : âˆ€ x : X, Tendsto (fun n â†¦ âˆ‘ i âˆˆ Finset.range n, coord i x â€¢ e i)
    atTop (ğ“ x)

namespace SchauderBasis

variable {e : â„• â†’ X} (b : SchauderBasis ğ•œ X e)

/-- The basis vectors are linearly independent. -/
theorem linearIndependent (h : SchauderBasis ğ•œ X e) : LinearIndependent ğ•œ e := by
  rw [linearIndependent_iff]
  intro l hl
  ext i
  have hsum : âˆ‘ i âˆˆ l.support, l i â€¢ e i = 0 := hl
  -- Apply the i-th coordinate functional to the linear combination
  have happ : h.coord i (âˆ‘ j âˆˆ l.support, l j â€¢ e j) = 0 := by rw [hsum, map_zero]
  rw [map_sum] at happ
  simp_rw [ContinuousLinearMap.map_smul] at happ
  rw [Finset.sum_eq_single i, h.ortho i i] at happ
  Â· simpa using happ
  Â· intro j _ hji; rw [h.ortho i j, if_neg hji.symm]; simp only [smul_eq_mul, mul_zero]
  Â· intro hi; simp only [Finsupp.notMem_support_iff.mp hi, smul_eq_mul, zero_mul]

/-- A canonical projection P_n associated to a Schauder basis given by coordinate functionals f_i:
    P_n x = âˆ‘_{i < n} f_i(x) e_i -/
def proj (n : â„•) : X â†’L[ğ•œ] X := âˆ‘ i âˆˆ Finset.range n, (b.coord i).smulRight (e i)

/-- The action of the canonical projection on a vector x. -/
theorem proj_apply (n : â„•) (x : X) : b.proj n x = âˆ‘ i âˆˆ Finset.range n, b.coord i x â€¢ e i := by
  simp only [proj, ContinuousLinearMap.sum_apply, ContinuousLinearMap.smulRight_apply]

/-- The action of the canonical projection on a basis element e i. -/
@[simp]
theorem proj_basis_element (n i : â„•) : b.proj n (e i) = if i < n then e i else 0 := by
  rw [proj_apply]
  by_cases hin : i < n
  Â· rw [Finset.sum_eq_single_of_mem i (Finset.mem_range.mpr hin)]
    Â· simp only [b.ortho, â†“reduceIte, one_smul, if_pos hin]
    Â· intro j _ hji; rw [b.ortho j i, if_neg hji, zero_smul]
  rw [if_neg hin, Finset.sum_eq_zero]
  intro j hj
  push_neg at hin
  rw [b.ortho j i, if_neg, zero_smul]
  exact (Finset.mem_range.mp hj).trans_le hin |>.ne

/-- The range of the canonical projection is the span of the first n basis elements. -/
theorem range_proj (n : â„•) :
    LinearMap.range (b.proj n) = Submodule.span ğ•œ (Set.range (fun i : Fin n => e i)) := by
  apply le_antisymm
  Â· rintro _ âŸ¨x, rflâŸ©
    rw [proj_apply b]
    apply Submodule.sum_mem
    intros i hi
    apply Submodule.smul_mem
    apply Submodule.subset_span
    exact âŸ¨âŸ¨i, Finset.mem_range.mp hiâŸ©, rflâŸ©
  Â· rw [Submodule.span_le]
    rintro _ âŸ¨i, rflâŸ©
    use e i
    rw [proj_basis_element , if_pos i.is_lt]

/-- The dimension of the range of the canonical projection P n is n. -/
theorem dim_range_proj (n : â„•) : Module.finrank ğ•œ (LinearMap.range (b.proj n)) = n := by
  rw [range_proj, finrank_span_eq_card]
  Â· exact Fintype.card_fin n
  Â· exact b.linearIndependent.comp (fun (i : Fin n) => (i : â„•)) Fin.val_injective

/-- The canonical projections converge pointwise to the identity map. -/
theorem proj_tendsto_id (x : X) : Tendsto (fun n â†¦ b.proj n x) atTop (ğ“ x) := by
  simp_rw [proj_apply, b.basis_expansion x]

/-- The canonical projections are uniformly bounded (Banach-Steinhaus). -/
theorem proj_uniform_bound [CompleteSpace X] : âˆƒ C : â„, âˆ€ n : â„•, â€–b.proj nâ€– â‰¤ C := by
  apply banach_steinhaus
  intro x
  let f: â„• â†’ X := fun n => b.proj n x
  have : âˆƒ M : â„, âˆ€ x âˆˆ Set.range f, â€–xâ€– â‰¤ M :=
      isBounded_iff_forall_norm_le.mp (Metric.isBounded_range_of_tendsto f (proj_tendsto_id b x ))
  rcases this with âŸ¨M, hMâŸ©
  rw [Set.forall_mem_range] at hM
  use M

/-- The basis constant is the infimum of the bounds on the canonical projections. -/
def basis_constant : â„ := sInf { C : â„ | âˆ€ n : â„•, â€–b.proj nâ€– â‰¤ C }

/-- Q_n = P_{n+1} - P_n. -/
def Q (P : â„• â†’ X â†’L[ğ•œ] X) (n : â„•) : X â†’L[ğ•œ] X := P (n + 1) - P n

/-- The sum of Q i over i < n equals P n. -/
@[simp]
lemma Q_sum (P : â„• â†’ X â†’L[ğ•œ] X) (h0 : P 0 = 0) (n : â„•) : âˆ‘ i âˆˆ Finset.range n, Q P i = P n := by
  induction n with
  | zero => simp [h0]
  | succ n ih => rw [Finset.sum_range_succ, ih, Q]; abel

/-- The operators Q i are orthogonal projections. -/
lemma Q_ortho {P : â„• â†’ X â†’L[ğ•œ] X} (hcomp : âˆ€ n m, âˆ€ x : X, P n (P m x) = P (min n m) x)
    (i j : â„•) (x : X) : (Q P i) (Q P j x) = if i = j then Q P j x else 0 := by
  simp only [Q, ContinuousLinearMap.sub_apply, map_sub, hcomp, Nat.add_min_add_right]
  split_ifs with h
  Â· rw [h, min_self, min_eq_right (Nat.le_succ j), Nat.min_eq_left (Nat.le_succ j)]
    abel
  Â· rcases Nat.lt_or_gt_of_ne h with h' | h'
    Â· rw [min_eq_left_of_lt h', min_eq_left (Nat.succ_le_of_lt h'),
        min_eq_left_of_lt (Nat.lt_succ_of_lt h')]
      abel
    Â· rw [min_eq_right_of_lt h', min_eq_right (Nat.succ_le_of_lt h'),
        min_eq_right_of_lt (Nat.lt_succ_of_lt h')]
      abel

/-- The rank of Q n is 1. -/
lemma Q_rank_one {P : â„• â†’ X â†’L[ğ•œ] X}
    (h0 : P 0 = 0)
    (hrank : âˆ€ n, Module.finrank ğ•œ (LinearMap.range (P n)) = n)
    (hcomp : âˆ€ n m, âˆ€ x : X, P n (P m x) = P (min n m) x) (n : â„•) :
    Module.finrank ğ•œ (LinearMap.range (Q P n)) = 1 := by
  let Q := Q P
  let U := LinearMap.range (Q n)
  let V := LinearMap.range (P n)
  have h_range_Pn_succ : LinearMap.range (P (n + 1)) = U âŠ” V := by
    apply le_antisymm
    Â· rintro x âŸ¨y, rflâŸ©; rw [â† sub_add_cancel (P (n + 1) y) (P n y)]
      exact Submodule.add_mem_sup (LinearMap.mem_range_self _ _) (LinearMap.mem_range_self _ _)
    Â· rw [sup_le_iff]
      have hV: âˆ€ y : X, P n y âˆˆ LinearMap.range (P (n + 1)) := by
        intro y
        use P n y
        rw [hcomp (n+1) n y, min_eq_right (Nat.le_succ n)]
      constructor
      Â· rintro x âŸ¨y, rflâŸ©
        apply Submodule.sub_mem _ (LinearMap.mem_range_self _ _)
        dsimp only [ContinuousLinearMap.coe_coe]
        exact hV y
      Â· rintro x âŸ¨y, rflâŸ©
        exact hV y
  have h_disjoint : U âŠ“ V = âŠ¥ := by
    rw [Submodule.eq_bot_iff]
    rintro x âŸ¨âŸ¨y, rflâŸ©, âŸ¨z, hzâŸ©âŸ©
    -- have : Q P n (P n z) = 0 := by simp [Q, h_comm, Nat.min_succ_self, min_self]
    have : Q n (P n z) = 0 := by
      simp_rw [Q, SchauderBasis.Q, ContinuousLinearMap.sub_apply, hcomp,
        min_eq_right (Nat.le_succ n), min_self, sub_self]
    rw [â† hz, â† this, hz, Q_ortho hcomp, if_pos rfl]
  have h_fin_Pn : âˆ€ n, FiniteDimensional ğ•œ (LinearMap.range (P n)) := by
      intro n
      by_cases hn : n = 0
      Â· rw [hn]
        apply FiniteDimensional.of_rank_eq_zero
        apply Submodule.rank_eq_zero.mpr
        exact LinearMap.range_eq_bot.mpr (by simp only [h0, ContinuousLinearMap.coe_zero])
      apply FiniteDimensional.of_finrank_pos
      rw [hrank n]
      exact Nat.pos_of_ne_zero hn
  have : FiniteDimensional ğ•œ U := by
    have : U â‰¤ LinearMap.range (P (n+1)) := by
      simp only [U, Q, SchauderBasis.Q]
      intro x âŸ¨y, hyâŸ©
      rw [â† hy, ContinuousLinearMap.sub_apply]
      apply Submodule.sub_mem _ (LinearMap.mem_range_self _ _)
      use P n y
      rw [hcomp (n+1) n y, min_eq_right (Nat.le_succ n)]
    exact Submodule.finiteDimensional_of_le this
  have : FiniteDimensional ğ•œ V := by simp only [V]; exact h_fin_Pn n
  have := Submodule.finrank_sup_add_finrank_inf_eq U V
  rw [h_disjoint, finrank_bot, add_zero, â† h_range_Pn_succ, hrank, hrank, Nat.add_comm] at this
  exact Nat.add_right_cancel this.symm

/-- Constructs a Schauder basis from a sequence of projections. -/
theorem basis_of_canonical_projections {P : â„• â†’ X â†’L[ğ•œ] X} (h0 : P 0 = 0)
    (hdim : âˆ€ n, Module.finrank ğ•œ (LinearMap.range (P n)) = n)
    (hcomp : âˆ€ n m, âˆ€ x : X, P n (P m x) = P (min n m) x)
    (hlim : âˆ€ x, Tendsto (fun n â†¦ P n x) atTop (ğ“ x)) :
    âˆƒ e : â„• â†’ X, Nonempty (SchauderBasis ğ•œ X e) := by
  let Q := Q P
  have hrankQ := Q_rank_one h0 hdim hcomp
  have : âˆ€ n, âˆƒ v, v âˆˆ LinearMap.range (Q n) âˆ§ v â‰  0 := by
      intro n
      refine exists_mem_ne_zero_of_rank_pos ?_
      apply Module.lt_rank_of_lt_finrank
      rw [hrankQ n]
      exact Nat.zero_lt_one
  choose e he_in_range he_ne using this
  have h_range_eq_span : âˆ€ n, LinearMap.range (Q n) = Submodule.span ğ•œ {e n} := by
    intro n
    symm
    have : FiniteDimensional ğ•œ â†¥(LinearMap.range (Q n)) := by
      apply FiniteDimensional.of_finrank_pos
      rw [hrankQ n]
      exact Nat.succ_pos 0
    apply Submodule.eq_of_le_of_finrank_eq
    Â· rw [Submodule.span_le, Set.singleton_subset_iff]
      exact he_in_range n
    Â· rw [hrankQ n, finrank_span_singleton (he_ne n)]
  let f_fun : â„• â†’ X â†’ ğ•œ := fun n x =>
    Classical.choose (Submodule.mem_span_singleton.mp (by
      rw [â† h_range_eq_span]
      exact LinearMap.mem_range_self (Q n) x))
  have hQf : âˆ€ n x, Q n x = f_fun n x â€¢ e n := fun n x =>
    (Classical.choose_spec (Submodule.mem_span_singleton.mp (by
      rw [â† h_range_eq_span]
      exact LinearMap.mem_range_self (Q n) x))).symm
  let f (n : â„•) : StrongDual ğ•œ X := LinearMap.mkContinuous (IsLinearMap.mk' (f_fun n) (by
    constructor
    Â· intro x y; apply smul_left_injective ğ•œ (he_ne n); dsimp only [smul_eq_mul];
      rw [â† hQf, map_add, add_smul, hQf, hQf]
    Â· intro c x; apply smul_left_injective ğ•œ (he_ne n);dsimp  only [smul_eq_mul];
      rw [â† hQf, map_smul, mul_smul, hQf]
    )) (â€–Q nâ€– / â€–e nâ€–) (by
      intro x; rw [div_mul_eq_mul_div, le_div_iffâ‚€ (norm_pos_iff.mpr (he_ne n))]
      calc â€–f_fun n xâ€– * â€–e nâ€– = â€–f_fun n x â€¢ e nâ€– := (norm_smul _ _).symm
        _ = â€–Q n xâ€– := by rw [hQf]
        _ â‰¤ â€–Q nâ€– * â€–xâ€– := ContinuousLinearMap.le_opNorm _ _)
  have ortho : âˆ€ i j, f i (e j) = if i = j then 1 else 0 := by
    intro i j
    apply smul_left_injective ğ•œ (he_ne i)
    dsimp only [smul_eq_mul]
    simp only [mkContinuous_apply, IsLinearMap.mk'_apply, ite_smul, one_smul, zero_smul, f]
    have : Q i (e j) = if i = j then e j else 0 := by
      obtain âŸ¨x, hxâŸ© := he_in_range j
      rw [â† hx, Q_ortho hcomp i j x]
    rw [â† hQf, this]
    split_ifs with hij
    Â· subst hij; simp only
    Â· simp only
  have lim : âˆ€ x, Tendsto (fun n â†¦ âˆ‘ i âˆˆ Finset.range n, f i x â€¢ e i) atTop (ğ“ x) := by
    intro x
    apply Tendsto.congr _ (hlim x)
    intro n
    simp_rw [f]
    dsimp only [mkContinuous_apply, IsLinearMap.mk'_apply]
    simp_rw [â† hQf, Q]
    simp only [â† Q_sum P h0 n, ContinuousLinearMap.coe_sum', Finset.sum_apply]
  use e
  exact âŸ¨SchauderBasis.mk f ortho limâŸ©

end SchauderBasis
