/-
Copyright (c) 2024 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Analysis.Normed.Operator.ContinuousLinearMap
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.LinearAlgebra.Basis.VectorSpace

/-!
# The span of a single vector

The equivalence of `ğ•œ` and `ğ•œ â€¢ x` for `x â‰  0` are defined as continuous linear equivalence and
isometry.

## Main definitions

* `ContinuousLinearEquiv.toSpanNonzeroSingleton`: The continuous linear equivalence between `ğ•œ` and
  `ğ•œ â€¢ x` for `x â‰  0`.
* `LinearIsometryEquiv.toSpanUnitSingleton`: For `â€–xâ€– = 1` the continuous linear equivalence is a
  linear isometry equivalence.

-/

variable {ğ•œ E : Type*}

namespace LinearMap

variable (ğ•œ)

section Seminormed

variable [NormedDivisionRing ğ•œ] [SeminormedAddCommGroup E] [Module ğ•œ E] [NormSMulClass ğ•œ E]

theorem toSpanSingleton_homothety (x : E) (c : ğ•œ) :
    â€–LinearMap.toSpanSingleton ğ•œ E x câ€– = â€–xâ€– * â€–câ€– := by
  rw [mul_comm]
  exact norm_smul _ _

end Seminormed

end LinearMap

namespace ContinuousLinearEquiv

variable (ğ•œ)

section Seminormed
variable [NormedDivisionRing ğ•œ] [SeminormedAddCommGroup E] [Module ğ•œ E] [NormSMulClass ğ•œ E]

theorem _root_.LinearEquiv.toSpanNonzeroSingleton_homothety (x : E) (h : x â‰  0) (c : ğ•œ) :
    â€–LinearEquiv.toSpanNonzeroSingleton ğ•œ E x h câ€– = â€–xâ€– * â€–câ€– :=
  LinearMap.toSpanSingleton_homothety _ _ _

end Seminormed

section Normed
variable [NormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- Given a nonzero element `x` of a normed space `Eâ‚` over a field `ğ•œ`, the natural
continuous linear equivalence from `ğ•œ` to the span of `x`. -/
@[simps!]
noncomputable def toSpanNonzeroSingleton (x : E) (h : x â‰  0) : ğ•œ â‰ƒL[ğ•œ] ğ•œ âˆ™ x :=
  ofHomothety (LinearEquiv.toSpanNonzeroSingleton ğ•œ E x h) â€–xâ€– (norm_pos_iff.mpr h)
    (LinearEquiv.toSpanNonzeroSingleton_homothety ğ•œ x h)

/-- Given a nonzero element `x` of a normed space `Eâ‚` over a field `ğ•œ`, the natural continuous
linear map from the span of `x` to `ğ•œ`. -/
noncomputable def coord (x : E) (h : x â‰  0) : StrongDual ğ•œ (ğ•œ âˆ™ x) :=
  (toSpanNonzeroSingleton ğ•œ x h).symm

@[simp]
theorem coe_toSpanNonzeroSingleton_symm {x : E} (h : x â‰  0) :
    â‡‘(toSpanNonzeroSingleton ğ•œ x h).symm = coord ğ•œ x h :=
  rfl

@[simp]
theorem coord_toSpanNonzeroSingleton {x : E} (h : x â‰  0) (c : ğ•œ) :
    coord ğ•œ x h (toSpanNonzeroSingleton ğ•œ x h c) = c :=
  (toSpanNonzeroSingleton ğ•œ x h).symm_apply_apply c

@[simp]
theorem toSpanNonzeroSingleton_coord {x : E} (h : x â‰  0) (y : ğ•œ âˆ™ x) :
    toSpanNonzeroSingleton ğ•œ x h (coord ğ•œ x h y) = y :=
  (toSpanNonzeroSingleton ğ•œ x h).apply_symm_apply y

@[simp]
theorem coord_self (x : E) (h : x â‰  0) :
    (coord ğ•œ x h) (âŸ¨x, Submodule.mem_span_singleton_self xâŸ© : ğ•œ âˆ™ x) = 1 :=
  LinearEquiv.coord_self ğ•œ E x h

end Normed

end ContinuousLinearEquiv

namespace LinearIsometryEquiv

variable [NormedDivisionRing ğ•œ] [SeminormedAddCommGroup E] [Module ğ•œ E] [NormSMulClass ğ•œ E]

/-- Given a unit element `x` of a normed space `E` over a field `ğ•œ`, the natural
linear isometry equivalence from `ğ•œ` to the span of `x`. -/
noncomputable def toSpanUnitSingleton (x : E) (hx : â€–xâ€– = 1) :
    ğ•œ â‰ƒâ‚—áµ¢[ğ•œ] ğ•œ âˆ™ x where
  toLinearEquiv := LinearEquiv.toSpanNonzeroSingleton ğ•œ E x (by aesop)
  norm_map' := by
    intro
    rw [LinearEquiv.toSpanNonzeroSingleton_homothety, hx, one_mul]

@[simp] theorem toSpanUnitSingleton_apply (x : E) (hx : â€–xâ€– = 1) (r : ğ•œ) :
    toSpanUnitSingleton x hx r = (âŸ¨r â€¢ x, by aesopâŸ© : ğ•œ âˆ™ x) := rfl

end LinearIsometryEquiv
