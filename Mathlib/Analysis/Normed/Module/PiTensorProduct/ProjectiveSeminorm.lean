/-
Copyright (c) 2024 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel
-/
module

public import Mathlib.Analysis.Normed.Module.Multilinear.Basic
public import Mathlib.LinearAlgebra.PiTensorProduct

/-!
# Projective seminorm on the tensor of a finite family of normed spaces.

Let `ğ•œ` be a nontrivially normed field and `E` be a family of normed `ğ•œ`-vector spaces `Eáµ¢`,
indexed by a finite type `Î¹`. We define a seminorm on `â¨‚[ğ•œ] i, Eáµ¢`, which we call the
"projective seminorm". For `x` an element of `â¨‚[ğ•œ] i, Eáµ¢`, its projective seminorm is the
infimum over all expressions of `x` as `âˆ‘ j, â¨‚â‚œ[ğ•œ] mâ±¼ i` (with the `mâ±¼` âˆˆ `Î  i, Eáµ¢`)
of `âˆ‘ j, Î  i, â€–mâ±¼ iâ€–`.

In particular, every norm `â€–.â€–` on `â¨‚[ğ•œ] i, Eáµ¢` satisfying `â€–â¨‚â‚œ[ğ•œ] i, m iâ€– â‰¤ Î  i, â€–m iâ€–`
for every `m` in `Î  i, Eáµ¢` is bounded above by the projective seminorm.

## Main definitions

* `PiTensorProduct.projectiveSeminorm`: The projective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.liftEquiv`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as a continuous linear equivalence.
* `PiTensorProduct.liftIsometry`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as an isometric linear equivalence.
* `PiTensorProduct.tprodL`: The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢`
  to `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.mapL`: The continuous linear map from `â¨‚[ğ•œ] i, Eáµ¢` to `â¨‚[ğ•œ] i, E'áµ¢`
  induced by a family of continuous linear maps `Eáµ¢ â†’L[ğ•œ] E'áµ¢`.
* `PiTensorProduct.mapLMultilinear`: The continuous multilinear map from
  `Î áµ¢ (Eáµ¢ â†’L[ğ•œ] E'áµ¢)` to `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] (â¨‚[ğ•œ] i, E'áµ¢)` sending a family
  `f` to `PiTensorProduct.mapL f`.
* `PiTensorProduct.injectiveSeminorm`: A "dual" definition of the projective seminorm.

## Main results

* `PiTensorProduct.norm_eval_le_projectiveSeminorm`: If `f` is a continuous multilinear map on
  `E = Î  i, Eáµ¢` and `x` is in `â¨‚[ğ•œ] i, Eáµ¢`, then `â€–f.lift xâ€– â‰¤ projectiveSeminorm x * â€–fâ€–`.
* `PiTensorProduct.mapL_opNorm`: If `f` is a family of continuous linear maps
  `fáµ¢ : Eáµ¢ â†’L[ğ•œ] Fáµ¢`, then `â€–PiTensorProduct.mapL fâ€– â‰¤ âˆ i, â€–fáµ¢â€–`.
* `PiTensorProduct.mapLMultilinear_opNorm` : If `F` is a normed vecteor space, then
  `â€–mapLMultilinear ğ•œ E Fâ€– â‰¤ 1`.
* `PiTensorProduct.injectiveSeminorm_eq_projectiveSeminorm`: The dual definition
   agrees with the primal definition

## TODO

* Adapt the remaining functoriality constructions/properties from `PiTensorProduct`.
-/

@[expose] public section

open scoped TensorProduct

namespace PiTensorProduct

universe uÎ¹ uğ•œ uE uF

variable {Î¹ : Type uÎ¹} [Fintype Î¹]
variable {ğ•œ : Type uğ•œ}

variable {E : Î¹ â†’ Type uE} [âˆ€ i, SeminormedAddCommGroup (E i)]

section NormedField

variable [NormedField ğ•œ]

/-- A lift of the projective seminorm to `FreeAddMonoid (ğ•œ Ã— Î  i, Eáµ¢)`, useful to prove the
properties of `projectiveSeminorm`.
-/
def projectiveSeminormAux : FreeAddMonoid (ğ•œ Ã— Î  i, E i) â†’ â„ :=
  fun p => (p.toList.map (fun p â†¦ â€–p.1â€– * âˆ i, â€–p.2 iâ€–)).sum

theorem projectiveSeminormAux_nonneg (p : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) :
    0 â‰¤ projectiveSeminormAux p := by
  simp only [projectiveSeminormAux]
  refine List.sum_nonneg ?_
  intro a
  simp only [List.mem_map, Prod.exists, forall_exists_index,
    and_imp]
  intro x m _ h
  rw [â† h]
  exact mul_nonneg (norm_nonneg _) (Finset.prod_nonneg (fun _ _ â†¦ norm_nonneg _))

theorem projectiveSeminormAux_add_le (p q : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) :
    projectiveSeminormAux (p + q) â‰¤ projectiveSeminormAux p + projectiveSeminormAux q := by
  simp [projectiveSeminormAux]

theorem projectiveSeminormAux_smul (p : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) (a : ğ•œ) :
    projectiveSeminormAux (p.map (fun (y : ğ•œ Ã— Î  i, E i) â†¦ (a * y.1, y.2))) =
    â€–aâ€– * projectiveSeminormAux p := by
  simp [projectiveSeminormAux, Function.comp_def, mul_assoc, List.sum_map_mul_left]

variable [âˆ€ i, NormedSpace ğ•œ (E i)]

theorem bddBelow_projectiveSemiNormAux (x : â¨‚[ğ•œ] i, E i) :
    BddBelow (Set.range (fun (p : lifts x) â†¦ projectiveSeminormAux p.1)) := by
  existsi 0
  rw [mem_lowerBounds]
  simp only [Set.mem_range, Subtype.exists, exists_prop, forall_exists_index, and_imp,
    forall_apply_eq_imp_iffâ‚‚]
  exact fun p _ â†¦ projectiveSeminormAux_nonneg p

/-- The projective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`. It sends an element `x` of `â¨‚[ğ•œ] i, Eáµ¢` to the
infimum over all expressions of `x` as `âˆ‘ j, â¨‚â‚œ[ğ•œ] mâ±¼ i` (with the `mâ±¼` âˆˆ `Î  i, Eáµ¢`)
of `âˆ‘ j, Î  i, â€–mâ±¼ iâ€–`.
-/
noncomputable def projectiveSeminorm : Seminorm ğ•œ (â¨‚[ğ•œ] i, E i) := by
  refine Seminorm.ofSMulLE (fun x â†¦ iInf (fun (p : lifts x) â†¦ projectiveSeminormAux p.1)) ?_ ?_ ?_
  Â· refine le_antisymm ?_ ?_
    Â· refine ciInf_le_of_le (bddBelow_projectiveSemiNormAux (0 : â¨‚[ğ•œ] i, E i)) âŸ¨0, lifts_zeroâŸ© ?_
      rfl
    Â· letI : Nonempty (lifts 0) := âŸ¨0, lifts_zero (R := ğ•œ) (s := E)âŸ©
      exact le_ciInf (fun p â†¦ projectiveSeminormAux_nonneg p.1)
  Â· intro x y
    letI := nonempty_subtype.mpr (nonempty_lifts x); letI := nonempty_subtype.mpr (nonempty_lifts y)
    exact le_ciInf_add_ciInf (fun p q â†¦ ciInf_le_of_le (bddBelow_projectiveSemiNormAux _)
      âŸ¨p.1 + q.1, lifts_add p.2 q.2âŸ© (projectiveSeminormAux_add_le p.1 q.1))
  Â· intro a x
    letI := nonempty_subtype.mpr (nonempty_lifts x)
    rw [Real.mul_iInf_of_nonneg (norm_nonneg _)]
    refine le_ciInf ?_
    intro p
    rw [â† projectiveSeminormAux_smul]
    exact ciInf_le_of_le (bddBelow_projectiveSemiNormAux _)
      âŸ¨(p.1.map (fun y â†¦ (a * y.1, y.2))), lifts_smul p.2 aâŸ© (le_refl _)

noncomputable instance : SeminormedAddCommGroup (â¨‚[ğ•œ] i, E i) :=
  AddGroupSeminorm.toSeminormedAddCommGroup projectiveSeminorm.toAddGroupSeminorm

theorem norm_eq_projectiveSeminorm (x : â¨‚[ğ•œ] i, E i) : â€–xâ€– = projectiveSeminorm x := rfl

noncomputable instance : NormedSpace ğ•œ (â¨‚[ğ•œ] i, E i) where
  norm_smul_le a x := by
    change projectiveSeminorm.toFun (a â€¢ x) â‰¤ _
    rw [projectiveSeminorm.smul']
    rfl

theorem projectiveSeminorm_apply (x : â¨‚[ğ•œ] i, E i) :
    projectiveSeminorm x = iInf (fun (p : lifts x) â†¦ projectiveSeminormAux p.1) := rfl

theorem projectiveSeminorm_tprod_le (m : Î  i, E i) :
    â€–(â¨‚â‚œ[ğ•œ] i, m i)â€– â‰¤ âˆ i, â€–m iâ€– := by
  rw [norm_eq_projectiveSeminorm, projectiveSeminorm_apply]
  convert ciInf_le (bddBelow_projectiveSemiNormAux _) âŸ¨FreeAddMonoid.of ((1 : ğ•œ), m), ?_âŸ©
  Â· simp [projectiveSeminormAux]
  Â· rw [mem_lifts_iff, FreeAddMonoid.toList_of, List.map_singleton, List.sum_singleton, one_smul]


end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ]
variable [âˆ€ i, NormedSpace ğ•œ (E i)]

theorem norm_eval_le_projectiveSeminorm {G : Type*} [SeminormedAddCommGroup G]
    [NormedSpace ğ•œ G] (f : ContinuousMultilinearMap ğ•œ E G)
    (x : â¨‚[ğ•œ] i, E i) :
    â€–lift f.toMultilinearMap xâ€– â‰¤ â€–fâ€– * projectiveSeminorm x := by
  letI := nonempty_subtype.mpr (nonempty_lifts x)
  rw [projectiveSeminorm_apply, mul_comm, Real.iInf_mul_of_nonneg (norm_nonneg _)]
  unfold projectiveSeminormAux
  refine le_ciInf ?_
  intro âŸ¨p, hpâŸ©
  rw [mem_lifts_iff] at hp
  conv_lhs => rw [â† hp, â† List.sum_map_hom, â† Multiset.sum_coe]
  refine le_trans (norm_multiset_sum_le _) ?_
  simp only [Multiset.map_coe, List.map_map, Multiset.sum_coe]
  rw [mul_comm, â† smul_eq_mul, List.smul_sum]
  refine List.Forallâ‚‚.sum_le_sum ?_
  simp only [smul_eq_mul, List.map_map, List.forallâ‚‚_map_right_iff, Function.comp_apply,
    List.forallâ‚‚_map_left_iff, map_smul, lift.tprod, ContinuousMultilinearMap.coe_coe,
    List.forallâ‚‚_same, Prod.forall]
  intro a m _
  rw [norm_smul, â† mul_assoc, mul_comm â€–fâ€– _, mul_assoc]
  exact mul_le_mul_of_nonneg_left (f.le_opNorm _) (norm_nonneg _)

variable {F : Type uF} [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ E F)

/-- The linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
induced by `PiTensorProduct.lift`, for every normed space `F`. -/
@[simps]
noncomputable def liftEquiv : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F where
  toFun f := LinearMap.mkContinuous (lift f.toMultilinearMap) â€–fâ€– fun x â†¦
      norm_eval_le_projectiveSeminorm f x
  map_add' f g := by ext; simp
  map_smul' a f := by ext; simp
  invFun l := MultilinearMap.mkContinuous (lift.symm l.toLinearMap) â€–lâ€– fun x â†¦ by
    simp only [lift_symm, LinearMap.compMultilinearMap_apply, ContinuousLinearMap.coe_coe]
    exact ContinuousLinearMap.le_opNorm_of_le _ (projectiveSeminorm_tprod_le x)
  left_inv f := by ext; simp
  right_inv l := by
    rw [â† ContinuousLinearMap.coe_inj]
    apply PiTensorProduct.ext; ext m
    simp

/-- For a normed space `F`, we have constructed with `PiTensorProduct.liftEquiv` the canonical
linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
(induced by `PiTensorProduct.lift`). Here we upgrade this equivalence to an isometric linear
equivalence; in particular, it is a continuous linear equivalence. -/
noncomputable def liftIsometry : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—áµ¢[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F :=
  { liftEquiv ğ•œ E F with
    norm_map' f := by
      refine le_antisymm ?_ ?_
      Â· simp only [liftEquiv, lift_symm, LinearEquiv.coe_mk, LinearMap.coe_mk, AddHom.coe_mk]
        exact LinearMap.mkContinuous_norm_le _ (norm_nonneg f) _
      Â· simp only [liftEquiv]
        conv_lhs => rw [â† (liftEquiv ğ•œ E F).symm_apply_apply f]
        rw [liftEquiv_symm_apply]
        exact MultilinearMap.mkContinuous_norm_le _ (norm_nonneg _) _ }

variable {ğ•œ E F}

@[simp]
theorem liftIsometry_apply_apply (f : ContinuousMultilinearMap ğ•œ E F) (x : â¨‚[ğ•œ] i, E i) :
    liftIsometry ğ•œ E F f x = lift f.toMultilinearMap x := by
  simp only [liftIsometry, LinearIsometryEquiv.coe_mk, liftEquiv_apply,
    LinearMap.mkContinuous_apply]

variable (ğ•œ) in
/-- The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢` to `â¨‚[ğ•œ] i, Eáµ¢`. -/
@[simps!]
noncomputable def tprodL : ContinuousMultilinearMap ğ•œ E (â¨‚[ğ•œ] i, E i) :=
  (liftIsometry ğ•œ E _).symm (ContinuousLinearMap.id ğ•œ _)

@[simp]
theorem tprodL_coe : (tprodL ğ•œ).toMultilinearMap = tprod ğ•œ (s := E) := by ext; simp

@[simp]
theorem liftIsometry_symm_apply (l : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F) :
    (liftIsometry ğ•œ E F).symm l = l.compContinuousMultilinearMap (tprodL ğ•œ) := by
  rfl

@[simp]
theorem liftIsometry_tprodL :
    liftIsometry ğ•œ E _ (tprodL ğ•œ) = ContinuousLinearMap.id ğ•œ (â¨‚[ğ•œ] i, E i) := by ext; simp

theorem norm_tprodL_le : â€–tprodL ğ•œ (E := E)â€– â‰¤ 1 :=
  ContinuousMultilinearMap.opNorm_le_bound zero_le_one fun m â†¦ by simp [projectiveSeminorm_tprod_le]

section map

variable {E' E'' : Î¹ â†’ Type*}
variable [âˆ€ i, SeminormedAddCommGroup (E' i)] [âˆ€ i, NormedSpace ğ•œ (E' i)]
variable [âˆ€ i, SeminormedAddCommGroup (E'' i)] [âˆ€ i, NormedSpace ğ•œ (E'' i)]
variable (g : Î  i, E' i â†’L[ğ•œ] E'' i) (f : Î  i, E i â†’L[ğ•œ] E' i)

/--
Let `Eáµ¢` and `E'áµ¢` be two families of normed `ğ•œ`-vector spaces.
Let `f` be a family of continuous `ğ•œ`-linear maps between `Eáµ¢` and `E'áµ¢`, i.e.
`f : Î áµ¢ Eáµ¢ â†’L[ğ•œ] E'áµ¢`, then there is an induced continuous linear map
`â¨‚áµ¢ Eáµ¢ â†’ â¨‚áµ¢ E'áµ¢` by `â¨‚ aáµ¢ â†¦ â¨‚ fáµ¢ aáµ¢`.
-/
noncomputable def mapL : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i :=
  liftIsometry ğ•œ E _ <| (tprodL ğ•œ).compContinuousLinearMap f

@[simp]
theorem mapL_coe : (mapL f).toLinearMap = map (fun i â†¦ (f i).toLinearMap) := by
  ext; simp [mapL]

@[simp]
theorem mapL_apply (x : â¨‚[ğ•œ] i, E i) : mapL f x = map (fun i â†¦ (f i).toLinearMap) x := by
  rfl

/-- Given submodules `páµ¢ âŠ† Eáµ¢`, this is the natural map: `â¨‚[ğ•œ] i, páµ¢ â†’ â¨‚[ğ•œ] i, Eáµ¢`.
This is the continuous version of `PiTensorProduct.mapIncl`.
-/
@[simp]
noncomputable def mapLIncl (p : Î  i, Submodule ğ•œ (E i)) : (â¨‚[ğ•œ] i, p i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i :=
  mapL fun (i : Î¹) â†¦ (p i).subtypeL

theorem mapL_comp : mapL (fun (i : Î¹) â†¦ g i âˆ˜L f i) = mapL g âˆ˜L mapL f := by
  apply ContinuousLinearMap.coe_injective
  ext
  simp only [mapL_coe, ContinuousLinearMap.coe_comp, LinearMap.compMultilinearMap_apply, map_tprod,
    LinearMap.coe_comp, ContinuousLinearMap.coe_coe, Function.comp_apply]

theorem liftIsometry_comp_mapL (h : ContinuousMultilinearMap ğ•œ E' F) :
    liftIsometry ğ•œ E' F h âˆ˜L mapL f = liftIsometry ğ•œ E F (h.compContinuousLinearMap f) := by
  apply ContinuousLinearMap.coe_injective
  ext; simp

@[simp]
theorem mapL_id : mapL (fun i â†¦ ContinuousLinearMap.id ğ•œ (E i)) = ContinuousLinearMap.id _ _ := by
  apply ContinuousLinearMap.coe_injective
  ext; simp

@[simp]
theorem mapL_one : mapL (fun (i : Î¹) â†¦ (1 : E i â†’L[ğ•œ] E i)) = 1 :=
  mapL_id

theorem mapL_mul (fâ‚ fâ‚‚ : Î  i, E i â†’L[ğ•œ] E i) :
    mapL (fun i â†¦ fâ‚ i * fâ‚‚ i) = mapL fâ‚ * mapL fâ‚‚ :=
  mapL_comp fâ‚ fâ‚‚

/-- Upgrading `PiTensorProduct.mapL` to a `MonoidHom` when `E = E'`. -/
@[simps]
noncomputable def mapLMonoidHom : (Î  i, E i â†’L[ğ•œ] E i) â†’* ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i) where
  toFun := mapL
  map_one' := mapL_one
  map_mul' := mapL_mul

@[simp]
protected theorem mapL_pow (f : Î  i, E i â†’L[ğ•œ] E i) (n : â„•) :
    mapL (f ^ n) = mapL f ^ n := MonoidHom.map_pow mapLMonoidHom f n

-- We redeclare `Î¹` here, and later dependent arguments,
-- to avoid the `[Fintype Î¹]` assumption present throughout the rest of the file.
open Function in
private theorem mapL_add_smul_aux {Î¹ : Type uÎ¹}
    {E : Î¹ â†’ Type uE} [(i : Î¹) â†’ SeminormedAddCommGroup (E i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E i)]
    {E' : Î¹ â†’ Type u_1} [(i : Î¹) â†’ SeminormedAddCommGroup (E' i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E' i)]
    (f : (i : Î¹) â†’ E i â†’L[ğ•œ] E' i)
    [DecidableEq Î¹] (i : Î¹) (u : E i â†’L[ğ•œ] E' i) :
    (fun j â†¦ (update f i u j).toLinearMap) =
      update (fun j â†¦ (f j).toLinearMap) i u.toLinearMap := by
  grind

open Function in
protected theorem mapL_add [DecidableEq Î¹] (i : Î¹) (u v : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (u + v)) = mapL (update f i u) + mapL (update f i v) := by
  ext x
  simp [mapL_add_smul_aux, PiTensorProduct.map_update_add]

open Function in
protected theorem mapL_smul [DecidableEq Î¹] (i : Î¹) (c : ğ•œ) (u : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (c â€¢ u)) = c â€¢ mapL (update f i u) := by
  ext x
  simp [mapL_add_smul_aux, PiTensorProduct.map_update_smul]

theorem mapL_opNorm : â€–mapL fâ€– â‰¤ âˆ i, â€–f iâ€– := by
  rw [ContinuousLinearMap.opNorm_le_iff (by positivity)]
  intro x
  rw [mapL, liftIsometry]
  simp only [LinearIsometryEquiv.coe_mk, liftEquiv_apply, LinearMap.mkContinuous_apply]
  refine le_trans (norm_eval_le_projectiveSeminorm _ _)
    (mul_le_mul_of_nonneg_right ?_ (norm_nonneg x))
  rw [ContinuousMultilinearMap.opNorm_le_iff (Finset.prod_nonneg (fun _ _ â†¦ norm_nonneg _))]
  intro m
  simp only [ContinuousMultilinearMap.compContinuousLinearMap_apply]
  refine le_trans (projectiveSeminorm_tprod_le (fun i â†¦ (f i) (m i))) ?_
  rw [â† Finset.prod_mul_distrib]
  exact Finset.prod_le_prod (fun _ _ â†¦ norm_nonneg _) (fun _ _ â†¦ ContinuousLinearMap.le_opNorm _ _)

variable (ğ•œ E E')

/-- The tensor of a family of linear maps from `Eáµ¢` to `E'áµ¢`, as a continuous multilinear map of
the family.
-/
@[simps!]
noncomputable def mapLMultilinear : ContinuousMultilinearMap ğ•œ (fun (i : Î¹) â†¦ E i â†’L[ğ•œ] E' i)
    ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i) :=
  MultilinearMap.mkContinuous
  { toFun := mapL
    map_update_smul' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_smul _ _ _ _
    map_update_add' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_add _ _ _ _ }
  1 (fun f â†¦ by rw [one_mul]; exact mapL_opNorm f)

variable {ğ•œ E E'}

section dualCharacterization

variable {F : Type uF} [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (F) in
/-- The linear map from `â¨‚[ğ•œ] i, Eáµ¢` to `ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F` sending
`x` in `â¨‚[ğ•œ] i, Eáµ¢` to the map `f â†¦ f.lift x`. -/
@[simps!]
noncomputable def toDualContinuousMultilinearMap : (â¨‚[ğ•œ] i, E i) â†’â‚—[ğ•œ]
    ContinuousMultilinearMap ğ•œ E F â†’L[ğ•œ] F where
  toFun x := LinearMap.mkContinuous
    ((LinearMap.flip lift.toLinearMap x) âˆ˜â‚— ContinuousMultilinearMap.toMultilinearMapLinear)
    (projectiveSeminorm x)
    (fun _ â†¦ by simp [mul_comm, norm_eval_le_projectiveSeminorm])
  map_add' x y := by ext; simp
  map_smul' a x := by ext; simp

theorem toDualContinuousMultilinearMap_le_projectiveSeminorm (x : â¨‚[ğ•œ] i, E i) :
    â€–toDualContinuousMultilinearMap F xâ€– â‰¤ projectiveSeminorm x := by
  simp only [toDualContinuousMultilinearMap, LinearMap.coe_mk, AddHom.coe_mk]
  apply LinearMap.mkContinuous_norm_le _ (apply_nonneg _ _)

/-- The injective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`. Morally, it sends `x` in `â¨‚[ğ•œ] i, Eáµ¢` to the
`sup` of the operator norms of the `PiTensorProduct.toDualContinuousMultilinearMap F x`, for all
normed vector spaces `F`. In fact, we only take in the same universe as `â¨‚[ğ•œ] i, Eáµ¢`, and then
prove in `PiTensorProduct.norm_eval_le_injectiveSeminorm` that this gives the same result.
-/
noncomputable irreducible_def injectiveSeminorm : Seminorm ğ•œ (â¨‚[ğ•œ] i, E i) :=
  sSup {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE)) (_ : SeminormedAddCommGroup G)
  (_ : NormedSpace ğ•œ G), p = Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
  (toDualContinuousMultilinearMap G (ğ•œ := ğ•œ) (E := E))}

lemma dualSeminorms_bounded : BddAbove {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE))
    (_ : SeminormedAddCommGroup G) (_ : NormedSpace ğ•œ G),
    p = Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
    (toDualContinuousMultilinearMap G)} := by
  use projectiveSeminorm
  simp only [mem_upperBounds, Set.mem_setOf_eq, forall_exists_index]
  intro p G _ _ hp x
  simp [hp, toDualContinuousMultilinearMap_le_projectiveSeminorm]

lemma projectiveSeminorn_mem_dualSeminorms : projectiveSeminorm âˆˆ {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE))
    (_ : SeminormedAddCommGroup G) (_ : NormedSpace ğ•œ G),
    p = Seminorm.comp (normSeminorm ğ•œ (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
    (toDualContinuousMultilinearMap G)} := by
  use (â¨‚[ğ•œ] i, E i), inferInstance, inferInstance
  ext x
  refine le_antisymm ?_ (toDualContinuousMultilinearMap_le_projectiveSeminorm x)
  have := ContinuousLinearMap.le_opNorm ((toDualContinuousMultilinearMap _) x) (tprodL ğ•œ)
  grw [norm_tprodL_le, mul_one] at this
  simpa

theorem injectiveSeminorm_eq_projectiveSeminorm :
    injectiveSeminorm (ğ•œ := ğ•œ) (E := E) = projectiveSeminorm := by
  rw [injectiveSeminorm]
  refine le_antisymm (csSup_le âŸ¨_, projectiveSeminorn_mem_dualSeminormsâŸ© fun p âŸ¨G, _, _, hâŸ© x â†¦ ?_)
    (le_csSup_of_le dualSeminorms_bounded projectiveSeminorn_mem_dualSeminorms (le_refl _))
  simp [h, toDualContinuousMultilinearMap_le_projectiveSeminorm]

-- This used to be a long proof; now somewhat redundant.
theorem norm_eval_le_injectiveSeminorm (f : ContinuousMultilinearMap ğ•œ E F) (x : â¨‚[ğ•œ] i, E i) :
    â€–lift f.toMultilinearMap xâ€– â‰¤ â€–fâ€– * injectiveSeminorm x := by
    simp [injectiveSeminorm_eq_projectiveSeminorm, norm_eval_le_projectiveSeminorm]

theorem injectiveSeminorm_apply (x : â¨‚[ğ•œ] i, E i) :
    injectiveSeminorm x = â¨† p : {p | âˆƒ (G : Type (max uÎ¹ uğ•œ uE))
    (_ : SeminormedAddCommGroup G) (_ : NormedSpace ğ•œ G), p = Seminorm.comp (normSeminorm ğ•œ
    (ContinuousMultilinearMap ğ•œ E G â†’L[ğ•œ] G))
    (toDualContinuousMultilinearMap G)}, p.1 x := by
  simpa only [injectiveSeminorm, Set.coe_setOf, Set.mem_setOf_eq]
    using Seminorm.sSup_apply dualSeminorms_bounded

end dualCharacterization

end map

end NontriviallyNormedField

end PiTensorProduct
