/-
Copyright (c) 2024 Sophie Morel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Sophie Morel
-/
module

public import Mathlib.Analysis.Normed.Module.Multilinear.Basic
public import Mathlib.LinearAlgebra.PiTensorProduct
public import Mathlib.LinearAlgebra.PiTensorProduct.Dual
public import Mathlib.Analysis.Normed.Module.HahnBanach

/-!
# Projective seminorm on the tensor of a finite family of normed spaces.

Let `ğ•œ` be a nontrivially normed field and `E` be a family of normed `ğ•œ`-vector spaces `Eáµ¢`,
indexed by a finite type `Î¹`. We define a seminorm on `â¨‚[ğ•œ] i, Eáµ¢`, which we call the
"projective seminorm". For `x` an element of `â¨‚[ğ•œ] i, Eáµ¢`, its projective seminorm is the
infimum over all expressions of `x` as `âˆ‘ j, â¨‚â‚œ[ğ•œ] mâ±¼ i` (with the `mâ±¼` âˆˆ `Î  i, Eáµ¢`)
of `âˆ‘ j, Î  i, â€–mâ±¼ iâ€–`.

In particular, every norm `â€–.â€–` on `â¨‚[ğ•œ] i, Eáµ¢` satisfying `â€–â¨‚â‚œ[ğ•œ] i, m iâ€– â‰¤ Î  i, â€–m iâ€–`
for every `m` in `Î  i, Eáµ¢` is bounded above by the projective seminorm.

## Main definitions

* `PiTensorProduct.projectiveSeminorm`: The projective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.liftEquiv`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as a continuous linear equivalence.
* `PiTensorProduct.liftIsometry`: The bijection between `ContinuousMultilinearMap ğ•œ E F`
  and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`, as an isometric linear equivalence.
* `PiTensorProduct.tprodL`: The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢`
  to `â¨‚[ğ•œ] i, Eáµ¢`.
* `PiTensorProduct.mapL`: The continuous linear map from `â¨‚[ğ•œ] i, Eáµ¢` to `â¨‚[ğ•œ] i, E'áµ¢`
  induced by a family of continuous linear maps `Eáµ¢ â†’L[ğ•œ] E'áµ¢`.
* `PiTensorProduct.mapLMultilinear`: The continuous multilinear map from
  `Î áµ¢ (Eáµ¢ â†’L[ğ•œ] E'áµ¢)` to `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] (â¨‚[ğ•œ] i, E'áµ¢)` sending a family
  `f` to `PiTensorProduct.mapL f`.

## Main results

* `PiTensorProduct.norm_eval_le_projectiveSeminorm`: If `f` is a continuous multilinear map on
  `E = Î  i, Eáµ¢` and `x` is in `â¨‚[ğ•œ] i, Eáµ¢`, then `â€–f.lift xâ€– â‰¤ projectiveSeminorm x * â€–fâ€–`.
* `PiTensorProduct.mapL_opNorm`: If `f` is a family of continuous linear maps
  `fáµ¢ : Eáµ¢ â†’L[ğ•œ] Fáµ¢`, then `â€–PiTensorProduct.mapL fâ€– â‰¤ âˆ i, â€–fáµ¢â€–`.
* `PiTensorProduct.mapLMultilinear_opNorm` : If `F` is a normed vecteor space, then
  `â€–mapLMultilinear ğ•œ E Fâ€– â‰¤ 1`.
* `PiTensorProduct.projectiveSeminorm_tprod_eq_of_dual_vectors`: the projective
  seminorm satisfies the multiplicativity property `â€–â¨‚ máµ¢â€– = âˆ â€–máµ¢â€–` if, for
  each `máµ¢`, there is an `fáµ¢` in the dual unit ball such that `â€–fáµ¢ máµ¢â€– = â€–máµ¢â€–`.

## TODO

* Adapt the remaining functoriality constructions/properties from `PiTensorProduct`.
-/

@[expose] public section

open scoped TensorProduct

namespace PiTensorProduct

universe uÎ¹ uğ•œ uE uF

variable {Î¹ : Type uÎ¹} [Fintype Î¹] {ğ•œ : Type uğ•œ}
variable {E : Î¹ â†’ Type uE} [âˆ€ i, SeminormedAddCommGroup (E i)]

section NormedField

variable [NormedField ğ•œ]

/-- A lift of the projective seminorm to `FreeAddMonoid (ğ•œ Ã— Î  i, Eáµ¢)`, useful to prove the
properties of `projectiveSeminorm`. -/
def projectiveSeminormAux : FreeAddMonoid (ğ•œ Ã— Î  i, E i) â†’ â„ :=
  fun p â†¦ (p.toList.map (fun p â†¦ â€–p.1â€– * âˆ i, â€–p.2 iâ€–)).sum

theorem projectiveSeminormAux_nonneg (p : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) :
    0 â‰¤ projectiveSeminormAux p := by
  refine List.sum_nonneg fun a â†¦ ?_
  simp only [List.mem_map, Prod.exists, forall_exists_index, and_imp]
  intro x m _ h
  rw [â† h]
  exact mul_nonneg (norm_nonneg _) (Finset.prod_nonneg (fun _ _ â†¦ norm_nonneg _))

theorem projectiveSeminormAux_add_le (p q : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) :
    projectiveSeminormAux (p + q) â‰¤ projectiveSeminormAux p + projectiveSeminormAux q := by
  simp [projectiveSeminormAux]

theorem projectiveSeminormAux_smul (p : FreeAddMonoid (ğ•œ Ã— Î  i, E i)) (a : ğ•œ) :
    projectiveSeminormAux (p.map (fun (y : ğ•œ Ã— Î  i, E i) â†¦ (a * y.1, y.2))) =
    â€–aâ€– * projectiveSeminormAux p := by
  simp [projectiveSeminormAux, Function.comp_def, mul_assoc, List.sum_map_mul_left]

variable [âˆ€ i, NormedSpace ğ•œ (E i)]

theorem bddBelow_projectiveSemiNormAux (x : â¨‚[ğ•œ] i, E i) :
    BddBelow (Set.range (fun (p : lifts x) â†¦ projectiveSeminormAux p.val)) :=
  âŸ¨0, by simp [mem_lowerBounds, projectiveSeminormAux_nonneg]âŸ©

noncomputable instance : Norm (â¨‚[ğ•œ] i, E i) :=
  âŸ¨fun x â†¦ iInf (fun (p : lifts x) â†¦ projectiveSeminormAux p.val)âŸ©

theorem norm_def (x : â¨‚[ğ•œ] i, E i) :
    â€–xâ€– = iInf (fun (p : lifts x) â†¦ projectiveSeminormAux p.val) := rfl

theorem projectiveSeminorm_zero : â€–(0 : â¨‚[ğ•œ] i, E i)â€– = 0 :=
  le_antisymm (ciInf_le (bddBelow_projectiveSemiNormAux _) âŸ¨0, lifts_zeroâŸ©)
    (le_ciInf (fun p â†¦ projectiveSeminormAux_nonneg p.val))

theorem projectiveSeminorm_add_le (x y : â¨‚[ğ•œ] i, E i) : â€–x+yâ€– â‰¤ â€–xâ€– + â€–yâ€– :=
  le_ciInf_add_ciInf (fun p q â†¦ ciInf_le_of_le (bddBelow_projectiveSemiNormAux _)
    âŸ¨p.1 + q.1, lifts_add p.2 q.2âŸ© (projectiveSeminormAux_add_le p.1 q.1))

theorem projectiveSeminorm_smul_le (a : ğ•œ) (x : â¨‚[ğ•œ] i, E i) : â€–a â€¢ xâ€– â‰¤ â€–aâ€– * â€–xâ€– := by
  simp only [norm_def, Real.mul_iInf_of_nonneg (norm_nonneg _)]
  refine le_ciInf fun p â†¦ ?_
  rw [â† projectiveSeminormAux_smul]
  exact ciInf_le_of_le (bddBelow_projectiveSemiNormAux _) âŸ¨_, lifts_smul p.2 aâŸ© (le_refl _)

/-- The projective seminorm on `â¨‚[ğ•œ] i, Eáµ¢`. It sends an element `x` of `â¨‚[ğ•œ] i, Eáµ¢` to the
infimum over all expressions of `x` as `âˆ‘ j, â¨‚â‚œ[ğ•œ] mâ±¼ i` (with the `mâ±¼` âˆˆ `Î  i, Eáµ¢`)
of `âˆ‘ j, Î  i, â€–mâ±¼ iâ€–`. -/
noncomputable def projectiveSeminorm : Seminorm ğ•œ (â¨‚[ğ•œ] i, E i) := .ofSMulLE
    _ projectiveSeminorm_zero projectiveSeminorm_add_le projectiveSeminorm_smul_le

noncomputable instance : SeminormedAddCommGroup (â¨‚[ğ•œ] i, E i) :=
  AddGroupSeminorm.toSeminormedAddCommGroup projectiveSeminorm.toAddGroupSeminorm

noncomputable instance : NormedSpace ğ•œ (â¨‚[ğ•œ] i, E i) := âŸ¨projectiveSeminorm_smul_leâŸ©

theorem projectiveSeminorm_tprod_le (m : Î  i, E i) :
    â€–(â¨‚â‚œ[ğ•œ] i, m i)â€– â‰¤ âˆ i, â€–m iâ€– := by
  convert ciInf_le (bddBelow_projectiveSemiNormAux _) âŸ¨FreeAddMonoid.of ((1 : ğ•œ), m), ?_âŸ©
  Â· simp [projectiveSeminormAux]
  Â· simp [mem_lifts_iff]

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ğ•œ] [âˆ€ i, NormedSpace ğ•œ (E i)]

theorem norm_eval_le_projectiveSeminorm {G : Type*} [SeminormedAddCommGroup G]
    [NormedSpace ğ•œ G] (f : ContinuousMultilinearMap ğ•œ E G) (x : â¨‚[ğ•œ] i, E i) :
    â€–lift f.toMultilinearMap xâ€– â‰¤ â€–fâ€– * â€–xâ€– := by
  rw [norm_def, mul_comm, Real.iInf_mul_of_nonneg (norm_nonneg _)]
  refine le_ciInf fun âŸ¨p, hpâŸ© â†¦ ?_
  simp_rw [â† ((mem_lifts_iff x p).mp hp), â† List.sum_map_hom, â† Multiset.sum_coe]
  refine le_trans (norm_multiset_sum_le _) ?_
  simp only [mul_comm, â† smul_eq_mul, List.smul_sum, projectiveSeminormAux]
  refine List.Forallâ‚‚.sum_le_sum ?_
  simp only [smul_eq_mul, List.forallâ‚‚_map_right_iff, Function.comp_apply,
    List.forallâ‚‚_map_left_iff, map_smul, lift.tprod, List.forallâ‚‚_same, Prod.forall]
  intro a m _
  rw [norm_smul, â† mul_assoc, mul_comm â€–fâ€– _, mul_assoc]
  exact mul_le_mul_of_nonneg_left (f.le_opNorm _) (norm_nonneg _)

variable {F : Type uF} [SeminormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ E F)

/-- The linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
induced by `PiTensorProduct.lift`, for every normed space `F`. -/
@[simps]
noncomputable def liftEquiv : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F where
  toFun f := LinearMap.mkContinuous (lift f.toMultilinearMap) â€–fâ€– fun x â†¦
      norm_eval_le_projectiveSeminorm f x
  map_add' f g := by ext; simp
  map_smul' a f := by ext; simp
  invFun l := MultilinearMap.mkContinuous (lift.symm l.toLinearMap) â€–lâ€– fun x â†¦
    ContinuousLinearMap.le_opNorm_of_le _ (projectiveSeminorm_tprod_le x)
  left_inv f := by ext; simp
  right_inv l := by
    rw [â† ContinuousLinearMap.coe_inj]
    ext; simp

/-- For a normed space `F`, we have constructed with `PiTensorProduct.liftEquiv` the canonical
linear equivalence between `ContinuousMultilinearMap ğ•œ E F` and `(â¨‚[ğ•œ] i, Eáµ¢) â†’L[ğ•œ] F`
(induced by `PiTensorProduct.lift`). Here we upgrade this equivalence to an isometric linear
equivalence; in particular, it is a continuous linear equivalence. -/
noncomputable def liftIsometry : ContinuousMultilinearMap ğ•œ E F â‰ƒâ‚—áµ¢[ğ•œ] (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F :=
  { liftEquiv ğ•œ E F with
    norm_map' f := by
      refine le_antisymm ?_ ?_
      Â· simp only [liftEquiv, lift_symm, LinearEquiv.coe_mk, LinearMap.coe_mk, AddHom.coe_mk]
        apply LinearMap.mkContinuous_norm_le _ (norm_nonneg f)
      Â· conv_lhs => rw [â† (liftEquiv ğ•œ E F).symm_apply_apply f, liftEquiv_symm_apply]
        exact MultilinearMap.mkContinuous_norm_le _ (norm_nonneg _) _ }

variable {ğ•œ E F}

@[simp]
theorem liftIsometry_apply_apply (f : ContinuousMultilinearMap ğ•œ E F) (x : â¨‚[ğ•œ] i, E i) :
    liftIsometry ğ•œ E F f x = lift f.toMultilinearMap x := by
  simp [liftIsometry]

variable (ğ•œ) in
/-- The canonical continuous multilinear map from `E = Î áµ¢ Eáµ¢` to `â¨‚[ğ•œ] i, Eáµ¢`. -/
@[simps!]
noncomputable def tprodL : ContinuousMultilinearMap ğ•œ E (â¨‚[ğ•œ] i, E i) :=
  (liftIsometry ğ•œ E _).symm (ContinuousLinearMap.id ğ•œ _)

@[simp]
theorem tprodL_coe : (tprodL ğ•œ).toMultilinearMap = tprod ğ•œ (s := E) := by ext; simp

@[simp]
theorem liftIsometry_symm_apply (l : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] F) :
    (liftIsometry ğ•œ E F).symm l = l.compContinuousMultilinearMap (tprodL ğ•œ) := by
  rfl

@[simp]
theorem liftIsometry_tprodL :
    liftIsometry ğ•œ E _ (tprodL ğ•œ) = ContinuousLinearMap.id ğ•œ (â¨‚[ğ•œ] i, E i) := by ext; simp

section map

variable {E' E'' : Î¹ â†’ Type*}
variable [âˆ€ i, SeminormedAddCommGroup (E' i)] [âˆ€ i, NormedSpace ğ•œ (E' i)]
variable [âˆ€ i, SeminormedAddCommGroup (E'' i)] [âˆ€ i, NormedSpace ğ•œ (E'' i)]
variable (g : Î  i, E' i â†’L[ğ•œ] E'' i) (f : Î  i, E i â†’L[ğ•œ] E' i)

/--
Let `Eáµ¢` and `E'áµ¢` be two families of normed `ğ•œ`-vector spaces.
Let `f` be a family of continuous `ğ•œ`-linear maps between `Eáµ¢` and `E'áµ¢`, i.e.
`f : Î áµ¢ Eáµ¢ â†’L[ğ•œ] E'áµ¢`, then there is an induced continuous linear map
`â¨‚áµ¢ Eáµ¢ â†’ â¨‚áµ¢ E'áµ¢` by `â¨‚ aáµ¢ â†¦ â¨‚ fáµ¢ aáµ¢`.
-/
noncomputable def mapL : (â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i :=
  liftIsometry ğ•œ E _ <| (tprodL ğ•œ).compContinuousLinearMap f

@[simp]
theorem mapL_coe : (mapL f).toLinearMap = map (fun i â†¦ (f i).toLinearMap) := by
  ext; simp [mapL]

@[simp]
theorem mapL_apply (x : â¨‚[ğ•œ] i, E i) : mapL f x = map (fun i â†¦ (f i).toLinearMap) x := by
  rfl

/-- Given submodules `páµ¢ âŠ† Eáµ¢`, this is the natural map: `â¨‚[ğ•œ] i, páµ¢ â†’ â¨‚[ğ•œ] i, Eáµ¢`.
This is the continuous version of `PiTensorProduct.mapIncl`.
-/
@[simp]
noncomputable def mapLIncl (p : Î  i, Submodule ğ•œ (E i)) : (â¨‚[ğ•œ] i, p i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i :=
  mapL fun (i : Î¹) â†¦ (p i).subtypeL

theorem mapL_comp : mapL (fun (i : Î¹) â†¦ g i âˆ˜L f i) = mapL g âˆ˜L mapL f := by
  apply ContinuousLinearMap.coe_injective
  ext; simp

theorem liftIsometry_comp_mapL (h : ContinuousMultilinearMap ğ•œ E' F) :
    liftIsometry ğ•œ E' F h âˆ˜L mapL f = liftIsometry ğ•œ E F (h.compContinuousLinearMap f) := by
  apply ContinuousLinearMap.coe_injective
  ext; simp

@[simp]
theorem mapL_id : mapL (fun i â†¦ ContinuousLinearMap.id ğ•œ (E i)) = ContinuousLinearMap.id _ _ := by
  apply ContinuousLinearMap.coe_injective
  ext; simp

@[simp]
theorem mapL_one : mapL (fun (i : Î¹) â†¦ (1 : E i â†’L[ğ•œ] E i)) = 1 :=
  mapL_id

theorem mapL_mul (fâ‚ fâ‚‚ : Î  i, E i â†’L[ğ•œ] E i) :
    mapL (fun i â†¦ fâ‚ i * fâ‚‚ i) = mapL fâ‚ * mapL fâ‚‚ :=
  mapL_comp fâ‚ fâ‚‚

/-- Upgrading `PiTensorProduct.mapL` to a `MonoidHom` when `E = E'`. -/
@[simps]
noncomputable def mapLMonoidHom : (Î  i, E i â†’L[ğ•œ] E i) â†’* ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E i) where
  toFun := mapL
  map_one' := mapL_one
  map_mul' := mapL_mul

@[simp]
protected theorem mapL_pow (f : Î  i, E i â†’L[ğ•œ] E i) (n : â„•) :
    mapL (f ^ n) = mapL f ^ n := MonoidHom.map_pow mapLMonoidHom f n

-- We redeclare `Î¹` here, and later dependent arguments,
-- to avoid the `[Fintype Î¹]` assumption present throughout the rest of the file.
open Function in
private theorem mapL_add_smul_aux {Î¹ : Type uÎ¹}
    {E : Î¹ â†’ Type uE} [(i : Î¹) â†’ SeminormedAddCommGroup (E i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E i)]
    {E' : Î¹ â†’ Type u_1} [(i : Î¹) â†’ SeminormedAddCommGroup (E' i)] [(i : Î¹) â†’ NormedSpace ğ•œ (E' i)]
    (f : (i : Î¹) â†’ E i â†’L[ğ•œ] E' i) [DecidableEq Î¹] (i : Î¹) (u : E i â†’L[ğ•œ] E' i) :
    (fun j â†¦ (update f i u j).toLinearMap) =
      update (fun j â†¦ (f j).toLinearMap) i u.toLinearMap := by
  grind

open Function in
protected theorem mapL_add [DecidableEq Î¹] (i : Î¹) (u v : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (u + v)) = mapL (update f i u) + mapL (update f i v) := by
  ext x
  simp [mapL_add_smul_aux, PiTensorProduct.map_update_add]

open Function in
protected theorem mapL_smul [DecidableEq Î¹] (i : Î¹) (c : ğ•œ) (u : E i â†’L[ğ•œ] E' i) :
    mapL (update f i (c â€¢ u)) = c â€¢ mapL (update f i u) := by
  ext x
  simp [mapL_add_smul_aux, PiTensorProduct.map_update_smul]

theorem mapL_opNorm : â€–mapL fâ€– â‰¤ âˆ i, â€–f iâ€– := by
  refine (ContinuousLinearMap.opNorm_le_iff (by positivity)).mpr fun x â†¦ ?_
  apply le_trans (norm_eval_le_projectiveSeminorm ..) (mul_le_mul_of_nonneg_right _ (norm_nonneg x))
  refine (ContinuousMultilinearMap.opNorm_le_iff (by positivity)).mpr fun m â†¦ ?_
  apply le_trans (projectiveSeminorm_tprod_le fun i â†¦ f i (m i))
  rw [â† Finset.prod_mul_distrib]
  exact Finset.prod_le_prod (fun _ _ â†¦ norm_nonneg _) (fun _ _ â†¦ ContinuousLinearMap.le_opNorm _ _)

variable (ğ•œ E E')

/-- The tensor of a family of linear maps from `Eáµ¢` to `E'áµ¢`, as a continuous multilinear map of
the family.
-/
@[simps!]
noncomputable def mapLMultilinear : ContinuousMultilinearMap ğ•œ (fun (i : Î¹) â†¦ E i â†’L[ğ•œ] E' i)
    ((â¨‚[ğ•œ] i, E i) â†’L[ğ•œ] â¨‚[ğ•œ] i, E' i) :=
  MultilinearMap.mkContinuous
  { toFun := mapL
    map_update_smul' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_smul _ _ _ _
    map_update_add' := fun _ _ _ _ â†¦ PiTensorProduct.mapL_add _ _ _ _ }
  1 (fun f â†¦ by rw [one_mul]; exact mapL_opNorm f)

end map

section projectiveSeminorm_tprod

theorem projectiveSeminorm_tprod_eq_of_dual_vectors {f : Î  i, StrongDual ğ•œ (E i)}
    (m : Î  i, E i) (hfâ‚ : âˆ€ i, â€–f iâ€– â‰¤ 1) (hfâ‚‚ : âˆ€ i, â€–f i (m i)â€– = â€–m iâ€–) :
    â€–â¨‚â‚œ[ğ•œ] i, m iâ€– = âˆ i, â€–m iâ€– := by
  apply eq_of_le_of_ge (projectiveSeminorm_tprod_le m) (le_ciInf (fun x â†¦ ?_))
  have hx := congr_arg (norm âˆ˜ dualDistrib (â¨‚â‚œ[ğ•œ] i, f i)) ((mem_lifts_iff _ _).mp x.prop)
  simp only [Function.comp_apply, dualDistrib_apply, ContinuousLinearMap.coe_coe, hfâ‚‚, norm_prod,
     map_list_sum, List.map_map] at hx
  grw [â† hx, List.le_sum_of_subadditive norm norm_zero.le norm_add_le, List.map_map]
  apply List.sum_le_sum (fun _ _ â†¦ ?_)
  simp only [Function.comp_apply, map_smul, dualDistrib_apply, ContinuousLinearMap.coe_coe,
    smul_eq_mul, norm_mul, norm_prod]
  gcongr
  grw [ContinuousLinearMap.le_opNorm, hfâ‚, one_mul]

end projectiveSeminorm_tprod

end NontriviallyNormedField

section RCLike

variable [RCLike ğ•œ]
variable {E : Î¹ â†’ Type uE} [âˆ€ i, NormedAddCommGroup (E i)] [âˆ€ i, NormedSpace ğ•œ (E i)]

@[simp]
theorem projectiveSeminorm_tprod (m : Î  i, E i)
    : projectiveSeminorm (â¨‚â‚œ[ğ•œ] i, m i) = âˆ i, â€–m iâ€– := by
  choose g hgâ‚ hgâ‚‚ using fun i â†¦ exists_dual_vector'' ğ•œ (m i)
  exact projectiveSeminorm_tprod_eq_of_dual_vectors m hgâ‚ (by simp [hgâ‚‚])

end RCLike

end PiTensorProduct
