/-
Copyright (c) 2022 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.Normed.Group.Completion
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace
import Mathlib.Topology.Algebra.UniformRing
import Mathlib.Topology.Algebra.UniformField

/-!
# Normed space structure on the completion of a normed space

If `E` is a normed space over `ğ•œ`, then so is `UniformSpace.Completion E`. In this file we provide
necessary instances and define `UniformSpace.Completion.toComplâ‚—áµ¢` - coercion
`E â†’ UniformSpace.Completion E` as a bundled linear isometry.

We also show that if `A` is a normed algebra over `ğ•œ`, then so is `UniformSpace.Completion A`.

TODO: Generalise the results here from the concrete `completion` to any `AbstractCompletion`.
-/


noncomputable section

namespace UniformSpace

namespace Completion

variable (ğ•œ E : Type*)

instance [NormedField ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] :
    NormedSpace ğ•œ (Completion E) where
  norm_smul_le := norm_smul_le

section Module

variable {ğ•œ E}
variable [Semiring ğ•œ] [SeminormedAddCommGroup E] [Module ğ•œ E] [UniformContinuousConstSMul ğ•œ E]

/-- Embedding of a normed space to its completion as a linear isometry. -/
def toComplâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] Completion E :=
  { toCompl with
    toFun := (â†‘)
    map_smul' := coe_smul
    norm_map' := norm_coe }

@[simp]
theorem coe_toComplâ‚—áµ¢ : â‡‘(toComplâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] Completion E) = ((â†‘) : E â†’ Completion E) :=
  rfl

/-- Embedding of a normed space to its completion as a continuous linear map. -/
def toComplL : E â†’L[ğ•œ] Completion E :=
  toComplâ‚—áµ¢.toContinuousLinearMap

@[simp]
theorem coe_toComplL : â‡‘(toComplL : E â†’L[ğ•œ] Completion E) = ((â†‘) : E â†’ Completion E) :=
  rfl

@[simp]
theorem norm_toComplL {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E]
    [NormedSpace ğ•œ E] [Nontrivial E] : â€–(toComplL : E â†’L[ğ•œ] Completion E)â€– = 1 :=
  (toComplâ‚—áµ¢ : E â†’â‚—áµ¢[ğ•œ] Completion E).norm_toContinuousLinearMap

end Module

section Algebra

variable (A : Type*)

instance [SeminormedRing A] : NormedRing (Completion A) where
  __ : NormedAddCommGroup (Completion A) := inferInstance
  __ : Ring (Completion A) := inferInstance
  norm_mul_le x y := by
    induction x, y using induction_onâ‚‚ with
    | hp => apply isClosed_le <;> fun_prop
    | ih x y => simpa only [â† coe_mul, norm_coe] using norm_mul_le x y

instance [SeminormedCommRing A] : NormedCommRing (Completion A) where
  __ : CommRing (Completion A) := inferInstance
  __ : NormedRing (Completion A) := inferInstance

instance [NormedField ğ•œ] [SeminormedCommRing A] [NormedAlgebra ğ•œ A] :
    NormedAlgebra ğ•œ (Completion A) where
  norm_smul_le := norm_smul_le

instance [NormedField A] [CompletableTopField A] :
    NormedField (UniformSpace.Completion A) where
  __ : NormedCommRing (Completion A) := inferInstance
  __ : Field (Completion A) := inferInstance
  norm_mul x y := induction_onâ‚‚ x y (isClosed_eq (by fun_prop) (by fun_prop)) (by simp [â† coe_mul])

end Algebra

end Completion

end UniformSpace
