/-
Copyright (c) 2025 IlmÄrs CÄ«rulis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IlmÄrs CÄ«rulis, Alex Meiburg
-/
module

public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.Data.Sign.Defs

/-!
# Normalized vector

Function that returns unit length vector that points in the same direction
(if the given vector is nonzero vector) or returns zero vector
(if the given vector is zero vector).
-/

@[expose] public section

variable (ğ•œ : Type*) {V : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [NormedSpace ğ•œ V]

/-- For a nonzero vector `x`, `normalize x` is the unit-length vector that points
in the same direction as `x`. If `x = 0`, then `normalize x = 0`. -/
noncomputable def NormedSpace.normalize (x : V) : V := (â€–xâ€–â»Â¹ : ğ•œ) â€¢ x

namespace NormedSpace

@[simp] theorem normalize_zero_eq_zero : normalize ğ•œ (0 : V) = 0 := by
  simp [normalize]

@[simp] theorem normalize_eq_zero_iff (x : V) : normalize ğ•œ x = 0 â†” x = 0 := by
  by_cases hx : x = 0 <;> simp [normalize, hx]

@[simp] theorem norm_smul_normalize (x : V) : (â€–xâ€– : ğ•œ) â€¢ normalize ğ•œ x = x := by
  by_cases hx : x = 0 <;> simp [normalize, hx]

@[simp] lemma norm_normalize_eq_one_iff {x : V} : â€–normalize ğ•œ xâ€– = 1 â†” x â‰  0 := by
  by_cases hx : x = 0 <;> simp [normalize, hx, norm_smul]

alias âŸ¨_, norm_normalizeâŸ© := norm_normalize_eq_one_iff

lemma normalize_eq_self_of_norm_eq_one {x : V} (h : â€–xâ€– = 1) : normalize ğ•œ x = x := by
  simp [normalize, h]

variable {ğ•œ} in
@[simp] theorem normalize_normalize (x : V) : normalize ğ•œ (normalize ğ•œ x) = normalize ğ•œ x := by
  by_cases hx : x = 0
  Â· simp [hx]
  Â· simp [normalize_eq_self_of_norm_eq_one, hx]

variable {ğ•œ} in
@[simp] theorem normalize_neg (x : V) : normalize ğ•œ (-x) = - normalize ğ•œ x := by
  simp [normalize]

open scoped ComplexOrder in
variable {ğ•œ} in
theorem normalize_smul_of_pos {r : ğ•œ} (hr : 0 < r) (x : V) :
    normalize ğ•œ (r â€¢ x) = normalize ğ•œ x := by
  simp [normalize, norm_smul, smul_smul, RCLike.norm_of_nonneg' hr.le, hr.ne']

open scoped ComplexOrder in
variable {ğ•œ} in
theorem normalize_smul_of_neg {r : ğ•œ} (hr : r < 0) (x : V) :
    normalize ğ•œ (r â€¢ x) = - normalize ğ•œ x := by
  simpa using normalize_smul_of_pos (show 0 < -r by grind) (-x)

theorem normalize_smul {V : Type*} [NormedAddCommGroup V] [NormedSpace â„ V] (r : â„) (x : V) :
    normalize â„ (r â€¢ x) = (SignType.sign r : â„) â€¢ normalize â„ x := by
  rcases lt_trichotomy 0 r with (h_pos | rfl | h_neg)
  Â· simp [normalize_smul_of_pos, h_pos]
  Â· simp
  Â· simp [normalize_smul_of_neg, h_neg]

end NormedSpace
