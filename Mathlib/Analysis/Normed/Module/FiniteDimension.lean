/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Asymptotics.AsymptoticEquivalent
import Mathlib.Analysis.Normed.Group.Lemmas
import Mathlib.Analysis.Normed.Affine.AddTorsor
import Mathlib.Analysis.Normed.Affine.Isometry
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace
import Mathlib.Analysis.NormedSpace.RieszLemma
import Mathlib.Analysis.NormedSpace.Pointwise
import Mathlib.Logic.Encodable.Pi
import Mathlib.Topology.Algebra.Module.FiniteDimension
import Mathlib.Topology.Algebra.InfiniteSum.Module
import Mathlib.Topology.Instances.Matrix
import Mathlib.LinearAlgebra.Dimension.LinearMap

/-!
# Finite dimensional normed spaces over complete fields

Over a complete nontrivially normed field, in finite dimension, all norms are equivalent and all
linear maps are continuous. Moreover, a finite-dimensional subspace is always complete and closed.

## Main results:

* `FiniteDimensional.complete` : a finite-dimensional space over a complete field is complete. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution.
* `Submodule.closed_of_finiteDimensional` : a finite-dimensional subspace over a complete field is
  closed
* `FiniteDimensional.proper` : a finite-dimensional space over a proper field is proper. This
  is not registered as an instance, as the field would be an unknown metavariable in typeclass
  resolution. It is however registered as an instance for `ğ•œ = â„` and `ğ•œ = â„‚`. As properness
  implies completeness, there is no need to also register `FiniteDimensional.complete` on `â„` or
  `â„‚`.
* `FiniteDimensional.of_isCompact_closedBall`: Riesz' theorem: if the closed unit ball is
  compact, then the space is finite-dimensional.

## Implementation notes

The fact that all norms are equivalent is not written explicitly, as it would mean having two norms
on a single space, which is not the way type classes work. However, if one has a
finite-dimensional vector space `E` with a norm, and a copy `E'` of this type with another norm,
then the identities from `E` to `E'` and from `E'`to `E` are continuous thanks to
`LinearMap.continuous_of_finiteDimensional`. This gives the desired norm equivalence.
-/

universe u v w x

noncomputable section

open Asymptotics Filter Module Metric Module NNReal Set TopologicalSpace Topology

namespace LinearIsometry

open LinearMap

variable {F Eâ‚ : Type*} [SeminormedAddCommGroup F] [NormedAddCommGroup Eâ‚]
variable {Râ‚ : Type*} [Field Râ‚] [Module Râ‚ Eâ‚] [Module Râ‚ F] [FiniteDimensional Râ‚ Eâ‚]
  [FiniteDimensional Râ‚ F]

/-- A linear isometry between finite dimensional spaces of equal dimension can be upgraded
    to a linear isometry equivalence. -/
def toLinearIsometryEquiv (li : Eâ‚ â†’â‚—áµ¢[Râ‚] F) (h : finrank Râ‚ Eâ‚ = finrank Râ‚ F) :
    Eâ‚ â‰ƒâ‚—áµ¢[Râ‚] F where
  toLinearEquiv := li.toLinearMap.linearEquivOfInjective li.injective h
  norm_map' := li.norm_map'

@[simp]
theorem coe_toLinearIsometryEquiv (li : Eâ‚ â†’â‚—áµ¢[Râ‚] F) (h : finrank Râ‚ Eâ‚ = finrank Râ‚ F) :
    (li.toLinearIsometryEquiv h : Eâ‚ â†’ F) = li :=
  rfl

@[simp]
theorem toLinearIsometryEquiv_apply (li : Eâ‚ â†’â‚—áµ¢[Râ‚] F) (h : finrank Râ‚ Eâ‚ = finrank Râ‚ F)
    (x : Eâ‚) : (li.toLinearIsometryEquiv h) x = li x :=
  rfl

end LinearIsometry

namespace AffineIsometry

open AffineMap

variable {ğ•œ : Type*} {Vâ‚ Vâ‚‚ : Type*} {Pâ‚ Pâ‚‚ : Type*} [NormedField ğ•œ] [NormedAddCommGroup Vâ‚]
  [SeminormedAddCommGroup Vâ‚‚] [NormedSpace ğ•œ Vâ‚] [NormedSpace ğ•œ Vâ‚‚] [MetricSpace Pâ‚]
  [PseudoMetricSpace Pâ‚‚] [NormedAddTorsor Vâ‚ Pâ‚] [NormedAddTorsor Vâ‚‚ Pâ‚‚]

variable [FiniteDimensional ğ•œ Vâ‚] [FiniteDimensional ğ•œ Vâ‚‚]

/-- An affine isometry between finite dimensional spaces of equal dimension can be upgraded
    to an affine isometry equivalence. -/
def toAffineIsometryEquiv [Inhabited Pâ‚] (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚) (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) :
    Pâ‚ â‰ƒáµƒâ±[ğ•œ] Pâ‚‚ :=
  AffineIsometryEquiv.mk' li (li.linearIsometry.toLinearIsometryEquiv h)
    (Inhabited.default (Î± := Pâ‚)) fun p => by simp

@[simp]
theorem coe_toAffineIsometryEquiv [Inhabited Pâ‚] (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚)
    (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) : (li.toAffineIsometryEquiv h : Pâ‚ â†’ Pâ‚‚) = li :=
  rfl

@[simp]
theorem toAffineIsometryEquiv_apply [Inhabited Pâ‚] (li : Pâ‚ â†’áµƒâ±[ğ•œ] Pâ‚‚)
    (h : finrank ğ•œ Vâ‚ = finrank ğ•œ Vâ‚‚) (x : Pâ‚) : (li.toAffineIsometryEquiv h) x = li x :=
  rfl

end AffineIsometry

section CompleteField

variable {ğ•œ : Type u} [NontriviallyNormedField ğ•œ] {E : Type v} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {F : Type w} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace ğ•œ]

section Affine

variable {PE PF : Type*} [MetricSpace PE] [NormedAddTorsor E PE] [MetricSpace PF]
  [NormedAddTorsor F PF] [FiniteDimensional ğ•œ E]

theorem AffineMap.continuous_of_finiteDimensional (f : PE â†’áµƒ[ğ•œ] PF) : Continuous f :=
  AffineMap.continuous_linear_iff.1 f.linear.continuous_of_finiteDimensional

theorem AffineEquiv.continuous_of_finiteDimensional (f : PE â‰ƒáµƒ[ğ•œ] PF) : Continuous f :=
  f.toAffineMap.continuous_of_finiteDimensional

/-- Reinterpret an affine equivalence as a homeomorphism. -/
def AffineEquiv.toHomeomorphOfFiniteDimensional (f : PE â‰ƒáµƒ[ğ•œ] PF) : PE â‰ƒâ‚œ PF where
  toEquiv := f.toEquiv
  continuous_toFun := f.continuous_of_finiteDimensional
  continuous_invFun :=
    haveI : FiniteDimensional ğ•œ F := f.linear.finiteDimensional
    f.symm.continuous_of_finiteDimensional

@[simp]
theorem AffineEquiv.coe_toHomeomorphOfFiniteDimensional (f : PE â‰ƒáµƒ[ğ•œ] PF) :
    â‡‘f.toHomeomorphOfFiniteDimensional = f :=
  rfl

@[simp]
theorem AffineEquiv.coe_toHomeomorphOfFiniteDimensional_symm (f : PE â‰ƒáµƒ[ğ•œ] PF) :
    â‡‘f.toHomeomorphOfFiniteDimensional.symm = f.symm :=
  rfl

end Affine

theorem ContinuousLinearMap.continuous_det : Continuous fun f : E â†’L[ğ•œ] E => f.det := by
  change Continuous fun f : E â†’L[ğ•œ] E => LinearMap.det (f : E â†’â‚—[ğ•œ] E)
  -- TODO: this could be easier with `det_cases`
  by_cases h : âˆƒ s : Finset E, Nonempty (Basis (â†¥s) ğ•œ E)
  Â· rcases h with âŸ¨s, âŸ¨bâŸ©âŸ©
    haveI : FiniteDimensional ğ•œ E := FiniteDimensional.of_fintype_basis b
    classical
    simp_rw [LinearMap.det_eq_det_toMatrix_of_finset b]
    refine Continuous.matrix_det ?_
    exact
      ((LinearMap.toMatrix b b).toLinearMap.comp
          (ContinuousLinearMap.coeLM ğ•œ)).continuous_of_finiteDimensional
  Â· rw [LinearMap.det]
    simpa only [h, MonoidHom.one_apply, dif_neg, not_false_iff] using continuous_const

/-- Any `K`-Lipschitz map from a subset `s` of a metric space `Î±` to a finite-dimensional real
vector space `E'` can be extended to a Lipschitz map on the whole space `Î±`, with a slightly worse
constant `C * K` where `C` only depends on `E'`. We record a working value for this constant `C`
as `lipschitzExtensionConstant E'`. -/
irreducible_def lipschitzExtensionConstant (E' : Type*) [NormedAddCommGroup E'] [NormedSpace â„ E']
  [FiniteDimensional â„ E'] : â„â‰¥0 :=
  let A := (Basis.ofVectorSpace â„ E').equivFun.toContinuousLinearEquiv
  max (â€–A.symm.toContinuousLinearMapâ€–â‚Š * â€–A.toContinuousLinearMapâ€–â‚Š) 1

theorem lipschitzExtensionConstant_pos (E' : Type*) [NormedAddCommGroup E'] [NormedSpace â„ E']
    [FiniteDimensional â„ E'] : 0 < lipschitzExtensionConstant E' := by
  rw [lipschitzExtensionConstant]
  exact zero_lt_one.trans_le (le_max_right _ _)

/-- Any `K`-Lipschitz map from a subset `s` of a metric space `Î±` to a finite-dimensional real
vector space `E'` can be extended to a Lipschitz map on the whole space `Î±`, with a slightly worse
constant `lipschitzExtensionConstant E' * K`. -/
theorem LipschitzOnWith.extend_finite_dimension {Î± : Type*} [PseudoMetricSpace Î±] {E' : Type*}
    [NormedAddCommGroup E'] [NormedSpace â„ E'] [FiniteDimensional â„ E'] {s : Set Î±} {f : Î± â†’ E'}
    {K : â„â‰¥0} (hf : LipschitzOnWith K f s) :
    âˆƒ g : Î± â†’ E', LipschitzWith (lipschitzExtensionConstant E' * K) g âˆ§ EqOn f g s := by
  /- This result is already known for spaces `Î¹ â†’ â„`. We use a continuous linear equiv between
    `E'` and such a space to transfer the result to `E'`. -/
  let Î¹ : Type _ := Basis.ofVectorSpaceIndex â„ E'
  let A := (Basis.ofVectorSpace â„ E').equivFun.toContinuousLinearEquiv
  have LA : LipschitzWith â€–A.toContinuousLinearMapâ€–â‚Š A := by apply A.lipschitz
  have L : LipschitzOnWith (â€–A.toContinuousLinearMapâ€–â‚Š * K) (A âˆ˜ f) s :=
    LA.comp_lipschitzOnWith hf
  obtain âŸ¨g, hg, gsâŸ© :
    âˆƒ g : Î± â†’ Î¹ â†’ â„, LipschitzWith (â€–A.toContinuousLinearMapâ€–â‚Š * K) g âˆ§ EqOn (A âˆ˜ f) g s :=
    L.extend_pi
  refine âŸ¨A.symm âˆ˜ g, ?_, ?_âŸ©
  Â· have LAsymm : LipschitzWith â€–A.symm.toContinuousLinearMapâ€–â‚Š A.symm := by
      apply A.symm.lipschitz
    apply (LAsymm.comp hg).weaken
    rw [lipschitzExtensionConstant, â† mul_assoc]
    exact mul_le_mul' (le_max_left _ _) le_rfl
  Â· intro x hx
    have : A (f x) = g x := gs hx
    simp only [(Â· âˆ˜ Â·), â† this, A.symm_apply_apply]

theorem LinearMap.exists_antilipschitzWith [FiniteDimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F)
    (hf : LinearMap.ker f = âŠ¥) : âˆƒ K > 0, AntilipschitzWith K f := by
  cases subsingleton_or_nontrivial E
  Â· exact âŸ¨1, zero_lt_one, AntilipschitzWith.of_subsingletonâŸ©
  Â· rw [LinearMap.ker_eq_bot] at hf
    let e : E â‰ƒL[ğ•œ] LinearMap.range f := (LinearEquiv.ofInjective f hf).toContinuousLinearEquiv
    exact âŸ¨_, e.nnnorm_symm_pos, e.antilipschitzâŸ©

open Function in
/-- A `LinearMap` on a finite-dimensional space over a complete field
  is injective iff it is anti-Lipschitz. -/
theorem LinearMap.injective_iff_antilipschitz [FiniteDimensional ğ•œ E] (f : E â†’â‚—[ğ•œ] F) :
    Injective f â†” âˆƒ K > 0, AntilipschitzWith K f := by
  constructor
  Â· rw [â† LinearMap.ker_eq_bot]
    exact f.exists_antilipschitzWith
  Â· rintro âŸ¨K, -, HâŸ©
    exact H.injective

open Function in
/-- The set of injective continuous linear maps `E â†’ F` is open,
  if `E` is finite-dimensional over a complete field. -/
theorem ContinuousLinearMap.isOpen_injective [FiniteDimensional ğ•œ E] :
    IsOpen { L : E â†’L[ğ•œ] F | Injective L } := by
  rw [isOpen_iff_eventually]
  rintro Ï†â‚€ hÏ†â‚€
  rcases Ï†â‚€.injective_iff_antilipschitz.mp hÏ†â‚€ with âŸ¨K, K_pos, HâŸ©
  have : âˆ€á¶  Ï† in ğ“ Ï†â‚€, â€–Ï† - Ï†â‚€â€–â‚Š < Kâ»Â¹ := eventually_nnnorm_sub_lt _ <| inv_pos_of_pos K_pos
  filter_upwards [this] with Ï† hÏ†
  apply Ï†.injective_iff_antilipschitz.mpr
  exact âŸ¨(Kâ»Â¹ - â€–Ï† - Ï†â‚€â€–â‚Š)â»Â¹, inv_pos_of_pos (tsub_pos_of_lt hÏ†),
    H.add_sub_lipschitzWith (Ï† - Ï†â‚€).lipschitz hÏ†âŸ©

protected theorem LinearIndependent.eventually {Î¹} [Finite Î¹] {f : Î¹ â†’ E}
    (hf : LinearIndependent ğ•œ f) : âˆ€á¶  g in ğ“ f, LinearIndependent ğ•œ g := by
  cases nonempty_fintype Î¹
  classical
  simp only [Fintype.linearIndependent_iff'] at hf âŠ¢
  rcases LinearMap.exists_antilipschitzWith _ hf with âŸ¨K, K0, hKâŸ©
  have : Tendsto (fun g : Î¹ â†’ E => âˆ‘ i, â€–g i - f iâ€–) (ğ“ f) (ğ“ <| âˆ‘ i, â€–f i - f iâ€–) :=
    tendsto_finset_sum _ fun i _ =>
      Tendsto.norm <| ((continuous_apply i).tendsto _).sub tendsto_const_nhds
  simp only [sub_self, norm_zero, Finset.sum_const_zero] at this
  refine (this.eventually (gt_mem_nhds <| inv_pos.2 K0)).mono fun g hg => ?_
  replace hg : âˆ‘ i, â€–g i - f iâ€–â‚Š < Kâ»Â¹ := by
    rw [â† NNReal.coe_lt_coe]
    push_cast
    exact hg
  rw [LinearMap.ker_eq_bot]
  refine (hK.add_sub_lipschitzWith (LipschitzWith.of_dist_le_mul fun v u => ?_) hg).injective
  simp only [dist_eq_norm, LinearMap.lsum_apply, Pi.sub_apply, LinearMap.sum_apply,
    LinearMap.comp_apply, LinearMap.proj_apply, LinearMap.smulRight_apply, LinearMap.id_apply, â†
    Finset.sum_sub_distrib, â† smul_sub, â† sub_smul, NNReal.coe_sum, coe_nnnorm, Finset.sum_mul]
  refine norm_sum_le_of_le _ fun i _ => ?_
  rw [norm_smul, mul_comm]
  gcongr
  exact norm_le_pi_norm (v - u) i

theorem isOpen_setOf_linearIndependent {Î¹ : Type*} [Finite Î¹] :
    IsOpen { f : Î¹ â†’ E | LinearIndependent ğ•œ f } :=
  isOpen_iff_mem_nhds.2 fun _ => LinearIndependent.eventually

theorem isOpen_setOf_nat_le_rank (n : â„•) :
    IsOpen { f : E â†’L[ğ•œ] F | â†‘n â‰¤ (f : E â†’â‚—[ğ•œ] F).rank } := by
  simp only [LinearMap.le_rank_iff_exists_linearIndependent_finset, setOf_exists, â† exists_prop]
  refine isOpen_biUnion fun t _ => ?_
  have : Continuous fun f : E â†’L[ğ•œ] F => fun x : (t : Set E) => f x :=
    continuous_pi fun x => (ContinuousLinearMap.apply ğ•œ F (x : E)).continuous
  exact isOpen_setOf_linearIndependent.preimage this

namespace Module.Basis

theorem opNNNorm_le {Î¹ : Type*} [Fintype Î¹] (v : Basis Î¹ ğ•œ E) {u : E â†’L[ğ•œ] F} (M : â„â‰¥0)
    (hu : âˆ€ i, â€–u (v i)â€–â‚Š â‰¤ M) : â€–uâ€–â‚Š â‰¤ Fintype.card Î¹ â€¢ â€–v.equivFunL.toContinuousLinearMapâ€–â‚Š * M :=
  u.opNNNorm_le_bound _ fun e => by
    set Ï† := v.equivFunL.toContinuousLinearMap
    calc
      â€–u eâ€–â‚Š = â€–u (âˆ‘ i, v.equivFun e i â€¢ v i)â€–â‚Š := by rw [v.sum_equivFun]
      _ = â€–âˆ‘ i, v.equivFun e i â€¢ (u <| v i)â€–â‚Š := by simp [map_sum]
      _ â‰¤ âˆ‘ i, â€–v.equivFun e i â€¢ (u <| v i)â€–â‚Š := nnnorm_sum_le _ _
      _ = âˆ‘ i, â€–v.equivFun e iâ€–â‚Š * â€–u (v i)â€–â‚Š := by simp only [nnnorm_smul]
      _ â‰¤ âˆ‘ i, â€–v.equivFun e iâ€–â‚Š * M := by gcongr; apply hu
      _ = (âˆ‘ i, â€–v.equivFun e iâ€–â‚Š) * M := by rw [Finset.sum_mul]
      _ â‰¤ Fintype.card Î¹ â€¢ (â€–Ï†â€–â‚Š * â€–eâ€–â‚Š) * M := by
        gcongr
        calc
          âˆ‘ i, â€–v.equivFun e iâ€–â‚Š â‰¤ Fintype.card Î¹ â€¢ â€–Ï† eâ€–â‚Š := Pi.sum_nnnorm_apply_le_nnnorm _
          _ â‰¤ Fintype.card Î¹ â€¢ (â€–Ï†â€–â‚Š * â€–eâ€–â‚Š) := nsmul_le_nsmul_right (Ï†.le_opNNNorm e) _
      _ = Fintype.card Î¹ â€¢ â€–Ï†â€–â‚Š * M * â€–eâ€–â‚Š := by simp only [smul_mul_assoc, mul_right_comm]

theorem opNorm_le {Î¹ : Type*} [Fintype Î¹] (v : Basis Î¹ ğ•œ E) {u : E â†’L[ğ•œ] F} {M : â„}
    (hM : 0 â‰¤ M) (hu : âˆ€ i, â€–u (v i)â€– â‰¤ M) :
    â€–uâ€– â‰¤ Fintype.card Î¹ â€¢ â€–v.equivFunL.toContinuousLinearMapâ€– * M := by
  simpa using NNReal.coe_le_coe.mpr (v.opNNNorm_le âŸ¨M, hMâŸ© hu)

/-- A weaker version of `Basis.opNNNorm_le` that abstracts away the value of `C`. -/
theorem exists_opNNNorm_le {Î¹ : Type*} [Finite Î¹] (v : Basis Î¹ ğ•œ E) :
    âˆƒ C > (0 : â„â‰¥0), âˆ€ {u : E â†’L[ğ•œ] F} (M : â„â‰¥0), (âˆ€ i, â€–u (v i)â€–â‚Š â‰¤ M) â†’ â€–uâ€–â‚Š â‰¤ C * M := by
  cases nonempty_fintype Î¹
  exact
    âŸ¨max (Fintype.card Î¹ â€¢ â€–v.equivFunL.toContinuousLinearMapâ€–â‚Š) 1,
      zero_lt_one.trans_le (le_max_right _ _), fun {u} M hu =>
      (v.opNNNorm_le M hu).trans <| mul_le_mul_of_nonneg_right (le_max_left _ _) (zero_le M)âŸ©

/-- A weaker version of `Basis.opNorm_le` that abstracts away the value of `C`. -/
theorem exists_opNorm_le {Î¹ : Type*} [Finite Î¹] (v : Basis Î¹ ğ•œ E) :
    âˆƒ C > (0 : â„), âˆ€ {u : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, â€–u (v i)â€– â‰¤ M) â†’ â€–uâ€– â‰¤ C * M := by
  obtain âŸ¨C, hC, hâŸ© := v.exists_opNNNorm_le (F := F)
  refine âŸ¨C, hC, ?_âŸ©
  intro u M hM H
  simpa using h âŸ¨M, hMâŸ© H

end Module.Basis

instance [FiniteDimensional ğ•œ E] [SecondCountableTopology F] :
    SecondCountableTopology (E â†’L[ğ•œ] F) := by
  set d := Module.finrank ğ•œ E
  suffices
    âˆ€ Îµ > (0 : â„), âˆƒ n : (E â†’L[ğ•œ] F) â†’ Fin d â†’ â„•, âˆ€ f g : E â†’L[ğ•œ] F, n f = n g â†’ dist f g â‰¤ Îµ from
    Metric.secondCountable_of_countable_discretization fun Îµ Îµ_pos =>
      âŸ¨Fin d â†’ â„•, by infer_instance, this Îµ Îµ_posâŸ©
  intro Îµ Îµ_pos
  obtain âŸ¨u : â„• â†’ F, hu : DenseRange uâŸ© := exists_dense_seq F
  let v := Module.finBasis ğ•œ E
  obtain
    âŸ¨C : â„, C_pos : 0 < C, hC :
      âˆ€ {Ï† : E â†’L[ğ•œ] F} {M : â„}, 0 â‰¤ M â†’ (âˆ€ i, â€–Ï† (v i)â€– â‰¤ M) â†’ â€–Ï†â€– â‰¤ C * MâŸ© :=
    v.exists_opNorm_le (E := E) (F := F)
  have h_2C : 0 < 2 * C := mul_pos zero_lt_two C_pos
  have hÎµ2C : 0 < Îµ / (2 * C) := div_pos Îµ_pos h_2C
  have : âˆ€ Ï† : E â†’L[ğ•œ] F, âˆƒ n : Fin d â†’ â„•, â€–Ï† - (v.constrL <| u âˆ˜ n)â€– â‰¤ Îµ / 2 := by
    intro Ï†
    have : âˆ€ i, âˆƒ n, â€–Ï† (v i) - u nâ€– â‰¤ Îµ / (2 * C) := by
      simp only [norm_sub_rev]
      intro i
      have : Ï† (v i) âˆˆ closure (range u) := hu _
      obtain âŸ¨n, hnâŸ© : âˆƒ n, â€–u n - Ï† (v i)â€– < Îµ / (2 * C) := by
        rw [mem_closure_iff_nhds_basis Metric.nhds_basis_ball] at this
        specialize this (Îµ / (2 * C)) hÎµ2C
        simpa [dist_eq_norm]
      exact âŸ¨n, le_of_lt hnâŸ©
    choose n hn using this
    use n
    replace hn : âˆ€ i : Fin d, â€–(Ï† - (v.constrL <| u âˆ˜ n)) (v i)â€– â‰¤ Îµ / (2 * C) := by simp [hn]
    have : C * (Îµ / (2 * C)) = Îµ / 2 := by
      rw [eq_div_iff (two_ne_zero : (2 : â„) â‰  0), mul_comm, â† mul_assoc,
        mul_div_cancelâ‚€ _ (ne_of_gt h_2C)]
    specialize hC (le_of_lt hÎµ2C) hn
    rwa [this] at hC
  choose n hn using this
  set Î¦ := fun Ï† : E â†’L[ğ•œ] F => v.constrL <| u âˆ˜ n Ï†
  change âˆ€ z, dist z (Î¦ z) â‰¤ Îµ / 2 at hn
  use n
  intro x y hxy
  calc
    dist x y â‰¤ dist x (Î¦ x) + dist (Î¦ x) y := dist_triangle _ _ _
    _ = dist x (Î¦ x) + dist y (Î¦ y) := by simp [Î¦, hxy, dist_comm]
    _ â‰¤ Îµ := by linarith [hn x, hn y]

theorem AffineSubspace.closed_of_finiteDimensional {P : Type*} [MetricSpace P]
    [NormedAddTorsor E P] (s : AffineSubspace ğ•œ P) [FiniteDimensional ğ•œ s.direction] :
    IsClosed (s : Set P) :=
  s.isClosed_direction_iff.mp s.direction.closed_of_finiteDimensional

section Riesz

/-- In an infinite dimensional space, given a finite number of points, one may find a point
with norm at most `R` which is at distance at least `1` of all these points. -/
theorem exists_norm_le_le_norm_sub_of_finset {c : ğ•œ} (hc : 1 < â€–câ€–) {R : â„} (hR : â€–câ€– < R)
    (h : Â¬FiniteDimensional ğ•œ E) (s : Finset E) : âˆƒ x : E, â€–xâ€– â‰¤ R âˆ§ âˆ€ y âˆˆ s, 1 â‰¤ â€–y - xâ€– := by
  let F := Submodule.span ğ•œ (s : Set E)
  haveI : FiniteDimensional ğ•œ F :=
    Module.finite_def.2
      ((Submodule.fg_top _).2 (Submodule.fg_def.2 âŸ¨s, Finset.finite_toSet _, rflâŸ©))
  have Fclosed : IsClosed (F : Set E) := Submodule.closed_of_finiteDimensional _
  have : âˆƒ x, x âˆ‰ F := by
    contrapose! h
    have : (âŠ¤ : Submodule ğ•œ E) = F := by
      ext x
      simp [h]
    have : FiniteDimensional ğ•œ (âŠ¤ : Submodule ğ•œ E) := by rwa [this]
    exact Module.finite_def.2 ((Submodule.fg_top _).1 (Module.finite_def.1 this))
  obtain âŸ¨x, xR, hxâŸ© : âˆƒ x : E, â€–xâ€– â‰¤ R âˆ§ âˆ€ y : E, y âˆˆ F â†’ 1 â‰¤ â€–x - yâ€– :=
    riesz_lemma_of_norm_lt hc hR Fclosed this
  have hx' : âˆ€ y : E, y âˆˆ F â†’ 1 â‰¤ â€–y - xâ€– := by
    intro y hy
    rw [â† norm_neg]
    simpa using hx y hy
  exact âŸ¨x, xR, fun y hy => hx' _ (Submodule.subset_span hy)âŸ©

/-- In an infinite-dimensional normed space, there exists a sequence of points which are all
bounded by `R` and at distance at least `1`. For a version not assuming `c` and `R`, see
`exists_seq_norm_le_one_le_norm_sub`. -/
theorem exists_seq_norm_le_one_le_norm_sub' {c : ğ•œ} (hc : 1 < â€–câ€–) {R : â„} (hR : â€–câ€– < R)
    (h : Â¬FiniteDimensional ğ•œ E) :
    âˆƒ f : â„• â†’ E, (âˆ€ n, â€–f nâ€– â‰¤ R) âˆ§ Pairwise fun m n => 1 â‰¤ â€–f m - f nâ€– := by
  have : IsSymm E fun x y : E => 1 â‰¤ â€–x - yâ€– := by
    constructor
    intro x y hxy
    rw [â† norm_neg]
    simpa
  apply
    exists_seq_of_forall_finset_exists' (fun x : E => â€–xâ€– â‰¤ R) fun (x : E) (y : E) => 1 â‰¤ â€–x - yâ€–
  rintro s -
  exact exists_norm_le_le_norm_sub_of_finset hc hR h s

theorem exists_seq_norm_le_one_le_norm_sub (h : Â¬FiniteDimensional ğ•œ E) :
    âˆƒ (R : â„) (f : â„• â†’ E), 1 < R âˆ§ (âˆ€ n, â€–f nâ€– â‰¤ R) âˆ§ Pairwise fun m n => 1 â‰¤ â€–f m - f nâ€– := by
  obtain âŸ¨c, hcâŸ© : âˆƒ c : ğ•œ, 1 < â€–câ€– := NormedField.exists_one_lt_norm ğ•œ
  have A : â€–câ€– < â€–câ€– + 1 := by linarith
  rcases exists_seq_norm_le_one_le_norm_sub' hc A h with âŸ¨f, hfâŸ©
  exact âŸ¨â€–câ€– + 1, f, hc.trans A, hf.1, hf.2âŸ©

variable (ğ•œ)

/-- **Riesz's theorem**: if a closed ball with center zero of positive radius is compact in a vector
space, then the space is finite-dimensional. -/
theorem FiniteDimensional.of_isCompact_closedBallâ‚€ {r : â„} (rpos : 0 < r)
    (h : IsCompact (Metric.closedBall (0 : E) r)) : FiniteDimensional ğ•œ E := by
  by_contra hfin
  obtain âŸ¨R, f, Rgt, fle, lefâŸ© :
    âˆƒ (R : â„) (f : â„• â†’ E), 1 < R âˆ§ (âˆ€ n, â€–f nâ€– â‰¤ R) âˆ§ Pairwise fun m n => 1 â‰¤ â€–f m - f nâ€– :=
    exists_seq_norm_le_one_le_norm_sub hfin
  have rRpos : 0 < r / R := div_pos rpos (zero_lt_one.trans Rgt)
  obtain âŸ¨c, hcâŸ© : âˆƒ c : ğ•œ, 0 < â€–câ€– âˆ§ â€–câ€– < r / R := NormedField.exists_norm_lt _ rRpos
  let g := fun n : â„• => c â€¢ f n
  have A : âˆ€ n, g n âˆˆ Metric.closedBall (0 : E) r := by
    intro n
    simp only [g, norm_smul, dist_zero_right, Metric.mem_closedBall]
    calc
      â€–câ€– * â€–f nâ€– â‰¤ r / R * R := by
        gcongr
        Â· exact hc.2.le
        Â· apply fle
      _ = r := by field_simp [(zero_lt_one.trans Rgt).ne']
  obtain âŸ¨x : E, _ : x âˆˆ Metric.closedBall (0 : E) r, Ï† : â„• â†’ â„•, Ï†mono : StrictMono Ï†,
    Ï†lim : Tendsto (g âˆ˜ Ï†) atTop (ğ“ x)âŸ© := h.tendsto_subseq A
  have B : CauchySeq (g âˆ˜ Ï†) := Ï†lim.cauchySeq
  obtain âŸ¨N, hNâŸ© : âˆƒ N : â„•, âˆ€ n : â„•, N â‰¤ n â†’ dist ((g âˆ˜ Ï†) n) ((g âˆ˜ Ï†) N) < â€–câ€– :=
    Metric.cauchySeq_iff'.1 B â€–câ€– hc.1
  apply lt_irrefl â€–câ€–
  calc
    â€–câ€– â‰¤ dist (g (Ï† (N + 1))) (g (Ï† N)) := by
      conv_lhs => rw [â† mul_one â€–câ€–]
      simp only [g, dist_eq_norm, â† smul_sub, norm_smul]
      gcongr
      apply lef (ne_of_gt _)
      exact Ï†mono (Nat.lt_succ_self N)
    _ < â€–câ€– := hN (N + 1) (Nat.le_succ N)

/-- **Riesz's theorem**: if a closed ball of positive radius is compact in a vector space, then the
space is finite-dimensional. -/
theorem FiniteDimensional.of_isCompact_closedBall {r : â„} (rpos : 0 < r) {c : E}
    (h : IsCompact (Metric.closedBall c r)) : FiniteDimensional ğ•œ E :=
  .of_isCompact_closedBallâ‚€ ğ•œ rpos <| by simpa using h.vadd (-c)

/-- **Riesz's theorem**: a locally compact normed vector space is finite-dimensional. -/
theorem FiniteDimensional.of_locallyCompactSpace [LocallyCompactSpace E] :
    FiniteDimensional ğ•œ E :=
  let âŸ¨_r, rpos, hrâŸ© := exists_isCompact_closedBall (0 : E)
  .of_isCompact_closedBallâ‚€ ğ•œ rpos hr

/-- If a function has compact support, then either the function is trivial
or the space is finite-dimensional. -/
theorem HasCompactSupport.eq_zero_or_finiteDimensional {X : Type*} [TopologicalSpace X] [Zero X]
    [T1Space X] {f : E â†’ X} (hf : HasCompactSupport f) (h'f : Continuous f) :
    f = 0 âˆ¨ FiniteDimensional ğ•œ E :=
  (HasCompactSupport.eq_zero_or_locallyCompactSpace_of_addGroup hf h'f).imp_right fun h â†¦
    -- TODO: Lean doesn't find the instance without this `have`
    have : LocallyCompactSpace E := h; .of_locallyCompactSpace ğ•œ

/-- If a function has compact multiplicative support, then either the function is trivial
or the space is finite-dimensional. -/
@[to_additive existing]
theorem HasCompactMulSupport.eq_one_or_finiteDimensional {X : Type*} [TopologicalSpace X] [One X]
    [T1Space X] {f : E â†’ X} (hf : HasCompactMulSupport f) (h'f : Continuous f) :
    f = 1 âˆ¨ FiniteDimensional ğ•œ E :=
  have : T1Space (Additive X) := â€¹_â€º
  HasCompactSupport.eq_zero_or_finiteDimensional (X := Additive X) ğ•œ hf h'f

/-- A locally compact normed vector space is proper. -/
lemma ProperSpace.of_locallyCompactSpace (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
    {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] [LocallyCompactSpace E] :
    ProperSpace E := by
  rcases exists_isCompact_closedBall (0 : E) with âŸ¨r, rpos, hrâŸ©
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  have hC : âˆ€ n, IsCompact (closedBall (0 : E) (â€–câ€–^n * r)) := fun n â†¦ by
    have : c ^ n â‰  0 := pow_ne_zero _ <| fun h â†¦ by simp [h, zero_le_one.not_gt] at hc
    simpa [_root_.smul_closedBall' this] using hr.smul (c ^ n)
  have hTop : Tendsto (fun n â†¦ â€–câ€–^n * r) atTop atTop :=
    Tendsto.atTop_mul_const rpos (tendsto_pow_atTop_atTop_of_one_lt hc)
  exact .of_seq_closedBall hTop (Eventually.of_forall hC)

variable (E)
lemma ProperSpace.of_locallyCompact_module [Nontrivial E] [LocallyCompactSpace E] :
    ProperSpace ğ•œ :=
  have : LocallyCompactSpace ğ•œ := by
    obtain âŸ¨v, hvâŸ© : âˆƒ v : E, v â‰  0 := exists_ne 0
    let L : ğ•œ â†’ E := fun t â†¦ t â€¢ v
    have : IsClosedEmbedding L := isClosedEmbedding_smul_left hv
    apply IsClosedEmbedding.locallyCompactSpace this
  .of_locallyCompactSpace ğ•œ

end Riesz

open ContinuousLinearMap

/-- Continuous linear equivalence between continuous linear functions `ğ•œâ¿ â†’ E` and `Eâ¿`.
The spaces `ğ•œâ¿` and `Eâ¿` are represented as `Î¹ â†’ ğ•œ` and `Î¹ â†’ E`, respectively,
where `Î¹` is a finite type. -/
def ContinuousLinearEquiv.piRing (Î¹ : Type*) [Fintype Î¹] [DecidableEq Î¹] :
    ((Î¹ â†’ ğ•œ) â†’L[ğ•œ] E) â‰ƒL[ğ•œ] Î¹ â†’ E :=
  { LinearMap.toContinuousLinearMap.symm.trans (LinearEquiv.piRing ğ•œ E Î¹ ğ•œ) with
    continuous_toFun := by
      refine continuous_pi fun i => ?_
      exact (ContinuousLinearMap.apply ğ•œ E (Pi.single i 1)).continuous
    continuous_invFun := by
      simp_rw [LinearEquiv.invFun_eq_symm, LinearEquiv.trans_symm, LinearEquiv.symm_symm]
      -- Note: added explicit type and removed `change` that tried to achieve the same
      refine AddMonoidHomClass.continuous_of_bound
        (LinearMap.toContinuousLinearMap.toLinearMap.comp
            (LinearEquiv.piRing ğ•œ E Î¹ ğ•œ).symm.toLinearMap)
        (Fintype.card Î¹ : â„) fun g => ?_
      rw [â† nsmul_eq_mul]
      refine opNorm_le_bound _ (nsmul_nonneg (norm_nonneg g) (Fintype.card Î¹)) fun t => ?_
      simp_rw [LinearMap.coe_comp, LinearEquiv.coe_toLinearMap, Function.comp_apply,
        LinearMap.coe_toContinuousLinearMap', LinearEquiv.piRing_symm_apply]
      apply le_trans (norm_sum_le _ _)
      rw [smul_mul_assoc]
      refine Finset.sum_le_card_nsmul _ _ _ fun i _ => ?_
      rw [norm_smul, mul_comm]
      gcongr <;> apply norm_le_pi_norm }

/-- A family of continuous linear maps is continuous on `s` if all its applications are. -/
theorem continuousOn_clm_apply {X : Type*} [TopologicalSpace X] [FiniteDimensional ğ•œ E]
    {f : X â†’ E â†’L[ğ•œ] F} {s : Set X} : ContinuousOn f s â†” âˆ€ y, ContinuousOn (fun x => f x y) s := by
  refine âŸ¨fun h y => (ContinuousLinearMap.apply ğ•œ F y).continuous.comp_continuousOn h, fun h => ?_âŸ©
  let d := finrank ğ•œ E
  have hd : d = finrank ğ•œ (Fin d â†’ ğ•œ) := (finrank_fin_fun ğ•œ).symm
  let eâ‚ : E â‰ƒL[ğ•œ] Fin d â†’ ğ•œ := ContinuousLinearEquiv.ofFinrankEq hd
  let eâ‚‚ : (E â†’L[ğ•œ] F) â‰ƒL[ğ•œ] Fin d â†’ F :=
    (eâ‚.arrowCongr (1 : F â‰ƒL[ğ•œ] F)).trans (ContinuousLinearEquiv.piRing (Fin d))
  rw [â† f.id_comp, â† eâ‚‚.symm_comp_self]
  exact eâ‚‚.symm.continuous.comp_continuousOn (continuousOn_pi.mpr fun i => h _)

theorem continuous_clm_apply {X : Type*} [TopologicalSpace X] [FiniteDimensional ğ•œ E]
    {f : X â†’ E â†’L[ğ•œ] F} : Continuous f â†” âˆ€ y, Continuous (f Â· y) := by
  simp_rw [â† continuousOn_univ, continuousOn_clm_apply]

end CompleteField

section LocallyCompactField

variable (ğ•œ : Type u) [NontriviallyNormedField ğ•œ] (E : Type v) [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [LocallyCompactSpace ğ•œ]

/-- Any finite-dimensional vector space over a locally compact field is proper.
We do not register this as an instance to avoid an instance loop when trying to prove the
properness of `ğ•œ`, and the search for `ğ•œ` as an unknown metavariable. Declare the instance
explicitly when needed. -/
theorem FiniteDimensional.proper [FiniteDimensional ğ•œ E] : ProperSpace E := by
  have : ProperSpace ğ•œ := .of_locallyCompactSpace ğ•œ
  set e := ContinuousLinearEquiv.ofFinrankEq (@finrank_fin_fun ğ•œ _ _ (finrank ğ•œ E)).symm
  exact e.symm.antilipschitz.properSpace e.symm.continuous e.symm.surjective

end LocallyCompactField

/- Over the real numbers, we can register the previous statement as an instance as it will not
cause problems in instance resolution since the properness of `â„` is already known. -/
instance (priority := 900) FiniteDimensional.proper_real (E : Type u) [NormedAddCommGroup E]
    [NormedSpace â„ E] [FiniteDimensional â„ E] : ProperSpace E :=
  FiniteDimensional.proper â„ E

/-- A submodule of a locally compact space over a complete field is also locally compact (and even
proper). -/
instance {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
    [NormedAddCommGroup E] [NormedSpace ğ•œ E] [LocallyCompactSpace E] (S : Submodule ğ•œ E) :
    ProperSpace S := by
  nontriviality E
  have : ProperSpace ğ•œ := .of_locallyCompact_module ğ•œ E
  have : FiniteDimensional ğ•œ E := .of_locallyCompactSpace ğ•œ
  exact FiniteDimensional.proper ğ•œ S

/-- If `E` is a finite dimensional normed real vector space, `x : E`, and `s` is a neighborhood of
`x` that is not equal to the whole space, then there exists a point `y âˆˆ frontier s` at distance
`Metric.infDist x sá¶œ` from `x`. See also
`IsCompact.exists_mem_frontier_infDist_compl_eq_dist`. -/
theorem exists_mem_frontier_infDist_compl_eq_dist {E : Type*} [NormedAddCommGroup E]
    [NormedSpace â„ E] [FiniteDimensional â„ E] {x : E} {s : Set E} (hx : x âˆˆ s) (hs : s â‰  univ) :
    âˆƒ y âˆˆ frontier s, Metric.infDist x sá¶œ = dist x y := by
  rcases Metric.exists_mem_closure_infDist_eq_dist (nonempty_compl.2 hs) x with âŸ¨y, hys, hydâŸ©
  rw [closure_compl] at hys
  refine âŸ¨y, âŸ¨Metric.closedBall_infDist_compl_subset_closure hx <|
    Metric.mem_closedBall.2 <| ge_of_eq ?_, hysâŸ©, hydâŸ©
  rwa [dist_comm]

/-- If `K` is a compact set in a nontrivial real normed space and `x âˆˆ K`, then there exists a point
`y` of the boundary of `K` at distance `Metric.infDist x Ká¶œ` from `x`. See also
`exists_mem_frontier_infDist_compl_eq_dist`. -/
nonrec theorem IsCompact.exists_mem_frontier_infDist_compl_eq_dist {E : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E] [Nontrivial E] {x : E} {K : Set E} (hK : IsCompact K)
    (hx : x âˆˆ K) :
    âˆƒ y âˆˆ frontier K, Metric.infDist x Ká¶œ = dist x y := by
  obtain hx' | hx' : x âˆˆ interior K âˆª frontier K := by
    rw [â† closure_eq_interior_union_frontier]
    exact subset_closure hx
  Â· rw [mem_interior_iff_mem_nhds, Metric.nhds_basis_closedBall.mem_iff] at hx'
    rcases hx' with âŸ¨r, hrâ‚€, hrKâŸ©
    have : FiniteDimensional â„ E :=
      .of_isCompact_closedBall â„ hrâ‚€
        (hK.of_isClosed_subset Metric.isClosed_closedBall hrK)
    exact exists_mem_frontier_infDist_compl_eq_dist hx hK.ne_univ
  Â· refine âŸ¨x, hx', ?_âŸ©
    rw [frontier_eq_closure_inter_closure] at hx'
    rw [Metric.infDist_zero_of_mem_closure hx'.2, dist_self]

/-- In a finite dimensional vector space over `â„`, the series `âˆ‘ x, â€–f xâ€–` is unconditionally
summable if and only if the series `âˆ‘ x, f x` is unconditionally summable. One implication holds in
any complete normed space, while the other holds only in finite dimensional spaces. -/
theorem summable_norm_iff {Î± E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f : Î± â†’ E} : (Summable fun x => â€–f xâ€–) â†” Summable f := by
  refine âŸ¨Summable.of_norm, fun hf â†¦ ?_âŸ©
  -- First we use a finite basis to reduce the problem to the case `E = Fin N â†’ â„`
  suffices âˆ€ {N : â„•} {g : Î± â†’ Fin N â†’ â„}, Summable g â†’ Summable fun x => â€–g xâ€– by
    obtain v := Module.finBasis â„ E
    set e := v.equivFunL
    have H : Summable fun x => â€–e (f x)â€– := this (e.summable.2 hf)
    refine .of_norm_bounded (H.mul_left â†‘â€–(e.symm : (Fin (finrank â„ E) â†’ â„) â†’L[â„] E)â€–â‚Š) fun i â†¦ ?_
    simpa using (e.symm : (Fin (finrank â„ E) â†’ â„) â†’L[â„] E).le_opNorm (e <| f i)
  clear! E
  -- Now we deal with `g : Î± â†’ Fin N â†’ â„`
  intro N g hg
  have : âˆ€ i, Summable fun x => â€–g x iâ€– := fun i => (Pi.summable.1 hg i).abs
  refine .of_norm_bounded (summable_sum fun i (_ : i âˆˆ Finset.univ) => this i) fun x => ?_
  rw [norm_norm, pi_norm_le_iff_of_nonneg]
  Â· refine fun i => Finset.single_le_sum (f := fun i => â€–g x iâ€–) (fun i _ => ?_) (Finset.mem_univ i)
    exact norm_nonneg (g x i)
  Â· exact Finset.sum_nonneg fun _ _ => norm_nonneg _

alias âŸ¨_, Summable.normâŸ© := summable_norm_iff

theorem summable_of_sum_range_norm_le {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {c : â„} {f : â„• â†’ E} (h : âˆ€ n, âˆ‘ i âˆˆ Finset.range n, â€–f iâ€– â‰¤ c) :
    Summable f :=
  summable_norm_iff.mp <| summable_of_sum_range_le (fun _ â†¦ norm_nonneg _)  h

theorem summable_of_isBigO' {Î¹ E F : Type*} [NormedAddCommGroup E] [CompleteSpace E]
    [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F] {f : Î¹ â†’ E} {g : Î¹ â†’ F}
    (hg : Summable g) (h : f =O[cofinite] g) : Summable f :=
  summable_of_isBigO hg.norm h.norm_right

lemma Asymptotics.IsBigO.comp_summable {Î¹ E F : Type*}
    [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
    [NormedAddCommGroup F] [CompleteSpace F]
    {f : E â†’ F} (hf : f =O[ğ“ 0] id) {g : Î¹ â†’ E} (hg : Summable g) : Summable (f âˆ˜ g) :=
  .of_norm <| hf.comp_summable_norm hg.norm

theorem summable_of_isBigO_nat' {E F : Type*} [NormedAddCommGroup E] [CompleteSpace E]
    [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F] {f : â„• â†’ E} {g : â„• â†’ F}
    (hg : Summable g) (h : f =O[atTop] g) : Summable f :=
  summable_of_isBigO_nat hg.norm h.norm_right

theorem summable_of_isEquivalent {Î¹ E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f : Î¹ â†’ E} {g : Î¹ â†’ E} (hg : Summable g) (h : f ~[cofinite] g) :
    Summable f :=
  hg.trans_sub (summable_of_isBigO' hg h.isLittleO.isBigO)

theorem summable_of_isEquivalent_nat {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f : â„• â†’ E} {g : â„• â†’ E} (hg : Summable g) (h : f ~[atTop] g) :
    Summable f :=
  hg.trans_sub (summable_of_isBigO_nat' hg h.isLittleO.isBigO)

theorem IsEquivalent.summable_iff {Î¹ E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f : Î¹ â†’ E} {g : Î¹ â†’ E} (h : f ~[cofinite] g) :
    Summable f â†” Summable g :=
  âŸ¨fun hf => summable_of_isEquivalent hf h.symm, fun hg => summable_of_isEquivalent hg hâŸ©

theorem IsEquivalent.summable_iff_nat {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    [FiniteDimensional â„ E] {f : â„• â†’ E} {g : â„• â†’ E} (h : f ~[atTop] g) : Summable f â†” Summable g :=
  âŸ¨fun hf => summable_of_isEquivalent_nat hf h.symm, fun hg => summable_of_isEquivalent_nat hg hâŸ©
