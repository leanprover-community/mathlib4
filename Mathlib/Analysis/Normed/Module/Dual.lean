/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.LocallyConvex.Polar
import Mathlib.Analysis.NormedSpace.HahnBanach.Extension
import Mathlib.Analysis.NormedSpace.RCLike
import Mathlib.Data.Set.Finite.Lemmas
import Mathlib.Analysis.LocallyConvex.AbsConvex
import Mathlib.Analysis.Normed.Module.Convex

/-!
# The topological dual of a normed space

In this file we define the topological dual `NormedSpace.Dual` of a normed space, and the
continuous linear map `NormedSpace.inclusionInDoubleDual` from a normed space into its double
dual.

For base field `ğ•œ = â„` or `ğ•œ = â„‚`, this map is actually an isometric embedding; we provide a
version `NormedSpace.inclusionInDoubleDualLi` of the map which is of type a bundled linear
isometric embedding, `E â†’â‚—áµ¢[ğ•œ] (Dual ğ•œ (Dual ğ•œ E))`.

Since a lot of elementary properties don't require `eq_of_dist_eq_zero` we start setting up the
theory for `SeminormedAddCommGroup` and we specialize to `NormedAddCommGroup` when needed.

## Main definitions

* `inclusionInDoubleDual` and `inclusionInDoubleDualLi` are the inclusion of a normed space
  in its double dual, considered as a bounded linear map and as a linear isometry, respectively.
* `polar ğ•œ s` is the subset of `Dual ğ•œ E` consisting of those functionals `x'` for which
  `â€–x' zâ€– â‰¤ 1` for every `z âˆˆ s`.

## References

* [Conway, John B., A course in functional analysis][conway1990]

## Tags

dual, polar
-/


noncomputable section

open Topology Bornology

universe u v

namespace NormedSpace

section General

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable (E : Type*) [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
variable (F : Type*) [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- The topological dual of a seminormed space `E`. -/
abbrev Dual : Type _ := E â†’L[ğ•œ] ğ•œ

/-- The inclusion of a normed space in its double (topological) dual, considered
as a bounded linear map. -/
def inclusionInDoubleDual : E â†’L[ğ•œ] Dual ğ•œ (Dual ğ•œ E) :=
  ContinuousLinearMap.apply ğ•œ ğ•œ

@[simp]
theorem dual_def (x : E) (f : Dual ğ•œ E) : inclusionInDoubleDual ğ•œ E x f = f x :=
  rfl

theorem inclusionInDoubleDual_norm_eq :
    â€–inclusionInDoubleDual ğ•œ Eâ€– = â€–ContinuousLinearMap.id ğ•œ (Dual ğ•œ E)â€– :=
  ContinuousLinearMap.opNorm_flip _

theorem inclusionInDoubleDual_norm_le : â€–inclusionInDoubleDual ğ•œ Eâ€– â‰¤ 1 := by
  rw [inclusionInDoubleDual_norm_eq]
  exact ContinuousLinearMap.norm_id_le

theorem double_dual_bound (x : E) : â€–(inclusionInDoubleDual ğ•œ E) xâ€– â‰¤ â€–xâ€– := by
  simpa using ContinuousLinearMap.le_of_opNorm_le _ (inclusionInDoubleDual_norm_le ğ•œ E) x

/-- The dual pairing as a bilinear form. -/
def dualPairing : Dual ğ•œ E â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ :=
  ContinuousLinearMap.coeLM ğ•œ

@[simp]
theorem dualPairing_apply {v : Dual ğ•œ E} {x : E} : dualPairing ğ•œ E v x = v x :=
  rfl

theorem dualPairing_separatingLeft : (dualPairing ğ•œ E).SeparatingLeft := by
  rw [LinearMap.separatingLeft_iff_ker_eq_bot, LinearMap.ker_eq_bot]
  exact ContinuousLinearMap.coe_injective

end General

section BidualIsometry

variable (ğ•œ : Type v) [RCLike ğ•œ] {E : Type u} [NormedAddCommGroup E] [NormedSpace ğ•œ E]

/-- If one controls the norm of every `f x`, then one controls the norm of `x`.
    Compare `ContinuousLinearMap.opNorm_le_bound`. -/
theorem norm_le_dual_bound (x : E) {M : â„} (hMp : 0 â‰¤ M) (hM : âˆ€ f : Dual ğ•œ E, â€–f xâ€– â‰¤ M * â€–fâ€–) :
    â€–xâ€– â‰¤ M := by
  classical
    by_cases h : x = 0
    Â· simp only [h, hMp, norm_zero]
    Â· obtain âŸ¨f, hfâ‚, hfxâŸ© : âˆƒ f : E â†’L[ğ•œ] ğ•œ, â€–fâ€– = 1 âˆ§ f x = â€–xâ€– := exists_dual_vector ğ•œ x h
      calc
        â€–xâ€– = â€–(â€–xâ€– : ğ•œ)â€– := RCLike.norm_coe_norm.symm
        _ = â€–f xâ€– := by rw [hfx]
        _ â‰¤ M * â€–fâ€– := hM f
        _ = M := by rw [hfâ‚, mul_one]

theorem eq_zero_of_forall_dual_eq_zero {x : E} (h : âˆ€ f : Dual ğ•œ E, f x = (0 : ğ•œ)) : x = 0 :=
  norm_le_zero_iff.mp (norm_le_dual_bound ğ•œ x le_rfl fun f => by simp [h f])

theorem eq_zero_iff_forall_dual_eq_zero (x : E) : x = 0 â†” âˆ€ g : Dual ğ•œ E, g x = 0 :=
  âŸ¨fun hx => by simp [hx], fun h => eq_zero_of_forall_dual_eq_zero ğ•œ hâŸ©

/-- See also `geometric_hahn_banach_point_point`. -/
theorem eq_iff_forall_dual_eq {x y : E} : x = y â†” âˆ€ g : Dual ğ•œ E, g x = g y := by
  rw [â† sub_eq_zero, eq_zero_iff_forall_dual_eq_zero ğ•œ (x - y)]
  simp [sub_eq_zero]

/-- The inclusion of a normed space in its double dual is an isometry onto its image. -/
def inclusionInDoubleDualLi : E â†’â‚—áµ¢[ğ•œ] Dual ğ•œ (Dual ğ•œ E) :=
  { inclusionInDoubleDual ğ•œ E with
    norm_map' := by
      intro x
      apply le_antisymm
      Â· exact double_dual_bound ğ•œ E x
      rw [ContinuousLinearMap.norm_def]
      refine le_csInf ContinuousLinearMap.bounds_nonempty ?_
      rintro c âŸ¨hc1, hc2âŸ©
      exact norm_le_dual_bound ğ•œ x hc1 hc2 }

end BidualIsometry

section PolarSets

open Metric Set NormedSpace

/-- Given a subset `s` in a normed space `E` (over a field `ğ•œ`), the polar
`polar ğ•œ s` is the subset of `Dual ğ•œ E` consisting of those functionals which
evaluate to something of norm at most one at all points `z âˆˆ s`. -/
def polar (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] {E : Type*} [SeminormedAddCommGroup E]
    [NormedSpace ğ•œ E] : Set E â†’ Set (Dual ğ•œ E) :=
  (dualPairing ğ•œ E).flip.polar

/-- Given a subset `s` in a normed space `E` (over a field `ğ•œ`) closed under scalar multiplication,
the polar `polarSubmodule ğ•œ s` is the submodule of `Dual ğ•œ E` consisting of those functionals which
evaluate to zero at all points `z âˆˆ s`. -/
def polarSubmodule (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] {E : Type*} [SeminormedAddCommGroup E]
    [NormedSpace ğ•œ E] {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) :
    Submodule ğ•œ (Dual ğ•œ E) := (dualPairing ğ•œ E).flip.polarSubmodule m

variable (ğ•œ : Type*) [NontriviallyNormedField ğ•œ]
variable {E : Type*} [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

lemma polarSubmodule_eq_polar (m : SubMulAction ğ•œ E) :
    (polarSubmodule ğ•œ m : Set (Dual ğ•œ E)) = polar ğ•œ m := rfl

theorem mem_polar_iff {x' : Dual ğ•œ E} (s : Set E) : x' âˆˆ polar ğ•œ s â†” âˆ€ z âˆˆ s, â€–x' zâ€– â‰¤ 1 :=
  Iff.rfl

lemma polarSubmodule_eq_setOf {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) :
    polarSubmodule ğ•œ m = { y : Dual ğ•œ E | âˆ€ x âˆˆ m, y x = 0 } :=
  (dualPairing ğ•œ E).flip.polar_subMulAction _

lemma mem_polarSubmodule {S : Type*} [SetLike S E] [SMulMemClass S ğ•œ E] (m : S) (y : Dual ğ•œ E) :
    y âˆˆ polarSubmodule ğ•œ m â†” âˆ€ x âˆˆ m, y x = 0 := by
  have := polarSubmodule_eq_setOf ğ•œ m
  apply_fun (y âˆˆ Â·) at this
  rwa [propext_iff] at this

@[simp]
theorem zero_mem_polar (s : Set E) : (0 : Dual ğ•œ E) âˆˆ polar ğ•œ s :=
  LinearMap.zero_mem_polar _ s

theorem polar_nonempty (s : Set E) : Set.Nonempty (polar ğ•œ s) :=
  LinearMap.polar_nonempty _ _

@[simp]
theorem polar_univ : polar ğ•œ (univ : Set E) = {(0 : Dual ğ•œ E)} :=
  (dualPairing ğ•œ E).flip.polar_univ
    (LinearMap.flip_separatingRight.mpr (dualPairing_separatingLeft ğ•œ E))

theorem isClosed_polar (s : Set E) : IsClosed (polar ğ•œ s) := by
  dsimp only [NormedSpace.polar]
  simp only [LinearMap.polar_eq_iInter, LinearMap.flip_apply]
  refine isClosed_biInter fun z _ => ?_
  exact isClosed_Iic.preimage (ContinuousLinearMap.apply ğ•œ ğ•œ z).continuous.norm

@[simp]
theorem polar_closure (s : Set E) : polar ğ•œ (closure s) = polar ğ•œ s :=
  ((dualPairing ğ•œ E).flip.polar_antitone subset_closure).antisymm <|
    (dualPairing ğ•œ E).flip.polar_gc.l_le <|
      closure_minimal ((dualPairing ğ•œ E).flip.polar_gc.le_u_l s) <| by
        simpa [LinearMap.flip_flip] using
          (isClosed_polar _ _).preimage (inclusionInDoubleDual ğ•œ E).continuous

variable {ğ•œ}

/-- If `x'` is a dual element such that the norms `â€–x' zâ€–` are bounded for `z âˆˆ s`, then a
small scalar multiple of `x'` is in `polar ğ•œ s`. -/
theorem smul_mem_polar {s : Set E} {x' : Dual ğ•œ E} {c : ğ•œ} (hc : âˆ€ z, z âˆˆ s â†’ â€–x' zâ€– â‰¤ â€–câ€–) :
    câ»Â¹ â€¢ x' âˆˆ polar ğ•œ s := by
  by_cases c_zero : c = 0
  Â· simp only [c_zero, inv_zero, zero_smul]
    exact (dualPairing ğ•œ E).flip.zero_mem_polar _
  have eq : âˆ€ z, â€–câ»Â¹ â€¢ x' zâ€– = â€–câ»Â¹â€– * â€–x' zâ€– := fun z => norm_smul câ»Â¹ _
  have le : âˆ€ z, z âˆˆ s â†’ â€–câ»Â¹ â€¢ x' zâ€– â‰¤ â€–câ»Â¹â€– * â€–câ€– := by
    intro z hzs
    rw [eq z]
    apply mul_le_mul (le_of_eq rfl) (hc z hzs) (norm_nonneg _) (norm_nonneg _)
  have cancel : â€–câ»Â¹â€– * â€–câ€– = 1 := by
    simp only [c_zero, norm_eq_zero, Ne, not_false_iff, inv_mul_cancelâ‚€, norm_inv]
  rwa [cancel] at le

theorem polar_ball_subset_closedBall_div {c : ğ•œ} (hc : 1 < â€–câ€–) {r : â„} (hr : 0 < r) :
    polar ğ•œ (ball (0 : E) r) âŠ† closedBall (0 : Dual ğ•œ E) (â€–câ€– / r) := by
  intro x' hx'
  rw [mem_polar_iff] at hx'
  simp only [polar, mem_setOf, mem_closedBall_zero_iff, mem_ball_zero_iff] at *
  have hcr : 0 < â€–câ€– / r := div_pos (zero_lt_one.trans hc) hr
  refine ContinuousLinearMap.opNorm_le_of_shell hr hcr.le hc fun x hâ‚ hâ‚‚ => ?_
  calc
    â€–x' xâ€– â‰¤ 1 := hx' _ hâ‚‚
    _ â‰¤ â€–câ€– / r * â€–xâ€– := (inv_le_iff_one_le_mulâ‚€' hcr).1 (by rwa [inv_div])

variable (ğ•œ)

theorem closedBall_inv_subset_polar_closedBall {r : â„} :
    closedBall (0 : Dual ğ•œ E) râ»Â¹ âŠ† polar ğ•œ (closedBall (0 : E) r) := fun x' hx' x hx =>
  calc
    â€–x' xâ€– â‰¤ â€–x'â€– * â€–xâ€– := x'.le_opNorm x
    _ â‰¤ râ»Â¹ * r :=
      (mul_le_mul (mem_closedBall_zero_iff.1 hx') (mem_closedBall_zero_iff.1 hx) (norm_nonneg _)
        (dist_nonneg.trans hx'))
    _ = r / r := inv_mul_eq_div _ _
    _ â‰¤ 1 := div_self_le_one r

/-- The `polar` of closed ball in a normed space `E` is the closed ball of the dual with
inverse radius. -/
theorem polar_closedBall {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {r : â„}
    (hr : 0 < r) : polar ğ•œ (closedBall (0 : E) r) = closedBall (0 : Dual ğ•œ E) râ»Â¹ := by
  refine Subset.antisymm ?_ (closedBall_inv_subset_polar_closedBall ğ•œ)
  intro x' h
  simp only [mem_closedBall_zero_iff]
  refine ContinuousLinearMap.opNorm_le_of_ball hr (inv_nonneg.mpr hr.le) fun z _ => ?_
  simpa only [one_div] using LinearMap.bound_of_ball_bound' hr 1 x'.toLinearMap h z

theorem polar_ball {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] {r : â„}
    (hr : 0 < r) : polar ğ•œ (ball (0 : E) r) = closedBall (0 : Dual ğ•œ E) râ»Â¹ := by
  apply le_antisymm
  Â· intro x hx
    rw [mem_closedBall_zero_iff]
    apply le_of_forall_gt_imp_ge_of_dense
    intro a ha
    rw [â† mem_closedBall_zero_iff, â† (mul_div_cancel_leftâ‚€ a (Ne.symm (ne_of_lt hr)))]
    rw [â† RCLike.norm_of_nonneg (K := ğ•œ) (le_trans zero_le_one
      (le_of_lt ((inv_lt_iff_one_lt_mulâ‚€' hr).mp ha)))]
    apply polar_ball_subset_closedBall_div _ hr hx
    rw [RCLike.norm_of_nonneg (K := ğ•œ) (le_trans zero_le_one
      (le_of_lt ((inv_lt_iff_one_lt_mulâ‚€' hr).mp ha)))]
    exact (inv_lt_iff_one_lt_mulâ‚€' hr).mp ha
  Â· rw [â† polar_closedBall hr]
    exact LinearMap.polar_antitone _ ball_subset_closedBall

/-- Given a neighborhood `s` of the origin in a normed space `E`, the dual norms
of all elements of the polar `polar ğ•œ s` are bounded by a constant. -/
theorem isBounded_polar_of_mem_nhds_zero {s : Set E} (s_nhds : s âˆˆ ğ“ (0 : E)) :
    IsBounded (polar ğ•œ s) := by
  obtain âŸ¨a, haâŸ© : âˆƒ a : ğ•œ, 1 < â€–aâ€– := NormedField.exists_one_lt_norm ğ•œ
  obtain âŸ¨r, r_pos, r_ballâŸ© : âˆƒ r : â„, 0 < r âˆ§ ball 0 r âŠ† s := Metric.mem_nhds_iff.1 s_nhds
  exact isBounded_closedBall.subset
    (((dualPairing ğ•œ E).flip.polar_antitone r_ball).trans <|
      polar_ball_subset_closedBall_div ha r_pos)

@[simp]
theorem polar_empty : polar ğ•œ (âˆ… : Set E) = Set.univ :=
  LinearMap.polar_empty _

@[simp]
theorem polar_singleton {a : E} : polar ğ•œ {a} = { x | â€–x aâ€– â‰¤ 1 } := by
  simp only [polar, LinearMap.polar_singleton, LinearMap.flip_apply, dualPairing_apply]

theorem mem_polar_singleton {a : E} (y : Dual ğ•œ E) : y âˆˆ polar ğ•œ {a} â†” â€–y aâ€– â‰¤ 1 := by
  simp only [polar_singleton, mem_setOf_eq]

theorem polar_zero : polar ğ•œ ({0} : Set E) = Set.univ :=
  LinearMap.polar_zero _

theorem sInter_polar_eq_closedBall {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
    {r : â„} (hr : 0 < r) :
    â‹‚â‚€ (polar ğ•œ '' { F | F.Finite âˆ§ F âŠ† closedBall (0 : E) râ»Â¹ }) = closedBall 0 r := by
  conv_rhs => rw [â† inv_inv r]
  rw [â† polar_closedBall (inv_pos_of_pos hr), polar,
    (dualPairing ğ•œ E).flip.sInter_polar_finite_subset_eq_polar (closedBall (0 : E) râ»Â¹)]

end PolarSets

end NormedSpace

namespace LinearMap

section NormedField

variable {ğ•œ E F : Type*}
variable [NormedField ğ•œ] [NormedSpace â„ ğ•œ] [AddCommMonoid E] [AddCommMonoid F]
variable [Module ğ•œ E] [Module ğ•œ F]

variable {B : E â†’â‚—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ} (s : Set E)

variable [Module â„ F] [IsScalarTower â„ ğ•œ F] [IsScalarTower â„ ğ•œ ğ•œ]

theorem polar_AbsConvex : AbsConvex ğ•œ (B.polar s) := by
  rw [polar_eq_biInter_preimage]
  exact AbsConvex.iInterâ‚‚ fun i hi =>
    âŸ¨balanced_closedBall_zero.mulActionHom_preimage (f := (B i : (F â†’â‚‘[(RingHom.id ğ•œ)] ğ•œ))),
      (convex_closedBall _ _).linear_preimage (B i)âŸ©

end NormedField

end LinearMap
