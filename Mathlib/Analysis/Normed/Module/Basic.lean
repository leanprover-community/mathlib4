/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl
-/
import Mathlib.Algebra.Algebra.Pi
import Mathlib.Algebra.Algebra.Prod
import Mathlib.Algebra.Algebra.Rat
import Mathlib.Algebra.Algebra.RestrictScalars
import Mathlib.Algebra.Module.Rat
import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.Normed.MulAction

/-!
# Normed spaces

In this file we define (semi)normed spaces and algebras. We also prove some theorems
about these definitions.
-/


variable {ğ•œ ğ•œ' E F Î± : Type*}

open Filter Metric Function Set Topology Bornology
open scoped NNReal ENNReal uniformity

section SeminormedAddCommGroup

-- Here, we set a rather high priority for the instance `[NormedSpace ğ•œ E] : Module ğ•œ E`
-- to take precedence over `Semiring.toModule` as this leads to instance paths with better
-- unification properties.
/-- A normed space over a normed field is a vector space endowed with a norm which satisfies the
equality `â€–c â€¢ xâ€– = â€–câ€– â€–xâ€–`. We require only `â€–c â€¢ xâ€– â‰¤ â€–câ€– â€–xâ€–` in the definition, then prove
`â€–c â€¢ xâ€– = â€–câ€– â€–xâ€–` in `norm_smul`.

Note that since this requires `SeminormedAddCommGroup` and not `NormedAddCommGroup`, this
typeclass can be used for "semi normed spaces" too, just as `Module` can be used for
"semi modules". -/
class NormedSpace (ğ•œ : Type*) (E : Type*) [NormedField ğ•œ] [SeminormedAddCommGroup E]
    extends Module ğ•œ E where
  protected norm_smul_le : âˆ€ (a : ğ•œ) (b : E), â€–a â€¢ bâ€– â‰¤ â€–aâ€– * â€–bâ€–

attribute [inherit_doc NormedSpace] NormedSpace.norm_smul_le

variable [NormedField ğ•œ] [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]
variable [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]

-- see Note [lower instance priority]
instance (priority := 100) NormedSpace.toNormSMulClass [NormedSpace ğ•œ E] : NormSMulClass ğ•œ E :=
  haveI : IsBoundedSMul ğ•œ E := .of_norm_smul_le NormedSpace.norm_smul_le
  NormedDivisionRing.toNormSMulClass

/-- This is a shortcut instance, which was found to help with performance in
https://leanprover.zulipchat.com/#narrow/channel/287929-mathlib4/topic/Normed.20modules/near/516757412.

It is implied via `NormedSpace.toNormSMulClass`. -/
instance NormedSpace.toIsBoundedSMul [NormedSpace ğ•œ E] : IsBoundedSMul ğ•œ E := inferInstance

instance NormedField.toNormedSpace : NormedSpace ğ•œ ğ•œ where norm_smul_le a b := norm_mul_le a b

variable (ğ•œ) in
theorem norm_zsmul (n : â„¤) (x : E) : â€–n â€¢ xâ€– = â€–(n : ğ•œ)â€– * â€–xâ€– := by
  rw [â† norm_smul, â† Int.smul_one_eq_cast, smul_assoc, one_smul]

theorem norm_intCast_eq_abs_mul_norm_one (Î±) [SeminormedRing Î±] [NormSMulClass â„¤ Î±] (n : â„¤) :
    â€–(n : Î±)â€– = |n| * â€–(1 : Î±)â€– := by
  rw [â† zsmul_one, norm_smul, Int.norm_eq_abs, Int.cast_abs]

theorem norm_natCast_eq_mul_norm_one (Î±) [SeminormedRing Î±] [NormSMulClass â„¤ Î±] (n : â„•) :
    â€–(n : Î±)â€– = n * â€–(1 : Î±)â€– := by
  simpa using norm_intCast_eq_abs_mul_norm_one Î± n

@[simp]
lemma norm_natCast {Î± : Type*} [SeminormedRing Î±] [NormOneClass Î±] [NormSMulClass â„¤ Î±]
    (a : â„•) : â€–(a : Î±)â€– = a := by
  simpa using norm_natCast_eq_mul_norm_one Î± a

theorem eventually_nhds_norm_smul_sub_lt (c : ğ•œ) (x : E) {Îµ : â„} (h : 0 < Îµ) :
    âˆ€á¶  y in ğ“ x, â€–c â€¢ (y - x)â€– < Îµ :=
  have : Tendsto (fun y â†¦ â€–c â€¢ (y - x)â€–) (ğ“ x) (ğ“ 0) :=
    Continuous.tendsto' (by fun_prop) _ _ (by simp)
  this.eventually (gt_mem_nhds h)

theorem Filter.Tendsto.zero_smul_isBoundedUnder_le {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (hf : Tendsto f l (ğ“ 0)) (hg : IsBoundedUnder (Â· â‰¤ Â·) l (Norm.norm âˆ˜ g)) :
    Tendsto (fun x => f x â€¢ g x) l (ğ“ 0) :=
  hf.op_zero_isBoundedUnder_le hg (Â· â€¢ Â·) norm_smul_le

theorem Filter.IsBoundedUnder.smul_tendsto_zero {f : Î± â†’ ğ•œ} {g : Î± â†’ E} {l : Filter Î±}
    (hf : IsBoundedUnder (Â· â‰¤ Â·) l (norm âˆ˜ f)) (hg : Tendsto g l (ğ“ 0)) :
    Tendsto (fun x => f x â€¢ g x) l (ğ“ 0) :=
  hg.op_zero_isBoundedUnder_le hf (flip (Â· â€¢ Â·)) fun x y =>
    (norm_smul_le y x).trans_eq (mul_comm _ _)

instance NormedSpace.discreteTopology_zmultiples
    {E : Type*} [NormedAddCommGroup E] [NormedSpace â„š E] (e : E) :
    DiscreteTopology <| AddSubgroup.zmultiples e := by
  rcases eq_or_ne e 0 with (rfl | he)
  Â· rw [AddSubgroup.zmultiples_zero_eq_bot]
    exact Subsingleton.discreteTopology (Î± := â†‘(âŠ¥ : Subspace â„š E))
  Â· rw [discreteTopology_iff_isOpen_singleton_zero, isOpen_induced_iff]
    refine âŸ¨Metric.ball 0 â€–eâ€–, Metric.isOpen_ball, ?_âŸ©
    ext âŸ¨x, hxâŸ©
    obtain âŸ¨k, rflâŸ© := AddSubgroup.mem_zmultiples_iff.mp hx
    rw [mem_preimage, mem_ball_zero_iff, AddSubgroup.coe_mk, mem_singleton_iff, Subtype.ext_iff,
      AddSubgroup.coe_mk, AddSubgroup.coe_zero, norm_zsmul â„š k e, Int.norm_cast_rat,
      Int.norm_eq_abs, mul_lt_iff_lt_one_left (norm_pos_iff.mpr he), â† @Int.cast_one â„ _,
      â† Int.cast_abs, Int.cast_lt, Int.abs_lt_one_iff, smul_eq_zero, or_iff_left he]

open NormedField

instance ULift.normedSpace : NormedSpace ğ•œ (ULift E) :=
  { __ := ULift.seminormedAddCommGroup (E := E),
    __ := ULift.module'
    norm_smul_le := fun s x => (norm_smul_le s x.down :) }

/-- The product of two normed spaces is a normed space, with the sup norm. -/
instance Prod.normedSpace : NormedSpace ğ•œ (E Ã— F) :=
  { Prod.seminormedAddCommGroup (E := E) (F := F), Prod.instModule with
    norm_smul_le := fun s x => by
      simp only [norm_smul, Prod.norm_def,
        mul_max_of_nonneg, norm_nonneg, le_rfl] }

/-- The product of finitely many normed spaces is a normed space, with the sup norm. -/
instance Pi.normedSpace {Î¹ : Type*} {E : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, SeminormedAddCommGroup (E i)]
    [âˆ€ i, NormedSpace ğ•œ (E i)] : NormedSpace ğ•œ (âˆ€ i, E i) where
  norm_smul_le a f := by
    simp_rw [â† coe_nnnorm, â† NNReal.coe_mul, NNReal.coe_le_coe, Pi.nnnorm_def,
      NNReal.mul_finset_sup]
    exact Finset.sup_mono_fun fun _ _ => norm_smul_le a _

instance SeparationQuotient.instNormedSpace : NormedSpace ğ•œ (SeparationQuotient E) where
  norm_smul_le := norm_smul_le

instance MulOpposite.instNormedSpace : NormedSpace ğ•œ Eáµáµ’áµ– where
  norm_smul_le _ x := norm_smul_le _ x.unop

/-- A subspace of a normed space is also a normed space, with the restriction of the norm. -/
instance Submodule.normedSpace {ğ•œ R : Type*} [SMul ğ•œ R] [NormedField ğ•œ] [Ring R] {E : Type*}
    [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] [Module R E] [IsScalarTower ğ•œ R E]
    (s : Submodule R E) : NormedSpace ğ•œ s where
  norm_smul_le c x := norm_smul_le c (x : E)

variable {S ğ•œ R E : Type*} [SMul ğ•œ R] [NormedField ğ•œ] [Ring R] [SeminormedAddCommGroup E]
variable [NormedSpace ğ•œ E] [Module R E] [IsScalarTower ğ•œ R E] [SetLike S E] [AddSubgroupClass S E]
variable [SMulMemClass S R E] (s : S)

instance (priority := 75) SubmoduleClass.toNormedSpace : NormedSpace ğ•œ s where
  norm_smul_le c x := norm_smul_le c (x : E)

end SeminormedAddCommGroup

/-- A linear map from a `Module` to a `NormedSpace` induces a `NormedSpace` structure on the
domain, using the `SeminormedAddCommGroup.induced` norm.

See note [reducible non-instances] -/
abbrev NormedSpace.induced {F : Type*} (ğ•œ E G : Type*) [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E]
    [SeminormedAddCommGroup G] [NormedSpace ğ•œ G] [FunLike F E G] [LinearMapClass F ğ•œ E G] (f : F) :
    @NormedSpace ğ•œ E _ (SeminormedAddCommGroup.induced E G f) :=
  let _ := SeminormedAddCommGroup.induced E G f
  âŸ¨fun a b â†¦ by simpa only [â† map_smul f a b] using norm_smul_le a (f b)âŸ©

section NontriviallyNormedSpace

variable (ğ•œ E)
variable [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E] [Nontrivial E]
include ğ•œ

/-- If `E` is a nontrivial normed space over a nontrivially normed field `ğ•œ`, then `E` is unbounded:
for any `c : â„`, there exists a vector `x : E` with norm strictly greater than `c`. -/
theorem NormedSpace.exists_lt_norm (c : â„) : âˆƒ x : E, c < â€–xâ€– := by
  rcases exists_ne (0 : E) with âŸ¨x, hxâŸ©
  rcases NormedField.exists_lt_norm ğ•œ (c / â€–xâ€–) with âŸ¨r, hrâŸ©
  use r â€¢ x
  rwa [norm_smul, â† div_lt_iffâ‚€]
  rwa [norm_pos_iff]

protected theorem NormedSpace.unbounded_univ : Â¬Bornology.IsBounded (univ : Set E) := fun h =>
  let âŸ¨R, hRâŸ© := isBounded_iff_forall_norm_le.1 h
  let âŸ¨x, hxâŸ© := NormedSpace.exists_lt_norm ğ•œ E R
  hx.not_ge (hR x trivial)

protected lemma NormedSpace.cobounded_neBot : NeBot (cobounded E) := by
  rw [neBot_iff, Ne, cobounded_eq_bot_iff, â† isBounded_univ]
  exact NormedSpace.unbounded_univ ğ•œ E

instance (priority := 100) NontriviallyNormedField.cobounded_neBot : NeBot (cobounded ğ•œ) :=
  NormedSpace.cobounded_neBot ğ•œ ğ•œ

instance (priority := 80) RealNormedSpace.cobounded_neBot [NormedSpace â„ E] :
    NeBot (cobounded E) := NormedSpace.cobounded_neBot â„ E

instance (priority := 80) NontriviallyNormedField.infinite : Infinite ğ•œ :=
  âŸ¨fun _ â†¦ NormedSpace.unbounded_univ ğ•œ ğ•œ (Set.toFinite _).isBoundedâŸ©

end NontriviallyNormedSpace

section NormedSpace

variable (ğ•œ E)
variable [NormedField ğ•œ] [Infinite ğ•œ] [NormedAddCommGroup E] [Nontrivial E] [NormedSpace ğ•œ E]
include ğ•œ

/-- A normed vector space over an infinite normed field is a noncompact space.
This cannot be an instance because in order to apply it,
Lean would have to search for `NormedSpace ğ•œ E` with unknown `ğ•œ`.
We register this as an instance in two cases: `ğ•œ = E` and `ğ•œ = â„`. -/
protected theorem NormedSpace.noncompactSpace : NoncompactSpace E := by
  by_cases H : âˆƒ c : ğ•œ, c â‰  0 âˆ§ â€–câ€– â‰  1
  Â· letI := NontriviallyNormedField.ofNormNeOne H
    exact âŸ¨fun h â†¦ NormedSpace.unbounded_univ ğ•œ E h.isBoundedâŸ©
  Â· push_neg at H
    rcases exists_ne (0 : E) with âŸ¨x, hxâŸ©
    suffices IsClosedEmbedding (Infinite.natEmbedding ğ•œ Â· â€¢ x) from this.noncompactSpace
    refine isClosedEmbedding_of_pairwise_le_dist (norm_pos_iff.2 hx) fun k n hne â†¦ ?_
    simp only [dist_eq_norm, â† sub_smul, norm_smul]
    rw [H, one_mul]
    rwa [sub_ne_zero, (Embedding.injective _).ne_iff]

instance (priority := 100) NormedField.noncompactSpace : NoncompactSpace ğ•œ :=
  NormedSpace.noncompactSpace ğ•œ ğ•œ

instance (priority := 100) RealNormedSpace.noncompactSpace [NormedSpace â„ E] : NoncompactSpace E :=
  NormedSpace.noncompactSpace â„ E

end NormedSpace

section NormedAlgebra

/-- A normed algebra `ğ•œ'` over `ğ•œ` is normed module that is also an algebra.

See the implementation notes for `Algebra` for a discussion about non-unital algebras. Following
the strategy there, a non-unital *normed* algebra can be written as:
```lean
variable [NormedField ğ•œ] [NonUnitalSeminormedRing ğ•œ']
variable [NormedSpace ğ•œ ğ•œ'] [SMulCommClass ğ•œ ğ•œ' ğ•œ'] [IsScalarTower ğ•œ ğ•œ' ğ•œ']
```
-/
class NormedAlgebra (ğ•œ : Type*) (ğ•œ' : Type*) [NormedField ğ•œ] [SeminormedRing ğ•œ'] extends
  Algebra ğ•œ ğ•œ' where
  norm_smul_le : âˆ€ (r : ğ•œ) (x : ğ•œ'), â€–r â€¢ xâ€– â‰¤ â€–râ€– * â€–xâ€–

attribute [inherit_doc NormedAlgebra] NormedAlgebra.norm_smul_le

variable (ğ•œ')
variable [NormedField ğ•œ] [SeminormedRing ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']

instance (priority := 100) NormedAlgebra.toNormedSpace : NormedSpace ğ•œ ğ•œ' :=
  { NormedAlgebra.toAlgebra.toModule with
  norm_smul_le := NormedAlgebra.norm_smul_le }

theorem norm_algebraMap (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€– = â€–xâ€– * â€–(1 : ğ•œ')â€– := by
  rw [Algebra.algebraMap_eq_smul_one]
  exact norm_smul _ _

theorem nnnorm_algebraMap (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š * â€–(1 : ğ•œ')â€–â‚Š :=
  Subtype.ext <| norm_algebraMap ğ•œ' x

theorem dist_algebraMap (x y : ğ•œ) :
    (dist (algebraMap ğ•œ ğ•œ' x) (algebraMap ğ•œ ğ•œ' y)) = dist x y * â€–(1 : ğ•œ')â€– := by
  simp only [dist_eq_norm, â† map_sub, norm_algebraMap]

/-- This is a simpler version of `norm_algebraMap` when `â€–1â€– = 1` in `ğ•œ'`. -/
@[simp]
theorem norm_algebraMap' [NormOneClass ğ•œ'] (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€– = â€–xâ€– := by
  rw [norm_algebraMap, norm_one, mul_one]

@[simp]
theorem Algebra.norm_smul_one_eq_norm [NormOneClass ğ•œ'] (x : ğ•œ) : â€–x â€¢ (1 : ğ•œ')â€– = â€–xâ€– := by
  simp [norm_smul]

/-- This is a simpler version of `nnnorm_algebraMap` when `â€–1â€– = 1` in `ğ•œ'`. -/
@[simp]
theorem nnnorm_algebraMap' [NormOneClass ğ•œ'] (x : ğ•œ) : â€–algebraMap ğ•œ ğ•œ' xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| norm_algebraMap' _ _

/-- This is a simpler version of `dist_algebraMap` when `â€–1â€– = 1` in `ğ•œ'`. -/
@[simp]
theorem dist_algebraMap' [NormOneClass ğ•œ'] (x y : ğ•œ) :
    (dist (algebraMap ğ•œ ğ•œ' x) (algebraMap ğ•œ ğ•œ' y)) = dist x y := by
  simp only [dist_eq_norm, â† map_sub, norm_algebraMap']

section NNReal

variable [NormOneClass ğ•œ'] [NormedAlgebra â„ ğ•œ']

@[simp]
theorem norm_algebraMap_nnreal (x : â„â‰¥0) : â€–algebraMap â„â‰¥0 ğ•œ' xâ€– = x :=
  (norm_algebraMap' ğ•œ' (x : â„)).symm â–¸ Real.norm_of_nonneg x.prop

@[simp]
theorem nnnorm_algebraMap_nnreal (x : â„â‰¥0) : â€–algebraMap â„â‰¥0 ğ•œ' xâ€–â‚Š = x :=
  Subtype.ext <| norm_algebraMap_nnreal ğ•œ' x

end NNReal

variable (ğ•œ)

/--
Preimages of cobounded sets under the algebra map are cobounded.
-/
theorem algebraMap_cobounded_le_cobounded [NormOneClass ğ•œ'] :
    Filter.map (algebraMap ğ•œ ğ•œ') (Bornology.cobounded ğ•œ) â‰¤ Bornology.cobounded ğ•œ' := by
  intro c hc
  rw [Filter.mem_map, â† Bornology.isCobounded_def, â† Bornology.isBounded_compl_iff,
    isBounded_iff_forall_norm_le]
  obtain âŸ¨s, hsâŸ© := isBounded_iff_forall_norm_le.1
    (Bornology.isBounded_compl_iff.2 (Bornology.isCobounded_def.1 hc))
  use s
  exact fun x hx â†¦ by simpa [norm_algebraMap, norm_one] using hs ((algebraMap ğ•œ ğ•œ') x) hx

/-- In a normed algebra, the inclusion of the base field in the extended field is an isometry. -/
theorem algebraMap_isometry [NormOneClass ğ•œ'] : Isometry (algebraMap ğ•œ ğ•œ') := by
  refine Isometry.of_dist_eq fun x y => ?_
  rw [dist_eq_norm, dist_eq_norm, â† RingHom.map_sub, norm_algebraMap']

instance NormedAlgebra.id : NormedAlgebra ğ•œ ğ•œ :=
  { NormedField.toNormedSpace, Algebra.id ğ•œ with }

/-- Any normed characteristic-zero division ring that is a normed algebra over the reals is also a
normed algebra over the rationals.

Phrased another way, if `ğ•œ` is a normed algebra over the reals, then `AlgebraRat` respects that
norm. -/
instance normedAlgebraRat {ğ•œ} [NormedDivisionRing ğ•œ] [CharZero ğ•œ] [NormedAlgebra â„ ğ•œ] :
    NormedAlgebra â„š ğ•œ where
  norm_smul_le q x := by
    rw [â† smul_one_smul â„ q x, Rat.smul_one_eq_cast, norm_smul, Rat.norm_cast_real]

instance PUnit.normedAlgebra : NormedAlgebra ğ•œ PUnit where
  norm_smul_le q _ := by simp only [norm_eq_zero, mul_zero, le_refl]

instance : NormedAlgebra ğ•œ (ULift ğ•œ') :=
  { ULift.normedSpace, ULift.algebra with }

/-- The product of two normed algebras is a normed algebra, with the sup norm. -/
instance Prod.normedAlgebra {E F : Type*} [SeminormedRing E] [SeminormedRing F] [NormedAlgebra ğ•œ E]
    [NormedAlgebra ğ•œ F] : NormedAlgebra ğ•œ (E Ã— F) :=
  { Prod.normedSpace, Prod.algebra ğ•œ E F with }

/-- The product of finitely many normed algebras is a normed algebra, with the sup norm. -/
instance Pi.normedAlgebra {Î¹ : Type*} {E : Î¹ â†’ Type*} [Fintype Î¹] [âˆ€ i, SeminormedRing (E i)]
    [âˆ€ i, NormedAlgebra ğ•œ (E i)] : NormedAlgebra ğ•œ (âˆ€ i, E i) :=
  { Pi.normedSpace, Pi.algebra _ E with }

variable [SeminormedRing E] [NormedAlgebra ğ•œ E]

instance SeparationQuotient.instNormedAlgebra : NormedAlgebra ğ•œ (SeparationQuotient E) where
  __ : NormedSpace ğ•œ (SeparationQuotient E) := inferInstance
  __ : Algebra ğ•œ (SeparationQuotient E) := inferInstance

instance MulOpposite.instNormedAlgebra {E : Type*} [SeminormedRing E] [NormedAlgebra ğ•œ E] :
    NormedAlgebra ğ•œ Eáµáµ’áµ– where
  __ := instAlgebra
  __ := instNormedSpace

end NormedAlgebra

/-- A non-unital algebra homomorphism from an `Algebra` to a `NormedAlgebra` induces a
`NormedAlgebra` structure on the domain, using the `SeminormedRing.induced` norm.

See note [reducible non-instances] -/
abbrev NormedAlgebra.induced {F : Type*} (ğ•œ R S : Type*) [NormedField ğ•œ] [Ring R] [Algebra ğ•œ R]
    [SeminormedRing S] [NormedAlgebra ğ•œ S] [FunLike F R S] [NonUnitalAlgHomClass F ğ•œ R S]
    (f : F) :
    @NormedAlgebra ğ•œ R _ (SeminormedRing.induced R S f) :=
  letI := SeminormedRing.induced R S f
  âŸ¨fun a b â†¦ show â€–f (a â€¢ b)â€– â‰¤ â€–aâ€– * â€–f bâ€– from (map_smul f a b).symm â–¸ norm_smul_le a (f b)âŸ©

instance Subalgebra.toNormedAlgebra {ğ•œ A : Type*} [SeminormedRing A] [NormedField ğ•œ]
    [NormedAlgebra ğ•œ A] (S : Subalgebra ğ•œ A) : NormedAlgebra ğ•œ S :=
  NormedAlgebra.induced ğ•œ S A S.val

section SubalgebraClass

variable {S ğ•œ E : Type*} [NormedField ğ•œ] [SeminormedRing E] [NormedAlgebra ğ•œ E]
variable [SetLike S E] [SubringClass S E] [SMulMemClass S ğ•œ E] (s : S)

instance (priority := 75) SubalgebraClass.toNormedAlgebra : NormedAlgebra ğ•œ s where
  norm_smul_le c x := norm_smul_le c (x : E)

end SubalgebraClass

section RestrictScalars

section NormInstances

instance [I : SeminormedAddCommGroup E] :
    SeminormedAddCommGroup (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NormedAddCommGroup E] :
    NormedAddCommGroup (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NonUnitalSeminormedRing E] :
    NonUnitalSeminormedRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NonUnitalNormedRing E] :
    NonUnitalNormedRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : SeminormedRing E] :
    SeminormedRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NormedRing E] :
    NormedRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NonUnitalSeminormedCommRing E] :
    NonUnitalSeminormedCommRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NonUnitalNormedCommRing E] :
    NonUnitalNormedCommRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : SeminormedCommRing E] :
    SeminormedCommRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

instance [I : NormedCommRing E] :
    NormedCommRing (RestrictScalars ğ•œ ğ•œ' E) :=
  I

end NormInstances

section NormedSpace

variable (ğ•œ ğ•œ' E)
variable [NormedField ğ•œ] [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  [SeminormedAddCommGroup E] [NormedSpace ğ•œ' E]

/-- If `E` is a normed space over `ğ•œ'` and `ğ•œ` is a normed algebra over `ğ•œ'`, then
`RestrictScalars.module` is additionally a `NormedSpace`. -/
instance RestrictScalars.normedSpace : NormedSpace ğ•œ (RestrictScalars ğ•œ ğ•œ' E) :=
  { RestrictScalars.module ğ•œ ğ•œ' E with
    norm_smul_le := fun c x =>
      (norm_smul_le (algebraMap ğ•œ ğ•œ' c) (_ : E)).trans_eq <| by rw [norm_algebraMap'] }

-- If you think you need this, consider instead reproducing `RestrictScalars.lsmul`
-- appropriately modified here.
/-- The action of the original normed_field on `RestrictScalars ğ•œ ğ•œ' E`.
This is not an instance as it would be contrary to the purpose of `RestrictScalars`.
-/
def Module.RestrictScalars.normedSpaceOrig {ğ•œ : Type*} {ğ•œ' : Type*} {E : Type*} [NormedField ğ•œ']
    [SeminormedAddCommGroup E] [I : NormedSpace ğ•œ' E] : NormedSpace ğ•œ' (RestrictScalars ğ•œ ğ•œ' E) :=
  I

/-- Warning: This declaration should be used judiciously.
Please consider using `IsScalarTower` and/or `RestrictScalars ğ•œ ğ•œ' E` instead.

This definition allows the `RestrictScalars.normedSpace` instance to be put directly on `E`
rather on `RestrictScalars ğ•œ ğ•œ' E`. This would be a very bad instance; both because `ğ•œ'` cannot be
inferred, and because it is likely to create instance diamonds.

See Note [reducible non-instances].
-/
abbrev NormedSpace.restrictScalars : NormedSpace ğ•œ E :=
  RestrictScalars.normedSpace _ ğ•œ' E

end NormedSpace

section NormedAlgebra

variable (ğ•œ ğ•œ' E)
variable [NormedField ğ•œ] [NormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  [SeminormedRing E] [NormedAlgebra ğ•œ' E]

/-- If `E` is a normed algebra over `ğ•œ'` and `ğ•œ` is a normed algebra over `ğ•œ'`, then
`RestrictScalars.module` is additionally a `NormedAlgebra`. -/
instance RestrictScalars.normedAlgebra : NormedAlgebra ğ•œ (RestrictScalars ğ•œ ğ•œ' E) :=
  { RestrictScalars.algebra ğ•œ ğ•œ' E with
    norm_smul_le := norm_smul_le }

-- If you think you need this, consider instead reproducing `RestrictScalars.lsmul`
-- appropriately modified here.
/-- The action of the original normed_field on `RestrictScalars ğ•œ ğ•œ' E`.
This is not an instance as it would be contrary to the purpose of `RestrictScalars`.
-/
def Module.RestrictScalars.normedAlgebraOrig {ğ•œ : Type*} {ğ•œ' : Type*} {E : Type*} [NormedField ğ•œ']
    [SeminormedRing E] [I : NormedAlgebra ğ•œ' E] : NormedAlgebra ğ•œ' (RestrictScalars ğ•œ ğ•œ' E) :=
  I

/-- Warning: This declaration should be used judiciously.
Please consider using `IsScalarTower` and/or `RestrictScalars ğ•œ ğ•œ' E` instead.

This definition allows the `RestrictScalars.normedAlgebra` instance to be put directly on `E`
rather on `RestrictScalars ğ•œ ğ•œ' E`. This would be a very bad instance; both because `ğ•œ'` cannot be
inferred, and because it is likely to create instance diamonds.

See Note [reducible non-instances].
-/
abbrev NormedAlgebra.restrictScalars : NormedAlgebra ğ•œ E :=
  RestrictScalars.normedAlgebra _ ğ•œ' _

end NormedAlgebra

end RestrictScalars

section Core
/-!
### Structures for constructing new normed spaces

This section contains tools meant for constructing new normed spaces. These allow one to easily
construct all the relevant instances (distances measures, etc) while proving only a minimal
set of axioms. Furthermore, tools are provided to add a norm structure to a type that already
has a preexisting uniformity or bornology: in such cases, it is necessary to keep the preexisting
instances, while ensuring that the norm induces the same uniformity/bornology.
-/

open scoped Uniformity Bornology

/-- A structure encapsulating minimal axioms needed to defined a seminormed vector space, as found
in textbooks. This is meant to be used to easily define `SeminormedSpace E` instances from
scratch on a type with no preexisting distance or topology. -/
structure SeminormedSpace.Core (ğ•œ : Type*) (E : Type*) [NormedField ğ•œ] [AddCommGroup E]
    [Norm E] [Module ğ•œ E] : Prop where
  norm_nonneg (x : E) : 0 â‰¤ â€–xâ€–
  norm_smul (c : ğ•œ) (x : E) : â€–c â€¢ xâ€– = â€–câ€– * â€–xâ€–
  norm_triangle (x y : E) : â€–x + yâ€– â‰¤ â€–xâ€– + â€–yâ€–

@[deprecated (since := "2025-06-03")] alias SeminormedAddCommGroup.Core := SeminormedSpace.Core

/-- Produces a `PseudoMetricSpace E` instance from a `SeminormedSpace.Core`. Note that
if this is used to define an instance on a type, it also provides a new uniformity and
topology on the type. See note [reducible non-instances]. -/
abbrev PseudoMetricSpace.ofSeminormedSpaceCore {ğ•œ E : Type*} [NormedField ğ•œ] [AddCommGroup E]
    [Norm E] [Module ğ•œ E] (core : SeminormedSpace.Core ğ•œ E) :
    PseudoMetricSpace E where
  dist x y := â€–x - yâ€–
  dist_self x := by
    show â€–x - xâ€– = 0
    simp only [sub_self]
    have : (0 : E) = (0 : ğ•œ) â€¢ (0 : E) := by simp
    rw [this, core.norm_smul]
    simp
  dist_comm x y := by
    show â€–x - yâ€– = â€–y - xâ€–
    have : y - x = (-1 : ğ•œ) â€¢ (x - y) := by simp
    rw [this, core.norm_smul]
    simp
  dist_triangle x y z := by
    show â€–x - zâ€– â‰¤ â€–x - yâ€– + â€–y - zâ€–
    have : x - z = (x - y) + (y - z) := by abel
    rw [this]
    exact core.norm_triangle _ _
  edist_dist x y := by exact (ENNReal.ofReal_eq_coe_nnreal _).symm

@[deprecated (since := "2025-06-03")]
alias PseudoMetricSpace.ofSeminormedAddCommGroupCore := PseudoMetricSpace.ofSeminormedSpaceCore

/-- Produces a `PseudoEMetricSpace E` instance from a `SeminormedSpace.Core`. Note that
if this is used to define an instance on a type, it also provides a new uniformity and
topology on the type. See note [reducible non-instances]. -/
abbrev PseudoEMetricSpace.ofSeminormedSpaceCore {ğ•œ E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E]
    (core : SeminormedSpace.Core ğ•œ E) : PseudoEMetricSpace E :=
  (PseudoMetricSpace.ofSeminormedSpaceCore core).toPseudoEMetricSpace

@[deprecated (since := "2025-06-03")]
alias PseudoEMetricSpace.ofSeminormedAddCommGroupCore := PseudoEMetricSpace.ofSeminormedSpaceCore

/-- Produces a `PseudoEMetricSpace E` instance from a `SeminormedSpace.Core` on a type that
already has an existing uniform space structure. This requires a proof that the uniformity induced
by the norm is equal to the preexisting uniformity. See note [reducible non-instances]. -/
abbrev PseudoMetricSpace.ofSeminormedSpaceCoreReplaceUniformity {ğ•œ E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [U : UniformSpace E]
    (core : SeminormedSpace.Core ğ•œ E)
    (H : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
        (self := PseudoEMetricSpace.ofSeminormedSpaceCore core)]) :
    PseudoMetricSpace E :=
  .replaceUniformity (.ofSeminormedSpaceCore core) H

@[deprecated (since := "2025-06-03")]
alias PseudoMetricSpace.ofSeminormedAddCommGroupCoreReplaceUniformity :=
  PseudoMetricSpace.ofSeminormedSpaceCoreReplaceUniformity

/-- Produces a `PseudoEMetricSpace E` instance from a `SeminormedSpace.Core` on a type that
already has an existing topology. This requires a proof that the topology induced
by the norm is equal to the preexisting topology. See note [reducible non-instances]. -/
abbrev PseudoMetricSpace.ofSeminormedSpaceCoreReplaceTopology {ğ•œ E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [T : TopologicalSpace E]
    (core : SeminormedSpace.Core ğ•œ E)
    (H : T = (PseudoEMetricSpace.ofSeminormedSpaceCore
      core).toUniformSpace.toTopologicalSpace) :
    PseudoMetricSpace E :=
  .replaceTopology (.ofSeminormedSpaceCore core) H

open Bornology in
/-- Produces a `PseudoEMetricSpace E` instance from a `SeminormedSpace.Core` on a type that
already has a preexisting uniform space structure and a preexisting bornology. This requires proofs
that the uniformity induced by the norm is equal to the preexisting uniformity, and likewise for
the bornology. See note [reducible non-instances]. -/
abbrev PseudoMetricSpace.ofSeminormedSpaceCoreReplaceAll {ğ•œ E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [U : UniformSpace E] [B : Bornology E]
    (core : SeminormedSpace.Core ğ•œ E)
    (HU : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
      (self := PseudoEMetricSpace.ofSeminormedSpaceCore core)])
    (HB : âˆ€ s : Set E, @IsBounded _ B s
      â†” @IsBounded _ (PseudoMetricSpace.ofSeminormedSpaceCore core).toBornology s) :
    PseudoMetricSpace E :=
  .replaceBornology (.replaceUniformity (.ofSeminormedSpaceCore core) HU) HB

@[deprecated (since := "2025-06-03")]
alias PseudoMetricSpace.ofSeminormedAddCommGroupCoreReplaceAll :=
  PseudoMetricSpace.ofSeminormedSpaceCoreReplaceAll

/-- Produces a `SeminormedAddCommGroup E` instance from a `SeminormedSpace.Core`. Note that
if this is used to define an instance on a type, it also provides a new distance measure from the
norm.  it must therefore not be used on a type with a preexisting distance measure or topology.
See note [reducible non-instances]. -/
abbrev SeminormedAddCommGroup.ofCore {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ] [AddCommGroup E]
    [Norm E] [Module ğ•œ E] (core : SeminormedSpace.Core ğ•œ E) : SeminormedAddCommGroup E :=
  { PseudoMetricSpace.ofSeminormedSpaceCore core with }

/-- Produces a `SeminormedAddCommGroup E` instance from a `SeminormedSpace.Core` on a type
that already has an existing uniform space structure. This requires a proof that the uniformity
induced by the norm is equal to the preexisting uniformity. See note [reducible non-instances]. -/
abbrev SeminormedAddCommGroup.ofCoreReplaceUniformity {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [U : UniformSpace E]
    (core : SeminormedSpace.Core ğ•œ E)
    (H : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
      (self := PseudoEMetricSpace.ofSeminormedSpaceCore core)]) :
    SeminormedAddCommGroup E :=
  { PseudoMetricSpace.ofSeminormedSpaceCoreReplaceUniformity core H with }

/-- Produces a `SeminormedAddCommGroup E` instance from a `SeminormedSpace.Core` on a type
that already has an existing topology. This requires a proof that the uniformity
induced by the norm is equal to the preexisting uniformity. See note [reducible non-instances]. -/
abbrev SeminormedAddCommGroup.ofCoreReplaceTopology {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [T : TopologicalSpace E]
    (core : SeminormedSpace.Core ğ•œ E)
    (H : T = (PseudoEMetricSpace.ofSeminormedSpaceCore
      core).toUniformSpace.toTopologicalSpace) :
    SeminormedAddCommGroup E :=
  { PseudoMetricSpace.ofSeminormedSpaceCoreReplaceTopology core H with }

open Bornology in
/-- Produces a `SeminormedAddCommGroup E` instance from a `SeminormedSpace.Core` on a type
that already has a preexisting uniform space structure and a preexisting bornology. This requires
proofs that the uniformity induced by the norm is equal to the preexisting uniformity, and likewise
for the bornology. See note [reducible non-instances]. -/
abbrev SeminormedAddCommGroup.ofCoreReplaceAll {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ]
    [AddCommGroup E] [Norm E] [Module ğ•œ E] [U : UniformSpace E] [B : Bornology E]
    (core : SeminormedSpace.Core ğ•œ E)
    (HU : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
      (self := PseudoEMetricSpace.ofSeminormedSpaceCore core)])
    (HB : âˆ€ s : Set E, @IsBounded _ B s
      â†” @IsBounded _ (PseudoMetricSpace.ofSeminormedSpaceCore core).toBornology s) :
    SeminormedAddCommGroup E :=
  { PseudoMetricSpace.ofSeminormedSpaceCoreReplaceAll core HU HB with }

/-- A structure encapsulating minimal axioms needed to defined a normed vector space, as found
in textbooks. This is meant to be used to easily define `NormedAddCommGroup E` and `NormedSpace E`
instances from scratch on a type with no preexisting distance or topology. -/
structure NormedSpace.Core (ğ•œ : Type*) (E : Type*)
    [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Norm E] : Prop
    extends SeminormedSpace.Core ğ•œ E where
  norm_eq_zero_iff (x : E) : â€–xâ€– = 0 â†” x = 0

variable {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ] [AddCommGroup E] [Module ğ•œ E] [Norm E]

/-- Produces a `NormedAddCommGroup E` instance from a `NormedSpace.Core`. Note that if this is
used to define an instance on a type, it also provides a new distance measure from the norm.
it must therefore not be used on a type with a preexisting distance measure.
See note [reducible non-instances]. -/
abbrev NormedAddCommGroup.ofCore (core : NormedSpace.Core ğ•œ E) : NormedAddCommGroup E :=
  { SeminormedAddCommGroup.ofCore core.toCore with
    eq_of_dist_eq_zero := by
      intro x y h
      rw [â† sub_eq_zero, â† core.norm_eq_zero_iff]
      exact h }

/-- Produces a `NormedAddCommGroup E` instance from a `NormedSpace.Core` on a type
that already has an existing uniform space structure. This requires a proof that the uniformity
induced by the norm is equal to the preexisting uniformity. See note [reducible non-instances]. -/
abbrev NormedAddCommGroup.ofCoreReplaceUniformity [U : UniformSpace E] (core : NormedSpace.Core ğ•œ E)
    (H : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
      (self := PseudoEMetricSpace.ofSeminormedSpaceCore core.toCore)]) :
    NormedAddCommGroup E :=
  { SeminormedAddCommGroup.ofCoreReplaceUniformity core.toCore H with
    eq_of_dist_eq_zero := by
      intro x y h
      rw [â† sub_eq_zero, â† core.norm_eq_zero_iff]
      exact h }

/-- Produces a `NormedAddCommGroup E` instance from a `NormedSpace.Core` on a type
that already has an existing topology. This requires a proof that the uniformity
induced by the norm is equal to the preexisting uniformity. See note [reducible non-instances]. -/
abbrev NormedAddCommGroup.ofCoreReplaceTopology [T : TopologicalSpace E]
    (core : NormedSpace.Core ğ•œ E)
    (H : T = (PseudoEMetricSpace.ofSeminormedSpaceCore
      core.toCore).toUniformSpace.toTopologicalSpace) :
    NormedAddCommGroup E :=
  { SeminormedAddCommGroup.ofCoreReplaceTopology core.toCore H with
    eq_of_dist_eq_zero := by
      intro x y h
      rw [â† sub_eq_zero, â† core.norm_eq_zero_iff]
      exact h }

open Bornology in
/-- Produces a `NormedAddCommGroup E` instance from a `NormedSpace.Core` on a type
that already has a preexisting uniform space structure and a preexisting bornology. This requires
proofs that the uniformity induced by the norm is equal to the preexisting uniformity, and likewise
for the bornology. See note [reducible non-instances]. -/
abbrev NormedAddCommGroup.ofCoreReplaceAll [U : UniformSpace E] [B : Bornology E]
    (core : NormedSpace.Core ğ•œ E)
    (HU : ğ“¤[U] = ğ“¤[PseudoEMetricSpace.toUniformSpace
      (self := PseudoEMetricSpace.ofSeminormedSpaceCore core.toCore)])
    (HB : âˆ€ s : Set E, @IsBounded _ B s
      â†” @IsBounded _ (PseudoMetricSpace.ofSeminormedSpaceCore core.toCore).toBornology s) :
    NormedAddCommGroup E :=
  { SeminormedAddCommGroup.ofCoreReplaceAll core.toCore HU HB with
    eq_of_dist_eq_zero := by
      intro x y h
      rw [â† sub_eq_zero, â† core.norm_eq_zero_iff]
      exact h }

/-- Produces a `NormedSpace ğ•œ E` instance from a `NormedSpace.Core`. This is meant to be used
on types where the `NormedAddCommGroup E` instance has also been defined using `core`.
See note [reducible non-instances]. -/
abbrev NormedSpace.ofCore {ğ•œ : Type*} {E : Type*} [NormedField ğ•œ] [SeminormedAddCommGroup E]
    [Module ğ•œ E] (core : NormedSpace.Core ğ•œ E) : NormedSpace ğ•œ E where
  norm_smul_le r x := by rw [core.norm_smul r x]

end Core

variable {G H : Type*} [SeminormedAddCommGroup G] [SeminormedAddCommGroup H] [NormedSpace â„ H]
  {s : Set G}

/-- A group homomorphism from a normed group to a real normed space,
bounded on a neighborhood of `0`, must be continuous. -/
lemma AddMonoidHom.continuous_of_isBounded_nhds_zero (f : G â†’+ H) (hs : s âˆˆ ğ“ (0 : G))
    (hbounded : IsBounded (f '' s)) : Continuous f := by
  obtain âŸ¨Î´, hÎ´, hUÎµâŸ© := Metric.mem_nhds_iff.mp hs
  obtain âŸ¨C, hCâŸ© := (isBounded_iff_subset_ball 0).1 (hbounded.subset <| image_mono hUÎµ)
  refine continuous_of_continuousAt_zero _ (continuousAt_iff.2 fun Îµ (hÎµ : _ < _) => ?_)
  simp only [dist_zero_right, map_zero]
  simp only [subset_def, mem_image, mem_ball, dist_zero_right, forall_exists_index, and_imp,
    forall_apply_eq_imp_iffâ‚‚] at hC
  have hCâ‚€ : 0 < C := (norm_nonneg _).trans_lt <| hC 0 (by simpa)
  obtain âŸ¨n, hnâŸ© := exists_nat_gt (C / Îµ)
  have hnpos : 0 < (n : â„) := (div_pos hCâ‚€ hÎµ).trans hn
  have hnâ‚€ : n â‰  0 := by rintro rfl; simp at hnpos
  refine âŸ¨Î´ / n, div_pos hÎ´ hnpos, fun {x} hxÎ´ => ?_âŸ©
  calc
    â€–f xâ€–
    _ = â€–(n : â„)â»Â¹ â€¢ f (n â€¢ x)â€– := by simp [â† Nat.cast_smul_eq_nsmul â„, hnâ‚€]
    _ â‰¤ â€–(n : â„)â»Â¹â€– * â€–f (n â€¢ x)â€– := norm_smul_le ..
    _ < â€–(n : â„)â»Â¹â€– * C := by
      gcongr
      Â· simpa [pos_iff_ne_zero]
      Â· refine hC _ <| norm_nsmul_le.trans_lt ?_
        simpa only [norm_mul, Real.norm_natCast, lt_div_iffâ‚€ hnpos, mul_comm] using hxÎ´
    _ = (n : â„)â»Â¹ * C := by simp
    _ < (C / Îµ : â„)â»Â¹ * C := by gcongr
    _ = Îµ := by simp [hCâ‚€.ne']
