/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Alexander Bentkamp, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Convex.Jensen
public import Mathlib.Analysis.Convex.PathConnected
public import Mathlib.Analysis.Convex.Topology
public import Mathlib.Analysis.Normed.Group.Pointwise
public import Mathlib.Analysis.Normed.Module.Basic
public import Mathlib.Analysis.Normed.Module.RCLike.Real

/-!
# Metric properties of convex sets in normed spaces

We prove the following facts:

* `convexOn_norm`, `convexOn_dist` : norm and distance to a fixed point is convex on any convex
  set;
* `convexOn_univ_norm`, `convexOn_univ_dist` : norm and distance to a fixed point is convex on
  the whole space;
* `convexHull_ediam`, `convexHull_diam` : convex hull of a set has the same (e)metric diameter
  as the original set;
* `isBounded_convexHull` : convex hull of a set is bounded if and only if the original set
  is bounded.
-/

@[expose] public section

-- TODO assert_not_exists Cardinal

variable {E : Type*}

open Metric Set

section SeminormedAddCommGroup
variable [SeminormedAddCommGroup E] [NormedSpace ‚Ñù E]
variable {s : Set E}

/-- The norm on a real normed space is convex on any convex set. See also `Seminorm.convexOn`
and `convexOn_univ_norm`. -/
theorem convexOn_norm (hs : Convex ‚Ñù s) : ConvexOn ‚Ñù s norm :=
  ‚ü®hs, fun x _ y _ a b ha hb _ =>
    calc
      ‚Äña ‚Ä¢ x + b ‚Ä¢ y‚Äñ ‚â§ ‚Äña ‚Ä¢ x‚Äñ + ‚Äñb ‚Ä¢ y‚Äñ := norm_add_le _ _
      _ = a * ‚Äñx‚Äñ + b * ‚Äñy‚Äñ := by
        rw [norm_smul, norm_smul, Real.norm_of_nonneg ha, Real.norm_of_nonneg hb]‚ü©

/-- The norm on a real normed space is convex on the whole space. See also `Seminorm.convexOn`
and `convexOn_norm`. -/
theorem convexOn_univ_norm : ConvexOn ‚Ñù univ (norm : E ‚Üí ‚Ñù) :=
  convexOn_norm convex_univ

theorem convexOn_dist (z : E) (hs : Convex ‚Ñù s) : ConvexOn ‚Ñù s fun z' => dist z' z := by
  simpa [dist_eq_norm, preimage_preimage] using
    (convexOn_norm (hs.translate (-z))).comp_affineMap (AffineMap.id ‚Ñù E - AffineMap.const ‚Ñù E z)

theorem convexOn_univ_dist (z : E) : ConvexOn ‚Ñù univ fun z' => dist z' z :=
  convexOn_dist z convex_univ

theorem convex_ball (a : E) (r : ‚Ñù) : Convex ‚Ñù (Metric.ball a r) := by
  simpa only [Metric.ball, sep_univ] using (convexOn_univ_dist a).convex_lt r

theorem convex_closedBall (a : E) (r : ‚Ñù) : Convex ‚Ñù (Metric.closedBall a r) := by
  simpa only [Metric.closedBall, sep_univ] using (convexOn_univ_dist a).convex_le r

open Pointwise in
theorem convexHull_sphere_eq_closedBall {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
    [Nontrivial F] (x : F) {r : ‚Ñù} (hr : 0 ‚â§ r) :
    convexHull ‚Ñù (sphere x r) = closedBall x r := by
  suffices convexHull ‚Ñù (sphere (0 : F) r) = closedBall 0 r by
    rw [‚Üê add_zero x, ‚Üê vadd_eq_add, ‚Üê vadd_sphere, convexHull_vadd,
      this, vadd_closedBall_zero, vadd_eq_add, add_zero]
  refine subset_antisymm (convexHull_min sphere_subset_closedBall (convex_closedBall 0 r))
    (fun x h ‚Ü¶ mem_convexHull_iff.mpr fun U hU_sub hU ‚Ü¶ ?_)
  have zero_mem : (0 : F) ‚àà U := by
    have _ : Invertible (2 : ‚Ñù) := by use 2‚Åª¬π <;> grind
    obtain ‚ü®z, hz‚ü© := NormedSpace.sphere_nonempty (E := F).mpr hr
    rw [‚Üê midpoint_self_neg (R := ‚Ñù) (x := z)]
    exact Convex.midpoint_mem hU (hU_sub hz) <| hU_sub (by simp_all)
  by_cases hr‚ÇÄ : r = 0
  ¬∑ simp_all
  by_cases x_zero : x = 0
  ¬∑ rwa [x_zero]
  set z := (r * ‚Äñx‚Äñ‚Åª¬π) ‚Ä¢ x with hz_def
  have hr‚ÇÅ : r‚Åª¬π * ‚Äñx‚Äñ ‚â§ 1 := by
    simp only [mem_closedBall, dist_zero_right] at h
    grw [h, inv_mul_le_one]
  have hz : z ‚àà U := by
    apply hU_sub
    simp_all [norm_smul]
  have := StarConvex.smul_mem (hU.starConvex zero_mem) hz (by positivity) hr‚ÇÅ
  rwa [hz_def, ‚Üê smul_assoc, smul_eq_mul, ‚Üê mul_assoc, mul_comm, mul_comm r‚Åª¬π, mul_assoc _ r‚Åª¬π,
    inv_mul_cancel‚ÇÄ hr‚ÇÄ, mul_one, inv_mul_cancel‚ÇÄ (by simp_all), one_smul] at this

/-- Given a point `x` in the convex hull of `s` and a point `y`, there exists a point
of `s` at distance at least `dist x y` from `y`. -/
theorem convexHull_exists_dist_ge {s : Set E} {x : E} (hx : x ‚àà convexHull ‚Ñù s) (y : E) :
    ‚àÉ x' ‚àà s, dist x y ‚â§ dist x' y :=
  (convexOn_dist y (convex_convexHull ‚Ñù _)).exists_ge_of_mem_convexHull (subset_convexHull ..) hx

theorem Convex.thickening (hs : Convex ‚Ñù s) (Œ¥ : ‚Ñù) : Convex ‚Ñù (thickening Œ¥ s) := by
  rw [‚Üê add_ball_zero]
  exact hs.add (convex_ball 0 _)

theorem Convex.cthickening (hs : Convex ‚Ñù s) (Œ¥ : ‚Ñù) : Convex ‚Ñù (cthickening Œ¥ s) := by
  obtain hŒ¥ | hŒ¥ := le_total 0 Œ¥
  ¬∑ rw [cthickening_eq_iInter_thickening hŒ¥]
    exact convex_iInter‚ÇÇ fun _ _ => hs.thickening _
  ¬∑ rw [cthickening_of_nonpos hŒ¥]
    exact hs.closure

/-- Given a point `x` in the convex hull of `s` and a point `y` in the convex hull of `t`,
there exist points `x' ‚àà s` and `y' ‚àà t` at distance at least `dist x y`. -/
theorem convexHull_exists_dist_ge2 {s t : Set E} {x y : E} (hx : x ‚àà convexHull ‚Ñù s)
    (hy : y ‚àà convexHull ‚Ñù t) : ‚àÉ x' ‚àà s, ‚àÉ y' ‚àà t, dist x y ‚â§ dist x' y' := by
  rcases convexHull_exists_dist_ge hx y with ‚ü®x', hx', Hx'‚ü©
  rcases convexHull_exists_dist_ge hy x' with ‚ü®y', hy', Hy'‚ü©
  use x', hx', y', hy'
  exact le_trans Hx' (dist_comm y x' ‚ñ∏ dist_comm y' x' ‚ñ∏ Hy')

/-- Emetric diameter of the convex hull of a set `s` equals the emetric diameter of `s`. -/
@[simp]
theorem convexHull_ediam (s : Set E) : ediam (convexHull ‚Ñù s) = ediam s := by
  refine (ediam_le fun x hx y hy => ?_).antisymm (ediam_mono <| subset_convexHull ‚Ñù s)
  rcases convexHull_exists_dist_ge2 hx hy with ‚ü®x', hx', y', hy', H‚ü©
  rw [edist_dist]
  apply le_trans (ENNReal.ofReal_le_ofReal H)
  rw [‚Üê edist_dist]
  exact edist_le_ediam_of_mem hx' hy'

/-- Diameter of the convex hull of a set `s` equals the emetric diameter of `s`. -/
@[simp]
theorem convexHull_diam (s : Set E) : Metric.diam (convexHull ‚Ñù s) = Metric.diam s := by
  simp only [Metric.diam, convexHull_ediam]

/-- Convex hull of `s` is bounded if and only if `s` is bounded. -/
@[simp]
theorem isBounded_convexHull {s : Set E} :
    Bornology.IsBounded (convexHull ‚Ñù s) ‚Üî Bornology.IsBounded s := by
  simp only [Metric.isBounded_iff_ediam_ne_top, convexHull_ediam]

instance (priority := 100) NormedSpace.instPathConnectedSpace : PathConnectedSpace E :=
  IsTopologicalAddGroup.pathConnectedSpace

/-- The set of vectors in the same ray as `x` is connected. -/
theorem isConnected_setOf_sameRay (x : E) : IsConnected { y | SameRay ‚Ñù x y } := by
  by_cases hx : x = 0; ¬∑ simpa [hx] using isConnected_univ (Œ± := E)
  simp_rw [‚Üê exists_nonneg_left_iff_sameRay hx]
  exact isConnected_Ici.image _ (by fun_prop)

/-- The set of nonzero vectors in the same ray as the nonzero vector `x` is connected. -/
theorem isConnected_setOf_sameRay_and_ne_zero {x : E} (hx : x ‚â† 0) :
    IsConnected { y | SameRay ‚Ñù x y ‚àß y ‚â† 0 } := by
  simp_rw [‚Üê exists_pos_left_iff_sameRay_and_ne_zero hx]
  exact isConnected_Ioi.image _ (by fun_prop)

lemma norm_sub_le_of_mem_segment {x y z : E} (hy : y ‚àà segment ‚Ñù x z) :
    ‚Äñy - x‚Äñ ‚â§ ‚Äñz - x‚Äñ := by
  rw [segment_eq_image'] at hy
  simp only [mem_image, mem_Icc] at hy
  obtain ‚ü®u, ‚ü®hu_nonneg, hu_le_one‚ü©, rfl‚ü© := hy
  simp only [add_sub_cancel_left, norm_smul, Real.norm_eq_abs]
  rw [abs_of_nonneg hu_nonneg]
  conv_rhs => rw [‚Üê one_mul (‚Äñz - x‚Äñ)]
  gcongr

namespace Filter

open scoped Convex Topology
variable {Œ± : Type*} {f : Filter Œ±} {x : E} {y z : Œ± ‚Üí E} {r : Œ± ‚Üí E ‚Üí Prop}

theorem Eventually.segment_of_prod_nhds (hy : Tendsto y f (ùìù x)) (hz : Tendsto z f (ùìù x))
    (hr : ‚àÄ·∂† p in f √óÀ¢ ùìù x, r p.1 p.2) : ‚àÄ·∂† œá in f, ‚àÄ v ‚àà [y œá -[‚Ñù] z œá], r œá v := by
  obtain ‚ü®p, hp, Œ¥, hŒ¥, hr‚ü© := eventually_prod_nhds_iff.mp hr
  rw [Metric.tendsto_nhds] at hy hz
  filter_upwards [hp, hy Œ¥ hŒ¥, hz Œ¥ hŒ¥] with œá hp hy hz
  exact fun v hv => hr hp <| convex_iff_segment_subset.mp (convex_ball x Œ¥) hy hz hv

theorem Eventually.segment_of_prod_nhdsWithin (hy : Tendsto y f (ùìù x)) (hz : Tendsto z f (ùìù x))
    (hr : ‚àÄ·∂† p in f √óÀ¢ ùìù[s] x, r p.1 p.2) (seg : ‚àÄ·∂† œá in f, [y œá -[‚Ñù] z œá] ‚äÜ s) :
    ‚àÄ·∂† œá in f, ‚àÄ v ‚àà [y œá -[‚Ñù] z œá], r œá v := by
  refine seg.mp <| .mono ?_ (fun _ => forall‚ÇÇ_imp)
  apply Eventually.segment_of_prod_nhds hy hz
  simpa [nhdsWithin, prod_eq_inf, ‚Üê inf_assoc, eventually_inf_principal] using hr

end Filter

end SeminormedAddCommGroup
