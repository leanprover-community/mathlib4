/-
Copyright (c) 2021 Christopher Hoskin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christopher Hoskin
-/
import Mathlib.Topology.Order.Lattice
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Algebra.Order.LatticeGroup

#align_import analysis.normed.order.lattice from "leanprover-community/mathlib"@"5dc275ec639221ca4d5f56938eb966f6ad9bc89f"

/-!
# Normed lattice ordered groups

Motivated by the theory of Banach Lattices, we then define `NormedLatticeAddCommGroup` as a
lattice with a covariant normed group addition satisfying the solid axiom.

## Main statements

We show that a normed lattice ordered group is a topological lattice with respect to the norm
topology.

## References

* [Meyer-Nieberg, Banach lattices][MeyerNieberg1991]

## Tags

normed, lattice, ordered, group
-/


/-!
### Normed lattice ordered groups

Motivated by the theory of Banach Lattices, this section introduces normed lattice ordered groups.
-/


-- porting note: this now exists as a global notation
-- local notation "|" a "|" => abs a

section SolidNorm

/-- Let `Œ±` be an `AddCommGroup` with a `Lattice` structure. A norm on `Œ±` is *solid* if, for `a`
and `b` in `Œ±`, with absolute values `|a|` and `|b|` respectively, `|a| ‚â§ |b|` implies `‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ`.
-/
class HasSolidNorm (Œ± : Type*) [NormedAddCommGroup Œ±] [Lattice Œ±] : Prop where
  solid : ‚àÄ ‚¶Éx y : Œ±‚¶Ñ, |x| ‚â§ |y| ‚Üí ‚Äñx‚Äñ ‚â§ ‚Äñy‚Äñ
#align has_solid_norm HasSolidNorm

variable {Œ± : Type*} [NormedAddCommGroup Œ±] [Lattice Œ±] [HasSolidNorm Œ±]

theorem norm_le_norm_of_abs_le_abs {a b : Œ±} (h : |a| ‚â§ |b|) : ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ :=
  HasSolidNorm.solid h
#align norm_le_norm_of_abs_le_abs norm_le_norm_of_abs_le_abs

/-- If `Œ±` has a solid norm, then the balls centered at the origin of `Œ±` are solid sets. -/
theorem LatticeOrderedAddCommGroup.isSolid_ball (r : ‚Ñù) :
    LatticeOrderedAddCommGroup.IsSolid (Metric.ball (0 : Œ±) r) := fun _ hx _ hxy =>
  mem_ball_zero_iff.mpr ((HasSolidNorm.solid hxy).trans_lt (mem_ball_zero_iff.mp hx))
#align lattice_ordered_add_comm_group.is_solid_ball LatticeOrderedAddCommGroup.isSolid_ball

instance : HasSolidNorm ‚Ñù := ‚ü®fun _ _ => id‚ü©

instance : HasSolidNorm ‚Ñö := ‚ü®fun _ _ _ => by simpa only [norm, ‚Üê Rat.cast_abs, Rat.cast_le] ‚ü©
                                              -- üéâ no goals

end SolidNorm

/--
Let `Œ±` be a normed commutative group equipped with a partial order covariant with addition, with
respect which `Œ±` forms a lattice. Suppose that `Œ±` is *solid*, that is to say, for `a` and `b` in
`Œ±`, with absolute values `|a|` and `|b|` respectively, `|a| ‚â§ |b|` implies `‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ`. Then `Œ±` is
said to be a normed lattice ordered group.
-/
class NormedLatticeAddCommGroup (Œ± : Type*) extends NormedAddCommGroup Œ±, Lattice Œ±, HasSolidNorm Œ±
  where
  add_le_add_left : ‚àÄ a b : Œ±, a ‚â§ b ‚Üí ‚àÄ c : Œ±, c + a ‚â§ c + b
#align normed_lattice_add_comm_group NormedLatticeAddCommGroup

instance Real.normedLatticeAddCommGroup : NormedLatticeAddCommGroup ‚Ñù where
  add_le_add_left _ _ h _ := add_le_add le_rfl h

-- see Note [lower instance priority]
/-- A normed lattice ordered group is an ordered additive commutative group
-/
instance (priority := 100) NormedLatticeAddCommGroup.toOrderedAddCommGroup {Œ± : Type*}
    [h : NormedLatticeAddCommGroup Œ±] : OrderedAddCommGroup Œ± :=
  { h with }
#align normed_lattice_add_comm_group_to_ordered_add_comm_group NormedLatticeAddCommGroup.toOrderedAddCommGroup

variable {Œ± : Type*} [NormedLatticeAddCommGroup Œ±]

open LatticeOrderedGroup LatticeOrderedCommGroup HasSolidNorm

theorem dual_solid (a b : Œ±) (h : b ‚äì -b ‚â§ a ‚äì -a) : ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ := by
  apply solid
  -- ‚ä¢ |a| ‚â§ |b|
  rw [abs_eq_sup_neg]
  -- ‚ä¢ a ‚äî -a ‚â§ |b|
  nth_rw 1 [‚Üê neg_neg a]
  -- ‚ä¢ - -a ‚äî -a ‚â§ |b|
  rw [‚Üê neg_inf_eq_sup_neg]
  -- ‚ä¢ -(-a ‚äì a) ‚â§ |b|
  rw [abs_eq_sup_neg]
  -- ‚ä¢ -(-a ‚äì a) ‚â§ b ‚äî -b
  nth_rw 1 [‚Üê neg_neg b]
  -- ‚ä¢ -(-a ‚äì a) ‚â§ - -b ‚äî -b
  rwa [‚Üê neg_inf_eq_sup_neg, neg_le_neg_iff, @inf_comm _ _ _ b, @inf_comm _ _ _ a]
  -- üéâ no goals
#align dual_solid dual_solid

-- see Note [lower instance priority]
/-- Let `Œ±` be a normed lattice ordered group, then the order dual is also a
normed lattice ordered group.
-/
instance (priority := 100) OrderDual.normedLatticeAddCommGroup : NormedLatticeAddCommGroup Œ±·µí·µà :=
  { OrderDual.orderedAddCommGroup, OrderDual.normedAddCommGroup, OrderDual.lattice Œ± with
    solid := dual_solid (Œ± := Œ±) }

theorem norm_abs_eq_norm (a : Œ±) : ‚Äñ|a|‚Äñ = ‚Äña‚Äñ :=
  (solid (abs_abs a).le).antisymm (solid (abs_abs a).symm.le)
#align norm_abs_eq_norm norm_abs_eq_norm

theorem norm_inf_sub_inf_le_add_norm (a b c d : Œ±) : ‚Äña ‚äì b - c ‚äì d‚Äñ ‚â§ ‚Äña - c‚Äñ + ‚Äñb - d‚Äñ := by
  rw [‚Üê norm_abs_eq_norm (a - c), ‚Üê norm_abs_eq_norm (b - d)]
  -- ‚ä¢ ‚Äña ‚äì b - c ‚äì d‚Äñ ‚â§ ‚Äñ|a - c|‚Äñ + ‚Äñ|b - d|‚Äñ
  refine' le_trans (solid _) (norm_add_le |a - c| |b - d|)
  -- ‚ä¢ |a ‚äì b - c ‚äì d| ‚â§ ||a - c| + |b - d||
  rw [abs_of_nonneg (|a - c| + |b - d|) (add_nonneg (abs_nonneg (a - c)) (abs_nonneg (b - d)))]
  -- ‚ä¢ |a ‚äì b - c ‚äì d| ‚â§ |a - c| + |b - d|
  calc
    |a ‚äì b - c ‚äì d| = |a ‚äì b - c ‚äì b + (c ‚äì b - c ‚äì d)| := by rw [sub_add_sub_cancel]
    _ ‚â§ |a ‚äì b - c ‚äì b| + |c ‚äì b - c ‚äì d| := (abs_add_le _ _)
    _ ‚â§ |a - c| + |b - d| := by
      apply add_le_add
      ¬∑ exact abs_inf_sub_inf_le_abs _ _ _
      ¬∑ rw [@inf_comm _ _ c, @inf_comm _ _ c]
        exact abs_inf_sub_inf_le_abs _ _ _
#align norm_inf_sub_inf_le_add_norm norm_inf_sub_inf_le_add_norm

theorem norm_sup_sub_sup_le_add_norm (a b c d : Œ±) : ‚Äña ‚äî b - c ‚äî d‚Äñ ‚â§ ‚Äña - c‚Äñ + ‚Äñb - d‚Äñ := by
  rw [‚Üê norm_abs_eq_norm (a - c), ‚Üê norm_abs_eq_norm (b - d)]
  -- ‚ä¢ ‚Äña ‚äî b - c ‚äî d‚Äñ ‚â§ ‚Äñ|a - c|‚Äñ + ‚Äñ|b - d|‚Äñ
  refine' le_trans (solid _) (norm_add_le |a - c| |b - d|)
  -- ‚ä¢ |a ‚äî b - c ‚äî d| ‚â§ ||a - c| + |b - d||
  rw [abs_of_nonneg (|a - c| + |b - d|) (add_nonneg (abs_nonneg (a - c)) (abs_nonneg (b - d)))]
  -- ‚ä¢ |a ‚äî b - c ‚äî d| ‚â§ |a - c| + |b - d|
  calc
    |a ‚äî b - c ‚äî d| = |a ‚äî b - c ‚äî b + (c ‚äî b - c ‚äî d)| := by rw [sub_add_sub_cancel]
    _ ‚â§ |a ‚äî b - c ‚äî b| + |c ‚äî b - c ‚äî d| := (abs_add_le _ _)
    _ ‚â§ |a - c| + |b - d| := by
      apply add_le_add
      ¬∑ exact abs_sup_sub_sup_le_abs _ _ _
      ¬∑ rw [@sup_comm _ _ c, @sup_comm _ _ c]
        exact abs_sup_sub_sup_le_abs _ _ _
#align norm_sup_sub_sup_le_add_norm norm_sup_sub_sup_le_add_norm

theorem norm_inf_le_add (x y : Œ±) : ‚Äñx ‚äì y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ := by
  have h : ‚Äñx ‚äì y - 0 ‚äì 0‚Äñ ‚â§ ‚Äñx - 0‚Äñ + ‚Äñy - 0‚Äñ := norm_inf_sub_inf_le_add_norm x y 0 0
  -- ‚ä¢ ‚Äñx ‚äì y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ
  simpa only [inf_idem, sub_zero] using h
  -- üéâ no goals
#align norm_inf_le_add norm_inf_le_add

theorem norm_sup_le_add (x y : Œ±) : ‚Äñx ‚äî y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ := by
  have h : ‚Äñx ‚äî y - 0 ‚äî 0‚Äñ ‚â§ ‚Äñx - 0‚Äñ + ‚Äñy - 0‚Äñ := norm_sup_sub_sup_le_add_norm x y 0 0
  -- ‚ä¢ ‚Äñx ‚äî y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ
  simpa only [sup_idem, sub_zero] using h
  -- üéâ no goals
#align norm_sup_le_add norm_sup_le_add

-- see Note [lower instance priority]
/-- Let `Œ±` be a normed lattice ordered group. Then the infimum is jointly continuous.
-/
instance (priority := 100) NormedLatticeAddCommGroup.continuousInf : ContinuousInf Œ± := by
  refine' ‚ü®continuous_iff_continuousAt.2 fun q => tendsto_iff_norm_tendsto_zero.2 <| _‚ü©
  -- ‚ä¢ Filter.Tendsto (fun e => ‚Äñe.fst ‚äì e.snd - (fun p => p.fst ‚äì p.snd) q‚Äñ) (nhds ‚Ä¶
  have : ‚àÄ p : Œ± √ó Œ±, ‚Äñp.1 ‚äì p.2 - q.1 ‚äì q.2‚Äñ ‚â§ ‚Äñp.1 - q.1‚Äñ + ‚Äñp.2 - q.2‚Äñ := fun _ =>
    norm_inf_sub_inf_le_add_norm _ _ _ _
  refine' squeeze_zero (fun e => norm_nonneg _) this _
  -- ‚ä¢ Filter.Tendsto (fun t => ‚Äñt.fst - q.fst‚Äñ + ‚Äñt.snd - q.snd‚Äñ) (nhds q) (nhds 0)
  convert ((continuous_fst.tendsto q).sub <| tendsto_const_nhds).norm.add
    ((continuous_snd.tendsto q).sub <| tendsto_const_nhds).norm
  simp
  -- üéâ no goals
#align normed_lattice_add_comm_group_has_continuous_inf NormedLatticeAddCommGroup.continuousInf

-- see Note [lower instance priority]
instance (priority := 100) NormedLatticeAddCommGroup.continuousSup {Œ± : Type*}
    [NormedLatticeAddCommGroup Œ±] : ContinuousSup Œ± :=
  OrderDual.continuousSup Œ±·µí·µà
#align normed_lattice_add_comm_group_has_continuous_sup NormedLatticeAddCommGroup.continuousSup

-- see Note [lower instance priority]
/--
Let `Œ±` be a normed lattice ordered group. Then `Œ±` is a topological lattice in the norm topology.
-/
instance (priority := 100) NormedLatticeAddCommGroup.toTopologicalLattice : TopologicalLattice Œ± :=
  TopologicalLattice.mk
#align normed_lattice_add_comm_group_topological_lattice NormedLatticeAddCommGroup.toTopologicalLattice

theorem norm_abs_sub_abs (a b : Œ±) : ‚Äñ|a| - |b|‚Äñ ‚â§ ‚Äña - b‚Äñ :=
  solid (LatticeOrderedCommGroup.abs_abs_sub_abs_le _ _)
#align norm_abs_sub_abs norm_abs_sub_abs

theorem norm_sup_sub_sup_le_norm (x y z : Œ±) : ‚Äñx ‚äî z - y ‚äî z‚Äñ ‚â§ ‚Äñx - y‚Äñ :=
  solid (abs_sup_sub_sup_le_abs x y z)
#align norm_sup_sub_sup_le_norm norm_sup_sub_sup_le_norm

theorem norm_inf_sub_inf_le_norm (x y z : Œ±) : ‚Äñx ‚äì z - y ‚äì z‚Äñ ‚â§ ‚Äñx - y‚Äñ :=
  solid (abs_inf_sub_inf_le_abs x y z)
#align norm_inf_sub_inf_le_norm norm_inf_sub_inf_le_norm

theorem lipschitzWith_sup_right (z : Œ±) : LipschitzWith 1 fun x => x ‚äî z :=
  LipschitzWith.of_dist_le_mul fun x y => by
    rw [NNReal.coe_one, one_mul, dist_eq_norm, dist_eq_norm]
    -- ‚ä¢ ‚Äñx ‚äî z - y ‚äî z‚Äñ ‚â§ ‚Äñx - y‚Äñ
    exact norm_sup_sub_sup_le_norm x y z
    -- üéâ no goals
#align lipschitz_with_sup_right lipschitzWith_sup_right

theorem lipschitzWith_pos : LipschitzWith 1 (PosPart.pos : Œ± ‚Üí Œ±) :=
  lipschitzWith_sup_right 0
#align lipschitz_with_pos lipschitzWith_pos

theorem continuous_pos : Continuous (PosPart.pos : Œ± ‚Üí Œ±) :=
  LipschitzWith.continuous lipschitzWith_pos
#align continuous_pos continuous_pos

theorem continuous_neg' : Continuous (NegPart.neg : Œ± ‚Üí Œ±) := by
  refine continuous_pos.comp <| @continuous_neg _ _ _ TopologicalAddGroup.toContinuousNeg
  -- üéâ no goals
  -- porting note: see the [Zulip thread](https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/can't.20infer.20.60ContinuousNeg.60)
#align continuous_neg' continuous_neg'

theorem isClosed_nonneg {E} [NormedLatticeAddCommGroup E] : IsClosed { x : E | 0 ‚â§ x } := by
  suffices { x : E | 0 ‚â§ x } = NegPart.neg ‚Åª¬π' {(0 : E)} by
    rw [this]
    exact IsClosed.preimage continuous_neg' isClosed_singleton
  ext1 x
  -- ‚ä¢ x ‚àà {x | 0 ‚â§ x} ‚Üî x ‚àà NegPart.neg ‚Åª¬π' {0}
  simp only [Set.mem_preimage, Set.mem_singleton_iff, Set.mem_setOf_eq,
    @neg_eq_zero_iff E _ _ (OrderedAddCommGroup.to_covariantClass_left_le E)]
  -- porting note: I'm not sure why Lean couldn't synthesize this instance because it works with
  -- `have : CovariantClass E E (¬∑ + ¬∑) (¬∑ ‚â§ ¬∑) := inferInstance`
#align is_closed_nonneg isClosed_nonneg

theorem isClosed_le_of_isClosed_nonneg {G} [OrderedAddCommGroup G] [TopologicalSpace G]
    [ContinuousSub G] (h : IsClosed { x : G | 0 ‚â§ x }) :
    IsClosed { p : G √ó G | p.fst ‚â§ p.snd } := by
  have : { p : G √ó G | p.fst ‚â§ p.snd } = (fun p : G √ó G => p.snd - p.fst) ‚Åª¬π' { x : G | 0 ‚â§ x } :=
    by ext1 p; simp only [sub_nonneg, Set.preimage_setOf_eq]
  rw [this]
  -- ‚ä¢ IsClosed ((fun p => p.snd - p.fst) ‚Åª¬π' {x | 0 ‚â§ x})
  exact IsClosed.preimage (continuous_snd.sub continuous_fst) h
  -- üéâ no goals
#align is_closed_le_of_is_closed_nonneg isClosed_le_of_isClosed_nonneg

-- See note [lower instance priority]
instance (priority := 100) NormedLatticeAddCommGroup.orderClosedTopology {E}
    [NormedLatticeAddCommGroup E] : OrderClosedTopology E :=
  ‚ü®isClosed_le_of_isClosed_nonneg isClosed_nonneg‚ü©
#align normed_lattice_add_comm_group.order_closed_topology NormedLatticeAddCommGroup.orderClosedTopology
