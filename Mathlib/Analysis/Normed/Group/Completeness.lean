/-
Copyright (c) 2023 Fr√©d√©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Fr√©d√©ric Dupuis
-/

import Mathlib.Analysis.Normed.Group.Uniform
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Completeness of normed groups

This file includes a completeness criterion for normed additive groups in terms of convergent
series.

## Main results

* `NormedAddCommGroup.completeSpace_of_summable_imp_tendsto`: A normed additive group is
  complete if any absolutely convergent series converges in the space.

## References

* [bergh_lofstrom_1976] `NormedAddCommGroup.completeSpace_of_summable_imp_tendsto` and
  `NormedAddCommGroup.summable_imp_tendsto_of_complete` correspond to the two directions of
  Lemma 2.2.1.

## Tags

CompleteSpace, CauchySeq
-/

open scoped Topology
open Filter Finset

section Metric

variable {Œ± : Type*} [PseudoMetricSpace Œ±]

lemma Metric.exists_subseq_summable_dist_of_cauchySeq (u : ‚Ñï ‚Üí Œ±) (hu : CauchySeq u) :
    ‚àÉ f : ‚Ñï ‚Üí ‚Ñï, StrictMono f ‚àß Summable fun i ‚Ü¶ dist (u (f (i+1))) (u (f i)) := by
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := Metric.exists_subseq_bounded_of_cauchySeq u hu
    (fun n ‚Ü¶ (1 / (2 : ‚Ñù))^n) (fun n ‚Ü¶ by positivity)
  refine ‚ü®f, hf‚ÇÅ, ?_‚ü©
  refine Summable.of_nonneg_of_le (fun n ‚Ü¶ by positivity) ?_ summable_geometric_two
  exact fun n ‚Ü¶ le_of_lt <| hf‚ÇÇ n (f (n + 1)) <| hf‚ÇÅ.monotone (Nat.le_add_right n 1)

end Metric

section Normed

variable {E : Type*} [NormedAddCommGroup E]

/-- A normed additive group is complete if any absolutely convergent series converges in the
space. -/
lemma NormedAddCommGroup.completeSpace_of_summable_imp_tendsto
    (h : ‚àÄ u : ‚Ñï ‚Üí E,
      Summable (‚Äñu ¬∑‚Äñ) ‚Üí ‚àÉ a, Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, u i) atTop (ùìù a)) :
    CompleteSpace E := by
  apply Metric.complete_of_cauchySeq_tendsto
  intro u hu
  obtain ‚ü®f, hf‚ÇÅ, hf‚ÇÇ‚ü© := Metric.exists_subseq_summable_dist_of_cauchySeq u hu
  simp only [dist_eq_norm] at hf‚ÇÇ
  let v n := u (f (n + 1)) - u (f n)
  have hv_sum : (fun n ‚Ü¶ (‚àë i ‚àà range n, v i)) = fun n ‚Ü¶ u (f n) - u (f 0) := by
    ext n
    exact sum_range_sub (u ‚àò f) n
  obtain ‚ü®a, ha‚ü© := h v hf‚ÇÇ
  refine ‚ü®a + u (f 0), ?_‚ü©
  refine tendsto_nhds_of_cauchySeq_of_subseq hu hf‚ÇÅ.tendsto_atTop ?_
  rw [hv_sum] at ha
  have h‚ÇÅ : Tendsto (fun n ‚Ü¶ u (f n) - u (f 0) + u (f 0)) atTop (ùìù (a + u (f 0))) :=
    Tendsto.add_const _ ha
  simpa only [sub_add_cancel] using h‚ÇÅ

/-- In a complete normed additive group, every absolutely convergent series converges in the
space. -/
lemma NormedAddCommGroup.summable_imp_tendsto_of_complete [CompleteSpace E] (u : ‚Ñï ‚Üí E)
    (hu : Summable (‚Äñu ¬∑‚Äñ)) : ‚àÉ a, Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, u i) atTop (ùìù a) := by
  refine cauchySeq_tendsto_of_complete <| cauchySeq_of_summable_dist ?_
  simp [dist_eq_norm, sum_range_succ, hu]

/-- In a normed additive group, every absolutely convergent series converges in the
space iff the space is complete. -/
lemma NormedAddCommGroup.summable_imp_tendsto_iff_completeSpace :
    (‚àÄ u : ‚Ñï ‚Üí E, Summable (‚Äñu ¬∑‚Äñ) ‚Üí ‚àÉ a, Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, u i) atTop (ùìù a))
     ‚Üî CompleteSpace E :=
  ‚ü®completeSpace_of_summable_imp_tendsto, fun _ u hu ‚Ü¶ summable_imp_tendsto_of_complete u hu‚ü©

end Normed
