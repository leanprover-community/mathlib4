/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl, Ya√´l Dillies
-/
import Mathlib.Analysis.Normed.Group.Seminorm
import Mathlib.Order.LiminfLimsup
import Mathlib.Topology.Instances.Rat
import Mathlib.Topology.MetricSpace.Algebra
import Mathlib.Topology.MetricSpace.IsometricSMul
import Mathlib.Topology.Sequences

#align_import analysis.normed.group.basic from "leanprover-community/mathlib"@"41bef4ae1254365bc190aee63b947674d2977f01"

/-!
# Normed (semi)groups

In this file we define 10 classes:

* `Norm`, `NNNorm`: auxiliary classes endowing a type `Œ±` with a function `norm : Œ± ‚Üí ‚Ñù`
  (notation: `‚Äñx‚Äñ`) and `nnnorm : Œ± ‚Üí ‚Ñù‚â•0` (notation: `‚Äñx‚Äñ‚Çä`), respectively;
* `Seminormed...Group`: A seminormed (additive) (commutative) group is an (additive) (commutative)
  group with a norm and a compatible pseudometric space structure:
  `‚àÄ x y, dist x y = ‚Äñx / y‚Äñ` or `‚àÄ x y, dist x y = ‚Äñx - y‚Äñ`, depending on the group operation.
* `Normed...Group`: A normed (additive) (commutative) group is an (additive) (commutative) group
  with a norm and a compatible metric space structure.

We also prove basic properties of (semi)normed groups and provide some instances.

## Notes

The current convention `dist x y = ‚Äñx - y‚Äñ` means that the distance is invariant under right
addition, but actions in mathlib are usually from the left. This means we might want to change it to
`dist x y = ‚Äñ-x + y‚Äñ`.

The normed group hierarchy would lend itself well to a mixin design (that is, having
`SeminormedGroup` and `SeminormedAddGroup` not extend `Group` and `AddGroup`), but we choose not
to for performance concerns.

## Tags

normed group
-/


variable {ùìï ùïú Œ± Œπ Œ∫ E F G : Type*}

open Filter Function Metric Bornology

open BigOperators ENNReal Filter NNReal Uniformity Pointwise Topology

/-- Auxiliary class, endowing a type `E` with a function `norm : E ‚Üí ‚Ñù` with notation `‚Äñx‚Äñ`. This
class is designed to be extended in more interesting classes specifying the properties of the norm.
-/
@[notation_class]
class Norm (E : Type*) where
  /-- the `‚Ñù`-valued norm function. -/
  norm : E ‚Üí ‚Ñù

/-- Auxiliary class, endowing a type `Œ±` with a function `nnnorm : Œ± ‚Üí ‚Ñù‚â•0` with notation `‚Äñx‚Äñ‚Çä`. -/
@[notation_class]
class NNNorm (E : Type*) where
  /-- the `‚Ñù‚â•0`-valued norm function. -/
  nnnorm : E ‚Üí ‚Ñù‚â•0

export Norm (norm)

export NNNorm (nnnorm)

@[inherit_doc]
notation "‚Äñ" e "‚Äñ" => norm e

@[inherit_doc]
notation "‚Äñ" e "‚Äñ‚Çä" => nnnorm e

/-- A seminormed group is an additive group endowed with a norm for which `dist x y = ‚Äñx - y‚Äñ`
defines a pseudometric space structure. -/
class SeminormedAddGroup (E : Type*) extends Norm E, AddGroup E, PseudoMetricSpace E where
  dist := fun x y => ‚Äñx - y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

/-- A seminormed group is a group endowed with a norm for which `dist x y = ‚Äñx / y‚Äñ` defines a
pseudometric space structure. -/
@[to_additive]
class SeminormedGroup (E : Type*) extends Norm E, Group E, PseudoMetricSpace E where
  dist := fun x y => ‚Äñx / y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx / y‚Äñ := by aesop

/-- A normed group is an additive group endowed with a norm for which `dist x y = ‚Äñx - y‚Äñ` defines a
metric space structure. -/
class NormedAddGroup (E : Type*) extends Norm E, AddGroup E, MetricSpace E where
  dist := fun x y => ‚Äñx - y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

/-- A normed group is a group endowed with a norm for which `dist x y = ‚Äñx / y‚Äñ` defines a metric
space structure. -/
@[to_additive]
class NormedGroup (E : Type*) extends Norm E, Group E, MetricSpace E where
  dist := fun x y => ‚Äñx / y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx / y‚Äñ := by aesop

/-- A seminormed group is an additive group endowed with a norm for which `dist x y = ‚Äñx - y‚Äñ`
defines a pseudometric space structure. -/
class SeminormedAddCommGroup (E : Type*) extends Norm E, AddCommGroup E,
  PseudoMetricSpace E where
  dist := fun x y => ‚Äñx - y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

/-- A seminormed group is a group endowed with a norm for which `dist x y = ‚Äñx / y‚Äñ`
defines a pseudometric space structure. -/
@[to_additive]
class SeminormedCommGroup (E : Type*) extends Norm E, CommGroup E, PseudoMetricSpace E where
  dist := fun x y => ‚Äñx / y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx / y‚Äñ := by aesop

/-- A normed group is an additive group endowed with a norm for which `dist x y = ‚Äñx - y‚Äñ` defines a
metric space structure. -/
class NormedAddCommGroup (E : Type*) extends Norm E, AddCommGroup E, MetricSpace E where
  dist := fun x y => ‚Äñx - y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx - y‚Äñ := by aesop

/-- A normed group is a group endowed with a norm for which `dist x y = ‚Äñx / y‚Äñ` defines a metric
space structure. -/
@[to_additive]
class NormedCommGroup (E : Type*) extends Norm E, CommGroup E, MetricSpace E where
  dist := fun x y => ‚Äñx / y‚Äñ
  /-- The distance function is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = ‚Äñx / y‚Äñ := by aesop

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedGroup.toSeminormedGroup [NormedGroup E] : SeminormedGroup E :=
  { ‚ÄπNormedGroup E‚Ä∫ with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedCommGroup.toSeminormedCommGroup [NormedCommGroup E] :
    SeminormedCommGroup E :=
  { ‚ÄπNormedCommGroup E‚Ä∫ with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.toSeminormedGroup [SeminormedCommGroup E] :
    SeminormedGroup E :=
  { ‚ÄπSeminormedCommGroup E‚Ä∫ with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedCommGroup.toNormedGroup [NormedCommGroup E] : NormedGroup E :=
  { ‚ÄπNormedCommGroup E‚Ä∫ with }

-- See note [reducible non-instances]
/-- Construct a `NormedGroup` from a `SeminormedGroup` satisfying `‚àÄ x, ‚Äñx‚Äñ = 0 ‚Üí x = 1`. This
avoids having to go back to the `(Pseudo)MetricSpace` level when declaring a `NormedGroup`
instance as a special case of a more general `SeminormedGroup` instance. -/
@[to_additive (attr := reducible) "Construct a `NormedAddGroup` from a `SeminormedAddGroup`
satisfying `‚àÄ x, ‚Äñx‚Äñ = 0 ‚Üí x = 0`. This avoids having to go back to the `(Pseudo)MetricSpace`
level when declaring a `NormedAddGroup` instance as a special case of a more general
`SeminormedAddGroup` instance."]
def NormedGroup.ofSeparation [SeminormedGroup E] (h : ‚àÄ x : E, ‚Äñx‚Äñ = 0 ‚Üí x = 1) : NormedGroup E :=
  { ‚ÄπSeminormedGroup E‚Ä∫ with
    toMetricSpace :=
      { eq_of_dist_eq_zero := fun hxy =>
          div_eq_one.1 <| h _ <| by exact (‚ÄπSeminormedGroup E‚Ä∫.dist_eq _ _).symm.trans hxy } }
        -- porting note: the `rwa` no longer worked, but it was easy enough to provide the term.
        -- however, notice that if you make `x` and `y` accessible, then the following does work:
        -- `have := ‚ÄπSeminormedGroup E‚Ä∫.dist_eq x y; rwa [‚Üêthis]`, so I'm not sure why the `rwa`
        -- was broken.

-- See note [reducible non-instances]
/-- Construct a `NormedCommGroup` from a `SeminormedCommGroup` satisfying
`‚àÄ x, ‚Äñx‚Äñ = 0 ‚Üí x = 1`. This avoids having to go back to the `(Pseudo)MetricSpace` level when
declaring a `NormedCommGroup` instance as a special case of a more general `SeminormedCommGroup`
instance. -/
@[to_additive (attr := reducible) "Construct a `NormedAddCommGroup` from a
`SeminormedAddCommGroup` satisfying `‚àÄ x, ‚Äñx‚Äñ = 0 ‚Üí x = 0`. This avoids having to go back to the
`(Pseudo)MetricSpace` level when declaring a `NormedAddCommGroup` instance as a special case
of a more general `SeminormedAddCommGroup` instance."]
def NormedCommGroup.ofSeparation [SeminormedCommGroup E] (h : ‚àÄ x : E, ‚Äñx‚Äñ = 0 ‚Üí x = 1) :
    NormedCommGroup E :=
  { ‚ÄπSeminormedCommGroup E‚Ä∫, NormedGroup.ofSeparation h with }

/-- Construct a seminormed group from a multiplication-invariant distance. -/
@[to_additive "Construct a seminormed group from a translation-invariant distance."]
def SeminormedGroup.ofMulDist [Norm E] [Group E] [PseudoMetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist x y ‚â§ dist (x * z) (y * z)) :
    SeminormedGroup E where
  dist_eq x y := by
    rw [h‚ÇÅ]; apply le_antisymm
    ¬∑ simpa only [div_eq_mul_inv, ‚Üê mul_right_inv y] using h‚ÇÇ _ _ _
    ¬∑ simpa only [div_mul_cancel', one_mul] using h‚ÇÇ (x / y) 1 y

/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedGroup.ofMulDist' [Norm E] [Group E] [PseudoMetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist (x * z) (y * z) ‚â§ dist x y) :
    SeminormedGroup E where
  dist_eq x y := by
    rw [h‚ÇÅ]; apply le_antisymm
    ¬∑ simpa only [div_mul_cancel', one_mul] using h‚ÇÇ (x / y) 1 y
    ¬∑ simpa only [div_eq_mul_inv, ‚Üê mul_right_inv y] using h‚ÇÇ _ _ _

/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedCommGroup.ofMulDist [Norm E] [CommGroup E] [PseudoMetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist x y ‚â§ dist (x * z) (y * z)) :
    SeminormedCommGroup E :=
  { SeminormedGroup.ofMulDist h‚ÇÅ h‚ÇÇ with
    mul_comm := mul_comm }

/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedCommGroup.ofMulDist' [Norm E] [CommGroup E] [PseudoMetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist (x * z) (y * z) ‚â§ dist x y) :
    SeminormedCommGroup E :=
  { SeminormedGroup.ofMulDist' h‚ÇÅ h‚ÇÇ with
    mul_comm := mul_comm }

/-- Construct a normed group from a multiplication-invariant distance. -/
@[to_additive "Construct a normed group from a translation-invariant distance."]
def NormedGroup.ofMulDist [Norm E] [Group E] [MetricSpace E] (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1)
    (h‚ÇÇ : ‚àÄ x y z : E, dist x y ‚â§ dist (x * z) (y * z)) : NormedGroup E :=
  { SeminormedGroup.ofMulDist h‚ÇÅ h‚ÇÇ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a normed group from a translation-invariant pseudodistance."]
def NormedGroup.ofMulDist' [Norm E] [Group E] [MetricSpace E] (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1)
    (h‚ÇÇ : ‚àÄ x y z : E, dist (x * z) (y * z) ‚â§ dist x y) : NormedGroup E :=
  { SeminormedGroup.ofMulDist' h‚ÇÅ h‚ÇÇ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a normed group from a translation-invariant pseudodistance."]
def NormedCommGroup.ofMulDist [Norm E] [CommGroup E] [MetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist x y ‚â§ dist (x * z) (y * z)) :
    NormedCommGroup E :=
  { NormedGroup.ofMulDist h‚ÇÅ h‚ÇÇ with
    mul_comm := mul_comm }

/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive "Construct a normed group from a translation-invariant pseudodistance."]
def NormedCommGroup.ofMulDist' [Norm E] [CommGroup E] [MetricSpace E]
    (h‚ÇÅ : ‚àÄ x : E, ‚Äñx‚Äñ = dist x 1) (h‚ÇÇ : ‚àÄ x y z : E, dist (x * z) (y * z) ‚â§ dist x y) :
    NormedCommGroup E :=
  { NormedGroup.ofMulDist' h‚ÇÅ h‚ÇÇ with
    mul_comm := mul_comm }

/-- Construct a seminormed group from a seminorm, i.e., registering the pseudodistance and the
pseudometric space structure from the seminorm properties. Note that in most cases this instance
creates bad definitional equalities (e.g., it does not take into account a possibly existing
`UniformSpace` instance on `E`). -/
@[to_additive "Construct a seminormed group from a seminorm, i.e., registering the pseudodistance
and the pseudometric space structure from the seminorm properties. Note that in most cases this
instance creates bad definitional equalities (e.g., it does not take into account a possibly
existing `UniformSpace` instance on `E`)."]
def GroupSeminorm.toSeminormedGroup [Group E] (f : GroupSeminorm E) : SeminormedGroup E where
  dist x y := f (x / y)
  norm := f
  dist_eq x y := rfl
  dist_self x := by simp only [div_self', map_one_eq_zero]
  dist_triangle := le_map_div_add_map_div f
  dist_comm := map_div_rev f
  edist_dist x y := by exact ENNReal.coe_nnreal_eq _
  -- porting note: how did `mathlib3` solve this automatically?

/-- Construct a seminormed group from a seminorm, i.e., registering the pseudodistance and the
pseudometric space structure from the seminorm properties. Note that in most cases this instance
creates bad definitional equalities (e.g., it does not take into account a possibly existing
`UniformSpace` instance on `E`). -/
@[to_additive "Construct a seminormed group from a seminorm, i.e., registering the pseudodistance
and the pseudometric space structure from the seminorm properties. Note that in most cases this
instance creates bad definitional equalities (e.g., it does not take into account a possibly
existing `UniformSpace` instance on `E`)."]
def GroupSeminorm.toSeminormedCommGroup [CommGroup E] (f : GroupSeminorm E) :
    SeminormedCommGroup E :=
  { f.toSeminormedGroup with
    mul_comm := mul_comm }

/-- Construct a normed group from a norm, i.e., registering the distance and the metric space
structure from the norm properties. Note that in most cases this instance creates bad definitional
equalities (e.g., it does not take into account a possibly existing `UniformSpace` instance on
`E`). -/
@[to_additive "Construct a normed group from a norm, i.e., registering the distance and the metric
space structure from the norm properties. Note that in most cases this instance creates bad
definitional equalities (e.g., it does not take into account a possibly existing `UniformSpace`
instance on `E`)."]
def GroupNorm.toNormedGroup [Group E] (f : GroupNorm E) : NormedGroup E :=
  { f.toGroupSeminorm.toSeminormedGroup with
    eq_of_dist_eq_zero := fun h => div_eq_one.1 <| eq_one_of_map_eq_zero f h }

/-- Construct a normed group from a norm, i.e., registering the distance and the metric space
structure from the norm properties. Note that in most cases this instance creates bad definitional
equalities (e.g., it does not take into account a possibly existing `UniformSpace` instance on
`E`). -/
@[to_additive "Construct a normed group from a norm, i.e., registering the distance and the metric
space structure from the norm properties. Note that in most cases this instance creates bad
definitional equalities (e.g., it does not take into account a possibly existing `UniformSpace`
instance on `E`)."]
def GroupNorm.toNormedCommGroup [CommGroup E] (f : GroupNorm E) : NormedCommGroup E :=
  { f.toNormedGroup with
    mul_comm := mul_comm }

instance PUnit.normedAddCommGroup : NormedAddCommGroup PUnit where
  norm := Function.const _ 0
  dist_eq _ _ := rfl

@[simp]
theorem PUnit.norm_eq_zero (r : PUnit) : ‚Äñr‚Äñ = 0 :=
  rfl

section SeminormedGroup

variable [SeminormedGroup E] [SeminormedGroup F] [SeminormedGroup G] {s : Set E}
  {a a‚ÇÅ a‚ÇÇ b b‚ÇÅ b‚ÇÇ : E} {r r‚ÇÅ r‚ÇÇ : ‚Ñù}

@[to_additive]
theorem dist_eq_norm_div (a b : E) : dist a b = ‚Äña / b‚Äñ :=
  SeminormedGroup.dist_eq _ _

@[to_additive]
theorem dist_eq_norm_div' (a b : E) : dist a b = ‚Äñb / a‚Äñ := by rw [dist_comm, dist_eq_norm_div]

alias dist_eq_norm := dist_eq_norm_sub

alias dist_eq_norm' := dist_eq_norm_sub'

@[to_additive]
instance NormedGroup.to_isometricSMul_right : IsometricSMul E·µê·µí·µñ E :=
  ‚ü®fun a => Isometry.of_dist_eq fun b c => by simp [dist_eq_norm_div]‚ü©

@[to_additive (attr := simp)]
theorem dist_one_right (a : E) : dist a 1 = ‚Äña‚Äñ := by rw [dist_eq_norm_div, div_one]

@[to_additive (attr := simp)]
theorem dist_one_left : dist (1 : E) = norm :=
  funext fun a => by rw [dist_comm, dist_one_right]

@[to_additive]
theorem Isometry.norm_map_of_map_one {f : E ‚Üí F} (hi : Isometry f) (h‚ÇÅ : f 1 = 1) (x : E) :
    ‚Äñf x‚Äñ = ‚Äñx‚Äñ := by rw [‚Üê dist_one_right, ‚Üê h‚ÇÅ, hi.dist_eq, dist_one_right]

@[to_additive (attr := simp) comap_norm_atTop]
theorem comap_norm_atTop' : comap norm atTop = cobounded E := by
  simpa only [dist_one_right] using comap_dist_right_atTop (1 : E)

@[to_additive (attr := simp) tendsto_norm_atTop_iff_cobounded]
theorem tendsto_norm_atTop_iff_cobounded' {f : Œ± ‚Üí E} {l : Filter Œ±} :
    Tendsto (‚Äñf ¬∑‚Äñ) l atTop ‚Üî Tendsto f l (cobounded E) := by
  rw [‚Üê comap_norm_atTop', tendsto_comap_iff]; rfl

@[to_additive tendsto_norm_cobounded_atTop]
theorem tendsto_norm_cobounded_atTop' : Tendsto norm (cobounded E) atTop :=
  tendsto_norm_atTop_iff_cobounded'.2 tendsto_id

@[to_additive tendsto_norm_cocompact_atTop]
theorem tendsto_norm_cocompact_atTop' [ProperSpace E] : Tendsto norm (cocompact E) atTop :=
  cobounded_eq_cocompact (Œ± := E) ‚ñ∏ tendsto_norm_cobounded_atTop'

@[to_additive]
theorem norm_div_rev (a b : E) : ‚Äña / b‚Äñ = ‚Äñb / a‚Äñ := by
  simpa only [dist_eq_norm_div] using dist_comm a b

@[to_additive (attr := simp) norm_neg]
theorem norm_inv' (a : E) : ‚Äña‚Åª¬π‚Äñ = ‚Äña‚Äñ := by simpa using norm_div_rev 1 a

@[to_additive]
theorem dist_mulIndicator (s t : Set Œ±) (f : Œ± ‚Üí E) (x : Œ±) :
    dist (s.mulIndicator f x) (t.mulIndicator f x) = ‚Äñ(s ‚àÜ t).mulIndicator f x‚Äñ := by
  rw [dist_eq_norm_div, Set.apply_mulIndicator_symmDiff norm_inv']

@[to_additive (attr := simp)]
theorem dist_mul_self_right (a b : E) : dist b (a * b) = ‚Äña‚Äñ := by
  rw [‚Üê dist_one_left, ‚Üê dist_mul_right 1 a b, one_mul]

@[to_additive (attr := simp)]
theorem dist_mul_self_left (a b : E) : dist (a * b) b = ‚Äña‚Äñ := by
  rw [dist_comm, dist_mul_self_right]

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_left (a b c : E) : dist (a / b) c = dist a (c * b) := by
  rw [‚Üê dist_mul_right _ _ b, div_mul_cancel']

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_right (a b c : E) : dist a (b / c) = dist (a * c) b := by
  rw [‚Üê dist_mul_right _ _ c, div_mul_cancel']

/-- In a (semi)normed group, inversion `x ‚Ü¶ x‚Åª¬π` tends to infinity at infinity. TODO: use
`Bornology.cobounded` instead of `Filter.comap Norm.norm Filter.atTop`. -/
@[to_additive "In a (semi)normed group, negation `x ‚Ü¶ -x` tends to infinity at infinity. TODO: use
`Bornology.cobounded` instead of `Filter.comap Norm.norm Filter.atTop`."]
theorem Filter.tendsto_inv_cobounded :
    Tendsto (Inv.inv : E ‚Üí E) (comap norm atTop) (comap norm atTop) := by
  simpa only [norm_inv', tendsto_comap_iff, (¬∑ ‚àò ¬∑)] using tendsto_comap

/-- **Triangle inequality** for the norm. -/
@[to_additive norm_add_le "**Triangle inequality** for the norm."]
theorem norm_mul_le' (a b : E) : ‚Äña * b‚Äñ ‚â§ ‚Äña‚Äñ + ‚Äñb‚Äñ := by
  simpa [dist_eq_norm_div] using dist_triangle a 1 b‚Åª¬π

@[to_additive]
theorem norm_mul_le_of_le (h‚ÇÅ : ‚Äña‚ÇÅ‚Äñ ‚â§ r‚ÇÅ) (h‚ÇÇ : ‚Äña‚ÇÇ‚Äñ ‚â§ r‚ÇÇ) : ‚Äña‚ÇÅ * a‚ÇÇ‚Äñ ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (norm_mul_le' a‚ÇÅ a‚ÇÇ).trans <| add_le_add h‚ÇÅ h‚ÇÇ

@[to_additive norm_add‚ÇÉ_le]
theorem norm_mul‚ÇÉ_le (a b c : E) : ‚Äña * b * c‚Äñ ‚â§ ‚Äña‚Äñ + ‚Äñb‚Äñ + ‚Äñc‚Äñ :=
  norm_mul_le_of_le (norm_mul_le' _ _) le_rfl

@[to_additive (attr := simp) norm_nonneg]
theorem norm_nonneg' (a : E) : 0 ‚â§ ‚Äña‚Äñ := by
  rw [‚Üê dist_one_right]
  exact dist_nonneg

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: multiplicative norms are nonnegative, via
`norm_nonneg'`. -/
@[positivity Norm.norm _]
def evalMulNorm : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app _ a ‚Üê whnfR e | throwError "not ‚Äñ ¬∑ ‚Äñ"
  let p ‚Üê mkAppM ``norm_nonneg' #[a]
  pure (.nonnegative p)

/-- Extension for the `positivity` tactic: additive norms are nonnegative, via `norm_nonneg`. -/
@[positivity Norm.norm _]
def evalAddNorm : PositivityExt where eval {_ _} _zŒ± _pŒ± e := do
  let .app _ a ‚Üê whnfR e | throwError "not ‚Äñ ¬∑ ‚Äñ"
  let p ‚Üê mkAppM ``norm_nonneg #[a]
  pure (.nonnegative p)

end Mathlib.Meta.Positivity

@[to_additive (attr := simp) norm_zero]
theorem norm_one' : ‚Äñ(1 : E)‚Äñ = 0 := by rw [‚Üê dist_one_right, dist_self]

@[to_additive]
theorem ne_one_of_norm_ne_zero : ‚Äña‚Äñ ‚â† 0 ‚Üí a ‚â† 1 :=
  mt <| by
    rintro rfl
    exact norm_one'

@[to_additive (attr := nontriviality) norm_of_subsingleton]
theorem norm_of_subsingleton' [Subsingleton E] (a : E) : ‚Äña‚Äñ = 0 := by
  rw [Subsingleton.elim a 1, norm_one']

@[to_additive zero_lt_one_add_norm_sq]
theorem zero_lt_one_add_norm_sq' (x : E) : 0 < 1 + ‚Äñx‚Äñ ^ 2 := by
  positivity

@[to_additive]
theorem norm_div_le (a b : E) : ‚Äña / b‚Äñ ‚â§ ‚Äña‚Äñ + ‚Äñb‚Äñ := by
  simpa [dist_eq_norm_div] using dist_triangle a 1 b

@[to_additive]
theorem norm_div_le_of_le {r‚ÇÅ r‚ÇÇ : ‚Ñù} (H‚ÇÅ : ‚Äña‚ÇÅ‚Äñ ‚â§ r‚ÇÅ) (H‚ÇÇ : ‚Äña‚ÇÇ‚Äñ ‚â§ r‚ÇÇ) : ‚Äña‚ÇÅ / a‚ÇÇ‚Äñ ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (norm_div_le a‚ÇÅ a‚ÇÇ).trans <| add_le_add H‚ÇÅ H‚ÇÇ

@[to_additive dist_le_norm_add_norm]
theorem dist_le_norm_add_norm' (a b : E) : dist a b ‚â§ ‚Äña‚Äñ + ‚Äñb‚Äñ := by
  rw [dist_eq_norm_div]
  apply norm_div_le

@[to_additive abs_norm_sub_norm_le]
theorem abs_norm_sub_norm_le' (a b : E) : |‚Äña‚Äñ - ‚Äñb‚Äñ| ‚â§ ‚Äña / b‚Äñ := by
  simpa [dist_eq_norm_div] using abs_dist_sub_le a b 1

@[to_additive norm_sub_norm_le]
theorem norm_sub_norm_le' (a b : E) : ‚Äña‚Äñ - ‚Äñb‚Äñ ‚â§ ‚Äña / b‚Äñ :=
  (le_abs_self _).trans (abs_norm_sub_norm_le' a b)

@[to_additive dist_norm_norm_le]
theorem dist_norm_norm_le' (a b : E) : dist ‚Äña‚Äñ ‚Äñb‚Äñ ‚â§ ‚Äña / b‚Äñ :=
  abs_norm_sub_norm_le' a b

@[to_additive]
theorem norm_le_norm_add_norm_div' (u v : E) : ‚Äñu‚Äñ ‚â§ ‚Äñv‚Äñ + ‚Äñu / v‚Äñ := by
  rw [add_comm]
  refine' (norm_mul_le' _ _).trans_eq' _
  rw [div_mul_cancel']

@[to_additive]
theorem norm_le_norm_add_norm_div (u v : E) : ‚Äñv‚Äñ ‚â§ ‚Äñu‚Äñ + ‚Äñu / v‚Äñ := by
  rw [norm_div_rev]
  exact norm_le_norm_add_norm_div' v u

alias norm_le_insert' := norm_le_norm_add_norm_sub'

alias norm_le_insert := norm_le_norm_add_norm_sub

@[to_additive]
theorem norm_le_mul_norm_add (u v : E) : ‚Äñu‚Äñ ‚â§ ‚Äñu * v‚Äñ + ‚Äñv‚Äñ :=
  calc
    ‚Äñu‚Äñ = ‚Äñu * v / v‚Äñ := by rw [mul_div_cancel'']
    _ ‚â§ ‚Äñu * v‚Äñ + ‚Äñv‚Äñ := norm_div_le _ _

@[to_additive ball_eq]
theorem ball_eq' (y : E) (Œµ : ‚Ñù) : ball y Œµ = { x | ‚Äñx / y‚Äñ < Œµ } :=
  Set.ext fun a => by simp [dist_eq_norm_div]

@[to_additive]
theorem ball_one_eq (r : ‚Ñù) : ball (1 : E) r = { x | ‚Äñx‚Äñ < r } :=
  Set.ext fun a => by simp

@[to_additive mem_ball_iff_norm]
theorem mem_ball_iff_norm'' : b ‚àà ball a r ‚Üî ‚Äñb / a‚Äñ < r := by rw [mem_ball, dist_eq_norm_div]

@[to_additive mem_ball_iff_norm']
theorem mem_ball_iff_norm''' : b ‚àà ball a r ‚Üî ‚Äña / b‚Äñ < r := by rw [mem_ball', dist_eq_norm_div]

@[to_additive] -- porting note: `simp` can prove it
theorem mem_ball_one_iff : a ‚àà ball (1 : E) r ‚Üî ‚Äña‚Äñ < r := by rw [mem_ball, dist_one_right]

@[to_additive mem_closedBall_iff_norm]
theorem mem_closedBall_iff_norm'' : b ‚àà closedBall a r ‚Üî ‚Äñb / a‚Äñ ‚â§ r := by
  rw [mem_closedBall, dist_eq_norm_div]

@[to_additive] -- porting note: `simp` can prove it
theorem mem_closedBall_one_iff : a ‚àà closedBall (1 : E) r ‚Üî ‚Äña‚Äñ ‚â§ r := by
  rw [mem_closedBall, dist_one_right]

@[to_additive mem_closedBall_iff_norm']
theorem mem_closedBall_iff_norm''' : b ‚àà closedBall a r ‚Üî ‚Äña / b‚Äñ ‚â§ r := by
  rw [mem_closedBall', dist_eq_norm_div]

@[to_additive norm_le_of_mem_closedBall]
theorem norm_le_of_mem_closedBall' (h : b ‚àà closedBall a r) : ‚Äñb‚Äñ ‚â§ ‚Äña‚Äñ + r :=
  (norm_le_norm_add_norm_div' _ _).trans <| add_le_add_left (by rwa [‚Üê dist_eq_norm_div]) _

@[to_additive norm_le_norm_add_const_of_dist_le]
theorem norm_le_norm_add_const_of_dist_le' : dist a b ‚â§ r ‚Üí ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ + r :=
  norm_le_of_mem_closedBall'

@[to_additive norm_lt_of_mem_ball]
theorem norm_lt_of_mem_ball' (h : b ‚àà ball a r) : ‚Äñb‚Äñ < ‚Äña‚Äñ + r :=
  (norm_le_norm_add_norm_div' _ _).trans_lt <| add_lt_add_left (by rwa [‚Üê dist_eq_norm_div]) _

@[to_additive]
theorem norm_div_sub_norm_div_le_norm_div (u v w : E) : ‚Äñu / w‚Äñ - ‚Äñv / w‚Äñ ‚â§ ‚Äñu / v‚Äñ := by
  simpa only [div_div_div_cancel_right'] using norm_sub_norm_le' (u / w) (v / w)

@[to_additive isBounded_iff_forall_norm_le]
theorem isBounded_iff_forall_norm_le' : Bornology.IsBounded s ‚Üî ‚àÉ C, ‚àÄ x ‚àà s, ‚Äñx‚Äñ ‚â§ C := by
  simpa only [Set.subset_def, mem_closedBall_one_iff] using isBounded_iff_subset_closedBall (1 : E)

alias ‚ü®Bornology.IsBounded.exists_norm_le', _‚ü© := isBounded_iff_forall_norm_le'

alias ‚ü®Bornology.IsBounded.exists_norm_le, _‚ü© := isBounded_iff_forall_norm_le

attribute [to_additive existing exists_norm_le] Bornology.IsBounded.exists_norm_le'

@[to_additive exists_pos_norm_le]
theorem Bornology.IsBounded.exists_pos_norm_le' (hs : IsBounded s) : ‚àÉ R > 0, ‚àÄ x ‚àà s, ‚Äñx‚Äñ ‚â§ R :=
  let ‚ü®R‚ÇÄ, hR‚ÇÄ‚ü© := hs.exists_norm_le'
  ‚ü®max R‚ÇÄ 1, by positivity, fun x hx => (hR‚ÇÄ x hx).trans <| le_max_left _ _‚ü©

@[to_additive (attr := simp 1001) mem_sphere_iff_norm]
-- porting note: increase priority so the left-hand side doesn't reduce
theorem mem_sphere_iff_norm' : b ‚àà sphere a r ‚Üî ‚Äñb / a‚Äñ = r := by simp [dist_eq_norm_div]

@[to_additive] -- `simp` can prove this
theorem mem_sphere_one_iff_norm : a ‚àà sphere (1 : E) r ‚Üî ‚Äña‚Äñ = r := by simp [dist_eq_norm_div]

@[to_additive (attr := simp) norm_eq_of_mem_sphere]
theorem norm_eq_of_mem_sphere' (x : sphere (1 : E) r) : ‚Äñ(x : E)‚Äñ = r :=
  mem_sphere_one_iff_norm.mp x.2

@[to_additive]
theorem ne_one_of_mem_sphere (hr : r ‚â† 0) (x : sphere (1 : E) r) : (x : E) ‚â† 1 :=
  ne_one_of_norm_ne_zero <| by rwa [norm_eq_of_mem_sphere' x]

@[to_additive ne_zero_of_mem_unit_sphere]
theorem ne_one_of_mem_unit_sphere (x : sphere (1 : E) 1) : (x : E) ‚â† 1 :=
  ne_one_of_mem_sphere one_ne_zero _

variable (E)

/-- The norm of a seminormed group as a group seminorm. -/
@[to_additive "The norm of a seminormed group as an additive group seminorm."]
def normGroupSeminorm : GroupSeminorm E :=
  ‚ü®norm, norm_one', norm_mul_le', norm_inv'‚ü©

@[to_additive (attr := simp)]
theorem coe_normGroupSeminorm : ‚áë(normGroupSeminorm E) = norm :=
  rfl

variable {E}

@[to_additive]
theorem NormedCommGroup.tendsto_nhds_one {f : Œ± ‚Üí E} {l : Filter Œ±} :
    Tendsto f l (ùìù 1) ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† x in l, ‚Äñf x‚Äñ < Œµ :=
  Metric.tendsto_nhds.trans <| by simp only [dist_one_right]

@[to_additive]
theorem NormedCommGroup.tendsto_nhds_nhds {f : E ‚Üí F} {x : E} {y : F} :
    Tendsto f (ùìù x) (ùìù y) ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x', ‚Äñx' / x‚Äñ < Œ¥ ‚Üí ‚Äñf x' / y‚Äñ < Œµ := by
  simp_rw [Metric.tendsto_nhds_nhds, dist_eq_norm_div]

@[to_additive]
theorem NormedCommGroup.cauchySeq_iff [Nonempty Œ±] [SemilatticeSup Œ±] {u : Œ± ‚Üí E} :
    CauchySeq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ m, N ‚â§ m ‚Üí ‚àÄ n, N ‚â§ n ‚Üí ‚Äñu m / u n‚Äñ < Œµ := by
  simp [Metric.cauchySeq_iff, dist_eq_norm_div]

@[to_additive]
theorem NormedCommGroup.nhds_basis_norm_lt (x : E) :
    (ùìù x).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => { y | ‚Äñy / x‚Äñ < Œµ } := by
  simp_rw [‚Üê ball_eq']
  exact Metric.nhds_basis_ball

@[to_additive]
theorem NormedCommGroup.nhds_one_basis_norm_lt :
    (ùìù (1 : E)).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => { y | ‚Äñy‚Äñ < Œµ } := by
  convert NormedCommGroup.nhds_basis_norm_lt (1 : E)
  simp

@[to_additive]
theorem NormedCommGroup.uniformity_basis_dist :
    (ùì§ E).HasBasis (fun Œµ : ‚Ñù => 0 < Œµ) fun Œµ => { p : E √ó E | ‚Äñp.fst / p.snd‚Äñ < Œµ } := by
  convert Metric.uniformity_basis_dist (Œ± := E) using 1
  simp [dist_eq_norm_div]

open Finset

/-- A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant `C` such that
for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`. -/
@[to_additive "A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant
`C` such that for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`."]
theorem MonoidHomClass.lipschitz_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : LipschitzWith (Real.toNNReal C) f :=
  LipschitzWith.of_dist_le' fun x y => by simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive]
theorem lipschitzOnWith_iff_norm_div_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} :
    LipschitzOnWith C f s ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí ‚Äñf x / f y‚Äñ ‚â§ C * ‚Äñx / y‚Äñ := by
  simp only [lipschitzOnWith_iff_dist_le_mul, dist_eq_norm_div]

alias ‚ü®LipschitzOnWith.norm_div_le, _‚ü© := lipschitzOnWith_iff_norm_div_le

attribute [to_additive] LipschitzOnWith.norm_div_le

@[to_additive]
theorem LipschitzOnWith.norm_div_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} (h : LipschitzOnWith C f s)
    (ha : a ‚àà s) (hb : b ‚àà s) (hr : ‚Äña / b‚Äñ ‚â§ r) : ‚Äñf a / f b‚Äñ ‚â§ C * r :=
  (h.norm_div_le ha hb).trans <| by gcongr

@[to_additive]
theorem lipschitzWith_iff_norm_div_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} :
    LipschitzWith C f ‚Üî ‚àÄ x y, ‚Äñf x / f y‚Äñ ‚â§ C * ‚Äñx / y‚Äñ := by
  simp only [lipschitzWith_iff_dist_le_mul, dist_eq_norm_div]

alias ‚ü®LipschitzWith.norm_div_le, _‚ü© := lipschitzWith_iff_norm_div_le

attribute [to_additive] LipschitzWith.norm_div_le

@[to_additive]
theorem LipschitzWith.norm_div_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} (h : LipschitzWith C f)
    (hr : ‚Äña / b‚Äñ ‚â§ r) : ‚Äñf a / f b‚Äñ ‚â§ C * r :=
  (h.norm_div_le _ _).trans <| by gcongr

/-- A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C` such that
for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. -/
@[to_additive "A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C`
such that for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`"]
theorem MonoidHomClass.continuous_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : Continuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).continuous

@[to_additive]
theorem MonoidHomClass.uniformContinuous_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : UniformContinuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).uniformContinuous

@[to_additive IsCompact.exists_bound_of_continuousOn]
theorem IsCompact.exists_bound_of_continuousOn' [TopologicalSpace Œ±] {s : Set Œ±} (hs : IsCompact s)
    {f : Œ± ‚Üí E} (hf : ContinuousOn f s) : ‚àÉ C, ‚àÄ x ‚àà s, ‚Äñf x‚Äñ ‚â§ C :=
  (isBounded_iff_forall_norm_le'.1 (hs.image_of_continuousOn hf).isBounded).imp fun _C hC _x hx =>
    hC _ <| Set.mem_image_of_mem _ hx

@[to_additive]
theorem HasCompactMulSupport.exists_bound_of_continuous [TopologicalSpace Œ±]
    {f : Œ± ‚Üí E} (hf : HasCompactMulSupport f) (h'f : Continuous f) : ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C := by
  simpa using (hf.isCompact_range h'f).isBounded.exists_norm_le'

@[to_additive]
theorem MonoidHomClass.isometry_iff_norm [MonoidHomClass ùìï E F] (f : ùìï) :
    Isometry f ‚Üî ‚àÄ x, ‚Äñf x‚Äñ = ‚Äñx‚Äñ := by
  simp only [isometry_iff_dist_eq, dist_eq_norm_div, ‚Üê map_div]
  refine' ‚ü®fun h x => _, fun h x y => h _‚ü©
  simpa using h x 1

alias ‚ü®_, MonoidHomClass.isometry_of_norm‚ü© := MonoidHomClass.isometry_iff_norm

attribute [to_additive] MonoidHomClass.isometry_of_norm

section NNNorm

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedGroup.toNNNorm : NNNorm E :=
  ‚ü®fun a => ‚ü®‚Äña‚Äñ, norm_nonneg' a‚ü©‚ü©

@[to_additive (attr := simp, norm_cast) coe_nnnorm]
theorem coe_nnnorm' (a : E) : (‚Äña‚Äñ‚Çä : ‚Ñù) = ‚Äña‚Äñ :=
  rfl

@[to_additive (attr := simp) coe_comp_nnnorm]
theorem coe_comp_nnnorm' : (toReal : ‚Ñù‚â•0 ‚Üí ‚Ñù) ‚àò (nnnorm : E ‚Üí ‚Ñù‚â•0) = norm :=
  rfl

@[to_additive norm_toNNReal]
theorem norm_toNNReal' : ‚Äña‚Äñ.toNNReal = ‚Äña‚Äñ‚Çä :=
  @Real.toNNReal_coe ‚Äña‚Äñ‚Çä

@[to_additive]
theorem nndist_eq_nnnorm_div (a b : E) : nndist a b = ‚Äña / b‚Äñ‚Çä :=
  NNReal.eq <| dist_eq_norm_div _ _

alias nndist_eq_nnnorm := nndist_eq_nnnorm_sub

@[to_additive (attr := simp) nnnorm_zero]
theorem nnnorm_one' : ‚Äñ(1 : E)‚Äñ‚Çä = 0 :=
  NNReal.eq norm_one'

@[to_additive]
theorem ne_one_of_nnnorm_ne_zero {a : E} : ‚Äña‚Äñ‚Çä ‚â† 0 ‚Üí a ‚â† 1 :=
  mt <| by
    rintro rfl
    exact nnnorm_one'

@[to_additive nnnorm_add_le]
theorem nnnorm_mul_le' (a b : E) : ‚Äña * b‚Äñ‚Çä ‚â§ ‚Äña‚Äñ‚Çä + ‚Äñb‚Äñ‚Çä :=
  NNReal.coe_le_coe.1 <| norm_mul_le' a b

@[to_additive (attr := simp) nnnorm_neg]
theorem nnnorm_inv' (a : E) : ‚Äña‚Åª¬π‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  NNReal.eq <| norm_inv' a

@[to_additive]
theorem nndist_mulIndicator (s t : Set Œ±) (f : Œ± ‚Üí E) (x : Œ±) :
    nndist (s.mulIndicator f x) (t.mulIndicator f x) = ‚Äñ(s ‚àÜ t).mulIndicator f x‚Äñ‚Çä :=
  NNReal.eq <| dist_mulIndicator s t f x

@[to_additive]
theorem nnnorm_div_le (a b : E) : ‚Äña / b‚Äñ‚Çä ‚â§ ‚Äña‚Äñ‚Çä + ‚Äñb‚Äñ‚Çä :=
  NNReal.coe_le_coe.1 <| norm_div_le _ _

@[to_additive nndist_nnnorm_nnnorm_le]
theorem nndist_nnnorm_nnnorm_le' (a b : E) : nndist ‚Äña‚Äñ‚Çä ‚Äñb‚Äñ‚Çä ‚â§ ‚Äña / b‚Äñ‚Çä :=
  NNReal.coe_le_coe.1 <| dist_norm_norm_le' a b

@[to_additive]
theorem nnnorm_le_nnnorm_add_nnnorm_div (a b : E) : ‚Äñb‚Äñ‚Çä ‚â§ ‚Äña‚Äñ‚Çä + ‚Äña / b‚Äñ‚Çä :=
  norm_le_norm_add_norm_div _ _

@[to_additive]
theorem nnnorm_le_nnnorm_add_nnnorm_div' (a b : E) : ‚Äña‚Äñ‚Çä ‚â§ ‚Äñb‚Äñ‚Çä + ‚Äña / b‚Äñ‚Çä :=
  norm_le_norm_add_norm_div' _ _

alias nnnorm_le_insert' := nnnorm_le_nnnorm_add_nnnorm_sub'

alias nnnorm_le_insert := nnnorm_le_nnnorm_add_nnnorm_sub

@[to_additive]
theorem nnnorm_le_mul_nnnorm_add (a b : E) : ‚Äña‚Äñ‚Çä ‚â§ ‚Äña * b‚Äñ‚Çä + ‚Äñb‚Äñ‚Çä :=
  norm_le_mul_norm_add _ _

@[to_additive ofReal_norm_eq_coe_nnnorm]
theorem ofReal_norm_eq_coe_nnnorm' (a : E) : ENNReal.ofReal ‚Äña‚Äñ = ‚Äña‚Äñ‚Çä :=
  ENNReal.ofReal_eq_coe_nnreal _

@[to_additive]
theorem edist_eq_coe_nnnorm_div (a b : E) : edist a b = ‚Äña / b‚Äñ‚Çä := by
  rw [edist_dist, dist_eq_norm_div, ofReal_norm_eq_coe_nnnorm']

@[to_additive edist_eq_coe_nnnorm]
theorem edist_eq_coe_nnnorm' (x : E) : edist x 1 = (‚Äñx‚Äñ‚Çä : ‚Ñù‚â•0‚àû) := by
  rw [edist_eq_coe_nnnorm_div, div_one]

@[to_additive]
theorem edist_mulIndicator (s t : Set Œ±) (f : Œ± ‚Üí E) (x : Œ±) :
    edist (s.mulIndicator f x) (t.mulIndicator f x) = ‚Äñ(s ‚àÜ t).mulIndicator f x‚Äñ‚Çä := by
  rw [edist_nndist, nndist_mulIndicator]

@[to_additive]
theorem mem_emetric_ball_one_iff {r : ‚Ñù‚â•0‚àû} : a ‚àà EMetric.ball (1 : E) r ‚Üî ‚Üë‚Äña‚Äñ‚Çä < r := by
  rw [EMetric.mem_ball, edist_eq_coe_nnnorm']

@[to_additive]
theorem MonoidHomClass.lipschitz_of_bound_nnnorm [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù‚â•0)
    (h : ‚àÄ x, ‚Äñf x‚Äñ‚Çä ‚â§ C * ‚Äñx‚Äñ‚Çä) : LipschitzWith C f :=
  @Real.toNNReal_coe C ‚ñ∏ MonoidHomClass.lipschitz_of_bound f C h

@[to_additive]
theorem MonoidHomClass.antilipschitz_of_bound [MonoidHomClass ùìï E F] (f : ùìï) {K : ‚Ñù‚â•0}
    (h : ‚àÄ x, ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ) : AntilipschitzWith K f :=
  AntilipschitzWith.of_le_mul_dist fun x y => by
    simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive LipschitzWith.norm_le_mul]
theorem LipschitzWith.norm_le_mul' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : ‚Äñf x‚Äñ ‚â§ K * ‚Äñx‚Äñ := by simpa only [dist_one_right, hf] using h.dist_le_mul x 1

@[to_additive LipschitzWith.nnorm_le_mul]
theorem LipschitzWith.nnorm_le_mul' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : ‚Äñf x‚Äñ‚Çä ‚â§ K * ‚Äñx‚Äñ‚Çä :=
  h.norm_le_mul' hf x

@[to_additive AntilipschitzWith.le_mul_norm]
theorem AntilipschitzWith.le_mul_norm' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ := by
  simpa only [dist_one_right, hf] using h.le_mul_dist x 1

@[to_additive AntilipschitzWith.le_mul_nnnorm]
theorem AntilipschitzWith.le_mul_nnnorm' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : ‚Äñx‚Äñ‚Çä ‚â§ K * ‚Äñf x‚Äñ‚Çä :=
  h.le_mul_norm' hf x

@[to_additive]
theorem OneHomClass.bound_of_antilipschitz [OneHomClass ùìï E F] (f : ùìï) {K : ‚Ñù‚â•0}
    (h : AntilipschitzWith K f) (x) : ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ :=
  h.le_mul_nnnorm' (map_one f) x

end NNNorm

@[to_additive]
theorem tendsto_iff_norm_div_tendsto_zero {f : Œ± ‚Üí E} {a : Filter Œ±} {b : E} :
    Tendsto f a (ùìù b) ‚Üî Tendsto (fun e => ‚Äñf e / b‚Äñ) a (ùìù 0) := by
  simp only [‚Üê dist_eq_norm_div, ‚Üê tendsto_iff_dist_tendsto_zero]

@[to_additive]
theorem tendsto_one_iff_norm_tendsto_zero {f : Œ± ‚Üí E} {a : Filter Œ±} :
    Tendsto f a (ùìù 1) ‚Üî Tendsto (‚Äñf ¬∑‚Äñ) a (ùìù 0) :=
  tendsto_iff_norm_div_tendsto_zero.trans <| by simp only [div_one]

@[to_additive]
theorem comap_norm_nhds_one : comap norm (ùìù 0) = ùìù (1 : E) := by
  simpa only [dist_one_right] using nhds_comap_dist (1 : E)

/-- Special case of the sandwich theorem: if the norm of `f` is eventually bounded by a real
function `a` which tends to `0`, then `f` tends to `1`. In this pair of lemmas (`squeeze_one_norm'`
and `squeeze_one_norm`), following a convention of similar lemmas in `Topology.MetricSpace.Basic`
and `Topology.Algebra.Order`, the `'` version is phrased using "eventually" and the non-`'` version
is phrased absolutely. -/
@[to_additive "Special case of the sandwich theorem: if the norm of `f` is eventually bounded by a
real function `a` which tends to `0`, then `f` tends to `1`. In this pair of lemmas
(`squeeze_zero_norm'` and `squeeze_zero_norm`), following a convention of similar lemmas in
`Topology.MetricSpace.PseudoMetric` and `Topology.Algebra.Order`, the `'` version is phrased using
\"eventually\" and the non-`'` version is phrased absolutely."]
theorem squeeze_one_norm' {f : Œ± ‚Üí E} {a : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : Filter Œ±} (h : ‚àÄ·∂† n in t‚ÇÄ, ‚Äñf n‚Äñ ‚â§ a n)
    (h' : Tendsto a t‚ÇÄ (ùìù 0)) : Tendsto f t‚ÇÄ (ùìù 1) :=
  tendsto_one_iff_norm_tendsto_zero.2 <|
    squeeze_zero' (eventually_of_forall fun _n => norm_nonneg' _) h h'

/-- Special case of the sandwich theorem: if the norm of `f` is bounded by a real function `a` which
tends to `0`, then `f` tends to `1`. -/
@[to_additive "Special case of the sandwich theorem: if the norm of `f` is bounded by a real
function `a` which tends to `0`, then `f` tends to `0`."]
theorem squeeze_one_norm {f : Œ± ‚Üí E} {a : Œ± ‚Üí ‚Ñù} {t‚ÇÄ : Filter Œ±} (h : ‚àÄ n, ‚Äñf n‚Äñ ‚â§ a n) :
    Tendsto a t‚ÇÄ (ùìù 0) ‚Üí Tendsto f t‚ÇÄ (ùìù 1) :=
  squeeze_one_norm' <| eventually_of_forall h

@[to_additive]
theorem tendsto_norm_div_self (x : E) : Tendsto (fun a => ‚Äña / x‚Äñ) (ùìù x) (ùìù 0) := by
  simpa [dist_eq_norm_div] using
    tendsto_id.dist (tendsto_const_nhds : Tendsto (fun _a => (x : E)) (ùìù x) _)

@[to_additive tendsto_norm]
theorem tendsto_norm' {x : E} : Tendsto (fun a => ‚Äña‚Äñ) (ùìù x) (ùìù ‚Äñx‚Äñ) := by
  simpa using tendsto_id.dist (tendsto_const_nhds : Tendsto (fun _a => (1 : E)) _ _)

@[to_additive]
theorem tendsto_norm_one : Tendsto (fun a : E => ‚Äña‚Äñ) (ùìù 1) (ùìù 0) := by
  simpa using tendsto_norm_div_self (1 : E)

@[to_additive (attr := continuity) continuous_norm]
theorem continuous_norm' : Continuous fun a : E => ‚Äña‚Äñ := by
  simpa using continuous_id.dist (continuous_const : Continuous fun _a => (1 : E))

@[to_additive (attr := continuity) continuous_nnnorm]
theorem continuous_nnnorm' : Continuous fun a : E => ‚Äña‚Äñ‚Çä :=
  continuous_norm'.subtype_mk _

@[to_additive lipschitzWith_one_norm]
theorem lipschitzWith_one_norm' : LipschitzWith 1 (norm : E ‚Üí ‚Ñù) := by
  simpa only [dist_one_left] using LipschitzWith.dist_right (1 : E)

@[to_additive lipschitzWith_one_nnnorm]
theorem lipschitzWith_one_nnnorm' : LipschitzWith 1 (NNNorm.nnnorm : E ‚Üí ‚Ñù‚â•0) :=
  lipschitzWith_one_norm'

@[to_additive uniformContinuous_norm]
theorem uniformContinuous_norm' : UniformContinuous (norm : E ‚Üí ‚Ñù) :=
  lipschitzWith_one_norm'.uniformContinuous

@[to_additive uniformContinuous_nnnorm]
theorem uniformContinuous_nnnorm' : UniformContinuous fun a : E => ‚Äña‚Äñ‚Çä :=
  uniformContinuous_norm'.subtype_mk _

@[to_additive]
theorem mem_closure_one_iff_norm {x : E} : x ‚àà closure ({1} : Set E) ‚Üî ‚Äñx‚Äñ = 0 := by
  rw [‚Üê closedBall_zero', mem_closedBall_one_iff, (norm_nonneg' x).le_iff_eq]

@[to_additive]
theorem closure_one_eq : closure ({1} : Set E) = { x | ‚Äñx‚Äñ = 0 } :=
  Set.ext fun _x => mem_closure_one_iff_norm

/-- A helper lemma used to prove that the (scalar or usual) product of a function that tends to one
and a bounded function tends to one. This lemma is formulated for any binary operation
`op : E ‚Üí F ‚Üí G` with an estimate `‚Äñop x y‚Äñ ‚â§ A * ‚Äñx‚Äñ * ‚Äñy‚Äñ` for some constant A instead of
multiplication so that it can be applied to `(*)`, `flip (*)`, `(‚Ä¢)`, and `flip (‚Ä¢)`. -/
@[to_additive "A helper lemma used to prove that the (scalar or usual) product of a function that
tends to zero and a bounded function tends to zero. This lemma is formulated for any binary
operation `op : E ‚Üí F ‚Üí G` with an estimate `‚Äñop x y‚Äñ ‚â§ A * ‚Äñx‚Äñ * ‚Äñy‚Äñ` for some constant A instead
of multiplication so that it can be applied to `(*)`, `flip (*)`, `(‚Ä¢)`, and `flip (‚Ä¢)`."]
theorem Filter.Tendsto.op_one_isBoundedUnder_le' {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} {l : Filter Œ±}
    (hf : Tendsto f l (ùìù 1)) (hg : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò g)) (op : E ‚Üí F ‚Üí G)
    (h_op : ‚àÉ A, ‚àÄ x y, ‚Äñop x y‚Äñ ‚â§ A * ‚Äñx‚Äñ * ‚Äñy‚Äñ) : Tendsto (fun x => op (f x) (g x)) l (ùìù 1) := by
  cases' h_op with A h_op
  rcases hg with ‚ü®C, hC‚ü©; rw [eventually_map] at hC
  rw [NormedCommGroup.tendsto_nhds_one] at hf ‚ä¢
  intro Œµ Œµ‚ÇÄ
  rcases exists_pos_mul_lt Œµ‚ÇÄ (A * C) with ‚ü®Œ¥, Œ¥‚ÇÄ, hŒ¥‚ü©
  filter_upwards [hf Œ¥ Œ¥‚ÇÄ, hC] with i hf hg
  refine' (h_op _ _).trans_lt _
  cases' le_total A 0 with hA hA
  ¬∑ exact (mul_nonpos_of_nonpos_of_nonneg (mul_nonpos_of_nonpos_of_nonneg hA <| norm_nonneg' _) <|
      norm_nonneg' _).trans_lt Œµ‚ÇÄ
  calc
    A * ‚Äñf i‚Äñ * ‚Äñg i‚Äñ ‚â§ A * Œ¥ * C := by gcongr; exact hg
    _ = A * C * Œ¥ := (mul_right_comm _ _ _)
    _ < Œµ := hŒ¥

/-- A helper lemma used to prove that the (scalar or usual) product of a function that tends to one
and a bounded function tends to one. This lemma is formulated for any binary operation
`op : E ‚Üí F ‚Üí G` with an estimate `‚Äñop x y‚Äñ ‚â§ ‚Äñx‚Äñ * ‚Äñy‚Äñ` instead of multiplication so that it
can be applied to `(*)`, `flip (*)`, `(‚Ä¢)`, and `flip (‚Ä¢)`. -/
@[to_additive "A helper lemma used to prove that the (scalar or usual) product of a function that
tends to zero and a bounded function tends to zero. This lemma is formulated for any binary
operation `op : E ‚Üí F ‚Üí G` with an estimate `‚Äñop x y‚Äñ ‚â§ ‚Äñx‚Äñ * ‚Äñy‚Äñ` instead of multiplication so
that it can be applied to `(*)`, `flip (*)`, `(‚Ä¢)`, and `flip (‚Ä¢)`."]
theorem Filter.Tendsto.op_one_isBoundedUnder_le {f : Œ± ‚Üí E} {g : Œ± ‚Üí F} {l : Filter Œ±}
    (hf : Tendsto f l (ùìù 1)) (hg : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò g)) (op : E ‚Üí F ‚Üí G)
    (h_op : ‚àÄ x y, ‚Äñop x y‚Äñ ‚â§ ‚Äñx‚Äñ * ‚Äñy‚Äñ) : Tendsto (fun x => op (f x) (g x)) l (ùìù 1) :=
  hf.op_one_isBoundedUnder_le' hg op ‚ü®1, fun x y => (one_mul ‚Äñx‚Äñ).symm ‚ñ∏ h_op x y‚ü©

section

variable {l : Filter Œ±} {f : Œ± ‚Üí E}

@[to_additive Filter.Tendsto.norm]
theorem Filter.Tendsto.norm' (h : Tendsto f l (ùìù a)) : Tendsto (fun x => ‚Äñf x‚Äñ) l (ùìù ‚Äña‚Äñ) :=
  tendsto_norm'.comp h

@[to_additive Filter.Tendsto.nnnorm]
theorem Filter.Tendsto.nnnorm' (h : Tendsto f l (ùìù a)) : Tendsto (fun x => ‚Äñf x‚Äñ‚Çä) l (ùìù ‚Äña‚Äñ‚Çä) :=
  Tendsto.comp continuous_nnnorm'.continuousAt h

end

section

variable [TopologicalSpace Œ±] {f : Œ± ‚Üí E}

@[to_additive Continuous.norm]
theorem Continuous.norm' : Continuous f ‚Üí Continuous fun x => ‚Äñf x‚Äñ :=
  continuous_norm'.comp

@[to_additive Continuous.nnnorm]
theorem Continuous.nnnorm' : Continuous f ‚Üí Continuous fun x => ‚Äñf x‚Äñ‚Çä :=
  continuous_nnnorm'.comp

@[to_additive ContinuousAt.norm]
theorem ContinuousAt.norm' {a : Œ±} (h : ContinuousAt f a) : ContinuousAt (fun x => ‚Äñf x‚Äñ) a :=
  Tendsto.norm' h

@[to_additive ContinuousAt.nnnorm]
theorem ContinuousAt.nnnorm' {a : Œ±} (h : ContinuousAt f a) : ContinuousAt (fun x => ‚Äñf x‚Äñ‚Çä) a :=
  Tendsto.nnnorm' h

@[to_additive ContinuousWithinAt.norm]
theorem ContinuousWithinAt.norm' {s : Set Œ±} {a : Œ±} (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x => ‚Äñf x‚Äñ) s a :=
  Tendsto.norm' h

@[to_additive ContinuousWithinAt.nnnorm]
theorem ContinuousWithinAt.nnnorm' {s : Set Œ±} {a : Œ±} (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x => ‚Äñf x‚Äñ‚Çä) s a :=
  Tendsto.nnnorm' h

@[to_additive ContinuousOn.norm]
theorem ContinuousOn.norm' {s : Set Œ±} (h : ContinuousOn f s) : ContinuousOn (fun x => ‚Äñf x‚Äñ) s :=
  fun x hx => (h x hx).norm'

@[to_additive ContinuousOn.nnnorm]
theorem ContinuousOn.nnnorm' {s : Set Œ±} (h : ContinuousOn f s) :
    ContinuousOn (fun x => ‚Äñf x‚Äñ‚Çä) s := fun x hx => (h x hx).nnnorm'

end

/-- If `‚Äñy‚Äñ ‚Üí ‚àû`, then we can assume `y ‚â† x` for any fixed `x`. -/
@[to_additive eventually_ne_of_tendsto_norm_atTop "If `‚Äñy‚Äñ‚Üí‚àû`, then we can assume `y‚â†x` for any
fixed `x`"]
theorem eventually_ne_of_tendsto_norm_atTop' {l : Filter Œ±} {f : Œ± ‚Üí E}
    (h : Tendsto (fun y => ‚Äñf y‚Äñ) l atTop) (x : E) : ‚àÄ·∂† y in l, f y ‚â† x :=
  (h.eventually_ne_atTop _).mono fun _x => ne_of_apply_ne norm

@[to_additive]
theorem SeminormedCommGroup.mem_closure_iff : a ‚àà closure s ‚Üî ‚àÄ Œµ, 0 < Œµ ‚Üí ‚àÉ b ‚àà s, ‚Äña / b‚Äñ < Œµ :=
  by simp [Metric.mem_closure_iff, dist_eq_norm_div]

@[to_additive norm_le_zero_iff']
theorem norm_le_zero_iff''' [T0Space E] {a : E} : ‚Äña‚Äñ ‚â§ 0 ‚Üî a = 1 := by
  letI : NormedGroup E :=
    { ‚ÄπSeminormedGroup E‚Ä∫ with toMetricSpace := MetricSpace.ofT0PseudoMetricSpace E }
  rw [‚Üê dist_one_right, dist_le_zero]

@[to_additive norm_eq_zero']
theorem norm_eq_zero''' [T0Space E] {a : E} : ‚Äña‚Äñ = 0 ‚Üî a = 1 :=
  (norm_nonneg' a).le_iff_eq.symm.trans norm_le_zero_iff'''

@[to_additive norm_pos_iff']
theorem norm_pos_iff''' [T0Space E] {a : E} : 0 < ‚Äña‚Äñ ‚Üî a ‚â† 1 := by
  rw [‚Üê not_le, norm_le_zero_iff''']

@[to_additive]
theorem SeminormedGroup.tendstoUniformlyOn_one {f : Œπ ‚Üí Œ∫ ‚Üí G} {s : Set Œ∫} {l : Filter Œπ} :
    TendstoUniformlyOn f 1 l s ‚Üî ‚àÄ Œµ > 0, ‚àÄ·∂† i in l, ‚àÄ x ‚àà s, ‚Äñf i x‚Äñ < Œµ := by
  simp_rw [tendstoUniformlyOn_iff, Pi.one_apply, dist_one_left]

@[to_additive]
theorem SeminormedGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_one {f : Œπ ‚Üí Œ∫ ‚Üí G}
    {l : Filter Œπ} {l' : Filter Œ∫} :
    UniformCauchySeqOnFilter f l l' ‚Üî
      TendstoUniformlyOnFilter (fun n : Œπ √ó Œπ => fun z => f n.fst z / f n.snd z) 1 (l √óÀ¢ l) l' := by
  refine' ‚ü®fun hf u hu => _, fun hf u hu => _‚ü©
  ¬∑ obtain ‚ü®Œµ, hŒµ, H‚ü© := uniformity_basis_dist.mem_uniformity_iff.mp hu
    refine'
      (hf { p : G √ó G | dist p.fst p.snd < Œµ } <| dist_mem_uniformity hŒµ).mono fun x hx =>
        H 1 (f x.fst.fst x.snd / f x.fst.snd x.snd) _
    simpa [dist_eq_norm_div, norm_div_rev] using hx
  ¬∑ obtain ‚ü®Œµ, hŒµ, H‚ü© := uniformity_basis_dist.mem_uniformity_iff.mp hu
    refine'
      (hf { p : G √ó G | dist p.fst p.snd < Œµ } <| dist_mem_uniformity hŒµ).mono fun x hx =>
        H (f x.fst.fst x.snd) (f x.fst.snd x.snd) _
    simpa [dist_eq_norm_div, norm_div_rev] using hx

@[to_additive]
theorem SeminormedGroup.uniformCauchySeqOn_iff_tendstoUniformlyOn_one {f : Œπ ‚Üí Œ∫ ‚Üí G} {s : Set Œ∫}
    {l : Filter Œπ} :
    UniformCauchySeqOn f l s ‚Üî
      TendstoUniformlyOn (fun n : Œπ √ó Œπ => fun z => f n.fst z / f n.snd z) 1 (l √óÀ¢ l) s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter,
    uniformCauchySeqOn_iff_uniformCauchySeqOnFilter,
    SeminormedGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_one]

end SeminormedGroup

section Induced

variable (E F)

-- See note [reducible non-instances]
/-- A group homomorphism from a `Group` to a `SeminormedGroup` induces a `SeminormedGroup`
structure on the domain. -/
@[to_additive (attr := reducible) "A group homomorphism from an `AddGroup` to a
`SeminormedAddGroup` induces a `SeminormedAddGroup` structure on the domain."]
def SeminormedGroup.induced [Group E] [SeminormedGroup F] [MonoidHomClass ùìï E F] (f : ùìï) :
    SeminormedGroup E :=
  { PseudoMetricSpace.induced f toPseudoMetricSpace with
    -- porting note: needed to add the instance explicitly, and `‚ÄπPseudoMetricSpace F‚Ä∫` failed
    norm := fun x => ‚Äñf x‚Äñ
    dist_eq := fun x y => by simp only [map_div, ‚Üê dist_eq_norm_div]; rfl }

-- See note [reducible non-instances]
/-- A group homomorphism from a `CommGroup` to a `SeminormedGroup` induces a
`SeminormedCommGroup` structure on the domain. -/
@[to_additive (attr := reducible) "A group homomorphism from an `AddCommGroup` to a
`SeminormedAddGroup` induces a `SeminormedAddCommGroup` structure on the domain."]
def SeminormedCommGroup.induced [CommGroup E] [SeminormedGroup F] [MonoidHomClass ùìï E F] (f : ùìï) :
    SeminormedCommGroup E :=
  { SeminormedGroup.induced E F f with
    mul_comm := mul_comm }

-- See note [reducible non-instances].
/-- An injective group homomorphism from a `Group` to a `NormedGroup` induces a `NormedGroup`
structure on the domain. -/
@[to_additive (attr := reducible) "An injective group homomorphism from an `AddGroup` to a
`NormedAddGroup` induces a `NormedAddGroup` structure on the domain."]
def NormedGroup.induced [Group E] [NormedGroup F] [MonoidHomClass ùìï E F] (f : ùìï) (h : Injective f) :
    NormedGroup E :=
  { SeminormedGroup.induced E F f, MetricSpace.induced f h _ with }

-- See note [reducible non-instances].
/-- An injective group homomorphism from a `CommGroup` to a `NormedGroup` induces a
`NormedCommGroup` structure on the domain. -/
@[to_additive (attr := reducible) "An injective group homomorphism from a `CommGroup` to a
`NormedCommGroup` induces a `NormedCommGroup` structure on the domain."]
def NormedCommGroup.induced [CommGroup E] [NormedGroup F] [MonoidHomClass ùìï E F] (f : ùìï)
    (h : Injective f) : NormedCommGroup E :=
  { SeminormedGroup.induced E F f, MetricSpace.induced f h _ with
    mul_comm := mul_comm }

end Induced

section SeminormedCommGroup

variable [SeminormedCommGroup E] [SeminormedCommGroup F] {a a‚ÇÅ a‚ÇÇ b b‚ÇÅ b‚ÇÇ : E} {r r‚ÇÅ r‚ÇÇ : ‚Ñù}

@[to_additive]
instance NormedGroup.to_isometricSMul_left : IsometricSMul E E :=
  ‚ü®fun a => Isometry.of_dist_eq fun b c => by simp [dist_eq_norm_div]‚ü©

@[to_additive]
theorem dist_inv (x y : E) : dist x‚Åª¬π y = dist x y‚Åª¬π := by
  simp_rw [dist_eq_norm_div, ‚Üê norm_inv' (x‚Åª¬π / y), inv_div, div_inv_eq_mul, mul_comm]

@[to_additive (attr := simp)]
theorem dist_self_mul_right (a b : E) : dist a (a * b) = ‚Äñb‚Äñ := by
  rw [‚Üê dist_one_left, ‚Üê dist_mul_left a 1 b, mul_one]

@[to_additive (attr := simp)]
theorem dist_self_mul_left (a b : E) : dist (a * b) a = ‚Äñb‚Äñ := by
  rw [dist_comm, dist_self_mul_right]

@[to_additive (attr := simp 1001)]
-- porting note: increase priority because `simp` can prove this
theorem dist_self_div_right (a b : E) : dist a (a / b) = ‚Äñb‚Äñ := by
  rw [div_eq_mul_inv, dist_self_mul_right, norm_inv']

@[to_additive (attr := simp 1001)]
-- porting note: increase priority because `simp` can prove this
theorem dist_self_div_left (a b : E) : dist (a / b) a = ‚Äñb‚Äñ := by
  rw [dist_comm, dist_self_div_right]

@[to_additive]
theorem dist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) : dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ dist a‚ÇÅ b‚ÇÅ + dist a‚ÇÇ b‚ÇÇ := by
  simpa only [dist_mul_left, dist_mul_right] using dist_triangle (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ)

@[to_additive]
theorem dist_mul_mul_le_of_le (h‚ÇÅ : dist a‚ÇÅ b‚ÇÅ ‚â§ r‚ÇÅ) (h‚ÇÇ : dist a‚ÇÇ b‚ÇÇ ‚â§ r‚ÇÇ) :
    dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (dist_mul_mul_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ).trans <| add_le_add h‚ÇÅ h‚ÇÇ

@[to_additive]
theorem dist_div_div_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) : dist (a‚ÇÅ / a‚ÇÇ) (b‚ÇÅ / b‚ÇÇ) ‚â§ dist a‚ÇÅ b‚ÇÅ + dist a‚ÇÇ b‚ÇÇ := by
  simpa only [div_eq_mul_inv, dist_inv_inv] using dist_mul_mul_le a‚ÇÅ a‚ÇÇ‚Åª¬π b‚ÇÅ b‚ÇÇ‚Åª¬π

@[to_additive]
theorem dist_div_div_le_of_le (h‚ÇÅ : dist a‚ÇÅ b‚ÇÅ ‚â§ r‚ÇÅ) (h‚ÇÇ : dist a‚ÇÇ b‚ÇÇ ‚â§ r‚ÇÇ) :
    dist (a‚ÇÅ / a‚ÇÇ) (b‚ÇÅ / b‚ÇÇ) ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (dist_div_div_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ).trans <| add_le_add h‚ÇÅ h‚ÇÇ

@[to_additive]
theorem abs_dist_sub_le_dist_mul_mul (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    |dist a‚ÇÅ b‚ÇÅ - dist a‚ÇÇ b‚ÇÇ| ‚â§ dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) := by
  simpa only [dist_mul_left, dist_mul_right, dist_comm b‚ÇÇ] using
    abs_dist_sub_le (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) (b‚ÇÅ * a‚ÇÇ)

theorem norm_multiset_sum_le {E} [SeminormedAddCommGroup E] (m : Multiset E) :
    ‚Äñm.sum‚Äñ ‚â§ (m.map fun x => ‚Äñx‚Äñ).sum :=
  m.le_sum_of_subadditive norm norm_zero norm_add_le

@[to_additive existing]
theorem norm_multiset_prod_le (m : Multiset E) : ‚Äñm.prod‚Äñ ‚â§ (m.map fun x => ‚Äñx‚Äñ).sum := by
  rw [‚Üê Multiplicative.ofAdd_le, ofAdd_multiset_prod, Multiset.map_map]
  refine' Multiset.le_prod_of_submultiplicative (Multiplicative.ofAdd ‚àò norm) _ (fun x y => _) _
  ¬∑ simp only [comp_apply, norm_one', ofAdd_zero]
  ¬∑ exact norm_mul_le' x y

-- porting note: had to add `Œπ` here because otherwise the universe order gets switched compared to
-- `norm_prod_le` below
theorem norm_sum_le {Œπ E} [SeminormedAddCommGroup E] (s : Finset Œπ) (f : Œπ ‚Üí E) :
    ‚Äñ‚àë i in s, f i‚Äñ ‚â§ ‚àë i in s, ‚Äñf i‚Äñ :=
  s.le_sum_of_subadditive norm norm_zero norm_add_le f

@[to_additive existing]
theorem norm_prod_le (s : Finset Œπ) (f : Œπ ‚Üí E) : ‚Äñ‚àè i in s, f i‚Äñ ‚â§ ‚àë i in s, ‚Äñf i‚Äñ := by
  rw [‚Üê Multiplicative.ofAdd_le, ofAdd_sum]
  refine' Finset.le_prod_of_submultiplicative (Multiplicative.ofAdd ‚àò norm) _ (fun x y => _) _ _
  ¬∑ simp only [comp_apply, norm_one', ofAdd_zero]
  ¬∑ exact norm_mul_le' x y

@[to_additive]
theorem norm_prod_le_of_le (s : Finset Œπ) {f : Œπ ‚Üí E} {n : Œπ ‚Üí ‚Ñù} (h : ‚àÄ b ‚àà s, ‚Äñf b‚Äñ ‚â§ n b) :
    ‚Äñ‚àè b in s, f b‚Äñ ‚â§ ‚àë b in s, n b :=
  (norm_prod_le s f).trans <| Finset.sum_le_sum h

@[to_additive]
theorem dist_prod_prod_le_of_le (s : Finset Œπ) {f a : Œπ ‚Üí E} {d : Œπ ‚Üí ‚Ñù}
    (h : ‚àÄ b ‚àà s, dist (f b) (a b) ‚â§ d b) :
    dist (‚àè b in s, f b) (‚àè b in s, a b) ‚â§ ‚àë b in s, d b := by
  simp only [dist_eq_norm_div, ‚Üê Finset.prod_div_distrib] at *
  exact norm_prod_le_of_le s h

@[to_additive]
theorem dist_prod_prod_le (s : Finset Œπ) (f a : Œπ ‚Üí E) :
    dist (‚àè b in s, f b) (‚àè b in s, a b) ‚â§ ‚àë b in s, dist (f b) (a b) :=
  dist_prod_prod_le_of_le s fun _ _ => le_rfl

@[to_additive]
theorem mul_mem_ball_iff_norm : a * b ‚àà ball a r ‚Üî ‚Äñb‚Äñ < r := by
  rw [mem_ball_iff_norm'', mul_div_cancel''']

@[to_additive]
theorem mul_mem_closedBall_iff_norm : a * b ‚àà closedBall a r ‚Üî ‚Äñb‚Äñ ‚â§ r := by
  rw [mem_closedBall_iff_norm'', mul_div_cancel''']

@[to_additive (attr := simp 1001)]
-- porting note: increase priority so that the left-hand side doesn't simplify
theorem preimage_mul_ball (a b : E) (r : ‚Ñù) : (¬∑ * ¬∑) b ‚Åª¬π' ball a r = ball (a / b) r := by
  ext c
  simp only [dist_eq_norm_div, Set.mem_preimage, mem_ball, div_div_eq_mul_div, mul_comm]

@[to_additive (attr := simp 1001)]
-- porting note: increase priority so that the left-hand side doesn't simplify
theorem preimage_mul_closedBall (a b : E) (r : ‚Ñù) :
    (¬∑ * ¬∑) b ‚Åª¬π' closedBall a r = closedBall (a / b) r := by
  ext c
  simp only [dist_eq_norm_div, Set.mem_preimage, mem_closedBall, div_div_eq_mul_div, mul_comm]

@[to_additive (attr := simp)]
theorem preimage_mul_sphere (a b : E) (r : ‚Ñù) : (¬∑ * ¬∑) b ‚Åª¬π' sphere a r = sphere (a / b) r := by
  ext c
  simp only [Set.mem_preimage, mem_sphere_iff_norm', div_div_eq_mul_div, mul_comm]

@[to_additive norm_nsmul_le]
theorem norm_pow_le_mul_norm (n : ‚Ñï) (a : E) : ‚Äña ^ n‚Äñ ‚â§ n * ‚Äña‚Äñ := by
  induction' n with n ih; ¬∑ simp
  simpa only [pow_succ', Nat.cast_succ, add_mul, one_mul] using norm_mul_le_of_le ih le_rfl

@[to_additive nnnorm_nsmul_le]
theorem nnnorm_pow_le_mul_norm (n : ‚Ñï) (a : E) : ‚Äña ^ n‚Äñ‚Çä ‚â§ n * ‚Äña‚Äñ‚Çä := by
  simpa only [‚Üê NNReal.coe_le_coe, NNReal.coe_mul, NNReal.coe_nat_cast] using
    norm_pow_le_mul_norm n a

@[to_additive]
theorem pow_mem_closedBall {n : ‚Ñï} (h : a ‚àà closedBall b r) :
    a ^ n ‚àà closedBall (b ^ n) (n ‚Ä¢ r) := by
  simp only [mem_closedBall, dist_eq_norm_div, ‚Üê div_pow] at h ‚ä¢
  refine' (norm_pow_le_mul_norm n (a / b)).trans _
  simpa only [nsmul_eq_mul] using mul_le_mul_of_nonneg_left h n.cast_nonneg

@[to_additive]
theorem pow_mem_ball {n : ‚Ñï} (hn : 0 < n) (h : a ‚àà ball b r) : a ^ n ‚àà ball (b ^ n) (n ‚Ä¢ r) := by
  simp only [mem_ball, dist_eq_norm_div, ‚Üê div_pow] at h ‚ä¢
  refine' lt_of_le_of_lt (norm_pow_le_mul_norm n (a / b)) _
  replace hn : 0 < (n : ‚Ñù)
  ¬∑ norm_cast
  rw [nsmul_eq_mul]
  nlinarith

@[to_additive] -- porting note: `simp` can prove this
theorem mul_mem_closedBall_mul_iff {c : E} : a * c ‚àà closedBall (b * c) r ‚Üî a ‚àà closedBall b r := by
  simp only [mem_closedBall, dist_eq_norm_div, mul_div_mul_right_eq_div]

@[to_additive] -- porting note: `simp` can prove this
theorem mul_mem_ball_mul_iff {c : E} : a * c ‚àà ball (b * c) r ‚Üî a ‚àà ball b r := by
  simp only [mem_ball, dist_eq_norm_div, mul_div_mul_right_eq_div]

@[to_additive]
theorem smul_closedBall'' : a ‚Ä¢ closedBall b r = closedBall (a ‚Ä¢ b) r := by
  ext
  simp [mem_closedBall, Set.mem_smul_set, dist_eq_norm_div, _root_.div_eq_inv_mul, ‚Üê
    eq_inv_mul_iff_mul_eq, mul_assoc]
  -- porting note: `ENNReal.div_eq_inv_mul` should be `protected`?

@[to_additive]
theorem smul_ball'' : a ‚Ä¢ ball b r = ball (a ‚Ä¢ b) r := by
  ext
  simp [mem_ball, Set.mem_smul_set, dist_eq_norm_div, _root_.div_eq_inv_mul,
    ‚Üê eq_inv_mul_iff_mul_eq, mul_assoc]

open Finset

@[to_additive]
theorem controlled_prod_of_mem_closure {s : Subgroup E} (hg : a ‚àà closure (s : Set E)) {b : ‚Ñï ‚Üí ‚Ñù}
    (b_pos : ‚àÄ n, 0 < b n) :
    ‚àÉ v : ‚Ñï ‚Üí E,
      Tendsto (fun n => ‚àè i in range (n + 1), v i) atTop (ùìù a) ‚àß
        (‚àÄ n, v n ‚àà s) ‚àß ‚Äñv 0 / a‚Äñ < b 0 ‚àß ‚àÄ n, 0 < n ‚Üí ‚Äñv n‚Äñ < b n := by
  obtain ‚ü®u : ‚Ñï ‚Üí E, u_in : ‚àÄ n, u n ‚àà s, lim_u : Tendsto u atTop (ùìù a)‚ü© :=
    mem_closure_iff_seq_limit.mp hg
  obtain ‚ü®n‚ÇÄ, hn‚ÇÄ‚ü© : ‚àÉ n‚ÇÄ, ‚àÄ n ‚â• n‚ÇÄ, ‚Äñu n / a‚Äñ < b 0 :=
    haveI : { x | ‚Äñx / a‚Äñ < b 0 } ‚àà ùìù a := by
      simp_rw [‚Üê dist_eq_norm_div]
      exact Metric.ball_mem_nhds _ (b_pos _)
    Filter.tendsto_atTop'.mp lim_u _ this
  set z : ‚Ñï ‚Üí E := fun n => u (n + n‚ÇÄ)
  have lim_z : Tendsto z atTop (ùìù a) := lim_u.comp (tendsto_add_atTop_nat n‚ÇÄ)
  have mem_ùì§ : ‚àÄ n, { p : E √ó E | ‚Äñp.1 / p.2‚Äñ < b (n + 1) } ‚àà ùì§ E := fun n => by
    simpa [‚Üê dist_eq_norm_div] using Metric.dist_mem_uniformity (b_pos <| n + 1)
  obtain ‚ü®œÜ : ‚Ñï ‚Üí ‚Ñï, œÜ_extr : StrictMono œÜ, hœÜ : ‚àÄ n, ‚Äñz (œÜ <| n + 1) / z (œÜ n)‚Äñ < b (n + 1)‚ü© :=
    lim_z.cauchySeq.subseq_mem mem_ùì§
  set w : ‚Ñï ‚Üí E := z ‚àò œÜ
  have hw : Tendsto w atTop (ùìù a) := lim_z.comp œÜ_extr.tendsto_atTop
  set v : ‚Ñï ‚Üí E := fun i => if i = 0 then w 0 else w i / w (i - 1)
  refine' ‚ü®v, Tendsto.congr (Finset.eq_prod_range_div' w) hw, _, hn‚ÇÄ _ (n‚ÇÄ.le_add_left _), _‚ü©
  ¬∑ rintro ‚ü®‚ü©
    ¬∑ change w 0 ‚àà s
      apply u_in
    ¬∑ apply s.div_mem <;> apply u_in
  ¬∑ intro l hl
    obtain ‚ü®k, rfl‚ü© : ‚àÉ k, l = k + 1
    exact Nat.exists_eq_succ_of_ne_zero hl.ne'
    apply hœÜ

@[to_additive]
theorem controlled_prod_of_mem_closure_range {j : E ‚Üí* F} {b : F}
    (hb : b ‚àà closure (j.range : Set F)) {f : ‚Ñï ‚Üí ‚Ñù} (b_pos : ‚àÄ n, 0 < f n) :
    ‚àÉ a : ‚Ñï ‚Üí E,
      Tendsto (fun n => ‚àè i in range (n + 1), j (a i)) atTop (ùìù b) ‚àß
        ‚Äñj (a 0) / b‚Äñ < f 0 ‚àß ‚àÄ n, 0 < n ‚Üí ‚Äñj (a n)‚Äñ < f n := by
  obtain ‚ü®v, sum_v, v_in, hv‚ÇÄ, hv_pos‚ü© := controlled_prod_of_mem_closure hb b_pos
  choose g hg using v_in
  exact
    ‚ü®g, by simpa [‚Üê hg] using sum_v, by simpa [hg 0] using hv‚ÇÄ,
      fun n hn => by simpa [hg] using hv_pos n hn‚ü©

@[to_additive]
theorem nndist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    nndist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ nndist a‚ÇÅ b‚ÇÅ + nndist a‚ÇÇ b‚ÇÇ :=
  NNReal.coe_le_coe.1 <| dist_mul_mul_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ

@[to_additive]
theorem edist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    edist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ edist a‚ÇÅ b‚ÇÅ + edist a‚ÇÇ b‚ÇÇ := by
  simp only [edist_nndist]
  norm_cast
  apply nndist_mul_mul_le

@[to_additive]
theorem nnnorm_multiset_prod_le (m : Multiset E) : ‚Äñm.prod‚Äñ‚Çä ‚â§ (m.map fun x => ‚Äñx‚Äñ‚Çä).sum :=
  NNReal.coe_le_coe.1 <| by
    push_cast
    rw [Multiset.map_map]
    exact norm_multiset_prod_le _

@[to_additive]
theorem nnnorm_prod_le (s : Finset Œπ) (f : Œπ ‚Üí E) : ‚Äñ‚àè a in s, f a‚Äñ‚Çä ‚â§ ‚àë a in s, ‚Äñf a‚Äñ‚Çä :=
  NNReal.coe_le_coe.1 <| by
    push_cast
    exact norm_prod_le _ _

@[to_additive]
theorem nnnorm_prod_le_of_le (s : Finset Œπ) {f : Œπ ‚Üí E} {n : Œπ ‚Üí ‚Ñù‚â•0} (h : ‚àÄ b ‚àà s, ‚Äñf b‚Äñ‚Çä ‚â§ n b) :
    ‚Äñ‚àè b in s, f b‚Äñ‚Çä ‚â§ ‚àë b in s, n b :=
  (norm_prod_le_of_le s h).trans_eq NNReal.coe_sum.symm

namespace Real

instance norm : Norm ‚Ñù where
  norm r := |r|

@[simp]
theorem norm_eq_abs (r : ‚Ñù) : ‚Äñr‚Äñ = |r| :=
  rfl

instance normedAddCommGroup : NormedAddCommGroup ‚Ñù :=
  ‚ü®fun _r _y => rfl‚ü©

theorem norm_of_nonneg (hr : 0 ‚â§ r) : ‚Äñr‚Äñ = r :=
  abs_of_nonneg hr

theorem norm_of_nonpos (hr : r ‚â§ 0) : ‚Äñr‚Äñ = -r :=
  abs_of_nonpos hr

theorem le_norm_self (r : ‚Ñù) : r ‚â§ ‚Äñr‚Äñ :=
  le_abs_self r

-- porting note: `simp` can prove this
theorem norm_coe_nat (n : ‚Ñï) : ‚Äñ(n : ‚Ñù)‚Äñ = n :=
  abs_of_nonneg n.cast_nonneg

@[simp]
theorem nnnorm_coe_nat (n : ‚Ñï) : ‚Äñ(n : ‚Ñù)‚Äñ‚Çä = n :=
  NNReal.eq <| norm_coe_nat _

-- porting note: `simp` can prove this
theorem norm_two : ‚Äñ(2 : ‚Ñù)‚Äñ = 2 :=
  abs_of_pos zero_lt_two

@[simp]
theorem nnnorm_two : ‚Äñ(2 : ‚Ñù)‚Äñ‚Çä = 2 :=
  NNReal.eq <| by simp

theorem nnnorm_of_nonneg (hr : 0 ‚â§ r) : ‚Äñr‚Äñ‚Çä = ‚ü®r, hr‚ü© :=
  NNReal.eq <| norm_of_nonneg hr

@[simp]
theorem nnnorm_abs (r : ‚Ñù) : ‚Äñ|r|‚Äñ‚Çä = ‚Äñr‚Äñ‚Çä := by simp [nnnorm]

theorem ennnorm_eq_ofReal (hr : 0 ‚â§ r) : (‚Äñr‚Äñ‚Çä : ‚Ñù‚â•0‚àû) = ENNReal.ofReal r := by
  rw [‚Üê ofReal_norm_eq_coe_nnnorm, norm_of_nonneg hr]

theorem ennnorm_eq_ofReal_abs (r : ‚Ñù) : (‚Äñr‚Äñ‚Çä : ‚Ñù‚â•0‚àû) = ENNReal.ofReal |r| := by
  rw [‚Üê Real.nnnorm_abs r, Real.ennnorm_eq_ofReal (abs_nonneg _)]

theorem toNNReal_eq_nnnorm_of_nonneg (hr : 0 ‚â§ r) : r.toNNReal = ‚Äñr‚Äñ‚Çä := by
  rw [Real.toNNReal_of_nonneg hr]
  ext
  rw [coe_mk, coe_nnnorm r, Real.norm_eq_abs r, abs_of_nonneg hr]
  -- porting note: this is due to the change from `Subtype.val` to `NNReal.toReal` for the coercion

theorem ofReal_le_ennnorm (r : ‚Ñù) : ENNReal.ofReal r ‚â§ ‚Äñr‚Äñ‚Çä := by
  obtain hr | hr := le_total 0 r
  ¬∑ exact (Real.ennnorm_eq_ofReal hr).ge
  ¬∑ rw [ENNReal.ofReal_eq_zero.2 hr]
    exact bot_le
-- porting note: should this be renamed to `Real.ofReal_le_nnnorm`?

end Real

namespace Int

instance normedAddCommGroup : NormedAddCommGroup ‚Ñ§ where
  norm n := ‚Äñ(n : ‚Ñù)‚Äñ
  dist_eq m n := by simp only [Int.dist_eq, norm, Int.cast_sub]

@[norm_cast]
theorem norm_cast_real (m : ‚Ñ§) : ‚Äñ(m : ‚Ñù)‚Äñ = ‚Äñm‚Äñ :=
  rfl

theorem norm_eq_abs (n : ‚Ñ§) : ‚Äñn‚Äñ = |n| :=
  show ‚Äñ(n : ‚Ñù)‚Äñ = |n| by rw [Real.norm_eq_abs, cast_abs]
-- porting note: I'm not sure why this isn't `rfl` anymore, but I suspect it's about coercions

@[simp]
theorem norm_coe_nat (n : ‚Ñï) : ‚Äñ(n : ‚Ñ§)‚Äñ = n := by simp [Int.norm_eq_abs]

theorem _root_.NNReal.coe_natAbs (n : ‚Ñ§) : (n.natAbs : ‚Ñù‚â•0) = ‚Äñn‚Äñ‚Çä :=
  NNReal.eq <|
    calc
      ((n.natAbs : ‚Ñù‚â•0) : ‚Ñù) = (n.natAbs : ‚Ñ§) := by simp only [Int.cast_ofNat, NNReal.coe_nat_cast]
      _ = |n| := by simp only [Int.coe_natAbs, Int.cast_abs]
      _ = ‚Äñn‚Äñ := (norm_eq_abs n).symm

theorem abs_le_floor_nnreal_iff (z : ‚Ñ§) (c : ‚Ñù‚â•0) : |z| ‚â§ ‚åäc‚åã‚Çä ‚Üî ‚Äñz‚Äñ‚Çä ‚â§ c := by
  rw [Int.abs_eq_natAbs, Int.ofNat_le, Nat.le_floor_iff (zero_le c), NNReal.coe_natAbs z]

end Int

namespace Rat

instance normedAddCommGroup : NormedAddCommGroup ‚Ñö where
  norm r := ‚Äñ(r : ‚Ñù)‚Äñ
  dist_eq r‚ÇÅ r‚ÇÇ := by simp only [Rat.dist_eq, norm, Rat.cast_sub]

@[norm_cast, simp 1001]
-- porting note: increase priority to prevent the left-hand side from simplifying
theorem norm_cast_real (r : ‚Ñö) : ‚Äñ(r : ‚Ñù)‚Äñ = ‚Äñr‚Äñ :=
  rfl

@[norm_cast, simp]
theorem _root_.Int.norm_cast_rat (m : ‚Ñ§) : ‚Äñ(m : ‚Ñö)‚Äñ = ‚Äñm‚Äñ := by
  rw [‚Üê Rat.norm_cast_real, ‚Üê Int.norm_cast_real]; congr 1

end Rat

-- Now that we've installed the norm on `‚Ñ§`,
-- we can state some lemmas about `zsmul`.
section

variable [SeminormedCommGroup Œ±]

@[to_additive norm_zsmul_le]
theorem norm_zpow_le_mul_norm (n : ‚Ñ§) (a : Œ±) : ‚Äña ^ n‚Äñ ‚â§ ‚Äñn‚Äñ * ‚Äña‚Äñ := by
  rcases n.eq_nat_or_neg with ‚ü®n, rfl | rfl‚ü© <;> simpa using norm_pow_le_mul_norm n a

@[to_additive nnnorm_zsmul_le]
theorem nnnorm_zpow_le_mul_norm (n : ‚Ñ§) (a : Œ±) : ‚Äña ^ n‚Äñ‚Çä ‚â§ ‚Äñn‚Äñ‚Çä * ‚Äña‚Äñ‚Çä := by
  simpa only [‚Üê NNReal.coe_le_coe, NNReal.coe_mul] using norm_zpow_le_mul_norm n a

end

namespace LipschitzWith

variable [PseudoEMetricSpace Œ±] {K Kf Kg : ‚Ñù‚â•0} {f g : Œ± ‚Üí E}

@[to_additive]
theorem inv (hf : LipschitzWith K f) : LipschitzWith K fun x => (f x)‚Åª¬π := fun x y =>
  (edist_inv_inv _ _).trans_le <| hf x y

@[to_additive add]
theorem mul' (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x => f x * g x := fun x y =>
  calc
    edist (f x * g x) (f y * g y) ‚â§ edist (f x) (f y) + edist (g x) (g y) :=
      edist_mul_mul_le _ _ _ _
    _ ‚â§ Kf * edist x y + Kg * edist x y := (add_le_add (hf x y) (hg x y))
    _ = (Kf + Kg) * edist x y := (add_mul _ _ _).symm

@[to_additive]
theorem div (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x => f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul' hg.inv

end LipschitzWith

namespace AntilipschitzWith

variable [PseudoEMetricSpace Œ±] {K Kf Kg : ‚Ñù‚â•0} {f g : Œ± ‚Üí E}

@[to_additive]
theorem mul_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg g) (hK : Kg < Kf‚Åª¬π) :
    AntilipschitzWith (Kf‚Åª¬π - Kg)‚Åª¬π fun x => f x * g x := by
  letI : PseudoMetricSpace Œ± := PseudoEMetricSpace.toPseudoMetricSpace hf.edist_ne_top
  refine' AntilipschitzWith.of_le_mul_dist fun x y => _
  rw [NNReal.coe_inv, ‚Üê _root_.div_eq_inv_mul]
  rw [le_div_iff (NNReal.coe_pos.2 <| tsub_pos_iff_lt.2 hK)]
  rw [mul_comm, NNReal.coe_sub hK.le, _root_.sub_mul]
  -- porting note: `ENNReal.sub_mul` should be `protected`?
  calc
    ‚ÜëKf‚Åª¬π * dist x y - Kg * dist x y ‚â§ dist (f x) (f y) - dist (g x) (g y) :=
      sub_le_sub (hf.mul_le_dist x y) (hg.dist_le_mul x y)
    _ ‚â§ _ := le_trans (le_abs_self _) (abs_dist_sub_le_dist_mul_mul _ _ _ _)

@[to_additive]
theorem mul_div_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg (g / f))
    (hK : Kg < Kf‚Åª¬π) : AntilipschitzWith (Kf‚Åª¬π - Kg)‚Åª¬π g := by
  simpa only [Pi.div_apply, mul_div_cancel'_right] using hf.mul_lipschitzWith hg hK

@[to_additive le_mul_norm_sub]
theorem le_mul_norm_div {f : E ‚Üí F} (hf : AntilipschitzWith K f) (x y : E) :
    ‚Äñx / y‚Äñ ‚â§ K * ‚Äñf x / f y‚Äñ := by simp [‚Üê dist_eq_norm_div, hf.le_mul_dist x y]

end AntilipschitzWith

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.to_lipschitzMul : LipschitzMul E :=
  ‚ü®‚ü®1 + 1, LipschitzWith.prod_fst.mul' LipschitzWith.prod_snd‚ü©‚ü©

-- See note [lower instance priority]
/-- A seminormed group is a uniform group, i.e., multiplication and division are uniformly
continuous. -/
@[to_additive "A seminormed group is a uniform additive group, i.e., addition and subtraction are
uniformly continuous."]
instance (priority := 100) SeminormedCommGroup.to_uniformGroup : UniformGroup E :=
  ‚ü®(LipschitzWith.prod_fst.div LipschitzWith.prod_snd).uniformContinuous‚ü©

-- short-circuit type class inference
-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.toTopologicalGroup : TopologicalGroup E :=
  inferInstance

@[to_additive]
theorem cauchySeq_prod_of_eventually_eq {u v : ‚Ñï ‚Üí E} {N : ‚Ñï} (huv : ‚àÄ n ‚â• N, u n = v n)
    (hv : CauchySeq fun n => ‚àè k in range (n + 1), v k) :
    CauchySeq fun n => ‚àè k in range (n + 1), u k := by
  let d : ‚Ñï ‚Üí E := fun n => ‚àè k in range (n + 1), u k / v k
  rw [show (fun n => ‚àè k in range (n + 1), u k) = d * fun n => ‚àè k in range (n + 1), v k
      by ext n; simp]
  suffices ‚àÄ n ‚â• N, d n = d N by exact (tendsto_atTop_of_eventually_const this).cauchySeq.mul hv
  intro n hn
  dsimp
  rw [eventually_constant_prod _ (add_le_add_right hn 1)]
  intro m hm
  simp [huv m (le_of_lt hm)]

end SeminormedCommGroup

section NormedGroup

variable [NormedGroup E] [NormedGroup F] {a b : E}

@[to_additive (attr := simp) norm_eq_zero]
theorem norm_eq_zero'' : ‚Äña‚Äñ = 0 ‚Üî a = 1 :=
  norm_eq_zero'''

@[to_additive norm_ne_zero_iff]
theorem norm_ne_zero_iff' : ‚Äña‚Äñ ‚â† 0 ‚Üî a ‚â† 1 :=
  norm_eq_zero''.not

@[to_additive (attr := simp) norm_pos_iff]
theorem norm_pos_iff'' : 0 < ‚Äña‚Äñ ‚Üî a ‚â† 1 :=
  norm_pos_iff'''

@[to_additive (attr := simp) norm_le_zero_iff]
theorem norm_le_zero_iff'' : ‚Äña‚Äñ ‚â§ 0 ‚Üî a = 1 :=
  norm_le_zero_iff'''

@[to_additive]
theorem norm_div_eq_zero_iff : ‚Äña / b‚Äñ = 0 ‚Üî a = b := by rw [norm_eq_zero'', div_eq_one]

@[to_additive]
theorem norm_div_pos_iff : 0 < ‚Äña / b‚Äñ ‚Üî a ‚â† b := by
  rw [(norm_nonneg' _).lt_iff_ne, ne_comm]
  exact norm_div_eq_zero_iff.not

@[to_additive eq_of_norm_sub_le_zero]
theorem eq_of_norm_div_le_zero (h : ‚Äña / b‚Äñ ‚â§ 0) : a = b := by
  rwa [‚Üê div_eq_one, ‚Üê norm_le_zero_iff'']

alias ‚ü®eq_of_norm_div_eq_zero, _‚ü© := norm_div_eq_zero_iff

attribute [to_additive] eq_of_norm_div_eq_zero

@[to_additive (attr := simp) nnnorm_eq_zero]
theorem nnnorm_eq_zero' : ‚Äña‚Äñ‚Çä = 0 ‚Üî a = 1 := by
  rw [‚Üê NNReal.coe_eq_zero, coe_nnnorm', norm_eq_zero'']

@[to_additive nnnorm_ne_zero_iff]
theorem nnnorm_ne_zero_iff' : ‚Äña‚Äñ‚Çä ‚â† 0 ‚Üî a ‚â† 1 :=
  nnnorm_eq_zero'.not

@[to_additive]
theorem tendsto_norm_div_self_punctured_nhds (a : E) :
    Tendsto (fun x => ‚Äñx / a‚Äñ) (ùìù[‚â†] a) (ùìù[>] 0) :=
  (tendsto_norm_div_self a).inf <|
    tendsto_principal_principal.2 fun _x hx => norm_pos_iff''.2 <| div_ne_one.2 hx

@[to_additive]
theorem tendsto_norm_nhdsWithin_one : Tendsto (norm : E ‚Üí ‚Ñù) (ùìù[‚â†] 1) (ùìù[>] 0) :=
  tendsto_norm_one.inf <| tendsto_principal_principal.2 fun _x => norm_pos_iff''.2

variable (E)

/-- The norm of a normed group as a group norm. -/
@[to_additive "The norm of a normed group as an additive group norm."]
def normGroupNorm : GroupNorm E :=
  { normGroupSeminorm _ with eq_one_of_map_eq_zero' := fun _ => norm_eq_zero''.1 }

@[simp]
theorem coe_normGroupNorm : ‚áë(normGroupNorm E) = norm :=
  rfl

end NormedGroup

section NormedAddGroup

variable [NormedAddGroup E] [TopologicalSpace Œ±] {f : Œ± ‚Üí E}

/-! Some relations with `HasCompactSupport` -/


theorem hasCompactSupport_norm_iff : (HasCompactSupport fun x => ‚Äñf x‚Äñ) ‚Üî HasCompactSupport f :=
  hasCompactSupport_comp_left norm_eq_zero

alias ‚ü®_, HasCompactSupport.norm‚ü© := hasCompactSupport_norm_iff

theorem Continuous.bounded_above_of_compact_support (hf : Continuous f) (h : HasCompactSupport f) :
    ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C := by
  simpa [bddAbove_def] using hf.norm.bddAbove_range_of_hasCompactSupport h.norm

end NormedAddGroup

section NormedAddGroupSource

variable [NormedAddGroup Œ±] {f : Œ± ‚Üí E}

@[to_additive]
theorem HasCompactMulSupport.exists_pos_le_norm [One E] (hf : HasCompactMulSupport f) :
    ‚àÉ R : ‚Ñù, 0 < R ‚àß ‚àÄ x : Œ±, R ‚â§ ‚Äñx‚Äñ ‚Üí f x = 1 := by
  obtain ‚ü®K, ‚ü®hK1, hK2‚ü©‚ü© := exists_compact_iff_hasCompactMulSupport.mpr hf
  obtain ‚ü®S, hS, hS'‚ü© := hK1.isBounded.exists_pos_norm_le
  refine' ‚ü®S + 1, by positivity, fun x hx => hK2 x ((mt <| hS' x) _)‚ü©
  -- porting note: `ENNReal.add_lt_add` should be `protected`?
  -- [context: we used `_root_.add_lt_add` in a previous version of this proof]
  contrapose! hx
  exact lt_add_of_le_of_pos hx zero_lt_one

end NormedAddGroupSource

/-! ### `ULift` -/


namespace ULift

section Norm

variable [Norm E]

instance norm : Norm (ULift E) :=
  ‚ü®fun x => ‚Äñx.down‚Äñ‚ü©

theorem norm_def (x : ULift E) : ‚Äñx‚Äñ = ‚Äñx.down‚Äñ :=
  rfl

@[simp]
theorem norm_up (x : E) : ‚ÄñULift.up x‚Äñ = ‚Äñx‚Äñ :=
  rfl

@[simp]
theorem norm_down (x : ULift E) : ‚Äñx.down‚Äñ = ‚Äñx‚Äñ :=
  rfl

end Norm

section NNNorm

variable [NNNorm E]

instance nnnorm : NNNorm (ULift E) :=
  ‚ü®fun x => ‚Äñx.down‚Äñ‚Çä‚ü©

theorem nnnorm_def (x : ULift E) : ‚Äñx‚Äñ‚Çä = ‚Äñx.down‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_up (x : E) : ‚ÄñULift.up x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_down (x : ULift E) : ‚Äñx.down‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

end NNNorm

@[to_additive]
instance seminormedGroup [SeminormedGroup E] : SeminormedGroup (ULift E) :=
  SeminormedGroup.induced _ _
  { toFun := ULift.down,
    map_one' := rfl,
    map_mul' := fun _ _ => rfl : ULift E ‚Üí* E }

@[to_additive]
instance seminormedCommGroup [SeminormedCommGroup E] : SeminormedCommGroup (ULift E) :=
  SeminormedCommGroup.induced _ _
  { toFun := ULift.down,
    map_one' := rfl,
    map_mul' := fun _ _ => rfl : ULift E ‚Üí* E }

@[to_additive]
instance normedGroup [NormedGroup E] : NormedGroup (ULift E) :=
  NormedGroup.induced _ _
  { toFun := ULift.down,
    map_one' := rfl,
    map_mul' := fun _ _ => rfl : ULift E ‚Üí* E }
  down_injective

@[to_additive]
instance normedCommGroup [NormedCommGroup E] : NormedCommGroup (ULift E) :=
  NormedCommGroup.induced _ _
  { toFun := ULift.down,
    map_one' := rfl,
    map_mul' := fun _ _ => rfl : ULift E ‚Üí* E }
  down_injective

end ULift

/-! ### `Additive`, `Multiplicative` -/


section AdditiveMultiplicative

open Additive Multiplicative

section Norm

variable [Norm E]

instance Additive.toNorm : Norm (Additive E) :=
  ‚ÄπNorm E‚Ä∫

instance Multiplicative.toNorm : Norm (Multiplicative E) :=
  ‚ÄπNorm E‚Ä∫

@[simp]
theorem norm_toMul (x) : ‚Äñ(toMul x : E)‚Äñ = ‚Äñx‚Äñ :=
  rfl

@[simp]
theorem norm_ofMul (x : E) : ‚ÄñofMul x‚Äñ = ‚Äñx‚Äñ :=
  rfl

@[simp]
theorem norm_toAdd (x) : ‚Äñ(toAdd x : E)‚Äñ = ‚Äñx‚Äñ :=
  rfl

@[simp]
theorem norm_ofAdd (x : E) : ‚ÄñofAdd x‚Äñ = ‚Äñx‚Äñ :=
  rfl

end Norm

section NNNorm

variable [NNNorm E]

instance Additive.toNNNorm : NNNorm (Additive E) :=
  ‚ÄπNNNorm E‚Ä∫

instance Multiplicative.toNNNorm : NNNorm (Multiplicative E) :=
  ‚ÄπNNNorm E‚Ä∫

@[simp]
theorem nnnorm_toMul (x) : ‚Äñ(toMul x : E)‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_ofMul (x : E) : ‚ÄñofMul x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_toAdd (x) : ‚Äñ(toAdd x : E)‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_ofAdd (x : E) : ‚ÄñofAdd x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

end NNNorm

instance Additive.seminormedAddGroup [SeminormedGroup E] : SeminormedAddGroup (Additive E) where
  dist_eq := fun x y => dist_eq_norm_div (toMul x) (toMul y)


instance Multiplicative.seminormedGroup [SeminormedAddGroup E] :
    SeminormedGroup (Multiplicative E) where
  dist_eq := fun x y => dist_eq_norm_sub (toMul x) (toMul y)

instance Additive.seminormedCommGroup [SeminormedCommGroup E] :
    SeminormedAddCommGroup (Additive E) :=
  { Additive.seminormedAddGroup with
    add_comm := add_comm }

instance Multiplicative.seminormedAddCommGroup [SeminormedAddCommGroup E] :
    SeminormedCommGroup (Multiplicative E) :=
  { Multiplicative.seminormedGroup with
    mul_comm := mul_comm }

instance Additive.normedAddGroup [NormedGroup E] : NormedAddGroup (Additive E) :=
  { Additive.seminormedAddGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

instance Multiplicative.normedGroup [NormedAddGroup E] : NormedGroup (Multiplicative E) :=
  { Multiplicative.seminormedGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

instance Additive.normedAddCommGroup [NormedCommGroup E] : NormedAddCommGroup (Additive E) :=
  { Additive.seminormedAddGroup with
    add_comm := add_comm
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

instance Multiplicative.normedCommGroup [NormedAddCommGroup E] :
    NormedCommGroup (Multiplicative E) :=
  { Multiplicative.seminormedGroup with
    mul_comm := mul_comm
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

end AdditiveMultiplicative

/-! ### Order dual -/


section OrderDual

open OrderDual

section Norm

variable [Norm E]

instance OrderDual.toNorm : Norm E·µí·µà :=
  ‚ÄπNorm E‚Ä∫

@[simp]
theorem norm_toDual (x : E) : ‚ÄñtoDual x‚Äñ = ‚Äñx‚Äñ :=
  rfl

@[simp]
theorem norm_ofDual (x : E·µí·µà) : ‚ÄñofDual x‚Äñ = ‚Äñx‚Äñ :=
  rfl

end Norm

section NNNorm

variable [NNNorm E]

instance OrderDual.toNNNorm : NNNorm E·µí·µà :=
  ‚ÄπNNNorm E‚Ä∫

@[simp]
theorem nnnorm_toDual (x : E) : ‚ÄñtoDual x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

@[simp]
theorem nnnorm_ofDual (x : E·µí·µà) : ‚ÄñofDual x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  rfl

end NNNorm

namespace OrderDual

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) seminormedGroup [SeminormedGroup E] : SeminormedGroup E·µí·µà :=
  ‚ÄπSeminormedGroup E‚Ä∫

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) seminormedCommGroup [SeminormedCommGroup E] :
    SeminormedCommGroup E·µí·µà :=
  ‚ÄπSeminormedCommGroup E‚Ä∫

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) normedGroup [NormedGroup E] : NormedGroup E·µí·µà :=
  ‚ÄπNormedGroup E‚Ä∫

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) normedCommGroup [NormedCommGroup E] : NormedCommGroup E·µí·µà :=
  ‚ÄπNormedCommGroup E‚Ä∫

end OrderDual

end OrderDual

/-! ### Binary product of normed groups -/


section Norm

variable [Norm E] [Norm F] {x : E √ó F} {r : ‚Ñù}

instance Prod.toNorm : Norm (E √ó F) :=
  ‚ü®fun x => ‚Äñx.1‚Äñ ‚äî ‚Äñx.2‚Äñ‚ü©

theorem Prod.norm_def (x : E √ó F) : ‚Äñx‚Äñ = max ‚Äñx.1‚Äñ ‚Äñx.2‚Äñ :=
  rfl

theorem norm_fst_le (x : E √ó F) : ‚Äñx.1‚Äñ ‚â§ ‚Äñx‚Äñ :=
  le_max_left _ _

theorem norm_snd_le (x : E √ó F) : ‚Äñx.2‚Äñ ‚â§ ‚Äñx‚Äñ :=
  le_max_right _ _

theorem norm_prod_le_iff : ‚Äñx‚Äñ ‚â§ r ‚Üî ‚Äñx.1‚Äñ ‚â§ r ‚àß ‚Äñx.2‚Äñ ‚â§ r :=
  max_le_iff

end Norm

section SeminormedGroup

variable [SeminormedGroup E] [SeminormedGroup F]

/-- Product of seminormed groups, using the sup norm. -/
@[to_additive "Product of seminormed groups, using the sup norm."]
instance Prod.seminormedGroup : SeminormedGroup (E √ó F) :=
  ‚ü®fun x y => by
    simp only [Prod.norm_def, Prod.dist_eq, dist_eq_norm_div, Prod.fst_div, Prod.snd_div]‚ü©

@[to_additive Prod.nnnorm_def']
theorem Prod.nnorm_def (x : E √ó F) : ‚Äñx‚Äñ‚Çä = max ‚Äñx.1‚Äñ‚Çä ‚Äñx.2‚Äñ‚Çä :=
  rfl

end SeminormedGroup

namespace Prod

/-- Product of seminormed groups, using the sup norm. -/
@[to_additive "Product of seminormed groups, using the sup norm."]
instance seminormedCommGroup [SeminormedCommGroup E] [SeminormedCommGroup F] :
    SeminormedCommGroup (E √ó F) :=
  { Prod.seminormedGroup with
    mul_comm := mul_comm }

/-- Product of normed groups, using the sup norm. -/
@[to_additive "Product of normed groups, using the sup norm."]
instance normedGroup [NormedGroup E] [NormedGroup F] : NormedGroup (E √ó F) :=
  { Prod.seminormedGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Product of normed groups, using the sup norm. -/
@[to_additive "Product of normed groups, using the sup norm."]
instance normedCommGroup [NormedCommGroup E] [NormedCommGroup F] : NormedCommGroup (E √ó F) :=
  { Prod.seminormedGroup with
    mul_comm := mul_comm
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-! ### Finite product of normed groups -/

end Prod

section Pi

variable {œÄ : Œπ ‚Üí Type*} [Fintype Œπ]

section SeminormedGroup

variable [‚àÄ i, SeminormedGroup (œÄ i)] [SeminormedGroup E] (f : ‚àÄ i, œÄ i) {x : ‚àÄ i, œÄ i} {r : ‚Ñù}

/-- Finite product of seminormed groups, using the sup norm. -/
@[to_additive "Finite product of seminormed groups, using the sup norm."]
instance Pi.seminormedGroup : SeminormedGroup (‚àÄ i, œÄ i) where
  norm f := ‚Üë(Finset.univ.sup fun b => ‚Äñf b‚Äñ‚Çä)
  dist_eq x y :=
    congr_arg (toReal : ‚Ñù‚â•0 ‚Üí ‚Ñù) <|
      congr_arg (Finset.sup Finset.univ) <|
        funext fun a => show nndist (x a) (y a) = ‚Äñx a / y a‚Äñ‚Çä from nndist_eq_nnnorm_div (x a) (y a)

@[to_additive Pi.norm_def]
theorem Pi.norm_def' : ‚Äñf‚Äñ = ‚Üë(Finset.univ.sup fun b => ‚Äñf b‚Äñ‚Çä) :=
  rfl

@[to_additive Pi.nnnorm_def]
theorem Pi.nnnorm_def' : ‚Äñf‚Äñ‚Çä = Finset.univ.sup fun b => ‚Äñf b‚Äñ‚Çä :=
  Subtype.eta _ _

/-- The seminorm of an element in a product space is `‚â§ r` if and only if the norm of each
component is. -/
@[to_additive pi_norm_le_iff_of_nonneg "The seminorm of an element in a product space is `‚â§ r` if
and only if the norm of each component is."]
theorem pi_norm_le_iff_of_nonneg' (hr : 0 ‚â§ r) : ‚Äñx‚Äñ ‚â§ r ‚Üî ‚àÄ i, ‚Äñx i‚Äñ ‚â§ r := by
  simp only [‚Üê dist_one_right, dist_pi_le_iff hr, Pi.one_apply]

@[to_additive pi_nnnorm_le_iff]
theorem pi_nnnorm_le_iff' {r : ‚Ñù‚â•0} : ‚Äñx‚Äñ‚Çä ‚â§ r ‚Üî ‚àÄ i, ‚Äñx i‚Äñ‚Çä ‚â§ r :=
  pi_norm_le_iff_of_nonneg' r.coe_nonneg

@[to_additive pi_norm_le_iff_of_nonempty]
theorem pi_norm_le_iff_of_nonempty' [Nonempty Œπ] : ‚Äñf‚Äñ ‚â§ r ‚Üî ‚àÄ b, ‚Äñf b‚Äñ ‚â§ r := by
  by_cases hr : 0 ‚â§ r
  ¬∑ exact pi_norm_le_iff_of_nonneg' hr
  ¬∑ exact
      iff_of_false (fun h => hr <| (norm_nonneg' _).trans h) fun h =>
        hr <| (norm_nonneg' _).trans <| h <| Classical.arbitrary _

/-- The seminorm of an element in a product space is `< r` if and only if the norm of each
component is. -/
@[to_additive pi_norm_lt_iff "The seminorm of an element in a product space is `< r` if and only
if the norm of each component is."]
theorem pi_norm_lt_iff' (hr : 0 < r) : ‚Äñx‚Äñ < r ‚Üî ‚àÄ i, ‚Äñx i‚Äñ < r := by
  simp only [‚Üê dist_one_right, dist_pi_lt_iff hr, Pi.one_apply]

@[to_additive pi_nnnorm_lt_iff]
theorem pi_nnnorm_lt_iff' {r : ‚Ñù‚â•0} (hr : 0 < r) : ‚Äñx‚Äñ‚Çä < r ‚Üî ‚àÄ i, ‚Äñx i‚Äñ‚Çä < r :=
  pi_norm_lt_iff' hr

@[to_additive norm_le_pi_norm]
theorem norm_le_pi_norm' (i : Œπ) : ‚Äñf i‚Äñ ‚â§ ‚Äñf‚Äñ :=
  (pi_norm_le_iff_of_nonneg' <| norm_nonneg' _).1 le_rfl i

@[to_additive nnnorm_le_pi_nnnorm]
theorem nnnorm_le_pi_nnnorm' (i : Œπ) : ‚Äñf i‚Äñ‚Çä ‚â§ ‚Äñf‚Äñ‚Çä :=
  norm_le_pi_norm' _ i

@[to_additive pi_norm_const_le]
theorem pi_norm_const_le' (a : E) : ‚Äñfun _ : Œπ => a‚Äñ ‚â§ ‚Äña‚Äñ :=
  (pi_norm_le_iff_of_nonneg' <| norm_nonneg' _).2 fun _ => le_rfl

@[to_additive pi_nnnorm_const_le]
theorem pi_nnnorm_const_le' (a : E) : ‚Äñfun _ : Œπ => a‚Äñ‚Çä ‚â§ ‚Äña‚Äñ‚Çä :=
  pi_norm_const_le' _

@[to_additive (attr := simp) pi_norm_const]
theorem pi_norm_const' [Nonempty Œπ] (a : E) : ‚Äñfun _i : Œπ => a‚Äñ = ‚Äña‚Äñ := by
  simpa only [‚Üê dist_one_right] using dist_pi_const a 1

@[to_additive (attr := simp) pi_nnnorm_const]
theorem pi_nnnorm_const' [Nonempty Œπ] (a : E) : ‚Äñfun _i : Œπ => a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  NNReal.eq <| pi_norm_const' a

/-- The $L^1$ norm is less than the $L^\infty$ norm scaled by the cardinality. -/
@[to_additive Pi.sum_norm_apply_le_norm "The $L^1$ norm is less than the $L^\\infty$ norm scaled by
the cardinality."]
theorem Pi.sum_norm_apply_le_norm' : ‚àë i, ‚Äñf i‚Äñ ‚â§ Fintype.card Œπ ‚Ä¢ ‚Äñf‚Äñ :=
  Finset.sum_le_card_nsmul _ _ _ fun i _hi => norm_le_pi_norm' _ i

/-- The $L^1$ norm is less than the $L^\infty$ norm scaled by the cardinality. -/
@[to_additive Pi.sum_nnnorm_apply_le_nnnorm "The $L^1$ norm is less than the $L^\\infty$ norm
scaled by the cardinality."]
theorem Pi.sum_nnnorm_apply_le_nnnorm' : ‚àë i, ‚Äñf i‚Äñ‚Çä ‚â§ Fintype.card Œπ ‚Ä¢ ‚Äñf‚Äñ‚Çä :=
  NNReal.coe_sum.trans_le <| Pi.sum_norm_apply_le_norm' _

end SeminormedGroup

/-- Finite product of seminormed groups, using the sup norm. -/
@[to_additive "Finite product of seminormed groups, using the sup norm."]
instance Pi.seminormedCommGroup [‚àÄ i, SeminormedCommGroup (œÄ i)] : SeminormedCommGroup (‚àÄ i, œÄ i) :=
  { Pi.seminormedGroup with
    mul_comm := mul_comm }

/-- Finite product of normed groups, using the sup norm. -/
@[to_additive "Finite product of seminormed groups, using the sup norm."]
instance Pi.normedGroup [‚àÄ i, NormedGroup (œÄ i)] : NormedGroup (‚àÄ i, œÄ i) :=
  { Pi.seminormedGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Finite product of normed groups, using the sup norm. -/
@[to_additive "Finite product of seminormed groups, using the sup norm."]
instance Pi.normedCommGroup [‚àÄ i, NormedCommGroup (œÄ i)] : NormedCommGroup (‚àÄ i, œÄ i) :=
  { Pi.seminormedGroup with
    mul_comm := mul_comm
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

end Pi

/-! ### Multiplicative opposite -/


namespace MulOpposite

/-- The (additive) norm on the multiplicative opposite is the same as the norm on the original type.

Note that we do not provide this more generally as `Norm E·µê·µí·µñ`, as this is not always a good
choice of norm in the multiplicative `SeminormedGroup E` case.

We could repeat this instance to provide a `[SeminormedGroup E] : SeminormedGroup E·µÉ·µí·µñ` instance,
but that case would likely never be used.
-/
instance seminormedAddGroup [SeminormedAddGroup E] : SeminormedAddGroup E·µê·µí·µñ where
  norm x := ‚Äñx.unop‚Äñ
  dist_eq _ _ := dist_eq_norm _ _
  toPseudoMetricSpace := MulOpposite.instPseudoMetricSpaceMulOpposite

theorem norm_op [SeminormedAddGroup E] (a : E) : ‚ÄñMulOpposite.op a‚Äñ = ‚Äña‚Äñ :=
  rfl

theorem norm_unop [SeminormedAddGroup E] (a : E·µê·µí·µñ) : ‚ÄñMulOpposite.unop a‚Äñ = ‚Äña‚Äñ :=
  rfl

theorem nnnorm_op [SeminormedAddGroup E] (a : E) : ‚ÄñMulOpposite.op a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  rfl

theorem nnnorm_unop [SeminormedAddGroup E] (a : E·µê·µí·µñ) : ‚ÄñMulOpposite.unop a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä :=
  rfl

instance normedAddGroup [NormedAddGroup E] : NormedAddGroup E·µê·µí·µñ :=
  { MulOpposite.seminormedAddGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

instance seminormedAddCommGroup [SeminormedAddCommGroup E] : SeminormedAddCommGroup E·µê·µí·µñ where
  dist_eq _ _ := dist_eq_norm _ _

instance normedAddCommGroup [NormedAddCommGroup E] : NormedAddCommGroup E·µê·µí·µñ :=
  { MulOpposite.seminormedAddCommGroup with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

end MulOpposite

/-! ### Subgroups of normed groups -/


namespace Subgroup

section SeminormedGroup

variable [SeminormedGroup E] {s : Subgroup E}

/-- A subgroup of a seminormed group is also a seminormed group,
with the restriction of the norm. -/
@[to_additive "A subgroup of a seminormed group is also a seminormed group, with the restriction of
the norm."]
instance seminormedGroup : SeminormedGroup s :=
  SeminormedGroup.induced _ _ s.subtype

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`. -/
@[to_additive (attr := simp) "If `x` is an element of a subgroup `s` of a seminormed group `E`, its
norm in `s` is equal to its norm in `E`."]
theorem coe_norm (x : s) : ‚Äñx‚Äñ = ‚Äñ(x : E)‚Äñ :=
  rfl

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`.

This is a reversed version of the `simp` lemma `Subgroup.coe_norm` for use by `norm_cast`. -/
@[to_additive (attr := norm_cast) "If `x` is an element of a subgroup `s` of a seminormed group `E`,
its norm in `s` is equal to its norm in `E`.

This is a reversed version of the `simp` lemma `AddSubgroup.coe_norm` for use by `norm_cast`."]
theorem norm_coe {s : Subgroup E} (x : s) : ‚Äñ(x : E)‚Äñ = ‚Äñx‚Äñ :=
  rfl

end SeminormedGroup

@[to_additive]
instance seminormedCommGroup [SeminormedCommGroup E] {s : Subgroup E} : SeminormedCommGroup s :=
  SeminormedCommGroup.induced _ _ s.subtype

@[to_additive]
instance normedGroup [NormedGroup E] {s : Subgroup E} : NormedGroup s :=
  NormedGroup.induced _ _ s.subtype Subtype.coe_injective

@[to_additive]
instance normedCommGroup [NormedCommGroup E] {s : Subgroup E} : NormedCommGroup s :=
  NormedCommGroup.induced _ _ s.subtype Subtype.coe_injective

end Subgroup

/-! ### Submodules of normed groups -/


namespace Submodule

-- See note [implicit instance arguments]
/-- A submodule of a seminormed group is also a seminormed group, with the restriction of the norm.
-/
instance seminormedAddCommGroup [Ring ùïú] [SeminormedAddCommGroup E] [Module ùïú E]
    (s : Submodule ùïú E) : SeminormedAddCommGroup s :=
  SeminormedAddCommGroup.induced _ _ s.subtype.toAddMonoidHom

-- See note [implicit instance arguments].
/-- If `x` is an element of a submodule `s` of a normed group `E`, its norm in `s` is equal to its
norm in `E`. -/
@[simp]
theorem coe_norm [Ring ùïú] [SeminormedAddCommGroup E] [Module ùïú E] {s : Submodule ùïú E}
    (x : s) : ‚Äñx‚Äñ = ‚Äñ(x : E)‚Äñ :=
  rfl

-- See note [implicit instance arguments].
/-- If `x` is an element of a submodule `s` of a normed group `E`, its norm in `E` is equal to its
norm in `s`.

This is a reversed version of the `simp` lemma `Submodule.coe_norm` for use by `norm_cast`. -/
@[norm_cast]
theorem norm_coe [Ring ùïú] [SeminormedAddCommGroup E] [Module ùïú E] {s : Submodule ùïú E}
    (x : s) : ‚Äñ(x : E)‚Äñ = ‚Äñx‚Äñ :=
  rfl

-- See note [implicit instance arguments].
/-- A submodule of a normed group is also a normed group, with the restriction of the norm. -/
instance normedAddCommGroup [Ring ùïú] [NormedAddCommGroup E] [Module ùïú E]
    (s : Submodule ùïú E) : NormedAddCommGroup s :=
  { Submodule.seminormedAddCommGroup s with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

end Submodule
