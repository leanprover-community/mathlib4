/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl, YaÃ«l Dillies
-/
import Mathlib.Algebra.CharP.Defs
import Mathlib.Algebra.Group.Subgroup.Basic
import Mathlib.Analysis.Normed.Group.Seminorm
import Mathlib.Topology.Metrizable.Uniformity
import Mathlib.Topology.Sequences

/-!
# Normed (semi)groups

In this file we define 10 classes:

* `Norm`, `NNNorm`: auxiliary classes endowing a type `Î±` with a function `norm : Î± â†’ â„`
  (notation: `â€–xâ€–`) and `nnnorm : Î± â†’ â„â‰¥0` (notation: `â€–xâ€–â‚Š`), respectively;
* `Seminormed...Group`: A seminormed (additive) (commutative) group is an (additive) (commutative)
  group with a norm and a compatible pseudometric space structure:
  `âˆ€ x y, dist x y = â€–x / yâ€–` or `âˆ€ x y, dist x y = â€–x - yâ€–`, depending on the group operation.
* `Normed...Group`: A normed (additive) (commutative) group is an (additive) (commutative) group
  with a norm and a compatible metric space structure.

We also prove basic properties of (semi)normed groups and provide some instances.

## TODO
This file is huge; move material into separate files,
such as `Mathlib/Analysis/Normed/Group/Lemmas.lean`.

## Notes

The current convention `dist x y = â€–x - yâ€–` means that the distance is invariant under right
addition, but actions in mathlib are usually from the left. This means we might want to change it to
`dist x y = â€–-x + yâ€–`.

The normed group hierarchy would lend itself well to a mixin design (that is, having
`SeminormedGroup` and `SeminormedAddGroup` not extend `Group` and `AddGroup`), but we choose not
to for performance concerns.

## Tags

normed group
-/


variable {ğ“• ğ•œ Î± Î¹ Îº E F G : Type*}

open Filter Function Metric Bornology

open ENNReal Filter NNReal Uniformity Pointwise Topology

/-- Auxiliary class, endowing a type `E` with a function `norm : E â†’ â„` with notation `â€–xâ€–`. This
class is designed to be extended in more interesting classes specifying the properties of the norm.
-/
@[notation_class]
class Norm (E : Type*) where
  /-- the `â„`-valued norm function. -/
  norm : E â†’ â„

/-- Auxiliary class, endowing a type `Î±` with a function `nnnorm : Î± â†’ â„â‰¥0` with notation `â€–xâ€–â‚Š`. -/
@[notation_class]
class NNNorm (E : Type*) where
  /-- the `â„â‰¥0`-valued norm function. -/
  nnnorm : E â†’ â„â‰¥0

export Norm (norm)

export NNNorm (nnnorm)

@[inherit_doc]
notation "â€–" e "â€–" => norm e

@[inherit_doc]
notation "â€–" e "â€–â‚Š" => nnnorm e

/-- A seminormed group is an additive group endowed with a norm for which `dist x y = â€–x - yâ€–`
defines a pseudometric space structure. -/
class SeminormedAddGroup (E : Type*) extends Norm E, AddGroup E, PseudoMetricSpace E where
  dist := fun x y => â€–x - yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x - yâ€– := by aesop

/-- A seminormed group is a group endowed with a norm for which `dist x y = â€–x / yâ€–` defines a
pseudometric space structure. -/
@[to_additive]
class SeminormedGroup (E : Type*) extends Norm E, Group E, PseudoMetricSpace E where
  dist := fun x y => â€–x / yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x / yâ€– := by aesop

/-- A normed group is an additive group endowed with a norm for which `dist x y = â€–x - yâ€–` defines a
metric space structure. -/
class NormedAddGroup (E : Type*) extends Norm E, AddGroup E, MetricSpace E where
  dist := fun x y => â€–x - yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x - yâ€– := by aesop

/-- A normed group is a group endowed with a norm for which `dist x y = â€–x / yâ€–` defines a metric
space structure. -/
@[to_additive]
class NormedGroup (E : Type*) extends Norm E, Group E, MetricSpace E where
  dist := fun x y => â€–x / yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x / yâ€– := by aesop

/-- A seminormed group is an additive group endowed with a norm for which `dist x y = â€–x - yâ€–`
defines a pseudometric space structure. -/
class SeminormedAddCommGroup (E : Type*) extends Norm E, AddCommGroup E,
  PseudoMetricSpace E where
  dist := fun x y => â€–x - yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x - yâ€– := by aesop

/-- A seminormed group is a group endowed with a norm for which `dist x y = â€–x / yâ€–`
defines a pseudometric space structure. -/
@[to_additive]
class SeminormedCommGroup (E : Type*) extends Norm E, CommGroup E, PseudoMetricSpace E where
  dist := fun x y => â€–x / yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x / yâ€– := by aesop

/-- A normed group is an additive group endowed with a norm for which `dist x y = â€–x - yâ€–` defines a
metric space structure. -/
class NormedAddCommGroup (E : Type*) extends Norm E, AddCommGroup E, MetricSpace E where
  dist := fun x y => â€–x - yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x - yâ€– := by aesop

/-- A normed group is a group endowed with a norm for which `dist x y = â€–x / yâ€–` defines a metric
space structure. -/
@[to_additive]
class NormedCommGroup (E : Type*) extends Norm E, CommGroup E, MetricSpace E where
  dist := fun x y => â€–x / yâ€–
  /-- The distance function is induced by the norm. -/
  dist_eq : âˆ€ x y, dist x y = â€–x / yâ€– := by aesop

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedGroup.toSeminormedGroup [NormedGroup E] : SeminormedGroup E :=
  { â€¹NormedGroup Eâ€º with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedCommGroup.toSeminormedCommGroup [NormedCommGroup E] :
    SeminormedCommGroup E :=
  { â€¹NormedCommGroup Eâ€º with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.toSeminormedGroup [SeminormedCommGroup E] :
    SeminormedGroup E :=
  { â€¹SeminormedCommGroup Eâ€º with }

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) NormedCommGroup.toNormedGroup [NormedCommGroup E] : NormedGroup E :=
  { â€¹NormedCommGroup Eâ€º with }

-- See note [reducible non-instances]
/-- Construct a `NormedGroup` from a `SeminormedGroup` satisfying `âˆ€ x, â€–xâ€– = 0 â†’ x = 1`. This
avoids having to go back to the `(Pseudo)MetricSpace` level when declaring a `NormedGroup`
instance as a special case of a more general `SeminormedGroup` instance. -/
@[to_additive (attr := reducible) "Construct a `NormedAddGroup` from a `SeminormedAddGroup`
satisfying `âˆ€ x, â€–xâ€– = 0 â†’ x = 0`. This avoids having to go back to the `(Pseudo)MetricSpace`
level when declaring a `NormedAddGroup` instance as a special case of a more general
`SeminormedAddGroup` instance."]
def NormedGroup.ofSeparation [SeminormedGroup E] (h : âˆ€ x : E, â€–xâ€– = 0 â†’ x = 1) :
    NormedGroup E where
  dist_eq := â€¹SeminormedGroup Eâ€º.dist_eq
  toMetricSpace :=
    { eq_of_dist_eq_zero := fun hxy =>
        div_eq_one.1 <| h _ <| by exact (â€¹SeminormedGroup Eâ€º.dist_eq _ _).symm.trans hxy }
      -- Porting note: the `rwa` no longer worked, but it was easy enough to provide the term.
      -- however, notice that if you make `x` and `y` accessible, then the following does work:
      -- `have := â€¹SeminormedGroup Eâ€º.dist_eq x y; rwa [â† this]`, so I'm not sure why the `rwa`
      -- was broken.

-- See note [reducible non-instances]
/-- Construct a `NormedCommGroup` from a `SeminormedCommGroup` satisfying
`âˆ€ x, â€–xâ€– = 0 â†’ x = 1`. This avoids having to go back to the `(Pseudo)MetricSpace` level when
declaring a `NormedCommGroup` instance as a special case of a more general `SeminormedCommGroup`
instance. -/
@[to_additive (attr := reducible) "Construct a `NormedAddCommGroup` from a
`SeminormedAddCommGroup` satisfying `âˆ€ x, â€–xâ€– = 0 â†’ x = 0`. This avoids having to go back to the
`(Pseudo)MetricSpace` level when declaring a `NormedAddCommGroup` instance as a special case
of a more general `SeminormedAddCommGroup` instance."]
def NormedCommGroup.ofSeparation [SeminormedCommGroup E] (h : âˆ€ x : E, â€–xâ€– = 0 â†’ x = 1) :
    NormedCommGroup E :=
  { â€¹SeminormedCommGroup Eâ€º, NormedGroup.ofSeparation h with }

-- See note [reducible non-instances]
/-- Construct a seminormed group from a multiplication-invariant distance. -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a translation-invariant distance."]
def SeminormedGroup.ofMulDist [Norm E] [Group E] [PseudoMetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist x y â‰¤ dist (x * z) (y * z)) :
    SeminormedGroup E where
  dist_eq x y := by
    rw [hâ‚]; apply le_antisymm
    Â· simpa only [div_eq_mul_inv, â† mul_right_inv y] using hâ‚‚ _ _ _
    Â· simpa only [div_mul_cancel, one_mul] using hâ‚‚ (x / y) 1 y

-- See note [reducible non-instances]
/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedGroup.ofMulDist' [Norm E] [Group E] [PseudoMetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist (x * z) (y * z) â‰¤ dist x y) :
    SeminormedGroup E where
  dist_eq x y := by
    rw [hâ‚]; apply le_antisymm
    Â· simpa only [div_mul_cancel, one_mul] using hâ‚‚ (x / y) 1 y
    Â· simpa only [div_eq_mul_inv, â† mul_right_inv y] using hâ‚‚ _ _ _

-- See note [reducible non-instances]
/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedCommGroup.ofMulDist [Norm E] [CommGroup E] [PseudoMetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist x y â‰¤ dist (x * z) (y * z)) :
    SeminormedCommGroup E :=
  { SeminormedGroup.ofMulDist hâ‚ hâ‚‚ with
    mul_comm := mul_comm }

-- See note [reducible non-instances]
/-- Construct a seminormed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a translation-invariant pseudodistance."]
def SeminormedCommGroup.ofMulDist' [Norm E] [CommGroup E] [PseudoMetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist (x * z) (y * z) â‰¤ dist x y) :
    SeminormedCommGroup E :=
  { SeminormedGroup.ofMulDist' hâ‚ hâ‚‚ with
    mul_comm := mul_comm }

-- See note [reducible non-instances]
/-- Construct a normed group from a multiplication-invariant distance. -/
@[to_additive (attr := reducible)
  "Construct a normed group from a translation-invariant distance."]
def NormedGroup.ofMulDist [Norm E] [Group E] [MetricSpace E] (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1)
    (hâ‚‚ : âˆ€ x y z : E, dist x y â‰¤ dist (x * z) (y * z)) : NormedGroup E :=
  { SeminormedGroup.ofMulDist hâ‚ hâ‚‚ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

-- See note [reducible non-instances]
/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
  "Construct a normed group from a translation-invariant pseudodistance."]
def NormedGroup.ofMulDist' [Norm E] [Group E] [MetricSpace E] (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1)
    (hâ‚‚ : âˆ€ x y z : E, dist (x * z) (y * z) â‰¤ dist x y) : NormedGroup E :=
  { SeminormedGroup.ofMulDist' hâ‚ hâ‚‚ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

-- See note [reducible non-instances]
/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
"Construct a normed group from a translation-invariant pseudodistance."]
def NormedCommGroup.ofMulDist [Norm E] [CommGroup E] [MetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist x y â‰¤ dist (x * z) (y * z)) :
    NormedCommGroup E :=
  { NormedGroup.ofMulDist hâ‚ hâ‚‚ with
    mul_comm := mul_comm }

-- See note [reducible non-instances]
/-- Construct a normed group from a multiplication-invariant pseudodistance. -/
@[to_additive (attr := reducible)
  "Construct a normed group from a translation-invariant pseudodistance."]
def NormedCommGroup.ofMulDist' [Norm E] [CommGroup E] [MetricSpace E]
    (hâ‚ : âˆ€ x : E, â€–xâ€– = dist x 1) (hâ‚‚ : âˆ€ x y z : E, dist (x * z) (y * z) â‰¤ dist x y) :
    NormedCommGroup E :=
  { NormedGroup.ofMulDist' hâ‚ hâ‚‚ with
    mul_comm := mul_comm }

-- See note [reducible non-instances]
/-- Construct a seminormed group from a seminorm, i.e., registering the pseudodistance and the
pseudometric space structure from the seminorm properties. Note that in most cases this instance
creates bad definitional equalities (e.g., it does not take into account a possibly existing
`UniformSpace` instance on `E`). -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a seminorm, i.e., registering the pseudodistance
and the pseudometric space structure from the seminorm properties. Note that in most cases this
instance creates bad definitional equalities (e.g., it does not take into account a possibly
existing `UniformSpace` instance on `E`)."]
def GroupSeminorm.toSeminormedGroup [Group E] (f : GroupSeminorm E) : SeminormedGroup E where
  dist x y := f (x / y)
  norm := f
  dist_eq x y := rfl
  dist_self x := by simp only [div_self', map_one_eq_zero]
  dist_triangle := le_map_div_add_map_div f
  dist_comm := map_div_rev f
  edist_dist x y := by exact ENNReal.coe_nnreal_eq _
  -- Porting note: how did `mathlib3` solve this automatically?

-- See note [reducible non-instances]
/-- Construct a seminormed group from a seminorm, i.e., registering the pseudodistance and the
pseudometric space structure from the seminorm properties. Note that in most cases this instance
creates bad definitional equalities (e.g., it does not take into account a possibly existing
`UniformSpace` instance on `E`). -/
@[to_additive (attr := reducible)
  "Construct a seminormed group from a seminorm, i.e., registering the pseudodistance
and the pseudometric space structure from the seminorm properties. Note that in most cases this
instance creates bad definitional equalities (e.g., it does not take into account a possibly
existing `UniformSpace` instance on `E`)."]
def GroupSeminorm.toSeminormedCommGroup [CommGroup E] (f : GroupSeminorm E) :
    SeminormedCommGroup E :=
  { f.toSeminormedGroup with
    mul_comm := mul_comm }

-- See note [reducible non-instances]
/-- Construct a normed group from a norm, i.e., registering the distance and the metric space
structure from the norm properties. Note that in most cases this instance creates bad definitional
equalities (e.g., it does not take into account a possibly existing `UniformSpace` instance on
`E`). -/
@[to_additive (attr := reducible)
  "Construct a normed group from a norm, i.e., registering the distance and the metric
space structure from the norm properties. Note that in most cases this instance creates bad
definitional equalities (e.g., it does not take into account a possibly existing `UniformSpace`
instance on `E`)."]
def GroupNorm.toNormedGroup [Group E] (f : GroupNorm E) : NormedGroup E :=
  { f.toGroupSeminorm.toSeminormedGroup with
    eq_of_dist_eq_zero := fun h => div_eq_one.1 <| eq_one_of_map_eq_zero f h }

-- See note [reducible non-instances]
/-- Construct a normed group from a norm, i.e., registering the distance and the metric space
structure from the norm properties. Note that in most cases this instance creates bad definitional
equalities (e.g., it does not take into account a possibly existing `UniformSpace` instance on
`E`). -/
@[to_additive (attr := reducible)
  "Construct a normed group from a norm, i.e., registering the distance and the metric
space structure from the norm properties. Note that in most cases this instance creates bad
definitional equalities (e.g., it does not take into account a possibly existing `UniformSpace`
instance on `E`)."]
def GroupNorm.toNormedCommGroup [CommGroup E] (f : GroupNorm E) : NormedCommGroup E :=
  { f.toNormedGroup with
    mul_comm := mul_comm }

section SeminormedGroup

variable [SeminormedGroup E] [SeminormedGroup F] [SeminormedGroup G] {s : Set E}
  {a aâ‚ aâ‚‚ b bâ‚ bâ‚‚ : E} {r râ‚ râ‚‚ : â„}

@[to_additive]
theorem dist_eq_norm_div (a b : E) : dist a b = â€–a / bâ€– :=
  SeminormedGroup.dist_eq _ _

@[to_additive]
theorem dist_eq_norm_div' (a b : E) : dist a b = â€–b / aâ€– := by rw [dist_comm, dist_eq_norm_div]

alias dist_eq_norm := dist_eq_norm_sub

alias dist_eq_norm' := dist_eq_norm_sub'

@[to_additive of_forall_le_norm]
lemma DiscreteTopology.of_forall_le_norm' (hpos : 0 < r) (hr : âˆ€ x : E, x â‰  1 â†’ r â‰¤ â€–xâ€–) :
    DiscreteTopology E :=
  .of_forall_le_dist hpos fun x y hne â†¦ by
    simp only [dist_eq_norm_div]
    exact hr _ (div_ne_one.2 hne)

@[to_additive (attr := simp)]
theorem dist_one_right (a : E) : dist a 1 = â€–aâ€– := by rw [dist_eq_norm_div, div_one]

@[to_additive]
theorem inseparable_one_iff_norm {a : E} : Inseparable a 1 â†” â€–aâ€– = 0 := by
  rw [Metric.inseparable_iff, dist_one_right]

@[to_additive (attr := simp)]
theorem dist_one_left : dist (1 : E) = norm :=
  funext fun a => by rw [dist_comm, dist_one_right]

@[to_additive]
theorem norm_div_rev (a b : E) : â€–a / bâ€– = â€–b / aâ€– := by
  simpa only [dist_eq_norm_div] using dist_comm a b

@[to_additive (attr := simp) norm_neg]
theorem norm_inv' (a : E) : â€–aâ»Â¹â€– = â€–aâ€– := by simpa using norm_div_rev 1 a

open scoped symmDiff in
@[to_additive]
theorem dist_mulIndicator (s t : Set Î±) (f : Î± â†’ E) (x : Î±) :
    dist (s.mulIndicator f x) (t.mulIndicator f x) = â€–(s âˆ† t).mulIndicator f xâ€– := by
  rw [dist_eq_norm_div, Set.apply_mulIndicator_symmDiff norm_inv']

/-- **Triangle inequality** for the norm. -/
@[to_additive norm_add_le "**Triangle inequality** for the norm."]
theorem norm_mul_le' (a b : E) : â€–a * bâ€– â‰¤ â€–aâ€– + â€–bâ€– := by
  simpa [dist_eq_norm_div] using dist_triangle a 1 bâ»Â¹

@[to_additive]
theorem norm_mul_le_of_le (hâ‚ : â€–aâ‚â€– â‰¤ râ‚) (hâ‚‚ : â€–aâ‚‚â€– â‰¤ râ‚‚) : â€–aâ‚ * aâ‚‚â€– â‰¤ râ‚ + râ‚‚ :=
  (norm_mul_le' aâ‚ aâ‚‚).trans <| add_le_add hâ‚ hâ‚‚

@[to_additive norm_addâ‚ƒ_le]
theorem norm_mulâ‚ƒ_le (a b c : E) : â€–a * b * câ€– â‰¤ â€–aâ€– + â€–bâ€– + â€–câ€– :=
  norm_mul_le_of_le (norm_mul_le' _ _) le_rfl

@[to_additive]
lemma norm_div_le_norm_div_add_norm_div (a b c : E) : â€–a / câ€– â‰¤ â€–a / bâ€– + â€–b / câ€– := by
  simpa only [dist_eq_norm_div] using dist_triangle a b c

@[to_additive (attr := simp) norm_nonneg]
theorem norm_nonneg' (a : E) : 0 â‰¤ â€–aâ€– := by
  rw [â† dist_one_right]
  exact dist_nonneg

@[to_additive (attr := simp) abs_norm]
theorem abs_norm' (z : E) : |â€–zâ€–| = â€–zâ€– := abs_of_nonneg <| norm_nonneg' _

namespace Mathlib.Meta.Positivity

open Lean Meta Qq Function

/-- Extension for the `positivity` tactic: multiplicative norms are nonnegative, via
`norm_nonneg'`. -/
@[positivity Norm.norm _]
def evalMulNorm : PositivityExt where eval {u Î±} _zÎ± _pÎ± e := do
  match u, Î±, e with
  | 0, ~q(â„), ~q(@Norm.norm $Î² $instDist $a) =>
    let _inst â† synthInstanceQ q(SeminormedGroup $Î²)
    assertInstancesCommute
    pure (.nonnegative q(norm_nonneg' $a))
  | _, _, _ => throwError "not â€– Â· â€–"

/-- Extension for the `positivity` tactic: additive norms are nonnegative, via `norm_nonneg`. -/
@[positivity Norm.norm _]
def evalAddNorm : PositivityExt where eval {u Î±} _zÎ± _pÎ± e := do
  match u, Î±, e with
  | 0, ~q(â„), ~q(@Norm.norm $Î² $instDist $a) =>
    let _inst â† synthInstanceQ q(SeminormedAddGroup $Î²)
    assertInstancesCommute
    pure (.nonnegative q(norm_nonneg $a))
  | _, _, _ => throwError "not â€– Â· â€–"

end Mathlib.Meta.Positivity

@[to_additive (attr := simp) norm_zero]
theorem norm_one' : â€–(1 : E)â€– = 0 := by rw [â† dist_one_right, dist_self]

@[to_additive]
theorem ne_one_of_norm_ne_zero : â€–aâ€– â‰  0 â†’ a â‰  1 :=
  mt <| by
    rintro rfl
    exact norm_one'

@[to_additive (attr := nontriviality) norm_of_subsingleton]
theorem norm_of_subsingleton' [Subsingleton E] (a : E) : â€–aâ€– = 0 := by
  rw [Subsingleton.elim a 1, norm_one']

@[to_additive zero_lt_one_add_norm_sq]
theorem zero_lt_one_add_norm_sq' (x : E) : 0 < 1 + â€–xâ€– ^ 2 := by
  positivity

@[to_additive]
theorem norm_div_le (a b : E) : â€–a / bâ€– â‰¤ â€–aâ€– + â€–bâ€– := by
  simpa [dist_eq_norm_div] using dist_triangle a 1 b

@[to_additive]
theorem norm_div_le_of_le {râ‚ râ‚‚ : â„} (Hâ‚ : â€–aâ‚â€– â‰¤ râ‚) (Hâ‚‚ : â€–aâ‚‚â€– â‰¤ râ‚‚) : â€–aâ‚ / aâ‚‚â€– â‰¤ râ‚ + râ‚‚ :=
  (norm_div_le aâ‚ aâ‚‚).trans <| add_le_add Hâ‚ Hâ‚‚

@[to_additive dist_le_norm_add_norm]
theorem dist_le_norm_add_norm' (a b : E) : dist a b â‰¤ â€–aâ€– + â€–bâ€– := by
  rw [dist_eq_norm_div]
  apply norm_div_le

@[to_additive abs_norm_sub_norm_le]
theorem abs_norm_sub_norm_le' (a b : E) : |â€–aâ€– - â€–bâ€–| â‰¤ â€–a / bâ€– := by
  simpa [dist_eq_norm_div] using abs_dist_sub_le a b 1

@[to_additive norm_sub_norm_le]
theorem norm_sub_norm_le' (a b : E) : â€–aâ€– - â€–bâ€– â‰¤ â€–a / bâ€– :=
  (le_abs_self _).trans (abs_norm_sub_norm_le' a b)

@[to_additive dist_norm_norm_le]
theorem dist_norm_norm_le' (a b : E) : dist â€–aâ€– â€–bâ€– â‰¤ â€–a / bâ€– :=
  abs_norm_sub_norm_le' a b

@[to_additive]
theorem norm_le_norm_add_norm_div' (u v : E) : â€–uâ€– â‰¤ â€–vâ€– + â€–u / vâ€– := by
  rw [add_comm]
  refine (norm_mul_le' _ _).trans_eq' ?_
  rw [div_mul_cancel]

@[to_additive]
theorem norm_le_norm_add_norm_div (u v : E) : â€–vâ€– â‰¤ â€–uâ€– + â€–u / vâ€– := by
  rw [norm_div_rev]
  exact norm_le_norm_add_norm_div' v u

alias norm_le_insert' := norm_le_norm_add_norm_sub'

alias norm_le_insert := norm_le_norm_add_norm_sub

@[to_additive]
theorem norm_le_mul_norm_add (u v : E) : â€–uâ€– â‰¤ â€–u * vâ€– + â€–vâ€– :=
  calc
    â€–uâ€– = â€–u * v / vâ€– := by rw [mul_div_cancel_right]
    _ â‰¤ â€–u * vâ€– + â€–vâ€– := norm_div_le _ _

@[to_additive ball_eq]
theorem ball_eq' (y : E) (Îµ : â„) : ball y Îµ = { x | â€–x / yâ€– < Îµ } :=
  Set.ext fun a => by simp [dist_eq_norm_div]

@[to_additive]
theorem ball_one_eq (r : â„) : ball (1 : E) r = { x | â€–xâ€– < r } :=
  Set.ext fun a => by simp

@[to_additive mem_ball_iff_norm]
theorem mem_ball_iff_norm'' : b âˆˆ ball a r â†” â€–b / aâ€– < r := by rw [mem_ball, dist_eq_norm_div]

@[to_additive mem_ball_iff_norm']
theorem mem_ball_iff_norm''' : b âˆˆ ball a r â†” â€–a / bâ€– < r := by rw [mem_ball', dist_eq_norm_div]

@[to_additive] -- Porting note (#10618): `simp` can prove it
theorem mem_ball_one_iff : a âˆˆ ball (1 : E) r â†” â€–aâ€– < r := by rw [mem_ball, dist_one_right]

@[to_additive mem_closedBall_iff_norm]
theorem mem_closedBall_iff_norm'' : b âˆˆ closedBall a r â†” â€–b / aâ€– â‰¤ r := by
  rw [mem_closedBall, dist_eq_norm_div]

@[to_additive] -- Porting note (#10618): `simp` can prove it
theorem mem_closedBall_one_iff : a âˆˆ closedBall (1 : E) r â†” â€–aâ€– â‰¤ r := by
  rw [mem_closedBall, dist_one_right]

@[to_additive mem_closedBall_iff_norm']
theorem mem_closedBall_iff_norm''' : b âˆˆ closedBall a r â†” â€–a / bâ€– â‰¤ r := by
  rw [mem_closedBall', dist_eq_norm_div]

@[to_additive norm_le_of_mem_closedBall]
theorem norm_le_of_mem_closedBall' (h : b âˆˆ closedBall a r) : â€–bâ€– â‰¤ â€–aâ€– + r :=
  (norm_le_norm_add_norm_div' _ _).trans <| add_le_add_left (by rwa [â† dist_eq_norm_div]) _

@[to_additive norm_le_norm_add_const_of_dist_le]
theorem norm_le_norm_add_const_of_dist_le' : dist a b â‰¤ r â†’ â€–aâ€– â‰¤ â€–bâ€– + r :=
  norm_le_of_mem_closedBall'

@[to_additive norm_lt_of_mem_ball]
theorem norm_lt_of_mem_ball' (h : b âˆˆ ball a r) : â€–bâ€– < â€–aâ€– + r :=
  (norm_le_norm_add_norm_div' _ _).trans_lt <| add_lt_add_left (by rwa [â† dist_eq_norm_div]) _

@[to_additive]
theorem norm_div_sub_norm_div_le_norm_div (u v w : E) : â€–u / wâ€– - â€–v / wâ€– â‰¤ â€–u / vâ€– := by
  simpa only [div_div_div_cancel_right'] using norm_sub_norm_le' (u / w) (v / w)

@[to_additive (attr := simp 1001) mem_sphere_iff_norm]
-- Porting note: increase priority so the left-hand side doesn't reduce
theorem mem_sphere_iff_norm' : b âˆˆ sphere a r â†” â€–b / aâ€– = r := by simp [dist_eq_norm_div]

@[to_additive] -- `simp` can prove this
theorem mem_sphere_one_iff_norm : a âˆˆ sphere (1 : E) r â†” â€–aâ€– = r := by simp [dist_eq_norm_div]

@[to_additive (attr := simp) norm_eq_of_mem_sphere]
theorem norm_eq_of_mem_sphere' (x : sphere (1 : E) r) : â€–(x : E)â€– = r :=
  mem_sphere_one_iff_norm.mp x.2

@[to_additive]
theorem ne_one_of_mem_sphere (hr : r â‰  0) (x : sphere (1 : E) r) : (x : E) â‰  1 :=
  ne_one_of_norm_ne_zero <| by rwa [norm_eq_of_mem_sphere' x]

@[to_additive ne_zero_of_mem_unit_sphere]
theorem ne_one_of_mem_unit_sphere (x : sphere (1 : E) 1) : (x : E) â‰  1 :=
  ne_one_of_mem_sphere one_ne_zero _

variable (E)

/-- The norm of a seminormed group as a group seminorm. -/
@[to_additive "The norm of a seminormed group as an additive group seminorm."]
def normGroupSeminorm : GroupSeminorm E :=
  âŸ¨norm, norm_one', norm_mul_le', norm_inv'âŸ©

@[to_additive (attr := simp)]
theorem coe_normGroupSeminorm : â‡‘(normGroupSeminorm E) = norm :=
  rfl

variable {E}

@[to_additive]
theorem NormedCommGroup.tendsto_nhds_one {f : Î± â†’ E} {l : Filter Î±} :
    Tendsto f l (ğ“ 1) â†” âˆ€ Îµ > 0, âˆ€á¶  x in l, â€–f xâ€– < Îµ :=
  Metric.tendsto_nhds.trans <| by simp only [dist_one_right]

@[to_additive]
theorem NormedCommGroup.tendsto_nhds_nhds {f : E â†’ F} {x : E} {y : F} :
    Tendsto f (ğ“ x) (ğ“ y) â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ x', â€–x' / xâ€– < Î´ â†’ â€–f x' / yâ€– < Îµ := by
  simp_rw [Metric.tendsto_nhds_nhds, dist_eq_norm_div]

@[to_additive]
theorem NormedCommGroup.nhds_basis_norm_lt (x : E) :
    (ğ“ x).HasBasis (fun Îµ : â„ => 0 < Îµ) fun Îµ => { y | â€–y / xâ€– < Îµ } := by
  simp_rw [â† ball_eq']
  exact Metric.nhds_basis_ball

@[to_additive]
theorem NormedCommGroup.nhds_one_basis_norm_lt :
    (ğ“ (1 : E)).HasBasis (fun Îµ : â„ => 0 < Îµ) fun Îµ => { y | â€–yâ€– < Îµ } := by
  convert NormedCommGroup.nhds_basis_norm_lt (1 : E)
  simp

@[to_additive]
theorem NormedCommGroup.uniformity_basis_dist :
    (ğ“¤ E).HasBasis (fun Îµ : â„ => 0 < Îµ) fun Îµ => { p : E Ã— E | â€–p.fst / p.sndâ€– < Îµ } := by
  convert Metric.uniformity_basis_dist (Î± := E) using 1
  simp [dist_eq_norm_div]

open Finset

variable [FunLike ğ“• E F]

section NNNorm

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedGroup.toNNNorm : NNNorm E :=
  âŸ¨fun a => âŸ¨â€–aâ€–, norm_nonneg' aâŸ©âŸ©

@[to_additive (attr := simp, norm_cast) coe_nnnorm]
theorem coe_nnnorm' (a : E) : (â€–aâ€–â‚Š : â„) = â€–aâ€– :=
  rfl

@[to_additive (attr := simp) coe_comp_nnnorm]
theorem coe_comp_nnnorm' : (toReal : â„â‰¥0 â†’ â„) âˆ˜ (nnnorm : E â†’ â„â‰¥0) = norm :=
  rfl

@[to_additive norm_toNNReal]
theorem norm_toNNReal' : â€–aâ€–.toNNReal = â€–aâ€–â‚Š :=
  @Real.toNNReal_coe â€–aâ€–â‚Š

@[to_additive]
theorem nndist_eq_nnnorm_div (a b : E) : nndist a b = â€–a / bâ€–â‚Š :=
  NNReal.eq <| dist_eq_norm_div _ _

alias nndist_eq_nnnorm := nndist_eq_nnnorm_sub

@[to_additive (attr := simp) nnnorm_zero]
theorem nnnorm_one' : â€–(1 : E)â€–â‚Š = 0 :=
  NNReal.eq norm_one'

@[to_additive]
theorem ne_one_of_nnnorm_ne_zero {a : E} : â€–aâ€–â‚Š â‰  0 â†’ a â‰  1 :=
  mt <| by
    rintro rfl
    exact nnnorm_one'

@[to_additive nnnorm_add_le]
theorem nnnorm_mul_le' (a b : E) : â€–a * bâ€–â‚Š â‰¤ â€–aâ€–â‚Š + â€–bâ€–â‚Š :=
  NNReal.coe_le_coe.1 <| norm_mul_le' a b

@[to_additive (attr := simp) nnnorm_neg]
theorem nnnorm_inv' (a : E) : â€–aâ»Â¹â€–â‚Š = â€–aâ€–â‚Š :=
  NNReal.eq <| norm_inv' a

open scoped symmDiff in
@[to_additive]
theorem nndist_mulIndicator (s t : Set Î±) (f : Î± â†’ E) (x : Î±) :
    nndist (s.mulIndicator f x) (t.mulIndicator f x) = â€–(s âˆ† t).mulIndicator f xâ€–â‚Š :=
  NNReal.eq <| dist_mulIndicator s t f x

@[to_additive]
theorem nnnorm_div_le (a b : E) : â€–a / bâ€–â‚Š â‰¤ â€–aâ€–â‚Š + â€–bâ€–â‚Š :=
  NNReal.coe_le_coe.1 <| norm_div_le _ _

@[to_additive nndist_nnnorm_nnnorm_le]
theorem nndist_nnnorm_nnnorm_le' (a b : E) : nndist â€–aâ€–â‚Š â€–bâ€–â‚Š â‰¤ â€–a / bâ€–â‚Š :=
  NNReal.coe_le_coe.1 <| dist_norm_norm_le' a b

@[to_additive]
theorem nnnorm_le_nnnorm_add_nnnorm_div (a b : E) : â€–bâ€–â‚Š â‰¤ â€–aâ€–â‚Š + â€–a / bâ€–â‚Š :=
  norm_le_norm_add_norm_div _ _

@[to_additive]
theorem nnnorm_le_nnnorm_add_nnnorm_div' (a b : E) : â€–aâ€–â‚Š â‰¤ â€–bâ€–â‚Š + â€–a / bâ€–â‚Š :=
  norm_le_norm_add_norm_div' _ _

alias nnnorm_le_insert' := nnnorm_le_nnnorm_add_nnnorm_sub'

alias nnnorm_le_insert := nnnorm_le_nnnorm_add_nnnorm_sub

@[to_additive]
theorem nnnorm_le_mul_nnnorm_add (a b : E) : â€–aâ€–â‚Š â‰¤ â€–a * bâ€–â‚Š + â€–bâ€–â‚Š :=
  norm_le_mul_norm_add _ _

@[to_additive ofReal_norm_eq_coe_nnnorm]
theorem ofReal_norm_eq_coe_nnnorm' (a : E) : ENNReal.ofReal â€–aâ€– = â€–aâ€–â‚Š :=
  ENNReal.ofReal_eq_coe_nnreal _

/-- The non negative norm seen as an `ENNReal` and then as a `Real` is equal to the norm. -/
@[to_additive toReal_coe_nnnorm "The non negative norm seen as an `ENNReal` and
then as a `Real` is equal to the norm."]
theorem toReal_coe_nnnorm' (a : E) : (â€–aâ€–â‚Š : â„â‰¥0âˆ).toReal = â€–aâ€– := rfl

@[to_additive]
theorem edist_eq_coe_nnnorm_div (a b : E) : edist a b = â€–a / bâ€–â‚Š := by
  rw [edist_dist, dist_eq_norm_div, ofReal_norm_eq_coe_nnnorm']

@[to_additive edist_eq_coe_nnnorm]
theorem edist_eq_coe_nnnorm' (x : E) : edist x 1 = (â€–xâ€–â‚Š : â„â‰¥0âˆ) := by
  rw [edist_eq_coe_nnnorm_div, div_one]

open scoped symmDiff in
@[to_additive]
theorem edist_mulIndicator (s t : Set Î±) (f : Î± â†’ E) (x : Î±) :
    edist (s.mulIndicator f x) (t.mulIndicator f x) = â€–(s âˆ† t).mulIndicator f xâ€–â‚Š := by
  rw [edist_nndist, nndist_mulIndicator]

@[to_additive]
theorem mem_emetric_ball_one_iff {r : â„â‰¥0âˆ} : a âˆˆ EMetric.ball (1 : E) r â†” â†‘â€–aâ€–â‚Š < r := by
  rw [EMetric.mem_ball, edist_eq_coe_nnnorm']

end NNNorm

@[to_additive]
theorem tendsto_iff_norm_div_tendsto_zero {f : Î± â†’ E} {a : Filter Î±} {b : E} :
    Tendsto f a (ğ“ b) â†” Tendsto (fun e => â€–f e / bâ€–) a (ğ“ 0) := by
  simp only [â† dist_eq_norm_div, â† tendsto_iff_dist_tendsto_zero]

@[to_additive]
theorem tendsto_one_iff_norm_tendsto_zero {f : Î± â†’ E} {a : Filter Î±} :
    Tendsto f a (ğ“ 1) â†” Tendsto (â€–f Â·â€–) a (ğ“ 0) :=
  tendsto_iff_norm_div_tendsto_zero.trans <| by simp only [div_one]

@[to_additive]
theorem comap_norm_nhds_one : comap norm (ğ“ 0) = ğ“ (1 : E) := by
  simpa only [dist_one_right] using nhds_comap_dist (1 : E)

/-- Special case of the sandwich theorem: if the norm of `f` is eventually bounded by a real
function `a` which tends to `0`, then `f` tends to `1` (neutral element of `SeminormedGroup`).
In this pair of lemmas (`squeeze_one_norm'` and `squeeze_one_norm`), following a convention of
similar lemmas in `Topology.MetricSpace.Basic` and `Topology.Algebra.Order`, the `'` version is
phrased using "eventually" and the non-`'` version is phrased absolutely. -/
@[to_additive "Special case of the sandwich theorem: if the norm of `f` is eventually bounded by a
real function `a` which tends to `0`, then `f` tends to `0`. In this pair of lemmas
(`squeeze_zero_norm'` and `squeeze_zero_norm`), following a convention of similar lemmas in
`Topology.MetricSpace.Pseudo.Defs` and `Topology.Algebra.Order`, the `'` version is phrased using
\"eventually\" and the non-`'` version is phrased absolutely."]
theorem squeeze_one_norm' {f : Î± â†’ E} {a : Î± â†’ â„} {tâ‚€ : Filter Î±} (h : âˆ€á¶  n in tâ‚€, â€–f nâ€– â‰¤ a n)
    (h' : Tendsto a tâ‚€ (ğ“ 0)) : Tendsto f tâ‚€ (ğ“ 1) :=
  tendsto_one_iff_norm_tendsto_zero.2 <|
    squeeze_zero' (eventually_of_forall fun _n => norm_nonneg' _) h h'

/-- Special case of the sandwich theorem: if the norm of `f` is bounded by a real function `a` which
tends to `0`, then `f` tends to `1`. -/
@[to_additive "Special case of the sandwich theorem: if the norm of `f` is bounded by a real
function `a` which tends to `0`, then `f` tends to `0`."]
theorem squeeze_one_norm {f : Î± â†’ E} {a : Î± â†’ â„} {tâ‚€ : Filter Î±} (h : âˆ€ n, â€–f nâ€– â‰¤ a n) :
    Tendsto a tâ‚€ (ğ“ 0) â†’ Tendsto f tâ‚€ (ğ“ 1) :=
  squeeze_one_norm' <| eventually_of_forall h

@[to_additive]
theorem tendsto_norm_div_self (x : E) : Tendsto (fun a => â€–a / xâ€–) (ğ“ x) (ğ“ 0) := by
  simpa [dist_eq_norm_div] using
    tendsto_id.dist (tendsto_const_nhds : Tendsto (fun _a => (x : E)) (ğ“ x) _)

@[to_additive tendsto_norm]
theorem tendsto_norm' {x : E} : Tendsto (fun a => â€–aâ€–) (ğ“ x) (ğ“ â€–xâ€–) := by
  simpa using tendsto_id.dist (tendsto_const_nhds : Tendsto (fun _a => (1 : E)) _ _)

@[to_additive]
theorem tendsto_norm_one : Tendsto (fun a : E => â€–aâ€–) (ğ“ 1) (ğ“ 0) := by
  simpa using tendsto_norm_div_self (1 : E)

@[to_additive (attr := continuity) continuous_norm]
theorem continuous_norm' : Continuous fun a : E => â€–aâ€– := by
  simpa using continuous_id.dist (continuous_const : Continuous fun _a => (1 : E))

@[to_additive (attr := continuity) continuous_nnnorm]
theorem continuous_nnnorm' : Continuous fun a : E => â€–aâ€–â‚Š :=
  continuous_norm'.subtype_mk _

@[to_additive]
theorem mem_closure_one_iff_norm {x : E} : x âˆˆ closure ({1} : Set E) â†” â€–xâ€– = 0 := by
  rw [â† closedBall_zero', mem_closedBall_one_iff, (norm_nonneg' x).le_iff_eq]

@[to_additive]
theorem closure_one_eq : closure ({1} : Set E) = { x | â€–xâ€– = 0 } :=
  Set.ext fun _x => mem_closure_one_iff_norm

section

variable {l : Filter Î±} {f : Î± â†’ E}

@[to_additive Filter.Tendsto.norm]
theorem Filter.Tendsto.norm' (h : Tendsto f l (ğ“ a)) : Tendsto (fun x => â€–f xâ€–) l (ğ“ â€–aâ€–) :=
  tendsto_norm'.comp h

@[to_additive Filter.Tendsto.nnnorm]
theorem Filter.Tendsto.nnnorm' (h : Tendsto f l (ğ“ a)) : Tendsto (fun x => â€–f xâ€–â‚Š) l (ğ“ â€–aâ€–â‚Š) :=
  Tendsto.comp continuous_nnnorm'.continuousAt h

end

section

variable [TopologicalSpace Î±] {f : Î± â†’ E}

@[to_additive (attr := fun_prop) Continuous.norm]
theorem Continuous.norm' : Continuous f â†’ Continuous fun x => â€–f xâ€– :=
  continuous_norm'.comp

@[to_additive (attr := fun_prop) Continuous.nnnorm]
theorem Continuous.nnnorm' : Continuous f â†’ Continuous fun x => â€–f xâ€–â‚Š :=
  continuous_nnnorm'.comp

@[to_additive (attr := fun_prop) ContinuousAt.norm]
theorem ContinuousAt.norm' {a : Î±} (h : ContinuousAt f a) : ContinuousAt (fun x => â€–f xâ€–) a :=
  Tendsto.norm' h

@[to_additive (attr := fun_prop) ContinuousAt.nnnorm]
theorem ContinuousAt.nnnorm' {a : Î±} (h : ContinuousAt f a) : ContinuousAt (fun x => â€–f xâ€–â‚Š) a :=
  Tendsto.nnnorm' h

@[to_additive ContinuousWithinAt.norm]
theorem ContinuousWithinAt.norm' {s : Set Î±} {a : Î±} (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x => â€–f xâ€–) s a :=
  Tendsto.norm' h

@[to_additive ContinuousWithinAt.nnnorm]
theorem ContinuousWithinAt.nnnorm' {s : Set Î±} {a : Î±} (h : ContinuousWithinAt f s a) :
    ContinuousWithinAt (fun x => â€–f xâ€–â‚Š) s a :=
  Tendsto.nnnorm' h

@[to_additive (attr := fun_prop) ContinuousOn.norm]
theorem ContinuousOn.norm' {s : Set Î±} (h : ContinuousOn f s) : ContinuousOn (fun x => â€–f xâ€–) s :=
  fun x hx => (h x hx).norm'

@[to_additive (attr := fun_prop) ContinuousOn.nnnorm]
theorem ContinuousOn.nnnorm' {s : Set Î±} (h : ContinuousOn f s) :
    ContinuousOn (fun x => â€–f xâ€–â‚Š) s := fun x hx => (h x hx).nnnorm'

end

/-- If `â€–yâ€– â†’ âˆ`, then we can assume `y â‰  x` for any fixed `x`. -/
@[to_additive eventually_ne_of_tendsto_norm_atTop "If `â€–yâ€–â†’âˆ`, then we can assume `yâ‰ x` for any
fixed `x`"]
theorem eventually_ne_of_tendsto_norm_atTop' {l : Filter Î±} {f : Î± â†’ E}
    (h : Tendsto (fun y => â€–f yâ€–) l atTop) (x : E) : âˆ€á¶  y in l, f y â‰  x :=
  (h.eventually_ne_atTop _).mono fun _x => ne_of_apply_ne norm

@[to_additive]
theorem SeminormedCommGroup.mem_closure_iff :
    a âˆˆ closure s â†” âˆ€ Îµ, 0 < Îµ â†’ âˆƒ b âˆˆ s, â€–a / bâ€– < Îµ := by
  simp [Metric.mem_closure_iff, dist_eq_norm_div]

@[to_additive norm_le_zero_iff']
theorem norm_le_zero_iff''' [T0Space E] {a : E} : â€–aâ€– â‰¤ 0 â†” a = 1 := by
  letI : NormedGroup E :=
    { â€¹SeminormedGroup Eâ€º with toMetricSpace := MetricSpace.ofT0PseudoMetricSpace E }
  rw [â† dist_one_right, dist_le_zero]

@[to_additive norm_eq_zero']
theorem norm_eq_zero''' [T0Space E] {a : E} : â€–aâ€– = 0 â†” a = 1 :=
  (norm_nonneg' a).le_iff_eq.symm.trans norm_le_zero_iff'''

@[to_additive norm_pos_iff']
theorem norm_pos_iff''' [T0Space E] {a : E} : 0 < â€–aâ€– â†” a â‰  1 := by
  rw [â† not_le, norm_le_zero_iff''']

@[to_additive]
theorem SeminormedGroup.tendstoUniformlyOn_one {f : Î¹ â†’ Îº â†’ G} {s : Set Îº} {l : Filter Î¹} :
    TendstoUniformlyOn f 1 l s â†” âˆ€ Îµ > 0, âˆ€á¶  i in l, âˆ€ x âˆˆ s, â€–f i xâ€– < Îµ := by
  #adaptation_note /-- nightly-2024-03-11.
  Originally this was `simp_rw` instead of `simp only`,
  but this creates a bad proof term with nested `OfNat.ofNat` that trips up `@[to_additive]`. -/
  simp only [tendstoUniformlyOn_iff, Pi.one_apply, dist_one_left]

@[to_additive]
theorem SeminormedGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_one {f : Î¹ â†’ Îº â†’ G}
    {l : Filter Î¹} {l' : Filter Îº} :
    UniformCauchySeqOnFilter f l l' â†”
      TendstoUniformlyOnFilter (fun n : Î¹ Ã— Î¹ => fun z => f n.fst z / f n.snd z) 1 (l Ã—Ë¢ l) l' := by
  refine âŸ¨fun hf u hu => ?_, fun hf u hu => ?_âŸ©
  Â· obtain âŸ¨Îµ, hÎµ, HâŸ© := uniformity_basis_dist.mem_uniformity_iff.mp hu
    refine
      (hf { p : G Ã— G | dist p.fst p.snd < Îµ } <| dist_mem_uniformity hÎµ).mono fun x hx =>
        H 1 (f x.fst.fst x.snd / f x.fst.snd x.snd) ?_
    simpa [dist_eq_norm_div, norm_div_rev] using hx
  Â· obtain âŸ¨Îµ, hÎµ, HâŸ© := uniformity_basis_dist.mem_uniformity_iff.mp hu
    refine
      (hf { p : G Ã— G | dist p.fst p.snd < Îµ } <| dist_mem_uniformity hÎµ).mono fun x hx =>
        H (f x.fst.fst x.snd) (f x.fst.snd x.snd) ?_
    simpa [dist_eq_norm_div, norm_div_rev] using hx

@[to_additive]
theorem SeminormedGroup.uniformCauchySeqOn_iff_tendstoUniformlyOn_one {f : Î¹ â†’ Îº â†’ G} {s : Set Îº}
    {l : Filter Î¹} :
    UniformCauchySeqOn f l s â†”
      TendstoUniformlyOn (fun n : Î¹ Ã— Î¹ => fun z => f n.fst z / f n.snd z) 1 (l Ã—Ë¢ l) s := by
  rw [tendstoUniformlyOn_iff_tendstoUniformlyOnFilter,
    uniformCauchySeqOn_iff_uniformCauchySeqOnFilter,
    SeminormedGroup.uniformCauchySeqOnFilter_iff_tendstoUniformlyOnFilter_one]

end SeminormedGroup

section Induced

variable (E F)
variable [FunLike ğ“• E F]

-- See note [reducible non-instances]
/-- A group homomorphism from a `Group` to a `SeminormedGroup` induces a `SeminormedGroup`
structure on the domain. -/
@[to_additive (attr := reducible) "A group homomorphism from an `AddGroup` to a
`SeminormedAddGroup` induces a `SeminormedAddGroup` structure on the domain."]
def SeminormedGroup.induced [Group E] [SeminormedGroup F] [MonoidHomClass ğ“• E F] (f : ğ“•) :
    SeminormedGroup E :=
  { PseudoMetricSpace.induced f toPseudoMetricSpace with
    -- Porting note: needed to add the instance explicitly, and `â€¹PseudoMetricSpace Fâ€º` failed
    norm := fun x => â€–f xâ€–
    dist_eq := fun x y => by simp only [map_div, â† dist_eq_norm_div]; rfl }

-- See note [reducible non-instances]
/-- A group homomorphism from a `CommGroup` to a `SeminormedGroup` induces a
`SeminormedCommGroup` structure on the domain. -/
@[to_additive (attr := reducible) "A group homomorphism from an `AddCommGroup` to a
`SeminormedAddGroup` induces a `SeminormedAddCommGroup` structure on the domain."]
def SeminormedCommGroup.induced
    [CommGroup E] [SeminormedGroup F] [MonoidHomClass ğ“• E F] (f : ğ“•) :
    SeminormedCommGroup E :=
  { SeminormedGroup.induced E F f with
    mul_comm := mul_comm }

-- See note [reducible non-instances].
/-- An injective group homomorphism from a `Group` to a `NormedGroup` induces a `NormedGroup`
structure on the domain. -/
@[to_additive (attr := reducible) "An injective group homomorphism from an `AddGroup` to a
`NormedAddGroup` induces a `NormedAddGroup` structure on the domain."]
def NormedGroup.induced
    [Group E] [NormedGroup F] [MonoidHomClass ğ“• E F] (f : ğ“•) (h : Injective f) :
    NormedGroup E :=
  { SeminormedGroup.induced E F f, MetricSpace.induced f h _ with }

-- See note [reducible non-instances].
/-- An injective group homomorphism from a `CommGroup` to a `NormedGroup` induces a
`NormedCommGroup` structure on the domain. -/
@[to_additive (attr := reducible) "An injective group homomorphism from a `CommGroup` to a
`NormedCommGroup` induces a `NormedCommGroup` structure on the domain."]
def NormedCommGroup.induced [CommGroup E] [NormedGroup F] [MonoidHomClass ğ“• E F] (f : ğ“•)
    (h : Injective f) : NormedCommGroup E :=
  { SeminormedGroup.induced E F f, MetricSpace.induced f h _ with
    mul_comm := mul_comm }

end Induced

section SeminormedCommGroup

variable [SeminormedCommGroup E] [SeminormedCommGroup F] {a aâ‚ aâ‚‚ b bâ‚ bâ‚‚ : E} {r râ‚ râ‚‚ : â„}

@[to_additive]
theorem dist_inv (x y : E) : dist xâ»Â¹ y = dist x yâ»Â¹ := by
  simp_rw [dist_eq_norm_div, â† norm_inv' (xâ»Â¹ / y), inv_div, div_inv_eq_mul, mul_comm]

theorem norm_multiset_sum_le {E} [SeminormedAddCommGroup E] (m : Multiset E) :
    â€–m.sumâ€– â‰¤ (m.map fun x => â€–xâ€–).sum :=
  m.le_sum_of_subadditive norm norm_zero norm_add_le

@[to_additive existing]
theorem norm_multiset_prod_le (m : Multiset E) : â€–m.prodâ€– â‰¤ (m.map fun x => â€–xâ€–).sum := by
  rw [â† Multiplicative.ofAdd_le, ofAdd_multiset_prod, Multiset.map_map]
  refine Multiset.le_prod_of_submultiplicative (Multiplicative.ofAdd âˆ˜ norm) ?_ (fun x y => ?_) _
  Â· simp only [comp_apply, norm_one', ofAdd_zero]
  Â· exact norm_mul_le' x y

-- Porting note: had to add `Î¹` here because otherwise the universe order gets switched compared to
-- `norm_prod_le` below
theorem norm_sum_le {Î¹ E} [SeminormedAddCommGroup E] (s : Finset Î¹) (f : Î¹ â†’ E) :
    â€–âˆ‘ i âˆˆ s, f iâ€– â‰¤ âˆ‘ i âˆˆ s, â€–f iâ€– :=
  s.le_sum_of_subadditive norm norm_zero norm_add_le f

@[to_additive existing]
theorem norm_prod_le (s : Finset Î¹) (f : Î¹ â†’ E) : â€–âˆ i âˆˆ s, f iâ€– â‰¤ âˆ‘ i âˆˆ s, â€–f iâ€– := by
  rw [â† Multiplicative.ofAdd_le, ofAdd_sum]
  refine Finset.le_prod_of_submultiplicative (Multiplicative.ofAdd âˆ˜ norm) ?_ (fun x y => ?_) _ _
  Â· simp only [comp_apply, norm_one', ofAdd_zero]
  Â· exact norm_mul_le' x y

@[to_additive]
theorem norm_prod_le_of_le (s : Finset Î¹) {f : Î¹ â†’ E} {n : Î¹ â†’ â„} (h : âˆ€ b âˆˆ s, â€–f bâ€– â‰¤ n b) :
    â€–âˆ b âˆˆ s, f bâ€– â‰¤ âˆ‘ b âˆˆ s, n b :=
  (norm_prod_le s f).trans <| Finset.sum_le_sum h

@[to_additive]
theorem dist_prod_prod_le_of_le (s : Finset Î¹) {f a : Î¹ â†’ E} {d : Î¹ â†’ â„}
    (h : âˆ€ b âˆˆ s, dist (f b) (a b) â‰¤ d b) :
    dist (âˆ b âˆˆ s, f b) (âˆ b âˆˆ s, a b) â‰¤ âˆ‘ b âˆˆ s, d b := by
  simp only [dist_eq_norm_div, â† Finset.prod_div_distrib] at *
  exact norm_prod_le_of_le s h

@[to_additive]
theorem dist_prod_prod_le (s : Finset Î¹) (f a : Î¹ â†’ E) :
    dist (âˆ b âˆˆ s, f b) (âˆ b âˆˆ s, a b) â‰¤ âˆ‘ b âˆˆ s, dist (f b) (a b) :=
  dist_prod_prod_le_of_le s fun _ _ => le_rfl

@[to_additive]
theorem mul_mem_ball_iff_norm : a * b âˆˆ ball a r â†” â€–bâ€– < r := by
  rw [mem_ball_iff_norm'', mul_div_cancel_left]

@[to_additive]
theorem mul_mem_closedBall_iff_norm : a * b âˆˆ closedBall a r â†” â€–bâ€– â‰¤ r := by
  rw [mem_closedBall_iff_norm'', mul_div_cancel_left]

@[to_additive (attr := simp 1001)]
-- Porting note: increase priority so that the left-hand side doesn't simplify
theorem preimage_mul_ball (a b : E) (r : â„) : (b * Â·) â»Â¹' ball a r = ball (a / b) r := by
  ext c
  simp only [dist_eq_norm_div, Set.mem_preimage, mem_ball, div_div_eq_mul_div, mul_comm]

@[to_additive (attr := simp 1001)]
-- Porting note: increase priority so that the left-hand side doesn't simplify
theorem preimage_mul_closedBall (a b : E) (r : â„) :
    (b * Â·) â»Â¹' closedBall a r = closedBall (a / b) r := by
  ext c
  simp only [dist_eq_norm_div, Set.mem_preimage, mem_closedBall, div_div_eq_mul_div, mul_comm]

@[to_additive (attr := simp)]
theorem preimage_mul_sphere (a b : E) (r : â„) : (b * Â·) â»Â¹' sphere a r = sphere (a / b) r := by
  ext c
  simp only [Set.mem_preimage, mem_sphere_iff_norm', div_div_eq_mul_div, mul_comm]

@[to_additive norm_nsmul_le]
theorem norm_pow_le_mul_norm (n : â„•) (a : E) : â€–a ^ nâ€– â‰¤ n * â€–aâ€– := by
  induction' n with n ih; Â· simp
  simpa only [pow_succ, Nat.cast_succ, add_mul, one_mul] using norm_mul_le_of_le ih le_rfl

@[to_additive nnnorm_nsmul_le]
theorem nnnorm_pow_le_mul_norm (n : â„•) (a : E) : â€–a ^ nâ€–â‚Š â‰¤ n * â€–aâ€–â‚Š := by
  simpa only [â† NNReal.coe_le_coe, NNReal.coe_mul, NNReal.coe_natCast] using
    norm_pow_le_mul_norm n a

@[to_additive]
theorem pow_mem_closedBall {n : â„•} (h : a âˆˆ closedBall b r) :
    a ^ n âˆˆ closedBall (b ^ n) (n â€¢ r) := by
  simp only [mem_closedBall, dist_eq_norm_div, â† div_pow] at h âŠ¢
  refine (norm_pow_le_mul_norm n (a / b)).trans ?_
  simpa only [nsmul_eq_mul] using mul_le_mul_of_nonneg_left h n.cast_nonneg

@[to_additive]
theorem pow_mem_ball {n : â„•} (hn : 0 < n) (h : a âˆˆ ball b r) : a ^ n âˆˆ ball (b ^ n) (n â€¢ r) := by
  simp only [mem_ball, dist_eq_norm_div, â† div_pow] at h âŠ¢
  refine lt_of_le_of_lt (norm_pow_le_mul_norm n (a / b)) ?_
  replace hn : 0 < (n : â„) := by norm_cast
  rw [nsmul_eq_mul]
  nlinarith

@[to_additive] -- Porting note (#10618): `simp` can prove this
theorem mul_mem_closedBall_mul_iff {c : E} : a * c âˆˆ closedBall (b * c) r â†” a âˆˆ closedBall b r := by
  simp only [mem_closedBall, dist_eq_norm_div, mul_div_mul_right_eq_div]

@[to_additive] -- Porting note (#10618): `simp` can prove this
theorem mul_mem_ball_mul_iff {c : E} : a * c âˆˆ ball (b * c) r â†” a âˆˆ ball b r := by
  simp only [mem_ball, dist_eq_norm_div, mul_div_mul_right_eq_div]

@[to_additive]
theorem smul_closedBall'' : a â€¢ closedBall b r = closedBall (a â€¢ b) r := by
  ext
  simp [mem_closedBall, Set.mem_smul_set, dist_eq_norm_div, _root_.div_eq_inv_mul, â†
    eq_inv_mul_iff_mul_eq, mul_assoc]
  -- Porting note: `ENNReal.div_eq_inv_mul` should be `protected`?

@[to_additive]
theorem smul_ball'' : a â€¢ ball b r = ball (a â€¢ b) r := by
  ext
  simp [mem_ball, Set.mem_smul_set, dist_eq_norm_div, _root_.div_eq_inv_mul,
    â† eq_inv_mul_iff_mul_eq, mul_assoc]

open Finset

@[to_additive]
theorem controlled_prod_of_mem_closure {s : Subgroup E} (hg : a âˆˆ closure (s : Set E)) {b : â„• â†’ â„}
    (b_pos : âˆ€ n, 0 < b n) :
    âˆƒ v : â„• â†’ E,
      Tendsto (fun n => âˆ i âˆˆ range (n + 1), v i) atTop (ğ“ a) âˆ§
        (âˆ€ n, v n âˆˆ s) âˆ§ â€–v 0 / aâ€– < b 0 âˆ§ âˆ€ n, 0 < n â†’ â€–v nâ€– < b n := by
  obtain âŸ¨u : â„• â†’ E, u_in : âˆ€ n, u n âˆˆ s, lim_u : Tendsto u atTop (ğ“ a)âŸ© :=
    mem_closure_iff_seq_limit.mp hg
  obtain âŸ¨nâ‚€, hnâ‚€âŸ© : âˆƒ nâ‚€, âˆ€ n â‰¥ nâ‚€, â€–u n / aâ€– < b 0 :=
    haveI : { x | â€–x / aâ€– < b 0 } âˆˆ ğ“ a := by
      simp_rw [â† dist_eq_norm_div]
      exact Metric.ball_mem_nhds _ (b_pos _)
    Filter.tendsto_atTop'.mp lim_u _ this
  set z : â„• â†’ E := fun n => u (n + nâ‚€)
  have lim_z : Tendsto z atTop (ğ“ a) := lim_u.comp (tendsto_add_atTop_nat nâ‚€)
  have mem_ğ“¤ : âˆ€ n, { p : E Ã— E | â€–p.1 / p.2â€– < b (n + 1) } âˆˆ ğ“¤ E := fun n => by
    simpa [â† dist_eq_norm_div] using Metric.dist_mem_uniformity (b_pos <| n + 1)
  obtain âŸ¨Ï† : â„• â†’ â„•, Ï†_extr : StrictMono Ï†, hÏ† : âˆ€ n, â€–z (Ï† <| n + 1) / z (Ï† n)â€– < b (n + 1)âŸ© :=
    lim_z.cauchySeq.subseq_mem mem_ğ“¤
  set w : â„• â†’ E := z âˆ˜ Ï†
  have hw : Tendsto w atTop (ğ“ a) := lim_z.comp Ï†_extr.tendsto_atTop
  set v : â„• â†’ E := fun i => if i = 0 then w 0 else w i / w (i - 1)
  refine âŸ¨v, Tendsto.congr (Finset.eq_prod_range_div' w) hw, ?_, hnâ‚€ _ (nâ‚€.le_add_left _), ?_âŸ©
  Â· rintro âŸ¨âŸ©
    Â· change w 0 âˆˆ s
      apply u_in
    Â· apply s.div_mem <;> apply u_in
  Â· intro l hl
    obtain âŸ¨k, rflâŸ© : âˆƒ k, l = k + 1 := Nat.exists_eq_succ_of_ne_zero hl.ne'
    apply hÏ†

@[to_additive]
theorem controlled_prod_of_mem_closure_range {j : E â†’* F} {b : F}
    (hb : b âˆˆ closure (j.range : Set F)) {f : â„• â†’ â„} (b_pos : âˆ€ n, 0 < f n) :
    âˆƒ a : â„• â†’ E,
      Tendsto (fun n => âˆ i âˆˆ range (n + 1), j (a i)) atTop (ğ“ b) âˆ§
        â€–j (a 0) / bâ€– < f 0 âˆ§ âˆ€ n, 0 < n â†’ â€–j (a n)â€– < f n := by
  obtain âŸ¨v, sum_v, v_in, hvâ‚€, hv_posâŸ© := controlled_prod_of_mem_closure hb b_pos
  choose g hg using v_in
  exact
    âŸ¨g, by simpa [â† hg] using sum_v, by simpa [hg 0] using hvâ‚€,
      fun n hn => by simpa [hg] using hv_pos n hnâŸ©

@[to_additive]
theorem nnnorm_multiset_prod_le (m : Multiset E) : â€–m.prodâ€–â‚Š â‰¤ (m.map fun x => â€–xâ€–â‚Š).sum :=
  NNReal.coe_le_coe.1 <| by
    push_cast
    rw [Multiset.map_map]
    exact norm_multiset_prod_le _

@[to_additive]
theorem nnnorm_prod_le (s : Finset Î¹) (f : Î¹ â†’ E) : â€–âˆ a âˆˆ s, f aâ€–â‚Š â‰¤ âˆ‘ a âˆˆ s, â€–f aâ€–â‚Š :=
  NNReal.coe_le_coe.1 <| by
    push_cast
    exact norm_prod_le _ _

@[to_additive]
theorem nnnorm_prod_le_of_le (s : Finset Î¹) {f : Î¹ â†’ E} {n : Î¹ â†’ â„â‰¥0} (h : âˆ€ b âˆˆ s, â€–f bâ€–â‚Š â‰¤ n b) :
    â€–âˆ b âˆˆ s, f bâ€–â‚Š â‰¤ âˆ‘ b âˆˆ s, n b :=
  (norm_prod_le_of_le s h).trans_eq NNReal.coe_sum.symm

namespace Real

instance norm : Norm â„ where
  norm r := |r|

@[simp]
theorem norm_eq_abs (r : â„) : â€–râ€– = |r| :=
  rfl

instance normedAddCommGroup : NormedAddCommGroup â„ :=
  âŸ¨fun _r _y => rflâŸ©

theorem norm_of_nonneg (hr : 0 â‰¤ r) : â€–râ€– = r :=
  abs_of_nonneg hr

theorem norm_of_nonpos (hr : r â‰¤ 0) : â€–râ€– = -r :=
  abs_of_nonpos hr

theorem le_norm_self (r : â„) : r â‰¤ â€–râ€– :=
  le_abs_self r

-- Porting note (#10618): `simp` can prove this
theorem norm_natCast (n : â„•) : â€–(n : â„)â€– = n :=
  abs_of_nonneg n.cast_nonneg

@[simp]
theorem nnnorm_natCast (n : â„•) : â€–(n : â„)â€–â‚Š = n :=
  NNReal.eq <| norm_natCast _

@[deprecated (since := "2024-04-05")] alias norm_coe_nat := norm_natCast
@[deprecated (since := "2024-04-05")] alias nnnorm_coe_nat := nnnorm_natCast

-- Porting note (#10618): `simp` can prove this
theorem norm_two : â€–(2 : â„)â€– = 2 :=
  abs_of_pos zero_lt_two

@[simp]
theorem nnnorm_two : â€–(2 : â„)â€–â‚Š = 2 :=
  NNReal.eq <| by simp

theorem nnnorm_of_nonneg (hr : 0 â‰¤ r) : â€–râ€–â‚Š = âŸ¨r, hrâŸ© :=
  NNReal.eq <| norm_of_nonneg hr

@[simp]
theorem nnnorm_abs (r : â„) : â€–|r|â€–â‚Š = â€–râ€–â‚Š := by simp [nnnorm]

theorem ennnorm_eq_ofReal (hr : 0 â‰¤ r) : (â€–râ€–â‚Š : â„â‰¥0âˆ) = ENNReal.ofReal r := by
  rw [â† ofReal_norm_eq_coe_nnnorm, norm_of_nonneg hr]

theorem ennnorm_eq_ofReal_abs (r : â„) : (â€–râ€–â‚Š : â„â‰¥0âˆ) = ENNReal.ofReal |r| := by
  rw [â† Real.nnnorm_abs r, Real.ennnorm_eq_ofReal (abs_nonneg _)]

theorem toNNReal_eq_nnnorm_of_nonneg (hr : 0 â‰¤ r) : r.toNNReal = â€–râ€–â‚Š := by
  rw [Real.toNNReal_of_nonneg hr]
  ext
  rw [coe_mk, coe_nnnorm r, Real.norm_eq_abs r, abs_of_nonneg hr]
  -- Porting note: this is due to the change from `Subtype.val` to `NNReal.toReal` for the coercion

theorem ofReal_le_ennnorm (r : â„) : ENNReal.ofReal r â‰¤ â€–râ€–â‚Š := by
  obtain hr | hr := le_total 0 r
  Â· exact (Real.ennnorm_eq_ofReal hr).ge
  Â· rw [ENNReal.ofReal_eq_zero.2 hr]
    exact bot_le
-- Porting note: should this be renamed to `Real.ofReal_le_nnnorm`?

end Real

namespace NNReal

instance : NNNorm â„â‰¥0 where
  nnnorm x := x

@[simp] lemma nnnorm_eq_self (x : â„â‰¥0) : â€–xâ€–â‚Š = x := rfl

end NNReal

end SeminormedCommGroup

section NormedGroup

variable [NormedGroup E] [NormedGroup F] {a b : E}

@[to_additive (attr := simp) norm_eq_zero]
theorem norm_eq_zero'' : â€–aâ€– = 0 â†” a = 1 :=
  norm_eq_zero'''

@[to_additive norm_ne_zero_iff]
theorem norm_ne_zero_iff' : â€–aâ€– â‰  0 â†” a â‰  1 :=
  norm_eq_zero''.not

@[to_additive (attr := simp) norm_pos_iff]
theorem norm_pos_iff'' : 0 < â€–aâ€– â†” a â‰  1 :=
  norm_pos_iff'''

@[to_additive (attr := simp) norm_le_zero_iff]
theorem norm_le_zero_iff'' : â€–aâ€– â‰¤ 0 â†” a = 1 :=
  norm_le_zero_iff'''

@[to_additive]
theorem norm_div_eq_zero_iff : â€–a / bâ€– = 0 â†” a = b := by rw [norm_eq_zero'', div_eq_one]

@[to_additive]
theorem norm_div_pos_iff : 0 < â€–a / bâ€– â†” a â‰  b := by
  rw [(norm_nonneg' _).lt_iff_ne, ne_comm]
  exact norm_div_eq_zero_iff.not

@[to_additive eq_of_norm_sub_le_zero]
theorem eq_of_norm_div_le_zero (h : â€–a / bâ€– â‰¤ 0) : a = b := by
  rwa [â† div_eq_one, â† norm_le_zero_iff'']

alias âŸ¨eq_of_norm_div_eq_zero, _âŸ© := norm_div_eq_zero_iff

attribute [to_additive] eq_of_norm_div_eq_zero

@[to_additive (attr := simp) nnnorm_eq_zero]
theorem nnnorm_eq_zero' : â€–aâ€–â‚Š = 0 â†” a = 1 := by
  rw [â† NNReal.coe_eq_zero, coe_nnnorm', norm_eq_zero'']

@[to_additive nnnorm_ne_zero_iff]
theorem nnnorm_ne_zero_iff' : â€–aâ€–â‚Š â‰  0 â†” a â‰  1 :=
  nnnorm_eq_zero'.not

@[to_additive (attr := simp) nnnorm_pos]
lemma nnnorm_pos' : 0 < â€–aâ€–â‚Š â†” a â‰  1 := pos_iff_ne_zero.trans nnnorm_ne_zero_iff'

@[to_additive]
theorem tendsto_norm_div_self_punctured_nhds (a : E) :
    Tendsto (fun x => â€–x / aâ€–) (ğ“[â‰ ] a) (ğ“[>] 0) :=
  (tendsto_norm_div_self a).inf <|
    tendsto_principal_principal.2 fun _x hx => norm_pos_iff''.2 <| div_ne_one.2 hx

@[to_additive]
theorem tendsto_norm_nhdsWithin_one : Tendsto (norm : E â†’ â„) (ğ“[â‰ ] 1) (ğ“[>] 0) :=
  tendsto_norm_one.inf <| tendsto_principal_principal.2 fun _x => norm_pos_iff''.2

variable (E)

/-- The norm of a normed group as a group norm. -/
@[to_additive "The norm of a normed group as an additive group norm."]
def normGroupNorm : GroupNorm E :=
  { normGroupSeminorm _ with eq_one_of_map_eq_zero' := fun _ => norm_eq_zero''.1 }

@[simp]
theorem coe_normGroupNorm : â‡‘(normGroupNorm E) = norm :=
  rfl

@[to_additive comap_norm_nhdsWithin_Ioi_zero]
lemma comap_norm_nhdsWithin_Ioi_zero' : comap norm (ğ“[>] 0) = ğ“[â‰ ] (1 : E) := by
  simp [nhdsWithin, comap_norm_nhds_one, Set.preimage, Set.compl_def]

end NormedGroup

section NormedAddGroup

variable [NormedAddGroup E] [TopologicalSpace Î±] {f : Î± â†’ E}

/-! Some relations with `HasCompactSupport` -/

theorem hasCompactSupport_norm_iff : (HasCompactSupport fun x => â€–f xâ€–) â†” HasCompactSupport f :=
  hasCompactSupport_comp_left norm_eq_zero

alias âŸ¨_, HasCompactSupport.normâŸ© := hasCompactSupport_norm_iff

end NormedAddGroup

/-! ### Subgroups of normed groups -/


namespace Subgroup

section SeminormedGroup

variable [SeminormedGroup E] {s : Subgroup E}

/-- A subgroup of a seminormed group is also a seminormed group,
with the restriction of the norm. -/
@[to_additive "A subgroup of a seminormed group is also a seminormed group, with the restriction of
the norm."]
instance seminormedGroup : SeminormedGroup s :=
  SeminormedGroup.induced _ _ s.subtype

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`. -/
@[to_additive (attr := simp) "If `x` is an element of a subgroup `s` of a seminormed group `E`, its
norm in `s` is equal to its norm in `E`."]
theorem coe_norm (x : s) : â€–xâ€– = â€–(x : E)â€– :=
  rfl

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`.

This is a reversed version of the `simp` lemma `Subgroup.coe_norm` for use by `norm_cast`. -/
@[to_additive (attr := norm_cast) "If `x` is an element of a subgroup `s` of a seminormed group `E`,
its norm in `s` is equal to its norm in `E`.

This is a reversed version of the `simp` lemma `AddSubgroup.coe_norm` for use by `norm_cast`."]
theorem norm_coe {s : Subgroup E} (x : s) : â€–(x : E)â€– = â€–xâ€– :=
  rfl

end SeminormedGroup

@[to_additive]
instance seminormedCommGroup [SeminormedCommGroup E] {s : Subgroup E} : SeminormedCommGroup s :=
  SeminormedCommGroup.induced _ _ s.subtype

@[to_additive]
instance normedGroup [NormedGroup E] {s : Subgroup E} : NormedGroup s :=
  NormedGroup.induced _ _ s.subtype Subtype.coe_injective

@[to_additive]
instance normedCommGroup [NormedCommGroup E] {s : Subgroup E} : NormedCommGroup s :=
  NormedCommGroup.induced _ _ s.subtype Subtype.coe_injective

end Subgroup

/-! ### Subgroup classes of normed groups -/


namespace SubgroupClass

section SeminormedGroup

variable [SeminormedGroup E] {S : Type*} [SetLike S E] [SubgroupClass S E] (s : S)

/-- A subgroup of a seminormed group is also a seminormed group,
with the restriction of the norm. -/
@[to_additive "A subgroup of a seminormed additive group is also a seminormed additive group, with
the restriction of the norm."]
instance (priority := 75) seminormedGroup : SeminormedGroup s :=
  SeminormedGroup.induced _ _ (SubgroupClass.subtype s)

/-- If `x` is an element of a subgroup `s` of a seminormed group `E`, its norm in `s` is equal to
its norm in `E`. -/
@[to_additive (attr := simp) "If `x` is an element of an additive subgroup `s` of a seminormed
additive group `E`, its norm in `s` is equal to its norm in `E`."]
theorem coe_norm (x : s) : â€–xâ€– = â€–(x : E)â€– :=
  rfl

end SeminormedGroup

@[to_additive]
instance (priority := 75) seminormedCommGroup [SeminormedCommGroup E] {S : Type*} [SetLike S E]
    [SubgroupClass S E] (s : S) : SeminormedCommGroup s :=
  SeminormedCommGroup.induced _ _ (SubgroupClass.subtype s)

@[to_additive]
instance (priority := 75) normedGroup [NormedGroup E] {S : Type*} [SetLike S E] [SubgroupClass S E]
    (s : S) : NormedGroup s :=
  NormedGroup.induced _ _ (SubgroupClass.subtype s) Subtype.coe_injective

@[to_additive]
instance (priority := 75) normedCommGroup [NormedCommGroup E] {S : Type*} [SetLike S E]
    [SubgroupClass S E] (s : S) : NormedCommGroup s :=
  NormedCommGroup.induced _ _ (SubgroupClass.subtype s) Subtype.coe_injective

end SubgroupClass
