/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl, YaÃ«l Dillies
-/
import Mathlib.Analysis.Normed.Group.Continuity
import Mathlib.Topology.MetricSpace.Bounded
import Mathlib.Order.Filter.Pointwise

/-!
# Boundedness in normed groups

This file rephrases metric boundedness in terms of norms.

## Tags

normed group
-/

open Filter Metric Bornology
open scoped Pointwise Topology

variable {Î± E F G : Type*}

section SeminormedGroup
variable [SeminormedGroup E] [SeminormedGroup F] [SeminormedGroup G] {s : Set E}

@[to_additive (attr := simp) comap_norm_atTop]
lemma comap_norm_atTop' : comap norm atTop = cobounded E := by
  simpa only [dist_one_right] using comap_dist_right_atTop (1 : E)

@[to_additive Filter.HasBasis.cobounded_of_norm]
lemma Filter.HasBasis.cobounded_of_norm' {Î¹ : Sort*} {p : Î¹ â†’ Prop} {s : Î¹ â†’ Set â„}
    (h : HasBasis atTop p s) : HasBasis (cobounded E) p fun i â†¦ norm â»Â¹' s i :=
  comap_norm_atTop' (E := E) â–¸ h.comap _

@[to_additive Filter.hasBasis_cobounded_norm]
lemma Filter.hasBasis_cobounded_norm' : HasBasis (cobounded E) (fun _ â†¦ True) ({x | Â· â‰¤ â€–xâ€–}) :=
  atTop_basis.cobounded_of_norm'

@[to_additive (attr := simp) tendsto_norm_atTop_iff_cobounded]
lemma tendsto_norm_atTop_iff_cobounded' {f : Î± â†’ E} {l : Filter Î±} :
    Tendsto (â€–f Â·â€–) l atTop â†” Tendsto f l (cobounded E) := by
  rw [â† comap_norm_atTop', tendsto_comap_iff]; rfl

@[to_additive tendsto_norm_cobounded_atTop]
lemma tendsto_norm_cobounded_atTop' : Tendsto norm (cobounded E) atTop :=
  tendsto_norm_atTop_iff_cobounded'.2 tendsto_id

@[to_additive eventually_cobounded_le_norm]
lemma eventually_cobounded_le_norm' (a : â„) : âˆ€á¶  x in cobounded E, a â‰¤ â€–xâ€– :=
  tendsto_norm_cobounded_atTop'.eventually_ge_atTop a

@[to_additive tendsto_norm_cocompact_atTop]
lemma tendsto_norm_cocompact_atTop' [ProperSpace E] : Tendsto norm (cocompact E) atTop :=
  cobounded_eq_cocompact (Î± := E) â–¸ tendsto_norm_cobounded_atTop'

@[to_additive (attr := simp)]
lemma Filter.inv_cobounded : (cobounded E)â»Â¹ = cobounded E := by
  simp only [â† comap_norm_atTop', â† Filter.comap_inv, comap_comap, Function.comp_def, norm_inv']

/-- In a (semi)normed group, inversion `x â†¦ xâ»Â¹` tends to infinity at infinity. -/
@[to_additive /-- In a (semi)normed group, negation `x â†¦ -x` tends to infinity at infinity. -/]
theorem Filter.tendsto_inv_cobounded : Tendsto Inv.inv (cobounded E) (cobounded E) :=
  inv_cobounded.le

@[to_additive isBounded_iff_forall_norm_le]
lemma isBounded_iff_forall_norm_le' : Bornology.IsBounded s â†” âˆƒ C, âˆ€ x âˆˆ s, â€–xâ€– â‰¤ C := by
  simpa only [Set.subset_def, mem_closedBall_one_iff] using isBounded_iff_subset_closedBall (1 : E)

alias âŸ¨Bornology.IsBounded.exists_norm_le', _âŸ© := isBounded_iff_forall_norm_le'

alias âŸ¨Bornology.IsBounded.exists_norm_le, _âŸ© := isBounded_iff_forall_norm_le

attribute [to_additive existing exists_norm_le] Bornology.IsBounded.exists_norm_le'

@[to_additive exists_pos_norm_le]
lemma Bornology.IsBounded.exists_pos_norm_le' (hs : IsBounded s) : âˆƒ R > 0, âˆ€ x âˆˆ s, â€–xâ€– â‰¤ R :=
  let âŸ¨Râ‚€, hRâ‚€âŸ© := hs.exists_norm_le'
  âŸ¨max Râ‚€ 1, by positivity, fun x hx â†¦ (hRâ‚€ x hx).trans <| le_max_left _ _âŸ©

@[to_additive Bornology.IsBounded.exists_pos_norm_lt]
lemma Bornology.IsBounded.exists_pos_norm_lt' (hs : IsBounded s) : âˆƒ R > 0, âˆ€ x âˆˆ s, â€–xâ€– < R :=
  let âŸ¨R, hRâ‚€, hRâŸ© := hs.exists_pos_norm_le'
  âŸ¨R + 1, by positivity, fun x hx â†¦ (hR x hx).trans_lt (lt_add_one _)âŸ©

@[to_additive]
lemma NormedCommGroup.cauchySeq_iff [Nonempty Î±] [SemilatticeSup Î±] {u : Î± â†’ E} :
    CauchySeq u â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ m, N â‰¤ m â†’ âˆ€ n, N â‰¤ n â†’ â€–u m / u nâ€– < Îµ := by
  simp [Metric.cauchySeq_iff, dist_eq_norm_div]

@[to_additive IsCompact.exists_bound_of_continuousOn]
lemma IsCompact.exists_bound_of_continuousOn' [TopologicalSpace Î±] {s : Set Î±} (hs : IsCompact s)
    {f : Î± â†’ E} (hf : ContinuousOn f s) : âˆƒ C, âˆ€ x âˆˆ s, â€–f xâ€– â‰¤ C :=
  (isBounded_iff_forall_norm_le'.1 (hs.image_of_continuousOn hf).isBounded).imp fun _C hC _x hx â†¦
    hC _ <| Set.mem_image_of_mem _ hx

@[to_additive]
lemma HasCompactMulSupport.exists_bound_of_continuous [TopologicalSpace Î±]
    {f : Î± â†’ E} (hf : HasCompactMulSupport f) (h'f : Continuous f) : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C := by
  simpa using (hf.isCompact_range h'f).isBounded.exists_norm_le'

/-- A helper lemma used to prove that the (scalar or usual) product of a function that tends to one
and a bounded function tends to one. This lemma is formulated for any binary operation
`op : E â†’ F â†’ G` with an estimate `â€–op x yâ€– â‰¤ A * â€–xâ€– * â€–yâ€–` for some constant A instead of
multiplication so that it can be applied to `(*)`, `flip (*)`, `(â€¢)`, and `flip (â€¢)`. -/
@[to_additive /-- A helper lemma used to prove that the (scalar or usual) product of a function that
tends to zero and a bounded function tends to zero. This lemma is formulated for any binary
operation `op : E â†’ F â†’ G` with an estimate `â€–op x yâ€– â‰¤ A * â€–xâ€– * â€–yâ€–` for some constant A instead
of multiplication so that it can be applied to `(*)`, `flip (*)`, `(â€¢)`, and `flip (â€¢)`. -/]
lemma Filter.Tendsto.op_one_isBoundedUnder_le' {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (hf : Tendsto f l (ð“ 1)) (hg : IsBoundedUnder (Â· â‰¤ Â·) l (Norm.norm âˆ˜ g)) (op : E â†’ F â†’ G)
    (h_op : âˆƒ A, âˆ€ x y, â€–op x yâ€– â‰¤ A * â€–xâ€– * â€–yâ€–) : Tendsto (fun x â†¦ op (f x) (g x)) l (ð“ 1) := by
  obtain âŸ¨A, h_opâŸ© := h_op
  rcases hg with âŸ¨C, hCâŸ©; rw [eventually_map] at hC
  rw [NormedCommGroup.tendsto_nhds_one] at hf âŠ¢
  intro Îµ Îµâ‚€
  rcases exists_pos_mul_lt Îµâ‚€ (A * C) with âŸ¨Î´, Î´â‚€, hÎ´âŸ©
  filter_upwards [hf Î´ Î´â‚€, hC] with i hf hg
  refine (h_op _ _).trans_lt ?_
  rcases le_total A 0 with hA | hA
  Â· exact (mul_nonpos_of_nonpos_of_nonneg (mul_nonpos_of_nonpos_of_nonneg hA <| norm_nonneg' _) <|
      norm_nonneg' _).trans_lt Îµâ‚€
  calc
    A * â€–f iâ€– * â€–g iâ€– â‰¤ A * Î´ * C := by gcongr; exact hg
    _ = A * C * Î´ := mul_right_comm _ _ _
    _ < Îµ := hÎ´

/-- A helper lemma used to prove that the (scalar or usual) product of a function that tends to one
and a bounded function tends to one. This lemma is formulated for any binary operation
`op : E â†’ F â†’ G` with an estimate `â€–op x yâ€– â‰¤ â€–xâ€– * â€–yâ€–` instead of multiplication so that it
can be applied to `(*)`, `flip (*)`, `(â€¢)`, and `flip (â€¢)`. -/
@[to_additive /-- A helper lemma used to prove that the (scalar or usual) product of a function that
tends to zero and a bounded function tends to zero. This lemma is formulated for any binary
operation `op : E â†’ F â†’ G` with an estimate `â€–op x yâ€– â‰¤ â€–xâ€– * â€–yâ€–` instead of multiplication so
that it can be applied to `(*)`, `flip (*)`, `(â€¢)`, and `flip (â€¢)`. -/]
theorem Filter.Tendsto.op_one_isBoundedUnder_le {f : Î± â†’ E} {g : Î± â†’ F} {l : Filter Î±}
    (hf : Tendsto f l (ð“ 1)) (hg : IsBoundedUnder (Â· â‰¤ Â·) l (Norm.norm âˆ˜ g)) (op : E â†’ F â†’ G)
    (h_op : âˆ€ x y, â€–op x yâ€– â‰¤ â€–xâ€– * â€–yâ€–) : Tendsto (fun x â†¦ op (f x) (g x)) l (ð“ 1) :=
  hf.op_one_isBoundedUnder_le' hg op âŸ¨1, fun x y â†¦ (one_mul â€–xâ€–).symm â–¸ h_op x yâŸ©

@[to_additive tendsto_norm_comp_cofinite_atTop_of_isClosedEmbedding]
lemma tendsto_norm_comp_cofinite_atTop_of_isClosedEmbedding' {X : Type*} [TopologicalSpace X]
    [DiscreteTopology X] [ProperSpace E] {e : X â†’ E}
    (he : Topology.IsClosedEmbedding e) : Tendsto (norm âˆ˜ e) cofinite atTop := by
  rw [â† Filter.cocompact_eq_cofinite X]
  apply tendsto_norm_cocompact_atTop'.comp (Topology.IsClosedEmbedding.tendsto_cocompact he)

end SeminormedGroup

section NormedAddGroup
variable [NormedAddGroup E] [TopologicalSpace Î±] {f : Î± â†’ E}

lemma Continuous.bounded_above_of_compact_support (hf : Continuous f) (h : HasCompactSupport f) :
    âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C := by
  simpa [bddAbove_def] using hf.norm.bddAbove_range_of_hasCompactSupport h.norm

end NormedAddGroup

section NormedAddGroupSource
variable [NormedAddGroup Î±] {f : Î± â†’ E}

@[to_additive]
lemma HasCompactMulSupport.exists_pos_le_norm [One E] (hf : HasCompactMulSupport f) :
    âˆƒ R : â„, 0 < R âˆ§ âˆ€ x : Î±, R â‰¤ â€–xâ€– â†’ f x = 1 := by
  obtain âŸ¨K, âŸ¨hK1, hK2âŸ©âŸ© := exists_compact_iff_hasCompactMulSupport.mpr hf
  obtain âŸ¨S, hS, hS'âŸ© := hK1.isBounded.exists_pos_norm_le
  refine âŸ¨S + 1, by positivity, fun x hx â†¦ hK2 x ((mt <| hS' x) ?_)âŸ©
  contrapose! hx
  exact lt_add_of_le_of_pos hx zero_lt_one

end NormedAddGroupSource
