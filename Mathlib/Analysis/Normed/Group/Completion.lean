/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin
-/
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Topology.Algebra.GroupCompletion
import Mathlib.Topology.MetricSpace.Completion

#align_import analysis.normed.group.completion from "leanprover-community/mathlib"@"17ef379e997badd73e5eabb4d38f11919ab3c4b3"

/-!
# Completion of a normed group

In this file we prove that the completion of a (semi)normed group is a normed group.

## Tags

normed group, completion
-/


noncomputable section

namespace UniformSpace

namespace Completion

variable (E : Type*)

instance [UniformSpace E] [Norm E] : Norm (Completion E) where
  norm := Completion.extension Norm.norm

@[simp]
theorem norm_coe {E} [SeminormedAddCommGroup E] (x : E) : ‖(x : Completion E)‖ = ‖x‖ :=
  Completion.extension_coe uniformContinuous_norm x
#align uniform_space.completion.norm_coe UniformSpace.Completion.norm_coe

instance [SeminormedAddCommGroup E] : NormedAddCommGroup (Completion E) where
  dist_eq x y := by
    induction x, y using Completion.induction_on₂
    -- ⊢ IsClosed {x | dist x.fst x.snd = ‖x.fst - x.snd‖}
    · refine' isClosed_eq (Completion.uniformContinuous_extension₂ _).continuous _
      -- ⊢ Continuous fun x => ‖x.fst - x.snd‖
      exact Continuous.comp Completion.continuous_extension continuous_sub
      -- 🎉 no goals
    · rw [← Completion.coe_sub, norm_coe, Completion.dist_eq, dist_eq_norm]
      -- 🎉 no goals

end Completion

end UniformSpace
