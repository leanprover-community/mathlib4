/-
Copyright (c) 2021 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Heather Macbeth, Johannes HÃ¶lzl, Yury Kudryashov
-/
import Mathlib.Algebra.BigOperators.Intervals
import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Topology.Instances.NNReal

#align_import analysis.normed.group.infinite_sum from "leanprover-community/mathlib"@"9a59dcb7a2d06bf55da57b9030169219980660cd"

/-!
# Infinite sums in (semi)normed groups

In a complete (semi)normed group,

- `summable_iff_vanishing_norm`: a series `âˆ‘' i, f i` is summable if and only if for any `Îµ > 0`,
  there exists a finite set `s` such that the sum `âˆ‘ i âˆˆ t, f i` over any finite set `t` disjoint
  with `s` has norm less than `Îµ`;

- `summable_of_norm_bounded`, `Summable.of_norm_bounded_eventually`: if `â€–f iâ€–` is bounded above by
  a summable series `âˆ‘' i, g i`, then `âˆ‘' i, f i` is summable as well; the same is true if the
  inequality hold only off some finite set.

- `tsum_of_norm_bounded`, `HasSum.norm_le_of_bounded`: if `â€–f iâ€– â‰¤ g i`, where `âˆ‘' i, g i` is a
  summable series, then `â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, g i`.

## Tags

infinite series, absolute convergence, normed group
-/


open Topology NNReal

open Finset Filter Metric

variable {Î¹ Î± E F : Type*} [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]

theorem cauchySeq_finset_iff_vanishing_norm {f : Î¹ â†’ E} :
    (CauchySeq fun s : Finset Î¹ => âˆ‘ i âˆˆ s, f i) â†”
      âˆ€ Îµ > (0 : â„), âˆƒ s : Finset Î¹, âˆ€ t, Disjoint t s â†’ â€–âˆ‘ i âˆˆ t, f iâ€– < Îµ := by
  rw [cauchySeq_finset_iff_sum_vanishing, nhds_basis_ball.forall_iff]
  Â· simp only [ball_zero_eq, Set.mem_setOf_eq]
  Â· rintro s t hst âŸ¨s', hs'âŸ©
    exact âŸ¨s', fun t' ht' => hst <| hs' _ ht'âŸ©
#align cauchy_seq_finset_iff_vanishing_norm cauchySeq_finset_iff_vanishing_norm

theorem summable_iff_vanishing_norm [CompleteSpace E] {f : Î¹ â†’ E} :
    Summable f â†” âˆ€ Îµ > (0 : â„), âˆƒ s : Finset Î¹, âˆ€ t, Disjoint t s â†’ â€–âˆ‘ i âˆˆ t, f iâ€– < Îµ := by
  rw [summable_iff_cauchySeq_finset, cauchySeq_finset_iff_vanishing_norm]
#align summable_iff_vanishing_norm summable_iff_vanishing_norm

theorem cauchySeq_finset_of_norm_bounded_eventually {f : Î¹ â†’ E} {g : Î¹ â†’ â„} (hg : Summable g)
    (h : âˆ€á¶  i in cofinite, â€–f iâ€– â‰¤ g i) : CauchySeq fun s => âˆ‘ i âˆˆ s, f i := by
  refine cauchySeq_finset_iff_vanishing_norm.2 fun Îµ hÎµ => ?_
  rcases summable_iff_vanishing_norm.1 hg Îµ hÎµ with âŸ¨s, hsâŸ©
  classical
  refine âŸ¨s âˆª h.toFinset, fun t ht => ?_âŸ©
  have : âˆ€ i âˆˆ t, â€–f iâ€– â‰¤ g i := by
    intro i hi
    simp only [disjoint_left, mem_union, not_or, h.mem_toFinset, Set.mem_compl_iff,
      Classical.not_not] at ht
    exact (ht hi).2
  calc
    â€–âˆ‘ i âˆˆ t, f iâ€– â‰¤ âˆ‘ i âˆˆ t, g i := norm_sum_le_of_le _ this
    _ â‰¤ â€–âˆ‘ i âˆˆ t, g iâ€– := le_abs_self _
    _ < Îµ := hs _ (ht.mono_right le_sup_left)
#align cauchy_seq_finset_of_norm_bounded_eventually cauchySeq_finset_of_norm_bounded_eventually

theorem cauchySeq_finset_of_norm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : Summable g)
    (h : âˆ€ i, â€–f iâ€– â‰¤ g i) : CauchySeq fun s : Finset Î¹ => âˆ‘ i âˆˆ s, f i :=
  cauchySeq_finset_of_norm_bounded_eventually hg <| eventually_of_forall h
#align cauchy_seq_finset_of_norm_bounded cauchySeq_finset_of_norm_bounded

/-- A version of the **direct comparison test** for conditionally convergent series.
See `cauchySeq_finset_of_norm_bounded` for the same statement about absolutely convergent ones. -/
theorem cauchySeq_range_of_norm_bounded {f : â„• â†’ E} (g : â„• â†’ â„)
    (hg : CauchySeq fun n => âˆ‘ i âˆˆ range n, g i) (hf : âˆ€ i, â€–f iâ€– â‰¤ g i) :
    CauchySeq fun n => âˆ‘ i âˆˆ range n, f i := by
  refine Metric.cauchySeq_iff'.2 fun Îµ hÎµ => ?_
  refine (Metric.cauchySeq_iff'.1 hg Îµ hÎµ).imp fun N hg n hn => ?_
  specialize hg n hn
  rw [dist_eq_norm, â† sum_Ico_eq_sub _ hn] at hg âŠ¢
  calc
    â€–âˆ‘ k âˆˆ Ico N n, f kâ€– â‰¤ âˆ‘ k âˆˆ _, â€–f kâ€– := norm_sum_le _ _
    _ â‰¤ âˆ‘ k âˆˆ _, g k := sum_le_sum fun x _ => hf x
    _ â‰¤ â€–âˆ‘ k âˆˆ _, g kâ€– := le_abs_self _
    _ < Îµ := hg
#align cauchy_seq_range_of_norm_bounded cauchySeq_range_of_norm_bounded

theorem cauchySeq_finset_of_summable_norm {f : Î¹ â†’ E} (hf : Summable fun a => â€–f aâ€–) :
    CauchySeq fun s : Finset Î¹ => âˆ‘ a âˆˆ s, f a :=
  cauchySeq_finset_of_norm_bounded _ hf fun _i => le_rfl
#align cauchy_seq_finset_of_summable_norm cauchySeq_finset_of_summable_norm

/-- If a function `f` is summable in norm, and along some sequence of finsets exhausting the space
its sum is converging to a limit `a`, then this holds along all finsets, i.e., `f` is summable
with sum `a`. -/
theorem hasSum_of_subseq_of_summable {f : Î¹ â†’ E} (hf : Summable fun a => â€–f aâ€–) {s : Î± â†’ Finset Î¹}
    {p : Filter Î±} [NeBot p] (hs : Tendsto s p atTop) {a : E}
    (ha : Tendsto (fun b => âˆ‘ i âˆˆ s b, f i) p (ğ“ a)) : HasSum f a :=
  tendsto_nhds_of_cauchySeq_of_subseq (cauchySeq_finset_of_summable_norm hf) hs ha
#align has_sum_of_subseq_of_summable hasSum_of_subseq_of_summable

theorem hasSum_iff_tendsto_nat_of_summable_norm {f : â„• â†’ E} {a : E} (hf : Summable fun i => â€–f iâ€–) :
    HasSum f a â†” Tendsto (fun n : â„• => âˆ‘ i âˆˆ range n, f i) atTop (ğ“ a) :=
  âŸ¨fun h => h.tendsto_sum_nat, fun h => hasSum_of_subseq_of_summable hf tendsto_finset_range hâŸ©
#align has_sum_iff_tendsto_nat_of_summable_norm hasSum_iff_tendsto_nat_of_summable_norm

/-- The direct comparison test for series:  if the norm of `f` is bounded by a real function `g`
which is summable, then `f` is summable. -/
theorem Summable.of_norm_bounded [CompleteSpace E] {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : Summable g)
    (h : âˆ€ i, â€–f iâ€– â‰¤ g i) : Summable f := by
  rw [summable_iff_cauchySeq_finset]
  exact cauchySeq_finset_of_norm_bounded g hg h
#align summable_of_norm_bounded Summable.of_norm_bounded

theorem HasSum.norm_le_of_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : E} {b : â„} (hf : HasSum f a)
    (hg : HasSum g b) (h : âˆ€ i, â€–f iâ€– â‰¤ g i) : â€–aâ€– â‰¤ b := by
  classical exact le_of_tendsto_of_tendsto' hf.norm hg fun _s â†¦ norm_sum_le_of_le _ fun i _hi â†¦ h i
#align has_sum.norm_le_of_bounded HasSum.norm_le_of_bounded

/-- Quantitative result associated to the direct comparison test for series:  If `âˆ‘' i, g i` is
summable, and for all `i`, `â€–f iâ€– â‰¤ g i`, then `â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, g i`. Note that we do not
assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
theorem tsum_of_norm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„} {a : â„} (hg : HasSum g a)
    (h : âˆ€ i, â€–f iâ€– â‰¤ g i) : â€–âˆ‘' i : Î¹, f iâ€– â‰¤ a := by
  by_cases hf : Summable f
  Â· exact hf.hasSum.norm_le_of_bounded hg h
  Â· rw [tsum_eq_zero_of_not_summable hf, norm_zero]
    classical exact ge_of_tendsto' hg fun s => sum_nonneg fun i _hi => (norm_nonneg _).trans (h i)
#align tsum_of_norm_bounded tsum_of_norm_bounded

/-- If `âˆ‘' i, â€–f iâ€–` is summable, then `â€–âˆ‘' i, f iâ€– â‰¤ (âˆ‘' i, â€–f iâ€–)`. Note that we do not assume
that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete space. -/
theorem norm_tsum_le_tsum_norm {f : Î¹ â†’ E} (hf : Summable fun i => â€–f iâ€–) :
    â€–âˆ‘' i, f iâ€– â‰¤ âˆ‘' i, â€–f iâ€– :=
  tsum_of_norm_bounded hf.hasSum fun _i => le_rfl
#align norm_tsum_le_tsum_norm norm_tsum_le_tsum_norm

/-- Quantitative result associated to the direct comparison test for series: If `âˆ‘' i, g i` is
summable, and for all `i`, `â€–f iâ€–â‚Š â‰¤ g i`, then `â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, g i`. Note that we
do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
theorem tsum_of_nnnorm_bounded {f : Î¹ â†’ E} {g : Î¹ â†’ â„â‰¥0} {a : â„â‰¥0} (hg : HasSum g a)
    (h : âˆ€ i, â€–f iâ€–â‚Š â‰¤ g i) : â€–âˆ‘' i : Î¹, f iâ€–â‚Š â‰¤ a := by
  simp only [â† NNReal.coe_le_coe, â† NNReal.hasSum_coe, coe_nnnorm] at *
  exact tsum_of_norm_bounded hg h
#align tsum_of_nnnorm_bounded tsum_of_nnnorm_bounded

/-- If `âˆ‘' i, â€–f iâ€–â‚Š` is summable, then `â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, â€–f iâ€–â‚Š`. Note that
we do not assume that `âˆ‘' i, f i` is summable, and it might not be the case if `Î±` is not a complete
space. -/
theorem nnnorm_tsum_le {f : Î¹ â†’ E} (hf : Summable fun i => â€–f iâ€–â‚Š) : â€–âˆ‘' i, f iâ€–â‚Š â‰¤ âˆ‘' i, â€–f iâ€–â‚Š :=
  tsum_of_nnnorm_bounded hf.hasSum fun _i => le_rfl
#align nnnorm_tsum_le nnnorm_tsum_le

variable [CompleteSpace E]

/-- Variant of the direct comparison test for series:  if the norm of `f` is eventually bounded by a
real function `g` which is summable, then `f` is summable. -/
theorem Summable.of_norm_bounded_eventually {f : Î¹ â†’ E} (g : Î¹ â†’ â„) (hg : Summable g)
    (h : âˆ€á¶  i in cofinite, â€–f iâ€– â‰¤ g i) : Summable f :=
  summable_iff_cauchySeq_finset.2 <| cauchySeq_finset_of_norm_bounded_eventually hg h
#align summable_of_norm_bounded_eventually Summable.of_norm_bounded_eventually

/-- Variant of the direct comparison test for series:  if the norm of `f` is eventually bounded by a
real function `g` which is summable, then `f` is summable. -/
theorem Summable.of_norm_bounded_eventually_nat {f : â„• â†’ E} (g : â„• â†’ â„) (hg : Summable g)
    (h : âˆ€á¶  i in atTop, â€–f iâ€– â‰¤ g i) : Summable f :=
  .of_norm_bounded_eventually g hg <| Nat.cofinite_eq_atTop â–¸ h

theorem Summable.of_nnnorm_bounded {f : Î¹ â†’ E} (g : Î¹ â†’ â„â‰¥0) (hg : Summable g)
    (h : âˆ€ i, â€–f iâ€–â‚Š â‰¤ g i) : Summable f :=
  .of_norm_bounded (fun i => (g i : â„)) (NNReal.summable_coe.2 hg) h
#align summable_of_nnnorm_bounded Summable.of_nnnorm_bounded

theorem Summable.of_norm {f : Î¹ â†’ E} (hf : Summable fun a => â€–f aâ€–) : Summable f :=
  .of_norm_bounded _ hf fun _i => le_rfl
#align summable_of_summable_norm Summable.of_norm

theorem Summable.of_nnnorm {f : Î¹ â†’ E} (hf : Summable fun a => â€–f aâ€–â‚Š) : Summable f :=
  .of_nnnorm_bounded _ hf fun _i => le_rfl
#align summable_of_summable_nnnorm Summable.of_nnnorm
