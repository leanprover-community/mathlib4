/-
Copyright (c) 2022 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.Normed.Group.Quotient
import Mathlib.Topology.Instances.AddCircle

#align_import analysis.normed.group.add_circle from "leanprover-community/mathlib"@"084f76e20c88eae536222583331abd9468b08e1c"

/-!
# The additive circle as a normed group

We define the normed group structure on `AddCircle p`, for `p : â„`. For example if `p = 1` then:
`â€–(x : AddCircle 1)â€– = |x - round x|` for any `x : â„` (see `UnitAddCircle.norm_eq`).

## Main definitions:

 * `AddCircle.norm_eq`: a characterisation of the norm on `AddCircle p`

## TODO

 * The fact `inner_product_geometry.angle (Real.cos Î¸) (Real.sin Î¸) = â€–(Î¸ : Real.Angle)â€–`

-/


noncomputable section

open Set

open Int hiding mem_zmultiples_iff

open AddSubgroup

namespace AddCircle

variable (p : â„)

instance : NormedAddCommGroup (AddCircle p) :=
  AddSubgroup.normedAddCommGroupQuotient _

@[simp]
theorem norm_coe_mul (x : â„) (t : â„) :
    â€–(â†‘(t * x) : AddCircle (t * p))â€– = |t| * â€–(x : AddCircle p)â€– := by
  have aux : âˆ€ {a b c : â„}, a âˆˆ zmultiples b â†’ c * a âˆˆ zmultiples (c * b) := fun {a b c} h => by
    simp only [mem_zmultiples_iff] at h âŠ¢
    obtain âŸ¨n, rflâŸ© := h
    exact âŸ¨n, (mul_smul_comm n c b).symmâŸ©
  rcases eq_or_ne t 0 with (rfl | ht); Â· simp
  -- âŠ¢ â€–â†‘(0 * x)â€– = |0| * â€–â†‘xâ€–
                                         -- ğŸ‰ no goals
  have ht' : |t| â‰  0 := (not_congr abs_eq_zero).mpr ht
  -- âŠ¢ â€–â†‘(t * x)â€– = |t| * â€–â†‘xâ€–
  simp only [quotient_norm_eq, Real.norm_eq_abs]
  -- âŠ¢ sInf ((fun a => |a|) '' {m | â†‘m = â†‘(t * x)}) = |t| * sInf ((fun a => |a|) '' â€¦
  conv_rhs => rw [â† smul_eq_mul, â† Real.sInf_smul_of_nonneg (abs_nonneg t)]
  -- âŠ¢ sInf ((fun a => |a|) '' {m | â†‘m = â†‘(t * x)}) = sInf (|t| â€¢ (fun a => |a|) '' â€¦
  simp only [QuotientAddGroup.mk'_apply, QuotientAddGroup.eq_iff_sub_mem]
  -- âŠ¢ sInf ((fun a => |a|) '' {m | m - t * x âˆˆ zmultiples (t * p)}) = sInf (|t| â€¢  â€¦
  congr 1
  -- âŠ¢ (fun a => |a|) '' {m | m - t * x âˆˆ zmultiples (t * p)} = |t| â€¢ (fun a => |a| â€¦
  ext z
  -- âŠ¢ z âˆˆ (fun a => |a|) '' {m | m - t * x âˆˆ zmultiples (t * p)} â†” z âˆˆ |t| â€¢ (fun  â€¦
  rw [mem_smul_set_iff_inv_smul_memâ‚€ ht']
  -- âŠ¢ z âˆˆ (fun a => |a|) '' {m | m - t * x âˆˆ zmultiples (t * p)} â†” |t|â»Â¹ â€¢ z âˆˆ (fu â€¦
  show
    (âˆƒ y, y - t * x âˆˆ zmultiples (t * p) âˆ§ |y| = z) â†” âˆƒ w, w - x âˆˆ zmultiples p âˆ§ |w| = |t|â»Â¹ * z
  constructor
  -- âŠ¢ (âˆƒ y, y - t * x âˆˆ zmultiples (t * p) âˆ§ |y| = z) â†’ âˆƒ w, w - x âˆˆ zmultiples p  â€¦
  Â· rintro âŸ¨y, hy, rflâŸ©
    -- âŠ¢ âˆƒ w, w - x âˆˆ zmultiples p âˆ§ |w| = |t|â»Â¹ * |y|
    refine' âŸ¨tâ»Â¹ * y, _, by rw [abs_mul, abs_inv]âŸ©
    -- âŠ¢ tâ»Â¹ * y - x âˆˆ zmultiples p
    rw [â† inv_mul_cancel_leftâ‚€ ht x, â† inv_mul_cancel_leftâ‚€ ht p, â† mul_sub]
    -- âŠ¢ tâ»Â¹ * (y - t * x) âˆˆ zmultiples (tâ»Â¹ * (t * p))
    exact aux hy
    -- ğŸ‰ no goals
  Â· rintro âŸ¨w, hw, hw'âŸ©
    -- âŠ¢ âˆƒ y, y - t * x âˆˆ zmultiples (t * p) âˆ§ |y| = z
    refine' âŸ¨t * w, _, by rw [â† (eq_inv_mul_iff_mul_eqâ‚€ ht').mp hw', abs_mul]âŸ©
    -- âŠ¢ t * w - t * x âˆˆ zmultiples (t * p)
    rw [â† mul_sub]
    -- âŠ¢ t * (w - x) âˆˆ zmultiples (t * p)
    exact aux hw
    -- ğŸ‰ no goals
#align add_circle.norm_coe_mul AddCircle.norm_coe_mul

theorem norm_neg_period (x : â„) : â€–(x : AddCircle (-p))â€– = â€–(x : AddCircle p)â€– := by
  suffices â€–(â†‘(-1 * x) : AddCircle (-1 * p))â€– = â€–(x : AddCircle p)â€– by
    rw [â† this, neg_one_mul]
    simp
  simp only [norm_coe_mul, abs_neg, abs_one, one_mul]
  -- ğŸ‰ no goals
#align add_circle.norm_neg_period AddCircle.norm_neg_period

@[simp]
theorem norm_eq_of_zero {x : â„} : â€–(x : AddCircle (0 : â„))â€– = |x| := by
  suffices { y : â„ | (y : AddCircle (0 : â„)) = (x : AddCircle (0 : â„)) } = {x} by
    rw [quotient_norm_eq, this, image_singleton, Real.norm_eq_abs, csInf_singleton]
  ext y
  -- âŠ¢ y âˆˆ {y | â†‘y = â†‘x} â†” y âˆˆ {x}
  simp [QuotientAddGroup.eq_iff_sub_mem, mem_zmultiples_iff, sub_eq_zero]
  -- ğŸ‰ no goals
#align add_circle.norm_eq_of_zero AddCircle.norm_eq_of_zero

theorem norm_eq {x : â„} : â€–(x : AddCircle p)â€– = |x - round (pâ»Â¹ * x) * p| := by
  suffices âˆ€ x : â„, â€–(x : AddCircle (1 : â„))â€– = |x - round x| by
    rcases eq_or_ne p 0 with (rfl | hp)
    Â· simp
    intros
    have hx := norm_coe_mul p x pâ»Â¹
    rw [abs_inv, eq_inv_mul_iff_mul_eqâ‚€ ((not_congr abs_eq_zero).mpr hp)] at hx
    rw [â† hx, inv_mul_cancel hp, this, â† abs_mul, mul_sub, mul_inv_cancel_leftâ‚€ hp, mul_comm p]
  clear! x p
  -- âŠ¢ âˆ€ (x : â„), â€–â†‘xâ€– = |x - â†‘(round x)|
  intros x
  -- âŠ¢ â€–â†‘xâ€– = |x - â†‘(round x)|
  rw [quotient_norm_eq, abs_sub_round_eq_min]
  -- âŠ¢ sInf (norm '' {m | â†‘m = â†‘x}) = min (fract x) (1 - fract x)
  have hâ‚ : BddBelow (abs '' { m : â„ | (m : AddCircle (1 : â„)) = x }) :=
    âŸ¨0, by simp [mem_lowerBounds]âŸ©
  have hâ‚‚ : (abs '' { m : â„ | (m : AddCircle (1 : â„)) = x }).Nonempty := âŸ¨|x|, âŸ¨x, rfl, rflâŸ©âŸ©
  -- âŠ¢ sInf (norm '' {m | â†‘m = â†‘x}) = min (fract x) (1 - fract x)
  apply le_antisymm
  -- âŠ¢ sInf (norm '' {m | â†‘m = â†‘x}) â‰¤ min (fract x) (1 - fract x)
  Â· simp_rw [Real.norm_eq_abs, csInf_le_iff hâ‚ hâ‚‚, le_min_iff]
    -- âŠ¢ âˆ€ (b : â„), b âˆˆ lowerBounds (abs '' {m | â†‘m = â†‘x}) â†’ b â‰¤ fract x âˆ§ b â‰¤ 1 - fr â€¦
    intro b h
    -- âŠ¢ b â‰¤ fract x âˆ§ b â‰¤ 1 - fract x
    refine'
      âŸ¨mem_lowerBounds.1 h _ âŸ¨fract x, _, abs_fractâŸ©,
        mem_lowerBounds.1 h _ âŸ¨fract x - 1, _, by rw [abs_sub_comm, abs_one_sub_fract]âŸ©âŸ©
    Â· simp only [mem_setOf, fract, sub_eq_self, QuotientAddGroup.mk_sub,
        QuotientAddGroup.eq_zero_iff, int_cast_mem_zmultiples_one]
    Â· simp only [mem_setOf, fract, sub_eq_self, QuotientAddGroup.mk_sub,
        QuotientAddGroup.eq_zero_iff, int_cast_mem_zmultiples_one, sub_sub,
        (by norm_cast : (âŒŠxâŒ‹ : â„) + 1 = (â†‘(âŒŠxâŒ‹ + 1) : â„))]
  Â· simp only [QuotientAddGroup.mk'_apply, Real.norm_eq_abs, le_csInf_iff hâ‚ hâ‚‚]
    -- âŠ¢ âˆ€ (b : â„), b âˆˆ abs '' {m | â†‘m = â†‘x} â†’ min (fract x) (1 - fract x) â‰¤ b
    rintro b' âŸ¨b, hb, rflâŸ©
    -- âŠ¢ min (fract x) (1 - fract x) â‰¤ |b|
    simp only [mem_setOf, QuotientAddGroup.eq_iff_sub_mem, mem_zmultiples_iff,
      smul_one_eq_coe] at hb
    obtain âŸ¨z, hzâŸ© := hb
    -- âŠ¢ min (fract x) (1 - fract x) â‰¤ |b|
    rw [(by rw [hz]; abel : x = b - z), fract_sub_int, â† abs_sub_round_eq_min]
    -- âŠ¢ |b - â†‘(round b)| â‰¤ |b|
    convert round_le b 0
    -- âŠ¢ b = b - â†‘0
    simp
    -- ğŸ‰ no goals
#align add_circle.norm_eq AddCircle.norm_eq

theorem norm_eq' (hp : 0 < p) {x : â„} : â€–(x : AddCircle p)â€– = p * |pâ»Â¹ * x - round (pâ»Â¹ * x)| := by
  conv_rhs =>
    congr
    rw [â† abs_eq_self.mpr hp.le]
  rw [â† abs_mul, mul_sub, mul_inv_cancel_leftâ‚€ hp.ne.symm, norm_eq, mul_comm p]
  -- ğŸ‰ no goals
#align add_circle.norm_eq' AddCircle.norm_eq'

theorem norm_le_half_period {x : AddCircle p} (hp : p â‰  0) : â€–xâ€– â‰¤ |p| / 2 := by
  obtain âŸ¨xâŸ© := x
  -- âŠ¢ â€–Quot.mk Setoid.r xâ€– â‰¤ |p| / 2
  change â€–(x : AddCircle p)â€– â‰¤ |p| / 2
  -- âŠ¢ â€–â†‘xâ€– â‰¤ |p| / 2
  rw [norm_eq, â† mul_le_mul_left (abs_pos.mpr (inv_ne_zero hp)), â† abs_mul, mul_sub, mul_left_comm,
    â† mul_div_assoc, â† abs_mul, inv_mul_cancel hp, mul_one, abs_one]
  exact abs_sub_round (pâ»Â¹ * x)
  -- ğŸ‰ no goals
#align add_circle.norm_le_half_period AddCircle.norm_le_half_period

@[simp]
theorem norm_half_period_eq : â€–(â†‘(p / 2) : AddCircle p)â€– = |p| / 2 := by
  rcases eq_or_ne p 0 with (rfl | hp); Â· simp
  -- âŠ¢ â€–â†‘(0 / 2)â€– = |0| / 2
                                         -- ğŸ‰ no goals
  rw [norm_eq, â† mul_div_assoc, inv_mul_cancel hp, one_div, round_two_inv, Int.cast_one,
    one_mul, (by linarith : p / 2 - p = -(p / 2)), abs_neg, abs_div, abs_two]
#align add_circle.norm_half_period_eq AddCircle.norm_half_period_eq

theorem norm_coe_eq_abs_iff {x : â„} (hp : p â‰  0) : â€–(x : AddCircle p)â€– = |x| â†” |x| â‰¤ |p| / 2 := by
  refine' âŸ¨fun hx => hx â–¸ norm_le_half_period p hp, fun hx => _âŸ©
  -- âŠ¢ â€–â†‘xâ€– = |x|
  suffices âˆ€ p : â„, 0 < p â†’ |x| â‰¤ p / 2 â†’ â€–(x : AddCircle p)â€– = |x| by
    -- Porting note: replaced `lt_trichotomy` which had trouble substituting `p = 0`.
    rcases hp.symm.lt_or_lt with (hp | hp)
    Â· rw [abs_eq_self.mpr hp.le] at hx
      exact this p hp hx
    Â· rw [â† norm_neg_period]
      rw [abs_eq_neg_self.mpr hp.le] at hx
      exact this (-p) (neg_pos.mpr hp) hx
  clear hx
  -- âŠ¢ âˆ€ (p : â„), 0 < p â†’ |x| â‰¤ p / 2 â†’ â€–â†‘xâ€– = |x|
  intro p hp hx
  -- âŠ¢ â€–â†‘xâ€– = |x|
  rcases eq_or_ne x (p / (2 : â„)) with (rfl | hx')
  -- âŠ¢ â€–â†‘(p / 2)â€– = |p / 2|
  Â· simp [abs_div, abs_two]
    -- ğŸ‰ no goals
  suffices round (pâ»Â¹ * x) = 0 by simp [norm_eq, this]
  -- âŠ¢ round (pâ»Â¹ * x) = 0
  rw [round_eq_zero_iff]
  -- âŠ¢ pâ»Â¹ * x âˆˆ Ico (-(1 / 2)) (1 / 2)
  obtain âŸ¨hxâ‚, hxâ‚‚âŸ© := abs_le.mp hx
  -- âŠ¢ pâ»Â¹ * x âˆˆ Ico (-(1 / 2)) (1 / 2)
  replace hxâ‚‚ := Ne.lt_of_le hx' hxâ‚‚
  -- âŠ¢ pâ»Â¹ * x âˆˆ Ico (-(1 / 2)) (1 / 2)
  constructor
  -- âŠ¢ -(1 / 2) â‰¤ pâ»Â¹ * x
  Â· rwa [â† mul_le_mul_left hp, â† mul_assoc, mul_inv_cancel hp.ne.symm, one_mul, mul_neg, â†
      mul_div_assoc, mul_one]
  Â· rwa [â† mul_lt_mul_left hp, â† mul_assoc, mul_inv_cancel hp.ne.symm, one_mul, â† mul_div_assoc,
      mul_one]
#align add_circle.norm_coe_eq_abs_iff AddCircle.norm_coe_eq_abs_iff

open Metric

theorem closedBall_eq_univ_of_half_period_le (hp : p â‰  0) (x : AddCircle p) {Îµ : â„}
    (hÎµ : |p| / 2 â‰¤ Îµ) : closedBall x Îµ = univ :=
  eq_univ_iff_forall.mpr fun x => by
    simpa only [mem_closedBall, dist_eq_norm] using (norm_le_half_period p hp).trans hÎµ
    -- ğŸ‰ no goals
#align add_circle.closed_ball_eq_univ_of_half_period_le AddCircle.closedBall_eq_univ_of_half_period_le

@[simp]
theorem coe_real_preimage_closedBall_period_zero (x Îµ : â„) :
    (â†‘) â»Â¹' closedBall (x : AddCircle (0 : â„)) Îµ = closedBall x Îµ := by
  ext y
  -- âŠ¢ y âˆˆ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ â†” y âˆˆ closedBall x Îµ
  -- Porting note: squeezed the simp
  simp only [Set.mem_preimage, dist_eq_norm, AddCircle.norm_eq_of_zero, iff_self,
    â† QuotientAddGroup.mk_sub, Metric.mem_closedBall, Real.norm_eq_abs]
#align add_circle.coe_real_preimage_closed_ball_period_zero AddCircle.coe_real_preimage_closedBall_period_zero

theorem coe_real_preimage_closedBall_eq_iUnion (x Îµ : â„) :
    (â†‘) â»Â¹' closedBall (x : AddCircle p) Îµ = â‹ƒ z : â„¤, closedBall (x + z â€¢ p) Îµ := by
  rcases eq_or_ne p 0 with (rfl | hp)
  -- âŠ¢ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ = â‹ƒ (z : â„¤), closedBall (x + z â€¢ 0 â€¦
  Â· simp [iUnion_const]
    -- ğŸ‰ no goals
  ext y
  -- âŠ¢ y âˆˆ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ â†” y âˆˆ â‹ƒ (z : â„¤), closedBall (x â€¦
  simp only [dist_eq_norm, mem_preimage, mem_closedBall, zsmul_eq_mul, mem_iUnion, Real.norm_eq_abs,
    â† QuotientAddGroup.mk_sub, norm_eq, â† sub_sub]
  refine' âŸ¨fun h => âŸ¨round (pâ»Â¹ * (y - x)), hâŸ©, _âŸ©
  -- âŠ¢ (âˆƒ i, |y - x - â†‘i * p| â‰¤ Îµ) â†’ |y - x - â†‘(round (pâ»Â¹ * (y - x))) * p| â‰¤ Îµ
  rintro âŸ¨n, hnâŸ©
  -- âŠ¢ |y - x - â†‘(round (pâ»Â¹ * (y - x))) * p| â‰¤ Îµ
  rw [â† mul_le_mul_left (abs_pos.mpr <| inv_ne_zero hp), â† abs_mul, mul_sub, mul_comm _ p,
    inv_mul_cancel_leftâ‚€ hp] at hn âŠ¢
  exact (round_le (pâ»Â¹ * (y - x)) n).trans hn
  -- ğŸ‰ no goals
#align add_circle.coe_real_preimage_closed_ball_eq_Union AddCircle.coe_real_preimage_closedBall_eq_iUnion

theorem coe_real_preimage_closedBall_inter_eq {x Îµ : â„} (s : Set â„)
    (hs : s âŠ† closedBall x (|p| / 2)) :
    (â†‘) â»Â¹' closedBall (x : AddCircle p) Îµ âˆ© s = if Îµ < |p| / 2 then closedBall x Îµ âˆ© s else s := by
  cases' le_or_lt (|p| / 2) Îµ with hÎµ hÎµ
  -- âŠ¢ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ âˆ© s = if Îµ < |p| / 2 then closedBa â€¦
  Â· rcases eq_or_ne p 0 with (rfl | hp)
    -- âŠ¢ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ âˆ© s = if Îµ < |0| / 2 then closedBa â€¦
    Â· simp only [abs_zero, zero_div] at hÎµ
      -- âŠ¢ QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ âˆ© s = if Îµ < |0| / 2 then closedBa â€¦
      simp only [not_lt.mpr hÎµ, coe_real_preimage_closedBall_period_zero, abs_zero, zero_div,
        if_false, inter_eq_right_iff_subset]
      exact hs.trans (closedBall_subset_closedBall <| by simp [hÎµ])
      -- ğŸ‰ no goals
    -- Porting note: was
    -- simp [closedBall_eq_univ_of_half_period_le p hp (â†‘x) hÎµ, not_lt.mpr hÎµ]
    simp only [not_lt.mpr hÎµ, ite_false, inter_eq_right_iff_subset]
    -- âŠ¢ s âŠ† QuotientAddGroup.mk â»Â¹' closedBall (â†‘x) Îµ
    rw [closedBall_eq_univ_of_half_period_le p hp (â†‘x : â„ â§¸ zmultiples p) hÎµ, preimage_univ]
    -- âŠ¢ s âŠ† univ
    apply subset_univ
    -- ğŸ‰ no goals
  Â· suffices âˆ€ z : â„¤, closedBall (x + z â€¢ p) Îµ âˆ© s = if z = 0 then closedBall x Îµ âˆ© s else âˆ… by
      simp [-zsmul_eq_mul, â† QuotientAddGroup.mk_zero, coe_real_preimage_closedBall_eq_iUnion,
        iUnion_inter, iUnion_ite, this, hÎµ]
    intro z
    -- âŠ¢ closedBall (x + z â€¢ p) Îµ âˆ© s = if z = 0 then closedBall x Îµ âˆ© s else âˆ…
    simp only [Real.closedBall_eq_Icc, zero_sub, zero_add] at hs âŠ¢
    -- âŠ¢ Icc (x + z â€¢ p - Îµ) (x + z â€¢ p + Îµ) âˆ© s = if z = 0 then Icc (x - Îµ) (x + Îµ)  â€¦
    rcases eq_or_ne z 0 with (rfl | hz)
    -- âŠ¢ Icc (x + 0 â€¢ p - Îµ) (x + 0 â€¢ p + Îµ) âˆ© s = if 0 = 0 then Icc (x - Îµ) (x + Îµ)  â€¦
    Â· simp
      -- ğŸ‰ no goals
    simp only [hz, zsmul_eq_mul, if_false, eq_empty_iff_forall_not_mem]
    -- âŠ¢ âˆ€ (x_1 : â„), Â¬x_1 âˆˆ Icc (x + â†‘z * p - Îµ) (x + â†‘z * p + Îµ) âˆ© s
    rintro y âŸ¨âŸ¨hyâ‚, hyâ‚‚âŸ©, hyâ‚€âŸ©
    -- âŠ¢ False
    obtain âŸ¨hyâ‚ƒ, hyâ‚„âŸ© := hs hyâ‚€
    -- âŠ¢ False
    rcases lt_trichotomy 0 p with (hp | (rfl : 0 = p) | hp)
    Â· cases' Int.cast_le_neg_one_or_one_le_cast_of_ne_zero â„ hz with hz' hz'
      -- âŠ¢ False
      Â· have : â†‘z * p â‰¤ -p
        -- âŠ¢ â†‘z * p â‰¤ -p
        nlinarith
        -- âŠ¢ False
        linarith [abs_eq_self.mpr hp.le]
        -- ğŸ‰ no goals
      Â· have : p â‰¤ â†‘z * p
        -- âŠ¢ p â‰¤ â†‘z * p
        nlinarith
        -- âŠ¢ False
        linarith [abs_eq_self.mpr hp.le]
        -- ğŸ‰ no goals
    Â· simp only [mul_zero, add_zero, abs_zero, zero_div] at hyâ‚ hyâ‚‚ hÎµ
      -- âŠ¢ False
      linarith
      -- ğŸ‰ no goals
    Â· cases' Int.cast_le_neg_one_or_one_le_cast_of_ne_zero â„ hz with hz' hz'
      -- âŠ¢ False
      Â· have : -p â‰¤ â†‘z * p
        -- âŠ¢ -p â‰¤ â†‘z * p
        nlinarith
        -- âŠ¢ False
        linarith [abs_eq_neg_self.mpr hp.le]
        -- ğŸ‰ no goals
      Â· have : â†‘z * p â‰¤ p
        -- âŠ¢ â†‘z * p â‰¤ p
        nlinarith
        -- âŠ¢ False
        linarith [abs_eq_neg_self.mpr hp.le]
        -- ğŸ‰ no goals
#align add_circle.coe_real_preimage_closed_ball_inter_eq AddCircle.coe_real_preimage_closedBall_inter_eq

section FiniteOrderPoints

variable {p} [hp : Fact (0 < p)]

theorem norm_div_nat_cast {m n : â„•} :
    â€–(â†‘(â†‘m / â†‘n * p) : AddCircle p)â€– = p * (â†‘(min (m % n) (n - m % n)) / n) := by
  have : pâ»Â¹ * (â†‘m / â†‘n * p) = â†‘m / â†‘n := by rw [mul_comm _ p, inv_mul_cancel_leftâ‚€ hp.out.ne.symm]
  -- âŠ¢ â€–â†‘(â†‘m / â†‘n * p)â€– = p * (â†‘(min (m % n) (n - m % n)) / â†‘n)
  rw [norm_eq' p hp.out, this, abs_sub_round_div_natCast_eq]
  -- ğŸ‰ no goals
#align add_circle.norm_div_nat_cast AddCircle.norm_div_nat_cast

theorem exists_norm_eq_of_isOfFinAddOrder {u : AddCircle p} (hu : IsOfFinAddOrder u) :
    âˆƒ k : â„•, â€–uâ€– = p * (k / addOrderOf u) := by
  let n := addOrderOf u
  -- âŠ¢ âˆƒ k, â€–uâ€– = p * (â†‘k / â†‘(addOrderOf u))
  change âˆƒ k : â„•, â€–uâ€– = p * (k / n)
  -- âŠ¢ âˆƒ k, â€–uâ€– = p * (â†‘k / â†‘n)
  obtain âŸ¨m, -, -, hmâŸ© := exists_gcd_eq_one_of_isOfFinAddOrder hu
  -- âŠ¢ âˆƒ k, â€–uâ€– = p * (â†‘k / â†‘n)
  refine' âŸ¨min (m % n) (n - m % n), _âŸ©
  -- âŠ¢ â€–uâ€– = p * (â†‘(min (m % n) (n - m % n)) / â†‘n)
  rw [â† hm, norm_div_nat_cast]
  -- ğŸ‰ no goals
#align add_circle.exists_norm_eq_of_fin_add_order AddCircle.exists_norm_eq_of_isOfFinAddOrder

theorem le_add_order_smul_norm_of_isOfFinAddOrder {u : AddCircle p} (hu : IsOfFinAddOrder u)
    (hu' : u â‰  0) : p â‰¤ addOrderOf u â€¢ â€–uâ€– := by
  obtain âŸ¨n, hnâŸ© := exists_norm_eq_of_isOfFinAddOrder hu
  -- âŠ¢ p â‰¤ addOrderOf u â€¢ â€–uâ€–
  replace hu : (addOrderOf u : â„) â‰  0
  -- âŠ¢ â†‘(addOrderOf u) â‰  0
  Â· norm_cast
    -- âŠ¢ Â¬addOrderOf u = 0
    exact (addOrderOf_pos_iff.mpr hu).ne'
    -- ğŸ‰ no goals
  conv_lhs => rw [â† mul_one p]
  -- âŠ¢ p * 1 â‰¤ addOrderOf u â€¢ â€–uâ€–
  rw [hn, nsmul_eq_mul, â† mul_assoc, mul_comm _ p, mul_assoc, mul_div_cancel' _ hu,
    mul_le_mul_left hp.out, Nat.one_le_cast, Nat.one_le_iff_ne_zero]
  contrapose! hu'
  -- âŠ¢ u = 0
  simpa only [hu', Nat.cast_zero, zero_div, mul_zero, norm_eq_zero] using hn
  -- ğŸ‰ no goals
#align add_circle.le_add_order_smul_norm_of_is_of_fin_add_order AddCircle.le_add_order_smul_norm_of_isOfFinAddOrder

end FiniteOrderPoints

end AddCircle

namespace UnitAddCircle

theorem norm_eq {x : â„} : â€–(x : UnitAddCircle)â€– = |x - round x| := by simp [AddCircle.norm_eq]
                                                                      -- ğŸ‰ no goals
#align unit_add_circle.norm_eq UnitAddCircle.norm_eq

end UnitAddCircle
