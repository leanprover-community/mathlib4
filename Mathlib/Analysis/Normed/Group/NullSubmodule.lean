/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto
-/
import Mathlib.Analysis.Normed.Group.Continuity
import Mathlib.Analysis.Normed.MulAction

/-!
# The null subgroup in a seminormed commutative group

For any `SeminormedAddCommGroup M`, the quotient `SeparationQuotient M` by the null subgroup is
defined as a `NormedAddCommGroup` instance in `Mathlib/Analysis/Normed/Group/Uniform.lean`. Here we
define the null space as a subgroup.

## Main definitions

We use `M` to denote seminormed groups.

* `nullAddSubgroup` : the subgroup of elements `x` with `â€–xâ€– = 0`.

If `E` is a vector space over `ğ•œ` with an appropriate continuous action, we also define the null
subspace as a submodule of `E`.

* `nullSubmodule` : the subspace of elements `x` with `â€–xâ€– = 0`.

-/

variable {M : Type*} [SeminormedCommGroup M]

variable (M) in
/-- The null subgroup with respect to the norm. -/
@[to_additive "The additive null subgroup with respect to the norm."]
def nullSubgroup : Subgroup M where
  carrier := {x : M | â€–xâ€– = 0}
  mul_mem' {x y} (hx : â€–xâ€– = 0) (hy : â€–yâ€– = 0) := by
    apply le_antisymm _ (norm_nonneg' _)
    refine (norm_mul_le' x y).trans_eq ?_
    rw [hx, hy, add_zero]
  one_mem' := norm_one'
  inv_mem' {x} (hx : â€–xâ€– = 0) := by simpa only [Set.mem_setOf_eq, norm_inv'] using hx

@[to_additive]
lemma isClosed_nullSubgroup : IsClosed (nullSubgroup M : Set M) := by
  apply isClosed_singleton.preimage continuous_norm'

@[to_additive (attr := simp)]
lemma mem_nullSubgroup_iff {x : M} : x âˆˆ nullSubgroup M â†” â€–xâ€– = 0 := Iff.rfl

variable {ğ•œ E : Type*}
variable [SeminormedAddCommGroup E] [SeminormedRing ğ•œ] [Module ğ•œ E] [IsBoundedSMul ğ•œ E]

variable (ğ•œ E) in
/-- The null space with respect to the norm. -/
def nullSubmodule : Submodule ğ•œ E where
  __ := nullAddSubgroup E
  smul_mem' c x (hx : â€–xâ€– = 0) := by
    apply le_antisymm _ (norm_nonneg _)
    refine (norm_smul_le _ _).trans_eq ?_
    rw [hx, mul_zero]

lemma isClosed_nullSubmodule : IsClosed (nullSubmodule ğ•œ E : Set E) := isClosed_nullAddSubgroup

@[simp]
lemma mem_nullSubmodule_iff {x : E} : x âˆˆ nullSubmodule ğ•œ E â†” â€–xâ€– = 0 := Iff.rfl
