/-
Copyright (c) 2024 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/

import Mathlib.Analysis.Normed.Group.Basic
import Mathlib.Topology.ContinuousMap.CocompactMap
import Mathlib.Topology.MetricSpace.Bounded

/-!
# Cocompact maps in normed groups

This file gives a characterization of cocompact maps in terms of norm estimates.

## Main statements

* `CocompactMapClass.norm_le`: Every cocompact map satisfies a norm estimate
* `ContinuousMapClass.toCocompactMapClass_of_norm`: Conversely, this norm estimate implies that a
  map is cocompact.

-/

open Filter Metric

variable {ğ•œ E F ğ“• : Type*}
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable {f : ğ“•}

theorem CocompactMapClass.norm_le [ProperSpace F] [FunLike ğ“• E F] [CocompactMapClass ğ“• E F]
    (Îµ : â„) : âˆƒ r : â„, âˆ€ x : E, r < â€–xâ€– â†’ Îµ < â€–f xâ€– := by
  have h := cocompact_tendsto f
  rw [tendsto_def] at h
  specialize h (Metric.closedBall 0 Îµ)á¶œ (mem_cocompact_of_closedBall_compl_subset 0 âŸ¨Îµ, rfl.subsetâŸ©)
  rcases closedBall_compl_subset_of_mem_cocompact h 0 with âŸ¨r, hrâŸ©
  use r
  intro x hx
  suffices x âˆˆ fâ»Â¹' (Metric.closedBall 0 Îµ)á¶œ by simp_all
  apply hr
  simp [hx]

theorem Filter.tendsto_cocompact_cocompact_of_norm [ProperSpace E] {f : E â†’ F}
    (h : âˆ€ Îµ : â„, âˆƒ r : â„, âˆ€ x : E, r < â€–xâ€– â†’ Îµ < â€–f xâ€–) :
    Tendsto f (cocompact E) (cocompact F) := by
  rw [tendsto_def]
  intro s hs
  rcases closedBall_compl_subset_of_mem_cocompact hs 0 with âŸ¨Îµ, hÎµâŸ©
  rcases h Îµ with âŸ¨r, hrâŸ©
  apply mem_cocompact_of_closedBall_compl_subset 0
  use r
  intro x hx
  simp only [Set.mem_compl_iff, Metric.mem_closedBall, dist_zero_right, not_le] at hx
  apply hÎµ
  simp [hr x hx]

theorem ContinuousMapClass.toCocompactMapClass_of_norm [ProperSpace E] [FunLike ğ“• E F]
    [ContinuousMapClass ğ“• E F] (h : âˆ€ (f : ğ“•) (Îµ : â„), âˆƒ r : â„, âˆ€ x : E, r < â€–xâ€– â†’ Îµ < â€–f xâ€–) :
    CocompactMapClass ğ“• E F where
  cocompact_tendsto := (tendsto_cocompact_cocompact_of_norm <| h Â·)
