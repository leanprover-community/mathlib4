/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes HÃ¶lzl, YaÃ«l Dillies
-/
import Mathlib.Analysis.Normed.Group.Continuity
import Mathlib.Topology.Algebra.IsUniformGroup.Basic
import Mathlib.Topology.MetricSpace.Algebra
import Mathlib.Topology.MetricSpace.IsometricSMul

/-!
# Normed groups are uniform groups

This file proves lipschitzness of normed group operations and shows that normed groups are uniform
groups.
-/

variable {ğ“• E F : Type*}

open Filter Function Metric Bornology
open scoped ENNReal NNReal Uniformity Pointwise Topology

section SeminormedGroup
variable [SeminormedGroup E] [SeminormedGroup F] {s : Set E} {a b : E} {r : â„}

@[to_additive]
instance NormedGroup.to_isIsometricSMul_right : IsIsometricSMul Eáµáµ’áµ– E :=
  âŸ¨fun a â†¦ Isometry.of_dist_eq fun b c â†¦ by simp [dist_eq_norm_div]âŸ©

@[to_additive]
theorem Isometry.norm_map_of_map_one {f : E â†’ F} (hi : Isometry f) (hâ‚ : f 1 = 1) (x : E) :
    â€–f xâ€– = â€–xâ€– := by rw [â† dist_one_right, â† hâ‚, hi.dist_eq, dist_one_right]

@[to_additive (attr := simp) norm_map]
theorem norm_map' [FunLike ğ“• E F] [IsometryClass ğ“• E F] [OneHomClass ğ“• E F] (f : ğ“•) (x : E) :
    â€–f xâ€– = â€–xâ€– :=
  (IsometryClass.isometry f).norm_map_of_map_one (map_one f) x

@[to_additive (attr := simp) nnnorm_map]
theorem nnnorm_map' [FunLike ğ“• E F] [IsometryClass ğ“• E F] [OneHomClass ğ“• E F] (f : ğ“•) (x : E) :
    â€–f xâ€–â‚Š = â€–xâ€–â‚Š :=
  NNReal.eq <| norm_map' f x

@[to_additive (attr := simp)]
theorem dist_mul_self_right (a b : E) : dist b (a * b) = â€–aâ€– := by
  rw [â† dist_one_left, â† dist_mul_right 1 a b, one_mul]

@[to_additive (attr := simp)]
theorem dist_mul_self_left (a b : E) : dist (a * b) b = â€–aâ€– := by
  rw [dist_comm, dist_mul_self_right]

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_left (a b c : E) : dist (a / b) c = dist a (c * b) := by
  rw [â† dist_mul_right _ _ b, div_mul_cancel]

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_right (a b c : E) : dist a (b / c) = dist (a * c) b := by
  rw [â† dist_mul_right _ _ c, div_mul_cancel]

open Finset

variable [FunLike ğ“• E F]

/-- A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant `C` such that
for all `x`, one has `â€–f xâ€– â‰¤ C * â€–xâ€–`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`. -/
@[to_additive /-- A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant
`C` such that for all `x`, one has `â€–f xâ€– â‰¤ C * â€–xâ€–`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`. -/]
theorem MonoidHomClass.lipschitz_of_bound [MonoidHomClass ğ“• E F] (f : ğ“•) (C : â„)
    (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) : LipschitzWith (Real.toNNReal C) f :=
  LipschitzWith.of_dist_le' fun x y â†¦ by simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive]
theorem lipschitzOnWith_iff_norm_div_le {f : E â†’ F} {C : â„â‰¥0} :
    LipschitzOnWith C f s â†” âˆ€ â¦ƒxâ¦„, x âˆˆ s â†’ âˆ€ â¦ƒyâ¦„, y âˆˆ s â†’ â€–f x / f yâ€– â‰¤ C * â€–x / yâ€– := by
  simp only [lipschitzOnWith_iff_dist_le_mul, dist_eq_norm_div]

alias âŸ¨LipschitzOnWith.norm_div_le, _âŸ© := lipschitzOnWith_iff_norm_div_le

attribute [to_additive] LipschitzOnWith.norm_div_le

@[to_additive]
theorem LipschitzOnWith.norm_div_le_of_le {f : E â†’ F} {C : â„â‰¥0} (h : LipschitzOnWith C f s)
    (ha : a âˆˆ s) (hb : b âˆˆ s) (hr : â€–a / bâ€– â‰¤ r) : â€–f a / f bâ€– â‰¤ C * r :=
  (h.norm_div_le ha hb).trans <| by gcongr

@[to_additive]
theorem lipschitzWith_iff_norm_div_le {f : E â†’ F} {C : â„â‰¥0} :
    LipschitzWith C f â†” âˆ€ x y, â€–f x / f yâ€– â‰¤ C * â€–x / yâ€– := by
  simp only [lipschitzWith_iff_dist_le_mul, dist_eq_norm_div]

alias âŸ¨LipschitzWith.norm_div_le, _âŸ© := lipschitzWith_iff_norm_div_le

attribute [to_additive] LipschitzWith.norm_div_le

@[to_additive]
theorem LipschitzWith.norm_div_le_of_le {f : E â†’ F} {C : â„â‰¥0} (h : LipschitzWith C f)
    (hr : â€–a / bâ€– â‰¤ r) : â€–f a / f bâ€– â‰¤ C * r :=
  (h.norm_div_le _ _).trans <| by gcongr

/-- A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C` such that
for all `x`, one has `â€–f xâ€– â‰¤ C * â€–xâ€–`. -/
@[to_additive /-- A homomorphism `f` of seminormed groups is continuous, if there exists a constant
`C` such that for all `x`, one has `â€–f xâ€– â‰¤ C * â€–xâ€–`. -/]
theorem MonoidHomClass.continuous_of_bound [MonoidHomClass ğ“• E F] (f : ğ“•) (C : â„)
    (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) : Continuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).continuous

@[to_additive]
theorem MonoidHomClass.uniformContinuous_of_bound [MonoidHomClass ğ“• E F] (f : ğ“•) (C : â„)
    (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) : UniformContinuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).uniformContinuous

@[to_additive]
theorem MonoidHomClass.isometry_iff_norm [MonoidHomClass ğ“• E F] (f : ğ“•) :
    Isometry f â†” âˆ€ x, â€–f xâ€– = â€–xâ€– := by
  simp only [isometry_iff_dist_eq, dist_eq_norm_div, â† map_div]
  refine âŸ¨fun h x â†¦ ?_, fun h x y â†¦ h _âŸ©
  simpa using h x 1

alias âŸ¨_, MonoidHomClass.isometry_of_normâŸ© := MonoidHomClass.isometry_iff_norm

attribute [to_additive] MonoidHomClass.isometry_of_norm

section NNNorm

@[to_additive]
theorem MonoidHomClass.lipschitz_of_bound_nnnorm [MonoidHomClass ğ“• E F] (f : ğ“•) (C : â„â‰¥0)
    (h : âˆ€ x, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š) : LipschitzWith C f :=
  @Real.toNNReal_coe C â–¸ MonoidHomClass.lipschitz_of_bound f C h

@[to_additive]
theorem MonoidHomClass.antilipschitz_of_bound [MonoidHomClass ğ“• E F] (f : ğ“•) {K : â„â‰¥0}
    (h : âˆ€ x, â€–xâ€– â‰¤ K * â€–f xâ€–) : AntilipschitzWith K f :=
  AntilipschitzWith.of_le_mul_dist fun x y â†¦ by
    simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive LipschitzWith.norm_le_mul]
theorem LipschitzWith.norm_le_mul' {f : E â†’ F} {K : â„â‰¥0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : â€–f xâ€– â‰¤ K * â€–xâ€– := by simpa only [dist_one_right, hf] using h.dist_le_mul x 1

@[to_additive LipschitzWith.nnorm_le_mul]
theorem LipschitzWith.nnorm_le_mul' {f : E â†’ F} {K : â„â‰¥0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : â€–f xâ€–â‚Š â‰¤ K * â€–xâ€–â‚Š :=
  h.norm_le_mul' hf x

@[to_additive AntilipschitzWith.le_mul_norm]
theorem AntilipschitzWith.le_mul_norm' {f : E â†’ F} {K : â„â‰¥0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : â€–xâ€– â‰¤ K * â€–f xâ€– := by
  simpa only [dist_one_right, hf] using h.le_mul_dist x 1

@[to_additive AntilipschitzWith.le_mul_nnnorm]
theorem AntilipschitzWith.le_mul_nnnorm' {f : E â†’ F} {K : â„â‰¥0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : â€–xâ€–â‚Š â‰¤ K * â€–f xâ€–â‚Š :=
  h.le_mul_norm' hf x

@[to_additive]
theorem OneHomClass.bound_of_antilipschitz [OneHomClass ğ“• E F] (f : ğ“•) {K : â„â‰¥0}
    (h : AntilipschitzWith K f) (x) : â€–xâ€– â‰¤ K * â€–f xâ€– :=
  h.le_mul_nnnorm' (map_one f) x

@[to_additive]
theorem Isometry.nnnorm_map_of_map_one {f : E â†’ F} (hi : Isometry f) (hâ‚ : f 1 = 1) (x : E) :
    â€–f xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| hi.norm_map_of_map_one hâ‚ x

end NNNorm

@[to_additive lipschitzWith_one_norm]
theorem lipschitzWith_one_norm' : LipschitzWith 1 (norm : E â†’ â„) := by
  simpa using LipschitzWith.dist_right (1 : E)

@[to_additive lipschitzWith_one_nnnorm]
theorem lipschitzWith_one_nnnorm' : LipschitzWith 1 (NNNorm.nnnorm : E â†’ â„â‰¥0) :=
  lipschitzWith_one_norm'

@[to_additive uniformContinuous_norm]
theorem uniformContinuous_norm' : UniformContinuous (norm : E â†’ â„) :=
  lipschitzWith_one_norm'.uniformContinuous

@[to_additive uniformContinuous_nnnorm]
theorem uniformContinuous_nnnorm' : UniformContinuous fun a : E â†¦ â€–aâ€–â‚Š :=
  uniformContinuous_norm'.subtype_mk _

end SeminormedGroup

section SeminormedCommGroup

variable [SeminormedCommGroup E] [SeminormedCommGroup F] {aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E} {râ‚ râ‚‚ : â„}

@[to_additive]
instance NormedGroup.to_isIsometricSMul_left : IsIsometricSMul E E :=
  âŸ¨fun a â†¦ Isometry.of_dist_eq fun b c â†¦ by simp [dist_eq_norm_div]âŸ©

@[to_additive (attr := simp)]
theorem dist_self_mul_right (a b : E) : dist a (a * b) = â€–bâ€– := by
  rw [â† dist_one_left, â† dist_mul_left a 1 b, mul_one]

@[to_additive (attr := simp)]
theorem dist_self_mul_left (a b : E) : dist (a * b) a = â€–bâ€– := by
  rw [dist_comm, dist_self_mul_right]

@[to_additive (attr := simp 1001)] -- Increase priority because `simp` can prove this
theorem dist_self_div_right (a b : E) : dist a (a / b) = â€–bâ€– := by
  rw [div_eq_mul_inv, dist_self_mul_right, norm_inv']

@[to_additive (attr := simp 1001)] -- Increase priority because `simp` can prove this
theorem dist_self_div_left (a b : E) : dist (a / b) a = â€–bâ€– := by
  rw [dist_comm, dist_self_div_right]

@[to_additive]
theorem dist_mul_mul_le (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E) : dist (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) â‰¤ dist aâ‚ bâ‚ + dist aâ‚‚ bâ‚‚ := by
  simpa only [dist_mul_left, dist_mul_right] using dist_triangle (aâ‚ * aâ‚‚) (bâ‚ * aâ‚‚) (bâ‚ * bâ‚‚)

@[to_additive]
theorem dist_mul_mul_le_of_le (hâ‚ : dist aâ‚ bâ‚ â‰¤ râ‚) (hâ‚‚ : dist aâ‚‚ bâ‚‚ â‰¤ râ‚‚) :
    dist (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) â‰¤ râ‚ + râ‚‚ :=
  (dist_mul_mul_le aâ‚ aâ‚‚ bâ‚ bâ‚‚).trans <| add_le_add hâ‚ hâ‚‚

@[to_additive]
theorem dist_div_div_le (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E) : dist (aâ‚ / aâ‚‚) (bâ‚ / bâ‚‚) â‰¤ dist aâ‚ bâ‚ + dist aâ‚‚ bâ‚‚ := by
  simpa only [div_eq_mul_inv, dist_inv_inv] using dist_mul_mul_le aâ‚ aâ‚‚â»Â¹ bâ‚ bâ‚‚â»Â¹

@[to_additive]
theorem dist_div_div_le_of_le (hâ‚ : dist aâ‚ bâ‚ â‰¤ râ‚) (hâ‚‚ : dist aâ‚‚ bâ‚‚ â‰¤ râ‚‚) :
    dist (aâ‚ / aâ‚‚) (bâ‚ / bâ‚‚) â‰¤ râ‚ + râ‚‚ :=
  (dist_div_div_le aâ‚ aâ‚‚ bâ‚ bâ‚‚).trans <| add_le_add hâ‚ hâ‚‚

@[to_additive]
theorem abs_dist_sub_le_dist_mul_mul (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E) :
    |dist aâ‚ bâ‚ - dist aâ‚‚ bâ‚‚| â‰¤ dist (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) := by
  simpa only [dist_mul_left, dist_mul_right, dist_comm bâ‚‚] using
    abs_dist_sub_le (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) (bâ‚ * aâ‚‚)

open Finset

@[to_additive]
theorem nndist_mul_mul_le (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E) :
    nndist (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) â‰¤ nndist aâ‚ bâ‚ + nndist aâ‚‚ bâ‚‚ :=
  NNReal.coe_le_coe.1 <| dist_mul_mul_le aâ‚ aâ‚‚ bâ‚ bâ‚‚

@[to_additive]
theorem edist_mul_mul_le (aâ‚ aâ‚‚ bâ‚ bâ‚‚ : E) :
    edist (aâ‚ * aâ‚‚) (bâ‚ * bâ‚‚) â‰¤ edist aâ‚ bâ‚ + edist aâ‚‚ bâ‚‚ := by
  simp only [edist_nndist]
  norm_cast
  apply nndist_mul_mul_le

section PseudoEMetricSpace
variable {Î± E : Type*} [SeminormedCommGroup E] [PseudoEMetricSpace Î±] {K Kf Kg : â„â‰¥0}
  {f g : Î± â†’ E} {s : Set Î±}

@[to_additive (attr := simp)]
lemma lipschitzWith_inv_iff : LipschitzWith K fâ»Â¹ â†” LipschitzWith K f := by simp [LipschitzWith]

@[to_additive (attr := simp)]
lemma antilipschitzWith_inv_iff : AntilipschitzWith K fâ»Â¹ â†” AntilipschitzWith K f := by
  simp [AntilipschitzWith]

@[to_additive (attr := simp)]
lemma lipschitzOnWith_inv_iff : LipschitzOnWith K fâ»Â¹ s â†” LipschitzOnWith K f s := by
  simp [LipschitzOnWith]

@[to_additive (attr := simp)]
lemma locallyLipschitz_inv_iff : LocallyLipschitz fâ»Â¹ â†” LocallyLipschitz f := by
  simp [LocallyLipschitz]

@[to_additive (attr := simp)]
lemma locallyLipschitzOn_inv_iff : LocallyLipschitzOn s fâ»Â¹ â†” LocallyLipschitzOn s f := by
  simp [LocallyLipschitzOn]

@[to_additive] alias âŸ¨LipschitzWith.of_inv, LipschitzWith.invâŸ© := lipschitzWith_inv_iff
@[to_additive] alias âŸ¨AntilipschitzWith.of_inv, AntilipschitzWith.invâŸ© := antilipschitzWith_inv_iff
@[to_additive] alias âŸ¨LipschitzOnWith.of_inv, LipschitzOnWith.invâŸ© := lipschitzOnWith_inv_iff
@[to_additive] alias âŸ¨LocallyLipschitz.of_inv, LocallyLipschitz.invâŸ© := locallyLipschitz_inv_iff
@[to_additive]
alias âŸ¨LocallyLipschitzOn.of_inv, LocallyLipschitzOn.invâŸ© := locallyLipschitzOn_inv_iff

@[to_additive]
lemma LipschitzOnWith.mul (hf : LipschitzOnWith Kf f s) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf + Kg) (fun x â†¦ f x * g x) s := fun x hx y hy â†¦
  calc
    edist (f x * g x) (f y * g y) â‰¤ edist (f x) (f y) + edist (g x) (g y) :=
      edist_mul_mul_le _ _ _ _
    _ â‰¤ Kf * edist x y + Kg * edist x y := add_le_add (hf hx hy) (hg hx hy)
    _ = (Kf + Kg) * edist x y := (add_mul _ _ _).symm

@[to_additive]
lemma LipschitzWith.mul (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x â†¦ f x * g x := by
  simpa [â† lipschitzOnWith_univ] using hf.lipschitzOnWith.mul hg.lipschitzOnWith

@[to_additive]
lemma LocallyLipschitzOn.mul (hf : LocallyLipschitzOn s f) (hg : LocallyLipschitzOn s g) :
    LocallyLipschitzOn s fun x â†¦ f x * g x := fun x hx â†¦ by
  obtain âŸ¨Kf, t, ht, hKfâŸ© := hf hx
  obtain âŸ¨Kg, u, hu, hKgâŸ© := hg hx
  exact âŸ¨Kf + Kg, t âˆ© u, inter_mem ht hu,
    (hKf.mono Set.inter_subset_left).mul (hKg.mono Set.inter_subset_right)âŸ©

@[to_additive]
lemma LocallyLipschitz.mul (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x â†¦ f x * g x := by
  simpa [â† locallyLipschitzOn_univ] using hf.locallyLipschitzOn.mul hg.locallyLipschitzOn

@[to_additive]
lemma LipschitzOnWith.div (hf : LipschitzOnWith Kf f s) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf + Kg) (fun x â†¦ f x / g x) s := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
theorem LipschitzWith.div (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x â†¦ f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
lemma LocallyLipschitzOn.div (hf : LocallyLipschitzOn s f) (hg : LocallyLipschitzOn s g) :
    LocallyLipschitzOn s fun x â†¦ f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
lemma LocallyLipschitz.div (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x â†¦ f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

namespace AntilipschitzWith

@[to_additive]
theorem mul_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg g) (hK : Kg < Kfâ»Â¹) :
    AntilipschitzWith (Kfâ»Â¹ - Kg)â»Â¹ fun x â†¦ f x * g x := by
  letI : PseudoMetricSpace Î± := PseudoEMetricSpace.toPseudoMetricSpace hf.edist_ne_top
  refine AntilipschitzWith.of_le_mul_dist fun x y â†¦ ?_
  rw [NNReal.coe_inv, â† _root_.div_eq_inv_mul]
  rw [le_div_iffâ‚€ (NNReal.coe_pos.2 <| tsub_pos_iff_lt.2 hK)]
  rw [mul_comm, NNReal.coe_sub hK.le, sub_mul]
  calc
    â†‘Kfâ»Â¹ * dist x y - Kg * dist x y â‰¤ dist (f x) (f y) - dist (g x) (g y) :=
      sub_le_sub (hf.mul_le_dist x y) (hg.dist_le_mul x y)
    _ â‰¤ _ := le_trans (le_abs_self _) (abs_dist_sub_le_dist_mul_mul _ _ _ _)

@[to_additive]
theorem mul_div_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg (g / f))
    (hK : Kg < Kfâ»Â¹) : AntilipschitzWith (Kfâ»Â¹ - Kg)â»Â¹ g := by
  simpa only [Pi.div_apply, mul_div_cancel] using hf.mul_lipschitzWith hg hK

@[to_additive le_mul_norm_sub]
theorem le_mul_norm_div {f : E â†’ F} (hf : AntilipschitzWith K f) (x y : E) :
    â€–x / yâ€– â‰¤ K * â€–f x / f yâ€– := by simp [â† dist_eq_norm_div, hf.le_mul_dist x y]

end AntilipschitzWith
end PseudoEMetricSpace

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.to_lipschitzMul : LipschitzMul E :=
  âŸ¨âŸ¨1 + 1, LipschitzWith.prod_fst.mul LipschitzWith.prod_sndâŸ©âŸ©

-- See note [lower instance priority]
/-- A seminormed group is a uniform group, i.e., multiplication and division are uniformly
continuous. -/
@[to_additive /-- A seminormed group is a uniform additive group, i.e., addition and subtraction are
uniformly continuous. -/]
instance (priority := 100) SeminormedCommGroup.to_isUniformGroup : IsUniformGroup E :=
  âŸ¨(LipschitzWith.prod_fst.div LipschitzWith.prod_snd).uniformContinuousâŸ©

-- short-circuit type class inference
-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.toIsTopologicalGroup : IsTopologicalGroup E :=
  inferInstance

/-! ### SeparationQuotient -/

namespace SeparationQuotient

@[to_additive instNorm]
instance instMulNorm : Norm (SeparationQuotient E) where
  norm := lift Norm.norm fun _ _ h â†¦ h.norm_eq_norm'

set_option linter.docPrime false in
@[to_additive (attr := simp) norm_mk]
theorem norm_mk' (p : E) : â€–mk pâ€– = â€–pâ€– := rfl

@[to_additive]
instance : NormedCommGroup (SeparationQuotient E) where
  __ : CommGroup (SeparationQuotient E) := instCommGroup
  dist_eq := Quotient.indâ‚‚ dist_eq_norm_div

@[to_additive]
theorem mk_eq_one_iff {p : E} : mk p = 1 â†” â€–pâ€– = 0 := by
  rw [â† norm_mk', norm_eq_zero']

set_option linter.docPrime false in
@[to_additive (attr := simp) nnnorm_mk]
theorem nnnorm_mk' (p : E) : â€–mk pâ€–â‚Š = â€–pâ€–â‚Š := rfl

end SeparationQuotient

@[to_additive]
theorem cauchySeq_prod_of_eventually_eq {u v : â„• â†’ E} {N : â„•} (huv : âˆ€ n â‰¥ N, u n = v n)
    (hv : CauchySeq fun n â†¦ âˆ k âˆˆ range (n + 1), v k) :
    CauchySeq fun n â†¦ âˆ k âˆˆ range (n + 1), u k := by
  let d : â„• â†’ E := fun n â†¦ âˆ k âˆˆ range (n + 1), u k / v k
  rw [show (fun n â†¦ âˆ k âˆˆ range (n + 1), u k) = d * fun n â†¦ âˆ k âˆˆ range (n + 1), v k
      by ext n; simp [d]]
  suffices âˆ€ n â‰¥ N, d n = d N from (tendsto_atTop_of_eventually_const this).cauchySeq.mul hv
  intro n hn
  dsimp [d]
  rw [eventually_constant_prod _ (add_le_add_right hn 1)]
  intro m hm
  simp [huv m (le_of_lt hm)]

@[to_additive CauchySeq.norm_bddAbove]
lemma CauchySeq.mul_norm_bddAbove {G : Type*} [SeminormedGroup G] {u : â„• â†’ G}
    (hu : CauchySeq u) : BddAbove (Set.range (fun n â†¦ â€–u nâ€–)) := by
  obtain âŸ¨C, -, hCâŸ© := cauchySeq_bdd hu
  simp_rw [SeminormedGroup.dist_eq] at hC
  have : âˆ€ n, â€–u nâ€– â‰¤ C + â€–u 0â€– := by
    intro n
    rw [add_comm]
    refine (norm_le_norm_add_norm_div' (u n) (u 0)).trans ?_
    simp [(hC _ _).le]
  rw [bddAbove_def]
  exact âŸ¨C + â€–u 0â€–, by simpa using thisâŸ©

end SeminormedCommGroup
