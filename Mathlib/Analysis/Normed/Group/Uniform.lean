/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl, Ya√´l Dillies
-/
import Mathlib.Analysis.Normed.Group.Continuity
import Mathlib.Topology.Algebra.IsUniformGroup.Basic
import Mathlib.Topology.MetricSpace.Algebra
import Mathlib.Topology.MetricSpace.IsometricSMul

/-!
# Normed groups are uniform groups

This file proves lipschitzness of normed group operations and shows that normed groups are uniform
groups.
-/

variable {ùìï E F : Type*}

open Filter Function Metric Bornology
open scoped ENNReal NNReal Uniformity Pointwise Topology

section SeminormedGroup
variable [SeminormedGroup E] [SeminormedGroup F] {s : Set E} {a b : E} {r : ‚Ñù}

@[to_additive]
instance NormedGroup.to_isIsometricSMul_right : IsIsometricSMul E·µê·µí·µñ E :=
  ‚ü®fun a => Isometry.of_dist_eq fun b c => by simp [dist_eq_norm_div]‚ü©

@[to_additive]
theorem Isometry.norm_map_of_map_one {f : E ‚Üí F} (hi : Isometry f) (h‚ÇÅ : f 1 = 1) (x : E) :
    ‚Äñf x‚Äñ = ‚Äñx‚Äñ := by rw [‚Üê dist_one_right, ‚Üê h‚ÇÅ, hi.dist_eq, dist_one_right]

@[to_additive (attr := simp) norm_map]
theorem norm_map' [FunLike ùìï E F] [IsometryClass ùìï E F] [OneHomClass ùìï E F] (f : ùìï) (x : E) :
    ‚Äñf x‚Äñ = ‚Äñx‚Äñ :=
  (IsometryClass.isometry f).norm_map_of_map_one (map_one f) x

@[to_additive (attr := simp) nnnorm_map]
theorem nnnorm_map' [FunLike ùìï E F] [IsometryClass ùìï E F] [OneHomClass ùìï E F] (f : ùìï) (x : E) :
    ‚Äñf x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  NNReal.eq <| norm_map' f x

@[to_additive (attr := simp)]
theorem dist_mul_self_right (a b : E) : dist b (a * b) = ‚Äña‚Äñ := by
  rw [‚Üê dist_one_left, ‚Üê dist_mul_right 1 a b, one_mul]

@[to_additive (attr := simp)]
theorem dist_mul_self_left (a b : E) : dist (a * b) b = ‚Äña‚Äñ := by
  rw [dist_comm, dist_mul_self_right]

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_left (a b c : E) : dist (a / b) c = dist a (c * b) := by
  rw [‚Üê dist_mul_right _ _ b, div_mul_cancel]

@[to_additive (attr := simp)]
theorem dist_div_eq_dist_mul_right (a b c : E) : dist a (b / c) = dist (a * c) b := by
  rw [‚Üê dist_mul_right _ _ c, div_mul_cancel]

open Finset

variable [FunLike ùìï E F]

/-- A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant `C` such that
for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`. -/
@[to_additive "A homomorphism `f` of seminormed groups is Lipschitz, if there exists a constant
`C` such that for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. The analogous condition for a linear map of
(semi)normed spaces is in `Mathlib/Analysis/NormedSpace/OperatorNorm.lean`."]
theorem MonoidHomClass.lipschitz_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : LipschitzWith (Real.toNNReal C) f :=
  LipschitzWith.of_dist_le' fun x y => by simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive]
theorem lipschitzOnWith_iff_norm_div_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} :
    LipschitzOnWith C f s ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí ‚àÄ ‚¶Éy‚¶Ñ, y ‚àà s ‚Üí ‚Äñf x / f y‚Äñ ‚â§ C * ‚Äñx / y‚Äñ := by
  simp only [lipschitzOnWith_iff_dist_le_mul, dist_eq_norm_div]

alias ‚ü®LipschitzOnWith.norm_div_le, _‚ü© := lipschitzOnWith_iff_norm_div_le

attribute [to_additive] LipschitzOnWith.norm_div_le

@[to_additive]
theorem LipschitzOnWith.norm_div_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} (h : LipschitzOnWith C f s)
    (ha : a ‚àà s) (hb : b ‚àà s) (hr : ‚Äña / b‚Äñ ‚â§ r) : ‚Äñf a / f b‚Äñ ‚â§ C * r :=
  (h.norm_div_le ha hb).trans <| by gcongr

@[to_additive]
theorem lipschitzWith_iff_norm_div_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} :
    LipschitzWith C f ‚Üî ‚àÄ x y, ‚Äñf x / f y‚Äñ ‚â§ C * ‚Äñx / y‚Äñ := by
  simp only [lipschitzWith_iff_dist_le_mul, dist_eq_norm_div]

alias ‚ü®LipschitzWith.norm_div_le, _‚ü© := lipschitzWith_iff_norm_div_le

attribute [to_additive] LipschitzWith.norm_div_le

@[to_additive]
theorem LipschitzWith.norm_div_le_of_le {f : E ‚Üí F} {C : ‚Ñù‚â•0} (h : LipschitzWith C f)
    (hr : ‚Äña / b‚Äñ ‚â§ r) : ‚Äñf a / f b‚Äñ ‚â§ C * r :=
  (h.norm_div_le _ _).trans <| by gcongr

/-- A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C` such that
for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`. -/
@[to_additive "A homomorphism `f` of seminormed groups is continuous, if there exists a constant `C`
such that for all `x`, one has `‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ`"]
theorem MonoidHomClass.continuous_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : Continuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).continuous

@[to_additive]
theorem MonoidHomClass.uniformContinuous_of_bound [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù)
    (h : ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñx‚Äñ) : UniformContinuous f :=
  (MonoidHomClass.lipschitz_of_bound f C h).uniformContinuous

@[to_additive]
theorem MonoidHomClass.isometry_iff_norm [MonoidHomClass ùìï E F] (f : ùìï) :
    Isometry f ‚Üî ‚àÄ x, ‚Äñf x‚Äñ = ‚Äñx‚Äñ := by
  simp only [isometry_iff_dist_eq, dist_eq_norm_div, ‚Üê map_div]
  refine ‚ü®fun h x => ?_, fun h x y => h _‚ü©
  simpa using h x 1

alias ‚ü®_, MonoidHomClass.isometry_of_norm‚ü© := MonoidHomClass.isometry_iff_norm

attribute [to_additive] MonoidHomClass.isometry_of_norm

section NNNorm

@[to_additive]
theorem MonoidHomClass.lipschitz_of_bound_nnnorm [MonoidHomClass ùìï E F] (f : ùìï) (C : ‚Ñù‚â•0)
    (h : ‚àÄ x, ‚Äñf x‚Äñ‚Çä ‚â§ C * ‚Äñx‚Äñ‚Çä) : LipschitzWith C f :=
  @Real.toNNReal_coe C ‚ñ∏ MonoidHomClass.lipschitz_of_bound f C h

@[to_additive]
theorem MonoidHomClass.antilipschitz_of_bound [MonoidHomClass ùìï E F] (f : ùìï) {K : ‚Ñù‚â•0}
    (h : ‚àÄ x, ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ) : AntilipschitzWith K f :=
  AntilipschitzWith.of_le_mul_dist fun x y => by
    simpa only [dist_eq_norm_div, map_div] using h (x / y)

@[to_additive LipschitzWith.norm_le_mul]
theorem LipschitzWith.norm_le_mul' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : ‚Äñf x‚Äñ ‚â§ K * ‚Äñx‚Äñ := by simpa only [dist_one_right, hf] using h.dist_le_mul x 1

@[to_additive LipschitzWith.nnorm_le_mul]
theorem LipschitzWith.nnorm_le_mul' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : LipschitzWith K f) (hf : f 1 = 1)
    (x) : ‚Äñf x‚Äñ‚Çä ‚â§ K * ‚Äñx‚Äñ‚Çä :=
  h.norm_le_mul' hf x

@[to_additive AntilipschitzWith.le_mul_norm]
theorem AntilipschitzWith.le_mul_norm' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ := by
  simpa only [dist_one_right, hf] using h.le_mul_dist x 1

@[to_additive AntilipschitzWith.le_mul_nnnorm]
theorem AntilipschitzWith.le_mul_nnnorm' {f : E ‚Üí F} {K : ‚Ñù‚â•0} (h : AntilipschitzWith K f)
    (hf : f 1 = 1) (x) : ‚Äñx‚Äñ‚Çä ‚â§ K * ‚Äñf x‚Äñ‚Çä :=
  h.le_mul_norm' hf x

@[to_additive]
theorem OneHomClass.bound_of_antilipschitz [OneHomClass ùìï E F] (f : ùìï) {K : ‚Ñù‚â•0}
    (h : AntilipschitzWith K f) (x) : ‚Äñx‚Äñ ‚â§ K * ‚Äñf x‚Äñ :=
  h.le_mul_nnnorm' (map_one f) x

@[to_additive]
theorem Isometry.nnnorm_map_of_map_one {f : E ‚Üí F} (hi : Isometry f) (h‚ÇÅ : f 1 = 1) (x : E) :
    ‚Äñf x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  Subtype.ext <| hi.norm_map_of_map_one h‚ÇÅ x

end NNNorm

@[to_additive lipschitzWith_one_norm]
theorem lipschitzWith_one_norm' : LipschitzWith 1 (norm : E ‚Üí ‚Ñù) := by
  simpa using LipschitzWith.dist_right (1 : E)

@[to_additive lipschitzWith_one_nnnorm]
theorem lipschitzWith_one_nnnorm' : LipschitzWith 1 (NNNorm.nnnorm : E ‚Üí ‚Ñù‚â•0) :=
  lipschitzWith_one_norm'

@[to_additive uniformContinuous_norm]
theorem uniformContinuous_norm' : UniformContinuous (norm : E ‚Üí ‚Ñù) :=
  lipschitzWith_one_norm'.uniformContinuous

@[to_additive uniformContinuous_nnnorm]
theorem uniformContinuous_nnnorm' : UniformContinuous fun a : E => ‚Äña‚Äñ‚Çä :=
  uniformContinuous_norm'.subtype_mk _

end SeminormedGroup

section SeminormedCommGroup

variable [SeminormedCommGroup E] [SeminormedCommGroup F] {a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E} {r‚ÇÅ r‚ÇÇ : ‚Ñù}

@[to_additive]
instance NormedGroup.to_isIsometricSMul_left : IsIsometricSMul E E :=
  ‚ü®fun a => Isometry.of_dist_eq fun b c => by simp [dist_eq_norm_div]‚ü©

@[to_additive (attr := simp)]
theorem dist_self_mul_right (a b : E) : dist a (a * b) = ‚Äñb‚Äñ := by
  rw [‚Üê dist_one_left, ‚Üê dist_mul_left a 1 b, mul_one]

@[to_additive (attr := simp)]
theorem dist_self_mul_left (a b : E) : dist (a * b) a = ‚Äñb‚Äñ := by
  rw [dist_comm, dist_self_mul_right]

@[to_additive (attr := simp 1001)] -- Increase priority because `simp` can prove this
theorem dist_self_div_right (a b : E) : dist a (a / b) = ‚Äñb‚Äñ := by
  rw [div_eq_mul_inv, dist_self_mul_right, norm_inv']

@[to_additive (attr := simp 1001)] -- Increase priority because `simp` can prove this
theorem dist_self_div_left (a b : E) : dist (a / b) a = ‚Äñb‚Äñ := by
  rw [dist_comm, dist_self_div_right]

@[to_additive]
theorem dist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) : dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ dist a‚ÇÅ b‚ÇÅ + dist a‚ÇÇ b‚ÇÇ := by
  simpa only [dist_mul_left, dist_mul_right] using dist_triangle (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ)

@[to_additive]
theorem dist_mul_mul_le_of_le (h‚ÇÅ : dist a‚ÇÅ b‚ÇÅ ‚â§ r‚ÇÅ) (h‚ÇÇ : dist a‚ÇÇ b‚ÇÇ ‚â§ r‚ÇÇ) :
    dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (dist_mul_mul_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ).trans <| add_le_add h‚ÇÅ h‚ÇÇ

@[to_additive]
theorem dist_div_div_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) : dist (a‚ÇÅ / a‚ÇÇ) (b‚ÇÅ / b‚ÇÇ) ‚â§ dist a‚ÇÅ b‚ÇÅ + dist a‚ÇÇ b‚ÇÇ := by
  simpa only [div_eq_mul_inv, dist_inv_inv] using dist_mul_mul_le a‚ÇÅ a‚ÇÇ‚Åª¬π b‚ÇÅ b‚ÇÇ‚Åª¬π

@[to_additive]
theorem dist_div_div_le_of_le (h‚ÇÅ : dist a‚ÇÅ b‚ÇÅ ‚â§ r‚ÇÅ) (h‚ÇÇ : dist a‚ÇÇ b‚ÇÇ ‚â§ r‚ÇÇ) :
    dist (a‚ÇÅ / a‚ÇÇ) (b‚ÇÅ / b‚ÇÇ) ‚â§ r‚ÇÅ + r‚ÇÇ :=
  (dist_div_div_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ).trans <| add_le_add h‚ÇÅ h‚ÇÇ

@[to_additive]
theorem abs_dist_sub_le_dist_mul_mul (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    |dist a‚ÇÅ b‚ÇÅ - dist a‚ÇÇ b‚ÇÇ| ‚â§ dist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) := by
  simpa only [dist_mul_left, dist_mul_right, dist_comm b‚ÇÇ] using
    abs_dist_sub_le (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) (b‚ÇÅ * a‚ÇÇ)

open Finset

@[to_additive]
theorem nndist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    nndist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ nndist a‚ÇÅ b‚ÇÅ + nndist a‚ÇÇ b‚ÇÇ :=
  NNReal.coe_le_coe.1 <| dist_mul_mul_le a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ

@[to_additive]
theorem edist_mul_mul_le (a‚ÇÅ a‚ÇÇ b‚ÇÅ b‚ÇÇ : E) :
    edist (a‚ÇÅ * a‚ÇÇ) (b‚ÇÅ * b‚ÇÇ) ‚â§ edist a‚ÇÅ b‚ÇÅ + edist a‚ÇÇ b‚ÇÇ := by
  simp only [edist_nndist]
  norm_cast
  apply nndist_mul_mul_le

section PseudoEMetricSpace
variable {Œ± E : Type*} [SeminormedCommGroup E] [PseudoEMetricSpace Œ±] {K Kf Kg : ‚Ñù‚â•0}
  {f g : Œ± ‚Üí E} {s : Set Œ±}

@[to_additive (attr := simp)]
lemma lipschitzWith_inv_iff : LipschitzWith K f‚Åª¬π ‚Üî LipschitzWith K f := by simp [LipschitzWith]

@[to_additive (attr := simp)]
lemma antilipschitzWith_inv_iff : AntilipschitzWith K f‚Åª¬π ‚Üî AntilipschitzWith K f := by
  simp [AntilipschitzWith]

@[to_additive (attr := simp)]
lemma lipschitzOnWith_inv_iff : LipschitzOnWith K f‚Åª¬π s ‚Üî LipschitzOnWith K f s := by
  simp [LipschitzOnWith]

@[to_additive (attr := simp)]
lemma locallyLipschitz_inv_iff : LocallyLipschitz f‚Åª¬π ‚Üî LocallyLipschitz f := by
  simp [LocallyLipschitz]

@[to_additive (attr := simp)]
lemma locallyLipschitzOn_inv_iff : LocallyLipschitzOn s f‚Åª¬π ‚Üî LocallyLipschitzOn s f := by
  simp [LocallyLipschitzOn]

@[to_additive] alias ‚ü®LipschitzWith.of_inv, LipschitzWith.inv‚ü© := lipschitzWith_inv_iff
@[to_additive] alias ‚ü®AntilipschitzWith.of_inv, AntilipschitzWith.inv‚ü© := antilipschitzWith_inv_iff
@[to_additive] alias ‚ü®LipschitzOnWith.of_inv, LipschitzOnWith.inv‚ü© := lipschitzOnWith_inv_iff
@[to_additive] alias ‚ü®LocallyLipschitz.of_inv, LocallyLipschitz.inv‚ü© := locallyLipschitz_inv_iff
@[to_additive]
alias ‚ü®LocallyLipschitzOn.of_inv, LocallyLipschitzOn.inv‚ü© := locallyLipschitzOn_inv_iff

@[to_additive]
lemma LipschitzOnWith.mul (hf : LipschitzOnWith Kf f s) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf + Kg) (fun x ‚Ü¶ f x * g x) s := fun x hx y hy ‚Ü¶
  calc
    edist (f x * g x) (f y * g y) ‚â§ edist (f x) (f y) + edist (g x) (g y) :=
      edist_mul_mul_le _ _ _ _
    _ ‚â§ Kf * edist x y + Kg * edist x y := add_le_add (hf hx hy) (hg hx hy)
    _ = (Kf + Kg) * edist x y := (add_mul _ _ _).symm

@[to_additive]
lemma LipschitzWith.mul (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x ‚Ü¶ f x * g x := by
  simpa [‚Üê lipschitzOnWith_univ] using hf.lipschitzOnWith.mul hg.lipschitzOnWith

@[to_additive]
lemma LocallyLipschitzOn.mul (hf : LocallyLipschitzOn s f) (hg : LocallyLipschitzOn s g) :
    LocallyLipschitzOn s fun x ‚Ü¶ f x * g x := fun x hx ‚Ü¶ by
  obtain ‚ü®Kf, t, ht, hKf‚ü© := hf hx
  obtain ‚ü®Kg, u, hu, hKg‚ü© := hg hx
  exact ‚ü®Kf + Kg, t ‚à© u, inter_mem ht hu,
    (hKf.mono Set.inter_subset_left).mul (hKg.mono Set.inter_subset_right)‚ü©

@[to_additive]
lemma LocallyLipschitz.mul (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x ‚Ü¶ f x * g x := by
  simpa [‚Üê locallyLipschitzOn_univ] using hf.locallyLipschitzOn.mul hg.locallyLipschitzOn

@[to_additive]
lemma LipschitzOnWith.div (hf : LipschitzOnWith Kf f s) (hg : LipschitzOnWith Kg g s) :
    LipschitzOnWith (Kf + Kg) (fun x ‚Ü¶ f x / g x) s := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
theorem LipschitzWith.div (hf : LipschitzWith Kf f) (hg : LipschitzWith Kg g) :
    LipschitzWith (Kf + Kg) fun x => f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
lemma LocallyLipschitzOn.div (hf : LocallyLipschitzOn s f) (hg : LocallyLipschitzOn s g) :
    LocallyLipschitzOn s fun x ‚Ü¶ f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

@[to_additive]
lemma LocallyLipschitz.div (hf : LocallyLipschitz f) (hg : LocallyLipschitz g) :
    LocallyLipschitz fun x ‚Ü¶ f x / g x := by
  simpa only [div_eq_mul_inv] using hf.mul hg.inv

namespace AntilipschitzWith

@[to_additive]
theorem mul_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg g) (hK : Kg < Kf‚Åª¬π) :
    AntilipschitzWith (Kf‚Åª¬π - Kg)‚Åª¬π fun x => f x * g x := by
  letI : PseudoMetricSpace Œ± := PseudoEMetricSpace.toPseudoMetricSpace hf.edist_ne_top
  refine AntilipschitzWith.of_le_mul_dist fun x y => ?_
  rw [NNReal.coe_inv, ‚Üê _root_.div_eq_inv_mul]
  rw [le_div_iff‚ÇÄ (NNReal.coe_pos.2 <| tsub_pos_iff_lt.2 hK)]
  rw [mul_comm, NNReal.coe_sub hK.le, sub_mul]
  calc
    ‚ÜëKf‚Åª¬π * dist x y - Kg * dist x y ‚â§ dist (f x) (f y) - dist (g x) (g y) :=
      sub_le_sub (hf.mul_le_dist x y) (hg.dist_le_mul x y)
    _ ‚â§ _ := le_trans (le_abs_self _) (abs_dist_sub_le_dist_mul_mul _ _ _ _)

@[to_additive]
theorem mul_div_lipschitzWith (hf : AntilipschitzWith Kf f) (hg : LipschitzWith Kg (g / f))
    (hK : Kg < Kf‚Åª¬π) : AntilipschitzWith (Kf‚Åª¬π - Kg)‚Åª¬π g := by
  simpa only [Pi.div_apply, mul_div_cancel] using hf.mul_lipschitzWith hg hK

@[to_additive le_mul_norm_sub]
theorem le_mul_norm_div {f : E ‚Üí F} (hf : AntilipschitzWith K f) (x y : E) :
    ‚Äñx / y‚Äñ ‚â§ K * ‚Äñf x / f y‚Äñ := by simp [‚Üê dist_eq_norm_div, hf.le_mul_dist x y]

end AntilipschitzWith
end PseudoEMetricSpace

-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.to_lipschitzMul : LipschitzMul E :=
  ‚ü®‚ü®1 + 1, LipschitzWith.prod_fst.mul LipschitzWith.prod_snd‚ü©‚ü©

-- See note [lower instance priority]
/-- A seminormed group is a uniform group, i.e., multiplication and division are uniformly
continuous. -/
@[to_additive "A seminormed group is a uniform additive group, i.e., addition and subtraction are
uniformly continuous."]
instance (priority := 100) SeminormedCommGroup.to_isUniformGroup : IsUniformGroup E :=
  ‚ü®(LipschitzWith.prod_fst.div LipschitzWith.prod_snd).uniformContinuous‚ü©

-- short-circuit type class inference
-- See note [lower instance priority]
@[to_additive]
instance (priority := 100) SeminormedCommGroup.toIsTopologicalGroup : IsTopologicalGroup E :=
  inferInstance

/-! ### SeparationQuotient -/

namespace SeparationQuotient

@[to_additive instNorm]
instance instMulNorm : Norm (SeparationQuotient E) where
  norm := lift Norm.norm fun _ _ h => h.norm_eq_norm'

set_option linter.docPrime false in
@[to_additive (attr := simp) norm_mk]
theorem norm_mk' (p : E) : ‚Äñmk p‚Äñ = ‚Äñp‚Äñ := rfl

@[to_additive]
instance : NormedCommGroup (SeparationQuotient E) where
  __ : CommGroup (SeparationQuotient E) := instCommGroup
  dist_eq := Quotient.ind‚ÇÇ dist_eq_norm_div

@[to_additive]
theorem mk_eq_one_iff {p : E} : mk p = 1 ‚Üî ‚Äñp‚Äñ = 0 := by
  rw [‚Üê norm_mk', norm_eq_zero']

set_option linter.docPrime false in
@[to_additive (attr := simp) nnnorm_mk]
theorem nnnorm_mk' (p : E) : ‚Äñmk p‚Äñ‚Çä = ‚Äñp‚Äñ‚Çä := rfl

end SeparationQuotient

@[to_additive]
theorem cauchySeq_prod_of_eventually_eq {u v : ‚Ñï ‚Üí E} {N : ‚Ñï} (huv : ‚àÄ n ‚â• N, u n = v n)
    (hv : CauchySeq fun n => ‚àè k ‚àà range (n + 1), v k) :
    CauchySeq fun n => ‚àè k ‚àà range (n + 1), u k := by
  let d : ‚Ñï ‚Üí E := fun n => ‚àè k ‚àà range (n + 1), u k / v k
  rw [show (fun n => ‚àè k ‚àà range (n + 1), u k) = d * fun n => ‚àè k ‚àà range (n + 1), v k
      by ext n; simp [d]]
  suffices ‚àÄ n ‚â• N, d n = d N from (tendsto_atTop_of_eventually_const this).cauchySeq.mul hv
  intro n hn
  dsimp [d]
  rw [eventually_constant_prod _ (add_le_add_right hn 1)]
  intro m hm
  simp [huv m (le_of_lt hm)]

@[to_additive CauchySeq.norm_bddAbove]
lemma CauchySeq.mul_norm_bddAbove {G : Type*} [SeminormedGroup G] {u : ‚Ñï ‚Üí G}
    (hu : CauchySeq u) : BddAbove (Set.range (fun n ‚Ü¶ ‚Äñu n‚Äñ)) := by
  obtain ‚ü®C, -, hC‚ü© := cauchySeq_bdd hu
  simp_rw [SeminormedGroup.dist_eq] at hC
  have : ‚àÄ n, ‚Äñu n‚Äñ ‚â§ C + ‚Äñu 0‚Äñ := by
    intro n
    rw [add_comm]
    refine (norm_le_norm_add_norm_div' (u n) (u 0)).trans ?_
    simp [(hC _ _).le]
  rw [bddAbove_def]
  exact ‚ü®C + ‚Äñu 0‚Äñ, by simpa using this‚ü©

end SeminormedCommGroup
