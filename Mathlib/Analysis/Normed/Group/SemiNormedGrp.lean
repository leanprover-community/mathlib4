/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Riccardo Brasca
-/
import Mathlib.Analysis.Normed.Group.Constructions
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.CategoryTheory.Limits.Shapes.ZeroMorphisms
import Mathlib.CategoryTheory.Elementwise

/-!
# The category of seminormed groups

We define `SemiNormedGrp`, the category of seminormed groups and normed group homs between
them, as well as `SemiNormedGrp‚ÇÅ`, the subcategory of norm non-increasing morphisms.
-/


noncomputable section

universe u

open CategoryTheory

/-- The category of seminormed abelian groups and bounded group homomorphisms. -/
structure SemiNormedGrp : Type (u + 1) where
  /-- The underlying seminormed abelian group. -/
  carrier : Type u
  [str : SeminormedAddCommGroup carrier]

attribute [instance] SemiNormedGrp.str

namespace SemiNormedGrp

instance : CoeSort SemiNormedGrp Type* where
  coe X := X.carrier

/-- Construct a bundled `SemiNormedGrp` from the underlying type and typeclass. -/
abbrev of (M : Type u) [SeminormedAddCommGroup M] : SemiNormedGrp where
  carrier := M

/-- The type of morphisms in `SemiNormedGrp` -/
@[ext]
structure Hom (M N : SemiNormedGrp.{u}) where
  /-- The underlying `NormedAddGroupHom`. -/
  hom' : NormedAddGroupHom M N

instance : LargeCategory.{u} SemiNormedGrp where
  Hom X Y := Hom X Y
  id X := ‚ü®NormedAddGroupHom.id X‚ü©
  comp f g := ‚ü®g.hom'.comp f.hom'‚ü©

instance : ConcreteCategory SemiNormedGrp (NormedAddGroupHom ¬∑ ¬∑) where
  hom f := f.hom'
  ofHom f := ‚ü®f‚ü©

/-- Turn a morphism in `SemiNormedGrp` back into a `NormedAddGroupHom`. -/
abbrev Hom.hom {M N : SemiNormedGrp.{u}} (f : Hom M N) :=
  ConcreteCategory.hom (C := SemiNormedGrp) f

/-- Typecheck a `NormedAddGroupHom` as a morphism in `SemiNormedGrp`. -/
abbrev ofHom {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) : of M ‚ü∂ of N :=
  ConcreteCategory.ofHom (C := SemiNormedGrp) f

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (M N : SemiNormedGrp.{u}) (f : Hom M N) :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/
@[ext]
lemma ext {M N : SemiNormedGrp} {f‚ÇÅ f‚ÇÇ : M ‚ü∂ N} (h : ‚àÄ (x : M), f‚ÇÅ x = f‚ÇÇ x) : f‚ÇÅ = f‚ÇÇ :=
  ConcreteCategory.ext_apply h

@[simp]
lemma hom_id {M : SemiNormedGrp} : (ùüô M : M ‚ü∂ M).hom = NormedAddGroupHom.id M := rfl

/- Provided for rewriting. -/
lemma id_apply (M : SemiNormedGrp) (r : M) :
    (ùüô M : M ‚ü∂ M) r = r := by simp

@[simp]
lemma hom_comp {M N O : SemiNormedGrp} (f : M ‚ü∂ N) (g : N ‚ü∂ O) :
    (f ‚â´ g).hom = g.hom.comp f.hom := rfl

/- Provided for rewriting. -/
lemma comp_apply {M N O : SemiNormedGrp} (f : M ‚ü∂ N) (g : N ‚ü∂ O) (r : M) :
    (f ‚â´ g) r = g (f r) := by simp

@[ext]
lemma hom_ext {M N : SemiNormedGrp} {f g : M ‚ü∂ N} (hf : f.hom = g.hom) : f = g :=
  Hom.ext hf

@[simp]
lemma hom_ofHom {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) : (ofHom f).hom = f := rfl

@[simp]
lemma ofHom_hom {M N : SemiNormedGrp} (f : M ‚ü∂ N) :
    ofHom (Hom.hom f) = f := rfl

@[simp]
lemma ofHom_id {M : Type u} [SeminormedAddCommGroup M] :
    ofHom (NormedAddGroupHom.id M) = ùüô (of M) := rfl

@[simp]
lemma ofHom_comp {M N O : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    [SeminormedAddCommGroup O] (f : NormedAddGroupHom M N) (g : NormedAddGroupHom N O) :
    ofHom (g.comp f) = ofHom f ‚â´ ofHom g :=
  rfl

lemma ofHom_apply {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) (r : M) : ofHom f r = f r := rfl

lemma inv_hom_apply {M N : SemiNormedGrp} (e : M ‚âÖ N) (r : M) : e.inv (e.hom r) = r := by
  simp

lemma hom_inv_apply {M N : SemiNormedGrp} (e : M ‚âÖ N) (s : N) : e.hom (e.inv s) = s := by
  simp

theorem coe_of (V : Type u) [SeminormedAddCommGroup V] : (SemiNormedGrp.of V : Type u) = V :=
  rfl

theorem coe_id (V : SemiNormedGrp) : (ùüô V : V ‚Üí V) = id :=
  rfl

theorem coe_comp {M N K : SemiNormedGrp} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : M ‚Üí K) = g ‚àò f :=
  rfl

instance : Inhabited SemiNormedGrp :=
  ‚ü®of PUnit‚ü©

instance {M N : SemiNormedGrp} : Zero (M ‚ü∂ N) where
  zero := ofHom 0

@[simp]
theorem hom_zero {V W : SemiNormedGrp} : (0 : V ‚ü∂ W).hom = 0 :=
  rfl

theorem zero_apply {V W : SemiNormedGrp} (x : V) : (0 : V ‚ü∂ W) x = 0 :=
  rfl

instance : Limits.HasZeroMorphisms.{u, u + 1} SemiNormedGrp where

theorem isZero_of_subsingleton (V : SemiNormedGrp) [Subsingleton V] : Limits.IsZero V := by
  refine ‚ü®fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©‚ü©
  ¬∑ ext x; have : x = 0 := Subsingleton.elim _ _; simp only [this, map_zero]
  ¬∑ ext; apply Subsingleton.elim

instance hasZeroObject : Limits.HasZeroObject SemiNormedGrp.{u} :=
  ‚ü®‚ü®of PUnit, isZero_of_subsingleton _‚ü©‚ü©

theorem iso_isometry_of_normNoninc {V W : SemiNormedGrp} (i : V ‚âÖ W) (h1 : i.hom.hom.NormNoninc)
    (h2 : i.inv.hom.NormNoninc) : Isometry i.hom := by
  apply AddMonoidHomClass.isometry_of_norm
  intro v
  apply le_antisymm (h1 v)
  calc
    ‚Äñv‚Äñ = ‚Äñi.inv (i.hom v)‚Äñ := by rw [‚Üê comp_apply, Iso.hom_inv_id, id_apply]
    _ ‚â§ ‚Äñi.hom v‚Äñ := h2 _

instance Hom.add {M N : SemiNormedGrp} : Add (M ‚ü∂ N) where
  add f g := ofHom (f.hom + g.hom)

@[simp]
theorem hom_add {V W : SemiNormedGrp} (f g : V ‚ü∂ W) : (f + g).hom = f.hom + g.hom :=
  rfl

instance Hom.neg {M N : SemiNormedGrp} : Neg (M ‚ü∂ N) where
  neg f := ofHom (- f.hom)

@[simp]
theorem hom_neg {V W : SemiNormedGrp} (f : V ‚ü∂ W) : (-f).hom = - f.hom :=
  rfl

instance Hom.sub {M N : SemiNormedGrp} : Sub (M ‚ü∂ N) where
  sub f g := ofHom (f.hom - g.hom)

@[simp]
theorem hom_sub {V W : SemiNormedGrp} (f g : V ‚ü∂ W) : (f - g).hom = f.hom - g.hom :=
  rfl

instance Hom.nsmul {M N : SemiNormedGrp} : SMul ‚Ñï (M ‚ü∂ N) where
  smul n f := ofHom (n ‚Ä¢ f.hom)

@[simp]
theorem hom_nsum {V W : SemiNormedGrp} (n : ‚Ñï) (f : V ‚ü∂ W) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom :=
  rfl

instance Hom.zsmul {M N : SemiNormedGrp} : SMul ‚Ñ§ (M ‚ü∂ N) where
  smul n f := ofHom (n ‚Ä¢ f.hom)

@[simp]
theorem hom_zsum {V W : SemiNormedGrp} (n : ‚Ñ§) (f : V ‚ü∂ W) : (n ‚Ä¢ f).hom = n ‚Ä¢ f.hom :=
  rfl

instance Hom.addCommGroup {V W : SemiNormedGrp} : AddCommGroup (V ‚ü∂ W) :=
  Function.Injective.addCommGroup _ ConcreteCategory.hom_injective rfl (fun _ _ => rfl)
    (fun _ => rfl) (fun _ _ => rfl) (fun _ _ => rfl) (fun _ _ => rfl)

end SemiNormedGrp

/-- `SemiNormedGrp‚ÇÅ` is a type synonym for `SemiNormedGrp`,
which we shall equip with the category structure consisting only of the norm non-increasing maps.
-/
structure SemiNormedGrp‚ÇÅ : Type (u + 1) where
  /-- The underlying seminormed abelian group. -/
  carrier : Type u
  [str : SeminormedAddCommGroup carrier]

attribute [instance] SemiNormedGrp‚ÇÅ.str

namespace SemiNormedGrp‚ÇÅ

instance : CoeSort SemiNormedGrp‚ÇÅ Type* where
  coe X := X.carrier

/-- Construct a bundled `SemiNormedGrp‚ÇÅ` from the underlying type and typeclass. -/
abbrev of (M : Type u) [SeminormedAddCommGroup M] : SemiNormedGrp‚ÇÅ where
  carrier := M

/-- The type of morphisms in `SemiNormedGrp‚ÇÅ` -/
@[ext]
structure Hom (M N : SemiNormedGrp‚ÇÅ.{u}) where
  /-- The underlying `NormedAddGroupHom`. -/
  hom' : NormedAddGroupHom M N
  normNoninc : hom'.NormNoninc

instance : LargeCategory.{u} SemiNormedGrp‚ÇÅ where
  Hom := Hom
  id X := ‚ü®NormedAddGroupHom.id X, NormedAddGroupHom.NormNoninc.id‚ü©
  comp {_ _ _} f g := ‚ü®g.1.comp f.1, g.2.comp f.2‚ü©

instance instFunLike (X Y : SemiNormedGrp‚ÇÅ) :
    FunLike { f : NormedAddGroupHom X Y // f.NormNoninc } X Y where
  coe f := f.1.toFun
  coe_injective' _ _ h := Subtype.val_inj.mp (NormedAddGroupHom.coe_injective h)

instance : ConcreteCategory SemiNormedGrp‚ÇÅ
    fun X Y => { f : NormedAddGroupHom X Y // f.NormNoninc } where
  hom f := ‚ü®f.1, f.2‚ü©
  ofHom f := ‚ü®f.1, f.2‚ü©

instance (X Y : SemiNormedGrp‚ÇÅ) :
    AddMonoidHomClass { f : NormedAddGroupHom X Y // f.NormNoninc } X Y where
  map_add f := map_add f.1
  map_zero f := map_zero f.1

/-- Turn a morphism in `SemiNormedGrp‚ÇÅ` back into a norm-nonincreasing `NormedAddGroupHom`. -/
abbrev Hom.hom {M N : SemiNormedGrp‚ÇÅ.{u}} (f : Hom M N) :=
  ConcreteCategory.hom (C := SemiNormedGrp‚ÇÅ) f

/-- Promote a `NormedAddGroupHom` to a morphism in `SemiNormedGrp‚ÇÅ`. -/
abbrev mkHom {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) (i : f.NormNoninc) :
    SemiNormedGrp‚ÇÅ.of M ‚ü∂ SemiNormedGrp‚ÇÅ.of N :=
  ConcreteCategory.ofHom ‚ü®f, i‚ü©

/-- Use the `ConcreteCategory.hom` projection for `@[simps]` lemmas. -/
def Hom.Simps.hom (M N : SemiNormedGrp‚ÇÅ.{u}) (f : Hom M N) : NormedAddGroupHom M N :=
  f.hom

initialize_simps_projections Hom (hom' ‚Üí hom)

instance (X Y : SemiNormedGrp‚ÇÅ) : CoeFun (X ‚ü∂ Y) (fun _ => X ‚Üí Y) where
  coe f := f.hom.1

theorem mkHom_apply {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) (i : f.NormNoninc) (x) :
    mkHom f i x = f x :=
  rfl

/-!
The results below duplicate the `ConcreteCategory` simp lemmas, but we can keep them for `dsimp`.
-/
@[ext]
lemma ext {M N : SemiNormedGrp‚ÇÅ} {f‚ÇÅ f‚ÇÇ : M ‚ü∂ N} (h : ‚àÄ (x : M), f‚ÇÅ x = f‚ÇÇ x) : f‚ÇÅ = f‚ÇÇ :=
  ConcreteCategory.ext_apply h

@[simp]
lemma hom_id {M : SemiNormedGrp‚ÇÅ} : (ùüô M : M ‚ü∂ M).hom = NormedAddGroupHom.id M := rfl

/- Provided for rewriting. -/
lemma id_apply (M : SemiNormedGrp‚ÇÅ) (r : M) :
    (ùüô M : M ‚ü∂ M) r = r := by simp

@[simp]
lemma hom_comp {M N O : SemiNormedGrp‚ÇÅ} (f : M ‚ü∂ N) (g : N ‚ü∂ O) :
    (f ‚â´ g).hom.1 = g.hom.1.comp f.hom.1 := rfl

/- Provided for rewriting. -/
lemma comp_apply {M N O : SemiNormedGrp‚ÇÅ} (f : M ‚ü∂ N) (g : N ‚ü∂ O) (r : M) :
    (f ‚â´ g) r = g (f r) := by simp

@[ext]
lemma hom_ext {M N : SemiNormedGrp‚ÇÅ} {f g : M ‚ü∂ N} (hf : f.hom = g.hom) : f = g :=
  Hom.ext (congr_arg Subtype.val hf)

@[simp]
lemma hom_mkHom {M N : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    (f : NormedAddGroupHom M N) (hf : f.NormNoninc) : (mkHom f hf).hom = f := rfl

@[simp]
lemma mkHom_hom {M N : SemiNormedGrp‚ÇÅ} (f : M ‚ü∂ N) :
    mkHom (Hom.hom f) f.normNoninc = f := rfl

@[simp]
lemma mkHom_id {M : Type u} [SeminormedAddCommGroup M] :
    mkHom (NormedAddGroupHom.id M) NormedAddGroupHom.NormNoninc.id = ùüô (of M) := rfl

@[simp]
lemma mkHom_comp {M N O : Type u} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]
    [SeminormedAddCommGroup O] (f : NormedAddGroupHom M N) (g : NormedAddGroupHom N O)
    (hf : f.NormNoninc) (hg : g.NormNoninc) (hgf : (g.comp f).NormNoninc) :
    mkHom (g.comp f) hgf = mkHom f hf ‚â´ mkHom g hg :=
  rfl

@[simp]
lemma inv_hom_apply {M N : SemiNormedGrp‚ÇÅ} (e : M ‚âÖ N) (r : M) : e.inv (e.hom r) = r := by
  rw [‚Üê comp_apply]
  simp

@[simp]
lemma hom_inv_apply {M N : SemiNormedGrp‚ÇÅ} (e : M ‚âÖ N) (s : N) : e.hom (e.inv s) = s := by
  rw [‚Üê comp_apply]
  simp

instance (M : SemiNormedGrp‚ÇÅ) : SeminormedAddCommGroup M :=
  M.str

/-- Promote an isomorphism in `SemiNormedGrp` to an isomorphism in `SemiNormedGrp‚ÇÅ`. -/
@[simps]
def mkIso {M N : SemiNormedGrp} (f : M ‚âÖ N) (i : f.hom.hom.NormNoninc) (i' : f.inv.hom.NormNoninc) :
    SemiNormedGrp‚ÇÅ.of M ‚âÖ SemiNormedGrp‚ÇÅ.of N where
  hom := mkHom f.hom.hom i
  inv := mkHom f.inv.hom i'

instance : HasForget‚ÇÇ SemiNormedGrp‚ÇÅ SemiNormedGrp where
  forget‚ÇÇ :=
    { obj := fun X => SemiNormedGrp.of X
      map := fun f => SemiNormedGrp.ofHom f.1 }

theorem coe_of (V : Type u) [SeminormedAddCommGroup V] : (SemiNormedGrp‚ÇÅ.of V : Type u) = V :=
  rfl

theorem coe_id (V : SemiNormedGrp‚ÇÅ) : ‚áë(ùüô V) = id :=
  rfl

theorem coe_comp {M N K : SemiNormedGrp‚ÇÅ} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : M ‚Üí K) = g ‚àò f :=
  rfl

instance : Inhabited SemiNormedGrp‚ÇÅ :=
  ‚ü®of PUnit‚ü©

instance (X Y : SemiNormedGrp‚ÇÅ) : Zero (X ‚ü∂ Y) where
  zero := ‚ü®0, NormedAddGroupHom.NormNoninc.zero‚ü©

@[simp]
theorem zero_apply {V W : SemiNormedGrp‚ÇÅ} (x : V) : (0 : V ‚ü∂ W) x = 0 :=
  rfl

instance : Limits.HasZeroMorphisms.{u, u + 1} SemiNormedGrp‚ÇÅ where

theorem isZero_of_subsingleton (V : SemiNormedGrp‚ÇÅ) [Subsingleton V] : Limits.IsZero V := by
  refine ‚ü®fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©‚ü©
  ¬∑ ext x; have : x = 0 := Subsingleton.elim _ _; simp only [this, map_zero]
  ¬∑ ext; apply Subsingleton.elim

instance hasZeroObject : Limits.HasZeroObject SemiNormedGrp‚ÇÅ.{u} :=
  ‚ü®‚ü®of PUnit, isZero_of_subsingleton _‚ü©‚ü©

theorem iso_isometry {V W : SemiNormedGrp‚ÇÅ} (i : V ‚âÖ W) : Isometry i.hom := by
  change Isometry (‚ü®‚ü®i.hom, map_zero _‚ü©, fun _ _ => map_add _ _ _‚ü© : V ‚Üí+ W)
  refine AddMonoidHomClass.isometry_of_norm _ ?_
  intro v
  apply le_antisymm (i.hom.2 v)
  calc
    ‚Äñv‚Äñ = ‚Äñi.inv (i.hom v)‚Äñ := by rw [‚Üê comp_apply, Iso.hom_inv_id, id_apply]
    _ ‚â§ ‚Äñi.hom v‚Äñ := i.inv.2 _

end SemiNormedGrp‚ÇÅ
