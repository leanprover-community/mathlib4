/-
Copyright (c) 2021 Johan Commelin. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Riccardo Brasca
-/
import Mathlib.Analysis.Normed.Group.Constructions
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.CategoryTheory.Limits.Shapes.ZeroMorphisms
import Mathlib.CategoryTheory.ConcreteCategory.BundledHom
import Mathlib.CategoryTheory.Elementwise

#align_import analysis.normed.group.SemiNormedGroup from "leanprover-community/mathlib"@"17ef379e997badd73e5eabb4d38f11919ab3c4b3"

/-!
# The category of seminormed groups

We define `SemiNormedGrp`, the category of seminormed groups and normed group homs between
them, as well as `SemiNormedGrp‚ÇÅ`, the subcategory of norm non-increasing morphisms.
-/

set_option linter.uppercaseLean3 false

noncomputable section

universe u

open CategoryTheory

/-- The category of seminormed abelian groups and bounded group homomorphisms. -/
def SemiNormedGrp : Type (u + 1) :=
  Bundled SeminormedAddCommGroup
#align SemiNormedGroup SemiNormedGrp

namespace SemiNormedGrp

instance bundledHom : BundledHom @NormedAddGroupHom where
  toFun := @NormedAddGroupHom.toFun
  id := @NormedAddGroupHom.id
  comp := @NormedAddGroupHom.comp
#align SemiNormedGroup.bundled_hom SemiNormedGrp.bundledHom

deriving instance LargeCategory for SemiNormedGrp

-- Porting note: deriving fails for ConcreteCategory, adding instance manually.
-- See https://github.com/leanprover-community/mathlib4/issues/5020
-- deriving instance LargeCategory, ConcreteCategory for SemiRingCat
instance : ConcreteCategory SemiNormedGrp := by
  dsimp [SemiNormedGrp]
  infer_instance

instance : CoeSort SemiNormedGrp Type* where
  coe X := X.Œ±

/-- Construct a bundled `SemiNormedGrp` from the underlying type and typeclass. -/
def of (M : Type u) [SeminormedAddCommGroup M] : SemiNormedGrp :=
  Bundled.of M
#align SemiNormedGrp.of SemiNormedGrp.of

instance (M : SemiNormedGrp) : SeminormedAddCommGroup M :=
  M.str

-- Porting note (#10754): added instance
instance funLike {V W : SemiNormedGrp} : FunLike (V ‚ü∂ W) V W where
  coe := (forget SemiNormedGrp).map
  coe_injective' := fun f g h => by cases f; cases g; congr

instance toAddMonoidHomClass {V W : SemiNormedGrp} : AddMonoidHomClass (V ‚ü∂ W) V W where
  map_add f := f.map_add'
  map_zero f := (AddMonoidHom.mk' f.toFun f.map_add').map_zero

-- Porting note (#10688): added to ease automation
@[ext]
lemma ext {M N : SemiNormedGrp} {f‚ÇÅ f‚ÇÇ : M ‚ü∂ N} (h : ‚àÄ (x : M), f‚ÇÅ x = f‚ÇÇ x) : f‚ÇÅ = f‚ÇÇ :=
  DFunLike.ext _ _ h

@[simp]
theorem coe_of (V : Type u) [SeminormedAddCommGroup V] : (SemiNormedGrp.of V : Type u) = V :=
  rfl
#align SemiNormedGroup.coe_of SemiNormedGrp.coe_of

-- Porting note: marked with high priority to short circuit simplifier's path
@[simp (high)]
theorem coe_id (V : SemiNormedGrp) : (ùüô V : V ‚Üí V) = id :=
  rfl
#align SemiNormedGroup.coe_id SemiNormedGrp.coe_id

-- Porting note: marked with high priority to short circuit simplifier's path
@[simp (high)]
theorem coe_comp {M N K : SemiNormedGrp} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : M ‚Üí K) = g ‚àò f :=
  rfl
#align SemiNormedGroup.coe_comp SemiNormedGrp.coe_comp

instance : Inhabited SemiNormedGrp :=
  ‚ü®of PUnit‚ü©

instance ofUnique (V : Type u) [SeminormedAddCommGroup V] [i : Unique V] :
    Unique (SemiNormedGrp.of V) :=
  i
#align SemiNormedGroup.of_unique SemiNormedGrp.ofUnique

instance {M N : SemiNormedGrp} : Zero (M ‚ü∂ N) :=
  NormedAddGroupHom.zero

@[simp]
theorem zero_apply {V W : SemiNormedGrp} (x : V) : (0 : V ‚ü∂ W) x = 0 :=
  rfl
#align SemiNormedGroup.zero_apply SemiNormedGrp.zero_apply

instance : Limits.HasZeroMorphisms.{u, u + 1} SemiNormedGrp where

theorem isZero_of_subsingleton (V : SemiNormedGrp) [Subsingleton V] : Limits.IsZero V := by
  refine ‚ü®fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©‚ü©
  ¬∑ ext x; have : x = 0 := Subsingleton.elim _ _; simp only [this, map_zero]
  ¬∑ ext; apply Subsingleton.elim
#align SemiNormedGroup.is_zero_of_subsingleton SemiNormedGrp.isZero_of_subsingleton

instance hasZeroObject : Limits.HasZeroObject SemiNormedGrp.{u} :=
  ‚ü®‚ü®of PUnit, isZero_of_subsingleton _‚ü©‚ü©
#align SemiNormedGroup.has_zero_object SemiNormedGrp.hasZeroObject

theorem iso_isometry_of_normNoninc {V W : SemiNormedGrp} (i : V ‚âÖ W) (h1 : i.hom.NormNoninc)
    (h2 : i.inv.NormNoninc) : Isometry i.hom := by
  apply AddMonoidHomClass.isometry_of_norm
  intro v
  apply le_antisymm (h1 v)
  calc
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    ‚Äñv‚Äñ = ‚Äñi.inv (i.hom v)‚Äñ := by erw [Iso.hom_inv_id_apply]
    _ ‚â§ ‚Äñi.hom v‚Äñ := h2 _
#align SemiNormedGroup.iso_isometry_of_norm_noninc SemiNormedGrp.iso_isometry_of_normNoninc

end SemiNormedGrp

/-- `SemiNormedGrp‚ÇÅ` is a type synonym for `SemiNormedGrp`,
which we shall equip with the category structure consisting only of the norm non-increasing maps.
-/
def SemiNormedGrp‚ÇÅ : Type (u + 1) :=
  Bundled SeminormedAddCommGroup
#align SemiNormedGroup‚ÇÅ SemiNormedGrp‚ÇÅ

namespace SemiNormedGrp‚ÇÅ

instance : CoeSort SemiNormedGrp‚ÇÅ Type* where
  coe X := X.Œ±

instance : LargeCategory.{u} SemiNormedGrp‚ÇÅ where
  Hom X Y := { f : NormedAddGroupHom X Y // f.NormNoninc }
  id X := ‚ü®NormedAddGroupHom.id X, NormedAddGroupHom.NormNoninc.id‚ü©
  comp {X Y Z} f g := ‚ü®g.1.comp f.1, g.2.comp f.2‚ü©

-- Porting note (#10754): added instance
instance instFunLike (X Y : SemiNormedGrp‚ÇÅ) : FunLike (X ‚ü∂ Y) X Y where
  coe f := f.1.toFun
  coe_injective' _ _ h := Subtype.val_inj.mp (NormedAddGroupHom.coe_injective h)

@[ext]
theorem hom_ext {M N : SemiNormedGrp‚ÇÅ} (f g : M ‚ü∂ N) (w : (f : M ‚Üí N) = (g : M ‚Üí N)) :
    f = g :=
  Subtype.eq (NormedAddGroupHom.ext (congr_fun w))
#align SemiNormedGroup‚ÇÅ.hom_ext SemiNormedGrp‚ÇÅ.hom_ext

instance : ConcreteCategory.{u} SemiNormedGrp‚ÇÅ where
  forget :=
    { obj := fun X => X
      map := fun f => f }
  forget_faithful := { }

-- Porting note (#10754): added instance
instance toAddMonoidHomClass {V W : SemiNormedGrp‚ÇÅ} : AddMonoidHomClass (V ‚ü∂ W) V W where
  map_add f := f.1.map_add'
  map_zero f := (AddMonoidHom.mk' f.1 f.1.map_add').map_zero

/-- Construct a bundled `SemiNormedGrp‚ÇÅ` from the underlying type and typeclass. -/
def of (M : Type u) [SeminormedAddCommGroup M] : SemiNormedGrp‚ÇÅ :=
  Bundled.of M
#align SemiNormedGroup‚ÇÅ.of SemiNormedGrp‚ÇÅ.of

instance (M : SemiNormedGrp‚ÇÅ) : SeminormedAddCommGroup M :=
  M.str

/-- Promote a morphism in `SemiNormedGrp` to a morphism in `SemiNormedGrp‚ÇÅ`. -/
def mkHom {M N : SemiNormedGrp} (f : M ‚ü∂ N) (i : f.NormNoninc) :
    SemiNormedGrp‚ÇÅ.of M ‚ü∂ SemiNormedGrp‚ÇÅ.of N :=
  ‚ü®f, i‚ü©
#align SemiNormedGroup‚ÇÅ.mk_hom SemiNormedGrp‚ÇÅ.mkHom

-- @[simp] -- Porting note: simpNF linter claims LHS simplifies with `SemiNormedGrp‚ÇÅ.coe_of`
theorem mkHom_apply {M N : SemiNormedGrp} (f : M ‚ü∂ N) (i : f.NormNoninc) (x) :
    mkHom f i x = f x :=
  rfl
#align SemiNormedGroup‚ÇÅ.mk_hom_apply SemiNormedGrp‚ÇÅ.mkHom_apply

/-- Promote an isomorphism in `SemiNormedGrp` to an isomorphism in `SemiNormedGrp‚ÇÅ`. -/
@[simps]
def mkIso {M N : SemiNormedGrp} (f : M ‚âÖ N) (i : f.hom.NormNoninc) (i' : f.inv.NormNoninc) :
    SemiNormedGrp‚ÇÅ.of M ‚âÖ SemiNormedGrp‚ÇÅ.of N where
  hom := mkHom f.hom i
  inv := mkHom f.inv i'
  hom_inv_id := by apply Subtype.eq; exact f.hom_inv_id
  inv_hom_id := by apply Subtype.eq; exact f.inv_hom_id
#align SemiNormedGroup‚ÇÅ.mk_iso SemiNormedGrp‚ÇÅ.mkIso

instance : HasForget‚ÇÇ SemiNormedGrp‚ÇÅ SemiNormedGrp where
  forget‚ÇÇ :=
    { obj := fun X => X
      map := fun f => f.1 }

@[simp]
theorem coe_of (V : Type u) [SeminormedAddCommGroup V] : (SemiNormedGrp‚ÇÅ.of V : Type u) = V :=
  rfl
#align SemiNormedGroup‚ÇÅ.coe_of SemiNormedGrp‚ÇÅ.coe_of

-- Porting note: marked with high priority to short circuit simplifier's path
@[simp (high)]
theorem coe_id (V : SemiNormedGrp‚ÇÅ) : ‚áë(ùüô V) = id :=
  rfl
#align SemiNormedGroup‚ÇÅ.coe_id SemiNormedGrp‚ÇÅ.coe_id

-- Porting note: marked with high priority to short circuit simplifier's path
@[simp (high)]
theorem coe_comp {M N K : SemiNormedGrp‚ÇÅ} (f : M ‚ü∂ N) (g : N ‚ü∂ K) :
    (f ‚â´ g : M ‚Üí K) = g ‚àò f :=
  rfl
#align SemiNormedGroup‚ÇÅ.coe_comp SemiNormedGrp‚ÇÅ.coe_comp

-- Porting note: deleted `coe_comp'`, as we no longer have the relevant coercion.
#noalign SemiNormedGroup‚ÇÅ.coe_comp'

instance : Inhabited SemiNormedGrp‚ÇÅ :=
  ‚ü®of PUnit‚ü©

instance ofUnique (V : Type u) [SeminormedAddCommGroup V] [i : Unique V] :
    Unique (SemiNormedGrp‚ÇÅ.of V) :=
  i
#align SemiNormedGroup‚ÇÅ.of_unique SemiNormedGrp‚ÇÅ.ofUnique

-- Porting note: extracted from `Limits.HasZeroMorphisms` instance below.
instance (X Y : SemiNormedGrp‚ÇÅ) : Zero (X ‚ü∂ Y) where
  zero := ‚ü®0, NormedAddGroupHom.NormNoninc.zero‚ü©

@[simp]
theorem zero_apply {V W : SemiNormedGrp‚ÇÅ} (x : V) : (0 : V ‚ü∂ W) x = 0 :=
  rfl
#align SemiNormedGroup‚ÇÅ.zero_apply SemiNormedGrp‚ÇÅ.zero_apply

instance : Limits.HasZeroMorphisms.{u, u + 1} SemiNormedGrp‚ÇÅ where

theorem isZero_of_subsingleton (V : SemiNormedGrp‚ÇÅ) [Subsingleton V] : Limits.IsZero V := by
  refine ‚ü®fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©, fun X => ‚ü®‚ü®‚ü®0‚ü©, fun f => ?_‚ü©‚ü©‚ü©
  ¬∑ ext x; have : x = 0 := Subsingleton.elim _ _; simp only [this, map_zero]
  ¬∑ ext; apply Subsingleton.elim
#align SemiNormedGroup‚ÇÅ.is_zero_of_subsingleton SemiNormedGrp‚ÇÅ.isZero_of_subsingleton

instance hasZeroObject : Limits.HasZeroObject SemiNormedGrp‚ÇÅ.{u} :=
  ‚ü®‚ü®of PUnit, isZero_of_subsingleton _‚ü©‚ü©
#align SemiNormedGroup‚ÇÅ.has_zero_object SemiNormedGrp‚ÇÅ.hasZeroObject

theorem iso_isometry {V W : SemiNormedGrp‚ÇÅ} (i : V ‚âÖ W) : Isometry i.hom := by
  change Isometry (‚ü®‚ü®i.hom, map_zero _‚ü©, fun _ _ => map_add _ _ _‚ü© : V ‚Üí+ W)
  refine AddMonoidHomClass.isometry_of_norm _ ?_
  intro v
  apply le_antisymm (i.hom.2 v)
  calc
    -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
    ‚Äñv‚Äñ = ‚Äñi.inv (i.hom v)‚Äñ := by erw [Iso.hom_inv_id_apply]
    _ ‚â§ ‚Äñi.hom v‚Äñ := i.inv.2 _
#align SemiNormedGroup‚ÇÅ.iso_isometry SemiNormedGrp‚ÇÅ.iso_isometry

end SemiNormedGrp‚ÇÅ
