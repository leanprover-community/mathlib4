/-
Copyright (c) 2021 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Riccardo Brasca
-/
import Mathlib.Analysis.Normed.Module.Basic
import Mathlib.Analysis.Normed.Group.Hom
import Mathlib.RingTheory.Ideal.Quotient.Operations
import Mathlib.Topology.MetricSpace.HausdorffDistance

/-!
# Quotients of seminormed groups

For any `SeminormedAddCommGroup M` and any `S : AddSubgroup M`, we provide a
`SeminormedAddCommGroup`, the group quotient `M ‚ß∏ S`.
If `S` is closed, we provide `NormedAddCommGroup (M ‚ß∏ S)` (regardless of whether `M` itself is
separated). The two main properties of these structures are the underlying topology is the quotient
topology and the projection is a normed group homomorphism which is norm non-increasing
(better, it has operator norm exactly one unless `S` is dense in `M`). The corresponding
universal property is that every normed group hom defined on `M` which vanishes on `S` descends
to a normed group hom defined on `M ‚ß∏ S`.

This file also introduces a predicate `IsQuotient` characterizing normed group homs that
are isomorphic to the canonical projection onto a normed group quotient.

In addition, this file also provides normed structures for quotients of modules by submodules, and
of (commutative) rings by ideals. The `SeminormedAddCommGroup` and `NormedAddCommGroup`
instances described above are transferred directly, but we also define instances of `NormedSpace`,
`SeminormedCommRing`, `NormedCommRing` and `NormedAlgebra` under appropriate type class
assumptions on the original space. Moreover, while `QuotientAddGroup.completeSpace` works
out-of-the-box for quotients of `NormedAddCommGroup`s by `AddSubgroup`s, we need to transfer
this instance in `Submodule.Quotient.completeSpace` so that it applies to these other quotients.

## Main definitions


We use `M` and `N` to denote seminormed groups and `S : AddSubgroup M`.
All the following definitions are in the `AddSubgroup` namespace. Hence we can access
`AddSubgroup.normedMk S` as `S.normedMk`.

* `seminormedAddCommGroupQuotient` : The seminormed group structure on the quotient by
    an additive subgroup. This is an instance so there is no need to explicitly use it.

* `normedAddCommGroupQuotient` : The normed group structure on the quotient by
    a closed additive subgroup. This is an instance so there is no need to explicitly use it.

* `normedMk S` : the normed group hom from `M` to `M ‚ß∏ S`.

* `lift S f hf`: implements the universal property of `M ‚ß∏ S`. Here
    `(f : NormedAddGroupHom M N)`, `(hf : ‚àÄ s ‚àà S, f s = 0)` and
    `lift S f hf : NormedAddGroupHom (M ‚ß∏ S) N`.

* `IsQuotient`: given `f : NormedAddGroupHom M N`, `IsQuotient f` means `N` is isomorphic
    to a quotient of `M` by a subgroup, with projection `f`. Technically it asserts `f` is
    surjective and the norm of `f x` is the infimum of the norms of `x + m` for `m` in `f.ker`.

## Main results

* `norm_normedMk` : the operator norm of the projection is `1` if the subspace is not dense.

* `IsQuotient.norm_lift`: Provided `f : normed_hom M N` satisfies `IsQuotient f`, for every
     `n : N` and positive `Œµ`, there exists `m` such that `f m = n ‚àß ‚Äñm‚Äñ < ‚Äñn‚Äñ + Œµ`.


## Implementation details

For any `SeminormedAddCommGroup M` and any `S : AddSubgroup M` we define a norm on `M ‚ß∏ S` by
`‚Äñx‚Äñ = sInf (norm '' {m | mk' S m = x})`. This formula is really an implementation detail, it
shouldn't be needed outside of this file setting up the theory.

Since `M ‚ß∏ S` is automatically a topological space (as any quotient of a topological space),
one needs to be careful while defining the `SeminormedAddCommGroup` instance to avoid having two
different topologies on this quotient. This is not purely a technological issue.
Mathematically there is something to prove. The main point is proved in the auxiliary lemma
`quotient_nhds_basis` that has no use beyond this verification and states that zero in the quotient
admits as basis of neighborhoods in the quotient topology the sets `{x | ‚Äñx‚Äñ < Œµ}` for positive `Œµ`.

Once this mathematical point is settled, we have two topologies that are propositionally equal. This
is not good enough for the type class system. As usual we ensure *definitional* equality
using forgetful inheritance, see Note [forgetful inheritance]. A (semi)-normed group structure
includes a uniform space structure which includes a topological space structure, together
with propositional fields asserting compatibility conditions.
The usual way to define a `SeminormedAddCommGroup` is to let Lean build a uniform space structure
using the provided norm, and then trivially build a proof that the norm and uniform structure are
compatible. Here the uniform structure is provided using `IsTopologicalAddGroup.toUniformSpace`
which uses the topological structure and the group structure to build the uniform structure. This
uniform structure induces the correct topological structure by construction, but the fact that it
is compatible with the norm is not obvious; this is where the mathematical content explained in
the previous paragraph kicks in.

-/


noncomputable section

open Metric Set Topology NNReal

namespace QuotientGroup
variable {M : Type*} [SeminormedCommGroup M] {S : Subgroup M} {x : M ‚ß∏ S} {m : M} {r Œµ : ‚Ñù}

@[to_additive add_norm_aux]
private lemma norm_aux (x : M ‚ß∏ S) : {m : M | (m : M ‚ß∏ S) = x}.Nonempty := Quot.exists_rep x

/-- The norm of `x` on the quotient by a subgroup `S` is defined as the infimum of the norm on
`x * M`. -/
@[to_additive
/-- The norm of `x` on the quotient by a subgroup `S` is defined as the infimum of the norm on
`x + S`. -/]
noncomputable def groupSeminorm : GroupSeminorm (M ‚ß∏ S) where
  toFun x := infDist 1 {m : M | (m : M ‚ß∏ S) = x}
  map_one' := infDist_zero_of_mem (by simp)
  mul_le' x y := by
    simp only [infDist_eq_iInf]
    have := (norm_aux x).to_subtype
    have := (norm_aux y).to_subtype
    refine le_ciInf_add_ciInf ?_
    rintro ‚ü®a, rfl‚ü© ‚ü®b, rfl‚ü©
    refine ciInf_le_of_le ‚ü®0, forall_mem_range.2 fun _ ‚Ü¶ dist_nonneg‚ü© ‚ü®a * b, rfl‚ü© ?_
    simpa using norm_mul_le' _ _
  inv' x := eq_of_forall_le_iff fun r ‚Ü¶ by
    simp only [le_infDist (norm_aux _)]
    exact (Equiv.inv _).forall_congr (by simp [‚Üê inv_eq_iff_eq_inv])

/-- The norm of `x` on the quotient by a subgroup `S` is defined as the infimum of the norm on
`x * S`. -/
@[to_additive
/-- The norm of `x` on the quotient by a subgroup `S` is defined as the infimum of the norm on
`x + S`. -/]
noncomputable instance instNorm : Norm (M ‚ß∏ S) where norm := groupSeminorm

@[to_additive]
lemma norm_eq_groupSeminorm (x : M ‚ß∏ S) : ‚Äñx‚Äñ = groupSeminorm x := rfl

@[to_additive]
lemma norm_eq_infDist (x : M ‚ß∏ S) : ‚Äñx‚Äñ = infDist 1 {m : M | (m : M ‚ß∏ S) = x} := rfl

@[to_additive]
lemma le_norm_iff : r ‚â§ ‚Äñx‚Äñ ‚Üî ‚àÄ m : M, ‚Üëm = x ‚Üí r ‚â§ ‚Äñm‚Äñ := by
  simp [norm_eq_infDist, le_infDist (norm_aux _)]

@[to_additive]
lemma norm_lt_iff : ‚Äñx‚Äñ < r ‚Üî ‚àÉ m : M, ‚Üëm = x ‚àß ‚Äñm‚Äñ < r := by
  simp [norm_eq_infDist, infDist_lt_iff (norm_aux _)]

@[to_additive]
lemma nhds_one_hasBasis : (ùìù (1 : M ‚ß∏ S)).HasBasis (fun Œµ ‚Ü¶ 0 < Œµ) fun Œµ ‚Ü¶ {x | ‚Äñx‚Äñ < Œµ} := by
  have : ‚àÄ Œµ : ‚Ñù, mk '' ball (1 : M) Œµ = {x : M ‚ß∏ S | ‚Äñx‚Äñ < Œµ} := by
    refine fun Œµ ‚Ü¶ Set.ext <| forall_mk.2 fun x ‚Ü¶ ?_
    rw [ball_one_eq, mem_setOf_eq, norm_lt_iff, mem_image]
    exact exists_congr fun _ ‚Ü¶ and_comm
  rw [‚Üê mk_one, nhds_eq, ‚Üê funext this]
  exact .map _ Metric.nhds_basis_ball

/-- An alternative definition of the norm on the quotient group: the norm of `((x : M) : M ‚ß∏ S)` is
equal to the distance from `x` to `S`. -/
@[to_additive
/-- An alternative definition of the norm on the quotient group: the norm of `((x : M) : M ‚ß∏ S)` is
equal to the distance from `x` to `S`. -/]
lemma norm_mk (x : M) : ‚Äñ(x : M ‚ß∏ S)‚Äñ = infDist x S := by
  rw [norm_eq_infDist, ‚Üê infDist_image (IsometryEquiv.divLeft x).isometry,
    ‚Üê IsometryEquiv.preimage_symm]
  simp

/-- The norm of the projection is smaller or equal to the norm of the original element. -/
@[to_additive
/-- The norm of the projection is smaller or equal to the norm of the original element. -/]
lemma norm_mk_le_norm : ‚Äñ(m : M ‚ß∏ S)‚Äñ ‚â§ ‚Äñm‚Äñ :=
  (infDist_le_dist_of_mem (by simp)).trans_eq (dist_one_left _)

/-- The norm of the image of `m : M` in the quotient by `S` is zero if and only if `m` belongs
to the closure of `S`. -/
@[to_additive /-- The norm of the image of `m : M` in the quotient by `S` is zero if and only if `m`
belongs to the closure of `S`. -/]
lemma norm_mk_eq_zero_iff_mem_closure : ‚Äñ(m : M ‚ß∏ S)‚Äñ = 0 ‚Üî m ‚àà closure (S : Set M) := by
  rw [norm_mk, ‚Üê mem_closure_iff_infDist_zero]
  exact ‚ü®1, S.one_mem‚ü©

/-- The norm of the image of `m : M` in the quotient by a closed subgroup `S` is zero if and only if
`m ‚àà S`. -/
@[to_additive /-- The norm of the image of `m : M` in the quotient by a closed subgroup `S` is zero
if and only if `m ‚àà S`. -/]
lemma norm_mk_eq_zero [hS : IsClosed (S : Set M)] : ‚Äñ(m : M ‚ß∏ S)‚Äñ = 0 ‚Üî m ‚àà S := by
  rw [norm_mk_eq_zero_iff_mem_closure, hS.closure_eq, SetLike.mem_coe]

/-- For any `x : M ‚ß∏ S` and any `0 < Œµ`, there is `m : M` such that `mk' S m = x`
and `‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ`. -/
@[to_additive /-- For any `x : M ‚ß∏ S` and any `0 < Œµ`, there is `m : M` such that `mk' S m = x`
and `‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ`. -/]
lemma exists_norm_mk_lt (x : M ‚ß∏ S) (hŒµ : 0 < Œµ) : ‚àÉ m : M, m = x ‚àß ‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ :=
  norm_lt_iff.1 <| lt_add_of_pos_right _ hŒµ

/-- For any `m : M` and any `0 < Œµ`, there is `s ‚àà S` such that `‚Äñm * s‚Äñ < ‚Äñmk' S m‚Äñ + Œµ`. -/
@[to_additive
/-- For any `m : M` and any `0 < Œµ`, there is `s ‚àà S` such that `‚Äñm + s‚Äñ < ‚Äñmk' S m‚Äñ + Œµ`. -/]
lemma exists_norm_mul_lt (S : Subgroup M) (m : M) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ s ‚àà S, ‚Äñm * s‚Äñ < ‚Äñmk' S m‚Äñ + Œµ := by
  obtain ‚ü®n : M, hn, hn'‚ü© := exists_norm_mk_lt (QuotientGroup.mk' S m) hŒµ
  exact ‚ü®m‚Åª¬π * n, by simpa [eq_comm, QuotientGroup.eq] using hn, by simpa‚ü©

variable (S) in
/-- The seminormed group structure on the quotient by a subgroup. -/
@[to_additive /-- The seminormed group structure on the quotient by an additive subgroup. -/]
noncomputable instance instSeminormedCommGroup : SeminormedCommGroup (M ‚ß∏ S) where
  toUniformSpace := IsTopologicalGroup.toUniformSpace (M ‚ß∏ S)
  __ := groupSeminorm.toSeminormedCommGroup
  uniformity_dist := by
    rw [uniformity_eq_comap_nhds_one', (nhds_one_hasBasis.comap _).eq_biInf]
    simp only [dist, preimage_setOf_eq, norm_eq_groupSeminorm, map_div_rev]

variable (S) in
/-- The quotient in the category of normed groups. -/
@[to_additive /-- The quotient in the category of normed groups. -/]
noncomputable instance instNormedCommGroup [hS : IsClosed (S : Set M)] :
    NormedCommGroup (M ‚ß∏ S) where
  __ := MetricSpace.ofT0PseudoMetricSpace _

-- This is a sanity check left here on purpose to ensure that potential refactors won't destroy
-- this important property.
example :
    (instTopologicalSpaceQuotient : TopologicalSpace <| M ‚ß∏ S) =
      (instSeminormedCommGroup S).toUniformSpace.toTopologicalSpace := rfl

example [IsClosed (S : Set M)] :
    (instSeminormedCommGroup S) = NormedCommGroup.toSeminormedCommGroup := rfl

end QuotientGroup

open QuotientAddGroup Metric Set Topology NNReal

variable {M N : Type*} [SeminormedAddCommGroup M] [SeminormedAddCommGroup N]

/-- The norm of the image under the natural morphism to the quotient. -/
theorem quotient_norm_mk_eq (S : AddSubgroup M) (m : M) :
    ‚Äñmk' S m‚Äñ = sInf ((‚Äñm + ¬∑‚Äñ) '' S) := by
  rw [mk'_apply, norm_mk, sInf_image', ‚Üê infDist_image isometry_neg, image_neg_eq_neg,
    neg_coe_set (H := S), infDist_eq_iInf]
  simp only [dist_eq_norm', sub_neg_eq_add, add_comm]

/-- The quotient norm satisfies the triangle inequality. -/
theorem quotient_norm_add_le (S : AddSubgroup M) (x y : M ‚ß∏ S) : ‚Äñx + y‚Äñ ‚â§ ‚Äñx‚Äñ + ‚Äñy‚Äñ :=
  norm_add_le x y

namespace AddSubgroup

open NormedAddGroupHom

/-- The morphism from a seminormed group to the quotient by a subgroup. -/
noncomputable def normedMk (S : AddSubgroup M) : NormedAddGroupHom M (M ‚ß∏ S) where
  __ := QuotientAddGroup.mk' S
  bound' := ‚ü®1, fun m => by simpa [one_mul] using norm_mk_le_norm‚ü©

/-- `S.normedMk` agrees with `QuotientAddGroup.mk' S`. -/
@[simp]
theorem normedMk.apply (S : AddSubgroup M) (m : M) : normedMk S m = QuotientAddGroup.mk' S m :=
  rfl

/-- `S.normedMk` is surjective. -/
theorem surjective_normedMk (S : AddSubgroup M) : Function.Surjective (normedMk S) :=
  Quot.mk_surjective

/-- The kernel of `S.normedMk` is `S`. -/
theorem ker_normedMk (S : AddSubgroup M) : S.normedMk.ker = S :=
  QuotientAddGroup.ker_mk' _

/-- The operator norm of the projection is at most `1`. -/
theorem norm_normedMk_le (S : AddSubgroup M) : ‚ÄñS.normedMk‚Äñ ‚â§ 1 :=
  NormedAddGroupHom.opNorm_le_bound _ zero_le_one fun m => by simp [norm_mk_le_norm]

theorem _root_.QuotientAddGroup.norm_lift_apply_le {S : AddSubgroup M} (f : NormedAddGroupHom M N)
    (hf : ‚àÄ x ‚àà S, f x = 0) (x : M ‚ß∏ S) : ‚Äñlift S f.toAddMonoidHom hf x‚Äñ ‚â§ ‚Äñf‚Äñ * ‚Äñx‚Äñ := by
  cases (norm_nonneg f).eq_or_lt' with
  | inl h =>
    rcases mk_surjective x with ‚ü®x, rfl‚ü©
    simpa [h] using le_opNorm f x
  | inr h =>
    rw [‚Üê not_lt, ‚Üê lt_div_iff‚ÇÄ' h, norm_lt_iff]
    rintro ‚ü®x, rfl, hx‚ü©
    exact ((lt_div_iff‚ÇÄ' h).1 hx).not_ge (le_opNorm f x)

/-- The operator norm of the projection is `1` if the subspace is not dense. -/
theorem norm_normedMk (S : AddSubgroup M) (h : (S.topologicalClosure : Set M) ‚â† univ) :
    ‚ÄñS.normedMk‚Äñ = 1 := by
  refine le_antisymm (norm_normedMk_le S) ?_
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : M, 0 < ‚Äñ(x : M ‚ß∏ S)‚Äñ := by
    refine (Set.nonempty_compl.2 h).imp fun x hx ‚Ü¶ ?_
    exact (norm_nonneg _).lt_of_ne' <| mt norm_mk_eq_zero_iff_mem_closure.1 hx
  refine (le_mul_iff_one_le_left hx).1 ?_
  exact norm_lift_apply_le S.normedMk (fun x ‚Ü¶ (eq_zero_iff x).2) x

/-- The operator norm of the projection is `0` if the subspace is dense. -/
theorem norm_trivial_quotient_mk (S : AddSubgroup M)
    (h : (S.topologicalClosure : Set M) = Set.univ) : ‚ÄñS.normedMk‚Äñ = 0 := by
  refine le_antisymm (opNorm_le_bound _ le_rfl fun x => ?_) (norm_nonneg _)
  have hker : x ‚àà S.normedMk.ker.topologicalClosure := by
    rw [S.ker_normedMk, ‚Üê SetLike.mem_coe, h]
    trivial
  rw [ker_normedMk] at hker
  simp [norm_mk_eq_zero_iff_mem_closure.mpr hker]

end AddSubgroup

namespace NormedAddGroupHom

/-- `IsQuotient f`, for `f : M ‚ü∂ N` means that `N` is isomorphic to the quotient of `M`
by the kernel of `f`. -/
structure IsQuotient (f : NormedAddGroupHom M N) : Prop where
  protected surjective : Function.Surjective f
  protected norm : ‚àÄ x, ‚Äñf x‚Äñ = sInf ((fun m => ‚Äñx + m‚Äñ) '' f.ker)

/-- Given `f : NormedAddGroupHom M N` such that `f s = 0` for all `s ‚àà S`, where,
`S : AddSubgroup M` is closed, the induced morphism `NormedAddGroupHom (M ‚ß∏ S) N`. -/
noncomputable def lift {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) : NormedAddGroupHom (M ‚ß∏ S) N :=
  { QuotientAddGroup.lift S f.toAddMonoidHom hf with
    bound' := ‚ü®‚Äñf‚Äñ, norm_lift_apply_le f hf‚ü© }

theorem lift_mk {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) (m : M) :
    lift S f hf (S.normedMk m) = f m :=
  rfl

theorem lift_unique {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) (g : NormedAddGroupHom (M ‚ß∏ S) N)
    (h : g.comp S.normedMk = f) : g = lift S f hf := by
  ext x
  rcases AddSubgroup.surjective_normedMk _ x with ‚ü®x, rfl‚ü©
  change g.comp S.normedMk x = _
  simp only [h]
  rfl

/-- `S.normedMk` satisfies `IsQuotient`. -/
theorem isQuotientQuotient (S : AddSubgroup M) : IsQuotient S.normedMk :=
  ‚ü®S.surjective_normedMk, fun m => by simpa [S.ker_normedMk] using quotient_norm_mk_eq _ m‚ü©

theorem IsQuotient.norm_lift {f : NormedAddGroupHom M N} (hquot : IsQuotient f) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ)
    (n : N) : ‚àÉ m : M, f m = n ‚àß ‚Äñm‚Äñ < ‚Äñn‚Äñ + Œµ := by
  obtain ‚ü®m, rfl‚ü© := hquot.surjective n
  have nonemp : ((fun m' => ‚Äñm + m'‚Äñ) '' f.ker).Nonempty := by
    rw [Set.image_nonempty]
    exact ‚ü®0, f.ker.zero_mem‚ü©
  rcases Real.lt_sInf_add_pos nonemp hŒµ
    with ‚ü®_, ‚ü®‚ü®x, hx, rfl‚ü©, H : ‚Äñm + x‚Äñ < sInf ((fun m' : M => ‚Äñm + m'‚Äñ) '' f.ker) + Œµ‚ü©‚ü©
  exact ‚ü®m + x, by rw [map_add, (NormedAddGroupHom.mem_ker f x).mp hx, add_zero], by
    rwa [hquot.norm]‚ü©

theorem IsQuotient.norm_le {f : NormedAddGroupHom M N} (hquot : IsQuotient f) (m : M) :
    ‚Äñf m‚Äñ ‚â§ ‚Äñm‚Äñ := by
  rw [hquot.norm]
  apply csInf_le
  ¬∑ use 0
    rintro _ ‚ü®m', -, rfl‚ü©
    apply norm_nonneg
  ¬∑ exact ‚ü®0, f.ker.zero_mem, by simp‚ü©

theorem norm_lift_le {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) :
    ‚Äñlift S f hf‚Äñ ‚â§ ‚Äñf‚Äñ :=
  opNorm_le_bound _ (norm_nonneg f) (norm_lift_apply_le f hf)

-- TODO: deprecate?
theorem lift_norm_le {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) {c : ‚Ñù‚â•0} (fb : ‚Äñf‚Äñ ‚â§ c) :
    ‚Äñlift S f hf‚Äñ ‚â§ c :=
  (norm_lift_le S f hf).trans fb

theorem lift_normNoninc {N : Type*} [SeminormedAddCommGroup N] (S : AddSubgroup M)
    (f : NormedAddGroupHom M N) (hf : ‚àÄ s ‚àà S, f s = 0) (fb : f.NormNoninc) :
    (lift S f hf).NormNoninc := fun x => by
  have fb' : ‚Äñf‚Äñ ‚â§ (1 : ‚Ñù‚â•0) := NormNoninc.normNoninc_iff_norm_le_one.mp fb
  simpa using le_of_opNorm_le _ (f.lift_norm_le _ _ fb') _

end NormedAddGroupHom

/-!
### Submodules and ideals

In what follows, the norm structures created above for quotients of (semi)`NormedAddCommGroup`s
by `AddSubgroup`s are transferred via definitional equality to quotients of modules by submodules,
and of rings by ideals, thereby preserving the definitional equality for the topological group and
uniform structures worked for above. Completeness is also transferred via this definitional
equality.

In addition, instances are constructed for `NormedSpace`, `SeminormedCommRing`,
`NormedCommRing` and `NormedAlgebra` under the appropriate hypotheses. Currently, we do not
have quotients of rings by two-sided ideals, hence the commutativity hypotheses are required.
-/

section Submodule

variable {R : Type*} [Ring R] [Module R M] (S : Submodule R M)

instance Submodule.Quotient.seminormedAddCommGroup : SeminormedAddCommGroup (M ‚ß∏ S) :=
  QuotientAddGroup.instSeminormedAddCommGroup S.toAddSubgroup

instance Submodule.Quotient.normedAddCommGroup [hS : IsClosed (S : Set M)] :
    NormedAddCommGroup (M ‚ß∏ S) :=
  QuotientAddGroup.instNormedAddCommGroup S.toAddSubgroup (hS := hS)

instance Submodule.Quotient.completeSpace [CompleteSpace M] : CompleteSpace (M ‚ß∏ S) :=
  QuotientAddGroup.completeSpace M S.toAddSubgroup

/-- For any `x : M ‚ß∏ S` and any `0 < Œµ`, there is `m : M` such that `Submodule.Quotient.mk m = x`
and `‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ`. -/
nonrec theorem Submodule.Quotient.norm_mk_lt {S : Submodule R M} (x : M ‚ß∏ S) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ m : M, Submodule.Quotient.mk m = x ‚àß ‚Äñm‚Äñ < ‚Äñx‚Äñ + Œµ :=
  exists_norm_mk_lt x hŒµ

theorem Submodule.Quotient.norm_mk_le (m : M) : ‚Äñ(Submodule.Quotient.mk m : M ‚ß∏ S)‚Äñ ‚â§ ‚Äñm‚Äñ :=
  norm_mk_le_norm

instance Submodule.Quotient.instIsBoundedSMul (ùïú : Type*)
    [SeminormedCommRing ùïú] [Module ùïú M] [IsBoundedSMul ùïú M] [SMul ùïú R] [IsScalarTower ùïú R M] :
    IsBoundedSMul ùïú (M ‚ß∏ S) :=
  .of_norm_smul_le fun k x =>
    -- this is `QuotientAddGroup.norm_lift_apply_le` for `f : M ‚Üí M ‚ß∏ S` given by
    -- `x ‚Ü¶ mk (k ‚Ä¢ x)`; todo: add scalar multiplication as `NormedAddGroupHom`, use it here
    _root_.le_of_forall_pos_le_add fun Œµ hŒµ => by
      have := (nhds_basis_ball.tendsto_iff nhds_basis_ball).mp
        ((@Real.uniformContinuous_const_mul ‚Äñk‚Äñ).continuous.tendsto ‚Äñx‚Äñ) Œµ hŒµ
      simp only [mem_ball, dist, abs_sub_lt_iff] at this
      rcases this with ‚ü®Œ¥, hŒ¥, h‚ü©
      obtain ‚ü®a, rfl, ha‚ü© := Submodule.Quotient.norm_mk_lt x hŒ¥
      specialize h ‚Äña‚Äñ ‚ü®by linarith, by linarith [Submodule.Quotient.norm_mk_le S a]‚ü©
      calc
        _ ‚â§ ‚Äñk‚Äñ * ‚Äña‚Äñ := (norm_mk_le ..).trans (norm_smul_le k a)
        _ ‚â§ _ := (sub_lt_iff_lt_add'.mp h.1).le

instance Submodule.Quotient.normedSpace (ùïú : Type*) [NormedField ùïú] [NormedSpace ùïú M] [SMul ùïú R]
    [IsScalarTower ùïú R M] : NormedSpace ùïú (M ‚ß∏ S) where
  norm_smul_le := norm_smul_le

end Submodule

section Ideal

variable {R : Type*} [SeminormedCommRing R] (I : Ideal R)

nonrec theorem Ideal.Quotient.norm_mk_lt {I : Ideal R} (x : R ‚ß∏ I) {Œµ : ‚Ñù} (hŒµ : 0 < Œµ) :
    ‚àÉ r : R, Ideal.Quotient.mk I r = x ‚àß ‚Äñr‚Äñ < ‚Äñx‚Äñ + Œµ :=
  exists_norm_mk_lt x hŒµ

theorem Ideal.Quotient.norm_mk_le (r : R) : ‚ÄñIdeal.Quotient.mk I r‚Äñ ‚â§ ‚Äñr‚Äñ := norm_mk_le_norm

instance Ideal.Quotient.semiNormedCommRing : SeminormedCommRing (R ‚ß∏ I) where
  dist_eq := dist_eq_norm
  mul_comm := _root_.mul_comm
  norm_mul_le x y := le_of_forall_pos_le_add fun Œµ hŒµ => by
    have := ((nhds_basis_ball.prod_nhds nhds_basis_ball).tendsto_iff nhds_basis_ball).mp
      (continuous_mul.tendsto (‚Äñx‚Äñ, ‚Äñy‚Äñ)) Œµ hŒµ
    simp only [Set.mem_prod, mem_ball, and_imp, Prod.forall, Prod.exists] at this
    rcases this with ‚ü®Œµ‚ÇÅ, Œµ‚ÇÇ, ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©, h‚ü©
    obtain ‚ü®‚ü®a, rfl, ha‚ü©, ‚ü®b, rfl, hb‚ü©‚ü© := Ideal.Quotient.norm_mk_lt x h‚ÇÅ,
      Ideal.Quotient.norm_mk_lt y h‚ÇÇ
    simp only [dist, abs_sub_lt_iff] at h
    specialize h ‚Äña‚Äñ ‚Äñb‚Äñ ‚ü®by linarith, by linarith [Ideal.Quotient.norm_mk_le I a]‚ü©
      ‚ü®by linarith, by linarith [Ideal.Quotient.norm_mk_le I b]‚ü©
    calc
      _ ‚â§ ‚Äña‚Äñ * ‚Äñb‚Äñ := (Ideal.Quotient.norm_mk_le I (a * b)).trans (norm_mul_le a b)
      _ ‚â§ _ := (sub_lt_iff_lt_add'.mp h.1).le

instance Ideal.Quotient.normedCommRing [IsClosed (I : Set R)] : NormedCommRing (R ‚ß∏ I) :=
  { Ideal.Quotient.semiNormedCommRing I, Submodule.Quotient.normedAddCommGroup I with }

variable (ùïú : Type*) [NormedField ùïú]

instance Ideal.Quotient.normedAlgebra [NormedAlgebra ùïú R] : NormedAlgebra ùïú (R ‚ß∏ I) :=
  { Submodule.Quotient.normedSpace I ùïú, Ideal.Quotient.algebra ùïú with }

end Ideal
