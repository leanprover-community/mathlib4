/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, Fr√©d√©ric Dupuis, Heather Macbeth
-/
import Mathlib.Algebra.Star.Basic
import Mathlib.Analysis.Normed.Group.Constructions
import Mathlib.Analysis.Normed.Group.Submodule
import Mathlib.Analysis.Normed.Group.Uniform
import Mathlib.LinearAlgebra.Basis.Defs
import Mathlib.LinearAlgebra.DFinsupp
import Mathlib.Topology.Algebra.Module.Equiv

/-!
# (Semi-)linear isometries

In this file we define `LinearIsometry œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ` (notation: `E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ`) to be a semilinear
isometric embedding of `E` into `E‚ÇÇ` and `LinearIsometryEquiv` (notation: `E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ`) to be
a semilinear isometric equivalence between `E` and `E‚ÇÇ`.  The notation for the associated purely
linear concepts is `E ‚Üí‚Çó·µ¢[R] E‚ÇÇ`, `E ‚âÉ‚Çó·µ¢[R] E‚ÇÇ`, and `E ‚Üí‚Çó·µ¢‚ãÜ[R] E‚ÇÇ`, `E ‚âÉ‚Çó·µ¢‚ãÜ[R] E‚ÇÇ` for
the star-linear versions.

We also prove some trivial lemmas and provide convenience constructors.

Since a lot of elementary properties don't require `‚Äñx‚Äñ = 0 ‚Üí x = 0` we start setting up the
theory for `SeminormedAddCommGroup` and we specialize to `NormedAddCommGroup` when needed.
-/

open Function Set Topology

variable {R R‚ÇÇ R‚ÇÉ R‚ÇÑ E E‚ÇÇ E‚ÇÉ E‚ÇÑ F ùìï : Type*} [Semiring R] [Semiring R‚ÇÇ] [Semiring R‚ÇÉ] [Semiring R‚ÇÑ]
  {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R} {œÉ‚ÇÅ‚ÇÉ : R ‚Üí+* R‚ÇÉ} {œÉ‚ÇÉ‚ÇÅ : R‚ÇÉ ‚Üí+* R} {œÉ‚ÇÅ‚ÇÑ : R ‚Üí+* R‚ÇÑ}
  {œÉ‚ÇÑ‚ÇÅ : R‚ÇÑ ‚Üí+* R} {œÉ‚ÇÇ‚ÇÉ : R‚ÇÇ ‚Üí+* R‚ÇÉ} {œÉ‚ÇÉ‚ÇÇ : R‚ÇÉ ‚Üí+* R‚ÇÇ} {œÉ‚ÇÇ‚ÇÑ : R‚ÇÇ ‚Üí+* R‚ÇÑ} {œÉ‚ÇÑ‚ÇÇ : R‚ÇÑ ‚Üí+* R‚ÇÇ}
  {œÉ‚ÇÉ‚ÇÑ : R‚ÇÉ ‚Üí+* R‚ÇÑ} {œÉ‚ÇÑ‚ÇÉ : R‚ÇÑ ‚Üí+* R‚ÇÉ} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
  [RingHomInvPair œÉ‚ÇÅ‚ÇÉ œÉ‚ÇÉ‚ÇÅ] [RingHomInvPair œÉ‚ÇÉ‚ÇÅ œÉ‚ÇÅ‚ÇÉ] [RingHomInvPair œÉ‚ÇÇ‚ÇÉ œÉ‚ÇÉ‚ÇÇ]
  [RingHomInvPair œÉ‚ÇÉ‚ÇÇ œÉ‚ÇÇ‚ÇÉ] [RingHomInvPair œÉ‚ÇÅ‚ÇÑ œÉ‚ÇÑ‚ÇÅ] [RingHomInvPair œÉ‚ÇÑ‚ÇÅ œÉ‚ÇÅ‚ÇÑ]
  [RingHomInvPair œÉ‚ÇÇ‚ÇÑ œÉ‚ÇÑ‚ÇÇ] [RingHomInvPair œÉ‚ÇÑ‚ÇÇ œÉ‚ÇÇ‚ÇÑ] [RingHomInvPair œÉ‚ÇÉ‚ÇÑ œÉ‚ÇÑ‚ÇÉ]
  [RingHomInvPair œÉ‚ÇÑ‚ÇÉ œÉ‚ÇÉ‚ÇÑ] [RingHomCompTriple œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÉ œÉ‚ÇÅ‚ÇÉ] [RingHomCompTriple œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÑ œÉ‚ÇÅ‚ÇÑ]
  [RingHomCompTriple œÉ‚ÇÇ‚ÇÉ œÉ‚ÇÉ‚ÇÑ œÉ‚ÇÇ‚ÇÑ] [RingHomCompTriple œÉ‚ÇÅ‚ÇÉ œÉ‚ÇÉ‚ÇÑ œÉ‚ÇÅ‚ÇÑ] [RingHomCompTriple œÉ‚ÇÉ‚ÇÇ œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÉ‚ÇÅ]
  [RingHomCompTriple œÉ‚ÇÑ‚ÇÇ œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÑ‚ÇÅ] [RingHomCompTriple œÉ‚ÇÑ‚ÇÉ œÉ‚ÇÉ‚ÇÇ œÉ‚ÇÑ‚ÇÇ] [RingHomCompTriple œÉ‚ÇÑ‚ÇÉ œÉ‚ÇÉ‚ÇÅ œÉ‚ÇÑ‚ÇÅ]
  [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ] [SeminormedAddCommGroup E‚ÇÉ]
  [SeminormedAddCommGroup E‚ÇÑ] [Module R E] [Module R‚ÇÇ E‚ÇÇ] [Module R‚ÇÉ E‚ÇÉ] [Module R‚ÇÑ E‚ÇÑ]
  [NormedAddCommGroup F] [Module R F]

/-- A `œÉ‚ÇÅ‚ÇÇ`-semilinear isometric embedding of a normed `R`-module into an `R‚ÇÇ`-module,
denoted as `f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ`. -/
structure LinearIsometry (œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ) (E E‚ÇÇ : Type*) [SeminormedAddCommGroup E]
  [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R‚ÇÇ E‚ÇÇ] extends E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ where
  norm_map' : ‚àÄ x, ‚ÄñtoLinearMap x‚Äñ = ‚Äñx‚Äñ

@[inherit_doc]
notation:25 E " ‚Üí‚Çõ‚Çó·µ¢[" œÉ‚ÇÅ‚ÇÇ:25 "] " E‚ÇÇ:0 => LinearIsometry œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ

/-- A linear isometric embedding of a normed `R`-module into another one. -/
notation:25 E " ‚Üí‚Çó·µ¢[" R:25 "] " E‚ÇÇ:0 => LinearIsometry (RingHom.id R) E E‚ÇÇ

/-- An antilinear isometric embedding of a normed `R`-module into another one. -/
notation:25 E " ‚Üí‚Çó·µ¢‚ãÜ[" R:25 "] " E‚ÇÇ:0 => LinearIsometry (starRingEnd R) E E‚ÇÇ

/-- `SemilinearIsometryClass F œÉ E E‚ÇÇ` asserts `F` is a type of bundled `œÉ`-semilinear isometries
`E ‚Üí E‚ÇÇ`.

See also `LinearIsometryClass F R E E‚ÇÇ` for the case where `œÉ` is the identity map on `R`.

A map `f` between an `R`-module and an `S`-module over a ring homomorphism `œÉ : R ‚Üí+* S`
is semilinear if it satisfies the two properties `f (x + y) = f x + f y` and
`f (c ‚Ä¢ x) = (œÉ c) ‚Ä¢ f x`. -/
class SemilinearIsometryClass (ùìï : Type*) {R R‚ÇÇ : outParam Type*} [Semiring R] [Semiring R‚ÇÇ]
    (œÉ‚ÇÅ‚ÇÇ : outParam <| R ‚Üí+* R‚ÇÇ) (E E‚ÇÇ : outParam Type*) [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R‚ÇÇ E‚ÇÇ] [FunLike ùìï E E‚ÇÇ] : Prop
    extends SemilinearMapClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ where
  norm_map : ‚àÄ (f : ùìï) (x : E), ‚Äñf x‚Äñ = ‚Äñx‚Äñ

/-- `LinearIsometryClass F R E E‚ÇÇ` asserts `F` is a type of bundled `R`-linear isometries
`M ‚Üí M‚ÇÇ`.

This is an abbreviation for `SemilinearIsometryClass F (RingHom.id R) E E‚ÇÇ`.
-/
abbrev LinearIsometryClass (ùìï : Type*) (R E E‚ÇÇ : outParam Type*) [Semiring R]
    [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R E‚ÇÇ]
    [FunLike ùìï E E‚ÇÇ] :=
  SemilinearIsometryClass ùìï (RingHom.id R) E E‚ÇÇ

namespace SemilinearIsometryClass

variable [FunLike ùìï E E‚ÇÇ]

protected theorem isometry [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) : Isometry f :=
  AddMonoidHomClass.isometry_of_norm _ (norm_map _)

@[continuity]
protected theorem continuous [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) : Continuous f :=
  (SemilinearIsometryClass.isometry f).continuous

-- Should be `@[simp]` but it doesn't fire due to https://github.com/leanprover/lean4/issues/3107.
theorem nnnorm_map [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) (x : E) : ‚Äñf x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  NNReal.eq <| norm_map f x

protected theorem lipschitz [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) : LipschitzWith 1 f :=
  (SemilinearIsometryClass.isometry f).lipschitz

protected theorem antilipschitz [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) :
    AntilipschitzWith 1 f :=
  (SemilinearIsometryClass.isometry f).antilipschitz

theorem ediam_image [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) (s : Set E) :
    EMetric.diam (f '' s) = EMetric.diam s :=
  (SemilinearIsometryClass.isometry f).ediam_image s

theorem ediam_range [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) :
    EMetric.diam (range f) = EMetric.diam (univ : Set E) :=
  (SemilinearIsometryClass.isometry f).ediam_range

theorem diam_image [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) (s : Set E) :
    Metric.diam (f '' s) = Metric.diam s :=
  (SemilinearIsometryClass.isometry f).diam_image s

theorem diam_range [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) :
    Metric.diam (range f) = Metric.diam (univ : Set E) :=
  (SemilinearIsometryClass.isometry f).diam_range

instance (priority := 100) toContinuousSemilinearMapClass
    [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] : ContinuousSemilinearMapClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ where
  map_continuous := SemilinearIsometryClass.continuous

end SemilinearIsometryClass

namespace LinearIsometry

variable (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (f‚ÇÅ : F ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)

theorem toLinearMap_injective : Injective (toLinearMap : (E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) ‚Üí E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, rfl => rfl

@[simp]
theorem toLinearMap_inj {f g : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} : f.toLinearMap = g.toLinearMap ‚Üî f = g :=
  toLinearMap_injective.eq_iff

instance instFunLike : FunLike (E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) E E‚ÇÇ where
  coe f := f.toFun
  coe_injective' _ _ h := toLinearMap_injective (DFunLike.coe_injective h)

instance instSemilinearIsometryClass : SemilinearIsometryClass (E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ where
  map_add f := map_add f.toLinearMap
  map_smul‚Çõ‚Çó f := map_smul‚Çõ‚Çó f.toLinearMap
  norm_map f := f.norm_map'

@[simp]
theorem coe_toLinearMap : ‚áëf.toLinearMap = f :=
  rfl

@[simp]
theorem coe_mk (f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (hf) : ‚áë(mk f hf) = f :=
  rfl

theorem coe_injective : @Injective (E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (E ‚Üí E‚ÇÇ) (fun f => f) := by
  rintro ‚ü®_‚ü© ‚ü®_‚ü©
  simp

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ) (E E‚ÇÇ : Type*) [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R‚ÇÇ E‚ÇÇ] (h : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : E ‚Üí E‚ÇÇ :=
  h

initialize_simps_projections LinearIsometry (toFun ‚Üí apply)

@[ext]
theorem ext {f g : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} (h : ‚àÄ x, f x = g x) : f = g :=
  coe_injective <| funext h

variable [FunLike ùìï E E‚ÇÇ]

protected theorem map_zero : f 0 = 0 :=
  f.toLinearMap.map_zero

protected theorem map_add (x y : E) : f (x + y) = f x + f y :=
  f.toLinearMap.map_add x y

protected theorem map_neg (x : E) : f (-x) = -f x :=
  f.toLinearMap.map_neg x

protected theorem map_sub (x y : E) : f (x - y) = f x - f y :=
  f.toLinearMap.map_sub x y

protected theorem map_smul‚Çõ‚Çó (c : R) (x : E) : f (c ‚Ä¢ x) = œÉ‚ÇÅ‚ÇÇ c ‚Ä¢ f x :=
  f.toLinearMap.map_smul‚Çõ‚Çó c x

protected theorem map_smul [Module R E‚ÇÇ] (f : E ‚Üí‚Çó·µ¢[R] E‚ÇÇ) (c : R) (x : E) : f (c ‚Ä¢ x) = c ‚Ä¢ f x :=
  f.toLinearMap.map_smul c x

@[simp]
theorem norm_map (x : E) : ‚Äñf x‚Äñ = ‚Äñx‚Äñ :=
  SemilinearIsometryClass.norm_map f x

@[simp] -- Should be replaced with `SemilinearIsometryClass.nnorm_map` when https://github.com/leanprover/lean4/issues/3107 is fixed.
theorem nnnorm_map (x : E) : ‚Äñf x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  NNReal.eq <| norm_map f x

@[simp] -- Should be replaced with `SemilinearIsometryClass.enorm_map` when https://github.com/leanprover/lean4/issues/3107 is fixed.
theorem enorm_map (x : E) : ‚Äñf x‚Äñ‚Çë = ‚Äñx‚Äñ‚Çë := by
  simp [enorm]

protected theorem isometry : Isometry f :=
  AddMonoidHomClass.isometry_of_norm f.toLinearMap (norm_map _)

lemma isEmbedding (f : F ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : IsEmbedding f := f.isometry.isEmbedding

-- Should be `@[simp]` but it doesn't fire due to https://github.com/leanprover/lean4/issues/3107.
theorem isComplete_image_iff [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) {s : Set E} :
    IsComplete (f '' s) ‚Üî IsComplete s :=
  _root_.isComplete_image_iff (SemilinearIsometryClass.isometry f).isUniformInducing

@[simp] -- Should be replaced with `LinearIsometry.isComplete_image_iff` when https://github.com/leanprover/lean4/issues/3107 is fixed.
theorem isComplete_image_iff' (f : LinearIsometry œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ) {s : Set E} :
    IsComplete (f '' s) ‚Üî IsComplete s :=
  LinearIsometry.isComplete_image_iff _

theorem isComplete_map_iff [RingHomSurjective œÉ‚ÇÅ‚ÇÇ] {p : Submodule R E} :
    IsComplete (p.map f.toLinearMap : Set E‚ÇÇ) ‚Üî IsComplete (p : Set E) :=
  isComplete_image_iff f

theorem isComplete_map_iff' [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) [RingHomSurjective œÉ‚ÇÅ‚ÇÇ]
    {p : Submodule R E} : IsComplete (p.map f : Set E‚ÇÇ) ‚Üî IsComplete (p : Set E) :=
  isComplete_image_iff f

instance completeSpace_map [SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] (f : ùìï) [RingHomSurjective œÉ‚ÇÅ‚ÇÇ]
    (p : Submodule R E) [CompleteSpace p] : CompleteSpace (p.map f) :=
  ((isComplete_map_iff' f).2 <| completeSpace_coe_iff_isComplete.1 ‚Äπ_‚Ä∫).completeSpace_coe

instance completeSpace_map' [RingHomSurjective œÉ‚ÇÅ‚ÇÇ] (p : Submodule R E) [CompleteSpace p] :
    CompleteSpace (p.map f.toLinearMap) :=
  (f.isComplete_map_iff.2 <| completeSpace_coe_iff_isComplete.1 ‚Äπ_‚Ä∫).completeSpace_coe

@[simp]
theorem dist_map (x y : E) : dist (f x) (f y) = dist x y :=
  f.isometry.dist_eq x y

@[simp]
theorem edist_map (x y : E) : edist (f x) (f y) = edist x y :=
  f.isometry.edist_eq x y

protected theorem injective : Injective f‚ÇÅ :=
  Isometry.injective (LinearIsometry.isometry f‚ÇÅ)

@[simp]
theorem map_eq_iff {x y : F} : f‚ÇÅ x = f‚ÇÅ y ‚Üî x = y :=
  f‚ÇÅ.injective.eq_iff

theorem map_ne {x y : F} (h : x ‚â† y) : f‚ÇÅ x ‚â† f‚ÇÅ y :=
  f‚ÇÅ.injective.ne h

protected theorem lipschitz : LipschitzWith 1 f :=
  f.isometry.lipschitz

protected theorem antilipschitz : AntilipschitzWith 1 f :=
  f.isometry.antilipschitz

@[continuity]
protected theorem continuous : Continuous f :=
  f.isometry.continuous

@[simp]
theorem preimage_ball (x : E) (r : ‚Ñù) : f ‚Åª¬π' Metric.ball (f x) r = Metric.ball x r :=
  f.isometry.preimage_ball x r

@[simp]
theorem preimage_sphere (x : E) (r : ‚Ñù) : f ‚Åª¬π' Metric.sphere (f x) r = Metric.sphere x r :=
  f.isometry.preimage_sphere x r

@[simp]
theorem preimage_closedBall (x : E) (r : ‚Ñù) :
    f ‚Åª¬π' Metric.closedBall (f x) r = Metric.closedBall x r :=
  f.isometry.preimage_closedBall x r

theorem ediam_image (s : Set E) : EMetric.diam (f '' s) = EMetric.diam s :=
  f.isometry.ediam_image s

theorem ediam_range : EMetric.diam (range f) = EMetric.diam (univ : Set E) :=
  f.isometry.ediam_range

theorem diam_image (s : Set E) : Metric.diam (f '' s) = Metric.diam s :=
  Isometry.diam_image (LinearIsometry.isometry f) s

theorem diam_range : Metric.diam (range f) = Metric.diam (univ : Set E) :=
  Isometry.diam_range (LinearIsometry.isometry f)

/-- Interpret a linear isometry as a continuous linear map. -/
def toContinuousLinearMap : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  ‚ü®f.toLinearMap, f.continuous‚ü©

theorem toContinuousLinearMap_injective :
    Function.Injective (toContinuousLinearMap : _ ‚Üí E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) := fun x _ h =>
  coe_injective (congr_arg _ h : ‚áëx.toContinuousLinearMap = _)

@[simp]
theorem toContinuousLinearMap_inj {f g : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} :
    f.toContinuousLinearMap = g.toContinuousLinearMap ‚Üî f = g :=
  toContinuousLinearMap_injective.eq_iff

@[simp]
theorem coe_toContinuousLinearMap : ‚áëf.toContinuousLinearMap = f :=
  rfl

@[simp]
theorem comp_continuous_iff {Œ± : Type*} [TopologicalSpace Œ±] {g : Œ± ‚Üí E} :
    Continuous (f ‚àò g) ‚Üî Continuous g :=
  f.isometry.comp_continuous_iff

/-- The identity linear isometry. -/
def id : E ‚Üí‚Çó·µ¢[R] E :=
  ‚ü®LinearMap.id, fun _ => rfl‚ü©

@[simp, norm_cast]
theorem coe_id : ((id : E ‚Üí‚Çó·µ¢[R] E) : E ‚Üí E) = _root_.id :=
  rfl

@[simp]
theorem id_apply (x : E) : (id : E ‚Üí‚Çó·µ¢[R] E) x = x :=
  rfl

@[simp]
theorem id_toLinearMap : (id.toLinearMap : E ‚Üí‚Çó[R] E) = LinearMap.id :=
  rfl

@[simp]
theorem id_toContinuousLinearMap : id.toContinuousLinearMap = ContinuousLinearMap.id R E :=
  rfl

instance instInhabited : Inhabited (E ‚Üí‚Çó·µ¢[R] E) := ‚ü®id‚ü©

/-- Composition of linear isometries. -/
def comp (g : E‚ÇÇ ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÉ] E‚ÇÉ :=
  ‚ü®g.toLinearMap.comp f.toLinearMap, fun _ => (norm_map g _).trans (norm_map f _)‚ü©

@[simp]
theorem coe_comp (g : E‚ÇÇ ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : ‚áë(g.comp f) = g ‚àò f :=
  rfl

@[simp]
theorem id_comp : (id : E‚ÇÇ ‚Üí‚Çó·µ¢[R‚ÇÇ] E‚ÇÇ).comp f = f :=
  ext fun _ => rfl

@[simp]
theorem comp_id : f.comp id = f :=
  ext fun _ => rfl

theorem comp_assoc (f : E‚ÇÉ ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÉ‚ÇÑ] E‚ÇÑ) (g : E‚ÇÇ ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) (h : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) :
    (f.comp g).comp h = f.comp (g.comp h) :=
  rfl

instance instMonoid : Monoid (E ‚Üí‚Çó·µ¢[R] E) where
  one := id
  mul := comp
  mul_assoc := comp_assoc
  one_mul := id_comp
  mul_one := comp_id

@[simp]
theorem coe_one : ((1 : E ‚Üí‚Çó·µ¢[R] E) : E ‚Üí E) = _root_.id :=
  rfl

@[simp]
theorem coe_mul (f g : E ‚Üí‚Çó·µ¢[R] E) : ‚áë(f * g) = f ‚àò g :=
  rfl

theorem one_def : (1 : E ‚Üí‚Çó·µ¢[R] E) = id :=
  rfl

theorem mul_def (f g : E ‚Üí‚Çó·µ¢[R] E) : (f * g : E ‚Üí‚Çó·µ¢[R] E) = f.comp g :=
  rfl

theorem coe_pow (f : E ‚Üí‚Çó·µ¢[R] E) (n : ‚Ñï) : ‚áë(f ^ n) = f^[n] :=
  hom_coe_pow _ rfl (fun _ _ ‚Ü¶ rfl) _ _

end LinearIsometry

/-- Construct a `LinearIsometry` from a `LinearMap` satisfying `Isometry`. -/
def LinearMap.toLinearIsometry (f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (hf : Isometry f) : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  { f with
    norm_map' := by
      simp_rw [‚Üê dist_zero_right]
      simpa using (hf.dist_eq ¬∑ 0) }

namespace Submodule

variable {R' : Type*} [Ring R'] [Module R' E] (p : Submodule R' E)

/-- `Submodule.subtype` as a `LinearIsometry`. -/
def subtype‚Çó·µ¢ : p ‚Üí‚Çó·µ¢[R'] E :=
  ‚ü®p.subtype, fun _ => rfl‚ü©

@[simp]
theorem coe_subtype‚Çó·µ¢ : ‚áëp.subtype‚Çó·µ¢ = p.subtype :=
  rfl

@[simp]
theorem subtype‚Çó·µ¢_toLinearMap : p.subtype‚Çó·µ¢.toLinearMap = p.subtype :=
  rfl

@[simp]
theorem subtype‚Çó·µ¢_toContinuousLinearMap : p.subtype‚Çó·µ¢.toContinuousLinearMap = p.subtypeL :=
  rfl

end Submodule

/-- A semilinear isometric equivalence between two normed vector spaces,
denoted as `f : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ`. -/
structure LinearIsometryEquiv (œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ) {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ]
  [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ] (E E‚ÇÇ : Type*) [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ]
  [Module R E] [Module R‚ÇÇ E‚ÇÇ] extends E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ where
  norm_map' : ‚àÄ x, ‚ÄñtoLinearEquiv x‚Äñ = ‚Äñx‚Äñ

@[inherit_doc]
notation:25 E " ‚âÉ‚Çõ‚Çó·µ¢[" œÉ‚ÇÅ‚ÇÇ:25 "] " E‚ÇÇ:0 => LinearIsometryEquiv œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ

/-- A linear isometric equivalence between two normed vector spaces. -/
notation:25 E " ‚âÉ‚Çó·µ¢[" R:25 "] " E‚ÇÇ:0 => LinearIsometryEquiv (RingHom.id R) E E‚ÇÇ

/-- An antilinear isometric equivalence between two normed vector spaces. -/
notation:25 E " ‚âÉ‚Çó·µ¢‚ãÜ[" R:25 "] " E‚ÇÇ:0 => LinearIsometryEquiv (starRingEnd R) E E‚ÇÇ

/-- `SemilinearIsometryEquivClass F œÉ E E‚ÇÇ` asserts `F` is a type of bundled `œÉ`-semilinear
isometric equivs `E ‚Üí E‚ÇÇ`.

See also `LinearIsometryEquivClass F R E E‚ÇÇ` for the case where `œÉ` is the identity map on `R`.

A map `f` between an `R`-module and an `S`-module over a ring homomorphism `œÉ : R ‚Üí+* S`
is semilinear if it satisfies the two properties `f (x + y) = f x + f y` and
`f (c ‚Ä¢ x) = (œÉ c) ‚Ä¢ f x`. -/
class SemilinearIsometryEquivClass (ùìï : Type*) {R R‚ÇÇ : outParam Type*} [Semiring R]
  [Semiring R‚ÇÇ] (œÉ‚ÇÅ‚ÇÇ : outParam <| R ‚Üí+* R‚ÇÇ) {œÉ‚ÇÇ‚ÇÅ : outParam <| R‚ÇÇ ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ]
  [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ] (E E‚ÇÇ : outParam Type*) [SeminormedAddCommGroup E]
  [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R‚ÇÇ E‚ÇÇ] [EquivLike ùìï E E‚ÇÇ] : Prop
  extends SemilinearEquivClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ where
  norm_map : ‚àÄ (f : ùìï) (x : E), ‚Äñf x‚Äñ = ‚Äñx‚Äñ

/-- `LinearIsometryEquivClass F R E E‚ÇÇ` asserts `F` is a type of bundled `R`-linear isometries
`M ‚Üí M‚ÇÇ`.

This is an abbreviation for `SemilinearIsometryEquivClass F (RingHom.id R) E E‚ÇÇ`.
-/
abbrev LinearIsometryEquivClass (ùìï : Type*) (R E E‚ÇÇ : outParam Type*) [Semiring R]
    [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ] [Module R E] [Module R E‚ÇÇ]
    [EquivLike ùìï E E‚ÇÇ] :=
  SemilinearIsometryEquivClass ùìï (RingHom.id R) E E‚ÇÇ

namespace SemilinearIsometryEquivClass

variable (ùìï)

-- `œÉ‚ÇÇ‚ÇÅ` becomes a metavariable, but it's OK since it's an outparam
instance (priority := 100) toSemilinearIsometryClass [EquivLike ùìï E E‚ÇÇ]
    [s : SemilinearIsometryEquivClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ] : SemilinearIsometryClass ùìï œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ :=
  { s with }

end SemilinearIsometryEquivClass

namespace LinearIsometryEquiv

variable (e : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)

theorem toLinearEquiv_injective : Injective (toLinearEquiv : (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) ‚Üí E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)
  | ‚ü®_, _‚ü©, ‚ü®_, _‚ü©, rfl => rfl

@[simp]
theorem toLinearEquiv_inj {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} : f.toLinearEquiv = g.toLinearEquiv ‚Üî f = g :=
  toLinearEquiv_injective.eq_iff

instance instEquivLike : EquivLike (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) E E‚ÇÇ where
  coe e := e.toFun
  inv e := e.invFun
  coe_injective' f g h‚ÇÅ h‚ÇÇ := by
    obtain ‚ü®f', _‚ü© := f
    obtain ‚ü®g', _‚ü© := g
    cases f'
    cases g'
    simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, DFunLike.coe_fn_eq] at h‚ÇÅ
    congr
  left_inv e := e.left_inv
  right_inv e := e.right_inv

instance instSemilinearIsometryEquivClass :
    SemilinearIsometryEquivClass (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) œÉ‚ÇÅ‚ÇÇ E E‚ÇÇ where
  map_add f := map_add f.toLinearEquiv
  map_smul‚Çõ‚Çó e := map_smul‚Çõ‚Çó e.toLinearEquiv
  norm_map e := e.norm_map'

/-- Shortcut instance, saving 8.5% of compilation time in
`Mathlib/Analysis/InnerProductSpace/Adjoint.lean`.

(This instance was pinpointed by benchmarks; we didn't do an in depth investigation why it is
specifically needed.)
-/
instance instCoeFun : CoeFun (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) fun _ ‚Ü¶ E ‚Üí E‚ÇÇ := ‚ü®DFunLike.coe‚ü©

theorem coe_injective : @Function.Injective (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (E ‚Üí E‚ÇÇ) (‚Üë) :=
  DFunLike.coe_injective

@[simp]
theorem coe_mk (e : E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (he : ‚àÄ x, ‚Äñe x‚Äñ = ‚Äñx‚Äñ) : ‚áë(mk e he) = e :=
  rfl

@[simp]
theorem coe_toLinearEquiv (e : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : ‚áëe.toLinearEquiv = e :=
  rfl

@[ext]
theorem ext {e e' : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} (h : ‚àÄ x, e x = e' x) : e = e' :=
  toLinearEquiv_injective <| LinearEquiv.ext h

protected theorem congr_arg {f : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} : ‚àÄ {x x' : E}, x = x' ‚Üí f x = f x'
  | _, _, rfl => rfl

protected theorem congr_fun {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} (h : f = g) (x : E) : f x = g x :=
  h ‚ñ∏ rfl

/-- Construct a `LinearIsometryEquiv` from a `LinearEquiv` and two inequalities:
`‚àÄ x, ‚Äñe x‚Äñ ‚â§ ‚Äñx‚Äñ` and `‚àÄ y, ‚Äñe.symm y‚Äñ ‚â§ ‚Äñy‚Äñ`. -/
def ofBounds (e : E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (h‚ÇÅ : ‚àÄ x, ‚Äñe x‚Äñ ‚â§ ‚Äñx‚Äñ) (h‚ÇÇ : ‚àÄ y, ‚Äñe.symm y‚Äñ ‚â§ ‚Äñy‚Äñ) :
    E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  ‚ü®e, fun x => le_antisymm (h‚ÇÅ x) <| by simpa only [e.symm_apply_apply] using h‚ÇÇ (e x)‚ü©

@[simp]
theorem norm_map (x : E) : ‚Äñe x‚Äñ = ‚Äñx‚Äñ :=
  e.norm_map' x

/-- Reinterpret a `LinearIsometryEquiv` as a `LinearIsometry`. -/
def toLinearIsometry : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  ‚ü®e.1, e.2‚ü©

theorem toLinearIsometry_injective : Function.Injective (toLinearIsometry : _ ‚Üí E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) :=
  fun x _ h => coe_injective (congr_arg _ h : ‚áëx.toLinearIsometry = _)

@[simp]
theorem toLinearIsometry_inj {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} :
    f.toLinearIsometry = g.toLinearIsometry ‚Üî f = g :=
  toLinearIsometry_injective.eq_iff

@[simp]
theorem coe_toLinearIsometry : ‚áëe.toLinearIsometry = e :=
  rfl

protected theorem isometry : Isometry e :=
  e.toLinearIsometry.isometry

/-- Reinterpret a `LinearIsometryEquiv` as an `IsometryEquiv`. -/
def toIsometryEquiv : E ‚âÉ·µ¢ E‚ÇÇ :=
  ‚ü®e.toLinearEquiv.toEquiv, e.isometry‚ü©

theorem toIsometryEquiv_injective :
    Function.Injective (toIsometryEquiv : (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) ‚Üí E ‚âÉ·µ¢ E‚ÇÇ) := fun x _ h =>
  coe_injective (congr_arg _ h : ‚áëx.toIsometryEquiv = _)

@[simp]
theorem toIsometryEquiv_inj {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} :
    f.toIsometryEquiv = g.toIsometryEquiv ‚Üî f = g :=
  toIsometryEquiv_injective.eq_iff

@[simp]
theorem coe_toIsometryEquiv : ‚áëe.toIsometryEquiv = e :=
  rfl

theorem range_eq_univ (e : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : Set.range e = Set.univ := by
  rw [‚Üê coe_toIsometryEquiv]
  exact IsometryEquiv.range_eq_univ _

/-- Reinterpret a `LinearIsometryEquiv` as a `Homeomorph`. -/
def toHomeomorph : E ‚âÉ‚Çú E‚ÇÇ :=
  e.toIsometryEquiv.toHomeomorph

theorem toHomeomorph_injective : Function.Injective (toHomeomorph : (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) ‚Üí E ‚âÉ‚Çú E‚ÇÇ) :=
  fun x _ h => coe_injective (congr_arg _ h : ‚áëx.toHomeomorph = _)

@[simp]
theorem toHomeomorph_inj {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} : f.toHomeomorph = g.toHomeomorph ‚Üî f = g :=
  toHomeomorph_injective.eq_iff

@[simp]
theorem coe_toHomeomorph : ‚áëe.toHomeomorph = e :=
  rfl

protected theorem continuous : Continuous e :=
  e.isometry.continuous

protected theorem continuousAt {x} : ContinuousAt e x :=
  e.continuous.continuousAt

protected theorem continuousOn {s} : ContinuousOn e s :=
  e.continuous.continuousOn

protected theorem continuousWithinAt {s x} : ContinuousWithinAt e s x :=
  e.continuous.continuousWithinAt

/-- Interpret a `LinearIsometryEquiv` as a `ContinuousLinearEquiv`. -/
def toContinuousLinearEquiv : E ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  { e.toLinearIsometry.toContinuousLinearMap, e.toHomeomorph with }

theorem toContinuousLinearEquiv_injective :
    Function.Injective (toContinuousLinearEquiv : _ ‚Üí E ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) := fun x _ h =>
  coe_injective (congr_arg _ h : ‚áëx.toContinuousLinearEquiv = _)

@[simp]
theorem toContinuousLinearEquiv_inj {f g : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ} :
    f.toContinuousLinearEquiv = g.toContinuousLinearEquiv ‚Üî f = g :=
  toContinuousLinearEquiv_injective.eq_iff

@[simp]
theorem coe_toContinuousLinearEquiv : ‚áëe.toContinuousLinearEquiv = e :=
  rfl

variable (R E)

/-- Identity map as a `LinearIsometryEquiv`. -/
def refl : E ‚âÉ‚Çó·µ¢[R] E :=
  ‚ü®LinearEquiv.refl R E, fun _ => rfl‚ü©

/-- Linear isometry equiv between a space and its lift to another universe. -/
def ulift : ULift E ‚âÉ‚Çó·µ¢[R] E :=
  { ContinuousLinearEquiv.ulift with norm_map' := fun _ => rfl }

variable {R E}

instance instInhabited : Inhabited (E ‚âÉ‚Çó·µ¢[R] E) := ‚ü®refl R E‚ü©

@[simp]
theorem coe_refl : ‚áë(refl R E) = id :=
  rfl

/-- The inverse `LinearIsometryEquiv`. -/
def symm : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÅ] E :=
  ‚ü®e.toLinearEquiv.symm, fun x =>
    (e.norm_map _).symm.trans <| congr_arg norm <| e.toLinearEquiv.apply_symm_apply x‚ü©

@[simp]
theorem apply_symm_apply (x : E‚ÇÇ) : e (e.symm x) = x :=
  e.toLinearEquiv.apply_symm_apply x

@[simp]
theorem symm_apply_apply (x : E) : e.symm (e x) = x :=
  e.toLinearEquiv.symm_apply_apply x

theorem map_eq_zero_iff {x : E} : e x = 0 ‚Üî x = 0 :=
  e.toLinearEquiv.map_eq_zero_iff

@[simp]
theorem symm_symm : e.symm.symm = e := rfl

theorem symm_bijective : Function.Bijective (symm : (E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÅ] E) ‚Üí _) :=
  Function.bijective_iff_has_inverse.mpr ‚ü®_, symm_symm, symm_symm‚ü©

@[simp]
theorem toLinearEquiv_symm : e.symm.toLinearEquiv = e.toLinearEquiv.symm :=
  rfl

@[simp]
theorem coe_symm_toLinearEquiv : ‚áëe.toLinearEquiv.symm = e.symm := rfl

@[simp]
theorem toContinuousLinearEquiv_symm :
    e.symm.toContinuousLinearEquiv = e.toContinuousLinearEquiv.symm := rfl

@[simp]
theorem coe_symm_toContinuousLinearEquiv : ‚áëe.toContinuousLinearEquiv.symm = e.symm :=
  rfl

@[simp]
theorem toIsometryEquiv_symm : e.symm.toIsometryEquiv = e.toIsometryEquiv.symm :=
  rfl

@[simp]
theorem coe_symm_toIsometryEquiv : ‚áëe.toIsometryEquiv.symm = e.symm := rfl

@[simp]
theorem toHomeomorph_symm : e.symm.toHomeomorph = e.toHomeomorph.symm :=
  rfl

@[simp]
theorem coe_symm_toHomeomorph : ‚áëe.toHomeomorph.symm = e.symm := rfl

/-- See Note [custom simps projection]. We need to specify this projection explicitly in this case,
  because it is a composition of multiple projections. -/
def Simps.apply (œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ) {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
    (E E‚ÇÇ : Type*) [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ] [Module R E]
    [Module R‚ÇÇ E‚ÇÇ] (h : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : E ‚Üí E‚ÇÇ :=
  h

/-- See Note [custom simps projection] -/
def Simps.symm_apply (œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* R‚ÇÇ) {œÉ‚ÇÇ‚ÇÅ : R‚ÇÇ ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ]
    [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ] (E E‚ÇÇ : Type*) [SeminormedAddCommGroup E] [SeminormedAddCommGroup E‚ÇÇ]
    [Module R E] [Module R‚ÇÇ E‚ÇÇ] (h : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) : E‚ÇÇ ‚Üí E :=
  h.symm

initialize_simps_projections LinearIsometryEquiv (toFun ‚Üí apply, invFun ‚Üí symm_apply)

/-- Composition of `LinearIsometryEquiv`s as a `LinearIsometryEquiv`. -/
def trans (e' : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÉ] E‚ÇÉ :=
  ‚ü®e.toLinearEquiv.trans e'.toLinearEquiv, fun _ => (e'.norm_map _).trans (e.norm_map _)‚ü©

@[simp]
theorem coe_trans (e‚ÇÅ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (e‚ÇÇ : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) : ‚áë(e‚ÇÅ.trans e‚ÇÇ) = e‚ÇÇ ‚àò e‚ÇÅ :=
  rfl

@[simp]
theorem trans_apply (e‚ÇÅ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (e‚ÇÇ : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) (c : E) :
    (e‚ÇÅ.trans e‚ÇÇ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÉ] E‚ÇÉ) c = e‚ÇÇ (e‚ÇÅ c) :=
  rfl

@[simp]
theorem toLinearEquiv_trans (e' : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) :
    (e.trans e').toLinearEquiv = e.toLinearEquiv.trans e'.toLinearEquiv :=
  rfl

@[simp]
theorem toIsometryEquiv_trans (e' : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) :
    (e.trans e').toIsometryEquiv = e.toIsometryEquiv.trans e'.toIsometryEquiv :=
  rfl

@[simp]
theorem toHomeomorph_trans (e' : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) :
    (e.trans e').toHomeomorph = e.toHomeomorph.trans e'.toHomeomorph :=
  rfl

@[simp]
theorem trans_refl : e.trans (refl R‚ÇÇ E‚ÇÇ) = e :=
  ext fun _ => rfl

@[simp]
theorem refl_trans : (refl R E).trans e = e :=
  ext fun _ => rfl

@[simp]
theorem self_trans_symm : e.trans e.symm = refl R E :=
  ext e.symm_apply_apply

@[simp]
theorem symm_trans_self : e.symm.trans e = refl R‚ÇÇ E‚ÇÇ :=
  ext e.apply_symm_apply

@[simp]
theorem symm_comp_self : e.symm ‚àò e = id :=
  funext e.symm_apply_apply

@[simp]
theorem self_comp_symm : e ‚àò e.symm = id :=
  e.symm.symm_comp_self

@[simp]
theorem symm_trans (e‚ÇÅ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (e‚ÇÇ : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) :
    (e‚ÇÅ.trans e‚ÇÇ).symm = e‚ÇÇ.symm.trans e‚ÇÅ.symm :=
  rfl

theorem coe_symm_trans (e‚ÇÅ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (e‚ÇÇ : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) :
    ‚áë(e‚ÇÅ.trans e‚ÇÇ).symm = e‚ÇÅ.symm ‚àò e‚ÇÇ.symm :=
  rfl

theorem trans_assoc (eEE‚ÇÇ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (eE‚ÇÇE‚ÇÉ : E‚ÇÇ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÇ‚ÇÉ] E‚ÇÉ) (eE‚ÇÉE‚ÇÑ : E‚ÇÉ ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÉ‚ÇÑ] E‚ÇÑ) :
    eEE‚ÇÇ.trans (eE‚ÇÇE‚ÇÉ.trans eE‚ÇÉE‚ÇÑ) = (eEE‚ÇÇ.trans eE‚ÇÇE‚ÇÉ).trans eE‚ÇÉE‚ÇÑ :=
  rfl

instance instGroup : Group (E ‚âÉ‚Çó·µ¢[R] E) where
  mul e‚ÇÅ e‚ÇÇ := e‚ÇÇ.trans e‚ÇÅ
  one := refl _ _
  inv := symm
  one_mul := trans_refl
  mul_one := refl_trans
  mul_assoc _ _ _ := trans_assoc _ _ _
  inv_mul_cancel := self_trans_symm

@[simp]
theorem coe_one : ‚áë(1 : E ‚âÉ‚Çó·µ¢[R] E) = id :=
  rfl

@[simp]
theorem coe_mul (e e' : E ‚âÉ‚Çó·µ¢[R] E) : ‚áë(e * e') = e ‚àò e' :=
  rfl

@[simp]
theorem coe_inv (e : E ‚âÉ‚Çó·µ¢[R] E) : ‚áëe‚Åª¬π = e.symm :=
  rfl

theorem one_def : (1 : E ‚âÉ‚Çó·µ¢[R] E) = refl _ _ :=
  rfl

theorem mul_def (e e' : E ‚âÉ‚Çó·µ¢[R] E) : (e * e' : E ‚âÉ‚Çó·µ¢[R] E) = e'.trans e :=
  rfl

theorem inv_def (e : E ‚âÉ‚Çó·µ¢[R] E) : (e‚Åª¬π : E ‚âÉ‚Çó·µ¢[R] E) = e.symm :=
  rfl

/-! Lemmas about mixing the group structure with definitions. Because we have multiple ways to
express `LinearIsometryEquiv.refl`, `LinearIsometryEquiv.symm`, and
`LinearIsometryEquiv.trans`, we want simp lemmas for every combination.
The assumption made here is that if you're using the group structure, you want to preserve it
after simp.

This copies the approach used by the lemmas near `Equiv.Perm.trans_one`. -/


@[simp]
theorem trans_one : e.trans (1 : E‚ÇÇ ‚âÉ‚Çó·µ¢[R‚ÇÇ] E‚ÇÇ) = e :=
  trans_refl _

@[simp]
theorem one_trans : (1 : E ‚âÉ‚Çó·µ¢[R] E).trans e = e :=
  refl_trans _

@[simp]
theorem refl_mul (e : E ‚âÉ‚Çó·µ¢[R] E) : refl _ _ * e = e :=
  trans_refl _

@[simp]
theorem mul_refl (e : E ‚âÉ‚Çó·µ¢[R] E) : e * refl _ _ = e :=
  refl_trans _

/-- Reinterpret a `LinearIsometryEquiv` as a `ContinuousLinearEquiv`. -/
instance instCoeTCContinuousLinearEquiv : CoeTC (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (E ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) :=
  ‚ü®fun e => ‚ü®e.toLinearEquiv, e.continuous, e.toIsometryEquiv.symm.continuous‚ü©‚ü©

instance instCoeTCContinuousLinearMap : CoeTC (E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) :=
  ‚ü®fun e => ‚Üë(e : E ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)‚ü©

@[simp]
theorem coe_coe : ‚áë(e : E ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) = e :=
  rfl

@[simp]
theorem coe_coe'' : ‚áë(e : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) = e :=
  rfl

theorem map_zero : e 0 = 0 :=
  e.1.map_zero

theorem map_add (x y : E) : e (x + y) = e x + e y :=
  e.1.map_add x y

theorem map_sub (x y : E) : e (x - y) = e x - e y :=
  e.1.map_sub x y

theorem map_smul‚Çõ‚Çó (c : R) (x : E) : e (c ‚Ä¢ x) = œÉ‚ÇÅ‚ÇÇ c ‚Ä¢ e x :=
  e.1.map_smul‚Çõ‚Çó c x

theorem map_smul [Module R E‚ÇÇ] {e : E ‚âÉ‚Çó·µ¢[R] E‚ÇÇ} (c : R) (x : E) : e (c ‚Ä¢ x) = c ‚Ä¢ e x :=
  e.1.map_smul c x

@[simp] -- Should be replaced with `SemilinearIsometryClass.nnorm_map` when https://github.com/leanprover/lean4/issues/3107 is fixed.
theorem nnnorm_map (x : E) : ‚Äñe x‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä :=
  SemilinearIsometryClass.nnnorm_map e x

@[simp]
theorem dist_map (x y : E) : dist (e x) (e y) = dist x y :=
  e.toLinearIsometry.dist_map x y

@[simp]
theorem edist_map (x y : E) : edist (e x) (e y) = edist x y :=
  e.toLinearIsometry.edist_map x y

protected theorem bijective : Bijective e :=
  e.1.bijective

protected theorem injective : Injective e :=
  e.1.injective

protected theorem surjective : Surjective e :=
  e.1.surjective

theorem map_eq_iff {x y : E} : e x = e y ‚Üî x = y :=
  e.injective.eq_iff

theorem map_ne {x y : E} (h : x ‚â† y) : e x ‚â† e y :=
  e.injective.ne h

protected theorem lipschitz : LipschitzWith 1 e :=
  e.isometry.lipschitz

protected theorem antilipschitz : AntilipschitzWith 1 e :=
  e.isometry.antilipschitz

theorem image_eq_preimage (s : Set E) : e '' s = e.symm ‚Åª¬π' s :=
  e.toLinearEquiv.image_eq_preimage s

@[simp]
theorem ediam_image (s : Set E) : EMetric.diam (e '' s) = EMetric.diam s :=
  e.isometry.ediam_image s

@[simp]
theorem diam_image (s : Set E) : Metric.diam (e '' s) = Metric.diam s :=
  e.isometry.diam_image s

@[simp]
theorem preimage_ball (x : E‚ÇÇ) (r : ‚Ñù) : e ‚Åª¬π' Metric.ball x r = Metric.ball (e.symm x) r :=
  e.toIsometryEquiv.preimage_ball x r

@[simp]
theorem preimage_sphere (x : E‚ÇÇ) (r : ‚Ñù) : e ‚Åª¬π' Metric.sphere x r = Metric.sphere (e.symm x) r :=
  e.toIsometryEquiv.preimage_sphere x r

@[simp]
theorem preimage_closedBall (x : E‚ÇÇ) (r : ‚Ñù) :
    e ‚Åª¬π' Metric.closedBall x r = Metric.closedBall (e.symm x) r :=
  e.toIsometryEquiv.preimage_closedBall x r

@[simp]
theorem image_ball (x : E) (r : ‚Ñù) : e '' Metric.ball x r = Metric.ball (e x) r :=
  e.toIsometryEquiv.image_ball x r

@[simp]
theorem image_sphere (x : E) (r : ‚Ñù) : e '' Metric.sphere x r = Metric.sphere (e x) r :=
  e.toIsometryEquiv.image_sphere x r

@[simp]
theorem image_closedBall (x : E) (r : ‚Ñù) : e '' Metric.closedBall x r = Metric.closedBall (e x) r :=
  e.toIsometryEquiv.image_closedBall x r

variable {Œ± : Type*} [TopologicalSpace Œ±]

@[simp]
theorem comp_continuousOn_iff {f : Œ± ‚Üí E} {s : Set Œ±} : ContinuousOn (e ‚àò f) s ‚Üî ContinuousOn f s :=
  e.isometry.comp_continuousOn_iff

@[simp]
theorem comp_continuous_iff {f : Œ± ‚Üí E} : Continuous (e ‚àò f) ‚Üî Continuous f :=
  e.isometry.comp_continuous_iff

instance completeSpace_map (p : Submodule R E) [CompleteSpace p] :
    CompleteSpace (p.map (e.toLinearEquiv : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ)) :=
  e.toLinearIsometry.completeSpace_map' p

/-- Construct a linear isometry equiv from a surjective linear isometry. -/
noncomputable def ofSurjective (f : F ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (hfr : Function.Surjective f) :
    F ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  { LinearEquiv.ofBijective f.toLinearMap ‚ü®f.injective, hfr‚ü© with norm_map' := f.norm_map }

@[simp]
theorem coe_ofSurjective (f : F ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (hfr : Function.Surjective f) :
    ‚áë(LinearIsometryEquiv.ofSurjective f hfr) = f := by
  ext
  rfl

/-- If a linear isometry has an inverse, it is a linear isometric equivalence. -/
def ofLinearIsometry (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (g : E‚ÇÇ ‚Üí‚Çõ‚Çó[œÉ‚ÇÇ‚ÇÅ] E)
    (h‚ÇÅ : f.toLinearMap.comp g = LinearMap.id) (h‚ÇÇ : g.comp f.toLinearMap = LinearMap.id) :
    E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ :=
  { toLinearEquiv := LinearEquiv.ofLinear f.toLinearMap g h‚ÇÅ h‚ÇÇ
    norm_map' := fun x => f.norm_map x }

@[simp]
theorem coe_ofLinearIsometry (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (g : E‚ÇÇ ‚Üí‚Çõ‚Çó[œÉ‚ÇÇ‚ÇÅ] E)
    (h‚ÇÅ : f.toLinearMap.comp g = LinearMap.id) (h‚ÇÇ : g.comp f.toLinearMap = LinearMap.id) :
    (ofLinearIsometry f g h‚ÇÅ h‚ÇÇ : E ‚Üí E‚ÇÇ) = (f : E ‚Üí E‚ÇÇ) :=
  rfl

@[simp]
theorem coe_ofLinearIsometry_symm (f : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ) (g : E‚ÇÇ ‚Üí‚Çõ‚Çó[œÉ‚ÇÇ‚ÇÅ] E)
    (h‚ÇÅ : f.toLinearMap.comp g = LinearMap.id) (h‚ÇÇ : g.comp f.toLinearMap = LinearMap.id) :
    ((ofLinearIsometry f g h‚ÇÅ h‚ÇÇ).symm : E‚ÇÇ ‚Üí E) = (g : E‚ÇÇ ‚Üí E) :=
  rfl

variable (R) in
/-- The negation operation on a normed space `E`, considered as a linear isometry equivalence. -/
def neg : E ‚âÉ‚Çó·µ¢[R] E :=
  { LinearEquiv.neg R with norm_map' := norm_neg }

@[simp]
theorem coe_neg : (neg R : E ‚Üí E) = fun x => -x :=
  rfl

@[simp]
theorem symm_neg : (neg R : E ‚âÉ‚Çó·µ¢[R] E).symm = neg R :=
  rfl

variable (R E E‚ÇÇ E‚ÇÉ)

/-- The natural equivalence `(E √ó E‚ÇÇ) √ó E‚ÇÉ ‚âÉ E √ó (E‚ÇÇ √ó E‚ÇÉ)` is a linear isometry. -/
def prodAssoc [Module R E‚ÇÇ] [Module R E‚ÇÉ] : (E √ó E‚ÇÇ) √ó E‚ÇÉ ‚âÉ‚Çó·µ¢[R] E √ó E‚ÇÇ √ó E‚ÇÉ :=
  { LinearEquiv.prodAssoc R E E‚ÇÇ E‚ÇÉ with
    norm_map' := by
      rintro ‚ü®‚ü®e, f‚ü©, g‚ü©
      simp only [LinearEquiv.prodAssoc_apply, AddEquiv.toEquiv_eq_coe,
        Equiv.toFun_as_coe, EquivLike.coe_coe, AddEquiv.coe_prodAssoc,
        Equiv.prodAssoc_apply, Prod.norm_def, max_assoc] }

@[simp]
theorem coe_prodAssoc [Module R E‚ÇÇ] [Module R E‚ÇÉ] :
    (prodAssoc R E E‚ÇÇ E‚ÇÉ : (E √ó E‚ÇÇ) √ó E‚ÇÉ ‚Üí E √ó E‚ÇÇ √ó E‚ÇÉ) = Equiv.prodAssoc E E‚ÇÇ E‚ÇÉ :=
  rfl

@[simp]
theorem coe_prodAssoc_symm [Module R E‚ÇÇ] [Module R E‚ÇÉ] :
    ((prodAssoc R E E‚ÇÇ E‚ÇÉ).symm : E √ó E‚ÇÇ √ó E‚ÇÉ ‚Üí (E √ó E‚ÇÇ) √ó E‚ÇÉ) = (Equiv.prodAssoc E E‚ÇÇ E‚ÇÉ).symm :=
  rfl

/-- If `p` is a submodule that is equal to `‚ä§`, then `LinearIsometryEquiv.ofTop p hp` is the
"identity" equivalence between `p` and `E`. -/
@[simps! toLinearEquiv apply symm_apply_coe]
def ofTop {R : Type*} [Ring R] [Module R E] (p : Submodule R E) (hp : p = ‚ä§) : p ‚âÉ‚Çó·µ¢[R] E :=
  { p.subtype‚Çó·µ¢ with toLinearEquiv := LinearEquiv.ofTop p hp }

variable {R E E‚ÇÇ E‚ÇÉ} {R' : Type*} [Ring R']
variable [Module R' E] (p q : Submodule R' E)

/-- `LinearEquiv.ofEq` as a `LinearIsometryEquiv`. -/
def ofEq (hpq : p = q) : p ‚âÉ‚Çó·µ¢[R'] q :=
  { LinearEquiv.ofEq p q hpq with norm_map' := fun _ => rfl }

variable {p q}

@[simp]
theorem coe_ofEq_apply (h : p = q) (x : p) : (ofEq p q h x : E) = x :=
  rfl

@[simp]
theorem ofEq_symm (h : p = q) : (ofEq p q h).symm = ofEq q p h.symm :=
  rfl

@[simp]
theorem ofEq_rfl : ofEq p p rfl = LinearIsometryEquiv.refl R' p := rfl

end LinearIsometryEquiv

/-- Two linear isometries are equal if they are equal on basis vectors. -/
theorem Module.Basis.ext_linearIsometry {Œπ : Type*} (b : Basis Œπ R E) {f‚ÇÅ f‚ÇÇ : E ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ}
    (h : ‚àÄ i, f‚ÇÅ (b i) = f‚ÇÇ (b i)) : f‚ÇÅ = f‚ÇÇ :=
  LinearIsometry.toLinearMap_injective <| b.ext h

/-- Two linear isometric equivalences are equal if they are equal on basis vectors. -/
theorem Module.Basis.ext_linearIsometryEquiv {Œπ : Type*} (b : Basis Œπ R E) {f‚ÇÅ f‚ÇÇ : E ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E‚ÇÇ}
    (h : ‚àÄ i, f‚ÇÅ (b i) = f‚ÇÇ (b i)) : f‚ÇÅ = f‚ÇÇ :=
  LinearIsometryEquiv.toLinearEquiv_injective <| b.ext' h

/-- Reinterpret a `LinearIsometry` as a `LinearIsometryEquiv` to the range. -/
@[simps! apply_coe]
noncomputable def LinearIsometry.equivRange {R S : Type*} [Semiring R] [Ring S] [Module S E]
    [Module R F] {œÉ‚ÇÅ‚ÇÇ : R ‚Üí+* S} {œÉ‚ÇÇ‚ÇÅ : S ‚Üí+* R} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
    (f : F ‚Üí‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] E) : F ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] (LinearMap.range f.toLinearMap) :=
  { f with toLinearEquiv := LinearEquiv.ofInjective f.toLinearMap f.injective }
