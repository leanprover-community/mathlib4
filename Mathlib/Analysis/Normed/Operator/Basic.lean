/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Algebra.Algebra.Tower
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Algebra.Module.StrongTopology
import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Analysis.Normed.Operator.ContinuousLinearMap
import Mathlib.Tactic.SuppressCompilation

/-!
# Operator norm on the space of continuous linear maps

Define the operator (semi)-norm on the space of continuous (semi)linear maps between (semi)-normed
spaces, and prove its basic properties. In particular, show that this space is itself a semi-normed
space.

Since a lot of elementary properties don't require `â€–xâ€– = 0 â†’ x = 0` we start setting up the
theory for `SeminormedAddCommGroup`. Later we will specialize to `NormedAddCommGroup` in the
file `NormedSpace.lean`.

Note that most of statements that apply to semilinear maps only hold when the ring homomorphism
is isometric, as expressed by the typeclass `[RingHomIsometric Ïƒ]`.

-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E F Fâ‚— G ğ“• : Type*}

section SemiNormed

open Metric ContinuousLinearMap

variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [SeminormedAddCommGroup Fâ‚—]
  [SeminormedAddCommGroup G]

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Fâ‚—] [NormedSpace ğ•œâ‚ƒ G]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

variable [FunLike ğ“• E F]

/-- If `â€–xâ€– = 0` and `f` is continuous then `â€–f xâ€– = 0`. -/
theorem norm_image_of_norm_zero [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•) (hf : Continuous f) {x : E}
    (hx : â€–xâ€– = 0) : â€–f xâ€– = 0 := by
  rw [â† mem_closure_zero_iff_norm, â† specializes_iff_mem_closure, â† map_zero f] at *
  exact hx.map hf

section

variable [RingHomIsometric Ïƒâ‚â‚‚]

theorem SemilinearMapClass.bound_of_shell_semi_normed [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    {Îµ C : â„} (Îµ_pos : 0 < Îµ) {c : ğ•œ} (hc : 1 < â€–câ€–)
    (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) {x : E} (hx : â€–xâ€– â‰  0) :
    â€–f xâ€– â‰¤ C * â€–xâ€– :=
  (normSeminorm ğ•œ E).bound_of_shell ((normSeminorm ğ•œâ‚‚ F).comp âŸ¨âŸ¨f, map_add fâŸ©, map_smulâ‚›â‚— fâŸ©)
    Îµ_pos hc hf hx

/-- A continuous linear map between seminormed spaces is bounded when the field is nontrivially
normed. The continuity ensures boundedness on a ball of some radius `Îµ`. The nontriviality of the
norm is then used to rescale any element into an element of norm in `[Îµ/C, Îµ]`, whose image has a
controlled norm. The norm control for the original element follows by rescaling. -/
theorem SemilinearMapClass.bound_of_continuous [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    (hf : Continuous f) : âˆƒ C, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€– :=
  let Ï† : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F := âŸ¨âŸ¨f, map_add fâŸ©, map_smulâ‚›â‚— fâŸ©
  ((normSeminorm ğ•œâ‚‚ F).comp Ï†).bound_of_continuous_normedSpace (continuous_norm.comp hf)

theorem SemilinearMapClass.nnbound_of_continuous [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    (hf : Continuous f) : âˆƒ C : â„â‰¥0, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š :=
  let âŸ¨c, hc, hcfâŸ© := SemilinearMapClass.bound_of_continuous f hf; âŸ¨âŸ¨c, hc.leâŸ©, hc, hcfâŸ©

theorem SemilinearMapClass.ebound_of_continuous [SemilinearMapClass ğ“• Ïƒâ‚â‚‚ E F] (f : ğ“•)
    (hf : Continuous f) : âˆƒ C : â„â‰¥0, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€–â‚‘ â‰¤ C * â€–xâ€–â‚‘ :=
  let âŸ¨c, hc, hcfâŸ© := SemilinearMapClass.nnbound_of_continuous f hf
  âŸ¨c, hc, fun x => ENNReal.coe_mono <| hcf xâŸ©

end

namespace ContinuousLinearMap

theorem bound [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) : âˆƒ C, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€– â‰¤ C * â€–xâ€– :=
  SemilinearMapClass.bound_of_continuous f f.2

theorem nnbound [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    âˆƒ C : â„â‰¥0, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€–â‚Š â‰¤ C * â€–xâ€–â‚Š :=
  SemilinearMapClass.nnbound_of_continuous f f.2

theorem ebound [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    âˆƒ C : â„â‰¥0, 0 < C âˆ§ âˆ€ x : E, â€–f xâ€–â‚‘ â‰¤ C * â€–xâ€–â‚‘ :=
  SemilinearMapClass.ebound_of_continuous f f.2

section

open Filter

variable (ğ•œ E)

/-- Given a unit-length element `x` of a normed space `E` over a field `ğ•œ`, the natural linear
isometry map from `ğ•œ` to `E` by taking multiples of `x`. -/
def _root_.LinearIsometry.toSpanSingleton {v : E} (hv : â€–vâ€– = 1) : ğ•œ â†’â‚—áµ¢[ğ•œ] E :=
  { LinearMap.toSpanSingleton ğ•œ E v with norm_map' := fun x => by simp [norm_smul, hv] }

variable {ğ•œ E}

@[simp]
theorem _root_.LinearIsometry.toSpanSingleton_apply {v : E} (hv : â€–vâ€– = 1) (a : ğ•œ) :
    LinearIsometry.toSpanSingleton ğ•œ E hv a = a â€¢ v :=
  rfl

@[simp]
theorem _root_.LinearIsometry.coe_toSpanSingleton {v : E} (hv : â€–vâ€– = 1) :
    (LinearIsometry.toSpanSingleton ğ•œ E hv).toLinearMap = LinearMap.toSpanSingleton ğ•œ E v :=
  rfl

end

section OpNorm

open Set Real

/-- The operator norm of a continuous linear map is the inf of all its bounds. -/
def opNorm (f : E â†’SL[Ïƒâ‚â‚‚] F) :=
  sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– }

instance hasOpNorm : Norm (E â†’SL[Ïƒâ‚â‚‚] F) :=
  âŸ¨opNormâŸ©

theorem norm_def (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–fâ€– = sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  rfl

-- So that invocations of `le_csInf` make sense: we show that the set of
-- bounds is nonempty and bounded below.
theorem bounds_nonempty [RingHomIsometric Ïƒâ‚â‚‚] {f : E â†’SL[Ïƒâ‚â‚‚] F} :
    âˆƒ c, c âˆˆ { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  let âŸ¨M, hMp, hMbâŸ© := f.bound
  âŸ¨M, le_of_lt hMp, hMbâŸ©

theorem bounds_bddBelow {f : E â†’SL[Ïƒâ‚â‚‚] F} : BddBelow { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€– } :=
  âŸ¨0, fun _ âŸ¨hn, _âŸ© => hnâŸ©

theorem isLeast_opNorm [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    IsLeast {c | 0 â‰¤ c âˆ§ âˆ€ x, â€–f xâ€– â‰¤ c * â€–xâ€–} â€–fâ€– := by
  refine IsClosed.isLeast_csInf ?_ bounds_nonempty bounds_bddBelow
  simp only [setOf_and, setOf_forall]
  refine isClosed_Ici.inter <| isClosed_iInter fun _ â†¦ isClosed_le ?_ ?_ <;> continuity


/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem opNorm_le_bound (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp : 0 â‰¤ M) (hM : âˆ€ x, â€–f xâ€– â‰¤ M * â€–xâ€–) :
    â€–fâ€– â‰¤ M :=
  csInf_le bounds_bddBelow âŸ¨hMp, hMâŸ©


/-- If one controls the norm of every `A x`, `â€–xâ€– â‰  0`, then one controls the norm of `A`. -/
theorem opNorm_le_bound' (f : E â†’SL[Ïƒâ‚â‚‚] F) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– â‰  0 â†’ â€–f xâ€– â‰¤ M * â€–xâ€–) : â€–fâ€– â‰¤ M :=
  opNorm_le_bound f hMp fun x =>
    (ne_or_eq â€–xâ€– 0).elim (hM x) fun h => by
      simp only [h, mul_zero, norm_image_of_norm_zero f f.2 h, le_refl]


theorem opNorm_eq_of_bounds {Ï† : E â†’SL[Ïƒâ‚â‚‚] F} {M : â„} (M_nonneg : 0 â‰¤ M)
    (h_above : âˆ€ x, â€–Ï† xâ€– â‰¤ M * â€–xâ€–) (h_below : âˆ€ N â‰¥ 0, (âˆ€ x, â€–Ï† xâ€– â‰¤ N * â€–xâ€–) â†’ M â‰¤ N) :
    â€–Ï†â€– = M :=
  le_antisymm (Ï†.opNorm_le_bound M_nonneg h_above)
    ((le_csInf_iff ContinuousLinearMap.bounds_bddBelow âŸ¨M, M_nonneg, h_aboveâŸ©).mpr
      fun N âŸ¨N_nonneg, hNâŸ© => h_below N N_nonneg hN)

theorem opNorm_neg (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–-fâ€– = â€–fâ€– := by simp only [norm_def, neg_apply, norm_neg]


theorem opNorm_nonneg (f : E â†’SL[Ïƒâ‚â‚‚] F) : 0 â‰¤ â€–fâ€– :=
  Real.sInf_nonneg fun _ â†¦ And.left


/-- The norm of the `0` operator is `0`. -/
theorem opNorm_zero : â€–(0 : E â†’SL[Ïƒâ‚â‚‚] F)â€– = 0 :=
  le_antisymm (opNorm_le_bound _ le_rfl fun _ â†¦ by simp) (opNorm_nonneg _)


/-- The norm of the identity is at most `1`. It is in fact `1`, except when the space is trivial
where it is `0`. It means that one cannot do better than an inequality in general. -/
theorem norm_id_le : â€–id ğ•œ Eâ€– â‰¤ 1 :=
  opNorm_le_bound _ zero_le_one fun x => by simp

section

variable [RingHomIsometric Ïƒâ‚â‚‚] [RingHomIsometric Ïƒâ‚‚â‚ƒ] (f g : E â†’SL[Ïƒâ‚â‚‚] F) (h : F â†’SL[Ïƒâ‚‚â‚ƒ] G)
  (x : E)

/-- The fundamental property of the operator norm: `â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€–`. -/
theorem le_opNorm : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := (isLeast_opNorm f).1.2 x

theorem dist_le_opNorm (x y : E) : dist (f x) (f y) â‰¤ â€–fâ€– * dist x y := by
  simp_rw [dist_eq_norm, â† map_sub, f.le_opNorm]


theorem le_of_opNorm_le_of_le {x} {a b : â„} (hf : â€–fâ€– â‰¤ a) (hx : â€–xâ€– â‰¤ b) :
    â€–f xâ€– â‰¤ a * b :=
  (f.le_opNorm x).trans <| by gcongr; exact (opNorm_nonneg f).trans hf


theorem le_opNorm_of_le {c : â„} {x} (h : â€–xâ€– â‰¤ c) : â€–f xâ€– â‰¤ â€–fâ€– * c :=
  f.le_of_opNorm_le_of_le le_rfl h


theorem le_of_opNorm_le {c : â„} (h : â€–fâ€– â‰¤ c) (x : E) : â€–f xâ€– â‰¤ c * â€–xâ€– :=
  f.le_of_opNorm_le_of_le h le_rfl


theorem opNorm_le_iff {f : E â†’SL[Ïƒâ‚â‚‚] F} {M : â„} (hMp : 0 â‰¤ M) :
    â€–fâ€– â‰¤ M â†” âˆ€ x, â€–f xâ€– â‰¤ M * â€–xâ€– :=
  âŸ¨f.le_of_opNorm_le, opNorm_le_bound f hMpâŸ©


theorem ratio_le_opNorm : â€–f xâ€– / â€–xâ€– â‰¤ â€–fâ€– :=
  div_le_of_le_mulâ‚€ (norm_nonneg _) f.opNorm_nonneg (le_opNorm _ _)


/-- The image of the unit ball under a continuous linear map is bounded. -/
theorem unit_le_opNorm : â€–xâ€– â‰¤ 1 â†’ â€–f xâ€– â‰¤ â€–fâ€– :=
  mul_one â€–fâ€– â–¸ f.le_opNorm_of_le


theorem opNorm_le_of_shell {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C) {c : ğ•œ}
    (hc : 1 < â€–câ€–) (hf : âˆ€ x, Îµ / â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
  f.opNorm_le_bound' hC fun _ hx => SemilinearMapClass.bound_of_shell_semi_normed f Îµ_pos hc hf hx


theorem opNorm_le_of_ball {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ : â„} {C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C)
    (hf : âˆ€ x âˆˆ ball (0 : E) Îµ, â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C := by
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  refine opNorm_le_of_shell Îµ_pos hC hc fun x _ hx => hf x ?_
  rwa [ball_zero_eq]


theorem opNorm_le_of_nhds_zero {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„} (hC : 0 â‰¤ C)
    (hf : âˆ€á¶  x in ğ“ (0 : E), â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C :=
  let âŸ¨_, Îµ0, hÎµâŸ© := Metric.eventually_nhds_iff_ball.1 hf
  opNorm_le_of_ball Îµ0 hC hÎµ


theorem opNorm_le_of_shell' {f : E â†’SL[Ïƒâ‚â‚‚] F} {Îµ C : â„} (Îµ_pos : 0 < Îµ) (hC : 0 â‰¤ C) {c : ğ•œ}
    (hc : â€–câ€– < 1) (hf : âˆ€ x, Îµ * â€–câ€– â‰¤ â€–xâ€– â†’ â€–xâ€– < Îµ â†’ â€–f xâ€– â‰¤ C * â€–xâ€–) : â€–fâ€– â‰¤ C := by
  by_cases h0 : c = 0
  Â· refine opNorm_le_of_ball Îµ_pos hC fun x hx => hf x ?_ ?_
    Â· simp [h0]
    Â· rwa [ball_zero_eq] at hx
  Â· rw [â† inv_inv c, norm_inv, inv_lt_oneâ‚€ (norm_pos_iff.2 <| inv_ne_zero h0)] at hc
    refine opNorm_le_of_shell Îµ_pos hC hc ?_
    rwa [norm_inv, div_eq_mul_inv, inv_inv]


/-- For a continuous real linear map `f`, if one controls the norm of every `f x`, `â€–xâ€– = 1`, then
one controls the norm of `f`. -/
theorem opNorm_le_of_unit_norm [NormedAlgebra â„ ğ•œ] {f : E â†’SL[Ïƒâ‚â‚‚] F} {C : â„}
    (hC : 0 â‰¤ C) (hf : âˆ€ x, â€–xâ€– = 1 â†’ â€–f xâ€– â‰¤ C) : â€–fâ€– â‰¤ C := by
  refine opNorm_le_bound' f hC fun x hx => ?_
  have Hâ‚ : â€–algebraMap _ ğ•œ â€–xâ€–â»Â¹ â€¢ xâ€– = 1 := by simp [norm_smul, inv_mul_cancelâ‚€ hx]
  have Hâ‚‚ : â€–xâ€–â»Â¹ * â€–f xâ€– â‰¤ C := by simpa [norm_smul] using hf _ Hâ‚
  rwa [â† div_eq_inv_mul, div_le_iffâ‚€] at Hâ‚‚
  exact (norm_nonneg x).lt_of_ne' hx


/-- The operator norm satisfies the triangle inequality. -/
theorem opNorm_add_le : â€–f + gâ€– â‰¤ â€–fâ€– + â€–gâ€– :=
  (f + g).opNorm_le_bound (add_nonneg f.opNorm_nonneg g.opNorm_nonneg) fun x =>
    (norm_add_le_of_le (f.le_opNorm x) (g.le_opNorm x)).trans_eq (add_mul _ _ _).symm


/-- If there is an element with norm different from `0`, then the norm of the identity equals `1`.
(Since we are working with seminorms supposing that the space is non-trivial is not enough.) -/
theorem norm_id_of_nontrivial_seminorm (h : âˆƒ x : E, â€–xâ€– â‰  0) : â€–id ğ•œ Eâ€– = 1 :=
  le_antisymm norm_id_le <| by
    let âŸ¨x, hxâŸ© := h
    have := (id ğ•œ E).ratio_le_opNorm x
    rwa [id_apply, div_self hx] at this

theorem opNorm_smul_le {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass ğ•œâ‚‚ ğ•œ' F]
    (c : ğ•œ') (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– :=
  (c â€¢ f).opNorm_le_bound (mul_nonneg (norm_nonneg _) (opNorm_nonneg _)) fun _ => by
    rw [smul_apply, norm_smul, mul_assoc]
    gcongr
    apply le_opNorm

theorem opNorm_le_iff_lipschitz {f : E â†’SL[Ïƒâ‚â‚‚] F} {K : â„â‰¥0} :
    â€–fâ€– â‰¤ K â†” LipschitzWith K f :=
  âŸ¨fun h â†¦ by simpa using AddMonoidHomClass.lipschitz_of_bound f K <| le_of_opNorm_le f h,
    fun hf â†¦ f.opNorm_le_bound K.2 <| hf.norm_le_mul (map_zero f)âŸ©

alias âŸ¨lipschitzWith_of_opNorm_le, opNorm_le_of_lipschitzâŸ© := opNorm_le_iff_lipschitz

/-- Operator seminorm on the space of continuous (semi)linear maps, as `Seminorm`.

We use this seminorm to define a `SeminormedGroup` structure on `E â†’SL[Ïƒ] F`,
but we have to override the projection `UniformSpace`
so that it is definitionally equal to the one coming from the topologies on `E` and `F`. -/
protected noncomputable def seminorm : Seminorm ğ•œâ‚‚ (E â†’SL[Ïƒâ‚â‚‚] F) :=
  .ofSMulLE norm opNorm_zero opNorm_add_le opNorm_smul_le

private lemma uniformity_eq_seminorm :
    ğ“¤ (E â†’SL[Ïƒâ‚â‚‚] F) = â¨… r > 0, ğ“Ÿ {f | â€–f.1 - f.2â€– < r} := by
  refine ContinuousLinearMap.seminorm (Ïƒâ‚â‚‚ := Ïƒâ‚â‚‚) (E := E) (F := F) |>.uniformity_eq_of_hasBasis
    (ContinuousLinearMap.hasBasis_nhds_zero_of_basis Metric.nhds_basis_closedBall)
    ?_ fun (s, r) âŸ¨hs, hrâŸ© â†¦ ?_
  Â· rcases NormedField.exists_lt_norm ğ•œ 1 with âŸ¨c, hcâŸ©
    refine âŸ¨â€–câ€–, ContinuousLinearMap.hasBasis_nhds_zero.mem_iff.2
      âŸ¨(closedBall 0 1, closedBall 0 1), ?_âŸ©âŸ©
    suffices âˆ€ f : E â†’SL[Ïƒâ‚â‚‚] F, (âˆ€ x, â€–xâ€– â‰¤ 1 â†’ â€–f xâ€– â‰¤ 1) â†’ â€–fâ€– â‰¤ â€–câ€– by
      simpa [NormedSpace.isVonNBounded_closedBall, closedBall_mem_nhds, subset_def] using this
    intro f hf
    refine opNorm_le_of_shell (f := f) one_pos (norm_nonneg c) hc fun x hcx hx â†¦ ?_
    exact (hf x hx.le).trans ((div_le_iffâ‚€' <| one_pos.trans hc).1 hcx)
  Â· rcases (NormedSpace.isVonNBounded_iff' _).1 hs with âŸ¨Îµ, hÎµâŸ©
    rcases exists_pos_mul_lt hr Îµ with âŸ¨Î´, hÎ´â‚€, hÎ´âŸ©
    refine âŸ¨Î´, hÎ´â‚€, fun f hf x hx â†¦ ?_âŸ©
    simp only [Seminorm.mem_ball_zero, mem_closedBall_zero_iff] at hf âŠ¢
    rw [mul_comm] at hÎ´
    exact le_trans (le_of_opNorm_le_of_le _ hf.le (hÎµ _ hx)) hÎ´.le

instance toPseudoMetricSpace : PseudoMetricSpace (E â†’SL[Ïƒâ‚â‚‚] F) := .replaceUniformity
  ContinuousLinearMap.seminorm.toSeminormedAddCommGroup.toPseudoMetricSpace uniformity_eq_seminorm

/-- Continuous linear maps themselves form a seminormed space with respect to the operator norm. -/
instance toSeminormedAddCommGroup : SeminormedAddCommGroup (E â†’SL[Ïƒâ‚â‚‚] F) where

instance toNormedSpace {ğ•œ' : Type*} [NormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass ğ•œâ‚‚ ğ•œ' F] :
    NormedSpace ğ•œ' (E â†’SL[Ïƒâ‚â‚‚] F) :=
  âŸ¨opNorm_smul_leâŸ©

/-- The operator norm is submultiplicative. -/
theorem opNorm_comp_le (f : E â†’SL[Ïƒâ‚â‚‚] F) : â€–h.comp fâ€– â‰¤ â€–hâ€– * â€–fâ€– :=
  csInf_le bounds_bddBelow âŸ¨by positivity, fun x => by
    rw [mul_assoc]
    exact h.le_opNorm_of_le (f.le_opNorm x)âŸ©

/-- Continuous linear maps form a seminormed ring with respect to the operator norm. -/
instance toSeminormedRing : SeminormedRing (E â†’L[ğ•œ] E) :=
  { toSeminormedAddCommGroup, ring with norm_mul_le := opNorm_comp_le }

/-- For a normed space `E`, continuous linear endomorphisms form a normed algebra with
respect to the operator norm. -/
instance toNormedAlgebra : NormedAlgebra ğ•œ (E â†’L[ğ•œ] E) := { toNormedSpace, algebra with }

end

variable [RingHomIsometric Ïƒâ‚â‚‚] (f : E â†’SL[Ïƒâ‚â‚‚] F)

@[simp, nontriviality]
theorem opNorm_subsingleton [Subsingleton E] : â€–fâ€– = 0 := by
  refine le_antisymm ?_ (norm_nonneg _)
  apply opNorm_le_bound _ rfl.ge
  intro x
  simp [Subsingleton.elim x 0]

/-- The fundamental property of the operator norm, expressed with extended norms:
`â€–f xâ€–â‚‘ â‰¤ â€–fâ€–â‚‘ * â€–xâ€–â‚‘`. -/
lemma le_opNorm_enorm (x : E) : â€–f xâ€–â‚‘ â‰¤ â€–fâ€–â‚‘ * â€–xâ€–â‚‘ := by
  simp_rw [â† ofReal_norm]
  rw [â† ENNReal.ofReal_mul (by positivity)]
  gcongr
  exact f.le_opNorm x

end OpNorm

section RestrictScalars

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ' ğ•œ]
variable [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ' ğ•œ E]
variable [NormedSpace ğ•œ' Fâ‚—] [IsScalarTower ğ•œ' ğ•œ Fâ‚—]

@[simp]
theorem norm_restrictScalars (f : E â†’L[ğ•œ] Fâ‚—) : â€–f.restrictScalars ğ•œ'â€– = â€–fâ€– :=
  le_antisymm (opNorm_le_bound _ (norm_nonneg _) fun x => f.le_opNorm x)
    (opNorm_le_bound _ (norm_nonneg _) fun x => f.le_opNorm x)

variable (ğ•œ E Fâ‚— ğ•œ') (ğ•œ'' : Type*) [Ring ğ•œ'']
variable [Module ğ•œ'' Fâ‚—] [ContinuousConstSMul ğ•œ'' Fâ‚—]
  [SMulCommClass ğ•œ ğ•œ'' Fâ‚—] [SMulCommClass ğ•œ' ğ•œ'' Fâ‚—]

/-- `ContinuousLinearMap.restrictScalars` as a `LinearIsometry`. -/
def restrictScalarsIsometry : (E â†’L[ğ•œ] Fâ‚—) â†’â‚—áµ¢[ğ•œ''] E â†’L[ğ•œ'] Fâ‚— :=
  âŸ¨restrictScalarsâ‚— ğ•œ E Fâ‚— ğ•œ' ğ•œ'', norm_restrictScalarsâŸ©

variable {ğ•œ''}

@[simp]
theorem coe_restrictScalarsIsometry :
    â‡‘(restrictScalarsIsometry ğ•œ E Fâ‚— ğ•œ' ğ•œ'') = restrictScalars ğ•œ' :=
  rfl

@[simp]
theorem restrictScalarsIsometry_toLinearMap :
    (restrictScalarsIsometry ğ•œ E Fâ‚— ğ•œ' ğ•œ'').toLinearMap = restrictScalarsâ‚— ğ•œ E Fâ‚— ğ•œ' ğ•œ'' :=
  rfl

end RestrictScalars

lemma norm_pi_le_of_le {Î¹ : Type*} [Fintype Î¹]
    {M : Î¹ â†’ Type*} [âˆ€ i, SeminormedAddCommGroup (M i)] [âˆ€ i, NormedSpace ğ•œ (M i)] {C : â„}
    {L : (i : Î¹) â†’ (E â†’L[ğ•œ] M i)} (hL : âˆ€ i, â€–L iâ€– â‰¤ C) (hC : 0 â‰¤ C) :
    â€–pi Lâ€– â‰¤ C := by
  refine opNorm_le_bound _ hC (fun x â†¦ ?_)
  refine (pi_norm_le_iff_of_nonneg (by positivity)).mpr (fun i â†¦ ?_)
  exact (L i).le_of_opNorm_le (hL i) _

end ContinuousLinearMap

namespace LinearMap

/-- If a continuous linear map is constructed from a linear map via the constructor `mkContinuous`,
then its norm is bounded by the bound given to the constructor if it is nonnegative. -/
theorem mkContinuous_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (hC : 0 â‰¤ C) (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    â€–f.mkContinuous C hâ€– â‰¤ C :=
  ContinuousLinearMap.opNorm_le_bound _ hC h

/-- If a continuous linear map is constructed from a linear map via the constructor `mkContinuous`,
then its norm is bounded by the bound or zero if bound is negative. -/
theorem mkContinuous_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) {C : â„} (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    â€–f.mkContinuous C hâ€– â‰¤ max C 0 :=
  ContinuousLinearMap.opNorm_le_bound _ (le_max_right _ _) fun x => (h x).trans <| by
    gcongr; apply le_max_left

end LinearMap

namespace LinearIsometry

theorem norm_toContinuousLinearMap_le (f : E â†’â‚›â‚—áµ¢[Ïƒâ‚â‚‚] F) : â€–f.toContinuousLinearMapâ€– â‰¤ 1 :=
  f.toContinuousLinearMap.opNorm_le_bound zero_le_one fun x => by simp

end LinearIsometry

namespace Submodule

theorem norm_subtypeL_le (K : Submodule ğ•œ E) : â€–K.subtypeLâ€– â‰¤ 1 :=
  K.subtypeâ‚—áµ¢.norm_toContinuousLinearMap_le

end Submodule

end SemiNormed
