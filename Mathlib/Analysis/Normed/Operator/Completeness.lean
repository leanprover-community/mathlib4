/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.Normed.Operator.Bilinear
import Mathlib.Analysis.Normed.Operator.NNNorm

/-!
# Operators on complete normed spaces

This file contains statements about norms of operators on complete normed spaces, such as a
version of the Banach-Alaoglu theorem (`ContinuousLinearMap.isCompact_image_coe_closedBall`).
-/

suppress_compilation

open Bornology Metric Set Real
open Filter hiding map_smul
open scoped NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ E F Fâ‚— : Type*}
variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup Fâ‚—]

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Fâ‚—]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} (f g : E â†’SL[Ïƒâ‚â‚‚] F)

namespace ContinuousLinearMap

section Completeness

variable {E' : Type*} [SeminormedAddCommGroup E'] [NormedSpace ğ•œ E'] [RingHomIsometric Ïƒâ‚â‚‚]

/-- Construct a bundled continuous (semi)linear map from a map `f : E â†’ F` and a proof of the fact
that it belongs to the closure of the image of a bounded set `s : Set (E â†’SL[Ïƒâ‚â‚‚] F)` under coercion
to function. Coercion to function of the result is definitionally equal to `f`. -/
@[simps! -fullyApplied apply]
def ofMemClosureImageCoeBounded (f : E' â†’ F) {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)} (hs : IsBounded s)
    (hf : f âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) : E' â†’SL[Ïƒâ‚â‚‚] F := by
  -- `f` is a linear map due to `linearMapOfMemClosureRangeCoe`
  refine (linearMapOfMemClosureRangeCoe f ?_).mkContinuousOfExistsBound ?_
  Â· refine closure_mono (image_subset_iff.2 fun g _ => ?_) hf
    exact âŸ¨g, rflâŸ©
  Â· -- We need to show that `f` has bounded norm. Choose `C` such that `â€–gâ€– â‰¤ C` for all `g âˆˆ s`.
    rcases isBounded_iff_forall_norm_le.1 hs with âŸ¨C, hCâŸ©
    -- Then `â€–g xâ€– â‰¤ C * â€–xâ€–` for all `g âˆˆ s`, `x : E`, hence `â€–f xâ€– â‰¤ C * â€–xâ€–` for all `x`.
    have : âˆ€ x, IsClosed { g : E' â†’ F | â€–g xâ€– â‰¤ C * â€–xâ€– } := fun x =>
      isClosed_Iic.preimage (@continuous_apply E' (fun _ => F) _ x).norm
    refine âŸ¨C, fun x => (this x).closure_subset_iff.2 (image_subset_iff.2 fun g hg => ?_) hfâŸ©
    exact g.le_of_opNorm_le (hC _ hg) _

/-- Let `f : E â†’ F` be a map, let `g : Î± â†’ E â†’SL[Ïƒâ‚â‚‚] F` be a family of continuous (semi)linear maps
that takes values in a bounded set and converges to `f` pointwise along a nontrivial filter. Then
`f` is a continuous (semi)linear map. -/
@[simps! -fullyApplied apply]
def ofTendstoOfBoundedRange {Î± : Type*} {l : Filter Î±} [l.NeBot] (f : E' â†’ F)
    (g : Î± â†’ E' â†’SL[Ïƒâ‚â‚‚] F) (hf : Tendsto (fun a x => g a x) l (ğ“ f))
    (hg : IsBounded (Set.range g)) : E' â†’SL[Ïƒâ‚â‚‚] F :=
  ofMemClosureImageCoeBounded f hg <| mem_closure_of_tendsto hf <|
    Eventually.of_forall fun _ => mem_image_of_mem _ <| Set.mem_range_self _

/-- If a Cauchy sequence of continuous linear map converges to a continuous linear map pointwise,
then it converges to the same map in norm. This lemma is used to prove that the space of continuous
linear maps is complete provided that the codomain is a complete space. -/
theorem tendsto_of_tendsto_pointwise_of_cauchySeq {f : â„• â†’ E' â†’SL[Ïƒâ‚â‚‚] F} {g : E' â†’SL[Ïƒâ‚â‚‚] F}
    (hg : Tendsto (fun n x => f n x) atTop (ğ“ g)) (hf : CauchySeq f) : Tendsto f atTop (ğ“ g) := by
  /- Since `f` is a Cauchy sequence, there exists `b â†’ 0` such that `â€–f n - f mâ€– â‰¤ b N` for any
    `m, n â‰¥ N`. -/
  rcases cauchySeq_iff_le_tendsto_0.1 hf with âŸ¨b, hbâ‚€, hfb, hb_limâŸ©
  -- Since `b â†’ 0`, it suffices to show that `â€–f n x - g xâ€– â‰¤ b n * â€–xâ€–` for all `n` and `x`.
  suffices âˆ€ n x, â€–f n x - g xâ€– â‰¤ b n * â€–xâ€– from
    tendsto_iff_norm_sub_tendsto_zero.2
    (squeeze_zero (fun n => norm_nonneg _) (fun n => opNorm_le_bound _ (hbâ‚€ n) (this n)) hb_lim)
  intro n x
  -- Note that `f m x â†’ g x`, hence `â€–f n x - f m xâ€– â†’ â€–f n x - g xâ€–` as `m â†’ âˆ`
  have : Tendsto (fun m => â€–f n x - f m xâ€–) atTop (ğ“ â€–f n x - g xâ€–) :=
    (tendsto_const_nhds.sub <| tendsto_pi_nhds.1 hg _).norm
  -- Thus it suffices to verify `â€–f n x - f m xâ€– â‰¤ b n * â€–xâ€–` for `m â‰¥ n`.
  refine le_of_tendsto this (eventually_atTop.2 âŸ¨n, fun m hm => ?_âŸ©)
  -- This inequality follows from `â€–f n - f mâ€– â‰¤ b n`.
  exact (f n - f m).le_of_opNorm_le (hfb _ _ _ le_rfl hm) _

/-- Let `s` be a bounded set in the space of continuous (semi)linear maps `E â†’SL[Ïƒ] F` taking values
in a proper space. Then `s` interpreted as a set in the space of maps `E â†’ F` with topology of
pointwise convergence is precompact: its closure is a compact set. -/
theorem isCompact_closure_image_coe_of_bounded [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) : IsCompact (closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) :=
  have : âˆ€ x, IsCompact (closure (apply' F Ïƒâ‚â‚‚ x '' s)) := fun x =>
    ((apply' F Ïƒâ‚â‚‚ x).lipschitz.isBounded_image hb).isCompact_closure
  (isCompact_pi_infinite this).closure_of_subset
    (image_subset_iff.2 fun _ hg _ => subset_closure <| mem_image_of_mem _ hg)

/-- Let `s` be a bounded set in the space of continuous (semi)linear maps `E â†’SL[Ïƒ] F` taking values
in a proper space. If `s` interpreted as a set in the space of maps `E â†’ F` with topology of
pointwise convergence is closed, then it is compact.

TODO: reformulate this in terms of a type synonym with the right topology. -/
theorem isCompact_image_coe_of_bounded_of_closed_image [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) (hc : IsClosed (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s)) :
    IsCompact (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  hc.closure_eq â–¸ isCompact_closure_image_coe_of_bounded hb

/-- If a set `s` of semilinear functions is bounded and is closed in the weak-* topology, then its
image under coercion to functions `E â†’ F` is a closed set. We don't have a name for `E â†’SL[Ïƒ] F`
with weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`).

TODO: reformulate this in terms of a type synonym with the right topology. -/
theorem isClosed_image_coe_of_bounded_of_weak_closed {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)} (hb : IsBounded s)
    (hc : âˆ€ f : E' â†’SL[Ïƒâ‚â‚‚] F,
      (â‡‘f : E' â†’ F) âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) â†’ f âˆˆ s) :
    IsClosed (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  isClosed_of_closure_subset fun f hf =>
    âŸ¨ofMemClosureImageCoeBounded f hb hf, hc (ofMemClosureImageCoeBounded f hb hf) hf, rflâŸ©

/-- If a set `s` of semilinear functions is bounded and is closed in the weak-* topology, then its
image under coercion to functions `E â†’ F` is a compact set. We don't have a name for `E â†’SL[Ïƒ] F`
with weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`).
-/
theorem isCompact_image_coe_of_bounded_of_weak_closed [ProperSpace F] {s : Set (E' â†’SL[Ïƒâ‚â‚‚] F)}
    (hb : IsBounded s) (hc : âˆ€ f : E' â†’SL[Ïƒâ‚â‚‚] F,
      (â‡‘f : E' â†’ F) âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) â†’ f âˆˆ s) :
    IsCompact (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' s) :=
  isCompact_image_coe_of_bounded_of_closed_image hb <|
    isClosed_image_coe_of_bounded_of_weak_closed hb hc

/-- A closed ball is closed in the weak-* topology. We don't have a name for `E â†’SL[Ïƒ] F` with
weak-* topology in `mathlib`, so we use an equivalent condition (see `isClosed_induced_iff'`). -/
theorem is_weak_closed_closedBall (fâ‚€ : E' â†’SL[Ïƒâ‚â‚‚] F) (r : â„) â¦ƒf : E' â†’SL[Ïƒâ‚â‚‚] Fâ¦„
    (hf : â‡‘f âˆˆ closure (((â†‘) : (E' â†’SL[Ïƒâ‚â‚‚] F) â†’ E' â†’ F) '' closedBall fâ‚€ r)) :
    f âˆˆ closedBall fâ‚€ r := by
  have hr : 0 â‰¤ r := nonempty_closedBall.1 (closure_nonempty_iff.1 âŸ¨_, hfâŸ©).of_image
  refine mem_closedBall_iff_norm.2 (opNorm_le_bound _ hr fun x => ?_)
  have : IsClosed { g : E' â†’ F | â€–g x - fâ‚€ xâ€– â‰¤ r * â€–xâ€– } :=
    isClosed_Iic.preimage ((@continuous_apply E' (fun _ => F) _ x).sub continuous_const).norm
  refine this.closure_subset_iff.2 (image_subset_iff.2 fun g hg => ?_) hf
  exact (g - fâ‚€).le_of_opNorm_le (mem_closedBall_iff_norm.1 hg) _

/-- The set of functions `f : E â†’ F` that represent continuous linear maps `f : E â†’SL[Ïƒâ‚â‚‚] F`
at distance `â‰¤ r` from `fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F` is closed in the topology of pointwise convergence.
This is one of the key steps in the proof of the **Banach-Alaoglu** theorem. -/
theorem isClosed_image_coe_closedBall (fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F) (r : â„) :
    IsClosed (((â†‘) : (E â†’SL[Ïƒâ‚â‚‚] F) â†’ E â†’ F) '' closedBall fâ‚€ r) :=
  isClosed_image_coe_of_bounded_of_weak_closed isBounded_closedBall (is_weak_closed_closedBall fâ‚€ r)

/-- **Banach-Alaoglu** theorem. The set of functions `f : E â†’ F` that represent continuous linear
maps `f : E â†’SL[Ïƒâ‚â‚‚] F` at distance `â‰¤ r` from `fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F` is compact in the topology of
pointwise convergence. Other versions of this theorem can be found in
`Analysis.Normed.Module.WeakDual`. -/
theorem isCompact_image_coe_closedBall [ProperSpace F] (fâ‚€ : E â†’SL[Ïƒâ‚â‚‚] F) (r : â„) :
    IsCompact (((â†‘) : (E â†’SL[Ïƒâ‚â‚‚] F) â†’ E â†’ F) '' closedBall fâ‚€ r) :=
  isCompact_image_coe_of_bounded_of_weak_closed isBounded_closedBall <|
    is_weak_closed_closedBall fâ‚€ r

end Completeness

section UniformlyExtend

section NonField

variable {R Râ‚‚ E F Fâ‚— : Type*} [AddCommGroup E] [UniformSpace E] [IsUniformAddGroup E]
  [AddCommGroup F] [UniformSpace F] [IsUniformAddGroup F] [T0Space F]
  [AddCommMonoid Fâ‚—] [UniformSpace Fâ‚—] [ContinuousAdd Fâ‚—]
  [Semiring R] [Semiring Râ‚‚] [Module R E] [Module Râ‚‚ F] [Module R Fâ‚—]
  [ContinuousConstSMul R Fâ‚—] [ContinuousConstSMul Râ‚‚ F]
  {Ïƒâ‚â‚‚ : R â†’+* Râ‚‚} (f g : E â†’SL[Ïƒâ‚â‚‚] F) [CompleteSpace F] (e : E â†’L[R] Fâ‚—) (h_dense : DenseRange e)

variable (h_e : IsUniformInducing e)

/-- Extension of a continuous linear map `f : E â†’SL[Ïƒâ‚â‚‚] F`, with `E` a normed space and `F` a
complete normed space, along a uniform and dense embedding `e : E â†’L[ğ•œ] Fâ‚—`. -/
def extend : Fâ‚— â†’SL[Ïƒâ‚â‚‚] F :=
  -- extension of `f` is continuous
  have cont := (uniformContinuous_uniformly_extend h_e h_dense f.uniformContinuous).continuous
  -- extension of `f` agrees with `f` on the domain of the embedding `e`
  have eq := uniformly_extend_of_ind h_e h_dense f.uniformContinuous
  { toFun := (h_e.isDenseInducing h_dense).extend f
    map_add' := by
      refine h_dense.induction_onâ‚‚ ?_ ?_
      Â· exact isClosed_eq (cont.comp continuous_add)
          ((cont.comp continuous_fst).add (cont.comp continuous_snd))
      Â· intro x y
        simp only [eq, â† e.map_add]
        exact f.map_add _ _
    map_smul' := fun k => by
      refine fun b => h_dense.induction_on b ?_ ?_
      Â· exact isClosed_eq (cont.comp (continuous_const_smul _))
          ((continuous_const_smul _).comp cont)
      Â· intro x
        rw [â† map_smul]
        simp only [eq]
        exact ContinuousLinearMap.map_smulâ‚›â‚— _ _ _
    cont }

@[simp]
theorem extend_eq (x : E) : extend f e h_dense h_e (e x) = f x :=
  IsDenseInducing.extend_eq (h_e.isDenseInducing h_dense) f.cont _

theorem extend_unique (g : Fâ‚— â†’SL[Ïƒâ‚â‚‚] F) (H : g.comp e = f) : extend f e h_dense h_e = g :=
  ContinuousLinearMap.coeFn_injective <|
    uniformly_extend_unique h_e h_dense (ContinuousLinearMap.ext_iff.1 H) g.continuous

@[simp]
theorem extend_zero : extend (0 : E â†’SL[Ïƒâ‚â‚‚] F) e h_dense h_e = 0 :=
  extend_unique _ _ _ _ _ (zero_comp _)

end NonField

section

variable [CompleteSpace F] (e : E â†’L[ğ•œ] Fâ‚—) (h_dense : DenseRange e)

variable {N : â„â‰¥0} (h_e : âˆ€ x, â€–xâ€– â‰¤ N * â€–e xâ€–) [RingHomIsometric Ïƒâ‚â‚‚]

/-- If a dense embedding `e : E â†’L[ğ•œ] G` expands the norm by a constant factor `Nâ»Â¹`, then the
norm of the extension of `f` along `e` is bounded by `N * â€–fâ€–`. -/
theorem opNorm_extend_le :
    â€–f.extend e h_dense (isUniformEmbedding_of_bound _ h_e).isUniformInducingâ€– â‰¤ N * â€–fâ€– := by
  -- Add `opNorm_le_of_dense`?
  refine opNorm_le_bound _ ?_ (isClosed_property h_dense (isClosed_le ?_ ?_) fun x â†¦ ?_)
  Â· cases le_total 0 N with
    | inl hN => exact mul_nonneg hN (norm_nonneg _)
    | inr hN =>
      have : Unique E := âŸ¨âŸ¨0âŸ©, fun x â†¦ norm_le_zero_iff.mp <|
        (h_e x).trans (mul_nonpos_of_nonpos_of_nonneg hN (norm_nonneg _))âŸ©
      obtain rfl : f = 0 := Subsingleton.elim ..
      simp
  Â· exact (cont _).norm
  Â· exact continuous_const.mul continuous_norm
  Â· rw [extend_eq]
    calc
      â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_opNorm _ _
      _ â‰¤ â€–fâ€– * (N * â€–e xâ€–) := mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _)
      _ â‰¤ N * â€–fâ€– * â€–e xâ€– := by rw [mul_comm â†‘N â€–fâ€–, mul_assoc]


end

end UniformlyExtend

end ContinuousLinearMap
