/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.Normed.Group.Uniform
import Mathlib.Analysis.Normed.MulAction
import Mathlib.LinearAlgebra.DFinsupp
import Mathlib.Topology.Algebra.Module.Equiv

/-! # Constructions of continuous linear maps between (semi-)normed spaces

A fundamental fact about (semi-)linear maps between normed spaces over sensible fields is that
continuity and boundedness are equivalent conditions.  That is, for normed spaces `E`, `F`, a
`LinearMap` `f : E â†’â‚›â‚—[Ïƒ] F` is the coercion of some `ContinuousLinearMap` `f' : E â†’SL[Ïƒ] F`, if
and only if there exists a bound `C` such that for all `x`, `â€–f xâ€– â‰¤ C * â€–xâ€–`.

We prove one direction in this file: `LinearMap.mkContinuous`, boundedness implies continuity. The
other direction, `ContinuousLinearMap.bound`, is deferred to a later file, where the
strong operator topology on `E â†’SL[Ïƒ] F` is available, because it is natural to use
`ContinuousLinearMap.bound` to define a norm `â¨† x, â€–f xâ€– / â€–xâ€–` on `E â†’SL[Ïƒ] F` and to show that
this is compatible with the strong operator topology.

This file also contains several corollaries of `LinearMap.mkContinuous`: other "easy"
constructions of continuous linear maps between normed spaces.

This file is meant to be lightweight (it is imported by much of the analysis library); think twice
before adding imports!
-/

open Metric ContinuousLinearMap

open Set Real

open NNReal

variable {ğ•œ ğ•œâ‚‚ E F G : Type*}

/-! # General constructions -/

section SeminormedAddCommGroup

variable [Ring ğ•œ] [Ring ğ•œâ‚‚]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F] [SeminormedAddCommGroup G]
variable [Module ğ•œ E] [Module ğ•œâ‚‚ F] [Module ğ•œ G]
variable {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f : E â†’â‚›â‚—[Ïƒ] F)

/-- Construct a continuous linear map from a linear map and a bound on this linear map.
The fact that the norm of the continuous linear map is then controlled is given in
`LinearMap.mkContinuous_norm_le`. -/
def LinearMap.mkContinuous (C : â„) (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) : E â†’SL[Ïƒ] F :=
  âŸ¨f, AddMonoidHomClass.continuous_of_bound f C hâŸ©

/-- Construct a continuous linear map from a linear map and the existence of a bound on this linear
map. If you have an explicit bound, use `LinearMap.mkContinuous` instead, as a norm estimate will
follow automatically in `LinearMap.mkContinuous_norm_le`. -/
def LinearMap.mkContinuousOfExistsBound (h : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) : E â†’SL[Ïƒ] F :=
  âŸ¨f,
    let âŸ¨C, hCâŸ© := h
    AddMonoidHomClass.continuous_of_bound f C hCâŸ©

theorem continuous_of_linear_of_boundâ‚›â‚— {f : E â†’ F} (h_add : âˆ€ x y, f (x + y) = f x + f y)
    (h_smul : âˆ€ (c : ğ•œ) (x), f (c â€¢ x) = Ïƒ c â€¢ f x) {C : â„} (h_bound : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    Continuous f :=
  let Ï† : E â†’â‚›â‚—[Ïƒ] F :=
    { toFun := f
      map_add' := h_add
      map_smul' := h_smul }
  AddMonoidHomClass.continuous_of_bound Ï† C h_bound

theorem continuous_of_linear_of_bound {f : E â†’ G} (h_add : âˆ€ x y, f (x + y) = f x + f y)
    (h_smul : âˆ€ (c : ğ•œ) (x), f (c â€¢ x) = c â€¢ f x) {C : â„} (h_bound : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    Continuous f :=
  let Ï† : E â†’â‚—[ğ•œ] G :=
    { toFun := f
      map_add' := h_add
      map_smul' := h_smul }
  AddMonoidHomClass.continuous_of_bound Ï† C h_bound

@[simp, norm_cast]
theorem LinearMap.mkContinuous_coe (C : â„) (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    (f.mkContinuous C h : E â†’â‚›â‚—[Ïƒ] F) = f :=
  rfl

@[simp]
theorem LinearMap.mkContinuous_apply (C : â„) (h : âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
    f.mkContinuous C h x = f x :=
  rfl

@[simp, norm_cast]
theorem LinearMap.mkContinuousOfExistsBound_coe (h : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) :
    (f.mkContinuousOfExistsBound h : E â†’â‚›â‚—[Ïƒ] F) = f :=
  rfl

@[simp]
theorem LinearMap.mkContinuousOfExistsBound_apply (h : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–xâ€–) (x : E) :
    f.mkContinuousOfExistsBound h x = f x :=
  rfl

namespace ContinuousLinearMap

theorem antilipschitz_of_bound (f : E â†’SL[Ïƒ] F) {K : â„â‰¥0} (h : âˆ€ x, â€–xâ€– â‰¤ K * â€–f xâ€–) :
    AntilipschitzWith K f :=
  AddMonoidHomClass.antilipschitz_of_bound _ h

theorem bound_of_antilipschitz (f : E â†’SL[Ïƒ] F) {K : â„â‰¥0} (h : AntilipschitzWith K f) (x) :
    â€–xâ€– â‰¤ K * â€–f xâ€– :=
  ZeroHomClass.bound_of_antilipschitz _ h x

end ContinuousLinearMap

section

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒ]

/-- Construct a continuous linear equivalence from a linear equivalence together with
bounds in both directions. -/
def LinearEquiv.toContinuousLinearEquivOfBounds (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (C_to C_inv : â„)
    (h_to : âˆ€ x, â€–e xâ€– â‰¤ C_to * â€–xâ€–) (h_inv : âˆ€ x : F, â€–e.symm xâ€– â‰¤ C_inv * â€–xâ€–) : E â‰ƒSL[Ïƒ] F where
  toLinearEquiv := e
  continuous_toFun := AddMonoidHomClass.continuous_of_bound e C_to h_to
  continuous_invFun := AddMonoidHomClass.continuous_of_bound e.symm C_inv h_inv

end

end SeminormedAddCommGroup

section SeminormedBounded
variable [SeminormedRing ğ•œ] [Ring ğ•œâ‚‚] [SeminormedAddCommGroup E]
variable [Module ğ•œ E] [IsBoundedSMul ğ•œ E]

/-- Reinterpret a linear map `ğ•œ â†’â‚—[ğ•œ] E` as a continuous linear map. This construction
is generalized to the case of any finite dimensional domain
in `LinearMap.toContinuousLinearMap`. -/
def LinearMap.toContinuousLinearMapâ‚ (f : ğ•œ â†’â‚—[ğ•œ] E) : ğ•œ â†’L[ğ•œ] E :=
  f.mkContinuous â€–f 1â€– fun x => by
    conv_lhs => rw [â† mul_one x]
    rw [â† smul_eq_mul, f.map_smul, mul_comm]; exact norm_smul_le _ _

@[simp]
theorem LinearMap.toContinuousLinearMapâ‚_coe (f : ğ•œ â†’â‚—[ğ•œ] E) :
    (f.toContinuousLinearMapâ‚ : ğ•œ â†’â‚—[ğ•œ] E) = f :=
  rfl

@[simp]
theorem LinearMap.toContinuousLinearMapâ‚_apply (f : ğ•œ â†’â‚—[ğ•œ] E) (x) :
    f.toContinuousLinearMapâ‚ x = f x :=
  rfl

end SeminormedBounded

section Normed
variable [Ring ğ•œ] [Ring ğ•œâ‚‚]
variable [NormedAddCommGroup E] [NormedAddCommGroup F] [Module ğ•œ E] [Module ğ•œâ‚‚ F]
variable {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f g : E â†’SL[Ïƒ] F) (x y z : E)

theorem ContinuousLinearMap.isUniformEmbedding_of_bound {K : â„â‰¥0} (hf : âˆ€ x, â€–xâ€– â‰¤ K * â€–f xâ€–) :
    IsUniformEmbedding f :=
  (AddMonoidHomClass.antilipschitz_of_bound f hf).isUniformEmbedding f.uniformContinuous

end Normed

/-! ## Homotheties -/

section Seminormed
variable [Ring ğ•œ] [Ring ğ•œâ‚‚]
variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup F]
variable [Module ğ•œ E] [Module ğ•œâ‚‚ F]
variable {Ïƒ : ğ•œ â†’+* ğ•œâ‚‚} (f : E â†’â‚›â‚—[Ïƒ] F)

/-- A (semi-)linear map which is a homothety is a continuous linear map.
    Since the field `ğ•œ` need not have `â„` as a subfield, this theorem is not directly deducible from
    the corresponding theorem about isometries plus a theorem about scalar multiplication.  Likewise
    for the other theorems about homotheties in this file.
-/
def ContinuousLinearMap.ofHomothety (f : E â†’â‚›â‚—[Ïƒ] F) (a : â„) (hf : âˆ€ x, â€–f xâ€– = a * â€–xâ€–) :
    E â†’SL[Ïƒ] F :=
  f.mkContinuous a fun x => le_of_eq (hf x)

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒ]

theorem ContinuousLinearEquiv.homothety_inverse (a : â„) (ha : 0 < a) (f : E â‰ƒâ‚›â‚—[Ïƒ] F) :
    (âˆ€ x : E, â€–f xâ€– = a * â€–xâ€–) â†’ âˆ€ y : F, â€–f.symm yâ€– = aâ»Â¹ * â€–yâ€– := by
  intro hf y
  calc
    â€–f.symm yâ€– = aâ»Â¹ * (a * â€–f.symm yâ€–) := by
      rw [â† mul_assoc, inv_mul_cancelâ‚€ (ne_of_lt ha).symm, one_mul]
    _ = aâ»Â¹ * â€–f (f.symm y)â€– := by rw [hf]
    _ = aâ»Â¹ * â€–yâ€– := by simp

/-- A linear equivalence which is a homothety is a continuous linear equivalence. -/
noncomputable def ContinuousLinearEquiv.ofHomothety (f : E â‰ƒâ‚›â‚—[Ïƒ] F) (a : â„) (ha : 0 < a)
    (hf : âˆ€ x, â€–f xâ€– = a * â€–xâ€–) : E â‰ƒSL[Ïƒ] F :=
  LinearEquiv.toContinuousLinearEquivOfBounds f a aâ»Â¹ (fun x => (hf x).le) fun x =>
    (ContinuousLinearEquiv.homothety_inverse a ha f hf x).le

end Seminormed
