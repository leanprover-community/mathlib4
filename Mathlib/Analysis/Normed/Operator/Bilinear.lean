/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Analysis.Normed.Operator.Basic
import Mathlib.Analysis.Normed.Operator.LinearIsometry
import Mathlib.Analysis.Normed.Operator.ContinuousLinearMap

/-!
# Operator norm: bilinear maps

This file contains lemmas concerning operator norm as applied to bilinear maps `E Ã— F â†’ G`,
interpreted as linear maps `E â†’ F â†’ G` as usual (and similarly for semilinear variants).

-/

suppress_compilation

open Bornology
open Filter hiding map_smul
open scoped NNReal Topology Uniformity

-- the `â‚—` subscript variables are for special cases about linear (as opposed to semilinear) maps
variable {ğ•œ ğ•œâ‚‚ ğ•œâ‚ƒ E Eâ‚— F Fâ‚— G Gâ‚— ğ“• : Type*}

section SemiNormed

open Metric ContinuousLinearMap

variable [SeminormedAddCommGroup E] [SeminormedAddCommGroup Eâ‚—] [SeminormedAddCommGroup F]
  [SeminormedAddCommGroup Fâ‚—] [SeminormedAddCommGroup G] [SeminormedAddCommGroup Gâ‚—]

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] [NontriviallyNormedField ğ•œâ‚ƒ]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ Eâ‚—] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Fâ‚—] [NormedSpace ğ•œâ‚ƒ G]
  [NormedSpace ğ•œ Gâ‚—] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} {Ïƒâ‚‚â‚ƒ : ğ•œâ‚‚ â†’+* ğ•œâ‚ƒ} {Ïƒâ‚â‚ƒ : ğ•œ â†’+* ğ•œâ‚ƒ}
  [RingHomCompTriple Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ]

variable [FunLike ğ“• E F]

namespace ContinuousLinearMap

section OpNorm

open Set Real

theorem opNorm_ext [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚‚] F) (g : E â†’SL[Ïƒâ‚â‚ƒ] G)
    (h : âˆ€ x, â€–f xâ€– = â€–g xâ€–) : â€–fâ€– = â€–gâ€– :=
  opNorm_eq_of_bounds (norm_nonneg _)
    (fun x => by
      rw [h x]
      exact le_opNorm _ _)
    fun c hc hâ‚‚ =>
    opNorm_le_bound _ hc fun z => by
      rw [â† h z]
      exact hâ‚‚ z


variable [RingHomIsometric Ïƒâ‚‚â‚ƒ]

theorem opNorm_le_boundâ‚‚ (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) {C : â„} (h0 : 0 â‰¤ C)
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–fâ€– â‰¤ C :=
  f.opNorm_le_bound h0 fun x => (f x).opNorm_le_bound (by positivity) <| hC x


theorem le_opNormâ‚‚ [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x : E) (y : F) :
    â€–f x yâ€– â‰¤ â€–fâ€– * â€–xâ€– * â€–yâ€– :=
  (f x).le_of_opNorm_le (f.le_opNorm x) y


theorem le_of_opNormâ‚‚_le_of_le [RingHomIsometric Ïƒâ‚â‚ƒ] (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) {x : E} {y : F}
    {a b c : â„} (hf : â€–fâ€– â‰¤ a) (hx : â€–xâ€– â‰¤ b) (hy : â€–yâ€– â‰¤ c) :
    â€–f x yâ€– â‰¤ a * b * c :=
  (f x).le_of_opNorm_le_of_le (f.le_of_opNorm_le_of_le hf hx) hy


end OpNorm

end ContinuousLinearMap

namespace LinearMap

lemma norm_mkContinuousâ‚‚_aux (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) (C : â„)
    (h : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) (x : E) :
    â€–(f x).mkContinuous (C * â€–xâ€–) (h x)â€– â‰¤ max C 0 * â€–xâ€– :=
  (mkContinuous_norm_le' (f x) (h x)).trans_eq <| by
    rw [max_mul_of_nonneg _ _ (norm_nonneg x), zero_mul]

variable [RingHomIsometric Ïƒâ‚‚â‚ƒ]

/-- Create a bilinear map (represented as a map `E â†’L[ğ•œ] F â†’L[ğ•œ] G`) from the corresponding linear
map and existence of a bound on the norm of the image. The linear map can be constructed using
`LinearMap.mkâ‚‚`.

If you have an explicit bound, use `LinearMap.mkContinuousâ‚‚` instead, as a norm estimate will
follow automatically in `LinearMap.mkContinuousâ‚‚_norm_le`. -/
def mkContinuousOfExistsBoundâ‚‚ (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G)
    (h : âˆƒ C, âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
  LinearMap.mkContinuousOfExistsBound
    { toFun := fun x => (f x).mkContinuousOfExistsBound <| let âŸ¨C, hCâŸ© := h; âŸ¨C * â€–xâ€–, hC xâŸ©
      map_add' := fun x y => by
        ext z
        simp
      map_smul' := fun c x => by
        ext z
        simp } <|
    let âŸ¨C, hCâŸ© := h; âŸ¨max C 0, norm_mkContinuousâ‚‚_aux f C hCâŸ©

/-- Create a bilinear map (represented as a map `E â†’L[ğ•œ] F â†’L[ğ•œ] G`) from the corresponding linear
map and a bound on the norm of the image. The linear map can be constructed using
`LinearMap.mkâ‚‚`. Lemmas `LinearMap.mkContinuousâ‚‚_norm_le'` and `LinearMap.mkContinuousâ‚‚_norm_le`
provide estimates on the norm of an operator constructed using this function. -/
def mkContinuousâ‚‚ (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) (C : â„) (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) :
    E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G :=
  mkContinuousOfExistsBoundâ‚‚ f âŸ¨C, hCâŸ©

@[simp]
theorem mkContinuousâ‚‚_apply (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) (x : E) (y : F) : f.mkContinuousâ‚‚ C hC x y = f x y :=
  rfl

theorem mkContinuousâ‚‚_norm_le' (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„}
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–f.mkContinuousâ‚‚ C hCâ€– â‰¤ max C 0 :=
  mkContinuous_norm_le _ (le_max_iff.2 <| Or.inr le_rfl) (norm_mkContinuousâ‚‚_aux f C hC)

theorem mkContinuousâ‚‚_norm_le (f : E â†’â‚›â‚—[Ïƒâ‚â‚ƒ] F â†’â‚›â‚—[Ïƒâ‚‚â‚ƒ] G) {C : â„} (h0 : 0 â‰¤ C)
    (hC : âˆ€ x y, â€–f x yâ€– â‰¤ C * â€–xâ€– * â€–yâ€–) : â€–f.mkContinuousâ‚‚ C hCâ€– â‰¤ C :=
  (f.mkContinuousâ‚‚_norm_le' hC).trans_eq <| max_eq_left h0

end LinearMap

namespace ContinuousLinearMap

variable [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚ƒ]

/-- Flip the order of arguments of a continuous bilinear map.
For a version bundled as `LinearIsometryEquiv`, see
`ContinuousLinearMap.flipL`. -/
def flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : F â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G :=
  LinearMap.mkContinuousâ‚‚
    (LinearMap.mkâ‚‚'â‚›â‚— Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ (fun y x => f x y) (fun x y z => (f z).map_add x y)
      (fun c y x => (f x).map_smulâ‚›â‚— c y) (fun z x y => by simp only [f.map_add, add_apply])
        (fun c y x => by simp only [f.map_smulâ‚›â‚—, smul_apply]))
    â€–fâ€– fun y x => (f.le_opNormâ‚‚ x y).trans_eq <| by simp only [mul_right_comm]

private theorem le_norm_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : â€–fâ€– â‰¤ â€–flip fâ€– :=
  f.opNorm_le_boundâ‚‚ (norm_nonneg f.flip) fun x y => by
    rw [mul_right_comm]
    exact (flip f).le_opNormâ‚‚ y x

@[simp]
theorem flip_apply (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (x : E) (y : F) : f.flip y x = f x y :=
  rfl

@[simp]
theorem flip_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : f.flip.flip = f := by
  ext
  rfl

@[simp]
theorem opNorm_flip (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : â€–f.flipâ€– = â€–fâ€– :=
  le_antisymm (by simpa only [flip_flip] using le_norm_flip f.flip) (le_norm_flip f)

@[simp]
lemma flip_zero : flip (0 : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) = 0 := rfl

@[simp]
theorem flip_add (f g : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : (f + g).flip = f.flip + g.flip :=
  rfl

@[simp]
theorem flip_smul (c : ğ•œâ‚ƒ) (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) : (c â€¢ f).flip = c â€¢ f.flip :=
  rfl

variable (E F G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ)

/-- Flip the order of arguments of a continuous bilinear map.
This is a version bundled as a `LinearIsometryEquiv`.
For an unbundled version see `ContinuousLinearMap.flip`. -/
def flipâ‚—áµ¢' : (E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) â‰ƒâ‚—áµ¢[ğ•œâ‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G where
  toFun := flip
  invFun := flip
  map_add' := flip_add
  map_smul' := flip_smul
  left_inv := flip_flip
  right_inv := flip_flip
  norm_map' := opNorm_flip

variable {E F G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ}

@[simp]
theorem flipâ‚—áµ¢'_symm : (flipâ‚—áµ¢' E F G Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ).symm = flipâ‚—áµ¢' F E G Ïƒâ‚â‚ƒ Ïƒâ‚‚â‚ƒ :=
  rfl

@[simp]
theorem coe_flipâ‚—áµ¢' : â‡‘(flipâ‚—áµ¢' E F G Ïƒâ‚‚â‚ƒ Ïƒâ‚â‚ƒ) = flip :=
  rfl

variable (ğ•œ E Fâ‚— Gâ‚—)

/-- Flip the order of arguments of a continuous bilinear map.
This is a version bundled as a `LinearIsometryEquiv`.
For an unbundled version see `ContinuousLinearMap.flip`. -/
def flipâ‚—áµ¢ : (E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) â‰ƒâ‚—áµ¢[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚— where
  toFun := flip
  invFun := flip
  map_add' := flip_add
  map_smul' := flip_smul
  left_inv := flip_flip
  right_inv := flip_flip
  norm_map' := opNorm_flip

variable {ğ•œ E Fâ‚— Gâ‚—}

@[simp]
theorem flipâ‚—áµ¢_symm : (flipâ‚—áµ¢ ğ•œ E Fâ‚— Gâ‚—).symm = flipâ‚—áµ¢ ğ•œ Fâ‚— E Gâ‚— :=
  rfl

@[simp]
theorem coe_flipâ‚—áµ¢ : â‡‘(flipâ‚—áµ¢ ğ•œ E Fâ‚— Gâ‚—) = flip :=
  rfl

variable (F Ïƒâ‚â‚‚)
variable [RingHomIsometric Ïƒâ‚â‚‚]

/-- The continuous semilinear map obtained by applying a continuous semilinear map at a given
vector.

This is the continuous version of `LinearMap.applyâ‚—`. -/
def apply' : E â†’SL[Ïƒâ‚â‚‚] (E â†’SL[Ïƒâ‚â‚‚] F) â†’L[ğ•œâ‚‚] F :=
  flip (.id ğ•œâ‚‚ (E â†’SL[Ïƒâ‚â‚‚] F))

variable {F Ïƒâ‚â‚‚}

@[simp]
theorem apply_apply' (v : E) (f : E â†’SL[Ïƒâ‚â‚‚] F) : apply' F Ïƒâ‚â‚‚ v f = f v :=
  rfl

variable (ğ•œ Fâ‚—)

/-- The continuous semilinear map obtained by applying a continuous semilinear map at a given
vector.

This is the continuous version of `LinearMap.applyâ‚—`. -/
def apply : E â†’L[ğ•œ] (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Fâ‚— :=
  flip (.id ğ•œ (E â†’L[ğ•œ] Fâ‚—))

variable {ğ•œ Fâ‚—}

@[simp]
theorem apply_apply (v : E) (f : E â†’L[ğ•œ] Fâ‚—) : apply ğ•œ Fâ‚— v f = f v :=
  rfl

variable (Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E F G)


/-- Composition of continuous semilinear maps as a continuous semibilinear map. -/
def compSL : (F â†’SL[Ïƒâ‚‚â‚ƒ] G) â†’L[ğ•œâ‚ƒ] (E â†’SL[Ïƒâ‚â‚‚] F) â†’SL[Ïƒâ‚‚â‚ƒ] E â†’SL[Ïƒâ‚â‚ƒ] G :=
  LinearMap.mkContinuousâ‚‚
    (LinearMap.mkâ‚‚'â‚›â‚— (RingHom.id ğ•œâ‚ƒ) Ïƒâ‚‚â‚ƒ comp add_comp smul_comp comp_add fun c f g => by
      ext
      simp only [ContinuousLinearMap.map_smulâ‚›â‚—, coe_smul', coe_comp', Function.comp_apply,
        Pi.smul_apply])
    1 fun f g => by simpa only [one_mul] using opNorm_comp_le f g

theorem norm_compSL_le : â€–compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒâ€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _

variable {Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E F G}

@[simp]
theorem compSL_apply (f : F â†’SL[Ïƒâ‚‚â‚ƒ] G) (g : E â†’SL[Ïƒâ‚â‚‚] F) : compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ f g = f.comp g :=
  rfl

theorem _root_.Continuous.const_clm_comp {X} [TopologicalSpace X] {f : X â†’ E â†’SL[Ïƒâ‚â‚‚] F}
    (hf : Continuous f) (g : F â†’SL[Ïƒâ‚‚â‚ƒ] G) :
    Continuous (fun x => g.comp (f x) : X â†’ E â†’SL[Ïƒâ‚â‚ƒ] G) :=
  (compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ g).continuous.comp hf

-- Giving the implicit argument speeds up elaboration significantly
theorem _root_.Continuous.clm_comp_const {X} [TopologicalSpace X] {g : X â†’ F â†’SL[Ïƒâ‚‚â‚ƒ] G}
    (hg : Continuous g) (f : E â†’SL[Ïƒâ‚â‚‚] F) :
    Continuous (fun x => (g x).comp f : X â†’ E â†’SL[Ïƒâ‚â‚ƒ] G) :=
  (@ContinuousLinearMap.flip _ _ _ _ _ (E â†’SL[Ïƒâ‚â‚ƒ] G) _ _ _ _ _ _ _ _ _ _ _ _ _
    (compSL E F G Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ) f).continuous.comp hg

variable (ğ•œ Ïƒâ‚â‚‚ Ïƒâ‚‚â‚ƒ E Fâ‚— Gâ‚—)

/-- Composition of continuous linear maps as a continuous bilinear map. -/
def compL : (Fâ‚— â†’L[ğ•œ] Gâ‚—) â†’L[ğ•œ] (E â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] E â†’L[ğ•œ] Gâ‚— :=
  compSL E Fâ‚— Gâ‚— (RingHom.id ğ•œ) (RingHom.id ğ•œ)

theorem norm_compL_le : â€–compL ğ•œ E Fâ‚— Gâ‚—â€– â‰¤ 1 :=
  norm_compSL_le _ _ _ _ _

@[simp]
theorem compL_apply (f : Fâ‚— â†’L[ğ•œ] Gâ‚—) (g : E â†’L[ğ•œ] Fâ‚—) : compL ğ•œ E Fâ‚— Gâ‚— f g = f.comp g :=
  rfl

variable (Eâ‚—) {ğ•œ E Fâ‚— Gâ‚—}

/-- Apply `L(x,-)` pointwise to bilinear maps, as a continuous bilinear map -/
@[simps! apply]
def precompR (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : E â†’L[ğ•œ] (Eâ‚— â†’L[ğ•œ] Fâ‚—) â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚— :=
  compL ğ•œ Eâ‚— Fâ‚— Gâ‚— âˆ˜L L

/-- Apply `L(-,y)` pointwise to bilinear maps, as a continuous bilinear map -/
def precompL (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : (Eâ‚— â†’L[ğ•œ] E) â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Eâ‚— â†’L[ğ•œ] Gâ‚— :=
  (precompR Eâ‚— (flip L)).flip

@[simp] lemma precompL_apply (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) (u : Eâ‚— â†’L[ğ•œ] E) (f : Fâ‚—) (g : Eâ‚—) :
    precompL Eâ‚— L u f g = L (u g) f := rfl

theorem norm_precompR_le (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : â€–precompR Eâ‚— Lâ€– â‰¤ â€–Lâ€– :=
  calc
    â€–precompR Eâ‚— Lâ€– â‰¤ â€–compL ğ•œ Eâ‚— Fâ‚— Gâ‚—â€– * â€–Lâ€– := opNorm_comp_le _ _
    _ â‰¤ 1 * â€–Lâ€– := by gcongr; apply norm_compL_le
    _ = â€–Lâ€– := by rw [one_mul]

theorem norm_precompL_le (L : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : â€–precompL Eâ‚— Lâ€– â‰¤ â€–Lâ€– := by
  rw [precompL, opNorm_flip, â† opNorm_flip L]
  exact norm_precompR_le _ L.flip

end ContinuousLinearMap

variable {Ïƒâ‚‚â‚ : ğ•œâ‚‚ â†’+* ğ•œ} [RingHomInvPair Ïƒâ‚â‚‚ Ïƒâ‚‚â‚] [RingHomInvPair Ïƒâ‚‚â‚ Ïƒâ‚â‚‚]

namespace ContinuousLinearMap

variable {E' F' : Type*} [SeminormedAddCommGroup E'] [SeminormedAddCommGroup F']
variable {ğ•œâ‚' : Type*} {ğ•œâ‚‚' : Type*} [NontriviallyNormedField ğ•œâ‚'] [NontriviallyNormedField ğ•œâ‚‚']
  [NormedSpace ğ•œâ‚' E'] [NormedSpace ğ•œâ‚‚' F'] {Ïƒâ‚' : ğ•œâ‚' â†’+* ğ•œ} {Ïƒâ‚â‚ƒ' : ğ•œâ‚' â†’+* ğ•œâ‚ƒ} {Ïƒâ‚‚' : ğ•œâ‚‚' â†’+* ğ•œâ‚‚}
  {Ïƒâ‚‚â‚ƒ' : ğ•œâ‚‚' â†’+* ğ•œâ‚ƒ} [RingHomCompTriple Ïƒâ‚' Ïƒâ‚â‚ƒ Ïƒâ‚â‚ƒ'] [RingHomCompTriple Ïƒâ‚‚' Ïƒâ‚‚â‚ƒ Ïƒâ‚‚â‚ƒ']
  [RingHomIsometric Ïƒâ‚‚â‚ƒ] [RingHomIsometric Ïƒâ‚â‚ƒ'] [RingHomIsometric Ïƒâ‚‚â‚ƒ']

/-- Compose a bilinear map `E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G` with two linear maps
`E' â†’SL[Ïƒâ‚'] E` and `F' â†’SL[Ïƒâ‚‚'] F`. -/
def bilinearComp (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (gE : E' â†’SL[Ïƒâ‚'] E) (gF : F' â†’SL[Ïƒâ‚‚'] F) :
    E' â†’SL[Ïƒâ‚â‚ƒ'] F' â†’SL[Ïƒâ‚‚â‚ƒ'] G :=
  ((f.comp gE).flip.comp gF).flip

@[simp]
theorem bilinearComp_apply (f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) (gE : E' â†’SL[Ïƒâ‚'] E) (gF : F' â†’SL[Ïƒâ‚‚'] F)
    (x : E') (y : F') : f.bilinearComp gE gF x y = f (gE x) (gF y) :=
  rfl

@[simp]
lemma bilinearComp_zero {gE : E' â†’SL[Ïƒâ‚'] E} {gF : F' â†’SL[Ïƒâ‚‚'] F} :
    bilinearComp (0 : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G) gE gF = 0 := rfl

@[simp]
lemma bilinearComp_zero_left {f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G} {gF : F' â†’SL[Ïƒâ‚‚'] F} :
    bilinearComp f (0 : E' â†’SL[Ïƒâ‚'] E) gF = 0 := by ext; simp

@[simp]
lemma bilinearComp_zero_right {f : E â†’SL[Ïƒâ‚â‚ƒ] F â†’SL[Ïƒâ‚‚â‚ƒ] G} {gE : E' â†’SL[Ïƒâ‚'] E} :
    bilinearComp f gE (0 : F' â†’SL[Ïƒâ‚‚'] F) = 0 := by ext; simp

variable [RingHomIsometric Ïƒâ‚â‚ƒ] [RingHomIsometric Ïƒâ‚'] [RingHomIsometric Ïƒâ‚‚']

/-- Derivative of a continuous bilinear map `f : E â†’L[ğ•œ] F â†’L[ğ•œ] G` interpreted as a map `E Ã— F â†’ G`
at point `p : E Ã— F` evaluated at `q : E Ã— F`, as a continuous bilinear map. -/
def derivâ‚‚ (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) : E Ã— Fâ‚— â†’L[ğ•œ] E Ã— Fâ‚— â†’L[ğ•œ] Gâ‚— :=
  f.bilinearComp (fst _ _ _) (snd _ _ _) + f.flip.bilinearComp (snd _ _ _) (fst _ _ _)

@[simp]
theorem coe_derivâ‚‚ (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) (p : E Ã— Fâ‚—) :
    â‡‘(f.derivâ‚‚ p) = fun q : E Ã— Fâ‚— => f p.1 q.2 + f q.1 p.2 :=
  rfl

theorem map_add_add (f : E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] Gâ‚—) (x x' : E) (y y' : Fâ‚—) :
    f (x + x') (y + y') = f x y + f.derivâ‚‚ (x, y) (x', y') + f x' y' := by
  simp only [map_add, add_apply, coe_derivâ‚‚, add_assoc]
  abel

/-- The norm of the tensor product of a scalar linear map and of an element of a normed space
is the product of the norms. -/
@[simp]
theorem norm_smulRight_apply (c : StrongDual ğ•œ E) (f : Fâ‚—) : â€–smulRight c fâ€– = â€–câ€– * â€–fâ€– := by
  refine le_antisymm ?_ ?_
  Â· refine opNorm_le_bound _ (by positivity) fun x => ?_
    calc
      â€–c x â€¢ fâ€– = â€–c xâ€– * â€–fâ€– := norm_smul _ _
      _ â‰¤ â€–câ€– * â€–xâ€– * â€–fâ€– := by gcongr; apply le_opNorm
      _ = â€–câ€– * â€–fâ€– * â€–xâ€– := by ring
  Â· obtain hf | hf := (norm_nonneg f).eq_or_lt'
    Â· simp [hf]
    Â· rw [â† le_div_iffâ‚€ hf]
      refine opNorm_le_bound _ (by positivity) fun x => ?_
      rw [div_mul_eq_mul_div, le_div_iffâ‚€ hf]
      calc
        â€–c xâ€– * â€–fâ€– = â€–c x â€¢ fâ€– := (norm_smul _ _).symm
        _ = â€–smulRight c f xâ€– := rfl
        _ â‰¤ â€–smulRight c fâ€– * â€–xâ€– := le_opNorm _ _

/-- The non-negative norm of the tensor product of a scalar linear map and of an element of a normed
space is the product of the non-negative norms. -/
@[simp]
theorem nnnorm_smulRight_apply (c : StrongDual ğ•œ E) (f : Fâ‚—) : â€–smulRight c fâ€–â‚Š = â€–câ€–â‚Š * â€–fâ€–â‚Š :=
  NNReal.eq <| c.norm_smulRight_apply f

variable (ğ•œ E Fâ‚—) in
/-- `ContinuousLinearMap.smulRight` as a continuous trilinear map:
`smulRightL (c : StrongDual ğ•œ E) (f : F) (x : E) = c x â€¢ f`. -/
def smulRightL : StrongDual ğ•œ E â†’L[ğ•œ] Fâ‚— â†’L[ğ•œ] E â†’L[ğ•œ] Fâ‚— :=
  LinearMap.mkContinuousâ‚‚
    { toFun := smulRightâ‚—
      map_add' := fun câ‚ câ‚‚ => by
        ext x
        simp only [add_smul, coe_smulRightâ‚—, add_apply, smulRight_apply, LinearMap.add_apply]
      map_smul' := fun m c => by
        ext x
        dsimp
        rw [smul_smul] }
    1 fun c x => by
      simp only [coe_smulRightâ‚—, one_mul, norm_smulRight_apply, LinearMap.coe_mk, AddHom.coe_mk,
        le_refl]


@[simp]
theorem norm_smulRightL_apply (c : StrongDual ğ•œ E) (f : Fâ‚—) : â€–smulRightL ğ•œ E Fâ‚— c fâ€– = â€–câ€– * â€–fâ€– :=
  norm_smulRight_apply c f

end ContinuousLinearMap

end SemiNormed

section Restrict

namespace ContinuousLinearMap

variable {ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ'] [NormedAlgebra ğ•œ ğ•œ']
  [SeminormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedSpace ğ•œ' E] [IsScalarTower ğ•œ ğ•œ' E]
  [SeminormedAddCommGroup F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ' F] [IsScalarTower ğ•œ ğ•œ' F]
  [SeminormedAddCommGroup G] [NormedSpace ğ•œ G] [NormedSpace ğ•œ' G] [IsScalarTower ğ•œ ğ•œ' G]

variable (ğ•œ) in
/-- Convenience function for restricting the linearity of a bilinear map. -/
def bilinearRestrictScalars (B : E â†’L[ğ•œ'] F â†’L[ğ•œ'] G) : E â†’L[ğ•œ] F â†’L[ğ•œ] G :=
  (restrictScalarsL ğ•œ' F G ğ•œ ğ•œ).comp (B.restrictScalars ğ•œ)

variable (B : E â†’L[ğ•œ'] F â†’L[ğ•œ'] G) (x : E) (y : F)

theorem bilinearRestrictScalars_eq_restrictScalarsL_comp_restrictScalars :
    B.bilinearRestrictScalars ğ•œ = (restrictScalarsL ğ•œ' F G ğ•œ ğ•œ).comp (B.restrictScalars ğ•œ) := rfl

theorem bilinearRestrictScalars_eq_restrictScalars_restrictScalarsL_comp :
    B.bilinearRestrictScalars ğ•œ = restrictScalars ğ•œ ((restrictScalarsL ğ•œ' F G ğ•œ ğ•œ').comp B) := rfl

variable (ğ•œ) in
@[simp]
theorem bilinearRestrictScalars_apply_apply : (B.bilinearRestrictScalars ğ•œ) x y = B x y := rfl

@[simp]
theorem norm_bilinearRestrictScalars : â€–B.bilinearRestrictScalars ğ•œâ€– = â€–Bâ€– := rfl

end ContinuousLinearMap

end Restrict
