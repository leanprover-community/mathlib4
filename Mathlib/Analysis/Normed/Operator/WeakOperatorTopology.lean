/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.Normed.Module.Dual

/-!
# The weak operator topology

This file defines a type copy of `E â†’L[ğ•œ] F` (where `F` is a normed space) which is
endowed with the weak operator topology (WOT) rather than the topology induced by the operator norm.
The WOT is defined as the coarsest topology such that the functional `fun A => y (A x)` is
continuous for any `x : E` and `y : NormedSpace.Dual ğ•œ F`. Equivalently, a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `y (f a x)` tends to `y (A x)` along the same filter.

Basic non-topological properties of `E â†’L[ğ•œ] F` (such as the module structure) are copied over to
the type copy.

We also prove that the WOT is induced by the family of seminorms `â€–y (A x)â€–` for `x : E` and
`y : NormedSpace.Dual ğ•œ F`.

## Main declarations

* `ContinuousLinearMapWOT ğ•œ E F`: The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator
  topology.
* `ContinuousLinearMapWOT.tendsto_iff_forall_dual_apply_tendsto`: a function `f` tends to
  `A : E â†’WOT[ğ•œ] F` along filter `l` iff `y ((f a) x)` tends to `y (A x)` along the same filter.
* `ContinuousLinearMap.toWOT`: the inclusion map from `E â†’L[ğ•œ] F` to the type copy
* `ContinuousLinearMap.continuous_toWOT`: the inclusion map is continuous, i.e. the WOT is coarser
  than the norm topology.
* `ContinuousLinearMapWOT.withSeminorms`: the WOT is induced by the family of seminorms
  `â€–y (A x)â€–` for `x : E` and `y : NormedSpace.Dual ğ•œ F`.

## Notation

* The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology is denoted by
  `E â†’WOT[ğ•œ] F`.
* We locally use the notation `Fâ‹†` for `NormedSpace.Dual ğ•œ F`.

## Implementation notes

In the literature, the WOT is only defined on maps between Banach spaces. Here, we generalize this
a bit to `E â†’L[ğ•œ] F` where `F` is an normed space, and `E` actually only needs to be a vector
space with some topology for most results in this file.
-/

open scoped Topology

/-- The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology, denoted as
`E â†’WOT[ğ•œ] F`. -/
@[irreducible]
def ContinuousLinearMapWOT (ğ•œ : Type*) (E : Type*) (F : Type*) [Semiring ğ•œ] [AddCommGroup E]
    [TopologicalSpace E] [Module ğ•œ E] [AddCommGroup F] [TopologicalSpace F] [Module ğ•œ F] :=
  E â†’L[ğ•œ] F

@[inherit_doc]
notation:25 E " â†’WOT[" ğ•œ "]" F => ContinuousLinearMapWOT ğ•œ E F

namespace ContinuousLinearMapWOT

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [RCLike ğ•œ] [AddCommGroup E] [TopologicalSpace E]
  [Module ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

local postfix:max "â‹†" => NormedSpace.Dual ğ•œ

/-!
### Basic properties common with `E â†’L[ğ•œ] F`

The section copies basic non-topological properties of `E â†’L[ğ•œ] F` over to `E â†’WOT[ğ•œ] F`, such as
the module structure, `FunLike`, etc.
-/
section Basic

unseal ContinuousLinearMapWOT in
instance instAddCommGroup : AddCommGroup (E â†’WOT[ğ•œ] F) :=
  inferInstanceAs <| AddCommGroup (E â†’L[ğ•œ] F)

unseal ContinuousLinearMapWOT in
instance instModule : Module ğ•œ (E â†’WOT[ğ•œ] F) := inferInstanceAs <| Module ğ•œ (E â†’L[ğ•œ] F)

variable (ğ•œ) (E) (F)

unseal ContinuousLinearMapWOT in
/-- The linear equivalence that sends a continuous linear map to the type copy endowed with the
weak operator topology.  -/
def _root_.ContinuousLinearMap.toWOT : (E â†’L[ğ•œ] F) â‰ƒâ‚—[ğ•œ] (E â†’WOT[ğ•œ] F) := LinearEquiv.refl ğ•œ _

variable {ğ•œ} {E} {F}

instance instFunLike : FunLike (E â†’WOT[ğ•œ] F) E F where
  coe f :=  ((ContinuousLinearMap.toWOT ğ•œ E F).symm f : E â†’ F)
  coe_injective' := by intro; simp

instance instContinuousLinearMapClass : ContinuousLinearMapClass (E â†’WOT[ğ•œ] F) ğ•œ E F where
  map_add f x y := by simp only [DFunLike.coe]; simp
  map_smulâ‚›â‚— f r x := by simp only [DFunLike.coe]; simp
  map_continuous f := ContinuousLinearMap.continuous ((ContinuousLinearMap.toWOT ğ•œ E F).symm f)

lemma _root_.ContinuousLinearMap.toWOT_apply {A : E â†’L[ğ•œ] F} {x : E} :
    ((ContinuousLinearMap.toWOT ğ•œ E F) A) x = A x := rfl

unseal ContinuousLinearMapWOT in
lemma ext {A B : E â†’WOT[ğ•œ] F} (h : âˆ€ x, A x = B x) : A = B := ContinuousLinearMap.ext h

unseal ContinuousLinearMapWOT in
lemma ext_iff {A B : E â†’WOT[ğ•œ] F} : A = B â†” âˆ€ x, A x = B x := ContinuousLinearMap.ext_iff

-- This `ext` lemma is set at a lower priority than the default of 1000, so that the
-- version with an inner product (`ContinuousLinearMapWOT.ext_inner`) takes precedence
-- in the case of Hilbert spaces.
@[ext 900]
lemma ext_dual {A B : E â†’WOT[ğ•œ] F} (h : âˆ€ x (y : Fâ‹†), y (A x) = y (B x)) : A = B := by
  rw [ext_iff]
  intro x
  specialize h x
  rwa [â† NormedSpace.eq_iff_forall_dual_eq ğ•œ] at h

lemma ext_dual_iff {A B : E â†’WOT[ğ•œ] F} :
    A = B â†” âˆ€ x (y : Fâ‹†), y (A x) = y (B x) :=
  âŸ¨fun h _ _ => by simp [h], ext_dualâŸ©

@[simp] lemma zero_apply (x : E) : (0 : E â†’WOT[ğ•œ] F) x = 0 := by simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma add_apply {f g : E â†’WOT[ğ•œ] F} (x : E) : (f + g) x = f x + g x := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma sub_apply {f g : E â†’WOT[ğ•œ] F} (x : E) : (f - g) x = f x - g x := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma neg_apply {f : E â†’WOT[ğ•œ] F} (x : E) : (-f) x = -(f x) := by
  simp only [DFunLike.coe]; rfl

unseal ContinuousLinearMapWOT in
@[simp] lemma smul_apply {f : E â†’WOT[ğ•œ] F} (c : ğ•œ) (x : E) : (c â€¢ f) x = c â€¢ (f x) := by
  simp only [DFunLike.coe]; rfl

end Basic

/-!
### The topology of `E â†’WOT[ğ•œ] F`

The section endows `E â†’WOT[ğ•œ] F` with the weak operator topology and shows the basic properties
of this topology. In particular, we show that it is a topological vector space.
-/
section Topology

variable (ğ•œ) (E) (F) in
/-- The function that induces the topology on `E â†’WOT[ğ•œ] F`, namely the function that takes
an `A` and maps it to `fun âŸ¨x, yâŸ© => y (A x)` in `E Ã— Fâ‹† â†’ ğ•œ`, bundled as a linear map to make
it easier to prove that it is a TVS. -/
def inducingFn : (E â†’WOT[ğ•œ] F) â†’â‚—[ğ•œ] (E Ã— Fâ‹† â†’ ğ•œ) where
  toFun := fun A âŸ¨x, yâŸ© => y (A x)
  map_add' := fun x y => by ext; simp
  map_smul' := fun x y => by ext; simp

/-- The weak operator topology is the coarsest topology such that `fun A => y (A x)` is
continuous for all `x, y`. -/
instance instTopologicalSpace : TopologicalSpace (E â†’WOT[ğ•œ] F) :=
  .induced (inducingFn _ _ _) Pi.topologicalSpace

@[fun_prop]
lemma continuous_inducingFn : Continuous (inducingFn ğ•œ E F) :=
  continuous_induced_dom

lemma continuous_dual_apply (x : E) (y : Fâ‹†) : Continuous fun (A : E â†’WOT[ğ•œ] F) => y (A x) := by
  refine (continuous_pi_iff.mp continuous_inducingFn) âŸ¨x, yâŸ©

@[fun_prop]
lemma continuous_of_dual_apply_continuous {Î± : Type*} [TopologicalSpace Î±] {g : Î± â†’ E â†’WOT[ğ•œ] F}
    (h : âˆ€ x (y : Fâ‹†), Continuous fun a => y (g a x)) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr fun p => h p.1 p.2)

lemma embedding_inducingFn : Embedding (inducingFn ğ•œ E F) := by
  refine Function.Injective.embedding_induced fun A B hAB => ?_
  rw [ext_dual_iff]
  simpa [Function.funext_iff] using hAB

open Filter in
/-- The defining property of the weak operator topology: a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `y (f a x)` tends to `y (A x)` along the same filter. -/
lemma tendsto_iff_forall_dual_apply_tendsto {Î± : Type*} {l : Filter Î±} {f : Î± â†’ E â†’WOT[ğ•œ] F}
    {A : E â†’WOT[ğ•œ] F} :
    Tendsto f l (ğ“ A) â†” âˆ€ x (y : Fâ‹†), Tendsto (fun a => y (f a x)) l (ğ“ (y (A x))) := by
  have hmain : (âˆ€ x (y : Fâ‹†), Tendsto (fun a => y (f a x)) l (ğ“ (y (A x))))
      â†” âˆ€ (p : E Ã— Fâ‹†), Tendsto (fun a => p.2 (f a p.1)) l (ğ“ (p.2 (A p.1))) :=
    âŸ¨fun h p => h p.1 p.2, fun h x y => h âŸ¨x, yâŸ©âŸ©
  rw [hmain, â† tendsto_pi_nhds, Embedding.tendsto_nhds_iff embedding_inducingFn]
  rfl

lemma le_nhds_iff_forall_dual_apply_le_nhds {l : Filter (E â†’WOT[ğ•œ] F)} {A : E â†’WOT[ğ•œ] F} :
    l â‰¤ ğ“ A â†” âˆ€ x (y : Fâ‹†), l.map (fun T => y (T x)) â‰¤ ğ“ (y (A x)) :=
  tendsto_iff_forall_dual_apply_tendsto (f := id)

instance instT3Space : T3Space (E â†’WOT[ğ•œ] F) := embedding_inducingFn.t3Space

instance instContinuousAdd : ContinuousAdd (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)
instance instContinuousNeg : ContinuousNeg (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)
instance instContinuousSMul : ContinuousSMul ğ•œ (E â†’WOT[ğ•œ] F) := .induced (inducingFn ğ•œ E F)

instance instTopologicalAddGroup : TopologicalAddGroup (E â†’WOT[ğ•œ] F) where

instance instUniformSpace : UniformSpace (E â†’WOT[ğ•œ] F) := .comap (inducingFn ğ•œ E F) inferInstance

instance instUniformAddGroup : UniformAddGroup (E â†’WOT[ğ•œ] F) := .comap (inducingFn ğ•œ E F)

end Topology

/-! ### The WOT is induced by a family of seminorms -/
section Seminorms

/-- The family of seminorms that induce the weak operator topology, namely `â€–y (A x)â€–` for
all `x` and `y`.  -/
def seminorm (x : E) (y : Fâ‹†) : Seminorm ğ•œ (E â†’WOT[ğ•œ] F) where
  toFun A := â€–y (A x)â€–
  map_zero' := by simp
  add_le' A B := by simpa using norm_add_le _ _
  neg' A := by simp
  smul' r A := by simp

variable (ğ•œ) (E) (F) in
/-- The family of seminorms that induce the weak operator topology, namely `â€–y (A x)â€–` for
all `x` and `y`.  -/
def seminormFamily : SeminormFamily ğ•œ (E â†’WOT[ğ•œ] F) (E Ã— Fâ‹†) :=
  fun âŸ¨x, yâŸ© => seminorm x y

lemma hasBasis_seminorms : (ğ“ (0 : E â†’WOT[ğ•œ] F)).HasBasis (seminormFamily ğ•œ E F).basisSets id := by
  let p := seminormFamily ğ•œ E F
  rw [nhds_induced, nhds_pi]
  simp only [map_zero, Pi.zero_apply]
  have h := Filter.hasBasis_pi (fun _ : (E Ã— Fâ‹†) â†¦ Metric.nhds_basis_ball (x := 0)) |>.comap
    (inducingFn ğ•œ E F)
  refine h.to_hasBasis' ?_ ?_
  Â· rintro âŸ¨s, Uâ‚‚âŸ© âŸ¨hs, hUâ‚‚âŸ©
    lift s to Finset (E Ã— Fâ‹†) using hs
    by_cases hUâ‚ƒ : s.Nonempty
    Â· refine âŸ¨(s.sup p).ball 0 <| s.inf' hUâ‚ƒ Uâ‚‚, p.basisSets_mem _ <| (Finset.lt_inf'_iff _).2 hUâ‚‚,
        fun x hx y hy => ?_âŸ©
      simp only [Set.mem_preimage, Set.mem_pi, mem_ball_zero_iff]
      rw [id, Seminorm.mem_ball_zero] at hx
      have hp : p y â‰¤ s.sup p := Finset.le_sup hy
      refine lt_of_le_of_lt (hp x) (lt_of_lt_of_le hx ?_)
      exact Finset.inf'_le _ hy
    Â· rw [Finset.not_nonempty_iff_eq_empty.mp hUâ‚ƒ]
      exact âŸ¨(p 0).ball 0 1, p.basisSets_singleton_mem 0 one_pos, by simpâŸ©
  Â· suffices âˆ€ U âˆˆ p.basisSets, U âˆˆ ğ“ (0 : E â†’WOT[ğ•œ] F) by simpa [nhds_induced, nhds_pi]
    exact p.basisSets_mem_nhds fun âŸ¨x, yâŸ© â†¦ continuous_dual_apply x y |>.norm

lemma withSeminorms : WithSeminorms (seminormFamily ğ•œ E F) :=
  SeminormFamily.withSeminorms_of_hasBasis _ hasBasis_seminorms

instance instLocallyConvexSpace [Module â„ (E â†’WOT[ğ•œ] F)] [IsScalarTower â„ ğ•œ (E â†’WOT[ğ•œ] F)] :
    LocallyConvexSpace â„ (E â†’WOT[ğ•œ] F) :=
  withSeminorms.toLocallyConvexSpace

end Seminorms

end ContinuousLinearMapWOT

section NormedSpace

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]

/-- The weak operator topology is coarser than the norm topology, i.e. the inclusion map is
continuous. -/
@[continuity, fun_prop]
lemma ContinuousLinearMap.continuous_toWOT :
    Continuous (ContinuousLinearMap.toWOT ğ•œ E F) := by
  refine ContinuousLinearMapWOT.continuous_of_dual_apply_continuous fun x y => ?_
  simp_rw [ContinuousLinearMap.toWOT_apply]
  change Continuous fun a => y <| (ContinuousLinearMap.id ğ•œ (E â†’L[ğ•œ] F)).flip x a
  fun_prop

/-- The inclusion map from `E â†’[ğ•œ] F` to `E â†’WOT[ğ•œ] F`, bundled as a continuous linear map. -/
def ContinuousLinearMap.toWOTCLM : (E â†’L[ğ•œ] F) â†’L[ğ•œ] (E â†’WOT[ğ•œ] F) :=
  âŸ¨LinearEquiv.toLinearMap (ContinuousLinearMap.toWOT ğ•œ E F), ContinuousLinearMap.continuous_toWOTâŸ©

end NormedSpace
