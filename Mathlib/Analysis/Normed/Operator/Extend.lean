/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Zhouhang Zhou
-/
import Mathlib.Analysis.Normed.Operator.Basic

/-!

# Extension of continuous linear maps on Banach spaces

In this file we provide two different ways to extend a continuous linear map defined on a dense
subspace to the entire Banach space.

* `ContinuousLinearMap.extend`: Extend from a dense subspace using `IsUniformInducing`
* `ContinuousLinearMap.extendOfNorm`: Extend from a continuous linear map that is a dense
injection into the domain and using a norm estimate.

-/

suppress_compilation

open scoped NNReal

variable {ğ•œ ğ•œâ‚‚ E Eâ‚— F Fâ‚— : Type*}

namespace ContinuousLinearMap

section Extend

section Ring

variable [AddCommGroup E] [UniformSpace E] [IsUniformAddGroup E]
  [AddCommGroup F] [UniformSpace F] [IsUniformAddGroup F] [T0Space F]
  [AddCommMonoid Eâ‚—] [UniformSpace Eâ‚—] [ContinuousAdd Eâ‚—]
  [Semiring ğ•œ] [Semiring ğ•œâ‚‚] [Module ğ•œ E] [Module ğ•œâ‚‚ F] [Module ğ•œ Eâ‚—]
  [ContinuousConstSMul ğ•œ Eâ‚—] [ContinuousConstSMul ğ•œâ‚‚ F]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} (f g : E â†’SL[Ïƒâ‚â‚‚] F) [CompleteSpace F] (e : E â†’L[ğ•œ] Eâ‚—)

variable (h_dense : DenseRange e) (h_e : IsUniformInducing e)

/-- Extension of a continuous linear map `f : E â†’SL[Ïƒâ‚â‚‚] F`, with `E` a normed space and `F` a
complete normed space, along a uniform and dense embedding `e : E â†’L[ğ•œ] Eâ‚—`. -/
def extend : Eâ‚— â†’SL[Ïƒâ‚â‚‚] F :=
  -- extension of `f` is continuous
  have cont := (uniformContinuous_uniformly_extend h_e h_dense f.uniformContinuous).continuous
  -- extension of `f` agrees with `f` on the domain of the embedding `e`
  have eq := uniformly_extend_of_ind h_e h_dense f.uniformContinuous
  { toFun := (h_e.isDenseInducing h_dense).extend f
    map_add' := by
      refine h_dense.induction_onâ‚‚ ?_ ?_
      Â· exact isClosed_eq (cont.comp continuous_add)
          ((cont.comp continuous_fst).add (cont.comp continuous_snd))
      Â· intro x y
        simp only [eq, â† e.map_add]
        exact f.map_add _ _
    map_smul' := fun k => by
      refine fun b => h_dense.induction_on b ?_ ?_
      Â· exact isClosed_eq (cont.comp (continuous_const_smul _))
          ((continuous_const_smul _).comp cont)
      Â· intro x
        rw [â† map_smul]
        simp only [eq]
        exact ContinuousLinearMap.map_smulâ‚›â‚— _ _ _
    cont }

@[simp]
theorem extend_eq (x : E) : extend f e h_dense h_e (e x) = f x :=
  IsDenseInducing.extend_eq (h_e.isDenseInducing h_dense) f.cont _

theorem extend_unique (g : Eâ‚— â†’SL[Ïƒâ‚â‚‚] F) (H : g.comp e = f) : extend f e h_dense h_e = g :=
  ContinuousLinearMap.coeFn_injective <|
    uniformly_extend_unique h_e h_dense (ContinuousLinearMap.ext_iff.1 H) g.continuous

@[simp]
theorem extend_zero : extend (0 : E â†’SL[Ïƒâ‚â‚‚] F) e h_dense h_e = 0 :=
  extend_unique _ _ _ _ _ (zero_comp _)

end Ring

section NormedField

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [NormedAddCommGroup E] [NormedAddCommGroup Eâ‚—] [NormedAddCommGroup F] [NormedAddCommGroup Fâ‚—]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ Eâ‚—] [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œâ‚‚ Fâ‚—] [CompleteSpace F]
  (f g : E â†’SL[Ïƒâ‚â‚‚] F) (e : E â†’L[ğ•œ] Eâ‚—)

variable (h_dense : DenseRange e) (h_e : IsUniformInducing e)

variable {N : â„â‰¥0} (h_e : âˆ€ x, â€–xâ€– â‰¤ N * â€–e xâ€–) [RingHomIsometric Ïƒâ‚â‚‚]

/-- If a dense embedding `e : E â†’L[ğ•œ] G` expands the norm by a constant factor `Nâ»Â¹`, then the
norm of the extension of `f` along `e` is bounded by `N * â€–fâ€–`. -/
theorem opNorm_extend_le :
    â€–f.extend e h_dense (isUniformEmbedding_of_bound _ h_e).isUniformInducingâ€– â‰¤ N * â€–fâ€– := by
  -- Add `opNorm_le_of_dense`?
  refine opNorm_le_bound _ ?_ (isClosed_property h_dense (isClosed_le ?_ ?_) fun x â†¦ ?_)
  Â· cases le_total 0 N with
    | inl hN => exact mul_nonneg hN (norm_nonneg _)
    | inr hN =>
      have : Unique E := âŸ¨âŸ¨0âŸ©, fun x â†¦ norm_le_zero_iff.mp <|
        (h_e x).trans (mul_nonpos_of_nonpos_of_nonneg hN (norm_nonneg _))âŸ©
      obtain rfl : f = 0 := Subsingleton.elim ..
      simp
  Â· exact (cont _).norm
  Â· exact continuous_const.mul continuous_norm
  Â· rw [extend_eq]
    calc
      â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_opNorm _ _
      _ â‰¤ â€–fâ€– * (N * â€–e xâ€–) := mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _)
      _ â‰¤ N * â€–fâ€– * â€–e xâ€– := by rw [mul_comm â†‘N â€–fâ€–, mul_assoc]


end NormedField

end Extend

end ContinuousLinearMap

namespace LinearMap

section LeftInverse

variable [DivisionRing ğ•œ] [AddCommGroup E] [AddCommGroup F] [Module ğ•œ E] [Module ğ•œ F]

variable (f : E â†’â‚—[ğ•œ] F)

open scoped Classical in
/-- The left inverse of `f : E â†’â‚—[ğ•œ] F`.

If `f` is not injective, then we use the junk value `0`. -/
def leftInverse : F â†’â‚—[ğ•œ] E :=
  if h_inj : LinearMap.ker f = âŠ¥ then
  Classical.choose (f.exists_leftInverse_of_injective h_inj)
  else 0

/-- If `f` is injective, then the left inverse composed with `f` is the identity. -/
@[simp]
theorem leftInverse_apply_of_inj (h_inj : LinearMap.ker f = âŠ¥) (x : E) :
    f.leftInverse (f x) = x := by
  have := Classical.choose_spec (f.exists_leftInverse_of_injective h_inj)
  rw [LinearMap.ext_iff] at this
  simpa [leftInverse, h_inj] using this x

end LeftInverse

section compInv

variable [DivisionRing ğ•œ] [DivisionRing ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [AddCommGroup E] [SeminormedAddCommGroup F] [SeminormedAddCommGroup Eâ‚—]
  [Module ğ•œ E] [Module ğ•œâ‚‚ F] [Module ğ•œ Eâ‚—]

variable (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) (g : E â†’â‚—[ğ•œ] Eâ‚—)

open scoped Classical in
/-- Composition with the left inverse as a CLM. -/
def compLeftInverse :=
  if h : LinearMap.ker g = âŠ¥ âˆ§ âˆƒ (C : â„), âˆ€ (x : E), â€–f xâ€– â‰¤ C * â€–g xâ€– then
  (f âˆ˜â‚›â‚— (g.leftInverse.domRestrict
    (LinearMap.range g))).mkContinuousOfExistsBound
  (by
    rcases h.2 with âŸ¨C, hCâŸ©
    use C
    rintro âŸ¨x, y, hxyâŸ©
    simp only [â† hxy, LinearMap.coe_comp, Function.comp_apply,
      LinearMap.domRestrict_apply, AddSubgroupClass.coe_norm]
    convert hC y
    apply g.leftInverse_apply_of_inj h.1)
  else 0

@[simp]
theorem compLeftInverse_apply_of_inj_bdd (h_inj : LinearMap.ker g = âŠ¥)
    (h_norm : âˆƒ (C : â„), âˆ€ (x : E), â€–f xâ€– â‰¤ C * â€–g xâ€–) (y : LinearMap.range g) :
    f.compLeftInverse g y = (f âˆ˜â‚›â‚— (g.leftInverse.domRestrict
      (LinearMap.range g))) y := by
  simp [compLeftInverse, h_inj, h_norm]

end compInv

section NormedDivisionRing

variable [NormedDivisionRing ğ•œ] [NormedDivisionRing ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [AddCommGroup E] [SeminormedAddCommGroup Eâ‚—] [NormedAddCommGroup F]
  [Module ğ•œ E] [Module ğ•œâ‚‚ F] [IsBoundedSMul ğ•œâ‚‚ F] [Module ğ•œ Eâ‚—] [IsBoundedSMul ğ•œ Eâ‚—]
  [CompleteSpace F]

variable (f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F) (e : E â†’â‚—[ğ•œ] Eâ‚—)

open scoped Classical in
/-- Extension of a continuous linear map `f : E â†’SL[Ïƒâ‚â‚‚] F` to `Fâ‚— â†’SL[Ïƒâ‚â‚‚] F`,
where `E` is a normed space and `F` a complete normed space,
using an injective dense embedding `e : E â†’L[ğ•œ] Fâ‚—` together with a bound `â€–f xâ€– â‰¤ C * â€–e xâ€–`
for all `x : E`. -/
def extendOfNorm : Eâ‚— â†’SL[Ïƒâ‚â‚‚] F :=
  if h : DenseRange e then
  (f.compLeftInverse e).extend (LinearMap.range e).subtypeL (by simpa using h)
    isUniformEmbedding_subtype_val.isUniformInducing
  else 0

variable {f e}

theorem extendOfNorm_eq (h_inj : LinearMap.ker e = âŠ¥)
    (h_dense : DenseRange e) (h_norm : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C * â€–e xâ€–) (x : E) :
    f.extendOfNorm e (e x) = f x := by
  simp only [extendOfNorm, h_dense, â†“reduceDIte]
  have := (f.compLeftInverse e).extend_eq (LinearMap.range e).subtypeL (by simpa using h_dense)
    isUniformEmbedding_subtype_val.isUniformInducing
  convert this âŸ¨e x, LinearMap.mem_range_self e xâŸ©
  simp only [h_inj, h_norm, compLeftInverse_apply_of_inj_bdd, LinearMap.coe_comp,
    Function.comp_apply, LinearMap.domRestrict_apply]
  congr
  apply (e.leftInverse_apply_of_inj h_inj _).symm

theorem extendOfNorm_norm_le (h_inj : LinearMap.ker e = âŠ¥) (h_dense : DenseRange e) (C : â„)
    (h_norm : âˆ€ (x : E), â€–f xâ€– â‰¤ C * â€–e xâ€–) (x : Eâ‚—) :
    â€–f.extendOfNorm e xâ€– â‰¤ C * â€–xâ€– := by
  have h_mem : âˆ€ (x : Eâ‚—) (hy : x âˆˆ (LinearMap.range e)), â€–extendOfNorm f e xâ€– â‰¤ C * â€–xâ€– := by
    rintro x âŸ¨y, hxyâŸ©
    rw [â† hxy]
    convert h_norm y
    apply extendOfNorm_eq h_inj h_dense âŸ¨C, h_normâŸ©
  exact h_dense.induction h_mem (isClosed_le (by fun_prop) (by fun_prop)) x

end NormedDivisionRing

section NormedField

variable [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œâ‚‚] {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚}
  [NormedAddCommGroup F] [SeminormedAddCommGroup Eâ‚—]
  [NormedSpace ğ•œâ‚‚ F] [NormedSpace ğ•œ Eâ‚—]
  [AddCommGroup E] [Module ğ•œ E] [CompleteSpace F]

variable {f : E â†’â‚›â‚—[Ïƒâ‚â‚‚] F} {e : E â†’â‚—[ğ•œ] Eâ‚—}

theorem extendOfNorm_opNorm_le (h_inj : LinearMap.ker e = âŠ¥)
    (h_dense : DenseRange e) {C : â„}
    (hC : 0 â‰¤ C) (h_norm : âˆ€ (x : E), â€–f xâ€– â‰¤ C * â€–e xâ€–) : â€–f.extendOfNorm eâ€– â‰¤ C :=
  (f.extendOfNorm e).opNorm_le_bound hC (extendOfNorm_norm_le h_inj h_dense C h_norm)

end NormedField

end LinearMap
