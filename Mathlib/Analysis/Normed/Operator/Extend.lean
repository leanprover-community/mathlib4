/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, Zhouhang Zhou
-/
module

public import Mathlib.Analysis.Normed.Operator.Basic
public import Mathlib.LinearAlgebra.Isomorphisms

/-!

# Extension of continuous linear maps on Banach spaces

In this file we provide two different ways to extend a continuous linear map defined on a dense
subspace to the entire Banach space.

* `ContinuousLinearMap.extend`: Extend `f : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F` to a continuous linear map
`E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F`, where `e : E ‚Üí‚Çó[ùïú] E‚Çó` is a dense map that is `IsUniformInducing`.
* `LinearMap.extendOfNorm`: Extend `f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F` to a continuous linear map
`E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F`, where `e : E ‚Üí‚Çó[ùïú] E‚Çó` is a dense map and we have the norm estimate
`‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ` for all `x : E`.

Moreover, we can extend a linear equivalence:
* `LinearEquiv.extend`: Extend a linear equivalence between normed spaces to a continuous linear
equivalence between Banach spaces with two dense maps `e‚ÇÅ` and `e‚ÇÇ` and the corresponding norm
estimates.
* `LinearEquiv.extendOfIsometry`: Extend `f : E ‚âÉ‚Çó[ùïú] F` to a linear isometry equivalence
`E‚Çó ‚Üí‚Çó·µ¢[ùïú] F‚Çó`, where `e‚ÇÅ : E ‚Üí‚Çó[ùïú] E‚Çó` and `e‚ÇÇ : F ‚Üí‚Çó[ùïú] F‚Çó` are dense maps into Banach spaces
and `f` preserves the norm.

-/

@[expose] public section

suppress_compilation

open scoped NNReal

variable {ùïú ùïú‚ÇÇ E E‚Çó F F‚Çó : Type*}

namespace ContinuousLinearMap

section Extend

section Ring

variable [AddCommGroup E] [UniformSpace E] [IsUniformAddGroup E]
  [AddCommGroup F] [UniformSpace F] [IsUniformAddGroup F] [T0Space F]
  [AddCommMonoid E‚Çó] [UniformSpace E‚Çó] [ContinuousAdd E‚Çó]
  [Semiring ùïú] [Semiring ùïú‚ÇÇ] [Module ùïú E] [Module ùïú‚ÇÇ F] [Module ùïú E‚Çó]
  [ContinuousConstSMul ùïú E‚Çó] [ContinuousConstSMul ùïú‚ÇÇ F]
  {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ} (f g : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) [CompleteSpace F] (e : E ‚ÜíL[ùïú] E‚Çó)

open scoped Classical in
/-- Extension of a continuous linear map `f : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F`, with `E` a normed space and `F` a
complete normed space, along a uniform and dense embedding `e : E ‚ÜíL[ùïú] E‚Çó`. -/
def extend : E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F :=
  if h : DenseRange e ‚àß IsUniformInducing e then
  -- extension of `f` is continuous
  have cont := (uniformContinuous_uniformly_extend h.2 h.1 f.uniformContinuous).continuous
  -- extension of `f` agrees with `f` on the domain of the embedding `e`
  have eq := uniformly_extend_of_ind h.2 h.1 f.uniformContinuous
  { toFun := (h.2.isDenseInducing h.1).extend f
    map_add' := by
      refine h.1.induction_on‚ÇÇ ?_ ?_
      ¬∑ exact isClosed_eq (cont.comp continuous_add)
          ((cont.comp continuous_fst).add (cont.comp continuous_snd))
      ¬∑ intro x y
        simp only [eq, ‚Üê e.map_add]
        exact f.map_add _ _
    map_smul' := fun k => by
      refine fun b => h.1.induction_on b ?_ ?_
      ¬∑ exact isClosed_eq (cont.comp (continuous_const_smul _))
          ((continuous_const_smul _).comp cont)
      ¬∑ intro x
        rw [‚Üê map_smul]
        simp only [eq]
        exact map_smul‚Çõ‚Çó _ _ _
    cont }
  else 0

variable {e}

@[simp]
theorem extend_eq (h_dense : DenseRange e) (h_e : IsUniformInducing e) (x : E) :
    extend f e (e x) = f x := by
  simp only [extend, h_dense, h_e, and_self, ‚ÜìreduceDIte, coe_mk', LinearMap.coe_mk, AddHom.coe_mk]
  exact IsDenseInducing.extend_eq (h_e.isDenseInducing h_dense) f.cont _

theorem extend_unique (h_dense : DenseRange e) (h_e : IsUniformInducing e) (g : E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F)
    (H : g.comp e = f) : extend f e = g := by
  simp only [extend, h_dense, h_e, and_self, ‚ÜìreduceDIte]
  exact ContinuousLinearMap.coeFn_injective <|
    uniformly_extend_unique h_e h_dense (ContinuousLinearMap.ext_iff.1 H) g.continuous

@[simp]
theorem extend_zero (h_dense : DenseRange e) (h_e : IsUniformInducing e) :
    extend (0 : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) e = 0 :=
  extend_unique _ h_dense h_e _ (zero_comp _)

end Ring

section NormedField

variable [NontriviallyNormedField ùïú] [NontriviallyNormedField ùïú‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ}
  [NormedAddCommGroup E] [NormedAddCommGroup E‚Çó] [NormedAddCommGroup F] [NormedAddCommGroup F‚Çó]
  [NormedSpace ùïú E] [NormedSpace ùïú E‚Çó] [NormedSpace ùïú‚ÇÇ F] [NormedSpace ùïú‚ÇÇ F‚Çó] [CompleteSpace F]
  (f g : E ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) {e : E ‚ÜíL[ùïú] E‚Çó}

variable (h_dense : DenseRange e) (h_e : IsUniformInducing e)

variable {N : ‚Ñù‚â•0} [RingHomIsometric œÉ‚ÇÅ‚ÇÇ]

/-- If a dense embedding `e : E ‚ÜíL[ùïú] G` expands the norm by a constant factor `N‚Åª¬π`, then the
norm of the extension of `f` along `e` is bounded by `N * ‚Äñf‚Äñ`. -/
theorem opNorm_extend_le (h_dense : DenseRange e) (h_e : ‚àÄ x, ‚Äñx‚Äñ ‚â§ N * ‚Äñe x‚Äñ) :
    ‚Äñf.extend e‚Äñ ‚â§ N * ‚Äñf‚Äñ := by
  -- Add `opNorm_le_of_dense`?
  refine opNorm_le_bound _ ?_ (isClosed_property h_dense (isClosed_le ?_ (by fun_prop)) fun x ‚Ü¶ ?_)
  ¬∑ cases le_total 0 N with
    | inl hN => exact mul_nonneg hN (norm_nonneg _)
    | inr hN =>
      have : Unique E := ‚ü®‚ü®0‚ü©, fun x ‚Ü¶ norm_le_zero_iff.mp <|
        (h_e x).trans (mul_nonpos_of_nonpos_of_nonneg hN (norm_nonneg _))‚ü©
      obtain rfl : f = 0 := Subsingleton.elim ..
      simp
  ¬∑ exact (cont _).norm
  ¬∑ rw [extend_eq _ h_dense (isUniformEmbedding_of_bound _ h_e).isUniformInducing]
    calc
      ‚Äñf x‚Äñ ‚â§ ‚Äñf‚Äñ * ‚Äñx‚Äñ := le_opNorm _ _
      _ ‚â§ ‚Äñf‚Äñ * (N * ‚Äñe x‚Äñ) := mul_le_mul_of_nonneg_left (h_e x) (norm_nonneg _)
      _ ‚â§ N * ‚Äñf‚Äñ * ‚Äñe x‚Äñ := by rw [mul_comm ‚ÜëN ‚Äñf‚Äñ, mul_assoc]


end NormedField

end Extend

end ContinuousLinearMap

namespace LinearMap

section compInv

variable [DivisionRing ùïú] [DivisionRing ùïú‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ}
  [AddCommGroup E] [NormedAddCommGroup F] [SeminormedAddCommGroup E‚Çó]
  [Module ùïú E] [Module ùïú‚ÇÇ F] [Module ùïú E‚Çó]

variable (f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F) (g : E ‚Üí‚Çó[ùïú] E‚Çó)

open scoped Classical in
/-- Composition of a semilinear map `f` with the left inverse of a linear map `g` as a continuous
linear map provided that the norm estimate `‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ` holds for all `x : E`. -/
def compLeftInverse : range g ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F :=
  if h : ‚àÉ (C : ‚Ñù), ‚àÄ (x : E), ‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ then
  (((LinearMap.ker g).liftQ f (by
    obtain ‚ü®C, h‚ü© := h
    intro x hx
    specialize h x
    rw [hx] at h
    simpa using h)).comp
    g.quotKerEquivRange.symm.toLinearMap).mkContinuousOfExistsBound
  (by
    obtain ‚ü®C, h‚ü© := h
    use C
    intro ‚ü®x, y, hxy‚ü©
    simpa [‚Üê hxy] using h y)
  else 0

theorem compLeftInverse_apply_of_bdd (h_norm : ‚àÉ (C : ‚Ñù), ‚àÄ (x : E), ‚Äñf x‚Äñ ‚â§ C * ‚Äñg x‚Äñ)
    (x : E) (y : E‚Çó) (hx : g x = y) :
    f.compLeftInverse g ‚ü®y, ‚ü®x, hx‚ü©‚ü© = f x := by
  simp [compLeftInverse, h_norm, ‚Üê hx]

end compInv

section NormedDivisionRing

variable [NormedDivisionRing ùïú] [NormedDivisionRing ùïú‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ}
  [AddCommGroup E] [SeminormedAddCommGroup E‚Çó] [NormedAddCommGroup F]
  [Module ùïú E] [Module ùïú‚ÇÇ F] [IsBoundedSMul ùïú‚ÇÇ F] [Module ùïú E‚Çó] [IsBoundedSMul ùïú E‚Çó]
  [CompleteSpace F]

variable (f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F) (e : E ‚Üí‚Çó[ùïú] E‚Çó)

/-- Extension of a linear map `f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F` to a continuous linear map `E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F`,
where `E` is a normed space and `F` a complete normed space, using a dense map `e : E ‚Üí‚Çó[ùïú] E‚Çó`
together with a bound `‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ` for all `x : E`. -/
def extendOfNorm : E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F := (f.compLeftInverse e).extend (LinearMap.range e).subtypeL

variable {f e}

theorem extendOfNorm_eq (h_dense : DenseRange e) (h_norm : ‚àÉ C, ‚àÄ x, ‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ)
    (x : E) : f.extendOfNorm e (e x) = f x := by
  have := (f.compLeftInverse e).extend_eq (e := (LinearMap.range e).subtypeL)
    (by simpa using h_dense) isUniformEmbedding_subtype_val.isUniformInducing
  convert this ‚ü®e x, LinearMap.mem_range_self e x‚ü©
  exact (compLeftInverse_apply_of_bdd _ _ h_norm _ _ rfl).symm

theorem norm_extendOfNorm_apply_le (h_dense : DenseRange e) (C : ‚Ñù)
    (h_norm : ‚àÄ (x : E), ‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ) (x : E‚Çó) :
    ‚Äñf.extendOfNorm e x‚Äñ ‚â§ C * ‚Äñx‚Äñ := by
  have h_mem : ‚àÄ (x : E‚Çó) (hy : x ‚àà (LinearMap.range e)), ‚ÄñextendOfNorm f e x‚Äñ ‚â§ C * ‚Äñx‚Äñ := by
    intro x ‚ü®y, hxy‚ü©
    simpa only [‚Üê hxy, extendOfNorm_eq h_dense ‚ü®C, h_norm‚ü© y] using h_norm y
  exact h_dense.induction h_mem (isClosed_le (by fun_prop) (by fun_prop)) x

theorem extendOfNorm_unique (h_dense : DenseRange e) (C : ‚Ñù) (h_norm : ‚àÄ (x : E), ‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ)
    (g : E‚Çó ‚ÜíSL[œÉ‚ÇÅ‚ÇÇ] F) (H : g.toLinearMap.comp e = f) : extendOfNorm f e = g := by
  apply ContinuousLinearMap.extend_unique
  ¬∑ simpa using h_dense
  ¬∑ exact isUniformEmbedding_subtype_val.isUniformInducing
  ext ‚ü®y, x, hxy‚ü©
  rw [compLeftInverse_apply_of_bdd _ _ ‚ü®C, h_norm‚ü© x y hxy]
  simp [‚Üê hxy, ‚Üê H]

end NormedDivisionRing

section NormedField

variable [NontriviallyNormedField ùïú] [NontriviallyNormedField ùïú‚ÇÇ] {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ}
  [NormedAddCommGroup F] [SeminormedAddCommGroup E‚Çó]
  [NormedSpace ùïú‚ÇÇ F] [NormedSpace ùïú E‚Çó]
  [AddCommGroup E] [Module ùïú E] [CompleteSpace F]

variable {f : E ‚Üí‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F} {e : E ‚Üí‚Çó[ùïú] E‚Çó}

theorem opNorm_extendOfNorm_le (h_dense : DenseRange e) {C : ‚Ñù} (hC : 0 ‚â§ C)
    (h_norm : ‚àÄ (x : E), ‚Äñf x‚Äñ ‚â§ C * ‚Äñe x‚Äñ) : ‚Äñf.extendOfNorm e‚Äñ ‚â§ C :=
  (f.extendOfNorm e).opNorm_le_bound hC (norm_extendOfNorm_apply_le h_dense C h_norm)

end NormedField

end LinearMap

namespace LinearEquiv

section extend

variable [NormedDivisionRing ùïú] [NormedDivisionRing ùïú‚ÇÇ]
  [AddCommGroup E] [NormedAddCommGroup E‚Çó] [AddCommGroup F] [NormedAddCommGroup F‚Çó]
  [Module ùïú E] [Module ùïú E‚Çó] [IsBoundedSMul ùïú E‚Çó] [Module ùïú‚ÇÇ F] [Module ùïú‚ÇÇ F‚Çó] [IsBoundedSMul ùïú‚ÇÇ F‚Çó]
  [CompleteSpace E‚Çó] [CompleteSpace F‚Çó]

variable {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : ùïú‚ÇÇ ‚Üí+* ùïú} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
variable (f : E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F) (e‚ÇÅ : E ‚Üí‚Çó[ùïú] E‚Çó) (e‚ÇÇ : F ‚Üí‚Çó[ùïú‚ÇÇ] F‚Çó)

/-- Extension of a linear equivalence `f : E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F` to a continuous linear equivalence
`E‚Çó ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] F‚Çó`, where `E` and `F` are normed spaces and `E‚Çó` and `F‚Çó` are Banach spaces,
using dense maps `e‚ÇÅ : E ‚Üí‚Çó[ùïú‚ÇÅ] E‚Çó` and `e‚ÇÇ : F ‚Üí‚Çó[ùïú‚ÇÇ] F‚ÇÇ` together with bounds
`‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ` for all `x : E` and `‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ` for all `x : F`. -/
def extend (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ)
    (h_dense‚ÇÇ : DenseRange e‚ÇÇ) (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) :
    E‚Çó ‚âÉSL[œÉ‚ÇÅ‚ÇÇ] F‚Çó where
  __ := (e‚ÇÇ ‚àò‚Çõ‚Çó f.toLinearMap).extendOfNorm e‚ÇÅ
  invFun := (e‚ÇÅ ‚àò‚Çõ‚Çó f.symm.toLinearMap).extendOfNorm e‚ÇÇ
  left_inv := by
    refine h_dense‚ÇÅ.induction ?_ ?_
    ¬∑ rintro _ ‚ü®_, rfl‚ü©
      simp [LinearMap.extendOfNorm_eq, h_dense‚ÇÅ, h_norm‚ÇÅ, h_dense‚ÇÇ, h_norm‚ÇÇ]
    ¬∑ exact isClosed_eq (by simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom,
      ContinuousLinearMap.coe_coe]; fun_prop) continuous_id
  right_inv := by
    refine h_dense‚ÇÇ.induction ?_ ?_
    ¬∑ rintro _ ‚ü®_, rfl‚ü©
      simp [LinearMap.extendOfNorm_eq, h_dense‚ÇÅ, h_norm‚ÇÅ, h_dense‚ÇÇ, h_norm‚ÇÇ]
    ¬∑ exact isClosed_eq (by simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom,
      ContinuousLinearMap.coe_coe]; fun_prop) continuous_id
  continuous_invFun := ContinuousLinearMap.continuous _

theorem extend_apply (h_dense‚ÇÅ : DenseRange e‚ÇÅ)
    (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : E‚Çó) :
    (f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_norm‚ÇÅ h_dense‚ÇÇ h_norm‚ÇÇ) x =
    (e‚ÇÇ ‚àò‚Çõ‚Çó f.toLinearMap).extendOfNorm e‚ÇÅ x := rfl

theorem extend_symm_apply (h_dense‚ÇÅ : DenseRange e‚ÇÅ)
    (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : F‚Çó) :
    (f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_norm‚ÇÅ h_dense‚ÇÇ h_norm‚ÇÇ).symm x =
    (e‚ÇÅ ‚àò‚Çõ‚Çó f.symm.toLinearMap).extendOfNorm e‚ÇÇ x := rfl

@[simp]
theorem extend_eq (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ)
    (h_dense‚ÇÇ : DenseRange e‚ÇÇ) (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : E) :
    f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_norm‚ÇÅ h_dense‚ÇÇ h_norm‚ÇÇ (e‚ÇÅ x) = e‚ÇÇ (f x) :=
  LinearMap.extendOfNorm_eq h_dense‚ÇÅ h_norm‚ÇÅ x

@[simp]
theorem extend_symm_eq (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ)
    (h_dense‚ÇÇ : DenseRange e‚ÇÇ) (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : F) :
    (f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_norm‚ÇÅ h_dense‚ÇÇ h_norm‚ÇÇ).symm (e‚ÇÇ x) = e‚ÇÅ (f.symm x) :=
  LinearMap.extendOfNorm_eq h_dense‚ÇÇ h_norm‚ÇÇ x

theorem norm_extend_le (C : ‚Ñù) (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_norm‚ÇÅ : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ)
    (h_dense‚ÇÇ : DenseRange e‚ÇÇ) (h_norm‚ÇÇ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : E‚Çó) :
    ‚Äñ(f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ ‚ü®C, h_norm‚ÇÅ‚ü© h_dense‚ÇÇ h_norm‚ÇÇ) x‚Äñ ‚â§ C * ‚Äñx‚Äñ :=
  LinearMap.norm_extendOfNorm_apply_le h_dense‚ÇÅ _ h_norm‚ÇÅ _

theorem norm_extend_symm_le (C : ‚Ñù) (h_dense‚ÇÅ : DenseRange e‚ÇÅ)
    (h_norm‚ÇÅ : ‚àÉ C, ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ ‚â§ C * ‚Äñe‚ÇÅ x‚Äñ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm‚ÇÇ : ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ ‚â§ C * ‚Äñe‚ÇÇ x‚Äñ) (x : F‚Çó) :
    ‚Äñ(f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_norm‚ÇÅ h_dense‚ÇÇ ‚ü®C, h_norm‚ÇÇ‚ü©).symm x‚Äñ ‚â§ C * ‚Äñx‚Äñ :=
  LinearMap.norm_extendOfNorm_apply_le h_dense‚ÇÇ _ h_norm‚ÇÇ _

end extend

section extendOfIsometry

variable [NormedField ùïú] [NormedField ùïú‚ÇÇ]
  [AddCommGroup E] [Module ùïú E]
  [AddCommGroup F] [Module ùïú‚ÇÇ F]
  [NormedAddCommGroup E‚Çó] [NormedSpace ùïú E‚Çó] [CompleteSpace E‚Çó]
  [NormedAddCommGroup F‚Çó] [NormedSpace ùïú‚ÇÇ F‚Çó] [CompleteSpace F‚Çó]

variable {œÉ‚ÇÅ‚ÇÇ : ùïú ‚Üí+* ùïú‚ÇÇ} {œÉ‚ÇÇ‚ÇÅ : ùïú‚ÇÇ ‚Üí+* ùïú} [RingHomInvPair œÉ‚ÇÅ‚ÇÇ œÉ‚ÇÇ‚ÇÅ] [RingHomInvPair œÉ‚ÇÇ‚ÇÅ œÉ‚ÇÅ‚ÇÇ]
variable (f : E ‚âÉ‚Çõ‚Çó[œÉ‚ÇÅ‚ÇÇ] F) (e‚ÇÅ : E ‚Üí‚Çó[ùïú] E‚Çó) (e‚ÇÇ : F ‚Üí‚Çó[ùïú‚ÇÇ] F‚Çó)

/-- Extend a densely defined operator that preserves the norm to a linear isometry equivalence. -/
def extendOfIsometry (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ = ‚Äñe‚ÇÅ x‚Äñ) :
    E‚Çó ‚âÉ‚Çõ‚Çó·µ¢[œÉ‚ÇÅ‚ÇÇ] F‚Çó :=
  have h_norm‚ÇÇ : ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ = ‚Äñe‚ÇÇ x‚Äñ := fun x ‚Ü¶ by simpa using (h_norm (f.symm x)).symm
  { __ := f.extend e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ ‚ü®1, by simp [h_norm]‚ü© h_dense‚ÇÇ ‚ü®1, by simp [h_norm‚ÇÇ]‚ü©
    norm_map' := by
      refine h_dense‚ÇÅ.induction ?_ (isClosed_eq (by
        simp only [ContinuousLinearEquiv.coe_toLinearEquiv]; fun_prop) continuous_norm)
      rintro x ‚ü®y, rfl‚ü©
      convert h_norm y
      apply LinearMap.extendOfNorm_eq h_dense‚ÇÅ (by use 1; simp [h_norm]) }

theorem extendOfIsometry_apply (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ = ‚Äñe‚ÇÅ x‚Äñ) (x : E‚Çó) :
    (f.extendOfIsometry e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_dense‚ÇÇ h_norm) x =
    (e‚ÇÇ ‚àò‚Çõ‚Çó f.toLinearMap).extendOfNorm e‚ÇÅ x := rfl

theorem extendOfIsometry_symm_apply (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ = ‚Äñe‚ÇÅ x‚Äñ) (x : F‚Çó) :
    (f.extendOfIsometry e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_dense‚ÇÇ h_norm).symm x =
    (e‚ÇÅ ‚àò‚Çõ‚Çó f.symm.toLinearMap).extendOfNorm e‚ÇÇ x := rfl

@[simp]
theorem extendOfIsometry_eq (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ = ‚Äñe‚ÇÅ x‚Äñ) (x : E) :
    f.extendOfIsometry e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_dense‚ÇÇ h_norm (e‚ÇÅ x) = e‚ÇÇ (f x) :=
  LinearMap.extendOfNorm_eq h_dense‚ÇÅ ‚ü®1, fun x ‚Ü¶ by simp [h_norm x]‚ü© x

@[simp]
theorem extendOfIsometry_symm_eq (h_dense‚ÇÅ : DenseRange e‚ÇÅ) (h_dense‚ÇÇ : DenseRange e‚ÇÇ)
    (h_norm : ‚àÄ x, ‚Äñe‚ÇÇ (f x)‚Äñ = ‚Äñe‚ÇÅ x‚Äñ) (x : F) :
    (f.extendOfIsometry e‚ÇÅ e‚ÇÇ h_dense‚ÇÅ h_dense‚ÇÇ h_norm).symm (e‚ÇÇ x) = e‚ÇÅ (f.symm x) :=
  have h_norm‚ÇÇ : ‚àÄ x, ‚Äñe‚ÇÅ (f.symm x)‚Äñ = ‚Äñe‚ÇÇ x‚Äñ :=
    fun x ‚Ü¶ by simpa using (h_norm (f.symm x)).symm
  LinearMap.extendOfNorm_eq h_dense‚ÇÇ ‚ü®1, fun x ‚Ü¶ by simp [h_norm‚ÇÇ x]‚ü© x

end extendOfIsometry

end LinearEquiv
