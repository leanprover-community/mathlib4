/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Analysis.InnerProductSpace.Adjoint
public import Mathlib.Analysis.InnerProductSpace.Positive
public import Mathlib.Analysis.LocallyConvex.SeparatingDual
public import Mathlib.Analysis.Normed.Operator.Banach
public import Mathlib.Topology.Algebra.Algebra.Equiv

import Mathlib.Algebra.Central.Basic

/-!
# Continuous (star-)algebra equivalences between continuous endomorphisms are (isometrically) inner

This file shows that continuous (star-)algebra equivalences between continuous endomorphisms are
(isometrically) inner.
See `Mathlib/LinearAlgebra/GeneralLinearGroup/AlgEquiv.lean` for the non-continuous version.
The proof follows the same idea as the non-continuous version.

### TODO:
- when `V = W`, we can state that the group homomorphism
  `(V â†’L[ğ•œ] V)Ë£ â†’* ((V â†’L[ğ•œ] V) â‰ƒA[ğ•œ] (V â†’L[ğ•œ] V))` is surjective,
  see `Module.End.mulSemiringActionToAlgEquiv_conjAct_surjective` for the non-continuous
  version of this.
-/

open ContinuousLinearMap ContinuousLinearEquiv

/-- This is the continuous version of `AlgEquiv.eq_linearEquivConjAlgEquiv`. -/
public theorem ContinuousAlgEquiv.eq_continuousLinearEquivConjContinuousAlgEquiv {ğ•œ V W : Type*}
    [NontriviallyNormedField ğ•œ] [SeminormedAddCommGroup V] [SeminormedAddCommGroup W]
    [NormedSpace ğ•œ V] [NormedSpace ğ•œ W] [SeparatingDual ğ•œ V] [SeparatingDual ğ•œ W]
    (f : (V â†’L[ğ•œ] V) â‰ƒA[ğ•œ] (W â†’L[ğ•œ] W)) :
    âˆƒ U : V â‰ƒL[ğ•œ] W, f = U.conjContinuousAlgEquiv := by
  /- The proof goes as follows:
    We want to show the existence of a continuous linear equivalence `U : V â‰ƒL[ğ•œ] W` such that
    `f A (U x) = U (A x)` for all `A : V â†’L[ğ•œ] V` and `x : V`.
    Assume nontriviality of `V`, and let `(u : V) â‰  0`. Let `v` be a strong dual on `V` such that
    `v u â‰  0` (exists since it has a separating dual).
    Let `z : W` such that `f (smulRight v u) z â‰  0`.
    Then we construct a bijective continuous linear map `T : V â†’L[ğ•œ] W`
    given by `x â†¦ f (smulRight v x) z` and so satisfies `T (A x) = f A (T x)` for all
    `A : V â†’L[ğ•œ] V` and `x : V`. So it remains to show that this map has a continuous inverse.
    Let `d` be a strong dual on `W` such that `d ((f (smulRight v u)) z) = 1` (exists since it has
    a separating dual).
    We then construct a right-inverse continuous linear map `T' : W â†’L[ğ•œ] V` given by
    `x â†¦ f.symm (smulRight d x) u`.
    And so it follows that `T` is also a continuous linear equivalence. -/
  by_cases! hV : Subsingleton V
  Â· by_cases! hV : Subsingleton W
    Â· exact âŸ¨{ toLinearEquiv := 0 }, ext <| Subsingleton.allEq _ _âŸ©
    simpa using congr(f $(Subsingleton.allEq 0 1))
  simp_rw [ContinuousAlgEquiv.ext_iff, funext_iff, conjContinuousAlgEquiv_apply, â† comp_assoc,
    eq_comp_toContinuousLinearMap_symm]
  obtain âŸ¨u, huâŸ© := exists_ne (0 : V)
  obtain âŸ¨v, huvâŸ© := SeparatingDual.exists_ne_zero (R := ğ•œ) hu
  obtain âŸ¨z, hzâŸ© : âˆƒ z : W, Â¬ f (smulRight v u) z = (0 : W â†’L[ğ•œ] W) z := by
    simp_rw [â† not_forall, â† ContinuousLinearMap.ext_iff, map_eq_zero_iff,
      ContinuousLinearMap.ext_iff, not_forall, smulRight_apply, zero_apply,
      smul_eq_zero_iff_left hu]
    exact âŸ¨u, huvâŸ©
  set T := apply' _ (.id ğ•œ) z âˆ˜L f.toContinuousAlgHom.toContinuousLinearMap âˆ˜L smulRightL ğ•œ _ _ v
  have hT x : T x = f (smulRight v x) z := rfl
  have this A x : T (A x) = f A (T x) := by
    simp only [hT, â† mul_apply, â† map_mul]
    congr; ext; simp
  have âŸ¨d, hdâŸ© := SeparatingDual.exists_eq_one (R := ğ•œ) hz
  have surj : Function.Surjective T := fun w â†¦ âŸ¨f.symm (smulRight d w) u, by simp [T, this, hd]âŸ©
  have inj : Function.Injective T := fun x y hxy â†¦ by
    have h_smul : smulRight v x = smulRight v y := by
      apply f.injective <| ContinuousLinearMap.ext fun z â†¦ ?_
      obtain âŸ¨w, rflâŸ© := surj z
      simp [â† this, hxy]
    simpa [huv.isUnit.smul_left_cancel] using congr((fun f â†¦ f u) $h_smul)
  set Tâ‚— : V â‰ƒâ‚—[ğ•œ] W := .ofBijective T.toLinearMap âŸ¨inj, surjâŸ©
  set T' := apply' _ (.id ğ•œ) u âˆ˜L f.symm.toContinuousAlgHom.toContinuousLinearMap âˆ˜L
    smulRightL ğ•œ _ _ d
  set TL : V â‰ƒL[ğ•œ] W := { Tâ‚— with
    continuous_toFun := T.continuous
    continuous_invFun := by
      change Continuous Tâ‚—.symm.toLinearMap
      suffices T'.toLinearMap = Tâ‚—.symm from this â–¸ T'.continuous
      simp [LinearMap.ext_iff, â† Tâ‚—.injective.eq_iff, T', this, hT, hd, Tâ‚—] }
  exact âŸ¨TL, fun A â†¦ (ContinuousLinearMap.ext <| this A).symmâŸ©

variable {ğ•œ V W : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
  [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]

section auxiliaryDefs

variable (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0)
  (hÎ±2 : Î±' * Î±' = Î±â»Â¹) (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
  (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
include hÎ± hÎ±2 he he'

/-- Scalar multiple of a continuous linear equivalence (given certain properties are satisfied). -/
noncomputable abbrev auxContinuousLinearEquiv :
    V â‰ƒL[ğ•œ] W where
  toLinearMap := (Î±' â€¢ e.toContinuousLinearMap).toLinearMap
  invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).toLinearMap
  left_inv x := by
    have := by simpa using congr($he x)
    simp [smul_smul, hÎ±2, this, hÎ±, â† mul_assoc]
  right_inv x := by
    have := by simpa using congr($he' x)
    simp [smul_smul, hÎ±2, this, hÎ±, â† mul_assoc]
  continuous_toFun := (Î±' â€¢ e.toContinuousLinearMap).continuous
  continuous_invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).continuous

@[simp] theorem coe_auxContinuousLinearEquiv :
    (auxContinuousLinearEquiv e hÎ± hÎ±2 he he').toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap := rfl

/-- Construct an isometry linear equivalence from a continuous linear equivalence
and that its adjoint is a real-scalar multiple of its inverse. -/
noncomputable abbrev auxIsometry (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    V â‰ƒâ‚—áµ¢[ğ•œ] W where
  __ := auxContinuousLinearEquiv e hÎ± hÎ±2 he he' |>.toLinearEquiv
  norm_map' := by
    rw [ContinuousLinearEquiv.coe_toLinearEquiv, â† ContinuousLinearEquiv.coe_coe,
      norm_map_iff_adjoint_comp_self, coe_auxContinuousLinearEquiv,
      MulActionSemiHomClass.map_smulâ‚›â‚—]
    simp only [hÎ±a, comp_smulâ‚›â‚—, RingHom.id_apply, smul_comp, smul_smul, hÎ±2]
    simp [he, smul_smul, hÎ±, one_def]

@[simp] theorem coe_auxIsometry (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap := rfl

@[simp] theorem coe_symm_auxIsometry (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.symm.toContinuousLinearMap =
      Î±'â»Â¹ â€¢ e.symm.toContinuousLinearMap := by
  ext y
  apply (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.injective
  simp [smul_smul, inv_mul_cancelâ‚€ (a := Î±') (by grind)]

end auxiliaryDefs

open ComplexOrder

/-- The â‹†-algebra equivalence version of
`ContinuousAlgEquiv.eq_continuousLinearEquivConjContinuousAlgEquiv`.

TODO: remove the hypothesis `Continuous f`, as star-algebra equivalences between endomorphisms are
automatically continuous. -/
public theorem StarAlgEquiv.eq_linearIsometryEquivConjStarAlgEquiv
    (f : (V â†’L[ğ•œ] V) â‰ƒâ‹†â‚[ğ•œ] (W â†’L[ğ•œ] W)) (hf : Continuous f) :
    âˆƒ U : V â‰ƒâ‚—áµ¢[ğ•œ] W, f = U.conjStarAlgEquiv := by
  -- Assume nontriviality of `V`.
  by_cases! Subsingleton V
  Â· by_cases! Subsingleton W
    Â· use { toLinearEquiv := 0, norm_map' _ := by simp [Subsingleton.eq_zero] }
      exact ext fun _ â†¦ Subsingleton.allEq _ _
    simpa using congr(f $(Subsingleton.allEq 0 1))
  /- By `ContinuousAlgEquiv.eq_continuousLinearEquivConjContinuousAlgEquiv`,
    we know there exists a continuous linear equivalence `y : V â‰ƒL[ğ•œ] W` such that
    `f = y.conjAlgEquiv`.
    Our goal will be to construct an isometry from `y`. We do this by first showing
    `adjoint y âˆ˜ y` is in the center of the endormorphisms, and as the algebra of endomorphisms
    are central, `adjoint y âˆ˜ y` is a scalar multiple of the identity. -/
  obtain âŸ¨y, hyâŸ© := (ContinuousAlgEquiv.mk f.toAlgEquiv hf
    (f.toAlgEquiv.toLinearEquiv.continuous_symm hf)).eq_continuousLinearEquivConjContinuousAlgEquiv
  have (x : V â†’L[ğ•œ] V) : adjoint (f x) = f (adjoint x) := map_star _ _ |>.symm
  rw [ContinuousAlgEquiv.ext_iff] at hy
  simp_rw [â† StarAlgEquiv.coe_toAlgEquiv, ContinuousAlgEquiv.coe_mk f.toAlgEquiv hf _ â–¸ hy,
    conjContinuousAlgEquiv_apply,  adjoint_comp] at this
  replace this (x : V â†’L[ğ•œ] V) : adjoint y.toContinuousLinearMap âˆ˜L y âˆ˜L adjoint x âˆ˜L y.symm =
      adjoint x âˆ˜L adjoint y.toContinuousLinearMap := by
    simp_rw [â† this x, â† comp_assoc, â† adjoint_comp]
    simp
  replace this (x : V â†’L[ğ•œ] V) : Commute x (adjoint y.toContinuousLinearMap âˆ˜L y) := by
    simp_rw [commute_iff_eq, mul_def, â† comp_assoc, â† (adjoint_adjoint x â–¸ this _), comp_assoc]
    simp
  -- Let `Î± : ğ•œ` be that scalar, i.e., `adjoint y âˆ˜ y = Î± â€¢ id`. This scalar is clearly real.
  obtain âŸ¨Î±, hÎ±âŸ© := by simpa using (Subalgebra.mem_center_iff (R := ğ•œ)).mpr fun _ â†¦ this _
  simp only [AlgHom.toRingHom_eq_coe, Algebra.toRingHom_ofId, Algebra.algebraMap_eq_smul_one] at hÎ±
  have : IsUnit (adjoint y.toContinuousLinearMap âˆ˜L y) :=
    isUnit_iff_exists.mpr âŸ¨y.symm âˆ˜L adjoint y.symm.toContinuousLinearMap, by
      simp [mul_def, â† comp_assoc, comp_assoc _ _ (adjoint y.toContinuousLinearMap),
        â† adjoint_comp, one_def, comp_assoc _ y.toContinuousLinearMap]âŸ©
  have hÎ±_re : Î± = RCLike.re Î± := by
    have := by simpa [IsSelfAdjoint, â† hÎ±, one_def, star_eq_adjoint] using
      (IsSelfAdjoint.one (W â†’L[ğ•œ] W)).adjoint_conj y.toContinuousLinearMap
    rwa [â† one_def, (smul_left_injective ğ•œ one_ne_zero).eq_iff, RCLike.conj_eq_iff_re,
      eq_comm] at this
  --  Also, as `adjoint y âˆ˜ y` is invertible, we get `Î± â‰  0`.
  have hÎ±_ne_zero : Î± â‰  0 := fun h â†¦ by simp [h, â† hÎ±] at this
  -- As `adjoint y âˆ˜ y` is positive, we then get `0 < Î±`.
  have hÎ±_nonneg : 0 â‰¤ Î± := by
    have := hÎ±_re.symm â–¸ (nonneg_iff_isPositive _ |>.mpr
      (hÎ±_re â–¸ hÎ± â–¸ isPositive_adjoint_comp_self y.toContinuousLinearMap))
    rw [â† LinearMap.isPositive_one.isPositive_smul_iff (E := V) (one_ne_zero' (V â†’â‚—[ğ•œ] V))]
    exact (nonneg_iff_isPositive _).mp this
  have hÎ±_pos := RCLike.ofReal_pos.mp <| hÎ±_re â–¸ (lt_of_le_of_ne' hÎ±_nonneg hÎ±_ne_zero)
  -- We also get `y âˆ˜ adjoint y = Î± â€¢ id`.
  have h_comp_adjoint : y.toContinuousLinearMap âˆ˜L adjoint y.toContinuousLinearMap =
      Î± â€¢ ContinuousLinearMap.id ğ•œ _ := by
    ext x
    simpa using congr(y (($hÎ± âˆ˜L y.symm.toContinuousLinearMap) x)).symm
  -- Finally, we construct our isometry `1/âˆš(re Î±) â€¢ y`.
  set Î² := (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ)
  have hÎ² : Î² * Î² = Î±â»Â¹ := by
    rw [hÎ±_re]
    norm_num [Î², â† RCLike.ofReal_mul, â† Real.rpow_add hÎ±_pos, Real.rpow_neg_one]
  set U := auxIsometry y hÎ±_ne_zero hÎ² hÎ±.symm h_comp_adjoint (by simp [Î²])
  use U
  have hÎ²â‚‚ : Î²â»Â¹ * Î² = 1 := by
    refine inv_mul_cancelâ‚€ ?_
    simp only [Î², ne_eq, map_eq_zero]
    rw [Real.rpow_eq_zero hÎ±_pos.le (by simp)]
    exact ne_of_gt hÎ±_pos
  ext
  simp [U.conjStarAlgEquiv_apply, U, smul_smul, hÎ²â‚‚, â† conjContinuousAlgEquiv_apply, â† hy]

/- TODO: Remove instance when we have `StarOrderedRing (V â†’L[ğ•œ] V)` since
this then becomes an instance from `StarRingEquivClass.instOrderIsoClass`. -/
public instance (priority := 100) {F : Type*} [EquivLike F (V â†’L[ğ•œ] V) (W â†’L[ğ•œ] W)]
    [NonUnitalAlgEquivClass F ğ•œ _ _] [StarHomClass F _ _] [ContinuousMapClass F _ _] :
    OrderIsoClass F _ _ where
  map_le_map_iff f x y := by
    obtain âŸ¨U, hUâŸ© := StarAlgEquiv.eq_linearIsometryEquivConjStarAlgEquiv
      (StarAlgEquivClass.toStarAlgEquiv f : _ â‰ƒâ‹†â‚[ğ•œ] _) (map_continuous f)
    have this a : f a = U.conjStarAlgEquiv a := by simpa using congr($hU a)
    simp_rw [le_def, â† _root_.map_sub, â† isPositive_toLinearMap_iff, this]
    exact LinearMap.isPositive_linearIsometryEquiv_conj_iff U
