/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Analysis.InnerProductSpace.Adjoint
public import Mathlib.Analysis.LocallyConvex.SeparatingDual
public import Mathlib.Analysis.Normed.Operator.Banach
public import Mathlib.Topology.Algebra.Algebra.Equiv

import Mathlib.Algebra.Central.Basic
import Mathlib.Algebra.Order.Module.PositiveLinearMap
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.SpecialFunctions.Pow.Real

/-!
# Continuous (star-)algebra equivalences between continuous endomorphisms are (isometrically) inner

This file shows that continuous (star-)algebra equivalences between continuous endomorphisms are
isometrically inner.
See `Mathlib/LinearAlgebra/GeneralLinearGroup/AlgEquiv.lean` for the non-continuous version.
The proof is essentially the same as the non-continuous version.

# TODO:
- when `V = W`, we can state that the group homomorphism
  `(V â†’L[ğ•œ] V)Ë£ â†’* ((V â†’L[ğ•œ] V) â‰ƒA[ğ•œ] (V â†’L[ğ•œ] V))` is surjective,
  see `Module.End.mulSemiringActionToAlgEquiv_conjAct_surjective` for the non-continuous
  version of this.
-/

open ContinuousLinearMap ContinuousLinearEquiv

/-- This is the continuous version of `AlgEquiv.eq_linearEquivConjAlgEquiv`. -/
public theorem ContinuousAlgEquiv.eq_continuousLinearEquivConjContinuousAlgEquiv {ğ•œ V W : Type*}
    [NontriviallyNormedField ğ•œ] [NormedAddCommGroup V] [NormedAddCommGroup W]
    [NormedSpace ğ•œ V] [NormedSpace ğ•œ W] [SeparatingDual ğ•œ V] [SeparatingDual ğ•œ W]
    [CompleteSpace V] [CompleteSpace W] (f : (V â†’L[ğ•œ] V) â‰ƒA[ğ•œ] (W â†’L[ğ•œ] W)) :
    âˆƒ U : V â‰ƒL[ğ•œ] W, f = U.conjContinuousAlgEquiv := by
  by_cases! hV : Subsingleton V
  Â· by_cases! hV : Subsingleton W
    Â· exact âŸ¨{ toLinearEquiv := 0 }, ext <| Subsingleton.allEq _ _âŸ©
    simpa using congr(f $(Subsingleton.allEq 0 1))
  simp_rw [ContinuousAlgEquiv.ext_iff, funext_iff, conjContinuousAlgEquiv_apply, â† comp_assoc,
    eq_comp_toContinuousLinearMap_symm]
  obtain âŸ¨u, huâŸ© := exists_ne (0 : V)
  obtain âŸ¨v, huvâŸ© := SeparatingDual.exists_ne_zero (R := ğ•œ) hu
  obtain âŸ¨z, hzâŸ© : âˆƒ z : W, Â¬ f (smulRight v u) z = (0 : W â†’L[ğ•œ] W) z := by
    rw [â† not_forall, â† ContinuousLinearMap.ext_iff, map_eq_zero_iff, ContinuousLinearMap.ext_iff]
    exact not_forall.mpr âŸ¨u, huv.isUnit.smul_eq_zero.not.mpr huâŸ©
  set T := apply' _ (.id ğ•œ) z âˆ˜L f.toContinuousAlgHom.toContinuousLinearMap âˆ˜L smulRightL ğ•œ _ _ v
  have hT x : T x = f (smulRight v x) z := rfl
  have this A x : T (A x) = f A (T x) := by
    simp only [hT, â† mul_apply, â† map_mul]
    congr; ext; simp
  have surj : Function.Surjective T := fun w â†¦ by
    obtain âŸ¨d, hdâŸ© := SeparatingDual.exists_eq_one (R := ğ•œ) hz
    exact âŸ¨f.symm (smulRight d w) u, by simp [T, this, hd]âŸ©
  have inj : Function.Injective T := fun x y hxy â†¦ by
    have h_smul : smulRight v x = smulRight v y := by
      apply f.injective <| ContinuousLinearMap.ext fun z â†¦ ?_
      obtain âŸ¨w, rflâŸ© := surj z
      simp [â† this, hxy]
    simpa [huv.isUnit.smul_left_cancel] using congr((fun f â†¦ f u) $h_smul)
  exact âŸ¨.ofBijective T ((LinearMapClass.ker_eq_bot _).mpr inj)
    (LinearMap.range_eq_top_of_surjective T surj), fun A â†¦ (ContinuousLinearMap.ext <| this A).symmâŸ©

variable {ğ•œ V W : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
  [NormedAddCommGroup W] [InnerProductSpace ğ•œ W] [CompleteSpace W]

/-- can't do this inline, it times out -/
noncomputable abbrev auxContinuousLinearEquiv (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0)
    (hÎ±2 : Î±' * Î±' = Î±â»Â¹) (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W) :
    V â‰ƒL[ğ•œ] W where
  toFun := (Î±' â€¢ e.toContinuousLinearMap).toLinearMap
  invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).toLinearMap
  left_inv := by
    simp only [coe_smul, Function.leftInverse_iff_comp, funext_iff, Function.comp_apply,
      LinearMap.smul_apply, ContinuousLinearMap.coe_coe, ContinuousLinearEquiv.coe_coe,
      _root_.map_smul, smul_smul, hÎ±2, id_eq]
    simp_rw [â† ContinuousLinearEquiv.coe_coe, â† comp_apply, he]
    simp [smul_smul, hÎ±]
  right_inv := by
    simp only [coe_smul, Function.rightInverse_iff_comp, funext_iff, Function.comp_apply,
      LinearMap.smul_apply, ContinuousLinearMap.coe_coe, _root_.map_smul,
      ContinuousLinearEquiv.coe_coe, smul_smul, hÎ±2, id_eq]
    simp_rw [â† ContinuousLinearEquiv.coe_coe, â† comp_apply, he']
    simp [smul_smul, hÎ±]
  map_add' := by simp
  map_smul' := by simp
  continuous_toFun := (Î±' â€¢ e.toContinuousLinearMap).continuous
  continuous_invFun := (Î±' â€¢ e.toContinuousLinearMap.adjoint).continuous

theorem coe_auxContinuousLinearEquiv (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W) :
    (auxContinuousLinearEquiv e hÎ± hÎ±2 he he').toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap := rfl

theorem adjoint_auxContinuousLinearEquiv (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0)
    (hÎ±2 : Î±' * Î±' = Î±â»Â¹) (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    adjoint (auxContinuousLinearEquiv e hÎ± hÎ±2 he he').toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap.adjoint := by
  ext x
  apply ext_inner_left ğ•œ fun y â†¦ ?_
  simp [auxContinuousLinearEquiv, adjoint_inner_right, inner_smul_left, inner_smul_right, hÎ±a]

/-- can't do this inline either, it times out -/
noncomputable abbrev auxIsometry (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    V â‰ƒâ‚—áµ¢[ğ•œ] W where
  __ := auxContinuousLinearEquiv e hÎ± hÎ±2 he he' |>.toLinearEquiv
  norm_map' := by
    rw [ContinuousLinearEquiv.coe_toLinearEquiv, â† ContinuousLinearEquiv.coe_coe,
      norm_map_iff_adjoint_comp_self, adjoint_auxContinuousLinearEquiv _ _ _ _ _ hÎ±a,
      coe_auxContinuousLinearEquiv]
    simp only [comp_smulâ‚›â‚—, RingHom.id_apply, smul_comp, smul_smul, hÎ±2]
    simp [he, smul_smul, hÎ±, one_def]

theorem coe_auxIsometry (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.toContinuousLinearMap =
      Î±' â€¢ e.toContinuousLinearMap := rfl

theorem coe_symm_auxIsometry (e : V â‰ƒL[ğ•œ] W) {Î± Î±' : ğ•œ} (hÎ± : Î± â‰  0) (hÎ±2 : Î±' * Î±' = Î±â»Â¹)
    (he : e.toContinuousLinearMap.adjoint âˆ˜L e = Î± â€¢ .id ğ•œ V)
    (he' : e âˆ˜L e.toContinuousLinearMap.adjoint = Î± â€¢ .id ğ•œ W)
    (hÎ±a : starRingEnd ğ•œ Î±' = Î±') :
    (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.symm.toContinuousLinearMap =
      Î±'â»Â¹ â€¢ e.symm.toContinuousLinearMap := by
  ext y
  apply (auxIsometry e hÎ± hÎ±2 he he' hÎ±a).toContinuousLinearEquiv.injective
  simp [smul_smul, inv_mul_cancelâ‚€ (a := Î±') (by grind)]

open ComplexOrder

public theorem StarAlgEquiv.coe_eq_linearIsometryEquiv_conjugate
    (f : (V â†’L[ğ•œ] V) â‰ƒâ‹†â‚[ğ•œ] (W â†’L[ğ•œ] W)) (hf : Continuous f) :
    âˆƒ U : V â‰ƒâ‚—áµ¢[ğ•œ] W,
      â‡‘f = fun x â†¦ U.toContinuousLinearEquiv âˆ˜L x âˆ˜L U.symm.toContinuousLinearEquiv := by
  by_cases! hV : Subsingleton V
  Â· by_cases! hV : Subsingleton W
    Â· use { toLinearEquiv := 0, norm_map' _ := by simp [Subsingleton.eq_zero] }
      exact Subsingleton.allEq _ _
    simpa using congr(f $(Subsingleton.allEq 0 1))
  obtain âŸ¨y, hyâŸ© := (ContinuousAlgEquiv.ofAlgEquiv f.toAlgEquiv hf
    (f.toAlgEquiv.toLinearEquiv.continuous_symm hf)).eq_continuousLinearEquivConjContinuousAlgEquiv
  have (x : V â†’L[ğ•œ] V) : adjoint (f x) = f (adjoint x) := map_star _ _ |>.symm
  rw [ContinuousAlgEquiv.ext_iff] at hy
  simp_rw [â† StarAlgEquiv.coe_toAlgEquiv, ContinuousAlgEquiv.coe_ofAlgEquiv f.toAlgEquiv hf _ â–¸ hy,
    conjContinuousAlgEquiv_apply,  adjoint_comp] at this
  replace this (x : V â†’L[ğ•œ] V) : adjoint y.toContinuousLinearMap âˆ˜L y âˆ˜L adjoint x âˆ˜L y.symm =
      adjoint x âˆ˜L adjoint y.toContinuousLinearMap := by
    simp_rw [â† this x, â† comp_assoc, â† adjoint_comp]
    simp
  replace this (x : V â†’L[ğ•œ] V) : Commute x (adjoint y.toContinuousLinearMap âˆ˜L y) := by
    specialize this (adjoint x)
    simp only [adjoint_adjoint] at this
    simp_rw [Commute, SemiconjBy, mul_def, â† comp_assoc, â† this, comp_assoc]
    simp
  replace this :
      (adjoint y.toContinuousLinearMap âˆ˜L y) âˆˆ Subalgebra.centralizer ğ•œ (âŠ¤ : Set (V â†’L[ğ•œ] V)) := by
    rw [Subalgebra.mem_centralizer_iff]
    exact fun _ _ â†¦ this _
  simp only [Set.top_eq_univ, Subalgebra.centralizer_univ, Algebra.IsCentral.center_eq_bot] at this
  obtain âŸ¨Î±, hÎ±âŸ© := this
  simp only [AlgHom.toRingHom_eq_coe, Algebra.toRingHom_ofId, Algebra.algebraMap_eq_smul_one] at hÎ±
  have this : IsUnit (adjoint y.toContinuousLinearMap âˆ˜L y) := isUnit_iff_exists.mpr
    âŸ¨y.symm âˆ˜L adjoint y.symm.toContinuousLinearMap, by
        simp [mul_def, â† comp_assoc, comp_assoc _ _ (adjoint y.toContinuousLinearMap),
          â† adjoint_comp, one_def, comp_assoc _ y.toContinuousLinearMap]âŸ©
  have thisÎ± : Î± = RCLike.re Î± := by
    have this10 := by simpa [IsSelfAdjoint, â† hÎ±, one_def, star_eq_adjoint] using
      IsSelfAdjoint.adjoint_conj (IsSelfAdjoint.one (W â†’L[ğ•œ] W)) y.toContinuousLinearMap
    rwa [â† one_def, (smul_left_injective ğ•œ one_ne_zero).eq_iff, RCLike.conj_eq_iff_re,
      eq_comm] at this10
  have thisÎ±' : Î± â‰  0 := fun h â†¦ by simp [h, â† hÎ±] at this
  have this2 : 0 â‰¤ Î± := by
    have this1 := thisÎ±.symm â–¸ (nonneg_iff_isPositive _ |>.mpr
      (thisÎ± â–¸ hÎ± â–¸ isPositive_adjoint_comp_self y.toContinuousLinearMap))
    rw [â† LinearMap.IsPositive.isPositive_smul_iff (E := V) isPositive_one]
    Â· exact (nonneg_iff_isPositive _).mp this1
    Â· exact one_ne_zero' (V â†’â‚—[ğ•œ] V)
  replace this2 := RCLike.ofReal_pos.mp <| thisÎ± â–¸ (lt_of_le_of_ne' this2 thisÎ±')
  have thisU : y.toContinuousLinearMap âˆ˜L adjoint y.toContinuousLinearMap =
      Î± â€¢ ContinuousLinearMap.id ğ•œ _ := by
        have := by simpa [one_def, comp_assoc] using congr($hÎ± âˆ˜L y.symm.toContinuousLinearMap)
        ext
        apply_fun y.symm using y.symm.injective
        simp [â† this]
  set Î±a := (((RCLike.re Î± : â„) ^ (-(1 / 2 : â„)) : â„) : ğ•œ)
  have Î±a2 : Î±a * Î±a = Î±â»Â¹ := by
    simp_rw [Î±a, â† RCLike.ofReal_mul, â† Real.rpow_add this2]
    rw [thisÎ±]
    norm_num
    simp [Real.rpow_neg_one]
  set U := auxIsometry y thisÎ±' Î±a2 hÎ±.symm thisU (by simp [Î±a])
  use U
  have la : Î±aâ»Â¹ * Î±a = 1 := by
    simp only [one_div, Î±a]
    exact inv_mul_cancelâ‚€ (by
      simp only [ne_eq, map_eq_zero]
      rw [Real.rpow_eq_zero this2.le (by simp)]
      exact ne_of_gt this2)
  simp [U, coe_auxIsometry, coe_symm_auxIsometry, smul_smul, la, â† conjContinuousAlgEquiv_apply,
    â† hy]

/- Remove instance when we have `StarOrderedRing (V â†’L[ğ•œ] V)` since
this then becomes an instance from `StarRingEquivClass.instOrderIsoClass`. -/
instance (priority := 100) {F : Type*} [EquivLike F (V â†’L[ğ•œ] V) (W â†’L[ğ•œ] W)]
    [NonUnitalAlgEquivClass F ğ•œ _ _] [StarHomClass F _ _] [ContinuousMapClass F _ _] :
    OrderIsoClass F _ _ where
  map_le_map_iff f x y := by
    obtain âŸ¨U, hUâŸ© := StarAlgEquiv.coe_eq_linearIsometryEquiv_conjugate
      (StarAlgEquivClass.toStarAlgEquiv f : _ â‰ƒâ‹†â‚[ğ•œ] _) (map_continuous f)
    simp_rw [LinearIsometryEquiv.toContinuousLinearEquiv_symm, funext_iff,
      fun x â†¦ show StarAlgEquivClass.toStarAlgEquiv f x = f x by rfl] at hU
    simp_rw [le_def, â† _root_.map_sub, â† isPositive_toLinearMap_iff, hU]
    exact LinearMap.isPositive_linearIsometryEquiv_conj_iff U
