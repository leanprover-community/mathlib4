/-
Copyright (c) 2019 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Topology.Baire.Lemmas
import Mathlib.Topology.Baire.CompleteMetrizable
import Mathlib.Analysis.Normed.Operator.NormedSpace
import Mathlib.Analysis.Normed.Group.InfiniteSum
import Mathlib.Analysis.Normed.Group.AddTorsor

/-!
# Banach open mapping theorem

This file contains the Banach open mapping theorem, i.e., the fact that a bijective
bounded linear map between Banach spaces has a bounded inverse.
-/

open Function Metric Set Filter Finset Topology NNReal

open LinearMap (range ker)

variable {ğ•œ ğ•œ' : Type*} [NontriviallyNormedField ğ•œ] [NontriviallyNormedField ğ•œ'] {Ïƒ : ğ•œ â†’+* ğ•œ'}
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ' F] (f : E â†’SL[Ïƒ] F)

namespace ContinuousLinearMap

/-- A (possibly nonlinear) right inverse to a continuous linear map, which doesn't have to be
linear itself but which satisfies a bound `â€–inverse xâ€– â‰¤ C * â€–xâ€–`. A surjective continuous linear
map doesn't always have a continuous linear right inverse, but it always has a nonlinear inverse
in this sense, by Banach's open mapping theorem. -/
structure NonlinearRightInverse where
  /-- The underlying function.

  Do NOT use directly. Use the coercion instead. -/
  toFun : F â†’ E
  /-- The bound `C` so that `â€–inverse xâ€– â‰¤ C * â€–xâ€–` for all `x`. -/
  nnnorm : â„â‰¥0
  bound' : âˆ€ y, â€–toFun yâ€– â‰¤ nnnorm * â€–yâ€–
  right_inv' : âˆ€ y, f (toFun y) = y

instance : CoeFun (NonlinearRightInverse f) fun _ => F â†’ E :=
  âŸ¨fun fsymm => fsymm.toFunâŸ©

@[simp]
theorem NonlinearRightInverse.right_inv {f : E â†’SL[Ïƒ] F} (fsymm : NonlinearRightInverse f) (y : F) :
    f (fsymm y) = y :=
  fsymm.right_inv' y

theorem NonlinearRightInverse.bound {f : E â†’SL[Ïƒ] F} (fsymm : NonlinearRightInverse f) (y : F) :
    â€–fsymm yâ€– â‰¤ fsymm.nnnorm * â€–yâ€– :=
  fsymm.bound' y

end ContinuousLinearMap

variable {Ïƒ' : ğ•œ' â†’+* ğ•œ} [RingHomInvPair Ïƒ Ïƒ'] [RingHomIsometric Ïƒ] [RingHomIsometric Ïƒ']

/-- Given a continuous linear equivalence, the inverse is in particular an instance of
`ContinuousLinearMap.NonlinearRightInverse` (which turns out to be linear). -/
noncomputable def ContinuousLinearEquiv.toNonlinearRightInverse
    [RingHomInvPair Ïƒ' Ïƒ] (f : E â‰ƒSL[Ïƒ] F) :
    ContinuousLinearMap.NonlinearRightInverse (f : E â†’SL[Ïƒ] F) where
  toFun := f.invFun
  nnnorm := â€–(f.symm : F â†’SL[Ïƒ'] E)â€–â‚Š
  bound' _ := ContinuousLinearMap.le_opNorm (f.symm : F â†’SL[Ïƒ'] E) _
  right_inv' := f.apply_symm_apply

noncomputable instance [RingHomInvPair Ïƒ' Ïƒ] (f : E â‰ƒSL[Ïƒ] F) :
    Inhabited (ContinuousLinearMap.NonlinearRightInverse (f : E â†’SL[Ïƒ] F)) :=
  âŸ¨f.toNonlinearRightInverseâŸ©

/-! ### Proof of the Banach open mapping theorem -/


variable [CompleteSpace F]

namespace ContinuousLinearMap

include Ïƒ' in
/-- First step of the proof of the Banach open mapping theorem (using completeness of `F`):
by Baire's theorem, there exists a ball in `E` whose image closure has nonempty interior.
Rescaling everything, it follows that any `y âˆˆ F` is arbitrarily well approached by
images of elements of norm at most `C * â€–yâ€–`.
For further use, we will only need such an element whose image
is within distance `â€–yâ€–/2` of `y`, to apply an iterative process. -/
theorem exists_approx_preimage_norm_le (surj : Surjective f) :
    âˆƒ C â‰¥ 0, âˆ€ y, âˆƒ x, dist (f x) y â‰¤ 1 / 2 * â€–yâ€– âˆ§ â€–xâ€– â‰¤ C * â€–yâ€– := by
  have A : â‹ƒ n : â„•, closure (f '' ball 0 n) = Set.univ := by
    refine Subset.antisymm (subset_univ _) fun y _ => ?_
    rcases surj y with âŸ¨x, hxâŸ©
    rcases exists_nat_gt â€–xâ€– with âŸ¨n, hnâŸ©
    refine mem_iUnion.2 âŸ¨n, subset_closure ?_âŸ©
    refine (mem_image _ _ _).2 âŸ¨x, âŸ¨?_, hxâŸ©âŸ©
    rwa [mem_ball, dist_eq_norm, sub_zero]
  have : âˆƒ (n : â„•) (x : _), x âˆˆ interior (closure (f '' ball 0 n)) :=
    nonempty_interior_of_iUnion_of_closed (fun n => isClosed_closure) A
  simp only [mem_interior_iff_mem_nhds, Metric.mem_nhds_iff] at this
  rcases this with âŸ¨n, a, Îµ, âŸ¨Îµpos, HâŸ©âŸ©
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  refine âŸ¨(Îµ / 2)â»Â¹ * â€–câ€– * 2 * n, by positivity, fun y => ?_âŸ©
  rcases eq_or_ne y 0 with rfl | hy
  Â· simp
  Â· have hc' : 1 < â€–Ïƒ câ€– := by simp only [RingHomIsometric.norm_map, hc]
    rcases rescale_to_shell hc' (half_pos Îµpos) hy with âŸ¨d, hd, ydlt, -, dinvâŸ©
    let Î´ := â€–dâ€– * â€–yâ€– / 4
    have Î´pos : 0 < Î´ := by positivity
    have : a + d â€¢ y âˆˆ ball a Îµ := by
      simp [dist_eq_norm, lt_of_le_of_lt ydlt.le (half_lt_self Îµpos)]
    rcases Metric.mem_closure_iff.1 (H this) _ Î´pos with âŸ¨zâ‚, zâ‚im, hâ‚âŸ©
    rcases (mem_image _ _ _).1 zâ‚im with âŸ¨xâ‚, hxâ‚, xzâ‚âŸ©
    rw [â† xzâ‚] at hâ‚
    rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚
    have : a âˆˆ ball a Îµ := by
      simp only [mem_ball, dist_self]
      exact Îµpos
    rcases Metric.mem_closure_iff.1 (H this) _ Î´pos with âŸ¨zâ‚‚, zâ‚‚im, hâ‚‚âŸ©
    rcases (mem_image _ _ _).1 zâ‚‚im with âŸ¨xâ‚‚, hxâ‚‚, xzâ‚‚âŸ©
    rw [â† xzâ‚‚] at hâ‚‚
    rw [mem_ball, dist_eq_norm, sub_zero] at hxâ‚‚
    let x := xâ‚ - xâ‚‚
    have I : â€–f x - d â€¢ yâ€– â‰¤ 2 * Î´ :=
      calc
        â€–f x - d â€¢ yâ€– = â€–f xâ‚ - (a + d â€¢ y) - (f xâ‚‚ - a)â€– := by
          congr 1
          simp only [x, f.map_sub]
          abel
        _ â‰¤ â€–f xâ‚ - (a + d â€¢ y)â€– + â€–f xâ‚‚ - aâ€– := norm_sub_le _ _
        _ â‰¤ Î´ + Î´ := by rw [dist_eq_norm'] at hâ‚ hâ‚‚; gcongr
        _ = 2 * Î´ := (two_mul _).symm
    have J : â€–f (Ïƒ' dâ»Â¹ â€¢ x) - yâ€– â‰¤ 1 / 2 * â€–yâ€– :=
      calc
        â€–f (Ïƒ' dâ»Â¹ â€¢ x) - yâ€– = â€–dâ»Â¹ â€¢ f x - (dâ»Â¹ * d) â€¢ yâ€– := by
          rwa [f.map_smulâ‚›â‚— _, inv_mul_cancelâ‚€, one_smul, map_invâ‚€, map_invâ‚€,
            RingHomCompTriple.comp_apply, RingHom.id_apply]
        _ = â€–dâ»Â¹ â€¢ (f x - d â€¢ y)â€– := by rw [mul_smul, smul_sub]
        _ = â€–dâ€–â»Â¹ * â€–f x - d â€¢ yâ€– := by rw [norm_smul, norm_inv]
        _ â‰¤ â€–dâ€–â»Â¹ * (2 * Î´) := by gcongr
        _ = â€–dâ€–â»Â¹ * â€–dâ€– * â€–yâ€– / 2 := by
          simp only [Î´]
          ring
        _ = â€–yâ€– / 2 := by
          simp [norm_eq_zero, hd]
        _ = 1 / 2 * â€–yâ€– := by ring
    rw [â† dist_eq_norm] at J
    have K : â€–Ïƒ' dâ»Â¹ â€¢ xâ€– â‰¤ (Îµ / 2)â»Â¹ * â€–câ€– * 2 * â†‘n * â€–yâ€– :=
      calc
        â€–Ïƒ' dâ»Â¹ â€¢ xâ€– = â€–dâ€–â»Â¹ * â€–xâ‚ - xâ‚‚â€– := by rw [norm_smul, RingHomIsometric.norm_map, norm_inv]
        _ â‰¤ (Îµ / 2)â»Â¹ * â€–câ€– * â€–yâ€– * (n + n) := by
          gcongr
          Â· simpa using dinv
          Â· exact le_trans (norm_sub_le _ _) (by gcongr)
        _ = (Îµ / 2)â»Â¹ * â€–câ€– * 2 * â†‘n * â€–yâ€– := by ring
    exact âŸ¨Ïƒ' dâ»Â¹ â€¢ x, J, KâŸ©

variable [CompleteSpace E]

section
include Ïƒ'

/-- The Banach open mapping theorem: if a bounded linear map between Banach spaces is onto, then
any point has a preimage with controlled norm. -/
theorem exists_preimage_norm_le (surj : Surjective f) :
    âˆƒ C > 0, âˆ€ y, âˆƒ x, f x = y âˆ§ â€–xâ€– â‰¤ C * â€–yâ€– := by
  obtain âŸ¨C, C0, hCâŸ© := exists_approx_preimage_norm_le f surj
  /- Second step of the proof: starting from `y`, we want an exact preimage of `y`. Let `g y` be
    the approximate preimage of `y` given by the first step, and `h y = y - f(g y)` the part that
    has no preimage yet. We will iterate this process, taking the approximate preimage of `h y`,
    leaving only `h^2 y` without preimage yet, and so on. Let `u n` be the approximate preimage
    of `h^n y`. Then `u` is a converging series, and by design the sum of the series is a
    preimage of `y`. This uses completeness of `E`. -/
  choose g hg using hC
  let h y := y - f (g y)
  have hle : âˆ€ y, â€–h yâ€– â‰¤ 1 / 2 * â€–yâ€– := by
    intro y
    rw [â† dist_eq_norm, dist_comm]
    exact (hg y).1
  refine âŸ¨2 * C + 1, by linarith, fun y => ?_âŸ©
  have hnle : âˆ€ n : â„•, â€–h^[n] yâ€– â‰¤ (1 / 2) ^ n * â€–yâ€– := by
    intro n
    induction n with
    | zero => simp only [one_div, one_mul, iterate_zero_apply, pow_zero, le_rfl]
    | succ n IH =>
      rw [iterate_succ']
      apply le_trans (hle _) _
      rw [pow_succ', mul_assoc]
      gcongr
  let u n := g (h^[n] y)
  have ule : âˆ€ n, â€–u nâ€– â‰¤ (1 / 2) ^ n * (C * â€–yâ€–) := fun n â†¦ by
    apply le_trans (hg _).2
    calc
      C * â€–h^[n] yâ€– â‰¤ C * ((1 / 2) ^ n * â€–yâ€–) := mul_le_mul_of_nonneg_left (hnle n) C0
      _ = (1 / 2) ^ n * (C * â€–yâ€–) := by ring
  have sNu : Summable fun n => â€–u nâ€– := by
    refine .of_nonneg_of_le (fun n => norm_nonneg _) ule ?_
    exact Summable.mul_right _ (summable_geometric_of_lt_one (by simp) (by norm_num))
  have su : Summable u := sNu.of_norm
  let x := tsum u
  have x_ineq : â€–xâ€– â‰¤ (2 * C + 1) * â€–yâ€– :=
    calc
      â€–xâ€– â‰¤ âˆ‘' n, â€–u nâ€– := norm_tsum_le_tsum_norm sNu
      _ â‰¤ âˆ‘' n, (1 / 2) ^ n * (C * â€–yâ€–) :=
        sNu.tsum_le_tsum ule <| Summable.mul_right _ summable_geometric_two
      _ = (âˆ‘' n, (1 / 2) ^ n) * (C * â€–yâ€–) := tsum_mul_right
      _ = 2 * C * â€–yâ€– := by rw [tsum_geometric_two, mul_assoc]
      _ â‰¤ 2 * C * â€–yâ€– + â€–yâ€– := le_add_of_nonneg_right (norm_nonneg y)
      _ = (2 * C + 1) * â€–yâ€– := by ring
  have fsumeq : âˆ€ n : â„•, f (âˆ‘ i âˆˆ Finset.range n, u i) = y - h^[n] y := by
    intro n
    induction n with
    | zero => simp [f.map_zero]
    | succ n IH => rw [sum_range_succ, f.map_add, IH, iterate_succ_apply', sub_add]
  have : Tendsto (fun n => âˆ‘ i âˆˆ Finset.range n, u i) atTop (ğ“ x) := su.hasSum.tendsto_sum_nat
  have Lâ‚ : Tendsto (fun n => f (âˆ‘ i âˆˆ Finset.range n, u i)) atTop (ğ“ (f x)) :=
    (f.continuous.tendsto _).comp this
  simp only [fsumeq] at Lâ‚
  have Lâ‚‚ : Tendsto (fun n => y - h^[n] y) atTop (ğ“ (y - 0)) := by
    refine tendsto_const_nhds.sub ?_
    rw [tendsto_iff_norm_sub_tendsto_zero]
    simp only [sub_zero]
    refine squeeze_zero (fun _ => norm_nonneg _) hnle ?_
    rw [â† zero_mul â€–yâ€–]
    refine (_root_.tendsto_pow_atTop_nhds_zero_of_lt_one ?_ ?_).mul tendsto_const_nhds <;> norm_num
  have feq : f x = y - 0 := tendsto_nhds_unique Lâ‚ Lâ‚‚
  rw [sub_zero] at feq
  exact âŸ¨x, feq, x_ineqâŸ©

/-- The Banach open mapping theorem: a surjective bounded linear map between Banach spaces is
open. -/
protected theorem isOpenMap (surj : Surjective f) : IsOpenMap f := by
  intro s hs
  rcases exists_preimage_norm_le f surj with âŸ¨C, Cpos, hCâŸ©
  refine isOpen_iff.2 fun y yfs => ?_
  rcases yfs with âŸ¨x, xs, fxyâŸ©
  rcases isOpen_iff.1 hs x xs with âŸ¨Îµ, Îµpos, hÎµâŸ©
  refine âŸ¨Îµ / C, div_pos Îµpos Cpos, fun z hz => ?_âŸ©
  rcases hC (z - y) with âŸ¨w, wim, wnormâŸ©
  have : f (x + w) = z := by rw [f.map_add, wim, fxy, add_sub_cancel]
  rw [â† this]
  have : x + w âˆˆ ball x Îµ :=
    calc
      dist (x + w) x = â€–wâ€– := by
        simp
      _ â‰¤ C * â€–z - yâ€– := wnorm
      _ < C * (Îµ / C) := by
        apply mul_lt_mul_of_pos_left _ Cpos
        rwa [mem_ball, dist_eq_norm] at hz
      _ = Îµ := mul_div_cancelâ‚€ _ (ne_of_gt Cpos)
  exact Set.mem_image_of_mem _ (hÎµ this)

theorem isQuotientMap (surj : Surjective f) : IsQuotientMap f :=
  (f.isOpenMap surj).isQuotientMap f.continuous surj

end

theorem _root_.AffineMap.isOpenMap {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    [CompleteSpace F] {P Q : Type*} [MetricSpace P] [NormedAddTorsor E P] [MetricSpace Q]
    [NormedAddTorsor F Q] (f : P â†’áµƒ[ğ•œ] Q) (hf : Continuous f) (surj : Surjective f) :
    IsOpenMap f :=
  AffineMap.isOpenMap_linear_iff.mp <|
    ContinuousLinearMap.isOpenMap { f.linear with cont := AffineMap.continuous_linear_iff.mpr hf }
      (f.linear_surjective_iff.mpr surj)

/-! ### Applications of the Banach open mapping theorem -/

section
include Ïƒ'

theorem interior_preimage (hsurj : Surjective f) (s : Set F) :
    interior (f â»Â¹' s) = f â»Â¹' interior s :=
  ((f.isOpenMap hsurj).preimage_interior_eq_interior_preimage f.continuous s).symm

theorem closure_preimage (hsurj : Surjective f) (s : Set F) : closure (f â»Â¹' s) = f â»Â¹' closure s :=
  ((f.isOpenMap hsurj).preimage_closure_eq_closure_preimage f.continuous s).symm

theorem frontier_preimage (hsurj : Surjective f) (s : Set F) :
    frontier (f â»Â¹' s) = f â»Â¹' frontier s :=
  ((f.isOpenMap hsurj).preimage_frontier_eq_frontier_preimage f.continuous s).symm

theorem exists_nonlinearRightInverse_of_surjective (f : E â†’SL[Ïƒ] F)
    (hsurj : LinearMap.range f = âŠ¤) :
    âˆƒ fsymm : NonlinearRightInverse f, 0 < fsymm.nnnorm := by
  choose C hC fsymm h using
    exists_preimage_norm_le _ (LinearMap.range_eq_top.1 hsurj)
  use {
      toFun := fsymm
      nnnorm := âŸ¨C, hC.lt.leâŸ©
      bound' := fun y => (h y).2
      right_inv' := fun y => (h y).1 }
  exact hC

end

/-- A surjective continuous linear map between Banach spaces admits a (possibly nonlinear)
controlled right inverse. In general, it is not possible to ensure that such a right inverse
is linear (take for instance the map from `E` to `E/F` where `F` is a closed subspace of `E`
without a closed complement. Then it doesn't have a continuous linear right inverse.) -/
noncomputable irreducible_def nonlinearRightInverseOfSurjective (f : E â†’SL[Ïƒ] F)
  (hsurj : LinearMap.range f = âŠ¤) : NonlinearRightInverse f :=
  Classical.choose (exists_nonlinearRightInverse_of_surjective f hsurj)

theorem nonlinearRightInverseOfSurjective_nnnorm_pos (f : E â†’SL[Ïƒ] F)
    (hsurj : LinearMap.range f = âŠ¤) : 0 < (nonlinearRightInverseOfSurjective f hsurj).nnnorm := by
  rw [nonlinearRightInverseOfSurjective]
  exact Classical.choose_spec (exists_nonlinearRightInverse_of_surjective f hsurj)

end ContinuousLinearMap

namespace LinearEquiv

variable [CompleteSpace E] [RingHomInvPair Ïƒ' Ïƒ]

/-- If a bounded linear map is a bijection, then its inverse is also a bounded linear map. -/
@[continuity]
theorem continuous_symm (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (h : Continuous e) : Continuous e.symm := by
  rw [continuous_def]
  intro s hs
  rw [â† e.image_eq_preimage]
  rw [â† e.coe_coe] at h âŠ¢
  exact ContinuousLinearMap.isOpenMap (Ïƒ := Ïƒ) âŸ¨_, hâŸ© e.surjective s hs

/-- Associating to a linear equivalence between Banach spaces a continuous linear equivalence when
the direct map is continuous, thanks to the Banach open mapping theorem that ensures that the
inverse map is also continuous. -/
def toContinuousLinearEquivOfContinuous (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (h : Continuous e) : E â‰ƒSL[Ïƒ] F :=
  { e with
    continuous_toFun := h
    continuous_invFun := e.continuous_symm h }

@[simp]
theorem coeFn_toContinuousLinearEquivOfContinuous (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (h : Continuous e) :
    â‡‘(e.toContinuousLinearEquivOfContinuous h) = e :=
  rfl

@[simp]
theorem coeFn_toContinuousLinearEquivOfContinuous_symm (e : E â‰ƒâ‚›â‚—[Ïƒ] F) (h : Continuous e) :
    â‡‘(e.toContinuousLinearEquivOfContinuous h).symm = e.symm :=
  rfl

end LinearEquiv

namespace ContinuousLinearMap

variable [CompleteSpace E] [RingHomInvPair Ïƒ' Ïƒ] {f : E â†’SL[Ïƒ] F}

/-- An injective continuous linear map with a closed range defines a continuous linear equivalence
between its domain and its range. -/
noncomputable def equivRange (hinj : Injective f) (hclo : IsClosed (range f)) :
    E â‰ƒSL[Ïƒ] LinearMap.range f :=
  have : CompleteSpace (LinearMap.range f) := hclo.completeSpace_coe
  LinearEquiv.toContinuousLinearEquivOfContinuous (LinearEquiv.ofInjective f.toLinearMap hinj) <|
    (f.continuous.codRestrict fun x â†¦ LinearMap.mem_range_self f x).congr fun _ â†¦ rfl

@[simp]
theorem coe_linearMap_equivRange (hinj : Injective f) (hclo : IsClosed (range f)) :
    f.equivRange hinj hclo = f.rangeRestrict :=
  rfl

@[simp]
theorem coe_equivRange (hinj : Injective f) (hclo : IsClosed (range f)) :
    (f.equivRange hinj hclo : E â†’ LinearMap.range f) = f.rangeRestrict :=
  rfl

@[simp]
lemma equivRange_symm_toLinearEquiv (hinj : Injective f) (hclo : IsClosed (range f)) :
    (f.equivRange hinj hclo).toLinearEquiv.symm =
      (LinearEquiv.ofInjective f.toLinearMap hinj).symm := rfl

@[simp]
lemma equivRange_symm_apply (hinj : Injective f) (hclo : IsClosed (range f))
    (x : E) : (f.equivRange hinj hclo).symm âŸ¨f x, by simpâŸ© = x := by
  suffices f ((f.equivRange hinj hclo).symm âŸ¨f x, by simpâŸ©) = f x from hinj this
  trans f ((f.equivRange hinj hclo).symm.toLinearEquiv âŸ¨f x, by simpâŸ©)
  Â· rfl -- is there an API lemma for this already?
  simp only [ContinuousLinearEquiv.toLinearEquiv_symm, equivRange_symm_toLinearEquiv]
  set x' : LinearMap.range f := âŸ¨f x, by simpâŸ©
  set f' : E â†’â‚›â‚—[Ïƒ] F := â†‘f
  change f' ((LinearEquiv.ofInjective f' hinj).symm x') = _
  rw [LinearEquiv.ofInjective_symm_apply (f := f') (h := hinj) x']

section

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [CompleteSpace E] [CompleteSpace F]

-- TODO: once mathlib has Fredholm operators, generalise the next two lemmas accordingly

/-- If `f : E â†’L[ğ•œ] F` is injective with closed range (and `E` and `F` are Banach spaces),
`f` is anti-Lipschitz. -/
lemma antilipschitz_of_injective_of_isClosed_range (f : E â†’L[ğ•œ] F)
    (hf : Injective f) (hf' : IsClosed (Set.range f)) : âˆƒ K, AntilipschitzWith K f := by
  let S : (LinearMap.range f) â†’L[ğ•œ] E := (f.equivRange hf hf').symm
  use âŸ¨S.opNorm, S.opNorm_nonnegâŸ©
  apply ContinuousLinearMap.antilipschitz_of_bound
  intro x
  calc â€–xâ€–
    _ = â€–S âŸ¨f x, by simpâŸ©â€– := by simp [S]
    _ â‰¤ S.opNorm * â€–f xâ€– := le_opNorm S âŸ¨f x, by simpâŸ©

/-- An injective bounded linear operator between Banach spaces has closed range
iff it is anti-Lipschitz. -/
lemma isClosed_range_iff_antilipschitz_of_injective (f : E â†’L[ğ•œ] F)
    (hf : Injective f) : IsClosed (Set.range f) â†” âˆƒ K, AntilipschitzWith K f := by
  refine âŸ¨fun h â†¦ f.antilipschitz_of_injective_of_isClosed_range hf h, fun h â†¦ ?_âŸ©
  choose K hf' using h
  exact hf'.isClosed_range f.uniformContinuous

end

end ContinuousLinearMap

namespace ContinuousLinearEquiv

variable [CompleteSpace E] [RingHomInvPair Ïƒ' Ïƒ]

/-- Convert a bijective continuous linear map `f : E â†’SL[Ïƒ] F` from a Banach space to a normed space
to a continuous linear equivalence. -/
noncomputable def ofBijective (f : E â†’SL[Ïƒ] F) (hinj : ker f = âŠ¥) (hsurj : LinearMap.range f = âŠ¤) :
    E â‰ƒSL[Ïƒ] F :=
  (LinearEquiv.ofBijective f
        âŸ¨LinearMap.ker_eq_bot.mp hinj,
          LinearMap.range_eq_top.mp hsurjâŸ©).toContinuousLinearEquivOfContinuous
    -- Porting note: `by exact` was not previously needed. Why is it needed now?
    (by exact f.continuous)

@[simp]
theorem coeFn_ofBijective (f : E â†’SL[Ïƒ] F) (hinj : ker f = âŠ¥) (hsurj : LinearMap.range f = âŠ¤) :
    â‡‘(ofBijective f hinj hsurj) = f :=
  rfl

theorem coe_ofBijective (f : E â†’SL[Ïƒ] F) (hinj : ker f = âŠ¥) (hsurj : LinearMap.range f = âŠ¤) :
    â†‘(ofBijective f hinj hsurj) = f := by
  ext
  rfl

@[simp]
theorem ofBijective_symm_apply_apply (f : E â†’SL[Ïƒ] F) (hinj : ker f = âŠ¥)
    (hsurj : LinearMap.range f = âŠ¤) (x : E) : (ofBijective f hinj hsurj).symm (f x) = x :=
  (ofBijective f hinj hsurj).symm_apply_apply x

@[simp]
theorem ofBijective_apply_symm_apply (f : E â†’SL[Ïƒ] F) (hinj : ker f = âŠ¥)
    (hsurj : LinearMap.range f = âŠ¤) (y : F) : f ((ofBijective f hinj hsurj).symm y) = y :=
  (ofBijective f hinj hsurj).apply_symm_apply y

lemma _root_.ContinuousLinearMap.isUnit_iff_bijective {f : E â†’L[ğ•œ] E} :
    IsUnit f â†” Bijective f := by
  constructor
  Â· rintro âŸ¨f, rflâŸ©
    exact ofUnit f |>.bijective
  Â· refine fun h â†¦ âŸ¨toUnit <| .ofBijective f ?_ ?_, rflâŸ© <;>
    simp only [LinearMap.range_eq_top, LinearMapClass.ker_eq_bot, h.1, h.2]

end ContinuousLinearEquiv

namespace ContinuousLinearMap

variable [CompleteSpace E]

/-- Intermediate definition used to show
`ContinuousLinearMap.closed_complemented_range_of_isCompl_of_ker_eq_bot`.

This is `f.coprod G.subtypeL` as a `ContinuousLinearEquiv`. -/
noncomputable def coprodSubtypeLEquivOfIsCompl {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
    [CompleteSpace F] (f : E â†’L[ğ•œ] F) {G : Submodule ğ•œ F}
    (h : IsCompl (LinearMap.range f) G) [CompleteSpace G] (hker : ker f = âŠ¥) : (E Ã— G) â‰ƒL[ğ•œ] F :=
  ContinuousLinearEquiv.ofBijective (f.coprod G.subtypeL)
    (by
      rw [ker_coprod_of_disjoint_range]
      Â· rw [hker, Submodule.ker_subtypeL, Submodule.prod_bot]
      Â· rw [Submodule.range_subtypeL]
        exact h.disjoint)
    (by simp only [range_coprod, Submodule.range_subtypeL, h.sup_eq_top])

theorem range_eq_map_coprodSubtypeLEquivOfIsCompl {F : Type*} [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] (f : E â†’L[ğ•œ] F) {G : Submodule ğ•œ F}
    (h : IsCompl (LinearMap.range f) G) [CompleteSpace G] (hker : ker f = âŠ¥) :
    LinearMap.range f =
      ((âŠ¤ : Submodule ğ•œ E).prod (âŠ¥ : Submodule ğ•œ G)).map
        (f.coprodSubtypeLEquivOfIsCompl h hker : E Ã— G â†’â‚—[ğ•œ] F) := by
  rw [coprodSubtypeLEquivOfIsCompl, ContinuousLinearEquiv.coe_ofBijective,
    coe_coprod, LinearMap.coprod_map_prod, Submodule.map_bot, sup_bot_eq, Submodule.map_top]
  rfl

/- TODO: remove the assumption `f.ker = âŠ¥` in the next lemma, by using the map induced by `f` on
`E / f.ker`, once we have quotient normed spaces. -/
theorem closed_complemented_range_of_isCompl_of_ker_eq_bot {F : Type*} [NormedAddCommGroup F]
    [NormedSpace ğ•œ F] [CompleteSpace F] (f : E â†’L[ğ•œ] F) (G : Submodule ğ•œ F)
    (h : IsCompl (LinearMap.range f) G) (hG : IsClosed (G : Set F)) (hker : ker f = âŠ¥) :
    IsClosed (LinearMap.range f : Set F) := by
  haveI : CompleteSpace G := hG.completeSpace_coe
  let g := coprodSubtypeLEquivOfIsCompl f h hker
  rw [range_eq_map_coprodSubtypeLEquivOfIsCompl f h hker]
  apply g.toHomeomorph.isClosed_image.2
  exact isClosed_univ.prod isClosed_singleton

end ContinuousLinearMap

section ClosedGraphThm

variable [CompleteSpace E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F] [CompleteSpace F] (g : E â†’â‚—[ğ•œ] F)

/-- The **closed graph theorem** : a linear map between two Banach spaces whose graph is closed
is continuous. -/
theorem LinearMap.continuous_of_isClosed_graph (hg : IsClosed (g.graph : Set <| E Ã— F)) :
    Continuous g := by
  letI : CompleteSpace g.graph := completeSpace_coe_iff_isComplete.mpr hg.isComplete
  let Ï†â‚€ : E â†’â‚—[ğ•œ] E Ã— F := LinearMap.id.prod g
  have : Function.LeftInverse Prod.fst Ï†â‚€ := fun x => rfl
  let Ï† : E â‰ƒâ‚—[ğ•œ] g.graph :=
    (LinearEquiv.ofLeftInverse this).trans (LinearEquiv.ofEq _ _ g.graph_eq_range_prod.symm)
  let Ïˆ : g.graph â‰ƒL[ğ•œ] E :=
    Ï†.symm.toContinuousLinearEquivOfContinuous continuous_subtype_val.fst
  exact (continuous_subtype_val.comp Ïˆ.symm.continuous).snd

/-- A useful form of the **closed graph theorem** : let `f` be a linear map between two Banach
spaces. To show that `f` is continuous, it suffices to show that for any convergent sequence
`uâ‚™ âŸ¶ x`, if `f(uâ‚™) âŸ¶ y` then `y = f(x)`. -/
theorem LinearMap.continuous_of_seq_closed_graph
    (hg : âˆ€ (u : â„• â†’ E) (x y), Tendsto u atTop (ğ“ x) â†’ Tendsto (g âˆ˜ u) atTop (ğ“ y) â†’ y = g x) :
    Continuous g := by
  refine g.continuous_of_isClosed_graph (IsSeqClosed.isClosed ?_)
  rintro Ï† âŸ¨x, yâŸ© hÏ†g hÏ†
  refine hg (Prod.fst âˆ˜ Ï†) x y ((continuous_fst.tendsto _).comp hÏ†) ?_
  have : g âˆ˜ Prod.fst âˆ˜ Ï† = Prod.snd âˆ˜ Ï† := by
    ext n
    exact (hÏ†g n).symm
  rw [this]
  exact (continuous_snd.tendsto _).comp hÏ†

variable {g}

namespace ContinuousLinearMap

/-- Upgrade a `LinearMap` to a `ContinuousLinearMap` using the **closed graph theorem**. -/
def ofIsClosedGraph (hg : IsClosed (g.graph : Set <| E Ã— F)) : E â†’L[ğ•œ] F where
  toLinearMap := g
  cont := g.continuous_of_isClosed_graph hg

@[simp]
theorem coeFn_ofIsClosedGraph (hg : IsClosed (g.graph : Set <| E Ã— F)) :
    â‡‘(ContinuousLinearMap.ofIsClosedGraph hg) = g :=
  rfl

theorem coe_ofIsClosedGraph (hg : IsClosed (g.graph : Set <| E Ã— F)) :
    â†‘(ContinuousLinearMap.ofIsClosedGraph hg) = g := by
  ext
  rfl

/-- Upgrade a `LinearMap` to a `ContinuousLinearMap` using a variation on the
**closed graph theorem**. -/
def ofSeqClosedGraph
    (hg : âˆ€ (u : â„• â†’ E) (x y), Tendsto u atTop (ğ“ x) â†’ Tendsto (g âˆ˜ u) atTop (ğ“ y) â†’ y = g x) :
    E â†’L[ğ•œ] F where
  toLinearMap := g
  cont := g.continuous_of_seq_closed_graph hg

@[simp]
theorem coeFn_ofSeqClosedGraph
    (hg : âˆ€ (u : â„• â†’ E) (x y), Tendsto u atTop (ğ“ x) â†’ Tendsto (g âˆ˜ u) atTop (ğ“ y) â†’ y = g x) :
    â‡‘(ContinuousLinearMap.ofSeqClosedGraph hg) = g :=
  rfl

theorem coe_ofSeqClosedGraph
    (hg : âˆ€ (u : â„• â†’ E) (x y), Tendsto u atTop (ğ“ x) â†’ Tendsto (g âˆ˜ u) atTop (ğ“ y) â†’ y = g x) :
    â†‘(ContinuousLinearMap.ofSeqClosedGraph hg) = g := by
  ext
  rfl

end ContinuousLinearMap

end ClosedGraphThm

section BijectivityCriteria

namespace ContinuousLinearMap

variable {Ïƒ : ğ•œ â†’+* ğ•œ'} {Ïƒ' : ğ•œ' â†’+* ğ•œ} [RingHomInvPair Ïƒ Ïƒ']
variable {F : Type u_4} [NormedAddCommGroup F] [NormedSpace ğ•œ' F]
variable [CompleteSpace E]

lemma closed_range_of_antilipschitz {f : E â†’SL[Ïƒ] F} {c : â„â‰¥0} (hf : AntilipschitzWith c f) :
    (LinearMap.range f).topologicalClosure = LinearMap.range f :=
  SetLike.ext'_iff.mpr <| (hf.isClosed_range f.uniformContinuous).closure_eq

variable [CompleteSpace F]

lemma _root_.AntilipschitzWith.completeSpace_range_clm {f : E â†’SL[Ïƒ] F} {c : â„â‰¥0}
    (hf : AntilipschitzWith c f) : CompleteSpace (LinearMap.range f) :=
  IsClosed.completeSpace_coe (hs := hf.isClosed_range f.uniformContinuous)

variable [RingHomInvPair Ïƒ' Ïƒ] [RingHomIsometric Ïƒ] [RingHomIsometric Ïƒ']

open Function
lemma bijective_iff_dense_range_and_antilipschitz (f : E â†’SL[Ïƒ] F) :
    Bijective f â†” (LinearMap.range f).topologicalClosure = âŠ¤ âˆ§ âˆƒ c, AntilipschitzWith c f := by
  refine âŸ¨fun h â†¦ âŸ¨?eq_top, ?antiâŸ©, fun âŸ¨hd, c, hfâŸ© â†¦ âŸ¨hf.injective, ?surjâŸ©âŸ©
  case eq_top => simpa [SetLike.ext'_iff] using h.2.denseRange.closure_eq
  case anti =>
    refine âŸ¨_, ContinuousLinearEquiv.ofBijective f ?_ ?_ |>.antilipschitzâŸ© <;>
    simp only [LinearMap.range_eq_top, LinearMapClass.ker_eq_bot, h.1, h.2]
  case surj => rwa [â† LinearMap.range_eq_top, â† closed_range_of_antilipschitz hf]

end ContinuousLinearMap

end BijectivityCriteria
