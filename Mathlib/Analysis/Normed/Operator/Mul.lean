/-
Copyright (c) 2019 Jan-David Salchow. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jan-David Salchow, SÃ©bastien GouÃ«zel, Jean Lo
-/
import Mathlib.Algebra.Algebra.Bilinear
import Mathlib.Analysis.Normed.Operator.NormedSpace

/-!
# Results about operator norms in normed algebras

This file (split off from `OperatorNorm.lean`) contains results about the operator norm
of multiplication and scalar-multiplication operations in normed algebras and normed modules.
-/

suppress_compilation

open Metric
open scoped NNReal Topology Uniformity

variable {ğ•œ E : Type*} [NontriviallyNormedField ğ•œ]

section SemiNormed

variable [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]

namespace ContinuousLinearMap

section MultiplicationLinear

section NonUnital

variable (ğ•œ) (R : Type*) [NonUnitalSeminormedRing R]
variable [NormedSpace ğ•œ R] [IsScalarTower ğ•œ R R] [SMulCommClass ğ•œ R R]

/-- Multiplication in a non-unital normed algebra as a continuous bilinear map. -/
def mul : R â†’L[ğ•œ] R â†’L[ğ•œ] R :=
  (LinearMap.mul ğ•œ R).mkContinuousâ‚‚ 1 fun x y => by simpa using norm_mul_le x y

@[simp]
theorem mul_apply' (x y : R) : mul ğ•œ R x y = x * y :=
  rfl

@[simp]
theorem opNorm_mul_apply_le (x : R) : â€–mul ğ•œ R xâ€– â‰¤ â€–xâ€– :=
  opNorm_le_bound _ (norm_nonneg x) (norm_mul_le x)


theorem opNorm_mul_le : â€–mul ğ•œ Râ€– â‰¤ 1 :=
  LinearMap.mkContinuousâ‚‚_norm_le _ zero_le_one _


/-- Multiplication on the left in a non-unital normed algebra `R` as a non-unital algebra
homomorphism into the algebra of *continuous* linear maps. This is the left regular representation
of `A` acting on itself.

This has more algebraic structure than `ContinuousLinearMap.mul`, but there is no longer continuity
bundled in the first coordinate.  An alternative viewpoint is that this upgrades
`NonUnitalAlgHom.lmul` from a homomorphism into linear maps to a homomorphism into *continuous*
linear maps. -/
def _root_.NonUnitalAlgHom.Lmul : R â†’â‚™â‚[ğ•œ] R â†’L[ğ•œ] R :=
  { mul ğ•œ R with
    map_mul' := fun _ _ â†¦ ext fun _ â†¦ mul_assoc _ _ _
    map_zero' := ext fun _ â†¦ zero_mul _ }

variable {ğ•œ R} in
@[simp]
theorem _root_.NonUnitalAlgHom.coe_Lmul : â‡‘(NonUnitalAlgHom.Lmul ğ•œ R) = mul ğ•œ R :=
  rfl

/-- Simultaneous left- and right-multiplication in a non-unital normed algebra, considered as a
continuous trilinear map. This is akin to its non-continuous version `LinearMap.mulLeftRight`,
but there is a minor difference: `LinearMap.mulLeftRight` is uncurried. -/
def mulLeftRight : R â†’L[ğ•œ] R â†’L[ğ•œ] R â†’L[ğ•œ] R :=
  ((compL ğ•œ R R R).comp (mul ğ•œ R).flip).flip.comp (mul ğ•œ R)

@[simp]
theorem mulLeftRight_apply (x y z : R) : mulLeftRight ğ•œ R x y z = x * z * y :=
  rfl

theorem opNorm_mulLeftRight_apply_apply_le (x y : R) : â€–mulLeftRight ğ•œ R x yâ€– â‰¤ â€–xâ€– * â€–yâ€– :=
  (opNorm_comp_le _ _).trans <|
    (mul_comm _ _).trans_le <|
      mul_le_mul (opNorm_mul_apply_le _ _ _)
        (opNorm_le_bound _ (norm_nonneg _) fun _ => (norm_mul_le _ _).trans_eq (mul_comm _ _))
        (norm_nonneg _) (norm_nonneg _)

theorem opNorm_mulLeftRight_apply_le (x : R) : â€–mulLeftRight ğ•œ R xâ€– â‰¤ â€–xâ€– :=
  opNorm_le_bound _ (norm_nonneg x) (opNorm_mulLeftRight_apply_apply_le ğ•œ R x)

theorem opNorm_mulLeftRight_le :
    â€–mulLeftRight ğ•œ Râ€– â‰¤ 1 :=
  opNorm_le_bound _ zero_le_one fun x => (one_mul â€–xâ€–).symm â–¸ opNorm_mulLeftRight_apply_le ğ•œ R x


/-- This is a mixin class for non-unital normed algebras which states that the left-regular
representation of the algebra on itself is isometric. Every unital normed algebra with `â€–1â€– = 1` is
a regular normed algebra (see `NormedAlgebra.instRegularNormedAlgebra`). In addition, so is every
Câ‹†-algebra, non-unital included (see `CStarRing.instRegularNormedAlgebra`), but there are yet other
examples. Any algebra with an approximate identity (e.g., $$L^1$$) is also regular.

This is a useful class because it gives rise to a nice norm on the unitization; in particular it is
a Câ‹†-norm when the norm on `A` is a Câ‹†-norm. -/
class _root_.RegularNormedAlgebra : Prop where
  /-- The left regular representation of the algebra on itself is an isometry. -/
  isometry_mul' : Isometry (mul ğ•œ R)

/-- Every (unital) normed algebra such that `â€–1â€– = 1` is a `RegularNormedAlgebra`. -/
instance _root_.NormedAlgebra.instRegularNormedAlgebra {ğ•œ R : Type*} [NontriviallyNormedField ğ•œ]
    [SeminormedRing R] [NormedAlgebra ğ•œ R] [NormOneClass R] : RegularNormedAlgebra ğ•œ R where
  isometry_mul' := AddMonoidHomClass.isometry_of_norm (mul ğ•œ R) <|
    fun x => le_antisymm (opNorm_mul_apply_le _ _ _) <| by
      convert ratio_le_opNorm ((mul ğ•œ R) x) (1 : R)
      simp [norm_one]

variable [RegularNormedAlgebra ğ•œ R]

lemma isometry_mul : Isometry (mul ğ•œ R) :=
  RegularNormedAlgebra.isometry_mul'

@[simp]
lemma opNorm_mul_apply (x : R) : â€–mul ğ•œ R xâ€– = â€–xâ€– :=
  (AddMonoidHomClass.isometry_iff_norm (mul ğ•œ R)).mp (isometry_mul ğ•œ R) x


@[simp]
lemma opNNNorm_mul_apply (x : R) : â€–mul ğ•œ R xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| opNorm_mul_apply ğ•œ R x


/-- Multiplication in a normed algebra as a linear isometry to the space of
continuous linear maps. -/
def mulâ‚—áµ¢ : R â†’â‚—áµ¢[ğ•œ] R â†’L[ğ•œ] R where
  toLinearMap := mul ğ•œ R
  norm_map' x := opNorm_mul_apply ğ•œ R x

@[simp]
theorem coe_mulâ‚—áµ¢ : â‡‘(mulâ‚—áµ¢ ğ•œ R) = mul ğ•œ R :=
  rfl

end NonUnital

section NonUnitalSeminormedCommRing
variable {R : Type*} [NonUnitalSeminormedCommRing R] [NormedSpace ğ•œ R] [IsScalarTower ğ•œ R R]
  [SMulCommClass ğ•œ R R]

@[simp] lemma flip_mul : (ContinuousLinearMap.mul ğ•œ R).flip = .mul ğ•œ R := by ext; simp [mul_comm]

end NonUnitalSeminormedCommRing

section RingEquiv

variable (ğ•œ E)

/-- If `M` is a normed space over `ğ•œ`, then the space of maps `ğ•œ â†’L[ğ•œ] M` is linearly equivalent
to `M`. (See `ring_lmap_equiv_self` for a stronger statement.) -/
def ring_lmap_equiv_selfâ‚— : (ğ•œ â†’L[ğ•œ] E) â‰ƒâ‚—[ğ•œ] E where
  toFun := fun f â†¦ f 1
  invFun := (ContinuousLinearMap.id ğ•œ ğ•œ).smulRight
  map_smul' := fun a f â†¦ by simp only [coe_smul', Pi.smul_apply, RingHom.id_apply]
  map_add' := fun f g â†¦ by simp only [add_apply]
  left_inv := fun f â†¦ by ext; simp only [smulRight_apply, coe_id', _root_.id, one_smul]
  right_inv := fun m â†¦ by simp only [smulRight_apply, id_apply, one_smul]

/-- If `M` is a normed space over `ğ•œ`, then the space of maps `ğ•œ â†’L[ğ•œ] M` is linearly isometrically
equivalent to `M`. -/
def ring_lmap_equiv_self : (ğ•œ â†’L[ğ•œ] E) â‰ƒâ‚—áµ¢[ğ•œ] E where
  toLinearEquiv := ring_lmap_equiv_selfâ‚— ğ•œ E
  norm_map' := by
    refine fun f â†¦ le_antisymm ?_ ?_
    Â· simpa only [norm_one, mul_one] using le_opNorm f 1
    Â· refine opNorm_le_bound' f (norm_nonneg <| f 1) (fun x _ â†¦ ?_)
      rw [(by rw [smul_eq_mul, mul_one] : f x = f (x â€¢ 1)), ContinuousLinearMap.map_smul,
        norm_smul, mul_comm, (by rfl : ring_lmap_equiv_selfâ‚— ğ•œ E f = f 1)]

end RingEquiv

end MultiplicationLinear

section SMulLinear

variable (ğ•œ) (R : Type*) [NormedField R]
variable [NormedAlgebra ğ•œ R] [NormedSpace R E] [IsScalarTower ğ•œ R E]

/-- Scalar multiplication as a continuous bilinear map. -/
def lsmul : R â†’L[ğ•œ] E â†’L[ğ•œ] E :=
  ((Algebra.lsmul ğ•œ ğ•œ E).toLinearMap : R â†’â‚—[ğ•œ] E â†’â‚—[ğ•œ] E).mkContinuousâ‚‚ 1 fun c x => by
    simpa only [one_mul] using norm_smul_le c x

@[simp]
theorem lsmul_apply (c : R) (x : E) : lsmul ğ•œ R c x = c â€¢ x :=
  rfl

variable {ğ•œ} in
@[simp]
theorem lsmul_flip_apply (x : E) :
    (lsmul ğ•œ ğ•œ).flip x = toSpanSingleton ğ•œ x :=
  rfl

@[deprecated (since := "29-08-2025")] alias comp_lsmul_flip_apply := comp_toSpanSingleton

variable {ğ•œ} in
theorem lsmul_flip_inj {x y : E} :
    (lsmul ğ•œ R).flip x = (lsmul ğ•œ R).flip y â†” x = y :=
  âŸ¨fun h => by simpa using congr($h 1), fun h => h â–¸ rflâŸ©

variable {R}

theorem norm_toSpanSingleton (x : E) : â€–toSpanSingleton ğ•œ xâ€– = â€–xâ€– := by
  refine opNorm_eq_of_bounds (norm_nonneg _) (fun x => ?_) fun N _ h => ?_
  Â· rw [toSpanSingleton_apply, norm_smul, mul_comm]
  Â· simpa [toSpanSingleton_apply, norm_smul] using h 1

variable {ğ•œ}

theorem opNorm_lsmul_apply_le (x : R) : â€–(lsmul ğ•œ R x : E â†’L[ğ•œ] E)â€– â‰¤ â€–xâ€– :=
  ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg x) fun y => norm_smul_le x y


/-- The norm of `lsmul` is at most 1 in any semi-normed group. -/
theorem opNorm_lsmul_le : â€–(lsmul ğ•œ R : R â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– â‰¤ 1 := by
  refine ContinuousLinearMap.opNorm_le_bound _ zero_le_one fun x => ?_
  simp_rw [one_mul]
  exact opNorm_lsmul_apply_le _

end SMulLinear

end ContinuousLinearMap

end SemiNormed

section Normed

namespace ContinuousLinearMap

variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable (ğ•œ) (R : Type*)

section

variable [NonUnitalNormedRing R] [NormedSpace ğ•œ R] [IsScalarTower ğ•œ R R]
variable [SMulCommClass ğ•œ R R] [RegularNormedAlgebra ğ•œ R] [Nontrivial R]

@[simp]
theorem opNorm_mul : â€–mul ğ•œ Râ€– = 1 :=
  (mulâ‚—áµ¢ ğ•œ R).norm_toContinuousLinearMap


@[simp]
theorem opNNNorm_mul : â€–mul ğ•œ Râ€–â‚Š = 1 :=
  Subtype.ext <| opNorm_mul ğ•œ R


end

/-- The norm of `lsmul` equals 1 in any nontrivial normed group.

This is `ContinuousLinearMap.opNorm_lsmul_le` as an equality. -/
@[simp]
theorem opNorm_lsmul [NormedField R] [NormedAlgebra ğ•œ R] [NormedSpace R E]
    [IsScalarTower ğ•œ R E] [Nontrivial E] : â€–(lsmul ğ•œ R : R â†’L[ğ•œ] E â†’L[ğ•œ] E)â€– = 1 := by
  refine ContinuousLinearMap.opNorm_eq_of_bounds zero_le_one (fun x => ?_) fun N _ h => ?_
  Â· rw [one_mul]
    apply opNorm_lsmul_apply_le
  obtain âŸ¨y, hyâŸ© := exists_ne (0 : E)
  refine le_of_mul_le_mul_right ?_ (norm_pos_iff.mpr hy)
  simpa using le_of_opNorm_le _ (h 1) y

end ContinuousLinearMap

end Normed
