/-
Copyright (c) 2023 Eric Wieser. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Eric Wieser
-/
import Mathlib.Topology.MetricSpace.Algebra
import Mathlib.Analysis.Normed.Field.Basic

#align_import analysis.normed.mul_action from "leanprover-community/mathlib"@"bc91ed7093bf098d253401e69df601fc33dde156"

/-!
# Lemmas for `BoundedSMul` over normed additive groups

Lemmas which hold only in `NormedSpace Œ± Œ≤` are provided in another file.

Notably we prove that `NonUnitalSeminormedRing`s have bounded actions by left- and right-
multiplication. This allows downstream files to write general results about `BoundedSMul`, and then
deduce `const_mul` and `mul_const` results as an immediate corollary.
-/


variable {Œ± Œ≤ : Type*}

section SeminormedAddGroup

variable [SeminormedAddGroup Œ±] [SeminormedAddGroup Œ≤] [SMulZeroClass Œ± Œ≤]

variable [BoundedSMul Œ± Œ≤]

theorem norm_smul_le (r : Œ±) (x : Œ≤) : ‚Äñr ‚Ä¢ x‚Äñ ‚â§ ‚Äñr‚Äñ * ‚Äñx‚Äñ := by
  simpa [smul_zero] using dist_smul_pair r 0 x
  -- üéâ no goals
#align norm_smul_le norm_smul_le

theorem nnnorm_smul_le (r : Œ±) (x : Œ≤) : ‚Äñr ‚Ä¢ x‚Äñ‚Çä ‚â§ ‚Äñr‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä :=
  norm_smul_le _ _
#align nnnorm_smul_le nnnorm_smul_le

theorem dist_smul_le (s : Œ±) (x y : Œ≤) : dist (s ‚Ä¢ x) (s ‚Ä¢ y) ‚â§ ‚Äñs‚Äñ * dist x y := by
  simpa only [dist_eq_norm, sub_zero] using dist_smul_pair s x y
  -- üéâ no goals
#align dist_smul_le dist_smul_le

theorem nndist_smul_le (s : Œ±) (x y : Œ≤) : nndist (s ‚Ä¢ x) (s ‚Ä¢ y) ‚â§ ‚Äñs‚Äñ‚Çä * nndist x y :=
  dist_smul_le s x y
#align nndist_smul_le nndist_smul_le

theorem lipschitzWith_smul (s : Œ±) : LipschitzWith ‚Äñs‚Äñ‚Çä ((¬∑ ‚Ä¢ ¬∑) s : Œ≤ ‚Üí Œ≤) :=
  lipschitzWith_iff_dist_le_mul.2 <| dist_smul_le _
#align lipschitz_with_smul lipschitzWith_smul

theorem edist_smul_le (s : Œ±) (x y : Œ≤) : edist (s ‚Ä¢ x) (s ‚Ä¢ y) ‚â§ ‚Äñs‚Äñ‚Çä ‚Ä¢ edist x y :=
  lipschitzWith_smul s x y
#align edist_smul_le edist_smul_le

end SeminormedAddGroup

/-- Left multiplication is bounded. -/
instance NonUnitalSeminormedRing.to_boundedSMul [NonUnitalSeminormedRing Œ±] : BoundedSMul Œ± Œ± where
  dist_smul_pair' x y‚ÇÅ y‚ÇÇ := by simpa [mul_sub, dist_eq_norm] using norm_mul_le x (y‚ÇÅ - y‚ÇÇ)
                                -- üéâ no goals
  dist_pair_smul' x‚ÇÅ x‚ÇÇ y := by simpa [sub_mul, dist_eq_norm] using norm_mul_le (x‚ÇÅ - x‚ÇÇ) y
                                -- üéâ no goals
#align non_unital_semi_normed_ring.to_has_bounded_smul NonUnitalSeminormedRing.to_boundedSMul

/-- Right multiplication is bounded. -/
instance NonUnitalSeminormedRing.to_has_bounded_op_smul [NonUnitalSeminormedRing Œ±] :
    BoundedSMul Œ±·µê·µí·µñ Œ± where
  dist_smul_pair' x y‚ÇÅ y‚ÇÇ := by
    simpa [sub_mul, dist_eq_norm, mul_comm] using norm_mul_le (y‚ÇÅ - y‚ÇÇ) x.unop
    -- üéâ no goals
  dist_pair_smul' x‚ÇÅ x‚ÇÇ y := by
    simpa [mul_sub, dist_eq_norm, mul_comm] using norm_mul_le y (x‚ÇÅ - x‚ÇÇ).unop
    -- üéâ no goals
#align non_unital_semi_normed_ring.to_has_bounded_op_smul NonUnitalSeminormedRing.to_has_bounded_op_smul

section SeminormedRing

variable [SeminormedRing Œ±] [SeminormedAddCommGroup Œ≤] [Module Œ± Œ≤]

theorem BoundedSMul.of_norm_smul_le (h : ‚àÄ (r : Œ±) (x : Œ≤), ‚Äñr ‚Ä¢ x‚Äñ ‚â§ ‚Äñr‚Äñ * ‚Äñx‚Äñ) :
    BoundedSMul Œ± Œ≤ :=
  { dist_smul_pair' := fun a b‚ÇÅ b‚ÇÇ => by simpa [smul_sub, dist_eq_norm] using h a (b‚ÇÅ - b‚ÇÇ)
                                         -- üéâ no goals
    dist_pair_smul' := fun a‚ÇÅ a‚ÇÇ b => by simpa [sub_smul, dist_eq_norm] using h (a‚ÇÅ - a‚ÇÇ) b }
                                         -- üéâ no goals
#align has_bounded_smul.of_norm_smul_le BoundedSMul.of_norm_smul_le

end SeminormedRing

section NormedDivisionRing

variable [NormedDivisionRing Œ±] [SeminormedAddGroup Œ≤]

variable [MulActionWithZero Œ± Œ≤] [BoundedSMul Œ± Œ≤]

theorem norm_smul (r : Œ±) (x : Œ≤) : ‚Äñr ‚Ä¢ x‚Äñ = ‚Äñr‚Äñ * ‚Äñx‚Äñ := by
  by_cases h : r = 0
  -- ‚ä¢ ‚Äñr ‚Ä¢ x‚Äñ = ‚Äñr‚Äñ * ‚Äñx‚Äñ
  ¬∑ simp [h, zero_smul Œ± x]
    -- üéâ no goals
  ¬∑ refine' le_antisymm (norm_smul_le r x) _
    -- ‚ä¢ ‚Äñr‚Äñ * ‚Äñx‚Äñ ‚â§ ‚Äñr ‚Ä¢ x‚Äñ
    calc
      ‚Äñr‚Äñ * ‚Äñx‚Äñ = ‚Äñr‚Äñ * ‚Äñr‚Åª¬π ‚Ä¢ r ‚Ä¢ x‚Äñ := by rw [inv_smul_smul‚ÇÄ h]
      _ ‚â§ ‚Äñr‚Äñ * (‚Äñr‚Åª¬π‚Äñ * ‚Äñr ‚Ä¢ x‚Äñ) := by gcongr; apply norm_smul_le
      _ = ‚Äñr ‚Ä¢ x‚Äñ := by rw [norm_inv, ‚Üê mul_assoc, mul_inv_cancel (mt norm_eq_zero.1 h), one_mul]
#align norm_smul norm_smul

theorem nnnorm_smul (r : Œ±) (x : Œ≤) : ‚Äñr ‚Ä¢ x‚Äñ‚Çä = ‚Äñr‚Äñ‚Çä * ‚Äñx‚Äñ‚Çä :=
  NNReal.eq <| norm_smul r x
#align nnnorm_smul nnnorm_smul

end NormedDivisionRing

section NormedDivisionRingModule

variable [NormedDivisionRing Œ±] [SeminormedAddCommGroup Œ≤]

variable [Module Œ± Œ≤] [BoundedSMul Œ± Œ≤]

theorem dist_smul‚ÇÄ (s : Œ±) (x y : Œ≤) : dist (s ‚Ä¢ x) (s ‚Ä¢ y) = ‚Äñs‚Äñ * dist x y := by
  simp_rw [dist_eq_norm, (norm_smul s (x - y)).symm, smul_sub]
  -- üéâ no goals
#align dist_smul‚ÇÄ dist_smul‚ÇÄ

theorem nndist_smul‚ÇÄ (s : Œ±) (x y : Œ≤) : nndist (s ‚Ä¢ x) (s ‚Ä¢ y) = ‚Äñs‚Äñ‚Çä * nndist x y :=
  NNReal.eq <| dist_smul‚ÇÄ s x y
#align nndist_smul‚ÇÄ nndist_smul‚ÇÄ

theorem edist_smul‚ÇÄ (s : Œ±) (x y : Œ≤) : edist (s ‚Ä¢ x) (s ‚Ä¢ y) = ‚Äñs‚Äñ‚Çä ‚Ä¢ edist x y := by
  simp only [edist_nndist, nndist_smul‚ÇÄ, ENNReal.coe_mul, ENNReal.smul_def, smul_eq_mul]
  -- üéâ no goals
#align edist_smul‚ÇÄ edist_smul‚ÇÄ

end NormedDivisionRingModule
