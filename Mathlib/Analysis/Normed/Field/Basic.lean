/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import Mathlib.Algebra.Field.Subfield.Defs
import Mathlib.Algebra.Order.Group.Pointwise.Interval
import Mathlib.Analysis.Normed.Ring.Basic

/-!
# Normed division rings and fields

In this file we define normed fields, and (more generally) normed division rings. We also prove
some theorems about these definitions.

Some useful results that relate the topology of the normed field to the discrete topology include:
* `norm_eq_one_iff_ne_zero_of_discrete`

Methods for constructing a normed field instance from a given real absolute value on a field are
given in:
* AbsoluteValue.toNormedField
-/

-- Guard against import creep.
assert_not_exists AddChar comap_norm_atTop DilationEquiv Finset.sup_mul_le_mul_sup_of_nonneg
  IsOfFinOrder Isometry.norm_map_of_map_one NNReal.isOpen_Ico_zero Rat.norm_cast_real
  RestrictScalars

variable {G Œ± Œ≤ Œπ : Type*}

open Filter
open scoped Topology NNReal

/-- A normed division ring is a division ring endowed with a seminorm which satisfies the equality
`‚Äñx y‚Äñ = ‚Äñx‚Äñ ‚Äñy‚Äñ`. -/
class NormedDivisionRing (Œ± : Type*) extends Norm Œ±, DivisionRing Œ±, MetricSpace Œ± where
  /-- The distance is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = norm (x - y)
  /-- The norm is multiplicative. -/
  protected norm_mul : ‚àÄ a b, norm (a * b) = norm a * norm b

-- see Note [lower instance priority]
/-- A normed division ring is a normed ring. -/
instance (priority := 100) NormedDivisionRing.toNormedRing [Œ≤ : NormedDivisionRing Œ±] :
    NormedRing Œ± :=
  { Œ≤ with norm_mul_le a b := (NormedDivisionRing.norm_mul a b).le }

section NormedDivisionRing

variable [NormedDivisionRing Œ±] {a b : Œ±}

@[simp]
theorem norm_mul (a b : Œ±) : ‚Äña * b‚Äñ = ‚Äña‚Äñ * ‚Äñb‚Äñ := NormedDivisionRing.norm_mul a b

instance (priority := 900) NormedDivisionRing.to_normOneClass : NormOneClass Œ± :=
  ‚ü®mul_left_cancel‚ÇÄ (mt norm_eq_zero.1 (one_ne_zero' Œ±)) <| by rw [‚Üê norm_mul, mul_one, mul_one]‚ü©

instance isAbsoluteValue_norm : IsAbsoluteValue (norm : Œ± ‚Üí ‚Ñù) where
  abv_nonneg' := norm_nonneg
  abv_eq_zero' := norm_eq_zero
  abv_add' := norm_add_le
  abv_mul' := norm_mul

@[simp] lemma nnnorm_mul (a b : Œ±) : ‚Äña * b‚Äñ‚Çä = ‚Äña‚Äñ‚Çä * ‚Äñb‚Äñ‚Çä := NNReal.eq <| norm_mul a b
@[simp] lemma enorm_mul (a b : Œ±) : ‚Äña * b‚Äñ‚Çë = ‚Äña‚Äñ‚Çë * ‚Äñb‚Äñ‚Çë := by simp [enorm]

/-- `norm` as a `MonoidWithZeroHom`. -/
@[simps]
def normHom : Œ± ‚Üí*‚ÇÄ ‚Ñù where
  toFun := (‚Äñ¬∑‚Äñ)
  map_zero' := norm_zero
  map_one' := norm_one
  map_mul' := norm_mul

/-- `nnnorm` as a `MonoidWithZeroHom`. -/
@[simps]
def nnnormHom : Œ± ‚Üí*‚ÇÄ ‚Ñù‚â•0 where
  toFun := (‚Äñ¬∑‚Äñ‚Çä)
  map_zero' := nnnorm_zero
  map_one' := nnnorm_one
  map_mul' := nnnorm_mul

@[simp]
theorem norm_pow (a : Œ±) : ‚àÄ n : ‚Ñï, ‚Äña ^ n‚Äñ = ‚Äña‚Äñ ^ n :=
  (normHom.toMonoidHom : Œ± ‚Üí* ‚Ñù).map_pow a

@[simp]
theorem nnnorm_pow (a : Œ±) (n : ‚Ñï) : ‚Äña ^ n‚Äñ‚Çä = ‚Äña‚Äñ‚Çä ^ n :=
  (nnnormHom.toMonoidHom : Œ± ‚Üí* ‚Ñù‚â•0).map_pow a n

@[simp] lemma enorm_pow (a : Œ±) (n : ‚Ñï) : ‚Äña ^ n‚Äñ‚Çë = ‚Äña‚Äñ‚Çë ^ n := by simp [enorm]

protected theorem List.norm_prod (l : List Œ±) : ‚Äñl.prod‚Äñ = (l.map norm).prod :=
  map_list_prod (normHom.toMonoidHom : Œ± ‚Üí* ‚Ñù) _

protected theorem List.nnnorm_prod (l : List Œ±) : ‚Äñl.prod‚Äñ‚Çä = (l.map nnnorm).prod :=
  map_list_prod (nnnormHom.toMonoidHom : Œ± ‚Üí* ‚Ñù‚â•0) _

@[simp]
theorem norm_div (a b : Œ±) : ‚Äña / b‚Äñ = ‚Äña‚Äñ / ‚Äñb‚Äñ :=
  map_div‚ÇÄ (normHom : Œ± ‚Üí*‚ÇÄ ‚Ñù) a b

@[simp]
theorem nnnorm_div (a b : Œ±) : ‚Äña / b‚Äñ‚Çä = ‚Äña‚Äñ‚Çä / ‚Äñb‚Äñ‚Çä :=
  map_div‚ÇÄ (nnnormHom : Œ± ‚Üí*‚ÇÄ ‚Ñù‚â•0) a b

@[simp]
theorem norm_inv (a : Œ±) : ‚Äña‚Åª¬π‚Äñ = ‚Äña‚Äñ‚Åª¬π :=
  map_inv‚ÇÄ (normHom : Œ± ‚Üí*‚ÇÄ ‚Ñù) a

@[simp]
theorem nnnorm_inv (a : Œ±) : ‚Äña‚Åª¬π‚Äñ‚Çä = ‚Äña‚Äñ‚Çä‚Åª¬π :=
  NNReal.eq <| by simp

@[simp]
lemma enorm_inv {a : Œ±} (ha : a ‚â† 0) : ‚Äña‚Åª¬π‚Äñ‚Çë = ‚Äña‚Äñ‚Çë‚Åª¬π := by simp [enorm, ENNReal.coe_inv, ha]

@[simp]
theorem norm_zpow : ‚àÄ (a : Œ±) (n : ‚Ñ§), ‚Äña ^ n‚Äñ = ‚Äña‚Äñ ^ n :=
  map_zpow‚ÇÄ (normHom : Œ± ‚Üí*‚ÇÄ ‚Ñù)

@[simp]
theorem nnnorm_zpow : ‚àÄ (a : Œ±) (n : ‚Ñ§), ‚Äña ^ n‚Äñ‚Çä = ‚Äña‚Äñ‚Çä ^ n :=
  map_zpow‚ÇÄ (nnnormHom : Œ± ‚Üí*‚ÇÄ ‚Ñù‚â•0)

theorem dist_inv_inv‚ÇÄ {z w : Œ±} (hz : z ‚â† 0) (hw : w ‚â† 0) :
    dist z‚Åª¬π w‚Åª¬π = dist z w / (‚Äñz‚Äñ * ‚Äñw‚Äñ) := by
  rw [dist_eq_norm, inv_sub_inv' hz hw, norm_mul, norm_mul, norm_inv, norm_inv, mul_comm ‚Äñz‚Äñ‚Åª¬π,
    mul_assoc, dist_eq_norm', div_eq_mul_inv, mul_inv]

theorem nndist_inv_inv‚ÇÄ {z w : Œ±} (hz : z ‚â† 0) (hw : w ‚â† 0) :
    nndist z‚Åª¬π w‚Åª¬π = nndist z w / (‚Äñz‚Äñ‚Çä * ‚Äñw‚Äñ‚Çä) :=
  NNReal.eq <| dist_inv_inv‚ÇÄ hz hw

lemma norm_commutator_sub_one_le (ha : a ‚â† 0) (hb : b ‚â† 0) :
    ‚Äña * b * a‚Åª¬π * b‚Åª¬π - 1‚Äñ ‚â§ 2 * ‚Äña‚Äñ‚Åª¬π * ‚Äñb‚Äñ‚Åª¬π * ‚Äña - 1‚Äñ * ‚Äñb - 1‚Äñ := by
  simpa using norm_commutator_units_sub_one_le (.mk0 a ha) (.mk0 b hb)

lemma nnnorm_commutator_sub_one_le (ha : a ‚â† 0) (hb : b ‚â† 0) :
    ‚Äña * b * a‚Åª¬π * b‚Åª¬π - 1‚Äñ‚Çä ‚â§ 2 * ‚Äña‚Äñ‚Çä‚Åª¬π * ‚Äñb‚Äñ‚Çä‚Åª¬π * ‚Äña - 1‚Äñ‚Çä * ‚Äñb - 1‚Äñ‚Çä := by
  simpa using nnnorm_commutator_units_sub_one_le (.mk0 a ha) (.mk0 b hb)

namespace NormedDivisionRing

section Discrete

variable {ùïú : Type*} [NormedDivisionRing ùïú] [DiscreteTopology ùïú]

lemma norm_eq_one_iff_ne_zero_of_discrete {x : ùïú} : ‚Äñx‚Äñ = 1 ‚Üî x ‚â† 0 := by
  constructor <;> intro hx
  ¬∑ contrapose! hx
    simp [hx]
  ¬∑ have : IsOpen {(0 : ùïú)} := isOpen_discrete {0}
    simp_rw [Metric.isOpen_singleton_iff, dist_eq_norm, sub_zero] at this
    obtain ‚ü®Œµ, Œµpos, h'‚ü© := this
    wlog h : ‚Äñx‚Äñ < 1 generalizing ùïú with H
    ¬∑ push_neg at h
      rcases h.eq_or_lt with h|h
      ¬∑ rw [h]
      replace h := norm_inv x ‚ñ∏ inv_lt_one_of_one_lt‚ÇÄ h
      rw [‚Üê inv_inj, inv_one, ‚Üê norm_inv]
      exact H (by simpa) h' h
    obtain ‚ü®k, hk‚ü© : ‚àÉ k : ‚Ñï, ‚Äñx‚Äñ ^ k < Œµ := exists_pow_lt_of_lt_one Œµpos h
    rw [‚Üê norm_pow] at hk
    specialize h' _ hk
    simp [hx] at h'

@[simp]
lemma norm_le_one_of_discrete
    (x : ùïú) : ‚Äñx‚Äñ ‚â§ 1 := by
  rcases eq_or_ne x 0 with rfl|hx
  ¬∑ simp
  ¬∑ simp [norm_eq_one_iff_ne_zero_of_discrete.mpr hx]

lemma unitClosedBall_eq_univ_of_discrete : (Metric.closedBall 0 1 : Set ùïú) = Set.univ := by
  ext
  simp

@[deprecated (since := "2024-12-01")]
alias discreteTopology_unit_closedBall_eq_univ := unitClosedBall_eq_univ_of_discrete

end Discrete

end NormedDivisionRing

end NormedDivisionRing

/-- A normed field is a field with a norm satisfying ‚Äñx y‚Äñ = ‚Äñx‚Äñ ‚Äñy‚Äñ. -/
class NormedField (Œ± : Type*) extends Norm Œ±, Field Œ±, MetricSpace Œ± where
  /-- The distance is induced by the norm. -/
  dist_eq : ‚àÄ x y, dist x y = norm (x - y)
  /-- The norm is multiplicative. -/
  protected norm_mul : ‚àÄ a b, norm (a * b) = norm a * norm b

/-- A nontrivially normed field is a normed field in which there is an element of norm different
from `0` and `1`. This makes it possible to bring any element arbitrarily close to `0` by
multiplication by the powers of any element, and thus to relate algebra and topology. -/
class NontriviallyNormedField (Œ± : Type*) extends NormedField Œ± where
  /-- The norm attains a value exceeding 1. -/
  non_trivial : ‚àÉ x : Œ±, 1 < ‚Äñx‚Äñ

/-- A densely normed field is a normed field for which the image of the norm is dense in `‚Ñù‚â•0`,
which means it is also nontrivially normed. However, not all nontrivally normed fields are densely
normed; in particular, the `Padic`s exhibit this fact. -/
class DenselyNormedField (Œ± : Type*) extends NormedField Œ± where
  /-- The range of the norm is dense in the collection of nonnegative real numbers. -/
  lt_norm_lt : ‚àÄ x y : ‚Ñù, 0 ‚â§ x ‚Üí x < y ‚Üí ‚àÉ a : Œ±, x < ‚Äña‚Äñ ‚àß ‚Äña‚Äñ < y

section NormedField

/-- A densely normed field is always a nontrivially normed field.
See note [lower instance priority]. -/
instance (priority := 100) DenselyNormedField.toNontriviallyNormedField [DenselyNormedField Œ±] :
    NontriviallyNormedField Œ± where
  non_trivial :=
    let ‚ü®a, h, _‚ü© := DenselyNormedField.lt_norm_lt 1 2 zero_le_one one_lt_two
    ‚ü®a, h‚ü©

variable [NormedField Œ±]

-- see Note [lower instance priority]
instance (priority := 100) NormedField.toNormedDivisionRing : NormedDivisionRing Œ± :=
  { ‚ÄπNormedField Œ±‚Ä∫ with }

-- see Note [lower instance priority]
instance (priority := 100) NormedField.toNormedCommRing : NormedCommRing Œ± :=
  { ‚ÄπNormedField Œ±‚Ä∫ with norm_mul_le a b := (norm_mul a b).le }

@[simp]
theorem norm_prod (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) : ‚Äñ‚àè b ‚àà s, f b‚Äñ = ‚àè b ‚àà s, ‚Äñf b‚Äñ :=
  map_prod normHom.toMonoidHom f s

@[simp]
theorem nnnorm_prod (s : Finset Œ≤) (f : Œ≤ ‚Üí Œ±) : ‚Äñ‚àè b ‚àà s, f b‚Äñ‚Çä = ‚àè b ‚àà s, ‚Äñf b‚Äñ‚Çä :=
  map_prod nnnormHom.toMonoidHom f s

end NormedField

namespace NormedField

section Nontrivially

variable (Œ±) [NontriviallyNormedField Œ±]

theorem exists_one_lt_norm : ‚àÉ x : Œ±, 1 < ‚Äñx‚Äñ :=
  ‚ÄπNontriviallyNormedField Œ±‚Ä∫.non_trivial

theorem exists_lt_norm (r : ‚Ñù) : ‚àÉ x : Œ±, r < ‚Äñx‚Äñ :=
  let ‚ü®w, hw‚ü© := exists_one_lt_norm Œ±
  let ‚ü®n, hn‚ü© := pow_unbounded_of_one_lt r hw
  ‚ü®w ^ n, by rwa [norm_pow]‚ü©

theorem exists_norm_lt {r : ‚Ñù} (hr : 0 < r) : ‚àÉ x : Œ±, 0 < ‚Äñx‚Äñ ‚àß ‚Äñx‚Äñ < r :=
  let ‚ü®w, hw‚ü© := exists_lt_norm Œ± r‚Åª¬π
  ‚ü®w‚Åª¬π, by rwa [‚Üê Set.mem_Ioo, norm_inv, ‚Üê Set.mem_inv, Set.inv_Ioo_0_left hr]‚ü©

theorem exists_norm_lt_one : ‚àÉ x : Œ±, 0 < ‚Äñx‚Äñ ‚àß ‚Äñx‚Äñ < 1 :=
  exists_norm_lt Œ± one_pos

variable {Œ±}

@[instance]
theorem nhdsNE_neBot (x : Œ±) : NeBot (ùìù[‚â†] x) := by
  rw [‚Üê mem_closure_iff_nhdsWithin_neBot, Metric.mem_closure_iff]
  rintro Œµ Œµ0
  rcases exists_norm_lt Œ± Œµ0 with ‚ü®b, hb0, hbŒµ‚ü©
  refine ‚ü®x + b, mt (Set.mem_singleton_iff.trans add_right_eq_self).1 <| norm_pos_iff.1 hb0, ?_‚ü©
  rwa [dist_comm, dist_eq_norm, add_sub_cancel_left]

@[deprecated (since := "2025-03-02")]
alias punctured_nhds_neBot := nhdsNE_neBot

@[instance]
theorem nhdsWithin_isUnit_neBot : NeBot (ùìù[{ x : Œ± | IsUnit x }] 0) := by
  simpa only [isUnit_iff_ne_zero] using nhdsNE_neBot (0 : Œ±)

end Nontrivially

section Densely

variable (Œ±) [DenselyNormedField Œ±]

theorem exists_lt_norm_lt {r‚ÇÅ r‚ÇÇ : ‚Ñù} (h‚ÇÄ : 0 ‚â§ r‚ÇÅ) (h : r‚ÇÅ < r‚ÇÇ) : ‚àÉ x : Œ±, r‚ÇÅ < ‚Äñx‚Äñ ‚àß ‚Äñx‚Äñ < r‚ÇÇ :=
  DenselyNormedField.lt_norm_lt r‚ÇÅ r‚ÇÇ h‚ÇÄ h

theorem exists_lt_nnnorm_lt {r‚ÇÅ r‚ÇÇ : ‚Ñù‚â•0} (h : r‚ÇÅ < r‚ÇÇ) : ‚àÉ x : Œ±, r‚ÇÅ < ‚Äñx‚Äñ‚Çä ‚àß ‚Äñx‚Äñ‚Çä < r‚ÇÇ :=
  mod_cast exists_lt_norm_lt Œ± r‚ÇÅ.prop h

instance denselyOrdered_range_norm : DenselyOrdered (Set.range (norm : Œ± ‚Üí ‚Ñù)) where
  dense := by
    rintro ‚ü®-, x, rfl‚ü© ‚ü®-, y, rfl‚ü© hxy
    let ‚ü®z, h‚ü© := exists_lt_norm_lt Œ± (norm_nonneg _) hxy
    exact ‚ü®‚ü®‚Äñz‚Äñ, z, rfl‚ü©, h‚ü©

instance denselyOrdered_range_nnnorm : DenselyOrdered (Set.range (nnnorm : Œ± ‚Üí ‚Ñù‚â•0)) where
  dense := by
    rintro ‚ü®-, x, rfl‚ü© ‚ü®-, y, rfl‚ü© hxy
    let ‚ü®z, h‚ü© := exists_lt_nnnorm_lt Œ± hxy
    exact ‚ü®‚ü®‚Äñz‚Äñ‚Çä, z, rfl‚ü©, h‚ü©

end Densely

end NormedField

/-- A normed field is nontrivially normed
provided that the norm of some nonzero element is not one. -/
def NontriviallyNormedField.ofNormNeOne {ùïú : Type*} [h' : NormedField ùïú]
    (h : ‚àÉ x : ùïú, x ‚â† 0 ‚àß ‚Äñx‚Äñ ‚â† 1) : NontriviallyNormedField ùïú where
  toNormedField := h'
  non_trivial := by
    rcases h with ‚ü®x, hx, hx1‚ü©
    rcases hx1.lt_or_lt with hlt | hlt
    ¬∑ use x‚Åª¬π
      rw [norm_inv]
      exact (one_lt_inv‚ÇÄ (norm_pos_iff.2 hx)).2 hlt
    ¬∑ exact ‚ü®x, hlt‚ü©

noncomputable instance Real.normedField : NormedField ‚Ñù :=
  { Real.normedAddCommGroup, Real.field with
    norm_mul := abs_mul }

noncomputable instance Real.denselyNormedField : DenselyNormedField ‚Ñù where
  lt_norm_lt _ _ h‚ÇÄ hr :=
    let ‚ü®x, h‚ü© := exists_between hr
    ‚ü®x, by rwa [Real.norm_eq_abs, abs_of_nonneg (h‚ÇÄ.trans h.1.le)]‚ü©

namespace Real

theorem toNNReal_mul_nnnorm {x : ‚Ñù} (y : ‚Ñù) (hx : 0 ‚â§ x) : x.toNNReal * ‚Äñy‚Äñ‚Çä = ‚Äñx * y‚Äñ‚Çä := by
  ext
  simp only [NNReal.coe_mul, nnnorm_mul, coe_nnnorm, Real.toNNReal_of_nonneg, norm_of_nonneg, hx,
    NNReal.coe_mk]

theorem nnnorm_mul_toNNReal (x : ‚Ñù) {y : ‚Ñù} (hy : 0 ‚â§ y) : ‚Äñx‚Äñ‚Çä * y.toNNReal = ‚Äñx * y‚Äñ‚Çä := by
  rw [mul_comm, mul_comm x, toNNReal_mul_nnnorm x hy]

end Real

/-! ### Induced normed structures -/

section Induced

variable {F : Type*} (R S : Type*) [FunLike F R S]

/-- An injective non-unital ring homomorphism from a `DivisionRing` to a `NormedRing` induces a
`NormedDivisionRing` structure on the domain.

See note [reducible non-instances] -/
abbrev NormedDivisionRing.induced [DivisionRing R] [NormedDivisionRing S]
    [NonUnitalRingHomClass F R S] (f : F) (hf : Function.Injective f) : NormedDivisionRing R :=
  { NormedAddCommGroup.induced R S f hf, ‚ÄπDivisionRing R‚Ä∫ with
    norm_mul x y := show ‚Äñf _‚Äñ = _ from (map_mul f x y).symm ‚ñ∏ norm_mul (f x) (f y) }

/-- An injective non-unital ring homomorphism from a `Field` to a `NormedRing` induces a
`NormedField` structure on the domain.

See note [reducible non-instances] -/
abbrev NormedField.induced [Field R] [NormedField S] [NonUnitalRingHomClass F R S] (f : F)
    (hf : Function.Injective f) : NormedField R :=
  { NormedDivisionRing.induced R S f hf with
    mul_comm := mul_comm }

end Induced

namespace SubfieldClass

variable {S F : Type*} [SetLike S F]

/--
If `s` is a subfield of a normed field `F`, then `s` is equipped with an induced normed
field structure.
-/
instance toNormedField [NormedField F] [SubfieldClass S F] (s : S) : NormedField s :=
  NormedField.induced s F (SubringClass.subtype s) Subtype.val_injective

end SubfieldClass

namespace AbsoluteValue

/-- A real absolute value on a field determines a `NormedField` structure. -/
noncomputable def toNormedField {K : Type*} [Field K] (v : AbsoluteValue K ‚Ñù) : NormedField K where
  toField := inferInstanceAs (Field K)
  __ := v.toNormedRing
  norm_mul := v.map_mul

end AbsoluteValue
