/-
Copyright (c) 2024 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
import Mathlib.Analysis.Normed.Module.Completion
import Mathlib.Analysis.Normed.Ring.WithAbs
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# WithAbs for fields

This extends the `WithAbs` mechanism to fields, providing a type synonym for a field which depends
on an absolute value. This is useful when dealing with several absolute values on the same field.

In particular this allows us to define the completion of a field at a given absolute value.
-/

open Topology

noncomputable section

variable {R S : Type*} [Semiring S] [PartialOrder S] [IsOrderedRing S]

namespace WithAbs

section more_instances

instance normedField [Field R] (v : AbsoluteValue R ‚Ñù) : NormedField (WithAbs v) :=
  v.toNormedField

end more_instances

/-!
### The completion of a field at an absolute value.
-/

variable {K : Type*} [Field K] {v : AbsoluteValue K ‚Ñù}
  {L : Type*} [NormedField L] {f : WithAbs v ‚Üí+* L}

/-- If the absolute value `v` factors through an embedding `f` into a normed field, then
`f` is an isometry. -/
theorem isometry_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) : Isometry f :=
  Isometry.of_dist_eq <| fun x y => by simp only [‚ÄπNormedField L‚Ä∫.dist_eq, ‚Üê f.map_sub, h]; rfl

/-- If the absolute value `v` factors through an embedding `f` into a normed field, then
the pseudo metric space associated to the absolute value is the same as the pseudo metric space
induced by `f`. -/
theorem pseudoMetricSpace_induced_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) :
    PseudoMetricSpace.induced f inferInstance = (normedField v).toPseudoMetricSpace := by
  ext; exact isometry_of_comp h |>.dist_eq _ _

/-- If the absolute value `v` factors through an embedding `f` into a normed field, then
the uniform structure associated to the absolute value is the same as the uniform structure
induced by `f`. -/
theorem uniformSpace_comap_eq_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) :
    UniformSpace.comap f inferInstance = (normedField v).toUniformSpace := by
  simp only [‚Üê pseudoMetricSpace_induced_of_comp h, PseudoMetricSpace.toUniformSpace]

/-- If the absolute value `v` factors through an embedding `f` into a normed field, then
`f` is uniform inducing. -/
theorem isUniformInducing_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) : IsUniformInducing f :=
  isUniformInducing_iff_uniformSpace.2 <| uniformSpace_comap_eq_of_comp h

end WithAbs

namespace AbsoluteValue

open WithAbs

variable {K : Type*} [Field K] (v : AbsoluteValue K ‚Ñù)

/-- The completion of a field with respect to a real absolute value. -/
abbrev Completion := UniformSpace.Completion (WithAbs v)

namespace Completion

instance : Coe K v.Completion :=
  inferInstanceAs <| Coe (WithAbs v) (UniformSpace.Completion (WithAbs v))

variable {L : Type*} [NormedField L] [CompleteSpace L] {f : WithAbs v ‚Üí+* L} {v}

/-- If the absolute value of a normed field factors through an embedding into another normed field
`L`, then we can extend that embedding to an embedding on the completion `v.Completion ‚Üí+* L`. -/
abbrev extensionEmbedding_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) : v.Completion ‚Üí+* L :=
  UniformSpace.Completion.extensionHom _
    (WithAbs.isUniformInducing_of_comp h).uniformContinuous.continuous

theorem extensionEmbedding_of_comp_coe (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) (x : K) :
    extensionEmbedding_of_comp h x = f x := by
  rw [‚Üê UniformSpace.Completion.extensionHom_coe f
    (WithAbs.isUniformInducing_of_comp h).uniformContinuous.continuous]

/-- If the absolute value of a normed field factors through an embedding into another normed field,
then the extended embedding `v.Completion ‚Üí+* L` preserves distances. -/
theorem extensionEmbedding_dist_eq_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) (x y : v.Completion) :
    dist (extensionEmbedding_of_comp h x) (extensionEmbedding_of_comp h y) =
      dist x y := by
  refine UniformSpace.Completion.induction_on‚ÇÇ x y ?_ (fun x y => ?_)
  ¬∑ refine isClosed_eq ?_ continuous_dist
    exact continuous_iff_continuous_dist.1 UniformSpace.Completion.continuous_extension
  ¬∑ simp only [extensionEmbedding_of_comp_coe]
    exact UniformSpace.Completion.dist_eq x y ‚ñ∏ (WithAbs.isometry_of_comp h).dist_eq _ _

/-- If the absolute value of a normed field factors through an embedding into another normed field,
then the extended embedding `v.Completion ‚Üí+* L` is an isometry. -/
theorem isometry_extensionEmbedding_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) :
    Isometry (extensionEmbedding_of_comp h) :=
  Isometry.of_dist_eq <| extensionEmbedding_dist_eq_of_comp h

/-- If the absolute value of a normed field factors through an embedding into another normed field,
then the extended embedding `v.Completion ‚Üí+* L` is a closed embedding. -/
theorem isClosedEmbedding_extensionEmbedding_of_comp (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) :
    IsClosedEmbedding (extensionEmbedding_of_comp h) :=
  (isometry_extensionEmbedding_of_comp h).isClosedEmbedding

/-- If the absolute value of a normed field factors through an embedding into another normed field
that is locally compact, then the completion of the first normed field is also locally compact. -/
theorem locallyCompactSpace [LocallyCompactSpace L] (h : ‚àÄ x, ‚Äñf x‚Äñ = v x) :
    LocallyCompactSpace (v.Completion) :=
  (isClosedEmbedding_extensionEmbedding_of_comp h).locallyCompactSpace

end AbsoluteValue.Completion

namespace WithAbs

open Filter
open scoped Topology

variable {R S : Type*} [Field R] [Field S] [LinearOrder S] {v w : AbsoluteValue R S}
  [TopologicalSpace S] [IsStrictOrderedRing S] [Archimedean S] [_i : OrderTopology S]

/--
The limit $v\left(\frac{1}{1 + a ^ n}\right)\to 1$, for an absolute value $v$ on a field
$F$ if $v(a) < 1$.
-/
private theorem tendsto_div_one_add_pow_nhds_one {v : AbsoluteValue R S} {a : R} (ha : v a < 1) :
    atTop.Tendsto (fun (n : ‚Ñï) ‚Ü¶ v (1 / (1 + a ^ n))) (ùìù 1) := by
  simp_rw [map_div‚ÇÄ v, v.map_one]
  apply one_div_one (G := S) ‚ñ∏ Tendsto.div tendsto_const_nhds _ one_ne_zero
  have h_add := tendsto_pow_atTop_nhds_zero_of_lt_one (v.nonneg _) ha |>.const_add (1 : S)
  have h_sub := tendsto_pow_atTop_nhds_zero_of_lt_one (v.nonneg _) ha |>.const_sub 1
  exact tendsto_of_tendsto_of_tendsto_of_le_of_le (by simpa using h_sub) (by simpa using h_add)
    (fun n ‚Ü¶ le_trans (by rw [map_one, map_pow]) (v.le_add _ _))
    (fun n ‚Ü¶ le_trans (v.add_le _ _) (by rw [map_one, map_pow]))

/--
The limit $v \left(\frac{1}{1 + a ^ n}\right)\to 0$, for an absolute value $v$ on a field
$F$ if $1 < v(a)$.
-/
private theorem tendsto_pow_div_one_add_pow_zero {v : AbsoluteValue R S} {a : R} (ha : 1 < v a) :
    Filter.Tendsto (fun (n : ‚Ñï) ‚Ü¶ v (1 / (1 + a ^ n))) Filter.atTop (ùìù 0) := by
  simp_rw [div_eq_mul_inv, one_mul, map_inv‚ÇÄ, fun n ‚Ü¶ add_comm 1 (a ^ n)]
  refine (tendsto_atTop_mono (fun n ‚Ü¶ v.le_add _ _) ?_).inv_tendsto_atTop
  simpa using tendsto_atTop_add_right_of_le _ _ (tendsto_pow_atTop_atTop_of_one_lt ha)
    (fun _ ‚Ü¶ le_rfl) |>.congr fun n ‚Ü¶ (sub_eq_add_neg (v a ^ n) 1).symm

end WithAbs
