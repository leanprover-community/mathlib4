/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
module

public import Mathlib.Analysis.Normed.Field.Basic
public import Mathlib.Analysis.Normed.Group.Rat
public import Mathlib.Analysis.Normed.Ring.Lemmas
public import Mathlib.Topology.MetricSpace.DilationEquiv
import Mathlib.Analysis.Normed.MulAction

/-!
# Normed fields

In this file we continue building the theory of normed division rings and fields.

Some useful results that relate the topology of the normed field to the discrete topology include:
* `discreteTopology_or_nontriviallyNormedField`
* `discreteTopology_of_bddAbove_range_norm`

-/

@[expose] public section

-- Guard against import creep.
assert_not_exists RestrictScalars

variable {Œ± Œ≤ Œπ : Type*}

open Filter Bornology Metric
open scoped Topology NNReal Pointwise Uniformity

section NormedDivisionRing

variable [NormedDivisionRing Œ±]

/-- Multiplication by a nonzero element `a` on the left
as a `DilationEquiv` of a normed division ring. -/
@[simps!]
def DilationEquiv.mulLeft (a : Œ±) (ha : a ‚â† 0) : Œ± ‚âÉ·µà Œ± where
  __ := Dilation.mulLeft a ha
  toEquiv := Equiv.mulLeft‚ÇÄ a ha

/-- Multiplication by a nonzero element `a` on the right
as a `DilationEquiv` of a normed division ring. -/
@[simps!]
def DilationEquiv.mulRight (a : Œ±) (ha : a ‚â† 0) : Œ± ‚âÉ·µà Œ± where
  __ := Dilation.mulRight a ha
  toEquiv := Equiv.mulRight‚ÇÄ a ha

namespace Filter

@[simp]
lemma map_mul_left_cobounded {a : Œ±} (ha : a ‚â† 0) :
    map (a * ¬∑) (cobounded Œ±) = cobounded Œ± :=
  DilationEquiv.map_cobounded (DilationEquiv.mulLeft a ha)

@[simp]
lemma map_mul_right_cobounded {a : Œ±} (ha : a ‚â† 0) :
    map (¬∑ * a) (cobounded Œ±) = cobounded Œ± :=
  DilationEquiv.map_cobounded (DilationEquiv.mulRight a ha)

/-- Multiplication on the left by a nonzero element of a normed division ring tends to infinity at
infinity. -/
theorem tendsto_mul_left_cobounded {a : Œ±} (ha : a ‚â† 0) :
    Tendsto (a * ¬∑) (cobounded Œ±) (cobounded Œ±) :=
  (map_mul_left_cobounded ha).le

/-- Multiplication on the right by a nonzero element of a normed division ring tends to infinity at
infinity. -/
theorem tendsto_mul_right_cobounded {a : Œ±} (ha : a ‚â† 0) :
    Tendsto (¬∑ * a) (cobounded Œ±) (cobounded Œ±) :=
  (map_mul_right_cobounded ha).le

@[simp]
lemma inv_cobounded‚ÇÄ : (cobounded Œ±)‚Åª¬π = ùìù[‚â†] 0 := by
  rw [‚Üê comap_norm_atTop, ‚Üê Filter.comap_inv, ‚Üê comap_norm_nhdsGT_zero, ‚Üê inv_atTop‚ÇÄ,
    ‚Üê Filter.comap_inv]
  simp only [comap_comap, Function.comp_def, norm_inv]

@[simp]
lemma inv_nhdsNE_zero : (ùìù[‚â†] (0 : Œ±))‚Åª¬π = cobounded Œ± := by
  rw [‚Üê inv_cobounded‚ÇÄ, inv_inv]

@[deprecated (since := "2025-11-26")] alias inv_nhdsWithin_ne_zero := inv_nhdsNE_zero

lemma tendsto_inv‚ÇÄ_cobounded' : Tendsto Inv.inv (cobounded Œ±) (ùìù[‚â†] 0) :=
  inv_cobounded‚ÇÄ.le

theorem tendsto_inv‚ÇÄ_cobounded : Tendsto Inv.inv (cobounded Œ±) (ùìù 0) :=
  tendsto_inv‚ÇÄ_cobounded'.mono_right inf_le_left

lemma tendsto_inv‚ÇÄ_nhdsNE_zero : Tendsto Inv.inv (ùìù[‚â†] 0) (cobounded Œ±) :=
  inv_nhdsNE_zero.le

@[deprecated (since := "2025-11-26")]
alias tendsto_inv‚ÇÄ_nhdsWithin_ne_zero := tendsto_inv‚ÇÄ_nhdsNE_zero

end Filter

/-- If `s` is a set disjoint from `ùìù 0`, then `fun x ‚Ü¶ x‚Åª¬π` is uniformly continuous on `s`. -/
theorem uniformContinuousOn_inv‚ÇÄ {s : Set Œ±} (hs : s·∂ú ‚àà ùìù 0) :
    UniformContinuousOn Inv.inv s := by
  rw [Metric.uniformContinuousOn_iff_le]
  intro Œµ hŒµ
  rcases NormedAddCommGroup.nhds_zero_basis_norm_lt.mem_iff.mp hs with ‚ü®r, hr‚ÇÄ, hr‚ü©
  simp only [Set.subset_compl_comm (t := s), Set.compl_setOf, not_lt] at hr
  have hs‚ÇÄ : ‚àÄ x ‚àà s, x ‚â† 0 := fun x hx ‚Ü¶ norm_pos_iff.mp <| hr‚ÇÄ.trans_le (hr hx)
  refine ‚ü®Œµ * r ^ 2, by positivity, fun x hx y hy hxy ‚Ü¶ ?_‚ü©
  calc
    dist x‚Åª¬π y‚Åª¬π = ‚Äñx‚Äñ‚Åª¬π * dist y x * ‚Äñy‚Äñ‚Åª¬π := by
      simp [dist_eq_norm, inv_sub_inv' (hs‚ÇÄ x hx) (hs‚ÇÄ y hy)]
    _ ‚â§ r‚Åª¬π * (Œµ * r ^ 2) * r‚Åª¬π := by
      rw [dist_comm]
      gcongr <;> exact hr ‚Äπ_‚Ä∫
    _ = Œµ := by field_simp

@[to_fun]
theorem UniformContinuousOn.inv‚ÇÄ {X : Type*} [UniformSpace X] {f : X ‚Üí Œ±} {s : Set X}
    (hf : UniformContinuousOn f s) (hf‚ÇÄ : (f '' s)·∂ú ‚àà ùìù 0) :
    UniformContinuousOn f‚Åª¬π s :=
  uniformContinuousOn_inv‚ÇÄ hf‚ÇÄ |>.comp hf (Set.mapsTo_image f s)

@[to_fun]
theorem UniformContinuous.inv‚ÇÄ {X : Type*} [UniformSpace X] {f : X ‚Üí Œ±}
    (hf : UniformContinuous f) (hf‚ÇÄ : (Set.range f)·∂ú ‚àà ùìù 0) :
    UniformContinuous f‚Åª¬π := by
  simp only [‚Üê uniformContinuousOn_univ, ‚Üê Set.image_univ] at *
  exact hf.inv‚ÇÄ hf‚ÇÄ

@[to_fun]
theorem TendstoLocallyUniformlyOn.inv‚ÇÄ_of_disjoint {X Œπ : Type*} [TopologicalSpace X]
    {s : Set X} {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformlyOn F f l s) (hf : ‚àÄ x ‚àà s, Disjoint (map f (ùìù[s] x)) (ùìù 0)) :
    TendstoLocallyUniformlyOn F‚Åª¬π f‚Åª¬π l s := by
  rw [tendstoLocallyUniformlyOn_iff_forall_tendsto] at *
  intro x hx
  rcases basis_sets _ |>.map _ |>.disjoint_iff nhds_basis_ball
    |>.mp (hf x hx) with ‚ü®U, hUx, r, hr‚ÇÄ, hr‚ü©
  refine Tendsto.comp (uniformContinuousOn_inv‚ÇÄ (s := (closedBall (0 : Œ±) (r / 2))·∂ú)
    (by simp [closedBall_mem_nhds, hr‚ÇÄ])) <| tendsto_inf.mpr ‚ü®hF x hx, tendsto_principal.mpr ?_‚ü©
  filter_upwards [hF x hx (dist_mem_uniformity (half_pos hr‚ÇÄ)), tendsto_snd hUx] with y hy‚ÇÅ hy‚ÇÇ
  have : r ‚â§ ‚Äñf y.2‚Äñ := by simp_all [Set.disjoint_left]
  have : r / 2 < ‚ÄñF y.1 y.2‚Äñ := by
    simp [dist_eq_norm_sub] at hy‚ÇÅ
    linarith [hy‚ÇÅ, norm_sub_norm_le (f y.2) (F y.1 y.2)]
  simp_all [(half_lt_self hr‚ÇÄ).trans_le]

@[to_fun]
theorem TendstoLocallyUniformly.inv‚ÇÄ_of_disjoint {X Œπ : Type*} [TopologicalSpace X]
    {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformly F f l) (hf : ‚àÄ x, Disjoint (map f (ùìù x)) (ùìù 0)) :
    TendstoLocallyUniformly F‚Åª¬π f‚Åª¬π l := by
  rw [‚Üê tendstoLocallyUniformlyOn_univ] at *
  apply hF.inv‚ÇÄ_of_disjoint
  simpa

@[to_fun]
theorem TendstoLocallyUniformlyOn.inv‚ÇÄ {X Œπ : Type*} [TopologicalSpace X]
    {s : Set X} {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformlyOn F f l s) (hf : ContinuousOn f s) (hf‚ÇÄ : ‚àÄ x ‚àà s, f x ‚â† 0) :
    TendstoLocallyUniformlyOn F‚Åª¬π f‚Åª¬π l s :=
  hF.inv‚ÇÄ_of_disjoint fun x hx ‚Ü¶ disjoint_nhds_nhds.2 (hf‚ÇÄ x hx) |>.mono_left (hf x hx)

@[to_fun]
theorem TendstoLocallyUniformly.inv‚ÇÄ {X Œπ : Type*} [TopologicalSpace X]
    {F : Œπ ‚Üí X ‚Üí Œ±} {f : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformly F f l) (hf : Continuous f) (hf‚ÇÄ : ‚àÄ x, f x ‚â† 0) :
    TendstoLocallyUniformly F‚Åª¬π f‚Åª¬π l :=
  hF.inv‚ÇÄ_of_disjoint fun x ‚Ü¶ disjoint_nhds_nhds.2 (hf‚ÇÄ x) |>.mono_left (hf.tendsto x)

-- see Note [lower instance priority]
instance (priority := 100) NormedDivisionRing.to_continuousInv‚ÇÄ : ContinuousInv‚ÇÄ Œ± where
  continuousAt_inv‚ÇÄ x hx := by
    refine uniformContinuousOn_inv‚ÇÄ (s := (Metric.closedBall x (‚Äñx‚Äñ / 2))) ?_
      |>.continuousOn |>.continuousAt ?_
    ¬∑ refine Metric.isClosed_closedBall.isOpen_compl.mem_nhds ?_
      simpa
    ¬∑ apply Metric.closedBall_mem_nhds
      simpa

@[deprecated (since := "2025-09-01")] alias NormedDivisionRing.to_hasContinuousInv‚ÇÄ :=
  NormedDivisionRing.to_continuousInv‚ÇÄ

@[to_fun]
theorem TendstoLocallyUniformlyOn.div‚ÇÄ {X Œπ : Type*} [TopologicalSpace X]
    {s : Set X} {F G : Œπ ‚Üí X ‚Üí Œ±} {f g : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformlyOn F f l s) (hG : TendstoLocallyUniformlyOn G g l s)
    (hf : ContinuousOn f s) (hg : ContinuousOn g s) (hg‚ÇÄ : ‚àÄ x ‚àà s, g x ‚â† 0) :
    TendstoLocallyUniformlyOn (F / G) (f / g) l s := by
  simp only [div_eq_mul_inv]
  exact hF.mul‚ÇÄ (hG.inv‚ÇÄ hg hg‚ÇÄ) hf <| hg.inv‚ÇÄ hg‚ÇÄ

@[to_fun]
theorem TendstoLocallyUniformly.div‚ÇÄ {X Œπ : Type*} [TopologicalSpace X]
    {F G : Œπ ‚Üí X ‚Üí Œ±} {f g : X ‚Üí Œ±} {l : Filter Œπ}
    (hF : TendstoLocallyUniformly F f l) (hG : TendstoLocallyUniformly G g l)
    (hf : Continuous f) (hg : Continuous g) (hg‚ÇÄ : ‚àÄ x, g x ‚â† 0) :
    TendstoLocallyUniformly (F / G) (f / g) l := by
  simp only [div_eq_mul_inv]
  exact hF.mul‚ÇÄ (hG.inv‚ÇÄ hg hg‚ÇÄ) hf <| hg.inv‚ÇÄ hg‚ÇÄ

-- see Note [lower instance priority]
/-- A normed division ring is a topological division ring. -/
instance (priority := 100) NormedDivisionRing.to_isTopologicalDivisionRing :
    IsTopologicalDivisionRing Œ± where

lemma tendsto_norm_inv_nhdsNE_zero_atTop : Tendsto (fun x : Œ± ‚Ü¶ ‚Äñx‚Åª¬π‚Äñ) (ùìù[‚â†] 0) atTop :=
  tendsto_norm_cobounded_atTop.comp tendsto_inv‚ÇÄ_nhdsNE_zero

@[deprecated (since := "2025-11-26")]
alias NormedField.tendsto_norm_inv_nhdsNE_zero_atTop := tendsto_norm_inv_nhdsNE_zero_atTop

lemma tendsto_zpow_nhdsNE_zero_cobounded {m : ‚Ñ§} (hm : m < 0) :
    Tendsto (¬∑ ^ m) (ùìù[‚â†] 0) (cobounded Œ±) := by
  obtain ‚ü®m, rfl‚ü© := neg_surjective m
  lift m to ‚Ñï using by lia
  simpa [Function.comp_def] using
    (tendsto_pow_cobounded_cobounded (by lia)).comp tendsto_inv‚ÇÄ_nhdsNE_zero

@[deprecated tendsto_zpow_nhdsNE_zero_cobounded (since := "2025-11-26")]
lemma NormedField.tendsto_norm_zpow_nhdsNE_zero_atTop {m : ‚Ñ§} (hm : m < 0) :
    Tendsto (fun x : Œ± ‚Ü¶ ‚Äñx ^ m‚Äñ) (ùìù[‚â†] 0) atTop :=
  tendsto_norm_cobounded_atTop.comp (tendsto_zpow_nhdsNE_zero_cobounded hm)

end NormedDivisionRing

namespace NormedField

/-- A normed field is either nontrivially normed or has a discrete topology.
In the discrete topology case, all the norms are 1, by `norm_eq_one_iff_ne_zero_of_discrete`.
The nontrivially normed field instance is provided by a subtype with a proof that the
forgetful inheritance to the existing `NormedField` instance is definitionally true.
This allows one to have the new `NontriviallyNormedField` instance without data clashes. -/
lemma discreteTopology_or_nontriviallyNormedField (ùïú : Type*) [h : NormedField ùïú] :
    DiscreteTopology ùïú ‚à® Nonempty ({h' : NontriviallyNormedField ùïú // h'.toNormedField = h}) := by
  by_cases H : ‚àÉ x : ùïú, x ‚â† 0 ‚àß ‚Äñx‚Äñ ‚â† 1
  ¬∑ exact Or.inr ‚ü®(‚ü®NontriviallyNormedField.ofNormNeOne H, rfl‚ü©)‚ü©
  ¬∑ simp_rw [discreteTopology_iff_isOpen_singleton_zero, Metric.isOpen_singleton_iff, dist_eq_norm,
             sub_zero]
    refine Or.inl ‚ü®1, zero_lt_one, ?_‚ü©
    contrapose! H
    refine H.imp ?_
    -- contextual to reuse the `a ‚â† 0` hypothesis in the proof of `a ‚â† 0 ‚àß ‚Äña‚Äñ ‚â† 1`
    simp +contextual [ne_of_lt]

lemma discreteTopology_of_bddAbove_range_norm {ùïú : Type*} [NormedField ùïú]
    (h : BddAbove (Set.range fun k : ùïú ‚Ü¶ ‚Äñk‚Äñ)) :
    DiscreteTopology ùïú := by
  refine (NormedField.discreteTopology_or_nontriviallyNormedField _).resolve_right ?_
  rintro ‚ü®_, rfl‚ü©
  obtain ‚ü®x, h‚ü© := h
  obtain ‚ü®k, hk‚ü© := NormedField.exists_lt_norm ùïú x
  exact hk.not_ge (h (Set.mem_range_self k))

section Densely

variable (Œ±) [DenselyNormedField Œ±]

theorem denseRange_nnnorm : DenseRange (nnnorm : Œ± ‚Üí ‚Ñù‚â•0) :=
  dense_of_exists_between fun _ _ hr =>
    let ‚ü®x, h‚ü© := exists_lt_nnnorm_lt Œ± hr
    ‚ü®‚Äñx‚Äñ‚Çä, ‚ü®x, rfl‚ü©, h‚ü©

end Densely

section NontriviallyNormedField
variable {ùïú : Type*} [NontriviallyNormedField ùïú] {n : ‚Ñ§} {x : ùïú}

@[simp]
protected lemma continuousAt_zpow : ContinuousAt (fun x ‚Ü¶ x ^ n) x ‚Üî x ‚â† 0 ‚à® 0 ‚â§ n := by
  refine ‚ü®?_, continuousAt_zpow‚ÇÄ _ _‚ü©
  contrapose!
  rintro ‚ü®rfl, hm‚ü© hc
  exact (hc.tendsto.mono_left nhdsWithin_le_nhds).not_tendsto (Metric.disjoint_nhds_cobounded _)
    (tendsto_zpow_nhdsNE_zero_cobounded hm)

@[simp]
protected lemma continuousAt_inv : ContinuousAt Inv.inv x ‚Üî x ‚â† 0 := by
  simpa using NormedField.continuousAt_zpow (n := -1) (x := x)

end NontriviallyNormedField
end NormedField

instance Rat.instNormedField : NormedField ‚Ñö where
  __ := instField
  __ := instNormedAddCommGroup
  norm_mul a b := by simp only [norm, Rat.cast_mul, abs_mul]

instance Rat.instDenselyNormedField : DenselyNormedField ‚Ñö where
  lt_norm_lt r‚ÇÅ r‚ÇÇ h‚ÇÄ hr :=
    let ‚ü®q, h‚ü© := exists_rat_btwn hr
    ‚ü®q, by rwa [‚Üê Rat.norm_cast_real, Real.norm_eq_abs, abs_of_pos (h‚ÇÄ.trans_lt h.1)]‚ü©

section Complete

lemma NormedField.completeSpace_iff_isComplete_closedBall {K : Type*} [NormedField K] :
    CompleteSpace K ‚Üî IsComplete (Metric.closedBall 0 1 : Set K) := by
  constructor <;> intro h
  ¬∑ exact Metric.isClosed_closedBall.isComplete
  rcases NormedField.discreteTopology_or_nontriviallyNormedField K with _ | ‚ü®_, rfl‚ü©
  ¬∑ rwa [completeSpace_iff_isComplete_univ,
         ‚Üê NormedDivisionRing.unitClosedBall_eq_univ_of_discrete]
  refine Metric.complete_of_cauchySeq_tendsto fun u hu ‚Ü¶ ?_
  obtain ‚ü®k, hk‚ü© := hu.norm_bddAbove
  have kpos : 0 ‚â§ k := (_root_.norm_nonneg (u 0)).trans (hk (by simp))
  obtain ‚ü®x, hx‚ü© := NormedField.exists_lt_norm K k
  have hu' : CauchySeq ((¬∑ / x) ‚àò u) := (uniformContinuous_div_const' x).comp_cauchySeq hu
  have hb : ‚àÄ n, ((¬∑ / x) ‚àò u) n ‚àà Metric.closedBall 0 1 := by
    intro
    simp only [Function.comp_apply, Metric.mem_closedBall, dist_zero_right, norm_div]
    rw [div_le_one (kpos.trans_lt hx)]
    exact hx.le.trans' (hk (by simp))
  obtain ‚ü®a, -, ha'‚ü© := cauchySeq_tendsto_of_isComplete h hb hu'
  refine ‚ü®a * x, (((continuous_mul_right x).tendsto a).comp ha').congr ?_‚ü©
  have hx' : x ‚â† 0 := by
    contrapose! hx
    simp [hx, kpos]
  simp [div_mul_cancel‚ÇÄ _ hx']

end Complete
