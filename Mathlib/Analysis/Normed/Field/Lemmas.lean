/-
Copyright (c) 2018 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Johannes H√∂lzl
-/
import Mathlib.GroupTheory.OrderOfElement
import Mathlib.Algebra.Group.AddChar
import Mathlib.Algebra.Group.TypeTags.Finite
import Mathlib.Algebra.Order.GroupWithZero.Finset
import Mathlib.Analysis.Normed.Field.Basic
import Mathlib.Analysis.Normed.Group.Bounded
import Mathlib.Analysis.Normed.Group.Rat
import Mathlib.Analysis.Normed.Group.Uniform
import Mathlib.Topology.MetricSpace.DilationEquiv
import Mathlib.Topology.Instances.NNReal.Defs

/-!
# Normed fields

In this file we continue building the theory of (semi)normed rings and fields.

Some useful results that relate the topology of the normed field to the discrete topology include:
* `discreteTopology_or_nontriviallyNormedField`
* `discreteTopology_of_bddAbove_range_norm`

-/

-- Guard against import creep.
assert_not_exists RestrictScalars

variable {Œ± : Type*} {Œ≤ : Type*} {Œπ : Type*}

open Filter Bornology
open scoped Topology NNReal Pointwise

section NonUnitalSeminormedRing

variable [NonUnitalSeminormedRing Œ±]

theorem Filter.Tendsto.zero_mul_isBoundedUnder_le {f g : Œπ ‚Üí Œ±} {l : Filter Œπ}
    (hf : Tendsto f l (ùìù 0)) (hg : IsBoundedUnder (¬∑ ‚â§ ¬∑) l ((‚Äñ¬∑‚Äñ) ‚àò g)) :
    Tendsto (fun x => f x * g x) l (ùìù 0) :=
  hf.op_zero_isBoundedUnder_le hg (¬∑ * ¬∑) norm_mul_le

theorem Filter.isBoundedUnder_le_mul_tendsto_zero {f g : Œπ ‚Üí Œ±} {l : Filter Œπ}
    (hf : IsBoundedUnder (¬∑ ‚â§ ¬∑) l (norm ‚àò f)) (hg : Tendsto g l (ùìù 0)) :
    Tendsto (fun x => f x * g x) l (ùìù 0) :=
  hg.op_zero_isBoundedUnder_le hf (flip (¬∑ * ¬∑)) fun x y =>
    (norm_mul_le y x).trans_eq (mul_comm _ _)

/-- Non-unital seminormed ring structure on the product of finitely many non-unital seminormed
rings, using the sup norm. -/
instance Pi.nonUnitalSeminormedRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ]
    [‚àÄ i, NonUnitalSeminormedRing (œÄ i)] : NonUnitalSeminormedRing (‚àÄ i, œÄ i) :=
  { Pi.seminormedAddCommGroup, Pi.nonUnitalRing with
    norm_mul := fun x y =>
      NNReal.coe_mono <|
        calc
          (Finset.univ.sup fun i => ‚Äñx i * y i‚Äñ‚Çä) ‚â§
              Finset.univ.sup ((fun i => ‚Äñx i‚Äñ‚Çä) * fun i => ‚Äñy i‚Äñ‚Çä) :=
            Finset.sup_mono_fun fun _ _ => norm_mul_le _ _
          _ ‚â§ (Finset.univ.sup fun i => ‚Äñx i‚Äñ‚Çä) * Finset.univ.sup fun i => ‚Äñy i‚Äñ‚Çä :=
            Finset.sup_mul_le_mul_sup_of_nonneg (fun _ _ => zero_le _) fun _ _ => zero_le _
           }

end NonUnitalSeminormedRing

section SeminormedRing

variable [SeminormedRing Œ±]

/-- Seminormed ring structure on the product of finitely many seminormed rings,
  using the sup norm. -/
instance Pi.seminormedRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, SeminormedRing (œÄ i)] :
    SeminormedRing (‚àÄ i, œÄ i) :=
  { Pi.nonUnitalSeminormedRing, Pi.ring with }

end SeminormedRing

section NonUnitalNormedRing

variable [NonUnitalNormedRing Œ±]

/-- Normed ring structure on the product of finitely many non-unital normed rings, using the sup
norm. -/
instance Pi.nonUnitalNormedRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, NonUnitalNormedRing (œÄ i)] :
    NonUnitalNormedRing (‚àÄ i, œÄ i) :=
  { Pi.nonUnitalSeminormedRing, Pi.normedAddCommGroup with }

end NonUnitalNormedRing

section NormedRing

variable [NormedRing Œ±]

/-- Normed ring structure on the product of finitely many normed rings, using the sup norm. -/
instance Pi.normedRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, NormedRing (œÄ i)] :
    NormedRing (‚àÄ i, œÄ i) :=
  { Pi.seminormedRing, Pi.normedAddCommGroup with }

end NormedRing

section NonUnitalSeminormedCommRing

variable [NonUnitalSeminormedCommRing Œ±]

/-- Non-unital seminormed commutative ring structure on the product of finitely many non-unital
seminormed commutative rings, using the sup norm. -/
instance Pi.nonUnitalSeminormedCommRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ]
    [‚àÄ i, NonUnitalSeminormedCommRing (œÄ i)] : NonUnitalSeminormedCommRing (‚àÄ i, œÄ i) :=
  { Pi.nonUnitalSeminormedRing, Pi.nonUnitalCommRing with }

end NonUnitalSeminormedCommRing

section NonUnitalNormedCommRing

variable [NonUnitalNormedCommRing Œ±]

/-- Normed commutative ring structure on the product of finitely many non-unital normed
commutative rings, using the sup norm. -/
instance Pi.nonUnitalNormedCommRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ]
    [‚àÄ i, NonUnitalNormedCommRing (œÄ i)] : NonUnitalNormedCommRing (‚àÄ i, œÄ i) :=
  { Pi.nonUnitalSeminormedCommRing, Pi.normedAddCommGroup with }

end NonUnitalNormedCommRing

section SeminormedCommRing

variable [SeminormedCommRing Œ±]

/-- Seminormed commutative ring structure on the product of finitely many seminormed commutative
rings, using the sup norm. -/
instance Pi.seminormedCommRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, SeminormedCommRing (œÄ i)] :
    SeminormedCommRing (‚àÄ i, œÄ i) :=
  { Pi.nonUnitalSeminormedCommRing, Pi.ring with }

end SeminormedCommRing

section NormedCommRing

variable [NormedCommRing Œ±]

/-- Normed commutative ring structure on the product of finitely many normed commutative rings,
using the sup norm. -/
instance Pi.normedCommutativeRing {œÄ : Œπ ‚Üí Type*} [Fintype Œπ] [‚àÄ i, NormedCommRing (œÄ i)] :
    NormedCommRing (‚àÄ i, œÄ i) :=
  { Pi.seminormedCommRing, Pi.normedAddCommGroup with }

end NormedCommRing

-- see Note [lower instance priority]
instance (priority := 100) NonUnitalSeminormedRing.toContinuousMul [NonUnitalSeminormedRing Œ±] :
    ContinuousMul Œ± :=
  ‚ü®continuous_iff_continuousAt.2 fun x =>
      tendsto_iff_norm_sub_tendsto_zero.2 <| by
        have : ‚àÄ e : Œ± √ó Œ±,
            ‚Äñe.1 * e.2 - x.1 * x.2‚Äñ ‚â§ ‚Äñe.1‚Äñ * ‚Äñe.2 - x.2‚Äñ + ‚Äñe.1 - x.1‚Äñ * ‚Äñx.2‚Äñ := by
          intro e
          calc
            ‚Äñe.1 * e.2 - x.1 * x.2‚Äñ ‚â§ ‚Äñe.1 * (e.2 - x.2) + (e.1 - x.1) * x.2‚Äñ := by
              rw [mul_sub, sub_mul, sub_add_sub_cancel]
            _ ‚â§ ‚Äñe.1‚Äñ * ‚Äñe.2 - x.2‚Äñ + ‚Äñe.1 - x.1‚Äñ * ‚Äñx.2‚Äñ :=
              norm_add_le_of_le (norm_mul_le _ _) (norm_mul_le _ _)
        refine squeeze_zero (fun e => norm_nonneg _) this ?_
        convert
          ((continuous_fst.tendsto x).norm.mul
                ((continuous_snd.tendsto x).sub tendsto_const_nhds).norm).add
            (((continuous_fst.tendsto x).sub tendsto_const_nhds).norm.mul _)
        -- Porting note: `show` used to select a goal to work on
        rotate_right
        ¬∑ show Tendsto _ _ _
          exact tendsto_const_nhds
        ¬∑ simp‚ü©

-- see Note [lower instance priority]
/-- A seminormed ring is a topological ring. -/
instance (priority := 100) NonUnitalSeminormedRing.toTopologicalRing [NonUnitalSeminormedRing Œ±] :
    TopologicalRing Œ± where

namespace SeparationQuotient

instance [NonUnitalSeminormedRing Œ±] : NonUnitalNormedRing (SeparationQuotient Œ±) where
  __ : NonUnitalRing (SeparationQuotient Œ±) := inferInstance
  __ : NormedAddCommGroup (SeparationQuotient Œ±) := inferInstance
  norm_mul := Quotient.ind‚ÇÇ norm_mul_le

instance [NonUnitalSeminormedCommRing Œ±] : NonUnitalNormedCommRing (SeparationQuotient Œ±) where
  __ : NonUnitalCommRing (SeparationQuotient Œ±) := inferInstance
  __ : NormedAddCommGroup (SeparationQuotient Œ±) := inferInstance
  norm_mul := Quotient.ind‚ÇÇ norm_mul_le

instance [SeminormedRing Œ±] : NormedRing (SeparationQuotient Œ±) where
  __ : Ring (SeparationQuotient Œ±) := inferInstance
  __ : NormedAddCommGroup (SeparationQuotient Œ±) := inferInstance
  norm_mul := Quotient.ind‚ÇÇ norm_mul_le

instance [SeminormedCommRing Œ±] : NormedCommRing (SeparationQuotient Œ±) where
  __ : CommRing (SeparationQuotient Œ±) := inferInstance
  __ : NormedAddCommGroup (SeparationQuotient Œ±) := inferInstance
  norm_mul := Quotient.ind‚ÇÇ norm_mul_le

instance [SeminormedAddCommGroup Œ±] [One Œ±] [NormOneClass Œ±] :
    NormOneClass (SeparationQuotient Œ±) where
  norm_one := norm_one (Œ± := Œ±)

end SeparationQuotient

section NormedDivisionRing

variable [NormedDivisionRing Œ±] {a : Œ±}

lemma antilipschitzWith_mul_left {a : Œ±} (ha : a ‚â† 0) : AntilipschitzWith (‚Äña‚Äñ‚Çä‚Åª¬π) (a * ¬∑) :=
  AntilipschitzWith.of_le_mul_dist fun _ _ ‚Ü¶ by simp [dist_eq_norm, ‚Üê _root_.mul_sub, ha]

lemma antilipschitzWith_mul_right {a : Œ±} (ha : a ‚â† 0) : AntilipschitzWith (‚Äña‚Äñ‚Çä‚Åª¬π) (¬∑ * a) :=
  AntilipschitzWith.of_le_mul_dist fun _ _ ‚Ü¶ by
    simp [dist_eq_norm, ‚Üê _root_.sub_mul, ‚Üê mul_comm (‚Äña‚Äñ), ha]

/-- Multiplication by a nonzero element `a` on the left
as a `DilationEquiv` of a normed division ring. -/
@[simps!]
def DilationEquiv.mulLeft (a : Œ±) (ha : a ‚â† 0) : Œ± ‚âÉ·µà Œ± where
  toEquiv := Equiv.mulLeft‚ÇÄ a ha
  edist_eq' := ‚ü®‚Äña‚Äñ‚Çä, nnnorm_ne_zero_iff.2 ha, fun x y ‚Ü¶ by
    simp [edist_nndist, nndist_eq_nnnorm, ‚Üê mul_sub]‚ü©

/-- Multiplication by a nonzero element `a` on the right
as a `DilationEquiv` of a normed division ring. -/
@[simps!]
def DilationEquiv.mulRight (a : Œ±) (ha : a ‚â† 0) : Œ± ‚âÉ·µà Œ± where
  toEquiv := Equiv.mulRight‚ÇÄ a ha
  edist_eq' := ‚ü®‚Äña‚Äñ‚Çä, nnnorm_ne_zero_iff.2 ha, fun x y ‚Ü¶ by
    simp [edist_nndist, nndist_eq_nnnorm, ‚Üê sub_mul, ‚Üê mul_comm (‚Äña‚Äñ‚Çä)]‚ü©

namespace Filter

@[simp]
lemma comap_mul_left_cobounded {a : Œ±} (ha : a ‚â† 0) :
    comap (a * ¬∑) (cobounded Œ±) = cobounded Œ± :=
  Dilation.comap_cobounded (DilationEquiv.mulLeft a ha)

@[simp]
lemma map_mul_left_cobounded {a : Œ±} (ha : a ‚â† 0) :
    map (a * ¬∑) (cobounded Œ±) = cobounded Œ± :=
  DilationEquiv.map_cobounded (DilationEquiv.mulLeft a ha)

@[simp]
lemma comap_mul_right_cobounded {a : Œ±} (ha : a ‚â† 0) :
    comap (¬∑ * a) (cobounded Œ±) = cobounded Œ± :=
  Dilation.comap_cobounded (DilationEquiv.mulRight a ha)

@[simp]
lemma map_mul_right_cobounded {a : Œ±} (ha : a ‚â† 0) :
    map (¬∑ * a) (cobounded Œ±) = cobounded Œ± :=
  DilationEquiv.map_cobounded (DilationEquiv.mulRight a ha)

/-- Multiplication on the left by a nonzero element of a normed division ring tends to infinity at
infinity. -/
theorem tendsto_mul_left_cobounded {a : Œ±} (ha : a ‚â† 0) :
    Tendsto (a * ¬∑) (cobounded Œ±) (cobounded Œ±) :=
  (map_mul_left_cobounded ha).le

/-- Multiplication on the right by a nonzero element of a normed division ring tends to infinity at
infinity. -/
theorem tendsto_mul_right_cobounded {a : Œ±} (ha : a ‚â† 0) :
    Tendsto (¬∑ * a) (cobounded Œ±) (cobounded Œ±) :=
  (map_mul_right_cobounded ha).le

@[simp]
lemma inv_cobounded‚ÇÄ : (cobounded Œ±)‚Åª¬π = ùìù[‚â†] 0 := by
  rw [‚Üê comap_norm_atTop, ‚Üê Filter.comap_inv, ‚Üê comap_norm_nhdsGT_zero, ‚Üê inv_atTop‚ÇÄ,
    ‚Üê Filter.comap_inv]
  simp only [comap_comap, Function.comp_def, norm_inv]

@[simp]
lemma inv_nhdsWithin_ne_zero : (ùìù[‚â†] (0 : Œ±))‚Åª¬π = cobounded Œ± := by
  rw [‚Üê inv_cobounded‚ÇÄ, inv_inv]

lemma tendsto_inv‚ÇÄ_cobounded' : Tendsto Inv.inv (cobounded Œ±) (ùìù[‚â†] 0) :=
  inv_cobounded‚ÇÄ.le

theorem tendsto_inv‚ÇÄ_cobounded : Tendsto Inv.inv (cobounded Œ±) (ùìù 0) :=
  tendsto_inv‚ÇÄ_cobounded'.mono_right inf_le_left

lemma tendsto_inv‚ÇÄ_nhdsWithin_ne_zero : Tendsto Inv.inv (ùìù[‚â†] 0) (cobounded Œ±) :=
  inv_nhdsWithin_ne_zero.le

end Filter

-- see Note [lower instance priority]
instance (priority := 100) NormedDivisionRing.to_hasContinuousInv‚ÇÄ : HasContinuousInv‚ÇÄ Œ± := by
  refine ‚ü®fun r r0 => tendsto_iff_norm_sub_tendsto_zero.2 ?_‚ü©
  have r0' : 0 < ‚Äñr‚Äñ := norm_pos_iff.2 r0
  rcases exists_between r0' with ‚ü®Œµ, Œµ0, Œµr‚ü©
  have : ‚àÄ·∂† e in ùìù r, ‚Äñe‚Åª¬π - r‚Åª¬π‚Äñ ‚â§ ‚Äñr - e‚Äñ / ‚Äñr‚Äñ / Œµ := by
    filter_upwards [(isOpen_lt continuous_const continuous_norm).eventually_mem Œµr] with e he
    have e0 : e ‚â† 0 := norm_pos_iff.1 (Œµ0.trans he)
    calc
      ‚Äñe‚Åª¬π - r‚Åª¬π‚Äñ = ‚Äñr‚Äñ‚Åª¬π * ‚Äñr - e‚Äñ * ‚Äñe‚Äñ‚Åª¬π := by
        rw [‚Üê norm_inv, ‚Üê norm_inv, ‚Üê norm_mul, ‚Üê norm_mul, mul_sub, sub_mul,
          mul_assoc _ e, inv_mul_cancel‚ÇÄ r0, mul_inv_cancel‚ÇÄ e0, one_mul, mul_one]
      _ = ‚Äñr - e‚Äñ / ‚Äñr‚Äñ / ‚Äñe‚Äñ := by field_simp [mul_comm]
      _ ‚â§ ‚Äñr - e‚Äñ / ‚Äñr‚Äñ / Œµ := by gcongr
  refine squeeze_zero' (Eventually.of_forall fun _ => norm_nonneg _) this ?_
  refine (((continuous_const.sub continuous_id).norm.div_const _).div_const _).tendsto' _ _ ?_
  simp

-- see Note [lower instance priority]
/-- A normed division ring is a topological division ring. -/
instance (priority := 100) NormedDivisionRing.to_topologicalDivisionRing :
    TopologicalDivisionRing Œ± where

protected lemma IsOfFinOrder.norm_eq_one (ha : IsOfFinOrder a) : ‚Äña‚Äñ = 1 :=
  ((normHom : Œ± ‚Üí*‚ÇÄ ‚Ñù).toMonoidHom.isOfFinOrder ha).eq_one <| norm_nonneg _

example [Monoid Œ≤] (œÜ : Œ≤ ‚Üí* Œ±) {x : Œ≤} {k : ‚Ñï+} (h : x ^ (k : ‚Ñï) = 1) :
    ‚ÄñœÜ x‚Äñ = 1 := (œÜ.isOfFinOrder <| isOfFinOrder_iff_pow_eq_one.2 ‚ü®_, k.2, h‚ü©).norm_eq_one

@[simp] lemma AddChar.norm_apply {G : Type*} [AddLeftCancelMonoid G] [Finite G] (œà : AddChar G Œ±)
    (x : G) : ‚Äñœà x‚Äñ = 1 := (œà.toMonoidHom.isOfFinOrder <| isOfFinOrder_of_finite _).norm_eq_one

lemma NormedField.tendsto_norm_inv_nhdsNE_zero_atTop : Tendsto (fun x : Œ± ‚Ü¶ ‚Äñx‚Åª¬π‚Äñ) (ùìù[‚â†] 0) atTop :=
  (tendsto_inv_nhdsGT_zero.comp tendsto_norm_nhdsNE_zero).congr fun x ‚Ü¶ (norm_inv x).symm

@[deprecated (since := "2024-12-22")]
alias NormedField.tendsto_norm_inverse_nhdsWithin_0_atTop :=
  NormedField.tendsto_norm_inv_nhdsNE_zero_atTop

lemma NormedField.tendsto_norm_zpow_nhdsNE_zero_atTop {m : ‚Ñ§} (hm : m < 0) :
    Tendsto (fun x : Œ± ‚Ü¶ ‚Äñx ^ m‚Äñ) (ùìù[‚â†] 0) atTop := by
  obtain ‚ü®m, rfl‚ü© := neg_surjective m
  rw [neg_lt_zero] at hm
  lift m to ‚Ñï using hm.le
  rw [Int.natCast_pos] at hm
  simp only [norm_pow, zpow_neg, zpow_natCast, ‚Üê inv_pow]
  exact (tendsto_pow_atTop hm.ne').comp NormedField.tendsto_norm_inv_nhdsNE_zero_atTop

@[deprecated (since := "2024-12-22")]
alias NormedField.tendsto_norm_zpow_nhdsWithin_0_atTop :=
  NormedField.tendsto_norm_zpow_nhdsNE_zero_atTop

end NormedDivisionRing

namespace NormedField

/-- A normed field is either nontrivially normed or has a discrete topology.
In the discrete topology case, all the norms are 1, by `norm_eq_one_iff_ne_zero_of_discrete`.
The nontrivially normed field instance is provided by a subtype with a proof that the
forgetful inheritance to the existing `NormedField` instance is definitionally true.
This allows one to have the new `NontriviallyNormedField` instance without data clashes. -/
lemma discreteTopology_or_nontriviallyNormedField (ùïú : Type*) [h : NormedField ùïú] :
    DiscreteTopology ùïú ‚à® Nonempty ({h' : NontriviallyNormedField ùïú // h'.toNormedField = h}) := by
  by_cases H : ‚àÉ x : ùïú, x ‚â† 0 ‚àß ‚Äñx‚Äñ ‚â† 1
  ¬∑ exact Or.inr ‚ü®(‚ü®NontriviallyNormedField.ofNormNeOne H, rfl‚ü©)‚ü©
  ¬∑ simp_rw [discreteTopology_iff_isOpen_singleton_zero, Metric.isOpen_singleton_iff, dist_eq_norm,
             sub_zero]
    refine Or.inl ‚ü®1, zero_lt_one, ?_‚ü©
    contrapose! H
    refine H.imp ?_
    -- contextual to reuse the `a ‚â† 0` hypothesis in the proof of `a ‚â† 0 ‚àß ‚Äña‚Äñ ‚â† 1`
    simp (config := {contextual := true}) [add_comm, ne_of_lt]

lemma discreteTopology_of_bddAbove_range_norm {ùïú : Type*} [NormedField ùïú]
    (h : BddAbove (Set.range fun k : ùïú ‚Ü¶ ‚Äñk‚Äñ)) :
    DiscreteTopology ùïú := by
  refine (NormedField.discreteTopology_or_nontriviallyNormedField _).resolve_right ?_
  rintro ‚ü®_, rfl‚ü©
  obtain ‚ü®x, h‚ü© := h
  obtain ‚ü®k, hk‚ü© := NormedField.exists_lt_norm ùïú x
  exact hk.not_le (h (Set.mem_range_self k))

section Densely

variable (Œ±) [DenselyNormedField Œ±]

theorem denseRange_nnnorm : DenseRange (nnnorm : Œ± ‚Üí ‚Ñù‚â•0) :=
  dense_of_exists_between fun _ _ hr =>
    let ‚ü®x, h‚ü© := exists_lt_nnnorm_lt Œ± hr
    ‚ü®‚Äñx‚Äñ‚Çä, ‚ü®x, rfl‚ü©, h‚ü©

end Densely

section NontriviallyNormedField
variable {ùïú : Type*} [NontriviallyNormedField ùïú] {n : ‚Ñ§} {x : ùïú}

@[simp]
protected lemma continuousAt_zpow : ContinuousAt (fun x ‚Ü¶ x ^ n) x ‚Üî x ‚â† 0 ‚à® 0 ‚â§ n := by
  refine ‚ü®?_, continuousAt_zpow‚ÇÄ _ _‚ü©
  contrapose!
  rintro ‚ü®rfl, hm‚ü© hc
  exact not_tendsto_atTop_of_tendsto_nhds (hc.tendsto.mono_left nhdsWithin_le_nhds).norm
    (NormedField.tendsto_norm_zpow_nhdsNE_zero_atTop hm)

@[simp]
protected lemma continuousAt_inv : ContinuousAt Inv.inv x ‚Üî x ‚â† 0 := by
  simpa using NormedField.continuousAt_zpow (n := -1) (x := x)

end NontriviallyNormedField
end NormedField

namespace NNReal

lemma lipschitzWith_sub : LipschitzWith 2 (fun (p : ‚Ñù‚â•0 √ó ‚Ñù‚â•0) ‚Ü¶ p.1 - p.2) := by
  rw [‚Üê isometry_subtype_coe.lipschitzWith_iff]
  have : Isometry (Prod.map ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù)) :=
    isometry_subtype_coe.prod_map isometry_subtype_coe
  convert (((LipschitzWith.prod_fst.comp this.lipschitz).sub
    (LipschitzWith.prod_snd.comp this.lipschitz)).max_const 0)
  norm_num

end NNReal

instance Int.instNormedCommRing : NormedCommRing ‚Ñ§ where
  __ := instCommRing
  __ := instNormedAddCommGroup
  norm_mul m n := by simp only [norm, Int.cast_mul, abs_mul, le_rfl]

instance Int.instNormOneClass : NormOneClass ‚Ñ§ :=
  ‚ü®by simp [‚Üê Int.norm_cast_real]‚ü©

instance Rat.instNormedField : NormedField ‚Ñö where
  __ := instField
  __ := instNormedAddCommGroup
  norm_mul' a b := by simp only [norm, Rat.cast_mul, abs_mul]

instance Rat.instDenselyNormedField : DenselyNormedField ‚Ñö where
  lt_norm_lt r‚ÇÅ r‚ÇÇ h‚ÇÄ hr :=
    let ‚ü®q, h‚ü© := exists_rat_btwn hr
    ‚ü®q, by rwa [‚Üê Rat.norm_cast_real, Real.norm_eq_abs, abs_of_pos (h‚ÇÄ.trans_lt h.1)]‚ü©

section Complete

lemma NormedField.completeSpace_iff_isComplete_closedBall {K : Type*} [NormedField K] :
    CompleteSpace K ‚Üî IsComplete (Metric.closedBall 0 1 : Set K) := by
  constructor <;> intro h
  ¬∑ exact Metric.isClosed_ball.isComplete
  rcases NormedField.discreteTopology_or_nontriviallyNormedField K with _|‚ü®_, rfl‚ü©
  ¬∑ rwa [completeSpace_iff_isComplete_univ,
         ‚Üê NormedDivisionRing.unitClosedBall_eq_univ_of_discrete]
  refine Metric.complete_of_cauchySeq_tendsto fun u hu ‚Ü¶ ?_
  obtain ‚ü®k, hk‚ü© := hu.norm_bddAbove
  have kpos : 0 ‚â§ k := (_root_.norm_nonneg (u 0)).trans (hk (by simp))
  obtain ‚ü®x, hx‚ü© := NormedField.exists_lt_norm K k
  have hu' : CauchySeq ((¬∑ / x) ‚àò u) := (uniformContinuous_div_const' x).comp_cauchySeq hu
  have hb : ‚àÄ n, ((¬∑ / x) ‚àò u) n ‚àà Metric.closedBall 0 1 := by
    intro
    simp only [Function.comp_apply, Metric.mem_closedBall, dist_zero_right, norm_div]
    rw [div_le_one (kpos.trans_lt hx)]
    exact hx.le.trans' (hk (by simp))
  obtain ‚ü®a, -, ha'‚ü© := cauchySeq_tendsto_of_isComplete h hb hu'
  refine ‚ü®a * x, (((continuous_mul_right x).tendsto a).comp ha').congr ?_‚ü©
  have hx' : x ‚â† 0 := by
    contrapose! hx
    simp [hx, kpos]
  simp [div_mul_cancel‚ÇÄ _ hx']

end Complete
