/-
Copyright (c) 2024 Yakov Pechersky. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yakov Pechersky
-/

import Mathlib.Analysis.Normed.Field.Lemmas
import Mathlib.Analysis.SpecificLimits.Basic
import Mathlib.Topology.MetricSpace.ProperSpace

/-!
# Proper nontrivially normed fields

Nontrivially normed fields are `ProperSpaces` when they are `WeaklyLocallyCompact`.

## Main results

* `ProperSpace.of_nontriviallyNormedField_of_weaklyLocallyCompactSpace`

## Implementation details

This is a special case of `ProperSpace.of_locallyCompactSpace` from
`Mathlib/Analysis/Normed/Module/FiniteDimension.lean`, specialized to be on the field itself
with a proof that requires fewer imports.
-/

assert_not_exists FiniteDimensional

open Metric Filter

/-- A weakly locally compact normed field is proper.
This is a specialization of `ProperSpace.of_locallyCompactSpace`
which holds for `NormedSpace`s but requires more imports. -/
lemma ProperSpace.of_nontriviallyNormedField_of_weaklyLocallyCompactSpace
    (ğ•œ : Type*) [NontriviallyNormedField ğ•œ] [WeaklyLocallyCompactSpace ğ•œ] :
    ProperSpace ğ•œ := by
  rcases exists_isCompact_closedBall (0 : ğ•œ) with âŸ¨r, rpos, hrâŸ©
  rcases NormedField.exists_one_lt_norm ğ•œ with âŸ¨c, hcâŸ©
  have hC n : IsCompact (closedBall (0 : ğ•œ) (â€–câ€–^n * r)) := by
    have : c ^ n â‰  0 := pow_ne_zero _ <| fun h â†¦ by simp [h, zero_le_one.not_gt] at hc
    convert hr.smul (c ^ n)
    ext
    simp only [mem_closedBall, dist_zero_right, Set.mem_smul_set_iff_inv_smul_memâ‚€ this,
      smul_eq_mul, norm_mul, norm_inv, norm_pow,
      inv_mul_le_iffâ‚€ (by simpa only [norm_pow] using norm_pos_iff.mpr this)]
  have hTop : Tendsto (fun n â†¦ â€–câ€–^n * r) atTop atTop :=
    Tendsto.atTop_mul_const rpos (tendsto_pow_atTop_atTop_of_one_lt hc)
  exact .of_seq_closedBall hTop (Eventually.of_forall hC)
