/-
Copyright (c) 2022 Mar√≠a In√©s de Frutos-Fern√°ndez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Mar√≠a In√©s de Frutos-Fern√°ndez, Ya√´l Dillies
-/
import Mathlib.Analysis.Normed.Field.Basic

#align_import analysis.normed.ring.seminorm from "leanprover-community/mathlib"@"7ea604785a41a0681eac70c5a82372493dbefc68"

/-!
# Seminorms and norms on rings

This file defines seminorms and norms on rings. These definitions are useful when one needs to
consider multiple (semi)norms on a given ring.

## Main declarations

For a ring `R`:
* `RingSeminorm`: A seminorm on a ring `R` is a function `f : R ‚Üí ‚Ñù` that preserves zero, takes
  nonnegative values, is subadditive and submultiplicative and such that `f (-x) = f x` for all
  `x ‚àà R`.
* `RingNorm`: A seminorm `f` is a norm if `f x = 0` if and only if `x = 0`.
* `MulRingSeminorm`: A multiplicative seminorm on a ring `R` is a ring seminorm that preserves
  multiplication.
* `MulRingNorm`: A multiplicative norm on a ring `R` is a ring norm that preserves multiplication.

## Notes

The corresponding hom classes are defined in `Mathlib.Analysis.Order.Hom.Basic` to be used by
absolute values.

## References

* [S. Bosch, U. G√ºntzer, R. Remmert, *Non-Archimedean Analysis*][bosch-guntzer-remmert]

## Tags
ring_seminorm, ring_norm
-/


open NNReal

variable {F R S : Type*} (x y : R) (r : ‚Ñù)

/-- A seminorm on a ring `R` is a function `f : R ‚Üí ‚Ñù` that preserves zero, takes nonnegative
  values, is subadditive and submultiplicative and such that `f (-x) = f x` for all `x ‚àà R`. -/
structure RingSeminorm (R : Type*) [NonUnitalNonAssocRing R] extends AddGroupSeminorm R where
  /-- The property of a `RingSeminorm` that for all `x` and `y` in the ring, the norm of `x * y` is
    less than the norm of `x` times the norm of `y`. -/
  mul_le' : ‚àÄ x y : R, toFun (x * y) ‚â§ toFun x * toFun y
#align ring_seminorm RingSeminorm

/-- A function `f : R ‚Üí ‚Ñù` is a norm on a (nonunital) ring if it is a seminorm and `f x = 0`
  implies `x = 0`. -/
structure RingNorm (R : Type*) [NonUnitalNonAssocRing R] extends RingSeminorm R, AddGroupNorm R
#align ring_norm RingNorm

/-- A multiplicative seminorm on a ring `R` is a function `f : R ‚Üí ‚Ñù` that preserves zero and
multiplication, takes nonnegative values, is subadditive and such that `f (-x) = f x` for all `x`.
-/
structure MulRingSeminorm (R : Type*) [NonAssocRing R] extends AddGroupSeminorm R,
  MonoidWithZeroHom R ‚Ñù
#align mul_ring_seminorm MulRingSeminorm

/-- A multiplicative norm on a ring `R` is a multiplicative ring seminorm such that `f x = 0`
implies `x = 0`. -/
structure MulRingNorm (R : Type*) [NonAssocRing R] extends MulRingSeminorm R, AddGroupNorm R
#align mul_ring_norm MulRingNorm

attribute [nolint docBlame]
  RingSeminorm.toAddGroupSeminorm RingNorm.toAddGroupNorm RingNorm.toRingSeminorm
    MulRingSeminorm.toAddGroupSeminorm MulRingSeminorm.toMonoidWithZeroHom
    MulRingNorm.toAddGroupNorm MulRingNorm.toMulRingSeminorm

namespace RingSeminorm

section NonUnitalRing

variable [NonUnitalRing R]

instance ringSeminormClass : RingSeminormClass (RingSeminorm R) R ‚Ñù where
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    -- ‚ä¢ { toAddGroupSeminorm := toAddGroupSeminorm‚úù, mul_le' := mul_le'‚úù } = g
    cases g
    -- ‚ä¢ { toAddGroupSeminorm := toAddGroupSeminorm‚úù¬π, mul_le' := mul_le'‚úù¬π } = { toA ‚Ä¶
    congr
    -- ‚ä¢ toAddGroupSeminorm‚úù¬π = toAddGroupSeminorm‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoAddGroupSeminorm‚úù¬π x = ‚ÜëtoAddGroupSeminorm‚úù x
    exact congr_fun h x
    -- üéâ no goals
  map_zero f := f.map_zero'
  map_add_le_add f := f.add_le'
  map_mul_le_mul f := f.mul_le'
  map_neg_eq_map f := f.neg'
#align ring_seminorm.ring_seminorm_class RingSeminorm.ringSeminormClass

/-- Helper instance for when there's too many metavariables to apply `FunLike.hasCoeToFun`. -/
instance : CoeFun (RingSeminorm R) fun _ => R ‚Üí ‚Ñù :=
  FunLike.hasCoeToFun

@[simp]
theorem toFun_eq_coe (p : RingSeminorm R) : (p.toAddGroupSeminorm : R ‚Üí ‚Ñù) = p :=
  rfl
#align ring_seminorm.to_fun_eq_coe RingSeminorm.toFun_eq_coe

@[ext]
theorem ext {p q : RingSeminorm R} : (‚àÄ x, p x = q x) ‚Üí p = q :=
  FunLike.ext p q
#align ring_seminorm.ext RingSeminorm.ext

instance : Zero (RingSeminorm R) :=
  ‚ü®{ AddGroupSeminorm.instZeroAddGroupSeminorm.zero with mul_le' :=
    fun _ _ => (zero_mul _).ge }‚ü©

theorem eq_zero_iff {p : RingSeminorm R} : p = 0 ‚Üî ‚àÄ x, p x = 0 :=
  FunLike.ext_iff
#align ring_seminorm.eq_zero_iff RingSeminorm.eq_zero_iff

theorem ne_zero_iff {p : RingSeminorm R} : p ‚â† 0 ‚Üî ‚àÉ x, p x ‚â† 0 := by simp [eq_zero_iff]
                                                                      -- üéâ no goals
#align ring_seminorm.ne_zero_iff RingSeminorm.ne_zero_iff

instance : Inhabited (RingSeminorm R) :=
  ‚ü®0‚ü©

/-- The trivial seminorm on a ring `R` is the `RingSeminorm` taking value `0` at `0` and `1` at
every other element. -/
instance [DecidableEq R] : One (RingSeminorm R) :=
  ‚ü®{ (1 : AddGroupSeminorm R) with
      mul_le' := fun x y => by
        by_cases h : x * y = 0
        -- ‚ä¢ AddGroupSeminorm.toFun { toFun := src‚úù.toFun, map_zero' := (_ : AddGroupSemi ‚Ä¶
        ¬∑ refine' (if_pos h).trans_le (mul_nonneg _ _) <;>
          -- ‚ä¢ 0 ‚â§ AddGroupSeminorm.toFun { toFun := src‚úù.toFun, map_zero' := (_ : AddGroup ‚Ä¶
            ¬∑ change _ ‚â§ ite _ _ _
              -- ‚ä¢ 0 ‚â§ if x = 0 then 0 else 1
              -- ‚ä¢ 0 ‚â§ if y = 0 then 0 else 1
              -- ‚ä¢ 0 ‚â§ 0
              split_ifs
              -- üéâ no goals
              -- ‚ä¢ 0 ‚â§ 0
              exacts [le_rfl, zero_le_one]
              -- üéâ no goals
        ¬∑ change ite _ _ _ ‚â§ ite _ _ _ * ite _ _ _
          -- ‚ä¢ (if x * y = 0 then 0 else 1) ‚â§ (if x = 0 then 0 else 1) * if y = 0 then 0 el ‚Ä¶
          simp only [if_false, h, left_ne_zero_of_mul h, right_ne_zero_of_mul h, mul_one,
            le_refl] }‚ü©

@[simp]
theorem apply_one [DecidableEq R] (x : R) : (1 : RingSeminorm R) x = if x = 0 then 0 else 1 :=
  rfl
#align ring_seminorm.apply_one RingSeminorm.apply_one

end NonUnitalRing

section Ring

variable [Ring R] (p : RingSeminorm R)

theorem seminorm_one_eq_one_iff_ne_zero (hp : p 1 ‚â§ 1) : p 1 = 1 ‚Üî p ‚â† 0 := by
  refine'
    ‚ü®fun h =>
      ne_zero_iff.mpr
        ‚ü®1, by
          rw [h]
          exact one_ne_zero‚ü©,
      fun h => ?_‚ü©
  obtain hp0 | hp0 := (map_nonneg p (1 : R)).eq_or_gt
  -- ‚ä¢ ‚Üëp 1 = 1
  ¬∑ exfalso
    -- ‚ä¢ False
    refine h (ext fun x => (map_nonneg _ _).antisymm' ?_)
    -- ‚ä¢ ‚Üëp x ‚â§ 0
    simpa only [hp0, mul_one, mul_zero] using map_mul_le_mul p x 1
    -- üéâ no goals
  ¬∑ refine' hp.antisymm ((le_mul_iff_one_le_left hp0).1 _)
    -- ‚ä¢ ‚Üëp 1 ‚â§ ‚Üëp 1 * ‚Üëp 1
    simpa only [one_mul] using map_mul_le_mul p (1 : R) _
    -- üéâ no goals
#align ring_seminorm.seminorm_one_eq_one_iff_ne_zero RingSeminorm.seminorm_one_eq_one_iff_ne_zero

end Ring

end RingSeminorm

/-- The norm of a `NonUnitalSeminormedRing` as a `RingSeminorm`. -/
def normRingSeminorm (R : Type*) [NonUnitalSeminormedRing R] : RingSeminorm R :=
  { normAddGroupSeminorm R with
    toFun := norm
    mul_le' := norm_mul_le }
#align norm_ring_seminorm normRingSeminorm

namespace RingNorm

variable [NonUnitalRing R]

instance ringNormClass : RingNormClass (RingNorm R) R ‚Ñù where
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    -- ‚ä¢ { toRingSeminorm := toRingSeminorm‚úù, eq_zero_of_map_eq_zero' := eq_zero_of_m ‚Ä¶
    cases g
    -- ‚ä¢ { toRingSeminorm := toRingSeminorm‚úù¬π, eq_zero_of_map_eq_zero' := eq_zero_of_ ‚Ä¶
    congr
    -- ‚ä¢ toRingSeminorm‚úù¬π = toRingSeminorm‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoRingSeminorm‚úù¬π x = ‚ÜëtoRingSeminorm‚úù x
    exact congr_fun h x
    -- üéâ no goals
  map_zero f := f.map_zero'
  map_add_le_add f := f.add_le'
  map_mul_le_mul f := f.mul_le'
  map_neg_eq_map f := f.neg'
  eq_zero_of_map_eq_zero f := f.eq_zero_of_map_eq_zero' _
#align ring_norm.ring_norm_class RingNorm.ringNormClass

/-- Helper instance for when there's too many metavariables to apply `FunLike.hasCoeToFun`. -/
instance : CoeFun (RingNorm R) fun _ => R ‚Üí ‚Ñù :=
  ‚ü®fun p => p.toFun‚ü©

-- Porting note: This is a syntactic tautology in Lean 4
-- @[simp]
-- theorem toFun_eq_coe (p : RingNorm R) : p.toFun = p := rfl
#noalign ring_norm.to_fun_eq_coe

@[ext]
theorem ext {p q : RingNorm R} : (‚àÄ x, p x = q x) ‚Üí p = q :=
  FunLike.ext p q
#align ring_norm.ext RingNorm.ext

variable (R)

/-- The trivial norm on a ring `R` is the `RingNorm` taking value `0` at `0` and `1` at every
  other element. -/
instance [DecidableEq R] : One (RingNorm R) :=
  ‚ü®{ (1 : RingSeminorm R), (1 : AddGroupNorm R) with }‚ü©

@[simp]
theorem apply_one [DecidableEq R] (x : R) : (1 : RingNorm R) x = if x = 0 then 0 else 1 :=
  rfl
#align ring_norm.apply_one RingNorm.apply_one

instance [DecidableEq R] : Inhabited (RingNorm R) :=
  ‚ü®1‚ü©

end RingNorm

namespace MulRingSeminorm

variable [NonAssocRing R]

instance mulRingSeminormClass : MulRingSeminormClass (MulRingSeminorm R) R ‚Ñù where
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    -- ‚ä¢ { toAddGroupSeminorm := toAddGroupSeminorm‚úù, map_one' := map_one'‚úù, map_mul' ‚Ä¶
    cases g
    -- ‚ä¢ { toAddGroupSeminorm := toAddGroupSeminorm‚úù¬π, map_one' := map_one'‚úù¬π, map_mu ‚Ä¶
    congr
    -- ‚ä¢ toAddGroupSeminorm‚úù¬π = toAddGroupSeminorm‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoAddGroupSeminorm‚úù¬π x = ‚ÜëtoAddGroupSeminorm‚úù x
    exact congr_fun h x
    -- üéâ no goals
  map_zero f := f.map_zero'
  map_one f := f.map_one'
  map_add_le_add f := f.add_le'
  map_mul f := f.map_mul'
  map_neg_eq_map f := f.neg'
#align mul_ring_seminorm.mul_ring_seminorm_class MulRingSeminorm.mulRingSeminormClass

/-- Helper instance for when there's too many metavariables to apply `FunLike.hasCoeToFun`. -/
instance : CoeFun (MulRingSeminorm R) fun _ => R ‚Üí ‚Ñù :=
  FunLike.hasCoeToFun

@[simp]
theorem toFun_eq_coe (p : MulRingSeminorm R) : (p.toAddGroupSeminorm : R ‚Üí ‚Ñù) = p :=
  rfl
#align mul_ring_seminorm.to_fun_eq_coe MulRingSeminorm.toFun_eq_coe

@[ext]
theorem ext {p q : MulRingSeminorm R} : (‚àÄ x, p x = q x) ‚Üí p = q :=
  FunLike.ext p q
#align mul_ring_seminorm.ext MulRingSeminorm.ext

variable [DecidableEq R] [NoZeroDivisors R] [Nontrivial R]

/-- The trivial seminorm on a ring `R` is the `MulRingSeminorm` taking value `0` at `0` and `1` at
every other element. -/
instance : One (MulRingSeminorm R) :=
  ‚ü®{ (1 : AddGroupSeminorm R) with
      map_one' := if_neg one_ne_zero
      map_mul' := fun x y => by
        obtain rfl | hx := eq_or_ne x 0
        -- ‚ä¢ AddGroupSeminorm.toFun { toFun := src‚úù.toFun, map_zero' := (_ : AddGroupSemi ‚Ä¶
        ¬∑ simp
          -- üéâ no goals
        obtain rfl | hy := eq_or_ne y 0
        -- ‚ä¢ AddGroupSeminorm.toFun { toFun := src‚úù.toFun, map_zero' := (_ : AddGroupSemi ‚Ä¶
        ¬∑ simp
          -- üéâ no goals
        ¬∑ simp [hx, hy] }‚ü©
          -- üéâ no goals

@[simp]
theorem apply_one (x : R) : (1 : MulRingSeminorm R) x = if x = 0 then 0 else 1 :=
  rfl
#align mul_ring_seminorm.apply_one MulRingSeminorm.apply_one

instance : Inhabited (MulRingSeminorm R) :=
  ‚ü®1‚ü©

end MulRingSeminorm

namespace MulRingNorm

variable [NonAssocRing R]

instance mulRingNormClass : MulRingNormClass (MulRingNorm R) R ‚Ñù where
  coe f := f.toFun
  coe_injective' f g h := by
    cases f
    -- ‚ä¢ { toMulRingSeminorm := toMulRingSeminorm‚úù, eq_zero_of_map_eq_zero' := eq_zer ‚Ä¶
    cases g
    -- ‚ä¢ { toMulRingSeminorm := toMulRingSeminorm‚úù¬π, eq_zero_of_map_eq_zero' := eq_ze ‚Ä¶
    congr
    -- ‚ä¢ toMulRingSeminorm‚úù¬π = toMulRingSeminorm‚úù
    ext x
    -- ‚ä¢ ‚ÜëtoMulRingSeminorm‚úù¬π x = ‚ÜëtoMulRingSeminorm‚úù x
    exact congr_fun h x
    -- üéâ no goals
  map_zero f := f.map_zero'
  map_one f := f.map_one'
  map_add_le_add f := f.add_le'
  map_mul f := f.map_mul'
  map_neg_eq_map f := f.neg'
  eq_zero_of_map_eq_zero f := f.eq_zero_of_map_eq_zero' _
#align mul_ring_norm.mul_ring_norm_class MulRingNorm.mulRingNormClass

/-- Helper instance for when there's too many metavariables to apply `FunLike.hasCoeToFun`. -/
instance : CoeFun (MulRingNorm R) fun _ => R ‚Üí ‚Ñù :=
  ‚ü®fun p => p.toFun‚ü©

-- Porting note: This is a syntactic tautology in Lean 4
-- @[simp]
-- theorem toFun_eq_coe (p : MulRingNorm R) : p.toFun = p := rfl
#noalign mul_ring_norm.to_fun_eq_coe

@[ext]
theorem ext {p q : MulRingNorm R} : (‚àÄ x, p x = q x) ‚Üí p = q :=
  FunLike.ext p q
#align mul_ring_norm.ext MulRingNorm.ext

variable (R)

variable [DecidableEq R] [NoZeroDivisors R] [Nontrivial R]

/-- The trivial norm on a ring `R` is the `MulRingNorm` taking value `0` at `0` and `1` at every
other element. -/
instance : One (MulRingNorm R) :=
  ‚ü®{ (1 : MulRingSeminorm R), (1 : AddGroupNorm R) with }‚ü©

@[simp]
theorem apply_one (x : R) : (1 : MulRingNorm R) x = if x = 0 then 0 else 1 :=
  rfl
#align mul_ring_norm.apply_one MulRingNorm.apply_one

instance : Inhabited (MulRingNorm R) :=
  ‚ü®1‚ü©

end MulRingNorm

/-- A nonzero ring seminorm on a field `K` is a ring norm. -/
def RingSeminorm.toRingNorm {K : Type*} [Field K] (f : RingSeminorm K) (hnt : f ‚â† 0) :
    RingNorm K :=
  { f with
    eq_zero_of_map_eq_zero' := fun x hx => by
      obtain ‚ü®c, hc‚ü© := RingSeminorm.ne_zero_iff.mp hnt
      -- ‚ä¢ x = 0
      by_contra hn0
      -- ‚ä¢ False
      have hc0 : f c = 0 := by
        rw [‚Üê mul_one c, ‚Üê mul_inv_cancel hn0, ‚Üê mul_assoc, mul_comm c, mul_assoc]
        exact
          le_antisymm
            (le_trans (map_mul_le_mul f _ _)
              (by rw [‚Üê RingSeminorm.toFun_eq_coe, ‚Üê AddGroupSeminorm.toFun_eq_coe, hx,
                zero_mul]))
            (map_nonneg f _)
      exact hc hc0 }
      -- üéâ no goals
#align ring_seminorm.to_ring_norm RingSeminorm.toRingNorm

/-- The norm of a `NonUnitalNormedRing` as a `RingNorm`. -/
@[simps!]
def normRingNorm (R : Type*) [NonUnitalNormedRing R] : RingNorm R :=
  { normAddGroupNorm R, normRingSeminorm R with }
#align norm_ring_norm normRingNorm
