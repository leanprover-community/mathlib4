/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.SpecificLimits.Normed
import Mathlib.Topology.Algebra.Ring.Ideal
import Mathlib.RingTheory.Ideal.Nonunits

/-!
# The group of units of a complete normed ring

This file contains the basic theory for the group of units (invertible elements) of a complete
normed ring (Banach algebras being a notable special case).

## Main results

The constructions `Units.add` and `Units.ofNearby` (based on `Units.oneSub` defined elsewhere)
state, in varying forms, that perturbations of a unit are units. They are not stated
in their optimal form; more precise versions would use the spectral radius.

The first main result is `Units.isOpen`: the group of units of a normed ring with summable
geometric series is an open subset of the ring.

The function `Ring.inverse` (defined elsewhere), for a ring `R`, sends `a : R` to `aâ»Â¹` if `a` is a
unit and `0` if not.  The other major results of this file (notably `NormedRing.inverse_add`,
`NormedRing.inverse_add_norm` and `NormedRing.inverse_add_norm_diff_nth_order`) cover the asymptotic
properties of `Ring.inverse (x + t)` as `t â†’ 0`.
-/

noncomputable section

open Topology

variable {R : Type*} [NormedRing R] [HasSummableGeomSeries R]

namespace Units

/-- In a normed ring with summable geometric series, a perturbation of a unit `x` by an
element `t` of distance less than `â€–xâ»Â¹â€–â»Â¹` from `x` is a unit.
Here we construct its `Units` structure. -/
@[simps! val]
def add (x : RË£) (t : R) (h : â€–tâ€– < â€–(â†‘xâ»Â¹ : R)â€–â»Â¹) : RË£ :=
  Units.copy -- to make `add_val` true definitionally, for convenience
    (x * Units.oneSub (-((xâ»Â¹).1 * t)) (by
      nontriviality R using zero_lt_one
      have hpos : 0 < â€–(â†‘xâ»Â¹ : R)â€– := Units.norm_pos xâ»Â¹
      calc
        â€–-(â†‘xâ»Â¹ * t)â€– = â€–â†‘xâ»Â¹ * tâ€– := by rw [norm_neg]
        _ â‰¤ â€–(â†‘xâ»Â¹ : R)â€– * â€–tâ€– := norm_mul_le (xâ»Â¹).1 _
        _ < â€–(â†‘xâ»Â¹ : R)â€– * â€–(â†‘xâ»Â¹ : R)â€–â»Â¹ := by nlinarith only [h, hpos]
        _ = 1 := mul_inv_cancelâ‚€ (ne_of_gt hpos)))
    (x + t) (by simp [mul_add]) _ rfl

/-- In a normed ring with summable geometric series, an element `y` of distance less
than `â€–xâ»Â¹â€–â»Â¹` from `x` is a unit. Here we construct its `Units` structure. -/
@[simps! val]
def ofNearby (x : RË£) (y : R) (h : â€–y - xâ€– < â€–(â†‘xâ»Â¹ : R)â€–â»Â¹) : RË£ :=
  (x.add (y - x : R) h).copy y (by simp) _ rfl

/-- The group of units of a normed ring with summable geometric series is an open subset
of the ring. -/
protected theorem isOpen : IsOpen { x : R | IsUnit x } := by
  nontriviality R
  rw [Metric.isOpen_iff]
  rintro _ âŸ¨x, rflâŸ©
  refine âŸ¨â€–(â†‘xâ»Â¹ : R)â€–â»Â¹, _root_.inv_pos.mpr (Units.norm_pos xâ»Â¹), fun y hy â†¦ ?_âŸ©
  rw [mem_ball_iff_norm] at hy
  exact (x.ofNearby y hy).isUnit

protected theorem nhds (x : RË£) : { x : R | IsUnit x } âˆˆ ğ“ (x : R) :=
  IsOpen.mem_nhds Units.isOpen x.isUnit

end Units

namespace nonunits

/-- The `nonunits` in a normed ring with summable geometric series are contained in the
complement of the ball of radius `1` centered at `1 : R`. -/
theorem subset_compl_ball : nonunits R âŠ† (Metric.ball (1 : R) 1)á¶œ := fun x hx hâ‚ â†¦ hx <|
  sub_sub_self 1 x â–¸ (Units.oneSub (1 - x) (by rwa [mem_ball_iff_norm'] at hâ‚)).isUnit

-- The `nonunits` in a normed ring with summable geometric series are a closed set
protected theorem isClosed : IsClosed (nonunits R) :=
  Units.isOpen.isClosed_compl

end nonunits

namespace NormedRing

open Asymptotics Filter Metric Finset Ring

theorem inverse_one_sub (t : R) (h : â€–tâ€– < 1) : inverse (1 - t) = â†‘(Units.oneSub t h)â»Â¹ := by
  rw [â† inverse_unit (Units.oneSub t h), Units.val_oneSub]

/-- The formula `Ring.inverse (x + t) = Ring.inverse (1 + xâ»Â¹ * t) * xâ»Â¹` holds for `t` sufficiently
small. -/
theorem inverse_add (x : RË£) :
    âˆ€á¶  t in ğ“ 0, inverse ((x : R) + t) = inverse (1 + â†‘xâ»Â¹ * t) * â†‘xâ»Â¹ := by
  nontriviality R
  rw [Metric.eventually_nhds_iff]
  refine âŸ¨â€–(â†‘xâ»Â¹ : R)â€–â»Â¹, by cancel_denoms, fun t ht â†¦ ?_âŸ©
  rw [dist_zero_right] at ht
  rw [â† x.val_add t ht, inverse_unit, Units.add, Units.copy_eq, mul_inv_rev, Units.val_mul,
    â† inverse_unit, Units.val_oneSub, sub_neg_eq_add]

theorem inverse_one_sub_nth_order' (n : â„•) {t : R} (ht : â€–tâ€– < 1) :
    inverse ((1 : R) - t) = (âˆ‘ i âˆˆ range n, t ^ i) + t ^ n * inverse (1 - t) :=
  have := _root_.summable_geometric_of_norm_lt_one ht
  calc inverse (1 - t) = âˆ‘' i : â„•, t ^ i := inverse_one_sub t ht
    _ = âˆ‘ i âˆˆ range n, t ^ i + âˆ‘' i : â„•, t ^ (i + n) := (this.sum_add_tsum_nat_add _).symm
    _ = (âˆ‘ i âˆˆ range n, t ^ i) + t ^ n * inverse (1 - t) := by
      simp only [inverse_one_sub t ht, add_comm _ n, pow_add, this.tsum_mul_left]; rfl

theorem inverse_one_sub_nth_order (n : â„•) :
    âˆ€á¶  t in ğ“ 0, inverse ((1 : R) - t) = (âˆ‘ i âˆˆ range n, t ^ i) + t ^ n * inverse (1 - t) :=
  Metric.eventually_nhds_iff.2 âŸ¨1, one_pos, fun t ht â†¦ inverse_one_sub_nth_order' n <| by
    rwa [â† dist_zero_right]âŸ©


/-- The formula
`Ring.inverse (x + t) =
  (âˆ‘ i âˆˆ Finset.range n, (- xâ»Â¹ * t) ^ i) * xâ»Â¹ + (- xâ»Â¹ * t) ^ n * Ring.inverse (x + t)`
holds for `t` sufficiently small. -/
theorem inverse_add_nth_order (x : RË£) (n : â„•) :
    âˆ€á¶  t in ğ“ 0, inverse ((x : R) + t) =
      (âˆ‘ i âˆˆ range n, (-â†‘xâ»Â¹ * t) ^ i) * â†‘xâ»Â¹ + (-â†‘xâ»Â¹ * t) ^ n * inverse (x + t) := by
  have hzero : Tendsto (-(â†‘xâ»Â¹ : R) * Â·) (ğ“ 0) (ğ“ 0) :=
    (mulLeft_continuous _).tendsto' _ _ <| mul_zero _
  filter_upwards [inverse_add x, hzero.eventually (inverse_one_sub_nth_order n)] with t ht ht'
  rw [neg_mul, sub_neg_eq_add] at ht'
  conv_lhs => rw [ht, ht', add_mul, â† neg_mul, mul_assoc]
  rw [ht]

theorem inverse_one_sub_norm : (fun t : R => inverse (1 - t)) =O[ğ“ 0] (fun _t => 1 : R â†’ â„) := by
  simp only [IsBigO, IsBigOWith, Metric.eventually_nhds_iff]
  refine âŸ¨â€–(1 : R)â€– + 1, (2 : â„)â»Â¹, by simp, fun t ht â†¦ ?_âŸ©
  rw [dist_zero_right] at ht
  have ht' : â€–tâ€– < 1 := by linarith
  simp only [inverse_one_sub t ht', norm_one, mul_one]
  change â€–âˆ‘' n : â„•, t ^ nâ€– â‰¤ _
  have := tsum_geometric_le_of_norm_lt_one t ht'
  have : (1 - â€–tâ€–)â»Â¹ â‰¤ 2 := inv_le_of_inv_leâ‚€ (by simp) (by linarith)
  linarith

/-- The function `fun t â†¦ inverse (x + t)` is O(1) as `t â†’ 0`. -/
theorem inverse_add_norm (x : RË£) : (fun t : R => inverse (â†‘x + t)) =O[ğ“ 0] fun _t => (1 : â„) := by
  refine EventuallyEq.trans_isBigO (inverse_add x) (one_mul (1 : â„) â–¸ ?_)
  simp only [â† sub_neg_eq_add, â† neg_mul]
  have hzero : Tendsto (-(â†‘xâ»Â¹ : R) * Â·) (ğ“ 0) (ğ“ 0) :=
    (mulLeft_continuous _).tendsto' _ _ <| mul_zero _
  exact (inverse_one_sub_norm.comp_tendsto hzero).mul (isBigO_const_const _ one_ne_zero _)

/-- The function
`fun t â†¦ Ring.inverse (x + t) - (âˆ‘ i âˆˆ Finset.range n, (- xâ»Â¹ * t) ^ i) * xâ»Â¹`
is `O(t ^ n)` as `t â†’ 0`. -/
theorem inverse_add_norm_diff_nth_order (x : RË£) (n : â„•) :
    (fun t : R => inverse (â†‘x + t) - (âˆ‘ i âˆˆ range n, (-â†‘xâ»Â¹ * t) ^ i) * â†‘xâ»Â¹) =O[ğ“ (0 : R)]
      fun t => â€–tâ€– ^ n := by
  refine EventuallyEq.trans_isBigO (.fun_sub (inverse_add_nth_order x n) (.refl _ _)) ?_
  simp only [add_sub_cancel_left]
  refine ((isBigO_refl _ _).norm_right.mul (inverse_add_norm x)).trans ?_
  simp only [mul_one, isBigO_norm_left]
  exact ((isBigO_refl _ _).norm_right.const_mul_left _).pow _

/-- The function `fun t â†¦ Ring.inverse (x + t) - xâ»Â¹` is `O(t)` as `t â†’ 0`. -/
theorem inverse_add_norm_diff_first_order (x : RË£) :
    (fun t : R => inverse (â†‘x + t) - â†‘xâ»Â¹) =O[ğ“ 0] fun t => â€–tâ€– := by
  simpa using inverse_add_norm_diff_nth_order x 1

/-- The function `fun t â†¦ Ring.inverse (x + t) - xâ»Â¹ + xâ»Â¹ * t * xâ»Â¹` is `O(t ^ 2)` as `t â†’ 0`. -/
theorem inverse_add_norm_diff_second_order (x : RË£) :
    (fun t : R => inverse (â†‘x + t) - â†‘xâ»Â¹ + â†‘xâ»Â¹ * t * â†‘xâ»Â¹) =O[ğ“ 0] fun t => â€–tâ€– ^ 2 := by
  convert inverse_add_norm_diff_nth_order x 2 using 2
  simp only [sum_range_succ, sum_range_zero, zero_add, pow_zero, pow_one, add_mul, one_mul,
    â† sub_sub, neg_mul, sub_neg_eq_add]

/-- The function `Ring.inverse` is continuous at each unit of `R`. -/
theorem inverse_continuousAt (x : RË£) : ContinuousAt inverse (x : R) := by
  have h_is_o : (fun t : R => inverse (â†‘x + t) - â†‘xâ»Â¹) =o[ğ“ 0] (fun _ => 1 : R â†’ â„) :=
    (inverse_add_norm_diff_first_order x).trans_isLittleO (isLittleO_id_const one_ne_zero).norm_left
  have h_lim : Tendsto (fun y : R => y - x) (ğ“ x) (ğ“ 0) := by
    refine tendsto_zero_iff_norm_tendsto_zero.mpr ?_
    exact tendsto_iff_norm_sub_tendsto_zero.mp tendsto_id
  rw [ContinuousAt, tendsto_iff_norm_sub_tendsto_zero, inverse_unit]
  simpa [Function.comp_def] using h_is_o.norm_left.tendsto_div_nhds_zero.comp h_lim

end NormedRing

namespace Units

open MulOpposite Filter NormedRing

/-- In a normed ring with summable geometric series, the coercion from `RË£` (equipped with the
induced topology from the embedding in `R Ã— R`) to `R` is an open embedding. -/
theorem isOpenEmbedding_val : IsOpenEmbedding (val : RË£ â†’ R) where
  toIsEmbedding := isEmbedding_val_mk'
    (fun _ âŸ¨u, huâŸ© â†¦ hu â–¸ (inverse_continuousAt u).continuousWithinAt) Ring.inverse_unit
  isOpen_range := Units.isOpen

/-- In a normed ring with summable geometric series, the coercion from `RË£` (equipped with the
induced topology from the embedding in `R Ã— R`) to `R` is an open map. -/
theorem isOpenMap_val : IsOpenMap (val : RË£ â†’ R) :=
  isOpenEmbedding_val.isOpenMap

end Units

namespace Ideal

/-- An ideal which contains an element within `1` of `1 : R` is the unit ideal. -/
theorem eq_top_of_norm_lt_one (I : Ideal R) {x : R} (hxI : x âˆˆ I) (hx : â€–1 - xâ€– < 1) : I = âŠ¤ :=
  let u := Units.oneSub (1 - x) hx
  I.eq_top_iff_one.mpr <| by
    simpa only [show u.inv * x = 1 by simp [u]] using I.mul_mem_left u.inv hxI

/-- The `Ideal.closure` of a proper ideal in a normed ring with summable
geometric series is proper. -/
theorem closure_ne_top (I : Ideal R) (hI : I â‰  âŠ¤) : I.closure â‰  âŠ¤ := by
  have h := closure_minimal (coe_subset_nonunits hI) nonunits.isClosed
  simpa only [I.closure.eq_top_iff_one, Ne] using mt (@h 1) one_notMem_nonunits

/-- The `Ideal.closure` of a maximal ideal in a normed ring with summable
geometric series is the ideal itself. -/
theorem IsMaximal.closure_eq {I : Ideal R} (hI : I.IsMaximal) : I.closure = I :=
  (hI.eq_of_le (I.closure_ne_top hI.ne_top) subset_closure).symm

/-- Maximal ideals in normed rings with summable geometric series are closed. -/
instance IsMaximal.isClosed {I : Ideal R} [hI : I.IsMaximal] : IsClosed (I : Set R) :=
  isClosed_of_closure_subset <| Eq.subset <| congr_arg ((â†‘) : Ideal R â†’ Set R) hI.closure_eq

end Ideal
