/-
Copyright (c) 2021 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.Normed.Group.InfiniteSum
import Mathlib.Topology.Algebra.InfiniteSum.Real
import Mathlib.Analysis.Normed.Ring.Lemmas

/-! # Multiplying two infinite sums in a normed ring

In this file, we prove various results about `(‚àë' x : Œπ, f x) * (‚àë' y : Œπ', g y)` in a normed
ring. There are similar results proven in `Mathlib/Topology/Algebra/InfiniteSum.lean` (e.g
`tsum_mul_tsum`), but in a normed ring we get summability results which aren't true in general.

We first establish results about arbitrary index types, `Œπ` and `Œπ'`, and then we specialize to
`Œπ = Œπ' = ‚Ñï` to prove the Cauchy product formula
(see `tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm`).
-/


variable {R : Type*} {Œπ : Type*} {Œπ' : Type*} [NormedRing R]

open scoped Topology

open Finset Filter

/-! ### Arbitrary index types -/

theorem Summable.mul_of_nonneg {f : Œπ ‚Üí ‚Ñù} {g : Œπ' ‚Üí ‚Ñù} (hf : Summable f) (hg : Summable g)
    (hf' : 0 ‚â§ f) (hg' : 0 ‚â§ g) : Summable fun x : Œπ √ó Œπ' ‚Ü¶ f x.1 * g x.2 :=
  (summable_prod_of_nonneg fun _ ‚Ü¶ mul_nonneg (hf' _) (hg' _)).2 ‚ü®fun x ‚Ü¶ hg.mul_left (f x),
    by simpa only [hg.tsum_mul_left _] using hf.mul_right (‚àë' x, g x)‚ü©

theorem Summable.mul_norm {f : Œπ ‚Üí R} {g : Œπ' ‚Üí R} (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) : Summable fun x : Œπ √ó Œπ' ‚Ü¶ ‚Äñf x.1 * g x.2‚Äñ :=
  .of_nonneg_of_le (fun _ ‚Ü¶ norm_nonneg _)
    (fun x ‚Ü¶ norm_mul_le (f x.1) (g x.2))
    (hf.mul_of_nonneg hg (fun x ‚Ü¶ norm_nonneg <| f x) fun x ‚Ü¶ norm_nonneg <| g x :)

theorem summable_mul_of_summable_norm [CompleteSpace R] {f : Œπ ‚Üí R} {g : Œπ' ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    Summable fun x : Œπ √ó Œπ' ‚Ü¶ f x.1 * g x.2 :=
  (hf.mul_norm hg).of_norm

theorem summable_mul_of_summable_norm' {f : Œπ ‚Üí R} {g : Œπ' ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    Summable fun x : Œπ √ó Œπ' ‚Ü¶ f x.1 * g x.2 := by
  classical
  suffices HasSum (fun x : Œπ √ó Œπ' ‚Ü¶ f x.1 * g x.2) ((‚àë' i, f i) * (‚àë' j, g j)) from this.summable
  let s : Finset Œπ √ó Finset Œπ' ‚Üí Finset (Œπ √ó Œπ') := fun p ‚Ü¶ p.1 √óÀ¢ p.2
  apply hasSum_of_subseq_of_summable (hf.mul_norm hg) tendsto_finset_prod_atTop
  rw [‚Üê prod_atTop_atTop_eq]
  have := Tendsto.prodMap h'f.hasSum h'g.hasSum
  rw [‚Üê nhds_prod_eq] at this
  convert ((continuous_mul (M := R)).continuousAt
      (x := (‚àë' (i : Œπ), f i, ‚àë' (j : Œπ'), g j))).tendsto.comp this with p
  simp [sum_product, ‚Üê mul_sum, ‚Üê sum_mul]

/-- Product of two infinite sums indexed by arbitrary types.
See also `tsum_mul_tsum` if `f` and `g` are *not* absolutely summable, and
`tsum_mul_tsum_of_summable_norm'` when the space is not complete. -/
theorem tsum_mul_tsum_of_summable_norm [CompleteSpace R] {f : Œπ ‚Üí R} {g : Œπ' ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    ((‚àë' x, f x) * ‚àë' y, g y) = ‚àë' z : Œπ √ó Œπ', f z.1 * g z.2 :=
  hf.of_norm.tsum_mul_tsum hg.of_norm (summable_mul_of_summable_norm hf hg)

theorem tsum_mul_tsum_of_summable_norm' {f : Œπ ‚Üí R} {g : Œπ' ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    ((‚àë' x, f x) * ‚àë' y, g y) = ‚àë' z : Œπ √ó Œπ', f z.1 * g z.2 :=
  h'f.tsum_mul_tsum h'g (summable_mul_of_summable_norm' hf h'f hg h'g)

/-! ### `‚Ñï`-indexed families (Cauchy product)

We prove two versions of the Cauchy product formula. The first one is
`tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm`, where the `n`-th term is a sum over
`Finset.range (n+1)` involving `Nat` subtraction.
In order to avoid `Nat` subtraction, we also provide
`tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm`,
where the `n`-th term is a sum over all pairs `(k, l)` such that `k+l=n`, which corresponds to the
`Finset` `Finset.antidiagonal n`. -/

section Nat

open Finset.Nat

theorem summable_norm_sum_mul_antidiagonal_of_summable_norm {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    Summable fun n ‚Ü¶ ‚Äñ‚àë kl ‚àà antidiagonal n, f kl.1 * g kl.2‚Äñ := by
  have :=
    summable_sum_mul_antidiagonal_of_summable_mul
      (Summable.mul_of_nonneg hf hg (fun _ ‚Ü¶ norm_nonneg _) fun _ ‚Ü¶ norm_nonneg _)
  refine this.of_nonneg_of_le (fun _ ‚Ü¶ norm_nonneg _) (fun n ‚Ü¶ ?_)
  calc
    ‚Äñ‚àë kl ‚àà antidiagonal n, f kl.1 * g kl.2‚Äñ ‚â§ ‚àë kl ‚àà antidiagonal n, ‚Äñf kl.1 * g kl.2‚Äñ :=
      norm_sum_le _ _
    _ ‚â§ ‚àë kl ‚àà antidiagonal n, ‚Äñf kl.1‚Äñ * ‚Äñg kl.2‚Äñ := by gcongr; apply norm_mul_le

theorem summable_sum_mul_antidiagonal_of_summable_norm' {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    Summable fun n ‚Ü¶ ‚àë kl ‚àà antidiagonal n, f kl.1 * g kl.2 :=
  summable_sum_mul_antidiagonal_of_summable_mul (summable_mul_of_summable_norm' hf h'f hg h'g)

/-- The Cauchy product formula for the product of two infinite sums indexed by `‚Ñï`,
expressed by summing on `Finset.antidiagonal`.
See also `tsum_mul_tsum_eq_tsum_sum_antidiagonal` if `f` and `g` are
*not* absolutely summable, and `tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm'`
when the space is not complete. -/
theorem tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm [CompleteSpace R] {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    ((‚àë' n, f n) * ‚àë' n, g n) = ‚àë' n, ‚àë kl ‚àà antidiagonal n, f kl.1 * g kl.2 :=
  hf.of_norm.tsum_mul_tsum_eq_tsum_sum_antidiagonal hg.of_norm (summable_mul_of_summable_norm hf hg)

theorem tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm' {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    ((‚àë' n, f n) * ‚àë' n, g n) = ‚àë' n, ‚àë kl ‚àà antidiagonal n, f kl.1 * g kl.2 :=
  h'f.tsum_mul_tsum_eq_tsum_sum_antidiagonal  h'g (summable_mul_of_summable_norm' hf h'f hg h'g)

theorem summable_norm_sum_mul_range_of_summable_norm {f g : ‚Ñï ‚Üí R} (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) : Summable fun n ‚Ü¶ ‚Äñ‚àë k ‚àà range (n + 1), f k * g (n - k)‚Äñ := by
  simp_rw [‚Üê sum_antidiagonal_eq_sum_range_succ fun k l ‚Ü¶ f k * g l]
  exact summable_norm_sum_mul_antidiagonal_of_summable_norm hf hg

theorem summable_sum_mul_range_of_summable_norm' {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    Summable fun n ‚Ü¶ ‚àë k ‚àà range (n + 1), f k * g (n - k) := by
  simp_rw [‚Üê sum_antidiagonal_eq_sum_range_succ fun k l ‚Ü¶ f k * g l]
  exact summable_sum_mul_antidiagonal_of_summable_norm' hf h'f hg h'g

/-- The Cauchy product formula for the product of two infinite sums indexed by `‚Ñï`,
expressed by summing on `Finset.range`.
See also `tsum_mul_tsum_eq_tsum_sum_range` if `f` and `g` are
not* absolutely summable, and `tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm'` when the
space is not complete. -/
theorem tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm [CompleteSpace R] {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    ((‚àë' n, f n) * ‚àë' n, g n) = ‚àë' n, ‚àë k ‚àà range (n + 1), f k * g (n - k) := by
  simp_rw [‚Üê sum_antidiagonal_eq_sum_range_succ fun k l ‚Ü¶ f k * g l]
  exact tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm hf hg

theorem hasSum_sum_range_mul_of_summable_norm [CompleteSpace R] {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) :
    HasSum (fun n ‚Ü¶ ‚àë k ‚àà range (n + 1), f k * g (n - k)) ((‚àë' n, f n) * ‚àë' n, g n) := by
  convert (summable_norm_sum_mul_range_of_summable_norm hf hg).of_norm.hasSum
  exact tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm hf hg

theorem tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm' {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    ((‚àë' n, f n) * ‚àë' n, g n) = ‚àë' n, ‚àë k ‚àà range (n + 1), f k * g (n - k) := by
  simp_rw [‚Üê sum_antidiagonal_eq_sum_range_succ fun k l ‚Ü¶ f k * g l]
  exact tsum_mul_tsum_eq_tsum_sum_antidiagonal_of_summable_norm' hf h'f hg h'g

theorem hasSum_sum_range_mul_of_summable_norm' {f g : ‚Ñï ‚Üí R}
    (hf : Summable fun x ‚Ü¶ ‚Äñf x‚Äñ) (h'f : Summable f)
    (hg : Summable fun x ‚Ü¶ ‚Äñg x‚Äñ) (h'g : Summable g) :
    HasSum (fun n ‚Ü¶ ‚àë k ‚àà range (n + 1), f k * g (n - k)) ((‚àë' n, f n) * ‚àë' n, g n) := by
  convert (summable_sum_mul_range_of_summable_norm' hf h'f hg h'g).hasSum
  exact tsum_mul_tsum_eq_tsum_sum_range_of_summable_norm' hf h'f hg h'g

end Nat

lemma summable_of_absolute_convergence_real {f : ‚Ñï ‚Üí ‚Ñù} :
    (‚àÉ r, Tendsto (fun n ‚Ü¶ ‚àë i ‚àà range n, |f i|) atTop (ùìù r)) ‚Üí Summable f
  | ‚ü®r, hr‚ü© => by
    refine .of_norm ‚ü®r, (hasSum_iff_tendsto_nat_of_nonneg ?_ _).2 ?_‚ü©
    ¬∑ exact fun i ‚Ü¶ norm_nonneg _
    ¬∑ simpa only using hr
