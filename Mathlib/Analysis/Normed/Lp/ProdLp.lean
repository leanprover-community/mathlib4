/-
Copyright (c) 2023 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, S√©bastien Gou√´zel, Jireh Loreaux
-/
import Mathlib.Analysis.MeanInequalities
import Mathlib.Analysis.Normed.Lp.WithLp

/-!
# `L^p` distance on products of two metric spaces
Given two metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a parameter `p : ‚Ñù‚â•0‚àû`, that also induce
the product topology. We define them in this file. For `0 < p < ‚àû`, the distance on `Œ± √ó Œ≤`
is given by
$$
d(x, y) = \left(d(x_1, y_1)^p + d(x_2, y_2)^p\right)^{1/p}.
$$
For `p = ‚àû` the distance is the supremum of the distances and `p = 0` the distance is the
cardinality of the elements that are not equal.

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Prod-type, named
`WithLp p (Œ± √ó Œ≤)`. The assumption `[Fact (1 ‚â§ p)]` is required for the metric and normed space
instances.

We ensure that the topology, bornology and uniform structure on `WithLp p (Œ± √ó Œ≤)` are (defeq to)
the product topology, product bornology and product uniformity, to be able to use freely continuity
statements for the coordinate functions, for instance.

# Implementation notes

This file is a straight-forward adaptation of `Mathlib/Analysis/Normed/Lp/PiLp.lean`.

-/

open Real Set Filter RCLike Bornology Uniformity Topology NNReal ENNReal

noncomputable section

variable (p : ‚Ñù‚â•0‚àû) (ùïú Œ± Œ≤ : Type*)

namespace WithLp

section algebra

/- Register simplification lemmas for the applications of `WithLp p (Œ± √ó Œ≤)` elements, as the usual
lemmas for `Prod` will not trigger. -/

variable {p ùïú Œ± Œ≤}
variable [Semiring ùïú] [AddCommGroup Œ±] [AddCommGroup Œ≤]
variable (x y : WithLp p (Œ± √ó Œ≤)) (c : ùïú)

@[simp]
theorem zero_fst : (0 : WithLp p (Œ± √ó Œ≤)).fst = 0 :=
  rfl

@[simp]
theorem zero_snd : (0 : WithLp p (Œ± √ó Œ≤)).snd = 0 :=
  rfl

@[simp]
theorem add_fst : (x + y).fst = x.fst + y.fst :=
  rfl

@[simp]
theorem add_snd : (x + y).snd = x.snd + y.snd :=
  rfl

@[simp]
theorem sub_fst : (x - y).fst = x.fst - y.fst :=
  rfl

@[simp]
theorem sub_snd : (x - y).snd = x.snd - y.snd :=
  rfl

@[simp]
theorem neg_fst : (-x).fst = -x.fst :=
  rfl

@[simp]
theorem neg_snd : (-x).snd = -x.snd :=
  rfl

variable [Module ùïú Œ±] [Module ùïú Œ≤]

@[simp]
theorem smul_fst : (c ‚Ä¢ x).fst = c ‚Ä¢ x.fst :=
  rfl

@[simp]
theorem smul_snd : (c ‚Ä¢ x).snd = c ‚Ä¢ x.snd :=
  rfl

end algebra

/-! Note that the unapplied versions of these lemmas are deliberately omitted, as they break
the use of the type synonym. -/

section equiv

variable {p Œ± Œ≤}

@[simp] lemma toLp_fst (x : Œ± √ó Œ≤) : (toLp p x).fst = x.fst := rfl
@[simp] lemma toLp_snd (x : Œ± √ó Œ≤) : (toLp p x).snd = x.snd := rfl
@[simp] lemma ofLp_fst (x : WithLp p (Œ± √ó Œ≤)) : (ofLp x).fst = x.fst := rfl
@[simp] lemma ofLp_snd (x : WithLp p (Œ± √ó Œ≤)) : (ofLp x).snd = x.snd := rfl

end equiv

section DistNorm

/-!
### Definition of `edist`, `dist` and `norm` on `WithLp p (Œ± √ó Œ≤)`

In this section we define the `edist`, `dist` and `norm` functions on `WithLp p (Œ± √ó Œ≤)` without
assuming `[Fact (1 ‚â§ p)]` or metric properties of the spaces `Œ±` and `Œ≤`. This allows us to provide
the rewrite lemmas for each of three cases `p = 0`, `p = ‚àû` and `0 < p.toReal`.
-/


section EDist

variable [EDist Œ±] [EDist Œ≤]

open scoped Classical in
/-- Endowing the space `WithLp p (Œ± √ó Œ≤)` with the `L^p` edistance. We register this instance
separate from `WithLp.instProdPseudoEMetric` since the latter requires the type class hypothesis
`[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future emetric-like structure on `WithLp p (Œ± √ó Œ≤)` for
`p < 1` satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance instProdEDist : EDist (WithLp p (Œ± √ó Œ≤)) where
  edist f g :=
    if _hp : p = 0 then
      (if edist f.fst g.fst = 0 then 0 else 1) + (if edist f.snd g.snd = 0 then 0 else 1)
    else if p = ‚àû then
      edist f.fst g.fst ‚äî edist f.snd g.snd
    else
      (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal)

variable {p Œ± Œ≤}

@[simp]
theorem prod_edist_eq_card (f g : WithLp 0 (Œ± √ó Œ≤)) :
    edist f g =
      (if edist f.fst g.fst = 0 then 0 else 1) + (if edist f.snd g.snd = 0 then 0 else 1) := by
  convert if_pos rfl

theorem prod_edist_eq_add (hp : 0 < p.toReal) (f g : WithLp p (Œ± √ó Œ≤)) :
    edist f g = (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem prod_edist_eq_sup (f g : WithLp ‚àû (Œ± √ó Œ≤)) :
    edist f g = edist f.fst g.fst ‚äî edist f.snd g.snd := rfl

end EDist

section EDistProp

variable {Œ± Œ≤}
variable [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]

/-- The distance from one point to itself is always zero.

This holds independent of `p` and does not require `[Fact (1 ‚â§ p)]`. We keep it separate
from `WithLp.instProdPseudoEMetricSpace` so it can be used also for `p < 1`. -/
theorem prod_edist_self (f : WithLp p (Œ± √ó Œ≤)) : edist f f = 0 := by
  rcases p.trichotomy with (rfl | rfl | h)
  ¬∑ classical
    simp
  ¬∑ simp [prod_edist_eq_sup]
  ¬∑ simp [prod_edist_eq_add h, ENNReal.zero_rpow_of_pos h,
      ENNReal.zero_rpow_of_pos (inv_pos.2 <| h)]

/-- The distance is symmetric.

This holds independent of `p` and does not require `[Fact (1 ‚â§ p)]`. We keep it separate
from `WithLp.instProdPseudoEMetricSpace` so it can be used also for `p < 1`. -/
theorem prod_edist_comm (f g : WithLp p (Œ± √ó Œ≤)) : edist f g = edist g f := by
  classical
  rcases p.trichotomy with (rfl | rfl | h)
  ¬∑ simp only [prod_edist_eq_card, edist_comm]
  ¬∑ simp only [prod_edist_eq_sup, edist_comm]
  ¬∑ simp only [prod_edist_eq_add h, edist_comm]

end EDistProp

section Dist

variable [Dist Œ±] [Dist Œ≤]

open scoped Classical in
/-- Endowing the space `WithLp p (Œ± √ó Œ≤)` with the `L^p` distance. We register this instance
separate from `WithLp.instProdPseudoMetricSpace` since the latter requires the type class hypothesis
`[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future metric-like structure on `WithLp p (Œ± √ó Œ≤)` for
`p < 1` satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance instProdDist : Dist (WithLp p (Œ± √ó Œ≤)) where
  dist f g :=
    if _hp : p = 0 then
      (if dist f.fst g.fst = 0 then 0 else 1) + (if dist f.snd g.snd = 0 then 0 else 1)
    else if p = ‚àû then
      dist f.fst g.fst ‚äî dist f.snd g.snd
    else
      (dist f.fst g.fst ^ p.toReal + dist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal)

variable {p Œ± Œ≤}

theorem prod_dist_eq_card (f g : WithLp 0 (Œ± √ó Œ≤)) : dist f g =
    (if dist f.fst g.fst = 0 then 0 else 1) + (if dist f.snd g.snd = 0 then 0 else 1) := by
  convert if_pos rfl

theorem prod_dist_eq_add (hp : 0 < p.toReal) (f g : WithLp p (Œ± √ó Œ≤)) :
    dist f g = (dist f.fst g.fst ^ p.toReal + dist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem prod_dist_eq_sup (f g : WithLp ‚àû (Œ± √ó Œ≤)) :
    dist f g = dist f.fst g.fst ‚äî dist f.snd g.snd := rfl

end Dist

section Norm

variable [Norm Œ±] [Norm Œ≤]

open scoped Classical in
/-- Endowing the space `WithLp p (Œ± √ó Œ≤)` with the `L^p` norm. We register this instance
separate from `WithLp.instProdSeminormedAddCommGroup` since the latter requires the type class
hypothesis `[Fact (1 ‚â§ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future norm-like structure on `WithLp p (Œ± √ó Œ≤)` for
`p < 1` satisfying a relaxed triangle inequality. These are called *quasi-norms*. -/
instance instProdNorm : Norm (WithLp p (Œ± √ó Œ≤)) where
  norm f :=
    if _hp : p = 0 then
      (if ‚Äñf.fst‚Äñ = 0 then 0 else 1) + (if ‚Äñf.snd‚Äñ = 0 then 0 else 1)
    else if p = ‚àû then
      ‚Äñf.fst‚Äñ ‚äî ‚Äñf.snd‚Äñ
    else
      (‚Äñf.fst‚Äñ ^ p.toReal + ‚Äñf.snd‚Äñ ^ p.toReal) ^ (1 / p.toReal)

variable {p Œ± Œ≤}

@[simp]
theorem prod_norm_eq_card (f : WithLp 0 (Œ± √ó Œ≤)) :
    ‚Äñf‚Äñ = (if ‚Äñf.fst‚Äñ = 0 then 0 else 1) + (if ‚Äñf.snd‚Äñ = 0 then 0 else 1) := by
  convert if_pos rfl

theorem prod_norm_eq_sup (f : WithLp ‚àû (Œ± √ó Œ≤)) : ‚Äñf‚Äñ = ‚Äñf.fst‚Äñ ‚äî ‚Äñf.snd‚Äñ := rfl

theorem prod_norm_eq_add (hp : 0 < p.toReal) (f : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñf‚Äñ = (‚Äñf.fst‚Äñ ^ p.toReal + ‚Äñf.snd‚Äñ ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

end Norm

end DistNorm

section Aux

/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `WithLp p (Œ± √ó Œ≤)`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Prod type (with the `L^‚àû` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/


variable [hp : Fact (1 ‚â§ p)]

/-- Endowing the space `WithLp p (Œ± √ó Œ≤)` with the `L^p` pseudoemetric structure. This definition is
not satisfactory, as it does not register the fact that the topology and the uniform structure
coincide with the product one. Therefore, we do not register it as an instance. Using this as a
temporary pseudoemetric space instance, we will show that the uniform structure is equal (but not
defeq) to the product one, and then register an instance in which we replace the uniform structure
by the product one using this pseudoemetric space and `PseudoEMetricSpace.replaceUniformity`. -/
def prodPseudoEMetricAux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    PseudoEMetricSpace (WithLp p (Œ± √ó Œ≤)) where
  edist_self := prod_edist_self p
  edist_comm := prod_edist_comm p
  edist_triangle f g h := by
    rcases p.dichotomy with (rfl | hp)
    ¬∑ simp only [prod_edist_eq_sup]
      exact sup_le ((edist_triangle _ g.fst _).trans <| add_le_add le_sup_left le_sup_left)
        ((edist_triangle _ g.snd _).trans <| add_le_add le_sup_right le_sup_right)
    ¬∑ simp only [prod_edist_eq_add (zero_lt_one.trans_le hp)]
      calc
        (edist f.fst h.fst ^ p.toReal + edist f.snd h.snd ^ p.toReal) ^ (1 / p.toReal) ‚â§
            ((edist f.fst g.fst + edist g.fst h.fst) ^ p.toReal +
              (edist f.snd g.snd + edist g.snd h.snd) ^ p.toReal) ^ (1 / p.toReal) := by
          gcongr <;> apply edist_triangle
        _ ‚â§
            (edist f.fst g.fst ^ p.toReal + edist f.snd g.snd ^ p.toReal) ^ (1 / p.toReal) +
              (edist g.fst h.fst ^ p.toReal + edist g.snd h.snd ^ p.toReal) ^ (1 / p.toReal) := by
          have := ENNReal.Lp_add_le {0, 1}
            (if ¬∑ = 0 then edist f.fst g.fst else edist f.snd g.snd)
            (if ¬∑ = 0 then edist g.fst h.fst else edist g.snd h.snd) hp
          simp only [Finset.mem_singleton, not_false_eq_true, Finset.sum_insert,
            Finset.sum_singleton, reduceCtorEq] at this
          exact this

attribute [local instance] WithLp.prodPseudoEMetricAux

variable {Œ± Œ≤}

/-- An auxiliary lemma used twice in the proof of `WithLp.prodPseudoMetricAux` below. Not intended
for use outside this file. -/
theorem prod_sup_edist_ne_top_aux [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    (f g : WithLp ‚àû (Œ± √ó Œ≤)) :
    edist f.fst g.fst ‚äî edist f.snd g.snd ‚â† ‚ä§ :=
  ne_of_lt <| by simp [edist, PseudoMetricSpace.edist_dist]

variable (Œ± Œ≤)

/-- Endowing the space `WithLp p (Œ± √ó Œ≤)` with the `L^p` pseudometric structure. This definition is
not satisfactory, as it does not register the fact that the topology, the uniform structure, and the
bornology coincide with the product ones. Therefore, we do not register it as an instance. Using
this as a temporary pseudoemetric space instance, we will show that the uniform structure is equal
(but not defeq) to the product one, and then register an instance in which we replace the uniform
structure and the bornology by the product ones using this pseudometric space,
`PseudoMetricSpace.replaceUniformity`, and `PseudoMetricSpace.replaceBornology`.

See note [reducible non-instances] -/
abbrev prodPseudoMetricAux [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    PseudoMetricSpace (WithLp p (Œ± √ó Œ≤)) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist dist
    (fun f g => by
      rcases p.dichotomy with (rfl | h)
      ¬∑ exact prod_sup_edist_ne_top_aux f g
      ¬∑ rw [prod_edist_eq_add (zero_lt_one.trans_le h)]
        finiteness)
    fun f g => by
    rcases p.dichotomy with (rfl | h)
    ¬∑ rw [prod_edist_eq_sup, prod_dist_eq_sup]
      refine le_antisymm (sup_le ?_ ?_) ?_
      ¬∑ rw [‚Üê ENNReal.ofReal_le_iff_le_toReal (prod_sup_edist_ne_top_aux f g),
          ‚Üê PseudoMetricSpace.edist_dist]
        exact le_sup_left
      ¬∑ rw [‚Üê ENNReal.ofReal_le_iff_le_toReal (prod_sup_edist_ne_top_aux f g),
          ‚Üê PseudoMetricSpace.edist_dist]
        exact le_sup_right
      ¬∑ refine ENNReal.toReal_le_of_le_ofReal ?_ ?_
        ¬∑ simp only [le_sup_iff, dist_nonneg, or_self]
        ¬∑ simp [-ofReal_max]
    ¬∑ have h1 : edist f.fst g.fst ^ p.toReal ‚â† ‚ä§ := by finiteness
      have h2 : edist f.snd g.snd ^ p.toReal ‚â† ‚ä§ := by finiteness
      simp only [prod_edist_eq_add (zero_lt_one.trans_le h), dist_edist, ENNReal.toReal_rpow,
        prod_dist_eq_add (zero_lt_one.trans_le h), ‚Üê ENNReal.toReal_add h1 h2]

attribute [local instance] WithLp.prodPseudoMetricAux

variable {Œ± Œ≤} in
private theorem edist_proj_le_edist_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤]
    (x y : WithLp p (Œ± √ó Œ≤)) :
    edist x.fst y.fst ‚â§ edist x y ‚àß edist x.snd y.snd ‚â§ edist x y := by
  rcases p.dichotomy with (rfl | h)
  ¬∑ simp [prod_edist_eq_sup]
  ¬∑ have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancel‚ÇÄ 1 (zero_lt_one.trans_le h).ne'
    rw [prod_edist_eq_add (zero_lt_one.trans_le h)]
    constructor
    ¬∑ calc
        edist x.fst y.fst ‚â§ (edist x.fst y.fst ^ p.toReal) ^ (1 / p.toReal) := by
          simp only [‚Üê ENNReal.rpow_mul, cancel, ENNReal.rpow_one, le_refl]
        _ ‚â§ (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          gcongr
          simp only [self_le_add_right]
    ¬∑ calc
        edist x.snd y.snd ‚â§ (edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          simp only [‚Üê ENNReal.rpow_mul, cancel, ENNReal.rpow_one, le_refl]
        _ ‚â§ (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) := by
          gcongr
          simp only [self_le_add_left]

private lemma prod_lipschitzWith_ofLp_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    LipschitzWith 1 (@ofLp p (Œ± √ó Œ≤)) := by
  intro x y
  change max _ _ ‚â§ _
  rw [ENNReal.coe_one, one_mul, sup_le_iff]
  exact edist_proj_le_edist_aux p x y

private lemma prod_antilipschitzWith_ofLp_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    AntilipschitzWith ((2 : ‚Ñù‚â•0) ^ (1 / p).toReal) (@ofLp p (Œ± √ó Œ≤)) := by
  intro x y
  rcases p.dichotomy with (rfl | h)
  ¬∑ simp [edist]
  ¬∑ have pos : 0 < p.toReal := by positivity
    have nonneg : 0 ‚â§ 1 / p.toReal := by positivity
    have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancel‚ÇÄ 1 (ne_of_gt pos)
    rw [prod_edist_eq_add pos, ENNReal.toReal_div 1 p]
    simp only [edist, ENNReal.toReal_one]
    calc
      (edist x.fst y.fst ^ p.toReal + edist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) ‚â§
          (edist (ofLp x) (ofLp y) ^ p.toReal +
          edist (ofLp x) (ofLp y) ^ p.toReal) ^ (1 / p.toReal) := by
        gcongr <;> simp [edist]
      _ = (2 ^ (1 / p.toReal) : ‚Ñù‚â•0) * edist (ofLp x) (ofLp y) := by
        simp only [‚Üê two_mul, ENNReal.mul_rpow_of_nonneg _ _ nonneg, ‚Üê ENNReal.rpow_mul, cancel,
          ENNReal.rpow_one, ENNReal.coe_rpow_of_nonneg _ nonneg, coe_ofNat]

private lemma isUniformInducing_ofLp_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    IsUniformInducing (@ofLp p (Œ± √ó Œ≤)) :=
  (prod_antilipschitzWith_ofLp_aux p Œ± Œ≤).isUniformInducing
    (prod_lipschitzWith_ofLp_aux p Œ± Œ≤).uniformContinuous

private lemma prod_uniformity_aux [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    ùì§ (WithLp p (Œ± √ó Œ≤)) = ùì§[instUniformSpaceProd] := by
  have : (fun x : WithLp p (Œ± √ó Œ≤) √ó WithLp p (Œ± √ó Œ≤) =>
    (ofLp x.fst, ofLp x.snd)) = id := rfl
  rw [‚Üê (isUniformInducing_ofLp_aux p Œ± Œ≤).comap_uniformity, this, comap_id]

private lemma prod_cobounded_aux [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    cobounded (WithLp p (Œ± √ó Œ≤)) = @cobounded _ Prod.instBornology :=
  calc
    cobounded (WithLp p (Œ± √ó Œ≤)) = comap (@ofLp p (Œ± √ó Œ≤)) (cobounded _) :=
      le_antisymm (prod_antilipschitzWith_ofLp_aux p Œ± Œ≤).tendsto_cobounded.le_comap
        (prod_lipschitzWith_ofLp_aux p Œ± Œ≤).comap_cobounded_le
    _ = _ := comap_id

end Aux

/-! ### Instances on `L^p` products -/

section TopologicalSpace

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤]

instance instProdTopologicalSpace : TopologicalSpace (WithLp p (Œ± √ó Œ≤)) :=
  instTopologicalSpaceProd

@[continuity, fun_prop]
lemma prod_continuous_toLp : Continuous (@toLp p (Œ± √ó Œ≤)) := continuous_id

@[continuity, fun_prop]
lemma prod_continuous_ofLp : Continuous (@ofLp p (Œ± √ó Œ≤)) := continuous_id

variable [T0Space Œ±] [T0Space Œ≤]

instance instProdT0Space : T0Space (WithLp p (Œ± √ó Œ≤)) :=
  Prod.instT0Space

variable [SecondCountableTopology Œ±] [SecondCountableTopology Œ≤]

instance secondCountableTopology : SecondCountableTopology (WithLp p (Œ± √ó Œ≤)) :=
  inferInstanceAs <| SecondCountableTopology (Œ± √ó Œ≤)

end TopologicalSpace

section UniformSpace

variable [UniformSpace Œ±] [UniformSpace Œ≤]

instance instProdUniformSpace : UniformSpace (WithLp p (Œ± √ó Œ≤)) :=
  instUniformSpaceProd

lemma prod_uniformContinuous_toLp : UniformContinuous (@toLp p (Œ± √ó Œ≤)) :=
  uniformContinuous_id

lemma prod_uniformContinuous_ofLp : UniformContinuous (@ofLp p (Œ± √ó Œ≤)) :=
  uniformContinuous_id

variable [CompleteSpace Œ±] [CompleteSpace Œ≤]

instance instProdCompleteSpace : CompleteSpace (WithLp p (Œ± √ó Œ≤)) :=
  CompleteSpace.prod

end UniformSpace

instance instProdBornology [Bornology Œ±] [Bornology Œ≤] : Bornology (WithLp p (Œ± √ó Œ≤)) :=
  Prod.instBornology

section ContinuousLinearEquiv

variable [TopologicalSpace Œ±] [TopologicalSpace Œ≤]
variable [Semiring ùïú] [AddCommGroup Œ±] [AddCommGroup Œ≤]
variable [Module ùïú Œ±] [Module ùïú Œ≤]

/-- `WithLp.equiv` as a continuous linear equivalence. -/
-- This is not specific to products and should be generalised!
def prodContinuousLinearEquiv : WithLp p (Œ± √ó Œ≤) ‚âÉL[ùïú] Œ± √ó Œ≤ where
  toLinearEquiv := WithLp.linearEquiv _ _ _
  continuous_toFun := continuous_id
  continuous_invFun := continuous_id

end ContinuousLinearEquiv

/-! Throughout the rest of the file, we assume `1 ‚â§ p` -/
variable [hp : Fact (1 ‚â§ p)]

/-- `PseudoEMetricSpace` instance on the product of two pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance instProdPseudoEMetricSpace [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    PseudoEMetricSpace (WithLp p (Œ± √ó Œ≤)) :=
  (prodPseudoEMetricAux p Œ± Œ≤).replaceUniformity (prod_uniformity_aux p Œ± Œ≤).symm

/-- `EMetricSpace` instance on the product of two emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance instProdEMetricSpace [EMetricSpace Œ±] [EMetricSpace Œ≤] : EMetricSpace (WithLp p (Œ± √ó Œ≤)) :=
  EMetricSpace.ofT0PseudoEMetricSpace (WithLp p (Œ± √ó Œ≤))

/-- `PseudoMetricSpace` instance on the product of two pseudometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance instProdPseudoMetricSpace [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] :
    PseudoMetricSpace (WithLp p (Œ± √ó Œ≤)) :=
  ((prodPseudoMetricAux p Œ± Œ≤).replaceUniformity
    (prod_uniformity_aux p Œ± Œ≤).symm).replaceBornology
    fun s => Filter.ext_iff.1 (prod_cobounded_aux p Œ± Œ≤).symm s·∂ú

/-- `MetricSpace` instance on the product of two metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance instProdMetricSpace [MetricSpace Œ±] [MetricSpace Œ≤] : MetricSpace (WithLp p (Œ± √ó Œ≤)) :=
  MetricSpace.ofT0PseudoMetricSpace _

variable {p Œ± Œ≤}

theorem prod_nndist_eq_add [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤]
    (hp : p ‚â† ‚àû) (x y : WithLp p (Œ± √ó Œ≤)) :
    nndist x y = (nndist x.fst y.fst ^ p.toReal + nndist x.snd y.snd ^ p.toReal) ^ (1 / p.toReal) :=
  NNReal.eq <| by
    push_cast
    exact prod_dist_eq_add (p.toReal_pos_iff_ne_top.mpr hp) _ _

theorem prod_nndist_eq_sup [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : WithLp ‚àû (Œ± √ó Œ≤)) :
    nndist x y = nndist x.fst y.fst ‚äî nndist x.snd y.snd :=
  NNReal.eq <| by
    push_cast
    exact prod_dist_eq_sup _ _

theorem edist_fst_le [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    edist x.fst y.fst ‚â§ edist x y :=
  (edist_proj_le_edist_aux p x y).1

theorem edist_snd_le [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    edist x.snd y.snd ‚â§ edist x y :=
  (edist_proj_le_edist_aux p x y).2

theorem nndist_fst_le [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    nndist x.fst y.fst ‚â§ nndist x y := by
  simpa [‚Üê coe_nnreal_ennreal_nndist] using edist_fst_le x y

theorem nndist_snd_le [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    nndist x.snd y.snd ‚â§ nndist x y := by
  simpa [‚Üê coe_nnreal_ennreal_nndist] using edist_snd_le x y

theorem dist_fst_le [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    dist x.fst y.fst ‚â§ dist x y :=
  nndist_fst_le x y

theorem dist_snd_le [PseudoMetricSpace Œ±] [PseudoMetricSpace Œ≤] (x y : WithLp p (Œ± √ó Œ≤)) :
    dist x.snd y.snd ‚â§ dist x y :=
  nndist_snd_le x y

variable (p Œ± Œ≤)

lemma prod_lipschitzWith_ofLp [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    LipschitzWith 1 (@ofLp p (Œ± √ó Œ≤)) :=
  prod_lipschitzWith_ofLp_aux p Œ± Œ≤

lemma prod_antilipschitzWith_ofLp [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    AntilipschitzWith ((2 : ‚Ñù‚â•0) ^ (1 / p).toReal) (@ofLp p (Œ± √ó Œ≤)) :=
  prod_antilipschitzWith_ofLp_aux p Œ± Œ≤

lemma prod_isometry_ofLp_infty [PseudoEMetricSpace Œ±] [PseudoEMetricSpace Œ≤] :
    Isometry (@ofLp ‚àû (Œ± √ó Œ≤)) :=
  fun x y =>
  le_antisymm (by simpa only [ENNReal.coe_one, one_mul] using prod_lipschitzWith_ofLp ‚àû Œ± Œ≤ x y)
    (by
      simpa only [ENNReal.div_top, ENNReal.toReal_zero, NNReal.rpow_zero, ENNReal.coe_one,
        one_mul] using prod_antilipschitzWith_ofLp ‚àû Œ± Œ≤ x y)

/-- Seminormed group instance on the product of two normed groups, using the `L^p`
norm. -/
instance instProdSeminormedAddCommGroup [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] :
    SeminormedAddCommGroup (WithLp p (Œ± √ó Œ≤)) where
  dist_eq x y := by
    rcases p.dichotomy with (rfl | h)
    ¬∑ simp only [prod_dist_eq_sup, prod_norm_eq_sup, dist_eq_norm]
      rfl
    ¬∑ simp only [prod_dist_eq_add (zero_lt_one.trans_le h),
        prod_norm_eq_add (zero_lt_one.trans_le h), dist_eq_norm]
      rfl

section
variable {Œ≤ p}

theorem enorm_fst_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.fst‚Äñ‚Çë ‚â§ ‚Äñx‚Äñ‚Çë := by
  simpa using edist_fst_le x 0

theorem enorm_snd_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.snd‚Äñ‚Çë ‚â§ ‚Äñx‚Äñ‚Çë := by
  simpa using edist_snd_le x 0

theorem nnnorm_fst_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.fst‚Äñ‚Çä ‚â§ ‚Äñx‚Äñ‚Çä := by
  simpa using nndist_fst_le x 0

theorem nnnorm_snd_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.snd‚Äñ‚Çä ‚â§ ‚Äñx‚Äñ‚Çä := by
  simpa using nndist_snd_le x 0

theorem norm_fst_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.fst‚Äñ ‚â§ ‚Äñx‚Äñ := by
  simpa using dist_fst_le x 0

theorem norm_snd_le [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤] (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx.snd‚Äñ ‚â§ ‚Äñx‚Äñ := by
  simpa using dist_snd_le x 0

end

/-- normed group instance on the product of two normed groups, using the `L^p` norm. -/
instance instProdNormedAddCommGroup [NormedAddCommGroup Œ±] [NormedAddCommGroup Œ≤] :
    NormedAddCommGroup (WithLp p (Œ± √ó Œ≤)) :=
  { instProdSeminormedAddCommGroup p Œ± Œ≤ with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

example [NormedAddCommGroup Œ±] [NormedAddCommGroup Œ≤] :
    (instProdNormedAddCommGroup p Œ± Œ≤).toMetricSpace.toUniformSpace.toTopologicalSpace =
    instProdTopologicalSpace p Œ± Œ≤ :=
  rfl

example [NormedAddCommGroup Œ±] [NormedAddCommGroup Œ≤] :
    (instProdNormedAddCommGroup p Œ± Œ≤).toMetricSpace.toUniformSpace = instProdUniformSpace p Œ± Œ≤ :=
  rfl

example [NormedAddCommGroup Œ±] [NormedAddCommGroup Œ≤] :
    (instProdNormedAddCommGroup p Œ± Œ≤).toMetricSpace.toBornology = instProdBornology p Œ± Œ≤ :=
  rfl

section norm_of

variable {p Œ± Œ≤}

theorem prod_norm_eq_of_nat [Norm Œ±] [Norm Œ≤] (n : ‚Ñï) (h : p = n) (f : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñf‚Äñ = (‚Äñf.fst‚Äñ ^ n + ‚Äñf.snd‚Äñ ^ n) ^ (1 / (n : ‚Ñù)) := by
  have := p.toReal_pos_iff_ne_top.mpr (ne_of_eq_of_ne h <| ENNReal.natCast_ne_top n)
  simp only [one_div, h, Real.rpow_natCast, ENNReal.toReal_natCast,
    prod_norm_eq_add this]

variable [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤]

theorem prod_nnnorm_eq_add (hp : p ‚â† ‚àû) (f : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñf‚Äñ‚Çä = (‚Äñf.fst‚Äñ‚Çä ^ p.toReal + ‚Äñf.snd‚Äñ‚Çä ^ p.toReal) ^ (1 / p.toReal) := by
  ext
  simp [prod_norm_eq_add (p.toReal_pos_iff_ne_top.mpr hp)]

theorem prod_nnnorm_eq_sup (f : WithLp ‚àû (Œ± √ó Œ≤)) : ‚Äñf‚Äñ‚Çä = ‚Äñf.fst‚Äñ‚Çä ‚äî  ‚Äñf.snd‚Äñ‚Çä := by
  ext
  norm_cast

@[simp] lemma prod_nnnorm_ofLp (f : WithLp ‚àû (Œ± √ó Œ≤)) : ‚ÄñofLp f‚Äñ‚Çä = ‚Äñf‚Äñ‚Çä := by
  rw [prod_nnnorm_eq_sup, Prod.nnnorm_def, ofLp_fst, ofLp_snd]

@[simp] lemma prod_nnnorm_toLp (f : Œ± √ó Œ≤) : ‚ÄñtoLp ‚ä§ f‚Äñ‚Çä = ‚Äñf‚Äñ‚Çä :=
  (prod_nnnorm_ofLp _).symm

@[simp] lemma prod_norm_ofLp (f : WithLp ‚àû (Œ± √ó Œ≤)) : ‚ÄñofLp f‚Äñ = ‚Äñf‚Äñ :=
  congr_arg NNReal.toReal <| prod_nnnorm_ofLp f

@[simp] lemma prod_norm_toLp (f : Œ± √ó Œ≤) : ‚ÄñtoLp ‚ä§ f‚Äñ = ‚Äñf‚Äñ :=
  (prod_norm_ofLp _).symm

section L1

theorem prod_norm_eq_of_L1 (x : WithLp 1 (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ = ‚Äñx.fst‚Äñ + ‚Äñx.snd‚Äñ := by
  simp [prod_norm_eq_add]

theorem prod_nnnorm_eq_of_L1 (x : WithLp 1 (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ‚Çä = ‚Äñx.fst‚Äñ‚Çä + ‚Äñx.snd‚Äñ‚Çä :=
  NNReal.eq <| by
    push_cast
    exact prod_norm_eq_of_L1 x

theorem prod_dist_eq_of_L1 (x y : WithLp 1 (Œ± √ó Œ≤)) :
    dist x y = dist x.fst y.fst + dist x.snd y.snd := by
  simp_rw [dist_eq_norm, prod_norm_eq_of_L1, sub_fst, sub_snd]

theorem prod_nndist_eq_of_L1 (x y : WithLp 1 (Œ± √ó Œ≤)) :
    nndist x y = nndist x.fst y.fst + nndist x.snd y.snd :=
  NNReal.eq <| by
    push_cast
    exact prod_dist_eq_of_L1 _ _

theorem prod_edist_eq_of_L1 (x y : WithLp 1 (Œ± √ó Œ≤)) :
    edist x y = edist x.fst y.fst + edist x.snd y.snd := by
  simp [prod_edist_eq_add]

end L1

section L2

theorem prod_norm_eq_of_L2 (x : WithLp 2 (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ = ‚àö(‚Äñx.fst‚Äñ ^ 2 + ‚Äñx.snd‚Äñ ^ 2) := by
  rw [prod_norm_eq_of_nat 2 (by norm_cast) _, Real.sqrt_eq_rpow]
  norm_cast

theorem prod_nnnorm_eq_of_L2 (x : WithLp 2 (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ‚Çä = NNReal.sqrt (‚Äñx.fst‚Äñ‚Çä ^ 2 + ‚Äñx.snd‚Äñ‚Çä ^ 2) :=
  NNReal.eq <| by
    push_cast
    exact prod_norm_eq_of_L2 x

theorem prod_norm_sq_eq_of_L2 (x : WithLp 2 (Œ± √ó Œ≤)) : ‚Äñx‚Äñ ^ 2 = ‚Äñx.fst‚Äñ ^ 2 + ‚Äñx.snd‚Äñ ^ 2 := by
  suffices ‚Äñx‚Äñ‚Çä ^ 2 = ‚Äñx.fst‚Äñ‚Çä ^ 2 + ‚Äñx.snd‚Äñ‚Çä ^ 2 by
    simpa only [NNReal.coe_sum] using congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) this
  rw [prod_nnnorm_eq_of_L2, NNReal.sq_sqrt]

theorem prod_dist_eq_of_L2 (x y : WithLp 2 (Œ± √ó Œ≤)) :
    dist x y = ‚àö(dist x.fst y.fst ^ 2 + dist x.snd y.snd ^ 2) := by
  simp_rw [dist_eq_norm, prod_norm_eq_of_L2, sub_fst, sub_snd]

theorem prod_nndist_eq_of_L2 (x y : WithLp 2 (Œ± √ó Œ≤)) :
    nndist x y = NNReal.sqrt (nndist x.fst y.fst ^ 2 + nndist x.snd y.snd ^ 2) :=
  NNReal.eq <| by
    push_cast
    exact prod_dist_eq_of_L2 _ _

theorem prod_edist_eq_of_L2 (x y : WithLp 2 (Œ± √ó Œ≤)) :
    edist x y = (edist x.fst y.fst ^ 2 + edist x.snd y.snd ^ 2) ^ (1 / 2 : ‚Ñù) := by
  simp [prod_edist_eq_add]

end L2

end norm_of

variable [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤]

section Single

@[simp] lemma nnnorm_toLp_inl (x : Œ±) : ‚ÄñtoLp p (x, (0 : Œ≤))‚Äñ‚Çä = ‚Äñx‚Äñ‚Çä := by
  induction p generalizing hp with
  | top =>
    simp [prod_nnnorm_eq_sup]
  | coe p =>
    have hp0 : (p : ‚Ñù) ‚â† 0 := mod_cast (zero_lt_one.trans_le <| Fact.out (p := 1 ‚â§ (p : ‚Ñù‚â•0‚àû))).ne'
    simp [prod_nnnorm_eq_add, NNReal.zero_rpow hp0, ‚Üê NNReal.rpow_mul, mul_inv_cancel‚ÇÄ hp0]

@[simp] lemma nnnorm_toLp_inr (y : Œ≤) : ‚ÄñtoLp p ((0 : Œ±), y)‚Äñ‚Çä = ‚Äñy‚Äñ‚Çä := by
  induction p generalizing hp with
  | top =>
    simp [prod_nnnorm_eq_sup]
  | coe p =>
    have hp0 : (p : ‚Ñù) ‚â† 0 := mod_cast (zero_lt_one.trans_le <| Fact.out (p := 1 ‚â§ (p : ‚Ñù‚â•0‚àû))).ne'
    simp [prod_nnnorm_eq_add, NNReal.zero_rpow hp0, ‚Üê NNReal.rpow_mul, mul_inv_cancel‚ÇÄ hp0]

@[simp]
lemma norm_toLp_fst (x : Œ±) : ‚ÄñtoLp p (x, (0 : Œ≤))‚Äñ = ‚Äñx‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_toLp_inl p Œ± Œ≤ x

@[simp]
lemma norm_toLp_snd (y : Œ≤) : ‚ÄñtoLp p ((0 : Œ±), y)‚Äñ = ‚Äñy‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_toLp_inr p Œ± Œ≤ y

@[simp]
lemma nndist_toLp_fst (x‚ÇÅ x‚ÇÇ : Œ±) :
    nndist (toLp p (x‚ÇÅ, (0 : Œ≤))) (toLp p (x‚ÇÇ, 0)) = nndist x‚ÇÅ x‚ÇÇ := by
  rw [nndist_eq_nnnorm, nndist_eq_nnnorm, ‚Üê toLp_sub, Prod.mk_sub_mk, sub_zero,
    nnnorm_toLp_inl]

@[simp]
lemma nndist_toLp_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    nndist (toLp p ((0 : Œ±), y‚ÇÅ)) (toLp p (0, y‚ÇÇ)) = nndist y‚ÇÅ y‚ÇÇ := by
  rw [nndist_eq_nnnorm, nndist_eq_nnnorm, ‚Üê toLp_sub, Prod.mk_sub_mk, sub_zero,
    nnnorm_toLp_inr]

@[simp]
lemma dist_toLp_fst (x‚ÇÅ x‚ÇÇ : Œ±) : dist (toLp p (x‚ÇÅ, (0 : Œ≤))) (toLp p (x‚ÇÇ, 0)) = dist x‚ÇÅ x‚ÇÇ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nndist_toLp_fst p Œ± Œ≤ x‚ÇÅ x‚ÇÇ

@[simp]
lemma dist_toLp_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    dist (toLp p ((0 : Œ±), y‚ÇÅ)) (toLp p (0, y‚ÇÇ)) = dist y‚ÇÅ y‚ÇÇ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nndist_toLp_snd p Œ± Œ≤ y‚ÇÅ y‚ÇÇ

@[simp]
lemma edist_toLp_fst (x‚ÇÅ x‚ÇÇ : Œ±) : edist (toLp p (x‚ÇÅ, (0 : Œ≤))) (toLp p (x‚ÇÇ, 0)) = edist x‚ÇÅ x‚ÇÇ := by
  simp only [edist_nndist, nndist_toLp_fst p Œ± Œ≤ x‚ÇÅ x‚ÇÇ]

@[simp]
lemma edist_toLp_snd (y‚ÇÅ y‚ÇÇ : Œ≤) :
    edist (toLp p ((0 : Œ±), y‚ÇÅ)) (toLp p (0, y‚ÇÇ)) = edist y‚ÇÅ y‚ÇÇ := by
  simp only [edist_nndist, nndist_toLp_snd p Œ± Œ≤ y‚ÇÅ y‚ÇÇ]

end Single

section IsBoundedSMul
variable [SeminormedRing ùïú] [Module ùïú Œ±] [Module ùïú Œ≤] [IsBoundedSMul ùïú Œ±] [IsBoundedSMul ùïú Œ≤]

instance instProdIsBoundedSMul : IsBoundedSMul ùïú (WithLp p (Œ± √ó Œ≤)) :=
  .of_nnnorm_smul_le fun c f => by
    rcases p.dichotomy with (rfl | hp)
    ¬∑ simp only [‚Üê prod_nnnorm_ofLp, ofLp_smul]
      exact norm_smul_le _ _
    ¬∑ have hp0 : 0 < p.toReal := zero_lt_one.trans_le hp
      have hpt : p ‚â† ‚ä§ := p.toReal_pos_iff_ne_top.mp hp0
      rw [prod_nnnorm_eq_add hpt, prod_nnnorm_eq_add hpt, one_div, NNReal.rpow_inv_le_iff hp0,
        NNReal.mul_rpow, ‚Üê NNReal.rpow_mul, inv_mul_cancel‚ÇÄ hp0.ne', NNReal.rpow_one, mul_add,
        ‚Üê NNReal.mul_rpow, ‚Üê NNReal.mul_rpow]
      exact add_le_add
        (NNReal.rpow_le_rpow (nnnorm_smul_le _ _) hp0.le)
        (NNReal.rpow_le_rpow (nnnorm_smul_le _ _) hp0.le)

variable {ùïú p Œ± Œ≤}

/-- The canonical map `WithLp.equiv` between `WithLp ‚àû (Œ± √ó Œ≤)` and `Œ± √ó Œ≤` as a linear isometric
equivalence. -/
def prodEquiv‚Çó·µ¢ : WithLp ‚àû (Œ± √ó Œ≤) ‚âÉ‚Çó·µ¢[ùïú] Œ± √ó Œ≤ where
  __ := WithLp.equiv p _
  map_add' _f _g := rfl
  map_smul' _c _f := rfl
  norm_map' := prod_norm_toLp

end IsBoundedSMul

instance instProdNormSMulClass [SeminormedRing ùïú] [Module ùïú Œ±] [Module ùïú Œ≤]
    [NormSMulClass ùïú Œ±] [NormSMulClass ùïú Œ≤] : NormSMulClass ùïú (WithLp p (Œ± √ó Œ≤)) :=
  .of_nnnorm_smul fun c f => by
    rcases p.dichotomy with (rfl | hp)
    ¬∑ simp only [‚Üê prod_nnnorm_ofLp, WithLp.ofLp_smul, nnnorm_smul]
    ¬∑ have hp0 : 0 < p.toReal := zero_lt_one.trans_le hp
      have hpt : p ‚â† ‚ä§ := p.toReal_pos_iff_ne_top.mp hp0
      rw [prod_nnnorm_eq_add hpt, prod_nnnorm_eq_add hpt, one_div, NNReal.rpow_inv_eq_iff hp0.ne',
        NNReal.mul_rpow, ‚Üê NNReal.rpow_mul, inv_mul_cancel‚ÇÄ hp0.ne', NNReal.rpow_one, mul_add,
        ‚Üê NNReal.mul_rpow, ‚Üê NNReal.mul_rpow, smul_fst, smul_snd, nnnorm_smul, nnnorm_smul]

section SeminormedAddCommGroup

open ENNReal

variable {p : ‚Ñù‚â•0‚àû} {Œ± Œ≤}

/-- Projection on `WithLp p (Œ± √ó Œ≤)` with range `Œ±` and kernel `Œ≤` -/
def idemFst : AddMonoid.End (WithLp p (Œ± √ó Œ≤)) := (AddMonoidHom.inl Œ± Œ≤).comp (AddMonoidHom.fst Œ± Œ≤)

/-- Projection on `WithLp p (Œ± √ó Œ≤)` with range `Œ≤` and kernel `Œ±` -/
def idemSnd : AddMonoid.End (WithLp p (Œ± √ó Œ≤)) := (AddMonoidHom.inr Œ± Œ≤).comp (AddMonoidHom.snd Œ± Œ≤)

lemma idemFst_apply (x : WithLp p (Œ± √ó Œ≤)) : idemFst x = toLp p (x.1, 0) := rfl

lemma idemSnd_apply (x : WithLp p (Œ± √ó Œ≤)) : idemSnd x = toLp p (0, x.2) := rfl

@[simp]
lemma idemFst_add_idemSnd :
    idemFst + idemSnd = (1 : AddMonoid.End (WithLp p (Œ± √ó Œ≤))) := AddMonoidHom.ext
  fun x => by
    rw [AddMonoidHom.add_apply, idemFst_apply, idemSnd_apply, AddMonoid.End.coe_one, id_eq,
      ‚Üê toLp_add, Prod.mk_add_mk, zero_add, add_zero]
    rfl

lemma idemFst_compl : (1 : AddMonoid.End (WithLp p (Œ± √ó Œ≤))) - idemFst = idemSnd := by
  rw [‚Üê idemFst_add_idemSnd, add_sub_cancel_left]

lemma idemSnd_compl : (1 : AddMonoid.End (WithLp p (Œ± √ó Œ≤))) - idemSnd = idemFst := by
  rw [‚Üê idemFst_add_idemSnd, add_sub_cancel_right]

theorem prod_norm_eq_idemFst_sup_idemSnd (x : WithLp ‚àû (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ = max ‚ÄñidemFst x‚Äñ ‚ÄñidemSnd x‚Äñ := by
  rw [WithLp.prod_norm_eq_sup, ‚Üê WithLp.norm_toLp_fst ‚àû Œ± Œ≤ x.1,
    ‚Üê WithLp.norm_toLp_snd ‚àû Œ± Œ≤ x.2]
  rfl

lemma prod_norm_eq_add_idemFst [Fact (1 ‚â§ p)] (hp : 0 < p.toReal) (x : WithLp p (Œ± √ó Œ≤)) :
    ‚Äñx‚Äñ = (‚ÄñidemFst x‚Äñ ^ p.toReal + ‚ÄñidemSnd x‚Äñ ^ p.toReal) ^ (1 / p.toReal) := by
  rw [WithLp.prod_norm_eq_add hp, ‚Üê WithLp.norm_toLp_fst p Œ± Œ≤ x.1,
    ‚Üê WithLp.norm_toLp_snd p Œ± Œ≤ x.2]
  rfl

lemma prod_norm_eq_idemFst_of_L1 (x : WithLp 1 (Œ± √ó Œ≤)) : ‚Äñx‚Äñ = ‚ÄñidemFst x‚Äñ + ‚ÄñidemSnd x‚Äñ := by
  rw [prod_norm_eq_add_idemFst (lt_of_lt_of_eq zero_lt_one toReal_one.symm)]
  simp only [toReal_one, Real.rpow_one, ne_eq, one_ne_zero, not_false_eq_true, div_self]

end SeminormedAddCommGroup

section NormedSpace

/-- The product of two normed spaces is a normed space, with the `L^p` norm. -/
instance instProdNormedSpace [NormedField ùïú] [NormedSpace ùïú Œ±] [NormedSpace ùïú Œ≤] :
    NormedSpace ùïú (WithLp p (Œ± √ó Œ≤)) where
  norm_smul_le := norm_smul_le

end NormedSpace

end WithLp
