/-
Copyright (c) 2020 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Jireh Loreaux
-/
import Mathlib.Analysis.MeanInequalities
import Mathlib.Data.Fintype.Order
import Mathlib.LinearAlgebra.Matrix.Basis
import Mathlib.Analysis.Normed.Lp.ProdLp

/-!
# `L^p` distance on finite products of metric spaces
Given finitely many metric spaces, one can put the max distance on their product, but there is also
a whole family of natural distances, indexed by a parameter `p : â„â‰¥0âˆ`, that also induce
the product topology. We define them in this file. For `0 < p < âˆ`, the distance on `Î  i, Î± i`
is given by
$$
d(x, y) = \left(\sum d(x_i, y_i)^p\right)^{1/p}.
$$,
whereas for `p = 0` it is the cardinality of the set ${i | d (x_i, y_i) â‰  0}$. For `p = âˆ` the
distance is the supremum of the distances.

We give instances of this construction for emetric spaces, metric spaces, normed groups and normed
spaces.

To avoid conflicting instances, all these are defined on a copy of the original Î -type, named
`PiLp p Î±`. The assumption `[Fact (1 â‰¤ p)]` is required for the metric and normed space instances.

We ensure that the topology, bornology and uniform structure on `PiLp p Î±` are (defeq to) the
product topology, product bornology and product uniformity, to be able to use freely continuity
statements for the coordinate functions, for instance.

## Implementation notes

We only deal with the `L^p` distance on a product of finitely many metric spaces, which may be
distinct. A closely related construction is `lp`, the `L^p` norm on a product of (possibly
infinitely many) normed spaces, where the norm is
$$
\left(\sum â€–f (x)â€–^p \right)^{1/p}.
$$
However, the topology induced by this construction is not the product topology, and some functions
have infinite `L^p` norm. These subtleties are not present in the case of finitely many metric
spaces, hence it is worth devoting a file to this specific case which is particularly well behaved.

Another related construction is `MeasureTheory.Lp`, the `L^p` norm on the space of functions from
a measure space to a normed space, where the norm is
$$
\left(\int â€–f (x)â€–^p dÎ¼\right)^{1/p}.
$$
This has all the same subtleties as `lp`, and the further subtlety that this only
defines a seminorm (as almost everywhere zero functions have zero `L^p` norm).
The construction `PiLp` corresponds to the special case of `MeasureTheory.Lp` in which the basis
is a finite space equipped with the counting measure.

To prove that the topology (and the uniform structure) on a finite product with the `L^p` distance
are the same as those coming from the `L^âˆ` distance, we could argue that the `L^p` and `L^âˆ` norms
are equivalent on `â„^n` for abstract (norm equivalence) reasons. Instead, we give a more explicit
(easy) proof which provides a comparison between these two norms with explicit constants.

We also set up the theory for `PseudoEMetricSpace` and `PseudoMetricSpace`.
-/


open Module Real Set Filter RCLike Bornology Uniformity Topology NNReal ENNReal WithLp

noncomputable section

/-- A copy of a Pi type, on which we will put the `L^p` distance. Since the Pi type itself is
already endowed with the `L^âˆ` distance, we need the type synonym to avoid confusing typeclass
resolution. Also, we let it depend on `p`, to get a whole family of type on which we can put
different distances. -/
abbrev PiLp (p : â„â‰¥0âˆ) {Î¹ : Type*} (Î± : Î¹ â†’ Type*) : Type _ :=
  WithLp p (âˆ€ i : Î¹, Î± i)

/-The following should not be a `FunLike` instance because then the coercion `â‡‘` would get
unfolded to `FunLike.coe` instead of `WithLp.equiv`. -/
instance (p : â„â‰¥0âˆ) {Î¹ : Type*} (Î± : Î¹ â†’ Type*) : CoeFun (PiLp p Î±) (fun _ â†¦ (i : Î¹) â†’ Î± i) where
  coe := ofLp

instance (p : â„â‰¥0âˆ) {Î¹ : Type*} (Î± : Î¹ â†’ Type*) [âˆ€ i, Inhabited (Î± i)] : Inhabited (PiLp p Î±) :=
  âŸ¨fun _ => defaultâŸ©

@[ext]
protected theorem PiLp.ext {p : â„â‰¥0âˆ} {Î¹ : Type*} {Î± : Î¹ â†’ Type*} {x y : PiLp p Î±}
    (h : âˆ€ i, x i = y i) : x = y := funext h

namespace PiLp

variable (p : â„â‰¥0âˆ) (ğ•œ : Type*) {Î¹ : Type*} (Î± : Î¹ â†’ Type*) (Î² : Î¹ â†’ Type*)
section
/- Register simplification lemmas for the applications of `PiLp` elements, as the usual lemmas
for Pi types will not trigger. -/
variable {ğ•œ p Î±}
variable [Semiring ğ•œ] [âˆ€ i, SeminormedAddCommGroup (Î² i)]
variable [âˆ€ i, Module ğ•œ (Î² i)] (c : ğ•œ)
variable (x y : PiLp p Î²) (i : Î¹)

@[simp]
theorem zero_apply : (0 : PiLp p Î²) i = 0 :=
  rfl

@[simp]
theorem add_apply : (x + y) i = x i + y i :=
  rfl

@[simp]
theorem sub_apply : (x - y) i = x i - y i :=
  rfl

@[simp]
theorem smul_apply : (c â€¢ x) i = c â€¢ x i :=
  rfl

@[simp]
theorem neg_apply : (-x) i = -x i :=
  rfl

variable (p) in
/-- The projection on the `i`-th coordinate of `WithLp p (âˆ€ i, Î± i)`, as a linear map. -/
@[simps!]
def projâ‚— (i : Î¹) : PiLp p Î² â†’â‚—[ğ•œ] Î² i :=
  (LinearMap.proj i : (âˆ€ i, Î² i) â†’â‚—[ğ•œ] Î² i) âˆ˜â‚— (WithLp.linearEquiv p ğ•œ (âˆ€ i, Î² i)).toLinearMap

end

/-! Note that the unapplied versions of these lemmas are deliberately omitted, as they break
the use of the type synonym. -/

@[simp] lemma ofLp_apply (x : PiLp p Î±) (i : Î¹) : ofLp x i = x i := rfl
@[simp] lemma toLp_apply (x : âˆ€ i, Î± i) (i : Î¹) : toLp p x i = x i := rfl

section DistNorm

variable [Fintype Î¹]

/-!
### Definition of `edist`, `dist` and `norm` on `PiLp`

In this section we define the `edist`, `dist` and `norm` functions on `PiLp p Î±` without assuming
`[Fact (1 â‰¤ p)]` or metric properties of the spaces `Î± i`. This allows us to provide the rewrite
lemmas for each of three cases `p = 0`, `p = âˆ` and `0 < p.to_real`.
-/


section Edist

variable [âˆ€ i, EDist (Î² i)]

/-- Endowing the space `PiLp p Î²` with the `L^p` edistance. We register this instance
separate from `pi_Lp.pseudo_emetric` since the latter requires the type class hypothesis
`[Fact (1 â‰¤ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future emetric-like structure on `PiLp p Î²` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance : EDist (PiLp p Î²) where
  edist f g :=
    if p = 0 then {i | edist (f i) (g i) â‰  0}.toFinite.toFinset.card
    else
      if p = âˆ then â¨† i, edist (f i) (g i) else (âˆ‘ i, edist (f i) (g i) ^ p.toReal) ^ (1 / p.toReal)

variable {Î²}

theorem edist_eq_card (f g : PiLp 0 Î²) :
    edist f g = {i | edist (f i) (g i) â‰  0}.toFinite.toFinset.card :=
  if_pos rfl

theorem edist_eq_sum {p : â„â‰¥0âˆ} (hp : 0 < p.toReal) (f g : PiLp p Î²) :
    edist f g = (âˆ‘ i, edist (f i) (g i) ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem edist_eq_iSup (f g : PiLp âˆ Î²) : edist f g = â¨† i, edist (f i) (g i) := rfl

end Edist

section EdistProp

variable {Î²}
variable [âˆ€ i, PseudoEMetricSpace (Î² i)]

/-- This holds independent of `p` and does not require `[Fact (1 â‰¤ p)]`. We keep it separate
from `pi_Lp.pseudo_emetric_space` so it can be used also for `p < 1`. -/
protected theorem edist_self (f : PiLp p Î²) : edist f f = 0 := by
  rcases p.trichotomy with (rfl | rfl | h)
  Â· simp [edist_eq_card]
  Â· simp [edist_eq_iSup]
  Â· simp [edist_eq_sum h, ENNReal.zero_rpow_of_pos h, ENNReal.zero_rpow_of_pos (inv_pos.2 <| h)]

/-- This holds independent of `p` and does not require `[Fact (1 â‰¤ p)]`. We keep it separate
from `pi_Lp.pseudo_emetric_space` so it can be used also for `p < 1`. -/
protected theorem edist_comm (f g : PiLp p Î²) : edist f g = edist g f := by
  rcases p.trichotomy with (rfl | rfl | h)
  Â· simp only [edist_eq_card, edist_comm]
  Â· simp only [edist_eq_iSup, edist_comm]
  Â· simp only [edist_eq_sum h, edist_comm]

end EdistProp

section Dist

variable [âˆ€ i, Dist (Î± i)]

/-- Endowing the space `PiLp p Î²` with the `L^p` distance. We register this instance
separate from `pi_Lp.pseudo_metric` since the latter requires the type class hypothesis
`[Fact (1 â‰¤ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future metric-like structure on `PiLp p Î²` for `p < 1`
satisfying a relaxed triangle inequality. The terminology for this varies throughout the
literature, but it is sometimes called a *quasi-metric* or *semi-metric*. -/
instance : Dist (PiLp p Î±) where
  dist f g :=
    if p = 0 then {i | dist (f i) (g i) â‰  0}.toFinite.toFinset.card
    else
      if p = âˆ then â¨† i, dist (f i) (g i) else (âˆ‘ i, dist (f i) (g i) ^ p.toReal) ^ (1 / p.toReal)

variable {Î±}

theorem dist_eq_card (f g : PiLp 0 Î±) :
    dist f g = {i | dist (f i) (g i) â‰  0}.toFinite.toFinset.card :=
  if_pos rfl

theorem dist_eq_sum {p : â„â‰¥0âˆ} (hp : 0 < p.toReal) (f g : PiLp p Î±) :
    dist f g = (âˆ‘ i, dist (f i) (g i) ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

theorem dist_eq_iSup (f g : PiLp âˆ Î±) : dist f g = â¨† i, dist (f i) (g i) := rfl

end Dist

section Norm

variable [âˆ€ i, Norm (Î² i)]

/-- Endowing the space `PiLp p Î²` with the `L^p` norm. We register this instance
separate from `PiLp.seminormedAddCommGroup` since the latter requires the type class hypothesis
`[Fact (1 â‰¤ p)]` in order to prove the triangle inequality.

Registering this separately allows for a future norm-like structure on `PiLp p Î²` for `p < 1`
satisfying a relaxed triangle inequality. These are called *quasi-norms*. -/
instance instNorm : Norm (PiLp p Î²) where
  norm f :=
    if p = 0 then {i | â€–f iâ€– â‰  0}.toFinite.toFinset.card
    else if p = âˆ then â¨† i, â€–f iâ€– else (âˆ‘ i, â€–f iâ€– ^ p.toReal) ^ (1 / p.toReal)

variable {p Î²}

theorem norm_eq_card (f : PiLp 0 Î²) : â€–fâ€– = {i | â€–f iâ€– â‰  0}.toFinite.toFinset.card :=
  if_pos rfl

theorem norm_eq_ciSup (f : PiLp âˆ Î²) : â€–fâ€– = â¨† i, â€–f iâ€– := rfl

theorem norm_eq_sum (hp : 0 < p.toReal) (f : PiLp p Î²) :
    â€–fâ€– = (âˆ‘ i, â€–f iâ€– ^ p.toReal) ^ (1 / p.toReal) :=
  let hp' := ENNReal.toReal_pos_iff.mp hp
  (if_neg hp'.1.ne').trans (if_neg hp'.2.ne)

end Norm

end DistNorm

section Aux

/-!
### The uniformity on finite `L^p` products is the product uniformity

In this section, we put the `L^p` edistance on `PiLp p Î±`, and we check that the uniformity
coming from this edistance coincides with the product uniformity, by showing that the canonical
map to the Pi type (with the `L^âˆ` distance) is a uniform embedding, as it is both Lipschitz and
antiLipschitz.

We only register this emetric space structure as a temporary instance, as the true instance (to be
registered later) will have as uniformity exactly the product uniformity, instead of the one coming
from the edistance (which is equal to it, but not defeq). See Note [forgetful inheritance]
explaining why having definitionally the right uniformity is often important.
-/


variable [Fact (1 â‰¤ p)] [âˆ€ i, PseudoMetricSpace (Î± i)] [âˆ€ i, PseudoEMetricSpace (Î² i)]
variable [Fintype Î¹]

/-- Endowing the space `PiLp p Î²` with the `L^p` pseudoemetric structure. This definition is not
satisfactory, as it does not register the fact that the topology and the uniform structure coincide
with the product one. Therefore, we do not register it as an instance. Using this as a temporary
pseudoemetric space instance, we will show that the uniform structure is equal (but not defeq) to
the product one, and then register an instance in which we replace the uniform structure by the
product one using this pseudoemetric space and `PseudoEMetricSpace.replaceUniformity`. -/
def pseudoEmetricAux : PseudoEMetricSpace (PiLp p Î²) where
  edist_self := PiLp.edist_self p
  edist_comm := PiLp.edist_comm p
  edist_triangle f g h := by
    rcases p.dichotomy with (rfl | hp)
    Â· simp only [edist_eq_iSup]
      cases isEmpty_or_nonempty Î¹
      Â· simp only [ciSup_of_empty, ENNReal.bot_eq_zero, add_zero, nonpos_iff_eq_zero]
      -- Porting note: `le_iSup` needed some help
      refine
        iSup_le fun i => (edist_triangle _ (g i) _).trans <| add_le_add
            (le_iSup (fun k => edist (f k) (g k)) i) (le_iSup (fun k => edist (g k) (h k)) i)
    Â· simp only [edist_eq_sum (zero_lt_one.trans_le hp)]
      calc
        (âˆ‘ i, edist (f i) (h i) ^ p.toReal) ^ (1 / p.toReal) â‰¤
            (âˆ‘ i, (edist (f i) (g i) + edist (g i) (h i)) ^ p.toReal) ^ (1 / p.toReal) := by
          gcongr
          apply edist_triangle
        _ â‰¤
            (âˆ‘ i, edist (f i) (g i) ^ p.toReal) ^ (1 / p.toReal) +
              (âˆ‘ i, edist (g i) (h i) ^ p.toReal) ^ (1 / p.toReal) :=
          ENNReal.Lp_add_le _ _ _ hp

attribute [local instance] PiLp.pseudoEmetricAux

/-- An auxiliary lemma used twice in the proof of `PiLp.pseudoMetricAux` below. Not intended for
use outside this file. -/
theorem iSup_edist_ne_top_aux {Î¹ : Type*} [Finite Î¹] {Î± : Î¹ â†’ Type*}
    [âˆ€ i, PseudoMetricSpace (Î± i)] (f g : PiLp âˆ Î±) : (â¨† i, edist (f i) (g i)) â‰  âŠ¤ := by
  cases nonempty_fintype Î¹
  obtain âŸ¨M, hMâŸ© := Finite.exists_le fun i => (âŸ¨dist (f i) (g i), dist_nonnegâŸ© : â„â‰¥0)
  refine ne_of_lt ((iSup_le fun i => ?_).trans_lt (@ENNReal.coe_lt_top M))
  simp only [edist, PseudoMetricSpace.edist_dist, ENNReal.ofReal_eq_coe_nnreal dist_nonneg]
  exact mod_cast hM i

/-- Endowing the space `PiLp p Î±` with the `L^p` pseudometric structure. This definition is not
satisfactory, as it does not register the fact that the topology, the uniform structure, and the
bornology coincide with the product ones. Therefore, we do not register it as an instance. Using
this as a temporary pseudoemetric space instance, we will show that the uniform structure is equal
(but not defeq) to the product one, and then register an instance in which we replace the uniform
structure and the bornology by the product ones using this pseudometric space,
`PseudoMetricSpace.replaceUniformity`, and `PseudoMetricSpace.replaceBornology`.

See note [reducible non-instances] -/
abbrev pseudoMetricAux : PseudoMetricSpace (PiLp p Î±) :=
  PseudoEMetricSpace.toPseudoMetricSpaceOfDist dist
    (fun f g => by
      rcases p.dichotomy with (rfl | h)
      Â· exact iSup_edist_ne_top_aux f g
      Â· rw [edist_eq_sum (zero_lt_one.trans_le h)]
        exact ENNReal.rpow_ne_top_of_nonneg (by positivity) <| ENNReal.sum_ne_top.2 fun _ _ â†¦
          by finiteness)
    fun f g => by
    rcases p.dichotomy with (rfl | h)
    Â· rw [edist_eq_iSup, dist_eq_iSup]
      cases isEmpty_or_nonempty Î¹
      Â· simp only [Real.iSup_of_isEmpty, ciSup_of_empty, ENNReal.bot_eq_zero, ENNReal.toReal_zero]
      Â· refine le_antisymm (ciSup_le fun i => ?_) ?_
        Â· rw [â† ENNReal.ofReal_le_iff_le_toReal (iSup_edist_ne_top_aux f g), â†
            PseudoMetricSpace.edist_dist]
          -- Porting note: `le_iSup` needed some help
          exact le_iSup (fun k => edist (f k) (g k)) i
        Â· refine ENNReal.toReal_le_of_le_ofReal (Real.sSup_nonneg ?_) (iSup_le fun i => ?_)
          Â· rintro - âŸ¨i, rflâŸ©
            exact dist_nonneg
          Â· change PseudoMetricSpace.edist _ _ â‰¤ _
            rw [PseudoMetricSpace.edist_dist]
            -- Porting note: `le_ciSup` needed some help
            exact ENNReal.ofReal_le_ofReal
              (le_ciSup (Finite.bddAbove_range (fun k => dist (f k) (g k))) i)
    Â· have A (i) : edist (f i) (g i) ^ p.toReal â‰  âŠ¤ := by finiteness
      simp only [edist_eq_sum (zero_lt_one.trans_le h), dist_edist, ENNReal.toReal_rpow,
        dist_eq_sum (zero_lt_one.trans_le h), â† ENNReal.toReal_sum fun i _ => A i]

attribute [local instance] PiLp.pseudoMetricAux

variable {p Î²} in
private theorem edist_apply_le_edist_aux (x y : PiLp p Î²) (i : Î¹) :
    edist (x i) (y i) â‰¤ edist x y := by
  rcases p.dichotomy with (rfl | h)
  Â· simpa only [edist_eq_iSup] using le_iSup (fun i => edist (x i) (y i)) i
  Â· have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancelâ‚€ 1 (zero_lt_one.trans_le h).ne'
    rw [edist_eq_sum (zero_lt_one.trans_le h)]
    calc
      edist (x i) (y i) = (edist (x i) (y i) ^ p.toReal) ^ (1 / p.toReal) := by
        simp [â† ENNReal.rpow_mul, cancel, -one_div]
      _ â‰¤ (âˆ‘ i, edist (x i) (y i) ^ p.toReal) ^ (1 / p.toReal) := by
        gcongr
        exact Finset.single_le_sum (fun i _ => (bot_le : (0 : â„â‰¥0âˆ) â‰¤ _)) (Finset.mem_univ i)

private lemma lipschitzWith_ofLp_aux : LipschitzWith 1 (@ofLp p (âˆ€ i, Î² i)) :=
  .of_edist_le fun x y => by
    simp_rw [edist_pi_def, Finset.sup_le_iff, Finset.mem_univ,
      forall_true_left, ofLp_apply]
    exact edist_apply_le_edist_aux _ _

private lemma antilipschitzWith_ofLp_aux :
    AntilipschitzWith ((Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal) (@ofLp p (âˆ€ i, Î² i)) := by
  intro x y
  rcases p.dichotomy with (rfl | h)
  Â· simp only [edist_eq_iSup, ENNReal.div_top, ENNReal.toReal_zero, NNReal.rpow_zero,
      ENNReal.coe_one, one_mul, iSup_le_iff]
    -- Porting note: `Finset.le_sup` needed some help
    exact fun i => Finset.le_sup (f := fun i => edist (x i) (y i)) (Finset.mem_univ i)
  Â· have pos : 0 < p.toReal := zero_lt_one.trans_le h
    have nonneg : 0 â‰¤ 1 / p.toReal := one_div_nonneg.2 (le_of_lt pos)
    have cancel : p.toReal * (1 / p.toReal) = 1 := mul_div_cancelâ‚€ 1 (ne_of_gt pos)
    rw [edist_eq_sum pos, ENNReal.toReal_div 1 p]
    simp only [edist, ENNReal.toReal_one]
    calc
      (âˆ‘ i, edist (x i) (y i) ^ p.toReal) ^ (1 / p.toReal) â‰¤
          (âˆ‘ _i, edist (ofLp x) (ofLp y) ^ p.toReal) ^ (1 / p.toReal) := by
        gcongr with i
        exact Finset.le_sup (f := fun i => edist (x i) (y i)) (Finset.mem_univ i)
      _ =
          ((Fintype.card Î¹ : â„â‰¥0) ^ (1 / p.toReal) : â„â‰¥0) *
            edist (ofLp x) (ofLp y) := by
        simp only [nsmul_eq_mul, Finset.card_univ, ENNReal.rpow_one, Finset.sum_const,
          ENNReal.mul_rpow_of_nonneg _ _ nonneg, â† ENNReal.rpow_mul, cancel]
        have : (Fintype.card Î¹ : â„â‰¥0âˆ) = (Fintype.card Î¹ : â„â‰¥0) :=
          (ENNReal.coe_natCast (Fintype.card Î¹)).symm
        rw [this, ENNReal.coe_rpow_of_nonneg _ nonneg]

private lemma isUniformInducing_ofLp_aux : IsUniformInducing (@ofLp p (âˆ€ i, Î² i)) :=
    (antilipschitzWith_ofLp_aux p Î²).isUniformInducing
      (lipschitzWith_ofLp_aux p Î²).uniformContinuous

private lemma uniformity_aux : ğ“¤ (PiLp p Î²) = ğ“¤[Pi.uniformSpace _] := by
  have : (fun x : PiLp p Î² Ã— PiLp p Î² => (ofLp x.fst, ofLp x.snd)) = id := rfl
  rw [â† (isUniformInducing_ofLp_aux p Î²).comap_uniformity, this, comap_id]

private lemma cobounded_aux : cobounded (PiLp p Î±) = @cobounded _ Pi.instBornology :=
  calc
    cobounded (PiLp p Î±) = comap ofLp (cobounded _) :=
      le_antisymm (antilipschitzWith_ofLp_aux p Î±).tendsto_cobounded.le_comap
        (lipschitzWith_ofLp_aux p Î±).comap_cobounded_le
    _ = _ := comap_id

end Aux

/-! ### Instances on finite `L^p` products -/

instance topologicalSpace [âˆ€ i, TopologicalSpace (Î² i)] : TopologicalSpace (PiLp p Î²) :=
  inferInstanceAs <| TopologicalSpace (Î  i, Î² i)

@[fun_prop, continuity]
theorem continuous_ofLp [âˆ€ i, TopologicalSpace (Î² i)] : Continuous (@ofLp p (âˆ€ i, Î² i)) :=
  continuous_id

@[fun_prop, continuity]
protected lemma continuous_apply [âˆ€ i, TopologicalSpace (Î² i)] (i : Î¹) :
    Continuous (fun f : PiLp p Î² â†¦ f i) := (continuous_apply i).comp (continuous_ofLp p Î²)

@[fun_prop, continuity]
theorem continuous_toLp [âˆ€ i, TopologicalSpace (Î² i)] : Continuous (@toLp p (âˆ€ i, Î² i)) :=
  continuous_id

/-- `WithLp.equiv` as a homeomorphism. -/
def homeomorph [âˆ€ i, TopologicalSpace (Î² i)] : PiLp p Î² â‰ƒâ‚œ (Î  i, Î² i) where
  toEquiv := WithLp.equiv p (Î  i, Î² i)
  continuous_toFun := continuous_ofLp p Î²
  continuous_invFun := continuous_toLp p Î²

@[simp]
lemma toEquiv_homeomorph [âˆ€ i, TopologicalSpace (Î² i)] :
    (homeomorph p Î²).toEquiv = WithLp.equiv p (Î  i, Î² i) := rfl

lemma isOpenMap_apply [âˆ€ i, TopologicalSpace (Î² i)] (i : Î¹) :
    IsOpenMap (fun f : PiLp p Î² â†¦ f i) := (isOpenMap_eval i).comp (homeomorph p Î²).symm.isOpenMap

instance instProdT0Space [âˆ€ i, TopologicalSpace (Î² i)] [âˆ€ i, T0Space (Î² i)] :
    T0Space (PiLp p Î²) :=
  (homeomorph p Î²).symm.t0Space

instance secondCountableTopology [Countable Î¹] [âˆ€ i, TopologicalSpace (Î² i)]
    [âˆ€ i, SecondCountableTopology (Î² i)] : SecondCountableTopology (PiLp p Î²) :=
  inferInstanceAs <| SecondCountableTopology (Î  i, Î² i)

instance uniformSpace [âˆ€ i, UniformSpace (Î² i)] : UniformSpace (PiLp p Î²) :=
  Pi.uniformSpace _

lemma uniformContinuous_ofLp [âˆ€ i, UniformSpace (Î² i)] :
    UniformContinuous (@ofLp p (âˆ€ i, Î² i)) :=
  uniformContinuous_id

lemma uniformContinuous_toLp [âˆ€ i, UniformSpace (Î² i)] :
    UniformContinuous (@toLp p (âˆ€ i, Î² i)) :=
  uniformContinuous_id

/-- `WithLp.equiv` as a uniform isomorphism. -/
def uniformEquiv [âˆ€ i, UniformSpace (Î² i)] : PiLp p Î² â‰ƒáµ¤ (Î  i, Î² i) where
  toEquiv := WithLp.equiv p (Î  i, Î² i)
  uniformContinuous_toFun := uniformContinuous_ofLp p Î²
  uniformContinuous_invFun := uniformContinuous_toLp p Î²

@[simp]
lemma toHomeomorph_uniformEquiv [âˆ€ i, UniformSpace (Î² i)] :
    (uniformEquiv p Î²).toHomeomorph = homeomorph p Î² := rfl

@[simp]
lemma toEquiv_uniformEquiv [âˆ€ i, UniformSpace (Î² i)] :
    (uniformEquiv p Î²).toEquiv = WithLp.equiv p (Î  i, Î² i) := rfl

instance completeSpace [âˆ€ i, UniformSpace (Î² i)] [âˆ€ i, CompleteSpace (Î² i)] :
    CompleteSpace (PiLp p Î²) :=
  inferInstanceAs <| CompleteSpace (Î  i, Î² i)

instance bornology [âˆ€ i, Bornology (Î² i)] : Bornology (PiLp p Î²) :=
  Pi.instBornology


section Fintype
variable [hp : Fact (1 â‰¤ p)]
variable [Fintype Î¹]

/-- pseudoemetric space instance on the product of finitely many pseudoemetric spaces, using the
`L^p` pseudoedistance, and having as uniformity the product uniformity. -/
instance [âˆ€ i, PseudoEMetricSpace (Î² i)] : PseudoEMetricSpace (PiLp p Î²) :=
  (pseudoEmetricAux p Î²).replaceUniformity (uniformity_aux p Î²).symm

/-- emetric space instance on the product of finitely many emetric spaces, using the `L^p`
edistance, and having as uniformity the product uniformity. -/
instance [âˆ€ i, EMetricSpace (Î± i)] : EMetricSpace (PiLp p Î±) :=
  @EMetricSpace.ofT0PseudoEMetricSpace (PiLp p Î±) _ Pi.instT0Space

/-- pseudometric space instance on the product of finitely many pseudometric spaces, using the
`L^p` distance, and having as uniformity the product uniformity. -/
instance [âˆ€ i, PseudoMetricSpace (Î² i)] : PseudoMetricSpace (PiLp p Î²) :=
  ((pseudoMetricAux p Î²).replaceUniformity (uniformity_aux p Î²).symm).replaceBornology fun s =>
    Filter.ext_iff.1 (cobounded_aux p Î²).symm sá¶œ

/-- metric space instance on the product of finitely many metric spaces, using the `L^p` distance,
and having as uniformity the product uniformity. -/
instance [âˆ€ i, MetricSpace (Î± i)] : MetricSpace (PiLp p Î±) :=
  MetricSpace.ofT0PseudoMetricSpace _

theorem nndist_eq_sum {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] {Î² : Î¹ â†’ Type*} [âˆ€ i, PseudoMetricSpace (Î² i)]
    (hp : p â‰  âˆ) (x y : PiLp p Î²) :
    nndist x y = (âˆ‘ i : Î¹, nndist (x i) (y i) ^ p.toReal) ^ (1 / p.toReal) :=
  NNReal.eq <| by
    push_cast
    exact dist_eq_sum (p.toReal_pos_iff_ne_top.mpr hp) _ _

theorem nndist_eq_iSup {Î² : Î¹ â†’ Type*} [âˆ€ i, PseudoMetricSpace (Î² i)] (x y : PiLp âˆ Î²) :
    nndist x y = â¨† i, nndist (x i) (y i) :=
  NNReal.eq <| by
    push_cast
    exact dist_eq_iSup _ _

section
variable {Î² p}

theorem edist_apply_le [âˆ€ i, PseudoEMetricSpace (Î² i)] (x y : PiLp p Î²) (i : Î¹) :
    edist (x i) (y i) â‰¤ edist x y :=
  edist_apply_le_edist_aux x y i

theorem nndist_apply_le [âˆ€ i, PseudoMetricSpace (Î² i)] (x y : PiLp p Î²) (i : Î¹) :
    nndist (x i) (y i) â‰¤ nndist x y := by
  simpa [â† coe_nnreal_ennreal_nndist] using edist_apply_le x y i

theorem dist_apply_le [âˆ€ i, PseudoMetricSpace (Î² i)] (x y : PiLp p Î²) (i : Î¹) :
    dist (x i) (y i) â‰¤ dist x y :=
  nndist_apply_le x y i

end

lemma lipschitzWith_ofLp [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    LipschitzWith 1 (@ofLp p (âˆ€ i, Î² i)) :=
  lipschitzWith_ofLp_aux p Î²

lemma antilipschitzWith_toLp [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    AntilipschitzWith 1 (@toLp p (âˆ€ i, Î² i)) :=
  (lipschitzWith_ofLp p Î²).to_rightInverse (ofLp_toLp p)

theorem antilipschitzWith_ofLp [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    AntilipschitzWith ((Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal) (@ofLp p (âˆ€ i, Î² i)) :=
  antilipschitzWith_ofLp_aux p Î²

lemma lipschitzWith_toLp [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    LipschitzWith ((Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal) (@toLp p (âˆ€ i, Î² i)) :=
  (antilipschitzWith_ofLp p Î²).to_rightInverse (ofLp_toLp p)

lemma isometry_ofLp_infty [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    Isometry (@ofLp âˆ (âˆ€ i, Î² i)) :=
  fun x y =>
  le_antisymm (by simpa only [ENNReal.coe_one, one_mul] using lipschitzWith_ofLp âˆ Î² x y)
    (by simpa only [ENNReal.div_top, ENNReal.toReal_zero, NNReal.rpow_zero, ENNReal.coe_one,
      one_mul] using antilipschitzWith_ofLp âˆ Î² x y)

/-- seminormed group instance on the product of finitely many normed groups, using the `L^p`
norm. -/
instance seminormedAddCommGroup [âˆ€ i, SeminormedAddCommGroup (Î² i)] :
    SeminormedAddCommGroup (PiLp p Î²) :=
  { Pi.addCommGroup with
    dist_eq := fun x y => by
      rcases p.dichotomy with (rfl | h)
      Â· simp only [dist_eq_iSup, norm_eq_ciSup, dist_eq_norm, sub_apply]
      Â· have : p â‰  âˆ := by
          intro hp
          rw [hp, ENNReal.toReal_top] at h
          linarith
        simp only [dist_eq_sum (zero_lt_one.trans_le h), norm_eq_sum (zero_lt_one.trans_le h),
          dist_eq_norm, sub_apply] }

lemma isUniformInducing_toLp [âˆ€ i, PseudoEMetricSpace (Î² i)] :
    IsUniformInducing (@toLp p (Î  i, Î² i)) :=
  (antilipschitzWith_toLp p Î²).isUniformInducing
    (lipschitzWith_toLp p Î²).uniformContinuous

section
variable {Î² p}

theorem enorm_apply_le [âˆ€ i, SeminormedAddCommGroup (Î² i)] (x : PiLp p Î²) (i : Î¹) :
    â€–x iâ€–â‚‘ â‰¤ â€–xâ€–â‚‘ := by
  simpa using edist_apply_le x 0 i

theorem nnnorm_apply_le [âˆ€ i, SeminormedAddCommGroup (Î² i)] (x : PiLp p Î²) (i : Î¹) :
    â€–x iâ€–â‚Š â‰¤ â€–xâ€–â‚Š := by
  simpa using nndist_apply_le x 0 i

theorem norm_apply_le [âˆ€ i, SeminormedAddCommGroup (Î² i)] (x : PiLp p Î²) (i : Î¹) :
    â€–x iâ€– â‰¤ â€–xâ€– := by
  simpa using dist_apply_le x 0 i

end

/-- normed group instance on the product of finitely many normed groups, using the `L^p` norm. -/
instance normedAddCommGroup [âˆ€ i, NormedAddCommGroup (Î± i)] : NormedAddCommGroup (PiLp p Î±) :=
  { PiLp.seminormedAddCommGroup p Î± with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

theorem nnnorm_eq_sum {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] {Î² : Î¹ â†’ Type*} (hp : p â‰  âˆ)
    [âˆ€ i, SeminormedAddCommGroup (Î² i)] (f : PiLp p Î²) :
    â€–fâ€–â‚Š = (âˆ‘ i, â€–f iâ€–â‚Š ^ p.toReal) ^ (1 / p.toReal) := by
  ext
  simp [NNReal.coe_sum, norm_eq_sum (p.toReal_pos_iff_ne_top.mpr hp)]

section Linfty
variable {Î²}
variable [âˆ€ i, SeminormedAddCommGroup (Î² i)]

theorem nnnorm_eq_ciSup (f : PiLp âˆ Î²) : â€–fâ€–â‚Š = â¨† i, â€–f iâ€–â‚Š := by
  ext
  simp [NNReal.coe_iSup, norm_eq_ciSup]

@[simp] lemma nnnorm_ofLp (f : PiLp âˆ Î²) : â€–ofLp fâ€–â‚Š = â€–fâ€–â‚Š := by
  rw [nnnorm_eq_ciSup, Pi.nnnorm_def, Finset.sup_univ_eq_ciSup]
  dsimp only [ofLp_apply]

@[simp] lemma nnnorm_toLp (f : âˆ€ i, Î² i) : â€–toLp âˆ fâ€–â‚Š = â€–fâ€–â‚Š := (nnnorm_ofLp _).symm

@[simp] lemma norm_ofLp (f : PiLp âˆ Î²) : â€–ofLp fâ€– = â€–fâ€– := congr_arg NNReal.toReal <| nnnorm_ofLp f
@[simp] lemma norm_toLp (f : âˆ€ i, Î² i) : â€–toLp âˆ fâ€– = â€–fâ€– := (norm_ofLp _).symm

end Linfty

theorem norm_eq_of_nat {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] {Î² : Î¹ â†’ Type*}
    [âˆ€ i, SeminormedAddCommGroup (Î² i)] (n : â„•) (h : p = n) (f : PiLp p Î²) :
    â€–fâ€– = (âˆ‘ i, â€–f iâ€– ^ n) ^ (1 / (n : â„)) := by
  have := p.toReal_pos_iff_ne_top.mpr (ne_of_eq_of_ne h <| ENNReal.natCast_ne_top n)
  simp only [one_div, h, Real.rpow_natCast, ENNReal.toReal_natCast,
    norm_eq_sum this]

section L1
variable {Î²} [âˆ€ i, SeminormedAddCommGroup (Î² i)]

theorem norm_eq_of_L1 (x : PiLp 1 Î²) : â€–xâ€– = âˆ‘ i : Î¹, â€–x iâ€– := by
  simp [norm_eq_sum]

theorem nnnorm_eq_of_L1 (x : PiLp 1 Î²) : â€–xâ€–â‚Š = âˆ‘ i : Î¹, â€–x iâ€–â‚Š :=
  NNReal.eq <| by push_cast; exact norm_eq_of_L1 x

theorem dist_eq_of_L1 (x y : PiLp 1 Î²) : dist x y = âˆ‘ i, dist (x i) (y i) := by
  simp_rw [dist_eq_norm, norm_eq_of_L1, sub_apply]

theorem nndist_eq_of_L1 (x y : PiLp 1 Î²) : nndist x y = âˆ‘ i, nndist (x i) (y i) :=
  NNReal.eq <| by push_cast; exact dist_eq_of_L1 _ _

theorem edist_eq_of_L1 (x y : PiLp 1 Î²) : edist x y = âˆ‘ i, edist (x i) (y i) := by
  simp [PiLp.edist_eq_sum]

end L1

section L2
variable {Î²} [âˆ€ i, SeminormedAddCommGroup (Î² i)]

theorem norm_eq_of_L2 (x : PiLp 2 Î²) :
    â€–xâ€– = âˆš(âˆ‘ i : Î¹, â€–x iâ€– ^ 2) := by
  rw [norm_eq_of_nat 2 (by norm_cast) _]
  rw [Real.sqrt_eq_rpow]
  norm_cast

theorem nnnorm_eq_of_L2 (x : PiLp 2 Î²) :
    â€–xâ€–â‚Š = NNReal.sqrt (âˆ‘ i : Î¹, â€–x iâ€–â‚Š ^ 2) :=
  NNReal.eq <| by
    push_cast
    exact norm_eq_of_L2 x

theorem norm_sq_eq_of_L2 (Î² : Î¹ â†’ Type*) [âˆ€ i, SeminormedAddCommGroup (Î² i)] (x : PiLp 2 Î²) :
    â€–xâ€– ^ 2 = âˆ‘ i : Î¹, â€–x iâ€– ^ 2 := by
  suffices â€–xâ€–â‚Š ^ 2 = âˆ‘ i : Î¹, â€–x iâ€–â‚Š ^ 2 by
    simpa only [NNReal.coe_sum] using congr_arg ((â†‘) : â„â‰¥0 â†’ â„) this
  rw [nnnorm_eq_of_L2, NNReal.sq_sqrt]

theorem dist_eq_of_L2 (x y : PiLp 2 Î²) :
    dist x y = âˆš(âˆ‘ i, dist (x i) (y i) ^ 2) := by
  simp_rw [dist_eq_norm, norm_eq_of_L2, sub_apply]

theorem dist_sq_eq_of_L2 (x y : PiLp 2 Î²) :
    dist x y ^ 2 = âˆ‘ i, dist (x i) (y i) ^ 2 := by
  simp_rw [dist_eq_norm, norm_sq_eq_of_L2, sub_apply]

theorem nndist_eq_of_L2 (x y : PiLp 2 Î²) :
    nndist x y = NNReal.sqrt (âˆ‘ i, nndist (x i) (y i) ^ 2) :=
  NNReal.eq <| by
    push_cast
    exact dist_eq_of_L2 _ _

theorem edist_eq_of_L2 (x y : PiLp 2 Î²) :
    edist x y = (âˆ‘ i, edist (x i) (y i) ^ 2) ^ (1 / 2 : â„) := by simp [PiLp.edist_eq_sum]

end L2

instance instIsBoundedSMul [SeminormedRing ğ•œ] [âˆ€ i, SeminormedAddCommGroup (Î² i)]
    [âˆ€ i, Module ğ•œ (Î² i)] [âˆ€ i, IsBoundedSMul ğ•œ (Î² i)] :
    IsBoundedSMul ğ•œ (PiLp p Î²) :=
  .of_nnnorm_smul_le fun c f => by
    rcases p.dichotomy with (rfl | hp)
    Â· rw [â† nnnorm_ofLp, â† nnnorm_ofLp, ofLp_smul]
      exact nnnorm_smul_le c (ofLp f)
    Â· have hp0 : 0 < p.toReal := zero_lt_one.trans_le hp
      have hpt : p â‰  âŠ¤ := p.toReal_pos_iff_ne_top.mp hp0
      rw [nnnorm_eq_sum hpt, nnnorm_eq_sum hpt, one_div, NNReal.rpow_inv_le_iff hp0,
        NNReal.mul_rpow, â† NNReal.rpow_mul, inv_mul_cancelâ‚€ hp0.ne', NNReal.rpow_one,
        Finset.mul_sum]
      simp_rw [â† NNReal.mul_rpow, smul_apply]
      gcongr
      apply nnnorm_smul_le

instance instNormSMulClass [SeminormedRing ğ•œ] [âˆ€ i, SeminormedAddCommGroup (Î² i)]
    [âˆ€ i, Module ğ•œ (Î² i)] [âˆ€ i, NormSMulClass ğ•œ (Î² i)] :
    NormSMulClass ğ•œ (PiLp p Î²) :=
  .of_nnnorm_smul fun c f => by
    rcases p.dichotomy with (rfl | hp)
    Â· rw [â† nnnorm_ofLp, â† nnnorm_ofLp, WithLp.ofLp_smul, nnnorm_smul]
    Â· have hp0 : 0 < p.toReal := zero_lt_one.trans_le hp
      have hpt : p â‰  âŠ¤ := p.toReal_pos_iff_ne_top.mp hp0
      rw [nnnorm_eq_sum hpt, nnnorm_eq_sum hpt, one_div, NNReal.rpow_inv_eq_iff hp0.ne',
        NNReal.mul_rpow, â† NNReal.rpow_mul, inv_mul_cancelâ‚€ hp0.ne', NNReal.rpow_one,
        Finset.mul_sum]
      simp_rw [â† NNReal.mul_rpow, smul_apply, nnnorm_smul]

/-- The product of finitely many normed spaces is a normed space, with the `L^p` norm. -/
instance normedSpace [NormedField ğ•œ] [âˆ€ i, SeminormedAddCommGroup (Î² i)]
    [âˆ€ i, NormedSpace ğ•œ (Î² i)] : NormedSpace ğ•œ (PiLp p Î²) where
  norm_smul_le := norm_smul_le

variable {ğ•œ p Î±}
variable [Semiring ğ•œ] [âˆ€ i, SeminormedAddCommGroup (Î± i)] [âˆ€ i, SeminormedAddCommGroup (Î² i)]
variable [âˆ€ i, Module ğ•œ (Î± i)] [âˆ€ i, Module ğ•œ (Î² i)] (c : ğ•œ)

/-- The canonical map `WithLp.equiv` between `PiLp âˆ Î²` and `Î  i, Î² i` as a linear isometric
equivalence. -/
def equivâ‚—áµ¢ : PiLp âˆ Î² â‰ƒâ‚—áµ¢[ğ•œ] (âˆ€ i, Î² i) where
  __ := WithLp.linearEquiv p ğ•œ _
  norm_map' := norm_ofLp

section piLpCongrLeft
variable {Î¹' : Type*}
variable [Fintype Î¹']
variable (p ğ•œ)
variable (E : Type*) [SeminormedAddCommGroup E] [Module ğ•œ E]

/-- An equivalence of finite domains induces a linearly isometric equivalence of finitely supported
functions -/
def _root_.LinearIsometryEquiv.piLpCongrLeft (e : Î¹ â‰ƒ Î¹') :
    (PiLp p fun _ : Î¹ => E) â‰ƒâ‚—áµ¢[ğ•œ] PiLp p fun _ : Î¹' => E where
  toLinearEquiv := LinearEquiv.piCongrLeft' ğ•œ (fun _ : Î¹ => E) e
  norm_map' x' := by
    rcases p.dichotomy with (rfl | h)
    Â· simp_rw [norm_eq_ciSup]
      exact e.symm.iSup_congr fun _ => rfl
    Â· simp only [norm_eq_sum (zero_lt_one.trans_le h)]
      congr 1
      exact Fintype.sum_equiv e.symm _ _ fun _ => rfl

variable {p ğ•œ E}

@[simp]
theorem _root_.LinearIsometryEquiv.piLpCongrLeft_apply (e : Î¹ â‰ƒ Î¹') (v : PiLp p fun _ : Î¹ => E) :
    LinearIsometryEquiv.piLpCongrLeft p ğ•œ E e v = Equiv.piCongrLeft' (fun _ : Î¹ => E) e v :=
  rfl

@[simp]
theorem _root_.LinearIsometryEquiv.piLpCongrLeft_symm (e : Î¹ â‰ƒ Î¹') :
    (LinearIsometryEquiv.piLpCongrLeft p ğ•œ E e).symm =
      LinearIsometryEquiv.piLpCongrLeft p ğ•œ E e.symm :=
  LinearIsometryEquiv.ext fun z â†¦
    congr_arg (Equiv.toFun Â· z) (Equiv.piCongrLeft'_symm _ _)

@[simp high]
theorem _root_.LinearIsometryEquiv.piLpCongrLeft_single [DecidableEq Î¹] [DecidableEq Î¹']
    (e : Î¹ â‰ƒ Î¹') (i : Î¹) (v : E) :
    LinearIsometryEquiv.piLpCongrLeft p ğ•œ E e (toLp p <| Pi.single i v) =
      toLp p (Pi.single (e i) v) := by
  funext x
  simp [LinearIsometryEquiv.piLpCongrLeft_apply, Equiv.piCongrLeft',
    Pi.single, Function.update, Equiv.symm_apply_eq]

end piLpCongrLeft

section piLpCongrRight
variable {Î²}

variable (p) in
/-- A family of linearly isometric equivalences in the codomain induces an isometric equivalence
between Pi types with the Lp norm.

This is the isometry version of `LinearEquiv.piCongrRight`. -/
protected def _root_.LinearIsometryEquiv.piLpCongrRight (e : âˆ€ i, Î± i â‰ƒâ‚—áµ¢[ğ•œ] Î² i) :
    PiLp p Î± â‰ƒâ‚—áµ¢[ğ•œ] PiLp p Î² where
  toLinearEquiv :=
    WithLp.linearEquiv _ _ _
      â‰ªâ‰«â‚— (LinearEquiv.piCongrRight fun i => (e i).toLinearEquiv)
      â‰ªâ‰«â‚— (WithLp.linearEquiv _ _ _).symm
  norm_map' := (WithLp.linearEquiv p ğ•œ _).symm.surjective.forall.2 fun x => by
    simp only [LinearEquiv.trans_apply, WithLp.linearEquiv_symm_apply, WithLp.linearEquiv_apply]
    obtain rfl | hp := p.dichotomy
    Â· simp_rw [PiLp.norm_toLp, Pi.norm_def, LinearEquiv.piCongrRight_apply,
        LinearIsometryEquiv.coe_toLinearEquiv, LinearIsometryEquiv.nnnorm_map,
        ofLp_apply, toLp_apply]
    Â· have : 0 < p.toReal := zero_lt_one.trans_le <| by norm_cast
      simp only [PiLp.norm_eq_sum this, toLp_apply, LinearEquiv.piCongrRight_apply,
        LinearIsometryEquiv.coe_toLinearEquiv, LinearIsometryEquiv.norm_map,
        ofLp_apply, toLp_apply]

@[simp]
theorem _root_.LinearIsometryEquiv.piLpCongrRight_apply (e : âˆ€ i, Î± i â‰ƒâ‚—áµ¢[ğ•œ] Î² i) (x : PiLp p Î±) :
    LinearIsometryEquiv.piLpCongrRight p e x = toLp p fun i => e i (x i) := rfl

@[simp]
theorem _root_.LinearIsometryEquiv.piLpCongrRight_refl :
    LinearIsometryEquiv.piLpCongrRight p (fun i => .refl ğ•œ (Î± i)) = .refl _ _ :=
  rfl

@[simp]
theorem _root_.LinearIsometryEquiv.piLpCongrRight_symm (e : âˆ€ i, Î± i â‰ƒâ‚—áµ¢[ğ•œ] Î² i) :
    (LinearIsometryEquiv.piLpCongrRight p e).symm =
      LinearIsometryEquiv.piLpCongrRight p (fun i => (e i).symm) :=
  rfl

@[simp high]
theorem _root_.LinearIsometryEquiv.piLpCongrRight_single (e : âˆ€ i, Î± i â‰ƒâ‚—áµ¢[ğ•œ] Î² i) [DecidableEq Î¹]
    (i : Î¹) (v : Î± i) :
    LinearIsometryEquiv.piLpCongrRight p e (toLp p <| Pi.single i v) =
      toLp p (Pi.single i (e _ v)) :=
  funext <| Pi.apply_single (e Â·) (fun _ => map_zero _) _ _

end piLpCongrRight

section piLpCurry

variable {Î¹ : Type*} {Îº : Î¹ â†’ Type*} (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)]
  [Fintype Î¹] [âˆ€ i, Fintype (Îº i)]
  (Î± : âˆ€ i, Îº i â†’ Type*) [âˆ€ i k, SeminormedAddCommGroup (Î± i k)] [âˆ€ i k, Module ğ•œ (Î± i k)]

variable (ğ•œ) in
/-- `LinearEquiv.piCurry` for `PiLp`, as an isometry. -/
def _root_.LinearIsometryEquiv.piLpCurry :
    PiLp p (fun i : Sigma _ => Î± i.1 i.2) â‰ƒâ‚—áµ¢[ğ•œ] PiLp p (fun i => PiLp p (Î± i)) where
  toLinearEquiv :=
    WithLp.linearEquiv _ _ _
      â‰ªâ‰«â‚— LinearEquiv.piCurry ğ•œ Î±
      â‰ªâ‰«â‚— (LinearEquiv.piCongrRight fun _ => (WithLp.linearEquiv _ _ _).symm)
      â‰ªâ‰«â‚— (WithLp.linearEquiv _ _ _).symm
  norm_map' := (WithLp.linearEquiv p ğ•œ _).symm.surjective.forall.2 fun x => by
    simp_rw [â† coe_nnnorm, NNReal.coe_inj]
    dsimp only [WithLp.linearEquiv_symm_apply]
    obtain rfl | hp := eq_or_ne p âŠ¤
    Â· simp_rw [â† PiLp.nnnorm_ofLp, Pi.nnnorm_def, â† PiLp.nnnorm_ofLp, Pi.nnnorm_def]
      dsimp [Sigma.curry]
      rw [â† Finset.univ_sigma_univ, Finset.sup_sigma]
    Â· have : 0 < p.toReal := (toReal_pos_iff_ne_top _).mpr hp
      simp_rw [PiLp.nnnorm_eq_sum hp, toLp_apply]
      dsimp [Sigma.curry]
      simp_rw [one_div, NNReal.rpow_inv_rpow this.ne', â† Finset.univ_sigma_univ, Finset.sum_sigma]

@[simp] theorem _root_.LinearIsometryEquiv.piLpCurry_apply
    (f : PiLp p (fun i : Sigma Îº => Î± i.1 i.2)) :
    _root_.LinearIsometryEquiv.piLpCurry ğ•œ p Î± f =
      toLp p (fun i => (toLp p) <| Sigma.curry (ofLp f) i) :=
  rfl

@[simp] theorem _root_.LinearIsometryEquiv.piLpCurry_symm_apply
    (f : PiLp p (fun i => PiLp p (Î± i))) :
    (_root_.LinearIsometryEquiv.piLpCurry ğ•œ p Î±).symm f =
      toLp p (Sigma.uncurry fun i j => f i j) :=
  rfl

end piLpCurry

section sumPiLpEquivProdLpPiLp

variable {Î¹ Îº : Type*} (p : â„â‰¥0âˆ) (Î± : Î¹ âŠ• Îº â†’ Type*) [Fintype Î¹] [Fintype Îº] [Fact (1 â‰¤ p)]
variable [âˆ€ i, SeminormedAddCommGroup (Î± i)] [âˆ€ i, Module ğ•œ (Î± i)]

/-- `LinearEquiv.sumPiEquivProdPi` for `PiLp`, as an isometry. -/
@[simps! +simpRhs]
def sumPiLpEquivProdLpPiLp :
    WithLp p (Î  i, Î± i) â‰ƒâ‚—áµ¢[ğ•œ]
      WithLp p (WithLp p (Î  i, Î± (.inl i)) Ã— WithLp p (Î  i, Î± (.inr i))) where
  toLinearEquiv :=
    WithLp.linearEquiv p _ _
      â‰ªâ‰«â‚— LinearEquiv.sumPiEquivProdPi _ _ _ Î±
      â‰ªâ‰«â‚— LinearEquiv.prodCongr (WithLp.linearEquiv p _ _).symm
        (WithLp.linearEquiv _ _ _).symm
      â‰ªâ‰«â‚— (WithLp.linearEquiv  p _ _).symm
  norm_map' := (WithLp.linearEquiv p ğ•œ _).symm.surjective.forall.2 fun x => by
    obtain rfl | hp := p.dichotomy
    Â· simp [â† Finset.univ_disjSum_univ, Finset.sup_disjSum, Pi.norm_def]
    Â· have : 0 < p.toReal := by positivity
      have hpt : p â‰  âŠ¤ := (toReal_pos_iff_ne_top p).mp this
      simp_rw [â† coe_nnnorm]; congr 1 -- convert to nnnorm to avoid needing positivity arguments
      simp [nnnorm_eq_sum hpt, WithLp.prod_nnnorm_eq_add hpt, NNReal.rpow_inv_rpow this.ne']

end sumPiLpEquivProdLpPiLp

section Single

variable (p)
variable [DecidableEq Î¹]

@[simp]
theorem nnnorm_toLp_single (i : Î¹) (b : Î² i) :
    â€–toLp p (Pi.single i b)â€–â‚Š = â€–bâ€–â‚Š := by
  haveI : Nonempty Î¹ := âŸ¨iâŸ©
  induction p generalizing hp with
  | top =>
    simp_rw [nnnorm_eq_ciSup, toLp_apply]
    refine
      ciSup_eq_of_forall_le_of_forall_lt_exists_gt (fun j => ?_) fun n hn => âŸ¨i, hn.trans_eq ?_âŸ©
    Â· obtain rfl | hij := Decidable.eq_or_ne i j
      Â· rw [Pi.single_eq_same]
      Â· rw [Pi.single_eq_of_ne' hij, nnnorm_zero]
        exact zero_le _
    Â· rw [Pi.single_eq_same]
  | coe p =>
    have hp0 : (p : â„) â‰  0 :=
      mod_cast (zero_lt_one.trans_le <| Fact.out (p := 1 â‰¤ (p : â„â‰¥0âˆ))).ne'
    rw [nnnorm_eq_sum ENNReal.coe_ne_top, ENNReal.coe_toReal, Fintype.sum_eq_single i,
      toLp_apply, Pi.single_eq_same, â† NNReal.rpow_mul, one_div,
      mul_inv_cancelâ‚€ hp0, NNReal.rpow_one]
    intro j hij
    rw [toLp_apply, Pi.single_eq_of_ne hij, nnnorm_zero, NNReal.zero_rpow hp0]

@[simp]
lemma norm_toLp_single (i : Î¹) (b : Î² i) : â€–toLp p (Pi.single i b)â€– = â€–bâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_toLp_single p Î² i b

@[simp]
lemma nndist_toLp_single_same (i : Î¹) (bâ‚ bâ‚‚ : Î² i) :
    nndist (toLp p (Pi.single i bâ‚)) (toLp p (Pi.single i bâ‚‚)) = nndist bâ‚ bâ‚‚ := by
  rw [nndist_eq_nnnorm, nndist_eq_nnnorm, â† toLp_sub, â† Pi.single_sub, nnnorm_toLp_single]

@[simp]
lemma dist_toLp_single_same (i : Î¹) (bâ‚ bâ‚‚ : Î² i) :
    dist (toLp p (Pi.single i bâ‚)) (toLp p (Pi.single i bâ‚‚)) = dist bâ‚ bâ‚‚ :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nndist_toLp_single_same p Î² i bâ‚ bâ‚‚

@[simp]
lemma edist_toLp_single_same (i : Î¹) (bâ‚ bâ‚‚ : Î² i) :
    edist (toLp p (Pi.single i bâ‚)) (toLp p (Pi.single i bâ‚‚)) = edist bâ‚ bâ‚‚ := by
  simp only [edist_nndist, nndist_toLp_single_same p Î² i bâ‚ bâ‚‚]

end Single

/-- When `p = âˆ`, this lemma does not hold without the additional assumption `Nonempty Î¹` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `â€–bâ€–â‚Š`. See
`PiLp.nnnorm_equiv_symm_const'` for a version which exchanges the hypothesis `p â‰  âˆ` for
`Nonempty Î¹`. -/
lemma nnnorm_toLp_const {Î²} [SeminormedAddCommGroup Î²] (hp : p â‰  âˆ) (b : Î²) :
    â€–toLp p (Function.const Î¹ b)â€–â‚Š =
      (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–bâ€–â‚Š := by
  rcases p.dichotomy with (h | h)
  Â· exact False.elim (hp h)
  Â· have ne_zero : p.toReal â‰  0 := (zero_lt_one.trans_le h).ne'
    simp_rw [nnnorm_eq_sum hp, toLp_apply, Function.const_apply, Finset.sum_const,
      Finset.card_univ, nsmul_eq_mul, NNReal.mul_rpow, â† NNReal.rpow_mul,
      mul_one_div_cancel ne_zero, NNReal.rpow_one, ENNReal.toReal_div, ENNReal.toReal_one]

/-- When `IsEmpty Î¹`, this lemma does not hold without the additional assumption `p â‰  âˆ` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `â€–bâ€–â‚Š`. See
`PiLp.nnnorm_toLp_const` for a version which exchanges the hypothesis `Nonempty Î¹`.
for `p â‰  âˆ`. -/
lemma nnnorm_toLp_const' {Î²} [SeminormedAddCommGroup Î²] [Nonempty Î¹] (b : Î²) :
    â€–toLp p (Function.const Î¹ b)â€–â‚Š =
      (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–bâ€–â‚Š := by
  rcases em <| p = âˆ with (rfl | hp)
  Â· simp only [toLp_apply, ENNReal.div_top, ENNReal.toReal_zero, NNReal.rpow_zero,
      one_mul, nnnorm_eq_ciSup, Function.const_apply, ciSup_const]
  Â· exact nnnorm_toLp_const hp b

/-- When `p = âˆ`, this lemma does not hold without the additional assumption `Nonempty Î¹` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `â€–bâ€–â‚Š`. See
`PiLp.norm_toLp_const'` for a version which exchanges the hypothesis `p â‰  âˆ` for
`Nonempty Î¹`. -/
lemma norm_toLp_const {Î²} [SeminormedAddCommGroup Î²] (hp : p â‰  âˆ) (b : Î²) :
    â€–toLp p (Function.const Î¹ b)â€– =
      (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–bâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_toLp_const hp b).trans <| by simp

/-- When `IsEmpty Î¹`, this lemma does not hold without the additional assumption `p â‰  âˆ` because
the left-hand side simplifies to `0`, while the right-hand side simplifies to `â€–bâ€–â‚Š`. See
`PiLp.norm_equiv_symm_const` for a version which exchanges the hypothesis `Nonempty Î¹`.
for `p â‰  âˆ`. -/
lemma norm_toLp_const' {Î²} [SeminormedAddCommGroup Î²] [Nonempty Î¹] (b : Î²) :
    â€–toLp p (Function.const Î¹ b)â€– =
      (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–bâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_toLp_const' b).trans <| by simp

lemma nnnorm_toLp_one {Î²} [SeminormedAddCommGroup Î²] (hp : p â‰  âˆ) [One Î²] :
    â€–toLp p (1 : Î¹ â†’ Î²)â€–â‚Š = (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–(1 : Î²)â€–â‚Š :=
  (nnnorm_toLp_const hp (1 : Î²)).trans rfl

lemma norm_toLp_one {Î²} [SeminormedAddCommGroup Î²] (hp : p â‰  âˆ) [One Î²] :
    â€–toLp p (1 : Î¹ â†’ Î²)â€– = (Fintype.card Î¹ : â„â‰¥0) ^ (1 / p).toReal * â€–(1 : Î²)â€– :=
  (norm_toLp_const hp (1 : Î²)).trans rfl

variable (ğ•œ p)

/-- `WithLp.linearEquiv` as a continuous linear equivalence. -/
@[simps! -fullyApplied apply symm_apply]
def continuousLinearEquiv : PiLp p Î² â‰ƒL[ğ•œ] âˆ€ i, Î² i where
  toLinearEquiv := WithLp.linearEquiv _ _ _
  continuous_toFun := continuous_ofLp _ _
  continuous_invFun := continuous_toLp p _

variable {ğ•œ} in
/-- The projection on the `i`-th coordinate of `PiLp p Î²`, as a continuous linear map. -/
@[simps!]
def proj (i : Î¹) : PiLp p Î² â†’L[ğ•œ] Î² i where
  __ := projâ‚— p Î² i
  cont := continuous_apply ..

end Fintype

section Basis

variable [Finite Î¹] [Ring ğ•œ]
variable (Î¹)

/-- A version of `Pi.basisFun` for `PiLp`. -/
def basisFun : Basis Î¹ ğ•œ (PiLp p fun _ : Î¹ => ğ•œ) :=
  Basis.ofEquivFun (WithLp.linearEquiv p ğ•œ (Î¹ â†’ ğ•œ))

@[simp]
theorem basisFun_apply [DecidableEq Î¹] (i) :
    basisFun p ğ•œ Î¹ i = toLp p (Pi.single i 1) := by
  simp_rw [basisFun, Basis.coe_ofEquivFun, WithLp.linearEquiv_symm_apply]

@[simp]
theorem basisFun_repr (x : PiLp p fun _ : Î¹ => ğ•œ) (i : Î¹) : (basisFun p ğ•œ Î¹).repr x i = x i :=
  rfl

@[simp]
theorem basisFun_equivFun : (basisFun p ğ•œ Î¹).equivFun = WithLp.linearEquiv p ğ•œ (Î¹ â†’ ğ•œ) :=
  Basis.equivFun_ofEquivFun _

theorem basisFun_eq_pi_basisFun :
    basisFun p ğ•œ Î¹ = (Pi.basisFun ğ•œ Î¹).map (WithLp.linearEquiv p ğ•œ (Î¹ â†’ ğ•œ)).symm :=
  rfl

@[simp]
theorem basisFun_map :
    (basisFun p ğ•œ Î¹).map (WithLp.linearEquiv p ğ•œ (Î¹ â†’ ğ•œ)) = Pi.basisFun ğ•œ Î¹ := rfl

end Basis

open Matrix

nonrec theorem basis_toMatrix_basisFun_mul [Fintype Î¹]
    {ğ•œ} [SeminormedCommRing ğ•œ] (b : Basis Î¹ ğ•œ (PiLp p fun _ : Î¹ => ğ•œ))
    (A : Matrix Î¹ Î¹ ğ•œ) :
    b.toMatrix (PiLp.basisFun _ _ _) * A =
      Matrix.of fun i j => b.repr (toLp p (Aáµ€ j)) i := by
  have := basis_toMatrix_basisFun_mul (b.map (WithLp.linearEquiv _ ğ•œ _)) A
  simp_rw [â† PiLp.basisFun_map p, Basis.map_repr, LinearEquiv.trans_apply,
    WithLp.linearEquiv_symm_apply, Basis.toMatrix_map, Function.comp_def, Basis.map_apply,
    LinearEquiv.symm_apply_apply] at this
  exact this

end PiLp
