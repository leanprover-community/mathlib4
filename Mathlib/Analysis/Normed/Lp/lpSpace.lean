/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.MeanInequalities
import Mathlib.Analysis.MeanInequalitiesPow
import Mathlib.Analysis.SpecialFunctions.Pow.Continuity
import Mathlib.Data.Set.Image
import Mathlib.Topology.Algebra.ContinuousMonoidHom

/-!
# â„“p space

This file describes properties of elements `f` of a pi-type `âˆ€ i, E i` with finite "norm",
defined for `p : â„â‰¥0âˆ` as the size of the support of `f` if `p=0`, `(âˆ‘' a, â€–f aâ€–^p) ^ (1/p)` for
`0 < p < âˆ` and `â¨† a, â€–f aâ€–` for `p=âˆ`.

The Prop-valued `Memâ„“p f p` states that a function `f : âˆ€ i, E i` has finite norm according
to the above definition; that is, `f` has finite support if `p = 0`, `Summable (fun a â†¦ â€–f aâ€–^p)` if
`0 < p < âˆ`, and `BddAbove (norm '' (Set.range f))` if `p = âˆ`.

The space `lp E p` is the subtype of elements of `âˆ€ i : Î±, E i` which satisfy `Memâ„“p f p`. For
`1 â‰¤ p`, the "norm" is genuinely a norm and `lp` is a complete metric space.

## Main definitions

* `Memâ„“p f p` : property that the function `f` satisfies, as appropriate, `f` finitely supported
  if `p = 0`, `Summable (fun a â†¦ â€–f aâ€–^p)` if `0 < p < âˆ`, and `BddAbove (norm '' (Set.range f))` if
  `p = âˆ`.
* `lp E p` : elements of `âˆ€ i : Î±, E i` such that `Memâ„“p f p`. Defined as an `AddSubgroup` of
  a type synonym `PreLp` for `âˆ€ i : Î±, E i`, and equipped with a `NormedAddCommGroup` structure.
  Under appropriate conditions, this is also equipped with the instances `lp.normedSpace`,
  `lp.completeSpace`. For `p=âˆ`, there is also `lp.inftyNormedRing`,
  `lp.inftyNormedAlgebra`, `lp.inftyStarRing` and `lp.inftyCStarRing`.

## Main results

* `Memâ„“p.of_exponent_ge`: For `q â‰¤ p`, a function which is `Memâ„“p` for `q` is also `Memâ„“p` for `p`.
* `lp.memâ„“p_of_tendsto`, `lp.norm_le_of_tendsto`: A pointwise limit of functions in `lp`, all with
  `lp` norm `â‰¤ C`, is itself in `lp` and has `lp` norm `â‰¤ C`.
* `lp.tsum_mul_le_mul_norm`: basic form of HÃ¶lder's inequality

## Implementation

Since `lp` is defined as an `AddSubgroup`, dot notation does not work. Use `lp.norm_neg f` to
say that `â€–-fâ€– = â€–fâ€–`, instead of the non-working `f.norm_neg`.

## TODO

* More versions of HÃ¶lder's inequality (for example: the case `p = 1`, `q = âˆ`; a version for normed
  rings which has `â€–âˆ‘' i, f i * g iâ€–` rather than `âˆ‘' i, â€–f iâ€– * g iâ€–` on the RHS; a version for
  three exponents satisfying `1 / r = 1 / p + 1 / q`)

-/

noncomputable section

open scoped NNReal ENNReal Function

variable {ğ•œ ğ•œ' : Type*} {Î± : Type*} {E : Î± â†’ Type*} {p q : â„â‰¥0âˆ} [âˆ€ i, NormedAddCommGroup (E i)]

/-!
### `Memâ„“p` predicate

-/


/-- The property that `f : âˆ€ i : Î±, E i`
* is finitely supported, if `p = 0`, or
* admits an upper bound for `Set.range (fun i â†¦ â€–f iâ€–)`, if `p = âˆ`, or
* has the series `âˆ‘' i, â€–f iâ€– ^ p` be summable, if `0 < p < âˆ`. -/
def Memâ„“p (f : âˆ€ i, E i) (p : â„â‰¥0âˆ) : Prop :=
  if p = 0 then Set.Finite { i | f i â‰  0 }
  else if p = âˆ then BddAbove (Set.range fun i => â€–f iâ€–)
  else Summable fun i => â€–f iâ€– ^ p.toReal

theorem memâ„“p_zero_iff {f : âˆ€ i, E i} : Memâ„“p f 0 â†” Set.Finite { i | f i â‰  0 } := by
  dsimp [Memâ„“p]
  rw [if_pos rfl]

theorem memâ„“p_zero {f : âˆ€ i, E i} (hf : Set.Finite { i | f i â‰  0 }) : Memâ„“p f 0 :=
  memâ„“p_zero_iff.2 hf

theorem memâ„“p_infty_iff {f : âˆ€ i, E i} : Memâ„“p f âˆ â†” BddAbove (Set.range fun i => â€–f iâ€–) := by
  simp [Memâ„“p]

theorem memâ„“p_infty {f : âˆ€ i, E i} (hf : BddAbove (Set.range fun i => â€–f iâ€–)) : Memâ„“p f âˆ :=
  memâ„“p_infty_iff.2 hf

theorem memâ„“p_gen_iff (hp : 0 < p.toReal) {f : âˆ€ i, E i} :
    Memâ„“p f p â†” Summable fun i => â€–f iâ€– ^ p.toReal := by
  rw [ENNReal.toReal_pos_iff] at hp
  dsimp [Memâ„“p]
  rw [if_neg hp.1.ne', if_neg hp.2.ne]

theorem memâ„“p_gen {f : âˆ€ i, E i} (hf : Summable fun i => â€–f iâ€– ^ p.toReal) : Memâ„“p f p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    have H : Summable fun _ : Î± => (1 : â„) := by simpa using hf
    exact (Set.Finite.of_summable_const (by simp) H).subset (Set.subset_univ _)
  Â· apply memâ„“p_infty
    have H : Summable fun _ : Î± => (1 : â„) := by simpa using hf
    simpa using ((Set.Finite.of_summable_const (by simp) H).image fun i => â€–f iâ€–).bddAbove
  exact (memâ„“p_gen_iff hp).2 hf

theorem memâ„“p_gen' {C : â„} {f : âˆ€ i, E i} (hf : âˆ€ s : Finset Î±, âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal â‰¤ C) :
    Memâ„“p f p := by
  apply memâ„“p_gen
  use â¨† s : Finset Î±, âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal
  apply hasSum_of_isLUB_of_nonneg
  Â· intro b
    exact Real.rpow_nonneg (norm_nonneg _) _
  apply isLUB_ciSup
  use C
  rintro - âŸ¨s, rflâŸ©
  exact hf s

theorem zero_memâ„“p : Memâ„“p (0 : âˆ€ i, E i) p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    simp
  Â· apply memâ„“p_infty
    simp only [norm_zero, Pi.zero_apply]
    exact bddAbove_singleton.mono Set.range_const_subset
  Â· apply memâ„“p_gen
    simp [Real.zero_rpow hp.ne', summable_zero]

theorem zero_mem_â„“p' : Memâ„“p (fun i : Î± => (0 : E i)) p :=
  zero_memâ„“p

namespace Memâ„“p

theorem finite_dsupport {f : âˆ€ i, E i} (hf : Memâ„“p f 0) : Set.Finite { i | f i â‰  0 } :=
  memâ„“p_zero_iff.1 hf

theorem bddAbove {f : âˆ€ i, E i} (hf : Memâ„“p f âˆ) : BddAbove (Set.range fun i => â€–f iâ€–) :=
  memâ„“p_infty_iff.1 hf

theorem summable (hp : 0 < p.toReal) {f : âˆ€ i, E i} (hf : Memâ„“p f p) :
    Summable fun i => â€–f iâ€– ^ p.toReal :=
  (memâ„“p_gen_iff hp).1 hf

theorem neg {f : âˆ€ i, E i} (hf : Memâ„“p f p) : Memâ„“p (-f) p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    simp [hf.finite_dsupport]
  Â· apply memâ„“p_infty
    simpa using hf.bddAbove
  Â· apply memâ„“p_gen
    simpa using hf.summable hp

@[simp]
theorem neg_iff {f : âˆ€ i, E i} : Memâ„“p (-f) p â†” Memâ„“p f p :=
  âŸ¨fun h => neg_neg f â–¸ h.neg, Memâ„“p.negâŸ©

theorem of_exponent_ge {p q : â„â‰¥0âˆ} {f : âˆ€ i, E i} (hfq : Memâ„“p f q) (hpq : q â‰¤ p) : Memâ„“p f p := by
  rcases ENNReal.trichotomyâ‚‚ hpq with
    (âŸ¨rfl, rflâŸ© | âŸ¨rfl, rflâŸ© | âŸ¨rfl, hpâŸ© | âŸ¨rfl, rflâŸ© | âŸ¨hq, rflâŸ© | âŸ¨hq, _, hpq'âŸ©)
  Â· exact hfq
  Â· apply memâ„“p_infty
    obtain âŸ¨C, hCâŸ© := (hfq.finite_dsupport.image fun i => â€–f iâ€–).bddAbove
    use max 0 C
    rintro x âŸ¨i, rflâŸ©
    by_cases hi : f i = 0
    Â· simp [hi]
    Â· exact (hC âŸ¨i, hi, rflâŸ©).trans (le_max_right _ _)
  Â· apply memâ„“p_gen
    have : âˆ€ i âˆ‰ hfq.finite_dsupport.toFinset, â€–f iâ€– ^ p.toReal = 0 := by
      intro i hi
      have : f i = 0 := by simpa using hi
      simp [this, Real.zero_rpow hp.ne']
    exact summable_of_ne_finset_zero this
  Â· exact hfq
  Â· apply memâ„“p_infty
    obtain âŸ¨A, hAâŸ© := (hfq.summable hq).tendsto_cofinite_zero.bddAbove_range_of_cofinite
    use A ^ q.toRealâ»Â¹
    rintro x âŸ¨i, rflâŸ©
    have : 0 â‰¤ â€–f iâ€– ^ q.toReal := by positivity
    simpa [â† Real.rpow_mul, mul_inv_cancelâ‚€ hq.ne'] using
      Real.rpow_le_rpow this (hA âŸ¨i, rflâŸ©) (inv_nonneg.mpr hq.le)
  Â· apply memâ„“p_gen
    have hf' := hfq.summable hq
    refine .of_norm_bounded_eventually hf' (@Set.Finite.subset _ { i | 1 â‰¤ â€–f iâ€– } ?_ _ ?_)
    Â· have H : { x : Î± | 1 â‰¤ â€–f xâ€– ^ q.toReal }.Finite := by
        simpa using hf'.tendsto_cofinite_zero.eventually_lt_const (by simp)
      exact H.subset fun i hi => Real.one_le_rpow hi hq.le
    Â· change âˆ€ i, Â¬|â€–f iâ€– ^ p.toReal| â‰¤ â€–f iâ€– ^ q.toReal â†’ 1 â‰¤ â€–f iâ€–
      intro i hi
      have : 0 â‰¤ â€–f iâ€– ^ p.toReal := Real.rpow_nonneg (norm_nonneg _) p.toReal
      simp only [abs_of_nonneg, this] at hi
      contrapose! hi
      exact Real.rpow_le_rpow_of_exponent_ge' (norm_nonneg _) hi.le hq.le hpq'

theorem add {f g : âˆ€ i, E i} (hf : Memâ„“p f p) (hg : Memâ„“p g p) : Memâ„“p (f + g) p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    refine (hf.finite_dsupport.union hg.finite_dsupport).subset fun i => ?_
    simp only [Pi.add_apply, Ne, Set.mem_union, Set.mem_setOf_eq]
    contrapose!
    rintro âŸ¨hf', hg'âŸ©
    simp [hf', hg']
  Â· apply memâ„“p_infty
    obtain âŸ¨A, hAâŸ© := hf.bddAbove
    obtain âŸ¨B, hBâŸ© := hg.bddAbove
    refine âŸ¨A + B, ?_âŸ©
    rintro a âŸ¨i, rflâŸ©
    exact le_trans (norm_add_le _ _) (add_le_add (hA âŸ¨i, rflâŸ©) (hB âŸ¨i, rflâŸ©))
  apply memâ„“p_gen
  let C : â„ := if p.toReal < 1 then 1 else (2 : â„) ^ (p.toReal - 1)
  refine .of_nonneg_of_le ?_ (fun i => ?_) (((hf.summable hp).add (hg.summable hp)).mul_left C)
  Â· intro; positivity
  Â· refine (Real.rpow_le_rpow (norm_nonneg _) (norm_add_le _ _) hp.le).trans ?_
    dsimp only [C]
    split_ifs with h
    Â· simpa using NNReal.coe_le_coe.2 (NNReal.rpow_add_le_add_rpow â€–f iâ€–â‚Š â€–g iâ€–â‚Š hp.le h.le)
    Â· let F : Fin 2 â†’ â„â‰¥0 := ![â€–f iâ€–â‚Š, â€–g iâ€–â‚Š]
      simp only [not_lt] at h
      simpa [Fin.sum_univ_succ] using
        Real.rpow_sum_le_const_mul_sum_rpow_of_nonneg Finset.univ h fun i _ => (F i).coe_nonneg

theorem sub {f g : âˆ€ i, E i} (hf : Memâ„“p f p) (hg : Memâ„“p g p) : Memâ„“p (f - g) p := by
  rw [sub_eq_add_neg]; exact hf.add hg.neg

theorem finset_sum {Î¹} (s : Finset Î¹) {f : Î¹ â†’ âˆ€ i, E i} (hf : âˆ€ i âˆˆ s, Memâ„“p (f i) p) :
    Memâ„“p (fun a => âˆ‘ i âˆˆ s, f i a) p := by
  haveI : DecidableEq Î¹ := Classical.decEq _
  revert hf
  refine Finset.induction_on s ?_ ?_
  Â· simp only [zero_mem_â„“p', Finset.sum_empty, imp_true_iff]
  Â· intro i s his ih hf
    simp only [his, Finset.sum_insert, not_false_iff]
    exact (hf i (s.mem_insert_self i)).add (ih fun j hj => hf j (Finset.mem_insert_of_mem hj))

section IsBoundedSMul

variable [NormedRing ğ•œ] [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, IsBoundedSMul ğ•œ (E i)]

theorem const_smul {f : âˆ€ i, E i} (hf : Memâ„“p f p) (c : ğ•œ) : Memâ„“p (c â€¢ f) p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    refine hf.finite_dsupport.subset fun i => (?_ : Â¬c â€¢ f i = 0 â†’ Â¬f i = 0)
    exact not_imp_not.mpr fun hf' => hf'.symm â–¸ smul_zero c
  Â· obtain âŸ¨A, hAâŸ© := hf.bddAbove
    refine memâ„“p_infty âŸ¨â€–câ€– * A, ?_âŸ©
    rintro a âŸ¨i, rflâŸ©
    dsimp only [Pi.smul_apply]
    refine (norm_smul_le _ _).trans ?_
    gcongr
    exact hA âŸ¨i, rflâŸ©
  Â· apply memâ„“p_gen
    dsimp only [Pi.smul_apply]
    have := (hf.summable hp).mul_left (â†‘(â€–câ€–â‚Š ^ p.toReal) : â„)
    simp_rw [â† coe_nnnorm, â† NNReal.coe_rpow, â† NNReal.coe_mul, NNReal.summable_coe,
      â† NNReal.mul_rpow] at this âŠ¢
    refine NNReal.summable_of_le ?_ this
    intro i
    gcongr
    apply nnnorm_smul_le

theorem const_mul {f : Î± â†’ ğ•œ} (hf : Memâ„“p f p) (c : ğ•œ) : Memâ„“p (fun x => c * f x) p :=
  hf.const_smul c

end IsBoundedSMul

end Memâ„“p

/-!
### lp space

The space of elements of `âˆ€ i, E i` satisfying the predicate `Memâ„“p`.
-/


/-- We define `PreLp E` to be a type synonym for `âˆ€ i, E i` which, importantly, does not inherit
the `pi` topology on `âˆ€ i, E i` (otherwise this topology would descend to `lp E p` and conflict
with the normed group topology we will later equip it with.)

We choose to deal with this issue by making a type synonym for `âˆ€ i, E i` rather than for the `lp`
subgroup itself, because this allows all the spaces `lp E p` (for varying `p`) to be subgroups of
the same ambient group, which permits lemma statements like `lp.monotone` (below). -/
@[nolint unusedArguments]
def PreLp (E : Î± â†’ Type*) [âˆ€ i, NormedAddCommGroup (E i)] : Type _ :=
  âˆ€ i, E i --deriving AddCommGroup

instance : AddCommGroup (PreLp E) := by unfold PreLp; infer_instance

instance PreLp.unique [IsEmpty Î±] : Unique (PreLp E) :=
  Pi.uniqueOfIsEmpty E

/-- lp space
The `p=âˆ` case has notation `â„“^âˆ(Î¹, E)` resp. `â„“^âˆ(Î¹)` (for `E = â„`) in the `lp` namespace. -/
def lp (E : Î± â†’ Type*) [âˆ€ i, NormedAddCommGroup (E i)] (p : â„â‰¥0âˆ) : AddSubgroup (PreLp E) where
  carrier := { f | Memâ„“p f p }
  zero_mem' := zero_memâ„“p
  add_mem' := Memâ„“p.add
  neg_mem' := Memâ„“p.neg

@[inherit_doc] scoped[lp] notation "â„“^âˆ(" Î¹ ", " E ")" => lp (fun i : Î¹ => E) âˆ
@[inherit_doc] scoped[lp] notation "â„“^âˆ(" Î¹ ")" => lp (fun i : Î¹ => â„) âˆ

namespace lp

instance : CoeOut (lp E p) (âˆ€ i, E i) :=
  âŸ¨Subtype.val (Î± := âˆ€ i, E i)âŸ©

instance coeFun : CoeFun (lp E p) fun _ => âˆ€ i, E i :=
  âŸ¨fun f => (f : âˆ€ i, E i)âŸ©

@[ext]
theorem ext {f g : lp E p} (h : (f : âˆ€ i, E i) = g) : f = g :=
  Subtype.ext h

theorem eq_zero' [IsEmpty Î±] (f : lp E p) : f = 0 :=
  Subsingleton.elim f 0

protected theorem monotone {p q : â„â‰¥0âˆ} (hpq : q â‰¤ p) : lp E q â‰¤ lp E p :=
  fun _ hf => Memâ„“p.of_exponent_ge hf hpq

protected theorem memâ„“p (f : lp E p) : Memâ„“p f p :=
  f.prop

variable (E p)

@[simp]
theorem coeFn_zero : â‡‘(0 : lp E p) = 0 :=
  rfl

variable {E p}

@[simp]
theorem coeFn_neg (f : lp E p) : â‡‘(-f) = -f :=
  rfl

@[simp]
theorem coeFn_add (f g : lp E p) : â‡‘(f + g) = f + g :=
  rfl

variable (p E) in
/-- Coercion to function as an `AddMonoidHom`. -/
def coeFnAddMonoidHom : lp E p â†’+ (âˆ€ i, E i) where
  toFun := (â‡‘)
  __ := AddSubgroup.subtype _

@[simp]
theorem coeFnAddMonoidHom_apply (x : lp E p) : coeFnAddMonoidHom E p x = â‡‘x := rfl

theorem coeFn_sum {Î¹ : Type*} (f : Î¹ â†’ lp E p) (s : Finset Î¹) :
    â‡‘(âˆ‘ i âˆˆ s, f i) = âˆ‘ i âˆˆ s, â‡‘(f i) := by
  simp

@[simp]
theorem coeFn_sub (f g : lp E p) : â‡‘(f - g) = f - g :=
  rfl

instance : Norm (lp E p) where
  norm f :=
    if hp : p = 0 then by
      subst hp
      exact ((lp.memâ„“p f).finite_dsupport.toFinset.card : â„)
    else if p = âˆ then â¨† i, â€–f iâ€– else (âˆ‘' i, â€–f iâ€– ^ p.toReal) ^ (1 / p.toReal)

theorem norm_eq_card_dsupport (f : lp E 0) : â€–fâ€– = (lp.memâ„“p f).finite_dsupport.toFinset.card :=
  dif_pos rfl

theorem norm_eq_ciSup (f : lp E âˆ) : â€–fâ€– = â¨† i, â€–f iâ€– := rfl

theorem isLUB_norm [Nonempty Î±] (f : lp E âˆ) : IsLUB (Set.range fun i => â€–f iâ€–) â€–fâ€– := by
  rw [lp.norm_eq_ciSup]
  exact isLUB_ciSup (lp.memâ„“p f)

theorem norm_eq_tsum_rpow (hp : 0 < p.toReal) (f : lp E p) :
    â€–fâ€– = (âˆ‘' i, â€–f iâ€– ^ p.toReal) ^ (1 / p.toReal) := by
  dsimp [norm]
  rw [ENNReal.toReal_pos_iff] at hp
  rw [dif_neg hp.1.ne', if_neg hp.2.ne]

theorem norm_rpow_eq_tsum (hp : 0 < p.toReal) (f : lp E p) :
    â€–fâ€– ^ p.toReal = âˆ‘' i, â€–f iâ€– ^ p.toReal := by
  rw [norm_eq_tsum_rpow hp, â† Real.rpow_mul]
  Â· field_simp
    simp
  apply tsum_nonneg
  intro i
  calc
    (0 : â„) = (0 : â„) ^ p.toReal := by rw [Real.zero_rpow hp.ne']
    _ â‰¤ _ := by gcongr; apply norm_nonneg

theorem hasSum_norm (hp : 0 < p.toReal) (f : lp E p) :
    HasSum (fun i => â€–f iâ€– ^ p.toReal) (â€–fâ€– ^ p.toReal) := by
  rw [norm_rpow_eq_tsum hp]
  exact ((lp.memâ„“p f).summable hp).hasSum

theorem norm_nonneg' (f : lp E p) : 0 â‰¤ â€–fâ€– := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· simp [lp.norm_eq_card_dsupport f]
  Â· rcases isEmpty_or_nonempty Î± with _i | _i
    Â· rw [lp.norm_eq_ciSup]
      simp [Real.iSup_of_isEmpty]
    inhabit Î±
    exact (norm_nonneg (f default)).trans ((lp.isLUB_norm f).1 âŸ¨default, rflâŸ©)
  Â· rw [lp.norm_eq_tsum_rpow hp f]
    refine Real.rpow_nonneg (tsum_nonneg ?_) _
    exact fun i => Real.rpow_nonneg (norm_nonneg _) _

@[simp]
theorem norm_zero : â€–(0 : lp E p)â€– = 0 := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· simp [lp.norm_eq_card_dsupport]
  Â· simp [lp.norm_eq_ciSup]
  Â· rw [lp.norm_eq_tsum_rpow hp]
    have hp' : 1 / p.toReal â‰  0 := one_div_ne_zero hp.ne'
    simpa [Real.zero_rpow hp.ne'] using Real.zero_rpow hp'

theorem norm_eq_zero_iff {f : lp E p} : â€–fâ€– = 0 â†” f = 0 := by
  refine âŸ¨fun h => ?_, by rintro rfl; exact norm_zeroâŸ©
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· ext i
    have : { i : Î± | Â¬f i = 0 } = âˆ… := by simpa [lp.norm_eq_card_dsupport f] using h
    have : (Â¬f i = 0) = False := congr_fun this i
    tauto
  Â· rcases isEmpty_or_nonempty Î± with _i | _i
    Â· simp [eq_iff_true_of_subsingleton]
    have H : IsLUB (Set.range fun i => â€–f iâ€–) 0 := by simpa [h] using lp.isLUB_norm f
    ext i
    have : â€–f iâ€– = 0 := le_antisymm (H.1 âŸ¨i, rflâŸ©) (norm_nonneg _)
    simpa using this
  Â· have hf : HasSum (fun i : Î± => â€–f iâ€– ^ p.toReal) 0 := by
      have := lp.hasSum_norm hp f
      rwa [h, Real.zero_rpow hp.ne'] at this
    have : âˆ€ i, 0 â‰¤ â€–f iâ€– ^ p.toReal := fun i => Real.rpow_nonneg (norm_nonneg _) _
    rw [hasSum_zero_iff_of_nonneg this] at hf
    ext i
    have : f i = 0 âˆ§ p.toReal â‰  0 := by
      simpa [Real.rpow_eq_zero_iff_of_nonneg (norm_nonneg (f i))] using congr_fun hf i
    exact this.1

theorem eq_zero_iff_coeFn_eq_zero {f : lp E p} : f = 0 â†” â‡‘f = 0 := by
  rw [lp.ext_iff, coeFn_zero]

@[simp]
theorem norm_neg â¦ƒf : lp E pâ¦„ : â€–-fâ€– = â€–fâ€– := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· simp only [norm_eq_card_dsupport, coeFn_neg, Pi.neg_apply, ne_eq, neg_eq_zero]
  Â· cases isEmpty_or_nonempty Î±
    Â· simp only [lp.eq_zero' f, neg_zero, norm_zero]
    apply (lp.isLUB_norm (-f)).unique
    simpa only [coeFn_neg, Pi.neg_apply, norm_neg] using lp.isLUB_norm f
  Â· suffices â€–-fâ€– ^ p.toReal = â€–fâ€– ^ p.toReal by
      exact Real.rpow_left_injOn hp.ne' (norm_nonneg' _) (norm_nonneg' _) this
    apply (lp.hasSum_norm hp (-f)).unique
    simpa only [coeFn_neg, Pi.neg_apply, _root_.norm_neg] using lp.hasSum_norm hp f

instance normedAddCommGroup [hp : Fact (1 â‰¤ p)] : WithNormedAddGroup (lp E p) := fast_instance%
  AddGroupNorm.toNormedAddGroup
    { toFun := norm
      map_zero' := norm_zero
      neg' := norm_neg
      add_le' := fun f g => by
        rcases p.dichotomy with (rfl | hp')
        Â· cases isEmpty_or_nonempty Î±
          Â· simp only [lp.eq_zero' f, zero_add, norm_zero, le_refl]
          refine (lp.isLUB_norm (f + g)).2 ?_
          rintro x âŸ¨i, rflâŸ©
          refine le_trans ?_ (add_mem_upperBounds_add
            (lp.isLUB_norm f).1 (lp.isLUB_norm g).1 âŸ¨_, âŸ¨i, rflâŸ©, _, âŸ¨i, rflâŸ©, rflâŸ©)
          exact norm_add_le (f i) (g i)
        Â· have hp'' : 0 < p.toReal := zero_lt_one.trans_le hp'
          have hfâ‚ : âˆ€ i, 0 â‰¤ â€–f iâ€– := fun i => norm_nonneg _
          have hgâ‚ : âˆ€ i, 0 â‰¤ â€–g iâ€– := fun i => norm_nonneg _
          have hfâ‚‚ := lp.hasSum_norm hp'' f
          have hgâ‚‚ := lp.hasSum_norm hp'' g
          -- apply Minkowski's inequality
          obtain âŸ¨C, hCâ‚, hCâ‚‚, hCfgâŸ© :=
            Real.Lp_add_le_hasSum_of_nonneg hp' hfâ‚ hgâ‚ (norm_nonneg' _) (norm_nonneg' _) hfâ‚‚ hgâ‚‚
          refine le_trans ?_ hCâ‚‚
          rw [â† Real.rpow_le_rpow_iff (norm_nonneg' (f + g)) hCâ‚ hp'']
          refine hasSum_le ?_ (lp.hasSum_norm hp'' (f + g)) hCfg
          intro i
          gcongr
          apply norm_add_le
      eq_zero_of_map_eq_zero' := fun _ => norm_eq_zero_iff.1 }

-- TODO: define an `ENNReal` version of `HolderConjugate`, and then express this inequality
-- in a better version which also covers the case `p = 1, q = âˆ`.
/-- HÃ¶lder inequality -/
protected theorem tsum_mul_le_mul_norm {p q : â„â‰¥0âˆ} (hpq : p.toReal.HolderConjugate q.toReal)
    (f : lp E p) (g : lp E q) :
    (Summable fun i => â€–f iâ€– * â€–g iâ€–) âˆ§ âˆ‘' i, â€–f iâ€– * â€–g iâ€– â‰¤ â€–fâ€– * â€–gâ€– := by
  have hfâ‚ : âˆ€ i, 0 â‰¤ â€–f iâ€– := fun i => norm_nonneg _
  have hgâ‚ : âˆ€ i, 0 â‰¤ â€–g iâ€– := fun i => norm_nonneg _
  have hfâ‚‚ := lp.hasSum_norm hpq.pos f
  have hgâ‚‚ := lp.hasSum_norm hpq.symm.pos g
  obtain âŸ¨C, -, hC', hCâŸ© :=
    Real.inner_le_Lp_mul_Lq_hasSum_of_nonneg hpq (norm_nonneg' _) (norm_nonneg' _) hfâ‚ hgâ‚ hfâ‚‚ hgâ‚‚
  rw [â† hC.tsum_eq] at hC'
  exact âŸ¨hC.summable, hC'âŸ©

protected theorem summable_mul {p q : â„â‰¥0âˆ} (hpq : p.toReal.HolderConjugate q.toReal)
    (f : lp E p) (g : lp E q) : Summable fun i => â€–f iâ€– * â€–g iâ€– :=
  (lp.tsum_mul_le_mul_norm hpq f g).1

protected theorem tsum_mul_le_mul_norm' {p q : â„â‰¥0âˆ} (hpq : p.toReal.HolderConjugate q.toReal)
    (f : lp E p) (g : lp E q) : âˆ‘' i, â€–f iâ€– * â€–g iâ€– â‰¤ â€–fâ€– * â€–gâ€– :=
  (lp.tsum_mul_le_mul_norm hpq f g).2

section ComparePointwise

theorem norm_apply_le_norm (hp : p â‰  0) (f : lp E p) (i : Î±) : â€–f iâ€– â‰¤ â€–fâ€– := by
  rcases eq_or_ne p âˆ with (rfl | hp')
  Â· haveI : Nonempty Î± := âŸ¨iâŸ©
    exact (isLUB_norm f).1 âŸ¨i, rflâŸ©
  have hp'' : 0 < p.toReal := ENNReal.toReal_pos hp hp'
  have : âˆ€ i, 0 â‰¤ â€–f iâ€– ^ p.toReal := fun i => Real.rpow_nonneg (norm_nonneg _) _
  rw [â† Real.rpow_le_rpow_iff (norm_nonneg _) (norm_nonneg' _) hp'']
  convert le_hasSum (hasSum_norm hp'' f) i fun i _ => this i

theorem sum_rpow_le_norm_rpow (hp : 0 < p.toReal) (f : lp E p) (s : Finset Î±) :
    âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal â‰¤ â€–fâ€– ^ p.toReal := by
  rw [lp.norm_rpow_eq_tsum hp f]
  have : âˆ€ i, 0 â‰¤ â€–f iâ€– ^ p.toReal := fun i => Real.rpow_nonneg (norm_nonneg _) _
  refine Summable.sum_le_tsum _ (fun i _ => this i) ?_
  exact (lp.memâ„“p f).summable hp

theorem norm_le_of_forall_le' [Nonempty Î±] {f : lp E âˆ} (C : â„) (hCf : âˆ€ i, â€–f iâ€– â‰¤ C) :
    â€–fâ€– â‰¤ C := by
  refine (isLUB_norm f).2 ?_
  rintro - âŸ¨i, rflâŸ©
  exact hCf i

theorem norm_le_of_forall_le {f : lp E âˆ} {C : â„} (hC : 0 â‰¤ C) (hCf : âˆ€ i, â€–f iâ€– â‰¤ C) :
    â€–fâ€– â‰¤ C := by
  cases isEmpty_or_nonempty Î±
  Â· simpa [eq_zero' f] using hC
  Â· exact norm_le_of_forall_le' C hCf

theorem norm_le_of_tsum_le (hp : 0 < p.toReal) {C : â„} (hC : 0 â‰¤ C) {f : lp E p}
    (hf : âˆ‘' i, â€–f iâ€– ^ p.toReal â‰¤ C ^ p.toReal) : â€–fâ€– â‰¤ C := by
  rw [â† Real.rpow_le_rpow_iff (norm_nonneg' _) hC hp, norm_rpow_eq_tsum hp]
  exact hf

theorem norm_le_of_forall_sum_le (hp : 0 < p.toReal) {C : â„} (hC : 0 â‰¤ C) {f : lp E p}
    (hf : âˆ€ s : Finset Î±, âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal â‰¤ C ^ p.toReal) : â€–fâ€– â‰¤ C :=
  norm_le_of_tsum_le hp hC (((lp.memâ„“p f).summable hp).tsum_le_of_sum_le hf)

end ComparePointwise

section IsBoundedSMul

variable [NormedRing ğ•œ] [NormedRing ğ•œ']
variable [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, Module ğ•œ' (E i)]

instance : Module ğ•œ (PreLp E) :=
  Pi.module Î± E ğ•œ

instance [âˆ€ i, SMulCommClass ğ•œ' ğ•œ (E i)] : SMulCommClass ğ•œ' ğ•œ (PreLp E) :=
  Pi.smulCommClass

instance [SMul ğ•œ' ğ•œ] [âˆ€ i, IsScalarTower ğ•œ' ğ•œ (E i)] : IsScalarTower ğ•œ' ğ•œ (PreLp E) :=
  Pi.isScalarTower

instance [âˆ€ i, Module ğ•œáµáµ’áµ– (E i)] [âˆ€ i, IsCentralScalar ğ•œ (E i)] : IsCentralScalar ğ•œ (PreLp E) :=
  Pi.isCentralScalar

variable [âˆ€ i, IsBoundedSMul ğ•œ (E i)] [âˆ€ i, IsBoundedSMul ğ•œ' (E i)]

theorem mem_lp_const_smul (c : ğ•œ) (f : lp E p) : c â€¢ (f : PreLp E) âˆˆ lp E p :=
  (lp.memâ„“p f).const_smul c

variable (ğ•œ E p)

/-- The `ğ•œ`-submodule of elements of `âˆ€ i : Î±, E i` whose `lp` norm is finite. This is `lp E p`,
with extra structure. -/
def _root_.lpSubmodule : Submodule ğ•œ (PreLp E) :=
  { lp E p with smul_mem' := fun c f hf => by simpa using mem_lp_const_smul c âŸ¨f, hfâŸ© }

variable {ğ•œ E p}

theorem coe_lpSubmodule : (lpSubmodule ğ•œ E p).toAddSubgroup = lp E p :=
  rfl

instance : Module ğ•œ (lp E p) :=
  { (lpSubmodule ğ•œ E p).module with }

@[simp]
theorem coeFn_smul (c : ğ•œ) (f : lp E p) : â‡‘(c â€¢ f) = c â€¢ â‡‘f :=
  rfl

instance [âˆ€ i, SMulCommClass ğ•œ' ğ•œ (E i)] : SMulCommClass ğ•œ' ğ•œ (lp E p) :=
  âŸ¨fun _ _ _ => Subtype.ext <| smul_comm _ _ _âŸ©

instance [SMul ğ•œ' ğ•œ] [âˆ€ i, IsScalarTower ğ•œ' ğ•œ (E i)] : IsScalarTower ğ•œ' ğ•œ (lp E p) :=
  âŸ¨fun _ _ _ => Subtype.ext <| smul_assoc _ _ _âŸ©

instance [âˆ€ i, Module ğ•œáµáµ’áµ– (E i)] [âˆ€ i, IsCentralScalar ğ•œ (E i)] : IsCentralScalar ğ•œ (lp E p) :=
  âŸ¨fun _ _ => Subtype.ext <| op_smul_eq_smul _ _âŸ©

theorem norm_const_smul_le (hp : p â‰  0) (c : ğ•œ) (f : lp E p) : â€–c â€¢ fâ€– â‰¤ â€–câ€– * â€–fâ€– := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· exact absurd rfl hp
  Â· cases isEmpty_or_nonempty Î±
    Â· simp [lp.eq_zero' f]
    have hcf := lp.isLUB_norm (c â€¢ f)
    have hfc := (lp.isLUB_norm f).mul_left (norm_nonneg c)
    simp_rw [â† Set.range_comp, Function.comp_def] at hfc
    -- TODO: some `IsLUB` API should make it a one-liner from here.
    refine hcf.right ?_
    have := hfc.left
    simp_rw [mem_upperBounds, Set.mem_range,
      forall_exists_index, forall_apply_eq_imp_iff] at this âŠ¢
    intro a
    exact (norm_smul_le _ _).trans (this a)
  Â· letI inst : NNNorm (lp E p) := âŸ¨fun f => âŸ¨â€–fâ€–, norm_nonneg' _âŸ©âŸ©
    have coe_nnnorm : âˆ€ f : lp E p, â†‘â€–fâ€–â‚Š = â€–fâ€– := fun _ => rfl
    suffices â€–c â€¢ fâ€–â‚Š ^ p.toReal â‰¤ (â€–câ€–â‚Š * â€–fâ€–â‚Š) ^ p.toReal by
      rwa [NNReal.rpow_le_rpow_iff hp] at this
    clear_value inst
    rw [NNReal.mul_rpow]
    have hLHS := lp.hasSum_norm hp (c â€¢ f)
    have hRHS := (lp.hasSum_norm hp f).mul_left (â€–câ€– ^ p.toReal)
    simp_rw [â† coe_nnnorm, â† _root_.coe_nnnorm, â† NNReal.coe_rpow, â† NNReal.coe_mul,
      NNReal.hasSum_coe] at hRHS hLHS
    refine hasSum_mono hLHS hRHS fun i => ?_
    dsimp only
    rw [â† NNReal.mul_rpow, lp.coeFn_smul, Pi.smul_apply]
    gcongr
    apply nnnorm_smul_le

instance [Fact (1 â‰¤ p)] : IsBoundedSMul ğ•œ (lp E p) :=
  IsBoundedSMul.of_norm_smul_le <| norm_const_smul_le (zero_lt_one.trans_le <| Fact.out).ne'

end IsBoundedSMul

section DivisionRing

variable [NormedDivisionRing ğ•œ] [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, IsBoundedSMul ğ•œ (E i)]

theorem norm_const_smul (hp : p â‰  0) {c : ğ•œ} (f : lp E p) : â€–c â€¢ fâ€– = â€–câ€– * â€–fâ€– := by
  obtain rfl | hc := eq_or_ne c 0
  Â· simp
  refine le_antisymm (norm_const_smul_le hp c f) ?_
  have := mul_le_mul_of_nonneg_left (norm_const_smul_le hp câ»Â¹ (c â€¢ f)) (norm_nonneg c)
  rwa [inv_smul_smulâ‚€ hc, norm_inv, mul_inv_cancel_leftâ‚€ (norm_ne_zero_iff.mpr hc)] at this

end DivisionRing

section NormedSpace

variable [NormedField ğ•œ] [âˆ€ i, NormedSpace ğ•œ (E i)]

instance instNormedSpace [Fact (1 â‰¤ p)] : NormSMulClass ğ•œ (lp E p) :=
  NormedDivisionRing.toNormSMulClass

end NormedSpace

section NormedStarGroup

variable [âˆ€ i, StarAddMonoid (E i)] [âˆ€ i, NormedStarGroup (E i)]

theorem _root_.Memâ„“p.star_mem {f : âˆ€ i, E i} (hf : Memâ„“p f p) : Memâ„“p (star f) p := by
  rcases p.trichotomy with (rfl | rfl | hp)
  Â· apply memâ„“p_zero
    simp [hf.finite_dsupport]
  Â· apply memâ„“p_infty
    simpa using hf.bddAbove
  Â· apply memâ„“p_gen
    simpa using hf.summable hp

@[simp]
theorem _root_.Memâ„“p.star_iff {f : âˆ€ i, E i} : Memâ„“p (star f) p â†” Memâ„“p f p :=
  âŸ¨fun h => star_star f â–¸ Memâ„“p.star_mem h, Memâ„“p.star_memâŸ©

instance : Star (lp E p) where
  star f := âŸ¨(star f : âˆ€ i, E i), f.property.star_memâŸ©

@[simp]
theorem coeFn_star (f : lp E p) : â‡‘(star f) = star (â‡‘f) :=
  rfl

@[simp]
protected theorem star_apply (f : lp E p) (i : Î±) : star f i = star (f i) :=
  rfl

instance instInvolutiveStar : InvolutiveStar (lp E p) where
  star_involutive x := by simp [star]

instance instStarAddMonoid : StarAddMonoid (lp E p) where
  star_add _f _g := ext <| star_add (R := âˆ€ i, E i) _ _

instance [hp : Fact (1 â‰¤ p)] : NormedStarGroup (lp E p) where
  norm_star_le f := le_of_eq <| by
    rcases p.trichotomy with (rfl | rfl | h)
    Â· exfalso
      have := ENNReal.toReal_mono ENNReal.zero_ne_top hp.elim
      norm_num at this
    Â· simp only [lp.norm_eq_ciSup, lp.star_apply, norm_star]
    Â· simp only [lp.norm_eq_tsum_rpow h, lp.star_apply, norm_star]

variable [Star ğ•œ] [NormedRing ğ•œ]
variable [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, IsBoundedSMul ğ•œ (E i)] [âˆ€ i, StarModule ğ•œ (E i)]

instance : StarModule ğ•œ (lp E p) where
  star_smul _r _f := ext <| star_smul (A := âˆ€ i, E i) _ _

end NormedStarGroup

section NonUnitalNormedRing

variable {I : Type*} {B : I â†’ Type*} [âˆ€ i, NonUnitalNormedRing (B i)]

theorem _root_.Memâ„“p.infty_mul {f g : âˆ€ i, B i} (hf : Memâ„“p f âˆ) (hg : Memâ„“p g âˆ) :
    Memâ„“p (f * g) âˆ := by
  rw [memâ„“p_infty_iff]
  obtain âŸ¨âŸ¨Cf, hCfâŸ©, âŸ¨Cg, hCgâŸ©âŸ© := hf.bddAbove, hg.bddAbove
  refine âŸ¨Cf * Cg, ?_âŸ©
  rintro _ âŸ¨i, rflâŸ©
  calc
    â€–(f * g) iâ€– â‰¤ â€–f iâ€– * â€–g iâ€– := norm_mul_le (f i) (g i)
    _ â‰¤ Cf * Cg :=
      mul_le_mul (hCf âŸ¨i, rflâŸ©) (hCg âŸ¨i, rflâŸ©) (norm_nonneg _)
        ((norm_nonneg _).trans (hCf âŸ¨i, rflâŸ©))

instance : Mul (lp B âˆ) where
  mul f g := âŸ¨HMul.hMul (Î± := âˆ€ i, B i) _ _, f.property.infty_mul g.propertyâŸ©

@[simp]
theorem infty_coeFn_mul (f g : lp B âˆ) : â‡‘(f * g) = â‡‘f * â‡‘g :=
  rfl

instance nonUnitalRing : NonUnitalRing (lp B âˆ) :=
  Function.Injective.nonUnitalRing lp.coeFun.coe Subtype.coe_injective (lp.coeFn_zero B âˆ)
    lp.coeFn_add infty_coeFn_mul lp.coeFn_neg lp.coeFn_sub (fun _ _ => rfl) fun _ _ => rfl

set_option synthInstance.maxHeartbeats 100000 in
-- something wrong here, investigate
instance nonUnitalNormedRing : WithNormedRing (lp B âˆ) :=
  { lp.normedAddCommGroup with
    norm_mul_le f g := lp.norm_le_of_forall_le (by positivity) fun i â†¦ calc
      â€–(f * g) iâ€– â‰¤ â€–f iâ€– * â€–g iâ€– := norm_mul_le _ _
      _ â‰¤ â€–fâ€– * â€–gâ€– := mul_le_mul (lp.norm_apply_le_norm ENNReal.top_ne_zero f i)
        (lp.norm_apply_le_norm ENNReal.top_ne_zero g i) (norm_nonneg _) (norm_nonneg _) }

instance nonUnitalCommRing {B : I â†’ Type*} [âˆ€ i, NonUnitalNormedCommRing (B i)] :
    NonUnitalCommRing (lp B âˆ) where
  mul_comm _ _ := ext <| mul_comm ..

-- we also want a `NonUnitalNormedCommRing` instance, but this has to wait for https://github.com/leanprover-community/mathlib3/pull/13719
instance infty_isScalarTower {ğ•œ} [NormedRing ğ•œ] [âˆ€ i, Module ğ•œ (B i)] [âˆ€ i, IsBoundedSMul ğ•œ (B i)]
    [âˆ€ i, IsScalarTower ğ•œ (B i) (B i)] : IsScalarTower ğ•œ (lp B âˆ) (lp B âˆ) :=
  âŸ¨fun r f g => lp.ext <| smul_assoc (N := âˆ€ i, B i) (Î± := âˆ€ i, B i) r (â‡‘f) (â‡‘g)âŸ©

instance infty_smulCommClass {ğ•œ} [NormedRing ğ•œ] [âˆ€ i, Module ğ•œ (B i)] [âˆ€ i, IsBoundedSMul ğ•œ (B i)]
    [âˆ€ i, SMulCommClass ğ•œ (B i) (B i)] : SMulCommClass ğ•œ (lp B âˆ) (lp B âˆ) :=
  âŸ¨fun r f g => lp.ext <| smul_comm (N := âˆ€ i, B i) (Î± := âˆ€ i, B i) r (â‡‘f) (â‡‘g)âŸ©

section StarRing

variable [âˆ€ i, StarRing (B i)] [âˆ€ i, NormedStarGroup (B i)]

instance inftyStarRing : StarRing (lp B âˆ) :=
  { lp.instStarAddMonoid with
    star_mul := fun _f _g => ext <| star_mul (R := âˆ€ i, B i) _ _ }

instance inftyCStarRing [âˆ€ i, CStarRing (B i)] : CStarRing (lp B âˆ) where
  norm_mul_self_le f := by
    rw [â† sq, â† Real.le_sqrt (norm_nonneg _) (norm_nonneg _)]
    refine lp.norm_le_of_forall_le â€–star f * fâ€–.sqrt_nonneg fun i => ?_
    rw [Real.le_sqrt (norm_nonneg _) (norm_nonneg _), sq, â† CStarRing.norm_star_mul_self]
    exact lp.norm_apply_le_norm ENNReal.top_ne_zero (star f * f) i

end StarRing

end NonUnitalNormedRing

section NormedRing

variable {I : Type*} {B : I â†’ Type*} [âˆ€ i, NormedRing (B i)]

instance _root_.PreLp.ring : Ring (PreLp B) :=
  Pi.ring

variable [âˆ€ i, NormOneClass (B i)]

theorem _root_.one_memâ„“p_infty : Memâ„“p (1 : âˆ€ i, B i) âˆ :=
  âŸ¨1, by rintro i âŸ¨i, rflâŸ©; exact norm_one.leâŸ©

variable (B) in
/-- The `ğ•œ`-subring of elements of `âˆ€ i : Î±, B i` whose `lp` norm is finite. This is `lp E âˆ`,
with extra structure. -/
def _root_.lpInftySubring : Subring (PreLp B) :=
  { lp B âˆ with
    carrier := { f | Memâ„“p f âˆ }
    one_mem' := one_memâ„“p_infty
    mul_mem' := Memâ„“p.infty_mul }

instance inftyRing : Ring (lp B âˆ) :=
  (lpInftySubring B).toRing

theorem _root_.Memâ„“p.infty_pow {f : âˆ€ i, B i} (hf : Memâ„“p f âˆ) (n : â„•) : Memâ„“p (f ^ n) âˆ :=
  (lpInftySubring B).pow_mem hf n

theorem _root_.natCast_memâ„“p_infty (n : â„•) : Memâ„“p (n : âˆ€ i, B i) âˆ :=
  natCast_mem (lpInftySubring B) n

theorem _root_.intCast_memâ„“p_infty (z : â„¤) : Memâ„“p (z : âˆ€ i, B i) âˆ :=
  intCast_mem (lpInftySubring B) z

@[simp]
theorem infty_coeFn_one : â‡‘(1 : lp B âˆ) = 1 :=
  rfl

@[simp]
theorem infty_coeFn_pow (f : lp B âˆ) (n : â„•) : â‡‘(f ^ n) = (â‡‘f) ^ n :=
  rfl

@[simp]
theorem infty_coeFn_natCast (n : â„•) : â‡‘(n : lp B âˆ) = n :=
  rfl

@[simp]
theorem infty_coeFn_intCast (z : â„¤) : â‡‘(z : lp B âˆ) = z :=
  rfl

instance [Nonempty I] : NormOneClass (lp B âˆ) where
  norm_one := by simp_rw [lp.norm_eq_ciSup, infty_coeFn_one, Pi.one_apply, norm_one, ciSup_const]

end NormedRing

section NormedCommRing

variable {I : Type*} {B : I â†’ Type*} [âˆ€ i, NormedCommRing (B i)] [âˆ€ i, NormOneClass (B i)]

instance inftyCommRing : CommRing (lp B âˆ) where
  mul_comm := mul_comm

example : NormedCommRing (lp B âˆ) := by infer_instance

end NormedCommRing

section Algebra

variable {I : Type*} {B : I â†’ Type*}
variable [NormedField ğ•œ] [âˆ€ i, NormedRing (B i)] [âˆ€ i, NormedAlgebra ğ•œ (B i)]

instance _root_.PreLp.algebra : Algebra ğ•œ (PreLp B) :=
  inferInstanceAs (Algebra ğ•œ (âˆ€ i, B i))

variable [âˆ€ i, NormOneClass (B i)]

theorem _root_.algebraMap_memâ„“p_infty (k : ğ•œ) : Memâ„“p (algebraMap ğ•œ (âˆ€ i, B i) k) âˆ := by
  rw [Algebra.algebraMap_eq_smul_one]
  exact (one_memâ„“p_infty.const_smul k : Memâ„“p (k â€¢ (1 : âˆ€ i, B i)) âˆ)

variable (ğ•œ B)

/-- The `ğ•œ`-subalgebra of elements of `âˆ€ i : Î±, B i` whose `lp` norm is finite. This is `lp E âˆ`,
with extra structure. -/
def _root_.lpInftySubalgebra : Subalgebra ğ•œ (PreLp B) :=
  { lpInftySubring B with
    carrier := { f | Memâ„“p f âˆ }
    algebraMap_mem' := algebraMap_memâ„“p_infty }

variable {ğ•œ B}

instance inftyAlgebra : Algebra ğ•œ (lp B âˆ) := (lpInftySubalgebra ğ•œ B).algebra

set_option synthInstance.maxHeartbeats 100000 in
-- something wrong here, investigate
example : NormedAlgebra ğ•œ (lp B âˆ) := by infer_instance

end Algebra

section Single

variable [NormedRing ğ•œ] [âˆ€ i, Module ğ•œ (E i)] [âˆ€ i, IsBoundedSMul ğ•œ (E i)]
variable [DecidableEq Î±]

/-- The element of `lp E p` which is `a : E i` at the index `i`, and zero elsewhere. -/
protected def single (p) (i : Î±) (a : E i) : lp E p :=
  âŸ¨Pi.single i a, by
    refine (memâ„“p_zero ?_).of_exponent_ge (zero_le p)
    refine (Set.finite_singleton i).subset ?_
    intro j
    simp only [Set.mem_singleton_iff, Ne,
      Set.mem_setOf_eq]
    rw [not_imp_comm]
    intro h
    exact Pi.single_eq_of_ne h _âŸ©

@[norm_cast]
protected theorem coeFn_single (p) (i : Î±) (a : E i) :
    â‡‘(lp.single p i a) = Pi.single i a := rfl

@[simp]
protected theorem single_apply (p) (i : Î±) (a : E i) (j : Î±) :
    lp.single p i a j = Pi.single i a j :=
  rfl

protected theorem single_apply_self (p) (i : Î±) (a : E i) : lp.single p i a i = a :=
  Pi.single_eq_same _ _

protected theorem single_apply_ne (p) (i : Î±) (a : E i) {j : Î±} (hij : j â‰  i) :
    lp.single p i a j = 0 :=
  Pi.single_eq_of_ne hij _

@[simp]
protected theorem single_zero (p) (i : Î±) :
    lp.single p i (0 : E i) = 0 :=
  ext <| Pi.single_zero _

@[simp]
protected theorem single_add (p) (i : Î±) (a b : E i) :
    lp.single p i (a + b) = lp.single p i a + lp.single p i b :=
  ext <| Pi.single_add _ _ _

/-- `single` as an `AddMonoidHom`. -/
@[simps]
def singleAddMonoidHom (p) (i : Î±) : E i â†’+ lp E p where
  toFun := lp.single p i
  map_zero' := lp.single_zero _ _
  map_add' := lp.single_add _ _

@[simp]
protected theorem single_neg (p) (i : Î±) (a : E i) : lp.single p i (-a) = -lp.single p i a :=
  ext <| Pi.single_neg _ _

@[simp]
protected theorem single_sub (p) (i : Î±) (a b : E i) :
    lp.single p i (a - b) = lp.single p i a - lp.single p i b :=
  ext <| Pi.single_sub _ _ _

@[simp]
protected theorem single_smul (p) (i : Î±) (c : ğ•œ) (a : E i) :
    lp.single p i (c â€¢ a) = c â€¢ lp.single p i a :=
  ext <| Pi.single_smul _ _ _

/-- `single` as a `LinearMap`. -/
@[simps]
def lsingle (p) (i : Î±) : E i â†’â‚—[ğ•œ] lp E p where
  toFun := lp.single p i
  __ := singleAddMonoidHom p i
  map_smul' := lp.single_smul p i

protected theorem norm_sum_single (hp : 0 < p.toReal) (f : âˆ€ i, E i) (s : Finset Î±) :
    â€–âˆ‘ i âˆˆ s, lp.single p i (f i)â€– ^ p.toReal = âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal := by
  refine (hasSum_norm hp (âˆ‘ i âˆˆ s, lp.single p i (f i))).unique ?_
  simp only [lp.coeFn_single, coeFn_sum, Finset.sum_apply, Finset.sum_pi_single]
  have h : âˆ€ i âˆ‰ s, â€–ite (i âˆˆ s) (f i) 0â€– ^ p.toReal = 0 := fun i hi â†¦ by
    simp [if_neg hi, Real.zero_rpow hp.ne']
  have h' : âˆ€ i âˆˆ s, â€–f iâ€– ^ p.toReal = â€–ite (i âˆˆ s) (f i) 0â€– ^ p.toReal := by
    intro i hi
    rw [if_pos hi]
  simpa [Finset.sum_congr rfl h'] using hasSum_sum_of_ne_finset_zero h

@[simp]
protected theorem norm_single (hp : 0 < p) (i : Î±) (x : E i) : â€–lp.single p i xâ€– = â€–xâ€– := by
  haveI : Nonempty Î± := âŸ¨iâŸ©
  induction p with
  | top =>
    simp only [norm_eq_ciSup, lp.coeFn_single]
    refine
      ciSup_eq_of_forall_le_of_forall_lt_exists_gt (fun j => ?_) fun n hn => âŸ¨i, hn.trans_eq ?_âŸ©
    Â· obtain rfl | hij := Decidable.eq_or_ne i j
      Â· rw [Pi.single_eq_same]
      Â· rw [Pi.single_eq_of_ne' hij, _root_.norm_zero]
        exact norm_nonneg _
    Â· rw [Pi.single_eq_same]
  | coe p =>
    have : 0 < (p : â„â‰¥0âˆ).toReal := by simpa using hp
    rw [norm_eq_tsum_rpow this, tsum_eq_single i, lp.coeFn_single, one_div,
      Real.rpow_rpow_inv _ this.ne', Pi.single_eq_same]
    Â· exact norm_nonneg _
    Â· intro j hji
      rw [lp.coeFn_single, Pi.single_eq_of_ne hji, _root_.norm_zero, Real.zero_rpow this.ne']

theorem isometry_single [Fact (1 â‰¤ p)] (i : Î±) : Isometry (lp.single (E := E) p i) :=
  AddMonoidHomClass.isometry_of_norm (lp.singleAddMonoidHom (E := E) p i) fun _ â†¦
    lp.norm_single (zero_lt_one.trans_le Fact.out) _ _

variable (p E) in
/-- `lp.single` as a continuous morphism of additive monoids. -/
def singleContinuousAddMonoidHom [Fact (1 â‰¤ p)] (i : Î±) :
    ContinuousAddMonoidHom (E i) (lp E p) where
  __ := singleAddMonoidHom p i
  continuous_toFun := isometry_single i |>.continuous

@[simp]
theorem singleContinuousAddMonoidHom_apply [Fact (1 â‰¤ p)] (i : Î±) (x : E i) :
    singleContinuousAddMonoidHom E p i x = lp.single p i x :=
  rfl

variable (ğ•œ p E) in
/-- `lp.single` as a continuous linear map. -/
def singleContinuousLinearMap [Fact (1 â‰¤ p)] (i : Î±) : E i â†’L[ğ•œ] lp E p where
  __ := lsingle p i
  cont := isometry_single i |>.continuous

@[simp]
theorem singleContinuousLinearMap_apply [Fact (1 â‰¤ p)] (i : Î±) (x : E i) :
    singleContinuousLinearMap ğ•œ E p i x = lp.single p i x :=
  rfl

protected theorem norm_sub_norm_compl_sub_single (hp : 0 < p.toReal) (f : lp E p) (s : Finset Î±) :
    â€–fâ€– ^ p.toReal - â€–f - âˆ‘ i âˆˆ s, lp.single p i (f i)â€– ^ p.toReal =
      âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal := by
  refine ((hasSum_norm hp f).sub (hasSum_norm hp (f - âˆ‘ i âˆˆ s, lp.single p i (f i)))).unique ?_
  let F : Î± â†’ â„ := fun i => â€–f iâ€– ^ p.toReal - â€–(f - âˆ‘ i âˆˆ s, lp.single p i (f i)) iâ€– ^ p.toReal
  have hF : âˆ€ i âˆ‰ s, F i = 0 := by
    intro i hi
    suffices â€–f iâ€– ^ p.toReal - â€–f i - ite (i âˆˆ s) (f i) 0â€– ^ p.toReal = 0 by
      simpa only [coeFn_sub, coeFn_sum, lp.coeFn_single, Pi.sub_apply, Finset.sum_apply,
        Finset.sum_pi_single, F] using this
    simp only [if_neg hi, sub_zero, sub_self]
  have hF' : âˆ€ i âˆˆ s, F i = â€–f iâ€– ^ p.toReal := by
    intro i hi
    simp only [F, coeFn_sum, lp.single_apply, if_pos hi, sub_self, coeFn_sub,
      Pi.sub_apply, Finset.sum_apply, Finset.sum_pi_single, sub_eq_self]
    simp [Real.zero_rpow hp.ne']
  have : HasSum F (âˆ‘ i âˆˆ s, F i) := hasSum_sum_of_ne_finset_zero hF
  rwa [Finset.sum_congr rfl hF'] at this

protected theorem norm_compl_sum_single (hp : 0 < p.toReal) (f : lp E p) (s : Finset Î±) :
    â€–f - âˆ‘ i âˆˆ s, lp.single p i (f i)â€– ^ p.toReal = â€–fâ€– ^ p.toReal - âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal := by
  linarith [lp.norm_sub_norm_compl_sub_single hp f s]

/-- The canonical finitely-supported approximations to an element `f` of `lp` converge to it, in the
`lp` topology. -/
protected theorem hasSum_single [Fact (1 â‰¤ p)] (hp : p â‰  âŠ¤) (f : lp E p) :
    HasSum (fun i : Î± => lp.single p i (f i : E i)) f := by
  have hpâ‚€ : 0 < p := zero_lt_one.trans_le Fact.out
  have hp' : 0 < p.toReal := ENNReal.toReal_pos hpâ‚€.ne' hp
  have := lp.hasSum_norm hp' f
  rw [HasSum, Metric.tendsto_nhds] at this âŠ¢
  intro Îµ hÎµ
  refine (this _ (Real.rpow_pos_of_pos hÎµ p.toReal)).mono ?_
  intro s hs
  rw [â† Real.rpow_lt_rpow_iff dist_nonneg (le_of_lt hÎµ) hp']
  rw [dist_comm] at hs
  simp only [dist_eq_norm, Real.norm_eq_abs] at hs âŠ¢
  have H : â€–(âˆ‘ i âˆˆ s, lp.single p i (f i : E i)) - fâ€– ^ p.toReal =
      â€–fâ€– ^ p.toReal - âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal := by
    simpa only [coeFn_neg, Pi.neg_apply, lp.single_neg, Finset.sum_neg_distrib, neg_sub_neg,
      norm_neg, _root_.norm_neg] using lp.norm_compl_sum_single hp' (-f) s
  rw [â† H] at hs
  have : |â€–(âˆ‘ i âˆˆ s, lp.single p i (f i : E i)) - fâ€– ^ p.toReal| =
      â€–(âˆ‘ i âˆˆ s, lp.single p i (f i : E i)) - fâ€– ^ p.toReal := by
    simp only [Real.abs_rpow_of_nonneg (norm_nonneg _), abs_norm]
  exact this â–¸ hs

/-- Two continuous additive maps from `lp E p` agree if they agree on `lp.single`.

See note [partially-applied ext lemmas]. -/
@[local ext] -- not globally `ext` due to `hp`
theorem ext_continuousAddMonoidHom
    {F : Type*} [AddCommMonoid F] [TopologicalSpace F] [T2Space F]
    [Fact (1 â‰¤ p)] (hp : p â‰  âŠ¤) â¦ƒf g : ContinuousAddMonoidHom (lp E p) Fâ¦„
    (h : âˆ€ i,
      f.comp (singleContinuousAddMonoidHom E p i) = g.comp (singleContinuousAddMonoidHom E p i)) :
    f = g := by
  ext x
  classical
  have := lp.hasSum_single hp x
  rw [â† (this.map f f.continuous).tsum_eq, â† (this.map g g.continuous).tsum_eq]
  congr! 2 with i
  exact DFunLike.congr_fun (h i) (x i)

/-- Two continuous linear maps from `lp E p` agree if they agree on `lp.single`.

See note [partially-applied ext lemmas]. -/
@[local ext] -- not globally `ext` due to `hp`
theorem ext_continuousLinearMap
    {F : Type*} [AddCommMonoid F] [Module ğ•œ F] [TopologicalSpace F] [T2Space F]
    [Fact (1 â‰¤ p)] (hp : p â‰  âŠ¤) â¦ƒf g : lp E p â†’L[ğ•œ] Fâ¦„
    (h : âˆ€ i,
      f.comp (singleContinuousLinearMap ğ•œ E p i) = g.comp (singleContinuousLinearMap ğ•œ E p i)) :
    f = g :=
  ContinuousLinearMap.toContinuousAddMonoidHom_injective <|
    ext_continuousAddMonoidHom hp fun i => ContinuousLinearMap.toContinuousAddMonoidHom_inj.2 (h i)

end Single

section Topology

open Filter

open scoped Topology uniformity

/-- The coercion from `lp E p` to `âˆ€ i, E i` is uniformly continuous. -/
theorem uniformContinuous_coe [_i : Fact (1 â‰¤ p)] :
    UniformContinuous (Î± := lp E p) ((â†‘) : lp E p â†’ âˆ€ i, E i) := by
  have hp : p â‰  0 := (zero_lt_one.trans_le _i.elim).ne'
  rw [uniformContinuous_pi]
  intro i
  rw [NormedAddCommGroup.uniformity_basis_dist.uniformContinuous_iff
    NormedAddCommGroup.uniformity_basis_dist]
  intro Îµ hÎµ
  refine âŸ¨Îµ, hÎµ, ?_âŸ©
  rintro f g (hfg : â€–f - gâ€– < Îµ)
  have : â€–f i - g iâ€– â‰¤ â€–f - gâ€– := norm_apply_le_norm hp (f - g) i
  exact this.trans_lt hfg

variable {Î¹ : Type*} {l : Filter Î¹} [Filter.NeBot l]

theorem norm_apply_le_of_tendsto {C : â„} {F : Î¹ â†’ lp E âˆ} (hCF : âˆ€á¶  k in l, â€–F kâ€– â‰¤ C)
    {f : âˆ€ a, E a} (hf : Tendsto (id fun i => F i : Î¹ â†’ âˆ€ a, E a) l (ğ“ f)) (a : Î±) : â€–f aâ€– â‰¤ C := by
  have : Tendsto (fun k => â€–F k aâ€–) l (ğ“ â€–f aâ€–) :=
    (Tendsto.comp (continuous_apply a).continuousAt hf).norm
  refine le_of_tendsto this (hCF.mono ?_)
  intro k hCFk
  exact (norm_apply_le_norm ENNReal.top_ne_zero (F k) a).trans hCFk

variable [_i : Fact (1 â‰¤ p)]

theorem sum_rpow_le_of_tendsto (hp : p â‰  âˆ) {C : â„} {F : Î¹ â†’ lp E p} (hCF : âˆ€á¶  k in l, â€–F kâ€– â‰¤ C)
    {f : âˆ€ a, E a} (hf : Tendsto (id fun i => F i : Î¹ â†’ âˆ€ a, E a) l (ğ“ f)) (s : Finset Î±) :
    âˆ‘ i âˆˆ s, â€–f iâ€– ^ p.toReal â‰¤ C ^ p.toReal := by
  have hp' : p â‰  0 := (zero_lt_one.trans_le _i.elim).ne'
  have hp'' : 0 < p.toReal := ENNReal.toReal_pos hp' hp
  let G : (âˆ€ a, E a) â†’ â„ := fun f => âˆ‘ a âˆˆ s, â€–f aâ€– ^ p.toReal
  have hG : Continuous G := by
    refine continuous_finset_sum s ?_
    intro a _
    have : Continuous fun f : âˆ€ a, E a => f a := continuous_apply a
    exact this.norm.rpow_const fun _ => Or.inr hp''.le
  refine le_of_tendsto (hG.continuousAt.tendsto.comp hf) ?_
  refine hCF.mono ?_
  intro k hCFk
  refine (lp.sum_rpow_le_norm_rpow hp'' (F k) s).trans ?_
  gcongr

/-- "Semicontinuity of the `lp` norm": If all sufficiently large elements of a sequence in `lp E p`
have `lp` norm `â‰¤ C`, then the pointwise limit, if it exists, also has `lp` norm `â‰¤ C`. -/
theorem norm_le_of_tendsto {C : â„} {F : Î¹ â†’ lp E p} (hCF : âˆ€á¶  k in l, â€–F kâ€– â‰¤ C) {f : lp E p}
    (hf : Tendsto (id fun i => F i : Î¹ â†’ âˆ€ a, E a) l (ğ“ f)) : â€–fâ€– â‰¤ C := by
  obtain âŸ¨i, hiâŸ© := hCF.exists
  have hC : 0 â‰¤ C := (norm_nonneg _).trans hi
  rcases eq_top_or_lt_top p with (rfl | hp)
  Â· apply norm_le_of_forall_le hC
    exact norm_apply_le_of_tendsto hCF hf
  Â· have : 0 < p := zero_lt_one.trans_le _i.elim
    have hp' : 0 < p.toReal := ENNReal.toReal_pos this.ne' hp.ne
    apply norm_le_of_forall_sum_le hp' hC
    exact sum_rpow_le_of_tendsto hp.ne hCF hf

/-- If `f` is the pointwise limit of a bounded sequence in `lp E p`, then `f` is in `lp E p`. -/
theorem memâ„“p_of_tendsto {F : Î¹ â†’ lp E p} (hF : Bornology.IsBounded (Set.range F)) {f : âˆ€ a, E a}
    (hf : Tendsto (id fun i => F i : Î¹ â†’ âˆ€ a, E a) l (ğ“ f)) : Memâ„“p f p := by
  obtain âŸ¨C, hCFâŸ© : âˆƒ C, âˆ€ k, â€–F kâ€– â‰¤ C := hF.exists_norm_le.imp fun _ â†¦ Set.forall_mem_range.1
  rcases eq_top_or_lt_top p with (rfl | hp)
  Â· apply memâ„“p_infty
    use C
    rintro _ âŸ¨a, rflâŸ©
    exact norm_apply_le_of_tendsto (Eventually.of_forall hCF) hf a
  Â· apply memâ„“p_gen'
    exact sum_rpow_le_of_tendsto hp.ne (Eventually.of_forall hCF) hf

/-- If a sequence is Cauchy in the `lp E p` topology and pointwise convergent to an element `f` of
`lp E p`, then it converges to `f` in the `lp E p` topology. -/
theorem tendsto_lp_of_tendsto_pi {F : â„• â†’ lp E p} (hF : CauchySeq F) {f : lp E p}
    (hf : Tendsto (id fun i => F i : â„• â†’ âˆ€ a, E a) atTop (ğ“ f)) : Tendsto F atTop (ğ“ f) := by
  rw [Metric.nhds_basis_closedBall.tendsto_right_iff]
  intro Îµ hÎµ
  have hÎµ' : { p : lp E p Ã— lp E p | â€–p.1 - p.2â€– < Îµ } âˆˆ uniformity (lp E p) :=
    NormedAddCommGroup.uniformity_basis_dist.mem_of_mem hÎµ
  refine (hF.eventually_eventually hÎµ').mono ?_
  rintro n (hn : âˆ€á¶  l in atTop, â€–(fun f => F n - f) (F l)â€– < Îµ)
  refine norm_le_of_tendsto (hn.mono fun k hk => hk.le) ?_
  rw [tendsto_pi_nhds]
  intro a
  exact (hf.apply_nhds a).const_sub (F n a)

variable [âˆ€ a, CompleteSpace (E a)]

instance completeSpace : CompleteSpace (lp E p) :=
  Metric.complete_of_cauchySeq_tendsto (by
    intro F hF
    -- A Cauchy sequence in `lp E p` is pointwise convergent; let `f` be the pointwise limit.
    obtain âŸ¨f, hfâŸ© := cauchySeq_tendsto_of_complete
      ((uniformContinuous_coe (p := p)).comp_cauchySeq hF)
    -- Since the Cauchy sequence is bounded, its pointwise limit `f` is in `lp E p`.
    have hf' : Memâ„“p f p := memâ„“p_of_tendsto hF.isBounded_range hf
    -- And therefore `f` is its limit in the `lp E p` topology as well as pointwise.
    exact âŸ¨âŸ¨f, hf'âŸ©, tendsto_lp_of_tendsto_pi hF hfâŸ©)

end Topology

end lp

section Lipschitz

open ENNReal lp
variable {Î¹ : Type*}

lemma LipschitzWith.uniformly_bounded [PseudoMetricSpace Î±] (g : Î± â†’ Î¹ â†’ â„) {K : â„â‰¥0}
    (hg : âˆ€ i, LipschitzWith K (g Â· i)) (aâ‚€ : Î±) (hgaâ‚€b : Memâ„“p (g aâ‚€) âˆ) (a : Î±) :
    Memâ„“p (g a) âˆ := by
  rcases hgaâ‚€b with âŸ¨M, hMâŸ©
  use â†‘K * dist a aâ‚€ + M
  rintro - âŸ¨i, rflâŸ©
  calc
    |g a i| = |g a i - g aâ‚€ i + g aâ‚€ i| := by simp
    _ â‰¤ |g a i - g aâ‚€ i| + |g aâ‚€ i| := abs_add _ _
    _ â‰¤ â†‘K * dist a aâ‚€ + M := by
        gcongr
        Â· exact lipschitzWith_iff_dist_le_mul.1 (hg i) a aâ‚€
        Â· exact hM âŸ¨i, rflâŸ©

theorem LipschitzOnWith.coordinate [PseudoMetricSpace Î±] (f : Î± â†’ â„“^âˆ(Î¹)) (s : Set Î±) (K : â„â‰¥0) :
    LipschitzOnWith K f s â†” âˆ€ i : Î¹, LipschitzOnWith K (fun a : Î± â†¦ f a i) s := by
  simp_rw [lipschitzOnWith_iff_dist_le_mul]
  constructor
  Â· intro hfl i x hx y hy
    calc
      dist (f x i) (f y i) â‰¤ dist (f x) (f y) := lp.norm_apply_le_norm top_ne_zero (f x - f y) i
      _ â‰¤ K * dist x y := hfl x hx y hy
  Â· intro hgl x hx y hy
    apply lp.norm_le_of_forall_le
    Â· positivity
    intro i
    apply hgl i x hx y hy

theorem LipschitzWith.coordinate [PseudoMetricSpace Î±] {f : Î± â†’ â„“^âˆ(Î¹)} (K : â„â‰¥0) :
    LipschitzWith K f â†” âˆ€ i : Î¹, LipschitzWith K (fun a : Î± â†¦ f a i) := by
  simp_rw [â† lipschitzOnWith_univ]
  apply LipschitzOnWith.coordinate

end Lipschitz
