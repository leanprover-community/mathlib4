/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.Normed.Affine.Isometry
import Mathlib.Topology.Algebra.ContinuousAffineMap
import Mathlib.Analysis.NormedSpace.OperatorNorm.NormedSpace

/-!
# Continuous affine maps between normed spaces.

This file develops the theory of continuous affine maps between affine spaces modelled on normed
spaces.

In the particular case that the affine spaces are just normed vector spaces `V`, `W`, we define a
norm on the space of continuous affine maps by defining the norm of `f : V â†’á´¬[ğ•œ] W` to be
`â€–fâ€– = max â€–f 0â€– â€–f.cont_linearâ€–`. This is chosen so that we have a linear isometry:
`(V â†’á´¬[ğ•œ] W) â‰ƒâ‚—áµ¢[ğ•œ] W Ã— (V â†’L[ğ•œ] W)`.

The abstract picture is that for an affine space `P` modelled on a vector space `V`, together with
a vector space `W`, there is an exact sequence of `ğ•œ`-modules: `0 â†’ C â†’ A â†’ L â†’ 0` where `C`, `A`
are the spaces of constant and affine maps `P â†’ W` and `L` is the space of linear maps `V â†’ W`.

Any choice of a base point in `P` corresponds to a splitting of this sequence so in particular if we
take `P = V`, using `0 : V` as the base point provides a splitting, and we prove this is an
isometric decomposition.

On the other hand, choosing a base point breaks the affine invariance so the norm fails to be
submultiplicative: for a composition of maps, we have only `â€–f.comp gâ€– â‰¤ â€–fâ€– * â€–gâ€– + â€–f 0â€–`.

## Main definitions:

* `ContinuousAffineMap.contLinear`
* `ContinuousAffineMap.hasNorm`
* `ContinuousAffineMap.norm_comp_le`
* `ContinuousAffineMap.toConstProdContinuousLinearMap`

-/


namespace ContinuousAffineMap

variable {ğ•œ R V W Wâ‚‚ P Q Qâ‚‚ : Type*}
variable [NormedAddCommGroup V] [MetricSpace P] [NormedAddTorsor V P]
variable [NormedAddCommGroup W] [MetricSpace Q] [NormedAddTorsor W Q]
variable [NormedAddCommGroup Wâ‚‚] [MetricSpace Qâ‚‚] [NormedAddTorsor Wâ‚‚ Qâ‚‚]
variable [NormedField R] [NormedSpace R V] [NormedSpace R W] [NormedSpace R Wâ‚‚]
variable [NontriviallyNormedField ğ•œ] [NormedSpace ğ•œ V] [NormedSpace ğ•œ W] [NormedSpace ğ•œ Wâ‚‚]

/-- The linear map underlying a continuous affine map is continuous. -/
def contLinear (f : P â†’á´¬[R] Q) : V â†’L[R] W :=
  { f.linear with
    toFun := f.linear
    cont := by rw [AffineMap.continuous_linear_iff]; exact f.cont }

@[simp]
theorem coe_contLinear (f : P â†’á´¬[R] Q) : (f.contLinear : V â†’ W) = f.linear :=
  rfl

@[simp]
theorem coe_contLinear_eq_linear (f : P â†’á´¬[R] Q) :
    (f.contLinear : V â†’â‚—[R] W) = (f : P â†’áµƒ[R] Q).linear := by ext; rfl

@[simp]
theorem coe_mk_const_linear_eq_linear (f : P â†’áµƒ[R] Q) (h) :
    ((âŸ¨f, hâŸ© : P â†’á´¬[R] Q).contLinear : V â†’ W) = f.linear :=
  rfl

theorem coe_linear_eq_coe_contLinear (f : P â†’á´¬[R] Q) :
    ((f : P â†’áµƒ[R] Q).linear : V â†’ W) = (â‡‘f.contLinear : V â†’ W) :=
  rfl

@[simp]
theorem comp_contLinear (f : P â†’á´¬[R] Q) (g : Q â†’á´¬[R] Qâ‚‚) :
    (g.comp f).contLinear = g.contLinear.comp f.contLinear :=
  rfl

@[simp]
theorem map_vadd (f : P â†’á´¬[R] Q) (p : P) (v : V) : f (v +áµ¥ p) = f.contLinear v +áµ¥ f p :=
  f.map_vadd' p v

@[simp]
theorem contLinear_map_vsub (f : P â†’á´¬[R] Q) (pâ‚ pâ‚‚ : P) : f.contLinear (pâ‚ -áµ¥ pâ‚‚) = f pâ‚ -áµ¥ f pâ‚‚ :=
  f.toAffineMap.linearMap_vsub pâ‚ pâ‚‚

@[simp]
theorem const_contLinear (q : Q) : (const R P q).contLinear = 0 :=
  rfl

theorem contLinear_eq_zero_iff_exists_const (f : P â†’á´¬[R] Q) :
    f.contLinear = 0 â†” âˆƒ q, f = const R P q := by
  have hâ‚ : f.contLinear = 0 â†” (f : P â†’áµƒ[R] Q).linear = 0 := by
    refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;> ext
    Â· rw [â† coe_contLinear_eq_linear, h]; rfl
    Â· rw [â† coe_linear_eq_coe_contLinear, h]; rfl
  have hâ‚‚ : âˆ€ q : Q, f = const R P q â†” (f : P â†’áµƒ[R] Q) = AffineMap.const R P q := by
    intro q
    refine âŸ¨fun h => ?_, fun h => ?_âŸ© <;> ext
    Â· rw [h]; rfl
    Â· rw [â† coe_toAffineMap, h, AffineMap.const_apply, coe_const, Function.const_apply]
  simp_rw [hâ‚, hâ‚‚]
  exact (f : P â†’áµƒ[R] Q).linear_eq_zero_iff_exists_const

@[simp]
theorem to_affine_map_contLinear (f : V â†’L[R] W) : f.toContinuousAffineMap.contLinear = f := by
  ext
  rfl

@[simp]
theorem zero_contLinear : (0 : P â†’á´¬[R] W).contLinear = 0 :=
  rfl

@[simp]
theorem add_contLinear (f g : P â†’á´¬[R] W) : (f + g).contLinear = f.contLinear + g.contLinear :=
  rfl

@[simp]
theorem sub_contLinear (f g : P â†’á´¬[R] W) : (f - g).contLinear = f.contLinear - g.contLinear :=
  rfl

@[simp]
theorem neg_contLinear (f : P â†’á´¬[R] W) : (-f).contLinear = -f.contLinear :=
  rfl

@[simp]
theorem smul_contLinear (t : R) (f : P â†’á´¬[R] W) : (t â€¢ f).contLinear = t â€¢ f.contLinear :=
  rfl

theorem decomp (f : V â†’á´¬[R] W) : (f : V â†’ W) = f.contLinear + Function.const V (f 0) := by
  rcases f with âŸ¨f, hâŸ©
  rw [coe_mk_const_linear_eq_linear, coe_mk, f.decomp, Pi.add_apply, LinearMap.map_zero, zero_add,
    â† Function.const_def]

/-- The space of continuous affine maps from `P` to `Q` is an affine space over the space of
continuous affine maps from `P` to `W`. -/
instance : AddTorsor (P â†’á´¬[R] W) (P â†’á´¬[R] Q) where
  vadd f g := { __ := f.toAffineMap +áµ¥ g.toAffineMap, cont := f.cont.vadd g.cont }
  zero_vadd _ := ext fun _ â†¦ zero_vadd _ _
  add_vadd _ _ _ := ext fun _ â†¦ add_vadd _ _ _
  vsub f g := { __ := f.toAffineMap -áµ¥ g.toAffineMap, cont := f.cont.vsub g.cont }
  vsub_vadd' _ _ := ext fun _ â†¦ vsub_vadd _ _
  vadd_vsub' _ _ := ext fun _ â†¦ vadd_vsub _ _

@[simp] lemma vadd_apply (f : P â†’á´¬[R] W) (g : P â†’á´¬[R] Q) (p : P) : (f +áµ¥ g) p = f p +áµ¥ g p :=
  rfl

@[simp] lemma vsub_apply (f g : P â†’á´¬[R] Q) (p : P) : (f -áµ¥ g) p = f p -áµ¥ g p :=
  rfl

@[simp] lemma vadd_toAffineMap (f : P â†’á´¬[R] W) (g : P â†’á´¬[R] Q) :
    (f +áµ¥ g).toAffineMap = f.toAffineMap +áµ¥ g.toAffineMap :=
  rfl

@[simp] lemma vsub_toAffineMap (f g : P â†’á´¬[R] Q) :
    (f -áµ¥ g).toAffineMap = f.toAffineMap -áµ¥ g.toAffineMap :=
  rfl

section NormedSpaceStructure

variable (f : V â†’á´¬[ğ•œ] W)

/-- Note that unlike the operator norm for linear maps, this norm is _not_ submultiplicative:
we do _not_ necessarily have `â€–f.comp gâ€– â‰¤ â€–fâ€– * â€–gâ€–`. See `norm_comp_le` for what we can say. -/
noncomputable instance hasNorm : Norm (V â†’á´¬[ğ•œ] W) :=
  âŸ¨fun f => max â€–f 0â€– â€–f.contLinearâ€–âŸ©

theorem norm_def : â€–fâ€– = max â€–f 0â€– â€–f.contLinearâ€– :=
  rfl

theorem norm_contLinear_le : â€–f.contLinearâ€– â‰¤ â€–fâ€– :=
  le_max_right _ _

theorem norm_image_zero_le : â€–f 0â€– â‰¤ â€–fâ€– :=
  le_max_left _ _

@[simp]
theorem norm_eq (h : f 0 = 0) : â€–fâ€– = â€–f.contLinearâ€– :=
  calc
    â€–fâ€– = max â€–f 0â€– â€–f.contLinearâ€– := by rw [norm_def]
    _ = max 0 â€–f.contLinearâ€– := by rw [h, norm_zero]
    _ = â€–f.contLinearâ€– := max_eq_right (norm_nonneg _)

noncomputable instance : NormedAddCommGroup (V â†’á´¬[ğ•œ] W) :=
  AddGroupNorm.toNormedAddCommGroup
    { toFun := fun f => max â€–f 0â€– â€–f.contLinearâ€–
      map_zero' := by simp [(ContinuousAffineMap.zero_apply)]
      neg' := fun f => by
        simp [(ContinuousAffineMap.neg_apply)]
      add_le' := fun f g => by
        simp only [coe_add, max_le_iff, Pi.add_apply, add_contLinear]
        exact
          âŸ¨(norm_add_le _ _).trans (add_le_add (le_max_left _ _) (le_max_left _ _)),
            (norm_add_le _ _).trans (add_le_add (le_max_right _ _) (le_max_right _ _))âŸ©
      eq_zero_of_map_eq_zero' := fun f hâ‚€ => by
        rcases max_eq_iff.mp hâ‚€ with (âŸ¨hâ‚, hâ‚‚âŸ© | âŸ¨hâ‚, hâ‚‚âŸ©) <;> rw [hâ‚] at hâ‚‚
        Â· rw [norm_le_zero_iff, contLinear_eq_zero_iff_exists_const] at hâ‚‚
          obtain âŸ¨q, rflâŸ© := hâ‚‚
          simp only [norm_eq_zero, coe_const, Function.const_apply] at hâ‚
          rw [hâ‚]
          rfl
        Â· rw [norm_eq_zero, contLinear_eq_zero_iff_exists_const] at hâ‚
          obtain âŸ¨q, rflâŸ© := hâ‚
          simp only [norm_le_zero_iff, coe_const, Function.const_apply] at hâ‚‚
          rw [hâ‚‚]
          rfl }

noncomputable instance : NormedSpace ğ•œ (V â†’á´¬[ğ•œ] W) where
  norm_smul_le t f := by
    simp only [norm_def, coe_smul, Pi.smul_apply, norm_smul, smul_contLinear,
      â† mul_max_of_nonneg _ _ (norm_nonneg t), le_refl]

theorem norm_comp_le (g : Wâ‚‚ â†’á´¬[ğ•œ] V) : â€–f.comp gâ€– â‰¤ â€–fâ€– * â€–gâ€– + â€–f 0â€– := by
  rw [norm_def, max_le_iff]
  constructor
  Â· calc
      â€–f.comp g 0â€– = â€–f (g 0)â€– := by simp
      _ = â€–f.contLinear (g 0) + f 0â€– := by rw [f.decomp]; simp
      _ â‰¤ â€–f.contLinearâ€– * â€–g 0â€– + â€–f 0â€– :=
        ((norm_add_le _ _).trans (add_le_add_right (f.contLinear.le_opNorm _) _))
      _ â‰¤ â€–fâ€– * â€–gâ€– + â€–f 0â€– :=
        add_le_add_right
          (mul_le_mul f.norm_contLinear_le g.norm_image_zero_le (norm_nonneg _) (norm_nonneg _)) _
  Â· calc
      â€–(f.comp g).contLinearâ€– â‰¤ â€–f.contLinearâ€– * â€–g.contLinearâ€– :=
        (g.comp_contLinear f).symm â–¸ f.contLinear.opNorm_comp_le _
      _ â‰¤ â€–fâ€– * â€–gâ€– :=
        (mul_le_mul f.norm_contLinear_le g.norm_contLinear_le (norm_nonneg _) (norm_nonneg _))
      _ â‰¤ â€–fâ€– * â€–gâ€– + â€–f 0â€– := by rw [le_add_iff_nonneg_right]; apply norm_nonneg

variable (ğ•œ V W)

/-- The space of affine maps between two normed spaces is linearly isometric to the product of the
codomain with the space of linear maps, by taking the value of the affine map at `(0 : V)` and the
linear part. -/
noncomputable def toConstProdContinuousLinearMap : (V â†’á´¬[ğ•œ] W) â‰ƒâ‚—áµ¢[ğ•œ] W Ã— (V â†’L[ğ•œ] W) where
  toFun f := âŸ¨f 0, f.contLinearâŸ©
  invFun p := p.2.toContinuousAffineMap + const ğ•œ V p.1
  left_inv f := by
    ext
    rw [f.decomp]
    simp only [coe_add, ContinuousLinearMap.coe_toContinuousAffineMap, Pi.add_apply, coe_const]
  right_inv := by rintro âŸ¨v, fâŸ©; ext <;> simp
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  norm_map' _ := rfl

@[simp]
theorem toConstProdContinuousLinearMap_fst (f : V â†’á´¬[ğ•œ] W) :
    (toConstProdContinuousLinearMap ğ•œ V W f).fst = f 0 :=
  rfl

@[simp]
theorem toConstProdContinuousLinearMap_snd (f : V â†’á´¬[ğ•œ] W) :
    (toConstProdContinuousLinearMap ğ•œ V W f).snd = f.contLinear :=
  rfl

end NormedSpaceStructure

end ContinuousAffineMap
