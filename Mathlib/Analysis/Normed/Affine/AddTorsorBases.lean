/-
Copyright (c) 2021 Oliver Nash. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Oliver Nash
-/
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.LinearAlgebra.AffineSpace.FiniteDimensional

/-!
# Bases in normed affine spaces.

This file contains results about bases in normed affine spaces.

## Main definitions:

* `continuous_barycentric_coord`
* `isOpenMap_barycentric_coord`
* `AffineBasis.interior_convexHull`
* `IsOpen.exists_subset_affineIndependent_span_eq_top`
* `interior_convexHull_nonempty_iff_affineSpan_eq_top`
-/

assert_not_exists HasFDerivAt

section Barycentric

variable {Î¹ ğ•œ E P : Type*} [NontriviallyNormedField ğ•œ] [CompleteSpace ğ•œ]
variable [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable [MetricSpace P] [NormedAddTorsor E P]

theorem isOpenMap_barycentric_coord [Nontrivial Î¹] (b : AffineBasis Î¹ ğ•œ P) (i : Î¹) :
    IsOpenMap (b.coord i) :=
  AffineMap.isOpenMap_linear_iff.mp <|
    (b.coord i).linear.isOpenMap_of_finiteDimensional <|
      (b.coord i).linear_surjective_iff.mpr (b.surjective_coord i)

variable [FiniteDimensional ğ•œ E] (b : AffineBasis Î¹ ğ•œ P)

@[continuity]
theorem continuous_barycentric_coord (i : Î¹) : Continuous (b.coord i) :=
  (b.coord i).continuous_of_finiteDimensional

end Barycentric

open Set

/-- Given a finite-dimensional normed real vector space, the interior of the convex hull of an
affine basis is the set of points whose barycentric coordinates are strictly positive with respect
to this basis.

TODO Restate this result for affine spaces (instead of vector spaces) once the definition of
convexity is generalised to this setting. -/
theorem AffineBasis.interior_convexHull {Î¹ E : Type*} [Finite Î¹] [NormedAddCommGroup E]
    [NormedSpace â„ E] (b : AffineBasis Î¹ â„ E) :
    interior (convexHull â„ (range b)) = {x | âˆ€ i, 0 < b.coord i x} := by
  cases subsingleton_or_nontrivial Î¹
  Â· -- The zero-dimensional case.
    have : range b = univ :=
      AffineSubspace.eq_univ_of_subsingleton_span_eq_top (subsingleton_range _) b.tot
    simp [this]
  Â· -- The positive-dimensional case.
    haveI : FiniteDimensional â„ E := b.finiteDimensional
    have : convexHull â„ (range b) = â‹‚ i, b.coord i â»Â¹' Ici 0 := by
      rw [b.convexHull_eq_nonneg_coord, setOf_forall]; rfl
    ext
    simp only [this, interior_iInter_of_finite, â†
      IsOpenMap.preimage_interior_eq_interior_preimage (isOpenMap_barycentric_coord b _)
        (continuous_barycentric_coord b _),
      interior_Ici, mem_iInter, mem_setOf_eq, mem_Ioi, mem_preimage]

variable {V P : Type*} [NormedAddCommGroup V] [NormedSpace â„ V] [MetricSpace P]
  [NormedAddTorsor V P]

open AffineMap

/-- Given a set `s` of affine-independent points belonging to an open set `u`, we may extend `s` to
an affine basis, all of whose elements belong to `u`. -/
theorem IsOpen.exists_between_affineIndependent_span_eq_top {s u : Set P} (hu : IsOpen u)
    (hsu : s âŠ† u) (hne : s.Nonempty) (h : AffineIndependent â„ ((â†‘) : s â†’ P)) :
    âˆƒ t : Set P, s âŠ† t âˆ§ t âŠ† u âˆ§ AffineIndependent â„ ((â†‘) : t â†’ P) âˆ§ affineSpan â„ t = âŠ¤ := by
  obtain âŸ¨q, hqâŸ© := hne
  obtain âŸ¨Îµ, Îµ0, hÎµuâŸ© := Metric.nhds_basis_closedBall.mem_iff.1 (hu.mem_nhds <| hsu hq)
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒâŸ© := exists_subset_affineIndependent_affineSpan_eq_top h
  let f : P â†’ P := fun y â†¦ lineMap q y (Îµ / dist y q)
  have hf : âˆ€ y, f y âˆˆ u := by
    refine fun y â†¦ hÎµu ?_
    simp only [f]
    rw [Metric.mem_closedBall, lineMap_apply, dist_vadd_left, norm_smul, Real.norm_eq_abs,
      dist_eq_norm_vsub V y q, abs_div, abs_of_pos Îµ0, abs_of_nonneg (norm_nonneg _), div_mul_comm]
    exact mul_le_of_le_one_left Îµ0.le (div_self_le_one _)
  have hÎµyq : âˆ€ y âˆ‰ s, Îµ / dist y q â‰  0 := fun y hy â†¦
    div_ne_zero Îµ0.ne' (dist_ne_zero.2 (ne_of_mem_of_not_mem hq hy).symm)
  classical
  let w : t â†’ â„Ë£ := fun p â†¦ if hp : (p : P) âˆˆ s then 1 else Units.mk0 _ (hÎµyq (â†‘p) hp)
  refine âŸ¨Set.range fun p : t â†¦ lineMap q p (w p : â„), ?_, ?_, ?_, ?_âŸ©
  Â· intro p hp; use âŸ¨p, htâ‚ hpâŸ©; simp [w, hp]
  Â· rintro y âŸ¨âŸ¨p, hpâŸ©, rflâŸ©
    by_cases hps : p âˆˆ s <;>
    simp only [w, hps, lineMap_apply_one, Units.val_mk0, dif_neg, dif_pos, not_false_iff,
      Units.val_one] <;>
    [exact hsu hps; exact hf p]
  Â· exact (htâ‚‚.units_lineMap âŸ¨q, htâ‚ hqâŸ© w).range
  Â· rw [affineSpan_eq_affineSpan_lineMap_units (htâ‚ hq) w, htâ‚ƒ]

theorem IsOpen.exists_subset_affineIndependent_span_eq_top {u : Set P} (hu : IsOpen u)
    (hne : u.Nonempty) : âˆƒ s âŠ† u, AffineIndependent â„ ((â†‘) : s â†’ P) âˆ§ affineSpan â„ s = âŠ¤ := by
  rcases hne with âŸ¨x, hxâŸ©
  rcases hu.exists_between_affineIndependent_span_eq_top (singleton_subset_iff.mpr hx)
    (singleton_nonempty _) (affineIndependent_of_subsingleton _ _) with âŸ¨s, -, hsu, hsâŸ©
  exact âŸ¨s, hsu, hsâŸ©

/-- The affine span of a nonempty open set is `âŠ¤`. -/
theorem IsOpen.affineSpan_eq_top {u : Set P} (hu : IsOpen u) (hne : u.Nonempty) :
    affineSpan â„ u = âŠ¤ :=
  let âŸ¨_, hsu, _, hs'âŸ© := hu.exists_subset_affineIndependent_span_eq_top hne
  top_unique <| hs' â–¸ affineSpan_mono _ hsu

theorem affineSpan_eq_top_of_nonempty_interior {s : Set V}
    (hs : (interior <| convexHull â„ s).Nonempty) : affineSpan â„ s = âŠ¤ :=
  top_unique <| isOpen_interior.affineSpan_eq_top hs â–¸
    (affineSpan_mono _ interior_subset).trans_eq (affineSpan_convexHull _)

theorem AffineBasis.centroid_mem_interior_convexHull {Î¹} [Fintype Î¹] (b : AffineBasis Î¹ â„ V) :
    Finset.univ.centroid â„ b âˆˆ interior (convexHull â„ (range b)) := by
  haveI := b.nonempty
  simp only [b.interior_convexHull, mem_setOf_eq, b.coord_apply_centroid (Finset.mem_univ _),
    inv_pos, Nat.cast_pos, Finset.card_pos, Finset.univ_nonempty, forall_true_iff]

theorem interior_convexHull_nonempty_iff_affineSpan_eq_top [FiniteDimensional â„ V] {s : Set V} :
    (interior (convexHull â„ s)).Nonempty â†” affineSpan â„ s = âŠ¤ := by
  refine âŸ¨affineSpan_eq_top_of_nonempty_interior, fun h â†¦ ?_âŸ©
  obtain âŸ¨t, hts, b, hbâŸ© := AffineBasis.exists_affine_subbasis h
  suffices (interior (convexHull â„ (range b))).Nonempty by
    rw [hb, Subtype.range_coe_subtype, setOf_mem_eq] at this
    refine this.mono (by gcongr)
  lift t to Finset V using b.finite_set
  exact âŸ¨_, b.centroid_mem_interior_convexHullâŸ©

theorem Convex.interior_nonempty_iff_affineSpan_eq_top [FiniteDimensional â„ V] {s : Set V}
    (hs : Convex â„ s) : (interior s).Nonempty â†” affineSpan â„ s = âŠ¤ := by
  rw [â† interior_convexHull_nonempty_iff_affineSpan_eq_top, hs.convexHull_eq]
