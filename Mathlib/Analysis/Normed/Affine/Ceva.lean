/-
Copyright (c) 2025 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers
-/
module

public import Mathlib.Analysis.Normed.Affine.AddTorsor
public import Mathlib.LinearAlgebra.AffineSpace.Ceva

/-!
# Ceva's theorem.

This file proves various versions of Ceva's theorem in a `NormedAddTorsor`.

## References

* https://en.wikipedia.org/wiki/Ceva%27s_theorem

-/

@[expose] public section


open scoped Affine

variable {ğ•œ V P : Type*} [SeminormedAddCommGroup V] [NormedField ğ•œ] [NormedSpace ğ•œ V]

namespace Affine.Triangle

variable [PseudoMetricSpace P] [NormedAddTorsor V P] in
/-- **Ceva's theorem** for a triangle, expressed in terms of multiplying distances. -/
lemma prod_dist_eq_prod_dist_of_mem_line_of_mem_line {t : Triangle ğ•œ P} {p : Fin 3 â†’ P} {p' : P}
    (hp : âˆ€ i : Fin 3, p i âˆˆ line[ğ•œ, t.points (i + 1), t.points (i + 2)])
    (hp' : âˆ€ i : Fin 3, p' âˆˆ line[ğ•œ, t.points i, p i]) :
    âˆ i, dist (t.points (i + 1)) (p i) = âˆ i, dist (p i) (t.points (i + 2)) := by
  simp_rw [mem_affineSpan_pair_iff_exists_lineMap_eq] at hp
  choose r hr using hp
  simp_rw [â† hr] at hp'
  simp_rw [â† hr, dist_lineMap_right, dist_left_lineMap, Finset.prod_mul_distrib, â† norm_prod,
    prod_eq_prod_one_sub_of_mem_line_point_lineMap hp']

variable [MetricSpace P] [NormedAddTorsor V P] in
/-- **Ceva's theorem** for a triangle, expressed using division of distances. -/
lemma prod_dist_div_dist_eq_one_of_mem_line_of_mem_line {t : Triangle ğ•œ P} {p : Fin 3 â†’ P} {p' : P}
    (hp0 : âˆ€ i, p i â‰  t.points (i + 1))
    (hp : âˆ€ i : Fin 3, p i âˆˆ line[ğ•œ, t.points (i + 1), t.points (i + 2)])
    (hp' : âˆ€ i : Fin 3, p' âˆˆ line[ğ•œ, t.points i, p i]) :
    âˆ i, dist (t.points (i + 1)) (p i) / dist (p i) (t.points (i + 2)) = 1 := by
  simp_rw [mem_affineSpan_pair_iff_exists_lineMap_eq] at hp
  choose r hr using hp
  simp_rw [â† hr] at hp'
  simp_rw [â† hr, dist_lineMap_right, dist_left_lineMap, Finset.prod_div_distrib,
    Finset.prod_mul_distrib, â† norm_prod]
  rw [mul_div_mul_right]
  Â· rw [â† norm_div, â† Finset.prod_div_distrib]
    have hr0 : âˆ€ i, r i â‰  0 := by
      intro i hri
      apply hp0 i
      simpa [hri] using (hr i).symm
    rw [prod_div_one_sub_eq_one_of_mem_line_point_lineMap hr0 hp', norm_one]
  Â· rw [Finset.prod_ne_zero_iff]
    rintro i -
    rw [dist_ne_zero]
    exact t.independent.injective.ne (by grind)

end Affine.Triangle
