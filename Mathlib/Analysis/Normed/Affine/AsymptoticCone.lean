/-
Copyright (c) 2025 Attila G√°sp√°r. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Attila G√°sp√°r
-/
import Mathlib.Analysis.Normed.Module.FiniteDimension
import Mathlib.Topology.Algebra.AsymptoticCone

/-!
# Asymptotic cones in normed spaces

In this file, we prove that the asymptotic cone of a set is non-trivial if and only if the set is
unbounded.
-/

open AffineSpace Bornology Filter Topology

variable
  {V P : Type*} [NormedAddCommGroup V] [NormedSpace ‚Ñù V] [MetricSpace P] [NormedAddTorsor V P]

theorem AffineSpace.asymptoticNhds_le_cobounded {v : V} (hv : v ‚â† 0) :
    asymptoticNhds ‚Ñù P v ‚â§ cobounded P := by
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  rw [‚Üê tendsto_id', ‚Üê Metric.tendsto_dist_right_atTop_iff p,
    asymptoticNhds_eq_smul_vadd v p, vadd_pure, ‚Üê map‚ÇÇ_smul, ‚Üê map_prod_eq_map‚ÇÇ, map_map,
    tendsto_map'_iff]
  change Tendsto (fun x : ‚Ñù √ó V => dist (x.1 ‚Ä¢ x.2 +·µ• p) p) (atTop √óÀ¢ ùìù v) atTop
  simp_rw [dist_vadd_left, norm_smul]
  exact Tendsto.atTop_mul_pos (norm_pos_iff.mpr hv)
    (tendsto_norm_atTop_atTop.comp tendsto_id.fst)
    tendsto_snd.norm

theorem asymptoticCone_subset_singleton_of_bounded {s : Set P} (hs : IsBounded s) :
    asymptoticCone ‚Ñù s ‚äÜ {0} := by
  intro v h
  by_contra! hv
  exact h (asymptoticNhds_le_cobounded hv hs)

variable [FiniteDimensional ‚Ñù V]

theorem AffineSpace.cobounded_eq_iSup_sphere_asymptoticNhds :
    cobounded P = ‚®Ü v ‚àà Metric.sphere 0 1, asymptoticNhds ‚Ñù P v := by
  refine le_antisymm ?_ <| iSup‚ÇÇ_le fun _ h => asymptoticNhds_le_cobounded <|
    Metric.ne_of_mem_sphere h one_ne_zero
  intro s hs
  have ‚ü®p‚ü© : Nonempty P := inferInstance
  simp_rw [mem_iSup, asymptoticNhds_eq_smul_vadd _ p, vadd_pure] at hs
  choose! t ht u hu smul_subset_s using hs
  have ‚ü®cover, h‚ÇÅ, h‚ÇÇ‚ü© := (isCompact_sphere 0 1).elim_nhds_subcover u hu
  rw [‚Üê Metric.comap_dist_left_atTop p]
  refine ‚ü®Set.Ioi 0 ‚à© ‚ãÇ x ‚àà cover, t x, inter_mem (Ioi_mem_atTop 0)
    (cover.iInter_mem_sets.mpr fun x hx => ht x (h‚ÇÅ x hx)), fun x hx => ?_‚ü©
  rw [Set.mem_preimage, dist_eq_norm_vsub'] at hx
  let x' := ‚Äñx -·µ• p‚Äñ‚Åª¬π ‚Ä¢ (x -·µ• p)
  have x'_mem : x' ‚àà Metric.sphere 0 1 := by
    rw [mem_sphere_zero_iff_norm, norm_smul, norm_inv, norm_norm, inv_mul_cancel‚ÇÄ hx.1.ne']
  have ‚ü®y, y_mem, hy‚ü© := Set.mem_iUnion‚ÇÇ.mp (h‚ÇÇ x'_mem)
  rw [‚Üê vsub_vadd x p, ‚Üê show ‚Äñx -·µ• p‚Äñ ‚Ä¢ x' = x -·µ• p from smul_inv_smul‚ÇÄ hx.1.ne' (x -·µ• p)]
  exact smul_subset_s y (h‚ÇÅ y y_mem) <| Set.smul_mem_smul (Set.biInter_subset_of_mem y_mem hx.2) hy

/-- In a finite dimensional normed affine space over `‚Ñù`, a set is bounded if and only if its
asymptotic cone is trivial. -/
theorem isBounded_iff_asymptoticCone_subset_singleton {s : Set P} :
    IsBounded s ‚Üî asymptoticCone ‚Ñù s ‚äÜ {0} := by
  refine ‚ü®asymptoticCone_subset_singleton_of_bounded, fun h => ?_‚ü©
  simp_rw [isBounded_def, cobounded_eq_iSup_sphere_asymptoticNhds, mem_iSup]
  intro v hv
  by_contra h'
  exact Metric.ne_of_mem_sphere hv one_ne_zero (h h')

/-- In a finite dimensional normed affine space over `‚Ñù`, a set is unbounded if and only if its
asymptotic cone contains a nonzero vector. -/
theorem not_bounded_iff_exists_ne_zero_mem_asymptoticCone {s : Set P} :
    ¬¨ IsBounded s ‚Üî ‚àÉ v ‚â† 0, v ‚àà asymptoticCone ‚Ñù s := by
  rw [isBounded_iff_asymptoticCone_subset_singleton, Set.subset_singleton_iff, not_forall]
  tauto
