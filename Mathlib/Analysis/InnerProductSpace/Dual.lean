/-
Copyright (c) 2020 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/
module

public import Mathlib.Analysis.InnerProductSpace.Projection.Submodule
public import Mathlib.Analysis.Normed.Group.NullSubmodule
public import Mathlib.Topology.Algebra.Module.PerfectPairing

/-!
# The FrÃ©chet-Riesz representation theorem

We consider an inner product space `E` over `ğ•œ`, which is either `â„` or `â„‚`. We define
`toDualMap`, a conjugate-linear isometric embedding of `E` into its dual, which maps an element `x`
of the space to `fun y => âŸªx, yâŸ«`.

Under the hypothesis of completeness (i.e., for Hilbert spaces), we upgrade this to `toDual`, a
conjugate-linear isometric *equivalence* of `E` onto its dual; that is, we establish the
surjectivity of `toDualMap`.  This is the FrÃ©chet-Riesz representation theorem: every element of the
dual of a Hilbert space `E` has the form `fun u => âŸªx, uâŸ«` for some `x : E`.

For a bounded sesquilinear form `B : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ`,
we define a map `InnerProductSpace.continuousLinearMapOfBilin B : E â†’L[ğ•œ] E`,
given by substituting `E â†’L[ğ•œ] ğ•œ` with `E` using `toDual`.


## References

* [M. Einsiedler and T. Ward, *Functional Analysis, Spectral Theory, and Applications*]
  [EinsiedlerWard2017]

## Tags

dual, FrÃ©chet-Riesz
-/

@[expose] public section

noncomputable section

open ComplexConjugate Module

universe u v

namespace InnerProductSpace

open RCLike ContinuousLinearMap

variable (ğ•œ E : Type*)

section Seminormed

variable [RCLike ğ•œ] [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

local postfix:90 "â€ " => starRingEnd _

/-- An element `x` of an inner product space `E` induces an element of the strong dual space
`StrongDual ğ•œ E`, the map `fun y => âŸªx, yâŸ«`; moreover this operation is a conjugate-linear isometric
embedding of `E` into `StrongDual ğ•œ E`.
If `E` is complete, this operation is surjective, hence a conjugate-linear isometric equivalence;
see `toDual`.
-/
def toDualMap : E â†’â‚—áµ¢â‹†[ğ•œ] StrongDual ğ•œ E :=
  { innerSL ğ•œ with norm_map' := innerSL_apply_norm _ }

variable {E}

@[simp]
theorem toContinuousLinearMap_toDualMap :
    (toDualMap ğ•œ E).toContinuousLinearMap = innerSL ğ•œ := rfl

@[simp]
theorem toDualMap_apply_apply {x y : E} : toDualMap ğ•œ E x y = âŸªx, yâŸ« := rfl

@[deprecated (since := "2025-11-15")] alias toDualMap_apply := toDualMap_apply_apply

variable {ğ•œ} in
@[simp]
theorem _root_.innerSL_inj {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] {x y : E} :
    innerSL ğ•œ x = innerSL ğ•œ y â†” x = y :=
  (toDualMap ğ•œ E).injective.eq_iff

section NullSubmodule

open LinearMap

/-- For each `x : E`, the kernel of `âŸªx, â¬âŸ«` includes the null space. -/
lemma nullSubmodule_le_ker_toDualMap_right (x : E) : nullSubmodule ğ•œ E â‰¤ (toDualMap ğ•œ E x).ker :=
  fun _ hx â†¦ inner_eq_zero_of_right x ((mem_nullSubmodule_iff).mp hx)

/-- The kernel of the map `x â†¦ âŸªÂ·, xâŸ«` includes the null space. -/
lemma nullSubmodule_le_ker_toDualMap_left : nullSubmodule ğ•œ E â‰¤ (toDualMap ğ•œ E).ker :=
  fun _ hx â†¦ ContinuousLinearMap.ext <| fun y â†¦ inner_eq_zero_of_left y hx

end NullSubmodule

end Seminormed

section Normed
variable [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

local postfix:90 "â€ " => starRingEnd _

theorem innerSL_norm [Nontrivial E] : â€–(innerSL ğ•œ : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ)â€– = 1 :=
  show â€–(toDualMap ğ•œ E).toContinuousLinearMapâ€– = 1 from LinearIsometry.norm_toContinuousLinearMap _

variable {E ğ•œ}

theorem ext_inner_left_basis {Î¹ : Type*} {x y : E} (b : Basis Î¹ ğ•œ E)
    (h : âˆ€ i : Î¹, âŸªb i, xâŸ« = âŸªb i, yâŸ«) : x = y := by
  apply (toDualMap ğ•œ E).map_eq_iff.mp
  refine (Function.Injective.eq_iff ContinuousLinearMap.coe_injective).mp (b.ext ?_)
  intro i
  simp only [ContinuousLinearMap.coe_coe, toDualMap_apply_apply]
  rw [â† inner_conj_symm]
  conv_rhs => rw [â† inner_conj_symm]
  exact congr_arg conj (h i)

theorem ext_inner_right_basis {Î¹ : Type*} {x y : E} (b : Basis Î¹ ğ•œ E)
    (h : âˆ€ i : Î¹, âŸªx, b iâŸ« = âŸªy, b iâŸ«) : x = y := by
  refine ext_inner_left_basis b fun i => ?_
  rw [â† inner_conj_symm]
  conv_rhs => rw [â† inner_conj_symm]
  exact congr_arg conj (h i)

variable (ğ•œ) (E)
variable [CompleteSpace E]

/-- **FrÃ©chet-Riesz representation**: any `â„“` in the dual of a Hilbert space `E` is of the form
`fun u => âŸªy, uâŸ«` for some `y : E`, i.e. `toDualMap` is surjective.
-/
def toDual : E â‰ƒâ‚—áµ¢â‹†[ğ•œ] StrongDual ğ•œ E :=
  LinearIsometryEquiv.ofSurjective (toDualMap ğ•œ E)
    (by
      intro â„“
      set Y := â„“.ker
      by_cases htriv : Y = âŠ¤
      Â· have hâ„“ : â„“ = 0 := by
          have h' := LinearMap.ker_eq_top.mp htriv
          rw [â† coe_zero] at h'
          apply coe_injective
          exact h'
        exact âŸ¨0, by simp [hâ„“]âŸ©
      Â· rw [â† Submodule.orthogonal_eq_bot_iff] at htriv
        change Yá—® â‰  âŠ¥ at htriv
        rw [Submodule.ne_bot_iff] at htriv
        obtain âŸ¨z : E, hz : z âˆˆ Yá—®, z_ne_0 : z â‰  0âŸ© := htriv
        refine âŸ¨(starRingEnd (R := ğ•œ) (â„“ z) / âŸªz, zâŸ«) â€¢ z, ?_âŸ©
        apply ContinuousLinearMap.ext
        intro x
        have hâ‚ : â„“ z â€¢ x - â„“ x â€¢ z âˆˆ Y := by
          rw [LinearMap.mem_ker, map_sub, map_smul, map_smul, smul_eq_mul,
            smul_eq_mul, mul_comm]
          exact sub_self (â„“ x * â„“ z)
        have hâ‚‚ : â„“ z * âŸªz, xâŸ« = â„“ x * âŸªz, zâŸ« :=
          haveI hâ‚ƒ :=
            calc
              0 = âŸªz, â„“ z â€¢ x - â„“ x â€¢ zâŸ« := by
                rw [(Y.mem_orthogonal' z).mp hz]
                exact hâ‚
              _ = âŸªz, â„“ z â€¢ xâŸ« - âŸªz, â„“ x â€¢ zâŸ« := by rw [inner_sub_right]
              _ = â„“ z * âŸªz, xâŸ« - â„“ x * âŸªz, zâŸ« := by simp [inner_smul_right]
          sub_eq_zero.mp hâ‚ƒ.symm
        calc
          âŸª(â„“ zâ€  / âŸªz, zâŸ«) â€¢ z, xâŸ« = â„“ z / âŸªz, zâŸ« * âŸªz, xâŸ« := by simp [inner_smul_left]
          _ = â„“ z * âŸªz, xâŸ« / âŸªz, zâŸ« := by rw [â† div_mul_eq_mul_div]
          _ = â„“ x * âŸªz, zâŸ« / âŸªz, zâŸ« := by rw [hâ‚‚]
          _ = â„“ x := by have : âŸªz, zâŸ« â‰  0 := inner_self_ne_zero.mpr z_ne_0; field)

variable {ğ•œ} {E}

@[simp]
theorem toDual_apply_apply {x y : E} : toDual ğ•œ E x y = âŸªx, yâŸ« := rfl

@[deprecated (since := "2025-11-15")] alias toDual_apply := toDual_apply_apply

@[simp]
theorem toDual_symm_apply {x : E} {y : StrongDual ğ•œ E} : âŸª(toDual ğ•œ E).symm y, xâŸ« = y x := by
  rw [â† toDual_apply_apply]
  simp only [LinearIsometryEquiv.apply_symm_apply]

@[simp]
lemma toLinearIsometry_toDual :
    (toDual ğ•œ E).toLinearIsometry = toDualMap ğ•œ E := rfl

lemma toDual_apply_eq_toDualMap_apply (x : E) :
    toDual ğ•œ E x = toDualMap ğ•œ E x := rfl

/-- Maps a bounded sesquilinear form to its continuous linear map,
given by interpreting the form as a map `B : E â†’Lâ‹†[ğ•œ] StrongDual ğ•œ E`
and dualizing the result using `toDual`.
-/
def continuousLinearMapOfBilin (B : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ) : E â†’L[ğ•œ] E :=
  (toDual ğ•œ E).symm.toContinuousLinearEquiv.toContinuousLinearMap.comp B

local postfix:1024 "â™¯" => continuousLinearMapOfBilin

variable (B : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ)

@[simp]
theorem continuousLinearMapOfBilin_zero : (0 : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ)â™¯ = 0 := by
  simp [continuousLinearMapOfBilin]

@[simp]
theorem continuousLinearMapOfBilin_apply (v w : E) : âŸªBâ™¯ v, wâŸ« = B v w := by
  rw [continuousLinearMapOfBilin, coe_comp', ContinuousLinearEquiv.coe_coe,
    LinearIsometryEquiv.coe_toContinuousLinearEquiv, Function.comp_apply, toDual_symm_apply]

theorem unique_continuousLinearMapOfBilin {v f : E} (is_lax_milgram : âˆ€ w, âŸªf, wâŸ« = B v w) :
    f = Bâ™¯ v := by
  refine ext_inner_right ğ•œ ?_
  intro w
  rw [continuousLinearMapOfBilin_apply]
  exact is_lax_milgram w

end Normed

instance [NormedAddCommGroup E] [CompleteSpace E] [InnerProductSpace â„ E] :
    (innerâ‚— E).IsContPerfPair where
  continuous_uncurry := continuous_inner
  bijective_left := (toDual â„ E).bijective
  bijective_right := by
    convert (toDual â„ E).bijective
    ext y
    simp

/-- A nonzero rank-one operator has rank one. -/
lemma rank_rankOne {ğ•œ E F : Type*} [RCLike ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
    [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] {x : E} {y : F} (hx : x â‰  0) (hy : y â‰  0) :
    (rankOne ğ•œ x y).rank = 1 := by
  rw [LinearMap.rank, rankOne_def, range_smulRight_apply, Module.rank_eq_one_iff_finrank_eq_one]
  Â· exact finrank_span_singleton hx
  Â· exact map_eq_zero_iff _ (toDualMap ğ•œ F).injective |>.not.mpr hy

end InnerProductSpace
