/-
Copyright (c) 2022 Daniel Roca GonzÃ¡lez. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Daniel Roca GonzÃ¡lez
-/
import Mathlib.Analysis.InnerProductSpace.Dual

/-!
# The Lax-Milgram Theorem

We consider a Hilbert space `V` over `â„`
equipped with a bounded bilinear form `B : V â†’L[â„] V â†’L[â„] â„`.

Recall that a bilinear form `B : V â†’L[â„] V â†’L[â„] â„` is *coercive*
iff `âˆƒ C, (0 < C) âˆ§ âˆ€ u, C * â€–uâ€– * â€–uâ€– â‰¤ B u u`.
Under the hypothesis that `B` is coercive we prove the Lax-Milgram theorem:
that is, the map `InnerProductSpace.continuousLinearMapOfBilin` from
`Analysis.InnerProductSpace.Dual` can be upgraded to a continuous equivalence
`IsCoercive.continuousLinearEquivOfBilin : V â‰ƒL[â„] V`.

## References

* We follow the notes of Peter Howard's Spring 2020 *M612: Partial Differential Equations* lecture,
  see[howard]

## Tags

dual, Lax-Milgram
-/


noncomputable section

open RCLike LinearMap ContinuousLinearMap InnerProductSpace

open LinearMap (ker range)

open RealInnerProductSpace NNReal

universe u

namespace IsCoercive

variable {V : Type u} [NormedAddCommGroup V] [InnerProductSpace â„ V] [CompleteSpace V]
variable {B : V â†’L[â„] V â†’L[â„] â„}

local postfix:1024 "â™¯" => continuousLinearMapOfBilin (ğ•œ := â„)

theorem bounded_below (coercive : IsCoercive B) : âˆƒ C, 0 < C âˆ§ âˆ€ v, C * â€–vâ€– â‰¤ â€–Bâ™¯ vâ€– := by
  rcases coercive with âŸ¨C, C_ge_0, coercivityâŸ©
  refine âŸ¨C, C_ge_0, ?_âŸ©
  intro v
  by_cases h : 0 < â€–vâ€–
  Â· refine (mul_le_mul_right h).mp ?_
    calc
      C * â€–vâ€– * â€–vâ€– â‰¤ B v v := coercivity v
      _ = âŸªBâ™¯ v, vâŸ«_â„ := (continuousLinearMapOfBilin_apply B v v).symm
      _ â‰¤ â€–Bâ™¯ vâ€– * â€–vâ€– := real_inner_le_norm (Bâ™¯ v) v
  Â· have : v = 0 := by simpa using h
    simp [this]

theorem antilipschitz (coercive : IsCoercive B) : âˆƒ C : â„â‰¥0, 0 < C âˆ§ AntilipschitzWith C Bâ™¯ := by
  rcases coercive.bounded_below with âŸ¨C, C_pos, below_boundâŸ©
  refine âŸ¨Câ»Â¹.toNNReal, Real.toNNReal_pos.mpr (inv_pos.mpr C_pos), ?_âŸ©
  refine ContinuousLinearMap.antilipschitz_of_bound Bâ™¯ ?_
  simp_rw [Real.coe_toNNReal', max_eq_left_of_lt (inv_pos.mpr C_pos), â†
    inv_mul_le_iffâ‚€ (inv_pos.mpr C_pos)]
  simpa using below_bound

theorem ker_eq_bot (coercive : IsCoercive B) : ker Bâ™¯ = âŠ¥ := by
  rw [LinearMapClass.ker_eq_bot]
  rcases coercive.antilipschitz with âŸ¨_, _, antilipschitzâŸ©
  exact antilipschitz.injective

theorem isClosed_range (coercive : IsCoercive B) : IsClosed (range Bâ™¯ : Set V) := by
  rcases coercive.antilipschitz with âŸ¨_, _, antilipschitzâŸ©
  exact antilipschitz.isClosed_range Bâ™¯.uniformContinuous


theorem range_eq_top (coercive : IsCoercive B) : range Bâ™¯ = âŠ¤ := by
  haveI := coercive.isClosed_range.completeSpace_coe
  rw [â† (range Bâ™¯).orthogonal_orthogonal]
  rw [Submodule.eq_top_iff']
  intro v w mem_w_orthogonal
  rcases coercive with âŸ¨C, C_pos, coercivityâŸ©
  obtain rfl : w = 0 := by
    rw [â† norm_eq_zero, â† mul_self_eq_zero, â† mul_right_inj' C_pos.ne', mul_zero, â†
      mul_assoc]
    apply le_antisymm
    Â· calc
        C * â€–wâ€– * â€–wâ€– â‰¤ B w w := coercivity w
        _ = âŸªBâ™¯ w, wâŸ«_â„ := (continuousLinearMapOfBilin_apply B w w).symm
        _ = 0 := mem_w_orthogonal _ âŸ¨w, rflâŸ©
    Â· positivity
  exact inner_zero_left _

/-- The Lax-Milgram equivalence of a coercive bounded bilinear operator:
for all `v : V`, `continuousLinearEquivOfBilin B v` is the unique element `V`
such that `continuousLinearEquivOfBilin B v, wâŸ« = B v w`.
The Lax-Milgram theorem states that this is a continuous equivalence.
-/
def continuousLinearEquivOfBilin (coercive : IsCoercive B) : V â‰ƒL[â„] V :=
  ContinuousLinearEquiv.ofBijective Bâ™¯ coercive.ker_eq_bot coercive.range_eq_top

@[simp]
theorem continuousLinearEquivOfBilin_apply (coercive : IsCoercive B) (v w : V) :
    âŸªcoercive.continuousLinearEquivOfBilin v, wâŸ«_â„ = B v w :=
  continuousLinearMapOfBilin_apply B v w

theorem unique_continuousLinearEquivOfBilin (coercive : IsCoercive B) {v f : V}
    (is_lax_milgram : âˆ€ w, âŸªf, wâŸ«_â„ = B v w) : f = coercive.continuousLinearEquivOfBilin v :=
  unique_continuousLinearMapOfBilin B is_lax_milgram

end IsCoercive
