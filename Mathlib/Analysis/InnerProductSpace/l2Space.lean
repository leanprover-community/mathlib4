/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Analysis.Normed.Lp.lpSpace
import Mathlib.Analysis.InnerProductSpace.PiL2

/-!
# Hilbert sum of a family of inner product spaces

Given a family `(G : Î¹ â†’ Type*) [Î  i, InnerProductSpace ğ•œ (G i)]` of inner product spaces, this
file equips `lp G 2` with an inner product space structure, where `lp G 2` consists of those
dependent functions `f : Î  i, G i` for which `âˆ‘' i, â€–f iâ€– ^ 2`, the sum of the norms-squared, is
summable.  This construction is sometimes called the *Hilbert sum* of the family `G`.  By choosing
`G` to be `Î¹ â†’ ğ•œ`, the Hilbert space `â„“Â²(Î¹, ğ•œ)` may be seen as a special case of this construction.

We also define a *predicate* `IsHilbertSum ğ•œ G V`, where `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E`, expressing that
`V` is an `OrthogonalFamily` and that the associated map `lp G 2 â†’â‚—áµ¢[ğ•œ] E` is surjective.

## Main definitions

* `OrthogonalFamily.linearIsometry`: Given a Hilbert space `E`, a family `G` of inner product
  spaces and a family `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E` of isometric embeddings of the `G i` into `E` with
  mutually-orthogonal images, there is an induced isometric embedding of the Hilbert sum of `G`
  into `E`.

* `IsHilbertSum`: Given a Hilbert space `E`, a family `G` of inner product
  spaces and a family `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E` of isometric embeddings of the `G i` into `E`,
  `IsHilbertSum ğ•œ G V` means that `V` is an `OrthogonalFamily` and that the above
  linear isometry is surjective.

* `IsHilbertSum.linearIsometryEquiv`: If a Hilbert space `E` is a Hilbert sum of the
  inner product spaces `G i` with respect to the family `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E`, then the
  corresponding `OrthogonalFamily.linearIsometry` can be upgraded to a `LinearIsometryEquiv`.

* `HilbertBasis`: We define a *Hilbert basis* of a Hilbert space `E` to be a structure whose single
  field `HilbertBasis.repr` is an isometric isomorphism of `E` with `â„“Â²(Î¹, ğ•œ)` (i.e., the Hilbert
  sum of `Î¹` copies of `ğ•œ`).  This parallels the definition of `Basis`, in `LinearAlgebra.Basis`,
  as an isomorphism of an `R`-module with `Î¹ â†’â‚€ R`.

* `HilbertBasis.instCoeFun`: More conventionally a Hilbert basis is thought of as a family
  `Î¹ â†’ E` of vectors in `E` satisfying certain properties (orthonormality, completeness).  We obtain
  this interpretation of a Hilbert basis `b` by defining `â‡‘b`, of type `Î¹ â†’ E`, to be the image
  under `b.repr` of `lp.single 2 i (1:ğ•œ)`.  This parallels the definition `Basis.coeFun` in
  `LinearAlgebra.Basis`.

* `HilbertBasis.mk`: Make a Hilbert basis of `E` from an orthonormal family `v : Î¹ â†’ E` of vectors
  in `E` whose span is dense.  This parallels the definition `Basis.mk` in `LinearAlgebra.Basis`.

* `HilbertBasis.mkOfOrthogonalEqBot`: Make a Hilbert basis of `E` from an orthonormal family
  `v : Î¹ â†’ E` of vectors in `E` whose span has trivial orthogonal complement.

## Main results

* `lp.instInnerProductSpace`: Construction of the inner product space instance on the Hilbert sum
  `lp G 2`. Note that from the file `Analysis.Normed.Lp.lpSpace`, the space `lp G 2` already
  held a normed space instance (`lp.normedSpace`), and if each `G i` is a Hilbert space (i.e.,
  complete), then `lp G 2` was already known to be complete (`lp.completeSpace`). So the work
  here is to define the inner product and show it is compatible.

* `OrthogonalFamily.range_linearIsometry`: Given a family `G` of inner product spaces and a family
  `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E` of isometric embeddings of the `G i` into `E` with mutually-orthogonal
  images, the image of the embedding `OrthogonalFamily.linearIsometry` of the Hilbert sum of `G`
  into `E` is the closure of the span of the images of the `G i`.

* `HilbertBasis.repr_apply_apply`: Given a Hilbert basis `b` of `E`, the entry `b.repr x i` of
  `x`'s representation in `â„“Â²(Î¹, ğ•œ)` is the inner product `âŸªb i, xâŸ«`.

* `HilbertBasis.hasSum_repr`: Given a Hilbert basis `b` of `E`, a vector `x` in `E` can be
  expressed as the "infinite linear combination" `âˆ‘' i, b.repr x i â€¢ b i` of the basis vectors
  `b i`, with coefficients given by the entries `b.repr x i` of `x`'s representation in `â„“Â²(Î¹, ğ•œ)`.

* `exists_hilbertBasis`: A Hilbert space admits a Hilbert basis.

## Keywords

Hilbert space, Hilbert sum, l2, Hilbert basis, unitary equivalence, isometric isomorphism
-/

open RCLike Submodule Filter
open scoped NNReal ENNReal ComplexConjugate Topology

noncomputable section

variable {Î¹ ğ•œ : Type*} [RCLike ğ•œ] {E : Type*}
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {G : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, InnerProductSpace ğ•œ (G i)]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-- `â„“Â²(Î¹, ğ•œ)` is the Hilbert space of square-summable functions `Î¹ â†’ ğ•œ`, herein implemented
as `lp (fun i : Î¹ => ğ•œ) 2`. -/
notation "â„“Â²(" Î¹ ", " ğ•œ ")" => lp (fun i : Î¹ => ğ•œ) 2

/-! ### Inner product space structure on `lp G 2` -/


namespace lp

theorem summable_inner (f g : lp G 2) : Summable fun i => âŸªf i, g iâŸ« := by
  -- Apply the Direct Comparison Test, comparing with âˆ‘' i, â€–f iâ€– * â€–g iâ€– (summable by HÃ¶lder)
  refine .of_norm_bounded (lp.summable_mul ?_ f g) ?_
  Â· rw [Real.holderConjugate_iff]; norm_num
  intro i
  -- Then apply Cauchy-Schwarz pointwise
  exact norm_inner_le_norm (ğ•œ := ğ•œ) _ _

instance instInnerProductSpace : InnerProductSpace ğ•œ (lp G 2) :=
  { lp.normedAddCommGroup (E := G) (p := 2) with
    inner := fun f g => âˆ‘' i, âŸªf i, g iâŸ«
    norm_sq_eq_re_inner := fun f => by
      calc
        â€–fâ€– ^ 2 = â€–fâ€– ^ (2 : â„â‰¥0âˆ).toReal := by norm_cast
        _ = âˆ‘' i, â€–f iâ€– ^ (2 : â„â‰¥0âˆ).toReal := lp.norm_rpow_eq_tsum ?_ f
        _ = âˆ‘' i, â€–f iâ€– ^ (2 : â„•) := by norm_cast
        _ = âˆ‘' i, re âŸªf i, f iâŸ« := by simp [norm_sq_eq_re_inner (ğ•œ := ğ•œ)]
        _ = re (âˆ‘' i, âŸªf i, f iâŸ«) := (RCLike.reCLM.map_tsum ?_).symm
      Â· norm_num
      Â· exact summable_inner f f
    conj_inner_symm := fun f g => by
      calc
        conj _ = conj (âˆ‘' i, âŸªg i, f iâŸ«) := by congr
        _ = âˆ‘' i, conj âŸªg i, f iâŸ« := RCLike.conjCLE.map_tsum
        _ = âˆ‘' i, âŸªf i, g iâŸ« := by simp only [inner_conj_symm]
        _ = _ := by congr
    add_left := fun fâ‚ fâ‚‚ g => by
      calc
        _ = âˆ‘' i, âŸª(fâ‚ + fâ‚‚) i, g iâŸ« := ?_
        _ = âˆ‘' i, (âŸªfâ‚ i, g iâŸ« + âŸªfâ‚‚ i, g iâŸ«) := by
          simp only [inner_add_left, Pi.add_apply, coeFn_add]
        _ = (âˆ‘' i, âŸªfâ‚ i, g iâŸ«) + âˆ‘' i, âŸªfâ‚‚ i, g iâŸ« := Summable.tsum_add ?_ ?_
        _ = _ := by congr
      Â· congr
      Â· exact summable_inner fâ‚ g
      Â· exact summable_inner fâ‚‚ g
    smul_left := fun f g c => by
      calc
        _ = âˆ‘' i, âŸªc â€¢ f i, g iâŸ« := ?_
        _ = âˆ‘' i, conj c * âŸªf i, g iâŸ« := by simp only [inner_smul_left]
        _ = conj c * âˆ‘' i, âŸªf i, g iâŸ« := tsum_mul_left
        _ = _ := ?_
      Â· simp only [coeFn_smul, Pi.smul_apply]
      Â· congr }

theorem inner_eq_tsum (f g : lp G 2) : âŸªf, gâŸ« = âˆ‘' i, âŸªf i, g iâŸ« :=
  rfl

theorem hasSum_inner (f g : lp G 2) : HasSum (fun i => âŸªf i, g iâŸ«) âŸªf, gâŸ« :=
  (summable_inner f g).hasSum

theorem inner_single_left [DecidableEq Î¹] (i : Î¹) (a : G i) (f : lp G 2) :
    âŸªlp.single 2 i a, fâŸ« = âŸªa, f iâŸ« := by
  refine (hasSum_inner (lp.single 2 i a) f).unique ?_
  simp_rw [lp.coeFn_single]
  convert hasSum_ite_eq i âŸªa, f iâŸ« using 1
  ext j
  split_ifs with h
  Â· subst h; rw [Pi.single_eq_same]
  Â· simp [Pi.single_eq_of_ne h]

theorem inner_single_right [DecidableEq Î¹] (i : Î¹) (a : G i) (f : lp G 2) :
    âŸªf, lp.single 2 i aâŸ« = âŸªf i, aâŸ« := by
  simpa [inner_conj_symm] using congr_arg conj (inner_single_left (ğ•œ := ğ•œ) i a f)

end lp

/-! ### Identification of a general Hilbert space `E` with a Hilbert sum -/


namespace OrthogonalFamily

variable [CompleteSpace E] {V : âˆ€ i, G i â†’â‚—áµ¢[ğ•œ] E} (hV : OrthogonalFamily ğ•œ G V)
include hV

protected theorem summable_of_lp (f : lp G 2) :
    Summable fun i => V i (f i) := by
  rw [hV.summable_iff_norm_sq_summable]
  convert (lp.memâ„“p f).summable _
  Â· norm_cast
  Â· norm_num

/-- A mutually orthogonal family of subspaces of `E` induce a linear isometry from `lp 2` of the
subspaces into `E`. -/
protected def linearIsometry (hV : OrthogonalFamily ğ•œ G V) : lp G 2 â†’â‚—áµ¢[ğ•œ] E where
  toFun f := âˆ‘' i, V i (f i)
  map_add' f g := by
    simp only [(hV.summable_of_lp f).tsum_add (hV.summable_of_lp g), lp.coeFn_add, Pi.add_apply,
      LinearIsometry.map_add]
  map_smul' c f := by
    simpa only [LinearIsometry.map_smul, Pi.smul_apply, lp.coeFn_smul] using
      (hV.summable_of_lp f).tsum_const_smul c
  norm_map' f := by
    classical
      -- needed for lattice instance on `Finset Î¹`, for `Filter.atTop_neBot`
      have H : 0 < (2 : â„â‰¥0âˆ).toReal := by norm_num
      suffices â€–âˆ‘' i : Î¹, V i (f i)â€– ^ (2 : â„â‰¥0âˆ).toReal = â€–fâ€– ^ (2 : â„â‰¥0âˆ).toReal by
        exact Real.rpow_left_injOn H.ne' (norm_nonneg _) (norm_nonneg _) this
      refine tendsto_nhds_unique ?_ (lp.hasSum_norm H f)
      convert (hV.summable_of_lp f).hasSum.norm.rpow_const (Or.inr H.le) using 1
      ext s
      exact mod_cast (hV.norm_sum f s).symm

protected theorem linearIsometry_apply (f : lp G 2) : hV.linearIsometry f = âˆ‘' i, V i (f i) :=
  rfl

protected theorem hasSum_linearIsometry (f : lp G 2) :
    HasSum (fun i => V i (f i)) (hV.linearIsometry f) :=
  (hV.summable_of_lp f).hasSum

@[simp]
protected theorem linearIsometry_apply_single [DecidableEq Î¹] {i : Î¹} (x : G i) :
    hV.linearIsometry (lp.single 2 i x) = V i x := by
  rw [hV.linearIsometry_apply, â† tsum_ite_eq i (V i x)]
  congr
  ext j
  rw [lp.single_apply]
  split_ifs with h
  Â· subst h; simp
  Â· simp [h]

protected theorem linearIsometry_apply_dfinsupp_sum_single [DecidableEq Î¹] [âˆ€ i, DecidableEq (G i)]
    (Wâ‚€ : Î â‚€ i : Î¹, G i) : hV.linearIsometry (Wâ‚€.sum (lp.single 2)) = Wâ‚€.sum fun i => V i := by
  simp

/-- The canonical linear isometry from the `lp 2` of a mutually orthogonal family of subspaces of
`E` into E, has range the closure of the span of the subspaces. -/
protected theorem range_linearIsometry [âˆ€ i, CompleteSpace (G i)] :
    LinearMap.range hV.linearIsometry.toLinearMap =
      (â¨† i, LinearMap.range (V i).toLinearMap).topologicalClosure := by
  classical
  refine le_antisymm ?_ ?_
  Â· rintro x âŸ¨f, rflâŸ©
    refine mem_closure_of_tendsto (hV.hasSum_linearIsometry f) (Eventually.of_forall ?_)
    intro s
    rw [SetLike.mem_coe]
    refine sum_mem ?_
    intro i _
    refine mem_iSup_of_mem i ?_
    exact LinearMap.mem_range_self _ (f i)
  Â· apply topologicalClosure_minimal
    Â· refine iSup_le ?_
      rintro i x âŸ¨x, rflâŸ©
      use lp.single 2 i x
      exact hV.linearIsometry_apply_single x
    exact hV.linearIsometry.isometry.isUniformInducing.isComplete_range.isClosed

end OrthogonalFamily

section IsHilbertSum

variable (ğ•œ G)
variable [CompleteSpace E] (V : âˆ€ i, G i â†’â‚—áµ¢[ğ•œ] E) (F : Î¹ â†’ Submodule ğ•œ E)

/-- Given a family of Hilbert spaces `G : Î¹ â†’ Type*`, a Hilbert sum of `G` consists of a Hilbert
space `E` and an orthogonal family `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E` such that the induced isometry
`Î¦ : lp G 2 â†’ E` is surjective.

Keeping in mind that `lp G 2` is "the" external Hilbert sum of `G : Î¹ â†’ Type*`, this is analogous
to `DirectSum.IsInternal`, except that we don't express it in terms of actual submodules. -/
structure IsHilbertSum : Prop where
  ofSurjective ::
  /-- The orthogonal family constituting the summands in the Hilbert sum. -/
  protected OrthogonalFamily : OrthogonalFamily ğ•œ G V
  /-- The isometry `lp G 2 â†’ E` induced by the orthogonal family is surjective. -/
  protected surjective_isometry : Function.Surjective OrthogonalFamily.linearIsometry

variable {ğ•œ G V}

/-- If `V : Î  i, G i â†’â‚—áµ¢[ğ•œ] E` is an orthogonal family such that the supremum of the ranges of
`V i` is dense, then `(E, V)` is a Hilbert sum of `G`. -/
theorem IsHilbertSum.mk [âˆ€ i, CompleteSpace <| G i] (hVortho : OrthogonalFamily ğ•œ G V)
    (hVtotal : âŠ¤ â‰¤ (â¨† i, LinearMap.range (V i).toLinearMap).topologicalClosure) :
    IsHilbertSum ğ•œ G V :=
  { OrthogonalFamily := hVortho
    surjective_isometry := by
      rw [â† LinearIsometry.coe_toLinearMap]
      exact LinearMap.range_eq_top.mp
        (eq_top_iff.mpr <| hVtotal.trans_eq hVortho.range_linearIsometry.symm) }

/-- This is `Orthonormal.isHilbertSum` in the case of actual inclusions from subspaces. -/
theorem IsHilbertSum.mkInternal [âˆ€ i, CompleteSpace <| F i]
    (hFortho : OrthogonalFamily ğ•œ (fun i => F i) fun i => (F i).subtypeâ‚—áµ¢)
    (hFtotal : âŠ¤ â‰¤ (â¨† i, F i).topologicalClosure) :
    IsHilbertSum ğ•œ (fun i => F i) fun i => (F i).subtypeâ‚—áµ¢ :=
  IsHilbertSum.mk hFortho (by simpa [subtypeâ‚—áµ¢_toLinearMap, range_subtype] using hFtotal)

/-- *A* Hilbert sum `(E, V)` of `G` is canonically isomorphic to *the* Hilbert sum of `G`,
i.e `lp G 2`.

Note that this goes in the opposite direction from `OrthogonalFamily.linearIsometry`. -/
noncomputable def IsHilbertSum.linearIsometryEquiv (hV : IsHilbertSum ğ•œ G V) : E â‰ƒâ‚—áµ¢[ğ•œ] lp G 2 :=
  LinearIsometryEquiv.symm <|
    LinearIsometryEquiv.ofSurjective hV.OrthogonalFamily.linearIsometry hV.surjective_isometry

/-- In the canonical isometric isomorphism between a Hilbert sum `E` of `G` and `lp G 2`,
a vector `w : lp G 2` is the image of the infinite sum of the associated elements in `E`. -/
protected theorem IsHilbertSum.linearIsometryEquiv_symm_apply (hV : IsHilbertSum ğ•œ G V)
    (w : lp G 2) : hV.linearIsometryEquiv.symm w = âˆ‘' i, V i (w i) := by
  simp [IsHilbertSum.linearIsometryEquiv, OrthogonalFamily.linearIsometry_apply]

/-- In the canonical isometric isomorphism between a Hilbert sum `E` of `G` and `lp G 2`,
a vector `w : lp G 2` is the image of the infinite sum of the associated elements in `E`, and this
sum indeed converges. -/
protected theorem IsHilbertSum.hasSum_linearIsometryEquiv_symm (hV : IsHilbertSum ğ•œ G V)
    (w : lp G 2) : HasSum (fun i => V i (w i)) (hV.linearIsometryEquiv.symm w) := by
  simp [IsHilbertSum.linearIsometryEquiv, OrthogonalFamily.hasSum_linearIsometry]

/-- In the canonical isometric isomorphism between a Hilbert sum `E` of `G : Î¹ â†’ Type*` and
`lp G 2`, an "elementary basis vector" in `lp G 2` supported at `i : Î¹` is the image of the
associated element in `E`. -/
@[simp]
protected theorem IsHilbertSum.linearIsometryEquiv_symm_apply_single
    [DecidableEq Î¹] (hV : IsHilbertSum ğ•œ G V) {i : Î¹} (x : G i) :
    hV.linearIsometryEquiv.symm (lp.single 2 i x) = V i x := by
  simp [IsHilbertSum.linearIsometryEquiv, OrthogonalFamily.linearIsometry_apply_single]

/-- In the canonical isometric isomorphism between a Hilbert sum `E` of `G : Î¹ â†’ Type*` and
`lp G 2`, a finitely-supported vector in `lp G 2` is the image of the associated finite sum of
elements of `E`. -/
protected theorem IsHilbertSum.linearIsometryEquiv_symm_apply_dfinsupp_sum_single
    [DecidableEq Î¹] [âˆ€ i, DecidableEq (G i)] (hV : IsHilbertSum ğ•œ G V) (Wâ‚€ : Î â‚€ i : Î¹, G i) :
    hV.linearIsometryEquiv.symm (Wâ‚€.sum (lp.single 2)) = Wâ‚€.sum fun i => V i := by
  simp only [map_dfinsuppSum, IsHilbertSum.linearIsometryEquiv_symm_apply_single]

/-- In the canonical isometric isomorphism between a Hilbert sum `E` of `G : Î¹ â†’ Type*` and
`lp G 2`, a finitely-supported vector in `lp G 2` is the image of the associated finite sum of
elements of `E`. -/
@[simp]
protected theorem IsHilbertSum.linearIsometryEquiv_apply_dfinsupp_sum_single
    [DecidableEq Î¹] [âˆ€ i, DecidableEq (G i)] (hV : IsHilbertSum ğ•œ G V) (Wâ‚€ : Î â‚€ i : Î¹, G i) :
    ((Wâ‚€.sum (Î³ := lp G 2) fun a b â†¦ hV.linearIsometryEquiv (V a b)) : âˆ€ i, G i) = Wâ‚€ := by
  rw [â† map_dfinsuppSum]
  rw [â† hV.linearIsometryEquiv_symm_apply_dfinsupp_sum_single]
  rw [LinearIsometryEquiv.apply_symm_apply]
  ext i
  simp +contextual [DFinsupp.sum, lp.single_apply]

/-- Given a total orthonormal family `v : Î¹ â†’ E`, `E` is a Hilbert sum of `fun i : Î¹ => ğ•œ`
relative to the family of linear isometries `fun i k => k â€¢ v i`. -/
theorem Orthonormal.isHilbertSum {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v)
    (hsp : âŠ¤ â‰¤ (span ğ•œ (Set.range v)).topologicalClosure) :
    IsHilbertSum ğ•œ (fun _ : Î¹ => ğ•œ) fun i => LinearIsometry.toSpanSingleton ğ•œ E (hv.1 i) :=
  IsHilbertSum.mk hv.orthogonalFamily (by
    convert hsp
    simp [â† LinearMap.span_singleton_eq_range, â† Submodule.span_iUnion])

theorem Submodule.isHilbertSumOrthogonal (K : Submodule ğ•œ E) [hK : CompleteSpace K] :
    IsHilbertSum ğ•œ (fun b => â†¥(cond b K Ká—®)) fun b => (cond b K Ká—®).subtypeâ‚—áµ¢ := by
  have : âˆ€ b, CompleteSpace (â†¥(cond b K Ká—®)) := by
    intro b
    cases b <;> first | exact instOrthogonalCompleteSpace K | assumption
  refine IsHilbertSum.mkInternal _ K.orthogonalFamily_self ?_
  refine le_trans ?_ (Submodule.le_topologicalClosure _)
  rw [iSup_bool_eq, cond, cond]
  refine Codisjoint.top_le ?_
  exact Submodule.isCompl_orthogonal_of_completeSpace.codisjoint

end IsHilbertSum

/-! ### Hilbert bases -/


section

variable (Î¹) (ğ•œ) (E)

/-- A Hilbert basis on `Î¹` for an inner product space `E` is an identification of `E` with the `lp`
space `â„“Â²(Î¹, ğ•œ)`. -/
structure HilbertBasis where ofRepr ::
  /-- The linear isometric equivalence implementing identifying the Hilbert space with `â„“Â²`. -/
  repr : E â‰ƒâ‚—áµ¢[ğ•œ] â„“Â²(Î¹, ğ•œ)

end

namespace HilbertBasis

instance {Î¹ : Type*} : Inhabited (HilbertBasis Î¹ ğ•œ â„“Â²(Î¹, ğ•œ)) :=
  âŸ¨ofRepr (LinearIsometryEquiv.refl ğ•œ _)âŸ©

open Classical in
/-- `b i` is the `i`th basis vector. -/
instance instFunLike : FunLike (HilbertBasis Î¹ ğ•œ E) Î¹ E where
  coe b i := b.repr.symm (lp.single 2 i (1 : ğ•œ))
  coe_injective'
  | âŸ¨bâ‚âŸ©, âŸ¨bâ‚‚âŸ©, h => by
    congr
    apply LinearIsometryEquiv.symm_bijective.injective
    apply LinearIsometryEquiv.toContinuousLinearEquiv_injective
    apply ContinuousLinearEquiv.coe_injective
    refine lp.ext_continuousLinearMap ( ENNReal.ofNat_ne_top (n := nat_lit 2)) fun i => ?_
    ext
    exact congr_fun h i

@[simp]
protected theorem repr_symm_single [DecidableEq Î¹] (b : HilbertBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr.symm (lp.single 2 i (1 : ğ•œ)) = b i := by
  dsimp [instFunLike]
  convert rfl


protected theorem repr_self [DecidableEq Î¹] (b : HilbertBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr (b i) = lp.single 2 i (1 : ğ•œ) := by
  simp only [LinearIsometryEquiv.apply_symm_apply, â† b.repr_symm_single]

protected theorem repr_apply_apply (b : HilbertBasis Î¹ ğ•œ E) (v : E) (i : Î¹) :
    b.repr v i = âŸªb i, vâŸ« := by
  classical
  rw [â† b.repr.inner_map_map (b i) v, b.repr_self, lp.inner_single_left]
  simp

@[simp]
protected theorem orthonormal (b : HilbertBasis Î¹ ğ•œ E) : Orthonormal ğ•œ b := by
  classical
  rw [orthonormal_iff_ite]
  intro i j
  rw [â† b.repr.inner_map_map (b i) (b j), b.repr_self, b.repr_self, lp.inner_single_left,
    lp.single_apply, Pi.single_apply]
  simp

protected theorem hasSum_repr_symm (b : HilbertBasis Î¹ ğ•œ E) (f : â„“Â²(Î¹, ğ•œ)) :
    HasSum (fun i => f i â€¢ b i) (b.repr.symm f) := by
  classical
  suffices H : (fun i : Î¹ => f i â€¢ b i) = fun b_1 : Î¹ => b.repr.symm.toContinuousLinearEquiv <|
      (fun i : Î¹ => lp.single 2 i (f i) (E := (fun _ : Î¹ => ğ•œ))) b_1 by
    rw [H]
    have : HasSum (fun i : Î¹ => lp.single 2 i (f i)) f := lp.hasSum_single ENNReal.ofNat_ne_top f
    exact (â†‘b.repr.symm.toContinuousLinearEquiv : â„“Â²(Î¹, ğ•œ) â†’L[ğ•œ] E).hasSum this
  ext i
  apply b.repr.injective
  letI : NormedSpace ğ•œ (lp (fun _i : Î¹ => ğ•œ) 2) := by infer_instance
  have : lp.single (E := (fun _ : Î¹ => ğ•œ)) 2 i (f i * 1) = f i â€¢ lp.single 2 i 1 :=
    lp.single_smul (E := (fun _ : Î¹ => ğ•œ)) 2 i (f i) (1 : ğ•œ)
  rw [mul_one] at this
  rw [LinearIsometryEquiv.map_smul, b.repr_self, â† this,
    LinearIsometryEquiv.coe_toContinuousLinearEquiv]
  exact (b.repr.apply_symm_apply (lp.single 2 i (f i))).symm

protected theorem hasSum_repr (b : HilbertBasis Î¹ ğ•œ E) (x : E) :
    HasSum (fun i => b.repr x i â€¢ b i) x := by simpa using b.hasSum_repr_symm (b.repr x)

@[simp]
protected theorem dense_span (b : HilbertBasis Î¹ ğ•œ E) :
    (span ğ•œ (Set.range b)).topologicalClosure = âŠ¤ := by
  classical
    rw [eq_top_iff]
    rintro x -
    refine mem_closure_of_tendsto (b.hasSum_repr x) (Eventually.of_forall ?_)
    intro s
    simp only [SetLike.mem_coe]
    refine sum_mem ?_
    rintro i -
    refine smul_mem _ _ ?_
    exact subset_span âŸ¨i, rflâŸ©

protected theorem hasSum_inner_mul_inner (b : HilbertBasis Î¹ ğ•œ E) (x y : E) :
    HasSum (fun i => âŸªx, b iâŸ« * âŸªb i, yâŸ«) âŸªx, yâŸ« := by
  convert (b.hasSum_repr y).mapL (innerSL ğ•œ x) using 1
  ext i
  rw [innerSL_apply, b.repr_apply_apply, inner_smul_right, mul_comm]

protected theorem summable_inner_mul_inner (b : HilbertBasis Î¹ ğ•œ E) (x y : E) :
    Summable fun i => âŸªx, b iâŸ« * âŸªb i, yâŸ« :=
  (b.hasSum_inner_mul_inner x y).summable

protected theorem tsum_inner_mul_inner (b : HilbertBasis Î¹ ğ•œ E) (x y : E) :
    âˆ‘' i, âŸªx, b iâŸ« * âŸªb i, yâŸ« = âŸªx, yâŸ« :=
  (b.hasSum_inner_mul_inner x y).tsum_eq

-- Note: this should be `b.repr` composed with an identification of `lp (fun i : Î¹ => ğ•œ) p` with
-- `PiLp p (fun i : Î¹ => ğ•œ)` (in this case with `p = 2`), but we don't have this yet (July 2022).
/-- A finite Hilbert basis is an orthonormal basis. -/
protected def toOrthonormalBasis [Fintype Î¹] (b : HilbertBasis Î¹ ğ•œ E) : OrthonormalBasis Î¹ ğ•œ E :=
  OrthonormalBasis.mk b.orthonormal
    (by
      refine Eq.ge ?_
      classical
      have := (span ğ•œ (Finset.univ.image b : Set E)).closed_of_finiteDimensional
      simpa only [Finset.coe_image, Finset.coe_univ, Set.image_univ, HilbertBasis.dense_span] using
        this.submodule_topologicalClosure_eq.symm)

@[simp]
theorem coe_toOrthonormalBasis [Fintype Î¹] (b : HilbertBasis Î¹ ğ•œ E) :
    (b.toOrthonormalBasis : Î¹ â†’ E) = b :=
  OrthonormalBasis.coe_mk _ _

protected theorem hasSum_orthogonalProjection {U : Submodule ğ•œ E} [CompleteSpace U]
    (b : HilbertBasis Î¹ ğ•œ U) (x : E) :
    HasSum (fun i => âŸª(b i : E), xâŸ« â€¢ b i) (U.orthogonalProjection x) := by
  simpa only [b.repr_apply_apply, inner_orthogonalProjection_eq_of_mem_left] using
    b.hasSum_repr (U.orthogonalProjection x)

theorem finite_spans_dense [DecidableEq E] (b : HilbertBasis Î¹ ğ•œ E) :
    (â¨† J : Finset Î¹, span ğ•œ (J.image b : Set E)).topologicalClosure = âŠ¤ :=
  eq_top_iff.mpr <| b.dense_span.ge.trans (by
    simp_rw [â† Submodule.span_iUnion]
    exact topologicalClosure_mono (span_mono <| Set.range_subset_iff.mpr fun i =>
      Set.mem_iUnion_of_mem {i} <| Finset.mem_coe.mpr <| Finset.mem_image_of_mem _ <|
      Finset.mem_singleton_self i))

variable [CompleteSpace E]

section
variable {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v)
include hv

/-- An orthonormal family of vectors whose span is dense in the whole module is a Hilbert basis. -/
protected def mk (hsp : âŠ¤ â‰¤ (span ğ•œ (Set.range v)).topologicalClosure) : HilbertBasis Î¹ ğ•œ E :=
  HilbertBasis.ofRepr <| (hv.isHilbertSum hsp).linearIsometryEquiv

theorem _root_.Orthonormal.linearIsometryEquiv_symm_apply_single_one [DecidableEq Î¹] (h i) :
    (hv.isHilbertSum h).linearIsometryEquiv.symm (lp.single 2 i 1) = v i := by
  rw [IsHilbertSum.linearIsometryEquiv_symm_apply_single, LinearIsometry.toSpanSingleton_apply,
    one_smul]

@[simp]
protected theorem coe_mk (hsp : âŠ¤ â‰¤ (span ğ•œ (Set.range v)).topologicalClosure) :
    â‡‘(HilbertBasis.mk hv hsp) = v := by
  classical
  apply funext <| Orthonormal.linearIsometryEquiv_symm_apply_single_one hv hsp

/-- An orthonormal family of vectors whose span has trivial orthogonal complement is a Hilbert
basis. -/
protected def mkOfOrthogonalEqBot (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) : HilbertBasis Î¹ ğ•œ E :=
  HilbertBasis.mk hv
    (by rw [â† orthogonal_orthogonal_eq_closure, â† eq_top_iff, orthogonal_eq_top_iff, hsp])

@[simp]
protected theorem coe_mkOfOrthogonalEqBot (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) :
    â‡‘(HilbertBasis.mkOfOrthogonalEqBot hv hsp) = v :=
  HilbertBasis.coe_mk hv _

-- Note : this should be `b.repr` composed with an identification of `lp (fun i : Î¹ => ğ•œ) p` with
-- `PiLp p (fun i : Î¹ => ğ•œ)` (in this case with `p = 2`), but we don't have this yet (July 2022).
/-- An orthonormal basis is a Hilbert basis. -/
protected def _root_.OrthonormalBasis.toHilbertBasis [Fintype Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) :
    HilbertBasis Î¹ ğ•œ E :=
  HilbertBasis.mk b.orthonormal <| by
    simpa only [â† OrthonormalBasis.coe_toBasis, b.toBasis.span_eq, eq_top_iff] using
      @subset_closure E _ _

end

@[simp]
theorem _root_.OrthonormalBasis.coe_toHilbertBasis [Fintype Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) :
    (b.toHilbertBasis : Î¹ â†’ E) = b :=
  HilbertBasis.coe_mk _ _

/-- A Hilbert space admits a Hilbert basis extending a given orthonormal subset. -/
theorem _root_.Orthonormal.exists_hilbertBasis_extension {s : Set E}
    (hs : Orthonormal ğ•œ ((â†‘) : s â†’ E)) :
    âˆƒ (w : Set E) (b : HilbertBasis w ğ•œ E), s âŠ† w âˆ§ â‡‘b = ((â†‘) : w â†’ E) :=
  let âŸ¨w, hws, hw_ortho, hw_maxâŸ© := exists_maximal_orthonormal hs
  âŸ¨w, HilbertBasis.mkOfOrthogonalEqBot hw_ortho
    (by simpa only [Subtype.range_coe_subtype, Set.setOf_mem_eq,
      maximal_orthonormal_iff_orthogonalComplement_eq_bot hw_ortho] using hw_max),
    hws, HilbertBasis.coe_mkOfOrthogonalEqBot _ _âŸ©

variable (ğ•œ E)

/-- A Hilbert space admits a Hilbert basis. -/
theorem _root_.exists_hilbertBasis : âˆƒ (w : Set E) (b : HilbertBasis w ğ•œ E), â‡‘b = ((â†‘) : w â†’ E) :=
  let âŸ¨w, hw, _, hw''âŸ© := (orthonormal_empty ğ•œ E).exists_hilbertBasis_extension
  âŸ¨w, hw, hw''âŸ©

end HilbertBasis
