/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, SÃ©bastien GouÃ«zel, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Analysis.NormedSpace.PiLp
import Mathlib.LinearAlgebra.FiniteDimensional
import Mathlib.LinearAlgebra.UnitaryGroup

#align_import analysis.inner_product_space.pi_L2 from "leanprover-community/mathlib"@"13bce9a6b6c44f6b4c91ac1c1d2a816e2533d395"

/-!
# `LÂ²` inner product space structure on finite products of inner product spaces

The `LÂ²` norm on a finite product of inner product spaces is compatible with an inner product
$$
\langle x, y\rangle = \sum \langle x_i, y_i \rangle.
$$
This is recorded in this file as an inner product space instance on `PiLp 2`.

This file develops the notion of a finite dimensional Hilbert space over `ğ•œ = â„‚, â„`, referred to as
`E`. We define an `OrthonormalBasis ğ•œ Î¹ E` as a linear isometric equivalence
between `E` and `EuclideanSpace ğ•œ Î¹`. Then `stdOrthonormalBasis` shows that such an equivalence
always exists if `E` is finite dimensional. We provide language for converting between a basis
that is orthonormal and an orthonormal basis (e.g. `Basis.toOrthonormalBasis`). We show that
orthonormal bases for each summand in a direct sum of spaces can be combined into an orthonormal
basis for the whole sum in `DirectSum.IsInternal.subordinateOrthonormalBasis`. In
the last section, various properties of matrices are explored.

## Main definitions

- `EuclideanSpace ğ•œ n`: defined to be `PiLp 2 (n â†’ ğ•œ)` for any `Fintype n`, i.e., the space
  from functions to `n` to `ğ•œ` with the `LÂ²` norm. We register several instances on it (notably
  that it is a finite-dimensional inner product space).

- `OrthonormalBasis ğ•œ Î¹`: defined to be an isometry to Euclidean space from a given
  finite-dimensional inner product space, `E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹`.

- `Basis.toOrthonormalBasis`: constructs an `OrthonormalBasis` for a finite-dimensional
  Euclidean space from a `Basis` which is `Orthonormal`.

- `Orthonormal.exists_orthonormalBasis_extension`: provides an existential result of an
  `OrthonormalBasis` extending a given orthonormal set

- `exists_orthonormalBasis`: provides an orthonormal basis on a finite dimensional vector space

- `stdOrthonormalBasis`: provides an arbitrarily-chosen `OrthonormalBasis` of a given finite
  dimensional inner product space

For consequences in infinite dimension (Hilbert bases, etc.), see the file
`Analysis.InnerProductSpace.L2Space`.

-/

set_option linter.uppercaseLean3 false

open Real Set Filter RCLike Submodule Function Uniformity Topology NNReal ENNReal
  ComplexConjugate DirectSum

noncomputable section

variable {Î¹ Î¹' ğ•œ : Type*} [RCLike ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E']
variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]
variable {F' : Type*} [NormedAddCommGroup F'] [InnerProductSpace â„ F']

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

/-
 If `Î¹` is a finite type and each space `f i`, `i : Î¹`, is an inner product space,
then `Î  i, f i` is an inner product space as well. Since `Î  i, f i` is endowed with the sup norm,
we use instead `PiLp 2 f` for the product space, which is endowed with the `L^2` norm.
-/
instance PiLp.innerProductSpace {Î¹ : Type*} [Fintype Î¹] (f : Î¹ â†’ Type*)
    [âˆ€ i, NormedAddCommGroup (f i)] [âˆ€ i, InnerProductSpace ğ•œ (f i)] :
    InnerProductSpace ğ•œ (PiLp 2 f) where
  inner x y := âˆ‘ i, inner (x i) (y i)
  norm_sq_eq_inner x := by
    simp only [PiLp.norm_sq_eq_of_L2, map_sum, â† norm_sq_eq_inner, one_div]
  conj_symm := by
    intro x y
    unfold inner
    rw [map_sum]
    apply Finset.sum_congr rfl
    rintro z -
    apply inner_conj_symm
  add_left x y z :=
    show (âˆ‘ i, inner (x i + y i) (z i)) = (âˆ‘ i, inner (x i) (z i)) + âˆ‘ i, inner (y i) (z i) by
      simp only [inner_add_left, Finset.sum_add_distrib]
  smul_left x y r :=
    show (âˆ‘ i : Î¹, inner (r â€¢ x i) (y i)) = conj r * âˆ‘ i, inner (x i) (y i) by
      simp only [Finset.mul_sum, inner_smul_left]
#align pi_Lp.inner_product_space PiLp.innerProductSpace

@[simp]
theorem PiLp.inner_apply {Î¹ : Type*} [Fintype Î¹] {f : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (f i)]
    [âˆ€ i, InnerProductSpace ğ•œ (f i)] (x y : PiLp 2 f) : âŸªx, yâŸ« = âˆ‘ i, âŸªx i, y iâŸ« :=
  rfl
#align pi_Lp.inner_apply PiLp.inner_apply

/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional
space use `EuclideanSpace ğ•œ (Fin n)`. -/
abbrev EuclideanSpace (ğ•œ : Type*) (n : Type*) : Type _ :=
  PiLp 2 fun _ : n => ğ•œ
#align euclidean_space EuclideanSpace

theorem EuclideanSpace.nnnorm_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x : EuclideanSpace ğ•œ n) : â€–xâ€–â‚Š = NNReal.sqrt (âˆ‘ i, â€–x iâ€–â‚Š ^ 2) :=
  PiLp.nnnorm_eq_of_L2 x
#align euclidean_space.nnnorm_eq EuclideanSpace.nnnorm_eq

theorem EuclideanSpace.norm_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x : EuclideanSpace ğ•œ n) : â€–xâ€– = âˆš(âˆ‘ i, â€–x iâ€– ^ 2) := by
  simpa only [Real.coe_sqrt, NNReal.coe_sum] using congr_arg ((â†‘) : â„â‰¥0 â†’ â„) x.nnnorm_eq
#align euclidean_space.norm_eq EuclideanSpace.norm_eq

theorem EuclideanSpace.dist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : dist x y = âˆš(âˆ‘ i, dist (x i) (y i) ^ 2) :=
  PiLp.dist_eq_of_L2 x y
#align euclidean_space.dist_eq EuclideanSpace.dist_eq

theorem EuclideanSpace.nndist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : nndist x y = NNReal.sqrt (âˆ‘ i, nndist (x i) (y i) ^ 2) :=
  PiLp.nndist_eq_of_L2 x y
#align euclidean_space.nndist_eq EuclideanSpace.nndist_eq

theorem EuclideanSpace.edist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : edist x y = (âˆ‘ i, edist (x i) (y i) ^ 2) ^ (1 / 2 : â„) :=
  PiLp.edist_eq_of_L2 x y
#align euclidean_space.edist_eq EuclideanSpace.edist_eq

theorem EuclideanSpace.ball_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.ball (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 < r ^ 2} := by
  ext x
  have : (0 : â„) â‰¤ âˆ‘ i, x i ^ 2 := Finset.sum_nonneg fun _ _ => sq_nonneg _
  simp_rw [mem_setOf, mem_ball_zero_iff, norm_eq, norm_eq_abs, sq_abs, sqrt_lt this hr]

theorem EuclideanSpace.closedBall_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.closedBall (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 â‰¤ r ^ 2} := by
  ext
  simp_rw [mem_setOf, mem_closedBall_zero_iff, norm_eq, norm_eq_abs, sq_abs, sqrt_le_left hr]

theorem EuclideanSpace.sphere_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.sphere (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 = r ^ 2} := by
  ext x
  have : (0 : â„) â‰¤ âˆ‘ i, x i ^ 2 := Finset.sum_nonneg fun _ _ => sq_nonneg _
  simp_rw [mem_setOf, mem_sphere_zero_iff_norm, norm_eq, norm_eq_abs, sq_abs,
    Real.sqrt_eq_iff_sq_eq this hr, eq_comm]

section

#align euclidean_space.finite_dimensional WithLp.instModuleFinite

variable [Fintype Î¹]

#align euclidean_space.inner_product_space PiLp.innerProductSpace

@[simp]
theorem finrank_euclideanSpace :
    FiniteDimensional.finrank ğ•œ (EuclideanSpace ğ•œ Î¹) = Fintype.card Î¹ := by
  simp [EuclideanSpace, PiLp, WithLp]
#align finrank_euclidean_space finrank_euclideanSpace

theorem finrank_euclideanSpace_fin {n : â„•} :
    FiniteDimensional.finrank ğ•œ (EuclideanSpace ğ•œ (Fin n)) = n := by simp
#align finrank_euclidean_space_fin finrank_euclideanSpace_fin

theorem EuclideanSpace.inner_eq_star_dotProduct (x y : EuclideanSpace ğ•œ Î¹) :
    âŸªx, yâŸ« = Matrix.dotProduct (star <| WithLp.equiv _ _ x) (WithLp.equiv _ _ y) :=
  rfl
#align euclidean_space.inner_eq_star_dot_product EuclideanSpace.inner_eq_star_dotProduct

theorem EuclideanSpace.inner_piLp_equiv_symm (x y : Î¹ â†’ ğ•œ) :
    âŸª(WithLp.equiv 2 _).symm x, (WithLp.equiv 2 _).symm yâŸ« = Matrix.dotProduct (star x) y :=
  rfl
#align euclidean_space.inner_pi_Lp_equiv_symm EuclideanSpace.inner_piLp_equiv_symm

/-- A finite, mutually orthogonal family of subspaces of `E`, which span `E`, induce an isometry
from `E` to `PiLp 2` of the subspaces equipped with the `L2` inner product. -/
def DirectSum.IsInternal.isometryL2OfOrthogonalFamily [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : DirectSum.IsInternal V)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    E â‰ƒâ‚—áµ¢[ğ•œ] PiLp 2 fun i => V i := by
  let eâ‚ := DirectSum.linearEquivFunOnFintype ğ•œ Î¹ fun i => V i
  let eâ‚‚ := LinearEquiv.ofBijective (DirectSum.coeLinearMap V) hV
  refine LinearEquiv.isometryOfInner (eâ‚‚.symm.trans eâ‚) ?_
  suffices âˆ€ (v w : PiLp 2 fun i => V i), âŸªv, wâŸ« = âŸªeâ‚‚ (eâ‚.symm v), eâ‚‚ (eâ‚.symm w)âŸ« by
    intro vâ‚€ wâ‚€
    convert this (eâ‚ (eâ‚‚.symm vâ‚€)) (eâ‚ (eâ‚‚.symm wâ‚€)) <;>
      simp only [LinearEquiv.symm_apply_apply, LinearEquiv.apply_symm_apply]
  intro v w
  trans âŸªâˆ‘ i, (V i).subtypeâ‚—áµ¢ (v i), âˆ‘ i, (V i).subtypeâ‚—áµ¢ (w i)âŸ«
  Â· simp only [sum_inner, hV'.inner_right_fintype, PiLp.inner_apply]
  Â· congr <;> simp
#align direct_sum.is_internal.isometry_L2_of_orthogonal_family DirectSum.IsInternal.isometryL2OfOrthogonalFamily

@[simp]
theorem DirectSum.IsInternal.isometryL2OfOrthogonalFamily_symm_apply [DecidableEq Î¹]
    {V : Î¹ â†’ Submodule ğ•œ E} (hV : DirectSum.IsInternal V)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) (w : PiLp 2 fun i => V i) :
    (hV.isometryL2OfOrthogonalFamily hV').symm w = âˆ‘ i, (w i : E) := by
  classical
    let eâ‚ := DirectSum.linearEquivFunOnFintype ğ•œ Î¹ fun i => V i
    let eâ‚‚ := LinearEquiv.ofBijective (DirectSum.coeLinearMap V) hV
    suffices âˆ€ v : â¨ i, V i, eâ‚‚ v = âˆ‘ i, eâ‚ v i by exact this (eâ‚.symm w)
    intro v
    -- Porting note: added `DFinsupp.lsum`
    simp [eâ‚, eâ‚‚, DirectSum.coeLinearMap, DirectSum.toModule, DFinsupp.lsum,
      DFinsupp.sumAddHom_apply]
#align direct_sum.is_internal.isometry_L2_of_orthogonal_family_symm_apply DirectSum.IsInternal.isometryL2OfOrthogonalFamily_symm_apply

end

variable (Î¹ ğ•œ)

/-- A shorthand for `PiLp.continuousLinearEquiv`. -/
abbrev EuclideanSpace.equiv : EuclideanSpace ğ•œ Î¹ â‰ƒL[ğ•œ] Î¹ â†’ ğ•œ :=
  PiLp.continuousLinearEquiv 2 ğ•œ _
#align euclidean_space.equiv EuclideanSpace.equiv
#noalign euclidean_space.equiv_to_linear_equiv_apply
#noalign euclidean_space.equiv_apply
#noalign euclidean_space.equiv_to_linear_equiv_symm_apply
#noalign euclidean_space.equiv_symm_apply

variable {Î¹ ğ•œ}

-- TODO : This should be generalized to `PiLp`.
/-- The projection on the `i`-th coordinate of `EuclideanSpace ğ•œ Î¹`, as a linear map. -/
@[simps!]
def EuclideanSpace.projâ‚— (i : Î¹) : EuclideanSpace ğ•œ Î¹ â†’â‚—[ğ•œ] ğ•œ :=
  (LinearMap.proj i).comp (WithLp.linearEquiv 2 ğ•œ (Î¹ â†’ ğ•œ) : EuclideanSpace ğ•œ Î¹ â†’â‚—[ğ•œ] Î¹ â†’ ğ•œ)
#align euclidean_space.projâ‚— EuclideanSpace.projâ‚—
#align euclidean_space.projâ‚—_apply EuclideanSpace.projâ‚—_apply

-- TODO : This should be generalized to `PiLp`.
/-- The projection on the `i`-th coordinate of `EuclideanSpace ğ•œ Î¹`,
as a continuous linear map. -/
@[simps! apply coe]
def EuclideanSpace.proj (i : Î¹) : EuclideanSpace ğ•œ Î¹ â†’L[ğ•œ] ğ•œ :=
  âŸ¨EuclideanSpace.projâ‚— i, continuous_apply iâŸ©
#align euclidean_space.proj EuclideanSpace.proj
#align euclidean_space.proj_coe EuclideanSpace.proj_coe
#align euclidean_space.proj_apply EuclideanSpace.proj_apply

section DecEq

variable [DecidableEq Î¹]

-- TODO : This should be generalized to `PiLp`.
/-- The vector given in euclidean space by being `a : ğ•œ` at coordinate `i : Î¹` and `0 : ğ•œ` at
all other coordinates. -/
def EuclideanSpace.single (i : Î¹) (a : ğ•œ) : EuclideanSpace ğ•œ Î¹ :=
  (WithLp.equiv _ _).symm (Pi.single i a)
#align euclidean_space.single EuclideanSpace.single

@[simp]
theorem WithLp.equiv_single (i : Î¹) (a : ğ•œ) :
    WithLp.equiv _ _ (EuclideanSpace.single i a) = Pi.single i a :=
  rfl
#align pi_Lp.equiv_single WithLp.equiv_single

@[simp]
theorem WithLp.equiv_symm_single (i : Î¹) (a : ğ•œ) :
    (WithLp.equiv _ _).symm (Pi.single i a) = EuclideanSpace.single i a :=
  rfl
#align pi_Lp.equiv_symm_single WithLp.equiv_symm_single

@[simp]
theorem EuclideanSpace.single_apply (i : Î¹) (a : ğ•œ) (j : Î¹) :
    (EuclideanSpace.single i a) j = ite (j = i) a 0 := by
  rw [EuclideanSpace.single, WithLp.equiv_symm_pi_apply, â† Pi.single_apply i a j]
#align euclidean_space.single_apply EuclideanSpace.single_apply

variable [Fintype Î¹]

theorem EuclideanSpace.inner_single_left (i : Î¹) (a : ğ•œ) (v : EuclideanSpace ğ•œ Î¹) :
    âŸªEuclideanSpace.single i (a : ğ•œ), vâŸ« = conj a * v i := by simp [apply_ite conj]
#align euclidean_space.inner_single_left EuclideanSpace.inner_single_left

theorem EuclideanSpace.inner_single_right (i : Î¹) (a : ğ•œ) (v : EuclideanSpace ğ•œ Î¹) :
    âŸªv, EuclideanSpace.single i (a : ğ•œ)âŸ« = a * conj (v i) := by simp [apply_ite conj, mul_comm]
#align euclidean_space.inner_single_right EuclideanSpace.inner_single_right

@[simp]
theorem EuclideanSpace.norm_single (i : Î¹) (a : ğ•œ) :
    â€–EuclideanSpace.single i (a : ğ•œ)â€– = â€–aâ€– :=
  PiLp.norm_equiv_symm_single 2 (fun _ => ğ•œ) i a
#align euclidean_space.norm_single EuclideanSpace.norm_single

@[simp]
theorem EuclideanSpace.nnnorm_single (i : Î¹) (a : ğ•œ) :
    â€–EuclideanSpace.single i (a : ğ•œ)â€–â‚Š = â€–aâ€–â‚Š :=
  PiLp.nnnorm_equiv_symm_single 2 (fun _ => ğ•œ) i a
#align euclidean_space.nnnorm_single EuclideanSpace.nnnorm_single

@[simp]
theorem EuclideanSpace.dist_single_same (i : Î¹) (a b : ğ•œ) :
    dist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = dist a b :=
  PiLp.dist_equiv_symm_single_same 2 (fun _ => ğ•œ) i a b
#align euclidean_space.dist_single_same EuclideanSpace.dist_single_same

@[simp]
theorem EuclideanSpace.nndist_single_same (i : Î¹) (a b : ğ•œ) :
    nndist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = nndist a b :=
  PiLp.nndist_equiv_symm_single_same 2 (fun _ => ğ•œ) i a b
#align euclidean_space.nndist_single_same EuclideanSpace.nndist_single_same

@[simp]
theorem EuclideanSpace.edist_single_same (i : Î¹) (a b : ğ•œ) :
    edist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = edist a b :=
  PiLp.edist_equiv_symm_single_same 2 (fun _ => ğ•œ) i a b
#align euclidean_space.edist_single_same EuclideanSpace.edist_single_same

/-- `EuclideanSpace.single` forms an orthonormal family. -/
theorem EuclideanSpace.orthonormal_single :
    Orthonormal ğ•œ fun i : Î¹ => EuclideanSpace.single i (1 : ğ•œ) := by
  simp_rw [orthonormal_iff_ite, EuclideanSpace.inner_single_left, map_one, one_mul,
    EuclideanSpace.single_apply]
  intros
  trivial
#align euclidean_space.orthonormal_single EuclideanSpace.orthonormal_single

theorem EuclideanSpace.piLpCongrLeft_single
    {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (e : Î¹' â‰ƒ Î¹) (i' : Î¹') (v : ğ•œ) :
    LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ e (EuclideanSpace.single i' v) =
      EuclideanSpace.single (e i') v :=
  LinearIsometryEquiv.piLpCongrLeft_single e i' _
#align euclidean_space.pi_Lp_congr_left_single EuclideanSpace.piLpCongrLeft_single

end DecEq

variable (Î¹ ğ•œ E)
variable [Fintype Î¹]

/-- An orthonormal basis on E is an identification of `E` with its dimensional-matching
`EuclideanSpace ğ•œ Î¹`. -/
structure OrthonormalBasis where ofRepr ::
  /-- Linear isometry between `E` and `EuclideanSpace ğ•œ Î¹` representing the orthonormal basis. -/
  repr : E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹
#align orthonormal_basis OrthonormalBasis
#align orthonormal_basis.of_repr OrthonormalBasis.ofRepr
#align orthonormal_basis.repr OrthonormalBasis.repr

variable {Î¹ ğ•œ E}

namespace OrthonormalBasis

theorem repr_injective :
    Injective (repr : OrthonormalBasis Î¹ ğ•œ E â†’ E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹) := fun f g h => by
  cases f
  cases g
  congr

-- Porting note: `CoeFun` â†’ `FunLike`
/-- `b i` is the `i`th basis vector. -/
instance instFunLike : FunLike (OrthonormalBasis Î¹ ğ•œ E) Î¹ E where
  coe b i := by classical exact b.repr.symm (EuclideanSpace.single i (1 : ğ•œ))
  coe_injective' b b' h := repr_injective <| LinearIsometryEquiv.toLinearEquiv_injective <|
    LinearEquiv.symm_bijective.injective <| LinearEquiv.toLinearMap_injective <| by
      classical
        rw [â† LinearMap.cancel_right (WithLp.linearEquiv 2 ğ•œ (_ â†’ ğ•œ)).symm.surjective]
        simp only [LinearIsometryEquiv.toLinearEquiv_symm]
        refine LinearMap.pi_ext fun i k => ?_
        have : k = k â€¢ (1 : ğ•œ) := by rw [smul_eq_mul, mul_one]
        rw [this, Pi.single_smul]
        replace h := congr_fun h i
        simp only [LinearEquiv.comp_coe, map_smul, LinearEquiv.coe_coe,
          LinearEquiv.trans_apply, WithLp.linearEquiv_symm_apply, WithLp.equiv_symm_single,
          LinearIsometryEquiv.coe_toLinearEquiv] at h âŠ¢
        rw [h]

#noalign orthonormal_basis.has_coe_to_fun

@[simp]
theorem coe_ofRepr [DecidableEq Î¹] (e : E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹) :
    â‡‘(OrthonormalBasis.ofRepr e) = fun i => e.symm (EuclideanSpace.single i (1 : ğ•œ)) := by
  -- Porting note: simplified with `congr!`
  dsimp only [DFunLike.coe]
  funext
  congr!
#align orthonormal_basis.coe_of_repr OrthonormalBasis.coe_ofRepr

@[simp]
protected theorem repr_symm_single [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr.symm (EuclideanSpace.single i (1 : ğ•œ)) = b i := by
  -- Porting note: simplified with `congr!`
  dsimp only [DFunLike.coe]
  congr!
#align orthonormal_basis.repr_symm_single OrthonormalBasis.repr_symm_single

@[simp]
protected theorem repr_self [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr (b i) = EuclideanSpace.single i (1 : ğ•œ) := by
  rw [â† b.repr_symm_single i, LinearIsometryEquiv.apply_symm_apply]
#align orthonormal_basis.repr_self OrthonormalBasis.repr_self

protected theorem repr_apply_apply (b : OrthonormalBasis Î¹ ğ•œ E) (v : E) (i : Î¹) :
    b.repr v i = âŸªb i, vâŸ« := by
  classical
    rw [â† b.repr.inner_map_map (b i) v, b.repr_self i, EuclideanSpace.inner_single_left]
    simp only [one_mul, eq_self_iff_true, map_one]
#align orthonormal_basis.repr_apply_apply OrthonormalBasis.repr_apply_apply

@[simp]
protected theorem orthonormal (b : OrthonormalBasis Î¹ ğ•œ E) : Orthonormal ğ•œ b := by
  classical
    rw [orthonormal_iff_ite]
    intro i j
    rw [â† b.repr.inner_map_map (b i) (b j), b.repr_self i, b.repr_self j,
      EuclideanSpace.inner_single_left, EuclideanSpace.single_apply, map_one, one_mul]
#align orthonormal_basis.orthonormal OrthonormalBasis.orthonormal

/-- The `Basis Î¹ ğ•œ E` underlying the `OrthonormalBasis` -/
protected def toBasis (b : OrthonormalBasis Î¹ ğ•œ E) : Basis Î¹ ğ•œ E :=
  Basis.ofEquivFun b.repr.toLinearEquiv
#align orthonormal_basis.to_basis OrthonormalBasis.toBasis

@[simp]
protected theorem coe_toBasis (b : OrthonormalBasis Î¹ ğ•œ E) : (â‡‘b.toBasis : Î¹ â†’ E) = â‡‘b := by
  rw [OrthonormalBasis.toBasis] -- Porting note: was `change`
  ext j
  classical
    rw [Basis.coe_ofEquivFun]
    congr
#align orthonormal_basis.coe_to_basis OrthonormalBasis.coe_toBasis

@[simp]
protected theorem coe_toBasis_repr (b : OrthonormalBasis Î¹ ğ•œ E) :
    b.toBasis.equivFun = b.repr.toLinearEquiv :=
  Basis.equivFun_ofEquivFun _
#align orthonormal_basis.coe_to_basis_repr OrthonormalBasis.coe_toBasis_repr

@[simp]
protected theorem coe_toBasis_repr_apply (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) (i : Î¹) :
    b.toBasis.repr x i = b.repr x i := by
  rw [â† Basis.equivFun_apply, OrthonormalBasis.coe_toBasis_repr];
  -- This used to be `rw`, but we need `erw` after leanprover/lean4#2644
  erw [LinearIsometryEquiv.coe_toLinearEquiv]
#align orthonormal_basis.coe_to_basis_repr_apply OrthonormalBasis.coe_toBasis_repr_apply

protected theorem sum_repr (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) : âˆ‘ i, b.repr x i â€¢ b i = x := by
  simp_rw [â† b.coe_toBasis_repr_apply, â† b.coe_toBasis]
  exact b.toBasis.sum_repr x
#align orthonormal_basis.sum_repr OrthonormalBasis.sum_repr

protected theorem sum_repr_symm (b : OrthonormalBasis Î¹ ğ•œ E) (v : EuclideanSpace ğ•œ Î¹) :
    âˆ‘ i, v i â€¢ b i = b.repr.symm v := by simpa using (b.toBasis.equivFun_symm_apply v).symm
#align orthonormal_basis.sum_repr_symm OrthonormalBasis.sum_repr_symm

protected theorem sum_inner_mul_inner (b : OrthonormalBasis Î¹ ğ•œ E) (x y : E) :
    âˆ‘ i, âŸªx, b iâŸ« * âŸªb i, yâŸ« = âŸªx, yâŸ« := by
  have := congr_arg (innerSL ğ•œ x) (b.sum_repr y)
  rw [map_sum] at this
  convert this
  rw [map_smul, b.repr_apply_apply, mul_comm]
  simp only [innerSL_apply, smul_eq_mul] -- Porting note: was `rfl`
#align orthonormal_basis.sum_inner_mul_inner OrthonormalBasis.sum_inner_mul_inner

protected theorem orthogonalProjection_eq_sum {U : Submodule ğ•œ E} [CompleteSpace U]
    (b : OrthonormalBasis Î¹ ğ•œ U) (x : E) :
    orthogonalProjection U x = âˆ‘ i, âŸª(b i : E), xâŸ« â€¢ b i := by
  simpa only [b.repr_apply_apply, inner_orthogonalProjection_eq_of_mem_left] using
    (b.sum_repr (orthogonalProjection U x)).symm
#align orthonormal_basis.orthogonal_projection_eq_sum OrthonormalBasis.orthogonalProjection_eq_sum

/-- Mapping an orthonormal basis along a `LinearIsometryEquiv`. -/
protected def map {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) : OrthonormalBasis Î¹ ğ•œ G where
  repr := L.symm.trans b.repr
#align orthonormal_basis.map OrthonormalBasis.map

@[simp]
protected theorem map_apply {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) (i : Î¹) : b.map L i = L (b i) :=
  rfl
#align orthonormal_basis.map_apply OrthonormalBasis.map_apply

@[simp]
protected theorem toBasis_map {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) :
    (b.map L).toBasis = b.toBasis.map L.toLinearEquiv :=
  rfl
#align orthonormal_basis.to_basis_map OrthonormalBasis.toBasis_map

/-- A basis that is orthonormal is an orthonormal basis. -/
def _root_.Basis.toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    OrthonormalBasis Î¹ ğ•œ E :=
  OrthonormalBasis.ofRepr <|
    LinearEquiv.isometryOfInner v.equivFun
      (by
        intro x y
        let p : EuclideanSpace ğ•œ Î¹ := v.equivFun x
        let q : EuclideanSpace ğ•œ Î¹ := v.equivFun y
        have key : âŸªp, qâŸ« = âŸªâˆ‘ i, p i â€¢ v i, âˆ‘ i, q i â€¢ v iâŸ« := by
          simp [sum_inner, inner_smul_left, hv.inner_right_fintype]
        convert key
        Â· rw [â† v.equivFun.symm_apply_apply x, v.equivFun_symm_apply]
        Â· rw [â† v.equivFun.symm_apply_apply y, v.equivFun_symm_apply])
#align basis.to_orthonormal_basis Basis.toOrthonormalBasis

@[simp]
theorem _root_.Basis.coe_toOrthonormalBasis_repr (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    ((v.toOrthonormalBasis hv).repr : E â†’ EuclideanSpace ğ•œ Î¹) = v.equivFun :=
  rfl
#align basis.coe_to_orthonormal_basis_repr Basis.coe_toOrthonormalBasis_repr

@[simp]
theorem _root_.Basis.coe_toOrthonormalBasis_repr_symm (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    ((v.toOrthonormalBasis hv).repr.symm : EuclideanSpace ğ•œ Î¹ â†’ E) = v.equivFun.symm :=
  rfl
#align basis.coe_to_orthonormal_basis_repr_symm Basis.coe_toOrthonormalBasis_repr_symm

@[simp]
theorem _root_.Basis.toBasis_toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    (v.toOrthonormalBasis hv).toBasis = v := by
  simp [Basis.toOrthonormalBasis, OrthonormalBasis.toBasis]
#align basis.to_basis_to_orthonormal_basis Basis.toBasis_toOrthonormalBasis

@[simp]
theorem _root_.Basis.coe_toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    (v.toOrthonormalBasis hv : Î¹ â†’ E) = (v : Î¹ â†’ E) :=
  calc
    (v.toOrthonormalBasis hv : Î¹ â†’ E) = ((v.toOrthonormalBasis hv).toBasis : Î¹ â†’ E) := by
      classical rw [OrthonormalBasis.coe_toBasis]
    _ = (v : Î¹ â†’ E) := by simp
#align basis.coe_to_orthonormal_basis Basis.coe_toOrthonormalBasis

/-- `Pi.orthonormalBasis (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i))` is the
`Î£ i, Î¹ i`-indexed orthonormal basis on `Î  i, E i` given by `B i` on each component. -/
protected def _root_.Pi.orthonormalBasis {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) :
    OrthonormalBasis ((i : Î·) Ã— Î¹ i) ğ•œ (PiLp 2 E) where
  repr := .trans
      (.piLpCongrRight 2 fun i => (B i).repr)
      (.symm <| .piLpCurry ğ•œ 2 fun _ _ => ğ•œ)

theorem _root_.Pi.orthonormalBasis.toBasis {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) :
    (Pi.orthonormalBasis B).toBasis =
      ((Pi.basis fun i : Î· â†¦ (B i).toBasis).map (WithLp.linearEquiv 2 _ _).symm) := by ext; rfl

@[simp]
theorem _root_.Pi.orthonormalBasis_apply {Î· : Type*} [Fintype Î·] [DecidableEq Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i))
    (j : (i : Î·) Ã— (Î¹ i)) :
    Pi.orthonormalBasis B j = (WithLp.equiv _ _).symm (Pi.single _ (B j.fst j.snd)) := by
  classical
  ext k
  obtain âŸ¨i, jâŸ© := j
  simp only [Pi.orthonormalBasis, coe_ofRepr, LinearIsometryEquiv.symm_trans,
    LinearIsometryEquiv.symm_symm, LinearIsometryEquiv.piLpCongrRight_symm,
    LinearIsometryEquiv.trans_apply, LinearIsometryEquiv.piLpCongrRight_apply,
    LinearIsometryEquiv.piLpCurry_apply, WithLp.equiv_single, WithLp.equiv_symm_pi_apply,
    Sigma.curry_single (Î³ := fun _ _ => ğ•œ)]
  obtain rfl | hi := Decidable.eq_or_ne i k
  Â· simp only [Pi.single_eq_same, WithLp.equiv_symm_single, OrthonormalBasis.repr_symm_single]
  Â· simp only [Pi.single_eq_of_ne' hi, WithLp.equiv_symm_zero, _root_.map_zero]

@[simp]
theorem _root_.Pi.orthonormalBasis_repr {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) (x : (i : Î· ) â†’ E i)
    (j : (i : Î·) Ã— (Î¹ i)) :
    (Pi.orthonormalBasis B).repr x j = (B j.fst).repr (x j.fst) j.snd := rfl

variable {v : Î¹ â†’ E}

/-- A finite orthonormal set that spans is an orthonormal basis -/
protected def mk (hon : Orthonormal ğ•œ v) (hsp : âŠ¤ â‰¤ Submodule.span ğ•œ (Set.range v)) :
    OrthonormalBasis Î¹ ğ•œ E :=
  (Basis.mk (Orthonormal.linearIndependent hon) hsp).toOrthonormalBasis (by rwa [Basis.coe_mk])
#align orthonormal_basis.mk OrthonormalBasis.mk

@[simp]
protected theorem coe_mk (hon : Orthonormal ğ•œ v) (hsp : âŠ¤ â‰¤ Submodule.span ğ•œ (Set.range v)) :
    â‡‘(OrthonormalBasis.mk hon hsp) = v := by
  classical rw [OrthonormalBasis.mk, _root_.Basis.coe_toOrthonormalBasis, Basis.coe_mk]
#align orthonormal_basis.coe_mk OrthonormalBasis.coe_mk

/-- Any finite subset of an orthonormal family is an `OrthonormalBasis` for its span. -/
protected def span [DecidableEq E] {v' : Î¹' â†’ E} (h : Orthonormal ğ•œ v') (s : Finset Î¹') :
    OrthonormalBasis s ğ•œ (span ğ•œ (s.image v' : Set E)) :=
  let eâ‚€' : Basis s ğ•œ _ :=
    Basis.span (h.linearIndependent.comp ((â†‘) : s â†’ Î¹') Subtype.val_injective)
  let eâ‚€ : OrthonormalBasis s ğ•œ _ :=
    OrthonormalBasis.mk
      (by
        convert orthonormal_span (h.comp ((â†‘) : s â†’ Î¹') Subtype.val_injective)
        simp [eâ‚€', Basis.span_apply])
      eâ‚€'.span_eq.ge
  let Ï† : span ğ•œ (s.image v' : Set E) â‰ƒâ‚—áµ¢[ğ•œ] span ğ•œ (range (v' âˆ˜ ((â†‘) : s â†’ Î¹'))) :=
    LinearIsometryEquiv.ofEq _ _
      (by
        rw [Finset.coe_image, image_eq_range]
        rfl)
  eâ‚€.map Ï†.symm
#align orthonormal_basis.span OrthonormalBasis.span

@[simp]
protected theorem span_apply [DecidableEq E] {v' : Î¹' â†’ E} (h : Orthonormal ğ•œ v') (s : Finset Î¹')
    (i : s) : (OrthonormalBasis.span h s i : E) = v' i := by
  simp only [OrthonormalBasis.span, Basis.span_apply, LinearIsometryEquiv.ofEq_symm,
    OrthonormalBasis.map_apply, OrthonormalBasis.coe_mk, LinearIsometryEquiv.coe_ofEq_apply,
    comp_apply]
#align orthonormal_basis.span_apply OrthonormalBasis.span_apply

open Submodule

/-- A finite orthonormal family of vectors whose span has trivial orthogonal complement is an
orthonormal basis. -/
protected def mkOfOrthogonalEqBot (hon : Orthonormal ğ•œ v) (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) :
    OrthonormalBasis Î¹ ğ•œ E :=
  OrthonormalBasis.mk hon
    (by
      refine Eq.ge ?_
      haveI : FiniteDimensional ğ•œ (span ğ•œ (range v)) :=
        FiniteDimensional.span_of_finite ğ•œ (finite_range v)
      haveI : CompleteSpace (span ğ•œ (range v)) := FiniteDimensional.complete ğ•œ _
      rwa [orthogonal_eq_bot_iff] at hsp)
#align orthonormal_basis.mk_of_orthogonal_eq_bot OrthonormalBasis.mkOfOrthogonalEqBot

@[simp]
protected theorem coe_of_orthogonal_eq_bot_mk (hon : Orthonormal ğ•œ v)
    (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) : â‡‘(OrthonormalBasis.mkOfOrthogonalEqBot hon hsp) = v :=
  OrthonormalBasis.coe_mk hon _
#align orthonormal_basis.coe_of_orthogonal_eq_bot_mk OrthonormalBasis.coe_of_orthogonal_eq_bot_mk

variable [Fintype Î¹']

/-- `b.reindex (e : Î¹ â‰ƒ Î¹')` is an `OrthonormalBasis` indexed by `Î¹'` -/
def reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') : OrthonormalBasis Î¹' ğ•œ E :=
  OrthonormalBasis.ofRepr (b.repr.trans (LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ e))
#align orthonormal_basis.reindex OrthonormalBasis.reindex

protected theorem reindex_apply (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (i' : Î¹') :
    (b.reindex e) i' = b (e.symm i') := by
  classical
    dsimp [reindex]
    rw [coe_ofRepr]
    dsimp
    rw [â† b.repr_symm_single, LinearIsometryEquiv.piLpCongrLeft_symm,
      EuclideanSpace.piLpCongrLeft_single]
#align orthonormal_basis.reindex_apply OrthonormalBasis.reindex_apply

@[simp]
theorem reindex_toBasis (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') :
    (b.reindex e).toBasis = b.toBasis.reindex e := Basis.eq_ofRepr_eq_repr fun _ â†¦ congr_fun rfl

@[simp]
protected theorem coe_reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') :
    â‡‘(b.reindex e) = b âˆ˜ e.symm :=
  funext (b.reindex_apply e)
#align orthonormal_basis.coe_reindex OrthonormalBasis.coe_reindex

@[simp]
protected theorem repr_reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (x : E) (i' : Î¹') :
    (b.reindex e).repr x i' = b.repr x (e.symm i') := by
  classical
  rw [OrthonormalBasis.repr_apply_apply, b.repr_apply_apply, OrthonormalBasis.coe_reindex,
    comp_apply]
#align orthonormal_basis.repr_reindex OrthonormalBasis.repr_reindex

end OrthonormalBasis

namespace EuclideanSpace

variable (ğ•œ Î¹)

/-- The basis `Pi.basisFun`, bundled as an orthornormal basis of `EuclideanSpace ğ•œ Î¹`. -/
noncomputable def basisFun : OrthonormalBasis Î¹ ğ•œ (EuclideanSpace ğ•œ Î¹) :=
  âŸ¨LinearIsometryEquiv.refl _ _âŸ©

@[simp]
theorem basisFun_apply [DecidableEq Î¹] (i : Î¹) : basisFun Î¹ ğ•œ i = EuclideanSpace.single i 1 :=
  PiLp.basisFun_apply _ _ _ _

@[simp]
theorem basisFun_repr (x : EuclideanSpace ğ•œ Î¹) (i : Î¹) : (basisFun Î¹ ğ•œ).repr x i = x i := rfl

theorem basisFun_toBasis : (basisFun Î¹ ğ•œ).toBasis = PiLp.basisFun _ ğ•œ Î¹ := rfl

end EuclideanSpace

instance OrthonormalBasis.instInhabited : Inhabited (OrthonormalBasis Î¹ ğ•œ (EuclideanSpace ğ•œ Î¹)) :=
  âŸ¨EuclideanSpace.basisFun Î¹ ğ•œâŸ©
#align orthonormal_basis.inhabited OrthonormalBasis.instInhabited

section Complex

/-- `![1, I]` is an orthonormal basis for `â„‚` considered as a real inner product space. -/
def Complex.orthonormalBasisOneI : OrthonormalBasis (Fin 2) â„ â„‚ :=
  Complex.basisOneI.toOrthonormalBasis
    (by
      rw [orthonormal_iff_ite]
      intro i; fin_cases i <;> intro j <;> fin_cases j <;> simp [real_inner_eq_re_inner])
#align complex.orthonormal_basis_one_I Complex.orthonormalBasisOneI

@[simp]
theorem Complex.orthonormalBasisOneI_repr_apply (z : â„‚) :
    Complex.orthonormalBasisOneI.repr z = ![z.re, z.im] :=
  rfl
#align complex.orthonormal_basis_one_I_repr_apply Complex.orthonormalBasisOneI_repr_apply

@[simp]
theorem Complex.orthonormalBasisOneI_repr_symm_apply (x : EuclideanSpace â„ (Fin 2)) :
    Complex.orthonormalBasisOneI.repr.symm x = x 0 + x 1 * I :=
  rfl
#align complex.orthonormal_basis_one_I_repr_symm_apply Complex.orthonormalBasisOneI_repr_symm_apply

@[simp]
theorem Complex.toBasis_orthonormalBasisOneI :
    Complex.orthonormalBasisOneI.toBasis = Complex.basisOneI :=
  Basis.toBasis_toOrthonormalBasis _ _
#align complex.to_basis_orthonormal_basis_one_I Complex.toBasis_orthonormalBasisOneI

@[simp]
theorem Complex.coe_orthonormalBasisOneI :
    (Complex.orthonormalBasisOneI : Fin 2 â†’ â„‚) = ![1, I] := by
  simp [Complex.orthonormalBasisOneI]
#align complex.coe_orthonormal_basis_one_I Complex.coe_orthonormalBasisOneI

/-- The isometry between `â„‚` and a two-dimensional real inner product space given by a basis. -/
def Complex.isometryOfOrthonormal (v : OrthonormalBasis (Fin 2) â„ F) : â„‚ â‰ƒâ‚—áµ¢[â„] F :=
  Complex.orthonormalBasisOneI.repr.trans v.repr.symm
#align complex.isometry_of_orthonormal Complex.isometryOfOrthonormal

@[simp]
theorem Complex.map_isometryOfOrthonormal (v : OrthonormalBasis (Fin 2) â„ F) (f : F â‰ƒâ‚—áµ¢[â„] F') :
    Complex.isometryOfOrthonormal (v.map f) = (Complex.isometryOfOrthonormal v).trans f := by
  simp [Complex.isometryOfOrthonormal, LinearIsometryEquiv.trans_assoc, OrthonormalBasis.map]
  -- Porting note: `LinearIsometryEquiv.trans_assoc` doesn't trigger in the `simp` above
  rw [LinearIsometryEquiv.trans_assoc]
#align complex.map_isometry_of_orthonormal Complex.map_isometryOfOrthonormal

theorem Complex.isometryOfOrthonormal_symm_apply (v : OrthonormalBasis (Fin 2) â„ F) (f : F) :
    (Complex.isometryOfOrthonormal v).symm f =
      (v.toBasis.coord 0 f : â„‚) + (v.toBasis.coord 1 f : â„‚) * I := by
  simp [Complex.isometryOfOrthonormal]
#align complex.isometry_of_orthonormal_symm_apply Complex.isometryOfOrthonormal_symm_apply

theorem Complex.isometryOfOrthonormal_apply (v : OrthonormalBasis (Fin 2) â„ F) (z : â„‚) :
    Complex.isometryOfOrthonormal v z = z.re â€¢ v 0 + z.im â€¢ v 1 := by
  -- Porting note: was
  -- simp [Complex.isometryOfOrthonormal, â† v.sum_repr_symm]
  rw [Complex.isometryOfOrthonormal, LinearIsometryEquiv.trans_apply]
  simp [â† v.sum_repr_symm]
#align complex.isometry_of_orthonormal_apply Complex.isometryOfOrthonormal_apply

end Complex

open FiniteDimensional

/-! ### Matrix representation of an orthonormal basis with respect to another -/


section ToMatrix

variable [DecidableEq Î¹]

section

variable (a b : OrthonormalBasis Î¹ ğ•œ E)

/-- The change-of-basis matrix between two orthonormal bases `a`, `b` is a unitary matrix. -/
theorem OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary :
    a.toBasis.toMatrix b âˆˆ Matrix.unitaryGroup Î¹ ğ•œ := by
  rw [Matrix.mem_unitaryGroup_iff']
  ext i j
  convert a.repr.inner_map_map (b i) (b j)
  rw [orthonormal_iff_ite.mp b.orthonormal i j]
  rfl
#align orthonormal_basis.to_matrix_orthonormal_basis_mem_unitary OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary

/-- The determinant of the change-of-basis matrix between two orthonormal bases `a`, `b` has
unit length. -/
@[simp]
theorem OrthonormalBasis.det_to_matrix_orthonormalBasis : â€–a.toBasis.det bâ€– = 1 := by
  have := (Matrix.det_of_mem_unitary (a.toMatrix_orthonormalBasis_mem_unitary b)).2
  rw [star_def, RCLike.mul_conj] at this
  norm_cast at this
  rwa [pow_eq_one_iff_of_nonneg (norm_nonneg _) two_ne_zero] at this
#align orthonormal_basis.det_to_matrix_orthonormal_basis OrthonormalBasis.det_to_matrix_orthonormalBasis

end

section Real

variable (a b : OrthonormalBasis Î¹ â„ F)

/-- The change-of-basis matrix between two orthonormal bases `a`, `b` is an orthogonal matrix. -/
theorem OrthonormalBasis.toMatrix_orthonormalBasis_mem_orthogonal :
    a.toBasis.toMatrix b âˆˆ Matrix.orthogonalGroup Î¹ â„ :=
  a.toMatrix_orthonormalBasis_mem_unitary b
#align orthonormal_basis.to_matrix_orthonormal_basis_mem_orthogonal OrthonormalBasis.toMatrix_orthonormalBasis_mem_orthogonal

/-- The determinant of the change-of-basis matrix between two orthonormal bases `a`, `b` is Â±1. -/
theorem OrthonormalBasis.det_to_matrix_orthonormalBasis_real :
    a.toBasis.det b = 1 âˆ¨ a.toBasis.det b = -1 := by
  rw [â† sq_eq_one_iff]
  simpa [unitary, sq] using Matrix.det_of_mem_unitary (a.toMatrix_orthonormalBasis_mem_unitary b)
#align orthonormal_basis.det_to_matrix_orthonormal_basis_real OrthonormalBasis.det_to_matrix_orthonormalBasis_real

end Real

end ToMatrix

/-! ### Existence of orthonormal basis, etc. -/


section FiniteDimensional

variable {v : Set E}
variable {A : Î¹ â†’ Submodule ğ•œ E}

/-- Given an internal direct sum decomposition of a module `M`, and an orthonormal basis for each
of the components of the direct sum, the disjoint union of these orthonormal bases is an
orthonormal basis for `M`. -/
noncomputable def DirectSum.IsInternal.collectedOrthonormalBasis
    (hV : OrthogonalFamily ğ•œ (fun i => A i) fun i => (A i).subtypeâ‚—áµ¢) [DecidableEq Î¹]
    (hV_sum : DirectSum.IsInternal fun i => A i) {Î± : Î¹ â†’ Type*} [âˆ€ i, Fintype (Î± i)]
    (v_family : âˆ€ i, OrthonormalBasis (Î± i) ğ•œ (A i)) : OrthonormalBasis (Î£i, Î± i) ğ•œ E :=
  (hV_sum.collectedBasis fun i => (v_family i).toBasis).toOrthonormalBasis <| by
    simpa using
      hV.orthonormal_sigma_orthonormal (show âˆ€ i, Orthonormal ğ•œ (v_family i).toBasis by simp)
#align direct_sum.is_internal.collected_orthonormal_basis DirectSum.IsInternal.collectedOrthonormalBasis

theorem DirectSum.IsInternal.collectedOrthonormalBasis_mem [DecidableEq Î¹]
    (h : DirectSum.IsInternal A) {Î± : Î¹ â†’ Type*} [âˆ€ i, Fintype (Î± i)]
    (hV : OrthogonalFamily ğ•œ (fun i => A i) fun i => (A i).subtypeâ‚—áµ¢)
    (v : âˆ€ i, OrthonormalBasis (Î± i) ğ•œ (A i)) (a : Î£i, Î± i) :
    h.collectedOrthonormalBasis hV v a âˆˆ A a.1 := by
  simp [DirectSum.IsInternal.collectedOrthonormalBasis]
#align direct_sum.is_internal.collected_orthonormal_basis_mem DirectSum.IsInternal.collectedOrthonormalBasis_mem

variable [FiniteDimensional ğ•œ E]

/-- In a finite-dimensional `InnerProductSpace`, any orthonormal subset can be extended to an
orthonormal basis. -/
theorem Orthonormal.exists_orthonormalBasis_extension (hv : Orthonormal ğ•œ ((â†‘) : v â†’ E)) :
    âˆƒ (u : Finset E) (b : OrthonormalBasis u ğ•œ E), v âŠ† u âˆ§ â‡‘b = ((â†‘) : u â†’ E) := by
  obtain âŸ¨uâ‚€, huâ‚€s, huâ‚€, huâ‚€_maxâŸ© := exists_maximal_orthonormal hv
  rw [maximal_orthonormal_iff_orthogonalComplement_eq_bot huâ‚€] at huâ‚€_max
  have huâ‚€_finite : uâ‚€.Finite := huâ‚€.linearIndependent.setFinite
  let u : Finset E := huâ‚€_finite.toFinset
  let fu : â†¥u â‰ƒ â†¥uâ‚€ := huâ‚€_finite.subtypeEquivToFinset.symm
  have hu : Orthonormal ğ•œ ((â†‘) : u â†’ E) := by simpa using huâ‚€.comp _ fu.injective
  refine âŸ¨u, OrthonormalBasis.mkOfOrthogonalEqBot hu ?_, ?_, ?_âŸ©
  Â· simpa [u] using huâ‚€_max
  Â· simpa [u] using huâ‚€s
  Â· simp
#align orthonormal.exists_orthonormal_basis_extension Orthonormal.exists_orthonormalBasis_extension

theorem Orthonormal.exists_orthonormalBasis_extension_of_card_eq {Î¹ : Type*} [Fintype Î¹]
    (card_Î¹ : finrank ğ•œ E = Fintype.card Î¹) {v : Î¹ â†’ E} {s : Set Î¹}
    (hv : Orthonormal ğ•œ (s.restrict v)) : âˆƒ b : OrthonormalBasis Î¹ ğ•œ E, âˆ€ i âˆˆ s, b i = v i := by
  have hsv : Injective (s.restrict v) := hv.linearIndependent.injective
  have hX : Orthonormal ğ•œ ((â†‘) : Set.range (s.restrict v) â†’ E) := by
    rwa [orthonormal_subtype_range hsv]
  obtain âŸ¨Y, bâ‚€, hX, hbâ‚€âŸ© := hX.exists_orthonormalBasis_extension
  have hÎ¹Y : Fintype.card Î¹ = Y.card := by
    refine card_Î¹.symm.trans ?_
    exact FiniteDimensional.finrank_eq_card_finset_basis bâ‚€.toBasis
  have hvsY : s.MapsTo v Y := (s.mapsTo_image v).mono_right (by rwa [â† range_restrict])
  have hsv' : Set.InjOn v s := by
    rw [Set.injOn_iff_injective]
    exact hsv
  obtain âŸ¨g, hgâŸ© := hvsY.exists_equiv_extend_of_card_eq hÎ¹Y hsv'
  use bâ‚€.reindex g.symm
  intro i hi
  simp [hbâ‚€, hg i hi]
#align orthonormal.exists_orthonormal_basis_extension_of_card_eq Orthonormal.exists_orthonormalBasis_extension_of_card_eq

variable (ğ•œ E)

/-- A finite-dimensional inner product space admits an orthonormal basis. -/
theorem _root_.exists_orthonormalBasis :
    âˆƒ (w : Finset E) (b : OrthonormalBasis w ğ•œ E), â‡‘b = ((â†‘) : w â†’ E) :=
  let âŸ¨w, hw, _, hw''âŸ© := (orthonormal_empty ğ•œ E).exists_orthonormalBasis_extension
  âŸ¨w, hw, hw''âŸ©
#align exists_orthonormal_basis exists_orthonormalBasis

/-- A finite-dimensional `InnerProductSpace` has an orthonormal basis. -/
irreducible_def stdOrthonormalBasis : OrthonormalBasis (Fin (finrank ğ•œ E)) ğ•œ E := by
  let b := Classical.choose (Classical.choose_spec <| exists_orthonormalBasis ğ•œ E)
  rw [finrank_eq_card_basis b.toBasis]
  exact b.reindex (Fintype.equivFinOfCardEq rfl)
#align std_orthonormal_basis stdOrthonormalBasis

/-- An orthonormal basis of `â„` is made either of the vector `1`, or of the vector `-1`. -/
theorem orthonormalBasis_one_dim (b : OrthonormalBasis Î¹ â„ â„) :
    (â‡‘b = fun _ => (1 : â„)) âˆ¨ â‡‘b = fun _ => (-1 : â„) := by
  have : Unique Î¹ := b.toBasis.unique
  have : b default = 1 âˆ¨ b default = -1 := by
    have : â€–b defaultâ€– = 1 := b.orthonormal.1 _
    rwa [Real.norm_eq_abs, abs_eq (zero_le_one' â„)] at this
  rw [eq_const_of_unique b]
  refine this.imp ?_ ?_ <;> (intro; ext; simp [*])
#align orthonormal_basis_one_dim orthonormalBasis_one_dim

variable {ğ•œ E}

section SubordinateOrthonormalBasis

open DirectSum

variable {n : â„•} (hn : finrank ğ•œ E = n) [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E} (hV : IsInternal V)

/-- Exhibit a bijection between `Fin n` and the index set of a certain basis of an `n`-dimensional
inner product space `E`.  This should not be accessed directly, but only via the subsequent API. -/
irreducible_def DirectSum.IsInternal.sigmaOrthonormalBasisIndexEquiv
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    (Î£i, Fin (finrank ğ•œ (V i))) â‰ƒ Fin n :=
  let b := hV.collectedOrthonormalBasis hV' fun i => stdOrthonormalBasis ğ•œ (V i)
  Fintype.equivFinOfCardEq <| (FiniteDimensional.finrank_eq_card_basis b.toBasis).symm.trans hn
#align direct_sum.is_internal.sigma_orthonormal_basis_index_equiv DirectSum.IsInternal.sigmaOrthonormalBasisIndexEquiv

/-- An `n`-dimensional `InnerProductSpace` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `Fin n` and subordinate to that direct sum. -/
irreducible_def DirectSum.IsInternal.subordinateOrthonormalBasis
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    OrthonormalBasis (Fin n) ğ•œ E :=
  (hV.collectedOrthonormalBasis hV' fun i => stdOrthonormalBasis ğ•œ (V i)).reindex
    (hV.sigmaOrthonormalBasisIndexEquiv hn hV')
#align direct_sum.is_internal.subordinate_orthonormal_basis DirectSum.IsInternal.subordinateOrthonormalBasis

/-- An `n`-dimensional `InnerProductSpace` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `Fin n` and subordinate to that direct sum. This function
provides the mapping by which it is subordinate. -/
irreducible_def DirectSum.IsInternal.subordinateOrthonormalBasisIndex (a : Fin n)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) : Î¹ :=
  ((hV.sigmaOrthonormalBasisIndexEquiv hn hV').symm a).1
#align direct_sum.is_internal.subordinate_orthonormal_basis_index DirectSum.IsInternal.subordinateOrthonormalBasisIndex

/-- The basis constructed in `DirectSum.IsInternal.subordinateOrthonormalBasis` is subordinate to
the `OrthogonalFamily` in question. -/
theorem DirectSum.IsInternal.subordinateOrthonormalBasis_subordinate (a : Fin n)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    hV.subordinateOrthonormalBasis hn hV' a âˆˆ V (hV.subordinateOrthonormalBasisIndex hn a hV') := by
  simpa only [DirectSum.IsInternal.subordinateOrthonormalBasis, OrthonormalBasis.coe_reindex,
    DirectSum.IsInternal.subordinateOrthonormalBasisIndex] using
    hV.collectedOrthonormalBasis_mem hV' (fun i => stdOrthonormalBasis ğ•œ (V i))
      ((hV.sigmaOrthonormalBasisIndexEquiv hn hV').symm a)
#align direct_sum.is_internal.subordinate_orthonormal_basis_subordinate DirectSum.IsInternal.subordinateOrthonormalBasis_subordinate

end SubordinateOrthonormalBasis

end FiniteDimensional

/-- Given a natural number `n` one less than the `finrank` of a finite-dimensional inner product
space, there exists an isometry from the orthogonal complement of a nonzero singleton to
`EuclideanSpace ğ•œ (Fin n)`. -/
def OrthonormalBasis.fromOrthogonalSpanSingleton (n : â„•) [Fact (finrank ğ•œ E = n + 1)] {v : E}
    (hv : v â‰  0) : OrthonormalBasis (Fin n) ğ•œ (ğ•œ âˆ™ v)á—® :=
  -- Porting note: was `attribute [local instance] FiniteDimensional.of_fact_finrank_eq_succ`
  haveI : FiniteDimensional ğ•œ E := .of_fact_finrank_eq_succ (K := ğ•œ) (V := E) n
  (stdOrthonormalBasis _ _).reindex <| finCongr <| finrank_orthogonal_span_singleton hv
#align orthonormal_basis.from_orthogonal_span_singleton OrthonormalBasis.fromOrthogonalSpanSingleton

section LinearIsometry

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [FiniteDimensional ğ•œ V]
variable {S : Submodule ğ•œ V} {L : S â†’â‚—áµ¢[ğ•œ] V}

open FiniteDimensional

/-- Let `S` be a subspace of a finite-dimensional complex inner product space `V`.  A linear
isometry mapping `S` into `V` can be extended to a full isometry of `V`.

TODO:  The case when `S` is a finite-dimensional subspace of an infinite-dimensional `V`. -/
noncomputable def LinearIsometry.extend (L : S â†’â‚—áµ¢[ğ•œ] V) : V â†’â‚—áµ¢[ğ•œ] V := by
  -- Build an isometry from Sá—® to L(S)á—® through `EuclideanSpace`
  let d := finrank ğ•œ Sá—®
  let LS := LinearMap.range L.toLinearMap
  have E : Sá—® â‰ƒâ‚—áµ¢[ğ•œ] LSá—® := by
    have dim_LS_perp : finrank ğ•œ LSá—® = d :=
      calc
        finrank ğ•œ LSá—® = finrank ğ•œ V - finrank ğ•œ LS := by
          simp only [â† LS.finrank_add_finrank_orthogonal, add_tsub_cancel_left]
        _ = finrank ğ•œ V - finrank ğ•œ S := by simp only [LinearMap.finrank_range_of_inj L.injective]
        _ = finrank ğ•œ Sá—® := by simp only [â† S.finrank_add_finrank_orthogonal, add_tsub_cancel_left]

    exact
      (stdOrthonormalBasis ğ•œ Sá—®).repr.trans
        ((stdOrthonormalBasis ğ•œ LSá—®).reindex <| finCongr dim_LS_perp).repr.symm
  let L3 := LSá—®.subtypeâ‚—áµ¢.comp E.toLinearIsometry
  -- Project onto S and Sá—®
  haveI : CompleteSpace S := FiniteDimensional.complete ğ•œ S
  haveI : CompleteSpace V := FiniteDimensional.complete ğ•œ V
  let p1 := (orthogonalProjection S).toLinearMap
  let p2 := (orthogonalProjection Sá—®).toLinearMap
  -- Build a linear map from the isometries on S and Sá—®
  let M := L.toLinearMap.comp p1 + L3.toLinearMap.comp p2
  -- Prove that M is an isometry
  have M_norm_map : âˆ€ x : V, â€–M xâ€– = â€–xâ€– := by
    intro x
    -- Apply M to the orthogonal decomposition of x
    have Mx_decomp : M x = L (p1 x) + L3 (p2 x) := by
      simp only [M, LinearMap.add_apply, LinearMap.comp_apply, LinearMap.comp_apply,
        LinearIsometry.coe_toLinearMap]
    -- Mx_decomp is the orthogonal decomposition of M x
    have Mx_orth : âŸªL (p1 x), L3 (p2 x)âŸ« = 0 := by
      have Lp1x : L (p1 x) âˆˆ LinearMap.range L.toLinearMap :=
        LinearMap.mem_range_self L.toLinearMap (p1 x)
      have Lp2x : L3 (p2 x) âˆˆ (LinearMap.range L.toLinearMap)á—® := by
        simp only [LinearIsometry.coe_comp, Function.comp_apply, Submodule.coe_subtypeâ‚—áµ¢, â†
          Submodule.range_subtype LSá—®]
        apply LinearMap.mem_range_self
      apply Submodule.inner_right_of_mem_orthogonal Lp1x Lp2x
    -- Apply the Pythagorean theorem and simplify
    rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), norm_sq_eq_add_norm_sq_projection x S]
    simp only [sq, Mx_decomp]
    rw [norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (L (p1 x)) (L3 (p2 x)) Mx_orth]
    simp only [p1, p2, LinearIsometry.norm_map, _root_.add_left_inj, mul_eq_mul_left_iff,
      norm_eq_zero, true_or_iff, eq_self_iff_true, ContinuousLinearMap.coe_coe, Submodule.coe_norm,
      Submodule.coe_eq_zero]
  exact
    { toLinearMap := M
      norm_map' := M_norm_map }
#align linear_isometry.extend LinearIsometry.extend

theorem LinearIsometry.extend_apply (L : S â†’â‚—áµ¢[ğ•œ] V) (s : S) : L.extend s = L s := by
  haveI : CompleteSpace S := FiniteDimensional.complete ğ•œ S
  simp only [LinearIsometry.extend, â† LinearIsometry.coe_toLinearMap]
  simp only [add_right_eq_self, LinearIsometry.coe_toLinearMap,
    LinearIsometryEquiv.coe_toLinearIsometry, LinearIsometry.coe_comp, Function.comp_apply,
    orthogonalProjection_mem_subspace_eq_self, LinearMap.coe_comp, ContinuousLinearMap.coe_coe,
    Submodule.coeSubtype, LinearMap.add_apply, Submodule.coe_eq_zero,
    LinearIsometryEquiv.map_eq_zero_iff, Submodule.coe_subtypeâ‚—áµ¢,
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero, Submodule.orthogonal_orthogonal,
    Submodule.coe_mem]
#align linear_isometry.extend_apply LinearIsometry.extend_apply

end LinearIsometry

section Matrix

open Matrix

variable {m n : Type*}

namespace Matrix

variable [Fintype m] [Fintype n] [DecidableEq n]

/-- `Matrix.toLin'` adapted for `EuclideanSpace ğ•œ _`. -/
def toEuclideanLin : Matrix m n ğ•œ â‰ƒâ‚—[ğ•œ] EuclideanSpace ğ•œ n â†’â‚—[ğ•œ] EuclideanSpace ğ•œ m :=
  Matrix.toLin' â‰ªâ‰«â‚—
    LinearEquiv.arrowCongr (WithLp.linearEquiv _ ğ•œ (n â†’ ğ•œ)).symm
      (WithLp.linearEquiv _ ğ•œ (m â†’ ğ•œ)).symm
#align matrix.to_euclidean_lin Matrix.toEuclideanLin

@[simp]
theorem toEuclideanLin_piLp_equiv_symm (A : Matrix m n ğ•œ) (x : n â†’ ğ•œ) :
    Matrix.toEuclideanLin A ((WithLp.equiv _ _).symm x) =
      (WithLp.equiv _ _).symm (Matrix.toLin' A x) :=
  rfl
#align matrix.to_euclidean_lin_pi_Lp_equiv_symm Matrix.toEuclideanLin_piLp_equiv_symm

@[simp]
theorem piLp_equiv_toEuclideanLin (A : Matrix m n ğ•œ) (x : EuclideanSpace ğ•œ n) :
    WithLp.equiv _ _ (Matrix.toEuclideanLin A x) = Matrix.toLin' A (WithLp.equiv _ _ x) :=
  rfl
#align matrix.pi_Lp_equiv_to_euclidean_lin Matrix.piLp_equiv_toEuclideanLin

theorem toEuclideanLin_apply (M : Matrix m n ğ•œ) (v : EuclideanSpace ğ•œ n) :
    toEuclideanLin M v = (WithLp.equiv 2 (m â†’ ğ•œ)).symm (M *áµ¥ (WithLp.equiv 2 (n â†’ ğ•œ)) v) :=
  rfl

@[simp]
theorem piLp_equiv_toEuclideanLin_apply (M : Matrix m n ğ•œ) (v : EuclideanSpace ğ•œ n) :
    WithLp.equiv 2 (m â†’ ğ•œ) (toEuclideanLin M v) = M *áµ¥ WithLp.equiv 2 (n â†’ ğ•œ) v :=
  rfl

@[simp]
theorem toEuclideanLin_apply_piLp_equiv_symm (M : Matrix m n ğ•œ) (v : n â†’ ğ•œ) :
    toEuclideanLin M ((WithLp.equiv 2 (nâ†’ ğ•œ)).symm v) = (WithLp.equiv 2 (m â†’ ğ•œ)).symm (M *áµ¥ v) :=
  rfl

-- `Matrix.toEuclideanLin` is the same as `Matrix.toLin` applied to `PiLp.basisFun`,
theorem toEuclideanLin_eq_toLin :
    (toEuclideanLin : Matrix m n ğ•œ â‰ƒâ‚—[ğ•œ] _) =
      Matrix.toLin (PiLp.basisFun _ _ _) (PiLp.basisFun _ _ _) :=
  rfl
#align matrix.to_euclidean_lin_eq_to_lin Matrix.toEuclideanLin_eq_toLin

open EuclideanSpace in
lemma toEuclideanLin_eq_toLin_orthonormal :
    toEuclideanLin = toLin (basisFun n ğ•œ).toBasis (basisFun m ğ•œ).toBasis :=
  rfl

end Matrix

local notation "âŸª" x ", " y "âŸ«â‚‘" =>
  @inner ğ•œ _ _ (Equiv.symm (WithLp.equiv 2 _) x) (Equiv.symm (WithLp.equiv 2 _) y)

/-- The inner product of a row of `A` and a row of `B` is an entry of `B * Aá´´`. -/
theorem inner_matrix_row_row [Fintype n] (A B : Matrix m n ğ•œ) (i j : m) :
    âŸªA i, B jâŸ«â‚‘ = (B * Aá´´) j i := by
  simp_rw [EuclideanSpace.inner_piLp_equiv_symm, Matrix.mul_apply', Matrix.dotProduct_comm,
    Matrix.conjTranspose_apply, Pi.star_def]
#align inner_matrix_row_row inner_matrix_row_row

/-- The inner product of a column of `A` and a column of `B` is an entry of `Aá´´ * B`. -/
theorem inner_matrix_col_col [Fintype m] (A B : Matrix m n ğ•œ) (i j : n) :
    âŸªAáµ€ i, Báµ€ jâŸ«â‚‘ = (Aá´´ * B) i j :=
  rfl
#align inner_matrix_col_col inner_matrix_col_col

end Matrix
