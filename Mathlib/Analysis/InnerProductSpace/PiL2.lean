/-
Copyright (c) 2020 Joseph Myers. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Joseph Myers, SÃ©bastien GouÃ«zel, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Projection.FiniteDimensional
import Mathlib.Analysis.Normed.Lp.PiLp
import Mathlib.LinearAlgebra.FiniteDimensional.Lemmas
import Mathlib.LinearAlgebra.UnitaryGroup
import Mathlib.Util.Superscript

/-!
# `LÂ²` inner product space structure on finite products of inner product spaces

The `LÂ²` norm on a finite product of inner product spaces is compatible with an inner product
$$
\langle x, y\rangle = \sum \langle x_i, y_i \rangle.
$$
This is recorded in this file as an inner product space instance on `PiLp 2`.

This file develops the notion of a finite-dimensional Hilbert space over `ğ•œ = â„‚, â„`, referred to as
`E`. We define an `OrthonormalBasis ğ•œ Î¹ E` as a linear isometric equivalence
between `E` and `EuclideanSpace ğ•œ Î¹`. Then `stdOrthonormalBasis` shows that such an equivalence
always exists if `E` is finite dimensional. We provide language for converting between a basis
that is orthonormal and an orthonormal basis (e.g. `Basis.toOrthonormalBasis`). We show that
orthonormal bases for each summand in a direct sum of spaces can be combined into an orthonormal
basis for the whole sum in `DirectSum.IsInternal.subordinateOrthonormalBasis`. In
the last section, various properties of matrices are explored.

## Main definitions

- `EuclideanSpace ğ•œ n`: defined to be `PiLp 2 (n â†’ ğ•œ)` for any `Fintype n`, i.e., the space
  from functions to `n` to `ğ•œ` with the `LÂ²` norm. We register several instances on it (notably
  that it is a finite-dimensional inner product space), and provide a `!â‚š[]` notation (for numeric
  subscripts like `â‚‚`) for the case when the indexing type is `Fin n`.

- `OrthonormalBasis ğ•œ Î¹`: defined to be an isometry to Euclidean space from a given
  finite-dimensional inner product space, `E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹`.

- `Basis.toOrthonormalBasis`: constructs an `OrthonormalBasis` for a finite-dimensional
  Euclidean space from a `Basis` which is `Orthonormal`.

- `Orthonormal.exists_orthonormalBasis_extension`: provides an existential result of an
  `OrthonormalBasis` extending a given orthonormal set

- `exists_orthonormalBasis`: provides an orthonormal basis on a finite-dimensional vector space

- `stdOrthonormalBasis`: provides an arbitrarily-chosen `OrthonormalBasis` of a given
finite-dimensional inner product space

For consequences in infinite dimension (Hilbert bases, etc.), see the file
`Analysis.InnerProductSpace.L2Space`.

-/


open Module Real Set Filter RCLike Submodule Function Uniformity Topology NNReal ENNReal
  ComplexConjugate DirectSum WithLp

noncomputable section

variable {Î¹ Î¹' ğ•œ : Type*} [RCLike ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]
variable {F' : Type*} [NormedAddCommGroup F'] [InnerProductSpace â„ F']

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-
If `Î¹` is a finite type and each space `f i`, `i : Î¹`, is an inner product space,
then `Î  i, f i` is an inner product space as well. Since `Î  i, f i` is endowed with the sup norm,
we use instead `PiLp 2 f` for the product space, which is endowed with the `L^2` norm.
-/
instance PiLp.innerProductSpace {Î¹ : Type*} [Fintype Î¹] (f : Î¹ â†’ Type*)
    [âˆ€ i, NormedAddCommGroup (f i)] [âˆ€ i, InnerProductSpace ğ•œ (f i)] :
    InnerProductSpace ğ•œ (PiLp 2 f) where
  inner x y := âˆ‘ i, âŸªx i, y iâŸ«
  norm_sq_eq_re_inner x := by
    simp only [PiLp.norm_sq_eq_of_L2, map_sum, â† norm_sq_eq_re_inner]
  conj_inner_symm := by
    intro x y
    unfold inner
    rw [map_sum]
    apply Finset.sum_congr rfl
    rintro z -
    apply inner_conj_symm
  add_left x y z :=
    show (âˆ‘ i, âŸªx i + y i, z iâŸ« = âˆ‘ i, âŸªx i, z iâŸ« + âˆ‘ i, âŸªy i, z iâŸ«) by
      simp only [inner_add_left, Finset.sum_add_distrib]
  smul_left x y r :=
    show (âˆ‘ i : Î¹, âŸªr â€¢ x i, y iâŸ« = conj r * âˆ‘ i, âŸªx i, y iâŸ«) by
      simp only [Finset.mul_sum, inner_smul_left]

theorem PiLp.inner_apply {Î¹ : Type*} [Fintype Î¹] {f : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (f i)]
    [âˆ€ i, InnerProductSpace ğ•œ (f i)] (x y : PiLp 2 f) : âŸªx, yâŸ« = âˆ‘ i, âŸªx i, y iâŸ« :=
  rfl

/-- The standard real/complex Euclidean space, functions on a finite type. For an `n`-dimensional
space use `EuclideanSpace ğ•œ (Fin n)`.

For the case when `n = Fin _`, there is `!â‚‚[x, y, ...]` notation for building elements of this type,
analogous to `![x, y, ...]` notation. -/
abbrev EuclideanSpace (ğ•œ : Type*) (n : Type*) : Type _ :=
  PiLp 2 fun _ : n => ğ•œ

section Notation
open Lean Meta Elab Term Macro TSyntax PrettyPrinter.Delaborator SubExpr
open Mathlib.Tactic (subscriptTerm)

/-- Notation for vectors in Lp space. `!â‚‚[x, y, ...]` is a shorthand for
`WithLp.toLp 2 ![x, y, ...]`, of type `EuclideanSpace _ (Fin _)`.

This also works for other subscripts. -/
syntax (name := PiLp.vecNotation) "!" noWs subscriptTerm noWs "[" term,* "]" : term
macro_rules | `(!$p:subscript[$e:term,*]) => do
  -- override the `Fin n.succ` to a literal
  let n := e.getElems.size
  `(WithLp.toLp $p (V := âˆ€ _ : Fin $(quote n), _) ![$e,*])

/-- Unexpander for the `!â‚‚[x, y, ...]` notation. -/
@[app_delab WithLp.toLp]
def EuclideanSpace.delabVecNotation : Delab :=
  whenNotPPOption getPPExplicit <| whenPPOption getPPNotation <| withOverApp 3 do
    -- check that the `WithLp.toLp _` is present
    let p : Term â† withNaryArg 0 <| delab
    -- to be conservative, only allow subscripts which are numerals
    guard <| p matches `($_:num)
    let `(![$elems,*]) := â† withNaryArg 2 delab | failure
    `(!$p[$elems,*])

end Notation

theorem EuclideanSpace.nnnorm_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x : EuclideanSpace ğ•œ n) : â€–xâ€–â‚Š = NNReal.sqrt (âˆ‘ i, â€–x iâ€–â‚Š ^ 2) :=
  PiLp.nnnorm_eq_of_L2 x

theorem EuclideanSpace.norm_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x : EuclideanSpace ğ•œ n) : â€–xâ€– = âˆš(âˆ‘ i, â€–x iâ€– ^ 2) := by
  simpa only [Real.coe_sqrt, NNReal.coe_sum] using congr_arg ((â†‘) : â„â‰¥0 â†’ â„) x.nnnorm_eq

theorem EuclideanSpace.norm_sq_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x : EuclideanSpace ğ•œ n) : â€–xâ€– ^ 2 = âˆ‘ i, â€–x iâ€– ^ 2 :=
  PiLp.norm_sq_eq_of_L2 _ x

theorem EuclideanSpace.dist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : dist x y = âˆš(âˆ‘ i, dist (x i) (y i) ^ 2) :=
  PiLp.dist_eq_of_L2 x y

theorem EuclideanSpace.dist_sq_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : dist x y ^ 2 = âˆ‘ i, dist (x i) (y i) ^ 2 :=
  PiLp.dist_sq_eq_of_L2 x y

theorem EuclideanSpace.nndist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : nndist x y = NNReal.sqrt (âˆ‘ i, nndist (x i) (y i) ^ 2) :=
  PiLp.nndist_eq_of_L2 x y

theorem EuclideanSpace.edist_eq {ğ•œ : Type*} [RCLike ğ•œ] {n : Type*} [Fintype n]
    (x y : EuclideanSpace ğ•œ n) : edist x y = (âˆ‘ i, edist (x i) (y i) ^ 2) ^ (1 / 2 : â„) :=
  PiLp.edist_eq_of_L2 x y

theorem EuclideanSpace.ball_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.ball (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 < r ^ 2} := by
  ext x
  have : (0 : â„) â‰¤ âˆ‘ i, x i ^ 2 := Finset.sum_nonneg fun _ _ => sq_nonneg _
  simp_rw [mem_setOf, mem_ball_zero_iff, norm_eq, norm_eq_abs, sq_abs, sqrt_lt this hr]

theorem EuclideanSpace.closedBall_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.closedBall (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 â‰¤ r ^ 2} := by
  ext
  simp_rw [mem_setOf, mem_closedBall_zero_iff, norm_eq, norm_eq_abs, sq_abs, sqrt_le_left hr]

theorem EuclideanSpace.sphere_zero_eq {n : Type*} [Fintype n] (r : â„) (hr : 0 â‰¤ r) :
    Metric.sphere (0 : EuclideanSpace â„ n) r = {x | âˆ‘ i, x i ^ 2 = r ^ 2} := by
  ext x
  have : (0 : â„) â‰¤ âˆ‘ i, x i ^ 2 := Finset.sum_nonneg fun _ _ => sq_nonneg _
  simp_rw [mem_setOf, mem_sphere_zero_iff_norm, norm_eq, norm_eq_abs, sq_abs,
    Real.sqrt_eq_iff_eq_sq this hr]

section

variable [Fintype Î¹]

@[simp]
theorem finrank_euclideanSpace :
    Module.finrank ğ•œ (EuclideanSpace ğ•œ Î¹) = Fintype.card Î¹ := by
  simp [WithLp]

theorem finrank_euclideanSpace_fin {n : â„•} :
    Module.finrank ğ•œ (EuclideanSpace ğ•œ (Fin n)) = n := by simp

theorem EuclideanSpace.inner_eq_star_dotProduct (x y : EuclideanSpace ğ•œ Î¹) :
    âŸªx, yâŸ« = ofLp y â¬áµ¥ star (ofLp x) := rfl

lemma EuclideanSpace.inner_toLp_toLp (x y : Î¹ â†’ ğ•œ) :
    âŸªtoLp 2 x, toLp 2 yâŸ« = dotProduct y (star x) := rfl

/-- A finite, mutually orthogonal family of subspaces of `E`, which span `E`, induce an isometry
from `E` to `PiLp 2` of the subspaces equipped with the `L2` inner product. -/
def DirectSum.IsInternal.isometryL2OfOrthogonalFamily [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : DirectSum.IsInternal V)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    E â‰ƒâ‚—áµ¢[ğ•œ] PiLp 2 fun i => V i := by
  let eâ‚ := DirectSum.linearEquivFunOnFintype ğ•œ Î¹ fun i => V i
  let eâ‚‚ := LinearEquiv.ofBijective (DirectSum.coeLinearMap V) hV
  refine LinearEquiv.isometryOfInner (eâ‚‚.symm.trans eâ‚) ?_
  suffices âˆ€ (v w : PiLp 2 fun i => V i), âŸªv, wâŸ« = âŸªeâ‚‚ (eâ‚.symm v), eâ‚‚ (eâ‚.symm w)âŸ« by
    intro vâ‚€ wâ‚€
    convert this (eâ‚ (eâ‚‚.symm vâ‚€)) (eâ‚ (eâ‚‚.symm wâ‚€)) <;>
      simp only [LinearEquiv.symm_apply_apply, LinearEquiv.apply_symm_apply]
  intro v w
  trans âŸªâˆ‘ i, (V i).subtypeâ‚—áµ¢ (v i), âˆ‘ i, (V i).subtypeâ‚—áµ¢ (w i)âŸ«
  Â· simp only [sum_inner, hV'.inner_right_fintype, PiLp.inner_apply]
  Â· congr <;> simp

@[simp]
theorem DirectSum.IsInternal.isometryL2OfOrthogonalFamily_symm_apply [DecidableEq Î¹]
    {V : Î¹ â†’ Submodule ğ•œ E} (hV : DirectSum.IsInternal V)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) (w : PiLp 2 fun i => V i) :
    (hV.isometryL2OfOrthogonalFamily hV').symm w = âˆ‘ i, (w i : E) := by
  classical
    let eâ‚ := DirectSum.linearEquivFunOnFintype ğ•œ Î¹ fun i => V i
    let eâ‚‚ := LinearEquiv.ofBijective (DirectSum.coeLinearMap V) hV
    suffices âˆ€ v : â¨ i, V i, eâ‚‚ v = âˆ‘ i, eâ‚ v i by exact this (eâ‚.symm w)
    intro v
    simp [eâ‚, eâ‚‚, DirectSum.coeLinearMap, DirectSum.toModule, DFinsupp.lsum,
      DFinsupp.sumAddHom_apply]

end

variable (Î¹ ğ•œ)

/-- A shorthand for `PiLp.continuousLinearEquiv`. -/
abbrev EuclideanSpace.equiv : EuclideanSpace ğ•œ Î¹ â‰ƒL[ğ•œ] Î¹ â†’ ğ•œ :=
  PiLp.continuousLinearEquiv 2 ğ•œ _

variable {Î¹ ğ•œ}

/-- The projection on the `i`-th coordinate of `EuclideanSpace ğ•œ Î¹`, as a linear map. -/
abbrev EuclideanSpace.projâ‚— (i : Î¹) : EuclideanSpace ğ•œ Î¹ â†’â‚—[ğ•œ] ğ•œ := PiLp.projâ‚— _ _ i

/-- The projection on the `i`-th coordinate of `EuclideanSpace ğ•œ Î¹`, as a continuous linear map. -/
abbrev EuclideanSpace.proj (i : Î¹) : StrongDual ğ•œ (EuclideanSpace ğ•œ Î¹) := PiLp.proj _ _ i

section DecEq

variable [DecidableEq Î¹]

-- TODO : This should be generalized to `PiLp`.
/-- The vector given in Euclidean space by being `a : ğ•œ` at coordinate `i : Î¹` and `0 : ğ•œ` at
all other coordinates. -/
def EuclideanSpace.single (i : Î¹) (a : ğ•œ) : EuclideanSpace ğ•œ Î¹ :=
  toLp _ (Pi.single i a)

@[simp] lemma EuclideanSpace.ofLp_single (i : Î¹) (a : ğ•œ) : ofLp (single i a) = Pi.single i a := rfl

@[simp]
lemma EuclideanSpace.toLp_single (i : Î¹) (a : ğ•œ) : toLp _ (Pi.single i a) = single i a := rfl

@[simp]
theorem EuclideanSpace.single_apply (i : Î¹) (a : ğ•œ) (j : Î¹) :
    (EuclideanSpace.single i a) j = ite (j = i) a 0 := by
  rw [EuclideanSpace.single, PiLp.toLp_apply, â† Pi.single_apply i a j]

@[simp]
theorem EuclideanSpace.single_eq_zero_iff {i : Î¹} {a : ğ•œ} :
    EuclideanSpace.single i a = 0 â†” a = 0 := Pi.single_eq_zero_iff

variable [Fintype Î¹]

theorem EuclideanSpace.inner_single_left (i : Î¹) (a : ğ•œ) (v : EuclideanSpace ğ•œ Î¹) :
    âŸªEuclideanSpace.single i (a : ğ•œ), vâŸ« = conj a * v i := by
  simp [PiLp.inner_apply, apply_ite conj, mul_comm]

theorem EuclideanSpace.inner_single_right (i : Î¹) (a : ğ•œ) (v : EuclideanSpace ğ•œ Î¹) :
    âŸªv, EuclideanSpace.single i (a : ğ•œ)âŸ« = a * conj (v i) := by simp [PiLp.inner_apply]

@[simp]
theorem EuclideanSpace.norm_single (i : Î¹) (a : ğ•œ) :
    â€–EuclideanSpace.single i (a : ğ•œ)â€– = â€–aâ€– :=
  PiLp.norm_toLp_single 2 (fun _ => ğ•œ) i a

@[simp]
theorem EuclideanSpace.nnnorm_single (i : Î¹) (a : ğ•œ) :
    â€–EuclideanSpace.single i (a : ğ•œ)â€–â‚Š = â€–aâ€–â‚Š :=
  PiLp.nnnorm_toLp_single 2 (fun _ => ğ•œ) i a

@[simp]
theorem EuclideanSpace.dist_single_same (i : Î¹) (a b : ğ•œ) :
    dist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = dist a b :=
  PiLp.dist_toLp_single_same 2 (fun _ => ğ•œ) i a b

@[simp]
theorem EuclideanSpace.nndist_single_same (i : Î¹) (a b : ğ•œ) :
    nndist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = nndist a b :=
  PiLp.nndist_toLp_single_same 2 (fun _ => ğ•œ) i a b

@[simp]
theorem EuclideanSpace.edist_single_same (i : Î¹) (a b : ğ•œ) :
    edist (EuclideanSpace.single i (a : ğ•œ)) (EuclideanSpace.single i (b : ğ•œ)) = edist a b :=
  PiLp.edist_toLp_single_same 2 (fun _ => ğ•œ) i a b

/-- `EuclideanSpace.single` forms an orthonormal family. -/
theorem EuclideanSpace.orthonormal_single :
    Orthonormal ğ•œ fun i : Î¹ => EuclideanSpace.single i (1 : ğ•œ) := by
  simp_rw [orthonormal_iff_ite, EuclideanSpace.inner_single_left, map_one, one_mul,
    EuclideanSpace.single_apply]
  intros
  trivial

theorem EuclideanSpace.piLpCongrLeft_single
    {Î¹' : Type*} [Fintype Î¹'] [DecidableEq Î¹'] (e : Î¹' â‰ƒ Î¹) (i' : Î¹') (v : ğ•œ) :
    LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ e (EuclideanSpace.single i' v) =
      EuclideanSpace.single (e i') v :=
  LinearIsometryEquiv.piLpCongrLeft_single e i' _

end DecEq

section finAddEquivProd

/-- The canonical linear homeomorphism between `EuclideanSpace ğ•œ (Î¹ âŠ• Îº)` and
`EuclideanSpace ğ•œ Î¹ Ã— EuclideanSpace ğ•œ Îº`.

See `PiLp.sumPiLpEquivProdLpPiLp` for the isometry version,
where the RHS is equipped with the Euclidean norm rather than the supremum norm. -/
abbrev EuclideanSpace.sumEquivProd {ğ•œ : Type*} [RCLike ğ•œ] {Î¹ Îº : Type*} [Fintype Î¹] [Fintype Îº] :
    EuclideanSpace ğ•œ (Î¹ âŠ• Îº) â‰ƒL[ğ•œ] EuclideanSpace ğ•œ Î¹ Ã— EuclideanSpace ğ•œ Îº :=
  (PiLp.sumPiLpEquivProdLpPiLp 2 _).toContinuousLinearEquiv.trans <|
    WithLp.prodContinuousLinearEquiv _ _ _ _

/-- The canonical linear homeomorphism between `EuclideanSpace ğ•œ (Fin (n + m))` and
`EuclideanSpace ğ•œ (Fin n) Ã— EuclideanSpace ğ•œ (Fin m)`. -/
abbrev EuclideanSpace.finAddEquivProd {ğ•œ : Type*} [RCLike ğ•œ] {n m : â„•} :
    EuclideanSpace ğ•œ (Fin (n + m)) â‰ƒL[ğ•œ] EuclideanSpace ğ•œ (Fin n) Ã— EuclideanSpace ğ•œ (Fin m) :=
  (LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ finSumFinEquiv.symm).toContinuousLinearEquiv.trans
    sumEquivProd

end finAddEquivProd

variable (Î¹ ğ•œ E)
variable [Fintype Î¹]

/-- An orthonormal basis on E is an identification of `E` with its dimensional-matching
`EuclideanSpace ğ•œ Î¹`. -/
structure OrthonormalBasis where ofRepr ::
  /-- Linear isometry between `E` and `EuclideanSpace ğ•œ Î¹` representing the orthonormal basis. -/
  repr : E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹

variable {Î¹ ğ•œ E}

namespace OrthonormalBasis

theorem repr_injective :
    Injective (repr : OrthonormalBasis Î¹ ğ•œ E â†’ E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹) := fun f g h => by
  cases f
  cases g
  congr

/-- `b i` is the `i`th basis vector. -/
instance instFunLike : FunLike (OrthonormalBasis Î¹ ğ•œ E) Î¹ E where
  coe b i := by classical exact b.repr.symm (EuclideanSpace.single i (1 : ğ•œ))
  coe_injective' b b' h := repr_injective <| LinearIsometryEquiv.toLinearEquiv_injective <|
    LinearEquiv.symm_bijective.injective <| LinearEquiv.toLinearMap_injective <| by
      classical
        rw [â† LinearMap.cancel_right (WithLp.linearEquiv 2 ğ•œ (_ â†’ ğ•œ)).symm.surjective]
        simp only
        refine LinearMap.pi_ext fun i k => ?_
        have : k = k â€¢ (1 : ğ•œ) := by rw [smul_eq_mul, mul_one]
        rw [this, Pi.single_smul]
        replace h := congr_fun h i
        simp only [LinearEquiv.comp_coe, map_smul, LinearEquiv.coe_coe,
          LinearEquiv.trans_apply, WithLp.linearEquiv_symm_apply, EuclideanSpace.toLp_single,
          LinearIsometryEquiv.coe_symm_toLinearEquiv] at h âŠ¢
        rw [h]

@[simp]
theorem coe_ofRepr [DecidableEq Î¹] (e : E â‰ƒâ‚—áµ¢[ğ•œ] EuclideanSpace ğ•œ Î¹) :
    â‡‘(OrthonormalBasis.ofRepr e) = fun i => e.symm (EuclideanSpace.single i (1 : ğ•œ)) := by
  dsimp only [DFunLike.coe]
  funext
  congr!

@[simp]
protected theorem repr_symm_single [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr.symm (EuclideanSpace.single i (1 : ğ•œ)) = b i := by
  dsimp only [DFunLike.coe]
  congr!

@[simp]
protected theorem repr_self [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    b.repr (b i) = EuclideanSpace.single i (1 : ğ•œ) := by
  rw [â† b.repr_symm_single i, LinearIsometryEquiv.apply_symm_apply]

protected theorem repr_apply_apply (b : OrthonormalBasis Î¹ ğ•œ E) (v : E) (i : Î¹) :
    b.repr v i = âŸªb i, vâŸ« := by
  classical
    rw [â† b.repr.inner_map_map (b i) v, b.repr_self i, EuclideanSpace.inner_single_left]
    simp only [one_mul, map_one]

@[simp]
protected theorem orthonormal (b : OrthonormalBasis Î¹ ğ•œ E) : Orthonormal ğ•œ b := by
  classical
    rw [orthonormal_iff_ite]
    intro i j
    rw [â† b.repr.inner_map_map (b i) (b j), b.repr_self i, b.repr_self j,
      EuclideanSpace.inner_single_left, EuclideanSpace.single_apply, map_one, one_mul]

@[simp]
lemma norm_eq_one (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    â€–b iâ€– = 1 := b.orthonormal.norm_eq_one i

@[simp]
lemma nnnorm_eq_one (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    â€–b iâ€–â‚Š = 1 := b.orthonormal.nnnorm_eq_one i

@[simp]
lemma enorm_eq_one (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) :
    â€–b iâ€–â‚‘ = 1 := b.orthonormal.enorm_eq_one i

@[simp]
lemma inner_eq_zero (b : OrthonormalBasis Î¹ ğ•œ E) {i j : Î¹} (hij : i â‰  j) :
    âŸªb i, b jâŸ« = 0 := b.orthonormal.inner_eq_zero hij

lemma inner_eq_one (b : OrthonormalBasis Î¹ ğ•œ E) (i : Î¹) : âŸªb i, b iâŸ« = 1 := by
  simp

lemma inner_eq_ite [DecidableEq Î¹] (b : OrthonormalBasis Î¹ ğ•œ E) (i j : Î¹) :
    âŸªb i, b jâŸ« = if i = j then 1 else 0 := by
  by_cases h : i = j <;> simp [h]

/-- The `Basis Î¹ ğ•œ E` underlying the `OrthonormalBasis` -/
protected def toBasis (b : OrthonormalBasis Î¹ ğ•œ E) : Basis Î¹ ğ•œ E :=
  Basis.ofEquivFun b.repr.toLinearEquiv

@[simp]
protected theorem coe_toBasis (b : OrthonormalBasis Î¹ ğ•œ E) : (â‡‘b.toBasis : Î¹ â†’ E) = â‡‘b := rfl

@[simp]
protected theorem coe_toBasis_repr (b : OrthonormalBasis Î¹ ğ•œ E) :
    b.toBasis.equivFun = b.repr.toLinearEquiv :=
  Basis.equivFun_ofEquivFun _

@[simp]
protected theorem coe_toBasis_repr_apply (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) (i : Î¹) :
    b.toBasis.repr x i = b.repr x i := by
  rw [â† Basis.equivFun_apply, OrthonormalBasis.coe_toBasis_repr]
  -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
  erw [LinearIsometryEquiv.coe_toLinearEquiv]

protected theorem sum_repr (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) : âˆ‘ i, b.repr x i â€¢ b i = x := by
  simp_rw [â† b.coe_toBasis_repr_apply, â† b.coe_toBasis]
  exact b.toBasis.sum_repr x

open scoped InnerProductSpace in
protected theorem sum_repr' (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) : âˆ‘ i, âŸªb i, xâŸ«_ğ•œ â€¢ b i = x := by
  nth_rw 2 [â† (b.sum_repr x)]
  simp_rw [b.repr_apply_apply x]

protected theorem sum_repr_symm (b : OrthonormalBasis Î¹ ğ•œ E) (v : EuclideanSpace ğ•œ Î¹) :
    âˆ‘ i, v i â€¢ b i = b.repr.symm v := by simpa using (b.toBasis.equivFun_symm_apply v).symm

protected theorem sum_inner_mul_inner (b : OrthonormalBasis Î¹ ğ•œ E) (x y : E) :
    âˆ‘ i, âŸªx, b iâŸ« * âŸªb i, yâŸ« = âŸªx, yâŸ« := by
  have := congr_arg (innerSL ğ•œ x) (b.sum_repr y)
  rw [map_sum] at this
  convert this
  rw [map_smul, b.repr_apply_apply, mul_comm]
  simp

lemma sum_sq_norm_inner_right (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) :
    âˆ‘ i, â€–âŸªb i, xâŸ«â€– ^ 2 = â€–xâ€– ^ 2 := by
  rw [@norm_eq_sqrt_re_inner ğ•œ, â† OrthonormalBasis.sum_inner_mul_inner b x x, map_sum]
  simp_rw [inner_mul_symm_re_eq_norm, norm_mul, â† inner_conj_symm x, starRingEnd_apply,
    norm_star, â† pow_two]
  rw [Real.sq_sqrt]
  exact Fintype.sum_nonneg fun _ â†¦ by positivity

@[deprecated (since := "2025-06-23")] alias sum_sq_norm_inner := sum_sq_norm_inner_right

lemma sum_sq_norm_inner_left (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) :
    âˆ‘ i, â€–âŸªx, b iâŸ«â€– ^ 2 = â€–xâ€– ^ 2 := by
  convert sum_sq_norm_inner_right b x using 2 with i -
  rw [â† inner_conj_symm, RCLike.norm_conj]

open scoped RealInnerProductSpace in
theorem sum_sq_inner_right {E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace â„ E] (b : OrthonormalBasis Î¹ â„ E) (x : E) :
    âˆ‘ i : Î¹, âŸªb i, xâŸ« ^ 2 = â€–xâ€– ^ 2 := by
  rw [â† b.sum_sq_norm_inner_right]
  simp

open scoped RealInnerProductSpace in
theorem sum_sq_inner_left {Î¹ E : Type*} [NormedAddCommGroup E]
    [InnerProductSpace â„ E] [Fintype Î¹] (b : OrthonormalBasis Î¹ â„ E) (x : E) :
    âˆ‘ i : Î¹, âŸªx, b iâŸ« ^ 2 = â€–xâ€– ^ 2 := by
  simp_rw [â† b.sum_sq_inner_right, real_inner_comm]

lemma norm_le_card_mul_iSup_norm_inner (b : OrthonormalBasis Î¹ ğ•œ E) (x : E) :
    â€–xâ€– â‰¤ âˆš(Fintype.card Î¹) * â¨† i, â€–âŸªb i, xâŸ«â€– := by
  calc â€–xâ€–
  _ = âˆš(âˆ‘ i, â€–âŸªb i, xâŸ«â€– ^ 2) := by rw [sum_sq_norm_inner_right, Real.sqrt_sq (by positivity)]
  _ â‰¤ âˆš(âˆ‘ _ : Î¹, (â¨† j, â€–âŸªb j, xâŸ«â€–) ^ 2) := by
    gcongr with i
    exact le_ciSup (f := fun j â†¦ â€–âŸªb j, xâŸ«â€–) (by simp) i
  _ = âˆš(Fintype.card Î¹) * â¨† i, â€–âŸªb i, xâŸ«â€– := by
    simp only [Finset.sum_const, Finset.card_univ, nsmul_eq_mul, Nat.cast_nonneg, Real.sqrt_mul]
    congr
    rw [Real.sqrt_sq]
    cases isEmpty_or_nonempty Î¹
    Â· simp
    Â· exact le_ciSup_of_le (by simp) (Nonempty.some inferInstance) (by positivity)

protected theorem orthogonalProjection_eq_sum {U : Submodule ğ•œ E} [CompleteSpace U]
    (b : OrthonormalBasis Î¹ ğ•œ U) (x : E) :
    U.orthogonalProjection x = âˆ‘ i, âŸª(b i : E), xâŸ« â€¢ b i := by
  simpa only [b.repr_apply_apply, inner_orthogonalProjection_eq_of_mem_left] using
    (b.sum_repr (U.orthogonalProjection x)).symm

/-- Mapping an orthonormal basis along a `LinearIsometryEquiv`. -/
protected def map {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) : OrthonormalBasis Î¹ ğ•œ G where
  repr := L.symm.trans b.repr

@[simp]
protected theorem map_apply {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) (i : Î¹) : b.map L i = L (b i) :=
  rfl

@[simp]
protected theorem toBasis_map {G : Type*} [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
    (b : OrthonormalBasis Î¹ ğ•œ E) (L : E â‰ƒâ‚—áµ¢[ğ•œ] G) :
    (b.map L).toBasis = b.toBasis.map L.toLinearEquiv :=
  rfl

/-- A basis that is orthonormal is an orthonormal basis. -/
def _root_.Module.Basis.toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    OrthonormalBasis Î¹ ğ•œ E :=
  OrthonormalBasis.ofRepr <|
    LinearEquiv.isometryOfInner v.equivFun
      (by
        intro x y
        let p : EuclideanSpace ğ•œ Î¹ := v.equivFun x
        let q : EuclideanSpace ğ•œ Î¹ := v.equivFun y
        have key : âŸªp, qâŸ« = âŸªâˆ‘ i, p i â€¢ v i, âˆ‘ i, q i â€¢ v iâŸ« := by
          simp [inner_sum, inner_smul_right, hv.inner_left_fintype, PiLp.inner_apply]
        convert key
        Â· rw [â† v.equivFun.symm_apply_apply x, v.equivFun_symm_apply]
        Â· rw [â† v.equivFun.symm_apply_apply y, v.equivFun_symm_apply])

@[simp]
theorem _root_.Module.Basis.coe_toOrthonormalBasis_repr (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    ((v.toOrthonormalBasis hv).repr : E â†’ EuclideanSpace ğ•œ Î¹) = v.equivFun :=
  rfl

@[simp]
theorem _root_.Module.Basis.coe_toOrthonormalBasis_repr_symm (v : Basis Î¹ ğ•œ E)
    (hv : Orthonormal ğ•œ v) :
    ((v.toOrthonormalBasis hv).repr.symm : EuclideanSpace ğ•œ Î¹ â†’ E) = v.equivFun.symm :=
  rfl

@[simp]
theorem _root_.Module.Basis.toBasis_toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    (v.toOrthonormalBasis hv).toBasis = v := by
  simp [Basis.toOrthonormalBasis, OrthonormalBasis.toBasis]

@[simp]
theorem _root_.Module.Basis.coe_toOrthonormalBasis (v : Basis Î¹ ğ•œ E) (hv : Orthonormal ğ•œ v) :
    (v.toOrthonormalBasis hv : Î¹ â†’ E) = (v : Î¹ â†’ E) :=
  calc
    (v.toOrthonormalBasis hv : Î¹ â†’ E) = ((v.toOrthonormalBasis hv).toBasis : Î¹ â†’ E) := by
      classical rw [OrthonormalBasis.coe_toBasis]
    _ = (v : Î¹ â†’ E) := by simp

section Singleton
variable {Î¹ ğ•œ : Type*} [Unique Î¹] [RCLike ğ•œ]

variable (Î¹ ğ•œ) in
/-- `OrthonormalBasis.singleton Î¹ ğ•œ` is the orthonormal basis sending the unique element of `Î¹` to
`1 : ğ•œ`. -/
protected noncomputable def singleton : OrthonormalBasis Î¹ ğ•œ ğ•œ :=
  (Basis.singleton Î¹ ğ•œ).toOrthonormalBasis (by simp [orthonormal_iff_ite, Unique.eq_default])

@[simp]
theorem singleton_apply (i) : OrthonormalBasis.singleton Î¹ ğ•œ i = 1 := Basis.singleton_apply _ _ _

@[simp]
theorem singleton_repr (x i) : (OrthonormalBasis.singleton Î¹ ğ•œ).repr x i = x :=
  Basis.singleton_repr _ _ _ _

@[simp]
theorem coe_singleton : â‡‘(OrthonormalBasis.singleton Î¹ ğ•œ) = 1 := by
  ext; simp

@[simp]
theorem toBasis_singleton : (OrthonormalBasis.singleton Î¹ ğ•œ).toBasis = Basis.singleton Î¹ ğ•œ :=
  Basis.toBasis_toOrthonormalBasis _ _

end Singleton

/-- `Pi.orthonormalBasis (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i))` is the
`Î£ i, Î¹ i`-indexed orthonormal basis on `Î  i, E i` given by `B i` on each component. -/
protected def _root_.Pi.orthonormalBasis {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) :
    OrthonormalBasis ((i : Î·) Ã— Î¹ i) ğ•œ (PiLp 2 E) where
  repr := .trans
      (.piLpCongrRight 2 fun i => (B i).repr)
      (.symm <| .piLpCurry ğ•œ 2 fun _ _ => ğ•œ)

theorem _root_.Pi.orthonormalBasis.toBasis {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) :
    (Pi.orthonormalBasis B).toBasis =
      ((Pi.basis fun i : Î· â†¦ (B i).toBasis).map (WithLp.linearEquiv 2 _ _).symm) := by ext; rfl

@[simp]
theorem _root_.Pi.orthonormalBasis_apply {Î· : Type*} [Fintype Î·] [DecidableEq Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i))
    (j : (i : Î·) Ã— (Î¹ i)) :
    Pi.orthonormalBasis B j = toLp _ (Pi.single _ (B j.fst j.snd)) := by
  classical
  ext k
  obtain âŸ¨i, jâŸ© := j
  simp only [Pi.orthonormalBasis, coe_ofRepr, LinearIsometryEquiv.symm_trans,
    LinearIsometryEquiv.symm_symm, LinearIsometryEquiv.piLpCongrRight_symm,
    LinearIsometryEquiv.trans_apply, LinearIsometryEquiv.piLpCongrRight_apply,
    LinearIsometryEquiv.piLpCurry_apply, EuclideanSpace.ofLp_single, PiLp.toLp_apply,
    Sigma.curry_single (Î³ := fun _ _ => ğ•œ)]
  obtain rfl | hi := Decidable.eq_or_ne i k
  Â· simp only [Pi.single_eq_same, EuclideanSpace.toLp_single, OrthonormalBasis.repr_symm_single]
  Â· simp only [Pi.single_eq_of_ne' hi, toLp_zero, map_zero]

@[simp]
theorem _root_.Pi.orthonormalBasis_repr {Î· : Type*} [Fintype Î·] {Î¹ : Î· â†’ Type*}
    [âˆ€ i, Fintype (Î¹ i)] {ğ•œ : Type*} [RCLike ğ•œ] {E : Î· â†’ Type*} [âˆ€ i, NormedAddCommGroup (E i)]
    [âˆ€ i, InnerProductSpace ğ•œ (E i)] (B : âˆ€ i, OrthonormalBasis (Î¹ i) ğ•œ (E i)) (x : (i : Î·) â†’ E i)
    (j : (i : Î·) Ã— (Î¹ i)) :
    (Pi.orthonormalBasis B).repr x j = (B j.fst).repr (x j.fst) j.snd := rfl

variable {v : Î¹ â†’ E}

/-- A finite orthonormal set that spans is an orthonormal basis -/
protected def mk (hon : Orthonormal ğ•œ v) (hsp : âŠ¤ â‰¤ Submodule.span ğ•œ (Set.range v)) :
    OrthonormalBasis Î¹ ğ•œ E :=
  (Basis.mk (Orthonormal.linearIndependent hon) hsp).toOrthonormalBasis (by rwa [Basis.coe_mk])

@[simp]
protected theorem coe_mk (hon : Orthonormal ğ•œ v) (hsp : âŠ¤ â‰¤ Submodule.span ğ•œ (Set.range v)) :
    â‡‘(OrthonormalBasis.mk hon hsp) = v := by
  classical rw [OrthonormalBasis.mk, _root_.Module.Basis.coe_toOrthonormalBasis, Basis.coe_mk]

/-- Any finite subset of an orthonormal family is an `OrthonormalBasis` for its span. -/
protected def span [DecidableEq E] {v' : Î¹' â†’ E} (h : Orthonormal ğ•œ v') (s : Finset Î¹') :
    OrthonormalBasis s ğ•œ (span ğ•œ (s.image v' : Set E)) :=
  let eâ‚€' : Basis s ğ•œ _ :=
    Basis.span (h.linearIndependent.comp ((â†‘) : s â†’ Î¹') Subtype.val_injective)
  let eâ‚€ : OrthonormalBasis s ğ•œ _ :=
    OrthonormalBasis.mk
      (by
        convert orthonormal_span (h.comp ((â†‘) : s â†’ Î¹') Subtype.val_injective)
        simp [eâ‚€', Basis.span_apply])
      eâ‚€'.span_eq.ge
  let Ï† : span ğ•œ (s.image v' : Set E) â‰ƒâ‚—áµ¢[ğ•œ] span ğ•œ (range (v' âˆ˜ ((â†‘) : s â†’ Î¹'))) :=
    LinearIsometryEquiv.ofEq _ _
      (by
        rw [Finset.coe_image, image_eq_range]
        rfl)
  eâ‚€.map Ï†.symm

@[simp]
protected theorem span_apply [DecidableEq E] {v' : Î¹' â†’ E} (h : Orthonormal ğ•œ v') (s : Finset Î¹')
    (i : s) : (OrthonormalBasis.span h s i : E) = v' i := by
  simp only [OrthonormalBasis.span, Basis.span_apply, LinearIsometryEquiv.ofEq_symm,
    OrthonormalBasis.map_apply, OrthonormalBasis.coe_mk, LinearIsometryEquiv.coe_ofEq_apply,
    comp_apply]

open Submodule

/-- A finite orthonormal family of vectors whose span has trivial orthogonal complement is an
orthonormal basis. -/
protected def mkOfOrthogonalEqBot (hon : Orthonormal ğ•œ v) (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) :
    OrthonormalBasis Î¹ ğ•œ E :=
  OrthonormalBasis.mk hon
    (by
      refine Eq.ge ?_
      haveI : FiniteDimensional ğ•œ (span ğ•œ (range v)) :=
        FiniteDimensional.span_of_finite ğ•œ (finite_range v)
      haveI : CompleteSpace (span ğ•œ (range v)) := FiniteDimensional.complete ğ•œ _
      rwa [orthogonal_eq_bot_iff] at hsp)

@[simp]
protected theorem coe_of_orthogonal_eq_bot_mk (hon : Orthonormal ğ•œ v)
    (hsp : (span ğ•œ (Set.range v))á—® = âŠ¥) : â‡‘(OrthonormalBasis.mkOfOrthogonalEqBot hon hsp) = v :=
  OrthonormalBasis.coe_mk hon _

variable [Fintype Î¹']

/-- `b.reindex (e : Î¹ â‰ƒ Î¹')` is an `OrthonormalBasis` indexed by `Î¹'` -/
def reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') : OrthonormalBasis Î¹' ğ•œ E :=
  OrthonormalBasis.ofRepr (b.repr.trans (LinearIsometryEquiv.piLpCongrLeft 2 ğ•œ ğ•œ e))

protected theorem reindex_apply (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (i' : Î¹') :
    (b.reindex e) i' = b (e.symm i') := by
  classical
    dsimp [reindex]
    rw [coe_ofRepr]
    dsimp
    rw [â† b.repr_symm_single, LinearIsometryEquiv.piLpCongrLeft_symm,
      EuclideanSpace.piLpCongrLeft_single]

@[simp]
theorem reindex_toBasis (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') :
    (b.reindex e).toBasis = b.toBasis.reindex e := Basis.eq_ofRepr_eq_repr fun _ â†¦ congr_fun rfl

@[simp]
protected theorem coe_reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') :
    â‡‘(b.reindex e) = b âˆ˜ e.symm :=
  funext (b.reindex_apply e)

@[simp]
protected theorem repr_reindex (b : OrthonormalBasis Î¹ ğ•œ E) (e : Î¹ â‰ƒ Î¹') (x : E) (i' : Î¹') :
    (b.reindex e).repr x i' = b.repr x (e.symm i') := by
  classical
  rw [OrthonormalBasis.repr_apply_apply, b.repr_apply_apply, OrthonormalBasis.coe_reindex,
    comp_apply]

end OrthonormalBasis

namespace EuclideanSpace

variable (ğ•œ Î¹)

/-- The basis `Pi.basisFun`, bundled as an orthornormal basis of `EuclideanSpace ğ•œ Î¹`. -/
noncomputable def basisFun : OrthonormalBasis Î¹ ğ•œ (EuclideanSpace ğ•œ Î¹) :=
  âŸ¨LinearIsometryEquiv.refl _ _âŸ©

@[simp]
theorem basisFun_apply [DecidableEq Î¹] (i : Î¹) : basisFun Î¹ ğ•œ i = EuclideanSpace.single i 1 :=
  PiLp.basisFun_apply _ _ _ _

@[simp]
theorem basisFun_repr (x : EuclideanSpace ğ•œ Î¹) (i : Î¹) : (basisFun Î¹ ğ•œ).repr x i = x i := rfl

@[simp]
theorem basisFun_inner (x : EuclideanSpace ğ•œ Î¹) (i : Î¹) : âŸªbasisFun Î¹ ğ•œ i, xâŸ« = x i := by
  simp [â† OrthonormalBasis.repr_apply_apply]

@[simp]
theorem inner_basisFun_real (x : EuclideanSpace â„ Î¹) (i : Î¹) :
    inner â„ x (basisFun Î¹ â„ i) = x i := by
  rw [real_inner_comm, basisFun_inner]

theorem basisFun_toBasis : (basisFun Î¹ ğ•œ).toBasis = PiLp.basisFun _ ğ•œ Î¹ := rfl

end EuclideanSpace

instance OrthonormalBasis.instInhabited : Inhabited (OrthonormalBasis Î¹ ğ•œ (EuclideanSpace ğ•œ Î¹)) :=
  âŸ¨EuclideanSpace.basisFun Î¹ ğ•œâŸ©

namespace OrthonormalBasis

variable {E' : Type*} [Fintype Î¹'] [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E']
    (b : OrthonormalBasis Î¹ ğ•œ E) (b' : OrthonormalBasis Î¹' ğ•œ E') (e : Î¹ â‰ƒ Î¹')

/-- The `LinearIsometryEquiv` which maps an orthonormal basis to another. This is a convenience
wrapper around `Orthonormal.equiv`. -/
protected def equiv : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
  b.repr.trans <| .trans (.piLpCongrLeft _ _ _ e) b'.repr.symm

@[simp]
lemma equiv_symm : (b.equiv b' e).symm = b'.equiv b e.symm := by
  apply b'.toBasis.ext_linearIsometryEquiv
  simp [OrthonormalBasis.equiv]

@[simp]
lemma equiv_apply_basis (i : Î¹) : b.equiv b' e (b i) = b' (e i) := by
  classical
  simp only [OrthonormalBasis.equiv, LinearIsometryEquiv.trans_apply, OrthonormalBasis.repr_self,
    LinearIsometryEquiv.piLpCongrLeft_apply]
  refine DFunLike.congr rfl ?_
  ext j
  simp [Equiv.symm_apply_eq]

@[simp]
lemma equiv_self_rfl : b.equiv b (.refl Î¹) = .refl ğ•œ E := by
  apply b.toBasis.ext_linearIsometryEquiv
  simp

lemma equiv_apply (x : E) : b.equiv b' e x = âˆ‘ i, b.repr x i â€¢ b' (e i) := by
  nth_rw 1 [â† b.sum_repr x, map_sum]
  simp_rw [map_smul, equiv_apply_basis]

lemma equiv_apply_euclideanSpace (x : EuclideanSpace ğ•œ Î¹) :
    (EuclideanSpace.basisFun Î¹ ğ•œ).equiv b (Equiv.refl Î¹) x = âˆ‘ i, x i â€¢ b i := by
  simp_rw [equiv_apply, EuclideanSpace.basisFun_repr, Equiv.refl_apply]

lemma coe_equiv_euclideanSpace :
    â‡‘((EuclideanSpace.basisFun Î¹ ğ•œ).equiv b (Equiv.refl Î¹)) = fun x â†¦ âˆ‘ i, x i â€¢ b i := by
  simp_rw [â† equiv_apply_euclideanSpace]

end OrthonormalBasis

section Complex

/-- `![1, I]` is an orthonormal basis for `â„‚` considered as a real inner product space. -/
def Complex.orthonormalBasisOneI : OrthonormalBasis (Fin 2) â„ â„‚ :=
  Complex.basisOneI.toOrthonormalBasis
    (by
      rw [orthonormal_iff_ite]
      intro i; fin_cases i <;> intro j <;> fin_cases j <;> simp [real_inner_eq_re_inner])

@[simp]
theorem Complex.orthonormalBasisOneI_repr_apply (z : â„‚) :
    Complex.orthonormalBasisOneI.repr z = ![z.re, z.im] :=
  rfl

@[simp]
theorem Complex.orthonormalBasisOneI_repr_symm_apply (x : EuclideanSpace â„ (Fin 2)) :
    Complex.orthonormalBasisOneI.repr.symm x = x 0 + x 1 * I :=
  rfl

@[simp]
theorem Complex.toBasis_orthonormalBasisOneI :
    Complex.orthonormalBasisOneI.toBasis = Complex.basisOneI :=
  Basis.toBasis_toOrthonormalBasis _ _

@[simp]
theorem Complex.coe_orthonormalBasisOneI :
    (Complex.orthonormalBasisOneI : Fin 2 â†’ â„‚) = ![1, I] := by
  simp [Complex.orthonormalBasisOneI]

/-- The isometry between `â„‚` and a two-dimensional real inner product space given by a basis. -/
def Complex.isometryOfOrthonormal (v : OrthonormalBasis (Fin 2) â„ F) : â„‚ â‰ƒâ‚—áµ¢[â„] F :=
  Complex.orthonormalBasisOneI.repr.trans v.repr.symm

@[simp]
theorem Complex.map_isometryOfOrthonormal (v : OrthonormalBasis (Fin 2) â„ F) (f : F â‰ƒâ‚—áµ¢[â„] F') :
    Complex.isometryOfOrthonormal (v.map f) = (Complex.isometryOfOrthonormal v).trans f := by
  simp only [isometryOfOrthonormal, OrthonormalBasis.map, LinearIsometryEquiv.symm_trans,
    LinearIsometryEquiv.symm_symm]
  -- Porting note: `LinearIsometryEquiv.trans_assoc` doesn't trigger in the `simp` above
  rw [LinearIsometryEquiv.trans_assoc]

theorem Complex.isometryOfOrthonormal_symm_apply (v : OrthonormalBasis (Fin 2) â„ F) (f : F) :
    (Complex.isometryOfOrthonormal v).symm f =
      (v.toBasis.coord 0 f : â„‚) + (v.toBasis.coord 1 f : â„‚) * I := by
  simp [Complex.isometryOfOrthonormal]

theorem Complex.isometryOfOrthonormal_apply (v : OrthonormalBasis (Fin 2) â„ F) (z : â„‚) :
    Complex.isometryOfOrthonormal v z = z.re â€¢ v 0 + z.im â€¢ v 1 := by
  simp [Complex.isometryOfOrthonormal, â† v.sum_repr_symm]

end Complex

open Module

/-! ### Matrix representation of an orthonormal basis with respect to another -/


section ToMatrix

variable [DecidableEq Î¹]

section
open scoped Matrix

/-- A version of `OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary` that works for bases with
different index types. -/
@[simp]
theorem OrthonormalBasis.toMatrix_orthonormalBasis_conjTranspose_mul_self [Fintype Î¹']
    (a : OrthonormalBasis Î¹' ğ•œ E) (b : OrthonormalBasis Î¹ ğ•œ E) :
    (a.toBasis.toMatrix b)á´´ * a.toBasis.toMatrix b = 1 := by
  ext i j
  convert a.repr.inner_map_map (b i) (b j)
  Â· simp only [Matrix.mul_apply, Matrix.conjTranspose_apply, star_def, PiLp.inner_apply,
      inner_apply']
    congr
  Â· rw [orthonormal_iff_ite.mp b.orthonormal i j, Matrix.one_apply]

/-- A version of `OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary` that works for bases with
different index types. -/
@[simp]
theorem OrthonormalBasis.toMatrix_orthonormalBasis_self_mul_conjTranspose [Fintype Î¹']
    (a : OrthonormalBasis Î¹ ğ•œ E) (b : OrthonormalBasis Î¹' ğ•œ E) :
    a.toBasis.toMatrix b * (a.toBasis.toMatrix b)á´´ = 1 := by
  classical
  rw [Matrix.mul_eq_one_comm_of_equiv (a.toBasis.indexEquiv b.toBasis),
    a.toMatrix_orthonormalBasis_conjTranspose_mul_self b]

variable (a b : OrthonormalBasis Î¹ ğ•œ E)

/-- The change-of-basis matrix between two orthonormal bases `a`, `b` is a unitary matrix. -/
theorem OrthonormalBasis.toMatrix_orthonormalBasis_mem_unitary :
    a.toBasis.toMatrix b âˆˆ Matrix.unitaryGroup Î¹ ğ•œ := by
  rw [Matrix.mem_unitaryGroup_iff']
  exact a.toMatrix_orthonormalBasis_conjTranspose_mul_self b

/-- The determinant of the change-of-basis matrix between two orthonormal bases `a`, `b` has
unit length. -/
@[simp]
theorem OrthonormalBasis.det_to_matrix_orthonormalBasis : â€–a.toBasis.det bâ€– = 1 := by
  have := (Matrix.det_of_mem_unitary (a.toMatrix_orthonormalBasis_mem_unitary b)).2
  rw [star_def, RCLike.mul_conj] at this
  norm_cast at this
  rwa [pow_eq_one_iff_of_nonneg (norm_nonneg _) two_ne_zero] at this

end

section Real

variable (a b : OrthonormalBasis Î¹ â„ F)

/-- The change-of-basis matrix between two orthonormal bases `a`, `b` is an orthogonal matrix. -/
theorem OrthonormalBasis.toMatrix_orthonormalBasis_mem_orthogonal :
    a.toBasis.toMatrix b âˆˆ Matrix.orthogonalGroup Î¹ â„ :=
  a.toMatrix_orthonormalBasis_mem_unitary b

/-- The determinant of the change-of-basis matrix between two orthonormal bases `a`, `b` is Â±1. -/
theorem OrthonormalBasis.det_to_matrix_orthonormalBasis_real :
    a.toBasis.det b = 1 âˆ¨ a.toBasis.det b = -1 := by
  rw [â† sq_eq_one_iff]
  simpa [unitary, sq] using Matrix.det_of_mem_unitary (a.toMatrix_orthonormalBasis_mem_unitary b)

end Real

end ToMatrix

/-! ### Existence of orthonormal basis, etc. -/


section FiniteDimensional

variable {v : Set E}
variable {A : Î¹ â†’ Submodule ğ•œ E}

/-- Given an internal direct sum decomposition of a module `M`, and an orthonormal basis for each
of the components of the direct sum, the disjoint union of these orthonormal bases is an
orthonormal basis for `M`. -/
noncomputable def DirectSum.IsInternal.collectedOrthonormalBasis
    (hV : OrthogonalFamily ğ•œ (fun i => A i) fun i => (A i).subtypeâ‚—áµ¢) [DecidableEq Î¹]
    (hV_sum : DirectSum.IsInternal fun i => A i) {Î± : Î¹ â†’ Type*} [âˆ€ i, Fintype (Î± i)]
    (v_family : âˆ€ i, OrthonormalBasis (Î± i) ğ•œ (A i)) : OrthonormalBasis (Î£ i, Î± i) ğ•œ E :=
  (hV_sum.collectedBasis fun i => (v_family i).toBasis).toOrthonormalBasis <| by
    simpa using
      hV.orthonormal_sigma_orthonormal (show âˆ€ i, Orthonormal ğ•œ (v_family i).toBasis by simp)

theorem DirectSum.IsInternal.collectedOrthonormalBasis_mem [DecidableEq Î¹]
    (h : DirectSum.IsInternal A) {Î± : Î¹ â†’ Type*} [âˆ€ i, Fintype (Î± i)]
    (hV : OrthogonalFamily ğ•œ (fun i => A i) fun i => (A i).subtypeâ‚—áµ¢)
    (v : âˆ€ i, OrthonormalBasis (Î± i) ğ•œ (A i)) (a : Î£ i, Î± i) :
    h.collectedOrthonormalBasis hV v a âˆˆ A a.1 := by
  simp [DirectSum.IsInternal.collectedOrthonormalBasis]

variable [FiniteDimensional ğ•œ E]

/-- In a finite-dimensional `InnerProductSpace`, any orthonormal subset can be extended to an
orthonormal basis. -/
theorem Orthonormal.exists_orthonormalBasis_extension (hv : Orthonormal ğ•œ ((â†‘) : v â†’ E)) :
    âˆƒ (u : Finset E) (b : OrthonormalBasis u ğ•œ E), v âŠ† u âˆ§ â‡‘b = ((â†‘) : u â†’ E) := by
  obtain âŸ¨uâ‚€, huâ‚€s, huâ‚€, huâ‚€_maxâŸ© := exists_maximal_orthonormal hv
  rw [maximal_orthonormal_iff_orthogonalComplement_eq_bot huâ‚€] at huâ‚€_max
  have huâ‚€_finite : uâ‚€.Finite := huâ‚€.linearIndependent.setFinite
  let u : Finset E := huâ‚€_finite.toFinset
  let fu : â†¥u â‰ƒ â†¥uâ‚€ := huâ‚€_finite.subtypeEquivToFinset.symm
  have hu : Orthonormal ğ•œ ((â†‘) : u â†’ E) := by simpa using huâ‚€.comp _ fu.injective
  refine âŸ¨u, OrthonormalBasis.mkOfOrthogonalEqBot hu ?_, ?_, ?_âŸ©
  Â· simpa [u] using huâ‚€_max
  Â· simpa [u] using huâ‚€s
  Â· simp

theorem Orthonormal.exists_orthonormalBasis_extension_of_card_eq {Î¹ : Type*} [Fintype Î¹]
    (card_Î¹ : finrank ğ•œ E = Fintype.card Î¹) {v : Î¹ â†’ E} {s : Set Î¹}
    (hv : Orthonormal ğ•œ (s.restrict v)) : âˆƒ b : OrthonormalBasis Î¹ ğ•œ E, âˆ€ i âˆˆ s, b i = v i := by
  have hsv : Injective (s.restrict v) := hv.linearIndependent.injective
  have hX : Orthonormal ğ•œ ((â†‘) : Set.range (s.restrict v) â†’ E) := by
    rwa [orthonormal_subtype_range hsv]
  obtain âŸ¨Y, bâ‚€, hX, hbâ‚€âŸ© := hX.exists_orthonormalBasis_extension
  have hÎ¹Y : Fintype.card Î¹ = Y.card := by
    refine card_Î¹.symm.trans ?_
    exact Module.finrank_eq_card_finset_basis bâ‚€.toBasis
  have hvsY : s.MapsTo v Y := (s.mapsTo_image v).mono_right (by rwa [â† range_restrict])
  have hsv' : Set.InjOn v s := by
    rw [Set.injOn_iff_injective]
    exact hsv
  obtain âŸ¨g, hgâŸ© := hvsY.exists_equiv_extend_of_card_eq hÎ¹Y hsv'
  use bâ‚€.reindex g.symm
  intro i hi
  simp [hbâ‚€, hg i hi]

variable (ğ•œ E)

/-- A finite-dimensional inner product space admits an orthonormal basis. -/
theorem _root_.exists_orthonormalBasis :
    âˆƒ (w : Finset E) (b : OrthonormalBasis w ğ•œ E), â‡‘b = ((â†‘) : w â†’ E) :=
  let âŸ¨w, hw, _, hw''âŸ© := (orthonormal_empty ğ•œ E).exists_orthonormalBasis_extension
  âŸ¨w, hw, hw''âŸ©

/-- A finite-dimensional `InnerProductSpace` has an orthonormal basis. -/
irreducible_def stdOrthonormalBasis : OrthonormalBasis (Fin (finrank ğ•œ E)) ğ•œ E := by
  let b := Classical.choose (Classical.choose_spec <| exists_orthonormalBasis ğ•œ E)
  rw [finrank_eq_card_basis b.toBasis]
  exact b.reindex (Fintype.equivFinOfCardEq rfl)

/-- An orthonormal basis of `â„` is made either of the vector `1`, or of the vector `-1`. -/
theorem orthonormalBasis_one_dim (b : OrthonormalBasis Î¹ â„ â„) :
    (â‡‘b = fun _ => (1 : â„)) âˆ¨ â‡‘b = fun _ => (-1 : â„) := by
  have : Unique Î¹ := b.toBasis.unique
  have : b default = 1 âˆ¨ b default = -1 := by
    have : â€–b defaultâ€– = 1 := b.orthonormal.1 _
    rwa [Real.norm_eq_abs, abs_eq (zero_le_one' â„)] at this
  rw [eq_const_of_unique b]
  grind

variable {ğ•œ E}

section SubordinateOrthonormalBasis

open DirectSum

variable {n : â„•} (hn : finrank ğ•œ E = n) [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E} (hV : IsInternal V)

/-- Exhibit a bijection between `Fin n` and the index set of a certain basis of an `n`-dimensional
inner product space `E`.  This should not be accessed directly, but only via the subsequent API. -/
irreducible_def DirectSum.IsInternal.sigmaOrthonormalBasisIndexEquiv
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    (Î£ i, Fin (finrank ğ•œ (V i))) â‰ƒ Fin n :=
  let b := hV.collectedOrthonormalBasis hV' fun i => stdOrthonormalBasis ğ•œ (V i)
  Fintype.equivFinOfCardEq <| (Module.finrank_eq_card_basis b.toBasis).symm.trans hn

/-- An `n`-dimensional `InnerProductSpace` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `Fin n` and subordinate to that direct sum. -/
irreducible_def DirectSum.IsInternal.subordinateOrthonormalBasis
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    OrthonormalBasis (Fin n) ğ•œ E :=
  (hV.collectedOrthonormalBasis hV' fun i => stdOrthonormalBasis ğ•œ (V i)).reindex
    (hV.sigmaOrthonormalBasisIndexEquiv hn hV')

/-- An `n`-dimensional `InnerProductSpace` equipped with a decomposition as an internal direct
sum has an orthonormal basis indexed by `Fin n` and subordinate to that direct sum. This function
provides the mapping by which it is subordinate. -/
irreducible_def DirectSum.IsInternal.subordinateOrthonormalBasisIndex (a : Fin n)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) : Î¹ :=
  ((hV.sigmaOrthonormalBasisIndexEquiv hn hV').symm a).1

/-- The basis constructed in `DirectSum.IsInternal.subordinateOrthonormalBasis` is subordinate to
the `OrthogonalFamily` in question. -/
theorem DirectSum.IsInternal.subordinateOrthonormalBasis_subordinate (a : Fin n)
    (hV' : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    hV.subordinateOrthonormalBasis hn hV' a âˆˆ V (hV.subordinateOrthonormalBasisIndex hn a hV') := by
  simpa only [DirectSum.IsInternal.subordinateOrthonormalBasis, OrthonormalBasis.coe_reindex,
    DirectSum.IsInternal.subordinateOrthonormalBasisIndex] using
    hV.collectedOrthonormalBasis_mem hV' (fun i => stdOrthonormalBasis ğ•œ (V i))
      ((hV.sigmaOrthonormalBasisIndexEquiv hn hV').symm a)

end SubordinateOrthonormalBasis

end FiniteDimensional

/-- Given a natural number `n` one less than the `finrank` of a finite-dimensional inner product
space, there exists an isometry from the orthogonal complement of a nonzero singleton to
`EuclideanSpace ğ•œ (Fin n)`. -/
def OrthonormalBasis.fromOrthogonalSpanSingleton (n : â„•) [Fact (finrank ğ•œ E = n + 1)] {v : E}
    (hv : v â‰  0) : OrthonormalBasis (Fin n) ğ•œ (ğ•œ âˆ™ v)á—® :=
  have : FiniteDimensional ğ•œ E := .of_fact_finrank_eq_succ (K := ğ•œ) (V := E) n
  (stdOrthonormalBasis _ _).reindex <| finCongr <| finrank_orthogonal_span_singleton hv

section LinearIsometry

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [FiniteDimensional ğ•œ V]
variable {S : Submodule ğ•œ V} {L : S â†’â‚—áµ¢[ğ•œ] V}

open Module

/-- Let `S` be a subspace of a finite-dimensional complex inner product space `V`.  A linear
isometry mapping `S` into `V` can be extended to a full isometry of `V`.

TODO:  The case when `S` is a finite-dimensional subspace of an infinite-dimensional `V`. -/
noncomputable def LinearIsometry.extend (L : S â†’â‚—áµ¢[ğ•œ] V) : V â†’â‚—áµ¢[ğ•œ] V := by
  -- Build an isometry from Sá—® to L(S)á—® through `EuclideanSpace`
  let d := finrank ğ•œ Sá—®
  let LS := LinearMap.range L.toLinearMap
  have E : Sá—® â‰ƒâ‚—áµ¢[ğ•œ] LSá—® := by
    have dim_LS_perp : finrank ğ•œ LSá—® = d :=
      calc
        finrank ğ•œ LSá—® = finrank ğ•œ V - finrank ğ•œ LS := by
          simp only [â† LS.finrank_add_finrank_orthogonal, add_tsub_cancel_left]
        _ = finrank ğ•œ V - finrank ğ•œ S := by
          simp only [LS, LinearMap.finrank_range_of_inj L.injective]
        _ = finrank ğ•œ Sá—® := by simp only [â† S.finrank_add_finrank_orthogonal, add_tsub_cancel_left]
    exact
      (stdOrthonormalBasis ğ•œ Sá—®).repr.trans
        ((stdOrthonormalBasis ğ•œ LSá—®).reindex <| finCongr dim_LS_perp).repr.symm
  let L3 := LSá—®.subtypeâ‚—áµ¢.comp E.toLinearIsometry
  -- Project onto S and Sá—®
  haveI : CompleteSpace S := FiniteDimensional.complete ğ•œ S
  haveI : CompleteSpace V := FiniteDimensional.complete ğ•œ V
  let p1 := S.orthogonalProjection.toLinearMap
  let p2 := Sá—®.orthogonalProjection.toLinearMap
  -- Build a linear map from the isometries on S and Sá—®
  let M := L.toLinearMap.comp p1 + L3.toLinearMap.comp p2
  -- Prove that M is an isometry
  have M_norm_map : âˆ€ x : V, â€–M xâ€– = â€–xâ€– := by
    intro x
    -- Apply M to the orthogonal decomposition of x
    have Mx_decomp : M x = L (p1 x) + L3 (p2 x) := by
      simp only [M, LinearMap.add_apply, LinearMap.comp_apply, LinearMap.comp_apply,
        LinearIsometry.coe_toLinearMap]
    -- Mx_decomp is the orthogonal decomposition of M x
    have Mx_orth : âŸªL (p1 x), L3 (p2 x)âŸ« = 0 := by
      have Lp1x : L (p1 x) âˆˆ LinearMap.range L.toLinearMap :=
        LinearMap.mem_range_self L.toLinearMap (p1 x)
      have Lp2x : L3 (p2 x) âˆˆ (LinearMap.range L.toLinearMap)á—® := by
        simp only [LS,
          â† Submodule.range_subtype LSá—®]
        apply LinearMap.mem_range_self
      apply Submodule.inner_right_of_mem_orthogonal Lp1x Lp2x
    -- Apply the Pythagorean theorem and simplify
    rw [â† sq_eq_sqâ‚€ (norm_nonneg _) (norm_nonneg _), norm_sq_eq_add_norm_sq_projection x S]
    simp only [sq, Mx_decomp]
    rw [norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (L (p1 x)) (L3 (p2 x)) Mx_orth]
    simp only [p1, p2, LinearIsometry.norm_map,
      ContinuousLinearMap.coe_coe, Submodule.coe_norm]
  exact
    { toLinearMap := M
      norm_map' := M_norm_map }

theorem LinearIsometry.extend_apply (L : S â†’â‚—áµ¢[ğ•œ] V) (s : S) : L.extend s = L s := by
  haveI : CompleteSpace S := FiniteDimensional.complete ğ•œ S
  simp only [LinearIsometry.extend, â† LinearIsometry.coe_toLinearMap]
  simp only [add_eq_left, LinearIsometry.coe_toLinearMap,
    LinearIsometryEquiv.coe_toLinearIsometry, LinearIsometry.coe_comp, Function.comp_apply,
    orthogonalProjection_mem_subspace_eq_self, LinearMap.coe_comp, ContinuousLinearMap.coe_coe,
    Submodule.coe_subtype, LinearMap.add_apply, Submodule.coe_eq_zero,
    LinearIsometryEquiv.map_eq_zero_iff, Submodule.coe_subtypeâ‚—áµ¢,
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero, Submodule.orthogonal_orthogonal,
    Submodule.coe_mem]

end LinearIsometry

section Matrix

open Matrix

variable {m n : Type*}

namespace Matrix

variable [Fintype n] [DecidableEq n]

/-- `Matrix.toLin'` adapted for `EuclideanSpace ğ•œ _`. -/
def toEuclideanLin : Matrix m n ğ•œ â‰ƒâ‚—[ğ•œ] EuclideanSpace ğ•œ n â†’â‚—[ğ•œ] EuclideanSpace ğ•œ m :=
  Matrix.toLin' â‰ªâ‰«â‚—
    LinearEquiv.arrowCongr (WithLp.linearEquiv _ ğ•œ (n â†’ ğ•œ)).symm
      (WithLp.linearEquiv _ ğ•œ (m â†’ ğ•œ)).symm

@[simp]
lemma toEuclideanLin_toLp (A : Matrix m n ğ•œ) (x : n â†’ ğ•œ) :
    Matrix.toEuclideanLin A (toLp _ x) = toLp _ (Matrix.toLin' A x) := rfl

@[simp]
theorem piLp_ofLp_toEuclideanLin (A : Matrix m n ğ•œ) (x : EuclideanSpace ğ•œ n) :
    ofLp (Matrix.toEuclideanLin A x) = Matrix.toLin' A (ofLp x) :=
  rfl

theorem toEuclideanLin_apply (M : Matrix m n ğ•œ) (v : EuclideanSpace ğ•œ n) :
    toEuclideanLin M v = toLp _ (M *áµ¥ ofLp v) := rfl

@[simp]
theorem ofLp_toEuclideanLin_apply (M : Matrix m n ğ•œ) (v : EuclideanSpace ğ•œ n) :
    ofLp (toEuclideanLin M v) = M *áµ¥ ofLp v :=
  rfl

@[simp]
theorem toEuclideanLin_apply_piLp_toLp (M : Matrix m n ğ•œ) (v : n â†’ ğ•œ) :
    toEuclideanLin M (toLp _ v) = toLp _ (M *áµ¥ v) :=
  rfl

-- `Matrix.toEuclideanLin` is the same as `Matrix.toLin` applied to `PiLp.basisFun`,
theorem toEuclideanLin_eq_toLin [Finite m] :
    (toEuclideanLin : Matrix m n ğ•œ â‰ƒâ‚—[ğ•œ] _) =
      Matrix.toLin (PiLp.basisFun _ _ _) (PiLp.basisFun _ _ _) :=
  rfl

open EuclideanSpace in
lemma toEuclideanLin_eq_toLin_orthonormal [Fintype m] :
    toEuclideanLin = toLin (basisFun n ğ•œ).toBasis (basisFun m ğ•œ).toBasis :=
  rfl

end Matrix

local notation "âŸª" x ", " y "âŸ«â‚‘" => inner ğ•œ (toLp 2 x) (toLp 2 y)

/-- The inner product of a row of `A` and a row of `B` is an entry of `B * Aá´´`. -/
theorem inner_matrix_row_row [Fintype n] (A B : Matrix m n ğ•œ) (i j : m) :
    âŸªA i, B jâŸ«â‚‘ = (B * Aá´´) j i := by
  simp [PiLp.inner_apply, dotProduct, mul_apply']

/-- The inner product of a column of `A` and a column of `B` is an entry of `Aá´´ * B`. -/
theorem inner_matrix_col_col [Fintype m] (A B : Matrix m n ğ•œ) (i j : n) :
    âŸªAáµ€ i, Báµ€ jâŸ«â‚‘ = (Aá´´ * B) i j := by
  simp [PiLp.inner_apply, dotProduct, mul_apply', mul_comm]

/-- The matrix representation of `innerSL ğ•œ x` given by an orthonormal basis `b` and `bâ‚‚`
is equal to `vecMulVec (star bâ‚‚) (star (b.repr x))`. -/
theorem toMatrix_innerSL_apply [Fintype n] [DecidableEq n] [Fintype m]
    (b : OrthonormalBasis n ğ•œ E) (bâ‚‚ : OrthonormalBasis m ğ•œ ğ•œ) (x : E) :
    (innerSL ğ•œ x).toMatrix b.toBasis bâ‚‚.toBasis = vecMulVec (star bâ‚‚) (star (b.repr x)) := by
  ext; simp [LinearMap.toMatrix_apply, vecMulVec_apply, OrthonormalBasis.repr_apply_apply, mul_comm]

end Matrix
