/-
Copyright (c) 2024 Yoh Tanimoto. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yoh Tanimoto
-/
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.Normed.Group.SeparationQuotient

/-!
# Canonial inner product space from Preinner product space

This file defines the inner product space from a preinner product space (the inner product
can be degenerate) by quotienting the null space.

## Main results

It is shown that ` âŸªx, yâŸ«_ğ•œ = 0` for all `y : E` using the Cauchy-Schwarz inequality.
-/

noncomputable section

open RCLike Submodule Quotient LinearMap InnerProductSpace InnerProductSpace.Core

variable (ğ•œ E : Type*) [k: RCLike ğ•œ]

section NullSubmodule

open SeparationQuotient

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

/-- The null space with respect to the norm. -/
def nullSubmodule : Submodule ğ•œ E :=
  { nullSpace with
    smul_mem' := by
      intro c x hx
      simp only [Set.mem_setOf_eq] at hx
      simp only [Set.mem_setOf_eq]
      simp only [AddSubsemigroup.mem_carrier, AddSubmonoid.mem_toSubsemigroup,
        AddSubgroup.mem_toAddSubmonoid]
      have : â€–c â€¢ xâ€– = 0 := by
        rw [norm_smul, hx, mul_zero]
      exact this }

@[simp]
lemma mem_nullSubmodule_iff {x : E} : x âˆˆ nullSubmodule ğ•œ E â†” â€–xâ€– = 0 := Iff.rfl

lemma inner_eq_zero_of_left_mem_nullSubmodule (x y : E) (h : x âˆˆ nullSubmodule ğ•œ E) :
    âŸªx, yâŸ«_ğ•œ = 0 := by
  rw [â† norm_eq_zero, â† sq_eq_zero_iff]
  apply le_antisymm _ (sq_nonneg _)
  rw [sq]
  nth_rw 2 [â† RCLike.norm_conj]
  rw [_root_.inner_conj_symm]
  calc â€–âŸªx, yâŸ«_ğ•œâ€– * â€–âŸªy, xâŸ«_ğ•œâ€– â‰¤ re âŸªx, xâŸ«_ğ•œ * re âŸªy, yâŸ«_ğ•œ := inner_mul_inner_self_le _ _
  _ = (â€–xâ€– * â€–xâ€–) * re âŸªy, yâŸ«_ğ•œ := by rw [inner_self_eq_norm_mul_norm x]
  _ = (0 * 0) * re âŸªy, yâŸ«_ğ•œ := by rw [(mem_nullSubmodule_iff ğ•œ E).mp h]
  _ = 0 := by ring

lemma inner_nullSubmodule_right_eq_zero (x y : E) (h : y âˆˆ nullSpace) : âŸªx, yâŸ«_ğ•œ = 0 := by
  rw [inner_eq_zero_symm]
  exact inner_eq_zero_of_left_mem_nullSubmodule ğ•œ E y x h

lemma inn_nullSubmodule_right_eq_zero (x y : E) (h : y âˆˆ (nullSubmodule ğ•œ E)) : â€–x - yâ€– = â€–xâ€– := by
  rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), sq, sq,
    â† @inner_self_eq_norm_mul_norm ğ•œ E _ _ _ x, â† @inner_self_eq_norm_mul_norm ğ•œ E _ _ _ (x-y),
    inner_sub_sub_self, inner_nullSubmodule_right_eq_zero ğ•œ E x y h,
    inner_eq_zero_of_left_mem_nullSubmodule ğ•œ E y x h,
      inner_eq_zero_of_left_mem_nullSubmodule ğ•œ E y y h]
  simp only [sub_zero, add_zero]

/-- For each `x : E`, the kernel of `âŸªx, â¬âŸ«` includes the null space. -/
lemma nullSubmodule_le_ker_toDualMap (x : E) : nullSubmodule ğ•œ E â‰¤ ker (toDualMap ğ•œ E x) := by
  intro y hy
  refine LinearMap.mem_ker.mpr ?_
  simp only [toDualMap_apply]
  exact inner_nullSubmodule_right_eq_zero ğ•œ E x y hy

/-- The kernel of the map `x â†¦ âŸªx, â¬âŸ«` includes the null space. -/
lemma nullSubmodule_le_ker_toDualMap' : nullSubmodule ğ•œ E â‰¤ ker (toDualMap ğ•œ E) := by
  intro x hx
  refine LinearMap.mem_ker.mpr ?_
  ext y
  simp only [toDualMap_apply, ContinuousLinearMap.zero_apply]
  exact inner_eq_zero_of_left_mem_nullSubmodule ğ•œ E x y hx

/-- An auxiliary map to define the inner product on the quotient. Only the first entry is
quotiented. -/
def preInnerQ : SeparationQuotient E â†’â‚—â‹†[ğ•œ] (NormedSpace.Dual ğ•œ E) :=
  (SeparationQuotient.liftCLM (toDualMap ğ•œ E).toContinuousLinearMap
  (by
  intro x y h
  rw [inseparable_iff_norm_zero] at h
  simp only [LinearIsometry.coe_toContinuousLinearMap]
  ext z
  simp only [toDualMap_apply]
  rw [â† sub_eq_zero, Eq.symm (_root_.inner_sub_left x y z)]
  exact inner_eq_zero_of_left_mem_nullSubmodule ğ•œ E (x - y) z h
  ))

lemma nullSubmodule_le_ker_preInnerQ (x : SeparationQuotient E) : nullSubmodule ğ•œ E â‰¤
    ker (preInnerQ ğ•œ E x) := by
  intro y hy
  simp only [LinearMap.mem_ker]
  obtain âŸ¨z, hzâŸ© := surjective_mk x
  rw [preInnerQ, â† hz]
  simp only [ContinuousLinearMap.coe_coe, SeparationQuotient.CLM_lift_apply,
    LinearIsometry.coe_toContinuousLinearMap, toDualMap_apply]
  exact inner_nullSubmodule_right_eq_zero ğ•œ E z y hy

lemma eq_of_inseparable (x : SeparationQuotient E) :
    âˆ€ (y z : E), Inseparable y z â†’ ((preInnerQ ğ•œ E) x) y = ((preInnerQ ğ•œ E) x) z := by
  intro y z h
  rw [inseparable_iff_norm_zero] at h
  obtain âŸ¨x', hx'âŸ© := surjective_mk x
  rw [preInnerQ, â† hx']
  simp only [ContinuousLinearMap.coe_coe, SeparationQuotient.CLM_lift_apply,
    LinearIsometry.coe_toContinuousLinearMap, toDualMap_apply]
  rw [â† sub_eq_zero, Eq.symm (_root_.inner_sub_right x' y z)]
  exact inner_nullSubmodule_right_eq_zero ğ•œ E x' (y - z) h


/-- The inner product on the quotient, composed as the composition of two lifts to the quotients. -/
def innerQ : SeparationQuotient E â†’ SeparationQuotient E â†’ ğ•œ :=
  fun x => SeparationQuotient.liftCLM (preInnerQ ğ•œ E x) (eq_of_inseparable ğ•œ E x)

instance : IsClosed ((nullSubmodule ğ•œ E) : Set E) := by
  rw [â† isOpen_compl_iff, isOpen_iff_nhds]
  intro x hx
  refine Filter.le_principal_iff.mpr ?_
  rw [mem_nhds_iff]
  use Metric.ball x (â€–xâ€–/2)
  have normxnezero : 0 < â€–xâ€– := (lt_of_le_of_ne (norm_nonneg x) (Ne.symm hx))
  refine âŸ¨?_, Metric.isOpen_ball, Metric.mem_ball_self <| half_pos normxnezeroâŸ©
  intro y hy
  have normy : â€–xâ€– / 2 â‰¤ â€–yâ€– := by
    rw [mem_ball_iff_norm, â† norm_neg] at hy
    simp only [neg_sub] at hy
    rw [â† sub_half]
    have hy' : â€–xâ€– - â€–yâ€– < â€–xâ€– / 2 := lt_of_le_of_lt (norm_sub_norm_le _ _) hy
    linarith
  exact Ne.symm (ne_of_lt (lt_of_lt_of_le (half_pos normxnezero) normy))

end NullSubmodule

section InnerProductSpace

open SeparationQuotient

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

instance : InnerProductSpace ğ•œ (SeparationQuotient E) where
  inner := innerQ ğ•œ E
  conj_symm x y:= by
    rw [inner]
    simp only
    rw [innerQ, innerQ]
    obtain âŸ¨z, hzâŸ© := surjective_mk x
    obtain âŸ¨w, hwâŸ© := surjective_mk y
    rw [â† hz, â† hw]
    simp only [SeparationQuotient.CLM_lift_apply]
    rw [preInnerQ]
    simp only [ContinuousLinearMap.coe_coe, CLM_lift_apply,
      LinearIsometry.coe_toContinuousLinearMap, toDualMap_apply, _root_.inner_conj_symm]
  norm_sq_eq_inner x := by
    obtain âŸ¨z, hzâŸ© := surjective_mk x
    rw [â† hz]
    simp only [quotient_norm_mk_eq]
    rw [innerQ]
    simp only [CLM_lift_apply]
    rw [preInnerQ]
    simp only [ContinuousLinearMap.coe_coe, CLM_lift_apply,
      LinearIsometry.coe_toContinuousLinearMap, toDualMap_apply]
    rw [inner_self_eq_norm_sq_to_K, sq (ofReal â€–zâ€–)]
    simp only [mul_re, ofReal_re, ofReal_im, mul_zero, sub_zero]
    rw [â† sq]
  add_left x y z:= by
    rw [inner]
    simp only
    rw [innerQ, innerQ, innerQ]
    obtain âŸ¨a, haâŸ© := surjective_mk x
    obtain âŸ¨b, hbâŸ© := surjective_mk y
    obtain âŸ¨c, hcâŸ© := surjective_mk z
    rw [â† ha, â† hb, â† hc]
    simp only [map_add, CLM_lift_apply, ContinuousLinearMap.add_apply]
  smul_left x y r := by
    rw [inner]
    simp only
    rw [innerQ, innerQ]
    obtain âŸ¨a, haâŸ© := surjective_mk x
    obtain âŸ¨b, hbâŸ© := surjective_mk y
    rw [â† ha, â† hb]
    simp only [LinearMap.map_smulâ‚›â‚—, CLM_lift_apply, ContinuousLinearMap.coe_smul', Pi.smul_apply,
      smul_eq_mul]

end InnerProductSpace

end
