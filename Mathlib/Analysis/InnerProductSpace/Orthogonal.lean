/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.LinearAlgebra.SesquilinearForm

#align_import analysis.inner_product_space.orthogonal from "leanprover-community/mathlib"@"f0c8bf9245297a541f468be517f1bde6195105e9"

/-!
# Orthogonal complements of submodules

In this file, the `orthogonal` complement of a submodule `K` is defined, and basic API established.
Some of the more subtle results about the orthogonal complement are delayed to
`Analysis.InnerProductSpace.Projection`.

See also `BilinForm.orthogonal` for orthogonality with respect to a general bilinear form.

## Notation

The orthogonal complement of a submodule `K` is denoted by `Ká—®`.

The proposition that two submodules are orthogonal, `Submodule.IsOrtho`, is denoted by `U âŸ‚ V`.
Note this is not the same unicode symbol as `âŠ¥` (`Bot`).
-/

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

namespace Submodule

variable (K : Submodule ğ•œ E)

/-- The subspace of vectors orthogonal to a given subspace. -/
def orthogonal : Submodule ğ•œ E where
  carrier := { v | âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 }
  zero_mem' _ _ := inner_zero_right _
  add_mem' hx hy u hu := by rw [inner_add_right, hx u hu, hy u hu, add_zero]
  smul_mem' c x hx u hu := by rw [inner_smul_right, hx u hu, mul_zero]
#align submodule.orthogonal Submodule.orthogonal

@[inherit_doc]
notation:1200 K "á—®" => orthogonal K

/-- When a vector is in `Ká—®`. -/
theorem mem_orthogonal (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 :=
  Iff.rfl
#align submodule.mem_orthogonal Submodule.mem_orthogonal

/-- When a vector is in `Ká—®`, with the inner product the
other way round. -/
theorem mem_orthogonal' (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªv, uâŸ« = 0 := by
  simp_rw [mem_orthogonal, inner_eq_zero_symm]
#align submodule.mem_orthogonal' Submodule.mem_orthogonal'

variable {K}

/-- A vector in `K` is orthogonal to one in `Ká—®`. -/
theorem inner_right_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªu, vâŸ« = 0 :=
  (K.mem_orthogonal v).1 hv u hu
#align submodule.inner_right_of_mem_orthogonal Submodule.inner_right_of_mem_orthogonal

/-- A vector in `Ká—®` is orthogonal to one in `K`. -/
theorem inner_left_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªv, uâŸ« = 0 := by
  rw [inner_eq_zero_symm]; exact inner_right_of_mem_orthogonal hu hv
#align submodule.inner_left_of_mem_orthogonal Submodule.inner_left_of_mem_orthogonal

/-- A vector is in `(ğ•œ âˆ™ u)á—®` iff it is orthogonal to `u`. -/
theorem mem_orthogonal_singleton_iff_inner_right {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªu, vâŸ« = 0 := by
  refine âŸ¨inner_right_of_mem_orthogonal (mem_span_singleton_self u), ?_âŸ©
  intro hv w hw
  rw [mem_span_singleton] at hw
  obtain âŸ¨c, rflâŸ© := hw
  simp [inner_smul_left, hv]
#align submodule.mem_orthogonal_singleton_iff_inner_right Submodule.mem_orthogonal_singleton_iff_inner_right

/-- A vector in `(ğ•œ âˆ™ u)á—®` is orthogonal to `u`. -/
theorem mem_orthogonal_singleton_iff_inner_left {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªv, uâŸ« = 0 := by
  rw [mem_orthogonal_singleton_iff_inner_right, inner_eq_zero_symm]
#align submodule.mem_orthogonal_singleton_iff_inner_left Submodule.mem_orthogonal_singleton_iff_inner_left

theorem sub_mem_orthogonal_of_inner_left {x y : E} (h : âˆ€ v : K, âŸªx, vâŸ« = âŸªy, vâŸ«) : x - y âˆˆ Ká—® := by
  rw [mem_orthogonal']
  intro u hu
  rw [inner_sub_left, sub_eq_zero]
  exact h âŸ¨u, huâŸ©
#align submodule.sub_mem_orthogonal_of_inner_left Submodule.sub_mem_orthogonal_of_inner_left

theorem sub_mem_orthogonal_of_inner_right {x y : E} (h : âˆ€ v : K, âŸª(v : E), xâŸ« = âŸª(v : E), yâŸ«) :
    x - y âˆˆ Ká—® := by
  intro u hu
  rw [inner_sub_right, sub_eq_zero]
  exact h âŸ¨u, huâŸ©
#align submodule.sub_mem_orthogonal_of_inner_right Submodule.sub_mem_orthogonal_of_inner_right

variable (K)

/-- `K` and `Ká—®` have trivial intersection. -/
theorem inf_orthogonal_eq_bot : K âŠ“ Ká—® = âŠ¥ := by
  rw [eq_bot_iff]
  intro x
  rw [mem_inf]
  exact fun âŸ¨hx, hoâŸ© => inner_self_eq_zero.1 (ho x hx)
#align submodule.inf_orthogonal_eq_bot Submodule.inf_orthogonal_eq_bot

/-- `K` and `Ká—®` have trivial intersection. -/
theorem orthogonal_disjoint : Disjoint K Ká—® := by simp [disjoint_iff, K.inf_orthogonal_eq_bot]
#align submodule.orthogonal_disjoint Submodule.orthogonal_disjoint

/-- `Ká—®` can be characterized as the intersection of the kernels of the operations of
inner product with each of the elements of `K`. -/
theorem orthogonal_eq_inter : Ká—® = â¨… v : K, LinearMap.ker (innerSL ğ•œ (v : E)) := by
  apply le_antisymm
  Â· rw [le_iInf_iff]
    rintro âŸ¨v, hvâŸ© w hw
    simpa using hw _ hv
  Â· intro v hv w hw
    simp only [mem_iInf] at hv
    exact hv âŸ¨w, hwâŸ©
#align submodule.orthogonal_eq_inter Submodule.orthogonal_eq_inter

/-- The orthogonal complement of any submodule `K` is closed. -/
theorem isClosed_orthogonal : IsClosed (Ká—® : Set E) := by
  rw [orthogonal_eq_inter K]
  have := fun v : K => ContinuousLinearMap.isClosed_ker (innerSL ğ•œ (v : E))
  convert isClosed_iInter this
  simp only [iInf_coe]
#align submodule.is_closed_orthogonal Submodule.isClosed_orthogonal

/-- In a complete space, the orthogonal complement of any submodule `K` is complete. -/
instance instOrthogonalCompleteSpace [CompleteSpace E] : CompleteSpace Ká—® :=
  K.isClosed_orthogonal.completeSpace_coe

variable (ğ•œ E)

/-- `orthogonal` gives a `GaloisConnection` between
`Submodule ğ•œ E` and its `OrderDual`. -/
theorem orthogonal_gc :
    @GaloisConnection (Submodule ğ•œ E) (Submodule ğ•œ E)áµ’áµˆ _ _ orthogonal orthogonal := fun _Kâ‚ _Kâ‚‚ =>
  âŸ¨fun h _v hv _u hu => inner_left_of_mem_orthogonal hv (h hu), fun h _v hv _u hu =>
    inner_left_of_mem_orthogonal hv (h hu)âŸ©
#align submodule.orthogonal_gc Submodule.orthogonal_gc

variable {ğ•œ E}

/-- `orthogonal` reverses the `â‰¤` ordering of two
subspaces. -/
theorem orthogonal_le {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) : Kâ‚‚á—® â‰¤ Kâ‚á—® :=
  (orthogonal_gc ğ•œ E).monotone_l h
#align submodule.orthogonal_le Submodule.orthogonal_le

/-- `orthogonal.orthogonal` preserves the `â‰¤` ordering of two
subspaces. -/
theorem orthogonal_orthogonal_monotone {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) : Kâ‚á—®á—® â‰¤ Kâ‚‚á—®á—® :=
  orthogonal_le (orthogonal_le h)
#align submodule.orthogonal_orthogonal_monotone Submodule.orthogonal_orthogonal_monotone

/-- `K` is contained in `Ká—®á—®`. -/
theorem le_orthogonal_orthogonal : K â‰¤ Ká—®á—® :=
  (orthogonal_gc ğ•œ E).le_u_l _
#align submodule.le_orthogonal_orthogonal Submodule.le_orthogonal_orthogonal

/-- The inf of two orthogonal subspaces equals the subspace orthogonal
to the sup. -/
theorem inf_orthogonal (Kâ‚ Kâ‚‚ : Submodule ğ•œ E) : Kâ‚á—® âŠ“ Kâ‚‚á—® = (Kâ‚ âŠ” Kâ‚‚)á—® :=
  (orthogonal_gc ğ•œ E).l_sup.symm
#align submodule.inf_orthogonal Submodule.inf_orthogonal

/-- The inf of an indexed family of orthogonal subspaces equals the
subspace orthogonal to the sup. -/
theorem iInf_orthogonal {Î¹ : Type*} (K : Î¹ â†’ Submodule ğ•œ E) : â¨… i, (K i)á—® = (iSup K)á—® :=
  (orthogonal_gc ğ•œ E).l_iSup.symm
#align submodule.infi_orthogonal Submodule.iInf_orthogonal

/-- The inf of a set of orthogonal subspaces equals the subspace orthogonal to the sup. -/
theorem sInf_orthogonal (s : Set <| Submodule ğ•œ E) : â¨… K âˆˆ s, Ká—® = (sSup s)á—® :=
  (orthogonal_gc ğ•œ E).l_sSup.symm
#align submodule.Inf_orthogonal Submodule.sInf_orthogonal

@[simp]
theorem top_orthogonal_eq_bot : (âŠ¤ : Submodule ğ•œ E)á—® = âŠ¥ := by
  ext x
  rw [mem_bot, mem_orthogonal]
  exact
    âŸ¨fun h => inner_self_eq_zero.mp (h x mem_top), by
      rintro rfl
      simpâŸ©
#align submodule.top_orthogonal_eq_bot Submodule.top_orthogonal_eq_bot

@[simp]
theorem bot_orthogonal_eq_top : (âŠ¥ : Submodule ğ•œ E)á—® = âŠ¤ := by
  rw [â† top_orthogonal_eq_bot, eq_top_iff]
  exact le_orthogonal_orthogonal âŠ¤
#align submodule.bot_orthogonal_eq_top Submodule.bot_orthogonal_eq_top

@[simp]
theorem orthogonal_eq_top_iff : Ká—® = âŠ¤ â†” K = âŠ¥ := by
  refine
    âŸ¨?_, by
      rintro rfl
      exact bot_orthogonal_eq_topâŸ©
  intro h
  have : K âŠ“ Ká—® = âŠ¥ := K.orthogonal_disjoint.eq_bot
  rwa [h, inf_comm, top_inf_eq] at this
#align submodule.orthogonal_eq_top_iff Submodule.orthogonal_eq_top_iff

theorem orthogonalFamily_self :
    OrthogonalFamily ğ•œ (fun b => â†¥(cond b K Ká—®)) fun b => (cond b K Ká—®).subtypeâ‚—áµ¢
  | true, true => absurd rfl
  | true, false => fun _ x y => inner_right_of_mem_orthogonal x.prop y.prop
  | false, true => fun _ x y => inner_left_of_mem_orthogonal y.prop x.prop
  | false, false => absurd rfl
#align submodule.orthogonal_family_self Submodule.orthogonalFamily_self

end Submodule

@[simp]
theorem bilinFormOfRealInner_orthogonal {E} [NormedAddCommGroup E] [InnerProductSpace â„ E]
    (K : Submodule â„ E) : K.orthogonalBilin bilinFormOfRealInner = Ká—® :=
  rfl
#align bilin_form_of_real_inner_orthogonal bilinFormOfRealInner_orthogonal

/-!
### Orthogonality of submodules

In this section we define `Submodule.IsOrtho U V`, with notation `U âŸ‚ V`.

The API roughly matches that of `Disjoint`.
-/


namespace Submodule

/-- The proposition that two submodules are orthogonal. Has notation `U âŸ‚ V`. -/
def IsOrtho (U V : Submodule ğ•œ E) : Prop :=
  U â‰¤ Vá—®
#align submodule.is_ortho Submodule.IsOrtho

@[inherit_doc]
infixl:50 " âŸ‚ " => Submodule.IsOrtho

theorem isOrtho_iff_le {U V : Submodule ğ•œ E} : U âŸ‚ V â†” U â‰¤ Vá—® :=
  Iff.rfl
#align submodule.is_ortho_iff_le Submodule.isOrtho_iff_le

@[symm]
theorem IsOrtho.symm {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : V âŸ‚ U :=
  (le_orthogonal_orthogonal _).trans (orthogonal_le h)
#align submodule.is_ortho.symm Submodule.IsOrtho.symm

theorem isOrtho_comm {U V : Submodule ğ•œ E} : U âŸ‚ V â†” V âŸ‚ U :=
  âŸ¨IsOrtho.symm, IsOrtho.symmâŸ©
#align submodule.is_ortho_comm Submodule.isOrtho_comm

theorem symmetric_isOrtho : Symmetric (IsOrtho : Submodule ğ•œ E â†’ Submodule ğ•œ E â†’ Prop) := fun _ _ =>
  IsOrtho.symm
#align submodule.symmetric_is_ortho Submodule.symmetric_isOrtho

theorem IsOrtho.inner_eq {U V : Submodule ğ•œ E} (h : U âŸ‚ V) {u v : E} (hu : u âˆˆ U) (hv : v âˆˆ V) :
    âŸªu, vâŸ« = 0 :=
  h.symm hv _ hu
#align submodule.is_ortho.inner_eq Submodule.IsOrtho.inner_eq

theorem isOrtho_iff_inner_eq {U V : Submodule ğ•œ E} : U âŸ‚ V â†” âˆ€ u âˆˆ U, âˆ€ v âˆˆ V, âŸªu, vâŸ« = 0 :=
  forallâ‚„_congr fun _u _hu _v _hv => inner_eq_zero_symm
#align submodule.is_ortho_iff_inner_eq Submodule.isOrtho_iff_inner_eq

/- TODO: generalize `Submodule.mapâ‚‚` to semilinear maps, so that we can state
`U âŸ‚ V â†” Submodule.mapâ‚‚ (innerâ‚›â‚— ğ•œ) U V â‰¤ âŠ¥`. -/
@[simp]
theorem isOrtho_bot_left {V : Submodule ğ•œ E} : âŠ¥ âŸ‚ V :=
  bot_le
#align submodule.is_ortho_bot_left Submodule.isOrtho_bot_left

@[simp]
theorem isOrtho_bot_right {U : Submodule ğ•œ E} : U âŸ‚ âŠ¥ :=
  isOrtho_bot_left.symm
#align submodule.is_ortho_bot_right Submodule.isOrtho_bot_right

theorem IsOrtho.mono_left {Uâ‚ Uâ‚‚ V : Submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (h : Uâ‚ âŸ‚ V) : Uâ‚‚ âŸ‚ V :=
  hU.trans h
#align submodule.is_ortho.mono_left Submodule.IsOrtho.mono_left

theorem IsOrtho.mono_right {U Vâ‚ Vâ‚‚ : Submodule ğ•œ E} (hV : Vâ‚‚ â‰¤ Vâ‚) (h : U âŸ‚ Vâ‚) : U âŸ‚ Vâ‚‚ :=
  (h.symm.mono_left hV).symm
#align submodule.is_ortho.mono_right Submodule.IsOrtho.mono_right

theorem IsOrtho.mono {Uâ‚ Vâ‚ Uâ‚‚ Vâ‚‚ : Submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (hV : Vâ‚‚ â‰¤ Vâ‚) (h : Uâ‚ âŸ‚ Vâ‚) :
    Uâ‚‚ âŸ‚ Vâ‚‚ :=
  (h.mono_right hV).mono_left hU
#align submodule.is_ortho.mono Submodule.IsOrtho.mono

@[simp]
theorem isOrtho_self {U : Submodule ğ•œ E} : U âŸ‚ U â†” U = âŠ¥ :=
  âŸ¨fun h => eq_bot_iff.mpr fun x hx => inner_self_eq_zero.mp (h hx x hx), fun h =>
    h.symm â–¸ isOrtho_bot_leftâŸ©
#align submodule.is_ortho_self Submodule.isOrtho_self

@[simp]
theorem isOrtho_orthogonal_right (U : Submodule ğ•œ E) : U âŸ‚ Uá—® :=
  le_orthogonal_orthogonal _
#align submodule.is_ortho_orthogonal_right Submodule.isOrtho_orthogonal_right

@[simp]
theorem isOrtho_orthogonal_left (U : Submodule ğ•œ E) : Uá—® âŸ‚ U :=
  (isOrtho_orthogonal_right U).symm
#align submodule.is_ortho_orthogonal_left Submodule.isOrtho_orthogonal_left

theorem IsOrtho.le {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : U â‰¤ Vá—® :=
  h
#align submodule.is_ortho.le Submodule.IsOrtho.le

theorem IsOrtho.ge {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : V â‰¤ Uá—® :=
  h.symm
#align submodule.is_ortho.ge Submodule.IsOrtho.ge

@[simp]
theorem isOrtho_top_right {U : Submodule ğ•œ E} : U âŸ‚ âŠ¤ â†” U = âŠ¥ :=
  âŸ¨fun h => eq_bot_iff.mpr fun _x hx => inner_self_eq_zero.mp (h hx _ mem_top), fun h =>
    h.symm â–¸ isOrtho_bot_leftâŸ©
#align submodule.is_ortho_top_right Submodule.isOrtho_top_right

@[simp]
theorem isOrtho_top_left {V : Submodule ğ•œ E} : âŠ¤ âŸ‚ V â†” V = âŠ¥ :=
  isOrtho_comm.trans isOrtho_top_right
#align submodule.is_ortho_top_left Submodule.isOrtho_top_left

/-- Orthogonal submodules are disjoint. -/
theorem IsOrtho.disjoint {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : Disjoint U V :=
  (Submodule.orthogonal_disjoint _).mono_right h.symm
#align submodule.is_ortho.disjoint Submodule.IsOrtho.disjoint

@[simp]
theorem isOrtho_sup_left {Uâ‚ Uâ‚‚ V : Submodule ğ•œ E} : Uâ‚ âŠ” Uâ‚‚ âŸ‚ V â†” Uâ‚ âŸ‚ V âˆ§ Uâ‚‚ âŸ‚ V :=
  sup_le_iff
#align submodule.is_ortho_sup_left Submodule.isOrtho_sup_left

@[simp]
theorem isOrtho_sup_right {U Vâ‚ Vâ‚‚ : Submodule ğ•œ E} : U âŸ‚ Vâ‚ âŠ” Vâ‚‚ â†” U âŸ‚ Vâ‚ âˆ§ U âŸ‚ Vâ‚‚ :=
  isOrtho_comm.trans <| isOrtho_sup_left.trans <| isOrtho_comm.and isOrtho_comm
#align submodule.is_ortho_sup_right Submodule.isOrtho_sup_right

@[simp]
theorem isOrtho_sSup_left {U : Set (Submodule ğ•œ E)} {V : Submodule ğ•œ E} :
    sSup U âŸ‚ V â†” âˆ€ Uáµ¢ âˆˆ U, Uáµ¢ âŸ‚ V :=
  sSup_le_iff
#align submodule.is_ortho_Sup_left Submodule.isOrtho_sSup_left

@[simp]
theorem isOrtho_sSup_right {U : Submodule ğ•œ E} {V : Set (Submodule ğ•œ E)} :
    U âŸ‚ sSup V â†” âˆ€ Váµ¢ âˆˆ V, U âŸ‚ Váµ¢ :=
  isOrtho_comm.trans <| isOrtho_sSup_left.trans <| by simp_rw [isOrtho_comm]
#align submodule.is_ortho_Sup_right Submodule.isOrtho_sSup_right

@[simp]
theorem isOrtho_iSup_left {Î¹ : Sort*} {U : Î¹ â†’ Submodule ğ•œ E} {V : Submodule ğ•œ E} :
    iSup U âŸ‚ V â†” âˆ€ i, U i âŸ‚ V :=
  iSup_le_iff
#align submodule.is_ortho_supr_left Submodule.isOrtho_iSup_left

@[simp]
theorem isOrtho_iSup_right {Î¹ : Sort*} {U : Submodule ğ•œ E} {V : Î¹ â†’ Submodule ğ•œ E} :
    U âŸ‚ iSup V â†” âˆ€ i, U âŸ‚ V i :=
  isOrtho_comm.trans <| isOrtho_iSup_left.trans <| by simp_rw [isOrtho_comm]
#align submodule.is_ortho_supr_right Submodule.isOrtho_iSup_right

@[simp]
theorem isOrtho_span {s t : Set E} :
    span ğ•œ s âŸ‚ span ğ•œ t â†” âˆ€ â¦ƒuâ¦„, u âˆˆ s â†’ âˆ€ â¦ƒvâ¦„, v âˆˆ t â†’ âŸªu, vâŸ« = 0 := by
  simp_rw [span_eq_iSup_of_singleton_spans s, span_eq_iSup_of_singleton_spans t, isOrtho_iSup_left,
    isOrtho_iSup_right, isOrtho_iff_le, span_le, Set.subset_def, SetLike.mem_coe,
    mem_orthogonal_singleton_iff_inner_left, Set.mem_singleton_iff, forall_eq]
#align submodule.is_ortho_span Submodule.isOrtho_span

theorem IsOrtho.map (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : U.map f âŸ‚ V.map f := by
  rw [isOrtho_iff_inner_eq] at *
  simp_rw [mem_map, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚,
    LinearIsometry.inner_map_map]
  exact h
#align submodule.is_ortho.map Submodule.IsOrtho.map

theorem IsOrtho.comap (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ F} (h : U âŸ‚ V) :
    U.comap f âŸ‚ V.comap f := by
  rw [isOrtho_iff_inner_eq] at *
  simp_rw [mem_comap, â† f.inner_map_map]
  intro u hu v hv
  exact h _ hu _ hv
#align submodule.is_ortho.comap Submodule.IsOrtho.comap

@[simp]
theorem IsOrtho.map_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ E} : U.map f âŸ‚ V.map f â†” U âŸ‚ V :=
  âŸ¨fun h => by
    have hf : âˆ€ p : Submodule ğ•œ E, (p.map f).comap f.toLinearIsometry = p :=
      comap_map_eq_of_injective f.injective
    simpa only [hf] using h.comap f.toLinearIsometry, IsOrtho.map f.toLinearIsometryâŸ©
#align submodule.is_ortho.map_iff Submodule.IsOrtho.map_iff

@[simp]
theorem IsOrtho.comap_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ F} : U.comap f âŸ‚ V.comap f â†” U âŸ‚ V :=
  âŸ¨fun h => by
    have hf : âˆ€ p : Submodule ğ•œ F, (p.comap f).map f.toLinearIsometry = p :=
      map_comap_eq_of_surjective f.surjective
    simpa only [hf] using h.map f.toLinearIsometry, IsOrtho.comap f.toLinearIsometryâŸ©
#align submodule.is_ortho.comap_iff Submodule.IsOrtho.comap_iff

end Submodule

theorem orthogonalFamily_iff_pairwise {Î¹} {V : Î¹ â†’ Submodule ğ•œ E} :
    (OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) â†” Pairwise ((Â· âŸ‚ Â·) on V) :=
  forallâ‚ƒ_congr fun _i _j _hij =>
    Subtype.forall.trans <|
      forallâ‚‚_congr fun _x _hx => Subtype.forall.trans <|
        forallâ‚‚_congr fun _y _hy => inner_eq_zero_symm
#align orthogonal_family_iff_pairwise orthogonalFamily_iff_pairwise

alias âŸ¨OrthogonalFamily.pairwise, OrthogonalFamily.of_pairwiseâŸ© := orthogonalFamily_iff_pairwise
#align orthogonal_family.pairwise OrthogonalFamily.pairwise
#align orthogonal_family.of_pairwise OrthogonalFamily.of_pairwise

/-- Two submodules in an orthogonal family with different indices are orthogonal. -/
theorem OrthogonalFamily.isOrtho {Î¹} {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) {i j : Î¹} (hij : i â‰  j) :
    V i âŸ‚ V j :=
  hV.pairwise hij
#align orthogonal_family.is_ortho OrthogonalFamily.isOrtho
