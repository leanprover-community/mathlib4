/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.Subspace
import Mathlib.LinearAlgebra.SesquilinearForm

/-!
# Orthogonal complements of submodules

In this file, the `orthogonal` complement of a submodule `K` is defined, and basic API established.
Some of the more subtle results about the orthogonal complement are delayed to
`Analysis.InnerProductSpace.Projection`.

See also `BilinForm.orthogonal` for orthogonality with respect to a general bilinear form.

## Notation

The orthogonal complement of a submodule `K` is denoted by `Ká—®`.

The proposition that two submodules are orthogonal, `Submodule.IsOrtho`, is denoted by `U âŸ‚ V`.
Note this is not the same unicode symbol as `âŠ¥` (`Bot`).
-/

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

namespace Submodule

variable (K : Submodule ğ•œ E)

/-- The subspace of vectors orthogonal to a given subspace, denoted `Ká—®`. -/
def orthogonal : Submodule ğ•œ E where
  carrier := { v | âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 }
  zero_mem' _ _ := inner_zero_right _
  add_mem' hx hy u hu := by rw [inner_add_right, hx u hu, hy u hu, add_zero]
  smul_mem' c x hx u hu := by rw [inner_smul_right, hx u hu, mul_zero]

@[inherit_doc]
notation:1200 K "á—®" => orthogonal K

/-- When a vector is in `Ká—®`. -/
theorem mem_orthogonal (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªu, vâŸ« = 0 :=
  Iff.rfl

/-- When a vector is in `Ká—®`, with the inner product the
other way round. -/
theorem mem_orthogonal' (v : E) : v âˆˆ Ká—® â†” âˆ€ u âˆˆ K, âŸªv, uâŸ« = 0 := by
  simp_rw [mem_orthogonal, inner_eq_zero_symm]

variable {K}

/-- A vector in `K` is orthogonal to one in `Ká—®`. -/
theorem inner_right_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªu, vâŸ« = 0 :=
  (K.mem_orthogonal v).1 hv u hu

/-- A vector in `Ká—®` is orthogonal to one in `K`. -/
theorem inner_left_of_mem_orthogonal {u v : E} (hu : u âˆˆ K) (hv : v âˆˆ Ká—®) : âŸªv, uâŸ« = 0 := by
  rw [inner_eq_zero_symm]; exact inner_right_of_mem_orthogonal hu hv

/-- A vector is in `(ğ•œ âˆ™ u)á—®` iff it is orthogonal to `u`. -/
theorem mem_orthogonal_singleton_iff_inner_right {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªu, vâŸ« = 0 := by
  refine âŸ¨inner_right_of_mem_orthogonal (mem_span_singleton_self u), ?_âŸ©
  intro hv w hw
  rw [mem_span_singleton] at hw
  obtain âŸ¨c, rflâŸ© := hw
  simp [inner_smul_left, hv]

/-- A vector in `(ğ•œ âˆ™ u)á—®` is orthogonal to `u`. -/
theorem mem_orthogonal_singleton_iff_inner_left {u v : E} : v âˆˆ (ğ•œ âˆ™ u)á—® â†” âŸªv, uâŸ« = 0 := by
  rw [mem_orthogonal_singleton_iff_inner_right, inner_eq_zero_symm]

theorem sub_mem_orthogonal_of_inner_left {x y : E} (h : âˆ€ v : K, âŸªx, vâŸ« = âŸªy, vâŸ«) : x - y âˆˆ Ká—® := by
  rw [mem_orthogonal']
  intro u hu
  rw [inner_sub_left, sub_eq_zero]
  exact h âŸ¨u, huâŸ©

theorem sub_mem_orthogonal_of_inner_right {x y : E} (h : âˆ€ v : K, âŸª(v : E), xâŸ« = âŸª(v : E), yâŸ«) :
    x - y âˆˆ Ká—® := by
  intro u hu
  rw [inner_sub_right, sub_eq_zero]
  exact h âŸ¨u, huâŸ©

variable (K)

/-- `K` and `Ká—®` have trivial intersection. -/
theorem inf_orthogonal_eq_bot : K âŠ“ Ká—® = âŠ¥ := by
  rw [eq_bot_iff]
  intro x
  rw [mem_inf]
  exact fun âŸ¨hx, hoâŸ© => inner_self_eq_zero.1 (ho x hx)

/-- `K` and `Ká—®` have trivial intersection. -/
theorem orthogonal_disjoint : Disjoint K Ká—® := by simp [disjoint_iff, K.inf_orthogonal_eq_bot]

/-- `Ká—®` can be characterized as the intersection of the kernels of the operations of
inner product with each of the elements of `K`. -/
theorem orthogonal_eq_inter : Ká—® = â¨… v : K, LinearMap.ker (innerSL ğ•œ (v : E)) := by
  apply le_antisymm
  Â· rw [le_iInf_iff]
    rintro âŸ¨v, hvâŸ© w hw
    simpa using hw _ hv
  Â· intro v hv w hw
    simp only [mem_iInf] at hv
    exact hv âŸ¨w, hwâŸ©

/-- The orthogonal complement of any submodule `K` is closed. -/
theorem isClosed_orthogonal : IsClosed (Ká—® : Set E) := by
  rw [orthogonal_eq_inter K]
  convert isClosed_iInter <| fun v : K => ContinuousLinearMap.isClosed_ker (innerSL ğ•œ (v : E))
  simp only [coe_iInf]

/-- In a complete space, the orthogonal complement of any submodule `K` is complete. -/
instance instOrthogonalCompleteSpace [CompleteSpace E] : CompleteSpace Ká—® :=
  K.isClosed_orthogonal.completeSpace_coe

variable (ğ•œ E)

/-- `orthogonal` gives a `GaloisConnection` between
`Submodule ğ•œ E` and its `OrderDual`. -/
theorem orthogonal_gc :
    @GaloisConnection (Submodule ğ•œ E) (Submodule ğ•œ E)áµ’áµˆ _ _ orthogonal orthogonal := fun _Kâ‚ _Kâ‚‚ =>
  âŸ¨fun h _v hv _u hu => inner_left_of_mem_orthogonal hv (h hu), fun h _v hv _u hu =>
    inner_left_of_mem_orthogonal hv (h hu)âŸ©

variable {ğ•œ E}

/-- `orthogonal` reverses the `â‰¤` ordering of two
subspaces. -/
theorem orthogonal_le {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) : Kâ‚‚á—® â‰¤ Kâ‚á—® :=
  (orthogonal_gc ğ•œ E).monotone_l h

/-- `orthogonal.orthogonal` preserves the `â‰¤` ordering of two
subspaces. -/
theorem orthogonal_orthogonal_monotone {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚) : Kâ‚á—®á—® â‰¤ Kâ‚‚á—®á—® :=
  orthogonal_le (orthogonal_le h)

/-- `K` is contained in `Ká—®á—®`. -/
theorem le_orthogonal_orthogonal : K â‰¤ Ká—®á—® :=
  (orthogonal_gc ğ•œ E).le_u_l _

/-- The inf of two orthogonal subspaces equals the subspace orthogonal
to the sup. -/
theorem inf_orthogonal (Kâ‚ Kâ‚‚ : Submodule ğ•œ E) : Kâ‚á—® âŠ“ Kâ‚‚á—® = (Kâ‚ âŠ” Kâ‚‚)á—® :=
  (orthogonal_gc ğ•œ E).l_sup.symm

/-- The inf of an indexed family of orthogonal subspaces equals the
subspace orthogonal to the sup. -/
theorem iInf_orthogonal {Î¹ : Type*} (K : Î¹ â†’ Submodule ğ•œ E) : â¨… i, (K i)á—® = (iSup K)á—® :=
  (orthogonal_gc ğ•œ E).l_iSup.symm

/-- The inf of a set of orthogonal subspaces equals the subspace orthogonal to the sup. -/
theorem sInf_orthogonal (s : Set <| Submodule ğ•œ E) : â¨… K âˆˆ s, Ká—® = (sSup s)á—® :=
  (orthogonal_gc ğ•œ E).l_sSup.symm

@[simp]
theorem top_orthogonal_eq_bot : (âŠ¤ : Submodule ğ•œ E)á—® = âŠ¥ := by
  ext x
  rw [mem_bot, mem_orthogonal]
  exact
    âŸ¨fun h => inner_self_eq_zero.mp (h x mem_top), by
      rintro rfl
      simpâŸ©

@[simp]
theorem bot_orthogonal_eq_top : (âŠ¥ : Submodule ğ•œ E)á—® = âŠ¤ := by
  rw [â† top_orthogonal_eq_bot, eq_top_iff]
  exact le_orthogonal_orthogonal âŠ¤

@[simp]
theorem orthogonal_eq_top_iff : Ká—® = âŠ¤ â†” K = âŠ¥ := by
  refine
    âŸ¨?_, by
      rintro rfl
      exact bot_orthogonal_eq_topâŸ©
  intro h
  have : K âŠ“ Ká—® = âŠ¥ := K.orthogonal_disjoint.eq_bot
  rwa [h, inf_comm, top_inf_eq] at this

/-- The closure of a submodule has the same orthogonal complement and the submodule itself. -/
@[simp]
lemma orthogonal_closure (K : Submodule ğ•œ E) : K.topologicalClosureá—® = Ká—® :=
  le_antisymm (orthogonal_le <| le_topologicalClosure _)
    fun x hx y hy â†¦ closure_minimal hx (isClosed_eq (by fun_prop) (by fun_prop)) hy

theorem orthogonalFamily_self :
    OrthogonalFamily ğ•œ (fun b => â†¥(cond b K Ká—®)) fun b => (cond b K Ká—®).subtypeâ‚—áµ¢
  | true, true => absurd rfl
  | true, false => fun _ x y => inner_right_of_mem_orthogonal x.prop y.prop
  | false, true => fun _ x y => inner_left_of_mem_orthogonal y.prop x.prop
  | false, false => absurd rfl

end Submodule

@[simp]
theorem bilinFormOfRealInner_orthogonal {E} [NormedAddCommGroup E] [InnerProductSpace â„ E]
    (K : Submodule â„ E) : K.orthogonalBilin bilinFormOfRealInner = Ká—® :=
  rfl

/-!
### Orthogonality of submodules

In this section we define `Submodule.IsOrtho U V`, denoted as `U âŸ‚ V`.

The API roughly matches that of `Disjoint`.
-/


namespace Submodule

/-- The proposition that two submodules are orthogonal, denoted as `U âŸ‚ V`. -/
def IsOrtho (U V : Submodule ğ•œ E) : Prop :=
  U â‰¤ Vá—®

@[inherit_doc]
infixl:50 " âŸ‚ " => Submodule.IsOrtho

theorem isOrtho_iff_le {U V : Submodule ğ•œ E} : U âŸ‚ V â†” U â‰¤ Vá—® :=
  Iff.rfl

@[symm]
theorem IsOrtho.symm {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : V âŸ‚ U :=
  (le_orthogonal_orthogonal _).trans (orthogonal_le h)

theorem isOrtho_comm {U V : Submodule ğ•œ E} : U âŸ‚ V â†” V âŸ‚ U :=
  âŸ¨IsOrtho.symm, IsOrtho.symmâŸ©

theorem symmetric_isOrtho : Symmetric (IsOrtho : Submodule ğ•œ E â†’ Submodule ğ•œ E â†’ Prop) := fun _ _ =>
  IsOrtho.symm

theorem IsOrtho.inner_eq {U V : Submodule ğ•œ E} (h : U âŸ‚ V) {u v : E} (hu : u âˆˆ U) (hv : v âˆˆ V) :
    âŸªu, vâŸ« = 0 :=
  h.symm hv _ hu

theorem isOrtho_iff_inner_eq {U V : Submodule ğ•œ E} : U âŸ‚ V â†” âˆ€ u âˆˆ U, âˆ€ v âˆˆ V, âŸªu, vâŸ« = 0 :=
  forallâ‚„_congr fun _u _hu _v _hv => inner_eq_zero_symm

/- TODO: generalize `Submodule.mapâ‚‚` to semilinear maps, so that we can state
`U âŸ‚ V â†” Submodule.mapâ‚‚ (innerâ‚›â‚— ğ•œ) U V â‰¤ âŠ¥`. -/
@[simp]
theorem isOrtho_bot_left {V : Submodule ğ•œ E} : âŠ¥ âŸ‚ V :=
  bot_le

@[simp]
theorem isOrtho_bot_right {U : Submodule ğ•œ E} : U âŸ‚ âŠ¥ :=
  isOrtho_bot_left.symm

theorem IsOrtho.mono_left {Uâ‚ Uâ‚‚ V : Submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (h : Uâ‚ âŸ‚ V) : Uâ‚‚ âŸ‚ V :=
  hU.trans h

theorem IsOrtho.mono_right {U Vâ‚ Vâ‚‚ : Submodule ğ•œ E} (hV : Vâ‚‚ â‰¤ Vâ‚) (h : U âŸ‚ Vâ‚) : U âŸ‚ Vâ‚‚ :=
  (h.symm.mono_left hV).symm

theorem IsOrtho.mono {Uâ‚ Vâ‚ Uâ‚‚ Vâ‚‚ : Submodule ğ•œ E} (hU : Uâ‚‚ â‰¤ Uâ‚) (hV : Vâ‚‚ â‰¤ Vâ‚) (h : Uâ‚ âŸ‚ Vâ‚) :
    Uâ‚‚ âŸ‚ Vâ‚‚ :=
  (h.mono_right hV).mono_left hU

@[simp]
theorem isOrtho_self {U : Submodule ğ•œ E} : U âŸ‚ U â†” U = âŠ¥ :=
  âŸ¨fun h => eq_bot_iff.mpr fun x hx => inner_self_eq_zero.mp (h hx x hx), fun h =>
    h.symm â–¸ isOrtho_bot_leftâŸ©

@[simp]
theorem isOrtho_orthogonal_right (U : Submodule ğ•œ E) : U âŸ‚ Uá—® :=
  le_orthogonal_orthogonal _

@[simp]
theorem isOrtho_orthogonal_left (U : Submodule ğ•œ E) : Uá—® âŸ‚ U :=
  (isOrtho_orthogonal_right U).symm

theorem IsOrtho.le {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : U â‰¤ Vá—® :=
  h

theorem IsOrtho.ge {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : V â‰¤ Uá—® :=
  h.symm

@[simp]
theorem isOrtho_top_right {U : Submodule ğ•œ E} : U âŸ‚ âŠ¤ â†” U = âŠ¥ :=
  âŸ¨fun h => eq_bot_iff.mpr fun _x hx => inner_self_eq_zero.mp (h hx _ mem_top), fun h =>
    h.symm â–¸ isOrtho_bot_leftâŸ©

@[simp]
theorem isOrtho_top_left {V : Submodule ğ•œ E} : âŠ¤ âŸ‚ V â†” V = âŠ¥ :=
  isOrtho_comm.trans isOrtho_top_right

/-- Orthogonal submodules are disjoint. -/
theorem IsOrtho.disjoint {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : Disjoint U V :=
  (Submodule.orthogonal_disjoint _).mono_right h.symm

@[simp]
theorem isOrtho_sup_left {Uâ‚ Uâ‚‚ V : Submodule ğ•œ E} : Uâ‚ âŠ” Uâ‚‚ âŸ‚ V â†” Uâ‚ âŸ‚ V âˆ§ Uâ‚‚ âŸ‚ V :=
  sup_le_iff

@[simp]
theorem isOrtho_sup_right {U Vâ‚ Vâ‚‚ : Submodule ğ•œ E} : U âŸ‚ Vâ‚ âŠ” Vâ‚‚ â†” U âŸ‚ Vâ‚ âˆ§ U âŸ‚ Vâ‚‚ :=
  isOrtho_comm.trans <| isOrtho_sup_left.trans <| isOrtho_comm.and isOrtho_comm

@[simp]
theorem isOrtho_sSup_left {U : Set (Submodule ğ•œ E)} {V : Submodule ğ•œ E} :
    sSup U âŸ‚ V â†” âˆ€ Uáµ¢ âˆˆ U, Uáµ¢ âŸ‚ V :=
  sSup_le_iff

@[simp]
theorem isOrtho_sSup_right {U : Submodule ğ•œ E} {V : Set (Submodule ğ•œ E)} :
    U âŸ‚ sSup V â†” âˆ€ Váµ¢ âˆˆ V, U âŸ‚ Váµ¢ :=
  isOrtho_comm.trans <| isOrtho_sSup_left.trans <| by simp_rw [isOrtho_comm]

@[simp]
theorem isOrtho_iSup_left {Î¹ : Sort*} {U : Î¹ â†’ Submodule ğ•œ E} {V : Submodule ğ•œ E} :
    iSup U âŸ‚ V â†” âˆ€ i, U i âŸ‚ V :=
  iSup_le_iff

@[simp]
theorem isOrtho_iSup_right {Î¹ : Sort*} {U : Submodule ğ•œ E} {V : Î¹ â†’ Submodule ğ•œ E} :
    U âŸ‚ iSup V â†” âˆ€ i, U âŸ‚ V i :=
  isOrtho_comm.trans <| isOrtho_iSup_left.trans <| by simp_rw [isOrtho_comm]

@[simp]
theorem isOrtho_span {s t : Set E} :
    span ğ•œ s âŸ‚ span ğ•œ t â†” âˆ€ â¦ƒuâ¦„, u âˆˆ s â†’ âˆ€ â¦ƒvâ¦„, v âˆˆ t â†’ âŸªu, vâŸ« = 0 := by
  simp_rw [span_eq_iSup_of_singleton_spans s, span_eq_iSup_of_singleton_spans t, isOrtho_iSup_left,
    isOrtho_iSup_right, isOrtho_iff_le, span_le, Set.subset_def, SetLike.mem_coe,
    mem_orthogonal_singleton_iff_inner_left, Set.mem_singleton_iff, forall_eq]

theorem IsOrtho.map (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ E} (h : U âŸ‚ V) : U.map f âŸ‚ V.map f := by
  rw [isOrtho_iff_inner_eq] at *
  simp_rw [mem_map, forall_exists_index, and_imp, forall_apply_eq_imp_iffâ‚‚,
    LinearIsometry.inner_map_map]
  exact h

theorem IsOrtho.comap (f : E â†’â‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ F} (h : U âŸ‚ V) :
    U.comap f âŸ‚ V.comap f := by
  rw [isOrtho_iff_inner_eq] at *
  simp_rw [mem_comap, â† f.inner_map_map]
  intro u hu v hv
  exact h _ hu _ hv

@[simp]
theorem IsOrtho.map_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ E} : U.map f âŸ‚ V.map f â†” U âŸ‚ V :=
  âŸ¨fun h => by
    have hf : âˆ€ p : Submodule ğ•œ E, (p.map f).comap f.toLinearIsometry = p :=
      comap_map_eq_of_injective f.injective
    simpa only [hf] using h.comap f.toLinearIsometry, IsOrtho.map f.toLinearIsometryâŸ©

@[simp]
theorem IsOrtho.comap_iff (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) {U V : Submodule ğ•œ F} : U.comap f âŸ‚ V.comap f â†” U âŸ‚ V :=
  âŸ¨fun h => by
    have hf : âˆ€ p : Submodule ğ•œ F, (p.comap f).map f.toLinearIsometry = p :=
      map_comap_eq_of_surjective f.surjective
    simpa only [hf] using h.map f.toLinearIsometry, IsOrtho.comap f.toLinearIsometryâŸ©

end Submodule

open scoped Function in -- required for scoped `on` notation
theorem orthogonalFamily_iff_pairwise {Î¹} {V : Î¹ â†’ Submodule ğ•œ E} :
    (OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) â†” Pairwise ((Â· âŸ‚ Â·) on V) :=
  forallâ‚ƒ_congr fun _i _j _hij =>
    Subtype.forall.trans <|
      forallâ‚‚_congr fun _x _hx => Subtype.forall.trans <|
        forallâ‚‚_congr fun _y _hy => inner_eq_zero_symm

alias âŸ¨OrthogonalFamily.pairwise, OrthogonalFamily.of_pairwiseâŸ© := orthogonalFamily_iff_pairwise

/-- Two submodules in an orthogonal family with different indices are orthogonal. -/
theorem OrthogonalFamily.isOrtho {Î¹} {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) {i j : Î¹} (hij : i â‰  j) :
    V i âŸ‚ V j :=
  hV.pairwise hij
