/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Analysis.InnerProductSpace.Convex
import Mathlib.Algebra.Module.LinearMap.Rat
import Mathlib.Tactic.Module

/-!
# Inner product space derived from a norm

This file defines an `InnerProductSpace` instance from a norm that respects the
parallellogram identity. The parallelogram identity is a way to express the inner product of `x` and
`y` in terms of the norms of `x`, `y`, `x + y`, `x - y`.

## Main results

- `InnerProductSpace.ofNorm`: a normed space whose norm respects the parallellogram identity,
  can be seen as an inner product space.

## Implementation notes

We define `inner_`

$$\langle x, y \rangle := \frac{1}{4} (â€–x + yâ€–^2 - â€–x - yâ€–^2 + i â€–ix + yâ€– ^ 2 - i â€–ix - yâ€–^2)$$

and use the parallelogram identity

$$â€–x + yâ€–^2 + â€–x - yâ€–^2 = 2 (â€–xâ€–^2 + â€–yâ€–^2)$$

to prove it is an inner product, i.e., that it is conjugate-symmetric (`inner_.conj_symm`) and
linear in the first argument. `add_left` is proved by judicious application of the parallelogram
identity followed by tedious arithmetic. `smul_left` is proved step by step, first noting that
$\langle Î» x, y \rangle = Î» \langle x, y \rangle$ for $Î» âˆˆ â„•$, $Î» = -1$, hence $Î» âˆˆ â„¤$ and $Î» âˆˆ â„š$
by arithmetic. Then by continuity and the fact that â„š is dense in â„, the same is true for â„.
The case of â„‚ then follows by applying the result for â„ and more arithmetic.

## TODO

Move upstream to `Analysis.InnerProductSpace.Basic`.

## References

- [Jordan, P. and von Neumann, J., *On inner products in linear, metric spaces*][Jordan1935]
- https://math.stackexchange.com/questions/21792/norms-induced-by-inner-products-and-the-parallelogram-law
- https://math.dartmouth.edu/archive/m113w10/public_html/jordan-vneumann-thm.pdf

## Tags

inner product space, Hilbert space, norm
-/


open RCLike

open scoped ComplexConjugate

variable {ğ•œ : Type*} [RCLike ğ•œ] (E : Type*) [NormedAddCommGroup E]

/-- Predicate for the parallelogram identity to hold in a normed group. This is a scalar-less
version of `InnerProductSpace`. If you have an `InnerProductSpaceable` assumption, you can
locally upgrade that to `InnerProductSpace ğ•œ E` using `casesI nonempty_innerProductSpace ğ•œ E`.
-/
class InnerProductSpaceable : Prop where
  parallelogram_identity :
    âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–)

variable (ğ•œ) {E}

theorem InnerProductSpace.toInnerProductSpaceable [InnerProductSpace ğ•œ E] :
    InnerProductSpaceable E :=
  âŸ¨parallelogram_law_with_norm ğ•œâŸ©

-- See note [lower instance priority]
instance (priority := 100) InnerProductSpace.toInnerProductSpaceable_ofReal
    [InnerProductSpace â„ E] : InnerProductSpaceable E :=
  âŸ¨parallelogram_law_with_norm â„âŸ©

variable [NormedSpace ğ•œ E]

local notation "ğ“š" => algebraMap â„ ğ•œ

/-- Auxiliary definition of the inner product derived from the norm. -/
private noncomputable def inner_ (x y : E) : ğ•œ :=
  4â»Â¹ * (ğ“š â€–x + yâ€– * ğ“š â€–x + yâ€– - ğ“š â€–x - yâ€– * ğ“š â€–x - yâ€– +
    (I : ğ•œ) * ğ“š â€–(I : ğ•œ) â€¢ x + yâ€– * ğ“š â€–(I : ğ•œ) â€¢ x + yâ€– -
    (I : ğ•œ) * ğ“š â€–(I : ğ•œ) â€¢ x - yâ€– * ğ“š â€–(I : ğ•œ) â€¢ x - yâ€–)

namespace InnerProductSpaceable

variable {ğ•œ} (E)

-- This has a prime added to avoid clashing with public `innerProp`
/-- Auxiliary definition for the `add_left` property. -/
private def innerProp' (r : ğ•œ) : Prop :=
  âˆ€ x y : E, inner_ ğ•œ (r â€¢ x) y = conj r * inner_ ğ•œ x y

variable {E}

theorem _root_.Continuous.inner_ {f g : â„ â†’ E} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun x => inner_ ğ•œ (f x) (g x) := by
  unfold _root_.inner_
  fun_prop

theorem inner_.norm_sq (x : E) : â€–xâ€– ^ 2 = re (inner_ ğ•œ x x) := by
  simp only [inner_, normSq_apply, ofNat_re, ofNat_im, map_sub, map_add,
    ofReal_re, ofReal_im, mul_re, inv_re, mul_im, I_re, inv_im]
  have hâ‚ : â€–x - xâ€– = 0 := by simp
  have hâ‚‚ : â€–x + xâ€– = 2 â€¢ â€–xâ€– := by convert norm_nsmul ğ•œ 2 x using 2; module
  rw [hâ‚, hâ‚‚]
  ring

theorem inner_.conj_symm (x y : E) : conj (inner_ ğ•œ y x) = inner_ ğ•œ x y := by
  simp only [inner_, map_sub, map_add, map_mul, map_invâ‚€, map_ofNat, conj_ofReal, conj_I]
  rw [add_comm y x, norm_sub_rev]
  by_cases hI : (I : ğ•œ) = 0
  Â· simp only [hI, neg_zero, zero_mul]
  have hI' := I_mul_I_of_nonzero hI
  have I_smul (v : E) : â€–(I : ğ•œ) â€¢ vâ€– = â€–vâ€– := by rw [norm_smul, norm_I_of_ne_zero hI, one_mul]
  have hâ‚ : â€–(I : ğ•œ) â€¢ y - xâ€– = â€–(I : ğ•œ) â€¢ x + yâ€– := by
    convert I_smul ((I : ğ•œ) â€¢ x + y) using 2
    linear_combination (norm := module) -hI' â€¢ x
  have hâ‚‚ : â€–(I : ğ•œ) â€¢ y + xâ€– = â€–(I : ğ•œ) â€¢ x - yâ€– := by
    convert (I_smul ((I : ğ•œ) â€¢ y + x)).symm using 2
    linear_combination (norm := module) -hI' â€¢ y
  rw [hâ‚, hâ‚‚]
  ring

variable [InnerProductSpaceable E]

private theorem add_left_aux1 (x y z : E) :
    â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€–
    = 2 * (â€–x + y + zâ€– * â€–x + y + zâ€– + â€–x - zâ€– * â€–x - zâ€–) := by
  convert parallelogram_identity (x + y + z) (x - z) using 4 <;> abel

private theorem add_left_aux2 (x y z : E) : â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–
    = 2 * (â€–x + y - zâ€– * â€–x + y - zâ€– + â€–x + zâ€– * â€–x + zâ€–) := by
  convert parallelogram_identity (x + y - z) (x + z) using 4 <;> abel

private theorem add_left_aux3 (y z : E) :
    â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– + â€–yâ€– * â€–yâ€– = 2 * (â€–y + zâ€– * â€–y + zâ€– + â€–zâ€– * â€–zâ€–) := by
  convert parallelogram_identity (y + z) z using 4 <;> abel

private theorem add_left_aux4 (y z : E) :
    â€–yâ€– * â€–yâ€– + â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€– = 2 * (â€–y - zâ€– * â€–y - zâ€– + â€–zâ€– * â€–zâ€–) := by
  convert parallelogram_identity (y - z) z using 4 <;> abel

variable (ğ•œ)

private theorem add_left_aux5 (x y z : E) :
    â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€–
    + â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€–
    = 2 * (â€–(I : ğ•œ) â€¢ (x + y) + zâ€– * â€–(I : ğ•œ) â€¢ (x + y) + zâ€–
    + â€–(I : ğ•œ) â€¢ x - zâ€– * â€–(I : ğ•œ) â€¢ x - zâ€–) := by
  convert parallelogram_identity ((I : ğ•œ) â€¢ (x + y) + z) ((I : ğ•œ) â€¢ x - z) using 4 <;> module

private theorem add_left_aux6 (x y z : E) :
    (â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– +
    â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€–)
    = 2 * (â€–(I : ğ•œ) â€¢ (x + y) - zâ€– * â€–(I : ğ•œ) â€¢ (x + y) - zâ€– +
    â€–(I : ğ•œ) â€¢ x + zâ€– * â€–(I : ğ•œ) â€¢ x + zâ€–) := by
  convert parallelogram_identity ((I : ğ•œ) â€¢ (x + y) - z) ((I : ğ•œ) â€¢ x + z) using 4 <;> module

private theorem add_left_aux7 (y z : E) :
    â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– + â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y + zâ€– * â€–(I : ğ•œ) â€¢ y + zâ€– + â€–zâ€– * â€–zâ€–) := by
  convert parallelogram_identity ((I : ğ•œ) â€¢ y + z) z using 4 <;> module

private theorem add_left_aux8 (y z : E) :
    â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– + â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y - zâ€– * â€–(I : ğ•œ) â€¢ y - zâ€– + â€–zâ€– * â€–zâ€–) := by
  convert parallelogram_identity ((I : ğ•œ) â€¢ y - z) z using 4 <;> module

variable {ğ•œ}

theorem add_left (x y z : E) : inner_ ğ•œ (x + y) z = inner_ ğ•œ x z + inner_ ğ•œ y z := by
  have H_re := congr(- $(add_left_aux1 x y z) + $(add_left_aux2 x y z)
    + $(add_left_aux3 y z) - $(add_left_aux4 y z))
  have H_im := congr(- $(add_left_aux5 ğ•œ x y z) + $(add_left_aux6 ğ•œ x y z)
      + $(add_left_aux7 ğ•œ y z) - $(add_left_aux8 ğ•œ y z))
  have H := congr(ğ“š $H_re + I * ğ“š $H_im)
  simp only [inner_, map_add, map_sub, map_neg, map_mul, map_ofNat] at H âŠ¢
  linear_combination H / 8

private theorem rat_prop (r : â„š) : innerProp' E (r : ğ•œ) := by
  intro x y
  let hom : ğ•œ â†’â‚—[â„š] ğ•œ := AddMonoidHom.toRatLinearMap <|
    AddMonoidHom.mk' (fun r â†¦ inner_ ğ•œ (r â€¢ x) y) <| fun a b â†¦ by
      simpa [add_smul] using add_left (a â€¢ x) (b â€¢ x) y
  simpa [hom, Rat.smul_def] using map_smul hom r 1

private theorem real_prop (r : â„) : innerProp' E (r : ğ•œ) := by
  intro x y
  revert r
  rw [â† funext_iff]
  refine Rat.isDenseEmbedding_coe_real.dense.equalizer ?_ ?_ (funext fun X => ?_)
  Â· exact (continuous_ofReal.smul continuous_const).inner_ continuous_const
  Â· exact (continuous_conj.comp continuous_ofReal).mul continuous_const
  Â· simp only [Function.comp_apply, RCLike.ofReal_ratCast, rat_prop _ _]

private theorem I_prop : innerProp' E (I : ğ•œ) := by
  by_cases hI : (I : ğ•œ) = 0
  Â· rw [hI]
    simpa using real_prop (ğ•œ := ğ•œ) 0
  intro x y
  have hI' := I_mul_I_of_nonzero hI
  rw [conj_I, inner_, inner_, mul_left_comm, smul_smul, hI', neg_one_smul]
  have hâ‚ : â€–-x - yâ€– = â€–x + yâ€– := by rw [â† neg_add', norm_neg]
  have hâ‚‚ : â€–-x + yâ€– = â€–x - yâ€– := by rw [â† neg_sub, norm_neg, sub_eq_neg_add]
  rw [hâ‚, hâ‚‚]
  linear_combination (- ğ“š â€–(I : ğ•œ) â€¢ x - yâ€– ^ 2 + ğ“š â€–(I : ğ•œ) â€¢ x + yâ€– ^ 2) * hI' / 4

theorem innerProp (r : ğ•œ) : innerProp' E r := by
  intro x y
  rw [â† re_add_im r, add_smul, add_left, real_prop _ x, â† smul_smul, real_prop _ _ y, I_prop,
    map_add, map_mul, conj_ofReal, conj_ofReal, conj_I]
  ring

end InnerProductSpaceable

open InnerProductSpaceable

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. -/
noncomputable def InnerProductSpace.ofNorm
    (h : âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–)) :
    InnerProductSpace ğ•œ E :=
  haveI : InnerProductSpaceable E := âŸ¨hâŸ©
  { inner := inner_ ğ•œ
    norm_sq_eq_re_inner := inner_.norm_sq
    conj_inner_symm := inner_.conj_symm
    add_left := InnerProductSpaceable.add_left
    smul_left := fun _ _ _ => innerProp _ _ _ }

variable (E)
variable [InnerProductSpaceable E]

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. Do
`casesI nonempty_innerProductSpace ğ•œ E` to locally upgrade `InnerProductSpaceable E` to
`InnerProductSpace ğ•œ E`. -/
theorem nonempty_innerProductSpace : Nonempty (InnerProductSpace ğ•œ E) :=
  âŸ¨{  inner := inner_ ğ•œ
      norm_sq_eq_re_inner := inner_.norm_sq
      conj_inner_symm := inner_.conj_symm
      add_left := add_left
      smul_left := fun _ _ _ => innerProp _ _ _ }âŸ©

variable {ğ•œ E}
variable [NormedSpace â„ E]

-- TODO: Replace `InnerProductSpace.toUniformConvexSpace`
-- See note [lower instance priority]
instance (priority := 100) InnerProductSpaceable.to_uniformConvexSpace : UniformConvexSpace E := by
  cases nonempty_innerProductSpace â„ E; infer_instance
