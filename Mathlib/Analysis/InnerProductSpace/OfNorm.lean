/-
Copyright (c) 2020 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Topology.Algebra.Algebra
import Mathlib.Analysis.InnerProductSpace.Basic

#align_import analysis.inner_product_space.of_norm from "leanprover-community/mathlib"@"baa88307f3e699fa7054ef04ec79fa4f056169cb"

/-!
# Inner product space derived from a norm

This file defines an `InnerProductSpace` instance from a norm that respects the
parallellogram identity. The parallelogram identity is a way to express the inner product of `x` and
`y` in terms of the norms of `x`, `y`, `x + y`, `x - y`.

## Main results

- `InnerProductSpace.ofNorm`: a normed space whose norm respects the parallellogram identity,
  can be seen as an inner product space.

## Implementation notes

We define `inner_`

$$\langle x, y \rangle := \frac{1}{4} (â€–x + yâ€–^2 - â€–x - yâ€–^2 + i â€–ix + yâ€– ^ 2 - i â€–ix - yâ€–^2)$$

and use the parallelogram identity

$$â€–x + yâ€–^2 + â€–x - yâ€–^2 = 2 (â€–xâ€–^2 + â€–yâ€–^2)$$

to prove it is an inner product, i.e., that it is conjugate-symmetric (`inner_.conj_symm`) and
linear in the first argument. `add_left` is proved by judicious application of the parallelogram
identity followed by tedious arithmetic. `smul_left` is proved step by step, first noting that
$\langle Î» x, y \rangle = Î» \langle x, y \rangle$ for $Î» âˆˆ â„•$, $Î» = -1$, hence $Î» âˆˆ â„¤$ and $Î» âˆˆ â„š$
by arithmetic. Then by continuity and the fact that â„š is dense in â„, the same is true for â„.
The case of â„‚ then follows by applying the result for â„ and more arithmetic.

## TODO

Move upstream to `Analysis.InnerProductSpace.Basic`.

## References

- [Jordan, P. and von Neumann, J., *On inner products in linear, metric spaces*][Jordan1935]
- https://math.stackexchange.com/questions/21792/norms-induced-by-inner-products-and-the-parallelogram-law
- https://math.dartmouth.edu/archive/m113w10/public_html/jordan-vneumann-thm.pdf

## Tags

inner product space, Hilbert space, norm
-/


open RCLike

open scoped ComplexConjugate

variable {ğ•œ : Type*} [RCLike ğ•œ] (E : Type*) [NormedAddCommGroup E]

/-- Predicate for the parallelogram identity to hold in a normed group. This is a scalar-less
version of `InnerProductSpace`. If you have an `InnerProductSpaceable` assumption, you can
locally upgrade that to `InnerProductSpace ğ•œ E` using `casesI nonempty_innerProductSpace ğ•œ E`.
-/
class InnerProductSpaceable : Prop where
  parallelogram_identity :
    âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–)
#align inner_product_spaceable InnerProductSpaceable

variable (ğ•œ) {E}

theorem InnerProductSpace.toInnerProductSpaceable [InnerProductSpace ğ•œ E] :
    InnerProductSpaceable E :=
  âŸ¨parallelogram_law_with_norm ğ•œâŸ©
#align inner_product_space.to_inner_product_spaceable InnerProductSpace.toInnerProductSpaceable

-- See note [lower instance priority]
instance (priority := 100) InnerProductSpace.toInnerProductSpaceable_ofReal
    [InnerProductSpace â„ E] : InnerProductSpaceable E :=
  âŸ¨parallelogram_law_with_norm â„âŸ©
#align inner_product_space.to_inner_product_spaceable_of_real InnerProductSpace.toInnerProductSpaceable_ofReal

variable [NormedSpace ğ•œ E]

local notation "ğ“š" => algebraMap â„ ğ•œ

/-- Auxiliary definition of the inner product derived from the norm. -/
private noncomputable def inner_ (x y : E) : ğ•œ :=
  4â»Â¹ * (ğ“š â€–x + yâ€– * ğ“š â€–x + yâ€– - ğ“š â€–x - yâ€– * ğ“š â€–x - yâ€– +
    (I : ğ•œ) * ğ“š â€–(I : ğ•œ) â€¢ x + yâ€– * ğ“š â€–(I : ğ•œ) â€¢ x + yâ€– -
    (I : ğ•œ) * ğ“š â€–(I : ğ•œ) â€¢ x - yâ€– * ğ“š â€–(I : ğ•œ) â€¢ x - yâ€–)

namespace InnerProductSpaceable

variable {ğ•œ} (E)

-- Porting note: prime added to avoid clashing with public `innerProp`
/-- Auxiliary definition for the `add_left` property. -/
private def innerProp' (r : ğ•œ) : Prop :=
  âˆ€ x y : E, inner_ ğ•œ (r â€¢ x) y = conj r * inner_ ğ•œ x y

variable {E}

theorem innerProp_neg_one : innerProp' E ((-1 : â„¤) : ğ•œ) := by
  intro x y
  simp only [inner_, neg_mul_eq_neg_mul, one_mul, Int.cast_one, one_smul, RingHom.map_one, map_neg,
    Int.cast_neg, neg_smul, neg_one_mul]
  rw [neg_mul_comm]
  congr 1
  have hâ‚ : â€–-x - yâ€– = â€–x + yâ€– := by rw [â† neg_add', norm_neg]
  have hâ‚‚ : â€–-x + yâ€– = â€–x - yâ€– := by rw [â† neg_sub, norm_neg, sub_eq_neg_add]
  have hâ‚ƒ : â€–(I : ğ•œ) â€¢ -x + yâ€– = â€–(I : ğ•œ) â€¢ x - yâ€– := by
    rw [â† neg_sub, norm_neg, sub_eq_neg_add, â† smul_neg]
  have hâ‚„ : â€–(I : ğ•œ) â€¢ -x - yâ€– = â€–(I : ğ•œ) â€¢ x + yâ€– := by rw [smul_neg, â† neg_add', norm_neg]
  rw [hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„]
  ring
#align inner_product_spaceable.inner_prop_neg_one InnerProductSpaceable.innerProp_neg_one

theorem _root_.Continuous.inner_ {f g : â„ â†’ E} (hf : Continuous f) (hg : Continuous g) :
    Continuous fun x => inner_ ğ•œ (f x) (g x) := by
  unfold inner_
  have := Continuous.const_smul (M := ğ•œ) hf I
  continuity
#align inner_product_spaceable.continuous.inner_ Continuous.inner_

theorem inner_.norm_sq (x : E) : â€–xâ€– ^ 2 = re (inner_ ğ•œ x x) := by
  simp only [inner_]
  have hâ‚ : RCLike.normSq (4 : ğ•œ) = 16 := by
    have : ((4 : â„) : ğ•œ) = (4 : ğ•œ) := by norm_cast
    rw [â† this, normSq_eq_def', RCLike.norm_of_nonneg (by norm_num : (0 : â„) â‰¤ 4)]
    norm_num
  have hâ‚‚ : â€–x + xâ€– = 2 * â€–xâ€– := by rw [â† two_smul ğ•œ, norm_smul, RCLike.norm_two]
  simp only [hâ‚, hâ‚‚, algebraMap_eq_ofReal, sub_self, norm_zero, mul_re, inv_re, ofNat_re, map_sub,
    map_add, ofReal_re, ofNat_im, ofReal_im, mul_im, I_re, inv_im]
  ring
#align inner_product_spaceable.inner_.norm_sq InnerProductSpaceable.inner_.norm_sq

theorem inner_.conj_symm (x y : E) : conj (inner_ ğ•œ y x) = inner_ ğ•œ x y := by
  simp only [inner_]
  have h4 : conj (4â»Â¹ : ğ•œ) = 4â»Â¹ := by norm_num
  rw [map_mul, h4]
  congr 1
  simp only [map_sub, map_add, algebraMap_eq_ofReal, â† ofReal_mul, conj_ofReal, map_mul, conj_I]
  rw [add_comm y x, norm_sub_rev]
  by_cases hI : (I : ğ•œ) = 0
  Â· simp only [hI, neg_zero, zero_mul]
  -- Porting note: this replaces `norm_I_of_ne_zero` which does not exist in Lean 4
  have : â€–(I : ğ•œ)â€– = 1 := by
    rw [â† mul_self_inj_of_nonneg (norm_nonneg I) zero_le_one, one_mul, â† norm_mul,
      I_mul_I_of_nonzero hI, norm_neg, norm_one]
  have hâ‚ : â€–(I : ğ•œ) â€¢ y - xâ€– = â€–(I : ğ•œ) â€¢ x + yâ€– := by
    trans â€–(I : ğ•œ) â€¢ ((I : ğ•œ) â€¢ y - x)â€–
    Â· rw [norm_smul, this, one_mul]
    Â· rw [smul_sub, smul_smul, I_mul_I_of_nonzero hI, neg_one_smul, â† neg_add', add_comm, norm_neg]
  have hâ‚‚ : â€–(I : ğ•œ) â€¢ y + xâ€– = â€–(I : ğ•œ) â€¢ x - yâ€– := by
    trans â€–(I : ğ•œ) â€¢ ((I : ğ•œ) â€¢ y + x)â€–
    Â· rw [norm_smul, this, one_mul]
    Â· rw [smul_add, smul_smul, I_mul_I_of_nonzero hI, neg_one_smul, â† neg_add_eq_sub]
  rw [hâ‚, hâ‚‚, â† sub_add_eq_add_sub]
  simp only [neg_mul, sub_eq_add_neg, neg_neg]
#align inner_product_spaceable.inner_.conj_symm InnerProductSpaceable.inner_.conj_symm

variable [InnerProductSpaceable E]

private theorem add_left_aux1 (x y z : E) : â€–x + y + zâ€– * â€–x + y + zâ€– =
    (â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€–) / 2 - â€–x - zâ€– * â€–x - zâ€– := by
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm]
  convert parallelogram_identity (x + y + z) (x - z) using 4 <;> Â· rw [two_smul]; abel

private theorem add_left_aux2 (x y z : E) : â€–x + y - zâ€– * â€–x + y - zâ€– =
    (â€–2 â€¢ x + yâ€– * â€–2 â€¢ x + yâ€– + â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 - â€–x + zâ€– * â€–x + zâ€– := by
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm]
  have hâ‚€ := parallelogram_identity (x + y - z) (x + z)
  convert hâ‚€ using 4 <;> Â· rw [two_smul]; abel

private theorem add_left_aux2' (x y z : E) :
    â€–x + y + zâ€– * â€–x + y + zâ€– - â€–x + y - zâ€– * â€–x + y - zâ€– =
    â€–x + zâ€– * â€–x + zâ€– - â€–x - zâ€– * â€–x - zâ€– +
    (â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– - â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 := by
  rw [add_left_aux1, add_left_aux2]; ring

private theorem add_left_aux3 (y z : E) :
    â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– = 2 * (â€–y + zâ€– * â€–y + zâ€– + â€–zâ€– * â€–zâ€–) - â€–yâ€– * â€–yâ€– := by
  apply eq_sub_of_add_eq
  convert parallelogram_identity (y + z) z using 4 <;> (try rw [two_smul]) <;> abel

private theorem add_left_aux4 (y z : E) :
    â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€– = 2 * (â€–y - zâ€– * â€–y - zâ€– + â€–zâ€– * â€–zâ€–) - â€–yâ€– * â€–yâ€– := by
  apply eq_sub_of_add_eq'
  have hâ‚€ := parallelogram_identity (y - z) z
  convert hâ‚€ using 4 <;> (try rw [two_smul]) <;> abel

private theorem add_left_aux4' (y z : E) :
    (â€–2 â€¢ z + yâ€– * â€–2 â€¢ z + yâ€– - â€–y - 2 â€¢ zâ€– * â€–y - 2 â€¢ zâ€–) / 2 =
    â€–y + zâ€– * â€–y + zâ€– - â€–y - zâ€– * â€–y - zâ€– := by
  rw [add_left_aux3, add_left_aux4]; ring

private theorem add_left_aux5 (x y z : E) :
    â€–(I : ğ•œ) â€¢ (x + y) + zâ€– * â€–(I : ğ•œ) â€¢ (x + y) + zâ€– =
    (â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– +
    â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€–) / 2 -
    â€–(I : ğ•œ) â€¢ x - zâ€– * â€–(I : ğ•œ) â€¢ x - zâ€– := by
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm]
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ (x + y) + z) ((I : ğ•œ) â€¢ x - z)
  convert hâ‚€ using 4 <;> Â· try simp only [two_smul, smul_add]; abel

private theorem add_left_aux6 (x y z : E) :
    â€–(I : ğ•œ) â€¢ (x + y) - zâ€– * â€–(I : ğ•œ) â€¢ (x + y) - zâ€– =
    (â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– * â€–(I : ğ•œ) â€¢ (2 â€¢ x + y)â€– +
    â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€–) / 2 -
    â€–(I : ğ•œ) â€¢ x + zâ€– * â€–(I : ğ•œ) â€¢ x + zâ€– := by
  rw [eq_sub_iff_add_eq, eq_div_iff (two_ne_zero' â„), mul_comm _ (2 : â„), eq_comm]
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ (x + y) - z) ((I : ğ•œ) â€¢ x + z)
  convert hâ‚€ using 4 <;> Â· try simp only [two_smul, smul_add]; abel

private theorem add_left_aux7 (y z : E) :
    â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y + 2 â€¢ zâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y + zâ€– * â€–(I : ğ•œ) â€¢ y + zâ€– + â€–zâ€– * â€–zâ€–) - â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– := by
  apply eq_sub_of_add_eq
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ y + z) z
  convert hâ‚€ using 4 <;> Â· (try simp only [two_smul, smul_add]); abel

private theorem add_left_aux8 (y z : E) :
    â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– * â€–(I : ğ•œ) â€¢ y - 2 â€¢ zâ€– =
    2 * (â€–(I : ğ•œ) â€¢ y - zâ€– * â€–(I : ğ•œ) â€¢ y - zâ€– + â€–zâ€– * â€–zâ€–) - â€–(I : ğ•œ) â€¢ yâ€– * â€–(I : ğ•œ) â€¢ yâ€– := by
  apply eq_sub_of_add_eq'
  have hâ‚€ := parallelogram_identity ((I : ğ•œ) â€¢ y - z) z
  convert hâ‚€ using 4 <;> Â· (try simp only [two_smul, smul_add]); abel

theorem add_left (x y z : E) : inner_ ğ•œ (x + y) z = inner_ ğ•œ x z + inner_ ğ•œ y z := by
  simp only [inner_, â† mul_add]
  congr
  simp only [mul_assoc, â† map_mul, add_sub_assoc, â† mul_sub, â† map_sub]
  rw [add_add_add_comm]
  simp only [â† map_add, â† mul_add]
  congr
  Â· rw [â† add_sub_assoc, add_left_aux2', add_left_aux4']
  Â· rw [add_left_aux5, add_left_aux6, add_left_aux7, add_left_aux8]
    simp only [map_sub, map_mul, map_add, div_eq_mul_inv]
    ring
#align inner_product_spaceable.add_left InnerProductSpaceable.add_left

theorem nat (n : â„•) (x y : E) : inner_ ğ•œ ((n : ğ•œ) â€¢ x) y = (n : ğ•œ) * inner_ ğ•œ x y := by
  induction' n with n ih
  Â· simp only [inner_, Nat.zero_eq, zero_sub, Nat.cast_zero, zero_mul,
      eq_self_iff_true, zero_smul, zero_add, mul_zero, sub_self, norm_neg, smul_zero]
  Â· simp only [Nat.cast_succ, add_smul, one_smul]
    rw [add_left, ih, add_mul, one_mul]
#align inner_product_spaceable.nat InnerProductSpaceable.nat

private theorem nat_prop (r : â„•) : innerProp' E (r : ğ•œ) := fun x y => by
  simp only [map_natCast]; exact nat r x y

private theorem int_prop (n : â„¤) : innerProp' E (n : ğ•œ) := by
  intro x y
  rw [â† n.sign_mul_natAbs]
  simp only [Int.cast_natCast, map_natCast, map_intCast, Int.cast_mul, map_mul, mul_smul]
  obtain hn | rfl | hn := lt_trichotomy n 0
  Â· rw [Int.sign_eq_neg_one_of_neg hn, innerProp_neg_one ((n.natAbs : ğ•œ) â€¢ x), nat]
    simp only [map_neg, neg_mul, one_mul, mul_eq_mul_left_iff, true_or_iff, Int.natAbs_eq_zero,
      eq_self_iff_true, Int.cast_one, map_one, neg_inj, Nat.cast_eq_zero, Int.cast_neg]
  Â· simp only [inner_, Int.cast_zero, zero_sub, Nat.cast_zero, zero_mul,
      eq_self_iff_true, Int.sign_zero, zero_smul, zero_add, mul_zero, smul_zero,
      sub_self, norm_neg, Int.natAbs_zero]
  Â· rw [Int.sign_eq_one_of_pos hn]
    simp only [one_mul, mul_eq_mul_left_iff, true_or_iff, Int.natAbs_eq_zero, eq_self_iff_true,
      Int.cast_one, one_smul, Nat.cast_eq_zero, nat]

private theorem rat_prop (r : â„š) : innerProp' E (r : ğ•œ) := by
  intro x y
  have : (r.den : ğ•œ) â‰  0 := by
    haveI : CharZero ğ•œ := RCLike.charZero_rclike
    exact mod_cast r.pos.ne'
  rw [â† r.num_div_den, â† mul_right_inj' this, â† nat r.den _ y, smul_smul, Rat.cast_div]
  simp only [map_natCast, Rat.cast_natCast, map_intCast, Rat.cast_intCast, map_divâ‚€]
  rw [â† mul_assoc, mul_div_cancelâ‚€ _ this, int_prop _ x, map_intCast]

private theorem real_prop (r : â„) : innerProp' E (r : ğ•œ) := by
  intro x y
  revert r
  rw [â† Function.funext_iff]
  refine Rat.denseEmbedding_coe_real.dense.equalizer ?_ ?_ (funext fun X => ?_)
  Â· exact (continuous_ofReal.smul continuous_const).inner_ continuous_const
  Â· exact (continuous_conj.comp continuous_ofReal).mul continuous_const
  Â· simp only [Function.comp_apply, RCLike.ofReal_ratCast, rat_prop _ _]

private theorem I_prop : innerProp' E (I : ğ•œ) := by
  by_cases hI : (I : ğ•œ) = 0
  Â· rw [hI, â† Nat.cast_zero]; exact nat_prop _
  intro x y
  have hI' : (-I : ğ•œ) * I = 1 := by rw [â† inv_I, inv_mul_cancel hI]
  rw [conj_I, inner_, inner_, mul_left_comm]
  congr 1
  rw [smul_smul, I_mul_I_of_nonzero hI, neg_one_smul]
  rw [mul_sub, mul_add, mul_sub, mul_assoc I (ğ“š â€–I â€¢ x - yâ€–), â† mul_assoc (-I) I, hI', one_mul,
    mul_assoc I (ğ“š â€–I â€¢ x + yâ€–), â† mul_assoc (-I) I, hI', one_mul]
  have hâ‚ : â€–-x - yâ€– = â€–x + yâ€– := by rw [â† neg_add', norm_neg]
  have hâ‚‚ : â€–-x + yâ€– = â€–x - yâ€– := by rw [â† neg_sub, norm_neg, sub_eq_neg_add]
  rw [hâ‚, hâ‚‚]
  simp only [sub_eq_add_neg, mul_assoc]
  rw [â† neg_mul_eq_neg_mul, â† neg_mul_eq_neg_mul]
  abel

theorem innerProp (r : ğ•œ) : innerProp' E r := by
  intro x y
  rw [â† re_add_im r, add_smul, add_left, real_prop _ x, â† smul_smul, real_prop _ _ y, I_prop,
    map_add, map_mul, conj_ofReal, conj_ofReal, conj_I]
  ring
#align inner_product_spaceable.inner_prop InnerProductSpaceable.innerProp

end InnerProductSpaceable

open InnerProductSpaceable

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. -/
noncomputable def InnerProductSpace.ofNorm
    (h : âˆ€ x y : E, â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–)) :
    InnerProductSpace ğ•œ E :=
  haveI : InnerProductSpaceable E := âŸ¨hâŸ©
  { inner := inner_ ğ•œ
    norm_sq_eq_inner := inner_.norm_sq
    conj_symm := inner_.conj_symm
    add_left := InnerProductSpaceable.add_left
    smul_left := fun _ _ _ => innerProp _ _ _ }
#align inner_product_space.of_norm InnerProductSpace.ofNorm

variable (E)
variable [InnerProductSpaceable E]

/-- **FrÃ©chetâ€“von Neumannâ€“Jordan Theorem**. A normed space `E` whose norm satisfies the
parallelogram identity can be given a compatible inner product. Do
`casesI nonempty_innerProductSpace ğ•œ E` to locally upgrade `InnerProductSpaceable E` to
`InnerProductSpace ğ•œ E`. -/
theorem nonempty_innerProductSpace : Nonempty (InnerProductSpace ğ•œ E) :=
  âŸ¨{  inner := inner_ ğ•œ
      norm_sq_eq_inner := inner_.norm_sq
      conj_symm := inner_.conj_symm
      add_left := add_left
      smul_left := fun _ _ _ => innerProp _ _ _ }âŸ©
#align nonempty_inner_product_space nonempty_innerProductSpace

variable {ğ•œ E}
variable [NormedSpace â„ E]

-- TODO: Replace `InnerProductSpace.toUniformConvexSpace`
-- See note [lower instance priority]
instance (priority := 100) InnerProductSpaceable.to_uniformConvexSpace : UniformConvexSpace E := by
  cases nonempty_innerProductSpace â„ E; infer_instance
#align inner_product_spaceable.to_uniform_convex_space InnerProductSpaceable.to_uniformConvexSpace
