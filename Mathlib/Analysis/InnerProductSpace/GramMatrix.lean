/-
Copyright (c) 2025 Peter Pfaffelhuber. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Pfaffelhuber
-/

import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # Gram Matrices

This file defines Gram matrices and proves their positive semi-definiteness.
Results require `RCLike ğ•œ`.

## Main definition

* `Matrix.Gram` : the `Matrix n n ğ•œ` with `âŸªv i, v jâŸ«` at `i j : n`, where `v : n â†’ Î±` for an
`InnerProductSpace ğ•œ Î±`.

## Main results

* `Matrix.IsGram.PosSemidef` Gram matrices are positive semi-definite.
-/

open RCLike Real Matrix

open scoped InnerProductSpace

open scoped ComplexOrder

variable {E n : Type*}
variable {Î± : Type*}
variable {ğ•œ : Type*} [RCLike ğ•œ]
variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

namespace Matrix

/-- The entries of a Gram matrix are inner products of vectors in an inner product space. -/
def gram (ğ•œ : Type*) [RCLike ğ•œ] [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
    (v : n â†’ E) : Matrix n n ğ•œ := fun i j â†¦ inner ğ•œ (v i) (v j)

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ  _ _ x y

/-- A `M : Matrix n n ğ•œ` is a Gram matrix if `M = Gram ğ•œ v` for some `v : n â†’ E`. -/
def IsGram (M : Matrix n n ğ•œ) : Prop := âˆƒ (v : n â†’ E), (M = gram ğ•œ v)

namespace IsGram

/-- For the matrix `gram ğ•œ v`, the entry at `i j : n` equals `âŸªv i, v jâŸ«`. -/
lemma entry {v : n â†’ E} (i j : n) : (gram _ v) i j = âŸªv i, v jâŸ« := by
  rw [gram]

/-- A Gram matrix is Hermitian. -/
lemma IsHermitian {v : n â†’ E} : (gram ğ•œ v).IsHermitian := by
  refine IsHermitian.ext_iff.mpr (fun i j â†¦ ?_)
  rw [gram, gram]
  simp only [RCLike.star_def, inner_conj_symm]

/-- A Gram matrix is positive semidefinite. -/
theorem PosSemidef [Fintype n] {v : n â†’ E} :
    PosSemidef (gram ğ•œ v) := by
  refine âŸ¨IsHermitian, fun x â†¦ ?_âŸ©
  let y := âˆ‘ (i : n), x i â€¢ v i
  have h : âŸªy, yâŸ« = star x â¬áµ¥ (gram ğ•œ v) *áµ¥ x := by
    simp [y]
    calc
      âŸªy, yâŸ« = (âˆ‘ (i : n), âˆ‘ (j : n), (starRingEnd ğ•œ) (x i) * (x j) * âŸªv i, v jâŸ«) := by
          simp_rw [y, sum_inner, inner_sum, inner_smul_left, inner_smul_right, mul_assoc]
        _ = (âˆ‘ (i : n), âˆ‘ (j : n), (starRingEnd ğ•œ) (x i) * (x j) * ((gram ğ•œ v) i j)) := by
          simp_rw [entry]
        _ = star x â¬áµ¥ (gram ğ•œ v) *áµ¥ x := by
          simp_rw [dotProduct, mul_assoc, â† Finset.mul_sum, mulVec, dotProduct,
            mul_comm, â† star_def]
          rfl
  rw [â† h, le_iff_re_im]
  refine âŸ¨?_, ?_âŸ©
  Â· simp only [map_zero]
    exact inner_self_nonneg
  Â· simp only [map_zero, inner_self_im, y]

end IsGram

end Matrix
