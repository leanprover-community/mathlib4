/-
Copyright (c) 2025 Peter Pfaffelhuber. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Pfaffelhuber
-/

import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # Gram Matrices

This file defines Gram matrices and proves their positive semi-definiteness.
Results require `RCLike ğ•œ`.

## Main definition

* `Matrix.gram` : the `Matrix n n ğ•œ` with `âŸªv i, v jâŸ«` at `i j : n`, where `v : n â†’ E` for an
  `Inner ğ•œ E`.

## Main results

* `Matrix.posSemidef_gram` Gram matrices are positive semi-definite.
* `Matrix.posDef_gram_iff_linearIndependent` Linear independence of `v` is
  equivalent to positive definiteness of `gram ğ•œ v`.
-/

open RCLike Real Matrix

open scoped InnerProductSpace ComplexOrder ComplexConjugate

variable {E n : Type*}
variable {Î± : Type*}
variable {ğ•œ : Type*}
namespace Matrix

/-- The entries of a Gram matrix are inner products of vectors in an inner product space. -/
def gram (ğ•œ : Type*) [Inner ğ•œ E] (v : n â†’ E) : Matrix n n ğ•œ := of fun i j â†¦ âŸªv i, v jâŸ«_ğ•œ

@[simp]
lemma gram_apply [Inner ğ•œ E] (v : n â†’ E) (i j : n) :
    (gram _ v) i j = âŸªv i, v jâŸ«_ğ•œ := rfl

variable [RCLike ğ•œ]

section SemiInnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

@[simp]
lemma gram_zero : gram ğ•œ (0 : n â†’ E) = 0 := Matrix.ext fun _ _ â†¦ inner_zero_left _

@[simp]
lemma gram_single [DecidableEq n] (i : n) (x : E) :
    gram ğ•œ (Pi.single i x) = Matrix.single i i âŸªx, xâŸ«_ğ•œ := by
  ext j k
  obtain hij | rfl := ne_or_eq i j
  Â· simp [hij]
  obtain hik | rfl := ne_or_eq i k
  Â· simp [hik]
  simp

variable (ğ•œ) in
/-- A Gram matrix is Hermitian. -/
lemma isHermitian_gram (v : n â†’ E) : (gram ğ•œ v).IsHermitian :=
  Matrix.ext fun _ _ â†¦ inner_conj_symm _ _

theorem star_dotProduct_gram_mulVec [Fintype n] {v : n â†’ E} (x : n â†’ ğ•œ) :
    star x â¬áµ¥ (gram ğ•œ v) *áµ¥ x = âŸªâˆ‘ i, x i â€¢ v i, âˆ‘ i, x i â€¢ v iâŸ«_ğ•œ := by
  trans âˆ‘ i, âˆ‘ j, conj (x i) * x j * âŸªv i, v jâŸ«_ğ•œ
  Â· simp_rw [dotProduct, mul_assoc, â† Finset.mul_sum, mulVec, dotProduct, mul_comm, â† star_def,
      gram_apply, Pi.star_apply]
  Â· simp_rw [sum_inner, inner_sum, inner_smul_left, inner_smul_right, mul_assoc]

variable (ğ•œ) in
/-- A Gram matrix is positive semidefinite. -/
theorem posSemidef_gram [Fintype n] (v : n â†’ E) :
    PosSemidef (gram ğ•œ v) := by
  refine âŸ¨isHermitian_gram _ _, fun x â†¦ ?_âŸ©
  rw [star_dotProduct_gram_mulVec, le_iff_re_im]
  simp only [map_zero, inner_self_im, and_true, inner_self_nonneg]

/-- In a seminormed space, positive definiteness of `gram ğ•œ v` implies linear independence of `v` -/
theorem linearIndependent_of_posDef_gram [Fintype n] {v : n â†’ E} (h_gram : PosDef (gram ğ•œ v)) :
    LinearIndependent ğ•œ v := by
  rw [Fintype.linearIndependent_iff]
  intro y hy
  obtain âŸ¨h1, h2âŸ© := h_gram
  specialize h2 y
  rw [star_dotProduct_gram_mulVec, â† not_imp_not, ne_eq, Decidable.not_not, RCLike.pos_iff,
    â† norm_sq_eq_re_inner, hy] at h2
  apply funext_iff.mp
  apply h2
  simp

end SemiInnerProductSpace

section NormedInnerProductSpace

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

/-- In a normed space, linear independence of `v` implies positive definiteness of `gram ğ•œ v`. -/
theorem posDef_gram_of_linearIndependent  [Fintype n]
    {v : n â†’ E} (h_li : LinearIndependent ğ•œ v) : PosDef (gram ğ•œ v) := by
  rw [Fintype.linearIndependent_iff] at h_li
  obtain âŸ¨h0, h1âŸ© := posSemidef_gram (ğ•œ := ğ•œ) (v := v)
  refine âŸ¨h0, fun x hx â†¦ (h1 x).lt_of_ne' ?_âŸ©
  specialize h_li x
  rw [â† funext_iff, â† not_imp_not] at h_li
  simp_rw [star_dotProduct_gram_mulVec]
  intro hz
  apply h_li hx
  rw [â† norm_eq_zero]
  have j1 := congrArg re hz
  rwa [map_zero, â† norm_sq_eq_re_inner, sq_eq_zero_iff] at j1

/-- In a normed space, linear independence of `v` is equivalent to positive definiteness of
`gram ğ•œ v`. -/
theorem posDef_gram_iff_linearIndependent [Fintype n] {v : n â†’ E} :
    PosDef (gram ğ•œ v) â†” LinearIndependent ğ•œ v :=
  âŸ¨linearIndependent_of_posDef_gram, posDef_gram_of_linearIndependentâŸ©

end NormedInnerProductSpace

end Matrix
