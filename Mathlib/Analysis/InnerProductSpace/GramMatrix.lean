/-
Copyright (c) 2025 Peter Pfaffelhuber. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Peter Pfaffelhuber
-/

import Mathlib.LinearAlgebra.Matrix.PosDef

/-! # Gram Matrices

This file defines Gram matrices and proves their positive semi-definiteness.
Results require `RCLike ğ•œ`.

## Main definition

* `Matrix.gram` : the `Matrix n n ğ•œ` with `âŸªv i, v jâŸ«` at `i j : n`, where `v : n â†’ E` for an
  `Inner ğ•œ E`.

## Main results

* `Matrix.gram_posSemidef` Gram matrices are positive semi-definite.
-/

open RCLike Real Matrix

open scoped InnerProductSpace

open scoped ComplexOrder

variable {E n : Type*}
variable {Î± : Type*}
variable {ğ•œ : Type*}
namespace Matrix

/-- The entries of a Gram matrix are inner products of vectors in an inner product space. -/
def gram (ğ•œ : Type*) [Inner ğ•œ E] (v : n â†’ E) : Matrix n n ğ•œ := of fun i j â†¦ inner ğ•œ (v i) (v j)

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ  _ _ x y

lemma gram_apply [Inner ğ•œ E] {v : n â†’ E} (i j : n) :
    (gram _ v) i j = âŸªv i, v jâŸ« := by
  rfl

variable [RCLike ğ•œ]

/-- A Gram matrix is Hermitian. -/
lemma isHermitian_gram
[SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E] {v : n â†’ E} : (gram ğ•œ v).IsHermitian := by
  refine IsHermitian.ext_iff.mpr (fun i j â†¦ ?_)
  rw [gram, of_apply, of_apply]
  simp only [RCLike.star_def, inner_conj_symm]

section SemiInnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

theorem star_dotProduct_gram_mulVec [Fintype n] {v : n â†’ E} (x : n â†’ ğ•œ) :
    star x â¬áµ¥ (gram ğ•œ v) *áµ¥ x = âŸªâˆ‘ i, x i â€¢ v i, âˆ‘ i, x i â€¢ v iâŸ« := by
  calc
    star x â¬áµ¥ (gram ğ•œ v) *áµ¥ x =
    (âˆ‘ (i : n), âˆ‘ (j : n), (starRingEnd ğ•œ) (x i) * (x j) * ((gram ğ•œ v) i j)) := by
      simp_rw [dotProduct, mul_assoc, â† Finset.mul_sum, mulVec, dotProduct, mul_comm, â† star_def]
      rfl
    _ = (âˆ‘ (i : n), âˆ‘ (j : n), (starRingEnd ğ•œ) (x i) * (x j) * âŸªv i, v jâŸ«) := by
      simp_rw [gram_apply]
    _ = âŸªâˆ‘ i, x i â€¢ v i, âˆ‘ i, x i â€¢ v iâŸ« := by
      simp_rw [sum_inner, inner_sum, inner_smul_left, inner_smul_right, mul_assoc]

/-- A Gram matrix is positive semidefinite. -/
theorem gram_posSemidef [Fintype n] {v : n â†’ E} :
    PosSemidef (gram ğ•œ v) := by
  refine âŸ¨isHermitian_gram, fun x â†¦ ?_âŸ©
  rw [star_dotProduct_gram_mulVec, le_iff_re_im]
  simp only [map_zero, inner_self_im, and_true]
  exact inner_self_nonneg

/-- In a seminormed space, positive definiteness of `gram ğ•œ v` implies linear independence of `v` -/
theorem linearIndependent_of_gram_posDef [Fintype n] {v : n â†’ E} (h_gram : PosDef (gram ğ•œ v)) :
    LinearIndependent ğ•œ v:= by
  rw [Fintype.linearIndependent_iff]
  obtain âŸ¨h0, h1âŸ© := gram_posSemidef (ğ•œ := ğ•œ) (v := v)
  intro y hy
  obtain âŸ¨h1, h2âŸ© := h_gram
  specialize h2 y
  rw [star_dotProduct_gram_mulVec, â† not_imp_not, ne_eq, Decidable.not_not, RCLike.pos_iff,
    â† norm_sq_eq_re_inner] at h2
  apply funext_iff.mp
  apply h2
  simp [hy]

end SemiInnerProductSpace

section NormedInnerProductSpace

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

/-- In a normed space, linear independence of `v` implies positive definiteness of `gram ğ•œ v`. -/
theorem gram_posDef_of_linearIndependent  [Fintype n]
    {v : n â†’ E} (h_li : LinearIndependent ğ•œ v) : PosDef (gram ğ•œ v) := by
  rw [Fintype.linearIndependent_iff] at h_li
  obtain âŸ¨h0, h1âŸ© := gram_posSemidef (ğ•œ := ğ•œ) (v := v)
  refine âŸ¨h0, fun x hx â†¦ lt_of_le_of_ne (h1 x) ?_âŸ©
  specialize h_li x
  rw [â† funext_iff, â† not_imp_not] at h_li
  simp_rw [star_dotProduct_gram_mulVec]
  intro j
  apply h_li hx
  rw [â† norm_eq_zero]
  obtain j1 := congrArg (â‡‘re) j
  rw [map_zero, â† norm_sq_eq_re_inner] at j1
  obtain j1' := Eq.symm j1
  rw [â† sq_eq_zero_iff.symm] at j1'
  exact j1'

/-- In a normed space, linear independence of `v` is equivalent to positive definiteness of
`gram ğ•œ v`. -/
theorem gram_posDef_iff_linearIndependent [Fintype n] {v : n â†’ E}  :
    PosDef (gram ğ•œ v) â†” LinearIndependent ğ•œ v :=
  âŸ¨linearIndependent_of_gram_posDef, gram_posDef_of_linearIndependentâŸ©

end NormedInnerProductSpace

end Matrix
