/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.InnerProductSpace.Defs

/-!
# Properties of inner product spaces

This file defines inner product spaces and proves the basic properties.  We do not formally
define Hilbert spaces, but they can be obtained using the set of assumptions
`[NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [CompleteSpace E]`.

An inner product space is a vector space endowed with an inner product. It generalizes the notion of
dot product in `â„^n` and provides the means of defining the length of a vector and the angle between
two vectors. In particular vectors `x` and `y` are orthogonal if their inner product equals zero.
We define both the real and complex cases at the same time using the `RCLike` typeclass.

This file proves general results on inner product spaces. For the specific construction of an inner
product structure on `n â†’ ğ•œ` for `ğ•œ = â„` or `â„‚`, see `EuclideanSpace` in
`Analysis.InnerProductSpace.PiL2`.

## Main results

- We define the class `InnerProductSpace ğ•œ E` extending `NormedSpace ğ•œ E` with a number of basic
  properties, most notably the Cauchy-Schwarz inequality. Here `ğ•œ` is understood to be either `â„`
  or `â„‚`, through the `RCLike` typeclass.
- We show that the inner product is continuous, `continuous_inner`, and bundle it as the
  continuous sesquilinear map `innerSL` (see also `innerâ‚›â‚—` for the non-continuous version).
- We define `Orthonormal`, a predicate on a function `v : Î¹ â†’ E`, and prove the existence of a
  maximal orthonormal set, `exists_maximal_orthonormal`.  Bessel's inequality,
  `Orthonormal.tsum_inner_products_le`, states that given an orthonormal set `v` and a vector `x`,
  the sum of the norm-squares of the inner products `âŸªv i, xâŸ«` is no more than the norm-square of
  `x`. For the existence of orthonormal bases, Hilbert bases, etc., see the file
  `Analysis.InnerProductSpace.projection`.

## Notation

We globally denote the real and complex inner products by `âŸªÂ·, Â·âŸ«_â„` and `âŸªÂ·, Â·âŸ«_â„‚` respectively.
We also provide two notation namespaces: `RealInnerProductSpace`, `ComplexInnerProductSpace`,
which respectively introduce the plain notation `âŸªÂ·, Â·âŸ«` for the real and complex inner product.

## Implementation notes

We choose the convention that inner products are conjugate linear in the first argument and linear
in the second.

## Tags

inner product space, Hilbert space, norm

## References
* [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
* [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/


noncomputable section

open RCLike Real Filter

open Topology ComplexConjugate Finsupp

open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

section BasicProperties_Seminormed

open scoped InnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [SeminormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

export InnerProductSpace (norm_sq_eq_inner)

@[simp]
theorem inner_conj_symm (x y : E) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« :=
  InnerProductSpace.conj_symm _ _

theorem real_inner_comm (x y : F) : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ :=
  @inner_conj_symm â„ _ _ _ _ x y

theorem inner_eq_zero_symm {x y : E} : âŸªx, yâŸ« = 0 â†” âŸªy, xâŸ« = 0 := by
  rw [â† inner_conj_symm]
  exact star_eq_zero

@[simp]
theorem inner_self_im (x : E) : im âŸªx, xâŸ« = 0 := by rw [â† @ofReal_inj ğ•œ, im_eq_conj_sub]; simp

theorem inner_add_left (x y z : E) : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
  InnerProductSpace.add_left _ _ _

theorem inner_add_right (x y z : E) : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« := by
  rw [â† inner_conj_symm, inner_add_left, RingHom.map_add]
  simp only [inner_conj_symm]

theorem inner_re_symm (x y : E) : re âŸªx, yâŸ« = re âŸªy, xâŸ« := by rw [â† inner_conj_symm, conj_re]

theorem inner_im_symm (x y : E) : im âŸªx, yâŸ« = -im âŸªy, xâŸ« := by rw [â† inner_conj_symm, conj_im]

section Algebra
variable {ğ• : Type*} [CommSemiring ğ•] [StarRing ğ•] [Algebra ğ• ğ•œ] [Module ğ• E]
  [IsScalarTower ğ• ğ•œ E] [StarModule ğ• ğ•œ]

/-- See `inner_smul_left` for the common special when `ğ•œ = ğ•`. -/
lemma inner_smul_left_eq_star_smul (x y : E) (r : ğ•) : âŸªr â€¢ x, yâŸ« = râ€  â€¢ âŸªx, yâŸ« := by
  rw [â† algebraMap_smul ğ•œ r, InnerProductSpace.smul_left, starRingEnd_apply, starRingEnd_apply,
    â† algebraMap_star_comm, â† smul_eq_mul, algebraMap_smul]

/-- Special case of `inner_smul_left_eq_star_smul` when the acting ring has a trivial star
(eg `â„•`, `â„¤`, `â„šâ‰¥0`, `â„š`, `â„`). -/
lemma inner_smul_left_eq_smul [TrivialStar ğ•] (x y : E) (r : ğ•) : âŸªr â€¢ x, yâŸ« = r â€¢ âŸªx, yâŸ« := by
  rw [inner_smul_left_eq_star_smul, starRingEnd_apply, star_trivial]

/-- See `inner_smul_right` for the common special when `ğ•œ = ğ•`. -/
lemma inner_smul_right_eq_smul (x y : E) (r : ğ•) : âŸªx, r â€¢ yâŸ« = r â€¢ âŸªx, yâŸ« := by
  rw [â† inner_conj_symm, inner_smul_left_eq_star_smul, starRingEnd_apply, starRingEnd_apply,
    star_smul, star_star, â† starRingEnd_apply, inner_conj_symm]

end Algebra

/-- See `inner_smul_left_eq_star_smul` for the case of a general algebra action. -/
theorem inner_smul_left (x y : E) (r : ğ•œ) : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
  inner_smul_left_eq_star_smul ..

theorem real_inner_smul_left (x y : F) (r : â„) : âŸªr â€¢ x, yâŸ«_â„ = r * âŸªx, yâŸ«_â„ :=
  inner_smul_left _ _ _

theorem inner_smul_real_left (x y : E) (r : â„) : âŸª(r : ğ•œ) â€¢ x, yâŸ« = r â€¢ âŸªx, yâŸ« := by
  rw [inner_smul_left, conj_ofReal, Algebra.smul_def]

/-- See `inner_smul_right_eq_smul` for the case of a general algebra action. -/
theorem inner_smul_right (x y : E) (r : ğ•œ) : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« :=
  inner_smul_right_eq_smul ..

theorem real_inner_smul_right (x y : F) (r : â„) : âŸªx, r â€¢ yâŸ«_â„ = r * âŸªx, yâŸ«_â„ :=
  inner_smul_right _ _ _

theorem inner_smul_real_right (x y : E) (r : â„) : âŸªx, (r : ğ•œ) â€¢ yâŸ« = r â€¢ âŸªx, yâŸ« := by
  rw [inner_smul_right, Algebra.smul_def]

/-- The inner product as a sesquilinear form.

Note that in the case `ğ•œ = â„` this is a bilinear form. -/
@[simps!]
def sesqFormOfInner : E â†’â‚—[ğ•œ] E â†’â‚—â‹†[ğ•œ] ğ•œ :=
  LinearMap.mkâ‚‚'â‚›â‚— (RingHom.id ğ•œ) (starRingEnd _) (fun x y => âŸªy, xâŸ«)
    (fun _x _y _z => inner_add_right _ _ _) (fun _r _x _y => inner_smul_right _ _ _)
    (fun _x _y _z => inner_add_left _ _ _) fun _r _x _y => inner_smul_left _ _ _

/-- The real inner product as a bilinear form.

Note that unlike `sesqFormOfInner`, this does not reverse the order of the arguments. -/
@[simps!]
def bilinFormOfRealInner : BilinForm â„ F := sesqFormOfInner.flip

/-- An inner product with a sum on the left. -/
theorem sum_inner {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ E) (x : E) :
    âŸªâˆ‘ i âˆˆ s, f i, xâŸ« = âˆ‘ i âˆˆ s, âŸªf i, xâŸ« :=
  map_sum (sesqFormOfInner (ğ•œ := ğ•œ) (E := E) x) _ _

/-- An inner product with a sum on the right. -/
theorem inner_sum {Î¹ : Type*} (s : Finset Î¹) (f : Î¹ â†’ E) (x : E) :
    âŸªx, âˆ‘ i âˆˆ s, f iâŸ« = âˆ‘ i âˆˆ s, âŸªx, f iâŸ« :=
  map_sum (LinearMap.flip sesqFormOfInner x) _ _

/-- An inner product with a sum on the left, `Finsupp` version. -/
protected theorem Finsupp.sum_inner {Î¹ : Type*} (l : Î¹ â†’â‚€ ğ•œ) (v : Î¹ â†’ E) (x : E) :
    âŸªl.sum fun (i : Î¹) (a : ğ•œ) => a â€¢ v i, xâŸ« = l.sum fun (i : Î¹) (a : ğ•œ) => conj a â€¢ âŸªv i, xâŸ« := by
  convert sum_inner (ğ•œ := ğ•œ) l.support (fun a => l a â€¢ v a) x
  simp only [inner_smul_left, Finsupp.sum, smul_eq_mul]

/-- An inner product with a sum on the right, `Finsupp` version. -/
protected theorem Finsupp.inner_sum {Î¹ : Type*} (l : Î¹ â†’â‚€ ğ•œ) (v : Î¹ â†’ E) (x : E) :
    âŸªx, l.sum fun (i : Î¹) (a : ğ•œ) => a â€¢ v iâŸ« = l.sum fun (i : Î¹) (a : ğ•œ) => a â€¢ âŸªx, v iâŸ« := by
  convert inner_sum (ğ•œ := ğ•œ) l.support (fun a => l a â€¢ v a) x
  simp only [inner_smul_right, Finsupp.sum, smul_eq_mul]

protected theorem DFinsupp.sum_inner {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*}
    [âˆ€ i, AddZeroClass (Î± i)] [âˆ€ (i) (x : Î± i), Decidable (x â‰  0)] (f : âˆ€ i, Î± i â†’ E)
    (l : Î â‚€ i, Î± i) (x : E) : âŸªl.sum f, xâŸ« = l.sum fun i a => âŸªf i a, xâŸ« := by
  simp +contextual only [DFinsupp.sum, sum_inner, smul_eq_mul]

protected theorem DFinsupp.inner_sum {Î¹ : Type*} [DecidableEq Î¹] {Î± : Î¹ â†’ Type*}
    [âˆ€ i, AddZeroClass (Î± i)] [âˆ€ (i) (x : Î± i), Decidable (x â‰  0)] (f : âˆ€ i, Î± i â†’ E)
    (l : Î â‚€ i, Î± i) (x : E) : âŸªx, l.sum fâŸ« = l.sum fun i a => âŸªx, f i aâŸ« := by
  simp +contextual only [DFinsupp.sum, inner_sum, smul_eq_mul]

@[simp]
theorem inner_zero_left (x : E) : âŸª0, xâŸ« = 0 := by
  rw [â† zero_smul ğ•œ (0 : E), inner_smul_left, RingHom.map_zero, zero_mul]

theorem inner_re_zero_left (x : E) : re âŸª0, xâŸ« = 0 := by
  simp only [inner_zero_left, AddMonoidHom.map_zero]

@[simp]
theorem inner_zero_right (x : E) : âŸªx, 0âŸ« = 0 := by
  rw [â† inner_conj_symm, inner_zero_left, RingHom.map_zero]

theorem inner_re_zero_right (x : E) : re âŸªx, 0âŸ« = 0 := by
  simp only [inner_zero_right, AddMonoidHom.map_zero]

theorem inner_self_nonneg {x : E} : 0 â‰¤ re âŸªx, xâŸ« :=
  PreInnerProductSpace.toCore.nonneg_re x

theorem real_inner_self_nonneg {x : F} : 0 â‰¤ âŸªx, xâŸ«_â„ :=
  @inner_self_nonneg â„ F _ _ _ x

@[simp]
theorem inner_self_ofReal_re (x : E) : (re âŸªx, xâŸ« : ğ•œ) = âŸªx, xâŸ« :=
  ((RCLike.is_real_TFAE (âŸªx, xâŸ« : ğ•œ)).out 2 3).2 (inner_self_im (ğ•œ := ğ•œ) x)

theorem inner_self_eq_norm_sq_to_K (x : E) : âŸªx, xâŸ« = (â€–xâ€– : ğ•œ) ^ 2 := by
  rw [â† inner_self_ofReal_re, â† norm_sq_eq_inner, ofReal_pow]

theorem inner_self_re_eq_norm (x : E) : re âŸªx, xâŸ« = â€–âŸªx, xâŸ«â€– := by
  conv_rhs => rw [â† inner_self_ofReal_re]
  symm
  exact norm_of_nonneg inner_self_nonneg

theorem inner_self_ofReal_norm (x : E) : (â€–âŸªx, xâŸ«â€– : ğ•œ) = âŸªx, xâŸ« := by
  rw [â† inner_self_re_eq_norm]
  exact inner_self_ofReal_re _

theorem real_inner_self_abs (x : F) : |âŸªx, xâŸ«_â„| = âŸªx, xâŸ«_â„ :=
  @inner_self_ofReal_norm â„ F _ _ _ x

theorem norm_inner_symm (x y : E) : â€–âŸªx, yâŸ«â€– = â€–âŸªy, xâŸ«â€– := by rw [â† inner_conj_symm, norm_conj]

@[simp]
theorem inner_neg_left (x y : E) : âŸª-x, yâŸ« = -âŸªx, yâŸ« := by
  rw [â† neg_one_smul ğ•œ x, inner_smul_left]
  simp

@[simp]
theorem inner_neg_right (x y : E) : âŸªx, -yâŸ« = -âŸªx, yâŸ« := by
  rw [â† inner_conj_symm, inner_neg_left]; simp only [RingHom.map_neg, inner_conj_symm]

theorem inner_neg_neg (x y : E) : âŸª-x, -yâŸ« = âŸªx, yâŸ« := by simp

-- Porting note: removed `simp` because it can prove it using `inner_conj_symm`
theorem inner_self_conj (x : E) : âŸªx, xâŸ«â€  = âŸªx, xâŸ« := inner_conj_symm _ _

theorem inner_sub_left (x y z : E) : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« := by
  simp [sub_eq_add_neg, inner_add_left]

theorem inner_sub_right (x y z : E) : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« := by
  simp [sub_eq_add_neg, inner_add_right]

theorem inner_mul_symm_re_eq_norm (x y : E) : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = â€–âŸªx, yâŸ« * âŸªy, xâŸ«â€– := by
  rw [â† inner_conj_symm, mul_comm]
  exact re_eq_norm_of_mul_conj (inner y x)

/-- Expand `âŸªx + y, x + yâŸ«` -/
theorem inner_add_add_self (x y : E) : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« := by
  simp only [inner_add_left, inner_add_right]; ring

/-- Expand `âŸªx + y, x + yâŸ«_â„` -/
theorem real_inner_add_add_self (x y : F) :
    âŸªx + y, x + yâŸ«_â„ = âŸªx, xâŸ«_â„ + 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ := by
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â† inner_conj_symm]; rfl
  simp only [inner_add_add_self, this, add_left_inj]
  ring

-- Expand `âŸªx - y, x - yâŸ«`
theorem inner_sub_sub_self (x y : E) : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« := by
  simp only [inner_sub_left, inner_sub_right]; ring

/-- Expand `âŸªx - y, x - yâŸ«_â„` -/
theorem real_inner_sub_sub_self (x y : F) :
    âŸªx - y, x - yâŸ«_â„ = âŸªx, xâŸ«_â„ - 2 * âŸªx, yâŸ«_â„ + âŸªy, yâŸ«_â„ := by
  have : âŸªy, xâŸ«_â„ = âŸªx, yâŸ«_â„ := by rw [â† inner_conj_symm]; rfl
  simp only [inner_sub_sub_self, this, add_left_inj]
  ring

/-- Parallelogram law -/
theorem parallelogram_law {x y : E} : âŸªx + y, x + yâŸ« + âŸªx - y, x - yâŸ« = 2 * (âŸªx, xâŸ« + âŸªy, yâŸ«) := by
  simp only [inner_add_add_self, inner_sub_sub_self]
  ring

/-- **Cauchyâ€“Schwarz inequality**. -/
theorem inner_mul_inner_self_le (x y : E) : â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« :=
  letI cd : PreInnerProductSpace.Core ğ•œ E := PreInnerProductSpace.toCore
  InnerProductSpace.Core.inner_mul_inner_self_le x y

/-- Cauchyâ€“Schwarz inequality for real inner products. -/
theorem real_inner_mul_inner_self_le (x y : F) : âŸªx, yâŸ«_â„ * âŸªx, yâŸ«_â„ â‰¤ âŸªx, xâŸ«_â„ * âŸªy, yâŸ«_â„ :=
  calc
    âŸªx, yâŸ«_â„ * âŸªx, yâŸ«_â„ â‰¤ â€–âŸªx, yâŸ«_â„â€– * â€–âŸªy, xâŸ«_â„â€– := by
      rw [real_inner_comm y, â† norm_mul]
      exact le_abs_self _
    _ â‰¤ âŸªx, xâŸ«_â„ * âŸªy, yâŸ«_â„ := @inner_mul_inner_self_le â„ _ _ _ _ x y


end BasicProperties_Seminormed

section BasicProperties

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

export InnerProductSpace (norm_sq_eq_inner)

@[simp]
theorem inner_self_eq_zero {x : E} : âŸªx, xâŸ« = 0 â†” x = 0 := by
  rw [inner_self_eq_norm_sq_to_K, sq_eq_zero_iff, ofReal_eq_zero, norm_eq_zero]

theorem inner_self_ne_zero {x : E} : âŸªx, xâŸ« â‰  0 â†” x â‰  0 :=
  inner_self_eq_zero.not

variable (ğ•œ)

theorem ext_inner_left {x y : E} (h : âˆ€ v, âŸªv, xâŸ« = âŸªv, yâŸ«) : x = y := by
  rw [â† sub_eq_zero, â† @inner_self_eq_zero ğ•œ, inner_sub_right, sub_eq_zero, h (x - y)]

theorem ext_inner_right {x y : E} (h : âˆ€ v, âŸªx, vâŸ« = âŸªy, vâŸ«) : x = y := by
  rw [â† sub_eq_zero, â† @inner_self_eq_zero ğ•œ, inner_sub_left, sub_eq_zero, h (x - y)]

variable {ğ•œ}

@[simp]
theorem inner_self_nonpos {x : E} : re âŸªx, xâŸ« â‰¤ 0 â†” x = 0 := by
  rw [â† norm_sq_eq_inner, (sq_nonneg _).le_iff_eq, sq_eq_zero_iff, norm_eq_zero]

open scoped InnerProductSpace in
theorem real_inner_self_nonpos {x : F} : âŸªx, xâŸ«_â„ â‰¤ 0 â†” x = 0 :=
  @inner_self_nonpos â„ F _ _ _ x

/-- A family of vectors is linearly independent if they are nonzero
and orthogonal. -/
theorem linearIndependent_of_ne_zero_of_inner_eq_zero {Î¹ : Type*} {v : Î¹ â†’ E} (hz : âˆ€ i, v i â‰  0)
    (ho : Pairwise fun i j => âŸªv i, v jâŸ« = 0) : LinearIndependent ğ•œ v := by
  rw [linearIndependent_iff']
  intro s g hg i hi
  have h' : g i * inner (v i) (v i) = inner (v i) (âˆ‘ j âˆˆ s, g j â€¢ v j) := by
    rw [inner_sum]
    symm
    convert Finset.sum_eq_single (Î² := ğ•œ) i ?_ ?_
    Â· rw [inner_smul_right]
    Â· intro j _hj hji
      rw [inner_smul_right, ho hji.symm, mul_zero]
    Â· exact fun h => False.elim (h hi)
  simpa [hg, hz] using h'

end BasicProperties

section Norm_Seminormed

open scoped InnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [SeminormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

theorem norm_eq_sqrt_inner (x : E) : â€–xâ€– = âˆš(re âŸªx, xâŸ«) :=
  calc
    â€–xâ€– = âˆš(â€–xâ€– ^ 2) := (sqrt_sq (norm_nonneg _)).symm
    _ = âˆš(re âŸªx, xâŸ«) := congr_arg _ (norm_sq_eq_inner _)

theorem norm_eq_sqrt_real_inner (x : F) : â€–xâ€– = âˆšâŸªx, xâŸ«_â„ :=
  @norm_eq_sqrt_inner â„ _ _ _ _ x

theorem inner_self_eq_norm_mul_norm (x : E) : re âŸªx, xâŸ« = â€–xâ€– * â€–xâ€– := by
  rw [@norm_eq_sqrt_inner ğ•œ, â† sqrt_mul inner_self_nonneg (re âŸªx, xâŸ«),
    sqrt_mul_self inner_self_nonneg]

theorem inner_self_eq_norm_sq (x : E) : re âŸªx, xâŸ« = â€–xâ€– ^ 2 := by
  rw [pow_two, inner_self_eq_norm_mul_norm]

theorem real_inner_self_eq_norm_mul_norm (x : F) : âŸªx, xâŸ«_â„ = â€–xâ€– * â€–xâ€– := by
  have h := @inner_self_eq_norm_mul_norm â„ F _ _ _ x
  simpa using h

theorem real_inner_self_eq_norm_sq (x : F) : âŸªx, xâŸ«_â„ = â€–xâ€– ^ 2 := by
  rw [pow_two, real_inner_self_eq_norm_mul_norm]

-- Porting note: this was present in mathlib3 but seemingly didn't do anything.
-- variable (ğ•œ)

/-- Expand the square -/
theorem norm_add_sq (x y : E) : â€–x + yâ€– ^ 2 = â€–xâ€– ^ 2 + 2 * re âŸªx, yâŸ« + â€–yâ€– ^ 2 := by
  repeat' rw [sq (M := â„), â† @inner_self_eq_norm_mul_norm ğ•œ]
  rw [inner_add_add_self, two_mul]
  simp only [add_assoc, add_left_inj, add_right_inj, AddMonoidHom.map_add]
  rw [â† inner_conj_symm, conj_re]

alias norm_add_pow_two := norm_add_sq

/-- Expand the square -/
theorem norm_add_sq_real (x y : F) : â€–x + yâ€– ^ 2 = â€–xâ€– ^ 2 + 2 * âŸªx, yâŸ«_â„ + â€–yâ€– ^ 2 := by
  have h := @norm_add_sq â„ _ _ _ _ x y
  simpa using h

alias norm_add_pow_two_real := norm_add_sq_real

/-- Expand the square -/
theorem norm_add_mul_self (x y : E) :
    â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + 2 * re âŸªx, yâŸ« + â€–yâ€– * â€–yâ€– := by
  repeat' rw [â† sq (M := â„)]
  exact norm_add_sq _ _

/-- Expand the square -/
theorem norm_add_mul_self_real (x y : F) :
    â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + 2 * âŸªx, yâŸ«_â„ + â€–yâ€– * â€–yâ€– := by
  have h := @norm_add_mul_self â„ _ _ _ _ x y
  simpa using h

/-- Expand the square -/
theorem norm_sub_sq (x y : E) : â€–x - yâ€– ^ 2 = â€–xâ€– ^ 2 - 2 * re âŸªx, yâŸ« + â€–yâ€– ^ 2 := by
  rw [sub_eq_add_neg, @norm_add_sq ğ•œ _ _ _ _ x (-y), norm_neg, inner_neg_right, map_neg, mul_neg,
    sub_eq_add_neg]

alias norm_sub_pow_two := norm_sub_sq

/-- Expand the square -/
theorem norm_sub_sq_real (x y : F) : â€–x - yâ€– ^ 2 = â€–xâ€– ^ 2 - 2 * âŸªx, yâŸ«_â„ + â€–yâ€– ^ 2 :=
  @norm_sub_sq â„ _ _ _ _ _ _

alias norm_sub_pow_two_real := norm_sub_sq_real

/-- Expand the square -/
theorem norm_sub_mul_self (x y : E) :
    â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– - 2 * re âŸªx, yâŸ« + â€–yâ€– * â€–yâ€– := by
  repeat' rw [â† sq (M := â„)]
  exact norm_sub_sq _ _

/-- Expand the square -/
theorem norm_sub_mul_self_real (x y : F) :
    â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– - 2 * âŸªx, yâŸ«_â„ + â€–yâ€– * â€–yâ€– := by
  have h := @norm_sub_mul_self â„ _ _ _ _ x y
  simpa using h

/-- Cauchyâ€“Schwarz inequality with norm -/
theorem norm_inner_le_norm (x y : E) : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := by
  rw [norm_eq_sqrt_inner (ğ•œ := ğ•œ) x, norm_eq_sqrt_inner (ğ•œ := ğ•œ) y]
  letI : PreInnerProductSpace.Core ğ•œ E := PreInnerProductSpace.toCore
  exact InnerProductSpace.Core.norm_inner_le_norm x y

theorem nnnorm_inner_le_nnnorm (x y : E) : â€–âŸªx, yâŸ«â€–â‚Š â‰¤ â€–xâ€–â‚Š * â€–yâ€–â‚Š :=
  norm_inner_le_norm x y

theorem re_inner_le_norm (x y : E) : re âŸªx, yâŸ« â‰¤ â€–xâ€– * â€–yâ€– :=
  le_trans (re_le_norm (inner x y)) (norm_inner_le_norm x y)

/-- Cauchyâ€“Schwarz inequality with norm -/
theorem abs_real_inner_le_norm (x y : F) : |âŸªx, yâŸ«_â„| â‰¤ â€–xâ€– * â€–yâ€– :=
  (Real.norm_eq_abs _).ge.trans (norm_inner_le_norm x y)

/-- Cauchyâ€“Schwarz inequality with norm -/
theorem real_inner_le_norm (x y : F) : âŸªx, yâŸ«_â„ â‰¤ â€–xâ€– * â€–yâ€– :=
  le_trans (le_abs_self _) (abs_real_inner_le_norm _ _)

lemma inner_eq_zero_of_left {x : E} (y : E) (h : â€–xâ€– = 0) : âŸªx, yâŸ«_ğ•œ = 0 := by
  rw [â† norm_eq_zero]
  refine le_antisymm ?_ (by positivity)
  exact norm_inner_le_norm _ _ |>.trans <| by simp [h]

lemma inner_eq_zero_of_right (x : E) {y : E} (h : â€–yâ€– = 0) : âŸªx, yâŸ«_ğ•œ = 0 := by
  rw [inner_eq_zero_symm, inner_eq_zero_of_left _ h]

variable (ğ•œ)

include ğ•œ in
theorem parallelogram_law_with_norm (x y : E) :
    â€–x + yâ€– * â€–x + yâ€– + â€–x - yâ€– * â€–x - yâ€– = 2 * (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) := by
  simp only [â† @inner_self_eq_norm_mul_norm ğ•œ]
  rw [â† re.map_add, parallelogram_law, two_mul, two_mul]
  simp only [re.map_add]

include ğ•œ in
theorem parallelogram_law_with_nnnorm (x y : E) :
    â€–x + yâ€–â‚Š * â€–x + yâ€–â‚Š + â€–x - yâ€–â‚Š * â€–x - yâ€–â‚Š = 2 * (â€–xâ€–â‚Š * â€–xâ€–â‚Š + â€–yâ€–â‚Š * â€–yâ€–â‚Š) :=
  Subtype.ext <| parallelogram_law_with_norm ğ•œ x y

variable {ğ•œ}

/-- Polarization identity: The real part of the inner product, in terms of the norm. -/
theorem re_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : E) :
    re âŸªx, yâŸ« = (â€–x + yâ€– * â€–x + yâ€– - â€–xâ€– * â€–xâ€– - â€–yâ€– * â€–yâ€–) / 2 := by
  rw [@norm_add_mul_self ğ•œ]
  ring

/-- Polarization identity: The real part of the inner product, in terms of the norm. -/
theorem re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : E) :
    re âŸªx, yâŸ« = (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 2 := by
  rw [@norm_sub_mul_self ğ•œ]
  ring

/-- Polarization identity: The real part of the inner product, in terms of the norm. -/
theorem re_inner_eq_norm_add_mul_self_sub_norm_sub_mul_self_div_four (x y : E) :
    re âŸªx, yâŸ« = (â€–x + yâ€– * â€–x + yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 4 := by
  rw [@norm_add_mul_self ğ•œ, @norm_sub_mul_self ğ•œ]
  ring

/-- Polarization identity: The imaginary part of the inner product, in terms of the norm. -/
theorem im_inner_eq_norm_sub_i_smul_mul_self_sub_norm_add_i_smul_mul_self_div_four (x y : E) :
    im âŸªx, yâŸ« = (â€–x - IK â€¢ yâ€– * â€–x - IK â€¢ yâ€– - â€–x + IK â€¢ yâ€– * â€–x + IK â€¢ yâ€–) / 4 := by
  simp only [@norm_add_mul_self ğ•œ, @norm_sub_mul_self ğ•œ, inner_smul_right, I_mul_re]
  ring

/-- Polarization identity: The inner product, in terms of the norm. -/
theorem inner_eq_sum_norm_sq_div_four (x y : E) :
    âŸªx, yâŸ« = ((â€–x + yâ€– : ğ•œ) ^ 2 - (â€–x - yâ€– : ğ•œ) ^ 2 +
              ((â€–x - IK â€¢ yâ€– : ğ•œ) ^ 2 - (â€–x + IK â€¢ yâ€– : ğ•œ) ^ 2) * IK) / 4 := by
  rw [â† re_add_im âŸªx, yâŸ«, re_inner_eq_norm_add_mul_self_sub_norm_sub_mul_self_div_four,
    im_inner_eq_norm_sub_i_smul_mul_self_sub_norm_add_i_smul_mul_self_div_four]
  push_cast
  simp only [sq, â† mul_div_right_comm, â† add_div]

-- See note [lower instance priority]
instance (priority := 100) InnerProductSpace.toUniformConvexSpace : UniformConvexSpace F :=
  âŸ¨fun Îµ hÎµ => by
    refine
      âŸ¨2 - âˆš(4 - Îµ ^ 2), sub_pos_of_lt <| (sqrt_lt' zero_lt_two).2 ?_, fun x hx y hy hxy => ?_âŸ©
    Â· norm_num
      exact pow_pos hÎµ _
    rw [sub_sub_cancel]
    refine le_sqrt_of_sq_le ?_
    rw [sq, eq_sub_iff_add_eq.2 (parallelogram_law_with_norm â„ x y), â† sq â€–x - yâ€–, hx, hy]
    ring_nf
    gcongrâŸ©

section Complex_Seminormed

variable {V : Type*} [SeminormedAddCommGroup V] [InnerProductSpace â„‚ V]

/-- A complex polarization identity, with a linear map
-/
theorem inner_map_polarization (T : V â†’â‚—[â„‚] V) (x y : V) :
    âŸªT y, xâŸ«_â„‚ =
      (âŸªT (x + y), x + yâŸ«_â„‚ - âŸªT (x - y), x - yâŸ«_â„‚ +
            Complex.I * âŸªT (x + Complex.I â€¢ y), x + Complex.I â€¢ yâŸ«_â„‚ -
          Complex.I * âŸªT (x - Complex.I â€¢ y), x - Complex.I â€¢ yâŸ«_â„‚) /
        4 := by
  simp only [map_add, map_sub, inner_add_left, inner_add_right, LinearMap.map_smul, inner_smul_left,
    inner_smul_right, Complex.conj_I, â† pow_two, Complex.I_sq, inner_sub_left, inner_sub_right,
    mul_add, â† mul_assoc, mul_neg, neg_neg, sub_neg_eq_add, one_mul, neg_one_mul, mul_sub, sub_sub]
  ring

theorem inner_map_polarization' (T : V â†’â‚—[â„‚] V) (x y : V) :
    âŸªT x, yâŸ«_â„‚ =
      (âŸªT (x + y), x + yâŸ«_â„‚ - âŸªT (x - y), x - yâŸ«_â„‚ -
            Complex.I * âŸªT (x + Complex.I â€¢ y), x + Complex.I â€¢ yâŸ«_â„‚ +
          Complex.I * âŸªT (x - Complex.I â€¢ y), x - Complex.I â€¢ yâŸ«_â„‚) /
        4 := by
  simp only [map_add, map_sub, inner_add_left, inner_add_right, LinearMap.map_smul, inner_smul_left,
    inner_smul_right, Complex.conj_I, â† pow_two, Complex.I_sq, inner_sub_left, inner_sub_right,
    mul_add, â† mul_assoc, mul_neg, neg_neg, sub_neg_eq_add, one_mul, neg_one_mul, mul_sub, sub_sub]
  ring

end Complex_Seminormed

section Complex

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„‚ V]

/-- A linear map `T` is zero, if and only if the identity `âŸªT x, xâŸ«_â„‚ = 0` holds for all `x`.
-/
theorem inner_map_self_eq_zero (T : V â†’â‚—[â„‚] V) : (âˆ€ x : V, âŸªT x, xâŸ«_â„‚ = 0) â†” T = 0 := by
  constructor
  Â· intro hT
    ext x
    rw [LinearMap.zero_apply, â† @inner_self_eq_zero â„‚ V, inner_map_polarization]
    simp only [hT]
    norm_num
  Â· rintro rfl x
    simp only [LinearMap.zero_apply, inner_zero_left]

/--
Two linear maps `S` and `T` are equal, if and only if the identity `âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚` holds
for all `x`.
-/
theorem ext_inner_map (S T : V â†’â‚—[â„‚] V) : (âˆ€ x : V, âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚) â†” S = T := by
  rw [â† sub_eq_zero, â† inner_map_self_eq_zero]
  refine forall_congr' fun x => ?_
  rw [LinearMap.sub_apply, inner_sub_left, sub_eq_zero]

end Complex

section

variable {Î¹ : Type*} {Î¹' : Type*} {Î¹'' : Type*}
variable {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']
variable {E'' : Type*} [SeminormedAddCommGroup E''] [InnerProductSpace ğ•œ E'']

/-- A linear isometry preserves the inner product. -/
@[simp]
theorem LinearIsometry.inner_map_map (f : E â†’â‚—áµ¢[ğ•œ] E') (x y : E) : âŸªf x, f yâŸ« = âŸªx, yâŸ« := by
  simp [inner_eq_sum_norm_sq_div_four, â† f.norm_map]

/-- A linear isometric equivalence preserves the inner product. -/
@[simp]
theorem LinearIsometryEquiv.inner_map_map (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (x y : E) : âŸªf x, f yâŸ« = âŸªx, yâŸ« :=
  f.toLinearIsometry.inner_map_map x y

/-- The adjoint of a linear isometric equivalence is its inverse. -/
theorem LinearIsometryEquiv.inner_map_eq_flip (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (x : E) (y : E') :
    âŸªf x, yâŸ«_ğ•œ = âŸªx, f.symm yâŸ«_ğ•œ := by
  conv_lhs => rw [â† f.apply_symm_apply y, f.inner_map_map]

/-- A linear map that preserves the inner product is a linear isometry. -/
def LinearMap.isometryOfInner (f : E â†’â‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) : E â†’â‚—áµ¢[ğ•œ] E' :=
  âŸ¨f, fun x => by simp only [@norm_eq_sqrt_inner ğ•œ, h]âŸ©

@[simp]
theorem LinearMap.coe_isometryOfInner (f : E â†’â‚—[ğ•œ] E') (h) : â‡‘(f.isometryOfInner h) = f :=
  rfl

@[simp]
theorem LinearMap.isometryOfInner_toLinearMap (f : E â†’â‚—[ğ•œ] E') (h) :
    (f.isometryOfInner h).toLinearMap = f :=
  rfl

/-- A linear equivalence that preserves the inner product is a linear isometric equivalence. -/
def LinearEquiv.isometryOfInner (f : E â‰ƒâ‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
  âŸ¨f, ((f : E â†’â‚—[ğ•œ] E').isometryOfInner h).norm_mapâŸ©

@[simp]
theorem LinearEquiv.coe_isometryOfInner (f : E â‰ƒâ‚—[ğ•œ] E') (h) : â‡‘(f.isometryOfInner h) = f :=
  rfl

@[simp]
theorem LinearEquiv.isometryOfInner_toLinearEquiv (f : E â‰ƒâ‚—[ğ•œ] E') (h) :
    (f.isometryOfInner h).toLinearEquiv = f :=
  rfl

/-- A linear map is an isometry if and it preserves the inner product. -/
theorem LinearMap.norm_map_iff_inner_map_map {F : Type*} [FunLike F E E'] [LinearMapClass F ğ•œ E E']
    (f : F) : (âˆ€ x, â€–f xâ€– = â€–xâ€–) â†” (âˆ€ x y, âŸªf x, f yâŸ«_ğ•œ = âŸªx, yâŸ«_ğ•œ) :=
  âŸ¨({ toLinearMap := LinearMapClass.linearMap f, norm_map' := Â· : E â†’â‚—áµ¢[ğ•œ] E' }.inner_map_map),
    (LinearMapClass.linearMap f |>.isometryOfInner Â· |>.norm_map)âŸ©

end

/-- Polarization identity: The real inner product, in terms of the norm. -/
theorem real_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two (x y : F) :
    âŸªx, yâŸ«_â„ = (â€–x + yâ€– * â€–x + yâ€– - â€–xâ€– * â€–xâ€– - â€–yâ€– * â€–yâ€–) / 2 :=
  re_to_real.symm.trans <|
    re_inner_eq_norm_add_mul_self_sub_norm_mul_self_sub_norm_mul_self_div_two x y

/-- Polarization identity: The real inner product, in terms of the norm. -/
theorem real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two (x y : F) :
    âŸªx, yâŸ«_â„ = (â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– - â€–x - yâ€– * â€–x - yâ€–) / 2 :=
  re_to_real.symm.trans <|
    re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two x y

/-- Pythagorean theorem, if-and-only-if vector inner product form. -/
theorem norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero (x y : F) :
    â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– â†” âŸªx, yâŸ«_â„ = 0 := by
  rw [@norm_add_mul_self â„, add_right_cancel_iff, add_right_eq_self, mul_eq_zero]
  norm_num

/-- Pythagorean theorem, if-and-if vector inner product form using square roots. -/
theorem norm_add_eq_sqrt_iff_real_inner_eq_zero {x y : F} :
    â€–x + yâ€– = âˆš(â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) â†” âŸªx, yâŸ«_â„ = 0 := by
  rw [â† norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero, eq_comm, sqrt_eq_iff_mul_self_eq,
    eq_comm] <;> positivity

/-- Pythagorean theorem, vector inner product form. -/
theorem norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (x y : E) (h : âŸªx, yâŸ« = 0) :
    â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– := by
  rw [@norm_add_mul_self ğ•œ, add_right_cancel_iff, add_right_eq_self, mul_eq_zero]
  apply Or.inr
  simp only [h, zero_re']

/-- Pythagorean theorem, vector inner product form. -/
theorem norm_add_sq_eq_norm_sq_add_norm_sq_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
    â€–x + yâ€– * â€–x + yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– :=
  (norm_add_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero x y).2 h

/-- Pythagorean theorem, subtracting vectors, if-and-only-if vector
inner product form. -/
theorem norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero (x y : F) :
    â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– â†” âŸªx, yâŸ«_â„ = 0 := by
  rw [@norm_sub_mul_self â„, add_right_cancel_iff, sub_eq_add_neg, add_right_eq_self, neg_eq_zero,
    mul_eq_zero]
  norm_num

/-- Pythagorean theorem, subtracting vectors, if-and-if vector inner product form using square
roots. -/
theorem norm_sub_eq_sqrt_iff_real_inner_eq_zero {x y : F} :
    â€–x - yâ€– = âˆš(â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€–) â†” âŸªx, yâŸ«_â„ = 0 := by
  rw [â† norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero, eq_comm, sqrt_eq_iff_mul_self_eq,
    eq_comm] <;> positivity

/-- Pythagorean theorem, subtracting vectors, vector inner product
form. -/
theorem norm_sub_sq_eq_norm_sq_add_norm_sq_real {x y : F} (h : âŸªx, yâŸ«_â„ = 0) :
    â€–x - yâ€– * â€–x - yâ€– = â€–xâ€– * â€–xâ€– + â€–yâ€– * â€–yâ€– :=
  (norm_sub_sq_eq_norm_sq_add_norm_sq_iff_real_inner_eq_zero x y).2 h

/-- The sum and difference of two vectors are orthogonal if and only
if they have the same norm. -/
theorem real_inner_add_sub_eq_zero_iff (x y : F) : âŸªx + y, x - yâŸ«_â„ = 0 â†” â€–xâ€– = â€–yâ€– := by
  conv_rhs => rw [â† mul_self_inj_of_nonneg (norm_nonneg _) (norm_nonneg _)]
  simp only [â† @inner_self_eq_norm_mul_norm â„, inner_add_left, inner_sub_right, real_inner_comm y x,
    sub_eq_zero, re_to_real]
  constructor
  Â· intro h
    rw [add_comm] at h
    linarith
  Â· intro h
    linarith

/-- Given two orthogonal vectors, their sum and difference have equal norms. -/
theorem norm_sub_eq_norm_add {v w : E} (h : âŸªv, wâŸ« = 0) : â€–w - vâ€– = â€–w + vâ€– := by
  rw [â† mul_self_inj_of_nonneg (norm_nonneg _) (norm_nonneg _)]
  simp only [h, â† @inner_self_eq_norm_mul_norm ğ•œ, sub_neg_eq_add, sub_zero, map_sub, zero_re',
    zero_sub, add_zero, map_add, inner_add_right, inner_sub_left, inner_sub_right, inner_re_symm,
    zero_add]

/-- The real inner product of two vectors, divided by the product of their
norms, has absolute value at most 1. -/
theorem abs_real_inner_div_norm_mul_norm_le_one (x y : F) : |âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–)| â‰¤ 1 := by
  rw [abs_div, abs_mul, abs_norm, abs_norm]
  exact div_le_one_of_leâ‚€ (abs_real_inner_le_norm x y) (by positivity)

/-- The inner product of a vector with a multiple of itself. -/
theorem real_inner_smul_self_left (x : F) (r : â„) : âŸªr â€¢ x, xâŸ«_â„ = r * (â€–xâ€– * â€–xâ€–) := by
  rw [real_inner_smul_left, â† real_inner_self_eq_norm_mul_norm]

/-- The inner product of a vector with a multiple of itself. -/
theorem real_inner_smul_self_right (x : F) (r : â„) : âŸªx, r â€¢ xâŸ«_â„ = r * (â€–xâ€– * â€–xâ€–) := by
  rw [inner_smul_right, â† real_inner_self_eq_norm_mul_norm]

variable (ğ•œ)

/-- The inner product as a sesquilinear map. -/
def innerâ‚›â‚— : E â†’â‚—â‹†[ğ•œ] E â†’â‚—[ğ•œ] ğ•œ :=
  LinearMap.mkâ‚‚'â‚›â‚— _ _ (fun v w => âŸªv, wâŸ«) inner_add_left (fun _ _ _ => inner_smul_left _ _ _)
    inner_add_right fun _ _ _ => inner_smul_right _ _ _

@[simp]
theorem innerâ‚›â‚—_apply_coe (v : E) : â‡‘(innerâ‚›â‚— ğ•œ v) = fun w => âŸªv, wâŸ« :=
  rfl

@[simp]
theorem innerâ‚›â‚—_apply (v w : E) : innerâ‚›â‚— ğ•œ v w = âŸªv, wâŸ« :=
  rfl

variable (F)
/-- The inner product as a bilinear map in the real case. -/
def innerâ‚— : F â†’â‚—[â„] F â†’â‚—[â„] â„ := innerâ‚›â‚— â„

@[simp] lemma flip_innerâ‚— : (innerâ‚— F).flip = innerâ‚— F := by
  ext v w
  exact real_inner_comm v w

variable {F}

@[simp] lemma innerâ‚—_apply (v w : F) : innerâ‚— F v w = âŸªv, wâŸ«_â„ := rfl

/-- The inner product as a continuous sesquilinear map. Note that `toDualMap` (resp. `toDual`)
in `InnerProductSpace.Dual` is a version of this given as a linear isometry (resp. linear
isometric equivalence). -/
def innerSL : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
  LinearMap.mkContinuousâ‚‚ (innerâ‚›â‚— ğ•œ) 1 fun x y => by
    simp only [norm_inner_le_norm, one_mul, innerâ‚›â‚—_apply]

@[simp]
theorem innerSL_apply_coe (v : E) : â‡‘(innerSL ğ•œ v) = fun w => âŸªv, wâŸ« :=
  rfl

@[simp]
theorem innerSL_apply (v w : E) : innerSL ğ•œ v w = âŸªv, wâŸ« :=
  rfl

/-- The inner product as a continuous sesquilinear map, with the two arguments flipped. -/
def innerSLFlip : E â†’L[ğ•œ] E â†’Lâ‹†[ğ•œ] ğ•œ :=
  @ContinuousLinearMap.flipâ‚—áµ¢' ğ•œ ğ•œ ğ•œ E E ğ•œ _ _ _ _ _ _ _ _ _ (RingHom.id ğ•œ) (starRingEnd ğ•œ) _ _
    (innerSL ğ•œ)

@[simp]
theorem innerSLFlip_apply (x y : E) : innerSLFlip ğ•œ x y = âŸªy, xâŸ« :=
  rfl

variable (F) in
@[simp] lemma innerSL_real_flip : (innerSL â„ (E := F)).flip = innerSL â„ := by
  ext v w
  exact real_inner_comm _ _

variable {ğ•œ}

namespace ContinuousLinearMap

variable {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']

-- Note: odd and expensive build behavior is explicitly turned off using `noncomputable`
/-- Given `f : E â†’L[ğ•œ] E'`, construct the continuous sesquilinear form `fun x y â†¦ âŸªx, A yâŸ«`, given
as a continuous linear map. -/
noncomputable def toSesqForm : (E â†’L[ğ•œ] E') â†’L[ğ•œ] E' â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
  (ContinuousLinearMap.flipâ‚—áµ¢' E E' ğ•œ (starRingEnd ğ•œ) (RingHom.id ğ•œ)).toContinuousLinearEquiv âˆ˜L
    ContinuousLinearMap.compSL E E' (E' â†’Lâ‹†[ğ•œ] ğ•œ) (RingHom.id ğ•œ) (RingHom.id ğ•œ) (innerSLFlip ğ•œ)

@[simp]
theorem toSesqForm_apply_coe (f : E â†’L[ğ•œ] E') (x : E') : toSesqForm f x = (innerSL ğ•œ x).comp f :=
  rfl

theorem toSesqForm_apply_norm_le {f : E â†’L[ğ•œ] E'} {v : E'} : â€–toSesqForm f vâ€– â‰¤ â€–fâ€– * â€–vâ€– := by
  refine opNorm_le_bound _ (by positivity) fun x â†¦ ?_
  have hâ‚ : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_opNorm _ _
  have hâ‚‚ := @norm_inner_le_norm ğ•œ E' _ _ _ v (f x)
  calc
    â€–âŸªv, f xâŸ«â€– â‰¤ â€–vâ€– * â€–f xâ€– := hâ‚‚
    _ â‰¤ â€–vâ€– * (â€–fâ€– * â€–xâ€–) := mul_le_mul_of_nonneg_left hâ‚ (norm_nonneg v)
    _ = â€–fâ€– * â€–vâ€– * â€–xâ€– := by ring


end ContinuousLinearMap

variable (ğ•œ)

/-- `innerSL` is an isometry. Note that the associated `LinearIsometry` is defined in
`InnerProductSpace.Dual` as `toDualMap`. -/
@[simp]
theorem innerSL_apply_norm (x : E) : â€–innerSL ğ•œ xâ€– = â€–xâ€– := by
  refine
    le_antisymm ((innerSL ğ•œ x).opNorm_le_bound (norm_nonneg _) fun y => norm_inner_le_norm _ _) ?_
  rcases (norm_nonneg x).eq_or_gt with (h | h)
  Â· simp [h]
  Â· refine (mul_le_mul_right h).mp ?_
    calc
      â€–xâ€– * â€–xâ€– = â€–(âŸªx, xâŸ« : ğ•œ)â€– := by
        rw [â† sq, inner_self_eq_norm_sq_to_K, norm_pow, norm_ofReal, abs_norm]
      _ â‰¤ â€–innerSL ğ•œ xâ€– * â€–xâ€– := (innerSL ğ•œ x).le_opNorm _

lemma norm_innerSL_le : â€–innerSL ğ•œ (E := E)â€– â‰¤ 1 :=
  ContinuousLinearMap.opNorm_le_bound _ zero_le_one (by simp)

variable {ğ•œ}

/-- When an inner product space `E` over `ğ•œ` is considered as a real normed space, its inner
product satisfies `IsBoundedBilinearMap`.

In order to state these results, we need a `NormedSpace â„ E` instance. We will later establish
such an instance by restriction-of-scalars, `InnerProductSpace.rclikeToReal ğ•œ E`, but this
instance may be not definitionally equal to some other â€œnaturalâ€ instance. So, we assume
`[NormedSpace â„ E]`.
-/
theorem _root_.isBoundedBilinearMap_inner [NormedSpace â„ E] [IsScalarTower â„ ğ•œ E] :
    IsBoundedBilinearMap â„ fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  { add_left := inner_add_left
    smul_left := fun r x y => by
      simp only [â† algebraMap_smul ğ•œ r x, algebraMap_eq_ofReal, inner_smul_real_left]
    add_right := inner_add_right
    smul_right := fun r x y => by
      simp only [â† algebraMap_smul ğ•œ r y, algebraMap_eq_ofReal, inner_smul_real_right]
    bound :=
      âŸ¨1, zero_lt_one, fun x y => by
        rw [one_mul]
        exact norm_inner_le_norm x yâŸ© }

/-- The inner product of two weighted sums, where the weights in each
sum add to 0, in terms of the norms of pairwise differences. -/
theorem inner_sum_smul_sum_smul_of_sum_eq_zero {Î¹â‚ : Type*} {sâ‚ : Finset Î¹â‚} {wâ‚ : Î¹â‚ â†’ â„}
    (vâ‚ : Î¹â‚ â†’ F) (hâ‚ : âˆ‘ i âˆˆ sâ‚, wâ‚ i = 0) {Î¹â‚‚ : Type*} {sâ‚‚ : Finset Î¹â‚‚} {wâ‚‚ : Î¹â‚‚ â†’ â„}
    (vâ‚‚ : Î¹â‚‚ â†’ F) (hâ‚‚ : âˆ‘ i âˆˆ sâ‚‚, wâ‚‚ i = 0) :
    âŸªâˆ‘ iâ‚ âˆˆ sâ‚, wâ‚ iâ‚ â€¢ vâ‚ iâ‚, âˆ‘ iâ‚‚ âˆˆ sâ‚‚, wâ‚‚ iâ‚‚ â€¢ vâ‚‚ iâ‚‚âŸ«_â„ =
      (-âˆ‘ iâ‚ âˆˆ sâ‚, âˆ‘ iâ‚‚ âˆˆ sâ‚‚, wâ‚ iâ‚ * wâ‚‚ iâ‚‚ * (â€–vâ‚ iâ‚ - vâ‚‚ iâ‚‚â€– * â€–vâ‚ iâ‚ - vâ‚‚ iâ‚‚â€–)) / 2 := by
  simp_rw [sum_inner, inner_sum, real_inner_smul_left, real_inner_smul_right,
    real_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two, â† div_sub_div_same,
    â† div_add_div_same, mul_sub_left_distrib, left_distrib, Finset.sum_sub_distrib,
    Finset.sum_add_distrib, â† Finset.mul_sum, â† Finset.sum_mul, hâ‚, hâ‚‚, zero_mul,
    mul_zero, Finset.sum_const_zero, zero_add, zero_sub, Finset.mul_sum, neg_div,
    Finset.sum_div, mul_div_assoc, mul_assoc]

end Norm_Seminormed

section Norm

open scoped InnerProductSpace

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace â„ F]
variable {Î¹ : Type*}

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

/-- Formula for the distance between the images of two nonzero points under an inversion with center
zero. See also `EuclideanGeometry.dist_inversion_inversion` for inversions around a general
point. -/
theorem dist_div_norm_sq_smul {x y : F} (hx : x â‰  0) (hy : y â‰  0) (R : â„) :
    dist ((R / â€–xâ€–) ^ 2 â€¢ x) ((R / â€–yâ€–) ^ 2 â€¢ y) = R ^ 2 / (â€–xâ€– * â€–yâ€–) * dist x y :=
  have hx' : â€–xâ€– â‰  0 := norm_ne_zero_iff.2 hx
  have hy' : â€–yâ€– â‰  0 := norm_ne_zero_iff.2 hy
  calc
    dist ((R / â€–xâ€–) ^ 2 â€¢ x) ((R / â€–yâ€–) ^ 2 â€¢ y) =
        âˆš(â€–(R / â€–xâ€–) ^ 2 â€¢ x - (R / â€–yâ€–) ^ 2 â€¢ yâ€– ^ 2) := by
      rw [dist_eq_norm, sqrt_sq (norm_nonneg _)]
    _ = âˆš((R ^ 2 / (â€–xâ€– * â€–yâ€–)) ^ 2 * â€–x - yâ€– ^ 2) :=
      congr_arg sqrt <| by
        field_simp [sq, norm_sub_mul_self_real, norm_smul, real_inner_smul_left, inner_smul_right,
          Real.norm_of_nonneg (mul_self_nonneg _)]
        ring
    _ = R ^ 2 / (â€–xâ€– * â€–yâ€–) * dist x y := by
      rw [sqrt_mul, sqrt_sq, sqrt_sq, dist_eq_norm] <;> positivity

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
theorem norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul {x : E} {r : ğ•œ} (hx : x â‰  0)
    (hr : r â‰  0) : â€–âŸªx, r â€¢ xâŸ«â€– / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 := by
  have hx' : â€–xâ€– â‰  0 := by simp [hx]
  have hr' : â€–râ€– â‰  0 := by simp [hr]
  rw [inner_smul_right, norm_mul, â† inner_self_re_eq_norm, inner_self_eq_norm_mul_norm, norm_smul]
  rw [â† mul_assoc, â† div_div, mul_div_cancel_rightâ‚€ _ hx', â† div_div, mul_comm,
    mul_div_cancel_rightâ‚€ _ hr', div_self hx']

/-- The inner product of a nonzero vector with a nonzero multiple of
itself, divided by the product of their norms, has absolute value
1. -/
theorem abs_real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul {x : F} {r : â„}
    (hx : x â‰  0) (hr : r â‰  0) : |âŸªx, r â€¢ xâŸ«_â„| / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 :=
  norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr

/-- The inner product of a nonzero vector with a positive multiple of
itself, divided by the product of their norms, has value 1. -/
theorem real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul {x : F} {r : â„} (hx : x â‰  0)
    (hr : 0 < r) : âŸªx, r â€¢ xâŸ«_â„ / (â€–xâ€– * â€–r â€¢ xâ€–) = 1 := by
  rw [real_inner_smul_self_right, norm_smul, Real.norm_eq_abs, â† mul_assoc â€–xâ€–, mul_comm _ |r|,
    mul_assoc, abs_of_nonneg hr.le, div_self]
  exact mul_ne_zero hr.ne' (mul_self_ne_zero.2 (norm_ne_zero_iff.2 hx))

/-- The inner product of a nonzero vector with a negative multiple of
itself, divided by the product of their norms, has value -1. -/
theorem real_inner_div_norm_mul_norm_eq_neg_one_of_ne_zero_of_neg_mul {x : F} {r : â„} (hx : x â‰  0)
    (hr : r < 0) : âŸªx, r â€¢ xâŸ«_â„ / (â€–xâ€– * â€–r â€¢ xâ€–) = -1 := by
  rw [real_inner_smul_self_right, norm_smul, Real.norm_eq_abs, â† mul_assoc â€–xâ€–, mul_comm _ |r|,
    mul_assoc, abs_of_neg hr, neg_mul, div_neg_eq_neg_div, div_self]
  exact mul_ne_zero hr.ne (mul_self_ne_zero.2 (norm_ne_zero_iff.2 hx))

theorem norm_inner_eq_norm_tfae (x y : E) :
    List.TFAE [â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€–,
      x = 0 âˆ¨ y = (âŸªx, yâŸ« / âŸªx, xâŸ«) â€¢ x,
      x = 0 âˆ¨ âˆƒ r : ğ•œ, y = r â€¢ x,
      x = 0 âˆ¨ y âˆˆ ğ•œ âˆ™ x] := by
  tfae_have 1 â†’ 2 := by
    refine fun h => or_iff_not_imp_left.2 fun hxâ‚€ => ?_
    have : â€–xâ€– ^ 2 â‰  0 := pow_ne_zero _ (norm_ne_zero_iff.2 hxâ‚€)
    rw [â† sq_eq_sqâ‚€, mul_pow, â† mul_right_inj' this, eq_comm, â† sub_eq_zero, â† mul_sub] at h <;>
      try positivity
    simp only [@norm_sq_eq_inner ğ•œ] at h
    letI : InnerProductSpace.Core ğ•œ E := InnerProductSpace.toCore
    erw [â† InnerProductSpace.Core.cauchy_schwarz_aux, InnerProductSpace.Core.normSq_eq_zero,
      sub_eq_zero] at h
    rw [div_eq_inv_mul, mul_smul, h, inv_smul_smulâ‚€]
    rwa [inner_self_ne_zero]
  tfae_have 2 â†’ 3 := fun h => h.imp_right fun h' => âŸ¨_, h'âŸ©
  tfae_have 3 â†’ 1 := by
    rintro (rfl | âŸ¨r, rflâŸ©) <;>
    simp [inner_smul_right, norm_smul, inner_self_eq_norm_sq_to_K, inner_self_eq_norm_mul_norm,
      sq, mul_left_comm]
  tfae_have 3 â†” 4 := by simp only [Submodule.mem_span_singleton, eq_comm]
  tfae_finish

/-- If the inner product of two vectors is equal to the product of their norms, then the two vectors
are multiples of each other. One form of the equality case for Cauchy-Schwarz.
Compare `inner_eq_norm_mul_iff`, which takes the stronger hypothesis `âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
theorem norm_inner_eq_norm_iff {x y : E} (hxâ‚€ : x â‰  0) (hyâ‚€ : y â‰  0) :
    â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€– â†” âˆƒ r : ğ•œ, r â‰  0 âˆ§ y = r â€¢ x :=
  calc
    â€–âŸªx, yâŸ«â€– = â€–xâ€– * â€–yâ€– â†” x = 0 âˆ¨ âˆƒ r : ğ•œ, y = r â€¢ x :=
      (@norm_inner_eq_norm_tfae ğ•œ _ _ _ _ x y).out 0 2
    _ â†” âˆƒ r : ğ•œ, y = r â€¢ x := or_iff_right hxâ‚€
    _ â†” âˆƒ r : ğ•œ, r â‰  0 âˆ§ y = r â€¢ x :=
      âŸ¨fun âŸ¨r, hâŸ© => âŸ¨r, fun hrâ‚€ => hyâ‚€ <| h.symm â–¸ smul_eq_zero.2 <| Or.inl hrâ‚€, hâŸ©,
        fun âŸ¨r, _hrâ‚€, hâŸ© => âŸ¨r, hâŸ©âŸ©

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
theorem norm_inner_div_norm_mul_norm_eq_one_iff (x y : E) :
    â€–âŸªx, yâŸ« / (â€–xâ€– * â€–yâ€–)â€– = 1 â†” x â‰  0 âˆ§ âˆƒ r : ğ•œ, r â‰  0 âˆ§ y = r â€¢ x := by
  constructor
  Â· intro h
    have hxâ‚€ : x â‰  0 := fun hâ‚€ => by simp [hâ‚€] at h
    have hyâ‚€ : y â‰  0 := fun hâ‚€ => by simp [hâ‚€] at h
    refine âŸ¨hxâ‚€, (norm_inner_eq_norm_iff hxâ‚€ hyâ‚€).1 <| eq_of_div_eq_one ?_âŸ©
    simpa using h
  Â· rintro âŸ¨hx, âŸ¨r, âŸ¨hr, rflâŸ©âŸ©âŸ©
    simp only [norm_div, norm_mul, norm_ofReal, abs_norm]
    exact norm_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_ne_zero_mul hx hr

/-- The inner product of two vectors, divided by the product of their
norms, has absolute value 1 if and only if they are nonzero and one is
a multiple of the other. One form of equality case for Cauchy-Schwarz. -/
theorem abs_real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
    |âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–)| = 1 â†” x â‰  0 âˆ§ âˆƒ r : â„, r â‰  0 âˆ§ y = r â€¢ x :=
  @norm_inner_div_norm_mul_norm_eq_one_iff â„ F _ _ _ x y

theorem inner_eq_norm_mul_iff_div {x y : E} (hâ‚€ : x â‰  0) :
    âŸªx, yâŸ« = (â€–xâ€– : ğ•œ) * â€–yâ€– â†” (â€–yâ€– / â€–xâ€– : ğ•œ) â€¢ x = y := by
  have hâ‚€' := hâ‚€
  rw [â† norm_ne_zero_iff, Ne, â† @ofReal_eq_zero ğ•œ] at hâ‚€'
  constructor <;> intro h
  Â· have : x = 0 âˆ¨ y = (âŸªx, yâŸ« / âŸªx, xâŸ« : ğ•œ) â€¢ x :=
      ((@norm_inner_eq_norm_tfae ğ•œ _ _ _ _ x y).out 0 1).1 (by simp [h])
    rw [this.resolve_left hâ‚€, h]
    simp [norm_smul, inner_self_ofReal_norm, mul_div_cancel_rightâ‚€ _ hâ‚€']
  Â· conv_lhs => rw [â† h, inner_smul_right, inner_self_eq_norm_sq_to_K]
    field_simp [sq, mul_left_comm]

/-- If the inner product of two vectors is equal to the product of their norms (i.e.,
`âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`), then the two vectors are nonnegative real multiples of each other. One form
of the equality case for Cauchy-Schwarz.
Compare `norm_inner_eq_norm_iff`, which takes the weaker hypothesis `abs âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
theorem inner_eq_norm_mul_iff {x y : E} :
    âŸªx, yâŸ« = (â€–xâ€– : ğ•œ) * â€–yâ€– â†” (â€–yâ€– : ğ•œ) â€¢ x = (â€–xâ€– : ğ•œ) â€¢ y := by
  rcases eq_or_ne x 0 with (rfl | hâ‚€)
  Â· simp
  Â· rw [inner_eq_norm_mul_iff_div hâ‚€, div_eq_inv_mul, mul_smul, inv_smul_eq_iffâ‚€]
    rwa [Ne, ofReal_eq_zero, norm_eq_zero]

/-- If the inner product of two vectors is equal to the product of their norms (i.e.,
`âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`), then the two vectors are nonnegative real multiples of each other. One form
of the equality case for Cauchy-Schwarz.
Compare `norm_inner_eq_norm_iff`, which takes the weaker hypothesis `abs âŸªx, yâŸ« = â€–xâ€– * â€–yâ€–`. -/
theorem inner_eq_norm_mul_iff_real {x y : F} : âŸªx, yâŸ«_â„ = â€–xâ€– * â€–yâ€– â†” â€–yâ€– â€¢ x = â€–xâ€– â€¢ y :=
  inner_eq_norm_mul_iff

/-- The inner product of two vectors, divided by the product of their
norms, has value 1 if and only if they are nonzero and one is
a positive multiple of the other. -/
theorem real_inner_div_norm_mul_norm_eq_one_iff (x y : F) :
    âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–) = 1 â†” x â‰  0 âˆ§ âˆƒ r : â„, 0 < r âˆ§ y = r â€¢ x := by
  constructor
  Â· intro h
    have hxâ‚€ : x â‰  0 := fun hâ‚€ => by simp [hâ‚€] at h
    have hyâ‚€ : y â‰  0 := fun hâ‚€ => by simp [hâ‚€] at h
    refine âŸ¨hxâ‚€, â€–yâ€– / â€–xâ€–, div_pos (norm_pos_iff.2 hyâ‚€) (norm_pos_iff.2 hxâ‚€), ?_âŸ©
    exact ((inner_eq_norm_mul_iff_div hxâ‚€).1 (eq_of_div_eq_one h)).symm
  Â· rintro âŸ¨hx, âŸ¨r, âŸ¨hr, rflâŸ©âŸ©âŸ©
    exact real_inner_div_norm_mul_norm_eq_one_of_ne_zero_of_pos_mul hx hr

/-- The inner product of two vectors, divided by the product of their
norms, has value -1 if and only if they are nonzero and one is
a negative multiple of the other. -/
theorem real_inner_div_norm_mul_norm_eq_neg_one_iff (x y : F) :
    âŸªx, yâŸ«_â„ / (â€–xâ€– * â€–yâ€–) = -1 â†” x â‰  0 âˆ§ âˆƒ r : â„, r < 0 âˆ§ y = r â€¢ x := by
  rw [â† neg_eq_iff_eq_neg, â† neg_div, â† inner_neg_right, â† norm_neg y,
    real_inner_div_norm_mul_norm_eq_one_iff, (@neg_surjective â„ _).exists]
  refine Iff.rfl.and (exists_congr fun r => ?_)
  rw [neg_pos, neg_smul, neg_inj]

/-- If the inner product of two unit vectors is `1`, then the two vectors are equal. One form of
the equality case for Cauchy-Schwarz. -/
theorem inner_eq_one_iff_of_norm_one {x y : E} (hx : â€–xâ€– = 1) (hy : â€–yâ€– = 1) :
    âŸªx, yâŸ« = 1 â†” x = y := by
  convert inner_eq_norm_mul_iff (ğ•œ := ğ•œ) (E := E) using 2 <;> simp [hx, hy]

theorem inner_lt_norm_mul_iff_real {x y : F} : âŸªx, yâŸ«_â„ < â€–xâ€– * â€–yâ€– â†” â€–yâ€– â€¢ x â‰  â€–xâ€– â€¢ y :=
  calc
    âŸªx, yâŸ«_â„ < â€–xâ€– * â€–yâ€– â†” âŸªx, yâŸ«_â„ â‰  â€–xâ€– * â€–yâ€– :=
      âŸ¨ne_of_lt, lt_of_le_of_ne (real_inner_le_norm _ _)âŸ©
    _ â†” â€–yâ€– â€¢ x â‰  â€–xâ€– â€¢ y := not_congr inner_eq_norm_mul_iff_real

/-- If the inner product of two unit vectors is strictly less than `1`, then the two vectors are
distinct. One form of the equality case for Cauchy-Schwarz. -/
theorem inner_lt_one_iff_real_of_norm_one {x y : F} (hx : â€–xâ€– = 1) (hy : â€–yâ€– = 1) :
    âŸªx, yâŸ«_â„ < 1 â†” x â‰  y := by convert inner_lt_norm_mul_iff_real (F := F) <;> simp [hx, hy]

/-- The sphere of radius `r = â€–yâ€–` is tangent to the plane `âŸªx, yâŸ« = â€–yâ€– ^ 2` at `x = y`. -/
theorem eq_of_norm_le_re_inner_eq_norm_sq {x y : E} (hle : â€–xâ€– â‰¤ â€–yâ€–) (h : re âŸªx, yâŸ« = â€–yâ€– ^ 2) :
    x = y := by
  suffices H : re âŸªx - y, x - yâŸ« â‰¤ 0 by rwa [inner_self_nonpos, sub_eq_zero] at H
  have Hâ‚ : â€–xâ€– ^ 2 â‰¤ â€–yâ€– ^ 2 := by gcongr
  have Hâ‚‚ : re âŸªy, xâŸ« = â€–yâ€– ^ 2 := by rwa [â† inner_conj_symm, conj_re]
  simpa [inner_sub_left, inner_sub_right, â† norm_sq_eq_inner, h, Hâ‚‚] using Hâ‚

end Norm

section RCLike

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

/-- A field `ğ•œ` satisfying `RCLike` is itself a `ğ•œ`-inner product space. -/
instance RCLike.innerProductSpace : InnerProductSpace ğ•œ ğ•œ where
  inner x y := conj x * y
  norm_sq_eq_inner x := by simp only [inner, conj_mul, â† ofReal_pow, ofReal_re]
  conj_symm x y := by simp only [mul_comm, map_mul, starRingEnd_self_apply]
  add_left x y z := by simp only [add_mul, map_add]
  smul_left x y z := by simp only [mul_assoc, smul_eq_mul, map_mul]

@[simp]
theorem RCLike.inner_apply (x y : ğ•œ) : âŸªx, yâŸ« = conj x * y :=
  rfl

end RCLike

section RCLikeToReal

open scoped InnerProductSpace

variable {G : Type*}
variable (ğ•œ E)
variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

/-- A general inner product implies a real inner product. This is not registered as an instance
since it creates problems with the case `ğ•œ = â„`. -/
def Inner.rclikeToReal : Inner â„ E where inner x y := re âŸªx, yâŸ«

/-- A general inner product space structure implies a real inner product structure. This is not
registered as an instance since it creates problems with the case `ğ•œ = â„`, but in can be used in a
proof to obtain a real inner product space structure from a given `ğ•œ`-inner product space
structure. -/
def InnerProductSpace.rclikeToReal : InnerProductSpace â„ E :=
  { Inner.rclikeToReal ğ•œ E,
    NormedSpace.restrictScalars â„ ğ•œ
      E with
    norm_sq_eq_inner := norm_sq_eq_inner
    conj_symm := fun _ _ => inner_re_symm _ _
    add_left := fun x y z => by
      change re âŸªx + y, zâŸ« = re âŸªx, zâŸ« + re âŸªy, zâŸ«
      simp only [inner_add_left, map_add]
    smul_left := fun x y r => by
      change re âŸª(r : ğ•œ) â€¢ x, yâŸ« = r * re âŸªx, yâŸ«
      simp only [inner_smul_left, conj_ofReal, re_ofReal_mul] }

variable {E}

theorem real_inner_eq_re_inner (x y : E) :
    @Inner.inner â„ E (Inner.rclikeToReal ğ•œ E) x y = re âŸªx, yâŸ« :=
  rfl

theorem real_inner_I_smul_self (x : E) :
    @Inner.inner â„ E (Inner.rclikeToReal ğ•œ E) x ((I : ğ•œ) â€¢ x) = 0 := by
  simp [real_inner_eq_re_inner ğ•œ, inner_smul_right]

/-- A complex inner product implies a real inner product. This cannot be an instance since it
creates a diamond with `PiLp.innerProductSpace` because `re (sum i, inner (x i) (y i))` and
`sum i, re (inner (x i) (y i))` are not defeq. -/
def InnerProductSpace.complexToReal [SeminormedAddCommGroup G] [InnerProductSpace â„‚ G] :
    InnerProductSpace â„ G :=
  InnerProductSpace.rclikeToReal â„‚ G

instance : InnerProductSpace â„ â„‚ := InnerProductSpace.complexToReal

@[simp]
protected theorem Complex.inner (w z : â„‚) : âŸªw, zâŸ«_â„ = (conj w * z).re :=
  rfl

/-- The inner product on an inner product space of dimension 2 can be evaluated in terms
of a complex-number representation of the space. -/
theorem inner_map_complex [SeminormedAddCommGroup G] [InnerProductSpace â„ G] (f : G â‰ƒâ‚—áµ¢[â„] â„‚)
    (x y : G) : âŸªx, yâŸ«_â„ = (conj (f x) * f y).re := by rw [â† Complex.inner, f.inner_map_map]

instance : InnerProductSpace â„ â„‚ := InnerProductSpace.complexToReal

end RCLikeToReal

section Continuous

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

/-!
### Continuity of the inner product
-/


theorem continuous_inner : Continuous fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : IsScalarTower â„ ğ•œ E := RestrictScalars.isScalarTower _ _ _
  isBoundedBilinearMap_inner.continuous

variable {Î± : Type*}

theorem Filter.Tendsto.inner {f g : Î± â†’ E} {l : Filter Î±} {x y : E} (hf : Tendsto f l (ğ“ x))
    (hg : Tendsto g l (ğ“ y)) : Tendsto (fun t => âŸªf t, g tâŸ«) l (ğ“ âŸªx, yâŸ«) :=
  (continuous_inner.tendsto _).comp (hf.prod_mk_nhds hg)

variable [TopologicalSpace Î±] {f g : Î± â†’ E} {x : Î±} {s : Set Î±}

theorem ContinuousWithinAt.inner (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun t => âŸªf t, g tâŸ«) s x :=
  Filter.Tendsto.inner hf hg

theorem ContinuousAt.inner (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun t => âŸªf t, g tâŸ«) x :=
  Filter.Tendsto.inner hf hg

theorem ContinuousOn.inner (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun t => âŸªf t, g tâŸ«) s := fun x hx => (hf x hx).inner (hg x hx)

@[continuity]
theorem Continuous.inner (hf : Continuous f) (hg : Continuous g) : Continuous fun t => âŸªf t, g tâŸ« :=
  continuous_iff_continuousAt.2 fun _x => hf.continuousAt.inner hg.continuousAt

end Continuous

section ReApplyInnerSelf

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

/-- Extract a real bilinear form from an operator `T`,
by taking the pairing `fun x â†¦ re âŸªT x, xâŸ«`. -/
def ContinuousLinearMap.reApplyInnerSelf (T : E â†’L[ğ•œ] E) (x : E) : â„ :=
  re âŸªT x, xâŸ«

theorem ContinuousLinearMap.reApplyInnerSelf_apply (T : E â†’L[ğ•œ] E) (x : E) :
    T.reApplyInnerSelf x = re âŸªT x, xâŸ« :=
  rfl

end ReApplyInnerSelf

section ReApplyInnerSelf_Seminormed

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

theorem ContinuousLinearMap.reApplyInnerSelf_continuous (T : E â†’L[ğ•œ] E) :
    Continuous T.reApplyInnerSelf :=
  reCLM.continuous.comp <| T.continuous.inner continuous_id

theorem ContinuousLinearMap.reApplyInnerSelf_smul (T : E â†’L[ğ•œ] E) (x : E) {c : ğ•œ} :
    T.reApplyInnerSelf (c â€¢ x) = â€–câ€– ^ 2 * T.reApplyInnerSelf x := by
  simp only [ContinuousLinearMap.map_smul, ContinuousLinearMap.reApplyInnerSelf_apply,
    inner_smul_left, inner_smul_right, â† mul_assoc, mul_conj, â† ofReal_pow, â† smul_re,
    Algebra.smul_def (â€–câ€– ^ 2) âŸªT x, xâŸ«, algebraMap_eq_ofReal]

end ReApplyInnerSelf_Seminormed
