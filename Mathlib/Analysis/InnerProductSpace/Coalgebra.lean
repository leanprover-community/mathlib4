/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
module

public import Mathlib.Analysis.InnerProductSpace.TensorProduct
public import Mathlib.RingTheory.Coalgebra.Basic

/-!
# Finite-dimensional inner product space with a (co)algebra structure

This file proves that a finite-dimensional inner product space has a
colagebra structure if it has an algebra structure, where
the comultiplication and counit maps are given by taking adjoints of the
multiplication and algebra linear maps, respectively.
This is implemented by providing a linear equivalence between the inner product space
and a normed algebra.

And similarly, a finite-dimensional inner product space has an algebra
structure if it has a coalgebra structure, where `x * y = (adjoint comul) (x âŠ—â‚œ y)`,
`(1 : A) = (adjoint counit) (1 : ğ•œ)` and `algebraMap = adjoint counit`.

This is useful for when we have a finite-dimensional Câ‹†-algebra with a faithful and
positive linear functional (so that it induces an inner product structure), and want the coalgebra
structure to be the _adjoint_ of the algebra structure.
This comes up in non-commutative graph theory for example.
-/

@[expose] public section

variable {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]
  [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]

open TensorProduct LinearMap LinearIsometryEquiv Coalgebra

namespace InnerProductSpace

section coalgebraOfAlgebra
variable {A : Type*} [NormedRing A] [NormedSpace ğ•œ A] [SMulCommClass ğ•œ A A] [IsScalarTower ğ•œ A A]

/- TODO: This does not require submultiplicativity of the norm. When we unbundle the algebra
and analysis hierachies, we should generalise this from `NormedRing` to `Ring`
and `NormedAddCommGroup`.
PR#24040 addresses this. -/
/-- A finite-dimensional inner product space with an algebra structure induces
a coalgebra, where comultiplication is given by the adjoint of multiplication
and the counit is given by the adjoint of the algebra map.

This is implemented by providing a linear equivalence between the inner product
space and a normed algebra.

See note [reducible non-instances]. -/
noncomputable abbrev coalgebraOfAlgebra (e : E â‰ƒâ‚—[ğ•œ] A) : Coalgebra ğ•œ E where
  comul := adjoint (e.symm.toLinearMap âˆ˜â‚— mul' ğ•œ A âˆ˜â‚— map e.toLinearMap e.toLinearMap)
  counit := innerâ‚›â‚— ğ•œ (e.symm 1)
  coassoc := by
    rw [â† adjoint_lTensor, â† adjoint_rTensor, â† toLinearEquiv_assocIsometry,
      â† (assocIsometry ğ•œ _ _ _).symm_symm, â† adjoint_toLinearMap_eq_symm]
    simp_rw [â† adjoint_comp]
    congr 1; ext; simp [mul_assoc]
  rTensor_counit_comp_comul := by
    rw [â† adjoint_toSpanSingleton, â† adjoint_rTensor, â† adjoint_comp, â† toLinearMap_symm_lid,
      â† toLinearEquiv_lidIsometry, â† toLinearEquiv_symm, â† adjoint_toLinearMap_eq_symm]
    congr 1; ext; simp
  lTensor_counit_comp_comul := by
    rw [â† adjoint_toSpanSingleton, â† adjoint_lTensor, â† adjoint_comp, â† toLinearMap_symm_rid,
      â† comm_trans_lid, â† toLinearEquiv_commIsometry, â† toLinearEquiv_lidIsometry,
      â† toLinearEquiv_trans, â† toLinearEquiv_symm, â† adjoint_toLinearMap_eq_symm]
    congr 1; ext; simp

end coalgebraOfAlgebra

section algebraOfCoalgebra
variable [Coalgebra ğ•œ E]

/-- The multiplication on a finite-dimensional inner product space with a coalgebra structure
given by `x * y = (adjoint comul) (x âŠ—â‚œ y)`.

See note [reducible non-instances]. -/
noncomputable abbrev mulOfCoalgebra :
    Mul E where mul x y := adjoint (comul (R := ğ•œ) (A := E)) (x âŠ—â‚œ y)

attribute [local instance] InnerProductSpace.mulOfCoalgebra in
lemma AlgebraOfCoalgebra.mul_def (x y : E) :
    x * y = adjoint (comul (R := ğ•œ) (A := E)) (x âŠ—â‚œ y) := rfl

attribute [local simp] AlgebraOfCoalgebra.mul_def

attribute [local instance] InnerProductSpace.mulOfCoalgebra in
/-- A finite-dimensional inner product space with a coalgebra structure induces a ring structure,
where multiplication is given by `x * y = (adjoint comul) (x âŠ—â‚œ y)` and
`1 = (adjoint counit) (1 : ğ•œ)`.

See note [reducible non-instances]. -/
noncomputable abbrev ringOfCoalgebra :
    Ring E where
  left_distrib x y z := by simp [tmul_add]
  right_distrib x y z := by simp [add_tmul]
  zero_mul x := by simp
  mul_zero x := by simp
  mul_assoc x y z := by
    simp_rw [AlgebraOfCoalgebra.mul_def, â† rTensor_tmul, â† comp_apply, â† adjoint_rTensor,
      â† adjoint_comp, â† coassoc_symm, adjoint_comp, adjoint_lTensor, comp_apply,
      â† toLinearEquiv_assocIsometry, â† toLinearEquiv_symm, adjoint_toLinearMap_eq_symm]
    rfl
  one := adjoint (counit (R := ğ•œ) (A := E)) 1
  one_mul x := by
    dsimp [OfNat.ofNat]
    rw [â† rTensor_tmul, â† comp_apply, â† adjoint_rTensor, â† adjoint_comp, rTensor_counit_comp_comul,
      â† toLinearMap_symm_lid, â† toLinearEquiv_lidIsometry, â† toLinearEquiv_symm,
      adjoint_toLinearMap_eq_symm]
    exact one_smul _ _
  mul_one x := by
    dsimp [OfNat.ofNat]
    rw [â† lTensor_tmul, â† comp_apply, â† adjoint_lTensor, â† adjoint_comp, lTensor_counit_comp_comul,
      â† toLinearMap_symm_rid, â† comm_trans_lid, â† toLinearEquiv_commIsometry,
      â† toLinearEquiv_lidIsometry, â† toLinearEquiv_trans, â† toLinearEquiv_symm,
      adjoint_toLinearMap_eq_symm]
    exact one_smul _ _

attribute [local instance] InnerProductSpace.ringOfCoalgebra in
/-- A finite-dimensional inner product space with a coalgebra structure induces an algebra
structure, where `x * y = (adjoint comul) (x âŠ—â‚œ y)`, `1 = (adjoint counit) 1` and
`algebraMap = adjoint counit`.

See note [reducible non-instances]. -/
noncomputable abbrev algebraOfCoalgebra : Algebra ğ•œ E where
  algebraMap :=
    { toFun := adjoint (Coalgebra.counit (R := ğ•œ) (A := E))
      map_one' := rfl
      map_mul' x y := by
        simp_rw [AlgebraOfCoalgebra.mul_def, â† map_tmul, â† adjoint_map, â† comp_apply,
          â† adjoint_comp, â† lTensor_comp_rTensor, comp_assoc, rTensor_counit_comp_comul,
          adjoint_comp, â† toLinearMap_symm_lid, â† toLinearEquiv_lidIsometry, â† toLinearEquiv_symm,
          adjoint_toLinearMap_eq_symm]
        simp only [LinearIsometryEquiv.symm_symm, toLinearEquiv_lidIsometry, adjoint_lTensor,
          coe_comp, LinearEquiv.coe_coe, Function.comp_apply, lTensor_tmul, lid_tmul]
        rw [â† smul_eq_mul, â† _root_.map_smul]
      map_zero' := map_zero _
      map_add' := map_add _ }
  commutes' r x := by
    dsimp
    simp_rw [â† rTensor_tmul, â† lTensor_tmul, â† adjoint_lTensor, â† adjoint_rTensor,
      â† comp_apply, â† adjoint_comp, rTensor_counit_comp_comul, lTensor_counit_comp_comul,
      â† toLinearMap_symm_rid, â† toLinearMap_symm_lid, â† comm_trans_lid,
      â† toLinearEquiv_commIsometry, â† toLinearEquiv_lidIsometry, â† toLinearEquiv_trans,
      â† toLinearEquiv_symm, adjoint_toLinearMap_eq_symm]
    rfl
  smul_def' r x := by
    dsimp
    simp_rw [â† rTensor_tmul, â† adjoint_rTensor, â† comp_apply, â† adjoint_comp,
      rTensor_counit_comp_comul, â† toLinearMap_symm_lid, â† toLinearEquiv_lidIsometry,
      â† toLinearEquiv_symm, adjoint_toLinearMap_eq_symm]
    rfl

end algebraOfCoalgebra
end InnerProductSpace
