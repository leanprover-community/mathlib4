/-
Copyright (c) 2021 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.InnerProductSpace.PiL2

#align_import analysis.inner_product_space.adjoint from "leanprover-community/mathlib"@"46b633fd842bef9469441c0209906f6dddd2b4f5"

/-!
# Adjoint of operators on Hilbert spaces

Given an operator `A : E â†’L[ğ•œ] F`, where `E` and `F` are Hilbert spaces, its adjoint
`adjoint A : F â†’L[ğ•œ] E` is the unique operator such that `âŸªx, A yâŸ« = âŸªadjoint A x, yâŸ«` for all
`x` and `y`.

We then use this to put a Câ‹†-algebra structure on `E â†’L[ğ•œ] E` with the adjoint as the star
operation.

This construction is used to define an adjoint for linear maps (i.e. not continuous) between
finite dimensional spaces.

## Main definitions

* `ContinuousLinearMap.adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] (F â†’L[ğ•œ] E)`: the adjoint of a continuous
  linear map, bundled as a conjugate-linear isometric equivalence.
* `LinearMap.adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] (F â†’â‚—[ğ•œ] E)`: the adjoint of a linear map between
  finite-dimensional spaces, this time only as a conjugate-linear equivalence, since there is no
  norm defined on these maps.

## Implementation notes

* The continuous conjugate-linear version `adjointAux` is only an intermediate
  definition and is not meant to be used outside this file.

## Tags

adjoint

-/

noncomputable section

open RCLike

open scoped ComplexConjugate

variable {ğ•œ E F G : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F] [InnerProductSpace ğ•œ G]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

/-! ### Adjoint operator -/


open InnerProductSpace

namespace ContinuousLinearMap

variable [CompleteSpace E] [CompleteSpace G]

-- Note: made noncomputable to stop excess compilation
-- leanprover-community/mathlib4#7103
/-- The adjoint, as a continuous conjugate-linear map. This is only meant as an auxiliary
definition for the main definition `adjoint`, where this is bundled as a conjugate-linear isometric
equivalence. -/
noncomputable def adjointAux : (E â†’L[ğ•œ] F) â†’Lâ‹†[ğ•œ] F â†’L[ğ•œ] E :=
  (ContinuousLinearMap.compSL _ _ _ _ _ ((toDual ğ•œ E).symm : NormedSpace.Dual ğ•œ E â†’Lâ‹†[ğ•œ] E)).comp
    (toSesqForm : (E â†’L[ğ•œ] F) â†’L[ğ•œ] F â†’Lâ‹†[ğ•œ] NormedSpace.Dual ğ•œ E)
#align continuous_linear_map.adjoint_aux ContinuousLinearMap.adjointAux

@[simp]
theorem adjointAux_apply (A : E â†’L[ğ•œ] F) (x : F) :
    adjointAux A x = ((toDual ğ•œ E).symm : NormedSpace.Dual ğ•œ E â†’ E) ((toSesqForm A) x) :=
  rfl
#align continuous_linear_map.adjoint_aux_apply ContinuousLinearMap.adjointAux_apply

theorem adjointAux_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªadjointAux A y, xâŸ« = âŸªy, A xâŸ« := by
  rw [adjointAux_apply, toDual_symm_apply, toSesqForm_apply_coe, coe_comp', innerSL_apply_coe,
    Function.comp_apply]
#align continuous_linear_map.adjoint_aux_inner_left ContinuousLinearMap.adjointAux_inner_left

theorem adjointAux_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) :
    âŸªx, adjointAux A yâŸ« = âŸªA x, yâŸ« := by
  rw [â† inner_conj_symm, adjointAux_inner_left, inner_conj_symm]
#align continuous_linear_map.adjoint_aux_inner_right ContinuousLinearMap.adjointAux_inner_right

variable [CompleteSpace F]

theorem adjointAux_adjointAux (A : E â†’L[ğ•œ] F) : adjointAux (adjointAux A) = A := by
  ext v
  refine ext_inner_left ğ•œ fun w => ?_
  rw [adjointAux_inner_right, adjointAux_inner_left]
#align continuous_linear_map.adjoint_aux_adjoint_aux ContinuousLinearMap.adjointAux_adjointAux

@[simp]
theorem adjointAux_norm (A : E â†’L[ğ•œ] F) : â€–adjointAux Aâ€– = â€–Aâ€– := by
  refine le_antisymm ?_ ?_
  Â· refine ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg _) fun x => ?_
    rw [adjointAux_apply, LinearIsometryEquiv.norm_map]
    exact toSesqForm_apply_norm_le
  Â· nth_rw 1 [â† adjointAux_adjointAux A]
    refine ContinuousLinearMap.opNorm_le_bound _ (norm_nonneg _) fun x => ?_
    rw [adjointAux_apply, LinearIsometryEquiv.norm_map]
    exact toSesqForm_apply_norm_le
#align continuous_linear_map.adjoint_aux_norm ContinuousLinearMap.adjointAux_norm

/-- The adjoint of a bounded operator from Hilbert space `E` to Hilbert space `F`. -/
def adjoint : (E â†’L[ğ•œ] F) â‰ƒâ‚—áµ¢â‹†[ğ•œ] F â†’L[ğ•œ] E :=
  LinearIsometryEquiv.ofSurjective { adjointAux with norm_map' := adjointAux_norm } fun A =>
    âŸ¨adjointAux A, adjointAux_adjointAux AâŸ©
#align continuous_linear_map.adjoint ContinuousLinearMap.adjoint

scoped[InnerProduct] postfix:1000 "â€ " => ContinuousLinearMap.adjoint
open InnerProduct

/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_left (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸª(Aâ€ ) y, xâŸ« = âŸªy, A xâŸ« :=
  adjointAux_inner_left A x y
#align continuous_linear_map.adjoint_inner_left ContinuousLinearMap.adjoint_inner_left

/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_right (A : E â†’L[ğ•œ] F) (x : E) (y : F) : âŸªx, (Aâ€ ) yâŸ« = âŸªA x, yâŸ« :=
  adjointAux_inner_right A x y
#align continuous_linear_map.adjoint_inner_right ContinuousLinearMap.adjoint_inner_right

/-- The adjoint is involutive. -/
@[simp]
theorem adjoint_adjoint (A : E â†’L[ğ•œ] F) : Aâ€ â€  = A :=
  adjointAux_adjointAux A
#align continuous_linear_map.adjoint_adjoint ContinuousLinearMap.adjoint_adjoint

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp]
theorem adjoint_comp (A : F â†’L[ğ•œ] G) (B : E â†’L[ğ•œ] F) : (A âˆ˜L B)â€  = Bâ€  âˆ˜L Aâ€  := by
  ext v
  refine ext_inner_left ğ•œ fun w => ?_
  simp only [adjoint_inner_right, ContinuousLinearMap.coe_comp', Function.comp_apply]
#align continuous_linear_map.adjoint_comp ContinuousLinearMap.adjoint_comp

theorem apply_norm_sq_eq_inner_adjoint_left (A : E â†’L[ğ•œ] F) (x : E) :
    â€–A xâ€– ^ 2 = re âŸª(Aâ€  âˆ˜L A) x, xâŸ« := by
  have h : âŸª(Aâ€  âˆ˜L A) x, xâŸ« = âŸªA x, A xâŸ« := by rw [â† adjoint_inner_left]; rfl
  rw [h, â† inner_self_eq_norm_sq (ğ•œ := ğ•œ) _]
#align continuous_linear_map.apply_norm_sq_eq_inner_adjoint_left ContinuousLinearMap.apply_norm_sq_eq_inner_adjoint_left

theorem apply_norm_eq_sqrt_inner_adjoint_left (A : E â†’L[ğ•œ] F) (x : E) :
    â€–A xâ€– = âˆš(re âŸª(Aâ€  âˆ˜L A) x, xâŸ«) := by
  rw [â† apply_norm_sq_eq_inner_adjoint_left, Real.sqrt_sq (norm_nonneg _)]
#align continuous_linear_map.apply_norm_eq_sqrt_inner_adjoint_left ContinuousLinearMap.apply_norm_eq_sqrt_inner_adjoint_left

theorem apply_norm_sq_eq_inner_adjoint_right (A : E â†’L[ğ•œ] F) (x : E) :
    â€–A xâ€– ^ 2 = re âŸªx, (Aâ€  âˆ˜L A) xâŸ« := by
  have h : âŸªx, (Aâ€  âˆ˜L A) xâŸ« = âŸªA x, A xâŸ« := by rw [â† adjoint_inner_right]; rfl
  rw [h, â† inner_self_eq_norm_sq (ğ•œ := ğ•œ) _]
#align continuous_linear_map.apply_norm_sq_eq_inner_adjoint_right ContinuousLinearMap.apply_norm_sq_eq_inner_adjoint_right

theorem apply_norm_eq_sqrt_inner_adjoint_right (A : E â†’L[ğ•œ] F) (x : E) :
    â€–A xâ€– = âˆš(re âŸªx, (Aâ€  âˆ˜L A) xâŸ«) := by
  rw [â† apply_norm_sq_eq_inner_adjoint_right, Real.sqrt_sq (norm_nonneg _)]
#align continuous_linear_map.apply_norm_eq_sqrt_inner_adjoint_right ContinuousLinearMap.apply_norm_eq_sqrt_inner_adjoint_right

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
theorem eq_adjoint_iff (A : E â†’L[ğ•œ] F) (B : F â†’L[ğ•œ] E) : A = Bâ€  â†” âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ« := by
  refine âŸ¨fun h x y => by rw [h, adjoint_inner_left], fun h => ?_âŸ©
  ext x
  exact ext_inner_right ğ•œ fun y => by simp only [adjoint_inner_left, h x y]
#align continuous_linear_map.eq_adjoint_iff ContinuousLinearMap.eq_adjoint_iff

@[simp]
theorem adjoint_id :
    ContinuousLinearMap.adjoint (ContinuousLinearMap.id ğ•œ E) = ContinuousLinearMap.id ğ•œ E := by
  refine Eq.symm ?_
  rw [eq_adjoint_iff]
  simp
#align continuous_linear_map.adjoint_id ContinuousLinearMap.adjoint_id

theorem _root_.Submodule.adjoint_subtypeL (U : Submodule ğ•œ E) [CompleteSpace U] :
    U.subtypeLâ€  = orthogonalProjection U := by
  symm
  rw [eq_adjoint_iff]
  intro x u
  rw [U.coe_inner, inner_orthogonalProjection_left_eq_right,
    orthogonalProjection_mem_subspace_eq_self]
  rfl
set_option linter.uppercaseLean3 false in
#align submodule.adjoint_subtypeL Submodule.adjoint_subtypeL

theorem _root_.Submodule.adjoint_orthogonalProjection (U : Submodule ğ•œ E) [CompleteSpace U] :
    (orthogonalProjection U : E â†’L[ğ•œ] U)â€  = U.subtypeL := by
  rw [â† U.adjoint_subtypeL, adjoint_adjoint]
#align submodule.adjoint_orthogonal_projection Submodule.adjoint_orthogonalProjection

/-- `E â†’L[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : Star (E â†’L[ğ•œ] E) :=
  âŸ¨adjointâŸ©

instance : InvolutiveStar (E â†’L[ğ•œ] E) :=
  âŸ¨adjoint_adjointâŸ©

instance : StarMul (E â†’L[ğ•œ] E) :=
  âŸ¨adjoint_compâŸ©

instance : StarRing (E â†’L[ğ•œ] E) :=
  âŸ¨LinearIsometryEquiv.map_add adjointâŸ©

instance : StarModule ğ•œ (E â†’L[ğ•œ] E) :=
  âŸ¨LinearIsometryEquiv.map_smulâ‚›â‚— adjointâŸ©

theorem star_eq_adjoint (A : E â†’L[ğ•œ] E) : star A = Aâ€  :=
  rfl
#align continuous_linear_map.star_eq_adjoint ContinuousLinearMap.star_eq_adjoint

/-- A continuous linear operator is self-adjoint iff it is equal to its adjoint. -/
theorem isSelfAdjoint_iff' {A : E â†’L[ğ•œ] E} : IsSelfAdjoint A â†” ContinuousLinearMap.adjoint A = A :=
  Iff.rfl
#align continuous_linear_map.is_self_adjoint_iff' ContinuousLinearMap.isSelfAdjoint_iff'

theorem norm_adjoint_comp_self (A : E â†’L[ğ•œ] F) :
    â€–ContinuousLinearMap.adjoint A âˆ˜L Aâ€– = â€–Aâ€– * â€–Aâ€– := by
  refine le_antisymm ?_ ?_
  Â· calc
      â€–Aâ€  âˆ˜L Aâ€– â‰¤ â€–Aâ€ â€– * â€–Aâ€– := opNorm_comp_le _ _
      _ = â€–Aâ€– * â€–Aâ€– := by rw [LinearIsometryEquiv.norm_map]
  Â· rw [â† sq, â† Real.sqrt_le_sqrt_iff (norm_nonneg _), Real.sqrt_sq (norm_nonneg _)]
    refine opNorm_le_bound _ (Real.sqrt_nonneg _) fun x => ?_
    have :=
      calc
        re âŸª(Aâ€  âˆ˜L A) x, xâŸ« â‰¤ â€–(Aâ€  âˆ˜L A) xâ€– * â€–xâ€– := re_inner_le_norm _ _
        _ â‰¤ â€–Aâ€  âˆ˜L Aâ€– * â€–xâ€– * â€–xâ€– := mul_le_mul_of_nonneg_right (le_opNorm _ _) (norm_nonneg _)
    calc
      â€–A xâ€– = âˆš(re âŸª(Aâ€  âˆ˜L A) x, xâŸ«) := by rw [apply_norm_eq_sqrt_inner_adjoint_left]
      _ â‰¤ âˆš(â€–Aâ€  âˆ˜L Aâ€– * â€–xâ€– * â€–xâ€–) := Real.sqrt_le_sqrt this
      _ = âˆšâ€–Aâ€  âˆ˜L Aâ€– * â€–xâ€– := by
        simp_rw [mul_assoc, Real.sqrt_mul (norm_nonneg _) (â€–xâ€– * â€–xâ€–),
          Real.sqrt_mul_self (norm_nonneg x)]

instance : CstarRing (E â†’L[ğ•œ] E) where
  norm_star_mul_self := norm_adjoint_comp_self _

theorem isAdjointPair_inner (A : E â†’L[ğ•œ] F) :
    LinearMap.IsAdjointPair (sesqFormOfInner : E â†’â‚—[ğ•œ] E â†’â‚—â‹†[ğ•œ] ğ•œ)
      (sesqFormOfInner : F â†’â‚—[ğ•œ] F â†’â‚—â‹†[ğ•œ] ğ•œ) A (Aâ€ ) := by
  intro x y
  simp only [sesqFormOfInner_apply_apply, adjoint_inner_left, coe_coe]
#align continuous_linear_map.is_adjoint_pair_inner ContinuousLinearMap.isAdjointPair_inner

end ContinuousLinearMap

/-! ### Self-adjoint operators -/


namespace IsSelfAdjoint

open ContinuousLinearMap

variable [CompleteSpace E] [CompleteSpace F]

theorem adjoint_eq {A : E â†’L[ğ•œ] E} (hA : IsSelfAdjoint A) : ContinuousLinearMap.adjoint A = A :=
  hA
#align is_self_adjoint.adjoint_eq IsSelfAdjoint.adjoint_eq

/-- Every self-adjoint operator on an inner product space is symmetric. -/
theorem isSymmetric {A : E â†’L[ğ•œ] E} (hA : IsSelfAdjoint A) : (A : E â†’â‚—[ğ•œ] E).IsSymmetric := by
  intro x y
  rw_mod_cast [â† A.adjoint_inner_right, hA.adjoint_eq]
#align is_self_adjoint.is_symmetric IsSelfAdjoint.isSymmetric

/-- Conjugating preserves self-adjointness. -/
theorem conj_adjoint {T : E â†’L[ğ•œ] E} (hT : IsSelfAdjoint T) (S : E â†’L[ğ•œ] F) :
    IsSelfAdjoint (S âˆ˜L T âˆ˜L ContinuousLinearMap.adjoint S) := by
  rw [isSelfAdjoint_iff'] at hT âŠ¢
  simp only [hT, adjoint_comp, adjoint_adjoint]
  exact ContinuousLinearMap.comp_assoc _ _ _
#align is_self_adjoint.conj_adjoint IsSelfAdjoint.conj_adjoint

/-- Conjugating preserves self-adjointness. -/
theorem adjoint_conj {T : E â†’L[ğ•œ] E} (hT : IsSelfAdjoint T) (S : F â†’L[ğ•œ] E) :
    IsSelfAdjoint (ContinuousLinearMap.adjoint S âˆ˜L T âˆ˜L S) := by
  rw [isSelfAdjoint_iff'] at hT âŠ¢
  simp only [hT, adjoint_comp, adjoint_adjoint]
  exact ContinuousLinearMap.comp_assoc _ _ _
#align is_self_adjoint.adjoint_conj IsSelfAdjoint.adjoint_conj

theorem _root_.ContinuousLinearMap.isSelfAdjoint_iff_isSymmetric {A : E â†’L[ğ•œ] E} :
    IsSelfAdjoint A â†” (A : E â†’â‚—[ğ•œ] E).IsSymmetric :=
  âŸ¨fun hA => hA.isSymmetric, fun hA =>
    ext fun x => ext_inner_right ğ•œ fun y => (A.adjoint_inner_left y x).symm â–¸ (hA x y).symmâŸ©
#align continuous_linear_map.is_self_adjoint_iff_is_symmetric ContinuousLinearMap.isSelfAdjoint_iff_isSymmetric

theorem _root_.LinearMap.IsSymmetric.isSelfAdjoint {A : E â†’L[ğ•œ] E}
    (hA : (A : E â†’â‚—[ğ•œ] E).IsSymmetric) : IsSelfAdjoint A := by
  rwa [â† ContinuousLinearMap.isSelfAdjoint_iff_isSymmetric] at hA
#align linear_map.is_symmetric.is_self_adjoint LinearMap.IsSymmetric.isSelfAdjoint

/-- The orthogonal projection is self-adjoint. -/
theorem _root_.orthogonalProjection_isSelfAdjoint (U : Submodule ğ•œ E) [CompleteSpace U] :
    IsSelfAdjoint (U.subtypeL âˆ˜L orthogonalProjection U) :=
  (orthogonalProjection_isSymmetric U).isSelfAdjoint
#align orthogonal_projection_is_self_adjoint orthogonalProjection_isSelfAdjoint

theorem conj_orthogonalProjection {T : E â†’L[ğ•œ] E} (hT : IsSelfAdjoint T) (U : Submodule ğ•œ E)
    [CompleteSpace U] :
    IsSelfAdjoint
      (U.subtypeL âˆ˜L orthogonalProjection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L orthogonalProjection U) := by
  rw [â† ContinuousLinearMap.comp_assoc]
  nth_rw 1 [â† (orthogonalProjection_isSelfAdjoint U).adjoint_eq]
  exact hT.adjoint_conj _
#align is_self_adjoint.conj_orthogonal_projection IsSelfAdjoint.conj_orthogonalProjection

end IsSelfAdjoint

namespace LinearMap

variable [CompleteSpace E]
variable {T : E â†’â‚—[ğ•œ] E}

/-- The **Hellinger--Toeplitz theorem**: Construct a self-adjoint operator from an everywhere
  defined symmetric operator. -/
def IsSymmetric.toSelfAdjoint (hT : IsSymmetric T) : selfAdjoint (E â†’L[ğ•œ] E) :=
  âŸ¨âŸ¨T, hT.continuousâŸ©, ContinuousLinearMap.isSelfAdjoint_iff_isSymmetric.mpr hTâŸ©
#align linear_map.is_symmetric.to_self_adjoint LinearMap.IsSymmetric.toSelfAdjoint

theorem IsSymmetric.coe_toSelfAdjoint (hT : IsSymmetric T) : (hT.toSelfAdjoint : E â†’â‚—[ğ•œ] E) = T :=
  rfl
#align linear_map.is_symmetric.coe_to_self_adjoint LinearMap.IsSymmetric.coe_toSelfAdjoint

theorem IsSymmetric.toSelfAdjoint_apply (hT : IsSymmetric T) {x : E} :
    (hT.toSelfAdjoint : E â†’ E) x = T x :=
  rfl
#align linear_map.is_symmetric.to_self_adjoint_apply LinearMap.IsSymmetric.toSelfAdjoint_apply

end LinearMap

namespace LinearMap

variable [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F] [FiniteDimensional ğ•œ G]

/- Porting note: Lean can't use `FiniteDimensional.complete` since it was generalized to topological
vector spaces. Use local instances instead. -/

/-- The adjoint of an operator from the finite-dimensional inner product space `E` to the
finite-dimensional inner product space `F`. -/
def adjoint : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—â‹†[ğ•œ] F â†’â‚—[ğ•œ] E :=
  have := FiniteDimensional.complete ğ•œ E
  have := FiniteDimensional.complete ğ•œ F
  /- Note: Instead of the two instances above, the following works:
    ```
      have := FiniteDimensional.complete ğ•œ
      have := FiniteDimensional.complete ğ•œ
    ```
    But removing one of the `have`s makes it fail. The reason is that `E` and `F` don't live
    in the same universe, so the first `have` can no longer be used for `F` after its universe
    metavariable has been assigned to that of `E`!
  -/
  ((LinearMap.toContinuousLinearMap : (E â†’â‚—[ğ•œ] F) â‰ƒâ‚—[ğ•œ] E â†’L[ğ•œ] F).trans
      ContinuousLinearMap.adjoint.toLinearEquiv).trans
    LinearMap.toContinuousLinearMap.symm
#align linear_map.adjoint LinearMap.adjoint

theorem adjoint_toContinuousLinearMap (A : E â†’â‚—[ğ•œ] F) :
    haveI := FiniteDimensional.complete ğ•œ E
    haveI := FiniteDimensional.complete ğ•œ F
    LinearMap.toContinuousLinearMap (LinearMap.adjoint A) =
      ContinuousLinearMap.adjoint (LinearMap.toContinuousLinearMap A) :=
  rfl
#align linear_map.adjoint_to_continuous_linear_map LinearMap.adjoint_toContinuousLinearMap

theorem adjoint_eq_toCLM_adjoint (A : E â†’â‚—[ğ•œ] F) :
    haveI := FiniteDimensional.complete ğ•œ E
    haveI := FiniteDimensional.complete ğ•œ F
    LinearMap.adjoint A = ContinuousLinearMap.adjoint (LinearMap.toContinuousLinearMap A) :=
  rfl
#align linear_map.adjoint_eq_to_clm_adjoint LinearMap.adjoint_eq_toCLM_adjoint

/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_left (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªadjoint A y, xâŸ« = âŸªy, A xâŸ« := by
  haveI := FiniteDimensional.complete ğ•œ E
  haveI := FiniteDimensional.complete ğ•œ F
  rw [â† coe_toContinuousLinearMap A, adjoint_eq_toCLM_adjoint]
  exact ContinuousLinearMap.adjoint_inner_left _ x y
#align linear_map.adjoint_inner_left LinearMap.adjoint_inner_left

/-- The fundamental property of the adjoint. -/
theorem adjoint_inner_right (A : E â†’â‚—[ğ•œ] F) (x : E) (y : F) : âŸªx, adjoint A yâŸ« = âŸªA x, yâŸ« := by
  haveI := FiniteDimensional.complete ğ•œ E
  haveI := FiniteDimensional.complete ğ•œ F
  rw [â† coe_toContinuousLinearMap A, adjoint_eq_toCLM_adjoint]
  exact ContinuousLinearMap.adjoint_inner_right _ x y
#align linear_map.adjoint_inner_right LinearMap.adjoint_inner_right

/-- The adjoint is involutive. -/
@[simp]
theorem adjoint_adjoint (A : E â†’â‚—[ğ•œ] F) : LinearMap.adjoint (LinearMap.adjoint A) = A := by
  ext v
  refine ext_inner_left ğ•œ fun w => ?_
  rw [adjoint_inner_right, adjoint_inner_left]
#align linear_map.adjoint_adjoint LinearMap.adjoint_adjoint

/-- The adjoint of the composition of two operators is the composition of the two adjoints
in reverse order. -/
@[simp]
theorem adjoint_comp (A : F â†’â‚—[ğ•œ] G) (B : E â†’â‚—[ğ•œ] F) :
    LinearMap.adjoint (A âˆ˜â‚— B) = LinearMap.adjoint B âˆ˜â‚— LinearMap.adjoint A := by
  ext v
  refine ext_inner_left ğ•œ fun w => ?_
  simp only [adjoint_inner_right, LinearMap.coe_comp, Function.comp_apply]
#align linear_map.adjoint_comp LinearMap.adjoint_comp

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all `x` and `y`. -/
theorem eq_adjoint_iff (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
    A = LinearMap.adjoint B â†” âˆ€ x y, âŸªA x, yâŸ« = âŸªx, B yâŸ« := by
  refine âŸ¨fun h x y => by rw [h, adjoint_inner_left], fun h => ?_âŸ©
  ext x
  exact ext_inner_right ğ•œ fun y => by simp only [adjoint_inner_left, h x y]
#align linear_map.eq_adjoint_iff LinearMap.eq_adjoint_iff

/-- The adjoint is unique: a map `A` is the adjoint of `B` iff it satisfies `âŸªA x, yâŸ« = âŸªx, B yâŸ«`
for all basis vectors `x` and `y`. -/
theorem eq_adjoint_iff_basis {Î¹â‚ : Type*} {Î¹â‚‚ : Type*} (bâ‚ : Basis Î¹â‚ ğ•œ E) (bâ‚‚ : Basis Î¹â‚‚ ğ•œ F)
    (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
    A = LinearMap.adjoint B â†” âˆ€ (iâ‚ : Î¹â‚) (iâ‚‚ : Î¹â‚‚), âŸªA (bâ‚ iâ‚), bâ‚‚ iâ‚‚âŸ« = âŸªbâ‚ iâ‚, B (bâ‚‚ iâ‚‚)âŸ« := by
  refine âŸ¨fun h x y => by rw [h, adjoint_inner_left], fun h => ?_âŸ©
  refine Basis.ext bâ‚ fun iâ‚ => ?_
  exact ext_inner_right_basis bâ‚‚ fun iâ‚‚ => by simp only [adjoint_inner_left, h iâ‚ iâ‚‚]
#align linear_map.eq_adjoint_iff_basis LinearMap.eq_adjoint_iff_basis

theorem eq_adjoint_iff_basis_left {Î¹ : Type*} (b : Basis Î¹ ğ•œ E) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
    A = LinearMap.adjoint B â†” âˆ€ i y, âŸªA (b i), yâŸ« = âŸªb i, B yâŸ« := by
  refine âŸ¨fun h x y => by rw [h, adjoint_inner_left], fun h => Basis.ext b fun i => ?_âŸ©
  exact ext_inner_right ğ•œ fun y => by simp only [h i, adjoint_inner_left]
#align linear_map.eq_adjoint_iff_basis_left LinearMap.eq_adjoint_iff_basis_left

theorem eq_adjoint_iff_basis_right {Î¹ : Type*} (b : Basis Î¹ ğ•œ F) (A : E â†’â‚—[ğ•œ] F) (B : F â†’â‚—[ğ•œ] E) :
    A = LinearMap.adjoint B â†” âˆ€ i x, âŸªA x, b iâŸ« = âŸªx, B (b i)âŸ« := by
  refine âŸ¨fun h x y => by rw [h, adjoint_inner_left], fun h => ?_âŸ©
  ext x
  exact ext_inner_right_basis b fun i => by simp only [h i, adjoint_inner_left]
#align linear_map.eq_adjoint_iff_basis_right LinearMap.eq_adjoint_iff_basis_right

/-- `E â†’â‚—[ğ•œ] E` is a star algebra with the adjoint as the star operation. -/
instance : Star (E â†’â‚—[ğ•œ] E) :=
  âŸ¨adjointâŸ©

instance : InvolutiveStar (E â†’â‚—[ğ•œ] E) :=
  âŸ¨adjoint_adjointâŸ©

instance : StarMul (E â†’â‚—[ğ•œ] E) :=
  âŸ¨adjoint_compâŸ©

instance : StarRing (E â†’â‚—[ğ•œ] E) :=
  âŸ¨LinearEquiv.map_add adjointâŸ©

instance : StarModule ğ•œ (E â†’â‚—[ğ•œ] E) :=
  âŸ¨LinearEquiv.map_smulâ‚›â‚— adjointâŸ©

theorem star_eq_adjoint (A : E â†’â‚—[ğ•œ] E) : star A = LinearMap.adjoint A :=
  rfl
#align linear_map.star_eq_adjoint LinearMap.star_eq_adjoint

/-- A continuous linear operator is self-adjoint iff it is equal to its adjoint. -/
theorem isSelfAdjoint_iff' {A : E â†’â‚—[ğ•œ] E} : IsSelfAdjoint A â†” LinearMap.adjoint A = A :=
  Iff.rfl
#align linear_map.is_self_adjoint_iff' LinearMap.isSelfAdjoint_iff'

theorem isSymmetric_iff_isSelfAdjoint (A : E â†’â‚—[ğ•œ] E) : IsSymmetric A â†” IsSelfAdjoint A := by
  rw [isSelfAdjoint_iff', IsSymmetric, â† LinearMap.eq_adjoint_iff]
  exact eq_comm
#align linear_map.is_symmetric_iff_is_self_adjoint LinearMap.isSymmetric_iff_isSelfAdjoint

theorem isAdjointPair_inner (A : E â†’â‚—[ğ•œ] F) :
    IsAdjointPair (sesqFormOfInner : E â†’â‚—[ğ•œ] E â†’â‚—â‹†[ğ•œ] ğ•œ) (sesqFormOfInner : F â†’â‚—[ğ•œ] F â†’â‚—â‹†[ğ•œ] ğ•œ) A
      (LinearMap.adjoint A) := by
  intro x y
  simp only [sesqFormOfInner_apply_apply, adjoint_inner_left]
#align linear_map.is_adjoint_pair_inner LinearMap.isAdjointPair_inner

/-- The Gram operator Tâ€ T is symmetric. -/
theorem isSymmetric_adjoint_mul_self (T : E â†’â‚—[ğ•œ] E) : IsSymmetric (LinearMap.adjoint T * T) := by
  intro x y
  simp only [mul_apply, adjoint_inner_left, adjoint_inner_right]
#align linear_map.is_symmetric_adjoint_mul_self LinearMap.isSymmetric_adjoint_mul_self

/-- The Gram operator Tâ€ T is a positive operator. -/
theorem re_inner_adjoint_mul_self_nonneg (T : E â†’â‚—[ğ•œ] E) (x : E) :
    0 â‰¤ re âŸªx, (LinearMap.adjoint T * T) xâŸ« := by
  simp only [mul_apply, adjoint_inner_right, inner_self_eq_norm_sq_to_K]
  norm_cast
  exact sq_nonneg _
#align linear_map.re_inner_adjoint_mul_self_nonneg LinearMap.re_inner_adjoint_mul_self_nonneg

@[simp]
theorem im_inner_adjoint_mul_self_eq_zero (T : E â†’â‚—[ğ•œ] E) (x : E) :
    im âŸªx, LinearMap.adjoint T (T x)âŸ« = 0 := by
  simp only [mul_apply, adjoint_inner_right, inner_self_eq_norm_sq_to_K]
  norm_cast
#align linear_map.im_inner_adjoint_mul_self_eq_zero LinearMap.im_inner_adjoint_mul_self_eq_zero

end LinearMap

section Unitary

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] [CompleteSpace H]

namespace ContinuousLinearMap

variable {K : Type*} [NormedAddCommGroup K] [InnerProductSpace ğ•œ K] [CompleteSpace K]

theorem inner_map_map_iff_adjoint_comp_self (u : H â†’L[ğ•œ] K) :
    (âˆ€ x y : H, âŸªu x, u yâŸ«_ğ•œ = âŸªx, yâŸ«_ğ•œ) â†” adjoint u âˆ˜L u = 1 := by
  refine âŸ¨fun h â†¦ ext fun x â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· refine ext_inner_right ğ•œ fun y â†¦ ?_
    simpa [star_eq_adjoint, adjoint_inner_left] using h x y
  Â· simp [â† adjoint_inner_left, â† comp_apply, h]

theorem norm_map_iff_adjoint_comp_self (u : H â†’L[ğ•œ] K) :
    (âˆ€ x : H, â€–u xâ€– = â€–xâ€–) â†” adjoint u âˆ˜L u = 1 := by
  rw [LinearMap.norm_map_iff_inner_map_map u, u.inner_map_map_iff_adjoint_comp_self]

@[simp]
lemma _root_.LinearIsometryEquiv.adjoint_eq_symm (e : H â‰ƒâ‚—áµ¢[ğ•œ] K) :
    adjoint (e : H â†’L[ğ•œ] K) = e.symm :=
  let e' := (e : H â†’L[ğ•œ] K)
  calc
    adjoint e' = adjoint e' âˆ˜L (e' âˆ˜L e.symm) := by
      convert (adjoint e').comp_id.symm
      ext
      simp [e']
    _ = e.symm := by
      rw [â† comp_assoc, norm_map_iff_adjoint_comp_self e' |>.mp e.norm_map]
      exact (e.symm : K â†’L[ğ•œ] H).id_comp

@[simp]
lemma _root_.LinearIsometryEquiv.star_eq_symm (e : H â‰ƒâ‚—áµ¢[ğ•œ] H) :
    star (e : H â†’L[ğ•œ] H) = e.symm :=
  e.adjoint_eq_symm

theorem norm_map_of_mem_unitary {u : H â†’L[ğ•œ] H} (hu : u âˆˆ unitary (H â†’L[ğ•œ] H)) (x : H) :
    â€–u xâ€– = â€–xâ€– :=
  -- Elaborates faster with this broken out #11299
  have := unitary.star_mul_self_of_mem hu
  u.norm_map_iff_adjoint_comp_self.mpr this x

theorem inner_map_map_of_mem_unitary {u : H â†’L[ğ•œ] H} (hu : u âˆˆ unitary (H â†’L[ğ•œ] H)) (x y : H) :
    âŸªu x, u yâŸ«_ğ•œ = âŸªx, yâŸ«_ğ•œ :=
  -- Elaborates faster with this broken out #11299
  have := unitary.star_mul_self_of_mem hu
  u.inner_map_map_iff_adjoint_comp_self.mpr this x y

end ContinuousLinearMap

namespace unitary

theorem norm_map (u : unitary (H â†’L[ğ•œ] H)) (x : H) : â€–(u : H â†’L[ğ•œ] H) xâ€– = â€–xâ€– :=
  u.val.norm_map_of_mem_unitary u.property x

theorem inner_map_map (u : unitary (H â†’L[ğ•œ] H)) (x y : H) :
    âŸª(u : H â†’L[ğ•œ] H) x, (u : H â†’L[ğ•œ] H) yâŸ«_ğ•œ = âŸªx, yâŸ«_ğ•œ :=
  u.val.inner_map_map_of_mem_unitary u.property x y

/-- The unitary elements of continuous linear maps on a Hilbert space coincide with the linear
isometric equivalences on that Hilbert space. -/
noncomputable def linearIsometryEquiv : unitary (H â†’L[ğ•œ] H) â‰ƒ* (H â‰ƒâ‚—áµ¢[ğ•œ] H) where
  toFun u :=
    { (u : H â†’L[ğ•œ] H) with
      norm_map' := norm_map u
      invFun := â†‘(star u)
      left_inv := fun x â†¦ congr($(star_mul_self u).val x)
      right_inv := fun x â†¦ congr($(mul_star_self u).val x) }
  invFun e :=
    { val := e
      property := by
        let e' : (H â†’L[ğ•œ] H)Ë£ :=
          { val := (e : H â†’L[ğ•œ] H)
            inv := (e.symm : H â†’L[ğ•œ] H)
            val_inv := by ext; simp
            inv_val := by ext; simp }
        exact IsUnit.mem_unitary_of_star_mul_self âŸ¨e', rflâŸ© <|
          (e : H â†’L[ğ•œ] H).norm_map_iff_adjoint_comp_self.mp e.norm_map }
  left_inv u := Subtype.ext rfl
  right_inv e := LinearIsometryEquiv.ext fun x â†¦ rfl
  map_mul' u v := by ext; rfl

@[simp]
lemma linearIsometryEquiv_coe_apply (u : unitary (H â†’L[ğ•œ] H)) :
    linearIsometryEquiv u = (u : H â†’L[ğ•œ] H) :=
  rfl

@[simp]
lemma linearIsometryEquiv_coe_symm_apply (e : H â‰ƒâ‚—áµ¢[ğ•œ] H) :
    linearIsometryEquiv.symm e = (e : H â†’L[ğ•œ] H) :=
  rfl

end unitary

end Unitary

section Matrix

open Matrix LinearMap

variable {m n : Type*} [Fintype m] [DecidableEq m] [Fintype n] [DecidableEq n]
variable [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]
variable (vâ‚ : OrthonormalBasis n ğ•œ E) (vâ‚‚ : OrthonormalBasis m ğ•œ F)

/-- The linear map associated to the conjugate transpose of a matrix corresponding to two
orthonormal bases is the adjoint of the linear map associated to the matrix. -/
lemma Matrix.toLin_conjTranspose (A : Matrix m n ğ•œ) :
    toLin vâ‚‚.toBasis vâ‚.toBasis Aá´´ = adjoint (toLin vâ‚.toBasis vâ‚‚.toBasis A) := by
  refine eq_adjoint_iff_basis vâ‚‚.toBasis vâ‚.toBasis _ _ |>.mpr fun i j â†¦ ?_
  simp_rw [toLin_self]
  simp [sum_inner, inner_smul_left, inner_sum, inner_smul_right,
    orthonormal_iff_ite.mp vâ‚.orthonormal, orthonormal_iff_ite.mp vâ‚‚.orthonormal]

/-- The matrix associated to the adjoint of a linear map corresponding to two orthonormal bases
is the conjugate tranpose of the matrix associated to the linear map. -/
lemma LinearMap.toMatrix_adjoint (f : E â†’â‚—[ğ•œ] F) :
    toMatrix vâ‚‚.toBasis vâ‚.toBasis (adjoint f) = (toMatrix vâ‚.toBasis vâ‚‚.toBasis f)á´´ :=
  toLin vâ‚‚.toBasis vâ‚.toBasis |>.injective <| by simp [toLin_conjTranspose]

/-- The star algebra equivalence between the linear endomorphisms of finite-dimensional inner
product space and square matrices induced by the choice of an orthonormal basis. -/
@[simps]
def LinearMap.toMatrixOrthonormal : (E â†’â‚—[ğ•œ] E) â‰ƒâ‹†â‚[ğ•œ] Matrix n n ğ•œ :=
  { LinearMap.toMatrix vâ‚.toBasis vâ‚.toBasis with
    map_mul' := LinearMap.toMatrix_mul vâ‚.toBasis
    map_star' := LinearMap.toMatrix_adjoint vâ‚ vâ‚ }

open scoped ComplexConjugate

/-- The adjoint of the linear map associated to a matrix is the linear map associated to the
conjugate transpose of that matrix. -/
theorem Matrix.toEuclideanLin_conjTranspose_eq_adjoint (A : Matrix m n ğ•œ) :
    Matrix.toEuclideanLin A.conjTranspose = LinearMap.adjoint (Matrix.toEuclideanLin A) :=
  A.toLin_conjTranspose (EuclideanSpace.basisFun n ğ•œ) (EuclideanSpace.basisFun m ğ•œ)
#align matrix.to_euclidean_lin_conj_transpose_eq_adjoint Matrix.toEuclideanLin_conjTranspose_eq_adjoint

end Matrix
