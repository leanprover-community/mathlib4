/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.LinearAlgebra.Basis.MulOpposite

/-!
# Inner product space on `H·µê·µí·µñ`

This file defines the inner product space structure on `H·µê·µí·µñ` where we define
the inner product naturally. We also define `OrthonormalBasis.mulOpposite`.
-/

variable {R ùïú H : Type*}

namespace MulOpposite

open MulOpposite

/-- The inner product of `H·µê·µí·µñ` is given by `‚ü™x, y‚ü´ ‚Ü¶ ‚ü™x.unop, y.unop‚ü´`. -/
instance [Inner ùïú H] : Inner ùïú H·µê·µí·µñ where inner x y := inner ùïú x.unop y.unop

@[simp] theorem inner_unop [Inner ùïú H] (x y : H·µê·µí·µñ) : inner ùïú x.unop y.unop = inner ùïú x y := rfl

@[simp] theorem inner_op [Inner ùïú H] (x y : H) : inner ùïú (op x) (op y) = inner ùïú x y := rfl

variable [SeminormedAddCommGroup H]

section InnerProductSpace
variable [RCLike ùïú] [InnerProductSpace ùïú H]

instance : InnerProductSpace ùïú H·µê·µí·µñ where
  norm_sq_eq_re_inner x := (inner_self_eq_norm_sq x.unop).symm
  conj_inner_symm x y := InnerProductSpace.conj_inner_symm x.unop y.unop
  add_left x y z := InnerProductSpace.add_left x.unop y.unop z.unop
  smul_left x y r := InnerProductSpace.smul_left x.unop y.unop r

section orthonormal

theorem _root_.Module.Basis.mulOpposite_is_orthonormal_iff {Œπ : Type*} (b : Module.Basis Œπ ùïú H) :
    Orthonormal ùïú b.mulOpposite ‚Üî Orthonormal ùïú b := Iff.rfl

variable {Œπ H : Type*} [NormedAddCommGroup H] [InnerProductSpace ùïú H] [Fintype Œπ]

/-- The multiplicative opposite of an orthonormal basis `b`, i.e., `b i ‚Ü¶ op (b i)`. -/
noncomputable def _root_.OrthonormalBasis.mulOpposite (b : OrthonormalBasis Œπ ùïú H) :
    OrthonormalBasis Œπ ùïú H·µê·µí·µñ := b.toBasis.mulOpposite.toOrthonormalBasis b.orthonormal

@[simp] lemma _root_.OrthonormalBasis.toBasis_mulOpposite (b : OrthonormalBasis Œπ ùïú H) :
    b.mulOpposite.toBasis = b.toBasis.mulOpposite := rfl

end orthonormal
end InnerProductSpace

theorem isometry_opLinearEquiv {R M : Type*} [Semiring R] [SeminormedAddCommGroup M] [Module R M] :
    Isometry (opLinearEquiv R (M := M)) := fun _ _ => rfl

variable [Semiring R] [Module R H]

variable (R H) in
/-- The linear isometry equivalence version of the function `op`. -/
@[simps!]
def opLinearIsometryEquiv : H ‚âÉ‚Çó·µ¢[R] H·µê·µí·µñ where
  toLinearEquiv := opLinearEquiv R
  norm_map' _ := rfl

@[simp]
theorem toLinearEquiv_opLinearIsometryEquiv :
    (opLinearIsometryEquiv R H).toLinearEquiv = opLinearEquiv R := rfl

@[simp]
theorem toContinuousLinearEquiv_opLinearIsometryEquiv :
    (opLinearIsometryEquiv R H).toContinuousLinearEquiv = opContinuousLinearEquiv R := rfl

end MulOpposite
