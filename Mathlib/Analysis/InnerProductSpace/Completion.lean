/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, S√©bastien Gou√´zel, Fr√©d√©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.Analysis.Normed.Module.Completion

/-!
# Completion of an inner product space

We show that the separation quotient and the completion of an inner product space are inner
product spaces.
-/

noncomputable section

variable {ùïú E F : Type*} [RCLike ùïú]

local notation "‚ü™" x ", " y "‚ü´" => inner ùïú x y

section SeparationQuotient
variable [SeminormedAddCommGroup E] [InnerProductSpace ùïú E]

theorem Inseparable.inner_eq_inner {x‚ÇÅ x‚ÇÇ y‚ÇÅ y‚ÇÇ : E}
    (hx : Inseparable x‚ÇÅ x‚ÇÇ) (hy : Inseparable y‚ÇÅ y‚ÇÇ) :
    ‚ü™x‚ÇÅ, y‚ÇÅ‚ü´ = ‚ü™x‚ÇÇ, y‚ÇÇ‚ü´ :=
  ((hx.prod hy).map continuous_inner).eq

namespace SeparationQuotient

instance : Inner ùïú (SeparationQuotient E) where
  inner := SeparationQuotient.lift‚ÇÇ (inner ùïú) fun _ _ _ _ => Inseparable.inner_eq_inner

@[simp]
theorem inner_mk_mk (x y : E) :
    ‚ü™mk x, mk y‚ü´ = ‚ü™x, y‚ü´ := rfl

instance : InnerProductSpace ùïú (SeparationQuotient E) where
  norm_sq_eq_re_inner := Quotient.ind norm_sq_eq_re_inner
  conj_inner_symm := Quotient.ind‚ÇÇ inner_conj_symm
  add_left := Quotient.ind fun x => Quotient.ind‚ÇÇ <| inner_add_left x
  smul_left := Quotient.ind‚ÇÇ inner_smul_left

end SeparationQuotient

end SeparationQuotient

section UniformSpace.Completion

variable [SeminormedAddCommGroup E] [InnerProductSpace ùïú E]

namespace UniformSpace.Completion

open RCLike Function

instance toInner {ùïú' E' : Type*} [TopologicalSpace ùïú'] [UniformSpace E'] [Inner ùïú' E'] :
    Inner ùïú' (Completion E') where
  inner := curry <| (isDenseInducing_coe.prodMap isDenseInducing_coe).extend (uncurry (inner ùïú'))

@[simp]
theorem inner_coe (a b : E) : ‚ü™(a : Completion E), (b : Completion E)‚ü´ = ‚ü™a, b‚ü´ :=
  (isDenseInducing_coe.prodMap isDenseInducing_coe).extend_eq
    (continuous_inner : Continuous (uncurry (inner ùïú))) (a, b)

protected theorem continuous_inner :
    Continuous (uncurry (inner ùïú (E := Completion E))) := by
  let inner' : E ‚Üí+ E ‚Üí+ ùïú :=
    { toFun := fun x => (inner‚Çõ‚Çó ùïú x).toAddMonoidHom
      map_zero' := by ext x; exact inner_zero_left _
      map_add' := fun x y => by ext z; exact inner_add_left _ _ _ }
  have : Continuous fun p : E √ó E => inner' p.1 p.2 := continuous_inner
  rw [Completion.toInner, inner, uncurry_curry _]
  change
    Continuous
      (((isDenseInducing_toCompl E).prodMap (isDenseInducing_toCompl E)).extend fun p : E √ó E =>
        inner' p.1 p.2)
  exact (isDenseInducing_toCompl E).extend_Z_bilin (isDenseInducing_toCompl E) this

@[fun_prop]
protected theorem Continuous.inner {Œ± : Type*} [TopologicalSpace Œ±] {f g : Œ± ‚Üí Completion E}
    (hf : Continuous f) (hg : Continuous g) : Continuous (fun x : Œ± => ‚ü™f x, g x‚ü´) :=
  UniformSpace.Completion.continuous_inner.comp (hf.prodMk hg :)

instance innerProductSpace : InnerProductSpace ùïú (Completion E) where
  norm_sq_eq_re_inner x :=
    Completion.induction_on x (isClosed_eq (by fun_prop) (by fun_prop))
      fun a => by simp only [norm_coe, inner_coe, inner_self_eq_norm_sq]
  conj_inner_symm x y :=
    Completion.induction_on‚ÇÇ x y
      (isClosed_eq (continuous_conj.comp (by fun_prop)) (by fun_prop))
      fun a b => by simp only [inner_coe, inner_conj_symm]
  add_left x y z :=
    Completion.induction_on‚ÇÉ x y z (isClosed_eq (by fun_prop) (by fun_prop))
      fun a b c => by simp only [‚Üê coe_add, inner_coe, inner_add_left]
  smul_left x y c :=
    Completion.induction_on‚ÇÇ x y
      (isClosed_eq (Continuous.inner (continuous_fst.const_smul c) continuous_snd)
        ((continuous_mul_left _).comp (by fun_prop)))
      fun a b => by simp only [‚Üê coe_smul c a, inner_coe, inner_smul_left]

end UniformSpace.Completion

end UniformSpace.Completion
