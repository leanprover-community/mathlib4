/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.Analysis.Normed.Module.Completion

/-!
# Completion of an inner product space

We show that the separation quotient and the completion of an inner product space are inner
product spaces.
-/

noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp
open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

section SeparationQuotient
variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

theorem Inseparable.inner_eq_inner {xâ‚ xâ‚‚ yâ‚ yâ‚‚ : E}
    (hx : Inseparable xâ‚ xâ‚‚) (hy : Inseparable yâ‚ yâ‚‚) :
    inner xâ‚ yâ‚ = (inner xâ‚‚ yâ‚‚ : ğ•œ) :=
  ((hx.prod hy).map continuous_inner).eq

namespace SeparationQuotient

instance : Inner ğ•œ (SeparationQuotient E) where
  inner := SeparationQuotient.liftâ‚‚ Inner.inner fun _ _ _ _ => Inseparable.inner_eq_inner

@[simp]
theorem inner_mk_mk (x y : E) :
    inner (mk x) (mk y) = (inner x y : ğ•œ) := rfl

instance : InnerProductSpace ğ•œ (SeparationQuotient E) where
  norm_sq_eq_inner := Quotient.ind norm_sq_eq_inner
  conj_symm := Quotient.indâ‚‚ inner_conj_symm
  add_left := Quotient.ind fun x => Quotient.indâ‚‚ <| inner_add_left x
  smul_left := Quotient.indâ‚‚ inner_smul_left

end SeparationQuotient

end SeparationQuotient

section UniformSpace.Completion

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

local notation "IK" => @RCLike.I ğ•œ _

local postfix:90 "â€ " => starRingEnd _

namespace UniformSpace.Completion

open UniformSpace Function

instance toInner {ğ•œ' E' : Type*} [TopologicalSpace ğ•œ'] [UniformSpace E'] [Inner ğ•œ' E'] :
    Inner ğ•œ' (Completion E') where
  inner := curry <| (isDenseInducing_coe.prodMap isDenseInducing_coe).extend (uncurry inner)

@[simp]
theorem inner_coe (a b : E) : inner (a : Completion E) (b : Completion E) = (inner a b : ğ•œ) :=
  (isDenseInducing_coe.prodMap isDenseInducing_coe).extend_eq
    (continuous_inner : Continuous (uncurry inner : E Ã— E â†’ ğ•œ)) (a, b)

protected theorem continuous_inner :
    Continuous (uncurry inner : Completion E Ã— Completion E â†’ ğ•œ) := by
  let inner' : E â†’+ E â†’+ ğ•œ :=
    { toFun := fun x => (innerâ‚›â‚— ğ•œ x).toAddMonoidHom
      map_zero' := by ext x; exact inner_zero_left _
      map_add' := fun x y => by ext z; exact inner_add_left _ _ _ }
  have : Continuous fun p : E Ã— E => inner' p.1 p.2 := continuous_inner
  rw [Completion.toInner, inner, uncurry_curry _]
  change
    Continuous
      (((isDenseInducing_toCompl E).prodMap (isDenseInducing_toCompl E)).extend fun p : E Ã— E =>
        inner' p.1 p.2)
  exact (isDenseInducing_toCompl E).extend_Z_bilin (isDenseInducing_toCompl E) this

protected theorem Continuous.inner {Î± : Type*} [TopologicalSpace Î±] {f g : Î± â†’ Completion E}
    (hf : Continuous f) (hg : Continuous g) : Continuous (fun x : Î± => inner (f x) (g x) : Î± â†’ ğ•œ) :=
  UniformSpace.Completion.continuous_inner.comp (hf.prod_mk hg : _)

instance innerProductSpace : InnerProductSpace ğ•œ (Completion E) where
  norm_sq_eq_inner x :=
    Completion.induction_on x
      (isClosed_eq (continuous_norm.pow 2)
        (continuous_re.comp (Continuous.inner continuous_id' continuous_id')))
      fun a => by simp only [norm_coe, inner_coe, inner_self_eq_norm_sq]
  conj_symm x y :=
    Completion.induction_onâ‚‚ x y
      (isClosed_eq (continuous_conj.comp (Continuous.inner continuous_snd continuous_fst))
        (Continuous.inner continuous_fst continuous_snd))
      fun a b => by simp only [inner_coe, inner_conj_symm]
  add_left x y z :=
    Completion.induction_onâ‚ƒ x y z
      (isClosed_eq
        (Continuous.inner (continuous_fst.add (continuous_fst.comp continuous_snd))
          (continuous_snd.comp continuous_snd))
        ((Continuous.inner continuous_fst (continuous_snd.comp continuous_snd)).add
          (Continuous.inner (continuous_fst.comp continuous_snd)
            (continuous_snd.comp continuous_snd))))
      fun a b c => by simp only [â† coe_add, inner_coe, inner_add_left]
  smul_left x y c :=
    Completion.induction_onâ‚‚ x y
      (isClosed_eq (Continuous.inner (continuous_fst.const_smul c) continuous_snd)
        ((continuous_mul_left _).comp (Continuous.inner continuous_fst continuous_snd)))
      fun a b => by simp only [â† coe_smul c a, inner_coe, inner_smul_left]

end UniformSpace.Completion

end UniformSpace.Completion
