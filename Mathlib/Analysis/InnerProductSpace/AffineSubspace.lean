/-
Copyright (c) 2023 Ricardo Prado Cunha. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ricardo Prado Cunha
-/
import Mathlib.LinearAlgebra.AffineSpace.AffineSubspace
import Mathlib.Analysis.InnerProductSpace.Orthogonal
import Mathlib.Analysis.InnerProductSpace.Projection

/-!
# Orthogonal complements of affine subspaces

In this file, the `orthogonal` complement of an affine subspace `P` is defined, and basic API is
established. The API is made to emulate that of `Submodule.orthogonal`.

## Notation

The orthogonal complement of a subspace `s` through `b` is denoted `sá—®á—® b`.

The proposition that two subspaces are orthogonal, `AffineSubspace.IsOrtho`, is denoted by `s âŸ‚âŸ‚ t`.
Note this is not the same unicode symbol as `âŠ¥` (`Bot`).
-/


open Affine

variable {ğ•œ : Type*} {V : Type*} {P : Type*} [IsROrC ğ•œ]

variable [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [AffineSpace V P]

namespace AffineSubspace

variable (s : AffineSubspace ğ•œ P)

open AffineEquiv

/-- Orthogonal complement to an affine subspace passing through a given point. -/
def orthogonal (b : P) : AffineSubspace ğ•œ P := mk' b s.directioná—®

@[inherit_doc]
infixl:55 "á—®á—® " => AffineSubspace.orthogonal

/-- When a point is in the orthogonal complement. -/
lemma mem_orthogonal (b c : P) :
    c âˆˆ sá—®á—® b â†” âˆ€ (v : V), v âˆˆ s.direction â†’ @inner ğ•œ _ _ v (c -áµ¥ b) = 0 := by
  apply Iff.intro
  Â· intro hc v hv
    rcases hc with âŸ¨w, hw, hcâŸ©
    rw [hc]
    simp
    apply (Submodule.mem_orthogonal _ w).mp
    <;> assumption
  Â· intro h
    simp [orthogonal]
    use c -áµ¥ b
    apply And.intro
    Â· exact h
    Â· simp

/-- When a point is in the orthogonal complement, with the inner product the other way around. -/
lemma mem_orthogonal' (b c : P) :
    c âˆˆ sá—®á—® b â†” âˆ€ (v : V), v âˆˆ s.direction â†’ @inner ğ•œ _ _ (c -áµ¥ b) v = 0 := by
  simp_rw [mem_orthogonal, inner_eq_zero_symm]

/-- `orthogonal` reverses the `â‰¤` ordering of two affine subspaces. -/
lemma orthogonal_le {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} (b : P) (h : sâ‚ â‰¤ sâ‚‚) :
    sâ‚‚.orthogonal b â‰¤ sâ‚.orthogonal b := by
  rw [orthogonal, orthogonal, le_def']
  intro p hp
  use p -áµ¥ b
  apply And.intro
  Â· rcases hp with âŸ¨v, hv, rflâŸ©
    simp
    exact Submodule.orthogonal_le (direction_le h) hv
  Â· symm
    exact vsub_vadd _ _

/-- Double application of `orthogonal` preserves the `â‰¤` ordering of two affine subspaces. -/
lemma orthogonal_orthogonal_monotone {sâ‚ sâ‚‚ : AffineSubspace ğ•œ P} (bâ‚ bâ‚‚ c : P) (h : sâ‚ â‰¤ sâ‚‚) :
    sâ‚á—®á—® bâ‚á—®á—® c â‰¤ sâ‚‚á—®á—® bâ‚‚á—®á—® c := by
  simp [orthogonal, le_def']
  intro p hp
  use p -áµ¥ c
  apply And.intro
  Â· rcases hp with âŸ¨v, hv, rflâŸ©
    simp
    exact Submodule.orthogonal_orthogonal_monotone (direction_le h) hv
  Â· symm
    exact vsub_vadd _ _

/-- `s` is contained in `(s.orthogonal b).orthogonal c` when `c âˆˆ s`. -/
lemma le_orthogonal_orthogonal (b c : P) (hc : c âˆˆ s) : s â‰¤ sá—®á—® bá—®á—® c := by
  simp [orthogonal, le_def']
  intros p hp
  exact âŸ¨ p -áµ¥ c
        , Submodule.le_orthogonal_orthogonal _ (vsub_mem_direction hp hc)
        , Eq.symm (vsub_vadd _ _)
        âŸ©

@[simp]
lemma top_orthogonal_eq_mk'_of_bot (b : P) : orthogonal (âŠ¤ : AffineSubspace ğ•œ P) b = mk' b âŠ¥ := by
  simp [orthogonal]

@[simp]
lemma bot_orthogonal_eq_top (b : P) : orthogonal (âŠ¥ : AffineSubspace ğ•œ P) b = âŠ¤ := by
  simp [orthogonal]
  ext x
  exact âŸ¨by simp, fun _ => âŸ¨x -áµ¥ b, by simpâŸ©âŸ©

@[simp]
lemma mk'_of_bot_orthogonal_eq_top (b c : P) : (mk' b (âŠ¥ : Submodule ğ•œ V))á—®á—® c = âŠ¤ := by
  rw [orthogonal, direction_mk', Submodule.bot_orthogonal_eq_top]
  ext x
  exact âŸ¨by simp, fun _ => âŸ¨x -áµ¥ c, by simpâŸ©âŸ©

@[simp]
lemma orthogonal_eq_top_iff (b : P) : sá—®á—® b = âŠ¤ â†” s.direction = âŠ¥ := by
  apply Iff.intro
  Â· intro hs
    rw [orthogonal] at hs
    rw [â† Submodule.orthogonal_eq_top_iff, â† direction_mk' b (direction s)á—®, hs]
    exact direction_top _ _ _
  Â· intro hs
    rw [orthogonal, hs, Submodule.bot_orthogonal_eq_top]
    ext x
    exact âŸ¨by simp, fun _ => âŸ¨x -áµ¥ b, by simpâŸ©âŸ©

/-- The orthogonal complements of two parallel affine subspaces through the same point are equal. -/
lemma orthogonal_of_parallel_eq (s t : AffineSubspace ğ•œ P) (b : P) (h : s âˆ¥ t) :
    sá—®á—® b = tá—®á—® b := by
  repeat rw [orthogonal]
  congr! 2
  exact h.direction_eq

/-- The orthogonal complements of two parallel subspaces through any two points are also parallel.
-/
lemma orthogonal_parallel_of_parallel (s t : AffineSubspace ğ•œ P) (b c : P) :
    s âˆ¥ t â†’ orthogonal s b âˆ¥ orthogonal t c := by
  intro hpar
  use c -áµ¥ b
  ext x
  apply Iff.intro
  Â· intro hx
    use x -áµ¥ c +áµ¥ b
    apply And.intro
    Â· rcases hx with âŸ¨w, hw, hxâŸ©
      rw [hx]
      simp
      rw [â† Parallel.direction_eq hpar] at hw
      exact âŸ¨w, hw, rflâŸ©
    Â· simp
  Â· intro hx
    rcases hx with âŸ¨w, hw, hxâŸ©
    rcases hw with âŸ¨v, hv, hwâŸ©
    rw [â† hx, hw]
    simp
    rw [Parallel.direction_eq hpar] at hv
    exact âŸ¨v, hv, rflâŸ©

/-- The orthogonal complements of an affine subspace through any points are parallel. -/
lemma orthogonal_parallel (b c : P) : orthogonal s b âˆ¥ orthogonal s c :=
  orthogonal_parallel_of_parallel s s b c (Parallel.refl s)

/-- The orthogonal complement through a point `c` of the orthogonal complement of an affine subspace
is equal to the original subspace when `c` is in the original subspace and the `direction` of the
original subspace is a `CompleteSpace`. -/
lemma orthogonal_orthogonal [CompleteSpace s.direction] (b c : P) :
    c âˆˆ s â†’ sá—®á—® bá—®á—® c = s := by
  intro hc
  simp [orthogonal, hc]

/-- Two affine subspaces with `direction` being `CompleteSpace`s are parallel iff their orthogonal
completements through two points are parallel. -/
lemma orthogonal_parallel_iff_parallel (s t : AffineSubspace ğ•œ P) [hs : Nonempty s]
    [ht : Nonempty t] [CompleteSpace s.direction] [CompleteSpace t.direction] (b c : P) :
    s âˆ¥ t â†” orthogonal s b âˆ¥ orthogonal t c := by
  apply Iff.intro
  Â· exact orthogonal_parallel_of_parallel _ _ _ _
  Â· intro hpar
    rcases hs with âŸ¨b', hb'âŸ©
    rcases ht with âŸ¨c', hc'âŸ©
    rw [â† orthogonal_orthogonal s b b' hb', â† orthogonal_orthogonal t c c' hc']
    exact orthogonal_parallel_of_parallel _ _ _ _ hpar

end AffineSubspace

/-!
### Orthogonality of affine subspaces

In this section we define `AffineSubspace.IsOrtho`, with notation `s âŸ‚âŸ‚ t`.

The API emulates that of `Submodule.IsOrtho`.
-/


namespace AffineSubspace

/-- The proposition that two affine subspaces are orthogonal. Has notation `s âŸ‚âŸ‚ t`. -/
def IsOrtho (s t : AffineSubspace ğ•œ P) : Prop := s.direction âŸ‚ t.direction

@[inherit_doc]
infixl:50 " âŸ‚âŸ‚ " => AffineSubspace.IsOrtho

@[symm]
lemma IsOrtho.symm {s t : AffineSubspace ğ•œ P} : s âŸ‚âŸ‚ t â†’ t âŸ‚âŸ‚ s :=
  Submodule.IsOrtho.symm

lemma IsOrtho_comm {s t : AffineSubspace ğ•œ P} : s âŸ‚âŸ‚ t â†” t âŸ‚âŸ‚ s :=
  âŸ¨IsOrtho.symm, IsOrtho.symmâŸ©

lemma symmetric_isOrtho : Symmetric (IsOrtho : AffineSubspace ğ•œ P â†’ AffineSubspace ğ•œ P â†’ Prop) :=
  fun _ _ => IsOrtho.symm

/-- The empty subspace is orthogonal to all subspaces. -/
@[simp]
lemma isOrtho_bot_left {s : AffineSubspace ğ•œ P} : IsOrtho âŠ¥ s := by simp [IsOrtho]

/-- All subspaces are orthogonal to the empty subspace -/
@[simp]
lemma isOrtho_bot_right {s : AffineSubspace ğ•œ P} : s âŸ‚âŸ‚ âŠ¥ := IsOrtho.symm isOrtho_bot_left

/-- If a subspace `sâ‚` is orthogonal to `t`, then so is any subspace `sâ‚‚ â‰¤ sâ‚`. -/
lemma IsOrtho.mono_left {sâ‚ sâ‚‚ t : AffineSubspace ğ•œ P} (hs : sâ‚‚ â‰¤ sâ‚) (h : sâ‚ âŸ‚âŸ‚ t) :
    sâ‚‚ âŸ‚âŸ‚ t := by
  simp [IsOrtho]
  exact Submodule.IsOrtho.mono_left (direction_le hs) h

/-- If a subspace `s` is orthogonal to `tâ‚`, then it is also orthogonal to any subspace `tâ‚‚ â‰¤ tâ‚`.
-/
lemma IsOrtho.mono_right {s tâ‚ tâ‚‚ : AffineSubspace ğ•œ P} (ht : tâ‚‚ â‰¤ tâ‚) (h : s âŸ‚âŸ‚ tâ‚) :
    s âŸ‚âŸ‚ tâ‚‚ := (h.symm.mono_left ht).symm

/-- If a subspace `sâ‚` is orthogonal to `tâ‚`, then any subspace `sâ‚‚ â‰¤ sâ‚` is also orthogonal to
`tâ‚‚ â‰¤ tâ‚` -/
lemma IsOrtho.mono {sâ‚ sâ‚‚ tâ‚ tâ‚‚ : AffineSubspace ğ•œ P} (hs : sâ‚‚ â‰¤ sâ‚) (ht : tâ‚‚ â‰¤ tâ‚)
    (h : sâ‚ âŸ‚âŸ‚ tâ‚) : sâ‚‚ âŸ‚âŸ‚ tâ‚‚ := (h.mono_right ht).mono_left hs

@[simp]
lemma isOrtho_self {s : AffineSubspace ğ•œ P} : s âŸ‚âŸ‚ s â†” s.direction = âŠ¥ :=
  Submodule.isOrtho_self

@[simp]
lemma isOrtho_orthogonal_right {s : AffineSubspace ğ•œ P} (b : P) : s âŸ‚âŸ‚ (sá—®á—® b) := by
  simp [IsOrtho, orthogonal]

@[simp]
lemma isOrtho_orthogonal_left {s : AffineSubspace ğ•œ P} (b : P) : (sá—®á—® b) âŸ‚âŸ‚ s :=
  IsOrtho.symm (isOrtho_orthogonal_right b)

/-- If a subspace `s` is orthogonal to `t`, then `s` is a subspace of the orthogonal complement to
`t` through some point `b`. -/
lemma IsOrtho.le {s t : AffineSubspace ğ•œ P} (h : s âŸ‚âŸ‚ t) :
    âˆƒ (b : P), s â‰¤ tá—®á—® b := by
  by_cases hs : s = âŠ¥
  Â· cases (AddTorsor.nonempty : Nonempty P) with | intro b =>
    use b
    rw [hs]
    exact bot_le
  Â· push_neg at hs
    rw [â† nonempty_iff_ne_bot] at hs
    use hs.some
    rw [le_def', orthogonal]
    intro p hp
    use p -áµ¥ hs.some
    apply And.intro
    Â· rw [IsOrtho] at h
      apply h.le
      exact vsub_mem_direction hp hs.some_mem
    Â· rw [vsub_vadd]

/-- If a subspace `s` is orthogonal `t`, then `t` is a subspace of the orthogonal complement to `s`
through some point `b`. -/
lemma IsOrtho.ge {s t : AffineSubspace ğ•œ P} (h : s âŸ‚âŸ‚ t) : âˆƒ (b : P), t â‰¤ sá—®á—® b :=
  h.symm.le

@[simp]
lemma isOrtho_top_right {s : AffineSubspace ğ•œ P} : s âŸ‚âŸ‚ âŠ¤ â†” s.direction = âŠ¥ := by
  rw [IsOrtho, direction_top]
  exact Submodule.isOrtho_top_right

@[simp]
lemma isOrtho_top_left {s : AffineSubspace ğ•œ P} : IsOrtho âŠ¤ s â†” s.direction = âŠ¥ := by
  rw [IsOrtho_comm]
  exact isOrtho_top_right

lemma IsOrtho.disjoint_direction {s t : AffineSubspace ğ•œ P} (h : s âŸ‚âŸ‚ t) :
    Disjoint s.direction t.direction := Submodule.IsOrtho.disjoint h

lemma IsOrtho.inf_direction {s t : AffineSubspace ğ•œ P} (h : s âŸ‚âŸ‚ t) :
    (s âŠ“ t).direction = âŠ¥ :=
  le_bot_iff.mp (le_trans (direction_inf s t) (disjoint_iff_inf_le.mp h.disjoint))

@[simp]
lemma isOrtho_sup_left {sâ‚ sâ‚‚ t : AffineSubspace ğ•œ P} (h : (sâ‚ âŠ” sâ‚‚) âŸ‚âŸ‚ t) :
    sâ‚ âŸ‚âŸ‚ t âˆ§ sâ‚‚ âŸ‚âŸ‚ t := by
  rw [IsOrtho, Submodule.IsOrtho] at h
  have := le_trans (sup_direction_le sâ‚ sâ‚‚) h
  exact Submodule.isOrtho_sup_left.mp this

@[simp]
lemma isOrtho_sup_right {s tâ‚ tâ‚‚ : AffineSubspace ğ•œ P} (h : s âŸ‚âŸ‚ (tâ‚ âŠ” tâ‚‚)) :
    s âŸ‚âŸ‚ tâ‚ âˆ§ s âŸ‚âŸ‚ tâ‚‚ := by
  rw [IsOrtho_comm] at h
  repeat rw [@IsOrtho_comm _ _ _ _ _ _ _ s]
  exact isOrtho_sup_left h

@[simp]
lemma isOrtho_mk' (b c : P) (dirâ‚ dirâ‚‚ : Submodule ğ•œ V) :
    (mk' b dirâ‚) âŸ‚âŸ‚ (mk' c dirâ‚‚) â†” dirâ‚ âŸ‚ dirâ‚‚ := by simp [IsOrtho]

@[simp]
lemma IsOrtho.trans_parallel_right {sâ‚ sâ‚‚ t : AffineSubspace ğ•œ P} (hs : sâ‚ âˆ¥ sâ‚‚) :
    sâ‚ âŸ‚âŸ‚ t â†” sâ‚‚ âŸ‚âŸ‚ t := by
  apply Iff.intro
  Â· intro h
    rw [IsOrtho, â† Parallel.direction_eq hs]
    exact h
  Â· intro h
    rw [IsOrtho, Parallel.direction_eq hs]
    exact h

@[simp]
lemma IsOrtho.trans_parallel_left {s tâ‚ tâ‚‚ : AffineSubspace ğ•œ P} (ht : tâ‚ âˆ¥ tâ‚‚) :
    s âŸ‚âŸ‚ tâ‚ â†” s âŸ‚âŸ‚ tâ‚‚ := by
  repeat rw [IsOrtho]
  congr! 1
  exact Parallel.direction_eq ht

end AffineSubspace
