/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.LinearAlgebra.TensorProduct.Basic
import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.Analysis.InnerProductSpace.PiL2

/-!

# Inner product space structure on tensor product spaces

This file provides an inner product space structure on tensor product spaces.

-/

variable {ğ•œ E F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F]

open scoped TensorProduct

instance TensorProduct.instInner : Inner ğ•œ (E âŠ—[ğ•œ] F) :=
  âŸ¨fun x y =>
    LinearMap.mul' ğ•œ ğ•œ ((homTensorHomMap ğ•œ _ _ _ _ ((mapâ‚›â‚— (innerâ‚›â‚— ğ•œ) (innerâ‚›â‚— ğ•œ)) x)) y)âŸ©

@[simp]
theorem inner_tmul (x x' : E) (y y' : F) :
    inner ğ•œ (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ[ğ•œ] y') = inner ğ•œ x x' * inner ğ•œ y y' := rfl

@[simp]
theorem TensorProduct.inner_add (x y z : E âŠ—[ğ•œ] F) :
    inner ğ•œ x (y + z) = inner ğ•œ x y + inner ğ•œ x z := by
  simp [inner]

@[simp]
theorem TensorProduct.add_inner (x y z : E âŠ—[ğ•œ] F) :
    inner ğ•œ (x + y) z = inner ğ•œ x z + inner ğ•œ y z := by
  simp [inner]

@[simp]
theorem TensorProduct.sum_inner {n : Type*} [Fintype n] (x : n â†’ E âŠ—[ğ•œ] F)
    (y : E âŠ—[ğ•œ] F) : inner ğ•œ (âˆ‘ i, x i) y = âˆ‘ i, inner ğ•œ (x i) y := by
  simp [inner]

@[simp]
theorem TensorProduct.inner_sum {n : Type*} [Fintype n] (x : E âŠ—[ğ•œ] F) (y : n â†’ E âŠ—[ğ•œ] F) :
    inner ğ•œ x (âˆ‘ i, y i) = âˆ‘ i, inner ğ•œ x (y i) := by
  simp [inner]

@[simp]
theorem TensorProduct.smul_inner (x y : E âŠ—[ğ•œ] F) (c : ğ•œ) :
    inner ğ•œ (c â€¢ x) y = starRingEnd ğ•œ c * inner ğ•œ x y := by
  simp [inner]

@[simp]
theorem TensorProduct.inner_smul (x y : E âŠ—[ğ•œ] F) (c : ğ•œ) :
    inner ğ•œ x (c â€¢ y) = c * inner ğ•œ x y := by
  simp [inner]

theorem TensorProduct.conj_inner (x y : E âŠ—[ğ•œ] F) : starRingEnd ğ•œ (inner ğ•œ x y) = inner ğ•œ y x :=
  x.induction_on (by simp [inner]) (y.induction_on (by simp [inner]) (fun x y => by simp)
    (fun x y hx hy a b => by simp_all [inner])) (fun x y hx hy => by simp_all [inner])

section move

-- move to `LinearAlgebra/TensorProduct/Basis`? or something
theorem TensorProduct.of_basis_eq_span {ğ•œ E F : Type*} [CommSemiring ğ•œ]
    [AddCommGroup E] [Module ğ•œ E] [AddCommGroup F] [Module ğ•œ F] (x : TensorProduct ğ•œ E F)
    {Î¹â‚ Î¹â‚‚ : Type _} [Fintype Î¹â‚] [Fintype Î¹â‚‚] (bâ‚ : Basis Î¹â‚ ğ•œ E) (bâ‚‚ : Basis Î¹â‚‚ ğ•œ F) :
    x = âˆ‘ i : Î¹â‚, âˆ‘ j : Î¹â‚‚, (bâ‚.tensorProduct bâ‚‚).repr x (i, j) â€¢ bâ‚ i âŠ—â‚œ[ğ•œ] bâ‚‚ j :=
  x.induction_on
  (by simp only [map_zero, Finsupp.zero_apply, zero_smul, Finset.sum_const_zero])
  (fun Î±â‚ Î±â‚‚ => by
    simp_rw [Basis.tensorProduct_repr_tmul_apply,
      smul_eq_mul, mul_comm, â† TensorProduct.smul_tmul_smul, â†
      TensorProduct.tmul_sum, â† TensorProduct.sum_tmul, Basis.sum_repr])
  (fun a b ha hb => by
    simp_rw [_root_.map_add, Finsupp.add_apply, add_smul, Finset.sum_add_distrib]
    rw [â† ha, â† hb])

-- move to `LinearAlgebra/FiniteDimensional/Basic`?
lemma toFiniteDimensional (K : Type*) {V : Type*} [DivisionRing K] [AddCommGroup V] [Module K V]
    (e : V) : âˆƒ (E' : Submodule K V) (_ : FiniteDimensional K E'), e âˆˆ E' := by
  classical
  let b := Basis.ofVectorSpace K V
  refine âŸ¨Submodule.span K (Finset.image b (b.repr e).support),
    FiniteDimensional.span_finset _ _, ?_âŸ©
  convert Basis.mem_span_repr_support b e
  simp

section

variable {R V V' : Type*} [CommSemiring R] [AddCommMonoid V] [AddCommMonoid V']
  [Module R V] [Module R V']

lemma TensorProduct.map_subtype_left_mono {E' E'' : Submodule R V} (F' : Submodule R V')
    (le1 : E' â‰¤ E'') :
    LinearMap.range (TensorProduct.map E'.subtype F'.subtype) â‰¤
      LinearMap.range (TensorProduct.map E''.subtype F'.subtype) := fun x hx => by
  obtain âŸ¨x, rflâŸ© := hx
  induction' x using TensorProduct.induction_on with e f xâ‚ xâ‚‚ ihâ‚ ihâ‚‚
  Â· rw [map_zero]
    exact Submodule.zero_mem _
  Â· exact âŸ¨âŸ¨e, le1 e.2âŸ© âŠ—â‚œ f, rflâŸ©
  Â· rw [map_add]
    exact Submodule.add_mem _ ihâ‚ ihâ‚‚

lemma TensorProduct.map_subtype_right_mono (E' : Submodule R V) {F' F'' : Submodule R V'}
    (le2 : F' â‰¤ F'') :
    LinearMap.range (TensorProduct.map E'.subtype F'.subtype) â‰¤
      LinearMap.range (TensorProduct.map E'.subtype F''.subtype) := fun x hx => by
  obtain âŸ¨x, rflâŸ© := hx
  induction' x using TensorProduct.induction_on with e f xâ‚ xâ‚‚ ihâ‚ ihâ‚‚
  Â· rw [map_zero]; exact Submodule.zero_mem _
  Â· exact âŸ¨e âŠ—â‚œ âŸ¨f, le2 f.2âŸ©, rflâŸ©
  Â· rw [map_add]; exact Submodule.add_mem _ ihâ‚ ihâ‚‚

end

lemma toTensorFiniteDimensional {K V V' : Type*} [Field K] [AddCommGroup V]
    [AddCommGroup V'] [Module K V] [Module K V']
    (z : V âŠ—[K] V') : âˆƒ (E' : Submodule K V) (F' : Submodule K V')
    (_ : FiniteDimensional K E') (_ : FiniteDimensional K F'),
    z âˆˆ LinearMap.range (TensorProduct.map E'.subtype F'.subtype) := by
  induction' z using TensorProduct.induction_on with e f zâ‚ zâ‚‚ ihâ‚ ihâ‚‚
  Â· exact âŸ¨âŠ¥, âŠ¥, finiteDimensional_bot K V, finiteDimensional_bot K V', Submodule.zero_mem _âŸ©
  Â· rcases toFiniteDimensional K e with âŸ¨E', iE', heâŸ©
    rcases toFiniteDimensional K f with âŸ¨F', iF', hfâŸ©
    exact âŸ¨E', F', iE', iF', âŸ¨âŸ¨e, heâŸ© âŠ—â‚œ âŸ¨f, hfâŸ©, rflâŸ©âŸ©
  Â· rcases ihâ‚ with âŸ¨E1, F1, iE1, iF1, âŸ¨z1, rflâŸ©âŸ©
    rcases ihâ‚‚ with âŸ¨E2, F2, iE2, iF2, âŸ¨z2, rflâŸ©âŸ©
    have le1 : LinearMap.range (TensorProduct.map E1.subtype F1.subtype) â‰¤
        LinearMap.range (TensorProduct.map (E1 âŠ” E2).subtype (F1 âŠ” F2).subtype) :=
      (TensorProduct.map_subtype_left_mono _ le_sup_left).trans
        (TensorProduct.map_subtype_right_mono _ le_sup_left)
    have le2 : LinearMap.range (TensorProduct.map E2.subtype F2.subtype) â‰¤
        LinearMap.range (TensorProduct.map (E1 âŠ” E2).subtype (F1 âŠ” F2).subtype) :=
      (TensorProduct.map_subtype_left_mono _ le_sup_right).trans
        (TensorProduct.map_subtype_right_mono _ le_sup_right)
    exact âŸ¨E1 âŠ” E2, F1 âŠ” F2, Submodule.finiteDimensional_sup _ _,
      Submodule.finiteDimensional_sup _ _, Submodule.add_mem _ (le1 âŸ¨z1, rflâŸ©) (le2 âŸ¨z2, rflâŸ©)âŸ©

end move

lemma TensorProduct.inner_coe_of_eq (x y : E âŠ—[ğ•œ] F)
    (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F) (x' y' : E' âŠ—[ğ•œ] F')
    (hx : x = TensorProduct.map E'.subtype F'.subtype x')
    (hy : y = TensorProduct.map E'.subtype F'.subtype y') :
    inner ğ•œ x' y' = inner ğ•œ x y := by
  rw [hx, hy]
  revert x
  induction' x' using TensorProduct.induction_on with e' f' xâ‚ xâ‚‚ ihâ‚ ihâ‚‚
  Â· simp [inner]
  Â· revert y
    induction' y' using TensorProduct.induction_on with e'' f'' yâ‚ yâ‚‚ ihâ‚ ihâ‚‚
    Â· simp [inner]
    Â· intro x h y h'
      rfl
    Â· intro x hx y hy
      simp_all
  Â· intro x hx
    simp_all

lemma TensorProduct.inner_coe_of_mem_range (x y : E âŠ—[ğ•œ] F)
    (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F)
    (hx : x âˆˆ LinearMap.range (TensorProduct.map E'.subtype F'.subtype))
    (hy : y âˆˆ LinearMap.range (TensorProduct.map E'.subtype F'.subtype)) :
    (inner ğ•œ hx.choose hy.choose) = (inner ğ•œ x y) := by
  apply TensorProduct.inner_coe_of_eq
  Â· exact (hx.choose_spec).symm
  Â· exact (hy.choose_spec).symm

open scoped ComplexOrder

theorem TensorProduct.inner_definite (x : E âŠ—[ğ•œ] F) (hx : inner ğ•œ x x = 0) : x = 0 := by
  obtain âŸ¨E', F', iE', iF', hzâŸ© := toTensorFiniteDimensional x
  rw [â† inner_coe_of_mem_range x x E' F' hz hz] at hx
  let y := hz.choose
  obtain e := stdOrthonormalBasis ğ•œ E'
  obtain f := stdOrthonormalBasis ğ•œ F'
  have hy : y = hz.choose := rfl
  rw [â† hy] at hx
  rw [TensorProduct.of_basis_eq_span y e.toBasis f.toBasis] at hx
  simp only [OrthonormalBasis.coe_toBasis, TensorProduct.inner_sum, TensorProduct.inner_smul,
    TensorProduct.sum_inner, TensorProduct.smul_inner, inner_tmul] at hx
  simp [OrthonormalBasis.inner_eq_ite] at hx
  simp [RCLike.mul_conj, â† Finset.sum_product'] at hx
  rw [Finset.sum_eq_zero_iff_of_nonneg (fun _ _ => by simp)] at hx
  simp at hx
  have : y = 0 := by
    rw [Basis.ext_elem_iff (e.toBasis.tensorProduct f.toBasis)]
    simp only [hx, map_zero, Finsupp.coe_zero, Pi.zero_apply, implies_true]
  rw [â† hz.choose_spec, â† hy, this, map_zero]

theorem TensorProduct.re_inner_self_nonneg (x : E âŠ—[ğ•œ] F) :
    0 â‰¤ RCLike.re (inner ğ•œ x x) := by
  obtain âŸ¨E', F', iE', iF', hzâŸ© := toTensorFiniteDimensional x
  rw [â† inner_coe_of_mem_range x x E' F' hz hz]
  let y := hz.choose
  obtain e := stdOrthonormalBasis ğ•œ E'
  obtain f := stdOrthonormalBasis ğ•œ F'
  have hy : y = hz.choose := rfl
  rw [â† hy]
  rw [TensorProduct.of_basis_eq_span y e.toBasis f.toBasis]
  simp only [OrthonormalBasis.coe_toBasis, TensorProduct.inner_sum, TensorProduct.inner_smul,
    TensorProduct.sum_inner, TensorProduct.smul_inner, inner_tmul]
  simp only [OrthonormalBasis.inner_eq_ite, mul_ite, mul_one, mul_zero, Finset.sum_ite_eq',
    Finset.mem_univ, â†“reduceIte, â† Finset.sum_product', RCLike.mul_conj, map_sum]
  apply Finset.sum_nonneg
  intro i hi
  rw [â† RCLike.ofReal_pow, RCLike.ofReal_re]
  exact sq_nonneg _

noncomputable instance TensorProduct.instNormedAddCommGroup : NormedAddCommGroup (E âŠ—[ğ•œ] F) :=
  @InnerProductSpace.Core.toNormedAddCommGroup ğ•œ (E âŠ—[ğ•œ] F) _ _ _
  { conj_inner_symm := fun x y => TensorProduct.conj_inner y x
    add_left := TensorProduct.add_inner
    smul_left := TensorProduct.smul_inner
    definite := TensorProduct.inner_definite
    re_inner_nonneg := TensorProduct.re_inner_self_nonneg }

noncomputable instance TensorProduct.instInnerProductSpace :
    @InnerProductSpace ğ•œ (E âŠ—[ğ•œ] F) _ _ :=
  InnerProductSpace.ofCore _

@[simp]
theorem norm_tmul (x : E) (y : F) :
    â€–x âŠ—â‚œ[ğ•œ] yâ€– = â€–xâ€– * â€–yâ€– := by
  rw [@norm_eq_sqrt_re_inner (ğ•œ := ğ•œ)]
  simp [Real.sqrt_mul inner_self_nonneg, â† norm_eq_sqrt_re_inner]

theorem dist_tmul_le (x x' : E) (y y' : F) :
    dist (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ y') â‰¤ â€–xâ€– * â€–yâ€– + â€–x'â€– * â€–y'â€– :=
  calc â€–x âŠ—â‚œ[ğ•œ] y - x' âŠ—â‚œ[ğ•œ] y'â€– â‰¤ â€–x âŠ—â‚œ[ğ•œ] yâ€– + â€–x' âŠ—â‚œ[ğ•œ] y'â€– := norm_sub_le _ _
    _ = â€–xâ€– * â€–yâ€– + â€–x'â€– * â€–y'â€– := by simp

theorem RCLike.inner_tmul_eq (a b c d : ğ•œ) :
    inner ğ•œ (a âŠ—â‚œ[ğ•œ] b) (c âŠ—â‚œ[ğ•œ] d) = inner ğ•œ (a * b) (c * d) := by
  simp; ring
