/-
Copyright (c) 2025 Monica Omar. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Monica Omar
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.LinearAlgebra.TensorProduct.Basic
import Mathlib.LinearAlgebra.TensorProduct.Finiteness
import Mathlib.RingTheory.TensorProduct.Finite

/-!

# Inner product space structure on tensor product spaces

This file provides the inner product space structure on tensor product spaces.

We define the inner product on `E âŠ— F` by `âŸªa âŠ—â‚œ b, c âŠ—â‚œ dâŸ« = âŸªa, câŸ« * âŸªb, dâŸ«`, when `E` and `F` are
inner product spaces.

## Main definitions:

* `TensorProduct.instNormedAddCommGroup`: the normed additive group structure on tensor products,
  where `â€–x âŠ—â‚œ yâ€– = â€–xâ€– * â€–yâ€–`.
* `TensorProduct.instInnerProductSpace`: the inner product space structure on tensor products, where
  `âŸªa âŠ—â‚œ b, c âŠ—â‚œ dâŸ« = âŸªa, câŸ« * âŸªb, dâŸ«`.
* `TensorProduct.mapIsometry`: the linear isometry version of `TensorProduct.map f g` when
  `f` and `g` are linear isometries.
* `TensorProduct.congrIsometry`: the linear isometry equivalence version of
  `TensorProduct.congr f g` when `f` and `g` are linear isometry equivalences.
* `TensorProduct.mapInclIsometry`: the linear isometry version of `TensorProduct.mapIncl`.
* `TensorProduct.commIsometry`: the linear isometry version of `TensorProduct.comm`.
* `TensorProduct.lidIsometry`: the linear isometry version of `TensorProduct.lid`.
* `TensorProduct.assocIsometry`: the linear isometry version of `TensorProduct.assoc`.
* `OrthonormalBasis.tensorProduct`: the orthonormal basis of the tensor product of two orthonormal
  bases.

## TODO:

* Define the continuous linear map version of `TensorProduct.map`.
* Complete space of tensor products.
* Define the normed space without needing inner products, this should be analogous to
  `Mathlib/Analysis/NormedSpace/PiTensorProduct/InjectiveSeminorm.lean`.

-/

variable {ğ•œ E F G H : Type*} [RCLike ğ•œ]
  [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
  [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]
  [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
  [NormedAddCommGroup H] [InnerProductSpace ğ•œ H]

open scoped TensorProduct

namespace TensorProduct

/-- Bilinear map for the inner product on tensor products.
On pure tensors: `inner_ (a âŠ—â‚œ b) (c âŠ—â‚œ d) = âŸªa, câŸ« * âŸªb, dâŸ«`. -/
private abbrev inner_ : E âŠ—[ğ•œ] F â†’â‚—â‹†[ğ•œ] E âŠ—[ğ•œ] F â†’â‚—[ğ•œ] ğ•œ :=
  (lift <| mapBilinear ğ•œ E F ğ•œ ğ•œ).comprâ‚‚ (LinearMap.mul' ğ•œ ğ•œ) âˆ˜â‚›â‚— map (innerâ‚›â‚— ğ•œ) (innerâ‚›â‚— ğ•œ)

instance instInner : Inner ğ•œ (E âŠ—[ğ•œ] F) := âŸ¨fun x y => inner_ x yâŸ©

private lemma inner_def (x y : E âŠ—[ğ•œ] F) : inner ğ•œ x y = inner_ x y := rfl

@[simp] theorem inner_tmul (x x' : E) (y y' : F) :
    inner ğ•œ (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ[ğ•œ] y') = inner ğ•œ x x' * inner ğ•œ y y' := rfl

@[simp] lemma inner_map_map (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) (x y : E âŠ—[ğ•œ] F) :
    inner ğ•œ (map f.toLinearMap g.toLinearMap x) (map f.toLinearMap g.toLinearMap y) = inner ğ•œ x y :=
  x.induction_on (by simp [inner_def]) (y.induction_on (by simp [inner_def]) (by simp)
    (by simp_all [inner_def])) (by simp_all [inner_def])

lemma inner_mapIncl_mapIncl (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F) (x y : E' âŠ—[ğ•œ] F') :
    inner ğ•œ (mapIncl E' F' x) (mapIncl E' F' y) = inner ğ•œ x y :=
  inner_map_map E'.subtypeâ‚—áµ¢ F'.subtypeâ‚—áµ¢ x y

open scoped ComplexOrder
open Module

/- This holds in any inner product space, but we need this to set up the instance.
This is a helper lemma for showing that this inner product is positive definite. -/
private theorem inner_self {Î¹ Î¹' : Type*} [Fintype Î¹] [Fintype Î¹'] (x : E âŠ—[ğ•œ] F)
    (e : OrthonormalBasis Î¹ ğ•œ E) (f : OrthonormalBasis Î¹' ğ•œ F) :
    inner ğ•œ x x = âˆ‘ i, â€–(e.toBasis.tensorProduct f.toBasis).repr x iâ€– ^ 2 := by
  classical
  have : x = âˆ‘ i : Î¹, âˆ‘ j : Î¹', (e.toBasis.tensorProduct f.toBasis).repr x (i, j) â€¢ e i âŠ—â‚œ f j := by
    conv_lhs => rw [â† (e.toBasis.tensorProduct f.toBasis).sum_repr x]
    simp [â† Finset.sum_product', Basis.tensorProduct_apply']
  conv_lhs => rw [this]
  simp only [inner_def, map_sum, LinearMap.sum_apply]
  simp [OrthonormalBasis.inner_eq_ite, â† Finset.sum_product', RCLike.mul_conj]

private theorem inner_definite (x : E âŠ—[ğ•œ] F) (hx : inner ğ•œ x x = 0) : x = 0 := by
  /-
  The way we prove this is by noting that every element of a tensor product lies
  in the tensor product of some finite submodules.
  So for `x : E âŠ— F`, there exists finite submodules `E', F'` such that `x âˆˆ mapIncl E' F'`.
  And so the rest then follows from the above lemmas `inner_mapIncl_mapIncl` and `inner_self`.
  -/
  obtain âŸ¨E', F', iE', iF', hzâŸ© := exists_finite_submodule_of_setFinite {x} (Set.finite_singleton x)
  obtain âŸ¨y : E' âŠ— F', rfl : mapIncl E' F' y = xâŸ© := Set.singleton_subset_iff.mp hz
  obtain e := stdOrthonormalBasis ğ•œ E'
  obtain f := stdOrthonormalBasis ğ•œ F'
  have (i) (j) : (e.toBasis.tensorProduct f.toBasis).repr y (i, j) = 0 := by
    rw [inner_mapIncl_mapIncl, inner_self y e f, RCLike.ofReal_eq_zero,
      Finset.sum_eq_zero_iff_of_nonneg fun _ _ => sq_nonneg _] at hx
    simpa using hx (i, j)
  have : y = 0 := by simp [(e.toBasis.tensorProduct f.toBasis).ext_elem_iff, this]
  rw [this, map_zero]

private protected theorem re_inner_self_nonneg (x : E âŠ—[ğ•œ] F) :
    0 â‰¤ RCLike.re (inner ğ•œ x x) := by
  /-
  Similarly to the above proof, for `x : E âŠ— F`, there exists finite submodules `E', F'` such that
  `x âˆˆ mapIncl E' F'`.
  And so the rest then follows from the above lemmas `inner_mapIncl_mapIncl` and `inner_self`.
  -/
  obtain âŸ¨E', F', iE', iF', hzâŸ© := exists_finite_submodule_of_setFinite {x} (Set.finite_singleton x)
  obtain âŸ¨y, rflâŸ© := Set.singleton_subset_iff.mp hz
  obtain e := stdOrthonormalBasis ğ•œ E'
  obtain f := stdOrthonormalBasis ğ•œ F'
  rw [inner_mapIncl_mapIncl, inner_self y e f, RCLike.ofReal_re]
  exact Finset.sum_nonneg fun _ _ â†¦ sq_nonneg _

noncomputable instance instNormedAddCommGroup : NormedAddCommGroup (E âŠ—[ğ•œ] F) :=
  letI : InnerProductSpace.Core ğ•œ (E âŠ—[ğ•œ] F) :=
  { conj_inner_symm x y :=
      x.induction_on (by simp [inner]) (y.induction_on (by simp [inner]) (by simp)
        (by simp_all [inner])) (by simp_all [inner])
    add_left _ _ _ := LinearMap.map_addâ‚‚ _ _ _ _
    smul_left _ _ _ := LinearMap.map_smulâ‚›â‚—â‚‚ _ _ _ _
    definite := TensorProduct.inner_definite
    re_inner_nonneg := TensorProduct.re_inner_self_nonneg }
  this.toNormedAddCommGroup

instance instInnerProductSpace : InnerProductSpace ğ•œ (E âŠ—[ğ•œ] F) := .ofCore _

@[simp] theorem norm_tmul (x : E) (y : F) :
    â€–x âŠ—â‚œ[ğ•œ] yâ€– = â€–xâ€– * â€–yâ€– := by
  simp [norm_eq_sqrt_re_inner (ğ•œ := ğ•œ), Real.sqrt_mul inner_self_nonneg]

@[simp] theorem nnnorm_tmul (x : E) (y : F) :
    â€–x âŠ—â‚œ[ğ•œ] yâ€–â‚Š = â€–xâ€–â‚Š * â€–yâ€–â‚Š := by simp [â† NNReal.coe_inj]

@[simp] theorem enorm_tmul (x : E) (y : F) :
    â€–x âŠ—â‚œ[ğ•œ] yâ€–â‚‘ = â€–xâ€–â‚‘ * â€–yâ€–â‚‘ := ENNReal.coe_inj.mpr <| by simp

theorem dist_tmul_le (x x' : E) (y y' : F) :
    dist (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ y') â‰¤ â€–xâ€– * â€–yâ€– + â€–x'â€– * â€–y'â€– := by
  grw [dist_eq_norm, norm_sub_le]; simp

theorem nndist_tmul_le (x x' : E) (y y' : F) :
    nndist (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ y') â‰¤ â€–xâ€–â‚Š * â€–yâ€–â‚Š + â€–x'â€–â‚Š * â€–y'â€–â‚Š := by
  grw [nndist_eq_nnnorm, nnnorm_sub_le]; simp

theorem edist_tmul_le (x x' : E) (y y' : F) :
    edist (x âŠ—â‚œ[ğ•œ] y) (x' âŠ—â‚œ y') â‰¤ â€–xâ€–â‚‘ * â€–yâ€–â‚‘ + â€–x'â€–â‚‘ * â€–y'â€–â‚‘ := by
  grw [edist_eq_enorm_sub, enorm_sub_le]; simp

/-- In `â„` or `â„‚` fields, the inner product on tensor products is essentially just the inner product
with multiplication instead of tensors, i.e., `âŸªa âŠ—â‚œ b, c âŠ—â‚œ dâŸ« = âŸªa * b, c * dâŸ«`. -/
theorem _root_.RCLike.inner_tmul_eq (a b c d : ğ•œ) :
    inner ğ•œ (a âŠ—â‚œ[ğ•œ] b) (c âŠ—â‚œ[ğ•œ] d) = inner ğ•œ (a * b) (c * d) := by
  simp; ring

/-- Given `x, y : E âŠ— F`, `x = y` iff `âŸªx, a âŠ—â‚œ bâŸ« = âŸªy, a âŠ—â‚œ bâŸ«` for all `a, b`. -/
protected theorem ext_iff_inner_right {x y : E âŠ—[ğ•œ] F} :
    x = y â†” âˆ€ a b, inner ğ•œ x (a âŠ—â‚œ[ğ•œ] b) = inner ğ•œ y (a âŠ—â‚œ[ğ•œ] b) :=
  âŸ¨fun h _ _ â†¦ h â–¸ rfl, fun h â†¦ innerSL_inj.mp <| ContinuousLinearMap.coe_inj.mp <| ext' hâŸ©

/-- Given `x, y : E âŠ— F`, `x = y` iff `âŸªa âŠ—â‚œ b, xâŸ« = âŸªa âŠ—â‚œ b, yâŸ«` for all `a, b`. -/
protected theorem ext_iff_inner_left {x y : E âŠ—[ğ•œ] F} :
    x = y â†” âˆ€ a b, inner ğ•œ (a âŠ—â‚œ b) x = inner ğ•œ (a âŠ—â‚œ b) y := by
  simpa only [â† inner_conj_symm x, â† inner_conj_symm y, starRingEnd_apply, star_inj] using
    TensorProduct.ext_iff_inner_right (x := x) (y := y)

/-- Given `x, y : E âŠ— F âŠ— G`, `x = y` iff `âŸªx, a âŠ—â‚œ b âŠ—â‚œ câŸ« = âŸªy, a âŠ—â‚œ b âŠ—â‚œ câŸ«` for all `a, b, c`.

See also `ext_iff_inner_right_threefold'` for when `x, y : E âŠ— (F âŠ— G)`. -/
theorem ext_iff_inner_right_threefold {x y : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G} :
    x = y â†” âˆ€ a b c, inner ğ•œ x (a âŠ—â‚œ[ğ•œ] b âŠ—â‚œ[ğ•œ] c) = inner ğ•œ y (a âŠ—â‚œ[ğ•œ] b âŠ—â‚œ[ğ•œ] c) :=
  âŸ¨fun h _ _ _ â†¦ h â–¸ rfl, fun h â†¦ innerSL_inj.mp (ContinuousLinearMap.coe_inj.mp (ext_threefold h))âŸ©

/-- Given `x, y : E âŠ— F âŠ— G`, `x = y` iff `âŸªa âŠ—â‚œ b âŠ—â‚œ c, xâŸ« = âŸªa âŠ—â‚œ b âŠ—â‚œ c, yâŸ«` for all `a, b, c`.

See also `ext_iff_inner_left_threefold'` for when `x, y : E âŠ— (F âŠ— G)`. -/
theorem ext_iff_inner_left_threefold {x y : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G} :
    x = y â†” âˆ€ a b c, inner ğ•œ (a âŠ—â‚œ b âŠ—â‚œ c) x = inner ğ•œ (a âŠ—â‚œ b âŠ—â‚œ c) y := by
  simpa only [â† inner_conj_symm x, â† inner_conj_symm y, starRingEnd_apply, star_inj] using
    ext_iff_inner_right_threefold (x := x) (y := y)

section isometry

/-- The tensor product map of two linear isometries is a linear isometry. In particular, this is
the linear isometry version of `TensorProduct.map f g` when `f` and `g` are linear isometries. -/
def mapIsometry (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) :
    E âŠ—[ğ•œ] F â†’â‚—áµ¢[ğ•œ] G âŠ—[ğ•œ] H :=
  map f.toLinearMap g.toLinearMap |>.isometryOfInner <| inner_map_map _ _

@[simp] lemma mapIsometry_apply (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) (x : E âŠ—[ğ•œ] F) :
    mapIsometry f g x = map f.toLinearMap g.toLinearMap x := rfl

@[simp] lemma toLinearMap_mapIsometry (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) :
    (mapIsometry f g).toLinearMap = map f.toLinearMap g.toLinearMap := rfl

@[simp] lemma norm_map (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) (x : E âŠ—[ğ•œ] F) :
    â€–map f.toLinearMap g.toLinearMap xâ€– = â€–xâ€– := mapIsometry f g |>.norm_map x
@[simp] lemma nnnorm_map (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) (x : E âŠ—[ğ•œ] F) :
    â€–map f.toLinearMap g.toLinearMap xâ€–â‚Š = â€–xâ€–â‚Š := mapIsometry f g |>.nnnorm_map x
@[simp] lemma enorm_map (f : E â†’â‚—áµ¢[ğ•œ] G) (g : F â†’â‚—áµ¢[ğ•œ] H) (x : E âŠ—[ğ•œ] F) :
    â€–map f.toLinearMap g.toLinearMap xâ€–â‚‘ = â€–xâ€–â‚‘ := mapIsometry f g |>.enorm_map x

@[simp] lemma mapIsometry_id_id :
    mapIsometry (.id : E â†’â‚—áµ¢[ğ•œ] E) (.id : F â†’â‚—áµ¢[ğ•œ] F) = .id := by ext; simp

variable (E) in
/-- This is the natural linear isometry induced by `f : F â‰ƒâ‚—áµ¢ G`. -/
def _root_.LinearIsometry.lTensor (f : F â†’â‚—áµ¢[ğ•œ] G) :
    E âŠ—[ğ•œ] F â†’â‚—áµ¢[ğ•œ] E âŠ—[ğ•œ] G := mapIsometry .id f

variable (G) in
/-- This is the natural linear isometry induced by `f : E â‰ƒâ‚—áµ¢ F`. -/
def _root_.LinearIsometry.rTensor (f : E â†’â‚—áµ¢[ğ•œ] F) :
    E âŠ—[ğ•œ] G â†’â‚—áµ¢[ğ•œ] F âŠ—[ğ•œ] G := mapIsometry f .id

lemma _root_.LinearIsometry.lTensor_def (f : F â†’â‚—áµ¢[ğ•œ] G) :
    f.lTensor E = mapIsometry .id f := rfl

lemma _root_.LinearIsometry.rTensor_def (f : E â†’â‚—áµ¢[ğ•œ] F) :
    f.rTensor G = mapIsometry f .id := rfl

@[simp] lemma _root_.LinearIsometry.toLinearMap_lTensor (f : F â†’â‚—áµ¢[ğ•œ] G) :
    (f.lTensor E).toLinearMap = f.toLinearMap.lTensor E := rfl

@[simp] lemma _root_.LinearIsometry.toLinearMap_rTensor (f : E â†’â‚—áµ¢[ğ•œ] F) :
    (f.rTensor G).toLinearMap = f.toLinearMap.rTensor G := rfl

@[simp] lemma _root_.LinearIsometry.lTensor_apply (f : F â†’â‚—áµ¢[ğ•œ] G) (x : E âŠ—[ğ•œ] F) :
    f.lTensor E x = f.toLinearMap.lTensor E x := rfl

@[simp] lemma _root_.LinearIsometry.rTensor_apply (f : E â†’â‚—áµ¢[ğ•œ] F) (x : E âŠ—[ğ•œ] G) :
    f.rTensor G x = f.toLinearMap.rTensor G x := rfl

/-- The tensor product of two linear isometry equivalences is a linear isometry equivalence.
In particular, this is the linear isometry equivalence version of `TensorProduct.congr f g` when `f`
and `g` are linear isometry equivalences. -/
def congrIsometry (f : E â‰ƒâ‚—áµ¢[ğ•œ] G) (g : F â‰ƒâ‚—áµ¢[ğ•œ] H) :
    E âŠ—[ğ•œ] F â‰ƒâ‚—áµ¢[ğ•œ] G âŠ—[ğ•œ] H :=
  congr f.toLinearEquiv g.toLinearEquiv |>.isometryOfInner <|
    inner_map_map f.toLinearIsometry g.toLinearIsometry

@[simp] lemma congrIsometry_apply (f : E â‰ƒâ‚—áµ¢[ğ•œ] G) (g : F â‰ƒâ‚—áµ¢[ğ•œ] H) (x : E âŠ—[ğ•œ] F) :
    congrIsometry f g x = congr f g x := rfl

lemma congrIsometry_symm (f : E â‰ƒâ‚—áµ¢[ğ•œ] G) (g : F â‰ƒâ‚—áµ¢[ğ•œ] H) :
    (congrIsometry f g).symm = congrIsometry f.symm g.symm := rfl

@[simp] lemma toLinearEquiv_congrIsometry (f : E â‰ƒâ‚—áµ¢[ğ•œ] G) (g : F â‰ƒâ‚—áµ¢[ğ•œ] H) :
    (congrIsometry f g).toLinearEquiv = congr f.toLinearEquiv g.toLinearEquiv := rfl

@[simp] lemma congrIsometry_refl_refl :
    congrIsometry (.refl ğ•œ E) (.refl ğ•œ F) = .refl ğ•œ (E âŠ— F) :=
  LinearIsometryEquiv.toLinearEquiv_inj.mp <| LinearEquiv.toLinearMap_inj.mp <| by ext; simp

variable (E) in
/-- This is the natural linear isometric equivalence induced by `f : F â‰ƒâ‚—áµ¢ G`. -/
def _root_.LinearIsometryEquiv.lTensor (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) :
    E âŠ—[ğ•œ] F â‰ƒâ‚—áµ¢[ğ•œ] E âŠ—[ğ•œ] G := congrIsometry (.refl ğ•œ E) f

variable (G) in
/-- This is the natural linear isometric equivalence induced by `f : E â‰ƒâ‚—áµ¢ F`. -/
def _root_.LinearIsometryEquiv.rTensor (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    E âŠ—[ğ•œ] G â‰ƒâ‚—áµ¢[ğ•œ] F âŠ—[ğ•œ] G := congrIsometry f (.refl ğ•œ G)

lemma _root_.LinearIsometryEquiv.lTensor_def (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) :
    f.lTensor E = congrIsometry (.refl ğ•œ E) f := rfl

lemma _root_.LinearIsometryEquiv.rTensor_def (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    f.rTensor G = congrIsometry f (.refl ğ•œ G) := rfl

lemma _root_.LinearIsometryEquiv.symm_lTensor (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) :
    (f.lTensor E).symm = f.symm.lTensor E := rfl

lemma _root_.LinearIsometryEquiv.symm_rTensor (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    (f.rTensor G).symm = f.symm.rTensor G := rfl

@[simp] lemma _root_.LinearIsometryEquiv.toLinearEquiv_lTensor (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) :
    (f.lTensor E).toLinearEquiv = f.toLinearEquiv.lTensor E := rfl

@[simp] lemma _root_.LinearIsometryEquiv.toLinearIsometry_lTensor (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) :
    (f.lTensor E).toLinearIsometry = f.toLinearIsometry.lTensor E := rfl

@[simp] lemma _root_.LinearIsometryEquiv.toLinearEquiv_rTensor (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    (f.rTensor G).toLinearEquiv = f.toLinearEquiv.rTensor G := rfl

@[simp] lemma _root_.LinearIsometryEquiv.toLinearIsometry_rTensor (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    (f.rTensor G).toLinearIsometry = f.toLinearIsometry.rTensor G := rfl

@[simp] lemma _root_.LinearIsometryEquiv.lTensor_apply (f : F â‰ƒâ‚—áµ¢[ğ•œ] G) (x : E âŠ—[ğ•œ] F) :
    f.lTensor E x = f.toLinearEquiv.lTensor E x := rfl

@[simp] lemma _root_.LinearIsometryEquiv.rTensor_apply (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) (x : E âŠ—[ğ•œ] G) :
    f.rTensor G x = f.toLinearEquiv.rTensor G x := rfl

/-- The linear isometry version of `TensorProduct.mapIncl`. -/
def mapInclIsometry (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F) :
    E' âŠ—[ğ•œ] F' â†’â‚—áµ¢[ğ•œ] E âŠ—[ğ•œ] F :=
  mapIsometry E'.subtypeâ‚—áµ¢ F'.subtypeâ‚—áµ¢

@[simp] lemma mapInclIsometry_apply (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F)
    (x : E' âŠ—[ğ•œ] F') : mapInclIsometry E' F' x = mapIncl E' F' x := rfl

@[simp] lemma toLinearMap_mapInclIsometry (E' : Submodule ğ•œ E) (F' : Submodule ğ•œ F) :
    (mapInclIsometry E' F').toLinearMap = mapIncl E' F' := rfl

@[simp] theorem inner_comm_comm (x y : E âŠ—[ğ•œ] F) :
    inner ğ•œ (TensorProduct.comm ğ•œ E F x) (TensorProduct.comm ğ•œ E F y) = inner ğ•œ x y :=
  x.induction_on (by simp) (fun _ _ =>
    y.induction_on (by simp) (by simp [mul_comm])
    fun _ _ h1 h2 => by simp only [inner_add_right, map_add, h1, h2])
  fun _ _ h1 h2 => by simp only [inner_add_left, map_add, h1, h2]

variable (ğ•œ E F) in
/-- The linear isometry equivalence version of `TensorProduct.comm`. -/
def commIsometry : E âŠ—[ğ•œ] F â‰ƒâ‚—áµ¢[ğ•œ] F âŠ—[ğ•œ] E :=
  TensorProduct.comm ğ•œ E F |>.isometryOfInner inner_comm_comm

@[simp] lemma commIsometry_apply (x : E âŠ—[ğ•œ] F) :
    commIsometry ğ•œ E F x = TensorProduct.comm ğ•œ E F x := rfl
@[simp] lemma commIsometry_symm :
    (commIsometry ğ•œ E F).symm = commIsometry ğ•œ F E := rfl

@[simp] lemma toLinearEquiv_commIsometry :
    (commIsometry ğ•œ E F).toLinearEquiv = TensorProduct.comm ğ•œ E F := rfl

@[simp] lemma norm_comm (x : E âŠ—[ğ•œ] F) :
    â€–TensorProduct.comm ğ•œ E F xâ€– = â€–xâ€– := commIsometry ğ•œ E F |>.norm_map x
@[simp] lemma nnnorm_comm (x : E âŠ—[ğ•œ] F) :
    â€–TensorProduct.comm ğ•œ E F xâ€–â‚Š = â€–xâ€–â‚Š := commIsometry ğ•œ E F |>.nnnorm_map x
@[simp] lemma enorm_comm (x : E âŠ—[ğ•œ] F) :
    â€–TensorProduct.comm ğ•œ E F xâ€–â‚‘ = â€–xâ€–â‚‘ := commIsometry ğ•œ E F |>.toLinearIsometry.enorm_map x

@[simp] theorem inner_lid_lid (x y : ğ•œ âŠ—[ğ•œ] E) :
    inner ğ•œ (TensorProduct.lid ğ•œ E x) (TensorProduct.lid ğ•œ E y) = inner ğ•œ x y :=
  x.induction_on (by simp) (fun _ _ =>
    y.induction_on (by simp) (by simp [inner_smul_left, inner_smul_right, mul_assoc])
    fun _ _ h1 h2 => by simp only [inner_add_right, map_add, h1, h2])
  fun _ _ h1 h2 => by simp only [inner_add_left, map_add, h1, h2]

variable (ğ•œ E) in
/-- The linear isometry equivalence version of `TensorProduct.lid`. -/
def lidIsometry : ğ•œ âŠ—[ğ•œ] E â‰ƒâ‚—áµ¢[ğ•œ] E :=
  TensorProduct.lid ğ•œ E |>.isometryOfInner inner_lid_lid

@[simp] lemma lidIsometry_apply (x : ğ•œ âŠ—[ğ•œ] E) :
    lidIsometry ğ•œ E x = TensorProduct.lid ğ•œ E x := rfl
@[simp] lemma lidIsometry_symm_apply (x : E) :
    (lidIsometry ğ•œ E).symm x = 1 âŠ—â‚œ x := rfl

@[simp] lemma toLinearEquiv_lidIsometry :
    (lidIsometry ğ•œ E).toLinearEquiv = TensorProduct.lid ğ•œ E := rfl

@[simp] lemma norm_lid (x : ğ•œ âŠ—[ğ•œ] E) :
    â€–TensorProduct.lid ğ•œ E xâ€– = â€–xâ€– := lidIsometry ğ•œ E |>.norm_map x
@[simp] lemma nnnorm_lid (x : ğ•œ âŠ—[ğ•œ] E) :
    â€–TensorProduct.lid ğ•œ E xâ€–â‚Š = â€–xâ€–â‚Š := lidIsometry ğ•œ E |>.nnnorm_map x
@[simp] lemma enorm_lid (x : ğ•œ âŠ—[ğ•œ] E) :
    â€–TensorProduct.lid ğ•œ E xâ€–â‚‘ = â€–xâ€–â‚‘ := lidIsometry ğ•œ E |>.toLinearIsometry.enorm_map x

@[simp] theorem inner_assoc_assoc (x y : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G) :
    inner ğ•œ (TensorProduct.assoc ğ•œ E F G x) (TensorProduct.assoc ğ•œ E F G y) = inner ğ•œ x y :=
  x.induction_on (by simp) (fun a _ =>
    y.induction_on (by simp) (fun c _ =>
      a.induction_on (by simp) (fun _ _ =>
        c.induction_on (by simp) (by simp [mul_assoc])
        fun _ _ h1 h2 => by simp only [add_tmul, inner_add_right, map_add, h1, h2])
      fun _ _ h1 h2 => by simp only [add_tmul, inner_add_left, map_add, h1, h2])
    fun _ _ h1 h2 => by simp only [inner_add_right, map_add, h1, h2])
  fun _ _ h1 h2 => by simp only [inner_add_left, map_add, h1, h2]

variable (ğ•œ E F G) in
/-- The linear isometry equivalence version of `TensorProduct.assoc`. -/
def assocIsometry : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G â‰ƒâ‚—áµ¢[ğ•œ] E âŠ—[ğ•œ] (F âŠ—[ğ•œ] G) :=
  TensorProduct.assoc ğ•œ E F G |>.isometryOfInner inner_assoc_assoc

@[simp] lemma assocIsometry_apply (x : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G) :
    assocIsometry ğ•œ E F G x = TensorProduct.assoc ğ•œ E F G x := rfl
@[simp] lemma assocIsometry_symm_apply (x : E âŠ—[ğ•œ] (F âŠ—[ğ•œ] G)) :
    (assocIsometry ğ•œ E F G).symm x = (TensorProduct.assoc ğ•œ E F G).symm x := rfl

@[simp] lemma toLinearEquiv_assocIsometry :
    (assocIsometry ğ•œ E F G).toLinearEquiv = TensorProduct.assoc ğ•œ E F G := rfl

@[simp] lemma norm_assoc (x : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G) :
    â€–TensorProduct.assoc ğ•œ E F G xâ€– = â€–xâ€– := assocIsometry ğ•œ E F G |>.norm_map x
@[simp] lemma nnnorm_assoc (x : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G) :
    â€–TensorProduct.assoc ğ•œ E F G xâ€–â‚Š = â€–xâ€–â‚Š := assocIsometry ğ•œ E F G |>.nnnorm_map x
@[simp] lemma enorm_assoc (x : E âŠ—[ğ•œ] F âŠ—[ğ•œ] G) :
    â€–TensorProduct.assoc ğ•œ E F G xâ€–â‚‘ = â€–xâ€–â‚‘ := assocIsometry ğ•œ E F G |>.toLinearIsometry.enorm_map x

end isometry

-- TODO: upgrade `map` to a `ContinuousLinearMap`
@[simp] theorem adjoint_map [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F] [FiniteDimensional ğ•œ G]
    [FiniteDimensional ğ•œ H] (f : E â†’â‚—[ğ•œ] F) (g : G â†’â‚—[ğ•œ] H) :
    LinearMap.adjoint (map f g) = map (LinearMap.adjoint f) (LinearMap.adjoint g) :=
  ext' fun _ _ => by simp [TensorProduct.ext_iff_inner_right, LinearMap.adjoint_inner_left]

open LinearMap

@[simp] theorem _root_.LinearMap.adjoint_rTensor [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]
    [FiniteDimensional ğ•œ G] (f : E â†’â‚—[ğ•œ] F) :
    adjoint (rTensor G f) = rTensor G f.adjoint := by simp [rTensor]

@[simp] theorem _root_.LinearMap.adjoint_lTensor [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]
    [FiniteDimensional ğ•œ G] (f : E â†’â‚—[ğ•œ] F) :
    adjoint (lTensor G f) = lTensor G f.adjoint := by simp [lTensor]

/-- Given `x, y : E âŠ— (F âŠ— G)`, `x = y` iff `âŸªx, a âŠ—â‚œ (b âŠ—â‚œ c)âŸ« = âŸªy, a âŠ—â‚œ (b âŠ—â‚œ c)âŸ«` for all
`a, b, c`.

See also `ext_iff_inner_right_threefold` for when `x, y : E âŠ— F âŠ— G`. -/
theorem ext_iff_inner_right_threefold' {x y : E âŠ—[ğ•œ] (F âŠ—[ğ•œ] G)} :
    x = y â†” âˆ€ a b c, inner ğ•œ x (a âŠ—â‚œ[ğ•œ] (b âŠ—â‚œ[ğ•œ] c)) = inner ğ•œ y (a âŠ—â‚œ[ğ•œ] (b âŠ—â‚œ[ğ•œ] c)) := by
  simp only [â† (assocIsometry ğ•œ E F G).symm.injective.eq_iff,
    ext_iff_inner_right_threefold, LinearIsometryEquiv.inner_map_eq_flip]
  simp

/-- Given `x, y : E âŠ— (F âŠ— G)`, `x = y` iff `âŸªa âŠ—â‚œ (b âŠ—â‚œ c), xâŸ« = âŸªa âŠ—â‚œ (b âŠ—â‚œ c), yâŸ«` for all
`a, b, c`.

See also `ext_iff_inner_left_threefold` for when `x, y : E âŠ— F âŠ— G`. -/
theorem ext_iff_inner_left_threefold' {x y : E âŠ—[ğ•œ] (F âŠ—[ğ•œ] G)} :
    x = y â†” âˆ€ a b c, inner ğ•œ (a âŠ—â‚œ[ğ•œ] (b âŠ—â‚œ[ğ•œ] c)) x = inner ğ•œ (a âŠ—â‚œ[ğ•œ] (b âŠ—â‚œ[ğ•œ] c)) y := by
  simpa only [â† inner_conj_symm x, â† inner_conj_symm y, starRingEnd_apply, star_inj] using
    ext_iff_inner_right_threefold' (x := x) (y := y)

end TensorProduct

section orthonormal
variable {Î¹â‚ Î¹â‚‚ : Type*} [DecidableEq Î¹â‚] [DecidableEq Î¹â‚‚]

open Module

/-- The tensor product of two orthonormal vectors is orthonormal. -/
theorem Orthonormal.tmul
    {bâ‚ : Î¹â‚ â†’ E} {bâ‚‚ : Î¹â‚‚ â†’ F} (hbâ‚ : Orthonormal ğ•œ bâ‚) (hbâ‚‚ : Orthonormal ğ•œ bâ‚‚) :
    Orthonormal ğ•œ fun i : Î¹â‚ Ã— Î¹â‚‚ â†¦ bâ‚ i.1 âŠ—â‚œ[ğ•œ] bâ‚‚ i.2 :=
  orthonormal_iff_ite.mpr fun âŸ¨iâ‚, iâ‚‚âŸ© âŸ¨jâ‚, jâ‚‚âŸ© => by
    simp [orthonormal_iff_ite.mp, hbâ‚, hbâ‚‚, â† ite_and, and_comm]

/-- The tensor product of two orthonormal bases is orthonormal. -/
theorem Orthonormal.basisTensorProduct
    {bâ‚ : Basis Î¹â‚ ğ•œ E} {bâ‚‚ : Basis Î¹â‚‚ ğ•œ F} (hbâ‚ : Orthonormal ğ•œ bâ‚) (hbâ‚‚ : Orthonormal ğ•œ bâ‚‚) :
    Orthonormal ğ•œ (bâ‚.tensorProduct bâ‚‚) := by
  convert hbâ‚.tmul hbâ‚‚
  exact bâ‚.tensorProduct_apply' bâ‚‚ _

namespace OrthonormalBasis
variable [Fintype Î¹â‚] [Fintype Î¹â‚‚]

/-- The orthonormal basis of the tensor product of two orthonormal bases. -/
protected noncomputable def tensorProduct
    (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F) :
    OrthonormalBasis (Î¹â‚ Ã— Î¹â‚‚) ğ•œ (E âŠ—[ğ•œ] F) :=
  (bâ‚.toBasis.tensorProduct bâ‚‚.toBasis).toOrthonormalBasis
    (bâ‚.orthonormal.basisTensorProduct bâ‚‚.orthonormal)

@[simp]
lemma tensorProduct_apply
    (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F) (i : Î¹â‚) (j : Î¹â‚‚) :
    bâ‚.tensorProduct bâ‚‚ (i, j) = bâ‚ i âŠ—â‚œ[ğ•œ] bâ‚‚ j := by simp [OrthonormalBasis.tensorProduct]

lemma tensorProduct_apply'
    (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F) (i : Î¹â‚ Ã— Î¹â‚‚) :
    bâ‚.tensorProduct bâ‚‚ i = bâ‚ i.1 âŠ—â‚œ[ğ•œ] bâ‚‚ i.2 := tensorProduct_apply _ _ _ _

@[simp]
lemma tensorProduct_repr_tmul_apply (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F)
    (x : E) (y : F) (i : Î¹â‚) (j : Î¹â‚‚) :
    (bâ‚.tensorProduct bâ‚‚).repr (x âŠ—â‚œ[ğ•œ] y) (i, j) = bâ‚‚.repr y j * bâ‚.repr x i := by
  simp [OrthonormalBasis.tensorProduct]

lemma tensorProduct_repr_tmul_apply'
    (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F) (x : E) (y : F) (i : Î¹â‚ Ã— Î¹â‚‚) :
    (bâ‚.tensorProduct bâ‚‚).repr (x âŠ—â‚œ[ğ•œ] y) i = bâ‚‚.repr y i.2 * bâ‚.repr x i.1 :=
  tensorProduct_repr_tmul_apply _ _ _ _ _ _

@[simp]
lemma toBasis_tensorProduct (bâ‚ : OrthonormalBasis Î¹â‚ ğ•œ E) (bâ‚‚ : OrthonormalBasis Î¹â‚‚ ğ•œ F) :
    (bâ‚.tensorProduct bâ‚‚).toBasis = bâ‚.toBasis.tensorProduct bâ‚‚.toBasis := by
  simp [OrthonormalBasis.tensorProduct]

end OrthonormalBasis
end orthonormal
