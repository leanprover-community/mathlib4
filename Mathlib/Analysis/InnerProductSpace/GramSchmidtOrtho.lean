/-
Copyright (c) 2022 Jiale Miao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jiale Miao, Kevin Buzzard, Alexander Bentkamp
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.LinearAlgebra.Matrix.Block

/-!
# Gram-Schmidt Orthogonalization and Orthonormalization

In this file we introduce Gram-Schmidt Orthogonalization and Orthonormalization.

The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span.

## Main results

- `gramSchmidt`: the Gram-Schmidt process
- `gramSchmidt_orthogonal`: `gramSchmidt` produces an orthogonal system of vectors.
- `span_gramSchmidt`: `gramSchmidt` preserves span of vectors.
- `gramSchmidt_linearIndependent`: if the input vectors of `gramSchmidt` are linearly independent,
  then so are the output vectors.
- `gramSchmidt_ne_zero`: if the input vectors of `gramSchmidt` are linearly independent,
  then the output vectors are non-zero.
- `gramSchmidtBasis`: the basis produced by the Gram-Schmidt process when given a basis as input
- `gramSchmidtNormed`:
  the normalized `gramSchmidt` process, i.e each vector in `gramSchmidtNormed` has unit length
- `gramSchmidt_orthonormal`: `gramSchmidtNormed` produces an orthornormal system of vectors.
- `gramSchmidtOrthonormalBasis`: orthonormal basis constructed by the Gram-Schmidt process from
  an indexed set of vectors of the right size
-/


open Finset Submodule Module

variable (ğ•œ : Type*) {E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {Î¹ : Type*} [LinearOrder Î¹] [LocallyFiniteOrderBot Î¹] [WellFoundedLT Î¹]

attribute [local instance] IsWellOrder.toHasWellFounded

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

namespace InnerProductSpace

/-- The Gram-Schmidt process takes a set of vectors as input
and outputs a set of orthogonal vectors which have the same span. -/
noncomputable def gramSchmidt [WellFoundedLT Î¹] (f : Î¹ â†’ E) (n : Î¹) : E :=
  f n - âˆ‘ i : Iio n, (ğ•œ âˆ™ gramSchmidt f i).starProjection (f n)
termination_by n
decreasing_by exact mem_Iio.1 i.2

/-- This lemma uses `âˆ‘ i in` instead of `âˆ‘ i :`. -/
theorem gramSchmidt_def (f : Î¹ â†’ E) (n : Î¹) :
    gramSchmidt ğ•œ f n = f n - âˆ‘ i âˆˆ Iio n, (ğ•œ âˆ™ gramSchmidt ğ•œ f i).starProjection (f n) := by
  rw [â† sum_attach, attach_eq_univ, gramSchmidt]

theorem gramSchmidt_def' (f : Î¹ â†’ E) (n : Î¹) :
    f n = gramSchmidt ğ•œ f n + âˆ‘ i âˆˆ Iio n, (ğ•œ âˆ™ gramSchmidt ğ•œ f i).starProjection (f n) := by
  rw [gramSchmidt_def, sub_add_cancel]

theorem gramSchmidt_def'' (f : Î¹ â†’ E) (n : Î¹) :
    f n = gramSchmidt ğ•œ f n + âˆ‘ i âˆˆ Iio n,
      (âŸªgramSchmidt ğ•œ f i, f nâŸ« / (â€–gramSchmidt ğ•œ f iâ€– : ğ•œ) ^ 2) â€¢ gramSchmidt ğ•œ f i := by
  simp only [â† map_pow, â† starProjection_singleton, â† gramSchmidt_def' ğ•œ f n]

@[simp]
theorem gramSchmidt_bot {Î¹ : Type*} [LinearOrder Î¹] [LocallyFiniteOrder Î¹] [OrderBot Î¹]
    [WellFoundedLT Î¹] (f : Î¹ â†’ E) : gramSchmidt ğ•œ f âŠ¥ = f âŠ¥ := by
  rw [gramSchmidt_def, Iio_eq_Ico, Finset.Ico_self, Finset.sum_empty, sub_zero]

@[simp]
theorem gramSchmidt_zero (n : Î¹) : gramSchmidt ğ•œ (0 : Î¹ â†’ E) n = 0 := by rw [gramSchmidt_def]; simp

/-- **Gram-Schmidt Orthogonalisation**:
`gramSchmidt` produces an orthogonal system of vectors. -/
theorem gramSchmidt_orthogonal (f : Î¹ â†’ E) {a b : Î¹} (hâ‚€ : a â‰  b) :
    âŸªgramSchmidt ğ•œ f a, gramSchmidt ğ•œ f bâŸ« = 0 := by
  suffices âˆ€ a b : Î¹, a < b â†’ âŸªgramSchmidt ğ•œ f a, gramSchmidt ğ•œ f bâŸ« = 0 by
    rcases hâ‚€.lt_or_gt with ha | hb
    Â· exact this _ _ ha
    Â· rw [inner_eq_zero_symm]
      exact this _ _ hb
  clear hâ‚€ a b
  intro a b hâ‚€
  revert a
  apply wellFounded_lt.induction b
  intro b ih a hâ‚€
  simp only [gramSchmidt_def ğ•œ f b, inner_sub_right, inner_sum,
    starProjection_singleton, inner_smul_right]
  rw [Finset.sum_eq_single_of_mem a (Finset.mem_Iio.mpr hâ‚€)]
  Â· by_cases h : gramSchmidt ğ•œ f a = 0
    Â· simp only [h, inner_zero_left, zero_div, zero_mul, sub_zero]
    Â· rw [RCLike.ofReal_pow, â† inner_self_eq_norm_sq_to_K, div_mul_cancelâ‚€, sub_self]
      rwa [inner_self_ne_zero]
  intro i hi hia
  simp only [mul_eq_zero, div_eq_zero_iff]
  right
  rcases hia.lt_or_gt with hiaâ‚ | hiaâ‚‚
  Â· rw [inner_eq_zero_symm]
    exact ih a hâ‚€ i hiaâ‚
  Â· exact ih i (mem_Iio.1 hi) a hiaâ‚‚

/-- This is another version of `gramSchmidt_orthogonal` using `Pairwise` instead. -/
theorem gramSchmidt_pairwise_orthogonal (f : Î¹ â†’ E) :
    Pairwise fun a b => âŸªgramSchmidt ğ•œ f a, gramSchmidt ğ•œ f bâŸ« = 0 := fun _ _ =>
  gramSchmidt_orthogonal ğ•œ f

theorem gramSchmidt_inv_triangular (v : Î¹ â†’ E) {i j : Î¹} (hij : i < j) :
    âŸªgramSchmidt ğ•œ v j, v iâŸ« = 0 := by
  rw [gramSchmidt_def'' ğ•œ v]
  simp only [inner_add_right, inner_sum, inner_smul_right]
  set b : Î¹ â†’ E := gramSchmidt ğ•œ v
  convert zero_add (0 : ğ•œ)
  Â· exact gramSchmidt_orthogonal ğ•œ v hij.ne'
  apply Finset.sum_eq_zero
  rintro k hki'
  have hki : k < i := by simpa using hki'
  have : âŸªb j, b kâŸ« = 0 := gramSchmidt_orthogonal ğ•œ v (hki.trans hij).ne'
  simp [this]

open Submodule Set Order

theorem mem_span_gramSchmidt (f : Î¹ â†’ E) {i j : Î¹} (hij : i â‰¤ j) :
    f i âˆˆ span ğ•œ (gramSchmidt ğ•œ f '' Set.Iic j) := by
  rw [gramSchmidt_def' ğ•œ f i]
  simp_rw [starProjection_singleton]
  exact Submodule.add_mem _ (subset_span <| mem_image_of_mem _ hij)
    (Submodule.sum_mem _ fun k hk => smul_mem (span ğ•œ (gramSchmidt ğ•œ f '' Set.Iic j)) _ <|
      subset_span <| mem_image_of_mem (gramSchmidt ğ•œ f) <| (Finset.mem_Iio.1 hk).le.trans hij)

theorem gramSchmidt_mem_span (f : Î¹ â†’ E) :
    âˆ€ {j i}, i â‰¤ j â†’ gramSchmidt ğ•œ f i âˆˆ span ğ•œ (f '' Set.Iic j) := by
  intro j i hij
  rw [gramSchmidt_def ğ•œ f i]
  simp_rw [starProjection_singleton]
  refine Submodule.sub_mem _ (subset_span (mem_image_of_mem _ hij))
    (Submodule.sum_mem _ fun k hk => ?_)
  let hkj : k < j := (Finset.mem_Iio.1 hk).trans_le hij
  exact smul_mem _ _
    (span_mono (image_mono <| Set.Iic_subset_Iic.2 hkj.le) <| gramSchmidt_mem_span _ le_rfl)
termination_by j => j

theorem span_gramSchmidt_Iic (f : Î¹ â†’ E) (c : Î¹) :
    span ğ•œ (gramSchmidt ğ•œ f '' Set.Iic c) = span ğ•œ (f '' Set.Iic c) :=
  span_eq_span (Set.image_subset_iff.2 fun _ => gramSchmidt_mem_span _ _) <|
    Set.image_subset_iff.2 fun _ => mem_span_gramSchmidt _ _

theorem span_gramSchmidt_Iio (f : Î¹ â†’ E) (c : Î¹) :
    span ğ•œ (gramSchmidt ğ•œ f '' Set.Iio c) = span ğ•œ (f '' Set.Iio c) :=
  span_eq_span (Set.image_subset_iff.2 fun _ hi =>
    span_mono (image_mono <| Iic_subset_Iio.2 hi) <| gramSchmidt_mem_span _ _ le_rfl) <|
      Set.image_subset_iff.2 fun _ hi =>
        span_mono (image_mono <| Iic_subset_Iio.2 hi) <| mem_span_gramSchmidt _ _ le_rfl

/-- `gramSchmidt` preserves span of vectors. -/
theorem span_gramSchmidt (f : Î¹ â†’ E) : span ğ•œ (range (gramSchmidt ğ•œ f)) = span ğ•œ (range f) :=
  span_eq_span (range_subset_iff.2 fun _ =>
    span_mono (image_subset_range _ _) <| gramSchmidt_mem_span _ _ le_rfl) <|
      range_subset_iff.2 fun _ =>
        span_mono (image_subset_range _ _) <| mem_span_gramSchmidt _ _ le_rfl

/-- If given an orthogonal set of vectors, `gramSchmidt` fixes its input. -/
theorem gramSchmidt_of_orthogonal {f : Î¹ â†’ E} (hf : Pairwise (âŸªf Â·, f Â·âŸ« = 0)) :
    gramSchmidt ğ•œ f = f := by
  ext i
  rw [gramSchmidt_def]
  trans f i - 0
  Â· congr
    apply Finset.sum_eq_zero
    intro j hj
    rw [Submodule.starProjection_apply, Submodule.coe_eq_zero]
    suffices span ğ•œ (f '' Set.Iic j) âŸ‚ ğ•œ âˆ™ f i by
      apply orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero
      rw [mem_orthogonal_singleton_iff_inner_left, â† mem_orthogonal_singleton_iff_inner_right]
      exact this (gramSchmidt_mem_span ğ•œ f (le_refl j))
    rw [isOrtho_span]
    rintro u âŸ¨k, hk, rflâŸ© v (rfl : v = f i)
    apply hf
    exact (lt_of_le_of_lt hk (Finset.mem_Iio.mp hj)).ne
  Â· simp

variable {ğ•œ}

theorem gramSchmidt_ne_zero_coe {f : Î¹ â†’ E} (n : Î¹)
    (hâ‚€ : LinearIndependent ğ•œ (f âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹))) : gramSchmidt ğ•œ f n â‰  0 := by
  by_contra h
  have hâ‚ : f n âˆˆ span ğ•œ (f '' Set.Iio n) := by
    rw [â† span_gramSchmidt_Iio ğ•œ f n, gramSchmidt_def' ğ•œ f, h, zero_add]
    apply Submodule.sum_mem _ _
    intro a ha
    simp only [starProjection_singleton]
    apply Submodule.smul_mem _ _ _
    rw [Finset.mem_Iio] at ha
    exact subset_span âŸ¨a, ha, by rflâŸ©
  have hâ‚‚ : (f âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹)) âŸ¨n, le_refl nâŸ© âˆˆ
      span ğ•œ (f âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹) '' Set.Iio âŸ¨n, le_refl nâŸ©) := by
    rw [image_comp]
    simpa using hâ‚
  apply LinearIndependent.notMem_span_image hâ‚€ _ hâ‚‚
  simp only [Set.mem_Iio, lt_self_iff_false, not_false_iff]

/-- If the input vectors of `gramSchmidt` are linearly independent,
then the output vectors are non-zero. -/
theorem gramSchmidt_ne_zero {f : Î¹ â†’ E} (n : Î¹) (hâ‚€ : LinearIndependent ğ•œ f) :
    gramSchmidt ğ•œ f n â‰  0 :=
  gramSchmidt_ne_zero_coe _ (LinearIndependent.comp hâ‚€ _ Subtype.coe_injective)

/-- `gramSchmidt` produces a triangular matrix of vectors when given a basis. -/
theorem gramSchmidt_triangular {i j : Î¹} (hij : i < j) (b : Basis Î¹ ğ•œ E) :
    b.repr (gramSchmidt ğ•œ b i) j = 0 := by
  have : gramSchmidt ğ•œ b i âˆˆ span ğ•œ (gramSchmidt ğ•œ b '' Set.Iio j) :=
    subset_span ((Set.mem_image _ _ _).2 âŸ¨i, hij, rflâŸ©)
  have : gramSchmidt ğ•œ b i âˆˆ span ğ•œ (b '' Set.Iio j) := by rwa [â† span_gramSchmidt_Iio ğ•œ b j]
  have : â†‘(b.repr (gramSchmidt ğ•œ b i)).support âŠ† Set.Iio j :=
    Basis.repr_support_subset_of_mem_span b (Set.Iio j) this
  exact (Finsupp.mem_supported' _ _).1 ((Finsupp.mem_supported ğ•œ _).2 this) j Set.notMem_Iio_self

/-- `gramSchmidt` produces linearly independent vectors when given linearly independent vectors. -/
theorem gramSchmidt_linearIndependent {f : Î¹ â†’ E} (hâ‚€ : LinearIndependent ğ•œ f) :
    LinearIndependent ğ•œ (gramSchmidt ğ•œ f) :=
  linearIndependent_of_ne_zero_of_inner_eq_zero (fun _ => gramSchmidt_ne_zero _ hâ‚€) fun _ _ =>
    gramSchmidt_orthogonal ğ•œ f

/-- When given a basis, `gramSchmidt` produces a basis. -/
noncomputable def gramSchmidtBasis (b : Basis Î¹ ğ•œ E) : Basis Î¹ ğ•œ E :=
  Basis.mk (gramSchmidt_linearIndependent b.linearIndependent)
    ((span_gramSchmidt ğ•œ b).trans b.span_eq).ge

theorem coe_gramSchmidtBasis (b : Basis Î¹ ğ•œ E) : (gramSchmidtBasis b : Î¹ â†’ E) = gramSchmidt ğ•œ b :=
  Basis.coe_mk _ _

variable (ğ•œ) in
/-- the normalized `gramSchmidt` (i.e each vector in `gramSchmidtNormed` has unit length.) -/
noncomputable def gramSchmidtNormed (f : Î¹ â†’ E) (n : Î¹) : E :=
  (â€–gramSchmidt ğ•œ f nâ€– : ğ•œ)â»Â¹ â€¢ gramSchmidt ğ•œ f n

theorem gramSchmidtNormed_unit_length_coe {f : Î¹ â†’ E} (n : Î¹)
    (hâ‚€ : LinearIndependent ğ•œ (f âˆ˜ ((â†‘) : Set.Iic n â†’ Î¹))) : â€–gramSchmidtNormed ğ•œ f nâ€– = 1 := by
  simp only [gramSchmidt_ne_zero_coe n hâ‚€, gramSchmidtNormed, norm_smul_inv_norm, Ne,
    not_false_iff]

theorem gramSchmidtNormed_unit_length {f : Î¹ â†’ E} (n : Î¹) (hâ‚€ : LinearIndependent ğ•œ f) :
    â€–gramSchmidtNormed ğ•œ f nâ€– = 1 :=
  gramSchmidtNormed_unit_length_coe _ (LinearIndependent.comp hâ‚€ _ Subtype.coe_injective)

theorem gramSchmidtNormed_unit_length' {f : Î¹ â†’ E} {n : Î¹} (hn : gramSchmidtNormed ğ•œ f n â‰  0) :
    â€–gramSchmidtNormed ğ•œ f nâ€– = 1 := by
  rw [gramSchmidtNormed] at *
  rw [norm_smul_inv_norm]
  simpa using hn

/-- **Gram-Schmidt Orthonormalization**:
`gramSchmidtNormed` applied to a linearly independent set of vectors produces an orthornormal
system of vectors. -/
theorem gramSchmidtNormed_orthonormal {f : Î¹ â†’ E} (hâ‚€ : LinearIndependent ğ•œ f) :
    Orthonormal ğ•œ (gramSchmidtNormed ğ•œ f) := by
  unfold Orthonormal
  constructor
  Â· simp only [gramSchmidtNormed_unit_length, hâ‚€, imp_true_iff]
  Â· intro i j hij
    simp only [gramSchmidtNormed, inner_smul_left, inner_smul_right, RCLike.conj_inv,
      RCLike.conj_ofReal, mul_eq_zero, inv_eq_zero, RCLike.ofReal_eq_zero, norm_eq_zero]
    repeat' right
    exact gramSchmidt_orthogonal ğ•œ f hij

@[deprecated (since := "2025-07-10")] alias gramSchmidt_orthonormal := gramSchmidtNormed_orthonormal

/-- **Gram-Schmidt Orthonormalization**:
`gramSchmidtNormed` produces an orthornormal system of vectors after removing the vectors which
become zero in the process. -/
theorem gramSchmidtNormed_orthonormal' (f : Î¹ â†’ E) :
    Orthonormal ğ•œ fun i : { i | gramSchmidtNormed ğ•œ f i â‰  0 } => gramSchmidtNormed ğ•œ f i := by
  refine âŸ¨fun i => gramSchmidtNormed_unit_length' i.prop, ?_âŸ©
  rintro i j (hij : Â¬_)
  rw [Subtype.ext_iff] at hij
  simp [gramSchmidtNormed, inner_smul_left, inner_smul_right, gramSchmidt_orthogonal ğ•œ f hij]

@[deprecated (since := "2025-07-10")]
alias gramSchmidt_orthonormal' := gramSchmidtNormed_orthonormal'

open Submodule Set Order

theorem span_gramSchmidtNormed (f : Î¹ â†’ E) (s : Set Î¹) :
    span ğ•œ (gramSchmidtNormed ğ•œ f '' s) = span ğ•œ (gramSchmidt ğ•œ f '' s) := by
  refine span_eq_span
    (Set.image_subset_iff.2 fun i hi => smul_mem _ _ <| subset_span <| mem_image_of_mem _ hi)
    (Set.image_subset_iff.2 fun i hi =>
      span_mono (image_mono <| singleton_subset_set_iff.2 hi) ?_)
  simp only [coe_singleton, Set.image_singleton]
  by_cases h : gramSchmidt ğ•œ f i = 0
  Â· simp [h]
  Â· refine mem_span_singleton.2 âŸ¨â€–gramSchmidt ğ•œ f iâ€–, smul_inv_smulâ‚€ ?_ _âŸ©
    exact mod_cast norm_ne_zero_iff.2 h

theorem span_gramSchmidtNormed_range (f : Î¹ â†’ E) :
    span ğ•œ (range (gramSchmidtNormed ğ•œ f)) = span ğ•œ (range (gramSchmidt ğ•œ f)) := by
  simpa only [image_univ.symm] using span_gramSchmidtNormed f univ

/-- `gramSchmidtNormed` produces linearly independent vectors when given linearly independent
vectors. -/
theorem gramSchmidtNormed_linearIndependent {f : Î¹ â†’ E} (hâ‚€ : LinearIndependent ğ•œ f) :
    LinearIndependent ğ•œ (gramSchmidtNormed ğ•œ f) := by
  unfold gramSchmidtNormed
  have (i : Î¹) : IsUnit (â€–gramSchmidt ğ•œ f iâ€–â»Â¹ : ğ•œ) :=
    isUnit_iff_ne_zero.mpr (by simp [gramSchmidt_ne_zero i hâ‚€])
  let w : Î¹ â†’ ğ•œË£ := fun i â†¦ (this i).unit
  apply (gramSchmidt_linearIndependent hâ‚€).units_smul (w := fun i â†¦ (this i).unit)

section OrthonormalBasis

variable [Fintype Î¹] [FiniteDimensional ğ•œ E] (h : finrank ğ•œ E = Fintype.card Î¹) (f : Î¹ â†’ E)

/-- Given an indexed family `f : Î¹ â†’ E` of vectors in an inner product space `E`, for which the
size of the index set is the dimension of `E`, produce an orthonormal basis for `E` which agrees
with the orthonormal set produced by the Gram-Schmidt orthonormalization process on the elements of
`Î¹` for which this process gives a nonzero number. -/
noncomputable def gramSchmidtOrthonormalBasis : OrthonormalBasis Î¹ ğ•œ E :=
  ((gramSchmidtNormed_orthonormal' f).exists_orthonormalBasis_extension_of_card_eq
    (v := gramSchmidtNormed ğ•œ f) h).choose

theorem gramSchmidtOrthonormalBasis_apply {f : Î¹ â†’ E} {i : Î¹} (hi : gramSchmidtNormed ğ•œ f i â‰  0) :
    gramSchmidtOrthonormalBasis h f i = gramSchmidtNormed ğ•œ f i :=
  ((gramSchmidtNormed_orthonormal' f).exists_orthonormalBasis_extension_of_card_eq
    (v := gramSchmidtNormed ğ•œ f) h).choose_spec i hi

theorem gramSchmidtOrthonormalBasis_apply_of_orthogonal {f : Î¹ â†’ E}
    (hf : Pairwise fun i j => âŸªf i, f jâŸ« = 0) {i : Î¹} (hi : f i â‰  0) :
    gramSchmidtOrthonormalBasis h f i = (â€–f iâ€–â»Â¹ : ğ•œ) â€¢ f i := by
  have H : gramSchmidtNormed ğ•œ f i = (â€–f iâ€–â»Â¹ : ğ•œ) â€¢ f i := by
    rw [gramSchmidtNormed, gramSchmidt_of_orthogonal ğ•œ hf]
  rw [gramSchmidtOrthonormalBasis_apply h, H]
  simpa [H] using hi

theorem inner_gramSchmidtOrthonormalBasis_eq_zero {f : Î¹ â†’ E} {i : Î¹}
    (hi : gramSchmidtNormed ğ•œ f i = 0) (j : Î¹) : âŸªgramSchmidtOrthonormalBasis h f i, f jâŸ« = 0 := by
  rw [â† mem_orthogonal_singleton_iff_inner_right]
  suffices span ğ•œ (gramSchmidtNormed ğ•œ f '' Set.Iic j) âŸ‚ ğ•œ âˆ™ gramSchmidtOrthonormalBasis h f i by
    apply this
    rw [span_gramSchmidtNormed]
    exact mem_span_gramSchmidt ğ•œ f le_rfl
  rw [isOrtho_span]
  rintro u âŸ¨k, _, rflâŸ© v (rfl : v = _)
  by_cases hk : gramSchmidtNormed ğ•œ f k = 0
  Â· rw [hk, inner_zero_left]
  rw [â† gramSchmidtOrthonormalBasis_apply h hk]
  have : k â‰  i := by
    rintro rfl
    exact hk hi
  exact (gramSchmidtOrthonormalBasis h f).orthonormal.2 this

theorem gramSchmidtOrthonormalBasis_inv_triangular {i j : Î¹} (hij : i < j) :
    âŸªgramSchmidtOrthonormalBasis h f j, f iâŸ« = 0 := by
  by_cases hi : gramSchmidtNormed ğ•œ f j = 0
  Â· rw [inner_gramSchmidtOrthonormalBasis_eq_zero h hi]
  Â· simp [gramSchmidtOrthonormalBasis_apply h hi, gramSchmidtNormed, inner_smul_left,
      gramSchmidt_inv_triangular ğ•œ f hij]

theorem gramSchmidtOrthonormalBasis_inv_triangular' {i j : Î¹} (hij : i < j) :
    (gramSchmidtOrthonormalBasis h f).repr (f i) j = 0 := by
  simpa [OrthonormalBasis.repr_apply_apply] using gramSchmidtOrthonormalBasis_inv_triangular h f hij

/-- Given an indexed family `f : Î¹ â†’ E` of vectors in an inner product space `E`, for which the
size of the index set is the dimension of `E`, the matrix of coefficients of `f` with respect to the
orthonormal basis `gramSchmidtOrthonormalBasis` constructed from `f` is upper-triangular. -/
theorem gramSchmidtOrthonormalBasis_inv_blockTriangular :
    ((gramSchmidtOrthonormalBasis h f).toBasis.toMatrix f).BlockTriangular id := fun _ _ =>
  gramSchmidtOrthonormalBasis_inv_triangular' h f

theorem gramSchmidtOrthonormalBasis_det [DecidableEq Î¹] :
    (gramSchmidtOrthonormalBasis h f).toBasis.det f =
      âˆ i, âŸªgramSchmidtOrthonormalBasis h f i, f iâŸ« := by
  convert Matrix.det_of_upperTriangular (gramSchmidtOrthonormalBasis_inv_blockTriangular h f)
  exact ((gramSchmidtOrthonormalBasis h f).repr_apply_apply (f _) _).symm

end OrthonormalBasis

end InnerProductSpace
