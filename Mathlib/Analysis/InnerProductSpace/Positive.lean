/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.LinearAlgebra.Matrix.PosDef

/-!
# Positive operators

In this file we define when an operator in a Hilbert space is positive. We follow Bourbaki's choice
of requiring self adjointness in the definition.

## Main definitions

* `LinearMap.IsPositive` : a linear map is positive if it is symmetric and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`.
* `ContinuousLinearMap.IsPositive` : a continuous linear map is positive if it is symmetric and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`.

## Main statements

* `ContinuousLinearMap.IsPositive.conj_adjoint` : if `T : E â†’L[ğ•œ] E` is positive,
  then for any `S : E â†’L[ğ•œ] F`, `S âˆ˜L T âˆ˜L Sâ€ ` is also positive.
* `ContinuousLinearMap.isPositive_iff_complex` : in a ***complex*** Hilbert space,
  checking that `âŸªT x, xâŸ«` is a nonnegative real number for all `x` suffices to prove that
  `T` is positive.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

Positive operator
-/

open InnerProductSpace RCLike LinearMap ContinuousLinearMap

open scoped InnerProduct ComplexConjugate

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

namespace LinearMap

/-- A linear operator `T` on a Hilbert space is **positive** if it is symmetric and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def IsPositive (T : E â†’â‚—[ğ•œ] E) : Prop :=
  IsSymmetric T âˆ§ âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«

theorem IsPositive.isSymmetric {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) :
    IsSymmetric T := hT.1

theorem IsPositive.re_inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T)
    (x : E) : 0 â‰¤ re âŸªT x, xâŸ« :=
  hT.2 x

theorem IsPositive.re_inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T)
    (x : E) : 0 â‰¤ re âŸªx, T xâŸ« :=
  inner_re_symm (ğ•œ := ğ•œ) _ x â–¸ hT.re_inner_nonneg_left x

section Complex

variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace â„‚ E']

theorem isPositive_iff_complex (T : E' â†’â‚—[â„‚] E') :
    IsPositive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ := by
  simp_rw [IsPositive, forall_and, isSymmetric_iff_inner_map_self_real,
    conj_eq_iff_re, re_to_complex, Complex.coe_algebraMap]

end Complex

theorem IsPositive.isSelfAdjoint [FiniteDimensional ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) :
    IsSelfAdjoint T := (isSymmetric_iff_isSelfAdjoint _).mp hT.isSymmetric

theorem IsPositive.adjoint_eq [FiniteDimensional ğ•œ E] {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) :
    T.adjoint = T := hT.isSelfAdjoint

open ComplexOrder in
theorem isPositive_iff (T : E â†’â‚—[ğ•œ] E) :
    IsPositive T â†” IsSymmetric T âˆ§ âˆ€ x, 0 â‰¤ âŸªT x, xâŸ« := by
  simp_rw [IsPositive, and_congr_right_iff, â† RCLike.ofReal_nonneg (K := ğ•œ)]
  intro hT
  simp [hT]

open ComplexOrder in
theorem IsPositive.inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) : 0 â‰¤ âŸªT x, xâŸ« :=
  (T.isPositive_iff.mp hT).right x

open ComplexOrder in
theorem IsPositive.inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªx, T xâŸ« :=
  hT.isSymmetric _ _ â–¸ hT.inner_nonneg_left x

@[simp]
theorem isPositive_zero : IsPositive (0 : E â†’â‚—[ğ•œ] E) := âŸ¨.zero, by simpâŸ©

@[simp]
theorem isPositive_one : IsPositive (1 : E â†’â‚—[ğ•œ] E) := âŸ¨.id, fun _ => inner_self_nonnegâŸ©

@[simp]
theorem isPositive_id : IsPositive (id : E â†’â‚—[ğ•œ] E) := isPositive_one

@[simp]
theorem isPositive_natCast {n : â„•} : IsPositive (n : E â†’â‚—[ğ•œ] E) := by
  refine âŸ¨IsSymmetric.natCast n, fun x => ?_âŸ©
  simp only [Module.End.natCast_apply, â† Nat.cast_smul_eq_nsmul ğ•œ, inner_smul_left, map_natCast,
    mul_re, natCast_re, inner_self_im, mul_zero, sub_zero]
  exact mul_nonneg n.cast_nonneg' inner_self_nonneg

@[simp]
theorem isPositive_ofNat {n : â„•} [n.AtLeastTwo] : IsPositive (ofNat(n) : E â†’â‚—[ğ•œ] E) :=
  isPositive_natCast

@[aesop safe apply]
theorem IsPositive.add {T S : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (hS : S.IsPositive) :
    (T + S).IsPositive := by
  refine âŸ¨hT.isSymmetric.add hS.isSymmetric, fun x => ?_âŸ©
  rw [add_apply, inner_add_left, map_add]
  exact add_nonneg (hT.re_inner_nonneg_left x) (hS.re_inner_nonneg_left x)

open ComplexOrder in
@[aesop safe apply]
theorem IsPositive.smul_of_nonneg {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) {c : ğ•œ} (hc : 0 â‰¤ c) :
    (c â€¢ T).IsPositive := by
  have hc' : starRingEnd ğ•œ c = c := by
    simp [conj_eq_iff_im, â† (le_iff_re_im.mp hc).right]
  refine âŸ¨hT.left.smul hc', fun x => ?_âŸ©
  rw [smul_apply, inner_smul_left, hc', mul_re, conj_eq_iff_im.mp hc', zero_mul, sub_zero]
  exact mul_nonneg ((re_nonneg_of_nonneg hc').mpr hc) (re_inner_nonneg_left hT x)

theorem IsPositive.nonneg_eigenvalues [FiniteDimensional ğ•œ E]
    {T : E â†’â‚—[ğ•œ] E} {n : â„•} (hT : T.IsPositive)
    (hn : Module.finrank ğ•œ E = n) (i : Fin n) : 0 â‰¤ hT.isSymmetric.eigenvalues hn i := by
  simpa only [hT.isSymmetric.apply_eigenvectorBasis, inner_smul_real_left, RCLike.smul_re,
    inner_self_eq_norm_sq, OrthonormalBasis.norm_eq_one, one_pow, mul_one]
      using hT.right (hT.isSymmetric.eigenvectorBasis hn i)

section PartialOrder

/-- The (Loewner) partial order on linear maps on a Hilbert space determined by `f â‰¤ g`
if and only if `g - f` is a positive linear map (in the sense of `LinearMap.IsPositive`). -/
instance instLoewnerPartialOrder : PartialOrder (E â†’â‚—[ğ•œ] E) where
  le f g := (g - f).IsPositive
  le_refl _ := by simp
  le_trans _ _ _ hâ‚ hâ‚‚ := by simpa using hâ‚.add hâ‚‚
  le_antisymm fâ‚ fâ‚‚ hâ‚ hâ‚‚ := by
    rw [â† sub_eq_zero, â† hâ‚‚.isSymmetric.inner_map_self_eq_zero]
    intro x
    have hba2 := hâ‚.2 x
    rw [â† neg_le_neg_iff, â† map_neg, â† inner_neg_left, â† neg_apply, neg_sub, neg_zero] at hba2
    rw [â† hâ‚‚.isSymmetric.coe_re_inner_apply_self, RCLike.ofReal_eq_zero]
    exact le_antisymm hba2 (hâ‚‚.2 _)

lemma le_def (f g : E â†’â‚—[ğ•œ] E) : f â‰¤ g â†” (g - f).IsPositive := Iff.rfl

lemma nonneg_iff_isPositive (f : E â†’â‚—[ğ•œ] E) : 0 â‰¤ f â†” f.IsPositive := by
  simpa using le_def 0 f

end PartialOrder

/-- An idempotent linear map is positive iff it is symmetric. -/
theorem IsIdempotentElem.isPositive_iff_isSymmetric {T : E â†’â‚—[ğ•œ] E} (hT : IsIdempotentElem T) :
    T.IsPositive â†” T.IsSymmetric := by
  refine âŸ¨fun h => h.isSymmetric, fun h => âŸ¨h, fun x => ?_âŸ©âŸ©
  rw [â† hT.eq, Module.End.mul_apply, h]
  exact inner_self_nonneg

theorem isPositive_linearIsometryEquiv_conj_iff {T : E â†’â‚—[ğ•œ] E} (f : E â‰ƒâ‚—áµ¢[ğ•œ] F) :
    IsPositive (f.toLinearMap âˆ˜â‚— T âˆ˜â‚— f.symm.toLinearMap) â†” IsPositive T := by
  simp_rw [IsPositive, isSymmetric_linearIsometryEquiv_conj_iff, and_congr_right_iff,
    LinearIsometryEquiv.toLinearEquiv_symm, coe_comp, LinearEquiv.coe_coe,
    LinearIsometryEquiv.coe_toLinearEquiv, LinearIsometryEquiv.coe_symm_toLinearEquiv,
    Function.comp_apply, LinearIsometryEquiv.inner_map_eq_flip]
  exact fun _ => âŸ¨fun h x => by simpa using h (f x), fun h x => h _âŸ©

open scoped ComplexOrder in
/-- `A.toEuclideanLin` is positive if and only if `A` is positive semi-definite. -/
@[simp] theorem _root_.Matrix.isPositive_toEuclideanLin_iff {n : Type*} [Fintype n] [DecidableEq n]
    {A : Matrix n n ğ•œ} : A.toEuclideanLin.IsPositive â†” A.PosSemidef := by
  simp_rw [LinearMap.IsPositive, â† Matrix.isHermitian_iff_isSymmetric, inner_re_symm,
    EuclideanSpace.inner_eq_star_dotProduct, Matrix.piLp_ofLp_toEuclideanLin, Matrix.toLin'_apply,
    dotProduct_comm (A.mulVec _), Matrix.PosSemidef, and_congr_right_iff,
    RCLike.nonneg_iff (K := ğ•œ)]
  refine fun hA â†¦ (EuclideanSpace.equiv n ğ•œ).forall_congr' fun x â†¦ ?_
  simp [hA.im_star_dotProduct_mulVec_self]

open ComplexOrder in
/-- `A.toMatrix` is positive semi-definite if and only if `A` is positive. -/
@[simp] theorem posSemidef_toMatrix_iff {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]
    {A : E â†’â‚—[ğ•œ] E} (b : OrthonormalBasis Î¹ ğ•œ E) :
    (A.toMatrix b.toBasis b.toBasis).PosSemidef â†” A.IsPositive := by
  rw [â† Matrix.isPositive_toEuclideanLin_iff, (by exact Matrix.toLin'_toMatrix' _ :
    (A.toMatrix b.toBasis b.toBasis).toEuclideanLin =
      b.repr.toLinearMap âˆ˜â‚— A âˆ˜â‚— b.repr.symm.toLinearMap), isPositive_linearIsometryEquiv_conj_iff]

/-- A symmetric projection is positive. -/
@[aesop 10% apply, grind â†’]
theorem IsPositive.of_isSymmetricProjection {p : E â†’â‚—[ğ•œ] E} (hp : p.IsSymmetricProjection) :
    p.IsPositive :=
  hp.isIdempotentElem.isPositive_iff_isSymmetric.mpr hp.isSymmetric

/-- A star projection operator is positive. -/
@[deprecated (since := "19-08-2025")]
alias IsPositive.of_isStarProjection := IsPositive.of_isSymmetricProjection

end LinearMap

namespace ContinuousLinearMap

/-- A continuous linear endomorphism `T` of a Hilbert space is **positive** if it is symmetric
  and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def IsPositive (T : E â†’L[ğ•œ] E) : Prop :=
  T.IsSymmetric âˆ§ âˆ€ x, 0 â‰¤ T.reApplyInnerSelf x

theorem isPositive_def {T : E â†’L[ğ•œ] E} :
    T.IsPositive â†” T.IsSymmetric âˆ§ âˆ€ x, 0 â‰¤ T.reApplyInnerSelf x := Iff.rfl

/-- In a complete space, a continuous linear endomorphism `T` is **positive** if it is
symmetric and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
theorem isPositive_def' [CompleteSpace E] {T : E â†’L[ğ•œ] E} :
    T.IsPositive â†” IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ T.reApplyInnerSelf x := by
  simp [IsPositive, isSelfAdjoint_iff_isSymmetric, LinearMap.IsSymmetric]

theorem IsPositive.isSymmetric {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) :
    T.IsSymmetric := hT.1

theorem IsPositive.isSelfAdjoint [CompleteSpace E] {T : E â†’L[ğ•œ] E} (hT : IsPositive T) :
    IsSelfAdjoint T := hT.isSymmetric.isSelfAdjoint

theorem IsPositive.inner_left_eq_inner_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x y : E) :
    âŸªT x, yâŸ« = âŸªx, T yâŸ« := hT.isSymmetric _ _

theorem IsPositive.re_inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªT x, xâŸ« := hT.2 x

lemma _root_.LinearMap.isPositive_toContinuousLinearMap_iff
    [FiniteDimensional ğ•œ E] (T : E â†’â‚—[ğ•œ] E) :
    T.toContinuousLinearMap.IsPositive â†” T.IsPositive := Iff.rfl

lemma isPositive_toLinearMap_iff (T : E â†’L[ğ•œ] E) :
    (T : E â†’â‚—[ğ•œ] E).IsPositive â†” T.IsPositive := Iff.rfl

alias âŸ¨_, IsPositive.toLinearMapâŸ© := isPositive_toLinearMap_iff

theorem IsPositive.re_inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªx, T xâŸ« := hT.toLinearMap.re_inner_nonneg_right x

open ComplexOrder in
/-- An operator is positive iff it is symmetric and `0 â‰¤ âŸªT x, xâŸ«`.

For the version with `IsSelfAdjoint` instead of `IsSymmetric`, see
`ContinuousLinearMap.isPositive_iff'`. -/
theorem isPositive_iff (T : E â†’L[ğ•œ] E) :
    IsPositive T â†” T.IsSymmetric âˆ§ âˆ€ x, 0 â‰¤ âŸªT x, xâŸ« := LinearMap.isPositive_iff _

open ComplexOrder in
/-- An operator is positive iff it is self-adjoint and `0 â‰¤ âŸªT x, xâŸ«`.

For the version with `IsSymmetric` instead of `IsSelfAdjoint`, see
`ContinuousLinearMap.isPositive_iff`. -/
theorem isPositive_iff' [CompleteSpace E] (T : E â†’L[ğ•œ] E) :
    IsPositive T â†” IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ âŸªT x, xâŸ« := by
  simp [isSelfAdjoint_iff_isSymmetric, isPositive_iff]

open ComplexOrder in
theorem IsPositive.inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªT x, xâŸ« := hT.toLinearMap.inner_nonneg_left x

open ComplexOrder in
theorem IsPositive.inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªx, T xâŸ« := hT.toLinearMap.inner_nonneg_right x

@[simp]
theorem isPositive_zero : IsPositive (0 : E â†’L[ğ•œ] E) := LinearMap.isPositive_zero

@[simp]
theorem isPositive_id : IsPositive (.id ğ•œ E : E â†’L[ğ•œ] E) := LinearMap.isPositive_id

@[simp]
theorem isPositive_one : IsPositive (1 : E â†’L[ğ•œ] E) := LinearMap.isPositive_one

@[simp]
theorem isPositive_natCast {n : â„•} : IsPositive (n : E â†’L[ğ•œ] E) :=
  (isPositive_toLinearMap_iff _).mp LinearMap.isPositive_natCast

@[simp]
theorem isPositive_ofNat {n : â„•} [n.AtLeastTwo] : IsPositive (ofNat(n) : E â†’L[ğ•œ] E) :=
  isPositive_natCast

@[aesop safe apply]
theorem IsPositive.add {T S : E â†’L[ğ•œ] E} (hT : T.IsPositive) (hS : S.IsPositive) :
    (T + S).IsPositive :=
  (isPositive_toLinearMap_iff _).mp (hT.toLinearMap.add hS.toLinearMap)

open ComplexOrder in
@[aesop safe apply]
theorem IsPositive.smul_of_nonneg {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) {c : ğ•œ} (hc : 0 â‰¤ c) :
    (c â€¢ T).IsPositive :=
  (isPositive_toLinearMap_iff _).mp (hT.toLinearMap.smul_of_nonneg hc)

@[aesop safe apply]
theorem IsPositive.conj_adjoint [CompleteSpace E] [CompleteSpace F] {T : E â†’L[ğ•œ] E}
    (hT : T.IsPositive) (S : E â†’L[ğ•œ] F) : (S âˆ˜L T âˆ˜L Sâ€ ).IsPositive := by
  refine isPositive_def'.mpr âŸ¨hT.isSelfAdjoint.conj_adjoint S, fun x => ?_âŸ©
  rw [reApplyInnerSelf, comp_apply, â† adjoint_inner_right]
  exact hT.re_inner_nonneg_left _

theorem isPositive_self_comp_adjoint [CompleteSpace E] [CompleteSpace F] (S : E â†’L[ğ•œ] F) :
    (S âˆ˜L Sâ€ ).IsPositive := by
  simpa using isPositive_one.conj_adjoint S

@[aesop safe apply]
theorem IsPositive.adjoint_conj [CompleteSpace E] [CompleteSpace F] {T : E â†’L[ğ•œ] E}
    (hT : T.IsPositive) (S : F â†’L[ğ•œ] E) : (Sâ€  âˆ˜L T âˆ˜L S).IsPositive := by
  convert hT.conj_adjoint (Sâ€ )
  rw [adjoint_adjoint]

theorem isPositive_adjoint_comp_self [CompleteSpace E] [CompleteSpace F] (S : E â†’L[ğ•œ] F) :
    (Sâ€  âˆ˜L S).IsPositive := by
  simpa using isPositive_one.adjoint_conj S

section LinearMap
variable [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]

@[aesop safe apply]
theorem _root_.LinearMap.IsPositive.conj_adjoint {T : E â†’â‚—[ğ•œ] E}
    (hT : T.IsPositive) (S : E â†’â‚—[ğ•œ] F) : (S âˆ˜â‚— T âˆ˜â‚— S.adjoint).IsPositive := by
  have := FiniteDimensional.complete ğ•œ E
  have := FiniteDimensional.complete ğ•œ F
  simpa [â† isPositive_toContinuousLinearMap_iff] using
    ((T.isPositive_toContinuousLinearMap_iff.mpr hT).conj_adjoint S.toContinuousLinearMap)

theorem _root_.LinearMap.isPositive_self_comp_adjoint (S : E â†’â‚—[ğ•œ] F) :
    (S âˆ˜â‚— S.adjoint).IsPositive := by
  simpa using LinearMap.isPositive_one.conj_adjoint S

@[aesop safe apply]
theorem _root_.LinearMap.IsPositive.adjoint_conj {T : E â†’â‚—[ğ•œ] E}
    (hT : T.IsPositive) (S : F â†’â‚—[ğ•œ] E) : (S.adjoint âˆ˜â‚— T âˆ˜â‚— S).IsPositive := by
  convert hT.conj_adjoint S.adjoint
  rw [LinearMap.adjoint_adjoint]

theorem _root_.LinearMap.isPositive_adjoint_comp_self (S : E â†’â‚—[ğ•œ] F) :
    (S.adjoint âˆ˜â‚— S).IsPositive := by
  simpa using LinearMap.isPositive_one.adjoint_conj S

end LinearMap

theorem IsPositive.conj_starProjection (U : Submodule ğ•œ E) {T : E â†’L[ğ•œ] E} (hT : T.IsPositive)
    [U.HasOrthogonalProjection] :
    (U.starProjection âˆ˜L T âˆ˜L U.starProjection).IsPositive := by
  simp only [isPositive_iff, IsSymmetric, coe_comp, LinearMap.coe_comp, coe_coe,
    Function.comp_apply, coe_comp']
  simp_rw [â† coe_coe, U.starProjection_isSymmetric _ , hT.isSymmetric _,
    U.starProjection_isSymmetric _, â† U.starProjection_isSymmetric _, coe_coe,
    hT.inner_nonneg_right, implies_true, and_self]

theorem IsPositive.orthogonalProjection_comp {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (U : Submodule ğ•œ E)
    [U.HasOrthogonalProjection] : (U.orthogonalProjection âˆ˜L T âˆ˜L U.subtypeL).IsPositive := by
  simp only [isPositive_iff, IsSymmetric, coe_comp, LinearMap.coe_comp, coe_coe,
    Function.comp_apply, coe_comp']
  simp_rw [U.inner_orthogonalProjection_eq_of_mem_right, Submodule.subtypeL_apply,
    U.inner_orthogonalProjection_eq_of_mem_left, â† coe_coe, hT.isSymmetric _, coe_coe,
    hT.inner_nonneg_right, implies_true, and_self]

open scoped NNReal

lemma antilipschitz_of_forall_le_inner_map {H : Type*} [NormedAddCommGroup H]
    [InnerProductSpace ğ•œ H] (f : H â†’L[ğ•œ] H) {c : â„â‰¥0} (hc : 0 < c)
    (h : âˆ€ x, â€–xâ€– ^ 2 * c â‰¤ â€–âŸªf x, xâŸ«_ğ•œâ€–) : AntilipschitzWith câ»Â¹ f := by
  refine f.antilipschitz_of_bound (K := câ»Â¹) fun x â†¦ ?_
  rw [NNReal.coe_inv, inv_mul_eq_div, le_div_iffâ‚€ (by exact_mod_cast hc)]
  simp_rw [sq, mul_assoc] at h
  by_cases hx0 : x = 0
  Â· simp [hx0]
  Â· apply (map_le_map_iff <| OrderIso.mulLeftâ‚€ â€–xâ€– (norm_pos_iff.mpr hx0)).mp
    exact (h x).trans <| (norm_inner_le_norm _ _).trans <| (mul_comm _ _).le

lemma isUnit_of_forall_le_norm_inner_map [CompleteSpace E] (f : E â†’L[ğ•œ] E) {c : â„â‰¥0} (hc : 0 < c)
    (h : âˆ€ x, â€–xâ€– ^ 2 * c â‰¤ â€–âŸªf x, xâŸ«_ğ•œâ€–) : IsUnit f := by
  rw [isUnit_iff_bijective, bijective_iff_dense_range_and_antilipschitz]
  have h_anti : AntilipschitzWith câ»Â¹ f := antilipschitz_of_forall_le_inner_map f hc h
  refine âŸ¨?_, âŸ¨_, h_antiâŸ©âŸ©
  rw [Submodule.topologicalClosure_eq_top_iff, Submodule.eq_bot_iff]
  intro x hx
  have : â€–xâ€– ^ 2 * c = 0 := le_antisymm (by simpa only [hx (f x) âŸ¨x, rflâŸ©, norm_zero] using h x)
    (by positivity)
  aesop

section Complex
variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace â„‚ E']

theorem isPositive_iff_complex (T : E' â†’L[â„‚] E') :
    IsPositive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ := by
  simp [â† isPositive_toLinearMap_iff, LinearMap.isPositive_iff_complex]

end Complex

section PartialOrder

/-- The (Loewner) partial order on continuous linear maps on a Hilbert space determined by
`f â‰¤ g` if and only if `g - f` is a positive linear map (in the sense of
`ContinuousLinearMap.IsPositive`). With this partial order, the continuous linear maps form a
`StarOrderedRing`. -/
instance instLoewnerPartialOrder : PartialOrder (E â†’L[ğ•œ] E) where
  le f g := (g - f).IsPositive
  le_refl _ := by simp
  le_trans _ _ _ hâ‚ hâ‚‚ := by simpa using hâ‚.add hâ‚‚
  le_antisymm _ _ hâ‚ hâ‚‚ := coe_inj.mp (le_antisymm hâ‚.toLinearMap hâ‚‚.toLinearMap)

lemma le_def (f g : E â†’L[ğ•œ] E) : f â‰¤ g â†” (g - f).IsPositive := Iff.rfl

lemma coe_le_coe_iff (f g : E â†’L[ğ•œ] E) :
    (f : E â†’â‚—[ğ•œ] E) â‰¤ g â†” f â‰¤ g :=
  isPositive_toLinearMap_iff (g - f)

lemma nonneg_iff_isPositive (f : E â†’L[ğ•œ] E) : 0 â‰¤ f â†” f.IsPositive := by
  simpa using le_def 0 f

end PartialOrder

/-- An idempotent operator is positive if and only if it is self-adjoint. -/
@[grind â†’]
theorem IsIdempotentElem.isPositive_iff_isSelfAdjoint [CompleteSpace E]
    {p : E â†’L[ğ•œ] E} (hp : IsIdempotentElem p) : p.IsPositive â†” IsSelfAdjoint p := by
  rw [â† isPositive_toLinearMap_iff, IsIdempotentElem.isPositive_iff_isSymmetric hp.toLinearMap]
  exact isSelfAdjoint_iff_isSymmetric.symm

/-- A star projection operator is positive.

The proof of this will soon be simplified to `IsStarProjection.nonneg` when we
have `StarOrderedRing (E â†’L[ğ•œ] E)`. -/
@[aesop 10% apply, grind â†’]
theorem IsPositive.of_isStarProjection [CompleteSpace E] {p : E â†’L[ğ•œ] E}
    (hp : IsStarProjection p) : p.IsPositive :=
  hp.isIdempotentElem.isPositive_iff_isSelfAdjoint.mpr hp.isSelfAdjoint

/-- For an idempotent operator `p`, TFAE:
* `(range p)á—® = ker p`
* `p` is normal
* `p` is self-adjoint
* `p` is positive -/
theorem IsIdempotentElem.TFAE [CompleteSpace E] {p : E â†’L[ğ•œ] E} (hp : IsIdempotentElem p) :
    [(LinearMap.range p)á—® = LinearMap.ker p,
      IsStarNormal p,
      IsSelfAdjoint p,
      p.IsPositive].TFAE := by
  tfae_have 2 â†” 3 := hp.isSelfAdjoint_iff_isStarNormal.symm
  tfae_have 3 â†” 4 := hp.isPositive_iff_isSelfAdjoint.symm
  tfae_have 3 â†” 1 := p.isSelfAdjoint_iff_isSymmetric.eq â–¸
    (ContinuousLinearMap.IsIdempotentElem.isSymmetric_iff_orthogonal_range hp)
  tfae_finish

end ContinuousLinearMap
