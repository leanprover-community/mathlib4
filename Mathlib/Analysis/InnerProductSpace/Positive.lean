/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint

#align_import analysis.inner_product_space.positive from "leanprover-community/mathlib"@"caa58cbf5bfb7f81ccbaca4e8b8ac4bc2b39cc1c"

/-!
# Positive operators

In this file we define positive operators in a Hilbert space. We follow Bourbaki's choice
of requiring self adjointness in the definition.

## Main definitions

* `IsPositive` : a continuous linear map is positive if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`

## Main statements

* `ContinuousLinearMap.IsPositive.conj_adjoint` : if `T : E â†’L[ğ•œ] E` is positive,
  then for any `S : E â†’L[ğ•œ] F`, `S âˆ˜L T âˆ˜L Sâ€ ` is also positive.
* `ContinuousLinearMap.isPositive_iff_complex` : in a ***complex*** Hilbert space,
  checking that `âŸªT x, xâŸ«` is a nonnegative real number for all `x` suffices to prove that
  `T` is positive

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

Positive operator
-/


open InnerProductSpace RCLike ContinuousLinearMap

open scoped InnerProduct ComplexConjugate

namespace ContinuousLinearMap

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F]
variable [CompleteSpace E] [CompleteSpace F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

/-- A continuous linear endomorphism `T` of a Hilbert space is **positive** if it is self adjoint
  and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def IsPositive (T : E â†’L[ğ•œ] E) : Prop :=
  IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ T.reApplyInnerSelf x
#align continuous_linear_map.is_positive ContinuousLinearMap.IsPositive

theorem IsPositive.isSelfAdjoint {T : E â†’L[ğ•œ] E} (hT : IsPositive T) : IsSelfAdjoint T :=
  hT.1
#align continuous_linear_map.is_positive.is_self_adjoint ContinuousLinearMap.IsPositive.isSelfAdjoint

theorem IsPositive.inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªT x, xâŸ« :=
  hT.2 x
#align continuous_linear_map.is_positive.inner_nonneg_left ContinuousLinearMap.IsPositive.inner_nonneg_left

theorem IsPositive.inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªx, T xâŸ« := by rw [inner_re_symm]; exact hT.inner_nonneg_left x
#align continuous_linear_map.is_positive.inner_nonneg_right ContinuousLinearMap.IsPositive.inner_nonneg_right

theorem isPositive_zero : IsPositive (0 : E â†’L[ğ•œ] E) := by
  refine âŸ¨isSelfAdjoint_zero _, fun x => ?_âŸ©
  change 0 â‰¤ re âŸª_, _âŸ«
  rw [zero_apply, inner_zero_left, ZeroHomClass.map_zero]
#align continuous_linear_map.is_positive_zero ContinuousLinearMap.isPositive_zero

theorem isPositive_one : IsPositive (1 : E â†’L[ğ•œ] E) :=
  âŸ¨isSelfAdjoint_one _, fun _ => inner_self_nonnegâŸ©
#align continuous_linear_map.is_positive_one ContinuousLinearMap.isPositive_one

theorem IsPositive.add {T S : E â†’L[ğ•œ] E} (hT : T.IsPositive) (hS : S.IsPositive) :
    (T + S).IsPositive := by
  refine âŸ¨hT.isSelfAdjoint.add hS.isSelfAdjoint, fun x => ?_âŸ©
  rw [reApplyInnerSelf, add_apply, inner_add_left, map_add]
  exact add_nonneg (hT.inner_nonneg_left x) (hS.inner_nonneg_left x)
#align continuous_linear_map.is_positive.add ContinuousLinearMap.IsPositive.add

theorem IsPositive.conj_adjoint {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (S : E â†’L[ğ•œ] F) :
    (S âˆ˜L T âˆ˜L Sâ€ ).IsPositive := by
  refine âŸ¨hT.isSelfAdjoint.conj_adjoint S, fun x => ?_âŸ©
  rw [reApplyInnerSelf, comp_apply, â† adjoint_inner_right]
  exact hT.inner_nonneg_left _
#align continuous_linear_map.is_positive.conj_adjoint ContinuousLinearMap.IsPositive.conj_adjoint

theorem IsPositive.adjoint_conj {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (S : F â†’L[ğ•œ] E) :
    (Sâ€  âˆ˜L T âˆ˜L S).IsPositive := by
  convert hT.conj_adjoint (Sâ€ )
  rw [adjoint_adjoint]
#align continuous_linear_map.is_positive.adjoint_conj ContinuousLinearMap.IsPositive.adjoint_conj

theorem IsPositive.conj_orthogonalProjection (U : Submodule ğ•œ E) {T : E â†’L[ğ•œ] E} (hT : T.IsPositive)
    [CompleteSpace U] :
    (U.subtypeL âˆ˜L
        orthogonalProjection U âˆ˜L T âˆ˜L U.subtypeL âˆ˜L orthogonalProjection U).IsPositive := by
  have := hT.conj_adjoint (U.subtypeL âˆ˜L orthogonalProjection U)
  rwa [(orthogonalProjection_isSelfAdjoint U).adjoint_eq] at this
#align continuous_linear_map.is_positive.conj_orthogonal_projection ContinuousLinearMap.IsPositive.conj_orthogonalProjection

theorem IsPositive.orthogonalProjection_comp {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (U : Submodule ğ•œ E)
    [CompleteSpace U] : (orthogonalProjection U âˆ˜L T âˆ˜L U.subtypeL).IsPositive := by
  have := hT.conj_adjoint (orthogonalProjection U : E â†’L[ğ•œ] U)
  rwa [U.adjoint_orthogonalProjection] at this
#align continuous_linear_map.is_positive.orthogonal_projection_comp ContinuousLinearMap.IsPositive.orthogonalProjection_comp

section Complex

variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace â„‚ E'] [CompleteSpace E']

theorem isPositive_iff_complex (T : E' â†’L[â„‚] E') :
    IsPositive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ := by
  simp_rw [IsPositive, forall_and, isSelfAdjoint_iff_isSymmetric,
    LinearMap.isSymmetric_iff_inner_map_self_real, conj_eq_iff_re]
  rfl
#align continuous_linear_map.is_positive_iff_complex ContinuousLinearMap.isPositive_iff_complex

end Complex

section PartialOrder

/-- The (Loewner) partial order on continuous linear maps on a Hilbert space determined by
`f â‰¤ g` if and only if `g - f` is a positive linear map (in the sense of
`ContinuousLinearMap.IsPositive`). With this partial order, the continuous linear maps form a
`StarOrderedRing`. -/
instance instLoewnerPartialOrder : PartialOrder (E â†’L[ğ•œ] E) where
  le f g := (g - f).IsPositive
  le_refl _ := by simpa using isPositive_zero
  le_trans _ _ _ hâ‚ hâ‚‚ := by simpa using hâ‚.add hâ‚‚
  le_antisymm fâ‚ fâ‚‚ hâ‚ hâ‚‚ := by
    rw [â† sub_eq_zero]
    have h_isSymm := isSelfAdjoint_iff_isSymmetric.mp hâ‚‚.isSelfAdjoint
    exact_mod_cast h_isSymm.inner_map_self_eq_zero.mp fun x â†¦ by
      apply RCLike.ext
      Â· rw [map_zero]
        apply le_antisymm
        Â· rw [â† neg_nonneg, â† map_neg, â† inner_neg_left]
          simpa using hâ‚.inner_nonneg_left _
        Â· exact hâ‚‚.inner_nonneg_left _
      Â· rw [coe_sub, LinearMap.sub_apply, coe_coe, coe_coe, map_zero, â† sub_apply,
          â† h_isSymm.coe_reApplyInnerSelf_apply (T := fâ‚ - fâ‚‚) x, RCLike.ofReal_im]

lemma le_def (f g : E â†’L[ğ•œ] E) : f â‰¤ g â†” (g - f).IsPositive := Iff.rfl

lemma nonneg_iff_isPositive (f : E â†’L[ğ•œ] E) : 0 â‰¤ f â†” f.IsPositive := by
  simpa using le_def 0 f

end PartialOrder

end ContinuousLinearMap
