/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.Spectrum

/-!
# Positive operators

In this file we define when an operator in a Hilbert space is positive. We follow Bourbaki's choice
of requiring self adjointness in the definition.

## Main definitions

* `LinearMap.IsPositive` : a linear map is positive if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`.
* `ContinuousLinearMap.IsPositive` : a continuous linear map is positive if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`.

## Main statements

* `ContinuousLinearMap.IsPositive.conj_adjoint` : if `T : E â†’L[ğ•œ] E` is positive,
  then for any `S : E â†’L[ğ•œ] F`, `S âˆ˜L T âˆ˜L Sâ€ ` is also positive.
* `ContinuousLinearMap.isPositive_iff_complex` : in a ***complex*** Hilbert space,
  checking that `âŸªT x, xâŸ«` is a nonnegative real number for all `x` suffices to prove that
  `T` is positive.

## References

* [Bourbaki, *Topological Vector Spaces*][bourbaki1987]

## Tags

Positive operator
-/

open InnerProductSpace RCLike LinearMap ContinuousLinearMap

open scoped InnerProduct ComplexConjugate

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

namespace ContinuousLinearMap

variable [CompleteSpace E] [CompleteSpace F]

/-- A continuous linear endomorphism `T` of a Hilbert space is **positive** if it is self adjoint
  and `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def IsPositive (T : E â†’L[ğ•œ] E) : Prop :=
  IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ T.reApplyInnerSelf x

theorem IsPositive.isSelfAdjoint {T : E â†’L[ğ•œ] E} (hT : IsPositive T) : IsSelfAdjoint T :=
  hT.1

theorem IsPositive.inner_left_eq_inner_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    âŸªT x, xâŸ« = âŸªx, T xâŸ« := by
  rw [â† adjoint_inner_left, show adjoint T = T from hT.left]

theorem IsPositive.re_inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªT x, xâŸ« :=
  hT.2 x

theorem IsPositive.re_inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ re âŸªx, T xâŸ« := by rw [inner_re_symm]; exact hT.re_inner_nonneg_left x

open ComplexOrder in
theorem isPositive_iff (T : E â†’L[ğ•œ] E) :
    IsPositive T â†” IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ âŸªT x, xâŸ« := by
  simp_rw [IsPositive, and_congr_right_iff, â† RCLike.ofReal_nonneg (K := ğ•œ), reApplyInnerSelf_apply]
  intro hT
  have := hT.isSymmetric.coe_re_inner_apply_self
  simp_all

open ComplexOrder in
theorem IsPositive.inner_nonneg_left {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªT x, xâŸ« :=
  ((isPositive_iff T).mp hT).right x

open ComplexOrder in
theorem IsPositive.inner_nonneg_right {T : E â†’L[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªx, T xâŸ« := by
  rw [â† hT.inner_left_eq_inner_right]
  exact inner_nonneg_left hT x

@[simp]
theorem isPositive_zero : IsPositive (0 : E â†’L[ğ•œ] E) := by
  refine âŸ¨.zero _, fun x => ?_âŸ©
  change 0 â‰¤ re âŸª_, _âŸ«
  rw [zero_apply, inner_zero_left, ZeroHomClass.map_zero]

@[simp]
theorem isPositive_one : IsPositive (1 : E â†’L[ğ•œ] E) :=
  âŸ¨.one _, fun _ => inner_self_nonnegâŸ©

@[simp]
theorem isPositive_natCast {n : â„•} : IsPositive (n : E â†’L[ğ•œ] E) := by
  refine âŸ¨IsSelfAdjoint.natCast n, ?_âŸ©
  intro x
  simp [reApplyInnerSelf, â† Nat.cast_smul_eq_nsmul ğ•œ, inner_smul_left]
  exact mul_nonneg n.cast_nonneg' inner_self_nonneg

@[simp]
theorem isPositive_ofNat {n : â„•} [n.AtLeastTwo] : IsPositive (ofNat(n) : E â†’L[ğ•œ] E) :=
  isPositive_natCast

@[aesop safe apply]
theorem IsPositive.add {T S : E â†’L[ğ•œ] E} (hT : T.IsPositive) (hS : S.IsPositive) :
    (T + S).IsPositive := by
  refine âŸ¨hT.isSelfAdjoint.add hS.isSelfAdjoint, fun x => ?_âŸ©
  rw [reApplyInnerSelf, add_apply, inner_add_left, map_add]
  exact add_nonneg (hT.re_inner_nonneg_left x) (hS.re_inner_nonneg_left x)

@[aesop safe apply]
theorem IsPositive.conj_adjoint {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (S : E â†’L[ğ•œ] F) :
    (S âˆ˜L T âˆ˜L Sâ€ ).IsPositive := by
  refine âŸ¨hT.isSelfAdjoint.conj_adjoint S, fun x => ?_âŸ©
  rw [reApplyInnerSelf, comp_apply, â† adjoint_inner_right]
  exact hT.re_inner_nonneg_left _

@[aesop safe apply]
theorem IsPositive.adjoint_conj {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (S : F â†’L[ğ•œ] E) :
    (Sâ€  âˆ˜L T âˆ˜L S).IsPositive := by
  convert hT.conj_adjoint (Sâ€ )
  rw [adjoint_adjoint]

theorem IsPositive.conj_orthogonalProjection (U : Submodule ğ•œ E) {T : E â†’L[ğ•œ] E} (hT : T.IsPositive)
    [CompleteSpace U] :
    (U.subtypeL âˆ˜L
        U.orthogonalProjection âˆ˜L T âˆ˜L U.subtypeL âˆ˜L U.orthogonalProjection).IsPositive := by
  have := hT.conj_adjoint (U.subtypeL âˆ˜L U.orthogonalProjection)
  rwa [(orthogonalProjection_isSelfAdjoint U).adjoint_eq] at this

theorem IsPositive.orthogonalProjection_comp {T : E â†’L[ğ•œ] E} (hT : T.IsPositive) (U : Submodule ğ•œ E)
    [CompleteSpace U] : (U.orthogonalProjection âˆ˜L T âˆ˜L U.subtypeL).IsPositive := by
  have := hT.conj_adjoint (U.orthogonalProjection : E â†’L[ğ•œ] U)
  rwa [U.adjoint_orthogonalProjection] at this

open scoped NNReal

lemma antilipschitz_of_forall_le_inner_map {H : Type*} [NormedAddCommGroup H]
    [InnerProductSpace ğ•œ H] (f : H â†’L[ğ•œ] H) {c : â„â‰¥0} (hc : 0 < c)
    (h : âˆ€ x, â€–xâ€– ^ 2 * c â‰¤ â€–âŸªf x, xâŸ«_ğ•œâ€–) : AntilipschitzWith câ»Â¹ f := by
  refine f.antilipschitz_of_bound (K := câ»Â¹) fun x â†¦ ?_
  rw [NNReal.coe_inv, inv_mul_eq_div, le_div_iffâ‚€ (by exact_mod_cast hc)]
  simp_rw [sq, mul_assoc] at h
  by_cases hx0 : x = 0
  Â· simp [hx0]
  Â· apply (map_le_map_iff <| OrderIso.mulLeftâ‚€ â€–xâ€– (norm_pos_iff.mpr hx0)).mp
    exact (h x).trans <| (norm_inner_le_norm _ _).trans <| (mul_comm _ _).le

lemma isUnit_of_forall_le_norm_inner_map (f : E â†’L[ğ•œ] E) {c : â„â‰¥0} (hc : 0 < c)
    (h : âˆ€ x, â€–xâ€– ^ 2 * c â‰¤ â€–âŸªf x, xâŸ«_ğ•œâ€–) : IsUnit f := by
  rw [isUnit_iff_bijective, bijective_iff_dense_range_and_antilipschitz]
  have h_anti : AntilipschitzWith câ»Â¹ f := antilipschitz_of_forall_le_inner_map f hc h
  refine âŸ¨?_, âŸ¨_, h_antiâŸ©âŸ©
  have _inst := h_anti.completeSpace_range_clm
  rw [Submodule.topologicalClosure_eq_top_iff, Submodule.eq_bot_iff]
  intro x hx
  have : â€–xâ€– ^ 2 * c = 0 := le_antisymm (by simpa only [hx (f x) âŸ¨x, rflâŸ©, norm_zero] using h x)
    (by positivity)
  aesop

section Complex

variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace â„‚ E'] [CompleteSpace E']

theorem isPositive_iff_complex (T : E' â†’L[â„‚] E') :
    IsPositive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ := by
  simp_rw [IsPositive, forall_and, isSelfAdjoint_iff_isSymmetric,
    LinearMap.isSymmetric_iff_inner_map_self_real, conj_eq_iff_re]
  rfl

end Complex

section PartialOrder

/-- The (Loewner) partial order on continuous linear maps on a Hilbert space determined by
`f â‰¤ g` if and only if `g - f` is a positive linear map (in the sense of
`ContinuousLinearMap.IsPositive`). With this partial order, the continuous linear maps form a
`StarOrderedRing`. -/
instance instLoewnerPartialOrder : PartialOrder (E â†’L[ğ•œ] E) where
  le f g := (g - f).IsPositive
  le_refl _ := by simp
  le_trans _ _ _ hâ‚ hâ‚‚ := by simpa using hâ‚.add hâ‚‚
  le_antisymm fâ‚ fâ‚‚ hâ‚ hâ‚‚ := by
    rw [â† sub_eq_zero]
    have h_isSymm := isSelfAdjoint_iff_isSymmetric.mp <| IsPositive.isSelfAdjoint hâ‚‚
    exact_mod_cast h_isSymm.inner_map_self_eq_zero.mp fun x â†¦ by
      open scoped ComplexOrder in
      refine le_antisymm ?_ (hâ‚‚.inner_nonneg_left x)
      rw [â† neg_nonneg, â† inner_neg_left]
      simpa using hâ‚.inner_nonneg_left x

lemma le_def (f g : E â†’L[ğ•œ] E) : f â‰¤ g â†” (g - f).IsPositive := Iff.rfl

lemma nonneg_iff_isPositive (f : E â†’L[ğ•œ] E) : 0 â‰¤ f â†” f.IsPositive := by
  simpa using le_def 0 f

end PartialOrder

end ContinuousLinearMap

namespace LinearMap

variable [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]

/-- A linear map `T` of a Hilbert space is **positive** if it is self adjoint and
  `âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«`. -/
def IsPositive (T : E â†’â‚—[ğ•œ] E) : Prop :=
  IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ re âŸªT x, xâŸ«

theorem IsPositive.isSelfAdjoint {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) :
    IsSelfAdjoint T := hT.1

theorem IsPositive.re_inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T)
    (x : E) : 0 â‰¤ re âŸªT x, xâŸ« :=
  hT.2 x

theorem IsPositive.re_inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T)
    (x : E) : 0 â‰¤ re âŸªx, T xâŸ« := by
  rw [inner_re_symm]
  exact hT.re_inner_nonneg_left x

lemma isPositive_toContinuousLinearMap_iff [CompleteSpace E] (T : E â†’â‚—[ğ•œ] E) :
    T.toContinuousLinearMap.IsPositive â†” T.IsPositive := by
  apply Iff.intro
  Â· intro hT
    apply And.intro
    Â· exact (isSelfAdjoint_toContinuousLinearMap_iff T).mp hT.left
    Â· intro x
      have hx : 0 â‰¤ re âŸªT x, xâŸ« := hT.right x
      exact hx
  Â· intro hT
    apply And.intro
    Â· exact (isSelfAdjoint_toContinuousLinearMap_iff T).mpr hT.left
    Â· intro x
      simp [ContinuousLinearMap.reApplyInnerSelf]
      exact hT.right x

lemma _root_.ContinuousLinearMap.isPositive_toLinearMap_iff [CompleteSpace E] (T : E â†’L[ğ•œ] E) :
    (T : E â†’â‚—[ğ•œ] E).IsPositive â†” T.IsPositive := by
  apply Iff.intro
  Â· intro hT
    apply And.intro
    Â· exact (isSelfAdjoint_toLinearMap_iff T).mp hT.left
    Â· intro x
      have hx : 0 â‰¤ re âŸªT x, xâŸ« := hT.right x
      exact hx
  Â· intro hT
    apply And.intro
    Â· exact (isSelfAdjoint_toLinearMap_iff T).mpr hT.left
    Â· intro x
      have hx : 0 â‰¤ re âŸªT x, xâŸ« := hT.right x
      simp [hx]

section Complex

variable {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace â„‚ E'] [FiniteDimensional â„‚ E']

theorem isPositive_iff_complex (T : E' â†’â‚—[â„‚] E') :
    IsPositive T â†” âˆ€ x, (re âŸªT x, xâŸ«_â„‚ : â„‚) = âŸªT x, xâŸ«_â„‚ âˆ§ 0 â‰¤ re âŸªT x, xâŸ«_â„‚ := by
  simp_rw [IsPositive, forall_and, â† isSymmetric_iff_isSelfAdjoint,
    LinearMap.isSymmetric_iff_inner_map_self_real, conj_eq_iff_re]
  rfl

end Complex

theorem IsPositive.isSymmetric {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) :
    IsSymmetric T := (isSymmetric_iff_isSelfAdjoint T).mpr hT.isSelfAdjoint

open ComplexOrder in
theorem isPositive_iff (T : E â†’â‚—[ğ•œ] E) :
    IsPositive T â†” IsSelfAdjoint T âˆ§ âˆ€ x, 0 â‰¤ âŸªT x, xâŸ« := by
  simp_rw [IsPositive, and_congr_right_iff, â† RCLike.ofReal_nonneg (K := ğ•œ)]
  intro hT
  simp [isSymmetric_iff_isSelfAdjoint _ |>.mpr hT]

open ComplexOrder in
theorem IsPositive.inner_nonneg_left {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) : 0 â‰¤ âŸªT x, xâŸ« :=
  ((isPositive_iff T).mp hT).right x

open ComplexOrder in
theorem IsPositive.inner_nonneg_right {T : E â†’â‚—[ğ•œ] E} (hT : IsPositive T) (x : E) :
    0 â‰¤ âŸªx, T xâŸ« := by
  rw [â† hT.isSymmetric]
  exact hT.inner_nonneg_left x

@[simp]
theorem isPositive_zero : IsPositive (0 : E â†’â‚—[ğ•œ] E) := âŸ¨.zero _, by simpâŸ©

@[simp]
theorem isPositive_one : IsPositive (1 : E â†’â‚—[ğ•œ] E) := âŸ¨.one _, fun _ => inner_self_nonnegâŸ©

@[simp]
theorem isPositive_natCast {n : â„•} : IsPositive (n : E â†’â‚—[ğ•œ] E) := by
  refine âŸ¨IsSelfAdjoint.natCast n, ?_âŸ©
  intro x
  simp only [Module.End.natCast_apply, â† Nat.cast_smul_eq_nsmul ğ•œ, inner_smul_left, map_natCast,
    mul_re, natCast_re, inner_self_im, mul_zero, sub_zero]
  exact mul_nonneg n.cast_nonneg' inner_self_nonneg

@[simp]
theorem isPositive_ofNat {n : â„•} [n.AtLeastTwo] : IsPositive (ofNat(n) : E â†’â‚—[ğ•œ] E) :=
  isPositive_natCast

@[aesop safe apply]
theorem IsPositive.add {T S : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (hS : S.IsPositive) :
    (T + S).IsPositive := by
  refine âŸ¨hT.isSelfAdjoint.add hS.isSelfAdjoint, fun x => ?_âŸ©
  rw [add_apply, inner_add_left, map_add]
  exact add_nonneg (hT.re_inner_nonneg_left x) (hS.re_inner_nonneg_left x)

@[aesop safe apply]
theorem IsPositive.conj_adjoint {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (S : E â†’â‚—[ğ•œ] F) :
    (S âˆ˜â‚— T âˆ˜â‚— S.adjoint).IsPositive := by
  refine And.intro ?_ ?_
  Â· rw [isSelfAdjoint_iff', adjoint_comp, adjoint_comp, adjoint_adjoint, â† star_eq_adjoint, hT.1]
    rfl
  Â· intro x
    rw [comp_apply, â† adjoint_inner_right]
    exact hT.re_inner_nonneg_left _

@[aesop safe apply]
theorem IsPositive.adjoint_conj {T : E â†’â‚—[ğ•œ] E} (hT : T.IsPositive) (S : F â†’â‚—[ğ•œ] E) :
    (S.adjoint âˆ˜â‚— T âˆ˜â‚— S).IsPositive := by
  convert hT.conj_adjoint S.adjoint
  rw [adjoint_adjoint]

theorem IsPositive.nonneg_eigenvalues {T : E â†’â‚—[ğ•œ] E} {n : â„•} (hT : T.IsPositive)
    (hn : Module.finrank ğ•œ E = n) (i : Fin n) : 0 â‰¤ hT.isSymmetric.eigenvalues hn i := by
  have h := hT.right (hT.isSymmetric.eigenvectorBasis hn i)
  rw [hT.isSymmetric.apply_eigenvectorBasis, inner_smul_real_left, RCLike.smul_re,
    inner_self_eq_norm_sq, OrthonormalBasis.norm_eq_one, one_pow, mul_one] at h
  exact h

section PartialOrder

/-- The (Loewner) partial order on linear maps on a Hilbert space determined by `f â‰¤ g`
if and only if `g - f` is a positive linear map (in the sense of `LinearMap.IsPositive`). -/
instance instLoewnerPartialOrder : PartialOrder (E â†’â‚—[ğ•œ] E) where
  le f g := (g - f).IsPositive
  le_refl _ := by simp
  le_trans _ _ _ hâ‚ hâ‚‚ := by simpa using hâ‚.add hâ‚‚
  le_antisymm fâ‚ fâ‚‚ hâ‚ hâ‚‚ := by
    rw [â† sub_eq_zero]
    have h_isSymm := (isSymmetric_iff_isSelfAdjoint (fâ‚ - fâ‚‚)).mpr hâ‚‚.isSelfAdjoint
    exact h_isSymm.inner_map_self_eq_zero.mp fun x â†¦ by
      open scoped ComplexOrder in
      refine le_antisymm ?_ (hâ‚‚.inner_nonneg_left x)
      rw [â† neg_nonneg, â† inner_neg_left]
      simpa using hâ‚.inner_nonneg_left x

lemma le_def (f g : E â†’â‚—[ğ•œ] E) : f â‰¤ g â†” (g - f).IsPositive := Iff.rfl

lemma nonneg_iff_isPositive (f : E â†’â‚—[ğ•œ] E) : 0 â‰¤ f â†” f.IsPositive := by
  simpa using le_def 0 f

end PartialOrder

end LinearMap
