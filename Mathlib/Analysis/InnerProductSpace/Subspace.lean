/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.Orthonormal

/-!
# Subspaces of inner product spaces

This file defines the inner-product structure on a subspace of an inner-product space, and proves
some theorems about orthogonal families of subspaces.
-/

noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp Module

open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

section Submodule

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-! ### Inner product space structure on subspaces -/

/-- Induced inner product on a submodule. -/
instance Submodule.innerProductSpace (W : Submodule ğ•œ E) : InnerProductSpace ğ•œ W :=
  { Submodule.normedSpace W with
    inner := fun x y => âŸª(x : E), (y : E)âŸ«
    conj_inner_symm := fun _ _ => inner_conj_symm _ _
    norm_sq_eq_re_inner := fun x => norm_sq_eq_re_inner (x : E)
    add_left := fun _ _ _ => inner_add_left _ _ _
    smul_left := fun _ _ _ => inner_smul_left _ _ _ }

/-- The inner product on submodules is the same as on the ambient space. -/
@[simp]
theorem Submodule.coe_inner (W : Submodule ğ•œ E) (x y : W) : âŸªx, yâŸ« = âŸª(x : E), â†‘yâŸ« :=
  rfl

theorem Orthonormal.codRestrict {Î¹ : Type*} {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (s : Submodule ğ•œ E)
    (hvs : âˆ€ i, v i âˆˆ s) : @Orthonormal ğ•œ s _ _ _ Î¹ (Set.codRestrict v s hvs) :=
  s.subtypeâ‚—áµ¢.orthonormal_comp_iff.mp hv

theorem orthonormal_span {Î¹ : Type*} {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) :
    @Orthonormal ğ•œ (Submodule.span ğ•œ (Set.range v)) _ _ _ Î¹ fun i : Î¹ =>
      âŸ¨v i, Submodule.subset_span (Set.mem_range_self i)âŸ© :=
  hv.codRestrict (Submodule.span ğ•œ (Set.range v)) fun i =>
    Submodule.subset_span (Set.mem_range_self i)

end Submodule

/-! ### Families of mutually-orthogonal subspaces of an inner product space -/

section OrthogonalFamily_Seminormed

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable {Î¹ : Type*} (ğ•œ)

open DirectSum

/-- An indexed family of mutually-orthogonal subspaces of an inner product space `E`.

The simple way to express this concept would be as a condition on `V : Î¹ â†’ Submodule ğ•œ E`.  We
instead implement it as a condition on a family of inner product spaces each equipped with an
isometric embedding into `E`, thus making it a property of morphisms rather than subobjects.
The connection to the subobject spelling is shown in `orthogonalFamily_iff_pairwise`.

This definition is less lightweight, but allows for better definitional properties when the inner
product space structure on each of the submodules is important -- for example, when considering
their Hilbert sum (`PiLp V 2`).  For example, given an orthonormal set of vectors `v : Î¹ â†’ E`,
we have an associated orthogonal family of one-dimensional subspaces of `E`, which it is convenient
to be able to discuss using `Î¹ â†’ ğ•œ` rather than `Î  i : Î¹, span ğ•œ (v i)`. -/
def OrthogonalFamily (G : Î¹ â†’ Type*) [âˆ€ i, SeminormedAddCommGroup (G i)]
    [âˆ€ i, InnerProductSpace ğ•œ (G i)] (V : âˆ€ i, G i â†’â‚—áµ¢[ğ•œ] E) : Prop :=
  Pairwise fun i j => âˆ€ v : G i, âˆ€ w : G j, âŸªV i v, V j wâŸ« = 0

variable {ğ•œ}
variable {G : Î¹ â†’ Type*} [âˆ€ i, NormedAddCommGroup (G i)] [âˆ€ i, InnerProductSpace ğ•œ (G i)]
  {V : âˆ€ i, G i â†’â‚—áµ¢[ğ•œ] E}

theorem Orthonormal.orthogonalFamily {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) :
    OrthogonalFamily ğ•œ (fun _i : Î¹ => ğ•œ) fun i => LinearIsometry.toSpanSingleton ğ•œ E (hv.1 i) :=
  fun i j hij a b => by simp [inner_smul_left, inner_smul_right, hv.2 hij]

section
variable (hV : OrthogonalFamily ğ•œ G V)
include hV

theorem OrthogonalFamily.eq_ite [DecidableEq Î¹] {i j : Î¹} (v : G i) (w : G j) :
    âŸªV i v, V j wâŸ« = ite (i = j) âŸªV i v, V j wâŸ« 0 := by
  split_ifs with h
  Â· rfl
  Â· exact hV h v w

theorem OrthogonalFamily.inner_right_dfinsupp
    [âˆ€ (i) (x : G i), Decidable (x â‰  0)] [DecidableEq Î¹] (l : â¨ i, G i) (i : Î¹) (v : G i) :
    âŸªV i v, l.sum fun j => V jâŸ« = âŸªv, l iâŸ« :=
  calc
    âŸªV i v, l.sum fun j => V jâŸ« = l.sum fun j => fun w => âŸªV i v, V j wâŸ« :=
      DFinsupp.inner_sum (fun j => V j) l (V i v)
    _ = l.sum fun j => fun w => ite (i = j) âŸªV i v, V j wâŸ« 0 :=
      (congr_arg l.sum <| funext fun _ => funext <| hV.eq_ite v)
    _ = âŸªv, l iâŸ« := by
      simp only [DFinsupp.sum, Finset.sum_ite_eq,
        DFinsupp.mem_support_toFun]
      split_ifs with h
      Â· simp only [LinearIsometry.inner_map_map]
      Â· simp only [of_not_not h, inner_zero_right]

theorem OrthogonalFamily.inner_right_fintype [Fintype Î¹] (l : âˆ€ i, G i) (i : Î¹) (v : G i) :
    âŸªV i v, âˆ‘ j : Î¹, V j (l j)âŸ« = âŸªv, l iâŸ« := by
  classical
  calc
    âŸªV i v, âˆ‘ j : Î¹, V j (l j)âŸ« = âˆ‘ j : Î¹, âŸªV i v, V j (l j)âŸ« := by rw [inner_sum]
    _ = âˆ‘ j, ite (i = j) âŸªV i v, V j (l j)âŸ« 0 :=
      (congr_arg (Finset.sum Finset.univ) <| funext fun j => hV.eq_ite v (l j))
    _ = âŸªv, l iâŸ« := by
      simp only [Finset.sum_ite_eq, Finset.mem_univ, (V i).inner_map_map, if_true]

nonrec theorem OrthogonalFamily.inner_sum (lâ‚ lâ‚‚ : âˆ€ i, G i) (s : Finset Î¹) :
    âŸªâˆ‘ i âˆˆ s, V i (lâ‚ i), âˆ‘ j âˆˆ s, V j (lâ‚‚ j)âŸ« = âˆ‘ i âˆˆ s, âŸªlâ‚ i, lâ‚‚ iâŸ« := by
  classical
  calc
    âŸªâˆ‘ i âˆˆ s, V i (lâ‚ i), âˆ‘ j âˆˆ s, V j (lâ‚‚ j)âŸ« = âˆ‘ j âˆˆ s, âˆ‘ i âˆˆ s, âŸªV i (lâ‚ i), V j (lâ‚‚ j)âŸ« := by
      simp only [sum_inner, inner_sum]
    _ = âˆ‘ j âˆˆ s, âˆ‘ i âˆˆ s, ite (i = j) âŸªV i (lâ‚ i), V j (lâ‚‚ j)âŸ« 0 := by
      congr with i
      congr with j
      apply hV.eq_ite
    _ = âˆ‘ i âˆˆ s, âŸªlâ‚ i, lâ‚‚ iâŸ« := by
      simp only [Finset.sum_ite_of_true, Finset.sum_ite_eq', LinearIsometry.inner_map_map,
        imp_self, imp_true_iff]

theorem OrthogonalFamily.norm_sum (l : âˆ€ i, G i) (s : Finset Î¹) :
    â€–âˆ‘ i âˆˆ s, V i (l i)â€– ^ 2 = âˆ‘ i âˆˆ s, â€–l iâ€– ^ 2 := by
  have : ((â€–âˆ‘ i âˆˆ s, V i (l i)â€– : â„) : ğ•œ) ^ 2 = âˆ‘ i âˆˆ s, ((â€–l iâ€– : â„) : ğ•œ) ^ 2 := by
    simp only [â† inner_self_eq_norm_sq_to_K, hV.inner_sum]
  exact mod_cast this

/-- The composition of an orthogonal family of subspaces with an injective function is also an
orthogonal family. -/
theorem OrthogonalFamily.comp {Î³ : Type*} {f : Î³ â†’ Î¹} (hf : Function.Injective f) :
    OrthogonalFamily ğ•œ (fun g => G (f g)) fun g => V (f g) :=
  fun _i _j hij v w => hV (hf.ne hij) v w

theorem OrthogonalFamily.orthonormal_sigma_orthonormal {Î± : Î¹ â†’ Type*} {v_family : âˆ€ i, Î± i â†’ G i}
    (hv_family : âˆ€ i, Orthonormal ğ•œ (v_family i)) :
    Orthonormal ğ•œ fun a : Î£ i, Î± i => V a.1 (v_family a.1 a.2) := by
  constructor
  Â· rintro âŸ¨i, vâŸ©
    simpa only [LinearIsometry.norm_map] using (hv_family i).left v
  rintro âŸ¨i, vâŸ© âŸ¨j, wâŸ© hvw
  by_cases hij : i = j
  Â· subst hij
    have : v â‰  w := fun h => by
      subst h
      exact hvw rfl
    simpa only [LinearIsometry.inner_map_map] using (hv_family i).2 this
  Â· exact hV hij (v_family i v) (v_family j w)

theorem OrthogonalFamily.norm_sq_diff_sum [DecidableEq Î¹] (f : âˆ€ i, G i) (sâ‚ sâ‚‚ : Finset Î¹) :
    â€–(âˆ‘ i âˆˆ sâ‚, V i (f i)) - âˆ‘ i âˆˆ sâ‚‚, V i (f i)â€– ^ 2 =
      (âˆ‘ i âˆˆ sâ‚ \ sâ‚‚, â€–f iâ€– ^ 2) + âˆ‘ i âˆˆ sâ‚‚ \ sâ‚, â€–f iâ€– ^ 2 := by
  rw [â† Finset.sum_sdiff_sub_sum_sdiff, sub_eq_add_neg, â† Finset.sum_neg_distrib]
  let F : âˆ€ i, G i := fun i => if i âˆˆ sâ‚ then f i else -f i
  have hFâ‚ : âˆ€ i âˆˆ sâ‚ \ sâ‚‚, F i = f i := fun i hi => if_pos (Finset.sdiff_subset hi)
  have hFâ‚‚ : âˆ€ i âˆˆ sâ‚‚ \ sâ‚, F i = -f i := fun i hi => if_neg (Finset.mem_sdiff.mp hi).2
  have hF : âˆ€ i, â€–F iâ€– = â€–f iâ€– := by
    intro i
    dsimp only [F]
    split_ifs <;> simp only [norm_neg]
  have :
    â€–(âˆ‘ i âˆˆ sâ‚ \ sâ‚‚, V i (F i)) + âˆ‘ i âˆˆ sâ‚‚ \ sâ‚, V i (F i)â€– ^ 2 =
      (âˆ‘ i âˆˆ sâ‚ \ sâ‚‚, â€–F iâ€– ^ 2) + âˆ‘ i âˆˆ sâ‚‚ \ sâ‚, â€–F iâ€– ^ 2 := by
    have hs : Disjoint (sâ‚ \ sâ‚‚) (sâ‚‚ \ sâ‚) := disjoint_sdiff_sdiff
    simpa only [Finset.sum_union hs] using hV.norm_sum F (sâ‚ \ sâ‚‚ âˆª sâ‚‚ \ sâ‚)
  convert this using 4
  Â· refine Finset.sum_congr rfl fun i hi => ?_
    simp only [hFâ‚ i hi]
  Â· refine Finset.sum_congr rfl fun i hi => ?_
    simp only [hFâ‚‚ i hi, LinearIsometry.map_neg]
  Â· simp only [hF]
  Â· simp only [hF]

/-- A family `f` of mutually-orthogonal elements of `E` is summable, if and only if
`(fun i â†¦ â€–f iâ€– ^ 2)` is summable. -/
theorem OrthogonalFamily.summable_iff_norm_sq_summable [CompleteSpace E] (f : âˆ€ i, G i) :
    (Summable fun i => V i (f i)) â†” Summable fun i => â€–f iâ€– ^ 2 := by
  classical
    simp only [summable_iff_cauchySeq_finset, NormedAddCommGroup.cauchySeq_iff, Real.norm_eq_abs]
    constructor
    Â· intro hf Îµ hÎµ
      obtain âŸ¨a, HâŸ© := hf _ (sqrt_pos.mpr hÎµ)
      use a
      intro sâ‚ hsâ‚ sâ‚‚ hsâ‚‚
      rw [â† Finset.sum_sdiff_sub_sum_sdiff]
      refine (abs_sub _ _).trans_lt ?_
      have : âˆ€ i, 0 â‰¤ â€–f iâ€– ^ 2 := fun i : Î¹ => sq_nonneg _
      simp only [Finset.abs_sum_of_nonneg' this]
      have : ((âˆ‘ i âˆˆ sâ‚ \ sâ‚‚, â€–f iâ€– ^ 2) + âˆ‘ i âˆˆ sâ‚‚ \ sâ‚, â€–f iâ€– ^ 2) < âˆšÎµ ^ 2 := by
        rw [â† hV.norm_sq_diff_sum, sq_lt_sq, abs_of_nonneg (sqrt_nonneg _),
          abs_of_nonneg (norm_nonneg _)]
        exact H sâ‚ hsâ‚ sâ‚‚ hsâ‚‚
      have hÎ· := sq_sqrt (le_of_lt hÎµ)
      linarith
    Â· intro hf Îµ hÎµ
      have hÎµ' : 0 < Îµ ^ 2 / 2 := half_pos (sq_pos_of_pos hÎµ)
      obtain âŸ¨a, HâŸ© := hf _ hÎµ'
      use a
      intro sâ‚ hsâ‚ sâ‚‚ hsâ‚‚
      refine (abs_lt_of_sq_lt_sq' ?_ (le_of_lt hÎµ)).2
      have has : a â‰¤ sâ‚ âŠ“ sâ‚‚ := le_inf hsâ‚ hsâ‚‚
      rw [hV.norm_sq_diff_sum]
      have Hsâ‚ : âˆ‘ x âˆˆ sâ‚ \ sâ‚‚, â€–f xâ€– ^ 2 < Îµ ^ 2 / 2 := by
        convert H _ hsâ‚ _ has
        have : sâ‚ âŠ“ sâ‚‚ âŠ† sâ‚ := Finset.inter_subset_left
        rw [â† Finset.sum_sdiff this, add_tsub_cancel_right, Finset.abs_sum_of_nonneg']
        Â· simp
        Â· exact fun i => sq_nonneg _
      have Hsâ‚‚ : âˆ‘ x âˆˆ sâ‚‚ \ sâ‚, â€–f xâ€– ^ 2 < Îµ ^ 2 / 2 := by
        convert H _ hsâ‚‚ _ has
        have : sâ‚ âŠ“ sâ‚‚ âŠ† sâ‚‚ := Finset.inter_subset_right
        rw [â† Finset.sum_sdiff this, add_tsub_cancel_right, Finset.abs_sum_of_nonneg']
        Â· simp
        Â· exact fun i => sq_nonneg _
      linarith

end

end OrthogonalFamily_Seminormed

section OrthogonalFamily

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable {Î¹ : Type*} {G : Î¹ â†’ Type*}

/-- An orthogonal family forms an independent family of subspaces; that is, any collection of
elements each from a different subspace in the family is linearly independent. In particular, the
pairwise intersections of elements of the family are 0. -/
theorem OrthogonalFamily.independent {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    iSupIndep V := by
  classical
  apply iSupIndep_of_dfinsupp_lsum_injective
  refine LinearMap.ker_eq_bot.mp ?_
  rw [Submodule.eq_bot_iff]
  intro v hv
  rw [LinearMap.mem_ker] at hv
  ext i
  suffices âŸª(v i : E), v iâŸ« = 0 by simpa only [inner_self_eq_zero] using this
  calc
    âŸª(v i : E), v iâŸ« = âŸª(v i : E), DFinsupp.lsum â„• (fun i => (V i).subtype) vâŸ« := by
      simpa only [DFinsupp.sumAddHom_apply, DFinsupp.lsum_apply_apply] using
        (hV.inner_right_dfinsupp v i (v i)).symm
    _ = 0 := by simp only [hv, inner_zero_right]

theorem DirectSum.IsInternal.collectedBasis_orthonormal [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢)
    (hV_sum : DirectSum.IsInternal fun i => V i) {Î± : Î¹ â†’ Type*}
    {v_family : âˆ€ i, Basis (Î± i) ğ•œ (V i)} (hv_family : âˆ€ i, Orthonormal ğ•œ (v_family i)) :
    Orthonormal ğ•œ (hV_sum.collectedBasis v_family) := by
  simpa only [hV_sum.collectedBasis_coe] using hV.orthonormal_sigma_orthonormal hv_family

end OrthogonalFamily
