/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Analysis.InnerProductSpace.Calculus
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.Calculus.LagrangeMultipliers
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.Algebra.EuclideanDomain.Basic

/-!
# The Rayleigh quotient

The Rayleigh quotient of a self-adjoint operator `T` on an inner product space `E` is the function
`fun x â†¦ âŸªT x, xâŸ« / â€–xâ€– ^ 2`.

The main results of this file are `IsSelfAdjoint.hasEigenvector_of_isMaxOn` and
`IsSelfAdjoint.hasEigenvector_of_isMinOn`, which state that if `E` is complete, and if the
Rayleigh quotient attains its global maximum/minimum over some sphere at the point `xâ‚€`, then `xâ‚€`
is an eigenvector of `T`, and the `iSup`/`iInf` of `fun x â†¦ âŸªT x, xâŸ« / â€–xâ€– ^ 2` is the corresponding
eigenvalue.

The corollaries `LinearMap.IsSymmetric.hasEigenvalue_iSup_of_finiteDimensional` and
`LinearMap.IsSymmetric.hasEigenvalue_iSup_of_finiteDimensional` state that if `E` is
finite-dimensional and nontrivial, then `T` has some (nonzero) eigenvectors with eigenvalue the
`iSup`/`iInf` of `fun x â†¦ âŸªT x, xâŸ« / â€–xâ€– ^ 2`.

## TODO

A slightly more elaborate corollary is that if `E` is complete and `T` is a compact operator, then
`T` has some (nonzero) eigenvector with eigenvalue either `â¨† x, âŸªT x, xâŸ« / â€–xâ€– ^ 2` or
`â¨… x, âŸªT x, xâŸ« / â€–xâ€– ^ 2` (not necessarily both).

-/


variable {ğ•œ : Type*} [RCLike ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

open scoped NNReal

open Module.End Metric

namespace ContinuousLinearMap

variable (T : E â†’L[ğ•œ] E)

/-- The *Rayleigh quotient* of a continuous linear map `T` (over `â„` or `â„‚`) at a vector `x` is
the quantity `re âŸªT x, xâŸ« / â€–xâ€– ^ 2`. -/
noncomputable abbrev rayleighQuotient (x : E) := T.reApplyInnerSelf x / â€–(x : E)â€– ^ 2

theorem rayleigh_smul (x : E) {c : ğ•œ} (hc : c â‰  0) :
    rayleighQuotient T (c â€¢ x) = rayleighQuotient T x := by
  by_cases hx : x = 0
  Â· simp [hx]
  simp [field, norm_smul, T.reApplyInnerSelf_smul]

theorem image_rayleigh_eq_image_rayleigh_sphere {r : â„} (hr : 0 < r) :
    rayleighQuotient T '' {0}á¶œ = rayleighQuotient T '' sphere 0 r := by
  ext a
  constructor
  Â· rintro âŸ¨x, hx : x â‰  0, hxTâŸ©
    have : â€–xâ€– â‰  0 := by simp [hx]
    let c : ğ•œ := â†‘â€–xâ€–â»Â¹ * r
    have : c â‰  0 := by simp [c, hx, hr.ne']
    refine âŸ¨c â€¢ x, ?_, ?_âŸ©
    Â· simp [field, c, norm_smul, abs_of_pos hr]
    Â· rw [T.rayleigh_smul x this]
      exact hxT
  Â· rintro âŸ¨x, hx, hxTâŸ©
    exact âŸ¨x, ne_zero_of_mem_sphere hr.ne' âŸ¨x, hxâŸ©, hxTâŸ©

theorem iSup_rayleigh_eq_iSup_rayleigh_sphere {r : â„} (hr : 0 < r) :
    â¨† x : { x : E // x â‰  0 }, rayleighQuotient T x =
      â¨† x : sphere (0 : E) r, rayleighQuotient T x :=
  show â¨† x : ({0}á¶œ : Set E), rayleighQuotient T x = _ by
    simp only [â† @sSup_image' _ _ _ _ (rayleighQuotient T),
      T.image_rayleigh_eq_image_rayleigh_sphere hr]

theorem iInf_rayleigh_eq_iInf_rayleigh_sphere {r : â„} (hr : 0 < r) :
    â¨… x : { x : E // x â‰  0 }, rayleighQuotient T x =
      â¨… x : sphere (0 : E) r, rayleighQuotient T x :=
  show â¨… x : ({0}á¶œ : Set E), rayleighQuotient T x = _ by
    simp only [â† @sInf_image' _ _ _ _ (rayleighQuotient T),
      T.image_rayleigh_eq_image_rayleigh_sphere hr]

end ContinuousLinearMap

namespace IsSelfAdjoint

section Real

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace â„ F]

theorem _root_.LinearMap.IsSymmetric.hasStrictFDerivAt_reApplyInnerSelf {T : F â†’L[â„] F}
    (hT : (T : F â†’â‚—[â„] F).IsSymmetric) (xâ‚€ : F) :
    HasStrictFDerivAt T.reApplyInnerSelf (2 â€¢ (innerSL â„ (T xâ‚€))) xâ‚€ := by
  convert T.hasStrictFDerivAt.inner â„ (hasStrictFDerivAt_id xâ‚€) using 1
  ext y
  rw [ContinuousLinearMap.smul_apply, ContinuousLinearMap.comp_apply, fderivInnerCLM_apply,
    ContinuousLinearMap.prod_apply, innerSL_apply, id, ContinuousLinearMap.id_apply,
    hT.apply_clm xâ‚€ y, real_inner_comm _ xâ‚€, two_smul]

variable [CompleteSpace F] {T : F â†’L[â„] F}

theorem linearly_dependent_of_isLocalExtrOn (hT : IsSelfAdjoint T) {xâ‚€ : F}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : F) â€–xâ‚€â€–) xâ‚€) :
    âˆƒ a b : â„, (a, b) â‰  0 âˆ§ a â€¢ xâ‚€ + b â€¢ T xâ‚€ = 0 := by
  have H : IsLocalExtrOn T.reApplyInnerSelf {x : F | â€–xâ€– ^ 2 = â€–xâ‚€â€– ^ 2} xâ‚€ := by
    convert hextr
    ext x
    simp
  -- find Lagrange multipliers for the function `T.re_apply_inner_self` and the
  -- hypersurface-defining function `fun x â†¦ â€–xâ€– ^ 2`
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© :=
    IsLocalExtrOn.exists_multipliers_of_hasStrictFDerivAt_1d H (hasStrictFDerivAt_norm_sq xâ‚€)
      (hT.isSymmetric.hasStrictFDerivAt_reApplyInnerSelf xâ‚€)
  refine âŸ¨a, b, hâ‚, ?_âŸ©
  apply (InnerProductSpace.toDualMap â„ F).injective
  simp only [LinearIsometry.map_add, LinearIsometry.map_zero]
  -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 changed `map_smulâ‚›â‚—` into `map_smulâ‚›â‚— _`
  simp only [map_smulâ‚›â‚— _, RCLike.conj_to_real]
  change a â€¢ innerSL â„ xâ‚€ + b â€¢ innerSL â„ (T xâ‚€) = 0
  apply smul_right_injective (F â†’L[â„] â„) (two_ne_zero : (2 : â„) â‰  0)
  simpa only [two_smul, smul_add, add_smul, add_zero] using hâ‚‚

open scoped InnerProductSpace in
theorem eq_smul_self_of_isLocalExtrOn_real (hT : IsSelfAdjoint T) {xâ‚€ : F}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : F) â€–xâ‚€â€–) xâ‚€) :
    T xâ‚€ = T.rayleighQuotient xâ‚€ â€¢ xâ‚€ := by
  obtain âŸ¨a, b, hâ‚, hâ‚‚âŸ© := hT.linearly_dependent_of_isLocalExtrOn hextr
  by_cases hxâ‚€ : xâ‚€ = 0
  Â· simp [hxâ‚€]
  by_cases hb : b = 0
  Â· have : a â‰  0 := by simpa [hb] using hâ‚
    refine absurd ?_ hxâ‚€
    apply smul_right_injective F this
    simpa [hb] using hâ‚‚
  let c : â„ := -bâ»Â¹ * a
  have hc : T xâ‚€ = c â€¢ xâ‚€ := by
    have : b * (bâ»Â¹ * a) = a := by field_simp
    apply smul_right_injective F hb
    simp [c, eq_neg_of_add_eq_zero_left hâ‚‚, â† mul_smul, this]
  convert hc
  have := congr_arg (fun x => âŸªx, xâ‚€âŸ«_â„) hc
  simp [field, inner_smul_left, real_inner_self_eq_norm_mul_norm, mul_comm a] at this âŠ¢
  exact this

end Real

section CompleteSpace

variable [CompleteSpace E] {T : E â†’L[ğ•œ] E}

theorem eq_smul_self_of_isLocalExtrOn (hT : IsSelfAdjoint T) {xâ‚€ : E}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : E) â€–xâ‚€â€–) xâ‚€) :
    T xâ‚€ = (â†‘(T.rayleighQuotient xâ‚€) : ğ•œ) â€¢ xâ‚€ := by
  letI := InnerProductSpace.rclikeToReal ğ•œ E
  let hSA := hT.isSymmetric.restrictScalars.toSelfAdjoint.prop
  exact hSA.eq_smul_self_of_isLocalExtrOn_real hextr

/-- For a self-adjoint operator `T`, a local extremum of the Rayleigh quotient of `T` on a sphere
centred at the origin is an eigenvector of `T`. -/
theorem hasEigenvector_of_isLocalExtrOn (hT : IsSelfAdjoint T) {xâ‚€ : E} (hxâ‚€ : xâ‚€ â‰  0)
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : E) â€–xâ‚€â€–) xâ‚€) :
    HasEigenvector (T : E â†’â‚—[ğ•œ] E) (â†‘(T.rayleighQuotient xâ‚€)) xâ‚€ := by
  refine âŸ¨?_, hxâ‚€âŸ©
  rw [Module.End.mem_eigenspace_iff]
  exact hT.eq_smul_self_of_isLocalExtrOn hextr

/-- For a self-adjoint operator `T`, a maximum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global supremum of the Rayleigh
quotient. -/
theorem hasEigenvector_of_isMaxOn (hT : IsSelfAdjoint T) {xâ‚€ : E} (hxâ‚€ : xâ‚€ â‰  0)
    (hextr : IsMaxOn T.reApplyInnerSelf (sphere (0 : E) â€–xâ‚€â€–) xâ‚€) :
    HasEigenvector (T : E â†’â‚—[ğ•œ] E) (â†‘(â¨† x : { x : E // x â‰  0 }, T.rayleighQuotient x)) xâ‚€ := by
  convert hT.hasEigenvector_of_isLocalExtrOn hxâ‚€ (Or.inr hextr.localize)
  have hxâ‚€' : 0 < â€–xâ‚€â€– := by simp [hxâ‚€]
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0 : E) â€–xâ‚€â€– := by simp
  rw [T.iSup_rayleigh_eq_iSup_rayleigh_sphere hxâ‚€']
  refine IsMaxOn.iSup_eq hxâ‚€'' ?_
  intro x hx
  dsimp
  have : â€–xâ€– = â€–xâ‚€â€– := by simpa using hx
  simp only [ContinuousLinearMap.rayleighQuotient]
  rw [this]
  gcongr
  exact hextr hx

/-- For a self-adjoint operator `T`, a minimum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global infimum of the Rayleigh
quotient. -/
theorem hasEigenvector_of_isMinOn (hT : IsSelfAdjoint T) {xâ‚€ : E} (hxâ‚€ : xâ‚€ â‰  0)
    (hextr : IsMinOn T.reApplyInnerSelf (sphere (0 : E) â€–xâ‚€â€–) xâ‚€) :
    HasEigenvector (T : E â†’â‚—[ğ•œ] E) (â†‘(â¨… x : { x : E // x â‰  0 }, T.rayleighQuotient x)) xâ‚€ := by
  convert hT.hasEigenvector_of_isLocalExtrOn hxâ‚€ (Or.inl hextr.localize)
  have hxâ‚€' : 0 < â€–xâ‚€â€– := by simp [hxâ‚€]
  have hxâ‚€'' : xâ‚€ âˆˆ sphere (0 : E) â€–xâ‚€â€– := by simp
  rw [T.iInf_rayleigh_eq_iInf_rayleigh_sphere hxâ‚€']
  refine IsMinOn.iInf_eq hxâ‚€'' ?_
  intro x hx
  dsimp
  have : â€–xâ€– = â€–xâ‚€â€– := by simpa using hx
  simp only [ContinuousLinearMap.rayleighQuotient]
  rw [this]
  gcongr
  exact hextr hx

end CompleteSpace

end IsSelfAdjoint

section FiniteDimensional

variable [FiniteDimensional ğ•œ E] {T : E â†’â‚—[ğ•œ] E}

namespace LinearMap

namespace IsSymmetric

/-- The supremum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
theorem hasEigenvalue_iSup_of_finiteDimensional [Nontrivial E] (hT : T.IsSymmetric) :
    HasEigenvalue T â†‘(â¨† x : { x : E // x â‰  0 }, RCLike.re âŸªT x, xâŸ« / â€–(x : E)â€– ^ 2 : â„) := by
  haveI := FiniteDimensional.proper_rclike ğ•œ E
  let T' := hT.toSelfAdjoint
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0
  have Hâ‚ : IsCompact (sphere (0 : E) â€–xâ€–) := isCompact_sphere _ _
  have Hâ‚‚ : (sphere (0 : E) â€–xâ€–).Nonempty := âŸ¨x, by simpâŸ©
  -- key point: in finite dimension, a continuous function on the sphere has a max
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_isMaxOn Hâ‚‚ T'.val.reApplyInnerSelf_continuous.continuousOn
  have hxâ‚€ : â€–xâ‚€â€– = â€–xâ€– := by simpa using hxâ‚€'
  have : IsMaxOn T'.val.reApplyInnerSelf (sphere 0 â€–xâ‚€â€–) xâ‚€ := by simpa only [â† hxâ‚€] using hTxâ‚€
  have hxâ‚€_ne : xâ‚€ â‰  0 := by
    have : â€–xâ‚€â€– â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, Ne, not_false_iff]
    simpa [â† norm_eq_zero, Ne]
  exact hasEigenvalue_of_hasEigenvector (T'.prop.hasEigenvector_of_isMaxOn hxâ‚€_ne this)

/-- The infimum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
theorem hasEigenvalue_iInf_of_finiteDimensional [Nontrivial E] (hT : T.IsSymmetric) :
    HasEigenvalue T â†‘(â¨… x : { x : E // x â‰  0 }, RCLike.re âŸªT x, xâŸ« / â€–(x : E)â€– ^ 2 : â„) := by
  haveI := FiniteDimensional.proper_rclike ğ•œ E
  let T' := hT.toSelfAdjoint
  obtain âŸ¨x, hxâŸ© : âˆƒ x : E, x â‰  0 := exists_ne 0
  have Hâ‚ : IsCompact (sphere (0 : E) â€–xâ€–) := isCompact_sphere _ _
  have Hâ‚‚ : (sphere (0 : E) â€–xâ€–).Nonempty := âŸ¨x, by simpâŸ©
  -- key point: in finite dimension, a continuous function on the sphere has a min
  obtain âŸ¨xâ‚€, hxâ‚€', hTxâ‚€âŸ© :=
    Hâ‚.exists_isMinOn Hâ‚‚ T'.val.reApplyInnerSelf_continuous.continuousOn
  have hxâ‚€ : â€–xâ‚€â€– = â€–xâ€– := by simpa using hxâ‚€'
  have : IsMinOn T'.val.reApplyInnerSelf (sphere 0 â€–xâ‚€â€–) xâ‚€ := by simpa only [â† hxâ‚€] using hTxâ‚€
  have hxâ‚€_ne : xâ‚€ â‰  0 := by
    have : â€–xâ‚€â€– â‰  0 := by simp only [hxâ‚€, norm_eq_zero, hx, Ne, not_false_iff]
    simpa [â† norm_eq_zero, Ne]
  exact hasEigenvalue_of_hasEigenvector (T'.prop.hasEigenvector_of_isMinOn hxâ‚€_ne this)

theorem subsingleton_of_no_eigenvalue_finiteDimensional (hT : T.IsSymmetric)
    (hT' : âˆ€ Î¼ : ğ•œ, Module.End.eigenspace (T : E â†’â‚—[ğ•œ] E) Î¼ = âŠ¥) : Subsingleton E :=
  (subsingleton_or_nontrivial E).resolve_right fun _h =>
    absurd (hT' _) hT.hasEigenvalue_iSup_of_finiteDimensional

end IsSymmetric

end LinearMap

end FiniteDimensional
