/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Fr√©d√©ric Dupuis, Wanli Ma,Yunfei Zhang
-/
module

public import Mathlib.Analysis.InnerProductSpace.Calculus
public import Mathlib.Analysis.InnerProductSpace.Dual
public import Mathlib.Analysis.InnerProductSpace.Adjoint
public import Mathlib.Analysis.Calculus.LagrangeMultipliers
public import Mathlib.LinearAlgebra.Eigenspace.Basic
public import Mathlib.Algebra.EuclideanDomain.Basic

/-!
# The Rayleigh quotient

The Rayleigh quotient of a self-adjoint operator `T` on an inner product space `E` is the function
`fun x ‚Ü¶ ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2`.

The main results of this file are `IsSelfAdjoint.hasEigenvector_of_isMaxOn` and
`IsSelfAdjoint.hasEigenvector_of_isMinOn`, which state that if `E` is complete, and if the
Rayleigh quotient attains its global maximum/minimum over some sphere at the point `x‚ÇÄ`, then `x‚ÇÄ`
is an eigenvector of `T`, and the `iSup`/`iInf` of `fun x ‚Ü¶ ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2` is the corresponding
eigenvalue.

The corollaries `LinearMap.IsSymmetric.hasEigenvalue_iSup_of_finiteDimensional` and
`LinearMap.IsSymmetric.hasEigenvalue_iSup_of_finiteDimensional` state that if `E` is
finite-dimensional and nontrivial, then `T` has some (nonzero) eigenvectors with eigenvalue the
`iSup`/`iInf` of `fun x ‚Ü¶ ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2`.

## TODO

A slightly more elaborate corollary is that if `E` is complete and `T` is a compact operator, then
`T` has some (nonzero) eigenvector with eigenvalue either `‚®Ü x, ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2` or
`‚®Ö x, ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2` (not necessarily both).

-/

@[expose] public section


variable {ùïú : Type*} [RCLike ùïú]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ùïú E]

local notation "‚ü™" x ", " y "‚ü´" => inner ùïú x y

open scoped NNReal

open Module.End Metric

namespace ContinuousLinearMap

variable (T : E ‚ÜíL[ùïú] E)

/-- The *Rayleigh quotient* of a continuous linear map `T` (over `‚Ñù` or `‚ÑÇ`) at a vector `x` is
the quantity `re ‚ü™T x, x‚ü´ / ‚Äñx‚Äñ ^ 2`. -/
noncomputable abbrev rayleighQuotient (x : E) := T.reApplyInnerSelf x / ‚Äñ(x : E)‚Äñ ^ 2

theorem rayleigh_smul (x : E) {c : ùïú} (hc : c ‚â† 0) :
    rayleighQuotient T (c ‚Ä¢ x) = rayleighQuotient T x := by
  by_cases hx : x = 0
  ¬∑ simp [hx]
  simp [field, norm_smul, T.reApplyInnerSelf_smul]

theorem image_rayleigh_eq_image_rayleigh_sphere {r : ‚Ñù} (hr : 0 < r) :
    rayleighQuotient T '' {0}·∂ú = rayleighQuotient T '' sphere 0 r := by
  ext a
  constructor
  ¬∑ rintro ‚ü®x, hx : x ‚â† 0, hxT‚ü©
    let c : ùïú := ‚Üë‚Äñx‚Äñ‚Åª¬π * r
    have : c ‚â† 0 := by simp [c, hx, hr.ne']
    refine ‚ü®c ‚Ä¢ x, ?_, ?_‚ü©
    ¬∑ simp [field, c, norm_smul, abs_of_pos hr]
    ¬∑ rw [T.rayleigh_smul x this]
      exact hxT
  ¬∑ rintro ‚ü®x, hx, hxT‚ü©
    exact ‚ü®x, ne_zero_of_mem_sphere hr.ne' ‚ü®x, hx‚ü©, hxT‚ü©

theorem iSup_rayleigh_eq_iSup_rayleigh_sphere {r : ‚Ñù} (hr : 0 < r) :
    ‚®Ü x : { x : E // x ‚â† 0 }, rayleighQuotient T x =
      ‚®Ü x : sphere (0 : E) r, rayleighQuotient T x :=
  show ‚®Ü x : ({0}·∂ú : Set E), rayleighQuotient T x = _ by
    simp only [‚Üê @sSup_image' _ _ _ _ (rayleighQuotient T),
      T.image_rayleigh_eq_image_rayleigh_sphere hr]

theorem iInf_rayleigh_eq_iInf_rayleigh_sphere {r : ‚Ñù} (hr : 0 < r) :
    ‚®Ö x : { x : E // x ‚â† 0 }, rayleighQuotient T x =
      ‚®Ö x : sphere (0 : E) r, rayleighQuotient T x :=
  show ‚®Ö x : ({0}·∂ú : Set E), rayleighQuotient T x = _ by
    simp only [‚Üê @sInf_image' _ _ _ _ (rayleighQuotient T),
      T.image_rayleigh_eq_image_rayleigh_sphere hr]

section RayleighGeneral

variable {x : E} {T : E ‚ÜíL[ùïú] E}

/-!
### General lemmas
theorem
`ContinuousLinearMap.rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors`:
For a continuous linear operator `T` with an orthonormal family of eigenvectors
`v j` with real eigenvalues `u j`, the Rayleigh quotient of a unit vector in the span
of `{v j | j ‚àà s}` lies between the minimum and maximum eigenvalues `u j` for `j ‚àà s`.

theorem
`ContinuousLinearMap.rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors_nonzero`:
Non-normalized version of the above.
-/

/-- The Rayleigh quotient is additive in the operator: the Rayleigh quotient of the sum of two
operators equals the sum of their individual Rayleigh quotients. This property is useful for
decomposing operators into simpler components. -/
@[simp]
theorem rayleighQuotient_add {S : E ‚ÜíL[ùïú] E} :
    (T + S).rayleighQuotient x = T.rayleighQuotient x + S.rayleighQuotient x := by
  simp [rayleighQuotient, reApplyInnerSelf_apply, inner_add_left, add_div]


variable {Œπ : Type*} {v : Œπ ‚Üí E} {u : Œπ ‚Üí ‚Ñù} {s : Finset Œπ}


/-- Rayleigh quotient expressed using coefficients in an orthonormal eigenbasis.
If `v` is an orthonormal family of eigenvectors of a continuous linear operator `T` with
real eigenvalues `u`, and `x` is a linear combination of `v j` for `j ‚àà s` with coefficients
`c j`, then the Rayleigh quotient of `x` is the weighted average of the corresponding
eigenvalues with weights `‚Äñc j‚Äñ¬≤`. -/
lemma rayleighQuotient_eq_sum_sqNorm_mul_eigenvalues {c : s ‚Üí ùïú} (hv : Orthonormal ùïú v)
    (h_eigen : ‚àÄ j : Œπ, T (v j) = (u j : ùïú) ‚Ä¢ v j) (hx : x = ‚àë j, c j ‚Ä¢ v j) :
    T.rayleighQuotient x = (‚àë j, ‚Äñc j‚Äñ ^ 2 * u j) / ‚Äñx‚Äñ ^ 2 := by
  have hv' : Orthonormal ùïú (fun j : s => v j) :=
    ‚ü®fun j => hv.1 j, fun i j hij => hv.2 (Subtype.coe_ne_coe.mpr hij)‚ü©
  rw [ContinuousLinearMap.rayleighQuotient, ContinuousLinearMap.reApplyInnerSelf, hx]
  congr 1
  simp only [map_sum, map_smul, h_eigen, smul_smul, Orthonormal.inner_sum hv']
  refine Finset.sum_congr rfl fun i _ => ?_
  rw [RingHom.map_mul, RCLike.conj_ofReal, mul_right_comm, mul_comm (starRingEnd ùïú (c i)),
    RCLike.mul_conj]
  simp


variable [LinearOrder Œπ]


/-- **Rayleigh quotient bounds for a unit vector** in the span of an orthonormal family
of eigenvectors, indexed by a finite set.
Assume the eigenvalues `u` are indexed in nonincreasing order with respect to the index
(order-preserving indices correspond to nonincreasing eigenvalues). For a unit vector in
the span of `{v j | j ‚àà s}`, its Rayleigh quotient lies between the minimal and maximal
eigenvalues among `u j` for `j ‚àà s`. -/
theorem rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors
    (hv : Orthonormal ùïú v)
    (h_eigen : ‚àÄ j : Œπ, T (v j) = (u j : ùïú) ‚Ä¢ v j)
    (hs : s.Nonempty) (h_norm : ‚Äñx‚Äñ = 1)
    (h_in_span : x ‚àà Submodule.span ùïú (Set.range fun j : s => v j))
    (h_sorted : Antitone u) :
    u (s.max' hs) ‚â§ T.rayleighQuotient x ‚àß
    T.rayleighQuotient x ‚â§ u (s.min' hs) := by
  have ‚ü®c, hc‚ü© : ‚àÉ (c : s ‚Üí ùïú), x = ‚àë j, c j ‚Ä¢ v j := by
    rw [Submodule.mem_span_range_iff_exists_fun] at h_in_span
    exact ‚ü®h_in_span.choose, h_in_span.choose_spec.symm‚ü©
  have hv' : Orthonormal ùïú (fun j : s => v j) :=
    ‚ü®fun j => hv.1 j, fun i j hij => hv.2 (Subtype.coe_ne_coe.mpr hij)‚ü©
  have hsum : ‚àë j, ‚Äñc j‚Äñ ^ 2 = 1 := by
    have : OrthogonalFamily ùïú (fun _ : s => ùïú)
        (fun j : s => LinearIsometry.toSpanSingleton ùïú E (hv'.1 j)) := by
      intro i j hij v w
      simp only [LinearIsometry.toSpanSingleton_apply, inner_smul_left, inner_smul_right,
        hv'.2 hij, mul_zero]
    have parseval := OrthogonalFamily.norm_sum this c  (Finset.univ : Finset s)
    simp only [LinearIsometry.toSpanSingleton_apply, Finset.univ_eq_attach] at parseval
    calc ‚àë j, ‚Äñc j‚Äñ ^ 2
        = ‚Äñ‚àë j, c j ‚Ä¢ v j‚Äñ ^ 2 := parseval.symm
      _ = ‚Äñx‚Äñ ^ 2 := by rw [hc]
      _ = 1 := by rw [h_norm]; ring
  rw [rayleighQuotient_eq_sum_sqNorm_mul_eigenvalues hv h_eigen hc]
  simp only [h_norm, one_pow, div_one]
  constructor
  ¬∑ calc u (s.max' hs) = u (s.max' hs) * ‚àë j : s, ‚Äñc j‚Äñ ^ 2 := by rw [hsum]; ring
      _ = ‚àë j : s, u (s.max' hs) * ‚Äñc j‚Äñ ^ 2 := by rw [‚Üê Finset.mul_sum]
      _ ‚â§ ‚àë j : s, u ‚Üëj * ‚Äñc j‚Äñ ^ 2 := by
          refine Finset.sum_le_sum fun j _ => ?_
          exact mul_le_mul_of_nonneg_right
            (h_sorted (Finset.le_max' s (‚Üëj) j.2)) (sq_nonneg _)
      _ = ‚àë j : s, ‚Äñc j‚Äñ ^ 2 * u ‚Üëj := by
          refine Finset.sum_congr rfl fun j _ => mul_comm _ _
  ¬∑ calc ‚àë j : s, ‚Äñc j‚Äñ ^ 2 * u ‚Üëj
        = ‚àë j : s, u ‚Üëj * ‚Äñc j‚Äñ ^ 2 := by
          refine Finset.sum_congr rfl fun j _ => mul_comm _ _
      _ ‚â§ ‚àë j : s, u (s.min' hs) * ‚Äñc j‚Äñ ^ 2 := by
          refine Finset.sum_le_sum fun j _ => ?_
          exact mul_le_mul_of_nonneg_right
            (h_sorted (Finset.min'_le s (‚Üëj) j.2)) (sq_nonneg _)
      _ = u (s.min' hs) * ‚àë j : s, ‚Äñc j‚Äñ ^ 2 := by rw [‚Üê Finset.mul_sum]
      _ = u (s.min' hs) := by rw [hsum]; ring


/-- **Rayleigh quotient bounds for a nonzero vector** in the span of an orthonormal family
of eigenvectors, indexed by a finite set.
This is the non-normalized version of
`ContinuousLinearMap.rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors`, obtained by
normalizing the vector and using the fact that the Rayleigh quotient is invariant
under nonzero scalar multiples. -/
theorem rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors_nonzero
    (hv : Orthonormal ùïú v)
    (h_eigen : ‚àÄ j : Œπ, T (v j) = (u j : ùïú) ‚Ä¢ v j)
    (hs : s.Nonempty) (h_nz : x ‚â† 0)
    (h_in_span : x ‚àà Submodule.span ùïú (Set.range fun j : s => v j))
    (h_sorted : Antitone u) :
    u (s.max' hs) ‚â§ T.rayleighQuotient x ‚àß
    T.rayleighQuotient x ‚â§ u (s.min' hs) := by
  by_cases h_unit : ‚Äñx‚Äñ = 1
  ¬∑ exact rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors
        hv h_eigen  hs  h_unit h_in_span h_sorted
  ¬∑ have h_norm_pos : 0 < ‚Äñx‚Äñ := norm_pos_iff.mpr h_nz
    set a : ùïú := ((‚Äñx‚Äñ : ‚Ñù)‚Åª¬π : ùïú) with ha
    set t := a ‚Ä¢ x with ht
    have h_t_norm : ‚Äñt‚Äñ = 1 := by
      rw [ht, norm_smul]; simp [ha, h_norm_pos.ne']
    have h_t_mem : t ‚àà Submodule.span ùïú (Set.range fun j : s => v j) :=
      Submodule.smul_mem _ _ h_in_span
    have ha_ne : a ‚â† 0 := by simp [ha, h_norm_pos.ne']
    rw [show T.rayleighQuotient x = T.rayleighQuotient t from
      (ht ‚ñ∏ (ContinuousLinearMap.rayleigh_smul T x ha_ne).symm)]
    exact rayleighQuotient_mem_Icc_of_mem_span_orthonormal_eigenvectors
        hv h_eigen hs  h_t_norm h_t_mem h_sorted

end RayleighGeneral

end ContinuousLinearMap



namespace IsSelfAdjoint

section Real

variable {F : Type*} [NormedAddCommGroup F] [InnerProductSpace ‚Ñù F]

theorem _root_.LinearMap.IsSymmetric.hasStrictFDerivAt_reApplyInnerSelf {T : F ‚ÜíL[‚Ñù] F}
    (hT : (T : F ‚Üí‚Çó[‚Ñù] F).IsSymmetric) (x‚ÇÄ : F) :
    HasStrictFDerivAt T.reApplyInnerSelf (2 ‚Ä¢ (innerSL ‚Ñù (T x‚ÇÄ))) x‚ÇÄ := by
  convert T.hasStrictFDerivAt.inner ‚Ñù (hasStrictFDerivAt_id x‚ÇÄ) using 1
  ext y
  rw [ContinuousLinearMap.smul_apply, ContinuousLinearMap.comp_apply, fderivInnerCLM_apply,
    ContinuousLinearMap.prod_apply, innerSL_apply_apply, id, ContinuousLinearMap.id_apply,
    hT.apply_clm x‚ÇÄ y, real_inner_comm _ x‚ÇÄ, two_smul]

variable [CompleteSpace F] {T : F ‚ÜíL[‚Ñù] F}

theorem linearly_dependent_of_isLocalExtrOn (hT : IsSelfAdjoint T) {x‚ÇÄ : F}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : F) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    ‚àÉ a b : ‚Ñù, (a, b) ‚â† 0 ‚àß a ‚Ä¢ x‚ÇÄ + b ‚Ä¢ T x‚ÇÄ = 0 := by
  have H : IsLocalExtrOn T.reApplyInnerSelf {x : F | ‚Äñx‚Äñ ^ 2 = ‚Äñx‚ÇÄ‚Äñ ^ 2} x‚ÇÄ := by
    convert hextr
    ext x
    simp
  -- find Lagrange multipliers for the function `T.re_apply_inner_self` and the
  -- hypersurface-defining function `fun x ‚Ü¶ ‚Äñx‚Äñ ^ 2`
  obtain ‚ü®a, b, h‚ÇÅ, h‚ÇÇ‚ü© :=
    IsLocalExtrOn.exists_multipliers_of_hasStrictFDerivAt_1d H (hasStrictFDerivAt_norm_sq x‚ÇÄ)
      (hT.isSymmetric.hasStrictFDerivAt_reApplyInnerSelf x‚ÇÄ)
  refine ‚ü®a, b, h‚ÇÅ, ?_‚ü©
  apply (InnerProductSpace.toDualMap ‚Ñù F).injective
  simp only [LinearIsometry.map_add, LinearIsometry.map_zero]
  -- Note: https://github.com/leanprover-community/mathlib4/pull/8386 changed `map_smul‚Çõ‚Çó` into `map_smul‚Çõ‚Çó _`
  simp only [map_smul‚Çõ‚Çó _, RCLike.conj_to_real]
  change a ‚Ä¢ innerSL ‚Ñù x‚ÇÄ + b ‚Ä¢ innerSL ‚Ñù (T x‚ÇÄ) = 0
  apply smul_right_injective (F ‚ÜíL[‚Ñù] ‚Ñù) (two_ne_zero : (2 : ‚Ñù) ‚â† 0)
  simpa only [two_smul, smul_add, add_smul, add_zero] using h‚ÇÇ

-- Non-terminal simp, used to be field_simp
set_option linter.flexible false in
open scoped InnerProductSpace in
theorem eq_smul_self_of_isLocalExtrOn_real (hT : IsSelfAdjoint T) {x‚ÇÄ : F}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : F) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    T x‚ÇÄ = T.rayleighQuotient x‚ÇÄ ‚Ä¢ x‚ÇÄ := by
  obtain ‚ü®a, b, h‚ÇÅ, h‚ÇÇ‚ü© := hT.linearly_dependent_of_isLocalExtrOn hextr
  by_cases hx‚ÇÄ : x‚ÇÄ = 0
  ¬∑ simp [hx‚ÇÄ]
  by_cases hb : b = 0
  ¬∑ have : a ‚â† 0 := by simpa [hb] using h‚ÇÅ
    refine absurd ?_ hx‚ÇÄ
    apply smul_right_injective F this
    simpa [hb] using h‚ÇÇ
  have hc : T x‚ÇÄ = (-b‚Åª¬π * a) ‚Ä¢ x‚ÇÄ := by
    linear_combination (norm := match_scalars <;> field) b‚Åª¬π ‚Ä¢ h‚ÇÇ
  set c : ‚Ñù := -b‚Åª¬π * a
  convert hc
  have := congr_arg (fun x => ‚ü™x, x‚ÇÄ‚ü´_‚Ñù) hc
  simp [field, inner_smul_left, mul_comm a] at this ‚ä¢
  exact this

end Real

section CompleteSpace

variable [CompleteSpace E] {T : E ‚ÜíL[ùïú] E}

theorem eq_smul_self_of_isLocalExtrOn (hT : IsSelfAdjoint T) {x‚ÇÄ : E}
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    T x‚ÇÄ = (‚Üë(T.rayleighQuotient x‚ÇÄ) : ùïú) ‚Ä¢ x‚ÇÄ := by
  letI := InnerProductSpace.rclikeToReal ùïú E
  let hSA := hT.isSymmetric.restrictScalars.toSelfAdjoint.prop
  exact hSA.eq_smul_self_of_isLocalExtrOn_real hextr

/-- For a self-adjoint operator `T`, a local extremum of the Rayleigh quotient of `T` on a sphere
centred at the origin is an eigenvector of `T`. -/
theorem hasEigenvector_of_isLocalExtrOn (hT : IsSelfAdjoint T) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚â† 0)
    (hextr : IsLocalExtrOn T.reApplyInnerSelf (sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    HasEigenvector (T : E ‚Üí‚Çó[ùïú] E) (‚Üë(T.rayleighQuotient x‚ÇÄ)) x‚ÇÄ := by
  refine ‚ü®?_, hx‚ÇÄ‚ü©
  rw [Module.End.mem_eigenspace_iff]
  exact hT.eq_smul_self_of_isLocalExtrOn hextr

/-- For a self-adjoint operator `T`, a maximum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global supremum of the Rayleigh
quotient. -/
theorem hasEigenvector_of_isMaxOn (hT : IsSelfAdjoint T) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚â† 0)
    (hextr : IsMaxOn T.reApplyInnerSelf (sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    HasEigenvector (T : E ‚Üí‚Çó[ùïú] E) (‚Üë(‚®Ü x : { x : E // x ‚â† 0 }, T.rayleighQuotient x)) x‚ÇÄ := by
  convert hT.hasEigenvector_of_isLocalExtrOn hx‚ÇÄ (Or.inr hextr.localize)
  have hx‚ÇÄ' : 0 < ‚Äñx‚ÇÄ‚Äñ := by simp [hx‚ÇÄ]
  have hx‚ÇÄ'' : x‚ÇÄ ‚àà sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ := by simp
  rw [T.iSup_rayleigh_eq_iSup_rayleigh_sphere hx‚ÇÄ']
  refine IsMaxOn.iSup_eq hx‚ÇÄ'' ?_
  intro x hx
  dsimp
  have : ‚Äñx‚Äñ = ‚Äñx‚ÇÄ‚Äñ := by simpa using hx
  simp only [ContinuousLinearMap.rayleighQuotient]
  rw [this]
  gcongr
  exact hextr hx

/-- For a self-adjoint operator `T`, a minimum of the Rayleigh quotient of `T` on a sphere centred
at the origin is an eigenvector of `T`, with eigenvalue the global infimum of the Rayleigh
quotient. -/
theorem hasEigenvector_of_isMinOn (hT : IsSelfAdjoint T) {x‚ÇÄ : E} (hx‚ÇÄ : x‚ÇÄ ‚â† 0)
    (hextr : IsMinOn T.reApplyInnerSelf (sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ) :
    HasEigenvector (T : E ‚Üí‚Çó[ùïú] E) (‚Üë(‚®Ö x : { x : E // x ‚â† 0 }, T.rayleighQuotient x)) x‚ÇÄ := by
  convert hT.hasEigenvector_of_isLocalExtrOn hx‚ÇÄ (Or.inl hextr.localize)
  have hx‚ÇÄ' : 0 < ‚Äñx‚ÇÄ‚Äñ := by simp [hx‚ÇÄ]
  have hx‚ÇÄ'' : x‚ÇÄ ‚àà sphere (0 : E) ‚Äñx‚ÇÄ‚Äñ := by simp
  rw [T.iInf_rayleigh_eq_iInf_rayleigh_sphere hx‚ÇÄ']
  refine IsMinOn.iInf_eq hx‚ÇÄ'' ?_
  intro x hx
  dsimp
  have : ‚Äñx‚Äñ = ‚Äñx‚ÇÄ‚Äñ := by simpa using hx
  simp only [ContinuousLinearMap.rayleighQuotient]
  rw [this]
  gcongr
  exact hextr hx

end CompleteSpace

end IsSelfAdjoint

section FiniteDimensional

variable [FiniteDimensional ùïú E] {T : E ‚Üí‚Çó[ùïú] E}

namespace LinearMap

namespace IsSymmetric

/-- The supremum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
theorem hasEigenvalue_iSup_of_finiteDimensional [Nontrivial E] (hT : T.IsSymmetric) :
    HasEigenvalue T ‚Üë(‚®Ü x : { x : E // x ‚â† 0 }, RCLike.re ‚ü™T x, x‚ü´ / ‚Äñ(x : E)‚Äñ ^ 2 : ‚Ñù) := by
  haveI := FiniteDimensional.proper_rclike ùïú E
  let T' := hT.toSelfAdjoint
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : E, x ‚â† 0 := exists_ne 0
  have H‚ÇÅ : IsCompact (sphere (0 : E) ‚Äñx‚Äñ) := isCompact_sphere _ _
  have H‚ÇÇ : (sphere (0 : E) ‚Äñx‚Äñ).Nonempty := ‚ü®x, by simp‚ü©
  -- key point: in finite dimension, a continuous function on the sphere has a max
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ', hTx‚ÇÄ‚ü© :=
    H‚ÇÅ.exists_isMaxOn H‚ÇÇ T'.val.reApplyInnerSelf_continuous.continuousOn
  have hx‚ÇÄ : ‚Äñx‚ÇÄ‚Äñ = ‚Äñx‚Äñ := by simpa using hx‚ÇÄ'
  have : IsMaxOn T'.val.reApplyInnerSelf (sphere 0 ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ := by simpa only [‚Üê hx‚ÇÄ] using hTx‚ÇÄ
  have hx‚ÇÄ_ne : x‚ÇÄ ‚â† 0 := by
    have : ‚Äñx‚ÇÄ‚Äñ ‚â† 0 := by simp only [hx‚ÇÄ, norm_eq_zero, hx, Ne, not_false_iff]
    simpa [‚Üê norm_eq_zero, Ne]
  exact hasEigenvalue_of_hasEigenvector (T'.prop.hasEigenvector_of_isMaxOn hx‚ÇÄ_ne this)

/-- The infimum of the Rayleigh quotient of a symmetric operator `T` on a nontrivial
finite-dimensional vector space is an eigenvalue for that operator. -/
theorem hasEigenvalue_iInf_of_finiteDimensional [Nontrivial E] (hT : T.IsSymmetric) :
    HasEigenvalue T ‚Üë(‚®Ö x : { x : E // x ‚â† 0 }, RCLike.re ‚ü™T x, x‚ü´ / ‚Äñ(x : E)‚Äñ ^ 2 : ‚Ñù) := by
  haveI := FiniteDimensional.proper_rclike ùïú E
  let T' := hT.toSelfAdjoint
  obtain ‚ü®x, hx‚ü© : ‚àÉ x : E, x ‚â† 0 := exists_ne 0
  have H‚ÇÅ : IsCompact (sphere (0 : E) ‚Äñx‚Äñ) := isCompact_sphere _ _
  have H‚ÇÇ : (sphere (0 : E) ‚Äñx‚Äñ).Nonempty := ‚ü®x, by simp‚ü©
  -- key point: in finite dimension, a continuous function on the sphere has a min
  obtain ‚ü®x‚ÇÄ, hx‚ÇÄ', hTx‚ÇÄ‚ü© :=
    H‚ÇÅ.exists_isMinOn H‚ÇÇ T'.val.reApplyInnerSelf_continuous.continuousOn
  have hx‚ÇÄ : ‚Äñx‚ÇÄ‚Äñ = ‚Äñx‚Äñ := by simpa using hx‚ÇÄ'
  have : IsMinOn T'.val.reApplyInnerSelf (sphere 0 ‚Äñx‚ÇÄ‚Äñ) x‚ÇÄ := by simpa only [‚Üê hx‚ÇÄ] using hTx‚ÇÄ
  have hx‚ÇÄ_ne : x‚ÇÄ ‚â† 0 := by
    have : ‚Äñx‚ÇÄ‚Äñ ‚â† 0 := by simp only [hx‚ÇÄ, norm_eq_zero, hx, Ne, not_false_iff]
    simpa [‚Üê norm_eq_zero, Ne]
  exact hasEigenvalue_of_hasEigenvector (T'.prop.hasEigenvector_of_isMinOn hx‚ÇÄ_ne this)

theorem subsingleton_of_no_eigenvalue_finiteDimensional (hT : T.IsSymmetric)
    (hT' : ‚àÄ Œº : ùïú, Module.End.eigenspace (T : E ‚Üí‚Çó[ùïú] E) Œº = ‚ä•) : Subsingleton E :=
  (subsingleton_or_nontrivial E).resolve_right fun _h =>
    absurd (hT' _) hT.hasEigenvalue_iSup_of_finiteDimensional

end IsSymmetric

end LinearMap

end FiniteDimensional
