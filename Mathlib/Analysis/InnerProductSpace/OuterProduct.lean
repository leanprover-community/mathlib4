/-
Copyright (c) 2025 IvÃ¡n Renison, Jam Khan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IvÃ¡n Renison, Jam Khan
-/
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.Trace

/-!
This file defines the outer product of two vectors as a linear map,
and proves basic properties of the outer product.
-/

namespace LinearMap

variable (ğ•œ : Type*) {E F : Type*} [RCLike ğ•œ]

variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

/-- The outer product of two vectors -/
def outerProduct (x : E) (y : F) : F â†’â‚—[ğ•œ] E where
  toFun := fun z => inner ğ•œ y z â€¢ x
  map_add' z w := by
    rw [â† Module.add_smul, inner_add_right y z w]
  map_smul' m z := by
    rw [RingHom.id_apply, inner_smul_right_eq_smul y z m]
    exact IsScalarTower.smul_assoc m (inner ğ•œ y z) x

lemma outerProduct_def (x : E) (y : F) (z : F) :
    outerProduct ğ•œ x y z = inner ğ•œ y z â€¢ x := rfl

lemma outerProduct_add_left (x : E) (y : E) (z : F) :
    outerProduct ğ•œ (x + y) z = outerProduct ğ•œ x z + outerProduct ğ•œ y z := by
  ext
  simp [add_apply, outerProduct_def, smul_add]

lemma outerProduct_add_right (x : E) (y : F) (z : F) :
    outerProduct ğ•œ x (y + z) = outerProduct ğ•œ x y + outerProduct ğ•œ x z := by
  ext
  simp [add_apply, outerProduct_def, inner_add_left, add_smul]

lemma outerProduct_sub_left (x : F) (y : F) (z : E) :
    outerProduct ğ•œ (x - y) z = outerProduct ğ•œ x z - outerProduct ğ•œ y z := by
  ext
  simp [sub_apply, outerProduct_def, smul_sub]

lemma outerProduct_sub_right (x : E) (y : F) (z : F) :
    outerProduct ğ•œ x (y - z) = outerProduct ğ•œ x y - outerProduct ğ•œ x z := by
  ext
  simp [sub_apply, outerProduct_def, inner_sub_left, sub_smul]

lemma outerProduct_assoc_right (x : E) (y : F) (z : F) :
    (outerProduct ğ•œ x y) z = inner ğ•œ y z â€¢ x := rfl

lemma outerProduct_smul_assoc_left (c : ğ•œ) (x : E) (y : F) :
    outerProduct ğ•œ (c â€¢ x) y = (c : ğ•œ) â€¢ outerProduct ğ•œ x y := by
  ext
  simp only [smul_apply, outerProduct_def]
  rw [smul_algebra_smul_comm]

lemma outerProduct_smul_assoc_right (c : ğ•œ) (x : E) (y : F) :
    outerProduct ğ•œ x (c â€¢ y) = starRingEnd ğ•œ c â€¢ outerProduct ğ•œ x y := by
  ext
  simp only [smul_apply, outerProduct_def]
  rw [starRingEnd_apply, smul_algebra_smul_comm, inner_smul_left, starRingEnd_apply, mul_smul]
  simp only [RCLike.star_def]
  rw [smul_algebra_smul_comm]

lemma inner_outerProduct_eq_inner_mul_inner (x : E) (y z : F) (w : E) :
    inner ğ•œ (outerProduct ğ•œ x y z) w = inner ğ•œ z y * inner ğ•œ x w := by
  simp [outerProduct_def, inner_smul_left, inner_conj_symm]

lemma outerProduct_comp_outerProduct_eq_inner_smul_outerProduct (x : E) (y z : F) (w : E) :
    outerProduct ğ•œ x y âˆ˜â‚— outerProduct ğ•œ z w = inner ğ•œ y z â€¢ outerProduct ğ•œ x w := by
  ext v
  simp only [comp_apply, outerProduct_def, map_smul, smul_apply]
  rw [smul_algebra_smul_comm]

lemma outerProduct_mul_outerProduct_eq_inner_smul_outerProduct (x y z w : E) :
    outerProduct ğ•œ x y * outerProduct ğ•œ z w = inner ğ•œ y z â€¢ outerProduct ğ•œ x w := by
  rw [Module.End.mul_eq_comp]
  exact outerProduct_comp_outerProduct_eq_inner_smul_outerProduct ğ•œ x y z w

lemma isIdempotentElem_outerProduct_self_of_norm_eq_one {x : E} (h : â€–xâ€– = 1) :
    IsIdempotentElem (outerProduct ğ•œ x x) := by
  ext y
  rw [Module.End.mul_eq_comp]
  simp [coe_comp, Function.comp_apply, outerProduct_def, inner_self_eq_norm_sq_to_K, h]

variable [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]

lemma adjoint_outerProduct (x : E) (y : F) :
    (outerProduct ğ•œ x y).adjoint = outerProduct ğ•œ y x := by
  symm
  rw [eq_adjoint_iff]
  intro v w
  repeat rw [outerProduct_def]
  rw [inner_smul_left, inner_conj_symm, inner_smul_right]
  exact mul_comm _ _

lemma star_outerProduct (x y : E) :
    star (outerProduct ğ•œ x y) = outerProduct ğ•œ y x := by
  rw [star_eq_adjoint, adjoint_outerProduct]

lemma isSelfAdjoint_outerProduct_self (x : E) :
    IsSelfAdjoint (outerProduct ğ•œ x x) := by
  rw [IsSelfAdjoint, star_eq_adjoint, adjoint_outerProduct]

lemma isSymmetric_outerProduct_self (x : E) : (outerProduct ğ•œ x x).IsSymmetric :=
  (outerProduct ğ•œ x x).isSymmetric_iff_isSelfAdjoint.mpr (isSelfAdjoint_outerProduct_self ğ•œ x)

lemma isPositive_outerProduct_self (x : E) :
    (outerProduct ğ•œ x x).IsPositive := by
  apply And.intro (isSymmetric_outerProduct_self ğ•œ x)
  intro y
  simp only [outerProduct_def]
  rw [inner_smul_left, InnerProductSpace.conj_inner_symm, inner_mul_symm_re_eq_norm]
  exact norm_nonneg (inner ğ•œ y x * inner ğ•œ x y)

lemma isStarProjection_outerProduct_self_of_norm_eq_one {x : E} (h : â€–xâ€– = 1) :
    IsStarProjection (outerProduct ğ•œ x x) :=
  âŸ¨isIdempotentElem_outerProduct_self_of_norm_eq_one ğ•œ h, isSelfAdjoint_outerProduct_self ğ•œ xâŸ©

lemma isSelfAdjoint_outerProduct_add (x y : E) :
    IsSelfAdjoint (outerProduct ğ•œ x y + outerProduct ğ•œ y x) := by
  rw [isSelfAdjoint_iff', map_add]
  repeat rw [adjoint_outerProduct]
  abel

lemma isSymmetric_outerProduct_add (x y : E) :
    (outerProduct ğ•œ x y + outerProduct ğ•œ y x).IsSymmetric :=
  (outerProduct ğ•œ x y + outerProduct ğ•œ y x).isSymmetric_iff_isSelfAdjoint.mpr
    (isSelfAdjoint_outerProduct_add ğ•œ x y)

omit [FiniteDimensional ğ•œ E] [FiniteDimensional ğ•œ F]
variable {Î¹ : Type*} [Fintype Î¹]

lemma sum_outerProduct (f g : Î¹ â†’ E) (x : E) :
    (âˆ‘ i, outerProduct ğ•œ (f i) (g i)) x = âˆ‘ i, outerProduct ğ•œ (f i) (g i) x := by
  simp only [sum_apply]

variable {ğ•œ}

lemma sum_outerProduct_OrthonormalBasis (b : OrthonormalBasis Î¹ ğ•œ E) :
    âˆ‘i, outerProduct ğ•œ (b i) (b i) = 1 := by
  ext x
  rw [â† LinearIsometryEquiv.map_eq_iff b.repr]
  simp only [coeFn_sum, Finset.sum_apply, Module.End.one_apply, outerProduct_def]
  congr
  exact b.sum_repr' x

variable [DecidableEq Î¹]

lemma trace_outerProduct (x y : E) (b : OrthonormalBasis Î¹ ğ•œ E) :
    trace ğ•œ E (outerProduct ğ•œ x y) = inner ğ•œ y x := by
  rw [(outerProduct ğ•œ x y).trace_eq_sum_inner b]
  simp +contextual [outerProduct_def, inner_smul_right]
  have : âˆ€i, inner ğ•œ y (b i) * inner ğ•œ (b i) x = inner ğ•œ (b i) x * inner ğ•œ y (b i) := by
    intro i
    apply mul_comm
  simp +contextual [this, â† inner_smul_right, â† outerProduct_def]
  rw [â† inner_sum, â† sum_outerProduct, sum_outerProduct_OrthonormalBasis b, Module.End.one_apply]

end LinearMap
