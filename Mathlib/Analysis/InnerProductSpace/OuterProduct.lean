/-
Copyright (c) 2025 IvÃ¡n Renison, Jam Khan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IvÃ¡n Renison, Jam Khan
-/
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.Trace

/-!
This file defines the outer product of two vectors as a linear map,
and proves basic properties of the outer product.
-/

namespace ContinuousLinearMap

section seminormed


variable {ğ•œ V W : Type*} [RCLike ğ•œ]
variable [SeminormedAddCommGroup V] [InnerProductSpace ğ•œ V]
variable [SeminormedAddCommGroup W] [InnerProductSpace ğ•œ W]

variable (ğ•œ) in
/-- A rank-one operator on an inner product space is given by `x â†¦ y â†¦ z â†¦ âŸªy, zâŸ« â€¢ x`. -/
noncomputable def rankOne : V â†’L[ğ•œ] W â†’Lâ‹†[ğ•œ] W â†’L[ğ•œ] V :=
  LinearMap.mkContinuousâ‚‚
  ({toFun := fun x =>
    { toFun := fun y => (lsmul ğ•œ ğ•œ).flip x âˆ˜L innerSL ğ•œ y
      map_add' := fun _ _ => by rw [map_add, comp_add]
      map_smul' := fun _ _ => by rw [map_smulâ‚›â‚—, comp_smulâ‚›â‚—]; rfl }
    map_add' := fun _ _ => by ext; simp
    map_smul' := fun _ _ => by ext; simp })
  1 (fun x y => calc _ â‰¤ _ := opNorm_comp_le _ _
      _ â‰¤ â€–xâ€– * â€–yâ€– := mul_le_mul (opNorm_le_bound _ (norm_nonneg x)
          (by simp [norm_smul, mul_comm]))
        (innerSL_apply_norm ğ•œ y â–¸ le_refl _) (norm_nonneg _) (norm_nonneg _)
      _ = _ := by rw [one_mul])

lemma rankOne_def (x : V) (y : W) :
    rankOne ğ•œ x y = (lsmul ğ•œ ğ•œ).flip x âˆ˜L innerSL ğ•œ y :=
  rfl

lemma rankOne_def' (x : V) (y : W) :
    rankOne ğ•œ x y = (innerSL ğ•œ y).smulRight x :=
  rfl

@[simp]
lemma rankOne_apply (x : V) (y z : W) :
    rankOne ğ•œ x y z = inner ğ•œ y z â€¢ x :=
  rfl

lemma rankOne_add_left (x : V) (y : V) (z : W) :
    rankOne ğ•œ (x + y) z = rankOne ğ•œ x z + rankOne ğ•œ y z := by
  ext
  simp [add_apply]

lemma rankOne_add_right (x : V) (y : W) (z : W) :
    rankOne ğ•œ x (y + z) = rankOne ğ•œ x y + rankOne ğ•œ x z := by
  ext
  simp [add_apply]

lemma rankOne_sub_left (x : W) (y : W) (z : V) :
    rankOne ğ•œ (x - y) z = rankOne ğ•œ x z - rankOne ğ•œ y z := by
  ext
  simp [sub_apply]

lemma rankOne_sub_right (x : V) (y : W) (z : W) :
    rankOne ğ•œ x (y - z) = rankOne ğ•œ x y - rankOne ğ•œ x z := by
  ext
  simp [sub_apply]

lemma rankOne_smul_left (c : ğ•œ) (x : V) (y : W) :
    rankOne ğ•œ (c â€¢ x) y = (c : ğ•œ) â€¢ rankOne ğ•œ x y := by
  ext
  simp only [smul_apply, rankOne_apply]
  rw [smul_algebra_smul_comm]

lemma rankOne_smul_right (c : ğ•œ) (x : V) (y : W) :
    rankOne ğ•œ x (c â€¢ y) = starRingEnd ğ•œ c â€¢ rankOne ğ•œ x y := by
  ext
  simp only [smul_apply, rankOne_apply]
  rw [starRingEnd_apply, smul_algebra_smul_comm, inner_smul_left, starRingEnd_apply, mul_smul]
  simp only [RCLike.star_def]
  rw [smul_algebra_smul_comm]

lemma inner_rankOne_eq_inner_mul_inner (x : V) (y z : W) (w : V) :
    inner ğ•œ (rankOne ğ•œ x y z) w = inner ğ•œ z y * inner ğ•œ x w := by
  simp [inner_smul_left, inner_conj_symm]

lemma rankOne_comp_rankOne_eq_inner_smul_rankOne (x : V) (y z : W) (w : V) :
    rankOne ğ•œ x y âˆ˜L rankOne ğ•œ z w = inner ğ•œ y z â€¢ rankOne ğ•œ x w := by
  ext v
  simp only [comp_apply, rankOne_apply, map_smul, smul_apply]
  rw [smul_algebra_smul_comm]

lemma rankOne_mul_rankOne_eq_inner_smul_rankOne (x y z w : V) :
    rankOne ğ•œ x y * rankOne ğ•œ z w = inner ğ•œ y z â€¢ rankOne ğ•œ x w := by
  rw [mul_def]
  exact rankOne_comp_rankOne_eq_inner_smul_rankOne x y z w

lemma isIdempotentElem_rankOne_self_of_norm_eq_one {x : V} (h : â€–xâ€– = 1) :
    IsIdempotentElem (rankOne ğ•œ x x) := by
  ext y
  rw [mul_def]
  simp [Function.comp_apply, rankOne_def, inner_smul_right, inner_self_eq_norm_sq_to_K, h]

end seminormed

section normed

variable {ğ•œ V W : Type*} [RCLike ğ•œ]

variable [NormedAddCommGroup V] [NormedAddCommGroup W]
variable [InnerProductSpace ğ•œ V] [InnerProductSpace ğ•œ W] [CompleteSpace V] [CompleteSpace W]

lemma adjoint_rankOne (x : V) (y : W) :
    (rankOne ğ•œ x y).adjoint = rankOne ğ•œ y x := by
  symm
  rw [eq_adjoint_iff]
  intro v w
  repeat rw [rankOne_apply]
  rw [inner_smul_left, inner_conj_symm, inner_smul_right]
  exact mul_comm _ _

lemma star_rankOne (x y : V) :
    star (rankOne ğ•œ x y) = rankOne ğ•œ y x := by
  rw [star_eq_adjoint, adjoint_rankOne]

lemma isSelfAdjoint_rankOne_self (x : V) :
    IsSelfAdjoint (rankOne ğ•œ x x) := by
  rw [IsSelfAdjoint, star_eq_adjoint, adjoint_rankOne]

lemma isPositive_rankOne_self (x : V) :
    (rankOne ğ•œ x x).IsPositive := by
  apply And.intro (isSelfAdjoint_rankOne_self x)
  intro y
  simp only [reApplyInnerSelf, rankOne_apply]
  rw [inner_smul_left, InnerProductSpace.conj_inner_symm, inner_mul_symm_re_eq_norm]
  exact norm_nonneg (inner ğ•œ y x * inner ğ•œ x y)

lemma isStarProjection_rankOne_self_of_norm_eq_one {x : V} (h : â€–xâ€– = 1) :
    IsStarProjection (rankOne ğ•œ x x) :=
  âŸ¨isIdempotentElem_rankOne_self_of_norm_eq_one h, isSelfAdjoint_rankOne_self xâŸ©

lemma isSelfAdjoint_rankOne_add (x y : V) :
    IsSelfAdjoint (rankOne ğ•œ x y + rankOne ğ•œ y x) := by
  rw [isSelfAdjoint_iff', map_add]
  repeat rw [adjoint_rankOne]
  abel

omit [CompleteSpace V] [CompleteSpace W]
variable {Î¹ : Type*} [Fintype Î¹]

lemma sum_rankOne_OrthonormalBasis (b : OrthonormalBasis Î¹ ğ•œ V) :
    âˆ‘i, rankOne ğ•œ (b i) (b i) = 1 := by
  ext x
  rw [â† LinearIsometryEquiv.map_eq_iff b.repr]
  simp only [sum_apply, rankOne_apply, one_apply]
  congr
  exact b.sum_repr' x

end normed

end ContinuousLinearMap
