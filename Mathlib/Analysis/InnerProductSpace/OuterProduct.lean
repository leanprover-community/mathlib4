/-
Copyright (c) 2025 IvÃ¡n Renison, Jam Khan. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IvÃ¡n Renison, Jam Khan
-/
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.Trace

/-!
This file defines the outer product of two vectors as a linear map,
and proves basic properties of the outer product.
-/

namespace ContinuousLinearMap

section seminormed

variable {ğ•œ V W : Type*} [RCLike ğ•œ]
variable [SeminormedAddCommGroup V] [InnerProductSpace ğ•œ V]
variable [SeminormedAddCommGroup W] [InnerProductSpace ğ•œ W]

variable (ğ•œ) in
/-- A rank-one operator on an inner product space is given by `x â†¦ y â†¦ z â†¦ âŸªy, zâŸ« â€¢ x`. -/
noncomputable def rankOne : V â†’L[ğ•œ] W â†’Lâ‹†[ğ•œ] W â†’L[ğ•œ] V :=
  LinearMap.mkContinuousâ‚‚
  ({toFun := fun x =>
    { toFun := fun y => (lsmul ğ•œ ğ•œ).flip x âˆ˜L innerSL ğ•œ y
      map_add' := fun _ _ => by rw [map_add, comp_add]
      map_smul' := fun _ _ => by rw [map_smulâ‚›â‚—, comp_smulâ‚›â‚—]; rfl }
    map_add' := fun _ _ => by ext; simp
    map_smul' := fun _ _ => by ext; simp })
  1 (fun x y => calc _ â‰¤ _ := opNorm_comp_le _ _
      _ â‰¤ â€–xâ€– * â€–yâ€– := mul_le_mul (opNorm_le_bound _ (norm_nonneg x)
          (by simp [norm_smul, mul_comm]))
        (innerSL_apply_norm ğ•œ y â–¸ le_refl _) (norm_nonneg _) (norm_nonneg _)
      _ = _ := by rw [one_mul])

lemma rankOne_def (x : V) (y : W) :
    rankOne ğ•œ x y = (lsmul ğ•œ ğ•œ).flip x âˆ˜L innerSL ğ•œ y :=
  rfl

lemma rankOne_def' (x : V) (y : W) :
    rankOne ğ•œ x y = (innerSL ğ•œ y).smulRight x :=
  rfl

@[simp]
lemma rankOne_apply (x : V) (y z : W) :
    rankOne ğ•œ x y z = inner ğ•œ y z â€¢ x :=
  rfl

lemma inner_left_rankOne_apply (x : V) (y z : W) (w : V) :
    inner ğ•œ (rankOne ğ•œ x y z) w = inner ğ•œ z y * inner ğ•œ x w := by
  simp [inner_smul_left, inner_conj_symm]

lemma inner_right_rankOne_apply (x y : V) (z w : W) :
    inner ğ•œ x (rankOne ğ•œ y z w) = inner ğ•œ x y * inner ğ•œ z w := by
  simp [inner_smul_right, mul_comm]

lemma rankOne_comp_rankOne (x : V) (y z : W) (w : V) :
    rankOne ğ•œ x y âˆ˜L rankOne ğ•œ z w = inner ğ•œ y z â€¢ rankOne ğ•œ x w := by
  ext v
  simp only [comp_apply, rankOne_apply, map_smul, smul_apply]
  rw [smul_algebra_smul_comm]

lemma isIdempotentElem_rankOne_self {x : V} (h : â€–xâ€– = 1) :
    IsIdempotentElem (rankOne ğ•œ x x) := by
  simp [IsIdempotentElem, mul_def, rankOne_comp_rankOne, inner_self_eq_norm_sq_to_K, h]

end seminormed

section normed

variable {ğ•œ V W : Type*} [RCLike ğ•œ]

variable [NormedAddCommGroup V] [NormedAddCommGroup W]
variable [InnerProductSpace ğ•œ V] [InnerProductSpace ğ•œ W]

section complete

variable [CompleteSpace V] [CompleteSpace W]

lemma adjoint_rankOne (x : V) (y : W) :
    (rankOne ğ•œ x y).adjoint = rankOne ğ•œ y x := by
  simp [rankOne_def, adjoint_comp, â† adjoint_innerSL_apply]

lemma isSelfAdjoint_rankOne_self (x : V) :
    IsSelfAdjoint (rankOne ğ•œ x x) :=
  adjoint_rankOne x x

lemma isPositive_rankOne_self (x : V) :
    (rankOne ğ•œ x x).IsPositive := by
  rw [rankOne_def, â† id_comp (innerSL ğ•œ x), â† adjoint_innerSL_apply]
  exact IsPositive.adjoint_conj isPositive_one _

lemma isStarProjection_rankOne_self {x : V} (h : â€–xâ€– = 1) :
    IsStarProjection (rankOne ğ•œ x x) :=
  âŸ¨isIdempotentElem_rankOne_self h, isSelfAdjoint_rankOne_self xâŸ©

lemma isSelfAdjoint_rankOne_add (x y : V) :
    IsSelfAdjoint (rankOne ğ•œ x y + rankOne ğ•œ y x) :=
  (adjoint_rankOne (ğ•œ := ğ•œ) y x) â–¸ IsSelfAdjoint.star_add_self _

omit [CompleteSpace V] in
lemma rankOne_comp (x : V) (y : W) (f : W â†’L[ğ•œ] W) :
    rankOne ğ•œ x y âˆ˜L f = rankOne ğ•œ x (adjoint f y) := by
  simp_rw [rankOne_def, comp_assoc, innerSL_apply_comp]

end complete

lemma comp_rankOne (x : V) (y : W) (f : V â†’L[ğ•œ] V) :
    f âˆ˜L rankOne ğ•œ x y = rankOne ğ•œ (f x) y := by
  simp_rw [rankOne_def, â† comp_assoc, comp_lsmul_flip_apply]

variable {Î¹ : Type*} [Fintype Î¹]

lemma sum_rankOne_OrthonormalBasis (b : OrthonormalBasis Î¹ ğ•œ V) :
    âˆ‘ i, rankOne ğ•œ (b i) (b i) = 1 := by
  ext x
  simp only [sum_apply, rankOne_apply, one_apply, b.sum_repr' x]

lemma trace_toLinearMap_rankOne (x y : V) (b : Module.Basis Î¹ ğ•œ V) :
    (rankOne ğ•œ x y).trace ğ•œ V = inner ğ•œ y x := by
  have : Module.Finite ğ•œ V := Module.Finite.of_basis b
  rw [rankOne_def, coe_comp, LinearMap.trace_comp_comm', â† coe_comp, comp_lsmul_flip_apply]
  simp [LinearMap.trace_eq_sum_inner _ ((Module.Basis.singleton Unit ğ•œ).toOrthonormalBasis
    (by simp [orthonormal_iff_ite]))]

end normed

end ContinuousLinearMap
