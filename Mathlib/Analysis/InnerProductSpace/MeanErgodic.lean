/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Dynamics.BirkhoffSum.NormedSpace

/-!
# Von Neumann Mean Ergodic Theorem in a Hilbert Space

In this file we prove the von Neumann Mean Ergodic Theorem for an operator in a Hilbert space.
It says that for a contracting linear self-map `f : E â†’â‚—[ğ•œ] E` of a Hilbert space,
the Birkhoff averages
```
birkhoffAverage ğ•œ f id N x = (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n âˆˆ Finset.range N, f^[n] x
```
converge to the orthogonal projection of `x` to the subspace of fixed points of `f`,
see `ContinuousLinearMap.tendsto_birkhoffAverage_orthogonalProjection`.
-/

open Filter Finset Function Bornology
open scoped Topology

variable {ğ•œ E : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]

/-- **Von Neumann Mean Ergodic Theorem**, a version for a normed space.

Let `f : E â†’ E` be a contracting linear self-map of a normed space.
Let `S` be the subspace of fixed points of `f`.
Let `g : E â†’ S` be a continuous linear projection, `g|_S=id`.
If the range of `f - id` is dense in the kernel of `g`,
then for each `x`, the Birkhoff averages
```
birkhoffAverage ğ•œ f id N x = (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n âˆˆ Finset.range N, f^[n] x
```
converge to `g x` as `N â†’ âˆ`.

Usually, this fact is not formulated as a separate lemma.
I chose to do it in order to isolate parts of the proof that do not rely
on the inner product space structure.
-/
theorem LinearMap.tendsto_birkhoffAverage_of_ker_subset_closure [NormedSpace ğ•œ E]
    (f : E â†’â‚—[ğ•œ] E) (hf : LipschitzWith 1 f) (g : E â†’L[ğ•œ] LinearMap.eqLocus f 1)
    (hg_proj : âˆ€ x : LinearMap.eqLocus f 1, g x = x)
    (hg_ker : (LinearMap.ker g : Set E) âŠ† closure (LinearMap.range (f - 1))) (x : E) :
    Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop (ğ“ (g x)) := by
  /- Any point can be represented as a sum of `y âˆˆ LinearMap.ker g` and a fixed point `z`. -/
  obtain âŸ¨y, hy, z, hz, rflâŸ© : âˆƒ y, g y = 0 âˆ§ âˆƒ z, IsFixedPt f z âˆ§ x = y + z :=
    âŸ¨x - g x, by simp [hg_proj], g x, (g x).2, by simpâŸ©
  /- For a fixed point, the theorem is trivial,
  so it suffices to prove it for `y âˆˆ LinearMap.ker g`. -/
  suffices Tendsto (birkhoffAverage ğ•œ f _root_.id Â· y) atTop (ğ“ 0) by
    have hgz : g z = z := congr_arg Subtype.val (hg_proj âŸ¨z, hzâŸ©)
    simpa [hy, hgz, birkhoffAverage, birkhoffSum, Finset.sum_add_distrib, smul_add]
      using this.add (hz.tendsto_birkhoffAverage ğ•œ _root_.id)
  /- By continuity, it suffices to prove the theorem on a dense subset of `LinearMap.ker g`.
  By assumption, `LinearMap.range (f - 1)` is dense in the kernel of `g`,
  so it suffices to prove the theorem for `y = f x - x`. -/
  have : IsClosed {x | Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop (ğ“ 0)} :=
    isClosed_setOf_tendsto_birkhoffAverage ğ•œ hf uniformContinuous_id continuous_const
  refine closure_minimal (Set.forall_mem_range.2 fun x â†¦ ?_) this (hg_ker hy)
  /- Finally, for `y = f x - x` the average is equal to the difference between averages
  along the orbits of `f x` and `x`, and most of the terms cancel. -/
  have : IsBounded (Set.range (_root_.id <| f^[Â·] x)) :=
    isBounded_iff_forall_norm_le.2 âŸ¨â€–xâ€–, Set.forall_mem_range.2 fun n â†¦ by
      have H : f^[n] 0 = 0 := iterate_map_zero (f : E â†’+ E) n
      simpa [H] using (hf.iterate n).dist_le_mul x 0âŸ©
  have H : âˆ€ n x y, f^[n] (x - y) = f^[n] x - f^[n] y := iterate_map_sub (f : E â†’+ E)
  simpa [birkhoffAverage, birkhoffSum, Finset.sum_sub_distrib, smul_sub, H]
    using tendsto_birkhoffAverage_apply_sub_birkhoffAverage ğ•œ this

variable [InnerProductSpace ğ•œ E] [CompleteSpace E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

/-- **Von Neumann Mean Ergodic Theorem** for an operator in a Hilbert space.
For a contracting continuous linear self-map `f : E â†’L[ğ•œ] E` of a Hilbert space, `â€–fâ€– â‰¤ 1`,
the Birkhoff averages
```
birkhoffAverage ğ•œ f id N x = (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n âˆˆ Finset.range N, f^[n] x
```
converge to the orthogonal projection of `x` to the subspace of fixed points of `f`. -/
theorem ContinuousLinearMap.tendsto_birkhoffAverage_orthogonalProjection (f : E â†’L[ğ•œ] E)
    (hf : â€–fâ€– â‰¤ 1) (x : E) :
    Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop
      (ğ“ <| orthogonalProjection (LinearMap.eqLocus f 1) x) := by
  /- Due to the previous theorem, it suffices to verify
  that the range of `f - 1` is dense in the orthogonal complement
  to the submodule of fixed points of `f`. -/
  apply (f : E â†’â‚—[ğ•œ] E).tendsto_birkhoffAverage_of_ker_subset_closure (f.lipschitz.weaken hf)
  Â· exact orthogonalProjection_mem_subspace_eq_self (K := LinearMap.eqLocus f 1)
  Â· clear x
    /- In other words, we need to verify that any vector that is orthogonal to the range of `f - 1`
    is a fixed point of `f`. -/
    rw [ker_orthogonalProjection, â† Submodule.topologicalClosure_coe, SetLike.coe_subset_coe,
      â† Submodule.orthogonal_orthogonal_eq_closure]
    /- To verify this, we verify `â€–f xâ€– â‰¤ â€–xâ€–` (because `â€–fâ€– â‰¤ 1`) and `âŸªf x, xâŸ« = â€–xâ€–Â²`. -/
    refine Submodule.orthogonal_le fun x hx â†¦ eq_of_norm_le_re_inner_eq_norm_sq (ğ•œ := ğ•œ) ?_ ?_
    Â· simpa using f.le_of_opNorm_le hf x
    Â· have : âˆ€ y, âŸªf y, xâŸ« = âŸªy, xâŸ« := by
        simpa [Submodule.mem_orthogonal, inner_sub_left, sub_eq_zero] using hx
      simp [this, â† norm_sq_eq_inner]
