/-
Copyright (c) 2023 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Dynamics.BirkhoffSum.NormedSpace

/-!
# Mean Ergodic Theorem in a Hilbert Space

In this file we prove the von Neumann Mean Ergodic Theorem for an operator in a Hilbert space.
It says that for a linear isometry `f : E â†’â‚—áµ¢[ğ•œ] E` of a Hilbert space,
the Birkhoff averages
```
birkhoffAverage ğ•œ f id N x = (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n in Finset.range N, f^[n] x
```
converge to the orthogonal projection of `x` to the subspace of fixed points of `f`.
-/

open Filter Finset Function
open scoped BigOperators Topology

variable {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E]

theorem LinearMap.tendsto_birkhoffAverage_of_ker_subset_closure [NormedSpace ğ•œ E]
    (f : E â†’â‚—[ğ•œ] E) (hf : LipschitzWith 1 f) (g : E â†’L[ğ•œ] LinearMap.eqLocus f 1)
    (hg_proj : âˆ€ x : LinearMap.eqLocus f 1, g x = x)
    (hg_ker : (LinearMap.ker g : Set E) âŠ† closure (LinearMap.range (f - 1))) (x : E) :
    Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop (ğ“ (g x)) := by
  obtain âŸ¨y, hy, z, hz, rflâŸ© : âˆƒ y, g y = 0 âˆ§ âˆƒ z, IsFixedPt f z âˆ§ x = y + z :=
    âŸ¨x - g x, by simp [hg_proj], g x, (g x).2, by simpâŸ©
  suffices : Tendsto (birkhoffAverage ğ•œ f _root_.id Â· y) atTop (ğ“ 0)
  Â· have hgz : g z = z := congr_arg Subtype.val (hg_proj âŸ¨z, hzâŸ©)
    simpa [hy, hgz, birkhoffAverage, birkhoffSum, Finset.sum_add_distrib, smul_add]
      using this.add (hz.tendsto_birkhoffAverage ğ•œ _root_.id)
  have : IsClosed {x | Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop (ğ“ 0)} :=
    isClosed_setOf_tendsto_birkhoffAverage ğ•œ hf uniformContinuous_id continuous_const
  refine closure_minimal (Set.forall_range_iff.2 fun x â†¦ ?_) this (hg_ker hy)
  have : Metric.Bounded (Set.range (_root_.id <| f^[Â·] x)) :=
    bounded_iff_forall_norm_le.2 âŸ¨â€–xâ€–, Set.forall_range_iff.2 fun n â†¦ by
      have H : f^[n] 0 = 0 := (f : E â†’+ E).iterate_map_zero n
      simpa [H] using (hf.iterate n).dist_le_mul x 0âŸ©
  have H : âˆ€ n x y, f^[n] (x - y) = f^[n] x - f^[n] y := (f : E â†’+ E).iterate_map_sub
  simpa [birkhoffAverage, birkhoffSum, Finset.sum_sub_distrib, smul_sub, H]
    using tendsto_birkhoffAverage_apply_sub_birkhoffAverage ğ•œ this

variable  [InnerProductSpace ğ•œ E] [CompleteSpace E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

theorem LinearIsometry.tendsto_birkhoffAverage_orthogonalProjection (f : E â†’â‚—áµ¢[ğ•œ] E) (x : E) :
    Tendsto (birkhoffAverage ğ•œ f _root_.id Â· x) atTop
      (ğ“ <| orthogonalProjection (LinearMap.eqLocus f 1) x) := by
  apply (f : E â†’â‚—[ğ•œ] E).tendsto_birkhoffAverage_of_ker_subset_closure f.lipschitz
  Â· exact orthogonalProjection_mem_subspace_eq_self (K := LinearMap.eqLocus f 1)
  Â· clear x
    rw [ker_orthogonalProjection, â† Submodule.topologicalClosure_coe, SetLike.coe_subset_coe,
      â† Submodule.orthogonal_orthogonal_eq_closure]
    refine Submodule.orthogonal_le fun x hx â†¦ ?_
    replace hx : âˆ€ y, âŸªf y, xâŸ« = âŸªy, xâŸ« := by
      simpa [Submodule.mem_orthogonal, inner_sub_left, sub_eq_zero] using hx
    suffices âŸªf x - x, f x - xâŸ« = 0 by simpa [sub_eq_zero] using this
    rw [inner_sub_right, inner_sub_left, inner_sub_left, f.inner_map_map, hx,
      â† inner_conj_symm x (f x), hx, inner_self_conj, sub_self]
