import Mathlib.Analysis.InnerProductSpace.Projection

open Filter Finset Function
open scoped BigOperators Topology

variable {ğ•œ E : Type _} [IsROrC ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
  [CompleteSpace E]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

theorem LinearIsometry.tendsto_inv_smul_sum_range_pow_apply_orthogonalProjection
    (f : E â†’â‚—áµ¢[ğ•œ] E) (x : E) :
    Tendsto (fun N : â„• â†¦ (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n in range N, (f ^ n) x) atTop
      (ğ“ <| orthogonalProjection (LinearMap.eqLocus f 1) x) := by
  set S := LinearMap.eqLocus f 1
  set P := orthogonalProjection S
  set g := f.toContinuousLinearMap
  set avg := fun N : â„• â†¦ (N : ğ•œ)â»Â¹ â€¢ âˆ‘ n in range N, g ^ n
  have havg_norm : âˆ€ N x, â€–avg N xâ€– â‰¤ â€–xâ€– := fun N x â†¦
    calc
      â€–avg N xâ€– = â€–âˆ‘ n in range N, (f^n) xâ€– / N := by simp [norm_smul, div_eq_inv_mul]
      _ â‰¤ (âˆ‘ n in range N, â€–(f ^ n) xâ€–) / N := by gcongr; apply norm_sum_le
      _ = (N / N) * â€–xâ€– := by simp only [norm_map]; simp [mul_div_right_comm]
      _ â‰¤ â€–xâ€– := mul_le_of_le_one_left (norm_nonneg _) (div_self_le_one _)
  suffices : Tendsto (avg Â· x) atTop (ğ“ (P x))
  Â· simpa using this
  have havgS : âˆ€ (y : S) {N : â„•}, N â‰  0 â†’ avg N y = y := fun y N hN â†¦
    calc
      avg N y = (N : ğ•œ)â»Â¹ â€¢ (N : ğ•œ) â€¢ y := by simp [iterate_fixed y.2, â† nsmul_eq_smul_cast]
      _ = y := inv_smul_smulâ‚€ (Nat.cast_ne_zero.2 hN) _
  suffices : Tendsto (avg Â· (x - P x)) atTop (ğ“ 0)
  Â· refine tendsto_sub_nhds_zero_iff.1 (this.congr' <| (eventually_ne_atTop 0).mono fun N hN â†¦ ?_)
    simp only [map_sub, havgS _ hN]
  -- TODO: move to a separate lemma; what's the right generality?
  have Hâ‚ : (LinearMap.range (1 - g))á—® = S
  Â· ext x
    suffices : (âˆ€ (a : E), âŸªa, xâŸ« = âŸªf a, xâŸ«) â†” f x = x
    Â· simpa [Submodule.mem_orthogonal, inner_sub_left, sub_eq_zero]
    refine âŸ¨fun h â†¦ ?_, fun h a â†¦ ?_âŸ©
    Â· rw [â† sub_eq_zero, â† inner_self_eq_zero (ğ•œ := ğ•œ), inner_sub_right,
        inner_sub_left, inner_sub_left, f.inner_map_map, â† h, â† inner_conj_symm x (f x), â† h,
        inner_self_conj, sub_self]
    Â· rw [â† f.inner_map_map, h]
  have Hâ‚‚ : Sá—® = (LinearMap.range (1 - g)).topologicalClosure
  Â· rw [â† Hâ‚, Submodule.orthogonal_orthogonal_eq_closure]
  have Hâ‚ƒ : x - P x âˆˆ closure (LinearMap.range (1 - g))
  Â· rw [â† Submodule.topologicalClosure_coe, â† Hâ‚‚]
    apply sub_orthogonalProjection_mem_orthogonal
  have Hâ‚„ : âˆ€ y, Tendsto (â€–avg Â· (y - f y)â€–) atTop (ğ“ 0) := fun y â†¦ by
    have : âˆ€ N, avg N (y - f y) = (N : ğ•œ) â»Â¹ â€¢ ((f ^ (0 : â„•)) y - (f ^ N) y) := fun N â†¦ by
      rw [â† sum_range_sub' (fun n : â„• â†¦ (f ^ n) y) N]
      simp [pow_succ', â† smul_sub]
    have : âˆ€ N : â„•, â€–avg N (y - f y)â€– â‰¤ (N : â„)â»Â¹ * (â€–yâ€– + â€–yâ€–)
    Â· intro N
      rw [this, norm_smul, norm_inv, IsROrC.norm_natCast]
      gcongr
      exact norm_sub_le_of_le (norm_map _ _).le (norm_map _ _).le
    refine squeeze_zero (fun _ â†¦ norm_nonneg _) this ?_
    rw [â† zero_mul (â€–yâ€– + â€–yâ€–)]
    refine Tendsto.mul ?_ tendsto_const_nhds
    exact tendsto_inv_atTop_zero.comp tendsto_nat_cast_atTop_atTop
  refine NormedAddCommGroup.tendsto_nhds_zero.2 fun Îµ Îµpos â†¦ ?_
  rcases SeminormedAddCommGroup.mem_closure_iff.1 Hâ‚ƒ _ (half_pos Îµpos) with âŸ¨_, âŸ¨y, rflâŸ©, hyâŸ©
  refine ((Hâ‚„ y).eventually (gt_mem_nhds <| half_pos Îµpos)).mono fun N hN â†¦ ?_
  calc
    â€–avg N (x - P x)â€– = â€–avg N (x - P x - (y - f y)) + avg N (y - f y)â€– := by
      rw [map_sub _ (x - P x), sub_add_cancel]
    _ â‰¤ â€–avg N (x - P x - (y - f y))â€– + â€–avg N (y - f y)â€– := norm_add_le _ _
    _ â‰¤ â€–x - P x - (y - f y)â€– + â€–avg N (y - f y)â€– :=
      add_le_add_right (havg_norm _ _) _
    _ < Îµ / 2 + Îµ / 2 := add_lt_add hy hN
    _ = Îµ := add_halves _
