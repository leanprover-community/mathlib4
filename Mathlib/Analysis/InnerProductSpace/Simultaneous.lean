/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jack Cheverton, Samyak Dhar Tuladhar
-/

import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Order.CompleteLattice

/-! # Simultaneous eigenspaces of a commuting pair of symmetric operators

This file collects various decomposition results for simultaneous eigenspaces of a commuting pair
of symmetric operators on a finite-dimensional inner product space.

# Main Result

* `DirectSum.IsInternal.eigenspaces_of_commuting_symmetric_pair` establishes that
   if `{A B : E â†’â‚—[ğ•œ] E}`, then `IsSymmetric A`, `IsSymmetric B` and `A âˆ˜â‚— B = B âˆ˜â‚— A` imply that
   `E` decomposes as an internal direct sum of the pairwise orthogonal spaces
   `eigenspace B Î¼ âŠ“ eigenspace A Î½`

## TODO

Develop a `Diagonalization` structure for linear maps and / or matrices which consists of a basis,
and a proof obligation that the basis vectors are eigenvectors.

## Tags

self-adjoint operator, simultaneous eigenspaces, simultaneous diagonalization

-/

variable {ğ•œ : Type*} [RCLike ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]

open Module.End

namespace LinearMap

namespace IsSymmetric

section Pair

variable {Î± : ğ•œ} {A B : E â†’â‚—[ğ•œ] E} (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : A âˆ˜â‚— B = B âˆ˜â‚— A)
/--If a pair of operators commute, then the eigenspaces of one are invariant under the other.-/
theorem eigenspace_invariant_of_commute (Î± : ğ•œ) : âˆ€ v âˆˆ (eigenspace A Î±), (B v âˆˆ eigenspace A Î±)
    := by
  intro v hv
  rw [eigenspace, mem_ker, sub_apply, Module.algebraMap_end_apply, â† comp_apply A B v, hAB,
  comp_apply B A v, â† map_smul, â† map_sub, hv, map_zero] at *

/--The inf of an eigenspace of an operator with another invariant subspace
agrees with the corresponding eigenspace of the restriction of that operator to the
invariant subspace-/
theorem invariant_subspace_inf_eigenspace_eq_restrict {F : Submodule ğ•œ E} (S : E â†’â‚—[ğ•œ] E)
    (Î¼ : ğ•œ) (hInv : âˆ€ v âˆˆ F, S v âˆˆ F) : (eigenspace S Î¼) âŠ“ F =
    Submodule.map (Submodule.subtype F)
    (eigenspace (S.restrict (hInv)) Î¼) := by
  ext v
  constructor
  Â· intro h
    simp only [Submodule.mem_map, Submodule.coeSubtype, Subtype.exists, exists_and_right,
      exists_eq_right, mem_eigenspace_iff]; use h.2
    exact Eq.symm (SetCoe.ext (_root_.id (Eq.symm (mem_eigenspace_iff.mp h.1))))
  Â· intro h
    simp only [Submodule.mem_inf]
    constructor
    Â· simp only [Submodule.mem_map, Submodule.coeSubtype, Subtype.exists, exists_and_right,
      exists_eq_right, mem_eigenspace_iff, SetLike.mk_smul_mk, restrict_apply,
      Subtype.mk.injEq] at h
      obtain âŸ¨_, hyâŸ© := h
      simpa [mem_eigenspace_iff]
    Â· simp only [Submodule.coeSubtype] at h
      obtain âŸ¨_, hyâŸ© := h
      simp only [â† hy.2, Submodule.coeSubtype, SetLike.coe_mem]

/--Auxiliary: function version of `invariant_subspace_inf_eigenspace_eq_restrict` -/
theorem invariant_subspace_inf_eigenspace_eq_restrict' : (fun (Î³ : ğ•œ) â†¦
    Submodule.map (Submodule.subtype (eigenspace A Î±)) (eigenspace (B.restrict
    (eigenspace_invariant_of_commute hAB Î±)) Î³))
    = (fun (Î³ : ğ•œ) â†¦ (eigenspace B Î³ âŠ“ eigenspace A Î±)) := by
  funext Î³
  exact Eq.symm (invariant_subspace_inf_eigenspace_eq_restrict B Î³
    (eigenspace_invariant_of_commute hAB Î±))

/-- If A and B are commuting symmetric operators then the eigenspaces of the restriction
of B to any eigenspace of A exhaust that eigenspace.-/
theorem iSup_inf_eq_top: (â¨† Î³ , (eigenspace (LinearMap.restrict B
    (eigenspace_invariant_of_commute hAB Î±)) Î³)) = âŠ¤ := by
    rw [â† Submodule.orthogonal_eq_bot_iff]
    exact orthogonalComplement_iSup_eigenspaces_eq_bot (LinearMap.IsSymmetric.restrict_invariant hB
    (eigenspace_invariant_of_commute hAB Î±))

/--If A and B are commuting symmetric operators acting on a Hilbert Space, then the
simultaneous eigenspaces of A and B exhaust the Hilbert Space. -/
theorem iSup_simultaneous_eigenspaces_eq_top :
    (â¨† (Î± : ğ•œ), (â¨† (Î³ : ğ•œ), (eigenspace B Î³ âŠ“ eigenspace A Î±))) = âŠ¤ := by
  have : (fun (Î± : ğ•œ) â†¦  eigenspace A Î±)  = fun (Î± : ğ•œ) â†¦
      (â¨† (Î³ : ğ•œ), (eigenspace B Î³ âŠ“ eigenspace A Î±)) := by
    funext Î³
    rw [â† invariant_subspace_inf_eigenspace_eq_restrict' hAB, â† Submodule.map_iSup,
      iSup_inf_eq_top hB hAB, Submodule.map_top, Submodule.range_subtype]
  rw [â† Submodule.orthogonal_eq_bot_iff.mp (hA.orthogonalComplement_iSup_eigenspaces_eq_bot), this]

/--The simultaneous eigenspaces of a pair of commuting symmetric operators form an
`OrthogonalFamily`.-/
theorem orthogonality_of_simultaneous_eigenspaces_of_commuting_symmetric_pair :
    OrthogonalFamily ğ•œ (fun (i : ğ•œ Ã— ğ•œ) => (eigenspace B i.1 âŠ“ eigenspace A i.2 : Submodule ğ•œ E))
    (fun i => (eigenspace B i.1 âŠ“ eigenspace A i.2).subtypeâ‚—áµ¢) := by
  refine orthogonalFamily_iff_pairwise.mpr ?_
  intro i j hij v âŸ¨hv1 , hv2âŸ©
  have H:=  (Iff.not (Iff.symm Prod.ext_iff)).mpr hij
  push_neg at H
  by_cases C: i.1 = j.1
  <;> intro w âŸ¨hw1, hw2âŸ©
  Â· exact orthogonalFamily_iff_pairwise.mp hA.orthogonalFamily_eigenspaces (H C) hv2 w hw2
  Â· exact orthogonalFamily_iff_pairwise.mp hB.orthogonalFamily_eigenspaces C hv1 w hw1

/-- Given a commuting pair of symmetric linear operators, the Hilbert space on which they act
decomposes as an internal direct sum of simultaneous eigenspaces. -/
theorem DirectSum.IsInternal_of_simultaneous_eigenspaces_of_commuting_symmetric_pair:
    DirectSum.IsInternal (fun (i : ğ•œ Ã— ğ•œ) â†¦ (eigenspace B i.1 âŠ“ eigenspace A i.2)):= by
  apply (OrthogonalFamily.isInternal_iff
    (orthogonality_of_simultaneous_eigenspaces_of_commuting_symmetric_pair hA hB)).mpr
  rw [Submodule.orthogonal_eq_bot_iff, iSup_prod, iSup_comm]
  exact iSup_simultaneous_eigenspaces_eq_top hA hB hAB

end Pair

end IsSymmetric

end LinearMap
