/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jack Cheverton, Samyak Dhar Tuladhar
-/

import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Order.CompleteLattice
import Mathlib.LinearAlgebra.Eigenspace.Basic

/-! # Simultaneous eigenspaces of a commuting pair of symmetric operators

This file collects various decomposition results for simultaneous eigenspaces of a commuting pair
of symmetric operators on a finite-dimensional inner product space.

# Main Result

* `DirectSum.IsInternal.eigenspaces_of_commuting_symmetric_pair` establishes that
   if `{A B : E â†’â‚—[ğ•œ] E}`, then `IsSymmetric A`, `IsSymmetric B` and `A âˆ˜â‚— B = B âˆ˜â‚— A` imply that
   `E` decomposes as an internal direct sum of the pairwise orthogonal spaces
   `eigenspace B Î¼ âŠ“ eigenspace A Î½`

## TODO

Develop a `Diagonalization` structure for linear maps and / or matrices which consists of a basis,
and a proof obligation that the basis vectors are eigenvectors.

## Tags

self-adjoint operator, simultaneous eigenspaces, simultaneous diagonalization

-/

variable {ğ•œ E : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]

open Module.End

namespace LinearMap

namespace IsSymmetric

section Pair

variable {Î± : ğ•œ} {A B : E â†’â‚—[ğ•œ] E} (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : A âˆ˜â‚— B = B âˆ˜â‚— A)

/--If a pair of operators commute, then the eigenspaces of one are invariant under the other.-/
theorem eigenspace_invariant_of_commute (Î± : ğ•œ) :
    âˆ€ v âˆˆ (eigenspace A Î±), (B v âˆˆ eigenspace A Î±) := by
  intro v hv
  rw [eigenspace, mem_ker, sub_apply, Module.algebraMap_end_apply, â† comp_apply A B v, hAB,
    comp_apply B A v, â† map_smul, â† map_sub, hv, map_zero] at *

open Submodule in

/-- The intersection of eigenspaces of commuting selfadjoint operators is equal to the eigenspace of
one operator restricted to the eigenspace of the other, which is an invariant subspace because the
operators commute. -/
theorem eigenspace_inf_eigenspace (Î³ : ğ•œ) :
    eigenspace A Î± âŠ“ eigenspace B Î³ = map (Submodule.subtype (eigenspace A Î±))
      (eigenspace (B.restrict (eigenspace_invariant_of_commute hAB Î±)) Î³) :=
  (eigenspace A Î±).inf_genEigenspace _ _ (k := 1)

/-- If A and B are commuting symmetric operators on a finite dimensional inner product space
then the eigenspaces of the restriction of B to any eigenspace of A exhaust that eigenspace.-/
theorem iSup_eigenspace_inf_eigenspace :
    (â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = eigenspace A Î± := by
  conv_rhs => rw [â† (eigenspace A Î±).map_subtype_top]
  simp only [eigenspace_inf_eigenspace hAB, â† Submodule.map_iSup]
  congr 1
  rw [â† Submodule.orthogonal_eq_bot_iff]
  exact orthogonalComplement_iSup_eigenspaces_eq_bot <|
    hB.restrict_invariant <| eigenspace_invariant_of_commute hAB Î±

/-- If A and B are commuting symmetric operators acting on a finite dimensional inner product space,
then the simultaneous eigenspaces of A and B exhaust the space. -/
theorem iSup_iSup_eigenspace_inf_eigenspace_eq_top :
    (â¨† Î±, â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = âŠ¤ := by
  simpa [iSup_eigenspace_inf_eigenspace hB hAB] using
    Submodule.orthogonal_eq_bot_iff.mp <| hA.orthogonalComplement_iSup_eigenspaces_eq_bot

/--The simultaneous eigenspaces of a pair of commuting symmetric operators form an
`OrthogonalFamily`.-/
theorem orthogonalFamily_eigenspace_inf_eigenspace :
    OrthogonalFamily ğ•œ (fun (i : ğ•œ Ã— ğ•œ) => (eigenspace A i.2 âŠ“ eigenspace B i.1 : Submodule ğ•œ E))
    (fun i => (eigenspace A i.2 âŠ“ eigenspace B i.1).subtypeâ‚—áµ¢) :=
     OrthogonalFamily.of_pairwise fun i j hij v âŸ¨hv1 , hv2âŸ© â†¦ by
    obtain (hâ‚ | hâ‚‚) : i.1 â‰  j.1 âˆ¨ i.2 â‰  j.2 := by rwa [Ne.eq_def, Prod.ext_iff, not_and_or] at hij
    all_goals intro w âŸ¨hw1, hw2âŸ©
    Â· exact hB.orthogonalFamily_eigenspaces.pairwise hâ‚ hv2 w hw2
    Â· exact hA.orthogonalFamily_eigenspaces.pairwise hâ‚‚ hv1 w hw1

/-- Given a commuting pair of symmetric linear operators on a finite dimensional inner product
space, the space decomposes as an internal direct sum of simultaneous eigenspaces of these
operators. -/
theorem directSum_isInteral_of_commute :
    DirectSum.IsInternal (fun (i : ğ•œ Ã— ğ•œ) â†¦ (eigenspace A i.2 âŠ“ eigenspace B i.1)):= by
  apply (orthogonalFamily_eigenspace_inf_eigenspace hA hB).isInternal_iff.mpr
  rw [Submodule.orthogonal_eq_bot_iff, iSup_prod, iSup_comm]
  exact iSup_iSup_eigenspace_inf_eigenspace_eq_top hA hB hAB

end Pair

end IsSymmetric

end LinearMap
