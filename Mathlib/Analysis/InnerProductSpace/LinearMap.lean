/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
module

public import Mathlib.Analysis.InnerProductSpace.Continuous
public import Mathlib.Analysis.Normed.Module.Normalize

/-!
# Linear maps on inner product spaces

This file studies linear maps on inner product spaces.

## Main results

- We define `innerSL` as the inner product bundled as a continuous sesquilinear map
- We prove a general polarization identity for linear maps (`inner_map_polarization`)
- We show that a linear map preserving the inner product is an isometry
  (`LinearMap.isometryOfInner`) and conversely an isometry preserves the inner product
  (`LinearIsometry.inner_map_map`).

## Tags

inner product space, Hilbert space, norm

-/

@[expose] public section

noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp

open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

section Norm_Seminormed

open scoped InnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [SeminormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

section Complex_Seminormed

variable {V : Type*} [SeminormedAddCommGroup V] [InnerProductSpace â„‚ V]

/-- A complex polarization identity, with a linear map. -/
theorem inner_map_polarization (T : V â†’â‚—[â„‚] V) (x y : V) :
    âŸªT y, xâŸ«_â„‚ =
      (âŸªT (x + y), x + yâŸ«_â„‚ - âŸªT (x - y), x - yâŸ«_â„‚ +
            Complex.I * âŸªT (x + Complex.I â€¢ y), x + Complex.I â€¢ yâŸ«_â„‚ -
          Complex.I * âŸªT (x - Complex.I â€¢ y), x - Complex.I â€¢ yâŸ«_â„‚) /
        4 := by
  simp only [map_add, map_sub, inner_add_left, inner_add_right, map_smul, inner_smul_left,
    inner_smul_right, Complex.conj_I, â† pow_two, Complex.I_sq, inner_sub_left, inner_sub_right,
    mul_add, â† mul_assoc, mul_neg, neg_neg, one_mul, neg_one_mul, mul_sub, sub_sub]
  ring

theorem inner_map_polarization' (T : V â†’â‚—[â„‚] V) (x y : V) :
    âŸªT x, yâŸ«_â„‚ =
      (âŸªT (x + y), x + yâŸ«_â„‚ - âŸªT (x - y), x - yâŸ«_â„‚ -
            Complex.I * âŸªT (x + Complex.I â€¢ y), x + Complex.I â€¢ yâŸ«_â„‚ +
          Complex.I * âŸªT (x - Complex.I â€¢ y), x - Complex.I â€¢ yâŸ«_â„‚) /
        4 := by
  simp only [map_add, map_sub, inner_add_left, inner_add_right, map_smul, inner_smul_left,
    inner_smul_right, Complex.conj_I, â† pow_two, Complex.I_sq, inner_sub_left, inner_sub_right,
    mul_add, â† mul_assoc, mul_neg, neg_neg, one_mul, neg_one_mul, mul_sub, sub_sub]
  ring

end Complex_Seminormed

section Complex

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„‚ V]

/-- A linear map `T` is zero, if and only if the identity `âŸªT x, xâŸ«_â„‚ = 0` holds for all `x`.
-/
theorem inner_map_self_eq_zero (T : V â†’â‚—[â„‚] V) : (âˆ€ x : V, âŸªT x, xâŸ«_â„‚ = 0) â†” T = 0 := by
  constructor
  Â· intro hT
    ext x
    rw [LinearMap.zero_apply, â† @inner_self_eq_zero â„‚ V, inner_map_polarization]
    simp only [hT]
    simp
  Â· rintro rfl x
    simp only [LinearMap.zero_apply, inner_zero_left]

/--
Two linear maps `S` and `T` are equal, if and only if the identity `âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚` holds
for all `x`.
-/
theorem ext_inner_map (S T : V â†’â‚—[â„‚] V) : (âˆ€ x : V, âŸªS x, xâŸ«_â„‚ = âŸªT x, xâŸ«_â„‚) â†” S = T := by
  rw [â† sub_eq_zero, â† inner_map_self_eq_zero]
  refine forall_congr' fun x => ?_
  rw [LinearMap.sub_apply, inner_sub_left, sub_eq_zero]

end Complex

section

variable {Î¹ : Type*} {Î¹' : Type*} {Î¹'' : Type*}
variable {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']
variable {E'' : Type*} [SeminormedAddCommGroup E''] [InnerProductSpace ğ•œ E'']

/-- A linear isometry preserves the inner product. -/
@[simp]
theorem LinearIsometry.inner_map_map (f : E â†’â‚—áµ¢[ğ•œ] E') (x y : E) : âŸªf x, f yâŸ« = âŸªx, yâŸ« := by
  simp [inner_eq_sum_norm_sq_div_four, â† f.norm_map]

/-- A linear isometric equivalence preserves the inner product. -/
@[simp]
theorem LinearIsometryEquiv.inner_map_map (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (x y : E) : âŸªf x, f yâŸ« = âŸªx, yâŸ« :=
  f.toLinearIsometry.inner_map_map x y

/-- The adjoint of a linear isometric equivalence is its inverse. -/
theorem LinearIsometryEquiv.inner_map_eq_flip (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (x : E) (y : E') :
    âŸªf x, yâŸ«_ğ•œ = âŸªx, f.symm yâŸ«_ğ•œ := by
  conv_lhs => rw [â† f.apply_symm_apply y, f.inner_map_map]

/-- A linear map that preserves the inner product is a linear isometry. -/
def LinearMap.isometryOfInner (f : E â†’â‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) : E â†’â‚—áµ¢[ğ•œ] E' :=
  âŸ¨f, fun x => by simp only [@norm_eq_sqrt_re_inner ğ•œ, h]âŸ©

@[simp]
theorem LinearMap.coe_isometryOfInner (f : E â†’â‚—[ğ•œ] E') (h) : â‡‘(f.isometryOfInner h) = f :=
  rfl

@[simp]
theorem LinearMap.isometryOfInner_toLinearMap (f : E â†’â‚—[ğ•œ] E') (h) :
    (f.isometryOfInner h).toLinearMap = f :=
  rfl

/-- A linear equivalence that preserves the inner product is a linear isometric equivalence. -/
def LinearEquiv.isometryOfInner (f : E â‰ƒâ‚—[ğ•œ] E') (h : âˆ€ x y, âŸªf x, f yâŸ« = âŸªx, yâŸ«) : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
  âŸ¨f, ((f : E â†’â‚—[ğ•œ] E').isometryOfInner h).norm_mapâŸ©

@[simp]
theorem LinearEquiv.coe_isometryOfInner (f : E â‰ƒâ‚—[ğ•œ] E') (h) : â‡‘(f.isometryOfInner h) = f :=
  rfl

@[simp]
theorem LinearEquiv.isometryOfInner_toLinearEquiv (f : E â‰ƒâ‚—[ğ•œ] E') (h) :
    (f.isometryOfInner h).toLinearEquiv = f :=
  rfl

/-- A linear map is an isometry if and it preserves the inner product. -/
theorem LinearMap.norm_map_iff_inner_map_map {F : Type*} [FunLike F E E'] [LinearMapClass F ğ•œ E E']
    (f : F) : (âˆ€ x, â€–f xâ€– = â€–xâ€–) â†” (âˆ€ x y, âŸªf x, f yâŸ«_ğ•œ = âŸªx, yâŸ«_ğ•œ) :=
  âŸ¨({ toLinearMap := LinearMapClass.linearMap f, norm_map' := Â· : E â†’â‚—áµ¢[ğ•œ] E' }.inner_map_map),
    (LinearMapClass.linearMap f |>.isometryOfInner Â· |>.norm_map)âŸ©

end

variable (ğ•œ)

/-- The inner product as a continuous sesquilinear map. Note that `toDualMap` (resp. `toDual`)
in `InnerProductSpace.Dual` is a version of this given as a linear isometry (resp. linear
isometric equivalence). -/
def innerSL : E â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
  LinearMap.mkContinuousâ‚‚ (innerâ‚›â‚— ğ•œ) 1 fun x y => by
    simp only [norm_inner_le_norm, one_mul, innerâ‚›â‚—_apply_apply]

@[simp]
theorem coe_innerSL_apply (v : E) : â‡‘(innerSL ğ•œ v) = fun w => âŸªv, wâŸ« :=
  rfl

theorem innerSL_apply_apply (v w : E) : innerSL ğ•œ v w = âŸªv, wâŸ« :=
  rfl

@[simp] theorem ContinuousLinearMap.toLinearMap_innerSL_apply (v : E) :
    (innerSL ğ•œ v).toLinearMap = innerâ‚›â‚— ğ•œ v := rfl

/-- The inner product as a continuous sesquilinear map, with the two arguments flipped. -/
def innerSLFlip : E â†’L[ğ•œ] E â†’Lâ‹†[ğ•œ] ğ•œ :=
  @ContinuousLinearMap.flipâ‚—áµ¢' ğ•œ ğ•œ ğ•œ E E ğ•œ _ _ _ _ _ _ _ _ _ (RingHom.id ğ•œ) (starRingEnd ğ•œ) _ _
    (innerSL ğ•œ)

@[simp]
theorem innerSLFlip_apply_apply (x y : E) : innerSLFlip ğ•œ x y = âŸªy, xâŸ« :=
  rfl

variable (F) in
@[simp] lemma flip_innerSL_real : (innerSL â„ (E := F)).flip = innerSL â„ (E := F) := by
  ext v w
  exact real_inner_comm _ _

@[deprecated (since := "2025-11-15")] alias innerâ‚›â‚—_apply_coe := coe_innerâ‚›â‚—_apply
@[deprecated (since := "2025-11-15")] alias innerâ‚›â‚—_apply := innerâ‚›â‚—_apply_apply
@[deprecated (since := "2025-11-15")] alias innerâ‚—_apply := innerâ‚—_apply_apply
@[deprecated (since := "2025-11-15")] alias innerSL_apply_coe := coe_innerSL_apply
@[deprecated (since := "2025-11-15")] alias innerSL_apply := innerSL_apply_apply
@[deprecated (since := "2025-11-15")] alias innerSLFlip_apply := innerSLFlip_apply_apply
@[deprecated (since := "2025-11-15")] alias innerSL_real_flip := flip_innerSL_real

variable {ğ•œ}

namespace ContinuousLinearMap

variable {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']

-- Note: odd and expensive build behavior is explicitly turned off using `noncomputable`
/-- Given `f : E â†’L[ğ•œ] E'`, construct the continuous sesquilinear form `fun x y â†¦ âŸªx, A yâŸ«`, given
as a continuous linear map. -/
noncomputable def toSesqForm : (E â†’L[ğ•œ] E') â†’L[ğ•œ] E' â†’Lâ‹†[ğ•œ] E â†’L[ğ•œ] ğ•œ :=
  (ContinuousLinearMap.flipâ‚—áµ¢' E E' ğ•œ (starRingEnd ğ•œ) (RingHom.id ğ•œ)).toContinuousLinearEquiv âˆ˜L
    ContinuousLinearMap.compSL E E' (E' â†’Lâ‹†[ğ•œ] ğ•œ) (RingHom.id ğ•œ) (RingHom.id ğ•œ) (innerSLFlip ğ•œ)

@[simp]
theorem toSesqForm_apply_coe (f : E â†’L[ğ•œ] E') (x : E') : toSesqForm f x = (innerSL ğ•œ x).comp f :=
  rfl

theorem toSesqForm_apply_norm_le {f : E â†’L[ğ•œ] E'} {v : E'} : â€–toSesqForm f vâ€– â‰¤ â€–fâ€– * â€–vâ€– := by
  refine opNorm_le_bound _ (by positivity) fun x â†¦ ?_
  have hâ‚ : â€–f xâ€– â‰¤ â€–fâ€– * â€–xâ€– := le_opNorm _ _
  have hâ‚‚ := @norm_inner_le_norm ğ•œ E' _ _ _ v (f x)
  calc
    â€–âŸªv, f xâŸ«â€– â‰¤ â€–vâ€– * â€–f xâ€– := hâ‚‚
    _ â‰¤ â€–vâ€– * (â€–fâ€– * â€–xâ€–) := mul_le_mul_of_nonneg_left hâ‚ (norm_nonneg v)
    _ = â€–fâ€– * â€–vâ€– * â€–xâ€– := by ring

end ContinuousLinearMap

variable (ğ•œ)

/-- `innerSL` is an isometry. Note that the associated `LinearIsometry` is defined in
`InnerProductSpace.Dual` as `toDualMap`. -/
@[simp]
theorem innerSL_apply_norm (x : E) : â€–innerSL ğ•œ xâ€– = â€–xâ€– := by
  refine
    le_antisymm ((innerSL ğ•œ x).opNorm_le_bound (norm_nonneg _) fun y => norm_inner_le_norm _ _) ?_
  rcases (norm_nonneg x).eq_or_lt' with (h | h)
  Â· simp [h]
  Â· refine (mul_le_mul_iff_leftâ‚€ h).mp ?_
    calc
      â€–xâ€– * â€–xâ€– = â€–(âŸªx, xâŸ« : ğ•œ)â€– := by
        rw [â† sq, inner_self_eq_norm_sq_to_K, norm_pow, norm_ofReal, abs_norm]
      _ â‰¤ â€–innerSL ğ•œ xâ€– * â€–xâ€– := (innerSL ğ•œ x).le_opNorm _

lemma norm_innerSL_le : â€–innerSL ğ•œ (E := E)â€– â‰¤ 1 :=
  ContinuousLinearMap.opNorm_le_bound _ zero_le_one (by simp)

end Norm_Seminormed

section RCLikeToReal

open scoped InnerProductSpace

variable {G : Type*}

/-- The inner product on an inner product space of dimension 2 can be evaluated in terms
of a complex-number representation of the space. -/
theorem inner_map_complex [SeminormedAddCommGroup G] [InnerProductSpace â„ G] (f : G â‰ƒâ‚—áµ¢[â„] â„‚)
    (x y : G) : âŸªx, yâŸ«_â„ = (f y * conj (f x)).re := by rw [â† Complex.inner, f.inner_map_map]

end RCLikeToReal

section ReApplyInnerSelf

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-- Extract a real bilinear form from an operator `T`,
by taking the pairing `fun x â†¦ re âŸªT x, xâŸ«`. -/
def ContinuousLinearMap.reApplyInnerSelf (T : E â†’L[ğ•œ] E) (x : E) : â„ :=
  re âŸªT x, xâŸ«

theorem ContinuousLinearMap.reApplyInnerSelf_apply (T : E â†’L[ğ•œ] E) (x : E) :
    T.reApplyInnerSelf x = re âŸªT x, xâŸ« :=
  rfl

end ReApplyInnerSelf

section ReApplyInnerSelf_Seminormed

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

theorem ContinuousLinearMap.reApplyInnerSelf_continuous (T : E â†’L[ğ•œ] E) :
    Continuous T.reApplyInnerSelf :=
  reCLM.continuous.comp <| T.continuous.inner continuous_id

theorem ContinuousLinearMap.reApplyInnerSelf_smul (T : E â†’L[ğ•œ] E) (x : E) {c : ğ•œ} :
    T.reApplyInnerSelf (c â€¢ x) = â€–câ€– ^ 2 * T.reApplyInnerSelf x := by
  simp only [map_smul, ContinuousLinearMap.reApplyInnerSelf_apply, inner_smul_left,
    inner_smul_right, â† mul_assoc, mul_conj, â† ofReal_pow, â† smul_re,
    Algebra.smul_def (â€–câ€– ^ 2) âŸªT x, xâŸ«, algebraMap_eq_ofReal]

end ReApplyInnerSelf_Seminormed

namespace InnerProductSpace
variable {ğ•œ E F G : Type*} [RCLike ğ•œ] [SeminormedAddCommGroup E] [NormedSpace ğ•œ E]
  [SeminormedAddCommGroup F] [InnerProductSpace ğ•œ F]
  [SeminormedAddCommGroup G] [InnerProductSpace ğ•œ G]

open ContinuousLinearMap

variable (ğ•œ) in
/-- A rank-one operator on an inner product space is given by `x â†¦ y â†¦ z â†¦ âŸªy, zâŸ« â€¢ x`.

This is also sometimes referred to as an outer product of vectors on a Hilbert space.
This corresponds to the matrix outer product `Matrix.vecMulVec`, see
`InnerProductSpace.toMatrix_rankOne` and `InnerProductSpace.symm_toEuclideanLin_rankOne` in
`Mathlib/Analysis/InnerProductSpace/PiL2.lean`. -/
noncomputable def rankOne : E â†’L[ğ•œ] F â†’Lâ‹†[ğ•œ] F â†’L[ğ•œ] E :=
  .flip <| .comp (.smulRightL ğ•œ _ _) (innerSL ğ•œ)

lemma rankOne_def (x : E) (y : F) : rankOne ğ•œ x y = (innerSL ğ•œ y).smulRight x := rfl

lemma rankOne_def' (x : E) (y : F) : rankOne ğ•œ x y = .toSpanSingleton ğ•œ x âˆ˜L innerSL ğ•œ y := rfl

lemma toLinearMap_rankOne (x : E) (y : F) :
    (rankOne ğ•œ x y).toLinearMap = (innerâ‚›â‚— ğ•œ y).smulRight x := rfl

@[simp] theorem norm_rankOne (x : E) (y : F) : â€–rankOne ğ•œ x yâ€– = â€–xâ€– * â€–yâ€– := by
  rw [rankOne_def, norm_smulRight_apply, innerSL_apply_norm, mul_comm]

@[simp] theorem nnnorm_rankOne (x : E) (y : F) : â€–rankOne ğ•œ x yâ€–â‚Š = â€–xâ€–â‚Š * â€–yâ€–â‚Š :=
  NNReal.eq <| norm_rankOne _ _

@[simp] theorem enorm_rankOne (x : E) (y : F) : â€–rankOne ğ•œ x yâ€–â‚‘ = â€–xâ€–â‚‘ * â€–yâ€–â‚‘ :=
  ENNReal.coe_inj.mpr <| nnnorm_rankOne _ _

@[simp] lemma rankOne_apply (x : E) (y z : F) : rankOne ğ•œ x y z = inner ğ•œ y z â€¢ x := rfl

lemma comp_rankOne {G : Type*} [SeminormedAddCommGroup G] [NormedSpace ğ•œ G]
    (x : E) (y : F) (f : E â†’L[ğ•œ] G) : f âˆ˜L rankOne ğ•œ x y = rankOne ğ•œ (f x) y := by
  simp_rw [rankOne_def', â† comp_assoc, comp_toSpanSingleton]

theorem isIdempotentElem_rankOne_self {x : F} (hx : â€–xâ€– = 1) :
    IsIdempotentElem (rankOne ğ•œ x x) := by simp [IsIdempotentElem, mul_def, comp_rankOne, hx]

@[simp] theorem rankOne_one_right_eq_toSpanSingleton (x : F) :
    rankOne ğ•œ x 1 = toSpanSingleton ğ•œ x := by ext; simp

@[simp] theorem rankOne_one_left_eq_innerSL (x : F) : rankOne ğ•œ 1 x = innerSL ğ•œ x := by ext; simp

lemma rankOne_comp_rankOne (x : E) (y z : F) (w : G) :
    rankOne ğ•œ x y âˆ˜L rankOne ğ•œ z w = inner ğ•œ y z â€¢ rankOne ğ•œ x w := by simp [comp_rankOne]

lemma inner_left_rankOne_apply (x : F) (y z : G) (w : F) :
    inner ğ•œ (rankOne ğ•œ x y z) w = inner ğ•œ z y * inner ğ•œ x w := by
  simp [inner_smul_left, inner_conj_symm]

lemma inner_right_rankOne_apply (x y : F) (z w : G) :
    inner ğ•œ x (rankOne ğ•œ y z w) = inner ğ•œ x y * inner ğ•œ z w := by
  simp [inner_smul_right, mul_comm]

section Normed
variable {F H : Type*} [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]
  [NormedAddCommGroup H] [InnerProductSpace ğ•œ H]

@[simp] theorem rankOne_eq_zero {x : E} {y : F} : rankOne ğ•œ x y = 0 â†” x = 0 âˆ¨ y = 0 := by
  simp [ContinuousLinearMap.ext_iff, rankOne_apply, forall_or_right, or_comm,
    ext_iff_inner_right ğ•œ (E := F)]

lemma rankOne_ne_zero {x : E} {y : F} (hx : x â‰  0) (hy : y â‰  0) : rankOne ğ•œ x y â‰  0 := by
  grind [rankOne_eq_zero]

theorem isIdempotentElem_rankOne_self_iff {x : F} (hx : x â‰  0) :
    IsIdempotentElem (rankOne ğ•œ x x) â†” â€–xâ€– = 1 := by
  refine âŸ¨?_, isIdempotentElem_rankOne_selfâŸ©
  simp only [IsIdempotentElem, mul_def, comp_rankOne, rankOne_apply, inner_self_eq_norm_sq_to_K,
    map_smul, coe_smul', Pi.smul_apply]
  nth_rw 2 [â† one_smul ğ•œ (rankOne ğ•œ x x)]
  rw [â† sub_eq_zero, â† sub_smul]
  simp only [smul_eq_zero, rankOne_eq_zero, hx, or_self, or_false, sub_eq_zero, sq_eq_one_iff,
    FaithfulSMul.algebraMap_eq_one_iff, â† show ((-(1 : â„) : â„) : ğ•œ) = -1 by grind, ofReal_inj]
  grind [norm_nonneg]

theorem rankOne_eq_rankOne_iff_comm {a c : F} {b d : H} :
    rankOne ğ•œ a b = rankOne ğ•œ c d â†” rankOne ğ•œ b a = rankOne ğ•œ d c := by
  simp_rw [ContinuousLinearMap.ext_iff, ext_iff_inner_left ğ•œ (E := F),
    ext_iff_inner_right ğ•œ (E := H)]
  rw [forall_comm]
  simp [inner_smul_left, inner_smul_right, mul_comm]

open ComplexOrder in
theorem exists_of_rankOne_eq_rankOne {a c : F} {b d : H}
    (ha : a â‰  0) (hb : b â‰  0) (h : rankOne ğ•œ a b = rankOne ğ•œ c d) :
    âˆƒ (Î± Î² : ğ•œ) (_ : Î± â‰  0) (_ : 0 < Î²), a = Î± â€¢ c âˆ§ b = (Î± * Î²) â€¢ d := by
  have hâ‚‚ := rankOne_eq_rankOne_iff_comm.mp h
  simp only [ContinuousLinearMap.ext_iff, rankOne_apply] at h hâ‚‚
  have hâ‚ƒ := calc
    a = (âŸªb, bâŸ«_ğ•œ / âŸªb, bâŸ«_ğ•œ) â€¢ a := by simp_all
    _ = (1 / âŸªb, bâŸ«_ğ•œ) â€¢ (âŸªb, bâŸ«_ğ•œ â€¢ a) := by simp only [smul_smul]; ring_nf
    _ = (âŸªd, bâŸ«_ğ•œ / âŸªb, bâŸ«_ğ•œ) â€¢ c := by simp only [h, smul_smul]; ring_nf
  have hâ‚„ := calc
    b = (âŸªa, aâŸ«_ğ•œ / âŸªa, aâŸ«_ğ•œ) â€¢ b := by simp_all
    _ = (1 / âŸªa, aâŸ«_ğ•œ) â€¢ (âŸªa, aâŸ«_ğ•œ â€¢ b) := by simp only [smul_smul]; ring_nf
    _ = ((âŸªd, bâŸ«_ğ•œ / âŸªb, bâŸ«_ğ•œ) * (âŸªc, câŸ«_ğ•œ / âŸªa, aâŸ«_ğ•œ)) â€¢ d := by
      simp_rw [hâ‚‚, hâ‚ƒ, inner_smul_right, smul_smul]; ring_nf
  have hâ‚… : âŸªd, bâŸ«_ğ•œ â‰  0 := fun h â†¦ by simp [h, hb] at hâ‚„
  have hâ‚† : c â‰  0 := fun h â†¦ by simp [h, ha] at hâ‚ƒ
  refine âŸ¨_, â€–câ€– ^ 2 / â€–aâ€– ^ 2, div_ne_zero hâ‚… <| by simpa, ?_, hâ‚ƒ, by simpa using hâ‚„âŸ©
  simp_rw [â† ofReal_pow, â† ofReal_div, pos_iff (K := ğ•œ), ofReal_re, ofReal_im, and_true]
  exact div_pos (by simpa [sq_pos_iff]) (by simpa [sq_pos_iff])

end Normed

end InnerProductSpace
