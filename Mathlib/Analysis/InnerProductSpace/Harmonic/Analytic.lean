/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.Calculus.FDeriv.Symmetric
import Mathlib.Analysis.Complex.Conformal
import Mathlib.Analysis.Complex.HasPrimitives
import Mathlib.Analysis.InnerProductSpace.Harmonic.Basic

/-!
# Analyticity of Harmonic Functions

If `f : â„‚ â†’ â„` is harmonic at `x`, we show that `âˆ‚f/âˆ‚1 - I â€¢ âˆ‚f/âˆ‚I` is complex-analytic at `x`. If
`f` is harmonic on an open ball, then it is the real part of a function `F : â„‚ â†’ â„‚` that is
holomorphic on the ball.

TODO: Show that harmonic functions are real-analytic.
-/

open Complex InnerProductSpace Metric Topology

variable
  {f : â„‚ â†’ â„} {x : â„‚}

/--
If `f : â„‚ â†’ â„` is harmonic at `x`, then `âˆ‚f/âˆ‚1 - I â€¢ âˆ‚f/âˆ‚I` is complex differentiable at `x`.
-/
theorem HarmonicAt.differentiableAt_complex_partial (hf : HarmonicAt f x) :
    DifferentiableAt â„‚ (fun z â†¦ fderiv â„ f z 1 - I * fderiv â„ f z I) x := by
  have : (fun z â†¦ fderiv â„ f z 1 - I * fderiv â„ f z I) =
      (ofRealCLM âˆ˜ (fderiv â„ f Â· 1) - I â€¢ ofRealCLM âˆ˜ (fderiv â„ f Â· I)) := by
    ext; simp
  rw [this]
  have hâ‚f := hf.1
  refine differentiableAt_complex_iff_differentiableAt_real.2 âŸ¨by fun_prop, ?_âŸ©
  rw [fderiv_sub (by fun_prop) (by fun_prop), fderiv_const_smul (by fun_prop)]
  repeat rw [fderiv_comp]; all_goals try fun_prop
  simp only [ContinuousLinearMap.fderiv, ContinuousLinearMap.coe_sub',
    ContinuousLinearMap.coe_comp', ContinuousLinearMap.coe_smul', Pi.sub_apply,
    Function.comp_apply, ofRealCLM_apply, Pi.smul_apply, smul_eq_mul, mul_sub]
  ring_nf
  rw [fderiv_clm_apply (by fun_prop) (by fun_prop), fderiv_clm_apply (by fun_prop) (by fun_prop)]
  simp only [fderiv_fun_const, Pi.zero_apply, ContinuousLinearMap.comp_zero, zero_add,
    ContinuousLinearMap.flip_apply, I_sq, neg_mul, one_mul, sub_neg_eq_add]
  rw [add_comm, sub_eq_add_neg]
  congr 1
  Â· norm_cast
    apply hâ‚f.isSymmSndFDerivAt (by simp)
  Â· have hâ‚‚f := hf.2.eq_of_nhds
    simp only [laplacian_eq_iteratedFDeriv_complexPlane, iteratedFDeriv_two_apply, Fin.isValue,
      Matrix.cons_val_zero, Matrix.cons_val_one, Matrix.cons_val_fin_one, Pi.zero_apply,
      add_eq_zero_iff_eq_neg] at hâ‚‚f
    simp [hâ‚‚f]

/--
If `f : â„‚ â†’ â„` is harmonic at `x`, then `âˆ‚f/âˆ‚1 - I â€¢ âˆ‚f/âˆ‚I` is complex analytic at `x`.
-/
theorem HarmonicAt.analyticAt_complex_partial (hf : HarmonicAt f x) :
    AnalyticAt â„‚ (fun z â†¦ fderiv â„ f z 1 - I * fderiv â„ f z I) x :=
  DifferentiableOn.analyticAt (s := { x | HarmonicAt f x })
    (fun _ hy â†¦ (HarmonicAt.differentiableAt_complex_partial hy).differentiableWithinAt)
    ((isOpen_setOf_harmonicAt f).mem_nhds hf)

/-
If a function `f : â„‚ â†’ â„` is harmonic on an open ball, then `f` is the real part of a function
`F : â„‚ â†’ â„‚` that is holomorphic on the ball.
-/
theorem harmonic_is_realOfHolomorphic {z : â„‚} {R : â„} (hf : HarmonicOnNhd f (ball z R)) :
    âˆƒ F : â„‚ â†’ â„‚, (AnalyticOnNhd â„‚ F (ball z R)) âˆ§ ((ball z R).EqOn (fun z â†¦ (F z).re) f) := by
  by_cases hR : R â‰¤ 0
  Â· simp [ball_eq_empty.2 hR]
  let g := ofRealCLM âˆ˜ (fderiv â„ f Â· 1) - I â€¢ ofRealCLM âˆ˜ (fderiv â„ f Â· I)
  have hg : DifferentiableOn â„‚ g (ball z R) :=
    fun x hx â†¦ (HarmonicAt.differentiableAt_complex_partial (hf x hx)).differentiableWithinAt
  obtain âŸ¨Fâ‚€, hFâ‚€âŸ© := hg.isExactOn_ball
  let F := fun x â†¦ Fâ‚€ x - Fâ‚€ z + f z
  have hâ‚F : âˆ€ zâ‚ âˆˆ ball z R, HasDerivAt F (g zâ‚) zâ‚ := by
    simp_all [F]
  have hâ‚‚F : DifferentiableOn â„‚ F (ball z R) :=
    fun x hx â†¦ (hâ‚F x hx).differentiableAt.differentiableWithinAt
  have hâ‚ƒF : DifferentiableOn â„ F (ball z R) :=
    hâ‚‚F.restrictScalars (ğ•œ := â„) (ğ•œ' := â„‚)
  use F, hâ‚‚F.analyticOnNhd isOpen_ball
  rw [(by aesop : (fun z â†¦ (F z).re) = Complex.reCLM âˆ˜ F)]
  intro x hx
  apply (convex_ball z R).eqOn_of_fderivWithin_eq (ğ•œ := â„) (x := z)
  Â· exact reCLM.differentiable.comp_differentiableOn hâ‚ƒF
  Â· exact fun y hy â†¦ (ContDiffAt.differentiableAt (hf y hy).1 one_le_two).differentiableWithinAt
  Â· exact isOpen_ball.uniqueDiffOn
  Â· intro y hy
    have hâ‚„F := (hâ‚F y hy).differentiableAt
    have hâ‚…F := hâ‚„F.restrictScalars (ğ•œ := â„) (ğ•œ' := â„‚)
    rw [fderivWithin_eq_fderiv (isOpen_ball.uniqueDiffWithinAt hy)
      (reCLM.differentiableAt.comp y hâ‚…F), fderivWithin_eq_fderiv
      (isOpen_ball.uniqueDiffWithinAt hy) ((hf y hy).1.differentiableAt one_le_two), fderiv_comp y
      (by fun_prop) hâ‚…F, ContinuousLinearMap.fderiv, hâ‚„F.fderiv_restrictScalars (ğ•œ := â„)]
    ext a
    nth_rw 2 [(by simp : a = a.re â€¢ (1 : â„‚) + a.im â€¢ (I : â„‚))]
    rw [ContinuousLinearMap.map_add, ContinuousLinearMap.map_smul, ContinuousLinearMap.map_smul]
    simp [HasDerivAt.deriv (hâ‚F y hy), g]
  Â· simp_all
  Â· simp [F]
  Â· assumption
