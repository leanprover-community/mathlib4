/-
Copyright (c) 2025 Stefan Kebekus. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Stefan Kebekus
-/
import Mathlib.Analysis.InnerProductSpace.Laplacian

/-!
# Harmonic Functions

This file defines harmonic functions on real, finite-dimensional, inner product spaces `E`.
-/

variable
  {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ‚Ñù E] [FiniteDimensional ‚Ñù E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace ‚Ñù G]
  {f f‚ÇÅ f‚ÇÇ : E ‚Üí F}
  {x : E} {s t : Set E} {c : ‚Ñù}

open Topology

namespace InnerProductSpace

/-!
## Definition
-/

variable (f x) in
/--
Let `E` be a real, finite-dimensional, inner product space and `x` be a point of `E`. A function `f`
on `E` is harmonic at `x` if it is two times continuously `‚Ñù`-differentiable and if its Laplacian
vanishes in a neighborhood of `x`.
-/
def HarmonicAt := (ContDiffAt ‚Ñù 2 f x) ‚àß (Œî f =·∂†[ùìù x] 0)

variable (f s) in
/--
Let `E` be a real, finite-dimensional, inner product space and `s` be a subset of `E`. A function
`f` on `E` is harmonic in a neighborhood of `s` if it is harmonic at every point of `s`.
-/
def HarmonicOnNhd := ‚àÄ x ‚àà s, HarmonicAt f x

/-!
## Elementary Properties
-/

/--
If two functions agree in a neighborhood of `x`, then one is harmonic at `x` iff so is the other.
-/
theorem harmonicAt_congr_nhds {f‚ÇÅ f‚ÇÇ : E ‚Üí F} {x : E} (h : f‚ÇÅ =·∂†[ùìù x] f‚ÇÇ) :
    HarmonicAt f‚ÇÅ x ‚Üî HarmonicAt f‚ÇÇ x := by
  constructor <;> intro hf
  ¬∑ exact ‚ü®hf.1.congr_of_eventuallyEq h.symm, (laplacian_congr_nhds h.symm).trans hf.2‚ü©
  ¬∑ exact ‚ü®hf.1.congr_of_eventuallyEq h, (laplacian_congr_nhds h).trans hf.2‚ü©

/--
If `f` is harmonic at `x`, then it is harmonic at all points in a neighborhood of `x`.
-/
theorem HarmonicAt.eventually {f : E ‚Üí F} {x : E} (h : HarmonicAt f x) :
    ‚àÄ·∂† y in ùìù x, HarmonicAt f y := by
  filter_upwards [h.1.eventually (by simp), h.2.eventually_nhds] with a h‚ÇÅa h‚ÇÇa
  exact ‚ü®h‚ÇÅa, h‚ÇÇa‚ü©

variable (f) in
/--
Harmonicity is an open property.
-/
theorem isOpen_setOf_harmonicAt : IsOpen { x : E | HarmonicAt f x } :=
  isOpen_iff_mem_nhds.2 (fun _ hx ‚Ü¶ hx.eventually)

/--
If `f` is harmonic in a neighborhood of `s`, it is harmonic in a neighborhood of every subset.
-/
lemma HarmonicOnNhd.mono (h : HarmonicOnNhd f s) (hst : t ‚äÜ s) :
    HarmonicOnNhd f t := fun x hx ‚Ü¶ h x (hst hx)

/-!
## Vector Space Structure
-/

/--
Sums of harmonic functions are harmonic.
-/
theorem HarmonicAt.add (h‚ÇÅ : HarmonicAt f‚ÇÅ x) (h‚ÇÇ : HarmonicAt f‚ÇÇ x) :
    HarmonicAt (f‚ÇÅ + f‚ÇÇ) x := by
  constructor
  ¬∑ exact h‚ÇÅ.1.add h‚ÇÇ.1
  ¬∑ filter_upwards [h‚ÇÅ.1.laplacian_add_nhds h‚ÇÇ.1, h‚ÇÅ.2, h‚ÇÇ.2]
    simp_all

/--
Sums of harmonic functions are harmonic.
-/
theorem HarmonicOnNhd.add (h‚ÇÅ : HarmonicOnNhd f‚ÇÅ s) (h‚ÇÇ : HarmonicOnNhd f‚ÇÇ s) :
    HarmonicOnNhd (f‚ÇÅ + f‚ÇÇ) s := fun x hx ‚Ü¶ (h‚ÇÅ x hx).add (h‚ÇÇ x hx)

/--
Scalar multiples of harmonic functions are harmonic.
-/
theorem HarmonicAt.const_smul (h : HarmonicAt f x) :
    HarmonicAt (c ‚Ä¢ f) x := by
  constructor
  ¬∑ exact h.1.const_smul c
  ¬∑ filter_upwards [laplacian_smul_nhds c h.1, h.2]
    simp_all

/--
Scalar multiples of harmonic functions are harmonic.
-/
theorem HarmonicOnNhd.const_smul (h : HarmonicOnNhd f s) :
    HarmonicOnNhd (c ‚Ä¢ f) s := fun x hx ‚Ü¶ (h x hx).const_smul

/-!
## Compatibility with Linear Maps
-/

/--
Compositions of continuous `‚Ñù`-linear maps with harmonic functions are harmonic.
-/
theorem HarmonicAt.comp_CLM (h : HarmonicAt f x) (l : F ‚ÜíL[‚Ñù] G) :
    HarmonicAt (l ‚àò f) x := by
  constructor
  ¬∑ exact h.1.continuousLinearMap_comp l
  ¬∑ filter_upwards [h.1.laplacian_CLM_comp_left_nhds (l := l), h.2]
    simp_all

/--
Compositions of continuous linear maps with harmonic functions are harmonic.
-/
theorem HarmonicOnNhd.comp_CLM (h : HarmonicOnNhd f s) (l : F ‚ÜíL[‚Ñù] G) :
    HarmonicOnNhd (l ‚àò f) s := fun x hx ‚Ü¶ (h x hx).comp_CLM l

/--
Functions are harmonic iff their compositions with continuous linear equivalences are harmonic.
-/
theorem harmonicAt_comp_CLE_iff (l : F ‚âÉL[‚Ñù] G) :
    HarmonicAt (l ‚àò f) x ‚Üî HarmonicAt f x := by
  constructor <;> intro h
  ¬∑ simpa [Function.comp_def] using h.comp_CLM l.symm.toContinuousLinearMap
  ¬∑ exact h.comp_CLM l.toContinuousLinearMap

/--
Functions are harmonic iff their compositions with continuous linear equivalences are harmonic.
-/
theorem harmonicOnNhd_comp_CLE_iff (l : F ‚âÉL[‚Ñù] G) :
    HarmonicOnNhd (l ‚àò f) s ‚Üî HarmonicOnNhd f s :=
  forall‚ÇÇ_congr fun _ _ ‚Ü¶ harmonicAt_comp_CLE_iff l

end InnerProductSpace
