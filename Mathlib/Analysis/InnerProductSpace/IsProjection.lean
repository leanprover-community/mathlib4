/-
Copyright (c) 2025 IvÃ¡n Renison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IvÃ¡n Renison
-/

import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.Projection

/-!
# `IsProjection` and `IsOrthogonalProjection` as predicates on linear maps

This files defines when a linear map is a projection and when it is an orthogonal projection.

## Main definitions

* `LinearMap.IsProjection`: We call a linear map `T` a projection when `T âˆ˜â‚— T = T`.
* `LinearMap.IsOrthogonalProjection`: We call a linear map `T` an orthogonal projection when it is a
projection and it is self-adjoint.

-/

namespace LinearMap

section IsProjection

variable {R E : Type*}
variable [Semiring R] [AddCommMonoid E] [Module R E]

/-- When a linear map is a projection. -/
def IsProjection (T : E â†’â‚—[R] E) : Prop :=
  T âˆ˜â‚— T = T

lemma IsProjection.app_range {T : E â†’â‚—[R] E} (hT : T.IsProjection) {x : E} (hx : x âˆˆ range T) :
    T x = x := by
  obtain âŸ¨y, hyâŸ© := hx
  rw [â† hy, â† comp_apply, hT]

lemma IsProjection.zero : (0 : E â†’â‚—[R] E).IsProjection :=
  rfl

lemma IsProjection.one : (1 : E â†’â‚—[R] E).IsProjection :=
  rfl

end IsProjection

section IsOrthogonalProjection

variable {ğ•œ E : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [FiniteDimensional ğ•œ E]

/-- When a linear map is an orthogonal projection. -/
structure IsOrthogonalProjection (T : E â†’â‚—[ğ•œ] E) : Prop where
  isProjection : T.IsProjection
  isSelfAdjoint : IsSelfAdjoint T

lemma IsOrthogonalProjection.isSymmetric {T : E â†’â‚—[ğ•œ] E} (hT : T.IsOrthogonalProjection) :
    T.IsSymmetric := (isSymmetric_iff_isSelfAdjoint T).mpr hT.isSelfAdjoint

lemma IsOrthogonalProjection.isPositive {T : E â†’â‚—[ğ•œ] E} (hT : T.IsOrthogonalProjection) :
    T.IsPositive := by
  apply And.intro hT.isSelfAdjoint
  intro x
  rw [â† hT.isProjection, comp_apply, hT.isSymmetric]
  exact inner_self_nonneg

lemma IsOrthogonalProjection.range_eq_orthogonal_ker {T : E â†’â‚—[ğ•œ] E}
    (hT : T.IsOrthogonalProjection) : range T = (ker T)á—® := by
  apply Submodule.eq_of_le_of_finrank_eq
  Â· intro x hx
    rw [Submodule.mem_orthogonal]
    intro u hu
    rw [â† hT.isProjection.app_range hx, â† (isSymmetric_iff_isSelfAdjoint T).mpr hT.isSelfAdjoint,
      hu]
    exact inner_zero_left x
  Â· rw [Nat.eq_sub_of_add_eq' (ker T).finrank_add_finrank_orthogonal,
      eq_tsub_iff_add_eq_of_le (ker T).finrank_le]
    exact finrank_range_add_finrank_ker T

/-- Get a linear map from a submodule. See `Submodule.toOrthogonalProjection_valid` for the proof
that it actually satisfies `LinearMap.IsOrthogonalProjection`. -/
noncomputable def Submodule.toOrthogonalProjection (K : Submodule ğ•œ E) : E â†’â‚—[ğ•œ] E :=
  K.subtype âˆ˜â‚— K.orthogonalProjection

lemma Submodule.toOrthogonalProjection_eq (K : Submodule ğ•œ E) (x : E) :
    toOrthogonalProjection K x = K.orthogonalProjection x := rfl

lemma Submodule.toOrthogonalProjection_valid (K : Submodule ğ•œ E) :
    (toOrthogonalProjection K).IsOrthogonalProjection := by
  constructor
  Â· ext
    simp [toOrthogonalProjection]
  Â· rw [â† isSymmetric_iff_isSelfAdjoint]
    intro x y
    unfold toOrthogonalProjection
    simp only [coe_comp]
    exact K.inner_orthogonalProjection_left_eq_right x y

lemma Submodule.range_toOrthogonalProjection_eq (K : Submodule ğ•œ E) :
    range (toOrthogonalProjection K) = K := by
  rw [(toOrthogonalProjection_valid K).range_eq_orthogonal_ker]
  unfold toOrthogonalProjection
  rw [â† Submodule.orthogonalComplement_eq_orthogonalComplement,
    Submodule.orthogonal_orthogonal]
  ext x
  rw [mem_ker, â† Submodule.orthogonalProjection_eq_zero_iff]
  simp

lemma IsOrthogonalProjection.toOrthogonalProjection_range_eq (T : E â†’â‚—[ğ•œ] E)
    (hT : T.IsOrthogonalProjection) : Submodule.toOrthogonalProjection (range T) = T := by
  ext x
  rw [hT.range_eq_orthogonal_ker]
  have hx := Submodule.exists_add_mem_mem_orthogonal (ker T) x
  obtain âŸ¨y, hy, z, hz, hxyzâŸ© := hx
  rw [hxyz]
  repeat rw [map_add, Submodule.toOrthogonalProjection_eq,
    Submodule.orthogonalProjection_orthogonal_val]
  apply Mathlib.Tactic.LinearCombination.add_eq_eq
  Â· rw [Submodule.orthogonalProjection_eq_self_iff.mpr hy, sub_self, hy]
  Â· rw [Submodule.orthogonalProjection_eq_zero_iff.mpr hz,
      ZeroMemClass.coe_zero, sub_zero]
    have hz' : z âˆˆ range T := by
      rw [hT.range_eq_orthogonal_ker]
      exact hz
    exact (hT.isProjection.app_range hz').symm

theorem IsOrthogonalProjection.eq_iff_range_eq {T S : E â†’â‚—[ğ•œ] E} (hT : T.IsOrthogonalProjection)
    (hS : S.IsOrthogonalProjection) : T = S â†” range T = range S := by
  apply Iff.intro
  Â· intro h
    rw [h]
  Â· intro h
    rw [â† hT.toOrthogonalProjection_range_eq, â† hS.toOrthogonalProjection_range_eq, h]

lemma IsOrthogonalProjection.zero : (0 : E â†’â‚—[ğ•œ] E).IsOrthogonalProjection :=
  âŸ¨IsProjection.zero, IsSelfAdjoint.zero (E â†’â‚—[ğ•œ] E)âŸ©

lemma IsOrthogonalProjection.one : (1 : E â†’â‚—[ğ•œ] E).IsOrthogonalProjection :=
  âŸ¨IsProjection.one, IsSelfAdjoint.one (E â†’â‚—[ğ•œ] E)âŸ©

lemma Submodule.toOrthogonalProjection_bot :
    toOrthogonalProjection (âŠ¥ : Submodule ğ•œ E) = (0 : E â†’â‚—[ğ•œ] E) := by
  ext x
  simp [toOrthogonalProjection_eq, Submodule.orthogonalProjection_bot]

lemma Submodule.toOrthogonalProjection_top :
    toOrthogonalProjection (âŠ¤ : Submodule ğ•œ E) = (1 : E â†’â‚—[ğ•œ] E) := by
  ext x
  simp [toOrthogonalProjection_eq, Submodule.orthogonalProjection_eq_self_iff]

end IsOrthogonalProjection

end LinearMap
