/-
Copyright (c) 2025 IvÃ¡n Renison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: IvÃ¡n Renison
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.LinearAlgebra.Trace

/-!
# Traces in inner product spaces

This file contains various results about traces of linear operators in inner product spaces.
-/

namespace LinearMap

variable {ğ•œ E Î¹ : Type*} [RCLike ğ•œ] [Fintype Î¹]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]

open scoped InnerProductSpace

lemma trace_eq_sum_inner (T : E â†’â‚—[ğ•œ] E) (b : OrthonormalBasis Î¹ ğ•œ E) :
    T.trace ğ•œ E = âˆ‘ i, âŸªb i, T (b i)âŸ«_ğ•œ := by
  classical
  rw [LinearMap.trace_eq_matrix_trace ğ•œ b.toBasis T]
  apply Fintype.sum_congr
  intro i
  rw [Matrix.diag_apply, T.toMatrix_apply, b.coe_toBasis, b.coe_toBasis_repr_apply,
    b.repr_apply_apply]

variable [FiniteDimensional ğ•œ E]
variable {n : â„•} (hn : Module.finrank ğ•œ E = n)

lemma IsSymmetric.trace_eq_sum_eigenvalues {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) :
    T.trace ğ•œ E = âˆ‘ i, hT.eigenvalues hn i := by
  let b := hT.eigenvectorBasis hn
  rw [T.trace_eq_sum_inner b, RCLike.ofReal_sum]
  apply Fintype.sum_congr
  intro i
  rw [hT.apply_eigenvectorBasis, inner_smul_real_right, inner_self_eq_norm_sq_to_K, b.norm_eq_one]
  simp [RCLike.ofReal_alg]

lemma IsSymmetric.re_trace_eq_sum_eigenvalues {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) :
    RCLike.re (T.trace ğ•œ E) = âˆ‘ i, hT.eigenvalues hn i := by
  rw [hT.trace_eq_sum_eigenvalues]
  exact RCLike.ofReal_re_ax _

end LinearMap
