/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Algebra.QuadraticDiscriminant
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.Complex.Basic

/-!
# Inner product spaces

This file defines inner product spaces.
Hilbert spaces can be obtained using the set of assumptions
`[RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [CompleteSpace E]`.
For convenience, a variable alias `HilbertSpace` is provided so that one can write
`variable? [HilbertSpace ğ•œ E]` and get this as a suggestion.

An inner product space is a vector space endowed with an inner product. It generalizes the notion of
dot product in `â„^n` and provides the means of defining the length of a vector and the angle between
two vectors. In particular vectors `x` and `y` are orthogonal if their inner product equals zero.
We define both the real and complex cases at the same time using the `RCLike` typeclass.

Rather than defining the norm on an inner product space to be `âˆš(re âŸªx, xâŸ«)`, we assume that a norm
is given, and add a hypothesis stating that `â€–xâ€– ^ 2 = re âŸªx, xâŸ«`. This makes it possible to
handle spaces where the norm is equal, but not defeq, to the square root of the
inner product. Defining a norm starting from an inner product is handled via the
`InnerProductSpace.Core` structure.

This file is intended to contain the minimal amount of machinery needed to define inner product
spaces, and to construct a normed space from an inner product space. Many more general lemmas can
be found in `Analysis.InnerProductSpace.Basic`. For the specific construction of an inner product
structure on `n â†’ ğ•œ` for `ğ•œ = â„` or `â„‚`, see `EuclideanSpace` in
`Analysis.InnerProductSpace.PiL2`.

## Main results

- We define the class `InnerProductSpace ğ•œ E` extending `NormedSpace ğ•œ E` with a number of basic
  properties, most notably the Cauchy-Schwarz inequality. Here `ğ•œ` is understood to be either `â„`
  or `â„‚`, through the `RCLike` typeclass.

## Notation

We globally denote the real and complex inner products by `âŸªÂ·, Â·âŸ«_â„` and `âŸªÂ·, Â·âŸ«_â„‚` respectively.
We also provide two notation namespaces: `RealInnerProductSpace`, `ComplexInnerProductSpace`,
which respectively introduce the plain notation `âŸªÂ·, Â·âŸ«` for the real and complex inner product.

## Implementation notes

We choose the convention that inner products are conjugate linear in the first argument and linear
in the second.

## Tags

inner product space, Hilbert space, norm

## References
* [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
* [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/


noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp Bornology

open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

/-- Syntactic typeclass for types endowed with an inner product -/
class Inner (ğ•œ E : Type*) where
  /-- The inner product function. -/
  inner (ğ•œ) : E â†’ E â†’ ğ•œ

export Inner (inner)

/-- The inner product with values in `ğ•œ`. -/
scoped[InnerProductSpace] notation:max "âŸª" x ", " y "âŸ«_" ğ•œ:max => inner ğ•œ x y

section Notations

/-- The inner product with values in `â„`. -/
scoped[RealInnerProductSpace] notation "âŸª" x ", " y "âŸ«" => inner â„ x y

/-- The inner product with values in `â„‚`. -/
scoped[ComplexInnerProductSpace] notation "âŸª" x ", " y "âŸ«" => inner â„‚ x y

end Notations

/-- A (pre) inner product space is a vector space with an additional operation called inner product.
The (semi)norm could be derived from the inner product, instead we require the existence of a
seminorm and the fact that `â€–xâ€–^2 = re âŸªx, xâŸ«` to be able to put instances on `ğ•‚` or product spaces.

Note that `NormedSpace` does not assume that `â€–xâ€–=0` implies `x=0` (it is rather a seminorm).

To construct a seminorm from an inner product, see `PreInnerProductSpace.ofCore`.
-/
class InnerProductSpace (ğ•œ : Type*) (E : Type*) [RCLike ğ•œ] [SeminormedAddCommGroup E] extends
    NormedSpace ğ•œ E, Inner ğ•œ E where
  /-- The inner product induces the norm. -/
  norm_sq_eq_re_inner : âˆ€ x : E, â€–xâ€– ^ 2 = re (inner x x)
  /-- The inner product is *Hermitian*, taking the `conj` swaps the arguments. -/
  conj_inner_symm : âˆ€ x y, conj (inner y x) = inner x y
  /-- The inner product is additive in the first coordinate. -/
  add_left : âˆ€ x y z, inner (x + y) z = inner x z + inner y z
  /-- The inner product is conjugate linear in the first coordinate. -/
  smul_left : âˆ€ x y r, inner (r â€¢ x) y = conj r * inner x y

/-!
### Constructing a normed space structure from an inner product

In the definition of an inner product space, we require the existence of a norm, which is equal
(but maybe not defeq) to the square root of the scalar product. This makes it possible to put
an inner product space structure on spaces with a preexisting norm (for instance `â„`), with good
properties. However, sometimes, one would like to define the norm starting only from a well-behaved
scalar product. This is what we implement in this paragraph, starting from a structure
`InnerProductSpace.Core` stating that we have a nice scalar product.

Our goal here is not to develop a whole theory with all the supporting API, as this will be done
below for `InnerProductSpace`. Instead, we implement the bare minimum to go as directly as
possible to the construction of the norm and the proof of the triangular inequality.

Warning: Do not use this `Core` structure if the space you are interested in already has a norm
instance defined on it, otherwise this will create a second non-defeq norm instance!
-/

/-- A structure requiring that a scalar product is positive semidefinite and symmetric. -/
structure PreInnerProductSpace.Core (ğ•œ : Type*) (F : Type*) [RCLike ğ•œ] [AddCommGroup F]
    [Module ğ•œ F] extends Inner ğ•œ F where
  /-- The inner product is *Hermitian*, taking the `conj` swaps the arguments. -/
  conj_inner_symm x y : conj (inner y x) = inner x y
  /-- The inner product is positive (semi)definite. -/
  re_inner_nonneg x : 0 â‰¤ re (inner x x)
  /-- The inner product is additive in the first coordinate. -/
  add_left x y z : inner (x + y) z = inner x z + inner y z
  /-- The inner product is conjugate linear in the first coordinate. -/
  smul_left x y r : inner (r â€¢ x) y = conj r * inner x y

@[deprecated (since := "2025-04-22")]
alias PreInnerProductSpace.Core.conj_symm := PreInnerProductSpace.Core.conj_inner_symm

@[deprecated (since := "2025-04-22")]
alias PreInnerProductSpace.Core.inner_nonneg := PreInnerProductSpace.Core.re_inner_nonneg

attribute [class] PreInnerProductSpace.Core

/-- A structure requiring that a scalar product is positive definite. Some theorems that
require this assumptions are put under section `InnerProductSpace.Core`. -/
structure InnerProductSpace.Core (ğ•œ : Type*) (F : Type*) [RCLike ğ•œ] [AddCommGroup F]
  [Module ğ•œ F] extends PreInnerProductSpace.Core ğ•œ F where
  /-- The inner product is positive definite. -/
  definite : âˆ€ x, inner x x = 0 â†’ x = 0

/- We set `InnerProductSpace.Core` to be a class as we will use it as such in the construction
of the normed space structure that it produces. However, all the instances we will use will be
local to this proof. -/
attribute [class] InnerProductSpace.Core

instance (ğ•œ : Type*) (F : Type*) [RCLike ğ•œ] [AddCommGroup F]
    [Module ğ•œ F] [cd : InnerProductSpace.Core ğ•œ F] : PreInnerProductSpace.Core ğ•œ F where
  inner := cd.inner
  conj_inner_symm := cd.conj_inner_symm
  re_inner_nonneg := cd.re_inner_nonneg
  add_left := cd.add_left
  smul_left := cd.smul_left

/-- Define `PreInnerProductSpace.Core` from `InnerProductSpace`. Defined to reuse lemmas about
`PreInnerProductSpace.Core` for `PreInnerProductSpace`s. Note that the `Seminorm` instance provided
by `PreInnerProductSpace.Core.norm` is propositionally but not definitionally equal to the original
norm. -/
def PreInnerProductSpace.toCore [SeminormedAddCommGroup E] [c : InnerProductSpace ğ•œ E] :
    PreInnerProductSpace.Core ğ•œ E where
  __ := c
  re_inner_nonneg x := by rw [â† InnerProductSpace.norm_sq_eq_re_inner]; apply sq_nonneg

/-- Define `InnerProductSpace.Core` from `InnerProductSpace`. Defined to reuse lemmas about
`InnerProductSpace.Core` for `InnerProductSpace`s. Note that the `Norm` instance provided by
`InnerProductSpace.Core.norm` is propositionally but not definitionally equal to the original
norm. -/
def InnerProductSpace.toCore [NormedAddCommGroup E] [c : InnerProductSpace ğ•œ E] :
    InnerProductSpace.Core ğ•œ E :=
  { c with
    re_inner_nonneg := fun x => by
      rw [â† InnerProductSpace.norm_sq_eq_re_inner]
      apply sq_nonneg
    definite := fun x hx =>
      norm_eq_zero.1 <| eq_zero_of_pow_eq_zero (n := 2) <| by
        rw [InnerProductSpace.norm_sq_eq_re_inner (ğ•œ := ğ•œ) x, hx, map_zero] }

namespace InnerProductSpace.Core

section PreInnerProductSpace.Core

variable [AddCommGroup F] [Module ğ•œ F] [c : PreInnerProductSpace.Core ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-- Local notation for `RCLike.ext_iff ğ•œ` -/
local notation "ext_iff" => @RCLike.ext_iff ğ•œ _

/-- Local notation for `starRingEnd _` -/
local postfix:90 "â€ " => starRingEnd _

/-- Inner product defined by the `PreInnerProductSpace.Core` structure. We can't reuse
`PreInnerProductSpace.Core.toInner` because it takes `PreInnerProductSpace.Core` as an explicit
argument. -/
def toPreInner' : Inner ğ•œ F :=
  c.toInner

attribute [local instance] toPreInner'

/-- The norm squared function for `PreInnerProductSpace.Core` structure. -/
def normSq (x : F) :=
  re âŸªx, xâŸ«

/-- The norm squared function for `PreInnerProductSpace.Core` structure. -/
local notation "normSqF" => @normSq ğ•œ F _ _ _ _

theorem inner_conj_symm (x y : F) : âŸªy, xâŸ«â€  = âŸªx, yâŸ« :=
  c.conj_inner_symm x y

theorem inner_self_nonneg {x : F} : 0 â‰¤ re âŸªx, xâŸ« :=
  c.re_inner_nonneg _

theorem inner_self_im (x : F) : im âŸªx, xâŸ« = 0 := by
  rw [â† @ofReal_inj ğ•œ, im_eq_conj_sub]
  simp [inner_conj_symm]

theorem inner_add_left (x y z : F) : âŸªx + y, zâŸ« = âŸªx, zâŸ« + âŸªy, zâŸ« :=
  c.add_left _ _ _

theorem inner_add_right (x y z : F) : âŸªx, y + zâŸ« = âŸªx, yâŸ« + âŸªx, zâŸ« := by
  rw [â† inner_conj_symm, inner_add_left, RingHom.map_add]; simp only [inner_conj_symm]

theorem ofReal_normSq_eq_inner_self (x : F) : (normSqF x : ğ•œ) = âŸªx, xâŸ« := by
  rw [ext_iff]
  exact âŸ¨by simp only [ofReal_re, normSq], by simp only [inner_self_im, ofReal_im]âŸ©

theorem inner_re_symm (x y : F) : re âŸªx, yâŸ« = re âŸªy, xâŸ« := by rw [â† inner_conj_symm, conj_re]

theorem inner_im_symm (x y : F) : im âŸªx, yâŸ« = -im âŸªy, xâŸ« := by rw [â† inner_conj_symm, conj_im]

theorem inner_smul_left (x y : F) {r : ğ•œ} : âŸªr â€¢ x, yâŸ« = râ€  * âŸªx, yâŸ« :=
  c.smul_left _ _ _

theorem inner_smul_right (x y : F) {r : ğ•œ} : âŸªx, r â€¢ yâŸ« = r * âŸªx, yâŸ« := by
  rw [â† inner_conj_symm, inner_smul_left]
  simp only [conj_conj, inner_conj_symm, RingHom.map_mul]

theorem inner_zero_left (x : F) : âŸª0, xâŸ« = 0 := by
  rw [â† zero_smul ğ•œ (0 : F), inner_smul_left]
  simp only [zero_mul, RingHom.map_zero]

theorem inner_zero_right (x : F) : âŸªx, 0âŸ« = 0 := by
  rw [â† inner_conj_symm, inner_zero_left]; simp only [RingHom.map_zero]

theorem inner_self_of_eq_zero {x : F} : x = 0 â†’ âŸªx, xâŸ« = 0 := by
  rintro rfl
  exact inner_zero_left _

theorem normSq_eq_zero_of_eq_zero {x : F} : x = 0 â†’ normSqF x = 0 := by
  rintro rfl
  simp [normSq, inner_self_of_eq_zero]

theorem ne_zero_of_inner_self_ne_zero {x : F} : âŸªx, xâŸ« â‰  0 â†’ x â‰  0 :=
  mt inner_self_of_eq_zero

theorem inner_self_ofReal_re (x : F) : (re âŸªx, xâŸ« : ğ•œ) = âŸªx, xâŸ« := by
  norm_num [ext_iff, inner_self_im]

theorem norm_inner_symm (x y : F) : â€–âŸªx, yâŸ«â€– = â€–âŸªy, xâŸ«â€– := by rw [â† inner_conj_symm, norm_conj]

theorem inner_neg_left (x y : F) : âŸª-x, yâŸ« = -âŸªx, yâŸ« := by
  rw [â† neg_one_smul ğ•œ x, inner_smul_left]
  simp

theorem inner_neg_right (x y : F) : âŸªx, -yâŸ« = -âŸªx, yâŸ« := by
  rw [â† inner_conj_symm, inner_neg_left]; simp only [RingHom.map_neg, inner_conj_symm]

theorem inner_sub_left (x y z : F) : âŸªx - y, zâŸ« = âŸªx, zâŸ« - âŸªy, zâŸ« := by
  simp [sub_eq_add_neg, inner_add_left, inner_neg_left]

theorem inner_sub_right (x y z : F) : âŸªx, y - zâŸ« = âŸªx, yâŸ« - âŸªx, zâŸ« := by
  simp [sub_eq_add_neg, inner_add_right, inner_neg_right]

theorem inner_mul_symm_re_eq_norm (x y : F) : re (âŸªx, yâŸ« * âŸªy, xâŸ«) = â€–âŸªx, yâŸ« * âŸªy, xâŸ«â€– := by
  rw [â† inner_conj_symm, mul_comm]
  exact re_eq_norm_of_mul_conj âŸªy, xâŸ«

/-- Expand `âŸªx + y, x + yâŸ«` -/
theorem inner_add_add_self (x y : F) : âŸªx + y, x + yâŸ« = âŸªx, xâŸ« + âŸªx, yâŸ« + âŸªy, xâŸ« + âŸªy, yâŸ« := by
  simp only [inner_add_left, inner_add_right]; ring

-- Expand `âŸªx - y, x - yâŸ«`
theorem inner_sub_sub_self (x y : F) : âŸªx - y, x - yâŸ« = âŸªx, xâŸ« - âŸªx, yâŸ« - âŸªy, xâŸ« + âŸªy, yâŸ« := by
  simp only [inner_sub_left, inner_sub_right]; ring

theorem inner_smul_ofReal_left (x y : F) {t : â„} : âŸª(t : ğ•œ) â€¢ x, yâŸ« = âŸªx, yâŸ« * t := by
  rw [inner_smul_left, conj_ofReal, mul_comm]

theorem inner_smul_ofReal_right (x y : F) {t : â„} : âŸªx, (t : ğ•œ) â€¢ yâŸ« = âŸªx, yâŸ« * t := by
  rw [inner_smul_right, mul_comm]

theorem re_inner_smul_ofReal_smul_self (x : F) {t : â„} :
    re âŸª(t : ğ•œ) â€¢ x, (t : ğ•œ) â€¢ xâŸ« = normSqF x * t * t := by
  simp [inner_smul_ofReal_left, inner_smul_ofReal_right, normSq]

/-- An auxiliary equality useful to prove the **Cauchyâ€“Schwarz inequality**. Here we use the
standard argument involving the discriminant of quadratic form. -/
lemma cauchy_schwarz_aux' (x y : F) (t : â„) : 0 â‰¤ normSqF x * t * t + 2 * re âŸªx, yâŸ« * t
    + normSqF y := by
  calc 0 â‰¤ re âŸª(ofReal t : ğ•œ) â€¢ x + y, (ofReal t : ğ•œ) â€¢ x + yâŸ« := inner_self_nonneg
  _ = re (âŸª(ofReal t : ğ•œ) â€¢ x, (ofReal t : ğ•œ) â€¢ xâŸ« + âŸª(ofReal t : ğ•œ) â€¢ x, yâŸ«
      + âŸªy, (ofReal t : ğ•œ) â€¢ xâŸ« + âŸªy, yâŸ«) := by rw [inner_add_add_self ((ofReal t : ğ•œ) â€¢ x) y]
  _ = re âŸª(ofReal t : ğ•œ) â€¢ x, (ofReal t : ğ•œ) â€¢ xâŸ«
      + re âŸª(ofReal t : ğ•œ) â€¢ x, yâŸ« + re âŸªy, (ofReal t : ğ•œ) â€¢ xâŸ« + re âŸªy, yâŸ« := by
      simp only [map_add]
  _ = normSq x * t * t + re (âŸªx, yâŸ« * t) + re (âŸªy, xâŸ« * t) + re âŸªy, yâŸ« := by rw
    [re_inner_smul_ofReal_smul_self, inner_smul_ofReal_left, inner_smul_ofReal_right]
  _ = normSq x * t * t + re âŸªx, yâŸ« * t + re âŸªy, xâŸ« * t + re âŸªy, yâŸ« := by rw [mul_comm âŸªx,yâŸ« _,
    RCLike.re_ofReal_mul, mul_comm t _, mul_comm âŸªy,xâŸ« _, RCLike.re_ofReal_mul, mul_comm t _]
  _ = normSq x * t * t + re âŸªx, yâŸ« * t + re âŸªy, xâŸ« * t + normSq y := by rw [â† normSq]
  _ = normSq x * t * t + re âŸªx, yâŸ« * t + re âŸªx, yâŸ« * t + normSq y := by rw [inner_re_symm]
  _ = normSq x * t * t + 2 * re âŸªx, yâŸ« * t + normSq y := by ring

/-- Another auxiliary equality related with the **Cauchyâ€“Schwarz inequality**: the square of the
seminorm of `âŸªx, yâŸ« â€¢ x - âŸªx, xâŸ« â€¢ y` is equal to `â€–xâ€– ^ 2 * (â€–xâ€– ^ 2 * â€–yâ€– ^ 2 - â€–âŸªx, yâŸ«â€– ^ 2)`.
We use `InnerProductSpace.ofCore.normSq x` etc. (defeq to `is_R_or_C.re âŸªx, xâŸ«`) instead of
`â€–xâ€– ^ 2` etc. to avoid extra rewrites when applying it to an `InnerProductSpace`. -/
theorem cauchy_schwarz_aux (x y : F) : normSqF (âŸªx, yâŸ« â€¢ x - âŸªx, xâŸ« â€¢ y)
    = normSqF x * (normSqF x * normSqF y - â€–âŸªx, yâŸ«â€– ^ 2) := by
  rw [â† @ofReal_inj ğ•œ, ofReal_normSq_eq_inner_self]
  simp only [inner_sub_sub_self, inner_smul_left, inner_smul_right, conj_ofReal, mul_sub, â†
    ofReal_normSq_eq_inner_self x, â† ofReal_normSq_eq_inner_self y]
  rw [â† mul_assoc, mul_conj, RCLike.conj_mul, mul_left_comm, â† inner_conj_symm y, mul_conj]
  push_cast
  ring

/-- **Cauchyâ€“Schwarz inequality**.
We need this for the `PreInnerProductSpace.Core` structure to prove the triangle inequality below
when showing the core is a normed group and to take the quotient.

(This is not intended for general use; see `Analysis.InnerProductSpace.Basic` for a variety of
versions of Cauchy-Schwarz for an inner product space, rather than a `PreInnerProductSpace.Core`).
-/
theorem inner_mul_inner_self_le (x y : F) : â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« := by
  suffices discrim (normSqF x) (2 * â€–âŸªx, yâŸ«_ğ•œâ€–) (normSqF y) â‰¤ 0 by
    rw [norm_inner_symm y x]
    rw [discrim, normSq, normSq, sq] at this
    linarith
  refine discrim_le_zero fun t â†¦ ?_
  by_cases hzero : âŸªx, yâŸ« = 0
  Â· simp only [â† sq, hzero, norm_zero, mul_zero, zero_mul, add_zero]
    obtain âŸ¨hx, hyâŸ© : (0 â‰¤ normSqF x âˆ§ 0 â‰¤ normSqF y) := âŸ¨inner_self_nonneg, inner_self_nonnegâŸ©
    positivity
  Â· have hzero' : â€–âŸªx, yâŸ«â€– â‰  0 := norm_ne_zero_iff.2 hzero
    convert cauchy_schwarz_aux' (ğ•œ := ğ•œ) (âŸªx, yâŸ« â€¢ x) y (t / â€–âŸªx, yâŸ«â€–) using 3
    Â· field_simp
      rw [normSq, normSq, inner_smul_right, inner_smul_left, â† mul_assoc _ _ âŸªx, xâŸ«,
        mul_conj]
      rw [â† ofReal_pow, re_ofReal_mul]
      ring
    Â· field_simp
      rw [inner_smul_left, mul_comm _ âŸªx, yâŸ«_ğ•œ, mul_conj, â† ofReal_pow, ofReal_re]
      ring

/-- (Semi)norm constructed from a `PreInnerProductSpace.Core` structure, defined to be the square
root of the scalar product. -/
def toNorm : Norm F where norm x := âˆš(re âŸªx, xâŸ«)

attribute [local instance] toNorm

theorem norm_eq_sqrt_re_inner (x : F) : â€–xâ€– = âˆš(re âŸªx, xâŸ«) := rfl

@[deprecated (since := "2025-04-22")] alias norm_eq_sqrt_inner := norm_eq_sqrt_re_inner

theorem inner_self_eq_norm_mul_norm (x : F) : re âŸªx, xâŸ« = â€–xâ€– * â€–xâ€– := by
  rw [norm_eq_sqrt_re_inner, â† sqrt_mul inner_self_nonneg, sqrt_mul_self inner_self_nonneg]

theorem sqrt_normSq_eq_norm (x : F) : âˆš(normSqF x) = â€–xâ€– := rfl

/-- Cauchyâ€“Schwarz inequality with norm -/
theorem norm_inner_le_norm (x y : F) : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– :=
  nonneg_le_nonneg_of_sq_le_sq (mul_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) <|
    calc
      â€–âŸªx, yâŸ«â€– * â€–âŸªx, yâŸ«â€– = â€–âŸªx, yâŸ«â€– * â€–âŸªy, xâŸ«â€– := by rw [norm_inner_symm]
      _ â‰¤ re âŸªx, xâŸ« * re âŸªy, yâŸ« := inner_mul_inner_self_le x y
      _ = â€–xâ€– * â€–yâ€– * (â€–xâ€– * â€–yâ€–) := by simp only [inner_self_eq_norm_mul_norm]; ring

/-- Seminormed group structure constructed from a `PreInnerProductSpace.Core` structure -/
def toSeminormedAddCommGroup : SeminormedAddCommGroup F :=
  AddGroupSeminorm.toSeminormedAddCommGroup
    { toFun := fun x => âˆš(re âŸªx, xâŸ«)
      map_zero' := by simp only [sqrt_zero, inner_zero_right, map_zero]
      neg' := fun x => by simp only [inner_neg_left, neg_neg, inner_neg_right]
      add_le' := fun x y => by
        have hâ‚ : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := norm_inner_le_norm _ _
        have hâ‚‚ : re âŸªx, yâŸ« â‰¤ â€–âŸªx, yâŸ«â€– := re_le_norm _
        have hâ‚ƒ : re âŸªx, yâŸ« â‰¤ â€–xâ€– * â€–yâ€– := hâ‚‚.trans hâ‚
        have hâ‚„ : re âŸªy, xâŸ« â‰¤ â€–xâ€– * â€–yâ€– := by rwa [â† inner_conj_symm, conj_re]
        have : â€–x + yâ€– * â€–x + yâ€– â‰¤ (â€–xâ€– + â€–yâ€–) * (â€–xâ€– + â€–yâ€–) := by
          simp only [â† inner_self_eq_norm_mul_norm, inner_add_add_self, mul_add, mul_comm, map_add]
          linarith
        exact nonneg_le_nonneg_of_sq_le_sq (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this }

attribute [local instance] toSeminormedAddCommGroup

/-- Normed space (which is actually a seminorm in general) structure constructed from a
`PreInnerProductSpace.Core` structure -/
def toNormedSpace : NormedSpace ğ•œ F where
  norm_smul_le r x := by
    rw [norm_eq_sqrt_re_inner, inner_smul_left, inner_smul_right, â† mul_assoc]
    rw [RCLike.conj_mul, â† ofReal_pow, re_ofReal_mul, sqrt_mul, â† ofReal_normSq_eq_inner_self,
      ofReal_re]
    Â· simp [sqrt_normSq_eq_norm]
    Â· positivity

@[deprecated (since := "2025-06-03")] alias toSeminormedSpace := toNormedSpace

omit c in
/-- Seminormed space core structure constructed from a `PreInnerProductSpace.Core` structure -/
lemma toSeminormedSpaceCore (c : PreInnerProductSpace.Core ğ•œ F) : SeminormedSpace.Core ğ•œ F where
  norm_nonneg x := norm_nonneg x
  norm_smul c x := by
    letI : NormedSpace ğ•œ F := toNormedSpace
    exact _root_.norm_smul c x
  norm_triangle x y := norm_add_le x y

end PreInnerProductSpace.Core

section InnerProductSpace.Core

variable [AddCommGroup F] [Module ğ•œ F] [cd : InnerProductSpace.Core ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

local notation "ext_iff" => @RCLike.ext_iff ğ•œ _

/-- Inner product defined by the `InnerProductSpace.Core` structure. We can't reuse
`InnerProductSpace.Core.toInner` because it takes `InnerProductSpace.Core` as an explicit
argument. -/
def toInner' : Inner ğ•œ F :=
  cd.toInner

attribute [local instance] toInner'

local notation "normSqF" => @normSq ğ•œ F _ _ _ _

theorem inner_self_eq_zero {x : F} : âŸªx, xâŸ« = 0 â†” x = 0 :=
  âŸ¨cd.definite _, inner_self_of_eq_zeroâŸ©

theorem normSq_eq_zero {x : F} : normSqF x = 0 â†” x = 0 :=
  Iff.trans
    (by simp only [normSq, ext_iff, map_zero, inner_self_im, and_true])
    (inner_self_eq_zero (ğ•œ := ğ•œ))

theorem inner_self_ne_zero {x : F} : âŸªx, xâŸ« â‰  0 â†” x â‰  0 :=
  inner_self_eq_zero.not

attribute [local instance] toNorm

/-- Normed group structure constructed from an `InnerProductSpace.Core` structure -/
def toNormedAddCommGroup : NormedAddCommGroup F :=
  AddGroupNorm.toNormedAddCommGroup
    { toFun := fun x => âˆš(re âŸªx, xâŸ«)
      map_zero' := by simp only [sqrt_zero, inner_zero_right, map_zero]
      neg' := fun x => by simp only [inner_neg_left, neg_neg, inner_neg_right]
      add_le' := fun x y => by
        have hâ‚ : â€–âŸªx, yâŸ«â€– â‰¤ â€–xâ€– * â€–yâ€– := norm_inner_le_norm _ _
        have hâ‚‚ : re âŸªx, yâŸ« â‰¤ â€–âŸªx, yâŸ«â€– := re_le_norm _
        have hâ‚ƒ : re âŸªx, yâŸ« â‰¤ â€–xâ€– * â€–yâ€– := hâ‚‚.trans hâ‚
        have hâ‚„ : re âŸªy, xâŸ« â‰¤ â€–xâ€– * â€–yâ€– := by rwa [â† inner_conj_symm, conj_re]
        have : â€–x + yâ€– * â€–x + yâ€– â‰¤ (â€–xâ€– + â€–yâ€–) * (â€–xâ€– + â€–yâ€–) := by
          simp only [â† inner_self_eq_norm_mul_norm, inner_add_add_self, mul_add, mul_comm, map_add]
          linarith
        exact nonneg_le_nonneg_of_sq_le_sq (add_nonneg (sqrt_nonneg _) (sqrt_nonneg _)) this
      eq_zero_of_map_eq_zero' := fun _ hx =>
        normSq_eq_zero.1 <| (sqrt_eq_zero inner_self_nonneg).1 hx }

section

attribute [local instance] toNormedAddCommGroup

omit cd in
/-- Normed space core structure constructed from an `InnerProductSpace.Core` structure -/
lemma toNormedSpaceCore (cd : InnerProductSpace.Core ğ•œ F) : NormedSpace.Core ğ•œ F where
  norm_nonneg x := norm_nonneg x
  norm_eq_zero_iff x := norm_eq_zero
  norm_smul c x := by
    letI : NormedSpace ğ•œ F := toNormedSpace
    exact _root_.norm_smul c x
  norm_triangle x y := norm_add_le x y

end

/-- In a topological vector space, if the unit ball of a continuous inner product is von Neumann
bounded, then the inner product defines the same topology as the original one. -/
lemma topology_eq
    [tF : TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
    (h : ContinuousAt (fun (v : F) â†¦ cd.inner v v) 0)
    (h' : IsVonNBounded ğ•œ {v : F | re (cd.inner v v) < 1}) :
    tF = cd.toNormedAddCommGroup.toMetricSpace.toUniformSpace.toTopologicalSpace := by
  let p : Seminorm ğ•œ F := @normSeminorm ğ•œ F _ cd.toNormedAddCommGroup.toSeminormedAddCommGroup
    InnerProductSpace.Core.toNormedSpace
  suffices WithSeminorms (fun (i : Fin 1) â†¦ p) by
    rw [(SeminormFamily.withSeminorms_iff_topologicalSpace_eq_iInf _).1 this]
    simp
  have : p.ball 0 1 = {v | re (cd.inner v v) < 1} := by
    ext v
    simp only [ball_normSeminorm, Metric.mem_ball, dist_eq_norm, sub_zero, Set.mem_setOf_eq, p]
    change âˆš(re (cd.inner v v)) < 1 â†” re (cd.inner v v) < 1
    conv_lhs => rw [show (1 : â„) = âˆš 1 by simp]
    rw [sqrt_lt_sqrt_iff]
    exact InnerProductSpace.Core.inner_self_nonneg
  rw [withSeminorms_iff_mem_nhds_isVonNBounded, this]
  refine âŸ¨?_, h'âŸ©
  have A : ContinuousAt (fun (v : F) â†¦ re (cd.inner v v)) 0 := by fun_prop
  have B : Set.Iio 1 âˆˆ ğ“ (re (cd.inner 0 0)) := by
    simp only [InnerProductSpace.Core.inner_zero_left, map_zero]
    exact Iio_mem_nhds (by positivity)
  exact A B

/-- Normed space structure constructed from an `InnerProductSpace.Core` structure, adjusting the
topology to make sure it is defeq to an already existing topology. -/
@[reducible] def toNormedAddCommGroupOfTopology
    [tF : TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
    (h : ContinuousAt (fun (v : F) â†¦ cd.inner v v) 0)
    (h' : IsVonNBounded ğ•œ {v : F | re (cd.inner v v) < 1}) :
    NormedAddCommGroup F :=
  NormedAddCommGroup.ofCoreReplaceTopology cd.toNormedSpaceCore (cd.topology_eq h h')

/-- Normed space structure constructed from an `InnerProductSpace.Core` structure, adjusting the
topology to make sure it is defeq to an already existing topology. -/
@[reducible] def toNormedSpaceOfTopology
    [tF : TopologicalSpace F] [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
    (h : ContinuousAt (fun (v : F) â†¦ cd.inner v v) 0)
    (h' : IsVonNBounded ğ•œ {v : F | re (cd.inner v v) < 1}) :
    letI : NormedAddCommGroup F := cd.toNormedAddCommGroupOfTopology h h';
    NormedSpace ğ•œ F :=
  letI : NormedAddCommGroup F := cd.toNormedAddCommGroupOfTopology h h'
  { norm_smul_le r x := by
      rw [norm_eq_sqrt_re_inner, inner_smul_left, inner_smul_right, â† mul_assoc]
      rw [RCLike.conj_mul, â† ofReal_pow, re_ofReal_mul, sqrt_mul, â† ofReal_normSq_eq_inner_self,
        ofReal_re]
      Â· simp [sqrt_normSq_eq_norm]
      Â· positivity }

end InnerProductSpace.Core

end InnerProductSpace.Core

section

attribute [local instance] InnerProductSpace.Core.toSeminormedAddCommGroup

/-- Given a `PreInnerProductSpace.Core` structure on a space, one can use it to turn
the space into a pre-inner product space (i.e., `SeminormedAddCommGroup` and `InnerProductSpace`).
The `SeminormedAddCommGroup` structure is expected to already be defined with
`InnerProductSpace.ofCore.toSeminormedAddCommGroup`. -/
def InnerProductSpace.ofCore [AddCommGroup F] [Module ğ•œ F] (cd : PreInnerProductSpace.Core ğ•œ F) :
    InnerProductSpace ğ•œ F :=
  letI : NormedSpace ğ•œ F := InnerProductSpace.Core.toNormedSpace
  { cd with
    norm_sq_eq_re_inner := fun x => by
      have hâ‚ : â€–xâ€– ^ 2 = âˆš(re (cd.inner x x)) ^ 2 := rfl
      have hâ‚‚ : 0 â‰¤ re (cd.inner x x) := InnerProductSpace.Core.inner_self_nonneg
      simp [hâ‚, sq_sqrt, hâ‚‚] }

end

/-- Given an `InnerProductSpace.Core` structure on a space with a topology, one can use it to turn
the space into an inner product space. The `NormedAddCommGroup` structure is expected
to already be defined with `InnerProductSpace.ofCore.toNormedAddCommGroupOfTopology`. -/
def InnerProductSpace.ofCoreOfTopology [AddCommGroup F] [hF : Module ğ•œ F] [TopologicalSpace F]
    [IsTopologicalAddGroup F] [ContinuousConstSMul ğ•œ F]
    (cd : InnerProductSpace.Core ğ•œ F)
    (h : ContinuousAt (fun (v : F) â†¦ cd.inner v v) 0)
    (h' : IsVonNBounded ğ•œ {v : F | re (cd.inner v v) < 1}) :
    letI : NormedAddCommGroup F := cd.toNormedAddCommGroupOfTopology h h';
    InnerProductSpace ğ•œ F :=
  letI : NormedAddCommGroup F := cd.toNormedAddCommGroupOfTopology h h'
  letI : NormedSpace ğ•œ F := cd.toNormedSpaceOfTopology h h'
  { cd with
    norm_sq_eq_re_inner := fun x => by
      have hâ‚ : â€–xâ€– ^ 2 = âˆš(re (cd.inner x x)) ^ 2 := rfl
      have hâ‚‚ : 0 â‰¤ re (cd.inner x x) := InnerProductSpace.Core.inner_self_nonneg
      simp [hâ‚, sq_sqrt, hâ‚‚] }

/-- A Hilbert space is a complete normed inner product space. -/
@[variable_alias]
structure HilbertSpace (ğ•œ E : Type*) [RCLike ğ•œ]
  [NormedAddCommGroup E] [InnerProductSpace ğ•œ E] [CompleteSpace E]

end
