/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.InnerProductSpace.Orthogonal
import Mathlib.Analysis.InnerProductSpace.Symmetric
import Mathlib.Analysis.NormedSpace.RCLike
import Mathlib.Analysis.RCLike.Lemmas
import Mathlib.Algebra.DirectSum.Decomposition

/-!
# The orthogonal projection

Given a nonempty complete subspace `K` of an inner product space `E`, this file constructs
`K.orthogonalProjection : E â†’L[ğ•œ] K`, the orthogonal projection of `E` onto `K`.  This map
satisfies: for any point `u` in `E`, the point `v = K.orthogonalProjection u` in `K` minimizes the
distance `â€–u - vâ€–` to `u`.

Also a linear isometry equivalence `K.reflection : E â‰ƒâ‚—áµ¢[ğ•œ] E` is constructed, by choosing, for
each `u : E`, the point `K.reflection u` to satisfy
`u + (K.reflection u) = 2 â€¢ K.orthogonalProjection u`.

Basic API for `orthogonalProjection` and `reflection` is developed.

Next, the orthogonal projection is used to prove a series of more subtle lemmas about the
orthogonal complement of complete subspaces of `E` (the orthogonal complement itself was
defined in `Analysis.InnerProductSpace.Orthogonal`); the lemma
`Submodule.sup_orthogonal_of_completeSpace`, stating that for a complete subspace `K` of `E` we have
`K âŠ” Ká—® = âŠ¤`, is a typical example.

## References

The orthogonal projection construction is adapted from
* [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
* [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/


noncomputable section

open InnerProductSpace

open RCLike Real Filter

open LinearMap (ker range)

open Topology Finsupp

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y
local notation "absR" => @abs â„ _ _

/-! ### Orthogonal projection in inner product spaces -/


-- FIXME this monolithic proof causes a deterministic timeout with `-T50000`
-- It should be broken in a sequence of more manageable pieces,
-- perhaps with individual statements for the three steps below.
/-- **Existence of minimizers**, aka the **Hilbert projection theorem**.

Let `u` be a point in a real inner product space, and let `K` be a nonempty complete convex subset.
Then there exists a (unique) `v` in `K` that minimizes the distance `â€–u - vâ€–` to `u`. -/
theorem exists_norm_eq_iInf_of_complete_convex {K : Set F} (ne : K.Nonempty) (hâ‚ : IsComplete K)
    (hâ‚‚ : Convex â„ K) : âˆ€ u : F, âˆƒ v âˆˆ K, â€–u - vâ€– = â¨… w : K, â€–u - wâ€– := fun u => by
  let Î´ := â¨… w : K, â€–u - wâ€–
  letI : Nonempty K := ne.to_subtype
  have zero_le_Î´ : 0 â‰¤ Î´ := le_ciInf fun _ => norm_nonneg _
  have Î´_le : âˆ€ w : K, Î´ â‰¤ â€–u - wâ€– := ciInf_le âŸ¨0, Set.forall_mem_range.2 fun _ => norm_nonneg _âŸ©
  have Î´_le' : âˆ€ w âˆˆ K, Î´ â‰¤ â€–u - wâ€– := fun w hw => Î´_le âŸ¨w, hwâŸ©
  -- Step 1: since `Î´` is the infimum, can find a sequence `w : â„• â†’ K` in `K`
  -- such that `â€–u - w nâ€– < Î´ + 1 / (n + 1)` (which implies `â€–u - w nâ€– --> Î´`);
  -- maybe this should be a separate lemma
  have exists_seq : âˆƒ w : â„• â†’ K, âˆ€ n, â€–u - w nâ€– < Î´ + 1 / (n + 1) := by
    have hÎ´ : âˆ€ n : â„•, Î´ < Î´ + 1 / (n + 1) := fun n =>
      lt_add_of_le_of_pos le_rfl Nat.one_div_pos_of_nat
    have h := fun n => exists_lt_of_ciInf_lt (hÎ´ n)
    let w : â„• â†’ K := fun n => Classical.choose (h n)
    exact âŸ¨w, fun n => Classical.choose_spec (h n)âŸ©
  rcases exists_seq with âŸ¨w, hwâŸ©
  have norm_tendsto : Tendsto (fun n => â€–u - w nâ€–) atTop (ğ“ Î´) := by
    have h : Tendsto (fun _ : â„• => Î´) atTop (ğ“ Î´) := tendsto_const_nhds
    have h' : Tendsto (fun n : â„• => Î´ + 1 / (n + 1)) atTop (ğ“ Î´) := by
      convert h.add tendsto_one_div_add_atTop_nhds_zero_nat
      simp only [add_zero]
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le h h' (fun x => Î´_le _) fun x => le_of_lt (hw _)
  -- Step 2: Prove that the sequence `w : â„• â†’ K` is a Cauchy sequence
  have seq_is_cauchy : CauchySeq fun n => (w n : F) := by
    rw [cauchySeq_iff_le_tendsto_0]
    -- splits into three goals
    let b := fun n : â„• => 8 * Î´ * (1 / (n + 1)) + 4 * (1 / (n + 1)) * (1 / (n + 1))
    use fun n => âˆš(b n)
    constructor
    -- first goal :  `âˆ€ (n : â„•), 0 â‰¤ âˆš(b n)`
    Â· intro n
      exact sqrt_nonneg _
    constructor
    -- second goal : `âˆ€ (n m N : â„•), N â‰¤ n â†’ N â‰¤ m â†’ dist â†‘(w n) â†‘(w m) â‰¤ âˆš(b N)`
    Â· intro p q N hp hq
      let wp := (w p : F)
      let wq := (w q : F)
      let a := u - wq
      let b := u - wp
      let half := 1 / (2 : â„)
      let div := 1 / ((N : â„) + 1)
      have :
        4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– + â€–wp - wqâ€– * â€–wp - wqâ€– =
          2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) :=
        calc
          4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– + â€–wp - wqâ€– * â€–wp - wqâ€– =
              2 * â€–u - half â€¢ (wq + wp)â€– * (2 * â€–u - half â€¢ (wq + wp)â€–) + â€–wp - wqâ€– * â€–wp - wqâ€– :=
            by ring
          _ =
              absR 2 * â€–u - half â€¢ (wq + wp)â€– * (absR 2 * â€–u - half â€¢ (wq + wp)â€–) +
                â€–wp - wqâ€– * â€–wp - wqâ€– := by
            rw [abs_of_nonneg]
            exact zero_le_two
          _ =
              â€–(2 : â„) â€¢ (u - half â€¢ (wq + wp))â€– * â€–(2 : â„) â€¢ (u - half â€¢ (wq + wp))â€– +
                â€–wp - wqâ€– * â€–wp - wqâ€– := by simp [norm_smul]
          _ = â€–a + bâ€– * â€–a + bâ€– + â€–a - bâ€– * â€–a - bâ€– := by
            rw [smul_sub, smul_smul, mul_one_div_cancel (_root_.two_ne_zero : (2 : â„) â‰  0), â†
              one_add_one_eq_two, add_smul]
            simp only [one_smul]
            have eqâ‚ : wp - wq = a - b := (sub_sub_sub_cancel_left _ _ _).symm
            have eqâ‚‚ : u + u - (wq + wp) = a + b := by
              show u + u - (wq + wp) = u - wq + (u - wp)
              abel
            rw [eqâ‚, eqâ‚‚]
          _ = 2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) := parallelogram_law_with_norm â„ _ _
      have eq : Î´ â‰¤ â€–u - half â€¢ (wq + wp)â€– := by
        rw [smul_add]
        apply Î´_le'
        apply hâ‚‚
        repeat' exact Subtype.mem _
        repeat' exact le_of_lt one_half_pos
        exact add_halves 1
      have eqâ‚ : 4 * Î´ * Î´ â‰¤ 4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– := by
        simp_rw [mul_assoc]
        gcongr
      have eqâ‚‚ : â€–aâ€– â‰¤ Î´ + div :=
          le_trans (le_of_lt <| hw q) (add_le_add_left (Nat.one_div_le_one_div hq) _)
      have eqâ‚‚' : â€–bâ€– â‰¤ Î´ + div :=
          le_trans (le_of_lt <| hw p) (add_le_add_left (Nat.one_div_le_one_div hp) _)
      rw [dist_eq_norm]
      apply nonneg_le_nonneg_of_sq_le_sq
      Â· exact sqrt_nonneg _
      rw [mul_self_sqrt]
      Â· calc
        â€–wp - wqâ€– * â€–wp - wqâ€– =
            2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) - 4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– := by
          simp [â† this]
        _ â‰¤ 2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) - 4 * Î´ * Î´ := by gcongr
        _ â‰¤ 2 * ((Î´ + div) * (Î´ + div) + (Î´ + div) * (Î´ + div)) - 4 * Î´ * Î´ := by gcongr
        _ = 8 * Î´ * div + 4 * div * div := by ring
      positivity
    -- third goal : `Tendsto (fun (n : â„•) => âˆš(b n)) atTop (ğ“ 0)`
    suffices Tendsto (fun x â†¦ âˆš(8 * Î´ * x + 4 * x * x) : â„ â†’ â„) (ğ“ 0) (ğ“ 0)
      from this.comp tendsto_one_div_add_atTop_nhds_zero_nat
    exact Continuous.tendsto' (by fun_prop) _ _ (by simp)
  -- Step 3: By completeness of `K`, let `w : â„• â†’ K` converge to some `v : K`.
  -- Prove that it satisfies all requirements.
  rcases cauchySeq_tendsto_of_isComplete hâ‚ (fun n => Subtype.mem _) seq_is_cauchy with
    âŸ¨v, hv, w_tendstoâŸ©
  use v
  use hv
  have h_cont : Continuous fun v => â€–u - vâ€– :=
    Continuous.comp continuous_norm (Continuous.sub continuous_const continuous_id)
  have : Tendsto (fun n => â€–u - w nâ€–) atTop (ğ“ â€–u - vâ€–) := by
    convert Tendsto.comp h_cont.continuousAt w_tendsto
  exact tendsto_nhds_unique this norm_tendsto

/-- Characterization of minimizers for the projection on a convex set in a real inner product
space. -/
theorem norm_eq_iInf_iff_real_inner_le_zero {K : Set F} (h : Convex â„ K) {u : F} {v : F}
    (hv : v âˆˆ K) : (â€–u - vâ€– = â¨… w : K, â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
  letI : Nonempty K := âŸ¨âŸ¨v, hvâŸ©âŸ©
  constructor
  Â· intro eq w hw
    let Î´ := â¨… w : K, â€–u - wâ€–
    let p := âŸªu - v, w - vâŸ«_â„
    let q := â€–w - vâ€– ^ 2
    have Î´_le (w : K) : Î´ â‰¤ â€–u - wâ€– := ciInf_le âŸ¨0, fun _ âŸ¨_, hâŸ© => h â–¸ norm_nonneg _âŸ© _
    have Î´_le' (w) (hw : w âˆˆ K) : Î´ â‰¤ â€–u - wâ€– := Î´_le âŸ¨w, hwâŸ©
    have (Î¸ : â„) (hÎ¸â‚ : 0 < Î¸) (hÎ¸â‚‚ : Î¸ â‰¤ 1) : 2 * p â‰¤ Î¸ * q := by
      have : â€–u - vâ€– ^ 2 â‰¤ â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 :=
        calc â€–u - vâ€– ^ 2
          _ â‰¤ â€–u - (Î¸ â€¢ w + (1 - Î¸) â€¢ v)â€– ^ 2 := by
            simp only [sq]; apply mul_self_le_mul_self (norm_nonneg _)
            rw [eq]; apply Î´_le'
            apply h hw hv
            exacts [le_of_lt hÎ¸â‚, sub_nonneg.2 hÎ¸â‚‚, add_sub_cancel _ _]
          _ = â€–u - v - Î¸ â€¢ (w - v)â€– ^ 2 := by
            have : u - (Î¸ â€¢ w + (1 - Î¸) â€¢ v) = u - v - Î¸ â€¢ (w - v) := by
              rw [smul_sub, sub_smul, one_smul]
              simp only [sub_eq_add_neg, add_comm, add_left_comm, add_assoc, neg_add_rev]
            rw [this]
          _ = â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 := by
            rw [@norm_sub_sq â„, inner_smul_right, norm_smul]
            simp only [sq]
            show
              â€–u - vâ€– * â€–u - vâ€– - 2 * (Î¸ * âŸªu - v, w - vâŸ«_â„) +
                absR Î¸ * â€–w - vâ€– * (absR Î¸ * â€–w - vâ€–) =
              â€–u - vâ€– * â€–u - vâ€– - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * (â€–w - vâ€– * â€–w - vâ€–)
            rw [abs_of_pos hÎ¸â‚]; ring
      have eqâ‚ :
        â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 =
          â€–u - vâ€– ^ 2 + (Î¸ * Î¸ * â€–w - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„) := by
        abel
      rw [eqâ‚, le_add_iff_nonneg_right] at this
      have eqâ‚‚ :
        Î¸ * Î¸ * â€–w - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ =
          Î¸ * (Î¸ * â€–w - vâ€– ^ 2 - 2 * âŸªu - v, w - vâŸ«_â„) := by ring
      rw [eqâ‚‚] at this
      exact le_of_sub_nonneg (nonneg_of_mul_nonneg_right this hÎ¸â‚)
    by_cases hq : q = 0
    Â· rw [hq] at this
      have : p â‰¤ 0 := by
        have := this (1 : â„) (by norm_num) (by norm_num)
        linarith
      exact this
    Â· have q_pos : 0 < q := lt_of_le_of_ne (sq_nonneg _) fun h â†¦ hq h.symm
      by_contra hp
      rw [not_le] at hp
      let Î¸ := min (1 : â„) (p / q)
      have eqâ‚ : Î¸ * q â‰¤ p :=
        calc
          Î¸ * q â‰¤ p / q * q := mul_le_mul_of_nonneg_right (min_le_right _ _) (sq_nonneg _)
          _ = p := div_mul_cancelâ‚€ _ hq
      have : 2 * p â‰¤ p :=
        calc
          2 * p â‰¤ Î¸ * q := by
            exact this Î¸ (lt_min (by norm_num) (div_pos hp q_pos)) (by norm_num [Î¸])
          _ â‰¤ p := eqâ‚
      linarith
  Â· intro h
    apply le_antisymm
    Â· apply le_ciInf
      intro w
      apply nonneg_le_nonneg_of_sq_le_sq (norm_nonneg _)
      have := h w w.2
      calc
        â€–u - vâ€– * â€–u - vâ€– â‰¤ â€–u - vâ€– * â€–u - vâ€– - 2 * âŸªu - v, w - vâŸ«_â„ := by linarith
        _ â‰¤ â€–u - vâ€– ^ 2 - 2 * âŸªu - v, w - vâŸ«_â„ + â€–w - vâ€– ^ 2 := by
          rw [sq]
          refine le_add_of_nonneg_right ?_
          exact sq_nonneg _
        _ = â€–u - v - (w - v)â€– ^ 2 := (@norm_sub_sq â„ _ _ _ _ _ _).symm
        _ = â€–u - wâ€– * â€–u - wâ€– := by
          have : u - v - (w - v) = u - w := by abel
          rw [this, sq]
    Â· show â¨… w : K, â€–u - wâ€– â‰¤ (fun w : K => â€–u - wâ€–) âŸ¨v, hvâŸ©
      apply ciInf_le
      use 0
      rintro y âŸ¨z, rflâŸ©
      exact norm_nonneg _

variable (K : Submodule ğ•œ E)

namespace Submodule

/-- Existence of projections on complete subspaces.
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a (unique) `v` in `K` that minimizes the distance `â€–u - vâ€–` to `u`.
This point `v` is usually called the orthogonal projection of `u` onto `K`.
-/
theorem exists_norm_eq_iInf_of_complete_subspace (h : IsComplete (â†‘K : Set E)) :
    âˆ€ u : E, âˆƒ v âˆˆ K, â€–u - vâ€– = â¨… w : (K : Set E), â€–u - wâ€– := by
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : Module â„ E := RestrictScalars.module â„ ğ•œ E
  let K' : Submodule â„ E := Submodule.restrictScalars â„ K
  exact exists_norm_eq_iInf_of_complete_convex âŸ¨0, K'.zero_memâŸ© h K'.convex

/-- Characterization of minimizers in the projection on a subspace, in the real case.
Let `u` be a point in a real inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `â€–u - vâ€–` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`).
This is superseded by `norm_eq_iInf_iff_inner_eq_zero` that gives the same conclusion over
any `RCLike` field.
-/
theorem norm_eq_iInf_iff_real_inner_eq_zero (K : Submodule â„ F) {u : F} {v : F} (hv : v âˆˆ K) :
    (â€–u - vâ€– = â¨… w : (â†‘K : Set F), â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ«_â„ = 0 :=
  Iff.intro
    (by
      intro h
      have h : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
        rwa [norm_eq_iInf_iff_real_inner_le_zero] at h
        exacts [K.convex, hv]
      intro w hw
      have le : âŸªu - v, wâŸ«_â„ â‰¤ 0 := by
        let w' := w + v
        have : w' âˆˆ K := Submodule.add_mem _ hw hv
        have hâ‚ := h w' this
        have hâ‚‚ : w' - v = w := by
          simp only [w', add_neg_cancel_right, sub_eq_add_neg]
        rw [hâ‚‚] at hâ‚
        exact hâ‚
      have ge : âŸªu - v, wâŸ«_â„ â‰¥ 0 := by
        let w'' := -w + v
        have : w'' âˆˆ K := Submodule.add_mem _ (Submodule.neg_mem _ hw) hv
        have hâ‚ := h w'' this
        have hâ‚‚ : w'' - v = -w := by
          simp only [w'', neg_inj, add_neg_cancel_right, sub_eq_add_neg]
        rw [hâ‚‚, inner_neg_right] at hâ‚
        linarith
      exact le_antisymm le ge)
    (by
      intro h
      have : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
        intro w hw
        let w' := w - v
        have : w' âˆˆ K := Submodule.sub_mem _ hw hv
        have hâ‚ := h w' this
        exact le_of_eq hâ‚
      rwa [norm_eq_iInf_iff_real_inner_le_zero]
      exacts [Submodule.convex _, hv])

/-- Characterization of minimizers in the projection on a subspace.
Let `u` be a point in an inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `â€–u - vâ€–` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`)
-/
theorem norm_eq_iInf_iff_inner_eq_zero {u : E} {v : E} (hv : v âˆˆ K) :
    (â€–u - vâ€– = â¨… w : K, â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0 := by
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : Module â„ E := RestrictScalars.module â„ ğ•œ E
  let K' : Submodule â„ E := K.restrictScalars â„
  constructor
  Â· intro H
    have A : âˆ€ w âˆˆ K, re âŸªu - v, wâŸ« = 0 := (K'.norm_eq_iInf_iff_real_inner_eq_zero hv).1 H
    intro w hw
    apply RCLike.ext
    Â· simp [A w hw]
    Â· symm
      calc
        im (0 : ğ•œ) = 0 := im.map_zero
        _ = re âŸªu - v, (-I : ğ•œ) â€¢ wâŸ« := (A _ (K.smul_mem (-I) hw)).symm
        _ = re (-I * âŸªu - v, wâŸ«) := by rw [inner_smul_right]
        _ = im âŸªu - v, wâŸ« := by simp
  Â· intro H
    have : âˆ€ w âˆˆ K', âŸªu - v, wâŸ«_â„ = 0 := by
      intro w hw
      rw [real_inner_eq_re_inner, H w hw]
      exact zero_re
    exact (K'.norm_eq_iInf_iff_real_inner_eq_zero hv).2 this

/-- A subspace `K : Submodule ğ•œ E` has an orthogonal projection if every vector `v : E` admits an
orthogonal projection to `K`. -/
class HasOrthogonalProjection (K : Submodule ğ•œ E) : Prop where
  exists_orthogonal (v : E) : âˆƒ w âˆˆ K, v - w âˆˆ Ká—®

instance (priority := 100) HasOrthogonalProjection.ofCompleteSpace [CompleteSpace K] :
    K.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases K.exists_norm_eq_iInf_of_complete_subspace (completeSpace_coe_iff_isComplete.mp â€¹_â€º) v
      with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨w, hwK, (K.mem_orthogonal' _).2 ?_âŸ©
    rwa [â† K.norm_eq_iInf_iff_inner_eq_zero hwK]

instance [K.HasOrthogonalProjection] : Ká—®.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) v with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨_, hw, ?_âŸ©
    rw [sub_sub_cancel]
    exact K.le_orthogonal_orthogonal hwK

instance HasOrthogonalProjection.map_linearIsometryEquiv [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
    (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')).HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) (f.symm v) with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨f w, Submodule.mem_map_of_mem hwK, Set.forall_mem_image.2 fun u hu â†¦ ?_âŸ©
    erw [â† f.symm.inner_map_map, f.symm_apply_apply, map_sub, f.symm_apply_apply, hw u hu]

instance HasOrthogonalProjection.map_linearIsometryEquiv' [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
    (K.map f.toLinearIsometry).HasOrthogonalProjection :=
  HasOrthogonalProjection.map_linearIsometryEquiv K f

instance : (âŠ¤ : Submodule ğ•œ E).HasOrthogonalProjection := âŸ¨fun v â†¦ âŸ¨v, trivial, by simpâŸ©âŸ©

section orthogonalProjection

variable [K.HasOrthogonalProjection]

/-- The orthogonal projection onto a complete subspace, as an
unbundled function.  This definition is only intended for use in
setting up the bundled version `orthogonalProjection` and should not
be used once that is defined. -/
def orthogonalProjectionFn (v : E) :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose

variable {K}

/-- The unbundled orthogonal projection is in the given subspace.
This lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_mem (v : E) : K.orthogonalProjectionFn v âˆˆ K :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.left

/-- The characterization of the unbundled orthogonal projection.  This
lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_inner_eq_zero (v : E) :
    âˆ€ w âˆˆ K, âŸªv - K.orthogonalProjectionFn v, wâŸ« = 0 :=
  (K.mem_orthogonal' _).1 (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.right

/-- The unbundled orthogonal projection is the unique point in `K`
with the orthogonality property.  This lemma is only intended for use
in setting up the bundled version and should not be used once that is
defined. -/
theorem eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero {u v : E} (hvm : v âˆˆ K)
    (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) : K.orthogonalProjectionFn u = v := by
  rw [â† sub_eq_zero, â† @inner_self_eq_zero ğ•œ]
  have hvs : K.orthogonalProjectionFn u - v âˆˆ K :=
    Submodule.sub_mem K (orthogonalProjectionFn_mem u) hvm
  have huo : âŸªu - K.orthogonalProjectionFn u, K.orthogonalProjectionFn u - vâŸ« = 0 :=
    orthogonalProjectionFn_inner_eq_zero u _ hvs
  have huv : âŸªu - v, K.orthogonalProjectionFn u - vâŸ« = 0 := hvo _ hvs
  have houv : âŸªu - v - (u - K.orthogonalProjectionFn u), K.orthogonalProjectionFn u - vâŸ« = 0 := by
    rw [inner_sub_left, huo, huv, sub_zero]
  rwa [sub_sub_sub_cancel_left] at houv

variable (K)

theorem orthogonalProjectionFn_norm_sq (v : E) :
    â€–vâ€– * â€–vâ€– =
      â€–v - K.orthogonalProjectionFn vâ€– * â€–v - K.orthogonalProjectionFn vâ€– +
        â€–K.orthogonalProjectionFn vâ€– * â€–K.orthogonalProjectionFn vâ€– := by
  set p := K.orthogonalProjectionFn v
  have h' : âŸªv - p, pâŸ« = 0 :=
    orthogonalProjectionFn_inner_eq_zero _ _ (orthogonalProjectionFn_mem v)
  convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (v - p) p h' using 2 <;> simp

/-- The orthogonal projection onto a complete subspace. -/
def orthogonalProjection : E â†’L[ğ•œ] K :=
  LinearMap.mkContinuous
    { toFun := fun v => âŸ¨K.orthogonalProjectionFn v, orthogonalProjectionFn_mem vâŸ©
      map_add' := fun x y => by
        have hm : K.orthogonalProjectionFn x + K.orthogonalProjectionFn y âˆˆ K :=
          Submodule.add_mem K (orthogonalProjectionFn_mem x) (orthogonalProjectionFn_mem y)
        have ho :
          âˆ€ w âˆˆ K, âŸªx + y - (K.orthogonalProjectionFn x + K.orthogonalProjectionFn y), wâŸ« = 0 := by
          intro w hw
          rw [add_sub_add_comm, inner_add_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            orthogonalProjectionFn_inner_eq_zero _ w hw, add_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho]
      map_smul' := fun c x => by
        have hm : c â€¢ K.orthogonalProjectionFn x âˆˆ K :=
          Submodule.smul_mem K _ (orthogonalProjectionFn_mem x)
        have ho : âˆ€ w âˆˆ K, âŸªc â€¢ x - c â€¢ K.orthogonalProjectionFn x, wâŸ« = 0 := by
          intro w hw
          rw [â† smul_sub, inner_smul_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            mul_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho] }
    1 fun x => by
    simp only [one_mul, LinearMap.coe_mk]
    refine le_of_pow_le_pow_leftâ‚€ two_ne_zero (norm_nonneg _) ?_
    change â€–K.orthogonalProjectionFn xâ€– ^ 2 â‰¤ â€–xâ€– ^ 2
    nlinarith [K.orthogonalProjectionFn_norm_sq x]

variable {K}

@[simp]
theorem orthogonalProjectionFn_eq (v : E) :
    K.orthogonalProjectionFn v = (K.orthogonalProjection v : E) :=
  rfl

/-- The characterization of the orthogonal projection. -/
@[simp]
theorem orthogonalProjection_inner_eq_zero (v : E) :
    âˆ€ w âˆˆ K, âŸªv - K.orthogonalProjection v, wâŸ« = 0 :=
  orthogonalProjectionFn_inner_eq_zero v

/-- The difference of `v` from its orthogonal projection onto `K` is in `Ká—®`. -/
@[simp]
theorem sub_orthogonalProjection_mem_orthogonal (v : E) : v - K.orthogonalProjection v âˆˆ Ká—® := by
  intro w hw
  rw [inner_eq_zero_symm]
  exact orthogonalProjection_inner_eq_zero _ _ hw

/-- The orthogonal projection is the unique point in `K` with the
orthogonality property. -/
theorem eq_orthogonalProjection_of_mem_of_inner_eq_zero {u v : E} (hvm : v âˆˆ K)
    (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) : (K.orthogonalProjection u : E) = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hvm hvo

/-- A point in `K` with the orthogonality property (here characterized in terms of `Ká—®`) must be the
orthogonal projection. -/
theorem eq_orthogonalProjection_of_mem_orthogonal {u v : E} (hv : v âˆˆ K)
    (hvo : u - v âˆˆ Ká—®) : (K.orthogonalProjection u : E) = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hv <| (Submodule.mem_orthogonal' _ _).1 hvo

/-- A point in `K` with the orthogonality property (here characterized in terms of `Ká—®`) must be the
orthogonal projection. -/
theorem eq_orthogonalProjection_of_mem_orthogonal' {u v z : E}
    (hv : v âˆˆ K) (hz : z âˆˆ Ká—®) (hu : u = v + z) : (K.orthogonalProjection u : E) = v :=
  eq_orthogonalProjection_of_mem_orthogonal hv (by simpa [hu] )

@[simp]
theorem orthogonalProjection_orthogonal_val (u : E) :
    (Ká—®.orthogonalProjection u : E) = u - K.orthogonalProjection u :=
  eq_orthogonalProjection_of_mem_orthogonal' (sub_orthogonalProjection_mem_orthogonal _)
    (K.le_orthogonal_orthogonal (K.orthogonalProjection u).2) <| by simp

theorem orthogonalProjection_orthogonal (u : E) :
    Ká—®.orthogonalProjection u =
      âŸ¨u - K.orthogonalProjection u, sub_orthogonalProjection_mem_orthogonal _âŸ© :=
  Subtype.eq <| orthogonalProjection_orthogonal_val _

/-- The orthogonal projection of `y` on `U` minimizes the distance `â€–y - xâ€–` for `x âˆˆ U`. -/
theorem orthogonalProjection_minimal {U : Submodule ğ•œ E} [U.HasOrthogonalProjection] (y : E) :
    â€–y - U.orthogonalProjection yâ€– = â¨… x : U, â€–y - xâ€– := by
  rw [U.norm_eq_iInf_iff_inner_eq_zero (Submodule.coe_mem _)]
  exact orthogonalProjection_inner_eq_zero _

/-- The orthogonal projections onto equal subspaces are coerced back to the same point in `E`. -/
theorem eq_orthogonalProjection_of_eq_submodule {K' : Submodule ğ•œ E} [K'.HasOrthogonalProjection]
    (h : K = K') (u : E) : (K.orthogonalProjection u : E) = (K'.orthogonalProjection u : E) := by
  subst h; rfl

/-- The orthogonal projection sends elements of `K` to themselves. -/
@[simp]
theorem orthogonalProjection_mem_subspace_eq_self (v : K) : K.orthogonalProjection v = v := by
  ext
  apply eq_orthogonalProjection_of_mem_of_inner_eq_zero <;> simp

/-- A point equals its orthogonal projection if and only if it lies in the subspace. -/
theorem orthogonalProjection_eq_self_iff {v : E} : (K.orthogonalProjection v : E) = v â†” v âˆˆ K := by
  refine âŸ¨fun h => ?_, fun h => eq_orthogonalProjection_of_mem_of_inner_eq_zero h ?_âŸ©
  Â· rw [â† h]
    simp
  Â· simp

@[simp]
theorem orthogonalProjection_eq_zero_iff {v : E} : K.orthogonalProjection v = 0 â†” v âˆˆ Ká—® := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ Subtype.eq <| eq_orthogonalProjection_of_mem_orthogonal
    (zero_mem _) ?_âŸ©
  Â· simpa [h] using sub_orthogonalProjection_mem_orthogonal (K := K) v
  Â· simpa

@[simp]
theorem ker_orthogonalProjection : LinearMap.ker K.orthogonalProjection = Ká—® := by
  ext; exact orthogonalProjection_eq_zero_iff

theorem _root_.LinearIsometry.map_orthogonalProjection {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â†’â‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection] [(p.map f.toLinearMap).HasOrthogonalProjection]
    (x : E) : f (p.orthogonalProjection x) = (p.map f.toLinearMap).orthogonalProjection (f x) := by
  refine (eq_orthogonalProjection_of_mem_of_inner_eq_zero ?_ fun y hy => ?_).symm
  Â· refine Submodule.apply_coe_mem_map _ _
  rcases hy with âŸ¨x', hx', rfl : f x' = yâŸ©
  rw [â† f.map_sub, f.inner_map_map, orthogonalProjection_inner_eq_zero x x' hx']

theorem _root_.LinearIsometry.map_orthogonalProjection' {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â†’â‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection] [(p.map f).HasOrthogonalProjection] (x : E) :
    f (p.orthogonalProjection x) = (p.map f).orthogonalProjection (f x) :=
  have : (p.map f.toLinearMap).HasOrthogonalProjection := â€¹_â€º
  f.map_orthogonalProjection p x

/-- Orthogonal projection onto the `Submodule.map` of a subspace. -/
theorem orthogonalProjection_map_apply {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection] (x : E') :
    ((p.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')).orthogonalProjection x : E') =
      f (p.orthogonalProjection (f.symm x)) := by
  simpa only [f.coe_toLinearIsometry, f.apply_symm_apply] using
    (f.toLinearIsometry.map_orthogonalProjection' p (f.symm x)).symm

/-- The orthogonal projection onto the trivial submodule is the zero map. -/
@[simp]
theorem orthogonalProjection_bot : (âŠ¥ : Submodule ğ•œ E).orthogonalProjection = 0 := by ext

variable (K)

/-- The orthogonal projection has norm `â‰¤ 1`. -/
theorem orthogonalProjection_norm_le : â€–K.orthogonalProjectionâ€– â‰¤ 1 :=
  LinearMap.mkContinuous_norm_le _ (by norm_num) _

variable (ğ•œ)

theorem smul_orthogonalProjection_singleton {v : E} (w : E) :
    ((â€–vâ€– ^ 2 : â„) : ğ•œ) â€¢ ((ğ•œ âˆ™ v).orthogonalProjection w : E) = âŸªv, wâŸ« â€¢ v := by
  suffices (((ğ•œ âˆ™ v).orthogonalProjection (((â€–vâ€– : ğ•œ) ^ 2) â€¢ w)) : E) = âŸªv, wâŸ« â€¢ v by
    simpa using this
  apply eq_orthogonalProjection_of_mem_of_inner_eq_zero
  Â· rw [Submodule.mem_span_singleton]
    use âŸªv, wâŸ«
  Â· rw [â† Submodule.mem_orthogonal', Submodule.mem_orthogonal_singleton_iff_inner_left]
    simp [inner_sub_left, inner_smul_left, inner_self_eq_norm_sq_to_K, mul_comm]

/-- Formula for orthogonal projection onto a single vector. -/
theorem orthogonalProjection_singleton {v : E} (w : E) :
    ((ğ•œ âˆ™ v).orthogonalProjection w : E) = (âŸªv, wâŸ« / ((â€–vâ€– ^ 2 : â„) : ğ•œ)) â€¢ v := by
  by_cases hv : v = 0
  Â· rw [hv, eq_orthogonalProjection_of_eq_submodule (Submodule.span_zero_singleton ğ•œ)]
    simp
  have hv' : â€–vâ€– â‰  0 := ne_of_gt (norm_pos_iff.mpr hv)
  have key :
    (((â€–vâ€– ^ 2 : â„) : ğ•œ)â»Â¹ * ((â€–vâ€– ^ 2 : â„) : ğ•œ)) â€¢ (((ğ•œ âˆ™ v).orthogonalProjection w) : E) =
      (((â€–vâ€– ^ 2 : â„) : ğ•œ)â»Â¹ * âŸªv, wâŸ«) â€¢ v := by
    simp [mul_smul, smul_orthogonalProjection_singleton ğ•œ w, -map_pow]
  convert key using 1 <;> field_simp [hv']

/-- Formula for orthogonal projection onto a single unit vector. -/
theorem orthogonalProjection_unit_singleton {v : E} (hv : â€–vâ€– = 1) (w : E) :
    ((ğ•œ âˆ™ v).orthogonalProjection w : E) = âŸªv, wâŸ« â€¢ v := by
  rw [â† smul_orthogonalProjection_singleton ğ•œ w]
  simp [hv]

end orthogonalProjection

section reflection

variable [K.HasOrthogonalProjection]

/-- Auxiliary definition for `reflection`: the reflection as a linear equivalence. -/
def reflectionLinearEquiv : E â‰ƒâ‚—[ğ•œ] E :=
  LinearEquiv.ofInvolutive
    (2 â€¢ (K.subtype.comp K.orthogonalProjection.toLinearMap) - LinearMap.id) fun x => by
    simp [two_smul]

/-- Reflection in a complete subspace of an inner product space.  The word "reflection" is
sometimes understood to mean specifically reflection in a codimension-one subspace, and sometimes
more generally to cover operations such as reflection in a point.  The definition here, of
reflection in a subspace, is a more general sense of the word that includes both those common
cases. -/
def reflection : E â‰ƒâ‚—áµ¢[ğ•œ] E :=
  { K.reflectionLinearEquiv with
    norm_map' := by
      intro x
      let w : K := K.orthogonalProjection x
      let v := x - w
      have : âŸªv, wâŸ« = 0 := orthogonalProjection_inner_eq_zero x w w.2
      convert norm_sub_eq_norm_add this using 2
      Â· dsimp [reflectionLinearEquiv, v]
        abel
      Â· simp only [v, add_sub_cancel, eq_self_iff_true] }

variable {K}

/-- The result of reflecting. -/
theorem reflection_apply (p : E) : K.reflection p = 2 â€¢ (K.orthogonalProjection p : E) - p :=
  rfl

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_symm : K.reflection.symm = K.reflection :=
  rfl

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_inv : K.reflectionâ»Â¹ = K.reflection :=
  rfl

variable (K)

/-- Reflecting twice in the same subspace. -/
@[simp]
theorem reflection_reflection (p : E) : K.reflection (K.reflection p) = p :=
  K.reflection.left_inv p

/-- Reflection is involutive. -/
theorem reflection_involutive : Function.Involutive K.reflection :=
  K.reflection_reflection

/-- Reflection is involutive. -/
@[simp]
theorem reflection_trans_reflection :
    K.reflection.trans K.reflection = LinearIsometryEquiv.refl ğ•œ E :=
  LinearIsometryEquiv.ext <| reflection_involutive K

/-- Reflection is involutive. -/
@[simp]
theorem reflection_mul_reflection : K.reflection * K.reflection = 1 :=
  reflection_trans_reflection _
theorem reflection_orthogonal_apply (v : E) : Ká—®.reflection v = -K.reflection v := by
  simp [reflection_apply]; abel

theorem reflection_orthogonal : Ká—®.reflection = .trans K.reflection (.neg _) := by
  ext; apply reflection_orthogonal_apply

variable {K}

theorem reflection_singleton_apply (u v : E) :
    reflection (ğ•œ âˆ™ u) v = 2 â€¢ (âŸªu, vâŸ« / ((â€–uâ€– : ğ•œ) ^ 2)) â€¢ u - v := by
  rw [reflection_apply, orthogonalProjection_singleton, ofReal_pow]

/-- A point is its own reflection if and only if it is in the subspace. -/
theorem reflection_eq_self_iff (x : E) : K.reflection x = x â†” x âˆˆ K := by
  rw [â† orthogonalProjection_eq_self_iff, reflection_apply, sub_eq_iff_eq_add', â† two_smul ğ•œ,
    two_smul â„•, â† two_smul ğ•œ]
  refine (smul_right_injective E ?_).eq_iff
  exact two_ne_zero

theorem reflection_mem_subspace_eq_self {x : E} (hx : x âˆˆ K) : K.reflection x = x :=
  (reflection_eq_self_iff x).mpr hx

/-- Reflection in the `Submodule.map` of a subspace. -/
theorem reflection_map_apply {E E' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (K : Submodule ğ•œ E)
    [K.HasOrthogonalProjection] (x : E') :
    reflection (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')) x = f (K.reflection (f.symm x)) := by
  simp [two_smul, reflection_apply, orthogonalProjection_map_apply f K x]

/-- Reflection in the `Submodule.map` of a subspace. -/
theorem reflection_map {E E' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (K : Submodule ğ•œ E)
    [K.HasOrthogonalProjection] :
    reflection (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')) = f.symm.trans (K.reflection.trans f) :=
  LinearIsometryEquiv.ext <| reflection_map_apply f K

/-- Reflection through the trivial subspace {0} is just negation. -/
@[simp]
theorem reflection_bot : reflection (âŠ¥ : Submodule ğ•œ E) = LinearIsometryEquiv.neg ğ•œ := by
  ext; simp [reflection_apply]

end reflection

end Submodule

section Orthogonal

namespace Submodule

/-- If `Kâ‚` is complete and contained in `Kâ‚‚`, `Kâ‚` and `Kâ‚á—® âŠ“ Kâ‚‚` span `Kâ‚‚`. -/
theorem sup_orthogonal_inf_of_completeSpace {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚)
    [Kâ‚.HasOrthogonalProjection] : Kâ‚ âŠ” Kâ‚á—® âŠ“ Kâ‚‚ = Kâ‚‚ := by
  ext x
  rw [Submodule.mem_sup]
  let v : Kâ‚ := orthogonalProjection Kâ‚ x
  have hvm : x - v âˆˆ Kâ‚á—® := sub_orthogonalProjection_mem_orthogonal x
  constructor
  Â· rintro âŸ¨y, hy, z, hz, rflâŸ©
    exact Kâ‚‚.add_mem (h hy) hz.2
  Â· exact fun hx => âŸ¨v, v.prop, x - v, âŸ¨hvm, Kâ‚‚.sub_mem hx (h v.prop)âŸ©, add_sub_cancel _ _âŸ©

variable {K} in
/-- If `K` is complete, `K` and `Ká—®` span the whole space. -/
theorem sup_orthogonal_of_completeSpace [K.HasOrthogonalProjection] : K âŠ” Ká—® = âŠ¤ := by
  convert Submodule.sup_orthogonal_inf_of_completeSpace (le_top : K â‰¤ âŠ¤) using 2
  simp

/-- If `K` is complete, any `v` in `E` can be expressed as a sum of elements of `K` and `Ká—®`. -/
theorem exists_add_mem_mem_orthogonal [K.HasOrthogonalProjection] (v : E) :
    âˆƒ y âˆˆ K, âˆƒ z âˆˆ Ká—®, v = y + z :=
  âŸ¨K.orthogonalProjection v, Subtype.coe_prop _, v - K.orthogonalProjection v,
    sub_orthogonalProjection_mem_orthogonal _, by simpâŸ©

/-- If `K` admits an orthogonal projection, then the orthogonal complement of its orthogonal
complement is itself. -/
@[simp]
theorem orthogonal_orthogonal [K.HasOrthogonalProjection] : Ká—®á—® = K := by
  ext v
  constructor
  Â· obtain âŸ¨y, hy, z, hz, rflâŸ© := K.exists_add_mem_mem_orthogonal v
    intro hv
    have hz' : z = 0 := by
      have hyz : âŸªz, yâŸ« = 0 := by simp [hz y hy, inner_eq_zero_symm]
      simpa [inner_add_right, hyz] using hv z hz
    simp [hy, hz']
  Â· intro hv w hw
    rw [inner_eq_zero_symm]
    exact hw v hv

lemma orthogonal_le_orthogonal_iff {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€á—® â‰¤ Kâ‚á—® â†” Kâ‚ â‰¤ Kâ‚€ :=
  âŸ¨fun h â†¦ by simpa using orthogonal_le h, orthogonal_leâŸ©

lemma orthogonal_le_iff_orthogonal_le {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€á—® â‰¤ Kâ‚ â†” Kâ‚á—® â‰¤ Kâ‚€ := by
  rw [â† orthogonal_le_orthogonal_iff, orthogonal_orthogonal]

lemma le_orthogonal_iff_le_orthogonal {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€ â‰¤ Kâ‚á—® â†” Kâ‚ â‰¤ Kâ‚€á—® := by
  rw [â† orthogonal_le_orthogonal_iff, orthogonal_orthogonal]

/-- In a Hilbert space, the orthogonal complement of the orthogonal complement of a subspace `K`
is the topological closure of `K`.

Note that the completeness assumption is necessary. Let `E` be the space `â„• â†’â‚€ â„` with inner space
structure inherited from `PiLp 2 (fun _ : â„• â†¦ â„)`. Let `K` be the subspace of sequences with the sum
of all elements equal to zero. Then `Ká—® = âŠ¥`, `Ká—®á—® = âŠ¤`. -/
theorem orthogonal_orthogonal_eq_closure [CompleteSpace E] :
    Ká—®á—® = K.topologicalClosure := by
  refine le_antisymm ?_ ?_
  Â· convert Submodule.orthogonal_orthogonal_monotone K.le_topologicalClosure using 1
    rw [K.topologicalClosure.orthogonal_orthogonal]
  Â· exact K.topologicalClosure_minimal K.le_orthogonal_orthogonal Ká—®.isClosed_orthogonal

variable {K}

/-- If `K` admits an orthogonal projection, `K` and `Ká—®` are complements of each other. -/
theorem isCompl_orthogonal_of_completeSpace [K.HasOrthogonalProjection] : IsCompl K Ká—® :=
  âŸ¨K.orthogonal_disjoint, codisjoint_iff.2 Submodule.sup_orthogonal_of_completeSpaceâŸ©

@[simp]
theorem orthogonalComplement_eq_orthogonalComplement {L : Submodule ğ•œ E} [K.HasOrthogonalProjection]
    [L.HasOrthogonalProjection] : Ká—® = Lá—® â†” K = L :=
  âŸ¨fun h â†¦ by simpa using congr(Submodule.orthogonal $(h)),
    fun h â†¦ congr(Submodule.orthogonal $(h))âŸ©

@[simp]
theorem orthogonal_eq_bot_iff [K.HasOrthogonalProjection] : Ká—® = âŠ¥ â†” K = âŠ¤ := by
  refine âŸ¨?_, fun h => by rw [h, Submodule.top_orthogonal_eq_bot]âŸ©
  intro h
  have : K âŠ” Ká—® = âŠ¤ := Submodule.sup_orthogonal_of_completeSpace
  rwa [h, sup_comm, bot_sup_eq] at this

/-- The orthogonal projection onto `K` of an element of `Ká—®` is zero. -/
theorem orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero [K.HasOrthogonalProjection]
    {v : E} (hv : v âˆˆ Ká—®) : K.orthogonalProjection v = 0 := by
  ext
  convert eq_orthogonalProjection_of_mem_orthogonal (K := K) _ _ <;> simp [hv]

/-- The projection into `U` from an orthogonal submodule `V` is the zero map. -/
theorem IsOrtho.orthogonalProjection_comp_subtypeL {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] (h : U âŸ‚ V) : U.orthogonalProjection âˆ˜L V.subtypeL = 0 :=
  ContinuousLinearMap.ext fun v =>
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero <| h.symm v.prop

/-- The projection into `U` from `V` is the zero map if and only if `U` and `V` are orthogonal. -/
theorem orthogonalProjection_comp_subtypeL_eq_zero_iff {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] : U.orthogonalProjection âˆ˜L V.subtypeL = 0 â†” U âŸ‚ V :=
  âŸ¨fun h u hu v hv => by
    convert orthogonalProjection_inner_eq_zero v u hu using 2
    have : U.orthogonalProjection v = 0 := DFunLike.congr_fun h (âŸ¨_, hvâŸ© : V)
    rw [this, Submodule.coe_zero, sub_zero], Submodule.IsOrtho.orthogonalProjection_comp_subtypeLâŸ©

theorem orthogonalProjection_eq_linear_proj [K.HasOrthogonalProjection] (x : E) :
    K.orthogonalProjection x =
      K.linearProjOfIsCompl _ Submodule.isCompl_orthogonal_of_completeSpace x := by
  have : IsCompl K Ká—® := Submodule.isCompl_orthogonal_of_completeSpace
  conv_lhs => rw [â† Submodule.linear_proj_add_linearProjOfIsCompl_eq_self this x]
  rw [map_add, orthogonalProjection_mem_subspace_eq_self,
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero (Submodule.coe_mem _), add_zero]

theorem orthogonalProjection_coe_linearMap_eq_linearProj [K.HasOrthogonalProjection] :
    (K.orthogonalProjection : E â†’â‚—[ğ•œ] K) =
      K.linearProjOfIsCompl _ Submodule.isCompl_orthogonal_of_completeSpace :=
  LinearMap.ext <| orthogonalProjection_eq_linear_proj

/-- The reflection in `K` of an element of `Ká—®` is its negation. -/
theorem reflection_mem_subspace_orthogonalComplement_eq_neg [K.HasOrthogonalProjection] {v : E}
    (hv : v âˆˆ Ká—®) : K.reflection v = -v := by
  simp [reflection_apply, orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv]

/-- The orthogonal projection onto `Ká—®` of an element of `K` is zero. -/
theorem orthogonalProjection_mem_subspace_orthogonal_precomplement_eq_zero
    [Ká—®.HasOrthogonalProjection] {v : E} (hv : v âˆˆ K) : Ká—®.orthogonalProjection v = 0 :=
  orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero (K.le_orthogonal_orthogonal hv)

/-- If `U â‰¤ V`, then projecting on `V` and then on `U` is the same as projecting on `U`. -/
theorem orthogonalProjection_orthogonalProjection_of_le {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U â‰¤ V) (x : E) :
    U.orthogonalProjection (V.orthogonalProjection x) = U.orthogonalProjection x :=
  Eq.symm <| by
    simpa only [sub_eq_zero, map_sub] using
      orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero
        (Submodule.orthogonal_le h (sub_orthogonalProjection_mem_orthogonal x))

/-- Given a monotone family `U` of complete submodules of `E` and a fixed `x : E`,
the orthogonal projection of `x` on `U i` tends to the orthogonal projection of `x` on
`(â¨† i, U i).topologicalClosure` along `atTop`. -/
theorem orthogonalProjection_tendsto_closure_iSup {Î¹ : Type*} [Preorder Î¹]
    (U : Î¹ â†’ Submodule ğ•œ E) [âˆ€ i, (U i).HasOrthogonalProjection]
    [(â¨† i, U i).topologicalClosure.HasOrthogonalProjection] (hU : Monotone U) (x : E) :
    Filter.Tendsto (fun i => ((U i).orthogonalProjection x : E)) atTop
      (ğ“ ((â¨† i, U i).topologicalClosure.orthogonalProjection x : E)) := by
  refine .of_neBot_imp fun h â†¦ ?_
  cases atTop_neBot_iff.mp h
  let y := ((â¨† i, U i).topologicalClosure.orthogonalProjection x : E)
  have proj_x : âˆ€ i, (U i).orthogonalProjection x = (U i).orthogonalProjection y := fun i =>
    (orthogonalProjection_orthogonalProjection_of_le
        ((le_iSup U i).trans (iSup U).le_topologicalClosure) _).symm
  suffices âˆ€ Îµ > 0, âˆƒ I, âˆ€ i â‰¥ I, â€–((U i).orthogonalProjection y : E) - yâ€– < Îµ by
    simpa only [proj_x, NormedAddCommGroup.tendsto_atTop] using this
  intro Îµ hÎµ
  obtain âŸ¨a, ha, hayâŸ© : âˆƒ a âˆˆ â¨† i, U i, dist y a < Îµ := by
    have y_mem : y âˆˆ (â¨† i, U i).topologicalClosure := Submodule.coe_mem _
    rw [â† SetLike.mem_coe, Submodule.topologicalClosure_coe, Metric.mem_closure_iff] at y_mem
    exact y_mem Îµ hÎµ
  rw [dist_eq_norm] at hay
  obtain âŸ¨I, hIâŸ© : âˆƒ I, a âˆˆ U I := by rwa [Submodule.mem_iSup_of_directed _ hU.directed_le] at ha
  refine âŸ¨I, fun i (hi : I â‰¤ i) => ?_âŸ©
  rw [norm_sub_rev, orthogonalProjection_minimal]
  refine lt_of_le_of_lt ?_ hay
  change _ â‰¤ â€–y - (âŸ¨a, hU hi hIâŸ© : U i)â€–
  exact ciInf_le âŸ¨0, Set.forall_mem_range.mpr fun _ => norm_nonneg _âŸ© _

/-- Given a monotone family `U` of complete submodules of `E` with dense span supremum,
and a fixed `x : E`, the orthogonal projection of `x` on `U i` tends to `x` along `at_top`. -/
theorem orthogonalProjection_tendsto_self {Î¹ : Type*} [Preorder Î¹]
    (U : Î¹ â†’ Submodule ğ•œ E) [âˆ€ t, (U t).HasOrthogonalProjection] (hU : Monotone U) (x : E)
    (hU' : âŠ¤ â‰¤ (â¨† t, U t).topologicalClosure) :
    Filter.Tendsto (fun t => ((U t).orthogonalProjection x : E)) atTop (ğ“ x) := by
  have : (â¨† i, U i).topologicalClosure.HasOrthogonalProjection := by
    rw [top_unique hU']
    infer_instance
  convert orthogonalProjection_tendsto_closure_iSup U hU x
  rw [eq_comm, orthogonalProjection_eq_self_iff, top_unique hU']
  trivial

/-- The orthogonal complement satisfies `Ká—®á—®á—® = Ká—®`. -/
theorem triorthogonal_eq_orthogonal [CompleteSpace E] : Ká—®á—®á—® = Ká—® := by
  rw [Ká—®.orthogonal_orthogonal_eq_closure]
  exact K.isClosed_orthogonal.submodule_topologicalClosure_eq

/-- The closure of `K` is the full space iff `Ká—®` is trivial. -/
theorem topologicalClosure_eq_top_iff [CompleteSpace E] :
    K.topologicalClosure = âŠ¤ â†” Ká—® = âŠ¥ := by
  rw [â† K.orthogonal_orthogonal_eq_closure]
  constructor <;> intro h
  Â· rw [â† Submodule.triorthogonal_eq_orthogonal, h, Submodule.top_orthogonal_eq_bot]
  Â· rw [h, Submodule.bot_orthogonal_eq_top]

end Submodule

namespace Dense

/- TODO: Move to another file? -/
open Submodule

variable {K} {x y : E}

theorem eq_zero_of_inner_left (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸªx, vâŸ« = 0) : x = 0 := by
  have : (âŸªx, Â·âŸ«) = 0 := (continuous_const.inner continuous_id).ext_on
    hK continuous_const (Subtype.forall.1 h)
  simpa using congr_fun this x

theorem eq_zero_of_mem_orthogonal (hK : Dense (K : Set E)) (h : x âˆˆ Ká—®) : x = 0 :=
  eq_zero_of_inner_left hK fun v â†¦ (mem_orthogonal' _ _).1 h _ v.2

/-- If `S` is dense and `x - y âˆˆ Ká—®`, then `x = y`. -/
theorem eq_of_sub_mem_orthogonal (hK : Dense (K : Set E)) (h : x - y âˆˆ Ká—®) : x = y :=
  sub_eq_zero.1 <| eq_zero_of_mem_orthogonal hK h

theorem eq_of_inner_left (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸªx, vâŸ« = âŸªy, vâŸ«) : x = y :=
  hK.eq_of_sub_mem_orthogonal (Submodule.sub_mem_orthogonal_of_inner_left h)

theorem eq_of_inner_right (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸª(v : E), xâŸ« = âŸª(v : E), yâŸ«) :
    x = y :=
  hK.eq_of_sub_mem_orthogonal (Submodule.sub_mem_orthogonal_of_inner_right h)

theorem eq_zero_of_inner_right (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸª(v : E), xâŸ« = 0) : x = 0 :=
  hK.eq_of_inner_right fun v => by rw [inner_zero_right, h v]

end Dense

namespace Submodule

variable {K}

/-- The reflection in `Ká—®` of an element of `K` is its negation. -/
theorem reflection_mem_subspace_orthogonal_precomplement_eq_neg [K.HasOrthogonalProjection] {v : E}
    (hv : v âˆˆ K) : Ká—®.reflection v = -v :=
  reflection_mem_subspace_orthogonalComplement_eq_neg (K.le_orthogonal_orthogonal hv)

/-- The orthogonal projection onto `(ğ•œ âˆ™ v)á—®` of `v` is zero. -/
theorem orthogonalProjection_orthogonalComplement_singleton_eq_zero (v : E) :
    (ğ•œ âˆ™ v)á—®.orthogonalProjection v = 0 :=
  orthogonalProjection_mem_subspace_orthogonal_precomplement_eq_zero
    (Submodule.mem_span_singleton_self v)

/-- The reflection in `(ğ•œ âˆ™ v)á—®` of `v` is `-v`. -/
theorem reflection_orthogonalComplement_singleton_eq_neg (v : E) : reflection (ğ•œ âˆ™ v)á—® v = -v :=
  reflection_mem_subspace_orthogonal_precomplement_eq_neg (Submodule.mem_span_singleton_self v)

theorem reflection_sub {v w : F} (h : â€–vâ€– = â€–wâ€–) : reflection (â„ âˆ™ (v - w))á—® v = w := by
  set R : F â‰ƒâ‚—áµ¢[â„] F := reflection (â„ âˆ™ v - w)á—®
  suffices R v + R v = w + w by
    apply smul_right_injective F (by norm_num : (2 : â„) â‰  0)
    simpa [two_smul] using this
  have hâ‚ : R (v - w) = -(v - w) := reflection_orthogonalComplement_singleton_eq_neg (v - w)
  have hâ‚‚ : R (v + w) = v + w := by
    apply reflection_mem_subspace_eq_self
    rw [Submodule.mem_orthogonal_singleton_iff_inner_left]
    rw [real_inner_add_sub_eq_zero_iff]
    exact h
  convert congr_argâ‚‚ (Â· + Â·) hâ‚‚ hâ‚ using 1
  Â· simp
  Â· abel

variable (K)

section FiniteDimensional

open Module

variable [FiniteDimensional ğ•œ K]

@[simp]
theorem topologicalClosure_eq_self : K.topologicalClosure = K :=
  K.closed_of_finiteDimensional.submodule_topologicalClosure_eq

@[simp]
theorem det_reflection : LinearMap.det K.reflection.toLinearMap = (-1) ^ finrank ğ•œ Ká—® := by
  by_cases hK : FiniteDimensional ğ•œ Ká—®
  swap
  Â· rw [finrank_of_infinite_dimensional hK, pow_zero, LinearMap.det_eq_one_of_finrank_eq_zero]
    exact finrank_of_infinite_dimensional fun h â†¦ hK (h.finiteDimensional_submodule _)
  let e := K.prodEquivOfIsCompl _ K.isCompl_orthogonal_of_completeSpace
  let b := (finBasis ğ•œ K).prod (finBasis ğ•œ Ká—®)
  have : LinearMap.toMatrix b b (e.symm âˆ˜â‚— K.reflection.toLinearMap âˆ˜â‚— e.symm.symm) =
      Matrix.fromBlocks 1 0 0 (-1) := by
    ext (_ | _) (_ | _) <;>
    simp [LinearMap.toMatrix_apply, b, Matrix.one_apply, Finsupp.single_apply, e, eq_comm,
      reflection_mem_subspace_eq_self, reflection_mem_subspace_orthogonalComplement_eq_neg]
  rw [â† LinearMap.det_conj _ e.symm, â† LinearMap.det_toMatrix b, this, Matrix.det_fromBlocks_zeroâ‚‚â‚,
    Matrix.det_one, one_mul, Matrix.det_neg, Fintype.card_fin, Matrix.det_one, mul_one]

@[simp]
theorem linearEquiv_det_reflection : K.reflection.det = (-1) ^ finrank ğ•œ Ká—® := by
  ext
  rw [LinearEquiv.coe_det, Units.val_pow_eq_pow_val]
  exact K.det_reflection

end FiniteDimensional

/-- If the orthogonal projection to `K` is well-defined, then a vector splits as the sum of its
orthogonal projections onto a complete submodule `K` and onto the orthogonal complement of `K`. -/
theorem orthogonalProjection_add_orthogonalProjection_orthogonal [K.HasOrthogonalProjection]
    (w : E) : (K.orthogonalProjection w : E) + (Ká—®.orthogonalProjection w : E) = w := by
  simp

/-- The Pythagorean theorem, for an orthogonal projection. -/
theorem norm_sq_eq_add_norm_sq_projection (x : E) (S : Submodule ğ•œ E) [S.HasOrthogonalProjection] :
    â€–xâ€– ^ 2 = â€–S.orthogonalProjection xâ€– ^ 2 + â€–Sá—®.orthogonalProjection xâ€– ^ 2 :=
  calc
    â€–xâ€– ^ 2 = â€–(S.orthogonalProjection x : E) + Sá—®.orthogonalProjection xâ€– ^ 2 := by
      rw [orthogonalProjection_add_orthogonalProjection_orthogonal]
    _ = â€–S.orthogonalProjection xâ€– ^ 2 + â€–Sá—®.orthogonalProjection xâ€– ^ 2 := by
      simp only [sq]
      exact norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ <|
        (S.mem_orthogonal _).1 (Sá—®.orthogonalProjection x).2 _ (S.orthogonalProjection x).2

/-- In a complete space `E`, the projection maps onto a complete subspace `K` and its orthogonal
complement sum to the identity. -/
theorem id_eq_sum_orthogonalProjection_self_orthogonalComplement [K.HasOrthogonalProjection] :
    ContinuousLinearMap.id ğ•œ E =
      K.subtypeL.comp K.orthogonalProjection + Ká—®.subtypeL.comp Ká—®.orthogonalProjection := by
  ext w
  exact (K.orthogonalProjection_add_orthogonalProjection_orthogonal w).symm

-- Porting note: The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_right [K.HasOrthogonalProjection] (u : K) (v : E) :
    âŸªK.orthogonalProjection v, uâŸ« = âŸªv, uâŸ« :=
  calc
    âŸªK.orthogonalProjection v, uâŸ« = âŸª(K.orthogonalProjection v : E), uâŸ« := K.coe_inner _ _
    _ = âŸª(K.orthogonalProjection v : E), uâŸ« + âŸªv - K.orthogonalProjection v, uâŸ« := by
      rw [orthogonalProjection_inner_eq_zero _ _ (Submodule.coe_mem _), add_zero]
    _ = âŸªv, uâŸ« := by rw [â† inner_add_left, add_sub_cancel]

-- Porting note: The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_left [K.HasOrthogonalProjection] (u : K) (v : E) :
    âŸªu, K.orthogonalProjection vâŸ« = âŸª(u : E), vâŸ« := by
  rw [â† inner_conj_symm, â† inner_conj_symm (u : E), inner_orthogonalProjection_eq_of_mem_right]

/-- The orthogonal projection is self-adjoint. -/
theorem inner_orthogonalProjection_left_eq_right [K.HasOrthogonalProjection] (u v : E) :
    âŸªâ†‘(K.orthogonalProjection u), vâŸ« = âŸªu, K.orthogonalProjection vâŸ« := by
  rw [â† inner_orthogonalProjection_eq_of_mem_left, inner_orthogonalProjection_eq_of_mem_right]

/-- The orthogonal projection is symmetric. -/
theorem orthogonalProjection_isSymmetric [K.HasOrthogonalProjection] :
    (K.subtypeL âˆ˜L K.orthogonalProjection : E â†’â‚—[ğ•œ] E).IsSymmetric :=
  inner_orthogonalProjection_left_eq_right K

lemma re_inner_orthogonalProjection_eq_normSq [K.HasOrthogonalProjection] (v : E) :
    re âŸªâ†‘(K.orthogonalProjection v), vâŸ« = â€–K.orthogonalProjection vâ€–^2 := by
  rw [re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
    div_eq_iff (NeZero.ne' 2).symm, pow_two, add_sub_assoc, â† eq_sub_iff_add_eq', coe_norm,
    â† mul_sub_one, show (2 : â„) - 1 = 1 by ring, mul_one, sub_eq_iff_eq_add', norm_sub_rev]
  exact orthogonalProjectionFn_norm_sq K v

lemma re_inner_orthogonalProjection_nonneg [K.HasOrthogonalProjection] (v : E) :
    0 â‰¤ re âŸªâ†‘(K.orthogonalProjection v), vâŸ« := by
  rw [re_inner_orthogonalProjection_eq_normSq K v]
  exact sq_nonneg â€–K.orthogonalProjection vâ€–

open Module

/-- Given a finite-dimensional subspace `Kâ‚‚`, and a subspace `Kâ‚`
contained in it, the dimensions of `Kâ‚` and the intersection of its
orthogonal subspace with `Kâ‚‚` add to that of `Kâ‚‚`. -/
theorem finrank_add_inf_finrank_orthogonal {Kâ‚ Kâ‚‚ : Submodule ğ•œ E}
    [FiniteDimensional ğ•œ Kâ‚‚] (h : Kâ‚ â‰¤ Kâ‚‚) :
    finrank ğ•œ Kâ‚ + finrank ğ•œ (Kâ‚á—® âŠ“ Kâ‚‚ : Submodule ğ•œ E) = finrank ğ•œ Kâ‚‚ := by
  haveI : FiniteDimensional ğ•œ Kâ‚ := Submodule.finiteDimensional_of_le h
  haveI := FiniteDimensional.proper_rclike ğ•œ Kâ‚
  have hd := Submodule.finrank_sup_add_finrank_inf_eq Kâ‚ (Kâ‚á—® âŠ“ Kâ‚‚)
  rw [â† inf_assoc, (Submodule.orthogonal_disjoint Kâ‚).eq_bot, bot_inf_eq, finrank_bot,
    Submodule.sup_orthogonal_inf_of_completeSpace h] at hd
  rw [add_zero] at hd
  exact hd.symm

/-- Given a finite-dimensional subspace `Kâ‚‚`, and a subspace `Kâ‚`
contained in it, the dimensions of `Kâ‚` and the intersection of its
orthogonal subspace with `Kâ‚‚` add to that of `Kâ‚‚`. -/
theorem finrank_add_inf_finrank_orthogonal' {Kâ‚ Kâ‚‚ : Submodule ğ•œ E}
    [FiniteDimensional ğ•œ Kâ‚‚] (h : Kâ‚ â‰¤ Kâ‚‚) {n : â„•} (h_dim : finrank ğ•œ Kâ‚ + n = finrank ğ•œ Kâ‚‚) :
    finrank ğ•œ (Kâ‚á—® âŠ“ Kâ‚‚ : Submodule ğ•œ E) = n := by
  rw [â† add_right_inj (finrank ğ•œ Kâ‚)]
  simp [Submodule.finrank_add_inf_finrank_orthogonal h, h_dim]

/-- Given a finite-dimensional space `E` and subspace `K`, the dimensions of `K` and `Ká—®` add to
that of `E`. -/
theorem finrank_add_finrank_orthogonal [FiniteDimensional ğ•œ E] (K : Submodule ğ•œ E) :
    finrank ğ•œ K + finrank ğ•œ Ká—® = finrank ğ•œ E := by
  convert Submodule.finrank_add_inf_finrank_orthogonal (le_top : K â‰¤ âŠ¤) using 1
  Â· rw [inf_top_eq]
  Â· simp

/-- Given a finite-dimensional space `E` and subspace `K`, the dimensions of `K` and `Ká—®` add to
that of `E`. -/
theorem finrank_add_finrank_orthogonal' [FiniteDimensional ğ•œ E] {K : Submodule ğ•œ E}
    {n : â„•} (h_dim : finrank ğ•œ K + n = finrank ğ•œ E) : finrank ğ•œ Ká—® = n := by
  rw [â† add_right_inj (finrank ğ•œ K)]
  simp [Submodule.finrank_add_finrank_orthogonal, h_dim]

/-- In a finite-dimensional inner product space, the dimension of the orthogonal complement of the
span of a nonzero vector is one less than the dimension of the space. -/
theorem finrank_orthogonal_span_singleton {n : â„•} [_i : Fact (finrank ğ•œ E = n + 1)] {v : E}
    (hv : v â‰  0) : finrank ğ•œ (ğ•œ âˆ™ v)á—® = n := by
  haveI : FiniteDimensional ğ•œ E := .of_fact_finrank_eq_succ n
  exact finrank_add_finrank_orthogonal' <| by
    simp [finrank_span_singleton hv, _i.elim, add_comm]

end Submodule

open Module Submodule

/-- An element `Ï†` of the orthogonal group of `F` can be factored as a product of reflections, and
specifically at most as many reflections as the dimension of the complement of the fixed subspace
of `Ï†`. -/
theorem LinearIsometryEquiv.reflections_generate_dim_aux [FiniteDimensional â„ F] {n : â„•}
    (Ï† : F â‰ƒâ‚—áµ¢[â„] F) (hn : finrank â„ (ker (ContinuousLinearMap.id â„ F - Ï†))á—® â‰¤ n) :
    âˆƒ l : List F, l.length â‰¤ n âˆ§ Ï† = (l.map fun v => (â„ âˆ™ v)á—®.reflection).prod := by
  -- We prove this by strong induction on `n`, the dimension of the orthogonal complement of the
  -- fixed subspace of the endomorphism `Ï†`
  induction' n with n IH generalizing Ï†
  Â· -- Base case: `n = 0`, the fixed subspace is the whole space, so `Ï† = id`
    refine âŸ¨[], rfl.le, show Ï† = 1 from ?_âŸ©
    have : ker (ContinuousLinearMap.id â„ F - Ï†) = âŠ¤ := by
      rwa [le_zero_iff, finrank_eq_zero, orthogonal_eq_bot_iff] at hn
    symm
    ext x
    have := LinearMap.congr_fun (LinearMap.ker_eq_top.mp this) x
    simpa only [sub_eq_zero, ContinuousLinearMap.coe_sub, LinearMap.sub_apply,
      LinearMap.zero_apply] using this
  Â· -- Inductive step.  Let `W` be the fixed subspace of `Ï†`.  We suppose its complement to have
    -- dimension at most n + 1.
    let W := ker (ContinuousLinearMap.id â„ F - Ï†)
    have hW : âˆ€ w âˆˆ W, Ï† w = w := fun w hw => (sub_eq_zero.mp hw).symm
    by_cases hn' : finrank â„ Wá—® â‰¤ n
    Â· obtain âŸ¨V, hVâ‚, hVâ‚‚âŸ© := IH Ï† hn'
      exact âŸ¨V, hVâ‚.trans n.le_succ, hVâ‚‚âŸ©
    -- Take a nonzero element `v` of the orthogonal complement of `W`.
    haveI : Nontrivial Wá—® := nontrivial_of_finrank_pos (by omega : 0 < finrank â„ Wá—®)
    obtain âŸ¨v, hvâŸ© := exists_ne (0 : Wá—®)
    have hÏ†v : Ï† v âˆˆ Wá—® := by
      intro w hw
      rw [â† hW w hw, LinearIsometryEquiv.inner_map_map]
      exact v.prop w hw
    have hv' : (v : F) âˆ‰ W := by
      intro h
      exact hv ((mem_left_iff_eq_zero_of_disjoint W.orthogonal_disjoint).mp h)
    -- Let `Ï` be the reflection in `v - Ï† v`; this is designed to swap `v` and `Ï† v`
    let x : F := v - Ï† v
    let Ï := (â„ âˆ™ x)á—®.reflection
    -- Notation: Let `V` be the fixed subspace of `Ï†.trans Ï`
    let V := ker (ContinuousLinearMap.id â„ F - Ï†.trans Ï)
    have hV : âˆ€ w, Ï (Ï† w) = w â†’ w âˆˆ V := by
      intro w hw
      change w - Ï (Ï† w) = 0
      rw [sub_eq_zero, hw]
    -- Everything fixed by `Ï†` is fixed by `Ï†.trans Ï`
    have Hâ‚‚V : W â‰¤ V := by
      intro w hw
      apply hV
      rw [hW w hw]
      refine reflection_mem_subspace_eq_self ?_
      rw [mem_orthogonal_singleton_iff_inner_left]
      exact Submodule.sub_mem _ v.prop hÏ†v _ hw
    -- `v` is also fixed by `Ï†.trans Ï`
    have Hâ‚V : (v : F) âˆˆ V := by
      apply hV
      have : Ï v = Ï† v := reflection_sub (Ï†.norm_map v).symm
      rw [â† this]
      exact reflection_reflection _ _
    -- By dimension-counting, the complement of the fixed subspace of `Ï†.trans Ï` has dimension at
    -- most `n`
    have : finrank â„ Vá—® â‰¤ n := by
      change finrank â„ Wá—® â‰¤ n + 1 at hn
      have : finrank â„ W + 1 â‰¤ finrank â„ V :=
        finrank_lt_finrank_of_lt (SetLike.lt_iff_le_and_exists.2 âŸ¨Hâ‚‚V, v, Hâ‚V, hv'âŸ©)
      have : finrank â„ V + finrank â„ Vá—® = finrank â„ F := V.finrank_add_finrank_orthogonal
      have : finrank â„ W + finrank â„ Wá—® = finrank â„ F := W.finrank_add_finrank_orthogonal
      omega
    -- So apply the inductive hypothesis to `Ï†.trans Ï`
    obtain âŸ¨l, hl, hÏ†lâŸ© := IH (Ï * Ï†) this
    -- Prepend `Ï` to the factorization into reflections obtained for `Ï†.trans Ï`; this gives a
    -- factorization into reflections for `Ï†`.
    refine âŸ¨x::l, Nat.succ_le_succ hl, ?_âŸ©
    rw [List.map_cons, List.prod_cons]
    have := congr_arg (Ï * Â·) hÏ†l
    dsimp only at this
    rwa [â† mul_assoc, reflection_mul_reflection, one_mul] at this

/-- The orthogonal group of `F` is generated by reflections; specifically each element `Ï†` of the
orthogonal group is a product of at most as many reflections as the dimension of `F`.

Special case of the **Cartanâ€“DieudonnÃ© theorem**. -/
theorem LinearIsometryEquiv.reflections_generate_dim [FiniteDimensional â„ F] (Ï† : F â‰ƒâ‚—áµ¢[â„] F) :
    âˆƒ l : List F, l.length â‰¤ finrank â„ F âˆ§ Ï† = (l.map fun v => reflection (â„ âˆ™ v)á—®).prod :=
  let âŸ¨l, hlâ‚, hlâ‚‚âŸ© := Ï†.reflections_generate_dim_aux le_rfl
  âŸ¨l, hlâ‚.trans (finrank_le _), hlâ‚‚âŸ©

/-- The orthogonal group of `F` is generated by reflections. -/
theorem LinearIsometryEquiv.reflections_generate [FiniteDimensional â„ F] :
    Subgroup.closure (Set.range fun v : F => reflection (â„ âˆ™ v)á—®) = âŠ¤ := by
  rw [Subgroup.eq_top_iff']
  intro Ï†
  rcases Ï†.reflections_generate_dim with âŸ¨l, _, rflâŸ©
  apply (Subgroup.closure _).list_prod_mem
  intro x hx
  rcases List.mem_map.mp hx with âŸ¨a, _, haxâŸ©
  exact Subgroup.subset_closure âŸ¨a, haxâŸ©

end Orthogonal

section OrthogonalFamily

open Submodule

variable {Î¹ : Type*}

/-- An orthogonal family of subspaces of `E` satisfies `DirectSum.IsInternal` (that is,
they provide an internal direct sum decomposition of `E`) if and only if their span has trivial
orthogonal complement. -/
theorem OrthogonalFamily.isInternal_iff_of_isComplete [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢)
    (hc : IsComplete (â†‘(iSup V) : Set E)) : DirectSum.IsInternal V â†” (iSup V)á—® = âŠ¥ := by
  haveI : CompleteSpace (â†¥(iSup V)) := hc.completeSpace_coe
  simp only [DirectSum.isInternal_submodule_iff_iSupIndep_and_iSup_eq_top, hV.independent,
    true_and, orthogonal_eq_bot_iff]

/-- An orthogonal family of subspaces of `E` satisfies `DirectSum.IsInternal` (that is,
they provide an internal direct sum decomposition of `E`) if and only if their span has trivial
orthogonal complement. -/
theorem OrthogonalFamily.isInternal_iff [DecidableEq Î¹] [FiniteDimensional ğ•œ E]
    {V : Î¹ â†’ Submodule ğ•œ E} (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) :
    DirectSum.IsInternal V â†” (iSup V)á—® = âŠ¥ :=
  haveI h := FiniteDimensional.proper_rclike ğ•œ (â†¥(iSup V))
  hV.isInternal_iff_of_isComplete (completeSpace_coe_iff_isComplete.mp inferInstance)

open DirectSum

/-- If `x` lies within an orthogonal family `v`, it can be expressed as a sum of projections. -/
theorem OrthogonalFamily.sum_projection_of_mem_iSup [Fintype Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    [âˆ€ i, CompleteSpace (V i)] (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢)
    (x : E) (hx : x âˆˆ iSup V) : (âˆ‘ i, ((V i).orthogonalProjection x : E)) = x := by
  induction hx using iSup_induction' with
  | mem i x hx =>
    refine
      (Finset.sum_eq_single_of_mem i (Finset.mem_univ _) fun j _ hij => ?_).trans
        (orthogonalProjection_eq_self_iff.mpr hx)
    rw [orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero, Submodule.coe_zero]
    exact hV.isOrtho hij.symm hx
  | zero =>
    simp_rw [map_zero, Submodule.coe_zero, Finset.sum_const_zero]
  | add x y _ _ hx hy =>
    simp_rw [map_add, Submodule.coe_add, Finset.sum_add_distrib]
    exact congr_argâ‚‚ (Â· + Â·) hx hy

/-- If a family of submodules is orthogonal, then the `orthogonalProjection` on a direct sum
is just the coefficient of that direct sum. -/
theorem OrthogonalFamily.projection_directSum_coeAddHom [DecidableEq Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢) (x : â¨ i, V i) (i : Î¹)
    [CompleteSpace (V i)] :
    (V i).orthogonalProjection (DirectSum.coeAddMonoidHom V x) = x i := by
  induction x using DirectSum.induction_on with
  | zero => simp
  | of j x =>
    simp_rw [DirectSum.coeAddMonoidHom_of, DirectSum.of]
    -- Porting note: was in the previous `simp_rw`, no longer works
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [DFinsupp.singleAddHom_apply]
    obtain rfl | hij := Decidable.eq_or_ne i j
    Â· rw [orthogonalProjection_mem_subspace_eq_self, DFinsupp.single_eq_same]
    Â· rw [orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero,
        DFinsupp.single_eq_of_ne hij.symm]
      exact hV.isOrtho hij.symm x.prop
  | add x y hx hy =>
    simp_rw [map_add]
    exact congr_argâ‚‚ (Â· + Â·) hx hy

/-- If a family of submodules is orthogonal and they span the whole space, then the orthogonal
projection provides a means to decompose the space into its submodules.

The projection function is `decompose V x i = (V i).orthogonalProjection x`.

See note [reducible non-instances]. -/
abbrev OrthogonalFamily.decomposition [DecidableEq Î¹] [Fintype Î¹] {V : Î¹ â†’ Submodule ğ•œ E}
    [âˆ€ i, CompleteSpace (V i)] (hV : OrthogonalFamily ğ•œ (fun i => V i) fun i => (V i).subtypeâ‚—áµ¢)
    (h : iSup V = âŠ¤) : DirectSum.Decomposition V where
  decompose' x := DFinsupp.equivFunOnFintype.symm fun i => (V i).orthogonalProjection x
  left_inv x := by
    dsimp only
    letI := fun i => Classical.decEq (V i)
    rw [DirectSum.coeAddMonoidHom, DirectSum.toAddMonoid, DFinsupp.liftAddHom_apply]
    -- This used to be `rw`, but we need `erw` after https://github.com/leanprover/lean4/pull/2644
    erw [DFinsupp.sumAddHom_apply]; rw [DFinsupp.sum_eq_sum_fintype]
    Â· simp_rw [Equiv.apply_symm_apply, AddSubmonoidClass.coe_subtype]
      exact hV.sum_projection_of_mem_iSup _ ((h.ge :) Submodule.mem_top)
    Â· intro i
      exact map_zero _
  right_inv x := by
    dsimp only
    simp_rw [hV.projection_directSum_coeAddHom, DFinsupp.equivFunOnFintype_symm_coe]

end OrthogonalFamily

section OrthonormalBasis

variable {v : Set E}

open Module Submodule Set

/-- An orthonormal set in an `InnerProductSpace` is maximal, if and only if the orthogonal
complement of its span is empty. -/
theorem maximal_orthonormal_iff_orthogonalComplement_eq_bot (hv : Orthonormal ğ•œ ((â†‘) : v â†’ E)) :
    (âˆ€ u âŠ‡ v, Orthonormal ğ•œ ((â†‘) : u â†’ E) â†’ u = v) â†” (span ğ•œ v)á—® = âŠ¥ := by
  rw [Submodule.eq_bot_iff]
  constructor
  Â· contrapose!
    -- ** direction 1: nonempty orthogonal complement implies nonmaximal
    rintro âŸ¨x, hx', hxâŸ©
    -- take a nonzero vector and normalize it
    let e := (â€–xâ€–â»Â¹ : ğ•œ) â€¢ x
    have he : â€–eâ€– = 1 := by simp [e, norm_smul_inv_norm hx]
    have he' : e âˆˆ (span ğ•œ v)á—® := smul_mem' _ _ hx'
    have he'' : e âˆ‰ v := by
      intro hev
      have : e = 0 := by
        have : e âˆˆ span ğ•œ v âŠ“ (span ğ•œ v)á—® := âŸ¨subset_span hev, he'âŸ©
        simpa [(span ğ•œ v).inf_orthogonal_eq_bot] using this
      have : e â‰  0 := hv.ne_zero âŸ¨e, hevâŸ©
      contradiction
    -- put this together with `v` to provide a candidate orthonormal basis for the whole space
    refine âŸ¨insert e v, v.subset_insert e, âŸ¨?_, ?_âŸ©, (ne_insert_of_notMem v he'').symmâŸ©
    Â· -- show that the elements of `insert e v` have unit length
      rintro âŸ¨a, ha'âŸ©
      rcases eq_or_mem_of_mem_insert ha' with ha | ha
      Â· simp [ha, he]
      Â· exact hv.1 âŸ¨a, haâŸ©
    Â· -- show that the elements of `insert e v` are orthogonal
      have h_end : âˆ€ a âˆˆ v, âŸªa, eâŸ« = 0 := by
        intro a ha
        exact he' a (Submodule.subset_span ha)
      rintro âŸ¨a, ha'âŸ©
      rcases eq_or_mem_of_mem_insert ha' with ha | ha
      Â· rintro âŸ¨b, hb'âŸ© hab'
        have hb : b âˆˆ v := by
          refine mem_of_mem_insert_of_ne hb' ?_
          intro hbe'
          apply hab'
          simp [ha, hbe']
        rw [inner_eq_zero_symm]
        simpa [ha] using h_end b hb
      rintro âŸ¨b, hb'âŸ© hab'
      rcases eq_or_mem_of_mem_insert hb' with hb | hb
      Â· simpa [hb] using h_end a ha
      have : (âŸ¨a, haâŸ© : v) â‰  âŸ¨b, hbâŸ© := by
        intro hab''
        apply hab'
        simpa using hab''
      exact hv.2 this
  Â· -- ** direction 2: empty orthogonal complement implies maximal
    simp only [Subset.antisymm_iff]
    rintro h u (huv : v âŠ† u) hu
    refine âŸ¨?_, huvâŸ©
    intro x hxu
    refine ((mt (h x)) (hu.ne_zero âŸ¨x, hxuâŸ©)).imp_symm ?_
    intro hxv y hy
    have hxv' : (âŸ¨x, hxuâŸ© : u) âˆ‰ ((â†‘) â»Â¹' v : Set u) := by simp [huv, hxv]
    obtain âŸ¨l, hl, rflâŸ© :
      âˆƒ l âˆˆ supported ğ•œ ğ•œ ((â†‘) â»Â¹' v : Set u), (linearCombination ğ•œ ((â†‘) : u â†’ E)) l = y := by
      rw [â† Finsupp.mem_span_image_iff_linearCombination]
      simp [huv, inter_eq_self_of_subset_right, hy]
    exact hu.inner_finsupp_eq_zero hxv' hl

variable [FiniteDimensional ğ•œ E]

/-- An orthonormal set in a finite-dimensional `InnerProductSpace` is maximal, if and only if it
is a basis. -/
theorem maximal_orthonormal_iff_basis_of_finiteDimensional (hv : Orthonormal ğ•œ ((â†‘) : v â†’ E)) :
    (âˆ€ u âŠ‡ v, Orthonormal ğ•œ ((â†‘) : u â†’ E) â†’ u = v) â†” âˆƒ b : Basis v ğ•œ E, â‡‘b = ((â†‘) : v â†’ E) := by
  haveI := FiniteDimensional.proper_rclike ğ•œ (span ğ•œ v)
  rw [maximal_orthonormal_iff_orthogonalComplement_eq_bot hv]
  rw [Submodule.orthogonal_eq_bot_iff]
  have hv_coe : range ((â†‘) : v â†’ E) = v := by simp
  constructor
  Â· refine fun h => âŸ¨Basis.mk hv.linearIndependent _, Basis.coe_mk _ ?_âŸ©
    convert h.ge
  Â· rintro âŸ¨h, coe_hâŸ©
    rw [â† h.span_eq, coe_h, hv_coe]

end OrthonormalBasis
