/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.Normed.Operator.WeakOperatorTopology

/-!
# The weak operator topology in Hilbert spaces

This file gives a few properties of the weak operator topology that are specific to operators on
Hilbert spaces. This mostly involves using the FrÃ©chet-Riesz representation to convert between
applications of elements of the dual and inner products with vectors in the space.
-/

open scoped Topology

namespace ContinuousLinearMapWOT

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [RCLike ğ•œ] [AddCommGroup E] [TopologicalSpace E]
  [Module ğ•œ E] [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

@[ext]
lemma ext_inner {A B : E â†’WOT[ğ•œ] F} (h : âˆ€ x y, âŸªy, A xâŸ«_ğ•œ = âŸªy, B xâŸ«_ğ•œ) : A = B := by
  rw [ext_iff]
  exact fun x => ext_inner_left ğ•œ fun y => h x y

lemma ext_inner_iff {A B : E â†’WOT[ğ•œ] F} : A = B â†” âˆ€ x y, âŸªy, A xâŸ«_ğ•œ = âŸªy, B xâŸ«_ğ•œ :=
  âŸ¨fun h _ _ => by simp [h], ext_innerâŸ©

open Filter in
/-- The defining property of the weak operator topology: a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `âŸªy, (f a) xâŸ«` tends to `âŸªy, A xâŸ«` along the same filter. -/
lemma tendsto_iff_forall_inner_apply_tendsto [CompleteSpace F] {Î± : Type*} {l : Filter Î±}
    {f : Î± â†’ E â†’WOT[ğ•œ] F} {A : E â†’WOT[ğ•œ] F} :
    Tendsto f l (ğ“ A) â†” âˆ€ x y, Tendsto (fun a => âŸªy, (f a) xâŸ«_ğ•œ) l (ğ“ âŸªy, A xâŸ«_ğ•œ) := by
  simp only [â† InnerProductSpace.toDual_apply]
  refine âŸ¨fun h x y => ?_, fun h => ?_âŸ©
  Â· exact (tendsto_iff_forall_dual_apply_tendsto.mp h) _ _
  Â· have h' : âˆ€ (x : E) (y : NormedSpace.Dual ğ•œ F),
        Tendsto (fun a => y (f a x)) l (ğ“ (y (A x))) := by
      intro x y
      convert h x ((InnerProductSpace.toDual ğ•œ F).symm y) <;> simp
    exact tendsto_iff_forall_dual_apply_tendsto.mpr h'

lemma le_nhds_iff_forall_inner_apply_le_nhds [CompleteSpace F] {l : Filter (E â†’WOT[ğ•œ] F)}
    {A : E â†’WOT[ğ•œ] F} : l â‰¤ ğ“ A â†” âˆ€ x y, l.map (fun T => âŸªy, T xâŸ«_ğ•œ) â‰¤ ğ“ (âŸªy, A xâŸ«_ğ•œ) :=
  tendsto_iff_forall_inner_apply_tendsto (f := id)

end ContinuousLinearMapWOT
