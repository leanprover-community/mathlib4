/-
Copyright (c) 2024 FrÃ©dÃ©ric Dupuis. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.LocallyConvex.WithSeminorms

/-!
# The weak operator topology

This file defines a type copy of `E â†’L[ğ•œ] F` (where `F` is an inner product space) which is
endowed with the weak operator topology (WOT) rather than the topology induced by the operator norm.
The WOT is defined as the coarsest topology such that the functional `fun A => âŸªy, A xâŸ«` is
continuous for any `x : E` and `y : F`. Basic non-topological properties of `E â†’L[ğ•œ] F` (such as
the module structure) are copied over to the type copy.

We also prove that the WOT is induced by the family of seminorms `â€–âŸªy, A xâŸ«â€–` for `x : E` and
`y : F`.

## Main declarations

* `ContinuousLinearMapWOT ğ•œ E F`: The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator
  topology.
* `ContinuousLinearMapWOT.tendsto_iff_forall_inner_apply_tendsto`: a function `f` tends to
  `A : E â†’WOT[ğ•œ] F` along filter `l` iff `âŸªy, (f a), xâŸ«` tends to `âŸªy, A xâŸ«` along the same filter.
* `ContinuousLinearMap.toWOT`: the inclusion map from `E â†’L[ğ•œ] F` to the type copy
* `ContinuousLinearMap.continuous_toWOT`: the inclusion map is continuous, i.e. the WOT is coarser
  than the norm topology.
* `ContinuousLinearMapWOT.withSeminorms`: the WOT is induced by the family of seminorms
  `â€–âŸªy, A xâŸ«â€–` for `x : E` and `y : F`.

## Notation

* The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology is denoted by
  `E â†’WOT[ğ•œ] F`.

## Implementation notes

In the literature, the WOT is only defined on operators on a Hilbert space, i.e. `E â†’L[ğ•œ] E`. Here
we generalize this a bit to `E â†’L[ğ•œ] F` where `F` is an inner product space, and `E` actually only
needs to be a vector space with some topology for most results in this file.
-/

open scoped Topology

/-- The type copy of `E â†’L[ğ•œ] F` endowed with the weak operator topology, denoted as
`E â†’WOT[ğ•œ] F`. -/
def ContinuousLinearMapWOT (ğ•œ : Type*) (E : Type*) (F : Type*) [RCLike ğ•œ] [AddCommGroup E]
    [TopologicalSpace E] [Module ğ•œ E] [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] := E â†’L[ğ•œ] F

notation E " â†’WOT[" ğ•œ "]" F => ContinuousLinearMapWOT ğ•œ E F

namespace ContinuousLinearMapWOT

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [RCLike ğ•œ] [AddCommGroup E] [TopologicalSpace E]
  [Module ğ•œ E] [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner (ğ•œ := ğ•œ) x y

/-!
### Basic properties common with `E â†’L[ğ•œ] F`

The section copies basic non-topological properties of `E â†’L[ğ•œ] F` over to `E â†’WOT[ğ•œ] F`, such as
the module structure, `FunLike`, etc.
-/
section Basic

instance instAddCommGroup : AddCommGroup (E â†’WOT[ğ•œ] F) :=
  inferInstanceAs <| AddCommGroup (E â†’L[ğ•œ] F)

instance instModule : Module ğ•œ (E â†’WOT[ğ•œ] F) := inferInstanceAs <| Module ğ•œ (E â†’L[ğ•œ] F)

variable (ğ•œ) (E) (F)
/-- The equivalence that sends a continuous linear map to the type copy endowed with the
weak operator topology.  -/
def _root_.ContinuousLinearMap.toWOT : (E â†’L[ğ•œ] F) â‰ƒâ‚—[ğ•œ] (E â†’WOT[ğ•œ] F) := LinearEquiv.refl ğ•œ _

variable {ğ•œ} {E} {F}

instance instFunLike : FunLike (E â†’WOT[ğ•œ] F) E F where
  coe f :=  ((ContinuousLinearMap.toWOT ğ•œ E F).symm f : E â†’ F)
  coe_injective' := by intro; simp

instance instContinuousLinearMapClass : ContinuousLinearMapClass (E â†’WOT[ğ•œ] F) ğ•œ E F where
  map_add f x y := by simp only [DFunLike.coe]; simp
  map_smulâ‚›â‚— f r x := by simp only [DFunLike.coe]; simp
  map_continuous f := ContinuousLinearMap.continuous ((ContinuousLinearMap.toWOT ğ•œ E F).symm f)

lemma _root_.ContinuousLinearMap.toWOT_apply {A : E â†’L[ğ•œ] F} {x : E} :
    ((ContinuousLinearMap.toWOT ğ•œ E F) A) x = A x := rfl

lemma ext_iff {A B : E â†’WOT[ğ•œ] F} : A = B â†” âˆ€ x, A x = B x := ContinuousLinearMap.ext_iff

lemma ext_inner_iff {A B : E â†’WOT[ğ•œ] F} : A = B â†” âˆ€ x y, âŸªy, A xâŸ« = âŸªy, B xâŸ« := by
  refine âŸ¨fun h _ _ => by simp [h], fun h => ?_âŸ©
  rw [ext_iff]
  exact fun x => ext_inner_left ğ•œ fun y => h x y

@[simp] lemma zero_apply (x : E) : (0 : E â†’WOT[ğ•œ] F) x = 0 := by simp only [DFunLike.coe]; rfl

@[simp] lemma add_apply {f g : E â†’WOT[ğ•œ] F} (x : E) : (f + g) x = f x + g x := by
  simp only [DFunLike.coe]; rfl

@[simp] lemma neg_apply {f : E â†’WOT[ğ•œ] F} (x : E) : (-f) x = -(f x) := by
  simp only [DFunLike.coe]; rfl

@[simp] lemma smul_apply {f : E â†’WOT[ğ•œ] F} (c : ğ•œ) (x : E) : (c â€¢ f) x = c â€¢ (f x) := by
  simp only [DFunLike.coe]; rfl

end Basic

/-!
### The topology of `E â†’WOT[ğ•œ] F`

The section endows `E â†’WOT[ğ•œ] F` with the weak operator topology and shows the basic properties
of this topology. In particular, we show that it is a topological vector space.
-/
section Topology

/-- The weak operator topology is the coarsest topology such that `fun A => âŸªx, A yâŸ«` is
continuous for all `x, y`. -/
instance instTopologicalSpace : TopologicalSpace (E â†’WOT[ğ•œ] F) :=
  TopologicalSpace.induced (fun A (âŸ¨x, yâŸ© : E Ã— F) => âŸªy, A xâŸ«) Pi.topologicalSpace

lemma coeFn_continuous : Continuous fun (A : E â†’WOT[ğ•œ] F) (âŸ¨x, yâŸ© : E Ã— F) => âŸªy, A xâŸ« :=
  continuous_induced_dom

lemma inner_apply_continuous (x : E) (y : F) : Continuous fun (A : E â†’WOT[ğ•œ] F) => âŸªy, A xâŸ« := by
  refine (continuous_pi_iff.mp coeFn_continuous) âŸ¨x, yâŸ©

lemma continuous_of_inner_apply_continuous {Î± : Type*} [TopologicalSpace Î±] {g : Î± â†’ E â†’WOT[ğ•œ] F}
    (h : âˆ€ x y, Continuous fun a => âŸªy, (g a) xâŸ«) : Continuous g :=
  continuous_induced_rng.2 (continuous_pi_iff.mpr fun p => h p.1 p.2)

lemma embedding : Embedding fun (A : E â†’WOT[ğ•œ] F) (p : E Ã— F) => âŸªp.2, A p.1âŸ« := by
  refine Function.Injective.embedding_induced fun A B hAB => ?_
  rw [ext_inner_iff]
  simpa [Function.funext_iff] using hAB

open Filter in
/-- The defining property of the weak operator topology: a function `f` tends to
`A : E â†’WOT[ğ•œ] F` along filter `l` iff `âŸªy, (f a), xâŸ«` tends to `âŸªy, A xâŸ«` along the same filter. -/
lemma tendsto_iff_forall_inner_apply_tendsto {Î± : Type*} {l : Filter Î±} {f : Î± â†’ E â†’WOT[ğ•œ] F}
    {A : E â†’WOT[ğ•œ] F} :
    Tendsto f l (ğ“ A) â†” âˆ€ x y, Tendsto (fun a => âŸªy, (f a) xâŸ«) l (ğ“ âŸªy, A xâŸ«) := by
  have hmain : (âˆ€ x y, Tendsto (fun a => âŸªy, (f a) xâŸ«) l (ğ“ âŸªy, A xâŸ«))
      â†” âˆ€ (p : E Ã— F), Tendsto (fun a => âŸªp.2, (f a) p.1âŸ«) l (ğ“ âŸªp.2, A p.1âŸ«) :=
    âŸ¨fun h p => h p.1 p.2, fun h x y => h âŸ¨x, yâŸ©âŸ©
  rw [hmain, â† tendsto_pi_nhds, Embedding.tendsto_nhds_iff embedding]
  rfl

lemma le_nhds_iff_forall_inner_apply_le_nhds {l : Filter (E â†’WOT[ğ•œ] F)} {A : E â†’WOT[ğ•œ] F} :
    l â‰¤ ğ“ A â†” âˆ€ x y, l.map (fun T => âŸªy, T xâŸ«) â‰¤ ğ“ (âŸªy, A xâŸ«) :=
  tendsto_iff_forall_inner_apply_tendsto (f := id)

instance instT2Space : T2Space (E â†’WOT[ğ•œ] F) := Embedding.t2Space embedding

instance instContinuousAdd : ContinuousAdd (E â†’WOT[ğ•œ] F) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  change Continuous (fun âŸ¨A, BâŸ© p => âŸªp.2, (A + B) p.1âŸ«)
  simp only [add_apply, inner_add_right]
  refine Continuous.add ?_ ?_
  Â· exact Continuous.comp coeFn_continuous continuous_fst
  Â· exact Continuous.comp coeFn_continuous continuous_snd

instance instContinuousSMul : ContinuousSMul ğ•œ (E â†’WOT[ğ•œ] F) := by
  refine âŸ¨continuous_induced_rng.2 ?_âŸ©
  change Continuous (fun âŸ¨r, AâŸ© p => âŸªp.2, (r â€¢ A) p.1âŸ«)
  simp only [smul_apply, inner_smul_right]
  refine Continuous.mul ?_ ?_
  Â· change Continuous ((fun x _ => x) âˆ˜ Prod.fst)
    exact Continuous.comp (by fun_prop) continuous_fst
  Â· exact Continuous.comp coeFn_continuous continuous_snd

instance instTopologicalAddGroup : TopologicalAddGroup (E â†’WOT[ğ•œ] F) where
  continuous_neg := by
    refine continuous_induced_rng.2 ?_
    change Continuous (fun (A : E â†’WOT[ğ•œ] F) (p : E Ã— F) => âŸªp.2, (-A) p.1âŸ«)
    simp only [neg_apply, inner_neg_right]
    exact Continuous.comp continuous_neg coeFn_continuous

instance instUniformSpace : UniformSpace (E â†’WOT[ğ•œ] F) := TopologicalAddGroup.toUniformSpace _

instance instUniformAddGroup : UniformAddGroup (E â†’WOT[ğ•œ] F) :=
  comm_topologicalAddGroup_is_uniform

end Topology

/-! ### The WOT is induced by a family of seminorms -/
section Seminorms

/-- The family of seminorms that induce the weak operator topology, namely `â€–âŸªy, A xâŸ«â€–` for
all `x` and `y`.  -/
def seminorm (x : E) (y : F) : Seminorm ğ•œ (E â†’WOT[ğ•œ] F) where
  toFun A := â€–âŸªy, A xâŸ«â€–
  map_zero' := by simp
  add_le' A B := by simp [inner_add_right]; exact norm_add_le _ _
  neg' A := by simp
  smul' r A := by simp [inner_smul_right]

variable (ğ•œ) (E) (F) in
/-- The family of seminorms that induce the weak operator topology, namely `â€–âŸªy, A xâŸ«â€–` for
all `x` and `y`.  -/
def seminormFamily : SeminormFamily ğ•œ (E â†’WOT[ğ•œ] F) (E Ã— F) :=
  fun âŸ¨x, yâŸ© => seminorm x y

lemma hasBasis_seminorms : (ğ“ (0 : E â†’WOT[ğ•œ] F)).HasBasis (seminormFamily ğ•œ E F).basisSets id := by
  let p := seminormFamily ğ•œ E F
  rw [nhds_induced, nhds_pi]
  simp only [map_zero, zero_apply, inner_zero_right]
  have h := @Metric.nhds_basis_ball ğ•œ _ 0
  have h' := Filter.hasBasis_pi fun _ : (E Ã— F) => h
  have h'' := Filter.HasBasis.comap (fun (A : E â†’WOT[ğ•œ] F) (p : E Ã— F) => âŸªp.2, A p.1âŸ«) h'
  refine h''.to_hasBasis ?_ ?_
  Â· intro U hU
    obtain âŸ¨hUâ‚, hUâ‚‚âŸ© := hU
    simp only [id]
    let U' := hUâ‚.toFinset
    by_cases hUâ‚ƒ : U.fst.Nonempty
    Â· have hUâ‚ƒ' : U'.Nonempty := hUâ‚.toFinset_nonempty.mpr hUâ‚ƒ
      refine âŸ¨(U'.sup p).ball 0 <| U'.inf' hUâ‚ƒ' U.snd, p.basisSets_mem _ <|
        (Finset.lt_inf'_iff _).2 fun y hy => hUâ‚‚ y <| hUâ‚.mem_toFinset.mp hy, fun x hx y hy => ?_âŸ©
      simp only [Set.mem_preimage, Set.mem_pi, mem_ball_zero_iff]
      rw [Seminorm.mem_ball_zero] at hx
      have hyU' : y âˆˆ U' := (Set.Finite.mem_toFinset hUâ‚).mpr hy
      have hp : p y â‰¤ U'.sup p := Finset.le_sup hyU'
      refine lt_of_le_of_lt (hp x) (lt_of_lt_of_le hx ?_)
      exact Finset.inf'_le _ hyU'
    Â· rw [Set.not_nonempty_iff_eq_empty.mp hUâ‚ƒ]
      simp only [Set.empty_pi, Set.preimage_univ, Set.subset_univ, and_true_iff]
      exact âŸ¨(p 0).ball 0 1, p.basisSets_singleton_mem 0 one_posâŸ©
  Â· intro U (hU : U âˆˆ p.basisSets)
    rw [SeminormFamily.basisSets_iff] at hU
    obtain âŸ¨s, r, hr, hUâŸ© := hU
    rw [hU]
    refine âŸ¨(s, fun _ => r), âŸ¨by simp only [s.finite_toSet], fun y _ => hrâŸ©, fun x hx => ?_âŸ©
    simp only [Set.mem_preimage, Set.mem_pi, Finset.mem_coe, mem_ball_zero_iff] at hx
    simp only [_root_.id, Seminorm.mem_ball, sub_zero]
    exact Seminorm.finset_sup_apply_lt hr fun y hy => hx y hy

lemma withSeminorms : WithSeminorms (seminormFamily ğ•œ E F) :=
  SeminormFamily.withSeminorms_of_hasBasis _ hasBasis_seminorms

instance instLocallyConvexSpace [Module â„ (E â†’WOT[ğ•œ] F)] [IsScalarTower â„ ğ•œ (E â†’WOT[ğ•œ] F)] :
    LocallyConvexSpace â„ (E â†’WOT[ğ•œ] F) :=
  withSeminorms.toLocallyConvexSpace

end Seminorms

end ContinuousLinearMapWOT

section NormedSpace

variable {ğ•œ : Type*} {E : Type*} {F : Type*} [RCLike ğ•œ] [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

/-- The weak operator topology is coarser than the norm topology, i.e. the inclusion map is
continuous. -/
@[continuity, fun_prop]
lemma ContinuousLinearMap.continuous_toWOT :
    Continuous (ContinuousLinearMap.toWOT ğ•œ E F) := by
  refine ContinuousLinearMapWOT.continuous_of_inner_apply_continuous fun x y => ?_
  simp_rw [ContinuousLinearMap.toWOT_apply]
  refine Continuous.inner continuous_const ?_
  change Continuous fun a => (ContinuousLinearMap.id ğ•œ (E â†’L[ğ•œ] F)).flip x a
  fun_prop

/-- The inclusion map from `E â†’[ğ•œ] F` to `E â†’WOT[ğ•œ] F`, bundled as a continuous linear map. -/
def ContinuousLinearMap.toWOTCLM : (E â†’L[ğ•œ] F) â†’L[ğ•œ] (E â†’WOT[ğ•œ] F) :=
  âŸ¨LinearEquiv.toLinearMap (ContinuousLinearMap.toWOT ğ•œ E F), ContinuousLinearMap.continuous_toWOTâŸ©

end NormedSpace
