/-
Copyright (c) 2024 Jireh Loreaux. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jireh Loreaux
-/
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.CStarAlgebra.ContinuousFunctionalCalculus.Basic
import Mathlib.Analysis.CStarAlgebra.ContinuousLinearMap

/-!
# Continuous linear maps on a Hilbert space are a `StarOrderedRing`

In this file we show that the continuous linear maps on a complex Hilbert space form a
`StarOrderedRing`.  Note that they are already equipped with the Loewner partial order. We also
prove that, with respect to this partial order, a map is positive if every element of the
real spectrum is nonnegative. Consequently, when `H` is a Hilbert space, then `H â†’L[â„‚] H` is
equipped with all the usual instances of the continuous functional calculus.

-/

namespace ContinuousLinearMap

open RCLike
open scoped NNReal

variable {ğ•œ H : Type*} [RCLike ğ•œ] [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] [CompleteSpace H]
variable [Algebra â„ (H â†’L[ğ•œ] H)] [IsScalarTower â„ ğ•œ (H â†’L[ğ•œ] H)]

open scoped InnerProductSpace in
lemma IsPositive.spectrumRestricts {f : H â†’L[ğ•œ] H} (hf : f.IsPositive) :
    SpectrumRestricts f ContinuousMap.realToNNReal := by
  rw [SpectrumRestricts.nnreal_iff]
  intro c hc
  contrapose! hc
  rw [spectrum.notMem_iff, IsUnit.sub_iff, sub_eq_add_neg, â† map_neg]
  rw [â† neg_pos] at hc
  set c := -c
  exact isUnit_of_forall_le_norm_inner_map _ (c := âŸ¨c, hc.leâŸ©) hc fun x â†¦ calc
    â€–xâ€– ^ 2 * c = re âŸªalgebraMap â„ (H â†’L[ğ•œ] H) c x, xâŸ«_ğ•œ := by
      rw [Algebra.algebraMap_eq_smul_one, â† algebraMap_smul ğ•œ c (1 : (H â†’L[ğ•œ] H)), coe_smul',
        Pi.smul_apply, one_apply, inner_smul_left, RCLike.algebraMap_eq_ofReal, conj_ofReal,
        re_ofReal_mul, inner_self_eq_norm_sq, mul_comm]
    _ â‰¤ re âŸª(f + (algebraMap â„ (H â†’L[ğ•œ] H)) c) x, xâŸ«_ğ•œ := by
      simpa only [add_apply, inner_add_left, map_add, le_add_iff_nonneg_left]
        using hf.re_inner_nonneg_left x
    _ â‰¤ â€–âŸª(f + (algebraMap â„ (H â†’L[ğ•œ] H)) c) x, xâŸ«_ğ•œâ€– := RCLike.re_le_norm _

instance : NonnegSpectrumClass â„ (H â†’L[ğ•œ] H) where
  quasispectrum_nonneg_of_nonneg f hf :=
    QuasispectrumRestricts.nnreal_iff.mp <| sub_zero f â–¸ hf.spectrumRestricts

/-- Because this takes `ContinuousFunctionalCalculus â„ (H â†’L[ğ•œ] H) IsSelfAdjoint` as an argument,
and for the moment we only have this for `ğ•œ := â„‚`, this is not registered as an instance. -/
lemma instStarOrderedRingRCLike
    [ContinuousFunctionalCalculus â„ (H â†’L[ğ•œ] H) IsSelfAdjoint] :
    StarOrderedRing (H â†’L[ğ•œ] H) where
  le_iff f g := by
    constructor
    Â· intro h
      rw [le_def] at h
      obtain âŸ¨p, hpâ‚, -, hpâ‚ƒâŸ© :=
        CFC.exists_sqrt_of_isSelfAdjoint_of_quasispectrumRestricts h.1 h.spectrumRestricts
      refine âŸ¨p ^ 2, ?_, by symm; rwa [add_comm, â† eq_sub_iff_add_eq]âŸ©
      exact AddSubmonoid.subset_closure âŸ¨p, by simp only [hpâ‚.star_eq, sq]âŸ©
    Â· rintro âŸ¨p, hp, rflâŸ©
      rw [le_def, add_sub_cancel_left]
      induction hp using AddSubmonoid.closure_induction with
      | mem _ hf =>
        obtain âŸ¨f, rflâŸ© := hf
        simpa using ContinuousLinearMap.IsPositive.adjoint_conj isPositive_one f
      | zero => exact isPositive_zero
      | add f g _ _ hf hg => exact hf.add hg

instance instStarOrderedRing {H : Type*} [NormedAddCommGroup H]
    [InnerProductSpace â„‚ H] [CompleteSpace H] : StarOrderedRing (H â†’L[â„‚] H) :=
  instStarOrderedRingRCLike

end ContinuousLinearMap
