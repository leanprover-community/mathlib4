/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jack Cheverton, Samyak Dhar Tuladhar
-/

import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Order.CompleteLattice
import Mathlib.LinearAlgebra.Eigenspace.Basic
import Mathlib.LinearAlgebra.Eigenspace.Triangularizable

/-! # Joint eigenspaces of commuting symmetric operators

This file collects various decomposition results for joint eigenspaces of commuting
symmetric operators on a finite-dimensional inner product space.

# Main Result

* `LinearMap.IsSymmetric.directSum_isInternal_of_commute` establishes that in finite dimensions
   if `{A B : E â†’â‚—[ğ•œ] E}`, then `IsSymmetric A`, `IsSymmetric B` and `A âˆ˜â‚— B = B âˆ˜â‚— A` imply that
   `E` decomposes as an internal direct sum of the pairwise orthogonal spaces
   `eigenspace B Î¼ âŠ“ eigenspace A Î½`
* `LinearMap.IsSymmetric.iSup_iInf_eq_top_of_commute` establishes that in finite dimensions,
   the indexed supremum of the joint eigenspaces of a commuting tuple of symmetric linear operators
   equals `âŠ¤`
* `LinearMap.IsSymmetric.directSum_isInternal_of_commute_of_fintype` establishes the
   analogous result to `LinearMap.IsSymmetric.directSum_isInternal_of_commute` for commuting
   tuples of symmetric operators.

## TODO

Develop a `Diagonalization` structure for linear maps and / or matrices which consists of a basis,
and a proof obligation that the basis vectors are eigenvectors.

## Tags

symmetric operator, simultaneous eigenspaces, joint eigenspaces

-/

open Module.End

namespace LinearMap

namespace IsSymmetric

variable {ğ•œ E n m : Type*}

open Submodule

section RCLike

variable [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {Î± : ğ•œ} {A B : E â†’â‚—[ğ•œ] E} {T : n â†’ E â†’â‚—[ğ•œ] E}

/-- The joint eigenspaces of a pair of commuting symmetric operators form an
`OrthogonalFamily`. -/
theorem orthogonalFamily_eigenspace_inf_eigenspace (hA : A.IsSymmetric) (hB : B.IsSymmetric) :
    OrthogonalFamily ğ•œ (fun (i : ğ•œ Ã— ğ•œ) => (eigenspace A i.2 âŠ“ eigenspace B i.1 : Submodule ğ•œ E))
    (fun i => (eigenspace A i.2 âŠ“ eigenspace B i.1).subtypeâ‚—áµ¢) :=
     OrthogonalFamily.of_pairwise fun i j hij v âŸ¨hv1 , hv2âŸ© â†¦ by
    obtain (hâ‚ | hâ‚‚) : i.1 â‰  j.1 âˆ¨ i.2 â‰  j.2 := by rwa [Ne.eq_def, Prod.ext_iff, not_and_or] at hij
    all_goals intro w âŸ¨hw1, hw2âŸ©
    Â· exact hB.orthogonalFamily_eigenspaces.pairwise hâ‚ hv2 w hw2
    Â· exact hA.orthogonalFamily_eigenspaces.pairwise hâ‚‚ hv1 w hw1

/-- The joint eigenspaces of a tuple of commuting symmetric operators form an
`OrthogonalFamily`. -/
theorem orthogonalFamily_iInf_eigenspaces
    (hT : âˆ€ i, (T i).IsSymmetric) :
    OrthogonalFamily ğ•œ (fun Î³ : n â†’ ğ•œ â†¦ (â¨… j, eigenspace (T j) (Î³ j) : Submodule ğ•œ E))
      fun Î³ : n â†’ ğ•œ â†¦ (â¨… j, eigenspace (T j) (Î³ j)).subtypeâ‚—áµ¢ := by
  intro f g hfg Ef Eg
  obtain âŸ¨a , haâŸ© := Function.ne_iff.mp hfg
  have H := (orthogonalFamily_eigenspaces (hT a) ha)
  simp only [Submodule.coe_subtypeâ‚—áµ¢, Submodule.coe_subtype, Subtype.forall] at H
  apply H
  Â· exact (Submodule.mem_iInf <| fun _ â†¦ eigenspace (T _) (f _)).mp Ef.2 _
  Â· exact (Submodule.mem_iInf <| fun _ â†¦ eigenspace (T _) (g _)).mp Eg.2 _

variable [FiniteDimensional ğ•œ E]

/-- If A and B are commuting symmetric operators on a finite dimensional inner product space
then the eigenspaces of the restriction of B to any eigenspace of A exhaust that eigenspace. -/
theorem iSup_eigenspace_inf_eigenspace (hB : B.IsSymmetric) (hAB : Commute A B) :
    (â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = eigenspace A Î± := by
  conv_rhs => rw [â† (eigenspace A Î±).map_subtype_top]
  have H : âˆ€ Î¼, genEigenspace B Î¼ 1 = eigenspace B Î¼ := fun Î¼ â†¦ rfl
  simp only [â† H, â† Submodule.map_iSup,
    (eigenspace A Î±).inf_genEigenspace _ (mapsTo_genEigenspace_of_comm hAB Î± 1)]
  congr 1
  simpa only [H, Submodule.orthogonal_eq_bot_iff]
    using orthogonalComplement_iSup_eigenspaces_eq_bot <|
      hB.restrict_invariant <| mapsTo_genEigenspace_of_comm hAB Î± 1

/-- If A and B are commuting symmetric operators acting on a finite dimensional inner product space,
then the simultaneous eigenspaces of A and B exhaust the space. -/
theorem iSup_iSup_eigenspace_inf_eigenspace_eq_top (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : Commute A B) :
    (â¨† Î±, â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = âŠ¤ := by
  simpa [iSup_eigenspace_inf_eigenspace hB hAB] using
    Submodule.orthogonal_eq_bot_iff.mp <| hA.orthogonalComplement_iSup_eigenspaces_eq_bot

/-- Given a commuting pair of symmetric linear operators on a finite dimensional inner product
space, the space decomposes as an internal direct sum of simultaneous eigenspaces of these
operators. -/
theorem directSum_isInternal_of_commute (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : Commute A B) :
    DirectSum.IsInternal (fun (i : ğ•œ Ã— ğ•œ) â†¦ (eigenspace A i.2 âŠ“ eigenspace B i.1)):= by
  apply (orthogonalFamily_eigenspace_inf_eigenspace hA hB).isInternal_iff.mpr
  rw [Submodule.orthogonal_eq_bot_iff, iSup_prod, iSup_comm]
  exact iSup_iSup_eigenspace_inf_eigenspace_eq_top hA hB hAB

/-- If `F` is an invariant subspace of a symmetric operator `S`, then `F` is the supremum of the
eigenspaces of the restriction of `S` to `F`. -/
theorem iSup_eigenspace_restrict {F : Submodule ğ•œ E}
    (S : E â†’â‚—[ğ•œ] E) (hS : IsSymmetric S) (hInv : Set.MapsTo S F F) :
    â¨† Î¼, map F.subtype (eigenspace (S.restrict hInv) Î¼) = F := by
  conv_lhs => rw [â† Submodule.map_iSup]
  conv_rhs => rw [â† map_subtype_top F]
  congr!
  have H : IsSymmetric (S.restrict hInv) := fun x y â†¦ hS (F.subtype x) y
  apply orthogonal_eq_bot_iff.mp (H.orthogonalComplement_iSup_eigenspaces_eq_bot)

/-- In finite dimensions, the indexed supremum of joint `maxGenEigenspaces` of a finite tuple of
commuting operators equals `âŠ¤` provided the indexed supremum of `maxGenEigenspaces` of each
operator in the tuple equals `âŠ¤`. -/
theorem iSup_iInf_maxGenEigenspace_eq_top_of_iSup_maxGenEigenspace_eq_top_of_commute {Î¹ K V : Type*}
    [Field K] [AddCommGroup V] [Module K V] [FiniteDimensional K V]
    (f : Î¹ â†’ Module.End K V) (h : Pairwise fun i j â†¦ Commute (f i) (f j))
    (h' : âˆ€ i, â¨† Î¼, (f i).maxGenEigenspace Î¼ = âŠ¤) :
    â¨† Ï‡ : Î¹ â†’ K, â¨… i, (f i).maxGenEigenspace (Ï‡ i) = âŠ¤ := by
  refine Module.End.iSup_iInf_maxGenEigenspace_eq_top_of_forall_mapsTo _
    (fun i j â†¦ Module.End.mapsTo_maxGenEigenspace_of_comm ?_) h'
  rcases eq_or_ne j i with rfl | hij <;> tauto

/-- Every generalized eigenspace of a symmetric operator is an eigenspace. -/
theorem genEigenspace_eq_eigenspace
    {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) {n : â„•} {Î¼ : ğ•œ} (hn : 1 â‰¤ n) :
    genEigenspace T Î¼ n = genEigenspace T Î¼ 1 := by
  refine Nat.le_induction rfl (fun k hk ih â†¦ ?_) n hn
  refine ih â–¸ le_antisymm (fun x hx â†¦ ?_) ((genEigenspace T Î¼).mono k.le_succ)
  obtain (rfl | hx_ne) := eq_or_ne x 0
  Â· exact zero_mem _
  Â· have hÎ¼ : HasEigenvalue T Î¼ := hasEigenvalue_of_hasGenEigenvalue (k := k + 1) <|
      (genEigenspace T Î¼ (k + 1)).ne_bot_iff.mpr âŸ¨x, hx, hx_neâŸ©
    have hT' := LinearMap.isSymmetric_iff_isSelfAdjoint T |>.mp hT
    have hTÎ¼ : ((T - Î¼ â€¢ 1) ^ k).IsSymmetric  := by
      rw [LinearMap.isSymmetric_iff_isSelfAdjoint]
      refine .pow (hT'.sub (.smul ?_ ?_)) k
      Â· exact hT.conj_eigenvalue_eq_self hÎ¼
      Â· exact (LinearMap.isSymmetric_iff_isSelfAdjoint 1).mp LinearMap.IsSymmetric.id
    rw [mem_genEigenspace, â† norm_eq_zero, â† sq_eq_zero_iff, norm_sq_eq_inner (ğ•œ := ğ•œ)]
    rw [hTÎ¼, â† LinearMap.comp_apply, â† LinearMap.mul_eq_comp, â† pow_add]
    simp [mem_genEigenspace .. |>.mp <| (genEigenspace T Î¼).mono (show k + 1 â‰¤ k + k by gcongr) hx]

lemma maxGenEigenspace_eq_eigenspace
    {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) {Î¼ : ğ•œ} :
    maxGenEigenspace T Î¼ = eigenspace T Î¼ := calc
  _ = â¨† n, genEigenspace T Î¼ (n + 1) := by
    rw [maxGenEigenspace_def, â† sup_iSup_nat_succ, genEigenspace_def]; simp [LinearMap.one_eq_id]
  _ = â¨† _ : â„•, genEigenspace T Î¼ 1 := by
    congr! 2 with n; exact genEigenspace_eq_eigenspace hT n.succ_pos
  _ = eigenspace T Î¼ := by simp [genEigenspace_def, eigenspace_def]

/-- In finite dimensions, the indexed supremum of the joint eigenspaces of a commuting tuple of
symmetric linear operators equals `âŠ¤`. -/
theorem iSup_iInf_eq_top_of_commute {Î¹ : Type*}
    (T : Î¹ â†’ E â†’â‚—[ğ•œ] E) (hT : âˆ€ i, (T i).IsSymmetric) (h : Pairwise fun i j â†¦ Commute (T i) (T j)):
    â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i, eigenspace (T i) (Ï‡ i) = âŠ¤ := calc
  _ = â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i, maxGenEigenspace (T i) (Ï‡ i) :=
    congr(â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i, $((hT i).maxGenEigenspace_eq_eigenspace.symm))
  _ = âŠ¤ :=
    iSup_iInf_maxGenEigenspace_eq_top_of_iSup_maxGenEigenspace_eq_top_of_commute T h fun i â†¦ by
    rw [â† orthogonal_eq_bot_iff, congr(â¨† Î¼, $((hT i).maxGenEigenspace_eq_eigenspace)),
      (hT i).orthogonalComplement_iSup_eigenspaces_eq_bot]

/-- In finite dimensions, given a finite commuting family of symmetric linear operators, the inner
product space on which they act decomposes as an internal direct sum of joint eigenspaces. -/
theorem LinearMap.IsSymmetric.directSum_isInternal_of_commute_of_fintype [Fintype n]
    (hT :âˆ€ i, (T i).IsSymmetric) (hC : âˆ€ i j, Commute (T i) (T j)) :
    DirectSum.IsInternal (fun Î± : n â†’ ğ•œ â†¦ â¨… j, eigenspace (T j) (Î± j)) := by
  rw [OrthogonalFamily.isInternal_iff]
  Â· rw [iSup_iInf_eq_top_of_commute _ hT fun â¦ƒ_ _â¦„ _ â†¦ hC _ _, top_orthogonal_eq_bot]
  Â· exact orthogonalFamily_iInf_eigenspaces hT

end RCLike

end IsSymmetric

end LinearMap
