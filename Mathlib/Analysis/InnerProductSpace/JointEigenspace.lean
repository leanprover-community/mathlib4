/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jack Cheverton, Samyak Dhar Tuladhar
-/

import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.Analysis.InnerProductSpace.Projection
import Mathlib.Order.CompleteLattice
import Mathlib.LinearAlgebra.Eigenspace.Basic

/-! # Joint eigenspaces of a commuting pair of symmetric operators

This file collects various decomposition results for joint eigenspaces of a commuting pair
of symmetric operators on a finite-dimensional inner product space.

# Main Result

* `LinearMap.IsSymmetric.directSum_isInternal_of_commute` establishes that
   if `{A B : E ‚Üí‚Çó[ùïú] E}`, then `IsSymmetric A`, `IsSymmetric B` and `A ‚àò‚Çó B = B ‚àò‚Çó A` imply that
   `E` decomposes as an internal direct sum of the pairwise orthogonal spaces
   `eigenspace B Œº ‚äì eigenspace A ŒΩ`

## TODO

Develop a `Diagonalization` structure for linear maps and / or matrices which consists of a basis,
and a proof obligation that the basis vectors are eigenvectors.

## Tags

self-adjoint operator, simultaneous eigenspaces, joint eigenspaces

-/

variable {ùïú E : Type*} [RCLike ùïú]
variable [NormedAddCommGroup E] [InnerProductSpace ùïú E]

open Module.End

namespace LinearMap

namespace IsSymmetric

section Pair

variable {Œ± : ùïú} {A B : E ‚Üí‚Çó[ùïú] E}

/--If a pair of operators commute, then the eigenspaces of one are invariant under the other.-/
theorem eigenspace_invariant_of_commute
    (hAB : A ‚àò‚Çó B = B ‚àò‚Çó A) (Œ± : ùïú) : ‚àÄ v ‚àà (eigenspace A Œ±), (B v ‚àà eigenspace A Œ±) := by
  intro v hv
  rw [eigenspace_def, mem_ker, sub_apply, smul_apply, one_apply, ‚Üê comp_apply A B v, hAB,
    comp_apply B A v, ‚Üê map_smul, ‚Üê map_sub, hv, map_zero] at *

/--The simultaneous eigenspaces of a pair of commuting symmetric operators form an
`OrthogonalFamily`.-/
theorem orthogonalFamily_eigenspace_inf_eigenspace (hA : A.IsSymmetric) (hB : B.IsSymmetric) :
    OrthogonalFamily ùïú (fun (i : ùïú √ó ùïú) => (eigenspace A i.2 ‚äì eigenspace B i.1 : Submodule ùïú E))
    (fun i => (eigenspace A i.2 ‚äì eigenspace B i.1).subtype‚Çó·µ¢) :=
     OrthogonalFamily.of_pairwise fun i j hij v ‚ü®hv1 , hv2‚ü© ‚Ü¶ by
    obtain (h‚ÇÅ | h‚ÇÇ) : i.1 ‚â† j.1 ‚à® i.2 ‚â† j.2 := by rwa [Ne.eq_def, Prod.ext_iff, not_and_or] at hij
    all_goals intro w ‚ü®hw1, hw2‚ü©
    ¬∑ exact hB.orthogonalFamily_eigenspaces.pairwise h‚ÇÅ hv2 w hw2
    ¬∑ exact hA.orthogonalFamily_eigenspaces.pairwise h‚ÇÇ hv1 w hw1

open Submodule in

/-- The intersection of eigenspaces of commuting selfadjoint operators is equal to the eigenspace of
one operator restricted to the eigenspace of the other, which is an invariant subspace because the
operators commute. -/
theorem eigenspace_inf_eigenspace
    (hAB : A ‚àò‚Çó B = B ‚àò‚Çó A) (Œ≥ : ùïú) :
    eigenspace A Œ± ‚äì eigenspace B Œ≥ = map (Submodule.subtype (eigenspace A Œ±))
      (eigenspace (B.restrict (eigenspace_invariant_of_commute hAB Œ±)) Œ≥) :=
  (eigenspace A Œ±).inf_genEigenspace _ _ (k := 1)

variable [FiniteDimensional ùïú E]

/-- If A and B are commuting symmetric operators on a finite dimensional inner product space
then the eigenspaces of the restriction of B to any eigenspace of A exhaust that eigenspace.-/
theorem iSup_eigenspace_inf_eigenspace (hB : B.IsSymmetric)
    (hAB : A ‚àò‚Çó B = B ‚àò‚Çó A):
    (‚®Ü Œ≥, eigenspace A Œ± ‚äì eigenspace B Œ≥) = eigenspace A Œ± := by
  conv_rhs => rw [‚Üê (eigenspace A Œ±).map_subtype_top]
  simp only [eigenspace_inf_eigenspace hAB, ‚Üê Submodule.map_iSup]
  congr 1
  rw [‚Üê Submodule.orthogonal_eq_bot_iff]
  exact orthogonalComplement_iSup_eigenspaces_eq_bot <|
    hB.restrict_invariant <| eigenspace_invariant_of_commute hAB Œ±

/-- If A and B are commuting symmetric operators acting on a finite dimensional inner product space,
then the simultaneous eigenspaces of A and B exhaust the space. -/
theorem iSup_iSup_eigenspace_inf_eigenspace_eq_top (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : A ‚àò‚Çó B = B ‚àò‚Çó A) :
    (‚®Ü Œ±, ‚®Ü Œ≥, eigenspace A Œ± ‚äì eigenspace B Œ≥) = ‚ä§ := by
  simpa [iSup_eigenspace_inf_eigenspace hB hAB] using
    Submodule.orthogonal_eq_bot_iff.mp <| hA.orthogonalComplement_iSup_eigenspaces_eq_bot

/-- Given a commuting pair of symmetric linear operators on a finite dimensional inner product
space, the space decomposes as an internal direct sum of simultaneous eigenspaces of these
operators. -/
theorem directSum_isInteral_of_commute (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : A ‚àò‚Çó B = B ‚àò‚Çó A) :
    DirectSum.IsInternal (fun (i : ùïú √ó ùïú) ‚Ü¶ (eigenspace A i.2 ‚äì eigenspace B i.1)):= by
  apply (orthogonalFamily_eigenspace_inf_eigenspace hA hB).isInternal_iff.mpr
  rw [Submodule.orthogonal_eq_bot_iff, iSup_prod, iSup_comm]
  exact iSup_iSup_eigenspace_inf_eigenspace_eq_top hA hB hAB

end Pair

end IsSymmetric

end LinearMap
