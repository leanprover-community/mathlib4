/-
Copyright (c) 2024 Jon Bannon. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jon Bannon, Jack Cheverton, Samyak Dhar Tuladhar
-/

import Mathlib.Analysis.InnerProductSpace.Spectrum
import Mathlib.LinearAlgebra.Eigenspace.Pi
import Mathlib.LinearAlgebra.Eigenspace.Semisimple
import Mathlib.Analysis.InnerProductSpace.Semisimple

/-! # Joint eigenspaces of commuting symmetric operators

This file collects various decomposition results for joint eigenspaces of commuting
symmetric operators on a finite-dimensional inner product space.

# Main Result

* `LinearMap.IsSymmetric.directSum_isInternal_of_commute` establishes that in finite dimensions
  if `{A B : E â†’â‚—[ğ•œ] E}`, then `IsSymmetric A`, `IsSymmetric B` and `Commute A B` imply that
  `E` decomposes as an internal direct sum of the pairwise orthogonal spaces
  `eigenspace B Î¼ âŠ“ eigenspace A Î½`
* `LinearMap.IsSymmetric.iSup_iInf_eigenspace_eq_top_of_commute` establishes that in finite
  dimensions, the indexed supremum of the joint eigenspaces of a commuting tuple of symmetric
  linear operators equals `âŠ¤`
* `LinearMap.IsSymmetric.directSum_isInternal_of_pairwise_commute` establishes the
  analogous result to `LinearMap.IsSymmetric.directSum_isInternal_of_commute` for commuting
  tuples of symmetric operators.

## TODO

Develop a `Diagonalization` structure for linear maps and / or matrices which consists of a basis,
and a proof obligation that the basis vectors are eigenvectors.

## Tags

symmetric operator, simultaneous eigenspaces, joint eigenspaces

-/

open Module.End

namespace LinearMap

namespace IsSymmetric

variable {ğ•œ E n m : Type*}

open Submodule

section RCLike

variable [RCLike ğ•œ] [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable {Î± : ğ•œ} {A B : E â†’â‚—[ğ•œ] E} {T : n â†’ Module.End ğ•œ E}

/-- The joint eigenspaces of a pair of symmetric operators form an
`OrthogonalFamily`. -/
theorem orthogonalFamily_eigenspace_inf_eigenspace (hA : A.IsSymmetric) (hB : B.IsSymmetric) :
    OrthogonalFamily ğ•œ (fun (i : ğ•œ Ã— ğ•œ) => (eigenspace A i.2 âŠ“ eigenspace B i.1 : Submodule ğ•œ E))
      fun i => (eigenspace A i.2 âŠ“ eigenspace B i.1).subtypeâ‚—áµ¢ :=
  OrthogonalFamily.of_pairwise fun i j hij v âŸ¨hv1 , hv2âŸ© â†¦ by
    obtain (hâ‚ | hâ‚‚) : i.1 â‰  j.1 âˆ¨ i.2 â‰  j.2 := by rwa [Ne.eq_def, Prod.ext_iff, not_and_or] at hij
    all_goals intro w âŸ¨hw1, hw2âŸ©
    Â· exact hB.orthogonalFamily_eigenspaces.pairwise hâ‚ hv2 w hw2
    Â· exact hA.orthogonalFamily_eigenspaces.pairwise hâ‚‚ hv1 w hw1

/-- The joint eigenspaces of a family of symmetric operators form an
`OrthogonalFamily`. -/
theorem orthogonalFamily_iInf_eigenspaces (hT : âˆ€ i, (T i).IsSymmetric) :
    OrthogonalFamily ğ•œ (fun Î³ : n â†’ ğ•œ â†¦ (â¨… j, eigenspace (T j) (Î³ j) : Submodule ğ•œ E))
      fun Î³ : n â†’ ğ•œ â†¦ (â¨… j, eigenspace (T j) (Î³ j)).subtypeâ‚—áµ¢ := by
  intro f g hfg Ef Eg
  obtain âŸ¨a , haâŸ© := Function.ne_iff.mp hfg
  have H := orthogonalFamily_eigenspaces (hT a) ha
  simp only [Submodule.coe_subtypeâ‚—áµ¢, Submodule.coe_subtype, Subtype.forall] at H
  apply H
  Â· exact (Submodule.mem_iInf <| fun _ â†¦ eigenspace (T _) (f _)).mp Ef.2 _
  Â· exact (Submodule.mem_iInf <| fun _ â†¦ eigenspace (T _) (g _)).mp Eg.2 _

variable [FiniteDimensional ğ•œ E]

open IsFinitelySemisimple

/-- If A and B are commuting symmetric operators on a finite dimensional inner product space
then the eigenspaces of the restriction of B to any eigenspace of A exhaust that eigenspace. -/
theorem iSup_eigenspace_inf_eigenspace_of_commute (hB : B.IsSymmetric) (hAB : Commute A B) :
    (â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = eigenspace A Î± := by
  conv_rhs => rw [â† (eigenspace A Î±).map_subtype_top]
  simp only [â† genEigenspace_eq_eigenspace (f := B), â† Submodule.map_iSup,
    (eigenspace A Î±).inf_genEigenspace _ (mapsTo_genEigenspace_of_comm hAB Î± 1)]
  congr 1
  simpa only [genEigenspace_eq_eigenspace, Submodule.orthogonal_eq_bot_iff]
    using orthogonalComplement_iSup_eigenspaces_eq_bot <|
      hB.restrict_invariant <| mapsTo_genEigenspace_of_comm hAB Î± 1

/-- If A and B are commuting symmetric operators acting on a finite dimensional inner product space,
then the simultaneous eigenspaces of A and B exhaust the space. -/
theorem iSup_iSup_eigenspace_inf_eigenspace_eq_top_of_commute (hA : A.IsSymmetric)
    (hB : B.IsSymmetric) (hAB : Commute A B) :
    (â¨† Î±, â¨† Î³, eigenspace A Î± âŠ“ eigenspace B Î³) = âŠ¤ := by
  simpa [iSup_eigenspace_inf_eigenspace_of_commute hB hAB] using
    Submodule.orthogonal_eq_bot_iff.mp <| hA.orthogonalComplement_iSup_eigenspaces_eq_bot

/-- Given a commuting pair of symmetric linear operators on a finite dimensional inner product
space, the space decomposes as an internal direct sum of simultaneous eigenspaces of these
operators. -/
theorem directSum_isInternal_of_commute (hA : A.IsSymmetric) (hB : B.IsSymmetric)
    (hAB : Commute A B) :
    DirectSum.IsInternal (fun (i : ğ•œ Ã— ğ•œ) â†¦ (eigenspace A i.2 âŠ“ eigenspace B i.1)):= by
  apply (orthogonalFamily_eigenspace_inf_eigenspace hA hB).isInternal_iff.mpr
  rw [Submodule.orthogonal_eq_bot_iff, iSup_prod, iSup_comm]
  exact iSup_iSup_eigenspace_inf_eigenspace_eq_top_of_commute hA hB hAB

open scoped Function -- required for scoped `on` notation

/-- A commuting family of symmetric linear maps on a finite dimensional inner
product space is simultaneously diagonalizable. -/
theorem iSup_iInf_eq_top_of_commute {Î¹ : Type*} {T : Î¹ â†’ E â†’â‚—[ğ•œ] E}
    (hT : âˆ€ i, (T i).IsSymmetric) (h : Pairwise (Commute on T)):
    â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i, eigenspace (T i) (Ï‡ i) = âŠ¤ :=
  calc
  _ = â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i, maxGenEigenspace (T i) (Ï‡ i) :=
    congr(â¨† Ï‡ : Î¹ â†’ ğ•œ, â¨… i,
      $(maxGenEigenspace_eq_eigenspace (isFinitelySemisimple <| hT _) (Ï‡ _))).symm
  _ = âŠ¤ :=
    iSup_iInf_maxGenEigenspace_eq_top_of_iSup_maxGenEigenspace_eq_top_of_commute T h fun _ â†¦ by
    rw [â† orthogonal_eq_bot_iff,
      congr(â¨† Î¼, $(maxGenEigenspace_eq_eigenspace (isFinitelySemisimple <| hT _) Î¼)),
      (hT _).orthogonalComplement_iSup_eigenspaces_eq_bot]

/-- In finite dimensions, given a commuting family of symmetric linear operators, the inner
product space on which they act decomposes as an internal direct sum of joint eigenspaces. -/
theorem LinearMap.IsSymmetric.directSum_isInternal_of_pairwise_commute [DecidableEq (n â†’ ğ•œ)]
    (hT : âˆ€ i, (T i).IsSymmetric) (hC : Pairwise (Commute on T)) :
    DirectSum.IsInternal (fun Î± : n â†’ ğ•œ â†¦ â¨… j, eigenspace (T j) (Î± j)) := by
  rw [OrthogonalFamily.isInternal_iff]
  Â· rw [iSup_iInf_eq_top_of_commute hT hC, top_orthogonal_eq_bot]
  Â· exact orthogonalFamily_iInf_eigenspaces hT

end RCLike

end IsSymmetric

end LinearMap
