import Mathlib.Analysis.InnerProductSpace.Completion
import Mathlib.Analysis.InnerProductSpace.Positive
import Mathlib.Analysis.InnerProductSpace.ProdL2
import Mathlib.Analysis.InnerProductSpace.TensorProduct


open scoped ComplexOrder
open ContinuousLinearMap

noncomputable section

--Move to better file
variable (ğ•œ : Type*) {X V : Type*} [RCLike ğ•œ] [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]

open InnerProductSpace in
theorem denseRange_ext_inner_zero {v : V} {X : Type*} {f : X â†’ V} (hd : DenseRange f)
    (h : âˆ€ x, âŸªf x, vâŸ«_ğ•œ = 0) : v = 0 := by
  rw [â† @inner_self_eq_zero ğ•œ, â† norm_eq_zero]
  refine le_antisymm (le_of_forall_pos_lt_add fun Îµ hÎµ â†¦ ?_) <| by simp
  by_cases h1 : â€–vâ€– = 0
  Â· simp [h1, hÎµ]
  have hpos : 0 < â€–vâ€– := lt_of_le_of_ne' (by simp) h1
  obtain âŸ¨c,hcâŸ© := hd.exists_dist_lt v <| div_pos hÎµ <| lt_of_le_of_ne' (by simp) h1
  rw [dist_eq_norm v (f c)] at hc
  calc
    _ = â€–âŸªv - f c + f c, vâŸ«_ğ•œâ€– := by simp
    _ = â€–âŸªv - f c, vâŸ«_ğ•œ + âŸªf c, vâŸ«_ğ•œâ€– := by simp only [inner_add_left]
    _ â‰¤ â€–v - f câ€– * â€–vâ€– := by simp [h, norm_inner_le_norm]
    _ < Îµ / â€–vâ€– * â€–vâ€– := by simp [hpos, hc]
    _ = _ := by simp[h1]

/--
Class of vector valued Reproducing Kernel Hilbert Spaces.
-/
class RKHS (ğ•œ X V : outParam Type*) (H : Type*) [RCLike ğ•œ]
    [NormedAddCommGroup V] [InnerProductSpace ğ•œ V]
    [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] where
  coeCLM (ğ•œ) : H â†’L[ğ•œ] X â†’ V
  coeCLM_injective : Function.Injective (coeCLM : H â†’ X â†’ V)

namespace RKHS

open InnerProductSpace
open Submodule

variable {ğ•œ : outParam Type*} [RCLike ğ•œ] --â„ or â„‚
variable {X : outParam Type*} --Domain
variable {V : outParam Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] --Co-domain
variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace ğ•œ H] --Our space of functions
variable [RKHS ğ•œ X V H]
local notation :90 "â€ " => starRingEnd ğ•œ

instance instFunLiketoFun : FunLike H X V where
  coe := fun f â†¦ coeCLM ğ•œ f
  coe_injective' := coeCLM_injective

@[ext]
lemma ext {f g : H} (h : âˆ€ x, f x = g x) : f = g := DFunLike.ext _ _ h

@[simp]
lemma coeCLM_apply (f : H) : coeCLM ğ•œ f = f := rfl

@[simp]
lemma coe_zero : â‡‘(0 : H) = 0 := (coeCLM ğ•œ).map_zero ..

@[simp]
lemma coe_add (f g : H) : â‡‘(f + g) = f + g := (coeCLM ğ•œ).map_add ..

@[simp]
lemma coe_sub (f g : H) : â‡‘(f - g) = f - g := (coeCLM ğ•œ).map_sub (Mâ‚‚ := X â†’ V) ..

@[simp]
lemma coe_neg (f : H) : â‡‘(-f) = -f := (coeCLM ğ•œ).map_neg (Mâ‚‚ := X â†’ V) ..

@[simp]
lemma coe_smul (f : H) (c : ğ•œ) : â‡‘(c â€¢ f) = c â€¢ f := (coeCLM ğ•œ).map_smul ..

@[simp]
lemma continuous_eval (x : X) : Continuous (fun (f : H) â†¦ f x) := by
  simp_rw [â† coeCLM_apply]
  fun_prop

variable [CompleteSpace H] [CompleteSpace V]

variable (H) in
def kerFun (x : X) : V â†’L[ğ•œ] H := ((ContinuousLinearMap.proj x) âˆ˜L (coeCLM ğ•œ)).adjoint

--simp?
lemma coeCLM_to_kerFun' :
    coeCLM ğ•œ = ContinuousLinearMap.pi (fun x â†¦ (kerFun H x).adjoint) := by simp [kerFun]

lemma coeCLM_to_kerFun (x : X) (f : H) : (coeCLM ğ•œ) f x = (kerFun H x).adjoint f := by simp [kerFun]

variable (H) in
def kernel : Matrix X X (V â†’L[ğ•œ] V) := .of fun x y â†¦ (kerFun H x).adjoint âˆ˜L kerFun H y

lemma kerFun_apply (y : X) (v : V) : kerFun H y v = fun x â†¦ kernel H x y v := by
  simp [kernel, kerFun]

lemma kernel_kerFun (x y : X) : kernel H x y = (kerFun H x).adjoint âˆ˜L (kerFun H y) := by
  simp [kerFun, kernel]

@[simp]
lemma kerFun_inner (x : X) (v : V) (f : H) : âŸªkerFun H x v, fâŸ«_ğ•œ = âŸªv, f xâŸ«_ğ•œ := by
  simp [kerFun, â† adjoint_inner_right]

@[simp]
lemma inner_kerFun (x : X) (v : V) (f : H) : âŸªf, kerFun H x vâŸ«_ğ•œ = âŸªf x, vâŸ«_ğ•œ := by
  simp [kerFun, â† adjoint_inner_left]

lemma kernel_inner (x y : X) (v w : V) :
    âŸª(kernel H x y) v, wâŸ«_ğ•œ = âŸªkerFun H y v, kerFun H x wâŸ«_ğ•œ := by
  simp [â† adjoint_inner_left, kernel_kerFun]

theorem kerFun_dense : topologicalClosure (span ğ•œ {kerFun H x v | (x) (v)}) = âŠ¤ := by
  refine (orthogonal_eq_bot_iff.mp ((Submodule.eq_bot_iff _).mpr fun f fin â†¦ (DFunLike.ext f 0) ?_))
  refine fun x â†¦ ext_inner_left ğ•œ (fun v â†¦ ?_)
  simp only [â† kerFun_inner, coe_zero, Pi.zero_apply, inner_zero_right]
  refine inner_right_of_mem_orthogonal (subset_closure ?_) fin
  simp [mem_span_of_mem]

variable (H) in
lemma isHermitian_kernel : (kernel H).IsHermitian := by
  ext _ _ _
  refine ext_inner_right ğ•œ fun w â†¦ ?_
  simp only [Matrix.conjTranspose_apply, star, adjoint_inner_left,
    â† inner_conj_symm _ ((kernel H _ _) _), kernel_inner, inner_conj_symm]

theorem kernelPossemiDef : (kernel H).PosSemidef := by
  refine âŸ¨isHermitian_kernel H, fun s â†¦ (ContinuousLinearMap.isPositive_iff' _).mpr âŸ¨?_,fun v â†¦ ?_âŸ©âŸ©
  Â· rw [IsSelfAdjoint, sub_zero, star_finsuppSum, Finsupp.sum_comm]
    simp [â† mul_assoc, Matrix.IsHermitian.apply (isHermitian_kernel H)]
  Â· simp [Finsupp.sum_apply'', Finsupp.sum_inner, star, adjoint_inner_left,
      kernel_inner, -inner_kerFun, -kerFun_inner]
    simp only [â† Finsupp.sum_inner, â† Finsupp.inner_sum,
      inner_self_eq_norm_sq_to_K, RCLike.ofReal_nonneg, norm_nonneg, pow_succ_nonneg]

end RKHS

section

variable {X : Type*}
variable {ğ•œ : Type*} [RCLike ğ•œ] --â„ or â„‚
variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace ğ•œ V] [CompleteSpace V]
local notation :90 "â€ " => starRingEnd ğ•œ

open InnerProductSpace
open Submodule

variable {K : Matrix X X (V â†’L[ğ•œ] V)}

@[simp]
lemma Hermitian_IsSelfAdjoint_Finsupp_sum (h : K.IsHermitian) (f : X â†’â‚€ V â†’L[ğ•œ] V) :
    IsSelfAdjoint (f.sum fun i xi â†¦ f.sum fun j xj â†¦ star xi * K i j * xj) := by
  simp only [mul_assoc, isSelfAdjoint_iff, star_finsuppSum, Pi.star_apply, star_mul, h.apply,
    star_star]
  rw [Finsupp.sum_comm]

theorem PosSemidef_iff : K.PosSemidef â†” K.IsHermitian âˆ§
    âˆ€ (f : X Ã— V â†’â‚€ ğ•œ), 0 â‰¤ RCLike.re
    (f.sum fun xv z â†¦ f.sum fun xv' w â†¦ (â€ ) z * w * âŸª(K xv'.1 xv.1) xv.2, xv'.2âŸ«_ğ•œ) := by
  have (T T' K: V â†’L[ğ•œ] V) (v : V) :
      âŸª(adjoint T) (K (T' v)), vâŸ«_ğ•œ = âŸªK (((apply ğ•œ V) v) T'), ((apply ğ•œ V) v) TâŸ«_ğ•œ := by
    simp [adjoint_inner_left]
  simp +contextual only [Matrix.PosSemidef, nonneg_iff_isPositive, isPositive_def',
    reApplyInnerSelf_apply, and_congr_right_iff, Hermitian_IsSelfAdjoint_Finsupp_sum, true_and]
  simp only [star_eq_adjoint, zero_apply, add_apply, implies_true, Finsupp.sum_apply'', coe_mul,
    Function.comp_apply, Finsupp.sum_inner, this]
  congr!
  refine (subsingleton_or_nontrivial V).elim (fun h â†¦ ?_) (fun _ â†¦ ?_) --nontriviality?
  Â· have : âˆ€ v : V, v = 0 := fun v â†¦ Subsingleton.elim v 0
    simp [this]
  obtain âŸ¨v, hvâŸ© := exists_ne (0 : V)
  letI p : Prop := âˆ€ (vv : X â†’â‚€ V),
      0 â‰¤ RCLike.re (vv.sum fun x w â†¦ (vv.sum fun x' w' â†¦ âŸª(K x' x) w, w'âŸ«_ğ•œ))
  have {a b : Prop} (c : Prop) (h : List.TFAE [a, b, c]) : a â†” b := h.out 0 1
  refine this p ?_
  tfae_have 1 â†’ 2 := fun h â†¦ fun ff â†¦ by
    rw [Finsupp.sum_comm]
    convert h (ff.sum fun xv z â†¦ .single xv.1
      ((z/â€–vâ€– ^ 2) â€¢ (innerSL ğ•œ v).smulRight xv.2)) v
    simp [Finsupp.sum_sum_index, inner_add_right, inner_add_left, â† smul_assoc, hv]
    simp [inner_smul_left, inner_smul_right, â† mul_assoc, mul_comm]
  tfae_have 2 â†’ 3 := fun h â†¦ fun vv â†¦ by
    simpa [add_mul, Finsupp.sum_sum_index] using (h (vv.sum fun x v â†¦ .single âŸ¨x, vâŸ© 1))
  tfae_have 3 â†’ 1 := fun h â†¦ fun ff v â†¦ by
    rw [Finsupp.sum_comm]
    simpa [Finsupp.sum_sum_index, inner_add_right, inner_add_left] using
      h (ff.sum fun x T â†¦ .single x (T v))
  tfae_finish

set_option linter.unusedVariables false in
abbrev Hâ‚€ (hK : K.PosSemidef) := (X Ã— V â†’â‚€ ğ•œ)

variable (hK : K.PosSemidef)

instance instkernelToPreInnerCore :
    PreInnerProductSpace.Core ğ•œ (Hâ‚€ hK) where
  inner := fun f g â†¦ f.sum fun âŸ¨y, uâŸ© z â†¦ g.sum fun âŸ¨x, vâŸ© w â†¦ star z * w * âŸª(K x y) u, vâŸ«_ğ•œ
  conj_inner_symm := fun f g â†¦ by
    rw [Finsupp.sum_comm]
    simp only [map_finsuppSum]
    congr! 6
    rw [â† hK.isHermitian.apply]
    simp [star, adjoint_inner_right, mul_comm]
  add_left := fun _ _ _ â†¦ by
    rw [Finsupp.sum_add_index'] <;> simp [â† Finsupp.sum_add, add_mul]
  smul_left := fun _ _ _ â†¦ by
    rw [Finsupp.sum_smul_index] <;> simp [Finsupp.mul_sum, â† mul_assoc]
  re_inner_nonneg := ((PosSemidef_iff).mp hK).2

instance instkernelToSeminormedAddCommGroup : SeminormedAddCommGroup (Hâ‚€ hK) :=
  InnerProductSpace.Core.toSeminormedAddCommGroup (ğ•œ := ğ•œ)

instance kernelToPreInnerProductSpace : InnerProductSpace ğ•œ (Hâ‚€ hK) :=
  InnerProductSpace.ofCore (instkernelToPreInnerCore hK)

lemma Hâ‚€inner_def (f g : Hâ‚€ hK) :
    âŸªf, gâŸ«_ğ•œ = f.sum fun âŸ¨y, uâŸ© z â†¦ g.sum fun âŸ¨x, vâŸ© w â†¦ star z * w * âŸª(K x y) u, vâŸ«_ğ•œ := rfl

abbrev Hâ‚ := SeparationQuotient (Hâ‚€ hK)

abbrev kernelToRKHS := UniformSpace.Completion (Hâ‚ hK)

abbrev pre_kerFun' (x : X) : V â†’â‚—[ğ•œ] (Hâ‚ hK) where
  toFun : V â†’ Hâ‚ hK := fun v â†¦ SeparationQuotient.mk (Finsupp.single âŸ¨x,vâŸ© 1)
  map_add' := fun v w â†¦ by
    refine (ext_iff_inner_left ğ•œ).mpr <|
      (Function.Surjective.forall SeparationQuotient.surjective_mk).mpr fun ff â†¦ ?_
    simp [â† SeparationQuotient.mk_add, Hâ‚€inner_def hK, Finsupp.sum_add_index', mul_assoc,
      â† mul_add, add_mul, inner_add_right]
  map_smul' := fun z v â†¦ by
    refine (ext_iff_inner_left ğ•œ).mpr <|
       (Function.Surjective.forall SeparationQuotient.surjective_mk).mpr fun ff â†¦ ?_
    simp [â† SeparationQuotient.mk_smul, Hâ‚€inner_def hK, inner_smul_right, mul_assoc]

abbrev pre_kerFun (x : X) : V â†’L[ğ•œ] (Hâ‚ hK) := (pre_kerFun' hK x).mkContinuous âˆšâ€–K x xâ€– <| by
  refine fun v â†¦ (sq_le_sqâ‚€ (by simp) (by simp [mul_nonneg])).mp ?_
  simp only [LinearMap.coe_mk, AddHom.coe_mk, SeparationQuotient.norm_mk,
    â† inner_self_eq_norm_sq (ğ•œ := ğ•œ), inner_self_re_eq_norm]
  simp only [Hâ‚€inner_def hK, RCLike.star_def, mul_zero, zero_mul, Finsupp.sum_single_index,
    mul_one, map_zero, map_one, one_mul]
  calc
    _ â‰¤ â€–K x x vâ€– * â€–vâ€– := by simp [norm_inner_le_norm]
    _ â‰¤ â€–K x xâ€– * â€–vâ€– * â€–vâ€– := by simp [mul_le_mul_of_nonneg_right, le_opNorm]
    _ â‰¤ _ := by simp [mul_pow, mul_assoc, â† sq]

def kerFun (x : X) : V â†’L[ğ•œ] kernelToRKHS hK :=
  UniformSpace.Completion.toComplL âˆ˜L pre_kerFun hK x

instance instKernelToRKHS : RKHS ğ•œ X V (kernelToRKHS hK) where
  coeCLM := ContinuousLinearMap.pi (fun x â†¦ (kerFun hK x).adjoint)
  coeCLM_injective := by
    refine (injective_iff_map_eq_zero _).mpr fun f h â†¦ ?_
    refine denseRange_ext_inner_zero ğ•œ UniformSpace.Completion.denseRange_coe ?_
    refine (Function.Surjective.forall SeparationQuotient.surjective_mk).mpr
      fun ff â†¦ ?_
    have : ff = ff.sum fun xv z â†¦ .single xv z := by simp
    rw [this, â† SeparationQuotient.mkCLM_apply ğ•œ, â† UniformSpace.Completion.coe_toComplL (ğ•œ := ğ•œ)]
    simp only [map_finsuppSum, Finsupp.sum_inner]
    have (i : X Ã— V) (a : ğ•œ): âŸªUniformSpace.Completion.toComplL (ğ•œ := ğ•œ)
        ((SeparationQuotient.mkCLM ğ•œ (Hâ‚€ hK)) (Finsupp.single i a)), fâŸ«_ğ•œ = 0 := by
      have := (ext_iff_inner_left ğ•œ).mp (congrFun h i.1) i.2
      simp only [kerFun, coe_pi', adjoint_inner_right, coe_comp',
        UniformSpace.Completion.coe_toComplL, Function.comp_apply, LinearMap.mkContinuous_apply,
        LinearMap.coe_mk, AddHom.coe_mk, Prod.mk.eta, Pi.zero_apply, inner_zero_right] at this
      rw [â† mul_zero ((â€ ) a), â† this, â† inner_smul_left]
      refine (ext_iff_inner_right ğ•œ).mp ?_ f
      simp [â† SeparationQuotient.mkCLM_apply ğ•œ, â† UniformSpace.Completion.coe_toComplL (ğ•œ := ğ•œ),
        â† map_smul, -SeparationQuotient.mkCLM_apply, -UniformSpace.Completion.coe_toComplL]
    simp only [this]
    simp

theorem Kernel_eq_Kernel : K = RKHS.kernel (kernelToRKHS hK) := by
  ext x y v
  refine ext_inner_right ğ•œ fun w â†¦ ?_
  simp [RKHS.kernel, adjoint_inner_left, -RKHS.inner_kerFun, -RKHS.kerFun_inner,
    RKHS.kerFun, RKHS.coeCLM, kerFun, Hâ‚€inner_def hK]

lemma kerFun_apply (y : X) (v : V) : (kerFun hK y) v = fun x â†¦ (K x y) v := by
  rw [show â‡‘((kerFun hK y) v) = (RKHS.coeCLM ğ•œ) ((kerFun hK y) v) from rfl]
  simp only [RKHS.coeCLM]
  ext x
  refine ext_inner_right ğ•œ fun w â†¦ ?_
  simp [adjoint_inner_left, kerFun, Hâ‚€inner_def hK]

theorem kerFun_eq_KerFun : RKHS.kerFun (kernelToRKHS hK) = kerFun hK := by
  ext y v x
  simp [kerFun_apply, RKHS.kerFun_apply, â† Kernel_eq_Kernel]

end
