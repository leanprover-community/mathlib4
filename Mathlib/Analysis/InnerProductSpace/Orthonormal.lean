/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.InnerProductSpace.LinearMap
import Mathlib.LinearAlgebra.FiniteDimensional.Lemmas
import Mathlib.RingTheory.LocalRing.Basic

/-!
# Orthonormal sets

This file defines orthonormal sets in inner product spaces.

## Main results

- We define `Orthonormal`, a predicate on a function `v : Î¹ â†’ E`, and prove the existence of a
  maximal orthonormal set, `exists_maximal_orthonormal`.
- Bessel's inequality, `Orthonormal.tsum_inner_products_le`, states that given an orthonormal set
  `v` and a vector `x`, the sum of the norm-squares of the inner products `âŸªv i, xâŸ«` is no more
  than the norm-square of `x`.

For the existence of orthonormal bases, Hilbert bases, etc., see the file
`Analysis.InnerProductSpace.projection`.
-/

noncomputable section

open RCLike Real Filter Module Topology ComplexConjugate Finsupp

open LinearMap (BilinForm)

variable {ğ•œ E F : Type*} [RCLike ğ•œ]

section OrthonormalSets_Seminormed

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [SeminormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable {Î¹ : Type*} (ğ•œ)

/-- An orthonormal set of vectors in an `InnerProductSpace` -/
def Orthonormal (v : Î¹ â†’ E) : Prop :=
  (âˆ€ i, â€–v iâ€– = 1) âˆ§ Pairwise fun i j => âŸªv i, v jâŸ« = 0

variable {ğ•œ}

@[simp]
lemma Orthonormal.of_isEmpty [IsEmpty Î¹] (v : Î¹ â†’ E) : Orthonormal ğ•œ v :=
  âŸ¨IsEmpty.elim â€¹_â€º, Subsingleton.pairwiseâŸ©

@[simp]
lemma orthonormal_vecCons_iff {n : â„•} {v : E} {vs : Fin n â†’ E} :
    Orthonormal ğ•œ (Matrix.vecCons v vs) â†” â€–vâ€– = 1 âˆ§ (âˆ€ i, âŸªv, vs iâŸ« = 0) âˆ§ Orthonormal ğ•œ vs := by
  simp_rw [Orthonormal, pairwise_fin_succ_iff_of_isSymm, Fin.forall_fin_succ]
  tauto

lemma Orthonormal.norm_eq_one {v : Î¹ â†’ E} (h : Orthonormal ğ•œ v) (i : Î¹) :
    â€–v iâ€– = 1 := h.1 i

lemma Orthonormal.nnnorm_eq_one {v : Î¹ â†’ E} (h : Orthonormal ğ•œ v) (i : Î¹) :
    â€–v iâ€–â‚Š = 1 := by
  suffices (â€–v iâ€–â‚Š : â„) = 1 by norm_cast at this
  simp [h.norm_eq_one]

lemma Orthonormal.enorm_eq_one {v : Î¹ â†’ E} (h : Orthonormal ğ•œ v) (i : Î¹) :
    â€–v iâ€–â‚‘ = 1 := by rw [â† ofReal_norm]; simp [h.norm_eq_one]

lemma Orthonormal.inner_eq_zero {v : Î¹ â†’ E} {i j : Î¹} (h : Orthonormal ğ•œ v) (hij : i â‰  j) :
    âŸªv i, v jâŸ« = 0 := h.2 hij

/-- `if ... then ... else` characterization of an indexed set of vectors being orthonormal.  (Inner
product equals Kronecker delta.) -/
theorem orthonormal_iff_ite [DecidableEq Î¹] {v : Î¹ â†’ E} :
    Orthonormal ğ•œ v â†” âˆ€ i j, âŸªv i, v jâŸ« = if i = j then (1 : ğ•œ) else (0 : ğ•œ) := by
  constructor
  Â· intro hv i j
    split_ifs with h
    Â· simp [h, inner_self_eq_norm_sq_to_K, hv.norm_eq_one]
    Â· exact hv.inner_eq_zero h
  Â· intro h
    constructor
    Â· intro i
      have h' : â€–v iâ€– ^ 2 = 1 ^ 2 := by simp [@norm_sq_eq_re_inner ğ•œ, h i i]
      have hâ‚ : 0 â‰¤ â€–v iâ€– := norm_nonneg _
      have hâ‚‚ : (0 : â„) â‰¤ 1 := zero_le_one
      rwa [sq_eq_sqâ‚€ hâ‚ hâ‚‚] at h'
    Â· intro i j hij
      simpa [hij] using h i j

/-- `if ... then ... else` characterization of a set of vectors being orthonormal.  (Inner product
equals Kronecker delta.) -/
theorem orthonormal_subtype_iff_ite [DecidableEq E] {s : Set E} :
    Orthonormal ğ•œ (Subtype.val : s â†’ E) â†” âˆ€ v âˆˆ s, âˆ€ w âˆˆ s, âŸªv, wâŸ« = if v = w then 1 else 0 := by
  rw [orthonormal_iff_ite]
  constructor
  Â· intro h v hv w hw
    convert h âŸ¨v, hvâŸ© âŸ¨w, hwâŸ© using 1
    simp
  Â· rintro h âŸ¨v, hvâŸ© âŸ¨w, hwâŸ©
    convert h v hv w hw using 1
    simp

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_right_finsupp {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’â‚€ ğ•œ) (i : Î¹) :
    âŸªv i, linearCombination ğ•œ v lâŸ« = l i := by
  classical
  simpa [linearCombination_apply, Finsupp.inner_sum, orthonormal_iff_ite.mp hv] using Eq.symm

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_right_sum {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) {s : Finset Î¹}
    {i : Î¹} (hi : i âˆˆ s) : âŸªv i, âˆ‘ i âˆˆ s, l i â€¢ v iâŸ« = l i := by
  classical
  simp [inner_sum, inner_smul_right, orthonormal_iff_ite.mp hv, hi]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_right_fintype [Fintype Î¹] {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ)
    (i : Î¹) : âŸªv i, âˆ‘ i : Î¹, l i â€¢ v iâŸ« = l i :=
  hv.inner_right_sum l (Finset.mem_univ _)

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_left_finsupp {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’â‚€ ğ•œ) (i : Î¹) :
    âŸªlinearCombination ğ•œ v l, v iâŸ« = conj (l i) := by rw [â† inner_conj_symm, hv.inner_right_finsupp]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_left_sum {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ) {s : Finset Î¹}
    {i : Î¹} (hi : i âˆˆ s) : âŸªâˆ‘ i âˆˆ s, l i â€¢ v i, v iâŸ« = conj (l i) := by
  classical
  simp only [sum_inner, inner_smul_left, orthonormal_iff_ite.mp hv, hi, mul_boole,
    Finset.sum_ite_eq', if_true]

/-- The inner product of a linear combination of a set of orthonormal vectors with one of those
vectors picks out the coefficient of that vector. -/
theorem Orthonormal.inner_left_fintype [Fintype Î¹] {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (l : Î¹ â†’ ğ•œ)
    (i : Î¹) : âŸªâˆ‘ i : Î¹, l i â€¢ v i, v iâŸ« = conj (l i) :=
  hv.inner_left_sum l (Finset.mem_univ _)

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum over the first `Finsupp`. -/
theorem Orthonormal.inner_finsupp_eq_sum_left {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’â‚€ ğ•œ) :
    âŸªlinearCombination ğ•œ v lâ‚, linearCombination ğ•œ v lâ‚‚âŸ« = lâ‚.sum fun i y => conj y * lâ‚‚ i := by
  simp only [lâ‚.linearCombination_apply _, Finsupp.sum_inner, hv.inner_right_finsupp, smul_eq_mul]

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum over the second `Finsupp`. -/
theorem Orthonormal.inner_finsupp_eq_sum_right {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’â‚€ ğ•œ) :
    âŸªlinearCombination ğ•œ v lâ‚, linearCombination ğ•œ v lâ‚‚âŸ« = lâ‚‚.sum fun i y => conj (lâ‚ i) * y := by
  simp only [lâ‚‚.linearCombination_apply _, Finsupp.inner_sum, hv.inner_left_finsupp, mul_comm,
             smul_eq_mul]

/-- The inner product of two linear combinations of a set of orthonormal vectors, expressed as
a sum. -/
protected theorem Orthonormal.inner_sum {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (lâ‚ lâ‚‚ : Î¹ â†’ ğ•œ)
    (s : Finset Î¹) : âŸªâˆ‘ i âˆˆ s, lâ‚ i â€¢ v i, âˆ‘ i âˆˆ s, lâ‚‚ i â€¢ v iâŸ« = âˆ‘ i âˆˆ s, conj (lâ‚ i) * lâ‚‚ i := by
  simp_rw [sum_inner, inner_smul_left]
  refine Finset.sum_congr rfl fun i hi => ?_
  rw [hv.inner_right_sum lâ‚‚ hi]

/--
The double sum of weighted inner products of pairs of vectors from an orthonormal sequence is the
sum of the weights.
-/
theorem Orthonormal.inner_left_right_finset {s : Finset Î¹} {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v)
    {a : Î¹ â†’ Î¹ â†’ ğ•œ} : (âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s, a i j â€¢ âŸªv j, v iâŸ«) = âˆ‘ k âˆˆ s, a k k := by
  classical
  simp [orthonormal_iff_ite.mp hv]

/-- An orthonormal set is linearly independent. -/
theorem Orthonormal.linearIndependent {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) :
    LinearIndependent ğ•œ v := by
  rw [linearIndependent_iff]
  intro l hl
  ext i
  have key : âŸªv i, Finsupp.linearCombination ğ•œ v lâŸ« = âŸªv i, 0âŸ« := by rw [hl]
  simpa only [hv.inner_right_finsupp, inner_zero_right] using key

/-- A subfamily of an orthonormal family (i.e., a composition with an injective map) is an
orthonormal family. -/
theorem Orthonormal.comp {Î¹' : Type*} {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (f : Î¹' â†’ Î¹)
    (hf : Function.Injective f) : Orthonormal ğ•œ (v âˆ˜ f) := by
  classical
  rw [orthonormal_iff_ite] at hv âŠ¢
  intro i j
  convert hv (f i) (f j) using 1
  simp [hf.eq_iff]

/-- An injective family `v : Î¹ â†’ E` is orthonormal if and only if `Subtype.val : (range v) â†’ E` is
orthonormal. -/
theorem orthonormal_subtype_range {v : Î¹ â†’ E} (hv : Function.Injective v) :
    Orthonormal ğ•œ (Subtype.val : Set.range v â†’ E) â†” Orthonormal ğ•œ v := by
  let f : Î¹ â‰ƒ Set.range v := Equiv.ofInjective v hv
  refine âŸ¨fun h => h.comp f f.injective, fun h => ?_âŸ©
  rw [â† Equiv.self_comp_ofInjective_symm hv]
  exact h.comp f.symm f.symm.injective

/-- If `v : Î¹ â†’ E` is an orthonormal family, then `Subtype.val : (range v) â†’ E` is an orthonormal
family. -/
theorem Orthonormal.toSubtypeRange {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) :
    Orthonormal ğ•œ (Subtype.val : Set.range v â†’ E) :=
  (orthonormal_subtype_range hv.linearIndependent.injective).2 hv

/-- A linear combination of some subset of an orthonormal set is orthogonal to other members of the
set. -/
theorem Orthonormal.inner_finsupp_eq_zero {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) {s : Set Î¹} {i : Î¹}
    (hi : i âˆ‰ s) {l : Î¹ â†’â‚€ ğ•œ} (hl : l âˆˆ Finsupp.supported ğ•œ ğ•œ s) :
    âŸªFinsupp.linearCombination ğ•œ v l, v iâŸ« = 0 := by
  rw [Finsupp.mem_supported'] at hl
  simp only [hv.inner_left_finsupp, hl i hi, map_zero]

/-- Given an orthonormal family, a second family of vectors is orthonormal if every vector equals
the corresponding vector in the original family or its negation. -/
theorem Orthonormal.orthonormal_of_forall_eq_or_eq_neg {v w : Î¹ â†’ E} (hv : Orthonormal ğ•œ v)
    (hw : âˆ€ i, w i = v i âˆ¨ w i = -v i) : Orthonormal ğ•œ w := by
  classical
  rw [orthonormal_iff_ite] at *
  intro i j
  rcases hw i with hi | hi <;> rcases hw j with hj | hj <;>
    replace hv := hv i j <;> split_ifs at hv âŠ¢ with h <;>
    simpa only [hi, hj, h, inner_neg_right, inner_neg_left, neg_neg, eq_self_iff_true,
      neg_eq_zero] using hv

/- The material that follows, culminating in the existence of a maximal orthonormal subset, is
adapted from the corresponding development of the theory of linearly independents sets.  See
`exists_linearIndependent` in particular. -/
variable (ğ•œ E)

theorem orthonormal_empty : Orthonormal ğ•œ (fun x => x : (âˆ… : Set E) â†’ E) := by
  classical
  simp

variable {ğ•œ E}

theorem orthonormal_iUnion_of_directed {Î· : Type*} {s : Î· â†’ Set E} (hs : Directed (Â· âŠ† Â·) s)
    (h : âˆ€ i, Orthonormal ğ•œ (fun x => x : s i â†’ E)) :
    Orthonormal ğ•œ (fun x => x : (â‹ƒ i, s i) â†’ E) := by
  classical
  rw [orthonormal_subtype_iff_ite]
  rintro x âŸ¨_, âŸ¨i, rflâŸ©, hxiâŸ© y âŸ¨_, âŸ¨j, rflâŸ©, hyjâŸ©
  obtain âŸ¨k, hik, hjkâŸ© := hs i j
  have h_orth : Orthonormal ğ•œ (fun x => x : s k â†’ E) := h k
  rw [orthonormal_subtype_iff_ite] at h_orth
  exact h_orth x (hik hxi) y (hjk hyj)

theorem orthonormal_sUnion_of_directed {s : Set (Set E)} (hs : DirectedOn (Â· âŠ† Â·) s)
    (h : âˆ€ a âˆˆ s, Orthonormal ğ•œ (fun x => ((x : a) : E))) :
    Orthonormal ğ•œ (fun x => x : â‹ƒâ‚€ s â†’ E) := by
  rw [Set.sUnion_eq_iUnion]; exact orthonormal_iUnion_of_directed hs.directed_val (by simpa using h)

/-- Given an orthonormal set `v` of vectors in `E`, there exists a maximal orthonormal set
containing it. -/
theorem exists_maximal_orthonormal {s : Set E} (hs : Orthonormal ğ•œ (Subtype.val : s â†’ E)) :
    âˆƒ w âŠ‡ s, Orthonormal ğ•œ (Subtype.val : w â†’ E) âˆ§
      âˆ€ u âŠ‡ w, Orthonormal ğ•œ (Subtype.val : u â†’ E) â†’ u = w := by
  have := zorn_subset_nonempty { b | Orthonormal ğ•œ (Subtype.val : b â†’ E) } ?_ _ hs
  Â· obtain âŸ¨b, hbâŸ© := this
    exact âŸ¨b, hb.1, hb.2.1, fun u hus hu => hb.2.eq_of_ge hu husâŸ©
  Â· refine fun c hc cc _c0 => âŸ¨â‹ƒâ‚€ c, ?_, ?_âŸ©
    Â· exact orthonormal_sUnion_of_directed cc.directedOn fun x xc => hc xc
    Â· exact fun _ => Set.subset_sUnion_of_mem

open Module

/-- A family of orthonormal vectors with the correct cardinality forms a basis. -/
def basisOfOrthonormalOfCardEqFinrank [Fintype Î¹] [Nonempty Î¹] {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v)
    (card_eq : Fintype.card Î¹ = finrank ğ•œ E) : Basis Î¹ ğ•œ E :=
  basisOfLinearIndependentOfCardEqFinrank hv.linearIndependent card_eq

@[simp]
theorem coe_basisOfOrthonormalOfCardEqFinrank [Fintype Î¹] [Nonempty Î¹] {v : Î¹ â†’ E}
    (hv : Orthonormal ğ•œ v) (card_eq : Fintype.card Î¹ = finrank ğ•œ E) :
    (basisOfOrthonormalOfCardEqFinrank hv card_eq : Î¹ â†’ E) = v :=
  coe_basisOfLinearIndependentOfCardEqFinrank _ _

theorem Orthonormal.ne_zero {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (i : Î¹) : v i â‰  0 := by
  refine ne_of_apply_ne norm ?_
  rw [hv.1 i, norm_zero]
  norm_num

end OrthonormalSets_Seminormed

section Norm_Seminormed

open scoped InnerProductSpace

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [SeminormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

section

variable {Î¹ : Type*} {Î¹' : Type*} {Î¹'' : Type*}
variable {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']
variable {E'' : Type*} [SeminormedAddCommGroup E''] [InnerProductSpace ğ•œ E'']

/-- A linear isometry preserves the property of being orthonormal. -/
theorem LinearIsometry.orthonormal_comp_iff {v : Î¹ â†’ E} (f : E â†’â‚—áµ¢[ğ•œ] E') :
    Orthonormal ğ•œ (f âˆ˜ v) â†” Orthonormal ğ•œ v := by
  classical simp_rw [orthonormal_iff_ite, Function.comp_apply, LinearIsometry.inner_map_map]

/-- A linear isometry preserves the property of being orthonormal. -/
theorem Orthonormal.comp_linearIsometry {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (f : E â†’â‚—áµ¢[ğ•œ] E') :
    Orthonormal ğ•œ (f âˆ˜ v) := by rwa [f.orthonormal_comp_iff]

/-- A linear isometric equivalence preserves the property of being orthonormal. -/
theorem Orthonormal.comp_linearIsometryEquiv {v : Î¹ â†’ E} (hv : Orthonormal ğ•œ v) (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
    Orthonormal ğ•œ (f âˆ˜ v) :=
  hv.comp_linearIsometry f.toLinearIsometry

/-- A linear isometric equivalence, applied with `Basis.map`, preserves the property of being
orthonormal. -/
theorem Orthonormal.mapLinearIsometryEquiv {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v)
    (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') : Orthonormal ğ•œ (v.map f.toLinearEquiv) :=
  hv.comp_linearIsometryEquiv f

/-- A linear map that sends an orthonormal basis to orthonormal vectors is a linear isometry. -/
def LinearMap.isometryOfOrthonormal (f : E â†’â‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v)
    (hf : Orthonormal ğ•œ (f âˆ˜ v)) : E â†’â‚—áµ¢[ğ•œ] E' :=
  f.isometryOfInner fun x y => by
    classical rw [â† v.linearCombination_repr x, â† v.linearCombination_repr y,
      Finsupp.apply_linearCombination, Finsupp.apply_linearCombination,
      hv.inner_finsupp_eq_sum_left, hf.inner_finsupp_eq_sum_left]

@[simp]
theorem LinearMap.coe_isometryOfOrthonormal (f : E â†’â‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E}
    (hv : Orthonormal ğ•œ v) (hf : Orthonormal ğ•œ (f âˆ˜ v)) : â‡‘(f.isometryOfOrthonormal hv hf) = f :=
  rfl

@[simp]
theorem LinearMap.isometryOfOrthonormal_toLinearMap (f : E â†’â‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E}
    (hv : Orthonormal ğ•œ v) (hf : Orthonormal ğ•œ (f âˆ˜ v)) :
    (f.isometryOfOrthonormal hv hf).toLinearMap = f :=
  rfl

/-- A linear equivalence that sends an orthonormal basis to orthonormal vectors is a linear
isometric equivalence. -/
def LinearEquiv.isometryOfOrthonormal (f : E â‰ƒâ‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v)
    (hf : Orthonormal ğ•œ (f âˆ˜ v)) : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
  f.isometryOfInner fun x y => by
    rw [â† LinearEquiv.coe_coe] at hf
    classical rw [â† v.linearCombination_repr x, â† v.linearCombination_repr y,
      â† LinearEquiv.coe_coe f, Finsupp.apply_linearCombination,
      Finsupp.apply_linearCombination, hv.inner_finsupp_eq_sum_left, hf.inner_finsupp_eq_sum_left]

@[simp]
theorem LinearEquiv.coe_isometryOfOrthonormal (f : E â‰ƒâ‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E}
    (hv : Orthonormal ğ•œ v) (hf : Orthonormal ğ•œ (f âˆ˜ v)) : â‡‘(f.isometryOfOrthonormal hv hf) = f :=
  rfl

@[simp]
theorem LinearEquiv.isometryOfOrthonormal_toLinearEquiv (f : E â‰ƒâ‚—[ğ•œ] E') {v : Basis Î¹ ğ•œ E}
    (hv : Orthonormal ğ•œ v) (hf : Orthonormal ğ•œ (f âˆ˜ v)) :
    (f.isometryOfOrthonormal hv hf).toLinearEquiv = f :=
  rfl

/-- A linear isometric equivalence that sends an orthonormal basis to a given orthonormal basis. -/
def Orthonormal.equiv {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v) {v' : Basis Î¹' ğ•œ E'}
    (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') : E â‰ƒâ‚—áµ¢[ğ•œ] E' :=
  (v.equiv v' e).isometryOfOrthonormal hv
    (by
      have h : v.equiv v' e âˆ˜ v = v' âˆ˜ e := by
        ext i
        simp
      rw [h]
      classical exact hv'.comp _ e.injective)

@[simp]
theorem Orthonormal.equiv_toLinearEquiv {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v)
    {v' : Basis Î¹' ğ•œ E'} (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') :
    (hv.equiv hv' e).toLinearEquiv = v.equiv v' e :=
  rfl

@[simp]
theorem Orthonormal.equiv_apply {Î¹' : Type*} {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v)
    {v' : Basis Î¹' ğ•œ E'} (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') (i : Î¹) :
    hv.equiv hv' e (v i) = v' (e i) :=
  Basis.equiv_apply _ _ _ _

@[simp]
theorem Orthonormal.equiv_trans {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v) {v' : Basis Î¹' ğ•œ E'}
    (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') {v'' : Basis Î¹'' ğ•œ E''} (hv'' : Orthonormal ğ•œ v'')
    (e' : Î¹' â‰ƒ Î¹'') : (hv.equiv hv' e).trans (hv'.equiv hv'' e') = hv.equiv hv'' (e.trans e') :=
  v.ext_linearIsometryEquiv fun i => by
    simp only [LinearIsometryEquiv.trans_apply, Orthonormal.equiv_apply, e.coe_trans,
      Function.comp_apply]

theorem Orthonormal.map_equiv {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v) {v' : Basis Î¹' ğ•œ E'}
    (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') :
    v.map (hv.equiv hv' e).toLinearEquiv = v'.reindex e.symm :=
  v.map_equiv _ _

end

section

variable {Î¹ : Type*} {Î¹' : Type*} {E' : Type*} [SeminormedAddCommGroup E'] [InnerProductSpace ğ•œ E']

@[simp]
theorem Orthonormal.equiv_refl {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v) :
    hv.equiv hv (Equiv.refl Î¹) = LinearIsometryEquiv.refl ğ•œ E :=
  v.ext_linearIsometryEquiv fun i => by
    simp only [Orthonormal.equiv_apply, Equiv.coe_refl, id, LinearIsometryEquiv.coe_refl]

@[simp]
theorem Orthonormal.equiv_symm {v : Basis Î¹ ğ•œ E} (hv : Orthonormal ğ•œ v) {v' : Basis Î¹' ğ•œ E'}
    (hv' : Orthonormal ğ•œ v') (e : Î¹ â‰ƒ Î¹') : (hv.equiv hv' e).symm = hv'.equiv hv e.symm :=
  v'.ext_linearIsometryEquiv fun i =>
    (hv.equiv hv' e).injective <| by
      simp only [LinearIsometryEquiv.apply_symm_apply, Orthonormal.equiv_apply, e.apply_symm_apply]

end

end Norm_Seminormed

section BesselsInequality

variable [SeminormedAddCommGroup E] [InnerProductSpace ğ•œ E]

variable {Î¹ : Type*} (x : E) {v : Î¹ â†’ E}

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

/-- Bessel's inequality for finite sums. -/
theorem Orthonormal.sum_inner_products_le {s : Finset Î¹} (hv : Orthonormal ğ•œ v) :
    âˆ‘ i âˆˆ s, â€–âŸªv i, xâŸ«â€– ^ 2 â‰¤ â€–xâ€– ^ 2 := by
  have hâ‚‚ :
    (âˆ‘ i âˆˆ s, âˆ‘ j âˆˆ s, âŸªv i, xâŸ« * âŸªx, v jâŸ« * âŸªv j, v iâŸ«) = (âˆ‘ k âˆˆ s, âŸªv k, xâŸ« * âŸªx, v kâŸ« : ğ•œ) := by
    classical exact hv.inner_left_right_finset
  have hâ‚ƒ : âˆ€ z : ğ•œ, re (z * conj z) = â€–zâ€– ^ 2 := by
    intro z
    simp only [mul_conj]
    norm_cast
  suffices hbf : â€–x - âˆ‘ i âˆˆ s, âŸªv i, xâŸ« â€¢ v iâ€– ^ 2 = â€–xâ€– ^ 2 - âˆ‘ i âˆˆ s, â€–âŸªv i, xâŸ«â€– ^ 2 by
    rw [â† sub_nonneg, â† hbf]
    simp only [norm_nonneg, pow_nonneg]
  rw [@norm_sub_sq ğ•œ, sub_add]
  simp only [@InnerProductSpace.norm_sq_eq_re_inner ğ•œ E, inner_sum, sum_inner]
  simp only [inner_smul_right, two_mul, inner_smul_left, inner_conj_symm, â† mul_assoc, hâ‚‚,
    add_sub_cancel_right, sub_right_inj]
  simp only [map_sum, â† inner_conj_symm x, â† hâ‚ƒ]

/-- Bessel's inequality. -/
theorem Orthonormal.tsum_inner_products_le (hv : Orthonormal ğ•œ v) :
    âˆ‘' i, â€–âŸªv i, xâŸ«â€– ^ 2 â‰¤ â€–xâ€– ^ 2 := by
  refine tsum_le_of_sum_le' ?_ fun s => hv.sum_inner_products_le x
  simp only [norm_nonneg, pow_nonneg]

/-- The sum defined in Bessel's inequality is summable. -/
theorem Orthonormal.inner_products_summable (hv : Orthonormal ğ•œ v) :
    Summable fun i => â€–âŸªv i, xâŸ«â€– ^ 2 := by
  use â¨† s : Finset Î¹, âˆ‘ i âˆˆ s, â€–âŸªv i, xâŸ«â€– ^ 2
  apply hasSum_of_isLUB_of_nonneg
  Â· intro b
    simp only [norm_nonneg, pow_nonneg]
  Â· refine isLUB_ciSup ?_
    use â€–xâ€– ^ 2
    rintro y âŸ¨s, rflâŸ©
    exact hv.sum_inner_products_le x

end BesselsInequality
