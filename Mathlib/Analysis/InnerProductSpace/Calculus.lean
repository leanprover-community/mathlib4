/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Analysis.SpecialFunctions.Sqrt
import Mathlib.Analysis.NormedSpace.HomeomorphBall

#align_import analysis.inner_product_space.calculus from "leanprover-community/mathlib"@"f9dd3204df14a0749cd456fac1e6849dfe7d2b88"

/-!
# Calculus in inner product spaces

In this file we prove that the inner product and square of the norm in an inner space are
infinitely `â„`-smooth. In order to state these results, we need a `NormedSpace â„ E`
instance. Though we can deduce this structure from `InnerProductSpace ğ•œ E`, this instance may be
not definitionally equal to some other â€œnaturalâ€ instance. So, we assume `[NormedSpace â„ E]`.

We also prove that functions to a `EuclideanSpace` are (higher) differentiable if and only if
their components are. This follows from the corresponding fact for finite product of normed spaces,
and from the equivalence of norms in finite dimensions.

## TODO

The last part of the file should be generalized to `PiLp`.
-/

noncomputable section

open RCLike Real Filter

open scoped Classical Topology

section DerivInner

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

variable (ğ•œ) [NormedSpace â„ E]

/-- Derivative of the inner product. -/
def fderivInnerCLM (p : E Ã— E) : E Ã— E â†’L[â„] ğ•œ :=
  isBoundedBilinearMap_inner.deriv p
#align fderiv_inner_clm fderivInnerCLM

@[simp]
theorem fderivInnerCLM_apply (p x : E Ã— E) : fderivInnerCLM ğ•œ p x = âŸªp.1, x.2âŸ« + âŸªx.1, p.2âŸ« :=
  rfl
#align fderiv_inner_clm_apply fderivInnerCLM_apply

variable {ğ•œ} -- Porting note: Lean 3 magically switches back to `{ğ•œ}` here

theorem contDiff_inner {n} : ContDiff â„ n fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  isBoundedBilinearMap_inner.contDiff
#align cont_diff_inner contDiff_inner

theorem contDiffAt_inner {p : E Ã— E} {n} : ContDiffAt â„ n (fun p : E Ã— E => âŸªp.1, p.2âŸ«) p :=
  ContDiff.contDiffAt contDiff_inner
#align cont_diff_at_inner contDiffAt_inner

theorem differentiable_inner : Differentiable â„ fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  isBoundedBilinearMap_inner.differentiableAt
#align differentiable_inner differentiable_inner

variable (ğ•œ)
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G] {f g : G â†’ E} {f' g' : G â†’L[â„] E}
  {s : Set G} {x : G} {n : â„•âˆ}

theorem ContDiffWithinAt.inner (hf : ContDiffWithinAt â„ n f s x) (hg : ContDiffWithinAt â„ n g s x) :
    ContDiffWithinAt â„ n (fun x => âŸªf x, g xâŸ«) s x :=
  contDiffAt_inner.comp_contDiffWithinAt x (hf.prod hg)
#align cont_diff_within_at.inner ContDiffWithinAt.inner

nonrec theorem ContDiffAt.inner (hf : ContDiffAt â„ n f x) (hg : ContDiffAt â„ n g x) :
    ContDiffAt â„ n (fun x => âŸªf x, g xâŸ«) x :=
  hf.inner ğ•œ hg
#align cont_diff_at.inner ContDiffAt.inner

theorem ContDiffOn.inner (hf : ContDiffOn â„ n f s) (hg : ContDiffOn â„ n g s) :
    ContDiffOn â„ n (fun x => âŸªf x, g xâŸ«) s := fun x hx => (hf x hx).inner ğ•œ (hg x hx)
#align cont_diff_on.inner ContDiffOn.inner

theorem ContDiff.inner (hf : ContDiff â„ n f) (hg : ContDiff â„ n g) :
    ContDiff â„ n fun x => âŸªf x, g xâŸ« :=
  contDiff_inner.comp (hf.prod hg)
#align cont_diff.inner ContDiff.inner

theorem HasFDerivWithinAt.inner (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) :
    HasFDerivWithinAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') s
      x :=
  (isBoundedBilinearMap_inner.hasFDerivAt (f x, g x)).comp_hasFDerivWithinAt x (hf.prod hg)
#align has_fderiv_within_at.inner HasFDerivWithinAt.inner

theorem HasStrictFDerivAt.inner (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') x :=
  (isBoundedBilinearMap_inner.hasStrictFDerivAt (f x, g x)).comp x (hf.prod hg)
#align has_strict_fderiv_at.inner HasStrictFDerivAt.inner

theorem HasFDerivAt.inner (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') x :=
  (isBoundedBilinearMap_inner.hasFDerivAt (f x, g x)).comp x (hf.prod hg)
#align has_fderiv_at.inner HasFDerivAt.inner

theorem HasDerivWithinAt.inner {f g : â„ â†’ E} {f' g' : E} {s : Set â„} {x : â„}
    (hf : HasDerivWithinAt f f' s x) (hg : HasDerivWithinAt g g' s x) :
    HasDerivWithinAt (fun t => âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) s x := by
  simpa using (hf.hasFDerivWithinAt.inner ğ•œ hg.hasFDerivWithinAt).hasDerivWithinAt
#align has_deriv_within_at.inner HasDerivWithinAt.inner

theorem HasDerivAt.inner {f g : â„ â†’ E} {f' g' : E} {x : â„} :
    HasDerivAt f f' x â†’ HasDerivAt g g' x â†’
      HasDerivAt (fun t => âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) x := by
  simpa only [â† hasDerivWithinAt_univ] using HasDerivWithinAt.inner ğ•œ
#align has_deriv_at.inner HasDerivAt.inner

theorem DifferentiableWithinAt.inner (hf : DifferentiableWithinAt â„ f s x)
    (hg : DifferentiableWithinAt â„ g s x) : DifferentiableWithinAt â„ (fun x => âŸªf x, g xâŸ«) s x :=
  ((differentiable_inner _).hasFDerivAt.comp_hasFDerivWithinAt x
      (hf.prod hg).hasFDerivWithinAt).differentiableWithinAt
#align differentiable_within_at.inner DifferentiableWithinAt.inner

theorem DifferentiableAt.inner (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x) :
    DifferentiableAt â„ (fun x => âŸªf x, g xâŸ«) x :=
  (differentiable_inner _).comp x (hf.prod hg)
#align differentiable_at.inner DifferentiableAt.inner

theorem DifferentiableOn.inner (hf : DifferentiableOn â„ f s) (hg : DifferentiableOn â„ g s) :
    DifferentiableOn â„ (fun x => âŸªf x, g xâŸ«) s := fun x hx => (hf x hx).inner ğ•œ (hg x hx)
#align differentiable_on.inner DifferentiableOn.inner

theorem Differentiable.inner (hf : Differentiable â„ f) (hg : Differentiable â„ g) :
    Differentiable â„ fun x => âŸªf x, g xâŸ« := fun x => (hf x).inner ğ•œ (hg x)
#align differentiable.inner Differentiable.inner

theorem fderiv_inner_apply (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x) (y : G) :
    fderiv â„ (fun t => âŸªf t, g tâŸ«) x y = âŸªf x, fderiv â„ g x yâŸ« + âŸªfderiv â„ f x y, g xâŸ« := by
  rw [(hf.hasFDerivAt.inner ğ•œ hg.hasFDerivAt).fderiv]; rfl
#align fderiv_inner_apply fderiv_inner_apply

theorem deriv_inner_apply {f g : â„ â†’ E} {x : â„} (hf : DifferentiableAt â„ f x)
    (hg : DifferentiableAt â„ g x) :
    deriv (fun t => âŸªf t, g tâŸ«) x = âŸªf x, deriv g xâŸ« + âŸªderiv f x, g xâŸ« :=
  (hf.hasDerivAt.inner ğ•œ hg.hasDerivAt).deriv
#align deriv_inner_apply deriv_inner_apply

theorem contDiff_norm_sq : ContDiff â„ n fun x : E => â€–xâ€– ^ 2 := by
  convert (reCLM : ğ•œ â†’L[â„] â„).contDiff.comp ((contDiff_id (E := E)).inner ğ•œ (contDiff_id (E := E)))
  exact (inner_self_eq_norm_sq _).symm
#align cont_diff_norm_sq contDiff_norm_sq

theorem ContDiff.norm_sq (hf : ContDiff â„ n f) : ContDiff â„ n fun x => â€–f xâ€– ^ 2 :=
  (contDiff_norm_sq ğ•œ).comp hf
#align cont_diff.norm_sq ContDiff.norm_sq

theorem ContDiffWithinAt.norm_sq (hf : ContDiffWithinAt â„ n f s x) :
    ContDiffWithinAt â„ n (fun y => â€–f yâ€– ^ 2) s x :=
  (contDiff_norm_sq ğ•œ).contDiffAt.comp_contDiffWithinAt x hf
#align cont_diff_within_at.norm_sq ContDiffWithinAt.norm_sq

nonrec theorem ContDiffAt.norm_sq (hf : ContDiffAt â„ n f x) : ContDiffAt â„ n (â€–f Â·â€– ^ 2) x :=
  hf.norm_sq ğ•œ
#align cont_diff_at.norm_sq ContDiffAt.norm_sq

theorem contDiffAt_norm {x : E} (hx : x â‰  0) : ContDiffAt â„ n norm x := by
  have : â€–id xâ€– ^ 2 â‰  0 := pow_ne_zero 2 (norm_pos_iff.2 hx).ne'
  simpa only [id, sqrt_sq, norm_nonneg] using (contDiffAt_id.norm_sq ğ•œ).sqrt this
#align cont_diff_at_norm contDiffAt_norm

theorem ContDiffAt.norm (hf : ContDiffAt â„ n f x) (h0 : f x â‰  0) :
    ContDiffAt â„ n (fun y => â€–f yâ€–) x :=
  (contDiffAt_norm ğ•œ h0).comp x hf
#align cont_diff_at.norm ContDiffAt.norm

theorem ContDiffAt.dist (hf : ContDiffAt â„ n f x) (hg : ContDiffAt â„ n g x) (hne : f x â‰  g x) :
    ContDiffAt â„ n (fun y => dist (f y) (g y)) x := by
  simp only [dist_eq_norm]
  exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)
#align cont_diff_at.dist ContDiffAt.dist

theorem ContDiffWithinAt.norm (hf : ContDiffWithinAt â„ n f s x) (h0 : f x â‰  0) :
    ContDiffWithinAt â„ n (fun y => â€–f yâ€–) s x :=
  (contDiffAt_norm ğ•œ h0).comp_contDiffWithinAt x hf
#align cont_diff_within_at.norm ContDiffWithinAt.norm

theorem ContDiffWithinAt.dist (hf : ContDiffWithinAt â„ n f s x) (hg : ContDiffWithinAt â„ n g s x)
    (hne : f x â‰  g x) : ContDiffWithinAt â„ n (fun y => dist (f y) (g y)) s x := by
  simp only [dist_eq_norm]; exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)
#align cont_diff_within_at.dist ContDiffWithinAt.dist

theorem ContDiffOn.norm_sq (hf : ContDiffOn â„ n f s) : ContDiffOn â„ n (fun y => â€–f yâ€– ^ 2) s :=
  fun x hx => (hf x hx).norm_sq ğ•œ
#align cont_diff_on.norm_sq ContDiffOn.norm_sq

theorem ContDiffOn.norm (hf : ContDiffOn â„ n f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
    ContDiffOn â„ n (fun y => â€–f yâ€–) s := fun x hx => (hf x hx).norm ğ•œ (h0 x hx)
#align cont_diff_on.norm ContDiffOn.norm

theorem ContDiffOn.dist (hf : ContDiffOn â„ n f s) (hg : ContDiffOn â„ n g s)
    (hne : âˆ€ x âˆˆ s, f x â‰  g x) : ContDiffOn â„ n (fun y => dist (f y) (g y)) s := fun x hx =>
  (hf x hx).dist ğ•œ (hg x hx) (hne x hx)
#align cont_diff_on.dist ContDiffOn.dist

theorem ContDiff.norm (hf : ContDiff â„ n f) (h0 : âˆ€ x, f x â‰  0) : ContDiff â„ n fun y => â€–f yâ€– :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.norm ğ•œ (h0 x)
#align cont_diff.norm ContDiff.norm

theorem ContDiff.dist (hf : ContDiff â„ n f) (hg : ContDiff â„ n g) (hne : âˆ€ x, f x â‰  g x) :
    ContDiff â„ n fun y => dist (f y) (g y) :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.dist ğ•œ hg.contDiffAt (hne x)
#align cont_diff.dist ContDiff.dist

-- Porting note: use `2 â€¢` instead of `bit0`
theorem hasStrictFDerivAt_norm_sq (x : F) :
    HasStrictFDerivAt (fun x => â€–xâ€– ^ 2) (2 â€¢ (innerSL â„ x)) x := by
  simp only [sq, â† @inner_self_eq_norm_mul_norm â„]
  convert (hasStrictFDerivAt_id x).inner â„ (hasStrictFDerivAt_id x)
  ext y
  simp [two_smul, real_inner_comm]
#align has_strict_fderiv_at_norm_sq hasStrictFDerivAt_norm_sqâ‚“

theorem HasFDerivAt.norm_sq {f : G â†’ F} {f' : G â†’L[â„] F} (hf : HasFDerivAt f f' x) :
    HasFDerivAt (â€–f Â·â€– ^ 2) (2 â€¢ (innerSL â„ (f x)).comp f') x :=
  (hasStrictFDerivAt_norm_sq _).hasFDerivAt.comp x hf

theorem HasDerivAt.norm_sq {f : â„ â†’ F} {f' : F} {x : â„} (hf : HasDerivAt f f' x) :
    HasDerivAt (â€–f Â·â€– ^ 2) (2 * Inner.inner (f x) f') x := by
  simpa using hf.hasFDerivAt.norm_sq.hasDerivAt

theorem HasFDerivWithinAt.norm_sq {f : G â†’ F} {f' : G â†’L[â„] F} (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (â€–f Â·â€– ^ 2) (2 â€¢ (innerSL â„ (f x)).comp f') s x :=
  (hasStrictFDerivAt_norm_sq _).hasFDerivAt.comp_hasFDerivWithinAt x hf

theorem HasDerivWithinAt.norm_sq {f : â„ â†’ F} {f' : F} {s : Set â„} {x : â„}
    (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (â€–f Â·â€– ^ 2) (2 * Inner.inner (f x) f') s x := by
  simpa using hf.hasFDerivWithinAt.norm_sq.hasDerivWithinAt

theorem DifferentiableAt.norm_sq (hf : DifferentiableAt â„ f x) :
    DifferentiableAt â„ (fun y => â€–f yâ€– ^ 2) x :=
  ((contDiffAt_id.norm_sq ğ•œ).differentiableAt le_rfl).comp x hf
#align differentiable_at.norm_sq DifferentiableAt.norm_sq

theorem DifferentiableAt.norm (hf : DifferentiableAt â„ f x) (h0 : f x â‰  0) :
    DifferentiableAt â„ (fun y => â€–f yâ€–) x :=
  ((contDiffAt_norm ğ•œ h0).differentiableAt le_rfl).comp x hf
#align differentiable_at.norm DifferentiableAt.norm

theorem DifferentiableAt.dist (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x)
    (hne : f x â‰  g x) : DifferentiableAt â„ (fun y => dist (f y) (g y)) x := by
  simp only [dist_eq_norm]; exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)
#align differentiable_at.dist DifferentiableAt.dist

theorem Differentiable.norm_sq (hf : Differentiable â„ f) : Differentiable â„ fun y => â€–f yâ€– ^ 2 :=
  fun x => (hf x).norm_sq ğ•œ
#align differentiable.norm_sq Differentiable.norm_sq

theorem Differentiable.norm (hf : Differentiable â„ f) (h0 : âˆ€ x, f x â‰  0) :
    Differentiable â„ fun y => â€–f yâ€– := fun x => (hf x).norm ğ•œ (h0 x)
#align differentiable.norm Differentiable.norm

theorem Differentiable.dist (hf : Differentiable â„ f) (hg : Differentiable â„ g)
    (hne : âˆ€ x, f x â‰  g x) : Differentiable â„ fun y => dist (f y) (g y) := fun x =>
  (hf x).dist ğ•œ (hg x) (hne x)
#align differentiable.dist Differentiable.dist

theorem DifferentiableWithinAt.norm_sq (hf : DifferentiableWithinAt â„ f s x) :
    DifferentiableWithinAt â„ (fun y => â€–f yâ€– ^ 2) s x :=
  ((contDiffAt_id.norm_sq ğ•œ).differentiableAt le_rfl).comp_differentiableWithinAt x hf
#align differentiable_within_at.norm_sq DifferentiableWithinAt.norm_sq

theorem DifferentiableWithinAt.norm (hf : DifferentiableWithinAt â„ f s x) (h0 : f x â‰  0) :
    DifferentiableWithinAt â„ (fun y => â€–f yâ€–) s x :=
  ((contDiffAt_id.norm ğ•œ h0).differentiableAt le_rfl).comp_differentiableWithinAt x hf
#align differentiable_within_at.norm DifferentiableWithinAt.norm

theorem DifferentiableWithinAt.dist (hf : DifferentiableWithinAt â„ f s x)
    (hg : DifferentiableWithinAt â„ g s x) (hne : f x â‰  g x) :
    DifferentiableWithinAt â„ (fun y => dist (f y) (g y)) s x := by
  simp only [dist_eq_norm]
  exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)
#align differentiable_within_at.dist DifferentiableWithinAt.dist

theorem DifferentiableOn.norm_sq (hf : DifferentiableOn â„ f s) :
    DifferentiableOn â„ (fun y => â€–f yâ€– ^ 2) s := fun x hx => (hf x hx).norm_sq ğ•œ
#align differentiable_on.norm_sq DifferentiableOn.norm_sq

theorem DifferentiableOn.norm (hf : DifferentiableOn â„ f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
    DifferentiableOn â„ (fun y => â€–f yâ€–) s := fun x hx => (hf x hx).norm ğ•œ (h0 x hx)
#align differentiable_on.norm DifferentiableOn.norm

theorem DifferentiableOn.dist (hf : DifferentiableOn â„ f s) (hg : DifferentiableOn â„ g s)
    (hne : âˆ€ x âˆˆ s, f x â‰  g x) : DifferentiableOn â„ (fun y => dist (f y) (g y)) s := fun x hx =>
  (hf x hx).dist ğ•œ (hg x hx) (hne x hx)
#align differentiable_on.dist DifferentiableOn.dist

end DerivInner

section PiLike

open ContinuousLinearMap

variable {ğ•œ Î¹ H : Type*} [RCLike ğ•œ] [NormedAddCommGroup H] [NormedSpace ğ•œ H] [Fintype Î¹]
  {f : H â†’ EuclideanSpace ğ•œ Î¹} {f' : H â†’L[ğ•œ] EuclideanSpace ğ•œ Î¹} {t : Set H} {y : H}

theorem differentiableWithinAt_euclidean :
    DifferentiableWithinAt ğ•œ f t y â†” âˆ€ i, DifferentiableWithinAt ğ•œ (fun x => f x i) t y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_differentiableWithinAt_iff, differentiableWithinAt_pi]
  rfl
#align differentiable_within_at_euclidean differentiableWithinAt_euclidean

theorem differentiableAt_euclidean :
    DifferentiableAt ğ•œ f y â†” âˆ€ i, DifferentiableAt ğ•œ (fun x => f x i) y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_differentiableAt_iff, differentiableAt_pi]
  rfl
#align differentiable_at_euclidean differentiableAt_euclidean

theorem differentiableOn_euclidean :
    DifferentiableOn ğ•œ f t â†” âˆ€ i, DifferentiableOn ğ•œ (fun x => f x i) t := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_differentiableOn_iff, differentiableOn_pi]
  rfl
#align differentiable_on_euclidean differentiableOn_euclidean

theorem differentiable_euclidean : Differentiable ğ•œ f â†” âˆ€ i, Differentiable ğ•œ fun x => f x i := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_differentiable_iff, differentiable_pi]
  rfl
#align differentiable_euclidean differentiable_euclidean

theorem hasStrictFDerivAt_euclidean :
    HasStrictFDerivAt f f' y â†”
      âˆ€ i, HasStrictFDerivAt (fun x => f x i) (EuclideanSpace.proj i âˆ˜L f') y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_hasStrictFDerivAt_iff, hasStrictFDerivAt_pi']
  rfl
#align has_strict_fderiv_at_euclidean hasStrictFDerivAt_euclidean

theorem hasFDerivWithinAt_euclidean :
    HasFDerivWithinAt f f' t y â†”
      âˆ€ i, HasFDerivWithinAt (fun x => f x i) (EuclideanSpace.proj i âˆ˜L f') t y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_hasFDerivWithinAt_iff, hasFDerivWithinAt_pi']
  rfl
#align has_fderiv_within_at_euclidean hasFDerivWithinAt_euclidean

theorem contDiffWithinAt_euclidean {n : â„•âˆ} :
    ContDiffWithinAt ğ•œ n f t y â†” âˆ€ i, ContDiffWithinAt ğ•œ n (fun x => f x i) t y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_contDiffWithinAt_iff, contDiffWithinAt_pi]
  rfl
#align cont_diff_within_at_euclidean contDiffWithinAt_euclidean

theorem contDiffAt_euclidean {n : â„•âˆ} :
    ContDiffAt ğ•œ n f y â†” âˆ€ i, ContDiffAt ğ•œ n (fun x => f x i) y := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_contDiffAt_iff, contDiffAt_pi]
  rfl
#align cont_diff_at_euclidean contDiffAt_euclidean

theorem contDiffOn_euclidean {n : â„•âˆ} :
    ContDiffOn ğ•œ n f t â†” âˆ€ i, ContDiffOn ğ•œ n (fun x => f x i) t := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_contDiffOn_iff, contDiffOn_pi]
  rfl
#align cont_diff_on_euclidean contDiffOn_euclidean

theorem contDiff_euclidean {n : â„•âˆ} : ContDiff ğ•œ n f â†” âˆ€ i, ContDiff ğ•œ n fun x => f x i := by
  rw [â† (EuclideanSpace.equiv Î¹ ğ•œ).comp_contDiff_iff, contDiff_pi]
  rfl
#align cont_diff_euclidean contDiff_euclidean

end PiLike

section DiffeomorphUnitBall

open Metric hiding mem_nhds_iff

variable {n : â„•âˆ} {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]

theorem PartialHomeomorph.contDiff_univUnitBall : ContDiff â„ n (univUnitBall : E â†’ E) := by
  suffices ContDiff â„ n fun x : E => (âˆš(1 + â€–xâ€– ^ 2 : â„))â»Â¹ from this.smul contDiff_id
  have h : âˆ€ x : E, (0 : â„) < (1 : â„) + â€–xâ€– ^ 2 := fun x => by positivity
  refine ContDiff.inv ?_ fun x => Real.sqrt_ne_zero'.mpr (h x)
  exact (contDiff_const.add <| contDiff_norm_sq â„).sqrt fun x => (h x).ne'

theorem PartialHomeomorph.contDiffOn_univUnitBall_symm :
    ContDiffOn â„ n univUnitBall.symm (ball (0 : E) 1) := fun y hy â†¦ by
  apply ContDiffAt.contDiffWithinAt
  suffices ContDiffAt â„ n (fun y : E => (âˆš(1 - â€–yâ€– ^ 2 : â„))â»Â¹) y from this.smul contDiffAt_id
  have h : (0 : â„) < (1 : â„) - â€–(y : E)â€– ^ 2 := by
    rwa [mem_ball_zero_iff, â† _root_.abs_one, â† abs_norm, â† sq_lt_sq, one_pow, â† sub_pos] at hy
  refine ContDiffAt.inv ?_ (Real.sqrt_ne_zero'.mpr h)
  refine (contDiffAt_sqrt h.ne').comp y ?_
  exact contDiffAt_const.sub (contDiff_norm_sq â„).contDiffAt

theorem Homeomorph.contDiff_unitBall : ContDiff â„ n fun x : E => (unitBall x : E) :=
  PartialHomeomorph.contDiff_univUnitBall
#align cont_diff_homeomorph_unit_ball Homeomorph.contDiff_unitBall

@[deprecated PartialHomeomorph.contDiffOn_univUnitBall_symm (since := "2023-07-26")]
theorem Homeomorph.contDiffOn_unitBall_symm {f : E â†’ E}
    (h : âˆ€ (y) (hy : y âˆˆ ball (0 : E) 1), f y = Homeomorph.unitBall.symm âŸ¨y, hyâŸ©) :
    ContDiffOn â„ n f <| ball 0 1 :=
  PartialHomeomorph.contDiffOn_univUnitBall_symm.congr h
#align cont_diff_on_homeomorph_unit_ball_symm Homeomorph.contDiffOn_unitBall_symm

namespace PartialHomeomorph

variable {c : E} {r : â„}

theorem contDiff_unitBallBall (hr : 0 < r) : ContDiff â„ n (unitBallBall c r hr) :=
  (contDiff_id.const_smul _).add contDiff_const

theorem contDiff_unitBallBall_symm (hr : 0 < r) : ContDiff â„ n (unitBallBall c r hr).symm :=
  (contDiff_id.sub contDiff_const).const_smul _

theorem contDiff_univBall : ContDiff â„ n (univBall c r) := by
  unfold univBall; split_ifs with h
  Â· exact (contDiff_unitBallBall h).comp contDiff_univUnitBall
  Â· exact contDiff_id.add contDiff_const

theorem contDiffOn_univBall_symm :
    ContDiffOn â„ n (univBall c r).symm (ball c r) := by
  unfold univBall; split_ifs with h
  Â· refine contDiffOn_univUnitBall_symm.comp (contDiff_unitBallBall_symm h).contDiffOn ?_
    rw [â† unitBallBall_source c r h, â† unitBallBall_target c r h]
    apply PartialHomeomorph.symm_mapsTo
  Â· exact contDiffOn_id.sub contDiffOn_const

end PartialHomeomorph

end DiffeomorphUnitBall
