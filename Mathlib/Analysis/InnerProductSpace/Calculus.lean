/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Analysis.SpecialFunctions.Sqrt
import Mathlib.Analysis.Normed.Module.Ball.Homeomorph
import Mathlib.Analysis.Calculus.ContDiff.WithLp
import Mathlib.Analysis.Calculus.FDeriv.WithLp

/-!
# Calculus in inner product spaces

In this file we prove that the inner product and square of the norm in an inner space are
infinitely `â„`-smooth. In order to state these results, we need a `NormedSpace â„ E`
instance. Though we can deduce this structure from `InnerProductSpace ğ•œ E`, this instance may be
not definitionally equal to some other â€œnaturalâ€ instance. So, we assume `[NormedSpace â„ E]`.

We also prove that functions to a `EuclideanSpace` are (higher) differentiable if and only if
their components are. This follows from the corresponding fact for finite product of normed spaces,
and from the equivalence of norms in finite dimensions.

## TODO

The last part of the file should be generalized to `PiLp`.
-/

noncomputable section

open RCLike Real Filter

section DerivInner

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (ğ•œ) [NormedSpace â„ E]

/-- Derivative of the inner product. -/
def fderivInnerCLM (p : E Ã— E) : E Ã— E â†’L[â„] ğ•œ :=
  isBoundedBilinearMap_inner.deriv p

@[simp]
theorem fderivInnerCLM_apply (p x : E Ã— E) : fderivInnerCLM ğ•œ p x = âŸªp.1, x.2âŸ« + âŸªx.1, p.2âŸ« :=
  rfl

variable {ğ•œ}

theorem contDiff_inner {n} : ContDiff â„ n fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  isBoundedBilinearMap_inner.contDiff

theorem contDiffAt_inner {p : E Ã— E} {n} : ContDiffAt â„ n (fun p : E Ã— E => âŸªp.1, p.2âŸ«) p :=
  ContDiff.contDiffAt contDiff_inner

theorem differentiable_inner : Differentiable â„ fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  isBoundedBilinearMap_inner.differentiableAt

variable (ğ•œ)
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G] {f g : G â†’ E} {f' g' : G â†’L[â„] E}
  {s : Set G} {x : G} {n : WithTop â„•âˆ}

theorem ContDiffWithinAt.inner (hf : ContDiffWithinAt â„ n f s x) (hg : ContDiffWithinAt â„ n g s x) :
    ContDiffWithinAt â„ n (fun x => âŸªf x, g xâŸ«) s x :=
  contDiffAt_inner.comp_contDiffWithinAt x (hf.prodMk hg)

nonrec theorem ContDiffAt.inner (hf : ContDiffAt â„ n f x) (hg : ContDiffAt â„ n g x) :
    ContDiffAt â„ n (fun x => âŸªf x, g xâŸ«) x :=
  hf.inner ğ•œ hg

theorem ContDiffOn.inner (hf : ContDiffOn â„ n f s) (hg : ContDiffOn â„ n g s) :
    ContDiffOn â„ n (fun x => âŸªf x, g xâŸ«) s := fun x hx => (hf x hx).inner ğ•œ (hg x hx)

theorem ContDiff.inner (hf : ContDiff â„ n f) (hg : ContDiff â„ n g) :
    ContDiff â„ n fun x => âŸªf x, g xâŸ« :=
  contDiff_inner.comp (hf.prodMk hg)

theorem HasFDerivWithinAt.inner (hf : HasFDerivWithinAt f f' s x)
    (hg : HasFDerivWithinAt g g' s x) :
    HasFDerivWithinAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') s
      x := by
  -- `by exact` to handle a tricky unification.
  exact isBoundedBilinearMap_inner (ğ•œ := ğ•œ) (E := E)
    |>.hasFDerivAt (f x, g x) |>.comp_hasFDerivWithinAt x (hf.prodMk hg)

theorem HasStrictFDerivAt.inner (hf : HasStrictFDerivAt f f' x) (hg : HasStrictFDerivAt g g' x) :
    HasStrictFDerivAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') x :=
  isBoundedBilinearMap_inner (ğ•œ := ğ•œ) (E := E)
    |>.hasStrictFDerivAt (f x, g x) |>.comp x (hf.prodMk hg)

theorem HasFDerivAt.inner (hf : HasFDerivAt f f' x) (hg : HasFDerivAt g g' x) :
    HasFDerivAt (fun t => âŸªf t, g tâŸ«) ((fderivInnerCLM ğ•œ (f x, g x)).comp <| f'.prod g') x := by
  -- `by exact` to handle a tricky unification.
  exact isBoundedBilinearMap_inner (ğ•œ := ğ•œ) (E := E)
    |>.hasFDerivAt (f x, g x) |>.comp x (hf.prodMk hg)

theorem HasDerivWithinAt.inner {f g : â„ â†’ E} {f' g' : E} {s : Set â„} {x : â„}
    (hf : HasDerivWithinAt f f' s x) (hg : HasDerivWithinAt g g' s x) :
    HasDerivWithinAt (fun t => âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) s x := by
  simpa using (hf.hasFDerivWithinAt.inner ğ•œ hg.hasFDerivWithinAt).hasDerivWithinAt

theorem HasDerivAt.inner {f g : â„ â†’ E} {f' g' : E} {x : â„} :
    HasDerivAt f f' x â†’ HasDerivAt g g' x â†’
      HasDerivAt (fun t => âŸªf t, g tâŸ«) (âŸªf x, g'âŸ« + âŸªf', g xâŸ«) x := by
  simpa only [â† hasDerivWithinAt_univ] using HasDerivWithinAt.inner ğ•œ

theorem DifferentiableWithinAt.inner (hf : DifferentiableWithinAt â„ f s x)
    (hg : DifferentiableWithinAt â„ g s x) : DifferentiableWithinAt â„ (fun x => âŸªf x, g xâŸ«) s x :=
  (hf.hasFDerivWithinAt.inner ğ•œ hg.hasFDerivWithinAt).differentiableWithinAt

theorem DifferentiableAt.inner (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x) :
    DifferentiableAt â„ (fun x => âŸªf x, g xâŸ«) x :=
  (hf.hasFDerivAt.inner ğ•œ hg.hasFDerivAt).differentiableAt

theorem DifferentiableOn.inner (hf : DifferentiableOn â„ f s) (hg : DifferentiableOn â„ g s) :
    DifferentiableOn â„ (fun x => âŸªf x, g xâŸ«) s := fun x hx => (hf x hx).inner ğ•œ (hg x hx)

theorem Differentiable.inner (hf : Differentiable â„ f) (hg : Differentiable â„ g) :
    Differentiable â„ fun x => âŸªf x, g xâŸ« := fun x => (hf x).inner ğ•œ (hg x)

theorem fderiv_inner_apply (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x) (y : G) :
    fderiv â„ (fun t => âŸªf t, g tâŸ«) x y = âŸªf x, fderiv â„ g x yâŸ« + âŸªfderiv â„ f x y, g xâŸ« := by
  rw [(hf.hasFDerivAt.inner ğ•œ hg.hasFDerivAt).fderiv]; rfl

theorem deriv_inner_apply {f g : â„ â†’ E} {x : â„} (hf : DifferentiableAt â„ f x)
    (hg : DifferentiableAt â„ g x) :
    deriv (fun t => âŸªf t, g tâŸ«) x = âŸªf x, deriv g xâŸ« + âŸªderiv f x, g xâŸ« :=
  (hf.hasDerivAt.inner ğ•œ hg.hasDerivAt).deriv

section
include ğ•œ

theorem contDiff_norm_sq : ContDiff â„ n fun x : E => â€–xâ€– ^ 2 := by
  convert (reCLM : ğ•œ â†’L[â„] â„).contDiff.comp ((contDiff_id (E := E)).inner ğ•œ (contDiff_id (E := E)))
  exact (inner_self_eq_norm_sq _).symm

theorem ContDiff.norm_sq (hf : ContDiff â„ n f) : ContDiff â„ n fun x => â€–f xâ€– ^ 2 :=
  (contDiff_norm_sq ğ•œ).comp hf

theorem ContDiffWithinAt.norm_sq (hf : ContDiffWithinAt â„ n f s x) :
    ContDiffWithinAt â„ n (fun y => â€–f yâ€– ^ 2) s x :=
  (contDiff_norm_sq ğ•œ).contDiffAt.comp_contDiffWithinAt x hf

nonrec theorem ContDiffAt.norm_sq (hf : ContDiffAt â„ n f x) : ContDiffAt â„ n (â€–f Â·â€– ^ 2) x :=
  hf.norm_sq ğ•œ

theorem contDiffAt_norm {x : E} (hx : x â‰  0) : ContDiffAt â„ n norm x := by
  have : â€–id xâ€– ^ 2 â‰  0 := pow_ne_zero 2 (norm_pos_iff.2 hx).ne'
  simpa only [id, sqrt_sq, norm_nonneg] using (contDiffAt_id.norm_sq ğ•œ).sqrt this

theorem ContDiffAt.norm (hf : ContDiffAt â„ n f x) (h0 : f x â‰  0) :
    ContDiffAt â„ n (fun y => â€–f yâ€–) x :=
  (contDiffAt_norm ğ•œ h0).comp x hf

theorem ContDiffAt.dist (hf : ContDiffAt â„ n f x) (hg : ContDiffAt â„ n g x) (hne : f x â‰  g x) :
    ContDiffAt â„ n (fun y => dist (f y) (g y)) x := by
  simp only [dist_eq_norm]
  exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)

theorem ContDiffWithinAt.norm (hf : ContDiffWithinAt â„ n f s x) (h0 : f x â‰  0) :
    ContDiffWithinAt â„ n (fun y => â€–f yâ€–) s x :=
  (contDiffAt_norm ğ•œ h0).comp_contDiffWithinAt x hf

theorem ContDiffWithinAt.dist (hf : ContDiffWithinAt â„ n f s x) (hg : ContDiffWithinAt â„ n g s x)
    (hne : f x â‰  g x) : ContDiffWithinAt â„ n (fun y => dist (f y) (g y)) s x := by
  simp only [dist_eq_norm]; exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)

theorem ContDiffOn.norm_sq (hf : ContDiffOn â„ n f s) : ContDiffOn â„ n (fun y => â€–f yâ€– ^ 2) s :=
  fun x hx => (hf x hx).norm_sq ğ•œ

theorem ContDiffOn.norm (hf : ContDiffOn â„ n f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
    ContDiffOn â„ n (fun y => â€–f yâ€–) s := fun x hx => (hf x hx).norm ğ•œ (h0 x hx)

theorem ContDiffOn.dist (hf : ContDiffOn â„ n f s) (hg : ContDiffOn â„ n g s)
    (hne : âˆ€ x âˆˆ s, f x â‰  g x) : ContDiffOn â„ n (fun y => dist (f y) (g y)) s := fun x hx =>
  (hf x hx).dist ğ•œ (hg x hx) (hne x hx)

theorem ContDiff.norm (hf : ContDiff â„ n f) (h0 : âˆ€ x, f x â‰  0) : ContDiff â„ n fun y => â€–f yâ€– :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.norm ğ•œ (h0 x)

theorem ContDiff.dist (hf : ContDiff â„ n f) (hg : ContDiff â„ n g) (hne : âˆ€ x, f x â‰  g x) :
    ContDiff â„ n fun y => dist (f y) (g y) :=
  contDiff_iff_contDiffAt.2 fun x => hf.contDiffAt.dist ğ•œ hg.contDiffAt (hne x)

end

section
open scoped RealInnerProductSpace

theorem hasStrictFDerivAt_norm_sq (x : F) :
    HasStrictFDerivAt (fun x => â€–xâ€– ^ 2) (2 â€¢ (innerSL â„ x)) x := by
  simp only [sq, â† @inner_self_eq_norm_mul_norm â„]
  convert (hasStrictFDerivAt_id x).inner â„ (hasStrictFDerivAt_id x)
  ext y
  simp [two_smul, real_inner_comm]

theorem HasFDerivAt.norm_sq {f : G â†’ F} {f' : G â†’L[â„] F} (hf : HasFDerivAt f f' x) :
    HasFDerivAt (â€–f Â·â€– ^ 2) (2 â€¢ (innerSL â„ (f x)).comp f') x :=
  (hasStrictFDerivAt_norm_sq _).hasFDerivAt.comp x hf

theorem HasDerivAt.norm_sq {f : â„ â†’ F} {f' : F} {x : â„} (hf : HasDerivAt f f' x) :
    HasDerivAt (â€–f Â·â€– ^ 2) (2 * âŸªf x, f'âŸ«) x := by
  simpa using hf.hasFDerivAt.norm_sq.hasDerivAt

theorem HasFDerivWithinAt.norm_sq {f : G â†’ F} {f' : G â†’L[â„] F} (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (â€–f Â·â€– ^ 2) (2 â€¢ (innerSL â„ (f x)).comp f') s x :=
  (hasStrictFDerivAt_norm_sq _).hasFDerivAt.comp_hasFDerivWithinAt x hf

theorem HasDerivWithinAt.norm_sq {f : â„ â†’ F} {f' : F} {s : Set â„} {x : â„}
    (hf : HasDerivWithinAt f f' s x) :
    HasDerivWithinAt (â€–f Â·â€– ^ 2) (2 * âŸªf x, f'âŸ«) s x := by
  simpa using hf.hasFDerivWithinAt.norm_sq.hasDerivWithinAt

end

section
include ğ•œ

theorem DifferentiableAt.norm_sq (hf : DifferentiableAt â„ f x) :
    DifferentiableAt â„ (fun y => â€–f yâ€– ^ 2) x :=
  ((contDiffAt_id.norm_sq ğ•œ).differentiableAt le_rfl).comp x hf

theorem DifferentiableAt.norm (hf : DifferentiableAt â„ f x) (h0 : f x â‰  0) :
    DifferentiableAt â„ (fun y => â€–f yâ€–) x :=
  ((contDiffAt_norm ğ•œ h0).differentiableAt le_rfl).comp x hf

theorem DifferentiableAt.dist (hf : DifferentiableAt â„ f x) (hg : DifferentiableAt â„ g x)
    (hne : f x â‰  g x) : DifferentiableAt â„ (fun y => dist (f y) (g y)) x := by
  simp only [dist_eq_norm]; exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)

theorem Differentiable.norm_sq (hf : Differentiable â„ f) : Differentiable â„ fun y => â€–f yâ€– ^ 2 :=
  fun x => (hf x).norm_sq ğ•œ

theorem Differentiable.norm (hf : Differentiable â„ f) (h0 : âˆ€ x, f x â‰  0) :
    Differentiable â„ fun y => â€–f yâ€– := fun x => (hf x).norm ğ•œ (h0 x)

theorem Differentiable.dist (hf : Differentiable â„ f) (hg : Differentiable â„ g)
    (hne : âˆ€ x, f x â‰  g x) : Differentiable â„ fun y => dist (f y) (g y) := fun x =>
  (hf x).dist ğ•œ (hg x) (hne x)

theorem DifferentiableWithinAt.norm_sq (hf : DifferentiableWithinAt â„ f s x) :
    DifferentiableWithinAt â„ (fun y => â€–f yâ€– ^ 2) s x :=
  ((contDiffAt_id.norm_sq ğ•œ).differentiableAt le_rfl).comp_differentiableWithinAt x hf

theorem DifferentiableWithinAt.norm (hf : DifferentiableWithinAt â„ f s x) (h0 : f x â‰  0) :
    DifferentiableWithinAt â„ (fun y => â€–f yâ€–) s x :=
  ((contDiffAt_id.norm ğ•œ h0).differentiableAt le_rfl).comp_differentiableWithinAt x hf

theorem DifferentiableWithinAt.dist (hf : DifferentiableWithinAt â„ f s x)
    (hg : DifferentiableWithinAt â„ g s x) (hne : f x â‰  g x) :
    DifferentiableWithinAt â„ (fun y => dist (f y) (g y)) s x := by
  simp only [dist_eq_norm]
  exact (hf.sub hg).norm ğ•œ (sub_ne_zero.2 hne)

theorem DifferentiableOn.norm_sq (hf : DifferentiableOn â„ f s) :
    DifferentiableOn â„ (fun y => â€–f yâ€– ^ 2) s := fun x hx => (hf x hx).norm_sq ğ•œ

theorem DifferentiableOn.norm (hf : DifferentiableOn â„ f s) (h0 : âˆ€ x âˆˆ s, f x â‰  0) :
    DifferentiableOn â„ (fun y => â€–f yâ€–) s := fun x hx => (hf x hx).norm ğ•œ (h0 x hx)

theorem DifferentiableOn.dist (hf : DifferentiableOn â„ f s) (hg : DifferentiableOn â„ g s)
    (hne : âˆ€ x âˆˆ s, f x â‰  g x) : DifferentiableOn â„ (fun y => dist (f y) (g y)) s := fun x hx =>
  (hf x hx).dist ğ•œ (hg x hx) (hne x hx)

end

end DerivInner

section PiLike

/-! ### Convenience aliases of `PiLp` lemmas for `EuclideanSpace` -/

open ContinuousLinearMap

variable {ğ•œ Î¹ H : Type*} [RCLike ğ•œ] [NormedAddCommGroup H] [NormedSpace ğ•œ H] [Fintype Î¹]
  {f : H â†’ EuclideanSpace ğ•œ Î¹} {f' : H â†’L[ğ•œ] EuclideanSpace ğ•œ Î¹} {t : Set H} {y : H}

theorem differentiableWithinAt_euclidean :
    DifferentiableWithinAt ğ•œ f t y â†” âˆ€ i, DifferentiableWithinAt ğ•œ (fun x => f x i) t y :=
  differentiableWithinAt_piLp _

theorem differentiableAt_euclidean :
    DifferentiableAt ğ•œ f y â†” âˆ€ i, DifferentiableAt ğ•œ (fun x => f x i) y :=
  differentiableAt_piLp _

theorem differentiableOn_euclidean :
    DifferentiableOn ğ•œ f t â†” âˆ€ i, DifferentiableOn ğ•œ (fun x => f x i) t :=
  differentiableOn_piLp _

theorem differentiable_euclidean : Differentiable ğ•œ f â†” âˆ€ i, Differentiable ğ•œ fun x => f x i :=
  differentiable_piLp _

theorem hasStrictFDerivAt_euclidean :
    HasStrictFDerivAt f f' y â†”
      âˆ€ i, HasStrictFDerivAt (fun x => f x i) (PiLp.proj _ _ i âˆ˜L f') y :=
  hasStrictFDerivAt_piLp _

theorem hasFDerivWithinAt_euclidean :
    HasFDerivWithinAt f f' t y â†”
      âˆ€ i, HasFDerivWithinAt (fun x => f x i) (PiLp.proj _ _ i âˆ˜L f') t y :=
  hasFDerivWithinAt_piLp _

theorem contDiffWithinAt_euclidean {n : WithTop â„•âˆ} :
    ContDiffWithinAt ğ•œ n f t y â†” âˆ€ i, ContDiffWithinAt ğ•œ n (fun x => f x i) t y :=
  contDiffWithinAt_piLp _

theorem contDiffAt_euclidean {n : WithTop â„•âˆ} :
    ContDiffAt ğ•œ n f y â†” âˆ€ i, ContDiffAt ğ•œ n (fun x => f x i) y :=
  contDiffAt_piLp _

theorem contDiffOn_euclidean {n : WithTop â„•âˆ} :
    ContDiffOn ğ•œ n f t â†” âˆ€ i, ContDiffOn ğ•œ n (fun x => f x i) t :=
  contDiffOn_piLp _

theorem contDiff_euclidean {n : WithTop â„•âˆ} : ContDiff ğ•œ n f â†” âˆ€ i, ContDiff ğ•œ n fun x => f x i :=
  contDiff_piLp _

end PiLike

section DiffeomorphUnitBall

open Metric hiding mem_nhds_iff

variable {n : â„•âˆ} {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E]

theorem OpenPartialHomeomorph.contDiff_univUnitBall : ContDiff â„ n (univUnitBall : E â†’ E) := by
  suffices ContDiff â„ n fun x : E => (âˆš(1 + â€–xâ€– ^ 2 : â„))â»Â¹ from this.smul contDiff_id
  have h : âˆ€ x : E, (0 : â„) < (1 : â„) + â€–xâ€– ^ 2 := fun x => by positivity
  refine ContDiff.inv ?_ fun x => Real.sqrt_ne_zero'.mpr (h x)
  exact (contDiff_const.add <| contDiff_norm_sq â„).sqrt fun x => (h x).ne'

theorem OpenPartialHomeomorph.contDiffOn_univUnitBall_symm :
    ContDiffOn â„ n univUnitBall.symm (ball (0 : E) 1) := fun y hy â†¦ by
  apply ContDiffAt.contDiffWithinAt
  suffices ContDiffAt â„ n (fun y : E => (âˆš(1 - â€–yâ€– ^ 2 : â„))â»Â¹) y from this.smul contDiffAt_id
  have h : (0 : â„) < (1 : â„) - â€–(y : E)â€– ^ 2 := by
    rwa [mem_ball_zero_iff, â† _root_.abs_one, â† abs_norm, â† sq_lt_sq, one_pow, â† sub_pos] at hy
  refine ContDiffAt.inv ?_ (Real.sqrt_ne_zero'.mpr h)
  change ContDiffAt â„ n ((fun y â†¦ âˆš(y)) âˆ˜ fun y â†¦ (1 - â€–yâ€– ^ 2)) y
  refine (contDiffAt_sqrt h.ne').comp y ?_
  exact contDiffAt_const.sub (contDiff_norm_sq â„).contDiffAt

theorem Homeomorph.contDiff_unitBall : ContDiff â„ n fun x : E => (unitBall x : E) :=
  OpenPartialHomeomorph.contDiff_univUnitBall

namespace OpenPartialHomeomorph

variable {c : E} {r : â„}

theorem contDiff_unitBallBall (hr : 0 < r) : ContDiff â„ n (unitBallBall c r hr) :=
  (contDiff_id.const_smul _).add contDiff_const

theorem contDiff_unitBallBall_symm (hr : 0 < r) : ContDiff â„ n (unitBallBall c r hr).symm :=
  (contDiff_id.sub contDiff_const).const_smul _

theorem contDiff_univBall : ContDiff â„ n (univBall c r) := by
  unfold univBall; split_ifs with h
  Â· exact (contDiff_unitBallBall h).comp contDiff_univUnitBall
  Â· exact contDiff_id.add contDiff_const

theorem contDiffOn_univBall_symm :
    ContDiffOn â„ n (univBall c r).symm (ball c r) := by
  unfold univBall; split_ifs with h
  Â· refine contDiffOn_univUnitBall_symm.comp (contDiff_unitBallBall_symm h).contDiffOn ?_
    rw [â† unitBallBall_source c r h, â† unitBallBall_target c r h]
    apply OpenPartialHomeomorph.symm_mapsTo
  Â· exact contDiffOn_id.sub contDiffOn_const

end OpenPartialHomeomorph

end DiffeomorphUnitBall
