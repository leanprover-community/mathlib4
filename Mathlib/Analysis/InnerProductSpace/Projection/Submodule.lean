/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
module

public import Mathlib.Analysis.InnerProductSpace.Projection.Basic

/-!
# Subspaces associated with orthogonal projections

Here, the orthogonal projection is used to prove a series of more subtle lemmas about the
orthogonal complement of subspaces of `E` (the orthogonal complement itself was
defined in `Mathlib/Analysis/InnerProductSpace/Orthogonal.lean`) such that they admit
orthogonal projections; the lemma
`Submodule.sup_orthogonal_of_hasOrthogonalProjection`,
stating that for a subspace `K` of `E` such that `K` admits an orthogonal projection we have
`K âŠ” Ká—® = âŠ¤`, is a typical example.
-/

@[expose] public section

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (K : Submodule ğ•œ E)

namespace Submodule

/-- If `Kâ‚` admits an orthogonal projection and is contained in `Kâ‚‚`,
then `Kâ‚` and `Kâ‚á—® âŠ“ Kâ‚‚` span `Kâ‚‚`. -/
theorem sup_orthogonal_inf_of_hasOrthogonalProjection {Kâ‚ Kâ‚‚ : Submodule ğ•œ E} (h : Kâ‚ â‰¤ Kâ‚‚)
    [Kâ‚.HasOrthogonalProjection] : Kâ‚ âŠ” Kâ‚á—® âŠ“ Kâ‚‚ = Kâ‚‚ := by
  ext x
  rw [Submodule.mem_sup]
  let v : Kâ‚ := orthogonalProjection Kâ‚ x
  have hvm : x - v âˆˆ Kâ‚á—® := sub_starProjection_mem_orthogonal x
  constructor
  Â· rintro âŸ¨y, hy, z, hz, rflâŸ©
    exact Kâ‚‚.add_mem (h hy) hz.2
  Â· exact fun hx => âŸ¨v, v.prop, x - v, âŸ¨hvm, Kâ‚‚.sub_mem hx (h v.prop)âŸ©, add_sub_cancel _ _âŸ©

@[deprecated (since := "2025-07-27")] alias sup_orthogonal_inf_of_completeSpace :=
  sup_orthogonal_inf_of_hasOrthogonalProjection

variable {K} in
/-- If `K` admits an orthogonal projection, then `K` and `Ká—®` span the whole space. -/
theorem sup_orthogonal_of_hasOrthogonalProjection [K.HasOrthogonalProjection] : K âŠ” Ká—® = âŠ¤ := by
  convert Submodule.sup_orthogonal_inf_of_hasOrthogonalProjection (le_top : K â‰¤ âŠ¤) using 2
  simp

@[deprecated (since := "2025-07-27")] alias sup_orthogonal_of_completeSpace :=
  sup_orthogonal_of_hasOrthogonalProjection

/-- If `K` admits an orthogonal projection, then the orthogonal complement of its orthogonal
complement is itself. -/
@[simp]
theorem orthogonal_orthogonal [K.HasOrthogonalProjection] : Ká—®á—® = K := by
  ext v
  constructor
  Â· obtain âŸ¨y, hy, z, hz, rflâŸ© := K.exists_add_mem_mem_orthogonal v
    intro hv
    have hz' : z = 0 := by
      have hyz : âŸªz, yâŸ« = 0 := by simp [hz y hy, inner_eq_zero_symm]
      simpa [inner_add_right, hyz] using hv z hz
    simp [hy, hz']
  Â· intro hv w hw
    rw [inner_eq_zero_symm]
    exact hw v hv

lemma orthogonal_le_orthogonal_iff {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€á—® â‰¤ Kâ‚á—® â†” Kâ‚ â‰¤ Kâ‚€ :=
  âŸ¨fun h â†¦ by simpa using orthogonal_le h, orthogonal_leâŸ©

lemma orthogonal_le_iff_orthogonal_le {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€á—® â‰¤ Kâ‚ â†” Kâ‚á—® â‰¤ Kâ‚€ := by
  rw [â† orthogonal_le_orthogonal_iff, orthogonal_orthogonal]

lemma le_orthogonal_iff_le_orthogonal {Kâ‚€ Kâ‚ : Submodule ğ•œ E} [Kâ‚€.HasOrthogonalProjection]
    [Kâ‚.HasOrthogonalProjection] : Kâ‚€ â‰¤ Kâ‚á—® â†” Kâ‚ â‰¤ Kâ‚€á—® := by
  rw [â† orthogonal_le_orthogonal_iff, orthogonal_orthogonal]

/-- In a Hilbert space, the orthogonal complement of the orthogonal complement of a subspace `K`
is the topological closure of `K`.

Note that the completeness assumption is necessary. Let `E` be the space `â„• â†’â‚€ â„` with inner space
structure inherited from `PiLp 2 (fun _ : â„• â†¦ â„)`. Let `K` be the subspace of sequences with the sum
of all elements equal to zero. Then `Ká—® = âŠ¥`, `Ká—®á—® = âŠ¤`. -/
theorem orthogonal_orthogonal_eq_closure [CompleteSpace E] :
    Ká—®á—® = K.topologicalClosure := by
  refine le_antisymm ?_ ?_
  Â· convert Submodule.orthogonal_orthogonal_monotone K.le_topologicalClosure using 1
    rw [K.topologicalClosure.orthogonal_orthogonal]
  Â· exact K.topologicalClosure_minimal K.le_orthogonal_orthogonal Ká—®.isClosed_orthogonal

variable {K}

/-- If `K` admits an orthogonal projection, `K` and `Ká—®` are complements of each other. -/
theorem isCompl_orthogonal_of_hasOrthogonalProjection [K.HasOrthogonalProjection] : IsCompl K Ká—® :=
  âŸ¨K.orthogonal_disjoint, codisjoint_iff.2 Submodule.sup_orthogonal_of_hasOrthogonalProjectionâŸ©

@[deprecated (since := "2025-07-27")] alias isCompl_orthogonal_of_completeSpace :=
  isCompl_orthogonal_of_hasOrthogonalProjection

@[simp]
theorem orthogonalComplement_eq_orthogonalComplement {L : Submodule ğ•œ E} [K.HasOrthogonalProjection]
    [L.HasOrthogonalProjection] : Ká—® = Lá—® â†” K = L :=
  âŸ¨fun h â†¦ by simpa using congr(Submodule.orthogonal $(h)),
    fun h â†¦ congr(Submodule.orthogonal $(h))âŸ©

@[simp]
theorem orthogonal_eq_bot_iff [K.HasOrthogonalProjection] : Ká—® = âŠ¥ â†” K = âŠ¤ := by
  refine âŸ¨?_, fun h => by rw [h, Submodule.top_orthogonal_eq_bot]âŸ©
  intro h
  have : K âŠ” Ká—® = âŠ¤ := Submodule.sup_orthogonal_of_hasOrthogonalProjection
  rwa [h, sup_comm, bot_sup_eq] at this

open Topology Finsupp RCLike Real Filter

/-- Given a monotone family `U` of complete submodules of `E` and a fixed `x : E`,
the orthogonal projection of `x` on `U i` tends to the orthogonal projection of `x` on
`(â¨† i, U i).topologicalClosure` along `atTop`. -/
theorem starProjection_tendsto_closure_iSup {Î¹ : Type*} [Preorder Î¹]
    (U : Î¹ â†’ Submodule ğ•œ E) [âˆ€ i, (U i).HasOrthogonalProjection]
    [(â¨† i, U i).topologicalClosure.HasOrthogonalProjection] (hU : Monotone U) (x : E) :
    Filter.Tendsto (fun i => (U i).starProjection x) atTop
      (ğ“ ((â¨† i, U i).topologicalClosure.starProjection x)) := by
  refine .of_neBot_imp fun h â†¦ ?_
  cases atTop_neBot_iff.mp h
  let y := (â¨† i, U i).topologicalClosure.starProjection x
  have proj_x : âˆ€ i, (U i).orthogonalProjection x = (U i).orthogonalProjection y := fun i =>
    (orthogonalProjection_starProjection_of_le
        ((le_iSup U i).trans (iSup U).le_topologicalClosure) _).symm
  suffices âˆ€ Îµ > 0, âˆƒ I, âˆ€ i â‰¥ I, â€–(U i).starProjection y - yâ€– < Îµ by
    simpa only [starProjection_apply, proj_x, NormedAddCommGroup.tendsto_atTop] using this
  intro Îµ hÎµ
  obtain âŸ¨a, ha, hayâŸ© : âˆƒ a âˆˆ â¨† i, U i, dist y a < Îµ := by
    have y_mem : y âˆˆ (â¨† i, U i).topologicalClosure := Submodule.coe_mem _
    rw [â† SetLike.mem_coe, Submodule.topologicalClosure_coe, Metric.mem_closure_iff] at y_mem
    exact y_mem Îµ hÎµ
  rw [dist_eq_norm] at hay
  obtain âŸ¨I, hIâŸ© : âˆƒ I, a âˆˆ U I := by rwa [Submodule.mem_iSup_of_directed _ hU.directed_le] at ha
  refine âŸ¨I, fun i (hi : I â‰¤ i) => ?_âŸ©
  rw [norm_sub_rev, starProjection_minimal]
  refine lt_of_le_of_lt ?_ hay
  change _ â‰¤ â€–y - (âŸ¨a, hU hi hIâŸ© : U i)â€–
  exact ciInf_le âŸ¨0, Set.forall_mem_range.mpr fun _ => norm_nonneg _âŸ© _

/-- Given a monotone family `U` of complete submodules of `E` with dense span supremum,
and a fixed `x : E`, the orthogonal projection of `x` on `U i` tends to `x` along `at_top`. -/
theorem starProjection_tendsto_self {Î¹ : Type*} [Preorder Î¹]
    (U : Î¹ â†’ Submodule ğ•œ E) [âˆ€ t, (U t).HasOrthogonalProjection] (hU : Monotone U) (x : E)
    (hU' : âŠ¤ â‰¤ (â¨† t, U t).topologicalClosure) :
    Filter.Tendsto (fun t => (U t).starProjection x) atTop (ğ“ x) := by
  have : (â¨† i, U i).topologicalClosure.HasOrthogonalProjection := by
    rw [top_unique hU']
    infer_instance
  convert starProjection_tendsto_closure_iSup U hU x
  rw [eq_comm, starProjection_eq_self_iff, top_unique hU']
  trivial

/-- The orthogonal complement satisfies `Ká—®á—®á—® = Ká—®`. -/
theorem triorthogonal_eq_orthogonal : Ká—®á—®á—® = Ká—® :=
  (orthogonal_gc ğ•œ E).u_l_u_eq_u K

/-- The closure of `K` is the full space iff `Ká—®` is trivial. -/
theorem topologicalClosure_eq_top_iff [CompleteSpace E] :
    K.topologicalClosure = âŠ¤ â†” Ká—® = âŠ¥ := by
  rw [â† K.orthogonal_orthogonal_eq_closure]
  constructor <;> intro h
  Â· rw [â† Submodule.triorthogonal_eq_orthogonal, h, Submodule.top_orthogonal_eq_bot]
  Â· rw [h, Submodule.bot_orthogonal_eq_top]

theorem orthogonalProjection_apply_eq_linearProjOfIsCompl [K.HasOrthogonalProjection] (x : E) :
    K.orthogonalProjection x =
      K.linearProjOfIsCompl _ Submodule.isCompl_orthogonal_of_hasOrthogonalProjection x := by
  have : IsCompl K Ká—® := Submodule.isCompl_orthogonal_of_hasOrthogonalProjection
  conv_lhs => rw [â† IsCompl.projection_add_projection_eq_self this x]
  simp_rw [IsCompl.projection_apply]
  rw [map_add, orthogonalProjection_mem_subspace_eq_self,
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero (Submodule.coe_mem _), add_zero]

@[deprecated (since := "2025-12-26")] alias orthogonalProjection_eq_linearProjOfIsCompl :=
  orthogonalProjection_apply_eq_linearProjOfIsCompl

theorem toLinearMap_orthogonalProjection_eq_linearProjOfIsCompl [K.HasOrthogonalProjection] :
    (K.orthogonalProjection : E â†’â‚—[ğ•œ] K) =
      K.linearProjOfIsCompl _ Submodule.isCompl_orthogonal_of_hasOrthogonalProjection :=
  LinearMap.ext orthogonalProjection_apply_eq_linearProjOfIsCompl

@[deprecated (since := "2025-12-26")] alias orthogonalProjection_coe_eq_linearProjOfIsCompl :=
  toLinearMap_orthogonalProjection_eq_linearProjOfIsCompl

open Submodule in
theorem toLinearMap_starProjection_eq_isComplProjection [K.HasOrthogonalProjection] :
    K.starProjection.toLinearMap = K.isCompl_orthogonal_of_hasOrthogonalProjection.projection := by
  simp [starProjection, toLinearMap_orthogonalProjection_eq_linearProjOfIsCompl, IsCompl.projection]

@[deprecated (since := "2025-12-26")] alias starProjection_coe_eq_isCompl_projection :=
  toLinearMap_starProjection_eq_isComplProjection

open Submodule in
theorem starProjection_apply_eq_isComplProjection [K.HasOrthogonalProjection] (x : E) :
    K.starProjection x = K.isCompl_orthogonal_of_hasOrthogonalProjection.projection x :=
  congr($toLinearMap_starProjection_eq_isComplProjection x)

end Submodule

namespace Dense

open Submodule

variable {K} {x y : E}

theorem eq_zero_of_inner_left (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸªx, vâŸ« = 0) : x = 0 := by
  have : (âŸªx, Â·âŸ«) = 0 := (continuous_const.inner continuous_id).ext_on
    hK continuous_const (Subtype.forall.1 h)
  simpa using congr_fun this x

theorem eq_zero_of_mem_orthogonal (hK : Dense (K : Set E)) (h : x âˆˆ Ká—®) : x = 0 :=
  eq_zero_of_inner_left hK fun v â†¦ (mem_orthogonal' _ _).1 h _ v.2

/-- If `S` is dense and `x - y âˆˆ Ká—®`, then `x = y`. -/
theorem eq_of_sub_mem_orthogonal (hK : Dense (K : Set E)) (h : x - y âˆˆ Ká—®) : x = y :=
  sub_eq_zero.1 <| eq_zero_of_mem_orthogonal hK h

theorem eq_of_inner_left (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸªx, vâŸ« = âŸªy, vâŸ«) : x = y :=
  hK.eq_of_sub_mem_orthogonal (Submodule.sub_mem_orthogonal_of_inner_left h)

theorem eq_of_inner_right (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸª(v : E), xâŸ« = âŸª(v : E), yâŸ«) :
    x = y :=
  hK.eq_of_sub_mem_orthogonal (Submodule.sub_mem_orthogonal_of_inner_right h)

theorem eq_zero_of_inner_right (hK : Dense (K : Set E)) (h : âˆ€ v : K, âŸª(v : E), xâŸ« = 0) : x = 0 :=
  hK.eq_of_inner_right fun v => by rw [inner_zero_right, h v]

end Dense

namespace ClosedSubmodule

instance (K : ClosedSubmodule ğ•œ E) : IsClosed K.carrier := K.isClosed'

instance (K : Submodule ğ•œ E) [CompleteSpace E] [IsClosed K.carrier] : CompleteSpace K := by
  have : CompleteSpace K.carrier := by infer_instance
  exact this

@[simp]
theorem eq_orthogonal_orthogonal (K : ClosedSubmodule ğ•œ E) [K.HasOrthogonalProjection] :
    (Ká—®)á—® = K := by ext x; simp

theorem eq_iff_orthogonal_eq_orthogonal (Kâ‚ Kâ‚‚ : ClosedSubmodule ğ•œ E) [Kâ‚.HasOrthogonalProjection]
    [Kâ‚‚.HasOrthogonalProjection] : Kâ‚á—® = Kâ‚‚á—® â†” Kâ‚ = Kâ‚‚ := by
  constructor
  Â· intro h
    rw [â† eq_orthogonal_orthogonal Kâ‚, â† eq_orthogonal_orthogonal Kâ‚‚]
    exact congrArg orthogonal h
  Â· intro h
    exact congrArg orthogonal h

/-- The sup of two orthogonal subspaces equals the subspace orthogonal
to the inf. -/
theorem sup_orthogonal (Kâ‚ Kâ‚‚ : ClosedSubmodule ğ•œ E) [CompleteSpace E] :
    Kâ‚á—® âŠ” Kâ‚‚á—® = (Kâ‚ âŠ“ Kâ‚‚)á—® := by
  rw [â† eq_orthogonal_orthogonal (Kâ‚á—® âŠ” Kâ‚‚á—®), â† inf_orthogonal]
  simp

end ClosedSubmodule
