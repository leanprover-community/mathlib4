/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Projection.Basic

/-!
# Reflection

A linear isometry equivalence `K.reflection : E â‰ƒâ‚—áµ¢[ğ•œ] E` in constructed, by choosing
for each `u : E`, `K.reflection u = 2 â€¢ K.starProjection u - u`.
-/

noncomputable section

namespace Submodule

section reflection

open Submodule RCLike

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

variable (K : Submodule ğ•œ E)
variable [K.HasOrthogonalProjection]

/-- Auxiliary definition for `reflection`: the reflection as a linear equivalence. -/
def reflectionLinearEquiv : E â‰ƒâ‚—[ğ•œ] E :=
  LinearEquiv.ofInvolutive
    (2 â€¢ (K.starProjection.toLinearMap) - LinearMap.id) fun x â†¦ by
    simp [two_smul, starProjection_eq_self_iff.mpr]

/-- Reflection in a complete subspace of an inner product space.  The word "reflection" is
sometimes understood to mean specifically reflection in a codimension-one subspace, and sometimes
more generally to cover operations such as reflection in a point.  The definition here, of
reflection in a subspace, is a more general sense of the word that includes both those common
cases. -/
def reflection : E â‰ƒâ‚—áµ¢[ğ•œ] E :=
  { K.reflectionLinearEquiv with
    norm_map' := by
      intro x
      let w : K := K.orthogonalProjection x
      let v := x - w
      have : âŸªv, wâŸ« = 0 := starProjection_inner_eq_zero x w w.2
      convert norm_sub_eq_norm_add this using 2
      Â· dsimp [reflectionLinearEquiv, v, w]
        abel
      Â· simp only [v, add_sub_cancel] }

variable {K}

/-- The result of reflecting. -/
theorem reflection_apply (p : E) : K.reflection p = 2 â€¢ K.starProjection p - p :=
  rfl

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_symm : K.reflection.symm = K.reflection :=
  rfl

/-- Reflection is its own inverse. -/
@[simp]
theorem reflection_inv : K.reflectionâ»Â¹ = K.reflection :=
  rfl

variable (K)

/-- Reflecting twice in the same subspace. -/
@[simp]
theorem reflection_reflection (p : E) : K.reflection (K.reflection p) = p :=
  K.reflection.left_inv p

/-- Reflection is involutive. -/
theorem reflection_involutive : Function.Involutive K.reflection :=
  K.reflection_reflection

/-- Reflection is involutive. -/
@[simp]
theorem reflection_trans_reflection :
    K.reflection.trans K.reflection = LinearIsometryEquiv.refl ğ•œ E :=
  LinearIsometryEquiv.ext <| reflection_involutive K

/-- Reflection is involutive. -/
@[simp]
theorem reflection_mul_reflection : K.reflection * K.reflection = 1 :=
  reflection_trans_reflection _
theorem reflection_orthogonal_apply (v : E) : Ká—®.reflection v = -K.reflection v := by
  simp [reflection_apply]; abel

theorem reflection_orthogonal : Ká—®.reflection = .trans K.reflection (.neg _) := by
  ext; apply reflection_orthogonal_apply

variable {K}

theorem reflection_singleton_apply (u v : E) :
    reflection (ğ•œ âˆ™ u) v = 2 â€¢ (âŸªu, vâŸ« / ((â€–uâ€– : ğ•œ) ^ 2)) â€¢ u - v := by
  rw [reflection_apply, starProjection_singleton, ofReal_pow]

/-- A point is its own reflection if and only if it is in the subspace. -/
theorem reflection_eq_self_iff (x : E) : K.reflection x = x â†” x âˆˆ K := by
  rw [â† starProjection_eq_self_iff, reflection_apply, sub_eq_iff_eq_add', â† two_smul ğ•œ,
    two_smul â„•, â† two_smul ğ•œ]
  refine (smul_right_injective E ?_).eq_iff
  exact two_ne_zero

theorem reflection_mem_subspace_eq_self {x : E} (hx : x âˆˆ K) : K.reflection x = x :=
  (reflection_eq_self_iff x).mpr hx

/-- Reflection in the `Submodule.map` of a subspace. -/
theorem reflection_map_apply {E E' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (K : Submodule ğ•œ E)
    [K.HasOrthogonalProjection] (x : E') :
    reflection (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')) x = f (K.reflection (f.symm x)) := by
  simp [two_smul, reflection_apply, starProjection_map_apply f K x]

/-- Reflection in the `Submodule.map` of a subspace. -/
theorem reflection_map {E E' : Type*} [NormedAddCommGroup E] [NormedAddCommGroup E']
    [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') (K : Submodule ğ•œ E)
    [K.HasOrthogonalProjection] :
    reflection (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')) = f.symm.trans (K.reflection.trans f) :=
  LinearIsometryEquiv.ext <| reflection_map_apply f K

/-- Reflection through the trivial subspace {0} is just negation. -/
@[simp]
theorem reflection_bot : reflection (âŠ¥ : Submodule ğ•œ E) = LinearIsometryEquiv.neg ğ•œ := by
  ext; simp [reflection_apply]

/-- The reflection in `K` of an element of `Ká—®` is its negation. -/
theorem reflection_mem_subspace_orthogonalComplement_eq_neg {v : E}
    (hv : v âˆˆ Ká—®) : K.reflection v = -v := by
  simp [starProjection_apply, reflection_apply,
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv]

/-- The reflection in `Ká—®` of an element of `K` is its negation. -/
theorem reflection_mem_subspace_orthogonal_precomplement_eq_neg {v : E}
    (hv : v âˆˆ K) : Ká—®.reflection v = -v :=
  reflection_mem_subspace_orthogonalComplement_eq_neg (K.le_orthogonal_orthogonal hv)

/-- The reflection in `(ğ•œ âˆ™ v)á—®` of `v` is `-v`. -/
theorem reflection_orthogonalComplement_singleton_eq_neg (v : E) : reflection (ğ•œ âˆ™ v)á—® v = -v :=
  reflection_mem_subspace_orthogonal_precomplement_eq_neg (Submodule.mem_span_singleton_self v)

theorem reflection_sub {v w : F} (h : â€–vâ€– = â€–wâ€–) : reflection (â„ âˆ™ (v - w))á—® v = w := by
  set R : F â‰ƒâ‚—áµ¢[â„] F := reflection (â„ âˆ™ v - w)á—®
  suffices R v + R v = w + w by
    apply smul_right_injective F (by simp : (2 : â„) â‰  0)
    simpa [two_smul] using this
  have hâ‚ : R (v - w) = -(v - w) := reflection_orthogonalComplement_singleton_eq_neg (v - w)
  have hâ‚‚ : R (v + w) = v + w := by
    apply reflection_mem_subspace_eq_self
    rw [Submodule.mem_orthogonal_singleton_iff_inner_left]
    rw [real_inner_add_sub_eq_zero_iff]
    exact h
  convert congr_argâ‚‚ (Â· + Â·) hâ‚‚ hâ‚ using 1
  Â· simp
  Â· abel

end reflection

end Submodule

end
