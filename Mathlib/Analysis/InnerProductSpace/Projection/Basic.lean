/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, Fr√©d√©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Projection.Minimal
import Mathlib.Analysis.InnerProductSpace.Symmetric
import Mathlib.Analysis.RCLike.Lemmas

/-!
# The orthogonal projection

Given a nonempty subspace `K` of an inner product space `E` such that `K`
admits an orthogonal projection, this file constructs
`K.orthogonalProjection : E ‚ÜíL[ùïú] K`, the orthogonal projection of `E` onto `K`. This map
satisfies: for any point `u` in `E`, the point `v = K.orthogonalProjection u` in `K` minimizes the
distance `‚Äñu - v‚Äñ` to `u`.

This file also defines `K.starProjection : E ‚ÜíL[ùïú] E` which is the
orthogonal projection of `E` onto `K` but as a map from `E` to `E` instead of `E` to `K`.

Basic API for `orthogonalProjection` and `starProjection` is developed.

## References

The orthogonal projection construction is adapted from
* [Cl√©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
* [Cl√©ment & Martin, *A Coq formal proof of the Lax‚ÄìMilgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

variable {ùïú E F : Type*} [RCLike ùïú]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ùïú E] [InnerProductSpace ‚Ñù F]

local notation "‚ü™" x ", " y "‚ü´" => inner ùïú x y
local notation "absR" => @abs ‚Ñù _ _

namespace Submodule

/-- A subspace `K : Submodule ùïú E` has an orthogonal projection if every vector `v : E` admits an
orthogonal projection to `K`. -/
class HasOrthogonalProjection (K : Submodule ùïú E) : Prop where
  exists_orthogonal (v : E) : ‚àÉ w ‚àà K, v - w ‚àà K·óÆ

variable (K : Submodule ùïú E)

instance (priority := 100) HasOrthogonalProjection.ofCompleteSpace [CompleteSpace K] :
    K.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases K.exists_norm_eq_iInf_of_complete_subspace (completeSpace_coe_iff_isComplete.mp ‚Äπ_‚Ä∫) v
      with ‚ü®w, hwK, hw‚ü©
    refine ‚ü®w, hwK, (K.mem_orthogonal' _).2 ?_‚ü©
    rwa [‚Üê K.norm_eq_iInf_iff_inner_eq_zero hwK]

instance [K.HasOrthogonalProjection] : K·óÆ.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) v with ‚ü®w, hwK, hw‚ü©
    refine ‚ü®_, hw, ?_‚ü©
    rw [sub_sub_cancel]
    exact K.le_orthogonal_orthogonal hwK

instance HasOrthogonalProjection.map_linearIsometryEquiv [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ùïú E'] (f : E ‚âÉ‚Çó·µ¢[ùïú] E') :
    (K.map (f.toLinearEquiv : E ‚Üí‚Çó[ùïú] E')).HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) (f.symm v) with ‚ü®w, hwK, hw‚ü©
    refine ‚ü®f w, Submodule.mem_map_of_mem hwK, Set.forall_mem_image.2 fun u hu ‚Ü¶ ?_‚ü©
    erw [‚Üê f.symm.inner_map_map, f.symm_apply_apply, map_sub, f.symm_apply_apply, hw u hu]

instance HasOrthogonalProjection.map_linearIsometryEquiv' [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ùïú E'] (f : E ‚âÉ‚Çó·µ¢[ùïú] E') :
    (K.map f.toLinearIsometry).HasOrthogonalProjection :=
  HasOrthogonalProjection.map_linearIsometryEquiv K f

instance : (‚ä§ : Submodule ùïú E).HasOrthogonalProjection := ‚ü®fun v ‚Ü¶ ‚ü®v, trivial, by simp‚ü©‚ü©

noncomputable section

section orthogonalProjection

variable [K.HasOrthogonalProjection]

/-- The orthogonal projection onto a complete subspace, as an
unbundled function. This definition is only intended for use in
setting up the bundled version `orthogonalProjection` and should not
be used once that is defined. -/
def orthogonalProjectionFn (v : E) :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose

variable {K}

/-- The unbundled orthogonal projection is in the given subspace.
This lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_mem (v : E) : K.orthogonalProjectionFn v ‚àà K :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.left

/-- The characterization of the unbundled orthogonal projection.  This
lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_inner_eq_zero (v : E) :
    ‚àÄ w ‚àà K, ‚ü™v - K.orthogonalProjectionFn v, w‚ü´ = 0 :=
  (K.mem_orthogonal' _).1 (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.right

/-- The unbundled orthogonal projection is the unique point in `K`
with the orthogonality property. This lemma is only intended for use
in setting up the bundled version and should not be used once that is
defined. -/
theorem eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero {u v : E} (hvm : v ‚àà K)
    (hvo : ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´ = 0) : K.orthogonalProjectionFn u = v := by
  rw [‚Üê sub_eq_zero, ‚Üê @inner_self_eq_zero ùïú]
  have hvs : K.orthogonalProjectionFn u - v ‚àà K :=
    Submodule.sub_mem K (orthogonalProjectionFn_mem u) hvm
  have huo : ‚ü™u - K.orthogonalProjectionFn u, K.orthogonalProjectionFn u - v‚ü´ = 0 :=
    orthogonalProjectionFn_inner_eq_zero u _ hvs
  have huv : ‚ü™u - v, K.orthogonalProjectionFn u - v‚ü´ = 0 := hvo _ hvs
  have houv : ‚ü™u - v - (u - K.orthogonalProjectionFn u), K.orthogonalProjectionFn u - v‚ü´ = 0 := by
    rw [inner_sub_left, huo, huv, sub_zero]
  rwa [sub_sub_sub_cancel_left] at houv

variable (K)

theorem orthogonalProjectionFn_norm_sq (v : E) :
    ‚Äñv‚Äñ * ‚Äñv‚Äñ =
      ‚Äñv - K.orthogonalProjectionFn v‚Äñ * ‚Äñv - K.orthogonalProjectionFn v‚Äñ +
        ‚ÄñK.orthogonalProjectionFn v‚Äñ * ‚ÄñK.orthogonalProjectionFn v‚Äñ := by
  set p := K.orthogonalProjectionFn v
  have h' : ‚ü™v - p, p‚ü´ = 0 :=
    orthogonalProjectionFn_inner_eq_zero _ _ (orthogonalProjectionFn_mem v)
  convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (v - p) p h' using 2 <;> simp

/-- The orthogonal projection onto a complete subspace. -/
def orthogonalProjection : E ‚ÜíL[ùïú] K :=
  LinearMap.mkContinuous
    { toFun := fun v => ‚ü®K.orthogonalProjectionFn v, orthogonalProjectionFn_mem v‚ü©
      map_add' := fun x y => by
        have hm : K.orthogonalProjectionFn x + K.orthogonalProjectionFn y ‚àà K :=
          Submodule.add_mem K (orthogonalProjectionFn_mem x) (orthogonalProjectionFn_mem y)
        have ho :
          ‚àÄ w ‚àà K, ‚ü™x + y - (K.orthogonalProjectionFn x + K.orthogonalProjectionFn y), w‚ü´ = 0 := by
          intro w hw
          rw [add_sub_add_comm, inner_add_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            orthogonalProjectionFn_inner_eq_zero _ w hw, add_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho]
      map_smul' := fun c x => by
        have hm : c ‚Ä¢ K.orthogonalProjectionFn x ‚àà K :=
          Submodule.smul_mem K _ (orthogonalProjectionFn_mem x)
        have ho : ‚àÄ w ‚àà K, ‚ü™c ‚Ä¢ x - c ‚Ä¢ K.orthogonalProjectionFn x, w‚ü´ = 0 := by
          intro w hw
          rw [‚Üê smul_sub, inner_smul_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            mul_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho] }
    1 fun x => by
    simp only [one_mul, LinearMap.coe_mk]
    refine le_of_pow_le_pow_left‚ÇÄ two_ne_zero (norm_nonneg _) ?_
    change ‚ÄñK.orthogonalProjectionFn x‚Äñ ^ 2 ‚â§ ‚Äñx‚Äñ ^ 2
    nlinarith [K.orthogonalProjectionFn_norm_sq x]

variable {K}

@[simp]
theorem orthogonalProjectionFn_eq (v : E) :
    K.orthogonalProjectionFn v = (K.orthogonalProjection v : E) :=
  rfl

/-- The orthogonal projection onto a subspace as a map from the full space to itself,
as opposed to `Submodule.orthogonalProjection`, which maps into the subtype. This
version is important as it satisfies `IsStarProjection`. -/
def starProjection (U : Submodule ùïú E) [U.HasOrthogonalProjection] :
    E ‚ÜíL[ùïú] E := U.subtypeL ‚àòL U.orthogonalProjection

lemma starProjection_apply (U : Submodule ùïú E) [U.HasOrthogonalProjection] (v : E) :
    U.starProjection v = U.orthogonalProjection v := rfl

@[simp]
lemma coe_orthogonalProjection_apply (U : Submodule ùïú E) [U.HasOrthogonalProjection] (v : E) :
     U.orthogonalProjection v = U.starProjection v := rfl

@[simp]
lemma starProjection_apply_mem (U : Submodule ùïú E) [U.HasOrthogonalProjection] (x : E) :
    U.starProjection x ‚àà U := by
  simp only [starProjection_apply, SetLike.coe_mem]

/-- The characterization of the orthogonal projection. -/
@[simp]
theorem starProjection_inner_eq_zero (v : E) :
    ‚àÄ w ‚àà K, ‚ü™v - K.starProjection v, w‚ü´ = 0 :=
  orthogonalProjectionFn_inner_eq_zero v

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_inner_eq_zero :=
  starProjection_inner_eq_zero

/-- The difference of `v` from its orthogonal projection onto `K` is in `K·óÆ`. -/
@[simp]
theorem sub_starProjection_mem_orthogonal (v : E) : v - K.starProjection v ‚àà K·óÆ := by
  intro w hw
  rw [inner_eq_zero_symm]
  exact starProjection_inner_eq_zero _ _ hw

@[deprecated (since := "2025-07-07")] alias sub_orthogonalProjection_mem_orthogonal :=
  sub_starProjection_mem_orthogonal

/-- The orthogonal projection is the unique point in `K` with the
orthogonality property. -/
theorem eq_starProjection_of_mem_of_inner_eq_zero {u v : E} (hvm : v ‚àà K)
    (hvo : ‚àÄ w ‚àà K, ‚ü™u - v, w‚ü´ = 0) : K.starProjection u = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hvm hvo

@[deprecated (since := "2025-07-07")] alias eq_orthogonalProjection_of_mem_of_inner_eq_zero :=
  eq_starProjection_of_mem_of_inner_eq_zero

/-- A point in `K` with the orthogonality property (here characterized in terms of `K·óÆ`) must be the
orthogonal projection. -/
theorem eq_starProjection_of_mem_orthogonal {u v : E} (hv : v ‚àà K)
    (hvo : u - v ‚àà K·óÆ) : K.starProjection u = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hv <| (Submodule.mem_orthogonal' _ _).1 hvo

@[deprecated (since := "2025-07-07")] alias eq_orthogonalProjection_of_mem_orthogonal :=
  eq_starProjection_of_mem_orthogonal

/-- A point in `K` with the orthogonality property (here characterized in terms of `K·óÆ`) must be the
orthogonal projection. -/
theorem eq_starProjection_of_mem_orthogonal' {u v z : E}
    (hv : v ‚àà K) (hz : z ‚àà K·óÆ) (hu : u = v + z) : K.starProjection u = v :=
  eq_starProjection_of_mem_orthogonal hv (by simpa [hu] )

@[deprecated (since := "2025-07-07")] alias eq_orthogonalProjection_of_mem_orthogonal' :=
  eq_starProjection_of_mem_orthogonal'

@[simp]
theorem starProjection_orthogonal_val (u : E) :
    K·óÆ.starProjection u = u - K.starProjection u :=
  eq_starProjection_of_mem_orthogonal' (sub_starProjection_mem_orthogonal _)
    (K.le_orthogonal_orthogonal (K.orthogonalProjection u).2) <| (sub_add_cancel _ _).symm

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_orthogonal_val :=
  starProjection_orthogonal_val

theorem orthogonalProjection_orthogonal (u : E) :
    K·óÆ.orthogonalProjection u =
      ‚ü®u - K.starProjection u, sub_starProjection_mem_orthogonal _‚ü© :=
  Subtype.eq <| starProjection_orthogonal_val _

lemma starProjection_orthogonal (U : Submodule ùïú E) [U.HasOrthogonalProjection] :
    U·óÆ.starProjection = ContinuousLinearMap.id ùïú E - U.starProjection := by
  ext
  simp only [starProjection, ContinuousLinearMap.comp_apply,
    orthogonalProjection_orthogonal]
  simp

lemma starProjection_orthogonal' (U : Submodule ùïú E) [U.HasOrthogonalProjection] :
    U·óÆ.starProjection = 1 - U.starProjection := starProjection_orthogonal U

/-- The orthogonal projection of `y` on `U` minimizes the distance `‚Äñy - x‚Äñ` for `x ‚àà U`. -/
theorem starProjection_minimal {U : Submodule ùïú E} [U.HasOrthogonalProjection] (y : E) :
    ‚Äñy - U.starProjection y‚Äñ = ‚®Ö x : U, ‚Äñy - x‚Äñ := by
  rw [starProjection_apply, U.norm_eq_iInf_iff_inner_eq_zero (Submodule.coe_mem _)]
  exact starProjection_inner_eq_zero _

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_minimal := starProjection_minimal

/-- The orthogonal projections onto equal subspaces are coerced back to the same point in `E`. -/
@[deprecated "As there are no subtypes causing dependent type issues, there is no need for this
result as `simp` will suffice" (since := "12-07-2025")]
theorem eq_starProjection_of_eq_submodule {K' : Submodule ùïú E} [K'.HasOrthogonalProjection]
    (h : K = K') (u : E) : K.starProjection u = K'.starProjection u := by
  simp [h]

@[deprecated (since := "2025-07-07")] alias eq_orthogonalProjection_of_eq_submodule :=
  eq_starProjection_of_eq_submodule

/-- The orthogonal projection sends elements of `K` to themselves. -/
@[simp]
theorem orthogonalProjection_mem_subspace_eq_self (v : K) : K.orthogonalProjection v = v := by
  ext
  apply eq_starProjection_of_mem_of_inner_eq_zero <;> simp

@[simp]
theorem starProjection_mem_subspace_eq_self (v : K) :
    K.starProjection v = v := by simp [starProjection_apply]

/-- A point equals its orthogonal projection if and only if it lies in the subspace. -/
theorem starProjection_eq_self_iff {v : E} : K.starProjection v = v ‚Üî v ‚àà K := by
  refine ‚ü®fun h => ?_, fun h => eq_starProjection_of_mem_of_inner_eq_zero h ?_‚ü©
  ¬∑ rw [‚Üê h]
    simp
  ¬∑ simp

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_eq_self_iff :=
  starProjection_eq_self_iff

variable (K) in
@[simp]
lemma isIdempotentElem_starProjection : IsIdempotentElem K.starProjection :=
  ContinuousLinearMap.ext fun x ‚Ü¶ starProjection_eq_self_iff.mpr <| by simp

@[simp]
lemma range_starProjection (U : Submodule ùïú E) [U.HasOrthogonalProjection] :
    LinearMap.range U.starProjection = U := by
  ext x
  exact ‚ü®fun ‚ü®y, hy‚ü© ‚Ü¶ hy ‚ñ∏ coe_mem (U.orthogonalProjection y),
    fun h ‚Ü¶ ‚ü®x, starProjection_eq_self_iff.mpr h‚ü©‚ü©

lemma starProjection_top : (‚ä§ : Submodule ùïú E).starProjection = ContinuousLinearMap.id ùïú E := by
  ext
  exact starProjection_eq_self_iff.mpr trivial

lemma starProjection_top' : (‚ä§ : Submodule ùïú E).starProjection = 1 :=
  starProjection_top

@[simp]
theorem orthogonalProjection_eq_zero_iff {v : E} : K.orthogonalProjection v = 0 ‚Üî v ‚àà K·óÆ := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h ‚Ü¶ Subtype.eq <| eq_starProjection_of_mem_orthogonal
    (zero_mem _) ?_‚ü©
  ¬∑ rw [‚Üê sub_zero v, ‚Üê coe_zero (p := K), ‚Üê h]
    exact sub_starProjection_mem_orthogonal (K := K) v
  ¬∑ simpa

@[simp]
theorem ker_orthogonalProjection : LinearMap.ker K.orthogonalProjection = K·óÆ := by
  ext; exact orthogonalProjection_eq_zero_iff

open ContinuousLinearMap in
@[simp]
lemma ker_starProjection (U : Submodule ùïú E) [U.HasOrthogonalProjection] :
    LinearMap.ker U.starProjection = U·óÆ := by
  rw [(isIdempotentElem_starProjection U).ker_eq_range, ‚Üê starProjection_orthogonal',
    range_starProjection]

theorem _root_.LinearIsometry.map_starProjection {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ùïú E] [InnerProductSpace ùïú E'] (f : E ‚Üí‚Çó·µ¢[ùïú] E')
    (p : Submodule ùïú E) [p.HasOrthogonalProjection] [(p.map f.toLinearMap).HasOrthogonalProjection]
    (x : E) : f (p.starProjection x) = (p.map f.toLinearMap).starProjection (f x) := by
  refine (eq_starProjection_of_mem_of_inner_eq_zero ?_ fun y hy => ?_).symm
  ¬∑ refine Submodule.apply_coe_mem_map _ _
  rcases hy with ‚ü®x', hx', rfl : f x' = y‚ü©
  rw [‚Üê f.map_sub, f.inner_map_map, starProjection_inner_eq_zero x x' hx']

@[deprecated (since := "2025-07-07")] alias _root_.LinearIsometry.map_orthogonalProjection :=
  LinearIsometry.map_starProjection

theorem _root_.LinearIsometry.map_starProjection' {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ùïú E] [InnerProductSpace ùïú E'] (f : E ‚Üí‚Çó·µ¢[ùïú] E')
    (p : Submodule ùïú E) [p.HasOrthogonalProjection] [(p.map f).HasOrthogonalProjection] (x : E) :
    f (p.starProjection x) = (p.map f).starProjection (f x) :=
  have : (p.map f.toLinearMap).HasOrthogonalProjection := ‚Äπ_‚Ä∫
  f.map_starProjection p x

@[deprecated (since := "2025-07-07")] alias _root_.LinearIsometry.map_orthogonalProjection' :=
  LinearIsometry.map_starProjection'

/-- Orthogonal projection onto the `Submodule.map` of a subspace. -/
theorem starProjection_map_apply {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ùïú E] [InnerProductSpace ùïú E'] (f : E ‚âÉ‚Çó·µ¢[ùïú] E')
    (p : Submodule ùïú E) [p.HasOrthogonalProjection] (x : E') :
    (p.map (f.toLinearEquiv : E ‚Üí‚Çó[ùïú] E')).starProjection x =
      f (p.starProjection (f.symm x)) := by
  simpa only [f.coe_toLinearIsometry, f.apply_symm_apply] using
    (f.toLinearIsometry.map_starProjection' p (f.symm x)).symm

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_map_apply :=
  starProjection_map_apply

/-- The orthogonal projection onto the trivial submodule is the zero map. -/
@[simp]
theorem orthogonalProjection_bot : (‚ä• : Submodule ùïú E).orthogonalProjection = 0 := by ext

@[simp]
lemma starProjection_bot : (‚ä• : Submodule ùïú E).starProjection = 0 := by
  rw [starProjection, orthogonalProjection_bot, ContinuousLinearMap.comp_zero]

variable (K)

/-- The orthogonal projection has norm `‚â§ 1`. -/
theorem orthogonalProjection_norm_le : ‚ÄñK.orthogonalProjection‚Äñ ‚â§ 1 :=
  LinearMap.mkContinuous_norm_le _ (by simp) _

theorem starProjection_norm_le : ‚ÄñK.starProjection‚Äñ ‚â§ 1 :=
  K.orthogonalProjection_norm_le

theorem norm_orthogonalProjection_apply {v : E} (hv : v ‚àà K) :
    ‚ÄñorthogonalProjection K v‚Äñ = ‚Äñv‚Äñ :=
  congr(‚Äñ$(K.starProjection_eq_self_iff.mpr hv)‚Äñ)

theorem norm_starProjection_apply {v : E} (hv : v ‚àà K) :
    ‚ÄñK.starProjection v‚Äñ = ‚Äñv‚Äñ :=
  norm_orthogonalProjection_apply _ hv

/-- The orthogonal projection onto a closed subspace is norm non-increasing. -/
theorem norm_orthogonalProjection_apply_le (v : E) :
    ‚ÄñorthogonalProjection K v‚Äñ ‚â§ ‚Äñv‚Äñ := by calc
  ‚ÄñorthogonalProjection K v‚Äñ ‚â§ ‚ÄñorthogonalProjection K‚Äñ * ‚Äñv‚Äñ := K.orthogonalProjection.le_opNorm _
  _ ‚â§ 1 * ‚Äñv‚Äñ := by gcongr; exact orthogonalProjection_norm_le K
  _ = _ := by simp

theorem norm_starProjection_apply_le (v : E) :
    ‚ÄñK.starProjection v‚Äñ ‚â§ ‚Äñv‚Äñ := K.norm_orthogonalProjection_apply_le v

/-- The orthogonal projection onto a closed subspace is a `1`-Lipschitz map. -/
theorem lipschitzWith_orthogonalProjection :
    LipschitzWith 1 (orthogonalProjection K) :=
  ContinuousLinearMap.lipschitzWith_of_opNorm_le (orthogonalProjection_norm_le K)

theorem lipschitzWith_starProjection :
    LipschitzWith 1 K.starProjection :=
  ContinuousLinearMap.lipschitzWith_of_opNorm_le (starProjection_norm_le K)

/-- The operator norm of the orthogonal projection onto a nontrivial subspace is `1`. -/
theorem norm_orthogonalProjection (hK : K ‚â† ‚ä•) :
    ‚ÄñK.orthogonalProjection‚Äñ = 1 := by
  refine le_antisymm K.orthogonalProjection_norm_le ?_
  obtain ‚ü®x, hxK, hx_ne_zero‚ü© := Submodule.exists_mem_ne_zero_of_ne_bot hK
  simpa [K.norm_orthogonalProjection_apply hxK, norm_eq_zero, hx_ne_zero]
    using K.orthogonalProjection.ratio_le_opNorm x

theorem norm_starProjection (hK : K ‚â† ‚ä•) :
    ‚ÄñK.starProjection‚Äñ = 1 :=
  K.norm_orthogonalProjection hK

variable (ùïú)

theorem smul_starProjection_singleton {v : E} (w : E) :
    ((‚Äñv‚Äñ ^ 2 : ‚Ñù) : ùïú) ‚Ä¢ (ùïú ‚àô v).starProjection w = ‚ü™v, w‚ü´ ‚Ä¢ v := by
  suffices ((ùïú ‚àô v).starProjection (((‚Äñv‚Äñ : ùïú) ^ 2) ‚Ä¢ w)) = ‚ü™v, w‚ü´ ‚Ä¢ v by
    simpa using this
  apply eq_starProjection_of_mem_of_inner_eq_zero
  ¬∑ rw [Submodule.mem_span_singleton]
    use ‚ü™v, w‚ü´
  ¬∑ rw [‚Üê Submodule.mem_orthogonal', Submodule.mem_orthogonal_singleton_iff_inner_left]
    simp [inner_sub_left, inner_smul_left, inner_self_eq_norm_sq_to_K, mul_comm]

@[deprecated (since := "2025-07-07")] alias smul_orthogonalProjection_singleton :=
  smul_starProjection_singleton

/-- Formula for orthogonal projection onto a single vector. -/
theorem starProjection_singleton {v : E} (w : E) :
    (ùïú ‚àô v).starProjection w = (‚ü™v, w‚ü´ / ((‚Äñv‚Äñ ^ 2 : ‚Ñù) : ùïú)) ‚Ä¢ v := by
  by_cases hv : v = 0
  ¬∑ rw [hv]
    simp [Submodule.span_zero_singleton ùïú]
  have hv' : ‚Äñv‚Äñ ‚â† 0 := ne_of_gt (norm_pos_iff.mpr hv)
  have key :
    (((‚Äñv‚Äñ ^ 2 : ‚Ñù) : ùïú)‚Åª¬π * ((‚Äñv‚Äñ ^ 2 : ‚Ñù) : ùïú)) ‚Ä¢ ((ùïú ‚àô v).starProjection w) =
      (((‚Äñv‚Äñ ^ 2 : ‚Ñù) : ùïú)‚Åª¬π * ‚ü™v, w‚ü´) ‚Ä¢ v := by
    simp [mul_smul, smul_starProjection_singleton ùïú w, -map_pow]
  convert key using 1 <;> field_simp [hv']

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_singleton :=
  starProjection_singleton

/-- Formula for orthogonal projection onto a single unit vector. -/
theorem starProjection_unit_singleton {v : E} (hv : ‚Äñv‚Äñ = 1) (w : E) :
    (ùïú ‚àô v).starProjection w = ‚ü™v, w‚ü´ ‚Ä¢ v := by
  rw [‚Üê smul_starProjection_singleton ùïú w]
  simp [hv]

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_unit_singleton :=
  starProjection_unit_singleton

end orthogonalProjection

variable {K}

/-- If `K` is complete, any `v` in `E` can be expressed as a sum of elements of `K` and `K·óÆ`. -/
theorem exists_add_mem_mem_orthogonal [K.HasOrthogonalProjection] (v : E) :
    ‚àÉ y ‚àà K, ‚àÉ z ‚àà K·óÆ, v = y + z :=
  ‚ü®K.orthogonalProjection v, Subtype.coe_prop _, v - K.orthogonalProjection v,
    sub_starProjection_mem_orthogonal _, by simp‚ü©

/-- The orthogonal projection onto `K` of an element of `K·óÆ` is zero. -/
theorem orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero [K.HasOrthogonalProjection]
    {v : E} (hv : v ‚àà K·óÆ) : K.orthogonalProjection v = 0 := by
  ext
  convert eq_starProjection_of_mem_orthogonal (K := K) _ _ <;> simp [hv]

/-- The projection into `U` from an orthogonal submodule `V` is the zero map. -/
theorem IsOrtho.orthogonalProjection_comp_subtypeL {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] (h : U ‚üÇ V) : U.orthogonalProjection ‚àòL V.subtypeL = 0 :=
  ContinuousLinearMap.ext fun v =>
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero <| h.symm v.prop

theorem IsOrtho.starProjection_comp_starProjection {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U ‚üÇ V) :
    U.starProjection ‚àòL V.starProjection = 0 :=
  calc _ = U.subtypeL ‚àòL (U.orthogonalProjection ‚àòL V.subtypeL) ‚àòL V.orthogonalProjection := by
        simp only [starProjection, ContinuousLinearMap.comp_assoc]
    _ = 0 := by simp [h.orthogonalProjection_comp_subtypeL]

/-- The projection into `U` from `V` is the zero map if and only if `U` and `V` are orthogonal. -/
theorem orthogonalProjection_comp_subtypeL_eq_zero_iff {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] : U.orthogonalProjection ‚àòL V.subtypeL = 0 ‚Üî U ‚üÇ V :=
  ‚ü®fun h u hu v hv => by
    convert starProjection_inner_eq_zero v u hu using 2
    have : U.orthogonalProjection v = 0 := DFunLike.congr_fun h (‚ü®_, hv‚ü© : V)
    rw [starProjection_apply, this, Submodule.coe_zero, sub_zero],
    Submodule.IsOrtho.orthogonalProjection_comp_subtypeL‚ü©

/-- `U.starProjection ‚àò V.starProjection = 0` iff `U` and `V` are pairwise orthogonal. -/
theorem starProjection_comp_starProjection_eq_zero_iff {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] :
    U.starProjection ‚àòL V.starProjection = 0 ‚Üî U ‚üÇ V := by
  refine ‚ü®fun h => ?_, fun h => h.starProjection_comp_starProjection‚ü©
  rw [‚Üê orthogonalProjection_comp_subtypeL_eq_zero_iff]
  simp only [ContinuousLinearMap.ext_iff, ContinuousLinearMap.comp_apply, subtypeL_apply,
    starProjection_apply, ContinuousLinearMap.zero_apply, coe_eq_zero] at h ‚ä¢
  intro x
  simpa using h (x : E)

/-- The orthogonal projection onto `K·óÆ` of an element of `K` is zero. -/
theorem orthogonalProjection_orthogonal_apply_eq_zero
    [K·óÆ.HasOrthogonalProjection] {v : E} (hv : v ‚àà K) : K·óÆ.orthogonalProjection v = 0 :=
  orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero (K.le_orthogonal_orthogonal hv)

@[deprecated (since := "22-07-2025")] alias
  orthogonalProjection_mem_subspace_orthogonal_precomplement_eq_zero :=
  orthogonalProjection_orthogonal_apply_eq_zero

theorem starProjection_orthogonal_apply_eq_zero
    [K·óÆ.HasOrthogonalProjection] {v : E} (hv : v ‚àà K) :
    K·óÆ.starProjection v = 0 := by
  rw [starProjection_apply, coe_eq_zero]
  exact orthogonalProjection_orthogonal_apply_eq_zero hv

/-- If `U ‚â§ V`, then projecting on `V` and then on `U` is the same as projecting on `U`. -/
theorem orthogonalProjection_starProjection_of_le {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U ‚â§ V) (x : E) :
    U.orthogonalProjection (V.starProjection x) = U.orthogonalProjection x :=
  Eq.symm <| by
    simpa only [sub_eq_zero, map_sub] using
      orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero
        (Submodule.orthogonal_le h (sub_starProjection_mem_orthogonal x))

@[deprecated (since := "2025-07-07")] alias orthogonalProjection_orthogonalProjection_of_le :=
  orthogonalProjection_starProjection_of_le

theorem starProjection_comp_starProjection_of_le {U V : Submodule ùïú E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U ‚â§ V) :
    U.starProjection ‚àòL V.starProjection = U.starProjection := ContinuousLinearMap.ext fun _ => by
  nth_rw 1 [starProjection]
  simp [orthogonalProjection_starProjection_of_le h]

open ContinuousLinearMap in
theorem _root_.ContinuousLinearMap.IsIdempotentElem.hasOrthogonalProjection_range [CompleteSpace E]
    {p : E ‚ÜíL[ùïú] E} (hp : IsIdempotentElem p) : (LinearMap.range p).HasOrthogonalProjection :=
  have := hp.isClosed_range.completeSpace_coe
  .ofCompleteSpace _

/-- The orthogonal projection onto `(ùïú ‚àô v)·óÆ` of `v` is zero. -/
theorem orthogonalProjection_orthogonalComplement_singleton_eq_zero (v : E) :
    (ùïú ‚àô v)·óÆ.orthogonalProjection v = 0 :=
  orthogonalProjection_orthogonal_apply_eq_zero
    (Submodule.mem_span_singleton_self v)

theorem starProjection_orthogonalComplement_singleton_eq_zero (v : E) :
    (ùïú ‚àô v)·óÆ.starProjection v = 0 := by
  rw [starProjection_apply, coe_eq_zero]
  exact orthogonalProjection_orthogonalComplement_singleton_eq_zero v


/-- If the orthogonal projection to `K` is well-defined, then a vector splits as the sum of its
orthogonal projections onto a complete submodule `K` and onto the orthogonal complement of `K`. -/
theorem starProjection_add_starProjection_orthogonal [K.HasOrthogonalProjection]
    (w : E) : K.starProjection w + K·óÆ.starProjection w = w := by
  simp

@[deprecated (since := "2025-07-07")] alias
  orthogonalProjection_add_orthogonalProjection_orthogonal :=
  starProjection_add_starProjection_orthogonal

/-- The Pythagorean theorem, for an orthogonal projection. -/
theorem norm_sq_eq_add_norm_sq_projection (x : E) (S : Submodule ùïú E) [S.HasOrthogonalProjection] :
    ‚Äñx‚Äñ ^ 2 = ‚ÄñS.orthogonalProjection x‚Äñ ^ 2 + ‚ÄñS·óÆ.orthogonalProjection x‚Äñ ^ 2 :=
  calc
    ‚Äñx‚Äñ ^ 2 = ‚ÄñS.starProjection x + S·óÆ.starProjection x‚Äñ ^ 2 := by
      rw [starProjection_add_starProjection_orthogonal]
    _ = ‚ÄñS.orthogonalProjection x‚Äñ ^ 2 + ‚ÄñS·óÆ.orthogonalProjection x‚Äñ ^ 2 := by
      simp only [sq]
      exact norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ <|
        (S.mem_orthogonal _).1 (S·óÆ.orthogonalProjection x).2 _ (S.orthogonalProjection x).2

theorem norm_sq_eq_add_norm_sq_starProjection (x : E) (S : Submodule ùïú E)
    [S.HasOrthogonalProjection] :
    ‚Äñx‚Äñ ^ 2 = ‚ÄñS.starProjection x‚Äñ ^ 2 + ‚ÄñS·óÆ.starProjection x‚Äñ ^ 2 :=
  norm_sq_eq_add_norm_sq_projection x S

theorem mem_iff_norm_starProjection (U : Submodule ùïú E)
    [U.HasOrthogonalProjection] (v : E) :
    v ‚àà U ‚Üî ‚ÄñU.starProjection v‚Äñ = ‚Äñv‚Äñ := by
  refine ‚ü®fun h => norm_starProjection_apply _ h, fun h => ?_‚ü©
  simpa [h, sub_eq_zero, eq_comm (a := v), starProjection_eq_self_iff] using
    U.norm_sq_eq_add_norm_sq_starProjection v

/-- In a complete space `E`, the projection maps onto a complete subspace `K` and its orthogonal
complement sum to the identity. -/
theorem id_eq_sum_starProjection_self_orthogonalComplement [K.HasOrthogonalProjection] :
    ContinuousLinearMap.id ùïú E =
      K.starProjection + K·óÆ.starProjection := by
  ext w
  exact (K.starProjection_add_starProjection_orthogonal w).symm

@[deprecated (since := "2025-07-07")] alias
  id_eq_sum_orthogonalProjection_self_orthogonalComplement :=
  id_eq_sum_starProjection_self_orthogonalComplement

-- Porting note: The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_right [K.HasOrthogonalProjection] (u : K) (v : E) :
    ‚ü™K.orthogonalProjection v, u‚ü´ = ‚ü™v, u‚ü´ :=
  calc
    ‚ü™K.orthogonalProjection v, u‚ü´ = ‚ü™K.starProjection v, u‚ü´ := K.coe_inner _ _
    _ = ‚ü™K.starProjection v, u‚ü´ + ‚ü™v - K.starProjection v, u‚ü´ := by
      rw [starProjection_inner_eq_zero _ _ (Submodule.coe_mem _), add_zero]
    _ = ‚ü™v, u‚ü´ := by rw [‚Üê inner_add_left, add_sub_cancel]

-- Porting note: The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_left [K.HasOrthogonalProjection] (u : K) (v : E) :
    ‚ü™u, K.orthogonalProjection v‚ü´ = ‚ü™(u : E), v‚ü´ := by
  rw [‚Üê inner_conj_symm, ‚Üê inner_conj_symm (u : E), inner_orthogonalProjection_eq_of_mem_right]

variable (K)

/-- The orthogonal projection is self-adjoint. -/
theorem inner_starProjection_left_eq_right [K.HasOrthogonalProjection] (u v : E) :
    ‚ü™K.starProjection u, v‚ü´ = ‚ü™u, K.starProjection v‚ü´ := by
  simp_rw [starProjection_apply, ‚Üê inner_orthogonalProjection_eq_of_mem_left,
    inner_orthogonalProjection_eq_of_mem_right]

@[deprecated (since := "2025-07-07")] alias
  inner_orthogonalProjection_left_eq_right := inner_starProjection_left_eq_right

/-- The orthogonal projection is symmetric. -/
theorem starProjection_isSymmetric [K.HasOrthogonalProjection] :
    (K.starProjection : E ‚Üí‚Çó[ùïú] E).IsSymmetric :=
  inner_starProjection_left_eq_right K

@[deprecated (since := "2025-07-07")] alias
  orthogonalProjection_isSymmetric := starProjection_isSymmetric

theorem starProjection_apply_eq_zero_iff [K.HasOrthogonalProjection] {v : E} :
    K.starProjection v = 0 ‚Üî v ‚àà K·óÆ := by
  refine ‚ü®fun h w hw => ?_, fun hv => ?_‚ü©
  ¬∑ rw [‚Üê starProjection_eq_self_iff.mpr hw, inner_starProjection_left_eq_right, h,
      inner_zero_right]
  ¬∑ simp [starProjection_apply, orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv]

open RCLike

lemma re_inner_starProjection_eq_normSq [K.HasOrthogonalProjection] (v : E) :
    re ‚ü™K.starProjection v, v‚ü´ = ‚ÄñK.orthogonalProjection v‚Äñ^2 := by
  rw [starProjection_apply,
    re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
    div_eq_iff (NeZero.ne' 2).symm, pow_two, add_sub_assoc, ‚Üê eq_sub_iff_add_eq', coe_norm,
    ‚Üê mul_sub_one, show (2 : ‚Ñù) - 1 = 1 by norm_num, mul_one, sub_eq_iff_eq_add', norm_sub_rev]
  exact orthogonalProjectionFn_norm_sq K v

@[deprecated (since := "2025-07-07")] alias
  re_inner_orthogonalProjection_eq_normSq := re_inner_starProjection_eq_normSq

lemma re_inner_starProjection_nonneg [K.HasOrthogonalProjection] (v : E) :
    0 ‚â§ re ‚ü™K.starProjection v, v‚ü´ := by
  rw [re_inner_starProjection_eq_normSq K v]
  exact sq_nonneg ‚ÄñK.orthogonalProjection v‚Äñ

@[deprecated (since := "2025-07-07")] alias
  re_inner_orthogonalProjection_nonneg := re_inner_starProjection_nonneg

end

end Submodule
