/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
module

public import Mathlib.Analysis.InnerProductSpace.Projection.Minimal
public import Mathlib.Analysis.InnerProductSpace.Symmetric
public import Mathlib.Analysis.RCLike.Lemmas

/-!
# The orthogonal projection

Given a nonempty subspace `K` of an inner product space `E` such that `K`
admits an orthogonal projection, this file constructs
`K.orthogonalProjection : E â†’L[ğ•œ] K`, the orthogonal projection of `E` onto `K`. This map
satisfies: for any point `u` in `E`, the point `v = K.orthogonalProjection u` in `K` minimizes the
distance `â€–u - vâ€–` to `u`.

This file also defines `K.starProjection : E â†’L[ğ•œ] E` which is the
orthogonal projection of `E` onto `K` but as a map from `E` to `E` instead of `E` to `K`.

Basic API for `orthogonalProjection` and `starProjection` is developed.

## References

The orthogonal projection construction is adapted from
* [ClÃ©ment & Martin, *The Lax-Milgram Theorem. A detailed proof to be formalized in Coq*]
* [ClÃ©ment & Martin, *A Coq formal proof of the Laxâ€“Milgram theorem*]

The Coq code is available at the following address: <http://www.lri.fr/~sboldo/elfic/index.html>
-/

@[expose] public section

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y
local notation "absR" => @abs â„ _ _

namespace Submodule

/-- A subspace `K : Submodule ğ•œ E` has an orthogonal projection if every vector `v : E` admits an
orthogonal projection to `K`. -/
class HasOrthogonalProjection (K : Submodule ğ•œ E) : Prop where
  exists_orthogonal (v : E) : âˆƒ w âˆˆ K, v - w âˆˆ Ká—®

variable (K : Submodule ğ•œ E)

instance (priority := 100) HasOrthogonalProjection.ofCompleteSpace [CompleteSpace K] :
    K.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases K.exists_norm_eq_iInf_of_complete_subspace (completeSpace_coe_iff_isComplete.mp â€¹_â€º) v
      with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨w, hwK, (K.mem_orthogonal' _).2 ?_âŸ©
    rwa [â† K.norm_eq_iInf_iff_inner_eq_zero hwK]

instance [K.HasOrthogonalProjection] : Ká—®.HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) v with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨_, hw, ?_âŸ©
    rw [sub_sub_cancel]
    exact K.le_orthogonal_orthogonal hwK

instance HasOrthogonalProjection.map_linearIsometryEquiv [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
    (K.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')).HasOrthogonalProjection where
  exists_orthogonal v := by
    rcases HasOrthogonalProjection.exists_orthogonal (K := K) (f.symm v) with âŸ¨w, hwK, hwâŸ©
    refine âŸ¨f w, Submodule.mem_map_of_mem hwK, Set.forall_mem_image.2 fun u hu â†¦ ?_âŸ©
    erw [â† f.symm.inner_map_map, f.symm_apply_apply, map_sub, f.symm_apply_apply, hw u hu]

instance HasOrthogonalProjection.map_linearIsometryEquiv' [K.HasOrthogonalProjection]
    {E' : Type*} [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E') :
    (K.map (f.toLinearIsometry : E â†’â‚—[ğ•œ] E')).HasOrthogonalProjection :=
  HasOrthogonalProjection.map_linearIsometryEquiv K f

instance : (âŠ¤ : Submodule ğ•œ E).HasOrthogonalProjection := âŸ¨fun v â†¦ âŸ¨v, trivial, by simpâŸ©âŸ©

instance (K : ClosedSubmodule ğ•œ E) [CompleteSpace E] : K.HasOrthogonalProjection := by
  letI := K.isClosed'
  infer_instance

noncomputable section

section orthogonalProjection

variable [K.HasOrthogonalProjection]

/-- The orthogonal projection onto a complete subspace, as an
unbundled function. This definition is only intended for use in
setting up the bundled version `orthogonalProjection` and should not
be used once that is defined. -/
def orthogonalProjectionFn (v : E) :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose

variable {K}

/-- The unbundled orthogonal projection is in the given subspace.
This lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_mem (v : E) : K.orthogonalProjectionFn v âˆˆ K :=
  (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.left

/-- The characterization of the unbundled orthogonal projection.  This
lemma is only intended for use in setting up the bundled version
and should not be used once that is defined. -/
theorem orthogonalProjectionFn_inner_eq_zero (v : E) :
    âˆ€ w âˆˆ K, âŸªv - K.orthogonalProjectionFn v, wâŸ« = 0 :=
  (K.mem_orthogonal' _).1 (HasOrthogonalProjection.exists_orthogonal (K := K) v).choose_spec.right

/-- The unbundled orthogonal projection is the unique point in `K`
with the orthogonality property. This lemma is only intended for use
in setting up the bundled version and should not be used once that is
defined. -/
theorem eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero {u v : E} (hvm : v âˆˆ K)
    (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) : K.orthogonalProjectionFn u = v := by
  rw [â† sub_eq_zero, â† @inner_self_eq_zero ğ•œ]
  have hvs : K.orthogonalProjectionFn u - v âˆˆ K :=
    Submodule.sub_mem K (orthogonalProjectionFn_mem u) hvm
  have huo : âŸªu - K.orthogonalProjectionFn u, K.orthogonalProjectionFn u - vâŸ« = 0 :=
    orthogonalProjectionFn_inner_eq_zero u _ hvs
  have huv : âŸªu - v, K.orthogonalProjectionFn u - vâŸ« = 0 := hvo _ hvs
  have houv : âŸªu - v - (u - K.orthogonalProjectionFn u), K.orthogonalProjectionFn u - vâŸ« = 0 := by
    rw [inner_sub_left, huo, huv, sub_zero]
  rwa [sub_sub_sub_cancel_left] at houv

variable (K)

theorem orthogonalProjectionFn_norm_sq (v : E) :
    â€–vâ€– * â€–vâ€– =
      â€–v - K.orthogonalProjectionFn vâ€– * â€–v - K.orthogonalProjectionFn vâ€– +
        â€–K.orthogonalProjectionFn vâ€– * â€–K.orthogonalProjectionFn vâ€– := by
  set p := K.orthogonalProjectionFn v
  have h' : âŸªv - p, pâŸ« = 0 :=
    orthogonalProjectionFn_inner_eq_zero _ _ (orthogonalProjectionFn_mem v)
  convert norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero (v - p) p h' using 2 <;> simp

/-- The orthogonal projection onto a complete subspace. -/
def orthogonalProjection : E â†’L[ğ•œ] K :=
  LinearMap.mkContinuous
    { toFun := fun v => âŸ¨K.orthogonalProjectionFn v, orthogonalProjectionFn_mem vâŸ©
      map_add' := fun x y => by
        have hm : K.orthogonalProjectionFn x + K.orthogonalProjectionFn y âˆˆ K :=
          Submodule.add_mem K (orthogonalProjectionFn_mem x) (orthogonalProjectionFn_mem y)
        have ho :
          âˆ€ w âˆˆ K, âŸªx + y - (K.orthogonalProjectionFn x + K.orthogonalProjectionFn y), wâŸ« = 0 := by
          intro w hw
          rw [add_sub_add_comm, inner_add_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            orthogonalProjectionFn_inner_eq_zero _ w hw, add_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho]
      map_smul' := fun c x => by
        have hm : c â€¢ K.orthogonalProjectionFn x âˆˆ K :=
          Submodule.smul_mem K _ (orthogonalProjectionFn_mem x)
        have ho : âˆ€ w âˆˆ K, âŸªc â€¢ x - c â€¢ K.orthogonalProjectionFn x, wâŸ« = 0 := by
          intro w hw
          rw [â† smul_sub, inner_smul_left, orthogonalProjectionFn_inner_eq_zero _ w hw,
            mul_zero]
        ext
        simp [eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hm ho] }
    1 fun x => by
    simp only [one_mul, LinearMap.coe_mk]
    refine le_of_pow_le_pow_leftâ‚€ two_ne_zero (norm_nonneg _) ?_
    change â€–K.orthogonalProjectionFn xâ€– ^ 2 â‰¤ â€–xâ€– ^ 2
    nlinarith [K.orthogonalProjectionFn_norm_sq x]

variable {K}

@[simp]
theorem orthogonalProjectionFn_eq (v : E) :
    K.orthogonalProjectionFn v = (K.orthogonalProjection v : E) :=
  rfl

/-- The orthogonal projection onto a subspace as a map from the full space to itself,
as opposed to `Submodule.orthogonalProjection`, which maps into the subtype. This
version is important as it satisfies `IsStarProjection`. -/
def starProjection (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    E â†’L[ğ•œ] E := U.subtypeL âˆ˜L U.orthogonalProjection

lemma starProjection_apply (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] (v : E) :
    U.starProjection v = U.orthogonalProjection v := rfl

@[simp]
lemma coe_orthogonalProjection_apply (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] (v : E) :
     U.orthogonalProjection v = U.starProjection v := rfl

@[simp]
lemma starProjection_apply_mem (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] (x : E) :
    U.starProjection x âˆˆ U := by
  simp only [starProjection_apply, SetLike.coe_mem]

/-- The characterization of the orthogonal projection. -/
@[simp]
theorem starProjection_inner_eq_zero (v : E) :
    âˆ€ w âˆˆ K, âŸªv - K.starProjection v, wâŸ« = 0 :=
  orthogonalProjectionFn_inner_eq_zero v

/-- The difference of `v` from its orthogonal projection onto `K` is in `Ká—®`. -/
@[simp]
theorem sub_starProjection_mem_orthogonal (v : E) : v - K.starProjection v âˆˆ Ká—® := by
  intro w hw
  rw [inner_eq_zero_symm]
  exact starProjection_inner_eq_zero _ _ hw

/-- The orthogonal projection is the unique point in `K` with the
orthogonality property. -/
theorem eq_starProjection_of_mem_of_inner_eq_zero {u v : E} (hvm : v âˆˆ K)
    (hvo : âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0) : K.starProjection u = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hvm hvo

/-- A point in `K` with the orthogonality property (here characterized in terms of `Ká—®`) must be the
orthogonal projection. -/
theorem eq_starProjection_of_mem_orthogonal {u v : E} (hv : v âˆˆ K)
    (hvo : u - v âˆˆ Ká—®) : K.starProjection u = v :=
  eq_orthogonalProjectionFn_of_mem_of_inner_eq_zero hv <| (Submodule.mem_orthogonal' _ _).1 hvo

/-- A point in `K` with the orthogonality property (here characterized in terms of `Ká—®`) must be the
orthogonal projection. -/
theorem eq_starProjection_of_mem_orthogonal' {u v z : E}
    (hv : v âˆˆ K) (hz : z âˆˆ Ká—®) (hu : u = v + z) : K.starProjection u = v :=
  eq_starProjection_of_mem_orthogonal hv (by simpa [hu])

@[simp]
theorem starProjection_orthogonal_val (u : E) :
    Ká—®.starProjection u = u - K.starProjection u :=
  eq_starProjection_of_mem_orthogonal' (sub_starProjection_mem_orthogonal _)
    (K.le_orthogonal_orthogonal (K.orthogonalProjection u).2) <| (sub_add_cancel _ _).symm

theorem orthogonalProjection_orthogonal (u : E) :
    Ká—®.orthogonalProjection u =
      âŸ¨u - K.starProjection u, sub_starProjection_mem_orthogonal _âŸ© :=
  Subtype.ext <| starProjection_orthogonal_val _

lemma starProjection_orthogonal (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    Uá—®.starProjection = ContinuousLinearMap.id ğ•œ E - U.starProjection := by
  ext
  simp only [starProjection, ContinuousLinearMap.comp_apply,
    orthogonalProjection_orthogonal]
  simp

lemma starProjection_orthogonal' (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    Uá—®.starProjection = 1 - U.starProjection := starProjection_orthogonal U

/-- The orthogonal projection of `y` on `U` minimizes the distance `â€–y - xâ€–` for `x âˆˆ U`. -/
theorem starProjection_minimal {U : Submodule ğ•œ E} [U.HasOrthogonalProjection] (y : E) :
    â€–y - U.starProjection yâ€– = â¨… x : U, â€–y - xâ€– := by
  rw [starProjection_apply, U.norm_eq_iInf_iff_inner_eq_zero (Submodule.coe_mem _)]
  exact starProjection_inner_eq_zero _

/-- The orthogonal projections onto equal subspaces are coerced back to the same point in `E`. -/
@[deprecated "As there are no subtypes causing dependent type issues, there is no need for this
result as `simp` will suffice" (since := "2025-07-12")]
theorem eq_starProjection_of_eq_submodule {K' : Submodule ğ•œ E} [K'.HasOrthogonalProjection]
    (h : K = K') (u : E) : K.starProjection u = K'.starProjection u := by
  simp [h]

/-- The orthogonal projection sends elements of `K` to themselves. -/
@[simp]
theorem orthogonalProjection_mem_subspace_eq_self (v : K) : K.orthogonalProjection v = v := by
  ext
  apply eq_starProjection_of_mem_of_inner_eq_zero <;> simp

@[simp]
theorem starProjection_mem_subspace_eq_self (v : K) :
    K.starProjection v = v := by simp [starProjection_apply]

/-- A point equals its orthogonal projection if and only if it lies in the subspace. -/
theorem starProjection_eq_self_iff {v : E} : K.starProjection v = v â†” v âˆˆ K := by
  refine âŸ¨fun h => ?_, fun h => eq_starProjection_of_mem_of_inner_eq_zero h ?_âŸ©
  Â· rw [â† h]
    simp
  Â· simp

variable (K) in
@[simp]
lemma isIdempotentElem_starProjection : IsIdempotentElem K.starProjection :=
  ContinuousLinearMap.ext fun x â†¦ starProjection_eq_self_iff.mpr <| by simp

@[simp]
lemma range_starProjection (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    U.starProjection.range = U := by
  ext x
  exact âŸ¨fun âŸ¨y, hyâŸ© â†¦ hy â–¸ coe_mem (U.orthogonalProjection y),
    fun h â†¦ âŸ¨x, starProjection_eq_self_iff.mpr hâŸ©âŸ©

lemma starProjection_top : (âŠ¤ : Submodule ğ•œ E).starProjection = ContinuousLinearMap.id ğ•œ E := by
  ext
  exact starProjection_eq_self_iff.mpr trivial

lemma starProjection_top' : (âŠ¤ : Submodule ğ•œ E).starProjection = 1 :=
  starProjection_top

@[simp]
theorem orthogonalProjection_eq_zero_iff {v : E} : K.orthogonalProjection v = 0 â†” v âˆˆ Ká—® := by
  refine âŸ¨fun h â†¦ ?_, fun h â†¦ Subtype.ext <| eq_starProjection_of_mem_orthogonal
    (zero_mem _) ?_âŸ©
  Â· rw [â† sub_zero v, â† coe_zero (p := K), â† h]
    exact sub_starProjection_mem_orthogonal (K := K) v
  Â· simpa

@[simp]
theorem ker_orthogonalProjection : K.orthogonalProjection.ker = Ká—® := by
  ext; exact orthogonalProjection_eq_zero_iff

open ContinuousLinearMap in
@[simp]
lemma ker_starProjection (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    U.starProjection.ker = Uá—® := by
  rw [LinearMap.IsIdempotentElem.ker_eq_range U.isIdempotentElem_starProjection.toLinearMap,
    â† range_starProjection Uá—®, starProjection_orthogonal, coe_sub, coe_id]

theorem _root_.LinearIsometry.map_starProjection {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â†’â‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection] [(p.map f.toLinearMap).HasOrthogonalProjection]
    (x : E) : f (p.starProjection x) = (p.map f.toLinearMap).starProjection (f x) := by
  refine (eq_starProjection_of_mem_of_inner_eq_zero ?_ fun y hy => ?_).symm
  Â· refine Submodule.apply_coe_mem_map _ _
  rcases hy with âŸ¨x', hx', rfl : f x' = yâŸ©
  rw [â† f.map_sub, f.inner_map_map, starProjection_inner_eq_zero x x' hx']

theorem _root_.LinearIsometry.map_starProjection' {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â†’â‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection]
    [(p.map (f : E â†’â‚—[ğ•œ] E')).HasOrthogonalProjection] (x : E) :
    f (p.starProjection x) = (p.map (f : E â†’â‚—[ğ•œ] E')).starProjection (f x) :=
  have : (p.map f.toLinearMap).HasOrthogonalProjection := â€¹_â€º
  f.map_starProjection p x

/-- Orthogonal projection onto the `Submodule.map` of a subspace. -/
theorem starProjection_map_apply {E E' : Type*} [NormedAddCommGroup E]
    [NormedAddCommGroup E'] [InnerProductSpace ğ•œ E] [InnerProductSpace ğ•œ E'] (f : E â‰ƒâ‚—áµ¢[ğ•œ] E')
    (p : Submodule ğ•œ E) [p.HasOrthogonalProjection] (x : E') :
    (p.map (f.toLinearEquiv : E â†’â‚—[ğ•œ] E')).starProjection x =
      f (p.starProjection (f.symm x)) := by
  simpa only [f.coe_toLinearIsometry, f.apply_symm_apply] using
    (f.toLinearIsometry.map_starProjection' p (f.symm x)).symm

/-- The orthogonal projection onto the trivial submodule is the zero map. -/
@[simp]
theorem orthogonalProjection_bot : (âŠ¥ : Submodule ğ•œ E).orthogonalProjection = 0 := by ext

@[simp]
lemma starProjection_bot : (âŠ¥ : Submodule ğ•œ E).starProjection = 0 := by
  rw [starProjection, orthogonalProjection_bot, ContinuousLinearMap.comp_zero]

variable (K)

/-- The orthogonal projection has norm `â‰¤ 1`. -/
theorem orthogonalProjection_norm_le : â€–K.orthogonalProjectionâ€– â‰¤ 1 :=
  LinearMap.mkContinuous_norm_le _ (by simp) _

theorem starProjection_norm_le : â€–K.starProjectionâ€– â‰¤ 1 :=
  K.orthogonalProjection_norm_le

theorem norm_orthogonalProjection_apply {v : E} (hv : v âˆˆ K) :
    â€–orthogonalProjection K vâ€– = â€–vâ€– :=
  congr(â€–$(K.starProjection_eq_self_iff.mpr hv)â€–)

theorem norm_starProjection_apply {v : E} (hv : v âˆˆ K) :
    â€–K.starProjection vâ€– = â€–vâ€– :=
  norm_orthogonalProjection_apply _ hv

/-- The orthogonal projection onto a closed subspace is norm non-increasing. -/
theorem norm_orthogonalProjection_apply_le (v : E) :
    â€–orthogonalProjection K vâ€– â‰¤ â€–vâ€– := by calc
  â€–orthogonalProjection K vâ€– â‰¤ â€–orthogonalProjection Kâ€– * â€–vâ€– := K.orthogonalProjection.le_opNorm _
  _ â‰¤ 1 * â€–vâ€– := by gcongr; exact orthogonalProjection_norm_le K
  _ = _ := by simp

theorem norm_starProjection_apply_le (v : E) :
    â€–K.starProjection vâ€– â‰¤ â€–vâ€– := K.norm_orthogonalProjection_apply_le v

/-- The orthogonal projection onto a closed subspace is a `1`-Lipschitz map. -/
theorem lipschitzWith_orthogonalProjection :
    LipschitzWith 1 (orthogonalProjection K) :=
  ContinuousLinearMap.lipschitzWith_of_opNorm_le (orthogonalProjection_norm_le K)

theorem lipschitzWith_starProjection :
    LipschitzWith 1 K.starProjection :=
  ContinuousLinearMap.lipschitzWith_of_opNorm_le (starProjection_norm_le K)

/-- The operator norm of the orthogonal projection onto a nontrivial subspace is `1`. -/
theorem norm_orthogonalProjection (hK : K â‰  âŠ¥) :
    â€–K.orthogonalProjectionâ€– = 1 := by
  refine le_antisymm K.orthogonalProjection_norm_le ?_
  obtain âŸ¨x, hxK, hx_ne_zeroâŸ© := Submodule.exists_mem_ne_zero_of_ne_bot hK
  simpa [K.norm_orthogonalProjection_apply hxK, norm_eq_zero, hx_ne_zero]
    using K.orthogonalProjection.ratio_le_opNorm x

theorem norm_starProjection (hK : K â‰  âŠ¥) :
    â€–K.starProjectionâ€– = 1 :=
  K.norm_orthogonalProjection hK

variable (ğ•œ)

theorem smul_starProjection_singleton {v : E} (w : E) :
    ((â€–vâ€– ^ 2 : â„) : ğ•œ) â€¢ (ğ•œ âˆ™ v).starProjection w = âŸªv, wâŸ« â€¢ v := by
  suffices ((ğ•œ âˆ™ v).starProjection (((â€–vâ€– : ğ•œ) ^ 2) â€¢ w)) = âŸªv, wâŸ« â€¢ v by
    simpa using this
  apply eq_starProjection_of_mem_of_inner_eq_zero
  Â· rw [Submodule.mem_span_singleton]
    use âŸªv, wâŸ«
  Â· rw [â† Submodule.mem_orthogonal', Submodule.mem_orthogonal_singleton_iff_inner_left]
    simp [inner_sub_left, inner_smul_left, inner_self_eq_norm_sq_to_K, mul_comm]

/-- Formula for orthogonal projection onto a single vector. -/
theorem starProjection_singleton {v : E} (w : E) :
    (ğ•œ âˆ™ v).starProjection w = (âŸªv, wâŸ« / ((â€–vâ€– ^ 2 : â„) : ğ•œ)) â€¢ v := by
  by_cases hv : v = 0
  Â· rw [hv]
    simp [Submodule.span_zero_singleton ğ•œ]
  have hv' : â€–vâ€– â‰  0 := ne_of_gt (norm_pos_iff.mpr hv)
  have key :
    (((â€–vâ€– ^ 2 : â„) : ğ•œ)â»Â¹ * ((â€–vâ€– ^ 2 : â„) : ğ•œ)) â€¢ (ğ•œ âˆ™ v).starProjection w =
      (((â€–vâ€– ^ 2 : â„) : ğ•œ)â»Â¹ * âŸªv, wâŸ«) â€¢ v := by
    simp [mul_smul, smul_starProjection_singleton ğ•œ w, -map_pow]
  convert key using 1 <;> match_scalars <;> field_simp [hv']

/-- Formula for orthogonal projection onto a single unit vector. -/
theorem starProjection_unit_singleton {v : E} (hv : â€–vâ€– = 1) (w : E) :
    (ğ•œ âˆ™ v).starProjection w = âŸªv, wâŸ« â€¢ v := by
  rw [â† smul_starProjection_singleton ğ•œ w]
  simp [hv]

end orthogonalProjection

variable {K}

/-- If `K` is complete, any `v` in `E` can be expressed as a sum of elements of `K` and `Ká—®`. -/
theorem exists_add_mem_mem_orthogonal [K.HasOrthogonalProjection] (v : E) :
    âˆƒ y âˆˆ K, âˆƒ z âˆˆ Ká—®, v = y + z :=
  âŸ¨K.orthogonalProjection v, Subtype.coe_prop _, v - K.orthogonalProjection v,
    sub_starProjection_mem_orthogonal _, by simpâŸ©

/-- The orthogonal projection onto `K` of an element of `Ká—®` is zero. -/
theorem orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero [K.HasOrthogonalProjection]
    {v : E} (hv : v âˆˆ Ká—®) : K.orthogonalProjection v = 0 := orthogonalProjection_eq_zero_iff.mpr hv

/-- The projection into `U` from an orthogonal submodule `V` is the zero map. -/
theorem IsOrtho.orthogonalProjection_comp_subtypeL {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] (h : U âŸ‚ V) : U.orthogonalProjection âˆ˜L V.subtypeL = 0 :=
  ContinuousLinearMap.ext fun v =>
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero <| h.symm v.prop

theorem IsOrtho.starProjection_comp_starProjection {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U âŸ‚ V) :
    U.starProjection âˆ˜L V.starProjection = 0 :=
  calc _ = U.subtypeL âˆ˜L (U.orthogonalProjection âˆ˜L V.subtypeL) âˆ˜L V.orthogonalProjection := by
        simp only [starProjection, ContinuousLinearMap.comp_assoc]
    _ = 0 := by simp [h.orthogonalProjection_comp_subtypeL]

/-- The projection into `U` from `V` is the zero map if and only if `U` and `V` are orthogonal. -/
theorem orthogonalProjection_comp_subtypeL_eq_zero_iff {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] : U.orthogonalProjection âˆ˜L V.subtypeL = 0 â†” U âŸ‚ V :=
  âŸ¨fun h u hu v hv => by
    convert starProjection_inner_eq_zero v u hu using 2
    have : U.orthogonalProjection v = 0 := DFunLike.congr_fun h (âŸ¨_, hvâŸ© : V)
    rw [starProjection_apply, this, Submodule.coe_zero, sub_zero],
    Submodule.IsOrtho.orthogonalProjection_comp_subtypeLâŸ©

/-- `U.starProjection âˆ˜ V.starProjection = 0` iff `U` and `V` are pairwise orthogonal. -/
theorem starProjection_comp_starProjection_eq_zero_iff {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] :
    U.starProjection âˆ˜L V.starProjection = 0 â†” U âŸ‚ V := by
  refine âŸ¨fun h => ?_, fun h => h.starProjection_comp_starProjectionâŸ©
  rw [â† orthogonalProjection_comp_subtypeL_eq_zero_iff]
  simp only [ContinuousLinearMap.ext_iff, ContinuousLinearMap.comp_apply, subtypeL_apply,
    starProjection_apply, ContinuousLinearMap.zero_apply, coe_eq_zero] at h âŠ¢
  intro x
  simpa using h (x : E)

/-- The orthogonal projection onto `Ká—®` of an element of `K` is zero. -/
theorem orthogonalProjection_orthogonal_apply_eq_zero
    [Ká—®.HasOrthogonalProjection] {v : E} (hv : v âˆˆ K) : Ká—®.orthogonalProjection v = 0 :=
  orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero (K.le_orthogonal_orthogonal hv)

@[deprecated (since := "2025-07-22")] alias
  orthogonalProjection_mem_subspace_orthogonal_precomplement_eq_zero :=
  orthogonalProjection_orthogonal_apply_eq_zero

theorem starProjection_orthogonal_apply_eq_zero
    [Ká—®.HasOrthogonalProjection] {v : E} (hv : v âˆˆ K) :
    Ká—®.starProjection v = 0 := by
  rw [starProjection_apply, coe_eq_zero]
  exact orthogonalProjection_orthogonal_apply_eq_zero hv

/-- If `U â‰¤ V`, then projecting on `V` and then on `U` is the same as projecting on `U`. -/
theorem orthogonalProjection_starProjection_of_le {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U â‰¤ V) (x : E) :
    U.orthogonalProjection (V.starProjection x) = U.orthogonalProjection x :=
  Eq.symm <| by
    simpa only [sub_eq_zero, map_sub] using
      orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero
        (Submodule.orthogonal_le h (sub_starProjection_mem_orthogonal x))

theorem starProjection_comp_starProjection_of_le {U V : Submodule ğ•œ E}
    [U.HasOrthogonalProjection] [V.HasOrthogonalProjection] (h : U â‰¤ V) :
    U.starProjection âˆ˜L V.starProjection = U.starProjection := ContinuousLinearMap.ext fun _ => by
  nth_rw 1 [starProjection]
  simp [orthogonalProjection_starProjection_of_le h]

open ContinuousLinearMap in
theorem _root_.ContinuousLinearMap.IsIdempotentElem.hasOrthogonalProjection_range [CompleteSpace E]
    {p : E â†’L[ğ•œ] E} (hp : IsIdempotentElem p) : p.range.HasOrthogonalProjection :=
  have := hp.isClosed_range.completeSpace_coe
  .ofCompleteSpace _

open LinearMap in
theorem _root_.LinearMap.IsSymmetricProjection.hasOrthogonalProjection_range
    {p : E â†’â‚—[ğ•œ] E} (hp : p.IsSymmetricProjection) :
    (range p).HasOrthogonalProjection :=
  âŸ¨fun v => âŸ¨p v, by
    simp [hp.isIdempotentElem.isSymmetric_iff_orthogonal_range.mp hp.isSymmetric,
      â† Module.End.mul_apply, hp.isIdempotentElem.eq]âŸ©âŸ©

/-- The orthogonal projection onto `(ğ•œ âˆ™ v)á—®` of `v` is zero. -/
theorem orthogonalProjection_orthogonalComplement_singleton_eq_zero (v : E) :
    (ğ•œ âˆ™ v)á—®.orthogonalProjection v = 0 :=
  orthogonalProjection_orthogonal_apply_eq_zero
    (Submodule.mem_span_singleton_self v)

theorem starProjection_orthogonalComplement_singleton_eq_zero (v : E) :
    (ğ•œ âˆ™ v)á—®.starProjection v = 0 := by
  rw [starProjection_apply, coe_eq_zero]
  exact orthogonalProjection_orthogonalComplement_singleton_eq_zero v

/-- If the orthogonal projection to `K` is well-defined, then a vector splits as the sum of its
orthogonal projections onto a complete submodule `K` and onto the orthogonal complement of `K`. -/
theorem starProjection_add_starProjection_orthogonal [K.HasOrthogonalProjection]
    (w : E) : K.starProjection w + Ká—®.starProjection w = w := by
  simp

/-- The Pythagorean theorem, for an orthogonal projection. -/
theorem norm_sq_eq_add_norm_sq_projection (x : E) (S : Submodule ğ•œ E) [S.HasOrthogonalProjection] :
    â€–xâ€– ^ 2 = â€–S.orthogonalProjection xâ€– ^ 2 + â€–Sá—®.orthogonalProjection xâ€– ^ 2 :=
  calc
    â€–xâ€– ^ 2 = â€–S.starProjection x + Sá—®.starProjection xâ€– ^ 2 := by
      rw [starProjection_add_starProjection_orthogonal]
    _ = â€–S.orthogonalProjection xâ€– ^ 2 + â€–Sá—®.orthogonalProjection xâ€– ^ 2 := by
      simp only [sq]
      exact norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero _ _ <|
        (S.mem_orthogonal _).1 (Sá—®.orthogonalProjection x).2 _ (S.orthogonalProjection x).2

theorem norm_sq_eq_add_norm_sq_starProjection (x : E) (S : Submodule ğ•œ E)
    [S.HasOrthogonalProjection] :
    â€–xâ€– ^ 2 = â€–S.starProjection xâ€– ^ 2 + â€–Sá—®.starProjection xâ€– ^ 2 :=
  norm_sq_eq_add_norm_sq_projection x S

theorem mem_iff_norm_starProjection (U : Submodule ğ•œ E)
    [U.HasOrthogonalProjection] (v : E) :
    v âˆˆ U â†” â€–U.starProjection vâ€– = â€–vâ€– := by
  refine âŸ¨fun h => norm_starProjection_apply _ h, fun h => ?_âŸ©
  simpa [h, sub_eq_zero, eq_comm (a := v), starProjection_eq_self_iff] using
    U.norm_sq_eq_add_norm_sq_starProjection v

/-- In a complete space `E`, the projection maps onto a complete subspace `K` and its orthogonal
complement sum to the identity. -/
theorem id_eq_sum_starProjection_self_orthogonalComplement [K.HasOrthogonalProjection] :
    ContinuousLinearMap.id ğ•œ E =
      K.starProjection + Ká—®.starProjection := by
  ext w
  exact (K.starProjection_add_starProjection_orthogonal w).symm

-- The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_right [K.HasOrthogonalProjection] (u : K) (v : E) :
    âŸªK.orthogonalProjection v, uâŸ« = âŸªv, uâŸ« :=
  calc
    âŸªK.orthogonalProjection v, uâŸ« = âŸªK.starProjection v, uâŸ« := K.coe_inner _ _
    _ = âŸªK.starProjection v, uâŸ« + âŸªv - K.starProjection v, uâŸ« := by
      rw [starProjection_inner_eq_zero _ _ (Submodule.coe_mem _), add_zero]
    _ = âŸªv, uâŸ« := by rw [â† inner_add_left, add_sub_cancel]

-- The priority should be higher than `Submodule.coe_inner`.
@[simp high]
theorem inner_orthogonalProjection_eq_of_mem_left [K.HasOrthogonalProjection] (u : K) (v : E) :
    âŸªu, K.orthogonalProjection vâŸ« = âŸª(u : E), vâŸ« := by
  rw [â† inner_conj_symm, â† inner_conj_symm (u : E), inner_orthogonalProjection_eq_of_mem_right]

variable (K)

/-- The orthogonal projection is self-adjoint. -/
theorem inner_starProjection_left_eq_right [K.HasOrthogonalProjection] (u v : E) :
    âŸªK.starProjection u, vâŸ« = âŸªu, K.starProjection vâŸ« := by
  simp_rw [starProjection_apply, â† inner_orthogonalProjection_eq_of_mem_left,
    inner_orthogonalProjection_eq_of_mem_right]

/-- The orthogonal projection is symmetric. -/
theorem starProjection_isSymmetric [K.HasOrthogonalProjection] :
    (K.starProjection : E â†’â‚—[ğ•œ] E).IsSymmetric :=
  inner_starProjection_left_eq_right K

open ContinuousLinearMap in
/-- `U.starProjection` is a symmetric projection. -/
@[simp]
theorem isSymmetricProjection_starProjection
    (U : Submodule ğ•œ E) [U.HasOrthogonalProjection] :
    U.starProjection.IsSymmetricProjection :=
  âŸ¨U.isIdempotentElem_starProjection.toLinearMap, U.starProjection_isSymmetricâŸ©

open LinearMap in
/-- An operator is a symmetric projection if and only if it is an orthogonal projection. -/
theorem _root_.LinearMap.isSymmetricProjection_iff_eq_coe_starProjection_range {p : E â†’â‚—[ğ•œ] E} :
    p.IsSymmetricProjection â†” âˆƒ (_ : (LinearMap.range p).HasOrthogonalProjection),
    p = (LinearMap.range p).starProjection := by
  refine âŸ¨fun hp â†¦ ?_, fun âŸ¨h, hpâŸ© â†¦ hp â–¸ isSymmetricProjection_starProjection _âŸ©
  have : (LinearMap.range p).HasOrthogonalProjection := hp.hasOrthogonalProjection_range
  refine âŸ¨this, Eq.symm ?_âŸ©
  ext x
  refine Submodule.eq_starProjection_of_mem_orthogonal (by simp) ?_
  rw [hp.isIdempotentElem.isSymmetric_iff_orthogonal_range.mp hp.isSymmetric]
  simpa using congr($hp.isIdempotentElem.mul_one_sub_self x)

lemma _root_.LinearMap.isSymmetricProjection_iff_eq_coe_starProjection {p : E â†’â‚—[ğ•œ] E} :
    p.IsSymmetricProjection
      â†” âˆƒ (K : Submodule ğ•œ E) (_ : K.HasOrthogonalProjection), p = K.starProjection :=
  âŸ¨fun h â†¦ âŸ¨LinearMap.range p, p.isSymmetricProjection_iff_eq_coe_starProjection_range.mp hâŸ©,
    by rintro âŸ¨_, _, rflâŸ©; exact isSymmetricProjection_starProjection _âŸ©

theorem starProjection_apply_eq_zero_iff [K.HasOrthogonalProjection] {v : E} :
    K.starProjection v = 0 â†” v âˆˆ Ká—® := by
  refine âŸ¨fun h w hw => ?_, fun hv => ?_âŸ©
  Â· rw [â† starProjection_eq_self_iff.mpr hw, inner_starProjection_left_eq_right, h,
      inner_zero_right]
  Â· simp [starProjection_apply, orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv]

open RCLike

lemma re_inner_starProjection_eq_normSq [K.HasOrthogonalProjection] (v : E) :
    re âŸªK.starProjection v, vâŸ« = â€–K.orthogonalProjection vâ€– ^ 2 := by
  rw [starProjection_apply,
    re_inner_eq_norm_mul_self_add_norm_mul_self_sub_norm_sub_mul_self_div_two,
    div_eq_iff (NeZero.ne' 2).symm, pow_two, add_sub_assoc, â† eq_sub_iff_add_eq', coe_norm,
    â† mul_sub_one, show (2 : â„) - 1 = 1 by norm_num, mul_one, sub_eq_iff_eq_add', norm_sub_rev]
  exact orthogonalProjectionFn_norm_sq K v

lemma re_inner_starProjection_nonneg [K.HasOrthogonalProjection] (v : E) :
    0 â‰¤ re âŸªK.starProjection v, vâŸ« := by
  rw [re_inner_starProjection_eq_normSq K v]
  exact sq_nonneg â€–K.orthogonalProjection vâ€–

end

end Submodule
