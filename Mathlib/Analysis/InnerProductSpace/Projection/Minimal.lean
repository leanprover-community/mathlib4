/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Existence of minimizers (Hilbert projection theorem)

This file shows the existence of minimizers (also known as the Hilbert projection theorem).
This is the key tool that is used to define `Submodule.orthogonalProjection` in
`Mathlib/Analysis/InnerProductSpace/Projection/Basic.lean`.
-/

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [NormedAddCommGroup F]
variable [InnerProductSpace ğ•œ E] [InnerProductSpace â„ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y
local notation "absR" => @abs â„ _ _

open Topology RCLike Real Filter InnerProductSpace

/-- **Existence of minimizers**, aka the **Hilbert projection theorem**.

Let `u` be a point in a real inner product space, and let `K` be a nonempty complete convex subset.
Then there exists a (unique) `v` in `K` that minimizes the distance `â€–u - vâ€–` to `u`. -/
theorem exists_norm_eq_iInf_of_complete_convex {K : Set F} (ne : K.Nonempty) (hâ‚ : IsComplete K)
    (hâ‚‚ : Convex â„ K) : âˆ€ u : F, âˆƒ v âˆˆ K, â€–u - vâ€– = â¨… w : K, â€–u - wâ€– := fun u => by
  let Î´ := â¨… w : K, â€–u - wâ€–
  letI : Nonempty K := ne.to_subtype
  have zero_le_Î´ : 0 â‰¤ Î´ := le_ciInf fun _ => norm_nonneg _
  have Î´_le : âˆ€ w : K, Î´ â‰¤ â€–u - wâ€– := ciInf_le âŸ¨0, Set.forall_mem_range.2 fun _ => norm_nonneg _âŸ©
  have Î´_le' : âˆ€ w âˆˆ K, Î´ â‰¤ â€–u - wâ€– := fun w hw => Î´_le âŸ¨w, hwâŸ©
  -- Step 1: since `Î´` is the infimum, can find a sequence `w : â„• â†’ K` in `K`
  -- such that `â€–u - w nâ€– < Î´ + 1 / (n + 1)` (which implies `â€–u - w nâ€– --> Î´`);
  -- maybe this should be a separate lemma
  have exists_seq : âˆƒ w : â„• â†’ K, âˆ€ n, â€–u - w nâ€– < Î´ + 1 / (n + 1) := by
    have hÎ´ : âˆ€ n : â„•, Î´ < Î´ + 1 / (n + 1) := fun n =>
      lt_add_of_le_of_pos le_rfl Nat.one_div_pos_of_nat
    have h := fun n => exists_lt_of_ciInf_lt (hÎ´ n)
    let w : â„• â†’ K := fun n => Classical.choose (h n)
    exact âŸ¨w, fun n => Classical.choose_spec (h n)âŸ©
  rcases exists_seq with âŸ¨w, hwâŸ©
  have norm_tendsto : Tendsto (fun n => â€–u - w nâ€–) atTop (ğ“ Î´) := by
    have h : Tendsto (fun _ : â„• => Î´) atTop (ğ“ Î´) := tendsto_const_nhds
    have h' : Tendsto (fun n : â„• => Î´ + 1 / (n + 1)) atTop (ğ“ Î´) := by
      convert h.add tendsto_one_div_add_atTop_nhds_zero_nat
      simp only [add_zero]
    exact tendsto_of_tendsto_of_tendsto_of_le_of_le h h' (fun x => Î´_le _) fun x => le_of_lt (hw _)
  -- Step 2: Prove that the sequence `w : â„• â†’ K` is a Cauchy sequence
  have seq_is_cauchy : CauchySeq fun n => (w n : F) := by
    rw [cauchySeq_iff_le_tendsto_0]
    -- splits into three goals
    let b := fun n : â„• => 8 * Î´ * (1 / (n + 1)) + 4 * (1 / (n + 1)) * (1 / (n + 1))
    use fun n => âˆš(b n)
    constructor
    -- first goal :  `âˆ€ (n : â„•), 0 â‰¤ âˆš(b n)`
    Â· intro n
      exact sqrt_nonneg _
    constructor
    -- second goal : `âˆ€ (n m N : â„•), N â‰¤ n â†’ N â‰¤ m â†’ dist â†‘(w n) â†‘(w m) â‰¤ âˆš(b N)`
    Â· intro p q N hp hq
      let wp := (w p : F)
      let wq := (w q : F)
      let a := u - wq
      let b := u - wp
      let half := 1 / (2 : â„)
      let div := 1 / ((N : â„) + 1)
      have :
        4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– + â€–wp - wqâ€– * â€–wp - wqâ€– =
          2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) :=
        calc
          4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– + â€–wp - wqâ€– * â€–wp - wqâ€– =
              2 * â€–u - half â€¢ (wq + wp)â€– * (2 * â€–u - half â€¢ (wq + wp)â€–) + â€–wp - wqâ€– * â€–wp - wqâ€– :=
            by ring
          _ =
              absR 2 * â€–u - half â€¢ (wq + wp)â€– * (absR 2 * â€–u - half â€¢ (wq + wp)â€–) +
                â€–wp - wqâ€– * â€–wp - wqâ€– := by
            rw [abs_of_nonneg]
            exact zero_le_two
          _ =
              â€–(2 : â„) â€¢ (u - half â€¢ (wq + wp))â€– * â€–(2 : â„) â€¢ (u - half â€¢ (wq + wp))â€– +
                â€–wp - wqâ€– * â€–wp - wqâ€– := by simp [norm_smul]
          _ = â€–a + bâ€– * â€–a + bâ€– + â€–a - bâ€– * â€–a - bâ€– := by
            rw [smul_sub, smul_smul, mul_one_div_cancel (_root_.two_ne_zero : (2 : â„) â‰  0), â†
              one_add_one_eq_two, add_smul]
            simp only [one_smul]
            have eqâ‚ : wp - wq = a - b := (sub_sub_sub_cancel_left _ _ _).symm
            have eqâ‚‚ : u + u - (wq + wp) = a + b := by
              change u + u - (wq + wp) = u - wq + (u - wp)
              abel
            rw [eqâ‚, eqâ‚‚]
          _ = 2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) := parallelogram_law_with_norm â„ _ _
      have eq : Î´ â‰¤ â€–u - half â€¢ (wq + wp)â€– := by
        rw [smul_add]
        apply Î´_le'
        apply hâ‚‚
        repeat' exact Subtype.mem _
        repeat' exact le_of_lt one_half_pos
        exact add_halves 1
      have eqâ‚‚ : â€–aâ€– â‰¤ Î´ + div := by grw [hw, Nat.one_div_le_one_div hq]
      have eqâ‚‚' : â€–bâ€– â‰¤ Î´ + div := by grw [hw, Nat.one_div_le_one_div hp]
      rw [dist_eq_norm]
      apply nonneg_le_nonneg_of_sq_le_sq
      Â· exact sqrt_nonneg _
      rw [mul_self_sqrt]
      Â· calc
        â€–wp - wqâ€– * â€–wp - wqâ€– =
            2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) - 4 * â€–u - half â€¢ (wq + wp)â€– * â€–u - half â€¢ (wq + wp)â€– := by
          simp [â† this]
        _ â‰¤ 2 * (â€–aâ€– * â€–aâ€– + â€–bâ€– * â€–bâ€–) - 4 * Î´ * Î´ := by gcongr
        _ â‰¤ 2 * ((Î´ + div) * (Î´ + div) + (Î´ + div) * (Î´ + div)) - 4 * Î´ * Î´ := by gcongr
        _ = 8 * Î´ * div + 4 * div * div := by ring
      positivity
    -- third goal : `Tendsto (fun (n : â„•) => âˆš(b n)) atTop (ğ“ 0)`
    suffices Tendsto (fun x â†¦ âˆš(8 * Î´ * x + 4 * x * x) : â„ â†’ â„) (ğ“ 0) (ğ“ 0)
      from this.comp tendsto_one_div_add_atTop_nhds_zero_nat
    exact Continuous.tendsto' (by fun_prop) _ _ (by simp)
  -- Step 3: By completeness of `K`, let `w : â„• â†’ K` converge to some `v : K`.
  -- Prove that it satisfies all requirements.
  rcases cauchySeq_tendsto_of_isComplete hâ‚ (fun n => Subtype.mem _) seq_is_cauchy with
    âŸ¨v, hv, w_tendstoâŸ©
  use v
  use hv
  have h_cont : Continuous fun v => â€–u - vâ€– :=
    Continuous.comp continuous_norm (Continuous.sub continuous_const continuous_id)
  have : Tendsto (fun n => â€–u - w nâ€–) atTop (ğ“ â€–u - vâ€–) := by
    convert Tendsto.comp h_cont.continuousAt w_tendsto
  exact tendsto_nhds_unique this norm_tendsto

/-- Characterization of minimizers for the projection on a convex set in a real inner product
space. -/
theorem norm_eq_iInf_iff_real_inner_le_zero {K : Set F} (h : Convex â„ K) {u : F} {v : F}
    (hv : v âˆˆ K) : (â€–u - vâ€– = â¨… w : K, â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
  letI : Nonempty K := âŸ¨âŸ¨v, hvâŸ©âŸ©
  constructor
  Â· intro eq w hw
    let Î´ := â¨… w : K, â€–u - wâ€–
    let p := âŸªu - v, w - vâŸ«_â„
    let q := â€–w - vâ€– ^ 2
    have Î´_le (w : K) : Î´ â‰¤ â€–u - wâ€– := ciInf_le âŸ¨0, fun _ âŸ¨_, hâŸ© => h â–¸ norm_nonneg _âŸ© _
    have Î´_le' (w) (hw : w âˆˆ K) : Î´ â‰¤ â€–u - wâ€– := Î´_le âŸ¨w, hwâŸ©
    have (Î¸ : â„) (hÎ¸â‚ : 0 < Î¸) (hÎ¸â‚‚ : Î¸ â‰¤ 1) : 2 * p â‰¤ Î¸ * q := by
      have : â€–u - vâ€– ^ 2 â‰¤ â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 :=
        calc â€–u - vâ€– ^ 2
          _ â‰¤ â€–u - (Î¸ â€¢ w + (1 - Î¸) â€¢ v)â€– ^ 2 := by
            simp only [sq]; apply mul_self_le_mul_self (norm_nonneg _)
            rw [eq]; apply Î´_le'
            apply h hw hv
            exacts [le_of_lt hÎ¸â‚, sub_nonneg.2 hÎ¸â‚‚, add_sub_cancel _ _]
          _ = â€–u - v - Î¸ â€¢ (w - v)â€– ^ 2 := by
            have : u - (Î¸ â€¢ w + (1 - Î¸) â€¢ v) = u - v - Î¸ â€¢ (w - v) := by
              rw [smul_sub, sub_smul, one_smul]
              simp only [sub_eq_add_neg, add_comm, add_left_comm, add_assoc, neg_add_rev]
            rw [this]
          _ = â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 := by
            rw [@norm_sub_sq â„, inner_smul_right, norm_smul]
            simp only [sq]
            change
              â€–u - vâ€– * â€–u - vâ€– - 2 * (Î¸ * âŸªu - v, w - vâŸ«_â„) +
                absR Î¸ * â€–w - vâ€– * (absR Î¸ * â€–w - vâ€–) =
              â€–u - vâ€– * â€–u - vâ€– - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * (â€–w - vâ€– * â€–w - vâ€–)
            rw [abs_of_pos hÎ¸â‚]; ring
      have eqâ‚ :
        â€–u - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ + Î¸ * Î¸ * â€–w - vâ€– ^ 2 =
          â€–u - vâ€– ^ 2 + (Î¸ * Î¸ * â€–w - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„) := by
        abel
      rw [eqâ‚, le_add_iff_nonneg_right] at this
      have eqâ‚‚ :
        Î¸ * Î¸ * â€–w - vâ€– ^ 2 - 2 * Î¸ * âŸªu - v, w - vâŸ«_â„ =
          Î¸ * (Î¸ * â€–w - vâ€– ^ 2 - 2 * âŸªu - v, w - vâŸ«_â„) := by ring
      rw [eqâ‚‚] at this
      exact le_of_sub_nonneg (nonneg_of_mul_nonneg_right this hÎ¸â‚)
    by_cases hq : q = 0
    Â· rw [hq] at this
      have : p â‰¤ 0 := by
        have := this (1 : â„) (by simp) (by simp)
        linarith
      exact this
    Â· have q_pos : 0 < q := lt_of_le_of_ne (sq_nonneg _) fun h â†¦ hq h.symm
      by_contra hp
      rw [not_le] at hp
      let Î¸ := min (1 : â„) (p / q)
      have eqâ‚ : Î¸ * q â‰¤ p :=
        calc
          Î¸ * q â‰¤ p / q * q := mul_le_mul_of_nonneg_right (min_le_right _ _) (sq_nonneg _)
          _ = p := div_mul_cancelâ‚€ _ hq
      have : 2 * p â‰¤ p :=
        calc
          2 * p â‰¤ Î¸ * q := by
            exact this Î¸ (lt_min (by simp) (div_pos hp q_pos)) (by simp [Î¸])
          _ â‰¤ p := eqâ‚
      linarith
  Â· intro h
    apply le_antisymm
    Â· apply le_ciInf
      intro w
      apply nonneg_le_nonneg_of_sq_le_sq (norm_nonneg _)
      have := h w w.2
      calc
        â€–u - vâ€– * â€–u - vâ€– â‰¤ â€–u - vâ€– * â€–u - vâ€– - 2 * âŸªu - v, w - vâŸ«_â„ := by linarith
        _ â‰¤ â€–u - vâ€– ^ 2 - 2 * âŸªu - v, w - vâŸ«_â„ + â€–w - vâ€– ^ 2 := by
          rw [sq]
          refine le_add_of_nonneg_right ?_
          exact sq_nonneg _
        _ = â€–u - v - (w - v)â€– ^ 2 := (@norm_sub_sq â„ _ _ _ _ _ _).symm
        _ = â€–u - wâ€– * â€–u - wâ€– := by
          have : u - v - (w - v) = u - w := by abel
          rw [this, sq]
    Â· change â¨… w : K, â€–u - wâ€– â‰¤ (fun w : K => â€–u - wâ€–) âŸ¨v, hvâŸ©
      apply ciInf_le
      use 0
      rintro y âŸ¨z, rflâŸ©
      exact norm_nonneg _

namespace Submodule

variable (K : Submodule ğ•œ E)

/-- Existence of projections on complete subspaces.
Let `u` be a point in an inner product space, and let `K` be a nonempty complete subspace.
Then there exists a (unique) `v` in `K` that minimizes the distance `â€–u - vâ€–` to `u`.
This point `v` is usually called the orthogonal projection of `u` onto `K`.
-/
theorem exists_norm_eq_iInf_of_complete_subspace (h : IsComplete (â†‘K : Set E)) :
    âˆ€ u : E, âˆƒ v âˆˆ K, â€–u - vâ€– = â¨… w : (K : Set E), â€–u - wâ€– := by
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : Module â„ E := RestrictScalars.module â„ ğ•œ E
  let K' : Submodule â„ E := Submodule.restrictScalars â„ K
  exact exists_norm_eq_iInf_of_complete_convex âŸ¨0, K'.zero_memâŸ© h K'.convex

/-- Characterization of minimizers in the projection on a subspace, in the real case.
Let `u` be a point in a real inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `â€–u - vâ€–` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`).
This is superseded by `norm_eq_iInf_iff_inner_eq_zero` that gives the same conclusion over
any `RCLike` field.
-/
theorem norm_eq_iInf_iff_real_inner_eq_zero (K : Submodule â„ F) {u : F} {v : F} (hv : v âˆˆ K) :
    (â€–u - vâ€– = â¨… w : (â†‘K : Set F), â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ«_â„ = 0 :=
  Iff.intro
    (by
      intro h
      have h : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
        rwa [norm_eq_iInf_iff_real_inner_le_zero] at h
        exacts [K.convex, hv]
      intro w hw
      have le : âŸªu - v, wâŸ«_â„ â‰¤ 0 := by
        let w' := w + v
        have : w' âˆˆ K := Submodule.add_mem _ hw hv
        have hâ‚ := h w' this
        have hâ‚‚ : w' - v = w := by
          simp only [w', add_neg_cancel_right, sub_eq_add_neg]
        rw [hâ‚‚] at hâ‚
        exact hâ‚
      have ge : âŸªu - v, wâŸ«_â„ â‰¥ 0 := by
        let w'' := -w + v
        have : w'' âˆˆ K := Submodule.add_mem _ (Submodule.neg_mem _ hw) hv
        have hâ‚ := h w'' this
        have hâ‚‚ : w'' - v = -w := by
          simp only [w'', add_neg_cancel_right, sub_eq_add_neg]
        rw [hâ‚‚, inner_neg_right] at hâ‚
        linarith
      exact le_antisymm le ge)
    (by
      intro h
      have : âˆ€ w âˆˆ K, âŸªu - v, w - vâŸ«_â„ â‰¤ 0 := by
        intro w hw
        let w' := w - v
        have : w' âˆˆ K := Submodule.sub_mem _ hw hv
        have hâ‚ := h w' this
        exact le_of_eq hâ‚
      rwa [norm_eq_iInf_iff_real_inner_le_zero]
      exacts [Submodule.convex _, hv])

/-- Characterization of minimizers in the projection on a subspace.
Let `u` be a point in an inner product space, and let `K` be a nonempty subspace.
Then point `v` minimizes the distance `â€–u - vâ€–` over points in `K` if and only if
for all `w âˆˆ K`, `âŸªu - v, wâŸ« = 0` (i.e., `u - v` is orthogonal to the subspace `K`)
-/
theorem norm_eq_iInf_iff_inner_eq_zero {u : E} {v : E} (hv : v âˆˆ K) :
    (â€–u - vâ€– = â¨… w : K, â€–u - wâ€–) â†” âˆ€ w âˆˆ K, âŸªu - v, wâŸ« = 0 := by
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ğ•œ E
  letI : Module â„ E := RestrictScalars.module â„ ğ•œ E
  let K' : Submodule â„ E := K.restrictScalars â„
  constructor
  Â· intro H
    have A : âˆ€ w âˆˆ K, re âŸªu - v, wâŸ« = 0 := (K'.norm_eq_iInf_iff_real_inner_eq_zero hv).1 H
    intro w hw
    apply RCLike.ext
    Â· simp [A w hw]
    Â· symm
      calc
        im (0 : ğ•œ) = 0 := im.map_zero
        _ = re âŸªu - v, (-I : ğ•œ) â€¢ wâŸ« := (A _ (K.smul_mem (-I) hw)).symm
        _ = re (-I * âŸªu - v, wâŸ«) := by rw [inner_smul_right]
        _ = im âŸªu - v, wâŸ« := by simp
  Â· intro H
    have : âˆ€ w âˆˆ K', âŸªu - v, wâŸ«_â„ = 0 := by
      intro w hw
      rw [real_inner_eq_re_inner, H w hw]
      exact zero_re
    exact (K'.norm_eq_iInf_iff_real_inner_eq_zero hv).2 this

end Submodule
