/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Rayleigh
import Mathlib.Analysis.InnerProductSpace.PiL2
import Mathlib.Algebra.DirectSum.Decomposition
import Mathlib.LinearAlgebra.Eigenspace.Minpoly
import Mathlib.Data.Fin.Tuple.Sort

/-! # Spectral theory of self-adjoint operators

This file covers the spectral theory of self-adjoint operators on an inner product space.

The first part of the file covers general properties, true without any condition on boundedness or
compactness of the operator or finite-dimensionality of the underlying space, notably:
* `LinearMap.IsSymmetric.conj_eigenvalue_eq_self`: the eigenvalues are real
* `LinearMap.IsSymmetric.orthogonalFamily_eigenspaces`: the eigenspaces are orthogonal
* `LinearMap.IsSymmetric.orthogonalComplement_iSup_eigenspaces`: the restriction of the operator to
  the mutual orthogonal complement of the eigenspaces has, itself, no eigenvectors

The second part of the file covers properties of self-adjoint operators in finite dimension.
Letting `T` be a self-adjoint operator on a finite-dimensional inner product space `T`,
* The definition `LinearMap.IsSymmetric.diagonalization` provides a linear isometry equivalence `E`
  to the direct sum of the eigenspaces of `T`.  The theorem
  `LinearMap.IsSymmetric.diagonalization_apply_self_apply` states that, when `T` is transferred via
  this equivalence to an operator on the direct sum, it acts diagonally.
* The definition `LinearMap.IsSymmetric.eigenvectorBasis` provides an orthonormal basis for `E`
  consisting of eigenvectors of `T`, with `LinearMap.IsSymmetric.eigenvalues` giving the
  corresponding list of eigenvalues, as real numbers.  The definition
  `LinearMap.IsSymmetric.eigenvectorBasis` gives the associated linear isometry equivalence
  from `E` to Euclidean space, and the theorem
  `LinearMap.IsSymmetric.eigenvectorBasis_apply_self_apply` states that, when `T` is
  transferred via this equivalence to an operator on Euclidean space, it acts diagonally.
* `LinearMap.IsSymmetric.eigenvalues` gives the eigenvalues in decreasing order.  This is
  done for several reasons: (i) This agrees with the standard convention of listing singular
  values in decreasing order, with the operator norm as the first singular value
  (ii) For positive compact operators on an infinite dimensional space, one can list the nonzero
  eigenvalues in decreasing (but not increasing) order since they converge to zero. (iii) This
  simplifies several theorem statements. For example the Schur-Horn theorem states that the diagonal
  of the matrix representation of a selfadjoint linear map is majorized by the eigenvalue sequence
  listed in decreasing order.

These are forms of the *diagonalization theorem* for self-adjoint operators on finite-dimensional
inner product spaces.

## TODO

Spectral theory for compact self-adjoint operators, bounded self-adjoint operators.

## Tags

self-adjoint operator, spectral theorem, diagonalization theorem

-/

variable {ùïú : Type*} [RCLike ùïú]
variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace ùïú E]

local notation "‚ü™" x ", " y "‚ü´" => inner ùïú x y

open scoped ComplexConjugate

open Module.End

namespace LinearMap

namespace IsSymmetric

variable {T : E ‚Üí‚Çó[ùïú] E}

/-- A self-adjoint operator preserves orthogonal complements of its eigenspaces. -/
theorem invariant_orthogonalComplement_eigenspace (hT : T.IsSymmetric) (Œº : ùïú)
    (v : E) (hv : v ‚àà (eigenspace T Œº)·óÆ) : T v ‚àà (eigenspace T Œº)·óÆ := by
  intro w hw
  have : T w = (Œº : ùïú) ‚Ä¢ w := by rwa [mem_eigenspace_iff] at hw
  simp [‚Üê hT w, this, inner_smul_left, hv w hw]

/-- The eigenvalues of a self-adjoint operator are real. -/
theorem conj_eigenvalue_eq_self (hT : T.IsSymmetric) {Œº : ùïú} (hŒº : HasEigenvalue T Œº) :
    conj Œº = Œº := by
  obtain ‚ü®v, hv‚ÇÅ, hv‚ÇÇ‚ü© := hŒº.exists_hasEigenvector
  rw [mem_eigenspace_iff] at hv‚ÇÅ
  simpa [hv‚ÇÇ, inner_smul_left, inner_smul_right, hv‚ÇÅ] using hT v v

/-- The eigenspaces of a self-adjoint operator are mutually orthogonal. -/
theorem orthogonalFamily_eigenspaces (hT : T.IsSymmetric) :
    OrthogonalFamily ùïú (fun Œº ‚Ü¶ eigenspace T Œº) fun Œº ‚Ü¶ (eigenspace T Œº).subtype‚Çó·µ¢ := by
  rintro Œº ŒΩ hŒºŒΩ ‚ü®v, hv‚ü© ‚ü®w, hw‚ü©
  by_cases hv' : v = 0
  ¬∑ simp [hv']
  have H := hT.conj_eigenvalue_eq_self (hasEigenvalue_of_hasEigenvector ‚ü®hv, hv'‚ü©)
  rw [mem_eigenspace_iff] at hv hw
  refine Or.resolve_left ?_ hŒºŒΩ.symm
  simpa [inner_smul_left, inner_smul_right, hv, hw, H] using (hT v w).symm

theorem orthogonalFamily_eigenspaces' (hT : T.IsSymmetric) :
    OrthogonalFamily ùïú (fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº) fun Œº ‚Ü¶
      (eigenspace T Œº).subtype‚Çó·µ¢ :=
  hT.orthogonalFamily_eigenspaces.comp Subtype.coe_injective

/-- The mutual orthogonal complement of the eigenspaces of a self-adjoint operator on an inner
product space is an invariant subspace of the operator. -/
theorem orthogonalComplement_iSup_eigenspaces_invariant (hT : T.IsSymmetric)
    ‚¶Év : E‚¶Ñ (hv : v ‚àà (‚®Ü Œº, eigenspace T Œº)·óÆ) : T v ‚àà (‚®Ü Œº, eigenspace T Œº)·óÆ := by
  rw [‚Üê Submodule.iInf_orthogonal] at hv ‚ä¢
  exact T.iInf_invariant hT.invariant_orthogonalComplement_eigenspace v hv

/-- The mutual orthogonal complement of the eigenspaces of a self-adjoint operator on an inner
product space has no eigenvalues. -/
theorem orthogonalComplement_iSup_eigenspaces (hT : T.IsSymmetric) (Œº : ùïú) :
    eigenspace (T.restrict hT.orthogonalComplement_iSup_eigenspaces_invariant) Œº = ‚ä• := by
  set p : Submodule ùïú E := (‚®Ü Œº, eigenspace T Œº)·óÆ
  refine eigenspace_restrict_eq_bot hT.orthogonalComplement_iSup_eigenspaces_invariant ?_
  have H‚ÇÇ : eigenspace T Œº ‚üÇ p := (Submodule.isOrtho_orthogonal_right _).mono_left (le_iSup _ _)
  exact H‚ÇÇ.disjoint

/-! ### Finite-dimensional theory -/

variable [FiniteDimensional ùïú E]

/-- The mutual orthogonal complement of the eigenspaces of a self-adjoint operator on a
finite-dimensional inner product space is trivial. -/
theorem orthogonalComplement_iSup_eigenspaces_eq_bot (hT : T.IsSymmetric) :
    (‚®Ü Œº, eigenspace T Œº)·óÆ = ‚ä• := by
  have hT' : IsSymmetric _ :=
    hT.restrict_invariant hT.orthogonalComplement_iSup_eigenspaces_invariant
  -- a self-adjoint operator on a nontrivial inner product space has an eigenvalue
  haveI :=
    hT'.subsingleton_of_no_eigenvalue_finiteDimensional hT.orthogonalComplement_iSup_eigenspaces
  exact Submodule.eq_bot_of_subsingleton

theorem orthogonalComplement_iSup_eigenspaces_eq_bot' (hT : T.IsSymmetric) :
    (‚®Ü Œº : Eigenvalues T, eigenspace T Œº)·óÆ = ‚ä• :=
  show (‚®Ü Œº : { Œº // eigenspace T Œº ‚â† ‚ä• }, eigenspace T Œº)·óÆ = ‚ä• by
    rw [iSup_ne_bot_subtype, hT.orthogonalComplement_iSup_eigenspaces_eq_bot]

/-- The eigenspaces of a self-adjoint operator on a finite-dimensional inner product space `E` gives
an internal direct sum decomposition of `E`.

Note this takes `hT` as a `Fact` to allow it to be an instance. -/
noncomputable instance directSumDecomposition [hT : Fact T.IsSymmetric] :
    DirectSum.Decomposition fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº :=
  haveI h : ‚àÄ Œº : Eigenvalues T, CompleteSpace (eigenspace T Œº) := fun Œº ‚Ü¶ by infer_instance
  hT.out.orthogonalFamily_eigenspaces'.decomposition
    (Submodule.orthogonal_eq_bot_iff.mp hT.out.orthogonalComplement_iSup_eigenspaces_eq_bot')

theorem directSum_decompose_apply [_hT : Fact T.IsSymmetric] (x : E) (Œº : Eigenvalues T) :
    DirectSum.decompose (fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº) x Œº =
      (eigenspace T Œº).orthogonalProjection x :=
  rfl

/-- The eigenspaces of a self-adjoint operator on a finite-dimensional inner product space `E` gives
an internal direct sum decomposition of `E`. -/
theorem direct_sum_isInternal (hT : T.IsSymmetric) :
    DirectSum.IsInternal fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº :=
  hT.orthogonalFamily_eigenspaces'.isInternal_iff.mpr
    hT.orthogonalComplement_iSup_eigenspaces_eq_bot'

section Version1

/-- Isometry from an inner product space `E` to the direct sum of the eigenspaces of some
self-adjoint operator `T` on `E`. -/
noncomputable def diagonalization (hT : T.IsSymmetric) : E ‚âÉ‚Çó·µ¢[ùïú] PiLp 2 fun Œº :
    Eigenvalues T ‚Ü¶ eigenspace T Œº :=
  hT.direct_sum_isInternal.isometryL2OfOrthogonalFamily hT.orthogonalFamily_eigenspaces'

@[simp]
theorem diagonalization_symm_apply (hT : T.IsSymmetric)
    (w : PiLp 2 fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº) :
    hT.diagonalization.symm w = ‚àë Œº, w Œº :=
  hT.direct_sum_isInternal.isometryL2OfOrthogonalFamily_symm_apply
    hT.orthogonalFamily_eigenspaces' w

/-- *Diagonalization theorem*, *spectral theorem*; version 1: A self-adjoint operator `T` on a
finite-dimensional inner product space `E` acts diagonally on the decomposition of `E` into the
direct sum of the eigenspaces of `T`. -/
theorem diagonalization_apply_self_apply (hT : T.IsSymmetric) (v : E) (Œº : Eigenvalues T) :
    hT.diagonalization (T v) Œº = (Œº : ùïú) ‚Ä¢ hT.diagonalization v Œº := by
  suffices
    ‚àÄ w : PiLp 2 fun Œº : Eigenvalues T ‚Ü¶ eigenspace T Œº,
      T (hT.diagonalization.symm w) = hT.diagonalization.symm fun Œº ‚Ü¶ (Œº : ùïú) ‚Ä¢ w Œº by
    simpa only [LinearIsometryEquiv.symm_apply_apply, LinearIsometryEquiv.apply_symm_apply] using
      congr_arg (fun w ‚Ü¶ hT.diagonalization w Œº) (this (hT.diagonalization v))
  intro w
  have hwT : ‚àÄ Œº, T (w Œº) = (Œº : ùïú) ‚Ä¢ w Œº := fun Œº ‚Ü¶ mem_eigenspace_iff.1 (w Œº).2
  simp only [hwT, diagonalization_symm_apply, map_sum, Submodule.coe_smul_of_tower]

end Version1

section Version2

variable {n : ‚Ñï}

/-- Unsorted eigenvalues and eigenvectors.  These private definitions should not be used directly.
Instead use the functions eigenvalues and eigenvectorBasis defined below. -/
private noncomputable def unsortedEigenvalues (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n)
    (i : Fin n) : ‚Ñù :=
  @RCLike.re ùïú _ <| (hT.direct_sum_isInternal.subordinateOrthonormalBasisIndex hn i
    hT.orthogonalFamily_eigenspaces').val

private noncomputable def unsortedEigenvectorBasis (hT : T.IsSymmetric)
    (hn : Module.finrank ùïú E = n) : OrthonormalBasis (Fin n) ùïú E :=
  hT.direct_sum_isInternal.subordinateOrthonormalBasis hn hT.orthogonalFamily_eigenspaces'

private theorem hasEigenvector_eigenvectorBasis_helper (hT : T.IsSymmetric)
    (hn : Module.finrank ùïú E = n) (i : Fin n) :
    HasEigenvector T (hT.unsortedEigenvalues hn i) (hT.unsortedEigenvectorBasis hn i) := by
  let v : E := hT.unsortedEigenvectorBasis hn i
  let Œº : ùïú :=
    (hT.direct_sum_isInternal.subordinateOrthonormalBasisIndex hn i
      hT.orthogonalFamily_eigenspaces').val
  simp_rw [unsortedEigenvalues]
  change HasEigenvector T (RCLike.re Œº) v
  have key : HasEigenvector T Œº v := by
    have H‚ÇÅ : v ‚àà eigenspace T Œº := by
      simp_rw [v, unsortedEigenvectorBasis]
      exact
        hT.direct_sum_isInternal.subordinateOrthonormalBasis_subordinate hn i
          hT.orthogonalFamily_eigenspaces'
    have H‚ÇÇ : v ‚â† 0 := by simpa using (hT.unsortedEigenvectorBasis hn).toBasis.ne_zero i
    exact ‚ü®H‚ÇÅ, H‚ÇÇ‚ü©
  have re_Œº : ‚Üë(RCLike.re Œº) = Œº := by
    rw [‚Üê RCLike.conj_eq_iff_re]
    exact hT.conj_eigenvalue_eq_self (hasEigenvalue_of_hasEigenvector key)
  simpa [re_Œº] using key

/-- The eigenvalues for a self-adjoint operator `T` on a
finite-dimensional inner product space `E`, sorted in decreasing order -/
noncomputable irreducible_def eigenvalues (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n) :
    Fin n ‚Üí ‚Ñù :=
  (hT.unsortedEigenvalues hn) ‚àò Tuple.sort (hT.unsortedEigenvalues hn) ‚àò @Fin.revPerm n

/-- A choice of orthonormal basis of eigenvectors for self-adjoint operator `T` on a
finite-dimensional inner product space `E`.  Eigenvectors are sorted in decreasing
order of their eigenvalues. -/
noncomputable irreducible_def eigenvectorBasis (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n) :
    OrthonormalBasis (Fin n) ùïú E :=
  (hT.direct_sum_isInternal.subordinateOrthonormalBasis
    hn hT.orthogonalFamily_eigenspaces').reindex
      (Tuple.sort (hT.unsortedEigenvalues hn) * @Fin.revPerm n).symm

theorem hasEigenvector_eigenvectorBasis (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n)
    (i : Fin n) : HasEigenvector T (hT.eigenvalues hn i) (hT.eigenvectorBasis hn i) := by
  rw [eigenvalues_def, eigenvectorBasis_def, OrthonormalBasis.reindex_apply]
  apply hasEigenvector_eigenvectorBasis_helper

/-- Eigenvalues are sorted in decreasing order. -/
theorem eigenvalues_antitone (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n) :
    Antitone (hT.eigenvalues hn) := by
  rw [eigenvalues_def, ‚Üê Function.comp_assoc]
  refine Monotone.comp_antitone ?_ ?_
  ¬∑ apply Tuple.monotone_sort
  intro _ _ h
  exact Fin.rev_le_rev.mpr h

theorem hasEigenvalue_eigenvalues (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n) (i : Fin n) :
    HasEigenvalue T (hT.eigenvalues hn i) :=
  Module.End.hasEigenvalue_of_hasEigenvector (hT.hasEigenvector_eigenvectorBasis hn i)

@[simp]
theorem apply_eigenvectorBasis (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n) (i : Fin n) :
    T (hT.eigenvectorBasis hn i) = (hT.eigenvalues hn i : ùïú) ‚Ä¢ hT.eigenvectorBasis hn i :=
  mem_eigenspace_iff.mp (hT.hasEigenvector_eigenvectorBasis hn i).1

/-- *Diagonalization theorem*, *spectral theorem*; version 2: A self-adjoint operator `T` on a
finite-dimensional inner product space `E` acts diagonally on the identification of `E` with
Euclidean space induced by an orthonormal basis of eigenvectors of `T`. -/
theorem eigenvectorBasis_apply_self_apply (hT : T.IsSymmetric) (hn : Module.finrank ùïú E = n)
    (v : E) (i : Fin n) :
    (hT.eigenvectorBasis hn).repr (T v) i =
      hT.eigenvalues hn i * (hT.eigenvectorBasis hn).repr v i := by
  suffices
    ‚àÄ w : EuclideanSpace ùïú (Fin n),
      T ((hT.eigenvectorBasis hn).repr.symm w) =
        (hT.eigenvectorBasis hn).repr.symm fun i ‚Ü¶ hT.eigenvalues hn i * w i by
    simpa [OrthonormalBasis.sum_repr_symm] using
      congr_arg (fun v ‚Ü¶ (hT.eigenvectorBasis hn).repr v i)
        (this ((hT.eigenvectorBasis hn).repr v))
  intro w
  simp_rw [‚Üê OrthonormalBasis.sum_repr_symm, map_sum, map_smul, apply_eigenvectorBasis]
  apply Fintype.sum_congr
  intro a
  rw [smul_smul, mul_comm]

end Version2

end IsSymmetric

end LinearMap

section Nonneg

-- Cannot be @[simp] because the LHS is not in simp normal form
theorem inner_product_apply_eigenvector {Œº : ùïú} {v : E} {T : E ‚Üí‚Çó[ùïú] E}
    (h : T v = Œº ‚Ä¢ v) : ‚ü™v, T v‚ü´ = Œº * (‚Äñv‚Äñ : ùïú) ^ 2 := by
  simp only [h, inner_smul_right, inner_self_eq_norm_sq_to_K]

theorem eigenvalue_nonneg_of_nonneg {Œº : ‚Ñù} {T : E ‚Üí‚Çó[ùïú] E} (hŒº : HasEigenvalue T Œº)
    (hnn : ‚àÄ x : E, 0 ‚â§ RCLike.re ‚ü™x, T x‚ü´) : 0 ‚â§ Œº := by
  obtain ‚ü®v, hv‚ÇÅ, hv‚ÇÇ‚ü© := hŒº.exists_hasEigenvector
  have hpos : (0 : ‚Ñù) < ‚Äñv‚Äñ ^ 2 := by simpa only [sq_pos_iff, norm_ne_zero_iff] using hv‚ÇÇ
  simp only [mem_genEigenspace_one] at hv‚ÇÅ
  have : RCLike.re ‚ü™v, T v‚ü´ = Œº * ‚Äñv‚Äñ ^ 2 :=
    mod_cast congr_arg RCLike.re (inner_product_apply_eigenvector hv‚ÇÅ)
  exact (mul_nonneg_iff_of_pos_right hpos).mp (this ‚ñ∏ hnn v)

theorem eigenvalue_pos_of_pos {Œº : ‚Ñù} {T : E ‚Üí‚Çó[ùïú] E} (hŒº : HasEigenvalue T Œº)
    (hnn : ‚àÄ x : E, 0 < RCLike.re ‚ü™x, T x‚ü´) : 0 < Œº := by
  obtain ‚ü®v, hv‚ÇÅ, hv‚ÇÇ‚ü© := hŒº.exists_hasEigenvector
  have hpos : (0 : ‚Ñù) < ‚Äñv‚Äñ ^ 2 := by simpa only [sq_pos_iff, norm_ne_zero_iff] using hv‚ÇÇ
  simp only [mem_genEigenspace_one] at hv‚ÇÅ
  have : RCLike.re ‚ü™v, T v‚ü´ = Œº * ‚Äñv‚Äñ ^ 2 :=
    mod_cast congr_arg RCLike.re (inner_product_apply_eigenvector hv‚ÇÅ)
  exact (mul_pos_iff_of_pos_right hpos).mp (this ‚ñ∏ hnn v)

end Nonneg
