/-
Copyright (c) 2019 Zhouhang Zhou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Zhouhang Zhou, SÃ©bastien GouÃ«zel, FrÃ©dÃ©ric Dupuis
-/

import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.Normed.Operator.BoundedLinearMaps

/-!
# Continuity of inner product

We show that the inner product is continuous, `continuous_inner`.

## Tags

inner product space, Hilbert space, norm

-/

noncomputable section

open RCLike Real Filter Topology ComplexConjugate Finsupp
open LinearMap renaming BilinForm â†’ BilinForm

variable {ð•œ E F : Type*} [RCLike ð•œ]


section Continuous

variable [SeminormedAddCommGroup E] [InnerProductSpace ð•œ E]

local notation "âŸª" x ", " y "âŸ«" => inner ð•œ x y

/-!
### Continuity of the inner product
-/

/-- When an inner product space `E` over `ð•œ` is considered as a real normed space, its inner
product satisfies `IsBoundedBilinearMap`.

In order to state these results, we need a `NormedSpace â„ E` instance. We will later establish
such an instance by restriction-of-scalars, `InnerProductSpace.rclikeToReal ð•œ E`, but this
instance may be not definitionally equal to some other â€œnaturalâ€ instance. So, we assume
`[NormedSpace â„ E]`.
-/
theorem _root_.isBoundedBilinearMap_inner [NormedSpace â„ E] [IsScalarTower â„ ð•œ E] :
    IsBoundedBilinearMap â„ fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  { add_left := inner_add_left
    smul_left := fun r x y => by
      simp only [â† algebraMap_smul ð•œ r x, algebraMap_eq_ofReal, inner_smul_real_left]
    add_right := inner_add_right
    smul_right := fun r x y => by
      simp only [â† algebraMap_smul ð•œ r y, algebraMap_eq_ofReal, inner_smul_real_right]
    bound :=
      âŸ¨1, zero_lt_one, fun x y => by
        rw [one_mul]
        exact norm_inner_le_norm x yâŸ© }

theorem continuous_inner : Continuous fun p : E Ã— E => âŸªp.1, p.2âŸ« :=
  letI : InnerProductSpace â„ E := InnerProductSpace.rclikeToReal ð•œ E
  letI : IsScalarTower â„ ð•œ E := RestrictScalars.isScalarTower _ _ _
  isBoundedBilinearMap_inner.continuous

variable {Î± : Type*}

theorem Filter.Tendsto.inner {f g : Î± â†’ E} {l : Filter Î±} {x y : E} (hf : Tendsto f l (ð“ x))
    (hg : Tendsto g l (ð“ y)) : Tendsto (fun t => âŸªf t, g tâŸ«) l (ð“ âŸªx, yâŸ«) :=
  (continuous_inner.tendsto _).comp (hf.prodMk_nhds hg)

variable [TopologicalSpace Î±] {f g : Î± â†’ E} {x : Î±} {s : Set Î±}

theorem ContinuousWithinAt.inner (hf : ContinuousWithinAt f s x) (hg : ContinuousWithinAt g s x) :
    ContinuousWithinAt (fun t => âŸªf t, g tâŸ«) s x :=
  Filter.Tendsto.inner hf hg

@[fun_prop]
theorem ContinuousAt.inner (hf : ContinuousAt f x) (hg : ContinuousAt g x) :
    ContinuousAt (fun t => âŸªf t, g tâŸ«) x :=
  Filter.Tendsto.inner hf hg

@[fun_prop]
theorem ContinuousOn.inner (hf : ContinuousOn f s) (hg : ContinuousOn g s) :
    ContinuousOn (fun t => âŸªf t, g tâŸ«) s := fun x hx => (hf x hx).inner (hg x hx)

@[continuity, fun_prop]
theorem Continuous.inner (hf : Continuous f) (hg : Continuous g) : Continuous fun t => âŸªf t, g tâŸ« :=
  continuous_iff_continuousAt.2 fun _x => by fun_prop

end Continuous
