/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.InnerProductSpace.Adjoint
import Mathlib.Analysis.InnerProductSpace.ProdL2
import Mathlib.Analysis.Normed.Operator.Completeness
import Mathlib.Topology.Algebra.Module.Equiv
import Mathlib.Topology.Algebra.Module.LinearPMap

/-!

# Partially defined linear operators on Hilbert spaces

We will develop the basics of the theory of unbounded operators on Hilbert spaces.

## Main definitions

* `LinearPMap.IsFormalAdjoint`: An operator `T` is a formal adjoint of `S` if for all `x` in the
  domain of `T` and `y` in the domain of `S`, we have that `âŸªT x, yâŸ« = âŸªx, S yâŸ«`.
* `LinearPMap.adjoint`: The adjoint of a map `E â†’â‚—.[ğ•œ] F` as a map `F â†’â‚—.[ğ•œ] E`.

## Main statements

* `LinearPMap.adjoint_isFormalAdjoint`: The adjoint is a formal adjoint
* `LinearPMap.IsFormalAdjoint.le_adjoint`: Every formal adjoint is contained in the adjoint
* `ContinuousLinearMap.toPMap_adjoint_eq_adjoint_toPMap_of_dense`: The adjoint on
  `ContinuousLinearMap` and `LinearPMap` coincide.
* `LinearPMap.adjoint_isClosed`: The adjoint is a closed operator.
* `IsSelfAdjoint.isClosed`: Every self-adjoint operator is closed.

## Notation

* For `T : E â†’â‚—.[ğ•œ] F` the adjoint can be written as `Tâ€ `.
  This notation is localized in `LinearPMap`.

## Implementation notes

We use the junk value pattern to define the adjoint for all `LinearPMap`s. In the case that
`T : E â†’â‚—.[ğ•œ] F` is not densely defined the adjoint `Tâ€ ` is the zero map from `T.adjoint.domain` to
`E`.

## References

* [J. Weidmann, *Linear Operators in Hilbert Spaces*][weidmann_linear]

## Tags

Unbounded operators, closed operators
-/


noncomputable section

open RCLike LinearPMap

open scoped ComplexConjugate

variable {ğ•œ E F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

namespace LinearPMap

/-- An operator `T` is a formal adjoint of `S` if for all `x` in the domain of `T` and `y` in the
domain of `S`, we have that `âŸªT x, yâŸ« = âŸªx, S yâŸ«`. -/
def IsFormalAdjoint (T : E â†’â‚—.[ğ•œ] F) (S : F â†’â‚—.[ğ•œ] E) : Prop :=
  âˆ€ (x : T.domain) (y : S.domain), âŸªT x, yâŸ« = âŸª(x : E), S yâŸ«

variable {T : E â†’â‚—.[ğ•œ] F} {S : F â†’â‚—.[ğ•œ] E}

@[symm]
protected theorem IsFormalAdjoint.symm (h : T.IsFormalAdjoint S) :
    S.IsFormalAdjoint T := fun y _ => by
  rw [â† inner_conj_symm, â† inner_conj_symm (y : F), h]

variable (T)

/-- The domain of the adjoint operator.

This definition is needed to construct the adjoint operator and the preferred version to use is
`T.adjoint.domain` instead of `T.adjointDomain`. -/
def adjointDomain : Submodule ğ•œ F where
  carrier := {y | Continuous ((innerâ‚›â‚— ğ•œ y).comp T.toFun)}
  zero_mem' := by
    rw [Set.mem_setOf_eq, LinearMap.map_zero, LinearMap.zero_comp]
    exact continuous_zero
  add_mem' hx hy := by rw [Set.mem_setOf_eq, LinearMap.map_add] at *; exact hx.add hy
  smul_mem' a x hx := by
    rw [Set.mem_setOf_eq, LinearMap.map_smulâ‚›â‚—] at *
    exact hx.const_smul (conj a)

/-- The operator `fun x â†¦ âŸªy, T xâŸ«` considered as a continuous linear operator
from `T.adjointDomain` to `ğ•œ`. -/
def adjointDomainMkCLM (y : T.adjointDomain) : StrongDual ğ•œ T.domain :=
  âŸ¨(innerâ‚›â‚— ğ•œ (y : F)).comp T.toFun, y.propâŸ©

theorem adjointDomainMkCLM_apply (y : T.adjointDomain) (x : T.domain) :
    adjointDomainMkCLM T y x = âŸª(y : F), T xâŸ« :=
  rfl

variable {T}
variable (hT : Dense (T.domain : Set E))

/-- The unique continuous extension of the operator `adjointDomainMkCLM` to `E`. -/
def adjointDomainMkCLMExtend (y : T.adjointDomain) : StrongDual ğ•œ E :=
  (T.adjointDomainMkCLM y).extend (Submodule.subtypeL T.domain) hT.denseRange_val
    isUniformEmbedding_subtype_val.isUniformInducing

@[simp]
theorem adjointDomainMkCLMExtend_apply (y : T.adjointDomain) (x : T.domain) :
    adjointDomainMkCLMExtend hT y (x : E) = âŸª(y : F), T xâŸ« :=
  ContinuousLinearMap.extend_eq _ _ _ _ _

variable [CompleteSpace E]

/-- The adjoint as a linear map from its domain to `E`.

This is an auxiliary definition needed to define the adjoint operator as a `LinearPMap` without
the assumption that `T.domain` is dense. -/
def adjointAux : T.adjointDomain â†’â‚—[ğ•œ] E where
  toFun y := (InnerProductSpace.toDual ğ•œ E).symm (adjointDomainMkCLMExtend hT y)
  map_add' x y :=
    hT.eq_of_inner_left fun _ => by
      simp only [inner_add_left, Submodule.coe_add, InnerProductSpace.toDual_symm_apply,
        adjointDomainMkCLMExtend_apply]
  map_smul' _ _ :=
    hT.eq_of_inner_left fun _ => by
      simp only [inner_smul_left, Submodule.coe_smul_of_tower, RingHom.id_apply,
        InnerProductSpace.toDual_symm_apply, adjointDomainMkCLMExtend_apply]

theorem adjointAux_inner (y : T.adjointDomain) (x : T.domain) :
    âŸªadjointAux hT y, xâŸ« = âŸª(y : F), T xâŸ« := by
  simp [adjointAux]

theorem adjointAux_unique (y : T.adjointDomain) {xâ‚€ : E}
    (hxâ‚€ : âˆ€ x : T.domain, âŸªxâ‚€, xâŸ« = âŸª(y : F), T xâŸ«) : adjointAux hT y = xâ‚€ :=
  hT.eq_of_inner_left fun v => (adjointAux_inner hT _ _).trans (hxâ‚€ v).symm

variable (T)

open scoped Classical in
/-- The adjoint operator as a partially defined linear operator, denoted as `Tâ€ `. -/
def adjoint : F â†’â‚—.[ğ•œ] E where
  domain := T.adjointDomain
  toFun := if hT : Dense (T.domain : Set E) then adjointAux hT else 0

@[inherit_doc]
scoped postfix:1024 "â€ " => LinearPMap.adjoint

theorem mem_adjoint_domain_iff (y : F) : y âˆˆ Tâ€ .domain â†” Continuous ((innerâ‚›â‚— ğ•œ y).comp T.toFun) :=
  Iff.rfl

variable {T}

theorem mem_adjoint_domain_of_exists (y : F) (h : âˆƒ w : E, âˆ€ x : T.domain, âŸªw, xâŸ« = âŸªy, T xâŸ«) :
    y âˆˆ Tâ€ .domain := by
  obtain âŸ¨w, hwâŸ© := h
  rw [T.mem_adjoint_domain_iff]
  have : Continuous ((innerSL ğ•œ w).comp T.domain.subtypeL) := by fun_prop
  convert this using 1
  exact funext fun x => (hw x).symm

theorem adjoint_apply_of_not_dense (hT : Â¬Dense (T.domain : Set E)) (y : Tâ€ .domain) : Tâ€  y = 0 := by
  classical
  change (if hT : Dense (T.domain : Set E) then adjointAux hT else 0) y = _
  simp only [hT, not_false_iff, dif_neg, LinearMap.zero_apply]

theorem adjoint_apply_of_dense (y : Tâ€ .domain) : Tâ€  y = adjointAux hT y := by
  classical
  change (if hT : Dense (T.domain : Set E) then adjointAux hT else 0) y = _
  simp only [hT, dif_pos]

include hT in
theorem adjoint_apply_eq (y : Tâ€ .domain) {xâ‚€ : E} (hxâ‚€ : âˆ€ x : T.domain, âŸªxâ‚€, xâŸ« = âŸª(y : F), T xâŸ«) :
    Tâ€  y = xâ‚€ :=
  (adjoint_apply_of_dense hT y).symm â–¸ adjointAux_unique hT _ hxâ‚€

include hT in
/-- The fundamental property of the adjoint. -/
theorem adjoint_isFormalAdjoint : Tâ€ .IsFormalAdjoint T := fun x =>
  (adjoint_apply_of_dense hT x).symm â–¸ adjointAux_inner hT x

include hT in
/-- The adjoint is maximal in the sense that it contains every formal adjoint. -/
theorem IsFormalAdjoint.le_adjoint (h : T.IsFormalAdjoint S) : S â‰¤ Tâ€  :=
  âŸ¨-- Trivially, every `x : S.domain` is in `T.adjoint.domain`
  fun x hx =>
    mem_adjoint_domain_of_exists _
      âŸ¨S âŸ¨x, hxâŸ©, h.symm âŸ¨x, hxâŸ©âŸ©,-- Equality on `S.domain` follows from equality
  -- `âŸªv, S xâŸ« = âŸªv, T.adjoint yâŸ«` for all `v : T.domain`:
  fun _ _ hxy => (adjoint_apply_eq hT _ fun _ => by rw [h.symm, hxy]).symmâŸ©

end LinearPMap

namespace ContinuousLinearMap

variable [CompleteSpace E] [CompleteSpace F]
variable (A : E â†’L[ğ•œ] F) {p : Submodule ğ•œ E}

/-- Restricting `A` to a dense submodule and taking the `LinearPMap.adjoint` is the same
as taking the `ContinuousLinearMap.adjoint` interpreted as a `LinearPMap`. -/
theorem toPMap_adjoint_eq_adjoint_toPMap_of_dense (hp : Dense (p : Set E)) :
    (A.toPMap p).adjoint = A.adjoint.toPMap âŠ¤ := by
  ext x y hxy
  Â· simp only [LinearMap.toPMap_domain, Submodule.mem_top, iff_true,
      LinearPMap.mem_adjoint_domain_iff, LinearMap.coe_comp, innerâ‚›â‚—_apply_coe]
    exact ((innerSL ğ•œ x).comp <| A.comp <| Submodule.subtypeL _).cont
  refine LinearPMap.adjoint_apply_eq hp _ fun v => ?_
  simp only [adjoint_inner_left, LinearMap.toPMap_apply, coe_coe]

end ContinuousLinearMap

section Star

namespace LinearPMap

variable [CompleteSpace E]

instance instStar : Star (E â†’â‚—.[ğ•œ] E) where
  star := fun A â†¦ A.adjoint

variable {A : E â†’â‚—.[ğ•œ] E}

theorem isSelfAdjoint_def : IsSelfAdjoint A â†” Aâ€  = A := Iff.rfl

/-- Every self-adjoint `LinearPMap` has dense domain.

This is not true by definition since we define the adjoint without the assumption that the
domain is dense, but the choice of the junk value implies that a `LinearPMap` cannot be self-adjoint
if it does not have dense domain. -/
theorem _root_.IsSelfAdjoint.dense_domain (hA : IsSelfAdjoint A) : Dense (A.domain : Set E) := by
  by_contra h
  rw [isSelfAdjoint_def] at hA
  have h' : A.domain = âŠ¤ := by
    rw [â† hA, Submodule.eq_top_iff']
    intro x
    rw [mem_adjoint_domain_iff, â† hA]
    refine (innerSL ğ•œ x).cont.comp ?_
    simp only [adjoint, h]
    exact continuous_const
  simp [h'] at h

end LinearPMap

end Star

/-! ### The graph of the adjoint -/

namespace Submodule

/-- The adjoint of a submodule

Note that the adjoint is taken with respect to the L^2 inner product on `E Ã— F`, which is defined
as `WithLp 2 (E Ã— F)`. -/
protected noncomputable
def adjoint (g : Submodule ğ•œ (E Ã— F)) : Submodule ğ•œ (F Ã— E) :=
  (g.map <| (LinearEquiv.skewSwap ğ•œ F E).symm.trans
    (WithLp.linearEquiv 2 ğ•œ (F Ã— E)).symm).orthogonal.map (WithLp.linearEquiv 2 ğ•œ (F Ã— E))

@[simp]
theorem mem_adjoint_iff (g : Submodule ğ•œ (E Ã— F)) (x : F Ã— E) :
    x âˆˆ g.adjoint â†”
    âˆ€ a b, (a, b) âˆˆ g â†’ inner ğ•œ b x.fst - inner ğ•œ a x.snd = 0 := by
  simp only [Submodule.adjoint, mem_map, mem_orthogonal, LinearEquiv.trans_apply,
    LinearEquiv.skewSwap_symm_apply, WithLp.linearEquiv_symm_apply, Prod.exists,
    WithLp.prod_inner_apply, WithLp.ofLp_fst, WithLp.ofLp_snd, forall_exists_index, and_imp,
    WithLp.linearEquiv_apply]
  constructor
  Â· rintro âŸ¨y, h1, h2âŸ© a b hab
    rw [â† h2, WithLp.ofLp_fst, WithLp.ofLp_snd]
    specialize h1 (b, -a) a b hab rfl
    dsimp at h1
    simp only [inner_neg_left, â† sub_eq_add_neg] at h1
    exact h1
  Â· intro h
    refine âŸ¨x, ?_, rflâŸ©
    intro u a b hab hu
    simp [â† hu, â† sub_eq_add_neg, h a b hab]

variable {T : E â†’â‚—.[ğ•œ] F} [CompleteSpace E]

theorem _root_.LinearPMap.adjoint_graph_eq_graph_adjoint (hT : Dense (T.domain : Set E)) :
    Tâ€ .graph = T.graph.adjoint := by
  ext x
  simp only [mem_graph_iff, Subtype.exists, exists_and_left, exists_eq_left, mem_adjoint_iff,
    forall_exists_index, forall_apply_eq_imp_iff]
  constructor
  Â· rintro âŸ¨hx, hâŸ© a ha
    rw [â† h, (adjoint_isFormalAdjoint hT).symm âŸ¨a, haâŸ© âŸ¨x.fst, hxâŸ©, sub_self]
  Â· intro h
    simp_rw [sub_eq_zero] at h
    have hx : x.fst âˆˆ Tâ€ .domain := by
      apply mem_adjoint_domain_of_exists
      use x.snd
      rintro âŸ¨a, haâŸ©
      rw [â† inner_conj_symm, â† h a ha, inner_conj_symm]
    use hx
    apply hT.eq_of_inner_right
    rintro âŸ¨a, haâŸ©
    rw [â† h a ha, (adjoint_isFormalAdjoint hT).symm âŸ¨a, haâŸ© âŸ¨x.fst, hxâŸ©]

@[simp]
theorem _root_.LinearPMap.graph_adjoint_toLinearPMap_eq_adjoint (hT : Dense (T.domain : Set E)) :
    T.graph.adjoint.toLinearPMap = Tâ€  := by
  apply eq_of_eq_graph
  rw [adjoint_graph_eq_graph_adjoint hT]
  apply Submodule.toLinearPMap_graph_eq
  intro x hx hx'
  simp only [mem_adjoint_iff, mem_graph_iff, Subtype.exists, exists_and_left, exists_eq_left, hx',
    inner_zero_right, zero_sub, neg_eq_zero, forall_exists_index, forall_apply_eq_imp_iff] at hx
  apply hT.eq_zero_of_inner_right
  rintro âŸ¨a, haâŸ©
  exact hx a ha

end Submodule

/-! ### Closedness -/

namespace LinearPMap

variable {T : E â†’â‚—.[ğ•œ] F} [CompleteSpace E]

theorem adjoint_isClosed (hT : Dense (T.domain : Set E)) :
    Tâ€ .IsClosed := by
  rw [IsClosed, adjoint_graph_eq_graph_adjoint hT, Submodule.adjoint]
  simp only [Submodule.map_coe]
  rw [LinearEquiv.image_eq_preimage]
  exact (Submodule.isClosed_orthogonal _).preimage (WithLp.prod_continuous_toLp _ _ _)

/-- Every self-adjoint `LinearPMap` is closed. -/
theorem _root_.IsSelfAdjoint.isClosed {A : E â†’â‚—.[ğ•œ] E} (hA : IsSelfAdjoint A) : A.IsClosed := by
  rw [â† isSelfAdjoint_def.mp hA]
  exact adjoint_isClosed hA.dense_domain

end LinearPMap
