/-
Copyright (c) 2022 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll, FrÃ©dÃ©ric Dupuis, Heather Macbeth
-/
import Mathlib.Analysis.InnerProductSpace.Basic
import Mathlib.Analysis.NormedSpace.Banach
import Mathlib.LinearAlgebra.SesquilinearForm

#align_import analysis.inner_product_space.symmetric from "leanprover-community/mathlib"@"3f655f5297b030a87d641ad4e825af8d9679eb0b"

/-!
# Symmetric linear maps in an inner product space

This file defines and proves basic theorems about symmetric **not necessarily bounded** operators
on an inner product space, i.e linear maps `T : E â†’ E` such that `âˆ€ x y, âŸªT x, yâŸ« = âŸªx, T yâŸ«`.

In comparison to `IsSelfAdjoint`, this definition works for non-continuous linear maps, and
doesn't rely on the definition of the adjoint, which allows it to be stated in non-complete space.

## Main definitions

* `LinearMap.IsSymmetric`: a (not necessarily bounded) operator on an inner product space is
symmetric, if for all `x`, `y`, we have `âŸªT x, yâŸ« = âŸªx, T yâŸ«`

## Main statements

* `IsSymmetric.continuous`: if a symmetric operator is defined on a complete space, then
  it is automatically continuous.

## Tags

self-adjoint, symmetric
-/


open RCLike

open ComplexConjugate

variable {ğ•œ E E' F G : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup E] [InnerProductSpace ğ•œ E]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F]
variable [NormedAddCommGroup G] [InnerProductSpace ğ•œ G]
variable [NormedAddCommGroup E'] [InnerProductSpace â„ E']

local notation "âŸª" x ", " y "âŸ«" => @inner ğ•œ _ _ x y

namespace LinearMap

/-! ### Symmetric operators -/


/-- A (not necessarily bounded) operator on an inner product space is symmetric, if for all
`x`, `y`, we have `âŸªT x, yâŸ« = âŸªx, T yâŸ«`. -/
def IsSymmetric (T : E â†’â‚—[ğ•œ] E) : Prop :=
  âˆ€ x y, âŸªT x, yâŸ« = âŸªx, T yâŸ«
#align linear_map.is_symmetric LinearMap.IsSymmetric

section Real

/-- An operator `T` on an inner product space is symmetric if and only if it is
`LinearMap.IsSelfAdjoint` with respect to the sesquilinear form given by the inner product. -/
theorem isSymmetric_iff_sesqForm (T : E â†’â‚—[ğ•œ] E) :
    T.IsSymmetric â†” LinearMap.IsSelfAdjoint (R := ğ•œ) (M := E) sesqFormOfInner T :=
  âŸ¨fun h x y => (h y x).symm, fun h x y => (h y x).symmâŸ©
#align linear_map.is_symmetric_iff_sesq_form LinearMap.isSymmetric_iff_sesqForm

end Real

theorem IsSymmetric.conj_inner_sym {T : E â†’â‚—[ğ•œ] E} (hT : IsSymmetric T) (x y : E) :
    conj âŸªT x, yâŸ« = âŸªT y, xâŸ« := by rw [hT x y, inner_conj_symm]
#align linear_map.is_symmetric.conj_inner_sym LinearMap.IsSymmetric.conj_inner_sym

@[simp]
theorem IsSymmetric.apply_clm {T : E â†’L[ğ•œ] E} (hT : IsSymmetric (T : E â†’â‚—[ğ•œ] E)) (x y : E) :
    âŸªT x, yâŸ« = âŸªx, T yâŸ« :=
  hT x y
#align linear_map.is_symmetric.apply_clm LinearMap.IsSymmetric.apply_clm

theorem isSymmetric_zero : (0 : E â†’â‚—[ğ•œ] E).IsSymmetric := fun x y =>
  (inner_zero_right x : âŸªx, 0âŸ« = 0).symm â–¸ (inner_zero_left y : âŸª0, yâŸ« = 0)
#align linear_map.is_symmetric_zero LinearMap.isSymmetric_zero

theorem isSymmetric_id : (LinearMap.id : E â†’â‚—[ğ•œ] E).IsSymmetric := fun _ _ => rfl
#align linear_map.is_symmetric_id LinearMap.isSymmetric_id

theorem IsSymmetric.add {T S : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) (hS : S.IsSymmetric) :
    (T + S).IsSymmetric := by
  intro x y
  rw [LinearMap.add_apply, inner_add_left, hT x y, hS x y, â† inner_add_right]
  rfl
#align linear_map.is_symmetric.add LinearMap.IsSymmetric.add

/-- The **Hellinger--Toeplitz theorem**: if a symmetric operator is defined on a complete space,
  then it is automatically continuous. -/
theorem IsSymmetric.continuous [CompleteSpace E] {T : E â†’â‚—[ğ•œ] E} (hT : IsSymmetric T) :
    Continuous T := by
  -- We prove it by using the closed graph theorem
  refine T.continuous_of_seq_closed_graph fun u x y hu hTu => ?_
  rw [â† sub_eq_zero, â† @inner_self_eq_zero ğ•œ]
  have hlhs : âˆ€ k : â„•, âŸªT (u k) - T x, y - T xâŸ« = âŸªu k - x, T (y - T x)âŸ« := by
    intro k
    rw [â† T.map_sub, hT]
  refine tendsto_nhds_unique ((hTu.sub_const _).inner tendsto_const_nhds) ?_
  simp_rw [Function.comp_apply, hlhs]
  rw [â† inner_zero_left (T (y - T x))]
  refine Filter.Tendsto.inner ?_ tendsto_const_nhds
  rw [â† sub_self x]
  exact hu.sub_const _
#align linear_map.is_symmetric.continuous LinearMap.IsSymmetric.continuous

/-- For a symmetric operator `T`, the function `fun x â†¦ âŸªT x, xâŸ«` is real-valued. -/
@[simp]
theorem IsSymmetric.coe_reApplyInnerSelf_apply {T : E â†’L[ğ•œ] E} (hT : IsSymmetric (T : E â†’â‚—[ğ•œ] E))
    (x : E) : (T.reApplyInnerSelf x : ğ•œ) = âŸªT x, xâŸ« := by
  rsuffices âŸ¨r, hrâŸ© : âˆƒ r : â„, âŸªT x, xâŸ« = r
  Â· simp [hr, T.reApplyInnerSelf_apply]
  rw [â† conj_eq_iff_real]
  exact hT.conj_inner_sym x x
#align linear_map.is_symmetric.coe_re_apply_inner_self_apply LinearMap.IsSymmetric.coe_reApplyInnerSelf_apply

/-- If a symmetric operator preserves a submodule, its restriction to that submodule is
symmetric. -/
theorem IsSymmetric.restrict_invariant {T : E â†’â‚—[ğ•œ] E} (hT : IsSymmetric T) {V : Submodule ğ•œ E}
    (hV : âˆ€ v âˆˆ V, T v âˆˆ V) : IsSymmetric (T.restrict hV) := fun v w => hT v w
#align linear_map.is_symmetric.restrict_invariant LinearMap.IsSymmetric.restrict_invariant

theorem IsSymmetric.restrictScalars {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) :
    @LinearMap.IsSymmetric â„ E _ _ (InnerProductSpace.rclikeToReal ğ•œ E)
      (@LinearMap.restrictScalars â„ ğ•œ _ _ _ _ _ _ (InnerProductSpace.rclikeToReal ğ•œ E).toModule
        (InnerProductSpace.rclikeToReal ğ•œ E).toModule _ _ _ T) :=
  fun x y => by simp [hT x y, real_inner_eq_re_inner, LinearMap.coe_restrictScalars â„]
#align linear_map.is_symmetric.restrict_scalars LinearMap.IsSymmetric.restrictScalars

section Complex

variable {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„‚ V]

/-- A linear operator on a complex inner product space is symmetric precisely when
`âŸªT v, vâŸ«_â„‚` is real for all v. -/
theorem isSymmetric_iff_inner_map_self_real (T : V â†’â‚—[â„‚] V) :
    IsSymmetric T â†” âˆ€ v : V, conj âŸªT v, vâŸ«_â„‚ = âŸªT v, vâŸ«_â„‚ := by
  constructor
  Â· intro hT v
    apply IsSymmetric.conj_inner_sym hT
  Â· intro h x y
    rw [â† inner_conj_symm x (T y)]
    rw [inner_map_polarization T x y]
    simp only [starRingEnd_apply, star_div', star_sub, star_add, star_mul]
    simp only [â† starRingEnd_apply]
    rw [h (x + y), h (x - y), h (x + Complex.I â€¢ y), h (x - Complex.I â€¢ y)]
    simp only [Complex.conj_I]
    rw [inner_map_polarization']
    norm_num
    ring
#align linear_map.is_symmetric_iff_inner_map_self_real LinearMap.isSymmetric_iff_inner_map_self_real

end Complex

/-- Polarization identity for symmetric linear maps.
See `inner_map_polarization` for the complex version without the symmetric assumption. -/
theorem IsSymmetric.inner_map_polarization {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) (x y : E) :
    âŸªT x, yâŸ« =
      (âŸªT (x + y), x + yâŸ« - âŸªT (x - y), x - yâŸ« - I * âŸªT (x + (I : ğ•œ) â€¢ y), x + (I : ğ•œ) â€¢ yâŸ« +
          I * âŸªT (x - (I : ğ•œ) â€¢ y), x - (I : ğ•œ) â€¢ yâŸ«) /
        4 := by
  rcases@I_mul_I_ax ğ•œ _ with (h | h)
  Â· simp_rw [h, zero_mul, sub_zero, add_zero, map_add, map_sub, inner_add_left,
      inner_add_right, inner_sub_left, inner_sub_right, hT x, â† inner_conj_symm x (T y)]
    suffices (re âŸªT y, xâŸ« : ğ•œ) = âŸªT y, xâŸ« by
      rw [conj_eq_iff_re.mpr this]
      ring
    rw [â† re_add_im âŸªT y, xâŸ«]
    simp_rw [h, mul_zero, add_zero]
    norm_cast
  Â· simp_rw [map_add, map_sub, inner_add_left, inner_add_right, inner_sub_left, inner_sub_right,
      LinearMap.map_smul, inner_smul_left, inner_smul_right, RCLike.conj_I, mul_add, mul_sub,
      sub_sub, â† mul_assoc, mul_neg, h, neg_neg, one_mul, neg_one_mul]
    ring
#align linear_map.is_symmetric.inner_map_polarization LinearMap.IsSymmetric.inner_map_polarization

/-- A symmetric linear map `T` is zero if and only if `âŸªT x, xâŸ«_â„ = 0` for all `x`.
See `inner_map_self_eq_zero` for the complex version without the symmetric assumption. -/
theorem IsSymmetric.inner_map_self_eq_zero {T : E â†’â‚—[ğ•œ] E} (hT : T.IsSymmetric) :
    (âˆ€ x, âŸªT x, xâŸ« = 0) â†” T = 0 := by
  simp_rw [LinearMap.ext_iff, zero_apply]
  refine âŸ¨fun h x => ?_, fun h => by simp_rw [h, inner_zero_left, forall_const]âŸ©
  rw [â† @inner_self_eq_zero ğ•œ, hT.inner_map_polarization]
  simp_rw [h _]
  ring
#align linear_map.is_symmetric.inner_map_self_eq_zero LinearMap.IsSymmetric.inner_map_self_eq_zero

end LinearMap
