/-
Copyright (c) 2024 Xavier Roblot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Xavier Roblot
-/
import Mathlib.Algebra.Module.ZLattice.Basic
import Mathlib.Analysis.BoxIntegral.Integrability
import Mathlib.Analysis.BoxIntegral.Partition.Measure
import Mathlib.Analysis.BoxIntegral.Partition.Tagged

/-!
# Unit Partition

Fix `n` a positive integer. `BoxIntegral.unitPartition.box` are boxes in `Œπ ‚Üí ‚Ñù` obtained by
dividing the unit box uniformly into boxes of side length `1 / n` and translating the boxes by
vectors `ŒΩ : Œπ ‚Üí ‚Ñ§`.

Let `B` be a `BoxIntegral`. A `unitPartition.box` is admissible for `B` (more precisely its index is
admissible) if it is contained in `B`. There are finitely many admissible `unitPartition.box` for
`B` and thus we can form the corresponding tagged prepartition, see
`BoxIntegral.unitPartition.prepartition` (note that each `unitPartition.box` comes with its
tag situated at its "upper most" vertex). If `B` satisfies `hasIntegralVertices`, that
is its vertices are in `Œπ ‚Üí ‚Ñ§`, then the corresponding prepartition is actually a partition.

## Main definitions and results

* `BoxIntegral.hasIntegralVertices`: a `Prop` that states that the vertices of the box have
  coordinates in `‚Ñ§`

* `BoxIntegral.unitPartition.box`: a `BoxIntegral`, indexed by `ŒΩ : Œπ ‚Üí ‚Ñ§`, with vertices
  `ŒΩ i / n` and of side length `1 / n`.

* `BoxIntegral.unitPartition.admissibleIndex`: For `B : BoxIntegral.Box`, the set of indices of
  `unitPartition.box` that are subsets of `B`. This is a finite set.

* `BoxIntegral.unitPartition.prepartition_isPartition`: For `B : BoxIntegral.Box`, if `B`
  has integral vertices, then the prepartition of `unitPartition.box` admissible for `B` is a
  partition of `B`.

* `tendsto_tsum_div_pow_atTop_integral`: let `s` be a bounded, measurable set of `Œπ ‚Üí ‚Ñù`
  whose frontier has zero volume and let `F` be a continuous function. Then the limit as `n ‚Üí ‚àû`
  of `‚àë F x / n ^ card Œπ`, where the sum is over the points in `s ‚à© n‚Åª¬π ‚Ä¢ (Œπ ‚Üí ‚Ñ§)`, tends to the
  integral of `F` over `s`.

* `tendsto_card_div_pow_atTop_volume`: let `s` be a bounded, measurable set of `Œπ ‚Üí ‚Ñù` whose
  frontier has zero volume. Then the limit as `n ‚Üí ‚àû` of `card (s ‚à© n‚Åª¬π ‚Ä¢ (Œπ ‚Üí ‚Ñ§)) / n ^ card Œπ`
  tends to the volume of `s`.

* `tendsto_card_div_pow_atTop_volume'`: a version of `tendsto_card_div_pow_atTop_volume` where we
  assume in addition that `x ‚Ä¢ s ‚äÜ y ‚Ä¢ s` whenever `0 < x ‚â§ y`. Then we get the same limit
  `card (s ‚à© x‚Åª¬π ‚Ä¢ (Œπ ‚Üí ‚Ñ§)) / x ^ card Œπ ‚Üí volume s` but the limit is over a real variable `x`.

-/

noncomputable section

variable {Œπ : Type*}

open scoped Topology

section hasIntegralVertices

open Bornology

/-- A `BoxIntegral.Box` has integral vertices if its vertices have coordinates in `‚Ñ§`. -/
def BoxIntegral.hasIntegralVertices (B : Box Œπ) : Prop :=
  ‚àÉ l u : Œπ ‚Üí ‚Ñ§, (‚àÄ i, B.lower i = l i) ‚àß (‚àÄ i, B.upper i = u i)

/-- Any bounded set is contained in a `BoxIntegral.Box` with integral vertices. -/
theorem BoxIntegral.le_hasIntegralVertices_of_isBounded [Finite Œπ] {s : Set (Œπ ‚Üí ‚Ñù)}
    (h : IsBounded s) :
    ‚àÉ B : BoxIntegral.Box Œπ, hasIntegralVertices B ‚àß s ‚â§ B := by
  have := Fintype.ofFinite Œπ
  obtain ‚ü®R, hR‚ÇÅ, hR‚ÇÇ‚ü© := IsBounded.subset_ball_lt h 0 0
  let C : ‚Ñï := ‚åàR‚åâ‚Çä
  have hC := Nat.ceil_pos.mpr hR‚ÇÅ
  let I : Box Œπ := Box.mk (fun _ ‚Ü¶ - C) (fun _ ‚Ü¶ C )
    (fun _ ‚Ü¶ by simp [C, neg_lt_self_iff, Nat.cast_pos, hC])
  refine ‚ü®I, ‚ü®fun _ ‚Ü¶ - C, fun _ ‚Ü¶ C, fun i ‚Ü¶ (Int.cast_neg_natCast C).symm, fun _ ‚Ü¶ rfl‚ü©,
    le_trans hR‚ÇÇ ?_‚ü©
  suffices Metric.ball (0 : Œπ ‚Üí ‚Ñù) C ‚â§ I from
    le_trans (Metric.ball_subset_ball (Nat.le_ceil R)) this
  intro x hx
  simp_rw [C, mem_ball_zero_iff, pi_norm_lt_iff (Nat.cast_pos.mpr hC),
    Real.norm_eq_abs, abs_lt] at hx
  exact fun i ‚Ü¶ ‚ü®(hx i).1, le_of_lt (hx i).2‚ü©

end hasIntegralVertices

namespace BoxIntegral.unitPartition

open Bornology MeasureTheory Fintype BoxIntegral

variable (n : ‚Ñï)

/-- A `BoxIntegral`, indexed by a positive integer `n` and `ŒΩ : Œπ ‚Üí ‚Ñ§`, with corners `ŒΩ i / n`
and of side length `1 / n`. -/
def box [NeZero n] (ŒΩ : Œπ ‚Üí ‚Ñ§) : Box Œπ where
  lower := fun i ‚Ü¶ ŒΩ i / n
  upper := fun i ‚Ü¶ (ŒΩ i + 1) / n
  lower_lt_upper := fun _ ‚Ü¶ by simp [add_div, n.pos_of_neZero]

@[simp]
theorem box_lower [NeZero n] (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    (box n ŒΩ).lower = fun i ‚Ü¶ (ŒΩ i / n : ‚Ñù) := rfl

@[simp]
theorem box_upper [NeZero n] (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    (box n ŒΩ).upper = fun i ‚Ü¶ ((ŒΩ i + 1) / n : ‚Ñù) := rfl

variable {n} in
@[simp]
theorem mem_box_iff [NeZero n] {ŒΩ : Œπ ‚Üí ‚Ñ§} {x : Œπ ‚Üí ‚Ñù} :
    x ‚àà box n ŒΩ ‚Üî ‚àÄ i, ŒΩ i / n < x i ‚àß x i ‚â§ (ŒΩ i + 1) / n := by
  simp_rw [Box.mem_def, box, Set.mem_Ioc]

variable {n} in
theorem mem_box_iff' [NeZero n] {ŒΩ : Œπ ‚Üí ‚Ñ§} {x : Œπ ‚Üí ‚Ñù} :
    x ‚àà box n ŒΩ ‚Üî ‚àÄ i, ŒΩ i < n * x i ‚àß n * x i ‚â§ ŒΩ i + 1 := by
  have h : 0 < (n : ‚Ñù) := Nat.cast_pos.mpr <| n.pos_of_neZero
  simp_rw [mem_box_iff, ‚Üê _root_.le_div_iff‚ÇÄ' h, ‚Üê div_lt_iff‚ÇÄ' h]

/-- The tag of (the index of) a `unitPartition.box`. -/
abbrev tag (ŒΩ : Œπ ‚Üí ‚Ñ§) : Œπ ‚Üí ‚Ñù := fun i ‚Ü¶ (ŒΩ i + 1) / n

@[simp]
theorem tag_apply (ŒΩ : Œπ ‚Üí ‚Ñ§) (i : Œπ) : tag n ŒΩ i = (ŒΩ i + 1) / n := rfl

variable [NeZero n]

theorem tag_injective : Function.Injective (fun ŒΩ : Œπ ‚Üí ‚Ñ§ ‚Ü¶ tag n ŒΩ) := by
  refine fun _ _ h ‚Ü¶ funext_iff.mpr fun i ‚Ü¶ ?_
  have := congr_arg (fun x ‚Ü¶ x i) h
  simp_rw [tag_apply, div_left_inj' (c := (n : ‚Ñù)) (Nat.cast_ne_zero.mpr (NeZero.ne n)),
    add_left_inj, Int.cast_inj] at this
  exact this

theorem tag_mem (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    tag n ŒΩ ‚àà box n ŒΩ := by
  refine mem_box_iff.mpr fun _ ‚Ü¶ ?_
  rw [tag, add_div]
  have h : 0 < (n : ‚Ñù) := Nat.cast_pos.mpr <| n.pos_of_neZero
  exact ‚ü®lt_add_of_pos_right _ (by positivity), le_rfl‚ü©

/-- For `x : Œπ ‚Üí ‚Ñù`, its index is the index of the unique `unitPartition.box` to which
it belongs. -/
def index (x : Œπ ‚Üí ‚Ñù) (i : Œπ) : ‚Ñ§ := ‚åàn * x i‚åâ - 1

@[simp]
theorem index_apply (m : ‚Ñï) {x : Œπ ‚Üí ‚Ñù} (i : Œπ) :
    index m x i = ‚åàm * x i‚åâ - 1 := rfl

variable {n} in
theorem mem_box_iff_index {x : Œπ ‚Üí ‚Ñù} {ŒΩ : Œπ ‚Üí ‚Ñ§} :
    x ‚àà box n ŒΩ ‚Üî index n x = ŒΩ := by
  simp_rw [mem_box_iff', funext_iff, index_apply, sub_eq_iff_eq_add, Int.ceil_eq_iff,
    Int.cast_add, Int.cast_one, add_sub_cancel_right]

@[simp]
theorem index_tag (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    index n (tag n ŒΩ) = ŒΩ := mem_box_iff_index.mp (tag_mem n ŒΩ)

variable {n} in
theorem disjoint {ŒΩ ŒΩ' : Œπ ‚Üí ‚Ñ§} :
    ŒΩ ‚â† ŒΩ' ‚Üî Disjoint (box n ŒΩ).toSet (box n ŒΩ').toSet := by
  rw [not_iff_comm, Set.not_disjoint_iff]
  refine ‚ü®fun ‚ü®x, hx, hx'‚ü© ‚Ü¶ ?_, fun h ‚Ü¶ ‚ü®tag n ŒΩ, tag_mem n ŒΩ, h ‚ñ∏ tag_mem n ŒΩ‚ü©‚ü©
  rw [‚Üê mem_box_iff_index.mp hx, ‚Üê mem_box_iff_index.mp hx']

theorem box_injective : Function.Injective (fun ŒΩ : Œπ ‚Üí ‚Ñ§ ‚Ü¶ box n ŒΩ) := by
  intro _ _ h
  contrapose! h
  exact Box.ne_of_disjoint_coe (disjoint.mp h)

lemma box.upper_sub_lower (ŒΩ : Œπ ‚Üí ‚Ñ§) (i : Œπ) :
    (box n ŒΩ ).upper i - (box n ŒΩ).lower i = 1 / n := by
  simp_rw [box, add_div, add_sub_cancel_left]

variable [Fintype Œπ]

theorem diam_boxIcc (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    Metric.diam (Box.Icc (box n ŒΩ)) ‚â§ 1 / n := by
  rw [BoxIntegral.Box.Icc_eq_pi]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) <| EMetric.diam_pi_le_of_le (fun i ‚Ü¶ ?_)
  simp_rw [Real.ediam_Icc, box.upper_sub_lower, le_rfl]

@[simp]
theorem volume_box (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    volume (box n ŒΩ : Set (Œπ ‚Üí ‚Ñù)) = 1 / n ^ card Œπ := by
  simp_rw [volume_pi, BoxIntegral.Box.coe_eq_pi, Measure.pi_pi, Real.volume_Ioc,
    box.upper_sub_lower, Finset.prod_const, ENNReal.ofReal_div_of_pos (Nat.cast_pos.mpr
    n.pos_of_neZero), ENNReal.ofReal_one, ENNReal.ofReal_natCast, one_div, ENNReal.inv_pow,
    Finset.card_univ]

theorem setFinite_index {s : Set (Œπ ‚Üí ‚Ñù)} (hs‚ÇÅ : NullMeasurableSet s) (hs‚ÇÇ : volume s ‚â† ‚ä§) :
    Set.Finite {ŒΩ : Œπ ‚Üí ‚Ñ§ | ‚Üë(box n ŒΩ) ‚äÜ s} := by
  refine (Measure.finite_const_le_meas_of_disjoint_iUnion‚ÇÄ volume (Œµ := 1 / n ^ card Œπ)
    (by simp) (As := fun ŒΩ : Œπ ‚Üí ‚Ñ§ ‚Ü¶ (box n ŒΩ) ‚à© s) (fun ŒΩ ‚Ü¶ ?_) (fun _ _ h ‚Ü¶ ?_) ?_).subset
      (fun _ hŒΩ ‚Ü¶ ?_)
  ¬∑ refine NullMeasurableSet.inter ?_ hs‚ÇÅ
    exact (box n ŒΩ).measurableSet_coe.nullMeasurableSet
  ¬∑ exact ((Disjoint.inter_right _ (disjoint.mp h)).inter_left _ ).aedisjoint
  ¬∑ exact lt_top_iff_ne_top.mp <| measure_lt_top_of_subset
      (by simp only [Set.iUnion_subset_iff, Set.inter_subset_right, implies_true]) hs‚ÇÇ
  ¬∑ rw [Set.mem_setOf, Set.inter_eq_self_of_subset_left hŒΩ, volume_box]

/-- For `B : BoxIntegral.Box`, the set of indices of `unitPartition.box` that are subsets of `B`.
This is a finite set. These boxes cover `B` if it has integral vertices, see
`unitPartition.prepartition_isPartition`. -/
def admissibleIndex (B : Box Œπ) : Finset (Œπ ‚Üí ‚Ñ§) := by
  refine (setFinite_index n B.measurableSet_coe.nullMeasurableSet ?_).toFinset
  exact lt_top_iff_ne_top.mp (IsBounded.measure_lt_top B.isBounded)

variable {n} in
theorem mem_admissibleIndex_iff {B : Box Œπ} {ŒΩ : Œπ ‚Üí ‚Ñ§} :
    ŒΩ ‚àà admissibleIndex n B ‚Üî box n ŒΩ ‚â§ B := by
  rw [admissibleIndex, Set.Finite.mem_toFinset, Set.mem_setOf_eq, Box.coe_subset_coe]

open Classical in
/-- For `B : BoxIntegral.Box`, the `TaggedPrepartition` formed by the set of all
`unitPartition.box` whose index is `B`-admissible. -/
def prepartition (B : Box Œπ) : TaggedPrepartition B where
  boxes := Finset.image (fun ŒΩ ‚Ü¶ box n ŒΩ) (admissibleIndex n B)
  le_of_mem' _ hI := by
    obtain ‚ü®_, hŒΩ, rfl‚ü© := Finset.mem_image.mp hI
    exact mem_admissibleIndex_iff.mp hŒΩ
  pairwiseDisjoint _ hI‚ÇÅ _ hI‚ÇÇ h := by
    obtain ‚ü®_, _, rfl‚ü© := Finset.mem_image.mp hI‚ÇÅ
    obtain ‚ü®_, _, rfl‚ü© := Finset.mem_image.mp hI‚ÇÇ
    exact disjoint.mp fun x ‚Ü¶ h (congrArg (box n) x)
  tag I :=
    if hI : ‚àÉ ŒΩ ‚àà admissibleIndex n B, I = box n ŒΩ then tag n hI.choose else B.exists_mem.choose
  tag_mem_Icc I := by
    by_cases hI : ‚àÉ ŒΩ ‚àà admissibleIndex n B, I = box n ŒΩ
    ¬∑ simp_rw [dif_pos hI]
      exact Box.coe_subset_Icc <| (mem_admissibleIndex_iff.mp hI.choose_spec.1) (tag_mem n _)
    ¬∑ simp_rw [dif_neg hI]
      exact Box.coe_subset_Icc B.exists_mem.choose_spec

variable {n} in
@[simp]
theorem mem_prepartition_iff {B I : Box Œπ} :
    I ‚àà prepartition n B ‚Üî ‚àÉ ŒΩ ‚àà admissibleIndex n B, box n ŒΩ = I := by
  classical
  rw [prepartition, TaggedPrepartition.mem_mk, Prepartition.mem_mk, Finset.mem_image]

variable {n} in
theorem mem_prepartition_boxes_iff {B I : Box Œπ} :
    I ‚àà (prepartition n B).boxes ‚Üî ‚àÉ ŒΩ ‚àà admissibleIndex n B, box n ŒΩ = I :=
  mem_prepartition_iff

theorem prepartition_tag {ŒΩ : Œπ ‚Üí ‚Ñ§} {B : Box Œπ} (hŒΩ : ŒΩ ‚àà admissibleIndex n B) :
    (prepartition n B).tag (box n ŒΩ) = tag n ŒΩ := by
  dsimp only [prepartition]
  have h : ‚àÉ ŒΩ' ‚àà admissibleIndex n B, box n ŒΩ = box n ŒΩ' := ‚ü®ŒΩ, hŒΩ, rfl‚ü©
  rw [dif_pos h, (tag_injective n).eq_iff, ‚Üê (box_injective n).eq_iff]
  exact h.choose_spec.2.symm

theorem box_index_tag_eq_self {B I : Box Œπ} (hI : I ‚àà (prepartition n B).boxes) :
    box n (index n ((prepartition n B).tag I)) = I := by
  obtain ‚ü®ŒΩ, hŒΩ, rfl‚ü© := mem_prepartition_boxes_iff.mp hI
  rw [prepartition_tag n hŒΩ, index_tag]

theorem prepartition_isHenstock (B : Box Œπ) :
    (prepartition n B).IsHenstock := by
  intro _ hI
  obtain ‚ü®ŒΩ, hŒΩ, rfl‚ü© := mem_prepartition_iff.mp hI
  rw [prepartition_tag n hŒΩ]
  exact Box.coe_subset_Icc (tag_mem _ _)

theorem prepartition_isSubordinate (B : Box Œπ) {r : ‚Ñù} (hr : 0 < r) (hn : 1 / n ‚â§ r) :
    (prepartition n B).IsSubordinate (fun _ ‚Ü¶ ‚ü®r, hr‚ü©) := by
  intro _ hI
  obtain ‚ü®ŒΩ, hŒΩ, rfl‚ü© := mem_prepartition_iff.mp hI
  refine fun _ h ‚Ü¶ le_trans (Metric.dist_le_diam_of_mem (Box.isBounded_Icc _) h ?_) ?_
  ¬∑ rw [prepartition_tag n hŒΩ]
    exact Box.coe_subset_Icc (tag_mem _ _)
  ¬∑ exact le_trans (diam_boxIcc n ŒΩ) hn

private theorem mem_admissibleIndex_of_mem_box_aux‚ÇÅ (x : ‚Ñù) (a : ‚Ñ§) :
    a < x ‚Üî a ‚â§ (‚åàn * x‚åâ - 1) / (n : ‚Ñù) := by
  have h : 0 < (n : ‚Ñù) := Nat.cast_pos.mpr <| n.pos_of_neZero
  rw [le_div_iff‚ÇÄ' h, le_sub_iff_add_le,
    show (n : ‚Ñù) * a + 1 = (n * a + 1 : ‚Ñ§) by norm_cast,
    Int.cast_le, Int.add_one_le_ceil_iff, Int.cast_mul, Int.cast_natCast, mul_lt_mul_left h]

private theorem mem_admissibleIndex_of_mem_box_aux‚ÇÇ (x : ‚Ñù) (a : ‚Ñ§) :
    x ‚â§ a ‚Üî (‚åàn * x‚åâ - 1 + 1) / (n : ‚Ñù) ‚â§ a := by
  have h : 0 < (n : ‚Ñù) := Nat.cast_pos.mpr <| n.pos_of_neZero
  rw [sub_add_cancel, div_le_iff‚ÇÄ' h,
    show (n : ‚Ñù) * a = (n * a : ‚Ñ§) by norm_cast,
    Int.cast_le, Int.ceil_le, Int.cast_mul, Int.cast_natCast, mul_le_mul_iff_right‚ÇÄ h]

/-- If `B : BoxIntegral.Box` has integral vertices and contains the point `x`, then the index of
`x` is admissible for `B`. -/
theorem mem_admissibleIndex_of_mem_box {B : Box Œπ} (hB : hasIntegralVertices B) {x : Œπ ‚Üí ‚Ñù}
    (hx : x ‚àà B) : index n x ‚àà admissibleIndex n B := by
  obtain ‚ü®l, u, hl, hu‚ü© := hB
  simp_rw [mem_admissibleIndex_iff, Box.le_iff_bounds, box_lower, box_upper, Pi.le_def,
    index_apply, hl, hu, ‚Üê forall_and]
  push_cast
  refine fun i ‚Ü¶ ‚ü®?_, ?_‚ü©
  ¬∑ exact (mem_admissibleIndex_of_mem_box_aux‚ÇÅ n (x i) (l i)).mp ((hl i) ‚ñ∏ (hx i).1)
  ¬∑ exact (mem_admissibleIndex_of_mem_box_aux‚ÇÇ n (x i) (u i)).mp ((hu i) ‚ñ∏ (hx i).2)

/-- If `B : BoxIntegral.Box` has integral vertices, then `prepartition n B` is a partition of
`B`. -/
theorem prepartition_isPartition {B : Box Œπ} (hB : hasIntegralVertices B) :
    (prepartition n B).IsPartition := by
  refine fun x hx ‚Ü¶ ‚ü®box n (index n x), ?_, mem_box_iff_index.mpr rfl‚ü©
  rw [TaggedPrepartition.mem_toPrepartition, mem_prepartition_iff]
  exact ‚ü®index n x, mem_admissibleIndex_of_mem_box n hB hx, rfl‚ü©

open Submodule Pointwise BigOperators

open scoped Pointwise

variable (c : ‚Ñù) (s : Set (Œπ ‚Üí ‚Ñù)) (F : (Œπ ‚Üí ‚Ñù) ‚Üí ‚Ñù)

-- The image of `Œπ ‚Üí ‚Ñ§` inside `Œπ ‚Üí ‚Ñù`
local notation "L" => span ‚Ñ§ (Set.range (Pi.basisFun ‚Ñù Œπ))

variable {n} in
theorem mem_smul_span_iff {v : Œπ ‚Üí ‚Ñù} :
    v ‚àà (n : ‚Ñù)‚Åª¬π ‚Ä¢ L ‚Üî ‚àÄ i, n * v i ‚àà Set.range (algebraMap ‚Ñ§ ‚Ñù) := by
  rw [ZSpan.smul _ (inv_ne_zero (NeZero.ne _)), Module.Basis.mem_span_iff_repr_mem]
  simp_rw [Module.Basis.repr_isUnitSMul, Pi.basisFun_repr, Units.smul_def, Units.val_inv_eq_inv_val,
    IsUnit.unit_spec, inv_inv, smul_eq_mul]

theorem tag_mem_smul_span (ŒΩ : Œπ ‚Üí ‚Ñ§) :
    tag n ŒΩ ‚àà (n : ‚Ñù)‚Åª¬π ‚Ä¢ L := by
  refine mem_smul_span_iff.mpr fun i ‚Ü¶ ‚ü®ŒΩ i + 1, ?_‚ü©
  rw [tag_apply, div_eq_inv_mul, ‚Üê mul_assoc, mul_inv_cancel_of_invertible, one_mul, map_add,
    map_one, eq_intCast]

theorem tag_index_eq_self_of_mem_smul_span {x : Œπ ‚Üí ‚Ñù} (hx : x ‚àà (n : ‚Ñù)‚Åª¬π ‚Ä¢ L) :
    tag n (index n x) = x := by
  rw [mem_smul_span_iff] at hx
  ext i
  obtain ‚ü®a, ha‚ü© : ‚àÉ a : ‚Ñ§, a = n * x i := hx i
  rwa [tag_apply, index_apply, Int.cast_sub, Int.cast_one, sub_add_cancel, ‚Üê ha, Int.ceil_intCast,
    div_eq_iff (NeZero.ne _), mul_comm]

theorem eq_of_mem_smul_span_of_index_eq_index {x y : Œπ ‚Üí ‚Ñù} (hx : x ‚àà (n : ‚Ñù)‚Åª¬π ‚Ä¢ L)
    (hy : y ‚àà (n : ‚Ñù)‚Åª¬π ‚Ä¢ L) (h : index n x = index n y) : x = y := by
  rw [‚Üê tag_index_eq_self_of_mem_smul_span n hx, ‚Üê tag_index_eq_self_of_mem_smul_span n hy, h]

theorem integralSum_eq_tsum_div {B : Box Œπ} (hB : hasIntegralVertices B) (hs‚ÇÄ : s ‚â§ B) :
    integralSum (Set.indicator s F) (BoxAdditiveMap.toSMul (Measure.toBoxAdditive volume))
      (prepartition n B) = (‚àë' x : ‚Üë(s ‚à© (n : ‚Ñù)‚Åª¬π ‚Ä¢ L), F x) / n ^ card Œπ := by
  classical
  unfold integralSum
  have : Fintype ‚Üë(s ‚à© (n : ‚Ñù)‚Åª¬π ‚Ä¢ L) := by
    apply Set.Finite.fintype
    rw [‚Üê coe_pointwise_smul, ZSpan.smul _ (inv_ne_zero (NeZero.ne _))]
    exact ZSpan.setFinite_inter _ (B.isBounded.subset hs‚ÇÄ)
  rw [tsum_fintype, Finset.sum_set_coe, Finset.sum_div, eq_comm]
  simp_rw [Set.indicator_apply, apply_ite, BoxAdditiveMap.toSMul_apply, Measure.toBoxAdditive_apply,
    smul_eq_mul, mul_zero, Finset.sum_ite, Finset.sum_const_zero, add_zero]
  refine Finset.sum_bij (fun x _ ‚Ü¶ box n (index n x)) (fun _ hx ‚Ü¶ Finset.mem_filter.mpr ?_)
    (fun _ hx _ hy h ‚Ü¶ ?_) (fun I hI ‚Ü¶ ?_) (fun _ hx ‚Ü¶ ?_)
  ¬∑ rw [Set.mem_toFinset] at hx
    refine ‚ü®mem_prepartition_boxes_iff.mpr
      ‚ü®index n _, mem_admissibleIndex_of_mem_box n hB (hs‚ÇÄ hx.1), rfl‚ü©, ?_‚ü©
    simp_rw [prepartition_tag n (mem_admissibleIndex_of_mem_box n hB (hs‚ÇÄ hx.1)),
      tag_index_eq_self_of_mem_smul_span n hx.2, hx.1]
  ¬∑ rw [Set.mem_toFinset] at hx hy
    exact eq_of_mem_smul_span_of_index_eq_index n hx.2 hy.2 (box_injective n h)
  ¬∑ rw [Finset.mem_filter] at hI
    refine ‚ü®(prepartition n B).tag I, Set.mem_toFinset.mpr ‚ü®hI.2, ?_‚ü©, box_index_tag_eq_self n hI.1‚ü©
    rw [‚Üê box_index_tag_eq_self n hI.1, prepartition_tag n
      (mem_admissibleIndex_of_mem_box n hB (hs‚ÇÄ hI.2))]
    exact tag_mem_smul_span _ _
  ¬∑ rw [Set.mem_toFinset] at hx
    rw [measureReal_def, volume_box,
      prepartition_tag n (mem_admissibleIndex_of_mem_box n hB (hs‚ÇÄ hx.1)),
      tag_index_eq_self_of_mem_smul_span n hx.2, ENNReal.toReal_div,
      ENNReal.toReal_one, ENNReal.toReal_pow, ENNReal.toReal_natCast, mul_comm_div, one_mul]

open Filter

/-- Let `s` be a bounded, measurable set of `Œπ ‚Üí ‚Ñù` whose frontier has zero volume and let `F`
be a continuous function. Then the limit as `n ‚Üí ‚àû` of `‚àë F x / n ^ card Œπ`, where the sum is
over the points in `s ‚à© n‚Åª¬π ‚Ä¢ (Œπ ‚Üí ‚Ñ§)`, tends to the integral of `F` over `s`. -/
theorem _root_.tendsto_tsum_div_pow_atTop_integral (hF : Continuous F) (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÇ : MeasurableSet s) (hs‚ÇÉ : volume (frontier s) = 0) :
    Tendsto (fun n : ‚Ñï ‚Ü¶ (‚àë' x : ‚Üë(s ‚à© (n : ‚Ñù)‚Åª¬π ‚Ä¢ L), F x) / n ^ card Œπ)
      atTop (nhds (‚à´ x in s, F x)) := by
  obtain ‚ü®B, hB, hs‚ÇÄ‚ü© := le_hasIntegralVertices_of_isBounded hs‚ÇÅ
  refine Metric.tendsto_atTop.mpr fun Œµ hŒµ ‚Ü¶ ?_
  have h‚ÇÅ : ‚àÉ C, ‚àÄ x ‚àà Box.Icc B, ‚ÄñSet.indicator s F x‚Äñ ‚â§ C := by
    obtain ‚ü®C‚ÇÄ, h‚ÇÄ‚ü© := (Box.isCompact_Icc B).exists_bound_of_continuousOn hF.continuousOn
    refine ‚ü®max 0 C‚ÇÄ, fun x hx ‚Ü¶ ?_‚ü©
    rw [Set.indicator]
    split_ifs with hs
    ¬∑ exact le_max_of_le_right (h‚ÇÄ x hx)
    ¬∑ exact norm_zero.trans_le <|le_max_left 0 _
  have h‚ÇÇ : ‚àÄ·µê x, ContinuousAt (s.indicator F) x := by
    filter_upwards [compl_mem_ae_iff.mpr hs‚ÇÉ] with _ h
      using (hF.continuousOn).continuousAt_indicator h
  obtain ‚ü®r, hr‚ÇÅ, hr‚ÇÇ‚ü© := (hasIntegral_iff.mp <|
      AEContinuous.hasBoxIntegral (volume : Measure (Œπ ‚Üí ‚Ñù)) h‚ÇÅ h‚ÇÇ
        IntegrationParams.Riemann) (Œµ / 2) (half_pos hŒµ)
  refine ‚ü®‚åà(r 0 0 : ‚Ñù)‚Åª¬π‚åâ‚Çä, fun n hn ‚Ü¶ lt_of_le_of_lt ?_ (half_lt_self_iff.mpr hŒµ)‚ü©
  have : NeZero n :=
    ‚ü®Nat.ne_zero_iff_zero_lt.mpr <| (Nat.ceil_pos.mpr (inv_pos.mpr (r 0 0).prop)).trans_le hn‚ü©
  rw [‚Üê integralSum_eq_tsum_div _ s F hB hs‚ÇÄ, ‚Üê Measure.restrict_restrict_of_subset hs‚ÇÄ,
    ‚Üê integral_indicator hs‚ÇÇ]
  refine hr‚ÇÇ 0 _ ‚ü®?_, fun _ ‚Ü¶ ?_, fun h ‚Ü¶ ?_, fun h ‚Ü¶ ?_‚ü© (prepartition_isPartition _ hB)
  ¬∑ rw [show r 0 = fun _ ‚Ü¶ r 0 0 from funext_iff.mpr (hr‚ÇÅ 0 rfl)]
    apply prepartition_isSubordinate n B
    rw [one_div, inv_le_comm‚ÇÄ (mod_cast (Nat.pos_of_neZero n)) (r 0 0).prop]
    exact le_trans (Nat.le_ceil _) (Nat.cast_le.mpr hn)
  ¬∑ exact prepartition_isHenstock n B
  ¬∑ simp only [IntegrationParams.Riemann, Bool.false_eq_true] at h
  ¬∑ simp only [IntegrationParams.Riemann, Bool.false_eq_true] at h

/-- Let `s` be a bounded, measurable set of `Œπ ‚Üí ‚Ñù` whose frontier has zero volume. Then the limit
as `n ‚Üí ‚àû` of `card (s ‚à© n‚Åª¬π ‚Ä¢ (Œπ ‚Üí ‚Ñ§)) / n ^ card Œπ` tends to the volume of `s`. This is a
special case of `tendsto_card_div_pow` with `F = 1`. -/
theorem _root_.tendsto_card_div_pow_atTop_volume (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÇ : MeasurableSet s) (hs‚ÇÉ : volume (frontier s) = 0) :
    Tendsto (fun n : ‚Ñï ‚Ü¶ (Nat.card ‚Üë(s ‚à© (n : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / n ^ card Œπ)
      atTop (ùìù (volume.real s)) := by
  convert tendsto_tsum_div_pow_atTop_integral s (fun _ ‚Ü¶ 1) continuous_const hs‚ÇÅ hs‚ÇÇ hs‚ÇÉ
  ¬∑ rw [tsum_const, nsmul_eq_mul, mul_one, Nat.cast_inj]
  ¬∑ rw [setIntegral_const, smul_eq_mul, mul_one]

private def tendsto_card_div_pow‚ÇÅ {c : ‚Ñù} (hc : c ‚â† 0) :
    ‚Üë(s ‚à© c‚Åª¬π ‚Ä¢ L) ‚âÉ ‚Üë(c ‚Ä¢ s ‚à© L) :=
  Equiv.subtypeEquiv (Equiv.smulRight hc) (fun x ‚Ü¶ by
    simp_rw [Set.mem_inter_iff, Equiv.smulRight_apply, Set.smul_mem_smul_set_iff‚ÇÄ hc,
      ‚Üê Set.mem_inv_smul_set_iff‚ÇÄ hc])

private theorem tendsto_card_div_pow‚ÇÇ (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÑ : ‚àÄ ‚¶Éx y : ‚Ñù‚¶Ñ, 0 < x ‚Üí x ‚â§ y ‚Üí x ‚Ä¢ s ‚äÜ y ‚Ä¢ s) {x y : ‚Ñù} (hx : 0 < x) (hy : x ‚â§ y) :
    Nat.card ‚Üë(s ‚à© x‚Åª¬π ‚Ä¢ L) ‚â§ Nat.card ‚Üë(s ‚à© y‚Åª¬π ‚Ä¢ L) := by
  rw [Nat.card_congr (tendsto_card_div_pow‚ÇÅ s hx.ne'),
      Nat.card_congr (tendsto_card_div_pow‚ÇÅ s (hx.trans_le hy).ne')]
  refine Nat.card_mono ?_ ?_
  ¬∑ exact ZSpan.setFinite_inter _ (IsBounded.smul‚ÇÄ hs‚ÇÅ y)
  ¬∑ exact Set.inter_subset_inter_left _ <| hs‚ÇÑ hx hy

private theorem tendsto_card_div_pow‚ÇÉ (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÑ : ‚àÄ ‚¶Éx y : ‚Ñù‚¶Ñ, 0 < x ‚Üí x ‚â§ y ‚Üí x ‚Ä¢ s ‚äÜ y ‚Ä¢ s) :
    ‚àÄ·∂† x : ‚Ñù in atTop, (Nat.card ‚Üë(s ‚à© (‚åäx‚åã‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ ‚â§
      (Nat.card ‚Üë(s ‚à© x‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  gcongr
  exact tendsto_card_div_pow‚ÇÇ s hs‚ÇÅ hs‚ÇÑ (Nat.cast_pos.mpr (Nat.floor_pos.mpr hx))
    (Nat.floor_le (zero_le_one.trans hx))

private theorem tendsto_card_div_pow‚ÇÑ (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÑ : ‚àÄ ‚¶Éx y : ‚Ñù‚¶Ñ, 0 < x ‚Üí x ‚â§ y ‚Üí x ‚Ä¢ s ‚äÜ y ‚Ä¢ s) :
    ‚àÄ·∂† x : ‚Ñù in atTop, (Nat.card ‚Üë(s ‚à© x‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ ‚â§
      (Nat.card ‚Üë(s ‚à© (‚åàx‚åâ‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ := by
  filter_upwards [eventually_gt_atTop 0] with x hx
  gcongr
  exact tendsto_card_div_pow‚ÇÇ s hs‚ÇÅ hs‚ÇÑ hx (Nat.le_ceil _)

private theorem tendsto_card_div_pow‚ÇÖ :
    (fun x ‚Ü¶ (Nat.card ‚Üë(s ‚à© (‚åäx‚åã‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / ‚åäx‚åã‚Çä ^ card Œπ * (‚åäx‚åã‚Çä / x) ^ card Œπ)
      =·∂†[atTop] (fun x ‚Ü¶ (Nat.card ‚Üë(s ‚à© (‚åäx‚åã‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ) := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  have : 0 < ‚åäx‚åã‚Çä := Nat.floor_pos.mpr hx
  rw [div_pow, mul_div, div_mul_cancel‚ÇÄ _ (by positivity)]

private theorem tendsto_card_div_pow‚ÇÜ :
    (fun x ‚Ü¶ (Nat.card ‚Üë(s ‚à© (‚åàx‚åâ‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / ‚åàx‚åâ‚Çä ^ card Œπ * (‚åàx‚åâ‚Çä / x) ^ card Œπ)
          =·∂†[atTop] (fun x ‚Ü¶ (Nat.card ‚Üë(s ‚à© (‚åàx‚åâ‚Çä : ‚Ñù)‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ) := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  rw [div_pow, mul_div, div_mul_cancel‚ÇÄ _ (by positivity)]

/-- A version of `tendsto_card_div_pow_atTop_volume` for a real variable. -/
theorem _root_.tendsto_card_div_pow_atTop_volume' (hs‚ÇÅ : IsBounded s)
    (hs‚ÇÇ : MeasurableSet s) (hs‚ÇÉ : volume (frontier s) = 0)
    (hs‚ÇÑ : ‚àÄ ‚¶Éx y : ‚Ñù‚¶Ñ, 0 < x ‚Üí x ‚â§ y ‚Üí x ‚Ä¢ s ‚äÜ y ‚Ä¢ s) :
    Tendsto (fun x : ‚Ñù ‚Ü¶ (Nat.card ‚Üë(s ‚à© x‚Åª¬π ‚Ä¢ L) : ‚Ñù) / x ^ card Œπ)
      atTop (ùìù (volume.real s)) := by
  rw [show volume.real s = volume.real s * 1 ^ card Œπ by ring]
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' ?_ ?_
    (tendsto_card_div_pow‚ÇÉ s hs‚ÇÅ hs‚ÇÑ) (tendsto_card_div_pow‚ÇÑ s hs‚ÇÅ hs‚ÇÑ)
  ¬∑ refine Tendsto.congr' (tendsto_card_div_pow‚ÇÖ s) (Tendsto.mul ?_ (Tendsto.pow ?_ _))
    ¬∑ exact Tendsto.comp (tendsto_card_div_pow_atTop_volume s hs‚ÇÅ hs‚ÇÇ hs‚ÇÉ) tendsto_nat_floor_atTop
    ¬∑ exact tendsto_nat_floor_div_atTop
  ¬∑ refine Tendsto.congr' (tendsto_card_div_pow‚ÇÜ s) (Tendsto.mul ?_ (Tendsto.pow ?_ _))
    ¬∑ exact Tendsto.comp (tendsto_card_div_pow_atTop_volume s hs‚ÇÅ hs‚ÇÇ hs‚ÇÉ) tendsto_nat_ceil_atTop
    ¬∑ exact tendsto_nat_ceil_div_atTop

end BoxIntegral.unitPartition
