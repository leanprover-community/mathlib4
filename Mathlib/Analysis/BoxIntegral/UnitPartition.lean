import Mathlib.Analysis.BoxIntegral.Basic
import Mathlib.MeasureTheory.Integral.Bochner.Basic

/-!
# Unit Partition

Fix `n` a positive integer. `BoxIntegral.unitPartition.box` are boxes in `Î¹ â†’ â„` obtained by
dividing the unit box uniformly into boxes of side length `1 / n` and translating the boxes by
vectors `Î½ : Î¹ â†’ â„¤`.

Let `B` be a `BoxIntegral`. A `unitPartition.box` is admissible for `B` (more precisely its index is
admissible) if it is contained in `B`. There are finitely many admissible `unitPartition.box` for
`B` and thus we can form the corresponding tagged prepartition, see
`BoxIntegral.unitPartition.prepartition` (note that each `unitPartition.box` comes with its
tag situated at its "upper most" vertex). If `B` satisfies `hasIntegralVertices`, that
is its vertices are in `Î¹ â†’ â„¤`, then the corresponding prepartition is actually a partition.

## Main definitions and results

* `BoxIntegral.hasIntegralVertices`: a `Prop` that states that the vertices of the box have
  coordinates in `â„¤`

* `BoxIntegral.unitPartition.box`: a `BoxIntegral`, indexed by `Î½ : Î¹ â†’ â„¤`, with vertices
  `Î½ i / n` and of side length `1 / n`.

* `BoxIntegral.unitPartition.admissibleIndex`: For `B : BoxIntegral.Box`, the set of indices of
  `unitPartition.box` that are subsets of `B`. This is a finite set.

* `BoxIntegral.unitPartition.prepartition_isPartition`: For `B : BoxIntegral.Box`, if `B`
  has integral vertices, then the prepartition of `unitPartition.box` admissible for `B` is a
  partition of `B`.

* `tendsto_tsum_div_pow_atTop_integral`: let `s` be a bounded, measurable set of `Î¹ â†’ â„`
  whose frontier has zero volume and let `F` be a continuous function. Then the limit as `n â†’ âˆ`
  of `âˆ‘ F x / n ^ card Î¹`, where the sum is over the points in `s âˆ© nâ»Â¹ â€¢ (Î¹ â†’ â„¤)`, tends to the
  integral of `F` over `s`.

* `tendsto_card_div_pow_atTop_volume`: let `s` be a bounded, measurable set of `Î¹ â†’ â„` whose
  frontier has zero volume. Then the limit as `n â†’ âˆ` of `card (s âˆ© nâ»Â¹ â€¢ (Î¹ â†’ â„¤)) / n ^ card Î¹`
  tends to the volume of `s`.

* `tendsto_card_div_pow_atTop_volume'`: a version of `tendsto_card_div_pow_atTop_volume` where we
  assume in addition that `x â€¢ s âŠ† y â€¢ s` whenever `0 < x â‰¤ y`. Then we get the same limit
  `card (s âˆ© xâ»Â¹ â€¢ (Î¹ â†’ â„¤)) / x ^ card Î¹ â†’ volume s` but the limit is over a real variable `x`.

-/

@[expose] public section

noncomputable section

variable {Î¹ : Type*}

open scoped Topology

section hasIntegralVertices

open Bornology

/-- A `BoxIntegral.Box` has integral vertices if its vertices have coordinates in `â„¤`. -/
def BoxIntegral.hasIntegralVertices (B : Box Î¹) : Prop :=
  âˆƒ l u : Î¹ â†’ â„¤, (âˆ€ i, B.lower i = l i) âˆ§ (âˆ€ i, B.upper i = u i)

/-- Any bounded set is contained in a `BoxIntegral.Box` with integral vertices. -/
theorem BoxIntegral.le_hasIntegralVertices_of_isBounded [Finite Î¹] {s : Set (Î¹ â†’ â„)}
    (h : IsBounded s) :
    âˆƒ B : BoxIntegral.Box Î¹, hasIntegralVertices B âˆ§ s â‰¤ B := by
  have := Fintype.ofFinite Î¹
  obtain âŸ¨R, hRâ‚, hRâ‚‚âŸ© := IsBounded.subset_ball_lt h 0 0
  let C : â„• := âŒˆRâŒ‰â‚Š
  have hC := Nat.ceil_pos.mpr hRâ‚
  let I : Box Î¹ := Box.mk (fun _ â†¦ -C) (fun _ â†¦ C)
    (fun _ â†¦ by simp [C, neg_lt_self_iff, Nat.cast_pos, hC])
  refine âŸ¨I, âŸ¨fun _ â†¦ - C, fun _ â†¦ C, fun i â†¦ (Int.cast_neg_natCast C).symm, fun _ â†¦ rflâŸ©,
    le_trans hRâ‚‚ ?_âŸ©
  suffices Metric.ball (0 : Î¹ â†’ â„) C â‰¤ I from
    le_trans (Metric.ball_subset_ball (Nat.le_ceil R)) this
  intro x hx
  simp_rw [C, mem_ball_zero_iff, pi_norm_lt_iff (Nat.cast_pos.mpr hC),
    Real.norm_eq_abs, abs_lt] at hx
  exact fun i â†¦ âŸ¨(hx i).1, le_of_lt (hx i).2âŸ©

end hasIntegralVertices

namespace BoxIntegral.unitPartition

open Bornology MeasureTheory Fintype BoxIntegral

variable (n : â„•)

/-- A `BoxIntegral`, indexed by a positive integer `n` and `Î½ : Î¹ â†’ â„¤`, with corners `Î½ i / n`
and of side length `1 / n`. -/
def box [NeZero n] (Î½ : Î¹ â†’ â„¤) : Box Î¹ where
  lower := fun i â†¦ Î½ i / n
  upper := fun i â†¦ (Î½ i + 1) / n
  lower_lt_upper := fun _ â†¦ by simp [add_div, n.pos_of_neZero]

@[simp]
theorem box_lower [NeZero n] (Î½ : Î¹ â†’ â„¤) :
    (box n Î½).lower = fun i â†¦ (Î½ i / n : â„) := rfl

@[simp]
theorem box_upper [NeZero n] (Î½ : Î¹ â†’ â„¤) :
    (box n Î½).upper = fun i â†¦ ((Î½ i + 1) / n : â„) := rfl

variable {n} in
@[simp]
theorem mem_box_iff [NeZero n] {Î½ : Î¹ â†’ â„¤} {x : Î¹ â†’ â„} :
    x âˆˆ box n Î½ â†” âˆ€ i, Î½ i / n < x i âˆ§ x i â‰¤ (Î½ i + 1) / n := by
  simp_rw [Box.mem_def, box, Set.mem_Ioc]

variable {n} in
theorem mem_box_iff' [NeZero n] {Î½ : Î¹ â†’ â„¤} {x : Î¹ â†’ â„} :
    x âˆˆ box n Î½ â†” âˆ€ i, Î½ i < n * x i âˆ§ n * x i â‰¤ Î½ i + 1 := by
  have h : 0 < (n : â„) := Nat.cast_pos.mpr <| n.pos_of_neZero
  simp_rw [mem_box_iff, â† _root_.le_div_iffâ‚€' h, â† div_lt_iffâ‚€' h]

/-- The tag of (the index of) a `unitPartition.box`. -/
abbrev tag (Î½ : Î¹ â†’ â„¤) : Î¹ â†’ â„ := fun i â†¦ (Î½ i + 1) / n

@[simp]
theorem tag_apply (Î½ : Î¹ â†’ â„¤) (i : Î¹) : tag n Î½ i = (Î½ i + 1) / n := rfl

variable [NeZero n]

theorem tag_injective : Function.Injective (fun Î½ : Î¹ â†’ â„¤ â†¦ tag n Î½) := by
  refine fun _ _ h â†¦ funext_iff.mpr fun i â†¦ ?_
  have := congr_arg (fun x â†¦ x i) h
  simp_rw [tag_apply, div_left_inj' (c := (n : â„)) (Nat.cast_ne_zero.mpr (NeZero.ne n)),
    add_left_inj, Int.cast_inj] at this
  exact this

theorem tag_mem (Î½ : Î¹ â†’ â„¤) :
    tag n Î½ âˆˆ box n Î½ := by
  refine mem_box_iff.mpr fun _ â†¦ ?_
  rw [tag, add_div]
  have h : 0 < (n : â„) := Nat.cast_pos.mpr <| n.pos_of_neZero
  exact âŸ¨lt_add_of_pos_right _ (by positivity), le_rflâŸ©

/-- For `x : Î¹ â†’ â„`, its index is the index of the unique `unitPartition.box` to which
it belongs. -/
def index (x : Î¹ â†’ â„) (i : Î¹) : â„¤ := âŒˆn * x iâŒ‰ - 1

@[simp]
theorem index_apply (m : â„•) {x : Î¹ â†’ â„} (i : Î¹) :
    index m x i = âŒˆm * x iâŒ‰ - 1 := rfl

variable {n} in
theorem mem_box_iff_index {x : Î¹ â†’ â„} {Î½ : Î¹ â†’ â„¤} :
    x âˆˆ box n Î½ â†” index n x = Î½ := by
  simp_rw [mem_box_iff', funext_iff, index_apply, sub_eq_iff_eq_add, Int.ceil_eq_iff,
    Int.cast_add, Int.cast_one, add_sub_cancel_right]

@[simp]
theorem index_tag (Î½ : Î¹ â†’ â„¤) :
    index n (tag n Î½) = Î½ := mem_box_iff_index.mp (tag_mem n Î½)

variable {n} in
theorem disjoint {Î½ Î½' : Î¹ â†’ â„¤} :
    Î½ â‰  Î½' â†” Disjoint (box n Î½).toSet (box n Î½').toSet := by
  rw [not_iff_comm, Set.not_disjoint_iff]
  refine âŸ¨fun âŸ¨x, hx, hx'âŸ© â†¦ ?_, fun h â†¦ âŸ¨tag n Î½, tag_mem n Î½, h â–¸ tag_mem n Î½âŸ©âŸ©
  rw [â† mem_box_iff_index.mp hx, â† mem_box_iff_index.mp hx']

theorem box_injective : Function.Injective (fun Î½ : Î¹ â†’ â„¤ â†¦ box n Î½) := by
  intro _ _ h
  contrapose! h
  exact Box.ne_of_disjoint_coe (disjoint.mp h)

lemma box.upper_sub_lower (Î½ : Î¹ â†’ â„¤) (i : Î¹) :
    (box n Î½).upper i - (box n Î½).lower i = 1 / n := by
  simp_rw [box, add_div, add_sub_cancel_left]

variable [Fintype Î¹]

theorem diam_boxIcc (Î½ : Î¹ â†’ â„¤) :
    Metric.diam (Box.Icc (box n Î½)) â‰¤ 1 / n := by
  rw [BoxIntegral.Box.Icc_eq_pi]
  refine ENNReal.toReal_le_of_le_ofReal (by positivity) <| Metric.ediam_pi_le_of_le fun i â†¦ ?_
  simp_rw [Real.ediam_Icc, box.upper_sub_lower, le_rfl]

@[simp]
theorem volume_box (Î½ : Î¹ â†’ â„¤) :
    volume (box n Î½ : Set (Î¹ â†’ â„)) = 1 / n ^ card Î¹ := by
  simp_rw [volume_pi, BoxIntegral.Box.coe_eq_pi, Measure.pi_pi, Real.volume_Ioc,
    box.upper_sub_lower, Finset.prod_const, ENNReal.ofReal_div_of_pos (Nat.cast_pos.mpr
    n.pos_of_neZero), ENNReal.ofReal_one, ENNReal.ofReal_natCast, one_div, ENNReal.inv_pow,
    Finset.card_univ]

theorem setFinite_index {s : Set (Î¹ â†’ â„)} (hsâ‚ : NullMeasurableSet s) (hsâ‚‚ : volume s â‰  âŠ¤) :
    Set.Finite {Î½ : Î¹ â†’ â„¤ | â†‘(box n Î½) âŠ† s} := by
  refine (Measure.finite_const_le_meas_of_disjoint_iUnionâ‚€ volume (Îµ := 1 / n ^ card Î¹)
    (by simp) (As := fun Î½ : Î¹ â†’ â„¤ â†¦ (box n Î½) âˆ© s) (fun Î½ â†¦ ?_) (fun _ _ h â†¦ ?_) ?_).subset
      (fun _ hÎ½ â†¦ ?_)
  Â· refine NullMeasurableSet.inter ?_ hsâ‚
    exact (box n Î½).measurableSet_coe.nullMeasurableSet
  Â· exact ((Disjoint.inter_right _ (disjoint.mp h)).inter_left _).aedisjoint
  Â· exact lt_top_iff_ne_top.mp <| measure_lt_top_of_subset
      (by simp only [Set.iUnion_subset_iff, Set.inter_subset_right, implies_true]) hsâ‚‚
  Â· rw [Set.mem_setOf, Set.inter_eq_self_of_subset_left hÎ½, volume_box]

/-- For `B : BoxIntegral.Box`, the set of indices of `unitPartition.box` that are subsets of `B`.
This is a finite set. These boxes cover `B` if it has integral vertices, see
`unitPartition.prepartition_isPartition`. -/
def admissibleIndex (B : Box Î¹) : Finset (Î¹ â†’ â„¤) := by
  refine (setFinite_index n B.measurableSet_coe.nullMeasurableSet ?_).toFinset
  exact lt_top_iff_ne_top.mp (IsBounded.measure_lt_top B.isBounded)

variable {n} in
theorem mem_admissibleIndex_iff {B : Box Î¹} {Î½ : Î¹ â†’ â„¤} :
    Î½ âˆˆ admissibleIndex n B â†” box n Î½ â‰¤ B := by
  rw [admissibleIndex, Set.Finite.mem_toFinset, Set.mem_setOf_eq, Box.coe_subset_coe]

open Classical in
/-- For `B : BoxIntegral.Box`, the `TaggedPrepartition` formed by the set of all
`unitPartition.box` whose index is `B`-admissible. -/
def prepartition (B : Box Î¹) : TaggedPrepartition B where
  boxes := Finset.image (fun Î½ â†¦ box n Î½) (admissibleIndex n B)
  le_of_mem' _ hI := by
    obtain âŸ¨_, hÎ½, rflâŸ© := Finset.mem_image.mp hI
    exact mem_admissibleIndex_iff.mp hÎ½
  pairwiseDisjoint _ hIâ‚ _ hIâ‚‚ h := by
    obtain âŸ¨_, _, rflâŸ© := Finset.mem_image.mp hIâ‚
    obtain âŸ¨_, _, rflâŸ© := Finset.mem_image.mp hIâ‚‚
    exact disjoint.mp fun x â†¦ h (congrArg (box n) x)
  tag I :=
    if hI : âˆƒ Î½ âˆˆ admissibleIndex n B, I = box n Î½ then tag n hI.choose else B.exists_mem.choose
  tag_mem_Icc I := by
    by_cases hI : âˆƒ Î½ âˆˆ admissibleIndex n B, I = box n Î½
    Â· simp_rw [dif_pos hI]
      exact Box.coe_subset_Icc <| (mem_admissibleIndex_iff.mp hI.choose_spec.1) (tag_mem n _)
    Â· simp_rw [dif_neg hI]
      exact Box.coe_subset_Icc B.exists_mem.choose_spec

variable {n} in
@[simp]
theorem mem_prepartition_iff {B I : Box Î¹} :
    I âˆˆ prepartition n B â†” âˆƒ Î½ âˆˆ admissibleIndex n B, box n Î½ = I := by
  classical
  rw [prepartition, TaggedPrepartition.mem_mk, Prepartition.mem_mk, Finset.mem_image]

variable {n} in
theorem mem_prepartition_boxes_iff {B I : Box Î¹} :
    I âˆˆ (prepartition n B).boxes â†” âˆƒ Î½ âˆˆ admissibleIndex n B, box n Î½ = I :=
  mem_prepartition_iff

theorem prepartition_tag {Î½ : Î¹ â†’ â„¤} {B : Box Î¹} (hÎ½ : Î½ âˆˆ admissibleIndex n B) :
    (prepartition n B).tag (box n Î½) = tag n Î½ := by
  dsimp only [prepartition]
  have h : âˆƒ Î½' âˆˆ admissibleIndex n B, box n Î½ = box n Î½' := âŸ¨Î½, hÎ½, rflâŸ©
  rw [dif_pos h, (tag_injective n).eq_iff, â† (box_injective n).eq_iff]
  exact h.choose_spec.2.symm

theorem box_index_tag_eq_self {B I : Box Î¹} (hI : I âˆˆ (prepartition n B).boxes) :
    box n (index n ((prepartition n B).tag I)) = I := by
  obtain âŸ¨Î½, hÎ½, rflâŸ© := mem_prepartition_boxes_iff.mp hI
  rw [prepartition_tag n hÎ½, index_tag]

theorem prepartition_isHenstock (B : Box Î¹) :
    (prepartition n B).IsHenstock := by
  intro _ hI
  obtain âŸ¨Î½, hÎ½, rflâŸ© := mem_prepartition_iff.mp hI
  rw [prepartition_tag n hÎ½]
  exact Box.coe_subset_Icc (tag_mem _ _)

theorem prepartition_isSubordinate (B : Box Î¹) {r : â„} (hr : 0 < r) (hn : 1 / n â‰¤ r) :
    (prepartition n B).IsSubordinate (fun _ â†¦ âŸ¨r, hrâŸ©) := by
  intro _ hI
  obtain âŸ¨Î½, hÎ½, rflâŸ© := mem_prepartition_iff.mp hI
  refine fun _ h â†¦ le_trans (Metric.dist_le_diam_of_mem (Box.isBounded_Icc _) h ?_) ?_
  Â· rw [prepartition_tag n hÎ½]
    exact Box.coe_subset_Icc (tag_mem _ _)
  Â· exact le_trans (diam_boxIcc n Î½) hn

private theorem mem_admissibleIndex_of_mem_box_auxâ‚ (x : â„) (a : â„¤) :
    a < x â†” a â‰¤ (âŒˆn * xâŒ‰ - 1) / (n : â„) := by
  have h : 0 < (n : â„) := Nat.cast_pos.mpr <| n.pos_of_neZero
  rw [le_div_iffâ‚€' h, le_sub_iff_add_le,
    show (n : â„) * a + 1 = (n * a + 1 : â„¤) by norm_cast,
    Int.cast_le, Int.add_one_le_iff, Int.lt_ceil, Int.cast_mul, Int.cast_natCast,
    mul_lt_mul_iff_rightâ‚€ h]

private theorem mem_admissibleIndex_of_mem_box_auxâ‚‚ (x : â„) (a : â„¤) :
    x â‰¤ a â†” (âŒˆn * xâŒ‰ - 1 + 1) / (n : â„) â‰¤ a := by
  have h : 0 < (n : â„) := Nat.cast_pos.mpr <| n.pos_of_neZero
  rw [sub_add_cancel, div_le_iffâ‚€' h,
    show (n : â„) * a = (n * a : â„¤) by norm_cast,
    Int.cast_le, Int.ceil_le, Int.cast_mul, Int.cast_natCast, mul_le_mul_iff_rightâ‚€ h]

/-- If `B : BoxIntegral.Box` has integral vertices and contains the point `x`, then the index of
`x` is admissible for `B`. -/
theorem mem_admissibleIndex_of_mem_box {B : Box Î¹} (hB : hasIntegralVertices B) {x : Î¹ â†’ â„}
    (hx : x âˆˆ B) : index n x âˆˆ admissibleIndex n B := by
  obtain âŸ¨l, u, hl, huâŸ© := hB
  simp_rw [mem_admissibleIndex_iff, Box.le_iff_bounds, box_lower, box_upper, Pi.le_def,
    index_apply, hl, hu, â† forall_and]
  push_cast
  refine fun i â†¦ âŸ¨?_, ?_âŸ©
  Â· exact (mem_admissibleIndex_of_mem_box_auxâ‚ n (x i) (l i)).mp ((hl i) â–¸ (hx i).1)
  Â· exact (mem_admissibleIndex_of_mem_box_auxâ‚‚ n (x i) (u i)).mp ((hu i) â–¸ (hx i).2)

/-- If `B : BoxIntegral.Box` has integral vertices, then `prepartition n B` is a partition of
`B`. -/
theorem prepartition_isPartition {B : Box Î¹} (hB : hasIntegralVertices B) :
    (prepartition n B).IsPartition := by
  refine fun x hx â†¦ âŸ¨box n (index n x), ?_, mem_box_iff_index.mpr rflâŸ©
  rw [TaggedPrepartition.mem_toPrepartition, mem_prepartition_iff]
  exact âŸ¨index n x, mem_admissibleIndex_of_mem_box n hB hx, rflâŸ©

open Submodule Pointwise BigOperators

open scoped Pointwise

variable (c : â„) (s : Set (Î¹ â†’ â„)) (F : (Î¹ â†’ â„) â†’ â„)

-- The image of `Î¹ â†’ â„¤` inside `Î¹ â†’ â„`
local notation "L" => span â„¤ (Set.range (Pi.basisFun â„ Î¹))

variable {n} in
theorem mem_smul_span_iff {v : Î¹ â†’ â„} :
    v âˆˆ (n : â„)â»Â¹ â€¢ L â†” âˆ€ i, n * v i âˆˆ Set.range (algebraMap â„¤ â„) := by
  rw [ZSpan.smul _ (inv_ne_zero (NeZero.ne _)), Module.Basis.mem_span_iff_repr_mem]
  simp_rw [Module.Basis.repr_isUnitSMul, Pi.basisFun_repr, Units.smul_def, Units.val_inv_eq_inv_val,
    IsUnit.unit_spec, inv_inv, smul_eq_mul]

theorem tag_mem_smul_span (Î½ : Î¹ â†’ â„¤) :
    tag n Î½ âˆˆ (n : â„)â»Â¹ â€¢ L := by
  refine mem_smul_span_iff.mpr fun i â†¦ âŸ¨Î½ i + 1, ?_âŸ©
  rw [tag_apply, div_eq_inv_mul, â† mul_assoc, mul_inv_cancel_of_invertible, one_mul, map_add,
    map_one, eq_intCast]

theorem tag_index_eq_self_of_mem_smul_span {x : Î¹ â†’ â„} (hx : x âˆˆ (n : â„)â»Â¹ â€¢ L) :
    tag n (index n x) = x := by
  rw [mem_smul_span_iff] at hx
  ext i
  obtain âŸ¨a, haâŸ© : âˆƒ a : â„¤, a = n * x i := hx i
  rwa [tag_apply, index_apply, Int.cast_sub, Int.cast_one, sub_add_cancel, â† ha, Int.ceil_intCast,
    div_eq_iff (NeZero.ne _), mul_comm]

theorem eq_of_mem_smul_span_of_index_eq_index {x y : Î¹ â†’ â„} (hx : x âˆˆ (n : â„)â»Â¹ â€¢ L)
    (hy : y âˆˆ (n : â„)â»Â¹ â€¢ L) (h : index n x = index n y) : x = y := by
  rw [â† tag_index_eq_self_of_mem_smul_span n hx, â† tag_index_eq_self_of_mem_smul_span n hy, h]

theorem integralSum_eq_tsum_div {B : Box Î¹} (hB : hasIntegralVertices B) (hsâ‚€ : s â‰¤ B) :
    integralSum (Set.indicator s F) (BoxAdditiveMap.toSMul (Measure.toBoxAdditive volume))
      (prepartition n B) = (âˆ‘' x : â†‘(s âˆ© (n : â„)â»Â¹ â€¢ L), F x) / n ^ card Î¹ := by
  classical
  unfold integralSum
  have : Fintype â†‘(s âˆ© (n : â„)â»Â¹ â€¢ L) := by
    apply Set.Finite.fintype
    rw [â† coe_pointwise_smul, ZSpan.smul _ (inv_ne_zero (NeZero.ne _))]
    exact ZSpan.setFinite_inter _ (B.isBounded.subset hsâ‚€)
  rw [tsum_fintype, Finset.sum_set_coe, Finset.sum_div, eq_comm]
  simp_rw [Set.indicator_apply, apply_ite, BoxAdditiveMap.toSMul_apply, Measure.toBoxAdditive_apply,
    smul_eq_mul, mul_zero, Finset.sum_ite, Finset.sum_const_zero, add_zero]
  refine Finset.sum_bij (fun x _ â†¦ box n (index n x)) (fun _ hx â†¦ Finset.mem_filter.mpr ?_)
    (fun _ hx _ hy h â†¦ ?_) (fun I hI â†¦ ?_) (fun _ hx â†¦ ?_)
  Â· rw [Set.mem_toFinset] at hx
    refine âŸ¨mem_prepartition_boxes_iff.mpr
      âŸ¨index n _, mem_admissibleIndex_of_mem_box n hB (hsâ‚€ hx.1), rflâŸ©, ?_âŸ©
    simp_rw [prepartition_tag n (mem_admissibleIndex_of_mem_box n hB (hsâ‚€ hx.1)),
      tag_index_eq_self_of_mem_smul_span n hx.2, hx.1]
  Â· rw [Set.mem_toFinset] at hx hy
    exact eq_of_mem_smul_span_of_index_eq_index n hx.2 hy.2 (box_injective n h)
  Â· rw [Finset.mem_filter] at hI
    refine âŸ¨(prepartition n B).tag I, Set.mem_toFinset.mpr âŸ¨hI.2, ?_âŸ©, box_index_tag_eq_self n hI.1âŸ©
    rw [â† box_index_tag_eq_self n hI.1, prepartition_tag n
      (mem_admissibleIndex_of_mem_box n hB (hsâ‚€ hI.2))]
    exact tag_mem_smul_span _ _
  Â· rw [Set.mem_toFinset] at hx
    rw [measureReal_def, volume_box,
      prepartition_tag n (mem_admissibleIndex_of_mem_box n hB (hsâ‚€ hx.1)),
      tag_index_eq_self_of_mem_smul_span n hx.2, ENNReal.toReal_div,
      ENNReal.toReal_one, ENNReal.toReal_pow, ENNReal.toReal_natCast, mul_comm_div, one_mul]

open Filter

/-- Let `s` be a bounded, measurable set of `Î¹ â†’ â„` whose frontier has zero volume and let `F`
be a continuous function. Then the limit as `n â†’ âˆ` of `âˆ‘ F x / n ^ card Î¹`, where the sum is
over the points in `s âˆ© nâ»Â¹ â€¢ (Î¹ â†’ â„¤)`, tends to the integral of `F` over `s`. -/
theorem _root_.tendsto_tsum_div_pow_atTop_integral (hF : Continuous F) (hsâ‚ : IsBounded s)
    (hsâ‚‚ : MeasurableSet s) (hsâ‚ƒ : volume (frontier s) = 0) :
    Tendsto (fun n : â„• â†¦ (âˆ‘' x : â†‘(s âˆ© (n : â„)â»Â¹ â€¢ L), F x) / n ^ card Î¹)
      atTop (nhds (âˆ« x in s, F x)) := by
  obtain âŸ¨B, hB, hsâ‚€âŸ© := le_hasIntegralVertices_of_isBounded hsâ‚
  refine Metric.tendsto_atTop.mpr fun Îµ hÎµ â†¦ ?_
  have hâ‚ : âˆƒ C, âˆ€ x âˆˆ Box.Icc B, â€–Set.indicator s F xâ€– â‰¤ C := by
    obtain âŸ¨Câ‚€, hâ‚€âŸ© := (Box.isCompact_Icc B).exists_bound_of_continuousOn hF.continuousOn
    refine âŸ¨max 0 Câ‚€, fun x hx â†¦ ?_âŸ©
    rw [Set.indicator]
    split_ifs with hs
    Â· exact le_max_of_le_right (hâ‚€ x hx)
    Â· exact norm_zero.trans_le <| le_max_left 0 _
  have hâ‚‚ : âˆ€áµ x, ContinuousAt (s.indicator F) x := by
    filter_upwards [compl_mem_ae_iff.mpr hsâ‚ƒ] with _ h
      using (hF.continuousOn).continuousAt_indicator h
  obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© := (hasIntegral_iff.mp <|
      AEContinuous.hasBoxIntegral (volume : Measure (Î¹ â†’ â„)) hâ‚ hâ‚‚
        IntegrationParams.Riemann) (Îµ / 2) (half_pos hÎµ)
  refine âŸ¨âŒˆ(r 0 0 : â„)â»Â¹âŒ‰â‚Š, fun n hn â†¦ lt_of_le_of_lt ?_ (half_lt_self_iff.mpr hÎµ)âŸ©
  have : NeZero n :=
    âŸ¨Nat.ne_zero_iff_zero_lt.mpr <| (Nat.ceil_pos.mpr (inv_pos.mpr (r 0 0).prop)).trans_le hnâŸ©
  rw [â† integralSum_eq_tsum_div _ s F hB hsâ‚€, â† Measure.restrict_restrict_of_subset hsâ‚€,
    â† integral_indicator hsâ‚‚]
  refine hrâ‚‚ 0 _ âŸ¨?_, fun _ â†¦ ?_, fun h â†¦ ?_, fun h â†¦ ?_âŸ© (prepartition_isPartition _ hB)
  Â· rw [show r 0 = fun _ â†¦ r 0 0 from funext_iff.mpr (hrâ‚ 0 rfl)]
    apply prepartition_isSubordinate n B
    rw [one_div, inv_le_commâ‚€ (mod_cast (Nat.pos_of_neZero n)) (r 0 0).prop]
    exact le_trans (Nat.le_ceil _) (Nat.cast_le.mpr hn)
  Â· exact prepartition_isHenstock n B
  Â· simp only [IntegrationParams.Riemann, Bool.false_eq_true] at h
  Â· simp only [IntegrationParams.Riemann, Bool.false_eq_true] at h

/-- Let `s` be a bounded, measurable set of `Î¹ â†’ â„` whose frontier has zero volume. Then the limit
as `n â†’ âˆ` of `card (s âˆ© nâ»Â¹ â€¢ (Î¹ â†’ â„¤)) / n ^ card Î¹` tends to the volume of `s`. This is a
special case of `tendsto_card_div_pow` with `F = 1`. -/
theorem _root_.tendsto_card_div_pow_atTop_volume (hsâ‚ : IsBounded s)
    (hsâ‚‚ : MeasurableSet s) (hsâ‚ƒ : volume (frontier s) = 0) :
    Tendsto (fun n : â„• â†¦ (Nat.card â†‘(s âˆ© (n : â„)â»Â¹ â€¢ L) : â„) / n ^ card Î¹)
      atTop (ğ“ (volume.real s)) := by
  convert tendsto_tsum_div_pow_atTop_integral s (fun _ â†¦ 1) continuous_const hsâ‚ hsâ‚‚ hsâ‚ƒ
  Â· rw [tsum_const, nsmul_eq_mul, mul_one, Nat.cast_inj]
  Â· rw [setIntegral_const, smul_eq_mul, mul_one]

private def tendsto_card_div_powâ‚ {c : â„} (hc : c â‰  0) :
    â†‘(s âˆ© câ»Â¹ â€¢ L) â‰ƒ â†‘(c â€¢ s âˆ© L) :=
  Equiv.subtypeEquiv (Equiv.smulRight hc) (fun x â†¦ by
    simp_rw [Set.mem_inter_iff, Equiv.smulRight_apply, Set.smul_mem_smul_set_iffâ‚€ hc,
      â† Set.mem_inv_smul_set_iffâ‚€ hc])

private theorem tendsto_card_div_powâ‚‚ (hsâ‚ : IsBounded s)
    (hsâ‚„ : âˆ€ â¦ƒx y : â„â¦„, 0 < x â†’ x â‰¤ y â†’ x â€¢ s âŠ† y â€¢ s) {x y : â„} (hx : 0 < x) (hy : x â‰¤ y) :
    Nat.card â†‘(s âˆ© xâ»Â¹ â€¢ L) â‰¤ Nat.card â†‘(s âˆ© yâ»Â¹ â€¢ L) := by
  rw [Nat.card_congr (tendsto_card_div_powâ‚ s hx.ne'),
      Nat.card_congr (tendsto_card_div_powâ‚ s (hx.trans_le hy).ne')]
  refine Nat.card_mono ?_ ?_
  Â· exact ZSpan.setFinite_inter _ (IsBounded.smulâ‚€ hsâ‚ y)
  Â· exact Set.inter_subset_inter_left _ <| hsâ‚„ hx hy

private theorem tendsto_card_div_powâ‚ƒ (hsâ‚ : IsBounded s)
    (hsâ‚„ : âˆ€ â¦ƒx y : â„â¦„, 0 < x â†’ x â‰¤ y â†’ x â€¢ s âŠ† y â€¢ s) :
    âˆ€á¶  x : â„ in atTop, (Nat.card â†‘(s âˆ© (âŒŠxâŒ‹â‚Š : â„)â»Â¹ â€¢ L) : â„) / x ^ card Î¹ â‰¤
      (Nat.card â†‘(s âˆ© xâ»Â¹ â€¢ L) : â„) / x ^ card Î¹ := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  gcongr
  exact tendsto_card_div_powâ‚‚ s hsâ‚ hsâ‚„ (Nat.cast_pos.mpr (Nat.floor_pos.mpr hx))
    (Nat.floor_le (zero_le_one.trans hx))

private theorem tendsto_card_div_powâ‚„ (hsâ‚ : IsBounded s)
    (hsâ‚„ : âˆ€ â¦ƒx y : â„â¦„, 0 < x â†’ x â‰¤ y â†’ x â€¢ s âŠ† y â€¢ s) :
    âˆ€á¶  x : â„ in atTop, (Nat.card â†‘(s âˆ© xâ»Â¹ â€¢ L) : â„) / x ^ card Î¹ â‰¤
      (Nat.card â†‘(s âˆ© (âŒˆxâŒ‰â‚Š : â„)â»Â¹ â€¢ L) : â„) / x ^ card Î¹ := by
  filter_upwards [eventually_gt_atTop 0] with x hx
  gcongr
  exact tendsto_card_div_powâ‚‚ s hsâ‚ hsâ‚„ hx (Nat.le_ceil _)

private theorem tendsto_card_div_powâ‚… :
    (fun x â†¦ (Nat.card â†‘(s âˆ© (âŒŠxâŒ‹â‚Š : â„)â»Â¹ â€¢ L) : â„) / âŒŠxâŒ‹â‚Š ^ card Î¹ * (âŒŠxâŒ‹â‚Š / x) ^ card Î¹)
      =á¶ [atTop] (fun x â†¦ (Nat.card â†‘(s âˆ© (âŒŠxâŒ‹â‚Š : â„)â»Â¹ â€¢ L) : â„) / x ^ card Î¹) := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  have : 0 < âŒŠxâŒ‹â‚Š := Nat.floor_pos.mpr hx
  rw [div_pow, mul_div, div_mul_cancelâ‚€ _ (by positivity)]

private theorem tendsto_card_div_powâ‚† :
    (fun x â†¦ (Nat.card â†‘(s âˆ© (âŒˆxâŒ‰â‚Š : â„)â»Â¹ â€¢ L) : â„) / âŒˆxâŒ‰â‚Š ^ card Î¹ * (âŒˆxâŒ‰â‚Š / x) ^ card Î¹)
          =á¶ [atTop] (fun x â†¦ (Nat.card â†‘(s âˆ© (âŒˆxâŒ‰â‚Š : â„)â»Â¹ â€¢ L) : â„) / x ^ card Î¹) := by
  filter_upwards [eventually_ge_atTop 1] with x hx
  rw [div_pow, mul_div, div_mul_cancelâ‚€ _ (by positivity)]

/-- A version of `tendsto_card_div_pow_atTop_volume` for a real variable. -/
theorem _root_.tendsto_card_div_pow_atTop_volume' (hsâ‚ : IsBounded s)
    (hsâ‚‚ : MeasurableSet s) (hsâ‚ƒ : volume (frontier s) = 0)
    (hsâ‚„ : âˆ€ â¦ƒx y : â„â¦„, 0 < x â†’ x â‰¤ y â†’ x â€¢ s âŠ† y â€¢ s) :
    Tendsto (fun x : â„ â†¦ (Nat.card â†‘(s âˆ© xâ»Â¹ â€¢ L) : â„) / x ^ card Î¹)
      atTop (ğ“ (volume.real s)) := by
  rw [show volume.real s = volume.real s * 1 ^ card Î¹ by ring]
  refine tendsto_of_tendsto_of_tendsto_of_le_of_le' ?_ ?_
    (tendsto_card_div_powâ‚ƒ s hsâ‚ hsâ‚„) (tendsto_card_div_powâ‚„ s hsâ‚ hsâ‚„)
  Â· refine Tendsto.congr' (tendsto_card_div_powâ‚… s) (Tendsto.mul ?_ (Tendsto.pow ?_ _))
    Â· exact Tendsto.comp (tendsto_card_div_pow_atTop_volume s hsâ‚ hsâ‚‚ hsâ‚ƒ) tendsto_nat_floor_atTop
    Â· exact tendsto_nat_floor_div_atTop
  Â· refine Tendsto.congr' (tendsto_card_div_powâ‚† s) (Tendsto.mul ?_ (Tendsto.pow ?_ _))
    Â· exact Tendsto.comp (tendsto_card_div_pow_atTop_volume s hsâ‚ hsâ‚‚ hsâ‚ƒ) tendsto_nat_ceil_atTop
    Â· exact tendsto_nat_ceil_div_atTop

end BoxIntegral.unitPartition
