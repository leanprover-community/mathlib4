/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Order.Interval.Set.Monotone
import Mathlib.Topology.MetricSpace.Basic
import Mathlib.Topology.MetricSpace.Bounded
import Mathlib.Topology.Order.MonotoneConvergence
import Mathlib.Topology.MetricSpace.Pseudo.Lemmas

#align_import analysis.box_integral.box.basic from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"
/-!
# Rectangular boxes in `‚Ñù‚Åø`

In this file we define rectangular boxes in `‚Ñù‚Åø`. As usual, we represent `‚Ñù‚Åø` as the type of
functions `Œπ ‚Üí ‚Ñù` (usually `Œπ = Fin n` for some `n`). When we need to interpret a box `[l, u]` as a
set, we use the product `{x | ‚àÄ i, l i < x i ‚àß x i ‚â§ u i}` of half-open intervals `(l i, u i]`. We
exclude `l i` because this way boxes of a partition are disjoint as sets in `‚Ñù‚Åø`.

Currently, the only use cases for these constructions are the definitions of Riemann-style integrals
(Riemann, Henstock-Kurzweil, McShane).

## Main definitions

We use the same structure `BoxIntegral.Box` both for ambient boxes and for elements of a partition.
Each box is stored as two points `lower upper : Œπ ‚Üí ‚Ñù` and a proof of `‚àÄ i, lower i < upper i`. We
define instances `Membership (Œπ ‚Üí ‚Ñù) (Box Œπ)` and `CoeTC (Box Œπ) (Set <| Œπ ‚Üí ‚Ñù)` so that each box is
interpreted as the set `{x | ‚àÄ i, x i ‚àà Set.Ioc (I.lower i) (I.upper i)}`. This way boxes of a
partition are pairwise disjoint and their union is exactly the original box.

We require boxes to be nonempty, because this way coercion to sets is injective. The empty box can
be represented as `‚ä• : WithBot (BoxIntegral.Box Œπ)`.

We define the following operations on boxes:

* coercion to `Set (Œπ ‚Üí ‚Ñù)` and `Membership (Œπ ‚Üí ‚Ñù) (BoxIntegral.Box Œπ)` as described above;
* `PartialOrder` and `SemilatticeSup` instances such that `I ‚â§ J` is equivalent to
  `(I : Set (Œπ ‚Üí ‚Ñù)) ‚äÜ J`;
* `Lattice` instances on `WithBot (BoxIntegral.Box Œπ)`;
* `BoxIntegral.Box.Icc`: the closed box `Set.Icc I.lower I.upper`; defined as a bundled monotone
  map from `Box Œπ` to `Set (Œπ ‚Üí ‚Ñù)`;
* `BoxIntegral.Box.face I i : Box (Fin n)`: a hyperface of `I : BoxIntegral.Box (Fin (n + 1))`;
* `BoxIntegral.Box.distortion`: the maximal ratio of two lengths of edges of a box; defined as the
  supremum of `nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`.

We also provide a convenience constructor `BoxIntegral.Box.mk' (l u : Œπ ‚Üí ‚Ñù) : WithBot (Box Œπ)`
that returns the box `‚ü®l, u, _‚ü©` if it is nonempty and `‚ä•` otherwise.

## Tags

rectangular box
-/


open Set Function Metric Filter

noncomputable section

open scoped Classical
open NNReal Topology

namespace BoxIntegral

variable {Œπ : Type*}

/-!
### Rectangular box: definition and partial order
-/


/-- A nontrivial rectangular box in `Œπ ‚Üí ‚Ñù` with corners `lower` and `upper`. Represents the product
of half-open intervals `(lower i, upper i]`. -/
structure Box (Œπ : Type*) where
  /-- coordinates of the lower and upper corners of the box -/
  (lower upper : Œπ ‚Üí ‚Ñù)
  /-- Each lower coordinate is less than its upper coordinate: i.e., the box is non-empty -/
  lower_lt_upper : ‚àÄ i, lower i < upper i
#align box_integral.box BoxIntegral.Box

attribute [simp] Box.lower_lt_upper

namespace Box

variable (I J : Box Œπ) {x y : Œπ ‚Üí ‚Ñù}

instance : Inhabited (Box Œπ) :=
  ‚ü®‚ü®0, 1, fun _ ‚Ü¶ zero_lt_one‚ü©‚ü©

theorem lower_le_upper : I.lower ‚â§ I.upper :=
  fun i ‚Ü¶ (I.lower_lt_upper i).le
#align box_integral.box.lower_le_upper BoxIntegral.Box.lower_le_upper

theorem lower_ne_upper (i) : I.lower i ‚â† I.upper i :=
  (I.lower_lt_upper i).ne
#align box_integral.box.lower_ne_upper BoxIntegral.Box.lower_ne_upper

instance : Membership (Œπ ‚Üí ‚Ñù) (Box Œπ) :=
  ‚ü®fun x I ‚Ü¶ ‚àÄ i, x i ‚àà Ioc (I.lower i) (I.upper i)‚ü©

-- Porting note: added
/-- The set of points in this box: this is the product of half-open intervals `(lower i, upper i]`,
where `lower` and `upper` are this box' corners. -/
@[coe]
def toSet (I : Box Œπ) : Set (Œπ ‚Üí ‚Ñù) := { x | x ‚àà I }

instance : CoeTC (Box Œπ) (Set <| Œπ ‚Üí ‚Ñù) :=
  ‚ü®toSet‚ü©

@[simp]
theorem mem_mk {l u x : Œπ ‚Üí ‚Ñù} {H} : x ‚àà mk l u H ‚Üî ‚àÄ i, x i ‚àà Ioc (l i) (u i) := Iff.rfl
#align box_integral.box.mem_mk BoxIntegral.Box.mem_mk

@[simp, norm_cast]
theorem mem_coe : x ‚àà (I : Set (Œπ ‚Üí ‚Ñù)) ‚Üî x ‚àà I := Iff.rfl
#align box_integral.box.mem_coe BoxIntegral.Box.mem_coe

theorem mem_def : x ‚àà I ‚Üî ‚àÄ i, x i ‚àà Ioc (I.lower i) (I.upper i) := Iff.rfl
#align box_integral.box.mem_def BoxIntegral.Box.mem_def

theorem mem_univ_Ioc {I : Box Œπ} : (x ‚àà pi univ fun i ‚Ü¶ Ioc (I.lower i) (I.upper i)) ‚Üî x ‚àà I :=
  mem_univ_pi
#align box_integral.box.mem_univ_Ioc BoxIntegral.Box.mem_univ_Ioc

theorem coe_eq_pi : (I : Set (Œπ ‚Üí ‚Ñù)) = pi univ fun i ‚Ü¶ Ioc (I.lower i) (I.upper i) :=
  Set.ext fun _ ‚Ü¶ mem_univ_Ioc.symm
#align box_integral.box.coe_eq_pi BoxIntegral.Box.coe_eq_pi

@[simp]
theorem upper_mem : I.upper ‚àà I :=
  fun i ‚Ü¶ right_mem_Ioc.2 <| I.lower_lt_upper i
#align box_integral.box.upper_mem BoxIntegral.Box.upper_mem

theorem exists_mem : ‚àÉ x, x ‚àà I :=
  ‚ü®_, I.upper_mem‚ü©
#align box_integral.box.exists_mem BoxIntegral.Box.exists_mem

theorem nonempty_coe : Set.Nonempty (I : Set (Œπ ‚Üí ‚Ñù)) :=
  I.exists_mem
#align box_integral.box.nonempty_coe BoxIntegral.Box.nonempty_coe

@[simp]
theorem coe_ne_empty : (I : Set (Œπ ‚Üí ‚Ñù)) ‚â† ‚àÖ :=
  I.nonempty_coe.ne_empty
#align box_integral.box.coe_ne_empty BoxIntegral.Box.coe_ne_empty

@[simp]
theorem empty_ne_coe : ‚àÖ ‚â† (I : Set (Œπ ‚Üí ‚Ñù)) :=
  I.coe_ne_empty.symm
#align box_integral.box.empty_ne_coe BoxIntegral.Box.empty_ne_coe

instance : LE (Box Œπ) :=
  ‚ü®fun I J ‚Ü¶ ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà I ‚Üí x ‚àà J‚ü©

theorem le_def : I ‚â§ J ‚Üî ‚àÄ x ‚àà I, x ‚àà J := Iff.rfl
#align box_integral.box.le_def BoxIntegral.Box.le_def

theorem le_TFAE : List.TFAE [I ‚â§ J, (I : Set (Œπ ‚Üí ‚Ñù)) ‚äÜ J,
    Icc I.lower I.upper ‚äÜ Icc J.lower J.upper, J.lower ‚â§ I.lower ‚àß I.upper ‚â§ J.upper] := by
  tfae_have 1 ‚Üî 2
  ¬∑ exact Iff.rfl
  tfae_have 2 ‚Üí 3
  ¬∑ intro h
    simpa [coe_eq_pi, closure_pi_set, lower_ne_upper] using closure_mono h
  tfae_have 3 ‚Üî 4
  ¬∑ exact Icc_subset_Icc_iff I.lower_le_upper
  tfae_have 4 ‚Üí 2
  ¬∑ exact fun h x hx i ‚Ü¶ Ioc_subset_Ioc (h.1 i) (h.2 i) (hx i)
  tfae_finish
#align box_integral.box.le_tfae BoxIntegral.Box.le_TFAE

variable {I J}

@[simp, norm_cast]
theorem coe_subset_coe : (I : Set (Œπ ‚Üí ‚Ñù)) ‚äÜ J ‚Üî I ‚â§ J := Iff.rfl
#align box_integral.box.coe_subset_coe BoxIntegral.Box.coe_subset_coe

theorem le_iff_bounds : I ‚â§ J ‚Üî J.lower ‚â§ I.lower ‚àß I.upper ‚â§ J.upper :=
  (le_TFAE I J).out 0 3
#align box_integral.box.le_iff_bounds BoxIntegral.Box.le_iff_bounds

theorem injective_coe : Injective ((‚Üë) : Box Œπ ‚Üí Set (Œπ ‚Üí ‚Ñù)) := by
  rintro ‚ü®l‚ÇÅ, u‚ÇÅ, h‚ÇÅ‚ü© ‚ü®l‚ÇÇ, u‚ÇÇ, h‚ÇÇ‚ü© h
  simp only [Subset.antisymm_iff, coe_subset_coe, le_iff_bounds] at h
  congr
  exacts [le_antisymm h.2.1 h.1.1, le_antisymm h.1.2 h.2.2]
#align box_integral.box.injective_coe BoxIntegral.Box.injective_coe

@[simp, norm_cast]
theorem coe_inj : (I : Set (Œπ ‚Üí ‚Ñù)) = J ‚Üî I = J :=
  injective_coe.eq_iff
#align box_integral.box.coe_inj BoxIntegral.Box.coe_inj

@[ext]
theorem ext (H : ‚àÄ x, x ‚àà I ‚Üî x ‚àà J) : I = J :=
  injective_coe <| Set.ext H
#align box_integral.box.ext BoxIntegral.Box.ext

theorem ne_of_disjoint_coe (h : Disjoint (I : Set (Œπ ‚Üí ‚Ñù)) J) : I ‚â† J :=
  mt coe_inj.2 <| h.ne I.coe_ne_empty
#align box_integral.box.ne_of_disjoint_coe BoxIntegral.Box.ne_of_disjoint_coe

instance : PartialOrder (Box Œπ) :=
  { PartialOrder.lift ((‚Üë) : Box Œπ ‚Üí Set (Œπ ‚Üí ‚Ñù)) injective_coe with le := (¬∑ ‚â§ ¬∑) }

/-- Closed box corresponding to `I : BoxIntegral.Box Œπ`. -/
protected def Icc : Box Œπ ‚Ü™o Set (Œπ ‚Üí ‚Ñù) :=
  OrderEmbedding.ofMapLEIff (fun I : Box Œπ ‚Ü¶ Icc I.lower I.upper) fun I J ‚Ü¶ (le_TFAE I J).out 2 0
#align box_integral.box.Icc BoxIntegral.Box.Icc

theorem Icc_def : Box.Icc I = Icc I.lower I.upper := rfl
#align box_integral.box.Icc_def BoxIntegral.Box.Icc_def

@[simp]
theorem upper_mem_Icc (I : Box Œπ) : I.upper ‚àà Box.Icc I :=
  right_mem_Icc.2 I.lower_le_upper
#align box_integral.box.upper_mem_Icc BoxIntegral.Box.upper_mem_Icc

@[simp]
theorem lower_mem_Icc (I : Box Œπ) : I.lower ‚àà Box.Icc I :=
  left_mem_Icc.2 I.lower_le_upper
#align box_integral.box.lower_mem_Icc BoxIntegral.Box.lower_mem_Icc

protected theorem isCompact_Icc (I : Box Œπ) : IsCompact (Box.Icc I) :=
  isCompact_Icc
#align box_integral.box.is_compact_Icc BoxIntegral.Box.isCompact_Icc

theorem Icc_eq_pi : Box.Icc I = pi univ fun i ‚Ü¶ Icc (I.lower i) (I.upper i) :=
  (pi_univ_Icc _ _).symm
#align box_integral.box.Icc_eq_pi BoxIntegral.Box.Icc_eq_pi

theorem le_iff_Icc : I ‚â§ J ‚Üî Box.Icc I ‚äÜ Box.Icc J :=
  (le_TFAE I J).out 0 2
#align box_integral.box.le_iff_Icc BoxIntegral.Box.le_iff_Icc

theorem antitone_lower : Antitone fun I : Box Œπ ‚Ü¶ I.lower :=
  fun _ _ H ‚Ü¶ (le_iff_bounds.1 H).1
#align box_integral.box.antitone_lower BoxIntegral.Box.antitone_lower

theorem monotone_upper : Monotone fun I : Box Œπ ‚Ü¶ I.upper :=
  fun _ _ H ‚Ü¶ (le_iff_bounds.1 H).2
#align box_integral.box.monotone_upper BoxIntegral.Box.monotone_upper

theorem coe_subset_Icc : ‚ÜëI ‚äÜ Box.Icc I :=
  fun _ hx ‚Ü¶ ‚ü®fun i ‚Ü¶ (hx i).1.le, fun i ‚Ü¶ (hx i).2‚ü©
#align box_integral.box.coe_subset_Icc BoxIntegral.Box.coe_subset_Icc

/-!
### Supremum of two boxes
-/


/-- `I ‚äî J` is the least box that includes both `I` and `J`. Since `‚ÜëI ‚à™ ‚ÜëJ` is usually not a box,
`‚Üë(I ‚äî J)` is larger than `‚ÜëI ‚à™ ‚ÜëJ`. -/
instance : Sup (Box Œπ) :=
  ‚ü®fun I J ‚Ü¶ ‚ü®I.lower ‚äì J.lower, I.upper ‚äî J.upper,
    fun i ‚Ü¶ (min_le_left _ _).trans_lt <| (I.lower_lt_upper i).trans_le (le_max_left _ _)‚ü©‚ü©

instance : SemilatticeSup (Box Œπ) :=
  { (inferInstance : PartialOrder (Box Œπ)),
    (inferInstance : Sup (Box Œπ)) with
    le_sup_left := fun _ _ ‚Ü¶ le_iff_bounds.2 ‚ü®inf_le_left, le_sup_left‚ü©
    le_sup_right := fun _ _ ‚Ü¶ le_iff_bounds.2 ‚ü®inf_le_right, le_sup_right‚ü©
    sup_le := fun _ _ _ h‚ÇÅ h‚ÇÇ ‚Ü¶ le_iff_bounds.2
      ‚ü®le_inf (antitone_lower h‚ÇÅ) (antitone_lower h‚ÇÇ),
        sup_le (monotone_upper h‚ÇÅ) (monotone_upper h‚ÇÇ)‚ü© }

/-!
### `WithBot (Box Œπ)`

In this section we define coercion from `WithBot (Box Œπ)` to `Set (Œπ ‚Üí ‚Ñù)` by sending `‚ä•` to `‚àÖ`.
-/

-- Porting note: added
/-- The set underlying this box: `‚ä•` is mapped to `‚àÖ`. -/
@[coe]
def withBotToSet (o : WithBot (Box Œπ)) : Set (Œπ ‚Üí ‚Ñù) := o.elim ‚àÖ (‚Üë)

instance withBotCoe : CoeTC (WithBot (Box Œπ)) (Set (Œπ ‚Üí ‚Ñù)) :=
  ‚ü®withBotToSet‚ü©
#align box_integral.box.with_bot_coe BoxIntegral.Box.withBotCoe

@[simp, norm_cast]
theorem coe_bot : ((‚ä• : WithBot (Box Œπ)) : Set (Œπ ‚Üí ‚Ñù)) = ‚àÖ := rfl
#align box_integral.box.coe_bot BoxIntegral.Box.coe_bot

@[simp, norm_cast]
theorem coe_coe : ((I : WithBot (Box Œπ)) : Set (Œπ ‚Üí ‚Ñù)) = I := rfl
#align box_integral.box.coe_coe BoxIntegral.Box.coe_coe

theorem isSome_iff : ‚àÄ {I : WithBot (Box Œπ)}, I.isSome ‚Üî (I : Set (Œπ ‚Üí ‚Ñù)).Nonempty
  | ‚ä• => by
    erw [Option.isSome]
    simp
  | (I : Box Œπ) => by
    erw [Option.isSome]
    simp [I.nonempty_coe]
#align box_integral.box.is_some_iff BoxIntegral.Box.isSome_iff

theorem biUnion_coe_eq_coe (I : WithBot (Box Œπ)) :
    ‚ãÉ (J : Box Œπ) (_ : ‚ÜëJ = I), (J : Set (Œπ ‚Üí ‚Ñù)) = I := by
  induction I <;> simp [WithBot.coe_eq_coe]
#align box_integral.box.bUnion_coe_eq_coe BoxIntegral.Box.biUnion_coe_eq_coe

@[simp, norm_cast]
theorem withBotCoe_subset_iff {I J : WithBot (Box Œπ)} : (I : Set (Œπ ‚Üí ‚Ñù)) ‚äÜ J ‚Üî I ‚â§ J := by
  induction I; ¬∑ simp
  induction J; ¬∑ simp [subset_empty_iff]
  simp [le_def]
#align box_integral.box.with_bot_coe_subset_iff BoxIntegral.Box.withBotCoe_subset_iff

@[simp, norm_cast]
theorem withBotCoe_inj {I J : WithBot (Box Œπ)} : (I : Set (Œπ ‚Üí ‚Ñù)) = J ‚Üî I = J := by
  simp only [Subset.antisymm_iff, ‚Üê le_antisymm_iff, withBotCoe_subset_iff]
#align box_integral.box.with_bot_coe_inj BoxIntegral.Box.withBotCoe_inj

/-- Make a `WithBot (Box Œπ)` from a pair of corners `l u : Œπ ‚Üí ‚Ñù`. If `l i < u i` for all `i`,
then the result is `‚ü®l, u, _‚ü© : Box Œπ`, otherwise it is `‚ä•`. In any case, the result interpreted
as a set in `Œπ ‚Üí ‚Ñù` is the set `{x : Œπ ‚Üí ‚Ñù | ‚àÄ i, x i ‚àà Ioc (l i) (u i)}`.  -/
def mk' (l u : Œπ ‚Üí ‚Ñù) : WithBot (Box Œπ) :=
  if h : ‚àÄ i, l i < u i then ‚Üë(‚ü®l, u, h‚ü© : Box Œπ) else ‚ä•
#align box_integral.box.mk' BoxIntegral.Box.mk'

@[simp]
theorem mk'_eq_bot {l u : Œπ ‚Üí ‚Ñù} : mk' l u = ‚ä• ‚Üî ‚àÉ i, u i ‚â§ l i := by
  rw [mk']
  split_ifs with h <;> simpa using h
#align box_integral.box.mk'_eq_bot BoxIntegral.Box.mk'_eq_bot

@[simp]
theorem mk'_eq_coe {l u : Œπ ‚Üí ‚Ñù} : mk' l u = I ‚Üî l = I.lower ‚àß u = I.upper := by
  cases' I with lI uI hI; rw [mk']; split_ifs with h
  ¬∑ simp [WithBot.coe_eq_coe]
  ¬∑ suffices l = lI ‚Üí u ‚â† uI by simpa
    rintro rfl rfl
    exact h hI
#align box_integral.box.mk'_eq_coe BoxIntegral.Box.mk'_eq_coe

@[simp]
theorem coe_mk' (l u : Œπ ‚Üí ‚Ñù) : (mk' l u : Set (Œπ ‚Üí ‚Ñù)) = pi univ fun i ‚Ü¶ Ioc (l i) (u i) := by
  rw [mk']; split_ifs with h
  ¬∑ exact coe_eq_pi _
  ¬∑ rcases not_forall.mp h with ‚ü®i, hi‚ü©
    rw [coe_bot, univ_pi_eq_empty]
    exact Ioc_eq_empty hi
#align box_integral.box.coe_mk' BoxIntegral.Box.coe_mk'

instance WithBot.inf : Inf (WithBot (Box Œπ)) :=
  ‚ü®fun I ‚Ü¶
    WithBot.recBotCoe (fun _ ‚Ü¶ ‚ä•)
      (fun I J ‚Ü¶ WithBot.recBotCoe ‚ä• (fun J ‚Ü¶ mk' (I.lower ‚äî J.lower) (I.upper ‚äì J.upper)) J) I‚ü©

@[simp]
theorem coe_inf (I J : WithBot (Box Œπ)) : (‚Üë(I ‚äì J) : Set (Œπ ‚Üí ‚Ñù)) = (I : Set _) ‚à© J := by
  induction I
  ¬∑ change ‚àÖ = _
    simp
  induction J
  ¬∑ change ‚àÖ = _
    simp
  change ((mk' _ _ : WithBot (Box Œπ)) : Set (Œπ ‚Üí ‚Ñù)) = _
  simp only [coe_eq_pi, ‚Üê pi_inter_distrib, Ioc_inter_Ioc, Pi.sup_apply, Pi.inf_apply, coe_mk',
    coe_coe]
#align box_integral.box.coe_inf BoxIntegral.Box.coe_inf

instance : Lattice (WithBot (Box Œπ)) :=
  { WithBot.semilatticeSup,
    Box.WithBot.inf with
    inf_le_left := fun I J ‚Ü¶ by
      rw [‚Üê withBotCoe_subset_iff, coe_inf]
      exact inter_subset_left
    inf_le_right := fun I J ‚Ü¶ by
      rw [‚Üê withBotCoe_subset_iff, coe_inf]
      exact inter_subset_right
    le_inf := fun I J‚ÇÅ J‚ÇÇ h‚ÇÅ h‚ÇÇ ‚Ü¶ by
      simp only [‚Üê withBotCoe_subset_iff, coe_inf] at *
      exact subset_inter h‚ÇÅ h‚ÇÇ }

@[simp, norm_cast]
theorem disjoint_withBotCoe {I J : WithBot (Box Œπ)} :
    Disjoint (I : Set (Œπ ‚Üí ‚Ñù)) J ‚Üî Disjoint I J := by
  simp only [disjoint_iff_inf_le, ‚Üê withBotCoe_subset_iff, coe_inf]
  rfl
#align box_integral.box.disjoint_with_bot_coe BoxIntegral.Box.disjoint_withBotCoe

theorem disjoint_coe : Disjoint (I : WithBot (Box Œπ)) J ‚Üî Disjoint (I : Set (Œπ ‚Üí ‚Ñù)) J :=
  disjoint_withBotCoe.symm
#align box_integral.box.disjoint_coe BoxIntegral.Box.disjoint_coe

theorem not_disjoint_coe_iff_nonempty_inter :
    ¬¨Disjoint (I : WithBot (Box Œπ)) J ‚Üî (I ‚à© J : Set (Œπ ‚Üí ‚Ñù)).Nonempty := by
  rw [disjoint_coe, Set.not_disjoint_iff_nonempty_inter]
#align box_integral.box.not_disjoint_coe_iff_nonempty_inter BoxIntegral.Box.not_disjoint_coe_iff_nonempty_inter

/-!
### Hyperface of a box in `‚Ñù‚Åø‚Å∫¬π = Fin (n + 1) ‚Üí ‚Ñù`
-/


/-- Face of a box in `‚Ñù‚Åø‚Å∫¬π = Fin (n + 1) ‚Üí ‚Ñù`: the box in `‚Ñù‚Åø = Fin n ‚Üí ‚Ñù` with corners at
`I.lower ‚àò Fin.succAbove i` and `I.upper ‚àò Fin.succAbove i`. -/
@[simps (config := { simpRhs := true })]
def face {n} (I : Box (Fin (n + 1))) (i : Fin (n + 1)) : Box (Fin n) :=
  ‚ü®I.lower ‚àò Fin.succAbove i, I.upper ‚àò Fin.succAbove i, fun _ ‚Ü¶ I.lower_lt_upper _‚ü©
#align box_integral.box.face BoxIntegral.Box.face

@[simp]
theorem face_mk {n} (l u : Fin (n + 1) ‚Üí ‚Ñù) (h : ‚àÄ i, l i < u i) (i : Fin (n + 1)) :
    face ‚ü®l, u, h‚ü© i = ‚ü®l ‚àò Fin.succAbove i, u ‚àò Fin.succAbove i, fun _ ‚Ü¶ h _‚ü© := rfl
#align box_integral.box.face_mk BoxIntegral.Box.face_mk

@[mono]
theorem face_mono {n} {I J : Box (Fin (n + 1))} (h : I ‚â§ J) (i : Fin (n + 1)) :
    face I i ‚â§ face J i :=
  fun _ hx _ ‚Ü¶ Ioc_subset_Ioc ((le_iff_bounds.1 h).1 _) ((le_iff_bounds.1 h).2 _) (hx _)
#align box_integral.box.face_mono BoxIntegral.Box.face_mono

theorem monotone_face {n} (i : Fin (n + 1)) : Monotone fun I ‚Ü¶ face I i :=
  fun _ _ h ‚Ü¶ face_mono h i
#align box_integral.box.monotone_face BoxIntegral.Box.monotone_face

theorem mapsTo_insertNth_face_Icc {n} (I : Box (Fin (n + 1))) {i : Fin (n + 1)} {x : ‚Ñù}
    (hx : x ‚àà Icc (I.lower i) (I.upper i)) :
    MapsTo (i.insertNth x) (Box.Icc (I.face i)) (Box.Icc I) :=
  fun _ hy ‚Ü¶ Fin.insertNth_mem_Icc.2 ‚ü®hx, hy‚ü©
#align box_integral.box.maps_to_insert_nth_face_Icc BoxIntegral.Box.mapsTo_insertNth_face_Icc

theorem mapsTo_insertNth_face {n} (I : Box (Fin (n + 1))) {i : Fin (n + 1)} {x : ‚Ñù}
    (hx : x ‚àà Ioc (I.lower i) (I.upper i)) :
    MapsTo (i.insertNth x) (I.face i : Set (_ ‚Üí _)) (I : Set (_ ‚Üí _)) := by
  intro y hy
  simp_rw [mem_coe, mem_def, i.forall_iff_succAbove, Fin.insertNth_apply_same,
    Fin.insertNth_apply_succAbove]
  exact ‚ü®hx, hy‚ü©
#align box_integral.box.maps_to_insert_nth_face BoxIntegral.Box.mapsTo_insertNth_face

theorem continuousOn_face_Icc {X} [TopologicalSpace X] {n} {f : (Fin (n + 1) ‚Üí ‚Ñù) ‚Üí X}
    {I : Box (Fin (n + 1))} (h : ContinuousOn f (Box.Icc I)) {i : Fin (n + 1)} {x : ‚Ñù}
    (hx : x ‚àà Icc (I.lower i) (I.upper i)) :
    ContinuousOn (f ‚àò i.insertNth x) (Box.Icc (I.face i)) :=
  h.comp (continuousOn_const.fin_insertNth i continuousOn_id) (I.mapsTo_insertNth_face_Icc hx)
#align box_integral.box.continuous_on_face_Icc BoxIntegral.Box.continuousOn_face_Icc

/-!
### Covering of the interior of a box by a monotone sequence of smaller boxes
-/


/-- The interior of a box. -/
protected def Ioo : Box Œπ ‚Üío Set (Œπ ‚Üí ‚Ñù) where
  toFun I := pi univ fun i ‚Ü¶ Ioo (I.lower i) (I.upper i)
  monotone' _ _ h :=
    pi_mono fun i _ ‚Ü¶ Ioo_subset_Ioo ((le_iff_bounds.1 h).1 i) ((le_iff_bounds.1 h).2 i)
#align box_integral.box.Ioo BoxIntegral.Box.Ioo

theorem Ioo_subset_coe (I : Box Œπ) : Box.Ioo I ‚äÜ I :=
  fun _ hx i ‚Ü¶ Ioo_subset_Ioc_self (hx i trivial)
#align box_integral.box.Ioo_subset_coe BoxIntegral.Box.Ioo_subset_coe

protected theorem Ioo_subset_Icc (I : Box Œπ) : Box.Ioo I ‚äÜ Box.Icc I :=
  I.Ioo_subset_coe.trans coe_subset_Icc
#align box_integral.box.Ioo_subset_Icc BoxIntegral.Box.Ioo_subset_Icc

theorem iUnion_Ioo_of_tendsto [Finite Œπ] {I : Box Œπ} {J : ‚Ñï ‚Üí Box Œπ} (hJ : Monotone J)
    (hl : Tendsto (lower ‚àò J) atTop (ùìù I.lower)) (hu : Tendsto (upper ‚àò J) atTop (ùìù I.upper)) :
    ‚ãÉ n, Box.Ioo (J n) = Box.Ioo I :=
  have hl' : ‚àÄ i, Antitone fun n ‚Ü¶ (J n).lower i :=
    fun i ‚Ü¶ (monotone_eval i).comp_antitone (antitone_lower.comp_monotone hJ)
  have hu' : ‚àÄ i, Monotone fun n ‚Ü¶ (J n).upper i :=
    fun i ‚Ü¶ (monotone_eval i).comp (monotone_upper.comp hJ)
  calc
    ‚ãÉ n, Box.Ioo (J n) = pi univ fun i ‚Ü¶ ‚ãÉ n, Ioo ((J n).lower i) ((J n).upper i) :=
      iUnion_univ_pi_of_monotone fun i ‚Ü¶ (hl' i).Ioo (hu' i)
    _ = Box.Ioo I :=
      pi_congr rfl fun i _ ‚Ü¶
        iUnion_Ioo_of_mono_of_isGLB_of_isLUB (hl' i) (hu' i)
          (isGLB_of_tendsto_atTop (hl' i) (tendsto_pi_nhds.1 hl _))
          (isLUB_of_tendsto_atTop (hu' i) (tendsto_pi_nhds.1 hu _))
#align box_integral.box.Union_Ioo_of_tendsto BoxIntegral.Box.iUnion_Ioo_of_tendsto

theorem exists_seq_mono_tendsto (I : Box Œπ) :
    ‚àÉ J : ‚Ñï ‚Üío Box Œπ,
      (‚àÄ n, Box.Icc (J n) ‚äÜ Box.Ioo I) ‚àß
        Tendsto (lower ‚àò J) atTop (ùìù I.lower) ‚àß Tendsto (upper ‚àò J) atTop (ùìù I.upper) := by
  choose a b ha_anti hb_mono ha_mem hb_mem hab ha_tendsto hb_tendsto using
    fun i ‚Ü¶ exists_seq_strictAnti_strictMono_tendsto (I.lower_lt_upper i)
  exact
    ‚ü®‚ü®fun k ‚Ü¶ ‚ü®flip a k, flip b k, fun i ‚Ü¶ hab _ _ _‚ü©, fun k l hkl ‚Ü¶
        le_iff_bounds.2 ‚ü®fun i ‚Ü¶ (ha_anti i).antitone hkl, fun i ‚Ü¶ (hb_mono i).monotone hkl‚ü©‚ü©,
      fun n x hx i _ ‚Ü¶ ‚ü®(ha_mem _ _).1.trans_le (hx.1 _), (hx.2 _).trans_lt (hb_mem _ _).2‚ü©,
      tendsto_pi_nhds.2 ha_tendsto, tendsto_pi_nhds.2 hb_tendsto‚ü©
#align box_integral.box.exists_seq_mono_tendsto BoxIntegral.Box.exists_seq_mono_tendsto

section Distortion

variable [Fintype Œπ]

/-- The distortion of a box `I` is the maximum of the ratios of the lengths of its edges.
It is defined as the maximum of the ratios
`nndist I.lower I.upper / nndist (I.lower i) (I.upper i)`. -/
def distortion (I : Box Œπ) : ‚Ñù‚â•0 :=
  Finset.univ.sup fun i : Œπ ‚Ü¶ nndist I.lower I.upper / nndist (I.lower i) (I.upper i)
#align box_integral.box.distortion BoxIntegral.Box.distortion

theorem distortion_eq_of_sub_eq_div {I J : Box Œπ} {r : ‚Ñù}
    (h : ‚àÄ i, I.upper i - I.lower i = (J.upper i - J.lower i) / r) :
    distortion I = distortion J := by
  simp only [distortion, nndist_pi_def, Real.nndist_eq', h, map_div‚ÇÄ]
  congr 1 with i
  have : 0 < r := by
    by_contra hr
    have := div_nonpos_of_nonneg_of_nonpos (sub_nonneg.2 <| J.lower_le_upper i) (not_lt.1 hr)
    rw [‚Üê h] at this
    exact this.not_lt (sub_pos.2 <| I.lower_lt_upper i)
  have hn0 := (map_ne_zero Real.nnabs).2 this.ne'
  simp_rw [NNReal.finset_sup_div, div_div_div_cancel_right _ hn0]
#align box_integral.box.distortion_eq_of_sub_eq_div BoxIntegral.Box.distortion_eq_of_sub_eq_div

theorem nndist_le_distortion_mul (I : Box Œπ) (i : Œπ) :
    nndist I.lower I.upper ‚â§ I.distortion * nndist (I.lower i) (I.upper i) :=
  calc
    nndist I.lower I.upper =
        nndist I.lower I.upper / nndist (I.lower i) (I.upper i) * nndist (I.lower i) (I.upper i) :=
      (div_mul_cancel‚ÇÄ _ <| mt nndist_eq_zero.1 (I.lower_lt_upper i).ne).symm
    _ ‚â§ I.distortion * nndist (I.lower i) (I.upper i) := by
      apply mul_le_mul_right'
      apply Finset.le_sup (Finset.mem_univ i)
#align box_integral.box.nndist_le_distortion_mul BoxIntegral.Box.nndist_le_distortion_mul

theorem dist_le_distortion_mul (I : Box Œπ) (i : Œπ) :
    dist I.lower I.upper ‚â§ I.distortion * (I.upper i - I.lower i) := by
  have A : I.lower i - I.upper i < 0 := sub_neg.2 (I.lower_lt_upper i)
  simpa only [‚Üê NNReal.coe_le_coe, ‚Üê dist_nndist, NNReal.coe_mul, Real.dist_eq, abs_of_neg A,
    neg_sub] using I.nndist_le_distortion_mul i
#align box_integral.box.dist_le_distortion_mul BoxIntegral.Box.dist_le_distortion_mul

theorem diam_Icc_le_of_distortion_le (I : Box Œπ) (i : Œπ) {c : ‚Ñù‚â•0} (h : I.distortion ‚â§ c) :
    diam (Box.Icc I) ‚â§ c * (I.upper i - I.lower i) :=
  have : (0 : ‚Ñù) ‚â§ c * (I.upper i - I.lower i) :=
    mul_nonneg c.coe_nonneg (sub_nonneg.2 <| I.lower_le_upper _)
  diam_le_of_forall_dist_le this fun x hx y hy ‚Ü¶
    calc
      dist x y ‚â§ dist I.lower I.upper := Real.dist_le_of_mem_pi_Icc hx hy
      _ ‚â§ I.distortion * (I.upper i - I.lower i) := I.dist_le_distortion_mul i
      _ ‚â§ c * (I.upper i - I.lower i) := by gcongr; exact sub_nonneg.2 (I.lower_le_upper i)
#align box_integral.box.diam_Icc_le_of_distortion_le BoxIntegral.Box.diam_Icc_le_of_distortion_le

end Distortion

end Box

end BoxIntegral
