/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Box.Basic
import Mathlib.Analysis.SpecificLimits.Basic

/-!
# Induction on subboxes

In this file we prove the following induction principle for `BoxIntegral.Box`, see
`BoxIntegral.Box.subbox_induction_on`. Let `p` be a predicate on `BoxIntegral.Box Œπ`, let `I` be a
box. Suppose that the following two properties hold true.

* Consider a smaller box `J ‚â§ I`. The hyperplanes passing through the center of `J` split it into
  `2 ^ n` boxes. If `p` holds true on each of these boxes, then it is true on `J`.
* For each `z` in the closed box `I.Icc` there exists a neighborhood `U` of `z` within `I.Icc` such
  that for every box `J ‚â§ I` such that `z ‚àà J.Icc ‚äÜ U`, if `J` is homothetic to `I` with a
  coefficient of the form `1 / 2 ^ m`, then `p` is true on `J`.

Then `p I` is true.

## Tags

rectangular box, induction
-/

open Set Function Filter Topology

noncomputable section

namespace BoxIntegral

namespace Box

variable {Œπ : Type*} {I J : Box Œπ}

open Classical in
/-- For a box `I`, the hyperplanes passing through its center split `I` into `2 ^ card Œπ` boxes.
`BoxIntegral.Box.splitCenterBox I s` is one of these boxes. See also
`BoxIntegral.Partition.splitCenter` for the corresponding `BoxIntegral.Partition`. -/
def splitCenterBox (I : Box Œπ) (s : Set Œπ) : Box Œπ where
  lower := s.piecewise (fun i ‚Ü¶ (I.lower i + I.upper i) / 2) I.lower
  upper := s.piecewise I.upper fun i ‚Ü¶ (I.lower i + I.upper i) / 2
  lower_lt_upper i := by
    dsimp only [Set.piecewise]
    split_ifs <;> simp only [left_lt_add_div_two, add_div_two_lt_right, I.lower_lt_upper]

theorem mem_splitCenterBox {s : Set Œπ} {y : Œπ ‚Üí ‚Ñù} :
    y ‚àà I.splitCenterBox s ‚Üî y ‚àà I ‚àß ‚àÄ i, (I.lower i + I.upper i) / 2 < y i ‚Üî i ‚àà s := by
  simp only [splitCenterBox, mem_def, ‚Üê forall_and]
  refine forall_congr' fun i ‚Ü¶ ?_
  dsimp only [Set.piecewise]
  split_ifs with hs <;> simp only [hs, iff_true, iff_false, not_lt]
  exacts [‚ü®fun H ‚Ü¶ ‚ü®‚ü®(left_lt_add_div_two.2 (I.lower_lt_upper i)).trans H.1, H.2‚ü©, H.1‚ü©,
      fun H ‚Ü¶ ‚ü®H.2, H.1.2‚ü©‚ü©,
    ‚ü®fun H ‚Ü¶ ‚ü®‚ü®H.1, H.2.trans (add_div_two_lt_right.2 (I.lower_lt_upper i)).le‚ü©, H.2‚ü©,
      fun H ‚Ü¶ ‚ü®H.1.1, H.2‚ü©‚ü©]

theorem splitCenterBox_le (I : Box Œπ) (s : Set Œπ) : I.splitCenterBox s ‚â§ I :=
  fun _ hx ‚Ü¶ (mem_splitCenterBox.1 hx).1

theorem disjoint_splitCenterBox (I : Box Œπ) {s t : Set Œπ} (h : s ‚â† t) :
    Disjoint (I.splitCenterBox s : Set (Œπ ‚Üí ‚Ñù)) (I.splitCenterBox t) := by
  rw [disjoint_iff_inf_le]
  rintro y ‚ü®hs, ht‚ü©; apply h
  ext i
  rw [mem_coe, mem_splitCenterBox] at hs ht
  rw [‚Üê hs.2, ‚Üê ht.2]

theorem injective_splitCenterBox (I : Box Œπ) : Injective I.splitCenterBox := fun _ _ H ‚Ü¶
  by_contra fun Hne ‚Ü¶ (I.disjoint_splitCenterBox Hne).ne (nonempty_coe _).ne_empty (H ‚ñ∏ rfl)

@[simp]
theorem exists_mem_splitCenterBox {I : Box Œπ} {x : Œπ ‚Üí ‚Ñù} : (‚àÉ s, x ‚àà I.splitCenterBox s) ‚Üî x ‚àà I :=
  ‚ü®fun ‚ü®s, hs‚ü© ‚Ü¶ I.splitCenterBox_le s hs, fun hx ‚Ü¶
    ‚ü®{ i | (I.lower i + I.upper i) / 2 < x i }, mem_splitCenterBox.2 ‚ü®hx, fun _ ‚Ü¶ Iff.rfl‚ü©‚ü©‚ü©

/-- `BoxIntegral.Box.splitCenterBox` bundled as a `Function.Embedding`. -/
@[simps]
def splitCenterBoxEmb (I : Box Œπ) : Set Œπ ‚Ü™ Box Œπ :=
  ‚ü®splitCenterBox I, injective_splitCenterBox I‚ü©

@[simp]
theorem iUnion_coe_splitCenterBox (I : Box Œπ) : ‚ãÉ s, (I.splitCenterBox s : Set (Œπ ‚Üí ‚Ñù)) = I := by
  ext x
  simp

@[simp]
theorem upper_sub_lower_splitCenterBox (I : Box Œπ) (s : Set Œπ) (i : Œπ) :
    (I.splitCenterBox s).upper i - (I.splitCenterBox s).lower i = (I.upper i - I.lower i) / 2 := by
  by_cases i ‚àà s <;> simp (disch := assumption) [field, splitCenterBox] <;> ring

/-- Let `p` be a predicate on `Box Œπ`, let `I` be a box. Suppose that the following two properties
hold true.

* `H_ind` : Consider a smaller box `J ‚â§ I`. The hyperplanes passing through the center of `J` split
  it into `2 ^ n` boxes. If `p` holds true on each of these boxes, then it true on `J`.

* `H_nhds` : For each `z` in the closed box `I.Icc` there exists a neighborhood `U` of `z` within
  `I.Icc` such that for every box `J ‚â§ I` such that `z ‚àà J.Icc ‚äÜ U`, if `J` is homothetic to `I`
  with a coefficient of the form `1 / 2 ^ m`, then `p` is true on `J`.

Then `p I` is true. See also `BoxIntegral.Box.subbox_induction_on` for a version using
`BoxIntegral.Prepartition.splitCenter` instead of `BoxIntegral.Box.splitCenterBox`.

The proof still works if we assume `H_ind` only for subboxes `J ‚â§ I` that are homothetic to `I` with
a coefficient of the form `2‚Åª·µê` but we do not need this generalization yet. -/
@[elab_as_elim]
theorem subbox_induction_on' {p : Box Œπ ‚Üí Prop} (I : Box Œπ)
    (H_ind : ‚àÄ J ‚â§ I, (‚àÄ s, p (splitCenterBox J s)) ‚Üí p J)
    (H_nhds : ‚àÄ z ‚àà Box.Icc I, ‚àÉ U ‚àà ùìù[Box.Icc I] z, ‚àÄ J ‚â§ I, ‚àÄ (m : ‚Ñï), z ‚àà Box.Icc J ‚Üí
      Box.Icc J ‚äÜ U ‚Üí (‚àÄ i, J.upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ m) ‚Üí p J) :
    p I := by
  by_contra hpI
  -- First we use `H_ind` to construct a decreasing sequence of boxes such that `‚àÄ m, ¬¨p (J m)`.
  replace H_ind := fun J hJ ‚Ü¶ not_imp_not.2 (H_ind J hJ)
  simp only [not_forall] at H_ind
  choose! s hs using H_ind
  set J : ‚Ñï ‚Üí Box Œπ := fun m ‚Ü¶ (fun J ‚Ü¶ splitCenterBox J (s J))^[m] I
  have J_succ : ‚àÄ m, J (m + 1) = splitCenterBox (J m) (s <| J m) :=
    fun m ‚Ü¶ iterate_succ_apply' _ _ _
  -- Now we prove some properties of `J`
  have hJmono : Antitone J :=
    antitone_nat_of_succ_le fun n ‚Ü¶ by simpa [J_succ] using splitCenterBox_le _ _
  have hJle : ‚àÄ m, J m ‚â§ I := fun m ‚Ü¶ hJmono (zero_le m)
  have hJp : ‚àÄ m, ¬¨p (J m) :=
    fun m ‚Ü¶ Nat.recOn m hpI fun m ‚Ü¶ by simpa only [J_succ] using hs (J m) (hJle m)
  have hJsub : ‚àÄ m i, (J m).upper i - (J m).lower i = (I.upper i - I.lower i) / 2 ^ m := by
    intro m i
    induction' m with m ihm
    ¬∑ simp [J]
    simp only [pow_succ, J_succ, upper_sub_lower_splitCenterBox, ihm, div_div]
  have h0 : J 0 = I := rfl
  clear_value J
  clear hpI hs J_succ s
  -- Let `z` be the unique common point of all `(J m).Icc`. Then `H_nhds` proves `p (J m)` for
  -- sufficiently large `m`. This contradicts `hJp`.
  set z : Œπ ‚Üí ‚Ñù := ‚®Ü m, (J m).lower
  have hzJ : ‚àÄ m, z ‚àà Box.Icc (J m) :=
    mem_iInter.1 (ciSup_mem_iInter_Icc_of_antitone_Icc
      ((@Box.Icc Œπ).monotone.comp_antitone hJmono) fun m ‚Ü¶ (J m).lower_le_upper)
  have hJl_mem : ‚àÄ m, (J m).lower ‚àà Box.Icc I := fun m ‚Ü¶ le_iff_Icc.1 (hJle m) (J m).lower_mem_Icc
  have hJu_mem : ‚àÄ m, (J m).upper ‚àà Box.Icc I := fun m ‚Ü¶ le_iff_Icc.1 (hJle m) (J m).upper_mem_Icc
  have hJlz : Tendsto (fun m ‚Ü¶ (J m).lower) atTop (ùìù z) :=
    tendsto_atTop_ciSup (antitone_lower.comp hJmono) ‚ü®I.upper, fun x ‚ü®m, hm‚ü© ‚Ü¶ hm ‚ñ∏ (hJl_mem m).2‚ü©
  have hJuz : Tendsto (fun m ‚Ü¶ (J m).upper) atTop (ùìù z) := by
    suffices Tendsto (fun m ‚Ü¶ (J m).upper - (J m).lower) atTop (ùìù 0) by simpa using hJlz.add this
    refine tendsto_pi_nhds.2 fun i ‚Ü¶ ?_
    simpa [hJsub] using
      tendsto_const_nhds.div_atTop (tendsto_pow_atTop_atTop_of_one_lt _root_.one_lt_two)
  replace hJlz : Tendsto (fun m ‚Ü¶ (J m).lower) atTop (ùìù[Icc I.lower I.upper] z) :=
    tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ hJlz (Eventually.of_forall hJl_mem)
  replace hJuz : Tendsto (fun m ‚Ü¶ (J m).upper) atTop (ùìù[Icc I.lower I.upper] z) :=
    tendsto_nhdsWithin_of_tendsto_nhds_of_eventually_within _ hJuz (Eventually.of_forall hJu_mem)
  rcases H_nhds z (h0 ‚ñ∏ hzJ 0) with ‚ü®U, hUz, hU‚ü©
  rcases (tendsto_lift'.1 (hJlz.Icc hJuz) U hUz).exists with ‚ü®m, hUm‚ü©
  exact hJp m (hU (J m) (hJle m) m (hzJ m) hUm (hJsub m))

end Box

end BoxIntegral
