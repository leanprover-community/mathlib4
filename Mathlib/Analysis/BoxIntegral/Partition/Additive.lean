/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Partition.Split
import Mathlib.Analysis.NormedSpace.OperatorNorm

#align_import analysis.box_integral.partition.additive from "leanprover-community/mathlib"@"70fd9563a21e7b963887c9360bd29b2393e6225a"

/-!
# Box additive functions

We say that a function `f : Box Œπ ‚Üí M` from boxes in `‚Ñù‚Åø` to a commutative additive monoid `M` is
*box additive* on subboxes of `I‚ÇÄ : WithTop (Box Œπ)` if for any box `J`, `‚ÜëJ ‚â§ I‚ÇÄ`, and a partition
`œÄ` of `J`, `f J = ‚àë J' in œÄ.boxes, f J'`. We use `I‚ÇÄ : WithTop (Box Œπ)` instead of `I‚ÇÄ : Box Œπ` to
use the same definition for functions box additive on subboxes of a box and for functions box
additive on all boxes.

Examples of box-additive functions include the measure of a box and the integral of a fixed
integrable function over a box.

In this file we define box-additive functions and prove that a function such that
`f J = f (J ‚à© {x | x i < y}) + f (J ‚à© {x | y ‚â§ x i})` is box-additive.

### Tags

rectangular box, additive function
-/


noncomputable section

open Classical BigOperators Function Set

namespace BoxIntegral

variable {Œπ M : Type*} {n : ‚Ñï}

/-- A function on `Box Œπ` is called box additive if for every box `J` and a partition `œÄ` of `J`
we have `f J = ‚àë Ji in œÄ.boxes, f Ji`. A function is called box additive on subboxes of `I : Box Œπ`
if the same property holds for `J ‚â§ I`. We formalize these two notions in the same definition
using `I : WithBot (Box Œπ)`: the value `I = ‚ä§` corresponds to functions box additive on the whole
space. -/
structure BoxAdditiveMap (Œπ M : Type*) [AddCommMonoid M] (I : WithTop (Box Œπ)) where
  toFun : Box Œπ ‚Üí M
  sum_partition_boxes' : ‚àÄ J : Box Œπ, ‚ÜëJ ‚â§ I ‚Üí ‚àÄ œÄ : Prepartition J, œÄ.IsPartition ‚Üí
    ‚àë Ji in œÄ.boxes, toFun Ji = toFun J
#align box_integral.box_additive_map BoxIntegral.BoxAdditiveMap

scoped notation:25 Œπ " ‚Üí·µá·µÉ " M => BoxIntegral.BoxAdditiveMap Œπ M ‚ä§
scoped notation:25 Œπ " ‚Üí·µá·µÉ[" I "] " M => BoxIntegral.BoxAdditiveMap Œπ M I

namespace BoxAdditiveMap

open Box Prepartition Finset

variable {N : Type*} [AddCommMonoid M] [AddCommMonoid N] {I‚ÇÄ : WithTop (Box Œπ)} {I J : Box Œπ}
  {i : Œπ}

instance : FunLike (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (Box Œπ) (fun _ ‚Ü¶ M) where
  coe := toFun
  coe_injective' f g h := by cases f; cases g; congr
                             -- ‚ä¢ { toFun := toFun‚úù, sum_partition_boxes' := sum_partition_boxes'‚úù } = g
                                      -- ‚ä¢ { toFun := toFun‚úù¬π, sum_partition_boxes' := sum_partition_boxes'‚úù¬π } = { toF ‚Ä¶
                                               -- üéâ no goals

initialize_simps_projections BoxIntegral.BoxAdditiveMap (toFun ‚Üí apply)

#noalign box_integral.box_additive_map.to_fun_eq_coe

@[simp]
theorem coe_mk (f h) : ‚áë(mk f h : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) = f := rfl
#align box_integral.box_additive_map.coe_mk BoxIntegral.BoxAdditiveMap.coe_mk

theorem coe_injective : Injective fun (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) x => f x :=
  FunLike.coe_injective
#align box_integral.box_additive_map.coe_injective BoxIntegral.BoxAdditiveMap.coe_injective

-- porting note: was @[simp], now can be proved by `simp`
theorem coe_inj {f g : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M} : (f : Box Œπ ‚Üí M) = g ‚Üî f = g := FunLike.coe_fn_eq
#align box_integral.box_additive_map.coe_inj BoxIntegral.BoxAdditiveMap.coe_inj

theorem sum_partition_boxes (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (hI : ‚ÜëI ‚â§ I‚ÇÄ) {œÄ : Prepartition I}
    (h : œÄ.IsPartition) : ‚àë J in œÄ.boxes, f J = f I :=
  f.sum_partition_boxes' I hI œÄ h
#align box_integral.box_additive_map.sum_partition_boxes BoxIntegral.BoxAdditiveMap.sum_partition_boxes

@[simps (config := { fullyApplied := false })]
instance : Zero (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) :=
  ‚ü®‚ü®0, fun _ _ _ _ => sum_const_zero‚ü©‚ü©

instance : Inhabited (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) :=
  ‚ü®0‚ü©

instance : Add (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) :=
  ‚ü®fun f g =>
    ‚ü®f + g, fun I hI œÄ hœÄ => by
      simp only [Pi.add_apply, sum_add_distrib, sum_partition_boxes _ hI hœÄ]‚ü©‚ü©
      -- üéâ no goals

instance {R} [Monoid R] [DistribMulAction R M] : SMul R (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) :=
  ‚ü®fun r f =>
    ‚ü®r ‚Ä¢ (f : Box Œπ ‚Üí M), fun I hI œÄ hœÄ => by
      simp only [Pi.smul_apply, ‚Üê smul_sum, sum_partition_boxes _ hI hœÄ]‚ü©‚ü©
      -- üéâ no goals

instance : AddCommMonoid (Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) :=
  Function.Injective.addCommMonoid _ coe_injective rfl (fun _ _ => rfl) fun _ _ => rfl

@[simp]
theorem map_split_add (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (hI : ‚ÜëI ‚â§ I‚ÇÄ) (i : Œπ) (x : ‚Ñù) :
    (I.splitLower i x).elim' 0 f + (I.splitUpper i x).elim' 0 f = f I := by
  rw [‚Üê f.sum_partition_boxes hI (isPartitionSplit I i x), sum_split_boxes]
  -- üéâ no goals
#align box_integral.box_additive_map.map_split_add BoxIntegral.BoxAdditiveMap.map_split_add

/-- If `f` is box-additive on subboxes of `I‚ÇÄ`, then it is box-additive on subboxes of any
`I ‚â§ I‚ÇÄ`. -/
@[simps]
def restrict (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (I : WithTop (Box Œπ)) (hI : I ‚â§ I‚ÇÄ) : Œπ ‚Üí·µá·µÉ[I] M :=
  ‚ü®f, fun J hJ => f.2 J (hJ.trans hI)‚ü©
#align box_integral.box_additive_map.restrict BoxIntegral.BoxAdditiveMap.restrict

/-- If `f : Box Œπ ‚Üí M` is box additive on partitions of the form `split I i x`, then it is box
additive. -/
def ofMapSplitAdd [Fintype Œπ] (f : Box Œπ ‚Üí M) (I‚ÇÄ : WithTop (Box Œπ))
    (hf : ‚àÄ I : Box Œπ, ‚ÜëI ‚â§ I‚ÇÄ ‚Üí ‚àÄ {i x}, x ‚àà Ioo (I.lower i) (I.upper i) ‚Üí
      (I.splitLower i x).elim' 0 f + (I.splitUpper i x).elim' 0 f = f I) :
    Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M := by
  refine' ‚ü®f, _‚ü©
  -- ‚ä¢ ‚àÄ (J : Box Œπ), ‚ÜëJ ‚â§ I‚ÇÄ ‚Üí ‚àÄ (œÄ : Prepartition J), IsPartition œÄ ‚Üí ‚àë Ji in œÄ.b ‚Ä¶
  replace hf : ‚àÄ I : Box Œπ, ‚ÜëI ‚â§ I‚ÇÄ ‚Üí ‚àÄ s, (‚àë J in (splitMany I s).boxes, f J) = f I
  -- ‚ä¢ ‚àÄ (I : Box Œπ), ‚ÜëI ‚â§ I‚ÇÄ ‚Üí ‚àÄ (s : Finset (Œπ √ó ‚Ñù)), ‚àë J in (splitMany I s).boxe ‚Ä¶
  ¬∑ intro I hI s
    -- ‚ä¢ ‚àë J in (splitMany I s).boxes, f J = f I
    induction' s using Finset.induction_on with a s _ ihs
    -- ‚ä¢ ‚àë J in (splitMany I ‚àÖ).boxes, f J = f I
    ¬∑ simp
      -- üéâ no goals
    rw [splitMany_insert, inf_split, ‚Üê ihs, biUnion_boxes, sum_biUnion_boxes]
    -- ‚ä¢ ‚àë J in (splitMany I s).boxes, ‚àë J' in (split J a.fst a.snd).boxes, f J' = ‚àë  ‚Ä¶
    refine' Finset.sum_congr rfl fun J' hJ' => _
    -- ‚ä¢ ‚àë J' in (split J' a.fst a.snd).boxes, f J' = f J'
    by_cases h : a.2 ‚àà Ioo (J'.lower a.1) (J'.upper a.1)
    -- ‚ä¢ ‚àë J' in (split J' a.fst a.snd).boxes, f J' = f J'
    ¬∑ rw [sum_split_boxes]
      -- ‚ä¢ Option.elim' 0 (fun J' => f J') (splitLower J' a.fst a.snd) + Option.elim' 0 ‚Ä¶
      exact hf _ ((WithTop.coe_le_coe.2 <| le_of_mem _ hJ').trans hI) h
      -- üéâ no goals
    ¬∑ rw [split_of_not_mem_Ioo h, top_boxes, Finset.sum_singleton]
      -- üéâ no goals
  intro I hI œÄ hœÄ
  -- ‚ä¢ ‚àë Ji in œÄ.boxes, f Ji = f I
  have Hle : ‚àÄ J ‚àà œÄ, ‚ÜëJ ‚â§ I‚ÇÄ := fun J hJ => (WithTop.coe_le_coe.2 <| œÄ.le_of_mem hJ).trans hI
  -- ‚ä¢ ‚àë Ji in œÄ.boxes, f Ji = f I
  rcases hœÄ.exists_splitMany_le with ‚ü®s, hs‚ü©
  -- ‚ä¢ ‚àë Ji in œÄ.boxes, f Ji = f I
  rw [‚Üê hf _ hI, ‚Üê inf_of_le_right hs, inf_splitMany, biUnion_boxes, sum_biUnion_boxes]
  -- ‚ä¢ ‚àë Ji in œÄ.boxes, f Ji = ‚àë J in œÄ.boxes, ‚àë J' in (splitMany J s).boxes, f J'
  exact Finset.sum_congr rfl fun J hJ => (hf _ (Hle _ hJ) _).symm
  -- üéâ no goals
#align box_integral.box_additive_map.of_map_split_add BoxIntegral.BoxAdditiveMap.ofMapSplitAdd

/-- If `g : M ‚Üí N` is an additive map and `f` is a box additive map, then `g ‚àò f` is a box additive
map. -/
@[simps (config := { fullyApplied := false })]
def map (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (g : M ‚Üí+ N) : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] N where
  toFun := g ‚àò f
  sum_partition_boxes' I hI œÄ hœÄ := by simp_rw [comp, ‚Üê g.map_sum, f.sum_partition_boxes hI hœÄ]
                                       -- üéâ no goals
#align box_integral.box_additive_map.map BoxIntegral.BoxAdditiveMap.map

/-- If `f` is a box additive function on subboxes of `I` and `œÄ‚ÇÅ`, `œÄ‚ÇÇ` are two prepartitions of
`I` that cover the same part of `I`, then `‚àë J in œÄ‚ÇÅ.boxes, f J = ‚àë J in œÄ‚ÇÇ.boxes, f J`. -/
theorem sum_boxes_congr [Finite Œπ] (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] M) (hI : ‚ÜëI ‚â§ I‚ÇÄ) {œÄ‚ÇÅ œÄ‚ÇÇ : Prepartition I}
    (h : œÄ‚ÇÅ.iUnion = œÄ‚ÇÇ.iUnion) : ‚àë J in œÄ‚ÇÅ.boxes, f J = ‚àë J in œÄ‚ÇÇ.boxes, f J := by
  rcases exists_splitMany_inf_eq_filter_of_finite {œÄ‚ÇÅ, œÄ‚ÇÇ} ((finite_singleton _).insert _) with
    ‚ü®s, hs‚ü©
  simp only [inf_splitMany] at hs
  -- ‚ä¢ ‚àë J in œÄ‚ÇÅ.boxes, ‚Üëf J = ‚àë J in œÄ‚ÇÇ.boxes, ‚Üëf J
  rcases hs _ (Or.inl rfl), hs _ (Or.inr rfl) with ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©; clear hs
  -- ‚ä¢ ‚àë J in œÄ‚ÇÅ.boxes, ‚Üëf J = ‚àë J in œÄ‚ÇÇ.boxes, ‚Üëf J
                                                             -- ‚ä¢ ‚àë J in œÄ‚ÇÅ.boxes, ‚Üëf J = ‚àë J in œÄ‚ÇÇ.boxes, ‚Üëf J
  rw [h] at h‚ÇÅ
  -- ‚ä¢ ‚àë J in œÄ‚ÇÅ.boxes, ‚Üëf J = ‚àë J in œÄ‚ÇÇ.boxes, ‚Üëf J
  calc
    ‚àë J in œÄ‚ÇÅ.boxes, f J = ‚àë J in œÄ‚ÇÅ.boxes, ‚àë J' in (splitMany J s).boxes, f J' :=
      Finset.sum_congr rfl fun J hJ => (f.sum_partition_boxes ?_ (isPartition_splitMany _ _)).symm
    _ = ‚àë J in (œÄ‚ÇÅ.biUnion fun J => splitMany J s).boxes, f J := (sum_biUnion_boxes _ _ _).symm
    _ = ‚àë J in (œÄ‚ÇÇ.biUnion fun J => splitMany J s).boxes, f J := by rw [h‚ÇÅ, h‚ÇÇ]
    _ = ‚àë J in œÄ‚ÇÇ.boxes, ‚àë J' in (splitMany J s).boxes, f J' := (sum_biUnion_boxes _ _ _)
    _ = ‚àë J in œÄ‚ÇÇ.boxes, f J :=
      Finset.sum_congr rfl fun J hJ => f.sum_partition_boxes ?_ (isPartition_splitMany _ _)
  exacts [(WithTop.coe_le_coe.2 <| œÄ‚ÇÅ.le_of_mem hJ).trans hI,
    (WithTop.coe_le_coe.2 <| œÄ‚ÇÇ.le_of_mem hJ).trans hI]
#align box_integral.box_additive_map.sum_boxes_congr BoxIntegral.BoxAdditiveMap.sum_boxes_congr

section ToSMul

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

/-- If `f` is a box-additive map, then so is the map sending `I` to the scalar multiplication
by `f I` as a continuous linear map from `E` to itself. -/
def toSMul (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] ‚Ñù) : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] E ‚ÜíL[‚Ñù] E :=
  f.map (ContinuousLinearMap.lsmul ‚Ñù ‚Ñù).toLinearMap.toAddMonoidHom
#align box_integral.box_additive_map.to_smul BoxIntegral.BoxAdditiveMap.toSMul

@[simp]
theorem toSMul_apply (f : Œπ ‚Üí·µá·µÉ[I‚ÇÄ] ‚Ñù) (I : Box Œπ) (x : E) : f.toSMul I x = f I ‚Ä¢ x := rfl
#align box_integral.box_additive_map.to_smul_apply BoxIntegral.BoxAdditiveMap.toSMul_apply

end ToSMul

/-- Given a box `I‚ÇÄ` in `‚Ñù‚Åø‚Å∫¬π`, `f x : Box (Fin n) ‚Üí G` is a family of functions indexed by a real
`x` and for `x ‚àà [I‚ÇÄ.lower i, I‚ÇÄ.upper i]`, `f x` is box-additive on subboxes of the `i`-th face of
`I‚ÇÄ`, then `fun J ‚Ü¶ f (J.upper i) (J.face i) - f (J.lower i) (J.face i)` is box-additive on subboxes
of `I‚ÇÄ`. -/
@[simps!]
def upperSubLower.{u} {G : Type u} [AddCommGroup G] (I‚ÇÄ : Box (Fin (n + 1))) (i : Fin (n + 1))
    (f : ‚Ñù ‚Üí Box (Fin n) ‚Üí G) (fb : Icc (I‚ÇÄ.lower i) (I‚ÇÄ.upper i) ‚Üí Fin n ‚Üí·µá·µÉ[I‚ÇÄ.face i] G)
    (hf : ‚àÄ (x) (hx : x ‚àà Icc (I‚ÇÄ.lower i) (I‚ÇÄ.upper i)) (J), f x J = fb ‚ü®x, hx‚ü© J) :
    Fin (n + 1) ‚Üí·µá·µÉ[I‚ÇÄ] G :=
  ofMapSplitAdd (fun J : Box (Fin (n + 1)) => f (J.upper i) (J.face i) - f (J.lower i) (J.face i))
    I‚ÇÄ
    (by
      intro J hJ j x
      -- ‚ä¢ x ‚àà Set.Ioo (lower J j) (upper J j) ‚Üí Option.elim' 0 (fun J => f (upper J i) ‚Ä¶
      rw [WithTop.coe_le_coe] at hJ
      -- ‚ä¢ x ‚àà Set.Ioo (lower J j) (upper J j) ‚Üí Option.elim' 0 (fun J => f (upper J i) ‚Ä¶
      refine' i.succAboveCases (fun hx => _) (fun j hx => _) j
      -- ‚ä¢ Option.elim' 0 (fun J => f (upper J i) (face J i) - f (lower J i) (face J i) ‚Ä¶
      ¬∑ simp only [Box.splitLower_def hx, Box.splitUpper_def hx, update_same, ‚Üê WithBot.some_eq_coe,
          Option.elim', Box.face, (¬∑ ‚àò ¬∑), update_noteq (Fin.succAbove_ne _ _)]
        abel
        -- üéâ no goals
        -- üéâ no goals
      ¬∑ have : (J.face i : WithTop (Box (Fin n))) ‚â§ I‚ÇÄ.face i :=
          WithTop.coe_le_coe.2 (face_mono hJ i)
        rw [le_iff_Icc, @Box.Icc_eq_pi _ I‚ÇÄ] at hJ
        -- ‚ä¢ Option.elim' 0 (fun J => f (upper J i) (face J i) - f (lower J i) (face J i) ‚Ä¶
        simp only
        -- ‚ä¢ Option.elim' 0 (fun J => f (upper J i) (face J i) - f (lower J i) (face J i) ‚Ä¶
        rw [hf _ (hJ J.upper_mem_Icc _ trivial), hf _ (hJ J.lower_mem_Icc _ trivial),
          ‚Üê (fb _).map_split_add this j x, ‚Üê (fb _).map_split_add this j x]
        have hx' : x ‚àà Ioo ((J.face i).lower j) ((J.face i).upper j) := hx
        -- ‚ä¢ Option.elim' 0 (fun J => f (upper J i) (face J i) - f (lower J i) (face J i) ‚Ä¶
        simp only [Box.splitLower_def hx, Box.splitUpper_def hx, Box.splitLower_def hx',
          Box.splitUpper_def hx', ‚Üê WithBot.some_eq_coe, Option.elim', Box.face_mk,
          update_noteq (Fin.succAbove_ne _ _).symm, sub_add_sub_comm,
          update_comp_eq_of_injective _ (Fin.strictMono_succAbove i).injective j x, ‚Üê hf]
        simp only [Box.face])
        -- üéâ no goals
#align box_integral.box_additive_map.upper_sub_lower BoxIntegral.BoxAdditiveMap.upperSubLower

end BoxAdditiveMap

end BoxIntegral
