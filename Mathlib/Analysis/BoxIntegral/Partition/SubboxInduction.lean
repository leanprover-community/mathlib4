/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Box.SubboxInduction
import Mathlib.Analysis.BoxIntegral.Partition.Tagged

#align_import analysis.box_integral.partition.subbox_induction from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Induction on subboxes

In this file we prove (see
`BoxIntegral.Box.exists_taggedPartition_isHenstock_isSubordinate_homothetic`) that for every box `I`
in `â„â¿` and a function `r : â„â¿ â†’ â„` positive on `I` there exists a tagged partition `Ï€` of `I` such
that

* `Ï€` is a Henstock partition;
* `Ï€` is subordinate to `r`;
* each box in `Ï€` is homothetic to `I` with coefficient of the form `1 / 2 ^ n`.

Later we will use this lemma to prove that the Henstock filter is nontrivial, hence the Henstock
integral is well-defined.

## Tags

partition, tagged partition, Henstock integral
-/


namespace BoxIntegral

open Set Metric

open scoped Classical
open Topology

noncomputable section

variable {Î¹ : Type*} [Fintype Î¹] {I J : Box Î¹}

namespace Prepartition

/-- Split a box in `â„â¿` into `2 ^ n` boxes by hyperplanes passing through its center. -/
def splitCenter (I : Box Î¹) : Prepartition I where
  boxes := Finset.univ.map (Box.splitCenterBoxEmb I)
  le_of_mem' := by simp [I.splitCenterBox_le]
  pairwiseDisjoint := by
    rw [Finset.coe_map, Finset.coe_univ, image_univ]
    rintro _ âŸ¨s, rflâŸ© _ âŸ¨t, rflâŸ© Hne
    exact I.disjoint_splitCenterBox (mt (congr_arg _) Hne)
#align box_integral.prepartition.split_center BoxIntegral.Prepartition.splitCenter

@[simp]
theorem mem_splitCenter : J âˆˆ splitCenter I â†” âˆƒ s, I.splitCenterBox s = J := by simp [splitCenter]
#align box_integral.prepartition.mem_split_center BoxIntegral.Prepartition.mem_splitCenter

theorem isPartition_splitCenter (I : Box Î¹) : IsPartition (splitCenter I) := fun x hx => by
  simp [hx]
#align box_integral.prepartition.is_partition_split_center BoxIntegral.Prepartition.isPartition_splitCenter

theorem upper_sub_lower_of_mem_splitCenter (h : J âˆˆ splitCenter I) (i : Î¹) :
    J.upper i - J.lower i = (I.upper i - I.lower i) / 2 :=
  let âŸ¨s, hsâŸ© := mem_splitCenter.1 h
  hs â–¸ I.upper_sub_lower_splitCenterBox s i
#align box_integral.prepartition.upper_sub_lower_of_mem_split_center BoxIntegral.Prepartition.upper_sub_lower_of_mem_splitCenter

end Prepartition

namespace Box

open Prepartition TaggedPrepartition

/-- Let `p` be a predicate on `Box Î¹`, let `I` be a box. Suppose that the following two properties
hold true.

* Consider a smaller box `J â‰¤ I`. The hyperplanes passing through the center of `J` split it into
  `2 ^ n` boxes. If `p` holds true on each of these boxes, then it true on `J`.
* For each `z` in the closed box `I.Icc` there exists a neighborhood `U` of `z` within `I.Icc` such
  that for every box `J â‰¤ I` such that `z âˆˆ J.Icc âŠ† U`, if `J` is homothetic to `I` with a
  coefficient of the form `1 / 2 ^ m`, then `p` is true on `J`.

Then `p I` is true. See also `BoxIntegral.Box.subbox_induction_on'` for a version using
`BoxIntegral.Box.splitCenterBox` instead of `BoxIntegral.Prepartition.splitCenter`. -/
@[elab_as_elim]
theorem subbox_induction_on {p : Box Î¹ â†’ Prop} (I : Box Î¹)
    (H_ind : âˆ€ J â‰¤ I, (âˆ€ J' âˆˆ splitCenter J, p J') â†’ p J)
    (H_nhds : âˆ€ z âˆˆ Box.Icc I, âˆƒ U âˆˆ ğ“[Box.Icc I] z, âˆ€ J â‰¤ I, âˆ€ (m : â„•),
      z âˆˆ Box.Icc J â†’ Box.Icc J âŠ† U â†’
        (âˆ€ i, J.upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ m) â†’ p J) :
    p I := by
  refine subbox_induction_on' I (fun J hle hs => H_ind J hle fun J' h' => ?_) H_nhds
  rcases mem_splitCenter.1 h' with âŸ¨s, rflâŸ©
  exact hs s
#align box_integral.box.subbox_induction_on BoxIntegral.Box.subbox_induction_on

/-- Given a box `I` in `â„â¿` and a function `r : â„â¿ â†’ (0, âˆ)`, there exists a tagged partition `Ï€` of
`I` such that

* `Ï€` is a Henstock partition;
* `Ï€` is subordinate to `r`;
* each box in `Ï€` is homothetic to `I` with coefficient of the form `1 / 2 ^ m`.

This lemma implies that the Henstock filter is nontrivial, hence the Henstock integral is
well-defined. -/
theorem exists_taggedPartition_isHenstock_isSubordinate_homothetic (I : Box Î¹)
    (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    âˆƒ Ï€ : TaggedPrepartition I, Ï€.IsPartition âˆ§ Ï€.IsHenstock âˆ§ Ï€.IsSubordinate r âˆ§
      (âˆ€ J âˆˆ Ï€, âˆƒ m : â„•, âˆ€ i, (J : _).upper i - J.lower i = (I.upper i - I.lower i) / 2 ^ m) âˆ§
        Ï€.distortion = I.distortion := by
  refine subbox_induction_on I (fun J _ hJ => ?_) fun z _ => ?_
  Â· choose! Ï€i hP hHen hr Hn _ using hJ
    choose! n hn using Hn
    have hP : ((splitCenter J).biUnionTagged Ï€i).IsPartition :=
      (isPartition_splitCenter _).biUnionTagged hP
    have hsub : âˆ€ J' âˆˆ (splitCenter J).biUnionTagged Ï€i, âˆƒ n : â„•, âˆ€ i,
        (J' : _).upper i - J'.lower i = (J.upper i - J.lower i) / 2 ^ n := by
      intro J' hJ'
      rcases (splitCenter J).mem_biUnionTagged.1 hJ' with âŸ¨Jâ‚, hâ‚, hâ‚‚âŸ©
      refine âŸ¨n Jâ‚ J' + 1, fun i => ?_âŸ©
      simp only [hn Jâ‚ hâ‚ J' hâ‚‚, upper_sub_lower_of_mem_splitCenter hâ‚, pow_succ', div_div]
    refine âŸ¨_, hP, isHenstock_biUnionTagged.2 hHen, isSubordinate_biUnionTagged.2 hr, hsub, ?_âŸ©
    refine TaggedPrepartition.distortion_of_const _ hP.nonempty_boxes fun J' h' => ?_
    rcases hsub J' h' with âŸ¨n, hnâŸ©
    exact Box.distortion_eq_of_sub_eq_div hn
  Â· refine âŸ¨Box.Icc I âˆ© closedBall z (r z),
      inter_mem_nhdsWithin _ (closedBall_mem_nhds _ (r z).coe_prop), ?_âŸ©
    intro J _ n Hmem HIcc Hsub
    rw [Set.subset_inter_iff] at HIcc
    refine âŸ¨single _ _ le_rfl _ Hmem, isPartition_single _, isHenstock_single _,
      (isSubordinate_single _ _).2 HIcc.2, ?_, distortion_single _ _âŸ©
    simp only [TaggedPrepartition.mem_single, forall_eq]
    refine âŸ¨0, fun i => ?_âŸ©
    simp
set_option linter.uppercaseLean3 false in
#align box_integral.box.exists_tagged_partition_is_Henstock_is_subordinate_homothetic BoxIntegral.Box.exists_taggedPartition_isHenstock_isSubordinate_homothetic

end Box

namespace Prepartition

open TaggedPrepartition Finset Function

/-- Given a box `I` in `â„â¿`, a function `r : â„â¿ â†’ (0, âˆ)`, and a prepartition `Ï€` of `I`, there
exists a tagged prepartition `Ï€'` of `I` such that

* each box of `Ï€'` is included in some box of `Ï€`;
* `Ï€'` is a Henstock partition;
* `Ï€'` is subordinate to `r`;
* `Ï€'` covers exactly the same part of `I` as `Ï€`;
* the distortion of `Ï€'` is equal to the distortion of `Ï€`.
-/
theorem exists_tagged_le_isHenstock_isSubordinate_iUnion_eq {I : Box Î¹} (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„))
    (Ï€ : Prepartition I) :
    âˆƒ Ï€' : TaggedPrepartition I, Ï€'.toPrepartition â‰¤ Ï€ âˆ§ Ï€'.IsHenstock âˆ§ Ï€'.IsSubordinate r âˆ§
      Ï€'.distortion = Ï€.distortion âˆ§ Ï€'.iUnion = Ï€.iUnion := by
  have := fun J => Box.exists_taggedPartition_isHenstock_isSubordinate_homothetic J r
  choose! Ï€i Ï€ip Ï€iH Ï€ir _ Ï€id using this
  refine âŸ¨Ï€.biUnionTagged Ï€i, biUnion_le _ _, isHenstock_biUnionTagged.2 fun J _ => Ï€iH J,
    isSubordinate_biUnionTagged.2 fun J _ => Ï€ir J, ?_, Ï€.iUnion_biUnion_partition fun J _ => Ï€ip JâŸ©
  rw [distortion_biUnionTagged]
  exact sup_congr rfl fun J _ => Ï€id J
set_option linter.uppercaseLean3 false in
#align box_integral.prepartition.exists_tagged_le_is_Henstock_is_subordinate_Union_eq BoxIntegral.Prepartition.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq

/-- Given a prepartition `Ï€` of a box `I` and a function `r : â„â¿ â†’ (0, âˆ)`, `Ï€.toSubordinate r`
is a tagged partition `Ï€'` such that

* each box of `Ï€'` is included in some box of `Ï€`;
* `Ï€'` is a Henstock partition;
* `Ï€'` is subordinate to `r`;
* `Ï€'` covers exactly the same part of `I` as `Ï€`;
* the distortion of `Ï€'` is equal to the distortion of `Ï€`.
-/
def toSubordinate (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : TaggedPrepartition I :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose
#align box_integral.prepartition.to_subordinate BoxIntegral.Prepartition.toSubordinate

theorem toSubordinate_toPrepartition_le (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€.toSubordinate r).toPrepartition â‰¤ Ï€ :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose_spec.1
#align box_integral.prepartition.to_subordinate_to_prepartition_le BoxIntegral.Prepartition.toSubordinate_toPrepartition_le

theorem isHenstock_toSubordinate (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€.toSubordinate r).IsHenstock :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose_spec.2.1
set_option linter.uppercaseLean3 false in
#align box_integral.prepartition.is_Henstock_to_subordinate BoxIntegral.Prepartition.isHenstock_toSubordinate

theorem isSubordinate_toSubordinate (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€.toSubordinate r).IsSubordinate r :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose_spec.2.2.1
#align box_integral.prepartition.is_subordinate_to_subordinate BoxIntegral.Prepartition.isSubordinate_toSubordinate

@[simp]
theorem distortion_toSubordinate (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€.toSubordinate r).distortion = Ï€.distortion :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose_spec.2.2.2.1
#align box_integral.prepartition.distortion_to_subordinate BoxIntegral.Prepartition.distortion_toSubordinate

@[simp]
theorem iUnion_toSubordinate (Ï€ : Prepartition I) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€.toSubordinate r).iUnion = Ï€.iUnion :=
  (Ï€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r).choose_spec.2.2.2.2
#align box_integral.prepartition.Union_to_subordinate BoxIntegral.Prepartition.iUnion_toSubordinate

end Prepartition

namespace TaggedPrepartition

/-- Given a tagged prepartition `Ï€â‚`, a prepartition `Ï€â‚‚` that covers exactly `I \ Ï€â‚.iUnion`, and
a function `r : â„â¿ â†’ (0, âˆ)`, returns the union of `Ï€â‚` and `Ï€â‚‚.toSubordinate r`. This partition
`Ï€` has the following properties:

* `Ï€` is a partition, i.e. it covers the whole `I`;
* `Ï€â‚.boxes âŠ† Ï€.boxes`;
* `Ï€.tag J = Ï€â‚.tag J` whenever `J âˆˆ Ï€â‚`;
* `Ï€` is Henstock outside of `Ï€â‚`: `Ï€.tag J âˆˆ J.Icc` whenever `J âˆˆ Ï€`, `J âˆ‰ Ï€â‚`;
* `Ï€` is subordinate to `r` outside of `Ï€â‚`;
* the distortion of `Ï€` is equal to the maximum of the distortions of `Ï€â‚` and `Ï€â‚‚`.
-/
def unionComplToSubordinate (Ï€â‚ : TaggedPrepartition I) (Ï€â‚‚ : Prepartition I)
    (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : TaggedPrepartition I :=
  Ï€â‚.disjUnion (Ï€â‚‚.toSubordinate r)
    (((Ï€â‚‚.iUnion_toSubordinate r).trans hU).symm â–¸ disjoint_sdiff_self_right)
#align box_integral.tagged_prepartition.union_compl_to_subordinate BoxIntegral.TaggedPrepartition.unionComplToSubordinate

theorem isPartition_unionComplToSubordinate (Ï€â‚ : TaggedPrepartition I) (Ï€â‚‚ : Prepartition I)
    (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    IsPartition (Ï€â‚.unionComplToSubordinate Ï€â‚‚ hU r) :=
  Prepartition.isPartitionDisjUnionOfEqDiff ((Ï€â‚‚.iUnion_toSubordinate r).trans hU)
#align box_integral.tagged_prepartition.is_partition_union_compl_to_subordinate BoxIntegral.TaggedPrepartition.isPartition_unionComplToSubordinate

@[simp]
theorem unionComplToSubordinate_boxes (Ï€â‚ : TaggedPrepartition I) (Ï€â‚‚ : Prepartition I)
    (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€â‚.unionComplToSubordinate Ï€â‚‚ hU r).boxes = Ï€â‚.boxes âˆª (Ï€â‚‚.toSubordinate r).boxes := rfl
#align box_integral.tagged_prepartition.union_compl_to_subordinate_boxes BoxIntegral.TaggedPrepartition.unionComplToSubordinate_boxes

@[simp]
theorem iUnion_unionComplToSubordinate_boxes (Ï€â‚ : TaggedPrepartition I) (Ï€â‚‚ : Prepartition I)
    (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€â‚.unionComplToSubordinate Ï€â‚‚ hU r).iUnion = I :=
  (isPartition_unionComplToSubordinate _ _ _ _).iUnion_eq
#align box_integral.tagged_prepartition.Union_union_compl_to_subordinate_boxes BoxIntegral.TaggedPrepartition.iUnion_unionComplToSubordinate_boxes

@[simp]
theorem distortion_unionComplToSubordinate (Ï€â‚ : TaggedPrepartition I) (Ï€â‚‚ : Prepartition I)
    (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    (Ï€â‚.unionComplToSubordinate Ï€â‚‚ hU r).distortion = max Ï€â‚.distortion Ï€â‚‚.distortion := by
  simp [unionComplToSubordinate]
#align box_integral.tagged_prepartition.distortion_union_compl_to_subordinate BoxIntegral.TaggedPrepartition.distortion_unionComplToSubordinate

end TaggedPrepartition

end

end BoxIntegral
