/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Partition.SubboxInduction
import Mathlib.Analysis.BoxIntegral.Partition.Split

#align_import analysis.box_integral.partition.filter from "leanprover-community/mathlib"@"92ca63f0fb391a9ca5f22d2409a6080e786d99f7"

/-!
# Filters used in box-based integrals

First we define a structure `BoxIntegral.IntegrationParams`. This structure will be used as an
argument in the definition of `BoxIntegral.integral` in order to use the same definition for a few
well-known definitions of integrals based on partitions of a rectangular box into subboxes (Riemann
integral, Henstock-Kurzweil integral, and McShane integral).

This structure holds three boolean values (see below), and encodes eight different sets of
parameters; only four of these values are used somewhere in `mathlib4`. Three of them correspond to
the integration theories listed above, and one is a generalization of the one-dimensional
Henstock-Kurzweil integral such that the divergence theorem works without additional integrability
assumptions.

Finally, for each set of parameters `l : BoxIntegral.IntegrationParams` and a rectangular box
`I : BoxIntegral.Box Œπ`, we define several `Filter`s that will be used either in the definition of
the corresponding integral, or in the proofs of its properties. We equip
`BoxIntegral.IntegrationParams` with a `BoundedOrder` structure such that larger
`IntegrationParams` produce larger filters.

## Main definitions

### Integration parameters

The structure `BoxIntegral.IntegrationParams` has 3 boolean fields with the following meaning:

* `bRiemann`: the value `true` means that the filter corresponds to a Riemann-style integral, i.e.
  in the definition of integrability we require a constant upper estimate `r` on the size of boxes
  of a tagged partition; the value `false` means that the estimate may depend on the position of the
  tag.

* `bHenstock`: the value `true` means that we require that each tag belongs to its own closed box;
  the value `false` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `true` means that `r` can depend on the maximal ratio of sides of the
  same box of a partition. Presence of this case make quite a few proofs harder but we can prove the
  divergence theorem only for the filter `BoxIntegral.IntegrationParams.GP = ‚ä• =
  {bRiemann := false, bHenstock := true, bDistortion := true}`.

### Well-known sets of parameters

Out of eight possible values of `BoxIntegral.IntegrationParams`, the following four are used in
the library.

* `BoxIntegral.IntegrationParams.Riemann` (`bRiemann = true`, `bHenstock = true`,
  `bDistortion = false`): this value corresponds to the Riemann integral; in the corresponding
  filter, we require that the diameters of all boxes `J` of a tagged partition are bounded from
  above by a constant upper estimate that may not depend on the geometry of `J`, and each tag
  belongs to the corresponding closed box.

* `BoxIntegral.IntegrationParams.Henstock` (`bRiemann = false`, `bHenstock = true`,
  `bDistortion = false`): this value corresponds to the most natural generalization of
  Henstock-Kurzweil integral to higher dimension; the only (but important!) difference between this
  theory and Riemann integral is that instead of a constant upper estimate on the size of all boxes
  of a partition, we require that the partition is *subordinate* to a possibly discontinuous
  function `r : (Œπ ‚Üí ‚Ñù) ‚Üí {x : ‚Ñù | 0 < x}`, i.e. each box `J` is included in a closed ball with
  center `œÄ.tag J` and radius `r J`.

* `BoxIntegral.IntegrationParams.McShane` (`bRiemann = false`, `bHenstock = false`,
  `bDistortion = false`): this value corresponds to the McShane integral; the only difference with
  the Henstock integral is that we allow tags to be outside of their boxes; the tags still have to
  be in the ambient closed box, and the partition still has to be subordinate to a function.

* `BoxIntegral.IntegrationParams.GP = ‚ä•` (`bRiemann = false`, `bHenstock = true`,
  `bDistortion = true`): this is the least integration theory in our list, i.e., all functions
  integrable in any other theory is integrable in this one as well.  This is a non-standard
  generalization of the Henstock-Kurzweil integral to higher dimension.  In dimension one, it
  generates the same filter as `Henstock`. In higher dimension, this generalization defines an
  integration theory such that the divergence of any Fr√©chet differentiable function `f` is
  integrable, and its integral is equal to the sum of integrals of `f` over the faces of the box,
  taken with appropriate signs.

  A function `f` is `GP`-integrable if for any `Œµ > 0` and `c : ‚Ñù‚â•0` there exists
  `r : (Œπ ‚Üí ‚Ñù) ‚Üí {x : ‚Ñù | 0 < x}` such that for any tagged partition `œÄ` subordinate to `r`, if each
  tag belongs to the corresponding closed box and for each box `J ‚àà œÄ`, the maximal ratio of its
  sides is less than or equal to `c`, then the integral sum of `f` over `œÄ` is `Œµ`-close to the
  integral.

### Filters and predicates on `TaggedPrepartition I`

For each value of `IntegrationParams` and a rectangular box `I`, we define a few filters on
`TaggedPrepartition I`. First, we define a predicate

```
structure BoxIntegral.IntegrationParams.MemBaseSet (l : BoxIntegral.IntegrationParams)
  (I : BoxIntegral.Box Œπ) (c : ‚Ñù‚â•0) (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù))
  (œÄ : BoxIntegral.TaggedPrepartition I) : Prop where
```

This predicate says that

* if `l.bHenstock`, then `œÄ` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `œÄ` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `œÄ` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `œÄ'` with distortion `‚â§ c` that covers
  exactly `I \ œÄ.iUnion`.

The last condition is always true for `c > 1`, see TODO section for more details.

Then we define a predicate `BoxIntegral.IntegrationParams.RCond` on functions
`r : (Œπ ‚Üí ‚Ñù) ‚Üí {x : ‚Ñù | 0 < x}`. If `l.bRiemann`, then this predicate requires `r` to be a constant
function, otherwise it imposes no restrictions on `r`. We introduce this definition to prove a few
dot-notation lemmas: e.g., `BoxIntegral.IntegrationParams.RCond.min` says that the pointwise
minimum of two functions that satisfy this condition satisfies this condition as well.

Then we define four filters on `BoxIntegral.TaggedPrepartition I`.

* `BoxIntegral.IntegrationParams.toFilterDistortion`: an auxiliary filter that takes parameters
  `(l : BoxIntegral.IntegrationParams) (I : BoxIntegral.Box Œπ) (c : ‚Ñù‚â•0)` and returns the
  filter generated by all sets `{œÄ | MemBaseSet l I c r œÄ}`, where `r` is a function satisfying
  the predicate `BoxIntegral.IntegrationParams.RCond l`;

* `BoxIntegral.IntegrationParams.toFilter l I`: the supremum of `l.toFilterDistortion I c`
  over all `c : ‚Ñù‚â•0`;

* `BoxIntegral.IntegrationParams.toFilterDistortioniUnion l I c œÄ‚ÇÄ`, where `œÄ‚ÇÄ` is a
  prepartition of `I`: the infimum of `l.toFilterDistortion I c` and the principal filter
  generated by `{œÄ | œÄ.iUnion = œÄ‚ÇÄ.iUnion}`;

* `BoxIntegral.IntegrationParams.toFilteriUnion l I œÄ‚ÇÄ`: the supremum of
  `l.toFilterDistortioniUnion l I c œÄ‚ÇÄ` over all `c : ‚Ñù‚â•0`. This is the filter (in the case
  `œÄ‚ÇÄ = ‚ä§` is the one-box partition of `I`) used in the definition of the integral of a function
  over a box.

## Implementation details

* Later we define the integral of a function over a rectangular box as the limit (if it exists) of
  the integral sums along `BoxIntegral.IntegrationParams.toFilteriUnion l I ‚ä§`. While it is
  possible to define the integral with a general filter on `BoxIntegral.TaggedPrepartition I` as a
  parameter, many lemmas (e.g., Sacks-Henstock lemma and most results about integrability of
  functions) require the filter to have a predictable structure. So, instead of adding assumptions
  about the filter here and there, we define this auxiliary type that can encode all integration
  theories we need in practice.

* While the definition of the integral only uses the filter
  `BoxIntegral.IntegrationParams.toFilteriUnion l I ‚ä§` and partitions of a box, some lemmas
  (e.g., the Henstock-Sacks lemmas) are best formulated in terms of the predicate `MemBaseSet` and
  other filters defined above.

* We use `Bool` instead of `Prop` for the fields of `IntegrationParams` in order to have decidable
  equality and inequalities.

## TODO

Currently, `BoxIntegral.IntegrationParams.MemBaseSet` explicitly requires that there exists a
partition of the complement `I \ œÄ.iUnion` with distortion `‚â§ c`. For `c > 1`, this condition is
always true but the proof of this fact requires more API about
`BoxIntegral.Prepartition.splitMany`. We should formalize this fact, then either require `c > 1`
everywhere, or replace `‚â§ c` with `< c` so that we automatically get `c > 1` for a non-trivial
prepartition (and consider the special case `œÄ = ‚ä•` separately if needed).

## Tags

integral, rectangular box, partition, filter
-/


open Set Function Filter Metric Finset Bool

open scoped Classical
open Topology Filter NNReal

noncomputable section

namespace BoxIntegral

variable {Œπ : Type*} [Fintype Œπ] {I J : Box Œπ} {c c‚ÇÅ c‚ÇÇ : ‚Ñù‚â•0} {r r‚ÇÅ r‚ÇÇ : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)}
  {œÄ œÄ‚ÇÅ œÄ‚ÇÇ : TaggedPrepartition I}

open TaggedPrepartition

/-- An `IntegrationParams` is a structure holding 3 boolean values used to define a filter to be
used in the definition of a box-integrable function.

* `bRiemann`: the value `true` means that the filter corresponds to a Riemann-style integral, i.e.
  in the definition of integrability we require a constant upper estimate `r` on the size of boxes
  of a tagged partition; the value `false` means that the estimate may depend on the position of the
  tag.

* `bHenstock`: the value `true` means that we require that each tag belongs to its own closed box;
  the value `false` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `true` means that `r` can depend on the maximal ratio of sides of the
  same box of a partition. Presence of this case makes quite a few proofs harder but we can prove
  the divergence theorem only for the filter `BoxIntegral.IntegrationParams.GP = ‚ä• =
  {bRiemann := false, bHenstock := true, bDistortion := true}`.
-/
@[ext]
structure IntegrationParams : Type where
  (bRiemann bHenstock bDistortion : Bool)
#align box_integral.integration_params BoxIntegral.IntegrationParams

variable {l l‚ÇÅ l‚ÇÇ : IntegrationParams}

namespace IntegrationParams

/-- Auxiliary equivalence with a product type used to lift an order. -/
def equivProd : IntegrationParams ‚âÉ Bool √ó Bool·µí·µà √ó Bool·µí·µà where
  toFun l := ‚ü®l.1, OrderDual.toDual l.2, OrderDual.toDual l.3‚ü©
  invFun l := ‚ü®l.1, OrderDual.ofDual l.2.1, OrderDual.ofDual l.2.2‚ü©
  left_inv _ := rfl
  right_inv _ := rfl
#align box_integral.integration_params.equiv_prod BoxIntegral.IntegrationParams.equivProd

instance : PartialOrder IntegrationParams :=
  PartialOrder.lift equivProd equivProd.injective

/-- Auxiliary `OrderIso` with a product type used to lift a `BoundedOrder` structure. -/
def isoProd : IntegrationParams ‚âÉo Bool √ó Bool·µí·µà √ó Bool·µí·µà :=
  ‚ü®equivProd, Iff.rfl‚ü©
#align box_integral.integration_params.iso_prod BoxIntegral.IntegrationParams.isoProd

instance : BoundedOrder IntegrationParams :=
  isoProd.symm.toGaloisInsertion.liftBoundedOrder

/-- The value `BoxIntegral.IntegrationParams.GP = ‚ä•`
(`bRiemann = false`, `bHenstock = true`, `bDistortion = true`)
corresponds to a generalization of the Henstock integral such that the Divergence theorem holds true
without additional integrability assumptions, see the module docstring for details. -/
instance : Inhabited IntegrationParams :=
  ‚ü®‚ä•‚ü©

instance : DecidableRel ((¬∑ ‚â§ ¬∑) : IntegrationParams ‚Üí IntegrationParams ‚Üí Prop) :=
  fun _ _ => And.decidable

instance : DecidableEq IntegrationParams :=
  fun x y => decidable_of_iff _ (IntegrationParams.ext_iff x y).symm

/-- The `BoxIntegral.IntegrationParams` corresponding to the Riemann integral. In the
corresponding filter, we require that the diameters of all boxes `J` of a tagged partition are
bounded from above by a constant upper estimate that may not depend on the geometry of `J`, and each
tag belongs to the corresponding closed box. -/
def Riemann : IntegrationParams where
  bRiemann := true
  bHenstock := true
  bDistortion := false
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.Riemann BoxIntegral.IntegrationParams.Riemann

/-- The `BoxIntegral.IntegrationParams` corresponding to the Henstock-Kurzweil integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r` and each tag belongs to the corresponding closed box. -/
def Henstock : IntegrationParams :=
  ‚ü®false, true, false‚ü©
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.Henstock BoxIntegral.IntegrationParams.Henstock

/-- The `BoxIntegral.IntegrationParams` corresponding to the McShane integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r`; the tags may be outside of the corresponding closed box
(but still inside the ambient closed box `I.Icc`). -/
def McShane : IntegrationParams :=
  ‚ü®false, false, false‚ü©
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.McShane BoxIntegral.IntegrationParams.McShane

/-- The `BoxIntegral.IntegrationParams` corresponding to the generalized Perron integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r` and each tag belongs to the corresponding closed box. We also
require an upper estimate on the distortion of all boxes of the partition. -/
def GP : IntegrationParams := ‚ä•
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.GP BoxIntegral.IntegrationParams.GP

theorem henstock_le_riemann : Henstock ‚â§ Riemann := by trivial
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.Henstock_le_Riemann BoxIntegral.IntegrationParams.henstock_le_riemann

theorem henstock_le_mcShane : Henstock ‚â§ McShane := by trivial
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.Henstock_le_McShane BoxIntegral.IntegrationParams.henstock_le_mcShane

theorem gp_le : GP ‚â§ l :=
  bot_le
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.GP_le BoxIntegral.IntegrationParams.gp_le

/-- The predicate corresponding to a base set of the filter defined by an
`IntegrationParams`. It says that

* if `l.bHenstock`, then `œÄ` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `œÄ` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `œÄ` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `œÄ'` with distortion `‚â§ c` that covers
  exactly `I \ œÄ.iUnion`.

The last condition is automatically verified for partitions, and is used in the proof of the
Sacks-Henstock inequality to compare two prepartitions covering the same part of the box.

It is also automatically satisfied for any `c > 1`, see TODO section of the module docstring for
details. -/
structure MemBaseSet (l : IntegrationParams) (I : Box Œπ) (c : ‚Ñù‚â•0) (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù))
    (œÄ : TaggedPrepartition I) : Prop where
  protected isSubordinate : œÄ.IsSubordinate r
  protected isHenstock : l.bHenstock ‚Üí œÄ.IsHenstock
  protected distortion_le : l.bDistortion ‚Üí œÄ.distortion ‚â§ c
  protected exists_compl : l.bDistortion ‚Üí ‚àÉ œÄ' : Prepartition I,
    œÄ'.iUnion = ‚ÜëI \ œÄ.iUnion ‚àß œÄ'.distortion ‚â§ c
#align box_integral.integration_params.mem_base_set BoxIntegral.IntegrationParams.MemBaseSet

/-- A predicate saying that in case `l.bRiemann = true`, the function `r` is a constant. -/
def RCond {Œπ : Type*} (l : IntegrationParams) (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)) : Prop :=
  l.bRiemann ‚Üí ‚àÄ x, r x = r 0
#align box_integral.integration_params.r_cond BoxIntegral.IntegrationParams.RCond

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilterDistortion I c` if there exists
a function `r : ‚Ñù‚Åø ‚Üí (0, ‚àû)` (or a constant `r` if `l.bRiemann = true`) such that `s` contains each
prepartition `œÄ` such that `l.MemBaseSet I c r œÄ`. -/
def toFilterDistortion (l : IntegrationParams) (I : Box Œπ) (c : ‚Ñù‚â•0) :
    Filter (TaggedPrepartition I) :=
  ‚®Ö (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)) (_ : l.RCond r), ùìü { œÄ | l.MemBaseSet I c r œÄ }
#align box_integral.integration_params.to_filter_distortion BoxIntegral.IntegrationParams.toFilterDistortion

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilter I` if for any `c : ‚Ñù‚â•0` there
exists a function `r : ‚Ñù‚Åø ‚Üí (0, ‚àû)` (or a constant `r` if `l.bRiemann = true`) such that
`s` contains each prepartition `œÄ` such that `l.MemBaseSet I c r œÄ`. -/
def toFilter (l : IntegrationParams) (I : Box Œπ) : Filter (TaggedPrepartition I) :=
  ‚®Ü c : ‚Ñù‚â•0, l.toFilterDistortion I c
#align box_integral.integration_params.to_filter BoxIntegral.IntegrationParams.toFilter

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilterDistortioniUnion I c œÄ‚ÇÄ` if
there exists a function `r : ‚Ñù‚Åø ‚Üí (0, ‚àû)` (or a constant `r` if `l.bRiemann = true`) such that `s`
contains each prepartition `œÄ` such that `l.MemBaseSet I c r œÄ` and `œÄ.iUnion = œÄ‚ÇÄ.iUnion`. -/
def toFilterDistortioniUnion (l : IntegrationParams) (I : Box Œπ) (c : ‚Ñù‚â•0) (œÄ‚ÇÄ : Prepartition I) :=
  l.toFilterDistortion I c ‚äì ùìü { œÄ | œÄ.iUnion = œÄ‚ÇÄ.iUnion }
#align box_integral.integration_params.to_filter_distortion_Union BoxIntegral.IntegrationParams.toFilterDistortioniUnion

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilteriUnion I œÄ‚ÇÄ` if for any `c : ‚Ñù‚â•0`
there exists a function `r : ‚Ñù‚Åø ‚Üí (0, ‚àû)` (or a constant `r` if `l.bRiemann = true`) such that `s`
contains each prepartition `œÄ` such that `l.MemBaseSet I c r œÄ` and `œÄ.iUnion = œÄ‚ÇÄ.iUnion`. -/
def toFilteriUnion (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I) :=
  ‚®Ü c : ‚Ñù‚â•0, l.toFilterDistortioniUnion I c œÄ‚ÇÄ
#align box_integral.integration_params.to_filter_Union BoxIntegral.IntegrationParams.toFilteriUnion

theorem rCond_of_bRiemann_eq_false {Œπ} (l : IntegrationParams) (hl : l.bRiemann = false)
    {r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)} : l.RCond r := by
  simp [RCond, hl]
set_option linter.uppercaseLean3 false in
#align box_integral.integration_params.r_cond_of_bRiemann_eq_ff BoxIntegral.IntegrationParams.rCond_of_bRiemann_eq_false

theorem toFilter_inf_iUnion_eq (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I) :
    l.toFilter I ‚äì ùìü { œÄ | œÄ.iUnion = œÄ‚ÇÄ.iUnion } = l.toFilteriUnion I œÄ‚ÇÄ :=
  (iSup_inf_principal _ _).symm
#align box_integral.integration_params.to_filter_inf_Union_eq BoxIntegral.IntegrationParams.toFilter_inf_iUnion_eq

theorem MemBaseSet.mono' (I : Box Œπ) (h : l‚ÇÅ ‚â§ l‚ÇÇ) (hc : c‚ÇÅ ‚â§ c‚ÇÇ) {œÄ : TaggedPrepartition I}
    (hr : ‚àÄ J ‚àà œÄ, r‚ÇÅ (œÄ.tag J) ‚â§ r‚ÇÇ (œÄ.tag J)) (hœÄ : l‚ÇÅ.MemBaseSet I c‚ÇÅ r‚ÇÅ œÄ) :
    l‚ÇÇ.MemBaseSet I c‚ÇÇ r‚ÇÇ œÄ :=
  ‚ü®hœÄ.1.mono' hr, fun h‚ÇÇ => hœÄ.2 (le_iff_imp.1 h.2.1 h‚ÇÇ),
    fun hD => (hœÄ.3 (le_iff_imp.1 h.2.2 hD)).trans hc,
    fun hD => (hœÄ.4 (le_iff_imp.1 h.2.2 hD)).imp fun _ hœÄ => ‚ü®hœÄ.1, hœÄ.2.trans hc‚ü©‚ü©
#align box_integral.integration_params.mem_base_set.mono' BoxIntegral.IntegrationParams.MemBaseSet.mono'

@[mono]
theorem MemBaseSet.mono (I : Box Œπ) (h : l‚ÇÅ ‚â§ l‚ÇÇ) (hc : c‚ÇÅ ‚â§ c‚ÇÇ) {œÄ : TaggedPrepartition I}
    (hr : ‚àÄ x ‚àà Box.Icc I, r‚ÇÅ x ‚â§ r‚ÇÇ x) (hœÄ : l‚ÇÅ.MemBaseSet I c‚ÇÅ r‚ÇÅ œÄ) : l‚ÇÇ.MemBaseSet I c‚ÇÇ r‚ÇÇ œÄ :=
  hœÄ.mono' I h hc fun J _ => hr _ <| œÄ.tag_mem_Icc J
#align box_integral.integration_params.mem_base_set.mono BoxIntegral.IntegrationParams.MemBaseSet.mono

theorem MemBaseSet.exists_common_compl (h‚ÇÅ : l.MemBaseSet I c‚ÇÅ r‚ÇÅ œÄ‚ÇÅ) (h‚ÇÇ : l.MemBaseSet I c‚ÇÇ r‚ÇÇ œÄ‚ÇÇ)
    (hU : œÄ‚ÇÅ.iUnion = œÄ‚ÇÇ.iUnion) :
    ‚àÉ œÄ : Prepartition I, œÄ.iUnion = ‚ÜëI \ œÄ‚ÇÅ.iUnion ‚àß
      (l.bDistortion ‚Üí œÄ.distortion ‚â§ c‚ÇÅ) ‚àß (l.bDistortion ‚Üí œÄ.distortion ‚â§ c‚ÇÇ) := by
  wlog hc : c‚ÇÅ ‚â§ c‚ÇÇ with H
  ¬∑ simpa [hU, _root_.and_comm] using
      @H _ _ I J c c‚ÇÇ c‚ÇÅ r r‚ÇÇ r‚ÇÅ œÄ œÄ‚ÇÇ œÄ‚ÇÅ _ l‚ÇÇ l‚ÇÅ h‚ÇÇ h‚ÇÅ hU.symm (le_of_not_le hc)
  by_cases hD : (l.bDistortion : Prop)
  ¬∑ rcases h‚ÇÅ.4 hD with ‚ü®œÄ, hœÄU, hœÄc‚ü©
    exact ‚ü®œÄ, hœÄU, fun _ => hœÄc, fun _ => hœÄc.trans hc‚ü©
  ¬∑ exact ‚ü®œÄ‚ÇÅ.toPrepartition.compl, œÄ‚ÇÅ.toPrepartition.iUnion_compl,
      fun h => (hD h).elim, fun h => (hD h).elim‚ü©
#align box_integral.integration_params.mem_base_set.exists_common_compl BoxIntegral.IntegrationParams.MemBaseSet.exists_common_compl

protected theorem MemBaseSet.unionComplToSubordinate (hœÄ‚ÇÅ : l.MemBaseSet I c r‚ÇÅ œÄ‚ÇÅ)
    (hle : ‚àÄ x ‚àà Box.Icc I, r‚ÇÇ x ‚â§ r‚ÇÅ x) {œÄ‚ÇÇ : Prepartition I} (hU : œÄ‚ÇÇ.iUnion = ‚ÜëI \ œÄ‚ÇÅ.iUnion)
    (hc : l.bDistortion ‚Üí œÄ‚ÇÇ.distortion ‚â§ c) :
    l.MemBaseSet I c r‚ÇÅ (œÄ‚ÇÅ.unionComplToSubordinate œÄ‚ÇÇ hU r‚ÇÇ) :=
  ‚ü®hœÄ‚ÇÅ.1.disjUnion ((œÄ‚ÇÇ.isSubordinate_toSubordinate r‚ÇÇ).mono hle) _,
    fun h => (hœÄ‚ÇÅ.2 h).disjUnion (œÄ‚ÇÇ.isHenstock_toSubordinate _) _,
    fun h => (distortion_unionComplToSubordinate _ _ _ _).trans_le (max_le (hœÄ‚ÇÅ.3 h) (hc h)),
    fun _ => ‚ü®‚ä•, by simp‚ü©‚ü©
#align box_integral.integration_params.mem_base_set.union_compl_to_subordinate BoxIntegral.IntegrationParams.MemBaseSet.unionComplToSubordinate

protected theorem MemBaseSet.filter (hœÄ : l.MemBaseSet I c r œÄ) (p : Box Œπ ‚Üí Prop) :
    l.MemBaseSet I c r (œÄ.filter p) := by
  refine ‚ü®fun J hJ => hœÄ.1 J (œÄ.mem_filter.1 hJ).1, fun hH J hJ => hœÄ.2 hH J (œÄ.mem_filter.1 hJ).1,
    fun hD => (distortion_filter_le _ _).trans (hœÄ.3 hD), fun hD => ?_‚ü©
  rcases hœÄ.4 hD with ‚ü®œÄ‚ÇÅ, hœÄ‚ÇÅU, hc‚ü©
  set œÄ‚ÇÇ := œÄ.filter fun J => ¬¨p J
  have : Disjoint œÄ‚ÇÅ.iUnion œÄ‚ÇÇ.iUnion := by
    simpa [œÄ‚ÇÇ, hœÄ‚ÇÅU] using disjoint_sdiff_self_left.mono_right sdiff_le
  refine ‚ü®œÄ‚ÇÅ.disjUnion œÄ‚ÇÇ.toPrepartition this, ?_, ?_‚ü©
  ¬∑ suffices ‚ÜëI \ œÄ.iUnion ‚à™ œÄ.iUnion \ (œÄ.filter p).iUnion = ‚ÜëI \ (œÄ.filter p).iUnion by
      simp [œÄ‚ÇÇ, *]
    have h : (œÄ.filter p).iUnion ‚äÜ œÄ.iUnion :=
      biUnion_subset_biUnion_left (Finset.filter_subset _ _)
    ext x
    fconstructor
    ¬∑ rintro (‚ü®hxI, hxœÄ‚ü© | ‚ü®hxœÄ, hxp‚ü©)
      exacts [‚ü®hxI, mt (@h x) hxœÄ‚ü©, ‚ü®œÄ.iUnion_subset hxœÄ, hxp‚ü©]
    ¬∑ rintro ‚ü®hxI, hxp‚ü©
      by_cases hxœÄ : x ‚àà œÄ.iUnion
      exacts [Or.inr ‚ü®hxœÄ, hxp‚ü©, Or.inl ‚ü®hxI, hxœÄ‚ü©]
  ¬∑ have : (œÄ.filter fun J => ¬¨p J).distortion ‚â§ c := (distortion_filter_le _ _).trans (hœÄ.3 hD)
    simpa [hc]
#align box_integral.integration_params.mem_base_set.filter BoxIntegral.IntegrationParams.MemBaseSet.filter

theorem biUnionTagged_memBaseSet {œÄ : Prepartition I} {œÄi : ‚àÄ J, TaggedPrepartition J}
    (h : ‚àÄ J ‚àà œÄ, l.MemBaseSet J c r (œÄi J)) (hp : ‚àÄ J ‚àà œÄ, (œÄi J).IsPartition)
    (hc : l.bDistortion ‚Üí œÄ.compl.distortion ‚â§ c) : l.MemBaseSet I c r (œÄ.biUnionTagged œÄi) := by
  refine ‚ü®TaggedPrepartition.isSubordinate_biUnionTagged.2 fun J hJ => (h J hJ).1,
    fun hH => TaggedPrepartition.isHenstock_biUnionTagged.2 fun J hJ => (h J hJ).2 hH,
    fun hD => ?_, fun hD => ?_‚ü©
  ¬∑ rw [Prepartition.distortion_biUnionTagged, Finset.sup_le_iff]
    exact fun J hJ => (h J hJ).3 hD
  ¬∑ refine ‚ü®_, ?_, hc hD‚ü©
    rw [œÄ.iUnion_compl, ‚Üê œÄ.iUnion_biUnion_partition hp]
    rfl
#align box_integral.integration_params.bUnion_tagged_mem_base_set BoxIntegral.IntegrationParams.biUnionTagged_memBaseSet

@[mono]
theorem RCond.mono {Œπ : Type*} {r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)} (h : l‚ÇÅ ‚â§ l‚ÇÇ) (hr : l‚ÇÇ.RCond r) :
    l‚ÇÅ.RCond r :=
  fun hR => hr (le_iff_imp.1 h.1 hR)
#align box_integral.integration_params.r_cond.mono BoxIntegral.IntegrationParams.RCond.mono

nonrec theorem RCond.min {Œπ : Type*} {r‚ÇÅ r‚ÇÇ : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)} (h‚ÇÅ : l.RCond r‚ÇÅ)
    (h‚ÇÇ : l.RCond r‚ÇÇ) : l.RCond fun x => min (r‚ÇÅ x) (r‚ÇÇ x) :=
  fun hR x => congr_arg‚ÇÇ min (h‚ÇÅ hR x) (h‚ÇÇ hR x)
#align box_integral.integration_params.r_cond.min BoxIntegral.IntegrationParams.RCond.min

@[mono]
theorem toFilterDistortion_mono (I : Box Œπ) (h : l‚ÇÅ ‚â§ l‚ÇÇ) (hc : c‚ÇÅ ‚â§ c‚ÇÇ) :
    l‚ÇÅ.toFilterDistortion I c‚ÇÅ ‚â§ l‚ÇÇ.toFilterDistortion I c‚ÇÇ :=
  iInf_mono fun _ =>
    iInf_mono' fun hr =>
      ‚ü®hr.mono h, principal_mono.2 fun _ => MemBaseSet.mono I h hc fun _ _ => le_rfl‚ü©
#align box_integral.integration_params.to_filter_distortion_mono BoxIntegral.IntegrationParams.toFilterDistortion_mono

@[mono]
theorem toFilter_mono (I : Box Œπ) {l‚ÇÅ l‚ÇÇ : IntegrationParams} (h : l‚ÇÅ ‚â§ l‚ÇÇ) :
    l‚ÇÅ.toFilter I ‚â§ l‚ÇÇ.toFilter I :=
  iSup_mono fun _ => toFilterDistortion_mono I h le_rfl
#align box_integral.integration_params.to_filter_mono BoxIntegral.IntegrationParams.toFilter_mono

@[mono]
theorem toFilteriUnion_mono (I : Box Œπ) {l‚ÇÅ l‚ÇÇ : IntegrationParams} (h : l‚ÇÅ ‚â§ l‚ÇÇ)
    (œÄ‚ÇÄ : Prepartition I) : l‚ÇÅ.toFilteriUnion I œÄ‚ÇÄ ‚â§ l‚ÇÇ.toFilteriUnion I œÄ‚ÇÄ :=
  iSup_mono fun _ => inf_le_inf_right _ <| toFilterDistortion_mono _ h le_rfl
#align box_integral.integration_params.to_filter_Union_mono BoxIntegral.IntegrationParams.toFilteriUnion_mono

theorem toFilteriUnion_congr (I : Box Œπ) (l : IntegrationParams) {œÄ‚ÇÅ œÄ‚ÇÇ : Prepartition I}
    (h : œÄ‚ÇÅ.iUnion = œÄ‚ÇÇ.iUnion) : l.toFilteriUnion I œÄ‚ÇÅ = l.toFilteriUnion I œÄ‚ÇÇ := by
  simp only [toFilteriUnion, toFilterDistortioniUnion, h]
#align box_integral.integration_params.to_filter_Union_congr BoxIntegral.IntegrationParams.toFilteriUnion_congr

theorem hasBasis_toFilterDistortion (l : IntegrationParams) (I : Box Œπ) (c : ‚Ñù‚â•0) :
    (l.toFilterDistortion I c).HasBasis l.RCond fun r => { œÄ | l.MemBaseSet I c r œÄ } :=
  hasBasis_biInf_principal'
    (fun _ hr‚ÇÅ _ hr‚ÇÇ =>
      ‚ü®_, hr‚ÇÅ.min hr‚ÇÇ, fun _ => MemBaseSet.mono _ le_rfl le_rfl fun _ _ => min_le_left _ _,
        fun _ => MemBaseSet.mono _ le_rfl le_rfl fun _ _ => min_le_right _ _‚ü©)
    ‚ü®fun _ => ‚ü®1, Set.mem_Ioi.2 zero_lt_one‚ü©, fun _ _ => rfl‚ü©
#align box_integral.integration_params.has_basis_to_filter_distortion BoxIntegral.IntegrationParams.hasBasis_toFilterDistortion

theorem hasBasis_toFilterDistortioniUnion (l : IntegrationParams) (I : Box Œπ) (c : ‚Ñù‚â•0)
    (œÄ‚ÇÄ : Prepartition I) :
    (l.toFilterDistortioniUnion I c œÄ‚ÇÄ).HasBasis l.RCond fun r =>
      { œÄ | l.MemBaseSet I c r œÄ ‚àß œÄ.iUnion = œÄ‚ÇÄ.iUnion } :=
  (l.hasBasis_toFilterDistortion I c).inf_principal _
#align box_integral.integration_params.has_basis_to_filter_distortion_Union BoxIntegral.IntegrationParams.hasBasis_toFilterDistortioniUnion

theorem hasBasis_toFilteriUnion (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I) :
    (l.toFilteriUnion I œÄ‚ÇÄ).HasBasis (fun r : ‚Ñù‚â•0 ‚Üí (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù) => ‚àÄ c, l.RCond (r c))
      fun r => { œÄ | ‚àÉ c, l.MemBaseSet I c (r c) œÄ ‚àß œÄ.iUnion = œÄ‚ÇÄ.iUnion } := by
  have := fun c => l.hasBasis_toFilterDistortioniUnion I c œÄ‚ÇÄ
  simpa only [setOf_and, setOf_exists] using hasBasis_iSup this
#align box_integral.integration_params.has_basis_to_filter_Union BoxIntegral.IntegrationParams.hasBasis_toFilteriUnion

theorem hasBasis_toFilteriUnion_top (l : IntegrationParams) (I : Box Œπ) :
    (l.toFilteriUnion I ‚ä§).HasBasis (fun r : ‚Ñù‚â•0 ‚Üí (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù) => ‚àÄ c, l.RCond (r c))
      fun r => { œÄ | ‚àÉ c, l.MemBaseSet I c (r c) œÄ ‚àß œÄ.IsPartition } := by
  simpa only [TaggedPrepartition.isPartition_iff_iUnion_eq, Prepartition.iUnion_top] using
    l.hasBasis_toFilteriUnion I ‚ä§
#align box_integral.integration_params.has_basis_to_filter_Union_top BoxIntegral.IntegrationParams.hasBasis_toFilteriUnion_top

theorem hasBasis_toFilter (l : IntegrationParams) (I : Box Œπ) :
    (l.toFilter I).HasBasis (fun r : ‚Ñù‚â•0 ‚Üí (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù) => ‚àÄ c, l.RCond (r c))
      fun r => { œÄ | ‚àÉ c, l.MemBaseSet I c (r c) œÄ } := by
  simpa only [setOf_exists] using hasBasis_iSup (l.hasBasis_toFilterDistortion I)
#align box_integral.integration_params.has_basis_to_filter BoxIntegral.IntegrationParams.hasBasis_toFilter

theorem tendsto_embedBox_toFilteriUnion_top (l : IntegrationParams) (h : I ‚â§ J) :
    Tendsto (TaggedPrepartition.embedBox I J h) (l.toFilteriUnion I ‚ä§)
      (l.toFilteriUnion J (Prepartition.single J I h)) := by
  simp only [toFilteriUnion, tendsto_iSup]; intro c
  set œÄ‚ÇÄ := Prepartition.single J I h
  refine le_iSup_of_le (max c œÄ‚ÇÄ.compl.distortion) ?_
  refine ((l.hasBasis_toFilterDistortioniUnion I c ‚ä§).tendsto_iff
    (l.hasBasis_toFilterDistortioniUnion J _ _)).2 fun r hr => ?_
  refine ‚ü®r, hr, fun œÄ hœÄ => ?_‚ü©
  rw [mem_setOf_eq, Prepartition.iUnion_top] at hœÄ
  refine ‚ü®‚ü®hœÄ.1.1, hœÄ.1.2, fun hD => le_trans (hœÄ.1.3 hD) (le_max_left _ _), fun _ => ?_‚ü©, ?_‚ü©
  ¬∑ refine ‚ü®_, œÄ‚ÇÄ.iUnion_compl.trans ?_, le_max_right _ _‚ü©
    congr 1
    exact (Prepartition.iUnion_single h).trans hœÄ.2.symm
  ¬∑ exact hœÄ.2.trans (Prepartition.iUnion_single _).symm
#align box_integral.integration_params.tendsto_embed_box_to_filter_Union_top BoxIntegral.IntegrationParams.tendsto_embedBox_toFilteriUnion_top

theorem exists_memBaseSet_le_iUnion_eq (l : IntegrationParams) (œÄ‚ÇÄ : Prepartition I)
    (hc‚ÇÅ : œÄ‚ÇÄ.distortion ‚â§ c) (hc‚ÇÇ : œÄ‚ÇÄ.compl.distortion ‚â§ c) (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)) :
    ‚àÉ œÄ, l.MemBaseSet I c r œÄ ‚àß œÄ.toPrepartition ‚â§ œÄ‚ÇÄ ‚àß œÄ.iUnion = œÄ‚ÇÄ.iUnion := by
  rcases œÄ‚ÇÄ.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r with ‚ü®œÄ, hle, hH, hr, hd, hU‚ü©
  refine ‚ü®œÄ, ‚ü®hr, fun _ => hH, fun _ => hd.trans_le hc‚ÇÅ, fun _ => ‚ü®œÄ‚ÇÄ.compl, ?_, hc‚ÇÇ‚ü©‚ü©, ‚ü®hle, hU‚ü©‚ü©
  exact Prepartition.compl_congr hU ‚ñ∏ œÄ.toPrepartition.iUnion_compl
#align box_integral.integration_params.exists_mem_base_set_le_Union_eq BoxIntegral.IntegrationParams.exists_memBaseSet_le_iUnion_eq

theorem exists_memBaseSet_isPartition (l : IntegrationParams) (I : Box Œπ) (hc : I.distortion ‚â§ c)
    (r : (Œπ ‚Üí ‚Ñù) ‚Üí Ioi (0 : ‚Ñù)) : ‚àÉ œÄ, l.MemBaseSet I c r œÄ ‚àß œÄ.IsPartition := by
  rw [‚Üê Prepartition.distortion_top] at hc
  have hc' : (‚ä§ : Prepartition I).compl.distortion ‚â§ c := by simp
  simpa [isPartition_iff_iUnion_eq] using l.exists_memBaseSet_le_iUnion_eq ‚ä§ hc hc' r
#align box_integral.integration_params.exists_mem_base_set_is_partition BoxIntegral.IntegrationParams.exists_memBaseSet_isPartition

theorem toFilterDistortioniUnion_neBot (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I)
    (hc‚ÇÅ : œÄ‚ÇÄ.distortion ‚â§ c) (hc‚ÇÇ : œÄ‚ÇÄ.compl.distortion ‚â§ c) :
    (l.toFilterDistortioniUnion I c œÄ‚ÇÄ).NeBot :=
  ((l.hasBasis_toFilterDistortion I _).inf_principal _).neBot_iff.2
    fun {r} _ => (l.exists_memBaseSet_le_iUnion_eq œÄ‚ÇÄ hc‚ÇÅ hc‚ÇÇ r).imp fun _ hœÄ => ‚ü®hœÄ.1, hœÄ.2.2‚ü©
#align box_integral.integration_params.to_filter_distortion_Union_ne_bot BoxIntegral.IntegrationParams.toFilterDistortioniUnion_neBot

instance toFilterDistortioniUnion_neBot' (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I) :
    (l.toFilterDistortioniUnion I (max œÄ‚ÇÄ.distortion œÄ‚ÇÄ.compl.distortion) œÄ‚ÇÄ).NeBot :=
  l.toFilterDistortioniUnion_neBot I œÄ‚ÇÄ (le_max_left _ _) (le_max_right _ _)
#align box_integral.integration_params.to_filter_distortion_Union_ne_bot' BoxIntegral.IntegrationParams.toFilterDistortioniUnion_neBot'

instance toFilterDistortion_neBot (l : IntegrationParams) (I : Box Œπ) :
    (l.toFilterDistortion I I.distortion).NeBot := by
  simpa using (l.toFilterDistortioniUnion_neBot' I ‚ä§).mono inf_le_left
#align box_integral.integration_params.to_filter_distortion_ne_bot BoxIntegral.IntegrationParams.toFilterDistortion_neBot

instance toFilter_neBot (l : IntegrationParams) (I : Box Œπ) : (l.toFilter I).NeBot :=
  (l.toFilterDistortion_neBot I).mono <| le_iSup _ _
#align box_integral.integration_params.to_filter_ne_bot BoxIntegral.IntegrationParams.toFilter_neBot

instance toFilteriUnion_neBot (l : IntegrationParams) (I : Box Œπ) (œÄ‚ÇÄ : Prepartition I) :
    (l.toFilteriUnion I œÄ‚ÇÄ).NeBot :=
  (l.toFilterDistortioniUnion_neBot' I œÄ‚ÇÄ).mono <|
    le_iSup (fun c => l.toFilterDistortioniUnion I c œÄ‚ÇÄ) _
#align box_integral.integration_params.to_filter_Union_ne_bot BoxIntegral.IntegrationParams.toFilteriUnion_neBot

theorem eventually_isPartition (l : IntegrationParams) (I : Box Œπ) :
    ‚àÄ·∂† œÄ in l.toFilteriUnion I ‚ä§, TaggedPrepartition.IsPartition œÄ :=
  eventually_iSup.2 fun _ =>
    eventually_inf_principal.2 <|
      eventually_of_forall fun œÄ h =>
        œÄ.isPartition_iff_iUnion_eq.2 (h.trans Prepartition.iUnion_top)
#align box_integral.integration_params.eventually_is_partition BoxIntegral.IntegrationParams.eventually_isPartition

end IntegrationParams

end BoxIntegral
