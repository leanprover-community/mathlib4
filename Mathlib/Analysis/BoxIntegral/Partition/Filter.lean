/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Partition.SubboxInduction
import Mathlib.Analysis.BoxIntegral.Partition.Split

/-!
# Filters used in box-based integrals

First we define a structure `BoxIntegral.IntegrationParams`. This structure will be used as an
argument in the definition of `BoxIntegral.integral` in order to use the same definition for a few
well-known definitions of integrals based on partitions of a rectangular box into subboxes (Riemann
integral, Henstock-Kurzweil integral, and McShane integral).

This structure holds three boolean values (see below), and encodes eight different sets of
parameters; only four of these values are used somewhere in `mathlib4`. Three of them correspond to
the integration theories listed above, and one is a generalization of the one-dimensional
Henstock-Kurzweil integral such that the divergence theorem works without additional integrability
assumptions.

Finally, for each set of parameters `l : BoxIntegral.IntegrationParams` and a rectangular box
`I : BoxIntegral.Box Î¹`, we define several `Filter`s that will be used either in the definition of
the corresponding integral, or in the proofs of its properties. We equip
`BoxIntegral.IntegrationParams` with a `BoundedOrder` structure such that larger
`IntegrationParams` produce larger filters.

## Main definitions

### Integration parameters

The structure `BoxIntegral.IntegrationParams` has 3 boolean fields with the following meaning:

* `bRiemann`: the value `true` means that the filter corresponds to a Riemann-style integral, i.e.
  in the definition of integrability we require a constant upper estimate `r` on the size of boxes
  of a tagged partition; the value `false` means that the estimate may depend on the position of the
  tag.

* `bHenstock`: the value `true` means that we require that each tag belongs to its own closed box;
  the value `false` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `true` means that `r` can depend on the maximal ratio of sides of the
  same box of a partition. Presence of this case make quite a few proofs harder but we can prove the
  divergence theorem only for the filter `BoxIntegral.IntegrationParams.GP = âŠ¥ =
  {bRiemann := false, bHenstock := true, bDistortion := true}`.

### Well-known sets of parameters

Out of eight possible values of `BoxIntegral.IntegrationParams`, the following four are used in
the library.

* `BoxIntegral.IntegrationParams.Riemann` (`bRiemann = true`, `bHenstock = true`,
  `bDistortion = false`): this value corresponds to the Riemann integral; in the corresponding
  filter, we require that the diameters of all boxes `J` of a tagged partition are bounded from
  above by a constant upper estimate that may not depend on the geometry of `J`, and each tag
  belongs to the corresponding closed box.

* `BoxIntegral.IntegrationParams.Henstock` (`bRiemann = false`, `bHenstock = true`,
  `bDistortion = false`): this value corresponds to the most natural generalization of
  Henstock-Kurzweil integral to higher dimension; the only (but important!) difference between this
  theory and Riemann integral is that instead of a constant upper estimate on the size of all boxes
  of a partition, we require that the partition is *subordinate* to a possibly discontinuous
  function `r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}`, i.e. each box `J` is included in a closed ball with
  center `Ï€.tag J` and radius `r J`.

* `BoxIntegral.IntegrationParams.McShane` (`bRiemann = false`, `bHenstock = false`,
  `bDistortion = false`): this value corresponds to the McShane integral; the only difference with
  the Henstock integral is that we allow tags to be outside of their boxes; the tags still have to
  be in the ambient closed box, and the partition still has to be subordinate to a function.

* `BoxIntegral.IntegrationParams.GP = âŠ¥` (`bRiemann = false`, `bHenstock = true`,
  `bDistortion = true`): this is the least integration theory in our list, i.e., all functions
  integrable in any other theory is integrable in this one as well.  This is a non-standard
  generalization of the Henstock-Kurzweil integral to higher dimension.  In dimension one, it
  generates the same filter as `Henstock`. In higher dimension, this generalization defines an
  integration theory such that the divergence of any FrÃ©chet differentiable function `f` is
  integrable, and its integral is equal to the sum of integrals of `f` over the faces of the box,
  taken with appropriate signs.

  A function `f` is `GP`-integrable if for any `Îµ > 0` and `c : â„â‰¥0` there exists
  `r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}` such that for any tagged partition `Ï€` subordinate to `r`, if each
  tag belongs to the corresponding closed box and for each box `J âˆˆ Ï€`, the maximal ratio of its
  sides is less than or equal to `c`, then the integral sum of `f` over `Ï€` is `Îµ`-close to the
  integral.

### Filters and predicates on `TaggedPrepartition I`

For each value of `IntegrationParams` and a rectangular box `I`, we define a few filters on
`TaggedPrepartition I`. First, we define a predicate

```
structure BoxIntegral.IntegrationParams.MemBaseSet (l : BoxIntegral.IntegrationParams)
  (I : BoxIntegral.Box Î¹) (c : â„â‰¥0) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„))
  (Ï€ : BoxIntegral.TaggedPrepartition I) : Prop where
```

This predicate says that

* if `l.bHenstock`, then `Ï€` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `Ï€` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `Ï€` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `Ï€'` with distortion `â‰¤ c` that covers
  exactly `I \ Ï€.iUnion`.

The last condition is always true for `c > 1`, see TODO section for more details.

Then we define a predicate `BoxIntegral.IntegrationParams.RCond` on functions
`r : (Î¹ â†’ â„) â†’ {x : â„ | 0 < x}`. If `l.bRiemann`, then this predicate requires `r` to be a constant
function, otherwise it imposes no restrictions on `r`. We introduce this definition to prove a few
dot-notation lemmas: e.g., `BoxIntegral.IntegrationParams.RCond.min` says that the pointwise
minimum of two functions that satisfy this condition satisfies this condition as well.

Then we define four filters on `BoxIntegral.TaggedPrepartition I`.

* `BoxIntegral.IntegrationParams.toFilterDistortion`: an auxiliary filter that takes parameters
  `(l : BoxIntegral.IntegrationParams) (I : BoxIntegral.Box Î¹) (c : â„â‰¥0)` and returns the
  filter generated by all sets `{Ï€ | MemBaseSet l I c r Ï€}`, where `r` is a function satisfying
  the predicate `BoxIntegral.IntegrationParams.RCond l`;

* `BoxIntegral.IntegrationParams.toFilter l I`: the supremum of `l.toFilterDistortion I c`
  over all `c : â„â‰¥0`;

* `BoxIntegral.IntegrationParams.toFilterDistortioniUnion l I c Ï€â‚€`, where `Ï€â‚€` is a
  prepartition of `I`: the infimum of `l.toFilterDistortion I c` and the principal filter
  generated by `{Ï€ | Ï€.iUnion = Ï€â‚€.iUnion}`;

* `BoxIntegral.IntegrationParams.toFilteriUnion l I Ï€â‚€`: the supremum of
  `l.toFilterDistortioniUnion l I c Ï€â‚€` over all `c : â„â‰¥0`. This is the filter (in the case
  `Ï€â‚€ = âŠ¤` is the one-box partition of `I`) used in the definition of the integral of a function
  over a box.

## Implementation details

* Later we define the integral of a function over a rectangular box as the limit (if it exists) of
  the integral sums along `BoxIntegral.IntegrationParams.toFilteriUnion l I âŠ¤`. While it is
  possible to define the integral with a general filter on `BoxIntegral.TaggedPrepartition I` as a
  parameter, many lemmas (e.g., Sacks-Henstock lemma and most results about integrability of
  functions) require the filter to have a predictable structure. So, instead of adding assumptions
  about the filter here and there, we define this auxiliary type that can encode all integration
  theories we need in practice.

* While the definition of the integral only uses the filter
  `BoxIntegral.IntegrationParams.toFilteriUnion l I âŠ¤` and partitions of a box, some lemmas
  (e.g., the Henstock-Sacks lemmas) are best formulated in terms of the predicate `MemBaseSet` and
  other filters defined above.

* We use `Bool` instead of `Prop` for the fields of `IntegrationParams` in order to have decidable
  equality and inequalities.

## TODO

Currently, `BoxIntegral.IntegrationParams.MemBaseSet` explicitly requires that there exists a
partition of the complement `I \ Ï€.iUnion` with distortion `â‰¤ c`. For `c > 1`, this condition is
always true but the proof of this fact requires more API about
`BoxIntegral.Prepartition.splitMany`. We should formalize this fact, then either require `c > 1`
everywhere, or replace `â‰¤ c` with `< c` so that we automatically get `c > 1` for a non-trivial
prepartition (and consider the special case `Ï€ = âŠ¥` separately if needed).

## Tags

integral, rectangular box, partition, filter
-/

open Set Function Filter Metric Finset Bool
open scoped Topology Filter NNReal

noncomputable section

namespace BoxIntegral

variable {Î¹ : Type*} [Fintype Î¹] {I J : Box Î¹} {c câ‚ câ‚‚ : â„â‰¥0}

open TaggedPrepartition

/-- An `IntegrationParams` is a structure holding 3 boolean values used to define a filter to be
used in the definition of a box-integrable function.

* `bRiemann`: the value `true` means that the filter corresponds to a Riemann-style integral, i.e.
  in the definition of integrability we require a constant upper estimate `r` on the size of boxes
  of a tagged partition; the value `false` means that the estimate may depend on the position of the
  tag.

* `bHenstock`: the value `true` means that we require that each tag belongs to its own closed box;
  the value `false` means that we only require that tags belong to the ambient box.

* `bDistortion`: the value `true` means that `r` can depend on the maximal ratio of sides of the
  same box of a partition. Presence of this case makes quite a few proofs harder but we can prove
  the divergence theorem only for the filter `BoxIntegral.IntegrationParams.GP = âŠ¥ =
  {bRiemann := false, bHenstock := true, bDistortion := true}`.
-/
@[ext]
structure IntegrationParams : Type where
  (bRiemann bHenstock bDistortion : Bool)

variable {l lâ‚ lâ‚‚ : IntegrationParams}

namespace IntegrationParams

/-- Auxiliary equivalence with a product type used to lift an order. -/
def equivProd : IntegrationParams â‰ƒ Bool Ã— Booláµ’áµˆ Ã— Booláµ’áµˆ where
  toFun l := âŸ¨l.1, OrderDual.toDual l.2, OrderDual.toDual l.3âŸ©
  invFun l := âŸ¨l.1, OrderDual.ofDual l.2.1, OrderDual.ofDual l.2.2âŸ©

instance : PartialOrder IntegrationParams :=
  PartialOrder.lift equivProd equivProd.injective

/-- Auxiliary `OrderIso` with a product type used to lift a `BoundedOrder` structure. -/
def isoProd : IntegrationParams â‰ƒo Bool Ã— Booláµ’áµˆ Ã— Booláµ’áµˆ :=
  âŸ¨equivProd, Iff.rflâŸ©

instance : BoundedOrder IntegrationParams :=
  isoProd.symm.toGaloisInsertion.liftBoundedOrder

/-- The value `BoxIntegral.IntegrationParams.GP = âŠ¥`
(`bRiemann = false`, `bHenstock = true`, `bDistortion = true`)
corresponds to a generalization of the Henstock integral such that the Divergence theorem holds true
without additional integrability assumptions, see the module docstring for details. -/
instance : Inhabited IntegrationParams :=
  âŸ¨âŠ¥âŸ©

instance : DecidableLE (IntegrationParams) :=
  fun _ _ â†¦ inferInstanceAs (Decidable (_ âˆ§ _))

instance : DecidableEq IntegrationParams :=
  fun _ _ â†¦ decidable_of_iff _ IntegrationParams.ext_iff.symm

/-- The `BoxIntegral.IntegrationParams` corresponding to the Riemann integral. In the
corresponding filter, we require that the diameters of all boxes `J` of a tagged partition are
bounded from above by a constant upper estimate that may not depend on the geometry of `J`, and each
tag belongs to the corresponding closed box. -/
def Riemann : IntegrationParams where
  bRiemann := true
  bHenstock := true
  bDistortion := false

/-- The `BoxIntegral.IntegrationParams` corresponding to the Henstock-Kurzweil integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r` and each tag belongs to the corresponding closed box. -/
def Henstock : IntegrationParams :=
  âŸ¨false, true, falseâŸ©

/-- The `BoxIntegral.IntegrationParams` corresponding to the McShane integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r`; the tags may be outside of the corresponding closed box
(but still inside the ambient closed box `I.Icc`). -/
def McShane : IntegrationParams :=
  âŸ¨false, false, falseâŸ©

/-- The `BoxIntegral.IntegrationParams` corresponding to the generalized Perron integral. In the
corresponding filter, we require that the tagged partition is subordinate to a (possibly,
discontinuous) positive function `r` and each tag belongs to the corresponding closed box. We also
require an upper estimate on the distortion of all boxes of the partition. -/
def GP : IntegrationParams := âŠ¥

theorem henstock_le_riemann : Henstock â‰¤ Riemann := by trivial

theorem henstock_le_mcShane : Henstock â‰¤ McShane := by trivial

theorem gp_le : GP â‰¤ l :=
  bot_le

/-- The predicate corresponding to a base set of the filter defined by an
`IntegrationParams`. It says that

* if `l.bHenstock`, then `Ï€` is a Henstock prepartition, i.e. each tag belongs to the corresponding
  closed box;
* `Ï€` is subordinate to `r`;
* if `l.bDistortion`, then the distortion of each box in `Ï€` is less than or equal to `c`;
* if `l.bDistortion`, then there exists a prepartition `Ï€'` with distortion `â‰¤ c` that covers
  exactly `I \ Ï€.iUnion`.

The last condition is automatically verified for partitions, and is used in the proof of the
Sacks-Henstock inequality to compare two prepartitions covering the same part of the box.

It is also automatically satisfied for any `c > 1`, see TODO section of the module docstring for
details. -/
structure MemBaseSet (l : IntegrationParams) (I : Box Î¹) (c : â„â‰¥0) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„))
    (Ï€ : TaggedPrepartition I) : Prop where
  protected isSubordinate : Ï€.IsSubordinate r
  protected isHenstock : l.bHenstock â†’ Ï€.IsHenstock
  protected distortion_le : l.bDistortion â†’ Ï€.distortion â‰¤ c
  protected exists_compl : l.bDistortion â†’ âˆƒ Ï€' : Prepartition I,
    Ï€'.iUnion = â†‘I \ Ï€.iUnion âˆ§ Ï€'.distortion â‰¤ c

/-- A predicate saying that in case `l.bRiemann = true`, the function `r` is a constant. -/
def RCond {Î¹ : Type*} (l : IntegrationParams) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : Prop :=
  l.bRiemann â†’ âˆ€ x, r x = r 0

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilterDistortion I c` if there exists
a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = true`) such that `s` contains each
prepartition `Ï€` such that `l.MemBaseSet I c r Ï€`. -/
def toFilterDistortion (l : IntegrationParams) (I : Box Î¹) (c : â„â‰¥0) :
    Filter (TaggedPrepartition I) :=
  â¨… (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) (_ : l.RCond r), ğ“Ÿ { Ï€ | l.MemBaseSet I c r Ï€ }

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilter I` if for any `c : â„â‰¥0` there
exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = true`) such that
`s` contains each prepartition `Ï€` such that `l.MemBaseSet I c r Ï€`. -/
def toFilter (l : IntegrationParams) (I : Box Î¹) : Filter (TaggedPrepartition I) :=
  â¨† c : â„â‰¥0, l.toFilterDistortion I c

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilterDistortioniUnion I c Ï€â‚€` if
there exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = true`) such that `s`
contains each prepartition `Ï€` such that `l.MemBaseSet I c r Ï€` and `Ï€.iUnion = Ï€â‚€.iUnion`. -/
def toFilterDistortioniUnion (l : IntegrationParams) (I : Box Î¹) (c : â„â‰¥0) (Ï€â‚€ : Prepartition I) :=
  l.toFilterDistortion I c âŠ“ ğ“Ÿ { Ï€ | Ï€.iUnion = Ï€â‚€.iUnion }

/-- A set `s : Set (TaggedPrepartition I)` belongs to `l.toFilteriUnion I Ï€â‚€` if for any `c : â„â‰¥0`
there exists a function `r : â„â¿ â†’ (0, âˆ)` (or a constant `r` if `l.bRiemann = true`) such that `s`
contains each prepartition `Ï€` such that `l.MemBaseSet I c r Ï€` and `Ï€.iUnion = Ï€â‚€.iUnion`. -/
def toFilteriUnion (I : Box Î¹) (Ï€â‚€ : Prepartition I) :=
  â¨† c : â„â‰¥0, l.toFilterDistortioniUnion I c Ï€â‚€

theorem rCond_of_bRiemann_eq_false {Î¹} (l : IntegrationParams) (hl : l.bRiemann = false)
    {r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} : l.RCond r := by
  simp [RCond, hl]

theorem toFilter_inf_iUnion_eq (l : IntegrationParams) (I : Box Î¹) (Ï€â‚€ : Prepartition I) :
    l.toFilter I âŠ“ ğ“Ÿ { Ï€ | Ï€.iUnion = Ï€â‚€.iUnion } = l.toFilteriUnion I Ï€â‚€ :=
  (iSup_inf_principal _ _).symm

variable {râ‚ râ‚‚ : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} {Ï€ Ï€â‚ Ï€â‚‚ : TaggedPrepartition I}

variable (I) in
theorem MemBaseSet.mono' (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚)
    (hr : âˆ€ J âˆˆ Ï€, râ‚ (Ï€.tag J) â‰¤ râ‚‚ (Ï€.tag J)) (hÏ€ : lâ‚.MemBaseSet I câ‚ râ‚ Ï€) :
    lâ‚‚.MemBaseSet I câ‚‚ râ‚‚ Ï€ :=
  âŸ¨hÏ€.1.mono' hr, fun hâ‚‚ â†¦ hÏ€.2 (le_iff_imp.1 h.2.1 hâ‚‚),
    fun hD â†¦ (hÏ€.3 (le_iff_imp.1 h.2.2 hD)).trans hc,
    fun hD â†¦ (hÏ€.4 (le_iff_imp.1 h.2.2 hD)).imp fun _ hÏ€ â†¦ âŸ¨hÏ€.1, hÏ€.2.trans hcâŸ©âŸ©

variable (I) in
@[mono]
theorem MemBaseSet.mono (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚)
    (hr : âˆ€ x âˆˆ Box.Icc I, râ‚ x â‰¤ râ‚‚ x) (hÏ€ : lâ‚.MemBaseSet I câ‚ râ‚ Ï€) : lâ‚‚.MemBaseSet I câ‚‚ râ‚‚ Ï€ :=
  hÏ€.mono' I h hc fun J _ â†¦ hr _ <| Ï€.tag_mem_Icc J

theorem MemBaseSet.exists_common_compl
    (hâ‚ : l.MemBaseSet I câ‚ râ‚ Ï€â‚) (hâ‚‚ : l.MemBaseSet I câ‚‚ râ‚‚ Ï€â‚‚)
    (hU : Ï€â‚.iUnion = Ï€â‚‚.iUnion) :
    âˆƒ Ï€ : Prepartition I, Ï€.iUnion = â†‘I \ Ï€â‚.iUnion âˆ§
      (l.bDistortion â†’ Ï€.distortion â‰¤ câ‚) âˆ§ (l.bDistortion â†’ Ï€.distortion â‰¤ câ‚‚) := by
  wlog hc : câ‚ â‰¤ câ‚‚ with H
  Â· simpa [hU, _root_.and_comm] using
      @H _ _ I câ‚‚ câ‚ l râ‚‚ râ‚ Ï€â‚‚ Ï€â‚ hâ‚‚ hâ‚ hU.symm (le_of_not_ge hc)
  by_cases hD : (l.bDistortion : Prop)
  Â· rcases hâ‚.4 hD with âŸ¨Ï€, hÏ€U, hÏ€câŸ©
    exact âŸ¨Ï€, hÏ€U, fun _ â†¦ hÏ€c, fun _ â†¦ hÏ€c.trans hcâŸ©
  Â· exact âŸ¨Ï€â‚.toPrepartition.compl, Ï€â‚.toPrepartition.iUnion_compl,
      fun h â†¦ (hD h).elim, fun h â†¦ (hD h).elimâŸ©

protected theorem MemBaseSet.unionComplToSubordinate (hÏ€â‚ : l.MemBaseSet I c râ‚ Ï€â‚)
    (hle : âˆ€ x âˆˆ Box.Icc I, râ‚‚ x â‰¤ râ‚ x) {Ï€â‚‚ : Prepartition I} (hU : Ï€â‚‚.iUnion = â†‘I \ Ï€â‚.iUnion)
    (hc : l.bDistortion â†’ Ï€â‚‚.distortion â‰¤ c) :
    l.MemBaseSet I c râ‚ (Ï€â‚.unionComplToSubordinate Ï€â‚‚ hU râ‚‚) :=
  âŸ¨hÏ€â‚.1.disjUnion ((Ï€â‚‚.isSubordinate_toSubordinate râ‚‚).mono hle) _,
    fun h â†¦ (hÏ€â‚.2 h).disjUnion (Ï€â‚‚.isHenstock_toSubordinate _) _,
    fun h â†¦ (distortion_unionComplToSubordinate _ _ _ _).trans_le (max_le (hÏ€â‚.3 h) (hc h)),
    fun _ â†¦ âŸ¨âŠ¥, by simpâŸ©âŸ©

variable {r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)}

protected theorem MemBaseSet.filter (hÏ€ : l.MemBaseSet I c r Ï€) (p : Box Î¹ â†’ Prop) :
    l.MemBaseSet I c r (Ï€.filter p) := by
  classical
  refine âŸ¨fun J hJ â†¦ hÏ€.1 J (Ï€.mem_filter.1 hJ).1, fun hH J hJ â†¦ hÏ€.2 hH J (Ï€.mem_filter.1 hJ).1,
    fun hD â†¦ (distortion_filter_le _ _).trans (hÏ€.3 hD), fun hD â†¦ ?_âŸ©
  rcases hÏ€.4 hD with âŸ¨Ï€â‚, hÏ€â‚U, hcâŸ©
  set Ï€â‚‚ := Ï€.filter fun J â†¦ Â¬p J
  have : Disjoint Ï€â‚.iUnion Ï€â‚‚.iUnion := by
    simpa [Ï€â‚‚, hÏ€â‚U] using disjoint_sdiff_self_left.mono_right sdiff_le
  refine âŸ¨Ï€â‚.disjUnion Ï€â‚‚.toPrepartition this, ?_, ?_âŸ©
  Â· suffices â†‘I \ Ï€.iUnion âˆª Ï€.iUnion \ (Ï€.filter p).iUnion = â†‘I \ (Ï€.filter p).iUnion by
      simp [Ï€â‚‚, *]
    have h : (Ï€.filter p).iUnion âŠ† Ï€.iUnion :=
      biUnion_subset_biUnion_left (Finset.filter_subset _ _)
    ext x
    fconstructor
    Â· rintro (âŸ¨hxI, hxÏ€âŸ© | âŸ¨hxÏ€, hxpâŸ©)
      exacts [âŸ¨hxI, mt (@h x) hxÏ€âŸ©, âŸ¨Ï€.iUnion_subset hxÏ€, hxpâŸ©]
    Â· rintro âŸ¨hxI, hxpâŸ©
      by_cases hxÏ€ : x âˆˆ Ï€.iUnion
      exacts [Or.inr âŸ¨hxÏ€, hxpâŸ©, Or.inl âŸ¨hxI, hxÏ€âŸ©]
  Â· have : (Ï€.filter fun J â†¦ Â¬p J).distortion â‰¤ c := (distortion_filter_le _ _).trans (hÏ€.3 hD)
    simpa [hc]

theorem biUnionTagged_memBaseSet {Ï€ : Prepartition I} {Ï€i : âˆ€ J, TaggedPrepartition J}
    (h : âˆ€ J âˆˆ Ï€, l.MemBaseSet J c r (Ï€i J)) (hp : âˆ€ J âˆˆ Ï€, (Ï€i J).IsPartition)
    (hc : l.bDistortion â†’ Ï€.compl.distortion â‰¤ c) : l.MemBaseSet I c r (Ï€.biUnionTagged Ï€i) := by
  refine âŸ¨TaggedPrepartition.isSubordinate_biUnionTagged.2 fun J hJ â†¦ (h J hJ).1,
    fun hH â†¦ TaggedPrepartition.isHenstock_biUnionTagged.2 fun J hJ â†¦ (h J hJ).2 hH,
    fun hD â†¦ ?_, fun hD â†¦ ?_âŸ©
  Â· rw [Prepartition.distortion_biUnionTagged, Finset.sup_le_iff]
    exact fun J hJ â†¦ (h J hJ).3 hD
  Â· refine âŸ¨_, ?_, hc hDâŸ©
    rw [Ï€.iUnion_compl, â† Ï€.iUnion_biUnion_partition hp]
    rfl

@[mono]
theorem RCond.mono {Î¹ : Type*} {r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} (h : lâ‚ â‰¤ lâ‚‚) (hr : lâ‚‚.RCond r) :
    lâ‚.RCond r :=
  fun hR â†¦ hr (le_iff_imp.1 h.1 hR)

nonrec theorem RCond.min {Î¹ : Type*} {râ‚ râ‚‚ : (Î¹ â†’ â„) â†’ Ioi (0 : â„)} (hâ‚ : l.RCond râ‚)
    (hâ‚‚ : l.RCond râ‚‚) : l.RCond fun x â†¦ min (râ‚ x) (râ‚‚ x) :=
  fun hR x â†¦ congr_argâ‚‚ min (hâ‚ hR x) (hâ‚‚ hR x)

@[gcongr, mono]
theorem toFilterDistortion_mono (I : Box Î¹) (h : lâ‚ â‰¤ lâ‚‚) (hc : câ‚ â‰¤ câ‚‚) :
    lâ‚.toFilterDistortion I câ‚ â‰¤ lâ‚‚.toFilterDistortion I câ‚‚ :=
  iInf_mono fun _ â†¦
    iInf_mono' fun hr â†¦
      âŸ¨hr.mono h, principal_mono.2 fun _ â†¦ MemBaseSet.mono I h hc fun _ _ â†¦ le_rflâŸ©

@[gcongr, mono]
theorem toFilter_mono (I : Box Î¹) {lâ‚ lâ‚‚ : IntegrationParams} (h : lâ‚ â‰¤ lâ‚‚) :
    lâ‚.toFilter I â‰¤ lâ‚‚.toFilter I :=
  iSup_mono fun _ â†¦ toFilterDistortion_mono I h le_rfl

@[gcongr, mono]
theorem toFilteriUnion_mono (I : Box Î¹) {lâ‚ lâ‚‚ : IntegrationParams} (h : lâ‚ â‰¤ lâ‚‚)
    (Ï€â‚€ : Prepartition I) : lâ‚.toFilteriUnion I Ï€â‚€ â‰¤ lâ‚‚.toFilteriUnion I Ï€â‚€ :=
  iSup_mono fun _ â†¦ inf_le_inf_right _ <| toFilterDistortion_mono _ h le_rfl

theorem toFilteriUnion_congr (I : Box Î¹) (l : IntegrationParams) {Ï€â‚ Ï€â‚‚ : Prepartition I}
    (h : Ï€â‚.iUnion = Ï€â‚‚.iUnion) : l.toFilteriUnion I Ï€â‚ = l.toFilteriUnion I Ï€â‚‚ := by
  simp only [toFilteriUnion, toFilterDistortioniUnion, h]

theorem hasBasis_toFilterDistortion (l : IntegrationParams) (I : Box Î¹) (c : â„â‰¥0) :
    (l.toFilterDistortion I c).HasBasis l.RCond fun r â†¦ { Ï€ | l.MemBaseSet I c r Ï€ } :=
  hasBasis_biInf_principal'
    (fun _ hrâ‚ _ hrâ‚‚ â†¦
      âŸ¨_, hrâ‚.min hrâ‚‚, fun _ â†¦ MemBaseSet.mono _ le_rfl le_rfl fun _ _ â†¦ min_le_left _ _,
        fun _ â†¦ MemBaseSet.mono _ le_rfl le_rfl fun _ _ â†¦ min_le_right _ _âŸ©)
    âŸ¨fun _ â†¦ âŸ¨1, Set.mem_Ioi.2 zero_lt_oneâŸ©, fun _ _ â†¦ rflâŸ©

theorem hasBasis_toFilterDistortioniUnion (l : IntegrationParams) (I : Box Î¹) (c : â„â‰¥0)
    (Ï€â‚€ : Prepartition I) :
    (l.toFilterDistortioniUnion I c Ï€â‚€).HasBasis l.RCond fun r â†¦
      { Ï€ | l.MemBaseSet I c r Ï€ âˆ§ Ï€.iUnion = Ï€â‚€.iUnion } :=
  (l.hasBasis_toFilterDistortion I c).inf_principal _

theorem hasBasis_toFilteriUnion (l : IntegrationParams) (I : Box Î¹) (Ï€â‚€ : Prepartition I) :
    (l.toFilteriUnion I Ï€â‚€).HasBasis (fun r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„) â†¦ âˆ€ c, l.RCond (r c))
      fun r â†¦ { Ï€ | âˆƒ c, l.MemBaseSet I c (r c) Ï€ âˆ§ Ï€.iUnion = Ï€â‚€.iUnion } := by
  have := fun c â†¦ l.hasBasis_toFilterDistortioniUnion I c Ï€â‚€
  simpa only [setOf_and, setOf_exists] using hasBasis_iSup this

theorem hasBasis_toFilteriUnion_top (l : IntegrationParams) (I : Box Î¹) :
    (l.toFilteriUnion I âŠ¤).HasBasis (fun r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„) â†¦ âˆ€ c, l.RCond (r c))
      fun r â†¦ { Ï€ | âˆƒ c, l.MemBaseSet I c (r c) Ï€ âˆ§ Ï€.IsPartition } := by
  simpa only [TaggedPrepartition.isPartition_iff_iUnion_eq, Prepartition.iUnion_top] using
    l.hasBasis_toFilteriUnion I âŠ¤

theorem hasBasis_toFilter (l : IntegrationParams) (I : Box Î¹) :
    (l.toFilter I).HasBasis (fun r : â„â‰¥0 â†’ (Î¹ â†’ â„) â†’ Ioi (0 : â„) â†¦ âˆ€ c, l.RCond (r c))
      fun r â†¦ { Ï€ | âˆƒ c, l.MemBaseSet I c (r c) Ï€ } := by
  simpa only [setOf_exists] using hasBasis_iSup (l.hasBasis_toFilterDistortion I)

theorem tendsto_embedBox_toFilteriUnion_top (l : IntegrationParams) (h : I â‰¤ J) :
    Tendsto (TaggedPrepartition.embedBox I J h) (l.toFilteriUnion I âŠ¤)
      (l.toFilteriUnion J (Prepartition.single J I h)) := by
  simp only [toFilteriUnion, tendsto_iSup]; intro c
  set Ï€â‚€ := Prepartition.single J I h
  refine le_iSup_of_le (max c Ï€â‚€.compl.distortion) ?_
  refine ((l.hasBasis_toFilterDistortioniUnion I c âŠ¤).tendsto_iff
    (l.hasBasis_toFilterDistortioniUnion J _ _)).2 fun r hr â†¦ ?_
  refine âŸ¨r, hr, fun Ï€ hÏ€ â†¦ ?_âŸ©
  rw [mem_setOf_eq, Prepartition.iUnion_top] at hÏ€
  refine âŸ¨âŸ¨hÏ€.1.1, hÏ€.1.2, fun hD â†¦ le_trans (hÏ€.1.3 hD) (le_max_left _ _), fun _ â†¦ ?_âŸ©, ?_âŸ©
  Â· refine âŸ¨_, Ï€â‚€.iUnion_compl.trans ?_, le_max_right _ _âŸ©
    congr 1
    exact (Prepartition.iUnion_single h).trans hÏ€.2.symm
  Â· exact hÏ€.2.trans (Prepartition.iUnion_single _).symm

theorem exists_memBaseSet_le_iUnion_eq (l : IntegrationParams) (Ï€â‚€ : Prepartition I)
    (hcâ‚ : Ï€â‚€.distortion â‰¤ c) (hcâ‚‚ : Ï€â‚€.compl.distortion â‰¤ c) (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) :
    âˆƒ Ï€, l.MemBaseSet I c r Ï€ âˆ§ Ï€.toPrepartition â‰¤ Ï€â‚€ âˆ§ Ï€.iUnion = Ï€â‚€.iUnion := by
  rcases Ï€â‚€.exists_tagged_le_isHenstock_isSubordinate_iUnion_eq r with âŸ¨Ï€, hle, hH, hr, hd, hUâŸ©
  refine âŸ¨Ï€, âŸ¨hr, fun _ â†¦ hH, fun _ â†¦ hd.trans_le hcâ‚, fun _ â†¦ âŸ¨Ï€â‚€.compl, ?_, hcâ‚‚âŸ©âŸ©, âŸ¨hle, hUâŸ©âŸ©
  exact Prepartition.compl_congr hU â–¸ Ï€.toPrepartition.iUnion_compl

theorem exists_memBaseSet_isPartition (l : IntegrationParams) (I : Box Î¹) (hc : I.distortion â‰¤ c)
    (r : (Î¹ â†’ â„) â†’ Ioi (0 : â„)) : âˆƒ Ï€, l.MemBaseSet I c r Ï€ âˆ§ Ï€.IsPartition := by
  rw [â† Prepartition.distortion_top] at hc
  have hc' : (âŠ¤ : Prepartition I).compl.distortion â‰¤ c := by simp
  simpa [isPartition_iff_iUnion_eq] using l.exists_memBaseSet_le_iUnion_eq âŠ¤ hc hc' r

theorem toFilterDistortioniUnion_neBot (l : IntegrationParams) (I : Box Î¹) (Ï€â‚€ : Prepartition I)
    (hcâ‚ : Ï€â‚€.distortion â‰¤ c) (hcâ‚‚ : Ï€â‚€.compl.distortion â‰¤ c) :
    (l.toFilterDistortioniUnion I c Ï€â‚€).NeBot :=
  ((l.hasBasis_toFilterDistortion I _).inf_principal _).neBot_iff.2
    fun {r} _ â†¦ (l.exists_memBaseSet_le_iUnion_eq Ï€â‚€ hcâ‚ hcâ‚‚ r).imp fun _ hÏ€ â†¦ âŸ¨hÏ€.1, hÏ€.2.2âŸ©

instance toFilterDistortioniUnion_neBot' (l : IntegrationParams) (I : Box Î¹) (Ï€â‚€ : Prepartition I) :
    (l.toFilterDistortioniUnion I (max Ï€â‚€.distortion Ï€â‚€.compl.distortion) Ï€â‚€).NeBot :=
  l.toFilterDistortioniUnion_neBot I Ï€â‚€ (le_max_left _ _) (le_max_right _ _)

instance toFilterDistortion_neBot (l : IntegrationParams) (I : Box Î¹) :
    (l.toFilterDistortion I I.distortion).NeBot := by
  simpa using (l.toFilterDistortioniUnion_neBot' I âŠ¤).mono inf_le_left

instance toFilter_neBot (l : IntegrationParams) (I : Box Î¹) : (l.toFilter I).NeBot :=
  (l.toFilterDistortion_neBot I).mono <| le_iSup _ _

instance toFilteriUnion_neBot (l : IntegrationParams) (I : Box Î¹) (Ï€â‚€ : Prepartition I) :
    (l.toFilteriUnion I Ï€â‚€).NeBot :=
  (l.toFilterDistortioniUnion_neBot' I Ï€â‚€).mono <|
    le_iSup (fun c â†¦ l.toFilterDistortioniUnion I c Ï€â‚€) _

theorem eventually_isPartition (l : IntegrationParams) (I : Box Î¹) :
    âˆ€á¶  Ï€ in l.toFilteriUnion I âŠ¤, TaggedPrepartition.IsPartition Ï€ :=
  eventually_iSup.2 fun _ â†¦
    eventually_inf_principal.2 <|
      Eventually.of_forall fun Ï€ h â†¦
        Ï€.isPartition_iff_iUnion_eq.2 (h.trans Prepartition.iUnion_top)

end IntegrationParams

end BoxIntegral
