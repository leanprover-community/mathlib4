/-
Copyright (c) 2021 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov
-/
import Mathlib.Analysis.BoxIntegral.Partition.Filter
import Mathlib.Analysis.BoxIntegral.Partition.Measure
import Mathlib.Analysis.Oscillation
import Mathlib.Data.Bool.Basic
import Mathlib.MeasureTheory.Measure.Real
import Mathlib.Topology.UniformSpace.Compact

/-!
# Integrals of Riemann, Henstock-Kurzweil, and McShane

In this file we define the integral of a function over a box in `â„â¿`. The same definition works for
Riemann, Henstock-Kurzweil, and McShane integrals.

As usual, we represent `â„â¿` as the type of functions `Î¹ â†’ â„` for some finite type `Î¹`. A rectangular
box `(l, u]` in `â„â¿` is defined to be the set `{x : Î¹ â†’ â„ | âˆ€ i, l i < x i âˆ§ x i â‰¤ u i}`, see
`BoxIntegral.Box`.

Let `vol` be a box-additive function on boxes in `â„â¿` with codomain `E â†’L[â„] F`. Given a function
`f : â„â¿ â†’ E`, a box `I` and a tagged partition `Ï€` of this box, the *integral sum* of `f` over `Ï€`
with respect to the volume `vol` is the sum of `vol J (f (Ï€.tag J))` over all boxes of `Ï€`. Here
`Ï€.tag J` is the point (tag) in `â„â¿` associated with the box `J`.

The integral is defined as the limit of integral sums along a filter. Different filters correspond
to different integration theories. In order to avoid code duplication, all our definitions and
theorems take an argument `l : BoxIntegral.IntegrationParams`. This is a type that holds three
boolean values, and encodes eight filters including those corresponding to Riemann,
Henstock-Kurzweil, and McShane integrals.

Following the design of infinite sums (see `hasSum` and `tsum`), we define a predicate
`BoxIntegral.HasIntegral` and a function `BoxIntegral.integral` that returns a vector satisfying
the predicate or zero if the function is not integrable.

Then we prove some basic properties of box integrals (linearity, a formula for the integral of a
constant). We also prove a version of the Henstock-Sacks inequality (see
`BoxIntegral.Integrable.dist_integralSum_le_of_memBaseSet` and
`BoxIntegral.Integrable.dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq`), prove
integrability of continuous functions, and provide a criterion for integrability w.r.t. a
non-Riemann filter (e.g., Henstock-Kurzweil and McShane).

## Notation

- `â„â¿`: local notation for `Î¹ â†’ â„`

## Tags

integral
-/

open scoped Topology NNReal Filter Uniformity BoxIntegral

open Set Finset Function Filter Metric BoxIntegral.IntegrationParams

noncomputable section

namespace BoxIntegral

universe u v w

variable {Î¹ : Type u} {E : Type v} {F : Type w} [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] {I J : Box Î¹} {Ï€ : TaggedPrepartition I}

open TaggedPrepartition

local notation "â„â¿" => Î¹ â†’ â„

/-!
### Integral sum and its basic properties
-/


/-- The integral sum of `f : â„â¿ â†’ E` over a tagged prepartition `Ï€` w.r.t. box-additive volume `vol`
with codomain `E â†’L[â„] F` is the sum of `vol J (f (Ï€.tag J))` over all boxes of `Ï€`. -/
def integralSum (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : TaggedPrepartition I) : F :=
  âˆ‘ J âˆˆ Ï€.boxes, vol J (f (Ï€.tag J))

theorem integralSum_biUnionTagged (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : Prepartition I)
    (Ï€i : âˆ€ J, TaggedPrepartition J) :
    integralSum f vol (Ï€.biUnionTagged Ï€i) = âˆ‘ J âˆˆ Ï€.boxes, integralSum f vol (Ï€i J) := by
  refine (Ï€.sum_biUnion_boxes _ _).trans <| sum_congr rfl fun J hJ => sum_congr rfl fun J' hJ' => ?_
  rw [Ï€.tag_biUnionTagged hJ hJ']

theorem integralSum_biUnion_partition (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F)
    (Ï€ : TaggedPrepartition I) (Ï€i : âˆ€ J, Prepartition J) (hÏ€i : âˆ€ J âˆˆ Ï€, (Ï€i J).IsPartition) :
    integralSum f vol (Ï€.biUnionPrepartition Ï€i) = integralSum f vol Ï€ := by
  refine (Ï€.sum_biUnion_boxes _ _).trans (sum_congr rfl fun J hJ => ?_)
  calc
    (âˆ‘ J' âˆˆ (Ï€i J).boxes, vol J' (f (Ï€.tag <| Ï€.toPrepartition.biUnionIndex Ï€i J'))) =
        âˆ‘ J' âˆˆ (Ï€i J).boxes, vol J' (f (Ï€.tag J)) :=
      sum_congr rfl fun J' hJ' => by rw [Prepartition.biUnionIndex_of_mem _ hJ hJ']
    _ = vol J (f (Ï€.tag J)) :=
      (vol.map âŸ¨âŸ¨fun g : E â†’L[â„] F => g (f (Ï€.tag J)), rflâŸ©, fun _ _ => rflâŸ©).sum_partition_boxes
        le_top (hÏ€i J hJ)

theorem integralSum_inf_partition (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : TaggedPrepartition I)
    {Ï€' : Prepartition I} (h : Ï€'.IsPartition) :
    integralSum f vol (Ï€.infPrepartition Ï€') = integralSum f vol Ï€ :=
  integralSum_biUnion_partition f vol Ï€ _ fun _J hJ => h.restrict (Prepartition.le_of_mem _ hJ)

open Classical in
theorem integralSum_fiberwise {Î±} (g : Box Î¹ â†’ Î±) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F)
    (Ï€ : TaggedPrepartition I) :
    (âˆ‘ y âˆˆ Ï€.boxes.image g, integralSum f vol (Ï€.filter (g Â· = y))) = integralSum f vol Ï€ :=
  Ï€.sum_fiberwise g fun J => vol J (f <| Ï€.tag J)

theorem integralSum_sub_partitions (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F)
    {Ï€â‚ Ï€â‚‚ : TaggedPrepartition I} (hâ‚ : Ï€â‚.IsPartition) (hâ‚‚ : Ï€â‚‚.IsPartition) :
    integralSum f vol Ï€â‚ - integralSum f vol Ï€â‚‚ =
      âˆ‘ J âˆˆ (Ï€â‚.toPrepartition âŠ“ Ï€â‚‚.toPrepartition).boxes,
        (vol J (f <| (Ï€â‚.infPrepartition Ï€â‚‚.toPrepartition).tag J) -
          vol J (f <| (Ï€â‚‚.infPrepartition Ï€â‚.toPrepartition).tag J)) := by
  rw [â† integralSum_inf_partition f vol Ï€â‚ hâ‚‚, â† integralSum_inf_partition f vol Ï€â‚‚ hâ‚,
    integralSum, integralSum, Finset.sum_sub_distrib]
  simp only [infPrepartition_toPrepartition, inf_comm]

@[simp]
theorem integralSum_disjUnion (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) {Ï€â‚ Ï€â‚‚ : TaggedPrepartition I}
    (h : Disjoint Ï€â‚.iUnion Ï€â‚‚.iUnion) :
    integralSum f vol (Ï€â‚.disjUnion Ï€â‚‚ h) = integralSum f vol Ï€â‚ + integralSum f vol Ï€â‚‚ := by
  refine (Prepartition.sum_disj_union_boxes h _).trans
      (congr_argâ‚‚ (Â· + Â·) (sum_congr rfl fun J hJ => ?_) (sum_congr rfl fun J hJ => ?_))
  Â· rw [disjUnion_tag_of_mem_left _ hJ]
  Â· rw [disjUnion_tag_of_mem_right _ hJ]

@[simp]
theorem integralSum_add (f g : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : TaggedPrepartition I) :
    integralSum (f + g) vol Ï€ = integralSum f vol Ï€ + integralSum g vol Ï€ := by
  simp only [integralSum, Pi.add_apply, (vol _).map_add, Finset.sum_add_distrib]

@[simp]
theorem integralSum_neg (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : TaggedPrepartition I) :
    integralSum (-f) vol Ï€ = -integralSum f vol Ï€ := by
  simp only [integralSum, Pi.neg_apply, (vol _).map_neg, Finset.sum_neg_distrib]

@[simp]
theorem integralSum_smul (c : â„) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (Ï€ : TaggedPrepartition I) :
    integralSum (c â€¢ f) vol Ï€ = c â€¢ integralSum f vol Ï€ := by
  simp only [integralSum, Finset.smul_sum, Pi.smul_apply, ContinuousLinearMap.map_smul]

variable [Fintype Î¹]

/-!
### Basic integrability theory
-/


/-- The predicate `HasIntegral I l f vol y` says that `y` is the integral of `f` over `I` along `l`
w.r.t. volume `vol`. This means that integral sums of `f` tend to `ğ“ y` along
`BoxIntegral.IntegrationParams.toFilteriUnion I âŠ¤`. -/
def HasIntegral (I : Box Î¹) (l : IntegrationParams) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) (y : F) :
    Prop :=
  Tendsto (integralSum f vol) (l.toFilteriUnion I âŠ¤) (ğ“ y)

/-- A function is integrable if there exists a vector that satisfies the `HasIntegral`
predicate. -/
def Integrable (I : Box Î¹) (l : IntegrationParams) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) :=
  âˆƒ y, HasIntegral I l f vol y

open Classical in
/-- The integral of a function `f` over a box `I` along a filter `l` w.r.t. a volume `vol`.
Returns zero on non-integrable functions. -/
def integral (I : Box Î¹) (l : IntegrationParams) (f : â„â¿ â†’ E) (vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F) :=
  if h : Integrable I l f vol then h.choose else 0

-- Porting note: using the above notation â„â¿ here causes the theorem below to be silently ignored
-- see https://leanprover.zulipchat.com/#narrow/stream/287929-mathlib4/topic/Lean.204.20doesn't.20add.20lemma.20to.20the.20environment/near/363764522
-- and https://github.com/leanprover/lean4/issues/2257
variable {l : IntegrationParams} {f g : (Î¹ â†’ â„) â†’ E} {vol : Î¹ â†’áµ‡áµƒ E â†’L[â„] F} {y y' : F}

/-- Reinterpret `BoxIntegral.HasIntegral` as `Filter.Tendsto`, e.g., dot-notation theorems
that are shadowed in the `BoxIntegral.HasIntegral` namespace. -/
theorem HasIntegral.tendsto (h : HasIntegral I l f vol y) :
    Tendsto (integralSum f vol) (l.toFilteriUnion I âŠ¤) (ğ“ y) :=
  h

/-- The `Îµ`-`Î´` definition of `BoxIntegral.HasIntegral`. -/
theorem hasIntegral_iff : HasIntegral I l f vol y â†”
    âˆ€ Îµ > (0 : â„), âˆƒ r : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.RCond (r c)) âˆ§
      âˆ€ c Ï€, l.MemBaseSet I c (r c) Ï€ â†’ IsPartition Ï€ â†’ dist (integralSum f vol Ï€) y â‰¤ Îµ :=
  ((l.hasBasis_toFilteriUnion_top I).tendsto_iff nhds_basis_closedBall).trans <| by
    simp [@forall_swap â„â‰¥0 (TaggedPrepartition I)]

/-- Quite often it is more natural to prove an estimate of the form `a * Îµ`, not `Îµ` in the RHS of
`BoxIntegral.hasIntegral_iff`, so we provide this auxiliary lemma. -/
theorem HasIntegral.of_mul (a : â„)
    (h : âˆ€ Îµ : â„, 0 < Îµ â†’ âˆƒ r : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.RCond (r c)) âˆ§ âˆ€ c Ï€,
      l.MemBaseSet I c (r c) Ï€ â†’ IsPartition Ï€ â†’ dist (integralSum f vol Ï€) y â‰¤ a * Îµ) :
    HasIntegral I l f vol y := by
  refine hasIntegral_iff.2 fun Îµ hÎµ => ?_
  rcases exists_pos_mul_lt hÎµ a with âŸ¨Îµ', hÎµ', haâŸ©
  rcases h Îµ' hÎµ' with âŸ¨r, hr, HâŸ©
  exact âŸ¨r, hr, fun c Ï€ hÏ€ hÏ€p => (H c Ï€ hÏ€ hÏ€p).trans ha.leâŸ©

theorem integrable_iff_cauchy [CompleteSpace F] :
    Integrable I l f vol â†” Cauchy ((l.toFilteriUnion I âŠ¤).map (integralSum f vol)) :=
  cauchy_map_iff_exists_tendsto.symm

/-- In a complete space, a function is integrable if and only if its integral sums form a Cauchy
net. Here we restate this fact in terms of `âˆ€ Îµ > 0, âˆƒ r, ...`. -/
theorem integrable_iff_cauchy_basis [CompleteSpace F] : Integrable I l f vol â†”
    âˆ€ Îµ > (0 : â„), âˆƒ r : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„), (âˆ€ c, l.RCond (r c)) âˆ§
      âˆ€ câ‚ câ‚‚ Ï€â‚ Ï€â‚‚, l.MemBaseSet I câ‚ (r câ‚) Ï€â‚ â†’ Ï€â‚.IsPartition â†’ l.MemBaseSet I câ‚‚ (r câ‚‚) Ï€â‚‚ â†’
        Ï€â‚‚.IsPartition â†’ dist (integralSum f vol Ï€â‚) (integralSum f vol Ï€â‚‚) â‰¤ Îµ := by
  rw [integrable_iff_cauchy, cauchy_map_iff',
    (l.hasBasis_toFilteriUnion_top _).prod_self.tendsto_iff uniformity_basis_dist_le]
  refine forallâ‚‚_congr fun Îµ _ => exists_congr fun r => ?_
  simp only [Prod.forall, exists_imp, prodMk_mem_set_prod_eq, and_imp, mem_setOf_eq]
  exact
    and_congr Iff.rfl
      âŸ¨fun H câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hUâ‚ hâ‚‚ hUâ‚‚ => H Ï€â‚ Ï€â‚‚ câ‚ hâ‚ hUâ‚ câ‚‚ hâ‚‚ hUâ‚‚,
        fun H Ï€â‚ Ï€â‚‚ câ‚ hâ‚ hUâ‚ câ‚‚ hâ‚‚ hUâ‚‚ => H câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hUâ‚ hâ‚‚ hUâ‚‚âŸ©

theorem HasIntegral.mono {lâ‚ lâ‚‚ : IntegrationParams} (h : HasIntegral I lâ‚ f vol y) (hl : lâ‚‚ â‰¤ lâ‚) :
    HasIntegral I lâ‚‚ f vol y :=
  h.mono_left <| IntegrationParams.toFilteriUnion_mono _ hl _

protected theorem Integrable.hasIntegral (h : Integrable I l f vol) :
    HasIntegral I l f vol (integral I l f vol) := by
  rw [integral, dif_pos h]
  exact Classical.choose_spec h

theorem Integrable.mono {l'} (h : Integrable I l f vol) (hle : l' â‰¤ l) : Integrable I l' f vol :=
  âŸ¨_, h.hasIntegral.mono hleâŸ©

theorem HasIntegral.unique (h : HasIntegral I l f vol y) (h' : HasIntegral I l f vol y') : y = y' :=
  tendsto_nhds_unique h h'

theorem HasIntegral.integrable (h : HasIntegral I l f vol y) : Integrable I l f vol :=
  âŸ¨_, hâŸ©

theorem HasIntegral.integral_eq (h : HasIntegral I l f vol y) : integral I l f vol = y :=
  h.integrable.hasIntegral.unique h

nonrec theorem HasIntegral.add (h : HasIntegral I l f vol y) (h' : HasIntegral I l g vol y') :
    HasIntegral I l (f + g) vol (y + y') := by
  simpa only [HasIntegral, â† integralSum_add] using h.add h'

theorem Integrable.add (hf : Integrable I l f vol) (hg : Integrable I l g vol) :
    Integrable I l (f + g) vol :=
  (hf.hasIntegral.add hg.hasIntegral).integrable

theorem integral_add (hf : Integrable I l f vol) (hg : Integrable I l g vol) :
    integral I l (f + g) vol = integral I l f vol + integral I l g vol :=
  (hf.hasIntegral.add hg.hasIntegral).integral_eq

nonrec theorem HasIntegral.neg (hf : HasIntegral I l f vol y) : HasIntegral I l (-f) vol (-y) := by
  simpa only [HasIntegral, â† integralSum_neg] using hf.neg

theorem Integrable.neg (hf : Integrable I l f vol) : Integrable I l (-f) vol :=
  hf.hasIntegral.neg.integrable

theorem Integrable.of_neg (hf : Integrable I l (-f) vol) : Integrable I l f vol :=
  neg_neg f â–¸ hf.neg

@[simp]
theorem integrable_neg : Integrable I l (-f) vol â†” Integrable I l f vol :=
  âŸ¨fun h => h.of_neg, fun h => h.negâŸ©

@[simp]
theorem integral_neg : integral I l (-f) vol = -integral I l f vol := by
  classical
  exact if h : Integrable I l f vol then h.hasIntegral.neg.integral_eq
  else by rw [integral, integral, dif_neg h, dif_neg (mt Integrable.of_neg h), neg_zero]

theorem HasIntegral.sub (h : HasIntegral I l f vol y) (h' : HasIntegral I l g vol y') :
    HasIntegral I l (f - g) vol (y - y') := by simpa only [sub_eq_add_neg] using h.add h'.neg

theorem Integrable.sub (hf : Integrable I l f vol) (hg : Integrable I l g vol) :
    Integrable I l (f - g) vol :=
  (hf.hasIntegral.sub hg.hasIntegral).integrable

theorem integral_sub (hf : Integrable I l f vol) (hg : Integrable I l g vol) :
    integral I l (f - g) vol = integral I l f vol - integral I l g vol :=
  (hf.hasIntegral.sub hg.hasIntegral).integral_eq

theorem hasIntegral_const (c : E) : HasIntegral I l (fun _ => c) vol (vol I c) :=
  tendsto_const_nhds.congr' <| (l.eventually_isPartition I).mono fun _Ï€ hÏ€ => Eq.symm <|
    (vol.map âŸ¨âŸ¨fun g : E â†’L[â„] F â†¦ g c, rflâŸ©, fun _ _ â†¦ rflâŸ©).sum_partition_boxes le_top hÏ€

@[simp]
theorem integral_const (c : E) : integral I l (fun _ => c) vol = vol I c :=
  (hasIntegral_const c).integral_eq

theorem integrable_const (c : E) : Integrable I l (fun _ => c) vol :=
  âŸ¨_, hasIntegral_const câŸ©

theorem hasIntegral_zero : HasIntegral I l (fun _ => (0 : E)) vol 0 := by
  simpa only [â† (vol I).map_zero] using hasIntegral_const (0 : E)

theorem integrable_zero : Integrable I l (fun _ => (0 : E)) vol :=
  âŸ¨0, hasIntegral_zeroâŸ©

theorem integral_zero : integral I l (fun _ => (0 : E)) vol = 0 :=
  hasIntegral_zero.integral_eq

theorem HasIntegral.sum {Î± : Type*} {s : Finset Î±} {f : Î± â†’ â„â¿ â†’ E} {g : Î± â†’ F}
    (h : âˆ€ i âˆˆ s, HasIntegral I l (f i) vol (g i)) :
    HasIntegral I l (fun x => âˆ‘ i âˆˆ s, f i x) vol (âˆ‘ i âˆˆ s, g i) := by
  classical
  induction' s using Finset.induction_on with a s ha ihs; Â· simp [hasIntegral_zero]
  simp only [Finset.sum_insert ha]; rw [Finset.forall_mem_insert] at h
  exact h.1.add (ihs h.2)

theorem HasIntegral.smul (hf : HasIntegral I l f vol y) (c : â„) :
    HasIntegral I l (c â€¢ f) vol (c â€¢ y) := by
  simpa only [HasIntegral, â† integralSum_smul] using
    (tendsto_const_nhds : Tendsto _ _ (ğ“ c)).smul hf

theorem Integrable.smul (hf : Integrable I l f vol) (c : â„) : Integrable I l (c â€¢ f) vol :=
  (hf.hasIntegral.smul c).integrable

theorem Integrable.of_smul {c : â„} (hf : Integrable I l (c â€¢ f) vol) (hc : c â‰  0) :
    Integrable I l f vol := by
  simpa [inv_smul_smulâ‚€ hc] using hf.smul câ»Â¹

@[simp]
theorem integral_smul (c : â„) : integral I l (fun x => c â€¢ f x) vol = c â€¢ integral I l f vol := by
  rcases eq_or_ne c 0 with (rfl | hc); Â· simp only [zero_smul, integral_zero]
  by_cases hf : Integrable I l f vol
  Â· exact (hf.hasIntegral.smul c).integral_eq
  Â· have : Â¬Integrable I l (fun x => c â€¢ f x) vol := mt (fun h => h.of_smul hc) hf
    rw [integral, integral, dif_neg hf, dif_neg this, smul_zero]

open MeasureTheory

/-- The integral of a nonnegative function w.r.t. a volume generated by a locally-finite measure is
nonnegative. -/
theorem integral_nonneg {g : â„â¿ â†’ â„} (hg : âˆ€ x âˆˆ Box.Icc I, 0 â‰¤ g x) (Î¼ : Measure â„â¿)
    [IsLocallyFiniteMeasure Î¼] : 0 â‰¤ integral I l g Î¼.toBoxAdditive.toSMul := by
  by_cases hgi : Integrable I l g Î¼.toBoxAdditive.toSMul
  Â· refine ge_of_tendsto' hgi.hasIntegral fun Ï€ => sum_nonneg fun J _ => ?_
    exact mul_nonneg ENNReal.toReal_nonneg (hg _ <| Ï€.tag_mem_Icc _)
  Â· rw [integral, dif_neg hgi]

/-- If `â€–f xâ€– â‰¤ g x` on `[l, u]` and `g` is integrable, then the norm of the integral of `f` is less
than or equal to the integral of `g`. -/
theorem norm_integral_le_of_norm_le {g : â„â¿ â†’ â„} (hle : âˆ€ x âˆˆ Box.Icc I, â€–f xâ€– â‰¤ g x)
    (Î¼ : Measure â„â¿) [IsLocallyFiniteMeasure Î¼] (hg : Integrable I l g Î¼.toBoxAdditive.toSMul) :
    â€–(integral I l f Î¼.toBoxAdditive.toSMul : E)â€– â‰¤ integral I l g Î¼.toBoxAdditive.toSMul := by
  by_cases hfi : Integrable.{u, v, v} I l f Î¼.toBoxAdditive.toSMul
  Â· refine le_of_tendsto_of_tendsto' hfi.hasIntegral.norm hg.hasIntegral fun Ï€ => ?_
    refine norm_sum_le_of_le _ fun J _ => ?_
    simp only [BoxAdditiveMap.toSMul_apply, norm_smul, smul_eq_mul, Real.norm_eq_abs,
      Î¼.toBoxAdditive_apply, abs_of_nonneg measureReal_nonneg]
    gcongr
    exact hle _ <| Ï€.tag_mem_Icc _
  Â· rw [integral, dif_neg hfi, norm_zero]
    exact integral_nonneg (fun x hx => (norm_nonneg _).trans (hle x hx)) Î¼

theorem norm_integral_le_of_le_const {c : â„}
    (hc : âˆ€ x âˆˆ Box.Icc I, â€–f xâ€– â‰¤ c) (Î¼ : Measure â„â¿) [IsLocallyFiniteMeasure Î¼] :
    â€–(integral I l f Î¼.toBoxAdditive.toSMul : E)â€– â‰¤ Î¼.real I * c := by
  simpa only [integral_const] using norm_integral_le_of_norm_le hc Î¼ (integrable_const c)

/-!
# Henstock-Sacks inequality and integrability on subboxes

Henstock-Sacks inequality for Henstock-Kurzweil integral says the following. Let `f` be a function
integrable on a box `I`; let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged partition of
`I` subordinate to `r`, the integral sum over this partition is `Îµ`-close to the integral. Then for
any tagged prepartition (i.e. a finite collections of pairwise disjoint subboxes of `I` with tagged
points) `Ï€`, the integral sum over `Ï€` differs from the integral of `f` over the part of `I` covered
by `Ï€` by at most `Îµ`. The actual statement in the library is a bit more complicated to make it work
for any `BoxIntegral.IntegrationParams`. We formalize several versions of this inequality in
`BoxIntegral.Integrable.dist_integralSum_le_of_memBaseSet`,
`BoxIntegral.Integrable.dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq`, and
`BoxIntegral.Integrable.dist_integralSum_sum_integral_le_of_memBaseSet`.

Instead of using predicate assumptions on `r`, we define
`BoxIntegral.Integrable.convergenceR (h : integrable I l f vol) (Îµ : â„) (c : â„â‰¥0) : â„â¿ â†’ (0, âˆ)`
to be a function `r` such that

- if `l.bRiemann`, then `r` is a constant;
- if `Îµ > 0`, then for any tagged partition `Ï€` of `I` subordinate to `r` (more precisely,
  satisfying the predicate `l.mem_base_set I c r`), the integral sum of `f` over `Ï€` differs from
  the integral of `f` over `I` by at most `Îµ`.

The proof is mostly based on
[Russel A. Gordon, *The integrals of Lebesgue, Denjoy, Perron, and Henstock*][Gordon55].

-/

namespace Integrable

/-- If `Îµ > 0`, then `BoxIntegral.Integrable.convergenceR` is a function `r : â„â‰¥0 â†’ â„â¿ â†’ (0, âˆ)`
such that for every `c : â„â‰¥0`, for every tagged partition `Ï€` subordinate to `r` (and satisfying
additional distortion estimates if `BoxIntegral.IntegrationParams.bDistortion l = true`), the
corresponding integral sum is `Îµ`-close to the integral.

If `BoxIntegral.IntegrationParams.bRiemann = true`, then `r c x` does not depend on `x`. If
`Îµ â‰¤ 0`, then we use `r c x = 1`. -/
def convergenceR (h : Integrable I l f vol) (Îµ : â„) : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„) :=
  if hÎµ : 0 < Îµ then (hasIntegral_iff.1 h.hasIntegral Îµ hÎµ).choose
  else fun _ _ => âŸ¨1, Set.mem_Ioi.2 zero_lt_oneâŸ©

variable {c câ‚ câ‚‚ : â„â‰¥0} {Îµ Îµâ‚ Îµâ‚‚ : â„} {Ï€â‚ Ï€â‚‚ : TaggedPrepartition I}

theorem convergenceR_cond (h : Integrable I l f vol) (Îµ : â„) (c : â„â‰¥0) :
    l.RCond (h.convergenceR Îµ c) := by
  rw [convergenceR]; split_ifs with hâ‚€
  exacts [(hasIntegral_iff.1 h.hasIntegral Îµ hâ‚€).choose_spec.1 _, fun _ x => rfl]

theorem dist_integralSum_integral_le_of_memBaseSet (h : Integrable I l f vol) (hâ‚€ : 0 < Îµ)
    (hÏ€ : l.MemBaseSet I c (h.convergenceR Îµ c) Ï€) (hÏ€p : Ï€.IsPartition) :
    dist (integralSum f vol Ï€) (integral I l f vol) â‰¤ Îµ := by
  rw [convergenceR, dif_pos hâ‚€] at hÏ€
  exact (hasIntegral_iff.1 h.hasIntegral Îµ hâ‚€).choose_spec.2 c _ hÏ€ hÏ€p

/-- **Henstock-Sacks inequality**. Let `râ‚ râ‚‚ : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged
*partition* of `I` subordinate to `râ‚–`, `k=1,2`, the integral sum of `f` over this partition differs
from the integral of `f` by at most `Îµâ‚–`. Then for any two tagged *prepartition* `Ï€â‚ Ï€â‚‚` subordinate
to `râ‚` and `râ‚‚` respectively and covering the same part of `I`, the integral sums of `f` over these
prepartitions differ from each other by at most `Îµâ‚ + Îµâ‚‚`.

The actual statement

- uses `BoxIntegral.Integrable.convergenceR` instead of a predicate assumption on `r`;
- uses `BoxIntegral.IntegrationParams.MemBaseSet` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion.

See also `BoxIntegral.Integrable.dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq` and
`BoxIntegral.Integrable.dist_integralSum_sum_integral_le_of_memBaseSet`.
-/
theorem dist_integralSum_le_of_memBaseSet (h : Integrable I l f vol) (hposâ‚ : 0 < Îµâ‚)
    (hposâ‚‚ : 0 < Îµâ‚‚) (hâ‚ : l.MemBaseSet I câ‚ (h.convergenceR Îµâ‚ câ‚) Ï€â‚)
    (hâ‚‚ : l.MemBaseSet I câ‚‚ (h.convergenceR Îµâ‚‚ câ‚‚) Ï€â‚‚) (HU : Ï€â‚.iUnion = Ï€â‚‚.iUnion) :
    dist (integralSum f vol Ï€â‚) (integralSum f vol Ï€â‚‚) â‰¤ Îµâ‚ + Îµâ‚‚ := by
  rcases hâ‚.exists_common_compl hâ‚‚ HU with âŸ¨Ï€, hÏ€U, hÏ€câ‚, hÏ€câ‚‚âŸ©
  set r : â„â¿ â†’ Ioi (0 : â„) := fun x => min (h.convergenceR Îµâ‚ câ‚ x) (h.convergenceR Îµâ‚‚ câ‚‚ x)
  set Ï€r := Ï€.toSubordinate r
  have Hâ‚ :
    dist (integralSum f vol (Ï€â‚.unionComplToSubordinate Ï€ hÏ€U r)) (integral I l f vol) â‰¤ Îµâ‚ :=
    h.dist_integralSum_integral_le_of_memBaseSet hposâ‚
      (hâ‚.unionComplToSubordinate (fun _ _ => min_le_left _ _) hÏ€U hÏ€câ‚)
      (isPartition_unionComplToSubordinate _ _ _ _)
  rw [HU] at hÏ€U
  have Hâ‚‚ :
    dist (integralSum f vol (Ï€â‚‚.unionComplToSubordinate Ï€ hÏ€U r)) (integral I l f vol) â‰¤ Îµâ‚‚ :=
    h.dist_integralSum_integral_le_of_memBaseSet hposâ‚‚
      (hâ‚‚.unionComplToSubordinate (fun _ _ => min_le_right _ _) hÏ€U hÏ€câ‚‚)
      (isPartition_unionComplToSubordinate _ _ _ _)
  simpa [unionComplToSubordinate] using (dist_triangle_right _ _ _).trans (add_le_add Hâ‚ Hâ‚‚)

/-- If `f` is integrable on `I` along `l`, then for two sufficiently fine tagged prepartitions
(in the sense of the filter `BoxIntegral.IntegrationParams.toFilter l I`) such that they cover
the same part of `I`, the integral sums of `f` over `Ï€â‚` and `Ï€â‚‚` are very close to each other. -/
theorem tendsto_integralSum_toFilter_prod_self_inf_iUnion_eq_uniformity (h : Integrable I l f vol) :
    Tendsto (fun Ï€ : TaggedPrepartition I Ã— TaggedPrepartition I =>
      (integralSum f vol Ï€.1, integralSum f vol Ï€.2))
        ((l.toFilter I Ã—Ë¢ l.toFilter I) âŠ“ ğ“Ÿ {Ï€ | Ï€.1.iUnion = Ï€.2.iUnion}) (ğ“¤ F) := by
  refine (((l.hasBasis_toFilter I).prod_self.inf_principal _).tendsto_iff
    uniformity_basis_dist_le).2 fun Îµ Îµ0 => ?_
  replace Îµ0 := half_pos Îµ0
  use h.convergenceR (Îµ / 2), h.convergenceR_cond (Îµ / 2); rintro âŸ¨Ï€â‚, Ï€â‚‚âŸ© âŸ¨âŸ¨hâ‚, hâ‚‚âŸ©, hUâŸ©
  rw [â† add_halves Îµ]
  exact h.dist_integralSum_le_of_memBaseSet Îµ0 Îµ0 hâ‚.choose_spec hâ‚‚.choose_spec hU

/-- If `f` is integrable on a box `I` along `l`, then for any fixed subset `s` of `I` that can be
represented as a finite union of boxes, the integral sums of `f` over tagged prepartitions that
cover exactly `s` form a Cauchy â€œsequenceâ€ along `l`. -/
theorem cauchy_map_integralSum_toFilteriUnion (h : Integrable I l f vol) (Ï€â‚€ : Prepartition I) :
    Cauchy ((l.toFilteriUnion I Ï€â‚€).map (integralSum f vol)) := by
  refine âŸ¨inferInstance, ?_âŸ©
  rw [prod_map_map_eq, â† toFilter_inf_iUnion_eq, â† prod_inf_prod, prod_principal_principal]
  exact h.tendsto_integralSum_toFilter_prod_self_inf_iUnion_eq_uniformity.mono_left
    (inf_le_inf_left _ <| principal_mono.2 fun Ï€ h => h.1.trans h.2.symm)

variable [CompleteSpace F]

theorem to_subbox_aux (h : Integrable I l f vol) (hJ : J â‰¤ I) :
    âˆƒ y : F, HasIntegral J l f vol y âˆ§
      Tendsto (integralSum f vol) (l.toFilteriUnion I (Prepartition.single I J hJ)) (ğ“ y) := by
  refine (cauchy_map_iff_exists_tendsto.1
    (h.cauchy_map_integralSum_toFilteriUnion (.single I J hJ))).imp fun y hy â†¦ âŸ¨?_, hyâŸ©
  convert hy.comp (l.tendsto_embedBox_toFilteriUnion_top hJ) -- faster than `exact` here

/-- If `f` is integrable on a box `I`, then it is integrable on any subbox of `I`. -/
theorem to_subbox (h : Integrable I l f vol) (hJ : J â‰¤ I) : Integrable J l f vol :=
  (h.to_subbox_aux hJ).imp fun _ => And.left

/-- If `f` is integrable on a box `I`, then integral sums of `f` over tagged prepartitions
that cover exactly a subbox `J â‰¤ I` tend to the integral of `f` over `J` along `l`. -/
theorem tendsto_integralSum_toFilteriUnion_single (h : Integrable I l f vol) (hJ : J â‰¤ I) :
    Tendsto (integralSum f vol) (l.toFilteriUnion I (Prepartition.single I J hJ))
      (ğ“ <| integral J l f vol) :=
  let âŸ¨_y, hâ‚, hâ‚‚âŸ© := h.to_subbox_aux hJ
  hâ‚.integral_eq.symm â–¸ hâ‚‚

/-- **Henstock-Sacks inequality**. Let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged
*partition* of `I` subordinate to `r`, the integral sum of `f` over this partition differs from the
integral of `f` by at most `Îµ`. Then for any tagged *prepartition* `Ï€` subordinate to `r`, the
integral sum of `f` over this prepartition differs from the integral of `f` over the part of `I`
covered by `Ï€` by at most `Îµ`.

The actual statement

- uses `BoxIntegral.Integrable.convergenceR` instead of a predicate assumption on `r`;
- uses `BoxIntegral.IntegrationParams.MemBaseSet` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion;
- takes an extra argument `Ï€â‚€ : prepartition I` and an assumption `Ï€.Union = Ï€â‚€.Union` instead of
  using `Ï€.to_prepartition`.
-/
theorem dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq (h : Integrable I l f vol)
    (h0 : 0 < Îµ) (hÏ€ : l.MemBaseSet I c (h.convergenceR Îµ c) Ï€) {Ï€â‚€ : Prepartition I}
    (hU : Ï€.iUnion = Ï€â‚€.iUnion) :
    dist (integralSum f vol Ï€) (âˆ‘ J âˆˆ Ï€â‚€.boxes, integral J l f vol) â‰¤ Îµ := by
  -- Let us prove that the distance is less than or equal to `Îµ + Î´` for all positive `Î´`.
  refine le_of_forall_pos_le_add fun Î´ Î´0 => ?_
  -- First we choose some constants.
  set Î´' : â„ := Î´ / (#Ï€â‚€.boxes + 1)
  have H0 : 0 < (#Ï€â‚€.boxes + 1 : â„) := Nat.cast_add_one_pos _
  have Î´'0 : 0 < Î´' := div_pos Î´0 H0
  set C := max Ï€â‚€.distortion Ï€â‚€.compl.distortion
  /- Next we choose a tagged partition of each `J âˆˆ Ï€â‚€` such that the integral sum of `f` over this
    partition is `Î´'`-close to the integral of `f` over `J`. -/
  have : âˆ€ J âˆˆ Ï€â‚€, âˆƒ Ï€i : TaggedPrepartition J,
      Ï€i.IsPartition âˆ§ dist (integralSum f vol Ï€i) (integral J l f vol) â‰¤ Î´' âˆ§
        l.MemBaseSet J C (h.convergenceR Î´' C) Ï€i := by
    intro J hJ
    have Hle : J â‰¤ I := Ï€â‚€.le_of_mem hJ
    have HJi : Integrable J l f vol := h.to_subbox Hle
    set r := fun x => min (h.convergenceR Î´' C x) (HJi.convergenceR Î´' C x)
    have hJd : J.distortion â‰¤ C := le_trans (Finset.le_sup hJ) (le_max_left _ _)
    rcases l.exists_memBaseSet_isPartition J hJd r with âŸ¨Ï€J, hC, hpâŸ©
    have hCâ‚ : l.MemBaseSet J C (HJi.convergenceR Î´' C) Ï€J := by
      refine hC.mono J le_rfl le_rfl fun x _ => ?_; exact min_le_right _ _
    have hCâ‚‚ : l.MemBaseSet J C (h.convergenceR Î´' C) Ï€J := by
      refine hC.mono J le_rfl le_rfl fun x _ => ?_; exact min_le_left _ _
    exact âŸ¨Ï€J, hp, HJi.dist_integralSum_integral_le_of_memBaseSet Î´'0 hCâ‚ hp, hCâ‚‚âŸ©
  /- Now we combine these tagged partitions into a tagged prepartition of `I` that covers the
    same part of `I` as `Ï€â‚€` and apply `BoxIntegral.dist_integralSum_le_of_memBaseSet` to
    `Ï€` and this prepartition. -/
  choose! Ï€i hÏ€ip hÏ€iÎ´' hÏ€iC using this
  have : l.MemBaseSet I C (h.convergenceR Î´' C) (Ï€â‚€.biUnionTagged Ï€i) :=
    biUnionTagged_memBaseSet hÏ€iC hÏ€ip fun _ => le_max_right _ _
  have hU' : Ï€.iUnion = (Ï€â‚€.biUnionTagged Ï€i).iUnion :=
    hU.trans (Prepartition.iUnion_biUnion_partition _ hÏ€ip).symm
  have := h.dist_integralSum_le_of_memBaseSet h0 Î´'0 hÏ€ this hU'
  rw [integralSum_biUnionTagged] at this
  calc
    dist (integralSum f vol Ï€) (âˆ‘ J âˆˆ Ï€â‚€.boxes, integral J l f vol) â‰¤
        dist (integralSum f vol Ï€) (âˆ‘ J âˆˆ Ï€â‚€.boxes, integralSum f vol (Ï€i J)) +
          dist (âˆ‘ J âˆˆ Ï€â‚€.boxes, integralSum f vol (Ï€i J)) (âˆ‘ J âˆˆ Ï€â‚€.boxes, integral J l f vol) :=
      dist_triangle _ _ _
    _ â‰¤ Îµ + Î´' + âˆ‘ _J âˆˆ Ï€â‚€.boxes, Î´' := add_le_add this (dist_sum_sum_le_of_le _ hÏ€iÎ´')
    _ = Îµ + Î´ := by simp [field, Î´']; ring

/-- **Henstock-Sacks inequality**. Let `r : â„â¿ â†’ (0, âˆ)` be a function such that for any tagged
*partition* of `I` subordinate to `r`, the integral sum of `f` over this partition differs from the
integral of `f` by at most `Îµ`. Then for any tagged *prepartition* `Ï€` subordinate to `r`, the
integral sum of `f` over this prepartition differs from the integral of `f` over the part of `I`
covered by `Ï€` by at most `Îµ`.

The actual statement

- uses `BoxIntegral.Integrable.convergenceR` instead of a predicate assumption on `r`;
- uses `BoxIntegral.IntegrationParams.MemBaseSet` instead of â€œsubordinate to `r`â€ to
  account for additional requirements like being a Henstock partition or having a bounded
  distortion;
-/
theorem dist_integralSum_sum_integral_le_of_memBaseSet (h : Integrable I l f vol) (h0 : 0 < Îµ)
    (hÏ€ : l.MemBaseSet I c (h.convergenceR Îµ c) Ï€) :
    dist (integralSum f vol Ï€) (âˆ‘ J âˆˆ Ï€.boxes, integral J l f vol) â‰¤ Îµ :=
  h.dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq h0 hÏ€ rfl

/-- Integral sum of `f` over a tagged prepartition `Ï€` such that `Ï€.Union = Ï€â‚€.Union` tends to the
sum of integrals of `f` over the boxes of `Ï€â‚€`. -/
theorem tendsto_integralSum_sum_integral (h : Integrable I l f vol) (Ï€â‚€ : Prepartition I) :
    Tendsto (integralSum f vol) (l.toFilteriUnion I Ï€â‚€)
      (ğ“ <| âˆ‘ J âˆˆ Ï€â‚€.boxes, integral J l f vol) := by
  refine ((l.hasBasis_toFilteriUnion I Ï€â‚€).tendsto_iff nhds_basis_closedBall).2 fun Îµ Îµ0 => ?_
  refine âŸ¨h.convergenceR Îµ, h.convergenceR_cond Îµ, ?_âŸ©
  simp only [mem_setOf_eq]
  rintro Ï€ âŸ¨c, hc, hUâŸ©
  exact h.dist_integralSum_sum_integral_le_of_memBaseSet_of_iUnion_eq Îµ0 hc hU

/-- If `f` is integrable on `I`, then `fun J â†¦ integral J l f vol` is box-additive on subboxes of
`I`: if `Ï€â‚`, `Ï€â‚‚` are two prepartitions of `I` covering the same part of `I`, the sum of integrals
of `f` over the boxes of `Ï€â‚` is equal to the sum of integrals of `f` over the boxes of `Ï€â‚‚`.

See also `BoxIntegral.Integrable.toBoxAdditive` for a bundled version. -/
theorem sum_integral_congr (h : Integrable I l f vol) {Ï€â‚ Ï€â‚‚ : Prepartition I}
    (hU : Ï€â‚.iUnion = Ï€â‚‚.iUnion) :
    âˆ‘ J âˆˆ Ï€â‚.boxes, integral J l f vol = âˆ‘ J âˆˆ Ï€â‚‚.boxes, integral J l f vol := by
  refine tendsto_nhds_unique (h.tendsto_integralSum_sum_integral Ï€â‚) ?_
  rw [l.toFilteriUnion_congr _ hU]
  exact h.tendsto_integralSum_sum_integral Ï€â‚‚

/-- If `f` is integrable on `I`, then `fun J â†¦ integral J l f vol` is box-additive on subboxes of
`I`: if `Ï€â‚`, `Ï€â‚‚` are two prepartitions of `I` covering the same part of `I`, the sum of integrals
of `f` over the boxes of `Ï€â‚` is equal to the sum of integrals of `f` over the boxes of `Ï€â‚‚`.

See also `BoxIntegral.Integrable.sum_integral_congr` for an unbundled version. -/
@[simps]
def toBoxAdditive (h : Integrable I l f vol) : Î¹ â†’áµ‡áµƒ[I] F where
  toFun J := integral J l f vol
  sum_partition_boxes' J hJ Ï€ hÏ€ := by
    replace hÏ€ := hÏ€.iUnion_eq; rw [â† Prepartition.iUnion_top] at hÏ€
    rw [(h.to_subbox (WithTop.coe_le_coe.1 hJ)).sum_integral_congr hÏ€, Prepartition.top_boxes,
      sum_singleton]

end Integrable

open MeasureTheory

/-!
### Integrability conditions
-/

open Prepartition EMetric ENNReal BoxAdditiveMap Finset Metric TaggedPrepartition

variable (l)

/-- A function that is bounded and a.e. continuous on a box `I` is integrable on `I`. -/
theorem integrable_of_bounded_and_ae_continuousWithinAt [CompleteSpace E] {I : Box Î¹} {f : â„â¿ â†’ E}
    (hb : âˆƒ C : â„, âˆ€ x âˆˆ Box.Icc I, â€–f xâ€– â‰¤ C) (Î¼ : Measure â„â¿) [IsLocallyFiniteMeasure Î¼]
    (hc : âˆ€áµ x âˆ‚(Î¼.restrict (Box.Icc I)), ContinuousWithinAt f (Box.Icc I) x) :
    Integrable I l f Î¼.toBoxAdditive.toSMul := by
  /- We prove that f is integrable by proving that we can ensure that the integrals over any
     two tagged prepartitions Ï€â‚ and Ï€â‚‚ can be made Îµ-close by making the partitions
     sufficiently fine.

     Start by defining some constants C, Îµâ‚, Îµâ‚‚ that will be useful later. -/
  refine integrable_iff_cauchy_basis.2 fun Îµ Îµ0 â†¦ ?_
  rcases exists_pos_mul_lt Îµ0 (2 * Î¼.toBoxAdditive I) with âŸ¨Îµâ‚, Îµâ‚0, hÎµâ‚âŸ©
  rcases hb with âŸ¨C, hCâŸ©
  have C0 : 0 â‰¤ C := by
    obtain âŸ¨x, hxâŸ© := BoxIntegral.Box.nonempty_coe I
    exact le_trans (norm_nonneg (f x)) <| hC x (I.coe_subset_Icc hx)
  rcases exists_pos_mul_lt Îµ0 (4 * C) with âŸ¨Îµâ‚‚, Îµâ‚‚0, hÎµâ‚‚âŸ©
  have Îµâ‚‚0' : ENNReal.ofReal Îµâ‚‚ â‰  0 := ne_of_gt <| ofReal_pos.2 Îµâ‚‚0
  -- The set of discontinuities of f is contained in an open set U with Î¼ U < Îµâ‚‚.
  let D := { x âˆˆ Box.Icc I | Â¬ ContinuousWithinAt f (Box.Icc I) x }
  let Î¼' := Î¼.restrict (Box.Icc I)
  have Î¼'D : Î¼' D = 0 := by
    rcases eventually_iff_exists_mem.1 hc with âŸ¨V, ae, hVâŸ©
    exact eq_of_le_of_not_lt (mem_ae_iff.1 ae â–¸ (Î¼'.mono <| fun x h xV â†¦ h.2 (hV x xV))) not_lt_zero
  obtain âŸ¨U, UD, Uopen, hUâŸ© := Set.exists_isOpen_lt_add D (show Î¼' D â‰  âŠ¤ by simp [Î¼'D]) Îµâ‚‚0'
  rw [Î¼'D, zero_add] at hU
  /- Box.Icc I \ U is compact and avoids discontinuities of f, so there exists r > 0 such that for
     every x âˆˆ Box.Icc I \ U, the oscillation (within Box.Icc I) of f on the ball of radius r
     centered at x is â‰¤ Îµâ‚ -/
  have comp : IsCompact (Box.Icc I \ U) :=
    I.isCompact_Icc.of_isClosed_subset (I.isCompact_Icc.isClosed.sdiff Uopen) Set.diff_subset
  have : âˆ€ x âˆˆ (Box.Icc I \ U), oscillationWithin f (Box.Icc I) x < (ENNReal.ofReal Îµâ‚) := by
    intro x hx
    suffices oscillationWithin f (Box.Icc I) x = 0 by rw [this]; exact ofReal_pos.2 Îµâ‚0
    simpa [OscillationWithin.eq_zero_iff_continuousWithinAt, D, hx.1] using hx.2 âˆ˜ (fun a â†¦ UD a)
  rcases comp.uniform_oscillationWithin this with âŸ¨r, r0, hrâŸ©
  /- We prove the claim for partitions Ï€â‚ and Ï€â‚‚ subordinate to r/2, by writing the difference as
     an integralSum over Ï€â‚ âŠ“ Ï€â‚‚ and considering separately the boxes of Ï€â‚ âŠ“ Ï€â‚‚ which are/aren't
     fully contained within U. -/
  refine âŸ¨fun _ _ â†¦ âŸ¨r / 2, half_pos r0âŸ©, fun _ _ _ â†¦ rfl, fun câ‚ câ‚‚ Ï€â‚ Ï€â‚‚ hâ‚ hâ‚p hâ‚‚ hâ‚‚p â†¦ ?_âŸ©
  simp only [dist_eq_norm, integralSum_sub_partitions _ _ hâ‚p hâ‚‚p, toSMul_apply, â† smul_sub]
  have Î¼I : Î¼ I < âŠ¤ := lt_of_le_of_lt (Î¼.mono I.coe_subset_Icc) I.isCompact_Icc.measure_lt_top
  let tâ‚ (J : Box Î¹) : â„â¿ := (Ï€â‚.infPrepartition Ï€â‚‚.toPrepartition).tag J
  let tâ‚‚ (J : Box Î¹) : â„â¿ := (Ï€â‚‚.infPrepartition Ï€â‚.toPrepartition).tag J
  let B := (Ï€â‚.toPrepartition âŠ“ Ï€â‚‚.toPrepartition).boxes
  classical
  let B' := {J âˆˆ B | J.toSet âŠ† U}
  have hB' : B' âŠ† B := B.filter_subset (fun J â†¦ J.toSet âŠ† U)
  have Î¼J_ne_top : âˆ€ J âˆˆ B, Î¼ J â‰  âŠ¤ :=
    fun J hJ â†¦ lt_top_iff_ne_top.1 <| lt_of_le_of_lt (Î¼.mono (Prepartition.le_of_mem' _ J hJ)) Î¼I
  have un : âˆ€ S âŠ† B, â‹ƒ J âˆˆ S, J.toSet âŠ† I.toSet :=
    fun S hS â†¦ iUnion_subset_iff.2 (fun J â†¦ iUnion_subset_iff.2 fun hJ â†¦ le_of_mem' _ J (hS hJ))
  rw [â† sum_sdiff hB', â† add_halves Îµ]
  apply le_trans (norm_add_le _ _) (add_le_add ?_ ?_)
  /- If a box J is not contained within U, then the oscillation of f on J is small, which bounds
     the contribution of J to the overall sum. -/
  Â· have : âˆ€ J âˆˆ B \ B', â€–Î¼.toBoxAdditive J â€¢ (f (tâ‚ J) - f (tâ‚‚ J))â€– â‰¤ Î¼.toBoxAdditive J * Îµâ‚ := by
      intro J hJ
      rw [mem_sdiff, B.mem_filter, not_and] at hJ
      rw [norm_smul, Î¼.toBoxAdditive_apply, Real.norm_of_nonneg measureReal_nonneg]
      gcongr _ * ?_
      obtain âŸ¨x, xJ, xnUâŸ© : âˆƒ x âˆˆ J, x âˆ‰ U := Set.not_subset.1 (hJ.2 hJ.1)
      have hx : x âˆˆ Box.Icc I \ U := âŸ¨Box.coe_subset_Icc ((le_of_mem' _ J hJ.1) xJ), xnUâŸ©
      have ineq : edist (f (tâ‚ J)) (f (tâ‚‚ J)) â‰¤ EMetric.diam (f '' (ball x r âˆ© (Box.Icc I))) := by
        apply edist_le_diam_of_mem <;>
          refine Set.mem_image_of_mem f âŸ¨?_, tag_mem_Icc _ JâŸ© <;>
          refine closedBall_subset_ball (div_two_lt_of_pos r0) <| mem_closedBall_comm.1 ?_
        Â· exact hâ‚.isSubordinate.infPrepartition Ï€â‚‚.toPrepartition J hJ.1 (Box.coe_subset_Icc xJ)
        Â· exact hâ‚‚.isSubordinate.infPrepartition Ï€â‚.toPrepartition J
            ((Ï€â‚.mem_infPrepartition_comm).1 hJ.1) (Box.coe_subset_Icc xJ)
      rw [â† emetric_ball] at ineq
      simpa only [edist_le_ofReal (le_of_lt Îµâ‚0), dist_eq_norm, hJ.1] using ineq.trans (hr x hx)
    refine (norm_sum_le _ _).trans <| (sum_le_sum this).trans ?_
    rw [â† sum_mul]
    trans Î¼.toBoxAdditive I * Îµâ‚; swap
    Â· linarith
    simp_rw [mul_le_mul_right Îµâ‚0, Î¼.toBoxAdditive_apply]
    refine le_trans ?_ <| toReal_mono (lt_top_iff_ne_top.1 Î¼I) <| Î¼.mono <| un (B \ B') sdiff_subset
    simp_rw [measureReal_def]
    rw [â† toReal_sum (fun J hJ â†¦ Î¼J_ne_top J (mem_sdiff.1 hJ).1), â† Finset.tsum_subtype]
    refine (toReal_mono <| ne_of_lt <| lt_of_le_of_lt (Î¼.mono <| un (B \ B') sdiff_subset) Î¼I) ?_
    refine le_of_eq (measure_biUnion (countable_toSet _) ?_ (fun J _ â†¦ J.measurableSet_coe)).symm
    exact fun J hJ J' hJ' hJJ' â†¦ pairwiseDisjoint _ (mem_sdiff.1 hJ).1 (mem_sdiff.1 hJ').1 hJJ'
  -- The contribution of the boxes contained within U is bounded because f is bounded and Î¼ U < Îµâ‚‚.
  Â· have : âˆ€ J âˆˆ B', â€–Î¼.toBoxAdditive J â€¢ (f (tâ‚ J) - f (tâ‚‚ J))â€– â‰¤ Î¼.toBoxAdditive J * (2 * C) := by
      intro J _
      rw [norm_smul, Î¼.toBoxAdditive_apply, Real.norm_of_nonneg measureReal_nonneg, two_mul]
      gcongr
      apply norm_sub_le_of_le <;> exact hC _ (TaggedPrepartition.tag_mem_Icc _ J)
    apply (norm_sum_le_of_le B' this).trans
    simp_rw [â† sum_mul, Î¼.toBoxAdditive_apply, measureReal_def,
      â† toReal_sum (fun J hJ â†¦ Î¼J_ne_top J (hB' hJ))]
    suffices (âˆ‘ J âˆˆ B', Î¼ J).toReal â‰¤ Îµâ‚‚ by
      linarith [mul_le_mul_of_nonneg_right this <| (mul_nonneg_iff_of_pos_left two_pos).2 C0]
    rw [â† toReal_ofReal (le_of_lt Îµâ‚‚0)]
    refine toReal_mono ofReal_ne_top (le_trans ?_ (le_of_lt hU))
    trans Î¼' (â‹ƒ J âˆˆ B', J)
    Â· simp only [Î¼', Î¼.restrict_eq_self <| (un _ hB').trans I.coe_subset_Icc]
      exact le_of_eq <| Eq.symm <| measure_biUnion_finset
        (fun J hJ K hK hJK â†¦ pairwiseDisjoint _ (hB' hJ) (hB' hK) hJK) fun J _ â†¦ J.measurableSet_coe
    Â· apply Î¼'.mono
      simp_rw [iUnion_subset_iff]
      exact fun J hJ â†¦ (mem_filter.1 hJ).2

/-- A function that is bounded on a box `I` and a.e. continuous is integrable on `I`.

This is a version of `integrable_of_bounded_and_ae_continuousWithinAt` with a stronger continuity
assumption so that the user does not need to specialize the continuity assumption to each box on
which the theorem is to be applied. -/
theorem integrable_of_bounded_and_ae_continuous [CompleteSpace E] {I : Box Î¹} {f : â„â¿ â†’ E}
    (hb : âˆƒ C : â„, âˆ€ x âˆˆ Box.Icc I, â€–f xâ€– â‰¤ C) (Î¼ : Measure â„â¿) [IsLocallyFiniteMeasure Î¼]
    (hc : âˆ€áµ x âˆ‚Î¼, ContinuousAt f x) : Integrable I l f Î¼.toBoxAdditive.toSMul :=
  integrable_of_bounded_and_ae_continuousWithinAt l hb Î¼ <|
    Eventually.filter_mono (ae_mono Î¼.restrict_le_self) (hc.mono fun _ h â†¦ h.continuousWithinAt)


/-- A continuous function is box-integrable with respect to any locally finite measure.

This is true for any volume with bounded variation. -/
theorem integrable_of_continuousOn [CompleteSpace E] {I : Box Î¹} {f : â„â¿ â†’ E}
    (hc : ContinuousOn f (Box.Icc I)) (Î¼ : Measure â„â¿) [IsLocallyFiniteMeasure Î¼] :
    Integrable.{u, v, v} I l f Î¼.toBoxAdditive.toSMul := by
  apply integrable_of_bounded_and_ae_continuousWithinAt
  Â· obtain âŸ¨C, hCâŸ© := (NormedSpace.isBounded_iff_subset_smul_closedBall â„).1
                        (I.isCompact_Icc.image_of_continuousOn hc).isBounded
    use â€–Câ€–, fun x hx â†¦ by
      simpa only [smul_unitClosedBall, mem_closedBall_zero_iff] using hC (Set.mem_image_of_mem f hx)
  Â· refine eventually_of_mem ?_ (fun x hx â†¦ hc.continuousWithinAt hx)
    rw [mem_ae_iff, Î¼.restrict_apply] <;> simp [MeasurableSet.compl_iff.2 I.measurableSet_Icc]

variable {l}

/-- This is an auxiliary lemma used to prove two statements at once. Use one of the next two
lemmas instead. -/
theorem HasIntegral.of_bRiemann_eq_false_of_forall_isLittleO (hl : l.bRiemann = false)
    (B : Î¹ â†’áµ‡áµƒ[I] â„) (hB0 : âˆ€ J, 0 â‰¤ B J) (g : Î¹ â†’áµ‡áµƒ[I] F) (s : Set â„â¿) (hs : s.Countable)
    (hlH : s.Nonempty â†’ l.bHenstock = true)
    (Hâ‚ : âˆ€ (c : â„â‰¥0), âˆ€ x âˆˆ Box.Icc I âˆ© s, âˆ€ Îµ > (0 : â„),
      âˆƒ Î´ > 0, âˆ€ J â‰¤ I, Box.Icc J âŠ† Metric.closedBall x Î´ â†’ x âˆˆ Box.Icc J â†’
        (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ)
    (Hâ‚‚ : âˆ€ (c : â„â‰¥0), âˆ€ x âˆˆ Box.Icc I \ s, âˆ€ Îµ > (0 : â„),
      âˆƒ Î´ > 0, âˆ€ J â‰¤ I, Box.Icc J âŠ† Metric.closedBall x Î´ â†’ (l.bHenstock â†’ x âˆˆ Box.Icc J) â†’
        (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
    HasIntegral I l f vol (g I) := by
  /- We choose `r x` differently for `x âˆˆ s` and `x âˆ‰ s`.

    For `x âˆˆ s`, we choose `Îµs` such that `âˆ‘' x : s, Îµs x < Îµ / 2 / 2 ^ #Î¹`, then choose `r x` so
    that `dist (vol J (f x)) (g J) â‰¤ Îµs x` for `J` in the `r x`-neighborhood of `x`. This guarantees
    that the sum of these distances over boxes `J` such that `Ï€.tag J âˆˆ s` is less than `Îµ / 2`. We
    need an additional multiplier `2 ^ #Î¹` because different boxes can have the same tag.

    For `x âˆ‰ s`, we choose `r x` so that `dist (vol (J (f x))) (g J) â‰¤ (Îµ / 2 / B I) * B J` for a
    box `J` in the `Î´`-neighborhood of `x`. -/
  refine ((l.hasBasis_toFilteriUnion_top _).tendsto_iff Metric.nhds_basis_closedBall).2 ?_
  intro Îµ Îµ0
  simp only [â† exists_prop, gt_iff_lt, Subtype.exists'] at Hâ‚ Hâ‚‚
  choose! Î´â‚ HÎ´â‚ using Hâ‚
  choose! Î´â‚‚ HÎ´â‚‚ using Hâ‚‚
  have Îµ0' := half_pos Îµ0; have H0 : 0 < (2 : â„) ^ Fintype.card Î¹ := pow_pos zero_lt_two _
  rcases hs.exists_pos_forall_sum_le (div_pos Îµ0' H0) with âŸ¨Îµs, hÎµs0, hÎµsâŸ©
  simp only [le_div_iffâ‚€' H0, mul_sum] at hÎµs
  rcases exists_pos_mul_lt Îµ0' (B I) with âŸ¨Îµ', Îµ'0, hÎµIâŸ©
  classical
  set Î´ : â„â‰¥0 â†’ â„â¿ â†’ Ioi (0 : â„) := fun c x => if x âˆˆ s then Î´â‚ c x (Îµs x) else (Î´â‚‚ c) x Îµ'
  refine âŸ¨Î´, fun c => l.rCond_of_bRiemann_eq_false hl, ?_âŸ©
  simp only [mem_setOf_eq]
  rintro Ï€ âŸ¨c, hÏ€Î´, hÏ€pâŸ©
  -- Now we split the sum into two parts based on whether `Ï€.tag J` belongs to `s` or not.
  rw [â† g.sum_partition_boxes le_rfl hÏ€p, Metric.mem_closedBall, integralSum,
    â† sum_filter_add_sum_filter_not Ï€.boxes fun J => Ï€.tag J âˆˆ s,
    â† sum_filter_add_sum_filter_not Ï€.boxes fun J => Ï€.tag J âˆˆ s, â† add_halves Îµ]
  refine dist_add_add_le_of_le ?_ ?_
  Â· rcases s.eq_empty_or_nonempty with (rfl | hsne); Â· simp [Îµ0'.le]
    /- For the boxes such that `Ï€.tag J âˆˆ s`, we use the fact that at most `2 ^ #Î¹` boxes have the
        same tag. -/
    specialize hlH hsne
    have : âˆ€ J âˆˆ {J âˆˆ Ï€.boxes | Ï€.tag J âˆˆ s},
        dist (vol J (f <| Ï€.tag J)) (g J) â‰¤ Îµs (Ï€.tag J) := fun J hJ â†¦ by
      rw [Finset.mem_filter] at hJ; obtain âŸ¨hJ, hJsâŸ© := hJ
      refine HÎ´â‚ c _ âŸ¨Ï€.tag_mem_Icc _, hJsâŸ© _ (hÎµs0 _) _ (Ï€.le_of_mem' _ hJ) ?_
        (hÏ€Î´.2 hlH J hJ) fun hD => (Finset.le_sup hJ).trans (hÏ€Î´.3 hD)
      convert hÏ€Î´.1 J hJ using 3; exact (if_pos hJs).symm
    refine (dist_sum_sum_le_of_le _ this).trans ?_
    rw [sum_comp]
    refine (sum_le_sum ?_).trans (hÎµs _ ?_)
    Â· rintro b -
      rw [â† Nat.cast_two, â† Nat.cast_pow, â† nsmul_eq_mul]
      refine nsmul_le_nsmul_left (hÎµs0 _).le ?_
      refine (Finset.card_le_card ?_).trans ((hÏ€Î´.isHenstock hlH).card_filter_tag_eq_le b)
      exact filter_subset_filter _ (filter_subset _ _)
    Â· rw [Finset.coe_image, Set.image_subset_iff]
      exact fun J hJ => (Finset.mem_filter.1 hJ).2
  /- Now we deal with boxes such that `Ï€.tag J âˆ‰ s`.
    In this case the estimate is straightforward. -/
  calc
    dist (âˆ‘ J âˆˆ Ï€.boxes with tag Ï€ J âˆ‰ s, vol J (f (tag Ï€ J)))
      (âˆ‘ J âˆˆ Ï€.boxes with tag Ï€ J âˆ‰ s, g J)
      â‰¤ âˆ‘ J âˆˆ Ï€.boxes with tag Ï€ J âˆ‰ s, Îµ' * B J := dist_sum_sum_le_of_le _ fun J hJ â†¦ by
      rw [Finset.mem_filter] at hJ; obtain âŸ¨hJ, hJsâŸ© := hJ
      refine HÎ´â‚‚ c _ âŸ¨Ï€.tag_mem_Icc _, hJsâŸ© _ Îµ'0 _ (Ï€.le_of_mem' _ hJ) ?_ (fun hH => hÏ€Î´.2 hH J hJ)
        fun hD => (Finset.le_sup hJ).trans (hÏ€Î´.3 hD)
      convert hÏ€Î´.1 J hJ using 3; exact (if_neg hJs).symm
    _ â‰¤ âˆ‘ J âˆˆ Ï€.boxes, Îµ' * B J := by
      gcongr
      Â· exact fun _ _ _ â†¦ mul_nonneg Îµ'0.le (hB0 _)
      Â· apply filter_subset
    _ = B I * Îµ' := by rw [â† mul_sum, B.sum_partition_boxes le_rfl hÏ€p, mul_comm]
    _ â‰¤ Îµ / 2 := hÎµI.le

/-- A function `f` has Henstock (or `âŠ¥`) integral over `I` is equal to the value of a box-additive
function `g` on `I` provided that `vol J (f x)` is sufficiently close to `g J` for sufficiently
small boxes `J âˆ‹ x`. This lemma is useful to prove, e.g., to prove the Divergence theorem for
integral along `âŠ¥`.

Let `l` be either `BoxIntegral.IntegrationParams.Henstock` or `âŠ¥`. Let `g` a box-additive function
on subboxes of `I`. Suppose that there exists a nonnegative box-additive function `B` and a
countable set `s` with the following property.

For every `c : â„â‰¥0`, a point `x âˆˆ I.Icc`, and a positive `Îµ` there exists `Î´ > 0` such that for any
box `J â‰¤ I` such that

- `x âˆˆ J.Icc âŠ† Metric.closedBall x Î´`;
- if `l.bDistortion` (i.e., `l = âŠ¥`), then the distortion of `J` is less than or equal to `c`,

the distance between the term `vol J (f x)` of an integral sum corresponding to `J` and `g J` is
less than or equal to `Îµ` if `x âˆˆ s` and is less than or equal to `Îµ * B J` otherwise.

Then `f` is integrable on `I` along `l` with integral `g I`. -/
theorem HasIntegral.of_le_Henstock_of_forall_isLittleO (hl : l â‰¤ Henstock) (B : Î¹ â†’áµ‡áµƒ[I] â„)
    (hB0 : âˆ€ J, 0 â‰¤ B J) (g : Î¹ â†’áµ‡áµƒ[I] F) (s : Set â„â¿) (hs : s.Countable)
    (Hâ‚ : âˆ€ (c : â„â‰¥0), âˆ€ x âˆˆ Box.Icc I âˆ© s, âˆ€ Îµ > (0 : â„),
      âˆƒ Î´ > 0, âˆ€ J â‰¤ I, Box.Icc J âŠ† Metric.closedBall x Î´ â†’ x âˆˆ Box.Icc J â†’
        (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ)
    (Hâ‚‚ : âˆ€ (c : â„â‰¥0), âˆ€ x âˆˆ Box.Icc I \ s, âˆ€ Îµ > (0 : â„),
      âˆƒ Î´ > 0, âˆ€ J â‰¤ I, Box.Icc J âŠ† Metric.closedBall x Î´ â†’ x âˆˆ Box.Icc J â†’
        (l.bDistortion â†’ J.distortion â‰¤ c) â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
    HasIntegral I l f vol (g I) :=
  have A : l.bHenstock := Bool.eq_true_of_true_le hl.2.1
  HasIntegral.of_bRiemann_eq_false_of_forall_isLittleO (Bool.eq_false_of_le_false hl.1) B hB0 _ s hs
    (fun _ => A) Hâ‚ <| by simpa only [A, true_imp_iff] using Hâ‚‚

/-- Suppose that there exists a nonnegative box-additive function `B` with the following property.

For every `c : â„â‰¥0`, a point `x âˆˆ I.Icc`, and a positive `Îµ` there exists `Î´ > 0` such that for any
box `J â‰¤ I` such that

- `J.Icc âŠ† Metric.closedBall x Î´`;
- if `l.bDistortion` (i.e., `l = âŠ¥`), then the distortion of `J` is less than or equal to `c`,

the distance between the term `vol J (f x)` of an integral sum corresponding to `J` and `g J` is
less than or equal to `Îµ * B J`.

Then `f` is McShane integrable on `I` with integral `g I`. -/
theorem HasIntegral.mcShane_of_forall_isLittleO (B : Î¹ â†’áµ‡áµƒ[I] â„) (hB0 : âˆ€ J, 0 â‰¤ B J)
    (g : Î¹ â†’áµ‡áµƒ[I] F) (H : âˆ€ (_ : â„â‰¥0), âˆ€ x âˆˆ Box.Icc I, âˆ€ Îµ > (0 : â„), âˆƒ Î´ > 0, âˆ€ J â‰¤ I,
      Box.Icc J âŠ† Metric.closedBall x Î´ â†’ dist (vol J (f x)) (g J) â‰¤ Îµ * B J) :
    HasIntegral I McShane f vol (g I) :=
  (HasIntegral.of_bRiemann_eq_false_of_forall_isLittleO (l := McShane) rfl B hB0 g âˆ… countable_empty
      (fun âŸ¨_x, hxâŸ© => hx.elim) fun _ _ hx => hx.2.elim) <| by
    simpa only [McShane, Bool.coe_sort_false, false_imp_iff, true_imp_iff, diff_empty] using H

end BoxIntegral
