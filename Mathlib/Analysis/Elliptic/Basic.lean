import Mathlib

noncomputable section

section foo

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E] --[ProperSpace E]
variable [FiniteDimensional ‚Ñù E] (L : Submodule ‚Ñ§ E) [DiscreteTopology L]
variable {X : Type*} [NormedAddCommGroup X]

variable {L}

lemma IsZLattice.exists_forall_norm_lt_mul_imp_abs_repr_lt
    {Œπ : Type*} (b : Basis Œπ ‚Ñ§ L) :
    ‚àÉ (Œµ : ‚Ñù), 0 < Œµ ‚àß ‚àÄ (x : L) (n : ‚Ñï), ‚Äñx‚Äñ < n * Œµ ‚Üí ‚àÄ i, |b.repr x i| < n := by
  wlog H : IsZLattice ‚Ñù L
  ¬∑ have inst : Finite Œπ := Module.Finite.finite_basis b
    let E' := Submodule.span ‚Ñù (L : Set E)
    let L' : Submodule ‚Ñ§ E' := ZLattice.comap ‚Ñù L E'.subtype
    have inst : DiscreteTopology L' :=
      ZLattice.comap_discreteTopology _ _ (by fun_prop) Subtype.val_injective
    let e : L' ‚âÉ‚Çó[‚Ñ§] L := Submodule.comapSubtypeEquivOfLe (p := L) (q := E'.restrictScalars ‚Ñ§)
      Submodule.subset_span
    have inst : IsZLattice ‚Ñù L' := by
      refine ‚ü®Submodule.map_injective_of_injective (f := E'.subtype) Subtype.val_injective ?_‚ü©
      simp [Submodule.map_span, E', L', Set.image_preimage_eq_inter_range,
        Set.inter_eq_left.mpr Submodule.subset_span]
    obtain ‚ü®Œµ, hŒµ, H‚ü© := this (b.map e.symm) inst
    exact ‚ü®Œµ, hŒµ, fun x n h i ‚Ü¶ by simpa using H ‚ü®‚ü®x.1, Submodule.subset_span x.2‚ü©, x.2‚ü© n h i‚ü©
  have : Finite Œπ := Module.Finite.finite_basis b
  let b' : Basis Œπ ‚Ñù E := Basis.ofZLatticeBasis ‚Ñù L b
  let e := ((b'.repr ‚â™‚â´‚Çó Finsupp.linearEquivFunOnFinite _ _ _).toContinuousLinearEquiv (ùïú := ‚Ñù))
  have := e.continuous.1 (Set.univ.pi fun _ ‚Ü¶ Set.Ioo (-1) 1)
    (isOpen_set_pi Set.finite_univ fun _ _ ‚Ü¶ isOpen_Ioo)
  obtain ‚ü®Œµ, hŒµ, hŒµ'‚ü© := Metric.isOpen_iff.mp this 0 (by simp)
  refine ‚ü®Œµ, hŒµ, fun x n hxn i ‚Ü¶ ?_‚ü©
  have hn : (n : ‚Ñù) ‚â† 0 := fun e ‚Ü¶ hxn.not_ge (by simp [e])
  have : ‚Äñ(n : ‚Ñù)‚Åª¬π ‚Ä¢ x.1‚Äñ < Œµ := by
    rwa [norm_smul, norm_inv, inv_mul_lt_iff‚ÇÄ (by positivity), Real.norm_natCast]
  have : (n : ‚Ñù)‚Åª¬π * |(b.repr x i : ‚Ñù)| < 1 := by
    have := hŒµ' (by simpa using this) i trivial
    simpa [b', e, ‚Üê abs_lt, abs_mul, abs_inv] using this
  rw [inv_mul_lt_iff‚ÇÄ (by positivity), mul_one] at this
  norm_cast at this

def IsZLattice.normBound {Œπ : Type*} (b : Basis Œπ ‚Ñ§ L) : ‚Ñù :=
  (exists_forall_norm_lt_mul_imp_abs_repr_lt b).choose

lemma IsZLattice.normBound_pos {Œπ : Type*} (b : Basis Œπ ‚Ñ§ L) : 0 < normBound b :=
  (exists_forall_norm_lt_mul_imp_abs_repr_lt b).choose_spec.1

lemma IsZLattice.normBound_spec {Œπ : Type*} (b : Basis Œπ ‚Ñ§ L) (x : L) (n : ‚Ñï)
    (hxn : ‚Äñx‚Äñ < n * normBound b) (i : Œπ) : |b.repr x i| < n :=
  (exists_forall_norm_lt_mul_imp_abs_repr_lt b).choose_spec.2 x n hxn i

lemma IsZLattice.mul_normBound_le_norm {Œπ : Type*} (b : Basis Œπ ‚Ñ§ L) (x : L) (n : ‚Ñï) (i : Œπ)
    (hi : n ‚â§ |b.repr x i|) : n * normBound b ‚â§ ‚Äñx‚Äñ := by
  contrapose! hi
  exact normBound_spec b x n hi i

@[to_additive]
lemma Finset.prod_eq_prod_range_sdiff
    {Œ± Œ≤ : Type*} [DecidableEq Œ±] [CommMonoid Œ≤] (s : ‚Ñï ‚Üí Finset Œ±) (hf : Monotone s)
     (g : Œ± ‚Üí Œ≤) (n : ‚Ñï) :
    ‚àè i ‚àà s n, g i = (‚àè i ‚àà s 0, g i) * ‚àè i ‚àà range n, ‚àè j ‚àà s (i + 1) \ s i, g j := by
  have : s n = s 0 ‚à™ (range n).biUnion fun i ‚Ü¶ s (i + 1) \ s i := by
    induction n with
    | zero => simp
    | succ n IH =>
      rw [range_succ, biUnion_insert, ‚Üê union_assoc, union_right_comm, ‚Üê IH,
        union_sdiff_self_eq_union, eq_comm, union_eq_right]
      exact hf n.le_succ
  rw [this, prod_union, prod_biUnion]
  ¬∑ rintro i - j - e
    simp only [Function.onFun, disjoint_iff_inter_eq_empty]
    wlog h : j < i generalizing i j
    ¬∑ rw [‚Üê this e.symm (lt_of_le_of_ne (le_of_not_gt h) e), inter_comm]
    ext x
    simp only [mem_inter, mem_sdiff, notMem_empty, iff_false, not_and, Decidable.not_not, and_imp]
    exact fun _ h‚ÇÅ h‚ÇÇ ‚Ü¶ (h‚ÇÅ (hf h h‚ÇÇ)).elim
  ¬∑ rw [disjoint_iff_inter_eq_empty]
    ext x
    have : x ‚àà s 0 ‚Üí ‚àÄ n, x ‚àà s n := fun H n ‚Ü¶ hf (Nat.zero_le _) H
    simp +contextual [this]

open Finset in
lemma IsZLattice.sum_piFinset_Icc_rpow_le {Œπ : Type*} [Fintype Œπ] [DecidableEq Œπ]
    (b : Basis Œπ ‚Ñ§ L) {d : ‚Ñï} (hd : d = Fintype.card Œπ)
    (n : ‚Ñï) (r : ‚Ñù) (hr : r < -d) :
    ‚àë p ‚àà Fintype.piFinset fun _ : Œπ ‚Ü¶ Icc (-n : ‚Ñ§) n, ‚Äñ‚àë i, p i ‚Ä¢ b i‚Äñ ^ r ‚â§
      2 * d * 3 ^ (d - 1) * normBound b ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (d - 1 + r) := by
  let s (n : ‚Ñï) := Fintype.piFinset fun i : Œπ ‚Ü¶ Icc (-n : ‚Ñ§) n
  subst hd
  set d := Fintype.card Œπ
  have hr' : r < 0 := hr.trans_le (by linarith)
  by_cases hd : d = 0
  ¬∑ have : IsEmpty Œπ := Fintype.card_eq_zero_iff.mp hd
    simp [hd, hr'.ne]
  replace hd : 1 ‚â§ d := by rwa [Nat.one_le_iff_ne_zero]
  have hs0 : s 0 = {0} := by ext; simp [s, funext_iff]
  have hs {a b : ‚Ñï} (ha : a ‚â§ b) : s a ‚äÜ s b := by
    intros x hx
    simp only [Fintype.mem_piFinset, s] at hx ‚ä¢
    exact fun i ‚Ü¶ Icc_subset_Icc (by simpa) (by simpa) (hx i)
  have (k : ‚Ñï) : #(s (k + 1) \ s k) ‚â§ 2 * d * (2 * k + 3) ^ (d - 1) := by
    trans (2 * k + 3) ^ d - (2 * k + 1) ^ d
    ¬∑ rw [card_sdiff]
      ¬∑ simp only [Fintype.card_piFinset, Int.card_Icc, prod_const, card_univ, s, sub_neg_eq_add]
        gcongr <;> norm_cast <;> omega
      ¬∑ exact hs (by simp)
    ¬∑ have := abs_pow_sub_pow_le (Œ± := ‚Ñ§) ‚Üë(2 * k + 3) ‚Üë(2 * k + 1) d
      simp only [Nat.cast_add, Nat.cast_mul, Nat.cast_ofNat, Nat.cast_one,
        add_sub_add_left_eq_sub, Int.reduceSub, Nat.abs_ofNat, s] at this
      zify
      convert this using 3
      ¬∑ rw [abs_eq_self.mpr, Nat.cast_sub]
        ¬∑ simp
        ¬∑ gcongr; omega
        ¬∑ rw [sub_nonneg]
          gcongr; omega
      ¬∑ rw [max_eq_left, abs_eq_self.mpr]
        ¬∑ positivity
        gcongr; omega
  let Œµ := normBound b
  have hŒµ : 0 < Œµ := normBound_pos b
  calc ‚àë p ‚àà s n, ‚Äñ‚àë i, p i ‚Ä¢ b i‚Äñ ^ r
      = ‚àë k ‚àà range n, ‚àë p ‚àà (s (k + 1) \ s k), ‚Äñ‚àë i, p i ‚Ä¢ b i‚Äñ ^ r := by
        rw [Finset.sum_eq_sum_range_sdiff _ @hs]
        simp [hs0, hr'.ne]
    _ ‚â§ ‚àë k ‚àà range n, ‚àë p ‚àà (s (k + 1) \ s k), ((k + 1) * Œµ) ^ r := by
        gcongr ‚àë k ‚àà Finset.range n, ‚àë p ‚àà (s (k + 1) \ s k), ?_ with k hk v hv
        rw [‚Üê Nat.cast_one, ‚Üê Nat.cast_add]
        refine Real.rpow_le_rpow_of_nonpos (by positivity) ?_ hr'.le
        obtain ‚ü®j, hj‚ü© : ‚àÉ i, v i ‚àâ Icc (-k : ‚Ñ§) k := by simpa [s] using (mem_sdiff.mp hv).2
        refine IsZLattice.mul_normBound_le_norm b _ _ j ?_
        suffices ‚Üëk + 1 ‚â§ |v j| by simpa [Finsupp.single_apply] using this
        by_contra! H
        rw [Int.lt_add_one_iff, abs_le, ‚Üê Finset.mem_Icc] at H
        exact hj H
    _ = ‚àë k ‚àà range n, #(s (k + 1) \ s k) * ((k + 1) * Œµ) ^ r := by
      simp only [sum_const, nsmul_eq_mul]
    _ ‚â§ ‚àë k ‚àà range n, ‚Üë(2 * d * (2 * k + 3) ^ (d - 1)) * ((k + 1) * Œµ) ^ r := by
      gcongr with k hk
      ¬∑ exact this _
    _ ‚â§ ‚àë k ‚àà range n, ‚Üë(2 * d * (3 * (k + 1)) ^ (d - 1)) * ((k + 1) * Œµ) ^ r := by
      gcongr; omega
    _ = 2 * d * 3 ^ (d - 1) * Œµ ^ r * ‚àë k ‚àà range n, (k + 1) ^ (d - 1) * (k + 1 : ‚Ñù) ^ r := by
      simp_rw [Finset.mul_sum]
      congr with k
      push_cast
      rw [Real.mul_rpow, mul_pow]
      ¬∑ group
      ¬∑ positivity
      ¬∑ exact (IsZLattice.normBound_pos b).le
    _ = 2 * d * 3 ^ (d - 1) * Œµ ^ r * ‚àë k ‚àà range n, (‚Üë(k + 1) : ‚Ñù) ^ (d - 1 + r) := by
      congr with k
      rw [‚Üê Real.rpow_natCast, ‚Üê Real.rpow_add (by positivity), Nat.cast_sub hd]
      norm_cast
    _ ‚â§ 2 * d * 3 ^ (d - 1) * Œµ ^ r * ‚àë k ‚àà range (n + 1), (k : ‚Ñù) ^ (d - 1 + r) := by
      gcongr
      rw [Finset.sum_range_succ', le_add_iff_nonneg_right]
      positivity
    _ ‚â§ 2 * d * 3 ^ (d - 1) * Œµ ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (d - 1 + r) := by
      gcongr
      refine Summable.sum_le_tsum _ (fun _ _ ‚Ü¶ by positivity) ?_
      rw [Real.summable_nat_rpow]
      linarith

variable (L)

lemma IsZLattice.exists_finset_sum_norm_rpow_le_tsum :
    ‚àÉ A > (0 : ‚Ñù), ‚àÄ r < (-(Module.finrank ‚Ñ§ L) : ‚Ñù), ‚àÄ s : Finset L,
      ‚àë z ‚àà s, ‚Äñz‚Äñ ^ r ‚â§ A ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (Module.finrank ‚Ñ§ L - 1 + r) := by
  cases subsingleton_or_nontrivial L
  ¬∑ refine ‚ü®1, zero_lt_one, fun r hr s ‚Ü¶ ?_‚ü©
    have hr : r ‚â† 0 := by linarith
    simpa [Subsingleton.elim _ (0 : L), Real.zero_rpow hr] using tsum_nonneg fun _ ‚Ü¶ by positivity
  classical
  let I : Type _ := Module.Free.ChooseBasisIndex ‚Ñ§ L
  have : Fintype I := inferInstance
  let b : Basis I ‚Ñ§ L := Module.Free.chooseBasis ‚Ñ§ L
  simp_rw [Module.finrank_eq_card_basis b]
  set d := Fintype.card I
  have hd : d ‚â† 0 := by simp [d]
  let Œµ := IsZLattice.normBound b
  obtain ‚ü®A, hA, B, hB, H‚ü© : ‚àÉ A > (0 : ‚Ñù), ‚àÉ B > (0 : ‚Ñù), ‚àÄ r < (-d : ‚Ñù), ‚àÄ s : Finset L,
      ‚àë z ‚àà s, ‚Äñz‚Äñ ^ r ‚â§ A * B ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (d - 1 + r) := by
    refine ‚ü®2 * d * 3 ^ (d - 1), by positivity, Œµ, normBound_pos b, fun r hr u ‚Ü¶ ?_‚ü©
    let e : (I ‚Üí ‚Ñ§) ‚âÉ‚Çó[‚Ñ§] L := (b.repr ‚â™‚â´‚Çó Finsupp.linearEquivFunOnFinite _ _ _).symm
    obtain ‚ü®u, rfl‚ü© : ‚àÉ u' : Finset _, u = u'.image e.toEmbedding :=
      ‚ü®u.image e.symm.toEmbedding, Finset.coe_injective
        (by simpa using (e.image_symm_image _).symm)‚ü©
    dsimp
    simp only [EmbeddingLike.apply_eq_iff_eq, implies_true, Set.injOn_of_eq_iff_eq,
      Finset.sum_image, ge_iff_le]
    obtain ‚ü®n, hn‚ü© : ‚àÉ n : ‚Ñï, u ‚äÜ Fintype.piFinset fun _ : I ‚Ü¶ Finset.Icc (-n : ‚Ñ§) n := by
      obtain ‚ü®r, hr, hr'‚ü© := u.finite_toSet.isCompact.isBounded.subset_closedBall_lt 0 0
      refine ‚ü®‚åär‚åã.toNat, fun x hx ‚Ü¶ ?_‚ü©
      have hr'' : ‚åär‚åã ‚äî 0 = ‚åär‚åã := by simp; positivity
      have := hr' hx
      simp only [Metric.mem_closedBall, dist_zero_right, pi_norm_le_iff_of_nonneg hr.le,
        Int.norm_eq_abs, ‚Üê Int.cast_abs, ‚Üê Int.le_floor] at this
      simpa only [Int.ofNat_toNat, Fintype.mem_piFinset, Finset.mem_Icc, ‚Üê abs_le, hr'']
    refine (Finset.sum_le_sum_of_subset_of_nonneg hn (by intros; positivity)).trans ?_
    dsimp
    simp only [Submodule.norm_coe]
    convert IsZLattice.sum_piFinset_Icc_rpow_le b rfl n r hr with x
    simp [e, Finsupp.linearCombination]
    rfl
  have (r : ‚Ñù) : 0 ‚â§ ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (d - 1 + r) := tsum_nonneg fun _ ‚Ü¶ by positivity
  by_cases hA' : A ‚â§ 1
  ¬∑ refine ‚ü®B, hB, fun r hr s ‚Ü¶ (H r hr s).trans ?_‚ü©
    rw [mul_assoc]
    exact mul_le_of_le_one_left (mul_nonneg (by positivity) (this r)) hA'
  ¬∑ refine ‚ü®A‚Åª¬π * B, mul_pos (inv_pos.mpr hA) hB, fun r hr s ‚Ü¶ (H r hr s).trans ?_‚ü©
    rw [Real.mul_rpow (inv_pos.mpr hA).le hB.le, mul_assoc, mul_assoc]
    refine mul_le_mul_of_nonneg_right ?_ (mul_nonneg (by positivity) (this r))
    rw [‚Üê Real.rpow_neg_one, ‚Üê Real.rpow_mul hA.le]
    refine Real.self_le_rpow_of_one_le (not_le.mp hA').le ?_
    simp only [neg_mul, one_mul, le_neg (b := r)]
    refine hr.le.trans ?_
    norm_num
    exact Nat.one_le_iff_ne_zero.mpr hd

def IsZLattice.tsumNormRPowBound : ‚Ñù :=
  (IsZLattice.exists_finset_sum_norm_rpow_le_tsum L).choose

lemma IsZLattice.tsumNormRPowBound_pos : 0 < tsumNormRPowBound L :=
  (IsZLattice.exists_finset_sum_norm_rpow_le_tsum L).choose_spec.1

lemma IsZLattice.tsumNormRPowBound_spec (r : ‚Ñù) (h : r < -(Module.finrank ‚Ñ§ L)) (s : Finset L) :
    ‚àë z ‚àà s, ‚Äñz‚Äñ ^ r ‚â§
      (tsumNormRPowBound L) ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (Module.finrank ‚Ñ§ L - 1 + r) :=
  (IsZLattice.exists_finset_sum_norm_rpow_le_tsum L).choose_spec.2 r h s

/-- If `L` is a `‚Ñ§`-lattice with rank `d` in `E`, then `‚àë z ‚àà L, ‚Äñz‚Äñ ≥` converges when `r < -d`. -/
lemma IsZLattice.summable_norm_rpow (r : ‚Ñù) (hr : r < -(Module.finrank ‚Ñ§ L)) :
    Summable fun z : L ‚Ü¶ ‚Äñz‚Äñ ^ r :=
  summable_of_sum_le (fun _ ‚Ü¶ by positivity) (tsumNormRPowBound_spec L r hr)

lemma IsZLattice.tsum_norm_rpow_le (r : ‚Ñù) (hr : r < -(Module.finrank ‚Ñ§ L)) :
    ‚àë' z : L, ‚Äñz‚Äñ ^ r ‚â§
      (tsumNormRPowBound L) ^ r * ‚àë' k : ‚Ñï, (k : ‚Ñù) ^ (Module.finrank ‚Ñ§ L - 1 + r) :=
  Summable.tsum_le_of_sum_le (summable_norm_rpow L r hr) (tsumNormRPowBound_spec L r hr)

lemma IsZLattice.summable_norm_sub_rpow (r : ‚Ñù) (hr : r < -(Module.finrank ‚Ñ§ L)) (x : E) :
    Summable fun z : L ‚Ü¶ ‚Äñz - x‚Äñ ^ r := by
  cases subsingleton_or_nontrivial L
  ¬∑ exact .of_finite
  refine Summable.of_norm_bounded_eventually
    (.mul_left ((1 / 2) ^ r) (IsZLattice.summable_norm_rpow L r hr)) ?_
  have H : IsClosed (X := E) L := @AddSubgroup.isClosed_of_discrete _ _ _ _ _
    L.toAddSubgroup (inferInstanceAs (DiscreteTopology L))
  have := ProperSpace.of_isClosed H
  refine ((Metric.isCompact_of_isClosed_isBounded Metric.isClosed_closedBall
    ((Metric.isBounded_closedBall (x := (0 : L)) (r := 2 * ‚Äñx‚Äñ)))).finite inferInstance).subset ?_
  intro t ht
  by_cases ht‚ÇÅ : ‚Äñt‚Äñ = 0
  ¬∑ simp [show t = 0 by simpa using ht‚ÇÅ]
  by_cases ht‚ÇÇ : ‚Äñt - x‚Äñ = 0
  ¬∑ simp [show t = x by simpa [sub_eq_zero] using ht‚ÇÇ, two_mul]
  have : 0 < Module.finrank ‚Ñ§ L := Module.finrank_pos
  have : ‚Äñt - x‚Äñ < 2‚Åª¬π * ‚Äñt‚Äñ := by
    rw [‚Üê Real.rpow_lt_rpow_iff_of_neg (z := r)
      (by positivity) (by positivity) (hr.trans (by simpa))]
    simpa [Real.mul_rpow, abs_eq_self.mpr (show 0 ‚â§ ‚Äñt - x‚Äñ ^ r by positivity)] using ht
  have := (norm_sub_norm_le _ _).trans_lt this
  rw [sub_lt_iff_lt_add, ‚Üê sub_lt_iff_lt_add', AddSubgroupClass.coe_norm] at this
  simp only [Metric.mem_closedBall, dist_zero_right, AddSubgroupClass.coe_norm, ge_iff_le]
  linarith

lemma IsZLattice.summable_norm_sub_zpow (r : ‚Ñ§) (hr : r < -(Module.finrank ‚Ñ§ L)) (x : E) :
    Summable fun z : L ‚Ü¶ ‚Äñz - x‚Äñ ^ r := by
  exact_mod_cast summable_norm_sub_rpow L r (by exact_mod_cast hr) x

lemma deriv_one_div_sub_pow (r : ‚Ñï) (z w : ‚ÑÇ) :
    deriv (fun z : ‚ÑÇ ‚Ü¶ 1 / (z - w) ^ r) z = - (r / (z - w) ^ (r + 1)) := by
  rw [deriv_comp_sub_const (f := fun z ‚Ü¶ 1 / z ^ r) (a := w)]
  simp only [one_div, ‚Üê zpow_natCast, ‚Üê zpow_neg, deriv_zpow]
  rw [sub_eq_add_neg (-r : ‚Ñ§), ‚Üê neg_add, zpow_neg]
  norm_cast
  field_simp

lemma iteratedDeriv_one_div_sub_pow (i : ‚Ñï) (r : ‚Ñï) (z w : ‚ÑÇ) :
    iteratedDeriv i (fun z : ‚ÑÇ ‚Ü¶ 1 / (z - w) ^ r) z =
      (-1) ^ i * r.ascFactorial i * (1 / (z - w) ^ (r + i)) := by
  revert z
  apply funext_iff.mp
  induction i with
  | zero => simp
  | succ n IH =>
    rw [iteratedDeriv_succ, IH]
    simp only [deriv_const_mul_field', deriv_one_div_sub_pow, Nat.ascFactorial_succ]
    ext
    simp
    ring

lemma deriv_one_div_sub_pow' (r : ‚Ñï) (z w : ‚ÑÇ) :
    deriv (fun z : ‚ÑÇ ‚Ü¶ 1 / (w - z) ^ r) z = r / (w - z) ^ (r + 1) := by
  rw [deriv_comp_const_sub (f := fun z ‚Ü¶ 1 / z ^ r) (a := w)]
  simp only [one_div, ‚Üê zpow_natCast, ‚Üê zpow_neg, deriv_zpow]
  rw [sub_eq_add_neg (-r : ‚Ñ§), ‚Üê neg_add, zpow_neg]
  norm_cast
  field_simp

lemma iteratedDeriv_one_div_sub_pow' (i : ‚Ñï) (r : ‚Ñï) (z w : ‚ÑÇ) :
    iteratedDeriv i (fun z : ‚ÑÇ ‚Ü¶ 1 / (w - z) ^ r) z =
      r.ascFactorial i * (1 / (w - z) ^ (r + i)) := by
  revert z
  apply funext_iff.mp
  induction i with
  | zero => simp
  | succ n IH =>
    rw [iteratedDeriv_succ, IH]
    simp only [deriv_const_mul_field', deriv_one_div_sub_pow', Nat.ascFactorial_succ]
    ext
    simp
    ring

lemma Nat.two_ascFactorial (i) : Nat.ascFactorial 2 i = i.factorial * (i + 1) := by
  rw [‚Üê Nat.one_ascFactorial, mul_comm, add_comm, ‚Üê Nat.succ_ascFactorial, one_mul]

lemma Complex.tsum_eq_one_div_sub_sq (w z : ‚ÑÇ) (hz : ‚Äñz‚Äñ < ‚Äñw‚Äñ) :
    ‚àë' i : ‚Ñï, (i + 1) * w ^ (- ‚Üë(i + 2) : ‚Ñ§) * z ^ i = 1 / (z - w) ^ 2 := by
  convert Complex.taylorSeries_eq_on_ball' (f := fun z ‚Ü¶ 1 / (z - w) ^ 2)
    (show z ‚àà Metric.ball 0 ‚Äñw‚Äñ by simpa using hz) _ using 4 with i
  ¬∑ rw [iteratedDeriv_one_div_sub_pow, eq_inv_mul_iff_mul_eq‚ÇÄ (by norm_cast; positivity),
      ‚Üê mul_assoc, mul_comm ((-1) ^ _), mul_assoc _ ((-1) ^ _), Nat.two_ascFactorial]
    congr 1
    ¬∑ norm_cast
    rw [zpow_neg, zpow_natCast, zero_sub, ‚Üê neg_one_mul w, mul_pow]
    ring_nf
    rw [mul_assoc, ‚Üê mul_pow, inv_mul_cancel‚ÇÄ (by simp)]
    norm_num
  ¬∑ simp
  ¬∑ exact .div (by fun_prop) (by fun_prop) (by simp [sub_eq_zero, imp_not_comm])

lemma Complex.tsum_add_const_mul_pow (z a : ‚ÑÇ) (hz : ‚Äñz‚Äñ < 1) :
    ‚àë' i : ‚Ñï, (i + a) * z ^ i = (a - 1) * 1 / (1 - z) + 1 / (1 - z) ^ 2 := by
  convert Complex.taylorSeries_eq_on_ball'
    (f := (fun z ‚Ü¶ (a - 1) * (1 / (1 - z) ^ 1)) + (fun z ‚Ü¶ 1 / (1 - z) ^ 2))
    (show z ‚àà Metric.ball 0 1 by simpa using hz) _ using 1
  ¬∑ rw [sub_zero]
    congr! 3 with i
    rw [iteratedDeriv_add, iteratedDeriv_const_mul, iteratedDeriv_one_div_sub_pow',
      iteratedDeriv_one_div_sub_pow', Nat.two_ascFactorial, Nat.one_ascFactorial]
    ¬∑ simp only [sub_zero, one_pow, ne_eq, one_ne_zero, not_false_eq_true, div_self, mul_one,
        Nat.cast_mul, Nat.cast_add, Nat.cast_one]
      rw [mul_comm (a - 1), ‚Üê mul_add, sub_add_add_cancel, add_comm, inv_mul_cancel_left‚ÇÄ]
      norm_num
      positivity
    ¬∑ exact .div contDiffAt_const (.pow (by fun_prop) _) (by simp)
    ¬∑ exact .mul (by fun_prop) (.div (by fun_prop) (.pow (by fun_prop) _) (by simp))
    ¬∑ exact .div (by fun_prop) (.pow (by fun_prop) _) (by simp)
  ¬∑ simp [div_eq_mul_inv]
  ¬∑ refine .add (.mul ?_ (.div ?_ ?_ (by simp [sub_eq_zero, imp_not_comm])))
      (.div ?_ ?_ (by simp [sub_eq_zero, imp_not_comm])) <;> fun_prop

lemma Real.tsum_eq_one_div_sub_sq (w z : ‚Ñù) (hz : |z| < |w|) :
    ‚àë' i : ‚Ñï, (i + 1) * w ^ (- ‚Üë(i + 2) : ‚Ñ§) * z ^ i = 1 / (z - w) ^ 2 := by
  exact_mod_cast Complex.tsum_eq_one_div_sub_sq w z (by simpa)

lemma Real.tsum_add_const_mul_pow (a z : ‚Ñù) (hz : |z| < 1) :
    ‚àë' i : ‚Ñï, (i + a) * z ^ i = (a - 1) * 1 / (1 - z) + 1 / (1 - z) ^ 2 := by
  exact_mod_cast Complex.tsum_add_const_mul_pow z a (by simpa)

lemma tsum_eq_one_div_sub_sq_sub_one_div_sq (w z x : ‚ÑÇ) (hz : ‚Äñz - x‚Äñ < ‚Äñw - x‚Äñ) :
    1 / (z - w) ^ 2 - 1 / w ^ 2 = ‚àë' i : ‚Ñï,
      ((i + 1) * (w - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (w ^ (-2 : ‚Ñ§)) 0) * (z - x) ^ i := by
  have := Complex.tsum_eq_one_div_sub_sq (w - x) (z - x) hz
  rw [sub_sub_sub_cancel_right] at this
  rw [‚Üê this, ‚Üê tsum_ite_eq 0 (1 / w ^ 2), ‚Üê Summable.tsum_sub]
  ¬∑ simp_rw [sub_mul]
    congr! 3 with i
    cases i <;> simp [zpow_ofNat]
  ¬∑ by_contra H
    obtain rfl : z = w := by
      simpa [sub_eq_zero] using this.symm.trans (tsum_eq_zero_of_not_summable H)
    simp at hz
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton 0).subset (by simp_all))

def optionProdEquiv {Œ± Œ≤ : Type*} : Option Œ± √ó Œ≤ ‚âÉ Œ≤ ‚äï Œ± √ó Œ≤ where
  toFun x := x.1.casesOn (.inl x.2) (fun a ‚Ü¶ .inr (a, x.2))
  invFun x := x.casesOn (Prod.mk none) (Prod.map some id)
  left_inv
  | (none, _) => rfl
  | (some _, _) => rfl
  right_inv
  | .inl _ => rfl
  | .inr (_, _) => rfl

lemma not_continuousAt_inv_sq_sub (x : ‚ÑÇ) : ¬¨ ContinuousAt (fun z ‚Ü¶ ((z - x) ^ 2)‚Åª¬π) x := by
  intro H
  have := H (U := Metric.ball 0 1) (Metric.isOpen_ball.mem_nhds (by simp))
  have : {x} ‚àà nhds x := by
    convert (nhds x).inter_mem this (Metric.isOpen_ball (x := x) (Œµ := 1).mem_nhds (by simp))
    ext a
    by_cases hx : a = x
    ¬∑ simp [hx]
    ¬∑ simp +contextual [hx, inv_lt_one_iff‚ÇÄ, Complex.dist_eq, sq_nonpos_iff, le_of_lt, sub_eq_zero]
  have := isOpen_singleton_of_finite_mem_nhds _ this (Set.finite_singleton x)
  exact not_isOpen_singleton _ this

@[simp]
lemma Nat.rec_succ {C : ‚Ñï ‚Üí Sort*} (h0 : C 0) (h : ‚àÄ n, C n ‚Üí C (n + 1)) (n : ‚Ñï) :
    Nat.rec (motive := C) h0 h ofNat(n + 1) = h n (Nat.rec h0 h n) := rfl

lemma Int.le_floor_add_one {Œ± : Type*}
    [Ring Œ±] [LinearOrder Œ±] [IsStrictOrderedRing Œ±] [FloorRing Œ±] (a : Œ±) :
    a ‚â§ ‚Üë‚åäa‚åã + 1 :=
  (Int.le_ceil a).trans (by norm_cast; exact Int.ceil_le_floor_add_one a)

lemma IsZLattice.isCompact_range_of_periodic
    {E F : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]
    [FiniteDimensional ‚Ñù E] (L : Submodule ‚Ñ§ E) [DiscreteTopology L] [IsZLattice ‚Ñù L] (f : E ‚Üí F)
    [TopologicalSpace F] (hf : Continuous f)
    (hf' : ‚àÄ z w, w ‚àà L ‚Üí f (z + w) = f z) : IsCompact (Set.range f) := by
  have := ZLattice.module_free ‚Ñù L
  let b := Module.Free.chooseBasis ‚Ñ§ L
  convert (b.ofZLatticeBasis ‚Ñù).parallelepiped.isCompact.image hf
  refine le_antisymm ?_ (Set.image_subset_range _ _)
  rintro _ ‚ü®x, rfl‚ü©
  let x' : L := b.repr.symm (Finsupp.equivFunOnFinite.symm
    fun i ‚Ü¶ ‚åä(b.ofZLatticeBasis ‚Ñù).repr x i‚åã)
  refine ‚ü®x + (- x'), ?_, hf' _ _ (- x').2‚ü©
  simp [parallelepiped_basis_eq, x', Int.floor_le, Int.le_floor_add_one, add_comm (1 : ‚Ñù)]

end foo


structure PeriodPairs : Type where
  œâ‚ÇÅ : ‚ÑÇ
  œâ‚ÇÇ : ‚ÑÇ
  indep : LinearIndependent ‚Ñù ![œâ‚ÇÅ, œâ‚ÇÇ]

variable {M : Type*} [AddCommMonoid M] [TopologicalSpace M] (L : PeriodPairs)

def PeriodPairs.basis : Basis (Fin 2) ‚Ñù ‚ÑÇ :=
  basisOfLinearIndependentOfCardEqFinrank L.indep (by simp)

@[simp] lemma PeriodPairs.basis_zero : L.basis 0 = L.œâ‚ÇÅ := by simp [basis]
@[simp] lemma PeriodPairs.basis_one : L.basis 1 = L.œâ‚ÇÇ := by simp [basis]

def PeriodPairs.lattice : Submodule ‚Ñ§ ‚ÑÇ := Submodule.span ‚Ñ§ {L.œâ‚ÇÅ, L.œâ‚ÇÇ}

lemma PeriodPairs.mem_lattice {L : PeriodPairs} {x : ‚ÑÇ} :
    x ‚àà L.lattice ‚Üî ‚àÉ m n : ‚Ñ§, m * L.œâ‚ÇÅ + n * L.œâ‚ÇÇ = x := by
  simp only [lattice, Submodule.mem_span_pair, zsmul_eq_mul]

lemma PeriodPairs.œâ‚ÇÅ_div_two_not_mem_lattice : L.œâ‚ÇÅ / 2 ‚àâ L.lattice := by
  intro h
  obtain ‚ü®m, n, e‚ü© := mem_lattice.mp h
  rw [‚Üê sub_eq_zero] at e
  have := (LinearIndependent.pair_iff.mp L.indep (m - 1/2) n (by convert e using 1; simp; ring)).1
  rw [sub_eq_zero, eq_div_iff (by norm_num)] at this
  norm_cast at this
  exact Int.two_dvd_ne_zero (n := 1).mpr rfl ‚ü®m, by linarith‚ü©

lemma PeriodPairs.œâ‚ÇÇ_div_two_not_mem_lattice : L.œâ‚ÇÇ / 2 ‚àâ L.lattice := by
  intro h
  obtain ‚ü®m, n, e‚ü© := mem_lattice.mp h
  rw [‚Üê sub_eq_zero] at e
  have := (LinearIndependent.pair_iff.mp L.indep m (n - 1/2) (by convert e using 1; simp; ring)).2
  rw [sub_eq_zero, eq_div_iff (by norm_num)] at this
  norm_cast at this
  exact Int.two_dvd_ne_zero (n := 1).mpr rfl ‚ü®n, by linarith‚ü©

-- helper lemma to connect to the ZLattice API
lemma PeriodPairs.lattice_eq_span_range_basis :
    L.lattice = Submodule.span ‚Ñ§ (Set.range ‚áëL.basis) := by
  have : Finset.univ (Œ± := Fin 2) = {0, 1} := rfl
  rw [PeriodPairs.lattice, ‚Üê Set.image_univ, ‚Üê Finset.coe_univ, this]
  simp [Set.image_insert_eq]

instance : DiscreteTopology L.lattice := by
  rw [L.lattice_eq_span_range_basis]
  infer_instance

instance : IsZLattice ‚Ñù L.lattice := by
  simp_rw [L.lattice_eq_span_range_basis]
  infer_instance

lemma PeriodPairs.isClosed_lattice : IsClosed (X := ‚ÑÇ) L.lattice :=
  @AddSubgroup.isClosed_of_discrete _ _ _ _ _ L.lattice.toAddSubgroup
    (inferInstanceAs (DiscreteTopology L.lattice))

lemma PeriodPairs.isClosed_lattice_sdiff (s : Set ‚ÑÇ) : IsClosed (L.lattice \ s) := by
  convert L.isClosed_lattice.isClosedMap_subtype_val _ (isClosed_discrete ((‚Üë) ‚Åª¬π' s·∂ú))
  ¬∑ convert Set.image_preimage_eq_inter_range.symm using 1; simp [Set.diff_eq_compl_inter]
  ¬∑ exact inferInstanceAs (DiscreteTopology L.lattice)

instance PeriodPairs.properSpace_lattice : ProperSpace L.lattice :=
  .of_isClosed L.isClosed_lattice

lemma PeriodPairs.œâ‚ÇÅ_mem_lattice : L.œâ‚ÇÅ ‚àà L.lattice := Submodule.subset_span (by simp)

lemma PeriodPairs.œâ‚ÇÇ_mem_lattice : L.œâ‚ÇÇ ‚àà L.lattice := Submodule.subset_span (by simp)

def PeriodPairs.latticeBasis : Basis (Fin 2) ‚Ñ§ L.lattice :=
  (Basis.span (v := ![L.œâ‚ÇÅ, L.œâ‚ÇÇ]) (L.indep.restrict_scalars' _)).map
    (.ofEq _ _ (by simp [lattice, Set.pair_comm L.œâ‚ÇÇ L.œâ‚ÇÅ]))

@[simp]
lemma PeriodPairs.latticeBasis_zero : L.latticeBasis 0 = L.œâ‚ÇÅ := by
  simp [latticeBasis, Basis.span_apply]

@[simp]
lemma PeriodPairs.latticeBasis_one : L.latticeBasis 1 = L.œâ‚ÇÇ := by
  simp [latticeBasis, Basis.span_apply]

@[simp]
lemma PeriodPairs.finrank_lattice : Module.finrank ‚Ñ§ L.lattice = 2 :=
    Module.finrank_eq_card_basis L.latticeBasis

def PeriodPairs.latticeEquivProd : L.lattice ‚âÉ‚Çó[‚Ñ§] ‚Ñ§ √ó ‚Ñ§ :=
  L.latticeBasis.repr ‚â™‚â´‚Çó Finsupp.linearEquivFunOnFinite _ _ _ ‚â™‚â´‚Çó .finTwoArrow ‚Ñ§ ‚Ñ§

lemma PeriodPairs.latticeEquiv_symm_apply (x : ‚Ñ§ √ó ‚Ñ§) :
    (L.latticeEquivProd.symm x).1 = x.1 * L.œâ‚ÇÅ + x.2 * L.œâ‚ÇÇ := by
  simp [latticeEquivProd, Finsupp.linearCombination]
  rfl

open Topology Filter in
lemma PeriodPairs.tendstoLocallyUniformly_aux (f : L.lattice ‚Üí ‚ÑÇ ‚Üí ‚ÑÇ)
    (u : ‚Ñù ‚Üí L.lattice ‚Üí ‚Ñù) (hu : ‚àÄ r > 0, Summable (u r))
    (hf : ‚àÄ r > 0, ‚àÄ·∂† (R : ‚Ñù) in atTop, ‚àÄ x, ‚Äñx‚Äñ < r ‚Üí
      ‚àÄ l : L.lattice, ‚Äñl.1‚Äñ = R ‚Üí ‚Äñf l x‚Äñ ‚â§ u r l) :
    TendstoLocallyUniformly (‚àë i ‚àà ¬∑, f i ¬∑) (‚àë' j, f j ¬∑) Filter.atTop := by
  rw [tendstoLocallyUniformly_iff_filter]
  intro x
  obtain ‚ü®r, hr, hr'‚ü© : ‚àÉ r, 0 < r ‚àß ùìù x ‚â§ ùìü (Metric.ball 0 r) :=
    ‚ü®‚Äñx‚Äñ + 1, by positivity, Filter.le_principal_iff.mpr (Metric.isOpen_ball.mem_nhds (by simp))‚ü©
  refine .mono_right ?_ hr'
  rw [‚Üê tendstoUniformlyOn_iff_tendstoUniformlyOnFilter]
  refine tendstoUniformlyOn_tsum_of_cofinite_eventually (hu r hr) ?_
  obtain ‚ü®R, hR‚ü© := eventually_atTop.mp (hf r hr)
  refine (isCompact_iff_finite.mp (isCompact_closedBall (0 : L.lattice) R)).subset ?_
  intros l hl
  simp only [Metric.mem_ball, dist_zero_right, Set.mem_compl_iff, Set.mem_setOf_eq, not_forall,
    not_le, Metric.mem_closedBall, AddSubgroupClass.coe_norm] at hl ‚ä¢
  obtain ‚ü®s, hs, hs'‚ü© := hl
  contrapose! hs'
  exact hR _ hs'.le _ hs _ rfl

lemma PeriodPairs.‚Ñò_bound (r : ‚Ñù) (hr : r > 0) (s : ‚ÑÇ) (hs : ‚Äñs‚Äñ < r)
    (l : ‚ÑÇ) (h : 2 * r ‚â§ ‚Äñl‚Äñ) :
    ‚Äñ1 / (s - l) ^ 2 - 1 / l ^ 2‚Äñ ‚â§ 10 * r * ‚Äñl‚Äñ ^ (-3 : ‚Ñù) := by
  have : s ‚â† ‚Üël := by rintro rfl; exfalso; linarith
  have : 0 < ‚Äñl‚Äñ := by
    suffices l ‚â† 0 by simpa
    rintro rfl
    simp only [norm_zero] at h
    linarith
  calc
    _ = ‚Äñ(‚Üël ^ 2 - (s - ‚Üël) ^ 2) / ((s - ‚Üël) ^ 2 * ‚Üël ^ 2)‚Äñ := by
      rw [div_sub_div, one_mul, mul_one]
      ¬∑ simpa [sub_eq_zero]
      ¬∑ simpa
    _ = ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / (‚Äñs - l‚Äñ ^ 2 * ‚Äñl‚Äñ ^ 2) := by simp
    _ ‚â§ ‚Äñl ^ 2 - (s - l) ^ 2‚Äñ / ((‚Äñl‚Äñ / 2) ^ 2 * ‚Äñl‚Äñ ^ 2) := by
      gcongr
      rw [norm_sub_rev]
      exact .trans (by linarith) (norm_sub_norm_le l s)
    _ = ‚Äñs * (2 * l - s)‚Äñ / (‚Äñl‚Äñ ^ 4 / 4) := by
      congr 1
      ¬∑ rw [sq_sub_sq]; simp [‚Üê sub_add, two_mul, sub_add_eq_add_sub]
      ¬∑ ring
    _ = (‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / (‚Äñl‚Äñ ^ 4 / 4) := by simp
    _ = (4 * ‚Äñs‚Äñ * ‚Äñ2 * l - s‚Äñ) / ‚Äñl‚Äñ ^ 4 := by field_simp; ring
    _ ‚â§ (4 * r * (2.5 * ‚Äñl‚Äñ)) / ‚Äñl‚Äñ ^ 4 := by
      gcongr (4 * ?_ * ?_) / ‚Äñl‚Äñ ^ 4
      refine (norm_sub_le _ _).trans ?_
      simp only [Complex.norm_mul, Complex.norm_ofNat]
      linarith
    _ = 10 * r / ‚Äñl‚Äñ ^ 3 := by field_simp; ring
    _ = _ := by norm_cast

section ‚ÑòExcept

def PeriodPairs.‚ÑòExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l.1 = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2)

lemma PeriodPairs.tendstoLocallyUniformly_‚ÑòExcept (l‚ÇÄ : ‚ÑÇ) :
    TendstoLocallyUniformly
      (fun (s : Finset L.lattice) (z : ‚ÑÇ) ‚Ü¶
        ‚àë l ‚àà s, if l.1 = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      (L.‚ÑòExcept l‚ÇÄ) Filter.atTop := by
  refine L.tendstoLocallyUniformly_aux (u := (10 * ¬∑ * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (IsZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simp; positivity
  ¬∑ exact PeriodPairs.‚Ñò_bound r hr s hs l h

lemma PeriodPairs.hasSum_‚ÑòExcept (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l.1 = l‚ÇÄ then 0 else (1 / (z - l) ^ 2 - 1 / l ^ 2))
      (L.‚ÑòExcept l‚ÇÄ z) :=
  (L.tendstoLocallyUniformly_‚ÑòExcept l‚ÇÄ).tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma PeriodPairs.differentiableOn_‚ÑòExcept (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ (L.‚ÑòExcept l‚ÇÄ) (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine (L.tendstoLocallyUniformly_‚ÑòExcept l‚ÇÄ).tendstoLocallyUniformlyOn.differentiableOn
    (.of_forall fun s ‚Ü¶ .sum fun i hi ‚Ü¶ ?_) (L.isClosed_lattice_sdiff _).isOpen_compl
  split_ifs
  ¬∑ simp
  refine .sub (.div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_) (by fun_prop)
  have : x ‚â† i := by rintro rfl; simp_all
  simpa [sub_eq_zero]

lemma PeriodPairs.‚ÑòExcept_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : L.‚ÑòExcept l‚ÇÄ (-z) = L.‚ÑòExcept (-l‚ÇÄ) z := by
  simp only [‚ÑòExcept]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  simp
  ring

end ‚ÑòExcept

section ‚Ñò

def PeriodPairs.‚Ñò (z : ‚ÑÇ) : ‚ÑÇ := ‚àë' l : L.lattice, (1 / (z - l) ^ 2 - 1 / l ^ 2)

lemma PeriodPairs.‚ÑòExcept_add (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    L.‚ÑòExcept l‚ÇÄ z + (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) = L.‚Ñò z := by
  trans L.‚ÑòExcept l‚ÇÄ z +
    ‚àë' i : L.lattice, if i.1 = l‚ÇÄ.1 then (1 / (z - l‚ÇÄ.1) ^ 2 - 1 / l‚ÇÄ.1 ^ 2) else 0
  ¬∑ simp
  rw [‚ÑòExcept, ‚Üê Summable.tsum_add]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *]
  ¬∑ exact ‚ü®_, L.hasSum_‚ÑòExcept _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma PeriodPairs.‚ÑòExcept_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    L.‚ÑòExcept l‚ÇÄ z = L.‚Ñò z + (1 / l‚ÇÄ.1 ^ 2 - 1 / (z - l‚ÇÄ.1) ^ 2) := by
  rw [‚Üê L.‚ÑòExcept_add l‚ÇÄ]
  abel

lemma PeriodPairs.‚ÑòExcept_of_not_mem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    L.‚ÑòExcept l‚ÇÄ = L.‚Ñò := by
  delta ‚ÑòExcept ‚Ñò
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this]

lemma PeriodPairs.tendstoLocallyUniformly_‚Ñò :
    TendstoLocallyUniformly
      (fun (s : Finset L.lattice) (z : ‚ÑÇ) ‚Ü¶ ‚àë l ‚àà s, (1 / (z - l) ^ 2 - 1 / l ^ 2))
      L.‚Ñò Filter.atTop := by
  convert L.tendstoLocallyUniformly_‚ÑòExcept (L.œâ‚ÇÅ / 2) using 4 with s z l hl
  ¬∑ rw [if_neg]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_not_mem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]

lemma PeriodPairs.hasSum_‚Ñò (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ (1 / (z - l) ^ 2 - 1 / l ^ 2)) (L.‚Ñò z) :=
  L.tendstoLocallyUniformly_‚Ñò.tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma PeriodPairs.differentiableOn_‚Ñò :
    DifferentiableOn ‚ÑÇ L.‚Ñò L.lattice·∂ú := by
  rw [‚Üê L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  convert L.differentiableOn_‚ÑòExcept _
  simp [Set.ext_iff, imp_not_comm, L.œâ‚ÇÅ_div_two_not_mem_lattice]

@[simp]
lemma PeriodPairs.‚Ñò_neg (z : ‚ÑÇ) : L.‚Ñò (-z) = L.‚Ñò z := by
  simp only [‚Ñò]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  congr with l
  simp
  ring

end ‚Ñò

section ‚Ñò'Except

def PeriodPairs.‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : ‚ÑÇ :=
  ‚àë' l : L.lattice, if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3

lemma PeriodPairs.tendstoLocallyUniformly_‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) :
    TendstoLocallyUniformly
      (fun (s : Finset L.lattice) (z : ‚ÑÇ) ‚Ü¶ ‚àë l ‚àà s, if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3)
      (L.‚Ñò'Except l‚ÇÄ) Filter.atTop := by
  refine L.tendstoLocallyUniformly_aux (u := fun _ ‚Ü¶ (16 * ‚Äñ¬∑‚Äñ ^ (-3 : ‚Ñù))) _
    (fun _ _ ‚Ü¶ (IsZLattice.summable_norm_rpow _ _ (by simp; norm_num)).mul_left _) fun r hr ‚Ü¶
    Filter.eventually_atTop.mpr ‚ü®2 * r, ?_‚ü©
  rintro _ h s hs l rfl
  split_ifs
  ¬∑ simp; positivity
  have : s ‚â† ‚Üël := by rintro rfl; exfalso; linarith
  have : l ‚â† 0 := by rintro rfl; simp_all; linarith
  simp only [Complex.norm_div, norm_neg, Complex.norm_ofNat, norm_pow, AddSubgroupClass.coe_norm]
  rw [Real.rpow_neg (by positivity), ‚Üê div_eq_mul_inv, div_le_div_iff‚ÇÄ, norm_sub_rev]
  ¬∑ refine LE.le.trans_eq (b := 2 * (2 * ‚Äñl - s‚Äñ) ^ 3) ?_ (by ring)
    norm_cast
    gcongr
    refine le_trans ?_ (mul_le_mul le_rfl (norm_sub_norm_le _ _) (by linarith) (by linarith))
    norm_cast at *
    linarith
  ¬∑ exact pow_pos (by simpa [sub_eq_zero]) _
  ¬∑ exact Real.rpow_pos_of_pos (by simpa) _

lemma PeriodPairs.hasSum_‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ if l.1 = l‚ÇÄ then 0 else -2 / (z - l) ^ 3)
      (L.‚Ñò'Except l‚ÇÄ z) :=
  (L.tendstoLocallyUniformly_‚Ñò'Except l‚ÇÄ).tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma PeriodPairs.differentiableOn_‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) :
    DifferentiableOn ‚ÑÇ (L.‚Ñò'Except l‚ÇÄ) (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine (L.tendstoLocallyUniformly_‚Ñò'Except l‚ÇÄ).tendstoLocallyUniformlyOn.differentiableOn
    (.of_forall fun s ‚Ü¶ .sum fun i hi ‚Ü¶ ?_) (L.isClosed_lattice_sdiff _).isOpen_compl
  split_ifs
  ¬∑ simp
  refine .div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_
  have : x ‚â† i := by rintro rfl; simp_all
  simpa [sub_eq_zero]

lemma PeriodPairs.eqOn_deriv_‚ÑòExcept_‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) :
    Set.EqOn (deriv (L.‚ÑòExcept l‚ÇÄ)) (L.‚Ñò'Except l‚ÇÄ) (L.lattice \ {l‚ÇÄ})·∂ú := by
  refine ((L.tendstoLocallyUniformly_‚ÑòExcept l‚ÇÄ).tendstoLocallyUniformlyOn.deriv
    (.of_forall fun s ‚Ü¶ ?_) (L.isClosed_lattice_sdiff _).isOpen_compl).unique ?_
  ¬∑ refine .sum fun i hi ‚Ü¶ ?_
    split_ifs
    ¬∑ simp
    refine .sub (.div (by fun_prop) (by fun_prop) fun x hx ‚Ü¶ ?_) (by fun_prop)
    have : x ‚â† i := by rintro rfl; simp_all
    simpa [sub_eq_zero]
  ¬∑ refine (L.tendstoLocallyUniformly_‚Ñò'Except l‚ÇÄ).tendstoLocallyUniformlyOn.congr ?_
    intro s l hl
    simp only [Function.comp_apply]
    rw [deriv_sum]
    ¬∑ congr with x
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      rw [deriv_sub (.div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop), deriv_const]
      simp_rw [‚Üê zpow_natCast, one_div, ‚Üê zpow_neg, Nat.cast_ofNat]
      rw [deriv_comp_sub_const (f := (¬∑ ^ (-2 : ‚Ñ§))), deriv_zpow]
      simp
      field_simp
    ¬∑ intros x hxs
      split_ifs with hl‚ÇÅ
      ¬∑ simp
      have hl‚ÇÅ : l - x ‚â† 0 := fun e ‚Ü¶ hl‚ÇÅ (by
        obtain rfl := sub_eq_zero.mp e
        simpa using hl)
      exact .sub (.div (by fun_prop) (by fun_prop) (by simpa)) (by fun_prop)

lemma PeriodPairs.‚Ñò'Except_neg (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) : L.‚Ñò'Except l‚ÇÄ (-z) = - L.‚Ñò'Except (-l‚ÇÄ) z := by
  simp only [‚Ñò'Except]
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub, neg_neg,
    ‚Üê div_neg, ‚Üê tsum_neg, apply_ite, neg_zero]
  congr! 3 with l
  ¬∑ simp [neg_eq_iff_eq_neg]
  ring

end ‚Ñò'Except

section Periodicity

lemma PeriodPairs.‚Ñò'Except_add_coe (l‚ÇÄ : ‚ÑÇ) (z : ‚ÑÇ) (l : L.lattice) :
    L.‚Ñò'Except l‚ÇÄ (z + l) = L.‚Ñò'Except (l‚ÇÄ - l) z := by
  simp only [‚Ñò'Except]
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [‚Üê tsum_neg, ‚Üê div_neg, Equiv.coe_addRight,
    Submodule.coe_add, add_sub_add_right_eq_sub, eq_sub_iff_add_eq]

-- Subsumed by `PeriodPairs.‚Ñò_add_coe`
private lemma PeriodPairs.‚ÑòExcept_add_coe_aux
    (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    Set.EqOn (L.‚ÑòExcept l‚ÇÄ <| ¬∑ + l) (L.‚ÑòExcept (l‚ÇÄ - l) ¬∑ + (1 / l‚ÇÄ ^ 2 - 1 / (l‚ÇÄ - ‚Üël) ^ 2))
      (L.lattice \ {l‚ÇÄ - l})·∂ú := by
  apply IsOpen.eqOn_of_deriv_eq (ùïú := ‚ÑÇ) (L.isClosed_lattice_sdiff _).isOpen_compl
    ?_ ?_ ?_ ?_ (x := - (l / 2)) ?_ ?_
  ¬∑ refine (Set.Countable.isConnected_compl_of_one_lt_rank (by simp) ?_).2
    exact .mono sdiff_le (countable_of_Lindelof_of_discrete (X := L.lattice))
  ¬∑ refine (L.differentiableOn_‚ÑòExcept l‚ÇÄ).comp (f := (¬∑ + l.1)) (by fun_prop) ?_
    rintro x h‚ÇÅ ‚ü®h‚ÇÇ : x + l ‚àà _, h‚ÇÉ : x + l ‚â† l‚ÇÄ‚ü©
    exact h‚ÇÅ ‚ü®by simpa using sub_mem h‚ÇÇ l.2, by rintro rfl; simp at h‚ÇÉ‚ü©
  ¬∑ refine .add (L.differentiableOn_‚ÑòExcept _) (by simp)
  ¬∑ intro x hx
    simp only [deriv_add_const', deriv_comp_add_const]
    rw [L.eqOn_deriv_‚ÑòExcept_‚Ñò'Except, L.eqOn_deriv_‚ÑòExcept_‚Ñò'Except, L.‚Ñò'Except_add_coe]
    ¬∑ simpa using hx
    ¬∑ simp only [Set.mem_compl_iff, Set.mem_diff, SetLike.mem_coe, Set.mem_singleton_iff, not_and,
        Decidable.not_not, eq_sub_iff_add_eq] at hx ‚ä¢
      exact fun H ‚Ü¶ hx (by simpa using sub_mem H l.2)
  ¬∑ simp [hl]
  ¬∑ rw [L.‚ÑòExcept_neg, L.‚ÑòExcept_def ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©, L.‚ÑòExcept_def ‚ü®_, neg_mem (sub_mem hl‚ÇÄ l.2)‚ü©,
      add_assoc]
    congr 2 <;> ring

-- Subsumed by `PeriodPairs.‚Ñò_add_coe`
private lemma PeriodPairs.‚Ñò_add_coe_aux (z : ‚ÑÇ) (l : L.lattice) (hl : l.1 / 2 ‚àâ L.lattice) :
    L.‚Ñò (z + l) = L.‚Ñò z := by
  have hl0 : l ‚â† 0 := by rintro rfl; simp at hl
  by_cases hz : z ‚àà L.lattice
  ¬∑ have := L.‚ÑòExcept_add_coe_aux (z + l) (add_mem hz l.2) l hl
      (x := z) (by simp [eq_sub_iff_add_eq, hl0])
    dsimp at this
    rw [‚Üê L.‚ÑòExcept_add ‚ü®z + l, add_mem hz l.2‚ü©, this, ‚Üê L.‚ÑòExcept_add ‚ü®z, hz‚ü©]
    simp
    ring
  ¬∑ have := L.‚ÑòExcept_add_coe_aux 0 (zero_mem _) l hl (x := z) (by simp [hz])
    simp only [zero_sub, ne_eq, OfNat.ofNat_ne_zero, not_false_eq_true, zero_pow, div_zero,
      even_two, Even.neg_pow, one_div] at this
    rw [‚Üê L.‚ÑòExcept_add 0, Submodule.coe_zero, this, ‚Üê L.‚ÑòExcept_add (-l)]
    simp
    ring

@[simp]
lemma PeriodPairs.‚Ñò_add_coe (z : ‚ÑÇ) (l : L.lattice) : L.‚Ñò (z + l) = L.‚Ñò z := by
  let G : AddSubgroup ‚ÑÇ :=
  { carrier := { z | (L.‚Ñò <| ¬∑ + z) = L.‚Ñò }
    add_mem' := by simp_all [funext_iff, ‚Üê add_assoc]
    zero_mem' := by simp
    neg_mem' {z} hz := funext fun i ‚Ü¶ by conv_lhs => rw [‚Üê hz]; simp }
  have : L.lattice ‚â§ G.toIntSubmodule := by
    rw [lattice, Submodule.span_le]
    rintro _ (rfl|rfl)
    ¬∑ ext i
      exact L.‚Ñò_add_coe_aux _ ‚ü®_, L.œâ‚ÇÅ_mem_lattice‚ü© L.œâ‚ÇÅ_div_two_not_mem_lattice
    ¬∑ ext i
      exact L.‚Ñò_add_coe_aux _ ‚ü®_, L.œâ‚ÇÇ_mem_lattice‚ü© L.œâ‚ÇÇ_div_two_not_mem_lattice
  exact congr_fun (this l.2) _

@[simp]
lemma PeriodPairs.‚Ñò_zero : L.‚Ñò 0 = 0 := by simp [‚Ñò]

@[simp]
lemma PeriodPairs.‚Ñò_coe (l : L.lattice) : L.‚Ñò l = 0 := by
  rw [‚Üê zero_add l.1, L.‚Ñò_add_coe, L.‚Ñò_zero]

@[simp]
lemma PeriodPairs.‚Ñò_sub_coe (z : ‚ÑÇ) (l : L.lattice) : L.‚Ñò (z - l) = L.‚Ñò z := by
  rw [‚Üê L.‚Ñò_add_coe _ l, sub_add_cancel]

end Periodicity

section ‚Ñò'

def PeriodPairs.‚Ñò' (z : ‚ÑÇ) : ‚ÑÇ := - ‚àë' l : L.lattice, 2 / (z - l) ^ 3

lemma PeriodPairs.‚Ñò'Except_sub (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    L.‚Ñò'Except l‚ÇÄ z - 2 / (z - l‚ÇÄ) ^ 3 = L.‚Ñò' z := by
  trans L.‚Ñò'Except l‚ÇÄ z + ‚àë' i : L.lattice, if i.1 = l‚ÇÄ.1 then (- 2 / (z - l‚ÇÄ) ^ 3) else 0
  ¬∑ simp [sub_eq_add_neg, neg_div]
  rw [‚Ñò', ‚Ñò'Except, ‚Üê Summable.tsum_add, ‚Üê tsum_neg]
  ¬∑ congr with w; split_ifs <;> simp only [zero_add, add_zero, *, neg_div]
  ¬∑ exact ‚ü®_, L.hasSum_‚Ñò'Except _ _‚ü©
  ¬∑ exact summable_of_finite_support ((Set.finite_singleton l‚ÇÄ).subset (by simp_all))

lemma PeriodPairs.‚Ñò'Except_def (l‚ÇÄ : L.lattice) (z : ‚ÑÇ) :
    L.‚Ñò'Except l‚ÇÄ z = L.‚Ñò' z + 2 / (z - l‚ÇÄ) ^ 3 := by
  rw [‚Üê L.‚Ñò'Except_sub l‚ÇÄ, sub_add_cancel]

lemma PeriodPairs.‚Ñò'Except_of_not_mem (l‚ÇÄ : ‚ÑÇ) (hl : l‚ÇÄ ‚àâ L.lattice) :
    L.‚Ñò'Except l‚ÇÄ = L.‚Ñò' := by
  delta ‚Ñò'Except ‚Ñò'
  simp_rw [‚Üê tsum_neg]
  congr! 3 with z l
  have : l.1 ‚â† l‚ÇÄ := by rintro rfl; simp at hl
  simp [this, neg_div]

lemma PeriodPairs.tendstoLocallyUniformly_‚Ñò' :
    TendstoLocallyUniformly (fun (s : Finset L.lattice) (z : ‚ÑÇ) ‚Ü¶ - ‚àë l ‚àà s, 2 / (z - l) ^ 3)
      L.‚Ñò' Filter.atTop := by
  simp_rw [‚Üê Finset.sum_neg_distrib]
  convert L.tendstoLocallyUniformly_‚Ñò'Except (L.œâ‚ÇÅ / 2) using 4 with s z l hl
  ¬∑ rw [if_neg, neg_div]; exact fun e ‚Ü¶ L.œâ‚ÇÅ_div_two_not_mem_lattice (e ‚ñ∏ l.2)
  ¬∑ rw [L.‚Ñò'Except_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]

lemma PeriodPairs.hasSum_‚Ñò' (z : ‚ÑÇ) :
    HasSum (fun l : L.lattice ‚Ü¶ (1 / (z - l) ^ 2 - 1 / l ^ 2)) (L.‚Ñò z) :=
  L.tendstoLocallyUniformly_‚Ñò.tendstoLocallyUniformlyOn.tendsto_at (Set.mem_univ z)

lemma PeriodPairs.differentiableOn_‚Ñò' :
    DifferentiableOn ‚ÑÇ L.‚Ñò' L.lattice·∂ú := by
  rw [‚Üê L.‚Ñò'Except_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  convert L.differentiableOn_‚Ñò'Except _
  simp [Set.ext_iff, imp_not_comm, L.œâ‚ÇÅ_div_two_not_mem_lattice]

@[simp]
lemma PeriodPairs.‚Ñò'_neg (z : ‚ÑÇ) : L.‚Ñò' (-z) = - L.‚Ñò' z := by
  simp only [‚Ñò']
  rw [‚Üê (Equiv.neg L.lattice).tsum_eq]
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, sub_neg_eq_add, neg_add_eq_sub, neg_neg,
    ‚Üê div_neg, ‚Üê tsum_neg]
  congr! with l
  ring

@[simp]
lemma PeriodPairs.‚Ñò'_add_coe (z : ‚ÑÇ) (l : L.lattice) : L.‚Ñò' (z + l) = L.‚Ñò' z := by
  simp only [‚Ñò']
  rw [‚Üê (Equiv.addRight l).tsum_eq]
  simp only [‚Üê tsum_neg, ‚Üê div_neg, Equiv.coe_addRight, Submodule.coe_add, add_sub_add_right_eq_sub]

@[simp]
lemma PeriodPairs.‚Ñò'_zero : L.‚Ñò' 0 = 0 := by
  rw [‚Üê CharZero.eq_neg_self_iff, ‚Üê L.‚Ñò'_neg, neg_zero]

@[simp]
lemma PeriodPairs.‚Ñò'_coe (l : L.lattice) : L.‚Ñò' l = 0 := by
  rw [‚Üê zero_add l.1, L.‚Ñò'_add_coe, L.‚Ñò'_zero]

@[simp]
lemma PeriodPairs.‚Ñò'_sub_coe (z : ‚ÑÇ) (l : L.lattice) : L.‚Ñò' (z - l) = L.‚Ñò' z := by
  rw [‚Üê L.‚Ñò'_add_coe _ l, sub_add_cancel]

lemma PeriodPairs.not_continuousAt_‚Ñò (x : ‚ÑÇ) (hx : x ‚àà L.lattice) : ¬¨ ContinuousAt L.‚Ñò x := by
  eta_expand
  simp_rw [‚Üê L.‚ÑòExcept_add ‚ü®x, hx‚ü©]
  intro H
  have := (H.sub ((L.differentiableOn_‚ÑòExcept x).differentiableAt (x := x) ?_).continuousAt).add
    (continuous_const (y := 1 / x ^ 2)).continuousAt
  ¬∑ exact not_continuousAt_inv_sq_sub x (by simpa using this)
  ¬∑ exact (L.isClosed_lattice_sdiff _).isOpen_compl.mem_nhds (by simp)

@[simp]
lemma PeriodPairs.deriv_‚Ñò : deriv L.‚Ñò = L.‚Ñò' := by
  ext x
  by_cases hx : x ‚àà L.lattice
  ¬∑ rw [deriv_zero_of_not_differentiableAt, L.‚Ñò'_coe ‚ü®x, hx‚ü©]
    exact fun H ‚Ü¶ L.not_continuousAt_‚Ñò x hx H.continuousAt
  ¬∑ rw [‚Üê L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice,
      ‚Üê L.‚Ñò'Except_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice,
      L.eqOn_deriv_‚ÑòExcept_‚Ñò'Except (L.œâ‚ÇÅ/2) (x := x) (by simp [hx])]

end ‚Ñò'

section Analytic‚ÑòExcept

def PeriodPairs.sumInvPow (x : ‚ÑÇ) (r : ‚Ñï) : ‚ÑÇ := ‚àë' l : L.lattice, ((l - x) ^ r)‚Åª¬π

def PeriodPairs.‚ÑòExceptSeriesSummand (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  if l.1 = l‚ÇÄ then 0 else ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

def PeriodPairs.‚ÑòExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ i.casesOn (L.‚ÑòExcept l‚ÇÄ x) fun i ‚Ü¶ (i + 2) *
    (L.sumInvPow x (i + 3) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 3))‚Åª¬π else 0)

lemma PeriodPairs.coeff_‚ÑòExceptSeries (l‚ÇÄ x : ‚ÑÇ) (i : ‚Ñï) :
    (L.‚ÑòExceptSeries l‚ÇÄ x).coeff i = ‚àë' l : L.lattice, L.‚ÑòExceptSeriesSummand l‚ÇÄ x i l := by
  delta ‚ÑòExceptSeriesSummand
  rw [‚ÑòExceptSeries, FormalMultilinearSeries.coeff_ofScalars]
  cases i with
  | zero => simp [‚ÑòExcept, sub_sq_comm x, zpow_ofNat]
  | succ i =>
    dsimp
    split_ifs with hl‚ÇÄ
    ¬∑ trans (i + 2) * (L.sumInvPow x (i + 3) -
        ‚àë' l : L.lattice, if l = ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü© then (l‚ÇÄ - x) ^ (-‚Üë(i + 3) : ‚Ñ§) else 0)
      ¬∑ congr 2
        rw [tsum_ite_eq, zpow_neg, zpow_natCast]
      ¬∑ rw [sumInvPow, ‚Üê Summable.tsum_sub]
        ¬∑ rw [‚Üê tsum_mul_left]
          simp_rw [Subtype.ext_iff, zpow_neg]
          congr with l
          split_ifs with e
          ¬∑ simp only [e, zpow_neg, zpow_natCast, sub_self, mul_zero]
          ¬∑ norm_cast; ring
        ¬∑ refine Summable.of_norm_bounded
            (IsZLattice.summable_norm_sub_zpow L.lattice (-‚Üë(i + 3)) ?_ x) fun l ‚Ü¶ ?_
          ¬∑ simp; linarith
          ¬∑ rw [‚Üê zpow_natCast, ‚Üê zpow_neg, ‚Üê norm_zpow]
        ¬∑ exact summable_of_finite_support ((Set.finite_singleton ‚ü®l‚ÇÄ, hl‚ÇÄ‚ü©).subset (by simp_all))
    ¬∑ have (l : L.lattice) : l.1 ‚â† l‚ÇÄ := fun e ‚Ü¶ hl‚ÇÄ (e ‚ñ∏ l.2)
      simp only [this, if_false, sub_zero, tsum_mul_left, sumInvPow]
      congr 1
      simp [add_assoc, one_add_one_eq_two]

lemma PeriodPairs.summable_‚ÑòExceptSeriesSummand (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.‚ÑòExceptSeriesSummand l‚ÇÄ x b c * (z - x) ^ b) := by
  obtain ‚ü®Œµ, hŒµ, hŒµ'‚ü© : ‚àÉ Œµ : ‚Ñù, 1 < Œµ ‚àß
      ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ * Œµ < ‚Äñl - x‚Äñ := by
    obtain ‚ü®Œ¥, hŒ¥, h‚ü© := Disjoint.exists_cthickenings (by
      simp only [Set.disjoint_iff_inter_eq_empty, Set.mem_diff, Set.mem_inter_iff, not_lt, not_and,
        Metric.mem_closedBall, Complex.dist_eq, Set.ext_iff, SetLike.mem_coe, Set.mem_singleton_iff,
        Set.mem_empty_iff_false, iff_false, Decidable.not_not, not_imp_comm (a := _ = _)] at hx ‚ä¢
      exact fun x h‚ÇÅ h‚ÇÇ ‚Ü¶ hx ‚ü®x, h‚ÇÇ‚ü© h‚ÇÅ)
        (isCompact_closedBall x ‚Äñz - x‚Äñ) (L.isClosed_lattice_sdiff {l‚ÇÄ})
    by_cases hz : z = x
    ¬∑ refine ‚ü®2, one_lt_two, fun l hl ‚Ü¶ by simpa [hz] using hx l hl‚ü©
    have : 0 < ‚Äñz - x‚Äñ := by simp [sub_eq_zero, hz]
    refine ‚ü®Œ¥ / ‚Äñz - x‚Äñ + 1, by simp; positivity, fun l hl ‚Ü¶ ?_‚ü©
    rw [mul_add, mul_one, mul_div, mul_div_cancel_left‚ÇÄ _ this.ne']
    rw [cthickening_closedBall hŒ¥.le (by positivity)] at h
    have := Set.subset_compl_iff_disjoint_left.mpr h (Metric.self_subset_cthickening _ ‚ü®l.2, hl‚ü©)
    simpa [Complex.dist_eq] using this
  let e : ‚Ñï √ó L.lattice ‚âÉ L.lattice ‚äï (‚Ñï √ó L.lattice) :=
    (Equiv.prodCongrLeft fun a ‚Ü¶ (Denumerable.eqv (Option ‚Ñï)).symm).trans (by exact? )
  have he‚ÇÅ : e.symm ‚àò Sum.inl = Prod.mk 0 := rfl
  have he‚ÇÇ : e.symm ‚àò Sum.inr = Prod.map Nat.succ id := rfl
  rw [‚Üê e.symm.summable_iff]
  apply Summable.sum
  ¬∑ simp only [Function.comp_assoc, he‚ÇÅ, ‚ÑòExceptSeriesSummand]
    simpa [Function.comp_def, sub_sq_comm x] using (L.hasSum_‚ÑòExcept l‚ÇÄ x).summable
  ¬∑ simp only [Function.comp_assoc, he‚ÇÇ]
    simp only [Nat.cast_add, Nat.cast_ofNat, neg_add_rev, Int.reduceNeg, zpow_neg, Pi.zero_apply,
      Function.comp_def, Function.uncurry, Prod.map_snd, id_eq, Prod.map_fst, Nat.succ_eq_add_one,
      Nat.cast_one, sub_zero]
    refine Summable.of_norm_bounded (g := fun p : ‚Ñï √ó L.lattice ‚Ü¶
        ((p.1 + 2) * Œµ ^ (- p.1 : ‚Ñ§)) * (‚Äñp.2 - x‚Äñ ^ (-3 : ‚Ñ§) * ‚Äñz - x‚Äñ)) ?_ ?_
    ¬∑ refine Summable.mul_of_nonneg (f := fun p : ‚Ñï ‚Ü¶ ((p + 2) * Œµ ^ (- p : ‚Ñ§)))
        (g := fun p : L.lattice ‚Ü¶ ‚Äñp - x‚Äñ ^ (-3 : ‚Ñ§) * ‚Äñz - x‚Äñ) ?_ ?_ ?_ ?_
      ¬∑ simp_rw [zpow_neg, zpow_natCast, ‚Üê inv_pow]
        by_contra H
        have : 1 - Œµ‚Åª¬π > 0 := by simp [inv_lt_one_iff‚ÇÄ, hŒµ]
        have : |Œµ‚Åª¬π| < 1 := by
          rw [abs_inv, abs_eq_self.mpr (by positivity), inv_lt_one_iff‚ÇÄ]; exact .inr hŒµ
        have := (tsum_eq_zero_of_not_summable H).symm.trans
          (Real.tsum_add_const_mul_pow 2 Œµ‚Åª¬π this)
        norm_num at this
        exact not_ne_iff.mpr this (by positivity)
      ¬∑ apply Summable.mul_right
        apply IsZLattice.summable_norm_sub_zpow
        simp
      ¬∑ intro; positivity
      ¬∑ intro; positivity
    ¬∑ intro p
      simp only [‚ÑòExceptSeriesSummand]
      split_ifs with hp
      ¬∑ simp; positivity
      simp only [zpow_neg, zpow_natCast, add_assoc]
      norm_num
      simp only [mul_assoc, add_assoc, one_add_one_eq_two]
      rw [pow_succ (n := p.1)]
      refine mul_le_mul ?_ ?_ (by positivity) (by positivity)
      ¬∑ norm_cast
      ¬∑ simp only [‚Üê mul_assoc]
        refine mul_le_mul ?_ le_rfl (by positivity) (by positivity)
        rw [pow_add, mul_inv_rev, mul_assoc, mul_comm ((Œµ ^ p.1)‚Åª¬π)]
        refine mul_le_mul le_rfl ?_ (by positivity) (by positivity)
        rw [‚Üê inv_pow, ‚Üê inv_pow, ‚Üê mul_pow]
        gcongr
        have : ‚Äñz - x‚Äñ * Œµ < ‚Äñp.2 - x‚Äñ := hŒµ' p.2 hp
        have h : 0 < ‚Äñp.2 - x‚Äñ := (show 0 ‚â§ ‚Äñz - x‚Äñ * Œµ by positivity).trans_lt this
        rw [inv_mul_le_iff‚ÇÄ h, le_mul_inv_iff‚ÇÄ (by positivity)]
        exact this.le

lemma PeriodPairs.‚ÑòExcept_eq_tsum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    L.‚ÑòExcept l‚ÇÄ z = ‚àë' i : ‚Ñï, (L.‚ÑòExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i := by
  trans ‚àë' (l : L.lattice) (i : ‚Ñï), if l.1 = l‚ÇÄ then 0 else
      ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0) * (z - x) ^ i
  ¬∑ delta ‚ÑòExcept
    congr 1 with l
    split_ifs with h
    ¬∑ simp
    rw [tsum_eq_one_div_sub_sq_sub_one_div_sq l z x (hx l h)]
  trans ‚àë' (l : ‚Ü•L.lattice) (i : ‚Ñï), L.‚ÑòExceptSeriesSummand l‚ÇÄ x i l * (z - x) ^ i
  ¬∑ simp only [‚ÑòExceptSeriesSummand, ‚Üê tsum_mul_right, ite_mul, zero_mul]
  ¬∑ simp_rw [coeff_‚ÑòExceptSeries, ‚Üê tsum_mul_right]
    apply Summable.tsum_comm
    exact L.summable_‚ÑòExceptSeriesSummand l‚ÇÄ z x hx

lemma PeriodPairs.‚ÑòExceptSeries_hasSum (l‚ÇÄ z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, l.1 ‚â† l‚ÇÄ ‚Üí ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.‚ÑòExceptSeries l‚ÇÄ x).coeff i * (z - x) ^ i) (L.‚ÑòExcept l‚ÇÄ z) := by
  refine (Summable.hasSum_iff ?_).mpr (L.‚ÑòExcept_eq_tsum l‚ÇÄ z x hx).symm
  simp_rw [coeff_‚ÑòExceptSeries, ‚Üê tsum_mul_right]
  exact (L.summable_‚ÑòExceptSeriesSummand l‚ÇÄ z x hx).prod

lemma PeriodPairs.hasFPowerSeriesOnBall_‚ÑòExcept (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall (L.‚ÑòExcept l‚ÇÄ) (L.‚ÑòExceptSeries l‚ÇÄ x) x r := by
  constructor
  ¬∑ apply FormalMultilinearSeries.le_radius_of_tendsto (l := 0)
    convert tendsto_norm.comp (L.‚ÑòExceptSeries_hasSum l‚ÇÄ (x + r) x
      ?_).summable.tendsto_atTop_zero using 2 with i
    ¬∑ simp
    ¬∑ simp
    ¬∑ intro l hl
      simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©
  ¬∑ exact ENNReal.coe_pos.mpr hr0
  ¬∑ intro z hz
    replace hz : ‚Äñz‚Äñ < r := by simpa using hz
    have := L.‚ÑòExceptSeries_hasSum l‚ÇÄ (x + z) x
    simp only [add_sub_cancel_left] at this
    convert this (fun l hl ‚Ü¶ hz.trans (by simpa using Set.subset_compl_comm.mp hr ‚ü®l.2, hl‚ü©)) with i
    rw [‚ÑòExceptSeries, FormalMultilinearSeries.ofScalars_apply_eq,
      FormalMultilinearSeries.coeff_ofScalars]
    rfl

lemma PeriodPairs.analyticOnNhd_‚ÑòExcept (l‚ÇÄ : ‚ÑÇ) :
    AnalyticOnNhd ‚ÑÇ (L.‚ÑòExcept l‚ÇÄ) (L.lattice \ {l‚ÇÄ})·∂ú := by
  intro x hx
  obtain ‚ü®Œµ, hŒµ, h‚ü© := Metric.isOpen_iff.mp (L.isClosed_lattice_sdiff {l‚ÇÄ}).isOpen_compl x hx
  lift Œµ to NNReal using hŒµ.le
  exact ‚ü®L.‚ÑòExceptSeries l‚ÇÄ x, _, L.hasFPowerSeriesOnBall_‚ÑòExcept l‚ÇÄ x (Œµ / 2)
    (div_pos hŒµ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact hŒµ)).trans h)‚ü©

end Analytic‚ÑòExcept

section Analytic‚Ñò'Except

def PeriodPairs.‚Ñò'ExceptSeries (l‚ÇÄ x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ (i + 1) * (i + 2) *
    (L.sumInvPow x (i + 3) - if l‚ÇÄ ‚àà L.lattice then ((l‚ÇÄ - x) ^ (i + 3))‚Åª¬π else 0)

lemma PeriodPairs.hasFPowerSeriesOnBall_‚Ñò'Except (l‚ÇÄ x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ (L.lattice \ {l‚ÇÄ})·∂ú) :
    HasFPowerSeriesOnBall (L.‚Ñò'Except l‚ÇÄ) (L.‚Ñò'ExceptSeries l‚ÇÄ x) x r := by
  refine .congr ?_ ((L.eqOn_deriv_‚ÑòExcept_‚Ñò'Except l‚ÇÄ).mono (subset_trans ?_ hr))
  ¬∑ have := (L.hasFPowerSeriesOnBall_‚ÑòExcept l‚ÇÄ x r hr0 hr).fderiv
    convert (ContinuousLinearMap.apply ‚ÑÇ ‚ÑÇ (1 : ‚ÑÇ)).comp_hasFPowerSeriesOnBall this
    ext n
    simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, smul_eq_mul,
      ContinuousLinearMap.compFormalMultilinearSeries_apply,
      ContinuousLinearMap.compContinuousMultilinearMap_coe, Function.comp_apply, map_smul,
      ContinuousLinearMap.apply_apply, FormalMultilinearSeries.derivSeries_coeff_one, nsmul_eq_mul,
      Nat.cast_add, Nat.cast_one, mul_eq_mul_left_iff]
    left
    simp [‚ÑòExceptSeries, ‚Ñò'ExceptSeries, mul_assoc]
  ¬∑ simpa using Metric.ball_subset_closedBall

lemma PeriodPairs.analyticOnNhd_‚Ñò'Except (l‚ÇÄ : ‚ÑÇ) :
    AnalyticOnNhd ‚ÑÇ (L.‚Ñò'Except l‚ÇÄ) (L.lattice \ {l‚ÇÄ})·∂ú := by
  intro x hx
  obtain ‚ü®Œµ, hŒµ, h‚ü© := Metric.isOpen_iff.mp (L.isClosed_lattice_sdiff {l‚ÇÄ}).isOpen_compl x hx
  lift Œµ to NNReal using hŒµ.le
  exact ‚ü®L.‚Ñò'ExceptSeries l‚ÇÄ x, _, L.hasFPowerSeriesOnBall_‚Ñò'Except l‚ÇÄ x (Œµ / 2)
    (div_pos hŒµ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact hŒµ)).trans h)‚ü©

end Analytic‚Ñò'Except

section Analytic

def PeriodPairs.‚ÑòSeriesSummand (x : ‚ÑÇ) (i : ‚Ñï) (l : L.lattice) : ‚ÑÇ :=
  ((i + 1) * (l.1 - x) ^ (- ‚Üë(i + 2) : ‚Ñ§) - i.casesOn (l.1 ^ (-2 : ‚Ñ§)) 0)

def PeriodPairs.‚ÑòSeries (x : ‚ÑÇ) : FormalMultilinearSeries ‚ÑÇ ‚ÑÇ ‚ÑÇ :=
  letI := Classical.propDecidable
  .ofScalars _ fun i ‚Ü¶ i.casesOn (L.‚Ñò x) fun i ‚Ü¶ (i + 2) * L.sumInvPow x (i + 3)

lemma PeriodPairs.‚ÑòExceptSeries_of_not_mem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.‚ÑòExceptSeries l‚ÇÄ = L.‚ÑòSeries := by
  delta ‚ÑòSeries ‚ÑòExceptSeries
  congr! with z i f
  ¬∑ rw [L.‚ÑòExcept_of_not_mem _ hl‚ÇÄ]
  ¬∑ simp [hl‚ÇÄ]

lemma PeriodPairs.‚ÑòExceptSeriesSummand_of_not_mem (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àâ L.lattice) :
    L.‚ÑòExceptSeriesSummand l‚ÇÄ = L.‚ÑòSeriesSummand := by
  delta ‚ÑòSeriesSummand ‚ÑòExceptSeriesSummand
  ext l z l'
  have : l'.1 ‚â† l‚ÇÄ := fun e ‚Ü¶ hl‚ÇÄ (e ‚ñ∏ l'.2)
  simp [this]

lemma PeriodPairs.coeff_‚ÑòSeries (x : ‚ÑÇ) (i : ‚Ñï) :
    (L.‚ÑòSeries x).coeff i = ‚àë' l : L.lattice, L.‚ÑòSeriesSummand x i l := by
  simp_rw [‚Üê L.‚ÑòExceptSeries_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice, L.coeff_‚ÑòExceptSeries,
    ‚Üê L.‚ÑòExceptSeriesSummand_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]

lemma PeriodPairs.summable_‚ÑòSeriesSummand (z x : ‚ÑÇ)
    (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    Summable (Function.uncurry fun b c ‚Ü¶ L.‚ÑòSeriesSummand x b c * (z - x) ^ b) := by
  simp_rw [‚Üê L.‚ÑòExceptSeriesSummand_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  refine L.summable_‚ÑòExceptSeriesSummand _ z x fun l hl ‚Ü¶ hx l

lemma PeriodPairs.‚ÑòSeries_hasSum (z x : ‚ÑÇ) (hx : ‚àÄ l : L.lattice, ‚Äñz - x‚Äñ < ‚Äñl - x‚Äñ) :
    HasSum (fun i ‚Ü¶ (L.‚ÑòSeries x).coeff i * (z - x) ^ i) (L.‚Ñò z) := by
  simp_rw [‚Üê L.‚ÑòExceptSeries_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice,
    ‚Üê L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  exact L.‚ÑòExceptSeries_hasSum _ z x fun l hl ‚Ü¶ hx l

lemma PeriodPairs.hasFPowerSeriesOnBall_‚Ñò (x : ‚ÑÇ) (r : NNReal) (hr0 : 0 < r)
    (hr : Metric.closedBall x r ‚äÜ L.lattice·∂ú) :
    HasFPowerSeriesOnBall L.‚Ñò (L.‚ÑòSeries x) x r := by
  simp_rw [‚Üê L.‚ÑòExceptSeries_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice,
    ‚Üê L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  exact L.hasFPowerSeriesOnBall_‚ÑòExcept _ x r hr0
    (hr.trans (Set.compl_subset_compl.mpr Set.diff_subset))

lemma PeriodPairs.analyticOnNhd_‚Ñò : AnalyticOnNhd ‚ÑÇ L.‚Ñò L.lattice·∂ú := by
  simp_rw [‚Üê L.‚ÑòExcept_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  refine (L.analyticOnNhd_‚ÑòExcept _).mono (Set.compl_subset_compl.mpr Set.diff_subset)

lemma PeriodPairs.ite_eq_one_sub_sq_mul_‚Ñò (l‚ÇÄ : ‚ÑÇ) (hl‚ÇÄ : l‚ÇÄ ‚àà L.lattice) (z : ‚ÑÇ) :
   (if z = l‚ÇÄ then 1 else (z - l‚ÇÄ) ^ 2 * L.‚Ñò z) =
    (z - l‚ÇÄ) ^ 2 * L.‚ÑòExcept l‚ÇÄ z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2 := by
  split_ifs with h
  ¬∑ subst h
    simp
  rw [‚Üê L.‚ÑòExcept_add ‚ü®_, hl‚ÇÄ‚ü©, mul_add, mul_sub, add_sub_assoc,
    ‚Üê mul_div_assoc, mul_one, ‚Üê mul_div_assoc, mul_one, div_self]
  simpa [sub_eq_zero] using h

lemma PeriodPairs.meromorphicAt_‚Ñò (x : ‚ÑÇ) : MeromorphicAt L.‚Ñò x := by
  by_cases hx : x ‚àà L.lattice
  ¬∑ use 3
    simp_rw [‚Üê L.‚ÑòExcept_add ‚ü®x, hx‚ü©, smul_eq_mul, mul_add, mul_sub]
    refine .add (.mul (by fun_prop) (L.analyticOnNhd_‚ÑòExcept x x (fun e ‚Ü¶ e.2 rfl)))
      (.sub ?_ (by fun_prop))
    convert_to AnalyticAt ‚ÑÇ (fun z ‚Ü¶ z - x) x
    ¬∑ ext z
      by_cases h : z - x = 0
      ¬∑ simp [h]
      ¬∑ rw [pow_succ', one_div, mul_inv_cancel_right‚ÇÄ]
        simpa
    ¬∑ fun_prop
  ¬∑ exact (L.analyticOnNhd_‚Ñò x hx).meromorphicAt

lemma PeriodPairs.order_‚Ñò (l‚ÇÄ : ‚ÑÇ) (h : l‚ÇÄ ‚àà L.lattice) :
    meromorphicOrderAt L.‚Ñò l‚ÇÄ = -2 := by
  trans ‚Üë(-2 : ‚Ñ§)
  ¬∑ rw [meromorphicOrderAt_eq_int_iff (L.meromorphicAt_‚Ñò l‚ÇÄ)]
    refine ‚ü®fun z ‚Ü¶ (z - l‚ÇÄ) ^ 2 * L.‚ÑòExcept l‚ÇÄ z + 1 - (z - l‚ÇÄ) ^ 2 / l‚ÇÄ ^ 2, ?_, ?_, ?_‚ü©
    ¬∑ refine .sub (.add (.mul (by fun_prop) ?_) (by fun_prop)) ?_
      ¬∑ exact L.analyticOnNhd_‚ÑòExcept l‚ÇÄ l‚ÇÄ (by simp)
      ¬∑ by_cases hl‚ÇÄ : l‚ÇÄ = 0
        ¬∑ simpa [hl‚ÇÄ] using analyticAt_const
        ¬∑ exact .div (by fun_prop) (by fun_prop) (by simpa)
    ¬∑ simp
    ¬∑ refine Filter.eventually_of_mem self_mem_nhdsWithin ?_
      rintro z (hz : _ ‚â† _)
      have : (z - l‚ÇÄ) ^ 2 ‚â† 0 := by simpa [sub_eq_zero]
      simp [‚Üê L.ite_eq_one_sub_sq_mul_‚Ñò l‚ÇÄ h, if_neg hz, inv_mul_cancel_left‚ÇÄ this, zpow_ofNat]

  ¬∑ norm_num

end Analytic

open Metric NNReal Finset in
lemma HasFPowerSeriesOnBall.exists_eq_add_mul_sub (f : ‚ÑÇ ‚Üí ‚ÑÇ) (a : ‚Ñï ‚Üí ‚ÑÇ) {x} {r : ‚Ñù‚â•0}
    (hf : HasFPowerSeriesOnBall f (.ofScalars ‚ÑÇ a) x r) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, HasFPowerSeriesOnBall g (.ofScalars ‚ÑÇ (a <| ¬∑ + 1)) x r ‚àß g x = a 1 ‚àß
      f = (fun z ‚Ü¶ a 0 + g z * (z - x)) := by
  have H : f x = a 0 := by symm; simpa using hf.coeff_zero
  refine ‚ü®fun z ‚Ü¶ if z = x then a 1 else (f z - a 0) / (z - x), ?_, (by simp), ?_‚ü©
  ¬∑ constructor
    ¬∑ refine hf.1.trans ?_
      unfold FormalMultilinearSeries.radius
      simp only [FormalMultilinearSeries.norm_apply_eq_norm_coef,
        FormalMultilinearSeries.coeff_ofScalars, iSup_le_iff]
      intro r' b hrb
      by_cases hr' : r' = 0
      ¬∑ simp [hr']
      refine le_iSup_of_le r' (le_iSup_of_le ((‚Üër')‚Åª¬π * b) (le_iSup_of_le (fun n ‚Ü¶ ?_) le_rfl))
      rw [le_inv_mul_iff‚ÇÄ (by positivity), mul_comm, mul_assoc, ‚Üê pow_succ]
      exact hrb _
    ¬∑ exact hf.2
    ¬∑ rintro y hy
      have := (hasSum_nat_add_iff' 1).mpr (hf.3 hy)
      simp only [FormalMultilinearSeries.apply_eq_prod_smul_coeff, prod_const, card_univ,
        Fintype.card_fin, FormalMultilinearSeries.coeff_ofScalars, smul_eq_mul, add_eq_left,
        add_sub_cancel_left, range_one, sum_singleton, pow_zero, one_mul] at this ‚ä¢
      split_ifs with hy'
      ¬∑ simp only [hy', zero_pow_eq, ite_mul, one_mul, zero_mul]
        convert hasSum_ite_eq 0 _
        simp_all
      ¬∑ convert this.div_const y using 2 with n
        rw [mul_comm (y ^ (n + 1)), pow_succ, ‚Üê mul_div, mul_div_cancel_right‚ÇÄ _ hy', mul_comm]
  ¬∑ ext z
    by_cases hz : x = z
    ¬∑ simp only [hz, sub_self, zero_pow_eq, mul_ite, mul_one, mul_zero, sum_ite_eq', mem_range]
      ¬∑ simp [‚Üê hz, H]
    ¬∑ have : z - x ‚â† 0 := by simp [sub_eq_zero, Ne.symm hz]
      simp [this, Ne.symm hz]

open Metric NNReal Finset in
lemma HasFPowerSeriesOnBall.exists_eq_add_mul_sub_pow (f : ‚ÑÇ ‚Üí ‚ÑÇ) (a : ‚Ñï ‚Üí ‚ÑÇ) {x} {r : ‚Ñù‚â•0}
    (hf : HasFPowerSeriesOnBall f (.ofScalars ‚ÑÇ a) x r) (k : ‚Ñï) :
    ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, HasFPowerSeriesOnBall g (.ofScalars ‚ÑÇ (a <| ¬∑ + k)) x r ‚àß g x = a k ‚àß
      f = (fun z ‚Ü¶ ‚àë i ‚àà range k, a i * (z - x) ^ i + g z * (z - x) ^ k) := by
  induction k with
  | zero => refine ‚ü®f, (by simpa using hf), by symm; simpa using hf.coeff_zero, by simp‚ü©
  | succ k IH =>
    obtain ‚ü®g, hg, h, rfl‚ü© := IH
    obtain ‚ü®g', hg', h', rfl‚ü© := hg.exists_eq_add_mul_sub
    simp_rw [add_assoc, add_comm 1] at hg'
    refine ‚ü®g', hg', add_comm 1 k ‚ñ∏ h', ?_‚ü©
    ext z
    simp [add_mul, pow_succ', mul_assoc, Finset.sum_range_succ, add_assoc]

section Relation

def PeriodPairs.G (n : ‚Ñï) : ‚ÑÇ := ‚àë' l : L.lattice, (l ^ n)‚Åª¬π

@[simp]
lemma PeriodPairs.sumInvPow_zero : L.sumInvPow 0 = L.G := by
  ext; simp [sumInvPow, G]

lemma PeriodPairs.G_eq_zero_of_odd (n : ‚Ñï) (hn : Odd n) : L.G n = 0 := by
  rw [‚Üê CharZero.eq_neg_self_iff, G, ‚Üê tsum_neg, ‚Üê (Equiv.neg _).tsum_eq]
  congr with l
  simp only [Equiv.neg_apply, NegMemClass.coe_neg, neg_inv, hn.neg_pow]

def PeriodPairs.g‚ÇÇ : ‚ÑÇ := 60 * L.G 4

def PeriodPairs.g‚ÇÉ : ‚ÑÇ := 140 * L.G 6

@[simp]
lemma PeriodPairs.‚ÑòExcept_zero (x) : L.‚ÑòExcept x 0 = 0 := by simp [‚ÑòExcept]

@[simp]
lemma PeriodPairs.‚Ñò'Except_zero : L.‚Ñò'Except 0 0 = 0 := by
  rw [L.‚Ñò'Except_def ‚ü®0, zero_mem _‚ü©]
  simp

-- `‚Ñò(z) = z‚Åª¬≤ + 3G‚ÇÑz¬≤ + 5G‚ÇÜz‚Å¥ + O(z‚Å∂)`
lemma PeriodPairs.exists_‚Ñò_expansion (k : ‚Ñï) : ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß
      g 0 = ‚Üë(2 * k + 3) * L.G (2 * k + 4) ‚àß ‚àÄ z, L.‚Ñò z = 1 / z ^ 2 + (‚àë i ‚àà Finset.range k,
        ‚Üë(2 * i + 3) * L.G (2 * i + 4) * z ^ (2 * i + 2)) + g z * z ^ (2 * k + 2) := by
  obtain ‚ü®r, h‚ÇÅ, h‚ÇÇ‚ü© := Metric.isOpen_iff.mp (L.isClosed_lattice_sdiff {0}).isOpen_compl 0 (by simp)
  lift r to NNReal using h‚ÇÅ.le
  have := L.hasFPowerSeriesOnBall_‚ÑòExcept 0 0 (r / 2)
    (div_pos h‚ÇÅ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact h‚ÇÅ)).trans h‚ÇÇ)
  obtain ‚ü®g, hg, h, e‚ü© := this.exists_eq_add_mul_sub_pow _ _ (2 * k + 2)
  refine ‚ü®g, hg.analyticAt, ?_, fun z ‚Ü¶ ?_‚ü©
  ¬∑ have : g 0 = (2 * ‚Üëk + 1 + 2) * L.G (2 * k + 1 + 3) := by simpa using h
    exact_mod_cast this
  rw [‚Üê L.‚ÑòExcept_add 0, Submodule.coe_zero, e]
  simp only [‚ÑòExcept_zero, sumInvPow_zero, Submodule.zero_mem, ‚ÜìreduceIte, sub_self, ne_eq,
    AddLeftCancelMonoid.add_eq_zero, OfNat.ofNat_ne_zero, and_false, not_false_eq_true, zero_pow,
    inv_zero, sub_zero, div_zero]
  rw [add_comm, add_assoc]
  congr 2
  clear h e hg
  induction k with
  | zero => simp [Finset.sum_range_succ, L.G_eq_zero_of_odd 3 (by decide)]
  | succ n IH =>
    rw [show 2 * n + 2 = 2 * (n + 1) by omega] at IH
    simp only [Finset.sum_range_succ, IH, Nat.cast_mul, Nat.cast_ofNat, Nat.cast_add, Nat.cast_one,
      add_assoc, add_right_inj]
    simp [mul_add, L.G_eq_zero_of_odd (2 * n + 2 + 3) ‚ü®n + 2, by omega‚ü©, add_assoc,
      show (1 : ‚ÑÇ) + 2 = 3 by norm_num]

-- `‚Ñò(z) = -2z‚Åª¬≥ + 6G‚ÇÑz + 20G‚ÇÜz¬≥ + O(z‚Åµ)`
lemma PeriodPairs.exists_‚Ñò'_expansion (k : ‚Ñï) : ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß
    g 0 = ‚Üë((2 * k + 2) * (2 * k + 3)) * L.G (2 * k + 4) ‚àß
      ‚àÄ z, L.‚Ñò' z = - 2 / z ^ 3 + (‚àë i ‚àà Finset.range k, ‚Üë((2 * i + 2) * (2 * i + 3)) *
        L.G (2 * i + 4) * z ^ (2 * i + 1)) + g z * z ^ (2 * k + 1) := by
  obtain ‚ü®r, h‚ÇÅ, h‚ÇÇ‚ü© := Metric.isOpen_iff.mp (L.isClosed_lattice_sdiff {0}).isOpen_compl 0 (by simp)
  lift r to NNReal using h‚ÇÅ.le
  have := L.hasFPowerSeriesOnBall_‚Ñò'Except 0 0 (r / 2)
    (div_pos h‚ÇÅ (by simp)) ((Metric.closedBall_subset_ball (by norm_num; exact h‚ÇÅ)).trans h‚ÇÇ)
  obtain ‚ü®g, hg, h, e‚ü© := this.exists_eq_add_mul_sub_pow _ _ (2 * k + 1)
  refine ‚ü®g, hg.analyticAt, ?_, fun z ‚Ü¶ ?_‚ü©
  ¬∑ have : g 0 = (2 * ‚Üëk + 1 + 1) * (2 * ‚Üëk + 1 + 2) * L.G (2 * k + 4) := by simpa using h
    exact_mod_cast this
  rw [‚Üê L.‚Ñò'Except_sub 0, Submodule.coe_zero, e]
  simp only [‚Ñò'Except_zero, Submodule.zero_mem, ‚ÜìreduceIte, sub_self, ne_eq,
    AddLeftCancelMonoid.add_eq_zero, OfNat.ofNat_ne_zero, and_false, not_false_eq_true, zero_pow,
    inv_zero, sub_zero, div_zero]
  rw [sub_eq_add_neg, add_comm, neg_div, add_assoc]
  congr 2
  clear h e hg
  induction k with
  | zero => simp [Finset.sum_range_succ, L.G_eq_zero_of_odd 3 (by decide)]
  | succ n IH =>
    rw [Finset.sum_range_succ, Finset.sum_range_succ, mul_add, Finset.sum_range_succ, IH]
    simp only [Nat.cast_mul, Nat.cast_ofNat, Nat.cast_add, Nat.cast_one, add_assoc, add_right_inj]
    simp only [show (1 : ‚ÑÇ) + 2 = 3 by norm_num, mul_add, Nat.reduceAdd, sumInvPow_zero, mul_one,
      L.G_eq_zero_of_odd (2 * n + 2 + 3) ‚ü®n + 2, by omega‚ü©, mul_zero, zero_mul, add_zero,
      mul_eq_mul_right_iff, ne_eq, AddLeftCancelMonoid.add_eq_zero, mul_eq_zero,
      OfNat.ofNat_ne_zero, false_or, one_ne_zero, and_false, not_false_eq_true, pow_eq_zero_iff]
    left
    left
    ring

def PeriodPairs.relation (z : ‚ÑÇ) : ‚ÑÇ :=
  letI := Classical.propDecidable
  if z ‚àà L.lattice then 0 else L.‚Ñò' z ^ 2 - 4 * L.‚Ñò z ^ 3 + L.g‚ÇÇ * L.‚Ñò z + L.g‚ÇÉ

lemma PeriodPairs.analyticAt_relation_zero : AnalyticAt ‚ÑÇ L.relation 0 := by
  obtain ‚ü®g‚ÇÅ, hg‚ÇÅ, h‚ÇÄ‚ÇÅ, e‚ÇÅ‚ü© : ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß g 0 = - 80 * L.G 6 ‚àß
      ‚àÄ z ‚â† 0, (L.‚Ñò' z) ^ 2 = 4 / z ^ 6 - 24 * L.G 4 / z ^ 2 + g z := by
    obtain ‚ü®g, h‚ÇÅ, h‚ÇÇ, e‚ü© := L.exists_‚Ñò'_expansion 1
    refine ‚ü®fun z ‚Ü¶ - 4 * g z + 12 * L.G 4 * z * g z * z ^ 3 + (6 * L.G 4 * z) ^ 2 +
      (g z * z ^ 3) ^ 2, by fun_prop, by simp [h‚ÇÇ, ‚Üê mul_assoc]; norm_num, fun z hz ‚Ü¶ ?_‚ü©
    simp only [e]
    field_simp
    ring
  obtain ‚ü®g‚ÇÇ, hg‚ÇÇ, h‚ÇÄ‚ÇÇ, e‚ÇÇ‚ü© : ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß g 0 = 5 * L.G 6 ‚àß
      ‚àÄ z, L.‚Ñò z = 1 / z ^ 2 + 3 * L.G 4 * z ^ 2 + g z * z ^ 4 := by
    obtain ‚ü®g, h‚ÇÅ, h‚ÇÇ, e‚ü© := L.exists_‚Ñò_expansion 1
    refine ‚ü®g, h‚ÇÅ, by simp [h‚ÇÇ, ‚Üê mul_assoc], by simpa [-one_div] using e‚ü©
  obtain ‚ü®g‚ÇÉ, hg‚ÇÉ, h‚ÇÄ‚ÇÉ, e‚ÇÉ‚ü© : ‚àÉ g : ‚ÑÇ ‚Üí ‚ÑÇ, AnalyticAt ‚ÑÇ g 0 ‚àß g 0 = 15 * L.G 6 ‚àß
      ‚àÄ z ‚â† 0, (L.‚Ñò z) ^ 3 = 1 / z ^ 6 + 9 * L.G 4 / z ^ 2 + g z := by
    refine ‚ü®fun z ‚Ü¶ (3 * L.G 4 * z ^ 2 + g‚ÇÇ z * z ^ 4) ^ 3 +
      3 * (3 * L.G 4 * z ^ 2 + g‚ÇÇ z * z ^ 4) * (3 * L.G 4 + g‚ÇÇ z * z ^ 2) + 3 * g‚ÇÇ z, by fun_prop,
      by simp [h‚ÇÄ‚ÇÇ, ‚Üê mul_assoc]; norm_num, fun z hz ‚Ü¶ ?_‚ü©
    simp only [e‚ÇÇ]
    field_simp
    ring
  let F (z) := z ^ 2 * L.G 4 ^ 2 * 180 + z ^ 4 * L.G 4 * g‚ÇÇ z * 60 + g‚ÇÅ z - g‚ÇÉ z * 4 + L.G 6 * 140
  refine (show AnalyticAt ‚ÑÇ F 0 by fun_prop).congr ?_
  refine Filter.eventuallyEq_of_mem
    ((L.isClosed_lattice_sdiff {0}).isOpen_compl.mem_nhds (by simp)) ?_
  intro z hz
  by_cases hz' : z ‚àà L.lattice
  ¬∑ obtain rfl : z = 0 := not_not.mp fun e ‚Ü¶ hz ‚ü®hz', e‚ü©
    simp [*, relation, F]
    ring
  have : z ‚â† 0 := by rintro rfl; simp at hz'
  simp only [relation, hz', ‚ÜìreduceIte]
  simp_rw [e‚ÇÅ z this, e‚ÇÉ z this, e‚ÇÇ z, PeriodPairs.g‚ÇÇ, PeriodPairs.g‚ÇÉ]
  ring

lemma PeriodPairs.analyticOnNhd_‚Ñò' : AnalyticOnNhd ‚ÑÇ L.‚Ñò' L.lattice·∂ú := by
  rw [‚Üê L.‚Ñò'Except_of_not_mem _ L.œâ‚ÇÅ_div_two_not_mem_lattice]
  exact (L.analyticOnNhd_‚Ñò'Except _).mono (Set.compl_subset_compl.mpr Set.diff_subset)

@[simp]
lemma PeriodPairs.relation_add_coe (x : ‚ÑÇ) (l : L.lattice) :
    L.relation (x + l) = L.relation x := by
  simp only [relation, Function.comp_apply, ‚Ñò'_add_coe, ‚Ñò_add_coe]
  congr 1
  simpa using (L.lattice.toAddSubgroup.add_mem_cancel_right (y := x) l.2)

@[simp]
lemma PeriodPairs.relation_sub_coe (x : ‚ÑÇ) (l : L.lattice) :
    L.relation (x - l) = L.relation x := by
  rw [‚Üê L.relation_add_coe _ l, sub_add_cancel]

lemma PeriodPairs.analyticAt_relation (x : ‚ÑÇ) : AnalyticAt ‚ÑÇ L.relation x := by
  by_cases hx : x ‚àà L.lattice
  ¬∑ lift x to L.lattice using hx
    have := L.analyticAt_relation_zero
    rw [‚Üê sub_self x.1] at this
    convert this.comp (f := (¬∑ - x.1)) (by fun_prop)
    ext a
    simp
  ¬∑ have : AnalyticAt ‚ÑÇ (fun z ‚Ü¶ L.‚Ñò' z ^ 2 - 4 * L.‚Ñò z ^ 3 + L.g‚ÇÇ * L.‚Ñò z + L.g‚ÇÉ) x := by
      refine .add (.add (.sub (.pow ?_ _) (.mul (by fun_prop) (.pow ?_ _)))
        (.mul (by fun_prop) ?_)) (by fun_prop)
      ¬∑ exact L.analyticOnNhd_‚Ñò' _ (by simpa)
      ¬∑ exact L.analyticOnNhd_‚Ñò _ (by simpa)
      ¬∑ exact L.analyticOnNhd_‚Ñò _ (by simpa)
    refine this.congr (Filter.eventuallyEq_of_mem
      (L.isClosed_lattice.isOpen_compl.mem_nhds (by simpa)) ?_)
    intro x hx
    simp_all [relation]

lemma PeriodPairs.relation_eq_zero : L.relation = 0 := by
  have : Differentiable ‚ÑÇ L.relation := fun x ‚Ü¶ (L.analyticAt_relation x).differentiableAt
  have := this.apply_eq_apply_of_bounded
    (IsZLattice.isCompact_range_of_periodic L.lattice _ this.continuous ?_).isBounded
  ¬∑ ext x
    rw [this x 0, relation]
    simp
  ¬∑ intro z w hw
    lift w to L.lattice using hw
    simp

lemma PeriodPairs.‚Ñò'_sq (z : ‚ÑÇ) (hz : z ‚àâ L.lattice) :
    L.‚Ñò' z ^ 2 = 4 * L.‚Ñò z ^ 3 - L.g‚ÇÇ * L.‚Ñò z - L.g‚ÇÉ := by
  rw [‚Üê sub_eq_zero]
  convert congr_fun L.relation_eq_zero z using 1
  simp [relation, hz, sub_sub, sub_add]

end Relation

set_option linter.style.longFile 0
