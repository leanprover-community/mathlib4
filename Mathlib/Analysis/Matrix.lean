/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Eric Wieser
-/
import Mathlib.Analysis.Normed.Lp.PiLp
import Mathlib.Analysis.InnerProductSpace.PiL2

/-!
# Matrices as a normed space

In this file we provide the following non-instances for norms on matrices:

* The elementwise norm:

  * `Matrix.seminormedAddCommGroup`
  * `Matrix.normedAddCommGroup`
  * `Matrix.normedSpace`
  * `Matrix.isBoundedSMul`
  * `Matrix.normSMulClass`

* The Frobenius norm:

  * `Matrix.frobeniusSeminormedAddCommGroup`
  * `Matrix.frobeniusNormedAddCommGroup`
  * `Matrix.frobeniusNormedSpace`
  * `Matrix.frobeniusNormedRing`
  * `Matrix.frobeniusNormedAlgebra`
  * `Matrix.frobeniusIsBoundedSMul`
  * `Matrix.frobeniusNormSMulClass`

* The $L^\infty$ operator norm:

  * `Matrix.linftyOpSeminormedAddCommGroup`
  * `Matrix.linftyOpNormedAddCommGroup`
  * `Matrix.linftyOpNormedSpace`
  * `Matrix.linftyOpIsBoundedSMul`
  * `Matrix.linftyOpNormSMulClass`
  * `Matrix.linftyOpNonUnitalSemiNormedRing`
  * `Matrix.linftyOpSemiNormedRing`
  * `Matrix.linftyOpNonUnitalNormedRing`
  * `Matrix.linftyOpNormedRing`
  * `Matrix.linftyOpNormedAlgebra`

These are not declared as instances because there are several natural choices for defining the norm
of a matrix.

The norm induced by the identification of `Matrix m n ùïú` with
`EuclideanSpace n ùïú ‚ÜíL[ùïú] EuclideanSpace m ùïú` (i.e., the ‚Ñì¬≤ operator norm) can be found in
`Analysis.CStarAlgebra.Matrix`. It is separated to avoid extraneous imports in this file.
-/

noncomputable section

open scoped NNReal Matrix

namespace Matrix

variable {R l m n Œ± Œ≤ Œπ : Type*} [Fintype l] [Fintype m] [Fintype n] [Unique Œπ]

/-! ### The elementwise supremum norm -/


section LinfLinf

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤]

/-- Seminormed group instance (using sup norm of sup norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def seminormedAddCommGroup : SeminormedAddCommGroup (Matrix m n Œ±) :=
  Pi.seminormedAddCommGroup


attribute [local instance] Matrix.seminormedAddCommGroup

theorem norm_def (A : Matrix m n Œ±) : ‚ÄñA‚Äñ = ‚Äñfun i j => A i j‚Äñ := rfl

/-- The norm of a matrix is the sup of the sup of the nnnorm of the entries -/
lemma norm_eq_sup_sup_nnnorm (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ = Finset.sup Finset.univ fun i ‚Ü¶ Finset.sup Finset.univ fun j ‚Ü¶ ‚ÄñA i j‚Äñ‚Çä := by
  simp_rw [Matrix.norm_def, Pi.norm_def, Pi.nnnorm_def]

theorem nnnorm_def (A : Matrix m n Œ±) : ‚ÄñA‚Äñ‚Çä = ‚Äñfun i j => A i j‚Äñ‚Çä := rfl

theorem norm_le_iff {r : ‚Ñù} (hr : 0 ‚â§ r) {A : Matrix m n Œ±} : ‚ÄñA‚Äñ ‚â§ r ‚Üî ‚àÄ i j, ‚ÄñA i j‚Äñ ‚â§ r := by
  simp_rw [norm_def, pi_norm_le_iff_of_nonneg hr]

theorem nnnorm_le_iff {r : ‚Ñù‚â•0} {A : Matrix m n Œ±} : ‚ÄñA‚Äñ‚Çä ‚â§ r ‚Üî ‚àÄ i j, ‚ÄñA i j‚Äñ‚Çä ‚â§ r := by
  simp_rw [nnnorm_def, pi_nnnorm_le_iff]

theorem norm_lt_iff {r : ‚Ñù} (hr : 0 < r) {A : Matrix m n Œ±} : ‚ÄñA‚Äñ < r ‚Üî ‚àÄ i j, ‚ÄñA i j‚Äñ < r := by
  simp_rw [norm_def, pi_norm_lt_iff hr]

theorem nnnorm_lt_iff {r : ‚Ñù‚â•0} (hr : 0 < r) {A : Matrix m n Œ±} :
    ‚ÄñA‚Äñ‚Çä < r ‚Üî ‚àÄ i j, ‚ÄñA i j‚Äñ‚Çä < r := by
  simp_rw [nnnorm_def, pi_nnnorm_lt_iff hr]

theorem norm_entry_le_entrywise_sup_norm (A : Matrix m n Œ±) {i : m} {j : n} : ‚ÄñA i j‚Äñ ‚â§ ‚ÄñA‚Äñ :=
  (norm_le_pi_norm (A i) j).trans (norm_le_pi_norm A i)

theorem nnnorm_entry_le_entrywise_sup_nnnorm (A : Matrix m n Œ±) {i : m} {j : n} : ‚ÄñA i j‚Äñ‚Çä ‚â§ ‚ÄñA‚Äñ‚Çä :=
  (nnnorm_le_pi_nnnorm (A i) j).trans (nnnorm_le_pi_nnnorm A i)

@[simp]
theorem nnnorm_map_eq (A : Matrix m n Œ±) (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ a, ‚Äñf a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä) :
    ‚ÄñA.map f‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä := by
  simp only [nnnorm_def, Pi.nnnorm_def, Matrix.map_apply, hf]

@[simp]
theorem norm_map_eq (A : Matrix m n Œ±) (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ a, ‚Äñf a‚Äñ = ‚Äña‚Äñ) : ‚ÄñA.map f‚Äñ = ‚ÄñA‚Äñ :=
  (congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_map_eq A f fun a => Subtype.ext <| hf a :)

@[simp]
theorem nnnorm_transpose (A : Matrix m n Œ±) : ‚ÄñA·µÄ‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä :=
  Finset.sup_comm _ _ _

@[simp]
theorem norm_transpose (A : Matrix m n Œ±) : ‚ÄñA·µÄ‚Äñ = ‚ÄñA‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_transpose A

@[simp]
theorem nnnorm_conjTranspose [StarAddMonoid Œ±] [NormedStarGroup Œ±] (A : Matrix m n Œ±) :
    ‚ÄñA·¥¥‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä :=
  (nnnorm_map_eq _ _ nnnorm_star).trans A.nnnorm_transpose

@[simp]
theorem norm_conjTranspose [StarAddMonoid Œ±] [NormedStarGroup Œ±] (A : Matrix m n Œ±) : ‚ÄñA·¥¥‚Äñ = ‚ÄñA‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_conjTranspose A

instance [StarAddMonoid Œ±] [NormedStarGroup Œ±] : NormedStarGroup (Matrix m m Œ±) :=
  ‚ü®(le_of_eq <| norm_conjTranspose ¬∑)‚ü©

@[simp]
theorem nnnorm_replicateCol (v : m ‚Üí Œ±) : ‚ÄñreplicateCol Œπ v‚Äñ‚Çä = ‚Äñv‚Äñ‚Çä := by
  simp [nnnorm_def, Pi.nnnorm_def]

@[deprecated (since := "2025-03-20")] alias nnnorm_col := nnnorm_replicateCol

@[simp]
theorem norm_replicateCol (v : m ‚Üí Œ±) : ‚ÄñreplicateCol Œπ v‚Äñ = ‚Äñv‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_replicateCol v

@[deprecated (since := "2025-03-20")] alias norm_col := norm_replicateCol

@[simp]
theorem nnnorm_replicateRow (v : n ‚Üí Œ±) : ‚ÄñreplicateRow Œπ v‚Äñ‚Çä = ‚Äñv‚Äñ‚Çä := by
  simp [nnnorm_def, Pi.nnnorm_def]

@[deprecated (since := "2025-03-20")] alias nnnorm_row := nnnorm_replicateRow

@[simp]
theorem norm_replicateRow (v : n ‚Üí Œ±) : ‚ÄñreplicateRow Œπ v‚Äñ = ‚Äñv‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_replicateRow v

@[deprecated (since := "2025-03-20")] alias norm_row := norm_replicateRow

@[simp]
theorem nnnorm_diagonal [DecidableEq n] (v : n ‚Üí Œ±) : ‚Äñdiagonal v‚Äñ‚Çä = ‚Äñv‚Äñ‚Çä := by
  simp_rw [nnnorm_def, Pi.nnnorm_def]
  congr 1 with i : 1
  refine le_antisymm (Finset.sup_le fun j hj => ?_) ?_
  ¬∑ obtain rfl | hij := eq_or_ne i j
    ¬∑ rw [diagonal_apply_eq]
    ¬∑ rw [diagonal_apply_ne _ hij, nnnorm_zero]
      exact zero_le _
  ¬∑ refine Eq.trans_le ?_ (Finset.le_sup (Finset.mem_univ i))
    rw [diagonal_apply_eq]

@[simp]
theorem norm_diagonal [DecidableEq n] (v : n ‚Üí Œ±) : ‚Äñdiagonal v‚Äñ = ‚Äñv‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| nnnorm_diagonal v

/-- Note this is safe as an instance as it carries no data. -/
-- Porting note: not yet implemented: `@[nolint fails_quickly]`
instance [Nonempty n] [DecidableEq n] [One Œ±] [NormOneClass Œ±] : NormOneClass (Matrix n n Œ±) :=
  ‚ü®(norm_diagonal _).trans <| norm_one‚ü©

end SeminormedAddCommGroup

/-- Normed group instance (using sup norm of sup norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedAddCommGroup [NormedAddCommGroup Œ±] : NormedAddCommGroup (Matrix m n Œ±) :=
  Pi.normedAddCommGroup

section NormedSpace

attribute [local instance] Matrix.seminormedAddCommGroup

/-- This applies to the sup norm of sup norm. -/
protected theorem isBoundedSMul [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±]
    [IsBoundedSMul R Œ±] : IsBoundedSMul R (Matrix m n Œ±) :=
  Pi.instIsBoundedSMul

@[deprecated (since := "2025-03-10")] protected alias boundedSMul := Matrix.isBoundedSMul

/-- This applies to the sup norm of sup norm. -/
protected theorem normSMulClass [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±]
    [NormSMulClass R Œ±] : NormSMulClass R (Matrix m n Œ±) :=
  Pi.instNormSMulClass

variable [NormedField R] [SeminormedAddCommGroup Œ±] [NormedSpace R Œ±]

/-- Normed space instance (using sup norm of sup norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedSpace : NormedSpace R (Matrix m n Œ±) :=
  Pi.normedSpace

end NormedSpace

end LinfLinf

/-! ### The $L_\infty$ operator norm

This section defines the matrix norm $\|A\|_\infty = \operatorname{sup}_i (\sum_j \|A_{ij}\|)$.

Note that this is equivalent to the operator norm, considering $A$ as a linear map between two
$L^\infty$ spaces.
-/


section LinftyOp

/-- Seminormed group instance (using sup norm of L1 norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSeminormedAddCommGroup [SeminormedAddCommGroup Œ±] :
    SeminormedAddCommGroup (Matrix m n Œ±) :=
  (by infer_instance : SeminormedAddCommGroup (m ‚Üí PiLp 1 fun j : n => Œ±))

/-- Normed group instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAddCommGroup [NormedAddCommGroup Œ±] :
    NormedAddCommGroup (Matrix m n Œ±) :=
  (by infer_instance : NormedAddCommGroup (m ‚Üí PiLp 1 fun j : n => Œ±))

/-- This applies to the sup norm of L1 norm. -/
@[local instance]
protected theorem linftyOpIsBoundedSMul
    [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±] [IsBoundedSMul R Œ±] :
    IsBoundedSMul R (Matrix m n Œ±) :=
  (by infer_instance : IsBoundedSMul R (m ‚Üí PiLp 1 fun j : n => Œ±))

/-- This applies to the sup norm of L1 norm. -/
@[local instance]
protected theorem linftyOpNormSMulClass
    [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±] [NormSMulClass R Œ±] :
    NormSMulClass R (Matrix m n Œ±) :=
  (by infer_instance : NormSMulClass R (m ‚Üí PiLp 1 fun j : n => Œ±))

/-- Normed space instance (using sup norm of L1 norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedSpace [NormedField R] [SeminormedAddCommGroup Œ±] [NormedSpace R Œ±] :
    NormedSpace R (Matrix m n Œ±) :=
  (by infer_instance : NormedSpace R (m ‚Üí PiLp 1 fun j : n => Œ±))

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Œ±]

theorem linfty_opNorm_def (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ = ((Finset.univ : Finset m).sup fun i : m => ‚àë j : n, ‚ÄñA i j‚Äñ‚Çä : ‚Ñù‚â•0) := by
  -- Porting note: added
  change ‚Äñfun i => (WithLp.equiv 1 _).symm (A i)‚Äñ = _
  simp [Pi.norm_def, PiLp.nnnorm_eq_of_L1]

theorem linfty_opNNNorm_def (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ‚Çä = (Finset.univ : Finset m).sup fun i : m => ‚àë j : n, ‚ÄñA i j‚Äñ‚Çä :=
  Subtype.ext <| linfty_opNorm_def A

@[simp]
theorem linfty_opNNNorm_replicateCol (v : m ‚Üí Œ±) : ‚ÄñreplicateCol Œπ v‚Äñ‚Çä = ‚Äñv‚Äñ‚Çä := by
  rw [linfty_opNNNorm_def, Pi.nnnorm_def]
  simp

@[deprecated (since := "2025-03-20")] alias linfty_opNNNorm_col := linfty_opNNNorm_replicateCol

@[simp]
theorem linfty_opNorm_replicateCol (v : m ‚Üí Œ±) : ‚ÄñreplicateCol Œπ v‚Äñ = ‚Äñv‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| linfty_opNNNorm_replicateCol v

@[deprecated (since := "2025-03-20")] alias linfty_opNorm_col := linfty_opNorm_replicateCol

@[simp]
theorem linfty_opNNNorm_replicateRow (v : n ‚Üí Œ±) : ‚ÄñreplicateRow Œπ v‚Äñ‚Çä = ‚àë i, ‚Äñv i‚Äñ‚Çä := by
  simp [linfty_opNNNorm_def]

@[deprecated (since := "2025-03-20")] alias linfty_opNNNorm_row := linfty_opNNNorm_replicateRow

@[simp]
theorem linfty_opNorm_replicateRow (v : n ‚Üí Œ±) : ‚ÄñreplicateRow Œπ v‚Äñ = ‚àë i, ‚Äñv i‚Äñ :=
  (congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| linfty_opNNNorm_replicateRow v).trans <| by simp [NNReal.coe_sum]

@[deprecated (since := "2025-03-20")] alias linfty_opNorm_row := linfty_opNNNorm_replicateRow

@[simp]
theorem linfty_opNNNorm_diagonal [DecidableEq m] (v : m ‚Üí Œ±) : ‚Äñdiagonal v‚Äñ‚Çä = ‚Äñv‚Äñ‚Çä := by
  rw [linfty_opNNNorm_def, Pi.nnnorm_def]
  congr 1 with i : 1
  refine (Finset.sum_eq_single_of_mem _ (Finset.mem_univ i) fun j _hj hij => ?_).trans ?_
  ¬∑ rw [diagonal_apply_ne' _ hij, nnnorm_zero]
  ¬∑ rw [diagonal_apply_eq]

@[simp]
theorem linfty_opNorm_diagonal [DecidableEq m] (v : m ‚Üí Œ±) : ‚Äñdiagonal v‚Äñ = ‚Äñv‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| linfty_opNNNorm_diagonal v

end SeminormedAddCommGroup

section NonUnitalSeminormedRing

variable [NonUnitalSeminormedRing Œ±]

theorem linfty_opNNNorm_mul (A : Matrix l m Œ±) (B : Matrix m n Œ±) : ‚ÄñA * B‚Äñ‚Çä ‚â§ ‚ÄñA‚Äñ‚Çä * ‚ÄñB‚Äñ‚Çä := by
  simp_rw [linfty_opNNNorm_def, Matrix.mul_apply]
  calc
    (Finset.univ.sup fun i => ‚àë k, ‚Äñ‚àë j, A i j * B j k‚Äñ‚Çä) ‚â§
        Finset.univ.sup fun i => ‚àë k, ‚àë j, ‚ÄñA i j‚Äñ‚Çä * ‚ÄñB j k‚Äñ‚Çä :=
      Finset.sup_mono_fun fun i _hi =>
        Finset.sum_le_sum fun k _hk => nnnorm_sum_le_of_le _ fun j _hj => nnnorm_mul_le _ _
    _ = Finset.univ.sup fun i => ‚àë j, ‚ÄñA i j‚Äñ‚Çä * ‚àë k, ‚ÄñB j k‚Äñ‚Çä := by
      simp_rw [@Finset.sum_comm m, Finset.mul_sum]
    _ ‚â§ Finset.univ.sup fun i => ‚àë j, ‚ÄñA i j‚Äñ‚Çä * Finset.univ.sup fun i => ‚àë j, ‚ÄñB i j‚Äñ‚Çä := by
      refine Finset.sup_mono_fun fun i _hi => ?_
      gcongr with j hj
      exact Finset.le_sup (f := fun i ‚Ü¶ ‚àë k : n, ‚ÄñB i k‚Äñ‚Çä) hj
    _ ‚â§ (Finset.univ.sup fun i => ‚àë j, ‚ÄñA i j‚Äñ‚Çä) * Finset.univ.sup fun i => ‚àë j, ‚ÄñB i j‚Äñ‚Çä := by
      simp_rw [‚Üê Finset.sum_mul, ‚Üê NNReal.finset_sup_mul]
      rfl

theorem linfty_opNorm_mul (A : Matrix l m Œ±) (B : Matrix m n Œ±) : ‚ÄñA * B‚Äñ ‚â§ ‚ÄñA‚Äñ * ‚ÄñB‚Äñ :=
  linfty_opNNNorm_mul _ _

theorem linfty_opNNNorm_mulVec (A : Matrix l m Œ±) (v : m ‚Üí Œ±) : ‚ÄñA *·µ• v‚Äñ‚Çä ‚â§ ‚ÄñA‚Äñ‚Çä * ‚Äñv‚Äñ‚Çä := by
  rw [‚Üê linfty_opNNNorm_replicateCol (Œπ := Fin 1) (A *·µ• v),
    ‚Üê linfty_opNNNorm_replicateCol v (Œπ := Fin 1)]
  exact linfty_opNNNorm_mul A (replicateCol (Fin 1) v)

theorem linfty_opNorm_mulVec (A : Matrix l m Œ±) (v : m ‚Üí Œ±) : ‚ÄñA *·µ• v‚Äñ ‚â§ ‚ÄñA‚Äñ * ‚Äñv‚Äñ :=
  linfty_opNNNorm_mulVec _ _

end NonUnitalSeminormedRing

/-- Seminormed non-unital ring instance (using sup norm of L1 norm) for matrices over a semi normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalSemiNormedRing [NonUnitalSeminormedRing Œ±] :
    NonUnitalSeminormedRing (Matrix n n Œ±) :=
  { Matrix.linftyOpSeminormedAddCommGroup, Matrix.instNonUnitalRing with
    norm_mul_le := linfty_opNorm_mul }

/-- The `L‚ÇÅ-L‚àû` norm preserves one on non-empty matrices. Note this is safe as an instance, as it
carries no data. -/
instance linfty_opNormOneClass [SeminormedRing Œ±] [NormOneClass Œ±] [DecidableEq n] [Nonempty n] :
    NormOneClass (Matrix n n Œ±) where norm_one := (linfty_opNorm_diagonal _).trans norm_one

/-- Seminormed ring instance (using sup norm of L1 norm) for matrices over a semi normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSemiNormedRing [SeminormedRing Œ±] [DecidableEq n] :
    SeminormedRing (Matrix n n Œ±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing, Matrix.instRing with }

/-- Normed non-unital ring instance (using sup norm of L1 norm) for matrices over a normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalNormedRing [NonUnitalNormedRing Œ±] :
    NonUnitalNormedRing (Matrix n n Œ±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed ring instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedRing [NormedRing Œ±] [DecidableEq n] : NormedRing (Matrix n n Œ±) :=
  { Matrix.linftyOpSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed algebra instance (using sup norm of L1 norm) for matrices over a normed algebra. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAlgebra [NormedField R] [SeminormedRing Œ±] [NormedAlgebra R Œ±]
    [DecidableEq n] : NormedAlgebra R (Matrix n n Œ±) :=
  { Matrix.linftyOpNormedSpace, Matrix.instAlgebra with }


section
variable [NormedDivisionRing Œ±] [NormedAlgebra ‚Ñù Œ±]

/-- Auxiliary construction; an element of norm 1 such that `a * unitOf a = ‚Äña‚Äñ`. -/
private def unitOf (a : Œ±) : Œ± := by classical exact if a = 0 then 1 else ‚Äña‚Äñ ‚Ä¢ a‚Åª¬π

private theorem norm_unitOf (a : Œ±) : ‚ÄñunitOf a‚Äñ‚Çä = 1 := by
  rw [unitOf]
  split_ifs with h
  ¬∑ simp
  ¬∑ rw [‚Üê nnnorm_eq_zero] at h
    rw [nnnorm_smul, nnnorm_inv, nnnorm_norm, mul_inv_cancel‚ÇÄ h]

private theorem mul_unitOf (a : Œ±) : a * unitOf a = algebraMap _ _ (‚Äña‚Äñ‚Çä : ‚Ñù)  := by
  simp only [unitOf, coe_nnnorm]
  split_ifs with h
  ¬∑ simp [h]
  ¬∑ rw [mul_smul_comm, mul_inv_cancel‚ÇÄ h, Algebra.algebraMap_eq_smul_one]

end

/-!
For a matrix over a field, the norm defined in this section agrees with the operator norm on
`ContinuousLinearMap`s between function types (which have the infinity norm).
-/
section
variable [NontriviallyNormedField Œ±] [NormedAlgebra ‚Ñù Œ±]

lemma linfty_opNNNorm_eq_opNNNorm (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ‚Çä = ‚ÄñContinuousLinearMap.mk (Matrix.mulVecLin A)‚Äñ‚Çä := by
  rw [ContinuousLinearMap.opNNNorm_eq_of_bounds _ (linfty_opNNNorm_mulVec _) fun N hN => ?_]
  rw [linfty_opNNNorm_def]
  refine Finset.sup_le fun i _ => ?_
  cases isEmpty_or_nonempty n
  ¬∑ simp
  classical
  let x : n ‚Üí Œ± := fun j => unitOf (A i j)
  have hxn : ‚Äñx‚Äñ‚Çä = 1 := by
    simp_rw [x, Pi.nnnorm_def, norm_unitOf, Finset.sup_const Finset.univ_nonempty]
  specialize hN x
  rw [hxn, mul_one, Pi.nnnorm_def, Finset.sup_le_iff] at hN
  replace hN := hN i (Finset.mem_univ _)
  dsimp [mulVec, dotProduct] at hN
  simp_rw [x, mul_unitOf, ‚Üê map_sum, nnnorm_algebraMap, ‚Üê NNReal.coe_sum, NNReal.nnnorm_eq,
    nnnorm_one, mul_one] at hN
  exact hN

lemma linfty_opNorm_eq_opNorm (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ = ‚ÄñContinuousLinearMap.mk (Matrix.mulVecLin A)‚Äñ :=
  congr_arg NNReal.toReal (linfty_opNNNorm_eq_opNNNorm A)

variable [DecidableEq n]

@[simp] lemma linfty_opNNNorm_toMatrix (f : (n ‚Üí Œ±) ‚ÜíL[Œ±] (m ‚Üí Œ±)) :
    ‚ÄñLinearMap.toMatrix' (‚Üëf : (n ‚Üí Œ±) ‚Üí‚Çó[Œ±] (m ‚Üí Œ±))‚Äñ‚Çä = ‚Äñf‚Äñ‚Çä := by
  rw [linfty_opNNNorm_eq_opNNNorm]
  simp only [‚Üê toLin'_apply', toLin'_toMatrix']

@[simp] lemma linfty_opNorm_toMatrix (f : (n ‚Üí Œ±) ‚ÜíL[Œ±] (m ‚Üí Œ±)) :
    ‚ÄñLinearMap.toMatrix' (‚Üëf : (n ‚Üí Œ±) ‚Üí‚Çó[Œ±] (m ‚Üí Œ±))‚Äñ = ‚Äñf‚Äñ :=
  congr_arg NNReal.toReal (linfty_opNNNorm_toMatrix f)

end

end LinftyOp

/-! ### The Frobenius norm

This is defined as $\|A\| = \sqrt{\sum_{i,j} \|A_{ij}\|^2}$.
When the matrix is over the real or complex numbers, this norm is submultiplicative.
-/


section frobenius

open scoped Matrix

/-- Seminormed group instance (using the Frobenius norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusSeminormedAddCommGroup [SeminormedAddCommGroup Œ±] :
    SeminormedAddCommGroup (Matrix m n Œ±) :=
  inferInstanceAs (SeminormedAddCommGroup (PiLp 2 fun _i : m => PiLp 2 fun _j : n => Œ±))

/-- Normed group instance (using the Frobenius norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAddCommGroup [NormedAddCommGroup Œ±] : NormedAddCommGroup (Matrix m n Œ±) :=
  (by infer_instance : NormedAddCommGroup (PiLp 2 fun i : m => PiLp 2 fun j : n => Œ±))

/-- This applies to the Frobenius norm. -/
@[local instance]
theorem frobeniusIsBoundedSMul [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±]
    [IsBoundedSMul R Œ±] :
    IsBoundedSMul R (Matrix m n Œ±) :=
  (by infer_instance : IsBoundedSMul R (PiLp 2 fun i : m => PiLp 2 fun j : n => Œ±))

/-- This applies to the Frobenius norm. -/
@[local instance]
theorem frobeniusNormSMulClass [SeminormedRing R] [SeminormedAddCommGroup Œ±] [Module R Œ±]
    [NormSMulClass R Œ±] :
    NormSMulClass R (Matrix m n Œ±) :=
  (by infer_instance : NormSMulClass R (PiLp 2 fun i : m => PiLp 2 fun j : n => Œ±))

@[deprecated (since := "2025-03-10")] alias frobeniusBoundedSMul := frobeniusIsBoundedSMul

/-- Normed space instance (using the Frobenius norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedSpace [NormedField R] [SeminormedAddCommGroup Œ±] [NormedSpace R Œ±] :
    NormedSpace R (Matrix m n Œ±) :=
  (by infer_instance : NormedSpace R (PiLp 2 fun i : m => PiLp 2 fun j : n => Œ±))

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Œ±] [SeminormedAddCommGroup Œ≤]

theorem frobenius_nnnorm_def (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ‚Çä = (‚àë i, ‚àë j, ‚ÄñA i j‚Äñ‚Çä ^ (2 : ‚Ñù)) ^ (1 / 2 : ‚Ñù) := by
  -- Porting note: added, along with `WithLp.equiv_symm_pi_apply` below
  change ‚Äñ(WithLp.equiv 2 _).symm fun i => (WithLp.equiv 2 _).symm fun j => A i j‚Äñ‚Çä = _
  simp_rw [PiLp.nnnorm_eq_of_L2, NNReal.sq_sqrt, NNReal.sqrt_eq_rpow, NNReal.rpow_two,
    WithLp.equiv_symm_pi_apply]

theorem frobenius_norm_def (A : Matrix m n Œ±) :
    ‚ÄñA‚Äñ = (‚àë i, ‚àë j, ‚ÄñA i j‚Äñ ^ (2 : ‚Ñù)) ^ (1 / 2 : ‚Ñù) :=
  (congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) (frobenius_nnnorm_def A)).trans <| by simp [NNReal.coe_sum]

@[simp]
theorem frobenius_nnnorm_map_eq (A : Matrix m n Œ±) (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ a, ‚Äñf a‚Äñ‚Çä = ‚Äña‚Äñ‚Çä) :
    ‚ÄñA.map f‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä := by simp_rw [frobenius_nnnorm_def, Matrix.map_apply, hf]

@[simp]
theorem frobenius_norm_map_eq (A : Matrix m n Œ±) (f : Œ± ‚Üí Œ≤) (hf : ‚àÄ a, ‚Äñf a‚Äñ = ‚Äña‚Äñ) :
    ‚ÄñA.map f‚Äñ = ‚ÄñA‚Äñ :=
  (congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| frobenius_nnnorm_map_eq A f fun a => Subtype.ext <| hf a :)

@[simp]
theorem frobenius_nnnorm_transpose (A : Matrix m n Œ±) : ‚ÄñA·µÄ‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä := by
  rw [frobenius_nnnorm_def, frobenius_nnnorm_def, Finset.sum_comm]
  simp_rw [Matrix.transpose_apply]

@[simp]
theorem frobenius_norm_transpose (A : Matrix m n Œ±) : ‚ÄñA·µÄ‚Äñ = ‚ÄñA‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| frobenius_nnnorm_transpose A

@[simp]
theorem frobenius_nnnorm_conjTranspose [StarAddMonoid Œ±] [NormedStarGroup Œ±] (A : Matrix m n Œ±) :
    ‚ÄñA·¥¥‚Äñ‚Çä = ‚ÄñA‚Äñ‚Çä :=
  (frobenius_nnnorm_map_eq _ _ nnnorm_star).trans A.frobenius_nnnorm_transpose

@[simp]
theorem frobenius_norm_conjTranspose [StarAddMonoid Œ±] [NormedStarGroup Œ±] (A : Matrix m n Œ±) :
    ‚ÄñA·¥¥‚Äñ = ‚ÄñA‚Äñ :=
  congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| frobenius_nnnorm_conjTranspose A

instance frobenius_normedStarGroup [StarAddMonoid Œ±] [NormedStarGroup Œ±] :
    NormedStarGroup (Matrix m m Œ±) :=
  ‚ü®(le_of_eq <| frobenius_norm_conjTranspose ¬∑)‚ü©

@[simp]
theorem frobenius_norm_replicateRow (v : m ‚Üí Œ±) :
    ‚ÄñreplicateRow Œπ v‚Äñ = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ := by
  rw [frobenius_norm_def, Fintype.sum_unique, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]
  simp only [replicateRow_apply, Real.rpow_two, WithLp.equiv_symm_pi_apply]

@[deprecated (since := "2025-03-20")] alias frobenius_norm_row := frobenius_norm_replicateRow

@[simp]
theorem frobenius_nnnorm_replicateRow (v : m ‚Üí Œ±) :
    ‚ÄñreplicateRow Œπ v‚Äñ‚Çä = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ‚Çä :=
  Subtype.ext <| frobenius_norm_replicateRow v

@[deprecated (since := "2025-03-20")] alias frobenius_nnnorm_row := frobenius_nnnorm_replicateRow

@[simp]
theorem frobenius_norm_replicateCol (v : n ‚Üí Œ±) :
    ‚ÄñreplicateCol Œπ v‚Äñ = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ := by
  simp_rw [frobenius_norm_def, Fintype.sum_unique, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]
  simp only [replicateCol_apply, Real.rpow_two, WithLp.equiv_symm_pi_apply]

@[deprecated (since := "2025-03-20")] alias frobenius_norm_col := frobenius_norm_replicateCol

@[simp]
theorem frobenius_nnnorm_replicateCol (v : n ‚Üí Œ±) :
    ‚ÄñreplicateCol Œπ v‚Äñ‚Çä = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ‚Çä :=
  Subtype.ext <| frobenius_norm_replicateCol v

@[deprecated (since := "2025-03-20")] alias frobenius_nnnorm_col := frobenius_nnnorm_replicateCol

@[simp]
theorem frobenius_nnnorm_diagonal [DecidableEq n] (v : n ‚Üí Œ±) :
    ‚Äñdiagonal v‚Äñ‚Çä = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ‚Çä := by
  simp_rw [frobenius_nnnorm_def, ‚Üê Finset.sum_product', Finset.univ_product_univ,
    PiLp.nnnorm_eq_of_L2]
  let s := (Finset.univ : Finset n).map ‚ü®fun i : n => (i, i), fun i j h => congr_arg Prod.fst h‚ü©
  rw [‚Üê Finset.sum_subset (Finset.subset_univ s) fun i _hi his => ?_]
  ¬∑ rw [Finset.sum_map, NNReal.sqrt_eq_rpow]
    dsimp
    simp_rw [diagonal_apply_eq, NNReal.rpow_two]
  ¬∑ suffices i.1 ‚â† i.2 by rw [diagonal_apply_ne _ this, nnnorm_zero, NNReal.zero_rpow two_ne_zero]
    intro h
    exact Finset.mem_map.not.mp his ‚ü®i.1, Finset.mem_univ _, Prod.ext rfl h‚ü©

@[simp]
theorem frobenius_norm_diagonal [DecidableEq n] (v : n ‚Üí Œ±) :
    ‚Äñdiagonal v‚Äñ = ‚Äñ(WithLp.equiv 2 _).symm v‚Äñ :=
  (congr_arg ((‚Üë) : ‚Ñù‚â•0 ‚Üí ‚Ñù) <| frobenius_nnnorm_diagonal v :).trans rfl

end SeminormedAddCommGroup

theorem frobenius_nnnorm_one [DecidableEq n] [SeminormedAddCommGroup Œ±] [One Œ±] :
    ‚Äñ(1 : Matrix n n Œ±)‚Äñ‚Çä = .sqrt (Fintype.card n) * ‚Äñ(1 : Œ±)‚Äñ‚Çä := by
  calc
    ‚Äñ(diagonal 1 : Matrix n n Œ±)‚Äñ‚Çä
    _ = ‚Äñ(WithLp.equiv 2 (n ‚Üí Œ±)).symm (Function.const _ 1)‚Äñ‚Çä := frobenius_nnnorm_diagonal _
    _ = .sqrt (Fintype.card n) * ‚Äñ(1 : Œ±)‚Äñ‚Çä := by
      rw [PiLp.nnnorm_equiv_symm_const (ENNReal.ofNat_ne_top (n := 2))]
      simp [NNReal.sqrt_eq_rpow]

section RCLike

variable [RCLike Œ±]

theorem frobenius_nnnorm_mul (A : Matrix l m Œ±) (B : Matrix m n Œ±) : ‚ÄñA * B‚Äñ‚Çä ‚â§ ‚ÄñA‚Äñ‚Çä * ‚ÄñB‚Äñ‚Çä := by
  simp_rw [frobenius_nnnorm_def, Matrix.mul_apply]
  rw [‚Üê NNReal.mul_rpow, @Finset.sum_comm _ _ m, Finset.sum_mul_sum]
  gcongr with i _ j
  rw [‚Üê NNReal.rpow_le_rpow_iff one_half_pos, ‚Üê NNReal.rpow_mul,
    mul_div_cancel‚ÇÄ (1 : ‚Ñù) two_ne_zero, NNReal.rpow_one, NNReal.mul_rpow]
  have :=
    @nnnorm_inner_le_nnnorm Œ± _ _ _ _ ((WithLp.equiv 2 <| _ ‚Üí Œ±).symm fun j => star (A i j))
      ((WithLp.equiv 2 <| _ ‚Üí Œ±).symm fun k => B k j)
  simpa only [WithLp.equiv_symm_pi_apply, PiLp.inner_apply, RCLike.inner_apply', starRingEnd_apply,
    Pi.nnnorm_def, PiLp.nnnorm_eq_of_L2, star_star, nnnorm_star, NNReal.sqrt_eq_rpow,
    NNReal.rpow_two] using this

theorem frobenius_norm_mul (A : Matrix l m Œ±) (B : Matrix m n Œ±) : ‚ÄñA * B‚Äñ ‚â§ ‚ÄñA‚Äñ * ‚ÄñB‚Äñ :=
  frobenius_nnnorm_mul A B

/-- Normed ring instance (using the Frobenius norm) for matrices over `‚Ñù` or `‚ÑÇ`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedRing [DecidableEq m] : NormedRing (Matrix m m Œ±) :=
  { Matrix.frobeniusSeminormedAddCommGroup, Matrix.instRing with
    norm := Norm.norm
    norm_mul_le := frobenius_norm_mul
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed algebra instance (using the Frobenius norm) for matrices over `‚Ñù` or `‚ÑÇ`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAlgebra [DecidableEq m] [NormedField R] [NormedAlgebra R Œ±] :
    NormedAlgebra R (Matrix m m Œ±) :=
  { Matrix.frobeniusNormedSpace, Matrix.instAlgebra with }

end RCLike

end frobenius

end Matrix
