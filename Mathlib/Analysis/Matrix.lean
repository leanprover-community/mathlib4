/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Eric Wieser
-/
import Mathlib.Analysis.InnerProductSpace.PiL2

/-!
# Matrices as a normed space

In this file we provide the following non-instances for norms on matrices:

* The elementwise norm (with `open scoped Matrix.Norms.Elementwise`):

  * `Matrix.seminormedAddCommGroup`
  * `Matrix.normedAddCommGroup`
  * `Matrix.normedSpace`
  * `Matrix.isBoundedSMul`
  * `Matrix.normSMulClass`

* The Frobenius norm (with `open scoped Matrix.Norms.Frobenius`):

  * `Matrix.frobeniusSeminormedAddCommGroup`
  * `Matrix.frobeniusNormedAddCommGroup`
  * `Matrix.frobeniusNormedSpace`
  * `Matrix.frobeniusNormedRing`
  * `Matrix.frobeniusNormedAlgebra`
  * `Matrix.frobeniusIsBoundedSMul`
  * `Matrix.frobeniusNormSMulClass`

* The $L^\infty$ operator norm (with `open scoped Matrix.Norms.Operator`):

  * `Matrix.linftyOpSeminormedAddCommGroup`
  * `Matrix.linftyOpNormedAddCommGroup`
  * `Matrix.linftyOpNormedSpace`
  * `Matrix.linftyOpIsBoundedSMul`
  * `Matrix.linftyOpNormSMulClass`
  * `Matrix.linftyOpNonUnitalSemiNormedRing`
  * `Matrix.linftyOpSemiNormedRing`
  * `Matrix.linftyOpNonUnitalNormedRing`
  * `Matrix.linftyOpNormedRing`
  * `Matrix.linftyOpNormedAlgebra`

These are not declared as instances because there are several natural choices for defining the norm
of a matrix.

The norm induced by the identification of `Matrix m n ğ•œ` with
`EuclideanSpace n ğ•œ â†’L[ğ•œ] EuclideanSpace m ğ•œ` (i.e., the â„“Â² operator norm) can be found in
`Mathlib/Analysis/CStarAlgebra/Matrix.lean` and `open scoped Matrix.Norms.L2Operator`.
It is separated to avoid extraneous imports in this file.
-/

noncomputable section

open WithLp
open scoped NNReal Matrix

namespace Matrix

variable {R l m n Î± Î² Î¹ : Type*} [Fintype l] [Fintype m] [Fintype n] [Unique Î¹]

/-! ### The elementwise supremum norm -/


section LinfLinf

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±] [SeminormedAddCommGroup Î²]

/-- Seminormed group instance (using sup norm of sup norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def seminormedAddCommGroup : SeminormedAddCommGroup (Matrix m n Î±) :=
  Pi.seminormedAddCommGroup

attribute [local instance] Matrix.seminormedAddCommGroup

theorem norm_def (A : Matrix m n Î±) : â€–Aâ€– = â€–fun i j => A i jâ€– := rfl

/-- The norm of a matrix is the sup of the sup of the nnnorm of the entries -/
lemma norm_eq_sup_sup_nnnorm (A : Matrix m n Î±) :
    â€–Aâ€– = Finset.sup Finset.univ fun i â†¦ Finset.sup Finset.univ fun j â†¦ â€–A i jâ€–â‚Š := by
  simp_rw [Matrix.norm_def, Pi.norm_def, Pi.nnnorm_def]

theorem nnnorm_def (A : Matrix m n Î±) : â€–Aâ€–â‚Š = â€–fun i j => A i jâ€–â‚Š := rfl

theorem norm_le_iff {r : â„} (hr : 0 â‰¤ r) {A : Matrix m n Î±} : â€–Aâ€– â‰¤ r â†” âˆ€ i j, â€–A i jâ€– â‰¤ r := by
  simp_rw [norm_def, pi_norm_le_iff_of_nonneg hr]

theorem nnnorm_le_iff {r : â„â‰¥0} {A : Matrix m n Î±} : â€–Aâ€–â‚Š â‰¤ r â†” âˆ€ i j, â€–A i jâ€–â‚Š â‰¤ r := by
  simp_rw [nnnorm_def, pi_nnnorm_le_iff]

theorem norm_lt_iff {r : â„} (hr : 0 < r) {A : Matrix m n Î±} : â€–Aâ€– < r â†” âˆ€ i j, â€–A i jâ€– < r := by
  simp_rw [norm_def, pi_norm_lt_iff hr]

theorem nnnorm_lt_iff {r : â„â‰¥0} (hr : 0 < r) {A : Matrix m n Î±} :
    â€–Aâ€–â‚Š < r â†” âˆ€ i j, â€–A i jâ€–â‚Š < r := by
  simp_rw [nnnorm_def, pi_nnnorm_lt_iff hr]

theorem norm_entry_le_entrywise_sup_norm (A : Matrix m n Î±) {i : m} {j : n} : â€–A i jâ€– â‰¤ â€–Aâ€– :=
  (norm_le_pi_norm (A i) j).trans (norm_le_pi_norm A i)

theorem nnnorm_entry_le_entrywise_sup_nnnorm (A : Matrix m n Î±) {i : m} {j : n} : â€–A i jâ€–â‚Š â‰¤ â€–Aâ€–â‚Š :=
  (nnnorm_le_pi_nnnorm (A i) j).trans (nnnorm_le_pi_nnnorm A i)

@[simp]
theorem nnnorm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€–â‚Š = â€–aâ€–â‚Š) :
    â€–A.map fâ€–â‚Š = â€–Aâ€–â‚Š := by
  simp only [nnnorm_def, Pi.nnnorm_def, Matrix.map_apply, hf]

@[simp]
theorem norm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€– = â€–aâ€–) : â€–A.map fâ€– = â€–Aâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_map_eq A f fun a => Subtype.ext <| hf a :)

@[simp]
theorem nnnorm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€–â‚Š = â€–Aâ€–â‚Š :=
  Finset.sup_comm _ _ _

@[simp]
theorem norm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_transpose A

@[simp]
theorem nnnorm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€–â‚Š = â€–Aâ€–â‚Š :=
  (nnnorm_map_eq _ _ nnnorm_star).trans A.nnnorm_transpose

@[simp]
theorem norm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) : â€–Aá´´â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_conjTranspose A

instance [StarAddMonoid Î±] [NormedStarGroup Î±] : NormedStarGroup (Matrix m m Î±) :=
  âŸ¨(le_of_eq <| norm_conjTranspose Â·)âŸ©

@[simp]
theorem nnnorm_replicateCol (v : m â†’ Î±) : â€–replicateCol Î¹ vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp [nnnorm_def, Pi.nnnorm_def]

@[deprecated (since := "2025-03-20")] alias nnnorm_col := nnnorm_replicateCol

@[simp]
theorem norm_replicateCol (v : m â†’ Î±) : â€–replicateCol Î¹ vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_replicateCol v

@[deprecated (since := "2025-03-20")] alias norm_col := norm_replicateCol

@[simp]
theorem nnnorm_replicateRow (v : n â†’ Î±) : â€–replicateRow Î¹ vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp [nnnorm_def, Pi.nnnorm_def]

@[deprecated (since := "2025-03-20")] alias nnnorm_row := nnnorm_replicateRow

@[simp]
theorem norm_replicateRow (v : n â†’ Î±) : â€–replicateRow Î¹ vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_replicateRow v

@[deprecated (since := "2025-03-20")] alias norm_row := norm_replicateRow

@[simp]
theorem nnnorm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp_rw [nnnorm_def, Pi.nnnorm_def]
  congr 1 with i : 1
  refine le_antisymm (Finset.sup_le fun j hj => ?_) ?_
  Â· obtain rfl | hij := eq_or_ne i j
    Â· rw [diagonal_apply_eq]
    Â· rw [diagonal_apply_ne _ hij, nnnorm_zero]
      exact zero_le _
  Â· refine Eq.trans_le ?_ (Finset.le_sup (Finset.mem_univ i))
    rw [diagonal_apply_eq]

@[simp]
theorem norm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_diagonal v

/-- Note this is safe as an instance as it carries no data. -/
instance [Nonempty n] [DecidableEq n] [One Î±] [NormOneClass Î±] : NormOneClass (Matrix n n Î±) :=
  âŸ¨(norm_diagonal _).trans <| norm_oneâŸ©

end SeminormedAddCommGroup

/-- Normed group instance (using sup norm of sup norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedAddCommGroup [NormedAddCommGroup Î±] : NormedAddCommGroup (Matrix m n Î±) :=
  Pi.normedAddCommGroup

section NormedSpace

attribute [local instance] Matrix.seminormedAddCommGroup

/-- This applies to the sup norm of sup norm. -/
protected theorem isBoundedSMul [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±]
    [IsBoundedSMul R Î±] : IsBoundedSMul R (Matrix m n Î±) :=
  Pi.instIsBoundedSMul

@[deprecated (since := "2025-03-10")] protected alias boundedSMul := Matrix.isBoundedSMul

/-- This applies to the sup norm of sup norm. -/
protected theorem normSMulClass [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±]
    [NormSMulClass R Î±] : NormSMulClass R (Matrix m n Î±) :=
  Pi.instNormSMulClass

variable [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±]

/-- Normed space instance (using sup norm of sup norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedSpace : NormedSpace R (Matrix m n Î±) :=
  Pi.normedSpace

namespace Norms.Elementwise

attribute [scoped instance]
  Matrix.seminormedAddCommGroup
  Matrix.normedAddCommGroup
  Matrix.normedSpace
  Matrix.isBoundedSMul
  Matrix.normSMulClass

end Norms.Elementwise

end NormedSpace

end LinfLinf

/-! ### The $L_\infty$ operator norm

This section defines the matrix norm $\|A\|_\infty = \operatorname{sup}_i (\sum_j \|A_{ij}\|)$.

Note that this is equivalent to the operator norm, considering $A$ as a linear map between two
$L^\infty$ spaces.
-/


section LinftyOp

/-- Seminormed group instance (using sup norm of L1 norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSeminormedAddCommGroup [SeminormedAddCommGroup Î±] :
    SeminormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : SeminormedAddCommGroup (m â†’ PiLp 1 fun j : n => Î±))

/-- Normed group instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAddCommGroup [NormedAddCommGroup Î±] :
    NormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : NormedAddCommGroup (m â†’ PiLp 1 fun j : n => Î±))

/-- This applies to the sup norm of L1 norm. -/
@[local instance]
protected theorem linftyOpIsBoundedSMul
    [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±] [IsBoundedSMul R Î±] :
    IsBoundedSMul R (Matrix m n Î±) :=
  (by infer_instance : IsBoundedSMul R (m â†’ PiLp 1 fun j : n => Î±))

/-- This applies to the sup norm of L1 norm. -/
@[local instance]
protected theorem linftyOpNormSMulClass
    [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±] [NormSMulClass R Î±] :
    NormSMulClass R (Matrix m n Î±) :=
  (by infer_instance : NormSMulClass R (m â†’ PiLp 1 fun j : n => Î±))

/-- Normed space instance (using sup norm of L1 norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedSpace [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±] :
    NormedSpace R (Matrix m n Î±) :=
  (by infer_instance : NormedSpace R (m â†’ PiLp 1 fun j : n => Î±))

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±]

theorem linfty_opNorm_def (A : Matrix m n Î±) :
    â€–Aâ€– = ((Finset.univ : Finset m).sup fun i : m => âˆ‘ j : n, â€–A i jâ€–â‚Š : â„â‰¥0) := by
  change â€–fun i => toLp 1 (A i)â€– = _
  simp [Pi.norm_def, PiLp.nnnorm_eq_of_L1]

theorem linfty_opNNNorm_def (A : Matrix m n Î±) :
    â€–Aâ€–â‚Š = (Finset.univ : Finset m).sup fun i : m => âˆ‘ j : n, â€–A i jâ€–â‚Š :=
  Subtype.ext <| linfty_opNorm_def A

@[simp]
theorem linfty_opNNNorm_replicateCol (v : m â†’ Î±) : â€–replicateCol Î¹ vâ€–â‚Š = â€–vâ€–â‚Š := by
  rw [linfty_opNNNorm_def, Pi.nnnorm_def]
  simp

@[deprecated (since := "2025-03-20")] alias linfty_opNNNorm_col := linfty_opNNNorm_replicateCol

@[simp]
theorem linfty_opNorm_replicateCol (v : m â†’ Î±) : â€–replicateCol Î¹ vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_opNNNorm_replicateCol v

@[deprecated (since := "2025-03-20")] alias linfty_opNorm_col := linfty_opNorm_replicateCol

@[simp]
theorem linfty_opNNNorm_replicateRow (v : n â†’ Î±) : â€–replicateRow Î¹ vâ€–â‚Š = âˆ‘ i, â€–v iâ€–â‚Š := by
  simp [linfty_opNNNorm_def]

@[deprecated (since := "2025-03-20")] alias linfty_opNNNorm_row := linfty_opNNNorm_replicateRow

@[simp]
theorem linfty_opNorm_replicateRow (v : n â†’ Î±) : â€–replicateRow Î¹ vâ€– = âˆ‘ i, â€–v iâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_opNNNorm_replicateRow v).trans <| by simp [NNReal.coe_sum]

@[deprecated (since := "2025-03-20")] alias linfty_opNorm_row := linfty_opNNNorm_replicateRow

@[simp]
theorem linfty_opNNNorm_diagonal [DecidableEq m] (v : m â†’ Î±) : â€–diagonal vâ€–â‚Š = â€–vâ€–â‚Š := by
  rw [linfty_opNNNorm_def, Pi.nnnorm_def]
  congr 1 with i : 1
  refine (Finset.sum_eq_single_of_mem _ (Finset.mem_univ i) fun j _hj hij => ?_).trans ?_
  Â· rw [diagonal_apply_ne' _ hij, nnnorm_zero]
  Â· rw [diagonal_apply_eq]

@[simp]
theorem linfty_opNorm_diagonal [DecidableEq m] (v : m â†’ Î±) : â€–diagonal vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_opNNNorm_diagonal v

end SeminormedAddCommGroup

section NonUnitalSeminormedRing

variable [NonUnitalSeminormedRing Î±]

theorem linfty_opNNNorm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–Bâ€–â‚Š := by
  simp_rw [linfty_opNNNorm_def, Matrix.mul_apply]
  calc
    (Finset.univ.sup fun i => âˆ‘ k, â€–âˆ‘ j, A i j * B j kâ€–â‚Š) â‰¤
        Finset.univ.sup fun i => âˆ‘ k, âˆ‘ j, â€–A i jâ€–â‚Š * â€–B j kâ€–â‚Š :=
      Finset.sup_mono_fun fun i _hi =>
        Finset.sum_le_sum fun k _hk => nnnorm_sum_le_of_le _ fun j _hj => nnnorm_mul_le _ _
    _ = Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š * âˆ‘ k, â€–B j kâ€–â‚Š := by
      simp_rw [@Finset.sum_comm m, Finset.mul_sum]
    _ â‰¤ Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š * Finset.univ.sup fun i => âˆ‘ j, â€–B i jâ€–â‚Š := by
      refine Finset.sup_mono_fun fun i _hi => ?_
      gcongr with j hj
      exact Finset.le_sup (f := fun i â†¦ âˆ‘ k : n, â€–B i kâ€–â‚Š) hj
    _ â‰¤ (Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š) * Finset.univ.sup fun i => âˆ‘ j, â€–B i jâ€–â‚Š := by
      simp_rw [â† Finset.sum_mul, â† NNReal.finset_sup_mul]
      rfl

theorem linfty_opNorm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€– â‰¤ â€–Aâ€– * â€–Bâ€– :=
  linfty_opNNNorm_mul _ _

theorem linfty_opNNNorm_mulVec (A : Matrix l m Î±) (v : m â†’ Î±) : â€–A *áµ¥ vâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–vâ€–â‚Š := by
  rw [â† linfty_opNNNorm_replicateCol (Î¹ := Fin 1) (A *áµ¥ v),
    â† linfty_opNNNorm_replicateCol v (Î¹ := Fin 1)]
  exact linfty_opNNNorm_mul A (replicateCol (Fin 1) v)

theorem linfty_opNorm_mulVec (A : Matrix l m Î±) (v : m â†’ Î±) : â€–A *áµ¥ vâ€– â‰¤ â€–Aâ€– * â€–vâ€– :=
  linfty_opNNNorm_mulVec _ _

end NonUnitalSeminormedRing

/-- Seminormed non-unital ring instance (using sup norm of L1 norm) for matrices over a semi normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalSemiNormedRing [NonUnitalSeminormedRing Î±] :
    NonUnitalSeminormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpSeminormedAddCommGroup, Matrix.instNonUnitalRing with
    norm_mul_le := linfty_opNorm_mul }

/-- The `Lâ‚-Lâˆ` norm preserves one on non-empty matrices. Note this is safe as an instance, as it
carries no data. -/
instance linfty_opNormOneClass [SeminormedRing Î±] [NormOneClass Î±] [DecidableEq n] [Nonempty n] :
    NormOneClass (Matrix n n Î±) where norm_one := (linfty_opNorm_diagonal _).trans norm_one

/-- Seminormed ring instance (using sup norm of L1 norm) for matrices over a semi normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSemiNormedRing [SeminormedRing Î±] [DecidableEq n] :
    SeminormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing, Matrix.instRing with }

/-- Normed non-unital ring instance (using sup norm of L1 norm) for matrices over a normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalNormedRing [NonUnitalNormedRing Î±] :
    NonUnitalNormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed ring instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedRing [NormedRing Î±] [DecidableEq n] : NormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed algebra instance (using sup norm of L1 norm) for matrices over a normed algebra. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAlgebra [NormedField R] [SeminormedRing Î±] [NormedAlgebra R Î±]
    [DecidableEq n] : NormedAlgebra R (Matrix n n Î±) :=
  { Matrix.linftyOpNormedSpace, Matrix.instAlgebra with }


section
variable [NormedDivisionRing Î±] [NormedAlgebra â„ Î±]

/-- Auxiliary construction; an element of norm 1 such that `a * unitOf a = â€–aâ€–`. -/
private def unitOf (a : Î±) : Î± := by classical exact if a = 0 then 1 else â€–aâ€– â€¢ aâ»Â¹

private theorem norm_unitOf (a : Î±) : â€–unitOf aâ€–â‚Š = 1 := by
  rw [unitOf]
  split_ifs with h
  Â· simp
  Â· rw [â† nnnorm_eq_zero] at h
    rw [nnnorm_smul, nnnorm_inv, nnnorm_norm, mul_inv_cancelâ‚€ h]

private theorem mul_unitOf (a : Î±) : a * unitOf a = algebraMap _ _ (â€–aâ€–â‚Š : â„)  := by
  simp only [unitOf, coe_nnnorm]
  split_ifs with h
  Â· simp [h]
  Â· rw [mul_smul_comm, mul_inv_cancelâ‚€ h, Algebra.algebraMap_eq_smul_one]

end

/-!
For a matrix over a field, the norm defined in this section agrees with the operator norm on
`ContinuousLinearMap`s between function types (which have the infinity norm).
-/
section
variable [NontriviallyNormedField Î±] [NormedAlgebra â„ Î±]

lemma linfty_opNNNorm_eq_opNNNorm (A : Matrix m n Î±) :
    â€–Aâ€–â‚Š = â€–ContinuousLinearMap.mk (Matrix.mulVecLin A)â€–â‚Š := by
  rw [ContinuousLinearMap.opNNNorm_eq_of_bounds _ (linfty_opNNNorm_mulVec _) fun N hN => ?_]
  rw [linfty_opNNNorm_def]
  refine Finset.sup_le fun i _ => ?_
  cases isEmpty_or_nonempty n
  Â· simp
  classical
  let x : n â†’ Î± := fun j => unitOf (A i j)
  have hxn : â€–xâ€–â‚Š = 1 := by
    simp_rw [x, Pi.nnnorm_def, norm_unitOf, Finset.sup_const Finset.univ_nonempty]
  specialize hN x
  rw [hxn, mul_one, Pi.nnnorm_def, Finset.sup_le_iff] at hN
  replace hN := hN i (Finset.mem_univ _)
  dsimp [mulVec, dotProduct] at hN
  simp_rw [x, mul_unitOf, â† map_sum, nnnorm_algebraMap, â† NNReal.coe_sum, NNReal.nnnorm_eq,
    nnnorm_one, mul_one] at hN
  exact hN

lemma linfty_opNorm_eq_opNorm (A : Matrix m n Î±) :
    â€–Aâ€– = â€–ContinuousLinearMap.mk (Matrix.mulVecLin A)â€– :=
  congr_arg NNReal.toReal (linfty_opNNNorm_eq_opNNNorm A)

variable [DecidableEq n]

@[simp] lemma linfty_opNNNorm_toMatrix (f : (n â†’ Î±) â†’L[Î±] (m â†’ Î±)) :
    â€–LinearMap.toMatrix' (â†‘f : (n â†’ Î±) â†’â‚—[Î±] (m â†’ Î±))â€–â‚Š = â€–fâ€–â‚Š := by
  rw [linfty_opNNNorm_eq_opNNNorm]
  simp only [â† toLin'_apply', toLin'_toMatrix']

@[simp] lemma linfty_opNorm_toMatrix (f : (n â†’ Î±) â†’L[Î±] (m â†’ Î±)) :
    â€–LinearMap.toMatrix' (â†‘f : (n â†’ Î±) â†’â‚—[Î±] (m â†’ Î±))â€– = â€–fâ€– :=
  congr_arg NNReal.toReal (linfty_opNNNorm_toMatrix f)

end

namespace Norms.Operator
attribute [scoped instance]
  Matrix.linftyOpSeminormedAddCommGroup
  Matrix.linftyOpNormedAddCommGroup
  Matrix.linftyOpNormedSpace
  Matrix.linftyOpIsBoundedSMul
  Matrix.linftyOpNormSMulClass
  Matrix.linftyOpNonUnitalSemiNormedRing
  Matrix.linftyOpSemiNormedRing
  Matrix.linftyOpNonUnitalNormedRing
  Matrix.linftyOpNormedRing
  Matrix.linftyOpNormedAlgebra
end Norms.Operator

end LinftyOp

/-! ### The Frobenius norm

This is defined as $\|A\| = \sqrt{\sum_{i,j} \|A_{ij}\|^2}$.
When the matrix is over the real or complex numbers, this norm is submultiplicative.
-/


section frobenius

open scoped Matrix

/-- Seminormed group instance (using the Frobenius norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusSeminormedAddCommGroup [SeminormedAddCommGroup Î±] :
    SeminormedAddCommGroup (Matrix m n Î±) :=
  inferInstanceAs (SeminormedAddCommGroup (PiLp 2 fun _i : m => PiLp 2 fun _j : n => Î±))

/-- Normed group instance (using the Frobenius norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAddCommGroup [NormedAddCommGroup Î±] : NormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : NormedAddCommGroup (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))

/-- This applies to the Frobenius norm. -/
@[local instance]
theorem frobeniusIsBoundedSMul [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±]
    [IsBoundedSMul R Î±] :
    IsBoundedSMul R (Matrix m n Î±) :=
  (by infer_instance : IsBoundedSMul R (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))

/-- This applies to the Frobenius norm. -/
@[local instance]
theorem frobeniusNormSMulClass [SeminormedRing R] [SeminormedAddCommGroup Î±] [Module R Î±]
    [NormSMulClass R Î±] :
    NormSMulClass R (Matrix m n Î±) :=
  (by infer_instance : NormSMulClass R (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))

@[deprecated (since := "2025-03-10")] alias frobeniusBoundedSMul := frobeniusIsBoundedSMul

/-- Normed space instance (using the Frobenius norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedSpace [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±] :
    NormedSpace R (Matrix m n Î±) :=
  (by infer_instance : NormedSpace R (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±] [SeminormedAddCommGroup Î²]

theorem frobenius_nnnorm_def (A : Matrix m n Î±) :
    â€–Aâ€–â‚Š = (âˆ‘ i, âˆ‘ j, â€–A i jâ€–â‚Š ^ (2 : â„)) ^ (1 / 2 : â„) := by
  change â€–toLp 2 fun i => toLp 2 fun j => A i jâ€–â‚Š = _
  simp_rw [PiLp.nnnorm_eq_of_L2, NNReal.sq_sqrt, NNReal.sqrt_eq_rpow, NNReal.rpow_two,
    PiLp.toLp_apply]

theorem frobenius_norm_def (A : Matrix m n Î±) :
    â€–Aâ€– = (âˆ‘ i, âˆ‘ j, â€–A i jâ€– ^ (2 : â„)) ^ (1 / 2 : â„) :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) (frobenius_nnnorm_def A)).trans <| by simp [NNReal.coe_sum]

@[simp]
theorem frobenius_nnnorm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€–â‚Š = â€–aâ€–â‚Š) :
    â€–A.map fâ€–â‚Š = â€–Aâ€–â‚Š := by simp_rw [frobenius_nnnorm_def, Matrix.map_apply, hf]

@[simp]
theorem frobenius_norm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€– = â€–aâ€–) :
    â€–A.map fâ€– = â€–Aâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_map_eq A f fun a => Subtype.ext <| hf a :)

@[simp]
theorem frobenius_nnnorm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€–â‚Š = â€–Aâ€–â‚Š := by
  rw [frobenius_nnnorm_def, frobenius_nnnorm_def, Finset.sum_comm]
  simp_rw [Matrix.transpose_apply]

@[simp]
theorem frobenius_norm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_transpose A

@[simp]
theorem frobenius_nnnorm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€–â‚Š = â€–Aâ€–â‚Š :=
  (frobenius_nnnorm_map_eq _ _ nnnorm_star).trans A.frobenius_nnnorm_transpose

@[simp]
theorem frobenius_norm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_conjTranspose A

instance frobenius_normedStarGroup [StarAddMonoid Î±] [NormedStarGroup Î±] :
    NormedStarGroup (Matrix m m Î±) :=
  âŸ¨(le_of_eq <| frobenius_norm_conjTranspose Â·)âŸ©

@[simp]
lemma frobenius_norm_replicateRow (v : m â†’ Î±) : â€–replicateRow Î¹ vâ€– = â€–toLp 2 vâ€– := by
  rw [frobenius_norm_def, Fintype.sum_unique, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]
  simp only [replicateRow_apply, Real.rpow_two, PiLp.toLp_apply]

@[deprecated (since := "2025-03-20")] alias frobenius_norm_row := frobenius_norm_replicateRow

@[simp]
lemma frobenius_nnnorm_replicateRow (v : m â†’ Î±) : â€–replicateRow Î¹ vâ€–â‚Š = â€–toLp 2 vâ€–â‚Š :=
  Subtype.ext <| frobenius_norm_replicateRow v

@[deprecated (since := "2025-03-20")] alias frobenius_nnnorm_row := frobenius_nnnorm_replicateRow

@[simp]
lemma frobenius_norm_replicateCol (v : n â†’ Î±) : â€–replicateCol Î¹ vâ€– = â€–toLp 2 vâ€– := by
  simp [frobenius_norm_def, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]

@[deprecated (since := "2025-03-20")] alias frobenius_norm_col := frobenius_norm_replicateCol

@[simp]
lemma frobenius_nnnorm_replicateCol (v : n â†’ Î±) : â€–replicateCol Î¹ vâ€–â‚Š = â€–toLp 2 vâ€–â‚Š :=
  Subtype.ext <| frobenius_norm_replicateCol v

@[deprecated (since := "2025-03-20")] alias frobenius_nnnorm_col := frobenius_nnnorm_replicateCol

@[simp]
lemma frobenius_nnnorm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€–â‚Š = â€–toLp 2 vâ€–â‚Š := by
  simp_rw [frobenius_nnnorm_def, â† Finset.sum_product', Finset.univ_product_univ,
    PiLp.nnnorm_eq_of_L2]
  let s := (Finset.univ : Finset n).map âŸ¨fun i : n => (i, i), fun i j h => congr_arg Prod.fst hâŸ©
  rw [â† Finset.sum_subset (Finset.subset_univ s) fun i _hi his => ?_]
  Â· rw [Finset.sum_map, NNReal.sqrt_eq_rpow]
    dsimp
    simp_rw [diagonal_apply_eq, NNReal.rpow_two]
  Â· suffices i.1 â‰  i.2 by rw [diagonal_apply_ne _ this, nnnorm_zero, NNReal.zero_rpow two_ne_zero]
    intro h
    exact Finset.mem_map.not.mp his âŸ¨i.1, Finset.mem_univ _, Prod.ext rfl hâŸ©

@[simp]
lemma frobenius_norm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€– = â€–toLp 2 vâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_diagonal v :).trans rfl

end SeminormedAddCommGroup

theorem frobenius_nnnorm_one [DecidableEq n] [SeminormedAddCommGroup Î±] [One Î±] :
    â€–(1 : Matrix n n Î±)â€–â‚Š = .sqrt (Fintype.card n) * â€–(1 : Î±)â€–â‚Š := by
  calc
    â€–(diagonal 1 : Matrix n n Î±)â€–â‚Š
    _ = â€–toLp 2 (Function.const _ 1)â€–â‚Š := frobenius_nnnorm_diagonal _
    _ = .sqrt (Fintype.card n) * â€–(1 : Î±)â€–â‚Š := by
      rw [PiLp.nnnorm_toLp_const (ENNReal.ofNat_ne_top (n := 2))]
      simp [NNReal.sqrt_eq_rpow]

section RCLike

variable [RCLike Î±]

theorem frobenius_nnnorm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–Bâ€–â‚Š := by
  simp_rw [frobenius_nnnorm_def, Matrix.mul_apply]
  rw [â† NNReal.mul_rpow, @Finset.sum_comm _ _ m, Finset.sum_mul_sum]
  gcongr with i _ j
  rw [â† NNReal.rpow_le_rpow_iff one_half_pos, â† NNReal.rpow_mul,
    mul_div_cancelâ‚€ (1 : â„) two_ne_zero, NNReal.rpow_one, NNReal.mul_rpow]
  simpa only [PiLp.toLp_apply, PiLp.inner_apply, RCLike.inner_apply', starRingEnd_apply,
    Pi.nnnorm_def, PiLp.nnnorm_eq_of_L2, star_star, nnnorm_star, NNReal.sqrt_eq_rpow,
    NNReal.rpow_two] using nnnorm_inner_le_nnnorm (ğ•œ := Î±) (toLp 2 (star <| A i Â·)) (toLp 2 (B Â· j))

theorem frobenius_norm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€– â‰¤ â€–Aâ€– * â€–Bâ€– :=
  frobenius_nnnorm_mul A B

/-- Normed ring instance (using the Frobenius norm) for matrices over `â„` or `â„‚`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedRing [DecidableEq m] : NormedRing (Matrix m m Î±) :=
  { Matrix.instRing, Matrix.frobeniusSeminormedAddCommGroup with
    norm_mul_le := frobenius_norm_mul
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }

/-- Normed algebra instance (using the Frobenius norm) for matrices over `â„` or `â„‚`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAlgebra [DecidableEq m] [NormedField R] [NormedAlgebra R Î±] :
    NormedAlgebra R (Matrix m m Î±) :=
  { Matrix.frobeniusNormedSpace, Matrix.instAlgebra with }

end RCLike

end frobenius

namespace Norms.Frobenius
attribute [scoped instance]
  Matrix.frobeniusSeminormedAddCommGroup
  Matrix.frobeniusNormedAddCommGroup
  Matrix.frobeniusNormedSpace
  Matrix.frobeniusNormedRing
  Matrix.frobeniusNormedAlgebra
  Matrix.frobeniusIsBoundedSMul
  Matrix.frobeniusNormSMulClass
end Norms.Frobenius

end Matrix
