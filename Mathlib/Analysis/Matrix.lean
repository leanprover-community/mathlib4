/-
Copyright (c) 2021 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth, Eric Wieser
-/
import Mathlib.Analysis.NormedSpace.Basic
import Mathlib.Analysis.NormedSpace.PiLp
import Mathlib.Analysis.InnerProductSpace.PiL2

#align_import analysis.matrix from "leanprover-community/mathlib"@"46b633fd842bef9469441c0209906f6dddd2b4f5"

/-!
# Matrices as a normed space

In this file we provide the following non-instances for norms on matrices:

* The elementwise norm:

  * `Matrix.seminormedAddCommGroup`
  * `Matrix.normedAddCommGroup`
  * `Matrix.normedSpace`

* The Frobenius norm:

  * `Matrix.frobeniusSeminormedAddCommGroup`
  * `Matrix.frobeniusNormedAddCommGroup`
  * `Matrix.frobeniusNormedSpace`
  * `Matrix.frobeniusNormedRing`
  * `Matrix.frobeniusNormedAlgebra`

* The $L^\infty$ operator norm:

  * `Matrix.linftyOpSeminormedAddCommGroup`
  * `Matrix.linftyOpNormedAddCommGroup`
  * `Matrix.linftyOpNormedSpace`
  * `Matrix.linftyOpNonUnitalSemiNormedRing`
  * `Matrix.linftyOpSemiNormedRing`
  * `Matrix.linftyOpNonUnitalNormedRing`
  * `Matrix.linftyOpNormedRing`
  * `Matrix.linftyOpNormedAlgebra`

These are not declared as instances because there are several natural choices for defining the norm
of a matrix.
-/

local macro_rules | `($x ^ $y) => `(HPow.hPow $x $y) -- Porting note: See issue lean4#2220


noncomputable section

open scoped BigOperators NNReal Matrix

namespace Matrix

variable {R l m n Î± Î² : Type*} [Fintype l] [Fintype m] [Fintype n]

/-! ### The elementwise supremum norm -/


section LinfLinf

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±] [SeminormedAddCommGroup Î²]

/-- Seminormed group instance (using sup norm of sup norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def seminormedAddCommGroup : SeminormedAddCommGroup (Matrix m n Î±) :=
  Pi.seminormedAddCommGroup
#align matrix.seminormed_add_comm_group Matrix.seminormedAddCommGroup


attribute [local instance] Matrix.seminormedAddCommGroup

-- porting note: new (along with all the uses of this lemma below)
theorem norm_def (A : Matrix m n Î±) : â€–Aâ€– = â€–fun i j => A i jâ€– := rfl

-- porting note: new  (along with all the uses of this lemma below)
theorem nnnorm_def (A : Matrix m n Î±) : â€–Aâ€–â‚Š = â€–fun i j => A i jâ€–â‚Š := rfl

theorem norm_le_iff {r : â„} (hr : 0 â‰¤ r) {A : Matrix m n Î±} : â€–Aâ€– â‰¤ r â†” âˆ€ i j, â€–A i jâ€– â‰¤ r := by
  simp_rw [norm_def, pi_norm_le_iff_of_nonneg hr]
  -- ğŸ‰ no goals
#align matrix.norm_le_iff Matrix.norm_le_iff

theorem nnnorm_le_iff {r : â„â‰¥0} {A : Matrix m n Î±} : â€–Aâ€–â‚Š â‰¤ r â†” âˆ€ i j, â€–A i jâ€–â‚Š â‰¤ r := by
  simp_rw [nnnorm_def, pi_nnnorm_le_iff]
  -- ğŸ‰ no goals
#align matrix.nnnorm_le_iff Matrix.nnnorm_le_iff

theorem norm_lt_iff {r : â„} (hr : 0 < r) {A : Matrix m n Î±} : â€–Aâ€– < r â†” âˆ€ i j, â€–A i jâ€– < r := by
  simp_rw [norm_def, pi_norm_lt_iff hr]
  -- ğŸ‰ no goals
#align matrix.norm_lt_iff Matrix.norm_lt_iff

theorem nnnorm_lt_iff {r : â„â‰¥0} (hr : 0 < r) {A : Matrix m n Î±} :
    â€–Aâ€–â‚Š < r â†” âˆ€ i j, â€–A i jâ€–â‚Š < r := by
  simp_rw [nnnorm_def, pi_nnnorm_lt_iff hr]
  -- ğŸ‰ no goals
#align matrix.nnnorm_lt_iff Matrix.nnnorm_lt_iff

theorem norm_entry_le_entrywise_sup_norm (A : Matrix m n Î±) {i : m} {j : n} : â€–A i jâ€– â‰¤ â€–Aâ€– :=
  (norm_le_pi_norm (A i) j).trans (norm_le_pi_norm A i)
#align matrix.norm_entry_le_entrywise_sup_norm Matrix.norm_entry_le_entrywise_sup_norm

theorem nnnorm_entry_le_entrywise_sup_nnnorm (A : Matrix m n Î±) {i : m} {j : n} : â€–A i jâ€–â‚Š â‰¤ â€–Aâ€–â‚Š :=
  (nnnorm_le_pi_nnnorm (A i) j).trans (nnnorm_le_pi_nnnorm A i)
#align matrix.nnnorm_entry_le_entrywise_sup_nnnorm Matrix.nnnorm_entry_le_entrywise_sup_nnnorm

@[simp]
theorem nnnorm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€–â‚Š = â€–aâ€–â‚Š) :
    â€–A.map fâ€–â‚Š = â€–Aâ€–â‚Š := by
  simp only [nnnorm_def, Pi.nnnorm_def, Matrix.map_apply, hf]
  -- ğŸ‰ no goals
#align matrix.nnnorm_map_eq Matrix.nnnorm_map_eq

@[simp]
theorem norm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€– = â€–aâ€–) : â€–A.map fâ€– = â€–Aâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_map_eq A f fun a => Subtype.ext <| hf a : _)
#align matrix.norm_map_eq Matrix.norm_map_eq

@[simp]
theorem nnnorm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€–â‚Š = â€–Aâ€–â‚Š :=
  Finset.sup_comm _ _ _
#align matrix.nnnorm_transpose Matrix.nnnorm_transpose

@[simp]
theorem norm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_transpose A
#align matrix.norm_transpose Matrix.norm_transpose

@[simp]
theorem nnnorm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€–â‚Š = â€–Aâ€–â‚Š :=
  (nnnorm_map_eq _ _ nnnorm_star).trans A.nnnorm_transpose
#align matrix.nnnorm_conj_transpose Matrix.nnnorm_conjTranspose

@[simp]
theorem norm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) : â€–Aá´´â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_conjTranspose A
#align matrix.norm_conj_transpose Matrix.norm_conjTranspose

instance [StarAddMonoid Î±] [NormedStarGroup Î±] : NormedStarGroup (Matrix m m Î±) :=
  âŸ¨norm_conjTransposeâŸ©

@[simp]
theorem nnnorm_col (v : m â†’ Î±) : â€–col vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp [nnnorm_def, Pi.nnnorm_def]
  -- ğŸ‰ no goals
#align matrix.nnnorm_col Matrix.nnnorm_col

@[simp]
theorem norm_col (v : m â†’ Î±) : â€–col vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_col v
#align matrix.norm_col Matrix.norm_col

@[simp]
theorem nnnorm_row (v : n â†’ Î±) : â€–row vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp [nnnorm_def, Pi.nnnorm_def]
  -- ğŸ‰ no goals
#align matrix.nnnorm_row Matrix.nnnorm_row

@[simp]
theorem norm_row (v : n â†’ Î±) : â€–row vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_row v
#align matrix.norm_row Matrix.norm_row

@[simp]
theorem nnnorm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€–â‚Š = â€–vâ€–â‚Š := by
  simp_rw [nnnorm_def, Pi.nnnorm_def]
  -- âŠ¢ (Finset.sup Finset.univ fun b => Finset.sup Finset.univ fun b_1 => â€–diagonal â€¦
  congr 1 with i : 1
  -- âŠ¢ (Finset.sup Finset.univ fun b => â€–diagonal v i bâ€–â‚Š) = â€–v iâ€–â‚Š
  refine' le_antisymm (Finset.sup_le fun j hj => _) _
  -- âŠ¢ â€–diagonal v i jâ€–â‚Š â‰¤ â€–v iâ€–â‚Š
  Â· obtain rfl | hij := eq_or_ne i j
    -- âŠ¢ â€–diagonal v i iâ€–â‚Š â‰¤ â€–v iâ€–â‚Š
    Â· rw [diagonal_apply_eq]
      -- ğŸ‰ no goals
    Â· rw [diagonal_apply_ne _ hij, nnnorm_zero]
      -- âŠ¢ 0 â‰¤ â€–v iâ€–â‚Š
      exact zero_le _
      -- ğŸ‰ no goals
  Â· refine' Eq.trans_le _ (Finset.le_sup (Finset.mem_univ i))
    -- âŠ¢ â€–v iâ€–â‚Š = â€–diagonal v i iâ€–â‚Š
    rw [diagonal_apply_eq]
    -- ğŸ‰ no goals
#align matrix.nnnorm_diagonal Matrix.nnnorm_diagonal

@[simp]
theorem norm_diagonal [DecidableEq n] (v : n â†’ Î±) : â€–diagonal vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| nnnorm_diagonal v
#align matrix.norm_diagonal Matrix.norm_diagonal

/-- Note this is safe as an instance as it carries no data. -/
-- Porting note: not yet implemented: `@[nolint fails_quickly]`
instance [Nonempty n] [DecidableEq n] [One Î±] [NormOneClass Î±] : NormOneClass (Matrix n n Î±) :=
  âŸ¨(norm_diagonal _).trans <| norm_oneâŸ©

end SeminormedAddCommGroup

/-- Normed group instance (using sup norm of sup norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedAddCommGroup [NormedAddCommGroup Î±] : NormedAddCommGroup (Matrix m n Î±) :=
  Pi.normedAddCommGroup
#align matrix.normed_add_comm_group Matrix.normedAddCommGroup

section NormedSpace

attribute [local instance] Matrix.seminormedAddCommGroup

variable [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±]

/-- Normed space instance (using sup norm of sup norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
protected def normedSpace : NormedSpace R (Matrix m n Î±) :=
  Pi.normedSpace
#align matrix.normed_space Matrix.normedSpace

end NormedSpace

end LinfLinf

/-! ### The $L_\infty$ operator norm

This section defines the matrix norm $\|A\|_\infty = \operatorname{sup}_i (\sum_j \|A_{ij}\|)$.

Note that this is equivalent to the operator norm, considering $A$ as a linear map between two
$L^\infty$ spaces.
-/


section LinftyOp

/-- Seminormed group instance (using sup norm of L1 norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSeminormedAddCommGroup [SeminormedAddCommGroup Î±] :
    SeminormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : SeminormedAddCommGroup (m â†’ PiLp 1 fun j : n => Î±))
      -- ğŸ‰ no goals
#align matrix.linfty_op_seminormed_add_comm_group Matrix.linftyOpSeminormedAddCommGroup

/-- Normed group instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAddCommGroup [NormedAddCommGroup Î±] :
    NormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : NormedAddCommGroup (m â†’ PiLp 1 fun j : n => Î±))
      -- ğŸ‰ no goals
#align matrix.linfty_op_normed_add_comm_group Matrix.linftyOpNormedAddCommGroup

/-- Normed space instance (using sup norm of L1 norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedSpace [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±] :
    NormedSpace R (Matrix m n Î±) :=
  (by infer_instance : NormedSpace R (m â†’ PiLp 1 fun j : n => Î±))
      -- ğŸ‰ no goals
#align matrix.linfty_op_normed_space Matrix.linftyOpNormedSpace

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±]

theorem linfty_op_norm_def (A : Matrix m n Î±) :
    â€–Aâ€– = ((Finset.univ : Finset m).sup fun i : m => âˆ‘ j : n, â€–A i jâ€–â‚Š : â„â‰¥0) := by
  -- porting note: added
  change â€–fun i => (PiLp.equiv 1 _).symm (A i)â€– = _
  -- âŠ¢ â€–fun i => â†‘(PiLp.equiv 1 fun i => Î±).symm (A i)â€– = â†‘(Finset.sup Finset.univ  â€¦
  simp [Pi.norm_def, PiLp.nnnorm_eq_sum ENNReal.one_ne_top]
  -- ğŸ‰ no goals
#align matrix.linfty_op_norm_def Matrix.linfty_op_norm_def

theorem linfty_op_nnnorm_def (A : Matrix m n Î±) :
    â€–Aâ€–â‚Š = (Finset.univ : Finset m).sup fun i : m => âˆ‘ j : n, â€–A i jâ€–â‚Š :=
  Subtype.ext <| linfty_op_norm_def A
#align matrix.linfty_op_nnnorm_def Matrix.linfty_op_nnnorm_def

@[simp, nolint simpNF] -- Porting note: linter times out
theorem linfty_op_nnnorm_col (v : m â†’ Î±) : â€–col vâ€–â‚Š = â€–vâ€–â‚Š := by
  rw [linfty_op_nnnorm_def, Pi.nnnorm_def]
  -- âŠ¢ (Finset.sup Finset.univ fun i => âˆ‘ j : Unit, â€–col v i jâ€–â‚Š) = Finset.sup Fins â€¦
  simp
  -- ğŸ‰ no goals
#align matrix.linfty_op_nnnorm_col Matrix.linfty_op_nnnorm_col

@[simp]
theorem linfty_op_norm_col (v : m â†’ Î±) : â€–col vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_op_nnnorm_col v
#align matrix.linfty_op_norm_col Matrix.linfty_op_norm_col

@[simp]
theorem linfty_op_nnnorm_row (v : n â†’ Î±) : â€–row vâ€–â‚Š = âˆ‘ i, â€–v iâ€–â‚Š := by simp [linfty_op_nnnorm_def]
                                                                        -- ğŸ‰ no goals
#align matrix.linfty_op_nnnorm_row Matrix.linfty_op_nnnorm_row

@[simp]
theorem linfty_op_norm_row (v : n â†’ Î±) : â€–row vâ€– = âˆ‘ i, â€–v iâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_op_nnnorm_row v).trans <| by simp [NNReal.coe_sum]
                                                                    -- ğŸ‰ no goals
#align matrix.linfty_op_norm_row Matrix.linfty_op_norm_row

@[simp]
theorem linfty_op_nnnorm_diagonal [DecidableEq m] (v : m â†’ Î±) : â€–diagonal vâ€–â‚Š = â€–vâ€–â‚Š := by
  rw [linfty_op_nnnorm_def, Pi.nnnorm_def]
  -- âŠ¢ (Finset.sup Finset.univ fun i => âˆ‘ j : m, â€–diagonal v i jâ€–â‚Š) = Finset.sup Fi â€¦
  congr 1 with i : 1
  -- âŠ¢ âˆ‘ j : m, â€–diagonal v i jâ€–â‚Š = â€–v iâ€–â‚Š
  refine' (Finset.sum_eq_single_of_mem _ (Finset.mem_univ i) fun j _hj hij => _).trans _
  -- âŠ¢ â€–diagonal v i jâ€–â‚Š = 0
  Â· rw [diagonal_apply_ne' _ hij, nnnorm_zero]
    -- ğŸ‰ no goals
  Â· rw [diagonal_apply_eq]
    -- ğŸ‰ no goals
#align matrix.linfty_op_nnnorm_diagonal Matrix.linfty_op_nnnorm_diagonal

@[simp]
theorem linfty_op_norm_diagonal [DecidableEq m] (v : m â†’ Î±) : â€–diagonal vâ€– = â€–vâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| linfty_op_nnnorm_diagonal v
#align matrix.linfty_op_norm_diagonal Matrix.linfty_op_norm_diagonal

end SeminormedAddCommGroup

section NonUnitalSeminormedRing

variable [NonUnitalSeminormedRing Î±]

/- ./././Mathport/Syntax/Translate/Expr.lean:107:6: warning: expanding binder group (k j) -/
theorem linfty_op_nnnorm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–Bâ€–â‚Š := by
  simp_rw [linfty_op_nnnorm_def, Matrix.mul_apply]
  -- âŠ¢ (Finset.sup Finset.univ fun i => âˆ‘ x : n, â€–âˆ‘ j : m, A i j * B j xâ€–â‚Š) â‰¤ (Fins â€¦
  calc
    (Finset.univ.sup fun i => âˆ‘ k, â€–âˆ‘ j, A i j * B j kâ€–â‚Š) â‰¤
        Finset.univ.sup fun i => âˆ‘ k, âˆ‘ j, â€–A i jâ€–â‚Š * â€–B j kâ€–â‚Š :=
      Finset.sup_mono_fun fun i _hi =>
        Finset.sum_le_sum fun k _hk => nnnorm_sum_le_of_le _ fun j _hj => nnnorm_mul_le _ _
    _ = Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š * âˆ‘ k, â€–B j kâ€–â‚Š := by
      simp_rw [@Finset.sum_comm _ m n, Finset.mul_sum]
    _ â‰¤ Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š * Finset.univ.sup fun i => âˆ‘ j, â€–B i jâ€–â‚Š := by
      refine Finset.sup_mono_fun fun i _hi => ?_
      gcongr with j hj
      exact Finset.le_sup (f := fun i â†¦ âˆ‘ k : n, â€–B i kâ€–â‚Š) hj
    _ â‰¤ (Finset.univ.sup fun i => âˆ‘ j, â€–A i jâ€–â‚Š) * Finset.univ.sup fun i => âˆ‘ j, â€–B i jâ€–â‚Š := by
      simp_rw [â† Finset.sum_mul, â† NNReal.finset_sup_mul]
      rfl
#align matrix.linfty_op_nnnorm_mul Matrix.linfty_op_nnnorm_mul

theorem linfty_op_norm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€– â‰¤ â€–Aâ€– * â€–Bâ€– :=
  linfty_op_nnnorm_mul _ _
#align matrix.linfty_op_norm_mul Matrix.linfty_op_norm_mul

theorem linfty_op_nnnorm_mulVec (A : Matrix l m Î±) (v : m â†’ Î±) : â€–A.mulVec vâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–vâ€–â‚Š := by
  rw [â† linfty_op_nnnorm_col (A.mulVec v), â† linfty_op_nnnorm_col v]
  -- âŠ¢ â€–col (mulVec A v)â€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–col vâ€–â‚Š
  exact linfty_op_nnnorm_mul A (col v)
  -- ğŸ‰ no goals
#align matrix.linfty_op_nnnorm_mul_vec Matrix.linfty_op_nnnorm_mulVec

theorem linfty_op_norm_mulVec (A : Matrix l m Î±) (v : m â†’ Î±) : â€–Matrix.mulVec A vâ€– â‰¤ â€–Aâ€– * â€–vâ€– :=
  linfty_op_nnnorm_mulVec _ _
#align matrix.linfty_op_norm_mul_vec Matrix.linfty_op_norm_mulVec

end NonUnitalSeminormedRing

/-- Seminormed non-unital ring instance (using sup norm of L1 norm) for matrices over a semi normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalSemiNormedRing [NonUnitalSeminormedRing Î±] :
    NonUnitalSeminormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpSeminormedAddCommGroup, Matrix.instNonUnitalRing with
    norm_mul := linfty_op_norm_mul }
#align matrix.linfty_op_non_unital_semi_normed_ring Matrix.linftyOpNonUnitalSemiNormedRing

/-- The `Lâ‚-Lâˆ` norm preserves one on non-empty matrices. Note this is safe as an instance, as it
carries no data. -/
instance linfty_op_normOneClass [SeminormedRing Î±] [NormOneClass Î±] [DecidableEq n] [Nonempty n] :
    NormOneClass (Matrix n n Î±) where norm_one := (linfty_op_norm_diagonal _).trans norm_one
#align matrix.linfty_op_norm_one_class Matrix.linfty_op_normOneClass

/-- Seminormed ring instance (using sup norm of L1 norm) for matrices over a semi normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpSemiNormedRing [SeminormedRing Î±] [DecidableEq n] :
    SeminormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing, Matrix.instRing with }
#align matrix.linfty_op_semi_normed_ring Matrix.linftyOpSemiNormedRing

/-- Normed non-unital ring instance (using sup norm of L1 norm) for matrices over a normed
non-unital ring. Not declared as an instance because there are several natural choices for defining
the norm of a matrix. -/
@[local instance]
protected def linftyOpNonUnitalNormedRing [NonUnitalNormedRing Î±] :
    NonUnitalNormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpNonUnitalSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }
#align matrix.linfty_op_non_unital_normed_ring Matrix.linftyOpNonUnitalNormedRing

/-- Normed ring instance (using sup norm of L1 norm) for matrices over a normed ring.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedRing [NormedRing Î±] [DecidableEq n] : NormedRing (Matrix n n Î±) :=
  { Matrix.linftyOpSemiNormedRing with
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }
#align matrix.linfty_op_normed_ring Matrix.linftyOpNormedRing

/-- Normed algebra instance (using sup norm of L1 norm) for matrices over a normed algebra. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
protected def linftyOpNormedAlgebra [NormedField R] [SeminormedRing Î±] [NormedAlgebra R Î±]
    [DecidableEq n] : NormedAlgebra R (Matrix n n Î±) :=
  { Matrix.linftyOpNormedSpace, Matrix.instAlgebra with }
#align matrix.linfty_op_normed_algebra Matrix.linftyOpNormedAlgebra

end LinftyOp

/-! ### The Frobenius norm

This is defined as $\|A\| = \sqrt{\sum_{i,j} \|A_{ij}\|^2}$.
When the matrix is over the real or complex numbers, this norm is submultiplicative.
-/


section frobenius

open scoped Matrix BigOperators

/-- Seminormed group instance (using frobenius norm) for matrices over a seminormed group. Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusSeminormedAddCommGroup [SeminormedAddCommGroup Î±] :
    SeminormedAddCommGroup (Matrix m n Î±) :=
  inferInstanceAs (SeminormedAddCommGroup (PiLp 2 fun _i : m => PiLp 2 fun _j : n => Î±))
#align matrix.frobenius_seminormed_add_comm_group Matrix.frobeniusSeminormedAddCommGroup

/-- Normed group instance (using frobenius norm) for matrices over a normed group.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAddCommGroup [NormedAddCommGroup Î±] : NormedAddCommGroup (Matrix m n Î±) :=
  (by infer_instance : NormedAddCommGroup (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))
      -- ğŸ‰ no goals
#align matrix.frobenius_normed_add_comm_group Matrix.frobeniusNormedAddCommGroup

/-- Normed space instance (using frobenius norm) for matrices over a normed space.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedSpace [NormedField R] [SeminormedAddCommGroup Î±] [NormedSpace R Î±] :
    NormedSpace R (Matrix m n Î±) :=
  (by infer_instance : NormedSpace R (PiLp 2 fun i : m => PiLp 2 fun j : n => Î±))
      -- ğŸ‰ no goals
#align matrix.frobenius_normed_space Matrix.frobeniusNormedSpace

section SeminormedAddCommGroup

variable [SeminormedAddCommGroup Î±] [SeminormedAddCommGroup Î²]

theorem frobenius_nnnorm_def (A : Matrix m n Î±) :
    â€–Aâ€–â‚Š = (âˆ‘ i, âˆ‘ j, â€–A i jâ€–â‚Š ^ (2 : â„)) ^ (1 / 2 : â„) := by
  -- porting note: added, along with `PiLp.equiv_symm_apply` below
  change â€–(PiLp.equiv 2 _).symm <| fun i => (PiLp.equiv 2 _).symm <| fun j => A i jâ€–â‚Š = _
  -- âŠ¢ â€–â†‘(PiLp.equiv 2 fun i => (fun x => PiLp 2 fun j => Î±) fun j => A i j).symm f â€¦
  simp_rw [PiLp.nnnorm_eq_of_L2, NNReal.sq_sqrt, NNReal.sqrt_eq_rpow, NNReal.rpow_two,
    PiLp.equiv_symm_apply]
#align matrix.frobenius_nnnorm_def Matrix.frobenius_nnnorm_def

theorem frobenius_norm_def (A : Matrix m n Î±) :
    â€–Aâ€– = (âˆ‘ i, âˆ‘ j, â€–A i jâ€– ^ (2 : â„)) ^ (1 / 2 : â„) :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) (frobenius_nnnorm_def A)).trans <| by simp [NNReal.coe_sum]
                                                                   -- ğŸ‰ no goals
#align matrix.frobenius_norm_def Matrix.frobenius_norm_def

@[simp]
theorem frobenius_nnnorm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€–â‚Š = â€–aâ€–â‚Š) :
    â€–A.map fâ€–â‚Š = â€–Aâ€–â‚Š := by simp_rw [frobenius_nnnorm_def, Matrix.map_apply, hf]
                            -- ğŸ‰ no goals
#align matrix.frobenius_nnnorm_map_eq Matrix.frobenius_nnnorm_map_eq

@[simp]
theorem frobenius_norm_map_eq (A : Matrix m n Î±) (f : Î± â†’ Î²) (hf : âˆ€ a, â€–f aâ€– = â€–aâ€–) :
    â€–A.map fâ€– = â€–Aâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_map_eq A f fun a => Subtype.ext <| hf a : _)
#align matrix.frobenius_norm_map_eq Matrix.frobenius_norm_map_eq

@[simp]
theorem frobenius_nnnorm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€–â‚Š = â€–Aâ€–â‚Š := by
  rw [frobenius_nnnorm_def, frobenius_nnnorm_def, Finset.sum_comm]
  -- âŠ¢ (âˆ‘ y : m, âˆ‘ x : n, â€–Aáµ€ x yâ€–â‚Š ^ 2) ^ (1 / 2) = (âˆ‘ i : m, âˆ‘ j : n, â€–A i jâ€–â‚Š ^  â€¦
  simp_rw [Matrix.transpose_apply]  -- porting note: added
  -- ğŸ‰ no goals
#align matrix.frobenius_nnnorm_transpose Matrix.frobenius_nnnorm_transpose

@[simp]
theorem frobenius_norm_transpose (A : Matrix m n Î±) : â€–Aáµ€â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_transpose A
#align matrix.frobenius_norm_transpose Matrix.frobenius_norm_transpose

@[simp]
theorem frobenius_nnnorm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€–â‚Š = â€–Aâ€–â‚Š :=
  (frobenius_nnnorm_map_eq _ _ nnnorm_star).trans A.frobenius_nnnorm_transpose
#align matrix.frobenius_nnnorm_conj_transpose Matrix.frobenius_nnnorm_conjTranspose

@[simp]
theorem frobenius_norm_conjTranspose [StarAddMonoid Î±] [NormedStarGroup Î±] (A : Matrix m n Î±) :
    â€–Aá´´â€– = â€–Aâ€– :=
  congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_conjTranspose A
#align matrix.frobenius_norm_conj_transpose Matrix.frobenius_norm_conjTranspose

instance frobenius_normedStarGroup [StarAddMonoid Î±] [NormedStarGroup Î±] :
    NormedStarGroup (Matrix m m Î±) :=
  âŸ¨frobenius_norm_conjTransposeâŸ©
#align matrix.frobenius_normed_star_group Matrix.frobenius_normedStarGroup

@[simp]
theorem frobenius_norm_row (v : m â†’ Î±) : â€–row vâ€– = â€–(PiLp.equiv 2 _).symm vâ€– := by
  rw [frobenius_norm_def, Fintype.sum_unique, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]
  -- âŠ¢ (âˆ‘ j : m, â€–row v default jâ€– ^ 2) ^ (1 / 2) = (âˆ‘ i : m, â€–â†‘(PiLp.equiv 2 fun i â€¦
  simp only [row_apply, Real.rpow_two, PiLp.equiv_symm_apply]
  -- ğŸ‰ no goals
#align matrix.frobenius_norm_row Matrix.frobenius_norm_row

@[simp]
theorem frobenius_nnnorm_row (v : m â†’ Î±) : â€–row vâ€–â‚Š = â€–(PiLp.equiv 2 _).symm vâ€–â‚Š :=
  Subtype.ext <| frobenius_norm_row v
#align matrix.frobenius_nnnorm_row Matrix.frobenius_nnnorm_row

@[simp]
theorem frobenius_norm_col (v : n â†’ Î±) : â€–col vâ€– = â€–(PiLp.equiv 2 _).symm vâ€– := by
  simp_rw [frobenius_norm_def, Fintype.sum_unique, PiLp.norm_eq_of_L2, Real.sqrt_eq_rpow]
  -- âŠ¢ (âˆ‘ x : n, â€–col v x defaultâ€– ^ 2) ^ (1 / 2) = (âˆ‘ i : n, â€–â†‘(PiLp.equiv 2 fun i â€¦
  simp only [col_apply, Real.rpow_two, PiLp.equiv_symm_apply]
  -- ğŸ‰ no goals
#align matrix.frobenius_norm_col Matrix.frobenius_norm_col

@[simp]
theorem frobenius_nnnorm_col (v : n â†’ Î±) : â€–col vâ€–â‚Š = â€–(PiLp.equiv 2 _).symm vâ€–â‚Š :=
  Subtype.ext <| frobenius_norm_col v
#align matrix.frobenius_nnnorm_col Matrix.frobenius_nnnorm_col

@[simp]
theorem frobenius_nnnorm_diagonal [DecidableEq n] (v : n â†’ Î±) :
    â€–diagonal vâ€–â‚Š = â€–(PiLp.equiv 2 _).symm vâ€–â‚Š := by
  simp_rw [frobenius_nnnorm_def, â† Finset.sum_product', Finset.univ_product_univ,
    PiLp.nnnorm_eq_of_L2]
  let s := (Finset.univ : Finset n).map âŸ¨fun i : n => (i, i), fun i j h => congr_arg Prod.fst hâŸ©
  -- âŠ¢ (âˆ‘ x : n Ã— n, â€–diagonal v x.fst x.sndâ€–â‚Š ^ 2) ^ (1 / 2) = â†‘NNReal.sqrt (âˆ‘ i : â€¦
  rw [â† Finset.sum_subset (Finset.subset_univ s) fun i _hi his => ?_]
  -- âŠ¢ (âˆ‘ x in s, â€–diagonal v x.fst x.sndâ€–â‚Š ^ 2) ^ (1 / 2) = â†‘NNReal.sqrt (âˆ‘ i : n, â€¦
  Â· rw [Finset.sum_map, NNReal.sqrt_eq_rpow]
    -- âŠ¢ (âˆ‘ x : n, â€–diagonal v (â†‘{ toFun := fun i => (i, i), inj' := (_ : âˆ€ (i j : n) â€¦
    dsimp
    -- âŠ¢ (âˆ‘ x : n, â€–diagonal v x xâ€–â‚Š ^ 2) ^ (1 / 2) = (âˆ‘ i : n, â€–v iâ€–â‚Š ^ 2) ^ (1 / 2)
    simp_rw [diagonal_apply_eq, NNReal.rpow_two]
    -- ğŸ‰ no goals
  Â· suffices i.1 â‰  i.2 by rw [diagonal_apply_ne _ this, nnnorm_zero, NNReal.zero_rpow two_ne_zero]
    -- âŠ¢ i.fst â‰  i.snd
    intro h
    -- âŠ¢ False
    exact Finset.mem_map.not.mp his âŸ¨i.1, Finset.mem_univ _, Prod.ext rfl hâŸ©
    -- ğŸ‰ no goals
#align matrix.frobenius_nnnorm_diagonal Matrix.frobenius_nnnorm_diagonal

@[simp]
theorem frobenius_norm_diagonal [DecidableEq n] (v : n â†’ Î±) :
    â€–diagonal vâ€– = â€–(PiLp.equiv 2 _).symm vâ€– :=
  (congr_arg ((â†‘) : â„â‰¥0 â†’ â„) <| frobenius_nnnorm_diagonal v : _).trans rfl
#align matrix.frobenius_norm_diagonal Matrix.frobenius_norm_diagonal

end SeminormedAddCommGroup

theorem frobenius_nnnorm_one [DecidableEq n] [SeminormedAddCommGroup Î±] [One Î±] :
    â€–(1 : Matrix n n Î±)â€–â‚Š = NNReal.sqrt (Fintype.card n) * â€–(1 : Î±)â€–â‚Š := by
  refine' (frobenius_nnnorm_diagonal _).trans _
  -- âŠ¢ â€–â†‘(PiLp.equiv 2 fun i => Î±).symm fun x => 1â€–â‚Š = â†‘NNReal.sqrt â†‘(Fintype.card  â€¦
  -- porting note: change to erw, since `fun x => 1` no longer matches `Function.const`
  erw [PiLp.nnnorm_equiv_symm_const ENNReal.two_ne_top]
  -- âŠ¢ â†‘(Fintype.card n) ^ ENNReal.toReal (1 / 2) * â€–1â€–â‚Š = â†‘NNReal.sqrt â†‘(Fintype.c â€¦
  simp_rw [NNReal.sqrt_eq_rpow]
  -- âŠ¢ â†‘(Fintype.card n) ^ ENNReal.toReal (1 / 2) * â€–1â€–â‚Š = â†‘(Fintype.card n) ^ (1 / â€¦
  -- porting note: added `ENNReal.toReal_ofNat`
  simp only [ENNReal.toReal_div, ENNReal.one_toReal, ENNReal.toReal_ofNat]
  -- ğŸ‰ no goals
#align matrix.frobenius_nnnorm_one Matrix.frobenius_nnnorm_one

section IsROrC

variable [IsROrC Î±]

theorem frobenius_nnnorm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€–â‚Š â‰¤ â€–Aâ€–â‚Š * â€–Bâ€–â‚Š := by
  simp_rw [frobenius_nnnorm_def, Matrix.mul_apply]
  -- âŠ¢ (âˆ‘ x : l, âˆ‘ x_1 : n, â€–âˆ‘ j : m, A x j * B j x_1â€–â‚Š ^ 2) ^ (1 / 2) â‰¤ (âˆ‘ i : l,  â€¦
  rw [â† NNReal.mul_rpow, @Finset.sum_comm _ n m, Finset.sum_mul_sum, Finset.sum_product]
  -- âŠ¢ (âˆ‘ x : l, âˆ‘ x_1 : n, â€–âˆ‘ j : m, A x j * B j x_1â€–â‚Š ^ 2) ^ (1 / 2) â‰¤ (âˆ‘ x : l,  â€¦
  refine' NNReal.rpow_le_rpow _ one_half_pos.le
  -- âŠ¢ âˆ‘ x : l, âˆ‘ x_1 : n, â€–âˆ‘ j : m, A x j * B j x_1â€–â‚Š ^ 2 â‰¤ âˆ‘ x : l, âˆ‘ y : n, (âˆ‘ j â€¦
  refine' Finset.sum_le_sum fun i _ => Finset.sum_le_sum fun j _ => _
  -- âŠ¢ â€–âˆ‘ j_1 : m, A i j_1 * B j_1 jâ€–â‚Š ^ 2 â‰¤ (âˆ‘ j_1 : m, â€–A (i, j).fst j_1â€–â‚Š ^ 2) * â€¦
  rw [â† NNReal.rpow_le_rpow_iff one_half_pos, â† NNReal.rpow_mul,
    mul_div_cancel' (1 : â„) two_ne_zero, NNReal.rpow_one, NNReal.mul_rpow]
  dsimp only
  -- âŠ¢ â€–âˆ‘ j_1 : m, A i j_1 * B j_1 jâ€–â‚Š â‰¤ (âˆ‘ j : m, â€–A i jâ€–â‚Š ^ 2) ^ (1 / 2) * (âˆ‘ x : â€¦
  have :=
    @nnnorm_inner_le_nnnorm Î± _ _ _ _ ((PiLp.equiv 2 fun _ => Î±).symm fun j => star (A i j))
      ((PiLp.equiv 2 fun _ => Î±).symm fun k => B k j)
  simpa only [PiLp.equiv_symm_apply, PiLp.inner_apply, IsROrC.inner_apply, starRingEnd_apply,
    Pi.nnnorm_def, PiLp.nnnorm_eq_of_L2, star_star, nnnorm_star, NNReal.sqrt_eq_rpow,
    NNReal.rpow_two] using this
#align matrix.frobenius_nnnorm_mul Matrix.frobenius_nnnorm_mul

theorem frobenius_norm_mul (A : Matrix l m Î±) (B : Matrix m n Î±) : â€–A * Bâ€– â‰¤ â€–Aâ€– * â€–Bâ€– :=
  frobenius_nnnorm_mul A B
#align matrix.frobenius_norm_mul Matrix.frobenius_norm_mul

/-- Normed ring instance (using frobenius norm) for matrices over `â„` or `â„‚`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedRing [DecidableEq m] : NormedRing (Matrix m m Î±) :=
  { Matrix.frobeniusSeminormedAddCommGroup, Matrix.instRing with
    norm := Norm.norm
    norm_mul := frobenius_norm_mul
    eq_of_dist_eq_zero := eq_of_dist_eq_zero }
#align matrix.frobenius_normed_ring Matrix.frobeniusNormedRing

/-- Normed algebra instance (using frobenius norm) for matrices over `â„` or `â„‚`.  Not
declared as an instance because there are several natural choices for defining the norm of a
matrix. -/
@[local instance]
def frobeniusNormedAlgebra [DecidableEq m] [NormedField R] [NormedAlgebra R Î±] :
    NormedAlgebra R (Matrix m m Î±) :=
  { Matrix.frobeniusNormedSpace, Matrix.instAlgebra with }
#align matrix.frobenius_normed_algebra Matrix.frobeniusNormedAlgebra

end IsROrC

end frobenius

end Matrix
