/-
Copyright (c) 2026 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.SchwartzSpace.Basic
public import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
public import Mathlib.Analysis.Calculus.LineDeriv.IntegrationByParts
public import Mathlib.Analysis.InnerProductSpace.Laplacian
public import Mathlib.MeasureTheory.Integral.IntegralEqImproper

/-!
# Derivatives of Schwartz functions

In this file we define the various notions of derivatives of Schwartz functions.

## Main definitions

* `SchwartzMap.fderivCLM`: The differential as a continuous linear map
  `ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F)`
* `SchwartzMap.derivCLM`: The one-dimensional derivative as a continuous linear map
  `ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F)`
* `SchwartzMap.instLineDeriv`: The directional derivative with notation `âˆ‚_{m} f`
* `SchwartzMap.instLaplacian`: The Laplacian for `ğ“¢(E, F)` as an instance of the notation type-class
  `Laplacian`.

## Main statements

* `SchwartzMap.iteratedLineDerivOp_eq_iteratedFDeriv`: the iterated directional derivative is given
  by the applied FrÃ©chet derivative of a Schwartz function.
* `SchwartzMap.laplacian_eq_sum`: the Laplacian is given by the sum of second derivatives in any
  orthonormal basis.
* `SchwartzMap.integral_bilinear_lineDerivOp_right_eq_neg_left`: Integration by parts using the
  directional derivative `âˆ‚_{m}`
* `SchwartzMap.integral_bilinear_laplacian_right_eq_left`: Integration by parts for the Laplacian

-/

@[expose] public noncomputable section

variable {Î¹ ğ•œ ğ•œ' D E F V F Fâ‚ Fâ‚‚ Fâ‚ƒ : Type*}

namespace SchwartzMap

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ F]

section Derivatives

/-! ### Derivatives of Schwartz functions -/

variable [NormedSpace â„ E]

variable (ğ•œ)
variable [RCLike ğ•œ] [NormedSpace ğ•œ F]

variable (F) in
/-- The 1-dimensional derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def derivCLM : ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F) :=
  mkCLM (deriv Â·) (fun f g _ => deriv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => deriv_const_smul a f.differentiableAt)
    (fun f => (contDiff_succ_iff_deriv.mp (f.smooth âŠ¤)).2.2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [Real.norm_eq_abs, Finset.sup_singleton, schwartzSeminormFamily_apply, one_mul,
        norm_iteratedFDeriv_eq_norm_iteratedDeriv, â† iteratedDeriv_succ'] using
        f.le_seminorm' ğ•œ k (n + 1) xâŸ©

@[simp]
theorem derivCLM_apply (f : ğ“¢(â„, F)) (x : â„) : derivCLM ğ•œ F f x = deriv f x :=
  rfl

theorem hasDerivAt (f : ğ“¢(â„, F)) (x : â„) : HasDerivAt f (deriv f x) x :=
  f.differentiableAt.hasDerivAt

variable [SMulCommClass â„ ğ•œ F]

open LineDeriv

variable (E F) in
/-- The FrÃ©chet derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def fderivCLM : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F) :=
  mkCLM (fderiv â„ Â·) (fun f g _ => fderiv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => fderiv_const_smul f.differentiableAt a)
    (fun f => (contDiff_succ_iff_fderiv.mp (f.smooth âŠ¤)).2.2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [schwartzSeminormFamily_apply, Seminorm.comp_apply, Finset.sup_singleton,
        one_smul, norm_iteratedFDeriv_fderiv, one_mul] using f.le_seminorm ğ•œ k (n + 1) xâŸ©

@[simp]
theorem fderivCLM_apply (f : ğ“¢(E, F)) (x : E) : fderivCLM ğ•œ E F f x = fderiv â„ f x :=
  rfl

theorem hasFDerivAt (f : ğ“¢(E, F)) (x : E) : HasFDerivAt f (fderiv â„ f x) x :=
  f.differentiableAt.hasFDerivAt

/-- The partial derivative (or directional derivative) in the direction `m : E` as a
continuous linear map on Schwartz space. -/
instance : LineDeriv E ğ“¢(E, F) ğ“¢(E, F) where
  lineDerivOp m f := (SchwartzMap.evalCLM â„ E F m âˆ˜L fderivCLM â„ E F) f

theorem lineDerivOp_apply_eq_fderiv (m : E) (f : ğ“¢(E, F)) (x : E) :
    âˆ‚_{m} f x = fderiv â„ f x m := rfl

instance : LineDerivAdd E ğ“¢(E, F) ğ“¢(E, F) where
  lineDerivOp_add m := ((SchwartzMap.evalCLM â„ E F m).comp (fderivCLM â„ E F)).map_add
  lineDerivOp_left_add v w f := by
    ext x
    simp [lineDerivOp_apply_eq_fderiv]

instance : LineDerivSMul ğ•œ E ğ“¢(E, F) ğ“¢(E, F) where
  lineDerivOp_smul m := (SchwartzMap.evalCLM ğ•œ E F m âˆ˜L fderivCLM ğ•œ E F).map_smul

instance : LineDerivLeftSMul â„ E ğ“¢(E, F) ğ“¢(E, F) where
  lineDerivOp_left_smul r y f := by
    ext x
    simp [lineDerivOp_apply_eq_fderiv]

instance : ContinuousLineDeriv E ğ“¢(E, F) ğ“¢(E, F) where
  continuous_lineDerivOp m := (SchwartzMap.evalCLM â„ E F m âˆ˜L fderivCLM â„ E F).continuous

open LineDeriv

theorem lineDerivOpCLM_eq (m : E) :
    lineDerivOpCLM ğ•œ ğ“¢(E, F) m = SchwartzMap.evalCLM ğ•œ E F m âˆ˜L fderivCLM ğ•œ E F := rfl

@[deprecated (since := "2025-11-25")]
alias pderivCLM := lineDerivOpCLM

@[deprecated (since := "2025-11-25")]
alias pderivCLM_apply := LineDeriv.lineDerivOpCLM_apply

theorem lineDerivOp_apply (m : E) (f : ğ“¢(E, F)) (x : E) : âˆ‚_{m} f x = lineDeriv â„ f x m :=
  f.differentiableAt.lineDeriv_eq_fderiv.symm

variable [NormedAddCommGroup D] [NormedSpace â„ D]

theorem lineDerivOp_compCLMOfContinuousLinearEquiv (m : D) (g : D â‰ƒL[â„] E) (f : ğ“¢(E, F)) :
    âˆ‚_{m} (compCLMOfContinuousLinearEquiv ğ•œ g f) =
    compCLMOfContinuousLinearEquiv ğ•œ g (âˆ‚_{g m} f) := by
  ext x
  simp [lineDerivOp_apply_eq_fderiv, ContinuousLinearEquiv.comp_right_fderiv]

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv := LineDeriv.iteratedLineDerivOpCLM

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv_zero := LineDeriv.iteratedLineDerivOp_zero

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv_one := LineDeriv.iteratedLineDerivOp_one

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv_succ_left := LineDeriv.iteratedLineDerivOp_succ_left

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv_succ_right := LineDeriv.iteratedLineDerivOp_succ_right

theorem iteratedLineDerivOp_eq_iteratedFDeriv {n : â„•} {m : Fin n â†’ E} {f : ğ“¢(E, F)} {x : E} :
    âˆ‚^{m} f x = iteratedFDeriv â„ n f x m := by
  induction n generalizing x with
  | zero => simp
  | succ n ih =>
    rw [iteratedLineDerivOp_succ_left, iteratedFDeriv_succ_apply_left,
      â† fderiv_continuousMultilinear_apply_const_apply]
    Â· simp only [lineDerivOp_apply_eq_fderiv, â† ih]
    Â· exact (f.smooth âŠ¤).differentiable_iteratedFDeriv (mod_cast ENat.coe_lt_top n) x

@[deprecated (since := "2025-11-25")]
alias iteratedPDeriv_eq_iteratedFDeriv := iteratedLineDerivOp_eq_iteratedFDeriv

end Derivatives

section Laplacian

/-! ## Laplacian on `ğ“¢(E, F)` -/

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E]

open Laplacian LineDeriv

instance : Laplacian ğ“¢(E, F) ğ“¢(E, F) where
  laplacian := laplacianCLM â„ E ğ“¢(E, F)

theorem laplacianCLM_eq' (f : ğ“¢(E, F)) : laplacianCLM â„ E ğ“¢(E, F) f = Î” f := rfl

theorem laplacian_eq_sum [Fintype Î¹] (b : OrthonormalBasis Î¹ â„ E) (f : ğ“¢(E, F)) :
    Î” f = âˆ‘ i, âˆ‚_{b i} (âˆ‚_{b i} f) :=
  LineDeriv.laplacianCLM_eq_sum b f

variable (ğ•œ) in
@[simp]
theorem laplacianCLM_eq [RCLike ğ•œ] [NormedSpace ğ•œ F] (f : ğ“¢(E, F)) :
    laplacianCLM ğ•œ E ğ“¢(E, F) f = Î” f := by
  simp [laplacianCLM, laplacian_eq_sum (stdOrthonormalBasis â„ E)]

theorem laplacian_apply (f : ğ“¢(E, F)) (x : E) : Î” f x = Î” (f : E â†’ F) x := by
  rw [laplacian_eq_sum (stdOrthonormalBasis â„ E)]
  simp only [InnerProductSpace.laplacian_eq_iteratedFDeriv_orthonormalBasis f
    (stdOrthonormalBasis â„ E), sum_apply]
  congr 1
  ext i
  rw [â† iteratedLineDerivOp_eq_iteratedFDeriv]
  rfl

end Laplacian

section integration_by_parts

variable [NormedSpace â„ E]

open ENNReal MeasureTheory

section one_dim

variable [NormedAddCommGroup V] [NormedSpace â„ V]

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for a general bilinear map. -/
theorem integral_bilinear_deriv_right_eq_neg_left (f : ğ“¢(â„, E)) (g : ğ“¢(â„, F))
    (L : E â†’L[â„] F â†’L[â„] V) :
    âˆ« (x : â„), L (f x) (deriv g x) = -âˆ« (x : â„), L (deriv f x) (g x) :=
  MeasureTheory.integral_bilinear_hasDerivAt_right_eq_neg_left_of_integrable
    f.hasDerivAt g.hasDerivAt (pairing L f (derivCLM â„ F g)).integrable
    (pairing L (derivCLM â„ E f) g).integrable (pairing L f g).integrable

variable [NormedRing ğ•œ] [NormedSpace â„ ğ•œ] [IsScalarTower â„ ğ•œ ğ•œ] [SMulCommClass â„ ğ•œ ğ•œ] in
/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for multiplication of scalar-valued Schwartz functions. -/
theorem integral_mul_deriv_eq_neg_deriv_mul (f : ğ“¢(â„, ğ•œ)) (g : ğ“¢(â„, ğ•œ)) :
    âˆ« (x : â„), f x * (deriv g x) = -âˆ« (x : â„), deriv f x * (g x) :=
  integral_bilinear_deriv_right_eq_neg_left f g (ContinuousLinearMap.mul â„ ğ•œ)

variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [NormedSpace ğ•œ V]

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_smul_deriv_right_eq_neg_left (f : ğ“¢(â„, ğ•œ)) (g : ğ“¢(â„, F)) :
    âˆ« (x : â„), f x â€¢ deriv g x = -âˆ« (x : â„), deriv f x â€¢ g x :=
  integral_bilinear_deriv_right_eq_neg_left f g (ContinuousLinearMap.lsmul â„ ğ•œ)

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_clm_comp_deriv_right_eq_neg_left (f : ğ“¢(â„, F â†’L[ğ•œ] V)) (g : ğ“¢(â„, F)) :
    âˆ« (x : â„), f x (deriv g x) = -âˆ« (x : â„), deriv f x (g x) :=
  integral_bilinear_deriv_right_eq_neg_left f g
    ((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).bilinearRestrictScalars â„)

end one_dim

variable [NormedAddCommGroup V] [NormedSpace â„ V]
  [NormedAddCommGroup D] [NormedSpace â„ D]
  [MeasurableSpace D] {Î¼ : Measure D} [BorelSpace D] [FiniteDimensional â„ D] [Î¼.IsAddHaarMeasure]

open scoped LineDeriv

/-- Integration by parts of Schwartz functions for directional derivatives.

Version for a general bilinear map. -/
theorem integral_bilinear_lineDerivOp_right_eq_neg_left (f : ğ“¢(D, E)) (g : ğ“¢(D, F))
    (L : E â†’L[â„] F â†’L[â„] V) (v : D) :
    âˆ« (x : D), L (f x) (âˆ‚_{v} g x) âˆ‚Î¼ = -âˆ« (x : D), L (âˆ‚_{v} f x) (g x) âˆ‚Î¼ := by
  apply integral_bilinear_hasLineDerivAt_right_eq_neg_left_of_integrable (v := v)
    (bilinLeftCLM L g.hasTemperateGrowth _).integrable
    (bilinLeftCLM L (âˆ‚_{v} g).hasTemperateGrowth _).integrable
    (bilinLeftCLM L g.hasTemperateGrowth _).integrable
  all_goals
  intro x
  exact (hasFDerivAt _ x).hasLineDerivAt v

variable [NormedRing ğ•œ] [NormedSpace â„ ğ•œ] [IsScalarTower â„ ğ•œ ğ•œ] [SMulCommClass â„ ğ•œ ğ•œ] in
/-- Integration by parts of Schwartz functions for directional derivatives.

Version for multiplication of scalar-valued Schwartz functions. -/
theorem integral_mul_lineDerivOp_right_eq_neg_left (f : ğ“¢(D, ğ•œ)) (g : ğ“¢(D, ğ•œ)) (v : D) :
    âˆ« (x : D), f x * âˆ‚_{v} g x âˆ‚Î¼ = -âˆ« (x : D), âˆ‚_{v} f x * g x âˆ‚Î¼ :=
  integral_bilinear_lineDerivOp_right_eq_neg_left f g (ContinuousLinearMap.mul â„ ğ•œ) v

variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [NormedSpace ğ•œ V]

/-- Integration by parts of Schwartz functions for directional derivatives.

Version for scalar multiplication. -/
theorem integral_smul_lineDerivOp_right_eq_neg_left (f : ğ“¢(D, ğ•œ)) (g : ğ“¢(D, F)) (v : D) :
    âˆ« (x : D), f x â€¢ âˆ‚_{v} g x âˆ‚Î¼ = -âˆ« (x : D), âˆ‚_{v} f x â€¢ g x âˆ‚Î¼ :=
  integral_bilinear_lineDerivOp_right_eq_neg_left f g (ContinuousLinearMap.lsmul â„ ğ•œ) v

/-- Integration by parts of Schwartz functions for directional derivatives.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_clm_comp_lineDerivOp_right_eq_neg_left (f : ğ“¢(D, F â†’L[ğ•œ] V)) (g : ğ“¢(D, F))
    (v : D) : âˆ« (x : D), f x (âˆ‚_{v} g x) âˆ‚Î¼ = -âˆ« (x : D), âˆ‚_{v} f x (g x) âˆ‚Î¼ :=
  integral_bilinear_lineDerivOp_right_eq_neg_left f g
    ((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).bilinearRestrictScalars â„) v

end integration_by_parts

section laplacian_integration_by_parts

open MeasureTheory Laplacian LineDeriv

/-! ### Integration by parts -/

variable [InnerProductSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup Fâ‚] [NormedSpace â„ Fâ‚]
  [NormedAddCommGroup Fâ‚‚] [NormedSpace â„ Fâ‚‚]
  [NormedAddCommGroup Fâ‚ƒ] [NormedSpace â„ Fâ‚ƒ]
  [MeasurableSpace E] {Î¼ : Measure E} [BorelSpace E] [Î¼.IsAddHaarMeasure]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for a general bilinear map. -/
theorem integral_bilinear_laplacian_right_eq_left (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚))
    (L : Fâ‚ â†’L[â„] Fâ‚‚ â†’L[â„] Fâ‚ƒ) :
    âˆ« x, L (f x) (Î” g x) âˆ‚Î¼ = âˆ« x, L (Î” f x) (g x) âˆ‚Î¼ := by
  simp_rw [laplacian_eq_sum (stdOrthonormalBasis â„ E), sum_apply, map_sum,
    ContinuousLinearMap.coe_sum', Finset.sum_apply]
  rw [MeasureTheory.integral_finset_sum, MeasureTheory.integral_finset_sum]
  Â· simp [integral_bilinear_lineDerivOp_right_eq_neg_left]
  Â· exact fun _ _ â†¦ (pairing L (âˆ‚_{_} <| âˆ‚_{_} f) g).integrable
  Â· exact fun _ _ â†¦ (pairing L f (âˆ‚_{_} <| âˆ‚_{_} g)).integrable

variable [NormedRing ğ•œ] [NormedSpace â„ ğ•œ] [IsScalarTower â„ ğ•œ ğ•œ] [SMulCommClass â„ ğ•œ ğ•œ] in
/-- Integration by parts of Schwartz functions for the Laplacian.

Version for multiplication of scalar-valued Schwartz functions. -/
theorem integral_mul_laplacian_right_eq_left (f : ğ“¢(E, ğ•œ)) (g : ğ“¢(E, ğ•œ)) :
    âˆ« x, f x * Î” g x âˆ‚Î¼ = âˆ« x, Î” f x * g x âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g (ContinuousLinearMap.mul â„ ğ•œ)

variable [RCLike ğ•œ] [NormedSpace ğ•œ F]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for scalar multiplication. -/
theorem integral_smul_laplacian_right_eq_left (f : ğ“¢(E, ğ•œ)) (g : ğ“¢(E, F)) :
    âˆ« x, f x â€¢ Î” g x âˆ‚Î¼ = âˆ« x, Î” f x â€¢ g x âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g (ContinuousLinearMap.lsmul â„ ğ•œ)

variable [NormedSpace ğ•œ Fâ‚] [NormedSpace ğ•œ Fâ‚‚]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_clm_comp_laplacian_right_eq_left (f : ğ“¢(E, Fâ‚ â†’L[ğ•œ] Fâ‚‚)) (g : ğ“¢(E, Fâ‚)) :
    âˆ« x, f x (Î” g x) âˆ‚Î¼ = âˆ« x, Î” f x (g x) âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g
    ((ContinuousLinearMap.id ğ•œ (Fâ‚ â†’L[ğ•œ] Fâ‚‚)).bilinearRestrictScalars â„)

end laplacian_integration_by_parts

end SchwartzMap
