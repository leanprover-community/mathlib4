/-
Copyright (c) 2026 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.TemperedDistribution
import Mathlib.Analysis.Calculus.BumpFunction.FiniteDimension
import Mathlib.Geometry.Manifold.PartitionOfUnity

/-! # Support of distributions


-/

@[expose] public noncomputable section

open SchwartzMap ContinuousLinearMap MeasureTheory MeasureTheory.Measure

open scoped Nat NNReal ContDiff

variable {Î¹ ğ•œ E F Fâ‚ Fâ‚‚ : Type*}

namespace TemperedDistribution

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

section IsVanishingOn

def IsVanishingOn (f : ğ“¢'(E, F)) (s : Set E) : Prop :=
    âˆ€ (u : ğ“¢(E, â„‚)), tsupport u âŠ† s â†’ f u = 0

variable {f : ğ“¢'(E, F)} {g : ğ“¢'(E, F)} {s sâ‚ sâ‚‚ : Set E}

variable (E F s) in
@[simp, grind .]
theorem isVanishingOn_zero : (0 : ğ“¢'(E, F)).IsVanishingOn s := by
  simp [IsVanishingOn]

@[simp]
theorem isVanishingOn_univ_iff : f.IsVanishingOn Set.univ â†” f = 0 := by
  refine âŸ¨fun hf â†¦ ?_, fun hf â†¦ by simp [hf]âŸ©
  ext u
  simpa [IsVanishingOn] using hf u

theorem IsVanishingOn.mono (hs : sâ‚‚ âŠ† sâ‚) (hf : f.IsVanishingOn sâ‚) : f.IsVanishingOn sâ‚‚ :=
  fun u hu â†¦ hf u (hu.trans hs)

open scoped Topology

-- HÃ¶rmander 7.1.8
variable [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E] in
theorem foo (f : ğ“¢(E, F)) : âˆƒ (u : â„• â†’ ğ“¢(E, F)), Filter.Tendsto u Filter.atTop (ğ“ f) âˆ§
    âˆ€ i, tsupport (u i) âŠ† tsupport f âˆ§ HasCompactSupport (u i) := by
  set g := ExistsContDiffBumpBase.y (E := E) (1/2)
  have hgâ‚ : ContDiff â„ âˆ g := by sorry
  have hgâ‚‚ : tsupport g âŠ† Metric.ball 0 (1/2) := by sorry
  sorry

variable [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E] in
theorem isVanishingOn_iff_forall_hasCompactSupport : f.IsVanishingOn s â†”
    âˆ€ (u : ğ“¢(E, â„‚)), HasCompactSupport u â†’ tsupport u âŠ† s â†’ f u = 0 := by
  constructor
  Â· intro h u huâ‚
    exact h u
  intro h u hu
  obtain âŸ¨v, hvâ‚, hvâ‚‚âŸ© := foo u
  have hvâ‚ƒ : f âˆ˜ v = 0 := by
    ext i
    apply h (v i) (hvâ‚‚ i).2 ((hvâ‚‚ i).1.trans hu)
  have limâ‚ : Filter.Tendsto (f âˆ˜ v) Filter.atTop (ğ“ (f u)) :=
    (f.continuous.tendsto u).comp hvâ‚
  have limâ‚‚ : Filter.Tendsto (f âˆ˜ v) Filter.atTop (ğ“ 0) := by
    rw [hvâ‚ƒ]
    apply tendsto_const_nhds
  exact tendsto_nhds_unique limâ‚ limâ‚‚

variable [FiniteDimensional â„ E] [Finite Î¹] in
theorem IsVanishingOn.iUnion {s : Î¹ â†’ Set E} (hs : âˆ€ i, IsOpen (s i))
    (hs' : âˆ€ i, Bornology.IsBounded (s i)) (hf : âˆ€ i, f.IsVanishingOn (s i)) :
    f.IsVanishingOn (â‹ƒ i, s i) := by
  -- The boundedness condition is not strictly necessary, but we would need a partition of unity
  -- with temperate growth functions to remove this restriction.
  intro u hu
  have : IsClosed (tsupport u) := isClosed_tsupport u
  obtain âŸ¨g, hgâŸ© := Normed.SmoothPartitionOfUnity.exists_isSubordinate (isClosed_tsupport u) s hs hu
  have hg' : âˆ€ i, (g i).HasTemperateGrowth := by
    intro i
    --apply Complex.ofRealCLM.hasTemperateGrowth.comp
    -- It remains to show that `g i` has temperate growth, which follows from being compactly
    -- supported
    have : HasCompactSupport (g i) := (hs' i).isCompact_closure.of_isClosed_subset
      (isClosed_tsupport _) ((hg i).trans subset_closure)
    exact this.hasTemperateGrowth (g.contDiff i)
  set u' := fun i â†¦ SchwartzMap.smulLeftCLM â„‚ (g i) u
  have hu' : âˆ€ i, u' i = fun x â†¦ g i x â€¢ u x := fun i â†¦ smulLeftCLM_apply (hg' i) u
  haveI := Fintype.ofFinite Î¹
  have : u = âˆ‘ i, u' i := by
    ext x
    have : âˆ€ y âˆˆ tsupport u, âˆ‘ i, g i y = 1 := by
      intro y hy
      simpa [finsum_eq_sum_of_fintype] using g.sum_eq_one' y hy
    simp only [SchwartzMap.sum_apply, hu', â† Finset.sum_smul, u']
    by_cases h : x âˆˆ tsupport u
    Â· simp [this x h]
    Â· simp [image_eq_zero_of_notMem_tsupport h]
  rw [this, _root_.map_sum]
  apply Fintype.sum_eq_zero
  intro i
  apply hf i
  grw [â† hg i]
  exact tsupport_smulLeftCLM_subset_left (g i) u

@[grind .]
theorem IsVanishingOn.neg (hf : f.IsVanishingOn s) : (-f).IsVanishingOn s := by
  intro u hu
  simpa using hf u hu

@[grind .]
theorem IsVanishingOn.add (hf : f.IsVanishingOn sâ‚) (hg : g.IsVanishingOn sâ‚‚) :
    (f + g).IsVanishingOn (sâ‚ âˆ© sâ‚‚) := by
  intro u hu
  simp [UniformConvergenceCLM.add_apply, hf u (hu.trans Set.inter_subset_left),
    hg u (hu.trans Set.inter_subset_right)]

@[grind .]
theorem IsVanishingOn.sub (hf : f.IsVanishingOn sâ‚) (hg : g.IsVanishingOn sâ‚‚) :
    (f - g).IsVanishingOn (sâ‚ âˆ© sâ‚‚) := by
  intro u hu
  simp [UniformConvergenceCLM.sub_apply, hf u (hu.trans Set.inter_subset_left),
    hg u (hu.trans Set.inter_subset_right)]

@[grind .]
theorem IsVanishingOn.smul (hf : f.IsVanishingOn s) (r : â„‚) :
    (r â€¢ f).IsVanishingOn s := by
  intro u hu
  simp [hf u hu]

@[grind .]
theorem IsVanishingOn.smulLeftCLM (hf : f.IsVanishingOn s) {g : E â†’ â„‚} (hg : g.HasTemperateGrowth) :
    (smulLeftCLM F g f).IsVanishingOn s := by
  intro u hu
  apply hf ((SchwartzMap.smulLeftCLM â„‚ g) u)
  rw [SchwartzMap.smulLeftCLM_apply hg]
  exact (tsupport_smul_subset_right g u).trans hu

open LineDeriv

@[grind .]
theorem IsVanishingOn.lineDerivOp (hf : f.IsVanishingOn s) (m : E) :
    (âˆ‚_{m} f).IsVanishingOn s := by
  intro u hu
  simp only [lineDerivOp_apply_apply, map_neg, neg_eq_zero]
  exact hf (âˆ‚_{m} u) <| (tsupport_lineDerivOp_subset m u).trans hu

@[grind .]
theorem IsVanishingOn.iteratedLineDerivOp {n : â„•} (hf : f.IsVanishingOn s) (m : Fin n â†’ E) :
    (âˆ‚^{m} f).IsVanishingOn s := by
  induction n with
  | zero =>
    exact hf
  | succ n IH =>
    exact (IH <| Fin.tail m).lineDerivOp (m 0)

@[grind .]
theorem isVanishingOn_delta (x : E) : (delta x).IsVanishingOn {x}á¶œ := by
  intro u hu
  rw [Set.subset_compl_singleton_iff] at hu
  apply image_eq_zero_of_notMem_tsupport hu

end IsVanishingOn

section Support

/-- The support is the smallest closed subset of `E` on which a distribution does not vanish. -/
def support (f : ğ“¢'(E, F)) : Set E := â‹‚â‚€ { s | f.IsVanishingOn sá¶œ âˆ§ IsClosed s}

variable {f : ğ“¢'(E, F)} {g : ğ“¢'(E, F)} {s : Set E}

theorem mem_support_iff (x : E) :
    x âˆˆ f.support â†” âˆ€ (s : Set E), f.IsVanishingOn sá¶œ â†’ IsClosed s â†’ x âˆˆ s := by
  simp [support]

theorem mem_support_of_forall_exists_ne (x : E) (h : âˆ€ (s : Set E) (_ : x âˆˆ s) (_ : IsOpen s),
    âˆƒ u : ğ“¢(E, â„‚), tsupport u âŠ† s âˆ§ f u â‰  0) : x âˆˆ f.support := by
  rw [mem_support_iff]
  intro s hs hs'
  by_cases! h' : x âˆˆ s
  Â· exact h'
  exfalso
  obtain âŸ¨u, hâ‚, hâ‚‚âŸ© := h sá¶œ h' IsClosed.isOpen_compl
  exact hâ‚‚ (hs u hâ‚)

@[simp high]
theorem mem_support_compl_iff (x : E) :
    x âˆˆ f.supportá¶œ â†” âˆƒ (s : Set E), f.IsVanishingOn s âˆ§ IsOpen s âˆ§ x âˆˆ s := by
  simp only [support, Set.mem_compl_iff, Set.mem_sInter, Set.mem_setOf_eq, and_imp, not_forall]
  constructor
  Â· intro âŸ¨s, hsâ‚, hsâ‚‚, hâŸ©
    use sá¶œ, hsâ‚, IsClosed.isOpen_compl
    exact h
  Â· intro âŸ¨s, hsâ‚, hsâ‚‚, hâŸ©
    use sá¶œ
    simp only [Set.mem_compl_iff, not_not, isClosed_compl_iff, exists_prop, compl_compl]
    exact âŸ¨hsâ‚, hsâ‚‚, hâŸ©

/-- The complement of the support is given by all open sets on which `f` vanishes. -/
theorem support_compl_eq : f.supportá¶œ = â‹ƒâ‚€ { a | f.IsVanishingOn a âˆ§ IsOpen a } := by
  simp [support, Set.compl_sInter, Set.compl_image_set_of]

/-- The complement of the support is given by all *bounded* open sets on which `f` vanishes. -/
theorem support_compl_eq_sUnion_isBounded :
    f.supportá¶œ = â‹ƒâ‚€ { a | f.IsVanishingOn a âˆ§ IsOpen a âˆ§ Bornology.IsBounded a } := by
  rw [support_compl_eq]
  apply subset_antisymm
  Â· simp only [Set.sUnion_subset_iff, Set.mem_setOf_eq, and_imp]
    intro s hsâ‚ hsâ‚‚
    have : s = â‹ƒ (Îµ : â„) (_ : 0 < Îµ), s âˆ© Metric.ball 0 Îµ := by
      have : â‹ƒ (Îµ : â„) (_ : 0 < Îµ), Metric.ball (0 : E) Îµ = Set.univ := by
        rw [Set.iUnionâ‚‚_eq_univ_iff]
        intro x
        use â€–xâ€– + 1, by positivity
        simp
      simp [â† Set.inter_iUnionâ‚‚, this]
    rw [this]
    simp only [Set.iUnion_subset_iff]
    intro Îµ hÎµ
    apply Set.subset_sUnion_of_mem
    refine âŸ¨hsâ‚.mono Set.inter_subset_left, hsâ‚‚.inter Metric.isOpen_ball, ?_âŸ©
    exact Bornology.IsBounded.subset Metric.isBounded_ball Set.inter_subset_right
  simp only [Set.sUnion_subset_iff, Set.mem_setOf_eq, and_imp]
  intro s hsâ‚ hsâ‚‚ hsâ‚ƒ
  exact Set.subset_sUnion_of_mem âŸ¨hsâ‚, hsâ‚‚âŸ©

@[grind .]
theorem support_subset_support
    (h : âˆ€ (s : Set E) (_ : IsOpen s), g.IsVanishingOn s â†’ f.IsVanishingOn s) :
    f.support âŠ† g.support := by
  intro x hx
  rw [mem_support_iff] at hx âŠ¢
  intro s hs hs'
  apply hx s (h sá¶œ IsClosed.isOpen_compl hs) hs'

@[grind .]
theorem isClosed_support : IsClosed f.support := by
  grind [support, isClosed_sInter]

variable [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E] in
theorem isVanishingOn_support_compl : f.IsVanishingOn (f.support)á¶œ := by
  rw [support_compl_eq_sUnion_isBounded, isVanishingOn_iff_forall_hasCompactSupport,
    Set.sUnion_eq_iUnion]
  intro u hu hf
  rw [hasCompactSupport_def] at hu
  obtain âŸ¨s, hsâŸ© := hu.elim_finite_subcover _ (fun âŸ¨s, _, h, _âŸ© â†¦ h) hf
  apply IsVanishingOn.iUnion (s := fun (i : s) â†¦ i) (fun âŸ¨âŸ¨s, _, h, _âŸ©, _âŸ© â†¦ h)
    (fun âŸ¨âŸ¨s, _, _, hâŸ©, _âŸ© â†¦ h) (fun âŸ¨âŸ¨s, h, _, _âŸ©, _âŸ© â†¦ h)
  rwa [Set.iUnion_subtype]

@[simp]
theorem support_zero_eq_emptyset : (0 : ğ“¢'(E, F)).support = âˆ… := by
  simp only [support, isVanishingOn_zero, true_and, Set.sInter_eq_empty_iff, Set.mem_setOf_eq]
  intro x
  use âˆ…
  simp

@[simp]
theorem support_neg_eq : (-f).support = f.support := by
  apply subset_antisymm
  all_goals grind [neg_neg]

theorem support_add_subset : (f + g).support âŠ† f.support âˆª g.support := by
  rw [â† Set.compl_subset_compl, Set.compl_union]
  intro x hx
  rw [mem_support_compl_iff]
  simp only [Set.mem_inter_iff, mem_support_compl_iff] at hx
  obtain âŸ¨âŸ¨sâ‚, hsâ‚, hsâ‚', hsâ‚''âŸ©, sâ‚‚, hsâ‚‚, hsâ‚‚', hsâ‚‚''âŸ© := hx
  use sâ‚ âˆ© sâ‚‚
  exact âŸ¨hsâ‚.add hsâ‚‚, hsâ‚'.inter hsâ‚‚', Set.mem_inter hsâ‚'' hsâ‚‚''âŸ©

theorem support_sub_subset : (f - g).support âŠ† f.support âˆª g.support := by
  grw [sub_eq_add_neg, support_add_subset, support_neg_eq]

theorem support_smul_subset (r : â„‚) : (r â€¢ f).support âŠ† f.support := by grind

theorem support_smulLeftCLM_subset {g : E â†’ â„‚} (hg : g.HasTemperateGrowth) :
    (smulLeftCLM F g f).support âŠ† f.support := by grind

open LineDeriv

theorem support_lineDerivOp_subset (m : E) : (âˆ‚_{m} f).support âŠ† f.support := by grind

theorem support_iteratedLineDerivOp_subset {n : â„•} (m : Fin n â†’ E) :
    (âˆ‚^{m} f).support âŠ† f.support := by grind

open scoped Topology

theorem support_delta [FiniteDimensional â„ E] (x : E) : (delta x).support = {x} := by
  apply subset_antisymm
  Â· intro x' hx'
    rw [mem_support_iff] at hx'
    exact hx' {x} (isVanishingOn_delta x) (T1Space.t1 x)
  rintro x rfl
  apply mem_support_of_forall_exists_ne
  intro s hx hs
  obtain âŸ¨u, hâ‚, hâ‚‚, hâ‚ƒ, -, hâ‚„âŸ© :=
    exists_contDiff_tsupport_subset (n := âŠ¤) ((IsOpen.mem_nhds_iff hs).mpr hx)
  have hâ‚' : tsupport (Complex.ofRealCLM âˆ˜ u) âŠ† s := (tsupport_comp_subset rfl _).trans hâ‚
  have hâ‚‚' : HasCompactSupport (Complex.ofRealCLM âˆ˜ u) := hâ‚‚.comp_left rfl
  use hâ‚‚'.toSchwartzMap (Complex.ofRealCLM.contDiff.comp hâ‚ƒ)
  exact âŸ¨hâ‚', by simp [hâ‚„]âŸ©

end Support

end TemperedDistribution
