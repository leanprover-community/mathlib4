/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.TemperedDistribution
public import Mathlib.Analysis.Fourier.LpSpace

/-! # Sobolev spaces (Bessel potential spaces)

-/

@[expose] public noncomputable section

variable {E F : Type*}
  [NormedAddCommGroup E] [NormedAddCommGroup F]
  [InnerProductSpace ‚Ñù E] [FiniteDimensional ‚Ñù E] [MeasurableSpace E] [BorelSpace E]
  [CompleteSpace F]

open FourierTransform TemperedDistribution ENNReal MeasureTheory
open scoped SchwartzMap

section normed

variable [NormedSpace ‚ÑÇ F]

def MemSobolev (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] (f : ùì¢'(E, F)) : Prop :=
  ‚àÉ (f' : Lp F p (volume : Measure E)),
    fourierMultiplierCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (s / 2) : ‚Ñù)) f = f'

theorem memSobolev_zero_iff {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] {f : ùì¢'(E, F)} : MemSobolev 0 p f ‚Üî
    ‚àÉ (f' : Lp F p (volume : Measure E)), f = f' := by
  simp [MemSobolev]

theorem memSobolev_add {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] {f g : ùì¢'(E, F)}
    (hf : MemSobolev s p f) (hg : MemSobolev s p g) : MemSobolev s p (f + g) := by
  obtain ‚ü®f', hf‚ü© := hf
  obtain ‚ü®g', hg‚ü© := hg
  use f' + g'
  change _ = Lp.toTemperedDistributionCLM F volume p (f' + g')
  simp [map_add, hf, hg]

theorem memSobolev_smul {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] (c : ‚ÑÇ) {f : ùì¢'(E, F)}
    (hf : MemSobolev s p f) : MemSobolev s p (c ‚Ä¢ f) := by
  obtain ‚ü®f', hf‚ü© := hf
  use c ‚Ä¢ f'
  change _ = Lp.toTemperedDistributionCLM F volume p (c ‚Ä¢ f')
  simp [hf]

variable (E F) in
theorem memSobolev_zero (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] : MemSobolev s p (0 : ùì¢'(E, F)) := by
  use 0
  change _ = Lp.toTemperedDistributionCLM F volume p 0
  simp only [map_zero]

variable (E F) in
def Sobolev (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] : Submodule ‚ÑÇ ùì¢'(E, F) where
  carrier := MemSobolev s p
  add_mem' := memSobolev_add
  zero_mem' := memSobolev_zero E F s p
  smul_mem' := memSobolev_smul

namespace Sobolev

def sobFn {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] (f : Sobolev E F s p) :
    Lp F p (volume : Measure E) :=
  f.2.choose

theorem sobFn_spec {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] {f : Sobolev E F s p} :
    fourierMultiplierCLM F (fun x : E ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (s / 2) : ‚Ñù)) f = sobFn f :=
  f.2.choose_spec

@[fun_prop]
theorem Complex.hasTemperateGrowth_ofReal : Function.HasTemperateGrowth Complex.ofReal :=
  ContinuousLinearMap.hasTemperateGrowth (Complex.ofRealCLM)

@[simp]
theorem fourierMultiplier_neg_sobFn_eq {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)]
    {f : Sobolev E F s p} :
    fourierMultiplierCLM F (fun x : E ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (-s / 2) : ‚Ñù)) (sobFn f) = f := by
  rw [‚Üê sobFn_spec, fourierMultiplierCLM_fourierMultiplierCLM_apply (by fun_prop) (by fun_prop)]
  convert fourierMultiplierCLM_const_apply f.1 1 with x
  ¬∑ simp only [Pi.mul_apply]
    norm_cast
    calc
      _ = (1 + ‚Äñx‚Äñ ^ 2) ^ (s / 2 + -s / 2) := by
        rw [‚Üê Real.rpow_add (by positivity)]
      _ = (1 + ‚Äñx‚Äñ ^ 2) ^ (0 : ‚Ñù) := by congr; ring
      _ = _ := by simp
  ¬∑ simp

theorem injective_sobFn {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] :
    Function.Injective (sobFn (s := s) (p := p) (E := E) (F := F)) := by
  intro ‚ü®f, hf‚ü© ‚ü®g, hg‚ü© hfg
  simp only [Subtype.mk.injEq]
  calc
    f = fourierMultiplierCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ ((-s + s) / 2) : ‚Ñù)) f := by simp
    _ = fourierMultiplierCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (-s / 2) : ‚Ñù))
        (Sobolev.sobFn ‚ü®f, hf‚ü©) := by simp
    _ = fourierMultiplierCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (-s / 2) : ‚Ñù))
        (Sobolev.sobFn ‚ü®g, hg‚ü©) := by congr
    _ = fourierMultiplierCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ ((-s + s) / 2) : ‚Ñù)) g := by simp
    _ = g := by simp

variable (E F) in
def toLp‚Çó (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] :
    Sobolev E F s p ‚Üí‚Çó[‚ÑÇ] Lp F p (volume : Measure E) where
  toFun := Sobolev.sobFn
  map_add' f g := by
    apply_fun Lp.toTemperedDistributionCLM F (volume : Measure E) p
    ¬∑ simp [map_add, ‚Üê sobFn_spec]
    rw [injective_iff_map_eq_zero, ‚Üê LinearMap.ker_eq_bot']
    exact Lp.ker_toTemperedDistributionCLM_eq_bot
  map_smul' c f := by
    apply_fun Lp.toTemperedDistributionCLM F (volume : Measure E) p
    ¬∑ simp [‚Üê sobFn_spec]
    rw [injective_iff_map_eq_zero, ‚Üê LinearMap.ker_eq_bot']
    exact Lp.ker_toTemperedDistributionCLM_eq_bot

theorem sobFn_add {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] (f g : Sobolev E F s p) :
    sobFn (f + g) = sobFn f + sobFn g := (toLp‚Çó E F s p).map_add f g

theorem sobFn_smul {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] (c : ‚ÑÇ) (f : Sobolev E F s p) :
    sobFn (c ‚Ä¢ f) = c ‚Ä¢ sobFn f := (toLp‚Çó E F s p).map_smul c f

@[simp]
theorem toLp‚Çó_apply {s : ‚Ñù} {p : ‚Ñù‚â•0‚àû} [hp : Fact (1 ‚â§ p)] (f : Sobolev E F s p) :
    toLp‚Çó E F s p f = sobFn f := rfl

instance instNormedAddCommGroup (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] :
    NormedAddCommGroup (Sobolev E F s p) :=
  NormedAddCommGroup.induced (Sobolev E F s p) (Lp F p (volume : Measure E)) (toLp‚Çó E F s p)
    injective_sobFn

@[simp]
theorem norm_sobFn_eq (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] (f : Sobolev E F s p) :
    ‚ÄñsobFn f‚Äñ = ‚Äñf‚Äñ :=
  rfl

instance instNormedSpace (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] :
    NormedSpace ‚ÑÇ (Sobolev E F s p) where
  norm_smul_le c f := by
    simp_rw [‚Üê norm_sobFn_eq, ‚Üê norm_smul]
    apply Eq.le
    congr
    exact (toLp‚Çó E F s p).map_smul c f

variable (E F) in
def toLp‚Çó·µ¢ (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)] :
    Sobolev E F s p ‚Üí‚Çó·µ¢[‚ÑÇ] Lp F p (volume : Measure E) where
  __ := toLp‚Çó E F s p
  norm_map' f := by simp

variable (s : ‚Ñù) (p : ‚Ñù‚â•0‚àû) [hp : Fact (1 ‚â§ p)]

example : NormedSpace ‚ÑÇ (Sobolev E F s p) := by
  infer_instance

end Sobolev

end normed

section inner

variable [InnerProductSpace ‚ÑÇ F]

theorem memSobolev_two_iff_fourier {s : ‚Ñù} {f : ùì¢'(E, F)} :
    MemSobolev s 2 f ‚Üî ‚àÉ (f' : Lp F 2 (volume : Measure E)),
    smulLeftCLM F (fun (x : E) ‚Ü¶ ((1 + ‚Äñx‚Äñ ^ 2) ^ (s / 2) : ‚Ñù)) (ùìï f) = f' := by
  rw [MemSobolev]
  constructor
  ¬∑ intro ‚ü®f', hf'‚ü©
    use ùìï f'
    apply_fun ùìï at hf'
    rw [TemperedDistribution.fourierMultiplierCLM_apply, fourier_fourierInv_eq] at hf'
    rw [hf', Lp.fourier_toTemperedDistribution_eq f']
  ¬∑ intro ‚ü®f', hf'‚ü©
    use ùìï‚Åª f'
    rw [TemperedDistribution.fourierMultiplierCLM_apply]
    apply_fun ùìï‚Åª at hf'
    rw [hf', Lp.fourierInv_toTemperedDistribution_eq f']

theorem memSobolev_zero_two_iff_fourierTransform {f : ùì¢'(E, F)} :
    MemSobolev 0 2 f ‚Üî ‚àÉ (f' : Lp F 2 (volume : Measure E)), ùìï f = f' := by
  simp [memSobolev_two_iff_fourier]

namespace Sobolev

instance instInnerProductSpace (s : ‚Ñù) :
    InnerProductSpace ‚ÑÇ (Sobolev E F s 2) where
  inner f g := inner ‚ÑÇ (sobFn f) (sobFn g)
  norm_sq_eq_re_inner f := by simp; norm_cast
  conj_inner_symm f g := by simp
  add_left f g h := by rw [sobFn_add, inner_add_left]
  smul_left f g c := by rw [sobFn_smul, inner_smul_left]

end Sobolev

end inner
