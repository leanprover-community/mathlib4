/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.TemperedDistribution
public import Mathlib.Analysis.InnerProductSpace.Laplacian

/-! # The Laplacian on Schwartz functions

We define the Laplacian on Schwartz functions.

## Main definitions

* `LineDeriv.laplacianCLM`: The abstract definition of a Laplacian as a sum over the second
  derivatives.
* `SchwartzMap.instLaplacian`: The Laplacian for `ğ“¢(E, F)` as an instance of the notation type-class
  `Laplacian`.

## Main statements
* `SchwartzMap.laplacian_eq_sum`: The Laplacian is equal to the sum of second derivatives in any
  orthonormal basis.
* `SchwartzMap.integral_bilinear_laplacian_right_eq_left`: Integration by parts for the Laplacian.

## Implementation notes
The abstract definition `LineDeriv.laplacianCLM` does not provide an instance of `Laplacian` because
the type-class system is not able to infer the inner product space `E`. In order to avoid duplicated
definitions, we do not define `LineDeriv.laplacian` and subsequently every concrete instance of
`LineDeriv` has to provide an instance for `Laplacian` and a proof that
`LineDeriv.laplacianCLM _ _ _ f = Î” f`, for example see `SchwartzMap.laplacianCLM_eq'` and
`SchwartzMap.laplacian_eq_sum'` below.

We also note that since `LineDeriv` merely notation and not tied to `fderiv`, it is not possible to
prove the independence of the basis in the definition of the Laplacian in the abstract setting.
In the case of sufficiently smooth functions, this follows from an equality of `lineDerivOp` and
`fderiv`, see for example `SchwartzMap.coe_laplacian_eq_sum` and in the case of distributions, this
follows from duality.

-/

@[expose] public noncomputable section

variable {Î¹ ğ•œ R E F Fâ‚ Fâ‚‚ Fâ‚ƒ Vâ‚ Vâ‚‚ Vâ‚ƒ : Type*}

namespace LineDeriv

/-! ## Laplacian of `LineDeriv` -/

variable [Ring R]
  [NormedAddCommGroup E] [InnerProductSpace â„ E] [FiniteDimensional â„ E]
  [LineDeriv E Vâ‚ Vâ‚‚] [LineDeriv E Vâ‚‚ Vâ‚ƒ]

section ContinuousLinearMap

variable
  [AddCommGroup Vâ‚] [Module R Vâ‚] [TopologicalSpace Vâ‚]
  [AddCommGroup Vâ‚‚] [Module R Vâ‚‚] [TopologicalSpace Vâ‚‚]
  [AddCommGroup Vâ‚ƒ] [Module R Vâ‚ƒ] [TopologicalSpace Vâ‚ƒ] [IsTopologicalAddGroup Vâ‚ƒ]
  [LineDerivAdd E Vâ‚ Vâ‚‚] [LineDerivSMul R E Vâ‚ Vâ‚‚] [ContinuousLineDeriv E Vâ‚ Vâ‚‚]
  [LineDerivAdd E Vâ‚‚ Vâ‚ƒ] [LineDerivSMul R E Vâ‚‚ Vâ‚ƒ] [ContinuousLineDeriv E Vâ‚‚ Vâ‚ƒ]

variable (R E Vâ‚) in
/-- The Laplacian defined by iterated `lineDerivOp` as a continuous linear map. -/
def laplacianCLM : Vâ‚ â†’L[R] Vâ‚ƒ :=
  âˆ‘ i, lineDerivOpCLM R Vâ‚‚ (stdOrthonormalBasis â„ E i) âˆ˜L
    lineDerivOpCLM R Vâ‚ (stdOrthonormalBasis â„ E i)

end ContinuousLinearMap

end LineDeriv

variable [NormedAddCommGroup E] [InnerProductSpace â„ E] [FiniteDimensional â„ E]
  [NormedAddCommGroup F]

namespace SchwartzMap

/-! ## Laplacian on `ğ“¢(E, F)` -/

variable [NormedSpace â„ F]

open Laplacian LineDeriv

instance instLaplacian : Laplacian ğ“¢(E, F) ğ“¢(E, F) where
  laplacian := laplacianCLM â„ E ğ“¢(E, F)

theorem laplacianCLM_eq' (f : ğ“¢(E, F)) : laplacianCLM â„ E ğ“¢(E, F) f = Î” f := rfl

theorem laplacian_eq_sum' (f : ğ“¢(E, F)) :
    Î” f = âˆ‘ i, âˆ‚_{stdOrthonormalBasis â„ E i} (âˆ‚_{stdOrthonormalBasis â„ E i} f) := by
  simp [â† laplacianCLM_eq', laplacianCLM]

variable (ğ•œ) in
@[simp]
theorem laplacianCLM_eq [RCLike ğ•œ] [NormedSpace ğ•œ F] (f : ğ“¢(E, F)) :
    laplacianCLM ğ•œ E ğ“¢(E, F) f = Î” f := by
  simp [laplacianCLM, laplacian_eq_sum']

theorem coe_laplacian_eq_sum [Fintype Î¹] (b : OrthonormalBasis Î¹ â„ E) (f : ğ“¢(E, F)) :
    Î” (f : E â†’ F) = âˆ‘ i, âˆ‚_{b i} (âˆ‚_{b i} f) := by
  ext x
  simp only [InnerProductSpace.laplacian_eq_iteratedFDeriv_orthonormalBasis f b,
    sum_apply]
  congr 1
  ext i
  rw [â† iteratedLineDerivOp_eq_iteratedFDeriv]
  rfl

theorem laplacian_apply (f : ğ“¢(E, F)) (x : E) : Î” f x = Î” (f : E â†’ F) x := by
  rw [coe_laplacian_eq_sum (stdOrthonormalBasis â„ E), laplacian_eq_sum']

theorem laplacian_eq_sum [Fintype Î¹] (b : OrthonormalBasis Î¹ â„ E) (f : ğ“¢(E, F)) :
    Î” f = âˆ‘ i, âˆ‚_{b i} (âˆ‚_{b i} f) := by
  ext x
  rw [laplacian_apply, coe_laplacian_eq_sum b]

open MeasureTheory

/-! ### Integration by parts -/

variable
  [NormedAddCommGroup Fâ‚] [NormedSpace â„ Fâ‚]
  [NormedAddCommGroup Fâ‚‚] [NormedSpace â„ Fâ‚‚]
  [NormedAddCommGroup Fâ‚ƒ] [NormedSpace â„ Fâ‚ƒ]
  [MeasurableSpace E] {Î¼ : Measure E} [BorelSpace E] [Î¼.IsAddHaarMeasure]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for a general bilinear map. -/
theorem integral_bilinear_laplacian_right_eq_left (f : ğ“¢(E, Fâ‚)) (g : ğ“¢(E, Fâ‚‚))
    (L : Fâ‚ â†’L[â„] Fâ‚‚ â†’L[â„] Fâ‚ƒ) :
    âˆ« x, L (f x) (Î” g x) âˆ‚Î¼ = âˆ« x, L (Î” f x) (g x) âˆ‚Î¼ := by
  simp_rw [laplacian_eq_sum', sum_apply, map_sum, ContinuousLinearMap.coe_sum', Finset.sum_apply]
  rw [MeasureTheory.integral_finset_sum, MeasureTheory.integral_finset_sum]
  Â· simp [integral_bilinear_lineDerivOp_right_eq_neg_left]
  Â· exact fun _ _ â†¦ (pairing L (âˆ‚_{_} <| âˆ‚_{_} f) g).integrable
  Â· exact fun _ _ â†¦ (pairing L f (âˆ‚_{_} <| âˆ‚_{_} g)).integrable

variable [NormedRing ğ•œ] [NormedSpace â„ ğ•œ] [IsScalarTower â„ ğ•œ ğ•œ] [SMulCommClass â„ ğ•œ ğ•œ] in
/-- Integration by parts of Schwartz functions for the Laplacian.

Version for multiplication of scalar-valued Schwartz functions. -/
theorem integral_mul_laplacian_right_eq_left (f : ğ“¢(E, ğ•œ)) (g : ğ“¢(E, ğ•œ)) :
    âˆ« x, f x * Î” g x âˆ‚Î¼ = âˆ« x, Î” f x * g x âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g (ContinuousLinearMap.mul â„ ğ•œ)

variable [RCLike ğ•œ] [NormedSpace ğ•œ F]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for scalar multiplication. -/
theorem integral_smul_laplacian_right_eq_left (f : ğ“¢(E, ğ•œ)) (g : ğ“¢(E, F)) :
    âˆ« x, f x â€¢ Î” g x âˆ‚Î¼ = âˆ« x, Î” f x â€¢ g x âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g (ContinuousLinearMap.lsmul â„ ğ•œ)

variable [NormedSpace ğ•œ Fâ‚] [NormedSpace ğ•œ Fâ‚‚]

/-- Integration by parts of Schwartz functions for the Laplacian.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_clm_comp_laplacian_right_eq_left (f : ğ“¢(E, Fâ‚ â†’L[ğ•œ] Fâ‚‚)) (g : ğ“¢(E, Fâ‚)) :
    âˆ« x, f x (Î” g x) âˆ‚Î¼ = âˆ« x, Î” f x (g x) âˆ‚Î¼ :=
  integral_bilinear_laplacian_right_eq_left f g
    ((ContinuousLinearMap.id ğ•œ (Fâ‚ â†’L[ğ•œ] Fâ‚‚)).bilinearRestrictScalars â„)

end SchwartzMap
