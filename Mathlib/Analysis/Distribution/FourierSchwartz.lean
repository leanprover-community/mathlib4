/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Calculus.BumpFunction.FiniteDimension
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

open Real MeasureTheory MeasureTheory.Measure Filter
open scoped FourierTransform ENNReal InnerProductSpace

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM (fun (f : V â†’ E) â†¦ ğ“• f) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourierIntegral_eq, Pi.add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourierIntegral_eq, Pi.smul_apply, RingHom.id_apply, smul_comm _ c, integral_smul]
  Â· intro f
    exact Real.contDiff_fourierIntegral (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourierIntegral_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      apply Finset.sum_le_sum (fun p hp â†¦ ?_)
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          omega
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

@[simp] lemma fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = ğ“• f := rfl

variable [CompleteSpace E]

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
noncomputable def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := fourierTransformCLM ğ•œ
  invFun := (compCLMOfContinuousLinearEquiv ğ•œ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM ğ•œ)
  left_inv := by
    intro f
    ext x
    change ğ“• (ğ“• f) (-x) = f x
    rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion f.continuous
      f.integrable (fourierTransformCLM ğ•œ f).integrable]
  right_inv := by
    intro f
    ext x
    change ğ“• (fun x â†¦ (ğ“• f) (-x)) x = f x
    simp_rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion_inv
      f.continuous f.integrable (fourierTransformCLM ğ•œ f).integrable]
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp] lemma fourierTransformCLE_apply (f : ğ“¢(V, E)) :
    fourierTransformCLE ğ•œ f = ğ“• f := rfl

@[simp] lemma fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) :
    (fourierTransformCLE ğ•œ).symm f = ğ“•â» f := by
  ext x
  exact (fourierIntegralInv_eq_fourierIntegral_neg f x).symm

-- TODO: Is it ugly to provide these definitions?

theorem continuous_fourier (f : ğ“¢(V, E)) : Continuous (ğ“• f) :=
  (fourierTransformCLE â„‚ f).continuous

theorem integrable_fourier (f : ğ“¢(V, E)) : Integrable (ğ“• f) :=
  (fourierTransformCLE â„‚ f).integrable

theorem memâ„’p_fourier (f : ğ“¢(V, E)) (p : â„â‰¥0âˆ)
    (Î¼ : Measure V := by volume_tac) [Î¼.HasTemperateGrowth] : Memâ„’p (ğ“• f) p Î¼ :=
  (fourierTransformCLE â„‚ f).memâ„’p p Î¼

end SchwartzMap


section Lp

open scoped SchwartzMap

variable {ğ•œ ğ•œ' V E F : Type*} [RCLike ğ•œ] [NormedField ğ•œ']
  [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup V]
  [NormedSpace â„‚ E] [InnerProductSpace â„‚ F] [CompleteSpace F]
  [InnerProductSpace â„ V] [MeasurableSpace V] [BorelSpace V] [FiniteDimensional â„ V]

-- TODO: Move to `Mathlib/Analysis/Fourier/FourierTransform.lean`?
-- TODO: Add to `simp`?
theorem Real.conj_fourierChar (x : â„) : starRingEnd â„‚ (ğ x) = ğ (-x) := by
  simp only [fourierChar, AddChar.coe_mk, mul_neg, Circle.exp_neg]
  exact (Circle.coe_inv_eq_conj _).symm

-- TODO: Rewrite for `VectorFourier.fourierIntegral`?
-- TODO: Move.
-- TODO: Is this useful?
-- Will it just require a lot of definitions if we try to avoid dropping down to integral?
theorem Real.fourierIntegral_congr_ae {f g : V â†’ E} (h : f =áµ[volume] g) : ğ“• f = ğ“• g := by
  ext Î¾
  refine integral_congr_ae ?_
  filter_upwards [h] with x h
  rw [h]

-- TODO: Move.
-- TODO: Adjust typeclasses?
theorem Real.conj_fourierIntegral (f : V â†’ â„‚) (Î¾ : V) :
    starRingEnd â„‚ (ğ“• f Î¾) = ğ“• (fun x â†¦ starRingEnd â„‚ (f x)) (-Î¾) := by
  simp only [fourierIntegral_eq]
  refine Eq.trans integral_conj.symm ?_
  simp [Circle.smul_def, conj_fourierChar]

theorem Real.fourierIntegral_conj (f : V â†’ â„‚) (Î¾ : V) :
    ğ“• (fun x â†¦ starRingEnd â„‚ (f x)) Î¾ = starRingEnd â„‚ (ğ“• f (-Î¾)) := by
  simp only [fourierIntegral_eq]
  refine Eq.trans ?_ integral_conj
  simp [Circle.smul_def, conj_fourierChar]

-- TODO: Move into `Mathlib/Analysis/Fourier/FourierTransform.lean`?
-- TODO: Check type classes for `V`.
-- TODO: Generalize to bilinear `L`?

-- This cannot be generalized from `âŸªÂ·, Â·âŸ«_â„‚` to `âŸªÂ·, Â·âŸ«_ğ•œ` with e.g. `NormedField ğ•œ`.
-- Firstly, we need `RCLike ğ•œ` for e.g. `InnerProductSpace ğ•œ F`.
-- Then, we cannot use `ğ•œ = â„` because we need e.g. `Algebra â„‚ ğ•œ` and `IsScalarTower â„‚ ğ•œ F` for
-- `âŸªf w, ğ (-âŸªv, wâŸ«_â„) â€¢ g vâŸ« = ğ (-âŸªv, wâŸ«_â„) â€¢ âŸªf w, g vâŸ« = âŸªğ âŸªw, vâŸ«_â„ â€¢ f w, g vâŸ«`.
-- Therefore, we may as well restrict ourselves to `ğ•œ = â„‚`.

/-- The L^2 inner product of a function with the Fourier transform of another is equal to the
L^2 inner product of its inverse Fourier transform with the other function.

This is useful for proving Plancherel's theorem.
Note that, unlike Plancherel's theorem, it does not require `Continuous`. -/
theorem Real.integral_inner_fourier_eq_integral_fourierInv_inner
    {f g : V â†’ F} (hf_int : Integrable f) (hg_int : Integrable g) :
    âˆ« w, âŸªf w, ğ“• g wâŸ«_â„‚ = âˆ« w, âŸªğ“•â» f w, g wâŸ«_â„‚ := by
  calc âˆ« w, âŸªf w, ğ“• g wâŸ«_â„‚
  _ = âˆ« w, âˆ« v, ğ (-âŸªw, vâŸ«_â„) â€¢ âŸªf w, g vâŸ«_â„‚ := by
    simp only [Real.fourierIntegral_eq]
    refine congrArg (integral _) (funext fun w â†¦ ?_)
    calc âŸªf w, âˆ« v, ğ (-âŸªv, wâŸ«_â„) â€¢ g vâŸ«_â„‚
    _ = âˆ« v, âŸªf w, ğ (-âŸªv, wâŸ«_â„) â€¢ g vâŸ«_â„‚ := by
      refine (integral_inner ?_ _).symm
      exact (fourierIntegral_convergent_iff w).mpr hg_int
    _ = âˆ« v, ğ (-âŸªv, wâŸ«_â„) â€¢ âŸªf w, g vâŸ«_â„‚ := by
      refine congrArg (integral _) (funext fun v â†¦ ?_)
      simp only [Circle.smul_def]  -- TODO: Need `InnerProductSpace â„‚` for this?
      exact inner_smul_right _ _ _
    _ = âˆ« v, ğ (-âŸªw, vâŸ«_â„) â€¢ âŸªf w, g vâŸ«_â„‚ := by simp only [real_inner_comm w]
  -- Change order of integration.
  _ = âˆ« v, âˆ« w, ğ (-âŸªw, vâŸ«_â„) â€¢ âŸªf w, g vâŸ«_â„‚ := by
    refine integral_integral_swap ?_
    simp only [Function.uncurry_def]
    -- TODO: Clean up. `h_prod` below could come from `this`?
    suffices Integrable (fun p : V Ã— V â†¦ âŸªf p.1, g p.2âŸ«_â„‚) (volume.prod volume) by
      refine (integrable_norm_iff (.smul ?_ this.1)).mp ?_
      Â· exact (continuous_fourierChar.comp continuous_inner.neg).aestronglyMeasurable
      simp only [Circle.norm_smul]
      exact (integrable_norm_iff this.1).mpr this
    have h_prod : AEStronglyMeasurable (fun p : V Ã— V â†¦ âŸªf p.1, g p.2âŸ«_â„‚) (volume.prod volume) :=
      hf_int.1.fst.inner hg_int.1.snd
    refine (integrable_prod_iff h_prod).mpr âŸ¨?_, ?_âŸ©
    Â· simp only  -- TODO: Some way to avoid this?
      exact .of_forall fun _ â†¦ .const_inner _ hg_int
    Â· simp only
      refine .mono' (g := fun w â†¦ âˆ« v, â€–f wâ€– * â€–g vâ€–) ?_ ?_ (.of_forall fun w â†¦ ?_)
      Â· simp_rw [integral_mul_left]
        exact hf_int.norm.mul_const _
      Â· exact (h_prod).norm.integral_prod_right'
      refine norm_integral_le_of_norm_le (hg_int.norm.const_mul _) (.of_forall fun v â†¦ ?_)
      rw [norm_norm]
      exact norm_inner_le_norm _ _
  _ = âˆ« w, âŸªğ“•â» f w, g wâŸ«_â„‚ := by
    refine congrArg (integral _) (funext fun v â†¦ ?_)
    -- TODO: Are the nested calcs confusing?
    calc âˆ« w, ğ (-âŸªw, vâŸ«_â„) â€¢ âŸªf w, g vâŸ«_â„‚
    -- Take conjugate to move `f w` to the right of the inner product.
    _ = âˆ« w, starRingEnd â„‚ (ğ âŸªw, vâŸ«_â„ â€¢ âŸªg v, f wâŸ«_â„‚) := by
      simp [Circle.smul_def, conj_fourierChar]
    _ = starRingEnd â„‚ âŸªg v, âˆ« w, ğ âŸªw, vâŸ«_â„ â€¢ f wâŸ«_â„‚ := by
      simp only [integral_conj]
      refine congrArg (starRingEnd â„‚) ?_
      calc âˆ« w, ğ âŸªw, vâŸ«_â„ â€¢ âŸªg v, f wâŸ«_â„‚
      _ = âˆ« w, âŸªg v, ğ âŸªw, vâŸ«_â„ â€¢ f wâŸ«_â„‚ := by simp [Circle.smul_def, inner_smul_right]
      _ = âŸªg v, âˆ« w, ğ âŸªw, vâŸ«_â„ â€¢ f wâŸ«_â„‚ := by
        refine integral_inner ?_ _
        suffices Integrable (fun w â†¦ ğ (-âŸªw, -vâŸ«_â„) â€¢ f w) volume by simpa using this
        exact (Real.fourierIntegral_convergent_iff (-v)).mpr hf_int
    _ = âŸªâˆ« w, ğ âŸªw, vâŸ«_â„ â€¢ f w, g vâŸ«_â„‚ := by simp_rw [inner_conj_symm]
    _ = âŸªğ“•â» f v, g vâŸ«_â„‚ := by simp_rw [Real.fourierIntegralInv_eq]

/-- The more familiar specialization of `integral_inner_fourier_eq_integral_fourierInv_inner` to
`â„‚` -/
theorem Real.integral_fourierTransform_mul_eq_integral_mul_fourierTransform {f g : V â†’ â„‚}
    (hf_int : Integrable f) (hg_int : Integrable g) :
    âˆ« w, ğ“• f w * g w = âˆ« w, f w * ğ“• g w := by
  have := integral_inner_fourier_eq_integral_fourierInv_inner
    (Complex.conjLIE.integrable_comp_iff.mpr hf_int) hg_int
  simp only [fourierIntegralInv_eq_fourierIntegral_neg] at this
  simp only [RCLike.inner_apply, conj_fourierIntegral] at this
  simpa using this.symm

-- TODO: Provide variant for `Continuous g`?
/-- The Fourier transform preserves the L^2 inner product. -/
theorem Real.integral_inner_fourier_eq_integral_inner {f g : V â†’ F} (hf_cont : Continuous f)
    (hf_int : Integrable f) (hf_int_fourier : Integrable (ğ“• f)) (hg_int : Integrable g) :
    âˆ« Î¾, âŸªğ“• f Î¾, ğ“• g Î¾âŸ«_â„‚ = âˆ« x, âŸªf x, g xâŸ«_â„‚ := by
  have := integral_inner_fourier_eq_integral_fourierInv_inner
    hf_int_fourier hg_int
  simp only [Continuous.fourier_inversion hf_cont hf_int hf_int_fourier] at this
  exact this

/-- Plancherel's theorem: The Fourier transform preserves the L^2 norm.

Requires that the norm of `F` is defined by an inner product. -/
theorem Real.integral_norm_sq_fourier_eq_integral_norm_sq {f : V â†’ F} (hf_cont : Continuous f)
    (hf_int : Integrable f) (hf_int_fourier : Integrable (ğ“• f)) :
    âˆ« Î¾, â€–ğ“• f Î¾â€– ^ 2 = âˆ« x, â€–f xâ€– ^ 2 := by
  have := integral_inner_fourier_eq_integral_inner hf_cont hf_int hf_int_fourier hf_int
  simp only [inner_self_eq_norm_sq_to_K] at this
  simp only [â† RCLike.ofReal_pow] at this
  simp only [integral_ofReal] at this
  simpa using this

/-- The Fourier transform preserves the L^2 norm, specialized to `â„‚`-valued functions. -/
theorem Real.integral_normSq_fourierIntegral_eq_integral_normSq {f : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f) (hf_int_fourier : Integrable (ğ“• f)) :
    âˆ« Î¾, Complex.normSq (ğ“• f Î¾) = âˆ« x, Complex.normSq (f x) := by
  have := integral_norm_sq_fourier_eq_integral_norm_sq hf_cont hf_int hf_int_fourier
  simpa [Complex.normSq_eq_abs] using this

-- TODO: Are the assumptions general enough to be useful?
-- TODO: Is it necessary to assume `Memâ„’p (ğ“• f) 2 volume`?
/-- Plancherel's theorem for continuous functions in L^1 âˆ© L^2. -/
theorem Real.eLpNorm_fourier_two_eq_eLpNorm_two {f : V â†’ F} (hf_cont : Continuous f)
    (hf_int : Integrable f) (hf_int2 : Memâ„’p f 2 volume) (hf_int_fourier : Integrable (ğ“• f))
    (hf_int2_fourier : Memâ„’p (ğ“• f) 2 volume) :
    eLpNorm (ğ“• f) 2 volume = eLpNorm f 2 volume := by
  rw [Memâ„’p.eLpNorm_eq_integral_rpow_norm two_ne_zero ENNReal.two_ne_top hf_int2]
  rw [Memâ„’p.eLpNorm_eq_integral_rpow_norm two_ne_zero ENNReal.two_ne_top hf_int2_fourier]
  congr 2
  simpa using integral_norm_sq_fourier_eq_integral_norm_sq hf_cont hf_int hf_int_fourier

/-- Plancherel's theorem for Schwartz functions. -/
theorem SchwartzMap.integral_norm_sq_fourier_eq_integral_norm_sq (f : ğ“¢(V, F)) :
    âˆ« Î¾, â€–ğ“• f Î¾â€– ^ 2 = âˆ« x, â€–f xâ€– ^ 2 :=
  Real.integral_norm_sq_fourier_eq_integral_norm_sq f.continuous f.integrable f.integrable_fourier

/-- Plancherel's theorem for Schwartz functions, `eLpNorm` version. -/
theorem SchwartzMap.eLpNorm_fourier_two_eq_eLpNorm_two (f : ğ“¢(V, F)) :
    eLpNorm (ğ“• f) 2 volume = eLpNorm f 2 volume :=
  Real.eLpNorm_fourier_two_eq_eLpNorm_two f.continuous f.integrable (f.memâ„’p 2 _)
    f.integrable_fourier (f.memâ„’p_fourier 2 _)


-- TODO: Move.
noncomputable instance MeasureTheory.Lp.LpSchwartzMap.instCoeFun {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)]
    {Î¼ : Measure V} : CoeFun (LpSchwartzMap E p Î¼) (fun _ â†¦ V â†’ E) where
  coe f := (((f : Lp E p Î¼) : V â†’â‚˜[Î¼] E) : V â†’ E)

section Fourier

variable [CompleteSpace E]

/-- The Fourier transform of a function in `L^p` which has a representative in the Schwartz space is
a function in `L^q`. -/
theorem MeasureTheory.Lp.LpSchwartzMap.memâ„’p_fourierIntegral
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)] (f : LpSchwartzMap (E := V) E p) :
    Memâ„’p (ğ“• f) q volume :=
  induction_on f (fun g â†¦ Memâ„’p (ğ“• g) q volume)
    (fun g hfg h â†¦ by
      simp only at h âŠ¢
      rw [Real.fourierIntegral_congr_ae hfg]  -- TODO: Check order.
      exact h)
    (fun g â†¦ g.memâ„’p_fourier q volume)

noncomputable def MeasureTheory.Lp.LpSchwartzMap.fourierTransform
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f : LpSchwartzMap E p (volume : Measure V)) :
    LpSchwartzMap E q (volume : Measure V) where
  val := (memâ„’p_fourierIntegral q f).toLp
  property := by
    rcases f with âŸ¨f, hfâŸ©
    rw [mem_iff_ae] at hf âŠ¢
    revert hf
    refine Exists.imp' (SchwartzMap.fourierTransformCLE â„‚) fun fâ‚€ hfâ‚€ â†¦ ?_
    simpa [Real.fourierIntegral_congr_ae hfâ‚€] using Memâ„’p.coeFn_toLp _

theorem MeasureTheory.Lp.LpSchwartzMap.coeFn_fourierTransform
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f : LpSchwartzMap E p (volume : Measure V)) :
    â‡‘(fourierTransform q f) =áµ[volume] ğ“• f := by
  simpa [fourierTransform] using Memâ„’p.coeFn_toLp _

theorem MeasureTheory.Lp.LpSchwartzMap.fourierTransform_add
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f g : LpSchwartzMap E p (volume : Measure V)) :
    fourierTransform q (f + g) = fourierTransform q f + fourierTransform q g := by
  ext
  filter_upwards [coeFn_fourierTransform q (f + g), coeFn_fourierTransform q f,
    coeFn_fourierTransform q g,
    AEEqFun.coeFn_add (Î± := V) (Î³ := E) (fourierTransform q f) (fourierTransform q g)]
    with Î¾ hfg hf hg hfg'
  calc fourierTransform q (f + g) Î¾
  _ = ğ“• (f + g) Î¾ := hfg
  _ = (ğ“• f + ğ“• g) Î¾ := by
    refine congrFun ?_ Î¾
    calc ğ“• (f + g)
    _ = ğ“• (â‡‘f + â‡‘g) := by
      refine Real.fourierIntegral_congr_ae ?_
      filter_upwards [AEEqFun.coeFn_add (Î± := V) (Î³ := E) f g] with x h
      simpa using h
    _ = ğ“• f + ğ“• g := by
      refine induction_onâ‚‚ f g (fun f g â†¦ ğ“• (f + g) = ğ“• f + ğ“• g) ?_ ?_
      Â· intro fâ‚€ gâ‚€ hfâ‚€ hgâ‚€ h
        simp only [Pi.add_def]
        rw [Real.fourierIntegral_congr_ae hfâ‚€, Real.fourierIntegral_congr_ae hgâ‚€]
        rw [Real.fourierIntegral_congr_ae (.add hfâ‚€ hgâ‚€)]
        exact h
      Â· intro fâ‚€ gâ‚€
        change ğ“• â‡‘(fâ‚€ + gâ‚€) = _
        simp only [â† SchwartzMap.fourierTransformCLM_apply â„‚]  -- TODO: Remove need to specify `â„‚`
        ext Î¾
        simp
  _ = (fourierTransform q f + fourierTransform q g) Î¾ := by simp [hfg', hf, hg]

section FourierSMul

variable [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]

theorem MeasureTheory.Lp.LpSchwartzMap.fourierTransform_smul
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (c : ğ•œ) (f : LpSchwartzMap E p (volume : Measure V)) :
    fourierTransform q (c â€¢ f) = c â€¢ fourierTransform q f := by
  ext
  filter_upwards [coeFn_fourierTransform q (c â€¢ f), coeFn_fourierTransform q f,
    coeFn_smul c (fourierTransform q f : Lp E q volume)]
    with Î¾ hcf hf hcf'
  calc fourierTransform q (c â€¢ f) Î¾
  _ = ğ“• (c â€¢ f) Î¾ := hcf
  _ = (c â€¢ ğ“• f) Î¾ := by
    refine congrFun ?_ Î¾
    calc ğ“• â‡‘(c â€¢ f)
    _ = ğ“• (c â€¢ â‡‘f) := by
      refine Real.fourierIntegral_congr_ae ?_
      filter_upwards [coeFn_smul c (f : Lp E p volume)] with x h
      simpa [coe_smul] using h
    _ = c â€¢ ğ“• f := by
      refine induction_on f (fun f â†¦ ğ“• (c â€¢ f) = c â€¢ ğ“• f) ?_ ?_
      Â· intro fâ‚€ hfâ‚€ h
        simp only [Pi.smul_def]
        rw [Real.fourierIntegral_congr_ae hfâ‚€, Real.fourierIntegral_congr_ae (hfâ‚€.const_smul c)]
        exact h
      Â· intro fâ‚€
        change ğ“• â‡‘(c â€¢ fâ‚€) = _
        simp only [â† SchwartzMap.fourierTransformCLM_apply ğ•œ]
        ext Î¾
        simp
  _ = (c â€¢ fourierTransform q f) Î¾ := by simp [coe_smul, hcf', hf]

variable (ğ•œ V E) in
/-- Fourier transform as a linear map from Schwartz maps in `L^p` to Schwartz maps in `L^q`. -/
noncomputable def MeasureTheory.Lp.LpSchwartzMap.fourierTransformLM
    (p q : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] :
    LpSchwartzMap E p (volume : Measure V) â†’â‚—[ğ•œ] LpSchwartzMap E q (volume : Measure V) where
  toFun := fourierTransform q
  map_add' f g := fourierTransform_add q f g
  map_smul' c f := fourierTransform_smul q c f

theorem MeasureTheory.Lp.LpSchwartzMap.coeFn_fourierTransformLM
    (p q : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] [Fact (1 â‰¤ q)] :
    â‡‘(fourierTransformLM ğ•œ V E p q) = fourierTransform q := rfl

-- TODO: Generalize CLM to `L^p` and `L^q` with `1 â‰¤ p â‰¤ 2`.

theorem MeasureTheory.Lp.LpSchwartzMap.uniformContinuous_fourierTransform_one_top :
    UniformContinuous (fun f : LpSchwartzMap (E := V) E 1 â†¦ fourierTransform âŠ¤ f) := by
  refine EMetric.uniformContinuous_iff.mpr ?_
  simp only [Subtype.edist_eq, edist_def]
  intro Îµ hÎµ
  use Îµ, hÎµ
  intro a b h
  calc eLpNorm (â‡‘(fourierTransform âŠ¤ a) - â‡‘(fourierTransform âŠ¤ b)) âŠ¤ volume
  _ = eLpNorm (ğ“• a - ğ“• b) âŠ¤ volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [coeFn_fourierTransform âŠ¤ a, coeFn_fourierTransform âŠ¤ b] with x hâ‚ hâ‚‚
    simp [hâ‚, hâ‚‚]
  _ = eLpNorm (ğ“• (a - b)) âŠ¤ volume := by
    refine congrArg (eLpNorm Â· âŠ¤ volume) ?_
    calc ğ“• a - ğ“• b
    _ = ğ“• (â‡‘a - â‡‘b) := by
      refine induction_onâ‚‚ a b (fun a b â†¦ ğ“• a - ğ“• b = ğ“• (a - b)) ?_ ?_
      Â· intro fâ‚€ gâ‚€ hf hg h
        simp only [Pi.sub_def]
        rw [Real.fourierIntegral_congr_ae hf, Real.fourierIntegral_congr_ae hg]
        rw [Real.fourierIntegral_congr_ae (hf.sub hg)]
        exact h
      intro fâ‚€ gâ‚€
      change _ = ğ“• (fâ‚€ - gâ‚€)
      simp only [â† SchwartzMap.fourierTransformCLM_apply â„‚]  -- TODO: Ok to specify `â„‚` here?
      ext Î¾
      simp
    _ = ğ“• (a - b) := by
      refine Real.fourierIntegral_congr_ae ?_
      filter_upwards [coeFn_sub a.val b.val] with x h
      simpa using h.symm
  _ â‰¤ ENNReal.ofReal (eLpNorm (â‡‘(a - b)) 1 volume).toReal := by
    simp only [eLpNorm_exponent_top]
    refine eLpNormEssSup_le_of_ae_nnnorm_bound ?_
    simp only [ENNReal.toNNReal_toReal_eq]
    refine ae_of_all _ fun x â†¦ ?_
    refine ENNReal.le_toNNReal_of_coe_le ?_ (eLpNorm_ne_top (a - b).val)
    simp only [Real.fourierIntegral_eq]
    refine le_trans (enorm_integral_le_lintegral_enorm _) ?_
    rw [eLpNorm_one_eq_lintegral_enorm]
    refine lintegral_mono_fn fun Î¾ â†¦ ?_
    -- Switch to real-valued norm in order to use `Circle.norm_smul`.
    simp [enorm_eq_nnnorm, â† NNReal.coe_le_coe]
  _ â‰¤ eLpNorm (a - b) 1 volume := ENNReal.ofReal_toReal_le
  _ = eLpNorm (â‡‘a - â‡‘b) 1 volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [coeFn_sub a.val b.val] with x h  -- TODO: Define `coe`?
    simpa using h
  _ < Îµ := h

theorem MeasureTheory.Lp.LpSchwartzMap.uniformContinuous_fourierTransform_two_two :
    UniformContinuous (fun f : LpSchwartzMap (E := V) F 2 â†¦ fourierTransform 2 f) := by
  refine EMetric.uniformContinuous_iff.mpr ?_
  simp only [Subtype.edist_eq, edist_def]
  intro Îµ hÎµ
  use Îµ, hÎµ
  intro f g h
  -- simp only [AddHom.toFun_eq_coe, LinearMap.coe_toAddHom, coeFn_fourierTransformLM]
  calc eLpNorm (â‡‘(fourierTransform 2 f) - â‡‘(fourierTransform 2 g)) 2 volume
  _ = eLpNorm (ğ“• f - ğ“• g) 2 volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [coeFn_fourierTransform 2 f, coeFn_fourierTransform 2 g] with x hâ‚ hâ‚‚
    simp [hâ‚, hâ‚‚]
  _ = eLpNorm (ğ“• (â‡‘f - â‡‘g)) 2 volume := by
    refine congrArg (eLpNorm Â· 2 volume) ?_
    refine induction_onâ‚‚ f g (fun f g â†¦ ğ“• f - ğ“• g = ğ“• (f - g)) ?_ ?_
    Â· intro fâ‚€ gâ‚€ hf hg h
      simp only [Pi.sub_def]
      rw [Real.fourierIntegral_congr_ae hf, Real.fourierIntegral_congr_ae hg]
      rw [Real.fourierIntegral_congr_ae (hf.sub hg)]
      exact h
    intro fâ‚€ gâ‚€
    change _ = ğ“• (fâ‚€ - gâ‚€)
    simp only [â† SchwartzMap.fourierTransformCLM_apply â„‚]  -- TODO: Ok to specify `â„‚` here?
    ext Î¾
    simp
  _ = eLpNorm (ğ“• (f - g)) 2 volume := by
    refine congrArg (eLpNorm Â· 2 volume) ?_
    refine Real.fourierIntegral_congr_ae ?_
    filter_upwards [coeFn_sub f.val g.val] with x h
    simpa using h.symm
  _ = eLpNorm (f - g) 2 volume := by
    refine induction_on (f - g) (fun r â†¦ eLpNorm (ğ“• r) 2 volume = eLpNorm r 2 volume) ?_ ?_
    Â· intro r hr h
      rw [Real.fourierIntegral_congr_ae hr, eLpNorm_congr_ae hr]
      exact h
    -- TODO: Just need to generalize beyond `â„‚`?
    exact SchwartzMap.eLpNorm_fourier_two_eq_eLpNorm_two
  _ = eLpNorm (â‡‘f - â‡‘g) 2 volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [coeFn_sub f.val g.val] with x h
    simpa using h
  _ < Îµ := h

noncomputable def MeasureTheory.Lp.LpSchwartzMap.fourierTransformCLM_one_top :
    LpSchwartzMap E 1 (volume : Measure V) â†’L[ğ•œ] LpSchwartzMap E âŠ¤ (volume : Measure V) :=
  { fourierTransformLM ğ•œ V E 1 âŠ¤ with
    cont := by
      simpa [coeFn_fourierTransformLM] using uniformContinuous_fourierTransform_one_top.continuous
  }

noncomputable def MeasureTheory.Lp.LpSchwartzMap.fourierTransformCLM_two_two :
    LpSchwartzMap F 2 (volume : Measure V) â†’L[ğ•œ] LpSchwartzMap F 2 (volume : Measure V) :=
  { fourierTransformLM ğ•œ V F 2 2 with
    cont := by
      simpa [coeFn_fourierTransformLM] using uniformContinuous_fourierTransform_two_two.continuous
  }

end FourierSMul

end Fourier

section Extend

variable [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  [NormedSpace ğ•œ' E] [SMulCommClass â„ ğ•œ' E]

-- TODO: Move.

variable (ğ•œ' E) in
def MeasureTheory.Lp.LpSchwartzMap.subtypeL (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] (Î¼ : Measure V) :
    LpSchwartzMap E p Î¼ â†’L[ğ•œ'] Lp E p Î¼ where
  toFun f := f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_subtype_val

@[simp]
theorem MeasureTheory.Lp.LpSchwartzMap.coeFn_subtypeL (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] (Î¼ : Measure V) :
    â‡‘(subtypeL ğ•œ' E p Î¼) = Subtype.val := rfl

variable [CompleteSpace E]

noncomputable def MeasureTheory.Lp.fourierTransformCLM_one_top :
    Lp E 1 (volume : Measure V) â†’L[ğ•œ] Lp E âŠ¤ (volume : Measure V) :=
  .extend
    (LpSchwartzMap.subtypeL ğ•œ E âŠ¤ volume âˆ˜L
      LpSchwartzMap.fourierTransformCLM_one_top (ğ•œ := ğ•œ) (V := V) (E := E))
    (LpSchwartzMap.subtypeL ğ•œ E 1 volume)
    (by
      simp only [LpSchwartzMap.coeFn_subtypeL, denseRange_subtype_val, SetLike.setOf_mem_eq]
      exact LpSchwartzMap.dense E ENNReal.one_ne_top volume)
    ((isUniformInducing_iff Subtype.val).mpr rfl)

noncomputable def MeasureTheory.Lp.fourierTransformCLM_two_two :
    Lp F 2 (volume : Measure V) â†’L[ğ•œ] Lp F 2 (volume : Measure V) :=
  .extend
    (LpSchwartzMap.subtypeL ğ•œ F 2 volume âˆ˜L
      LpSchwartzMap.fourierTransformCLM_two_two (ğ•œ := ğ•œ) (V := V) (F := F))
    (LpSchwartzMap.subtypeL ğ•œ F 2 volume)
    (by
      simp only [LpSchwartzMap.coeFn_subtypeL, denseRange_subtype_val, SetLike.setOf_mem_eq]
      exact LpSchwartzMap.dense F ENNReal.two_ne_top volume)
    ((isUniformInducing_iff Subtype.val).mpr rfl)

end Extend

end Lp
