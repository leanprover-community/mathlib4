/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

open Real MeasureTheory MeasureTheory.Measure
open scoped FourierTransform ComplexInnerProductSpace

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {W : Type*} [NormedAddCommGroup W] [NormedSpace â„‚ W] [NormedSpace ğ•œ W]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM (ğ“• Â·) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourierIntegral_eq, add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourierIntegral_eq, smul_apply, smul_comm _ c, integral_smul, RingHom.id_apply]
  Â· intro f
    exact Real.contDiff_fourierIntegral (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourierIntegral_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      gcongr with p hp
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          omega
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

@[simp] lemma fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = ğ“• f := rfl

variable [CompleteSpace E]

@[simp]
theorem fourier_inversion (f : ğ“¢(V, E)) (x : V) : ğ“•â» (ğ“• f) x = f x :=
  Integrable.fourier_inversion f.integrable (fourierTransformCLM â„‚ f).integrable
    f.continuous.continuousAt

@[simp]
theorem fourier_inversion_inv (f : ğ“¢(V, E)) (x : V) : ğ“• (ğ“•â» f) x = f x :=
  Integrable.fourier_inversion_inv f.integrable (fourierTransformCLM â„‚ f).integrable
    f.continuous.continuousAt

variable
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„‚ G]

variable [CompleteSpace F]

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.
Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourierIntegral_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“• g x) := by
  have := VectorFourier.integral_bilin_fourierIntegral_eq_flip M (Î¼ := volume) (Î½ := volume)
    (L := (innerâ‚— V)) continuous_fourierChar continuous_inner f.integrable g.integrable
  rwa [flip_innerâ‚—] at this

theorem integral_sesq_fourierIntegral_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  have := VectorFourier.integral_sesq_fourierIntegral_eq_neg_flip M (Î¼ := volume) (Î½ := volume)
    (L := (innerâ‚— V)) continuous_fourierChar continuous_inner f.integrable g.integrable
  rwa [flip_innerâ‚—] at this

/-- Plancherel's theorem for Schwartz functions.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_sesq_fourier_fourier (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (ğ“• g Î¾) = âˆ« x, M (f x) (g x) := by
  simpa only [fourierTransformCLM_apply, fourier_inversion]
    using integral_sesq_fourierIntegral_eq f (fourierTransformCLM â„‚ g) M

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [CompleteSpace H]

/-- Plancherel's theorem for Schwartz functions. -/
theorem integral_inner_fourier_fourier (f g : ğ“¢(V, H)) :
    âˆ« Î¾, âŸªğ“• f Î¾, ğ“• g Î¾âŸ« = âˆ« x, âŸªf x, g xâŸ« :=
  integral_sesq_fourier_fourier f g (innerSL â„‚)

theorem integral_norm_sq_fourier (f : ğ“¢(V, H)) :
    âˆ« Î¾, â€–ğ“• f Î¾â€–^2 = âˆ« x, â€–f xâ€–^2 := by
  apply Complex.ofRealLI.injective
  simp only [â† LinearIsometry.integral_comp_comm]
  convert integral_inner_fourier_fourier f f <;>
  simp [inner_self_eq_norm_sq_to_K]

theorem inner_fourierTransformCLM_toL2_eq (f : ğ“¢(V, H)) :
    âŸª(fourierTransformCLM â„‚ f).toLp 2, (fourierTransformCLM â„‚ f).toLp 2âŸ« =
    âŸªf.toLp 2, f.toLp 2âŸ« := by
  simp only [inner_toL2_toL2_eq]
  exact integral_sesq_fourier_fourier f f (innerSL â„‚)

theorem norm_fourierTransformCLM_toL2_eq (f : ğ“¢(V, H)) :
    â€–(fourierTransformCLM â„‚ f).toLp 2â€– = â€–f.toLp 2â€– := by
  simp_rw [norm_eq_sqrt_re_inner (ğ•œ := â„‚), inner_fourierTransformCLM_toL2_eq]

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
noncomputable def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := fourierTransformCLM ğ•œ
  invFun := (compCLMOfContinuousLinearEquiv ğ•œ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM ğ•œ)
  left_inv := by
    intro f
    ext x
    change ğ“• (ğ“• f) (-x) = f x
    rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion f.continuous
      f.integrable (fourierTransformCLM ğ•œ f).integrable]
  right_inv := by
    intro f
    ext x
    change ğ“• (fun x â†¦ (ğ“• f) (-x)) x = f x
    simp_rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion_inv
      f.continuous f.integrable (fourierTransformCLM ğ•œ f).integrable]
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp] lemma fourierTransformCLE_apply (f : ğ“¢(V, E)) :
    fourierTransformCLE ğ•œ f = ğ“• f := rfl

@[simp] lemma fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) :
    (fourierTransformCLE ğ•œ).symm f = ğ“•â» f := by
  ext x
  exact (fourierIntegralInv_eq_fourierIntegral_neg f x).symm

end SchwartzMap
