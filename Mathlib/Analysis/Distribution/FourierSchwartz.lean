/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.SchwartzSpace
public import Mathlib.Analysis.Fourier.FourierTransformDeriv
public import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

@[expose] public section

open Real MeasureTheory MeasureTheory.Measure
open scoped FourierTransform ComplexInnerProductSpace

noncomputable section

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {W : Type*} [NormedAddCommGroup W] [NormedSpace â„‚ W] [NormedSpace ğ•œ W]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

section definition

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM ((ğ“• : (V â†’ E) â†’ (V â†’ E)) Â·) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourier_eq, add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourier_eq, smul_apply, smul_comm _ c, integral_smul, RingHom.id_apply]
  Â· intro f
    exact Real.contDiff_fourier (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourier_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      gcongr with p hp
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          lia
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

instance instFourierTransform : FourierTransform ğ“¢(V, E) ğ“¢(V, E) where
  fourier f := fourierTransformCLM â„‚ f

lemma fourier_coe (f : ğ“¢(V, E)) : ğ“• f = ğ“• (f : V â†’ E) := rfl

instance instFourierModule : FourierModule ğ•œ ğ“¢(V, E) ğ“¢(V, E) where
  fourier_add := ContinuousLinearMap.map_add _
  fourier_smul := (fourierTransformCLM ğ•œ).map_smul

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = ğ“• f := rfl

instance instFourierTransformInv : FourierTransformInv ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv := (compCLMOfContinuousLinearEquiv â„‚ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM â„‚)

lemma fourierInv_coe (f : ğ“¢(V, E)) :
    ğ“•â» f = ğ“•â» (f : V â†’ E) := by
  ext x
  exact (fourierInv_eq_fourier_neg f x).symm

instance instFourierInvModule : FourierInvModule ğ•œ ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv_add := ContinuousLinearMap.map_add _
  fourierInv_smul := ((compCLMOfContinuousLinearEquiv ğ•œ (D := V) (E := V) (F := E)
    (LinearIsometryEquiv.neg â„ (E := V))) âˆ˜L (fourierTransformCLM ğ•œ)).map_smul

variable [CompleteSpace E]

instance instFourierPair : FourierPair ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv_fourier_eq := by
    intro f
    ext x
    rw [fourierInv_coe, fourier_coe, f.continuous.fourierInv_fourier_eq f.integrable
      (ğ“• f).integrable]

instance instFourierInvPair : FourierInvPair ğ“¢(V, E) ğ“¢(V, E) where
  fourier_fourierInv_eq := by
    intro f
    ext x
    rw [fourier_coe, fourierInv_coe, f.continuous.fourier_fourierInv_eq f.integrable
      (ğ“• f).integrable]

@[deprecated (since := "2025-11-13")]
alias fourier_inversion := FourierTransform.fourierInv_fourier_eq

@[deprecated (since := "2025-11-13")]
alias fourier_inversion_inv := FourierTransform.fourier_fourierInv_eq

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := FourierTransform.fourierEquiv ğ•œ ğ“¢(V, E) ğ“¢(V, E)
  continuous_toFun := (fourierTransformCLM ğ•œ).continuous
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp]
lemma fourierTransformCLE_apply (f : ğ“¢(V, E)) : fourierTransformCLE ğ•œ f = ğ“• f := rfl

@[simp]
lemma fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) : (fourierTransformCLE ğ•œ).symm f = ğ“•â» f := rfl

end definition

section fubini

variable
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„‚ G]

variable [CompleteSpace E] [CompleteSpace F]

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourier_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“• g x) := by
  simpa using VectorFourier.integral_bilin_fourierIntegral_eq_flip M (L := innerâ‚— V)
    continuous_fourierChar continuous_inner f.integrable g.integrable

@[deprecated (since := "2025-11-16")]
alias integral_bilin_fourierIntegral_eq := integral_bilin_fourier_eq

/-- The Fourier transform satisfies `âˆ« ğ“• f â€¢ g = âˆ« f â€¢ ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourier_smul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, F)) :
    âˆ« Î¾, ğ“• f Î¾ â€¢ g Î¾ = âˆ« x, f x â€¢ ğ“• g x :=
  integral_bilin_fourier_eq f g (ContinuousLinearMap.lsmul â„‚ â„‚)

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourier_mul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, â„‚)) :
    âˆ« Î¾, ğ“• f Î¾ * g Î¾ = âˆ« x, f x * ğ“• g x :=
  integral_bilin_fourier_eq f g (ContinuousLinearMap.mul â„‚ â„‚)

/-- The inverse Fourier transform satisfies `âˆ« ğ“•â» f * g = âˆ« f * ğ“•â» g`, i.e., it is self-adjoint.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourierInv_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“•â» f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  convert (integral_bilin_fourier_eq (ğ“•â» f) (ğ“•â» g) M).symm
  Â· exact (FourierTransform.fourier_fourierInv_eq g).symm
  Â· exact (FourierTransform.fourier_fourierInv_eq f).symm

/-- The inverse Fourier transform satisfies `âˆ« ğ“•â» f â€¢ g = âˆ« f â€¢ ğ“•â» g`, i.e., it is self-adjoint. -/
theorem integral_fourierInv_smul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, F)) :
    âˆ« Î¾, ğ“•â» f Î¾ â€¢ g Î¾ = âˆ« x, f x â€¢ ğ“•â» g x :=
  integral_bilin_fourierInv_eq f g (ContinuousLinearMap.lsmul â„‚ â„‚)

/-- The inverse Fourier transform satisfies `âˆ« ğ“•â» f * g = âˆ« f * ğ“•â» g`, i.e., it is self-adjoint. -/
theorem integral_fourierInv_mul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, â„‚)) :
    âˆ« Î¾, ğ“•â» f Î¾ * g Î¾ = âˆ« x, f x * ğ“•â» g x :=
  integral_bilin_fourierInv_eq f g (ContinuousLinearMap.mul â„‚ â„‚)

theorem integral_sesq_fourier_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  simpa [fourierInv_coe] using VectorFourier.integral_sesq_fourierIntegral_eq_neg_flip M
    (L := innerâ‚— V) continuous_fourierChar continuous_inner f.integrable g.integrable

@[deprecated (since := "2025-11-16")]
alias integral_sesq_fourierIntegral_eq := integral_sesq_fourier_eq

/-- Plancherel's theorem for Schwartz functions.

Version where the inner product is replaced by a general sesquilinear form `M`. -/
theorem integral_sesq_fourier_fourier (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (ğ“• g Î¾) = âˆ« x, M (f x) (g x) := by
  simpa using integral_sesq_fourier_eq f (ğ“• g) M

end fubini

section L2

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [CompleteSpace H]

/-- Plancherel's theorem for Schwartz functions. -/
@[simp] theorem integral_inner_fourier_fourier (f g : ğ“¢(V, H)) :
    âˆ« Î¾, âŸªğ“• f Î¾, ğ“• g Î¾âŸ« = âˆ« x, âŸªf x, g xâŸ« :=
  integral_sesq_fourier_fourier f g (innerSL â„‚)

theorem integral_norm_sq_fourier (f : ğ“¢(V, H)) :
    âˆ« Î¾, â€–ğ“• f Î¾â€– ^ 2 = âˆ« x, â€–f xâ€– ^ 2 := by
  apply Complex.ofRealLI.injective
  simpa [â† LinearIsometry.integral_comp_comm, inner_self_eq_norm_sq_to_K] using
    integral_inner_fourier_fourier f f

theorem inner_fourier_toL2_eq (f g : ğ“¢(V, H)) :
    âŸª(ğ“• f).toLp 2, (ğ“• g).toLp 2âŸ« = âŸªf.toLp 2, g.toLp 2âŸ« := by simp

@[deprecated (since := "2025-11-13")]
alias inner_fourierTransformCLM_toL2_eq := inner_fourier_toL2_eq

@[simp] theorem norm_fourier_toL2_eq (f : ğ“¢(V, H)) :
    â€–(ğ“• f).toLp 2â€– = â€–f.toLp 2â€– := by
  simp_rw [norm_eq_sqrt_re_inner (ğ•œ := â„‚), inner_fourier_toL2_eq]

@[deprecated (since := "2025-11-13")]
alias norm_fourierTransformCLM_toL2_eq := norm_fourier_toL2_eq

end L2

end SchwartzMap
