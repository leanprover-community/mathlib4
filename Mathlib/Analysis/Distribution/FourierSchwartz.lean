/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel, Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.SchwartzSpace
public import Mathlib.Analysis.Fourier.FourierTransformDeriv
public import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

@[expose] public section

open Real MeasureTheory MeasureTheory.Measure
open scoped FourierTransform ComplexInnerProductSpace

noncomputable section

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {W : Type*} [NormedAddCommGroup W]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

section definition

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM ((ğ“• : (V â†’ E) â†’ (V â†’ E)) Â·) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourier_eq, add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourier_eq, smul_apply, smul_comm _ c, integral_smul, RingHom.id_apply]
  Â· intro f
    exact Real.contDiff_fourier (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourier_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      gcongr with p hp
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          lia
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

instance instFourierTransform : FourierTransform ğ“¢(V, E) ğ“¢(V, E) where
  fourier f := fourierTransformCLM â„‚ f

lemma fourier_coe (f : ğ“¢(V, E)) : ğ“• f = ğ“• (f : V â†’ E) := rfl

instance instFourierModule : FourierModule ğ•œ ğ“¢(V, E) ğ“¢(V, E) where
  fourier_add := ContinuousLinearMap.map_add _
  fourier_smul := (fourierTransformCLM ğ•œ).map_smul

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = ğ“• f := rfl

instance instFourierTransformInv : FourierTransformInv ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv := (compCLMOfContinuousLinearEquiv â„‚ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM â„‚)

lemma fourierInv_coe (f : ğ“¢(V, E)) :
    ğ“•â» f = ğ“•â» (f : V â†’ E) := by
  ext x
  exact (fourierInv_eq_fourier_neg f x).symm

instance instFourierInvModule : FourierInvModule ğ•œ ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv_add := ContinuousLinearMap.map_add _
  fourierInv_smul := ((compCLMOfContinuousLinearEquiv ğ•œ (D := V) (E := V) (F := E)
    (LinearIsometryEquiv.neg â„ (E := V))) âˆ˜L (fourierTransformCLM ğ•œ)).map_smul

variable [CompleteSpace E]

instance instFourierPair : FourierPair ğ“¢(V, E) ğ“¢(V, E) where
  fourierInv_fourier_eq := by
    intro f
    ext x
    rw [fourierInv_coe, fourier_coe, f.continuous.fourierInv_fourier_eq f.integrable
      (ğ“• f).integrable]

instance instFourierInvPair : FourierInvPair ğ“¢(V, E) ğ“¢(V, E) where
  fourier_fourierInv_eq := by
    intro f
    ext x
    rw [fourier_coe, fourierInv_coe, f.continuous.fourier_fourierInv_eq f.integrable
      (ğ“• f).integrable]

@[deprecated (since := "2025-11-13")]
alias fourier_inversion := FourierTransform.fourierInv_fourier_eq

@[deprecated (since := "2025-11-13")]
alias fourier_inversion_inv := FourierTransform.fourier_fourierInv_eq

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := FourierTransform.fourierEquiv ğ•œ ğ“¢(V, E) ğ“¢(V, E)
  continuous_toFun := (fourierTransformCLM ğ•œ).continuous
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp]
lemma fourierTransformCLE_apply (f : ğ“¢(V, E)) : fourierTransformCLE ğ•œ f = ğ“• f := rfl

@[simp]
lemma fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) : (fourierTransformCLE ğ•œ).symm f = ğ“•â» f := rfl

end definition

section eval

variable {ğ•œ' : Type*} [NormedField ğ•œ']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„‚ G] [NormedSpace ğ•œ' G] [SMulCommClass â„ ğ•œ' G]

variable (ğ•œ') in
theorem fourier_evalCLM_eq (f : ğ“¢(V, F â†’L[â„] G)) (m : F) :
    ğ“• (SchwartzMap.evalCLM ğ•œ' F G m f) = SchwartzMap.evalCLM ğ•œ' F G m (ğ“• f) := by
  ext x
  exact (fourier_continuousLinearMap_apply f.integrable).symm

end eval

section fubini

variable
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„‚ G]

variable [CompleteSpace E] [CompleteSpace F]

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourier_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“• g x) := by
  simpa using VectorFourier.integral_bilin_fourierIntegral_eq_flip M (L := innerâ‚— V)
    continuous_fourierChar continuous_inner f.integrable g.integrable

@[deprecated (since := "2025-11-16")]
alias integral_bilin_fourierIntegral_eq := integral_bilin_fourier_eq

/-- The Fourier transform satisfies `âˆ« ğ“• f â€¢ g = âˆ« f â€¢ ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourier_smul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, F)) :
    âˆ« Î¾, ğ“• f Î¾ â€¢ g Î¾ = âˆ« x, f x â€¢ ğ“• g x :=
  integral_bilin_fourier_eq f g (ContinuousLinearMap.lsmul â„‚ â„‚)

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourier_mul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, â„‚)) :
    âˆ« Î¾, ğ“• f Î¾ * g Î¾ = âˆ« x, f x * ğ“• g x :=
  integral_bilin_fourier_eq f g (ContinuousLinearMap.mul â„‚ â„‚)

/-- The inverse Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourierInv_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“•â» f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  convert (integral_bilin_fourier_eq (ğ“•â» f) (ğ“•â» g) M).symm
  Â· exact (FourierTransform.fourier_fourierInv_eq g).symm
  Â· exact (FourierTransform.fourier_fourierInv_eq f).symm

/-- The inverse Fourier transform satisfies `âˆ« ğ“• f â€¢ g = âˆ« f â€¢ ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourierInv_smul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, F)) :
    âˆ« Î¾, ğ“•â» f Î¾ â€¢ g Î¾ = âˆ« x, f x â€¢ ğ“•â» g x :=
  integral_bilin_fourierInv_eq f g (ContinuousLinearMap.lsmul â„‚ â„‚)

/-- The inverse Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint. -/
theorem integral_fourierInv_mul_eq (f : ğ“¢(V, â„‚)) (g : ğ“¢(V, â„‚)) :
    âˆ« Î¾, ğ“•â» f Î¾ * g Î¾ = âˆ« x, f x * ğ“•â» g x :=
  integral_bilin_fourierInv_eq f g (ContinuousLinearMap.mul â„‚ â„‚)

theorem integral_sesq_fourier_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  simpa [fourierInv_coe] using VectorFourier.integral_sesq_fourierIntegral_eq_neg_flip M
    (L := innerâ‚— V) continuous_fourierChar continuous_inner f.integrable g.integrable

@[deprecated (since := "2025-11-16")]
alias integral_sesq_fourierIntegral_eq := integral_sesq_fourier_eq

/-- Plancherel's theorem for Schwartz functions.

Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_sesq_fourier_fourier (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (ğ“• g Î¾) = âˆ« x, M (f x) (g x) := by
  simpa using integral_sesq_fourier_eq f (ğ“• g) M

end fubini

section deriv

open ContinuousLinearMap
open scoped ContDiff

variable [NormedSpace â„ W] (L : V â†’L[â„] W â†’L[â„] â„) (f : V â†’ E)

def fourierSMulRightCLM : ğ“¢(V, E) â†’L[â„‚] ğ“¢(V, W â†’L[â„] E) :=
  mkCLM (VectorFourier.fourierSMulRight L Â·) (by intros; ext; simp) (by
    intro c g x
    ext v
    simp only [VectorFourier.fourierSMulRight_apply, smul_apply, neg_smul, RingHom.id_apply,
      ContinuousLinearMap.coe_smul', Pi.smul_apply, smul_neg, neg_inj]
    calc
      _ = (L x) v â€¢ (2 * Ï€ * Complex.I) â€¢ c â€¢ g x := by rw [smul_comm]
      _ = (L x) v â€¢ c â€¢ (2 * Ï€ * Complex.I) â€¢ g x := by congr 1; rw [smul_comm]
      _ = c â€¢ (L x) v â€¢ (2 * Ï€ * Complex.I) â€¢ g x := by rw [smul_comm]
      _ = _ := by congr 1; rw [smul_comm]) (by
    intro f
    unfold VectorFourier.fourierSMulRight
    fun_prop) (by
    intro âŸ¨k, nâŸ©
    use {(k + 1, n), (k, n - 1)}, 4 * Ï€ * â€–Lâ€– * (max 1 n), by positivity
    intro f x
    calc
      _ = â€–xâ€– ^ k * (2 * Ï€ * â€–iteratedFDeriv â„ n (fun x â†¦ (L x).smulRight (f x)) xâ€–) := by
        congr 1
        unfold VectorFourier.fourierSMulRight
        have : ContDiffAt â„ n f x := f.contDiffAt n
        rw [iteratedFDeriv_const_smul_apply' (by fun_prop), norm_smul]
        have : 0 â‰¤ Ï€ := by positivity
        simp [this]
      _ = 2 * Ï€ * â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (fun x â†¦ (L x).smulRight (f x)) xâ€– := by grind
      _ â‰¤ 2 * Ï€ * â€–xâ€– ^ k * âˆ‘ i âˆˆ Finset.range (n + 1), (n.choose i) *
          â€–iteratedFDeriv â„ i L xâ€– * â€–iteratedFDeriv â„ (n - i) f xâ€– := by
        gcongr 1
        exact norm_iteratedFDeriv_le_of_bilinear_of_le_one (smulRightL â„ W E)
          (by fun_prop) (f.smooth âŠ¤) x (ENat.LEInfty.out) norm_smulRightL_le
      _ â‰¤ 2 * Ï€ * â€–xâ€– ^ k *
          (â€–L xâ€– * â€–iteratedFDeriv â„ n f xâ€– + n * â€–Lâ€– * â€–iteratedFDeriv â„ (n - 1) f xâ€–) := by
        gcongr 1
        rw [Finset.sum_range_succ', add_comm]
        simp only [Nat.choose_zero_right, Nat.cast_one, norm_iteratedFDeriv_zero, one_mul,
          Nat.sub_zero, add_le_add_iff_left]
        by_cases! h : n = 0
        Â· simp only [h, Finset.range_zero, Nat.choose_zero_succ, CharP.cast_eq_zero, zero_mul,
          Finset.sum_const_zero]
          positivity
        Â· obtain âŸ¨n', hn'âŸ© : âˆƒ n', n' + 1 = n := by simpa using Nat.zero_lt_of_ne_zero h
          have : âˆ‘ k âˆˆ Finset.range n',
              (((n' + 1).choose (k + 1 + 1)) : â„) * â€–iteratedFDeriv â„ (k + 1 + 1) L xâ€– *
              â€–iteratedFDeriv â„ (n' + 1 - (k + 1 + 1)) f xâ€– = 0 := by
            apply Finset.sum_eq_zero
            intro nâ‚‚ hnâ‚‚
            simp only [mul_eq_zero, Nat.cast_eq_zero, norm_eq_zero]
            left; right
            simp [iteratedFDeriv_succ_eq_comp_right, iteratedFDeriv_succ_const]
          rw [â† hn', Finset.sum_range_succ', this]
          simp only [zero_add, Nat.choose_one_right, Nat.cast_add, Nat.cast_one, Nat.reduceAdd,
            Nat.add_one_sub_one, ge_iff_le]
          gcongr
          sorry
      _ = 2 * Ï€ * â€–xâ€– ^ k * â€–L xâ€– * â€–iteratedFDeriv â„ n (â‡‘f) xâ€– +
            2 * Ï€ * â€–xâ€– ^ k * â†‘n * â€–Lâ€– * â€–iteratedFDeriv â„ (n - 1) (â‡‘f) xâ€– := by ring
      _ â‰¤ 2 * Ï€ * â€–Lâ€– * 1 * (SchwartzMap.seminorm â„‚ (k + 1) n) f +
            2 * Ï€ * â€–Lâ€– * n * (SchwartzMap.seminorm â„‚ k (n - 1) f) := by
        apply add_le_add
        Â· grw [le_opNorm]
          simp only [mul_one]
          move_mul [2, Ï€, â€–Lâ€–, â€–Lâ€–]
          gcongr
          have : â€–xâ€– ^ k * â€–xâ€– = â€–xâ€– ^ (k + 1) := by ring
          rw [this]
          exact le_seminorm â„‚ (k + 1) n f x
        Â· move_mul [2, Ï€, (n : â„), â€–Lâ€–]
          gcongr
          exact le_seminorm â„‚ k (n - 1) f x
      _ â‰¤ 2 * Ï€ * â€–Lâ€– * max 1 n *
          max ((SchwartzMap.seminorm â„‚ (k + 1) n) f) ((SchwartzMap.seminorm â„‚ k (n - 1)) f) +
          2 * Ï€ * â€–Lâ€– * max 1 n *
          max ((SchwartzMap.seminorm â„‚ (k + 1) n) f) ((SchwartzMap.seminorm â„‚ k (n - 1)) f) := by
        apply add_le_add
        all_goals {gcongr; all_goals simp}
      _ = _ := by
        simp only [Finset.sup_insert, schwartzSeminormFamily_apply, Finset.sup_singleton,
          Seminorm.coe_sup, Pi.sup_apply]
        ring)

@[simp]
theorem fourierSMulRightCLM_apply_apply (f : ğ“¢(V, E)) (x : V) :
    fourierSMulRightCLM L f x = -(2 * Ï€ * Complex.I) â€¢ (L x).smulRight (f x) := rfl

theorem fderivCLM_fourier_eq (f : ğ“¢(V, E)) :
    fderivCLM ğ•œ V E (ğ“• f) = ğ“• (fourierSMulRightCLM (innerSL â„) f) := by
  ext1 x
  calc
    _ = fderiv â„ (ğ“• (f : V â†’ E)) x := by simp [fourier_coe]
    _ = ğ“• (VectorFourier.fourierSMulRight (innerSL â„) (f : V â†’ E)) x := by
      rw [Real.fderiv_fourier f.integrable]
      convert f.integrable_pow_mul volume 1
      simp

theorem fourier_fderivCLM_eq (f : ğ“¢(V, E)) :
    ğ“• (fderivCLM ğ•œ V E f) = fourierSMulRightCLM (-innerSL â„) (ğ“• f) := by
  ext1 x
  change ğ“• (fderiv â„ (f : V â†’ E)) x = VectorFourier.fourierSMulRight (-innerSL â„) (ğ“• (f : V â†’ E)) x
  rw [Real.fourier_fderiv f.integrable f.differentiable (fderivCLM â„ V E f).integrable]

open LineDeriv

theorem lineDerivOp_fourier_eq (f : ğ“¢(V, E)) (m : V) :
    âˆ‚_{m} (ğ“• f) = ğ“• (-(2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) f) := calc
  _ = SchwartzMap.evalCLM â„ V E m (fderivCLM â„ V E (ğ“• f)) := rfl
  _ = SchwartzMap.evalCLM â„ V E m (ğ“• (fourierSMulRightCLM (innerSL â„) f)) := by
    rw [fderivCLM_fourier_eq]
  _ = ğ“• (SchwartzMap.evalCLM â„ V E m (fourierSMulRightCLM (innerSL â„) f)) := by
    rw [fourier_evalCLM_eq â„ (fourierSMulRightCLM (innerSL â„) f) m]
  _ = _ := by
    congr
    ext x
    have : (inner â„ Â· m).HasTemperateGrowth := ((innerSL â„).flip m).hasTemperateGrowth
    simp [this, innerSL_apply_apply â„]

theorem fourier_lineDerivOp_eq (f : ğ“¢(V, E)) (m : V) :
    ğ“• (âˆ‚_{m} f) = (2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) (ğ“• f) := calc
  _ = ğ“• (SchwartzMap.evalCLM â„ V E m (fderivCLM â„ V E f)) := rfl
  _ = SchwartzMap.evalCLM â„ V E m (ğ“• (fderivCLM â„ V E f)) := by
    rw [fourier_evalCLM_eq â„]
  _ = SchwartzMap.evalCLM â„ V E m (fourierSMulRightCLM (-innerSL â„) (ğ“• f)) := by
    rw [fourier_fderivCLM_eq]
  _ = _ := by
    ext x
    have : (inner â„ Â· m).HasTemperateGrowth := ((innerSL â„).flip m).hasTemperateGrowth
    simp [this, innerSL_apply_apply â„]

variable [CompleteSpace E]

theorem lineDerivOp_fourierInv_eq (f : ğ“¢(V, E)) (m : V) :
    âˆ‚_{m} (ğ“•â» f) = ğ“•â» ((2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) f) := calc
  _ = ğ“•â» (ğ“• (âˆ‚_{m} (ğ“•â» f))) := by simp
  _ = ğ“•â» ((2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) (ğ“• (ğ“•â» f))) := by
    rw [fourier_lineDerivOp_eq]
  _ = _ := by simp

theorem fourierInv_lineDerivOp_eq (f : ğ“¢(V, E)) (m : V) :
    ğ“•â» (âˆ‚_{m} f) = -(2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) (ğ“•â» f) := calc
  _ = ğ“•â» (âˆ‚_{m} (ğ“• (ğ“•â» f))) := by simp
  _ = ğ“•â» (ğ“• (-(2 * Ï€ * Complex.I) â€¢ smulLeftCLM E (inner â„ Â· m) (ğ“•â» f))) := by
    rw [lineDerivOp_fourier_eq]
  _ = _ := by simp

end deriv

section L2

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [CompleteSpace H]

/-- Plancherel's theorem for Schwartz functions. -/
@[simp] theorem integral_inner_fourier_fourier (f g : ğ“¢(V, H)) :
    âˆ« Î¾, âŸªğ“• f Î¾, ğ“• g Î¾âŸ« = âˆ« x, âŸªf x, g xâŸ« :=
  integral_sesq_fourier_fourier f g (innerSL â„‚)

theorem integral_norm_sq_fourier (f : ğ“¢(V, H)) :
    âˆ« Î¾, â€–ğ“• f Î¾â€–^2 = âˆ« x, â€–f xâ€–^2 := by
  apply Complex.ofRealLI.injective
  simpa [â† LinearIsometry.integral_comp_comm, inner_self_eq_norm_sq_to_K] using
    integral_inner_fourier_fourier f f

theorem inner_fourier_toL2_eq (f g : ğ“¢(V, H)) :
    âŸª(ğ“• f).toLp 2, (ğ“• g).toLp 2âŸ« = âŸªf.toLp 2, g.toLp 2âŸ« := by simp

@[deprecated (since := "2025-11-13")]
alias inner_fourierTransformCLM_toL2_eq := inner_fourier_toL2_eq

@[simp] theorem norm_fourier_toL2_eq (f : ğ“¢(V, H)) :
    â€–(ğ“• f).toLp 2â€– = â€–f.toLp 2â€– := by
  simp_rw [norm_eq_sqrt_re_inner (ğ•œ := â„‚), inner_fourier_toL2_eq]

@[deprecated (since := "2025-11-13")]
alias norm_fourierTransformCLM_toL2_eq := norm_fourier_toL2_eq

end L2

end SchwartzMap
