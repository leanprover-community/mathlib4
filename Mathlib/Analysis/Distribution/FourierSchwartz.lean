/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

open Real MeasureTheory MeasureTheory.Measure Filter
open scoped FourierTransform ENNReal

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM (fun (f : V â†’ E) â†¦ ğ“• f) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourierIntegral_eq, Pi.add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourierIntegral_eq, Pi.smul_apply, RingHom.id_apply, smul_comm _ c, integral_smul]
  Â· intro f
    exact Real.contDiff_fourierIntegral (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourierIntegral_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      apply Finset.sum_le_sum (fun p hp â†¦ ?_)
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          omega
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

@[simp] lemma fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = ğ“• f := rfl

variable [CompleteSpace E]

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
noncomputable def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := fourierTransformCLM ğ•œ
  invFun := (compCLMOfContinuousLinearEquiv ğ•œ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM ğ•œ)
  left_inv := by
    intro f
    ext x
    change ğ“• (ğ“• f) (-x) = f x
    rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion f.continuous
      f.integrable (fourierTransformCLM ğ•œ f).integrable]
  right_inv := by
    intro f
    ext x
    change ğ“• (fun x â†¦ (ğ“• f) (-x)) x = f x
    simp_rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion_inv
      f.continuous f.integrable (fourierTransformCLM ğ•œ f).integrable]
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp] lemma fourierTransformCLE_apply (f : ğ“¢(V, E)) :
    fourierTransformCLE ğ•œ f = ğ“• f := rfl

@[simp] lemma fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) :
    (fourierTransformCLE ğ•œ).symm f = ğ“•â» f := by
  ext x
  exact (fourierIntegralInv_eq_fourierIntegral_neg f x).symm

-- TODO: Is it ugly to provide these definitions?

theorem continuous_fourier (f : ğ“¢(V, E)) : Continuous (ğ“• f) :=
  (fourierTransformCLE â„‚ f).continuous

theorem integrable_fourier (f : ğ“¢(V, E)) : Integrable (ğ“• f) :=
  (fourierTransformCLE â„‚ f).integrable

theorem memâ„’p_fourier (f : ğ“¢(V, E)) (p : â„â‰¥0âˆ)
    (Î¼ : Measure V := by volume_tac) [Î¼.HasTemperateGrowth] : Memâ„’p (ğ“• f) p Î¼ :=
  (fourierTransformCLE â„‚ f).memâ„’p p Î¼

-- TODO: Move to `Mathlib/Analysis/Fourier/FourierTransform.lean`?
-- TODO: Add to `simp`?
theorem _root_.Real.conj_fourierChar (x : â„) : starRingEnd â„‚ (ğ x) = ğ (-x) := by
  simp only [Real.fourierChar, AddChar.coe_mk, mul_neg, Circle.exp_neg]
  exact (Circle.coe_inv_eq_conj _).symm

-- TODO: Move.
-- TODO: Adjust typeclasses?
theorem _root_.Real.star_fourierIntegral (f : V â†’ â„‚) (Î¾ : V) :
    starRingEnd â„‚ (ğ“• f Î¾) = ğ“• (fun x â†¦ starRingEnd â„‚ (f x)) (-Î¾) := by
  simp only [fourierIntegral_eq]
  refine Eq.trans integral_conj.symm ?_
  simp [Circle.smul_def, Real.conj_fourierChar]

theorem _root_.Real.fourierIntegral_star (f : V â†’ â„‚) (Î¾ : V) :
    ğ“• (fun x â†¦ starRingEnd â„‚ (f x)) Î¾ = starRingEnd â„‚ (ğ“• f (-Î¾)) := by
  simp only [fourierIntegral_eq]
  refine Eq.trans ?_ integral_conj
  simp [Circle.smul_def, Real.conj_fourierChar]

-- TODO: Move into `Mathlib/Analysis/Fourier/FourierTransform.lean`?
-- TODO: Check type classes for `V`.
-- TODO: Generalize to bilinear `L`?
theorem _root_.Real.integral_fourierTransform_mul_eq_integral_mul_fourierTransform {f g : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f)
    (hg_cont : Continuous g) (hg_int : Integrable g) :
    âˆ« w, ğ“• f w * g w = âˆ« w, f w * ğ“• g w := by
  calc âˆ« w, ğ“• f w * g w
  _ = âˆ« w, âˆ« v, ğ (-inner v w) â€¢ f v * g w := by simp [Real.fourierIntegral_eq, integral_mul_right]
  _ = âˆ« w, âˆ« v, ğ (-inner v w) â€¢ (f v * g w) := by
    simp only [Circle.smul_def, smul_eq_mul]
    refine congrArg (integral _) (funext fun w â†¦ ?_)
    refine congrArg (integral _) (funext fun v â†¦ ?_)
    ring
  _ = âˆ« v, âˆ« w, ğ (-inner v w) â€¢ (f v * g w) := by
    symm
    refine integral_integral_swap ?_
    simp only [Function.uncurry_def]
    rw [â† integrable_norm_iff]
    swap
    Â· refine Continuous.aestronglyMeasurable (.smul ?_ ?_)
      Â· exact .comp Real.continuous_fourierChar continuous_inner.neg
      Â· exact .mul (hf_cont.comp continuous_fst) (hg_cont.comp continuous_snd)
    simp only [Circle.norm_smul, norm_mul]
    exact .prod_mul hf_int.norm hg_int.norm
  _ = âˆ« v, âˆ« w, f v * (ğ (-inner v w)) â€¢ g w := by
    simp only [Circle.smul_def, smul_eq_mul]
    refine congrArg (integral _) (funext fun w â†¦ ?_)
    refine congrArg (integral _) (funext fun v â†¦ ?_)
    ring
  _ = âˆ« v, f v * âˆ« w, ğ (-inner v w) â€¢ g w := by simp [integral_mul_left]
  _ = âˆ« (w : V), f w * ğ“• g w := by simp [real_inner_comm, Real.fourierIntegral_eq]

-- TODO: Generalize to `RCLike.innerProductSpace : InnerProductSpace ğ•œ ğ•œ`?
-- TODO: Generalize beyond `â„‚`?
/-- The Fourier transform preserves the L^2 norm. -/
theorem _root_.Real.integral_conj_mul_fourierIntegral_eq_integral_conj_mul {f g : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f)
    (hf_cont_fourier : Continuous (ğ“• f)) (hf_int_fourier : Integrable (ğ“• f))
    (hg_cont : Continuous g) (hg_int : Integrable g) :
    âˆ« Î¾, starRingEnd â„‚ (ğ“• f Î¾) * ğ“• g Î¾ = âˆ« x, starRingEnd â„‚ (f x) * g x := by
  -- Consider `âˆ« x, ğ“• f x * g x` with `g x = starRingEnd â„‚ (ğ“• f x)`.
  rw [â† integral_fourierTransform_mul_eq_integral_mul_fourierTransform _ _ hg_cont hg_int]
  rotate_left
  Â· exact Complex.continuous_conj.comp hf_cont_fourier
  Â· exact (LinearIsometryEquiv.integrable_comp_iff Complex.conjLIE).mpr hf_int_fourier
  refine congrArg (integral _) (funext fun x â†¦ ?_)
  rw [Real.fourierIntegral_star]
  rw [â† Real.fourierIntegralInv_eq_fourierIntegral_neg]
  rw [Continuous.fourier_inversion hf_cont hf_int hf_int_fourier]

-- TODO: Is it useful to have this variant?
/-- The Fourier transform preserves the L^2 inner product. -/
theorem _root_.Real.integral_conj_mul_fourierIntegral_eq_integral_conj_mul' {f g : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f)
    (hg_cont : Continuous g) (hg_int : Integrable g)
    (hg_cont_fourier : Continuous (ğ“• g)) (hg_int_fourier : Integrable (ğ“• g)) :
    âˆ« Î¾, starRingEnd â„‚ (ğ“• f Î¾) * ğ“• g Î¾ = âˆ« x, starRingEnd â„‚ (f x) * g x := by
  -- Take conjugate of both sides.
  rw [â† Complex.conjLIE.map_eq_iff]
  simp only [Complex.conjLIE_apply, â† integral_conj, map_mul, Complex.conj_conj]
  simp only [â† mul_comm (starRingEnd â„‚ _)]
  exact integral_conj_mul_fourierIntegral_eq_integral_conj_mul hg_cont hg_int hg_cont_fourier
    hg_int_fourier hf_cont hf_int

-- TODO: Possible to generalize beyond `â„‚`?
-- TODO: Provide eLpNorm version? Requires `Memâ„’p f 2`?
/-- Parseval's theorem: The Fourier transform preserves the L^2 norm. -/
theorem _root_.Real.integral_normSq_fourierIntegral_eq_integral_normSq {f : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f)
    (hf_cont_fourier : Continuous (ğ“• f)) (hf_int_fourier : Integrable (ğ“• f)) :
    âˆ« Î¾, Complex.normSq (ğ“• f Î¾) = âˆ« x, Complex.normSq (f x) := by
  -- Switch to integral taking values in `â„‚`.
  rw [â† Complex.ofRealLI.map_eq_iff]
  simp only [â† LinearIsometry.integral_comp_comm]
  change âˆ« Î¾, (Complex.normSq (ğ“• f Î¾) : â„‚) = âˆ« x, (Complex.normSq (f x) : â„‚)
  simp only [Complex.normSq_eq_conj_mul_self]
  exact integral_conj_mul_fourierIntegral_eq_integral_conj_mul' hf_cont hf_int hf_cont hf_int
    hf_cont_fourier hf_int_fourier

theorem integral_normSq_fourierIntegral_eq_integral_normSq (f : ğ“¢(V, â„‚)) :
    âˆ« Î¾, Complex.normSq (ğ“• f Î¾) = âˆ« x, Complex.normSq (f x) :=
  Real.integral_normSq_fourierIntegral_eq_integral_normSq f.continuous f.integrable
    f.continuous_fourier f.integrable_fourier

-- TODO: Provide version using `eLpNorm _ 2`. Requires `Memâ„’p f 2`? `Memâ„’p (ğ“• f) 2`?
-- Wait until we know what we need it for.

-- TODO: Make more general? Don't require Continuous?
/-- Parseval's theorem for continuous functions in L^1 âˆ© L^2. -/
theorem _root_.Real.eLpNorm_fourier_two_eq_eLpNorm_two {f : V â†’ â„‚}
    (hf_cont : Continuous f) (hf_int : Integrable f) (hf_mem : Memâ„’p f 2 volume)
    (hf_cont_fourier : Continuous (ğ“• f)) (hf_int_fourier : Integrable (ğ“• f))
    (hf_mem_fourier : Memâ„’p (ğ“• f) 2 volume) :
    eLpNorm (ğ“• f) 2 volume = eLpNorm f 2 volume := by
  rw [Memâ„’p.eLpNorm_eq_integral_rpow_norm two_ne_zero ENNReal.two_ne_top hf_mem,
    Memâ„’p.eLpNorm_eq_integral_rpow_norm two_ne_zero ENNReal.two_ne_top hf_mem_fourier]
  refine congrArg (fun x â†¦ ENNReal.ofReal (x ^ _)) ?_
  simp only [ENNReal.toReal_ofNat, rpow_two, â† Complex.normSq_eq_norm_sq]
  exact Real.integral_normSq_fourierIntegral_eq_integral_normSq hf_cont hf_int hf_cont_fourier
      hf_int_fourier

/-- Parseval's theorem for Schwartz functions. -/
theorem eLpNorm_fourier_two_eq_eLpNorm_two (f : ğ“¢(V, â„‚)) :
    eLpNorm (ğ“• f) 2 volume = eLpNorm f 2 volume :=
  Real.eLpNorm_fourier_two_eq_eLpNorm_two f.continuous f.integrable (f.memâ„’p 2 _)
    f.continuous_fourier f.integrable_fourier (f.memâ„’p_fourier 2 _)

/-- Parseval's theorem for Schwartz functions: The Fourier transform preserves the L^2 norm. -/
theorem eLpNorm_fourier_two_lt_top (f : ğ“¢(V, â„‚)) : eLpNorm (ğ“• f) 2 volume < âŠ¤ :=
  (memâ„’p_fourier f 2 volume).eLpNorm_lt_top

/-- Parseval's theorem for Schwartz functions: The Fourier transform preserves the L^2 norm. -/
theorem eLpNorm_two_lt_top (f : ğ“¢(V, â„‚)) : eLpNorm f 2 volume < âŠ¤ :=
  (memâ„’p f 2 volume).eLpNorm_lt_top

-- TODO: Move.
-- TODO: Typeclasses.
omit [CompleteSpace E]
theorem _root_.Real.fourierIntegral_congr_ae {f g : V â†’ E} (h : f =áµ[volume] g) : ğ“• f = ğ“• g := by
  ext Î¾
  refine integral_congr_ae ?_
  filter_upwards [h] with x h
  rw [h]

-- TODO: Move.
noncomputable instance _root_.MeasureTheory.Lp.LpSchwartzMap.instCoeFun {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)]
    {Î¼ : Measure V} : CoeFun (MeasureTheory.Lp.LpSchwartzMap E p Î¼) (fun _ â†¦ V â†’ E) where
  coe f := (((f : Lp E p Î¼) : V â†’â‚˜[Î¼] E) : V â†’ E)

/-- The Fourier transform of a function in L^p that has a representative in the Schwartz space is a
function in L^q. -/
theorem _root_.MeasureTheory.Lp.LpSchwartzMap.memâ„’p_fourierTransform [CompleteSpace E]
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)] (f : Lp.LpSchwartzMap (E := V) E p) :
    Memâ„’p (ğ“• f) q volume :=
  Lp.LpSchwartzMap.induction_on f (fun f â†¦ Memâ„’p (ğ“• f) q volume)
    (fun g h â†¦ by simpa [Real.fourierIntegral_congr_ae h] using g.memâ„’p_fourier q _)

/-- The Fourier transform of a function in L^p that has a representative in the Schwartz space is a
function in L^q. -/
theorem _root_.MeasureTheory.Lp.LpSchwartzMap.fourierTransform_mem_LpSchwartzMap [CompleteSpace E]
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f : Lp.LpSchwartzMap E p (volume : Measure V)) :
    (Lp.LpSchwartzMap.memâ„’p_fourierTransform q f).toLp âˆˆ Lp.LpSchwartzMap E q volume := by
  -- TODO: Use `Lp.LpSchwartzMap.induction_on`? Need `Lp.LpSchwartzMap.induction_memâ„’p`?
  obtain âŸ¨f, hfâŸ© := f
  rw [Lp.LpSchwartzMap.mem_iff_ae] at hf âŠ¢
  obtain âŸ¨g, hgâŸ© := hf
  use SchwartzMap.fourierTransformCLE â„‚ g
  simp only [fourierTransformCLE_apply]
  rw [Real.fourierIntegral_congr_ae hg]
  symm
  exact Memâ„’p.coeFn_toLp _

noncomputable def _root_.MeasureTheory.Lp.LpSchwartzMap.fourierTransform [CompleteSpace E]
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f : Lp.LpSchwartzMap E p (volume : Measure V)) : Lp.LpSchwartzMap E q (volume : Measure V) :=
  âŸ¨_, Lp.LpSchwartzMap.fourierTransform_mem_LpSchwartzMap q fâŸ©

theorem _root_.MeasureTheory.Lp.LpSchwartzMap.coeFn_fourierTransform [CompleteSpace E]
    {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] (q : â„â‰¥0âˆ) [Fact (1 â‰¤ q)]
    (f : Lp.LpSchwartzMap E p (volume : Measure V)) :
    â‡‘(Lp.LpSchwartzMap.fourierTransform q f) =áµ[volume] ğ“• f := by
  simpa [Lp.LpSchwartzMap.fourierTransform] using Memâ„’p.coeFn_toLp _

theorem _root_.MeasureTheory.Lp.LpSchwartzMap.uniformContinuous_fourierTransform_two :
    UniformContinuous (Lp.LpSchwartzMap.fourierTransform 2 :
      Lp.LpSchwartzMap â„‚ 2 (volume : Measure V) â†’ Lp.LpSchwartzMap â„‚ 2 (volume : Measure V)) := by
  rw [EMetric.uniformContinuous_iff]
  intro Îµ hÎµ
  simp only [Subtype.edist_eq, Lp.edist_def]
  use Îµ, hÎµ
  intro f g h
  calc
  _ = eLpNorm (ğ“• f - ğ“• g) 2 volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [Lp.LpSchwartzMap.coeFn_fourierTransform 2 f,
      Lp.LpSchwartzMap.coeFn_fourierTransform 2 g] with x hâ‚ hâ‚‚
    simp [hâ‚, hâ‚‚]
  _ = eLpNorm (ğ“• (â‡‘f - â‡‘g)) 2 volume := by
    refine congrArg (eLpNorm Â· 2 volume) ?_
    refine Lp.LpSchwartzMap.induction_onâ‚‚ f g (fun f g â†¦ ğ“• f - ğ“• g = ğ“• (f - g)) ?_
    intro fâ‚€ gâ‚€ hf hg
    ext x
    simp only [Pi.sub_apply]
    rw [Real.fourierIntegral_congr_ae hf, Real.fourierIntegral_congr_ae hg]
    have : SchwartzMap.fourierTransformCLE â„‚ fâ‚€ x - SchwartzMap.fourierTransformCLE â„‚ gâ‚€ x =
        SchwartzMap.fourierTransformCLE â„‚ (fâ‚€ - gâ‚€) x := by simp
    simp only [fourierTransformCLE_apply] at this
    refine Eq.trans this ?_
    refine congrFun ?_ x
    refine Real.fourierIntegral_congr_ae ?_
    filter_upwards [hf, hg] with x hâ‚ hâ‚‚
    simp [hâ‚, hâ‚‚]
  _ = eLpNorm (ğ“• (f - g)) 2 volume := by
    refine congrArg (eLpNorm Â· 2 volume) ?_
    refine Real.fourierIntegral_congr_ae ?_
    filter_upwards [AEEqFun.coeFn_sub (f : V â†’â‚˜[volume] â„‚) g] with x h
    simp [h]
  _ = eLpNorm (f - g) 2 volume := by
    refine Lp.LpSchwartzMap.induction_on (f - g)
      (fun r â†¦ eLpNorm (ğ“• r) 2 volume = eLpNorm r 2 volume) ?_
    intro r hr
    rw [Real.fourierIntegral_congr_ae hr, eLpNorm_congr_ae hr]
    exact r.eLpNorm_fourier_two_eq_eLpNorm_two
  _ = eLpNorm (â‡‘f - â‡‘g) 2 volume := by
    refine eLpNorm_congr_ae ?_
    filter_upwards [AEEqFun.coeFn_sub (f : V â†’â‚˜[volume] â„‚) g] with x h
    simp [h]
  _ < Îµ := h

end SchwartzMap
