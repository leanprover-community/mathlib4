/-
Copyright (c) 2024 SÃ©bastien GouÃ«zel. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: SÃ©bastien GouÃ«zel
-/
import Mathlib.Analysis.Distribution.SchwartzSpace
import Mathlib.Analysis.Fourier.FourierTransformDeriv
import Mathlib.Analysis.Fourier.Inversion

/-!
# Fourier transform on Schwartz functions

This file constructs the Fourier transform as a continuous linear map acting on Schwartz
functions, in `fourierTransformCLM`. It is also given as a continuous linear equiv, in
`fourierTransformCLE`.
-/

open Real MeasureTheory MeasureTheory.Measure
open scoped FourierTransform

namespace SchwartzMap

variable
  (ğ•œ : Type*) [RCLike ğ•œ]
  {W : Type*} [NormedAddCommGroup W] [NormedSpace â„‚ W] [NormedSpace ğ•œ W]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

section definition

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransformCLM : ğ“¢(V, E) â†’L[ğ•œ] ğ“¢(V, E) := by
  refine mkCLM (fun (f : V â†’ E) â†¦ ğ“• f) ?_ ?_ ?_ ?_
  Â· intro f g x
    simp only [fourierIntegral_eq, Pi.add_apply, smul_add]
    rw [integral_add]
    Â· exact (fourierIntegral_convergent_iff _).2 f.integrable
    Â· exact (fourierIntegral_convergent_iff _).2 g.integrable
  Â· intro c f x
    simp only [fourierIntegral_eq, Pi.smul_apply, RingHom.id_apply, smul_comm _ c, integral_smul]
  Â· intro f
    exact Real.contDiff_fourierIntegral (fun n _ â†¦ integrable_pow_mul volume f n)
  Â· rintro âŸ¨k, nâŸ©
    refine âŸ¨Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1),
       (2 * Ï€) ^ n * (2 * â†‘n + 2) ^ k * (Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1)).card
         * 2 ^ integrablePower (volume : Measure V) *
         (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2,
       âŸ¨by positivity, fun f x â†¦ ?_âŸ©âŸ©
    apply (pow_mul_norm_iteratedFDeriv_fourierIntegral_le (f.smooth âŠ¤)
      (fun k n _hk _hn â†¦ integrable_pow_mul_iteratedFDeriv _ f k n) le_top le_top x).trans
    simp only [mul_assoc]
    gcongr
    calc
    âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        âˆ« (v : V), â€–vâ€– ^ p.1 * â€–iteratedFDeriv â„ p.2 (â‡‘f) vâ€–
      â‰¤ âˆ‘ p âˆˆ Finset.range (n + 1) Ã—Ë¢ Finset.range (k + 1),
        2 ^ integrablePower (volume : Measure V) *
        (âˆ« (x : V), (1 + â€–xâ€–) ^ (- (integrablePower (volume : Measure V) : â„))) * 2 *
        ((Finset.range (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)).sup
          (schwartzSeminormFamily ğ•œ V E)) f := by
      apply Finset.sum_le_sum (fun p hp â†¦ ?_)
      simp only [Finset.mem_product, Finset.mem_range] at hp
      apply (f.integral_pow_mul_iteratedFDeriv_le ğ•œ _ _ _).trans
      simp only [mul_assoc]
      rw [two_mul]
      gcongr
      Â· apply Seminorm.le_def.1
        have : (0, p.2) âˆˆ (Finset.range (n + integrablePower (volume : Measure V) + 1)
            Ã—Ë¢ Finset.range (k + 1)) := by simp [hp.2]
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
      Â· apply Seminorm.le_def.1
        have : (p.1 + integrablePower (volume : Measure V), p.2) âˆˆ (Finset.range
            (n + integrablePower (volume : Measure V) + 1) Ã—Ë¢ Finset.range (k + 1)) := by
          simp [hp.2]
          omega
        apply Finset.le_sup this (f := fun p â†¦ SchwartzMap.seminorm ğ•œ p.1 p.2 (E := V) (F := E))
    _ = _ := by simp [mul_assoc]

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransform (f : ğ“¢(V, E)) : ğ“¢(V, E) := fourierTransformCLM â„‚ f

@[simp]
theorem coe_fourierTransform (f : ğ“¢(V, E)) : (f.fourierTransform : V â†’ E) = ğ“• f := rfl

theorem fourierTransform_apply (f : ğ“¢(V, E)) (x : V) : f.fourierTransform x = ğ“• f x := rfl

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢(V, E)) :
    fourierTransformCLM ğ•œ f = f.fourierTransform := rfl

variable [CompleteSpace E]

/-- The Fourier transform on a real inner product space, as a continuous linear equiv on the
Schwartz space. -/
noncomputable def fourierTransformCLE : ğ“¢(V, E) â‰ƒL[ğ•œ] ğ“¢(V, E) where
  __ := fourierTransformCLM ğ•œ
  invFun := (compCLMOfContinuousLinearEquiv ğ•œ (LinearIsometryEquiv.neg â„ (E := V)))
      âˆ˜L (fourierTransformCLM ğ•œ)
  left_inv := by
    intro f
    ext x
    change ğ“• (ğ“• f) (-x) = f x
    rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion f.continuous
      f.integrable (fourierTransformCLM ğ•œ f).integrable]
  right_inv := by
    intro f
    ext x
    change ğ“• (fun x â†¦ (ğ“• f) (-x)) x = f x
    simp_rw [â† fourierIntegralInv_eq_fourierIntegral_neg, Continuous.fourier_inversion_inv
      f.continuous f.integrable (fourierTransformCLM ğ•œ f).integrable]
  continuous_invFun := ContinuousLinearMap.continuous _

@[simp]
theorem fourierTransformCLE_apply (f : ğ“¢(V, E)) :
    fourierTransformCLE ğ•œ f = f.fourierTransform := rfl

/-- The Fourier transform on a real inner product space, as a continuous linear map on the
Schwartz space. -/
noncomputable def fourierTransformInv (f : ğ“¢(V, E)) : ğ“¢(V, E) := (fourierTransformCLE â„‚).symm f

@[simp]
theorem coe_fourierTransformInv (f : ğ“¢(V, E)) : (f.fourierTransformInv : V â†’ E) = ğ“•â» f := by
  ext x
  exact (fourierIntegralInv_eq_fourierIntegral_neg f x).symm

theorem fourierTransformInv_apply (f : ğ“¢(V, E)) (x : V) : f.fourierTransformInv x = ğ“•â» f x :=
  (fourierIntegralInv_eq_fourierIntegral_neg f x).symm

@[simp]
theorem fourierTransformCLE_symm_apply (f : ğ“¢(V, E)) :
    (fourierTransformCLE ğ•œ).symm f = f.fourierTransformInv := by rfl

@[simp]
theorem fourier_inversion (f : ğ“¢(V, E)) : f.fourierTransform.fourierTransformInv = f :=
  (fourierTransformCLE â„‚).left_inv f

@[simp]
theorem fourier_inversion_apply (f : ğ“¢(V, E)) (x : V) : ğ“•â» (ğ“• f) x = f x := by
  rw [â† coe_fourierTransform, â† fourierTransformInv_apply, fourier_inversion]

@[simp]
theorem fourier_inversion_inv (f : ğ“¢(V, E)) : f.fourierTransformInv.fourierTransform = f :=
  (fourierTransformCLE â„‚).right_inv f

@[simp]
theorem fourier_inversion_inv_apply (f : ğ“¢(V, E)) (x : V) : ğ“• (ğ“•â» f) x = f x := by
  rw [â† coe_fourierTransformInv, â† fourierTransform_apply, fourier_inversion_inv]

end definition

section fubini

variable
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„‚ F] [NormedSpace ğ•œ F] [SMulCommClass â„‚ ğ•œ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„‚ G]

variable [CompleteSpace E] [CompleteSpace F]

/-- The Fourier transform satisfies `âˆ« ğ“• f * g = âˆ« f * ğ“• g`, i.e., it is self-adjoint.
Version where the multiplication is replaced by a general bilinear form `M`. -/
theorem integral_bilin_fourierIntegral_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’L[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“• g x) := by
  have := VectorFourier.integral_bilin_fourierIntegral_eq_flip M (Î¼ := volume) (Î½ := volume)
    (L := (innerâ‚— V)) continuous_fourierChar continuous_inner f.integrable g.integrable
  rwa [flip_innerâ‚—] at this

theorem integral_sesq_fourierIntegral_eq (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (g Î¾) = âˆ« x, M (f x) (ğ“•â» g x) := by
  have := VectorFourier.integral_sesq_fourierIntegral_eq_neg_flip M (Î¼ := volume) (Î½ := volume)
    (L := (innerâ‚— V)) continuous_fourierChar continuous_inner f.integrable g.integrable
  rwa [flip_innerâ‚—] at this

/-- Plancherel's theorem for Schwartz functions. -/
theorem integral_sesq_fourier_fourier (f : ğ“¢(V, E)) (g : ğ“¢(V, F)) (M : E â†’Lâ‹†[â„‚] F â†’L[â„‚] G) :
    âˆ« Î¾, M (ğ“• f Î¾) (ğ“• g Î¾) = âˆ« x, M (f x) (g x) := by
  have := integral_sesq_fourierIntegral_eq f g.fourierTransform M
  simp only [fourierTransform_apply, coe_fourierTransform, fourier_inversion_apply] at this
  assumption

end fubini

variable {H : Type*} [NormedAddCommGroup H] [InnerProductSpace â„‚ H] [CompleteSpace H]

section toL2

theorem inner_fourierTransform_toL2_eq (f : ğ“¢(V, H)) :
    inner â„‚ (f.fourierTransform.toLp 2) (f.fourierTransform.toLp 2) =
    inner â„‚ (f.toLp 2) (f.toLp 2) := by
  simp only [inner_toL2_toL2_eq]
  exact integral_sesq_fourier_fourier f f (innerSL â„‚)

theorem norm_fourierTransform_toL2_eq (f : ğ“¢(V, H)) :
    â€–f.fourierTransform.toLp 2â€– = â€–f.toLp 2â€– := by
  simp_rw [norm_eq_sqrt_re_inner (ğ•œ := â„‚), inner_fourierTransform_toL2_eq]

end toL2

end SchwartzMap
