/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
import Mathlib.Analysis.Calculus.LineDeriv.Basic
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Analysis.Normed.Group.ZeroAtInfty
import Mathlib.Analysis.SpecialFunctions.Pow.Real
import Mathlib.Analysis.Distribution.TemperateGrowth
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.Tactic.MoveAdd
import Mathlib.MeasureTheory.Function.L2Space

/-!
# Schwartz space

This file defines the Schwartz space. Usually, the Schwartz space is defined as the set of smooth
functions $f : â„^n â†’ â„‚$ such that there exists $C_{Î±Î²} > 0$ with $$|x^Î± âˆ‚^Î² f(x)| < C_{Î±Î²}$$ for
all $x âˆˆ â„^n$ and for all multiindices $Î±, Î²$.
In mathlib, we use a slightly different approach and define the Schwartz space as all
smooth functions `f : E â†’ F`, where `E` and `F` are real normed vector spaces such that for all
natural numbers `k` and `n` we have uniform bounds `â€–xâ€–^k * â€–iteratedFDeriv â„ n f xâ€– < C`.
This approach completely avoids using partial derivatives as well as polynomials.
We construct the topology on the Schwartz space by a family of seminorms, which are the best
constants in the above estimates. The abstract theory of topological vector spaces developed in
`SeminormFamily.moduleFilterBasis` and `WithSeminorms.toLocallyConvexSpace` turns the
Schwartz space into a locally convex topological vector space.

## Main definitions

* `SchwartzMap`: The Schwartz space is the space of smooth functions such that all derivatives
  decay faster than any power of `â€–xâ€–`.
* `SchwartzMap.seminorm`: The family of seminorms as described above
* `SchwartzMap.compCLM`: Composition with a function on the right as a continuous linear map
  `ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(D, F)`, provided that the function is temperate and grows polynomially near
  infinity
* `SchwartzMap.fderivCLM`: The differential as a continuous linear map
  `ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F)`
* `SchwartzMap.derivCLM`: The one-dimensional derivative as a continuous linear map
  `ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F)`
* `SchwartzMap.integralCLM`: Integration as a continuous linear map `ğ“¢(â„, F) â†’L[â„] F`

## Main statements

* `SchwartzMap.instIsUniformAddGroup` and `SchwartzMap.instLocallyConvexSpace`: The Schwartz space
  is a locally convex topological vector space.
* `SchwartzMap.one_add_le_sup_seminorm_apply`: For a Schwartz function `f` there is a uniform bound
  on `(1 + â€–xâ€–) ^ k * â€–iteratedFDeriv â„ n f xâ€–`.

## Implementation details

The implementation of the seminorms is taken almost literally from `ContinuousLinearMap.opNorm`.

## Notation

* `ğ“¢(E, F)`: The Schwartz space `SchwartzMap E F` localized in `SchwartzSpace`

## Tags

Schwartz space, tempered distributions
-/

noncomputable section

open scoped Nat NNReal ContDiff

variable {ğ•œ ğ•œ' D E F G H V : Type*}
variable [NormedAddCommGroup E] [NormedSpace â„ E]
variable [NormedAddCommGroup F] [NormedSpace â„ F]

variable (E F) in
/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of `â€–xâ€–`. -/
structure SchwartzMap where
  /-- The underlying function.

  Do NOT use directly. Use the coercion instead. -/
  toFun : E â†’ F
  smooth' : ContDiff â„ âˆ toFun
  decay' : âˆ€ k n : â„•, âˆƒ C : â„, âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n toFun xâ€– â‰¤ C

/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of `â€–xâ€–`. -/
scoped[SchwartzMap] notation "ğ“¢(" E ", " F ")" => SchwartzMap E F

namespace SchwartzMap

instance instFunLike : FunLike ğ“¢(E, F) E F where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr

/-- All derivatives of a Schwartz function are rapidly decaying. -/
theorem decay (f : ğ“¢(E, F)) (k n : â„•) :
    âˆƒ C : â„, 0 < C âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ C := by
  rcases f.decay' k n with âŸ¨C, hCâŸ©
  exact âŸ¨max C 1, by positivity, fun x => (hC x).trans (le_max_left _ _)âŸ©

/-- Every Schwartz function is smooth. -/
theorem smooth (f : ğ“¢(E, F)) (n : â„•âˆ) : ContDiff â„ n f :=
  f.smooth'.of_le (mod_cast le_top)

/-- Every Schwartz function is continuous. -/
@[continuity]
protected theorem continuous (f : ğ“¢(E, F)) : Continuous f :=
  (f.smooth 0).continuous

instance instContinuousMapClass : ContinuousMapClass ğ“¢(E, F) E F where
  map_continuous := SchwartzMap.continuous

/-- Every Schwartz function is differentiable. -/
protected theorem differentiable (f : ğ“¢(E, F)) : Differentiable â„ f :=
  (f.smooth 1).differentiable rfl.le

/-- Every Schwartz function is differentiable at any point. -/
protected theorem differentiableAt (f : ğ“¢(E, F)) {x : E} : DifferentiableAt â„ f x :=
  f.differentiable.differentiableAt

@[ext]
theorem ext {f g : ğ“¢(E, F)} (h : âˆ€ x, (f : E â†’ F) x = g x) : f = g :=
  DFunLike.ext f g h

section IsBigO

open Asymptotics Filter

variable (f : ğ“¢(E, F))

/-- Auxiliary lemma, used in proving the more general result `isBigO_cocompact_rpow`. -/
theorem isBigO_cocompact_zpow_neg_nat (k : â„•) :
    f =O[cocompact E] fun x => â€–xâ€– ^ (-k : â„¤) := by
  obtain âŸ¨d, _, hd'âŸ© := f.decay k 0
  simp only [norm_iteratedFDeriv_zero] at hd'
  simp_rw [Asymptotics.IsBigO, Asymptotics.IsBigOWith]
  refine âŸ¨d, Filter.Eventually.filter_mono Filter.cocompact_le_cofinite ?_âŸ©
  refine (Filter.eventually_cofinite_ne 0).mono fun x hx => ?_
  rw [Real.norm_of_nonneg (zpow_nonneg (norm_nonneg _) _), zpow_neg, â† div_eq_mul_inv, le_div_iffâ‚€']
  exacts [hd' x, zpow_pos (norm_pos_iff.mpr hx) _]

theorem isBigO_cocompact_rpow [ProperSpace E] (s : â„) :
    f =O[cocompact E] fun x => â€–xâ€– ^ s := by
  let k := âŒˆ-sâŒ‰â‚Š
  have hk : -(k : â„) â‰¤ s := neg_le.mp (Nat.le_ceil (-s))
  refine (isBigO_cocompact_zpow_neg_nat f k).trans ?_
  suffices (fun x : â„ => x ^ (-k : â„¤)) =O[atTop] fun x : â„ => x ^ s
    from this.comp_tendsto tendsto_norm_cocompact_atTop
  simp_rw [Asymptotics.IsBigO, Asymptotics.IsBigOWith]
  refine âŸ¨1, (Filter.eventually_ge_atTop 1).mono fun x hx => ?_âŸ©
  rw [one_mul, Real.norm_of_nonneg (Real.rpow_nonneg (zero_le_one.trans hx) _),
    Real.norm_of_nonneg (zpow_nonneg (zero_le_one.trans hx) _), â† Real.rpow_intCast, Int.cast_neg,
    Int.cast_natCast]
  exact Real.rpow_le_rpow_of_exponent_le hx hk

theorem isBigO_cocompact_zpow [ProperSpace E] (k : â„¤) :
    f =O[cocompact E] fun x => â€–xâ€– ^ k := by
  simpa only [Real.rpow_intCast] using isBigO_cocompact_rpow f k

end IsBigO

section Aux

theorem bounds_nonempty (k n : â„•) (f : ğ“¢(E, F)) :
    âˆƒ c : â„, c âˆˆ { c : â„ | 0 â‰¤ c âˆ§ âˆ€ x : E, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c } :=
  let âŸ¨M, hMp, hMbâŸ© := f.decay k n
  âŸ¨M, le_of_lt hMp, hMbâŸ©

theorem bounds_bddBelow (k n : â„•) (f : ğ“¢(E, F)) :
    BddBelow { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c } :=
  âŸ¨0, fun _ âŸ¨hn, _âŸ© => hnâŸ©

theorem decay_add_le_aux (k n : â„•) (f g : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n ((f : E â†’ F) + (g : E â†’ F)) xâ€– â‰¤
      â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– + â€–xâ€– ^ k * â€–iteratedFDeriv â„ n g xâ€– := by
  rw [â† mul_add]
  gcongr _ * ?_
  rw [iteratedFDeriv_add_apply (f.smooth _).contDiffAt (g.smooth _).contDiffAt]
  exact norm_add_le _ _

theorem decay_neg_aux (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (-f : E â†’ F) xâ€– = â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– := by
  rw [iteratedFDeriv_neg_apply, norm_neg]

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

theorem decay_smul_aux (k n : â„•) (f : ğ“¢(E, F)) (c : ğ•œ) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (c â€¢ (f : E â†’ F)) xâ€– =
      â€–câ€– * â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– := by
  rw [mul_comm â€–câ€–, mul_assoc, iteratedFDeriv_const_smul_apply (f.smooth _).contDiffAt,
    norm_smul c (iteratedFDeriv â„ n (â‡‘f) x)]

end Aux

section SeminormAux

/-- Helper definition for the seminorms of the Schwartz space. -/
protected def seminormAux (k n : â„•) (f : ğ“¢(E, F)) : â„ :=
  sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c }

theorem seminormAux_nonneg (k n : â„•) (f : ğ“¢(E, F)) : 0 â‰¤ f.seminormAux k n :=
  le_csInf (bounds_nonempty k n f) fun _ âŸ¨hx, _âŸ© => hx

theorem le_seminormAux (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (â‡‘f) xâ€– â‰¤ f.seminormAux k n :=
  le_csInf (bounds_nonempty k n f) fun _ âŸ¨_, hâŸ© => h x

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem seminormAux_le_bound (k n : â„•) (f : ğ“¢(E, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ M) : f.seminormAux k n â‰¤ M :=
  csInf_le (bounds_bddBelow k n f) âŸ¨hMp, hMâŸ©

end SeminormAux

/-! ### Algebraic properties -/

section SMul

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F] [NormedField ğ•œ'] [NormedSpace ğ•œ' F]
  [SMulCommClass â„ ğ•œ' F]

instance instSMul : SMul ğ•œ ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' k n := .intro (f.seminormAux k n * â€–câ€–) fun x â†¦ calc
        â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (c â€¢ â‡‘f) xâ€– = â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– * â€–câ€– := by
          rw [mul_comm _ â€–câ€–, â† mul_assoc]
          exact decay_smul_aux k n f c x
        _ â‰¤ SchwartzMap.seminormAux k n f * â€–câ€– := by
          gcongr
          apply f.le_seminormAux }âŸ©

@[simp]
theorem smul_apply {f : ğ“¢(E, F)} {c : ğ•œ} {x : E} : (c â€¢ f) x = c â€¢ f x :=
  rfl

instance instIsScalarTower [SMul ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' F] : IsScalarTower ğ•œ ğ•œ' ğ“¢(E, F) :=
  âŸ¨fun a b f => ext fun x => smul_assoc a b (f x)âŸ©

instance instSMulCommClass [SMulCommClass ğ•œ ğ•œ' F] : SMulCommClass ğ•œ ğ•œ' ğ“¢(E, F) :=
  âŸ¨fun a b f => ext fun x => smul_comm a b (f x)âŸ©

theorem seminormAux_smul_le (k n : â„•) (c : ğ•œ) (f : ğ“¢(E, F)) :
    (c â€¢ f).seminormAux k n â‰¤ â€–câ€– * f.seminormAux k n := by
  refine (c â€¢ f).seminormAux_le_bound k n (mul_nonneg (norm_nonneg _) (seminormAux_nonneg _ _ _))
      fun x => (decay_smul_aux k n f c x).trans_le ?_
  rw [mul_assoc]
  exact mul_le_mul_of_nonneg_left (f.le_seminormAux k n x) (norm_nonneg _)

instance instNSMul : SMul â„• ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' := by simpa [â† Nat.cast_smul_eq_nsmul â„] using ((c : â„) â€¢ f).decay' }âŸ©

instance instZSMul : SMul â„¤ ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' := by simpa [â† Int.cast_smul_eq_zsmul â„] using ((c : â„) â€¢ f).decay' }âŸ©

end SMul

section Zero

instance instZero : Zero ğ“¢(E, F) :=
  âŸ¨{  toFun := fun _ => 0
      smooth' := contDiff_const
      decay' := fun _ _ => âŸ¨1, fun _ => by simpâŸ© }âŸ©

instance instInhabited : Inhabited ğ“¢(E, F) :=
  âŸ¨0âŸ©

theorem coe_zero : DFunLike.coe (0 : ğ“¢(E, F)) = (0 : E â†’ F) :=
  rfl

@[simp]
theorem coeFn_zero : â‡‘(0 : ğ“¢(E, F)) = (0 : E â†’ F) :=
  rfl

@[simp]
theorem zero_apply {x : E} : (0 : ğ“¢(E, F)) x = 0 :=
  rfl

theorem seminormAux_zero (k n : â„•) : (0 : ğ“¢(E, F)).seminormAux k n = 0 :=
  le_antisymm (seminormAux_le_bound k n _ rfl.le fun _ => by simp [Pi.zero_def])
    (seminormAux_nonneg _ _ _)

end Zero

section Neg

instance instNeg : Neg ğ“¢(E, F) :=
  âŸ¨fun f =>
    âŸ¨-f, (f.smooth _).neg, fun k n =>
      âŸ¨f.seminormAux k n, fun x => (decay_neg_aux k n f x).le.trans (f.le_seminormAux k n x)âŸ©âŸ©âŸ©

@[simp]
theorem neg_apply (f : ğ“¢(E, F)) (x : E) : (-f) x = - (f x) := rfl

end Neg

section Add

instance instAdd : Add ğ“¢(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f + g, (f.smooth _).add (g.smooth _), fun k n =>
      âŸ¨f.seminormAux k n + g.seminormAux k n, fun x =>
        (decay_add_le_aux k n f g x).trans
          (add_le_add (f.le_seminormAux k n x) (g.le_seminormAux k n x))âŸ©âŸ©âŸ©

@[simp]
theorem add_apply {f g : ğ“¢(E, F)} {x : E} : (f + g) x = f x + g x :=
  rfl

theorem seminormAux_add_le (k n : â„•) (f g : ğ“¢(E, F)) :
    (f + g).seminormAux k n â‰¤ f.seminormAux k n + g.seminormAux k n :=
  (f + g).seminormAux_le_bound k n
    (add_nonneg (seminormAux_nonneg _ _ _) (seminormAux_nonneg _ _ _)) fun x =>
    (decay_add_le_aux k n f g x).trans <|
      add_le_add (f.le_seminormAux k n x) (g.le_seminormAux k n x)

end Add

section Sub

instance instSub : Sub ğ“¢(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f - g, (f.smooth _).sub (g.smooth _), by
      intro k n
      refine âŸ¨f.seminormAux k n + g.seminormAux k n, fun x => ?_âŸ©
      grw [â† f.le_seminormAux k n x, â† g.le_seminormAux k n x]
      rw [sub_eq_add_neg]
      rw [â† decay_neg_aux k n g x]
      exact decay_add_le_aux k n f (-g) xâŸ©âŸ©

@[simp]
theorem sub_apply {f g : ğ“¢(E, F)} {x : E} : (f - g) x = f x - g x :=
  rfl

end Sub

section AddCommGroup

instance instAddCommGroup : AddCommGroup ğ“¢(E, F) :=
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl

variable (E F)

/-- Coercion as an additive homomorphism. -/
def coeHom : ğ“¢(E, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

variable {E F}

theorem coe_coeHom : (coeHom E F : ğ“¢(E, F) â†’ E â†’ F) = DFunLike.coe :=
  rfl

theorem coeHom_injective : Function.Injective (coeHom E F) := by
  rw [coe_coeHom]
  exact DFunLike.coe_injective

end AddCommGroup

section Module

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

instance instModule : Module ğ•œ ğ“¢(E, F) :=
  coeHom_injective.module ğ•œ (coeHom E F) fun _ _ => rfl

end Module

section Seminorms

/-! ### Seminorms on Schwartz space -/


variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
variable (ğ•œ)

/-- The seminorms of the Schwartz space given by the best constants in the definition of
`ğ“¢(E, F)`. -/
protected def seminorm (k n : â„•) : Seminorm ğ•œ ğ“¢(E, F) :=
  Seminorm.ofSMulLE (SchwartzMap.seminormAux k n) (seminormAux_zero k n) (seminormAux_add_le k n)
    (seminormAux_smul_le k n)

/-- If one controls the seminorm for every `x`, then one controls the seminorm. -/
theorem seminorm_le_bound (k n : â„•) (f : ğ“¢(E, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ M) : SchwartzMap.seminorm ğ•œ k n f â‰¤ M :=
  f.seminormAux_le_bound k n hMp hM

/-- If one controls the seminorm for every `x`, then one controls the seminorm.

Variant for functions `ğ“¢(â„, F)`. -/
theorem seminorm_le_bound' (k n : â„•) (f : ğ“¢(â„, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, |x| ^ k * â€–iteratedDeriv n f xâ€– â‰¤ M) : SchwartzMap.seminorm ğ•œ k n f â‰¤ M := by
  refine seminorm_le_bound ğ•œ k n f hMp ?_
  simpa only [Real.norm_eq_abs, norm_iteratedFDeriv_eq_norm_iteratedDeriv]

/-- The seminorm controls the Schwartz estimate for any fixed `x`. -/
theorem le_seminorm (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ SchwartzMap.seminorm ğ•œ k n f :=
  f.le_seminormAux k n x

/-- The seminorm controls the Schwartz estimate for any fixed `x`.

Variant for functions `ğ“¢(â„, F)`. -/
theorem le_seminorm' (k n : â„•) (f : ğ“¢(â„, F)) (x : â„) :
    |x| ^ k * â€–iteratedDeriv n f xâ€– â‰¤ SchwartzMap.seminorm ğ•œ k n f := by
  have := le_seminorm ğ•œ k n f x
  rwa [â† Real.norm_eq_abs, â† norm_iteratedFDeriv_eq_norm_iteratedDeriv]

theorem norm_iteratedFDeriv_le_seminorm (f : ğ“¢(E, F)) (n : â„•) (xâ‚€ : E) :
    â€–iteratedFDeriv â„ n f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ 0 n) f := by
  have := SchwartzMap.le_seminorm ğ•œ 0 n f xâ‚€
  rwa [pow_zero, one_mul] at this

theorem norm_pow_mul_le_seminorm (f : ğ“¢(E, F)) (k : â„•) (xâ‚€ : E) :
    â€–xâ‚€â€– ^ k * â€–f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ k 0) f := by
  have := SchwartzMap.le_seminorm ğ•œ k 0 f xâ‚€
  rwa [norm_iteratedFDeriv_zero] at this

theorem norm_le_seminorm (f : ğ“¢(E, F)) (xâ‚€ : E) : â€–f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ 0 0) f := by
  have := norm_pow_mul_le_seminorm ğ•œ f 0 xâ‚€
  rwa [pow_zero, one_mul] at this

variable (E F)

/-- The family of Schwartz seminorms. -/
def _root_.schwartzSeminormFamily : SeminormFamily ğ•œ ğ“¢(E, F) (â„• Ã— â„•) :=
  fun m => SchwartzMap.seminorm ğ•œ m.1 m.2

@[simp]
theorem schwartzSeminormFamily_apply (n k : â„•) :
    schwartzSeminormFamily ğ•œ E F (n, k) = SchwartzMap.seminorm ğ•œ n k :=
  rfl

@[simp]
theorem schwartzSeminormFamily_apply_zero :
    schwartzSeminormFamily ğ•œ E F 0 = SchwartzMap.seminorm ğ•œ 0 0 :=
  rfl

variable {ğ•œ E F}

/-- A more convenient version of `le_sup_seminorm_apply`.

The set `Finset.Iic m` is the set of all pairs `(k', n')` with `k' â‰¤ m.1` and `n' â‰¤ m.2`.
Note that the constant is far from optimal. -/
theorem one_add_le_sup_seminorm_apply {m : â„• Ã— â„•} {k n : â„•} (hk : k â‰¤ m.1) (hn : n â‰¤ m.2)
    (f : ğ“¢(E, F)) (x : E) :
    (1 + â€–xâ€–) ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤
      2 ^ m.1 * (Finset.Iic m).sup (fun m => SchwartzMap.seminorm ğ•œ m.1 m.2) f := by
  rw [add_comm, add_pow]
  simp only [one_pow, mul_one, Finset.sum_mul]
  norm_cast
  rw [â† Nat.sum_range_choose m.1]
  push_cast
  rw [Finset.sum_mul]
  have hk' : Finset.range (k + 1) âŠ† Finset.range (m.1 + 1) := by grind
  grw [hk']
  gcongr âˆ‘ _i âˆˆ Finset.range (m.1 + 1), ?_ with i hi
  move_mul [(Nat.choose k i : â„), (Nat.choose m.1 i : â„)]
  gcongr
  Â· apply (le_seminorm ğ•œ i n f x).trans
    apply Seminorm.le_def.1
    exact Finset.le_sup_of_le (Finset.mem_Iic.2 <|
      Prod.mk_le_mk.2 âŸ¨Finset.mem_range_succ_iff.mp hi, hnâŸ©) le_rfl
  Â· exact mod_cast Nat.choose_le_choose i hk

end Seminorms

section Topology

/-! ### The topology on the Schwartz space -/


variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
variable (ğ•œ E F)

instance instTopologicalSpace : TopologicalSpace ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).moduleFilterBasis.topology'

theorem _root_.schwartz_withSeminorms : WithSeminorms (schwartzSeminormFamily ğ•œ E F) := by
  have A : WithSeminorms (schwartzSeminormFamily â„ E F) := âŸ¨rflâŸ©
  rw [SeminormFamily.withSeminorms_iff_nhds_eq_iInf] at A âŠ¢
  rw [A]
  rfl

variable {ğ•œ E F}

instance instContinuousSMul : ContinuousSMul ğ•œ ğ“¢(E, F) := by
  rw [(schwartz_withSeminorms ğ•œ E F).withSeminorms_eq]
  exact (schwartzSeminormFamily ğ•œ E F).moduleFilterBasis.continuousSMul

instance instIsTopologicalAddGroup : IsTopologicalAddGroup ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.isTopologicalAddGroup

instance instUniformSpace : UniformSpace ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.uniformSpace

instance instIsUniformAddGroup : IsUniformAddGroup ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.isUniformAddGroup

@[deprecated (since := "2025-03-31")] alias instUniformAddGroup :=
  SchwartzMap.instIsUniformAddGroup

instance instLocallyConvexSpace : LocallyConvexSpace â„ ğ“¢(E, F) :=
  (schwartz_withSeminorms â„ E F).toLocallyConvexSpace

instance instFirstCountableTopology : FirstCountableTopology ğ“¢(E, F) :=
  (schwartz_withSeminorms â„ E F).firstCountableTopology

end Topology

theorem hasTemperateGrowth (f : ğ“¢(E, F)) : Function.HasTemperateGrowth f := by
  refine âŸ¨smooth f âŠ¤, fun n => ?_âŸ©
  rcases f.decay 0 n with âŸ¨C, Cpos, hCâŸ©
  exact âŸ¨0, C, by simpa using hCâŸ©

section HasCompactSupport

/-- A smooth compactly supported function is a Schwartz function. -/
@[simps]
def _root_.HasCompactSupport.toSchwartzMap {f : E â†’ F} (hâ‚ : HasCompactSupport f)
    (hâ‚‚ : ContDiff â„ âˆ f) : ğ“¢(E, F) where
  toFun := f
  smooth' := hâ‚‚
  decay' k n := by
    set g := fun x â†¦ â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€–
    have hgâ‚ : Continuous g := by
      apply Continuous.mul (by fun_prop)
      exact (hâ‚‚.of_le (right_eq_inf.mp rfl)).continuous_iteratedFDeriv'.norm
    have hgâ‚‚ : HasCompactSupport g := (hâ‚.iteratedFDeriv _).norm.mul_left
    obtain âŸ¨xâ‚€, hxâ‚€âŸ© := hgâ‚.exists_forall_ge_of_hasCompactSupport hgâ‚‚
    exact âŸ¨g xâ‚€, hxâ‚€âŸ©

end HasCompactSupport

section CLM

/-! ### Construction of continuous linear maps between Schwartz spaces -/


variable [NormedField ğ•œ] [NormedField ğ•œ']
variable [NormedAddCommGroup D] [NormedSpace â„ D]
variable [NormedSpace ğ•œ E] [SMulCommClass â„ ğ•œ E]
variable [NormedAddCommGroup G] [NormedSpace â„ G] [NormedSpace ğ•œ' G] [SMulCommClass â„ ğ•œ' G]
variable {Ïƒ : ğ•œ â†’+* ğ•œ'}

/-- Create a semilinear map between Schwartz spaces.

Note: This is a helper definition for `mkCLM`. -/
def mkLM (A : ğ“¢(D, E) â†’ F â†’ G) (hadd : âˆ€ (f g : ğ“¢(D, E)) (x), A (f + g) x = A f x + A g x)
    (hsmul : âˆ€ (a : ğ•œ) (f : ğ“¢(D, E)) (x), A (a â€¢ f) x = Ïƒ a â€¢ A f x)
    (hsmooth : âˆ€ f : ğ“¢(D, E), ContDiff â„ âˆ (A f))
    (hbound : âˆ€ n : â„• Ã— â„•, âˆƒ (s : Finset (â„• Ã— â„•)) (C : â„), 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(D, E)) (x : F),
      â€–xâ€– ^ n.fst * â€–iteratedFDeriv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartzSeminormFamily ğ•œ D E) f) :
    ğ“¢(D, E) â†’â‚›â‚—[Ïƒ] ğ“¢(F, G) where
  toFun f :=
    { toFun := A f
      smooth' := hsmooth f
      decay' := by
        intro k n
        rcases hbound âŸ¨k, nâŸ© with âŸ¨s, C, _, hâŸ©
        exact âŸ¨C * (s.sup (schwartzSeminormFamily ğ•œ D E)) f, h fâŸ© }
  map_add' f g := ext (hadd f g)
  map_smul' a f := ext (hsmul a f)

/-- Create a continuous semilinear map between Schwartz spaces.

For an example of using this definition, see `fderivCLM`. -/
def mkCLM [RingHomIsometric Ïƒ] (A : ğ“¢(D, E) â†’ F â†’ G)
    (hadd : âˆ€ (f g : ğ“¢(D, E)) (x), A (f + g) x = A f x + A g x)
    (hsmul : âˆ€ (a : ğ•œ) (f : ğ“¢(D, E)) (x), A (a â€¢ f) x = Ïƒ a â€¢ A f x)
    (hsmooth : âˆ€ f : ğ“¢(D, E), ContDiff â„ âˆ (A f))
    (hbound : âˆ€ n : â„• Ã— â„•, âˆƒ (s : Finset (â„• Ã— â„•)) (C : â„), 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(D, E)) (x : F),
      â€–xâ€– ^ n.fst * â€–iteratedFDeriv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartzSeminormFamily ğ•œ D E) f) :
    ğ“¢(D, E) â†’SL[Ïƒ] ğ“¢(F, G) where
  cont := by
    change Continuous (mkLM A hadd hsmul hsmooth hbound : ğ“¢(D, E) â†’â‚›â‚—[Ïƒ] ğ“¢(F, G))
    refine
      Seminorm.continuous_from_bounded (schwartz_withSeminorms ğ•œ D E)
        (schwartz_withSeminorms ğ•œ' F G) _ fun n => ?_
    rcases hbound n with âŸ¨s, C, hC, hâŸ©
    refine âŸ¨s, âŸ¨C, hCâŸ©, fun f => ?_âŸ©
    exact (mkLM A hadd hsmul hsmooth hbound f).seminorm_le_bound ğ•œ' n.1 n.2 (by positivity) (h f)
  toLinearMap := mkLM A hadd hsmul hsmooth hbound

/-- Define a continuous semilinear map from Schwartz space to a normed space. -/
def mkCLMtoNormedSpace [RingHomIsometric Ïƒ] (A : ğ“¢(D, E) â†’ G)
    (hadd : âˆ€ (f g : ğ“¢(D, E)), A (f + g) = A f + A g)
    (hsmul : âˆ€ (a : ğ•œ) (f : ğ“¢(D, E)), A (a â€¢ f) = Ïƒ a â€¢ A f)
    (hbound : âˆƒ (s : Finset (â„• Ã— â„•)) (C : â„), 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(D, E)),
      â€–A fâ€– â‰¤ C * s.sup (schwartzSeminormFamily ğ•œ D E) f) :
    ğ“¢(D, E) â†’SL[Ïƒ] G :=
  letI f : ğ“¢(D, E) â†’â‚›â‚—[Ïƒ] G :=
    { toFun := (A Â·)
      map_add' := hadd
      map_smul' := hsmul }
  { toLinearMap := f
    cont := by
      change Continuous (LinearMap.mk _ _)
      apply Seminorm.cont_withSeminorms_normedSpace G (schwartz_withSeminorms ğ•œ D E)
      rcases hbound with âŸ¨s, C, hC, hâŸ©
      exact âŸ¨s, âŸ¨C, hCâŸ©, hâŸ© }

end CLM

section EvalCLM

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The map applying a vector to Hom-valued Schwartz function as a continuous linear map. -/
protected def evalCLM (m : E) : ğ“¢(E, E â†’L[â„] F) â†’L[ğ•œ] ğ“¢(E, F) :=
  mkCLM (fun f x => f x m) (fun _ _ _ => rfl) (fun _ _ _ => rfl)
    (fun f => ContDiff.clm_apply f.2 contDiff_const) <| by
  rintro âŸ¨k, nâŸ©
  use {(k, n)}, â€–mâ€–, norm_nonneg _
  intro f x
  simp only [Finset.sup_singleton, schwartzSeminormFamily_apply]
  calc
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (f Â· m) xâ€– â‰¤ â€–xâ€– ^ k * (â€–mâ€– * â€–iteratedFDeriv â„ n f xâ€–) := by
      gcongr
      exact norm_iteratedFDeriv_clm_apply_const (f.smooth _).contDiffAt le_rfl
    _ â‰¤ â€–mâ€– * SchwartzMap.seminorm ğ•œ k n f := by
      move_mul [â€–mâ€–]
      gcongr
      apply le_seminorm

end EvalCLM

section Multiplication

variable [NontriviallyNormedField ğ•œ] [NormedAlgebra â„ ğ•œ]
  [NormedAddCommGroup D] [NormedSpace â„ D]
  [NormedAddCommGroup G] [NormedSpace â„ G]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] [NormedSpace ğ•œ G]

/-- The map `f â†¦ (x â†¦ B (f x) (g x))` as a continuous `ğ•œ`-linear map on Schwartz space,
where `B` is a continuous `ğ•œ`-linear map and `g` is a function of temperate growth. -/
def bilinLeftCLM (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G) {g : D â†’ F} (hg : g.HasTemperateGrowth) :
    ğ“¢(D, E) â†’L[ğ•œ] ğ“¢(D, G) := by
  refine mkCLM (fun f x => B (f x) (g x))
    (fun _ _ _ => by simp) (fun _ _ _ => by simp)
    (fun f => (B.bilinearRestrictScalars â„).isBoundedBilinearMap.contDiff.comp
      ((f.smooth âŠ¤).prodMk hg.1)) ?_
  rintro âŸ¨k, nâŸ©
  rcases hg.norm_iteratedFDeriv_le_uniform_aux n with âŸ¨l, C, hC, hgrowthâŸ©
  use
    Finset.Iic (l + k, n), â€–Bâ€– * ((n : â„) + (1 : â„)) * n.choose (n / 2) * (C * 2 ^ (l + k)),
    by positivity
  intro f x
  have hxk : 0 â‰¤ â€–xâ€– ^ k := by positivity
  simp_rw [â† ContinuousLinearMap.bilinearRestrictScalars_apply_apply â„ B]
  have hnorm_mul :=
    ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear (B.bilinearRestrictScalars â„)
    (f.smooth âŠ¤) hg.1 x (n := n) (mod_cast le_top)
  grw [hnorm_mul]
  rw [ContinuousLinearMap.norm_bilinearRestrictScalars]
  move_mul [â† â€–Bâ€–]
  simp_rw [mul_assoc â€–Bâ€–]
  gcongr _ * ?_
  rw [Finset.mul_sum]
  have : (âˆ‘ _x âˆˆ Finset.range (n + 1), (1 : â„)) = n + 1 := by simp
  simp_rw [mul_assoc ((n : â„) + 1)]
  rw [â† this, Finset.sum_mul]
  refine Finset.sum_le_sum fun i hi => ?_
  simp only [one_mul]
  move_mul [(Nat.choose n i : â„), (Nat.choose n (n / 2) : â„)]
  gcongr ?_ * ?_
  swap
  Â· norm_cast
    exact i.choose_le_middle n
  specialize hgrowth (n - i) (by simp only [tsub_le_self]) x
  grw [hgrowth]
  move_mul [C]
  gcongr ?_ * C
  rw [Finset.mem_range_succ_iff] at hi
  change i â‰¤ (l + k, n).snd at hi
  refine le_trans ?_ (one_add_le_sup_seminorm_apply le_rfl hi f x)
  rw [pow_add]
  move_mul [(1 + â€–xâ€–) ^ l]
  gcongr
  simp

@[simp]
theorem bilinLeftCLM_apply (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G) {g : D â†’ F} (hg : g.HasTemperateGrowth)
    (f : ğ“¢(D, E)) : bilinLeftCLM B hg f = fun x => B (f x) (g x) := rfl

end Multiplication

section Comp

variable (ğ•œ)
variable [RCLike ğ•œ]
variable [NormedAddCommGroup D] [NormedSpace â„ D]
variable [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- Composition with a function on the right is a continuous linear map on Schwartz space
provided that the function is temperate and growths polynomially near infinity. -/
def compCLM {g : D â†’ E} (hg : g.HasTemperateGrowth)
    (hg_upper : âˆƒ (k : â„•) (C : â„), âˆ€ x, â€–xâ€– â‰¤ C * (1 + â€–g xâ€–) ^ k) : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(D, F) := by
  refine mkCLM (fun f => f âˆ˜ g) (fun _ _ _ => by simp) (fun _ _ _ => rfl)
    (fun f => (f.smooth âŠ¤).comp hg.1) ?_
  rintro âŸ¨k, nâŸ©
  rcases hg.norm_iteratedFDeriv_le_uniform_aux n with âŸ¨l, C, hC, hgrowthâŸ©
  rcases hg_upper with âŸ¨kg, Cg, hg_upper'âŸ©
  have hCg : 1 â‰¤ 1 + Cg := by
    refine le_add_of_nonneg_right ?_
    specialize hg_upper' 0
    rw [norm_zero] at hg_upper'
    exact nonneg_of_mul_nonneg_left hg_upper' (by positivity)
  let k' := kg * (k + l * n)
  use Finset.Iic (k', n), (1 + Cg) ^ (k + l * n) * ((C + 1) ^ n * n ! * 2 ^ k'), by positivity
  intro f x
  let seminorm_f := ((Finset.Iic (k', n)).sup (schwartzSeminormFamily ğ•œ _ _)) f
  have hg_upper'' : (1 + â€–xâ€–) ^ (k + l * n) â‰¤ (1 + Cg) ^ (k + l * n) * (1 + â€–g xâ€–) ^ k' := by
    rw [pow_mul, â† mul_pow]
    gcongr
    rw [add_mul]
    refine add_le_add ?_ (hg_upper' x)
    nth_rw 1 [â† one_mul (1 : â„)]
    gcongr
    apply one_le_powâ‚€
    simp only [le_add_iff_nonneg_right, norm_nonneg]
  have hbound (i) (hi : i â‰¤ n) :
      â€–iteratedFDeriv â„ i f (g x)â€– â‰¤ 2 ^ k' * seminorm_f / (1 + â€–g xâ€–) ^ k' := by
    have hpos : 0 < (1 + â€–g xâ€–) ^ k' := by positivity
    rw [le_div_iffâ‚€' hpos]
    change i â‰¤ (k', n).snd at hi
    exact one_add_le_sup_seminorm_apply le_rfl hi _ _
  have hgrowth' (N : â„•) (hNâ‚ : 1 â‰¤ N) (hNâ‚‚ : N â‰¤ n) :
      â€–iteratedFDeriv â„ N g xâ€– â‰¤ ((C + 1) * (1 + â€–xâ€–) ^ l) ^ N := by
    refine (hgrowth N hNâ‚‚ x).trans ?_
    rw [mul_pow]
    have hNâ‚' := (lt_of_lt_of_le zero_lt_one hNâ‚).ne'
    gcongr
    Â· exact le_trans (by simp) (le_self_powâ‚€ (by simp [hC]) hNâ‚')
    Â· refine le_self_powâ‚€ (one_le_powâ‚€ ?_) hNâ‚'
      simp only [le_add_iff_nonneg_right, norm_nonneg]
  have := norm_iteratedFDeriv_comp_le (f.smooth âŠ¤) hg.1 (mod_cast le_top) x hbound hgrowth'
  have hxk : â€–xâ€– ^ k â‰¤ (1 + â€–xâ€–) ^ k :=
    pow_le_pow_leftâ‚€ (norm_nonneg _) (by simp only [zero_le_one, le_add_iff_nonneg_left]) _
  grw [hxk, this]
  have rearrange :
    (1 + â€–xâ€–) ^ k *
        (n ! * (2 ^ k' * seminorm_f / (1 + â€–g xâ€–) ^ k') * ((C + 1) * (1 + â€–xâ€–) ^ l) ^ n) =
      (1 + â€–xâ€–) ^ (k + l * n) / (1 + â€–g xâ€–) ^ k' *
        ((C + 1) ^ n * n ! * 2 ^ k' * seminorm_f) := by
    rw [mul_pow, pow_add, â† pow_mul]
    ring
  rw [rearrange]
  have hgxk' : 0 < (1 + â€–g xâ€–) ^ k' := by positivity
  rw [â† div_le_iffâ‚€ hgxk'] at hg_upper''
  grw [hg_upper'', â† mul_assoc]

@[simp] lemma compCLM_apply {g : D â†’ E} (hg : g.HasTemperateGrowth)
    (hg_upper : âˆƒ (k : â„•) (C : â„), âˆ€ x, â€–xâ€– â‰¤ C * (1 + â€–g xâ€–) ^ k) (f : ğ“¢(E, F)) :
    compCLM ğ•œ hg hg_upper f = f âˆ˜ g := rfl

/-- Composition with a function on the right is a continuous linear map on Schwartz space
provided that the function is temperate and antilipschitz. -/
def compCLMOfAntilipschitz {K : â„â‰¥0} {g : D â†’ E}
    (hg : g.HasTemperateGrowth) (h'g : AntilipschitzWith K g) :
    ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(D, F) := by
  refine compCLM ğ•œ hg âŸ¨1, K * max 1 â€–g 0â€–, fun x â†¦ ?_âŸ©
  calc
  â€–xâ€– â‰¤ K * â€–g x - g 0â€– := by
    rw [â† dist_zero_right, â† dist_eq_norm]
    apply h'g.le_mul_dist
  _ â‰¤ K * (â€–g xâ€– + â€–g 0â€–) := by
    gcongr
    exact norm_sub_le _ _
  _ â‰¤ K * (â€–g xâ€– + max 1 â€–g 0â€–) := by
    gcongr
    exact le_max_right _ _
  _ â‰¤ (K * max 1 â€–g 0â€– : â„) * (1 + â€–g xâ€–) ^ 1 := by
    simp only [mul_add, add_comm (K * â€–g xâ€–), pow_one, mul_one, add_le_add_iff_left]
    gcongr
    exact le_mul_of_one_le_right (by positivity) (le_max_left _ _)

@[simp] lemma compCLMOfAntilipschitz_apply {K : â„â‰¥0} {g : D â†’ E} (hg : g.HasTemperateGrowth)
    (h'g : AntilipschitzWith K g) (f : ğ“¢(E, F)) :
    compCLMOfAntilipschitz ğ•œ hg h'g f = f âˆ˜ g := rfl

/-- Composition with a continuous linear equiv on the right is a continuous linear map on
Schwartz space. -/
def compCLMOfContinuousLinearEquiv (g : D â‰ƒL[â„] E) :
    ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(D, F) :=
  compCLMOfAntilipschitz ğ•œ (g.toContinuousLinearMap.hasTemperateGrowth) g.antilipschitz

@[simp] lemma compCLMOfContinuousLinearEquiv_apply (g : D â‰ƒL[â„] E) (f : ğ“¢(E, F)) :
    compCLMOfContinuousLinearEquiv ğ•œ g f = f âˆ˜ g := rfl

end Comp

section Derivatives

/-! ### Derivatives of Schwartz functions -/


variable (ğ•œ)
variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The FrÃ©chet derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def fderivCLM : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F) :=
  mkCLM (fderiv â„ Â·) (fun f g _ => fderiv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => fderiv_const_smul f.differentiableAt a)
    (fun f => (contDiff_succ_iff_fderiv.mp (f.smooth âŠ¤)).2.2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [schwartzSeminormFamily_apply, Seminorm.comp_apply, Finset.sup_singleton,
        one_smul, norm_iteratedFDeriv_fderiv, one_mul] using f.le_seminorm ğ•œ k (n + 1) xâŸ©

@[simp]
theorem fderivCLM_apply (f : ğ“¢(E, F)) (x : E) : fderivCLM ğ•œ f x = fderiv â„ f x :=
  rfl

theorem hasFDerivAt (f : ğ“¢(E, F)) (x : E) : HasFDerivAt f (fderiv â„ f x) x :=
  f.differentiableAt.hasFDerivAt

/-- The 1-dimensional derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def derivCLM : ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F) :=
  mkCLM (deriv Â·) (fun f g _ => deriv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => deriv_const_smul a f.differentiableAt)
    (fun f => (contDiff_succ_iff_deriv.mp (f.smooth âŠ¤)).2.2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [Real.norm_eq_abs, Finset.sup_singleton, schwartzSeminormFamily_apply, one_mul,
        norm_iteratedFDeriv_eq_norm_iteratedDeriv, â† iteratedDeriv_succ'] using
        f.le_seminorm' ğ•œ k (n + 1) xâŸ©

@[simp]
theorem derivCLM_apply (f : ğ“¢(â„, F)) (x : â„) : derivCLM ğ•œ f x = deriv f x :=
  rfl

theorem hasDerivAt (f : ğ“¢(â„, F)) (x : â„) : HasDerivAt f (deriv f x) x :=
  f.differentiableAt.hasDerivAt

/-- The partial derivative (or directional derivative) in the direction `m : E` as a
continuous linear map on Schwartz space. -/
def pderivCLM (m : E) : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, F) :=
  (SchwartzMap.evalCLM m).comp (fderivCLM ğ•œ)

@[simp]
theorem pderivCLM_apply (m : E) (f : ğ“¢(E, F)) (x : E) : pderivCLM ğ•œ m f x = fderiv â„ f x m :=
  rfl

theorem pderivCLM_eq_lineDeriv (m : E) (f : ğ“¢(E, F)) (x : E) :
    pderivCLM ğ•œ m f x = lineDeriv â„ f x m := by
  simp only [pderivCLM_apply, f.differentiableAt.lineDeriv_eq_fderiv]

/-- The iterated partial derivative (or directional derivative) as a continuous linear map on
Schwartz space. -/
def iteratedPDeriv {n : â„•} : (Fin n â†’ E) â†’ ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, F) :=
  Nat.recOn n (fun _ => ContinuousLinearMap.id ğ•œ _) fun _ rec x =>
    (pderivCLM ğ•œ (x 0)).comp (rec (Fin.tail x))

@[simp]
theorem iteratedPDeriv_zero (m : Fin 0 â†’ E) (f : ğ“¢(E, F)) : iteratedPDeriv ğ•œ m f = f :=
  rfl

@[simp]
theorem iteratedPDeriv_one (m : Fin 1 â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = pderivCLM ğ•œ (m 0) f :=
  rfl

theorem iteratedPDeriv_succ_left {n : â„•} (m : Fin (n + 1) â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = pderivCLM ğ•œ (m 0) (iteratedPDeriv ğ•œ (Fin.tail m) f) :=
  rfl

theorem iteratedPDeriv_succ_right {n : â„•} (m : Fin (n + 1) â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = iteratedPDeriv ğ•œ (Fin.init m) (pderivCLM ğ•œ (m (Fin.last n)) f) := by
  induction n with
  | zero =>
    rw [iteratedPDeriv_zero, iteratedPDeriv_one, Fin.last_zero]
  -- The proof is `âˆ‚^{n + 2} = âˆ‚ âˆ‚^{n + 1} = âˆ‚ âˆ‚^n âˆ‚ = âˆ‚^{n+1} âˆ‚`
  | succ n IH =>
    have hmzero : Fin.init m 0 = m 0 := by simp only [Fin.init_def, Fin.castSucc_zero]
    have hmtail : Fin.tail m (Fin.last n) = m (Fin.last n.succ) := by
      simp only [Fin.tail_def, Fin.succ_last]
    calc
      _ = pderivCLM ğ•œ (m 0) (iteratedPDeriv ğ•œ _ f) := iteratedPDeriv_succ_left _ _ _
      _ = pderivCLM ğ•œ (m 0) ((iteratedPDeriv ğ•œ _) ((pderivCLM ğ•œ _) f)) := by
        congr 1
        exact IH _
      _ = _ := by
        simp only [hmtail, iteratedPDeriv_succ_left, hmzero, Fin.tail_init_eq_init_tail]

theorem iteratedPDeriv_eq_iteratedFDeriv {n : â„•} {m : Fin n â†’ E} {f : ğ“¢(E, F)} {x : E} :
    iteratedPDeriv ğ•œ m f x = iteratedFDeriv â„ n f x m := by
  induction n generalizing x with
  | zero => simp
  | succ n ih =>
    simp only [iteratedPDeriv_succ_left, iteratedFDeriv_succ_apply_left]
    rw [â† fderiv_continuousMultilinear_apply_const_apply]
    Â· simp [â† ih]
    Â· exact (f.smooth âŠ¤).differentiable_iteratedFDeriv (mod_cast ENat.coe_lt_top n) x


end Derivatives

section Integration

/-! ### Integration -/


open Real Complex Filter MeasureTheory MeasureTheory.Measure Module

variable [RCLike ğ•œ]
variable [NormedAddCommGroup D] [NormedSpace â„ D]
variable [NormedAddCommGroup V] [NormedSpace â„ V] [NormedSpace ğ•œ V]
variable [MeasurableSpace D]

variable {Î¼ : Measure D} [hÎ¼ : HasTemperateGrowth Î¼]

attribute [local instance 101] secondCountableTopologyEither_of_left

variable (ğ•œ Î¼) in
lemma integral_pow_mul_iteratedFDeriv_le (f : ğ“¢(D, V)) (k n : â„•) :
    âˆ« x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– âˆ‚Î¼ â‰¤ 2 ^ Î¼.integrablePower *
      (âˆ« x, (1 + â€–xâ€–) ^ (- (Î¼.integrablePower : â„)) âˆ‚Î¼) *
        (SchwartzMap.seminorm ğ•œ 0 n f + SchwartzMap.seminorm ğ•œ (k + Î¼.integrablePower) n f) :=
  integral_pow_mul_le_of_le_of_pow_mul_le (norm_iteratedFDeriv_le_seminorm â„ _ _)
    (le_seminorm â„ _ _ _)

variable [BorelSpace D] [SecondCountableTopology D]

variable (Î¼) in
lemma integrable_pow_mul_iteratedFDeriv
    (f : ğ“¢(D, V))
    (k n : â„•) : Integrable (fun x â†¦ â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€–) Î¼ :=
  integrable_of_le_of_pow_mul_le (norm_iteratedFDeriv_le_seminorm â„ _ _) (le_seminorm â„ _ _ _)
    ((f.smooth âŠ¤).continuous_iteratedFDeriv (mod_cast le_top)).aestronglyMeasurable

variable (Î¼) in
lemma integrable_pow_mul (f : ğ“¢(D, V))
    (k : â„•) : Integrable (fun x â†¦ â€–xâ€– ^ k * â€–f xâ€–) Î¼ := by
  convert integrable_pow_mul_iteratedFDeriv Î¼ f k 0 with x
  simp

lemma integrable (f : ğ“¢(D, V)) : Integrable f Î¼ :=
  (f.integrable_pow_mul Î¼ 0).mono f.continuous.aestronglyMeasurable
    (Eventually.of_forall (fun _ â†¦ by simp))

variable (ğ•œ Î¼) in
/-- The integral as a continuous linear map from Schwartz space to the codomain. -/
def integralCLM : ğ“¢(D, V) â†’L[ğ•œ] V := by
  refine mkCLMtoNormedSpace (âˆ« x, Â· x âˆ‚Î¼)
    (fun f g â†¦ integral_add f.integrable g.integrable) (integral_smul Â· Â·) ?_
  rcases hÎ¼.exists_integrable with âŸ¨n, hâŸ©
  let m := (n, 0)
  use Finset.Iic m, 2 ^ n * âˆ« x : D, (1 + â€–xâ€–) ^ (- (n : â„)) âˆ‚Î¼
  refine âŸ¨by positivity, fun f â†¦ (norm_integral_le_integral_norm f).trans ?_âŸ©
  have h' : âˆ€ x, â€–f xâ€– â‰¤ (1 + â€–xâ€–) ^ (-(n : â„)) *
      (2 ^ n * ((Finset.Iic m).sup (fun m' => SchwartzMap.seminorm ğ•œ m'.1 m'.2) f)) := by
    intro x
    rw [rpow_neg (by positivity), â† div_eq_inv_mul, le_div_iffâ‚€' (by positivity), rpow_natCast]
    simpa using one_add_le_sup_seminorm_apply (m := m) (k := n) (n := 0) le_rfl le_rfl f x
  apply (integral_mono (by simpa using f.integrable_pow_mul Î¼ 0) _ h').trans
  Â· unfold schwartzSeminormFamily
    rw [integral_mul_const, â† mul_assoc, mul_comm (2 ^ n)]
  apply h.mul_const

variable (ğ•œ) in
@[simp]
lemma integralCLM_apply (f : ğ“¢(D, V)) : integralCLM ğ•œ Î¼ f = âˆ« x, f x âˆ‚Î¼ := by rfl

end Integration

section BoundedContinuousFunction

/-! ### Inclusion into the space of bounded continuous functions -/


open scoped BoundedContinuousFunction

instance instBoundedContinuousMapClass : BoundedContinuousMapClass ğ“¢(E, F) E F where
  __ := instContinuousMapClass
  map_bounded := fun f â†¦ âŸ¨2 * (SchwartzMap.seminorm â„ 0 0) f,
    (BoundedContinuousFunction.dist_le_two_norm' (norm_le_seminorm â„ f))âŸ©

/-- Schwartz functions as bounded continuous functions -/
def toBoundedContinuousFunction (f : ğ“¢(E, F)) : E â†’áµ‡ F :=
  BoundedContinuousFunction.ofNormedAddCommGroup f (SchwartzMap.continuous f)
    (SchwartzMap.seminorm â„ 0 0 f) (norm_le_seminorm â„ f)

@[simp]
theorem toBoundedContinuousFunction_apply (f : ğ“¢(E, F)) (x : E) :
    f.toBoundedContinuousFunction x = f x :=
  rfl

/-- Schwartz functions as continuous functions -/
def toContinuousMap (f : ğ“¢(E, F)) : C(E, F) :=
  f.toBoundedContinuousFunction.toContinuousMap

variable (ğ•œ E F)
variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The inclusion map from Schwartz functions to bounded continuous functions as a continuous linear
map. -/
def toBoundedContinuousFunctionCLM : ğ“¢(E, F) â†’L[ğ•œ] E â†’áµ‡ F :=
  mkCLMtoNormedSpace toBoundedContinuousFunction (by intro f g; ext; exact add_apply)
    (by intro a f; ext; exact smul_apply)
    (âŸ¨{0}, 1, zero_le_one, by
      simpa [BoundedContinuousFunction.norm_le (apply_nonneg _ _)] using norm_le_seminorm ğ•œ âŸ©)

@[simp]
theorem toBoundedContinuousFunctionCLM_apply (f : ğ“¢(E, F)) (x : E) :
    toBoundedContinuousFunctionCLM ğ•œ E F f x = f x :=
  rfl

variable {E}

section DiracDelta

/-- The Dirac delta distribution -/
def delta (x : E) : ğ“¢(E, F) â†’L[ğ•œ] F :=
  (BoundedContinuousFunction.evalCLM ğ•œ x).comp (toBoundedContinuousFunctionCLM ğ•œ E F)

@[simp]
theorem delta_apply (xâ‚€ : E) (f : ğ“¢(E, F)) : delta ğ•œ F xâ‚€ f = f xâ‚€ :=
  rfl

open MeasureTheory MeasureTheory.Measure

variable [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E] [CompleteSpace F]

/-- Integrating against the Dirac measure is equal to the delta distribution. -/
@[simp]
theorem integralCLM_dirac_eq_delta (x : E) : integralCLM ğ•œ (dirac x) = delta ğ•œ F x := by aesop

end DiracDelta

end BoundedContinuousFunction

section ZeroAtInfty

open scoped ZeroAtInfty

variable [ProperSpace E]

instance instZeroAtInftyContinuousMapClass : ZeroAtInftyContinuousMapClass ğ“¢(E, F) E F where
  __ := instContinuousMapClass
  zero_at_infty := by
    intro f
    apply zero_at_infty_of_norm_le
    intro Îµ hÎµ
    use (SchwartzMap.seminorm â„ 1 0) f / Îµ
    intro x hx
    rw [div_lt_iffâ‚€ hÎµ] at hx
    have hxpos : 0 < â€–xâ€– := by
      rw [norm_pos_iff]
      intro hxzero
      simp only [hxzero, norm_zero, zero_mul, â† not_le] at hx
      exact hx (apply_nonneg (SchwartzMap.seminorm â„ 1 0) f)
    have := norm_pow_mul_le_seminorm â„ f 1 x
    rw [pow_one, â† le_div_iffâ‚€' hxpos] at this
    apply lt_of_le_of_lt this
    rwa [div_lt_iffâ‚€' hxpos]

/-- Schwartz functions as continuous functions vanishing at infinity. -/
def toZeroAtInfty (f : ğ“¢(E, F)) : Câ‚€(E, F) where
  toFun := f
  zero_at_infty' := zero_at_infty f

@[simp] theorem toZeroAtInfty_apply (f : ğ“¢(E, F)) (x : E) : f.toZeroAtInfty x = f x :=
  rfl

@[simp] theorem toZeroAtInfty_toBCF (f : ğ“¢(E, F)) :
    f.toZeroAtInfty.toBCF = f.toBoundedContinuousFunction :=
  rfl

variable (ğ•œ E F)
variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The inclusion map from Schwartz functions to continuous functions vanishing at infinity as a
continuous linear map. -/
def toZeroAtInftyCLM : ğ“¢(E, F) â†’L[ğ•œ] Câ‚€(E, F) :=
  mkCLMtoNormedSpace toZeroAtInfty (by intro f g; ext; exact add_apply)
    (by intro a f; ext; exact smul_apply)
    (âŸ¨{0}, 1, zero_le_one, by simpa [â† ZeroAtInftyContinuousMap.norm_toBCF_eq_norm,
      BoundedContinuousFunction.norm_le (apply_nonneg _ _)] using norm_le_seminorm ğ•œ âŸ©)

@[simp] theorem toZeroAtInftyCLM_apply (f : ğ“¢(E, F)) (x : E) : toZeroAtInftyCLM ğ•œ E F f x = f x :=
  rfl

end ZeroAtInfty

section Lp

/-! ### Inclusion into L^p space -/

open MeasureTheory
open scoped NNReal ENNReal

variable [NormedAddCommGroup D] [MeasurableSpace D] [MeasurableSpace E] [OpensMeasurableSpace E]
  [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

variable (ğ•œ F) in
/-- The `L^p` norm of a Schwartz function is controlled by a finite family of Schwartz seminorms.

The maximum index `k` and the constant `C` depend on `p` and `Î¼`.
-/
theorem eLpNorm_le_seminorm (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] :
    âˆƒ (k : â„•) (C : â„â‰¥0), âˆ€ (f : ğ“¢(E, F)), eLpNorm f p Î¼ â‰¤
      C * ENNReal.ofReal ((Finset.Iic (k, 0)).sup (schwartzSeminormFamily ğ•œ E F) f) := by
  -- Apply HÃ¶lder's inequality `â€–fâ€–_p â‰¤ â€–fâ‚â€–_p * â€–fâ‚‚â€–_âˆ` to obtain the `L^p` norm of `f = fâ‚ â€¢ fâ‚‚`
  -- using `fâ‚ = (1 + â€–xâ€–) ^ (-k)` and `fâ‚‚ = (1 + â€–xâ€–) ^ k â€¢ f x`.
  rcases hÎ¼.exists_eLpNorm_lt_top p with âŸ¨k, hkâŸ©
  refine âŸ¨k, (eLpNorm (fun x â†¦ (1 + â€–xâ€–) ^ (-k : â„)) p Î¼).toNNReal * 2 ^ k, fun f â†¦ ?_âŸ©
  have h_one_add (x : E) : 0 < 1 + â€–xâ€– := lt_add_of_pos_of_le zero_lt_one (norm_nonneg x)
  calc eLpNorm (â‡‘f) p Î¼
  _ = eLpNorm ((fun x : E â†¦ (1 + â€–xâ€–) ^ (-k : â„)) â€¢ fun x â†¦ (1 + â€–xâ€–) ^ k â€¢ f x) p Î¼ := by
    refine congrArg (eLpNorm Â· p Î¼) (funext fun x â†¦ ?_)
    simp [(h_one_add x).ne']
  _ â‰¤ eLpNorm (fun x â†¦ (1 + â€–xâ€–) ^ (-k : â„)) p Î¼ * eLpNorm (fun x â†¦ (1 + â€–xâ€–) ^ k â€¢ f x) âŠ¤ Î¼ := by
    refine eLpNorm_smul_le_eLpNorm_mul_eLpNorm_top p _ ?_
    refine Continuous.aestronglyMeasurable ?_
    exact .rpow_const (.add continuous_const continuous_norm) fun x â†¦ .inl (h_one_add x).ne'
  _ â‰¤ eLpNorm (fun x â†¦ (1 + â€–xâ€–) ^ (-k : â„)) p Î¼ *
      (2 ^ k * ENNReal.ofReal (((Finset.Iic (k, 0)).sup (schwartzSeminormFamily ğ•œ E F)) f)) := by
    gcongr
    refine eLpNormEssSup_le_of_ae_nnnorm_bound (ae_of_all Î¼ fun x â†¦ ?_)
    rw [â† norm_toNNReal, Real.toNNReal_le_iff_le_coe]
    simpa [norm_smul, abs_of_nonneg (h_one_add x).le] using
      one_add_le_sup_seminorm_apply (m := (k, 0)) (le_refl k) (le_refl 0) f x
  _ = _ := by
    rw [ENNReal.coe_mul, ENNReal.coe_toNNReal hk.ne]
    simp only [ENNReal.coe_pow, ENNReal.coe_ofNat]
    ring

/-- The `L^p` norm of a Schwartz function is finite. -/
theorem eLpNorm_lt_top (f : ğ“¢(E, F)) (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] : eLpNorm f p Î¼ < âŠ¤ := by
  rcases eLpNorm_le_seminorm â„ F p Î¼ with âŸ¨k, C, hCâŸ©
  exact lt_of_le_of_lt (hC f) (ENNReal.mul_lt_top ENNReal.coe_lt_top ENNReal.ofReal_lt_top)

variable [SecondCountableTopologyEither E F]

/-- Schwartz functions are in `L^âˆ`; does not require `hÎ¼.HasTemperateGrowth`. -/
theorem memLp_top (f : ğ“¢(E, F)) (Î¼ : Measure E := by volume_tac) : MemLp f âŠ¤ Î¼ := by
  rcases f.decay 0 0 with âŸ¨C, _, hCâŸ©
  refine memLp_top_of_bound f.continuous.aestronglyMeasurable C (ae_of_all Î¼ fun x â†¦ ?_)
  simpa using hC x

/-- Schwartz functions are in `L^p` for any `p`. -/
theorem memLp (f : ğ“¢(E, F)) (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] : MemLp f p Î¼ :=
  âŸ¨f.continuous.aestronglyMeasurable, f.eLpNorm_lt_top p Î¼âŸ©

/-- Map a Schwartz function to an `Lp` function for any `p`. -/
def toLp (f : ğ“¢(E, F)) (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth] :
    Lp F p Î¼ := (f.memLp p Î¼).toLp

theorem coeFn_toLp (f : ğ“¢(E, F)) (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] : f.toLp p Î¼ =áµ[Î¼] f := (f.memLp p Î¼).coeFn_toLp

theorem norm_toLp {f : ğ“¢(E, F)} {p : â„â‰¥0âˆ} {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth] :
    â€–f.toLp p Î¼â€– = ENNReal.toReal (eLpNorm f p Î¼) := by
  rw [Lp.norm_def, eLpNorm_congr_ae (coeFn_toLp f p Î¼)]

theorem injective_toLp (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth]
    [Î¼.IsOpenPosMeasure] : Function.Injective (fun f : ğ“¢(E, F) â†¦ f.toLp p Î¼) :=
  fun f g â†¦ by simpa [toLp] using (Continuous.ae_eq_iff_eq Î¼ f.continuous g.continuous).mp

variable (ğ•œ F) in
theorem norm_toLp_le_seminorm (p : â„â‰¥0âˆ) (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] :
    âˆƒ k C, 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(E, F)), â€–f.toLp p Î¼â€– â‰¤
      C * (Finset.Iic (k, 0)).sup (schwartzSeminormFamily ğ•œ E F) f := by
  rcases eLpNorm_le_seminorm ğ•œ F p Î¼ with âŸ¨k, C, hCâŸ©
  refine âŸ¨k, C, C.coe_nonneg, fun f â†¦ ?_âŸ©
  rw [norm_toLp]
  refine ENNReal.toReal_le_of_le_ofReal (by simp [mul_nonneg]) ?_
  rw [ENNReal.ofReal_mul NNReal.zero_le_coe]
  simpa using hC f

variable (ğ•œ F) in
/-- Continuous linear map from Schwartz functions to `L^p`. -/
def toLpCLM (p : â„â‰¥0âˆ) [Fact (1 â‰¤ p)] (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] : ğ“¢(E, F) â†’L[ğ•œ] Lp F p Î¼ :=
  mkCLMtoNormedSpace (fun f â†¦ f.toLp p Î¼) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl) <| by
    rcases norm_toLp_le_seminorm ğ•œ F p Î¼ with âŸ¨k, C, hC_pos, hCâŸ©
    exact âŸ¨Finset.Iic (k, 0), C, hC_pos, hCâŸ©

@[simp] theorem toLpCLM_apply {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
    {f : ğ“¢(E, F)} : toLpCLM ğ•œ F p Î¼ f = f.toLp p Î¼ := rfl

@[fun_prop]
theorem continuous_toLp {p : â„â‰¥0âˆ} [Fact (1 â‰¤ p)] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth] :
    Continuous (fun f : ğ“¢(E, F) â†¦ f.toLp p Î¼) := (toLpCLM â„ F p Î¼).continuous

end Lp

section L2

open MeasureTheory

variable [NormedAddCommGroup H] [NormedSpace â„ H] [FiniteDimensional â„ H]
  [MeasurableSpace H] [BorelSpace H]
  [NormedAddCommGroup V] [InnerProductSpace â„‚ V]

@[simp]
theorem inner_toL2_toL2_eq (f g : ğ“¢(H, V)) (Î¼ : Measure H := by volume_tac) [Î¼.HasTemperateGrowth] :
    inner â„‚ (f.toLp 2 Î¼) (g.toLp 2 Î¼) = âˆ« x, inner â„‚ (f x) (g x) âˆ‚Î¼ := by
  apply integral_congr_ae
  have hf_ae := f.coeFn_toLp 2 Î¼
  have hg_ae := g.coeFn_toLp 2 Î¼
  filter_upwards [hf_ae, hg_ae] with _ hf hg
  rw [hf, hg]

end L2

section integration_by_parts

open ENNReal MeasureTheory

variable [NormedAddCommGroup V] [NormedSpace â„ V]

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for a general bilinear map. -/
theorem integral_bilinear_deriv_right_eq_neg_left (f : ğ“¢(â„, E)) (g : ğ“¢(â„, F))
    (L : E â†’L[â„] F â†’L[â„] V) :
    âˆ« (x : â„), L (f x) (deriv g x) = -âˆ« (x : â„), L (deriv f x) (g x) :=
  MeasureTheory.integral_bilinear_hasDerivAt_right_eq_neg_left_of_integrable
    f.hasDerivAt g.hasDerivAt (bilinLeftCLM L (derivCLM â„ g).hasTemperateGrowth f).integrable
    (bilinLeftCLM L g.hasTemperateGrowth (derivCLM â„ f)).integrable
    (bilinLeftCLM L g.hasTemperateGrowth f).integrable

variable [RCLike ğ•œ] [NormedSpace ğ•œ F] [NormedSpace ğ•œ V]

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for a Schwartz function with values in continuous linear maps. -/
theorem integral_clm_comp_deriv_right_eq_neg_left (f : ğ“¢(â„, F â†’L[ğ•œ] V)) (g : ğ“¢(â„, F)) :
    âˆ« (x : â„), f x (deriv g x) = -âˆ« (x : â„), deriv f x (g x) :=
  integral_bilinear_deriv_right_eq_neg_left f g
    ((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).bilinearRestrictScalars â„)

/-- Integration by parts of Schwartz functions for the 1-dimensional derivative.

Version for multiplication of scalar-valued Schwartz functions. -/
theorem integral_mul_deriv_eq_neg_deriv_mul (f : ğ“¢(â„, ğ•œ)) (g : ğ“¢(â„, ğ•œ)) :
    âˆ« (x : â„), f x * (deriv g x) = -âˆ« (x : â„), deriv f x * (g x) :=
  integral_bilinear_deriv_right_eq_neg_left f g (ContinuousLinearMap.mul â„ ğ•œ)

end integration_by_parts


end SchwartzMap
