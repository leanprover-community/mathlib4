/-
Copyright (c) 2022 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.Calculus.Deriv.Add
import Mathlib.Analysis.Calculus.Deriv.Mul
import Mathlib.Analysis.Calculus.ContDiff.Bounds
import Mathlib.Analysis.Calculus.IteratedDeriv.Defs
import Mathlib.Analysis.LocallyConvex.WithSeminorms
import Mathlib.Topology.Algebra.UniformFilterBasis
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Analysis.SpecialFunctions.Pow.Real

#align_import analysis.schwartz_space from "leanprover-community/mathlib"@"e137999b2c6f2be388f4cd3bbf8523de1910cd2b"

/-!
# Schwartz space

This file defines the Schwartz space. Usually, the Schwartz space is defined as the set of smooth
functions $f : â„^n â†’ â„‚$ such that there exists $C_{Î±Î²} > 0$ with $$|x^Î± âˆ‚^Î² f(x)| < C_{Î±Î²}$$ for
all $x âˆˆ â„^n$ and for all multiindices $Î±, Î²$.
In mathlib, we use a slightly different approach and define the Schwartz space as all
smooth functions `f : E â†’ F`, where `E` and `F` are real normed vector spaces such that for all
natural numbers `k` and `n` we have uniform bounds `â€–xâ€–^k * â€–iteratedFDeriv â„ n f xâ€– < C`.
This approach completely avoids using partial derivatives as well as polynomials.
We construct the topology on the Schwartz space by a family of seminorms, which are the best
constants in the above estimates. The abstract theory of topological vector spaces developed in
`SeminormFamily.moduleFilterBasis` and `WithSeminorms.toLocallyConvexSpace` turns the
Schwartz space into a locally convex topological vector space.

## Main definitions

* `SchwartzMap`: The Schwartz space is the space of smooth functions such that all derivatives
decay faster than any power of `â€–xâ€–`.
* `SchwartzMap.seminorm`: The family of seminorms as described above
* `SchwartzMap.fderivCLM`: The differential as a continuous linear map
`ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F)`
* `SchwartzMap.derivCLM`: The one-dimensional derivative as a continuous linear map
`ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F)`

## Main statements

* `SchwartzMap.instUniformAddGroup` and `SchwartzMap.instLocallyConvexSpace`: The Schwartz space
is a locally convex topological vector space.
* `SchwartzMap.one_add_le_sup_seminorm_apply`: For a Schwartz function `f` there is a uniform bound
on `(1 + â€–xâ€–) ^ k * â€–iteratedFDeriv â„ n f xâ€–`.

## Implementation details

The implementation of the seminorms is taken almost literally from `ContinuousLinearMap.opNorm`.

## Notation

* `ğ“¢(E, F)`: The Schwartz space `SchwartzMap E F` localized in `SchwartzSpace`

## Tags

Schwartz space, tempered distributions
-/

noncomputable section

open scoped BigOperators Nat

variable {ğ•œ ğ•œ' D E F G : Type*}

variable [NormedAddCommGroup E] [NormedSpace â„ E]

variable [NormedAddCommGroup F] [NormedSpace â„ F]

variable (E F)

/-- A function is a Schwartz function if it is smooth and all derivatives decay faster than
  any power of `â€–xâ€–`. -/
structure SchwartzMap where
  toFun : E â†’ F
  smooth' : ContDiff â„ âŠ¤ toFun
  decay' : âˆ€ k n : â„•, âˆƒ C : â„, âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n toFun xâ€– â‰¤ C
#align schwartz_map SchwartzMap

-- mathport name: Â«exprğ“¢( , )Â»
scoped[SchwartzSpace] notation "ğ“¢(" E ", " F ")" => SchwartzMap E F

variable {E F}

namespace SchwartzMap

open SchwartzSpace

-- porting note: removed
-- instance : Coe ğ“¢(E, F) (E â†’ F) := âŸ¨toFunâŸ©

instance instFunLike : FunLike ğ“¢(E, F) E F where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
#align schwartz_map.fun_like SchwartzMap.instFunLike

/-- Helper instance for when there's too many metavariables to apply `DFunLike.hasCoeToFun`. -/
instance instCoeFun : CoeFun ğ“¢(E, F) fun _ => E â†’ F :=
  DFunLike.hasCoeToFun
#align schwartz_map.has_coe_to_fun SchwartzMap.instCoeFun

/-- All derivatives of a Schwartz function are rapidly decaying. -/
theorem decay (f : ğ“¢(E, F)) (k n : â„•) :
    âˆƒ C : â„, 0 < C âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ C := by
  rcases f.decay' k n with âŸ¨C, hCâŸ©
  exact âŸ¨max C 1, by positivity, fun x => (hC x).trans (le_max_left _ _)âŸ©
#align schwartz_map.decay SchwartzMap.decay

/-- Every Schwartz function is smooth. -/
theorem smooth (f : ğ“¢(E, F)) (n : â„•âˆ) : ContDiff â„ n f :=
  f.smooth'.of_le le_top
#align schwartz_map.smooth SchwartzMap.smooth

/-- Every Schwartz function is continuous. -/
@[continuity]
protected theorem continuous (f : ğ“¢(E, F)) : Continuous f :=
  (f.smooth 0).continuous
#align schwartz_map.continuous SchwartzMap.continuous

/-- Every Schwartz function is differentiable. -/
protected theorem differentiable (f : ğ“¢(E, F)) : Differentiable â„ f :=
  (f.smooth 1).differentiable rfl.le
#align schwartz_map.differentiable SchwartzMap.differentiable

/-- Every Schwartz function is differentiable at any point. -/
protected theorem differentiableAt (f : ğ“¢(E, F)) {x : E} : DifferentiableAt â„ f x :=
  f.differentiable.differentiableAt
#align schwartz_map.differentiable_at SchwartzMap.differentiableAt

@[ext]
theorem ext {f g : ğ“¢(E, F)} (h : âˆ€ x, (f : E â†’ F) x = g x) : f = g :=
  DFunLike.ext f g h
#align schwartz_map.ext SchwartzMap.ext

section IsBigO

open Asymptotics Filter

variable (f : ğ“¢(E, F))

/-- Auxiliary lemma, used in proving the more general result `isBigO_cocompact_rpow`. -/
theorem isBigO_cocompact_zpow_neg_nat (k : â„•) :
    f =O[cocompact E] fun x => â€–xâ€– ^ (-k : â„¤) := by
  obtain âŸ¨d, _, hd'âŸ© := f.decay k 0
  simp only [norm_iteratedFDeriv_zero] at hd'
  simp_rw [Asymptotics.IsBigO, Asymptotics.IsBigOWith]
  refine' âŸ¨d, Filter.Eventually.filter_mono Filter.cocompact_le_cofinite _âŸ©
  refine' (Filter.eventually_cofinite_ne 0).mono fun x hx => _
  rw [Real.norm_of_nonneg (zpow_nonneg (norm_nonneg _) _), zpow_neg, â† div_eq_mul_inv, le_div_iff']
  exacts [hd' x, zpow_pos_of_pos (norm_pos_iff.mpr hx) _]
set_option linter.uppercaseLean3 false in
#align schwartz_map.is_O_cocompact_zpow_neg_nat SchwartzMap.isBigO_cocompact_zpow_neg_nat

theorem isBigO_cocompact_rpow [ProperSpace E] (s : â„) :
    f =O[cocompact E] fun x => â€–xâ€– ^ s := by
  let k := âŒˆ-sâŒ‰â‚Š
  have hk : -(k : â„) â‰¤ s := neg_le.mp (Nat.le_ceil (-s))
  refine' (isBigO_cocompact_zpow_neg_nat f k).trans _
  suffices (fun x : â„ => x ^ (-k : â„¤)) =O[atTop] fun x : â„ => x ^ s
    from this.comp_tendsto tendsto_norm_cocompact_atTop
  simp_rw [Asymptotics.IsBigO, Asymptotics.IsBigOWith]
  refine' âŸ¨1, (Filter.eventually_ge_atTop 1).mono fun x hx => _âŸ©
  rw [one_mul, Real.norm_of_nonneg (Real.rpow_nonneg (zero_le_one.trans hx) _),
    Real.norm_of_nonneg (zpow_nonneg (zero_le_one.trans hx) _), â† Real.rpow_int_cast, Int.cast_neg,
    Int.cast_ofNat]
  exact Real.rpow_le_rpow_of_exponent_le hx hk
set_option linter.uppercaseLean3 false in
#align schwartz_map.is_O_cocompact_rpow SchwartzMap.isBigO_cocompact_rpow

theorem isBigO_cocompact_zpow [ProperSpace E] (k : â„¤) :
    f =O[cocompact E] fun x => â€–xâ€– ^ k := by
  simpa only [Real.rpow_int_cast] using isBigO_cocompact_rpow f k
set_option linter.uppercaseLean3 false in
#align schwartz_map.is_O_cocompact_zpow SchwartzMap.isBigO_cocompact_zpow

end IsBigO

section Aux

theorem bounds_nonempty (k n : â„•) (f : ğ“¢(E, F)) :
    âˆƒ c : â„, c âˆˆ { c : â„ | 0 â‰¤ c âˆ§ âˆ€ x : E, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c } :=
  let âŸ¨M, hMp, hMbâŸ© := f.decay k n
  âŸ¨M, le_of_lt hMp, hMbâŸ©
#align schwartz_map.bounds_nonempty SchwartzMap.bounds_nonempty

theorem bounds_bddBelow (k n : â„•) (f : ğ“¢(E, F)) :
    BddBelow { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c } :=
  âŸ¨0, fun _ âŸ¨hn, _âŸ© => hnâŸ©
#align schwartz_map.bounds_bdd_below SchwartzMap.bounds_bddBelow

theorem decay_add_le_aux (k n : â„•) (f g : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n ((f : E â†’ F) + (g : E â†’ F)) xâ€– â‰¤
      â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– + â€–xâ€– ^ k * â€–iteratedFDeriv â„ n g xâ€– := by
  rw [â† mul_add]
  refine' mul_le_mul_of_nonneg_left _ (by positivity)
  rw [iteratedFDeriv_add_apply (f.smooth _) (g.smooth _)]
  exact norm_add_le _ _
#align schwartz_map.decay_add_le_aux SchwartzMap.decay_add_le_aux

theorem decay_neg_aux (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (-f : E â†’ F) xâ€– = â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– := by
  rw [iteratedFDeriv_neg_apply, norm_neg]
#align schwartz_map.decay_neg_aux SchwartzMap.decay_neg_aux

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

theorem decay_smul_aux (k n : â„•) (f : ğ“¢(E, F)) (c : ğ•œ) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (c â€¢ (f : E â†’ F)) xâ€– =
      â€–câ€– * â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– := by
  rw [mul_comm â€–câ€–, mul_assoc, iteratedFDeriv_const_smul_apply (f.smooth _),
    norm_smul c (iteratedFDeriv â„ n (â‡‘f) x)]
#align schwartz_map.decay_smul_aux SchwartzMap.decay_smul_aux

end Aux

section SeminormAux

/-- Helper definition for the seminorms of the Schwartz space. -/
protected def seminormAux (k n : â„•) (f : ğ“¢(E, F)) : â„ :=
  sInf { c | 0 â‰¤ c âˆ§ âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ c }
#align schwartz_map.seminorm_aux SchwartzMap.seminormAux

theorem seminormAux_nonneg (k n : â„•) (f : ğ“¢(E, F)) : 0 â‰¤ f.seminormAux k n :=
  le_csInf (bounds_nonempty k n f) fun _ âŸ¨hx, _âŸ© => hx
#align schwartz_map.seminorm_aux_nonneg SchwartzMap.seminormAux_nonneg

theorem le_seminormAux (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n (â‡‘f) xâ€– â‰¤ f.seminormAux k n :=
  le_csInf (bounds_nonempty k n f) fun _ âŸ¨_, hâŸ© => h x
#align schwartz_map.le_seminorm_aux SchwartzMap.le_seminormAux

/-- If one controls the norm of every `A x`, then one controls the norm of `A`. -/
theorem seminormAux_le_bound (k n : â„•) (f : ğ“¢(E, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ M) : f.seminormAux k n â‰¤ M :=
  csInf_le (bounds_bddBelow k n f) âŸ¨hMp, hMâŸ©
#align schwartz_map.seminorm_aux_le_bound SchwartzMap.seminormAux_le_bound

end SeminormAux

/-! ### Algebraic properties -/

section SMul

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F] [NormedField ğ•œ'] [NormedSpace ğ•œ' F]
  [SMulCommClass â„ ğ•œ' F]

instance instSMul : SMul ğ•œ ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' := fun k n => by
        refine' âŸ¨f.seminormAux k n * (â€–câ€– + 1), fun x => _âŸ©
        have hc : 0 â‰¤ â€–câ€– := by positivity
        refine' le_trans _ ((mul_le_mul_of_nonneg_right (f.le_seminormAux k n x) hc).trans _)
        Â· apply Eq.le
          rw [mul_comm _ â€–câ€–, â† mul_assoc]
          exact decay_smul_aux k n f c x
        Â· apply mul_le_mul_of_nonneg_left _ (f.seminormAux_nonneg k n)
          linarith }âŸ©
#align schwartz_map.has_smul SchwartzMap.instSMul

@[simp]
theorem smul_apply {f : ğ“¢(E, F)} {c : ğ•œ} {x : E} : (c â€¢ f) x = c â€¢ f x :=
  rfl
#align schwartz_map.smul_apply SchwartzMap.smul_apply

instance instIsScalarTower [SMul ğ•œ ğ•œ'] [IsScalarTower ğ•œ ğ•œ' F] : IsScalarTower ğ•œ ğ•œ' ğ“¢(E, F) :=
  âŸ¨fun a b f => ext fun x => smul_assoc a b (f x)âŸ©
#align schwartz_map.is_scalar_tower SchwartzMap.instIsScalarTower

instance instSMulCommClass [SMulCommClass ğ•œ ğ•œ' F] : SMulCommClass ğ•œ ğ•œ' ğ“¢(E, F) :=
  âŸ¨fun a b f => ext fun x => smul_comm a b (f x)âŸ©
#align schwartz_map.smul_comm_class SchwartzMap.instSMulCommClass

theorem seminormAux_smul_le (k n : â„•) (c : ğ•œ) (f : ğ“¢(E, F)) :
    (c â€¢ f).seminormAux k n â‰¤ â€–câ€– * f.seminormAux k n := by
  refine'
    (c â€¢ f).seminormAux_le_bound k n (mul_nonneg (norm_nonneg _) (seminormAux_nonneg _ _ _))
      fun x => (decay_smul_aux k n f c x).le.trans _
  rw [mul_assoc]
  exact mul_le_mul_of_nonneg_left (f.le_seminormAux k n x) (norm_nonneg _)
#align schwartz_map.seminorm_aux_smul_le SchwartzMap.seminormAux_smul_le

instance instNSMul : SMul â„• ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' := by
        have : c â€¢ (f : E â†’ F) = (c : â„) â€¢ f := by
          ext x
          simp only [Pi.smul_apply, smul_apply]
          exact nsmul_eq_smul_cast _ _ _
        simp only [this]
        exact ((c : â„) â€¢ f).decay' }âŸ©
#align schwartz_map.has_nsmul SchwartzMap.instNSMul

instance instZSMul : SMul â„¤ ğ“¢(E, F) :=
  âŸ¨fun c f =>
    { toFun := c â€¢ (f : E â†’ F)
      smooth' := (f.smooth _).const_smul c
      decay' := by
        have : c â€¢ (f : E â†’ F) = (c : â„) â€¢ f := by
          ext x
          simp only [Pi.smul_apply, smul_apply]
          exact zsmul_eq_smul_cast _ _ _
        simp only [this]
        exact ((c : â„) â€¢ f).decay' }âŸ©
#align schwartz_map.has_zsmul SchwartzMap.instZSMul

end SMul

section Zero

instance instZero : Zero ğ“¢(E, F) :=
  âŸ¨{  toFun := fun _ => 0
      smooth' := contDiff_const
      decay' := fun _ _ => âŸ¨1, fun _ => by simpâŸ© }âŸ©
#align schwartz_map.has_zero SchwartzMap.instZero

instance instInhabited : Inhabited ğ“¢(E, F) :=
  âŸ¨0âŸ©
#align schwartz_map.inhabited SchwartzMap.instInhabited

theorem coe_zero : DFunLike.coe (0 : ğ“¢(E, F)) = (0 : E â†’ F) :=
  rfl
#align schwartz_map.coe_zero SchwartzMap.coe_zero

@[simp]
theorem coeFn_zero : â‡‘(0 : ğ“¢(E, F)) = (0 : E â†’ F) :=
  rfl
#align schwartz_map.coe_fn_zero SchwartzMap.coeFn_zero

@[simp]
theorem zero_apply {x : E} : (0 : ğ“¢(E, F)) x = 0 :=
  rfl
#align schwartz_map.zero_apply SchwartzMap.zero_apply

theorem seminormAux_zero (k n : â„•) : (0 : ğ“¢(E, F)).seminormAux k n = 0 :=
  le_antisymm (seminormAux_le_bound k n _ rfl.le fun _ => by simp [Pi.zero_def])
    (seminormAux_nonneg _ _ _)
#align schwartz_map.seminorm_aux_zero SchwartzMap.seminormAux_zero

end Zero

section Neg

instance instNeg : Neg ğ“¢(E, F) :=
  âŸ¨fun f =>
    âŸ¨-f, (f.smooth _).neg, fun k n =>
      âŸ¨f.seminormAux k n, fun x => (decay_neg_aux k n f x).le.trans (f.le_seminormAux k n x)âŸ©âŸ©âŸ©
#align schwartz_map.has_neg SchwartzMap.instNeg

end Neg

section Add

instance instAdd : Add ğ“¢(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f + g, (f.smooth _).add (g.smooth _), fun k n =>
      âŸ¨f.seminormAux k n + g.seminormAux k n, fun x =>
        (decay_add_le_aux k n f g x).trans
          (add_le_add (f.le_seminormAux k n x) (g.le_seminormAux k n x))âŸ©âŸ©âŸ©
#align schwartz_map.has_add SchwartzMap.instAdd

@[simp]
theorem add_apply {f g : ğ“¢(E, F)} {x : E} : (f + g) x = f x + g x :=
  rfl
#align schwartz_map.add_apply SchwartzMap.add_apply

theorem seminormAux_add_le (k n : â„•) (f g : ğ“¢(E, F)) :
    (f + g).seminormAux k n â‰¤ f.seminormAux k n + g.seminormAux k n :=
  (f + g).seminormAux_le_bound k n
    (add_nonneg (seminormAux_nonneg _ _ _) (seminormAux_nonneg _ _ _)) fun x =>
    (decay_add_le_aux k n f g x).trans <|
      add_le_add (f.le_seminormAux k n x) (g.le_seminormAux k n x)
#align schwartz_map.seminorm_aux_add_le SchwartzMap.seminormAux_add_le

end Add

section Sub

instance instSub : Sub ğ“¢(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f - g, (f.smooth _).sub (g.smooth _), by
      intro k n
      refine' âŸ¨f.seminormAux k n + g.seminormAux k n, fun x => _âŸ©
      refine' le_trans _ (add_le_add (f.le_seminormAux k n x) (g.le_seminormAux k n x))
      rw [sub_eq_add_neg]
      rw [â† decay_neg_aux k n g x]
      convert decay_add_le_aux k n f (-g) xâŸ©âŸ©
#align schwartz_map.has_sub SchwartzMap.instSub

-- exact fails with deterministic timeout
@[simp]
theorem sub_apply {f g : ğ“¢(E, F)} {x : E} : (f - g) x = f x - g x :=
  rfl
#align schwartz_map.sub_apply SchwartzMap.sub_apply

end Sub

section AddCommGroup

instance instAddCommGroup : AddCommGroup ğ“¢(E, F) :=
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl
#align schwartz_map.add_comm_group SchwartzMap.instAddCommGroup

variable (E F)

/-- Coercion as an additive homomorphism. -/
def coeHom : ğ“¢(E, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl
#align schwartz_map.coe_hom SchwartzMap.coeHom

variable {E F}

theorem coe_coeHom : (coeHom E F : ğ“¢(E, F) â†’ E â†’ F) = DFunLike.coe :=
  rfl
#align schwartz_map.coe_coe_hom SchwartzMap.coe_coeHom

theorem coeHom_injective : Function.Injective (coeHom E F) := by
  rw [coe_coeHom]
  exact DFunLike.coe_injective
#align schwartz_map.coe_hom_injective SchwartzMap.coeHom_injective

end AddCommGroup

section Module

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

instance instModule : Module ğ•œ ğ“¢(E, F) :=
  coeHom_injective.module ğ•œ (coeHom E F) fun _ _ => rfl
#align schwartz_map.module SchwartzMap.instModule

end Module

section Seminorms

/-! ### Seminorms on Schwartz space-/


variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

variable (ğ•œ)

/-- The seminorms of the Schwartz space given by the best constants in the definition of
`ğ“¢(E, F)`. -/
protected def seminorm (k n : â„•) : Seminorm ğ•œ ğ“¢(E, F) :=
  Seminorm.ofSMulLE (SchwartzMap.seminormAux k n) (seminormAux_zero k n) (seminormAux_add_le k n)
    (seminormAux_smul_le k n)
#align schwartz_map.seminorm SchwartzMap.seminorm

/-- If one controls the seminorm for every `x`, then one controls the seminorm. -/
theorem seminorm_le_bound (k n : â„•) (f : ğ“¢(E, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ M) : SchwartzMap.seminorm ğ•œ k n f â‰¤ M :=
  f.seminormAux_le_bound k n hMp hM
#align schwartz_map.seminorm_le_bound SchwartzMap.seminorm_le_bound

/-- If one controls the seminorm for every `x`, then one controls the seminorm.

Variant for functions `ğ“¢(â„, F)`. -/
theorem seminorm_le_bound' (k n : â„•) (f : ğ“¢(â„, F)) {M : â„} (hMp : 0 â‰¤ M)
    (hM : âˆ€ x, |x| ^ k * â€–iteratedDeriv n f xâ€– â‰¤ M) : SchwartzMap.seminorm ğ•œ k n f â‰¤ M := by
  refine' seminorm_le_bound ğ•œ k n f hMp _
  simpa only [Real.norm_eq_abs, norm_iteratedFDeriv_eq_norm_iteratedDeriv]
#align schwartz_map.seminorm_le_bound' SchwartzMap.seminorm_le_bound'

/-- The seminorm controls the Schwartz estimate for any fixed `x`. -/
theorem le_seminorm (k n : â„•) (f : ğ“¢(E, F)) (x : E) :
    â€–xâ€– ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤ SchwartzMap.seminorm ğ•œ k n f :=
  f.le_seminormAux k n x
#align schwartz_map.le_seminorm SchwartzMap.le_seminorm

/-- The seminorm controls the Schwartz estimate for any fixed `x`.

Variant for functions `ğ“¢(â„, F)`. -/
theorem le_seminorm' (k n : â„•) (f : ğ“¢(â„, F)) (x : â„) :
    |x| ^ k * â€–iteratedDeriv n f xâ€– â‰¤ SchwartzMap.seminorm ğ•œ k n f := by
  have := le_seminorm ğ•œ k n f x
  rwa [â† Real.norm_eq_abs, â† norm_iteratedFDeriv_eq_norm_iteratedDeriv]
#align schwartz_map.le_seminorm' SchwartzMap.le_seminorm'

theorem norm_iteratedFDeriv_le_seminorm (f : ğ“¢(E, F)) (n : â„•) (xâ‚€ : E) :
    â€–iteratedFDeriv â„ n f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ 0 n) f := by
  have := SchwartzMap.le_seminorm ğ•œ 0 n f xâ‚€
  rwa [pow_zero, one_mul] at this
#align schwartz_map.norm_iterated_fderiv_le_seminorm SchwartzMap.norm_iteratedFDeriv_le_seminorm

theorem norm_pow_mul_le_seminorm (f : ğ“¢(E, F)) (k : â„•) (xâ‚€ : E) :
    â€–xâ‚€â€– ^ k * â€–f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ k 0) f := by
  have := SchwartzMap.le_seminorm ğ•œ k 0 f xâ‚€
  rwa [norm_iteratedFDeriv_zero] at this
#align schwartz_map.norm_pow_mul_le_seminorm SchwartzMap.norm_pow_mul_le_seminorm

theorem norm_le_seminorm (f : ğ“¢(E, F)) (xâ‚€ : E) : â€–f xâ‚€â€– â‰¤ (SchwartzMap.seminorm ğ•œ 0 0) f := by
  have := norm_pow_mul_le_seminorm ğ•œ f 0 xâ‚€
  rwa [pow_zero, one_mul] at this
#align schwartz_map.norm_le_seminorm SchwartzMap.norm_le_seminorm

variable (E F)

/-- The family of Schwartz seminorms. -/
def _root_.schwartzSeminormFamily : SeminormFamily ğ•œ ğ“¢(E, F) (â„• Ã— â„•) :=
  fun m => SchwartzMap.seminorm ğ•œ m.1 m.2
#align schwartz_seminorm_family schwartzSeminormFamily

@[simp]
theorem schwartzSeminormFamily_apply (n k : â„•) :
    schwartzSeminormFamily ğ•œ E F (n, k) = SchwartzMap.seminorm ğ•œ n k :=
  rfl
#align schwartz_map.schwartz_seminorm_family_apply SchwartzMap.schwartzSeminormFamily_apply

@[simp]
theorem schwartzSeminormFamily_apply_zero :
    schwartzSeminormFamily ğ•œ E F 0 = SchwartzMap.seminorm ğ•œ 0 0 :=
  rfl
#align schwartz_map.schwartz_seminorm_family_apply_zero SchwartzMap.schwartzSeminormFamily_apply_zero

variable {ğ•œ E F}

/-- A more convenient version of `le_sup_seminorm_apply`.

The set `Finset.Iic m` is the set of all pairs `(k', n')` with `k' â‰¤ m.1` and `n' â‰¤ m.2`.
Note that the constant is far from optimal. -/
theorem one_add_le_sup_seminorm_apply {m : â„• Ã— â„•} {k n : â„•} (hk : k â‰¤ m.1) (hn : n â‰¤ m.2)
    (f : ğ“¢(E, F)) (x : E) :
    (1 + â€–xâ€–) ^ k * â€–iteratedFDeriv â„ n f xâ€– â‰¤
      2 ^ m.1 * (Finset.Iic m).sup (fun m => SchwartzMap.seminorm ğ•œ m.1 m.2) f := by
  rw [add_comm, add_pow]
  simp only [one_pow, mul_one, Finset.sum_congr, Finset.sum_mul]
  norm_cast
  rw [â† Nat.sum_range_choose m.1]
  push_cast
  rw [Finset.sum_mul]
  have hk' : Finset.range (k + 1) âŠ† Finset.range (m.1 + 1) := by
    rwa [Finset.range_subset, add_le_add_iff_right]
  refine' le_trans (Finset.sum_le_sum_of_subset_of_nonneg hk' fun _ _ _ => by positivity) _
  refine' Finset.sum_le_sum fun i hi => _
  rw [mul_comm (â€–xâ€– ^ i), mul_assoc]
  refine' mul_le_mul _ _ (by positivity) (by positivity)
  Â· exact mod_cast Nat.choose_le_choose i hk
  Â· trans
    Â· exact le_seminorm ğ•œ i n f x
    Â· apply Seminorm.le_def.1
      exact
        Finset.le_sup_of_le (Finset.mem_Iic.2 <|
          Prod.mk_le_mk.2 âŸ¨Finset.mem_range_succ_iff.mp hi, hnâŸ©) le_rfl
#align schwartz_map.one_add_le_sup_seminorm_apply SchwartzMap.one_add_le_sup_seminorm_apply

end Seminorms

section Topology

/-! ### The topology on the Schwartz space-/


variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

variable (ğ•œ E F)

instance instTopologicalSpace : TopologicalSpace ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).moduleFilterBasis.topology'
#align schwartz_map.topological_space SchwartzMap.instTopologicalSpace

theorem _root_.schwartz_withSeminorms : WithSeminorms (schwartzSeminormFamily ğ•œ E F) := by
  have A : WithSeminorms (schwartzSeminormFamily â„ E F) := âŸ¨rflâŸ©
  rw [SeminormFamily.withSeminorms_iff_nhds_eq_iInf] at A âŠ¢
  rw [A]
  rfl
#align schwartz_with_seminorms schwartz_withSeminorms

variable {ğ•œ E F}

instance instContinuousSMul : ContinuousSMul ğ•œ ğ“¢(E, F) := by
  rw [(schwartz_withSeminorms ğ•œ E F).withSeminorms_eq]
  exact (schwartzSeminormFamily ğ•œ E F).moduleFilterBasis.continuousSMul
#align schwartz_map.has_continuous_smul SchwartzMap.instContinuousSMul

instance instTopologicalAddGroup : TopologicalAddGroup ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.isTopologicalAddGroup
#align schwartz_map.topological_add_group SchwartzMap.instTopologicalAddGroup

instance instUniformSpace : UniformSpace ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.uniformSpace
#align schwartz_map.uniform_space SchwartzMap.instUniformSpace

instance instUniformAddGroup : UniformAddGroup ğ“¢(E, F) :=
  (schwartzSeminormFamily â„ E F).addGroupFilterBasis.uniformAddGroup
#align schwartz_map.uniform_add_group SchwartzMap.instUniformAddGroup

instance instLocallyConvexSpace : LocallyConvexSpace â„ ğ“¢(E, F) :=
  (schwartz_withSeminorms â„ E F).toLocallyConvexSpace
#align schwartz_map.locally_convex_space SchwartzMap.instLocallyConvexSpace

instance instFirstCountableTopology : FirstCountableTopology ğ“¢(E, F) :=
  (schwartz_withSeminorms â„ E F).first_countable
#align schwartz_map.topological_space.first_countable_topology SchwartzMap.instFirstCountableTopology

end Topology

section TemperateGrowth

/-! ### Functions of temperate growth -/


/-- A function is called of temperate growth if it is smooth and all iterated derivatives are
polynomially bounded. -/
def _root_.Function.HasTemperateGrowth (f : E â†’ F) : Prop :=
  ContDiff â„ âŠ¤ f âˆ§ âˆ€ n : â„•, âˆƒ (k : â„•) (C : â„), âˆ€ x, â€–iteratedFDeriv â„ n f xâ€– â‰¤ C * (1 + â€–xâ€–) ^ k
#align function.has_temperate_growth Function.HasTemperateGrowth

theorem _root_.Function.HasTemperateGrowth.norm_iteratedFDeriv_le_uniform_aux {f : E â†’ F}
    (hf_temperate : f.HasTemperateGrowth) (n : â„•) :
    âˆƒ (k : â„•) (C : â„), 0 â‰¤ C âˆ§ âˆ€ N â‰¤ n, âˆ€ x : E, â€–iteratedFDeriv â„ N f xâ€– â‰¤ C * (1 + â€–xâ€–) ^ k := by
  choose k C f using hf_temperate.2
  use (Finset.range (n + 1)).sup k
  let C' := max (0 : â„) ((Finset.range (n + 1)).sup' (by simp) C)
  have hC' : 0 â‰¤ C' := by simp only [le_refl, Finset.le_sup'_iff, true_or_iff, le_max_iff]
  use C', hC'
  intro N hN x
  rw [â† Finset.mem_range_succ_iff] at hN
  refine' le_trans (f N x) (mul_le_mul _ _ (by positivity) hC')
  Â· simp only [Finset.le_sup'_iff, le_max_iff]
    right
    exact âŸ¨N, hN, rfl.leâŸ©
  refine' pow_le_pow_right (by simp only [le_add_iff_nonneg_right, norm_nonneg]) _
  exact Finset.le_sup hN
#align function.has_temperate_growth.norm_iterated_fderiv_le_uniform_aux Function.HasTemperateGrowth.norm_iteratedFDeriv_le_uniform_aux

end TemperateGrowth

section CLM

/-! ### Construction of continuous linear maps between Schwartz spaces -/


variable [NormedField ğ•œ] [NormedField ğ•œ']

variable [NormedAddCommGroup D] [NormedSpace â„ D]

variable [NormedSpace ğ•œ E] [SMulCommClass â„ ğ•œ E]

variable [NormedAddCommGroup G] [NormedSpace â„ G] [NormedSpace ğ•œ' G] [SMulCommClass â„ ğ•œ' G]

variable {Ïƒ : ğ•œ â†’+* ğ•œ'}

/-- Create a semilinear map between Schwartz spaces.

Note: This is a helper definition for `mkCLM`. -/
def mkLM (A : (D â†’ E) â†’ F â†’ G) (hadd : âˆ€ (f g : ğ“¢(D, E)) (x), A (f + g) x = A f x + A g x)
    (hsmul : âˆ€ (a : ğ•œ) (f : ğ“¢(D, E)) (x), A (a â€¢ f) x = Ïƒ a â€¢ A f x)
    (hsmooth : âˆ€ f : ğ“¢(D, E), ContDiff â„ âŠ¤ (A f))
    (hbound : âˆ€ n : â„• Ã— â„•, âˆƒ (s : Finset (â„• Ã— â„•)) (C : â„), 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(D, E)) (x : F),
      â€–xâ€– ^ n.fst * â€–iteratedFDeriv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartzSeminormFamily ğ•œ D E) f) :
    ğ“¢(D, E) â†’â‚›â‚—[Ïƒ] ğ“¢(F, G) where
  toFun f :=
    { toFun := A f
      smooth' := hsmooth f
      decay' := by
        intro k n
        rcases hbound âŸ¨k, nâŸ© with âŸ¨s, C, _, hâŸ©
        exact âŸ¨C * (s.sup (schwartzSeminormFamily ğ•œ D E)) f, h fâŸ© }
  map_add' f g := ext (hadd f g)
  map_smul' a f := ext (hsmul a f)
#align schwartz_map.mk_lm SchwartzMap.mkLM

/-- Create a continuous semilinear map between Schwartz spaces.

For an example of using this definition, see `fderivCLM`. -/
def mkCLM [RingHomIsometric Ïƒ] (A : (D â†’ E) â†’ F â†’ G)
    (hadd : âˆ€ (f g : ğ“¢(D, E)) (x), A (f + g) x = A f x + A g x)
    (hsmul : âˆ€ (a : ğ•œ) (f : ğ“¢(D, E)) (x), A (a â€¢ f) x = Ïƒ a â€¢ A f x)
    (hsmooth : âˆ€ f : ğ“¢(D, E), ContDiff â„ âŠ¤ (A f))
    (hbound : âˆ€ n : â„• Ã— â„•, âˆƒ (s : Finset (â„• Ã— â„•)) (C : â„), 0 â‰¤ C âˆ§ âˆ€ (f : ğ“¢(D, E)) (x : F),
      â€–xâ€– ^ n.fst * â€–iteratedFDeriv â„ n.snd (A f) xâ€– â‰¤ C * s.sup (schwartzSeminormFamily ğ•œ D E) f) :
    ğ“¢(D, E) â†’SL[Ïƒ] ğ“¢(F, G) where
  cont := by
    change Continuous (mkLM A hadd hsmul hsmooth hbound : ğ“¢(D, E) â†’â‚›â‚—[Ïƒ] ğ“¢(F, G))
    refine'
      Seminorm.continuous_from_bounded (schwartz_withSeminorms ğ•œ D E)
        (schwartz_withSeminorms ğ•œ' F G) _ fun n => _
    rcases hbound n with âŸ¨s, C, hC, hâŸ©
    refine' âŸ¨s, âŸ¨C, hCâŸ©, fun f => _âŸ©
    simp only [Seminorm.comp_apply, Seminorm.smul_apply, NNReal.smul_def, Algebra.id.smul_eq_mul,
      Subtype.coe_mk]
    exact (mkLM A hadd hsmul hsmooth hbound f).seminorm_le_bound ğ•œ' n.1 n.2 (by positivity) (h f)
  toLinearMap := mkLM A hadd hsmul hsmooth hbound
#align schwartz_map.mk_clm SchwartzMap.mkCLM

end CLM

section EvalCLM

variable [NormedField ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The map applying a vector to Hom-valued Schwartz function as a continuous linear map. -/
protected def evalCLM (m : E) : ğ“¢(E, E â†’L[â„] F) â†’L[ğ•œ] ğ“¢(E, F) :=
  mkCLM (fun f x => f x m) (fun _ _ _ => rfl) (fun _ _ _ => rfl)
    (fun f => ContDiff.clm_apply f.2 contDiff_const)
    (by
      rintro âŸ¨k, nâŸ©
      use {(k, n)}, â€–mâ€–, norm_nonneg _
      intro f x
      refine'
        le_trans
          (mul_le_mul_of_nonneg_left (norm_iteratedFDeriv_clm_apply_const f.2 le_top)
            (by positivity))
          _
      rw [â† mul_assoc, â† mul_comm â€–mâ€–, mul_assoc]
      refine' mul_le_mul_of_nonneg_left _ (norm_nonneg _)
      simp only [Finset.sup_singleton, schwartzSeminormFamily_apply, le_seminorm])
#align schwartz_map.eval_clm SchwartzMap.evalCLM

end EvalCLM

section Multiplication

variable [NormedAddCommGroup D] [NormedSpace â„ D]

variable [NormedAddCommGroup G] [NormedSpace â„ G]

/-- The map `f â†¦ (x â†¦ B (f x) (g x))` as a continuous `ğ•œ`-linear map on Schwartz space,
where `B` is a continuous `ğ•œ`-linear map and `g` is a function of temperate growth. -/
def bilinLeftCLM (B : E â†’L[â„] F â†’L[â„] G) {g : D â†’ F} (hg : g.HasTemperateGrowth) :
    ğ“¢(D, E) â†’L[â„] ğ“¢(D, G) :=
  -- Todo (after port): generalize to `B : E â†’L[ğ•œ] F â†’L[ğ•œ] G` and `ğ•œ`-linear
    mkCLM
    (fun f x => B (f x) (g x))
    (fun _ _ _ => by
      simp only [map_add, add_left_inj, Pi.add_apply, eq_self_iff_true,
        ContinuousLinearMap.add_apply])
    (fun _ _ _ => by
      simp only [smul_apply, map_smul, ContinuousLinearMap.coe_smul', Pi.smul_apply,
        RingHom.id_apply])
    (fun f => (B.isBoundedBilinearMap.contDiff.restrict_scalars â„).comp (f.smooth'.prod hg.1))
    (by
      -- Porting note: rewrite this proof with `rel_congr`
      rintro âŸ¨k, nâŸ©
      rcases hg.norm_iteratedFDeriv_le_uniform_aux n with âŸ¨l, C, hC, hgrowthâŸ©
      use
        Finset.Iic (l + k, n), â€–Bâ€– * ((n : â„) + (1 : â„)) * n.choose (n / 2) * (C * 2 ^ (l + k)),
        by positivity
      intro f x
      have hxk : 0 â‰¤ â€–xâ€– ^ k := by positivity
      have hnorm_mul :=
        ContinuousLinearMap.norm_iteratedFDeriv_le_of_bilinear B f.smooth' hg.1 x (n := n) le_top
      refine' le_trans (mul_le_mul_of_nonneg_left hnorm_mul hxk) _
      rw [â† mul_assoc (â€–xâ€– ^ k), mul_comm (â€–xâ€– ^ k)]
      simp_rw [mul_assoc â€–Bâ€–]
      refine' mul_le_mul_of_nonneg_left _ (by positivity)
      rw [Finset.mul_sum]
      have : (âˆ‘ _x : â„• in Finset.range (n + 1), (1 : â„)) = n + 1 := by simp
      repeat rw [mul_assoc ((n : â„) + 1)]
      rw [â† this, Finset.sum_mul]
      refine' Finset.sum_le_sum fun i hi => _
      simp only [one_mul]
      rw [â† mul_assoc, mul_comm (â€–xâ€– ^ k), mul_assoc, mul_assoc, mul_assoc]
      refine' mul_le_mul _ _ (by positivity) (by positivity)
      Â· norm_cast
        exact i.choose_le_middle n
      specialize hgrowth (n - i) (by simp only [tsub_le_self]) x
      rw [â† mul_assoc]
      refine' le_trans (mul_le_mul_of_nonneg_left hgrowth (by positivity)) _
      rw [mul_comm _ (C * _), mul_assoc, mul_assoc C]
      refine' mul_le_mul_of_nonneg_left _ hC
      rw [mul_comm _ (â€–xâ€– ^ k)]
      rw [â† mul_assoc]
      rw [Finset.mem_range_succ_iff] at hi
      change i â‰¤ (l + k, n).snd at hi
      refine' le_trans _ (one_add_le_sup_seminorm_apply le_rfl hi f x)
      refine' mul_le_mul_of_nonneg_right _ (norm_nonneg _)
      rw [pow_add]
      refine' mul_le_mul_of_nonneg_left _ (by positivity)
      refine' pow_le_pow_left (norm_nonneg _) _ _
      simp only [zero_le_one, le_add_iff_nonneg_left])
#align schwartz_map.bilin_left_clm SchwartzMap.bilinLeftCLM

end Multiplication

section Comp

variable (ğ•œ)

variable [IsROrC ğ•œ]

variable [NormedAddCommGroup D] [NormedSpace â„ D]

variable [NormedAddCommGroup G] [NormedSpace â„ G]

variable [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

variable [NormedSpace ğ•œ G] [SMulCommClass â„ ğ•œ G]

/-- Composition with a function on the right is a continuous linear map on Schwartz space
provided that the function is temperate and growths polynomially near infinity. -/
def compCLM {g : D â†’ E} (hg : g.HasTemperateGrowth)
    (hg_upper : âˆƒ (k : â„•) (C : â„), âˆ€ x, â€–xâ€– â‰¤ C * (1 + â€–g xâ€–) ^ k) : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(D, F) :=
  mkCLM (fun f x => f (g x))
    (fun _ _ _ => by simp only [add_left_inj, Pi.add_apply, eq_self_iff_true]) (fun _ _ _ => rfl)
    (fun f => f.smooth'.comp hg.1)
    (by
      rintro âŸ¨k, nâŸ©
      rcases hg.norm_iteratedFDeriv_le_uniform_aux n with âŸ¨l, C, hC, hgrowthâŸ©
      rcases hg_upper with âŸ¨kg, Cg, hg_upper'âŸ©
      have hCg : 1 â‰¤ 1 + Cg := by
        refine' le_add_of_nonneg_right _
        specialize hg_upper' 0
        rw [norm_zero] at hg_upper'
        refine' nonneg_of_mul_nonneg_left hg_upper' (by positivity)
      let k' := kg * (k + l * n)
      use Finset.Iic (k', n), (1 + Cg) ^ (k + l * n) * ((C + 1) ^ n * n ! * 2 ^ k'), by positivity
      intro f x
      let seminorm_f := ((Finset.Iic (k', n)).sup (schwartzSeminormFamily ğ•œ _ _)) f
      have hg_upper'' : (1 + â€–xâ€–) ^ (k + l * n) â‰¤ (1 + Cg) ^ (k + l * n) * (1 + â€–g xâ€–) ^ k' := by
        rw [pow_mul, â† mul_pow]
        refine' pow_le_pow_left (by positivity) _ _
        rw [add_mul]
        refine' add_le_add _ (hg_upper' x)
        nth_rw 1 [â† one_mul (1 : â„)]
        refine' mul_le_mul (le_refl _) (one_le_pow_of_one_le _ _) zero_le_one zero_le_one
        simp only [le_add_iff_nonneg_right, norm_nonneg]
      have hbound :
        âˆ€ i, i â‰¤ n â†’ â€–iteratedFDeriv â„ i f (g x)â€– â‰¤ 2 ^ k' * seminorm_f / (1 + â€–g xâ€–) ^ k' := by
        intro i hi
        have hpos : 0 < (1 + â€–g xâ€–) ^ k' := by positivity
        rw [le_div_iff' hpos]
        change i â‰¤ (k', n).snd at hi
        exact one_add_le_sup_seminorm_apply le_rfl hi _ _
      have hgrowth' : âˆ€ N : â„•, 1 â‰¤ N â†’ N â‰¤ n â†’
          â€–iteratedFDeriv â„ N g xâ€– â‰¤ ((C + 1) * (1 + â€–xâ€–) ^ l) ^ N := by
        intro N hNâ‚ hNâ‚‚
        refine' (hgrowth N hNâ‚‚ x).trans _
        rw [mul_pow]
        have hNâ‚' := (lt_of_lt_of_le zero_lt_one hNâ‚).ne'
        refine' mul_le_mul _ _ (by positivity) (by positivity)
        Â· exact le_trans (by simp [hC]) (le_self_pow (by simp [hC]) hNâ‚')
        Â· refine' le_self_pow (one_le_pow_of_one_le _ l) hNâ‚'
          simp only [le_add_iff_nonneg_right, norm_nonneg]
      have := norm_iteratedFDeriv_comp_le f.smooth' hg.1 le_top x hbound hgrowth'
      have hxk : â€–xâ€– ^ k â‰¤ (1 + â€–xâ€–) ^ k :=
        pow_le_pow_left (norm_nonneg _) (by simp only [zero_le_one, le_add_iff_nonneg_left]) _
      refine' le_trans (mul_le_mul hxk this (by positivity) (by positivity)) _
      have rearrange :
        (1 + â€–xâ€–) ^ k *
            (n ! * (2 ^ k' * seminorm_f / (1 + â€–g xâ€–) ^ k') * ((C + 1) * (1 + â€–xâ€–) ^ l) ^ n) =
          (1 + â€–xâ€–) ^ (k + l * n) / (1 + â€–g xâ€–) ^ k' *
            ((C + 1) ^ n * n ! * 2 ^ k' * seminorm_f) := by
        rw [mul_pow, pow_add, â† pow_mul]
        ring
      rw [rearrange]
      have hgxk' : 0 < (1 + â€–g xâ€–) ^ k' := by positivity
      rw [â† div_le_iff hgxk'] at hg_upper''
      have hpos : (0 : â„) â‰¤ (C + 1) ^ n * n ! * 2 ^ k' * seminorm_f := by
        have : 0 â‰¤ seminorm_f := map_nonneg _ _
        positivity
      refine' le_trans (mul_le_mul_of_nonneg_right hg_upper'' hpos) _
      rw [â† mul_assoc])
#align schwartz_map.comp_clm SchwartzMap.compCLM

end Comp

section Derivatives

/-! ### Derivatives of Schwartz functions -/


variable (ğ•œ)

variable [IsROrC ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The FrÃ©chet derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def fderivCLM : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, E â†’L[â„] F) :=
  mkCLM (fderiv â„) (fun f g _ => fderiv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => fderiv_const_smul f.differentiableAt a)
    (fun f => (contDiff_top_iff_fderiv.mp f.smooth').2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [schwartzSeminormFamily_apply, Seminorm.comp_apply, Finset.sup_singleton,
        one_smul, norm_iteratedFDeriv_fderiv, one_mul] using f.le_seminorm ğ•œ k (n + 1) xâŸ©
#align schwartz_map.fderiv_clm SchwartzMap.fderivCLM

@[simp]
theorem fderivCLM_apply (f : ğ“¢(E, F)) (x : E) : fderivCLM ğ•œ f x = fderiv â„ f x :=
  rfl
#align schwartz_map.fderiv_clm_apply SchwartzMap.fderivCLM_apply

/-- The 1-dimensional derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def derivCLM : ğ“¢(â„, F) â†’L[ğ•œ] ğ“¢(â„, F) :=
  mkCLM (fun f => deriv f) (fun f g _ => deriv_add f.differentiableAt g.differentiableAt)
    (fun a f _ => deriv_const_smul a f.differentiableAt)
    (fun f => (contDiff_top_iff_deriv.mp f.smooth').2) fun âŸ¨k, nâŸ© =>
    âŸ¨{âŸ¨k, n + 1âŸ©}, 1, zero_le_one, fun f x => by
      simpa only [Real.norm_eq_abs, Finset.sup_singleton, schwartzSeminormFamily_apply, one_mul,
        norm_iteratedFDeriv_eq_norm_iteratedDeriv, â† iteratedDeriv_succ'] using
        f.le_seminorm' ğ•œ k (n + 1) xâŸ©
#align schwartz_map.deriv_clm SchwartzMap.derivCLM

@[simp]
theorem derivCLM_apply (f : ğ“¢(â„, F)) (x : â„) : derivCLM ğ•œ f x = deriv f x :=
  rfl
#align schwartz_map.deriv_clm_apply SchwartzMap.derivCLM_apply

/-- The partial derivative (or directional derivative) in the direction `m : E` as a
continuous linear map on Schwartz space. -/
def pderivCLM (m : E) : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, F) :=
  (SchwartzMap.evalCLM m).comp (fderivCLM ğ•œ)
#align schwartz_map.pderiv_clm SchwartzMap.pderivCLM

@[simp]
theorem pderivCLM_apply (m : E) (f : ğ“¢(E, F)) (x : E) : pderivCLM ğ•œ m f x = fderiv â„ f x m :=
  rfl
#align schwartz_map.pderiv_clm_apply SchwartzMap.pderivCLM_apply

/-- The iterated partial derivative (or directional derivative) as a continuous linear map on
Schwartz space. -/
def iteratedPDeriv {n : â„•} : (Fin n â†’ E) â†’ ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, F) :=
  Nat.recOn n (fun _ => ContinuousLinearMap.id ğ•œ _) fun _ rec x =>
    (pderivCLM ğ•œ (x 0)).comp (rec (Fin.tail x))
#align schwartz_map.iterated_pderiv SchwartzMap.iteratedPDeriv

@[simp]
theorem iteratedPDeriv_zero (m : Fin 0 â†’ E) (f : ğ“¢(E, F)) : iteratedPDeriv ğ•œ m f = f :=
  rfl
#align schwartz_map.iterated_pderiv_zero SchwartzMap.iteratedPDeriv_zero

@[simp]
theorem iteratedPDeriv_one (m : Fin 1 â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = pderivCLM ğ•œ (m 0) f :=
  rfl
#align schwartz_map.iterated_pderiv_one SchwartzMap.iteratedPDeriv_one

theorem iteratedPDeriv_succ_left {n : â„•} (m : Fin (n + 1) â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = pderivCLM ğ•œ (m 0) (iteratedPDeriv ğ•œ (Fin.tail m) f) :=
  rfl
#align schwartz_map.iterated_pderiv_succ_left SchwartzMap.iteratedPDeriv_succ_left

theorem iteratedPDeriv_succ_right {n : â„•} (m : Fin (n + 1) â†’ E) (f : ğ“¢(E, F)) :
    iteratedPDeriv ğ•œ m f = iteratedPDeriv ğ•œ (Fin.init m) (pderivCLM ğ•œ (m (Fin.last n)) f) := by
  induction' n with n IH
  Â· rw [iteratedPDeriv_zero, iteratedPDeriv_one]
    rfl
  -- The proof is `âˆ‚^{n + 2} = âˆ‚ âˆ‚^{n + 1} = âˆ‚ âˆ‚^n âˆ‚ = âˆ‚^{n+1} âˆ‚`
  have hmzero : Fin.init m 0 = m 0 := by simp only [Fin.init_def, Fin.castSucc_zero]
  have hmtail : Fin.tail m (Fin.last n) = m (Fin.last n.succ) := by
    simp only [Fin.tail_def, Fin.succ_last]
  -- Porting note: changed to `calc` proof
  calc
    _ = pderivCLM ğ•œ (m 0) (iteratedPDeriv ğ•œ _ f) := iteratedPDeriv_succ_left _ _ _
    _ = pderivCLM ğ•œ (m 0) ((iteratedPDeriv ğ•œ _) ((pderivCLM ğ•œ _) f)) := by
      congr 1
      exact IH _
    _ = _ := by
      simp only [hmtail, iteratedPDeriv_succ_left, hmzero, Fin.tail_init_eq_init_tail]
#align schwartz_map.iterated_pderiv_succ_right SchwartzMap.iteratedPDeriv_succ_right

-- Todo: `iteratedPDeriv ğ•œ m f x = iteratedFDeriv â„ f x m`
end Derivatives

section BoundedContinuousFunction

/-! ### Inclusion into the space of bounded continuous functions -/


open scoped BoundedContinuousFunction

/-- Schwartz functions as bounded continuous functions -/
def toBoundedContinuousFunction (f : ğ“¢(E, F)) : E â†’áµ‡ F :=
  BoundedContinuousFunction.ofNormedAddCommGroup f (SchwartzMap.continuous f)
    (SchwartzMap.seminorm â„ 0 0 f) (norm_le_seminorm â„ f)
#align schwartz_map.to_bounded_continuous_function SchwartzMap.toBoundedContinuousFunction

@[simp]
theorem toBoundedContinuousFunction_apply (f : ğ“¢(E, F)) (x : E) :
    f.toBoundedContinuousFunction x = f x :=
  rfl
#align schwartz_map.to_bounded_continuous_function_apply SchwartzMap.toBoundedContinuousFunction_apply

/-- Schwartz functions as continuous functions -/
def toContinuousMap (f : ğ“¢(E, F)) : C(E, F) :=
  f.toBoundedContinuousFunction.toContinuousMap
#align schwartz_map.to_continuous_map SchwartzMap.toContinuousMap

variable (ğ•œ E F)

variable [IsROrC ğ•œ] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]

/-- The inclusion map from Schwartz functions to bounded continuous functions as a linear map. -/
def toBoundedContinuousFunctionLM : ğ“¢(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ F where
  toFun f := f.toBoundedContinuousFunction
  map_add' f g := by ext; exact add_apply
  map_smul' a f := by ext; exact smul_apply
#align schwartz_map.to_bounded_continuous_function_lm SchwartzMap.toBoundedContinuousFunctionLM

@[simp]
theorem toBoundedContinuousFunctionLM_apply (f : ğ“¢(E, F)) (x : E) :
    toBoundedContinuousFunctionLM ğ•œ E F f x = f x :=
  rfl
#align schwartz_map.to_bounded_continuous_function_lm_apply SchwartzMap.toBoundedContinuousFunctionLM_apply

/-- The inclusion map from Schwartz functions to bounded continuous functions as a continuous linear
map. -/
def toBoundedContinuousFunctionCLM : ğ“¢(E, F) â†’L[ğ•œ] E â†’áµ‡ F :=
  { toBoundedContinuousFunctionLM ğ•œ E F with
    cont := by
      change Continuous (toBoundedContinuousFunctionLM ğ•œ E F)
      refine'
        Seminorm.continuous_from_bounded (schwartz_withSeminorms ğ•œ E F)
          (norm_withSeminorms ğ•œ (E â†’áµ‡ F)) _ fun _ => âŸ¨{0}, 1, fun f => _âŸ©
      -- Porting note: Lean failed to find this instance
      have : MulAction NNReal (Seminorm ğ•œ ğ“¢(E, F)) := Seminorm.instDistribMulAction.toMulAction
      simp only [Seminorm.comp_apply, coe_normSeminorm, Finset.sup_singleton,
        schwartzSeminormFamily_apply_zero, Seminorm.smul_apply, one_smul, ge_iff_le,
        BoundedContinuousFunction.norm_le (map_nonneg _ _)]
      intro x
      exact norm_le_seminorm ğ•œ _ _ }
#align schwartz_map.to_bounded_continuous_function_clm SchwartzMap.toBoundedContinuousFunctionCLM

@[simp]
theorem toBoundedContinuousFunctionCLM_apply (f : ğ“¢(E, F)) (x : E) :
    toBoundedContinuousFunctionCLM ğ•œ E F f x = f x :=
  rfl
#align schwartz_map.to_bounded_continuous_function_clm_apply SchwartzMap.toBoundedContinuousFunctionCLM_apply

variable {E}

/-- The Dirac delta distribution -/
def delta (x : E) : ğ“¢(E, F) â†’L[ğ•œ] F :=
  (BoundedContinuousFunction.evalCLM ğ•œ x).comp (toBoundedContinuousFunctionCLM ğ•œ E F)
#align schwartz_map.delta SchwartzMap.delta

@[simp]
theorem delta_apply (xâ‚€ : E) (f : ğ“¢(E, F)) : delta ğ•œ F xâ‚€ f = f xâ‚€ :=
  rfl
#align schwartz_map.delta_apply SchwartzMap.delta_apply

end BoundedContinuousFunction

end SchwartzMap
