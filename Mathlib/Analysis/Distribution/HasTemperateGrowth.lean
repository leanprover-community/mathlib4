/-
Copyright (c) 2024 Jack Valmadre. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack Valmadre
-/
import Mathlib.Analysis.Calculus.FDeriv.Add
import Mathlib.Analysis.Distribution.SchwartzSpace

/-!
# Temperate growth
-/

open ContDiff

namespace Function

section PeriodicUtil

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]

variable (ğ•œ) in
/-- If a function is is periodic, then its derivative is periodic. -/
theorem Periodic.fderiv {f : E â†’ F} {c : E} (hf : Periodic f c) : Periodic (fderiv ğ•œ f) c := by
  intro x
  rw [â† fderiv_comp_add_right, hf.funext]

variable (ğ•œ) in
/-- If a function is is periodic, then all of its derivatives are periodic. -/
theorem Periodic.iteratedFDeriv (n : â„•) {f : E â†’ F} {c : E} (hf : Periodic f c) :
    Periodic (iteratedFDeriv ğ•œ n f) c := by
  intro x
  rw [â† iteratedFDeriv_comp_add_right, hf.funext]

variable {Î± Î² : Type*}
  [LinearOrderedAddCommGroup Î±] [Archimedean Î±] [TopologicalSpace Î±] [CompactIccSpace Î±]
  [LinearOrder Î²] [TopologicalSpace Î²] [ClosedIciTopology Î²]

theorem Periodic.bddAbove_range_of_continuous [Nonempty Î²] {f : Î± â†’ Î²} {c : Î±}
    (hf : Periodic f c) (hc : 0 < c) (hf_cont : Continuous f) :
    BddAbove (Set.range f) := by
  -- TODO: Can we change the proof to reduce the assumptions on `Î²`?
  rw [â† hf.image_Icc hc 0]
  exact IsCompact.bddAbove_image isCompact_Icc hf_cont.continuousOn

-- TODO: Generalize to finite-dimensional vector space?
/-- Continuous periodic functions on are bounded. -/
theorem Periodic.exists_bound_of_continuous {f : Î± â†’ F} {c : Î±}
    (hf : Periodic f c) (hc : 0 < c) (hf_cont : Continuous f) : âˆƒ C, âˆ€ x, â€–f xâ€– â‰¤ C := by
  have h := (hf.comp fun y â†¦ â€–yâ€–).bddAbove_range_of_continuous hc hf_cont.norm
  rcases h.exists_ge 0 with âŸ¨C, _, hCâŸ©
  exact âŸ¨C, fun x â†¦ by simpa using hC â€–f xâ€–âŸ©

end PeriodicUtil

section HasTemperateGrowth

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

-- TODO: Could generalize beyond `â„`? Not necessary at this stage.
theorem Periodic.hasTemperateGrowth {f : â„ â†’ F} {c : â„}
    (hf : Periodic f c) (hc : 0 < c) (hf_smooth : ContDiff â„ âˆ f) : f.HasTemperateGrowth := by
  refine âŸ¨hf_smooth, ?_âŸ©
  intro n
  use 0
  have := (hf.iteratedFDeriv â„ n).exists_bound_of_continuous hc
    (hf_smooth.continuous_iteratedFDeriv (by norm_cast; simp))
  simpa

end HasTemperateGrowth

end Function

namespace Real

theorem hasTemperateGrowth_cos : cos.HasTemperateGrowth :=
  cos_periodic.hasTemperateGrowth two_pi_pos contDiff_cos

theorem hasTemperateGrowth_sin : sin.HasTemperateGrowth :=
  sin_periodic.hasTemperateGrowth two_pi_pos contDiff_sin

end Real

namespace Complex

open scoped Real

theorem exp_ofReal_mul_I_periodic : Function.Periodic (fun x : â„ â†¦ exp (x * I)) (2 * Ï€) :=
  fun x â†¦ by simp [add_mul, exp_add]

theorem exp_ofReal_mul_I_hasTemperateGrowth : Function.HasTemperateGrowth fun x : â„ â†¦ exp (x * I) :=
  exp_ofReal_mul_I_periodic.hasTemperateGrowth Real.two_pi_pos
    (ofRealCLM.contDiff.mul contDiff_const).cexp

end Complex
