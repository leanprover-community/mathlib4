/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/

import Mathlib.Analysis.Calculus.ContDiff
import Mathlib.Topology.ContinuousFunction.Bounded
import Mathlib.Analysis.Seminorm

open TopologicalSpace SeminormFamily
open scoped BoundedContinuousFunction Topology

variable (ğ•œ E F : Type _) [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedSpace ğ•œ E] [NormedSpace ğ•œ F] {n : â„•âˆ}

structure BoundedContDiffMap (n : â„•âˆ) : Type _ where
  protected toFun : E â†’ F
  protected contDiff' : ContDiff ğ•œ n toFun
  protected bounded' : âˆ€ i : â„•, i â‰¤ n â†’ âˆƒ C, âˆ€ x, â€–iteratedFDeriv ğ•œ i toFun xâ€– â‰¤ C

notation:25 E " â†’áµ‡[" ğ•œ ", " n "] " F => BoundedContDiffMap ğ•œ E F n

class BoundedContDiffMapClass (B : Type _) (ğ•œ E F : outParam <| Type _) [NontriviallyNormedField ğ•œ]
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace ğ•œ E] [NormedSpace ğ•œ F]
    (n : outParam â„•âˆ) extends FunLike B E (fun _ â†¦ F) where
  protected contDiff (f : B) : ContDiff ğ•œ n f
  protected bounded (f : B) : âˆ€ â¦ƒi : â„•â¦„, i â‰¤ n â†’ âˆƒ C, âˆ€ x, â€–iteratedFDeriv ğ•œ i f xâ€– â‰¤ C

namespace BoundedContDiffMap

instance toBoundedContDiffMapClass : BoundedContDiffMapClass (E â†’áµ‡[ğ•œ, n] F) ğ•œ E F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  contDiff f := f.contDiff'
  bounded f := f.bounded'

variable {ğ•œ E F}

protected theorem contDiff (f : E â†’áµ‡[ğ•œ, n] F) : ContDiff ğ•œ n f := f.contDiff'
protected theorem bounded (f : E â†’áµ‡[ğ•œ, n] F) :
    âˆ€ i : â„•, i â‰¤ n â†’ âˆƒ C, âˆ€ x, â€–iteratedFDeriv ğ•œ i f xâ€– â‰¤ C :=
  f.bounded'


@[simp]
theorem toFun_eq_coe {f : E â†’áµ‡[ğ•œ, n] F} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.apply (f : E â†’áµ‡[ğ•œ, n] F) : E â†’F  := f

-- this must come after the coe_to_fun definition
initialize_simps_projections BoundedContDiffMap (toFun â†’ apply)

@[ext]
theorem ext {f g : E â†’áµ‡[ğ•œ, n] F} (h : âˆ€ a, f a = g a) : f = g :=
  FunLike.ext _ _ h

/-- Copy of a `BoundedContDiffMap` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : E â†’áµ‡[ğ•œ, n] F) (f' : E â†’ F) (h : f' = f) : E â†’áµ‡[ğ•œ, n] F where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  bounded' := h.symm â–¸ f.bounded

@[simp]
theorem coe_copy (f : E â†’áµ‡[ğ•œ, n] F) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : E â†’áµ‡[ğ•œ, n] F) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  FunLike.ext' h

instance : AddCommGroup (E â†’áµ‡[ğ•œ, n] F) where
  add f g := BoundedContDiffMap.mk (f + g) (f.contDiff.add g.contDiff) fun i hi â†¦ by
    rcases f.bounded i hi with âŸ¨Cf, hCfâŸ©; rcases g.bounded i hi with âŸ¨Cg, hCgâŸ©
    refine âŸ¨Cf + Cg, fun x â†¦ ?_âŸ©
    rw [iteratedFDeriv_add_apply (f.contDiff.of_le hi) (g.contDiff.of_le hi)]
    exact norm_add_le_of_le (hCf x) (hCg x)
  add_assoc fâ‚ fâ‚‚ fâ‚ƒ := by ext; exact add_assoc _ _ _
  add_comm f g := by ext; exact add_comm _ _
  zero := BoundedContDiffMap.mk 0 contDiff_zero_fun fun i _ â†¦ by
    refine âŸ¨0, fun x â†¦ ?_âŸ©
    rw [Pi.zero_def, iteratedFDeriv_zero_fun, Pi.zero_apply, norm_zero]
  zero_add f := by ext; exact zero_add _
  add_zero f := by ext; exact add_zero _
  neg f := BoundedContDiffMap.mk (-f) (f.contDiff.neg) fun i hi â†¦ by
    simpa only [iteratedFDeriv_neg_apply, norm_neg] using (f.bounded i hi)
  add_left_neg f := by ext; exact add_left_neg _

instance : Module ğ•œ (E â†’áµ‡[ğ•œ, n] F) where
  smul c f := BoundedContDiffMap.mk (c â€¢ (f : E â†’ F)) (f.contDiff.const_smul c) fun i hi â†¦ by
    rcases f.bounded i hi with âŸ¨B, hBâŸ©
    refine âŸ¨â€–câ€– * B, fun x â†¦ ?_âŸ©
    rw [iteratedFDeriv_const_smul_apply (f.contDiff.of_le hi), norm_smul]
    exact mul_le_mul_of_nonneg_left (hB x) (norm_nonneg _)
  one_smul f := by ext; exact one_smul _ _
  mul_smul câ‚ câ‚‚ f := by ext; exact mul_smul _ _ _
  smul_zero c := by ext; exact smul_zero _
  smul_add c f g := by ext; exact smul_add _ _ _
  add_smul câ‚ câ‚‚ f := by ext; exact add_smul _ _ _
  zero_smul f := by ext; exact zero_smul _ _

noncomputable def iteratedFDerivâ‚— (i : â„•) :
    (E â†’áµ‡[ğ•œ, n] F) â†’â‚—[ğ•œ] (E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F)) :=
  if hi : i â‰¤ n then
  { toFun := fun f â†¦ .ofNormedAddCommGroup (iteratedFDeriv ğ•œ i f)
      (f.contDiff.continuous_iteratedFDeriv hi) (f.bounded i hi).choose
      (fun x â†¦ (f.bounded i hi).choose_spec x),
    map_add' := by
      intro f g
      ext : 1
      exact iteratedFDeriv_add_apply (f.contDiff.of_le hi) (g.contDiff.of_le hi),
    map_smul' := by
      intro c f
      ext : 1
      exact iteratedFDeriv_const_smul_apply (f.contDiff.of_le hi) }
  else 0

@[simp]
theorem iteratedFDerivâ‚—_apply (i : â„•) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    iteratedFDerivâ‚— i f x = if i â‰¤ n then iteratedFDeriv ğ•œ i f x else 0 := by
  rw [iteratedFDerivâ‚—]
  split_ifs <;> rfl

@[simp]
theorem iteratedFDerivâ‚—_apply_of_le {i : â„•} (hin : i â‰¤ n) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    iteratedFDerivâ‚— i f x = iteratedFDeriv ğ•œ i f x := by
  rw [iteratedFDerivâ‚—_apply]
  exact dif_pos hin

theorem iteratedFDerivâ‚—_of_gt {i : â„•} (hin : i > n) :
    (iteratedFDerivâ‚— i : (E â†’áµ‡[ğ•œ, n] F) â†’â‚—[ğ•œ] (E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F))) = 0 :=
  dif_neg (not_le_of_gt hin)

theorem iteratedFDerivâ‚—_apply_of_gt {i : â„•} (hin : i > n) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    (iteratedFDerivâ‚— i f x) = 0 := by
  rw [iteratedFDerivâ‚—_apply]
  exact dif_neg (not_le_of_gt hin)

section Topology

instance : TopologicalSpace (E â†’áµ‡[ğ•œ, n] F) :=
  â¨… (i : â„•), induced (iteratedFDerivâ‚— i) inferInstance

noncomputable instance : UniformSpace (E â†’áµ‡[ğ•œ, n] F) := .replaceTopology
  (â¨… (i : â„•), UniformSpace.comap (iteratedFDerivâ‚— i) inferInstance)
  toTopologicalSpace_iInf.symm

protected theorem BoundedContDiffMap.uniformSpace_eq_iInf :
    (instUniformSpaceBoundedContDiffMap : UniformSpace (E â†’áµ‡[ğ•œ, n] F)) =
    â¨… (i : â„•), UniformSpace.comap (iteratedFDerivâ‚— i) inferInstance :=
  UniformSpace.replaceTopology_eq _ toTopologicalSpace_iInf.symm

instance : UniformAddGroup (E â†’áµ‡[ğ•œ, n] F) := by
  rw [BoundedContDiffMap.uniformSpace_eq_iInf]
  refine uniformAddGroup_iInf (fun i â†¦ ?_)
  exact uniformAddGroup_comap _

noncomputable def iteratedFDerivL (i : â„•) :
    (E â†’áµ‡[ğ•œ, n] F) â†’L[ğ•œ] (E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F)) :=
  { iteratedFDerivâ‚— i with
    cont := continuous_iInf_dom continuous_induced_dom }

@[simp]
theorem iteratedFDerivL_apply (i : â„•) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    iteratedFDerivL i f x = if i â‰¤ n then iteratedFDeriv ğ•œ i f x else 0 := by
  simp [iteratedFDerivL]

@[simp]
theorem iteratedFDerivL_apply_of_le {i : â„•} (hin : i â‰¤ n) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    iteratedFDerivL i f x = iteratedFDeriv ğ•œ i f x := by
  rw [iteratedFDerivL_apply]
  exact dif_pos hin

theorem iteratedFDerivL_of_gt {i : â„•} (hin : i > n) :
    (iteratedFDerivL i : (E â†’áµ‡[ğ•œ, n] F) â†’L[ğ•œ] (E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F))) = 0 :=
  ContinuousLinearMap.coe_injective (iteratedFDerivâ‚—_of_gt hin)

theorem iteratedFDerivL_apply_of_gt {i : â„•} (hin : i > n) (f : E â†’áµ‡[ğ•œ, n] F) (x : E) :
    (iteratedFDerivL i f x) = 0 := by
  rw [iteratedFDerivL_apply]
  exact dif_neg (not_le_of_gt hin)

/-- This is mostly for dot notation. Should I keep it? -/
protected noncomputable abbrev iteratedFDeriv (i : â„•) (f : E â†’áµ‡[ğ•œ, n] F) : E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F) :=
  iteratedFDerivL i f

protected theorem continuous_iff {X : Type _} [TopologicalSpace X] (Ï† : X â†’ E â†’áµ‡[ğ•œ, n] F) :
  Continuous Ï† â†” âˆ€ (i : â„•) (_ : â†‘i â‰¤ n), Continuous
    ((BoundedContDiffMap.iteratedFDeriv i) âˆ˜ Ï†) :=
âŸ¨ fun hÏ† i _ â†¦ (iteratedFDerivL i).continuous.comp hÏ†,
  fun h â†¦ continuous_iInf_rng.mpr fun i â†¦ continuous_induced_rng.mpr <| by
    by_cases hin : i â‰¤ n
    Â· exact h i hin
    Â· simpa [iteratedFDerivâ‚—_of_gt (lt_of_not_ge hin)] using continuous_zero âŸ©

variable (ğ•œ E F n)

protected noncomputable def seminorm (i : â„•) : Seminorm ğ•œ (E â†’áµ‡[ğ•œ, n] F) :=
  (normSeminorm ğ•œ <| E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F)).comp (iteratedFDerivâ‚— i)

protected noncomputable def seminorm' (i : â„•) : Seminorm ğ•œ (E â†’áµ‡[ğ•œ, n] F) :=
  (Finset.Iic i).sup (BoundedContDiffMap.seminorm ğ•œ E F n)

protected theorem withSeminorms :
    WithSeminorms (BoundedContDiffMap.seminorm ğ•œ E F n) := by
  let p : SeminormFamily ğ•œ (E â†’áµ‡[ğ•œ, n] F) ((_ : â„•) Ã— Fin 1) :=
    SeminormFamily.sigma fun i â†¦ fun _ â†¦
      (normSeminorm ğ•œ (E â†’áµ‡ (E [Ã—i]â†’L[ğ•œ] F))).comp (BoundedContDiffMap.iteratedFDerivâ‚— i)
  have : WithSeminorms p :=
    withSeminorms_iInf fun i â†¦ LinearMap.withSeminorms_induced (norm_withSeminorms _ _) _
  exact this.congr_equiv (Equiv.sigmaUnique _ _).symm

protected theorem withSeminorms' :
    WithSeminorms (BoundedContDiffMap.seminorm' ğ•œ E F n) :=
  (BoundedContDiffMap.withSeminorms ğ•œ E F n).partial_sups

variable {ğ•œ E F n}

protected theorem seminorm_apply (i : â„•) (f : E â†’áµ‡[ğ•œ, n] F) :
    BoundedContDiffMap.seminorm ğ•œ E F n i f = â€–f.iteratedFDeriv iâ€– :=
  rfl

protected theorem seminorm_eq_bot {i : â„•} (hin : n < i) :
    BoundedContDiffMap.seminorm ğ•œ E F n i = âŠ¥ := by
  ext f
  rw [BoundedContDiffMap.seminorm_apply, BoundedContDiffMap.iteratedFDeriv,
      iteratedFDerivL_of_gt hin, ContinuousLinearMap.zero_apply, norm_zero]
  rfl

end Topology

section fderiv

noncomputable def fderivâ‚—' (n : â„•âˆ) : (E â†’áµ‡[ğ•œ, n+1] F) â†’â‚—[ğ•œ] (E â†’áµ‡[ğ•œ, n] (E â†’L[ğ•œ] F)) where
  toFun f :=
  { toFun := fderiv ğ•œ f
    contDiff' := f.contDiff.fderiv_right le_rfl
    bounded' := fun i hin â†¦ by
      rcases f.bounded (i+1) (add_le_add_right hin 1) with âŸ¨C, hCâŸ©
      refine âŸ¨C, fun x â†¦ ?_âŸ©
      rw [norm_iteratedFDeriv_fderiv]
      exact hC x }
  map_add' fâ‚ fâ‚‚ := by
    ext : 1
    exact fderiv_add
      (fâ‚.contDiff.differentiable le_add_self).differentiableAt
      (fâ‚‚.contDiff.differentiable le_add_self).differentiableAt
  map_smul' c f := by
    ext : 1
    exact fderiv_const_smul (f.contDiff.differentiable le_add_self).differentiableAt c

@[simp]
theorem fderivâ‚—'_apply (n : â„•âˆ) (f : E â†’áµ‡[ğ•œ, n+1] F) (x : E) :
    fderivâ‚—' n f x = fderiv ğ•œ f x :=
  rfl

theorem seminorm_fderivâ‚—' (i : â„•) (f : E â†’áµ‡[ğ•œ, n+1] F) :
    BoundedContDiffMap.seminorm ğ•œ E (E â†’L[ğ•œ] F) n i (fderivâ‚—' n f) =
      BoundedContDiffMap.seminorm ğ•œ E F (n+1) (i+1) f := by
  rw [BoundedContDiffMap.seminorm_apply, BoundedContDiffMap.seminorm_apply,
      BoundedContinuousFunction.norm_eq_of_nonempty, BoundedContinuousFunction.norm_eq_of_nonempty]
  refine congr_arg _ (Set.ext fun C â†¦ forall_congr' fun x â†¦ iff_of_eq <| congrArgâ‚‚ _ ?_ rfl)
  rcases le_or_gt (i : â„•âˆ) n with (hin|hin)
  Â· have hin' : (i + 1 : â„•) â‰¤ n + 1 := add_le_add_right hin _
    rw [iteratedFDerivL_apply_of_le hin, iteratedFDerivL_apply_of_le hin',
        â† norm_iteratedFDeriv_fderiv]
    rfl
  Â· have hin' : (i + 1 : â„•) > n + 1 := WithTop.add_lt_add_right WithTop.one_ne_top hin
    rw [iteratedFDerivL_apply_of_gt hin, iteratedFDerivL_apply_of_gt hin', norm_zero, norm_zero]

noncomputable def fderivL' (n : â„•âˆ) : (E â†’áµ‡[ğ•œ, n+1] F) â†’L[ğ•œ] (E â†’áµ‡[ğ•œ, n] (E â†’L[ğ•œ] F)) where
  toLinearMap := fderivâ‚—' n
  cont := by
    refine Seminorm.continuous_from_bounded  (Ï„â‚â‚‚ := RingHom.id ğ•œ)
      (BoundedContDiffMap.withSeminorms ğ•œ E F (n+1))
      (BoundedContDiffMap.withSeminorms ğ•œ E (E â†’L[ğ•œ] F) n) ?_ ?_
    refine fun i â†¦ âŸ¨{i+1}, 1, fun f â†¦ ?_âŸ©
    rw [Finset.sup_singleton, one_smul]
    exact (seminorm_fderivâ‚—' i f).le

@[simp]
theorem fderivL'_apply (n : â„•âˆ) (f : E â†’áµ‡[ğ•œ, n+1] F) (x : E) :
    fderivL' n f x = fderiv ğ•œ f x :=
  rfl

section infinite

noncomputable def fderivâ‚— : (E â†’áµ‡[ğ•œ, âŠ¤] F) â†’â‚—[ğ•œ] (E â†’áµ‡[ğ•œ, âŠ¤] (E â†’L[ğ•œ] F)) :=
  fderivâ‚—' âŠ¤

@[simp]
theorem fderivâ‚—_apply (f : E â†’áµ‡[ğ•œ, âŠ¤] F) (x : E) : fderivâ‚— f x = fderiv ğ•œ f x :=
  rfl

noncomputable def fderivL : (E â†’áµ‡[ğ•œ, âŠ¤] F) â†’L[ğ•œ] (E â†’áµ‡[ğ•œ, âŠ¤] (E â†’L[ğ•œ] F)) :=
  fderivL' âŠ¤

@[simp]
theorem fderivL_apply (n : â„•âˆ) (f : E â†’áµ‡[ğ•œ, n+1] F) (x : E) :
    fderivL' n f x = fderiv ğ•œ f x :=
  rfl

end infinite

end fderiv

section finite

variable {n : â„•}

protected theorem withSeminorms_of_finite : WithSeminorms
    (fun _ : Fin 1 â†¦ (BoundedContDiffMap.seminorm' ğ•œ E F n n)) := by
  refine (BoundedContDiffMap.withSeminorms ğ•œ E F n).congr ?_ ?_
  Â· intro _
    use Finset.Iic n, 1
    rw [one_smul]
    rfl
  Â· intro i
    use {0}, 1
    rw [one_smul, Finset.sup_singleton, Seminorm.comp_id]
    rcases le_or_gt i n with (hin|hin)
    Â· rw [â† Finset.mem_Iic] at hin
      exact Finset.le_sup (Î± := Seminorm ğ•œ (E â†’áµ‡[ğ•œ, n] F)) hin
    Â· rw [BoundedContDiffMap.seminorm_eq_bot (by exact_mod_cast hin)]
      exact bot_le

end finite

end BoundedContDiffMap

instance {E F} [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F] :
    LocallyConvexSpace â„ (E â†’áµ‡[â„, n] F) :=
  locallyConvexSpace_iInf fun _ â†¦ locallyConvexSpace_induced _
