/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.FourierSchwartz
public import Mathlib.Analysis.LocallyConvex.PointwiseConvergence

/-!
# TemperedDistribution

## Main definitions

* `TemperedDistribution ğ•œ E F V`: The space `ğ“¢(E, F) â†’L[ğ•œ] V` equipped with the pointwise
convergence topology.
* `MeasureTheory.Measure.toTemperedDistribution`: Every measure of temperate growth is a tempered
distribution.
* `TemperedDistribution.fourierTransformCLM`: The Fourier transform on tempered distributions.

## Notation
* `ğ“¢'(ğ•œ, E, F, V)`: The space of tempered distributions `TemperedDistribution ğ•œ E F V` localized
in `SchwartzSpace`
* `ğ“¢'(E, V)`: A shorthand for `ğ“¢'(â„‚, E, â„‚, V)`, the most common use-case.
-/

@[expose] public section

noncomputable section

open SchwartzMap ContinuousLinearMap
open MeasureTheory MeasureTheory.Measure

open scoped Nat NNReal ContDiff

variable {Î± ğ•œ ğ•œ' H D E F G V W R : Type*}

variable [RCLike ğ•œ] [NormedAddCommGroup D] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup G] [NormedAddCommGroup H] [NormedAddCommGroup V] [NormedAddCommGroup W]

section definition

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable (ğ•œ E F V)

/-- A tempered distribution is a continuous linear map from the Schwartz to -/
abbrev TemperedDistribution := ğ“¢(E, F) â†’Lâ‚šâ‚œ[ğ•œ] V

scoped[SchwartzMap] notation "ğ“¢'(" ğ•œ ", " E ", " F ", " V ")" => TemperedDistribution ğ•œ E F V

scoped[SchwartzMap] notation "ğ“¢'(" E ", " V ")" => TemperedDistribution â„‚ E â„‚ V

end definition

namespace TemperedDistribution

section Embeddings

section measure

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [MeasurableSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
variable [BorelSpace E] [SecondCountableTopology E]

variable (ğ•œ F Î¼) in
def MeasureTheory.Measure.toTemperedDistribution : ğ“¢'(ğ•œ, E, F, F) :=
  toPointwiseConvergenceCLM _ _ _ _ (integralCLM ğ•œ Î¼)

variable (ğ•œ) in
@[simp]
theorem MeasureTheory.Measure.toTemperedDistribution_apply (g : ğ“¢(E, F)) :
    Measure.toTemperedDistribution ğ•œ F Î¼ g = âˆ« (x : E), g x âˆ‚Î¼ := by
  rfl

end measure

end Embeddings

section fourier

open FourierTransform

variable
  [NormedSpace â„‚ E]
  [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  [InnerProductSpace â„ H] [FiniteDimensional â„ H]
  [MeasurableSpace H] [BorelSpace H]
  [NormedSpace ğ•œ V]

variable (ğ•œ H E V) in
def fourierTransformCLM : ğ“¢'(ğ•œ, H, E, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, H, E, V) :=
  PointwiseConvergenceCLM.precomp V (SchwartzMap.fourierTransformCLM ğ•œ)

instance instFourierTransform : FourierTransform ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourier := fourierTransformCLM ğ•œ H E V

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢'(ğ•œ, H, E, V)) :
    fourierTransformCLM ğ•œ H E V f = ğ“• f := rfl

@[simp]
theorem fourierTransform_apply (f : ğ“¢'(ğ•œ, H, E, V)) (g : ğ“¢(H, E)) : ğ“• f g = f (ğ“• g) := rfl

variable [CompleteSpace E]

variable (ğ•œ H E V) in
def fourierTransformInvCLM : ğ“¢'(ğ•œ, H, E, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, H, E, V) :=
  PointwiseConvergenceCLM.precomp V (SchwartzMap.fourierTransformCLE ğ•œ).symm.toContinuousLinearMap

instance instFourierTransformInv : FourierTransformInv ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourierInv := fourierTransformInvCLM ğ•œ H E V

@[simp]
theorem fourierTransformInvCLM_apply (f : ğ“¢'(ğ•œ, H, E, V)) :
    fourierTransformInvCLM ğ•œ H E V f = ğ“•â» f := rfl

@[simp]
theorem fourierTransformInv_apply (f : ğ“¢'(ğ•œ, H, E, V)) (g : ğ“¢(H, E)) : ğ“•â» f g = f (ğ“•â» g) := rfl

instance instFourierPair : FourierPair ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourierInv_fourier_eq f := by ext; simp

instance instFourierPairInv : FourierInvPair ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourier_fourierInv_eq f := by ext; simp

end fourier

end TemperedDistribution
