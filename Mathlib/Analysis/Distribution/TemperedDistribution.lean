/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.FourierSchwartz
public import Mathlib.Analysis.LocallyConvex.PointwiseConvergence

/-!
# TemperedDistribution

## Main definitions

* `TemperedDistribution E F`: The space `ğ“¢(E, â„‚) â†’L[â„‚] F` equipped with the pointwise
convergence topology.
* `MeasureTheory.Measure.toTemperedDistribution`: Every measure of temperate growth is a tempered
distribution.
* `TemperedDistribution.instLineDeriv`: The directional derivative on tempered distributions.
* `TemperedDistribution.fourierTransformCLM`: The Fourier transform on tempered distributions.

## Notation
* `ğ“¢'(E, F)`: The space of tempered distributions `TemperedDistribution E F` scoped in
`SchwartzMap`
-/

@[expose] public noncomputable section

noncomputable section

open SchwartzMap ContinuousLinearMap

open scoped Nat NNReal ContDiff

variable {E F : Type*}

section definition

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

variable (E F) in
/-- The space of tempered distribution is the space of continuous linear maps from the Schwartz to
a normed space, equipped with the topology of pointwise convergence. -/
abbrev TemperedDistribution := ğ“¢(E, â„‚) â†’Lâ‚šâ‚œ[â„‚] F
/- Since mathlib is missing quite a few results that show that continuity of linear maps and
convergence of sequences can be checked for strong duals of FrÃ©chet-Montel spaces pointwise, we
use the pointwise topology for now and not the strong topology. The pointwise topology is
conventially used in PDE texts, but has the downside that it is not barrelled, hence the uniform
boundedness principle does not hold. -/

@[inherit_doc]
scoped[SchwartzMap] notation "ğ“¢'(" E ", " F ")" => TemperedDistribution E F

end definition

/-! ### Embeddings into tempered distributions -/

section Embeddings

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

namespace MeasureTheory.Measure

variable [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]
  (Î¼ : Measure E := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth]

/-- Every temperate growth measure defines a tempered distribution. -/
def toTemperedDistribution : ğ“¢'(E, â„‚) :=
  toPointwiseConvergenceCLM _ _ _ _ (integralCLM â„‚ Î¼)

@[simp]
theorem toTemperedDistribution_apply (g : ğ“¢(E, â„‚)) :
    Î¼.toTemperedDistribution g = âˆ« (x : E), g x âˆ‚Î¼ := by
  rfl

end MeasureTheory.Measure

end Embeddings

namespace TemperedDistribution

/-! ### FDerivatives -/

section deriv

variable [NormedAddCommGroup F] [NormedSpace â„‚ F]

variable (F) in
/-- The 1-dimensional derivative on tempered distribution as a continuous `â„‚`-linear map. -/
def derivCLM : ğ“¢'(â„, F) â†’L[â„‚] ğ“¢'(â„, F) :=
  PointwiseConvergenceCLM.precomp F (-SchwartzMap.derivCLM â„‚)

@[simp]
theorem derivCLM_apply_apply (f : ğ“¢'(â„, F)) (g : ğ“¢(â„, â„‚)) :
    derivCLM F f g = f (-SchwartzMap.derivCLM â„‚ g) := rfl

end deriv

section lineDeriv

open LineDeriv

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

/-- The partial derivative (or directional derivative) in the direction `m : E` as a
continuous linear map on tempered distributions. -/
instance instLineDeriv : LineDeriv E ğ“¢'(E, F) ğ“¢'(E, F) where
  lineDerivOp m f := PointwiseConvergenceCLM.precomp F (-lineDerivOpCLM â„‚ ğ“¢(E, â„‚) m) f

instance instLineDerivAdd : LineDerivAdd E ğ“¢'(E, F) ğ“¢'(E, F) where
  lineDerivOp_add m := (PointwiseConvergenceCLM.precomp F (-lineDerivOpCLM â„‚ ğ“¢(E, â„‚) m)).map_add

instance instLineDerivSMul : LineDerivSMul â„‚ E ğ“¢'(E, F) ğ“¢'(E, F) where
  lineDerivOp_smul m := (PointwiseConvergenceCLM.precomp F (-lineDerivOpCLM â„‚ ğ“¢(E, â„‚) m)).map_smul

instance instContinuousLineDeriv : ContinuousLineDeriv E ğ“¢'(E, F) ğ“¢'(E, F) where
  continuous_lineDerivOp m :=
    (PointwiseConvergenceCLM.precomp F (-lineDerivOpCLM â„‚ ğ“¢(E, â„‚) m)).continuous

theorem lineDerivOpCLM_eq (m : E) : lineDerivOpCLM â„‚ ğ“¢'(E, F) m =
  PointwiseConvergenceCLM.precomp F (-lineDerivOpCLM â„‚ ğ“¢(E, â„‚) m) := rfl

end lineDeriv

/-! ### Fourier transform -/

section Fourier

open FourierTransform

variable [NormedAddCommGroup E] [NormedAddCommGroup F]
  [InnerProductSpace â„ E] [NormedSpace â„‚ F]
  [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]

variable (E F) in
/-- The Fourier transform on tempered distributions as a continuous linear map. -/
def fourierTransformCLM : ğ“¢'(E, F) â†’L[â„‚] ğ“¢'(E, F) :=
  PointwiseConvergenceCLM.precomp F (SchwartzMap.fourierTransformCLM â„‚)

instance instFourierTransform : FourierTransform ğ“¢'(E, F) ğ“¢'(E, F) where
  fourier := fourierTransformCLM E F

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢'(E, F)) :
  fourierTransformCLM E F f = ğ“• f := rfl

@[simp]
theorem fourierTransform_apply (f : ğ“¢'(E, F)) (g : ğ“¢(E, â„‚)) : ğ“• f g = f (ğ“• g) := rfl

variable (E F) in
/-- The inverse Fourier transform on tempered distributions as a continuous linear map. -/
def fourierTransformInvCLM : ğ“¢'(E, F) â†’L[â„‚] ğ“¢'(E, F) :=
  PointwiseConvergenceCLM.precomp F (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap

instance instFourierTransformInv : FourierTransformInv ğ“¢'(E, F) ğ“¢'(E, F) where
  fourierInv := fourierTransformInvCLM E F

@[simp]
theorem fourierTransformInvCLM_apply (f : ğ“¢'(E, F)) :
    fourierTransformInvCLM E F f = ğ“•â» f := rfl

@[simp]
theorem fourierTransformInv_apply (f : ğ“¢'(E, F)) (g : ğ“¢(E, â„‚)) : ğ“•â» f g = f (ğ“•â» g) := rfl

instance instFourierPair : FourierPair ğ“¢'(E, F) ğ“¢'(E, F) where
  fourierInv_fourier_eq f := by ext; simp

instance instFourierPairInv : FourierInvPair ğ“¢'(E, F) ğ“¢'(E, F) where
  fourier_fourierInv_eq f := by ext; simp

end Fourier

end TemperedDistribution
