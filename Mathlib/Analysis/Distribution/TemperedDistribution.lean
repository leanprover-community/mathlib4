/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
module

public import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff
public import Mathlib.Analysis.Distribution.FourierSchwartz
public import Mathlib.Analysis.LocallyConvex.PointwiseConvergence
public import Mathlib.MeasureTheory.Function.Holder

/-!
# TemperedDistribution

## Main definitions

* `TemperedDistribution E F`: The space `ğ“¢(E, â„‚) â†’L[â„‚] F` equipped with the pointwise
convergence topology.
* `MeasureTheory.Measure.toTemperedDistribution`: Every measure of temperate growth is a tempered
distribution.
* `MeasureTheory.Lp.toTemperedDistribution`: Every `Lp` function is a tempered distribution.
* `TemperedDistribution.fourierTransformCLM`: The Fourier transform on tempered distributions.

## Notation
* `ğ“¢'(E, F)`: The space of tempered distributions `TemperedDistribution E F` scoped in
`SchwartzMap`
-/

@[expose] public noncomputable section

noncomputable section

open SchwartzMap ContinuousLinearMap MeasureTheory MeasureTheory.Measure

open scoped Nat NNReal ContDiff

variable {E F : Type*}

section definition

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

variable (E F) in
/-- The space of tempered distribution is the space of continuous linear maps from the Schwartz to
a normed space, equipped with the topology of pointwise convergence. -/
abbrev TemperedDistribution := ğ“¢(E, â„‚) â†’Lâ‚šâ‚œ[â„‚] F
/- Since mathlib is missing quite a few results that show that continuity of linear maps and
convergence of sequences can be checked for strong duals of FrÃ©chet-Montel spaces pointwise, we
use the pointwise topology for now and not the strong topology. The pointwise topology is
conventially used in PDE texts, but has the downside that it is not barrelled, hence the uniform
boundedness principle does not hold. -/

@[inherit_doc]
scoped[SchwartzMap] notation "ğ“¢'(" E ", " F ")" => TemperedDistribution E F

end definition

/-! ### Embeddings into tempered distributions -/

section Embeddings

variable [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„‚ F]

namespace MeasureTheory.Measure

variable [MeasurableSpace E] [BorelSpace E] [SecondCountableTopology E]
  (Î¼ : Measure E := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth]

set_option backward.privateInPublic true in
/-- Every temperate growth measure defines a tempered distribution. -/
def toTemperedDistribution : ğ“¢'(E, â„‚) :=
  toPointwiseConvergenceCLM _ _ _ _ (integralCLM â„‚ Î¼)

set_option backward.privateInPublic true in
@[simp]
theorem toTemperedDistribution_apply (g : ğ“¢(E, â„‚)) :
    Î¼.toTemperedDistribution g = âˆ« (x : E), g x âˆ‚Î¼ := by
  rfl

end MeasureTheory.Measure

namespace MeasureTheory.Lp

open scoped ENNReal

variable [CompleteSpace F]

variable [MeasurableSpace E] [BorelSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]

/-- Define a tempered distribution from a L^p function. -/
def toTemperedDistribution {p : â„â‰¥0âˆ}
    [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼) : ğ“¢'(E, F) :=
  haveI := ENNReal.HolderConjugate.inv_one_sub_inv' hp.out
  haveI : Fact (1 â‰¤ (1 - pâ»Â¹)â»Â¹) := by simp [fact_iff]
  toPointwiseConvergenceCLM _ _ _ _ <|
    (lsmul â„‚ â„‚).flip.lpPairing Î¼ p (1 - pâ»Â¹)â»Â¹ f âˆ˜L toLpCLM â„‚ â„‚ (1 - pâ»Â¹)â»Â¹ Î¼

@[simp]
theorem toTemperedDistribution_apply {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼)
    (g : ğ“¢(E, â„‚)) :
    toTemperedDistribution f g = âˆ« (x : E), g x â€¢ f x âˆ‚Î¼ := by
  simp only [toTemperedDistribution, toPointwiseConvergenceCLM_apply, comp_apply _, toLpCLM_apply,
    lpPairing_eq_integral, lsmul_flip_apply, toSpanSingleton_apply]
  apply integral_congr_ae
  filter_upwards [g.coeFn_toLp (1 - pâ»Â¹)â»Â¹ Î¼] with x hg
  rw [hg]

instance instCoeDep {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼) :
    CoeDep (Lp F p Î¼) f ğ“¢'(E, F) where
  coe := toTemperedDistribution f

variable (F) in
/-- The natural embedding of L^p into tempered distributions. -/
def toTemperedDistributionCLM (Î¼ : Measure E := by volume_tac) [Î¼.HasTemperateGrowth]
    (p : â„â‰¥0âˆ) [hp : Fact (1 â‰¤ p)] :
    Lp F p Î¼ â†’L[â„‚] ğ“¢'(E, F) where
  toFun := toTemperedDistribution
  map_add' f g := by simp [Lp.toTemperedDistribution]
  map_smul' a f := by simp [Lp.toTemperedDistribution]
  cont := by
    apply PointwiseConvergenceCLM.continuous_of_continuous_eval
    intro g
    haveI : Fact (1 â‰¤ (1 - pâ»Â¹)â»Â¹) := by simp [fact_iff]
    have hpq : ENNReal.HolderConjugate p (1 - pâ»Â¹)â»Â¹ :=
      ENNReal.HolderConjugate.inv_one_sub_inv' hp.out
    exact (((lsmul â„‚ â„‚ (E := F)).flip.lpPairing Î¼ p (1 - pâ»Â¹)â»Â¹).flip (g.toLp (1 - pâ»Â¹)â»Â¹ Î¼)).cont

@[simp]
theorem toTemperedDistributionCLM_apply {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼) :
    toTemperedDistributionCLM F Î¼ p f = f := rfl

variable [FiniteDimensional â„ E] [IsLocallyFiniteMeasure Î¼]

theorem ker_toTemperedDistributionCLM_eq_bot {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] :
    LinearMap.ker (MeasureTheory.Lp.toTemperedDistributionCLM F Î¼ p) = âŠ¥ := by
  rw [LinearMap.ker_eq_bot']
  intro f hf
  rw [eq_zero_iff_ae_eq_zero]
  apply ae_eq_zero_of_integral_contDiff_smul_eq_zero
  Â· exact (Lp.memLp f).locallyIntegrable hp.elim
  Â· intro g g_smooth g_cpt
    have hgâ‚ : HasCompactSupport (Complex.ofRealCLM âˆ˜ g) := g_cpt.comp_left rfl
    have hgâ‚‚ : ContDiff â„ âˆ (Complex.ofRealCLM âˆ˜ g) := by fun_prop
    calc
      _ = toTemperedDistributionCLM F Î¼ p f (hgâ‚.toSchwartzMap hgâ‚‚) := by simp
      _ = _ := by simp [hf]

end MeasureTheory.Lp

end Embeddings

namespace TemperedDistribution

/-! ### Fourier transform -/

section Fourier

open FourierTransform

variable [NormedAddCommGroup E] [NormedAddCommGroup F]
  [InnerProductSpace â„ E] [NormedSpace â„‚ F]
  [FiniteDimensional â„ E] [MeasurableSpace E] [BorelSpace E]

variable (E F) in
/-- The Fourier transform on tempered distributions as a continuous linear map. -/
def fourierTransformCLM : ğ“¢'(E, F) â†’L[â„‚] ğ“¢'(E, F) :=
  PointwiseConvergenceCLM.precomp F (SchwartzMap.fourierTransformCLM â„‚)

instance instFourierTransform : FourierTransform ğ“¢'(E, F) ğ“¢'(E, F) where
  fourier := fourierTransformCLM E F

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢'(E, F)) :
  fourierTransformCLM E F f = ğ“• f := rfl

@[simp]
theorem fourierTransform_apply (f : ğ“¢'(E, F)) (g : ğ“¢(E, â„‚)) : ğ“• f g = f (ğ“• g) := rfl

variable (E F) in
/-- The inverse Fourier transform on tempered distributions as a continuous linear map. -/
def fourierTransformInvCLM : ğ“¢'(E, F) â†’L[â„‚] ğ“¢'(E, F) :=
  PointwiseConvergenceCLM.precomp F (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap

instance instFourierTransformInv : FourierTransformInv ğ“¢'(E, F) ğ“¢'(E, F) where
  fourierInv := fourierTransformInvCLM E F

@[simp]
theorem fourierTransformInvCLM_apply (f : ğ“¢'(E, F)) :
    fourierTransformInvCLM E F f = ğ“•â» f := rfl

@[simp]
theorem fourierTransformInv_apply (f : ğ“¢'(E, F)) (g : ğ“¢(E, â„‚)) : ğ“•â» f g = f (ğ“•â» g) := rfl

instance instFourierPair : FourierPair ğ“¢'(E, F) ğ“¢'(E, F) where
  fourierInv_fourier_eq f := by ext; simp

instance instFourierPairInv : FourierInvPair ğ“¢'(E, F) ğ“¢'(E, F) where
  fourier_fourierInv_eq f := by ext; simp

end Fourier

end TemperedDistribution
