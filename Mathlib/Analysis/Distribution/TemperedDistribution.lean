/-
Copyright (c) 2025 Moritz Doll. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Moritz Doll
-/
import Mathlib.Analysis.Distribution.AEEqOfIntegralContDiff
import Mathlib.Analysis.Distribution.FourierSchwartz
import Mathlib.Analysis.LocallyConvex.WeakOperatorTopology
import Mathlib.MeasureTheory.Integral.IntegralEqImproper
import Mathlib.MeasureTheory.Function.Holder

/-!
# TemperedDistribution

## Main definitions

* `TemperedDistribution ğ•œ E F V`: The space `ğ“¢(E, F) â†’L[ğ•œ] V` with the weak operator topology
* `TemperedDistribution.derivCLM`: The one-dimensional distributional derivative
* `TemperedDistribution.pderivCLM`: Partial distributional derivatives
* `SchwartzMap.toTemperedDistributionCLM`: The canonical embedding of `ğ“¢(E, F)` into
`ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V)`.
* `Function.HasTemperateGrowth.toTemperedDistribution`: Every function of temperate growth is a
tempered distribution.
* `MeasureTheory.Measure.HasTemperateGrowth`: Every measure of temperate growth is a tempered
distribution.

## Main statements

* `derivCLM_toTemperedDistributionCLM_eq`: The equality of the distributional derivative and the
classical derivative.

## Notation

* `ğ“¢'(ğ•œ, E, F, V)`: The space of tempered distributions `TemperedDistribution ğ•œ E F V` localized
in `SchwartzSpace`



## Implementation details



## References

* [F. Bar, *Quuxes*][bibkey]

## Tags

Foobars, barfoos
-/

noncomputable section

open SchwartzMap ContinuousLinearMap
open MeasureTheory MeasureTheory.Measure

open scoped Nat NNReal ContDiff

variable {ğ•œ ğ•œ' H D E F G V W R : Type*}

variable [RCLike ğ•œ] [NormedAddCommGroup D] [NormedAddCommGroup E] [NormedAddCommGroup F]
  [NormedAddCommGroup G] [NormedAddCommGroup H] [NormedAddCommGroup V] [NormedAddCommGroup W]

section definition

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable (ğ•œ E F V)

abbrev TemperedDistribution := ğ“¢(E, F) â†’WOT[ğ•œ] V

scoped[SchwartzMap] notation "ğ“¢'(" ğ•œ ", " E ", " F ", " V ")" => TemperedDistribution ğ•œ E F V

end definition

section DiracDelta

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]

variable (ğ•œ F) in
/-- The Dirac delta distribution -/
def delta' (x : E) : ğ“¢'(ğ•œ, E, F, F) :=
  ContinuousLinearMap.toWOT ğ•œ ğ“¢(E, F) F
    ((BoundedContinuousFunction.evalCLM ğ•œ x).comp (toBoundedContinuousFunctionCLM ğ•œ E F))

@[simp]
theorem delta'_apply (xâ‚€ : E) (f : ğ“¢(E, F)) : delta' ğ•œ F xâ‚€ f = f xâ‚€ :=
  rfl

end DiracDelta

namespace SchwartzMap

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]

section pairing

variable [NormedSpace â„ V] [SMulCommClass â„ ğ•œ V]

def pairingLM : ğ“¢(E, F) â†’â‚—[ğ•œ] ğ“¢(E, F â†’L[ğ•œ] V) â†’L[ğ•œ] ğ“¢(E, V) where
  toFun f := bilinLeftSchwartzCLM (.id ğ•œ _) f
  map_add' _ _ := by ext; simp
  map_smul' _ _ := by ext; simp

@[simp]
theorem pairingLM_apply_apply (f : ğ“¢(E, F)) (g : ğ“¢(E, F â†’L[ğ•œ] V)) (x : E) :
    pairingLM f g x = g x (f x) := by
  simp [pairingLM]

theorem pairingLM_apply (f : ğ“¢(E, F)) (g : ğ“¢(E, F â†’L[ğ•œ] V)) :
    pairingLM f g = fun x â†¦ g x (f x) := by
  ext x
  simp

end pairing

open scoped ENNReal
open MeasureTheory

end SchwartzMap

section pairingCLM


variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [NormedSpace â„ V] [SMulCommClass â„ ğ•œ V]


def pairingCLM (g : ğ“¢(E, F â†’L[ğ•œ] V)) : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, V) :=
  mkCLM (fun f => pairingLM f g)
  (fun _ _ _ => by simp)
  (fun _ _ _ => by simp)
  (fun f => by
    convert ((ContinuousLinearMap.restrictScalarsL _ F _ â„ â„).contDiff.fun_comp
      (g.smooth âŠ¤)).clm_apply (f.smooth âŠ¤)
    simp)
  (by
      intro (k, n)
      simp only [pairingLM_apply]
      use Finset.Iic (k, n), (Finset.card (Finset.range (n+1))) â€¢ ((2 ^ n) *
        (((Finset.Iic (0, n)).sup (schwartzSeminormFamily ğ•œ _ _)) g) * (2 ^ k)), by positivity
      intro f x
      have := ((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).bilinearRestrictScalars
          â„).norm_iteratedFDeriv_le_of_bilinear_of_le_one (g.smooth âŠ¤) (f.smooth âŠ¤) x (n := n) (by
        simp only [WithTop.le_def, WithTop.coe_eq_coe, forall_eq', le_top, and_true]
        use n
        simp only [WithTop.coe_natCast]) (by simp only [norm_bilinearRestrictScalars, norm_id_le])
      simp only [bilinearRestrictScalars_apply_apply, coe_id', id_eq] at this
      grw [this]
      rw [Finset.mul_sum, smul_mul_assoc]
      apply Finset.sum_le_card_nsmul
      intro i hi
      simp only [Finset.mem_range] at hi
      have hg : â€–iteratedFDeriv â„ i g xâ€– â‰¤
          ((Finset.Iic (0, n)).sup (schwartzSeminormFamily ğ•œ _ _)) g := by
        grw [norm_iteratedFDeriv_le_seminorm ğ•œ g]
        rw [â† schwartzSeminormFamily_apply]
        apply Seminorm.le_finset_sup_apply
        simp only [Finset.mem_Iic, Prod.mk_le_mk, le_refl, true_and]
        exact Nat.le_of_lt_succ hi
      grw [Nat.choose_le_two_pow _ _, hg]
      move_mul [((Finset.Iic (0, n)).sup (schwartzSeminormFamily ğ•œ _ _)) g]
      apply mul_le_mul_of_nonneg_right _ (apply_nonneg _ _)
      simp only [Nat.cast_pow, Nat.cast_ofNat]
      move_mul [2 ^ n]
      have hf : â€–xâ€– ^ k * â€–iteratedFDeriv â„ (n - i) f xâ€– â‰¤
          ((Finset.Iic (k, n)).sup (schwartzSeminormFamily ğ•œ _ _)) f := by
        grw [le_seminorm ğ•œ]
        rw [â† schwartzSeminormFamily_apply]
        apply Seminorm.le_finset_sup_apply
        simp only [Finset.mem_Iic, Prod.mk_le_mk, le_refl, tsub_le_iff_right,
          le_add_iff_nonneg_right, zero_le, and_self]
      grw [hf]
      move_mul [((Finset.Iic (k, n)).sup (schwartzSeminormFamily ğ•œ _ _)) f]
      apply mul_le_mul_of_nonneg_right _ (apply_nonneg _ _)
      exact le_mul_of_one_le_right (pow_nonneg zero_le_two _) (one_le_powâ‚€ one_le_two))

end pairingCLM

section measure_theory

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [MeasurableSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
variable [BorelSpace E] [SecondCountableTopology E]

variable (ğ•œ F Î¼) in
def MeasureTheory.Measure.toTemperedDistribution : ğ“¢'(ğ•œ, E, F, F) :=
  (toWOT _ _ _) (integralCLM ğ•œ Î¼)

variable (ğ•œ) in
@[simp]
theorem MeasureTheory.Measure.toTemperedDistribution_apply (g : ğ“¢(E, F)) :
    Measure.toTemperedDistribution ğ•œ F Î¼ g = âˆ« (x : E), g x âˆ‚Î¼ := by
  rfl

end measure_theory

namespace Function.HasTemperateGrowth

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [MeasurableSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
variable [BorelSpace E] [SecondCountableTopology E]

variable [NormedSpace â„ V]

variable (ğ•œ V) in
def toTemperedDistribution (f : E â†’ F) : ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) :=
    (ContinuousLinearMap.toWOT _ _ _) ((integralCLM ğ•œ Î¼) âˆ˜L (bilinLeftCLM (.id ğ•œ _) f))

@[simp]
theorem toTemperedDistribution_apply {f : E â†’ F} (hf : f.HasTemperateGrowth) (g : ğ“¢(E, F â†’L[ğ•œ] V)) :
    toTemperedDistribution ğ•œ V (Î¼ := Î¼) f g = âˆ« (x : E), (g x) (f x) âˆ‚Î¼ := by
  simp [toTemperedDistribution, ContinuousLinearMap.toWOT_apply, hf]

end Function.HasTemperateGrowth

section LpSpace

namespace MeasureTheory.Lp

open scoped ENNReal

variable [NormedSpace â„ E] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [MeasurableSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
variable [BorelSpace E] [SecondCountableTopology E]
variable [NormedSpace â„ V] [CompleteSpace V]

theorem _root_.ENNReal.inv_one_sub_inv' (p : â„â‰¥0âˆ) [hp : Fact (1 â‰¤ p)] :
    p.HolderConjugate (1 - pâ»Â¹)â»Â¹ := by
  rw [ENNReal.holderConjugate_iff]
  simp only [inv_inv]
  refine add_tsub_cancel_of_le ?_
  simpa only [ENNReal.inv_le_one] using hp.elim

/-- Create a tempered distribution from a L^p function.

This is a helper definition with unnecessary parameters. -/
def toTemperedDistribution_aux (p q : â„â‰¥0âˆ) (hp : Fact (1 â‰¤ p)) (hq : Fact (1 â‰¤ q))
    (hpq : ENNReal.HolderConjugate p q) (f : Lp F p Î¼) :
    ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) :=
  (ContinuousLinearMap.toWOT _ _ _)
    (((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).flip.lpPairing Î¼ p q f) âˆ˜L (toLpCLM ğ•œ (F â†’L[ğ•œ] V) q Î¼))

variable (ğ•œ V) in
/-- Create a tempered distribution from a L^p function. -/
def toTemperedDistribution {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼) :
    ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) :=
  toTemperedDistribution_aux p ((1 - pâ»Â¹)â»Â¹) hp (by simp [fact_iff]) p.inv_one_sub_inv' f

@[simp]
theorem toTemperedDistribution_apply {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼)
    (g : ğ“¢(E, F â†’L[ğ•œ] V)) :
    (toTemperedDistribution ğ•œ V f) g = âˆ« (x : E), ((g.toLp (1 - pâ»Â¹)â»Â¹ Î¼) x) (f x) âˆ‚Î¼ := by
  unfold Lp.toTemperedDistribution Lp.toTemperedDistribution_aux
  simp [toWOT_apply, lpPairing_eq_integral]


variable (ğ•œ F V Î¼) in
/-- The natural embedding of L^p into tempered distributions. -/
def toTemperedDistributionCLM (p : â„â‰¥0âˆ) [hp : Fact (1 â‰¤ p)] :
    Lp F p Î¼ â†’L[ğ•œ] ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) where
  toFun := toTemperedDistribution ğ•œ V
  map_add' f g := by
    ext x
    unfold Lp.toTemperedDistribution Lp.toTemperedDistribution_aux
    simp only [map_add, add_comp, ContinuousLinearMapWOT.add_apply]
  map_smul' a f := by
    ext x
    unfold Lp.toTemperedDistribution Lp.toTemperedDistribution_aux
    simp
  cont := by
    apply ContinuousLinearMapWOT.continuous_of_dual_apply_continuous
    intro g y
    apply y.cont.comp
    set q := (1 - pâ»Â¹)â»Â¹
    have hq : Fact (1 â‰¤ q) := by simp [q, fact_iff]
    have hpq : ENNReal.HolderConjugate p q := p.inv_one_sub_inv'
    exact (((ContinuousLinearMap.id ğ•œ (F â†’L[ğ•œ] V)).flip.lpPairing Î¼ p q).flip (g.toLp q Î¼)).cont

@[simp]
theorem toTemperedDistributionCLM_apply {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] (f : Lp F p Î¼) :
    toTemperedDistributionCLM ğ•œ F V Î¼ p f = toTemperedDistribution ğ•œ V f := rfl

variable [FiniteDimensional â„ E] [NormedSpace â„ F] [CompleteSpace F] [IsLocallyFiniteMeasure Î¼]

theorem injective_toTemperedDistributionCLM {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] :
    LinearMap.ker (MeasureTheory.Lp.toTemperedDistributionCLM ğ•œ F F Î¼ p) = âŠ¥ := by
  refine LinearMap.ker_eq_bot'.mpr ?_
  intro f hf
  rw [eq_zero_iff_ae_eq_zero]
  apply ae_eq_zero_of_integral_contDiff_smul_eq_zero
  Â· exact MemLp.locallyIntegrable (Î¼ := Î¼) (Lp.memLp f) hp.elim
  Â· intro g g_smooth g_cpt
    have hg1 : HasCompactSupport (fun (x : E) â†¦ g x â€¢ (ContinuousLinearMap.id ğ•œ F)) := by
      apply g_cpt.smul_right
    have hg2 : ContDiff â„ âˆ (fun (x : E) â†¦ g x â€¢ (ContinuousLinearMap.id ğ•œ F)) := by fun_prop
    have : (fun x â†¦ (((hg1.toSchwartzMap hg2).toLp (1 - pâ»Â¹)â»Â¹ Î¼) x) (f x)) =áµ[Î¼]
        g â€¢ f := by
      filter_upwards [coeFn_toLp (hg1.toSchwartzMap hg2) (1 - pâ»Â¹)â»Â¹ Î¼] with x hgg
      simp [hgg]
    have hf_applied : (toTemperedDistributionCLM ğ•œ F F Î¼ p) f (hg1.toSchwartzMap hg2) = 0 := by
      rw [hf, ContinuousLinearMapWOT.zero_apply]
    simpa [integral_congr_ae this] using hf_applied

end MeasureTheory.Lp

end LpSpace

namespace SchwartzMap

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ V] [NormedSpace ğ•œ F]
variable [SecondCountableTopology E] [NormedSpace â„ V]

section MeasurableSpace

variable [MeasurableSpace E] [BorelSpace E]

variable (ğ•œ E F V) in
def toTemperedDistributionCLM (Î¼ : Measure E := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth] :
    ğ“¢(E, F) â†’L[ğ•œ] ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) where
  toFun f := (toWOT _ _ _) ((integralCLM ğ•œ Î¼) âˆ˜L (pairingLM f))
  map_add' _ _ := by ext; simp
  map_smul' _ _ := by ext; simp
  cont := by
    apply ContinuousLinearMapWOT.continuous_of_dual_apply_continuous
    intro g y
    exact y.cont.comp ((integralCLM ğ•œ Î¼).cont.comp (pairingCLM g).cont)

variable (f : ğ“¢(E, F)) (g : ğ“¢(E, F â†’L[ğ•œ] V)) (x : E)

@[simp]
theorem toTemperedDistributionCLM_apply_apply (Î¼ : Measure E := by volume_tac)
    [hÎ¼ : Î¼.HasTemperateGrowth] (f : ğ“¢(E, F)) (g : ğ“¢(E, F â†’L[ğ•œ] V)) :
    toTemperedDistributionCLM ğ•œ E F V Î¼ f g = âˆ« (x : E), (g x) (f x) âˆ‚Î¼ := by
  simp [toTemperedDistributionCLM, ContinuousLinearMap.toWOT_apply]

end MeasurableSpace

variable [MeasureSpace E] [BorelSpace E]

instance instCoeToTemperedDistribution [(volume (Î± := E)).HasTemperateGrowth] :
    Coe ğ“¢(E, F) ğ“¢'(ğ•œ, E, F â†’L[ğ•œ] V, V) where
  coe := toTemperedDistributionCLM ğ•œ E F V volume

end SchwartzMap

section Composition

variable [NormedSpace â„ E] [NormedSpace ğ•œ V]
variable [MeasurableSpace E] {Î¼ : Measure E} [hÎ¼ : Î¼.HasTemperateGrowth]
variable [BorelSpace E] [SecondCountableTopology E]
variable [NormedSpace â„ V]

variable [NormedSpace ğ•œ F] [NormedSpace â„ F] [CompleteSpace V]

variable (f : ğ“¢(E, V))

@[simp]
theorem eq_embeddings (f : ğ“¢(E, F)) : Lp.toTemperedDistribution ğ•œ V (f.toLp 2 Î¼) =
    SchwartzMap.toTemperedDistributionCLM ğ•œ E F V Î¼ f := by
  ext g y
  congr 1
  simp only [Lp.toTemperedDistribution_apply, toTemperedDistributionCLM_apply_apply]
  apply integral_congr_ae
  filter_upwards [f.coeFn_toLp 2 Î¼, g.coeFn_toLp (1 - 2â»Â¹)â»Â¹ Î¼] with x hf hg
  rw [hf, hg]


end Composition

section Construction

variable [NormedSpace â„ E] [NormedSpace â„ D]
  [NormedSpace â„ F] [NormedSpace ğ•œ F]
  [NormedSpace â„ G] [NormedSpace ğ•œ G]
  [NormedSpace ğ•œ V] [NormedSpace ğ•œ W]

variable (V W) in
def mkCLM (A : (ğ“¢(E, F) â†’L[ğ•œ] V) â†’â‚—[ğ•œ] (ğ“¢(D, G) â†’L[ğ•œ] W))
  (hbound : âˆ€ (f : ğ“¢(D, G)) (a : W â†’L[ğ•œ] ğ•œ), âˆƒ (s : Finset (ğ“¢(E, F) Ã— (V â†’L[ğ•œ] ğ•œ))) (C : â„â‰¥0),
  âˆ€ (B : ğ“¢(E, F) â†’L[ğ•œ] V), âˆƒ (g : ğ“¢(E, F)) (b : V â†’L[ğ•œ] ğ•œ) (_hb : (g, b) âˆˆ s),
  â€–a ((A B) f)â€– â‰¤ C â€¢ â€–b (B g)â€–) : ğ“¢'(ğ•œ, E, F, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, D, G, W) where
  __ := (toWOT _ _ _).toLinearMap.comp (A.comp (toWOT _ _ _).symm.toLinearMap)
  cont := by
    apply Seminorm.continuous_from_bounded ContinuousLinearMapWOT.withSeminorms
      ContinuousLinearMapWOT.withSeminorms
    intro (f, a)
    rcases hbound f a with âŸ¨s, C, hâŸ©
    use s, C
    rw [â† Seminorm.finset_sup_smul]
    intro B
    rcases h ((toWOT _ _ _).symm B) with âŸ¨g, b, hb, h'âŸ©
    refine le_trans ?_ (Seminorm.le_finset_sup_apply hb)
    unfold ContinuousLinearMapWOT.seminormFamily
    simpa using h'

variable (V) in
def mkCompCLM (A : ğ“¢(D, G) â†’L[ğ•œ] ğ“¢(E, F)) : ğ“¢'(ğ•œ, E, F, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, D, G, V) :=
    mkCLM V V
      {toFun f := f âˆ˜L A, map_add' f g := by simp, map_smul' := by simp}
      (by
        intro f a
        use {(A f, a)}, 1
        simp)

@[simp]
theorem mkCompCLM_apply_apply (A : ğ“¢(D, G) â†’L[ğ•œ] ğ“¢(E, F)) (f : ğ“¢'(ğ•œ, E, F, V)) (g : ğ“¢(D, G)) :
    (mkCompCLM V A) f g = f (A g) := rfl

theorem mkCompCLM_comp (A B : ğ“¢(E, F) â†’L[ğ•œ] ğ“¢(E, F)) :
    (mkCompCLM V A) âˆ˜L (mkCompCLM V B) = mkCompCLM V (B âˆ˜L A) := by
  ext f g y
  simp only [coe_comp', Function.comp_apply, mkCompCLM_apply_apply]

theorem mkCompCLM_id : (mkCompCLM V (.id ğ•œ ğ“¢(E, F))) = .id _ _ := by
  ext f g y
  simp only [mkCompCLM_apply_apply, coe_id', id_eq]

end Construction

section Multiplication

variable [NormedSpace â„ D]
  [NormedSpace â„ E] [NormedSpace ğ•œ E]
  [NormedSpace â„ F] [NormedSpace ğ•œ F]
  [NormedSpace â„ G] [NormedSpace ğ•œ G]
  [NormedSpace ğ•œ V]

variable (V) in
/-- The map `f â†¦ (x â†¦ B (f x) (g x))` as a continuous `ğ•œ`-linear map on Schwartz space,
where `B` is a continuous `ğ•œ`-linear map and `g` is a function of temperate growth. -/
def bilinLeftCLM (B : E â†’L[ğ•œ] F â†’L[ğ•œ] G) (g : D â†’ F) :
    ğ“¢'(ğ•œ, D, G, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, D, E, V) := mkCompCLM V (SchwartzMap.bilinLeftCLM B g)

variable [NonUnitalNormedRing R] [NormedSpace ğ•œ R] [NormedSpace â„ R] [IsScalarTower ğ•œ R R]
  [SMulCommClass ğ•œ R R]

def mulLeftCLM (g : D â†’ R) : ğ“¢'(ğ•œ, D, R, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, D, R, V) :=
    bilinLeftCLM V (ContinuousLinearMap.mul ğ•œ R) g

variable (E V) in
def smulLeftCLM (g : D â†’ ğ•œ) : ğ“¢'(ğ•œ, D, E, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, D, E, V) :=
    bilinLeftCLM V (ContinuousLinearMap.lsmul ğ•œ ğ•œ).flip g

@[simp]
theorem smulLeftCLM_apply_apply (g : D â†’ ğ•œ) (f : ğ“¢'(ğ•œ, D, E, V)) (f' : ğ“¢(D, E)) :
    smulLeftCLM E V g f f' = f (SchwartzMap.smulLeftCLM ğ•œ _ g f') := by
  rfl

theorem mul_smulLeftCLM {gâ‚ gâ‚‚ : D â†’ ğ•œ} (hgâ‚ : gâ‚.HasTemperateGrowth)
    (hgâ‚‚ : gâ‚‚.HasTemperateGrowth) :
    smulLeftCLM E V gâ‚‚ âˆ˜L smulLeftCLM E V gâ‚ = smulLeftCLM E V (gâ‚ * gâ‚‚) := by
  ext f f' y
  congr 1
  have := DFunLike.congr_fun (smulLeftCLM_mul hgâ‚ hgâ‚‚ (ğ•œ := ğ•œ) (E := E)) f'
  simp only [coe_comp', Function.comp_apply] at this
  simp [this]

variable [MeasurableSpace D] [BorelSpace D] [SecondCountableTopology D] {Î¼ : Measure D}
  [Î¼.HasTemperateGrowth] [NormedSpace â„ V]

theorem smulLeftCLM_toTemperedDistributionCLM_eq {g : D â†’ ğ•œ} (hg : g.HasTemperateGrowth)
    (f : ğ“¢(D, E)) : smulLeftCLM (E â†’L[ğ•œ] V) V g (toTemperedDistributionCLM ğ•œ D E V Î¼ f) =
    toTemperedDistributionCLM ğ•œ D E V Î¼ (SchwartzMap.smulLeftCLM ğ•œ E g f) := by
  ext f' y
  simp [hg]

end Multiplication


section deriv

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ V]

variable (V) in
/-- The 1-dimensional derivative on Schwartz space as a continuous `ğ•œ`-linear map. -/
def derivCLM : ğ“¢'(ğ•œ, â„, F, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, â„, F, V) := mkCompCLM V (-SchwartzMap.derivCLM ğ•œ)

@[simp]
theorem derivCLM_apply_apply (f : ğ“¢'(ğ•œ, â„, F, V)) (g : ğ“¢(â„, F)) :
    derivCLM V f g = f (-derivCLM ğ•œ g) := rfl

open scoped ENNReal

variable [NormedSpace â„ V]

/-- The distributional derivative and the classical derivative coincide on `ğ“¢(â„, F)`. -/
theorem derivCLM_toTemperedDistributionCLM_eq (f : ğ“¢(â„, F)) :
    derivCLM V (toTemperedDistributionCLM ğ•œ â„ F V volume f) =
    toTemperedDistributionCLM ğ•œ â„ F V volume f.deriv := by
  ext g y
  simp
  simp [integral_clm_comp_deriv_right_eq_neg_left, integral_neg]

end deriv

section pderiv

variable [NormedSpace â„ E] [NormedSpace â„ F] [NormedSpace ğ•œ F] [NormedSpace ğ•œ V]

variable (V) in
def TemperedDistribution.pderivCLM (m : E) : ğ“¢'(ğ•œ, E, F, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, E, F, V) :=
  mkCompCLM V (-SchwartzMap.pderivCLM ğ•œ m)

lemma pderivCLM_apply (m : E) (f : ğ“¢'(ğ•œ, E, F, V)) (g : ğ“¢(E, F)) :
    TemperedDistribution.pderivCLM V m f g = f (-SchwartzMap.pderivCLM ğ•œ m g) := by rfl

end pderiv

section fourier

namespace TemperedDistribution

open FourierTransform

variable
  [NormedSpace â„‚ E]
  [NormedSpace ğ•œ E] [SMulCommClass â„‚ ğ•œ E]
  [InnerProductSpace â„ H] [FiniteDimensional â„ H]
  [MeasurableSpace H] [BorelSpace H]
  [NormedSpace ğ•œ V]

variable (ğ•œ H E V) in
def fourierTransformCLM : ğ“¢'(ğ•œ, H, E, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, H, E, V) :=
  mkCompCLM V (SchwartzMap.fourierTransformCLM ğ•œ)

instance instFourierTransform : FourierTransform ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourierTransform := fourierTransformCLM ğ•œ H E V

@[simp]
theorem fourierTransformCLM_apply (f : ğ“¢'(ğ•œ, H, E, V)) :
    fourierTransformCLM ğ•œ H E V f = ğ“• f := rfl

@[simp]
theorem fourierTransform_apply (f : ğ“¢'(ğ•œ, H, E, V)) (g : ğ“¢(H, E)) : ğ“• f g = f (ğ“• g) := rfl

section FourierTransformInv

variable [CompleteSpace E]

variable (ğ•œ H E V) in
def fourierTransformInvCLM : ğ“¢'(ğ•œ, H, E, V) â†’L[ğ•œ] ğ“¢'(ğ•œ, H, E, V) :=
  mkCompCLM V (SchwartzMap.fourierTransformCLE ğ•œ).symm.toContinuousLinearMap

instance instFourierTransformInv : FourierTransformInv ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourierTransformInv := fourierTransformInvCLM ğ•œ H E V

@[simp]
theorem fourierTransformInvCLM_apply (f : ğ“¢'(ğ•œ, H, E, V)) :
    fourierTransformInvCLM ğ•œ H E V f = ğ“•â» f := rfl

@[simp]
theorem fourierTransformInv_apply (f : ğ“¢'(ğ•œ, H, E, V)) (g : ğ“¢(H, E)) : ğ“•â» f g = f (ğ“•â» g) := rfl

noncomputable
instance instFourierPair : FourierPair ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  inv_fourier f := by ext; simp

noncomputable
instance instFourierPairInv : FourierPairInv ğ“¢'(ğ•œ, H, E, V) ğ“¢'(ğ•œ, H, E, V) where
  fourier_inv f := by ext; simp

end FourierTransformInv

variable (f : ğ“¢(H, E))

variable [NormedSpace â„‚ V]
variable [CompleteSpace E] [CompleteSpace V]

/-- The distributional Fourier transform and the classical Fourier transform coincide on
`ğ“¢(â„, F)`. -/
theorem fourierTransformCLM_toTemperedDistributionCLM_eq (f : ğ“¢(H, E)) :
    ğ“• (f : ğ“¢'(â„‚, H, E â†’L[â„‚] V, V)) = ğ“• f := by
  ext g
  congr 1
  simp only [fourierTransform_apply, toTemperedDistributionCLM_apply_apply,
    SchwartzMap.fourierTransform_apply]
  exact integral_bilin_fourierIntegral_eq_flip g f (.id â„‚ _)

theorem fourierTransformInv_toTemperedDistributionCLM_eq (f : ğ“¢(H, E)) :
    ğ“•â» (f : ğ“¢'(â„‚, H, E â†’L[â„‚] V, V)) = ğ“•â» f := by
  have := fourierTransformCLM_toTemperedDistributionCLM_eq (V := V) (ğ“•â» f)
  apply_fun ğ“•â» at this
  simp only [FourierPair.inv_fourier, FourierPairInv.fourier_inv] at this
  rw [this]

example : ğ“• (delta' ğ•œ E (0 : H)) = volume.toTemperedDistribution ğ•œ E := by
  ext f x
  simp [SchwartzMap.fourierTransform_apply, Real.fourierIntegral_eq]

end TemperedDistribution

end fourier
