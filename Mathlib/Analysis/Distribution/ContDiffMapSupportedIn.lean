/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Luigi Massacci
-/
module

public import Mathlib.Analysis.Calculus.ContDiff.Operations
public import Mathlib.Topology.ContinuousMap.Bounded.Normed
public import Mathlib.Topology.Sets.Compacts

/-!
# Continuously differentiable functions supported in a given compact set

This file develops the basic theory of bundled `n`-times continuously differentiable functions
with support contained in a given compact set.

Given `n : â„•âˆ` and a compact subset `K` of a normed space `E`, we consider the type of bundled
functions `f : E â†’ F` (where `F` is a normed vector space) such that:

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` vanishes outside of a compact set: `EqOn f 0 Ká¶œ`.

The main reason this exists as a bundled type is to be endowed with its natural locally convex
topology (namely, uniform convergence of `f` and its derivatives up to order `n`).
Taking the locally convex inductive limit of these as `K` varies yields the natural topology on test
functions, used to define distributions. While most of distribution theory cares only about `C^âˆ`
functions, we also want to endow the space of `C^n` test functions with its natural topology.
Indeed, distributions of order less than `n` are precisely those which extend continuously to this
larger space of test functions.

## Main definitions

- `ContDiffMapSupportedIn E F n K`: the type of bundled `n`-times continuously differentiable
  functions `E â†’ F` which vanish outside of `K`.
- `ContDiffMapSupportedIn.iteratedFDerivWithOrderLM`: wrapper, as a `ğ•œ`-linear map, for
  `iteratedFDeriv` from `ContDiffMapSupportedIn E F n K` to
  `ContDiffMapSupportedIn E (E [Ã—i]â†’L[â„] F) k K`.
- `ContDiffMapSupportedIn.iteratedFDerivLM`: specialization of the above, giving a `ğ•œ`-linear map
  from `ContDiffMapSupportedIn E F âŠ¤ K` to `ContDiffMapSupportedIn E (E [Ã—i]â†’L[â„] F) âŠ¤ K`.
- `ContDiffMapSupportedIn.topologicalSpace`, `ContDiffMapSupportedIn.uniformSpace`: the topology
  and uniform structures on `ğ““^{n}_{K}(E, F)`, given by uniform convergence of the functions and
  all their derivatives up to order `n`.

## Main statements

- `ContDiffMapSupportedIn.isTopologicalAddGroup`, `ContDiffMapSupportedIn.continuousSMul` and
  `ContDiffMapSupportedIn.instLocallyConvexSpace`: `ğ““^{n}_{K}(E, F)` is a locally convex
  topological vector space.

## Notation

- `ğ““^{n}_{K}(E, F)`:  the space of `n`-times continuously differentiable functions `E â†’ F`
  which vanish outside of `K`.
- `ğ““_{K}(E, F)`:  the space of smooth (infinitely differentiable) functions `E â†’ F`
  which vanish outside of `K`, i.e. `ğ““^{âŠ¤}_{K}(E, F)`.

## Implementation details

* The technical choice of spelling `EqOn f 0 Ká¶œ` in the definition, as opposed to `tsupport f âŠ† K`
  is to make rewriting `f x` to `0` easier when `x âˆ‰ K`.
* Since the most common case is by far the smooth case, we often reserve the "expected" name
  of a result/definition to this case, and add `WithOrder` to the declaration applying to
  any regularity.
* In `iteratedFDerivWithOrderLM`, we define the `i`-th iterated differentiation operator as
  a map from `ğ““^{n}_{K}` to `ğ““^{k}_{K}` without imposing relations on `n`, `k` and `i`. Of course
  this is defined as `0` if `k + i > n`. This creates some verbosity as all of these variables are
  explicit, but it allows the most flexibility while avoiding DTT hell.

## Tags

distributions
-/

@[expose] public section

open TopologicalSpace SeminormFamily Set Function Seminorm UniformSpace
open scoped BoundedContinuousFunction Topology NNReal ContDiff

variable (ğ•œ E F : Type*) [NontriviallyNormedField ğ•œ]
  [NormedAddCommGroup E] [NormedSpace â„ E]
  [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
  {n k : â„•âˆ} {K : Compacts E}

/-- The type of bundled `n`-times continuously differentiable maps which vanish outside of a fixed
compact set `K`. -/
structure ContDiffMapSupportedIn (n : â„•âˆ) (K : Compacts E) : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected zero_on_compl' : EqOn toFun 0 Ká¶œ

/-- Notation for the space of bundled `n`-times continuously differentiable
functions with support in a compact set `K`. -/
scoped[Distributions] notation "ğ““^{" n "}_{"K"}(" E ", " F ")" =>
  ContDiffMapSupportedIn E F n K

/-- Notation for the space of bundled smooth (infinitely differentiable)
functions with support in a compact set `K`. -/
scoped[Distributions] notation "ğ““_{"K"}(" E ", " F ")" =>
  ContDiffMapSupportedIn E F âŠ¤ K

open Distributions

/-- `ContDiffMapSupportedInClass B E F n K` states that `B` is a type of bundled `n`-times
continuously differentiable functions with support in the compact set `K`. -/
class ContDiffMapSupportedInClass (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  map_zero_on_compl (f : B) : EqOn f 0 Ká¶œ

open ContDiffMapSupportedInClass

namespace ContDiffMapSupportedInClass

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    [ContDiffMapSupportedInClass B E F n K] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    [ContDiffMapSupportedInClass B E F n K] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    have := HasCompactSupport.intro K.isCompact (map_zero_on_compl f)
    rcases (map_continuous f).bounded_above_of_compact_support this with âŸ¨C, hCâŸ©
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

end ContDiffMapSupportedInClass

namespace ContDiffMapSupportedIn

instance toContDiffMapSupportedInClass :
    ContDiffMapSupportedInClass ğ““^{n}_{K}(E, F) E F n K where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  map_zero_on_compl f := f.zero_on_compl'

variable {E F}

protected theorem contDiff (f : ğ““^{n}_{K}(E, F)) : ContDiff â„ n f := map_contDiff f
protected theorem zero_on_compl (f : ğ““^{n}_{K}(E, F)) : EqOn f 0 Ká¶œ := map_zero_on_compl f
protected theorem compact_supp (f : ğ““^{n}_{K}(E, F)) : HasCompactSupport f :=
  .intro K.isCompact (map_zero_on_compl f)

@[simp]
theorem toFun_eq_coe {f : ğ““^{n}_{K}(E, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.coe (f : ğ““^{n}_{K}(E, F)) : E â†’ F := f

initialize_simps_projections ContDiffMapSupportedIn (toFun â†’ coe, as_prefix coe)

@[ext]
theorem ext {f g : ğ““^{n}_{K}(E, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `ContDiffMapSupportedIn` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : ğ““^{n}_{K}(E, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  zero_on_compl' := h.symm â–¸ f.zero_on_compl

@[simp]
theorem coe_copy (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem coe_toBoundedContinuousFunction (f : ğ““^{n}_{K}(E, F)) :
   (f : BoundedContinuousFunction E F) = (f : E â†’ F) := rfl

section AddCommGroup

@[simps -fullyApplied]
instance : Zero ğ““^{n}_{K}(E, F) where
  zero := .mk 0 contDiff_zero_fun fun _ _ â†¦ rfl

@[simps -fullyApplied]
instance : Add ğ““^{n}_{K}(E, F) where
  add f g := .mk (f + g) (f.contDiff.add g.contDiff) <| by
    rw [â† add_zero 0]
    exact f.zero_on_compl.comp_leftâ‚‚ g.zero_on_compl

@[simps -fullyApplied]
instance : Neg ğ““^{n}_{K}(E, F) where
  neg f := .mk (-f) (f.contDiff.neg) <| by
    rw [â† neg_zero]
    exact f.zero_on_compl.comp_left

@[simps -fullyApplied]
instance instSub : Sub ğ““^{n}_{K}(E, F) where
  sub f g := .mk (f - g) (f.contDiff.sub g.contDiff) <| by
    rw [â† sub_zero 0]
    exact f.zero_on_compl.comp_leftâ‚‚ g.zero_on_compl

@[simps -fullyApplied]
instance instSMul {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
   SMul R ğ““^{n}_{K}(E, F) where
  smul c f := .mk (c â€¢ (f : E â†’ F)) (f.contDiff.const_smul c) <| by
    rw [â† smul_zero c]
    exact f.zero_on_compl.comp_left

instance : AddCommGroup ğ““^{n}_{K}(E, F) := fast_instance%
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) fun _ _ â†¦ rfl

variable (E F K n)

/-- Coercion as an additive homomorphism. -/
def coeHom : ğ““^{n}_{K}(E, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

variable {E F}

theorem coe_coeHom : (coeHom E F n K : ğ““^{n}_{K}(E, F) â†’ E â†’ F) = DFunLike.coe :=
  rfl

theorem coeHom_injective : Function.Injective (coeHom E F n K) := by
  rw [coe_coeHom]
  exact DFunLike.coe_injective

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ğ““^{n}_{K}(E, F) := fast_instance%
  (coeHom_injective n K).module R (coeHom E F n K) fun _ _ â†¦ rfl

end Module

protected theorem support_subset (f : ğ““^{n}_{K}(E, F)) : support f âŠ† K :=
  support_subset_iff'.mpr f.zero_on_compl

protected theorem tsupport_subset (f : ğ““^{n}_{K}(E, F)) : tsupport f âŠ† K :=
  closure_minimal f.support_subset K.isCompact.isClosed

protected theorem hasCompactSupport (f : ğ““^{n}_{K}(E, F)) : HasCompactSupport f :=
  HasCompactSupport.intro K.isCompact f.zero_on_compl

/-- Inclusion of unbundled `n`-times continuously differentiable function with support included
in a compact `K` into the space `ğ““^{n}_{K}`. -/
@[simps]
protected def of_support_subset {f : E â†’ F} (hf : ContDiff â„ n f) (hsupp : support f âŠ† K) :
    ğ““^{n}_{K}(E, F) where
  toFun := f
  contDiff' := hf
  zero_on_compl' := support_subset_iff'.mp hsupp

protected theorem bounded_iteratedFDeriv (f : ğ““^{n}_{K}(E, F)) {i : â„•} (hi : i â‰¤ n) :
    âˆƒ C, âˆ€ x, â€–iteratedFDeriv â„ i f xâ€– â‰¤ C :=
  Continuous.bounded_above_of_compact_support
    (f.contDiff.continuous_iteratedFDeriv <| (WithTop.le_coe rfl).mpr hi)
    (f.hasCompactSupport.iteratedFDeriv i)

/-- Inclusion of `ğ““^{n}_{K}(E, F)` into the space `E â†’áµ‡ F` of bounded continuous maps
as a `ğ•œ`-linear map.

This is subsumed by `toBoundedContinuousFunctionCLM` (not yet in Mathlib), which also bundles the
continuity. -/
@[simps -fullyApplied]
noncomputable def toBoundedContinuousFunctionLM : ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ F where
  toFun f := f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

-- Workaround for simps' automatic name generation: manually specifying names is not supported yet.
alias toBoundedContinuousFunctionLM_apply := toBoundedContinuousFunctionLM_apply_apply

lemma toBoundedContinuousFunctionLM_eq_of_scalars (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (toBoundedContinuousFunctionLM ğ•œ : ğ““^{n}_{K}(E, F) â†’ _) = toBoundedContinuousFunctionLM ğ•œ' :=
  rfl

variable (n k) in
/-- `iteratedFDerivWithOrderLM ğ•œ n k i` is the `ğ•œ`-linear-map sending `f : ğ““^{n}_{K}(E, F)` to
its `i`-th iterated derivative as an element of `ğ““^{k}_{K}(E, E [Ã—i]â†’L[â„] F)`.
This only makes mathematical sense if `k + i â‰¤ n`, otherwise we define it as the zero map.

See `iteratedFDerivLM` for the very common case where everything is infinitely differentiable.

This is subsumed by `iteratedFDerivWithOrderCLM` (not yet in Mathlib), which also bundles the
continuity. -/
noncomputable def iteratedFDerivWithOrderLM (i : â„•) :
    ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] ğ““^{k}_{K}(E, E [Ã—i]â†’L[â„] F) where
  /-
  Note: it is tempting to define this as some linear map if `k + i â‰¤ n`,
  and the zero map otherwise. However, we would lose the definitional equality between
  `iteratedFDerivWithOrderLM ğ•œ n k i f` and `iteratedFDerivWithOrderLM â„ n k i f`.

  This is caused by the fact that the equality `f (if p then x else y) = if p then f x else f y`
  is not definitional.
  -/
  toFun f :=
    if hi : k + i â‰¤ n then
      .of_support_subset
        (f.contDiff.iteratedFDeriv_right <| by exact_mod_cast hi)
        ((support_iteratedFDeriv_subset i).trans f.tsupport_subset)
    else 0
  map_add' f g := by
    split_ifs with hi
    Â· have hi' : (i : WithTop â„•âˆ) â‰¤ n := by exact_mod_cast le_of_add_le_right hi
      ext
      simp [iteratedFDeriv_add (f.contDiff.of_le hi') (g.contDiff.of_le hi')]
    Â· simp
  map_smul' c f := by
    split_ifs with hi
    Â· have hi' : (i : WithTop â„•âˆ) â‰¤ n := by exact_mod_cast le_of_add_le_right hi
      ext
      simp [iteratedFDeriv_const_smul_apply (f.contDiff.of_le hi').contDiffAt]
    Â· simp

@[simp]
lemma iteratedFDerivWithOrderLM_apply {i : â„•} (f : ğ““^{n}_{K}(E, F)) :
    iteratedFDerivWithOrderLM ğ•œ n k i f = if k + i â‰¤ n then iteratedFDeriv â„ i f else 0 := by
  rw [ContDiffMapSupportedIn.iteratedFDerivWithOrderLM]
  split_ifs <;> rfl

lemma iteratedFDerivWithOrderLM_apply_of_le {i : â„•} (f : ğ““^{n}_{K}(E, F)) (hin : k + i â‰¤ n) :
    iteratedFDerivWithOrderLM ğ•œ n k i f = iteratedFDeriv â„ i f := by
  simp [hin]

lemma iteratedFDerivWithOrderLM_apply_of_gt {i : â„•} (f : ğ““^{n}_{K}(E, F)) (hin : Â¬ (k + i â‰¤ n)) :
    iteratedFDerivWithOrderLM ğ•œ n k i f = 0 := by
  ext : 1
  simp [hin]

lemma iteratedFDerivWithOrderLM_eq_of_scalars {i : â„•} (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (iteratedFDerivWithOrderLM ğ•œ n k i : ğ““^{n}_{K}(E, F) â†’ _)
      = iteratedFDerivWithOrderLM ğ•œ' n k i :=
  rfl

/-- `iteratedFDerivLM ğ•œ i` is the `ğ•œ`-linear-map sending `f : ğ““_{K}(E, F)` to
its `i`-th iterated derivative as an element of `ğ““_{K}(E, E [Ã—i]â†’L[â„] F)`.

See also `iteratedFDerivWithOrderLM` if you need more control on the regularities.

This is subsumed by `iteratedFDerivCLM` (not yet in Mathlib), which also bundles the
continuity. -/
noncomputable def iteratedFDerivLM (i : â„•) :
    ğ““_{K}(E, F) â†’â‚—[ğ•œ] ğ““_{K}(E, E [Ã—i]â†’L[â„] F) where
  toFun f := .of_support_subset
    (f.contDiff.iteratedFDeriv_right le_rfl)
    ((support_iteratedFDeriv_subset i).trans f.tsupport_subset)
  map_add' f g := by
    have hi : (i : WithTop â„•âˆ) â‰¤ âˆ := by exact_mod_cast le_top
    ext
    simp [iteratedFDeriv_add (f.contDiff.of_le hi) (g.contDiff.of_le hi)]
  map_smul' c f := by
    have hi : (i : WithTop â„•âˆ) â‰¤ âˆ := by exact_mod_cast le_top
    ext
    simp [iteratedFDeriv_const_smul_apply (f.contDiff.of_le hi).contDiffAt]

@[simp]
lemma iteratedFDerivLM_apply {i : â„•} (f : ğ““_{K}(E, F)) :
    iteratedFDerivLM ğ•œ i f = iteratedFDeriv â„ i f :=
  rfl

/-- Note: this turns out to be a definitional equality thanks to decidablity of the order
on `â„•âˆ`. This means we could have *defined* `iteratedFDerivLM` this way, but we avoid it
to make sure that `if`s won't appear in the smooth case. -/
lemma iteratedFDerivLM_eq_withOrder (i : â„•) :
    (iteratedFDerivLM ğ•œ i : ğ““_{K}(E, F) â†’â‚—[ğ•œ] _) = iteratedFDerivWithOrderLM ğ•œ âŠ¤ âŠ¤ i :=
  rfl

lemma iteratedFDerivLM_eq_of_scalars {i : â„•} (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (iteratedFDerivLM ğ•œ i : ğ““_{K}(E, F) â†’ _) = iteratedFDerivLM ğ•œ' i :=
  rfl

variable (n) in
/-- `structureMapLM ğ•œ n i` is the `ğ•œ`-linear-map sending `f : ğ““^{n}_{K}(E, F)` to its
`i`-th iterated derivative as an element of `E â†’áµ‡ (E [Ã—i]â†’L[â„] F)`. In other words, it
is the composition of `toBoundedContinuousFunctionLM ğ•œ` and `iteratedFDerivWithOrderLM ğ•œ n 0 i`.
This only makes mathematical sense if `i â‰¤ n`, otherwise we define it as the zero map.

We call these "structure maps" because they define the topology on `ğ““^{n}_{K}(E, F)`.

This is subsumed by `structureMapCLM`, which also bundles the
continuity. -/
noncomputable def structureMapLM (i : â„•) :
    ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ (E [Ã—i]â†’L[â„] F) :=
  toBoundedContinuousFunctionLM ğ•œ âˆ˜â‚— iteratedFDerivWithOrderLM ğ•œ n 0 i

lemma structureMapLM_eq {i : â„•} :
    (structureMapLM ğ•œ âŠ¤ i : ğ““_{K}(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ (E [Ã—i]â†’L[â„] F)) =
      (toBoundedContinuousFunctionLM ğ•œ : ğ““_{K}(E, E [Ã—i]â†’L[â„] F) â†’â‚—[ğ•œ] E â†’áµ‡ (E [Ã—i]â†’L[â„] F)) âˆ˜â‚—
      (iteratedFDerivLM ğ•œ i : ğ““_{K}(E, F) â†’â‚—[ğ•œ] ğ““_{K}(E, E [Ã—i]â†’L[â„] F)) :=
  rfl

lemma structureMapLM_apply_withOrder {i : â„•} (f : ğ““^{n}_{K}(E, F)) :
    structureMapLM ğ•œ n i f = if i â‰¤ n then iteratedFDeriv â„ i f else 0 := by
  simp [structureMapLM]

lemma structureMapLM_apply {i : â„•} (f : ğ““_{K}(E, F)) :
    structureMapLM ğ•œ âŠ¤ i f = iteratedFDeriv â„ i f := by
  simp [structureMapLM_eq]

lemma structureMapLM_eq_of_scalars {i : â„•} (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (structureMapLM ğ•œ n i : ğ““^{n}_{K}(E, F) â†’ _) = structureMapLM ğ•œ' n i :=
  rfl

section Topology

noncomputable instance topologicalSpace : TopologicalSpace ğ““^{n}_{K}(E, F) :=
  â¨… (i : â„•), induced (structureMapLM â„ n i) inferInstance

noncomputable instance uniformSpace : UniformSpace ğ““^{n}_{K}(E, F) := .replaceTopology
  (â¨… (i : â„•), UniformSpace.comap (structureMapLM â„ n i) inferInstance)
  toTopologicalSpace_iInf.symm

protected theorem uniformSpace_eq_iInf : (uniformSpace : UniformSpace ğ““^{n}_{K}(E, F)) =
    â¨… (i : â„•), UniformSpace.comap (structureMapLM â„ n i) inferInstance :=
  UniformSpace.replaceTopology_eq _ toTopologicalSpace_iInf.symm

instance isTopologicalAddGroup : IsTopologicalAddGroup ğ““^{n}_{K}(E, F) :=
  topologicalAddGroup_iInf fun _ â†¦ topologicalAddGroup_induced _

instance isUniformAddGroup : IsUniformAddGroup ğ““^{n}_{K}(E, F) := by
  rw [ContDiffMapSupportedIn.uniformSpace_eq_iInf]
  exact isUniformAddGroup_iInf fun _ â†¦ IsUniformAddGroup.comap _

instance continuousSMul : ContinuousSMul ğ•œ ğ““^{n}_{K}(E, F) :=
  continuousSMul_iInf fun i â†¦ continuousSMul_induced (structureMapLM ğ•œ n i)

instance locallyConvexSpace : LocallyConvexSpace â„ ğ““^{n}_{K}(E, F) :=
  LocallyConvexSpace.iInf fun _ â†¦ LocallyConvexSpace.induced _

variable (n) in
/-- `structureMapCLM ğ•œ n i` is the continuous `ğ•œ`-linear-map sending `f : ğ““^{n}_{K}(E, F)` to its
`i`-th iterated derivative as an element of `E â†’áµ‡ (E [Ã—i]â†’L[â„] F)`.
This only makes mathematical sense if `i â‰¤ n`, otherwise we define it as the zero map.

We call these "structure maps" because they define the topology on `ğ““^{n}_{K}(E, F)`. -/
noncomputable def structureMapCLM (i : â„•) :
    ğ““^{n}_{K}(E, F) â†’L[ğ•œ] E â†’áµ‡ (E [Ã—i]â†’L[â„] F) where
  toLinearMap := structureMapLM ğ•œ n i
  cont := continuous_iInf_dom continuous_induced_dom

lemma structureMapCLM_apply_withOrder {i : â„•} (f : ğ““^{n}_{K}(E, F)) :
    structureMapCLM ğ•œ n i f = if i â‰¤ n then iteratedFDeriv â„ i f else 0 := by
  simp [structureMapCLM, structureMapLM_apply_withOrder]

lemma structureMapCLM_apply {i : â„•} (f : ğ““_{K}(E, F)) :
    structureMapCLM ğ•œ âŠ¤ i f = iteratedFDeriv â„ i f := by
  simp [structureMapCLM, structureMapLM_apply]

lemma structureMapCLM_eq_of_scalars {i : â„•} (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (structureMapCLM ğ•œ n i : ğ““^{n}_{K}(E, F) â†’ _) = structureMapCLM ğ•œ' n i :=
  rfl

/-- The **universal property** of the topology on `ğ““^{n}_{K}(E, F)`: a map to `ğ““^{n}_{K}(E, F)`
is continuous if and only if its composition with each structure map
`structureMapCLM â„ n i : ğ““^{n}_{K}(E, F) â†’ (E â†’áµ‡ (E [Ã—i]â†’L[â„] F))` is continuous.

Since `structureMapCLM â„ n i` is zero whenever `i > n`, it suffices to check it for `i â‰¤ n`,
as proven by `continuous_iff_comp_withOrder`. -/
-- Note: if needed, we could allow an extra parameter `ğ•œ` in case the user wants to use
-- `structureMapCLM ğ•œ n i`.
theorem continuous_iff_comp {X} [TopologicalSpace X] (Ï† : X â†’ ğ““^{n}_{K}(E, F)) :
    Continuous Ï† â†” âˆ€ i, Continuous (structureMapCLM â„ n i âˆ˜ Ï†) := by
  simp [continuous_iInf_rng, continuous_induced_rng, structureMapCLM]

/-- The **universal property** of the topology on `ğ““^{n}_{K}(E, F)`: a map to `ğ““^{n}_{K}(E, F)`
is continuous if and only if its composition with the structure map
`structureMapCLM â„ n i : ğ““^{n}_{K}(E, F) â†’ (E â†’áµ‡ (E [Ã—i]â†’L[â„] F))` is continuous for each
`i â‰¤ n`. -/
-- Note: if needed, we could allow an extra parameter `ğ•œ` in case the user wants to use
-- `structureMapCLM ğ•œ n i`.
theorem continuous_iff_comp_withOrder {X : Type*} [TopologicalSpace X] (Ï† : X â†’ ğ““^{n}_{K}(E, F)) :
    Continuous Ï† â†” âˆ€ (i : â„•), i â‰¤ n â†’ Continuous (structureMapCLM â„ n i âˆ˜ Ï†) := by
  rw [continuous_iff_comp]
  congrm (âˆ€ i, ?_)
  by_cases hin : i â‰¤ n <;> simp only [hin, true_imp_iff, false_imp_iff, iff_true]
  refine continuous_zero.congr fun x â†¦ ?_
  ext t : 1
  simp [hin, structureMapCLM_apply_withOrder]

end Topology

end ContDiffMapSupportedIn
