/-
Copyright (c) 2023 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker, Luigi Massacci
-/

import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Topology.ContinuousMap.Bounded.Normed
import Mathlib.Topology.Sets.Compacts

/-!
# Continuously differentiable functions supported in a compact

This file develops the basic theory of `n`-times continuously differentiable functions with support
contained in a given compact.

Given `n : â„•âˆ`and a compact `K` of a normed space `E`, we consider the type of functions `f : E â†’ F`
(where `F` is a normed vector space) such that:

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` vanishes outside of a compact: `EqOn f 0 Ká¶œ`.

## Main definitions

- `ContDiffMapSupportedIn E F n K`: the type of `n`-times continuously differentiable
  functions `E â†’ F` which vanish outside of `K`.

## Notation

- `ğ““^{n}_{K}(E, F)`:  the space of `n`-times continuously differentiable functions `E â†’ F`
  which vanish outside of `K`.
- `ğ““_{K}(E, F)`:  the space of smooth (infinitely differentiable) functions `E â†’ F`
  which vanish outside of `K`, i.e. `ğ““^{âŠ¤}_{K}(E, F)`.

## Implementation details

Thes technical choice of spelling `EqOn f 0 Ká¶œ` as opposed to `support f = K` is to make the
development somewhat easier.

## Tags

distributions
-/

open TopologicalSpace SeminormFamily Set Function Seminorm UniformSpace
open scoped BoundedContinuousFunction Topology NNReal

variable (ğ•œ E F : Type*) [NontriviallyNormedField ğ•œ]
variable [NormedAddCommGroup E] [NormedSpace â„ E]
variable [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
variable {n : â„•âˆ} {K : Compacts E}

/-- The type of `n`-times continuously differentiable maps which vanish outside of a fixed
compact `K`. -/
structure ContDiffMapSupportedIn (n : â„•âˆ) (K : Compacts E) : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected zero_on_compl' : EqOn toFun 0 Ká¶œ

/-- Notation for the space of `n`-times continuously differentiable
functions with support in a compact `K` -/
scoped[Distributions] notation "ğ““^{" n "}_{"K"}(" E ", " F ")" =>
  ContDiffMapSupportedIn E F n K

/-- Notation for the space of smooth (inifinitely differentiable)
functions with support in a compact `K` -/
scoped[Distributions] notation "ğ““_{"K"}(" E ", " F ")" =>
  ContDiffMapSupportedIn E F âŠ¤ K

open Distributions

/-- `BoundedContinuousMapClass B E F` states that `B` is a type of `n`-times continously
differentiable functions with support in the compact `K`. -/
class ContDiffMapSupportedInClass (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  map_zero_on_compl (f : B) : EqOn f 0 Ká¶œ

open ContDiffMapSupportedInClass

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    [ContDiffMapSupportedInClass B E F n K] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆ) (K : outParam <| Compacts E)
    [ContDiffMapSupportedInClass B E F n K] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    have := HasCompactSupport.intro K.isCompact (map_zero_on_compl f)
    rcases (map_continuous f).bounded_above_of_compact_support this with âŸ¨C, hCâŸ©
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

namespace ContDiffMapSupportedIn

instance toContDiffMapSupportedInClass :
    ContDiffMapSupportedInClass ğ““^{n}_{K}(E, F) E F n K where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  map_zero_on_compl f := f.zero_on_compl'

variable {E F}

protected theorem contDiff (f : ğ““^{n}_{K}(E, F)) : ContDiff â„ n f := map_contDiff f
protected theorem zero_on_compl (f : ğ““^{n}_{K}(E, F)) : EqOn f 0 Ká¶œ := map_zero_on_compl f
protected theorem compact_supp (f : ğ““^{n}_{K}(E, F)) : HasCompactSupport f :=
  .intro K.isCompact (map_zero_on_compl f)

@[simp]
theorem toFun_eq_coe {f : ğ““^{n}_{K}(E, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.apply (f : ğ““^{n}_{K}(E, F)) : E â†’F  := f

-- this must come after the coe_to_fun definition.
initialize_simps_projections ContDiffMapSupportedIn (toFun â†’ apply)

@[ext]
theorem ext {f g : ğ““^{n}_{K}(E, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `BoundedContDiffMap` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : ğ““^{n}_{K}(E, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  zero_on_compl' := h.symm â–¸ f.zero_on_compl

@[simp]
theorem coe_copy (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ğ““^{n}_{K}(E, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem toBoundedContinuousFunction_apply (f : ğ““^{n}_{K}(E, F)) (x : E) :
   (f : BoundedContinuousFunction E F) x  = (f x) := rfl

theorem _root_.Set.EqOn.comp_leftâ‚‚ {Î± Î² Î´ Î³} {op : Î± â†’ Î² â†’ Î´} {aâ‚ aâ‚‚ : Î³ â†’ Î±}
    {bâ‚ bâ‚‚ : Î³ â†’ Î²} {s : Set Î³} (ha : s.EqOn aâ‚ aâ‚‚) (hb : s.EqOn bâ‚ bâ‚‚) :
    s.EqOn (fun x â†¦ op (aâ‚ x) (bâ‚ x)) (fun x â†¦ op (aâ‚‚ x) (bâ‚‚ x)) := fun _ hx =>
  congr_argâ‚‚ _ (ha hx) (hb hx)

section AddCommGroup

instance : Zero ğ““^{n}_{K}(E, F) where
  zero := ContDiffMapSupportedIn.mk 0 contDiff_zero_fun fun _ _ â†¦ rfl

@[simp]
lemma coe_zero : (0 : ğ““^{n}_{K}(E, F)) = (0 : E â†’ F) :=
  rfl

@[simp]
lemma zero_apply (x : E) : (0 : ğ““^{n}_{K}(E, F)) x = 0 :=
  rfl

instance : Add ğ““^{n}_{K}(E, F) where
  add f g := ContDiffMapSupportedIn.mk (f + g) (f.contDiff.add g.contDiff) <| by
    rw [â† add_zero 0]
    exact f.zero_on_compl.comp_leftâ‚‚ g.zero_on_compl

@[simp]
lemma coe_add (f g : ğ““^{n}_{K}(E, F)) : (f + g : ğ““^{n}_{K}(E, F)) = (f : E â†’ F) + g :=
  rfl

@[simp]
lemma add_apply (f g : ğ““^{n}_{K}(E, F)) (x : E) : (f + g) x = f x + g x :=
  rfl

instance : Neg ğ““^{n}_{K}(E, F) where
  neg f := ContDiffMapSupportedIn.mk (-f) (f.contDiff.neg) <| by
    rw [â† neg_zero]
    exact f.zero_on_compl.comp_left

instance instSub : Sub ğ““^{n}_{K}(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f - g, (f.contDiff).sub (g.contDiff), by
      intro x hx
      simp [f.zero_on_compl hx, g.zero_on_compl hx]
    âŸ©
  âŸ©

instance instSMul {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
   SMul R ğ““^{n}_{K}(E, F) :=
âŸ¨fun c f â†¦
  ContDiffMapSupportedIn.mk (c â€¢ (f : E â†’ F)) (f.contDiff.const_smul c) <| by
    rw [â† smul_zero c]
    exact f.zero_on_compl.comp_leftâŸ©

@[simp]
lemma coe_smul {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    (c : R) (f : ğ““^{n}_{K}(E, F)) : (c â€¢ f : ğ““^{n}_{K}(E, F)) = c â€¢ (f : E â†’ F) :=
  rfl

@[simp]
lemma smul_apply {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    (c : R) (f : ğ““^{n}_{K}(E, F)) (x : E) : (c â€¢ f) x = c â€¢ (f x) :=
  rfl

instance instNSMul : SMul â„• ğ““^{n}_{K}(E, F) :=
 âŸ¨fun c f â†¦
    {
      toFun := c â€¢ f
      contDiff' := (f.contDiff).const_smul c
      zero_on_compl' := by
        rw [â† smul_zero c]
        exact f.zero_on_compl.comp_left
    }
  âŸ©

instance instZSMul : SMul â„¤ ğ““^{n}_{K}(E, F) :=
 âŸ¨fun c f â†¦
    {
      toFun := c â€¢ f
      contDiff' := (f.contDiff).const_smul c
      zero_on_compl' := by
        rw [â† smul_zero c]
        exact f.zero_on_compl.comp_left
    }
  âŸ©

instance : AddCommGroup ğ““^{n}_{K}(E, F) :=
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl

variable (E F K n)

/-- Coercion as an additive homomorphism. -/
def coeHom : ğ““^{n}_{K}(E, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

variable {E F}

theorem coe_coeHom : (coeHom E F n K : ğ““^{n}_{K}(E, F) â†’ E â†’ F) = DFunLike.coe :=
  rfl

theorem coeHom_injective : Function.Injective (coeHom E F n K) := by
  rw [coe_coeHom]
  exact DFunLike.coe_injective

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ğ““^{n}_{K}(E, F) :=
  (coeHom_injective n K).module R (coeHom E F n K) fun _ _ => rfl

end Module

protected theorem support_subset (f : ğ““^{n}_{K}(E, F)) : support f âŠ† K :=
  support_subset_iff'.mpr f.zero_on_compl

protected theorem tsupport_subset (f : ğ““^{n}_{K}(E, F)) : tsupport f âŠ† K :=
  closure_minimal f.support_subset K.isCompact.isClosed

protected theorem hasCompactSupport (f : ğ““^{n}_{K}(E, F)) : HasCompactSupport f :=
  HasCompactSupport.intro K.isCompact f.zero_on_compl

/-- Inclusion of unbundled `n`-times continuously differentiable function with support included
in a compact `K` into the space `ğ““^{n}_{K}`. -/
protected def of_support_subset {f : E â†’ F} (hf : ContDiff â„ n f) (hsupp : support f âŠ† K) :
    ğ““^{n}_{K}(E, F) where
  toFun := f
  contDiff' := hf
  zero_on_compl' := support_subset_iff'.mp hsupp

protected theorem bounded_iteratedFDeriv (f : ğ““^{n}_{K}(E, F)) {i : â„•} (hi : i â‰¤ n) :
    âˆƒ C, âˆ€ x, â€–iteratedFDeriv â„ i f xâ€– â‰¤ C :=
  Continuous.bounded_above_of_compact_support
    (f.contDiff.continuous_iteratedFDeriv <| (WithTop.le_coe rfl).mpr hi)
    (f.hasCompactSupport.iteratedFDeriv i)


/-- Inclusion of `ğ““^{n}_{K}(E, F)` into the space `E â†’áµ‡ F` of bounded continuous maps
as a `ğ•œ`-linear map. -/
@[simps]
noncomputable def toBoundedContinuousFunctionâ‚— : ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ F  where
  toFun f := f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

/-- Wrapper for `iteratedFDeriv i` on `ğ““^{n}_{K}(E, F)`,
as a map into `ğ““^{n-i}_{K}(E, E [Ã—i]â†’L[â„] F)`. -/
noncomputable def iteratedFDeriv' (i : â„•) (f : ğ““^{n}_{K}(E, F)) :
    ğ““^{n-i}_{K}(E, E [Ã—i]â†’L[â„] F) :=
  if hi : i â‰¤ n then
    .of_support_subset
    (f.contDiff.iteratedFDeriv_right <| (WithTop.coe_le_coe.mpr ((tsub_add_cancel_of_le hi).le)))
    ((support_iteratedFDeriv_subset i).trans f.tsupport_subset)
  else 0

@[simp]
lemma iteratedFDeriv'_apply (i : â„•) (f : ğ““^{n}_{K}(E, F)) (x : E) :
    f.iteratedFDeriv' i x = if i â‰¤ n then iteratedFDeriv â„ i f x else 0 := by
  rw [ContDiffMapSupportedIn.iteratedFDeriv']
  split_ifs <;> rfl

@[simp]
lemma coe_iteratedFDeriv'_of_le {i : â„•} (hin : i â‰¤ n) (f : ğ““^{n}_{K}(E, F)) :
    f.iteratedFDeriv' i = iteratedFDeriv â„ i f := by
  ext : 1
  rw [iteratedFDeriv'_apply]
  exact dif_pos hin

@[simp]
lemma coe_iteratedFDeriv'_of_gt {i : â„•} (hin : i > n) (f : ğ““^{n}_{K}(E, F)) :
    f.iteratedFDeriv' i = 0 := by
  ext : 1
  rw [iteratedFDeriv'_apply]
  exact dif_neg (not_le_of_gt hin)

@[simp]
lemma coe_iteratedFDeriv'_of_gt' {i : â„•} (hin : i > n) :
    (iteratedFDeriv' i : ğ““^{n}_{K}(E, F) â†’ _) = 0 := by
  ext : 2
  rw [iteratedFDeriv'_apply]
  exact dif_neg (not_le_of_gt hin)

lemma iteratedFDeriv'_add (i : â„•) {f g : ğ““^{n}_{K}(E, F)} :
    (f + g).iteratedFDeriv' i = f.iteratedFDeriv' i + g.iteratedFDeriv' i := by
  ext : 1
  simp only [iteratedFDeriv'_apply, add_apply]
  split_ifs with hin
  Â· refine iteratedFDeriv_add_apply (ContDiff.contDiffAt ?_) (ContDiff.contDiffAt ?_)
    Â· exact f.contDiff.of_le (by exact_mod_cast hin)
    Â· exact g.contDiff.of_le (by exact_mod_cast hin)
  Â· rw [add_zero]

lemma iteratedFDeriv'_smul (i : â„•) {c : ğ•œ} {f : ğ““^{n}_{K}(E, F)} :
    (c â€¢ f).iteratedFDeriv' i = c â€¢ f.iteratedFDeriv' i := by
  ext : 1
  simp only [iteratedFDeriv'_apply, smul_apply]
  split_ifs with hin
  Â· apply iteratedFDeriv_const_smul_apply
    refine ContDiff.contDiffAt <| f.contDiff.of_le (by exact_mod_cast hin)
  Â· rw [smul_zero]

/-- Wrapper for iteratedFDeriv' as a `ğ•œ`-linear map. -/
@[simps]
noncomputable def iteratedFDerivâ‚—' (i : â„•) :
    ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] ğ““^{n-i}_{K}(E, E [Ã—i]â†’L[â„] F) where
  toFun f := f.iteratedFDeriv' i
  map_add' _ _ := iteratedFDeriv'_add i
  map_smul' _ _ := iteratedFDeriv'_smul ğ•œ i

lemma iteratedFDerivâ‚—'_eq_iteratedFDeriv' (i : â„•) :
  (iteratedFDerivâ‚—' ğ•œ i : ğ““^{n}_{K}(E, F) â†’ _) = (iteratedFDeriv' i : _) := by
  congr

lemma iteratedFDeriv'_zero (i : â„•) :
    (0 : ğ““^{n}_{K}(E, F)).iteratedFDeriv' i = 0 :=
  map_zero (iteratedFDerivâ‚—' â„ i)

/-- The composition of `ContDiffMapSupportedIn.toBoundedContinuousFunctionâ‚—` and
`ContDiffMapSupportedIn.iteratedFDerivâ‚—`. We define this as a separate `abbrev` because this family
of maps is used a lot for defining and using the topology on `ContDiffMapSupportedIn`, and Lean
takes a long time to infer the type of `toBoundedContinuousFunctionâ‚— ğ•œ âˆ˜â‚— iteratedFDerivâ‚— ğ•œ i`. -/
noncomputable def iteratedFDeriv_toBoundedContinuousFunctionâ‚— (i : â„•) :
    ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] E â†’áµ‡ (E [Ã—i]â†’L[â„] F) :=
  toBoundedContinuousFunctionâ‚— ğ•œ âˆ˜â‚— iteratedFDerivâ‚—' ğ•œ i

section Topology

noncomputable instance topologicalSpace : TopologicalSpace ğ““^{n}_{K}(E, F) :=
  â¨… (i : â„•), induced (iteratedFDeriv_toBoundedContinuousFunctionâ‚— â„ i) inferInstance

noncomputable instance uniformSpace : UniformSpace ğ““^{n}_{K}(E, F) := .replaceTopology
  (â¨… (i : â„•), UniformSpace.comap (iteratedFDeriv_toBoundedContinuousFunctionâ‚— â„ i) inferInstance)
  toTopologicalSpace_iInf.symm

protected theorem uniformSpace_eq_iInf : (uniformSpace : UniformSpace ğ““^{n}_{K}(E, F)) =
    â¨… (i : â„•), UniformSpace.comap (iteratedFDeriv_toBoundedContinuousFunctionâ‚— â„ i)
      inferInstance :=
  UniformSpace.replaceTopology_eq _ toTopologicalSpace_iInf.symm

instance : IsUniformAddGroup ğ““^{n}_{K}(E, F) := by
  rw [ContDiffMapSupportedIn.uniformSpace_eq_iInf]
  refine isUniformAddGroup_iInf (fun i â†¦ ?_)
  exact IsUniformAddGroup.comap _

instance : ContinuousSMul ğ•œ ğ““^{n}_{K}(E, F) := by
  refine continuousSMul_iInf
    (fun i â†¦ continuousSMul_induced (iteratedFDeriv_toBoundedContinuousFunctionâ‚— ğ•œ i))

instance : LocallyConvexSpace â„ ğ““^{n}_{K}(E, F) :=
  LocallyConvexSpace.iInf fun _ â†¦ LocallyConvexSpace.induced _

lemma continuous_iff_comp {X} [TopologicalSpace X] (Ï† : X â†’ ğ““^{n}_{K}(E, F)) :
    Continuous Ï† â†” âˆ€ i, Continuous (iteratedFDeriv_toBoundedContinuousFunctionâ‚— â„ i âˆ˜ Ï†) := by
  simp_rw [continuous_iInf_rng, continuous_induced_rng]


variable (E F n K)

/-- The seminorms on the space `ğ““^{n}_{K}(E, F)` given by sup norm on the `i`-th derivative. -/
protected noncomputable def seminorm (i : â„•) : Seminorm ğ•œ ğ““^{n}_{K}(E, F) :=
  (normSeminorm ğ•œ (E â†’áµ‡ (E [Ã—i]â†’L[â„] F))).comp (iteratedFDeriv_toBoundedContinuousFunctionâ‚— ğ•œ i)

/-- The seminorms on the space `ğ““^{n}_{K}(E, F)` given by sup of the
`ContDiffMapSupportedIn.seminorm k`for `k â‰¤ i`. -/
protected noncomputable def seminorm' (i : â„•) : Seminorm ğ•œ ğ““^{n}_{K}(E, F) :=
  (Finset.Iic i).sup (ContDiffMapSupportedIn.seminorm ğ•œ E F n K)

protected theorem withSeminorms :
    WithSeminorms (ContDiffMapSupportedIn.seminorm ğ•œ E F n K) := by
  let p : SeminormFamily ğ•œ ğ““^{n}_{K}(E, F) ((_ : â„•) Ã— Fin 1) :=
    SeminormFamily.sigma fun i â†¦ fun _ â†¦
      (normSeminorm ğ•œ (E â†’áµ‡ (E [Ã—i]â†’L[â„] F))).comp (iteratedFDeriv_toBoundedContinuousFunctionâ‚— ğ•œ i)
  have : WithSeminorms p :=
    withSeminorms_iInf fun i â†¦ LinearMap.withSeminorms_induced (norm_withSeminorms _ _) _
  exact this.congr_equiv (Equiv.sigmaUnique _ _).symm

protected theorem withSeminorms' :
    WithSeminorms (ContDiffMapSupportedIn.seminorm' ğ•œ E F n K) :=
  (ContDiffMapSupportedIn.withSeminorms ğ•œ E F n K).partial_sups

variable {E F n K}

@[simp]
protected theorem seminorm_apply (i : â„•) (f : ğ““^{n}_{K}(E, F)) :
    ContDiffMapSupportedIn.seminorm ğ•œ E F n K i f =
      â€–(f.iteratedFDeriv' i : E â†’áµ‡ (E [Ã—i]â†’L[â„] F))â€– :=
  rfl

protected theorem seminorm_eq_bot {i : â„•} (hin : n < i) :
    ContDiffMapSupportedIn.seminorm ğ•œ E F n K i = âŠ¥ := by
  ext f
  rw [ContDiffMapSupportedIn.seminorm_apply,
      coe_iteratedFDeriv'_of_gt hin]
  exact norm_zero

theorem norm_toBoundedContinuousFunctionâ‚— (f : ğ““^{n}_{K}(E, F)) :
    â€–toBoundedContinuousFunctionâ‚— ğ•œ fâ€– = ContDiffMapSupportedIn.seminorm ğ•œ E F n K 0 f := by
  simp only [BoundedContinuousFunction.norm_eq_iSup_norm, toBoundedContinuousFunctionâ‚—_apply_apply,
    ContDiffMapSupportedIn.seminorm_apply]
  simp only [toBoundedContinuousFunction_apply, iteratedFDeriv'_apply, CharP.cast_eq_zero,
  zero_le, â†“reduceIte, norm_iteratedFDeriv_zero]

/-- The inclusion of the space  `ğ““^{n}_{K}(E, F)` into the space `E â†’áµ‡ F` of bounded continuous
functions as a continuous `ğ•œ`-linear map. -/
@[simps!]
noncomputable def toBoundedContinuousFunctionCLM : ğ““^{n}_{K}(E, F) â†’L[ğ•œ] E â†’áµ‡ F :=
  { toLinearMap := toBoundedContinuousFunctionâ‚— ğ•œ
    cont := show Continuous (toBoundedContinuousFunctionâ‚— ğ•œ) by
      refine continuous_from_bounded (ContDiffMapSupportedIn.withSeminorms _ _ _ _ _)
        (norm_withSeminorms ğ•œ _) _ (fun _ â†¦ âŸ¨{0}, 1, fun f â†¦ ?_âŸ©)
      simp [Seminorm.comp_apply, coe_normSeminorm, norm_toBoundedContinuousFunctionâ‚—,
        one_smul, Finset.sup_singleton] }

protected theorem continuous_iff {X : Type*} [TopologicalSpace X] (Ï† : X â†’ ğ““^{n}_{K}(E, F)) :
    Continuous Ï† â†” âˆ€ (i : â„•) (_ : â†‘i â‰¤ n), Continuous
      (toBoundedContinuousFunctionâ‚— ğ•œ âˆ˜ ContDiffMapSupportedIn.iteratedFDeriv' i âˆ˜ Ï†) := by
  simp_rw [continuous_iInf_rng, continuous_induced_rng]
  constructor <;> intro H i
  Â· exact fun _ â†¦ H i
  Â· by_cases hin : i â‰¤ n
    Â· exact H i hin
    Â· simp [iteratedFDeriv_toBoundedContinuousFunctionâ‚—, iteratedFDerivâ‚—'_eq_iteratedFDeriv',
            coe_iteratedFDeriv'_of_gt' (lt_of_not_ge hin), continuous_zero]

end Topology

section fderiv

open Distributions

/-- Wrapper for `fderiv` on `ğ““^{n}_{K}(E, F)`, as a map into `ğ““^{n-1}_{K}(E, E â†’L[â„] F)` -/
protected noncomputable def fderiv' (f : ğ““^{n}_{K}(E, F)) :
    ğ““^{n-1}_{K}(E, E â†’L[â„] F) :=
  if hn : n = 0 then 0 else
    .of_support_subset
    (f.contDiff.fderiv_right <|
    (by exact_mod_cast (tsub_add_cancel_of_le <| ENat.one_le_iff_ne_zero.mpr hn).le))
    ((support_fderiv_subset â„).trans f.tsupport_subset)

@[simp]
lemma fderiv'_apply (f : ğ““^{n}_{K}(E, F)) (x : E) :
    f.fderiv' x = if n = 0 then 0 else fderiv â„ f x := by
  rw [ContDiffMapSupportedIn.fderiv']
  split_ifs <;> rfl

@[simp]
lemma coe_fderiv'_of_ne (hn : n â‰  0) (f : ğ““^{n}_{K}(E, F)) :
    f.fderiv' = fderiv â„ f := by
  ext : 1
  rw [fderiv'_apply]
  exact if_neg hn

@[simp]
lemma coe_fderiv'_zero (f : ğ““^{0}_{K}(E, F)) :
    f.fderiv' = 0 := by
  ext : 1
  rw [fderiv'_apply]
  exact if_pos rfl

/-- Bundling of `fderiv` as a `ğ•œ`-linear map. -/
@[simps]
noncomputable def fderivâ‚—' {n : â„•âˆ} : ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] ğ““^{n-1}_{K}(E, E â†’L[â„] F) where
  toFun f := f.fderiv'
  map_add' fâ‚ fâ‚‚ := by
    ext : 1
    simp only [fderiv'_apply, add_apply]
    split_ifs with hn
    Â· rw [add_zero]
    Â· rw [â† ne_eq, â† ENat.one_le_iff_ne_zero] at hn
      exact fderiv_add
        (fâ‚.contDiff.differentiable (by exact_mod_cast hn)).differentiableAt
        (fâ‚‚.contDiff.differentiable (by exact_mod_cast hn)).differentiableAt
  map_smul' c f := by
    ext : 1
    simp only [fderiv'_apply, smul_apply]
    split_ifs with hn
    Â· rw [smul_zero]
    Â· rw [â† ne_eq, â† ENat.one_le_iff_ne_zero] at hn
      exact fderiv_const_smul (f.contDiff.differentiable (by exact_mod_cast hn)).differentiableAt c

theorem _root_.ENat.eq_zero_or_add_one (i : â„•âˆ) : i = 0 âˆ¨ âˆƒ k, i = k + 1 := by
  refine or_iff_not_imp_left.mpr fun h â†¦ âŸ¨i - 1, ?_âŸ©
  rw [tsub_add_cancel_of_le (ENat.one_le_iff_ne_zero.mpr h)]

theorem seminorm_fderiv' (i : â„•) (f : ğ““^{n}_{K}(E, F)) :
    ContDiffMapSupportedIn.seminorm ğ•œ E (E â†’L[â„] F) (n - 1) K i f.fderiv' =
      ContDiffMapSupportedIn.seminorm ğ•œ E F n K (i+1) f := by
  simp_rw [ContDiffMapSupportedIn.seminorm_apply, BoundedContinuousFunction.norm_eq_iSup_norm]
  refine iSup_congr fun x â†¦ ?_
  simp only [toBoundedContinuousFunction_apply]
  rcases eq_or_ne n 0 with rfl | hn
  Â· simp [iteratedFDeriv'_zero]
  Â· rcases lt_or_ge (i : â„•âˆ) n with (hin|hin)
    Â· have hin' : i + 1 â‰¤ n := by
        exact Order.add_one_le_of_lt hin
      have hin'' : i â‰¤ n - 1 := by
        refine ENat.le_sub_of_add_le_left (ENat.one_ne_top) (add_comm _ (1 : â„•âˆ) â–¸ hin')
      simp [hin', hin'', hn, â† norm_iteratedFDeriv_fderiv]
    Â· have hin' : n - 1 < i:= by
        refine (ENat.add_one_le_iff ?_).mp ?_
        Â· refine ENat.sub_ne_top_iff.mpr (Or.inl (ne_top_of_le_ne_top (ENat.coe_ne_top i) hin))
        Â· rw [tsub_add_cancel_of_le (ENat.one_le_iff_ne_zero.mpr hn )]
          exact hin
      have hin'' : n < i + 1 := by
        exact lt_of_tsub_lt_tsub_right hin'
      simp [hin', hin'']

/-- Bundling of `fderiv'` as continuous `ğ•œ`-linear map. -/
@[simps! apply]
noncomputable def fderivCLM' : ğ““^{n}_{K}(E, F) â†’L[ğ•œ] ğ““^{n-1}_{K}(E, E â†’L[â„] F) where
  toLinearMap := fderivâ‚—' ğ•œ
  cont := by
    refine Seminorm.continuous_from_bounded  (Ï„â‚â‚‚ := RingHom.id ğ•œ)
      (ContDiffMapSupportedIn.withSeminorms ğ•œ E F n K)
      (ContDiffMapSupportedIn.withSeminorms ğ•œ E (E â†’L[â„] F) (n-1) K) _
      fun i â†¦ âŸ¨{i+1}, 1, fun f â†¦ ?_âŸ©
    simp only [Seminorm.comp_apply, fderivâ‚—'_apply,
      Finset.sup_singleton, one_smul]
    rw [seminorm_fderiv']

end fderiv

end ContDiffMapSupportedIn
