/-
Copyright (c) 2025 Anatole Dedecker. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Anatole Dedecker
-/
module

public import Mathlib.Analysis.Distribution.TestFunction
public import Mathlib.Analysis.LocallyConvex.StrongTopology

/-!
# Distributions
-/

@[expose] public section

open Function Seminorm SeminormFamily Set TopologicalSpace UniformSpace MeasureTheory
open scoped BoundedContinuousFunction NNReal Topology ContDiff Distributions

variable {ğ•œ ğ•‚ : Type*} [NontriviallyNormedField ğ•œ] [RCLike ğ•‚]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {Î© : Opens E}
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [NormedSpace ğ•‚ F]
  {n k : â„•âˆ}

-- TODO: def or abbrev?
variable (Î© F n) in
abbrev Distribution := ğ““^{n}(Î©, â„) â†’L[â„] F

-- TODO: I'm not sure these notations are good
/-- Notation for the space of distributions of order less than `n`. -/
scoped[Distributions] notation "ğ““'^{" n "}(" Î© ", " F ")" => Distribution Î© F n

/-- Notation for the space of distributions. -/
scoped[Distributions] notation "ğ““'(" Î© ", " F ")" => Distribution Î© F âŠ¤

noncomputable example : TopologicalSpace ğ““'(Î©, F) := inferInstance
example : IsTopologicalAddGroup ğ““'(Î©, F) := inferInstance

-- TODO: generalize `ContinuousLinearMap.continuousSMul`
example : ContinuousSMul â„ ğ““'(Î©, F) := inferInstance
example : LocallyConvexSpace â„ ğ““'(Î©, F) := inferInstance

namespace Distribution

section ofFun

variable [MeasurableSpace E] [OpensMeasurableSpace E]

variable (Î© n) in
noncomputable def ofFunWithOrder (f : E â†’ F) (Î¼ : Measure E := by volume_tac) :
    ğ““'^{n}(Î©, F) :=
  TestFunction.integralAgainstBilinCLM (ContinuousLinearMap.lsmul â„ â„) Î¼ f

variable (Î©) in
noncomputable def ofFun (f : E â†’ F) (Î¼ : Measure E := by volume_tac) : ğ““'(Î©, F) :=
  ofFunWithOrder Î© âŠ¤ f Î¼

-- TODO: be more consistent about the naming: which is Ï† and which is f ?

@[simp]
lemma ofFunWithOrder_apply {f : E â†’ F} {Î¼ : Measure E} (hf : LocallyIntegrableOn f Î© Î¼)
    {Ï† : ğ““^{n}(Î©, â„)} :
    ofFunWithOrder Î© n f Î¼ Ï† = âˆ« x, Ï† x â€¢ f x âˆ‚Î¼ := by
  simp [ofFunWithOrder, hf]

@[simp]
lemma ofFun_apply {f : E â†’ F} {Î¼ : Measure E} (hf : LocallyIntegrableOn f Î© Î¼)
    {Ï† : ğ““(Î©, â„)} :
    ofFun Î© f Î¼ Ï† = âˆ« x, Ï† x â€¢ f x âˆ‚Î¼ :=
  ofFunWithOrder_apply hf

end ofFun

section lineDeriv

-- TODO: where to put the minus ? Doesn't matter mathematically of course
variable (n k) in
noncomputable def lineDerivWithOrderCLM (v : E) :
    ğ““'^{n}(Î©, F) â†’L[â„] ğ““'^{k}(Î©, F) :=
  .precomp F (- TestFunction.lineDerivWithOrderCLM k n v)

@[simp]
lemma lineDerivWithOrderCLM_apply {v : E} {T : ğ““'^{n}(Î©, F)} {Ï† : ğ““^{k}(Î©, â„)} :
    lineDerivWithOrderCLM n k v T Ï† = T (- TestFunction.lineDerivWithOrderCLM k n v Ï†) :=
  rfl

-- TODO: where to put the minus ? Doesn't matter mathematically of course
noncomputable def lineDerivCLM (v : E) :
    ğ““'(Î©, F) â†’L[â„] ğ““'(Î©, F) :=
  .precomp F (- TestFunction.lineDerivCLM v)

@[simp]
lemma lineDerivCLM_apply {v : E} {T : ğ““'(Î©, F)} {Ï† : ğ““(Î©, â„)} :
    lineDerivCLM v T Ï† = T (- TestFunction.lineDerivCLM v Ï†) :=
  rfl

end lineDeriv

end Distribution
