/-
Copyright (c) 2024 Jack Valmadre. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack Valmadre
-/
import Mathlib.Analysis.Distribution.FourierSchwartz
import Mathlib.Analysis.Distribution.Periodic
import Mathlib.MeasureTheory.Function.LpSpace
import Mathlib.Topology.Algebra.Module.WeakDual

/-!
# Tempered distributions

## Main theorems

* `fourierTransform_delta_zero`
* `fourierTransform_one`
* `fourierTransform_exp_inner_mul_I`
* `fourierTransform_delta`
-/

open MeasureTheory
open scoped ContinuousLinearMap ENNReal SchwartzMap FourierTransform

variable {ğ•œ D E F G V : Type*}
  [NormedAddCommGroup D] [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedAddCommGroup G]
  [NormedAddCommGroup V]

namespace SchwartzMap

section Basic

-- TODO: Review `SMulCommClass â„ ğ•œ ğ•œ`. Could use `NormedAlgebra â„ ğ•œ`; less general?
variable [NormedField ğ•œ] [NormedSpace â„ ğ•œ] [SMulCommClass â„ ğ•œ ğ•œ] [MeasurableSpace D]
  [NormedSpace â„ E]

variable (ğ•œ E) in
/-- Type copy of `ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ•œ` endowed with the weak star topology.

Assumes test functions, linear functionals and linearity have same type `ğ•œ`.
-/
def Distribution := ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ•œ  -- WeakDual ğ•œ ğ“¢(E, ğ•œ)

-- TODO: Should we just use `WeakDual ğ•œ ğ“¢(E, ğ•œ)` directly?
-- TODO: Should this be `ğ“¢â€²` (prime) rather than `ğ“¢'` (apostrophe)?
/-- Notation for the tempered distributions as the dual of the Schwartz space. -/
scoped[SchwartzMap] notation "ğ“¢'(" E ", " ğ•œ ")" => Distribution ğ•œ E

namespace Distribution

noncomputable instance instAddCommMonoid : AddCommMonoid (ğ“¢'(E, ğ•œ)) := WeakDual.instAddCommMonoid

noncomputable instance instModule : Module ğ•œ (ğ“¢'(E, ğ•œ)) := WeakDual.instModule

/-- Weak star topology as defined in `WeakDual`. -/
instance instTopologicalSpace : TopologicalSpace (ğ“¢'(E, ğ•œ)) := WeakDual.instTopologicalSpace

instance instContinuousAdd : ContinuousAdd (ğ“¢'(E, ğ•œ)) := WeakDual.instContinuousAdd

instance instFunLike : FunLike (ğ“¢'(E, ğ•œ)) (ğ“¢(E, ğ•œ)) ğ•œ :=
  WeakDual.instFunLike

instance instContinuousLinearMapClass : ContinuousLinearMapClass ğ“¢'(E, ğ•œ) ğ•œ ğ“¢(E, ğ•œ) ğ•œ :=
  WeakDual.instContinuousLinearMapClass

-- TODO: Can this be added to `WeakDual`?
instance instNeg : Neg (ğ“¢'(E, ğ•œ)) := ContinuousLinearMap.neg

section Monoid

variable (M : Type*) [Monoid M] [DistribMulAction M ğ•œ]
  [SMulCommClass ğ•œ M ğ•œ] [ContinuousConstSMul M ğ•œ]

instance instMulAction : MulAction M ğ“¢'(E, ğ•œ) := WeakDual.instMulAction M

instance instDistribMulAction : DistribMulAction M ğ“¢'(E, ğ•œ) := WeakDual.instDistribMulAction M

instance instContinuousConstSMul : ContinuousConstSMul M ğ“¢'(E, ğ•œ) :=
  WeakDual.instContinuousConstSMul M

instance instContinuousSMul [TopologicalSpace M] [ContinuousSMul M ğ•œ] : ContinuousSMul M ğ“¢'(E, ğ•œ) :=
  WeakDual.instContinuousSMul M

end Monoid

-- TODO: Cleaner to use `DFunLike.ext f g h`?
@[ext] theorem ext {f g : ğ“¢'(E, ğ•œ)} (h : âˆ€ Ï†, f Ï† = g Ï†) : f = g := ContinuousLinearMap.ext h

-- Note: Does not assume `RCLike ğ•œ`.
variable (ğ•œ) in
/-- The Dirac delta as a tempered distribution. -/
def delta (x : E) : ğ“¢'(E, ğ•œ) :=
  SchwartzMap.mkCLMtoNormedSpace (Â· x) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)
    âŸ¨Finset.Iic 0, 1, zero_le_one, fun Ï† â†¦ by
      simpa using one_add_le_sup_seminorm_apply (m := 0) (le_refl 0) (le_refl 0) Ï† xâŸ©

theorem delta_apply (x : E) (Ï† : ğ“¢(E, ğ•œ)) : delta ğ•œ x Ï† = Ï† x := rfl

/-- Pre-composition with a constant linear map `L` as a continuous linear map `f â†¦ f.comp L`.

This is used to define the Fourier transform of a tempered distribution as a continuous linear map.

(We can't use `ContinuousLinearMap.compL` as it would require `SeminormedAddCommGroup` and
`NormedSpace ğ•œ` for `SchwartzMap`.)
-/
def precomp (L : ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ“¢(E, ğ•œ)) : ğ“¢'(E, ğ•œ) â†’L[ğ•œ] ğ“¢'(E, ğ•œ) where
  toFun f := f âˆ˜L L
  map_add' f g := ContinuousLinearMap.add_comp f g L
  map_smul' c f := ContinuousLinearMap.smul_comp c f L
  cont := WeakDual.continuous_of_continuous_eval fun Ï† â†¦ WeakDual.eval_continuous (L Ï†)

@[simp]
theorem precomp_apply (L : ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ“¢(E, ğ•œ)) (f : ğ“¢'(E, ğ•œ)) : precomp L f = f âˆ˜L L := rfl

theorem coeFn_precomp (L : ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ“¢(E, ğ•œ)) : â‡‘(precomp L) = (Â· âˆ˜L L) := rfl

-- TODO: Should `f, Î¼` be `outParam`?
/-- The condition that a tempered distribution is equal to an integral against a known function and
measure. -/
def IsIntegral [MeasurableSpace E] (F : ğ“¢'(E, ğ•œ)) (Î¼ : Measure E) (f : E â†’ ğ•œ) : Prop :=
  âˆ€ Ï†, F Ï† = âˆ« x, Ï† x * f x âˆ‚Î¼

theorem isIntegral_iff [MeasurableSpace E] (F : ğ“¢'(E, ğ•œ)) (Î¼ : Measure E) (f : E â†’ ğ•œ) :
    F.IsIntegral Î¼ f â†” âˆ€ Ï†, F Ï† = âˆ« x, Ï† x * f x âˆ‚Î¼ := Iff.rfl

end Distribution

end Basic

namespace Distribution

section One

variable [RCLike ğ•œ] [InnerProductSpace â„ D] [FiniteDimensional â„ D]
  [MeasurableSpace D] [BorelSpace D]

/-- The constant function `1` as a tempered distribution. -/
noncomputable instance one : One ğ“¢'(D, ğ•œ) where
  one := SchwartzMap.integralCLM ğ•œ volume

@[simp]
theorem coeFn_one : â‡‘(1 : ğ“¢'(D, ğ•œ)) = fun Ï† â†¦ âˆ« x, Ï† x := rfl

end One

section Lp

variable [RCLike ğ•œ] [NormedSpace â„ D] [MeasurableSpace D] [OpensMeasurableSpace D]

variable {p : â„â‰¥0âˆ} [hp : Fact (1 â‰¤ p)] {Î¼ : Measure D} [hÎ¼ : Î¼.HasTemperateGrowth]

instance : Fact (1 â‰¤ p.conjExponent) :=
  âŸ¨ENNReal.IsConjExponent.one_le <| .symm <| .conjExponent hp.outâŸ©

-- TODO: Is this less general than `ofLp`? Does it support `AEEqFun`?
/-- Define tempered distribution `Ï† â†¦ âˆ« x, Ï† x * f x âˆ‚Î¼` from function in `L^p` on given measure. -/
noncomputable def ofMemâ„’p (f : D â†’ ğ•œ) (hf : Memâ„’p f p Î¼) : ğ“¢'(D, ğ•œ) :=
  L1.integralCLM' ğ•œ
  âˆ˜L .bilinearRightLpL ğ•œ (.flip <| .mul ğ•œ ğ•œ)
    (.trans (ENNReal.IsConjExponent.conjExponent hp.out).inv_add_inv_conj (@inv_one ENNReal).symm)
    (hf.toLp)
  âˆ˜L SchwartzMap.toLpCLM ğ•œ ğ•œ p.conjExponent Î¼

theorem coeFn_ofMemâ„’p {f : D â†’ ğ•œ} (hf : Memâ„’p f p Î¼) :
    â‡‘(ofMemâ„’p f hf) = (âˆ« x, Â· x * f x âˆ‚Î¼) := by
  ext Ï†
  rw [ofMemâ„’p]
  simp only [@ContinuousLinearMap.comp_apply _]
  change L1.integralCLM _ = _
  rw [â† MeasureTheory.L1.integral_eq, L1.integral_eq_integral]
  refine integral_congr_ae ?_
  refine .trans (ContinuousLinearMap.coeFn_bilinearRightLpL _ _ _ _) ?_
  simpa using .mul SchwartzMap.coeFn_toLpCLM hf.coeFn_toLp

theorem ofMemâ„’p_isIntegral {f : D â†’ ğ•œ} (hf : Memâ„’p f p Î¼) : (ofMemâ„’p f hf).IsIntegral Î¼ f :=
  congrFun (coeFn_ofMemâ„’p hf)

end Lp

section HasTemperateGrowth

variable [RCLike ğ•œ] [NormedSpace â„ D]

-- TODO: Remove this?
-- TODO: Define analogous method for L^p?
/-- Multiplication on the right by a `HasTemperateGrowth` function. -/
noncomputable def mulRight_hasTemperateGrowth (g : ğ“¢'(D, ğ•œ)) {f : D â†’ ğ•œ}
    (hf : Function.HasTemperateGrowth f) : ğ“¢'(D, ğ•œ) :=
  g.comp (SchwartzMap.bilinLeftCLM (.mul ğ•œ ğ•œ) hf)

variable [MeasurableSpace D] [BorelSpace D] [SecondCountableTopology D]

/-- Define tempered distribution `Ï† â†¦ âˆ« x, Ï† x * f x âˆ‚Î¼` from function and measure with temperate
growth. -/
noncomputable def ofHasTemperateGrowth (f : D â†’ ğ•œ) (hf : f.HasTemperateGrowth)
    (Î¼ : Measure D := by volume_tac) [hÎ¼ : Î¼.HasTemperateGrowth] : ğ“¢'(D, ğ•œ) :=
  SchwartzMap.integralCLM ğ•œ Î¼ âˆ˜L SchwartzMap.bilinLeftCLM (.mul ğ•œ ğ•œ) hf

theorem coeFn_ofHasTemperateGrowth {f : D â†’ ğ•œ} {hf : f.HasTemperateGrowth}
    {Î¼ : Measure D} [hÎ¼ : Î¼.HasTemperateGrowth] :
    â‡‘(ofHasTemperateGrowth f hf Î¼) = (âˆ« x, Â· x * f x âˆ‚Î¼) := rfl

theorem ofHasTemperateGrowth_isIntegral {f : D â†’ ğ•œ} {hf : f.HasTemperateGrowth}
    {Î¼ : Measure D} [hÎ¼ : Î¼.HasTemperateGrowth] : (ofHasTemperateGrowth f hf Î¼).IsIntegral Î¼ f :=
  fun _ â†¦ rfl

end HasTemperateGrowth

section Fourier

variable [InnerProductSpace â„ V] [FiniteDimensional â„ V] [MeasurableSpace V] [BorelSpace V]

-- TODO: Could be generalized from `â„‚` to `ğ•œ` with `NormedSpace â„‚ ğ•œ`?
-- Currently require `RCLike ğ•œ` and `NormedSpace â„‚ ğ•œ` from `SchwartzMap.fourierTransformCLM`.
variable (V) in
/-- Auxiliary function for the definition of `fourierTransformCLE`. -/
noncomputable def fourierTransformCLM : ğ“¢'(V, â„‚) â†’L[â„‚] ğ“¢'(V, â„‚) :=
  precomp <| SchwartzMap.fourierTransformCLM â„‚

theorem coeFn_fourierTransformCLM (f : ğ“¢'(V, â„‚)) :
    fourierTransformCLM V f = fun Ï† â†¦ f (SchwartzMap.fourierTransformCLE â„‚ Ï†) := rfl

variable (V) in
/-- Auxiliary function for the definition of `fourierTransformCLE`. -/
noncomputable def fourierTransformInvCLM : ğ“¢'(V, â„‚) â†’L[â„‚] ğ“¢'(V, â„‚) :=
  precomp (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap

theorem coeFn_fourierTransformInvCLM (f : ğ“¢'(V, â„‚)) :
    fourierTransformInvCLM V f = fun Ï† â†¦ f ((SchwartzMap.fourierTransformCLE â„‚).symm Ï†) := rfl

theorem leftInverse_fourierTransformCLM :
    Function.LeftInverse (fourierTransformInvCLM V) (fourierTransformCLM V) :=
  fun f â†¦ ext fun Ï† â†¦ by simp [coeFn_fourierTransformCLM, coeFn_fourierTransformInvCLM]

theorem rightInverse_fourierTransformCLM :
    Function.RightInverse (fourierTransformInvCLM V) (fourierTransformCLM V) :=
  fun f â†¦ ext fun Ï† â†¦ by simp [coeFn_fourierTransformCLM, coeFn_fourierTransformInvCLM]

-- TODO: Should `fourierTransformInvCLM` be moved inside here to avoid accumulating definitions?
/-- The Fourier transform of a tempered distribution as a `ContinuousLinearEquiv`.
The inverse Fourier transform is represented by `(fourierTransformCLE V).symm`.
-/
noncomputable def fourierTransformCLE : ğ“¢'(V, â„‚) â‰ƒL[â„‚] ğ“¢'(V, â„‚) :=
  { fourierTransformCLM V with
    invFun := fourierTransformInvCLM V
    left_inv := leftInverse_fourierTransformCLM
    right_inv := rightInverse_fourierTransformCLM }

theorem coeFn_fourierTransformCLE (f : ğ“¢'(V, â„‚)) :
    fourierTransformCLE f = f âˆ˜ SchwartzMap.fourierTransformCLE â„‚ := rfl

theorem coeFn_fourierTransformCLE_symm (f : ğ“¢'(V, â„‚)) :
    fourierTransformCLE.symm f = f âˆ˜ (SchwartzMap.fourierTransformCLE â„‚).symm := rfl

/-- Notation for the Fourier transform of a Schwartz function. -/
notation "ğ“•â‚›[" ğ•œ "]" => SchwartzMap.fourierTransformCLE ğ•œ

/-- Notation for the Fourier transform of a tempered distribution. -/
notation "ğ“•â‚œ" => fourierTransformCLE

/-- The inverse Fourier transform is the mirror of the Fourier transform. -/
theorem fourierTransform_symm_eq_comp_fourierTransform_compNeg (f : ğ“¢'(V, â„‚)) :
    (ğ“•â‚œ).symm f = ğ“•â‚œ f âˆ˜L SchwartzMap.compCLMOfContinuousLinearEquiv â„‚ (.neg â„) := by
  ext Ï†
  simp only [@coeFn_fourierTransformCLE _, coeFn_fourierTransformCLE_symm, Function.comp_apply,
    @ContinuousLinearMap.comp_apply _]
  refine congrArg f ?_
  -- TODO: Add `SchwartzMap.coeFn_fourierTransformCLE` (avoid `ext`).
  ext x
  simpa using congrFun (Real.fourierIntegralInv_eq_fourierIntegral_comp_neg Ï†) x

/-- The inverse Fourier transform is the Fourier transform of the mirror. -/
theorem fourierTransform_symm_eq_fourierTransform_comp_compNeg (f : ğ“¢'(V, â„‚)) :
    (ğ“•â‚œ).symm f = ğ“•â‚œ (f âˆ˜L SchwartzMap.compCLMOfContinuousLinearEquiv â„‚ (.neg â„)) := by
  ext Ï†
  simp only [coeFn_fourierTransformCLE, coeFn_fourierTransformCLE_symm, Function.comp_apply,
    @ContinuousLinearMap.comp_apply _]
  refine congrArg f ?_
  ext x
  simpa using Real.fourierIntegralInv_eq_fourierIntegral_neg Ï† x

/-- Duality of the Fourier transform. -/
theorem fourierTransform_eq_of_fourierTransform_eq {g g' : ğ“¢'(V, â„‚)} (h : ğ“•â‚œ g = g') :
    ğ“•â‚œ g' = g âˆ˜L compCLMOfContinuousLinearEquiv â„‚ (.neg â„) := by
  have : compCLMOfContinuousLinearEquiv â„‚ (.neg â„) âˆ˜L compCLMOfContinuousLinearEquiv â„‚ (.neg â„) =
      ContinuousLinearMap.id â„‚ ğ“¢(V, â„‚) := by ext; simp
  rw [â† fourierTransformCLE.eq_symm_apply, fourierTransform_symm_eq_fourierTransform_comp_compNeg,
    ContinuousLinearMap.comp_assoc, this, ContinuousLinearMap.comp_id]
  exact h.symm

/-- Transfer results from `SchwartzMap` to `TemperedDistribution`.

Given a Fourier transform for Schwartz maps `ğ“•â‚›[â„‚] (a Ï†) = b (ğ“•â‚›[â„‚] Ï†)`, obtain the Fourier
transform of the distribution `g âˆ˜L b`.
-/
theorem of_schwartzMapFourier_eq {a : ğ“¢(V, â„‚) â†’L[â„‚] ğ“¢(V, â„‚)} {b : ğ“¢(V, â„‚) â†’L[â„‚] ğ“¢(V, â„‚)}
    (h : âˆ€ Ï† : ğ“¢(V, â„‚), ğ“•â‚›[â„‚] (a Ï†) = b (ğ“•â‚›[â„‚] Ï†)) (g : ğ“¢'(V, â„‚)) :
    ğ“•â‚œ (g âˆ˜L b) = (ğ“•â‚œ g) âˆ˜L a := by
  ext Ï†
  simpa [coeFn_fourierTransformCLE] using congrArg g (h Ï†).symm

variable (V) in
theorem fourierTransform_delta_zero : ğ“•â‚œ (delta â„‚ (0 : V)) = 1 := by
  ext Ï†
  simp [coeFn_fourierTransformCLE, delta_apply, Real.fourierIntegral_eq]

variable (V) in
theorem fourierTransform_one : ğ“•â‚œ (1 : ğ“¢'(V, â„‚)) = delta â„‚ 0 := by
  rw [fourierTransform_eq_of_fourierTransform_eq (fourierTransform_delta_zero V)]
  ext Ï†
  simp [@ContinuousLinearMap.comp_apply _, @delta_apply _]

end Fourier

section Composition

variable [RCLike ğ•œ] [NormedSpace â„ D]

-- TODO: Instead write `compAffineEquivCLM` and eliminate this definition?
-- TODO: Later useful to write this as CLE `â‰ƒL[ğ•œ]`?
variable (ğ•œ) in
/-- Composition with addition on the right as a continuous linear map `Ï† â†¦ Ï† âˆ˜ (Â· + a)`. -/
def _root_.SchwartzMap.compAddRight (a : D) : ğ“¢(D, ğ•œ) â†’L[ğ•œ] ğ“¢(D, ğ•œ) :=
  compCLMOfAntilipschitz ğ•œ (g := (Â· + a)) (K := 1) (.add .id (.const a)) (fun _ _ â†¦ by simp)

@[simp]
theorem _root_.SchwartzMap.coeFn_compAddRight (a : D) (Ï† : ğ“¢(D, ğ•œ)) :
    â‡‘(SchwartzMap.compAddRight ğ•œ a Ï†) = Ï† âˆ˜ (Â· + a) := rfl

end Composition

section ExpInner

open Complex
open scoped Real

variable [InnerProductSpace â„ V] [FiniteDimensional â„ V] [MeasurableSpace V] [BorelSpace V]

/-- Statement of `VectorFourier.fourierIntegral_comp_add_right` for tempered distributions. -/
theorem fourierTransform_comp_mul_exp_inner_mul_I (G : ğ“¢'(V, â„‚)) (a : V) :
    ğ“•â‚œ (G âˆ˜L bilinLeftCLM (.mul â„‚ â„‚) (g := fun x â†¦ exp ((2 * Ï€ * inner a x : â„) * I))
      (.comp Complex.exp_ofReal_mul_I_hasTemperateGrowth
        (.mul (.const _) (innerSL â„ a).hasTemperateGrowth))) =
    ğ“•â‚œ G âˆ˜L compAddRight â„‚ a := by
  symm  -- TODO: Should we flip the definition?
  ext Ï†
  simp only [@coeFn_fourierTransformCLE _, Function.comp_apply, @ContinuousLinearMap.comp_apply _]
  refine congrArg G ?_
  ext x
  simp only [fourierTransformCLE_apply, coeFn_compAddRight, coeFn_bilinLeftCLM,
    ContinuousLinearMap.mul_apply', Real.fourierIntegral]
  rw [VectorFourier.fourierIntegral_comp_add_right]
  simp only [Circle.smul_def, Real.fourierChar_apply, smul_eq_mul, innerâ‚—_apply]
  ring

theorem one_comp_bilinLeftCLM_mul {f : V â†’ â„‚} (hf : f.HasTemperateGrowth) :
    (1 : ğ“¢'(V, â„‚)) âˆ˜L bilinLeftCLM (.mul â„‚ â„‚) hf = ofHasTemperateGrowth f hf := rfl

-- TODO: Check that the below works with `ofMemâ„’p`:
-- ofMemâ„’p (fun x â†¦ exp ((inner a x : â„) * I)) (p := âŠ¤) (Î¼ := volume)
--   (memâ„’p_top_of_bound (Continuous.aestronglyMeasurable <| .cexp <| .mul
--     (.comp ofRealCLM.continuous (innerSL â„ a).continuous) continuous_const) 1 (by simp))

-- TODO: Use `fun x â†¦ (ğ (inner a x) : â„‚)` (Real.fourierChar) instead?
-- TODO: Add two_pi to the name or use `delta â„‚ ((2 * Ï€)â»Â¹ â€¢ a)`?

/-- The Fourier transform of `x â†¦ exp (2 * Ï€ * inner a x * I)` is `Î´ a` -/
theorem fourierTransform_exp_inner_mul_I (a : V) :
    ğ“•â‚œ (ofHasTemperateGrowth (fun x â†¦ exp ((2 * Ï€ * inner a x : â„) * I))
      (.comp Complex.exp_ofReal_mul_I_hasTemperateGrowth
        (.mul (.const _) (innerSL â„ a).hasTemperateGrowth))) =
    delta â„‚ a := by
  ext Ï†
  simpa [fourierTransform_one, @ContinuousLinearMap.comp_apply _, @delta_apply _] using
    DFunLike.congr_fun (fourierTransform_comp_mul_exp_inner_mul_I (1 : ğ“¢'(V, â„‚)) a) Ï†

/-- The Fourier transform of `x â†¦ exp (2 * Ï€ * inner a x * I)` is `Î´ a` -/
theorem fourierTransform_delta (a : V) :
    ğ“•â‚œ (delta â„‚ a) = ofHasTemperateGrowth (fun x â†¦ exp ((2 * Ï€ * -inner a x : â„) * I))
      (.comp Complex.exp_ofReal_mul_I_hasTemperateGrowth
        (.mul (.const _) (innerSL â„ a).hasTemperateGrowth.neg)) := by
  rw [fourierTransform_eq_of_fourierTransform_eq (fourierTransform_exp_inner_mul_I a)]
  ext Ï†
  rw [ContinuousLinearMap.comp_apply]
  simp only [@coeFn_ofHasTemperateGrowth _]
  rw [â† MeasureTheory.integral_neg_eq_self]
  simp

end ExpInner

end Distribution

end SchwartzMap
