/-
Copyright (c) 2024 Jack Valmadre. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jack Valmadre
-/
import Mathlib.Analysis.Distribution.FourierSchwartz
import Mathlib.Topology.Algebra.Module.WeakDual

/-!
# Tempered distributions
-/

open MeasureTheory
open scoped SchwartzMap ContinuousLinearMap

variable {ğ•œ : Type*} [RCLike ğ•œ] -- [DenselyNormedField ğ•œ] [NormedAlgebra â„ ğ•œ]
  {D : Type*} [NormedAddCommGroup D] [NormedSpace â„ D] -- [MeasurableSpace E]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] -- [MeasurableSpace E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
  {U : Type*} [NormedAddCommGroup U] [InnerProductSpace â„ U]
  {V : Type*} [NormedAddCommGroup V] [InnerProductSpace â„ V] [FiniteDimensional â„ V]
  [MeasurableSpace V] [BorelSpace V]

-- TODO: Preferable for `ğ•œ` to come after `V` like `ğ“¢(V, ğ•œ)`? Use name like `ğ•œ' R C r`?
-- TODO: Better to define directly as type copy of `ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ•œ`?
variable (ğ•œ E) in
/-- Type copy of `ğ“¢(E, ğ•œ) â†’L[ğ•œ] ğ•œ` endowed with the weak star topology.

Assumes test functions, linear functionals and linearity have same type `ğ•œ`.
-/
def TemperedDistribution := WeakDual ğ•œ ğ“¢(E, ğ•œ)

-- TODO: Should this be `ğ“¢â€²` (prime) rather than `ğ“¢'` (apostrophe)?
/-- Notation for the tempered distributions as the dual of the Schwartz space. -/
scoped[TemperedDistribution] notation "ğ“¢'(" E ", " ğ•œ ")" => TemperedDistribution ğ•œ E

namespace TemperedDistribution

/-- Weak star topology as defined in `WeakDual`. -/
instance instTopologicalSpace : TopologicalSpace (ğ“¢'(E, ğ•œ)) :=
  WeakDual.instTopologicalSpace

noncomputable instance instAddCommMonoid : AddCommMonoid (ğ“¢'(E, ğ•œ)) :=
  WeakDual.instAddCommMonoid

noncomputable instance instModule : Module ğ•œ (ğ“¢'(E, ğ•œ)) :=
  WeakDual.instModule

instance neg : Neg (ğ“¢'(E, ğ•œ)) := ContinuousLinearMap.neg

instance instFunLike : FunLike (ğ“¢'(E, ğ•œ)) (ğ“¢(E, ğ•œ)) ğ•œ :=
  WeakDual.instFunLike

-- TODO: Cleaner to use `DFunLike.ext f g h`?
@[ext] theorem ext {f g : ğ“¢'(E, ğ•œ)} (h : âˆ€ Ï†, f Ï† = g Ï†) : f = g := ContinuousLinearMap.ext h

variable (ğ•œ) in
/-- The Dirac delta as a tempered distribution. -/
noncomputable def delta (x : E) : ğ“¢'(E, ğ•œ) := SchwartzMap.delta ğ•œ ğ•œ x

variable (k V) in
/-- The constant function `1` as a tempered distribution. -/
noncomputable instance one : One ğ“¢'(V, ğ•œ) where
  one := SchwartzMap.integralCLM ğ•œ volume

-- TODO: Is it better to define this using `ofHasTemperateGrowth`?
variable (V) in
/-- The constant function as a tempered distribution. -/
noncomputable def const (c : ğ•œ) : ğ“¢'(V, ğ•œ) := c â€¢ 1

theorem delta_apply {x : E} {Ï† : ğ“¢(E, ğ•œ)} : delta ğ•œ x Ï† = Ï† x := rfl

theorem one_apply {Ï† : ğ“¢(V, â„‚)} : (1 : ğ“¢'(V, â„‚)) Ï† = âˆ« x, Ï† x := rfl

-- TODO: Remove this and just keep `fourierTransformCLM` to avoid accumulating definitions?
/-- The Fourier transform of a tempered distribution.

The Fourier transform of the continuous linear functional is the linear functional defined by the
the linear functional of the Fourier transform of the test function.
-/
noncomputable def fourierTransform (f : ğ“¢'(V, â„‚)) : ğ“¢'(V, â„‚) :=
  f âˆ˜L SchwartzMap.fourierTransformCLM â„‚

-- TODO: simp?
theorem fourierTransform_apply {f : ğ“¢'(V, â„‚)} :
    fourierTransform f = f âˆ˜L SchwartzMap.fourierTransformCLM â„‚ := rfl

-- TODO: simp?
theorem fourierTransform_apply_apply {f : ğ“¢'(V, â„‚)} {Ï† : ğ“¢(V, â„‚)} :
    f.fourierTransform Ï† = f (Ï†.fourierTransformCLM â„‚) := rfl

-- Can't use `compL (ğ•œ := â„‚) (E := ğ“¢(V, â„‚)) (Fâ‚— := ğ“¢(V, â„‚)) (Gâ‚— := â„‚)` because
-- it requires `[SeminormedAddCommGroup E]`, `[NormedSpace ğ•œ E]`, etc.
-- #check ContinuousLinearMap.compL â„‚ ğ“¢(V, â„‚) ğ“¢(V, â„‚) â„‚

/-- Pre-composition with a constant linear map `L` as a continuous linear map `f â†¦ f.comp L`.

This is used to define the Fourier transform of a tempered distribution as a continuous linear map.
-/
def precompCLM (L : ğ“¢(D, ğ•œ) â†’L[ğ•œ] ğ“¢(E, ğ•œ)) : ğ“¢'(E, ğ•œ) â†’L[ğ•œ] ğ“¢'(D, ğ•œ) where
  toFun f := f âˆ˜L L
  map_add' f g := ContinuousLinearMap.add_comp f g L
  map_smul' c f := ContinuousLinearMap.smul_comp c f L
  cont := WeakDual.continuous_of_continuous_eval fun Ï† â†¦ WeakDual.eval_continuous (L Ï†)

@[simp]
theorem precompCLM_apply {L : ğ“¢(D, ğ•œ) â†’L[ğ•œ] ğ“¢(E, ğ•œ)} {f : ğ“¢'(E, ğ•œ)} : precompCLM L f = f âˆ˜L L := rfl

variable (V) in
/-- Auxiliary function for the definition of `fourierTransformCLE`. -/
noncomputable def fourierTransformCLM : ğ“¢'(V, â„‚) â†’L[â„‚] ğ“¢'(V, â„‚) :=
  precompCLM <| SchwartzMap.fourierTransformCLM â„‚

-- TODO: Should this use `fourierTransformCLE` or `fourierTransformCLM`?
-- Using `fourierTransformCLE` makes it easier to prove inverse below.
theorem fourierTransformCLM_apply (f : ğ“¢'(V, â„‚)) :
    fourierTransformCLM V f = f âˆ˜L (SchwartzMap.fourierTransformCLE â„‚).toContinuousLinearMap := rfl

theorem fourierTransformCLM_apply_apply (f : ğ“¢'(V, â„‚)) (Ï† : ğ“¢(V, â„‚)) :
    fourierTransformCLM V f Ï† = f (SchwartzMap.fourierTransformCLE â„‚ Ï†) := rfl

variable (V) in
/-- Auxiliary function for the definition of `fourierTransformCLE`. -/
noncomputable def fourierTransformInvCLM : ğ“¢'(V, â„‚) â†’L[â„‚] ğ“¢'(V, â„‚) :=
  precompCLM (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap

theorem fourierTransformInvCLM_apply (f : ğ“¢'(V, â„‚)) :
    fourierTransformInvCLM V f =
    f âˆ˜L (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap := rfl

theorem fourierTransformInvCLM_apply_apply (f : ğ“¢'(V, â„‚)) (Ï† : ğ“¢(V, â„‚)) :
    fourierTransformInvCLM V f Ï† = f ((SchwartzMap.fourierTransformCLE â„‚).symm Ï†) := rfl

-- TODO: Why does `(Râ‚ := _)` help in `simp`?
theorem leftInverse_fourierTransformCLM :
    Function.LeftInverse (fourierTransformInvCLM V) (fourierTransformCLM V) :=
  fun f â†¦ ext fun Ï† â†¦ by simp [fourierTransformInvCLM_apply, fourierTransformCLM_apply,
    ContinuousLinearMap.comp_apply (Râ‚ := _)]

theorem rightInverse_fourierTransformCLM :
    Function.RightInverse (fourierTransformInvCLM V) (fourierTransformCLM V) :=
  fun f â†¦ ext fun Ï† â†¦ by simp [fourierTransformInvCLM_apply, fourierTransformCLM_apply,
    ContinuousLinearMap.comp_apply (Râ‚ := _)]


-- TODO: Should `fourierTransformInvCLM` be moved inside here to avoid accumulating definitions?
-- TODO: Generalize beyond `â„‚`?
variable (V) in
/-- The Fourier transform of a tempered distribution as a `ContinuousLinearEquiv`.

The inverse Fourier transform is represented by `(fourierTransformCLE V).symm`.
-/
noncomputable def fourierTransformCLE : ğ“¢'(V, â„‚) â‰ƒL[â„‚] ğ“¢'(V, â„‚) where
  __ := fourierTransformCLM V
  invFun := fourierTransformInvCLM V
  left_inv := leftInverse_fourierTransformCLM
  right_inv := rightInverse_fourierTransformCLM

theorem fourierTransformCLE_apply {f : ğ“¢'(V, â„‚)} :
    fourierTransformCLE V f = f âˆ˜L (SchwartzMap.fourierTransformCLE â„‚).toContinuousLinearMap := rfl

theorem fourierTransformCLE_symm_apply {f : ğ“¢'(V, â„‚)} :
    (fourierTransformCLE V).symm f =
    f âˆ˜L (SchwartzMap.fourierTransformCLE â„‚).symm.toContinuousLinearMap := rfl

-- TODO: Define `_apply_coeFn` with `â‡‘` rather than `_apply_apply`?
theorem fourierTransformCLE_apply_apply {f : ğ“¢'(V, â„‚)} {Ï† : ğ“¢(V, â„‚)} :
    fourierTransformCLE V f Ï† = f (SchwartzMap.fourierTransformCLE â„‚ Ï†) := rfl

theorem fourierTransformCLE_symm_apply_apply {f : ğ“¢'(V, â„‚)} {Ï† : ğ“¢(V, â„‚)} :
    (fourierTransformCLE V).symm f Ï† = f ((SchwartzMap.fourierTransformCLE â„‚).symm Ï†) := rfl

-- Note: Prefer use of `fourierTransformCLE` over `fourierTransformCLM` and `fourierTransform`.
-- TODO: Rename `fourierTransformCLE` to `fourierTransform` and remove others or make "aux"?

-- TODO: Should this be notation instead of a definition?
/-- The linear map representing composition of the test function with an equivalence.

That is, given an equivalence `e : D â‰ƒL[â„] E`, we can map a Schwartz function `Ï† : ğ“¢(E, ğ•œ)` to
a Schwartz function `Ï† âˆ˜ e : ğ“¢(D, ğ•œ)` (using `SchwartzMap.compCLMOfContinuousLinearEquiv`).
The `T` function represents the action of this composition as a continuous linear map on the space
of tempered distributions, such that `T e Ï† g = g (Ï† âˆ˜ e)` for `g : ğ“¢'(E, ğ•œ)`.
-/
def T (e : D â‰ƒL[â„] E) : ğ“¢'(D, ğ•œ) â†’L[ğ•œ] ğ“¢'(E, ğ•œ) :=
  precompCLM (SchwartzMap.compCLMOfContinuousLinearEquiv ğ•œ e)

theorem T_apply (e : D â‰ƒL[â„] E) (g : ğ“¢'(D, ğ•œ)) (Ï† : ğ“¢(E, ğ•œ)) :
    T e g Ï† = g (SchwartzMap.compCLMOfContinuousLinearEquiv ğ•œ e Ï†) := rfl

theorem fourierTransformCLE_symm_apply_eq_fourierTransformCLE_T_neg {g : ğ“¢'(V, â„‚)} :
    (fourierTransformCLE V).symm g = fourierTransformCLE V (T (.neg â„) g) := by
  ext Ï†
  simp only [fourierTransformCLE_apply_apply, fourierTransformCLE_symm_apply_apply,
    ContinuousLinearMap.comp_apply (Râ‚ := _), T_apply]
  refine congrArg g ?_
  ext x
  simp [Real.fourierIntegralInv_eq_fourierIntegral_neg]

theorem fourierTransform_apply_eq_fourierTransformInv_T_neg {g : ğ“¢'(V, â„‚)} :
    fourierTransformCLE V g =
    (fourierTransformCLE V).symm (T (.neg â„) g) := by
  ext Ï†
  simp only [fourierTransformCLE_apply_apply, fourierTransformCLE_symm_apply_apply,
    ContinuousLinearMap.comp_apply (Râ‚ := _), T_apply]
  refine congrArg g ?_
  ext x
  simp [Real.fourierIntegralInv_eq_fourierIntegral_neg]

/-- Duality property of the Fourier transform.

If the Fourier transform of `f(x)` is `g(Î¾)`, then the Fourier transform of `g(x)` is `f(-Î¾)`.
-/
theorem fourierTransform_eq_T_neg_of_fourierTransform_eq {f : ğ“¢'(V, â„‚)} {g : ğ“¢'(V, â„‚)}
    (h : fourierTransformCLE V f = g) :
    fourierTransformCLE V g = T (.neg â„) f := by
  rw [fourierTransform_apply_eq_fourierTransformInv_T_neg] at h
  simp [â† h]

-- TODO: Does this definition make it unintuitive to do `rw`?
/-- Even symmetry `f(-x) = f(x)` for tempered distributions. -/
def EvenSymm (f : ğ“¢'(E, ğ•œ)) : Prop := T (.neg â„) f = f

/-- Odd symmetry `f(-x) = -f(x)` for tempered distributions. -/
def OddSymm (f : ğ“¢'(E, ğ•œ)) : Prop := T (.neg â„) f = -f

theorem delta_evenSymm : EvenSymm (delta ğ•œ 0 : ğ“¢'(E, ğ•œ)) := by
  ext Ï†
  simp [ContinuousLinearMap.comp_apply (Râ‚ := _), delta_apply (ğ•œ := _), T_apply]

theorem one_evenSymm : EvenSymm (1 : ğ“¢'(V, â„‚)) := ext (integral_neg_eq_self Â· volume)

theorem fourierTransformCLE_delta_eq_one : fourierTransformCLE V (delta â„‚ 0) = 1 := by
  ext Ï†
  simp [fourierTransformCLE_apply_apply, delta_apply, one_apply, Real.fourierIntegral_eq]

theorem fourierTransformCLE_one_eq_delta :
    fourierTransformCLE V 1 = delta â„‚ 0 := by
  rw [fourierTransform_eq_T_neg_of_fourierTransform_eq
    fourierTransformCLE_delta_eq_one]
  rw [delta_evenSymm]

theorem fourierTransformCLE_const_eq_smul_delta {c : â„‚} :
    fourierTransformCLE V (const V c) = c â€¢ delta â„‚ 0 := by
  rw [const, ContinuousLinearEquiv.map_smul, fourierTransformCLE_one_eq_delta]

/-! # From continuous functions -/

section Continuous

/-- Multiplication on the right by a `HasTemperateGrowth` function. -/
noncomputable def mulRight_hasTemperateGrowth (g : ğ“¢'(U, ğ•œ)) {f : U â†’ ğ•œ}
    (hf : Function.HasTemperateGrowth f) : ğ“¢'(U, ğ•œ) :=
  g.comp (SchwartzMap.bilinLeftCLM (.mul ğ•œ ğ•œ) hf)

lemma mulRight_hasTemperateGrowth_apply (g : ğ“¢'(U, ğ•œ)) {f : U â†’ ğ•œ}
    (hf : Function.HasTemperateGrowth f) (Ï† : ğ“¢(U, ğ•œ)) :
    mulRight_hasTemperateGrowth g hf Ï† = g (Ï†.mulRight hf) := rfl

/-- Multiplication on the left by a `HasTemperateGrowth` function. -/
noncomputable def mulLeft_hasTemperateGrowth (g : ğ“¢'(U, ğ•œ)) {f : U â†’ ğ•œ}
    (hf : Function.HasTemperateGrowth f) : ğ“¢'(U, ğ•œ) :=
  g.comp (SchwartzMap.bilinLeftCLM (.flip (.mul ğ•œ ğ•œ)) hf)

lemma mulLeft_hasTemperateGrowth_apply (g : ğ“¢'(U, ğ•œ)) {f : U â†’ ğ•œ}
    (hf : Function.HasTemperateGrowth f) (Ï† : ğ“¢(U, ğ•œ)) :
    mulLeft_hasTemperateGrowth g hf Ï† = g (Ï†.mulLeft hf) := rfl

/-- Any `HasTemperateGrowth` function defines a tempered distribution `Ï† â†¦ âˆ« x, Ï†(x) * f(x)`. -/
noncomputable def ofHasTemperateGrowth {f : V â†’ ğ•œ} (hf : Function.HasTemperateGrowth f) :
    ğ“¢'(V, ğ•œ) := mulRight_hasTemperateGrowth 1 hf

theorem ofHasTemperateGrowth_apply {f : V â†’ ğ•œ} (hf : Function.HasTemperateGrowth f) (Ï† : ğ“¢(V, ğ•œ)) :
    ofHasTemperateGrowth hf Ï† = âˆ« x, Ï† x * f x := rfl

end Continuous

end TemperedDistribution
