/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci, Anatole Dedecker
-/
module

public import Mathlib.Analysis.Distribution.ContDiffMapSupportedIn
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.Topology.ContinuousMap.Bounded.Normed

/-!
# Continuously differentiable functions with compact support

This file develops the basic theory of bundled `n`-times continuously differentiable functions
with compact support contained in some open set `Î©`. More explicitly, given normed spaces `E`
and `F`, an open set `Î© : Opens E` and `n : â„•âˆž`, we are interested in the space `ð““^{n}(Î©, F)` of
maps `f : E â†’ F` such that:

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` has compact support: `HasCompactSupport f`.
- the support of `f` is inside the open set `Î©`: `tsupport f âŠ† Î©`.

This exists as a bundled type to equip it with the canonical LF topology induced by the inclusions
`ð““_{K}^{n}(Î©, F) â†’ ð““^{n}(Î©, F)` (see `ContDiffMapSupportedIn`). The dual space is then the space of
distributions, or "weak solutions" to PDEs, on `Î©`.

## Main definitions

- `TestFunction Î© F n`: the type of bundled `n`-times continuously differentiable
  functions `E â†’ F` with compact support contained in `Î©`.
- `TestFunction.topologicalSpace`: the canonical LF topology on `ð““^{n}(Î©, F)`. It is the
  locally convex inductive limit of the topologies on each `ð““_{K}^{n}(Î©, F)`.

## Main statements

- `TestFunction.continuous_iff_continuous_comp`: a linear map from `ð““^{n}(E, F)`
  to a locally convex space is continuous iff its restriction to `ð““^{n}_{K}(E, F)` is
  continuous for each compact set `K`. We will later translate this concretely in terms
  of seminorms.

## Notation

- `ð““^{n}(Î©, F)`: the space of bundled `n`-times continuously differentiable functions `E â†’ F`
  with compact support contained in `Î©`.
- `ð““(Î©, F)`: the space of bundled smooth (infinitely differentiable) functions `E â†’ F`
  with compact support contained in `Î©`, i.e. `ð““^{âŠ¤}(Î©, F)`.

## Tags

distributions, test function
-/

@[expose] public section

open Function Seminorm SeminormFamily Set TopologicalSpace UniformSpace
open scoped BoundedContinuousFunction NNReal Topology

variable {ð•œ ð•‚ : Type*} [NontriviallyNormedField ð•œ] [RCLike ð•‚]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {Î© : Opens E}
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ð•œ F] [NormedSpace ð•‚ F]
  {n : â„•âˆž}

variable (Î© F n) in
/-- The type of bundled `n`-times continuously differentiable maps with compact support -/
structure TestFunction : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected hasCompactSupport' : HasCompactSupport toFun
  protected tsupport_subset' : tsupport toFun âŠ† Î©

/-- Notation for the space of bundled `n`-times continuously differentiable maps
with compact support. -/
scoped[Distributions] notation "ð““^{" n "}(" Î© ", " F ")" => TestFunction Î© F n

/-- Notation for the space of "test functions", i.e. bundled smooth (infinitely differentiable) maps
with compact support. -/
scoped[Distributions] notation "ð““(" Î© ", " F ")" => TestFunction Î© F âŠ¤

open Distributions

/-- `TestFunctionClass B Î© F n` states that `B` is a type of `n`-times continously
differentiable functions `E â†’ F` with compact support contained in `Î© : Opens E`. -/
class TestFunctionClass (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  map_hasCompactSupport (f : B) : HasCompactSupport f
  tsupport_map_subset (f : B) : tsupport f âŠ† Î©

open TestFunctionClass

namespace TestFunctionClass

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B Î© F n] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B Î© F n] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    obtain âŸ¨C, hCâŸ© := (map_continuous f).bounded_above_of_compact_support (map_hasCompactSupport f)
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

end TestFunctionClass

namespace TestFunction

instance toTestFunctionClass : TestFunctionClass ð““^{n}(Î©, F) Î© F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  map_hasCompactSupport f := f.hasCompactSupport'
  tsupport_map_subset f := f.tsupport_subset'

protected theorem contDiff (f : ð““^{n}(Î©, F)) : ContDiff â„ n f := map_contDiff f
protected theorem hasCompactSupport (f : ð““^{n}(Î©, F)) : HasCompactSupport f :=
  map_hasCompactSupport f
protected theorem tsupport_subset (f : ð““^{n}(Î©, F)) : tsupport f âŠ† Î© := tsupport_map_subset f

@[fun_prop]
protected theorem continuous (f : ð““^{n}(Î©, F)) : Continuous f :=
  f.contDiff.continuous

@[simp]
theorem toFun_eq_coe {f : ð““^{n}(Î©, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.coe (f : ð““^{n}(Î©, F)) : E â†’ F := f

initialize_simps_projections TestFunction (toFun â†’ coe, as_prefix coe)

@[ext]
theorem ext {f g : ð““^{n}(Î©, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `TestFunction` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : ð““^{n}(Î©, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  hasCompactSupport' := h.symm â–¸ f.hasCompactSupport
  tsupport_subset' := h.symm â–¸ f.tsupport_subset

@[simp]
theorem coe_copy (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem coe_toBoundedContinuousFunction (f : ð““^{n}(Î©, F)) :
    (f : BoundedContinuousFunction E F) = (f : E â†’ F) := rfl

section AddCommGroup

@[simps -fullyApplied]
instance : Zero ð““^{n}(Î©, F) where
  zero := âŸ¨0, contDiff_zero_fun, .zero, by simp only [tsupport_zero, empty_subset]âŸ©

@[simps -fullyApplied]
instance : Add ð““^{n}(Î©, F) where
  add f g := âŸ¨f + g, f.contDiff.add g.contDiff, f.hasCompactSupport.add g.hasCompactSupport,
    tsupport_add f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Neg ð““^{n}(Î©, F) where
  neg f := âŸ¨-f, f.contDiff.neg, f.hasCompactSupport.neg, tsupport_neg f â–¸ f.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Sub ð““^{n}(Î©, F) where
  sub f g := âŸ¨f - g, f.contDiff.sub g.contDiff, f.hasCompactSupport.sub g.hasCompactSupport,
    tsupport_sub f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    SMul R ð““^{n}(Î©, F) where
  smul c f := âŸ¨c â€¢ f, f.contDiff.const_smul c, f.hasCompactSupport.smul_left,
    tsupport_smul_subset_right _ _ |>.trans f.tsupport_subsetâŸ©

instance : AddCommGroup ð““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

variable (Î© F n) in
/-- Coercion as an additive homomorphism. -/
@[simps -fullyApplied]
def coeFnAddMonoidHom : ð““^{n}(Î©, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ð““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.module R (coeFnAddMonoidHom Î© F n) fun _ _ â†¦ rfl

instance {R S} [Semiring R] [Semiring S] [Module R F] [Module S F] [SMulCommClass â„ R F]
    [SMulCommClass â„ S F] [ContinuousConstSMul R F] [ContinuousConstSMul S F] [SMul R S]
    [IsScalarTower R S F] :
    IsScalarTower R S ð““^{n}(Î©, F) where
  smul_assoc _ _ _ := by ext; simp

end Module

open ContDiffMapSupportedIn

/-- The natural inclusion `ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)` when `K âŠ† Î©`. -/
@[simps -fullyApplied]
def ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ð““^{n}_{K}(E, F)) :
    ð““^{n}(Î©, F) :=
  âŸ¨f, f.contDiff, f.compact_supp, f.tsupport_subset.trans K_sub_Î©âŸ©

section Topology

variable {V : Type*} [AddCommGroup V] [Module â„ V] [t : TopologicalSpace V]
  [IsTopologicalAddGroup V] [ContinuousSMul â„ V] [LocallyConvexSpace â„ V]

variable (Î© F n) in
/-- The "original topology" on `ð““^{n}(Î©, F)`, defined as the supremum over all compacts `K âŠ† Î©` of
the topology on `ð““^{n}_{K}(E, F)`. In other words, this topology makes `ð““^{n}(Î©, F)` the inductive
limit of the `ð““^{n}_{K}(E, F)`s **in the category of topological spaces**.

Note that this has no reason to be a locally convex (or even vector space) topology. For this
reason, we actually endow `ð““^{n}(Î©, F)` with another topology, namely the finest locally convex
topology which is coarser than this original topology. See `TestFuntion.topologicalSpace`. -/
noncomputable def originalTop : TopologicalSpace ð““^{n}(Î©, F) :=
  â¨† (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
    coinduced (ofSupportedIn K_sub_Î©) ContDiffMapSupportedIn.topologicalSpace

variable (Î© F n) in
/-- The canonical LF topology on `ð““^{n}(Î©, F)`. This makes `ð““^{n}(Î©, F)` the inductive
limit of the `ð““^{n}_{K}(E, F)`s **in the category of locally convex topological vector spaces**
(over â„). See `TestFunction.continuous_iff_continuous_comp` for the corresponding universal
property.

More concretely, this is defined as the infimum of *all* locally convex topologies which are
coarser than the "original topology" `TestFunction.originalTop`, which corresponds to taking
the inductive limit in the category of topological spaces. -/
noncomputable instance topologicalSpace : TopologicalSpace ð““^{n}(Î©, F) :=
  sInf {t : TopologicalSpace ð““^{n}(Î©, F) | originalTop Î© F n â‰¤ t âˆ§
    @IsTopologicalAddGroup ð““^{n}(Î©, F) t _ âˆ§
    @ContinuousSMul â„ ð““^{n}(Î©, F) _ _ t âˆ§
    @LocallyConvexSpace â„ ð““^{n}(Î©, F) _ _ _ _ t}

noncomputable instance : IsTopologicalAddGroup ð““^{n}(Î©, F) :=
  topologicalAddGroup_sInf fun _ âŸ¨_, ht, _, _âŸ© â†¦ ht

noncomputable instance uniformSpace : UniformSpace ð““^{n}(Î©, F) :=
  IsTopologicalAddGroup.rightUniformSpace ð““^{n}(Î©, F)

noncomputable instance : IsUniformAddGroup ð““^{n}(Î©, F) :=
  isUniformAddGroup_of_addCommGroup

-- TODO: deduce for `RCLike` field `ð•‚`
noncomputable instance : ContinuousSMul â„ ð““^{n}(Î©, F) :=
  continuousSMul_sInf fun _ âŸ¨_, _, ht, _âŸ© â†¦ ht

noncomputable instance : LocallyConvexSpace â„ ð““^{n}(Î©, F) :=
  .sInf fun _ âŸ¨_, _, _, htâŸ© â†¦ ht

theorem originalTop_le : originalTop Î© F n â‰¤ topologicalSpace Î© F n :=
  le_sInf fun _t âŸ¨ht, _âŸ© â†¦ ht

/-- Fix a locally convex topology `t` on `ð““^{n}(Î©, F)`. `t` is coarser than the canonical topology
on `ð““^{n}(Î©, F)` if and only if it is coarser than the "original topology" given by
`TestFunction.originalTop`. -/
theorem topologicalSpace_le_iff {t : TopologicalSpace ð““^{n}(Î©, F)}
    [@IsTopologicalAddGroup _ t _] [@ContinuousSMul â„ _ _ _ t]
    [@LocallyConvexSpace â„ _ _ _ _ _ t] :
    topologicalSpace Î© F n â‰¤ t â†” originalTop Î© F n â‰¤ t :=
  âŸ¨le_trans originalTop_le, fun H â†¦ sInf_le âŸ¨H, inferInstance, inferInstance, inferInstanceâŸ©âŸ©

/-- For every compact `K âŠ† Î©`, the inclusion map `ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)` is
continuous. It is in fact a topological embedding, though this fact is not in Mathlib yet. -/
theorem continuous_ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    Continuous (ofSupportedIn K_sub_Î© : ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)) := by
  rw [continuous_iff_coinduced_le]
  exact le_trans (le_iSupâ‚‚_of_le K K_sub_Î© le_rfl) originalTop_le

variable (ð•œ) in
/-- The natural inclusion `ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)`, when `K âŠ† Î©`, as a continuous
linear map. -/
def ofSupportedInCLM [SMulCommClass â„ ð•œ F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    ð““^{n}_{K}(E, F) â†’L[ð•œ] ð““^{n}(Î©, F) where
  toFun f := ofSupportedIn K_sub_Î© f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_ofSupportedIn K_sub_Î©

@[simp] theorem coe_ofSupportedInCLM [SMulCommClass â„ ð•œ F] {K : Compacts E}
    (K_sub_Î© : (K : Set E) âŠ† Î©) :
    (ofSupportedInCLM ð•œ K_sub_Î© : ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)) = ofSupportedIn K_sub_Î© :=
  rfl

/-- The **universal property** of the topology on `ð““^{n}(Î©, F)`: a **linear** map from
`ð““^{n}(Î©, F)` to a locally convex topological vector space is continuous if and only if its
precomposition with the inclusion `ofSupportedIn K_sub_Î© : ð““^{n}_{K}(E, F) â†’ ð““^{n}(Î©, F)` is
continuous for every compact `K âŠ† Î©`. -/
protected theorem continuous_iff_continuous_comp [Algebra â„ ð•œ] [IsScalarTower â„ ð•œ F]
    [Module ð•œ V] [IsScalarTower â„ ð•œ V] (f : ð““^{n}(Î©, F) â†’â‚—[ð•œ] V) :
    Continuous f â†” âˆ€ (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
      Continuous (f âˆ˜ ofSupportedIn K_sub_Î©) := by
  simp_rw [â† f.coe_restrictScalars â„]
  rw [continuous_iff_le_induced]
  have : @IsTopologicalAddGroup _ (induced (f.restrictScalars â„) t) _ :=
    topologicalAddGroup_induced _
  have : @ContinuousSMul â„ _ _ _ (induced (f.restrictScalars â„) t) := continuousSMul_induced _
  have : @LocallyConvexSpace â„ _ _ _ _ _ (induced (f.restrictScalars â„) t) := .induced _
  simp_rw [topologicalSpace_le_iff, originalTop, iSupâ‚‚_le_iff, â† continuous_iff_le_induced,
    continuous_coinduced_dom]

variable (ð•œ) in
/-- Reformulation of the universal property of the topology on `ð““^{n}(Î©, F)`, in the form of a
custom constructor for continuous linear maps `ð““^{n}(Î©, F) â†’L[ð•œ] V`, where `V` is an arbitrary
locally convex topological vector space. -/
@[simps]
protected def mkCLM [Algebra â„ ð•œ] [IsScalarTower â„ ð•œ F] [Module ð•œ V] [IsScalarTower â„ ð•œ V]
    (toFun : ð““^{n}(Î©, F) â†’ V)
    (map_add : âˆ€ f g, toFun (f + g) = toFun f + toFun g)
    (map_smul : âˆ€ c : ð•œ, âˆ€ f, toFun (c â€¢ f) = c â€¢ toFun f)
    (cont : âˆ€ (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
      Continuous (toFun âˆ˜ ofSupportedIn K_sub_Î©)) :
    ð““^{n}(Î©, F) â†’L[ð•œ] V :=
  letI Î¦ : ð““^{n}(Î©, F) â†’â‚—[ð•œ] V := âŸ¨âŸ¨toFun, map_addâŸ©, map_smulâŸ©
  { toLinearMap := Î¦
    cont := show Continuous Î¦ by rwa [TestFunction.continuous_iff_continuous_comp] }

end Topology

section ToBoundedContinuousFunctionCLM

variable (ð•œ) in
/-- The inclusion of the space `ð““^{n}(Î©, F)` into the space `E â†’áµ‡ F` of bounded continuous
functions as a continuous `ð•œ`-linear map. -/
@[simps! apply]
noncomputable def toBoundedContinuousFunctionCLM [Algebra â„ ð•œ] [IsScalarTower â„ ð•œ F] :
    ð““^{n}(Î©, F) â†’L[ð•œ] E â†’áµ‡ F :=
  TestFunction.mkCLM ð•œ (â†‘) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ (ContDiffMapSupportedIn.toBoundedContinuousFunctionCLM ð•œ).continuous)

lemma toBoundedContinuousFunctionCLM_eq_of_scalars [Algebra â„ ð•œ] [IsScalarTower â„ ð•œ F] (ð•œ' : Type*)
    [NontriviallyNormedField ð•œ'] [NormedSpace ð•œ' F] [Algebra â„ ð•œ'] [IsScalarTower â„ ð•œ' F] :
    (toBoundedContinuousFunctionCLM ð•œ : ð““^{n}(Î©, F) â†’ _) = toBoundedContinuousFunctionCLM ð•œ' :=
  rfl

variable (ð•œ) in
theorem injective_toBoundedContinuousFunctionCLM [Algebra â„ ð•œ] [IsScalarTower â„ ð•œ F] :
    Function.Injective (toBoundedContinuousFunctionCLM ð•œ : ð““^{n}(Î©, F) â†’L[ð•œ] E â†’áµ‡ F) :=
  fun f g â†¦ by simp [toBoundedContinuousFunctionCLM]

instance : T3Space ð““^{n}(Î©, F) :=
  suffices T2Space ð““^{n}(Î©, F) from inferInstance
  .of_injective_continuous (injective_toBoundedContinuousFunctionCLM â„)
    (ContinuousLinearMap.continuous _)

end ToBoundedContinuousFunctionCLM

section Integral

open MeasureTheory

variable {m : MeasurableSpace E} [OpensMeasurableSpace E] {Fâ‚ Fâ‚‚ Fâ‚ƒ : Type*}
  [NormedAddCommGroup Fâ‚] [NormedSpace ð•œ Fâ‚] [NormedSpace â„ Fâ‚]
  [NormedAddCommGroup Fâ‚‚] [NormedSpace ð•œ Fâ‚‚]
  [NormedAddCommGroup Fâ‚ƒ] [NormedSpace ð•œ Fâ‚ƒ]

@[fun_prop]
protected theorem stronglyMeasurable (f : ð““^{n}(Î©, F)) :
    StronglyMeasurable f := by
  exact f.continuous.stronglyMeasurable_of_hasCompactSupport f.hasCompactSupport

@[fun_prop]
protected theorem aestronglyMeasurable {Î¼ : Measure E} (f : ð““^{n}(Î©, F)) :
    AEStronglyMeasurable f Î¼ :=
  f.stronglyMeasurable.aestronglyMeasurable

protected theorem memLp_top {Î¼ : Measure E} (f : ð““^{n}(Î©, F)) :
    MemLp f âŠ¤ Î¼ :=
  f.continuous.memLp_top_of_hasCompactSupport f.hasCompactSupport Î¼

protected theorem integrable {Î¼ : Measure E}
    (H : âˆ€ K : Set E, IsCompact K â†’ K âŠ† Î© â†’ IsFiniteMeasure (Î¼.restrict K)) -- TODO
    (f : ð““^{n}(Î©, F)) : Integrable f Î¼ := by
  rw [â† integrableOn_iff_integrable_of_support_subset (subset_tsupport f)]
  specialize H (tsupport f) f.hasCompactSupport f.tsupport_subset
  exact f.continuous.integrable_of_hasCompactSupport f.hasCompactSupport

protected theorem integrable_bilin (B : Fâ‚ â†’L[ð•œ] Fâ‚‚ â†’L[ð•œ] Fâ‚ƒ) {Î¼ : Measure E} {Ï† : E â†’ Fâ‚‚}
    (hÏ† : LocallyIntegrableOn Ï† Î© Î¼) (f : ð““^{n}(Î©, Fâ‚)) :
    Integrable (fun x â†¦ B (f x) (Ï† x)) Î¼ := by
  suffices IntegrableOn (fun x â†¦ B (f x) (Ï† x)) (tsupport f) Î¼ by
    rwa [integrableOn_iff_integrable_of_support_subset] at this
    refine subset_trans ?_ (subset_tsupport f)
    exact fun x hx hfx â†¦ hx (by simp [hfx])
  replace hÏ† := hÏ†.integrableOn_compact_subset f.tsupport_subset f.hasCompactSupport
  rw [IntegrableOn, â† memLp_one_iff_integrable] at hÏ† âŠ¢
  exact B.memLp_of_bilin 1 f.memLp_top hÏ†

variable [SMulCommClass â„ ð•œ Fâ‚] [NormedSpace â„ Fâ‚ƒ] [SMulCommClass â„ ð•œ Fâ‚ƒ]

noncomputable def integralAgainstBilinLM (B : Fâ‚ â†’L[ð•œ] Fâ‚‚ â†’L[ð•œ] Fâ‚ƒ) (Î¼ : Measure E) (Ï† : E â†’ Fâ‚‚) :
    ð““^{n}(Î©, Fâ‚) â†’â‚—[ð•œ] Fâ‚ƒ where
  toFun f := open scoped Classical in
    if LocallyIntegrableOn Ï† Î© Î¼ then âˆ« x, B (f x) (Ï† x) âˆ‚Î¼ else 0
  map_add' f g := by
    split_ifs with hÏ†
    Â· simp_rw [coe_add, Pi.add_apply, map_add, ContinuousLinearMap.add_apply,
        integral_add (f.integrable_bilin B hÏ†) (g.integrable_bilin B hÏ†)]
    Â· simp
  map_smul' c f := by
    split_ifs with hÏ†
    Â· simp_rw [coe_smul, Pi.smul_apply, map_smul, ContinuousLinearMap.smul_apply,
        integral_smul c, RingHom.id_apply]
    Â· simp

@[simp]
lemma integralAgainstBilinLM_apply {B : Fâ‚ â†’L[ð•œ] Fâ‚‚ â†’L[ð•œ] Fâ‚ƒ} {Î¼ : Measure E} {Ï† : E â†’ Fâ‚‚}
    (hÏ† : LocallyIntegrableOn Ï† Î© Î¼) {f : ð““^{n}(Î©, Fâ‚)} :
    integralAgainstBilinLM B Î¼ Ï† f = âˆ« x, B (f x) (Ï† x) âˆ‚Î¼ := by
  simp [integralAgainstBilinLM, hÏ†]

lemma integralAgainstBilinLM_eq_zero {B : Fâ‚ â†’L[ð•œ] Fâ‚‚ â†’L[ð•œ] Fâ‚ƒ} {Î¼ : Measure E} {Ï† : E â†’ Fâ‚‚}
    (hÏ† : Â¬ LocallyIntegrableOn Ï† Î© Î¼) :
    (integralAgainstBilinLM B Î¼ Ï† : ð““^{n}(Î©, Fâ‚) â†’â‚—[ð•œ] Fâ‚ƒ) = 0 := by
  ext
  simp [integralAgainstBilinLM, hÏ†]

lemma integralAgainstBilinLM_ofSupportedIn {B : Fâ‚ â†’L[ð•œ] Fâ‚‚ â†’L[ð•œ] Fâ‚ƒ} {Î¼ : Measure E} {Ï† : E â†’ Fâ‚‚}
    (hÏ† : LocallyIntegrableOn Ï† Î© Î¼) {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©)
    {f : ð““^{n}_{K}(E, Fâ‚)} :
    integralAgainstBilinLM B Î¼ Ï† (ofSupportedIn K_sub_Î© f) =
      ContDiffMapSupportedIn.integralAgainstBilinLM B Î¼ Ï† f := by
  have hÏ†' := hÏ†.integrableOn_compact_subset K_sub_Î© K.isCompact
  simp [hÏ†, hÏ†']

variable [NormedSpace â„ Fâ‚‚]

-- TODO: generalize to ð•œ
noncomputable def integralAgainstBilinCLM (B : Fâ‚ â†’L[â„] Fâ‚‚ â†’L[â„] Fâ‚ƒ) (Î¼ : Measure E) (Ï† : E â†’ Fâ‚‚) :
    ð““^{n}(Î©, Fâ‚) â†’L[â„] Fâ‚ƒ where
  toLinearMap := integralAgainstBilinLM B Î¼ Ï†
  cont := show Continuous (integralAgainstBilinLM B Î¼ Ï†) by
    rw [TestFunction.continuous_iff_continuous_comp]
    intro K K_sub_Î©
    by_cases hÏ† : LocallyIntegrableOn Ï† Î© Î¼
    Â· refine .congr ?_ fun f â†¦ (integralAgainstBilinLM_ofSupportedIn hÏ† K_sub_Î©).symm
      exact ContDiffMapSupportedIn.integralAgainstBilinCLM B Î¼ Ï† |>.continuous
    Â· simpa [integralAgainstBilinLM_eq_zero hÏ†] using continuous_zero

end Integral

end TestFunction
