/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci, Anatole Dedecker
-/
module

public import Mathlib.Analysis.Distribution.ContDiffMapSupportedIn
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.Topology.ContinuousMap.Bounded.Normed

/-!
# Continuously differentiable functions with compact support

This file develops the basic theory of bundled `n`-times continuously differentiable functions
with compact support contained in some open set `Î©`. More explicitly, given normed spaces `E`
and `F`, an open set `Î© : Opens E` and `n : â„•âˆ`, we are interested in the space `ğ““^{n}(Î©, F)` of
maps `f : E â†’ F` such that:

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` has compact support: `HasCompactSupport f`.
- the support of `f` is inside the open set `Î©`: `tsupport f âŠ† Î©`.

This exists as a bundled type to equip it with the canonical LF topology induced by the inclusions
`ğ““_{K}^{n}(Î©, F) â†’ ğ““^{n}(Î©, F)` (see `ContDiffMapSupportedIn`). The dual space is then the space of
distributions, or "weak solutions" to PDEs, on `Î©`.

## Main definitions

- `TestFunction Î© F n`: the type of bundled `n`-times continuously differentiable
  functions `E â†’ F` with compact support contained in `Î©`.
- `TestFunction.topologicalSpace`: the canonical LF topology on `ğ““^{n}(Î©, F)`. It is the
  locally convex inductive limit of the topologies on each `ğ““_{K}^{n}(Î©, F)`.

## Main statements

- `TestFunction.continuous_iff_continuous_comp` a linear map from `ğ““^{n}(E, F)`
  to a locally convex space is continuous iff its restriction to `ğ““^{n}_{K}(E, F)` is
  continuous for each compact set `K`. We will later translate this concretely in terms
  of seminorms.

## Notation

- `ğ““^{n}(Î©, F)`: the space of bundled `n`-times continuously differentiable functions `E â†’ F`
  with compact support contained in `Î©`.
- `ğ““(Î©, F)`: the space of bundled smooth (infinitely differentiable) functions `E â†’ F`
  with compact support contained in `Î©`, i.e. `ğ““^{âŠ¤}(Î©, F)`.

## Tags

distributions, test function
-/

@[expose] public section

open Function Seminorm SeminormFamily Set TopologicalSpace UniformSpace
open scoped BoundedContinuousFunction NNReal Topology

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {Î© : Opens E}
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [SMulCommClass â„ ğ•œ F]
  {n : â„•âˆ}

variable (Î© F n) in
/-- The type of bundled `n`-times continuously differentiable maps with compact support -/
structure TestFunction : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected hasCompactSupport' : HasCompactSupport toFun
  protected tsupport_subset' : tsupport toFun âŠ† Î©

/-- Notation for the space of bundled `n`-times continuously differentiable maps
with compact support. -/
scoped[Distributions] notation "ğ““^{" n "}(" Î© ", " F ")" => TestFunction Î© F n

/-- Notation for the space of "test functions", i.e. bundled smooth (infinitely differentiable) maps
with compact support. -/
scoped[Distributions] notation "ğ““(" Î© ", " F ")" => TestFunction Î© F âŠ¤

open Distributions

/-- `TestFunctionClass B Î© F n` states that `B` is a type of `n`-times continously
differentiable functions `E â†’ F` with compact support contained in `Î© : Opens E`. -/
class TestFunctionClass (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  map_hasCompactSupport (f : B) : HasCompactSupport f
  tsupport_map_subset (f : B) : tsupport f âŠ† Î©

open TestFunctionClass

namespace TestFunctionClass

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) [TestFunctionClass B Î© F n] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) [TestFunctionClass B Î© F n] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    obtain âŸ¨C, hCâŸ© := (map_continuous f).bounded_above_of_compact_support (map_hasCompactSupport f)
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

end TestFunctionClass

namespace TestFunction

instance toTestFunctionClass : TestFunctionClass ğ““^{n}(Î©, F) Î© F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  map_hasCompactSupport f := f.hasCompactSupport'
  tsupport_map_subset f := f.tsupport_subset'

protected theorem contDiff (f : ğ““^{n}(Î©, F)) : ContDiff â„ n f := map_contDiff f
protected theorem hasCompactSupport (f : ğ““^{n}(Î©, F)) : HasCompactSupport f :=
  map_hasCompactSupport f
protected theorem tsupport_subset (f : ğ““^{n}(Î©, F)) : tsupport f âŠ† Î© := tsupport_map_subset f

@[simp]
theorem toFun_eq_coe {f : ğ““^{n}(Î©, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.coe (f : ğ““^{n}(Î©, F)) : E â†’ F := f

initialize_simps_projections TestFunction (toFun â†’ coe, as_prefix coe)

@[ext]
theorem ext {f g : ğ““^{n}(Î©, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `TestFunction` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : ğ““^{n}(Î©, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  hasCompactSupport' := h.symm â–¸ f.hasCompactSupport
  tsupport_subset' := h.symm â–¸ f.tsupport_subset

@[simp]
theorem coe_copy (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem coe_toBoundedContinuousFunction (f : ğ““^{n}(Î©, F)) :
    (f : BoundedContinuousFunction E F) = (f : E â†’ F) := rfl

section AddCommGroup

@[simps -fullyApplied]
instance : Zero ğ““^{n}(Î©, F) where
  zero := âŸ¨0, contDiff_zero_fun, .zero, by simp only [tsupport_zero, empty_subset]âŸ©

@[simps -fullyApplied]
instance : Add ğ““^{n}(Î©, F) where
  add f g := âŸ¨f + g, f.contDiff.add g.contDiff, f.hasCompactSupport.add g.hasCompactSupport,
    tsupport_add f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Neg ğ““^{n}(Î©, F) where
  neg f := âŸ¨-f, f.contDiff.neg, f.hasCompactSupport.neg, tsupport_neg f â–¸ f.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Sub ğ““^{n}(Î©, F) where
  sub f g := âŸ¨f - g, f.contDiff.sub g.contDiff, f.hasCompactSupport.sub g.hasCompactSupport,
    tsupport_sub f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    SMul R ğ““^{n}(Î©, F) where
  smul c f := âŸ¨c â€¢ f, f.contDiff.const_smul c, f.hasCompactSupport.smul_left,
    tsupport_smul_subset_right _ _ |>.trans f.tsupport_subsetâŸ©

instance : AddCommGroup ğ““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

variable (Î© F n) in
/-- Coercion as an additive homomorphism. -/
@[simps -fullyApplied]
def coeFnAddMonoidHom : ğ““^{n}(Î©, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ğ““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.module R (coeFnAddMonoidHom Î© F n) fun _ _ â†¦ rfl

end Module

open ContDiffMapSupportedIn

/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` when `K âŠ† Î©`. -/
@[simps -fullyApplied]
def ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
    ğ““^{n}(Î©, F) :=
  âŸ¨f, f.contDiff, f.compact_supp, f.tsupport_subset.trans K_sub_Î©âŸ©

/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)`, when `K âŠ† Î©`, as a linear map. -/
def ofSupportedInLM (R) [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    ğ““^{n}_{K}(E, F) â†’â‚—[R] ğ““^{n}(Î©, F) where
  toFun f := ofSupportedIn K_sub_Î© f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp] theorem ofSupportedInLM_apply (R) [Semiring R] [Module R F] [SMulCommClass â„ R F]
    [ContinuousConstSMul R F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
    ofSupportedInLM R K_sub_Î© f = ofSupportedIn K_sub_Î© f :=
  rfl

section Topology

variable {V : Type*} [AddCommGroup V] [Module â„ V] [t : TopologicalSpace V]
  [IsTopologicalAddGroup V] [ContinuousSMul â„ V] [LocallyConvexSpace â„ V]

variable (Î© F n) in
/-- The original topology on `ğ““^{n}(E, F)`, defined as the supremum over all compacts of the
topologies from each `ğ““^{n}_{K}(E, F)`. -/
noncomputable def originalTop : TopologicalSpace ğ““^{n}(Î©, F) :=
  â¨† (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
    coinduced (ofSupportedIn K_sub_Î©) ContDiffMapSupportedIn.topologicalSpace

variable (Î© F n) in
noncomputable instance topologicalSpace : TopologicalSpace ğ““^{n}(Î©, F) :=
  sInf {t : TopologicalSpace ğ““^{n}(Î©, F) | originalTop Î© F n â‰¤ t âˆ§
    @IsTopologicalAddGroup ğ““^{n}(Î©, F) t _ âˆ§
    @ContinuousSMul â„ ğ““^{n}(Î©, F) _ _ t âˆ§
    @LocallyConvexSpace â„ ğ““^{n}(Î©, F) _ _ _ _ t}

noncomputable instance : IsTopologicalAddGroup ğ““^{n}(Î©, F) := by
  apply topologicalAddGroup_sInf
  exact fun t âŸ¨_, ht, _, _âŸ© â†¦ ht

--TODO: deduce for `RCLike` field `ğ•œ`
noncomputable instance : ContinuousSMul â„ ğ““^{n}(Î©, F) := by
  apply continuousSMul_sInf
  exact fun t âŸ¨_, _, ht, _âŸ© â†¦ ht

noncomputable instance : LocallyConvexSpace â„ ğ““^{n}(Î©, F) := by
  apply LocallyConvexSpace.sInf
  exact fun t âŸ¨_, _, _, htâŸ© â†¦ ht

theorem originalTop_le : originalTop Î© F n â‰¤ topologicalSpace Î© F n :=
  le_sInf fun _t âŸ¨ht, _âŸ© â†¦ ht

theorem topologicalSpace_le_iff {t : TopologicalSpace ğ““^{n}(Î©, F)}
    [@IsTopologicalAddGroup _ t _] [@ContinuousSMul â„ _ _ _ t]
    [@LocallyConvexSpace â„ _ _ _ _ _ t] :
    topologicalSpace Î© F n â‰¤ t â†” originalTop Î© F n â‰¤ t :=
  âŸ¨le_trans originalTop_le, fun H â†¦ sInf_le âŸ¨H, inferInstance, inferInstance, inferInstanceâŸ©âŸ©

/-- For every compact `K âŠ† Î©`, the inclusion map `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` is
continuous. We will show later that it is in fact a topological embedding. -/
theorem continuous_ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    Continuous (ofSupportedIn K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)) := by
  rw [continuous_iff_coinduced_le]
  exact le_trans (le_iSupâ‚‚_of_le K K_sub_Î© le_rfl) originalTop_le

/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)`, when `K âŠ† Î©`, as a continuous
linear map. -/
def ofSupportedInCLM (R) [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    ğ““^{n}_{K}(E, F) â†’L[R] ğ““^{n}(Î©, F) where
  toLinearMap := ofSupportedInLM R K_sub_Î©
  cont := continuous_ofSupportedIn K_sub_Î©

@[simp] theorem ofSupportedInCLM_apply (R) [Semiring R] [Module R F] [SMulCommClass â„ R F]
    [ContinuousConstSMul R F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
    ofSupportedInCLM R K_sub_Î© f = ofSupportedIn K_sub_Î© f :=
  rfl

-- TODO: Should we spell it using `âˆ˜â‚—`?
/-- The **universal property** of the topology on `ğ““^{n}(Î©, F)`: a **linear** map from
`ğ““^{n}(Î©, F)` to a locally convex topological vector space is continuous if and only if its
precomposition with the inclusion `ofSupportedIn K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` is
continuous for every compact `K âŠ† Î©`. -/
protected theorem continuous_iff_continuous_comp (f : ğ““^{n}(Î©, F) â†’â‚—[â„] V) :
    Continuous f â†” âˆ€ (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
      Continuous (f âˆ˜ ofSupportedIn K_sub_Î©) := by
  rw [continuous_iff_le_induced]
  have : @IsTopologicalAddGroup _ (induced f t) _ := topologicalAddGroup_induced _
  have : @ContinuousSMul â„ _ _ _ (induced f t) := continuousSMul_induced _
  have : @LocallyConvexSpace â„ _ _ _ _ _ (induced f t) := .induced _
  simp_rw [topologicalSpace_le_iff, originalTop, iSupâ‚‚_le_iff, â† continuous_iff_le_induced,
    continuous_coinduced_dom]

end Topology

end TestFunction
