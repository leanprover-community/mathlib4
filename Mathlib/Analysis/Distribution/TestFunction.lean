/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci
-/

import Mathlib.Analysis.Calculus.ContDiff.Operations
import Mathlib.Topology.ContinuousMap.Bounded.Normed

/-!
# Continuously differentiable functions supported in a compact

This file develops the basic theory of `n`-times continuously differentiable functions with compact
support. That is, for `f : E â†’ F` (where `E`, `F` are normed spaces) and `n : â„•âˆž`,

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` has compact support: `HasCompactSupport f`.

## Main definitions

- `TestFunction E F n`: the type of `n`-times continuously differentiable
  functions `E â†’ F` with compact support.

## Notation

- `ð““^{n}(E, F)`:  the space of `n`-times continuously differentiable functions `E â†’ F`
  with compact support.
- `ð““(E, F)`:   the space of smooth (infinitely differentiable) functions `E â†’ F`
  with compact support i.e. `ð““^{âŠ¤}_{K}(E, F)`.

## Tags

distributions
-/

open TopologicalSpace SeminormFamily Set Function Seminorm UniformSpace
open scoped BoundedContinuousFunction Topology NNReal

variable (ð•œ E F : Type*) [NontriviallyNormedField ð•œ]
variable [NormedAddCommGroup E] [NormedSpace â„ E]
variable [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ð•œ F] [SMulCommClass â„ ð•œ F]
variable {n : â„•âˆž}

/-- The type of `n`-times continuously differentiable maps with compact support. -/
structure TestFunction (n : â„•âˆž) : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected compact_supp' : HasCompactSupport toFun

/-- Notation for the space of `n`-times continuously differentiable maps
with compact support. -/
scoped[Distributions] notation "ð““^{" n "}(" E ", " F ")" =>
  TestFunction E F n

/-- Notation for the space of "test functions", i.e. smooth (infinitely differentiable) maps
with compact support. -/
scoped[Distributions] notation "ð““(" E ", " F ")" =>
  TestFunction E F âŠ¤

open Distributions

/-- `TestFunctionClass B E F n K` states that `B` is a type of `n`-times continously
differentiable functions `E â†’ F` with compact support. -/
class TestFunctionClass (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆž) extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  compact_supp (f : B) : HasCompactSupport f

open TestFunctionClass

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B E F n] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*) (E F : outParam <| Type*)
    [NormedAddCommGroup E] [NormedAddCommGroup F] [NormedSpace â„ E] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B E F n] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    rcases (map_continuous f).bounded_above_of_compact_support (compact_supp f) with âŸ¨C, hCâŸ©
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

namespace TestFunction

instance toTestFunctionClass :
    TestFunctionClass ð““^{n}(E, F) E F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  compact_supp f := f.compact_supp'

variable {E F}

protected theorem contDiff (f : ð““^{n}(E, F)) : ContDiff â„ n f := map_contDiff f
protected theorem compact_supp (f : ð““^{n}(E, F)) : HasCompactSupport f := compact_supp f

@[simp]
theorem toFun_eq_coe {f : ð““^{n}(E, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.apply (f : ð““^{n}(E, F)) : E â†’F  := f

-- this must come after the coe_to_fun definition
initialize_simps_projections TestFunction (toFun â†’ apply)

@[ext]
theorem ext {f g : ð““^{n}(E, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `BoundedContDiffMap` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ð““^{n}(E, F)) (f' : E â†’ F) (h : f' = f) : ð““^{n}(E, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  compact_supp' := h.symm â–¸ f.compact_supp

@[simp]
theorem coe_copy (f : ð““^{n}(E, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ð““^{n}(E, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem toBoundedContinuousFunction_apply (f : ð““^{n}(E, F)) (x : E) :
   (f : BoundedContinuousFunction E F) x  = (f x) := rfl

section AddCommGroup

instance : Zero ð““^{n}(E, F) where
  zero := TestFunction.mk 0 contDiff_zero_fun HasCompactSupport.zero

@[simp]
lemma coe_zero : (0 : ð““^{n}(E, F)) = (0 : E â†’ F) :=
  rfl

@[simp]
lemma zero_apply (x : E) : (0 : ð““^{n}(E, F)) x = 0 :=
  rfl

instance : Add ð““^{n}(E, F) where
  add f g := TestFunction.mk (f + g) (f.contDiff.add g.contDiff) (f.compact_supp.add g.compact_supp)

@[simp]
lemma coe_add (f g : ð““^{n}(E, F)) : (f + g : ð““^{n}(E, F)) = (f : E â†’ F) + g :=
  rfl

@[simp]
lemma add_apply (f g : ð““^{n}(E, F)) (x : E) : (f + g) x = f x + g x :=
  rfl

instance : Neg ð““^{n}(E, F) where
  neg f := TestFunction.mk (-f) (f.contDiff.neg) (f.compact_supp.neg)

instance instSub : Sub ð““^{n}(E, F) :=
  âŸ¨fun f g =>
    âŸ¨f - g, (f.contDiff').sub (g.contDiff'),
    sub_eq_add_neg (f : E â†’ F) g â–¸ f.compact_supp.add g.compact_supp.neg
    âŸ©
  âŸ©

instance instSMul {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
   SMul R ð““^{n}(E, F) :=
  âŸ¨fun c f â†¦
    TestFunction.mk (c â€¢ (f : E â†’ F)) (f.contDiff.const_smul c)  f.compact_supp.smul_left
  âŸ©

@[simp]
lemma coe_smul {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    (c : R) (f : ð““^{n}(E, F)) : (c â€¢ f : ð““^{n}(E, F)) = c â€¢ (f : E â†’ F) :=
  rfl

@[simp]
lemma smul_apply {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F]
    (c : R) (f : ð““^{n}(E, F)) (x : E) : (c â€¢ f) x = c â€¢ (f x) :=
  rfl

instance instNSMul : SMul â„• ð““^{n}(E, F) :=
 âŸ¨fun c f â†¦
    {
      toFun := c â€¢ f
      contDiff' := (f.contDiff').const_smul c
      compact_supp' := f.compact_supp.smul_left
    }
  âŸ©

instance instZSMul : SMul â„¤ ð““^{n}(E, F) :=
 âŸ¨fun c f â†¦
    {
      toFun := c â€¢ f
      contDiff' := (f.contDiff').const_smul c
      compact_supp' := f.compact_supp.smul_left
    }
  âŸ©

instance : AddCommGroup ð““^{n}(E, F) :=
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ => rfl) (fun _ => rfl) (fun _ _ => rfl)
    (fun _ _ => rfl) fun _ _ => rfl

variable (E F K n)

/-- Coercion as an additive homomorphism. -/
def coeHom : ð““^{n}(E, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

variable {E F}

theorem coe_coeHom : (coeHom E F n : ð““^{n}(E, F) â†’ E â†’ F) = DFunLike.coe :=
  rfl

theorem coeHom_injective : Function.Injective (coeHom E F n) := by
  rw [coe_coeHom]
  exact DFunLike.coe_injective

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ð““^{n}(E, F) :=
  (coeHom_injective n).module R (coeHom E F n) fun _ _ => rfl

end Module

end TestFunction
#min_imports
