/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci
-/

import Mathlib.Analysis.Calculus.ContDiff.Defs
import Mathlib.Topology.ContinuousMap.Bounded.Normed

/-!
# Continuously differentiable bundled functions supported in a compact

This file develops the basic theory of bundled `n`-times continuously differentiable functions
with compact support. That is, for `f : E â†’ F` (where `E`, `F` are normed spaces) and `n : â„•âˆž`,

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` has compact support: `HasCompactSupport f`.

This exists as a bundled type to equip it with the canonical LF topology induced by the inclusions
`ð““_K^{n}(E, F) â†’ ð““^{n}(E, F)` (see `ContDiffMapSupportedIn`). The dual space is then the space of
distributions, or "weak solutions" to PDEs.

## Main definitions

- `TestFunction E F n`: the type of bundled `n`-times continuously differentiable
  functions `E â†’ F` with compact support.

## Notation

- `ð““^{n}(E, F)`: the space of bundled `n`-times continuously differentiable functions `E â†’ F`
  with compact support.
- `ð““(E, F)`: the space of bundled smooth (infinitely differentiable) functions `E â†’ F`
  with compact support i.e. `ð““^{âŠ¤}_{K}(E, F)`.

## Tags

distributions
-/

open TopologicalSpace SeminormFamily Set Function Seminorm UniformSpace
open scoped BoundedContinuousFunction Topology NNReal

variable (ð•œ : Type*) [NontriviallyNormedField ð•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : Opens E)
variable (F : Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
variable [NormedSpace ð•œ F] [SMulCommClass â„ ð•œ F]
variable {n : â„•âˆž}

variable (n) in
/-- The type of bundled `n`-times continuously differentiable maps with compact support. -/
structure TestFunction : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected compact_supp' : HasCompactSupport toFun
  protected supp_subset' : tsupport toFun âŠ† Î©

/-- Notation for the space of bundled `n`-times continuously differentiable maps
with compact support. -/
scoped[Distributions] notation "ð““^{" n "}(" Î© ", " F ")" =>
  TestFunction Î© F n

/-- Notation for the space of "test functions", i.e. bundled smooth (infinitely differentiable) maps
with compact support. -/
scoped[Distributions] notation "ð““(" Î© ", " F ")" =>
  TestFunction Î© F âŠ¤

namespace TestFunction

open Distributions

/-- `TestFunctionClass B E F n K` states that `B` is a type of `n`-times continously
differentiable functions `E â†’ F` with compact support. -/
class TestFunctionClass (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  compact_supp (f : B) : HasCompactSupport f
  supp_subset (f : B) : tsupport f âŠ† Î©

open TestFunctionClass

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B Î© F n] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆž) [TestFunctionClass B Î© F n] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    rcases (map_continuous f).bounded_above_of_compact_support (compact_supp f) with âŸ¨C, hCâŸ©
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

instance toTestFunctionClass :
    TestFunctionClass ð““^{n}(Î©, F) Î© F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  compact_supp f := f.compact_supp'
  supp_subset f := f.supp_subset'

variable {Î© F}

protected theorem contDiff (f : ð““^{n}(Î©, F)) : ContDiff â„ n f := map_contDiff f
protected theorem compact_supp (f : ð““^{n}(Î©, F)) : HasCompactSupport f := compact_supp f
protected theorem supp_subset (f : ð““^{n}(Î©, F)) : tsupport f âŠ† Î© := supp_subset f

@[simp]
theorem toFun_eq_coe {f : ð““^{n}(Î©, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.apply (f : ð““^{n}(Î©, F)) : E â†’F  := f

-- this must come after the coe_to_fun definition
initialize_simps_projections TestFunction (toFun â†’ apply)

@[ext]
theorem ext {f g : ð““^{n}(Î©, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `TestFunction` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : ð““^{n}(Î©, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  compact_supp' := h.symm â–¸ f.compact_supp
  supp_subset' := h.symm â–¸ f.supp_subset

@[simp]
theorem coe_copy (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ð““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem toBoundedContinuousFunction_apply (f : ð““^{n}(Î©, F)) (x : E) :
   (f : BoundedContinuousFunction E F) x  = (f x) := rfl

end TestFunction
