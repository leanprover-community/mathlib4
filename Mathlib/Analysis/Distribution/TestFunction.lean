/-
Copyright (c) 2025 Luigi Massacci. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Luigi Massacci, Anatole Dedecker
-/
module

public import Mathlib.Analysis.Distribution.ContDiffMapSupportedIn
public import Mathlib.Analysis.RCLike.Basic
public import Mathlib.Topology.ContinuousMap.Bounded.Normed

/-!
# Continuously differentiable functions with compact support

This file develops the basic theory of bundled `n`-times continuously differentiable functions
with compact support contained in some open set `Î©`. More explicitly, given normed spaces `E`
and `F`, an open set `Î© : Opens E` and `n : â„•âˆ`, we are interested in the space `ğ““^{n}(Î©, F)` of
maps `f : E â†’ F` such that:

- `f` is `n`-times continuously differentiable: `ContDiff â„ n f`.
- `f` has compact support: `HasCompactSupport f`.
- the support of `f` is inside the open set `Î©`: `tsupport f âŠ† Î©`.

This exists as a bundled type to equip it with the canonical LF topology induced by the inclusions
`ğ““_{K}^{n}(Î©, F) â†’ ğ““^{n}(Î©, F)` (see `ContDiffMapSupportedIn`). The dual space is then the space of
distributions, or "weak solutions" to PDEs, on `Î©`.

## Main definitions

- `TestFunction Î© F n`: the type of bundled `n`-times continuously differentiable
  functions `E â†’ F` with compact support contained in `Î©`.
- `TestFunction.topologicalSpace`: the canonical LF topology on `ğ““^{n}(Î©, F)`. It is the
  locally convex inductive limit of the topologies on each `ğ““_{K}^{n}(Î©, F)`.

## Main statements

- `TestFunction.continuous_iff_continuous_comp`: a linear map from `ğ““^{n}(E, F)`
  to a locally convex space is continuous iff its restriction to `ğ““^{n}_{K}(E, F)` is
  continuous for each compact set `K`. We will later translate this concretely in terms
  of seminorms.

## Notation

- `ğ““^{n}(Î©, F)`: the space of bundled `n`-times continuously differentiable functions `E â†’ F`
  with compact support contained in `Î©`.
- `ğ““(Î©, F)`: the space of bundled smooth (infinitely differentiable) functions `E â†’ F`
  with compact support contained in `Î©`, i.e. `ğ““^{âŠ¤}(Î©, F)`.

## Tags

distributions, test function
-/

@[expose] public section

open Function Seminorm SeminormFamily Set TopologicalSpace UniformSpace
open scoped BoundedContinuousFunction NNReal Topology ContDiff

variable {ğ•œ ğ•‚ : Type*} [NontriviallyNormedField ğ•œ] [RCLike ğ•‚]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {Î© : Opens E}
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [NormedSpace ğ•œ F] [NormedSpace ğ•‚ F]
  {F' : Type*} [NormedAddCommGroup F'] [NormedSpace â„ F'] [NormedSpace ğ•œ F'] [NormedSpace ğ•‚ F']
  {n k : â„•âˆ}

variable (Î© F n) in
/-- The type of bundled `n`-times continuously differentiable maps with compact support -/
structure TestFunction : Type _ where
  /-- The underlying function. Use coercion instead. -/
  protected toFun : E â†’ F
  protected contDiff' : ContDiff â„ n toFun
  protected hasCompactSupport' : HasCompactSupport toFun
  protected tsupport_subset' : tsupport toFun âŠ† Î©

/-- Notation for the space of bundled `n`-times continuously differentiable maps
with compact support. -/
scoped[Distributions] notation "ğ““^{" n "}(" Î© ", " F ")" => TestFunction Î© F n

/-- Notation for the space of "test functions", i.e. bundled smooth (infinitely differentiable) maps
with compact support. -/
scoped[Distributions] notation "ğ““(" Î© ", " F ")" => TestFunction Î© F âŠ¤

open Distributions

/-- `TestFunctionClass B Î© F n` states that `B` is a type of `n`-times continuously
differentiable functions `E â†’ F` with compact support contained in `Î© : Opens E`. -/
class TestFunctionClass (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) extends FunLike B E F where
  map_contDiff (f : B) : ContDiff â„ n f
  map_hasCompactSupport (f : B) : HasCompactSupport f
  tsupport_map_subset (f : B) : tsupport f âŠ† Î©

open TestFunctionClass

namespace TestFunctionClass

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) [TestFunctionClass B Î© F n] :
    ContinuousMapClass B E F where
  map_continuous f := (map_contDiff f).continuous

instance (B : Type*)
    {E : outParam <| Type*} [NormedAddCommGroup E] [NormedSpace â„ E] (Î© : outParam <| Opens E)
    (F : outParam <| Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
    (n : outParam â„•âˆ) [TestFunctionClass B Î© F n] :
    BoundedContinuousMapClass B E F where
  map_bounded f := by
    obtain âŸ¨C, hCâŸ© := (map_continuous f).bounded_above_of_compact_support (map_hasCompactSupport f)
    exact map_bounded (BoundedContinuousFunction.ofNormedAddCommGroup f (map_continuous f) C hC)

end TestFunctionClass

namespace TestFunction

instance toTestFunctionClass : TestFunctionClass ğ““^{n}(Î©, F) Î© F n where
  coe f := f.toFun
  coe_injective' f g h := by cases f; cases g; congr
  map_contDiff f := f.contDiff'
  map_hasCompactSupport f := f.hasCompactSupport'
  tsupport_map_subset f := f.tsupport_subset'

protected theorem contDiff (f : ğ““^{n}(Î©, F)) : ContDiff â„ n f := map_contDiff f
protected theorem hasCompactSupport (f : ğ““^{n}(Î©, F)) : HasCompactSupport f :=
  map_hasCompactSupport f
protected theorem tsupport_subset (f : ğ““^{n}(Î©, F)) : tsupport f âŠ† Î© := tsupport_map_subset f

@[simp]
theorem toFun_eq_coe {f : ğ““^{n}(Î©, F)} : f.toFun = (f : E â†’ F) :=
  rfl

/-- See note [custom simps projection]. -/
def Simps.coe (f : ğ““^{n}(Î©, F)) : E â†’ F := f

initialize_simps_projections TestFunction (toFun â†’ coe, as_prefix coe)

@[ext]
theorem ext {f g : ğ““^{n}(Î©, F)} (h : âˆ€ a, f a = g a) : f = g :=
  DFunLike.ext _ _ h

/-- Copy of a `TestFunction` with a new `toFun` equal to the old one. Useful to fix
definitional equalities. -/
protected def copy (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : ğ““^{n}(Î©, F) where
  toFun := f'
  contDiff' := h.symm â–¸ f.contDiff
  hasCompactSupport' := h.symm â–¸ f.hasCompactSupport
  tsupport_subset' := h.symm â–¸ f.tsupport_subset

@[simp]
theorem coe_copy (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : â‡‘(f.copy f' h) = f' :=
  rfl

theorem copy_eq (f : ğ““^{n}(Î©, F)) (f' : E â†’ F) (h : f' = f) : f.copy f' h = f :=
  DFunLike.ext' h

@[simp]
theorem coe_toBoundedContinuousFunction (f : ğ““^{n}(Î©, F)) :
    (f : BoundedContinuousFunction E F) = (f : E â†’ F) := rfl

@[simp]
theorem coe_mk {f : E â†’ F} {contDiff : ContDiff â„ n f} {hasCompactSupport : HasCompactSupport f}
    {tsupport_subset : tsupport f âŠ† Î©} :
    TestFunction.mk f contDiff hasCompactSupport tsupport_subset = f :=
  rfl

section AddCommGroup

@[simps -fullyApplied]
instance : Zero ğ““^{n}(Î©, F) where
  zero := âŸ¨0, contDiff_zero_fun, .zero, by simp only [tsupport_zero, empty_subset]âŸ©

@[simps -fullyApplied]
instance : Add ğ““^{n}(Î©, F) where
  add f g := âŸ¨f + g, f.contDiff.add g.contDiff, f.hasCompactSupport.add g.hasCompactSupport,
    tsupport_add f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Neg ğ““^{n}(Î©, F) where
  neg f := âŸ¨-f, f.contDiff.neg, f.hasCompactSupport.neg, tsupport_neg f â–¸ f.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance : Sub ğ““^{n}(Î©, F) where
  sub f g := âŸ¨f - g, f.contDiff.sub g.contDiff, f.hasCompactSupport.sub g.hasCompactSupport,
    tsupport_sub f g |>.trans <| union_subset f.tsupport_subset g.tsupport_subsetâŸ©

@[simps -fullyApplied]
instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    SMul R ğ““^{n}(Î©, F) where
  smul c f := âŸ¨c â€¢ f, f.contDiff.const_smul c, f.hasCompactSupport.smul_left,
    tsupport_smul_subset_right _ _ |>.trans f.tsupport_subsetâŸ©

instance : AddCommGroup ğ““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.addCommGroup _ rfl (fun _ _ â†¦ rfl) (fun _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)

variable (Î© F n) in
/-- Coercion as an additive homomorphism. -/
@[simps -fullyApplied]
def coeFnAddMonoidHom : ğ““^{n}(Î©, F) â†’+ E â†’ F where
  toFun f := f
  map_zero' := coe_zero
  map_add' _ _ := rfl

end AddCommGroup

section Module

instance {R} [Semiring R] [Module R F] [SMulCommClass â„ R F] [ContinuousConstSMul R F] :
    Module R ğ““^{n}(Î©, F) := fast_instance%
  DFunLike.coe_injective.module R (coeFnAddMonoidHom Î© F n) fun _ _ â†¦ rfl

instance {R S} [Semiring R] [Semiring S] [Module R F] [Module S F] [SMulCommClass â„ R F]
    [SMulCommClass â„ S F] [ContinuousConstSMul R F] [ContinuousConstSMul S F] [SMul R S]
    [IsScalarTower R S F] :
    IsScalarTower R S ğ““^{n}(Î©, F) where
  smul_assoc _ _ _ := by ext; simp

end Module

open ContDiffMapSupportedIn

/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` when `K âŠ† Î©`. -/
@[simps -fullyApplied]
def ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
    ğ““^{n}(Î©, F) :=
  âŸ¨f, f.contDiff, f.compact_supp, f.tsupport_subset.trans K_sub_Î©âŸ©

variable (ğ•œ) in
/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)`, when `K âŠ† Î©`, as a linear map. -/
def ofSupportedInLM [SMulCommClass â„ ğ•œ F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    ğ““^{n}_{K}(E, F) â†’â‚—[ğ•œ] ğ““^{n}(Î©, F) where
  toFun f := ofSupportedIn K_sub_Î© f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl

@[simp] theorem coe_ofSupportedInLM [SMulCommClass â„ ğ•œ F] {K : Compacts E}
    (K_sub_Î© : (K : Set E) âŠ† Î©) :
    (ofSupportedInLM ğ•œ K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)) = ofSupportedIn K_sub_Î© :=
  rfl

variable (ğ•œ n k) in
/-- `fderivWithOrderLM ğ•œ n k` is the `ğ•œ`-linear-map sending `f : ğ““^{n}(Î©, F)` to
its derivative as an element of `ğ““^{k}(Î©, E â†’L[â„] F)`.
This only makes mathematical sense if `k + 1 â‰¤ n`, otherwise we define it as the zero map.

See `fderivLM` for the very common case where everything is infinitely differentiable.

This is subsumed by `fderivWithOrderCLM`, which also bundles the continuity. -/
noncomputable def fderivWithOrderLM [SMulCommClass â„ ğ•œ F] :
    ğ““^{n}(Î©, F) â†’â‚—[ğ•œ] ğ““^{k}(Î©, E â†’L[â„] F) where
  toFun f :=
    if hk : k + 1 â‰¤ n then
      âŸ¨fderiv â„ f, f.contDiff.fderiv_right (mod_cast hk),
        f.hasCompactSupport.fderiv â„, tsupport_fderiv_subset â„ |>.trans f.tsupport_subsetâŸ©
    else 0
  map_add' f g := by
    split_ifs with hk
    Â· have hk' : 1 â‰¤ (n : WithTop â„•âˆ) := mod_cast (le_of_add_le_right hk)
      ext
      simp [fderiv_add (f.contDiff.differentiable hk').differentiableAt
                       (g.contDiff.differentiable hk').differentiableAt]
    Â· simp
  map_smul' c f := by
    split_ifs with hk
    Â· have hk' : 1 â‰¤ (n : WithTop â„•âˆ) := mod_cast (le_of_add_le_right hk)
      ext
      simp [fderiv_const_smul (f.contDiff.differentiable hk').differentiableAt]
    Â· simp

@[simp]
lemma fderivWithOrderLM_apply [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) :
    fderivWithOrderLM ğ•œ n k f = if k + 1 â‰¤ n then fderiv â„ f else 0 := by
  rw [fderivWithOrderLM]
  split_ifs <;> rfl

lemma fderivWithOrderLM_apply_of_le [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) (hk : k + 1 â‰¤ n) :
    fderivWithOrderLM ğ•œ n k f = fderiv â„ f := by
  simp [hk]

lemma fderivWithOrderLM_apply_of_gt [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) (hk : Â¬ (k + 1 â‰¤ n)) :
    fderivWithOrderLM ğ•œ n k f = 0 := by
  ext : 1
  simp [hk]

variable (ğ•œ) in
lemma fderivWithOrderLM_eq_of_scalars [SMulCommClass â„ ğ•œ F] (ğ•œ' : Type*)
    [NontriviallyNormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (fderivWithOrderLM ğ•œ n k : ğ““^{n}(Î©, F) â†’ _) = fderivWithOrderLM ğ•œ' n k :=
  rfl

variable (ğ•œ) in
lemma fderivWithOrderLM_ofSupportedIn [SMulCommClass â„ ğ•œ F] {K : Compacts E}
    (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
    fderivWithOrderLM ğ•œ n k (ofSupportedIn K_sub_Î© f) =
      ofSupportedIn K_sub_Î© (ContDiffMapSupportedIn.fderivWithOrderLM ğ•œ n k f) := by
  ext
  simp

variable (ğ•œ) in
/-- `fderivLM ğ•œ` is the `ğ•œ`-linear-map sending `f : ğ““_{K}(E, F)` to
its derivative as an element of `ğ““_{K}(E, E â†’L[â„] F)`.

See also `fderivWithOrderLM` if you need more control on the regularities.

This is subsumed by `fderivCLM`, which also bundles the continuity. -/
noncomputable def fderivLM [SMulCommClass â„ ğ•œ F] :
    ğ““(Î©, F) â†’â‚—[ğ•œ] ğ““(Î©, E â†’L[â„] F) where
  toFun f := âŸ¨fderiv â„ f, f.contDiff.fderiv_right le_rfl, f.hasCompactSupport.fderiv â„,
      tsupport_fderiv_subset â„ |>.trans f.tsupport_subsetâŸ©
  map_add' f g := by
    have h : 1 â‰¤ âˆ := mod_cast le_top
    ext
    simp [fderiv_add (f.contDiff.differentiable h).differentiableAt
                     (g.contDiff.differentiable h).differentiableAt]
  map_smul' c f := by
    have h : 1 â‰¤ âˆ := mod_cast le_top
    ext
    simp [fderiv_const_smul (f.contDiff.differentiable h).differentiableAt]

@[simp]
lemma fderivLM_apply [SMulCommClass â„ ğ•œ F] (f : ğ““(Î©, F)) :
    fderivLM ğ•œ f = fderiv â„ f :=
  rfl

/-- Note: this turns out to be a definitional equality thanks to decidablity of the order
on `â„•âˆ`. This means we could have *defined* `fderivLM` this way, but we avoid it
to make sure that `if`s won't appear in the smooth case. -/
lemma fderivLM_eq_withOrder [SMulCommClass â„ ğ•œ F] :
    (fderivLM ğ•œ : ğ““(Î©, F) â†’â‚—[ğ•œ] _) = fderivWithOrderLM ğ•œ âŠ¤ âŠ¤ :=
  rfl

variable (ğ•œ) in
lemma fderivLM_eq_of_scalars [SMulCommClass â„ ğ•œ F] (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (fderivLM ğ•œ : ğ““(Î©, F) â†’ _) = fderivLM ğ•œ' :=
  rfl

variable (ğ•œ) in
lemma fderivLM_ofSupportedIn [SMulCommClass â„ ğ•œ F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©)
    (f : ğ““_{K}(E, F)) :
    fderivLM ğ•œ (ofSupportedIn K_sub_Î© f) =
      ofSupportedIn K_sub_Î© (ContDiffMapSupportedIn.fderivLM ğ•œ f) :=
  rfl

section Topology

variable {V : Type*} [AddCommGroup V] [Module â„ V] [t : TopologicalSpace V]
  [IsTopologicalAddGroup V] [ContinuousSMul â„ V] [LocallyConvexSpace â„ V]

variable (Î© F n) in
/-- The "original topology" on `ğ““^{n}(Î©, F)`, defined as the supremum over all compacts `K âŠ† Î©` of
the topology on `ğ““^{n}_{K}(E, F)`. In other words, this topology makes `ğ““^{n}(Î©, F)` the inductive
limit of the `ğ““^{n}_{K}(E, F)`s **in the category of topological spaces**.

Note that this has no reason to be a locally convex (or even vector space) topology. For this
reason, we actually endow `ğ““^{n}(Î©, F)` with another topology, namely the finest locally convex
topology which is coarser than this original topology. See `TestFunction.topologicalSpace`. -/
noncomputable def originalTop : TopologicalSpace ğ““^{n}(Î©, F) :=
  â¨† (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
    coinduced (ofSupportedIn K_sub_Î©) ContDiffMapSupportedIn.topologicalSpace

variable (Î© F n) in
/-- The canonical LF topology on `ğ““^{n}(Î©, F)`. This makes `ğ““^{n}(Î©, F)` the inductive
limit of the `ğ““^{n}_{K}(E, F)`s **in the category of locally convex topological vector spaces**
(over â„). See `TestFunction.continuous_iff_continuous_comp` for the corresponding universal
property.

More concretely, this is defined as the infimum of *all* locally convex topologies which are
coarser than the "original topology" `TestFunction.originalTop`, which corresponds to taking
the inductive limit in the category of topological spaces. -/
noncomputable instance topologicalSpace : TopologicalSpace ğ““^{n}(Î©, F) :=
  sInf {t : TopologicalSpace ğ““^{n}(Î©, F) | originalTop Î© F n â‰¤ t âˆ§
    @IsTopologicalAddGroup ğ““^{n}(Î©, F) t _ âˆ§
    @ContinuousSMul â„ ğ““^{n}(Î©, F) _ _ t âˆ§
    @LocallyConvexSpace â„ ğ““^{n}(Î©, F) _ _ _ _ t}

noncomputable instance : IsTopologicalAddGroup ğ““^{n}(Î©, F) :=
  topologicalAddGroup_sInf fun _ âŸ¨_, ht, _, _âŸ© â†¦ ht

noncomputable instance uniformSpace : UniformSpace ğ““^{n}(Î©, F) :=
  IsTopologicalAddGroup.rightUniformSpace ğ““^{n}(Î©, F)

noncomputable instance : IsUniformAddGroup ğ““^{n}(Î©, F) :=
  isUniformAddGroup_of_addCommGroup

-- TODO: deduce for `RCLike` field `ğ•‚`
noncomputable instance : ContinuousSMul â„ ğ““^{n}(Î©, F) :=
  continuousSMul_sInf fun _ âŸ¨_, _, ht, _âŸ© â†¦ ht

noncomputable instance : LocallyConvexSpace â„ ğ““^{n}(Î©, F) :=
  .sInf fun _ âŸ¨_, _, _, htâŸ© â†¦ ht

theorem originalTop_le : originalTop Î© F n â‰¤ topologicalSpace Î© F n :=
  le_sInf fun _t âŸ¨ht, _âŸ© â†¦ ht

/-- Fix a locally convex topology `t` on `ğ““^{n}(Î©, F)`. `t` is coarser than the canonical topology
on `ğ““^{n}(Î©, F)` if and only if it is coarser than the "original topology" given by
`TestFunction.originalTop`. -/
theorem topologicalSpace_le_iff {t : TopologicalSpace ğ““^{n}(Î©, F)}
    [@IsTopologicalAddGroup _ t _] [@ContinuousSMul â„ _ _ _ t]
    [@LocallyConvexSpace â„ _ _ _ _ _ t] :
    topologicalSpace Î© F n â‰¤ t â†” originalTop Î© F n â‰¤ t :=
  âŸ¨le_trans originalTop_le, fun H â†¦ sInf_le âŸ¨H, inferInstance, inferInstance, inferInstanceâŸ©âŸ©

/-- For every compact `K âŠ† Î©`, the inclusion map `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` is
continuous. It is in fact a topological embedding, though this fact is not in Mathlib yet. -/
@[fun_prop]
theorem continuous_ofSupportedIn {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    Continuous (ofSupportedIn K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)) := by
  rw [continuous_iff_coinduced_le]
  exact le_trans (le_iSupâ‚‚_of_le K K_sub_Î© le_rfl) originalTop_le

variable (ğ•œ) in
/-- The natural inclusion `ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)`, when `K âŠ† Î©`, as a continuous
linear map. -/
def ofSupportedInCLM [SMulCommClass â„ ğ•œ F] {K : Compacts E} (K_sub_Î© : (K : Set E) âŠ† Î©) :
    ğ““^{n}_{K}(E, F) â†’L[ğ•œ] ğ““^{n}(Î©, F) where
  toFun f := ofSupportedIn K_sub_Î© f
  map_add' _ _ := rfl
  map_smul' _ _ := rfl
  cont := continuous_ofSupportedIn K_sub_Î©

@[deprecated (since := "2025-12-10")] alias ofSupportedInLM := ofSupportedInCLM

@[simp] theorem coe_ofSupportedInCLM [SMulCommClass â„ ğ•œ F] {K : Compacts E}
    (K_sub_Î© : (K : Set E) âŠ† Î©) :
    (ofSupportedInCLM ğ•œ K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)) = ofSupportedIn K_sub_Î© :=
  rfl

@[deprecated (since := "2025-12-10")] alias coe_ofSupportedInLM := coe_ofSupportedInCLM

/-- The **universal property** of the topology on `ğ““^{n}(Î©, F)`: a **linear** map from
`ğ““^{n}(Î©, F)` to a locally convex topological vector space is continuous if and only if its
precomposition with the inclusion `ofSupportedIn K_sub_Î© : ğ““^{n}_{K}(E, F) â†’ ğ““^{n}(Î©, F)` is
continuous for every compact `K âŠ† Î©`. -/
protected theorem continuous_iff_continuous_comp [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F]
    [Module ğ•œ V] [IsScalarTower â„ ğ•œ V] (f : ğ““^{n}(Î©, F) â†’â‚—[ğ•œ] V) :
    Continuous f â†” âˆ€ (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
      Continuous (f âˆ˜ ofSupportedIn K_sub_Î©) := by
  simp_rw [â† f.coe_restrictScalars â„]
  rw [continuous_iff_le_induced]
  have : @IsTopologicalAddGroup _ (induced (f.restrictScalars â„) t) _ :=
    topologicalAddGroup_induced _
  have : @ContinuousSMul â„ _ _ _ (induced (f.restrictScalars â„) t) := continuousSMul_induced _
  have : @LocallyConvexSpace â„ _ _ _ _ _ (induced (f.restrictScalars â„) t) := .induced _
  simp_rw [topologicalSpace_le_iff, originalTop, iSupâ‚‚_le_iff, â† continuous_iff_le_induced,
    continuous_coinduced_dom]

variable (ğ•œ) in
/-- Reformulation of the universal property of the topology on `ğ““^{n}(Î©, F)`, in the form of a
custom constructor for continuous linear maps `ğ““^{n}(Î©, F) â†’L[ğ•œ] V`, where `V` is an arbitrary
locally convex topological vector space. -/
@[simps]
protected def mkCLM [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F] [Module ğ•œ V] [IsScalarTower â„ ğ•œ V]
    (toFun : ğ““^{n}(Î©, F) â†’ V)
    (map_add : âˆ€ f g, toFun (f + g) = toFun f + toFun g)
    (map_smul : âˆ€ c : ğ•œ, âˆ€ f, toFun (c â€¢ f) = c â€¢ toFun f)
    (cont : âˆ€ (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©),
      Continuous (toFun âˆ˜ ofSupportedIn K_sub_Î©)) :
    ğ““^{n}(Î©, F) â†’L[ğ•œ] V :=
  letI Î¦ : ğ““^{n}(Î©, F) â†’â‚—[ğ•œ] V := âŸ¨âŸ¨toFun, map_addâŸ©, map_smulâŸ©
  { toLinearMap := Î¦
    cont := show Continuous Î¦ by rwa [TestFunction.continuous_iff_continuous_comp] }

end Topology

section ToBoundedContinuousFunctionCLM

variable (ğ•œ) in
/-- The inclusion of the space `ğ““^{n}(Î©, F)` into the space `E â†’áµ‡ F` of bounded continuous
functions as a continuous `ğ•œ`-linear map. -/
@[simps! apply]
noncomputable def toBoundedContinuousFunctionCLM [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F] :
    ğ““^{n}(Î©, F) â†’L[ğ•œ] E â†’áµ‡ F :=
  TestFunction.mkCLM ğ•œ (â†‘) (fun _ _ â†¦ rfl) (fun _ _ â†¦ rfl)
    (fun _ _ â†¦ (ContDiffMapSupportedIn.toBoundedContinuousFunctionCLM ğ•œ).continuous)

lemma toBoundedContinuousFunctionCLM_eq_of_scalars [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F] (ğ•œ' : Type*)
    [NontriviallyNormedField ğ•œ'] [NormedSpace ğ•œ' F] [Algebra â„ ğ•œ'] [IsScalarTower â„ ğ•œ' F] :
    (toBoundedContinuousFunctionCLM ğ•œ : ğ““^{n}(Î©, F) â†’ _) = toBoundedContinuousFunctionCLM ğ•œ' :=
  rfl

variable (ğ•œ) in
theorem injective_toBoundedContinuousFunctionCLM [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F] :
    Function.Injective (toBoundedContinuousFunctionCLM ğ•œ : ğ““^{n}(Î©, F) â†’L[ğ•œ] E â†’áµ‡ F) :=
  fun f g â†¦ by simp [toBoundedContinuousFunctionCLM]

instance : T3Space ğ““^{n}(Î©, F) :=
  suffices T2Space ğ““^{n}(Î©, F) from inferInstance
  .of_injective_continuous (injective_toBoundedContinuousFunctionCLM â„)
    (ContinuousLinearMap.continuous _)

end ToBoundedContinuousFunctionCLM

section postcomp

variable [Algebra â„ ğ•œ] [IsScalarTower â„ ğ•œ F] [IsScalarTower â„ ğ•œ F']

-- Note: generalizing this to a semilinear setting would require a typeclass-way of saying that
-- the `RingHom` is `â„`-linear.
/-- Given `T : F â†’L[ğ•œ] F'`, `postcompCLM T` is the continuous `ğ•œ`-linear-map sending
`f : ğ““^{n}(Î©, F)` to `T âˆ˜ f` as an element of `ğ““^{n}(Î©, F')`. -/
noncomputable def postcompCLM (T : F â†’L[ğ•œ] F') :
    ğ““^{n}(Î©, F) â†’L[ğ•œ] ğ““^{n}(Î©, F') :=
  letI Î¦ (f : ğ““^{n}(Î©, F)) : ğ““^{n}(Î©, F') :=
    âŸ¨T âˆ˜ f, T.restrictScalars â„ |>.contDiff.comp f.contDiff,
      f.hasCompactSupport.comp_left (map_zero _),
      (tsupport_comp_subset (map_zero _) f).trans f.tsupport_subsetâŸ©
  haveI key (K : Compacts E) (K_sub_Î© : (K : Set E) âŠ† Î©) (f : ğ““^{n}_{K}(E, F)) :
      ofSupportedIn K_sub_Î© (ContDiffMapSupportedIn.postcompCLM T f) =
        Î¦ (ofSupportedIn K_sub_Î© f) := by
    ext; simp [Î¦]
  TestFunction.mkCLM ğ•œ Î¦
    (fun f g â†¦ by ext; simp [Î¦]) (fun c f â†¦ by ext; simp [Î¦])
    (fun K K_sub_Î© â†¦ by refine .congr ?_ (key K K_sub_Î©); fun_prop)

@[simp]
lemma postcompCLM_apply (T : F â†’L[ğ•œ] F')
    (f : ğ““^{n}(Î©, F)) :
    postcompCLM T f = T âˆ˜ f :=
  rfl

end postcomp

section FDerivCLM

variable (ğ•œ n k) in
/-- `fderivWithOrderCLM ğ•œ n k` is the continuous `ğ•œ`-linear-map sending `f : ğ““^{n}_{K}(E, F)` to
its derivative as an element of `ğ““^{k}_{K}(E, E â†’L[â„] F)`.
This only makes mathematical sense if `k + 1 â‰¤ n`, otherwise we define it as the zero map.

See `fderivCLM` for the very common case where everything is infinitely differentiable. -/
noncomputable def fderivWithOrderCLM [SMulCommClass â„ ğ•œ F] :
    ğ““^{n}(Î©, F) â†’L[ğ•œ] ğ““^{k}(Î©, E â†’L[â„] F) where
  toLinearMap := fderivWithOrderLM ğ•œ n k
  cont := show Continuous (fderivWithOrderLM ğ•œ n k) by
    rw [fderivWithOrderLM_eq_of_scalars ğ•œ â„, TestFunction.continuous_iff_continuous_comp]
    intro K K_sub_Î©
    refine .congr ?_ fun f â†¦ (fderivWithOrderLM_ofSupportedIn ğ•œ K_sub_Î© f).symm
    exact (continuous_ofSupportedIn K_sub_Î©).comp
      (ContDiffMapSupportedIn.fderivWithOrderCLM ğ•œ n k).continuous

@[simp]
lemma fderivWithOrderCLM_apply [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) :
    fderivWithOrderCLM ğ•œ n k f = if k + 1 â‰¤ n then fderiv â„ f else 0 :=
  fderivWithOrderLM_apply f

lemma fderivWithOrderCLM_apply_of_le [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) (hk : k + 1 â‰¤ n) :
    fderivWithOrderCLM ğ•œ n k f = fderiv â„ f :=
  fderivWithOrderLM_apply_of_le f hk

lemma fderivWithOrderCLM_apply_of_gt [SMulCommClass â„ ğ•œ F] (f : ğ““^{n}(Î©, F)) (hk : Â¬ (k + 1 â‰¤ n)) :
    fderivWithOrderCLM ğ•œ n k f = 0 :=
  fderivWithOrderLM_apply_of_gt f hk

variable (ğ•œ) in
lemma fderivWithOrderCLM_eq_of_scalars [SMulCommClass â„ ğ•œ F] (ğ•œ' : Type*)
    [NontriviallyNormedField ğ•œ'] [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (fderivWithOrderLM ğ•œ n k : ğ““^{n}(Î©, F) â†’ _) = fderivWithOrderLM ğ•œ' n k :=
  rfl

variable (ğ•œ) in
/-- `fderivCLM ğ•œ` is the continuous `ğ•œ`-linear-map sending `f : ğ““_{K}(E, F)` to
its derivative as an element of `ğ““_{K}(E, E â†’L[â„] F)`.

See also `fderivWithOrderCLM` if you need more control on the regularities. -/
noncomputable def fderivCLM [SMulCommClass â„ ğ•œ F] :
    ğ““(Î©, F) â†’L[ğ•œ] ğ““(Î©, E â†’L[â„] F) where
  toLinearMap := fderivLM ğ•œ
  cont := show Continuous (fderivLM ğ•œ) by
    rw [fderivLM_eq_of_scalars ğ•œ â„, TestFunction.continuous_iff_continuous_comp]
    intro K K_sub_Î©
    refine .congr ?_ fun f â†¦ (fderivLM_ofSupportedIn ğ•œ K_sub_Î© f).symm
    exact (continuous_ofSupportedIn K_sub_Î©).comp
      (ContDiffMapSupportedIn.fderivCLM ğ•œ).continuous

@[simp]
lemma fderivCLM_apply [SMulCommClass â„ ğ•œ F] (f : ğ““(Î©, F)) :
    fderivCLM ğ•œ f = fderiv â„ f :=
  rfl

/-- Note: this turns out to be a definitional equality thanks to decidablity of the order
on `â„•âˆ`. This means we could have *defined* `fderivLM` this way, but we avoid it
to make sure that `if`s won't appear in the smooth case. -/
lemma fderivCLM_eq_withOrder [SMulCommClass â„ ğ•œ F] :
    (fderivCLM ğ•œ : ğ““(Î©, F) â†’L[ğ•œ] _) = fderivWithOrderCLM ğ•œ âŠ¤ âŠ¤ :=
  rfl

variable (ğ•œ) in
lemma fderivCLM_eq_of_scalars [SMulCommClass â„ ğ•œ F] (ğ•œ' : Type*) [NontriviallyNormedField ğ•œ']
    [NormedSpace ğ•œ' F] [SMulCommClass â„ ğ•œ' F] :
    (fderivCLM ğ•œ : ğ““(Î©, F) â†’ _) = fderivCLM ğ•œ' :=
  rfl

end FDerivCLM

end TestFunction
