/-
Copyright (c) 2020 Yury Kudryashov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Yury Kudryashov, FrÃ©dÃ©ric Dupuis
-/
import Mathlib.Data.Real.Archimedean
import Mathlib.Geometry.Convex.Cone.Basic
import Mathlib.LinearAlgebra.LinearPMap

/-!
# Extension theorems

We prove two extension theorems:
* `riesz_extension`:
  [M. Riesz extension theorem](https://en.wikipedia.org/wiki/M._Riesz_extension_theorem) says that
  if `s` is a convex cone in a real vector space `E`, `p` is a submodule of `E`
  such that `p + s = E`, and `f` is a linear function `p â†’ â„` which is
  nonnegative on `p âˆ© s`, then there exists a globally defined linear function
  `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.
* `exists_extension_of_le_sublinear`:
  Hahn-Banach theorem: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
  defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
  then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
  for all `x`

-/

open Set LinearMap

variable {ğ•œ E F G : Type*}

/-!
### M. Riesz extension theorem

Given a convex cone `s` in a vector space `E`, a submodule `p`, and a linear `f : p â†’ â„`, assume
that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then there exists a globally defined linear
function `g : E â†’ â„` that agrees with `f` on `p`, and is nonnegative on `s`.

We prove this theorem using Zorn's lemma. `RieszExtension.step` is the main part of the proof.
It says that if the domain `p` of `f` is not the whole space, then `f` can be extended to a larger
subspace `p âŠ” span â„ {y}` without breaking the non-negativity condition.

In `RieszExtension.exists_top` we use Zorn's lemma to prove that we can extend `f`
to a linear map `g` on `âŠ¤ : Submodule E`. Mathematically this is the same as a linear map on `E`
but in Lean `âŠ¤ : Submodule E` is isomorphic but is not equal to `E`. In `riesz_extension`
we use this isomorphism to prove the theorem.
-/

variable [AddCommGroup E] [Module â„ E]

namespace RieszExtension

open Submodule

variable (s : ConvexCone â„ E) (f : E â†’â‚—.[â„] â„)

/-- Induction step in M. Riesz extension theorem. Given a convex cone `s` in a vector space `E`,
a partially defined linear map `f : f.domain â†’ â„`, assume that `f` is nonnegative on `f.domain âˆ© p`
and `p + s = E`. If `f` is not defined on the whole `E`, then we can extend it to a larger
submodule without breaking the non-negativity condition. -/
theorem step (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x)
    (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) (hdom : f.domain â‰  âŠ¤) :
    âˆƒ g, f < g âˆ§ âˆ€ x : g.domain, (x : E) âˆˆ s â†’ 0 â‰¤ g x := by
  obtain âŸ¨y, -, hyâŸ© : âˆƒ y âˆˆ âŠ¤, y âˆ‰ f.domain := SetLike.exists_of_lt (lt_top_iff_ne_top.2 hdom)
  obtain âŸ¨c, le_c, c_leâŸ© :
      âˆƒ c, (âˆ€ x : f.domain, -(x : E) - y âˆˆ s â†’ f x â‰¤ c) âˆ§
        âˆ€ x : f.domain, (x : E) + y âˆˆ s â†’ c â‰¤ f x := by
    set Sp := f '' { x : f.domain | (x : E) + y âˆˆ s }
    set Sn := f '' { x : f.domain | -(x : E) - y âˆˆ s }
    suffices (upperBounds Sn âˆ© lowerBounds Sp).Nonempty by
      simpa only [Sp, Sn, Set.Nonempty, upperBounds, lowerBounds, forall_mem_image] using this
    refine exists_between_of_forall_le (Nonempty.image f ?_) (Nonempty.image f (dense y)) ?_
    Â· rcases dense (-y) with âŸ¨x, hxâŸ©
      rw [â† neg_neg x, NegMemClass.coe_neg, â† sub_eq_add_neg] at hx
      exact âŸ¨_, hxâŸ©
    rintro a âŸ¨xn, hxn, rflâŸ© b âŸ¨xp, hxp, rflâŸ©
    have := s.add_mem hxp hxn
    rw [add_assoc, add_sub_cancel, â† sub_eq_add_neg, â† AddSubgroupClass.coe_sub] at this
    replace := nonneg _ this
    rwa [f.map_sub, sub_nonneg] at this
  refine âŸ¨f.supSpanSingleton y (-c) hy, ?_, ?_âŸ©
  Â· refine lt_iff_le_not_ge.2 âŸ¨f.left_le_sup _ _, fun H => ?_âŸ©
    replace H := LinearPMap.domain_mono.monotone H
    rw [LinearPMap.domain_supSpanSingleton, sup_le_iff, span_le, singleton_subset_iff] at H
    exact hy H.2
  Â· rintro âŸ¨z, hzâŸ© hzs
    rcases mem_sup.1 hz with âŸ¨x, hx, y', hy', rflâŸ©
    rcases mem_span_singleton.1 hy' with âŸ¨r, rflâŸ©
    simp only at hzs
    rw [LinearPMap.supSpanSingleton_apply_mk _ _ _ _ _ hx, smul_neg, â† sub_eq_add_neg, sub_nonneg]
    rcases lt_trichotomy r 0 with (hr | hr | hr)
    Â· have : -(râ»Â¹ â€¢ x) - y âˆˆ s := by
        rwa [â† s.smul_mem_iff (neg_pos.2 hr), smul_sub, smul_neg, neg_smul, neg_neg, smul_smul,
          mul_inv_cancelâ‚€ hr.ne, one_smul, sub_eq_add_neg, neg_smul, neg_neg]
      replace := le_c (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this
      rwa [â† mul_le_mul_iff_rightâ‚€ (neg_pos.2 hr), neg_mul, neg_mul, neg_le_neg_iff, f.map_smul,
        smul_eq_mul, â† mul_assoc, mul_inv_cancelâ‚€ hr.ne, one_mul] at this
    Â· subst r
      simp only [zero_smul, add_zero] at hzs âŠ¢
      apply nonneg
      exact hzs
    Â· have : râ»Â¹ â€¢ x + y âˆˆ s := by
        rwa [â† s.smul_mem_iff hr, smul_add, smul_smul, mul_inv_cancelâ‚€ hr.ne', one_smul]
      replace := c_le (râ»Â¹ â€¢ âŸ¨x, hxâŸ©) this
      rwa [â† mul_le_mul_iff_rightâ‚€ hr, f.map_smul, smul_eq_mul, â† mul_assoc, mul_inv_cancelâ‚€ hr.ne',
        one_mul] at this

theorem exists_top (p : E â†’â‚—.[â„] â„) (hp_nonneg : âˆ€ x : p.domain, (x : E) âˆˆ s â†’ 0 â‰¤ p x)
    (hp_dense : âˆ€ y, âˆƒ x : p.domain, (x : E) + y âˆˆ s) :
    âˆƒ q â‰¥ p, q.domain = âŠ¤ âˆ§ âˆ€ x : q.domain, (x : E) âˆˆ s â†’ 0 â‰¤ q x := by
  set S := { p : E â†’â‚—.[â„] â„ | âˆ€ x : p.domain, (x : E) âˆˆ s â†’ 0 â‰¤ p x }
  have hSc : âˆ€ c, c âŠ† S â†’ IsChain (Â· â‰¤ Â·) c â†’ âˆ€ y âˆˆ c, âˆƒ ub âˆˆ S, âˆ€ z âˆˆ c, z â‰¤ ub := by
    intro c hcs c_chain y hy
    clear hp_nonneg hp_dense p
    have cne : c.Nonempty := âŸ¨y, hyâŸ©
    have hcd : DirectedOn (Â· â‰¤ Â·) c := c_chain.directedOn
    refine âŸ¨LinearPMap.sSup c hcd, ?_, fun _ â†¦ LinearPMap.le_sSup hcdâŸ©
    rintro âŸ¨x, hxâŸ© hxs
    have hdir : DirectedOn (Â· â‰¤ Â·) (LinearPMap.domain '' c) :=
      directedOn_image.2 (hcd.mono LinearPMap.domain_mono.monotone)
    rcases (mem_sSup_of_directed (cne.image _) hdir).1 hx with âŸ¨_, âŸ¨f, hfc, rflâŸ©, hfxâŸ©
    have : f â‰¤ LinearPMap.sSup c hcd := LinearPMap.le_sSup _ hfc
    convert â† hcs hfc âŸ¨x, hfxâŸ© hxs using 1
    exact this.2 rfl
  obtain âŸ¨q, hpq, hqs, hqâŸ© := zorn_le_nonemptyâ‚€ S hSc p hp_nonneg
  refine âŸ¨q, hpq, ?_, hqsâŸ©
  contrapose! hq
  have hqd : âˆ€ y, âˆƒ x : q.domain, (x : E) + y âˆˆ s := fun y â†¦
    let âŸ¨x, hxâŸ© := hp_dense y
    âŸ¨Submodule.inclusion hpq.left x, hxâŸ©
  rcases step s q hqs hqd hq with âŸ¨r, hqr, hrâŸ©
  exact âŸ¨r, hr, hqr.le, fun hrq â†¦ hqr.ne' <| hrq.antisymm hqr.leâŸ©

end RieszExtension

/-- M. **Riesz extension theorem**: given a convex cone `s` in a vector space `E`, a submodule `p`,
and a linear `f : p â†’ â„`, assume that `f` is nonnegative on `p âˆ© s` and `p + s = E`. Then
there exists a globally defined linear function `g : E â†’ â„` that agrees with `f` on `p`,
and is nonnegative on `s`. -/
theorem riesz_extension (s : ConvexCone â„ E) (f : E â†’â‚—.[â„] â„)
    (nonneg : âˆ€ x : f.domain, (x : E) âˆˆ s â†’ 0 â‰¤ f x)
    (dense : âˆ€ y, âˆƒ x : f.domain, (x : E) + y âˆˆ s) :
    âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ âˆ€ x âˆˆ s, 0 â‰¤ g x := by
  rcases RieszExtension.exists_top s f nonneg dense
    with âŸ¨âŸ¨g_dom, gâŸ©, âŸ¨-, hfgâŸ©, rfl : g_dom = âŠ¤, hgsâŸ©
  refine âŸ¨g.comp (LinearMap.id.codRestrict âŠ¤ fun _ â†¦ trivial), ?_, ?_âŸ©
  Â· exact fun x => (hfg rfl).symm
  Â· exact fun x hx => hgs âŸ¨x, _âŸ© hx

/-- **Hahn-Banach theorem**: if `N : E â†’ â„` is a sublinear map, `f` is a linear map
defined on a subspace of `E`, and `f x â‰¤ N x` for all `x` in the domain of `f`,
then `f` can be extended to the whole space to a linear map `g` such that `g x â‰¤ N x`
for all `x`. -/
theorem exists_extension_of_le_sublinear (f : E â†’â‚—.[â„] â„) (N : E â†’ â„)
    (N_hom : âˆ€ c : â„, 0 < c â†’ âˆ€ x, N (c â€¢ x) = c * N x) (N_add : âˆ€ x y, N (x + y) â‰¤ N x + N y)
    (hf : âˆ€ x : f.domain, f x â‰¤ N x) :
    âˆƒ g : E â†’â‚—[â„] â„, (âˆ€ x : f.domain, g x = f x) âˆ§ âˆ€ x, g x â‰¤ N x := by
  let s : ConvexCone â„ (E Ã— â„) :=
    { carrier := { p : E Ã— â„ | N p.1 â‰¤ p.2 }
      smul_mem' := fun c hc p hp =>
        calc
          N (c â€¢ p.1) = c * N p.1 := N_hom c hc p.1
          _ â‰¤ c * p.2 := mul_le_mul_of_nonneg_left hp hc.le
      add_mem' := fun x hx y hy => (N_add _ _).trans (add_le_add hx hy) }
  set f' := (-f).coprod (LinearMap.id.toPMap âŠ¤)
  have hf'_nonneg : âˆ€ x : f'.domain, x.1 âˆˆ s â†’ 0 â‰¤ f' x := fun x (hx : N x.1.1 â‰¤ x.1.2) â†¦ by
    simpa [f'] using le_trans (hf âŸ¨x.1.1, x.2.1âŸ©) hx
  have hf'_dense : âˆ€ y : E Ã— â„, âˆƒ x : f'.domain, â†‘x + y âˆˆ s := by
    rintro âŸ¨x, yâŸ©
    refine âŸ¨âŸ¨(0, N x - y), âŸ¨f.domain.zero_mem, trivialâŸ©âŸ©, ?_âŸ©
    simp only [s, ConvexCone.mem_mk, mem_setOf_eq, Prod.fst_add, Prod.snd_add, zero_add,
      sub_add_cancel, le_rfl]
  obtain âŸ¨g, g_eq, g_nonnegâŸ© := riesz_extension s f' hf'_nonneg hf'_dense
  replace g_eq : âˆ€ (x : f.domain) (y : â„), g (x, y) = y - f x := fun x y â†¦
    (g_eq âŸ¨(x, y), âŸ¨x.2, trivialâŸ©âŸ©).trans (sub_eq_neg_add _ _).symm
  refine âŸ¨-g.comp (inl â„ E â„), fun x â†¦ ?_, fun x â†¦ ?_âŸ©
  Â· simp [g_eq x 0]
  Â· calc -g (x, 0) = g (0, N x) - g (x, N x) := by simp [â† map_sub, â† map_neg]
      _ = N x - g (x, N x) := by simpa using g_eq 0 (N x)
      _ â‰¤ N x := by simpa using g_nonneg âŸ¨x, N xâŸ© (le_refl (N x))
